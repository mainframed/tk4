         MACRO
&NM     LISTTA &RLN=,&ADDR=,&SCREEN=
.*
.*       RELEASE 21 TCAM LEVEL 4, 02/28/72, CHANGE LEVEL=01
.*
.*
.*FUNCTION -- THIS MACRO GIVES THE USER THE CAPABILITY TO SPECIFY
.*   ATTRIBUTES OF HIS CONFIGURATION WHICH VARY AMONG LINES OR
.*   TERMINALS WITHIN A LINE GROUP.
.*
.*ENTRY POINTS -- NOT APPLICABLE
.*
.*INPUT -- MACRO CALL PARAMETERS
.*
.*OUTPUT -- VALUES IN GLOBAL VARIABLES TO BE USED LATER BY THE
.*   'TSOMCP' MACRO.  MNOTE MESSAGES IF INVALID PARAMETERS ARE
.*   DETECTED.
.*
.*EXTERNAL REFERENCES - NOT APPLICABLE
.*
.*EXITS,NORMAL -- NORMAL EXIT OCCURS IF NO SERIOUS ERRORS IN THE
.*   CALLER'S PARAMETERS ARE DETECTED.
.*
.*EXITS, ERROR -- UPON DETECTION OF A SERIOUS ERROR, THE 'QUITSW'
.*   IS SET TO 1 AND FURTHER PROCESSING IS BYPASSED.
.*
.*TABLES/WORK AREAS -- SEE DESCRIPTIONS ACCOMPANYING VARIABLE
.*   DECLARATIONS.
.*
.*ATTRIBUTES -- NOT APPLICABLE
.*
.*CHARACTER CODE DEPENDENCY - NONE
.*
.*NOTES -- NONE
.*
.*
.*       GLOBAL VARIABLES
.*
         GBLA  &NLG                NUMBER OF LINEGRP MACROS ISSUED
         GBLA  &LNO(63)            NUMBER OF LINES/LINE GROUP
         GBLA  &ADRPT              POINTER TO LAST ADDR PARM GROUP
         GBLA  &SCRPT              POINTER TO LAST SCREEN PARM GROUP
         GBLA  &SCRH(2500)         CONTAINS SCREEN HEIGHT
         GBLA  &SCRW(2500)         CONTAINS SCREEN WIDTH
         GBLA  &TRMNO(2500)        NUMBER OF TERMINALS/LINE
.*
         GBLB  &LGRPSW             INDICATES LINEGRP ISSUED
         GBLB  &MCPSW              INDICATES TSOMCP ISSUED
         GBLB  &QUITSW             GENERATION QUIT SWITCH
         GBLB  &DIALSW(63)         ON MEANS SWITCHED LINE
.*
         GBLC  &T(63)              CONTAINS TERM= PARAMETER VALUES
         GBLC  &ADCHAR(2500)       CONTAINS ADDR= PARAMETER VALUES
.*
.*       LOCAL VARIABLES
.*
         LCLA  &P                  POINTER TO SCREEN/TERMNO ARRAYS
         LCLA  &I                  WORK VARIABLE
         LCLA  &J                  WORK VARIABLE
.*
.*       TEST FOR MACRO CALL OUT OF ORDER
.*
         AIF   (&LGRPSW AND NOT &MCPSW).INORDER
         MNOTE 16,'IKJ54120I LISTTA MACRO OUT OF ORDER -- IGNORED'
         AGO   .GIVEUP             GO SET QUIT SW & EXIT
.INORDER ANOP
.*
.*       TEST IF CALL IS CONSISTANT WITH LAST LINEGRP CALL
.*
         AIF   (N'&RLN EQ 1 AND T'&RLN EQ 'N').RLNOK
         MNOTE 16,'IKJ54121I RLN OPERAND MISSING OR INVALID'
&QUITSW  SETB  1                   PREVENT MCP GENERATION
         MEXIT
.RLNOK   ANOP
         AIF   (&RLN LE &LNO(&NLG) AND &RLN NE 0).REFOK
         MNOTE 16,'IKJ54122I RLN OPERAND REFERS TO NON-EXISTENT LINE'
         AGO   .GIVEUP             GO SET QUIT SWITCH & EXIT
.*
.REFOK   ANOP
         AIF   ('&T(&NLG)'(1,3) EQ '226').TERMOK
         AIF   ('&T(&NLG)'(1,3) EQ '327').TERMOK
         AIF   ('&T(&NLG)' EQ '1050').TERMOK
         AIF   ('&T(&NLG)' EQ '5041').TERMOK
         MNOTE 16,'IKJ54123I LISTTA MACRO NOT ALLOWED WITH TERMINAL TYP-
               E OF PREVIOUS LINEGRP'
         AGO   .GIVEUP             GO SET QUIT SW & EXIT
.TERMOK  ANOP
.*
.*       SET POINTER TO SCREEN/TERMNO ARRAYS
.*
&P       SETA  &SCRPT+&RLN-1
.*
.*********************************************************************
.*       ADDR=
.*********************************************************************
.*
         AIF   (N'&ADDR EQ 0).SCREEN  BR IF OPERAND OMITTED
         AIF   ('&T(&NLG)' EQ '226L').ADDER1  BR IF 226L
         AIF   ('&T(&NLG)' NE '327L').NOTLOCL BR IF NOT 327L
.ADDER1  ANOP
         MNOTE 4,'IKJ54114I ADDR OPERAND NOT ALLOWED FOR THIS TERMINAL -
                TYPE'
         AGO   .SCREEN             SKIP ADDR PROCESSING
.NOTLOCL ANOP
         AIF   (N'&ADDR LE &TRMNO(&P)).PUTADDR
         MNOTE 4,'IKJ54124I ADDR OPERAND REFERS TO NON-EXISTENT TERMINA-
               L(S)'
         AGO   .SCREEN
.*
.*       SET POINTER TO ADCHAR ARRAY
.*
.PUTADDR ANOP
&I       SETA  1                   INITIALIZE LOOP CONTROL VARIABLE
&J       SETA  &ADRPT              INITIALIZE ADCHAR ARRAY POINTER
.ADDR1   ANOP                      LOOP RLN-1 TIMES
         AIF   (&I GE &RLN).MVADDR
&J       SETA  &J+&TRMNO(&SCRPT+&I-1)  INCREMENT POINTER BY NUMBER
.*                                 OF TERMINALS ON LINE
&I       SETA  &I+1                BUMP LOOP CONTROL VARIABLE
         AGO   .ADDR1              GO TO LOOP TEST
.*
.*       MOVE ADDRESS CHARACTERS INTO ARRAY
.*
.MVADDR  ANOP                      LOOP EXECUTED N'&ADDR TIMES
&I       SETA  0                   SET LOOP CONTROL VARIABLE
.ADDR2   ANOP                      LOOP EXECUTED FOR EACH SUBOPERAND
         AIF   ('&ADDR(&I+1)' EQ '').ADDRLT  SKIP NULL SUBOPERANDS
         AIF   (K'&ADDR(&I+1) EQ 2 AND '&T(&NLG)' EQ '1050').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 2 AND '&T(&NLG)' EQ '5041').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '226R').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '2265').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '327R').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '327S').ADSZOK
         MNOTE 4,'IKJ54118I ADDR=&ADDR(&I+1) INCORRECT LENGTH -- IGNORE-
               D'
         AGO   .ADDRLT
.ADSZOK  ANOP
&ADCHAR(&J+&I) SETC '&ADDR(&I+1)'  PUT IN ARRAY
.ADDRLT  ANOP
&I       SETA  &I+1                BUMP LOOP CONTROL VARIABLE
         AIF   (&I LT N'&ADDR).ADDR2   BR UNTIL ALL OPERANDS HANDLED
.*
.*********************************************************************
.*       SCREEN
.*********************************************************************
.*
.SCREEN  ANOP
         AIF   ('&SCREEN' NE '').CHKDSPY
         MEXIT                     EXIT IF OPERAND OMITTED
.CHKDSPY ANOP
         AIF   ('&T(&NLG)'(1,3) EQ '226').CHKNO
         AIF   ('&T(&NLG)'(1,3) EQ '327').CHKNO
         MNOTE 4,'IKJ54114I SCREEN OPERAND NOT ALLOWED FOR THIS TERMINAL
               L TYPE'
         MEXIT
.*
.CHKNO   ANOP
         AIF   (N'&SCREEN  EQ 2).CHKSTD
         MNOTE 4,'IKJ54116I SCREEN OPERAND MUST BE A TWO-OPERAND SUBLIST
               T'
         MEXIT
.*
.*       CHECK FOR STANDARD VALUES
.*
.CHKSTD  ANOP
&I       SETA  &SCREEN(1)
&J       SETA  &SCREEN(2)
         AIF   (&I EQ 12 AND &J EQ 40).PUTSCR
         AIF   ('&T(&NLG)'(1,3) EQ '327').ANR
         AIF   (&I EQ 12 AND &J EQ 80).PUTSCR
         AIF   (&I EQ 6 AND &J EQ 40).PUTSCR
         AIF   (&I EQ 15 AND &J EQ 64).PUTSCR
         AGO   .SCRER
.ANR     ANOP
         AIF   (&I EQ 24 AND &J EQ 80).PUTSCR
.SCRER   ANOP
         MNOTE 4,'IKJ54117I NON-STANDARD SCREEN DIMENSIONS -- ACCEPTED'
.*
.PUTSCR  ANOP
&SCRH(&P) SETA &SCREEN(1)               MOVE IN HEIGHT
&SCRW(&P) SETA &SCREEN(2)               MOVE IN WIDTH
         MEXIT                          NORMAL EXIT
.*
.GIVEUP  ANOP
&QUITSW  SETB  1                        SERIOUS ERROR - DON'T GENERATE
         MEXIT                          THE MCP
         MEND
