          MACRO
&NAME   STAX  &EXADDR,&OBUF=,&IBUF=,&USADDR=,&REPLACE=,&DEFER=,&MF=I
         LCLA  &A
         LCLC  &GNAME,&H
         LCLA  &REPLSW,&REPL,&DEFRSW
&REPL    SETA  X'80'
&GNAME   SETC  'IHB'.'&SYSNDX'
         AIF   ('&EXADDR' EQ '' AND '&DEFER' EQ '').CANCEL
         AIF   ('&EXADDR' NE '').DEFER4
         AIF   ('&OBUF' NE '' OR '&IBUF' NE '').CANCEL
         AIF   ('&REPLACE' NE '' OR '&USADDR' NE '').CANCEL
.DEFER4  AIF   ('&REPLACE' EQ '').DEFER5
         AIF   ('&REPLACE' EQ 'YES').DEFER5
&REPLSW  SETA  X'40'
.DEFER5  ANOP
         AIF   ('&NAME' EQ '' OR '&MF' EQ 'L').CONTA
&NAME    DS    0H
         AGO   .CONTA
.ERROR   MNOTE 'INVALID REGISTER SPECIFICATION'
         MEXIT
.ERROR1  MNOTE 'INVALID OPTION SPECIFIED'
         MEXIT
.CONTA   ANOP
         AIF   ('&DEFER' EQ '').DEFER1       IS DEFER REQUESTED
         AIF   ('&DEFER' EQ 'YES').DEFER2    SHOULD DEFER ATTN
         AIF   ('&DEFER' EQ 'NO').DEFER3     SHOULD CANCEL DEFER ATTN
         AGO   .ERROR1        INVALID OPERAND HAS BEEN SPECIFIED
.DEFER2  ANOP                 SET DEFER SWITCH EQUAL TO
&DEFRSW  SETA  X'20'          DEFER/YES OR X'20'
         AGO   .DEFER1
.DEFER3  ANOP                 SET DEFER SWITCH EQUAL TO
&DEFRSW  SETA  X'10'          DEFER/NO OR X'10'
.DEFER1  ANOP
         AIF   ('&MF' EQ 'L').LROUT      IS THIS A LIST REQUEST
         AIF   ('&MF' EQ 'I').IROUT
         AIF   ('&MF(1)' EQ 'E').EROUT
         MNOTE 'INVALID MF SPECIFICATION'
         MEXIT
.EROUT   ANOP
         AIF   ('&MF'(4,1) EQ '(').ISAREG
         LA    1,&MF(2)      ADDRESS OF PARAMETER LIST INTO REG 1
         AGO   .CONTB
.ISAREG  AIF   ('&MF(2)' EQ '(1)').CONTB
&A       SETA  K'&MF-6
&H       SETC  '&MF'(5,&A)
         LR    1,&H          ADDRESS OF PARAMETER LIST INTO REG 1
.CONTB   ANOP
         AIF   ('&EXADDR' EQ '').CONTC
         AIF   ('&EXADDR'(1,1) EQ '(').ISAREG1
         LA    14,&EXADDR    ADDRESS OF EXIT ROUTINE
         ST    14,0(1,0)     INTO PARAMETER LIST
         AGO   .CONTC
.ISAREG1 ST    &EXADDR(1),0(1,0)  ADDRESS OF EXIT ROUTINE INTO LIST
.CONTC   AIF   ('&USADDR' EQ '').CONTD
         AIF   ('&USADDR'(1,1) EQ '(').ISAREG2
         LA    14,&USADDR    ADDRESS OF USER PARAMETERS
         ST    14,16(1,0)    INTO LIST
         AGO   .CONTD
.ISAREG2 ST    &USADDR(1),16(1,0) ADDRESS OF USER PARAMETERS INTO
*                                 LIST
.CONTD   AIF   ('&OBUF(1)' EQ '').CNTSUB1
         AIF   ('&OBUF(1)'(1,1) EQ '(').IRGSUB1
         LA    14,&OBUF(1)   ADDRESS OF OUTPUT BUFFERS
         ST    14,8(1,0)     INTO LIST
         AGO   .CNTSUB1
.IRGSUB1 ST   &OBUF(1),8(1,0)  ADDRESS OF OUTPUT BUFFERS INTO LIST
.CNTSUB1 AIF  ('&OBUF(2)' EQ '').CONTE
         AIF   ('&OBUF(2)'(1,1) EQ '(').IRGSUB2
         B     &GNAME.B            AROUND CONSTANT           AJ A52533
&GNAME.A DC    AL2(&OBUF(2))       OBUF LENGTH               AJ A52533
&GNAME.B MVC   6(2,1),&GNAME.A     MOVE LENGTH TO PARM LIST  AJ A52533
         AGO   .CONTE
.IRGSUB2 STH  &OBUF(2),6(1)  LENGTH OF OUTPUT BUFFERS INTO LIST
.CONTE   AIF   ('&IBUF(1)' EQ '').CNTSUB2
         AIF   ('&IBUF(1)'(1,1) EQ '(').IRGSUB3
         LA    14,&IBUF(1)    ADDRESS OF INPUT BUFFERS
         ST    14,12(1,0)    INTO LIST
         AGO   .CNTSUB2
.IRGSUB3 ST   &IBUF(1),12(1,0)    ADDRESS OF INPUT BUFFERS INTO LIST
.CNTSUB2 AIF  ('&IBUF(2)' EQ '').CONTG
         AIF   ('&IBUF(2)'(1,1) EQ '(').IRGSUB4
         B     &GNAME.D            GO AROUND CONSTANT        AJ A52533
&GNAME.C DC    AL2(&IBUF(2))       IBUF LENGTH               AJ A52533
&GNAME.D MVC   4(2,1),&GNAME.C     IBUF LENGTH TO PARM LIST  AJ A52533
         AGO   .CONTG
.IRGSUB4 STH  &IBUF(2),4(1)  LENGTH OF INPUT BUFFERS INTO LIST
.CONTG   AIF   ('&REPLACE' EQ 'YES').CNTSUB5
         AIF   ('&REPLACE' EQ 'NO').CNTSUB6
.CNTSUB5 NI    16(1),&REPL   INDICATE REPLACE PREVIOUS STAX
.CNTSUB6 OI    16(1),&REPLSW+&DEFRSW   REPLACE/NO REPLACE, DEFERRAL IND
         AGO   .SVC
.LROUT   ANOP
&NAME    DS    0F
.CONTL4  AIF   ('&EXADDR' EQ '').DEFER6
         AIF   ('&EXADDR'(1,1) EQ '(').ERROR
         DC    AL4(&EXADDR)  ADDRESS OF EXIT ROUTINE
         AGO   .DEFER7
.DEFER6  DC    AL4(0)         ADDRESS OF EXIT ROUTINE
.DEFER7  ANOP
.CNTL6   AIF   ('&IBUF(2)' EQ '').CONTL1
         AIF   ('&IBUF(2)'(1,1) EQ '(').ERROR
         DC    AL2(&IBUF(2)) LENGTH OF INPUT BUFFERS
         AGO   .CONTL2
.CONTL1  DC    AL2(0)        LENGTH OF INPUT BUFFERS
.CONTL2  AIF   ('&OBUF(2)' EQ '').CONTL3
         AIF   ('&OBUF(2)'(1,1) EQ '(').ERROR
         DC    AL2(&OBUF(2)) LENGTH OF OUTPUT BUFFERS
         AGO   .CONTLA
.CONTL3  DC    AL2(0)        LENGTH OF OUTPUT BUFFERS
.CONTLA  AIF   ('&OBUF(1)' EQ '').CONTLB
         AIF   ('&OBUF(1)'(1,1) EQ '(').ERROR
         DC    AL4(&OBUF(1))  ADDRESS OF OUTPUT BUFFERS
         AGO   .CONTL8
.CONTLB  DC    A(0)          ADDRESS OF OUTPUT BUFFERS
.CONTL8  AIF   ('&IBUF(1)' EQ '').CONTL9
         AIF   ('&IBUF(1)'(1,1) EQ '(').ERROR
         DC    AL4(&IBUF(1))    ADDRESS OF INPUT BUFFERS
         AGO   .CONTL0
.CONTL9  DC    A(0)          ADDRESS OF INPUT BUFFERS
.CONTL0  DC    AL1(&REPLSW+&DEFRSW)    REPLACE/NO REPLACE, DEFERRAL IND
.CONTL6  AIF   ('&USADDR' EQ '').CONTL7
         AIF   ('&USADDR'(1,1) EQ '(').ERROR
         DC    AL3(&USADDR)   ADDRESS OF USER PARAMETERS
         AGO   .ENDLIST
.CONTL7  DC    AL3(0)        ADDRESS OF USER PARAMETERS
.ENDLIST  ANOP
         MEXIT
.IROUT   ANOP
         LA    1,&GNAME      ADDRESS OF PARAMETER LIST
         AIF   ('&IBUF(2)' EQ '').IROUT1
         AIF   ('&IBUF(2)'(1,1) NE '(').IROUT1
         STH   &IBUF(2),4(1) LENGTH OF INPUT BUFFERS
.IROUT1  AIF   ('&OBUF(2)' EQ '').IROUT2
         AIF   ('&OBUF(2)'(1,1) NE '(').IROUT2
         STH   &OBUF(2),6(1) LENGTH OF OUTPUT BUFFERS
.IROUT2  AIF   ('&EXADDR' EQ '').IROUT3
         AIF   ('&EXADDR'(1,1) NE '(').IROUT3
         ST    &EXADDR(1),0(1,0)  ADDRESS OF EXIT ROUTINE INTO LIST
.IROUT3  AIF   ('&USADDR' EQ '').IROUT4
         AIF   ('&USADDR'(1,1) NE '(').IROUT4
         IC    15,16(1)           GET OPTIONS BYTE
         ST    &USADDR(1),16(1,0) ADDRESS OF USER PARAMETERS IN LIST
         STC   15,16(1)           PRESERVE OPTIONS BYTE
.IROUT4  AIF   ('&IBUF' EQ '').IROUT5
         AIF   ('&IBUF(1)'(1,1) NE '(').IROUT5
         ST    &IBUF(1),12(1,0)   ADDRESS OF INPUT BUFFERS INTO LIST
.IROUT5  AIF   ('&OBUF' EQ '').IROUT6
         AIF   ('&OBUF(1)'(1,1) NE '(').IROUT6
         ST    &OBUF(1),8(1,0)    ADDRESS OF OUTPUT BUFFERS INTO LIST
.IROUT6  ANOP
         B     20(0,1)
&GNAME   DS    0F
.CNTD    AIF   ('&EXADDR' EQ '').CNTE
         AIF   ('&EXADDR'(1,1) EQ '(').CNTE
         DC    AL4(&EXADDR)  ADDRESS OF EXIT ROUTINE
         AGO  .CONTF2
.CNTE    DC    AL4(0)   ADDRESS OF EXIT ROUTINE
.CONTF2  AIF   ('&IBUF(2)' EQ '').CNTA
         AIF   ('&IBUF(2)'(1,1) EQ '(').CNTA
         DC    AL2(&IBUF(2)) LENGTH OF INPUT BUFFERS
         AGO   .CNTB
.CNTA    DC    AL2(0)        LENGTH OF INPUT BUFFERS
.CNTB    AIF   ('&OBUF(2)' EQ '').CNTC
         AIF   ('&OBUF(2)'(1,1) EQ '(').CNTC
         DC    AL2(&OBUF(2))  LENGTH OF OUTPUT BUFFERS
         AGO   .CNTK
.CNTC    DC    AL2(0)        LENGTH OF OUTPUT BUFFERS
.CNTK    AIF   ('&OBUF(1)' EQ '').CNTL
         AIF   ('&OBUF(1)'(1,1) EQ '(').CNTL
         DC    AL4(&OBUF(1)) ADDRESS OF OUTPUT BUFFERS
         AGO   .CNTI
.CNTL    DC    A(0)          ADDRESS OF OUPTUT BUFFERS
.CNTI    AIF   ('&IBUF(1)' EQ '').CNTJ
         AIF   ('&IBUF(1)'(1,1) EQ '(').CNTJ
         DC    AL4(&IBUF(1)) ADDRESS OF INPUT BUFFERS
         AGO   .CNTM
.CNTJ    DC    A(0)          ADDRESS OF INPUT BUFFERS
.CNTM    DC    AL1(&REPLSW+&DEFRSW)    REPLACE/NO REPLACE, DEFERRAL IND
.CNTF    AIF   ('&USADDR' EQ '').CNTH
         AIF   ('&USADDR'(1,1) EQ '(').CNTH
         DC    AL3(&USADDR)  ADDRESS OF USER PARAMETERS
         AGO   .SVC
.CNTH    DC    AL3(0)        ADDRESS OF USER PARAMETERS
         AGO   .SVC
.CANCEL  ANOP
&NAME    DS    0H
         SR    1,1           CANCEL PREVIOUS STAX
.SVC     SVC   96
         MEND
