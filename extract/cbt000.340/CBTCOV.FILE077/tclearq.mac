         MACRO
&NAME    TCLEARQ &OPT
.*********************************************************************.
.*                                                                   *.
.*RELEASE 23 DELETIONS                                               *.
.*                                                                   *.
.*RELEASE 22 DELETIONS                                               *.
.*                                                                   *.
.*RELEASE 21 DELETIONS                                               *.
.*                                                                   *.
.*STATUS - CHANGE LEVEL 000                                          *.
.*                                                                   *.
.*NAME - TCLEARQ                                                     *.
.*                                                                   *.
.*FUNCTION - THE TCLEARQ MACRO CLEARS THE TIOC INPUT OR OUTPUT QUEUE.*.
.*   IF INPUT IS SPECIFIED, THE TERMINAL INPUT QUEUE WILL BE FLUSHED *.
.*   INCLUDING ANY LINE CURRENTLY BEING ENTERED. IF OUTPUT IS        *.
.*   SPECIFIED, THE ENTIRE TIOC OUTPUT QUEUE WILL BE FLUSHED.        *.
.*                                                                   *.
.*                                                                   *.
.*   TCLEARQ INPUT OR OUTPUT (DEFAULT IS INPUT)                      *.
.*                                                                   *.
.*********************************************************************.
         AIF   ('&OPT' EQ 'INPUT' OR '&OPT' EQ '' OR '&OPT' EQ         X
               'OUTPUT').EXPAND
.ERROR   IHBERMAC 49,,&OPT
         MEXIT
.EXPAND  ANOP
&NAME    LA    0,1                      SET ENTRY CODE
         AIF   ('&OPT' EQ 'OUTPUT').OUT
         LA    1,X'80'                  INDICATE INPUT
         AGO   .SVC
.OUT     SR    1,1                      INDICATE OUTPUT
.SVC     SLDL  0,24                     MOVE TO HI-ORDER BYTES
         SVC   94                       ISSUE SVC
         MEND
