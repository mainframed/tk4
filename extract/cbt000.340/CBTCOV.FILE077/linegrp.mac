         MACRO
&LINNME  LINEGRP &TERM=,&DDNAME=,&LINENO=,&UNITNO=1,&TRANTAB=,&TERMNO=,X
               &CODE=,&DIAL=YES,&INTVL=,&FEATURE=,&ADDR=,&SCREEN=,     X
               &PCI=                                            SA59163
.*
.*        POST RELEASE 21.8 CHANGES
.*C596959,596979,596991,596995                                  SA72293
.*D596997-597498                                                SA72293
.*        RELEASE 21.8, 01/17/74, CHANGE LEVEL=02               SA68208
.*
.*
.*FUNCTION -- AFTER EXAMINING THE CALLER'S PARAMETERS FOR VALIDITY,
.*   VALUES ARE PLACED IN GLOBAL VARIABLES TO REFLECT THE CALLER'S
.*   SPECIFICATIONS FOR THIS LINE GROUP.  THE VALUES IN THE GLOBAL
.*   VARIABLES WILL BE USED LATER BY THE 'TSOMCP' MACRO TO GOVERN
.*   GENERATION OF THE TCAM MCP MACRO STATEMENTS.
.*
.*ENTRY POINTS -- NOT APPLICABLE
.*
.*INPUT -- MACRO CALL PARAMETERS
.*
.*OUTPUT -- VALUES IN GLOBAL VARIABLES TO BE USED LATER BY THE
.*   'TSOMCP' MACRO.  MNOTE MESSAGES IF INVALID PARAMETERS ARE
.*   DETECTED.
.*
.*EXTERNAL REFERENCES -- NOT APPLICABLE
.*
.*EXITS, NORMAL -- NORMAL EXIT OCCURS IF NO SERIOUS ERRORS IN THE
.*   CALLER'S PARAMETERS ARE DETECTED.
.*
.*EXITS,ERROR -- UPON DETECTION OF A SERIOUS ERROR, THE 'QUITSW'
.*   IS SET TO 1 AND FURTHER PROCESSING IS BYPASSED.
.*
.*TABLES/WORK AREAS -- SEE DESCRIPTIONS ACCOMPANYING VARIABLE
.*   DECLARATIONS.
.*
.*ATTRIBUTES -- NOT APPLICABLE
.*
.*CHARACTER CODE DEPENDENCY -- NONE
.*
.*NOTES -- NONE
.*
.*
.*       GLOBAL VARIABLES
.*
         GBLA  &NLG                NUMBER OF LINE GROUP MACROS ISSUED
         GBLA  &LNO(63)            CONTAINS LINENO= PARAMETER VALUES
         GBLA  &UNO(63)            CONTAINS UNITNO= PARAMETER VALUES
         GBLA  &INT(63)            CONTAINS INTVL= PARAMETER VALUES
         GBLA  &ADRPT              POINTER TO LAST ADDR PARM GROUP
         GBLA  &SCRPT              POINTER TO LAST SCREEN PARM GROUP
         GBLA  &SCRH(2500)         CONTAINS SCREEN HEIGHT
         GBLA  &SCRW(2500)         CONTAINS SCREEN WIDTH
         GBLA  &TRMNO(2500)        CONTAINS NO. OF TERMINALS/LINE
         GBLB  &LGRPSW                 LINEGRP ISSUED SWITCH
         GBLB  &MCPSW                  TSOMCP  ISSUED SWITCH
         GBLB  &QUITSW                 GENERATION QUIT SWITCH
         GBLB  &BISYNC                 BISYNCHRONOUS LINE
         GBLB  &DIALSW(63)             DIAL LINE SWITCHES
         GBLB  &FETAB(189)             FEATURE= SWITCHES
.*                                     (3 X 63) = 189
         GBLC  &T(63)              CONTAINS TERM= PARAMETER VALUES
         GBLC  &DDNME(63)          CONTAINS DDNAME= PARAMETER VALUES
         GBLC  &TTAB(252)          CONTAINS TRANTAB= PARAMETER VALUES
         GBLC  &CTAB(252)          CONTAINS CODE= PARAMETER VALUES
.*                                     (4 X 63) = 252
         GBLC  &ADCHAR(2500)       CONTAINS ADDR= PARAMETER VALUES
         GBLC  &PC(63)             CONTAINS PCI SPECIFICATIONS  SA59163
.*
.*       LOCAL VARIABLES
.*
         LCLA  &A
         LCLA  &B
         LCLA  &C
         LCLA  &D
         LCLA  &I
         LCLB  &BADTERM                TERM= BAD
         LCLB  &BADDDNM                DDNAME= BAD
         LCLB  &BADLNO                 LINENO= BAD
         LCLB  &BADUNO                 UNITNO= BAD
         LCLB  &BADTTAB                TRANTAB= BAD
         LCLB  &BADCODE                CODE= BAD
         LCLB  &BADDIAL                DIAL= BAD
         LCLB  &BADINTV                INTVL= BAD
         LCLB  &BADFE                  FEATURE= BAD
         LCLB  &QTDFTSW                QUOTE DEFAULT SW
         LCLB  &SWBC               BC41 TRAN TABLE SPECIFIED
         LCLB  &SWEB               EB41 TRAN TABLE SPECIFIED
         LCLB  &SWEBAS             EBCD OR ASCI TABLE SPECIFIED
         LCLB  &SW2260             INDICATES 2260/65 LINE GROUP
         LCLB  &SW3270             INDICATES 3270 LINE GROUP
         LCLC  &F                  HOLDS A FEATURE SUB-LIST MEMBER,
.*                                 OR A PCI SUB-LIST MEMBER     SA59163
.*
.*       TEST FOR MACRO CALL OUT OF ORDER OR TOO MANY LINEGRP MACRO
.*       CALLS ISSUED.
.*
&NLG     SETA  &NLG+1
&I       SETA  &NLG                     SHORT FORM FOR NLG
         AIF   (NOT &MCPSW).A0         TSOMCP MACRO ISSUED?
         MNOTE 16,'IKJ54101I LINEGRP MACRO MUST PRECEDE TSOMCP MACRO'
&QUITSW  SETB  1                       YES, SET QUIT SW.
.A0      ANOP
&LGRPSW  SETB  1                       SET LINEGRP ISSUED SW.
         AIF   (&NLG LE 63).A1         MAX. LINEGRP COUNT EXCEEDED?
         MNOTE 16,'IKJ54102I MORE THAN 63 LINEGRP MACROS'
&QUITSW  SETB  1                       YES, SET QUIT SW.
         MEXIT
.*
.*       ARE REQUIRED OPERANDS SPECIFIED
.*
.A1      AIF   ('&TERM' NE '' AND '&DDNAME' NE '' AND '&LINENO' NE '').*
               TERM
         MNOTE 12,'IKJ54103I REQUIRED OPERANDS(S) NOT SPECIFIED'
&QUITSW  SETB  1                       NO, SET QUIT SW.
         MEXIT
.*  -------------------------------------------------------------------
.*  -----      TERM= KEYWORD ANALYSIS       ---------------------------
.*  -------------------------------------------------------------------
.TERM    AIF   (K'&TERM GT 4 OR N'&TERM GT 1).T4    TERM=KEYWORD VALID?
         AIF   ('&TERM' EQ '1050').T1   BRANCH IF TERM=1050
         AIF   ('&TERM' EQ '2741').T1   BRANCH IF TERM=2741
         AIF   ('&TERM' EQ '5041').T1   BRANCH IF TERM=5041
         AIF   ('&TERM' EQ '3335').T1   BRANCH IF TERM=3335
         AIF   ('&TERM' EQ '226R').T1   BRANCH IF TERM=226R
         AIF   ('&TERM' EQ '226L').T1   BRANCH IF TERM=226L
         AIF   ('&TERM' EQ '2265').T1   BRANCH IF TERM=2265
         AIF   ('&TERM' EQ '327L').T2   BRANCH IF TERM=327L
         AIF   ('&TERM' EQ '327R').T2   BRANCH IF TERM=327R
         AIF   ('&TERM' EQ '327S').T2   BRANCH IF TERM=327S
.T4      MNOTE 16,'IKJ54104I &TERM INVALID FOR TERM OPERAND'
&BADTERM SETB  1                       INDICATE BAD TERM= KEYWORD.
&QUITSW  SETB  1                       SET QUIT SW.
         AGO   .DDNME              GO CHECK DD NAME PARAMETER
.T2      ANOP
&BISYNC  SETB  1                   BISYNCHRONOUS LINE
.T1      ANOP
&T(&I)   SETC  '&TERM'             PUT VALUE IN ARRAY
.*  -------------------------------------------------------------------
.*  -----      DDNAME= KEYWORD ANALYSIS     ---------------------------
.*  -------------------------------------------------------------------
.DDNME   AIF   (K'&DDNAME LE 8 AND N'&DDNAME EQ 1).DD1   KEYWORD VALID?
         MNOTE 16,'IKJ54101I &DDNAME INVALID FOR DDNAME OPERAND'
&BADDDNM SETB  1                       INDICATE BAD DDNAME= KEYWORD.
&QUITSW  SETB 1                        NO, SET QUIT SW,
         AGO   .LNO                    BR TO TEST LINENO.
.DD1     ANOP
&DDNME(&I) SETC '&DDNAME'              SAVE NAME.
.*  -------------------------------------------------------------------
.*  -----      LINENO= KEYWORD ANALYSIS     ---------------------------
.*  -------------------------------------------------------------------
.LNO     AIF   (K'&LINENO GT 2 OR N'&LINENO GT 1).L1     KEYWORD VALID?
         AIF   (T'&LINENO NE 'N').L1
         AIF   (&LINENO LE 51 AND &LINENO GT 0).L2
.L1      MNOTE 16,'IKJ54104I &LINENO INVALID FOR LINENO OPERAND'
&BADLNO  SETB  1                       INDICATE BAD LINENO= KEYWORD.
&QUITSW  SETB  1                       NO, SET QUIT SW,
         AGO   .UNO                    BR TO TEST UNITNO.
.L2      ANOP
&LNO(&I) SETA  &LINENO                 SAVE LINENO.
.*  -------------------------------------------------------------------
.*  -----      UNITNO= KEYWORD ANALYSIS     ---------------------------
.*  -------------------------------------------------------------------
.UNO     AIF   (N'&UNITNO GT 1).U1     UNITNO= KEYWORD VALID?
         AIF   (T'&UNITNO EQ 'N').U2
.U1      MNOTE 16,'IKJ54104I &UNITNO INVALID FOR UNITNO OPERAND'
&BADUNO  SETB  1                       INDICATE BAD UNITNO= KEYWORD.
&QUITSW  SETB  1                       NO, SET QUIT SW,
         AGO   .TTAB                   BR TO TEST TRANTAB.
.U2      ANOP
         AIF   (&UNITNO GT 0 AND &UNITNO LT 256).U3  OPERAND IN RANGE
         MNOTE 4,'IKJ54110I UNITNO OPERAND OUT OF RANGE, DEFAULT VALUE -
               USED'
&UNO(&I) SETA  1                        USE DEFAULT VALUE
         AGO   .TTAB                    BR TO TEST TRANTAB
.U3      ANOP
&UNO(&I) SETA  &UNITNO                 SAVE UNITNO.
.*  -------------------------------------------------------------------
.*  -----      TRANTAB= KEYWORD ANALYSIS    ---------------------------
.*  -------------------------------------------------------------------
.TTAB    ANOP
         AIF   (&BADTERM).TTQUIT
         AIF   ('&TRANTAB' EQ '').TTABDFT   TEST TRANTAB FOR DEFAULT.
&C       SETA  N'&TRANTAB               GET NO. OF SUBLIST OPERANDS
.*
.*
.*       SET UP FOR LOOP TO CHECK TABLE VALIDITY & STORE IN ARRAY
.*
&A       SETA  (&NLG-1)*4          SET ARRAY POINTER
&B       SETA  0                   SET SUBLIST POINTER
&D       SETA  1                   SET ARRAY OFFSET POINTER
.*
.TTLOOP1 ANOP                      LOOP EXECUTED ONCE/TRANSLATE TABLE
&B       SETA  &B+1                INCREMENT LOOP COUNTER
         AIF   (&B GT &C).CODE     BR IF NO MORE TABLES
.*
.*       SELECT CHECKING ROUTINE ACCORDING TO TERMINAL TYPE
.*
         AIF   ('&TERM' EQ '5041').TT5041
         AIF   ('&TERM' EQ '1050').TT5041
         AIF   ('&TERM' EQ '2741').TT2741
         AIF   ('&TERM' EQ '3335').TT3335
         AIF   ('&TERM' EQ '226L').TT226L
         AIF   ('&TERM' EQ '226R').TT226X
         AIF   ('&TERM' EQ '2265').TT226X
         AIF   ('&TERM' EQ '327L').TT226L
         AIF   ('&TERM' EQ '327R').TT327X
         AIF   ('&TERM' EQ '327S').TT327X
.*
.*       5041 & 1050
.*
.TT5041  ANOP
         AIF   ('&TRANTAB(&B)' EQ '1050').TTEB41
         AIF   ('&TERM' EQ '1050').TTBAD
.*
.*       2741
.*
.TT2741  AIF   ('&TRANTAB(&B)' EQ 'EB41').TTEB41
         AIF   ('&TRANTAB(&B)' EQ 'BC41').TTBC41
         AIF   ('&TRANTAB(&B)' EQ 'CR41').TTOK
         AGO   .TTBAD
.*
.*       3335
.*
.TT3335  ANOP
         AIF   ('&TRANTAB(&B)' EQ 'TTYB').TTOK
         AIF   ('&TRANTAB(&B)' EQ 'TTYC').TTOK
         AGO   .TTBAD
.*
.*       226L
.*
.TT226L  ANOP
         AIF   ('&TRANTAB(&B)' EQ 'EBCD').TTOK
         AGO   .TTBAD
.*
.*       226X
.*
.TT226X  ANOP
         AIF   ('&TRANTAB(&B)' EQ '2260').TTOK
         AIF   ('&TRANTAB(&B)' EQ '2265').TTOK
.*
.*       327R
.*
.TT327X  ANOP
         AIF   ('&TRANTAB(&B)' EQ 'EBCD').TT1
         AIF   ('&TRANTAB(&B)' NE 'ASCI').TTBAD
.TT1     ANOP
         AIF   (NOT &SWEBAS).TT2
         MNOTE 4,'IKJ54113I EBCD OR ASCI (BUT NOT BOTH) ALLOWED FOR THI-
               S TERMINAL TYPE, DEFAULT VALUE USED'
         AGO   .TTABDFT
.TT2     ANOP
&SWEBAS  SETB  1
         AGO   .TTOK
.*
.TTBAD   ANOP
         MNOTE 12,'IKJ54105I TRANTAB=&TRANTAB(&B) INVALID FOR THIS TERM-
               INAL TYPE'
&BADTTAB SETB  1                   INDICATE TRANTAB PARAMETER BAD
&QUITSW  SETB  1                   INDICATE SERIOUS ERROR
         AGO   .TTLOOP1            GO CHECK NEXT SUB-OPERAND
.*
.*       PUT TRANSLATE TABLE NAME INTO ARRAY
.*
.TTOK    ANOP
&TTAB(&A+&D) SETC '&TRANTAB(&B)'   INSERT TBL NAME IN ARRAY
&D       SETA  &D+1                BUMP ARRAY OFFSET POINTER
         AGO   .TTLOOP1            GO PROCESS NEXT SUB-OPERAND
.*
.TTEB41  ANOP
&SWEB    SETB  1                   INDICATE EB41 TABLE SPECIFIED
         AGO   .TTOK               GO PUT TABLE INTO ARRAY
.*
.TTBC41  ANOP
&SWBC    SETB  1                   INDICATE BC41 TABLE SPECIFIED
         AGO   .TTOK               GO PUT TABLE INTO ARRAY
.TTQUIT ANOP
&BADTTAB SETB  1                       INDICATE BAD TRANTAB= KEYWORD.
&QUITSW  SETB  1                       SET QUIT SW.
         AGO   .CODE
.*
.*    PROCESS TRANTAB DEFAULT VALUES.
.*
.TTABDFT AIF   ('&TERM' NE '1050').T2741  TERM=1050 SPECIFIED?
&A       SETA  (&NLG-1)*4              INITILIZE INDEX A.
&TTAB(&A+1) SETC '1050'                YES, DEFAULT TO TAB 1050.
         AGO   .CODE
.T2741   AIF   ('&TERM' NE '2741').T5041  TERM=2741 SPECIFIED?
&A       SETA  (&NLG-1)*4              INITILIZE INDEX A.
&TTAB(&A+1) SETC 'CR41'                 YES, DEFAULT TO TAB CR41,
&TTAB(&A+2) SETC 'EB41'                                 TAB EB41,
         AGO   .CODE
.T5041   AIF   ('&TERM' NE '5041').T3335  TERM=5041 SPECIFIED?
&A       SETA  (&NLG-1)*4              INITILIZE INDEX A.
&TTAB(&A+1) SETC '1050'                 YES, DEFAULT TO TAB 1050,
&TTAB(&A+2) SETC 'CR41'                                 TAB CR41.
&TTAB(&A+3) SETC 'EB41'                                 TAB EB41,
         AGO   .CODE
.T3335   AIF   ('&TERM' NE '3335').T2260  TERM=3335 SPECIFIED?
&A       SETA  (&NLG-1)*4              INITILIZE INDEX A.
&TTAB(&A+1) SETC 'TTYB'                 YES, DEFAULT TO TAB TTYB,
         AGO   .CODE
.T2260   AIF   ('&TERM' NE '226R').T226L  TERM=226R SPECIFIED?
&TTAB((&NLG-1)*4+1) SETC '2260'         SET 2260 DEFAULT
         AGO   .CODE
.T226L   AIF   ('&TERM' NE '226L').T2265  TERM=226L SPECIFIED?
&TTAB((&NLG-1)*4+1) SETC 'EBCD'         SET 2260 LOCAL DEFAULT
         AGO   .CODE
.T2265   AIF   ('&TERM' NE '2265').T3270  TERM=265 SPECIFIED?
&TTAB((&NLG-1)*4+1) SETC '2265'         SET 2265 DEFAULT
         AGO   .CODE
.T3270   ANOP
         AIF   ('&TERM' EQ '327L').TT3  TERM=327L SPECIFIED?
         AIF   ('&TERM' EQ '327R').TT3  TERM=327R SPECIFIED
         AIF   ('&TERM' NE '327S').TTQUIT
.TT3     ANOP
&TTAB((&NLG-1)*4+1) SETC 'EBCD'         SET 3720 DEFAULT
.CODE    ANOP
.*  -------------------------------------------------------------------
.*  -----      CODE= KEYWORD ANALYSIS    ------------------------------
.*  -------------------------------------------------------------------
         AIF   (&BADTTAB).CODER1        TRANTAB SPECIFIED CORRECTLY?
         AIF   ('&CODE' EQ '').CODEDFT TEST CODE FOR DEFAULT.
.*
.*             NOT DEFAULT, TEST FOR VALIDITY.
.*
         AIF   ('&TTAB((&NLG-1)*4+2)' EQ '').CODER2  BR IF ONLY 1 TBL
&C       SETA  N'&CODE                 GET NO. OF SUBLIST OPERANDS.
         AIF   (&C GT 4).CODER3        GT 4 OPERANDS SPECIFIED?
.*
.*   TEST CHARACTER COUNT OF EACH SUBLIST OPERAND.
.*
&D       SETA  1
.CLOOP1  AIF   (K'&CODE(&D) GT 8).CODER4  GT 8 CHARACTERS SPECIFIED?
         AIF   (&D EQ &C).CODEOK       MORE SUB-OPERANDS? NO, BR.
&D       SETA  &D+1                    INIT. TO TEST NEXT SUB-OPERAND
         AGO   .CLOOP1                 BR TO TEST AGAIN.
.CODER1  ANOP
         AGO   .CQUIT
.CODER2  MNOTE 4,'IKJ54106I CODE OPERAND SHOULD BE SPECIFIED ONLY IF   X
                MORE THAN ONE TRANSLATE TABLE IS SPECIFIED'
         AGO   .DIAL
.CODER3  MNOTE 12,'IKJ54107I CODE OPERAND CONTAINS MORE THAN 4 SUBLIST X
                OPERANDS'
         AGO   .CQUIT
.CODER4  MNOTE 12,'IKJ54108I CODE SUBOPERAND TOO LONG'
.CQUIT   ANOP
&BADCODE SETB  1                        INDICATE BAD CODE= KEYWORD.
&QUITSW  SETB  1                       SET QUIT SW.
         AGO   .DIAL                    BR TO TEST DIAL.
.*
.*   PROCESS CODE VALUES INTO TABLE.
.*
.CODEOK  ANOP
&A       SETA  (&NLG-1)*4               INITILIZE INDEX A.
&B       SETA  0                   INITIALIZE INDEX B
.CLOOP2  ANOP
&B       SETA  &B+1                     INCREMENT INDEX B.
         AIF   ('&CODE(&B)' EQ '').CODNULL  SKIP NULL OPERANDS
&CTAB(&A+1) SETC '&CODE(&B)'           SAVE CODE SUB-OPERAND IN TABLE.
&A       SETA  &A+1                    YES, INCREMENT INDEX A.
.CODNULL ANOP
         AIF   (N'&CODE LT (&B+1)).DIAL MORE SUB-OPERANDS? NO, BR.
         AGO   .CLOOP2
.*
.*   PROCESS CODE DEFAULT VALUES.
.*
.CODEDFT ANOP
         AIF   (NOT &SWBC  OR NOT &SWEB).CODESET
&QTDFTSW SETB  1                   INDICATE QUOTE IS DEFAULT VALUE
.CODESET ANOP
&A       SETA  (&NLG-1)*4               INITILIZE INDEX A.
         AIF   (&QTDFTSW).C1           DEFAULT OF (") TO BE USED.
&CTAB(&A+1) SETC 'LOGON'               NO, USE 'LOGON'.
         AGO   .DIAL                    BR TO TEST DIAL.
.C1      ANOP
&CTAB(&A+1) SETC '"LOGON'          SET DEFAULT FOR BC41/EB41
.DIAL    ANOP
.*  -------------------------------------------------------------------
.*  -----      DIAL= KEYWORD ANALYSIS.     ----------------------------
.*  -------------------------------------------------------------------
         AIF   (N'&DIAL GT 1).D1       TEST FOR VALID KEYWORD.
         AIF   ('&DIAL' EQ 'YES' OR '&DIAL' EQ 'NO').D2
.D1      MNOTE 16,'IKJ54104I &DIAL INVALID FOR DIAL OPERAND'
         AGO   .DQUIT
.D2      AIF   (NOT &BADTERM).D3       TERM= KEYWORD BAD?
         AGO   .DQUIT
.D3      AIF   ('&DIAL' EQ 'YES').D4   DIAL=YES TO BE USED?
         AIF   ('&TERM' EQ '5041' OR '&TERM' EQ '3335').D5
         AGO   .INTVL                  BR TO TEST INTVL.
.D4      ANOP
         AIF   ('&TERM'(1,3) EQ '226' OR '&TERM'(1,3) EQ '327').D5
.*                                 2260'S AND 3270'S ARE NON-SWITCHED
&DIALSW(&I) SETB 1                     INDICATE DIAL=YES SPECIFIED.
         AGO   .INTVL                  BR TO TEST INTVL.
.D5      MNOTE 12,'IKJ54109I DIAL OPERAND NOT CONSISTANT WITH TERM     X
                OPERAND'
.DQUIT   ANOP
&BADDIAL SETB  1                       INDICATE BAD DIAL=KEYWORD.
&QUITSW SETB  1                        SET QUIT SW.
.INTVL ANOP
.*  -------------------------------------------------------------------
.*  ----- INTVL= KEYWORD ANALYSIS     ---------------------------------
.*  -------------------------------------------------------------------
         AIF   (N'&INTVL GT 1).I1      TEST FOR VALID KEYWORD.
         AIF   ('&INTVL' EQ '').I3  IF OMITTED, GO SET DEFAULT
         AIF   (&INTVL GE 0 AND &INTVL LE 255).I2
.I1      MNOTE 4,'IKJ54110I INTVL OPERAND OUT OF RANGE, DEFAULT VALUE U-
               SED'
.I3      ANOP
&INT(&I) SETA  0                   USE DEFAULT VALUE
         AIF   ('&TERM' EQ '2741').FEATURE
         AIF   ('&TERM' EQ '226L').FEATURE
         AIF   ('&TERM' EQ '327L').FEATURE
         AIF   ('&TERM' EQ '3335').FEATURE
&INT(&I) SETA  2                   SET DEFAULT FOR POLLED TERMINALS
         AGO   .FEATURE                BR TO TEST FEATURE.
.I2      ANOP
&INT(&I) SETA  &INTVL                  SAVE INTVL VALUE.
.FEATURE ANOP
.*  -------------------------------------------------------------------
.*   -----      FEATURE= KEYWORD ANALYSIS     -------------------------
.*  -------------------------------------------------------------------
         AIF   (N'&FEATURE GT 3).FEER1 TEST FOR EXCESSIVE OPERANDS.
         AIF   (&BADTERM).FEER2        TEST FOR BAD TERM= KEYWORD.
.*
.*       ESTABLISH DEFAULT ASSUMPTIONS
.*
&A       SETA  (&NLG-1)*3          SET TABLE GROUP POINTER
         AIF   ('&TERM' EQ '2741' OR '&TERM' EQ '3335').F1
         AIF   ('&TERM' EQ '226L' OR '&TERM' EQ '226R').F226
         AIF   ('&TERM' EQ '327L' OR '&TERM' EQ '327R').F327
         AIF   ('&TERM' EQ '327S').F327
         AIF   ('&TERM' NE '2265').NOBATN
.F226    ANOP
.*  ASSUME (NOBREAK,NOATTN) FOR 2260/65
&SW2260  SETB  1                   INDICATE 2260/65 LINE GROUP
         AGO   .F2                 GO PROCESS FEATURE SUBLIST
.F327    ANOP
.*  ASSUME (NOBREAK,NOATTN) FOR 3270
&SW3270  SETB  1                   INDICATE 3270 LINE GROUP
         AGO   .F2
.NOBATN  ANOP
.* ASSUME (NOBREAK,ATTN) FOR 1050 AND 5041
&FETAB(&A+2) SETB 1
         AGO   .F2
.* ASSUME (BREAK,ATTN,TOSUPPR) FOR 2741 AND 3335
.F1      ANOP
&FETAB(&A+1) SETB 1
&FETAB(&A+2) SETB 1
&FETAB(&A+3) SETB 1
.*
.*       PROCESS FEATURE SUBLIST, SETTING TABLE SWITCHES IF INDICATED
.*       FEATURES ARE VALID.
.*
.F2      ANOP
         AIF   ('&FEATURE' EQ '').TERMNO  BR IF DEFAULTS WANTED
&B       SETA  1              SET SUB LIST POINTER
.FLOOP   ANOP
&F       SETC  '&FEATURE(&B)'
         AIF   ('&F' NE 'BREAK').FL1
         AIF   (&SW2260 OR &SW3270).FEER3
&FETAB(&A+1) SETB 1
         AGO   .FTEST
.FL1     AIF   ('&F' NE 'NOBREAK').FL2
         AIF   ('&TERM' EQ '3335').FEER3
&FETAB(&A+1) SETB 0
         AGO   .FTEST
.FL2     AIF   ('&F' NE 'ATTN').FL3
         AIF   (&SW2260 OR &SW3270).FEER3
&FETAB(&A+2) SETB 1
         AGO   .FTEST
.FL3     AIF   ('&F' NE 'NOATTN').FL4
         AIF   ('&TERM' EQ '3335').FEER3
&FETAB(&A+2) SETB 0
         AGO   .FTEST
.FL4     AIF   ('&F' NE 'TOSUPPR').FEER3
         AIF   (&SW2260 OR &SW3270).FEER3
&FETAB(&A+3) SETB 1
.FTEST   ANOP
&B       SETA  &B+1
         AIF   (&B LE N'&FEATURE).FLOOP
         AGO   .TERMNO
.FEER1   MNOTE 12,'IKJ54111I FEATURE OPERAND CONTAINS TOO MANY SUBLIST -
                OPERANDS'
         AGO   .FQUIT
.FEER2   ANOP
         AGO   .FQUIT
.FEER3   MNOTE 4,'IKJ54112I SUBLIST OPERAND &B OF FEATURE OPERAND IS IN-
               VALID'
         AGO   .FTEST              GO CONTINUE TESTING
.FQUIT   ANOP
&BADFE   SETB  1                       INDICATE BAD FEATURE= KEYWORD.
&QUITSW  SETB  1                       SET QUIT SW.
.**********************************************************************
.*       TERMNO= KEYWORD ANALYSIS
.**********************************************************************
.TERMNO  ANOP
.*
.*       CHECK FOR TOO MANY TERMINALS
.*
&A       SETA  1                   SET SUBPARAMETER SELECTOR
&B       SETA  0                   CLEAR TERMINAL COUNT
.TCNTLOP ANOP                      LOOP EXECUTED ONCE/LINE IN LINEGRP
         AIF   ('&TERMNO(&A)' EQ '').COUNT1  TEST FOR DEFAULT
&B       SETA  &B+&TERMNO(&A)      UPDATE TERMINAL COUNT
         AGO   .TCNTTST            GO TO LOOP TEST
.COUNT1  ANOP
&B       SETA  &B+1                UPDATE TERMINAL COUNT BY 1
.TCNTTST ANOP
&A       SETA  &A+1                INCREMENT SUBPARAMETER SELECTOR
         AIF   (&A LE &LNO(&NLG)).TCNTLOP
.*
.*       UPDATE SCREEN AND ADDR CHAR ARRAY POINTERS
.*
         AIF   (&NLG NE 1).TRM1    BRANCH UNLESS THIS IS FIRST CALL
&ADRPT   SETA  1                   INITIALIZE ADDR CHARS ARRAY POINTER
&SCRPT   SETA  1                   INITIALIZE SCREEN ARRAY POINTER
         AGO   .TRM2               GO TEST PARAMETER VALIDITY
.TRM1    ANOP
&B       SETA  0                   SET LOOP VARIABLE
.TRMLP1  ANOP                      LOOP ONCE/LINE IN LAST LINEGRP
&ADRPT   SETA  &ADRPT+&TRMNO(&SCRPT+&B) UPDATE ADDR ARRAY POINTER
&B       SETA  &B+1                BUMP LOOP CONTROL VARIABLE
         AIF   (&B LT &LNO(&NLG-1)).TRMLP1  BR IF MORE LINES
&SCRPT   SETA  &SCRPT+&B           UPDATE SCREEN ARRAY POINTER
.*
.*       CHECK PARAMETER VALIDITY
.*
.TRM2    AIF   ('&TERMNO' EQ '').TRMDEF
         AIF   (&SW2260 OR &SW3270).TRM3
.*                                 TERMO VALID FOR 2260/2265/3270
         MNOTE 4,'IKJ54114I TERMNO OPERAND NOT ALLOWED FOR THIS TERMINA-
               L TYPE'
         AGO   .TRMDEF             GO GENERATE DEFAULT VALUES
.TRM3    ANOP
         AIF   (&BADLNO).PCICK
         AIF   (N'&TERMNO LE &LNO(&NLG)).TRM4
         MNOTE 8,'IKJ54115I TERMNO OPERAND REFERENCES NON-EXISTENT LINE-
               S'
.*
.*       MOVE PARAMETER VALUES INTO ARRAY
.*
.TRM4    ANOP
&A       SETA  N'&TERMNO           SET NUMBER OF OPERANDS TO MOVE
.TRM5    ANOP                      ENTER HERE IF NOT TO MOVE ALL
&B       SETA  1                   SET LOOP CONTROL VARIABLE
.TRMMOV  ANOP                      LOOP ONCE/LINE IN LINE GROUP
         AIF   (&B GT &A).TRMONE   BR IF DEFAULT NEEDED
         AIF   ('&TERMNO(&B)' EQ '').TRMONE  BR IF DEFAULT NEEDED
&TRMNO(&SCRPT+&B-1) SETA &TERMNO(&B) MOVE OPERAND INTO ARRAY
         AGO   .TRM6               GO TO LOOP TEST
.TRMONE  ANOP
&TRMNO(&SCRPT+&B-1) SETA 1         PUT DEFAULT VALUE IN ARRAY
.TRM6    ANOP
&B       SETA  &B+1                BUMP LOOP CONTROL VARIABLE
         AIF   (&B LE &LNO(&NLG)).TRMMOV  LOOP BACK IF MORE LINES
         AGO   .SCREEN             GO PROCESS SCREEN PARAMETER
.TRMDEF  ANOP                      SET DEFAULT ONE TERMINAL/LINE
&A       SETA  0                   ASK TO MOVE NO OPERANDS
         AGO   .TRM5               GO MOVE DEFAULT VALUES INTO ARRAY
.*
.*********************************************************************
.*       SCREEN=
.*********************************************************************
.*
.SCREEN  ANOP
         AIF   ('&SCREEN' EQ '').SCRDEF BR IF DEFAULT VALUES NEEDED
         AIF   (&SW2260 OR &SW3270).SCR1
.*                                      BR IF VALID TERMINAL TYPE
         MNOTE 4,'IKJ54114I SCREEN OPERAND NOT ALLOWED FOR THIS TERMINA-
               L TYPE'
         AGO   .ADDR
.SCR1    ANOP
         AIF   (N'&SCREEN EQ 2).SCR2    BR IF NO. OF OPERANDS OK
         MNOTE 4,'IKJ54116I SCREEN OPERAND MUST BE A TWO-OPERAND SUBLIS-
               T'
         AGO   .SCRDEF                  GO GENERATE DEFAULT VALUES
.*
.*       CHECK FOR STANDARD VALUES
.*
.SCR2    ANOP
&A       SETA  &SCREEN(1)
&B       SETA  &SCREEN(2)
         AIF   (&A EQ 12 AND &B EQ 40).SCRSTD
         AIF   (&A EQ 12 AND &B EQ 80).SCR3
         AIF   (&A EQ 6 AND &B EQ 40).SCR3
         AIF   (&A EQ 15 AND &B EQ 64).SCR3
         AIF   (&A EQ 24 AND &B EQ 80).SCR4
         AGO   .SCRNSTD
.SCR3    ANOP
         AIF   (&SW2260).SCRSTD
         AGO   .SCRNSTD
.SCR4    ANOP
         AIF   (&SW3270).SCRSTD
.SCRNSTD ANOP
         MNOTE 4,'IKJ54117I NON-STANDARD SCREEN DIMENSIONS -- ACCEPTED'
         AGO   .SCRSTD                                          SA68208
.*
.SCRDEF  ANOP
         AIF   (NOT &SW2260).SCRDEF1    GENERATE FOR 2260/65
&A       SETA  12                       SET DEFAULT HEIGHT
&B       SETA  80                       SET DEFAULT WIDTH
         AGO   .SCRSTD
.SCRDEF1 ANOP
         AIF   (NOT &SW3270).ADDR       GENERATE FOR 3270
&A       SETA  24                       SET DEFAULT HEIGHT
&B       SETA  80                       SET DEFAULT WIDTH
.*
.SCRSTD  ANOP
.*
.*       INSERT SCREEN DIMENSIONS IN ARRAYS
.*
&C       SETA  0                        SET LOOP CONTROL VARIABLE
.SCRLOOP ANOP                           LOOP ONCE/LINE IN LINE GROUP
&SCRH(&SCRPT+&C) SETA &A                SET SCREEN HEIGHT
&SCRW(&SCRPT+&C) SETA &B                SET SCREEN WIDTH
&C       SETA  &C+1                     BUMP LOOP CONTROL VARIABLE
         AIF   (&C LT &LNO(&NLG)).SCRLOOP  LOOP IF MORE LINES
.*
.*********************************************************************
.*       ADDR=
.*********************************************************************
.*
.ADDR    ANOP
         AIF   ('&ADDR' EQ '').PCICK
         AIF   (N'&ADDR GT 1).ADER3
         AIF   ('&TERM' NE '1050' AND '&TERM' NE '5041').ADDR226
         AIF   (K'&ADDR NE 2).ADER2
         AGO   .PUTADDR
.ADDR226 ANOP
         AIF   ('&TERM' EQ '226R' OR '&TERM' EQ '2265').ADDR1
         AIF   ('&TERM' EQ '327R' OR '&TERM' EQ '327S').ADDR1
         AGO   .ADER1
.ADDR1   ANOP
         AIF   (K'&ADDR NE 4).ADER2
.*
.*       PUT ADDR CHARS INTO ARRAY
.*
.PUTADDR ANOP
&A       SETA  0                        SET LOOP CONTROL VARIABLE
&C       SETA  0                   SET TERMINAL COUNTER
.ADRLOOP ANOP                           LOOP ONCE/LINE IN LINE GROUP
&B       SETA  0                        SET INNER LOOP CTRL VARIABLE
.ADRINLP ANOP                           LOOP ONCE/TERMINAL ON LINE
&ADCHAR(&ADRPT+&C) SETC '&ADDR'    PUT PARAMETER VALUE IN ARRAY
&C       SETA  &C+1                BUMP TERMINAL COUNTER
&B       SETA  &B+1                     BUMP INNER LOOP COUNTER
         AIF   (&B LT &TRMNO(&SCRPT+&A)).ADRINLP  LOOP IF MORE TERM'S
&A       SETA  &A+1                     BUMP LOOP CONTROL VARIABLE
         AIF   (&A LT &LNO(&NLG)).ADRLOOP  LOOP IF MORE LINES
         AGO   .PCICK
.*
.*       ADDR PARAMETER ERROR MESSAGES
.*
.ADER1   MNOTE 4,'IKJ54114I ADDR OPERAND NOT ALLOWED FOR THIS TERMINAL -
                TYPE'
         AGO   .PCICK
.ADER2   MNOTE 4,'IKJ54118I ADDR OPERAND INCORRECT LENGTH -- IGNORED'
         AGO   .PCICK
.ADER3   MNOTE 8,'IKJ54104I &ADDR INVALID FOR ADDR OPERAND'
         AGO   .PCICK
.PCICK ANOP                                                     SA59163
.*
.**********************************************************************
.*       PCI KEYWORD ANALYSIS
.**********************************************************************
.*
         AIF   ('&PCI' EQ '').DEFPCI                            SA59163
         AIF   (N'&PCI NE 2).BADPCI                             SA59163
&F       SETC  '&PCI(1)'                                        SA59163
.NEXTCK  ANOP                                                   SA59163
         AIF   ('&F' EQ 'N' OR '&F' EQ 'R' OR '&F' EQ 'X').PCI1 SA59163
         AIF   ('&F' EQ 'A').PCI1                               SA59163
.BADPCI  ANOP                           SA59163                 SA59163
         MNOTE 4,'IKJ54119I PCI OPERAND MUST BE (X,Y), WHERE X AND Y ARX
               E A, X, R, OR N.  PCI DEFAULTED.'                SA59163
         AGO   .DEFPCI                                          SA59163
.PCI1    ANOP                                                   SA59163
         AIF   ('&F' EQ '&PCI(2)').PUTPCI                       SA59163
&F       SETC  '&PCI(2)'                                        SA59163
         AGO   .NEXTCK                                          SA59163
.*
.*       DEFAULT PCI SPECIFICATIONS                             SA59163
.*
.DEFPCI  ANOP                                                   SA59163
         AIF   ('&TERM' EQ '226L').PCINN                        SA72293
         AIF   ('&TERM' EQ '327L').PCINN                        SA72293
&PC(&I)  SETC  '(A,A)'                                          SA59163
         AGO   .ALLDONE                                         SA59163
.PCINN   ANOP                                                   SA72293
&PC(&I)  SETC  '(N,N)'        SET PCI FOR LOCAL 3270 AND 2260   SA72293
         AGO   .ALLDONE                                         SA59163
.PUTPCI  ANOP                                                   SA59163
&PC(&I)  SETC  '&PCI'                   USER'S SPECIFICATION    SA59163
.ALLDONE ANOP                                                   SA59163
         MEND
