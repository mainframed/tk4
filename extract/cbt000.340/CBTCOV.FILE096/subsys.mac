SUBSYS   TITLE 'QUEUE COMMAND - SUBSYSTEM VALIDATE AND LOCATE'    BFG13
SUBSYS   QSTART ,                  START SUBSYS CSECT ASSEMBLY    BFG13
******************************************************************BFG16
* BFG CHANGES:                                                    BFG16
*      (1) BFG16 - CLEAR AND SET (IF VALID) COMCHR FROM FOUND     BFG16
*                  JES ON VALIDATE FUNCTION                       BFG16
*      (2) BFG32   CHANGE SVTCHKPT TO SVTCKPT1 (JES2.2.0)         BFG32
******************************************************************BFG16
******************************************************************BFG13
*                                                                 BFG13
*   THIS MODULE HANDLES ALL INTERFACING BETWEEN THE QUEUE COMMAND BFG13
*   AND THE MVS SUBSYSTEM CONTROL BLOCK CHAIN.                    BFG13
*                                                                 BFG13
*   THE BASIC SUBFUNCTIONS ARE:                                   BFG13
*      REG 1     FUNCTION                                         BFG13
*       0        VALIDATE THAT THE NAME IN 'QSUBSYS' IS IN FACT   BFG13
*                A VALID JOB ENTRY SUBSYSTEM AND MATCHES THE      BFG13
*                CURRENT CHECKPOINT RECORD.                       BFG13
*                ON EXIT R15 = 0  VALID JES SUBSYSTEM NAME        BFG13
*                              4  SUBSYSTEM DOES NOT EXIST        BFG13
*                              8  SUBSYSTEM NOT UP (NO SSVT)      BFG13
*                             12  SUBSYSTEM NOT A JES             BFG13
*                             16  Q VER. DOES NOT MATCH JES2 VER. BFG13
*                             20  CKPT VOLSER DOES NOT MATCH      BFG13
*       4        RETURN THE ADDRESS OF THE SSVT ASSOCIATED WITH   BFG13
*                THE NAME IN 'QSUBSYS'                            BFG13
*                ON EXIT R15 = 0  VALID JES SUBSYSTEM NAME        BFG13
*                              4  SUBSYSTEM DOES NOT EXIST        BFG13
*                              8  SUBSYSTEM NOT UP (NO SSVT)      BFG13
*                        R1  =    SSVT ADDRESS IF R15=0           BFG13
*       8        THE COMMAND 'SBSY', ALLOWS RESET OF SUBSYSTEM    BFG13
*                NAME, WILL CAUSE REVALIDATION VIA RECURSION      BFG13
*                                                                 BFG13
******************************************************************BFG13
         L     R10,QVCKPT     LOAD BASE REG                       BFG13
         USING WORK,R13       BASE REG FOR LOCAL WORK AREA        BFG13
******************************************************************BFG13
*                                                                 BFG13
*   BRANCH TO PROPER ROUTINE                                      BFG13
*                                                                 BFG13
******************************************************************BFG13
         CH    R1,=AL2(CODEND-4-CODSTRT) IS THE FCN SUPPORTED?    BFG13
         BH    TILTCODE            NO. RETURN.                    BFG13
         B     CODSTRT(R1)         BRANCH TO ROUTINE              BFG13
CODSTRT  B     SBVALID             0   VALIDATE                   BFG13
         B     SBFIND              4   FIND SSVT                  BFG13
         B     SBSBSY              8   'SBSY' COMMAND             BFG13
CODEND   EQU   *                   END OF TABLE                   BFG13
*ILTCODE QTILT '*** LOGIC ERROR IN SUBSYS MODULE ***'             EGG02
TILTCODE QTILT 'QSUBSYS - Logic Error Occured'                    EGG02
         SPACE 2                                                  BFG13
******************************************************************BFG13
*                                                                 BFG13
*   VALIDATE THE SELECTED SUBSYSTEM CODE                          BFG13
*                                                                 BFG13
******************************************************************BFG13
         SPACE 1                                                  BFG13
SBVALID  DS    0H                  LOCATE THE SUBSYSTEM           BFG13
         MVI   QCOMCHR,C' '        KILL COMCHR                    BFG16
         BAL   R6,SSVTFIND         TRY FINDING AND SET REGS       BFG13
         LTR   R2,R2               ANY FOUND??                    BFG13
         BNZ   STOP                NO. - THEN ERROR               BFG13
         LA    R2,12               SET POSSIBLE ERROR (NOT A JES) BFG13
         USING SSVT,R4             SET ADDRESSABILITY             BFG13
         USING HCCT,R7             TELL THE ASSEMBLER             EGG01
         ICM   R3,1,SSVTFCOD-1+SSOBRQST SEE IF TRUE JES           BFG13
         BZ    STOP                NO REQUEST JOB FCN THEN NOT JESBFG13
         ICM   R3,1,SSVTFCOD-1+SSOBRTRN                           BFG13
         BZ    STOP                NO RETURN JOB FCN THEN NOT JES BFG13
         LA    R2,16               SET POSSIBLE ERROR (NOT JES2)  BFG13
         LA    R1,0(,R7)           GET hcct ADDRESS               BFG13
         S     R1,=F'8'                                           BFG13
         CLC   =CL4'HCCT',0(R1)                                   EGG01
         BNE   STOP                                               BFG13
*        LA    R0,SVTLEN+8         GET LENGTH                     EGG01
*        LA    R0,CCTLEN+8         GET LENGTH                     EGG01
*        CLM   R0,7,5(R1)          DOES LENGTH MATCH              EGG01
*        BNE   STOP                NO - ERROR NOT JES2 SSVT       EGG01
*        LA    R2,20               SET POSSIBLE ERROR (WRONG CKPT)BFG13
*        CLC   SVTCKPT1(6),QCKPTVL SEE IF SAME CKPT VOLSER        EGG01
*        L     R6,CCTHCT           GET THE HCT ADDRESS            EGG01
*        USING HCT,R6              ADDRESS IT                     EGG01
*        L     R6,$HFAM            GET HFAM  ADDRESS              EGG01
*        USING HFAM,R6             ADDRESS IT                     EGG01
*        LA    R6,HFAMCKP1         GET ADDRESS OF                 EGG01
*        USING HFE,R6              ADDRESS IT                     EGG01
*        CLC   HFEVOL(6),QCKPTVL SEE IF SAME CKPT VOLSER          EGG01
*        BNE   STOP                NO - SO ERROR                  BFG13
*        L     R1,QVCKPT           GET CHECKPOINT WORK AREA       BFG13
*        USING QCKPT,R1            SETUP ADDRESSABILITY           BFG13
*        CLC   SVTTONOD,QCNODE     SEE IF SAME NODE ID            EGG01
*        CLC   CCTTONOD,QCNODE     SEE IF SAME NODE ID            EGG01
*        DROP  R1                                                 BFG13
*        BNE   STOP                NO - SO ERROR                  BFG13
*        MVC   QCOMCHR,SVTCOMCH    GET COMCHR FROM SUBSYSTEM      EGG01
*        MVC   QCOMCHR,CCTCOMCH    GET COMCHR FROM SUBSYSTEM      EGG01
         SR    R2,R2               SAY SUBSYS IS VALID            BFG13
         B     STOP                ALL DONE                       BFG13
         DROP  R4                                                 BFG13
         SPACE 2                                                  BFG13
******************************************************************BFG13
*                                                                 BFG13
*   FIND THE DESIRED SUBSYSTEM                                    BFG13
*                                                                 BFG13
******************************************************************BFG13
         SPACE 1                                                  BFG13
SBFIND   DS    0H                  LOCATE THE SUBSYSTEM           BFG13
         BAL   R6,SSVTFIND         TRY FINDING AND SET REGS       BFG13
         L     R3,4(R13)           GET PREVIOUS SAVE AREA         BFG13
*        ST    R4,12+4+4+4(R3)     RESET REGISTER 1 ON INPUT      BFG13
         ST    R7,12+4+4+4(R3)     RESET REGISTER 1 ON INPUT      EGG01
         B     STOP                ALL DONE                       BFG13
         SPACE 2                                                  BFG13
******************************************************************BFG13
*                                                                 BFG13
*   RESET THE SUBSYSTEM ID DYNAMICALLY                            BFG13
*                                                                 BFG13
******************************************************************BFG13
         SPACE 1                                                  BFG13
SBSBSY   DS    0H                                                 BFG13
         LH    R1,QLNG1            GET LENGTH OF PARM 1           BFG13
         SH    R1,=H'1'            DECREMENT                      BFG13
         BNM   HAPARM              SKIP CODE IF HAVE PARM         BFG13
         L     R1,PSATOLD-PSA(0)   GET OUR TCB                    BFG13
         L     R1,TCBJSCB-TCB(R1)  GET OUR JSCB                   BFG13
         L     R1,JSCBACT-IEZJSCB(R1) GET ACTIVE JSCB             BFG13
         L     R1,JSCBSSIB-IEZJSCB(R1) GET OUR SSIB               BFG13
         MVC   QPARM1(4),SSIBSSNM-SSIB(R1) DEFAULT TO OWNER       BFG13
         LA    R1,3                SET PROPER LENGTH              BFG13
HAPARM   CH    R1,=H'3'            SEE IF TOO BIG                 BFG13
         BH    TILTBAPA            ERROR TO LENGTH TO BIG         BFG13
         MVC   QSUBSYS,=CL4' '     SET TO BLANK                   BFG13
         EX    R1,MVCSUB           MOVE                           BFG13
         OI    QAFLAGS,QAFXSBSY    INDICATE                       BFG13
         OI    QASCFLGS,QASXSBSY     THAT WE MAY                  BFG13
         OI    QAHELP,QAHXSBSY         REALLY KNOW PROPER SSVT    BFG13
         LA    R1,0                SET CODE                       BFG13
         L     R15,=V(SUBSYS)      CALL TO VALIDATE               BFG13
         BALR  R14,R15                                            BFG13
         LTR   R15,R15             ANY ERROR                      BFG13
         BZ    TILTOK              NO - TELL THAT ALL IS RESET    BFG13
         NI    QAFLAGS,255-QAFXSBSY INDICATE THAT                 BFG13
         NI    QASCFLGS,255-QASXSBSY   WE REALLY DONT             BFG13
         NI    QAHELP,255-QAHXSBSY     KNOW THE PROPER SSVT       BFG13
         B     TILTTBL-4(R15)                                     BFG13
TILTTBL  B     TILTBDSC        4  SUBSYSTEM DOES NOT EXIST        BFG13
         B     TILTNOUP        8  SUBSYSTEM NOT UP (NO SSVT)      BFG13
         B     TILTNJES       12  SUBSYSTEM NOT A JES             BFG13
         B     TILTWRVR       16  Q VER. DOES NOT MATCH JES2 VER. BFG13
         B     TILTWVOL       20  CKPT VOLSER DOES NOT MATCH      BFG13
*ILTOK   QTILT 'QUEUE NOW FUNCTIONING WITH REQUESTED SUBSYSTEM'   EGG02
*ILTBAPA QTILT 'SUBSYSTEM NAME MUST BE 4 OR LESS CHARACTERS'      EGG02
*ILTBDSC QTILT 'SUBSYSTEM DOES NOT EXIST'                         EGG02
*ILTNOUP QTILT 'SUBSYSTEM IS NOT UP'                              EGG02
*ILTNJES QTILT 'REQUESTED SUBSYSTEM IS NOT A JES2'                EGG02
*ILTWRVR QTILT 'JES2 VERSION IS DIFFERENT FROM QUEUE VERSION'     EGG02
*ILTWVOL QTILT 'JES2 SUBSYSTEM NOT USING SAME CHECKPOINT VOLUME'  EGG02
TILTOK   QTILT 'QSUBSYS - Now functioning with new Subsystem'     EGG02
TILTBAPA QTILT 'QSUBSYS - Subsystem cannot exceed 4 positions'    EGG02
TILTBDSC QTILT 'QSUBSYS - Requested Subsystem does not exist'     EGG02
TILTNOUP QTILT 'QSUBSYS - Requested Subsystem is not up'          EGG02
TILTNJES QTILT 'QSUBSYS - Requested Subsystem is not JES2'        EGG02
TILTWRVR QTILT 'QSUBSYS - JES2 and QUEUE versions are different'  EGG02
TILTWVOL QTILT 'QSUBSYS - Subsystem is using different CKPT VOL'  EGG02
MVCSUB   MVC   QSUBSYS(*-*),QPARM1 ** EXECUTED ONLY **            BFG13
         SPACE 2                                                  BFG13
******************************************************************BFG13
*                                                                 BFG13
*   GO HOME                                                       BFG13
*                                                                 BFG13
******************************************************************BFG13
         SPACE 1                                                  BFG13
STOP     LR    R15,R2              SET THE RETURN CODE            BFG13
         QSTOP RC=(15)                                            BFG13
         SPACE 2                                                  BFG13
******************************************************************BFG13
*                                                                 BFG13
*   INTERNAL SUBROUTINE TO LOOK UP SUBSYSTEM NAME                 BFG13
*                                                                 BFG13
*   INPUT : NAME IN 'QSUBSYS'                                     BFG13
*   OUTPUT: R2  =  0 IF FOUND AND R4 ==> SSVT                     BFG13
*                  4 IF SSCT NOT FOUND, R4 = 0                    BFG13
*                  8 IF SSCT EXISTS BUT SSVT DOES NOT, R4 = 0     BFG13
*                                                                 BFG13
******************************************************************BFG13
         SPACE 1                                                  BFG13
SSVTFIND DS    0H                                                 BFG13
         L     R5,CVTPTR           GET CVT                        BFG13
         USING CVTDSECT,R5                                        BFG13
         L     R5,CVTJESCT         POIN AT THE JESCT              BFG13
         USING JESCT,R5                                           BFG13
         L     R5,JESSSCT          POINT AT FIRST SSCT            BFG13
         USING SSCT,R5                                            BFG13
         LA    R2,4                SET ERROR RETURN CODE          BFG13
         SR    R4,R4               CLEAR SSVT POINTER             BFG13
SSCTLP   DS    0H                                                 BFG13
         LA    R5,0(,R5)           CLEAR ANY GARBAGE              BFG13
         LTR   R5,R5               ANY LEFT?                      BFG13
         BZR   R6                  NO. - SIGNAL NOT FOUND         BFG13
         CLC   QSUBSYS,SSCTSNAM    DOES NAME MATCH??              BFG13
         BE    FND                 YES. SO CHECK SSVT             BFG13
         L     R5,SSCTSCTA         GET ADDRESS OF NEXT            BFG13
         B     SSCTLP              AND LOOP TILL END              BFG13
FND      LA    R2,8                SET ERROR THAT SUBSYSTEM NO UP BFG13
*        L     R4,SSCTSSVT         GET POINTER TO SSVT            EGG01
         L     R7,SSCTSUS2         GET POINTER TO HCCT            EGG01
         USING HCCT,R7             TELL THE ASSEMBLER             EGG01
         L     R4,CCTSSVT          GET POINTER TO SSVT            EGG01
         LTR   R4,R4               DOES SSVT EXIST                BFG13
         BZR   R6                  NO. THEN EXIT WITH ERROR       BFG13
         SR    R2,R2               CLEAR ERROR CODE               BFG13
         BR    R6                  EXIT WITH GOOD RESULT          BFG13
         DROP  R5                                                 BFG13
         SPACE 2                                                  BFG13
******************************************************************BFG13
*                                                                 BFG13
*   MISCELLANEOUS GARBAGE                                         BFG13
*                                                                 BFG13
******************************************************************BFG13
         LTORG ,                                                  BFG13
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
SUBSYS   CSECT ,                                                  UF023
         COPY $HASPGBL
*        QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         QCOMMON ,                                                BFG13
         IHAPSA ,                                                 BFG13
         CVT   DSECT=YES,PREFIX=NO                                BFG13
CVTDSECT EQU   CVT                                                BFG13
         IKJTCB ,                                                 BFG13
         IEFJSSIB ,                                               BFG13
         IEZJSCB ,                                                BFG13
         IEFJESCT TYPE=DSECT                                      BFG13
         IEFJSCVT ,                                               BFG13
         IEFJSSVT ,                                               EGG01
         IEFJSSOB (RR),CONTIG=YES                                 BFG13
         $HASPEQU ,                                               BFG13
&MACSPLV SETC '3.0'                ADD FOR 3 1 1                  EGG01
         $XECB ,                   **NEEDED FOR $SVT              BFG13
         $SCAT ,                   **NEEDED FOR $SVT              BFG13
*        $SVT  ,                   REMOVE FOR 3.1.1               EGG01
         $HCCT ,                   ADD FOR 3 1 1                  EGG01
         $PCE  ,                   ADD FOR 3 1 1                  EGG01
         $MIT  ,                   ADD FOR 3 1 1                  EGG01
         $HCT  ,                   ADD FOR 3 1 1                  EGG01
         $HFAM ,                   ADD FOR 3 1 1                  EGG01
         $HFAME ,                                                 SP220
*        QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
         SPACE 1                                                  UF001
WORK     DSECT
         DS    CL72
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
