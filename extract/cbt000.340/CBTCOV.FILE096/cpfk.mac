CPFK     TITLE 'QUEUE COMMAND - LIST / CHANGE PFK DEFINITIONS'
CPFK     QSTART  ,                 START CPFK  CSECT ASSEMBLY
         USING QDISPLAY,R9
         L     R9,QVDSPL           LOAD BASE REG FOR DISPLAY AREA
         USING WORK,R13            BASE FOR SAVE / WORK AREA
         LH    R1,QLNG1            WAS A PARM PASSED?
         LTR   R1,R1
         BZ    PFKDISP             NO, GO DISPLAY CURRENT DEFNS
         SPACE 1
*        CHANGE THE CURRENT SETTING FOR THE PFK DEFINED BY QPARM1
         SPACE 1
         LH    R2,QLNG1            LENGTH OF DATASET ID FIELD
         SH    R2,=H'1'            IS THE DATASET ID FIELD ZERO LENGTH?
         BM    TILT                YES. QUIT.
         MVC   QFZONES,QFZONE      INITIALIZE NUMERIC TEST
         EX    R2,MVZ              MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE      IS THE FIELD NUMERIC?
         BNE   TILT                NO. QUIT.
         EX    R2,PACK             PACK THE FIELD
         CVB   R2,DWORD            CONVERT TO BINARY
         LA    R15,QDREPLY         POINT TO REPLY AREA
         AH    R15,QOFF2           ADD OFFSET PAST PFK NUMBER
         BCTR  R2,0                RELATIVE PFK NUMBER
         SLL   R2,3                TIME LENGTH OF AN ENTRY
         LA    R1,QDPFKTAB(R2)     POINT TO TABLE ENTRY
         MVC   0(8,R1),0(R15)      SET NEW DEFINITION
         SPACE 1
*        DISPLAY THE CURRENT PFK DEFINITIONS
PFKDISP  MVC   QDHLINE,HEADING     SET HEADING
         LA    R2,QDPFKTAB         POINT TO TABLE
         LA    R3,1                FIRST ENTRY
         LA    R5,13               THIRTEENTH
         LA    R4,24               NUMBER OF ENTRIES
DISPLOOP MVC   QDMSG,QBLANK        BLANK THE OUTPUT LINE
         MVC   QDMSG+1(4),=X'40202120' SET MASK
         CVD   R3,DWORD            PFK NUMBER TO DECIMAL
         ED    QDMSG+1(4),DWORD+6  SET IN MESSAGE
         LR    R15,R3              GET PFK NUMBER
         BCTR  R15,0               DROP BY ONE
         SLL   R15,3               TIMES 8
         AR    R15,R2              POINT TO PFK TEXT
         MVC   QDMSG+8(8),0(R15)   COPY TEXT
         LA    R3,1(,R3)           BUMP TO NEXT VALUE
         BCT   R4,DISPLOO1         LOOP FOR ALL PFK'S
         B     PUTHELP
DISPLOO1 MVC   QDMSG+19(4),=X'40202120' SET MASK
         CVD   R5,DWORD            PFK NUMBER TO DECIMAL
         ED    QDMSG+19(4),DWORD+6  SET IN MESSAGE
         LR    R15,R5              GET PFK NUMBER
         BCTR  R15,0               DROP BY ONE
         SLL   R15,3               TIMES 8
         AR    R15,R2              POINT TO PFK TEXT
         MVC   QDMSG+28(8),0(R15)   COPY TEXT
         LA    R0,QDMSG            POINT TO MESSAGE
         ST    R0,QDMSGA           SAVE MSG POINTER
         MVC   QDMLNG,=H'80'       SET MESSAGE LENGTH
         L     R15,=V(DISPLAY)   WRITE THE
         BALR  R14,R15               MESSAGE
         LA    R5,1(,R5)           BUMP TO NEXT VALUE
         BCT   R4,DISPLOOP         LOOP FOR ALL PFK'S
         SPACE 1
PUTHELP  MVC   QDMLNG,=H'80'   SET MSG LENGTH
         LA    R2,PFKHMN       NUMBER OF LINES
         LA    R3,PFKHM        START OF TEXT AREA
PUTHELP1 ST    R3,QDMSGA       SET MESSAGE ADDRESS
         L     R15,=V(DISPLAY) ADDRESS OF DISPLAY ROUTINE
         BALR  R14,R15         LINK TO ROUTINE
         LA    R3,80(,R3)      POINT TO NEXT LINE
         BCT   R2,PUTHELP1     LOOP TILL DONE
         SPACE 1
         QSTOP
TILT     QTILT  'QCPFK - PFK Number is invalid'
MVZ      MVZ   QFZONES(*-*),QPARM1 CHECK FOR NUMERIC
PACK     PACK  DWORD,QPARM1(*-*)   CONVERT TO BINARY
HEADING  DC    CL80'    PFK Current Definitions'
PFKHM    EQU   *
 DC CL84' '
 DC CL80'To specify operands (For PF5, and optionally for PF6, 9, 12),'
 DC CL80'or to temporarily override the defaults (for PF7, 8, 10, 11),'
 DC CL80'key in the value and press the appropriate PF key.'
 DC CL80' '
 DC CL80'To redefine a PFK, enter "PFK # TEXT"'
 DC CL80'where # is the PFK number and TEXT is the desired string'
 DC CL4' '
PFKHMN   EQU   (*-PFKHM)/80    NUMBER OF MESSAGES
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION
         QPRBGEN  BEGIN            SET PRINT FOR CNTL BLOCK GEN
         QCOMMON
         $HASPEQU
         QPRBGEN  DONE             RESTORE NORMAL PRINT STATUS
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION
WORK     DSECT
         DS    18F
DWORD    DS    D                   SCRATCH DOUBLE WORD
         END
