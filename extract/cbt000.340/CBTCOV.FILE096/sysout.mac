SYSOUT   TITLE 'QUEUE COMMAND - MANIPULATE SYSOUT'                ONL01
SYSOUT   QSTART ,                  START SYSOUT CSECT ASSEMBLY    ONL01
******************************************************************BFG09
* BFG CHANGES:                                                    BFG09
*      (1) BFG09 - USE AUTH ROUTINE TO GET INTO AND OUT OF KEY 0  BFG09
*                  AND SUPERVISOR STATE                           BFG09
*      (2) BFG13 - CREATE SSIB FOR REQUESTED SUBSYSTEM TO MAKE    BFG13
*                  REQUEST IS AGAINST CORRECT ONE.                BFG13
*                  GIVE DESCRIPTIVE ERROR IF SUBSYSTEM THAT       BFG13
*                  STARTED JOB (TSO USER) IS NOT THE ONE THAT     BFG13
*                  THE REQUEST WAS SENT TO (RC=4 FROM SSI)        BFG13
*      (3) BFG20 - CHANGE MESSAGE TO INDICATE THAT A 'Y'          BFG20
*                  IS THE VALID ACKNOWLEDGEMENT                   BFG20
******************************************************************BFG09
***********************************************************************
* RNB CHANGES:                                                        *
*      (1) RNB00 - MOVED THINGS AROUND FOR FOX ASSEMBLER              *
*      (2) RNB09 - FOR A REQ COMMAND, IF NO NEWCLASS GIVEN USE CLASS C*
*                  KEYED TO QRNB=1.                                   *
***********************************************************************
         GBLB  &QRNB                                              RNB09
         USING QCKPT,R10
         L     R10,QVCKPT
         USING QDISPLAY,R9
         L     R9,QVDSPL
         USING WORK,R13
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT AND IOT                       *
*                                                                     *
***********************************************************************
FJOB     L     R15,=V(FINDJOB)    ADDR OF MODULE TO FIND JOB
         BALR  R14,R15            GO TO IT
         L     R4,QCJCTA          ADDR OF JCT                     ONL02
         USING JCT,R4             NOTE JCT ADDRESSABILITY         ONL16
         SPACE 2                                                  ONL02
***********************************************************************
*                                                                     *
*   VALIDATE THE FUNCTION CODE                                        *
*                                                                     *
***********************************************************************
         LH    R1,QCODEH          GET FUNCTION CODE
         CH    R1,=H'8'           ?/VALID FUNCTION CODE
         BH    STOP               NO. EXIT
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AUTHORIZATION CHECK FOR JOB                           ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         QAUTHCK QASYSOUT          PERFORM JOB AUTHORIZATION      ONL02
         SPACE 2                                                  ONL02
***********************************************************************
*                                                                     *
*   BRANCH TO THE APPROPIATE PROCESSOR                                *
*                                                                     *
***********************************************************************
         LA    R7,SSOBHDR         ADDR FOR SUBSYSTEM OPTION BLOCK
         USING SSOB,R7
         B     *+4(R1)
         B     DELETE             0   DELETE REQUEST
         B     REQUEUE            4   REQUEUE REQUEST
         B     CANCEL             8   CANCEL REQUEST
***********************************************************************
*                                                                     *
*   PROCESS CANCEL REQUEST                                            *
*                                                                     *
***********************************************************************
CANCEL   LA    R5,SSCSBGN         A(CANCEL PARAMETER LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB.
         MVC   SSOBFUNC,=AL2(SSOBCANC)  SET THE FUNCTION
         XC    SSCSBGN(SSCSIZE),SSCSBGN CLEAR PARM LIST
         MVC   SSCSLEN,=AL2(SSCSIZE) SET LENGTH OF CANCEL PARM LIST
         CLI   QPARM2,C'P'        ?/PURGE THE OUTPUT
         BNE   CAN000             NO. DON'T SET THE FLAG
         OI    SSCSFLGS,SSCSCOUT  YES
CAN000   MVC   SSCSJOBN,JCTJNAME  JOBNAME
         MVC   SSCSJOBI,JCTJOBID  JES2 JOB ID
         MVC   SSCSDIMP,=H'16'
         LA    R5,SSCSJOBI        A(JES2 JOB ID)
*        BAL   R6,FIXJOBID        ENSURE JOBID HAS NO IMBEDDED BLEGG01
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN
         L     R1,SSOBRETN        GET RC FOR CANCEL FUNCTION
         B     *+4(R1)
         B     FUNCTOK            0 -  CANCEL COMPLETED
         B     TILTNOJB           4 -  JOBNAME NOT FOUND
         B     TILTBADI           8 -  INVALID JOBNAME/JOB ID COMB.
         B     TILTNCAN           12 - JOB NOT CANCELLED - DUP JOBN
         B     TILTMALL           16 - STATUS ARRAY TOO SMALL
         B     TILTOUTP           20 - JOB NOT CANCELLED - ON OUTPUT Q
         B     TILTYNTX           24 - JOB ID WITH INVALID SYNTAX
         B     TILTICAN           28 - INVALID CANCEL REQUEST
***********************************************************************
*                                                                     *
*   PROCESS REQUEUE REQUEST                                           *
*                                                                     *
***********************************************************************
REQUEUE  LA    R5,SSSOBGN         A(REQUEUE SYSOUT PARM LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB
         CLI   QPARM2,C' '        WAS NEW CLASS GIVEN?            RNB09
         BNE   NOCLASS            /YES - USE IT                   RNB09
         MVI   QPARM2,C'A'        /NO  - DEFAULT TO CLASS C       RNB09
NOCLASS  MVC   SSOBFUNC,=AL2(SSOBSOUT) INDICATE SYSOUT IS THE FUNCTION
         XC    SSSOBGN(SSSOSIZE),SSSOBGN CLEAR PARM LIST
         MVC   SSSOLEN,=AL2(SSSOSIZE) SET LENGTH OF SYSOUT PARM LIST
         OI    SSSOUFLG,SSSOSETC  USE SSSOCLAS AS DISP
         OI    SSSOUFLG,SSSORLSE  RELEASE ALL SELECTED DATA SETS
         OI    SSSOFLG1,SSSOHLD   SELECTION INCLUDES HELD SYSOUT DS
         OI    SSSOFLG1,SSSOSJBN  JOB NAME IS PRESENT
         OI    SSSOFLG1,SSSOSJBI  JOB ID IS PRESENT
         OI    SSSOFLG2,SSSOCTRL  PROCESSING COMPLETED
         MVC   SSSOJOBN,JCTJNAME  JOBNAME
         MVC   SSSOJOBI,JCTJOBID  JOB ID
         MVC   SSSOCLAS,QPARM2    NEWCLASS
         LA    R5,SSSOJOBI        A(JES JOB ID)
*        BAL   R6,FIXJOBID        ENSURE JOBID HAS NO EMBEDDED BEGG01
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN
CHKSORC  L     R1,SSOBRETN        GET RETURN CODE FOR SYSOUT FUNCTION
         B     *+4(R1)
         B     FUNCTOK            0 -  SYSOUT COMPLETED
         B     TILTEODS           4 -  NO MORE DS TO SELECT
         B     TILTNJOB           8 -  JOB NOT FOUND
         B     TILTINVA           12 - INVALID SEARCH ARGUMENTS
         B     TILTUNAV           16 - UNABLE TO PROCESS NOW
         B     TILTDUPJ           20 - DUPLICATE JOB NAMES
         B     TILTINVJ           24 - INVALID JOBN/JOBID COMBO
         B     TILTIDST           28 - INVALID DEST SPECIFIED
***********************************************************************
*                                                                     *
*   PROCESS DELETE  REQUEST                                           *
*                                                                     *
***********************************************************************
DELETE   LA    R5,SSSOBGN         A(DELETE SYSOUT PARM LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB
         MVC   SSOBFUNC,=AL2(SSOBSOUT) INDICATE SYSOUT IS THE FUNCTION
         XC    SSSOBGN(SSSOSIZE),SSSOBGN CLEAR PARM LIST
         MVC   SSSOLEN,=AL2(SSSOSIZE) SET LENGTH OF SYSOUT PARM LIST
         OI    SSSOUFLG,SSSODELC  INDICATE DELETE REQUEST
         OI    SSSOFLG1,SSSOHLD   SELECTION INCLUDES HELD DS
         OI    SSSOFLG1,SSSOSJBN  JOB NAME PRESENT
         OI    SSSOFLG1,SSSOSJBI  JES2 JOB ID PRESENT
         OI    SSSOFLG2,SSSOCTRL  PROCESSING COMPLETED
         MVC   SSSOJOBN,JCTJNAME  JOBNAME
         MVC   SSSOJOBI,JCTJOBID  JES2 JOBID
         LA    R5,SSSOJOBI        A(JES2 JOBID)
*        BAL   R6,FIXJOBID        ENSURE JOBID HAS NO EMBEDDED BLEGG01
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN.
         B     CHKSORC            GO CHECK RC
***********************************************************************
*                                                                     *
*   INITIALIZE THE SUBSYSTEM OPTION BLOCK (SSOB)                      *
*                                                                     *
*        R5 - ADDRESS OF FUNCTION PARM LIST                           *
*        R6 - RETURN ADDRESS                                          *
*        R7 - A(SSOB)                                                 *
*                                                                     *
***********************************************************************
INITSSOB XC    SSOBEGIN(SSOBHSIZ),SSOBEGIN CLEAR THE SSOB
         LA    R1,QSSIB            POINT AT SSIB                  BFG13
         USING SSIB,R1             SETUP ADDRESSABILITY           BFG13
         XC    SSIB(SSIBSIZE),SSIB                                BFG13
         MVC   SSIBID,=C'SSIB'     SET EYEBALL                    BFG13
         MVC   SSIBLEN,=AL2(SSIBSIZE) SET SIZE                    BFG13
         MVC   SSIBSSNM,QSUBSYS    SET SUBSYS NAME                BFG13
         ST    R1,SSOBSSIB         POINT AT SSIB                  BFG13
         DROP  R1                                                 BFG13
         MVC   SSOBID,=C'SSOB'
         MVC   SSOBLEN,=AL2(SSOBHSIZ) LENGTH OF SSOB HEADER
         ST    R5,SSOBINDV        FUNCTION DEPENDENT AREA POINTER
         ST    R7,SSOBPTR         SAVE ADDR OF SSOB
         OI    SSOBPTR,X'80'      REQUIRED FOR IEFSSREQ INTERFACE
         BR    R6
***********************************************************************
*                                                                     *
*   REMOVE EMBEDDED BLANKS IN JES2 JOB ID  NOT NECESSARY ON 311       *
*                                                                     *
***********************************************************************
*FIXJOBID LA    R8,5               MAX LENGTH OF SCAN             EGG01
*         LA    R5,3(R5)           START LOCATION FOR SCAN
*FIX000   CLI   0(R5),C' '         ?/EMBEDDED BLANK
*         BNE   FIX010             NO. CONTINUE WITH THE SCAN
*         MVI   0(R5),C'0'         YES. REPLACE WITH 0
*FIX010   LA    R5,1(R5)           POINT TO NEXT BYTE
*         BCT   R8,FIX000          GO DO IT AGAIN
*         BR    R6                 ALL OVER. RETURN TO CALLER
******************************************************************
*
*   TELL THE USER WHAT HE IS ABOUT TO DO
*
******************************************************************
VERREQ   MVC   VCLEAR,WARNING     MOVE IN THE WARNING MSG
         MVC   VJOBN(8),JCTJNAME
         MVC   VJOBID(8),JCTJOBID                                 EGG05
         LR    R2,R6              SAVE RETURN ADDR                EGG05
         LA    R5,VJOBID          A(JES JOB ID)                   EGG05
*        BAL   R6,FIXJOBID        INSURE NO IMBEDDED BLANKS       EGG05
         LR    R6,R2              RESTORE RETURN ADDR             EGG05
         CLI   QSUBNAME,C'P'      ?/DELETE COMMAND                EGG05
         BNE   VER000                                             EGG05
         MVC   VCMD,=C'PURGE   '                                  EGG05
         B     VER020                                             EGG05
VER000   CLI   QSUBNAME,C'C'      ?/CANCEL COMMAND                EGG05
         BNE   VER010                                             EGG05
         MVC   VCMD,=C'CANCEL  '                                  EGG05
         CLI   QPARM2,C'P'        PURGE SPECIFIED                 EGG05
         BNE   VER020                                             EGG05
         MVC   VACTION(5),QPARM2                                  EGG05
         B     VER020                                             EGG05
VER010   MVC   VCLASS(1),QPARM2                                   EGG05
         B     VER030                                             EGG05
VER020   MVC   QDMLNG,=H'0'       TELL DISPLAY TO PRINT IT NOW    EGG05
         L     R15,=V(DISPLAY)    A(MODULE TO DISPLAY THE MESSAGE)EGG05
         BALR  R14,R15            GO DISPLAY THE WARNING          EGG05
VER030   BR    R6                 RETURN TO THE CALLER            EGG05
***********************************************************************
*                                                                     *
*   INTERFACE TO THE SUBSYSTEM                                        *
*                                                                     *
***********************************************************************
CALLSSI  QAUTHCK QAKEY0           TRY TO GET INTO KEY 0 SUP STATE BFG09
         L     R2,16              A(CVT)                          BFG09
         L     R2,296(R2)         A(JESCT)
         LA    R1,SSOBPTR         ADDR OF PTR TO SSOB
         L     R15,20(R2)         A(JESSSREQ)
         BALR  R14,R15
         LR    R2,R15             SAVE RETURN CODE
         QAUTHCK QAKEYP           RETURN TO NORMAL STATE          BFG09
         B     *+4(R2)
         BR    R6                 0 -  SUCCESSFUL INSTRUCTION
         BR    R6                      DUMMY INSTRUCTION
         B     TILTNSUP           4 -  SS DOESN'T SUPPORT THIS FUNCTION
         B     TILTNTUP           8 -  SS EXIST, BUT IS NOT UP
         B     TILTNOSS           12 - SS DOES NOT EXIST
         B     TILTDIST           16 - FUNCTION NOT SUPPORTED
         B     TILTLERR           20 - LOGICAL ERROR
***********************************************************************
*                                                                     *
*   RETURN TO CALLER WITH AN AUTOMATIC STATUS COMMAND                 *
*                                                                     *
***********************************************************************
STOP     QSTOP                                                    EGG05
FUNCTOK  MVC   QSUBNAME,=C'ST      '                              EGG05
         MVC   QPARM1,=C'        '                                EGG05
         MVC   QPARM1(3),VJOBN                                    EGG05
         MVC   QPARM2,=C'        '                                EGG05
         MVC   QPARM3,=C'        '                                EGG05
         MVC   QPARM4,=C'        '                                EGG05
         QTILT 'QSYSOUT - Command successful'                     EGG05
***********************************************************************
*                                                                     *
*   ERROR MESSAGES                                                    *
*                                                                     *
***********************************************************************
*ILTNSUP QTILT ' *** UNSUPPORTED FUNCTION - SUBSYSTEM MAY NOT BE USER O
*              WNER'                                              BFG13
*ILTNTUP QTILT ' *** JES2 IS NOT UP ***'
*ILTNOSS QTILT ' *** QUEUE LOGIC ERROR -- RC =12 FROM SSI ***'
*ILTDIST QTILT ' *** DISASTROUS ERROR DURING PROCESSING ***'
*ILTLERR QTILT ' *** QUEUE LOGIC ERROR -- RC =20 FROM SSI ***'
*ILTNOJB EQU   *
*ILTNJOB QTILT ' *** JOBNAME NOT FOUND ***'
*ILTBADI EQU   *
*ILTINVJ QTILT ' *** INVALID JOBNAME/JOB ID COMBINATION ***'
*ILTNCAN EQU   *
*ILTDUPJ QTILT ' *** DUPLICATE JOBNAME AND NO JOBID GIVEN ***'
*ILTEODS QTILT ' *** JOB HAS NO HELD DATA SETS ***'
*ILTICAN QTILT ' *** CAN''T CANCEL YOUR TSO SESSION OR A STARTED TASK *
*              **'
*ILTOUTP QTILT ' *** JOB NOT CANCELLED - JOB ON OUTPUT QUEUE ***'
*ILTMALL EQU   *
*ILTYNTX EQU   *
*ILTINVA EQU   *
*ILTUNAV EQU   *
*ILTIDST QTILT ' *** QUEUE LOGIC ERROR ***'
TILTNSUP QTILT 'QSYSOUT - Subsystem function not supported'       EGG02
TILTNTUP QTILT 'QSYSOUT - JES2 is not Operational'                EGG02
TILTNOSS QTILT 'QSYSOUT - Logic Error -> RC 12 From SSI <-'       EGG02
TILTDIST QTILT 'QSYSOUT - Disaster occured during processing'     EGG02
TILTLERR QTILT 'QSYSOUT - Logic Error -> RC 20 From SSI <-'       EGG02
TILTNOJB EQU   *
TILTNJOB QTILT 'QSYSOUT - Requested Jobname not found'            EGG02
TILTBADI EQU   *
TILTINVJ QTILT 'QSYSOUT - Invalid Jobname/Job ID combination'     EGG02
TILTNCAN EQU   *
TILTDUPJ QTILT 'QSYSOUT - Duplicate Jobname and no Job ID given'  EGG02
TILTEODS QTILT 'QSYSOUT - Requested Job has NO Held Datasets'     EGG02
TILTICAN QTILT 'QSYSOUT - Cancel Requested for TSO/STC -ignored'  EGG02
TILTOUTP QTILT 'QSYSOUT - Job not cancelled - on output queue'    EGG02
TILTMALL EQU   *
TILTYNTX EQU   *
TILTINVA EQU   *
TILTUNAV EQU   *
TILTIDST QTILT 'QSYSOUT - Logic Error Occured'                    EGG02
***********************************************************************
*                                                                     *
*   MISCELLANEOUS NUTS, BOLTS, ETC.                                   *
*                                                                     *
***********************************************************************
WARNING  DS    0CL80
         DC    CL21' --- '
         DC    CL1'('
         DC    CL8' '
         DC    CL1')'
         DC    CL12' '
         DC    CL37'  Enter "Y" if OK or respecify.   ---'        EGG02
RQACTION DC    CL11'Newclass( )'                                  EGG02
         LTORG
         DROP  ,                   DROP ALL ADDRESSINGS           NERDC
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
SYSOUT   CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         $HASPEQU
         IFGRPL
        $BUFFER ,                                                 ONL16
         $TAB
         $JCT
         $PDDB                                                    RNB00
         $IOT                                                     RNB00
         IEFJSSIB ,                                               BFG13
         IEFJSSOB (SO,CS),CONTIG=YES
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
         ORG   QDHLINE
VCLEAR   DS    0CL80
         DS    CL5
VCMD     DS    CL8
VJOBN    DS    CL9
VJOBID   DS    CL10
VACTION  DS    CL11
         DS    CL37
VCLASS   EQU   VACTION+9
WORK     DSECT
FILLER   DS    CL512              BIG FILLER
QSSIB    DS    (SSIBSIZE/4)F       SPACE FOR SSIB                 BFG13
SSOBPTR  DS    F
SSOBHDR  DS    CL140
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
