CHIST    TITLE 'SP-AS COMMAND - PRINT JOB HISTORY/SOURCE'
CHIST    QSTART ,                  START CHIST    CSECT ASSEMBLY
         SPACE 2
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     LOAD BASE REG
         USING WORK,R13       BASE REG FOR LOCAL WORK AREA
***********************************************************************
*
*   PROCESS THE HISTORY COMMAND
*
***********************************************************************
         SPACE 1
         L     R15,=V(FINDJOB)  GO FIND JOB/JCT/JQE
         BALR  R14,R15
         L     R9,QCJCTA     GET ADDRESS OF JCT
         USING JCT,R9         ADDRESSABILITY
         SPACE 1
         TM    QFLAG1,QFLG1SDS     IS THIS A SYSTEM DATA SET JOB?
         BO    TILTSYSD            TILT IF YES
         SPACE 2
*
*   PERFORM AUTHORIZATION CHECK FOR JOB
*
         SPACE 1
         QAUTHCK QAJHIST          PERFORM JOB AUTHORIZATION
         SPACE 2
FORMATIT MVC   QDHLINE,HEADER
         MVC   QDMSG,QBLANK   BLANK OUT LINE
         BAL   R7,DISPLAY     GO DO THE DISPLAY
*
LINE3    MVC   QDMSG,HEAD3    MOVE IN LINE MASK
         MVC   JOBNUMB,JCTJOBID     JOB ID
         MVC   JOBNAME,JCTJNAME     JOBNAME
         MVC   JOBSCE,JCTINDEV      INPUT DEVICE
         MVC   BIN#,JCTROOMN        BIN NUMBER
         MVC   JOBCLASS,JCTJCLAS    JOB CLASS FROM JOBCARD
         MVC   MSGCLASS,JCTMCLAS    MESSAGE CLASS
         MVC   NOTIFY,JCTTSUID      WHO TO CONTACT
         BAL   R7,DISPLAY           PUT OUT LINE3
*
LINE4    BAL   R7,DISPBLNK       GO PUT OUT A BLANK LINE
*
LINE5    MVC   QDMSG,HEAD5        MOVE IN HEADING
         MVC   PROCDDN,JCTPROCN   PROCLIB DDNAME
         LA    R15,PRTDEST
         LA    R1,JCTPROUT
         BAL   R7,RMTORLCL       CONVERT PRT= FIELD
         LA    R15,PUNDEST
         LA    R1,JCTPUOUT
         BAL   R7,RMTORLCL       CONVERT PUN= FIELD
         LM    R0,R1,JCTRDRON    GET READER TIME/DATE
         LTR   R1,R1             ANYBODY (DATE) HOME
         BZ    NODATE5
         BAL   R7,TIMECONV       FORMAT IT PRETTY
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD
NODATE5  BAL   R7,DISPLAY        GO SHOW IT OFF
*
LINE6    MVC   QDMSG,HEAD6        MOVE IN HEADING
         MVC   PROGAMER,JCTPNAME  PROGRAMMER NAME
         LM    R0,R1,JCTXEQON    GET XEQ START TIME/DATE
         LTR   R1,R1             ANYBODY (DATE) HOME
         BZ    NODATE6
         BAL   R7,TIMECONV       FORMAT IT PRETTY
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD
NODATE6  BAL   R7,DISPLAY        GO SHOW IT OFF
*
LINE7    MVC   QDMSG,HEAD7        MOVE IN HEADING
         LA    R15,CARDSIN       POINT TO POSITION
         L     R1,JCTCARDS       GET VALUE
         BAL   R7,FITINUM        AND PUT IT AWAY
         LM    R0,R1,JCTXEQOF    GET XEQ STOP TIME/DATE
         LTR   R1,R1             ANYBODY (DATE) HOME
         BZ    NODATE7
         BAL   R7,TIMECONV       FORMAT IT PRETTY
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD
NODATE7  BAL   R7,DISPLAY        GO SHOW IT OFF
*
LINE8    MVC   QDMSG,HEAD8        MOVE IN HEADING
         LA    R15,LINESOUT      POINT TO POSITION
         L     R1,JCTLINES       GET VALUE
         BAL   R7,FITINUM        AND PUT IT AWAY
         LA    R15,CARDSOUT      POINT TO POSITION
         L     R1,JCTPUNCH       GET VALUE
         BAL   R7,FITINUM        AND PUT IT AWAY
         LM    R0,R1,JCTOUTON    GET PRT/PUN START TIME/DATE
         LTR   R1,R1             ANYBODY (DATE) HOME
         BZ    NODATE8
         BAL   R7,TIMECONV       FORMAT IT PRETTY
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD
NODATE8  BAL   R7,DISPLAY        GO SHOW IT OFF
         SPACE 1
RETURN   QSTOP
         SPACE 2
***********************************************************************
*   LOCAL SUBROUTINE TO PUT OUT LINE VIA Q DISPLAY
***********************************************************************
DISPBLNK MVC   QDMSG,QBLANK   CLEAR OUT MESSAGE
*
DISPLAY  MVC   QDMLNG,=H'80'  SET LENGTH
         LA    R0,QDMSG       GET ADDRESS OF MESSAGE
         ST    R0,QDMSGA      AND SAVE ADDRESS
         L     R15,=V(DISPLAY) GET ADDRESS OF MODULE FOR DISPLAY
         BALR  R14,R15        CALL THE MOTHER
         BR    R7             RETURN TO MAINLINE...
         EJECT ,
***********************************************************************
*
* FORMAT DATE TIME  HEADING   ENTER WITH BAL R7,TIMECONV
*   R0=TIME IN 100THS OF SECOND SINCE MIDNIGHT
*   R1=DATE IN 00YYDDDS FORMAT
*
* RETURNS WITH CDAYN(LEN) = DDMONYY (DDD) DAY HH:MM:SS.TH
*                E.G.       07JUL80 (189) MON 13:45:24.56
*                           123456789.123456789.123456789
*
***********************************************************************
TIMECONV MVC   MONMSK(MVCLEN),XMONMSK  INIT DATE/TIME WORK AREA
         ST    R1,DATE                 SAVE DATE FOR LATER
         CVD   R0,DOUBLEWD            GET TIME IN DEC 100TH/SEC
         DP    DOUBLEWD(8),DEC6000(3) /6000 : REM IS SS.TH
         DP    DOUBLEWD(5),DEC60(2)   /60   : REM IS MM
         MVC   EDITWORK(L'HHMASK),HHMASK   SETUP MULT EDIT MASK
         ED    EDITWORK(L'HHMASK),DOUBLEWD EDIT HH,MM,SS
FINTIME  MVC   CHH(2),EDITWORK+4      MOVE IN HOURS
         MVC   CMM(2),EDITWORK+8      MOVE IN MM
         MVC   CSS(5),EDITWORK+12     MOVE IN SS.TH
*
***FORMAT THE DATE INTO THE OUTPUT MSG
*
         MVC   WORKWORD(4),DATE        MOVE DATE TO WORK AREA.
         MVO   WORKWORD+1(3),WORKWORD(2)   MAKE 00YYDDDS INTO 00000YYS.
         UNPK  CYR(2),WORKWORD+2(2)    FORMAT YEAR.
         XC    DOUBLEWD,DOUBLEWD
         MVC   DOUBLEWD+4(4),WORKWORD  CNVT YEAR TO BIN
         CVB   R0,DOUBLEWD
         ST    R0,BINYEAR              AND SAVE
         TM    BINYEAR+3,X'03'         LEAP YEAR?
         BNZ   MON1                    NO.
         MVI   MONMSK+10,29            YES, CORRECT
MON1     XC    DOUBLEWD,DOUBLEWD       CNVT DAYS TO BIN
         MVC   DOUBLEWD+6(2),DATE+2
         CVB   R2,DOUBLEWD
         ST    R2,BINDAYS              SAVE FOR LATER
         XR    R1,R1
         LA    R15,11
MON2     IC    R1,MONMSK(R15)          COMPUTE MON & DAY
         SR    R2,R1
         BNP   MONOVR
         BCT   R15,MON2
         B     *+6
MONOVR   AR    R2,R1                   CORRECT OVERDRAW
         CVD   R2,DOUBLEWD             FORMAT DAY
         UNPK  CDAYN(2),DOUBLEWD+6(2)
         OI    CDAYN+1,X'F0'           FIX UP SIGN
         MH    R15,=H'3'               GET TABLE OFFSET
         LA    R15,MONTAB(R15)         AND POINT AT MONTH
         MVC   CMON(3),0(R15)          MOVE IT TO THE MSG
         EJECT ,
***DAY OF WEEK
         L     R15,BINYEAR
         SH    R15,=H'69'
         BNP   OUTT                    MUST BE GREATER OR BAD
         XR    R14,R14                 CLEAR DAY REG
         D     R14,=F'4'
         MH    R15,=H'5'
         AR    R14,R15
         A     R14,BINDAYS
         SRDA  R14,32
         D     R14,=F'7'               MODULO 7 FOR WEEK
         MH    R14,=H'3'
         LA    R14,DAYTAB(R14)         POINT AT TODAY
         MVC   CDAY(3),0(R14)          INSERT IN MSG
***
MOVEJD   MVC   JDATE-1(7),=X'4020204B202020'
         ED    JDATE-1(7),DATE+1
         MVC   CJDAY(3),JDATE+3         MOVE DAY TO MESSAGE
*
*
DATERTN  BR    R7                      RETURN TO MAINLINE
*
OUTT     MVC   CDAY(3),=CL3'???'
         B     MOVEJD
         SPACE 2
         EJECT
***********************************************************************
*
*       CONSTANTS FOR DATE ROUTINE USAGE
*
***********************************************************************
         SPACE ,
HHMASK   DC    X'40202021202022212020222120204B2020'
DEC60    DC    X'060C'
DEC6000  DC    X'06000C'
DEC50    DC    X'050C'
***********************************************************************
* LEAVE FIELDS TOGETHER.. INITIALIZED BY ONE MVC WITH VALUES
*
XMONMSK  DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)        )
         DC    CL1' '                                          )
         DC    C'XX.XXX'                           L           )
*                                                  E  F        )
         DC    CL2' '                              A  I  T     )
         DC    CL3' '                              V  E  O  F  )
         DC    CL2' ',C' ('                        E  L  G  O  )
         DC    CL3' ',C') '                           D  E  R  )
         DC    CL3' ',C' '                            S  T     )
         DC    CL2' ',C':'                               H  I  )
         DC    CL2' ',C':'                               E  N  )
         DC    CL5'SS.TH'                                R  I  )
*                                                           T  )
MVCLEN   EQU   *-XMONMSK
*
***********************************************************************
DAYTAB   DC    C'TUEWEDTHRFRISATSUNMON'
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'
         EJECT
***********************************************************************
*RMTORLCL SUBROUTINE - DETERMINE REMOTE OR LOCAL DESTINATION
* R1 POINTS TO PRTRT/PUNRT, R15 TO ASSEMBLY POINTER
***********************************************************************
RMTORLCL CLI   1(R1),0        IS IT FOR REMOTE 00=LOCAL
         BNE   RMTRMT         NO LUCK .. HAVE TO WORK FOR IT
*
         MVC   0(L'LOCAL,R15),LOCAL   MOVE IN LOCAL
         LA    R15,L'LOCAL(R15)       BUMP POINTER AND
         BR    R7                       RETURN
*
RMTRMT   MVC   0(L'RMT,R15),RMT       MOVE IN 'RMT'
         CLC   0(2,R1),=X'0000'    BR IF NOT
         BNE   *+10                 SPECIAL LOCAL
         MVC   0(L'LCL,R15),LCL    MOVE IN 'LCL'
         LA    R15,L'RMT(R15)  BUMP POINTER
         SR    R14,R14        LOAD RMT FLAGS
         IC    R14,1(R1)      FROM PRT/PUN
         CVD   R14,DOUBLEWD   RMT NUMBER TO DECIMAL
         B     FITINUM1       FIT THE NUMBER IN RMT MESSAGE
         SPACE 2
***********************************************************************
* FITINUM SUBROUTINE - CONVERT BIN NUMBER TO NICE FORMAT
*
***********************************************************************
FITINUM  CVD   R1,DOUBLEWD    CONVERT TO PACKED DECIMAL
FITINUM1 MVC   EDITWORK(L'NORMAL),NORMAL INIT THE EDIT FORMAT
         LA    R1,EDITWORK+SIGNORM IN CASE OF ZEROES
         EDMK  EDITWORK(L'NORMAL),DOUBLEWD+2 CONVERT TO EBCDIC
         LA    R14,EDITWORK+L'NORMAL-1 A(END OF CONVERTED NUMBER)
         SLR   R14,R1         LENGTH OF THE CONVERTED NUMBER - 1
         EX    R14,MVNUMBER   PUT THE NUMBER IN THE MSG
         LA    R15,1(R14,R15) A(NEXT SPOT IN MSG)
         BR    R7             RETURN TO OUR CALLER
         SPACE 3
MVNUMBER MVC   0(0,R15),0(R1) TO BE EXECUTED
         SPACE 1
TILTSYSD QTILT  'CHIST - Command invalid for System Jobs'
         SPACE 2
NORMAL   DC    X'402020202020202020202120' EDIT MASK
SIGNORM  EQU   11             OFFSET TO LAST DIGIT
RMT      DC    C'RMT'
LCL      DC    C'LCL'
LOCAL    DC    C'Local'
         EJECT ,
         LTORG
         EJECT ,
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION
CHIST    CSECT ,
         PUSH  PRINT
         PRINT NOGEN
         QCOMMON                   MOVE SPCOMMON BEFORE EQUATES
         $HASPEQU
         POP   PRINT
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION
CHIST    CSECT ,
***********************************************************************
*
* LINE MAPPINGS
*
***********************************************************************
*                   0123456789.123456789.123456789.123456789.123456789.
*              123456789.123456789.123456789.
HEADER   DC    CL80'                           Job History Information'
HEAD3    DC    CL80'XXXXXXXX  XXXXXXXX Via=XXXXXXXX Bin=XXXX  Class=X M*
               sglcass=X  Notify=XXXXXXX'
*              123456789.123456789.123456789
HEAD5    DC    CL80'Procddn=XXXXXXXX Prt=       Pun=        RDR Time'
HEAD6    DC    CL80'Programmer=XXXXXXXXXXXXXXXXXXXX         XEQ Start'
HEAD7    DC    CL80'Cards  In=                              XEQ Stop'
HEAD8    DC    CL80'Lines Out=             Cards Out=       OUT Start'
*
         EJECT ,
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION
CHIST    CSECT ,
         DROP  ,
         QPRBGEN  BEGIN            SET PRINT FOR CNTL BLOCK GEN
         IFGRPL
        $BUFFER ,
        $JCT   ,
         QPRBGEN  DONE             RESTORE NORMAL PRINT STATUS
         EJECT  ,
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION
         SPACE 2
*                            OVERLAY FOR HEAD3
JOBNAME  EQU   QDMSG+00,8
JOBNUMB  EQU   QDMSG+10,8
JOBSCE   EQU   QDMSG+23,8
BIN#     EQU   QDMSG+36,4
JOBCLASS EQU   QDMSG+48,1
MSGCLASS EQU   QDMSG+59,1
NOTIFY   EQU   QDMSG+69,7
*
*                            OVERLAY FOR HEAD5
PROCDDN  EQU   QDMSG+08,8
PRTDEST  EQU   QDMSG+21,6
PUNDEST  EQU   QDMSG+32,6
TIMESTMP EQU   QDMSG+50,29     COMMON FOR READER/XEQ/PRT
*
*                            OVERLAY FOR HEAD6
PROGAMER EQU   QDMSG+11,20
*                            OVERLAY FOR HEAD7
CARDSIN  EQU   QDMSG+10,8
*                            OVERLAY FOR HEAD8
LINESOUT EQU   QDMSG+10,8
CARDSOUT EQU   QDMSG+33,8
         SPACE 1
         EJECT ,
***********************************************************************
*
* WORK AREA DSECT
*
***********************************************************************
WORK     DSECT
         DS    CL72
DOUBLEWD DC    D'0'
BINYEAR  DC    F'0'
BINDAYS  DC    F'0'
DATE     DC    F'0'
WORKWORD DC    2F'0'
EDITWORK DC    12H'0'
***********************************************************************
* LEAVE FIELDS TOGETHER.. INITIALIZED BY ONE MVC WITH VALUES
*
MONMSK   DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)        )
         DC    CL1' '                                          )
*        0123456789.123456789.123456789.                       )
*        DDMONYY (DDD) DAY HH:MM:SS.TH                         )
*                                                              )
JDATE    DC    C'XX.XXX'                           L           )
*                                                  E  F        )
CDAYN    DC    CL2' '                              A  I  T     )
CMON     DC    CL3' '                              V  E  O  F  )
CYR      DC    CL2' ',C' ('                        E  L  G  O  )
CJDAY    DC    CL3' ',C') '                           D  E  R  )
CDAY     DC    CL3' ',C' '                            S  T     )
CHH      DC    CL2' ',C':'                               H  I  )
CMM      DC    CL2' ',C':'                               E  N  )
CSS      DC    CL5'SS.TH'                                R  I  )
*                                                           T  )
***********************************************************************
LEN      EQU   *-CDAYN
         END  ,
