LIST     TITLE 'QUEUE COMMAND - PRINT A DATASET FROM SPOOL BY ID' ONL01
LIST     QSTART ,                  START LIST CSECT ASSEMBLY      ONL01
         GBLB  &QJTIP                                             ONL05
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       BASE REG FOR TEMP WORK
         CLC   QSUBNAME(2),=C'PU' IS THIS A PURGE COMMAND
         BE    CONTPGM
RESTART  CLI   QSUBNAME,C'N'  IS THIS A NEXT COMMAND
         BNE   CHKPREV
         MVI   QPARM1,C'*'
         MVI   QPARM2,C'+'
         LA    R2,1           YES, SET VALUE OF 1
         STH   R2,QLNG2       YES, SET VALUE OF 1
         B     CONTPGM
CHKPREV  CLI   QSUBNAME,C'P'  IS THIS A PREV COMMAND
         BNE   CONTPGM
         MVI   QPARM1,C'*'
         MVI   QPARM2,C'-'
         LA    R2,1           YES, SET VALUE OF 1
         STH   R2,QLNG2       YES, SET VALUE OF 1
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
CONTPGM  L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE DATASET ID NUMBER                           *
*                                                                     *
***********************************************************************
         LH    R2,QLNG2       LENGTH OF DATASET ID FIELD
         SH    R2,=H'1'       IS THE DATASET ID FIELD ZERO LENGTH?
         BM    TILT           YES. QUIT.
*        LH    R4,QOLDDSID    GET PREVIOUS DSID VALUE             EGG01
         L     R4,QOLDDSID    GET PREVIOUS DSID VALUE             EGG01
         LA    R3,DSIDPLUS    POINT TO ADD INSTRUCTION            UF040
         CLI   QPARM2,C'+'    RELATIVE DSID NUMBER?               UF040
         BE    DSIDREL        YES, GO PROCESS IT                  UF040
         LA    R3,DSIDSUB     POINT TO SUBTRACT INSTRUCTION       UF040
         CLI   QPARM2,C'-'    RELATIVE DSID NUMBER?               UF020
         BE    DSIDREL        YES, GO PROCESS IT                  UF040
         LA    R3,DSIDABS     POINT TO NOP INSTRUCTION            UF040
         B     DSIDTEST       NO, CONTINUE                        UF040
DSIDREL  CLI   QPARM1,C'*'    WANT THE CURRENT JOB?               UF040
         BNE   TILTREL        NO, RELATIVE REQUEST INVALID        UF040
         MVI   QPARM2,C'0'    REPLACE WITH NUMERIC VALUE          UF040
         LTR   R2,R2          WAS FIELD ONLY THE +/-?             UF040
         BP    DSIDTEST       NO, CONTINUE AS BEFORE              UF040
         LA    R2,1           YES, SET VALUE OF 1                 UF040
         B     DSIDCHK        AND SKIP NUMERIC CHECKS             UF040
DSIDTEST DS    0H                                                 UF040
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK        PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         SPACE 1                                                  UF005
DSIDCHK  DS    0H                                                 UF040
         EX    0,0(R3)        ADJUST VALUE IN R4 BY OFFSET        UF040
         LR    R2,R4          PUT BACK IN R2                      UF040
         LTR   R2,R2          MAKE SURE IS VALID                  EGG01
         BNP   TILTP           AKE SURE IS VALID                  EGG01
**       C     R2,=F'5'       IS THIS DSID 5                      EGG01
*        BNE   STORID         NO CONTINUE                         EGG01
*        LA    R2,1(R2)       ADD 1 TO BYPASS DSID 5              EGG01
STORID   ST    R2,QPDSID      STORE DATASET ID                    EGG01
         ST    R2,QOLDDSID    SET PREVIOUS DSID TO CURRENT        EGG01
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AUTHORIZATION CHECK FOR JOB AND DSID                  ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         QAUTHCK QALIST            PERFORM JOB/DSID AUTHORIZATION ONL02
         SPACE 2                                                  ONL02
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE PRINT OFFSET                                *
*                                                                     *
***********************************************************************
         MVC   QPOFFSET,=H'0' DEFAULT TO ZERO
         LH    R2,QLNG3       LENGTH OF OFFSET FIELD
         SH    R2,=H'1'       IS THE OFFSET FIELD ZERO LENGTH?
         BM    CALLLIST       YES. USE ZERO OFFSET.
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ2        MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK2       PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         STH   R2,QPOFFSET    STORE OFFSET
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
CALLLIST L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         LTR   R15,R15
         BZ    STOPIT
******************************************************************
*
*   THIS CODE CONTROLS THE NEXT AND PREVIOUS COMMAND FUNCTIONS
*
******************************************************************
         CLI   QSUBNAME,C'P'  IS THIS A PREVIOUS
         BNE   CHK001
         CLC   QOLDDSID,=F'100'
         BE    SETTO10
         B     RESTART
SETTO10  LA    R2,10
         ST    R2,QOLDDSID
         ST    R2,QPDSID
         B     RESTART
SETTO99  LA    R2,101
         ST    R2,QOLDDSID
         ST    R2,QPDSID
         B     RESTART
CHK001   CLI   QSUBNAME,C'N'  IS THIS A NEXT
         BNE   TILT
         CLC   QOLDDSID,=F'256'
         BH    TILTN
         B     RESTART
STOPIT   QSTOP
TILTP    LA    R2,0           RESET DSID TO 0
         ST    R2,QOLDDSID    STORIT
         ST    R2,QPDSID
         QTILT 'QLIST - No more "Previous" DSID''S available'
TILTN    QTILT 'QLIST - No more "Next" DSID''S available'
TILT     QTILT 'QLIST - Dataset ID is Invalid'
TILTREL  QTILT 'QLIST - Relative dataset only valid if Jobid is "*"'
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
DSIDPLUS AR    R4,R2          ADD TO PRESENT VALUE                UF040
DSIDSUB  SR    R4,R2          SUBTRACT FROM PRESENT VALUE         UF040
DSIDABS  LR    R4,R2          SET TO PRESENT VALUE                UF040
MVZ      MVZ   QFZONES(1),QPARM2 CHECK FOR NUMERIC
MVZ2     MVZ   QFZONES(1),QPARM3 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM2(1) CONVERT TO BINARY
PACK2    PACK  CONVERT,QPARM3(1) CONVERT TO BINARY
         LTORG
         DROP  ,                   DROP ALL ADDRESSINGS           NERDC
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
LIST     CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         $HASPEQU
         IFGRPL
         $BUFFER                                                  UF020
         $JCT
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
WORK     DSECT
         DS    72C
CONVERT  DS    D
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
