SYSLOG   TITLE 'QUEUE COMMAND - PRINT THE SYSTEM LOG DATASET'     ONL01
SYSLOG   QSTART ,                  START SYSLOG CSECT ASSEMBLY    ONL01
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       BASE REG FOR TEMP WORK
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CHECK JOBNAME EQUAL SYSLOG                                        *
*                                                                     *
***********************************************************************
         L     R9,QCJCTA      ADDR OF IOAREA FOR JCT
         USING JCT,R9         BASE REG FOR JCT                    ONL16
         CLC   JCTJNAME,=CL8'SYSLOG' IS THIS SYSLOG?
         BNE   TILT           NO. TILT.
***********************************************************************
*                                                                     *
*   DETERMINE NUMBER OF DATASETS FROM CURRENT DATASET                 *
*                                                                     *
***********************************************************************
*        LH    R3,JCTPDDBK    HIGHEST DATASET ID NUMBER           EGG01
         L     R3,JCTPDDBK    HIGHEST DATASET ID NUMBER           EGG01
         LH    R2,QLNG2       LENGTH OF BACKUP PARM
         SH    R2,=H'1'       IS THE BACKUP PARM ZERO LENGTH?
         BM    CALLLIST       YES. SKIP.
         CLI   QPARM2,C'-'    IS THERE A MINUS SIGN?
         BNE   PLUS           NO. SKIP.
         MVI   QPARM2,C'0'    CHANGE MINUS TO ZERO
PLUS     MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK        PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         SR    R3,R2          BACK UP NUMBER OF DATASETS
*        CH    R3,=H'101'     IS THE NUMBER LESS THAN 101?        EGG01
         C     R3,=F'101'     IS THE NUMBER LESS THAN 101?        EGG01
         BL    TILT           YES. TILT.
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AUTHORIZATION CHECK FOR LOOKING AT THE SYSTEM LOG     ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         QAUTHCK QASYSLOG          SEE IF USER CAN LOOK AT SYSLOG ONL02
         SPACE 2                                                  ONL02
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
*ALLLIST STH   R3,QPDSID      STORE DATASET ID                    EGG01
CALLLIST ST    R3,QPDSID      STORE DATASET ID                    EGG01
         MVC   QPOFFSET,=H'0' PRINT OFFSET ZERO
*        TM    QFLAG2,QFLG2X22 Q. IS THIS XA 2.2 LOG              EGG01
*        BZ    NXA22          A. NOPE                             EGG01
         MVC   QPOFFSET,=H'25' REPOSITION AT START OF TIME        BFG31
*XA22    DS    0H                                                 EGG01
         L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
*ILT     QTILT '*** INVALID PARAMETER ***'                        EGG02
TILT     QTILT 'QSYSLOG - Invalid Parameter'                      EGG02
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM2 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM2(1) CONVERT TO BINARY
         LTORG
         DROP  ,                   DROP ALL ADDRESSING            NERDC
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
SYSLOG   CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
        $HASPEQU
         IFGRPL
        $BUFFER ,                                                 ONL16
        $JCT   ,
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
WORK     DSECT
         DS    72C
CONVERT  DS    D
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
