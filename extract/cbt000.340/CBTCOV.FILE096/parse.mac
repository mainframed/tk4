PARSE    TITLE 'QUEUE COMMAND - COMMAND LINE PARSE ROUTINES'
PARSE    QSTART  ,                 START PARSE   CSECT ASSEMBLY
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     LOAD BASE REG
         USING WORK,R13       BASE REG FOR LOCAL WORK AREA
******************************************************************
*
*   PARSE SUBCOMMAND NAME
*
******************************************************************
         LH    R2,QDRLNG      LENGTH OF REPLY LINE
         OC    QDREPLY,QBLANK TRANSLATE TO UPPER CASE
         CLC   QDREPLY,QBLANK IS THE ENTIRE REPLY BLANK?
         BE    LOOKUP         YES. DO LOOKUP.
         MVC   QDTLINE,QDREPLY MOVE COMMAND LINE TO HEADING
         MVC   FIELD,QBLANK   BLANK THE WORK FIELD
         MVC   OFFSET(4),=F'0' ZERO THE OFFSET AND LENGTH
         MVC   QOFF0(12),OFFSET INITIALIZE FIRST FIELD
         MVC   QOFF1(48),QOFF0 INITIALIZE NEXT FOUR FIELDS
         LA    R6,QOFF4+12    ADDR PAST LAST FIELD
         LA    R5,QOFF0       ADDR OF FIRST SET OF FIELDS
         LA    R3,QDREPLY     FIRST BYTE OF REPLY LINE
ENCORE   LA    R4,FIELD       FIRST BYTE OF WORK FIELD
BLANK    CLI   0(R3),C' '     IS THIS BYTE BLANK?
         BNE   FIRST          NO. START OF FIELD.
         LA    R3,1(R3)       YES. SKIP IT.
         BCT   R2,BLANK       TRY NEXT BYTE
         B     EMPTY          END OF REPLY LINE.
FIRST    LH    R1,QDRLNG      REPLY LENGTH
         SR    R1,R2          COMPUTE OFFSET TO START OF FIELD
         STH   R1,OFFSET      STORE OFFSET
         LR    R1,R2          SAVE COUNT OF REMAINING BYTES
         B     CHAR           CONTINUE
LOOP     CLI   0(R3),C' '     IS THIS BYTE BLANK?
         BE    LAST           YES. END OF FIELD.
CHAR     MVC   0(1,R4),0(R3)  MOVE BYTE TO SUBNAME
         LA    R3,1(R3)       INCREMENT
         LA    R4,1(R4)       INCREMENT
         BCT   R2,LOOP        TRY NEXT BYTE
LAST     SR    R1,R2          COMPUTE FIELD LENGTH
         CH    R1,=H'8'       IS LENGTH GREATER THAN 8?
         BNH   STORE          NO. USE IT.
         LA    R1,8           YES. USE LENGTH OF EIGHT.
STORE    STH   R1,LENGTH      STORE FIELD LENGTH
EMPTY    MVC   0(12,R5),OFFSET MOVE FIELD TO SPCOMMON
         LTR   R2,R2          IS THE REMAINING LENGTH ZERO?
         BZ    LOOKUP         YES. DO THE TABLE LOOKUP.
         MVC   FIELD,QBLANK   BLANK THE WORK FIELD
         MVC   OFFSET(4),=F'0' ZERO OUT OFFSET AND LENGTH
         LA    R5,12(R5)      INCREMENT TO NEXT FIELD
         CR    R5,R6          WAS THAT THE LAST FIELD?
         BL    ENCORE         NO. PROCESS NEXT FIELD.
         SPACE 2
******************************************************************
*
*   PERFORM A TABLE LOOK UP FOR THE SP-AS SUB-COMMAND
*
******************************************************************
         SPACE 1
LOOKUP   DS    0H                  FIND QUE SUB-COMMAND IN TABLE
         LA    R2,TABLE            ADDR OF SUB-COMMAND TABLE
         SPACE 1
NEXT     CLC   0(4,R2),QSUBNAME    COMPARE FIRST 4 CHARACTERS
         BE    FOUND               THIS IS THE ONE
         LA    R2,12(R2)           NEXT ENTRY
         CLI   0(R2),X'FF'         IS THIS THE END OF TABLE?
         BNE   NEXT                NO. TRY NEXT ENTRY.
         SPACE 1
FOUND    DS    0H                  SUB-CMD FOUND OR END OF TABLE
         MVC   QSUBCMD,4(R2)       SET SUB-COMMAND ADDR FROM TABLE
         MVC   QCODEH,8(R2)        SET FUNCTION CODE FROM TABLE
         MVC   QASCFLGS,10(R2)     SET TYPE FLAGS FROM TABLE
         MVC   QASCUSER,11(R2)     SET USER FLAGS FROM TABLE
         SPACE 2
******************************************************************
*
*   PERFORM AN AUTHORIZATION CHECK AND RETURN TO PROCESS COMMAND
*
******************************************************************
         SPACE 1
         QAUTHCK QAPARSE          PERFORM AUTHORIZATION CHECK
         SPACE 1
         QSTOP  ,                  RETURN TO PROCESS COMMAND
         EJECT ,
******************************************************************
*
*   TABLE OF SP-AS SUB-COMMANDS
*
******************************************************************
         SPACE 1
TABLE    DS    0F                  QUE SUB-COMMAND COMMAND TABLE
         QSCE STAT,SEARCH                  STATUS
         QSCE ST,SEARCH                    STATUS
         QSCE PREV,LIST                    GO BACK ONE DSID
         QSCE PRE,LIST                    GO BACK ONE DSID
         QSCE PR,LIST                    GO BACK ONE DSID
         QSCE P,LIST                    GO BACK ONE DSID
         QSCE NEXT,LIST                    GO FORWARD ONE DSID
         QSCE NEX,LIST                    GO FORWARD ONE DSID
         QSCE NE,LIST                    GO FORWARD ONE DSID
         QSCE N,LIST                    GO FORWARD ONE DSID
         QSCE DD,DDNAME                    LIST DSID'S FOR A JOB
         QSCE LIST,LIST                    LIST SYSIN/SYSOUT FILE
         QSCE HELP,HELP                    LIST SYSIN/SYSOUT FILE
         QSCE L,LIST                       LIST SYSIN/SYSOUT FILE
         QSCE FIND,REPOS,FNC=4             FIND
         QSCE FALL,REPOS,FNC=4             FIND ALL
         QSCE FA,REPOS,FNC=4               FIND ALL
         QSCE F,REPOS,FNC=4                FIND
         QSCE FTIM,REPOS,FNC=8             FTIME (SYSLOG)
         QSCE FT,REPOS,FNC=8               FTIME (SYSLOG)
         QSCE COL,REPOS,FNC=12             COLUMN
         QSCE CO,REPOS,FNC=12              COLUMN
         QSCE C,REPOS,FNC=12               COLUMN
         QSCE '@',REPOS,FNC=16             REPOS TO RECORD NUMBER
         QSCE MD,REPOS,FNC=16              REPOS TO RECORD NUMBER
         QSCE '+',REPOS,FNC=20             DOWN
         QSCE D,REPOS,FNC=20               DOWN
         QSCE PF,REPOS,FNC=20              PAGE FORWARD
         QSCE HF,REPOS,FNC=20              HALF PAGE FORWARD
         QSCE '-',REPOS,FNC=24             UP
         QSCE UP,REPOS,FNC=24              UP
         QSCE PB,REPOS,FNC=24              PAGE BACK
         QSCE HB,REPOS,FNC=24              HALF BACK
         QSCE TOP,REPOS,FNC=28             TOP
         QSCE T,REPOS,FNC=28               TOP
         QSCE BOTT,REPOS,FNC=32            BOTTOM
         QSCE BOT,REPOS,FNC=32             BOTTOM
         QSCE B,REPOS,FNC=32               BOTTOM
         QSCE MODE,REPOS,FNC=36            MODEL
         QSCE MO,REPOS,FNC=36              MODEL
         QSCE M,REPOS,FNC=36               MODEL            ONL02
         QSCE SAVE,SAVE,FNC=4              SAVE CURRENT FILE
         QSCE SPIN,SPIN,FNC=4              SPIN CURRENT FILE
         QSCE JCL,JCL                      LIST JCL
         QSCE JC,JCL                       LIST JCL
         QSCE JLOG,JLOG                    LIST JOBLOG
         QSCE JL,JLOG                      LIST JOBLOG
         QSCE JMSG,JMSG                    LIST SYSMSGS
         QSCE JM,JMSG                      LIST SYSMSGS
         QSCE JH,CHIST                     LIST JOB HISTORY
         QSCE JHIS,CHIST                   LIST JOB HISTORY
         QSCE PFK,CPFK                     PFK LIST/CHANGE CMD
         QSCE PDDB,CPDDB                   LIST PDDB'S FOR JOB
         QSCE DC,ACTIVE                       DI ALL IN EXEC
         QSCE DA,SEARCH,FNC=4
         QSCE DI,SEARCH,FNC=8              DI ALL IN INPUT CLASS
         QSCE DO,SEARCH,FNC=12             DI ALL IN OUTPUT CLAS
         QSCE WO,SEARCH,FNC=52,            OUTPUT QUEUE W/ WTRID       +
               TYPE=QASXBLOG
         QSCE AI,SEARCH,FNC=16,            DI ALL AVAIL FOR EXEC       +
               TYPE=QASXBLOG
         QSCE AO,SEARCH,FNC=20,            DI ALL AVAIL OUTPUT         +
               TYPE=QASXBLOG
         QSCE HI,SEARCH,FNC=24             DI ALL IN INPUT HOLD
         QSCE HO,SEARCH,FNC=28             DI ALL IN OUTPUT HOLD
         QSCE DT,SEARCH,FNC=32,TYPE=QASXACTV DI TSO USERS
         QSCE DJ,SEARCH,FNC=36             DILAY JOB
         QSCE DS,SEARCH,FNC=40,TYPE=QASXACTV DILAY STC
         QSCE DQ,SEARCH,FNC=44,TYPE=QASXBLOG DI INPUT QUEUES
         QSCE DF,SEARCH,FNC=48,TYPE=QASXBLOG DI OUTPUT QUEUES
         QSCE NJ,SEARCH,FNC=56,TYPE=QASXBLOG DI NJE QUEUES
         QSCE PURGE,SYSOUT                          DELETE JOB
         QSCE PUR,SYSOUT                            DELETE JOB
         QSCE PU,SYSOUT                             DELETE JOB
         QSCE REQ,SYSOUT,FNC=4                         REQUEUE JOB
         QSCE RE,SYSOUT,FNC=4                          REQUEUE JOB
         QSCE CANC,SYSOUT,FNC=8                         CANCEL JOB
         QSCE CAN,SYSOUT,FNC=8                          CANCEL JOB
         QSCE CA,SYSOUT,FNC=8                           CANCEL JOB
         QSCE TSO,CTSO                     ISSUE ANY TSO COMMAND
         QSCE EX,CTSO                      IMPLICIT CLIST INVOKE
         QSCE EXEC,CTSO                    IMPLICIT CLIST INVOKE
         QSCE SLOG,SYSLOG                  LIST THE SYSTEM LOG
         QSCE SL,SYSLOG                    LIST THE SYSTEM LOG
         QSCE XI,INITS,TYPE=QASXSLOG            ACTIVE INITS
         QSCE XB,HEXBLK,TYPE=QASXSYSP      DISP SPOOL BLOCK
         QSCE XJ,SEARCH,FNC=36,TYPE=QASXSYSP DISP HEX JQE/JOE
         QSCE XD,XDS,TYPE=QASXSYSP         UNRESTRICTED FILE DISP
         QSCE XC,CONSOLE,TYPE=QASXSLOG            CONSOLE DISPLAY
         QSCE XP,HELP,TYPE=QASXOPER        ENTER PASSWORD
         QSCE JQE,CJQE,TYPE=QASXSYSP        HEX DUMP OF JQE
         QSCE JCT,CJCT,TYPE=QASXSYSP        HEX DUMP OF JCT
         QSCE IOT,CIOT,TYPE=QASXSYSP        HEX DUMP OF IOTS
         QSCE HCT,CHCT,TYPE=QASXSYSP        HEX DUMP OF HCT SAVEARE
         QSCE JOE,CJOE,TYPE=QASXSYSP        HEX DUMP OF JOE
         QSCE TGPS,CTGPS,TYPE=QASXSLOG      LIST LARGE SPOOL USERS
         QSCE SBSY,SUBSYS,FNC=8,TYPE=QASXSYSP GE SUBSYSTEM NAME
         QSCE DIE,CDIE,TYPE=QASXSYSP        FORCE 0C1 PROGRAM CHECK
         SPACE 1
HELPCC   DC    F'-1',V(SEARCH),Y(0),AL1(0,0) MARK END OF TABLE
         EJECT ,
******************************************************************
*
*   MIQSCELLANY
*
******************************************************************
         LTORG
WPASS    DC    X'C1115D7F1140403C4040001D4C13'  *** SPF TCAM
         SPACE 1
WORK     DSECT
         DS    CL72
OFFSET   DS    H
LENGTH   DS    H
FIELD    DS    CL8
RPASS    DS    CL8
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION
         QPRBGEN BEGIN              SET PRINT FOR CNTL BLOCK GEN
         QCOMMON
         $HASPEQU
         QPRBGEN DONE             RESTORE NORMAL PRINT STATUS
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION
         END
