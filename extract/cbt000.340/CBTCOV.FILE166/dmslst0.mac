LST0     TITLE 'D M S L S T 0  LIST ROUTINE'
         PRINT NOGEN
         DMSMACRO
         PRINT ON,GEN
         EJECT
DMSLST0  PGMSTART
         USING DMSCONST,DMSREG
         OI    PRTFLAG,PRTCONFO         FORCE MESSAGES TO CONSOLE
         L     R5,DMSLNGT               GET MACRO SIZE
         LA    R0,16(,R5)               ADD PREFIX
         BAL   R1,*+4                   MAKE R1 NEGATIVE
         SVC   10                       GETMAIN PSEUDO CONST
         LR    R2,R1                    SAVE GOTTEN AREA
         LA    R4,16(,R1)               POINT TO MACRO ORIGIN
         LR    R7,R5                    SET LENGTH OPERAND
         LA    R6,0(,DMSREG)            FROM ADDRESS IS MACRO
         MVCL  R4,R6                    MOVE PSEUDO MACRO
         LA    R4,=CL8'EDTCON0'         POINT TO ROUTINE NAME
         LOAD  EPLOC=(R4),ERRET=LISTRET
         LR    R15,R0                   ENTRY ADDRESS
         LR    R1,R2                    PARAMETER ADDRESS
         BALR  R14,R15                  GO TO LIST ROUTINE
         DELETE EPLOC=(R4)              DELETE ROUTINE AGAIN
LISTRET  L     R5,DMSLNGT               LENGTH OF MACRO
         LA    R0,16(,R5)               WITH PREFIX
         LA    R1,0(,R2)                FREEMAIN PSEUDO
         SVC   10                       CONST
         NI    PRTFLAG,255-PRTCONFO     RESET FORCE FLAG
         L     R13,4(,R13)
         L     R14,12(,R13)
         SR    R15,R15
         LM    R0,R12,20(R13)
         BR    R14
         LTORG
         END
