         PRINT NOGEN
IEAVLK01 SPZAP MEMBER=IEANUC01,BASE=X'00',SIZE=X'0988'
NO       EQU   0
R1       EQU   1
TCBREG   EQU   4
RBREG    EQU   5
BASEREG  EQU   6
GR10     EQU   10
CDEREG   EQU   11
CDIREG   EQU   12
MAX      EQU   X'FF'
SPRENT   EQU   252
         USING IEAVLK01,BASEREG
         USING TCB,TCBREG
         USING RBSECT,RBREG
         USING CDENTRY,CDEREG
*
*        SD00006 - SDLODLPA MOD
*              LEVEL: 7611, PTF# 6213
*
         VER   X'05BC'
         TM    TCBTRN,TCBTCP
RETURN1  BZ    FETCHIT
*
         VER   X'05CC'
FETCHIT  LA    GR10,SPRENT
         NI    CDATTR,MAX-CDJPA
KETCHIT  L     11,PSACSTK-PSA(0,0)
*
         VER   X'066E'
         TM    TCBTRN,TCBTCP
RETURN2  BO    DSS06
*
         VER   X'0680'
SETAUTHL OI    CDATTR2-CDENTRY(CDIREG),CDSYSLIB
*
         VER   X'073E'
DSS06    BCR   NO,NO
*
         REP   X'05BC'
         B     ZAP1
*
         REP   X'066E'
         B     ZAP2
*
ZAP1     PATCH (DA00001,'ALLOW MODULES TO BE LOADED INTO CSA')
         TM    RBSTAB2,RBTCBNXT   IS THERE PREVIOUS RB?
         BO    BACK               NO - RETURN
         L     R1,RBLINK          ADDR OF PREVIOUS RB
         TM    RBSTAB1-RBSECT(R1),X'E0'     IS IT A PRB?
         BNZ   BACK               NO - RETURN
         L     R1,RBCDE-RBSECT(,R1)    GET CDE ADDR FROM PRB
         CLC   CDNAME-CDENTRY(8,R1),SDLODLPA     SPECIAL LOADER NAME?
         BNE   BACK               NO - RETURN
         LA    GR10,241           USE CSA SUBPOOL 241
         B     KETCHIT            CONTINUE
BACK     TM    TCBTRN,TCBTCP      EXECUTE ZAPPED INSTRUCTION
         B     RETURN1            PERFORM RETURN
SDLODLPA DC    CL8'SDLODLPA'      SPECIAL LOADER NAME
*
ZAP2     DS    0H
         LA    R1,241             GET CSA SUBPOOL NUMBER
         CR    R1,GR10            SPECIAL LOAD?
         BE    SETAUTHL           YES - SET AUTH LIB INDICATOR
         TM    TCBTRN,TCBTCP      EXECUTE ZAPPED INSTRUCTION
         B     RETURN2            PERFORM RETURN
         PRINT ON,NOGEN
         IKJTCB
         IHARB
         IHACDE
         IHAPSA
         PRINT ON,GEN
         END
