LPAL     TITLE 'LPALINK - LINK TO SDLODLPA MODULE'
********************
* LPALINK  ROUTINE *
********************
*
* REQUIRED JCL:
*
*   //STEP EXEC PGM=LPALINK,PARM=XXXXXXXX
*                              WHERE 'XXXXXXXX' IS THE NAME OF THE
*                              MODULE TO BE LOADED.
*   //STEPLIB DD ........
*   //SDLODLPA DD .......      DD CARD FOR AUTHORIZED PGM LIBRARY
*                              CONTAINING THE SDLODLPA ROUTINE.
*   //MODULE DD .........      DD CARD FOR AUTHORIZED PGM LIBRARY
*                              CONTAINING THE MODULE TO BE LOADED.
         EJECT ,
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
         IHADCB DSORG=PO,DEVD=DA
         PRINT GEN
         EJECT
LPALINK  CSECT
         USING LPALINK,R15             TEMP BASE
         STM   R14,R12,12(R13)         SAVE
         LR    R4,R13                  SAVE AREA PTR
         LA    R13,SAVE                OUR SAVE
         ST    R4,4(R13)
         ST    R13,8(R4)
         LR    R12,R15                 PROGRAM BASE
         DROP  R15
         USING LPALINK,R12
         B     SAVE+72
         DC    CL8'&SYSDATE'
SAVE     DS    18F
         L     R1,0(,R1)           ADDR OF PARMLIST
         LH    R2,0(,R1)           LENGTH OF PARM
         LTR   R2,R2               ANY PARMS ?
         BZ    PARMERR             NO-FORGET IT
         C     R2,=F'8'            8 BYTES OR LESS ?
         BH    PARMERR             NO-FORGET IT
         BCTR  R2,0
         EX    R2,MOVENAME
         SPACE 1
         OPEN  (MODULDCB,(INPUT),SDLODDCB,(INPUT))
         LA    R1,MODULDCB
         USING IHADCB,R1
         TM    DCBOFLGS,DCBOFOPN   OPEN OK ?
         BNO   OPENERR
         LA    R1,SDLODDCB
         TM    DCBOFLGS,DCBOFOPN   OPEN OK ?
         BNO   OPENERR
         DROP  R1
         SPACE 1
         LA    R2,PARMLIST
         LINK  EP=SDLODLPA,DCB=SDLODDCB,PARAM=((R2)),ERRET=LINKERR
         SPACE 2
         CLOSE (MODULDCB,,SDLODDCB)
         SPACE 2
         SR    R7,R7
EXIT     LR    R15,R7                  SET RETURN CODE
         L     R13,4(R13)              BACK CHAIN TO SAVE AREA
         L     R14,12(R13)             GET RETURN ADDRESS
         LM    R0,R12,20(R13)          RESTORE REGS
         BR    R14                     RETURN
         EJECT
PARMERR  DS    0H
         WTO   'LPALINK - INVALID PARAMETER LIST',ROUTCDE=(2,11)
         LA    R7,4
         B     EXIT
         SPACE 2
OPENERR  DS    0H
         WTO   'LPALINK - OPEN ERROR',ROUTCDE=(2,11)
         ABEND 12,DUMP
         SPACE 2
LINKERR  DS    0H
         WTO   'LPALINK - LINK ERROR',ROUTCDE=(2,11)
         LR    R7,R15
         B     EXIT
         SPACE 3
MOVENAME MVC   MODNAME(*-*),2(R1)
         SPACE 1
PARMLIST DS    0F
MODNAME  DC    CL8' '
DCBADDR  DC    AL4(MODULDCB)
         SPACE 1
         PRINT NOGEN
SDLODDCB DCB   DSORG=PO,DDNAME=SDLODLPA,MACRF=(E)
MODULDCB DCB   DSORG=PO,DDNAME=MODULE,MACRF=(E)
         PRINT GEN
         SPACE 2
         LTORG
         DROP  R12
         SPACE 2
         END   LPALINK
