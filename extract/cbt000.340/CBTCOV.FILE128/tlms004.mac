./ ADD NAME=MDSBYDSP
***********************************************************************
*                                                                     *
*    EXIT FOR DSP SVC TO VERIFY USER MAY USER BYPASS TLMS             *
*         PROCESSING ( SPACE=(1,(1,1)) ). THE EXIT WILL               *
*         ASK OPERATOR WHETHER TO PERMIT BYPASS OF DATA SET           *
*         PROTECT FEATURE. RESPONSES ARE:                             *
*            "YES"    - PERMIT BYPASS OF DATA SET PROTECT FEATURE     *
*            "NO"     - DO NOT PERMIT BYPASS - JOB FAILS WITH A 713   *
*                       ABEND.                                        *
*         IF THE STEP IS NOT ABENDED, RETURN CODES ARE:               *
*               0     - PERMIT BYPASS OF DSP                          *
*               4     - DO NOT PERMIT DSP; FAIL JOB                   *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
MDSBYDSP CSECT
         LA    R0,MDSBYWKL             LOAD LENGTH OF WORK AREA
         USING MDSBYDSP,R15
         GETMAIN R,LV=(0)              GET CORE FOR MSG
         DROP  R15
         USING MDSBYWKA,R1
         STM   R2,R14,MDSBYSVR         SAVE REGISTERS (NON STANDARD)
         LR    R13,R1                  CREATE A DUMMY SAVE AREA
         DROP  R1
         USING MDSBYWKA,R13
         BALR  R12,0                   SET UP A BASE REGISTER
         USING *,R12
         XC    MDSBYSVE(12),MDSBYSVE   CLEAR TO PREVENT BAD RETURNS
         XC    MDSBYECB,MDSBYECB       CLEAR THE ECB
         MVC   MDSBYREP(3),=CL3' '     CLEAR RESONSE AREA
         MVC   MDSBYPRM(MDSBYWL1),MDSBYWT1  INIT WTOR PARM FIELD
         WTOR  ,MDSBYREP,5,MDSBYECB,MF=(E,MDSBYPRM)
MDSBYLOP DS    0H
         WAIT  ECB=MDSBYECB,LONG=YES
         OC    MDSBYREP(3),=CL3' '     SWITCH TO UPPER CASE
         CLC   MDSBYREP(3),=CL3'CONT'  OK?
         BE    MDSBYRET                GO LET OS CHECK DATE
         XC    MDSBYECB,MDSBYECB       CLEAR THE ECB
         MVC   MDSBYREP(3),=CL3' '     CLEAR RESONSE AREA
         MVC   MDSBYPRM(MDSBYWL2),MDSBYWT2  INIT WTOR PARM FIELD
         WTOR  ,MDSBYREP,5,MDSBYECB,MF=(E,MDSBYPRM)
         B     MDSBYLOP                GET ANOTHER RESPONSE
MDSBYRET DS    0H
         LR    R1,R13                  LOAD ADDRESS OF WORK AREA
         LM    R2,R14,MDSBYSVR         RETORE REGISTER'S (R15 RET CODE)
         LA    R0,MDSBYWKL             LOAD LENGTH OF WORK AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED STORAGE
         SR    R15,R15
         BR    R14                     RETURN TO CALLER (DSP SVC)
MDSBYWT1 WTOR  ('OP996D ATTEMPTING TO BYPASS TLMS DATA SET PROTECT FACIX
               LITY - PLEASE REPLY ''CONT'' OR CANCEL JOB'),           X
               ,,,ROUTCDE=(9,11),MF=L
MDSBYWL1 EQU   *-MDSBYWT1
MDSBYWT2 WTOR  ('OP997D INVALID REPLY - PLEASE REPLY ''CONT'' TO PERMITX
                TLMS DATA SET PROTECT BYPASS OR CANCEL JOB'),          X
               ,,,ROUTCDE=(9),MF=L
MDSBYWL2 EQU   *-MDSBYWT2
         LTORG
MDSBYWKA DSECT
MDSBYSVE DS    18A                     SAVE AREA (PSUEDO CALLER'S)
MDSBYSVR DS    13A                     SAVE CALLER'S REGISTERS AREA
MDSBYECB DS    A                       EVENT CONTROL BLOCK FOR WTOR'S
MDSBYREP DS    CL3                     OPERATOR ANSWER AREA
         DS    0D
MDSBYPRM WTOR  ('                                                      X
                                                                       X
                                                                    '),X
               ,0,3,0,MF=L
MDSBYWKL EQU   (*-MDSBYWKA+7)/8*8      ROUND TO DOUBLE WORD
         END   MDSBYDSP
