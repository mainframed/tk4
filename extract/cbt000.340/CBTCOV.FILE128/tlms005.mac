./       CHANGE NAME=TLMSDSPF
./       NUMBER SEQ1=215000,NEW1=215010,INCR=10,INSERT=YES
*** PREVIOUS CARD IS THE SVC 1 (WAIT AFTER TEST TLMS MASTER) **** MDS01
*                                                               * MDS01
*     CALL RACF EXIT TO ADD NEW PROTECTION FOR VOLUME IF        * MDS01
*     REQUIRED. THE EXIT (ICHRTX01) IS CALLED IF THE TAPE       * MDS01
*     HAS BEEN OPENED FOR OUTPUT PROCESSING AND THE TAPE        * MDS01
*     VOLUME IS EXTERNAL TO TLMS PROCESSING OR IS AN            * MDS01
*     AVAILABLE SCRATCH TLMS TAPE. THE EXIT WILL OPERATE        * MDS01
*     IN SUPERVISOR STATE AND KEY ZERO. (TAKE CARE)             * MDS01
*                                                               * MDS01
*     PARAMETERS FOR ICHRTX01 :                                 * MDS01
*                                                               * MDS01
*      R1 --> +0  POINTER TO VOLUME NAME (LENGTH OF 6 BYTES)    * MDS01
*             +4  POINTER TO THE IOS WORK AREA.(MACRO IECDSECT) * MDS01
*             +8  POINTER TO FULLWORD CONTAIN VOLUME TYPE       * MDS01
*                      0 - VOLUME UNDER TLMS CONTROL            * MDS01
*                      4 - EXTERNAL VOLUME                      * MDS01
*                                                               * MDS01
*     NO RETURN CODES PROCESSED FROM ICHRTX01                   * MDS01
*                                                               * MDS01
***************************************************************** MDS01
         TM    DSPFECB+3,3             VOL UNDER TLMS CONTROL BUT MDS01
*                                      IS NOT SCRATCH             MDS01
         BM    REMOUNT                 IF SO REJECT VOLUME        MDS01
         LA    R0,DSPFVSN              LOAD ADDRESS OF VOL-ID     MDS01
         ST    R0,MDSPVSN              STORE ADDRESS IN PARM LIST MDS01
         ST    RCORE,MDSPOPWA          STORE ADDRESS IN PARM LIST MDS01
         LA    R0,MDSTYPE              LOAD ADDRESS OF TYPE PARM  MDS01
         ST    R0,MDSPTYPE             STORE ADDRESS IN PARM LIST MDS01
         OI    MDSPTYPE,X'80'          INDICATE END OF PARM LIST  MDS01
         SR    R0,R0                   CLEAR REGISTER ZERO        MDS01
         CLI   DSPFECB+3,0             VOLUME UNDER TLMS CONTROL  MDS01
*                                      AND IS SCRATCH AND AVAIL   MDS01
         BE    MDS00100                                           MDS01
         LA    R0,4                    4 SHOW VOLUME IS EXTERNAL  MDS01
MDS00100 DS    0H                                                 MDS01
         ST    R0,MDSTYPE              STORE VALUE IN TYPE PARM   MDS01
         LR    R7,RD                   SAVE REGISTER 13           MDS01
         LR    R8,RE                   SAVE REGISTER 14           MDS01
         LA    RD,MDSSAVE              LOAD ADDRESS OF SAVE AREA  MDS01
         LA    R1,MDSPARM              LOAD ADDRESS OF PARM LIST  MDS01
         L     RF,=V(ICHRTX01)         LOAD ADDRESS OF RACF EXIT  MDS01
         BALR  RE,RF                   CALL RACF EXIT             MDS01
         CH    RF,=H'4'                IS THIS A RACF TAPE?       MDS01
         BNE   MDS00200                IF NOT DON'T MARK THE TAPE MDS01
*                                                                 MDS01
*        BUILD QUEUE MANAGER PARAMETER BLOCK                      MDS01
*                                                                 MDS01
         MVI   DSPFUPRC,0              CLEAR UPDA RETURN CODE     MDS01
         MVI   DSPFMTYP,C'R'           INDICATE VSN TO BE MARKED  MDS01
*                                      RACF PROTECTED             MDS01
         SR    R0,R0                   SET REGISTER 0 TO ZERO     MDS01
         ST    R0,DSPFPCDE             CLEAR CNTL RETURN CODE     MDS01
         ST    R0,DSPFECB              CLEAR DSP ECB              MDS01
*                                                                 MDS01
*        ISSUE REQUEST THAT TAPE BE MARKED RACF PROTECTED         MDS01
*                                                                 MDS01
         LA    R0,2                    LOAD PUT QUEUE CODE        MDS01
         LA    R1,DSPFCNTL             POINT TO COMMUNICATION BLK MDS01
         SVC   &QMG                    EXECUTE QUEUE MANAGER SVC  MDS01
         LTR   R0,R0                   SUCCESSFULL?               MDS01
         BNE   NOPUT                   IF NOT ISSUE WARNING       MDS01
         WAIT  ECB=DSPFECB             WAIT FOR ACTION COMPLETION MDS01
MDS00200 DS    0H                                                 MDS01
         LR    RD,R7                   RESTORE OLD REGISTER 13    MDS01
         LR    RE,R8                   RESTORE OLD REGISTER 13    MDS01
./       NUMBER SEQ1=460000,NEW1=460010,INCR=10,INSERT=YES
MDSPARM  DS    0A                      PARM LIST FOR RACF EXIT    MDS01
MDSPVSN  DS    A                       POINTER TO VOLUME NAME     MDS01
MDSPOPWA DS    A                       POINTER TO DATASET NAME    MDS01
MDSPTYPE DS    A                       POINTER TO TYPE WORD       MDS01
MDSTYPE  DS    F                       TYPE WORD EXT/INT TLMS     MDS01
MDSSAVE  DS    18A                     SAVE AREA FOR RACF EXIT    MDS01
./       CHANGE NAME=TLMBMASS
         IF DELETE-CODE IN MAS-REC-SEQ = 'S' OR = 'R',
./       CHANGE NAME=TLMSCTL
./       NUMBER SEQ1=385000,NEW1=385100,INCR=100,INSERT=YES
* UPDATE SHOULD FOLLOW SIMILAR TEST FOR DSP TRANSACTION (C'6')
         CLI   TLMSG+3,C'R'   TEST FOR RACF TRANSACTION           MDS01
         BE    DSPCALL        IF SO MUST POST USER, WORK DONE     MDS01
./       CHANGE NAME=TLMBUPDA
./       NUMBER SEQ1=219000,NEW1=219010,INCR=10,INSERT=YES
       01  RACF-CHANGE-MSG REDEFINES GUMMED-LABEL-MSG.
           02  FILLER PICTURE X(20).
           02  VSN    PICTURE X(6).
./       NUMBER SEQ1=242000,NEW1=242010,INCR=10,INSERT=YES
      *   -UPDATE FOLLOWS "IF MSG-TYPE EQUAL TO '6' GO TO DSP-READ"
      *   CHECK IF THIS IS A RACF REQUEST
      *
         IF MSG-TYPE EQUAL TO 'R' GO TO RACF-PROTECT.
./       NUMBER SEQ1=386000,NEW1=386010,INCR=10,INSERT=YES
      *********************************************************** MDS01
      *  -UPDATE FOLLOWS "MOVE SCT-FLG TO SCT IN MAS-REC" IN    * MDS01
      *  PARAGRAPH SCT-10.                                      * MDS01
      *                                                         * MDS01
      *   CALL THE REMOVE RACF PROTECTION EXIT BEFORE SETTING   * MDS01
      *   THE TLMS CONTROLLED TAPE TO SCRATCH STATUS. THE EXIT  * MDS01
      *   HAS ONE ARGUEMENT, A POINTER TO THE NAME OF THE       * MDS01
      *   VOLUME SERIAL WHICH IS TO BE SCRATCHED. THE EXIT      * MDS01
      *   WILL BE CALLED BY AN AUTHORIZED PROGRAM AND HENCE     * MDS01
      *   SHOULD BE LINK EDITED WITH THE AC(1) LKED OPTION.     * MDS01
      *   IN ADDITION THE EXIT WILL OPERATE IN PROGRAM STATE    * MDS01
      *   AND PROGRAM KEY.                                      * MDS01
      *                                                         * MDS01
      *       RETURN CODES :                                    * MDS01
      *                                                         * MDS01
      *            0 - MARK VOLUME SERIAL SCRATCH               * MDS01
      *            4 - DO NOT SCRATCH VOLUME                    * MDS01
      *                                                         * MDS01
      *********************************************************** MDS01
      *                                                           MDS01
      *    IF TLMBUPDA CALLED BY TLMBRBLD DO NOT DO ANY RACF      MDS01
      *    PROCESSING. THE RACF DATASET IS VALID AND ONLY THE     MDS01
      *    TLMS MASTER FILE HAS TO BE UPDATED                     MDS01
      *                                                           MDS01
           IF CALL-KEY NOT EQUAL '6'
             IF DELETE-CODE IN MAS-REC EQUAL 'R'
               CALL 'ICHRTX02' USING VSN-KEY
               IF RETURN-CODE NOT EQUAL ZERO
                 DISPLAY 'TLMBUPDA  - VOLUME ' VSN-KEY
                         ' NOT SCRATCHED BECAUSE OF RACF EXIT'
                         UPON CONSOLE
                 GO TO SCT-EX.
./       NUMBER SEQ1=481000,NEW1=481010,INCR=10,INSERT=YES
      *   -INSERT AFTER PARAGRAPH "SCRATCH-TAPE"
      *
      *   FUNCTION SETS THE DELETE CODE FOR A VSN TO "R" TO INDICATE
      *   THE VOLUME IS RACF PROTECTED.
      *
       RACF-PROTECT.
         MOVE VSN IN RACF-CHANGE-MSG TO VSN-KEY.
         PERFORM VERIFY-READ-REC THRU END-VERIFY-READ.
         MOVE 'R' TO DELETE-CODE IN MAS-REC.
         GO TO RETURN-PT.
./       ENDUP
