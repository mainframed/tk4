         TITLE 'ICHRTX02 - EXIT FROM TLMS-TSCRATCH TO RDELETE TAPES'
***********************************************************************
***********************************************************************
***                                                                 ***
***  TLMS EXIT FROM TLMS TSCRATCH PROGRAM TO DELETE THE TAPES       ***
***  PROFILE FROM RACF,AND HENCE REMOVE THE VOLUME'S RACF PROTECTION***
***                                                                 ***
***********************************************************************
***********************************************************************
         SPACE 5
***   TAPES ARE DELETED FROM RACF WHENEVER TLMS DECIDES THAT A TAPE ***
*** IS IN SCRATCH STATUS. THE ONLY FUNCTION OF THIS MODULE IS TO    ***
*** ISSUE A RACDEF TYPE=DELETE FOR THE TAPE VOLUME SPECIFIED ON ENTRY**
         SPACE 3
ICHRTX02 CSECT
         SAVE  (14,12),T,ICHRTX02_&SYSDATE_&SYSTIME
         LR    12,15                   * COPY ENTRY ADDRESS
         LR    2,1                     * SAVE PARAMETER ADDRESS
         USING ICHRTX02,12             * ADDRESSABILITY
         GETMAIN R,LV=$DSALEN          * GET DYNAMIC STORAGE
         LR    10,13                   * SAVE OLD SAVE AREA ADDR
         ST    1,8(13)                 * LINK IN PREVIOUS S.A.
         LR    13,1                    * ADDRESS NEW SAVE AREA
         USING $DSA,13                 * ADDRESS DYNAMIC STORAGE
         LA    7,$DSALEN               * SET UP MVCL FOR DSA INIT
         LR    15,7                    * LENGTH
         LR    6,13                    * ADDRESS TARGET
         LA    14,$DSAINIT             * ADDRESS SOURCE
         MVCL  6,14                    * INITIALIZE DSA
         ST    10,4(13)                * LINK BACK TO PREV. S.A.
         EJECT
** PARAMETERS ON ENTRY:
**    R1  -> AL4(VOLNAME) -> CL6'VOLUME'    VOL-SER OF TAPE
**
** PARAMETERS RETURNED:
**    R15  = RETURN CODE
**               0 IF TAPE HAS BEEN DELETED FROM RACF
**               4 IF TAPE HAS NOT BEEN DELETED FROM RACF
**
         SPACE 2
         USING PARMLIST,2              * ADDRESS THE PARAMETERS BY NAME
         L     6,TLMSVOL
         RACDEF ENTITY=(6),CLASS=CLASST,MF=(E,RACDEFL)
         SPACE
** RETURN CODES FROM RACDEF ARE:
**       -  0  RACDEF HAS COMPLETED SUCCESSFULLY.
**       -  4  FOR DEFINE THE RESOURCE WAS PREVIOUSLY DEFINED.
**       -  8  FOR DEFINE/DELETE THE REQUEST WAS FAILED BY THE
**             INSTALLATION EXITS,USER NOT RACF DEFINED,
**             NOT AUTHORIZED TO CREATE GROUP DATASETS,
**             OR FOR ADDVOL THE FIRST VOL WAS NOT DEFINED.
**       - 12  FOR NEWNAME, THE OLD DATASET WAS NOT DEFINED.
**       - 16  FOR DEFINE WITH MENTITY, THE MODEL WAS NOT DEFINED.
**
         CH    15,=H'16'               * CHECK IF UNKNOWN CODE
         BH    RETURN0                 * WHAT TO DO NOW?
*
         B     *+4(15)                 * CHECK RETURN
         B     RETURN0                 * RC=0 - BUILD ACCESS LIST
         B     RETURN0                 * RC=4 - DUPLICATE DEFINE
*                                      * DO NOT ADD ANY USERS TO
*                                      * ACCESS LIST.
         B     RETURN0                 * RC=8 - SHOULDN'T HAPPEN
         B     RETURN0                 * RC=12- SHOULDN'T HAPPEN
         B     RETURN0                 * RC=16- SHOULDN'T HAPPEN
         EJECT
RETURN4  LR    10,13                   *
         L     13,4(13)                *
         FREEMAIN R,LV=$DSALEN,A=(10)  *
         RETURN (14,12),RC=4           * RETURN TO TLMS
         SPACE 3
RETURN0  LR    10,13                   *
         L     13,4(13)                *
         FREEMAIN R,LV=$DSALEN,A=(10)  *
         RETURN (14,12),RC=0           * RETURN TO TLMS
         EJECT
CLASST   DC    X'07',CL7'TAPEVOL'      * CLASS NAME
         LTORG
         EJECT
$DSAINIT DS    0D                      * INITIAL VALUES FOR DSA
$SAVE    DS    18F'0'                  * DYNAMIC SAVE AREA
$RACDEFL RACDEF ENTITY=,TYPE=DELETE,CLASS=CLASST,MF=L
$TVOLLEN DS    AL1                     * LENGTH OF ENTITY
$TVOL    DS    CL44' '                 * ENTITY NAME
         EJECT
$DSA     DSECT
SAVE     DS    18F                     * DYNAMIC SAVE AREA
RACDEFL  RACDEF ENTITY=,TYPE=DELETE,MF=L
TVOLLEN  DS    AL1                     * LENGTH OF ENTITY
TVOL     DS    CL44                    * ENTITY NAME
$DSAEND  EQU   *
$DSALEN  EQU   *-$DSA
*
PARMLIST DSECT
TLMSVOL  DS    AL4                     * ADDRESS OF CL6'VOLSER'
         END
