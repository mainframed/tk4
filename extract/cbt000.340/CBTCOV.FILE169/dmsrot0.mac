ROT0     TITLE 'DMSROT0 / DYNAMIC MONITOR SYSTEM ROOTING CSECT'
* * * * * * * * * * * * * * * * * * * * *
*                                       *
*              D M S R O T 0            *
*                                       *
* * * * * * * * * * * * * * * * * * * * *
         SPACE
* FUNCTION - OPERATION
*        DMSROT0 RECEIVES CONTROL FROM THE OPERATING SYSTEM AND
*        CONTROLS THE PROGRAM INITIALISATION, EXEC-PARM PROCESSING
*        AND START PARAMETER PROCESSING.
*        IT CONTROLS ALSO THE WORK-MODULE DISPATCHING AND ALL
*        ABNORMAL PROCESSINGS. IT CONTAINS ALL SHARED CONSTANTS,
*        WHICH CAN BE REFERED VIA REG 10 AND THE DMSMACRO MACRO.
         SPACE
* INPUT
*        EXECUTE PARAMETERS FROM THE OPERATING SYSTEM.
*        START PARAMETERS, IF STARTED FROM THE CONSOLE.
         SPACE
* OUTPUT
*        NONE
* RETURN
*        BACK TO THE OPERATING SYSTEM WITH THE HIGHEST
*        DETECTED RETURN CODE
         PRINT NOGEN
         DMSMACRO
         PRINT ON,GEN
         EJECT
DMSROT0  PGMSTART
         SPACE 3
         L     R10,=V(DMSCONST)          BASE SHARED CONST
         LR    R3,R1                    SAVE -> EXEC-PARM ADDR
         OI    INPFLAG,INPCRDIN         SET CARD INPUT DEFAULT
         SPACE
         EXTRACT ROTCSCLP,FIELDS=COMM
         SPACE
         L     R8,ROTCSCLP              -> TO COMMUNICATION POINTERS
         MVC   DMSCECBA(3),1(R8)        -> TO COMM ECB
         L     R7,4(R8)                 -> CIB
         LA    R7,0(R7)                 CLEAR BYTE 0
         LTR   R7,R7                    CONSOL STARTED ?
         BZ    ROT001                   NO, GO
         OI    PRTFLAG,PRTCONS          PRINTOUT TO CONSOLE
         NI    INPFLAG,255-INPCRDIN     CLEAR CARD INPUT DEFAULT
         OI    DMSFLAG1,CONSTASK        SET CONSOLE STARTED TASK
ROT001   CALL  DMSDATE,(ROTDATE,ROTTIME),VL
         MVC   ROTTEXT(64),TITLETXT
         SPACE
         PRINTER ROTTITLE               PRINT TITLE
         MVI   TITLETXT,64              BLANK
         MVC   TITLETXT+1(63),TITLETXT  OUT THE LINE
         TM    DMSFLAG1,NOGO            IS DDNAME OK
         LA    R15,12                   SET RETURN CODE
         BO    ROTRET                   RETURN
         PRINTER ROTSPACE               SPACE ONE
         L     R1,0(R3)                 -> EXEC PARM
         CALL  DMSINP0                  PROCESS EXEC - PARM
         TM    DMSFLAG1,CONSTASK        IS IT CONSOLE TASK
         BZ    ROT002                   NO, GO
ROT005   LA    R1,CIBCLGTH              BASE COMMAND INPUT
         CALL  DMSINP0                  PROCESS COMMAND INPUT
         SPACE
         QEDIT ORIGIN=4(R8),BLOCK=(R7)  FREE START BUFFER
ROT002   QEDIT ORIGIN=4(R8),CIBCTR=1    STOP & MODIFY COMMANDS
         SPACE
         TM    INPFLAG,INPGO            WAS GO KEYWORD IN THE FIELDS RN
         BO    ROT007A                  YES,SKIP CARD PROCESSING     RN
ROT007   SR    R1,R1                    ASK FOR
         CALL  DMSINP0                  INPUT
ROT007A  TM    DMSFLAG1,NOGO            IS ALL OK                    RN
         BZ    ROTPROC                  YES, START PROCESSING
         MVI   DMSRETCD,12              RETURN CODE 12
         B     ROTRETRN                 GO BACK
         EJECT
***
*        START PROCESSING
***
         SPACE
ROTPROC  TM    DMSFLAG1,TESTINP         IS CONTROL REQUESTED
         BZ    ROT008                   NO, GO
         SPACE
         CALL  DMSTST0
         B     ROT011
ROT008   TM    DMSFUNCT,DMSMONIT        IS IT FOR MONITOR
         BZ    ROT009                   NO, GO
         SPACE
         CALL  DMSMAIN                  MAIN DMS CONTROLLER
         L     R1,DMSVADP               -> TO REAL CONSTANTS
         LTR   R1,R1                    SET ?
         BZ    ROT010                   NO, USE VIRTUAL
         LR    R10,R1                   SET BASE FOR CONST
ROT010   TM    DMSFUNCT,DMSEOJ          TO BE STOPPED ?
         BO    ROTRETRN                 YES, GO
         CALL  DMSWRT0                  OUTPUT DATA SAVE ROUTINE
         CALL  DMSMAIN                  RECALL MAIN CONTROLLER
         L     R10,=V(DMSCONST)         RELOAD THE VIRTUAL BASE
         L     R1,DMSVADP               CHECK FOR REAL PAGE
         LTR   R1,R1                    ALLOCATED ?
         BZ    ROT012                   NO, GO
         LR    R10,R1                   YES, USE REAL CONSTANTS
ROT012   TM    DMSFUNCT,DMSEOJ          IS END OF JOB REQUIRED ?
         BO    ROTRETRN                 YES, RETURN
ROT009   TM    DMSFUNCT,DMSEDT          IS EDIT SELECTED
         BZ    ROT011                   NO, CONTINUE
         SPACE
         CALL  DMSEDIT                  CALL THE MAIN EDITOR
         TM    DMSFUNCT,DMSEOJ          IS END OF JOB ON
         BO    ROTRETRN                 YES, NORMAL RETURN
         SPACE 3
***
*        CHECK FUNCTION RETURN
***
         SPACE
ROT011   TM    DMSFLAG1,NOGO            IS FUNCTION FAILED
         BO    ROTRETRN                 YES, RETURN
         TM    DMSFUNCT,DMSEOJ          IS END OF JOB REQUESTED
         BO    ROTRETRN                 YES, GO TO PROGRAM END
         L     R8,ROTCSCLP              -> COMM ->
         L     R7,0(R8)                 -> COMM ECB
         TM    0(R7),BIT1               COMMAND INPUT
         BZ    ROT006                   NO, GO
         L     R7,4(R8)                 -> CIB
         CLI   CIBVERB,CVSTOP           STOP COMMAND
         BNE   ROT004                   NO, GO
         MESSAGE 008                    STOP COMMAND INPUT
         B     ROTRETRN                 RETURN
ROT004   NI    INPFLAG,255-INPCRDIN     SWITCH OFF CARD INPUT
         B     ROT005                   PROCESS
ROT006   TM    INPFLAG,INPCRDIN         IS CARD INPUT REQUESTED
         BZ    ROT007                   NO, GO
         TM    INPFLAG,INPCARD          IS CARD AVAILABLE
         BZ    ROTRETRN                 NO, STOP PROCESS
         B     ROT007
         SPACE 3
***
*        RETURN
***
         SPACE
ROTRETRN L     R2,PRTDCBP               -> PRINT DCB
         CLOSE ((R2))                   AND CLOSE IT
         L     R2,INPDCBP               -> SYSIN DCB                 RN
         CLOSE ((R2))                   AND CLOSE IT                 RN
         SR    R15,R15                  CLEAR
         IC    R15,DMSRETCD             SET RETURN CODE
         B     ROTRET                   AND RETURN
         SPACE 2
ROTRET   L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14                      RETURN TO OS
         EJECT
***
*        LOCAL CONSTANTS
***
         SPACE
ROTTITLE DC    AL2(TITLEND-ROTTITLE,0)
         DC    C'1'                     NEW PAGE
ROTTEXT  DC    120C' '
         ORG   ROTTEXT+90
         DC    CL06'DATE:'
ROTDATE  DS    8C
         ORG   ROTTEXT+106
         DC    CL06'TIME:'
ROTTIME  DS    8C
TITLEND  EQU   *
         ORG   ROTTEXT+120
ROTSPACE DC    AL2(6,0),C'  '
ROTCSCLP DS    A                        -> COMMUNICATION POINTERS
         LTORG
         END
