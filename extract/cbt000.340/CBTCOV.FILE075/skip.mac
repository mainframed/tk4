         MACRO
&NAME    SKIP  &PAR1
         LCLA  &K
         AIF   ('&PAR1' NE '').OK
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      AIF   ('&PAR1'(1,1) EQ 'X' OR '&PAR1'(1,1) EQ 'C').CHAR
         AIF   (K'&PAR1 GT 3).ER
&K       SETA  K'&PAR1
.TEST    AIF   ('&PAR1'(&K,1) GT '9' OR '&PAR1'(&K,1) LT '0').ER
&K       SETA  &K-1
         AIF   (&K NE 0).TEST
&NAME    L     15,=V(IECKSKPC)         SKIP COUNT ROUTINE ADDRESS
         BALR  14,15                   BRANCH TO THE SKIP ROUTINE
         DC    AL2(&PAR1)
         AGO   .END
.CHAR    AIF   (K'&PAR1 LT 4).ER
&K       SETA  K'&PAR1-3
         AIF   ('&PAR1'(1,1) NE 'X').NOHEX
&K       SETA  (&K+1)/2
.NOHEX   AIF   ('&PAR1'(2,1) NE '''' OR '&PAR1'(K'&PAR1,1) NE '''').ER
&NAME    L     15,=V(IECKSKPS)         SKIP CHARACTER ROUTINE ADDRESS
         BALR  14,15                   BRANCH TO SKIP ROUTINE
         DC    AL2(&K)
         DC    &PAR1
         AGO   .END
.ER      MNOTE 12,'***  IHB002  INVALID SKIP OPERAND SPECIFIED'
.END     MEND
