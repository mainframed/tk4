         MACRO
&NAME    CTRSCHED   &SCHED
         GBLA  &IECCTOT,&IECPCTN
         GBLA  &IECNDGP(100),&IECSCNO
.*             IECNDGP(1) NUMBER OF BYTES COUNTER
.*             IECSCNO   HIGHEST SCHEDULE REFFERRED TO IN CTRGROUPS
         GBLB  &IECSEQ(10),&IECSW(9)
.*             IECSEQ(9)  SEQUENCE INDICATOR
         LCLA  &A,&B,&C,&D
         LCLB  &NOCODE
         LCLC  &CHAR
         AIF   (&IECSW(9)).SEQCHK
         MNOTE 12,'CTRSCHED MACRO NOT VALID BECAUSE PC=YES NOT CODED INX
               CONFIGUR MACRO'
         MEXIT
.SEQCHK  ANOP
         AIF  (&IECSEQ(1) AND &IECSEQ(2) AND &IECSEQ(3) AND &IECSEQ(7) X
               AND &IECSEQ(8) AND NOT&IECSEQ(9) AND NOT&IECSEQ(4)).RRR
.SSS     ANOP
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
         AGO   .YYY
.RRR     ANOP
         AIF   (&IECSEQ(5) OR &IECSEQ(6)).SSS
.YYY     ANOP
         AIF   (&IECCTOT EQ &IECPCTN).CONT
&NOCODE  SETB  1
         MNOTE 12,'HIGH COUNTERS FOR SOME AREA STATIONS HAVE NOT BEEN  X
               DEFINED BY CTRGROUP MACROS'
         MNOTE 12,'ALL HIGHCTR OPERANDS OF ASCTR MACROS MUST BE        X
               ACCURATE'
&A       SETA  N'&SYSLIST
         AIF   (&A GE &IECSCNO).CONT1
         MNOTE 12,'SOME SCHEDULES DEFINED IN CTRGROUP MACROS ARE NOT   X
               DEFINED BY CTRSCHED MACRO'
&NOCODE  SETB  1
.CONT1   ANOP
.CONT    ANOP
         AIF   (N'&SYSLIST LE 15 AND N'&SYSLIST GE 1).OPCHK
&NOCODE  SETB  1
         MNOTE 12,'NUMBER OF SCHED OPERANDS IS NOT IN RANGE OF 1 TO 15'
.OPCHK   ANOP
&A       SETA  N'&SYSLIST
.LOOP1   ANOP
&B       SETA  &B+1
         AIF   (&B GT &A).VAL
         AIF   (T'&SYSLIST(&B) EQ 'N').LOOP1
&CHAR    SETC  T'&SYSLIST(&B)
&NOCODE  SETB  1
         MNOTE 12,'SCHED OPERAND HAS INVALID TYPE ATTRIBUTE = &CHAR'
         AGO   .LOOP1
.VAL     ANOP
&A       SETA  N'&SYSLIST
&B       SETA  0
.VAL1    ANOP
&B       SETA  &B+1
         AIF   (&B GT &A).DEFINE
         AIF ('&SYSLIST(&B)' GE '1' AND '&SYSLIST(&B)' LE '2047').VAL1
&NOCODE  SETB  1
         MNOTE 12,'SCHED OPERAND=&SYSLIST(&B) NOT BETWEEN 1 AND 2047'
         AGO   .VAL1
.DEFINE  ANOP
         AIF   (&NOCODE).NOGEN
&A       SETA  N'&SYSLIST
&IECNDGP(1) SETA &IECNDGP(1)+2*&A
         SPACE 1
         SPACE 1
*                       SCHEDULE TABLE
         SPACE 1
         SPACE 1
IECSCHED EQU   *
&B       SETA  0
.LOOP2   ANOP
&B       SETA  &B+1
&C       SETA  0
&C       SETA  &SYSLIST(&B)
&D       SETA  2048-&C
         AIF   (&B EQ &A).LASTENT
.YES     ANOP
         AIF   (&B EQ 3 OR &B EQ 6 OR &B EQ 9 OR &B EQ 12 OR &B EQ 15).X
               SETBIT
.HIGHBIT ANOP
&D       SETA  &D+32768
         DC    AL2(&D)
         AGO   .LOOP2
.SETBIT  ANOP
&D       SETA  &D+8192
         AGO   .HIGHBIT
.LASTENT ANOP
         AIF   (&B EQ 3 OR &B EQ 6 OR &B EQ 9 OR &B EQ 12 OR &B EQ 15).X
               HIGHB
         AGO   .AFTER
.HIGHB   ANOP
&D       SETA  &D+8192
.AFTER   ANOP
&D       SETA  &D+49152
         DC    AL2(&D)
&IECSEQ(9) SETB 1
         AGO   .MEND
.NOGEN   ANOP
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
.MEND    MEND
