         MACRO
&NAME    LOAD  &EP=,&EPLOC=,&DE=,&DCB=,&HIARCHY=
.*0014000200,004400                                                LCS0
         AIF   ('&HIARCHY' EQ '' OR '&HIARCHY' EQ '0' OR '&HIARCHY' EQ C
               '1').CONTA
         IHBERMAC  195
         MEXIT
.CONTA   AIF   ('&HIARCHY' EQ '').CONT
         CNOP  0,4
.CONT    AIF   ('&EP' NE '').PROCEP     BRANCH IF EP PRESENT
         AIF   ('&EPLOC' EQ '' AND '&DE' EQ '').E1
         AIF   ('&EPLOC' NE '' AND '&DE' NE '').E2
         AIF   ('&DE&EPLOC'(1,1) NE '(').RXTYPE
         AIF   ('&DE(1)&EPLOC(1)' NE '0').NOTZER
&NAME    DS    0H
         AGO   .EPHERE
.NOTZER  ANOP
&NAME    LA    0,0(&EPLOC(1)&DE(1)) LOAD PARAMETER INTO REGISTER ZERO
         AGO   .EPHERE
.RXTYPE  ANOP
&NAME    LA    0,&EPLOC&DE         LOAD PARAMETER INTO REGISTER ZERO
.EPHERE  AIF   ('&DCB' NE '').DCB
.EPOK    AIF   ('&HIARCHY' NE '').FIN
.SRONE   ANOP
         SR    1,1                 SHOW NO DCB PRESENT
.SVC     AIF   ('&DE' EQ '').HIAR
         LNR   0,0   LOAD NEGITIVE OF REG. ZERO TO INDICATE DE
.HIAR    ANOP
         SVC   8                   ISSUE LOAD SVC
         MEXIT
.FIN     ANOP
         L     1,*+8    NO DCB SPECIFIED,HIARCHY SPECIFIED
         AGO   .TESTO
.DCB     AIF   ('&DCB'(1,1) NE '(').RXSPEC
         LA    1,0(&DCB(1))        LOAD ADDR. OF DCB INTO REG. ONE
         AGO   .TESTHY
.RXSPEC  ANOP
         LA    1,&DCB              LOAD ADDR. OF DCB INTO REG. ONE
.TESTHY  AIF   ('&HIARCHY' EQ '').SVC
         O     1,*+8               INSERT HIARCHY PARAMETER
.TESTO   ANOP
         B     *+8   BRANCH AROUND CONSTANT(S)
         AIF   ('&HIARCHY' EQ '1').HONE
         DC    X'01000000'         HIARCHY ZERO SPECIFIED
         AGO   .SVC
.HONE    ANOP
         DC    X'02000000'         HIARCHY ONE SPECIFIED
         AGO   .SVC
.PROCEP  AIF   ('&EPLOC' NE '' OR '&DE' NE '').E2
         AIF   ('&EP'(1,1) EQ '(').E4
         AIF   ('&HIARCHY' NE '').IARCHY
&NAME    LA    0,*+8               LOAD PARAMETER INTO REGISTER ZERO
         B     *+12  BRANCH AROUND CONSTANT(S)
         DC    CL8'&EP'            ENTRY POINT NAME
         AIF   ('&DCB' NE '').NEXTON
         AGO  .SRONE
.NEXT    ANOP
         LA    1,0(&DCB(1))        LOAD ADDR. OF DCB INTO REG. ONE
         AGO   .SVC
.NEXTON  AIF   ('&DCB'(1,1) EQ '(').NEXT
         LA    1,&DCB              LOAD ADDR. OF DCB INTO REG. ONE
         AGO   .SVC
.IARCHY  AIF   ('&DCB' NE '').PREDCB
&NAME    LA    0,*+16              LOAD PARAMETER INTO REGISTER ZERO
         L     1,*+8               NO DCB SPECIFIED,HIARCHY SPECIFIED
         B     *+16  BRANCH AROUND CONSTANT(S)
         AIF   ('&HIARCHY' EQ '1').HIAR1
         DC    X'01000000'         HIARCHY ZERO SPECIFIED
         AGO   .EP
.HIAR1   ANOP
         DC    X'02000000'         HIARCHY ONE SPECIFIED
.EP      ANOP
         DC    CL8'&EP'            ENTRY POINT NAME
         AGO   .SVC
.PREDCB  ANOP
&NAME    LA    0,*+20              LOAD PARAMETER INTO REGISTER ZERO
         AIF   ('&DCB'(1,1) EQ '(').OKAY
         LA    1,&DCB              LOAD ADDR. OF DCB INTO REG. ONE
         AGO  .PAST
.OKAY    ANOP
         LA    1,0(&DCB(1))   LOAD ADDR. OF DCB INTO REG. ONE
.PAST    ANOP
         O     1,*+8               GENERATE HIARCHY PARAMETER
         B     *+16  BRANCH AROUND CONSTANT(S)
         AIF   ('&HIARCHY' EQ '1').PASS
         DC    X'01000000'         HIARCHY ZERO SPECIFIED
         AGO   .PASSES
.PASS    ANOP
         DC    X'02000000'         HIARCHY ONE SPECIFIED
.PASSES  ANOP
         DC    CL8'&EP'            ENTRY POINT NAME
         AGO   .SVC
.E1      IHBERMAC 31,345
         MEXIT
.E2      IHBERMAC 88,345
         MEXIT
.E4      IHBERMAC  26
         MEND
