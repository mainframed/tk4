         MACRO
&NAME    MODE  &PAR1,&PAR2,&WRT60=
         GBLB  &SHDR,&RHDR
         LCLA  &K
         LCLB  &NOGO
         LCLC  &TEMP
.* 055000,431000-442000                                          A29188
         AIF   (&SHDR OR &RHDR).SEQ
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW SENDHDR OR RX
               CVHDR MACRO'
&NOGO    SETB  1
.SEQ     AIF   ('&PAR1' NE '').OK
         MNOTE 12,'***  IHB001  TYPE OPERAND REQ''D-NOT SPECIFIED'
&NOGO    SETB  1
.OK      AIF   ('&PAR2' EQ '').NB
         AIF   (K'&PAR2 LT 4 OR K'&PAR2 GT 7 OR '&PAR2'(K'&PAR2,1) NE 'X
               ''').ER
         AIF   (K'&PAR2 EQ 4 AND '&PAR2'(1,2) EQ 'C''').NB
         AIF   (K'&PAR2 LT 6 AND '&PAR2'(1,2) EQ 'X''').NB
         AIF   (K'&PAR2 GT 5 AND '&PAR2'(1,4) EQ 'XL1''').NB
         AIF   (K'&PAR2 EQ 6 AND '&PAR2'(1,4) EQ 'CL1''').NB
.ER      MNOTE 12,'***  IHB002  INVALID CHARACTER OPERAND SPECIFIED'
         MEXIT
.NB      AIF   (&NOGO).END
         AIF   ('&PAR1' NE 'PRIORITY').INIT
&NAME    L     1,=V(IECKPRTY)          ADDRESS OF PRIORITY ROUTINE
         AGO   .GO
.INIT    AIF   ('&PAR1' NE 'INITIATE').CONV
&NAME    L     1,=V(IECKNATE)          ADDRESS OF INITIATE ROUTINE
         AGO   .GO
.CONV    AIF   ('&PAR1' NE 'CONVERSE').MOD
&NAME    L     1,=V(IECKCVRS)          ADDRESS OF CONVERSE ROUTINE
         AGO   .GO
.MOD     AIF   ('&PAR1' NE 'MOD2260').USER
         AIF   ('&WRT60' NE '').IS
         MNOTE 12,'***  IHB001  WRT60 OPERAND REQ''D-NOT SPECIFIED'
         MEXIT
.IS      AIF   ('&WRT60' EQ '1' OR '&WRT60' EQ '2').OK1
         MNOTE 12,'***  IHB002  INVALID WRT60 OPERAND SPECIFIED &WRT60'
         MEXIT
.OK1     ANOP
&K       SETA  12
         AIF   ('&WRT60' EQ '2').MOVE
&K       SETA  14
.MOVE    ANOP
&TEMP    SETC  '&NAME'
         AIF   ('&PAR2' EQ '').MUVIT
&TEMP    SETC  'IHB'.'&SYSNDX'
&NAME    LA    1,&TEMP    RETURN ADDRESS IF CONDITION MET
         L     15,=V(IECKMODE)   ADDRESS OF MODE ROUTINE
         BALR  14,15   RETURN ON REGISTER 14 IF CONDITIONS NOT MET
         DC    XL2'1'
         DC    &PAR2
         DC    C' '
         B     *+8
.MUVIT   ANOP
&TEMP    MVI   4(4),&K
         MEXIT
.USER    ANOP
&NAME    L     1,=A(&PAR1)             ADDRESS OF USER SUPPLIED RT.
.GO      AIF   ('&PAR2' NE '').COND
         BALR  14,1                    LINK
         AIF   ('&PAR1' NE 'PRIORITY').DUM
         DC    X'00010000'             FIELD LENGTH AND SPACER
         AGO   .END
.DUM     DC    X'00000000'         DUMMY
         AGO   .END
.COND    L     15,=V(IECKMODE)         ADDRESS OF THE MODE ROUTINE
         BALR  14,15                   LINK
         DC    XL2'1'              FIELD SIZE
         DC    &PAR2               SC HEADER CHAR
         DC    C' '
.END     MEND
