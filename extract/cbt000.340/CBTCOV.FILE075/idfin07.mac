         MACRO
         IDFIN07
         COPY IDFGBL
         AIF   (NOT &PIB(48)).TR
         MNOTE *,'IDF100 IN TRACE MODE ENTERING IDFIN07'
.TR      ANOP
         AIF   (&PIB(11)).INDFX
         AIF   (&PIB(2)).FLD10
         AIF   (&PIB(12)).INDFX
.**********************************************************************
.*                                                                    *
.*                           CTR SINK GENERATOR                       *
.*                                                                    *
.**********************************************************************
&PIA(6)  SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18
&FB(22)  SETB  1
&FB(23)  SETB  1
&FB(24)  SETB  0
&FB(25)  SETB  0
&FB(26)  SETB  0
&FB(27)  SETB  0
&FB(28)  SETB  0
&N       SETA  1
&K       SETA  5
.FLD7    AIF   (&DSKB1(&K+50) OR &DSKB1(&K+75)).FLD9
         AIF   (NOT (&DSKB1(&K) AND &DSKB1(&K+100) AND                 X
               &DSKB1(&K+125))).FLD9
         AIF   (NOT &FB(28)).FLD8
         IDFASM (,4671)
&A(1)    SETA  &J-1
         IDFASM A
.FLD8    ANOP
&PIA(2)  SETA  4
&J       SETA  &DSKA(&K)
&PIB(1)  SETB  1
&MB(8)   SETB  1  SUPPRESS HEADING MNOTE
         AIF   (NOT &PIB(1)).I0245
         IDFMSG1 123
.I0245   ANOP ,
&PIB(1)  SETB  (NOT &CTRB(&J+1) AND &PIB(10))
         AIF   (NOT &PIB(1)).I0246
         IDFMSG 025
.I0246   ANOP ,
&PIB(1)  SETB  (&CTRB(&J+1) AND &CTRB(&J+64))
         AIF   (NOT &PIB(1)).I0247
         IDFMSG 021
.I0247   ANOP ,
&CTRB(&J+22) SETB (&CTRB(&J+1) AND &CTRB(&J+22) OR NOT &CTRB(&J+1)     X
               AND &PIB(10))
&CTRB(&J+43) SETB (&CTRB(&J+1) AND &CTRB(&J+43))
&CTRB(&J+64) SETB (&CTRB(&J+64) OR NOT &CTRB(&J+1))
&CTRB(&J+1) SETB 1
&CTRB(1) SETB  1
&FB(28)  SETB  1
.FLD9    ANOP
&N       SETA  &N+1
&K       SETA  &K+5
         AIF   (&N LE 5).FLD7
.FLD10   ANOP
.**********************************************************************
.*                                                                    *
.*             CTR (FIELD) OPERAND PROCESSOR                          *
.*                                                                    *
.**********************************************************************
&M       SETA  18                       POINT TO CTR
&PIB(1)  SETB  (&PIB(2) AND &PIA(10) NE &M AND N'&SYSLIST(&M) GT 0)
         AIF   (NOT &PIB(1)).I0248
         IDFMSG1 100     CONTINUATION MODE, CTR OPERAND IGNORED
.I0248   AIF   (&PIB(2) AND &PIA(10) NE &M).FLD11
&PIB(1)  SETB  (&PIB(2) AND N'&SYSLIST(&M) EQ 0)
         AIF   (NOT &PIB(1)).I0249
         IDFMSG1 102    EXPECTED CONTINUATION NOT FOUND
.I0249   ANOP ,
&PIB(15) SETB  (&PIB(1) OR &PIB(15))    ACCUMULATE ERROR
&PIB(2)  SETB  (&PIB(2) AND N'&SYSLIST(&M) GT 0)   CONTINUE CURRENT OP
         AIF   (N'&SYSLIST(&M) EQ 0).CTRFX
         AIF   (NOT &B(20)).I0249A                                FSJW
&PIB(1)  SETB  1                                                  FSJW
         IDFMSG1 101                                              FSJW
         AGO   .FLD11                                             FSJW
.I0249A  ANOP  ,                                                  FSJW
&PIB(1) SETB (&B(13))
         AIF   (NOT &PIB(1)).I0250
         IDFMSG3 540
.I0250   ANOP ,
         AIF   (&PIB(1)).CTRFX
&PIB(1) SETB ('&SYSLIST(&M)'(1,1) NE '(')
         AIF   (NOT &PIB(1)).I0251
         IDFMSG2 200
.I0251   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))    ERROR, TERMINATE ASM
&PIB(2)  SETB  (&PIB(2) AND NOT &PIB(1))
         AIF   (&PIB(1)).CTRFX          EXIT IF ERROR
&N       SETA  1                        INITIALIZE &N, SUBOPERAND NO.
&I       SETA  2                        OPERAND CHARACTER POINTER
.CTRF1   ANOP  , LOOP TO SEARCH FOR INNER GROUP
&B(8)    SETB  ('&SYSLIST(&M)'(&I,1) EQ '(' OR '&SYSLIST(&M)'(&I,1)    *
               EQ ',')                  TEST FOR GROUP OR NULL
&N       SETA  &N+1                     ADVANCE SUBOPERAND INDEX
&I       SETA  &I+1+K'&SYSLIST(&M,&N)   ACCUM NUMBER CHARACTER
         AIF   (&I LE K'&SYSLIST(&M) AND NOT &B(8)).CTRF1
&A(4)    SETA  K'&SYSLIST(&M)-&B(8)     FINAL SEARCH LIMIT
&A(3)    SETA  &A(4)+&B(8)*(K'&SYSLIST(&M,1)+1-&A(4)) INTERMEDIATE
&I       SETA  1+&B(8)
&N       SETA  1                        POINT TO FIRST SUBOP
         AIF   (&PIB(2)).CTRF2
&PIA(10) SETA  &M                       ESTABLISH OPERAND BEING CONT'D
&A(5)    SETA  1                        SET LOWER LIMIT
&A(6)    SETA  21                       SET UPPER LIMIT
&FB(28)  SETB  (&FB(15))
.CTRF2   ANOP  , HEAD OF SUBOPERAND LOOP
&PIB(2)  SETB  0
         AIF   (&I GT &A(3)).CTRF8
         AIF   (NOT &FB(28)).CTRF3
&PIA(6)  SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18
&A(1)    SETA  64*&FB(22)+32*&FB(23)+16*&FB(24)+8*&FB(25)+4*&FB(26)+2*&X
               FB(27)+&FB(28)
         IDFASM A
&A(1)    SETA  &J-1
         IDFASM A
.CTRF3   ANOP
&B(1)    SETB  ('&SYSLIST(&M)'(&I,1) EQ '(') INNER FRAME
&I       SETA  &I+&B(1)                 SKIP INNER FRAME IF ANY
&C(1)    SETC  '0000000'                INITIALIZE DIGIT COLLECTOR
&C(2)    SETC  '&SYSLIST(&M)'(&I,1)     EXTRACT FIRST CHARACTER
.CTRF4   ANOP
&PIB(1)  SETB  ('&C(2)' LT '0' OR '&C(2)' GT '9')
         AIF   (NOT &PIB(1)).I0252
         IDFMSG2 214
.I0252   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))    ACCUMLATE ERROR
         AIF   (&PIB(1)).CTRF8          NEXT SUBOP IF ERROR
&C(1)    SETC  '&C(1)'(2,6).'&C(2)'     CONCATENATE DIGIT
&I       SETA  &I+1                     INCREMENT POINTER
&C(2)    SETC  '&SYSLIST(&M)'(&I,1)     EXTRACT NEXT CHARACTER
         AIF   ('&C(2)' NE ',' AND '&C(2)' NE ')' AND '&C(1)'(1,1) EQ  X
               '0').CTRF4               TEST FOR COMPLETION
&A(1)    SETA  &C(1)                    READY CTR NUMBER FOR ASSEMBLY
&PIB(1)  SETB  (&A(1) LT &A(5) OR &A(1) GT &A(6))     RANGE TEST
         AIF   (NOT &PIB(1)).I0254
         IDFMSG2 201                    OUT OF RANGE
.I0254   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))    ACCUMULATE ERROR
         AIF   (&PIB(1)).CTRF8          NEXT SUBOP IF ERROR
&J       SETA  &A(1)                    HOLD COUNTER NUMBER FOR CTRB
&I       SETA  &I+1                     PASS DELIMITER
         AIF   (&I+2 GT &A(3)).CTRF7
&C(1)    SETC  '&SYSLIST(&M)'(&I,3)
&FB(22)  SETB  0
&FB(23)  SETB  ('&C(1)' EQ 'ADD' OR '&C(1)' EQ '''+''')
&FB(24)  SETB  ('&C(1)' EQ 'SUB' OR '&C(1)' EQ '''-''')
&FB(25)  SETB  ('&C(1)' EQ 'MPY' OR '&C(1)' EQ '''*''')
&FB(26)  SETB  ('&C(1)' EQ 'DIV' OR '&C(1)' EQ '''/''')
&FB(27)  SETB  ('&C(1)' EQ 'DVR')
         AIF   (&FB(23) OR &FB(24) OR &FB(25) OR &FB(26) OR            X
               &FB(27)).CTRF5
         AIF   (&I+3 GT &A(3)).CTRF7
&FB(27)  SETB  ('&SYSLIST(&M)'(&I,4) EQ '''/+''')
         AIF   (NOT &FB(27)).CTRF7
&I       SETA  &I+1
.CTRF5   ANOP
&I       SETA  &I+3
         AIF   (&I+5 GT &A(3)).CTRF6    IF NOT NOISE, MAY BE ERROR
         AIF   ('&SYSLIST(&M)'(&I,6) NE ',FIELD').CTRF6
&I       SETA  &I+6
.CTRF6   AIF   (&I LT &A(3) AND '&SYSLIST(&M)'(&I,1) NE ')').CTRF7
&PIB(1)  SETB  (&I LT &A(3))            FINDS EXCESS CHARACTERS
         AIF   (NOT &PIB(1)).I0255
         IDFMSG1 105                    EXCESS CHARACTERS IGNORED
.I0255   ANOP ,
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0256
         IDFMSG 019
.I0256   ANOP ,
&PIB(1)  SETB  (NOT &CTRB(&J+1))
         AIF   (NOT &PIB(1)).I0257
         IDFMSG 025
.I0257   ANOP ,
&PIB(1)  SETB  (&CTRB(&J+1) AND &CTRB(&J+64))
         AIF   (NOT &PIB(1)).I0259
         IDFMSG 021
.I0259   ANOP ,
&CTRB(&J+64) SETB 1
&CTRB(&J+43) SETB (NOT &CTRB(&J+1) OR &CTRB(&J+1) AND &CTRB(&J+43))
&CTRB(&J+22) SETB (&CTRB(&J+1) AND &CTRB(&J+22) OR NOT &CTRB(&J+1) AND *
               &PIB(10))
&CTRB(&J+1) SETB 1
&CTRB(1) SETB  1
&FB(28)  SETB  (&A(3) LT &A(4) AND &A(3)+N'&SYSLIST(&M)-&N+1 LE &A(4))
         AIF   (&B(10) OR &B(11) OR &B(12) OR                          *
               NOT &FB(1) AND NOT &FB(2) AND &FB(3)).CTRF6A
&FB(1)   SETB  0
&FB(2)   SETB  0
&FB(3)   SETB  1
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0261
         IDFMSG3 541
.I0261   ANOP ,
&PIA(6)  SETA  &FA(1)
&A(1)    SETA  64*&FB(1)+32*&FB(2)+16*&FB(3)+8*&FB(4)+4*&FB(5)+2*&FB(6)X
               +&FB(7)
         IDFASM A
.CTRF6A  AIF   (&FB(15)).CTRF8
&PIA(6)  SETA  &FA(2)+&FB(6)*(2+(&FA(2)-&FA(2)/486*486)/478*18)
&FB(15)  SETB  1
&A(1)    SETA  64*&FB(15)+32*&FB(16)+16*&FB(17)+8*&FB(18)+4*&FB(19)+2*&X
               FB(20)+&FB(21)
         IDFASM A
         AGO   .CTRF8
.CTRF7   ANOP
&PIB(15) SETB  1
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0262
         IDFMSG3 542
.I0262   ANOP ,
.CTRF8   ANOP
&N       SETA  &N+1                     INCREMENT SUBOPERAND INDEX
&I       SETA  &A(3)+2                  INCREMENT OPERAND POINTER
&A(3)    SETA  &A(3)+1+K'&SYSLIST(&M,&N) SET NEW SHORT SCAN STOP
&PIB(2)  SETB  (&N EQ N'&SYSLIST(&M) AND '&SYSLIST(&M,&N)' EQ 'C')
         AIF   (&I LT &A(4) AND NOT &PIB(2)).CTRF2
&FB(28)  SETB  (&FB(28) OR &PIB(2))
         AIF   (&PIB(2)).FLD11
.CTRFX   AIF   (NOT &FB(15)).FLD11
&FB(28)  SETB  0                       NO MORE CTR OPNS
&PIA(6)  SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18
&A(1)    SETA  64*&FB(22)+32*&FB(23)+16*&FB(24)+8*&FB(25)+4*&FB(26)+2*&X
               FB(27)+&FB(28)
         IDFASM A
&A(1)    SETA  &J-1
         IDFASM A
.FLD11   ANOP
.**********************************************************************
.*                                                                    *
.*                  INDICATOR OPERAND (FDFIELD)                       *
.*                                                                    *
.**********************************************************************
&M       SETA  19                       POINT TO IND OPERAND
&PIB(1)  SETB  (&PIB(2) AND &PIA(10) NE &M AND N'&SYSLIST(&M) GT 0)
.* IN CONTINUATION MODE WITH OPERAND DIFFERENT THAN IND, IND IS CODED
         AIF   (NOT &PIB(1)).I0264
         IDFMSG1 100     CONTINUATION MODE, IND OPERAND IGNORED
.I0264   ANOP ,
         AIF   (&PIB(2) AND &PIA(10) NE &M).INDFX CONT PREVIOUS OPERAND
&PIB(1)  SETB  (&PIB(2) AND N'&SYSLIST(&M) EQ 0)   IND IS CONT'D
         AIF   (NOT &PIB(1)).I0265
         IDFMSG1 102    EXPECTED CONTINUATION NOT FOUND
.I0265   ANOP ,
&PIB(15) SETB  (&PIB(1) OR &PIB(15))    ACCUMULATE ERROR
&PIB(2)  SETB  (&PIB(2) AND N'&SYSLIST(&M) GT 0)   CONTINUE CURRENT OP
         AIF   (N'&SYSLIST(&M) EQ 0).INDFX
         AIF   (NOT &B(20)).I0265A                                FSJW
&PIB(1)  SETB  1                                                  FSJW
         IDFMSG1 101                                              FSJW
         AGO   .INDFX                                             FSJW
.I0265A  ANOP  ,                                                  FSJW
&PIB(1)  SETB  (&B(10) OR &B(13))
         AIF   (NOT &PIB(1)).I0266
         IDFMSG3 543
.I0266   ANOP ,
         AIF   (&PIB(1)).INDFX
&N       SETA  0                        INITIALIZE &N, SUBOPERAND NO.
&PIB(1)  SETB  ('&SYSLIST(&M)'(1,1) NE '(')
         AIF   (NOT &PIB(1)).I0267
         IDFMSG2 200
.I0267   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))    ERROR, TERMINATE ASM
&PIB(2)  SETB  (&PIB(2) AND NOT &PIB(1))
         AIF   (&PIB(1)).INDFX          EXIT IF ERROR
&N       SETA  0
&I       SETA  2              SCRATCH VARIABLE USED FOR INDEXING
.INDF1   ANOP  , LOOP TO SEARCH FOR INNER GROUPING
&B(8)    SETB  ('&SYSLIST(&M)'(&I,1) EQ '(' OR '&SYSLIST(&M)'(&I,1)    *
               EQ ',')                  TEST FOR INNER GROUP OR NULL
&N       SETA  &N+1                     ADVANCE SUBOPERAND INDEX
&I       SETA  &I+1+K'&SYSLIST(&M,&N)   ACCUM NUMBER CHARACTER
         AIF   (&I LT K'&SYSLIST(&M) AND NOT &B(8)).INDF1
&N       SETA  1                        POINT TO FIRST SUBOP
&I       SETA  1+&B(8)                  INITIAL SCAN POSITION
&A(4)    SETA  K'&SYSLIST(&M)-&B(8)
&A(3)    SETA  &A(4)+&B(8)*(K'&SYSLIST(&M,1)+1-&A(4))
         AIF   (&PIB(2)).INDF2         APPROPRIATE VALUES ALREADY SET
&PIA(10) SETA  &M                      TO FLAG WHICH OPND IS CONT'D
&A(5)    SETA  1                       LOWER LIMIT FOR IND NUMBER
&A(6)    SETA  84                      UPPER LIMIT FOR SAME
&B(17)   SETB  0                        LAST TIME SWITCH OFF      FSDB
&FB(16)  SETB  0                       FLAG IN FUNCTION BYTE
&FB(26)  SETB  0                       CHAIN BIT IN IND CTRL BYTE
&B(1)    SETB  0                       TENTATIVE VALUE FOR CHAIN BIT
&FB(27)  SETB  0                       AND BIT IN IND CTRL BYTE
&B(2)    SETB  0                       TENTATIVE VALUE FOR SAME
&FB(28)  SETB  0                       OR BIT IN IND CTRL BYTE
&B(3)    SETB  0                       TENTATIVE VALUE FOR SAME
&B(4)    SETB  (NOT &FB(1) AND NOT &FB(2) AND &FB(3))
.*                                     SIGNALS NUMERIC COMPARISONS
&B(5)    SETB  0                       SIGNALS CHARACTER COMPARISONS
&TB(3)   SETB  (&DSCB(55))
&TB(5)   SETB  (&DSCB(65))
.INDF2   AIF   (&I GT &A(3)).INDF18    BRANCH IF SUBOP OMMITTED
&B(8)    SETB  ('&SYSLIST(&M)'(&I,1) EQ '(')
&I SETA &I+&B(8)                       SKIP OVER LEADING APOSTROPHE
.INDF2A  AIF   (NOT (&FB(26) OR &FB(27) OR &FB(28))).INDF5
.*                                     SKIP IF NONE PREVIOUS
         AIF   (&FB(26) EQ &B(1) AND &FB(27) EQ &B(2) AND &FB(28)      X
               EQ &B(3)).INDF3         SKIP IF WE LUCKED OUT ON GUESS
&PIA(6)  SETA  &FA(3)
&A(1)    SETA  64*&FB(22)+32*&FB(23)+16*&FB(24)+8*&FB(25)+4*&FB(26)+2*&X
               FB(27)+&FB(28)
         IDFASM A                      MAKE THE PATCH
.INDF3   ANOP
&A(1)    SETA  &FA(3)+4                TAKE DIFFERENCES TO CALCULATE
&A(1)    SETA  &PIA(3)+&PIA(4)-&A(1)-(&PIA(3)/486-&A(1)/486)*18
&A(1)    SETA  &A(1)/2                 ACTUAL NUMBER OF CHARACTERS
&PIB(1)  SETB  (&A(1) LT 1 OR &A(1) GT 127)
         AIF   (NOT &PIB(1)).I0268
         IDFMSG3 544
.I0268   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).INDF16
         AIF   (&A(1) EQ &A(2)).INDF4  SKIP IF LUCKY GUESS
&PIA(6)  SETA  &FA(3)+2+(&FA(3)-&FA(3)/486*486)/478*18
&A(2)    SETA  &A(1)
         IDFASM A
.INDF4   AIF   (NOT &FB(26)).INDF7     SKIP IF AND OR OR
&PIA(6)  SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18
&A(1)    SETA  &J-1
&A(1)    SETA  16*&A(1)-&A(1)/7*111
         IDFASM A                      ASSEMBLE THE INDICATOR BYTE
.INDF4A  AIF   (&J NE 890).INDF5        NOT A BRANCH              FSDB
&K       SETA  &QA4(1)                  GET FREEQ PTR             FSDB
         AIF   (&K GT 0).INDF4B         FREEQ NOT EMPTY           FSDB
&PIB(1)  SETB  1                        ISSUE MESSAGE             FSDB
         IDFMSG3 510                    TOO MANY BRANCHES         FSDB
&PIB(15) SETB  1                        SET ERROR                 FSDB
         AGO   .INDFX                   EXIT                      FSDB
.INDF4B  ANOP  ,                                                  FSDB
&J       SETA  &QA2(1)                  GET TEMPQ PTR             FSDB
&QC(&K)  SETC  '&C(7)'                  QUEUE TARGET NAME         FSDB
&L       SETA  0                        L FOLLOWS J               FSDB
.INDF4C  AIF   (&J EQ 0).INDF4D         NEW IS FIRST OR LAST      FSDB
         AIF   ('&QC(&J)' GE '&QC(&K)').INDF4D INSERT HERE?       FSDB
&L       SETA  &J                       SAVE THIS PTR             FSDB
&J       SETA  &QA1(&J)/65536           CHAIN TO NEXT             FSDB
         AGO   .INDF4C                                            FSDB
.INDF4D  ANOP  ,                                                  FSDB
&QA4(1)  SETA  &QA1(&K)/65536           UPDATE FREEQ PTR          FSDB
&QA1(&K) SETA  &J*65536                 CHAIN NEW TO FIRST        FSDB
         AIF   (&L EQ 0).INDF4E                                   FSDB
&QA1(&L) SETA  &K*65536                 CHAIN PREVIOUS TO NEW     FSDB
         AGO   .INDF4F                                            FSDB
.INDF4E  ANOP  ,                                                  FSDB
&QA2(1)  SETA  &K                       POINT TEMPQ TO NEW FIRST  FSDB
.INDF4F  ANOP  ,                                                  FSDB
&QA2(&K) SETA  &PIA(6)                  QUEUE LOCATION OF BR ADDR FSDB
         IDFASM (,4670)                 GEN NO OPS TO RESERVE     FSDB
         IDFASM (,4670)                 SPACE FOR TARGET ADDR     FSDB
&PIB(39) SETB  1                        INVOKE IND PART 2         FSDB
&PIB(10) SETB  1                        SET CONDITIONAL CODE      FSDB
.INDF5   AIF   (&B(17)).INDFX           LAST SUBOP?               FSDB
&C(1)    SETC  '0000000'                INITIALIZE DIGIT COLLECTOR
&C(2)    SETC  '&SYSLIST(&M)'(&I,1)     EXTRACT FIRST CHARACTER
         AIF   ('&C(2)' GE '0' AND '&C(2)' LE '9' OR '&C(2)' EQ ',' OR C
               '&C(2)' EQ ')').INDF6    NOT A TARGET              FSDB
&C(1)    SETC  ''                       INITIALIZE TARGET         FSDB
&A(1)    SETA  0                    SEE NOTE BELOW ON IPK089
.INDF5A  ANOP  ,                                                  FSDB
&C(1)    SETC  '&C(1)'.'&C(2)'          APPEND CHAR               FSDB
&I       SETA  &I+1                     POINT TO NEXT CHAR        FSDB
&C(2)    SETC  '&SYSLIST(&M)'(&I,1)     PICK UP CHAR              FSDB
&A(1)    SETA  &A(1)+1              SEE NOTE BELOW ON IPK089
         AIF   ('&C(2)' NE ',' AND '&C(2)' NE ')').INDF5A END?    FSDB
.** THE FOLLOWING SEVEN CARDS WERE USED TO CONCATENATE
.** BLANKS BECAUSE OF A PROBLEM WITH DOS/VS (IPK089)
.** -- SETC SYMBOL GREATER THAN 8 CHARACTERS.
         AIF   (&A(1) GE 8).INDF5B
&A(2)    SETA  8-&A(1)
&C(7)    SETC  '&C(1)'(1,&A(1)).'       '(1,&A(2))
         AGO   .INDF5C
.INDF5B  ANOP
&C(7)    SETC  '&C(1)'(1,8)
.INDF5C   ANOP
&J       SETA  890                      IND 7F MEANS BRANCH       FSDB
&I       SETA  &I+1                     POINT TO NEXT CHAR        FSDB
&B(18)   SETB  (&DSKB1(5) AND NOT(&DSKB1(55) OR &DSKB1(80)) AND        C
               &DSKB1(105) AND &DSKB1(130)) CTR SINK?             FSDB
         AIF   (&PIB(39) OR N'&SYSLIST(3) LE 0 AND NOT(&DSCB(35) AND   C
               &DSCB(40)) AND NOT &B(18)).INDF7 TARGET,CTR,SELFGENFSDB
         MNOTE 0,'IDF463 SELFCHK GENERATE AND CTR OPERATIONS'     FSDB
         MNOTE *,'       ARE PERFORMED BEFORE IND BRANCHING'      FSDB
         AGO   .INDF7                   GO TEST FOR END           FSDB
.INDF6   ANOP
&PIB(1)  SETB  ('&C(2)' LT '0' OR '&C(2)' GT '9')
         AIF   (NOT &PIB(1)).I0269
         IDFMSG2 215
.I0269   ANOP ,
         AIF   (&PIB(1)).INDF16        ON ERROR, SKIP TO RESET FLAGS
&C(1)    SETC  '&C(1)'(2,6).'&C(2)'     CONCATENATE DIGIT
&I       SETA  &I+1                     INCREMENT POINTER
&C(2)    SETC  '&SYSLIST(&M)'(&I,1)     EXTRACT NEXT CHARACTER
         AIF   ('&C(2)' NE ',' AND '&C(2)' NE ')' AND '&C(1)'(1,1) EQ  X
               '0').INDF6              TEST FOR COMPLETION
&J       SETA  &C(1)                    SET IND NUMBER
&PIB(1)  SETB  (&J LT &A(5) OR &J GT &A(6))
         AIF   (NOT &PIB(1)).I0270
         IDFMSG2 201                    OUT OF RANGE
.I0270   ANOP ,
         AIF   (&PIB(1)).INDF16        ON ERROR, SKIP TO RESET FLAGS
&I       SETA  &I+1                     PASS DELIMITER
.INDF7   AIF   (&I GT &A(3)).INDF14     START SEARCH FOR COMPARISON OP
&PIB(2)  SETB  ((&FB(27) OR &FB(28)) AND &I EQ &A(4)-1 AND             X
               '&SYSLIST(&M)'(&I,1) EQ 'C')
         AIF   (&PIB(2)).INDFX         BRANCH IF CONTINUATION
         AIF   (&I+5 GT &A(3)).INDF8
         AIF   ('&SYSLIST(&M)'(&I,6) NE 'FIELD,').INDF8
&I       SETA  &I+6                    SKIP THE NOISE-WORD
.INDF8   AIF   (&I GT &A(3)).INDF14    BRANCH IF CHARACTERS EXHAUSTED
&B(8)    SETB  ('&SYSLIST(&M)'(&I,1) EQ '''')    TEST APOSTROPHE
&I       SETA  &I+&B(8)                 SKIP APOSTROPHE IF PRESENT
         AIF   (&I+2 GT &A(3)).INDF14  SKIP IF INSUFFICIENT CHARS
&C(1)    SETC  '&SYSLIST(&M)'(&I,3)
&FB(22)  SETB  ('&C(1)' EQ 'LT,' OR '&C(1)' EQ '<'',' OR '&C(1)' EQ    X
               'GE,' OR '&C(1)' EQ 'NL,')
&FB(23)  SETB  ('&C(1)' EQ 'EQ,' OR '&C(1)' EQ '='',' OR '&C(1)' EQ    X
               'NE,')
&FB(24)  SETB  ('&C(1)' EQ 'GT,' OR '&C(1)' EQ '>'',' OR '&C(1)' EQ    X
               'LE,' OR '&C(1)' EQ 'NG,')
&FB(25)  SETB  ('&SYSLIST(&M)'(&I,1) EQ 'N' OR '&SYSLIST(&M)'(&I,1) EQ X
               '^' OR '&SYSLIST(&M)'(&I+1,1) EQ 'E')
&FB(25)  SETB  ('&SYSLIST(&M)'(&I+1,1) EQ '=' OR &FB(25))
         AIF   (&I+3 GT &A(3) OR &FB(22) OR &FB(23) OR &FB(24)).INDF9
&C(1)    SETC  '&SYSLIST(&M)'(&I,4)    TRY FOR THE 4-CHAR OPERATORS
&FB(22)  SETB  ('&C(1)' EQ '>='',' OR '&C(1)' EQ '^<'',')
&FB(23)  SETB  ('&C(1)' EQ '^='',')
&FB(24)  SETB  ('&C(1)' EQ '<='',' OR '&C(1)' EQ '^>'',')
&I       SETA  &I+1                    ASSUME ALL IS OKAY
.INDF9   AIF   (NOT (&FB(22) OR &FB(23) OR &FB(24))).INDF14
&I       SETA  &I+3                    ADVANCE OVER COMPARISON OP
&PIA(6)  SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18 ADV LOCCTR TO HI BYTE
&FA(3)   SETA  &PIA(6)                 SAVE IND CTRL LOCATION
         AIF   (&B(4)).INDF9A          BRANCH IF NUMERIC COMPND REQ'D
         AIF   (&B(5)).INDF11          OR CHARACTER COMPND REQ'D
         AIF   ('&SYSLIST(&M)'(&I,1) EQ '''').INDF11       OR FOUND
.INDF9A  ANOP
&B(6)    SETB  ('&SYSLIST(&M)'(&I,1) EQ '-')
&B(7)    SETB  ('&SYSLIST(&M)'(&I,1) EQ '+')
&I       SETA  &I+&B(6)+&B(7)          POINT TO THE FIRST DIGIT
         AIF   (&A(3)-&I LE 0).INDF15  INVALID COMPARAND
.*                                     MAKE A GUESS ABOUT WHAT FOLLOWS
&B(8)    SETB  (NOT (&FB(27) OR &FB(28)) OR &I+&A(2)+5 GT &A(3))
&A(2)    SETA  &A(2)+&B(8)*(&A(3)-&I-&A(2)+&B(6))
&B(2)    SETB  (&I+&A(2)+5 LE &A(3))
&B(3)    SETB  0
&B(1)    SETB  (NOT &B(2) AND &A(3) LT &A(4) AND                       X
               &A(3)+N'&SYSLIST(&M)-&N+1 LT &A(4))
.*                                     PLUG IN THE GUESSES
&FB(26)  SETB  (&B(1))
&FB(27)  SETB  (&B(2))
&FB(28)  SETB  (&B(3))
&A(1)    SETA  64*&FB(22)+32*&FB(23)+16*&FB(24)+8*&FB(25)+4*&FB(26)+2*&X
               FB(27)+&FB(28)
         IDFASM A                      ASSEMBLE IND CTRL BYTE
&A(1)    SETA  &A(2)-&A(2)/128*128
&A(2)    SETA  &A(1)                   RECALCULATE, MODULO 128
         IDFASM A                      AND ASSEMBLE THE COUNT
&C(2)    SETC  '&SYSLIST(&M)'(&I,1)
         AIF   (NOT &B(6)).INDF10
         IDFASM (,427D)                GENERATE A MINUS, IGNORE A PLUS
.INDF10  AIF   ('&C(2)' LT '0' OR '&C(2)' GT '9').INDF15
&A(1)    SETA  48+&C(2)                X'3N' IS DECIMAL N
         IDFASM A                      ASSEMBLE THE DIGIT
&I       SETA  &I+1
&C(2)    SETC  '&SYSLIST(&M)'(&I,1)
         AIF   ('&C(2)' NE ',' AND '&C(2)' NE ')').INDF10  REPEAT
         AGO   .INDF12                 BRANCH TO CHECK FOR LOGICAL OP
.INDF11  AIF   ('&SYSLIST(&M)'(&I,1) NE '''').INDF15
&I       SETA  &I+1
.*                                     MAKE A GUESS ABOUT WHAT FOLLOWS
&B(8)    SETB  (NOT (&FB(27) OR &FB(28)) OR &I+&A(2)+11 GT &A(3))
&A(2)    SETA  &A(2)+&B(8)*(&A(3)-&I-1-&A(2))
&B(2)    SETB  0
&B(3)    SETB  (&I+&A(2)+11 LE &A(3))
&B(1)    SETB  (NOT &B(3) AND &A(3) LT &A(4) AND                       X
               &A(3)+N'&SYSLIST(&M)-&N+1 LT &A(4))
.*                                     PLUG IN THE GUESSES
&FB(26)  SETB  (&B(1))
&FB(27)  SETB  (&B(2))
&FB(28)  SETB  (&B(3))
&A(1)    SETA  64*&FB(22)+32*&FB(23)+16*&FB(24)+8*&FB(25)+4*&FB(26)+2*&X
               FB(27)+&FB(28)
         IDFASM A                      ASSEMBLE THE IND CTRL BYTE
&A(1)    SETA  &A(2)-&A(2)/128*128
&A(2)    SETA  &A(1)                   RECALCULATE COUNT, MODULO 128
         IDFASM A
&TB(4)   SETB  (&DSCB(60))
*                                      TRANSLATE
         AIF   (&PIB(23)).INDF11A
         IDFTR &SYSLIST(&M)             TRANSLATE CHARACTER STRING
         AGO   .INDF11B
.INDF11A IDFTRK &SYSLIST(&M)
.INDF11B ANOP
*                                      END TRANSLATE
&PIB(1)  SETB  (&TB(4) NE &DSCB(60))
         AIF   (&PIB(1)).INDF15        BRANCH IF ILLEGAL CHARACTER
&I       SETA  &I+1                    SKIP TERMINAL APOSTROPHE
&B(5)    SETB  1                       NEXT COMPARANDS WILL BE CHAR
&PIB(1)  SETB  (&TB(6))
         AIF   (NOT &PIB(1)).I0271
         IDFMSG3 501
.I0271   ANOP ,
&PIB(1)  SETB  (&TB(7))
         AIF   (NOT &PIB(1)).I0272
         IDFMSG3 502
.I0272   ANOP ,
&PIB(1)  SETB  (&TB(8))
         AIF   (NOT &PIB(1)).I0273
         IDFMSG3 503
.I0273   ANOP ,
.INDF12  ANOP
.*                                     BRANCH IF SUBOP FINISHED
         AIF   ('&SYSLIST(&M)'(&I,1) EQ ')' AND &I EQ &A(3)).INDF17
         AIF   ('&SYSLIST(&M)'(&I,1) NE ',').INDF13   COMMA REQUIRED
&I       SETA  &I+1                    SKIP OVER THE COMMAS
         AIF   (&I+3 GT &A(3)).INDF13  INSUFFICIENT CHARACTERS
&FB(26)  SETB  0                       CHAIN BIT NOW SET OFF
&C(1)    SETC  '&SYSLIST(&M)'(&I,3)
&I       SETA  &I+3
&FB(27)  SETB  0
&FB(28)  SETB  ('&C(1)' EQ 'OR,')
         AIF   (&FB(28)).INDF2A        RETURN IF EXTENDED COMPARISON
         AIF   (&I GT &A(3)).INDF13
&C(1)    SETC  '&C(1)'.'&SYSLIST(&M)'(&I,1)      ADD A CHARACTER
&I       SETA  &I+1
&FB(27)  SETB  ('&C(1)' EQ 'AND,')     TRY AGAIN
&FB(28)  SETB  ('&C(1)' EQ '''×'',')
         AIF   (&FB(27) OR &FB(28)).INDF2A
         AIF   (&I GT &A(3)).INDF13
&C(1)    SETC  '&C(1)'.'&SYSLIST(&M)'(&I,1)
&I       SETA  &I+1
&FB(27)  SETB  ('&C(1)' EQ '''&&'',')
         AIF   (&FB(27)).INDF2A
.INDF13  ANOP  ,                       HERE IF LOGICAL OP INVALID
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0274
         IDFMSG3 560
.I0274   ANOP ,
         AGO   .INDF16
.INDF14  ANOP
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0275
         IDFMSG3 561
.I0275   ANOP ,
         AGO   .INDF16
.INDF15  ANOP
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0277
         IDFMSG3 562
.I0277   ANOP ,
.INDF16  ANOP
&PIB(2)  SETB  0
&PIB(15) SETB  1
         AGO   .INDF18
.INDF17  ANOP
&FB(27)  SETB  0                       NO EXTENDED COMPARISON IF
&FB(28)  SETB  0                       SUBOP TERMINATED
         AIF   (&J EQ 890).INDF17A      BRANCHING SUBOP?          FSDB
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0279
&MB(1)   SETB  1         ISSUE MSG
&MB(2)   SETB  0         IND SET
         IDFMSG1 127
.I0279   ANOP ,
&PIB(1)  SETB  (NOT &INDB(&J+1) AND &PIB(10))
         AIF   (NOT &PIB(1)).I0280
         IDFMSG1 128
.I0280   ANOP ,
&PIB(1)  SETB  (&INDB(&J+1) AND &INDB(&J+169))
         AIF   (NOT &PIB(1)).I0281
         IDFMSG 023
.I0281   ANOP ,
&INDB(&J+85) SETB (NOT &INDB(&J+1) AND &PIB(10) OR &INDB(&J+1) AND     X
               &INDB(&J+85))
&INDB(&J+169) SETB 1
&INDB(&J+1) SETB 1
&INDB(1) SETB  1
.INDF17A AIF   (&FB(16)).INDF18
&FB(16)  SETB  1                       SET FLAG IN FUNCTION BYTE
&PIA(6)  SETA  &FA(2)+&FB(6)*(2+(&FA(2)-&FA(2)/486*486)/478*18)
&A(1)    SETA  64*&FB(15)+32*&FB(16)+16*&FB(17)
         IDFASM A                      REASSEMBLE FUNCTION BYTE
         AIF   (&B(4) OR &B(5)).INDF18
&B(4)    SETB  1                       SET ON NUMERICS REQ'D BIT
&FB(1)   SETB  0                       RESET THE
&FB(2)   SETB  0                            ALPHANUMERIC
&FB(3)   SETB  1                                 CHECKING BITS
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0282
         IDFMSG3 563
.I0282   ANOP ,
&PIA(6)  SETA  &FA(1)
&A(1)    SETA  64*&FB(1)+32*&FB(2)+16*&FB(3)+8*&FB(4)+4*&FB(5)+2*&FB(6)X
               +&FB(7)
         IDFASM A                      REASSEMBLE DATA TYPE BYTE
.INDF18  ANOP
&FB(27)  SETB  0                       NO EXTENDED COMPARISONS
&FB(28)  SETB  0                       IF SUBOP EXHAUSTED
&N       SETA  &N+1                    ADVANCE TO NEXT SUBOP
&I       SETA  &A(3)+2
&B(18)   SETB  (&A(3) NE &A(4))         SUBOPERANDS
&A(3)    SETA  &A(3)+1+K'&SYSLIST(&M,&N)
&PIB(2)  SETB  ('&SYSLIST(&M,&N)' EQ 'C' AND &N EQ N'&SYSLIST(&M))
&FB(26)  SETB  (&PIB(2) AND &B(18))     CHAINED SUBOPS
         AIF   (&PIB(2)).INDFX          CHAINING
&FB(26)  SETB  (&FB(16))
         AIF   (&A(3) LE &A(4)).INDF2   AT END YET
         AIF   (NOT &FB(16)).INDFX     IF A BUNCH OF NULL SUBOPS
&FB(26)  SETB  0                        NO CHAINING
         AIF   (&FB(26) EQ &B(1) AND &FB(27) EQ &B(2) AND &FB(28)      X
               EQ &B(3)).INDF19
&PIA(6)  SETA  &FA(3)
&A(1)    SETA  64*&FB(22)+32*&FB(23)+16*&FB(24)+8*&FB(25)+4*&FB(26)+2*&X
               FB(27)+&FB(28)
         IDFASM A                      REASSEMBLE IND CTRL BYTE
.INDF19  ANOP
&A(1)    SETA  &FA(3)+4
&A(1)    SETA  &PIA(3)+&PIA(4)-&A(1)-(&PIA(3)/486-&A(1)/486)*18
&A(1)    SETA  &A(1)/2
&PIB(1)  SETB  (&A(1) LT 1 OR &A(1) GT 127)
         AIF   (NOT &PIB(1)).I0283
         IDFMSG3 564
.I0283   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).INDFX
         AIF   (&A(1) EQ &A(2)).INDF20
&PIA(6)  SETA  &FA(3)+2+(&FA(3)-&FA(3)/486*486)/478*18
         IDFASM A                      AND THE COUNT, IF NEC'Y
.INDF20  ANOP
&PIA(6)  SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18
&A(1)    SETA  &J-1
&A(1)    SETA  16*&A(1)-&A(1)/7*111
         IDFASM A                      ASSEMBLE THE TERMINAL IND NUM
&B(17)   SETB  1                        SET TO EXIT AFTER NXT RTN FSDB
         AIF   (&J EQ 890).INDF4A       IF BRANCH GEN NO OPS      FSDB
.INDFX   ANOP
         AIF   (NOT(&PIB(47) AND &PIB(39))).INDFXA                FSDB
         IDFDSP 'AFTER IND - FDFIELD',QUEUE                       FSDB
.INDFXA  AIF   (NOT &PIB(48)).TX
         MNOTE *,'IDF100 IN TRACE MODE LEAVING IDFIN07'
.TX      ANOP
         MEND
