         MACRO
         IDFMSG2
         COPY  IDFGBL
         AIF   (NOT &PIB(48)).TR
         MNOTE *,'IDF100 IN TRACE MODE ENTERING IDFMSG2'
.TR      ANOP
         AIF   (&PIB(1)).VV
         AGO   .MEX
.**********************************************************************
.*
.*       VARIABLE FORMAT VARIABLE MESSAGE NUMBER MNOTES
.*
.**********************************************************************
.VV      AIF   (T'&SYSLIST(1) NE 'N').ERR
         AIF   (&SYSLIST(1) EQ 0).ERR
         AIF   (&SYSLIST(1) LT 200).ERR
&S       SETA  0
&L       SETA  0
.**********************************************************************
.*
.*       LOKUP ROUTINE ON &M
.*      FIRST &C(1) SET GETS TENS DIGIT OF DESIRED OPERAND STARTING
.* POSITION IN THE CHARACTER STRING AND TENS DIGIT OF NEXT OPERAND
.* STARTING POSITION.  &C(2) SET GETS UNITS DIGITS OF EACH STARTING
.* POSITION.  &S IS SET TO DESIRED OPERAND START POSITION THEN USING
.* NEXT OPERAND START POSITION &L IS SET TO DESIRED OPERAND LENGTH
.*
.**********************************************************************
         AIF   (&M GT 27 OR &M LT 1).ERR    &M VALID?
         AIF   (&M GT 13 AND &PIB(3) EQ 1).ERR   &M VALID?
&M       SETA  &M+27*&PIB(3)                CTL OPERAND
         AIF   (&M GT 20).CS2               KEYWORD IN NEXT STRING?
&C(1)    SETC  '0011223344555667788899'(&M,2)
&C(2)    SETC  '1748263504156171636967'(&M,2)
&S       SETA  &C(1)/10*10+&C(2)/10         EXTRACT START POS
&L       SETA  (&C(1)-&C(1)/10*10)*10+(&C(2)-&C(2)/10*10)-&S
&C(1)    SETC  'SOURCESELFCHKKINDSINKFILLJUSTIFYULWIDTHHMRGSAVELOCNAME C
               CYCLEHEIGHTVMRGCOUNTCOMPARECTRINDPICTURE'(&S,&L)
         AGO   .VFM
.CS2     ANOP
&M       SETA  &M-20
&C(2)    SETC  '1674818570305901293490'(&M,2)
&C(1)    SETC  '0001122334455566667778'(&M,2)
&S       SETA  &C(1)/10*10+&C(2)/10
&L       SETA  (&C(1)-&C(1)/10*10)*10+(&C(2)-&C(2)/10*10)-&S
&C(1)    SETC  'BATCH MESSAGEHTABFIDPACKINGMRGSTOPIFINDCTRCOMMANDTOTALGC
               OTO   SAVELOCNAME CYCLE '(&S,&L)
&M       SETA  &M+20                        RESTORE &M
.VFM     ANOP
&C(5)    SETC  ''
&C(2)    SETC  ''
         AIF   (&M NE 12 AND &M NE 22).VFB
         AIF   (&PIA(1) GT 4 OR &PIA(1) LT 2).ERR
&C(1)    SETC  'NUMBERMARGIN'(1+&PIA(1)/4*6,6)
&C(2)    SETC  'PAGE LINE FIELD '(1+&PIA(1)/3*5+&PIA(1)/4*5,5+&PIA(1)/4C
               )
         AIF   (&PIA(1) NE 4).VFB
&C(5)    SETC  'LEFT RIGHT '(1+&M/22*5,5+&M/22)
.VFB     ANOP
         AIF   (&M NE 9 AND &M NE 15).VV00
&S       SETA  1+(&M+&N)/11*5+(&M+&N)/16*6+(&M+&N)/17*4
&C(2)    SETC  'LEFT RIGHT TOP BOTTOM '(&S,5+(&M+&N)/11*1-(&M+&N)/16*2+C
               (&M+&N)/17*3)
.VV00    ANOP
&M       SETA  &M-27*&PIB(3)           RESTORE &M IF CTL
&C(3)    SETC  ''
&C(4)    SETC  ''
         AIF   (&N LT 1).VV00A
&C(3)    SETC  'SUB'
&C(4)    SETC  '&N '
.VV00A   ANOP
         AIF   (&SYSLIST(1) NE 200 AND &SYSLIST(1) NE 202).VV01
&L       SETA  717+&SYSLIST(1)/202*202/&SYSLIST(1)
&C(6)    SETC  'INVALIDOMITTED'(1+&SYSLIST(1)/202*7,7)
         MNOTE 8,'IDF&L &C(2)&C(5)&C(1) &C(3)OPERAND &C(4)&C(6)'
         AGO   .MEX
.VV01    AIF   (&SYSLIST(1) NE 201).VV02
         MNOTE 8,'IDF719 &C(2)&C(5)&C(1) &C(3)OPERAND &C(4)'
         MNOTE 0,'       NOT BETWEEN &A(5) AND &A(6)'
         AGO   .MEX
.VV02    AIF   (&SYSLIST(1) LT 212).ERR
         AIF   (&SYSLIST(1) GT 251).VV52
&S       SETA  &SYSLIST(1)-210
&S       SETA  1+&S/3*5+&S/4*3+&S/5*3-&S/6*2+&S/7*3+&S/8*5
&L       SETA  &SYSLIST(1)-210
&L       SETA  3+2*(&L/2)*(2/&L)+5*(&L/7)*(7/&L)+(&L/8)*(8/&L)
         AIF   (&S LT 1 OR &S GT 26 OR &L LT 3 OR &L GT 8).ERR
&C(2)    SETC  'BATCHFIDCTRINDEOFOPERATOREMIT'(&S,&L)
&L       SETA  508+&SYSLIST(1)
         MNOTE 8,'IDF&L &C(2) FOR &C(1) SUBOPERAND &N INVALID'
         AGO   .MEX
.VV52    AIF   (&SYSLIST(1) GT 271).VV04
&S       SETA  &SYSLIST(1)-250
&L       SETA  3+2*(&S/2)*(2/&S)+(&S/7)*(7/&S)
&S       SETA  1+&S/3*5+&S/4*3+&S/5*3-&S/6*2+&S/7*3
         AIF   (&S LT 1 OR &S GT 18 OR &L LT 3 OR &L GT 5).ERR
&C(2)    SETC  'BATCHFIDCTRINDEOFSKIP'(&S,&L)
&L       SETA  475+&SYSLIST(1)
         MNOTE 8,'IDF&L &C(2) FOR &C(1) SUBOPERAND &N NOT'
         MNOTE 0,'       BETWEEN &A(5) AND &A(6)'
         AGO   .MEX
.VV04    ANOP
.**********************************************************************
.*
.*             ERROR MNOTE
.*
.**********************************************************************
.ERR     ANOP
         MNOTE 8,'IDF999 FDM SYSTEM ERROR'
         MNOTE 0,'       INVALID CALL OF INNER MACRO IDFMSG2'
         MNOTE 0,'       MESSAGE CALL PARAMETER=&SYSLIST(1)'
.MEX     ANOP  ,                                              A01301
&PIB(15) SETB  (&PIB(15) OR &PIB(1))                          A01301
         AIF   (NOT &PIB(48)).TX
         MNOTE *,'IDF100 IN TRACE MODE LEAVING IDFMSG2'
.TX      ANOP
         MEND
