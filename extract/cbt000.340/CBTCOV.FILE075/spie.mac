         MACRO
&NAME    SPIE  &EXIT,&INTS,&MF=I
         LCLA  &A,&B,&DNO,&ENO
         LCLB  &BIT(15),&LR,&IR,&ER
         LCLC  &C,&D,&E
.CONT    AIF   ('&MF' EQ 'L').LROUT
         AIF   ('&MF' EQ 'I').IROUT
         AIF   (N'&MF LE 1).ERROR1
         AIF   ('&MF(1)' NE 'E').ERROR1
&NAME    IHBINNRA &MF(2)
.CONTA   AIF   ('&INTS' EQ '').CONTC
&ER      SETB  1
         AGO   .INTROUT
.INTROUT ANOP
&A       SETA  1
.MAINLUP ANOP
         AIF   ('&INTS(&A)' EQ '').RETURN
&C       SETC  '&INTS(&A)'
         AIF   ('&C'(1,1) EQ '(').REGLOOP
         AIF   (T'&INTS(&A) NE 'N').ERROR2
         AIF   (&INTS(&A) LT 1 OR &INTS(&A) GT 15).ERROR2
&BIT(&C) SETB  1
.RETURN  AIF   (&A EQ N'&INTS).ALLDONE
&A       SETA  &A+1
         AGO   .MAINLUP
.REGLOOP AIF   ('&C'(3,1) EQ ',').SHORTNO
&D       SETC  '&C'(2,2)
&E       SETC  '&C'(5,2)
         AGO   .SUBLOOP
.SHORTNO ANOP
&D       SETC  '&C'(2,1)
         AIF   ('&C'(5,1) EQ ')').SMALLNO
&E       SETC  '&C'(4,2)
         AGO   .SUBLOOP
.SMALLNO ANOP
&E       SETC  '&C'(4,1)
.SUBLOOP ANOP
&DNO     SETA  &D
&ENO     SETA  &E
         AIF   (&DNO GT &ENO OR &DNO LT 1 OR &ENO GT 15).ERROR2
.INRLOOP ANOP
&BIT(&DNO) SETB 1
         AIF   (&DNO EQ &ENO).RETURN
&DNO     SETA  &DNO+1
         AGO   .INRLOOP
.ALLDONE AIF   (&ER).CONTB
         AIF   (&LR).CONTF
         AGO   .CONTQ
.CONTB   AIF   ('&EXIT' EQ '').NONEXIT
         AIF   ('&EXIT'(1,1) EQ '(').EXITREG
         B     *+10                              BRANCH AROUND CONSTANT
         DC    B'0000&BIT(8)&BIT(10)&BIT(13)&BIT(14)' PROGRAM MASK BITS
         DC    AL3(&EXIT)                        EXIT ROUTINE ADDRESS
         DC    B'0&BIT(1)&BIT(2)&BIT(3)&BIT(4)&BIT(5)&BIT(6)&BIT(7)'
         DC    B'&BIT(8)&BIT(9)&BIT(10)&BIT(11)&BIT(12)&BIT(13)&BIT(14)X
               &BIT(15)'                         INTERUPTION MASK
         MVC   0(6,1),*-6                        MOVE TO PARAM LIST
         AGO   .SVC
.EXITREG ST    &EXIT(1),0(1,0)                   STORE EXIT ROUT. ADDR.
.NONEXIT ANOP
&A       SETA  &BIT(14)+&BIT(13)*2+&BIT(10)*4+&BIT(8)*8
         MVI   0(1),&A                           MOVE PROGRAM MASK BITS
&A       SETA  &BIT(7)+&BIT(6)*2+&BIT(5)*4+&BIT(4)*8+&BIT(3)*16+&BIT(2)X
               *32+&BIT(1)*64
         MVI   4(1),&A                           MOVE IN INTERUPT MASK
&A       SETA  &BIT(15)+&BIT(14)*2+&BIT(13)*4+&BIT(12)*8+&BIT(11)*16
&A       SETA  &A+&BIT(10)*32+&BIT(9)*64+&BIT(8)*128
         MVI   5(1),&A                           MOVE IN INTERUPT MASK
         AGO   .SVC
.CONTC   AIF   ('&EXIT' EQ '').SVC
         IC    14,0(1,0)                         SAVE PROGRAM MASK BITS
         AIF   ('&EXIT'(1,1) EQ '(').REGEXIT
         LA    15,&EXIT                          LOAD EXIT ROUT. ADDR.
         ST    15,0(1,0)                         STORE EXIT ROUT. ADDR.
.CONTD   STC   14,0(1,0)                         RESTORE PROG.MASK BITS
         AGO   .SVC
.REGEXIT ST    &EXIT(1),0(1,0)                   STORE EXIT ROUT. ADDR.
         AGO   .CONTD
.LROUT   AIF   ('&INTS' EQ '').CONTE
&LR      SETB  1
         AGO   .INTROUT
.CONTF   ANOP
&NAME    DS    0F
         DC    B'0000&BIT(8)&BIT(10)&BIT(13)&BIT(14)' PROGRAM MASK BITS
         AIF   ('&EXIT' EQ '').ZEROOUT
         AIF   ('&EXIT'(1,1) EQ '(').ERROR1
         DC    AL3(&EXIT)                        EXIT ROUTINE ADDRESS
         AGO   .CONTG
.ZEROOUT DC    AL3(0)                            EXIT ROUTINE ADDRESS
.CONTG   DC    B'0&BIT(1)&BIT(2)&BIT(3)&BIT(4)&BIT(5)&BIT(6)&BIT(7)'
         DC    B'&BIT(8)&BIT(9)&BIT(10)&BIT(11)&BIT(12)&BIT(13)&BIT(14)X
               &BIT(15)'                         INTERUPTION MASK
         AGO   .END
.CONTE   AIF   ('&EXIT' EQ '').ALLZERO
         AIF   ('&EXIT'(1,1) EQ '(').ERROR1
&NAME    DC    A(&EXIT)                          EXIT ROUTINE ADDRESS
         DC    AL2(0)                            INTERUPTION MASK
         AGO   .END
.ALLZERO DS    0F
&NAME    DC    FL6'0'                            PARAMETER LIST
         AGO   .END
.IROUT   AIF   ('&INTS' EQ '').CONTH
         AGO   .INTROUT
.CONTQ   ANOP
         CNOP  2,4
&NAME    LA    1,*+12                            LOAD BRANCH ADDRESS
         BALR  1,1                               BRANCH AROUND PARAMS.
         DC    B'0000&BIT(8)&BIT(10)&BIT(13)&BIT(14)' PROGRAM MASK BITS
         AIF   ('&EXIT' EQ '').ZEROA
         AIF   ('&EXIT'(1,1) EQ '(').SPZERO
         DC    AL3(&EXIT)                        EXIT ROUTINE ADDRESS
         AGO   .CONTI
.SPZERO  ANOP
&IR      SETB  1
.ZEROA   DC    AL3(0)                            EXIT ROUTINE ADDRESS
.CONTI   DC    B'0&BIT(1)&BIT(2)&BIT(3)&BIT(4)&BIT(5)&BIT(6)&BIT(7)'
         DC    B'&BIT(8)&BIT(9)&BIT(10)&BIT(11)&BIT(12)&BIT(13)&BIT(14)X
               &BIT(15)'                         INTERUPTION MASK
         AIF   (NOT &IR).SVC
         ST    &EXIT(1),0(0,1)
&A       SETA  &BIT(14)+&BIT(13)*2+&BIT(10)*4+&BIT(8)*8
         MVI   0(1),&A                           MOVE PROGRAM MASK BITS
         AGO   .SVC
.CONTH   CNOP  2,4
&NAME    LA    1,*+12                            LOAD BRANCH ADDRESS
         BALR  1,1                               BRANCH AROUND PARAMS.
         AIF   ('&EXIT' EQ '').ZEROB
         AIF   ('&EXIT'(1,1) EQ '(').SPZEROB
         DC    A(&EXIT)                          EXIT ROUTINE ADDRESS
         AGO   .CONTJ
.SPZEROB ANOP
&IR      SETB  1
.ZEROB   DC    A(0)                              EXIT ROUTINE ADDRESS
.CONTJ   DC    AL2(0)                            INTERUPTION MASK
         AIF   (NOT &IR).SVC
         ST    &EXIT(1),0(1,0)                   STORE EXIT ROUT. ADDR.
         AGO   .SVC
.ERROR1  IHBERMAC 69
         MEXIT
.ERROR2  IHBERMAC 37,,&INTS
         MEXIT
.SVC     SVC   14                                ISSUE SPIE SVC
.END     MEND
