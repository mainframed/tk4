         MACRO
&NAME    RETRIEVE &TERM,&WKA,&PAR3,&SEQ,&TYPE=
         GBLB  &IEDQE7
         LCLB  &NOGO
.* 050000,072000,843000-873000                                    20002
&NAME    DS    0H
         AIF   ('&TERM' EQ '' OR '&WKA' EQ '' OR '&TYPE' EQ '').ER
         AIF   ('&TYPE' EQ 'S' AND '&PAR3' EQ '').ER
         AIF   ('&TYPE' EQ 'S' AND '&SEQ' EQ '').ER
         AIF   ('&TYPE' EQ 'S' OR '&TYPE' EQ 'D').OKTY
         MNOTE 12,'***  IHB002  INVALID TYPE OPERAND SPECIFIED'
&NOGO    SETB  1
.OKTY    AIF   ('&PAR3' EQ '' OR '&PAR3' EQ 'IN' OR '&PAR3' EQ 'OUT').X
         MNOTE 12,'***  IHB002  INVALID IN/OUT OPERAND SPECIFIED'
&NOGO    SETB  1
.X       AIF   ('&TYPE' EQ 'S' OR '&TERM'(1,1) EQ '(').VAL
         AIF   ('&TYPE' NE 'D').VAL
         MNOTE 12,'***  IHB002  INVALID DASDADDR OPERAND SPECIFIED'
         AGO   .END
.ER      MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         AGO   .END
.VAL     AIF   (&NOGO).END
         AIF   ('&WKA' EQ '(0)').NEXT
         AIF   ('&WKA'(1,1) EQ '(').LR
         LA    0,&WKA                  WORKAREA ADDRESS TO REGISTER 0
         AGO   .NEXT
.LR      LR    0,&WKA(1)               WORKAREA ADDRESS TO REGISTER 0
.NEXT    AIF   ('&TYPE' EQ 'D').DASD
         LR    15,0                    WORKAREA ADDRESS TO REGISTER 15
         AIF   ('&TERM'(1,1) EQ '(').R
         MVC   0(8,15),=CL8'&TERM'     TERMNAME TO THE WORKAREA
         AGO   .XX
.R       LR    14,&TERM(1)
         MVC   0(8,15),0(14)           TERMNAME TO THE WORKAREA
.XX      AIF   ('&SEQ'(1,1) NE '(').LA
         AIF   ('&SEQ(1)' EQ '1').OUT
         LR    1,&SEQ(1)               SEQUENCE NUMBER TO REGISTER 1
         AGO   .OUT
.LA      LA    1,&SEQ                  SEQUENCE NUMBER TO REGISTER 1
         AGO   .OUT
.DASD    AIF   ('&TERM(1)' EQ '1').OUT
         LR    1,&TERM(1)              DASD ADDRESS TO REGISTER 1
.OUT     AIF   ('&PAR3' EQ '' OR '&PAR3' EQ 'OUT').GEN
         LCR   1,1                     COMPLIMENT INPUT SEQUENCE NUMBER
.GEN     ANOP
         AIF   (&IEDQE7).TCAM                                     20002
         L     15,=V(IECKRET&TYPE)     ADDRESS OF RETRIEVE ROUTINE
         AGO   .CALL                                              20002
.TCAM    ANOP
         AIF   ('&TYPE' EQ 'S').TCAM1                             20002
         LR    15,0 .                   WORKAREA ADDRESS          20002
         MVI   0(15),0 .                INDICATE DASD ADDR        20002
.TCAM1   ANOP
         L     15,=V(IEDQES) .          ADDRESS OF RETRIEVE ROUTIN20002
.CALL    ANOP
         BALR  14,15                   LINK TO RETRIEVE ROUTINE
.END     MEND
