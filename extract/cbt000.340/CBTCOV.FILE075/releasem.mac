         MACRO
&NAME    RELEASEM &TERM
         GBLB  &IEDQE7
.* 110000,308000-340000,370000,751000-780000                      20002
         AIF   ('&TERM' NE '').GO
         MNOTE 12,'*** IHB001  TERMNAME OPERAND REQ''D-NOT SPECIFIED'
         MEXIT
.GO      ANOP
         AIF   (K'&TERM LT 9).OK2                                 20002
         AIF   ('&TERM'(1,1) EQ '(').OK2                          20002
         MNOTE 12,'*** IHB002 INVALID TERMNAME OPERAND SPECIFIED'
         MEXIT
.OK2     ANOP
&NAME    DS    0H
         AIF   (&IEDQE7 EQ 1).TCAM                                20002
         AIF   ('&TERM'(1,1) EQ '(').REG1
         BAL   1,*+12                  LOAD ADDRESS OF TERMNAME
         DC    CL8'&TERM'
         AGO   .OK
.REG1    AIF   ('&TERM(1)' EQ '1').OK
         LR    1,&TERM(1)          LOAD ADDRESS OF TERMNAME
.OK      L     15,=V(IECKRELM)     LOAD SUBROUTINE ADDRESS
         BALR  14,15               LINK
         MEXIT
.TCAM    ANOP
         SR    0,0 .                    INDICATE NO PASSWORD      20002
         AIF   ('&TERM'(1,1) EQ '(').REG                          20002
         BAL   1,*+12 .                 ADDRESS OF TERMNAME       20002
         DC    CL8'&TERM' .             TERMINAL NAME             20002
         AGO   .SETCIB                                            20002
.REG     ANOP
         AIF   ('&TERM' EQ '(1)').SETCIB                          20002
         LR    1,&TERM(1) .             ADDRESS OF TERMNAME       20002
.SETCIB  ANOP
         MVC   IEDQ&SYSNDX+16(8),0(1) . MOVE TERMNAME INTO CIB    20002
         MVI   IEDQ&SYSNDX+11,X'FF' .   FLAG AS CQTAM MACRO       20002
         CNOP  0,4 .                                              20002
         BAL   1,*+44 .                 SET CIB ADDR IN PARM REG  20002
IEDQ&SYSNDX DS 0F .                     COMMAND INPUT BUFFER      20002
         DC    F'0' .                   OPERATOR CONTROL QCB ADDR 20002
         DC    XL1'E0' .                CIB QUEUING PRIORITY      20002
         DC    XL3'00' .                LINK FIELD                20002
         DC    XL1'87' .                VERB CODE FOR LINE RELEASE20002
         DC    XL1'1C' .                CIB LENGTH                20002
         DC    XL1'00' .                RELATIVE LINE NUMBER      20002
         DC    XL1'FF' .                COMPLETION CODE(CQTAM)    20002
         DC    A(IEDQ&SYSNDX+32) .      ECB ADDRESS               20002
         DC    XL8'00' .                TERMINAL NAME             20002
         DC    XL4'00' .                WORKAREA ADDRESS          20002
         DC    A(IEDQ&SYSNDX) .         ADDRESS OF CIB            20002
         DC    F'0' .                   PCB WORKAREA/ECB          20002
         DC    F'0' .                   ADDRESS OF CIB LENGTH     20002
         LINK  EP=IEDQET .                                        20002
         MEND
