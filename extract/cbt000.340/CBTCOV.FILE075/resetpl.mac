         MACRO
&NAME    RESETPL &DECB,&TYPE,&SPEC                            SB A60866
.*  FOLLOWING APARS FIXED:
.*          SA74703         4/02/75
.*          SA74737        11/05/75
.*          SA75275        10/15/75
.*          SA76139        2/19/76
.*          SA76579        2/19/76
.*          SA77287        6/25/76          SA77537      09/25/76
.*          SA77542        09/20/76         SA77552      10/07/76
.*          SA77563        11/12/76         SA78113      05/26/77
.*          SA79840        09/25/77
.*
         LCLC  &X
&X       SETC  '&SYSNDX'
.* FOLLOWING GENERATED SYMBOLS AVAILABLE: NONE
&NAME    DS    0H
         AIF   ('&DECB'(1,1) EQ '(').REG
         LA    1,&DECB
         AGO   .CON
.REG     AIF   ('&DECB(1)' EQ '1').CON
         AIF   (T'&DECB EQ 'U').UREG
         AIF   ('&DECB(1)' GT '12').ER
.UREG    LR    1,&DECB(1)
.CON     SR    0,0
         SR    15,15
         IC    0,25(1)        GET RELATIVE LINE NUMBER FROM DECB.
         L     14,8(1)        GET DCB ADDRESS FROM DECB.
         TM    48(14),X'10'            HAS DCB BEEN OPENED?
         BO    IECA&X              IF SO, CONTINUE.
IECJ&X   LA    15,12                   IF NOT, SET RETURN CODE   A28622
         B     IECB&X              AND EXIT.
IECA&X   EQU   *
         AIF   ('&TYPE' EQ 'ATTENT' OR '&TYPE' EQ '').ANR
.NOTANR  MVC   IECK&X.(1),26(1)    MOVE RESPN TO SAVE
         MVC   IECK&X+1(1),5(1)    MOVE OPERATION TYPE             000H
         NI    IECK&X+1,X'7F'    HIGH ORDER BIT OFF                000H
         SR    1,1                                                 000G
         IC    1,36(14)       GET IOB SIZE FROM DCB.
         MR    0,0            MULTIPLY IOB SIZE TIMES RELATIVE LINE NO.
         A     1,28(14)       ADD IOB ADDRESS MINUS SIZE TO RESULT.
         OI    1(1),X'01'     SET POLLING RESET BIT IN IOB.        000A
         CLI   19(15),X'82'   IS THIS A 2260                   @SA77563
         BE    IECE&X         YES, GIVE ZERO RD                @SA79840
.*
         AIF   ('&TYPE' EQ 'POLLING' OR '&TYPE' EQ '').POLL        000D
         AIF   ('&TYPE' EQ 'ANSRING').ANSR
         MNOTE 12,'***  IHB002  INVALID TYPE OPERAND SPECIFIED-&TYPE'
         MEXIT
.*
.*
.POLL    ANOP                                                      000A
.*
         L     15,16(1)       LOAD ADDRESS OF FIRST CCW.
         CLI   0(15),X'06' IS THE CCW A PREPARE COMMAND            000G
         BE    IECR&X     IF SO BRANCH                             000H
         CLI   IECK&X+1,X'0E'     IS THIS WRITE TCO                000H
         BE    IECV&X     YES, CHECK FOR PREPARE                   000H
         CLI   IECK&X+1,X'1E'  READ CONNECT WITH TONE          @SA77537
         BE    IECY&X          YES, HANDLE                     @SA70024
         CLI   8(15),X'06'  IS NEXT CCW A PREPARE                  000H
         BNE   IECC&X   BRANCH IF NOT                              000H
         CLI   16(15),X'04'  IS THE CCW A SENSE                    000H
         BNE   IECC&X         BRANCH IF NOT                        000H
         B     IECS&X     BRANCH TO CHECK RESPONSE                 000H
IECV&X   LA    15,40(15)  POINT TO SIXTH CCW                       000H
         CLI   0(15),X'06'  IS THIS CCW A PREPARE                  000H
         BE    IECR&X     CHECK FOR SENSE COMMAND NEXT             000H
         B     IECE&X                                              000H
IECR&X   CLI   8(15),X'04'  IS THE CCW A SENSE                     000H
         BNE   IECC&X   BRANCH IF NOT                              000G
IECS&X   CLI   IECK&X,X'FF'     IS RESPN X'FF'                     000H
         BE    IECP&X   BRANCH IF YES                              000G
         B     IECE&X                                              000G
IECC&X   LA    15,72(1)    POLL ALWAYS AT IOBPCA+8               A53945
         CLI   0(15),X'09'     IS IT THERE                       A53945
         BE    IECD&X         YES, CK FOR WRAPLIST               A53945
         AIF   ('&TYPE' EQ 'POLLING').POLL01                     A53945
         B     IECF&X        CK ENABLE, PREPARE                  A53945
         AGO   .POLL02                                           A53945
.POLL01  B     IECE&X     QUIT                                   A53945
.POLL02  ANOP                                                    A53945
IECD&X   CLI   8(15),X'08'    IS THE CCW FOLLOWING THE POLL        000A
         BNE   IECQ&X    COMMAND A TIC COMMAND?  IF NOT, EXIT. @SA77287
         MVI   8(15),X'03'    CHANGE COMMAND CODE FROM A TIC TO    000A
*                        AN I/O NO-OP.                             000A
         L     15,8(15)       LOAD ADDRESS OF SECOND POLL COMMAND. 000A
         MVI   8(15),X'03'    SET COMMAND CODE IN CCW FOLLOWING    000A
*                        SECOND POLL COMMAND TO AN I/O NO-OP.      000A
         B     IECE&X                                              000A
.*
         AIF   ('&TYPE' EQ 'POLLING').END                        A28606
IECF&X   EQU   *                                                   000D
.*
.*
.ANSR    ANOP                                                      000A
.*
.*
         L     15,16(1)       LOAD ADDRESS OF FIRST CCW.
         CLI   0(15),X'2F'    IS THE CCW A DISABLE COMMAND?        000A
         BNE   IECG&X         IF NOT, BRANCH.                      000A
         TM    4(15),X'40'    IS THE CCW COMMAND CHAINED?          000A
         BNO   IEC0&X         IF NOT, EXIT WITH RTRN CODE X10  @SA74737
         LA    15,8(15)       LOAD ADDRESS OF NEXT CCW.            000A
         CLI   0(15),X'08'        IS IT A TIC?                   A63481
         BNE   IECG&X             NO, BRANCH                     A63481
         L     15,0(15)           GET NEXT CCW ADDRESS           A63481
         LA    15,0(15)           CLEAR HI ORDER BYTE            A63481
IECG&X   CLI   0(15),X'27'        IS THE CCW AN ENABLE COMMAND     000B
         BE    IECN&X                                              000B
         CLI   0(15),X'06'        IS THE CCW A PREPARE COMMAND     000B
         BNE   IEC1&X             IF NOT, EXIT                 @SA78113
         CLI   13(15),X'0B'       IS THE NEXT CCW READ INQUIRY     000B
         BE    IECP&X                                              000B
         CLI   13(15),X'8B'            IS IT READ INQUIRY          3128
         BE    IECP&X                                              3128
         B     IECI&X                                              000H
.*
IECN&X   CLI   13(15),X'0B'       IS THE NEXT CCW READ INQUIRY     000B
         BE    IECP&X                                              000B
         CLI   13(15),X'8B'            IS IT READ INQUIRY          3128
         BE    IECP&X                                   Y          3128
         CLI   13(15),X'07'            IS NEXT CCW READ ID ENQ?  A43785
         BE    IECP&X                                            A43785
         CLI   21(15),X'87'  RD ID ENQ W/ LAST CCW FLAG ON ?   @SA75275
         BE    IECP&X                   IF YES ISSUE HIO       @SA75275
         TM    4(15),X'40'              IS CCW COMMAND CHAIN     A30763
         BNO   IECP&X                   NO, ISSUE HIO            A30763
IECH&X   LA    15,8(15)       LOAD ADDRESS OF NEXT CCW.            000H
         CLI   0(15),X'06'              IS THIS CCW PREP?          000H
         BE    IECI&X                                              000H
IEC1&X   EQU   *                                               @SA78113
         TM    4(15),X'40'              IS THE CCW COMMAND CHAINED? 00H
         BNO   IEC0&X         IF NOT, EXIT WITH RTRN CODE X10  @SA74737
         B     IECH&X                   TRY NEXT CCW.              000H
IEC0&X   LA    15,16          SET RTRN CODE TO X'10'           @SA74737
         B     IECB&X                  EXIT                    @SA74737
*           LINE #'S 768500 AND 774000 REMOVED                   A66477
IECI&X   CLI   8(15),X'0A'     IS NEXT CCW INHIBIT ?             A66477
         AIF   ('&SPEC' EQ 'HIO2740').HAL2740                 SB A60866
         AIF   (T'&SPEC EQ 'O').NO2740                        SB A60866
         MNOTE *,'***          SPEC &SPEC NOT RECOGNIZED.'    SB A60866
         AGO   .NO2740                                        SB A60866
.HAL2740 ANOP                                                 SB A60866
         BE    IECP&X                  YES, DO THE HIO        SB A60866
         CLI   13(15),X'11'            READ DATA ?            SB A60866
         BE    IECP&X                  YES, DO THE HIO        SB A60866
         CLI   13(15),X'91'            LAST A READ DATA ?     SB A60866
         BE    IECP&X                  YES, BRANCH               A66477
*        THE ABOVE GENERATED CODE WILL CAUSE A HALT I/O TO    SB A60866
*        BE INDISCRIMINENTLY ISSUED TO A 2740                 SB A60866
         AGO   .YES2740                                       SB A60866
.NO2740  ANOP                                                 SB A60866
         BE    IECP&X                  YES, DO THE HALT I/O    @SA74703
.YES2740 ANOP                                                 SB A60866
        CLI   13(15),X'0B'             THIRD CCW READ RESPONSE   A66477
        BE    IECP&X                   YES, BRANCH               A66477
        CLI   13(15),X'8B'             LAST CCW READ RESPONSE    A66477
        BNE   IECE&X                   NO, BRANCH                A66477
.*
.END     ANOP                                                    A28606
IECP&X   SR    15,15                                               000B
         IC    15,32(1)       GET EXTENT NUMBER.
         SLL   15,2           MULTIPLY EXTENT NUMBER BY 4.
         L     14,44(14)      GET DEB ADDRESS.
         L     1,32(14,15)    LOAD UCB ADDRESS INTO PARAMTER REG.
         IOHALT (1)
         B     IECB&X
IECY&X   CLI   0(15),X'27'     STILL ENABLE ?                  @SA70024
         BE    IECP&X          YES, RESET                      @SA70024
         B     IECE&X          RETURN                          @SA70024
.*
.ANREND  ANOP
IECK&X   DC    H'0' SAVE AREA FOR WTTA                             000G
*
IECQ&X   LA    15,4                SET RETURN CODE 4           @SA77552
         B     IECB&X              AND EXIT                    @SA77552
*
IECE&X   SR    15,15         RETURN CODE ZERO                    A62267
IECB&X   EQU   *
         MEXIT                                                     000D
.*
.ER      MNOTE 12,'***  IHB078  DECB OPERAND REGISTER NOTATION INVALID-X
               &DECB'
         MEXIT
.ANR     ANOP
         L     15,44(14)           GET ADDR OF DEB
         LA    15,0(15)            CLEAR HIGH-ORDER BYTE
         SLA   0,2                 MULTIPLY RLN BY 4
         AR    15,0                USE RLN*4 AS INDEX
         L     15,28(15)           TO DEBUCBAD-4 AND GET UCB ADDR
         CLI   18(15),X'10'        DEVICE CLASS = GRAPHICS
         BNE   IECO&X              NO, DEVICE IS NOT ANR
         CLI   5(1),X'01'          IS OPERATION READ INITIAL
         BNE   IECX&X              NO, GIVE RC=X'10' AND EXIT
         TM    0(1),X'40'          IS THE OPERATION COMPLETE
         BO    IECQ&X              YES, GIVE RC=4 AND EXIT
         LR    0,1                 DECB ADDR TO REG 0
         LR    1,15                UCB ADDR TO REG 1
         LA    15,4                ROUTING CODE OF 4 IN REG 15
         SVC   116                 INVOKE ESR TO DO RESET
         LR    0,15                LOAD DECB ADDR              @SA77542
         SRL   0,8       SHIFT DECB ADDR TO LOW 3 BYTES        @SA77542
         SLL   15,24               MAKE SURE THAT ONLY         @SA72223
         SRL   15,24               RC IS IN REG 15             @SA72223
         LA    1,4                 ESTABLISH COMPARAND
         CR    15,1                IS RC 0 OR 4
         BH    IECZ&X              RETURN WITH RC              @SA77542
         STH   15,IECK&X           SAVE RETURN CODE
*
         L     1,28(14)            GET IOB BASE FROM DCB
         LA    1,88(1)             GET ADDR OF FIRST IOB
         L     14,44(14)           GET DEB ADDR FROM DCB
         SR    0,0
         IC    0,16(14)            GET NO. OF EXTENTS FROM DEB
*                                  AND USE AS LOOP COUNTER
         LA    14,32(14)           GET ADDR OF FIRST DEB UCB PTR
*
IECT&X   OI    1(1),X'01'          SET RESETPL ISSUED FLAG
         TM    28(1),X'01'         RFT IN PROGRESS
         BO    IECW&X              YES, GIVE R.C. =  0         @SA76579
         L     15,0(14)            GET ADDR OF UCB
         TM    6(15),X'20'         OUTSTANDING I/O OPERATION
         BO    IECU&X              YES, KEEP IOB BUSY
         NI    28(1),X'BF'         TURN OFF IOB BUSY FLAG
*
IECU&X   LA    1,88(1)             STEP TO NEXT IOB
         LA    14,4(14)            STEP TO NEXT UCB PTR
         BCT   0,IECT&X            LOOP IF NOT FINISHED
         LH    15,IECK&X           RESTORE RETURN CODE
         B     IECB&X              EXIT WITH RC
*
IECZ&X   LA    1,16                ESTABLISH COMPARAND         @SA77542
         CR    15,1                IS RC=16                    @SA77542
         BNE   IECB&X              N0,EXIT WITH RC             @SA77542
         LR    1,0                 LOAD DECB ADDR              @SA77542
         TM    0(1),X'40'          IS OPERATION COMPLETE       @SA77542
         BO    IECQ&X              YES,GIVE RC=4 AND EXIT      @SA77542
         B     IECB&X              EXIT WITH ORIGINAL RC       @SA77542
IECW&X   XC    IECK&X,IECK&X       SET RC TO ZERO
         B     IECU&X
*
IECX&X   LA    15,16               SET RETURN CODE OF X'10'
         B     IECB&X              AND EXIT
*
.*
         AIF   ('&TYPE' EQ 'ATTENT').ANRRC8
IECO&X   SRA   0,2                 RESTORE REG 0 (RLN)
         AGO   .NOTANR
.*
.ANRRC8  ANOP
IECO&X   LA     15,8               INVALID DEVICE, SET RC = 8
         B      IECB&X             AND EXIT
         AGO    .ANREND
.*
         MEND                                                      000D
