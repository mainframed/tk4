         MACRO
&NAME    LINK  &EP=,&EPLOC=,&DE=,&PARAM=,&DCB=,&VL=,&HIARCHY=,         X
               &ID=,&SF=I,&MF=I
         GBLB  &IHBSWA,&IHBSWB
.*3781000400,001600,002600                                         LCS0
&IHBSWA  SETB  ('&VL' EQ '1')
&IHBSWB  SETB  ('&SF' EQ '(E,(15))')
         AIF   ('&MF' EQ 'L').E1
         AIF   ('&SF' EQ 'L' AND '&MF&PARAM&ID&VL' NE 'I').E2
         AIF   ('&HIARCHY' NE '').E0
.CONT    AIF   ('&NAME' EQ '' OR '&SF' EQ 'L').CONTA
&NAME    DS    0H
.CONTA   AIF   ('&PARAM' EQ '' AND '&MF(1)' NE 'E').CONTB
         IHBOPLST ,&PARAM,MF=&MF
.CONTB   ANOP
&NAME    IHBINNRB  &EP,&EPLOC,&DE,&DCB,&HIARCHY,SF=&SF
         AIF   ('&SF' EQ 'L').END
         AIF   ('&MF(1)' NE 'E' AND '&SF(1)' NE 'E').LCS
         AIF   ('&HIARCHY' EQ '').LCS
         AIF   ('&HIARCHY' EQ '0').OVER
         MVI   4(15),X'02'
         AGO   .LCS
.OVER    ANOP
         MVI   4(15),X'01'
.LCS     ANOP
         SVC   6                                 ISSUE LINK SVC
         AIF   ('&ID' EQ '').END
         DC    X'4700'                           NOP INSTRUCTION WITH
         DC    AL2(&ID)                          ID IN LAST TWO BYTES
         MEXIT
.E0      AIF   ('&HIARCHY' LT '0' OR '&HIARCHY' GT '1').E3
         AGO   .CONT
.E1      IHBERMAC 84
         MEXIT
.E2      IHBERMAC 75
         MEXIT
.E3      IHBERMAC 195
.END     MEND
