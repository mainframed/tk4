         MACRO
&NAME    STATUS &TYPE,&CLASS,&SCOPE,&MASK,&TCB=                   20021
         LCLB  &A(16)                   MASK VALUES (0 OR 1)
         LCLA  &B                       CODE VALUE (0-5)
         LCLB  &C                       SET/RESET VALUE (0 OR 1)
         LCLB  &D                       TCBLOC STORAGE INDICATOR
         LCLA  &E                       MASK CHARACTER SUBSCRIPT
         LCLA  &F                       MASK SUB-OPERAND SUBSCRIPT
         LCLA  &G                       MASK VALUE SUBSCRIPT
         LCLA  &H                       SCOPE CHARACTER SUBSCRIPT
         LCLB  &I                       TCBLOC REGISTER INDICATOR
         LCLC  &J                       TCBLOC REGISTER NAME
.*
.*     CODE DELETIONS AND/OR CHANGES
.*
.*
         AIF   ('&NAME' EQ '').NONAME .                        @JK64971
&NAME    EQU   * .                                             @JK64971
.NONAME  AIF   ('&TYPE' EQ 'START' OR '&TYPE' EQ 'STOP').SSCK     20021
.*                                 LABEL ADDED FOR             @JK64971
.*
.* TYPE OPERAND SCAN.
.*
         AIF   (T'&TYPE EQ 'O').ERROR10
&C       SETB  ('&TYPE' EQ 'RESET')
         AIF   (NOT &C AND '&TYPE' NE 'SET').ERROR1
.CONT1   ANOP
.*
.* CLASS OPERAND SCAN.
.*
         AIF   (T'&CLASS EQ 'O').ERROR11
         AIF   ('&CLASS' EQ 'MC').SCOPEMC
         AIF   ('&CLASS' EQ 'NR').SCOPENR
         AIF   ('&CLASS' EQ 'ND').SCOPEND
         AGO   .ERROR2
.*
.* SCOPE OPERAND SCAN FOR CLASS=MC.
.*
.SCOPEMC AIF   (T'&MASK NE 'O').ERROR3
         AIF   (T'&SCOPE EQ 'O').CONT2
         AIF   ('&SCOPE' EQ 'STEP').CONT2
         AIF   ('&SCOPE' EQ 'SYSTEM').CONT3
         AGO   .ERROR4
.CONT2   ANOP
&B       SETA  1
         AGO   .CONT9
.CONT3   ANOP
&B       SETA  2
         AGO   .CONT9
.*
.* SCOPE OPERAND SCAN FOR CLASS=NR.
.*
.SCOPENR AIF   (T'&MASK NE 'O').ERROR5
         AIF   (T'&SCOPE EQ 'O').CONT4
         AIF   ('&SCOPE' EQ '''S''').CONT4
&H       SETA  K'&SCOPE
         AIF   ('&SCOPE'(1,1) EQ '''').ERROR6
         AIF   ('&SCOPE'(&H,1) EQ '''').ERROR6
         AIF   ('&SCOPE'(1,1) EQ '(').CONT31
         AIF   ('&SCOPE'(&H,1) EQ ')').ERROR6
         AIF   (K'&SCOPE GT 8).ERROR6
&D       SETB  1
         AGO   .CONT4
.CONT31  ANOP
         AIF   ('&SCOPE'(&H,1) NE ')').ERROR6
         AIF   (K'&SCOPE GT 10).ERROR6
         AIF   (K'&SCOPE LT 3).ERROR6
&H       SETA  &H-2
&J       SETC  '&SCOPE'(2,&H)
&I       SETB  1
.CONT4   ANOP
&B       SETA  0
         AGO   .CONT9
.*
.* SCOPE OPERAND SCAN FOR CLASS=ND.
.*
.SCOPEND AIF   (T'&MASK EQ 'O').ERROR7
         AIF   (T'&SCOPE EQ 'O').CONT5
         AIF   ('&SCOPE' EQ 'STEP').CONT5
         AIF   ('&SCOPE' EQ 'SYSTEM').CONT6
&H       SETA  K'&SCOPE
         AIF   ('&SCOPE'(1,1) EQ '''').ERROR8
         AIF   ('&SCOPE'(&H,1) EQ '''').ERROR8
         AIF   ('&SCOPE'(1,1) EQ '(').CONT41
         AIF   ('&SCOPE'(&H,1) EQ ')').ERROR8
         AIF   (K'&SCOPE GT 8).ERROR8
&D       SETB  1
         AGO   .CONT42
.CONT41  ANOP
         AIF   ('&SCOPE'(&H,1) NE ')').ERROR8
         AIF   (K'&SCOPE GT 10).ERROR8
         AIF   (K'&SCOPE LT 3).ERROR8
&H       SETA  &H-2
&J       SETC  '&SCOPE'(2,&H)
&I       SETB  1
.CONT42  ANOP
&B       SETA  5
         AGO   .CONT7
.CONT5   ANOP
&B       SETA  3
         AGO   .CONT7
.CONT6   ANOP
&B       SETA  4
.*
.* MASK OPERAND SCAN FOR CLASS=ND.
.*
.CONT7   ANOP
         AIF   ('&MASK'(1,1) NE '(').ERROR9
         AIF   (K'&MASK GT 40).ERROR9
&E       SETA  K'&MASK
         AIF   ('&MASK'(&E,1) NE ')').ERROR9
         AIF   (N'&MASK EQ 0).ERROR9
         AIF   (N'&MASK GT 16).ERROR9
.CONT8   ANOP
         AIF   (&F EQ N'&MASK).CONT9
&F       SETA  &F+1
         AIF   ('&MASK(&F)' LT '1').ERROR9
         AIF   ('&MASK(&F)' GT '16').ERROR9
&G       SETA  &MASK(&F)
         AIF   (&A(&G)).ERROR9
&A(&G)   SETB  1
         AGO   .CONT8
.*
.* CODE GENERATION SEQUENCE.
.*
.CONT9   ANOP
         CNOP  0,4
         B     *+12               BRANCH AROUND CONSTANT    JK A64971
         DC    BL2'&A(1)&A(2)&A(3)&A(4)&A(5)&A(6)&A(7)&A(8)&A(9)&A(10)&X
               A(11)&A(12)&A(13)&A(14)&A(15)&A(16)'    MASK
         DC    AL2(&B)                   CODE
         DC    BL1'&C.0000000'            SET/RESET INDICATOR
         DC    AL3(0)                   CONSTANT OF ZERO
         L     0,*-8                    LOAD MASK AND CODE
         AIF   (&D).CONT10
         AIF   (&I).CONT11
         SR    1,1                      CLEAR REGISTER
         O     1,*-10                   'OR IN' SET/RESET INDICATOR
         AGO   .CONT12
.CONT10  ANOP
         L     1,&SCOPE                 LOAD TCB ADDRESS
         O     1,*-12                   'OR IN' SET/RESET INDICATOR
         AGO   .CONT12
.CONT11  ANOP
         LR    1,&J                     LOAD TCB ADDRESS
         O     1,*-10                   'OR IN' SET/RESET INDICATOR
         AGO   .CONT12
.SSCK     ANOP                                                    20021
 AIF (T'&CLASS NE 'O' OR T'&SCOPE NE 'O' OR T'&MASK NE 'O').ERROR12
         AIF   ('&TYPE' EQ 'STOP').STPCK                          20021
         AIF   ('&TYPE' NE 'START').ERROR1                        20021
.STRTCK  LA    0,7                     PUT START CODE IN REG 0    20021
         AIF   ('&TCB' EQ '').ALTSK                               20021
         AIF   ('&TCB'(1,1) EQ '(').TCBREG                        20021
         L     1,&TCB(1)               PUT TCB ADDR IN REG 1      20021
         AGO   .CONT12                                            20021
.TCBREG  LR    1,&TCB(1)               PUT TCB ADDR IN REG 1      20021
         AGO   .CONT12                                            20021
.STPCK   LA    0,6                     PUT STOP CODE IN REG 0     20021
         AIF   ('&TCB' EQ '').ALTSK                               20021
         AIF   ('&TCB'(1,1) EQ '(').TCBREG                        20021
         L     1,&TCB(1)               GET TCB ADDRESS            20021
         AGO   .CONT12                                            20021
.ALTSK   SR    1,1                     INDICATE ALL TASK CHANGE   20021
.CONT12  ANOP
         SVC   79                       ISSUE STATUS SVC
         MEXIT
.*
.* ERROR EXITS.
.*
         MEXIT
.ERROR1  MNOTE 12,'TYPE OPERAND &TYPE INVALID'
         MEXIT
.ERROR2  MNOTE 12,'CLASS OPERAND &CLASS INVALID'
         MEXIT
.ERROR3  MNOTE 12,'INVALID OPERAND FOR MC OPTION'
         MEXIT
.ERROR4  MNOTE 12,'SCOPE OPERAND &SCOPE INVALID'
         MEXIT
.ERROR5  MNOTE 12,'INVALID OPERAND FOR NR OPTION'
         MEXIT
.ERROR6  MNOTE 12,'SCOPE OPERAND &SCOPE INVALID'
         MEXIT
.ERROR7  MNOTE 12,'REQUIRED OPERAND NOT SUPPLIED WITH ND OPTION'
         MEXIT
.ERROR8  MNOTE 12,'SCOPE OPERAND &SCOPE INVALID'
         MEXIT
.ERROR9  MNOTE 12,'MASK OPERAND &MASK INVALID'
         MEXIT
.ERROR10 MNOTE 12,'TYPE OPERAND OMITTED'
         MEXIT
.ERROR11 MNOTE 12,'CLASS OPERAND OMITTED'
         MEXIT                                                    20021
.ERROR12 MNOTE 12,'INVALID POSITIONAL START/STOP OPERAND'         20021
         MEND
