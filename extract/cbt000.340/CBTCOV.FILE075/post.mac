         MACRO
&LABEL   POST   &ECB,&CODE,&TJID=,&TCB=,&MF=I
.*
.*  DEFINE LOCAL VARIABLES
.*
         LCLC   &LIST,&MASK,&NX
&LIST    SETC   'PST'.'&SYSNDX'
&MASK    SETC   'POST'.'&SYSNDX'
.*
.*  LIST FORMAT DOES NOT ALLOW THE CODE PARAMETER
         AIF    ('&MF' EQ 'L').LIST1
.*
.*  SORT OUT POSSIBLE CONDITION CODE FORMATS
.*
         AIF    ('&CODE' EQ '' OR '&CODE' EQ '0').C1
         AIF    ('&CODE'(1,1) EQ '(').C2
         AIF    (T'&CODE NE 'N').C3
         AIF    (&CODE GT 4095).C4
.*
.*  ESTABLISH THE POST LABLE AND SET CONDITION CODE IN REG ZERO
.*
.* CODE IS NUMERIC, LESS THAN 4096
&LABEL   LA    0,&CODE     . SET POST CODE REG ZERO
         AGO   .TYPE
.*
.* CODE IS BLANK OR ZERO, SET ZERO CONDITION CODE
.C1      ANOP
&LABEL   SR    0,0                    . SET POST CODE REG ZERO
         AGO   .TYPE
.*
.* CODE IS IN A REGISTER
.C2      AIF   (N'&CODE GT 1).ERR3
         AIF   ('&CODE(1)' EQ '0').C2A
&LABEL   LR    0,&CODE(1)             . SET POST CODE REG ZERO
         AGO   .TYPE
.C2A     AIF   ('&LABEL' EQ '').TYPE
&LABEL   DS    0H
         AGO   .TYPE
.* CODE IS NON-NUMERIC, E.G., A LABEL
.C3      ANOP
&LABEL   LA    0,&CODE                 . SET POST CODE REG ZERO
         AGO   .TYPE
.* CODE IS A NUMBER GREATER THAN 4095
.C4      CNOP  0,4
&LABEL   B     *+8                     . BRANCH AROUND CONSTANT
         DC    A(&CODE)                . FULLWORD CODE VALUE
         L     0,*-4                   . LOAD POST CODE REG ZERO
.*
.*  SORT MACRO FORM: IN-LINE OR NOT, SIMPLE OR INTERREGIONAL.  THE
.*  IN-LINE INTERREGIONAL FORM MUST HAVE THE TJID CODED,BUT MAY
.*  DEFAULT THE TCB TO ZERO; THE OLD FORM MAY HAVE NEITHER.
.*
.TYPE    AIF   ('&MF' NE 'I' OR '&TJID' NE '').INTRREG
         AIF   ('&TCB' NE '').ERR1
.*
.*  OLD FORM IN-LINE POST
.*    R0 IS ALREADY SET WITH THE COMPLETION CODE
.*    R1 NEEDS THE ECB ADDRESS
         AIF   ('&ECB' EQ '').ERR2    . ECB MUST BE CODED
         AIF   ('&ECB'(1,1) NE '(').S1 . LABEL
         LA    1,0(0,&ECB(1))         . LOAD ECB REG 1, CLEAR HI BYTE
         AGO   .SVC
.S1      LA    1,&ECB                 . LOAD ECB ADDRESS IN REG 1
         AGO   .SVC
.*
.*  INTERREGIONAL REQUESTS
.*    R0  CONTAINS THE CONDITION CODE
.*    R1  CONTAINS THE ADDRESS OF A LIST IN THE FORMAT:
.*          WORD 1  ECB ADDRESS
.*          WORD 2  TJID ADDRESS
.*          WORD 3  TCB ADDRESS
.*         WITH THE HIGH ORDER BIT IN R1 ON TO INDICATE THE
.*         REQUEST TYPE
.*
.INTRREG AIF   (T'&ECB EQ 'O').I1       SKIP SUBSTRING TEST
         AIF   ('&ECB'(1,1) EQ '(' AND N'&ECB GT 1).ERR3
.I1      AIF   (T'&TJID EQ 'O').I2        SKIP SUBSTRING TEST
         AIF   ('&TJID'(1,1) EQ '(' AND N'&TJID GT 1).ERR3
.I2      AIF   (T'&TCB EQ 'O').I3
         AIF   ('&TCB'(1,1) EQ '(' AND N'&TCB GT 1).ERR3
.I3      AIF   ('&MF' NE 'I').EXECUTE
.*
.*  INTERREGIONAL IN-LINE POST
.*
.*   SET REGISTER INDICATED VALUES IN THE PARAMETER LIST
         AIF   ('&ECB' EQ '').ERR2      ECB PARAMETER MUST BE CODED
         AIF   ('&ECB'(1,1) NE '(').R1
         ST    &ECB(1),&LIST          . SET THE ECB ADDRESS
         AGO   .R1A
.R1      LA    1,&ECB                 . ADDRESS THE ECB
         ST    1,&LIST                . STORE IN PARM LIST
.R1A     AIF   (T'&TJID EQ 'O' OR '&TJID' EQ '0').R2A
         AIF   ('&TJID'(1,1) NE '(').R2
         ST    &TJID(1),&LIST+4       . SET THE TJID ADDRESS
         AGO   .R2A
.R2      LA    1,&TJID                . ADDRESS THE TJID WORD
         ST    1,&LIST+4              . STORE IN PARM LIST
.R2A     AIF   (T'&TCB EQ 'O' OR '&TCB' EQ '0').R3A
         AIF   ('&TCB'(1,1) NE '(').R3
         ST    &TCB(1),&LIST+8        . SET THE TCB ADDRESS
         AGO   .R3A
.R3      LA    1,&TCB                 . ADDRESS THE TCB
         ST    1,&LIST+8              . STORE IN PARM LIST
.*
.* ADDRESS AND DEFINE THE PARAMETER LIST
.R3A     CNOP  0,4
         BAL   1,*+16                 . ADDRESS PARM LIST AND BYPASS IT
&LIST    DC    A(0)                   . 1ST WORD - ECB ADDRESS
         DC    A(0)                   . 2ND WORD - TJID ADDRESS
         DC    A(0)                   . 3RD WORD - TCB ADDRESS
         AGO   .SVC
.*
.*  INTERREGIONAL LIST FORM
.*
.LIST1   AIF   ('&CODE' NE '').ERR4     CC NOT ALLOWED IN LIST
.L1A     AIF   (T'&TJID EQ 'O').L1B
         AIF   ('&TJID'(1,1) EQ '(').ERR5
.L1B     AIF   (T'&TCB EQ 'O').L1C
         AIF   ('&TCB'(1,1) EQ '(').ERR5
.L1C     AIF   ('&ECB' EQ '').L2
         AIF   ('&ECB'(1,1) EQ '(').ERR5
&LABEL   DC    A(&ECB)                . 1ST WORD - ECB ADDRESS
         AGO   .L3
.L2      ANOP
&LABEL   DC    A(0)                   . 1ST WORD - ECB ADDRESS
.L3      AIF   ('&TJID' EQ '').L4
         DC    A(&TJID)               . 2ND WORD - TJID ADDRESS
         AGO   .L5
.L4      DC    A(0)                   . 2ND WORD - TJID ADDRESS
.L5      AIF   ('&TCB' EQ '').L6
         DC    A(&TCB)                . 3RD WORD - TCB ADDRESS
         MEXIT
.L6      DC    A(0)                   . 3RD WORD - TCB ADDRESS
         MEXIT
.*
.*  INTERREGIONAL EXECUTE FORM
.*
.EXECUTE AIF   ('&MF'(1,1) NE '(').ERR6
         AIF   ('&MF(1)' NE 'E').ERR6
         AIF   (N'&MF NE 2).ERR6
.*   LIST PARAMETER
         AIF   ('&MF(2)'(1,1) EQ '(').X2   CHECK LIST IN A REG
&NX      SETC  '+'                             'PLUS' A LABEL
.*
.*   ECB PARAMETER
.X2      AIF   ('&ECB' EQ '').X4
         AIF   ('&ECB'(1,1) EQ '(').X3
         LA    15,&ECB               . ADDRESS OF ECB
         ST    15,0&NX&MF(2)         . STORE ECB ADDRESS
         AGO   .X4
.X3      ST    &ECB(1),0&NX&MF(2)    . STORE ECB ADDRESS
.*
.*   TJID PARAMETER
.X4      AIF   ('&TJID' EQ '').X6
         AIF   ('&TJID'(1,1) EQ '(').X5
         LA    15,&TJID              . ADDRESS OF TJID
         ST    15,4&NX&MF(2)         . STORE TJID ADDRESS
         AGO   .X6
.X5      ST    &TJID(1),4&NX&MF(2)   . STORE TJID ADDRESS
.*
.*   TCB PARAMETER
.X6      AIF   ('&TCB' EQ '').X8
         AIF   ('&TCB'(1,1) EQ '(').X7
         LA    15,&TCB               . ADDRESS OF TCB
         ST    15,8&NX&MF(2)         . STORE TCB ADDRESS
         AGO   .X8
.X7      ST    &TCB(1),8&NX&MF(2)    . STORE TCB ADDRESS
.*
.*  ADDRESS LIST AND SET ON HIGH ORDER BIT
.X8      AIF   ('&MF(2)'(1,1) EQ '(').X9
         LA    1,&MF(2)               . ADDRESS LIST
         AGO   .X10
.X9      IHBINNRA &MF(2)              . SET REG 1
.X10     CNOP  0,4
         O     1,&MASK                . SET LIST FORM INDICATOR
         B     *+8                    . BRANCH AROUND MASK
&MASK    DC    X'80000000'
.*
.*  THE SVC CALL
.*
.SVC     SVC   2                      . ISSUE THE POST SVC
         MEXIT
.*
.*  ERROR EXITS
.*
.ERR1    ANOP
         IHBERMAC  1010,TCB,TJID        TCB OPERAND REQUIRES TJID OP
         MEXIT
.ERR2    ANOP
         IHBERMAC  11                   ECB MISSING
         MEXIT
.ERR3    ANOP
         IHBERMAC  203                  REGISTER SPECIFICATION INVALID
         MEXIT
.ERR4    ANOP
         IHBERMAC  1009                 NO CC ALLOWED IN LIST FORM
         MEXIT
.ERR5    ANOP
         IHBERMAC  69                   INVALID REG SPEC WITH MF=L
         MEXIT
.ERR6    ANOP
         IHBERMAC  35,,&MF              EXECUTE FORM MF= ERROR
         MEND
