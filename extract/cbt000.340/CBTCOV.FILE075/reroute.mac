         MACRO
&NAME    REROUTE &PAR1,&PAR2
         LCLB  &NOGO
         AIF   ('&PAR1' NE '' AND '&PAR2' NE '').OK
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      AIF   (K'&PAR1 LT 6 OR K'&PAR1 GT 9 OR '&PAR1'(K'&PAR1,1) NE 'X
               ''').ER1
         AIF   (K'&PAR1 EQ 7 AND '&PAR1'(1,2) EQ 'X''').OK1
         AIF   ('&PAR1'(1,4) EQ 'XL2''').OK1
.ER1     MNOTE 12,'***  IHB002  INVALID ERR. HALF WORD OPERAND SPECIFIEX
               D'
&NOGO    SETB  1
.OK1     AIF   ('&PAR2'(1,1) NE '=').OK2
         AIF   (K'&PAR2 LT 5 OR K'&PAR2 GT 14).ER2
         AIF   ('&PAR2'(2,1) NE 'C' OR '&PAR2'(K'&PAR2,1) NE '''').ER2
         AIF   ('&PAR2'(3,1) EQ 'L' AND '&PAR2'(5,1) EQ '''' AND '&PAR2X
               '(4,1) LT '9' AND '&PAR2'(4,1) GT '0').OK2
         AIF   ('&PAR2'(2,2) EQ 'C''' AND K'&PAR2 LT 13).OK2
.ER2     MNOTE 12,'***  IHB002  INVALID DESTINATION OPERAND SPECIFIED'
         AGO   .END
.OK2     AIF   (&NOGO).END
         AIF   ('&PAR2' NE 'SOURCE').LA
&NAME    L     2,=A(TERMTBL)           ADDRESS OF THE TERMINAL TABLE
         LH    9,10(6)                 TTSKEY
         LA    2,9(2,9)                ADDRESS OF TERM ENTRY ID FIELD
         AGO   .SETSREG
.LA      ANOP
&NAME    LA    2,&PAR2                 ADDRESS OF TERM NAME
.SETSREG L     15,=V(IECKRRTE)         ADDRESS OF REROUTE ROUTINE
         BALR  14,15                   BRANCH TO REROUTE ROUTINE
         DC    &PAR1
.END     MEND
