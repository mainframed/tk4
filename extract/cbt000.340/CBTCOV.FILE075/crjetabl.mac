         MACRO
&TABLNAM CRJETABL &JOB=,&USERS=,&SYSCRJE=,&JOBEXIT=,&ONEXIT=,&OFFEXIT=,C
               &BUFNO=1,&MSGNO=100,&BRDCST=100,&OUTNO=10,&MSGRC=8,     C
               &ALIAS=,&USRMCMD=,&USRSCMD=,&CMDEXIT=,&PL1LNO=2,        C
               &FORTLNO=2
         GBLA  &IHKLINS,&AUSERS
         GBLB  &IHKTABL,&GNOGO,&G274041,&GTABLE1,&GTABLE2,&GTABLE3
         GBLB  &GTABLE4
         GBLC  &G1050(100),&G1050D(100)
         LCLA  &MMAX,&M1,&SMAX,&S1,&INDEX,&LNOGO,&M2,&S2,&A
         LCLA  &INDEX1,&INDEX2,&TEMP,&ATEST,&KTEST
         LCLB  &NOJOB,&NOON,&NOOFF,&NOEXIT,&NOMCMD,&NOSCMD,&NOALIAS
         LCLB  &FOUND
         LCLC  &STAB(150),&USRCEX,&USRONEX,&USRJOEX,&TAB5,&CHAR
         LCLC  &USROFEX,&INCODE,&MTAB(150),&TAB1,&TAB2,&TAB3,&TAB4
         ACTR  20000
.*       CHECK FOR CORRECT MACRO SEQUENCING
         AIF   (NOT &IHKTABL).CHECK
         MNOTE 8,'ONLY ONE CRJETABL MACRO MAY BE SPECIFIED, GENERATION C
                  TERMINATED'
&GNOGO   SETB  1
         MEXIT
.CHECK   AIF   (&IHKLINS NE 0).CHKJOB1
         MNOTE 8,'CRJELINE MACROS MUST PRECEDE CRJETABL MACRO'
&GNOGO   SETB  1
.*       OPERAND VALIDIZATION
.CHKJOB1 ANOP
&IHKTABL SETB  1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF JOB OPERAND
         AIF   ('&JOB' NE '').CHKJOB2
         MNOTE 4,'JOB KEYWORD OPERAND OMITTED'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKUSR1
.CHKJOB2 AIF   (T'&JOB EQ 'N').CHKJOB3
         MNOTE 4,'JOB OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKUSR1
.CHKJOB3 ANOP
&ATEST   SETA  &JOB
         AIF   (&ATEST GE 1 AND &ATEST LE 999).CHKUSR1
         MNOTE 4,'JOB OPERAND NOT BETWEEN 1 AND 999 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF USERS OPERAND
.CHKUSR1 AIF   ('&USERS' NE '').CHKUSR2
         MNOTE 4,'USERS KEYWORD OPERAND OMITTED'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKSYS1
.CHKUSR2 AIF   (T'&USERS EQ 'N').CHKUSR3
         MNOTE 4,'USERS OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKSYS1
.CHKUSR3 ANOP
&AUSERS  SETA  &USERS
         AIF   (&AUSERS GE 1 AND &AUSERS LE 9999).CHKSYS1
         MNOTE 4,'USERS OPERAND NOT BETWEEN 1 AND 9,999 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       SYSCRJE OPERAND
.CHKSYS1 AIF   ('&SYSCRJE' NE '').CHKSYS2
         MNOTE 4,'SYSCRJE KEYWORD OPERAND OMITTED'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKJOB4
.CHKSYS2 AIF   (K'&SYSCRJE EQ 1).CHKTAB
         MNOTE 4,'SYSCRJE OPERAND EXCEEDS ONE CHARACTER IN LENGTH'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKJOB4
.CHKTAB  ANOP
.*       TABLES FOR VALID VALUES OF SYSCRJE OPERAND
&TAB1    SETC  'ABCDEFGH'
&TAB2    SETC  'IJKLMNOP'
&TAB3    SETC  'QRSTUVWX'
&TAB4    SETC  'YZ012345'
&TAB5    SETC  '6789'
&M1      SETA  0
&M2      SETA  0
.CHKTAB1 ANOP
&M1      SETA  &M1+1
         AIF   (&M1 GT 8).CHKTAB2
         AIF   ('&SYSCRJE' EQ '&TAB1'(&M1,1)).COMPIT
         AGO   .CHKTAB1
.CHKTAB2 ANOP
&M1      SETA  0
&M2      SETA  1
.CHKTAB3 ANOP
&M1      SETA  &M1+1
         AIF   (&M1 GT 8).CHKTAB4
         AIF   ('&SYSCRJE' EQ '&TAB2'(&M1,1)).COMPIT
         AGO   .CHKTAB3
.CHKTAB4 ANOP
&M1      SETA  0
&M2      SETA  2
.CHKTAB5 ANOP
&M1      SETA  &M1+1
         AIF   (&M1 GT 8).CHKTAB6
         AIF   ('&SYSCRJE' EQ '&TAB3'(&M1,1)).COMPIT
         AGO   .CHKTAB5
.CHKTAB6 ANOP
&M1      SETA  0
&M2      SETA  3
.CHKTAB7 ANOP
&M1      SETA  &M1+1
         AIF   (&M1 GT 8).CHKTAB8
         AIF   ('&SYSCRJE' EQ '&TAB4'(&M1,1)).COMPIT
         AGO   .CHKTAB7
.CHKTAB8 ANOP
&M1      SETA  0
&M2      SETA  4
.CHKTAB9 ANOP
&M1      SETA  &M1+1
         AIF   (&M1 GT 4).ERROR6
         AIF   ('&SYSCRJE' EQ '&TAB5'(&M1,1)).COMPIT
         AGO   .CHKTAB9
.ERROR6  MNOTE 4,'SYSCRJE OPERAND SPECIFIED INCORRECTLY'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKJOB4
.COMPIT  ANOP
&S2      SETA  8*&M2+&M1+2
&INCODE  SETC  '&S2'
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       JOBEXIT OPERAND
.CHKJOB4 ANOP
&NOJOB   SETB  ('&JOBEXIT' EQ '')
         AIF   (&NOJOB).CHKON1
         AIF   (K'&JOBEXIT LE 8).CHKJOB5
         MNOTE 4,'JOBEXIT OPERAND EXCEEDS EIGHT CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
.CHKJOB5 ANOP
&CHAR    SETC  '&JOBEXIT'(1,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKJOB6
         MNOTE 4,'JOBEXIT OPERAND CONTAINS NON-ALPHABETIC FIRST        C
                  CHARACTER'
&LNOGO   SETA  &LNOGO+1
.CHKJOB6 ANOP
&A       SETA  1
.CHKJOB7 ANOP
&A       SETA  &A+1
         AIF   (&A GT K'&JOBEXIT).CHKJOB8
&CHAR    SETC  '&JOBEXIT'(&A,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKJOB7
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHKJOB7
         MNOTE 4,'JOBEXIT OPERAND CONTAINS NON-ALPHAMERIC CHARACTER(S)'
&LNOGO   SETA  &LNOGO+1
.CHKJOB8 AIF   ('&JOBEXIT'(1,3) NE 'IHK').CHKON1
         MNOTE 4,'JOBEXIT OPERAND CANNOT BEGIN WITH ''IHK'' '
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       ONEXIT OPERAND
.CHKON1  ANOP
&NOON    SETB  ('&ONEXIT' EQ '')
         AIF   (&NOON).CHKOFF1
         AIF   (K'&ONEXIT LE 8).CHKON2
         MNOTE 4,'ONEXIT OPERAND EXCEEDS EIGHT CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
.CHKON2  ANOP
&CHAR    SETC  '&ONEXIT'(1,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKON3
         MNOTE 4,'ONEXIT OPERAND CONTAINS NON-ALPHABETIC FIRST         C
                  CHARACTER'
&LNOGO   SETA  &LNOGO+1
.CHKON3  ANOP
&A       SETA  1
.CHKON4  ANOP
&A       SETA  &A+1
         AIF   (&A GT K'&ONEXIT).CHKON5
&CHAR    SETC  '&ONEXIT'(&A,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKON4
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHKON4
         MNOTE 4,'ONEXIT OPERAND CONTAINS NON-ALPHAMERIC CHARACTER(S)'
&LNOGO   SETA  &LNOGO+1
.CHKON5  AIF   ('&ONEXIT'(1,3) NE 'IHK').CHKOFF1
         MNOTE 4,'ONEXIT OPERAND CANNOT BEGIN WITH ''IHK'' '
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       OFFEXIT OPERAND
.CHKOFF1 ANOP
&NOOFF   SETB  ('&OFFEXIT' EQ '')
         AIF   (&NOOFF).CHKBUF1
         AIF   (K'&OFFEXIT LE 8).CHKOFF2
         MNOTE 4,'OFFEXIT OPERAND EXCEEDS EIGHT CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
.CHKOFF2 ANOP
&CHAR    SETC  '&OFFEXIT'(1,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKOFF3
         MNOTE 4,'OFFEXIT OPERAND CONTAINS NON-ALPHABETIC FIRST        C
                  CHARACTER'
&LNOGO   SETA  &LNOGO+1
.CHKOFF3 ANOP
&A       SETA  1
.CHKOFF4 ANOP
&A       SETA  &A+1
         AIF   (&A GT K'&OFFEXIT).CHKOFF5
&CHAR    SETC  '&OFFEXIT'(&A,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKOFF4
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHKOFF4
         MNOTE 4,'OFFEXIT OPERAND CONTAINS NON-ALPHAMERIC CHARACTER(S)'
&LNOGO   SETA  &LNOGO+1
.CHKOFF5 AIF   ('&OFFEXIT'(1,3) NE 'IHK').CHKBUF1
         MNOTE 4,'OFFEXIT OPERAND CANNOT BEGIN WITH ''IHK'' '
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF BUFNO OPERAND
.CHKBUF1 AIF   (T'&BUFNO EQ 'N').CHKBUF2
         MNOTE 4,'BUFNO OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKMSG1
.CHKBUF2 ANOP
&ATEST   SETA  &BUFNO
         AIF   (&ATEST GE 1 AND &ATEST LE 100).CHKMSG1
         MNOTE 4,'BUFNO OPERAND NOT BETWEEN 1 AND 100 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF MSGNO OPERAND
.CHKMSG1 AIF   (T'&MSGNO EQ 'N').CHKMSG2
         MNOTE 4,'MSGNO OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKBRD1
.CHKMSG2 ANOP
&ATEST   SETA  &MSGNO
         AIF   (&ATEST GE 1 AND &ATEST LE 999).CHKBRD1
         MNOTE 4,'MSGNO OPERAND NOT BETWEEN 1 AND 999 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF BRDCST OPERAND
.CHKBRD1 AIF   (T'&BRDCST EQ 'N').CHKBRD2
         MNOTE 4,'BRDCST OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKOUT2
.CHKBRD2 ANOP
&ATEST   SETA  &BRDCST
         AIF   (&ATEST GE 1 AND &ATEST LE 100).CHKOUT2
         MNOTE 4,'BRDCST OPERAND NOT BETWEEN 1 AND 100 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF OUTNO OPERAND
.CHKOUT2 AIF   (T'&OUTNO EQ 'N').CHKOUT3
         MNOTE 4,'OUTNO OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKMRC1
.CHKOUT3 ANOP
&ATEST   SETA  &OUTNO
         AIF   (&ATEST GT 0 AND &ATEST LT 65535).CHKMRC1
         MNOTE 4,'OUTNO OPERAND NOT BETWEEN 1 AND 65535 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF MSGRC OPERAND
.CHKMRC1 AIF   (T'&MSGRC EQ 'N').CHKMRC2
         MNOTE 4,'MSGRC OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKEXIT
.CHKMRC2 ANOP
&ATEST   SETA  &MSGRC
         AIF   (&ATEST GE 1 AND &ATEST LE 16).CHKEXIT
         MNOTE 4,'MSGRC OPERAND NOT BETWEEN 1 AND 16 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       CMDEXIT OPERAND
.CHKEXIT ANOP
&NOEXIT  SETB  ('&CMDEXIT' EQ '')
         AIF   (&NOEXIT).GENTAB
         AIF   (K'&CMDEXIT LE 8).CHKCMD1
         MNOTE 4,'CMDEXIT OPERAND EXCEEDS EIGHT CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
.CHKCMD1 ANOP
&CHAR    SETC  '&CMDEXIT'(1,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKCMD2
         MNOTE 4,'CMDEXIT OPERAND CONTAINS NON-ALPHABETIC FIRST        C
                  CHARACTER'
&LNOGO   SETA  &LNOGO+1
.CHKCMD2 ANOP
&A       SETA  1
.CHKCMD3 ANOP
&A       SETA  &A+1
         AIF   (&A GT K'&CMDEXIT).CHKCMD4
&CHAR    SETC  '&CMDEXIT'(&A,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKCMD3
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHKCMD3
         MNOTE 4,'CMDEXIT OPERAND CONTAINS NON-ALPHAMERIC CHARACTER(S)'
&LNOGO   SETA  &LNOGO+1
.CHKCMD4 AIF   ('&CMDEXIT'(1,3) NE 'IHK').GENTAB
         MNOTE 4,'CMDEXIT OPERAND CANNOT BEGIN WITH ''IHK'' '
&LNOGO   SETA  &LNOGO+1
.*       BASIC TABLE OF CRJE MAJOR COMMANDS
.GENTAB  ANOP
&MTAB(1)   SETC '15'
&MTAB(2)   SETC '5'
&MTAB(3)   SETC 'LOGON'
&MTAB(4)   SETC '81'
&MTAB(5)   SETC '6'
&MTAB(6)   SETC 'LOGOFF'
&MTAB(7)   SETC '82'
&MTAB(8)   SETC '6'
&MTAB(9)   SETC 'SUBMIT'
&MTAB(10)  SETC '88'
&MTAB(11)  SETC '6'
&MTAB(12)  SETC 'CANCEL'
&MTAB(13)  SETC '89'
&MTAB(14)  SETC '6'
&MTAB(15)  SETC 'OUTPUT'
&MTAB(16)  SETC '8D'
&MTAB(17)  SETC '8'
&MTAB(18)  SETC 'CONTINUE'
&MTAB(19)  SETC '8E'
&MTAB(20)  SETC '6'
&MTAB(21)  SETC 'LISTBC'
&MTAB(22)  SETC '08'
&MTAB(23)  SETC '6'
&MTAB(24)  SETC 'LISTDS'
&MTAB(25)  SETC 'A6'
&MTAB(26)  SETC '6'
&MTAB(27)  SETC 'STATUS'
&MTAB(28)  SETC '90'
&MTAB(29)  SETC '4'
&MTAB(30)  SETC 'EXEC'
&MTAB(31)  SETC '91'
&MTAB(32)  SETC '6'
&MTAB(33)  SETC 'DELETE'
&MTAB(34)  SETC '92'
&MTAB(35)  SETC '4'
&MTAB(36)  SETC 'SEND'
&MTAB(37)  SETC '97'
&MTAB(38)  SETC '4'
&MTAB(39)  SETC 'EDIT'
&MTAB(40)  SETC '93'
&MTAB(41)  SETC '7'
&MTAB(42)  SETC 'LISTLIB'
&MTAB(43)  SETC 'A7'
&MTAB(44)  SETC '6'
&MTAB(45)  SETC 'TABSET'
&MTAB(46)  SETC '8F'
&MMAX    SETA  46
&M1      SETA  2
&M2      SETA  3
.*       BASIC TABLE OF CRJE EDIT SUBCOMMANDS AND ABBREVIATIONS
&STAB(1)   SETC '24'
&STAB(2)   SETC '5'
&STAB(3)   SETC 'INPUT'
&STAB(4)   SETC '98'
&STAB(5)   SETC '1'
&STAB(6)   SETC 'I'
&STAB(7)   SETC '98'
&STAB(8)   SETC '6'
&STAB(9)   SETC 'DELETE'
&STAB(10)  SETC '07'
&STAB(11)  SETC '1'
&STAB(12)  SETC 'D'
&STAB(13)  SETC '07'
&STAB(14)  SETC '4'
&STAB(15)  SETC 'SCAN'
&STAB(16)  SETC '03'
&STAB(17)  SETC '2'
&STAB(18)  SETC 'SC'
&STAB(19)  SETC '03'
&STAB(20)  SETC '6'
&STAB(21)  SETC 'CHANGE'
&STAB(22)  SETC '99'
&STAB(23)  SETC '1'
&STAB(24)  SETC 'C'
&STAB(25)  SETC '99'
&STAB(26)  SETC '8'
&STAB(27)  SETC 'RENUMBER'
&STAB(28)  SETC '9D'
&STAB(29)  SETC '3'
&STAB(30)  SETC 'REN'
&STAB(31)  SETC '9D'
&STAB(32)  SETC '4'
&STAB(33)  SETC 'SAVE'
&STAB(34)  SETC '9E'
&STAB(35)  SETC '1'
&STAB(36)  SETC 'S'
&STAB(37)  SETC '9E'
&STAB(38)  SETC '4'
&STAB(39)  SETC 'SEND'
&STAB(40)  SETC '97'
&STAB(41)  SETC '6'
&STAB(42)  SETC 'TABSET'
&STAB(43)  SETC '8F'
&STAB(44)  SETC '3'
&STAB(45)  SETC 'TAB'
&STAB(46)  SETC '8F'
&STAB(47)  SETC '4'
&STAB(48)  SETC 'LIST'
&STAB(49)  SETC '01'
&STAB(50)  SETC '1'
&STAB(51)  SETC 'L'
&STAB(52)  SETC '01'
&STAB(53)  SETC '6'
&STAB(54)  SETC 'SUBMIT'
&STAB(55)  SETC '88'
&STAB(56)  SETC '3'
&STAB(57)  SETC 'SUB'
&STAB(58)  SETC '88'
&STAB(59)  SETC '6'
&STAB(60)  SETC 'CANCEL'
&STAB(61)  SETC '89'
&STAB(62)  SETC '2'
&STAB(63)  SETC 'CA'
&STAB(64)  SETC '89'
&STAB(65)  SETC '5'
&STAB(66)  SETC 'MERGE'
&STAB(67)  SETC '9A'
&STAB(68)  SETC '1'
&STAB(69)  SETC 'M'
&STAB(70)  SETC '9A'
&STAB(71)  SETC '3'
&STAB(72)  SETC 'END'
&STAB(73)  SETC '83'
&SMAX    SETA  73
&S1      SETA  2
&S2      SETA  3
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       INSTALLATION DEFINED MAJOR COMMANDS
.CHKMAJ1 ANOP
&NOMCMD  SETB  ('&USRMCMD' EQ '')
         AIF   (&NOMCMD).CHKSUB1
&INDEX   SETA  0
         AIF   (NOT &NOEXIT).CHKMAJ2
         MNOTE 4,'CMDEXIT OPERAND MUST BE SPECIFIED WITH USRMCMD       C
                  OPERAND'
&LNOGO   SETA  &LNOGO+1
.CHKMAJ2 ANOP
&INDEX   SETA  &INDEX+1
         AIF   (K'&USRMCMD(&INDEX) LE 8).CHKMAJ3
         MNOTE 4,'USRMCMD SUBPARAMETER &USRMCMD(&INDEX) EXCEEDS EIGHT  C
                 CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
&NOMCMD  SETB  1
.CHKMAJ3 ANOP
&CHAR    SETC  '&USRMCMD(&INDEX)'(1,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKMAJ4
         MNOTE 4,'NON-ALPHABETIC FIRST CHARACTER ON USRMCMD            C
                  SUBPARAMETER &USRMCMD(&INDEX)'
&LNOGO   SETA  &LNOGO+1
&NOMCMD  SETB  1
.CHKMAJ4 ANOP
&A       SETA  1
.CHKMAJ5 ANOP
&A       SETA  &A+1
         AIF   (&A GT K'&USRMCMD(&INDEX)).CHKMAJ6
&CHAR    SETC  '&USRMCMD(&INDEX)'(&A,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKMAJ5
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHKMAJ5
         MNOTE 4,'NON-ALPHAMERIC CHARACTER(S) IN USRMCMD SUBPARAMETER  C
                  &USRMCMD(&INDEX)'
&LNOGO   SETA  &LNOGO+1
&NOMCMD  SETB  1
.*       CHECK FOR UNIQUENESS OF INSTALLATION DEFINED MAJOR COMMANDS
.CHKMAJ6 AIF   (&NOMCMD).CHKMAJ7
&INDEX1  SETA  0
&INDEX2  SETA  0
.LOOPST1 ANOP
&INDEX1  SETA  &INDEX1+3
         AIF   (&INDEX1 GE &MMAX).ADDMAJ
         AIF   ('&USRMCMD(&INDEX)' NE '&MTAB(&INDEX1)').LOOPST1
.ERROR4  MNOTE 4,'USRMCMD SUBOPERAND &USRMCMD(&INDEX) IS A DUPLICATE'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKMAJ7
.*       ADDITION OF INSTALLATION DEFINED MAJOR COMMANDS
.*       TO MAJOR COMMAND TABLE
.ADDMAJ  ANOP
&TEMP    SETA  &MTAB(1)
&TEMP    SETA  &TEMP+1
&MTAB(1) SETC  '&TEMP'
&KTEST   SETA  K'&USRMCMD(&INDEX)
&MTAB(&MMAX+1) SETC '&KTEST'
&MTAB(&MMAX+2) SETC '&USRMCMD(&INDEX)'
&MTAB(&MMAX+3) SETC '00'
&MMAX    SETA  &MMAX+3
.CHKMAJ7 ANOP
&NOMCMD  SETB  0
         AIF   (&INDEX LE (N'&USRMCMD-1)).CHKMAJ2
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       INSTALLATION DEFINED SUBCOMMANDS
.CHKSUB1 ANOP
&NOSCMD  SETB  ('&USRSCMD' EQ '')
         AIF   (&NOSCMD).CHKALI1
&INDEX   SETA  0
         AIF   (NOT &NOEXIT).CHKSUB2
         MNOTE 4,'CMDEXIT OPERAND MUST BE SPECIFIED WITH USRSCMD       C
                  OPERAND'
&LNOGO   SETA  &LNOGO+1
.CHKSUB2 ANOP
&INDEX   SETA  &INDEX+1
         AIF   (K'&USRSCMD(&INDEX) LE 8).CHKSUB3
         MNOTE 4,'USRSCMD SUBPARAMETER &USRSCMD(&INDEX) EXCEEDS EIGHT  C
                 CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
&NOSCMD  SETB  1
.CHKSUB3 ANOP
&CHAR    SETC  '&USRSCMD(&INDEX)'(1,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKSUB4
         MNOTE 4,'NON-ALPHABETIC FIRST CHARACTER ON USRSCMD            C
                  SUBPARAMETER &USRSCMD(&INDEX)'
&LNOGO   SETA  &LNOGO+1
&NOSCMD  SETB  1
.CHKSUB4 ANOP
&A       SETA  1
.CHKSUB5 ANOP
&A       SETA  &A+1
         AIF   (&A GT K'&USRSCMD(&INDEX)).CHKSUB6
&CHAR    SETC  '&USRSCMD(&INDEX)'(&A,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKSUB5
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHKSUB5
         MNOTE 4,'NON-ALPHAMERIC CHARACTER(S) IN USRSCMD SUBPARAMETER  C
                  &USRSCMD(&INDEX)'
&LNOGO   SETA  &LNOGO+1
&NOSCMD  SETB  1
.*       CHECK FOR UNIQUENESS OF INSTALLATION DEFINED SUBCOMMANDS
.CHKSUB6 AIF   (&NOSCMD).CHKSUB7
&INDEX1  SETA  0
&INDEX2  SETA  0
.LOOPST3 ANOP
&INDEX1  SETA  &INDEX1+3
         AIF   (&INDEX1 GE &SMAX).ADDSUB
         AIF   ('&USRSCMD(&INDEX)' NE '&STAB(&INDEX1)').LOOPST3
.ERROR5  MNOTE 4,'USRSCMD SUBOPERAND &USRSCMD(&INDEX) IS A DUPLICATE'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKSUB7
.*       ADDITION OF INSTALLATION DEFINED SUBCOMMANDS
.*       TO SUBCOMMAND TABLE
.ADDSUB  ANOP
&TEMP    SETA  &STAB(1)
&TEMP    SETA  &TEMP+1
&STAB(1) SETC  '&TEMP'
&KTEST   SETA  K'&USRSCMD(&INDEX)
&STAB(&SMAX+1) SETC '&KTEST'
&STAB(&SMAX+2) SETC '&USRSCMD(&INDEX)'
&STAB(&SMAX+3) SETC '00'
&SMAX    SETA  &SMAX+3
.CHKSUB7 ANOP
&NOSCMD  SETB  0
         AIF   (&INDEX LE (N'&USRSCMD-1)).CHKSUB2
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       MAJOR AND SUBCOMMAND ALIASES
.CHKALI1 ANOP
&NOALIAS SETB  ('&ALIAS' EQ '')
         AIF   (&NOALIAS).CHKPL11
&INDEX   SETA  0
         AIF   ((N'&ALIAS+1)/2 EQ N'&ALIAS/2).CHKALI2
         MNOTE 4,'ALIAS OPERAND CONTAINS ODD SUBPARAMETER'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKPL11
.CHKALI2 ANOP
&INDEX   SETA  &INDEX+2
         AIF   (K'&ALIAS(&INDEX) LE 8).CHKALI3
         MNOTE 4,'ALIAS SUBPARAMETER &ALIAS(&INDEX) EXCEEDS EIGHT      C
                 CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
.CHKALI3 ANOP
&CHAR    SETC  '&ALIAS(&INDEX)'(1,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKALI4
         MNOTE 4,'NON-ALPHABETIC FIRST CHARACTER ON ALIAS SUBPARAMETER C
                  &ALIAS(&INDEX)'
&LNOGO   SETA  &LNOGO+1
.CHKALI4 ANOP
&A       SETA  1
.CHKALI5 ANOP
&A       SETA  &A+1
         AIF   (&A GT K'&ALIAS(&INDEX)).CHKALI6
&CHAR    SETC  '&ALIAS(&INDEX)'(&A,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHKALI5
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHKALI5
         MNOTE 4,'NON-ALPHAMERIC CHARACTER(S) IN ALIAS SUBPARAMETER    C
                  &ALIAS(&INDEX)'
&LNOGO   SETA  &LNOGO+1
.CHKALI6 ANOP
.*       CHECK FOR EXISTENCE OF COMMANDS TO BE ASSIGNED ALIASES
&M2      SETA  3
&S2      SETA  3
.CHKM1   ANOP
         AIF   ('&ALIAS(&INDEX-1)' EQ '&MTAB(&M2)').INSERT
&M2      SETA  &M2+3
         AIF   (&M2 LE &MMAX).CHKM1
.CHKS1   ANOP
         AIF   ('&ALIAS(&INDEX-1)' EQ '&STAB(&S2)').INSERT1
&S2      SETA  &S2+3
         AIF   (&S2 LE &SMAX).CHKS1
         AIF   (NOT &FOUND).ERROR7
&M2      SETA  3
&S2      SETA  3
         AGO   .CHKALI7
.ERROR7  MNOTE 4,'ALIAS SUBPARAMETER &ALIAS(&INDEX-1) NOT FOUND'
&LNOGO   SETA  &LNOGO+1
&M2      SETA  3
&S2      SETA  3
         AGO   .CHKALI7
.*       ADDITION OF ALIASES TO MAJOR COMMAND TABLE
.INSERT  ANOP
&S2      SETA  0
.CHKALID ANOP
&S2      SETA  &S2+3
         AIF   ('&ALIAS(&INDEX)' EQ '&MTAB(&S2)').CHKALIE
         AIF   (&S2 LT &MMAX).CHKALID
         AGO   .INSERT3
.CHKALIE MNOTE 4,'ALIAS SUBPARAMETER &ALIAS(&INDEX) IS A DUPLICATE OF  C
                 AN EXISTING MAJOR COMMAND'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKALI7
.INSERT3 ANOP
&FOUND   SETB  1
&TEMP    SETA  &MTAB(1)
&TEMP    SETA  &TEMP+1
&MTAB(1) SETC  '&TEMP'
&KTEST   SETA  K'&ALIAS(&INDEX)
&MTAB(&MMAX+1) SETC '&KTEST'
&MTAB(&MMAX+2) SETC '&ALIAS(&INDEX)'
&MTAB(&MMAX+3) SETC '&MTAB(&M2+1)'
&MMAX    SETA  &MMAX+3
&M2      SETA  3
&S2      SETA  3
         AGO   .CHKS1
.*       ADDITION OF ALIASES TO SUBCOMMAND TABLE
.INSERT1 ANOP
&M2      SETA  0
.CHKALI8 ANOP
&M2      SETA  &M2+3
         AIF   ('&ALIAS(&INDEX)' EQ '&STAB(&M2)').CHKALI9
         AIF   (&M2 LT &SMAX).CHKALI8
         AGO   .INSERT2
.CHKALI9 AIF   (&FOUND).CHKALI7
         MNOTE 4,'ALIAS SUBPARAMETER &ALIAS(&INDEX) IS A DUPLICATE OF  C
                 AN EXISTING EDIT SUBCOMMAND'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKALI7
.INSERT2 ANOP
&TEMP    SETA  &STAB(1)
&TEMP    SETA  &TEMP+1
&STAB(1) SETC  '&TEMP'
&KTEST   SETA  K'&ALIAS(&INDEX)
&STAB(&SMAX+1) SETC '&KTEST'
&STAB(&SMAX+2) SETC '&ALIAS(&INDEX)'
&STAB(&SMAX+3) SETC '&STAB(&S2+1)'
&SMAX    SETA  &SMAX+3
&M2      SETA  3
&S2      SETA  3
.CHKALI7 ANOP
&FOUND   SETB  0
         AIF   (&INDEX LE (N'&ALIAS-2)).CHKALI2
.*       CHECK FOR CORRECT SPECIFICATION OF PL1LNO OPERAND
.CHKPL11 AIF   (T'&PL1LNO EQ 'N').CHKPL12
         MNOTE 4,'PL1NO OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKFOR1
.CHKPL12 ANOP
&ATEST   SETA  &PL1LNO
         AIF   (&ATEST GE 0 AND &ATEST LE 37).CHKFOR1
         MNOTE 4,'PL1LNO OPERAND NOT BETWEEN 0 AND 37 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF FORTLNO OPERAND
.CHKFOR1 AIF   (T'&FORTLNO EQ 'N').CHKFOR2
         MNOTE 4,'FORTLNO OPERAND OTHER THAN SELF-DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKPNT
.CHKFOR2 ANOP
&ATEST   SETA  &FORTLNO
         AIF   (&ATEST GE 0 AND &ATEST LE 19).CHKPNT
         MNOTE 4,'FORTLNO OPERAND NOT BETWEEN 0 AND 19 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR MACRO TERMINATION CONDITIONS
.CHKPNT  AIF   (&LNOGO EQ 0 AND &GNOGO).ERROR1
         AIF   (&LNOGO NE 0 AND NOT &GNOGO).ERROR2
         AIF   (&LNOGO EQ 0 AND NOT &GNOGO).GENER
.ERROR3  MNOTE 8,'&LNOGO TERMINATING ERROR(S) INCURRED, NO GENERATION'
         MEXIT
.ERROR1  MNOTE 8,'NO TERMINATING ERRORS INCURRED, NO GENERATION DUE TO C
                  PREVIOUS ERRORS'
         MEXIT
.ERROR2  MNOTE 8,'&LNOGO TERMINATING ERROR(S) INCURRED, GENERATION     C
                       TERMINATED'
&GNOGO   SETB  1
         MEXIT
.GENER   ANOP
.*
.*       GENERATION OF CONTROL BLOCKS
.*
         SPACE 2
*        TRANSLATE TABLES
         AIF   (NOT &GTABLE1).CHKT2
         SPACE 2
         ASMTRTAB  SD50,RC50,RF50
.CHKT2   AIF   (NOT &GTABLE2).CHKT3
         SPACE 2
         ASMTRTAB SD40,RC40,RF40
.CHKT3   AIF   (NOT &GTABLE3).CHKT4
         SPACE 2
         ASMTRTAB SD41,RC41,RF41
.CHKT4   AIF   (NOT &GTABLE4).CHKTRL
         SPACE 2
         ASMTRTAB SB40,RB40,RU40
.CHKTRL  ANOP
&M1      SETA  0
         SPACE 4
.CHKTRL1 ANOP
*        DFTRMLST'S
&M1      SETA  &M1+1
         AIF   ('&G1050(&M1)' EQ '').CHKTRL2
&CHAR    SETC  '&G1050(&M1)'(1,2)
         SPACE 2
T&G1050(&M1)     DFTRMLST WRAPLST,&CHAR.15
         AGO   .CHKTRL1
.CHKTRL2 ANOP
&M1      SETA  0
.CHKTRL3 ANOP
&M1      SETA  &M1+1
         AIF   ('&G1050D(&M1)' EQ '').CHKTRL4
         SPACE 2
&CHAR    SETC  '&G1050D(&M1)'(1,2)
T&G1050D(&M1)    DFTRMLST DIALST,0,&CHAR.15
         AGO   .CHKTRL3
.CHKTRL4 AIF   (NOT &G274041).SETEND
         SPACE 2
T0       DFTRMLST DIALST,0
.SETEND  ANOP
         SPACE 4
         AIF   (&NOEXIT).SETZER1
&USRCEX  SETC  '&CMDEXIT'
         AGO   .CHKONEX
.SETZER1 ANOP
&USRCEX  SETC  '0'
.CHKONEX AIF   (&NOON).SETZER2
&USRONEX SETC  '&ONEXIT'
         AGO   .CHKJOEX
.SETZER2 ANOP
&USRONEX SETC  '0'
.CHKJOEX AIF   (&NOJOB).SETZER3
&USRJOEX SETC  '&JOBEXIT'
         AGO   .CHKOFEX
.SETZER3 ANOP
&USRJOEX SETC  '0'
.CHKOFEX AIF   (&NOOFF).SETZER4
&USROFEX SETC  '&OFFEXIT'
         AGO   .CCTGEN
.SETZER4 ANOP
&USROFEX SETC  '0'
.CCTGEN  ANOP
&CHAR    SETC  '&IHKLINS'
*
*        CRJE CONTROL TABLE (CCT)
*
         SPACE 2
IHKCCT   CSECT
         SPACE 1
         ENTRY CCTCLECB,CCTJSECB,CCTCSECB,CCTJLECB,CCTMSECB
         SPACE 1
         DC    X'0080' .           INSTALLATION OPTION FLAGS
         DC    H'&OUTNO' .         NUMBER LINES FOR SIMULATED BREAK
         DC    H'&BRDCST' .        MAXIMUM NUMBER OF BROADCAST MESSAGES
         DC    H'&MSGNO' .         MAXIMUM NUMBER OF DELAYED MESSAGES
         DC    AL1(&INCODE) .      SYSOUT CODE - INTERNAL
         DC    C'&SYSCRJE' .       SYSOUT CODE - EXTERNAL
         DC    AL1(&MSGRC) .       MCS ROUTING CODE              A47964
         DC    X'0' .              NUMBER OF REQUESTING CONSOLES
&INDEX   SETA  &PL1LNO
&INDEX1  SETA  &FORTLNO
&INDEX   SETA  &INDEX+1
&INDEX1  SETA  &INDEX1+1
&CHAR    SETC  '&INDEX'
         DC    H'&CHAR' .          MAXIMUM CONTINUATIONS - PL/1
&CHAR    SETC  '&INDEX1'
         DC    H'&CHAR' .          MAXIMUM CONTINUATIONS - FORTRAN
         DC    H'&JOB' .           MAXIMUM NUMBER OF JOBS ALLOWED
         DC    H'&USERS' .         MAXIMUM NUMBER USERS ALLOWED
         DC    F'0' .              PL/1 FLAG BYTE AND WORK AREA
         DC    F'0' .              FORTRAN FLAG BYTE AND WORK AREA
CCTCLECB DC    F'0' .              ECB - IHKCCI TO LOADER/CONTROLLER
CCTJSECB DC    F'0' .                  - IHKDEQ TO IHKSRV
CCTCSECB DC    F'0' .                  - CENTRAL COMMANDS TO IHKSRV
CCTJLECB DC    F'0' .                  - IHKSDQ TO LOADER/CONTROLLER
CCTMSECB DC    F'0' .                  - IHKSRV TIMER ROUTINE TO IHKSRV
         DC    H'&BUFNO' .         MAXIMUM ENQUEUED CENTRAL COMMANDS
&CHAR    SETC  '&IHKLINS'
         DC    H'&CHAR' .          NO LINES IN SYSTEM
         AIF   ('&USRCEX' EQ '0').ACON1
         DC    V(&USRCEX) .        ADDRESS OF USER COMMAND PROCESSOR
         AGO   .VCON1
.ACON1   ANOP
         DC    A(&USRCEX) .        ADDRESS OF USER COMMAND PROCESSOR
.VCON1   AIF   ('&USRONEX' EQ '0').ACON2
         DC    V(&USRONEX) .       ADDRESS OF USER LOGON EXIT
         AGO   .VCON2
.ACON2   ANOP
         DC    A(&USRONEX) .       ADDRESS OF USER LOGON EXIT
.VCON2   AIF   ('&USRJOEX' EQ '0').ACON3
         DC    V(&USRJOEX) .       ADDRESS OF USER JOBCARD EXIT
         AGO   .VCON3
.ACON3   ANOP
         DC    A(&USRJOEX) .       ADDRESS OF USER JOBCARD EXIT
.VCON3   AIF   ('&USROFEX' EQ '0').ACON4
         DC    V(&USROFEX) .       ADDRESS OF USER LOGOFF EXIT
         AGO   .VCON4
.ACON4   ANOP
         DC    A(&USROFEX) .       ADDRESS OF USER LOGOFF EXIT
.VCON4   ANOP
         EJECT
*
*        MAJOR COMMAND LIST
*
IHKMCL   CSECT
         SPACE 1
&M1      SETA  1
         DC    AL1(&MTAB(1)) .     NUMBER OF COMMANDS, ALIASES, OR
*                                  ABBREVIATIONS IN MAJOR LIST
.GENMAJ  ANOP
&M1      SETA  &M1+1
.GENMAJ1 ANOP
         AIF   (&M1 GT &MMAX).ENDMAJ
         DC    X'&MTAB(&M1)',C'&MTAB(&M1+1)',X'&MTAB(&M1+2)'
&M1      SETA  &M1+3
         AGO   .GENMAJ1
.ENDMAJ  ANOP
         EJECT
*
*        SUBCOMMAND LIST
*
IHKSCL   CSECT
         SPACE 1
&M1      SETA  1
         DC    AL1(&STAB(1)) .     NUMBER OF COMMANDS, ALIASES, OR
*                                  ABBREVIATIONS IN SUB LIST
.GENSUB  ANOP
&M1      SETA  &M1+1
.GENSUB1 ANOP
         AIF   (&M1 GT &SMAX).ENDSUB
         DC    X'&STAB(&M1)',C'&STAB(&M1+1)',X'&STAB(&M1+2)'
&M1      SETA  &M1+3
         AGO   .GENSUB1
.ENDSUB  ANOP
         SPACE 1
         MEND
