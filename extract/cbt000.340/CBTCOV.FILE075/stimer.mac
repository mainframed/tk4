         MACRO
&NAME    STIMER &TYPE,&EXIT,&DINTVL=,&BINTVL=,&TUINTVL=,&TOD=
         LCLB  &B(7)
         LCLA  &SUM
         AIF   ('&TYPE' EQ '' OR '&DINTVL&BINTVL&TUINTVL&TOD' EQ '').E3
&B(1)    SETB  ('&TYPE' EQ 'TASK')
&B(2)    SETB  ('&TYPE' EQ 'REAL')
&B(3)    SETB  ('&TYPE' EQ 'WAIT')
&B(4)    SETB  ('&DINTVL' NE '')
&B(5)    SETB  ('&BINTVL' NE '')
&B(6)    SETB  ('&TUINTVL' NE '')
&B(7)    SETB  ('&TOD' NE '')
         AIF   (&B(1)+&B(2)+&B(3) NE 1).E1
         AIF   (&B(4)+&B(5)+&B(6)+&B(7) NE 1).E2
&NAME    IHBINNRA &DINTVL&BINTVL&TUINTVL&TOD,&EXIT
         AIF   ('&EXIT' NE '').CONTA
         AIF   (&B(1) AND &B(6)).SRSVC
         AIF   (&B(1) AND &B(7)).SPZRT
&SUM     SETA  &B(3)+3*&B(2)+16*&B(5)+48*&B(4)+112*&B(7)
         LA    0,&SUM.(0,0)                      LOAD FLAG BYTE
.ZSHIFT  SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         AGO   .SVC
.SRSVC   SR    0,0                               INDICATE TASK,TUINTVL=
         AGO   .SVC
.SPZRT   LA    0,48(0,0)                         LOAD FLAG BYTE
         AGO   .ZSHIFT
.CONTA   AIF   (&B(1) AND &B(6)).SVC
         AIF   (&B(1) AND &B(7)).SPECRT
         AIF   (NOT &B(3)).NMNOTE                                A26596
         MNOTE 12,'WAIT AND EXIT ADDRESS INVALID'                A26596
.NMNOTE  ANOP                                                    A26596
&SUM     SETA  &B(3)+3*&B(2)+16*&B(5)+48*&B(4)+112*&B(7)
         LA    14,&SUM.(0,0)                     LOAD FLAG BYTE
.SHIFT   SLL   14,24(0)                          SHIFT TO HI-ORDER BYTE
         OR    0,14                              AND PACK WITHEXIT ADDR
.SVC     SVC   47                                ISSUE STIMER SVC
         MEXIT
.SPECRT  LA    14,48(0,0)                        LOAD FLAG BYTE
         AGO   .SHIFT
.E1      IHBERMAC 36,,&TYPE
         MEXIT
.E2      IHBERMAC 38
         MEXIT
.E3      IHBERMAC 01
         MEND
