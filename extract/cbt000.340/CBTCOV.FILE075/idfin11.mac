         MACRO
         IDFIN11
         COPY IDFGBL
         AIF   (NOT &PIB(48)).TR
         MNOTE *,'IDF100 IN TRACE MODE ENTERING IDFIN11'
.TR      ANOP
.**********************************************************************
.*                                                                    *
.*                           IMPLICIT GOTO PROCESSOR                  *
.*                                                                    *
.**********************************************************************
         AIF   (NOT &PIB(42) OR &PIB(6)).DSP16 NOT NEEDED
         AIF   (&PIB(2)).IGX  CONTINUATION?
&K       SETA  &QA4(1) GET FREE Q PTR
&PIB(1)  SETB  (&K LE 0)
         AIF   (NOT &PIB(1)).I0367
         IDFMSG3 510
.I0367   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).DSP16
&I       SETA  &QA1(1) GET ORGQ PTR
&QA4(1)  SETA  &QA1(&K)/65536 UPDATE FREE Q PTR
&QA1(&K) SETA  &I*65536 SET CHAIN PTR IN NEW ENTRY
&QA1(1)  SETA  &K ORGQ PTR TO NEW ENTRY
&I       SETA  &QA3(1) GET DEST Q PTR
&PIB(1)  SETB  (&I LE 0)
         AIF   (NOT &PIB(1)).I0368
         IDFMSG 0
         IDFMSG3 511
.I0368   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).DSP16
.* GET CURRENT PATH/SEGMENT FROM 1ST DEST Q ENTRY
&QA1(&K) SETA  &QA1(&K)/65536*65536+(&QA1(&I)-&QA1(&I)/65536*65536)
.* SET LENGTH OF 10 AND LOCATION CTR
&L       SETA  10
&QA2(&K) SETA  &L*65536+&PIA(3)+&PIA(4)+&PIA(4)/480*18
.* SET CURRENT LINE/COLUMN
&QA4(&K) SETA  &PRTA(7)*256+&PRTA(12)
&QA3(&I) SETA  &QA3(&I)+65536 BUMP CTR OF UNRESOLVED BRANCHES
&QB1(&K) SETB  1 INDICATE IMPLICIT GOTO
&QB2(&K) SETB  (&PIA(1) EQ 4 AND NOT (&PIB(3) OR &PIB(14) OR &PIB(44)))
.IG1      AIF   (&PRTA(11) GE &PRTA(12)).IG2  SCAN FOR TAB     A17854
         AIF   (&PRTA(11) EQ 130).IG2     END OF PTAB GLOBAL   SA55108
&PRTA(11) SETA &PRTA(11)+1
&QB3(&K) SETB  (&PTAB(&PRTA(11))) SHOW TAB IMBEDDED IN FIELD
         AIF   (NOT &QB3(&K)).IG1
.IG2     ANOP
&PRTA(11) SETA &PRTA(12)
&QB4(&K) SETB  (&PIB(5)) IN CYCLE
&QB5(&K) SETB  (&PIB(8)) IN SUMMARY
&PIA(8)  SETA  &PIA(6)+&L RESERVE BYTES FOR MOTION FCD
&PIA(8)  SETA  (&PIA(8)-&PIA(8)/486*486)/478*18 SUBTRACT CARRY IF ANY
         AIF   (NOT &PIB(47)).DSP16
         IDFDSP 'AFTER IMPLICIT GOTO',QUEUE,PIB
.DSP16   ANOP
&PIB(42) SETB  0   RESET IMPLICIT GOTO BIT
.IGX     ANOP
.**********************************************************************
.*                                                                    *
.*                  CYCLE - PART 3 (CYCLE LIMIT)                      *
.*                                                                    *
.**********************************************************************
         AIF   (NOT &PIB(36)).CY3X
.* RESOLVE ADDR FOR FALL THRU IMPLICIT IND BRANCH IN CYCLE LIMIT  FSDB
&I       SETA  &QA1(1)                  GET ORG Q PTR             FSDB
         AIF   (&I LE 0).CY2            ORGQ EMPTY OR             FSDB
         AIF   (NOT &QB1(&I)).CY2       NOT IMPLICIT ELEMENT      FSDB
&QA2(&I) SETA  &QA2(&I)-&QA2(&I)/65536*65536                      FSDB
&PIA(6)  SETA  &QA2(&I)                 GO BACK TO BRANCH ADDR    FSDB
         IDFASM (,4070)                 DUMMY EC BYTE             FSDB
         IDFASM (,4671)                                           FSDB
&A(2)    SETA  &QA2(&I)+8+(&QA2(&I)-&QA2(&I)/486*486)/472*18      FSDB
&A(3)    SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18                     FSDB
&A(2)    SETA  &A(3)-&A(2)-(&A(3)/486-&A(2)/486)*18               FSDB
&A(2)    SETA  &A(2)/2                  PACKED BYTE DISP          FSDB
&A(1)    SETA  &A(2)/234*2+(&A(2)-&A(2)/234*234)/128              FSDB
         IDFASM A                                                 FSDB
&A(2)    SETA  &A(2)-&A(2)/234*234                                FSDB
&A(1)    SETA  &A(2)-&A(2)/128*128                                FSDB
         IDFASM A                                                 FSDB
&QA1(1)  SETA  &QA1(&I)/65536           UPDATE ORGQ PTR           FSDB
&QA1(&I) SETA  &QA4(1)*65536            PLACE ENTRY ON FREEQ      FSDB
&QA4(1)  SETA  &I                       UPDATE FREEQ PTR          FSDB
.CY2     ANOP  ,                                                  FSDB
&CYA(5)  SETA  0
&A(2)    SETA  &CYA(2)/256         DESTINATION LINE
&A(3)    SETA  &CYA(2)-&A(2)*256      AND COLUMN
&B(1)    SETB  (&A(3) GT &ECA(1))  DC GT HIGHEST TAB
&ECA(3)  SETA  &A(3)+&B(1)*(&ECA(1)-&A(3))  SET REGION BOUNDS TO EITHER
&ECA(4)  SETA  &ECA(3)       DEST COL OR HIGHEST TAB
.CY3     AIF   (&PTAB(&ECA(3))).CY4   TAB SET AT THIS COL?
&B(1)    SETB  (&ECA(3) GE &A(3))
&ECA(3)  SETA  2*&A(3)-&ECA(3)-&B(1)  INVERT AND SPREAD
&ECA(4)  SETA  2*&A(3)-&ECA(3)          REGION POINTERS
         AGO   .CY3
.CY4     ANOP
&PIA(6)  SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18
&N       SETA  &PRTA(12)
&B(1)    SETB  (&DFA(19) LT &CYA(3) AND &DFA(11) LT &CYA(3)) SAME LINE
&B(2)    SETB  1
&B(3)    SETB  (&B(2) AND &N LT &ECA(3) AND &N LT &ECA(4) AND          C
               (&B(1) OR &N NE &DFA(2)))
&B(5)    SETB  ((&N LE &ECA(3) OR &N LE &ECA(4)) AND &N NE &DFA(2))
&B(6)    SETB  (&N GT &ECA(1) AND 2*&A(3) GT &ECA(1)+&N AND            C
               (&B(1) OR &A(3) LE &DFA(2)+127))
&B(4)    SETB  ((&B(5) OR &B(6)) AND &B(2) AND NOT &B(3) AND &N        C
               NE &A(3))
         AIF   (&PIA(1) EQ 4 AND NOT (&PIB(3) OR &PIB(12) OR           C
               &PIB(39))).CY4A          IMMEDIATE BYTE NOT NEEDED
         IDFASM (,4770)
.CY4A    AIF   (&CYB(2)).CY4AB  PRINTING FIELD IN CYCLE NEEDS EC
         IDFASM (,4070)
         AGO   .CY9A  NON-PRINT FIELDS NO MORE EC
.CY4AB   AIF   (&B(4)).CY6   SPACES
         AIF   (NOT &B(3)).CY7          LINES
&A(1)    SETA  32                       TABS
.CY4B    AIF   (&PIA(1) NE 3 OR &PIB(3) OR &PRTA(11) GE &PRTA(12)).CY5
&PRTA(11) SETA &PRTA(11)+1
         AIF   (NOT &PTAB(&PRTA(11))).CY4B NO IMBEDDED TAB
         IDFASM (,4070)  IF TAB GEN SPACE ZERO
.CY5     ANOP
&N       SETA  &N+1
&A(1)    SETA  &A(1)+&PTAB(&N)
.CY51    AIF   (&N NE &ECA(3) AND &A(1) NE 47).CY5            ADX00919
         AIF   (&A(1) EQ 32).CY6  NO TABS EQ 32,GOTO SPACES     A00919
         IDFASM A
         AIF   (&N EQ &A(3) AND &B(1)).CY9   DONE
         IDFASM (,4770)
&A(1)    SETA  32
         AIF   (&N NE &ECA(3)).CY5      MORE TABS
         AIF   (NOT &B(1)).CY7          LINES
.CY6     ANOP
&A(1)    SETA  (&A(3)-&N)*2
&A(1)    SETA  31-(&A(1)+31)/(&A(1)+30)*(15+&A(1)/2)+(&A(1)+3)/(&A(1)+2X
               )*(&A(1)-16)+(&A(1)-29)/(&A(1)-30)*(15-&A(1)/2)
&N       SETA  &N+&A(1)+&A(1)/16*2*(8-&A(1))
         IDFASM A
         AIF   (&N EQ &A(3) AND &B(1)).CY9   DONE
         IDFASM (,4770)
         AIF   (&N NE &A(3)).CY6  MORE SPACES
.CY7     ANOP
&B(7)    SETB  (&A(3) LT &N)       NL IF PSC LT PEC
&A(1)    SETA  49+&B(7)*16
&N       SETA  &N+&B(7)*(&DFA(2)-&N)
         IDFASM A
&B(1)    SETB  1                   INDICATE SAME LINE
         AIF   (&N EQ &DFA(2)).CY8  NO IS BYTE
         IDFASM (,4676)
&A(1)    SETA  &N-&DFA(2)
         IDFASM A
.CY8     AIF   (&N EQ &A(3)).CY9   AT DEST COL
         IDFASM (,4770)
&A(1)    SETA  32                 SET UP FOR TABS               A00919
         AGO   .CY51              GO TO CHK TABS              ADX00919
.CY9     ANOP
&B(1)    SETB  (&CYA(3) LE &DFA(19) OR &CYA(3) LE &DFA(11)) NEW LINE
&PRTA(7) SETA  &PRTA(7)+&B(1)
&CYA(5)  SETA  (&PRTA(7)-&CYA(2)/256)*(&CYA(1)-1) RELOCATION FACTOR
.CY9A    IDFASM (,4674)  REPEAT CYCLE BYTE
&B(1)    SETB  ('&CYC(2)'(1,1) NE ' ')  EXPLICIT TARGET
&J       SETA  11+&B(1)*3  LENGTH TO SAVE FOR MOTION
&PIA(8)  SETA  &PIA(6)+2*&J+(&PIA(6)-&PIA(6)/486*486+2*&J)/480*18
&A(2)    SETA  &CYA(4)+12+(&CYA(4)-&CYA(4)/486*486)/468*18
&A(2)    SETA  &PIA(6)-&A(2)-(&PIA(6)/486-&A(2)/486)*18
&A(2)    SETA  &A(2)/2   PACKED BYTE DISP
&A(1)    SETA  &A(2)/234*2+(&A(2)-&A(2)/234*234)/128 HI ADDR BYTE
&PIA(6)  SETA  &CYA(4)+8+(&CYA(4)-&CYA(4)/486*486)/472*18
         IDFASM A
&A(2)    SETA  &A(2)-&A(2)/234*234
&A(1)    SETA  &A(2)-&A(2)/128*128   LO ADDR BYTE
         IDFASM A
&J       SETA  0
&I       SETA  &QA1(1)  GET ORIGIN Q PTR
&K       SETA  &QA4(1)  GET FREE Q PTR
&PIB(1)  SETB  (&K LE 0)
         IDFMSG3 510
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CY3X
&L       SETA  &QA3(1)  GET DESTQ PTR
&PIB(1)  SETB  (&L LE 0)
         IDFMSG 0
         IDFMSG3 511
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CY3X
&QC(&K)  SETC  '&CYC(2)' QUEUE TARGET NAME
         AIF   ('&CYC(2)'(1,1) EQ ' ').CY11 IMPLICIT
.CY10    AIF   (&I EQ 0).CY11
         AIF   ('&QC(&I)' GE '&QC(&K)' AND NOT &QB1(&I)).CY11  HIT
&J       SETA  &I
&I       SETA  &QA1(&I)/65536
         AGO   .CY10
.CY11    ANOP
&QA4(1)  SETA  &QA1(&K)/65536
&QA1(&K) SETA  &I*65536
         AIF   (&J EQ 0).CY12
&QA1(&J) SETA  &QA1(&J)-&QA1(&J)/65536*65536+&K*65536
         AGO   .CY13
.CY12    ANOP
&QA1(1)  SETA  &K
.CY13    ANOP
&QA1(&K) SETA  &QA1(&K)/65536*65536+&QA1(&L)-&QA1(&L)/65536*65536
&QB1(&K) SETB  ('&QC(&K)'(1,1) EQ ' ')
&PIB(11) SETB  (NOT &QB1(&K))  EXPLICIT TARGET SETS DEAD CODE
&J       SETA  13-&QB1(&K)*3
&QA2(&K) SETA  &J*65536+&PIA(3)+&PIA(4)+&PIA(4)/480*18
&QA4(&K) SETA  &PRTA(7)*256+&A(3)
&QA3(&L) SETA  &QA3(&L)+65536
&QB2(&K) SETB  0
&QB3(&K) SETB  0
&QB4(&K) SETB  1    CYCLE
&QB5(&K) SETB  0
&PIB(36) SETB  0 RESET CYCLE LIMIT INVOKE
&PIB(6)  SETB  0 RESET CYCLE LIMIT STATE
&PIB(5)  SETB  0 RESET CYCLE STATE
&PIB(7)  SETB  1 SET POST LIMIT STATE
.CY13A   ANOP
&J       SETA  &L
&L       SETA  &QA1(&L)/65536
.CY13B   AIF   (&L LE 0).CY3X
         AIF   (NOT &QB4(&L)).CY13A
&K       SETA  &QA1(&L)/65536
&QA1(&J) SETA  &QA1(&J)-&QA1(&J)/65536*65536+&QA1(&L)/65536*65536
&QA1(&L) SETA  &QA4(1)*65536
&QA4(1)  SETA  &L
&L       SETA  &K
         AGO   .CY13B
.CY3X    ANOP
         AIF   (NOT &PIB(47)).DSP17
         IDFDSP 'AFTER CYCLE PART 3',QUEUE,A,CYA
.DSP17   ANOP
&PIB(39) SETB  0                        RESET IND BRANCH BIT      FSDB
.**********************************************************************
.*                                                                    *
.*                           FDEND FUNCTION                           *
.*                                                                    *
.**********************************************************************
         AIF   (&PIA(1) NE 0).END2 NOT FDEND
&I       SETA  &QA1(1)    GET ORGQ PTR
.END1    AIF   (&I EQ 0).END1A  EMPTY OR DONE
&PIB(1)  SETB  (&QB1(&I))  IMPLICIT?
         IDFMSG 0    SYSTEM ERROR
         IDFMSG3 545
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
&PIB(1)  SETB  (NOT &QB1(&I))  EXPLICIT?
&A(1)    SETA  (&QA1(&I)-&QA1(&I)/65536*65536)
&A(2)    SETA  &A(1)-&A(1)/256*256
&A(1)    SETA  &A(1)/256
         IDFMSG3 546   UNRESOLVED BRANCH
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
&I       SETA  &QA1(&I)/65536
         AGO   .END1
.END1A   ANOP
&I       SETA  &QA3(1)  GET DESTQ PTR
&PIB(1)  SETB  (&I LE 0)
         IDFMSG 0
         IDFMSG3 511
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).END1B
.END1AA  ANOP
&I       SETA  &QA1(&I)/65536  CHAIN TO NEXT
         AIF   (&I LE 0).END1B                 DONE
&PIB(1)  SETB  (&QA3(&I) GT 0 AND &QA3(&I) LT 256) UNUSED SAVELOCS
         IDFMSG3 581
         AGO   .END1AA
.END1B   ANOP
&PIB(1)  SETB  (&PIB(15))
         IDFMSG3 513 DISASTER ERROR
.END2    AIF   (&PIB(16)).ENDX  BEEN HERE BEFORE?
         AIF   (&PIB(15)).END3  ERROR?
         AIF   (&PIA(1) NE 0).ENDX  NOT FDEND
&PIA(6)  SETA  &PIA(5)+8  ORG TO SECTOR COUNT AND
         IDFASM (,0000)   INDICATE VALID FORM
&PIA(6)  SETA  &PIA(5)+20  ORG TO LINES BYTES
&A(1)    SETA  &PRTA(15)/128  MATRIX LINES HIGH ORDER AND
         IDFASM A
&A(1)    SETA  &PRTA(15)-&A(1)*128  LOW ORDER
         IDFASM A
         AIF   (&PIB(19)).END2A         VARIABLE LENGTH FORM      FSDB
&A(1)    SETA  &PRTA(3)/128  SELECTRIC LINES HIGH ORDER AND
         IDFASM A
&A(1)    SETA  &PRTA(3)-&A(1)*128  LOW ORDER
         IDFASM A
.END2A   ANOP  ,                                               A01317
&PIB(1)  SETB  1
         IDFMSG3 579  LINES IN FORMS
&PIA(6)  SETA  &PIA(3)+&PIA(4)+&PIA(4)/480*18
         IDFASM (,4679)  END FORM BYTE
         IDFASM (,401E)  FDP DELIMITER
&A(1)    SETA  (480-&PIA(4))/2  NUMBER OF PADS NEEDED
         DC    (&A(1))X'4070'
         DC    3H'-1'  LAST SECTOR FLAG
         DC    (2+&SB(1))H'-1'  LAST CSECT FLAG
         ORG   *-4-2*&SB(1)
         IDFMSG3 580
&PIA(5)  SETA  &PIA(3)+486
&PIB(16) SETB  1
&PIB(1)  SETB  1
         IDFMSG3 512
         AGO   .ENDX
.END3    ANOP
&PIA(6)  SETA  &PIA(5)+8  ORG TO SECTOR COUNT AND
&PIB(15) SETB  0 SET OFF ERROR BIT TO ASM FLAG
         IDFASM (,FFFF)   INDICATE INVALID FORM
         DC    235X'4070'  PAD OUT SECTOR
         DC    3H'-1'   LAST SECTOR FLAG
         DC    (2+&SB(1))H'-1'  LAST CSECT FLAG
&PIA(5)  SETA  &PIA(5)+486  BUMP SECTORS BY 1
         ORG   *-4-2*&SB(1)
&PIB(15) SETB  1 SET ERROR BIT BACK ON
&PIB(16) SETB  1 INDICATE THIS ROUTINE PERFORMED
.ENDX    ANOP
&PIA(2)  SETA  &PIA(1)  SAVE THIS LEVEL INDICATION
         AIF   (NOT &PIB(48)).TX
         MNOTE *,'IDF100 IN TRACE MODE LEAVING IDFIN11'
.TX      ANOP
         MEND
