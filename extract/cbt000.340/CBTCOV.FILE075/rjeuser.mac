         MACRO
&USER    RJEUSER &OPERAD
         GBLA  &IHKNIDO
         LCLA  &N,&X,&A,&AD,&CT,&THR,&B,&Y,&Z
         LCLA  &MX,&USRNO
         LCLC  &L,&C,&CHAR,&NC,&AC
         LCLC  &OKUSER
.* 186000,708000,714000,852000                                   A22272
IHKTBLUR CSECT
&A       SETA  N'&SYSLIST
&AC      SETC  '&A'
         MNOTE *,'THERE ARE &AC OPERANDS, INCLUDING MAXUSER NUMBER.'
         AIF   (&A EQ 0).NOENT
&Y       SETA  &A/2
&Z       SETA  &Y*2
         AIF   (&Z NE &A).ODOK
         MNOTE 'USERID AND KEY ARE NOT IN PAIR FORM. JOB HALTED.'
         SPACE 3
         MEXIT
.ODOK    AIF   (T'&SYSLIST(1) NE 'N').MXERR
&N       SETA  &SYSLIST(1)
         AIF   (&N LT 1).MXERR
         AIF   (&N LT 1000).OPCK
.MXERR   MNOTE 'INVALID MAXIMUM USERID-KEY NUMBER. JOB ABORTED.'
         SPACE 3
         MEXIT
.OPCK    ANOP
&NC      SETC  '&N'
         MNOTE *,'THE MAXIMUM NUMBER OF USERID-KEY PAIRS IS &NC .'
&X       SETA  &N+&N
&MX      SETA  &X+1
         AIF   (&A GE 1).MTMX                                    A22272
.NOENT   MNOTE *,'NO ENTRY. JOB IS NOT CONTINUED.'
         MEXIT
.MTMX    ANOP
&AD      SETA  2
&B       SETA  &A-1
         AIF   (&A EQ 1).NEWTAG                                  A22272
         AIF   (&B LE &X).OKWAG
         MNOTE 'INVALID OPERANDS. MORE THAN MAXIMUM.'
         MEXIT
.OKWAG   ANOP
&CT      SETA  K'&SYSLIST(&AD)
&L       SETC  '&SYSLIST(&AD)'
         AIF   (&CT EQ 3).CAK
         MNOTE ' &L HAS INVALID CHARACTER NUMBER. JOB DISCONTINUED.'
         SPACE 3
         MEXIT
.CAK     ANOP
&CHAR    SETC  '&L'(1,1)
.CHECK   AIF   ('&CHAR' EQ '$').OK
         AIF   ('&CHAR' EQ '#').OK
         AIF   ('&CHAR' EQ '@').OK
         AIF   ('&CHAR' LT 'A').ERMSG
         AIF   ('&CHAR' LE '9').OK
.ERMSG   MNOTE '&L HAS INVALID CHARACTER.  GENERATION TERMINATED'
&THR     SETA  &THR-&THR
         SPACE 3
         MEXIT
.OK      ANOP
         AIF   (&THR NE 0).SCD
&THR     SETA  &THR+1
&CHAR    SETC  '&L'(2,1)
         AGO   .CHECK
.SCD     AIF   (&THR NE 1).TRD
&THR     SETA  &THR+1
&CHAR    SETC  '&L'(3,1)
         AGO   .CHECK
.TRD     ANOP
&THR     SETA  &THR-&THR
         MNOTE *,'&L CONSISTS OF 3 VALID CHARACTERS. O.K.'
.AADD    ANOP
         AIF   (&AD EQ &A).ENDEN
&AD      SETA  &AD+1
         AGO   .OKWAG
.ENDEN   ANOP
&AD      SETA  2
.ABABA   ANOP
&OKUSER  SETC  '&SYSLIST(&AD)'
.NEWTAG  ANOP                                                    A22272
         AIF   (&IHKNIDO NE 0).SECD
**********************************
*
&USRNO   SETA  &B/2
IHKCDUNB DC    H'&N' .             MAXIMUM USER NUMBER
IHKCDULN DC    H'18'               NUMBER OF BYTES DESCRIBING USER
         ENTRY IHKCDUDR,IHKCDUNB,IHKCDULN
*********************************************************************
*                                                                    *
*                                                                    *
* THE USER DIRECTORY, A LIST OF AUTHORIZED USERS IN THE RJE SYSTEM
*                                                                    *
*                                                                    *
*********************************************************************
.*
.*
.*       ******************************                              *
.*       *+0                           *
.*       * TERMINAL IDENTIFICATION     *
.*       *                             *                             *
.*       ******************************                              *
.*       *+8       *+9                 *
.*       * STATUS  * USER IDENTIFICATON*
.*       *OF USER  *                   *
.*       *         *                   *                             *
.*       ******************************                              *
.*       *+12                *+15      *
.*       *  USER KEY         *ADDR OF  *
.*       *                   *         *                             *
.*       ******************************                              *
.*       *+16                          *
.*       *THIS ENTRY    *
.*       *              *              *
.*       *              *              *
.*       ******************************                              *
&IHKNIDO SETA  1
*
***********************************
*                                                                    *
*                                                                    *
IHKCDUDR DS    0H                                                A22272
         AIF   (&A EQ 1).EXPL                                    A22272
.SECD    DC    XL8'0' .            TERMINAL IDENTIFIER
.HOP     DC    X'00' .             STATUS OF USER
         DC    C'&OKUSER' .        USER IDENTIFICATION
&AD      SETA  &AD+1
&OKUSER  SETC  '&SYSLIST(&AD)'
         DC    C'&OKUSER' .        USER KEY ASSIGNMENT
         DC    XL3'0' .            TTR ADDRESS OF TDIR ON DISC
*                                                                    *
***********************************
*                                                                    *
         AIF   (&AD EQ &A).EXPL
&AD      SETA  &AD+1
         AGO   .ABABA
.EXPL    ANOP
         AIF   (&AD EQ &MX).OUSTER
*********************************************************************
*                                                                    *
*  THE RESERVED SPACE IN THE DIRECTORY FOR THE MAXIMUM NUMBER OF     *
*  USERS, AVAILABLE FOR DYNAMICAL ADDITION OF THEM.                  *
*                                                                    *
*********************************************************************
.MORZO   ANOP
         AIF   (&AD GE &MX).OUSTER                               A22272
&AD      SETA  &AD+2
*                                                                    *
*********************************************************************
*                                                                    *
         DC    18X'40'
*                                                                    *
         AGO   .MORZO
.OUSTER  ANOP
         MEND
