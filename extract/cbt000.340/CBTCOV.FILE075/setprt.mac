         MACRO
&ANYNAME SETPRT &DCB,&UCS=,&FCB=,&MF=,&OPTCD=
.*
.*   THE ABOVE IS MACRO PROTOTYPE
.*
         LCLA  &L
         LCLA  &K
         LCLB  &SW1
         LCLC  &PAREN1
         LCLC  &PAREN2
         LCLC  &PAREN3
         LCLC  &PAREN4
         LCLC  &MFTWO
&K       SETA  K'&DCB                  STORE NO. OF CHARACTERS IN DCB
&L       SETA  K'&MF(2)                LENGTH OF &MF(2)
         AIF   ('&DCB' EQ '').CHECKL
&PAREN1  SETC  '&DCB'(1,1)             FIRST CHARACTER OF &DCB
&PAREN2  SETC  '&DCB'(&K,1)            LAST CHARACTER OF &DCB
         AIF   ('&DCB' NE '&DCB(1)').DCBNREG DCB IN REG
         AGO   .CHECKL
.DCBNREG ANOP                           DCB IS IN REGISTER
&K       SETA  K'&DCB(1)                LENGTH OF &DCB(1)
.CHECKL  AIF   ('&MF(2)' EQ '').TESTUCS
&MFTWO   SETC  '&MF(2)'
&PAREN3  SETC  '&MFTWO'(1,1)           FIRST CHARACTER OF &MF(2)
&PAREN4  SETC  '&MFTWO'(&L,1)          LAST CHARACTER OF &MF(2)
.TESTUCS AIF   (N'&UCS GT 0).CHECK1    TEST IF UCS PARAMETERS PRESENT
         AGO   .TSTFCB                  GO CK FCB PARAMETERS
.CHECK1  AIF   ('&UCS(1)' EQ '').ERROR02 TEST IF UCS-ID IS MISSING
.TSTFCB  AIF   (N'&FCB GT 0).FCBCK1     TEST IF FCB PARAMETERS PRESENT
         AGO   .BYPASS                  GO CK DCB SPECIFIED
.FCBCK1  AIF   ('&FCB(1)' EQ '').ERROR06  TEST IF FCB ID MISSING
.BYPASS  AIF   ('&DCB' NE '&DCB(1)').BYPASS2
         AIF   (K'&DCB GT 8).ERROR01    TEST IF DCB NAME GT 8
.BYPASS2 ANOP
         AIF   (K'&DCB(1) GT 8).ERROR01  TEST IF REG NAME GT 8
         AIF   ('&DCB' EQ '(1)' OR '&DCB' EQ '(0)').ERROR01
         AIF   ('&MF' EQ '' OR '&MF(1)' EQ 'E' OR '&MF' EQ 'L').GOGOUCS
         AGO   .ERROR03
.GOGOUCS AIF   ('&UCS(2)' EQ '' OR '&UCS(2)' EQ 'F' OR '&UCS(2)' EQ 'FOC
               LD').CHECK3
         AGO   .ERROR02
.CHECK3  AIF   ('&UCS(3)' EQ '' OR '&UCS(3)' EQ 'V' OR '&UCS(3)' EQ 'VEC
               RIFY').GOGOFCB
         AGO   .ERROR02
.GOGOFCB AIF   ('&FCB(2)' EQ '' OR '&FCB(2)' EQ 'ALIGN' OR '&FCB(2)' EQX
                'VERIFY' OR '&FCB(2)' EQ 'V' OR '&FCB(2)' EQ 'A').GOGOCX
               D
         AGO   .ERROR06                 IF FCB PARM IN ERROR
.GOGOCD  AIF   ('&OPTCD' EQ '' OR '&OPTCD' EQ 'U' OR '&OPTCD' EQ 'B').GC
               OGODCB
         AIF   ('&OPTCD(1)' EQ 'B' OR '&OPTCD(1)' EQ 'U').CKOPT
         AGO   .ERROR04                 NO HAVE INVALID OPTCD PARM'S
.CKOPT   AIF   ('&OPTCD(2)' EQ '' OR '&OPTCD(2)' EQ 'FOLD' OR '&OPTCD(2X
               )' EQ 'UNFOLD' OR '&OPTCD(2)' EQ 'F' OR '&OPTCD(2)' EQ 'X
               U').GOGODCB
         AGO   .ERROR04
.GOGODCB AIF   ('&DCB' EQ '').UCSERR
         AIF   ('&PAREN1' EQ '(' AND '&PAREN2' EQ ')').SETSW
         AGO   .UCSERR
.SETSW   ANOP
&SW1     SETB  (1)                           IF YES SET SW1=1
.UCSERR  AIF   (K'&UCS(1) GT 4).ERROR02      TEST IF UCS ID GT 4
         AIF   (K'&FCB(1) GT 4).ERROR06
         AIF   ('&MF(1)' EQ 'E').EFORMMF     TEST IF MF=E
         AIF   ('&MF' EQ 'L').LIST     TEST IF MF=L
         AIF   ('&DCB' EQ '').ERROR01        TEST IF NO DCB SPECIFIED
         AIF   ('&UCS(1)' EQ '').TSTFCB2
*
         AGO   .MACRGO                       IF NOT MF=L,MF=I
.TSTFCB2 AIF   ('&FCB(1)' EQ '').TSTOPCD CK IF FCB SPECIFIED
         AGO   .MACRGO
.TSTOPCD AIF   ('&OPTCD' EQ '').ERROR05
         AGO   .MACRGO
.LIST    AIF   ( &SW1 ).REGDCB               TEST IF DCB=(R)
         DS    0F
         AIF   ('&DCB' NE '').SKIP
&ANYNAME DC    AL4(0)                  DCB ADDR
         AGO   .SKIPGO
.SKIP    ANOP
&ANYNAME DC    AL4(&DCB)               DCB ADDR
         AGO   .SKIPGO
.REGDCB  ANOP                                DCB=(R)
         CNOP  0,4
&ANYNAME BAL   1,*+20
         DC    AL4(0)                   DCB ADDR
         AGO   .SKIPGO
.MACRGO  AIF   ( &SW1 ).REGDCB               TEST IF DCB=(R)
         CNOP  0,4
&ANYNAME BAL   1,*+20
         DC    AL4(&DCB)                DCB ADDR
.SKIPGO  AIF   ('&UCS(1)' NE '').NONULL
         DC    AL4(0)                   CHARACTER SET IMAGE ID
         AGO   .CONTSKP
.NONULL  ANOP
         DC    CL4'&UCS(1)'             CHARACTER SET IMAGE ID
.CONTSKP ANOP
         AIF   ('&UCS(2)' EQ 'F' OR '&UCS(2)' EQ 'FOLD').FOLD
         DC    X'00'                    LOAD MODE = NO FOLD
         AGO   .TSTVRFY
.FOLD    ANOP                           FOLD SPECIFIED
         DC    X'40'                    LOAD MODE = FOLD
.TSTVRFY ANOP                           TEST IF VERIFY IS SPECIFIED
         AIF   ('&UCS(3)' EQ 'V' OR '&UCS(3)' EQ 'VERIFY').VERIFY
         DC    X'00'                    VERIFY = NO
         AGO   .OPTCD
.VERIFY  ANOP                           VERIFY SPECIFIED
         DC    X'10'                    VERIFY = YES
.OPTCD   AIF   ('&OPTCD' EQ 'U').UNBLK       TEST BLOCK/UN DATA CHECK
         AIF   ('&OPTCD(1)' EQ 'U').TSTFDOP  GO CK FOR UCS OPTIONS
         AIF   ('&OPTCD' EQ 'B').BLOCK
         AIF   ('&OPTCD(1)' EQ 'B').TSTFLD2  GO CK UCS OPTION
         DC    X'00'                    DATA CHECK = AS DCB SPEC.
         AGO   .FCBPAR
.TSTFDOP AIF   ('&OPTCD(2)' EQ 'FOLD' OR '&OPTCD(2)' EQ 'F').UNBKFD
         AIF   ('&OPTCD(2)' EQ 'UNFOLD' OR '&OPTCD(2)' EQ 'U').UNBKUFD
.UNBLK   ANOP                           OPTCD = U
         DC    X'40'                    DATA CHECK = UNBLOCK DATA CHECK
         AGO   .FCBPAR
.UNBKFD  ANOP
         DC    X'44'                    UNBLOCK D.C. & FOLD UCS(3211)
         AGO   .FCBPAR
.UNBKUFD ANOP
         DC    X'48'                    UNBLOCD D.C. + UNFOLD UCS 3211
         AGO   .FCBPAR
.TSTFLD2 AIF   ('&OPTCD(2)' EQ 'FOLD' OR '&OPTCD(2)' EQ 'F').BKFLD
         AIF   ('&OPTCD(2)' EQ 'UNFOLD' OR '&OPTCD(2)' EQ 'U').BKUFLD
.BLOCK   ANOP                           OPTCD = B
         DC    X'80'                    DATA CHECK = BLOCK DATA CHECK
         AGO   .FCBPAR
.BKFLD   ANOP
         DC    X'84'                    BLOCK D.C.& FOLD UCS(3211)
         AGO   .FCBPAR
.BKUFLD  ANOP
         DC    X'88'                    BLOCK D.C.& UNFOLD UCS(3211)
.FCBPAR  AIF   ('&FCB(1)' NE '').FCBDC
         DC    AL4(0)                   NO FCB ID SPECIFIED
         DC    X'00'                    NO FCB PARAMETER OPTION BITS
         AGO   .TSTMF
.FCBDC   ANOP
         DC    CL4'&FCB(1)'      FCB IMAGE ID
         AIF   ('&FCB(2)' NE '').CKVFY  CK FOR VERIFY/ALIGN PARM
         DC    X'00'        NO FCB PARAMETER OPTION BITS SPECIFIED
         AGO   .TSTMF
.CKVFY   AIF   ('&FCB(2)' EQ 'VERIFY' OR '&FCB(2)' EQ 'V').STVFY
         DC    X'01'                    ALIGN FORMS SPECIFIED
         AGO   .TSTMF
.STVFY   ANOP
         DC    X'80'                    VERIFY FCB SPECIFIED
.TSTMF   ANOP
*
         AIF   ( &SW1 ).GOSTORE                   TEST IF DCB=(R)
         AGO   .TSTEXIT
.GOSTORE ANOP
         ST    &DCB,0(0,1)              STORE DCB ADDR IN PARM LIST
.TSTEXIT AIF   ('&MF(1)' EQ 'L').EXIT        TEST MF=L.IF YES EXIT
         SVC   81                       ISSUE SETPRT SVC
*
.EXIT    MEXIT                          EXIT
.EFORMMF ANOP                                MF=E
         AIF   ('&ANYNAME' EQ '').OVER
&ANYNAME EQU   *
.OVER    ANOP
*
         AIF   ('&MF(2)' EQ '' OR '&MF(2)' EQ '(1)').TSTUCS
         AIF   ('&MF(2)' EQ '').DONTEST
         AIF   ('&PAREN3' EQ '(' AND '&PAREN4' EQ ')').LOADREG
.DONTEST ANOP
         LA    1,&MF(2)                 LOAD PARAMETER LIST ADDR
         AGO   .TSTUCS
.LOADREG ANOP
         LR    1,&MF(2)                 LOAD PARAMETER LIST ADDR
.TSTUCS  AIF   ('&UCS(1)' EQ '').TSTDCB      TEST IF UCS ID SPECIFIED
         B     *+8
         DC    CL4'&UCS(1)'             CHARACTER SET IMAGE ID
         MVC   4(4,1),*-4               MOVE ID TO PARAMETER LIST
.TSTDCB  AIF   ('&DCB' EQ '').TSTFLD    TEST NO DCB
         AIF   ( &SW1 ).DCBREG          TEST DCB=(R)
         LA    0,&DCB                   LOAD DCB ADDR
         ST    0,0(0,1)                 STORE DCB ADDR IN PARM LIST
         AGO   .TSTFLD
.DCBREG  ANOP
         ST    &DCB,0(0,1)              STORE DCB ADDR IN PARM LIST
.TSTFLD  AIF   ('&UCS(1)' EQ '').CHKOPCD
         AIF   ('&UCS(2)' EQ '').NULFOLD
         MVI   8(1),X'40'               LOAD MODE = FOLD
        AGO   .CHKVRFY
.NULFOLD ANOP
         MVI   8(1),X'00'              LOAD MODE = NO FOLD
.CHKVRFY AIF   ('&UCS(3)' EQ '').NULVRFY
         MVI   9(1),X'10'               VERIFY = YES
         AGO   .CHKOPCD
.NULVRFY ANOP
         MVI   9(1),X'00'              VERIFY = NO
.CHKOPCD AIF   ('&OPTCD' EQ 'U').GOUNBLK
         AIF   ('&OPTCD(1)' EQ 'U').GOUNBLK
         AIF   ('&OPTCD' EQ '').CKFCBPM
         MVI   10(1),X'80'              DATA CHECK = BLOCK DATA CHECK
         AGO   .CKUCSPM
.GOUNBLK ANOP
         MVI   10(1),X'40'              DATA CHECK = UNBLOCK DATA CHECK
.CKUCSPM AIF   ('&OPTCD(2)' EQ '').CKFCBPM
         AIF   ('&OPTCD(2)' EQ 'FOLD' OR '&OPTCD(2)' EQ 'F').UNFLDS
         OI    10(1),X'08'              SET FOLD BIT ON
         AGO   .CKFCBPM
.UNFLDS  ANOP
         OI    10(1),X'04'              SET UNFOLD BIT ON
.CKFCBPM AIF   ('&FCB(1)' EQ '').GOGOSVC
         B     *+8
         DC    CL4'&FCB(1)'             FCB IMAGE ID.
         MVC   11(4,1),*-4              MOVE IN FCB ID TO PARM LIST
         AIF   ('&FCB(2)' EQ '').NOVFYAL
         AIF   ('&FCB(2)' EQ 'VERIFY' OR '&FCB(2)' EQ 'V').SETVFY
         MVI   15(1),X'01'              SET ALIGN FORMS BIT IN PARM LS
         AGO   .GOGOSVC
.NOVFYAL ANOP
         MVI   15(1),X'00'     SET TO INDICATE NO FCB OPTIONS SPEC
         AGO   .GOGOSVC
.SETVFY  ANOP
         MVI   15(1),X'80'              SET VERIFY BIT IN PARM LIST
.GOGOSVC ANOP
*
         SVC   81                       ISSUE SETPRT SVC
*
         MEXIT
.ERROR01 IHBERMAC 190
         MEXIT
.ERROR02 IHBERMAC 191
         MEXIT
.ERROR03 IHBERMAC 192
         MEXIT
.ERROR04 IHBERMAC 193
         MEXIT
.ERROR05 IHBERMAC 194
         MEXIT
.ERROR06 IHBERMAC 237                                            A45586
         MEND
