         MACRO
&NAME    STARTLN &DCB,&RLN
         GBLB  &IEDQE7
.* 044000,052000-056000,744000-960000                             20002
&NAME    DS    0H
         AIF   (&IEDQE7 EQ 1).TCAM                                20002
         AIF   ('&DCB' NE '' AND '&RLN' NE '').OK
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      AIF   ('&DCB'(1,1) EQ '(' OR K'&DCB LT 9).OK2
         MNOTE 12,'***  IHB002  INVALID TERMNAME OPERAND SPECIFIED'
         MEXIT
.OK2     ANOP
         AIF   ('&RLN' EQ 'ALL').ALL
         AIF   ('&RLN' EQ '(0)').SHIFT
         AIF   ('&RLN'(1,1) NE '(').LA
         LR    0,&RLN(1)               REL. LINE NUMBER TO REGISTER 0
         AGO   .SHIFT
.LA      LA    0,&RLN                  REL. LINE NUMBER TO REGISTER 0
.SHIFT   SLL   0,24                    POSITION RELATIVE LINE NUMBER
.ALL     AIF   ('&DCB'(1,1) EQ '(').NOLOC
         AIF   ('&RLN' NE 'ALL').NOSR
         SR    0,0                     ZERO RELATIVE LINE NUMBER
.NOSR    BAL   1,*+12                  SET TERMNAME ADDRESS IN REG. ONE
         DC    CL8'&DCB'
         L     15,=V(IECKDCBL)         ADDRESS OF THE DCB LOCATE RT.
         BALR  14,15                   LINK TO THE DCB LOCATE ROUTINE
         LTR   15,15                   TEST THE RETURN CODE
         BC    7,*+12                  BRANCH ON ERROR-INVALID TERMNAME
         AGO   .AFT
.NOLOC   AIF   ('&RLN' EQ 'ALL').NORLN
         LA    1,0(&DCB(1))            DCB ADDRESS TO REGISTER ONE
         ALR   0,1                     COMBINE DCB AND REL. LINE NUMBR.
         AGO   .AFT
.NORLN   LA    0,0(&DCB(1))            DCB ADDRESS TO REGISTER ZERO
.AFT     L     15,=V(IECKLNCH)         ADDRESS OF THE CHANGE LINE RT.
         AIF   ('&RLN' EQ 'ALL').EX2
         BAL   14,2(15)               TO START 1 LINE ENTRY IN IECKLNCH
         AGO   .END
.EX2     BAL   14,6(15)                TO START ALL ENTRY IN IECKLNCH
.END     MEXIT
.TCAM    ANOP
         AIF   ('&DCB' NE '' AND '&RLN' NE '').OK1                20002
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK1     ANOP
         AIF   ('&DCB'(1,1) EQ '(' OR K'&DCB LT 9).OK3            20002
         MNOTE 12,'***  IHB002  INVALID TERMNAME OPERAND SPECIFIED'
         MEXIT
.OK3     ANOP
         AIF   ('&RLN'(1,1) EQ '(').STC                           20002
         AIF   ('&RLN' NE '0').ALLX                               20002
         MNOTE 12,'*** IHB002 INVALID RLN OPERAND SPECIFIED'
         MEXIT
.STC     LTR   &RLN(1),&RLN(1) .        INVALID LINE NUMBER       20002
         BZ    IEDQ&SYSNDX+40           BRANCH IF YES             20002
         STC   &RLN(1),IEDQ&SYSNDX+10 . RELATIVE LINE NUMBER      20002
         AGO   .TEST                                              20002
.ALLX    AIF   ('&RLN' NE 'ALL').NEXT                             20002
         MVI   IEDQ&SYSNDX+10,0 .       'ALL' INDICATOR           20002
         AGO   .TEST                                              20002
.NEXT    ANOP
         LA    15,&RLN .                RELATIVE LINE NUMBER      20002
         STC   15,IEDQ&SYSNDX+10 .      RELATIVE LINE NUMBER      20002
.TEST    ANOP
         AIF   ('&DCB'(1,1) EQ '(').DCB                           20002
         BAL   15,*+12 .                TERMNAME ADDRESS          20002
         DC    CL8'&DCB' .              TERMINAL NAME             20002
         MVC   IEDQ&SYSNDX+16(8),0(15) MOVE TERMNAME INTO CIB     20002
         AGO   .LINK                                              20002
.DCB     ANOP
         ST    &DCB(1),IEDQ&SYSNDX+16 . ADDRESS OF DCB            20002
         MVI   IEDQ&SYSNDX+16,0 .       SET DCB ADDR FLAG         20002
.LINK    ANOP
         SR    0,0 .                    INDICATE NO PASSWORD      20002
         MVI   IEDQ&SYSNDX+11,X'FF' .   FLAG AS CQTAM MACRO       20002
         CNOP  0,4 .                                              20002
         BAL   1,*+46 .                 SET CIB ADDR IN PARM REG  20002
IEDQ&SYSNDX DS 0F .                     COMMAND INPUT BUFFER      20002
         DC    F'0' .                   OPERATOR CONTROL QCB ADDR 20002
         DC    XL1'E0' .                CIB QUEUING PRIORITY      20002
         DC    XL3'00' .                LINK FIELD                20002
         DC    XL1'40' .                VERB CODE FOR STARTLN     20002
         DC    XL1'1C' .                CIB LENGTH                20002
         DC    XL1'00' .                RELATIVE LINE NUMBER      20002
         DC    XL1'FF' .                COMPLETION CODE(CQTAM)    20002
         DC    A(IEDQ&SYSNDX+32) .      ECB ADDRESS               20002
         DC    XL8'00' .                TERMINAL NAME             20002
         DC    XL4'00' .                WORKAREA ADDRESS          20002
         DC    A(IEDQ&SYSNDX) .         ADDRESS OF CIB            20002
         DC    F'0' .                   PCB WORKAREA/ECB          20002
         DC    F'0' .                   ADDRESS OF CIB LENGTH     20002
         SR    1,1                                                20002
         LINK  EP=IEDQET .                                        20002
         MEND
