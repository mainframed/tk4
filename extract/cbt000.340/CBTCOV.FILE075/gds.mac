       MACRO
&NAME  GDS     &X,&Y,&I
       GBLA    &IHBBLC,&IHBBLIM
         LCLC  &BEAM,&C,&XBUK,&YBUK
         LCLA  &XA,&YA
&C       SETC  '01010101'
&IHBBLC    SETA  &IHBBLC+1              UPDATE BUFFER-LOC COUNTER
       AIF     (&IHBBLC LE &IHBBLIM).TESTI
         IHBERMAC 182
.TESTI   AIF   (T'&I EQ 'O').NOTE       TEST BEAM INTENSITY
         AIF   ('&I'(1,1) EQ 'U' OR '&I' EQ 'ON').ONRT
         AIF   ('&I'(1,1) EQ 'B' OR '&I' EQ 'OFF').OFFRT
.NOTE    IHBERMAC  180,BEAM,BLANKED
.OFFRT ANOP
&BEAM    SETC  '1'
         AGO   .TESTX
.ONRT  ANOP
&BEAM    SETC  '0'
.TESTX   AIF   (T'&X EQ 'O').XNG        VALIDATE X-COORDINATE
         AIF   ('&X'(1,1) EQ '+' OR '&X'(1,1) EQ '-').XNG
         AIF   (T'&X NE 'N').XNG
.XPL     AIF   (&X GT 7).XNG
&XA      SETA  &X
         AGO   .TESTY
.XNG     IHBERMAC  180,X,0
&XA      SETA  0
.TESTY   AIF   (T'&Y EQ 'O').YNG        VALIDATE Y-COORDINATE
         AIF   ('&Y'(1,1) EQ '+' OR '&Y'(1,1) EQ '-').YNG
         AIF   (T'&Y NE 'N').YNG
.YPL     AIF   (&Y GT 7).YNG
&YA      SETA  &Y
         AGO   .DONE
.YNG     IHBERMAC  180,Y,0
&YA      SETA  0
.DONE    ANOP
&XBUK  SETC  '&BEAM'.'&C'((&XA+4)/4,1).'&C'((&XA+2)/2,1).'&C'(&XA+1,1)
&YBUK    SETC    '0'.'&C'((&YA+4)/4,1).'&C'((&YA+2)/2,1).'&C'(&YA+1,1)
&NAME    DC    BL1'&XBUK&YBUK'
         MEND
