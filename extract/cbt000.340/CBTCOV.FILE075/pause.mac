         MACRO
&NAME    PAUSE &PAR1,&PAR2
         GBLB  &SEN
         LCLA  &CNT,&IX,&COUNT
         LCLB  &NOGO
         LCLC  &GNAME
&IX      SETA  1
         AIF   (&SEN).SEQOK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW SEND DELIMITX
               ER MACRO'
&NOGO    SETB  1
.SEQOK   AIF   ('&PAR1' NE '' AND '&PAR2' NE '').OK
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      AIF   (K'&PAR1 LT 5 OR K'&PAR1 GT 7 OR '&PAR1'(K'&PAR1,1) NE 'X
               ''').ER
         AIF   ('&PAR1'(1,2) EQ 'X''' AND K'&PAR1 EQ 5).OK2
         AIF   ('&PAR1'(1,4) EQ 'XL1''' AND K'&PAR1 GT 5).OK2
.ER      MNOTE 12,'***  IHB002  INVALID CHARACTER OPERAND SPECIFIED'
&NOGO    SETB  1
.OK2     AIF   (K'&PAR2 LT 5 OR '&PAR2'(K'&PAR2,1) NE '''').ER2
&CNT     SETA  &CNT+1
         AIF   ('&PAR2'(&CNT,2) EQ 'X''').FND
         AIF   ('&PAR2'(&CNT,1) LE '9' AND '&PAR2'(&CNT,1) GE '0' AND KX
               '&PAR2 NE &CNT).OK2
.ER2     MNOTE 12,'***  IHB002  INVALID ''ID'' OPERAND SPECIFIED'
         MEXIT
.FND     AIF   ((K'&PAR2-&CNT)/2*2 NE K'&PAR2-&CNT OR K'&PAR2-&CNT LT 4X
               ).ER2
.OK5     AIF   ('&PAR2'(1,2) EQ 'X''').BY
&GNAME   SETC  '&PAR2'(1,&CNT-1)
&IX      SETA  &GNAME
.BY      AIF   (&NOGO).END
&COUNT   SETA  &IX*((K'&PAR2-2-&CNT)/2)
&CNT     SETA  (&IX*((K'&PAR2-2-&CNT)/2))/2*2+14
&NAME    LA    3,*+&CNT                COMPUTE RETURN ADDRESS
         L     15,=V(IECKPAUS)         ADDRESS OF PAUSE ROUTINE
         BALR  14,15                   TO THE PAUSE ROUTINE
         DC    AL2(&COUNT)
         DC    &PAR1
         DC    &PAR2
         DS    0H
.END     MEND
