         MACRO
&ENTRY   TERM  &QTYP,&DCB,&RLN,&DVAD,&OPDT,&CALL=,&ID=,&BUFSIZE=
         GBLA  &QNAME,&TSIZE,&ACLOCX(21)
         GBLC  &QDCB,&LRLN,&OPTYPE(20),&SECT,&QCB
         LCLC  &D(17)
         LCLC  &ATR
         LCLA  &COUNT
         LCLB  &NOGO
.*3160410000-420000                                                000A
.*2719706000,790000                                                000C
.*1200542000-552000,930300-938400                                  000D
.* 210000,231000-239000,626000-632000                            A27843
         AIF   ('&ENTRY' NE '' AND '&QTYP' NE '' AND '&DCB' NE '' AND 'X
               &RLN' NE '').OK
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      AIF   (K'&ENTRY LE &TSIZE).OK1
         MNOTE 12,'***  IHB002  INVALID ENTRY OPERAND SPECIFIED'
&NOGO    SETB  1
.OK1     AIF   ('&QTYP' EQ 'T' OR '&QTYP' EQ 'L').OK2
         MNOTE 12,'***  IHB002  INVALID QUEUE OPERAND SPECIFIED'
&NOGO    SETB  1
.OK2     ANOP
&COUNT   SETA  &RLN
         AIF   (&COUNT LE 255).CHECK
         MNOTE 12,'***  IHB002  INVALID REL. LINE OPERAND SPECIFIED'
&NOGO    SETB  1
.CHECK   ANOP
         AIF   ('&OPDT' EQ '').OK3
&COUNT   SETA  N'&OPDT
         AIF   ('&OPTYPE(&COUNT)' EQ '').ERR
         AGO   .OK3
.ERR     MNOTE  12,'*** IHB088 REQUIRED OPTION MACRO NOT GENERATED'
&NOGO    SETB  1
.OK3     AIF   (&NOGO).EXIT
&COUNT   SETA  0
         AIF   ('&QTYP'  EQ 'T').QGEN
         AIF   ('&DCB' NE '&QDCB').QGEN
         AIF   ('&RLN' EQ '&LRLN').NOQ
.QGEN    ANOP
IECKQUE  CSECT
&QNAME   SETA  &QNAME+1
&QDCB    SETC  '&DCB'
&LRLN    SETC  '&RLN'
&QCB     SETC  'IHB'.'&SYSNDX'.'Q'
&QCB     DS    0F
         DC    AL1(3)             QKEY
         DC    AL3(&QNAME)         QUEUE NUMBER
         DC    A(0)               QLINK
         DC    X'68'         ENTRY CODE TO SEND SCHEDULER
         DC    AL3(&QCB+8)
         AIF   ('&CALL' EQ '').ND                                  000A
         AIF   ('&CALL' EQ 'NONE').ND                              000A
         DC    FL1'1'                                              000A
         AGO   .NDA                                                000A
.ND      DC    FL1'2'                                              000A
.NDA     DC    AL3(0)             ADDRESS OF MESSAGE QCB           000A
         DC    FL1'&RLN'           RELATIVE LINE NO.
         DC    AL3(&DCB)           DCB ADDR
         DC    H'0'
         DC    AL3(&QNAME)
         DC    AL3(&QCB+1)
         DC    A(0)
&SECT    CSECT
.NOQ     ANOP
&ENTRY   DC    AL1(IHB&SYSNDX-&ENTRY)  SIZE OF ENTRY
         DC    AL3(&QCB)               QUEUE CONTROL BLOCK ADDRESS
         DC    X'0001'       SEQIN
         DC    X'0001'       SEQOUT
         AIF   ('&BUFSIZE' EQ '').NOBUF                            000D
         DC    X'13'               TSTATUS                         000D
         AGO   .BUF                                                000D
.NOBUF   ANOP                                                      000D
         DC    X'03'               TSTATUS                         000D
.BUF     ANOP                                                      000D
         DC    CL&TSIZE'&ENTRY'        TERMINAL IDENTIFICATION
         DC    AL1(&ACLOCX(N'&OPDT+1)) OFFSET TO ADDR. OR DIAL DIGITS
         AIF   ('&OPDT' EQ '').DONE
.NEXT    AIF   (&COUNT EQ N'&OPDT).DONE
&COUNT   SETA  &COUNT+1
&ATR     SETC  '&OPTYPE(&COUNT)'
         AIF   ('&OPDT(&COUNT)' EQ '').B
         AIF   ('&ATR'(1,1) EQ 'A' OR '&ATR'(1,1) EQ 'V' OR '&ATR'(1,1)X
                EQ 'S' OR '&ATR'(1,1) EQ 'Y').A
         DC    &ATR'&OPDT(&COUNT)'
         AGO   .NEXT
.A       DC   &ATR.(&OPDT(&COUNT))           OPTION NO.&COUNT
         AGO   .NEXT
.B       DS    &ATR
         AGO   .NEXT
.DONE    AIF   ('&CALL' EQ '').ADDR
         AIF   ('&CALL' NE 'NONE').NDB                             000A
         DC    AL1(0)             NO OF DIAL DIGITS                000A
         AGO   .TERMID                                             000C
.NDB     ANOP                                                      000A
&COUNT   SETA  K'&CALL
         DC    AL1(&COUNT) NO. OF DIAL DIGITS
.CALLP   ANOP
&D(&COUNT) SETC '0'.'&CALL'(&COUNT,1)
&COUNT   SETA  &COUNT-1
         AIF   (&COUNT GT 0).CALLP
         DC    X'&D(1).&D(2).&D(3).&D(4).&D(5).&D(6).&D(7).&D(8).&D(9).-
               &D(10).&D(11).&D(12).&D(13).&D(14).&D(15).&D(16).&D(17)'
.TERMID  AIF   ('&ID' EQ '').ADDR                                  000C
&COUNT   SETA  (K'&ID+1)/2
         DC    AL1(&COUNT) NO. OF ID CHARACTERS
         DC    2X'&ID'
         AGO   .OUT
.ADDR    AIF   ('&DVAD' EQ '' AND '&CALL' EQ '').SKIP
         AIF   ('&DVAD' EQ '').OUT
         DC    X'&DVAD'
         AGO   .OUT
.SKIP    ANOP
*
*     CALL OR DEVICE ADDRESS NOT SPECIFIED
*     2740 MOD 1 OR MOD 6 ASSUMED.  ***********
*
.OUT     ANOP
IHB&SYSNDX DS  0F
.EXIT    MEND
