         MACRO
&NAME    TRANS &PAR1
         LCLC  &GNAME
.*2719342000-354000                                                000C
&NAME   DS    0H
         AIF   ('&PAR1' NE '').OK
         MNOTE 12,'***  IHB001  TABLE OPERAND REQ''D-NOT SPECIFIED'
         MEXIT
.OK      AIF   (K'&PAR1 LT 6 OR K'&PAR1 EQ 7).USEAD
         AIF   ('&PAR1'(1,3) NE 'RCV').SEND
&GNAME   SETC  'IECKR'.'&PAR1'(4,1)
         AIF   ('&PAR1'(4,1) EQ 'F').CK
&GNAME   SETC  'IECKRV'
         AIF   ('&PAR1'(4,1) EQ 'E').CK
.SEND    AIF   ('&PAR1'(1,4) NE 'SEND').USEAD
&GNAME   SETC  'IECKSD'
.CK      AIF   (K'&PAR1 EQ 6).SIX
         AIF   ('&PAR1'(5,4) EQ '2260').TWEN
         AIF   ('&PAR1'(5,4) NE 'ITA2').ZSC3                       000C
&GNAME   SETC  '&GNAME'.'TW'                                       000C
         AGO   .GEN                                                000C
.ZSC3    AIF   ('&PAR1'(5,4) NE 'ZSC3').NOWTTA                     000C
&GNAME   SETC  '&GNAME'.'TZ'                                       000C
         AGO   .GEN                                                000C
.NOWTTA  ANOP                                                      000C
&GNAME   SETC  '&GNAME'.'&PAR1'(7,2)
         AIF   ('&PAR1'(4,1) EQ 'F').LAST
         AIF   ('&PAR1'(5,4) EQ '1030').GEN
         AIF   ('&PAR1'(5,4) EQ '1060').GEN
.LAST    AIF   ('&PAR1'(5,4) EQ '1050').GEN
         AIF   ('&PAR1'(5,4) EQ '2740').GEN
         AGO   .USEAD
.SIX     ANOP
&GNAME   SETC  '&GNAME'.'&PAR1'(5,2)
         AIF   ('&PAR1'(5,2) EQ 'T1' OR '&PAR1'(5,2) EQ 'T2' OR '&PAR1'X
               (5,2) EQ 'T3').GEN
         AGO   .USEAD
.TWEN    ANOP
&GNAME   SETC  'IECK'.'&PAR1'(1,1).'260'
         AGO   .GEN
.USEAD   L     1,=A(&PAR1)             ADDRESS OF USER SUPPLIED TABLE
         AGO   .SREG
.GEN     L     1,=V(&GNAME)            ADDRESS OF TRANSLATE TABLE
.SREG    L     15,=V(IECKTRNS)         ADDRESS OF TRANSLATE ROUTINE
         BALR  14,15                   LINK TO THE TRANSLATE ROUTINE
         MEND
