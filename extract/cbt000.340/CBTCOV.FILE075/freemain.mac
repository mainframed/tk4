         MACRO
&NAME    FREEMAIN &MODE,&LV=,&LA=,&A=,&SP=,&MF=I
         LCLB  &AA,&B,&C,&M(3)
         LCLA  &CTR
         AIF   ('&MODE' EQ '' AND '&MF' EQ 'I').ERROR1
         AIF   ('&LV' NE '' AND '&LA' NE '').ERROR5
         AIF   ('&MODE' EQ '').CONTA
         AIF   ('&MODE' EQ 'E' AND '&LA' NE '').ERROR6
         AIF   ('&MODE' EQ 'R' AND '&LA' NE '').ERROR6
         AIF   ('&MODE' EQ 'L' AND '&LV' NE '').ERROR4
         AIF   ('&MODE' EQ 'V' AND '&LV' NE '').ERROR4
         AIF   ('&MODE' EQ 'V' AND '&LA' NE '').ERROR6
.CONTA   AIF   ('&MF' NE 'I').CONTB
         AIF   ('&MODE' EQ 'E' AND '&LV' EQ '').ERROR8
         AIF   ('&MODE' EQ 'L' AND '&LA' EQ '').ERROR8
         AIF   ('&MODE' NE 'R' AND '&A' EQ '').ERROR8
.CONTB   AIF   ('&MF' EQ 'L').LROUT
         AIF   ('&MF' EQ 'I').IROUT
         AIF   (N'&MF LE 1).ERROR2
         AIF   ('&MF(1)' NE 'E').ERROR2
&NAME    IHBINNRA &MF(2)
.CONTC   AIF   ('&MODE' EQ '').CONTD
&M(1)    SETB  ('&MODE' EQ 'E')
&M(2)    SETB  ('&MODE' EQ 'L')
&M(3)    SETB  ('&MODE' EQ 'V')
         AIF   (&M(1)+&M(2)+&M(3) NE 1).ERROR7
&CTR     SETA  128*&M(2)+192*&M(3)
         MVI   8(1),&CTR                         MOVE MODE BYTE TO LIST
.CONTD   AIF   ('&LV' EQ '').CONTG
         AIF   ('&LV'(1,1) EQ '(').ISAREGA
         AIF   (T'&LV EQ 'N').CONTAA
.CONTBB  ANOP
         B     *+8                               BRANCH AROUND LENGTH
         DC    AL4(&LV)                          LENGTH
         MVC   0(4,1),*-4                        MOVE LENGTH INTO LIST
         AGO   .CONTG
.CONTAA  AIF   (&LV GT 4095).CONTBB
         LA    14,&LV.(0,0)                      PICK UP LENGTH
         ST    14,0(1,0)                         STORE LENGTH INTO LIST
         AGO   .CONTG
.ISAREGA ST    &LV(1),0(1,0)                     STORE LENGTH INTO LIST
.CONTG   AIF   ('&LA' EQ '').CONTH
         AIF   ('&LA'(1,1) EQ '(').ISAREGB
         LA    14,&LA                            PICK UP LIST ADDRESS
         ST    14,0(1,0)                         STORE LA INTO LIST
         AGO   .CONTH
.ISAREGB ST    &LA(1),0(1,0)                     STORE LA INTO LIST
.CONTH   AIF   ('&A' EQ '').CONTI
         AIF   ('&A'(1,1) EQ '(').ISAREGC
         LA    14,&A                             PICK UP AREA LIST ADDR
         ST    14,4(1,0)                         STORE INTO LIST
         AGO   .CONTI
.ISAREGC ST    &A(1),4(1,0)                      STORE A-ADDR INTO LIST
.CONTI   AIF   ('&SP' EQ '').FINI
         AIF   ('&SP'(1,1) EQ '(').ISAREGD
         MVI   9(1),&SP                          MOVE SUBPOOL INTO LIST
         AGO   .FINI
.ISAREGD STC   &SP(1),9(1,0)                     STORE SUBPOOL IN LIST
.FINI    SVC   5                                 ISSUE FREEMAIN SVC
         MEXIT
.LROUT   AIF   ('&LV' EQ '').CONTJ
         AIF   ('&LV'(1,1) EQ '(').ERROR3
&NAME    DC    A(&LV)                            LENGTH
         AGO   .CONTL
.CONTJ   AIF   ('&LA' EQ '').CONTK
         AIF   ('&LA'(1,1) EQ '(').ERROR3
&NAME    DC    A(&LA)                            LIST ADDRESS
         AGO   .CONTL
.CONTK   ANOP
&NAME    DC    F'0'
.CONTL   AIF   ('&A' EQ '').CONTM
         AIF   ('&A'(1,1) EQ '(').ERROR3
         DC    A(&A)                             AREA LIST ADDRESS
         AGO   .CONTN
.CONTM   DC    F'0'                              AREA LIST ADDRESS
.CONTN   AIF   ('&MODE' EQ '').CONTO
&M(1)    SETB  ('&MODE' EQ 'E')
&M(2)    SETB  ('&MODE' EQ 'L')
&M(3)    SETB  ('&MODE' EQ 'V')
         AIF   (&M(1)+&M(2)+&M(3) NE 1).ERROR7
&CTR     SETA  128*&M(2)+192*&M(3)
         DC    AL1(&CTR)                         MODE BYTE
         AGO   .CONTSP
.CONTO   DC    AL1(0)                            MODE BYTE
.CONTSP  AIF   ('&SP' EQ '').CONTU
         AIF   ('&SP'(1,1) EQ '(').ERROR3
         DC    AL1(&SP)                          SUBPOOL VALUE
         AGO   .FINISH
.CONTU   DC    AL1(0)                            SUBPOOL VALUE
.FINISH  MEXIT
.IROUT   AIF   ('&MODE' EQ 'R').RROUT
         CNOP  0,4
&NAME    BAL   1,*+14                            BRANCH AROUND LIST
         AIF   ('&LV' EQ '').CNTA
         AIF   ('&LV'(1,1) EQ '(').CNTB
         DC    A(&LV)                            LENGTH
         AGO   .CNTC
.CNTB    DC    A(0)                              LENGTH
&AA      SETB  1
         AGO   .CNTC
.CNTA    AIF   ('&LA' EQ '').CNTDD
         AIF   ('&LA'(1,1) EQ '(').CNTD
         DC    A(&LA)                            LIST ADDRESS
         AGO   .CNTC
.CNTD    ANOP
&B       SETB  1
.CNTDD   DC    A(0)
.CNTC    AIF   ('&A'(1,1) EQ '(').CNTE
         DC    A(&A)                             AREA LIST ADDRESS
         AGO   .CNTF
.CNTE    DC    A(0)                              AREA LIST ADDRESS
&C       SETB  1
.CNTF    ANOP
&M(1)    SETB  ('&MODE' EQ 'E')
&M(2)    SETB  ('&MODE' EQ 'L')
&M(3)    SETB  ('&MODE' EQ 'V')
         AIF   (&M(1)+&M(2)+&M(3) NE 1).ERROR7
&CTR     SETA  128*&M(2)+192*&M(3)
         DC    AL1(&CTR)                         MODE BYTE
.CNTSP   AIF   ('&SP' EQ '').CNTL
         AIF   ('&SP'(1,1) EQ '(').ISAREGQ
         DC    AL1(&SP)                          SUBPOOL VALUE
         AGO   .CNTM
.ISAREGQ DC    AL1(0)                            SUBPOOL VALUE
         STC   &SP(1),9(1,0)                     STORE SUBPOOL
         AGO   .CNTM
.CNTL    DC    AL1(0)                            SUBPOOL VALUE
.CNTM    AIF   (NOT &AA).CNTN
         ST    &LV(1),0(1,0)                     STORE LENGTH IN LIST
         AGO   .CNTO
.CNTN    AIF   (NOT &B).CNTO
         ST    &LA(1),0(1,0)                     STORE LA INTO LIST
.CNTO    AIF   (NOT &C).CNTP
         ST    &A(1),4(1,0)                      STORE A-ADDR INTO LIST
.CNTP    SVC   5                                 ISSUE FREEMAIN SVC
         MEXIT
.RROUT   AIF   ('&LV' EQ '' AND '&A' EQ '').SPROUT
         AIF   ('&A' EQ '').ERROR8
         AIF   ('&LV' EQ '').ERROR8
         AIF   ('&LV' EQ '(0)' AND '&SP' NE '').ERROR9
         AIF   ('&LV'(1,1) EQ '(').ISARGA
         AIF   ('&SP' EQ '').CTUA
         AIF   ('&SP'(1,1) EQ '(').ISARGB
         CNOP  0,4
&NAME    B     *+8                               BRANCH AROUND SP+LV
         DC    AL1(&SP)                          SUBPOOL VALUE
         DC    AL3(&LV)                          LENGTH
         L     0,*-4                             LOAD SP AND LV
         AGO   .CTUD
.CTUA    AIF   (T'&LV EQ 'N').CONTCC
.CTUAA   CNOP  0,4
&NAME    B     *+8                               BRANCH AROUND LENGTH
         DC    A(&LV)                            LENGTH
         L     0,*-4                             LOAD LENGTH
.CTUD    AIF   ('&A'(1,1) EQ '(').ISARGD
         L     1,&A                              LOAD AREA ADDRESS
.CTUC    LA    1,0(1)                       CLEAR THE HIGH ORDER BYTE
.CTUCX   SVC   10                      ISSUE FREEMAIN SVC         P2504
         MEXIT
.CONTCC  AIF   (&LV GT 4095).CTUAA
&NAME    LA    0,&LV.(0,0)                       LOAD LENGTH
         AGO   .CTUD
.ISARGD  AIF   ('&A(1)' EQ '1').CTUC
         LR    1,&A(1)                           LOAD AREA ADDRESS
         AGO   .CTUC
.ISARGB  AIF   (T'&LV EQ 'N').CONTEE
.CONTFF  CNOP  0,4
&NAME    B     *+8                               BRANCH AROUND LENGTH
         DC    A(&LV)                            LENGTH
         LR    0,&SP(1)                          PICK UP SUBPOOL
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         O     0,*-10                            PACK SP AND LV
         AGO   .CTUD
.CONTEE  AIF   (&LV GT 4095).CONTFF
&NAME    LR    0,&SP(1)                          PICK UP SUBPOOL
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         LA    14,&LV.(0,0)                      PICK UP LENGTH
         OR    0,14                              PACK SP AND LV
         AGO   .CTUD
.ISARGA  ANOP
         AIF   ('&NAME' EQ '').UNNAMED
&NAME    DS    0H
.UNNAMED AIF   ('&LV(1)' EQ '0').CTUD
         AIF   ('&SP' EQ '').CTUG
         AIF   ('&SP'(1,1) EQ '(').CTUGP
         LA    0,&SP.(0,0)                       LOAD SUBPOOL
         AGO   .CTUGPP
.CTUGP   LR    0,&SP(1)                          LOAD SUBPOOL
.CTUGPP  SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         OR    0,&LV(1)                          PACK SP AND LV
         AGO   .CTUD
.CTUG    LR    0,&LV(1)                          LOAD LENGTH
         AGO   .CTUD
.SPROUT  AIF   ('&SP' EQ '').ERROR8
         AIF   ('&SP'(1,1) EQ '(').ISARGQ
&NAME    LA    0,&SP.(0,0)                       LOAD SUBPOOL
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         SR    1,1                               INDICATE SP RELEASE
         AGO   .CTUCX                                            P2504
.ISARGQ  AIF   ('&NAME' EQ '').NONE
&NAME    DS    0H
.NONE    AIF   ('&SP(1)' EQ '0').CTUXX
         LR    0,&SP(1)                          LOAD SUBPOOL
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
.CTUXX   SR    1,1                               INDICATE SP RELEASE
         AGO   .CTUCX                                            P2504
.ERROR1  IHBERMAC 17
         MEXIT
.ERROR2  IHBERMAC 35,,&MF
         MEXIT
.ERROR3  IHBERMAC 69
         MEXIT
.ERROR4  IHBERMAC 89
         MEXIT
.ERROR5  IHBERMAC 91
         MEXIT
.ERROR6  IHBERMAC 90
         MEXIT
.ERROR7  IHBERMAC 45,,&MODE
         MEXIT
.ERROR8  IHBERMAC 01
         MEXIT
.ERROR9  IHBERMAC 92
         MEXIT                                                BC A45446
.ERROR10 IHNERMAC 46,,&SP,1                                   BC A45446
         MEND
