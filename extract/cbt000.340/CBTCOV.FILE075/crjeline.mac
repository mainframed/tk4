         MACRO
&LINENAM CRJELINE &DDLINE=,&DDSYSIN=,&RLN=1,&LERB=(255,10,5,5),        C
               &TYPE=,&CODE=EBCD,&ADDR=,&FEATURE=,&ONLNT=YES
         GBLA  &TABIN,&TABIN1,&IHKLINS,&GRLN
         GBLB  &GNOGO,&IHKTABL,&GTABLE1,&GTABLE2,&GTABLE3,&SIM
         GBLB  &GTABLE4,&G274041,&GDIAL,&GINT
         GBLC  &G1050(100),&G1050D(100),&GDD,&GTYPE,&GCODE,&GN
         LCLA  &LNOGO,&ARLN,&MAX,&ALERB,&TYPTERM,&INDEX,&M
         LCLB  &NOTYPE,&BADFEAT,&CEBCD,&CBCD,&CCORRES
         LCLB  &OLTEST,&BADCODE,&NODCB,&LDIAL,&LINT
         LCLC  &TREBTC,&TRTCLC,&TRTCUC,&N,&FINTC,&CADDR,&L,&CHAR
.* 200000,205000-209000                                          A34586
.* C 348000,A 433000                                             A45647
.*       PARAMETER INITIALIZATION
&IHKLINS SETA  &IHKLINS+1
&N       SETC  '&IHKLINS'
&ARLN    SETA  4*&IHKLINS+4
&L       SETC  '&ARLN'
&INDEX   SETA  1
&FINTC   SETC  '0'
.*       CHECK FOR CORRECT MACRO SEQUENCING
         AIF   (NOT &IHKTABL).CHKTYP1
         MNOTE 8,'ALL CRJELINE MACROS MUST PRECEDE CRJETABL MACRO'
&GNOGO   SETB  1
.*
.*       PARAMETER VALIDIZATION AND COMPATIBILITY
.*
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF TYPE OPERAND
.CHKTYP1 AIF   ('&TYPE' NE '').CHK1050
         MNOTE 4,'TYPE KEYWORD OPERAND OMITTED'
&LNOGO   SETA  &LNOGO+1
&NOTYPE  SETB  1
         AGO   .TESTOPS
.CHK1050 AIF   ('&TYPE' NE '1050').CHK2740
&TYPTERM SETA  2
         AGO   .TESTOPS
.CHK2740 AIF   ('&TYPE' NE '2740-1').CHK2741
&TYPTERM SETA  4
         AGO   .TESTOPS
.CHK2741 AIF   ('&TYPE' NE '2741').CHKSIM
.*.CHK2741 AIF ('&TYPE' NE '2741').INVAL1 TEMPORARY
&TYPTERM SETA  8
         AGO   .TESTOPS
.* THE NEXT THREE CARDS ARE TEMPORARY FOR SIMULATED LINES
.CHKSIM  AIF   ('&TYPE' NE 'SIM').INVAL1
&TYPTERM SETA  127
         AGO   .TESTOPS
.INVAL1  ANOP
&NOTYPE  SETB  1
         MNOTE 4,'TYPE SPECIFIED NOT SUPPORTED BY CRJE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       DDLINE OPERAND
.TESTOPS AIF   ('&DDLINE' NE '').CHKLIN1
         MNOTE 4,'DDLINE KEYWORD OPERAND OMITTED'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKDD1
.CHKLIN1 AIF   (K'&DDLINE LE 8).CHKLIN2
         MNOTE 4,'DDLINE OPERAND EXCEEDS EIGHT CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
&MAX     SETA  8
         AGO   .CHKLIN3
.CHKLIN2 ANOP
&MAX     SETA  K'&DDLINE
.CHKLIN3 AIF   ('&DDLINE'(1,1) GE 'A' AND '&DDLINE'(1,1) LE 'Z').CHAR1
         MNOTE 4,'DDLINE OPERAND CONTAINS NON-ALPHABETIC FIRST         C
                  CHARACTER'
&LNOGO   SETA  &LNOGO+1
.CHAR1   ANOP
&INDEX   SETA  &INDEX+1
         AIF   (&INDEX GT &MAX).CHKDD1
&CHAR    SETC  '&DDLINE'(&INDEX,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHAR1
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHAR1
         MNOTE 4,'DDLINE OPERAND CONTAINS NON-ALPHAMERIC CHARACTER(S)'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF
.*       DDSYSIN OPERAND
.CHKDD1  ANOP
&INDEX   SETA  1
         AIF   ('&DDSYSIN' NE '').CHKDD2
         MNOTE 4,'DDSYSIN KEYWORD OPERAND OMITTED'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKLRB1
.CHKDD2  AIF   (K'&DDSYSIN LE 8).CHKDD3
         MNOTE 4,'DDSYSIN OPERAND EXCEEDS EIGHT CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
&MAX     SETA  8
         AGO   .CHKDD4
.CHKDD3  ANOP
&MAX     SETA  K'&DDSYSIN
.CHKDD4  AIF   ('&DDSYSIN'(1,1) GE 'A' AND '&DDSYSIN'(1,1) LE 'Z').CHAR
         MNOTE 4,'DDSYSIN OPERAND CONTAINS NON-ALPHABETIC FIRST        C
                  CHARACTER'
&LNOGO   SETA  &LNOGO+1
.CHAR    ANOP
&INDEX   SETA  &INDEX+1
         AIF   (&INDEX GT &MAX).CHKLRB1
&CHAR    SETC  '&DDSYSIN'(&INDEX,1)
         AIF   ('&CHAR' GE 'A' AND '&CHAR' LE 'Z').CHAR
         AIF   ('&CHAR' GE '0' AND '&CHAR' LE '9').CHAR
         MNOTE 4,'DDSYSIN OPERAND CONTAINS NON-ALPHAMERIC CHARACTER(S)'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF LERB OPERANDS
.CHKLRB1 AIF   (&TYPTERM EQ 127).CHKPNT
&INDEX   SETA  0
         AIF   (N'&LERB LE 4).CHKLRB2
         MNOTE 4,'EXCESSIVE SUBPARAMETERS ON LERB OPERAND'
&LNOGO   SETA  &LNOGO+1
.CHKLRB2 ANOP
&INDEX   SETA  &INDEX+1
         AIF   ('&LERB(&INDEX)' NE '').CHKLRBB
         MNOTE 4,'LERB OPERAND &INDEX OMITTED, MUST BE SPECIFIED'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKIND
.CHKLRBB AIF   (T'&LERB(&INDEX) EQ 'N').CHKLRB3
         MNOTE 4,'LERB OPERAND &LERB(&INDEX) OTHER THAN SELF DEFINING  C
                  TERM'
&LNOGO   SETA  &LNOGO+1
.CHKIND  AIF   (&INDEX NE 1).CHKLRB2
&MAX     SETA  255
         AGO   .CHKLRB2
.CHKLRB3 ANOP
&ALERB   SETA  &LERB(&INDEX)
         AIF   (&INDEX NE 1).CHKLRB4
         AIF   (&ALERB GE 1 AND &ALERB LE 255).SETUP1
         MNOTE 4,'LERB TRANSMCT VALUE NOT BETWEEN 1 AND 255 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
&MAX     SETA  255
         AGO   .CHKLRB2
.SETUP1  ANOP
&MAX     SETA  &LERB(1)
         AGO   .CHKLRB2
.CHKLRB4 AIF   (&INDEX NE 2).CHKLRB5
         AIF   (&ALERB GE 1 AND &ALERB LE &MAX).CHKLRB2
         MNOTE 4,'LERB DATACK VALUE NOT BETWEEN 1 AND LERB TRANSMCT    C
                  VALUE OR 255 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKLRB2
.CHKLRB5 AIF   (&INDEX NE 3).CHKLRB6
         AIF   (&ALERB GE 1 AND &ALERB LE &MAX).CHKLRB2
         MNOTE 4,'LERB INTREQ VALUE NOT BETWEEN 1 AND LERB TRANSMCT    C
                  VALUE OR 255 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKLRB2
.CHKLRB6 AIF   (&ALERB GE 1 AND &ALERB LE &MAX).CHKFEAT
         MNOTE 4,'LERB NOTTO VALUE NOT BETWEEN 1 AND LERB TRANSMCT     C
                  VALUE OR 255 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF FEATURE OPERANDS
.CHKFEAT AIF   ('&FEATURE' EQ '').CHKCODE
         AIF   (N'&FEATURE LE 2).CHKFEA1
         MNOTE 4,'EXCESSIVE SUBPARAMETERS ON FEATURE OPERAND'
&LNOGO   SETA  &LNOGO+1
&BADFEAT SETB  1
.CHKFEA1 ANOP
&INDEX   SETA  0
&FINTC   SETC  '0'
.CHKFEA2 ANOP
&INDEX   SETA  &INDEX+1
         AIF   (&INDEX GT 2).CHKCOM1
         AIF   ('&FEATURE(&INDEX)' EQ '').CHKFEA2
         AIF   ('&FEATURE(&INDEX)' NE 'DIAL').CHKINT
&LDIAL   SETB  1
         AGO   .CHKFEA2
.CHKINT  AIF   ('&FEATURE(&INDEX)' NE 'INTERRUPT').CHKFEA3
&LINT    SETB  1
&FINTC   SETC  '1'
         AGO   .CHKFEA2
.CHKFEA3 MNOTE 4,'FEATURE SUBPARAMETER &FEATURE(&INDEX) NOT SUPPORTED  C
                 BY CRJE'
&LNOGO   SETA  &LNOGO+1
&BADFEAT SETB  1
         AGO   .CHKFEA2
.*       CHECK FOR UNIQUENESS OF FEATURES SPECIFIED AND FOR
.*       COMPATIBILITY WITH TYPE SPECIFIED
.CHKCOM1 AIF   ('&FEATURE(1)' NE '&FEATURE(2)').CHKCOM2
         MNOTE 4,'DUPLICATE SUBPARAMETERS ON FEATURE OPERAND'
&LNOGO   SETA  &LNOGO+1
&BADFEAT SETB  1
.CHKCOM2 AIF   (&NOTYPE OR &BADFEAT).CHKCODE
         AIF   (NOT &LINT).CHKCODE
         AIF   (&TYPTERM EQ 8).CHKCODE
         MNOTE 4,'INTERRUPT FEATURE SUPPORTED FOR 2741 TERMINALS ONLY'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR CORRECT SPECIFICATION OF CODE OPERAND
.CHKCODE AIF   ('&CODE' NE 'EBCD').CHKBCD
&CEBCD   SETB  1
         AGO   .CHKCOM3
.CHKBCD  AIF   ('&CODE' NE 'BCD').CHKCORR
&CBCD    SETB  1
         AGO   .CHKCOM3
.CHKCORR AIF   ('&CODE' NE 'CORRES').CHKCOD1
&CCORRES SETB  1
         AGO   .CHKCOM3
.CHKCOD1 MNOTE 4,'CODE SPECIFIED NOT SUPPORTED BY CRJE'
&LNOGO   SETA  &LNOGO+1
&BADCODE SETB  1
         AGO   .CHKADDR
.*       CHECK FOR COMPATIBILITY OF CODE AND TYPE SPECIFIED
.CHKCOM3 AIF   (&NOTYPE OR &BADCODE).CHKADDR
         AIF   (&TYPTERM NE 2).CHKADDR                           A34586
         AIF   (&CEBCD).CHKADDR
         MNOTE 4,'CODES OTHER THAN EBCD NOT SUPPORTED FOR 1050         C
                  TERMINALS'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF ADDR OPERAND
.*       AND COMPATIBILITY WITH TYPE SPECIFIED
.CHKADDR AIF   (&NOTYPE).CHKONL1
         AIF   (&TYPTERM NE 2).CHKADD1
         AIF   ('&ADDR' NE '').CHKADD2
         MNOTE 4,'ADDR OPERAND CANNOT BE OMITTED FOR 1050 TERMINALS'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKONLT
.CHKADD1 AIF   ('&ADDR' EQ '').CHKONL1
         MNOTE 1,'ADDR OPERAND IGNORED FOR TERMINALS OTHER THAN 1050'
         AGO   .CHKONL1
.CHKADD2 AIF   (K'&ADDR EQ 2).CHKONL2
         MNOTE 4,'ADDR OPERAND INCORRECTLY SPECIFIED'
&LNOGO   SETA  &LNOGO+1
.CHKONL1 ANOP
&CADDR   SETC  '0'
         AGO   .CHKONLT
.CHKONL2 AIF   (&LDIAL).CHKONL3
&CADDR   SETC  '&ADDR.&N'
         AGO   .CHKONLT
.CHKONL3 ANOP
&CADDR   SETC  '&ADDR.&N.D'
.*       CHECK FOR CORRECT SPECIFICATION OF ONLNT OPERAND
.CHKONLT AIF   ('&ONLNT' EQ 'YES').SETONLT
         AIF   ('&ONLNT' EQ 'NO').CHKRLN1
         MNOTE 4,'ONLNT VALUE SPECIFIED NOT RECOGNIZED'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKRLN1
.SETONLT ANOP
&OLTEST  SETB  1
.*       CHECK FOR CORRECT SPECIFICATION OF RLN OPERAND
.CHKRLN1 AIF   (T'&RLN EQ 'N').CHKRLN2
         MNOTE 4,'RLN OPERAND &RLN OTHER THAN SELF DEFINING TERM'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKPNT
.CHKRLN2 ANOP
&ARLN    SETA  &RLN
         AIF   (&ARLN GE 1 AND &ARLN LE 255).CHKCOM5
         MNOTE 4,'RLN OPERAND NOT BETWEEN 1 AND 255 INCLUSIVE'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKPNT
.*       CHECK FOR COMPATIBILITY OF CURRENT AND PREVIOUS RLN OPERANDS
.CHKCOM5 AIF   (&IHKLINS EQ 1 AND &ARLN NE 1).ERROR1
         AIF   (&ARLN EQ 1).SETG
         AIF   (&ARLN EQ (&GRLN+1)).CHKCOM6
         MNOTE 4,'RLN OPERAND &RLN NOT IN SEQUENCE FOR LINE GROUP'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR COMPATIBILITY OF LINE GROUP DEPENDENT OPERANDS
.CHKCOM6 ANOP
&GRLN    SETA  &GRLN+1
&NODCB   SETB  1
         AIF   ('&GDD' EQ '&DDLINE').CHKCOM7
         MNOTE 4,'DDLINE OPERAND MUST BE THE SAME FOR ALL LINES IN A   C
                  LINE GROUP'
&LNOGO   SETA  &LNOGO+1
.CHKCOM7 AIF   ('&GTYPE' EQ '&TYPE').CHKCOM8
         MNOTE 4,'TYPE OPERAND MUST BE THE SAME FOR ALL LINES IN A     C
                  LINE GROUP'
&LNOGO   SETA  &LNOGO+1
.CHKCOM8 AIF   (&GDIAL AND &LDIAL).CHKPNT
         AIF   (NOT &GDIAL AND NOT &LDIAL).CHKPNT
         MNOTE 4,'DIAL FEATURE MUST BE EITHER OMITTED OR SPECIFIED FOR C
                  ALL LINES IN A LINE GROUP'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKPNT
.ERROR1  MNOTE 4,'RLN OPERAND MUST BE 1 ON THE FIRST LINE DEFINED'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKPNT
.SETG    AIF   (&LNOGO NE 0 OR &GNOGO).CHKPNT
&GRLN    SETA  1
&GDD     SETC  '&DDLINE'
&GTYPE   SETC  '&TYPE'
&GCODE   SETC  '&CODE'
&GDIAL   SETB  (&LDIAL)
&GINT    SETB  (&LINT)
&GN      SETC  '&N'
&NODCB   SETB  0
.*       CHECK FOR MACRO TERMINATION CONDITIONS
.CHKPNT  AIF   (&LNOGO EQ 0 AND &GNOGO).ERROR3
         AIF   (&LNOGO NE 0 AND NOT &GNOGO).ERROR2
         AIF   (&LNOGO EQ 0 AND NOT &GNOGO).GENER
         MNOTE 8,'&LNOGO TERMINATING ERROR(S) INCURRED, NO GENERATION'
         MEXIT
.ERROR2  MNOTE 8,'&LNOGO TERMINATING ERROR(S) INCURRED, GENERATION     C
                       TERMINATED'
&GNOGO   SETB  1
         MEXIT
.ERROR3  MNOTE 8,'NO TERMINATING ERRORS INCURRED, NO GENERATION DUE TO C
                  PREVIOUS ERRORS'
         MEXIT
.*       SET UP GLOBAL PARAMETERS TO INCLUDE NECESSARY TRANSLATE TABLES
.GENER   ANOP
         AIF   (&TYPTERM NE 2).GENER1
.TABLE1  ANOP
&TREBTC  SETC  'SD50'
&TRTCLC  SETC  'RC50'
&TRTCUC  SETC  'RF50'
&GTABLE1 SETB  1
         AGO   .GENER3
.GENER1  AIF   (&TYPTERM EQ 8).GENER2
         AIF   (&TYPTERM EQ 127).GENER3
         AIF   (&CCORRES).TABLE3
.TABLE2  ANOP
&TREBTC  SETC  'SD40'
&TRTCLC  SETC  'RC40'
&TRTCUC  SETC  'RF40'
&GTABLE2 SETB  1
         AGO   .GENER3
.TABLE3  ANOP
&TREBTC  SETC  'SD41'
&TRTCLC  SETC  'RC41'
&TRTCUC  SETC  'RF41'
&GTABLE3 SETB  1
         AGO   .GENER3
.GENER2  AIF   (&CEBCD).TABLE2
         AIF   (&CCORRES).TABLE3
.TABLE4  ANOP
&TREBTC  SETC  'SB40'
&TRTCLC  SETC  'RB40'
&TRTCUC  SETC  'RU40'
&GTABLE4 SETB  1
.GENER3  ANOP
.*
.*       GENERATION OF CONTROL BLOCKS
.*
         SPACE 2
IHKMAC   CSECT
         AIF   (&IHKLINS NE 1).GENER4
         SPACE 2
         ENTRY IHKGML,IHKGMS
         ENTRY IHKSTCB1,IHKSTCB2,IHKSTCB3
         EXTRN IHKCSV
         DC    CL8' IHKMAC '
         SPACE 2
*        OUTPUT WRITER STCB
         SPACE 1
IHKSTCB2 DC    A(IHKSTCB2)
         DC    A(ECBLIST)
         DC    2F'0'
         SPACE 2
*        CENTRAL COMMAND STCB
         SPACE 1
IHKSTCB3 DC    A(IHKSTCB3)
         DC    A(ECBLIST+4)
         DC    A(IHKCSV)
         DC    F'0'
         SPACE 2
*        LINE STCB(S) (CHAINED)
         SPACE 1
IHKSTCB1 DC    A(IHKSTCB1) .       OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    A(ECBLIST+8)
         DC    A(CLB&N+52)
         DC    F'0'
         AGO   .GENER5
.GENER4  ANOP
         SPACE 2
*        LINE STCB(S) (CHAINED)
         SPACE 1
         ORG   *-16
         DC    A(*+16-IHKMAC)
         ORG   *+12
         SPACE 1
STCB&N   DC    A(IHKSTCB1) .       OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    A(ECBLIST+&L)
         DC    A(CLB&N+52)
         DC    F'0'
.GENER5  ANOP
         SPACE 2
IHKECD   CSECT
         AIF   (&IHKLINS NE 1).GENER6
         SPACE 1
ECBLIST  EQU   *
         SPACE 1
         DC    A(IHKSTCB2+12)
         DC    X'80'
         DC    AL3(IHKSTCB3+12)
         DC    X'80' .             OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    AL3(IHKSTCB1+12)
         DC    A(0)
         SPACE 1
         AGO   .GENER7
.GENER6  ANOP
         SPACE 1
         ORG   *-8
         DC    X'00'
         ORG   *+3
         SPACE 1
         DC    X'80' .             OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    AL3(STCB&N+12)
         DC    A(0)
         SPACE 1
.*       CONSTRUCT GLOBAL TABLE OF UNIQUE 1050 TERMINAL
.*       ADDRESSING CHARACTERS FOR DTFRMLST'S
.GENER7  AIF   (&TYPTERM EQ 127).GENER10
         AIF   (&LDIAL).GENER8
         AIF   (&TYPTERM NE 2).GENER10
&TABIN   SETA  &TABIN+1
&G1050(&TABIN) SETC '&CADDR'
         AGO   .GENER10
.GENER8  ANOP
&TYPTERM SETA  &TYPTERM+1
         AIF   (&TYPTERM NE 3).GENER9
&TABIN1  SETA  &TABIN1+1
&G1050D(&TABIN1) SETC '&CADDR'
         AGO   .GENER10
.GENER9  ANOP
&G274041 SETB  1
.GENER10 ANOP
         SPACE 1
IHKBLKS  CSECT
         AIF   (&IHKLINS NE 1).GENER11
         SPACE 2
*        GETMAIN LIST
         SPACE 1
IHKGML   GETMAIN EC,MF=L
IHKGMS   GETMAIN  EC,MF=L
.GENER11 ANOP
         SPACE 1
*        CRJE CONTROL BLOCKS - CLB,DECB,DCB,LERB
         SPACE 1
         DS    0F
         AIF   (&TYPTERM NE 127).GENR
         AIF   (&SIM).GENT
         EXTRN ABTERM
&SIM     SETB  1
.GENT    ANOP
CLB&N    DC    AL1(&TYPTERM) .     DEVICE TYPE
         DC    AL3(DECB&N) .       LINE DECB ADDRESS
         DC    7F'0'
         DC    CL8'&DDSYSIN' .     DDNAME FOR SYSIN DATA SET
         DC    F'0'
         DC    AL1(&N) .           LINE SEQUENCE NUMBER
         DC    3X'00' .            LINE ADDRESS
         DC    4F'0'
         DC    V(IHKCMD)
         DC    V(IHKCMD)
         DC    F'0'
         DC    A(CLB&N)
         DC    11F'0'
         SPACE 2
         READ  DECB&N,SF,DCB&N,MF=L
         SPACE 2
DCB&N    DCB   DSORG=PO,BLKSIZE=880,LRECL=88,RECFM=FB,MACRF=(R),       C
               DDNAME=&DDLINE,EODAD=ABTERM
         AGO   .GENS
.GENR    ANOP
CLB&N    DC    X'0&TYPTERM' .      DEVICE TYPE
         DC    AL3(DECB&N) .       LINE DECB ADDRESS
         AIF   ('&TYPE' NE '1050' AND NOT &LDIAL).GENW
         DC    A(T&CADDR) .        DFTRMLST ADDRESS FOR READ TI
         AGO   .GENU
.GENW    ANOP
         DC    A(0) .              DFRTMLST FOR READ TI
.GENU    ANOP
         DC    3F'0'
         AIF   (NOT &CCORRES).GENCOR
&MAX     SETA  &FINTC
&MAX     SETA  &MAX+2
&FINTC   SETC  '&MAX'
.GENCOR  ANOP
         DC    X'0&FINTC' .        TERMINAL FEATURE FIELD
         DC    AL3(IECT&TREBTC) .  TRANSLATE - EBCDIC TO TERMINAL CODE
         DC    A(IECT&TRTCUC)   .  TABLE     - TERM. CODE TO UPPERCASE
         DC    A(IECT&TRTCLC)   .  ADDRESSES - TERM. CODE TO LOWERCASE
         DC    CL8'&DDSYSIN' .     DDNAME FOR SYSIN DATASET
         DC    F'0'
         DC    AL1(&N) .           LINE SEQUENCE NUMBER
         DC    3C' ' .             PHYSICAL LINE ADDRESS
         DC    4F'0'
         DC    V(IHKCMD)
         DC    V(IHKCMD)
         DC    F'0'
         DC    A(CLB&N)
         DC    11F'0'
         AIF   ('&TYPE' NE '1050' OR &LDIAL).GENX
         DC    2F'0' .             IHKLAY QUEUE POINTER AND TIME FIELD
.GENX    ANOP
         SPACE 2
         READ  DECB&N,T,DCB&GN,(,),(,),,&RLN,MF=L
         AIF   (&NODCB).GENER12
         SPACE 2
         AIF   (NOT &OLTEST).ERROPT
DCB&GN   DCB   DSORG=CX,MACRF=(R,W),DDNAME=&DDLINE,ERROPT=WCT,         C
               LERB=LERB&GN
         AGO   .GENER13
.ERROPT  ANOP
DCB&GN   DCB   DSORG=CX,MACRF=(R,W),DDNAME=&DDLINE,ERROPT=WC,          C
               LERB=LERB&GN
         AGO   .GENER13
.GENER12 ANOP
         SPACE 2
*
*        NO DCB GENERATED
*
.GENER13 ANOP
         SPACE 2
IHKLERB  CSECT
         SPACE 1
LERB&N   LERB  1,&LERB
.GENS    ANOP
         SPACE 2
IHKCLB   CSECT
         SPACE 2
*        CLB LIST
         SPACE 1
         AIF   (&IHKLINS NE 1).GENER14
         DC    X'80' .             OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    AL3(CLB&N)
         AGO   .GENER15
.GENER14 ANOP
         ORG   *-4
         DC    X'00'
         ORG   *+3
         SPACE 1
         DC    X'80' .             OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    AL3(CLB&N)
.GENER15 ANOP
         SPACE 2
IHKECL   CSECT
         SPACE 1
*        LOADER CONTROLLER ECB LIST
         SPACE 1
         AIF   (&IHKLINS NE 1).GENER16
         DC    V(CCTJLECB)
         DC    V(CCTCLECB)
         DC    X'80' .             OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    AL3(CLB&N+48)
         AGO   .GENER17
.GENER16 ANOP
         ORG   *-4
         DC    X'00'
         ORG   *+3
         SPACE 1
         DC    X'80' .             OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    AL3(CLB&N+48)
.GENER17 ANOP
         SPACE 2
IHKECS   CSECT
         SPACE 1
*        ECB LIST FOR PASSING CONTROL FROM
*        COMMAND PROCESSORS TO IHKSRV
         SPACE 1
         AIF   (&IHKLINS NE 1).GENER18
         DC    V(CCTMSECB)
         DC    V(CCTJSECB)
         DC    V(CCTCSECB)
         DC    X'80'               OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    AL3(CLB&N+16)
         AGO   .GENER19
.GENER18 ANOP
         ORG   *-4
         DC    X'00'
         ORG   *+3
         SPACE 1
         DC    X'80' .             OVERLAID ON FOLLOWING CRJELINE MACRO
         DC    AL3(CLB&N+16)
         MEXIT
.GENER19 ANOP
         MEND
