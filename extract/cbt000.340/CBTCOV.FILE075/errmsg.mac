         MACRO
&NAME    ERRMSG &PAR1,&PAR2,&PAR3
         LCLA  &K
         LCLB  &NOGO
         LCLA  &Q
         LCLC  &R
.* 086000-092000,490000,520000,623000-650000                     A29185
         AIF   ('&PAR1' NE '' AND '&PAR2' NE '' AND '&PAR3' NE '').OK
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      AIF   (K'&PAR1 LT 6 OR K'&PAR1 GT 9 OR '&PAR1'(K'&PAR1,1) NE 'X
               ''').ER
         AIF   (K'&PAR1 EQ 7 AND '&PAR1'(1,2) EQ 'X''').OK2
         AIF   ('&PAR1'(1,4) EQ 'XL2''').OK2
.ER      MNOTE 12,'***  IHB002  INVALID ERR. HALF WORD OPERAND SPECIFIEX
               D'
&NOGO    SETB  1
.OK2     AIF   ('&PAR2'(1,1) NE '=').OK3
         AIF   (K'&PAR2 LT 5).ER2
         AIF   ('&PAR2'(1,3) EQ '=CL' AND K'&PAR2 LT 15 AND '&PAR2'(K'&X
               PAR2,1) EQ '''').OK3
         AIF   ('&PAR2'(1,3) EQ '=C''' AND K'&PAR2 LT 13 AND '&PAR2'(K'X
               &PAR2,1) EQ '''').OK3
.ER2     MNOTE 12,'***  IHB002  INVALID DESTINATION OPERAND SPECIFIED'
&NOGO    SETB  1
.OK3     AIF   ('&PAR3'(1,1) NE '=').OK5
         AIF   (K'&PAR3 LT 4).ER3
&K       SETA  K'&PAR3-4
         AIF   ('&PAR3'(1,3) EQ '=C''' AND '&PAR3'(K'&PAR3,1) EQ '''').X
               OK5
         AIF   ('&PAR3'(1,3) EQ '=CL' AND '&PAR3'(K'&PAR3,1) EQ '''').OX
               K4
.ER3     MNOTE 12,'***  IHB002  INVALID ERROR MESSAGE OPERAND SPECIFIEDX
               '
         AGO   .END
.OK4     ANOP
&Q       SETA  0
.CT      AIF   ('&PAR3'(&Q+4,1) EQ '''').ENDCT
         AIF   ('&PAR3'(&Q+4,1) LT '1' OR '&PAR3'(&Q+4,1) GT '9').ER3
&Q       SETA  &Q+1
         AGO   .CT
.ENDCT   AIF   (&Q EQ 0 OR &Q GT 3).ER3
&R       SETC  '&PAR3'(4,&Q)
&K       SETA  &R
.OK5     AIF   (&NOGO).END
         AIF   ('&PAR3'(1,1) EQ '=').SETREG
&K       SETA  L'&PAR3
.SETREG  ANOP
&NAME    LA    0,&K                    LOAD SIZE OF ERROR MESSAGE
         LA    1,&PAR3                 ADDRESS OF ERROR MESSAGE
         AIF   ('&PAR2' NE 'SOURCE').NOTSRCE
         L     2,=A(TERMTBL)           TERM TABLE ADDRESS
         LH    9,10(6)                 OFFSET INTO TERM TABLE
         LA    2,9(9,2)                ADDRESS OF DESTINATION NAME
         AGO   .GETPAR1
.NOTSRCE LA    2,&PAR2                 ADDRESS OF DESTINATION NAME
.GETPAR1 L     15,=V(IECKERMG)         ERROR MESSAGE ROUTINE ADDRESS
         BALR  14,15                   TO THE ERROR MESSAGE ROUTINE
         DC    &PAR1
.END     MEND
