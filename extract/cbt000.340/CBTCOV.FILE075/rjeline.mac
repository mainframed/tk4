         MACRO
&LINNME  RJELINE &DDLINE=,&DDSYSIN=,&ID=,&MODE=,&RLN=1,                X
               &LERB=(255,10,5,5),&CP=MVT,&ONLNT=,&MODEM=,         000DX
               &MSGQEB=4,&JOBQEB=10,                             A26525X
               &IDVER=                                             000D
         GBLA  &IHKM
         GBLA  &IHKLINS,&IHKNDCB
         GBLB  &IHKCDSW
         GBLB  &IHKCPTP CONTROL PROGRAM TYPE SWITCH                000B
         GBLC  &IHKSAVE
         GBLB  &IHKMDSW
         GBLC  &IHKLNNM,&IHKLRBN
         GBLA  &IHKLBNM                                            000C
         GBLB  &IHKLCBM                                            000C
         GBLA  &IHKMQ,&IHKJQ                                     A26525
         LCLA  &N
         LCLA  &CDSW,&M,&RLNNO,&CT
         LCLA  &B,&A,&KDDSYSN,&KDDNME,&ISW,&NO,&HNO,&MNO,&KPC,&KCD
         LCLB  &CPTYP LOCAL CONTROL PROGRAM TYPE SWITCH            000B
         LCLC  &PC(31)
         LCLC  &CIBC,&INTFACE,&TRNSCOD,&ONL
         LCLB  &C                                                 M3102
         LCLB  &D                                                 M3102
         LCLA  &ATZ                                                000D
         LCLA  &LSTEN,&MAXID,&IDCNT,&IDCNTA,&UNONE,&LEFT           000D
.*34701660,1860,1880-1960,2060,4820-4840,4920-5160,5480,5620-5640  000A
.*34705700,5840-5900,5960,6100-6120                                000A
.*1210006000,296000-332000,384000-386000                           000B
.*2514006000,474000,630000,766000,772000                           000C
.*003000,453000-454000,458000-459000,463000,465000,468000-469000 A26525
.*477000,479000,482000-496000,500000,502000-503000,505000        A26535
.*511000-512000,514000-515000                                    A26525
.*181000,192000,205000-207000,248000-249000,266000,269000-270000 A26525
.*006000,474000,630000,766000,772000,117000,150000                 000D
.*468400-470000,487000-496000                                      000D
.*D 463600-463800,477600-477800                                  A60660
.*A 463600,469200,477600,488000                                  A60660
.*
.*       RJE LINE DEFINITION
.*
         AIF   (T'&MSGQEB NE 'N').ERRMSG                         A26525
         AIF   (&MSGQEB LT 1 OR &MSGQEB GT 255).ERRMSG           A26525
         AIF   (T'&JOBQEB NE 'N').ERRJOB                         A26525
         AIF   (&JOBQEB LT 1 OR &JOBQEB GT 255).ERRJOB           A26525
         AIF   ('&CP' NE 'MVT').ABC CHECK FOR MVT                  000B
         AGO   .ABE .                                              000B
.ABC     AIF   ('&CP' EQ 'MFT').ABD CHECK FOR MFT                  000B
         MNOTE 'CP INCORRECTLY SPECIFIED. MVT ASSUMED.'            000B
         AGO   .ABE .                                              000B
.ABD     ANOP .                                                    000B
&CPTYP   SETB  1 SET SWITCH FOR MFT                                000B
.ABE     ANOP .                                                    000B
         AIF   (&IHKCDSW).NFIRST CHECK FOR FIRST PASS              000B
&IHKCPTP SETB  (&CPTYP) IF FIRST, SET GLOBAL TO FIRST VALUE        000B
.NFIRST  AIF   ('&IHKCPTP' EQ '&CPTYP').TYPOK CHECK SET TYPE       000B
         MNOTE 'CP DEFINITIONS CONFLICT.'                          000B
         MNOTE 'GENERATION TERMINATED.'                            000B
         MEXIT                                                     000B
.TYPOK   ANOP .                                                    000B
         AIF   ('&MODE' EQ '').SETDF
         AIF   ('&MODE(1)' EQ '' OR '&MODE(1)' EQ 'IBC').SET1
         MNOTE 6,'MODE=&MODE(1) INCORRECTLY CODED.DEFAULT ASSUMED'
&CIBC    SETC  ''
         AGO   .X1
.SET1    ANOP
&CIBC    SETC  '&MODE(1)'
.X1      AIF   ('&MODE(2)' EQ 'A' OR '&MODE(2)' EQ 'B').SET2
         AIF   ('&MODE(2)' EQ '').DFLT1
         MNOTE 6,'MODE=&MODE(2) INCORRECTLY CODED.DEFAULT=A ASSUMED'
.DFLT1   ANOP
&INTFACE SETC  'A'
         AGO   .X2
.SET2    ANOP
&INTFACE SETC  '&MODE(2)'
.X2      AIF   ('&MODE(3)' EQ 'A' OR '&MODE(3)' EQ 'B').SET3
         AIF   ('&MODE(3)' EQ '').DFLT2
         MNOTE 6,'MODE=&MODE(3) INCORRECTLY CODED.DEFAULT=A ASSUMED'
.DFLT2   ANOP
&TRNSCOD SETC  'A'
         AGO   .X3
.SET3    ANOP
&TRNSCOD SETC  '&MODE(3)'
         AGO   .X3
.SETDF   ANOP
&CIBC    SETC  ''
&INTFACE SETC  'A'
&TRNSCOD SETC  'A'
.X3      ANOP
.Z1A     AIF   ('&ONLNT' EQ '' OR '&ONLNT' EQ 'NO').XA3            000C
         AIF   ('&ONLNT' EQ 'YES').SETONLT                         000C
         MNOTE 'ONLNT INCORRECT NO ONLINE TEST GENERATED'          000C
         AGO   .XA3                                                000C
.SETONLT ANOP                                                      000C
&ONL     SETC  'T'                                                 000C
.XA3     ANOP                                                      000C
         AIF   ('&LINNME' NE '').Z1
         MNOTE 'NAME FIELD OF RJELINE CONTAINS NO SYMBOLIC NAME'
         MNOTE 'GENERATION TERMINATED'
         MEXIT
.Z1      AIF   ('&DDSYSIN' NE '').Z2
         MNOTE 'REQUIRED KEYWORD = DDSYSIN MISSING'
         MNOTE 'GENERATION TERMINATED'
         MEXIT
.Z2      ANOP
         AIF   ('&MODEM' EQ '').MIDMAN      TEST TO SEE IF OPERAND USED
         AIF   ('&MODEM' EQ 'STANDARD').MIDMAN  DETERMINE TYPE OF MODEM
         AIF   ('&MODEM' EQ 'CC3977').ACT1
         AIF   ('&MODEM' EQ 'PTT').ACT2
         MNOTE 'KEYWORD CODED INCORRECTLY, DEFAULT ASSUMED'
         AGO   .MIDMAN
.ACT1    ANOP
&C       SETB  1             SWITCH INDICATING CCSUPPORT
         AGO   .MIDMAN
.ACT2    ANOP
&D       SETB  1             SWITCH INDICATING WORLD TRADE SUPPORT
.MIDMAN  ANOP
&KDDSYSN SETA  K'&DDSYSIN
         AIF   (&KDDSYSN LE 8).A1
         MNOTE 'DDSYSIN EXCEEDS 8 CHARACTERS'
.A1      AIF   ('&DDLINE' NE '').A2
         MNOTE 'KEYWORD MISSING   GENERATION TERMINATED'
         MEXIT
.A2      ANOP
&KDDNME  SETA  K'&DDLINE
         AIF   (&KDDNME LE 8).A3
         MNOTE 'DDLINE EXCEEDS 8 CHARACTERS'
.A3      ANOP
         AIF   (&IHKLINS EQ 0 AND &RLN NE 1).ERROR3
&IHKLINS SETA  &IHKLINS+1
         AIF   (&RLN EQ 1).SWOFF
&IHKNDCB SETA  1
         AGO   .CKTYP
.SWOFF   ANOP
&IHKNDCB SETA  0
.CKTYP   ANOP
         AIF   (T'&RLN NE 'N').ERROR1
         AIF   (&RLN LT 1 OR &RLN GT 255).ERROR1
&RLNNO   SETA  &RLN
         AIF   ('&ID' NE '').CKID
&CDSW    SETA  1
&ISW     SETA  1
         AGO   .CKLRB
.CKID    ANOP
&NO      SETA  N'&ID-1                                             000A
&HNO     SETA  N'&ID/2                                             000A
&MNO     SETA  &HNO*2                                              000A
         AIF   (&NO NE &MNO).ERR                                   000A
         AIF   ('&ID(1)' NE '1130' AND '&ID(1)' NE '2780' AND '&ID(1)' *
               NE '2020' AND '&ID(1)' NE 'INTMX' AND '&ID(1)'          X
               NE '2770').ERROR6                                   000D
&CDSW    SETA  0
&A       SETA  2                                                   000A
&N       SETA  K'&ID(2)                                            000A
.B3      ANOP
&CT      SETA  &CT+1
&PC(&CT) SETC  '&ID(&A)'
&KPC     SETA  K'&ID(&A)
         AIF   (&KPC NE &N).ERROR5                                 000A
         AIF   ('&ID(&A)'(1,2) EQ 'FE' OR '&ID(&A)'(&KPC-1,2) EQ       X
               'FE').ERROR7                                        000A
         AIF   ('&ID(1)' EQ '2780' AND '&ID(&A)'(&KPC-1,2)             X
               NE  'F6').ERROR8                                    000A
         AIF   ('&ID(1)' EQ '2770' AND '&ID(&A)'(&KPC-1,2)             X
               NE 'F0').ERROR9                                     000D
&A       SETA  &A+1
&KCD     SETA  K'&ID(&A)
         AIF   (&KCD LE 8).B6
         MNOTE 'TERMID EXCEEDS 8 CHARACTERS'
.B6      AIF   (&A EQ &NO+1).CKLRB                                 000A
&A       SETA  &A+1
         AGO   .B3
.ERR     MNOTE 12,'TERMID OR POLLING CHARACTER MISSING FROM ID'
         MNOTE 'GENERATION TERMINATED'
         MEXIT
.ERROR1  MNOTE 12,'RLN=&RLN NOT BETWEEN 1 AND 200'
         MNOTE 'GENERATION TERMINATED'
         MEXIT
.ERROR3  MNOTE 12,'RLN=&RLN INCORRECTLY SPECIFIED RLN MUST EQ 1 OR BE  X
               OMITTED FOR FIRST RJELINE MACRO CODED'
         MEXIT
.ERROR4  MNOTE 12,'POLLING CHARACTER NOT ALPHABETIC'
         MNOTE 'GENERATION TERMINATED'
         MEXIT
.ERROR5  MNOTE 12,'LENGTH OF ALL POLLING CHARACTERS ARE NOT EQUAL' 000D
         MNOTE 'GENERATION TERMINATED'
         MEXIT
.ERROR6  MNOTE 12,'TERMINAL TYPE EQUAL &ID(1) NOT SUPPORTED'       000A
         MEXIT                                                     000A
.ERROR7  MNOTE 12,'POLLING CHARACTER OF X''FE'' IS NOT ALLOWED'    000A
         MEXIT                                                     000A
.ERROR8  MNOTE 12,'POLLING CHARACTERS FOR 2780 INCORRECTLY CODED.      X
               SECOND CHARACTER NOT F6'                            000A
        MEXIT
.ERROR9  MNOTE 12,'POLLING CHARACTERS FOR 2770 INCORRECTLY CODED.      X
               DEVICE SELECTION CHARACTER NOT F0'                  000D
         MEXIT                                                     000D
.ERR1    MNOTE 8,'VALUE SET IN LENGTH FIELD NOT NUMERIC OR IS 0        X
               OR IS GREATER THAN 9 '                             .000D
         MEXIT                                                     000D
.ERR2    MNOTE 12,'CODED ID CHARACTERS GREATER THAN SPECIFED '    .000D
         MEXIT
.ERRMSG  MNOTE 12,'MSGQEB=&MSGQEB NOT BETWEEN 1 AND 255'         A26525
         MNOTE 'GENERATION TERMINATED'                           A26525
         MEXIT                                                   A26525
.ERRJOB  MNOTE 12,'JOBQEB=&JOBQEB NOT BETWEEN 1 AND 255'         A26525
         MNOTE 'GENERATION TERMINATED'                           A26525
         MEXIT                                                   A26525
.CKLRB   ANOP
IHKCSTCB CSECT
.*                  PICTURE OF SUBTASK CONTROL BLOCK
.*                  ************************************
.*                  *+0                                *
.*                  *    PTR TO NEXT STCB              *
.*                  *                                  *
.*                  ************************************
.*                  *+4                                *
.*                  *    PTR TO ECBLIST ENTRY          *
.*                  *                                  *
.*                  ************************************
.*                  *+8                                *
.*                  *    PTR TO REG SAVE AREA          *
.*                  *                                  *
.*                  ************************************
         SPACE 1
&N       SETA  &IHKLINS*16-16 .                                    000B
         AIF   (&IHKCDSW).GENCBS
         DC    AL1(&IHKLINS+2)
         DC    AL3(*-&N-1)
         DC    A(IHKAALST+20)     ADDR OF PTR TO ECB FOR SUBTASK A24702
         DC    A(&LINNME)          PTR TO REG SAVE AREA FOR A LINE
IHK&SYSNDX DC  X'40000000'
         AGO   .THRU
.GENCBS  ANOP
         ORG   *-15
         DC    AL3(*-IHKCSTCB+15)
         ORG   *+12
         DC    AL1(&IHKLINS+2)
         DC    AL3(*-&N-1)
&IHKM    SETA  &IHKM+1
&MNO     SETA   &IHKM*4+20                                       A24702
         DC    A(IHKAALST+&MNO)    ADDR OF PTR TO ECB FOR A SUB-TASK
         DC    A(&LINNME)          PTR TO REG SAVE AREA FOR A LINE
IHK&SYSNDX  DC X'40000000'
.THRU    ANOP
IHKAALST CSECT
         SPACE 1
*                   POINTERS TO ECB'S FOR COMMUNICATION SUB-TASKS
         SPACE 1
         AIF   (&IHKCDSW).NOECBNM
         ENTRY ECBWTR,ECBCMD
ECBRDR   DC    A(0)
ECBWTR   DC    V(IHKPOST1) .                                       000B
ECBCMD   DC    V(IHKPOST2)         ECB FOR CENTRAL COMMANDS      A24702
ECBQM    DC    A(IHKBGNIN+8)       ABNORMAL TERMINATION WILL USE A24702
ECBVH1   DS    0A                  THESE ECBS FOR SUBTASK ABEND  A24702
         DC    X'80'              ECBLIST ENDS HERE DURING START A24702
         DC    AL3(IHKBGNIN+12)                                  A24702
         EXTRN IHKBGNIN                                          A24702
         ENTRY ECBQM,ECBVH1                                      A24702
         DC    X'80'  WILL BE OVERLAID IF ANY RJELINE FOLLOWS
         DC    AL3(IHK&SYSNDX)
        AGO   .BYPASS
.NOECBNM ANOP
        ORG   *-4
         DC    X'00'
         ORG   *+3
         DC    X'80'  WILL BE OVERLAID IF ANY RJELINE FOLLOWS
         DC    AL3(IHK&SYSNDX)
.BYPASS  ANOP
         AIF   (&CPTYP).PROCEED SKIP NEXT SECTION ON MFT           000B
         AIF   (&IHKCDSW).NOFRST CHECK FOR FIRST PASS              000B
         SPACE 1                                                   000B
*ECB FOR R/I ATTACH FUNCTION                                       000B
         SPACE 1                                                   000B
IHKRIECB CSECT                                                     000B
         DC    A(0)                                                000B
         SPACE 1                                                   000A
*ECB LIST USED BY Q-MGR ATTACH RTN TO WAIT FOR LINES               000A
         SPACE 1                                                   000A
.NOFRST  ANOP .                                                    000B
IHKCDQMG CSECT                                                     000A
         AIF   (&IHKCDSW).NOTFRST       BRANCH IF NOT FIRST LINE   000A
         DC    A(IHKRIECB)                                         000B
         DC    X'80'     WILL BE OVERLAID IF ANY RJELINE FOLLOWS   000A
         DC    AL3(IHKH&SYSNDX)         ADDRESS OF LINE'S QMGR ECB 000A
         AGO   .PROCEED                                            000A
         SPACE 1                                                   000A
.NOTFRST ORG   *-4                                                 000A
         DC    X'00'                    OVERLAY X'80'              000A
         ORG   *+3                                                 000A
         DC    X'80'     WILL BE OVERLAID IF ANY RJELINE FOLLOWS   000A
         DC    AL3(IHKH&SYSNDX)         ADDRESS OF LINE'S QMGR ECB 000A
.PROCEED ANOP                                                      000A
         SPACE 1                                                   000A
IHKCLERB CSECT
*        LERB  1,&LERB
IHKP&SYSNDX LERB 1,&LERB
&IHKLRBN SETC  'IHKP&SYSNDX'
         SPACE 1
****************************************************************** 000C
*                                                                * 000C
*                  THE LINE CONTROL BLOCK TABLE                  * 000C
*        THE FIRST HALFWORD OF THIS TABLE CONTAINS THE NUMBER OF * 000C
*              LINE CONTROL BLOCKS                               * 000C
*        THE SECOND HALFWORD CONTAINS THE LENGTH OF EACH LCB     * 000C
*              IF THE LENGTH OF THE LCB IS TO BE CHANGED, THE DC * 000C
*              STATEMENT DEFINING THIS ATTRIBUTE MUST BE CHANGED * 000C
*                                                                * 000C
****************************************************************** 000C
         SPACE 1                                                 A24702
IHKCDNOL CSECT                                                   A24702
&IHKLBNM SETA  &IHKLBNM+1                                          000C
         AIF   (&IHKLCBM).LCBSK                                    000C
         DS    0F                                                  000C
         DC    HL2'&IHKLBNM'                                       000C
IHKCDLGL DS    0H                                                A24702
         ENTRY IHKCDLGL                                          A24702
         AGO   .LBSK                                               000C
.LCBSK   ANOP                                                      000C
         ORG   *-4                                               A24702
         DC    HL2'&IHKLBNM'                                       000C
.LBSK    ANOP                                                      000C
         DC    H'60'               LENGTH OF LCB                  19018
&IHKLCBM SETB  1                                                   000C
.LCBTIT  ANOP                                                      000C
         ENTRY IHKCDLCB                                            000C
IHKCDLCB CSECT                                                     000C
         SPACE 1
.*       PICTURE OF LINE CONTROL BLOCK
.*
.*                  ************************************
.*                  * +0       * +1                    *
.*                  * CODE =   *   POINTER  TO  DECB   *
.*                  * LINE TYPE*      FOR   DISK       *
.*                  *          *                       *
.*                  ************************************
.*                  * +4                               *
.*                  *                                  *
.*                  *  POINTER TO DECB FOR A LINE      *
.*                  *                                  *
.*                  ************************************
.*                  * +8       * +9                    *
.*                  * SWITCHES *    POINTER  TO FIRST  *
.*                  *          *  BYTE OF DISK BUFFER  *
.*                  *          *                       *
.*                  ************************************
.*                  * +12                              *
.*                  *    POINTER TO CURRENT TERMINAL   *
.*                  *        DIRECTORY  ENTRY          *
.*                  *                                  *
.*                  ************************************
.*                  * +16      *+17                    *
.*                  * NUMBER OF*  POINTER  TO  DEFINE  *
.*                  * TERMINALS*    TERMINAL  LIST     *
.*                  * ON LINE  *                       *
.*                  ************************************
.*                  * +20                              *
.*                  *                                  *
.*                  *  DDNAME = &DDSYSIN  (8BYTES)     *
.*                  *                                  *
.*                  ************************************
.*                  * +28                              *
.*                  *    POINTER TO MAIN STORAGE       *
.*                  * OBTAINED BY A GETMAIN IN THE     *
.*                  * LINE  WRITE ROUTINE              *
.*                  ************************************
.*                  * +32      * +33                   *
.*                  * SWITCHES *  POINTER TO           *           000A
.*                  *          *  TERMINAL TABLE       *           000A
.*                  *          *                       *
.*                  ************************************
.*                  * +36 (12 BYTES)                   *           000A
.*                  *   3  WORD PARAMETER AREA USED    *           000A
.*                  *  BY THE COLLECTOR/EMITTER        *
.*                  *  ROUTINES                        *
.*                  ************************************
.*                  * +48                              *           000A
.*                  *                                  *           000A
.*                  *  ECB USED BY Q-MGR ATTACH RTN.   *           000A
.*                  *                                  *           000A
.*                  ************************************           000A
.*                  *+52             *+54              *           000C
.*                  *    LCBBLKSZ    *    LCBSMFCT     *           000C
.*                  ************************************           000C
.*                  *+56             *                            19018
.*                  *    LCBCHARS    *                            19018
.*                  ******************                            19018
.*
.*
*                        LINE CONTROL BLOCK
*
IHKM&SYSNDX DS 0F                  ALINE LCB ON FULL WORD
         DC    X'00'  LINE TYPE -  SET BY LINE SCHEDULER AT INITIALIZE
*                     TIME
         DC    AL3(IHKR&SYSNDX)    PTR TO BSAM DECB
         AIF   ('&C' EQ '1').VALUE1  DETERMINE MODEM BYTE IN LCB
         AIF   ('&D' EQ '1').VALUE2
         DC    X'00'    VALUE FOR STANDARD MODEM IN LCB
         AGO   .REST
.VALUE1  ANOP
         DC    X'40'    CCSUPPORT INDICATOR IN LCB
         AGO   .REST
.VALUE2  DC    X'20'    WORLD TRADE SUPPORT INDICATOR IN LCB       000D
.REST    ANOP
         DC  AL3(IHKW&SYSNDX)          PTR TO BTAM DECB
         DC    A(IHKB&SYSNDX)      PTR TO DISK BUFFER AREA
         DC    A(0)                PTR TO CURRENT TRM DIR ENTRY
         DC    X'&HNO'             NO OF TERMS ON THIS LINE
         DC    AL3(IHKT&SYSNDX)    PTR TO DEFINE TERMINAL LIST
         DC    CL8'&DDSYSIN'
         DC    F'0'                PTR TO LWR  GETMAIN
         AIF   ('&ID' EQ '').A99                                   000A
         DC    A(IHKQ&SYSNDX)                                      000A
         AGO   .BY99                                               000A
.A99     DC    F'0'                                                000A
.BY99    ANOP                                                      000A
         DC    F'0'                USED BY C/E TO PASS PARAMETERS
         DC    F'0'                USED BY C/E TO PASS PARAMETERS
         DC    F'0'                USED BY C/E TO PASS PARAMETERS  000A
IHKH&SYSNDX DC F'0'                     ECB FOR QMGR ATTACH RTN    000A
.NOQMR   ANOP .                                                    000B
         DC    H'0'                DEFAULT PROC BLOCK              000C
         DC    H'0'                SYSIN COUNT FOR SMF             000C
         DC    H'0'                NO. OF CHARS. IN JOBNAME       19018
         SPACE 1                                                   000A
IHKCDTBL CSECT
         AIF   (&ISW EQ 1).DNTRM
&B       SETA  1
         SPACE 1                                                   000A
*                   TERMINAL  TABLE                                000A
         SPACE 1                                                   000A
IHKQ&SYSNDX DS 0F                                                  000A
         DC    A(*+6)                                              000A
         DC    2XL1'00'                                            000A
.LOOP    ANOP
&B       SETA  &B+1
&ATZ     SETA  K'&ID(2)/2                                          000D
         DC    XL&ATZ'&ID(&B)'                                     000D
.BYFIX   ANOP                                                      000A
&B       SETA  &B+1
         DC    AL4(&ID(&B))        PTR TO TERM DIR ENTRY
         AIF   (&B EQ &NO+1).DNTRM                                 000A
         AGO   .LOOP
.DNTRM   ANOP
         AIF   (&CDSW EQ 1).NOIDSP
         SPACE 2
*                   DEFINE TERMINAL LIST
         SPACE 1
         DC    2YL1(&HNO+1) TOTAL AND ACTIVE ENTRY COUNT           000A
&KPC     SETA  K'&ID(2)/2   NO OF POLLING CHARACTERS PER ENTRY     000A
&N       SETA  &KPC+2     WIDTH OF ENTRY                           000A
         DC    YL1(X'20'*&N+X'10'*1)   PACKED WIDTH AND WRAP BIT   000A
IHKT&SYSNDX DS 0X                                                  000A
&A       SETA  1
.LOOPB   ANOP
         DC    XL&KPC'&PC(&A)'                                     000A
         DC    X'2D'               ENQ
         DC    AL1(&A)             INDEX
         AIF   (&A EQ &HNO).OUT
&A       SETA  &A+1
         AGO   .LOOPB
.OUT     ANOP
&N       SETA  &N-1                                                000A
&A       SETA  &A+1                                                000A
         DC    &N.X'37'                                            000A
         DC    X'&A'                                               000A
         DC    X'FE'    END OF LIST CHARACTER                      000A
&N       SETA  &N+1                                                000A
&HNO     SETA  &HNO+1                                              000A
         DC    YL2(&HNO*&N+4)    OFFSET                            000A
         DC    X'FF'         END OF LIST CHAR                      000C
         AGO   .BY
.NOIDSP  ANOP
         SPACE 1
*                   DEFINE  TERMINAL  LIST
         AIF   ('&D' EQ '0').NOWRTD                               M3102
IHKY&SYSNDX    EQU    *                                          M3120
IHKT&SYSNDX DFTRMLST WTLIST,0,1,2D,2,1070,410,IHKY&SYSNDX        M3120
         AGO   .BY                                                M3102
.NOWRTD  ANOP                                                     M3102
         AIF   ('&IDVER' EQ '').NOTANLS                            000D
         AIF   (T'&IDVER(1) NE 'N').ERR1                           000D
         AIF   ('&IDVER(1)' GT '9').ERR1                           000D
         AIF   ('&IDVER(1)' EQ '0').ERR1                           000D
&LSTEN   SETA  N'&IDVER                                            000D
&MAXID   SETA  &IDVER(1)                                           000D
IHKT&SYSNDX  DS  0F                                                000D
         DC    X'FF'                                               000D
         DC    XL3'00'                                             000D
&UNONE   SETA  1                                                   000D
         DC    X'&LSTEN'                                           000D
&MAXID   SETA  &MAXID+&UNONE                                       000D
         DC    X'&MAXID'                                           000D
&MAXID   SETA  &MAXID-&UNONE                                       000D
         DC    X'&MAXID'                                           000D
         DC    X'00'                                               000D
         DC    XL&MAXID.'00'                                       000D
         DC    X'02'                                               000D
         DC    X'1070'                                             000D
&IDCNT   SETA  2                                                   000D
.CHKID   ANOP                                                      000D
         DC    X'&IDVER(&IDCNT)'                                   000D
&LEFT    SETA  K'&IDVER(&IDCNT)/2                                  000D
         AIF   ('&LEFT' GT '&MAXID').ERR2                          000D
&IDCNTA  SETA  &MAXID-&LEFT                                        000D
         AIF   ('&IDCNTA' EQ '0').DONTGEN                          000D
         DC    XL&IDCNTA.'00'                                      000D
.DONTGEN ANOP                                                      000D
         DC    X'00'                                               000D
         AIF   ('&IDCNT' EQ '&LSTEN').QUIT                         000D
&IDCNT   SETA  &IDCNT+&UNONE                                      .000D
         AGO   .CHKID                                              000D
.QUIT    ANOP                                                      000D
         DC    X'2D'                                               000D
         DC    XL&MAXID'00'                                        000D
         AGO   .BY                                                 000D
.NOTANLS ANOP                                                      000D
IHKT&SYSNDX DFTRMLST BSCLST,0,1,2D,2,1070
.BY      ANOP
IHKD&SYSNDX DS 22F                 DCBS WILL BE BUILT IN THIS SPACE
         SPACE 2
*        DECB  FOR DISK
         SPACE 2
*        READ  IHKR&SYSNDX,SF,IHKD&SYSNDX,,'S',,,MF=L
         READ  IHKR&SYSNDX,SF,IHKD&SYSNDX,,'S',,,MF=L
         AIF   (&IHKNDCB EQ 1).NODCB
         SPACE 2
*        DATA  CONTROL BLOCK FOR LINE
         SPACE 2
*        DCB   DSORG=CX,MACRF=(R,W),DDNAME=&DDLINE,
*              BUFL=410,EROPT=C.&ONL,LERB=&IHKLRBN                 000C
*              DEVD=BS,CODE=EBCDIC,MODE=(&CIBC,,&INTFACE,&TRANSCD)
IHKL&SYSNDX    DCB DSORG=CX,MACRF=(R,W),DDNAME=&DDLINE,                X
               BUFL=410,EROPT=C.&ONL,LERB=&IHKLRBN,                000CX
               DEVD=BS,CODE=EBCDIC,MODE=(&CIBC,,&INTFACE,&TRNSCOD)
&IHKLNNM SETC  'IHKL&SYSNDX'
.NODCB   ANOP
         ORG   &IHKLNNM+20         TO BEGIN. OF DCB + 20
         DC    AL1(&RLN)           UPDATE NO. OF BUFFERS REQUESTED
*                                  IN DCB TO NO. LINES IN LINE GROUP
         ORG
&LINNME  DC    3F'0'               BEGINNING OF REG SAVE AREA
        DC    V(IHKABLSD)    ENTRY POINT IN LINE SCHEDULER
         DC    3F'0'
         DC    A(IHKM&SYSNDX)      PTR TO LCB
         DC    10F'0'              END REG SAVE AREA
         SPACE 2
*        DECB  FOR LINE
         SPACE 2
*        READ IHKW&SYSNDX,TI,&IHKLNNM,'S','S','S',&RLN,MF=L
         READ IHKW&SYSNDX,TI,&IHKLNNM,'S','S','S',&RLN,MF=L
IHKB&SYSNDX DC 452X'00'
         AIF   (&IHKCDSW).DONOT
         SPACE 1
*                   POOL  CONTROL  LIST
         SPACE 1
IHKCCFP1 DS   0F
        ENTRY IHKCCFP1
         DC    A(LBLK1)            PTR TO 1ST BLOCK IN SUBPOOL1
&IHKMQ   SETA  &MSGQEB                                           A26525
         DC    H'&MSGQEB'                                        A26525
IHKCCFP2 DS    0F
         ENTRY IHKCCFP2
         DC    A(SBLK1)            PTR TO 1ST BLOCK IN SUBPOOL2
&IHKJQ   SETA  &JOBQEB                                           A26525
         DC    H'&JOBQEB'                                        A26525
         SPACE 1
*                   BEGIN  CHAIN OF BLOCKS IN SUBPOOL1
         SPACE 1
LBLK1    DS    0F                                                A26525
.NEW1    ANOP                                                    A26525
&A       SETA  &MSGQEB                                           A26525
         AIF   (&A EQ 1).GO                                      A60660
.USE1    ANOP
         DC    A(*+64)
         DS    CL60
&A       SETA  &A-1                                              A26525
         AIF   (&A NE 1).USE1                                      000D
.GO      ANOP                                                    A60660
IHKA&SYSNDX DC F'0'                                                000D
&IHKSAVE SETC  '&SYSNDX'
         DS    CL60
         AIF   (&IHKCDSW).USE22
         SPACE 1
*        BEGIN CHAIN OF BLOCKS IN SUBPOOL2
         SPACE 1
SBLK1    DS    0F                                                A26525
.NEW2    ANOP                                                    A26525
&A       SETA  &JOBQEB                                           A26525
         AIF   (&A EQ 1).GOT                                     A60660
.USE2    ANOP
         DC    A(*+8)
         DS    CL4
&A       SETA  &A-1                                              A26525
         AIF   (&A NE 1).USE2                                      000D
.GOT     ANOP                                                    A60660
IHKC&SYSNDX DC F'0'                                                000D
         DS    CL4                                                 000D
         AGO   .DONOT1
.DONOT   ANOP
         ORG   IHKCCFP1+4
&IHKMQ   SETA  &IHKMQ+&MSGQEB                                    A26525
         DC   H'&IHKMQ'                                          A26525
         ORG   IHKCCFP2+4
&IHKJQ   SETA  &IHKJQ+&JOBQEB                                    A26525
         DC   H'&IHKJQ'                                          A26525
         ORG   IHKA&IHKSAVE
         DC    A(IHKF&SYSNDX)      OVERLAY PRECEEDING PTR TO NEXT BLOCK
         ORG   IHKC&IHKSAVE
         DC    A(IHKG&SYSNDX)      OVERLAY PRECEEDING PTR TO NEXT BLOCK
         ORG
IHKF&SYSNDX DS   0F                                              A26525
         AGO   .NEW1                                             A26525
.USE22   ANOP
IHKG&SYSNDX DS   0F                                              A26525
         AGO   .NEW2                                             A26525
.DONOT1  ANOP
IHKLNTBL CSECT
         AIF   (&IHKCDSW).NOTBLNM
&IHKCDSW SETB  1
         SPACE 1
*                   LINE TABLE
         SPACE 1
IHKCDLNT DS    0F                  ALINE TABLE AND PROVIDE ENTRY POINT
         ENTRY IHKCDLNT
.NOTBLNM ANOP
         DC    CL8'&LINNME'        LINE NAME
         DC    A(&IHKLNNM)         PTR TO DCB FOR LINE
         DC    F'&RLNNO'           RELATIVE LINE NO
IHKMDLNO CSECT
         AIF   (&IHKMDSW).OMIT
IHKCDLLN DC    H'16'               NO BYTES PER ENTRY IN LINE TABLE
         ENTRY IHKCDLLN
IHKCDLNB DC    1H'0'  WILL BE OVERLAID BY SUCEEDING RJELINE MACROS
         ENTRY IHKCDLNB
&IHKMDSW SETB 1
.OMIT    ANOP
         ORG   *-2
         DC    H'&IHKLINS'         NO OF ENTRIES IN LINE TABLE
         MEND
