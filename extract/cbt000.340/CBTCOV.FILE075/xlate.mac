         MACRO
&NAME    XLATE &AREA,&LENGTH,&TO=E
.*         VS1 RELEASE 031 DELETIONS/CHANGES
.*0000480000                                                    XM09351
.*         RELEASE 21.8 DELETIONS/CHANGES
.*410000,420500-430000                                          SA67817
.*0000030000-060000,080000-170000,190000,210000-310000,330000-  SM00239
.*0000340000,360000,380000-520000,540000,570000                 SM00239
.*  TRANSLATE ASCII TO/FROM EBCDIC, USUALLY FOR READING/WRITING ASCII
.*  AL OR NL MAGNETIC TAPE.  THIS MACRO LOADS REGISTER 0 WITH A BYTE
.*  COUNT, REGISTER 1 WITH AN ADDRESS OF DATA TO BE TRANSLATED, AND
.*  SETS BIT 0 OF REGISTER 1 TO INDICATE THE DIRECTION OF TRANSLATION.
.*  THE AREA ARGUMENT MAY BE 1) NAME OF DATA AREA, 2) REGISTER 1 OR
.*  2-12 CONTAINING THE ADDRESS OF THE DATA AREA.  THE LENGTH ARGUMENT
.*  MAY BE 1) BYTE COUNT, 2) REGISTER 0 OR 2-12 CONTAINING BYTE COUNT.
.*  REGISTERS USED AS ARGUMENTS MUST BE IN THE FORM (R)
.*  AND THE HIGH ORDER BYTE IS ASSUMED TO BE ZERO.
         AIF   (N'&SYSLIST EQ 2).TWOARGS
         AIF   (N'&SYSLIST LT 2).ERR1
         MNOTE 8,'EXCESSIVE POSITIONAL PARAMETERS - IGNORED'
.TWOARGS AIF   ('&AREA' EQ '&LENGTH').ERR
         AIF   ('&AREA' EQ '(0)' OR '&LENGTH' EQ '(1)').ERR2
         AIF   ('&AREA'(1,1) EQ '(').REG1
&NAME    LA    1,&AREA                  ADDRESS OF AREA TO TRANSLATE
         AGO   .LEN1
.REG1    AIF   ('&AREA' EQ '(1)').NAME
&NAME    LR    1,&AREA(1)               ADDRESS OF AREA TO TRANSLATE
         AGO   .LEN1
.NAME    AIF   ('&NAME' EQ '').LEN1
&NAME    DS    0H
.LEN1    AIF   ('&LENGTH'(1,1) EQ '(').REG2
         LA    0,&LENGTH                LENGTH TO TRANSLATE
         AGO   .TO
.REG2    AIF   ('&LENGTH' EQ '(0)').TO
         LR    0,&LENGTH(1)             LENGTH TO TRANSLATE
.TO      AIF   ('&TO' EQ 'E').SVC       ALREADY SET ASCII TO EBCDIC
         LA    15,X'80'                 CONSTRUCT HIGH ORDER
         SLL   15,24                    BIT MASK
         OR    1,15                     SET EBCDIC TO ASCII INDICATOR
.SVC     SVC   103                      ISSUE XLATE SVC
         MEXIT
.ERR     MNOTE 8,'AREA AND LENGTH OPERANDS ARE EQUAL'
         MEXIT
.ERR1    IHBERMAC 24          IHB004 REQUIRED OPERAND(S) NOT SPECIFIED
         MEXIT
.ERR2    MNOTE 8,'INVALID REGISTER SPECIFICATION'
         MEND
