         MACRO
&NAME    ABEND &CC,&DUMP,&STEP                                    19872
.*000400,000800,001600-001700,001800-002600,003400-005400         19872
.*005800-006000,006200,006800-007200,007600-008400,008800         19872
         LCLA  &CTR
         LCLB  &B(2)                                              19872
&NAME    DS    0H
         AIF   ('&CC' EQ '').E1
&B(1)    SETB  ('&DUMP' EQ 'DUMP')
&B(2)    SETB  ('&STEP' EQ 'STEP')
         AIF   ('&DUMP' NE '' AND NOT &B(1)).E2                   19872
.TEST    AIF   ('&STEP' NE '' AND NOT &B(2)).E3                   19872
.SETFLGS ANOP
&CTR     SETA  128*&B(1)+64*&B(2)                                 19872
         AIF   ('&CC'(1,1) EQ '(').CONTA
         AIF   (T'&CC NE 'N' OR '&DUMP&STEP' NE '').AA
         AIF   (&CC GT 4095).AA
.CONTA   ANOP
         IHBINNRA &CC         LOAD REG1 WITH COMPCODE             19872
         AIF   (&CTR EQ 0).SVC                                    19872
         LA    0,&CTR         PICK UP DUMP/STEP CODE
         SLL   0,24(0)                           SHIFT TO HIGH BYTE
         OR    1,0                               OR IN WITH COMPCODE
         AGO   .SVC
.AA      CNOP  0,4
         B     *+8            BRANCH AROUND CONSTANT
         DC    AL1(&CTR)                         DUMP/STEP CODE
         DC    AL3(&CC)                          COMPLETION CODE
         L     1,*-4                             LOAD CODES INTO REG 1
.SVC     SVC   13                                LINK TO ABEND ROUTINE
         MEXIT
.E2      IHBERMAC  37,,&DUMP
         AGO   .TEST
.E3      IHBERMAC  38,,&STEP
         AGO   .SETFLGS
.E1      IHBERMAC 28,303
         MEND
