         MACRO
         IDFIN04
         COPY IDFGBL
         AIF   (NOT &PIB(48)).TR
         MNOTE *,'IDF100 IN TRACE MODE ENTERING IDFIN04'
.TR      ANOP
.*********************************************************************
.*                                                                   *
.*                       DOCUMENT FIELD BOUNDARIES                   *
.*                                                                   *
.*********************************************************************
&B(9)    SETB  (&PIA(1) EQ 4)
&B(20)   SETB  ('&SYSLIST(1,1)' EQ 'X1' OR '&SYSLIST(1,1)' EQ 'X2')
&PIB(14) SETB  (&B(20))                                           FSJW
&B(19)   SETB  0                                                  FSJW
         AIF   (NOT &B(9)).BX
&M       SETA  12                       POINT TO LEFT BOUND IN CALL
&PIB(1)  SETB  (&PIB(2) AND (N'&SYSLIST(&M) NE 0 OR                    X
               N'&SYSLIST(22) NE 0))
         AIF   (NOT &PIB(1)).I0118
         IDFMSG1 100
.I0118   ANOP ,
         AIF   (&PIB(2)).BDX
         AIF   (T'&SYSLIST(&M,1) EQ 'O').BD1
&N       SETA  1
&PIB(12) SETB  ('&SYSLIST(&M,1)' EQ 'DUMMY' AND &PIB(6))
         AIF   (&PIB(12)).BD1
&PIB(1)  SETB  (T'&SYSLIST(&M,1) NE 'N' OR '&SYSLIST(&M,1)'(1,1) EQ    *
               'C')
         AIF   (NOT &PIB(1)).I0119
         IDFMSG2 200
.I0119   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).BD1
&A(1)    SETA  &SYSLIST(&M,1)           CONVERT CODE TO NUMERIC
&A(5)    SETA  &DFA(3)                  LOWER LIMIT
&A(6)    SETA  &DFA(27)                 UPPER LIMIT
&PIB(1)  SETB  (&A(1) LT &A(5) OR &A(1) GT &A(6))
         AIF   (NOT &PIB(1)).I0120
         IDFMSG2 201
.I0120   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).BD1
&DFA(11) SETA  &A(1)
.BD1     ANOP
&DFA(19) SETA  &DFA(11)-1 DEFAULT LENGTH ZERO
&B(1)    SETB  (N'&SYSLIST(&M) LT 2)
&M       SETA  12+10*&B(1)
&N       SETA  2-&B(1)
         AIF   (T'&SYSLIST(&M,&N) EQ 'O').BD4
         AIF   (NOT &PIB(12) AND K'&SYSLIST(&M,&N) GT 5 AND            X
               T'&SYSLIST(&M,&N) NE 'N').BD2
&PIB(1)  SETB  (T'&SYSLIST(&M,&N) NE 'N' OR '&SYSLIST(&M,&N)'(1,1)     X
               EQ 'C')
         AIF   (NOT &PIB(1)).I0121
         IDFMSG2 200
.I0121   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).BDX
&A(1)    SETA  &SYSLIST(&M,&N)
&A(5)    SETA  &DFA(11)
&A(6)    SETA  &DFA(27)
&PIB(1)  SETB  (&A(1) LT &A(5) OR &A(1) GT &A(6))
         AIF   (NOT &PIB(1)).I0122
         IDFMSG2 201
.I0122   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).BDX
&DFA(19) SETA  &A(1)
         AGO   .BD4
.BD2     ANOP
&PIB(1)  SETB  ('&SYSLIST(&M,&N)'(1,4) NE 'LNG(')
         AIF   (NOT &PIB(1)).I0123
         IDFMSG2 200
.I0123   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).BDX
&I       SETA  5
&C(1)    SETC  '0000000'                INITIALIZE CHARACTER COLLECTOR
&C(2)    SETC  '&SYSLIST(&M,&N)'(&I,1)
.BD3     ANOP
&PIB(1)  SETB  ('&C(2)' LT '0' OR '&C(2)' GT '9')
         AIF   (NOT &PIB(1)).I0124
         IDFMSG3 514
.I0124   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).BDX
&C(1)    SETC  '&C(1)'(2,6).'&C(2)'
&I       SETA  &I+1                     MOVE SCAN POINTER
&C(2)    SETC  '&SYSLIST(&M,&N)'(&I,1)
         AIF   ('&C(2)' NE ')' AND '&C(1)'(1,1) EQ '0').BD3
&A(1)    SETA  &C(1)
&A(5)    SETA  1
&A(6)    SETA  &DFA(27)-&DFA(11)+1
&PIB(1)  SETB  (&A(1) LT &A(5) OR &A(1) GT &A(6))
         AIF   (NOT &PIB(1)).I0125
         IDFMSG2 201
.I0125   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).BDX
&DFA(19) SETA  &DFA(11)+&A(1)-1
&I       SETA  &I+1
&PIB(1)  SETB  (&I LT K'&SYSLIST(&M,&N))
         AIF   (NOT &PIB(1)).I0126
         IDFMSG3 515
.I0126   ANOP ,
.BD4     ANOP
&PIB(1)  SETB  (N'&SYSLIST(&M) GT &N)
         AIF   (NOT &PIB(1)).I0127
         IDFMSG1 104
.I0127   ANOP ,
&PRTA(12) SETA &PRTA(11)
.BDX     ANOP
&PIB(1)  SETB  (&PIB(11))
         AIF   (NOT &PIB(1)).I0128
         IDFMSG3 516  DEAD CODE
.I0128   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CTX
.**********************************************************************
.*                                                                    *
.*                           BATCH OPERAND                            *
.*                                                                    *
.**********************************************************************
&M       SETA  21                       POINT TO BATCH IN CALL
&PIB(1)  SETB  (&PIB(2) AND N'&SYSLIST(&M) NE 0)
         AIF   (NOT &PIB(1)).I0129
         IDFMSG1 100                    CONTINUATION MESSAGE
.I0129   ANOP ,
         AIF   (&PIB(2)).BX
&PIB(1)  SETB  (&PIB(12) AND N'&SYSLIST(&M) NE 0)
         AIF   (NOT &PIB(1)).I0130
         IDFMSG1 101
.I0130   ANOP ,
         AIF   (&PIB(12)).BX
.B1      AIF   (&PIA(6) GE &PIA(8)).B2
         IDFASM (,4670)
         AGO   .B1
.B2      ANOP
&FB(3)   SETB  0                        NUMERIC
&PIB(1)  SETB  (T'&SYSLIST(&M,1) NE 'O' AND &B(20))               FSJW
         AIF   (NOT &PIB(1)).I0132A                               FSJW
&PIB(1)  SETB  1                                                  FSJW
         IDFMSG1 101                                              FSJW
.I0132A  ANOP  ,                                                  FSJW
         AIF   (T'&SYSLIST(&M,1) EQ 'O' OR &B(20)).BXS            FSJW
&PIB(1)  SETB  (T'&SYSLIST(&M,1) NE 'N' OR '&SYSLIST(&M,1)'(1,1) EQ    *
               'C')                     TEST FOR NUMERIC
&N       SETA  0
         AIF   (NOT &PIB(1)).I0131
         IDFMSG2 200                    INVALID OPERAND MESSAGE
.I0131   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).BX             EXIT ON ERROR
&A(1)    SETA  &SYSLIST(&M,1)           CONVERT CODE TO INTERNAL VALUE
&A(5)    SETA  1                        SET LOWER LIMIT
&A(6)    SETA  128                      SET UPPER LIMIT
&PIB(1)  SETB  (&A(1) LT &A(5) OR &A(1) GT &A(6))
         AIF   (NOT &PIB(1)).I0132
         IDFMSG2 201                    INVALID OPERAND MESSAGE
.I0132   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))    ACCUMULATE ERROR
         AIF   (&PIB(1)).BX             EXIT ON ERROR
         IDFASM (,4677)                 DEFINE-BATCH OPCODE PART 1
         IDFASM (,4070)                 DEFINE-BATCH OPCODE PART 2
&A(1)    SETA  &A(1)-1                  DECREMENT TO INTERNAL VALUE
         IDFASM A                       BATCH NUMBER
&FB(3)   SETB  1                        SET NUMERIC CHECKING FLAG
.BXS     ANOP
&N       SETA  1
&PIB(1)  SETB  (N'&SYSLIST(&M) GT &N)
         AIF   (NOT &PIB(1)).I0133
         IDFMSG1 104                    EXCESS SUBOPERANDS MESSAGE
.I0133   ANOP ,
.BX      ANOP  , GENERAL EXIT FROM BATCH OPERAND PROCESSING
.**********************************************************************
.*                                                                    *
.*                           SOURCE OPERAND                           *
.*                                                                    *
.**********************************************************************
&M       SETA  1
&PIB(1)  SETB  (&PIB(2) AND &PIA(10) NE &M AND                         X
               N'&SYSLIST(&M) NE 0)
         AIF   (NOT &PIB(1)).I0134
         IDFMSG1 100
.I0134   ANOP ,
         AIF   (&PIB(2)).SRX           IF CONTINUED
&PIB(12) SETB  (&PIB(12) AND &B(9))
&PIB(1)  SETB  (&PIB(12) AND N'&SYSLIST(&M) NE 0)
         AIF   (NOT &PIB(1)).I0135
         IDFMSG1 101
.I0135   ANOP ,
         AIF   (&PIB(12)).SRX
&K       SETA  &PIA(1)+1
&N       SETA  0                       SET INDEX FOR INVALID SOURCE MSG
         AIF   (K'&SYSLIST(&M,1)   LE 8).SR01                 IR00304
&C(1)    SETC  '&SYSLIST(&M,1)'(1,8)                          IR00304
         AGO   .SR1                                           IR00304
.SR01    ANOP                                                 IR00304
&C(1)    SETC  '&SYSLIST(&M,1)'
         AIF   ('&C(1)' NE '').SR1
&DSCB(&K)      SETB (&DSCB(&K-1))      ASSIGN
&DSCA(&K)      SETA &DSCA(&K-1)        DEFAULTS
&DSCB(&K+5)    SETB (&DSCB(&K+4))      IF
&DSCB(&K+10)   SETB (&DSCB(&K+9))      OMITTED
         AIF   (NOT &DSCB(&K) AND N'&SYSLIST(&M) NE 0).SR4
&DSCB(&K+15)   SETB (&DSCB(&K+14))
&DSCB(&K+20)   SETB (&DSCB(&K+19))
&DSCB(&K+25)   SETB (&DSCB(&K+24))
&DSCB(&K+65)  SETB  (&DSCB(&K+64))                                FSJW
&DSCB(&K+70)  SETB  (&DSCB(&K+69))                                FSJW
&B(1)    SETB  1                       INDICATE SAME AS HIGHER LEVEL
&B(10)   SETB  (&DSCB(&K) AND NOT (&DSCB(&K+5) OR &DSCB(&K+10) OR      X
               &DSCB(&K+15) OR &DSCB(&K+20) OR &DSCB(&K+25)))
&B(11)   SETB  (&DSCB(&K) AND NOT (&DSCB(&K+5) OR &DSCB(&K+10) OR      X
               &DSCB(&K+15) OR &DSCB(&K+20)) AND &DSCB(&K+25))
&B(12)   SETB  (&DSCB(&K) AND NOT (&DSCB(&K+5) OR &DSCB(&K+10) OR      X
               &DSCB(&K+15)) AND &DSCB(&K+20) AND &DSCB(&K+25))
&B(13)   SETB  0
&B(14)   SETB  (&DSCB(&K) AND (&DSCB(&K+10) OR &DSCB(&K+15)))
&DSCB(&K+65)  SETB  (&DSCB(&K+65) OR (&B(14) AND                       C
               '&SYSLIST(&M,2)' EQ 'X2'))                         FSJW
&DSCB(&K+70)  SETB  (&DSCB(&K+70) OR (&B(14) AND                       C
               '&SYSLIST(&M,2)' EQ 'X1'))                         FSJW
         AIF   (&B(14)).SR8
&N       SETA  1                       SET MAX OPNDS
         AGO   .SR16                   AND TEST
.SR1     ANOP  ,                       HERE IF SOMETHING CODED
&B(10)   SETB  0                                                  FSJW
&B(11)   SETB  0                                                  FSJW
&B(12)   SETB  0                                                  FSJW
&B(13)   SETB  0                                                  FSJW
&B(14)   SETB  0                                                  FSJW
         AIF   (&B(20)).SR15A          IF SOURCE=X1/X2            FSJW
         AIF   (K'&SYSLIST(&M,1) LT 3).SR5
&DSCB(&K)      SETB ('&C(1)' NE 'KBD')
&DSCB(&K+5)    SETB ('&C(1)' EQ 'RPB' AND &PIB(24) OR '&C(1)' EQ       X
               'CCR')
&DSCB(&K+10)   SETB 0
         AIF   (NOT &DSCB(&K)).SR4     PICK UP KBD OPTIONS
.*                                     ACCUMULATE &B(1)
&DSCB(&K+10)   SETB ('&C(1)' EQ 'CCR' OR '&C(1)' EQ 'IDR' OR '&C(1)'   C
               EQ 'IOB')                                          FSJW
&B(1)    SETB  (&DSCB(&K) EQ &DSCB(&K-1) AND &DSCB(&K+5) EQ            X
               &DSCB(&K+4) AND &DSCB(&K+10) EQ &DSCB(&K+9)) AJU 4/23/73
&DSCB(&K+15)   SETB (&DSCB(&K+5) OR '&C(1)' EQ 'STG' OR                X
               '&C(1)' EQ 'INQ' OR '&C(1)' EQ 'RDR' AND NOT &PIB(24))
&DSCB(&K+15)  SETB  (&DSCB(&K+15) OR '&C(1)' EQ 'LPB'                  C
               OR '&C(1)' EQ 'IDR')                               FSJW
&B(1)    SETB  (&B(1) AND &DSCB(&K+15) EQ &DSCB(&K+14))
&B(14)   SETB  (&DSCB(&K+10) OR &DSCB(&K+15))
&B(12)   SETB  ('&C(1)'(1,3) EQ 'CTR')
&B(13)   SETB  (&B(9) AND '&C(1)'(1,1) EQ '''' AND NOT &FB(3))
&DSCB(&K+20)   SETB (&DSCB(&K+5) OR '&C(1)' EQ 'RDR' AND NOT &PIB(24)  X
               OR '&C(1)' EQ 'LPB' OR &B(13))                     FSJW
&DSCB(&K+20)  SETB (&DSCB(&K+20) OR '&C(1)' NE 'IOB' AND &DSCB(&K+10))
&DSCB(&K+20) SETB (&B(12) OR &DSCB(&K+20))
&B(1)    SETB  (&B(1) AND &DSCB(&K+20) EQ &DSCB(&K+19))
&B(11)   SETB  ('&C(1)' EQ 'RSN')
&DSCB(&K+25)   SETB (&B(11) OR '&C(1)' EQ 'INQ' OR '&C(1)' EQ 'LPB'    X
               OR &B(12) OR '&C(1)' NE 'IOB' AND &DSCB(&K+10))    FSJW
&DSCB(&K+65)  SETB  (&B(14) AND '&SYSLIST(&M,2)' EQ 'X2')         FSJW
&DSCB(&K+70)  SETB  (&B(14) AND '&SYSLIST(&M,2)' EQ 'X1')         FSJW
&B(1)    SETB  (&B(1) AND &DSCB(&K+25) EQ &DSCB(&K+24))
&B(10)   SETB  ('&C(1)' EQ 'FID')
         AIF   (&B(14)).SR8
         AIF   (NOT (&B(10) OR &B(11) OR &B(12) OR &B(13))).SR5
         AIF   (NOT &B(12)).SR3
         AIF   (K'&SYSLIST(&M,1) LT 6).SR5
         AIF   ('&SYSLIST(&M,1)'(4,1) NE '(').SR5
&I       SETA  5
&C(2)    SETC  '&SYSLIST(&M,1)'(&I,1)
&C(1)    SETC  '0000000'
.SR2     ANOP
&PIB(1)  SETB  ('&C(2)' LT '0' OR '&C(2)' GT '9')
         AIF   (NOT &PIB(1)).I0136
&N       SETA  1
         IDFMSG2 214
.I0136   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).SR6
&C(1)    SETC  '&C(1)'(2,6).'&C(2)'
&I       SETA  &I+1
&C(2)    SETC  '&SYSLIST(&M,1)'(&I,1)
         AIF   ('&C(2)' NE ')' AND '&C(1)'(1,1) EQ '0').SR2
&DSCA(&K) SETA &C(1)
&A(5)    SETA  1
&A(6)    SETA  21
&PIB(1)  SETB  (&DSCA(&K) LT &A(5) OR &DSCA(&K) GT &A(6))
         AIF   (NOT &PIB(1)).I0137
         IDFMSG2 254
.I0137   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).SR6
         AIF   (&I EQ K'&SYSLIST(&M,1)).SR3
&I       SETA  &I+1
         AIF   (&I EQ K'&SYSLIST(&M,1) AND '&SYSLIST(&M,1)'(&I,1) EQ   X
               ')').SR3
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0138
&N       SETA  1
         IDFMSG1 105
.I0138   ANOP ,
.SR3     ANOP
         AIF   (&B(13)).SR17
&N       SETA  1
         AGO   .SR16
.SR4     ANOP
&C(1)    SETC  '&SYSLIST(&M,2)'
&C(2)    SETC  '&SYSLIST(&M,3)'
&C(3)    SETC  '&SYSLIST(&M,4)'
&B(2)    SETB ('&C(1)' EQ 'NUMPAD' OR '&C(2)' EQ 'NUMPAD' OR           X
               '&C(3)' EQ 'NUMPAD')
&B(3)    SETB ('&C(1)' NE 'AUTOEOF' AND '&C(2)' NE 'AUTOEOF' AND       X
               '&C(3)' NE 'AUTOEOF')
&B(4)    SETB  ('&C(1)' NE 'OPTIONAL' AND '&C(2)' NE 'OPTIONAL' AND    X
               '&C(3)' NE 'OPTIONAL')
&PIB(1)  SETB  (K'&SYSLIST(&M,2)+K'&SYSLIST(&M,3)+K'&SYSLIST(&M,4)     X
               NE 15+6*&B(2)-7*&B(3)-8*&B(4))
         AIF   (NOT &PIB(1)).I0139
         IDFMSG3 519
.I0139   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
&DSCB(&K+15)   SETB (&B(2) AND NOT &PIB(1))
&DSCB(&K+20)   SETB (&B(3) OR &PIB(1))
&DSCB(&K+25)   SETB (&B(4) OR &PIB(1))
&B(10)   SETB  0
&B(11)   SETB  0
&B(12)   SETB  0
&B(13)   SETB  0
&B(14)   SETB  0
&N       SETA  4
         AGO   .SR16
.SR5     ANOP
&PIB(1)  SETB  1
         AIF   (NOT &PIB(1)).I0140
         IDFMSG2 200
.I0140   ANOP ,
.SR6     ANOP
&PIB(15) SETB  1
&DSCB(&K)      SETB (&DSCB(&K-1))
&DSCA(&K)      SETA &DSCA(&K-1)
&DSCB(&K+5)    SETB (&DSCB(&K+4))
&DSCB(&K+10)   SETB (&DSCB(&K+9))
&DSCB(&K+15)   SETB (&DSCB(&K+14))
&DSCB(&K+20)   SETB (&DSCB(&K+19))
&DSCB(&K+25)   SETB (&DSCB(&K+24))
&B(10)   SETB  (&DSCB(&K) AND NOT (&DSCB(&K+5) OR &DSCB(&K+10) OR      X
               &DSCB(&K+15) OR &DSCB(&K+20) OR &DSCB(&K+25)))
&B(11)   SETB  (&DSCB(&K) AND NOT (&DSCB(&K+5) OR &DSCB(&K+10) OR      X
               &DSCB(&K+15) OR &DSCB(&K+20)) AND &DSCB(&K+25))
&B(12)   SETB  (&DSCB(&K) AND NOT (&DSCB(&K+5) OR &DSCB(&K+10) OR      X
               &DSCB(&K+15)) AND &DSCB(&K+20) AND &DSCB(&K+25))
&B(13)   SETB  0
&B(14)   SETB  (&DSCB(&K) AND (&DSCB(&K+10) OR &DSCB(&K+15)))
         AIF   (NOT &B(14)).SR17
&I       SETA  24*&DSCB(&K+10)+12*&DSCB(&K+15)+6*&DSCB(&K+20)+3*&DSCB(&X
               K+25)-9
&I       SETA  &I+3*&DSCB(&K)*&DSCB(&K+5)*&DSCB(&K+10)*&DSCB(&K+15)*&DSX
               CB(&K+20)*&DSCB(&K+25)
&B(2)    SETB  (&DSCA(&K-1) GT 0)
&B(3)    SETB  (&B(9) AND NOT &B(2))
         AGO   .SR10
.SR8     ANOP
&I       SETA  24*&DSCB(&K+10)+12*&DSCB(&K+15)+6*&DSCB(&K+20)+3*&DSCB(&X
               K+25)-9
&I       SETA  &I+3*&DSCB(&K)*&DSCB(&K+5)*&DSCB(&K+10)*&DSCB(&K+15)*&DSX
               CB(&K+20)*&DSCB(&K+25)
&B(2)    SETB  (&B(1) AND &DSCA(&K) GT 0)
&B(3)    SETB  (&B(9) AND NOT &B(2))
         AIF   (T'&SYSLIST(&M,2) NE 'O').SR9
         AIF  (&DSCB(&K+65) OR &DSCB(&K+70)).SR8A    X1/X2 CODED  FSJW
&DSCA(&K)      SETA 1+&B(2)*(&DSCA(&K-1)-1)+&B(3)*(&BUFA(&I-2)-1)
.SR8A    ANOP  ,                                                  FSJW
         AIF   (&B(9)).SR11
&N       SETA  2
         AGO   .SR16
.SR9     ANOP
&B(16)   SETB  (&DSCB(&K) AND &DSCB(&K+5) AND &DSCB(&K+10))       FSJW
&B(16)   SETB  (&B(16) OR (&DSCB(&K) AND NOT &DSCB(&K+5)               C
               AND &DSCB(&K+10) AND &DSCB(&K+15)))                FSJW
         AIF   ((&DSCB(&K+65) OR &DSCB(&K+70)) AND NOT &B(16)).SR9A
&PIB(1)  SETB  (T'&SYSLIST(&M,2) NE 'N' OR '&SYSLIST(&M,2)'(1,1) EQ    X
               'C')
         AIF   (NOT &PIB(1)).I0141
         IDFMSG3 520
         AIF   (NOT &B(16)).I0141      IF IDR/CCR ERROR CODING    FSJW
&DSCB(&K+65)  SETB  0                  X1/X2, RESET X1/X2 BITS    FSJW
&DSCB(&K+70)  SETB  0                                             FSJW
.I0141   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).SR10
&DSCA(&K)      SETA &SYSLIST(&M,2)
&A(5)    SETA  1
&A(6)    SETA  &BUFA(&I)
&PIB(1)  SETB  (&DSCA(&K) LT &A(5) OR &DSCA(&K) GT &A(6))
         AIF   (NOT &PIB(1)).I0142
         IDFMSG3 521
.I0142   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).SR10
.SR9A    ANOP  ,                                                  FSJW
         AIF   (&B(9)).SR11
&N       SETA  2
         AGO   .SR16
.SR10    ANOP
&PIB(15) SETB  1
&DSCA(&K)      SETA 1+&B(2)*(&DSCA(&K-1)-1)+&B(3)*(&BUFA(&I-2)-1)
         AIF   (NOT &B(9)).SRX
&DSCA(&K-1)    SETA &B(3)*&DSCA(&K-1)  SWITCH OFF LINE DEFAULT
         AGO   .SR15
.SR11    ANOP
         AIF   (&DSCB(70) OR &DSCB(75)).SR11A                     FSJW
&DSCA(&K-1)    SETA &B(3)*&DSCA(&K-1)  SWITCH OFF LINE DEFAULT
.SR11A   ANOP  ,                                                  FSJW
&N       SETA  3                       SET MAX VALID OPNDS
         AIF   (T'&SYSLIST(&M,3) NE 'O').SR12
&CTA(6)  SETA  0                       ZERO IF OMITTED
         AGO   .SR16
.SR12    AIF   (T'&SYSLIST(&M,3) NE 'N' OR '&SYSLIST(&M,3)'(1,1)       X
               EQ 'C').SR13
         AIF   (NOT(&DSCB(70) OR &DSCB(75))).SR12A                FSJW
&PIB(1)  SETB  1                                                  FSJW
         IDFMSG3 522                  SOURCE END POSITION INVALID FSJW
.**************          LNG(D) REQUIRED WHEN INDEX COUNTER USED  FSJW
         AGO   .SR15                                              FSJW
.SR12A   ANOP  ,                                                  FSJW
&A(5)    SETA  &DSCA(&K)
&B(1)    SETB  (&BUFA(&I) GT &A(5)+126)
&A(6)    SETA  &BUFA(&I)+&B(1)*(&A(5)+126-&BUFA(&I))
&PIB(1)  SETB  (&SYSLIST(&M,3) LT &A(5) OR &SYSLIST(&M,3) GT &A(6))
         AIF   (NOT &PIB(1)).I0143
         IDFMSG3 521
.I0143   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).SR15
&CTA(6)  SETA  &SYSLIST(&M,3)-&DSCA(&K)+1
         AGO   .SR16
.SR13    ANOP
&PIB(1)  SETB  (K'&SYSLIST(&M,3) LT 6)
         AIF   (NOT &PIB(1)).I0144
         IDFMSG3 522
.I0144   ANOP ,
         AIF   (&PIB(1)).SR15
&PIB(1)  SETB  ('&SYSLIST(&M,3)'(1,4) NE 'LNG(')
         AIF   (NOT &PIB(1)).I0145
         IDFMSG3 522
.I0145   ANOP ,
         AIF   (&PIB(1)).SR15
&J       SETA  5
&C(1)    SETC  '0000000'
&C(2)    SETC  '&SYSLIST(&M,3)'(&J,1)
.SR14    ANOP
&PIB(1)  SETB  ('&C(2)' LT '0' OR '&C(2)' GT '9')
         AIF   (NOT &PIB(1)).I0146
         IDFMSG3 522
.I0146   ANOP ,
         AIF   (&PIB(1)).SR15
&C(1)    SETC  '&C(1)'(2,6).'&C(2)'
&J       SETA  &J+1
&C(2)    SETC  '&SYSLIST(&M,3)'(&J,1)
         AIF   ('&C(2)' NE ')' AND '&C(1)'(1,1) EQ '0').SR14
&A(5)    SETA  1
&B(1)    SETB  (&BUFA(&I) LE &DSCA(&K)+126 AND NOT (&DSCB(70)          C
               OR &DSCB(75)))                                     FSJW
&A(6) SETA 127+&B(1)*(&BUFA(&I)-&DSCA(&K)-126)
&CTA(6)  SETA  &C(1)
&PIB(1) SETB (&CTA(6) LT &A(5) OR &CTA(6) GT &A(6))
         AIF   (NOT &PIB(1)).I0147
         IDFMSG3 523
.I0147   ANOP ,
         AIF   (NOT &PIB(1)).SR16
.SR15    ANOP
&PIB(15) SETB  1
&CTA(6)  SETA  0
         AGO   .SR17
.SR15A   ANOP  ,                                                  FSJW
&N       SETA  1             SET N FOR EXCESS CHARS CHECK         FSJW
&B(19)   SETB  ('&C(1)' EQ 'X2')                                  FSJW
&A(1)    SETA  4*&B(19)                                           FSJW
&A(2)    SETA  1+&B(19)                                           FSJW
         AIF   (&B(9)).SR15B                                      FSJW
&PIB(15) SETB  1                                                  FSJW
&PIB(1)  SETB  1                                                  FSJW
         IDFMSG2 200                                              FSJW
         AGO   .CTX                                               FSJW
.SR15B   ANOP  ,                                                  FSJW
&PIB(1)  SETB  1                                                  FSJW
         IDFMSG1 126                       SOURCE IS --           FSJW
.SR16    ANOP
&PIB(1)  SETB  (N'&SYSLIST(&M) GT &N)
         AIF   (NOT &PIB(1)).I0148
         IDFMSG1 104
.I0148   ANOP ,
         AIF   (&B(20)).SRX       IF X1/X2 SKIP THIS ROUTINE      FSJW
.**********************************************************************
.*                                                                    *
.*                           DATA SOURCE GROUP GENERATOR              *
.*                                                                    *
.**********************************************************************
.SR17    ANOP  ,                                                  FSJW
&ROIB(35) SETB  (&PIB(10))             CONDITIONAL CODE FOR       FSJW
.***                                   NULL INDICATOR     R       FSJW
&PIB(1)  SETB  (&DSCB(&K) OR &DSCB(&K+5) OR &DSCB(&K+10) OR            C
               &DSCB(&K+15) OR &DSCB(&K+20) OR &DSCB(&K+25))      FSJW
.***                                   NULL INDICATOR             FSJW
         AIF   (NOT &B(9)).CTX         NOT FIELD LEVEL            FSJW
&CTA(7)  SETA  0                        INITIALIZE MIN SOURCE COUNT
&FB(8)   SETB  (NOT &DSCB(5) AND &DSCB(20))
&FB(9)   SETB  (NOT &DSCB(5) AND &DSCB(25))
&FB(10)  SETB  (NOT &DSCB(5) AND &DSCB(30))
         AIF   (NOT &DSCB(5)).SRX
&FB(11)  SETB  0
&FB(12)  SETB  0
&A(1)    SETA  80+8*&DSCB(15)+4*&DSCB(20)+2*&DSCB(25)+&DSCB(30)
         AIF   (&A(1) NE 86 OR NOT &DSCB(10)).SR17A  IR00866
         IDFASM (,457F)            IR00866 SET DATA SOURCE FOR RPB
         AGO   .SR17B              IR00866
.SR17A   ANOP  *                   IR00866
         IDFASM A
.SR17B   ANOP  *                   IR00866
         AIF   (&B(12)).SR18
         AIF   (&B(14)).SR19
         AIF   (&B(13)).SRX
&CTA(6)  SETA  3
&M       SETA  16
&PIB(1)  SETB  (T'&SYSLIST(&M) NE 'O')
         AIF   (NOT &PIB(1)).I0149
         IDFMSG3 524
.I0149   ANOP ,
         AGO   .CTX
.SR18    ANOP
&A(1)    SETA  &DSCA(5)-1
         IDFASM A
&J       SETA  &DSCA(5)
&PIB(1)  SETB  (NOT &CTRB(&J+1))
         AIF   (NOT &PIB(1)).I0150
         IDFMSG 022
.I0150   ANOP ,
&CTRB(&J+22)   SETB (NOT &CTRB(&J+1) OR &CTRB(&J+22))
&CTRB(&J+43)   SETB (NOT &CTRB(&J+1) OR &CTRB(&J+43))
&CTRB(&J+1)    SETB 1
&CTRB(&J+64)   SETB 0
&CTRB(1) SETB  1
&CTA(6)  SETA  &PIA(9)             SET COUNTER SIZE        CTRSIZE
&M       SETA  16
&PIB(1)  SETB  (N'&SYSLIST(&M) NE 0)
         AIF   (NOT &PIB(1)).I0151
         IDFMSG3 524
.I0151   ANOP ,
         AGO   .CTX
.SR19    ANOP
         AIF   (&DSCB(70) OR &DSCB(75)).SR19A   TEST X1/X2 CODED  FSJW
&BUFA(&I-2) SETA &DSCA(5)               UPDATE BUFFER NEXT-GET LOC
&A(2)    SETA  &DSCA(5)+3+192*(&A(1)/95*95/&A(1))  INTERNAL BUF START
&A(1)    SETA  &A(2)/128
&A(2)    SETA  &A(2)-128*&A(1)
         IDFASM A
&A(1)    SETA  &A(2)
         IDFASM A
         AGO   .SR19B                                             FSJW
.SR19A   ANOP  ,                       ROUTINE FOR GENERATING     FSJW
&A(1)    SETA  126+&DSCB(75)           BYTES FOR X1/X2 AS         FSJW
         IDFASM A                      STARTING POSITION          FSJW
         IDFASM (,4070)                FOR BUFFER SOURCE          FSJW
.SR19B   ANOP  ,                                                  FSJW
&J       SETA  &I/3
&PIB(1)  SETB  (NOT &BUFB(&J+1))
         AIF   (NOT &PIB(1)).I0152
         IDFMSG1 132
.I0152   ANOP ,
&BUFB(&J+14)   SETB (NOT &BUFB(&J+1) OR &BUFB(&J+14))
&BUFB(&J+27)   SETB (NOT &BUFB(&J+1) OR &BUFB(&J+27))
&BUFB(&J+1)    SETB 1
&BUFB(&J+40)   SETB 0
&BUFB(1) SETB 1
&C(1)    SETC  '&SYSLIST(&M,2)'                                   FSJW
         AIF   ('&C(1)' NE 'X1' AND '&C(1)' NE 'X2').SR20X        FSJW
&J       SETA  1                       INDEX COUNTER X1           FSJW
         AIF   ('&C(1)' EQ 'X1').SR20A                            FSJW
&J       SETA  2                       INDEX COUNTER X2           FSJW
.SR20A   ANOP  ,                                                  FSJW
&NDXB(&J+5)    SETB  1       LATEST OPERATION IS RETREIVAL        FSJW
&NDXB(&J+1)    SETB  1       INDICATE THIS INDEX COUNTER USED     FSJW
         AIF   (&NDXB(1)).SR20B         CHECK FOR USE OF X1/X2    FSJW
         MNOTE 0,'IDF465 INDEX COUNTER &C(1) MAY HAVE BEEN USED'  FSJW
         MNOTE 0,'       WITHOUT A PRIOR VALUE ASSIGNED'
&NDXB(1)  SETB  1            INDICATE AN INDEX COUNTER IN USE     FSJW
         AGO   .SR20X                                             FSJW
.SR20B   ANOP  ,                                                  FSJW
         AIF   (&NDXB(&J+3)).SR20X                                FSJW
         MNOTE 0,'IDF465 INDEX COUNTER &C(1) MAY HAVE BEEN USED'  FSJW
         MNOTE 0,'       WITHOUT A PRIOR VALUE ASSIGNED'
.SR20X   ANOP  ,                                                  FSJW
&M       SETA  16
&PIB(1)  SETB  (&CTA(6) GT 0 AND N'&SYSLIST(&M) NE 0)
         AIF   (NOT &PIB(1)).I0153
         IDFMSG3 524
.I0153   ANOP ,
         AIF   (&CTA(6) GT 0).CT8       SKIP COUNT OPND IF NOT NEEDED
.SRX     ANOP
.**********************************************************************
.*                                                                    *
.*                           COUNT OPERAND                            *
.*                                                                    *
.**********************************************************************
&M       SETA  16
&PIB(1)  SETB  (&PIB(2) AND N'&SYSLIST(&M) NE 0)
         AIF   (NOT &PIB(1)).I0154
         IDFMSG1 100
.I0154   ANOP ,
         AIF   (&PIB(2)).CTX
&PIB(1)  SETB  (&PIB(12) AND N'&SYSLIST(&M) NE 0)
         AIF   (NOT &PIB(1)).I0155
         IDFMSG1 101
.I0155   ANOP ,
         AIF   (&PIB(12)).CTX
&B(8)    SETB  (&B(14) AND &DSCB(15) AND &DSCB(20) AND &DSCB(25) AND   X
               &DSCB(30) AND &DSCA(5) EQ 1)
&B(7)    SETB  (NOT &B(8) AND &DFA(19)-&DFA(11) LE 126)
&CTA(6)  SETA  &B(8)*(18+21*&DSCB(10))+&B(7)*(&DFA(19)-&DFA(11)+1)
&CTA(6)  SETA  &CTA(6)+&B(20)*(3-&CTA(6))                         FSJW
&FB(11)  SETB  0
&FB(12)  SETB  0
         AIF   (N'&SYSLIST(&M) EQ 0).CTX
&PIB(1)  SETB  (&B(20))                                           FSJW
         AIF   (NOT &PIB(1)).CT0                                  FSJW
         IDFMSG1 101         OP IGNORED WHEN SOURCE=X1/X2         FSJW
         AGO   .CTX                                               FSJW
.CT0     ANOP  ,                                                  FSJW
&C(1)    SETC  '&SYSLIST(&M,1)'
&C(2)    SETC  '&SYSLIST(&M,2)'
&C(3)    SETC  '&SYSLIST(&M,3)'
&B(1)    SETB  ('&C(1)' EQ 'MAX')
&B(2)    SETB  ('&C(1)' EQ 'MIN')
&B(3)    SETB  (&B(1) AND '&C(3)' EQ 'MIN' OR &B(2) AND '&C(3)' EQ     X
               'MAX')
&B(4)    SETB  0
         AIF   (&B(1) OR &B(2) OR '&C(2)' EQ '').CT1
&N       SETA  0
&PIB(1)  SETB  ('&C(1)' EQ '')
         AIF   (NOT &PIB(1)).I0156
         IDFMSG2 200
.I0156   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CTX
&B(1)    SETB  ('&C(2)' EQ 'MIN')
&B(2)    SETB  ('&C(2)' EQ 'MAX')
&B(3)    SETB  1
&B(4)    SETB  1
.CT1     ANOP
&PIB(1) SETB   ((&B(13) OR &B(14)) AND (&B(1) OR &B(3)))
         AIF   (NOT &PIB(1)).I0157
         IDFMSG3 549
.I0157   ANOP ,
&PIB(1)  SETB  ((&B(13) OR &B(14)) AND (&B(2) OR &B(3)))
         AIF   (NOT &PIB(1)).I0158
         IDFMSG3 550
.I0158   ANOP ,
&B(5)    SETB  0
&B(6)    SETB  0
         AIF   (&B(2) AND NOT &B(3)).CT2
&N       SETA  1+&B(2)*&B(3)*&B(4)
&B(8)    SETB  ('&SYSLIST(&M,&N)' EQ 'MIN')
&N       SETA  &N+2*&B(8)
&B(8)    SETB  ('&SYSLIST(&M,&N)' EQ 'MAX')
&N       SETA  &N+&B(8)
&PIB(1)  SETB  (T'&SYSLIST(&M,&N) NE 'N')
         AIF   (NOT &PIB(1)).I0159
         IDFMSG2 200
.I0159   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CT2
&PIB(1)  SETB  ('&SYSLIST(&M,&N)'(1,1) EQ 'C')
         AIF   (NOT &PIB(1)).I0160
         IDFMSG2 200
.I0160   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CT2
&B(5)    SETB  1
&A(1)    SETA  &SYSLIST(&M,&N)
.CT2     AIF   ((&B(13) OR &B(14)) AND (&B(1) OR &B(2)) OR             X
               NOT (&B(2) OR &B(3))).CT3
&N       SETA  2+&B(1)-&B(2)
&B(8)    SETB  ('&SYSLIST(&M,&N)' EQ 'MAX')
&N       SETA  &N+2*&B(8)
&B(8)    SETB  ('&SYSLIST(&M,&N)' EQ 'MIN')
&N       SETA  &N+&B(8)
&PIB(1)  SETB  (T'&SYSLIST(&M,&N) NE 'N')
         AIF   (NOT &PIB(1)).I0161
         IDFMSG2 200
.I0161   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CT3
&PIB(1)  SETB  ('&SYSLIST(&M,&N)'(1,1) EQ 'C')
         AIF   (NOT &PIB(1)).I0162
         IDFMSG2 200
.I0162   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CT3
&B(6)    SETB  1
&A(2)    SETA  &SYSLIST(&M,&N)
.CT3     AIF   (NOT &B(5) AND NOT &B(6)).CTX
         AIF   (&B(1) OR &B(2) OR NOT (&B(3) AND &B(5) AND &B(6)) OR   X
               &A(2) LE &A(1)).CT4
&A(1)    SETA  &A(1)+&A(2)
&A(2)    SETA  &A(1)-&A(2)
&A(1)    SETA  &A(1)-&A(2)
.CT4     ANOP
&A(5)    SETA  1
&A(6)    SETA  127
         AIF   (NOT &B(14)).CT5
&I       SETA  24*&DSCB(15)+12*&DSCB(20)+6*&DSCB(25)+3*&DSCB(30)-9
&I       SETA  &I+3*&DSCB(10)*&DSCB(15)*&DSCB(20)*&DSCB(25)*&DSCB(30)
&B(8)    SETB  (&BUFA(&I)-&DSCA(5)+1 LT 127 AND NOT (&DSCB(70)         C
               OR &DSCB(75)))                                     FSJW
&A(6)    SETA  127+&B(8)*(&BUFA(&I)-&DSCA(5)-126)
.CT5     AIF   (NOT &B(5)).CT6
&B(5)    SETB  0
&PIB(1)  SETB  (&A(1) LT &A(5) OR &A(1) GT &A(6))
         AIF   (NOT &PIB(1)).I0163
         IDFMSG3 526
.I0163   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CT6
&B(5)    SETB  1
&FB(11)  SETB  (NOT (&DSCB(5) OR &B(1) OR &B(3)))
&CTA(6)  SETA  &A(1)
&A(6)    SETA  &CTA(6)
.CT6     AIF   (NOT &B(6)).CT7
&B(6)    SETB  0
&PIB(1)  SETB  (&A(2) LT &A(5) OR &A(2) GT &A(6))
         AIF   (NOT &PIB(1)).I0164
         IDFMSG3 527
.I0164   ANOP ,
&PIB(15) SETB  (&PIB(15) OR &PIB(1))
         AIF   (&PIB(1)).CT7
&B(6)    SETB  1
&CTA(7)  SETA  &A(2)
&FB(11)  SETB  (&CTA(6) EQ &CTA(7) AND &CTA(6) GT 1)
&FB(12)  SETB  (NOT &FB(11) AND &CTA(7) GT 1)
.CT7     AIF   (&B(3) AND NOT (&B(5) AND &B(6)) OR (&B(1) OR NOT       *
               (&B(2) OR &B(3))) AND NOT &B(5)).CT8
         AIF   (&B(2) AND NOT &B(6)).CT8
&N       SETA  1+&B(1)+&B(2)+2*&B(3)-&B(4)
&PIB(1)  SETB  (N'&SYSLIST(&M) GT &N)
         AIF   (NOT &PIB(1)).I0165
         IDFMSG1 104
.I0165   ANOP ,
.CT8     AIF   (NOT &B(14) OR &CTA(6) EQ 0 OR &DSCB(70)                C
               OR &DSCB(75)).CTX                                  FSJW
&BUFA(&I-2) SETA &BUFA(&I-2)+&CTA(6)    INCREMENT NEXT-GET POSITION
.CTX     AIF   (NOT &PIB(48)).TX
         MNOTE *,'IDF100 IN TRACE MODE LEAVING IDFIN04'
.TX      ANOP
         MEND
