         MACRO
&NAME    OPTION  &T,&TYP=(H,Y,S,V,Q,A,E,F,D,Z,P,X,C,B),&CN=(2,2,2,4,4,4X
               ,4,4,8,0,0,0,0,0),                                      X
               &CH=(230,194,196,200,208,193,128,240,129,1,64,228,0,226)
         GBLA  &IEDQAR(64)
         GBLA  &OPTCT,&ACLOCX(21),&TSIZE
         GBLC  &OPTYPE(20)
         GBLC  &IEDQOP(255),&IEDQSV(8)
         GBLB  &IEDQZA(64)
         LCLA  &L,&M,&N,&P
         LCLA  &WKA
         LCLB  &NOGO
         LCLC  &WKC
         LCLC  &Q,&R
.* 028000-052000,085000-095000,142000,150100-157400,220000        20002
.* 380000-460000,520000,640000,668000-676000,692000,820000-840000 20002
.* 466000-490000,640000,668000-692000                            A27843
         AIF   (NOT &IEDQZA(7)).CQTAM                             20002
         AIF   (&IEDQZA(55) NE &IEDQZA(54)).OK                    20002
         MNOTE 12,'*** IHB311 OPTION MACRO OUT OF SEQUENCE'
         MEXIT
.OK      AIF   (&IEDQAR(3) NE 254).OK4                            20002
         MNOTE 12,'*** IHB316 MAXIMUM NUMBER OF OPTION FIELDS EXCEEDED'
         MEXIT
.OK4     AIF   ('&T' EQ '' OR '&NAME' EQ '').ERR1                 20002
&R       SETC  ''''                                               20002
.LP      AIF   (&N EQ N'&TYP).ERI                                 20002
&N       SETA  &N+1                                               20002
         AIF   ('&T'(1,1) NE '&TYP(&N)').LP                       20002
&L       SETA  &CN(&N)                                            20002
         AIF   (&N LT 2 OR &N GT 6).QT                            20002
&R       SETC  '('                                                20002
.QT      AIF   (K'&T LE 2).CKL                                    20002
         AIF   ('&T'(2,1) EQ 'L').GETL                            20002
.CKL     AIF   (&N GT 9).ERQ                                      20002
.GO      AIF   (&IEDQAR(3) NE 0).ZO                               20002
         ORG   IEDOPTPT .                                         20002
         DC    A(IEDQOPT) .             ADDRESS OF OPTION TABLE   20002
         ORG
IEDQOPT  CSECT
         DC    A(IEDQOPTN) .            END OF OPTION TABLE       20002
         DC    A(IEDQOPC) .             OPTION TABLE LOOK UP      20002
.ZO      SPACE 2 .                                                20002
&IEDQAR(3)  SETA  &IEDQAR(3)+1                                    20002
IEDQOPC  CSECT
&NAME    EQU   &IEDQAR(3) .                                       20002
&IEDQAR(15) SETA &IEDQAR(15)+&L                                   20002
&L       SETA  &L-1                                               20002
&M       SETA  &CH(&N)                                            20002
         AIF   (&IEDQAR(3) EQ 1).NOR                              20002
         ORG   *-1 .                                              20002
.NOR     DC    AL1(&L),AL1(&M),CL8'&NAME',X'FF'                   20002
&IEDQSV(4)  CSECT
&L       SETA  K'&T                                               20002
&Q       SETC  '&L'                                               20002
&IEDQOP(&IEDQAR(3))  SETC   '&Q'.'&T'.'&R'                        20002
         SPACE 2 .                                                20002
         MEXIT
.GETL    AIF   (K'&T LT 3).ERX                                    20002
         AIF   ('&T'(3,1) NE '.').CN                              20002
         AIF   (&N GE 2 AND &N LE 5).ERX                          20002
&M       SETA  1                                                  20002
.CN      AIF  (K'&T LT &M+3).ERX                                  20002
&L       SETA  1                                                  20002
.CL      AIF   ('&T'(&M+&L+2,1) GT '9' OR '&T'(&M+&L+2,1) LT '0').ERX
&L       SETA  &L+1                                               20002
         AIF   (&L LE K'&T-&M-2).CL                               20002
&Q       SETC  '&T'(&M+3,K'&T-&M-2)                               20002
&L       SETA  &Q                                                 20002
         AIF   (&M EQ 0).CK1                                      20002
&L       SETA  (&L+7)/8                                           20002
.CK1     AIF   (&L LE 0).ERX                                      20002
         AIF   (&L GT 256).ERX                                    20002
         AIF   (&N GE 12).GO                                      20002
         AIF   (&L GT 16).ERX                                     20002
         AIF   (&N GE 10).GO                                      20002
         AIF   (&L GT 8).ERX                                      20002
         AIF   (&N GE 7 OR &N EQ 1).GO                            20002
         AIF   (&L GT 4).ERX                                      20002
         AIF   (&N GE 5).GO                                       20002
         AIF   ((&N EQ 3 AND &L EQ 2) OR (&N EQ 4 AND (&L EQ 3 OR &L EQX
                4)) OR (&N EQ 2 AND &L LE 2)).GO                  20002
.ERX     MNOTE 12,'*** IHB308 INVALID LENGTH QUALIFIER SPECIFIED'
         MEXIT
.ERR1    MNOTE 12,'*** IHB002 REQUIRED OPERANDS OMITTED'
         MEXIT
.ERQ     MNOTE 12,'*** IHB309 LENGTH QUALIFIER REQUIRED'
         MEXIT
.ERI     MNOTE 12,'*** IHB310 INVALID CONSTANT TYPE SPECIFIED'
         MEXIT
.CQTAM   ANOP
         AIF   ('&NAME' NE '').OK1
         MNOTE 12,'***  IHB001  SUBFIELD OPERAND REQ''D-NOT SPECIFIED'
&NOGO    SETB  1
.OK1     AIF   ('&T' NE '').OK2
         MNOTE 12,'***  IHB001  TYPE-LENGTH OPERAND REQ''D-NOT SPECIFIEX
               D'
&NOGO    SETB  1
.OK2     AIF   ('&SYSECT' EQ 'IECKTERM').OK3
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW TERMTBL MACRX
               O'
&NOGO    SETB  1
.OK3     AIF   (K'&T EQ 1).HAF
         AIF   (K'&T LT 3 OR K'&T GT 5).ERR
         AIF   ('&T'(1,2) EQ 'XL' OR '&T'(1,2) EQ 'CL' OR '&T'(1,2) EQ X
               'BL' OR '&T'(1,2) EQ 'AL' OR '&T'(1,2) EQ 'HL').VAL
         AIF   ('&T'(1,2) EQ 'EL' OR '&T'(1,2) EQ 'DL' OR '&T'(1,2) EQ X
               'PL' OR '&T'(1,2) EQ 'ZL' OR '&T'(1,2) EQ 'VL').VAL
         AIF   ('&T'(1,2) EQ 'SL' OR '&T'(1,2) EQ 'YL' OR '&T'(1,2) EQ X
               'FL').VAL
         AGO   .ERR
.VAL     AIF   (&NOGO).END
&WKC     SETC  '&T'(3,K'&T-2)
&WKA     SETA  &WKC
&ACLOCX(&OPTCT+2) SETA &ACLOCX(&OPTCT+1)+&WKA
         AGO   .GEN
.HAF     ANOP
&WKA     SETA  2
         AIF   ('&T' EQ 'H' OR '&T' EQ 'S' OR '&T' EQ 'Y').CK
&WKA     SETA  4
         AIF   ('&T' EQ 'F' OR '&T' EQ 'E' OR '&T' EQ 'A' OR '&T' EQ 'VX
               ').CK
&WKA     SETA  8
         AIF   ('&T' EQ 'D').CK
.ERR     MNOTE 12,'***  IHB002  INVALID TYPE-LENGTH OPERAND SPECIFIED'
         MEXIT
.CK      AIF   (&NOGO).END
&ACLOCX(&OPTCT+2) SETA (&ACLOCX(&OPTCT+1)+(&WKA+&WKA-1))/&WKA*&WKA
.GEN     ANOP
&OPTCT   SETA  &OPTCT+1
&OPTYPE(&OPTCT) SETC '&T'
&NAME    DS    &T
.END     MEND
