         MACRO
&NAME    LPSTART &PAR,&TERM=,&INTRCPT=
         GBLB  &SW,&RCV,&SEN,&RHDR,&SHDR
         GBLB  &WRUHDR,&WRUEND,&BWTTA                              000C
         GBLC  &WRUFLG                                             000C
         GBLC  &SEND,&TEXT,&MGTP
         LCLA  &LA1
         LCLB  &LB1,&LB2,&LBT
         LCLB  &LB3,&LB4,&LB5,&LB6,&LB8
.*1171020000,754000-774000                                         6498
.*2719033000-036000,115000,450000,492000-498000                    000C
.*2719570000,703000-706000,747000,802000-814000                    000C
.*1200818000-828000,963000-966000                                 21843
         AIF   ('&TERM' NE '').TERM
         MNOTE 12,'NO TERMINAL SPECIFICATION, MACRO IGNORED.'
         MEXIT
.TERM    ANOP
&BWTTA   SETB  0                                                   000C
&LA1     SETA  N'&TERM
&LA1     SETA  1
.T3      AIF   ('&TERM(&LA1)' NE '1030').T5
         AIF   (&LB3).DUB
&LB3     SETB  (1)
         AGO   .T
.T5      AIF   ('&TERM(&LA1)' NE '1050').T6
         AIF   (&LB5).DUB
&LB5     SETB  (1)
         AGO   .T
.T6      AIF   ('&TERM(&LA1)' NE '1060').T8
         AIF   (&LB6).DUB
&LB6     SETB  (1)
         AGO   .T
.T8      AIF   ('&TERM(&LA1)' NE '2848').T4
         AIF   (&LB8).DUB
.SET2848 ANOP
&LB8     SETB  (1)
         AGO   .T
.T4      AIF   ('&TERM(&LA1)' NE '2740').TTY
         AIF   (&LB4).DUB
&LB4     SETB  (1)
         AGO   .T
.TTY     AIF   ('&TERM(&LA1)' NE '83B3' AND '&TERM(&LA1)' NE '115A' AND*
               '&TERM(&LA1)' NE 'TWX').TRY2260
.SETTTY  ANOP
&LBT     SETB  (1)
         AGO   .T
.TRY2260 AIF   ('&TERM(&LA1)' NE '2260').TRYTTY
         AIF   (&LB1).SET2848
         MNOTE 0,'2260 SPECIFICATION SHOULD BE 2848'
&LB1     SETB  (1)
         AGO   .SET2848
.TRYTTY  AIF   ('&TERM(&LA1)' NE 'TTY').TWTTA                      000C
         AIF   (&LB2).T
         MNOTE 0,'TTY SPECIFICATION SHOULD BE 83B3, 115A OR TWX'
&LB2     SETB  (1)
         AGO   .SETTTY
.TWTTA   AIF   ('&TERM(&LA1)' NE 'WTTA').TERMX                     000C
         AIF   (&BWTTA).DUB                                        000C
&BWTTA   SETB  (1)                                                 000C
         AGO   .T                                                  000C
.DUB     MNOTE 0,'WARNING--DUPLICATE &TERM(&LA1) TERMINAL SUBOPERANDS.'
         AGO   .T
.TERMX   MNOTE 4,'INVALID TERMINAL TYPE SPECIFICATION--&TERM(&LA1)'
.T       ANOP
&LA1     SETA  &LA1+1
         AIF   (&LA1 NE (N'&TERM+1)).T3
         AIF   (&LB3 OR &LB4 OR &LB5 OR &LB8 OR &LBT).TERMOK
         AIF   (&LB6 OR &BWTTA).TERMOK                             000C
         MNOTE 12,'NO VALID TERMINAL SPECIFIED, MACRO IGNORED.'
         MEXIT
.TERMOK  ANOP
         AIF   ('&NAME' NE '').GO
         MNOTE 12,'***  IHB001  LABEL OPERAND REQ''D NOT SPECIFIED'
         MEXIT
.GO      USING &NAME,7                 BASE REGISTER FOR LPS
.X       USING IECKTERM,8              TERM TABLE DSECT BASE
&SW      SETB  1
&SEN     SETB  0
&RCV     SETB  1
&RHDR    SETB  0
&SHDR    SETB  0
&WRUHDR  SETB  0                                                   000C
&WRUEND  SETB  0                                                   000C
&SEND    SETC  'IHB'.'&SYSNDX'
&TEXT    SETC  ''
&MGTP    SETC  ''
         DS    0F
&NAME    B     *+10+2*&BWTTA           BRANCH AROUND DC'S          000C
         AIF   ('&INTRCPT' EQ 'NO' OR '&INTRCPT' EQ '').GO1
         DC    AL1(INTERCPT-IECKTERM)    OFFSET TO INTERCEPT FIELD
         DC    VL3(IECKSCAN)
         AGO   .GO2
.GO1     DC    V(IECKSCAN)
.GO2     AIF   ('&PAR' NE '').NB
         DC    FL2'0'              BUFFER SPACE FOR HEADER EXP.
         AGO   .CONT
.NB      DC    FL2'&PAR'           BUFFER SPACE FOR HEADER EXP.
.CONT    AIF   (NOT &BWTTA).CONT2                                  000C
&WRUFLG  SETC  'IHBW'.'&SYSNDX'                                    000C
         DC    AL1(&WRUFLG)                                        000C
         DC    AL1(0)                  NOT USED                    000C
.CONT2   AIF  (&LB3 OR &LB4 OR &LB5 OR &LB6 OR &LB8).TA1          21843
         AGO   .NTEST                                             21843
.TA1     BAL   1,IHBX&SYSNDX
.TA      AIF   (&LB4 NE 1).TB
         DC    X'0113'                  FOR 2740
.TB      AIF (NOT &LB5 AND NOT &LB6 AND NOT &LB4).TC
         DC    X'0213'                  FOR 1050, 1060 OR 2740
.TC      AIF   (&LB3 NE 1).TD
         DC    X'0313'                  FOR 1030
.TD      AIF   (&LB8 NE 1).TE
         DC    X'0359'                  FOR 2848
.TE      DC    X'FF00'                  END OF LIST
IHBX&SYSNDX  L 15,=V(IECKONLT)
         BALR  14,15
.NTEST   TM    0(4),X'04'         TEST FOR SEND STATUS            21843
         BC    1,&SEND            BRANCH IF SEND STATUS           21843
         MEND
