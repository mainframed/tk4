         MACRO
&NAME    IHBINNRB &EP,&EPLOC,&DE,&DCB,&HIARCHY,&SF=I               LC0A
         GBLC  &IHBNO
         LCLB  &B(3)
         LCLA  &CTR,&A
         LCLC  &CHARS
&B(1)    SETB  ('&EP' NE '')
&B(2)    SETB  ('&EPLOC' NE '')
&B(3)    SETB  ('&DE' NE '')
.*0000000400,003600,008800,009600,010000,012800,013200,016600,     LC0A
.*    016800,017200                                                LC0A
         AIF   (&B(1)+&B(2)+&B(3) GT 1).E1
         AIF   ('&EP' EQ '').AGONE                                 LCOA
         AIF   ('&EP'(1,1) EQ '(').E1A                             LC0A
.AGONE   ANOP                                                      LCOA
         AIF   ('&SF' EQ 'I').IROUT
         AIF   ('&SF' EQ 'L').LROUT
         AIF   (N'&SF NE 2).E2
         AIF   ('&SF(1)' NE 'E').E2
         AIF   ('&SF'(4,1) EQ '(').ISAREG
         LA    15,&SF(2)                         LOAD SUP.PARAMLIST ADR
         AGO   .CONTA
.E1      IHBERMAC 88,&IHBNO            TOO MANY OPERANDS PRESENT   LC0A
         MEXIT
.E1A     IHBERMAC 42,&EP               R-FORM INVALID FOR EP NAME  LC0A
         MEXIT                                                     LC0A
.E2      IHBERMAC 41,&IHBNO,&SF             SF PARAMETER BAD
         MEXIT
.E3      IHBERMAC 72,&IHBNO                 REG. NOT. W/SF=L
         MEXIT
.E4      IHBERMAC 31,&IHBNO                 EP,EPLOC,DE ALL MISSING
         MEXIT
.ISAREG  AIF   ('&SF(2)' EQ '(15)').CONTA
&A       SETA  K'&SF-6
&CHARS   SETC  '&SF'(5,&A)
         LR    15,&CHARS                         LOAD SUP.PARAMLIST ADR
.CONTA   AIF   (NOT &B(1)).CONTB
         LA    0,*+8                             LOAD EP ADDR IN REG 0
         B     *+12                              BRANCH AROUND EP
         DC    CL8'&EP'                          EP SYMPOL
         ST    0,0(0,15)                         STORE IN SUP.PARAMLIST
         AGO   .DCBCHK
.CONTB   AIF   (&B(2)+&B(3) EQ 0).DCBCHK
         AIF   ('&EPLOC&DE'(1,1) EQ '(').ISAREGA
         LA    0,&EPLOC&DE                       PICK UP SUP.PARAMETER
         ST    0,0(0,15)                         STORE IN SUP.PARAMLIST
         AGO   .DECHK
.ISAREGA ST    &EPLOC(1)&DE(1),0(0,15)           STORE IN SUP.PARAMLIST
.DECHK   AIF   (NOT &B(3)).DCBCHK
         MVI   0(15),X'80'                       SET DE SWITCH BIT ON
.DCBCHK   AIF   ('&DCB' EQ '').HCHK
         AIF   ('&DCB'(1,1) EQ '(').ISAREGB
         LA    0,&DCB                            PICK UP DCB PARM  LC0A
         AGO   .STORDCB
.ISAREGB LR    0,&DCB(1)                         PICK UP DCB PARM  LC0A
.STORDCB STH   0,6(15)                           STORE THREE-BYTE  LC0A
         SRL   0,16                              DCB PARAMETER IN  LC0A
         STC   0,5(15)                           SUPV PARM LIST    LC0A
.HCHK    AIF   ('&HIARCHY' EQ '').END                              LC0A
         AGO   .HCHKB
.LROUT   DS    0F
         AIF   (NOT &B(1)).CKEPLOC
&NAME    DC    A(*+8)                            ADDR OF EP PARAM
         AGO .CHKDCB
.CKEPLOC AIF   (&B(2)+&B(3) EQ 0).PUTZERO
         AIF   ('&EPLOC&DE'(1,1) EQ '(').E3
&NAME    DC    B'&B(3).0000000'                  SET DE-EPLOC SW BIT
         DC    AL3(&EPLOC&DE)                    EPLOC OR DE PARAMETER
         AGO   .CHKDCB
.PUTZERO ANOP
&NAME    DC    A(0)                              SUPERVISOR PARAMETER
.CHKDCB  AIF   ('&DCB' EQ '').ZERODCB
         AIF   ('&DCB'(1,1) EQ '(').E3
         AIF   ('&HIARCHY' EQ '').DCNOHI                           LC0A
         AIF   ('&HIARCHY' EQ '1').H0                              LC0A
         DC    B'00000001'             HIERARCHY 0 REQUESTED       LC0A
         DC    AL3(&DCB)               DCB ADDRESS PARAMETER       LC0A
         AGO   .CHKEP                                              LC0A
.H0      DC    B'00000010'             HIERARCHY 1 REQUESTED       LC0A
         DC    AL3(&DCB)               DCB ADDRESS PARAMETER       LC0A
         AGO   .CHKEP                                              LC0A
.DCNOHI  DC    A(&DCB)                 DCB ADDRESS PARAMETER       LC0A
         AGO   .CHKEP
.ZERODCB AIF   ('&HIARCHY' EQ '').NODCHI                           LC0A
         AIF   ('&HIARCHY' EQ '1').H1                              LC0A
         DC    B'00000001'             HIERARCHY 0 REQUESTED       LC0A
         DC    AL3(0)                  DCB ADDRESS PARAMETER       LC0A
         AGO   .CHKEP                                              LC0A
.H1      DC    B'00000010'             HIERARCHY 1 REQUESTED       LC0A
         DC    AL3(0)                  DCB ADDRESS PARAMETER       LC0A
         AGO   .CHKEP                                              LC0A
.NODCHI  DC    A(0)                    DCB ADDRESS PARAMETER       LC0A
.CHKEP   AIF   (NOT &B(1)).END
         DC    CL8'&EP'                          EP PARAMETER
         AGO   .END
.IROUT   AIF   (&B(1)+&B(2)+&B(3) NE 1).E4
&CTR     SETA  12+8*&B(1)
         CNOP  0,4
         BAL   15,*+&CTR                         LOAD SUP.PARAMLIST ADR
         AIF   (NOT &B(1)).NOEP
         DC    A(*+8)                            ADDR OF EP PARAMETER
         AGO   .DCB
.NOEP    AIF   ('&EPLOC&DE'(1,1)  EQ '(').ZERODE
         DC    B'&B(3).0000000'                  SET DE-EPLOC SW BIT
         DC    AL3(&EPLOC&DE)                    EPLOC OR DE PARAMETER
         AGO   .DCB
.ZERODE  DC    A(0)                              EPLOC OR DE PARAMETER
.DCB     AIF   ('&DCB' EQ '').ZDCB
         AIF   ('&DCB'(1,1) EQ '(').NODCH0                         LC0A
         AIF   ('&HIARCHY' EQ '').DCNOHJ                         A29383
         AIF   ('&HIARCHY' EQ '1').H3                              LC0A
         DC    B'00000001'             HIERARCHY 0 REQUESTED       LC0A
         DC    AL3(&DCB)               DCB ADDRESS PARAMETER       LC0A
         AGO   .EPCHECK                                            LC0A
.H3      DC    B'00000010'             HIERARCHY 1 REQUESTED       LC0A
         DC    AL3(&DCB)               DCB ADDRESS PARAMETER       LC0A
         AGO   .EPCHECK                                            LC0A
.DCNOHJ  DC    A(&DCB)                 DCB ADDRESS PARAMETER       LC0A
         AGO   .EPCHECK
.ZDCB    AIF   ('&HIARCHY' EQ '').NODCH0                           LC0A
         AIF   ('&HIARCHY' EQ '1').H2                              LC0A
         DC    B'00000001'             HIERARCHY 0 REQUESTED       LC0A
         DC    AL3(0)                  DCB ADDRESS PARAMETER       LC0A
         AGO   .EPCHECK                                            LC0A
.H2      DC    B'00000010'             HIERARCHY 1 REQUESTED       LC0A
         DC    AL3(0)                  DCB ADDRESS PARAMETER       LC0A
         AGO   .EPCHECK                                            LC0A
.NODCH0  DC    A(0)                    DCB ADDRESS PARAMETER       LC0A
.EPCHECK AIF   (NOT &B(1)).ACTION
         DC    CL8'&EP'                          EP PARAMETER
.ACTION  AIF   (&B(1)).TESTDCB
         AIF   ('&EPLOC&DE'(1,1) NE '(').TESTDCB
         ST    &EPLOC(1)&DE(1),0(0,15)           STORE IN SUP.PARAMLIST
         AIF   (NOT &B(3)).TESTDCB
         MVI   0(15),X'80'                       SET DE SWITCH BIT ON
.TESTDCB AIF   ('&DCB' EQ '').END
         AIF   ('&DCB'(1,1) NE '(').END
         ST    &DCB(1),4(0,15)                   STORE IN SUP.PARAMLIST
.HICHK   AIF   ('&HIARCHY' EQ '').PREND                            LC0A
.HCHKB   AIF   ('&HIARCHY' EQ '0').HI0                             LC0A
         MVI   4(15),X'02'             HIERARCHY 1 REQUESTED       LC0A
         AGO   .END                                                LC0A
.HI0     MVI   4(15),X'01'             HIERARCHY 0 REQUESTED       LC0A
         AGO   .END                                                LC0A
.PREND   MVI   4(15),X'00'             CLEAR HIGH-ORDER BYTE       LC0A
.END     MEND
