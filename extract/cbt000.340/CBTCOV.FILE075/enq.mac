         MACRO
&NAME    ENQ   &PARAM,&SMC=,&RET=,&UCB=,&MF=I
         GBLB  &IHBRES                                             SDS1
         GBLB  &IHBDEQ
         GBLC  &LABLE                                              0242
         LCLA  &CTR8                                               0721
         LCLA  &CTR,&CTR1,&CTR2,&CTR3,&CTR4,&CTR5,&CTR6,&CTR7,&NUMBER
         LCLB  &B(8),&BB(5),&DENQ                                  SDS1
         LCLC  &C
.*2663 0007,0013,0024,0063,0066,0140-0142,0247
.*0460 0004,0010,0102-0108,0144,0246,0268,0276-0278,0318-0320,0332-0336
.*0340-0344,0348                                                   SDS1
&DENQ    SETB  (NOT(&IHBDEQ))
         AIF   ('&MF(1)' EQ 'E').ARND
         AIF   ('&PARAM' EQ '').E1
.ARND    ANOP
&NUMBER  SETA  N'&PARAM
         AIF   ('&NUMBER' GT '5' AND '&UCB' NE '').ENQ130          SDS1
.SMC     AIF   ('&SMC' EQ '' OR '&SMC' EQ 'NONE').RET
&B(2)    SETB  ('&SMC' EQ 'SYSTEM')
&B(3)    SETB  ('&SMC' EQ 'STEP')
         AIF   (&B(2) OR &B(3)).RET
         IHBERMAC 157,SMC,&SMC
.RET     AIF   ('&RET' EQ '' OR '&RET' EQ 'NONE').LOOP
         AIF   (&IHBDEQ).DEQ0
&B(5)    SETB  ('&RET' EQ 'TEST')
&B(6)    SETB  ('&RET' EQ 'TEST' OR '&RET' EQ 'USE' OR '&RET' EQ 'CHNG'X
               )                                                 20021
&B(7)    SETB  ('&RET' EQ 'TEST' OR '&RET' EQ 'USE' OR '&RET' EQ 'HAVE'X
               )
         AGO   .ENQ0
.DEQ0    ANOP
&B(7)    SETB  ('&RET' EQ 'HAVE')
.ENQ0    AIF   (&B(7) OR '&RET' EQ 'CHNG').LOOP                   20021
         IHBERMAC 157,RET,&RET
.LOOP    ANOP
&LABLE   SETC  'IHB'.'&SYSNDX'                                     0242
         AIF   ('&MF(1)' EQ 'E').EROUT
         AIF   ('&MF' EQ 'L').DS
         CNOP  0,4
&NAME    BAL   1,&LABLE                          BRANCH AROUND LIST0242
         AGO   .NEXT
.DS      ANOP
&NAME    DS    0F
.NEXT    ANOP
&CTR     SETA  1
.LOOP1   AIF   (&CTR GT &NUMBER).END                               5345
&B(8)    SETB  0
&B(1)    SETB  0
&B(4)    SETB  0                                                   SDS1
&CTR1    SETA  &CTR+1
&CTR2    SETA  &CTR1+&DENQ
&CTR3    SETA  &CTR2+1
&CTR7    SETA  &CTR3+1
         AIF   (&IHBDEQ).NOB0
         AIF   ('&PARAM(&CTR2)' EQ '' OR '&PARAM(&CTR2)' EQ 'E').NOB0
&B(8)    SETB  ('&PARAM(&CTR2)' EQ 'S')
         AIF   (&B(8)).NOB0
         IHBERMAC 157,CODE,&PARAM(&CTR2)
.NOB0    AIF   ('&UCB' NE '').ENQ020                               SDS1
         AIF   ('&PARAM(&CTR7)' EQ '' OR '&PARAM(&CTR7)' EQ 'STEP').ENQX
               030                                                 SDS1
&B(1)    SETB  ('&PARAM(&CTR7)' EQ 'SYSTEMS')                      SDS1
&B(4)    SETB  ('&PARAM(&CTR7)' EQ 'SYSTEMS')                      SDS1
         AIF   (&B(1)).ENQ030                                      SDS1
&B(1)    SETB  ('&PARAM(&CTR7)' EQ 'SYSTEM')                       SDS1
         AIF   (&B(1)).ENQ030                                      SDS1
         IHBERMAC 157,SYSTEMS,&PARAM(&CTR7)                        SDS1
         AGO   .ENQ030                                             SDS1
.ENQ020  ANOP                                                      SDS1
&B(4)    SETB  1                                                   SDS1
.ENQ030  ANOP                                                      SDS1
.NOB     AIF   (&CTR+4+&DENQ GT &NUMBER).LAST
         DC    AL1(0)
         AGO   .LENGTH
.LAST    DC    AL1(255)                      LAST ENTRY INDICATION
.LENGTH  AIF   ('&PARAM(&CTR3)' EQ '' AND '&MF' EQ 'L').NAMEL
         AIF   ('&PARAM(&CTR1)' EQ '').SKIP                       17892
         AIF   ('&PARAM(&CTR3)' EQ '' AND '&PARAM(&CTR1)'(1,1) EQ '(').X
               E2
.SKIP    AIF    ('&PARAM(&CTR3)' EQ '').NAMEL                     17892
         AIF   ('&PARAM(&CTR3)'(1,1) EQ '(').REGL
         DC    AL1(&PARAM(&CTR3))                 LENGTH
         AGO   .CODE
.REGL    AIF   ('&MF' EQ 'L').E3
.ZEROL   DC    AL1(0)                             LENGTH
         AGO   .CODE
.NAMEL   AIF   ('&PARAM(&CTR1)' EQ '').ZEROL
&CTR8    SETA  L'&PARAM(&CTR1)                                     0721
         DC    AL1(&CTR8)                   LENGTH                 0721
.CODE    DC    BL1'&B(8)&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)'       SDS1
         DC    AL1(0)                             RETURN CODE FIELD
         AIF   ('&PARAM(&CTR)' EQ '' AND '&MF' EQ 'L').ZEROQ
         AIF   ('&PARAM(&CTR)' EQ '').E2
         AIF   ('&PARAM(&CTR)'(1,1) EQ '(').REGQ
         DC    A(&PARAM(&CTR))                    QNAME ADDRESS
         AGO   .RNAME
.REGQ    AIF   ('&MF' EQ 'L').E3
.ZEROQ   DC    A(0)                               QNAME ADDRESS
.RNAME   AIF   ('&PARAM(&CTR1)' EQ '' AND '&MF' EQ 'L').ZEROR
         AIF   ('&PARAM(&CTR1)' EQ '').E2
         AIF   ('&PARAM(&CTR1)'(1,1) EQ '(').REGR
         DC    A(&PARAM(&CTR1))                   RNAME ADDRESS
         AGO   .CONT
.REGR    AIF   ('&MF' EQ 'L').E3
.ZEROR   DC    A(0)                               RNAME ADDRESS
.CONT    ANOP
&CTR     SETA  &CTR+4+&DENQ
         AGO   .LOOP1
.END     ANOP
         AIF   ('&UCB' EQ '').ENQ050                               SDS1
         AIF   ('&UCB'(1,1) EQ '(').ENQ040                         SDS1
         DC    A(&UCB)            UCB PARAMETER                    SDS1
         AGO   .ENQ050                                             SDS1
.ENQ040  AIF   ('&MF' EQ 'L').E3                                   SDS1
         DC    A(0)               UCB PARAMETER                    SDS1
.ENQ050  ANOP                                                      SDS1
         AIF   ('&MF' NE 'L').NEXT1
&IHBDEQ  SETB  0
&IHBRES  SETB  0                                                   SDS1
         MEXIT
.NEXT1   ANOP
&LABLE   DS    0H                                                  0242
&CTR     SETA  1
&CTR2    SETA  0
.LOOP2   AIF   (&CTR GT &NUMBER).END1
&CTR1    SETA  &CTR+1
&CTR3    SETA  &CTR+2+&DENQ
&CTR2    SETA  &CTR2+1
         AIF   ('&PARAM(&CTR3)' EQ '').NOREGL
         AIF   ('&PARAM(&CTR3)'(1,1) NE '(').NOREGL
&CTR4    SETA  K'&PARAM(&CTR3)-2
&C       SETC  '&PARAM(&CTR3)'(2,&CTR4)
         STC   &C,&CTR2.(0,1)                     STORE RNAME LENGTH
.NOREGL  ANOP
&CTR2    SETA  &CTR2+3
         AIF   ('&PARAM(&CTR)'(1,1) NE '(').NOREGQ
&CTR4    SETA  K'&PARAM(&CTR)-2
&C       SETC  '&PARAM(&CTR)'(2,&CTR4)
         ST    &C,&CTR2.(0,1)                     STORE QNAME ADDRESS
.NOREGQ  ANOP
&CTR2    SETA  &CTR2+4
         AIF   ('&PARAM(&CTR1)'(1,1) NE '(').NOREGR
&CTR4    SETA  K'&PARAM(&CTR1)-2
&C       SETC  '&PARAM(&CTR1)'(2,&CTR4)
         ST    &C,&CTR2.(0,1)                     STORE RNAME ADDRESS
.NOREGR  ANOP
&CTR2    SETA  &CTR2+4
&CTR     SETA  &CTR+4+&DENQ
         AGO   .LOOP2
.END1    AIF   ('&UCB' EQ '').ENQ060                               SDS1
         AIF   ('&UCB'(1,1) NE '(').ENQ060                         SDS1
&CTR4    SETA  K'&UCB-2                                            SDS1
&C       SETC  '&UCB'(2,&CTR4)                                     SDS1
         ST    &C,&CTR2.(0,1)     STORE UCB PARAMETER              SDS1
.ENQ060  AIF   (&IHBDEQ OR &IHBRES).MEXIT                          SDS1
         SVC   56
.MEXIT   MEXIT
.EROUT   ANOP
         AIF   (N'&MF NE 2).E4
&NAME    IHBINNRA &MF(2)
&CTR     SETA  1
&CTR6    SETA  0
&BB(1)   SETB  ('&SMC' EQ '')
&BB(2)   SETB  ('&RET' EQ '')
&BB(5)   SETB  ('&UCB' EQ '')                                      SDS1
         AIF   ('&PARAM' NE '').LLOOP
         AIF   (&BB(1) AND &BB(2) AND &BB(5)).ENQ060               SDS1
&BB(3)   SETB  1
&BB(4)   SETB  1
         AIF   (&BB(5)).ENQ065                                     SDS1
&BB(4)   SETB  0                                                   SDS1
&B(4)    SETB  1                                                   SDS1
.ENQ065  ANOP                                                      SDS1
&CTR6    SETA  2
         AGO   .ENQ080                                             SDS1
.LLOOP AIF  (&CTR GT &NUMBER).ENQ110                           BH 33990
&CTR1    SETA  &CTR+1
&CTR2    SETA  &CTR1+&DENQ
&CTR3    SETA  &CTR2+1
&CTR7    SETA  &CTR3+1
         AIF   (&IHBDEQ).SHR
&BB(3)   SETB  ('&PARAM(&CTR2)' EQ '')
.SHR     ANOP
&BB(4)   SETB  ('&PARAM(&CTR7)' EQ '')
&CTR6    SETA  &CTR6+1
         AIF   ('&PARAM(&CTR3)' EQ '').ECODES
         AIF   ('&PARAM(&CTR3)'(1,1) EQ '(').EREGL
         MVI   &CTR6.(1),&PARAM(&CTR3)        MOVE RNAME LENGTH TO LIST
         AGO   .ECODES
.EREGL   ANOP
&CTR4    SETA  K'&PARAM(&CTR3)-2
&C       SETC  '&PARAM(&CTR3)'(2,&CTR4)
         STC   &C,&CTR6.(0,1)
.ECODES  ANOP
&CTR6    SETA  &CTR6+1
         AIF   (&BB(1) AND &BB(2) AND &BB(5) AND (&BB(3) OR &IHBDEQ) ANX
               D &BB(4)).ENQ090                                    SDS1
         AIF   (&IHBDEQ).SHRD
&B(8)    SETB  ('&PARAM(&CTR2)' EQ 'S')
         AIF   (&B(8) OR &BB(3) OR '&PARAM(&CTR2)' EQ 'E').SHRD
         IHBERMAC 157,CODE,&PARAM(&CTR2)
.SHRD    ANOP
&BB(4)   SETB  0                                                   SDS1
&BB(5)   SETB  0                                                   SDS1
         AIF   (&IHBRES).ENQ070                                    SDS1
         AIF   ('&PARAM(&CTR7)' EQ 'STEP').ENQ080              BH 33990
&B(1)    SETB  ('&PARAM(&CTR7)' EQ 'SYSTEMS')                      SDS1
&B(4)    SETB  ('&PARAM(&CTR7)' EQ 'SYSTEMS')                      SDS1
         AIF   (&B(1)).ENQ080                                      SDS1
&B(1)    SETB  ('&PARAM(&CTR7)' EQ 'SYSTEM')                       SDS1
         AIF   (&B(1)).ENQ080                                      SDS1
       AIF   ('&PARAM(&CTR7)' EQ '' AND ('&MF' EQ 'L')).ENQ080 BH 33990
&BB(4)   SETB  1                                               BH 33990
     AIF  ('&PARAM(&CTR7)' EQ '' AND ('&MF(1)' EQ 'E')).ENQ080 BH 33990
         IHBERMAC 157,SYSTEMS,&PARAM(&CTR7)                        SDS1
         AGO   .ENQ080                                             SDS1
.ENQ070  ANOP                                                      SDS1
&B(4)    SETB  1                                                   SDS1
.ENQ080  ANOP                                                      SDS1
&CTR5    SETA  128*&BB(3)+64*&BB(4)+48*&BB(1)+8*&BB(5)+7*&BB(2)    SDS1
         NI    &CTR6.(1),&CTR5                    TURN OFF OPTIONS
&CTR5    SETA  128*&B(8)+64*&B(1)+32*&B(2)+16*&B(3)+8*&B(4)+4*&B(5)+2*&X
               B(6)+&B(7)                                          SDS1
         AIF   (&CTR5 EQ 0).ENQ090                                 SDS1
         OI    &CTR6.(1),&CTR5                    TURN ON OPTIONS
.ENQ090  AIF   ('&PARAM' EQ '').ENQ100                             SDS1
&CTR6    SETA  &CTR6+2
         AIF   ('&PARAM(&CTR)' EQ '').ERN
         AIF   ('&PARAM(&CTR)'(1,1) EQ '(').EQNREG
         LA    14,&PARAM(&CTR)                    GET QNAME ADDRESS
         ST    14,&CTR6.(0,1)                     STORE QNAME ADDRESS
         AGO   .ERN
.EQNREG  ANOP
&CTR4    SETA  K'&PARAM(&CTR)-2
&C       SETC  '&PARAM(&CTR)'(2,&CTR4)
         ST    &C,&CTR6.(0,1)                     STORE QNAME ADDRESS
.ERN     ANOP
&CTR6    SETA  &CTR6+4
         AIF   ('&PARAM(&CTR1)' EQ '').EEND
         AIF   ('&PARAM(&CTR1)'(1,1) EQ '(').ERNREG
         LA    14,&PARAM(&CTR1)                   GET RNAME ADDRESS
         ST    14,&CTR6.(0,1)                     STORE RNAME ADDRESS
         AGO   .EEND
.ERNREG  ANOP
&CTR4    SETA  K'&PARAM(&CTR1)-2
&C       SETC  '&PARAM(&CTR1)'(2,&CTR4)
         ST    &C,&CTR6.(0,1)                     STORE RNAME ADDRESS
.EEND    ANOP
&CTR6    SETA  &CTR6+4
&CTR     SETA  &CTR+4+&DENQ
         AGO   .LLOOP
.ENQ100  ANOP                                                      SDS1
&CTR6    SETA  12                                                  SDS1
.ENQ110  AIF   ('&UCB' EQ '').ENQ060                               SDS1
         AIF   ('&UCB'(1,1) EQ '(').ENQ120                         SDS1
         LA    14,&UCB            GET UCB PARAMETER                SDS1
         ST    14,&CTR6.(0,1)     STORE UCB PARAMETER              SDS1
         AGO   .ENQ060                                             SDS1
.ENQ120  ANOP                                                      SDS1
&CTR4    SETA  K'&UCB-2                                            SDS1
&C       SETC  '&UCB'(2,&CTR4)                                     SDS1
         ST    &C,&CTR6.(0,1)     STORE UCB PARAMETER              SDS1
         AGO   .ENQ060                                             SDS1
.ENQ130  ANOP                                                      SDS1
         IHBERMAC 196                                              SDS1
&IHBRES  SETB  0                                                   SDS1
         MEXIT
.E1      IHBERMAC 2
&IHBDEQ  SETB  0
&IHBRES  SETB  0                                                   SDS1
         MEXIT
.E2      IHBERMAC  24
&IHBDEQ  SETB  0
&IHBRES  SETB  0                                                   SDS1
         MEXIT
.E3      IHBERMAC 69
&IHBDEQ  SETB  0
&IHBRES  SETB  0                                                   SDS1
         MEXIT
.E4      IHBERMAC  35,,&MF
&IHBDEQ  SETB  0
&IHBRES  SETB  0                                                   SDS1
         MEND
