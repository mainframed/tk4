         MACRO
         IDFTR
         COPY  IDFGBL
         AIF   (NOT &PIB(48)).TR
         MNOTE *,'IDF100 IN TRACE MODE ENTERING IDFTR'
.TR      ANOP
.* &I POINTS AT 1ST CHARACTER TO XLATE
         AIF   (&TB(3)).TR8 KATAKANA?
&TB(6)   SETB  0
&TB(7)   SETB  0
&TB(8)   SETB  0
&TB(9)   SETB  0  SET UPPER CASE
.TR1     ANOP
&TB(1)   SETB  0
&TB(2)   SETB  0
         AIF   (&I GE K'&SYSLIST(1)).TR9   DONE?
&S       SETA  1
&C(2)    SETC  '&SYSLIST(1)'(&I,1) PICK UP A CHAR
         AIF   ('&C(2)' NE '&SYSLIST(1)'(&I+1,1) AND                   X
               '&C(2)' EQ '''').TR9    UNPAIRED APOS'PHE ENDS SCAN
         AIF   ('&C(2)' NE '_').TR1AAA
&TB(9)   SETB  (NOT &TB(9))
&I       SETA  &I+1
         AGO   .TR1
.TR1AAA  ANOP
.* PLUS-MINUS, RIGHT AND LEFT BRACKET, LEFT SLANT, LEFT AND RIGHT BRACE
         AIF   ('&C(2)' EQ '’' OR '&C(2)' EQ ']' OR '&C(2)' EQ '[' OR  X
               '&C(2)' EQ '\' OR '&C(2)' EQ '{' OR '&C(2)' EQ '}').TR6
         AIF   ('&C(2)' EQ '~').TR6        TILDE
         AIF   ('&C(2)' EQ '×').TR7  LOGICAL OR
.TR1AA   AIF   ('&C(2)' NE ' ').TR1A
&C(1)    SETC  '2'  ZONE AND
&C(2)    SETC  '0'    DIGIT OF BLANK
         AGO   .TR13
.TR1A    AIF   ('&C(2)' LT 'a' OR '&C(2)' GT 'Z').TR4 NOT ALPHA?
&TB(2)   SETB  ('&C(2)' LE 'z')  LOWER CASE
         AIF   (&TB(2)).TR3
.TR2     AIF   ('&C(2)' EQ 'AEIOUSTDCLMNPRYBFGHJKQVWXZ'(&S,1)).TR10
&S       SETA  &S+1
         AIF   (&S LE 26).TR2
         AGO   .TR8  NOT EXPECTED CAPITAL, ERROR
.TR3     ANOP
.* FOLLOWING CHAR STRING IS SMALL AEIOUSTDCLMNPRYBFGHJKQVWXZ
         AIF   ('&C(2)' EQ 'aeioustdclmnprybfghjkqvwxz'(&S,1)).TR10
&S       SETA  &S+1
         AIF   (&S LE 26).TR3
         AGO   .TR8  NOT EXPECTED SMALL, ERROR
.TR4     AIF   (&TB(4) AND NOT &TB(5)).TR8 NOT EXPECTED ALPHA - ERROR
         AIF   ('&C(2)' LT '0' OR '&C(2)' GT '9').TR5 VALID NUMERIC?
&C(1)    SETC  '3'            SET ASCII ZONE - DIGIT IN &C(2)
         AGO   .TR13
.TR5     AIF   (&TB(4) OR &TB(5)).TR8 NOT EXPECTED ALPHAMERIC - ERROR
&S       SETA  1
.TR6     AIF   ('&C(2)' EQ '.,$*#@?/():;%"-+=[]!’\<>^Ö`~{}'(&S,1)).TR11
.* PERIOD, COMMA, DOLLAR, ASTERISK, NUMBER, AT, QUESTION, SLASH,
.* LEFT PAREN, RIGHT PAREN, COLON, SEMI-COLON, PERCENT, QUOTE,
.* MINUS, PLUS, EQUAL, LESS THAN, GREATER THAN, LEFT AND RIGHT BRACKETS
.* EXCLAMATION, PLUS-MINUS, LEFT SLANT, LOGICAL NOT, CENT
.* GRAVE ACCENT, TILDE, LEFT AND RIGHT BRACES
&S       SETA  &S+1
         AIF   (&S LE 30).TR6
&S       SETA  1
.TR7     AIF   ('&C(2)' EQ '×''&&'(&S,1)).TR12
.* LOGICAL OR, APOSTROPHE, AMPERSAND (DOUBLE LOGICAL OR IS CIRCUMFLEX)
&S       SETA  &S+1
         AIF   (&S LE 3).TR7
.TR8     ANOP
&TB(4)   SETB  (NOT &TB(4)) NO EXPECTED COMPARE - ERROR
.TR9     AGO   .TR16
.TR10    ANOP
&TB(2)   SETB  (&TB(2) OR &TB(9))  LOWER CASE
&C(1)    SETC  '6464646475757564646464647575756464646464647575757575'(&C
               S*2-&TB(2),1) ZONE OF SMALL OR CAP ALPHA
&C(2)    SETC  '159F53443CDE0292678AB1678A'(&S,1) DIGIT OF ALPHA
         AGO   .TR13
.TR11    ANOP
&C(1)    SETC  '222224322233222235521533116777'(&S,1) ZONE AND
&C(2)    SETC  'EC4A30FF89AB52DBDBD11CCE200EBD'(&S,1) DIGIT OF SPECIAL
&TB(6)   SETB  (&S GE 25 OR &TB(6)) UNPRINTABLE ON ANY 3735
&TB(7)   SETB  (&S EQ 21 OR &TB(7)) UNPRINTABLE ON ASCII 3735
&TB(8)   SETB  (&S LE 24 AND &S GE 22 OR &TB(8)) UNPRT ON EBCDIC
         AGO   .TR13
.TR12    ANOP
&TB(1)   SETB  ('&C(2)' EQ '&SYSLIST(1)'(&I+1,1)) CHAR DOUBLED
.* CHARS DOUBLED FOR ASM OR DIFFERENT XLATE
&C(1)    SETC  '752222'(2*&S-&TB(1),1) ZONE AND
&C(2)    SETC  'CE7766'(2*&S-&TB(1),1) DIGIT OF COUBLED CHARS
&TB(8)   SETB  (&S EQ 1 AND &TB(1) OR &TB(8)) EBCDIC 3735 UNPRINTABLE
&TB(6)   SETB  (&S EQ 1 AND NOT &TB(1) OR &TB(6)) 3735 UNPRINTABLE
.TR13    ANOP
&I       SETA  &I+1+&TB(1) BUM I (BY 2 IF CHAR COUBLED)
         AIF   (&PIB(15)).TR15
         AIF   (&PIA(6) EQ &PIA(7)).TR14 WILL ASM CROSS SECTOR?
         IDFASM C       YES, LET IDFASM ASSEMBLE CHAR
         AGO   .TR1
.TR14    ANOP
.* ASSEMBLE XLATED CHARACTER, UPDATE LOCATION COUNTERS
         DC    X'4&C(1)7&C(2)'          TRANSLATED CHARACTER
.TR15    ANOP
&PIA(6)  SETA  (&PIA(6)-&PIA(6)/486*486)/478*18+&PIA(6)+2
&PIA(7)  SETA  &PIA(7)+2
&PIA(4)  SETA  &PIA(4)+2
         AGO   .TR1
.TR16    AIF   (NOT &PIB(48)).TX
         MNOTE *,'IDF100 IN TRACE MODE LEAVING IDFTR'
.TX      ANOP
         MEND
