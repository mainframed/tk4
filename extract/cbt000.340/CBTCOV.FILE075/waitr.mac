         MACRO
&NAME    WAITR &CNT,&ECB=,&ECBLIST=
         AIF   ('&ECB&ECBLIST' EQ '').E1
         AIF   (T'&ECB NE 'O' AND T'&ECBLIST NE 'O').E2
         AIF   ('&ECB' NE '').CHECK
         AIF   ('&ECBLIST'(1,1) EQ '(').REG
         AGO   .CONTD
.CHECK   AIF   ('&ECB'(1,1) NE '(').CONTD
.REG     ANOP
&NAME    LA    1,0(0,&ECB(1)&ECBLIST(1))   CLEAR HIGH BYTE OF REG
         AIF   ('&CNT' EQ '').CONTF
         AIF   ('&CNT'(1,1) EQ '(').CONTG
         LA    0,&CNT                         LOAD PARAMETER REG 0
         AGO   .CONTA
.CONTD   ANOP
         AIF   ('&CNT' EQ '&CNT(1)' AND '&CNT' NE '').CONTC
&NAME    IHBINNRA &ECB&ECBLIST,&CNT         LOAD REG 0 AND REG 1
         AIF   ('&CNT' NE '').CONTA
.CONTF   LA    0,1(0,0)                      COUNT OMITTED,1 USED
         AGO   .CONTA
.CONTG ANOP
         LR    0,&CNT(1)
         AGO   .CONTA
.CONTC   ANOP
&NAME    IHBINNRA &ECB&ECBLIST,&CNT.(0,0)
.CONTA   AIF   ('&ECBLIST' EQ '').CONTB
         LCR   1,1                               INDICATE ECBLIST USED
.CONTB   LCR   0,0                               INDICATE WAITR MACRO
         SVC   1                                 LINK TO WAIT ROUTINE
         MEXIT
.E1      IHBERMAC 11                        ECB AND ECBLIST  MISSING
         MEXIT
.E2      IHBERMAC 54,,'ECB=&ECB AND ECBLIST=&ECBLIST'
         MEND
