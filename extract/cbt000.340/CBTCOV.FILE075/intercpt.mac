         MACRO
&NAME    INTERCPT &PAR1
         GBLB  &SEN
         LCLB  &NOGO
         AIF   (&SEN).SEQOK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW SEND DELIMITX
               ER MACRO'
&NOGO    SETB  1
.SEQOK   AIF   ('&PAR1' NE '').OK
         MNOTE 12,'***  IHB001  ERROR WORD OPERAND REQ''D-NOT SPECIFIEDX
               '
         AGO   .END
.OK      AIF   (K'&PAR1 NE 7).TST2
         AIF   ('&PAR1'(1,2) EQ 'X''' AND '&PAR1'(7,1) EQ '''').OK2
.TST2    AIF   (K'&PAR1 LT 4).ER
         AIF   (K'&PAR1 LT 10 AND '&PAR1'(1,4) EQ 'XL2''' AND '&PAR1'(KX
               '&PAR1,1) EQ '''').OK2
.ER      MNOTE 12,'***  IHB002  INVALID ERR. HALF WORD OPERAND SPECIFIEX
               D'
         AGO   .END
.OK2     AIF   (&NOGO).END
&NAME    LA    1,INTERCPT              TERM TABLE INTERCEPT FLD. ADDR.
         L     15,=V(IECKITCP)         ADDRESS OF INTERCEPT ROUTINE
         BALR  14,15                   TO THE INTERCEPT ROUTINE
         DC    &PAR1
.END     MEND
