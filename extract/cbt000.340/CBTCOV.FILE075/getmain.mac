         MACRO
&NAME    GETMAIN &MODE,&LV=,&LA=,&A=,&SP=,&MF=I,&HIARCHY=
         LCLA  &NO
         LCLB  &B,&C,&D,&E,&F
         LCLC  &GNAME,&AA
.*0000000400,012200,013000-013200,016800-017600,026600-026800      LC0A
.*    047000                                                       LC0A
&GNAME   SETC  'IHB'.'&SYSNDX'
         AIF   ('&MODE' EQ '' AND '&MF' EQ 'I').ERROR1
         AIF   ('&LV' NE '' AND '&LA' NE '').ERROR5
         AIF   ('&MODE' EQ '').CONT1
         AIF   ('&MODE'(1,1) EQ 'E' AND '&LA' NE '').ERROR6
         AIF   ('&MODE' EQ 'R' AND '&LA' NE '').ERROR6
         AIF   ('&MODE'(1,1) EQ 'L' AND '&LV' NE '').ERROR4
         AIF   ('&MODE'(1,1) EQ 'V' AND '&LV' NE '').ERROR4
         AIF   ('&HIARCHY' EQ '' OR '&HIARCHY' EQ '0' OR '&HIARCHY' EQ X
               '1').CONT1
         IHBERMAC 195
         MEXIT
.CONT1   ANOP
         AIF   ('&MF' EQ 'L').LROUT
         AIF   ('&MF' EQ 'I').IROUT
         AIF   (N'&MF LE 1).ERROR2
         AIF   ('&MF(1)' NE 'E').ERROR2
&NAME    IHBINNRA &MF(2)
.CONTA   AIF   ('&LV' EQ '').CONTB
         AIF   ('&LV'(1,1) EQ '(').ISAREG
         AIF   (T'&LV EQ 'N').CONTAA
.CONTBB  ANOP
         CNOP  0,4
         B     *+8                               BRANCH AROUND LENGTH
         DC    A(&LV)                            LENGTH
         MVC   0(4,1),*-4                        MOVE LENGTH INTO LIST
         AGO   .CONTB
.CONTAA  AIF   (&LV GT 4095).CONTBB
         LA    14,&LV.(0,0)                      PICK UP LENGTH
         ST    14,0(1,0)                         STORE INTO LIST
         AGO   .CONTB
.ISAREG  ANOP
         ST    &LV(1),0(1,0)                     STORE LENGTH INTO LIST
.CONTB   AIF   ('&MODE' EQ '').CONTC
         AIF   ('&MODE'(1,1) NE 'E' AND '&LV' NE '').ERROR4
         AIF   ('&MODE' NE 'EC').ISAU
         MVI   8(1),X'20'                        SET MODE TO EC
         AGO   .CONTC
.ISAU    AIF   ('&MODE' NE 'EU').CONTC
         MVI   8(1),X'00'                        SET MODE TO EU
.CONTC   AIF   ('&LA' EQ '').CONTD
         AIF   ('&LA'(1,1) EQ '(').ISAREGA
         LA    14,&LA                            PICK UP LIST ADDRESS
         ST    14,0(1,0)                         STORE INTO PARAM LIST
         AGO   .CONTD
.ISAREGA ST    &LA(1),0(1,0)                     STORE LA IN PARAM LIST
.CONTD   AIF   ('&MODE' EQ '').CONTE
         AIF   ('&MODE' EQ 'EU' OR '&MODE' EQ 'EC').CONTE
         AIF   ('&MODE' NE 'LC').CONTF
         MVI   8(1),X'A0'                        SET MODE TO LC
         AGO   .CONTE
.CONTF   AIF   ('&MODE' NE 'LU').CONTG
         MVI   8(1),X'80'                        SET MODE TO LU
         AGO   .CONTE
.CONTG   AIF   ('&MODE' NE 'VC').CONTH
         MVI  8(1),X'E0'                         SET MODE TO VC
         AGO   .CONTE
.CONTH   AIF   ('&MODE' NE 'VU').ERROR7
         MVI   8(1),X'C0'                        SET MODE TO VU
.CONTE   AIF   ('&A' EQ '').HID
         AIF   ('&A'(1,1) EQ '(').ISAREGB
         LA    14,&A                             LOAD AREA LIST ADDRESS
         ST    14,4(1,0)                         STORE INTO PARAM LIST
         AGO   .HID
.ISAREGB ST    &A(1),4(1,0)                      STORE INTO PARAM LIST
.HID     AIF   ('&HIARCHY' EQ '').CONTI
         MVI   4(1),&HIARCHY                     MOVE IN HIAR BYTE
.CONTI   AIF   ('&SP' EQ '').FINI
         AIF   ('&SP'(1,1) EQ '(').ISAREGC
         MVI   9(1),&SP                          MOVE IN SUBPOOL VALUE
         AGO   .FINI
.ISAREGC STC   &SP(1),9(1,0)                     STORE SUBPOOL VALUE
.FINI    SVC   4                                 ISSUE GETMAIN SVC
         MEXIT
.LROUT   AIF   ('&LV' EQ  '').CONTJ
         AIF   ('&LV'(1,1) EQ '(').ERROR3
&NAME    DC    A(&LV)                            LENGTH
         AGO   .CONTL
.CONTJ   AIF   ('&LA' EQ '').CONTK
         AIF   ('&LA'(1,1) EQ '(').ERROR3
&NAME    DC    A(&LA)                            ADDR. OF LENGTH LIST
         AGO   .CONTL
.CONTK   ANOP
&NAME    DC   A(0)                               LA OR LU
.CONTL   AIF   ('&HIARCHY' EQ '').NOHID
         DC    AL1(&HIARCHY)
         AGO   .CONTLL
.NOHID   DC    AL1(0)                            HIARCHY FILL
.CONTLL  AIF   ('&A' EQ '').CONTM
         AIF   ('&A'(1,1) EQ '(').ERROR3
         DC    AL3(&A)                           ADDR. OF ADDR. LIST
         AGO   .CONTN
.CONTM   DC    AL3(0)                            ADDR. OF ADDR. LIST
.CONTN   AIF   ('&MODE' EQ '').CONTO
         AIF   ('&MODE' NE 'EC').CONTQ
         DC    X'20'                             EC MODE
         AGO   .CONTSP
.CONTQ   AIF   ('&MODE' NE 'EU').CONTP
         DC    X'00'                             EU MODE
         AGO   .CONTSP
.CONTP   AIF   ('&MODE' NE 'LC').CONTS
         DC    X'A0'                             LC MODE
         AGO   .CONTSP
.CONTS   AIF   ('&MODE' NE 'LU').CONTR
         DC    X'80'                             LU MODE
         AGO   .CONTSP
.CONTR   AIF   ('&MODE' NE 'VC').CONTT
         DC    X'E0'                             VC MODE
         AGO   .CONTSP
.CONTT   AIF   ('&MODE' NE 'VU').ERROR7
         DC    X'C0'                             VU MODE
         AGO   .CONTSP
.CONTO   DC    X'00'                             MODE
.CONTSP  AIF   ('&SP' EQ '').CONTU
         AIF   ('&SP'(1,1) EQ '(').ERROR3
         DC    AL1(&SP)                          SUBPOOL VALUE
         AGO   .FINISH
.CONTU   DC    AL1(0)                            SUBPOOL VALUE
.FINISH  MEXIT
.IROUT   AIF   ('&MODE' EQ 'R').RROUT
         AIF   ('&LV' EQ '' AND '&LA' EQ '').ERROR8
         CNOP  0,4
&NAME    BAL   1,*+14                            BRANCH AROUND LIST
         AIF   ('&LV' EQ '').CNTA
         AIF   ('&LV'(1,1) EQ '(').CNTB
         DC    A(&LV)                            LENGTH
         AGO   .CNTC
.CNTB    DC    A(0)                              LENGTH
&B       SETB  1
         AGO   .CNTC
.CNTA    AIF   ('&LA'(1,1) EQ '(').CNTD
         DC    A(&LA)                            ADDR. OF LENGTH LIST
         AGO   .CNTC
.CNTD    DC    A(0)                              ADDR. OF LENGTH LIST
&C       SETB  1
.CNTC    AIF   ('&A' EQ '').ERROR8
         AIF   ('&A'(1,1) EQ '(').CNTE
         AIF   ('&HIARCHY' EQ '' OR '&HIARCHY' EQ '0').CONTCCC
.CNTCC   DC    AL1(&HIARCHY)                     HIAR BYTE
         DC    AL3(&A)                           ADDR. OF ADDR. LIST
         AGO   .CNTF
.CONTCCC DC    A(&A)                  ADDR. OF ADDR. LIST
         AGO   .CNTF
.CNTE    DC    A(0)                              ADDR. OF ADDR. LIST
&D       SETB  1
.CNTF    AIF   ('&MODE' NE 'EU').CNTG
         DC    X'00'                             EU MODE
         AGO   .CNTSP
.CNTG    AIF   ('&MODE' NE 'EC').CNTH
         DC    X'20'                             EC MODE
         AGO   .CNTSP
.CNTH    AIF   ('&MODE' NE 'LU').CNTI
         DC    X'80'                             LU MODE
         AGO   .CNTSP
.CNTI    AIF   ('&MODE' NE 'LC').CNTJ
         DC    X'A0'                             LC MODE
         AGO   .CNTSP
.CNTJ    AIF   ('&MODE' NE 'VU').CNTK
         DC   X'C0'                              VU MODE
         AGO   .CNTSP
.CNTK    AIF   ('&MODE' NE 'VC').ERROR7
         DC   X'E0'                              VC MODE
.CNTSP   AIF   ('&SP' EQ '').CNTL
         AIF   ('&SP'(1,1) EQ '(').ISAREGQ
         DC    AL1(&SP)                          SUBPOOL VALUE
         AGO   .CNTM
.ISAREGQ DC    AL1(0)                            SUBPOOL VALUE
         STC   &SP(1),9(1,0)                     STORE SP INTO LIST
         AGO   .CNTM
.CNTL    DC    AL1(0)                            SUBPOOL VALUE
.CNTM    AIF   (NOT &B).CNTN
         ST    &LV(1),0(1,0)                     STORE LENGTH INTO LIST
         AGO   .CNTO
.CNTN    AIF   (NOT &C).CNTO
         ST    &LA(1),0(1,0)                     STORE LA INTO LIST
.CNTO    AIF   (NOT &D).CNTP
         ST    &A(1),4(1,0)                      STORE INTO PARAM LIST
         AIF   ('&HIARCHY' EQ '' OR '&HIARCHY' EQ '0').CNTP
         MVI   4(1),&HIARCHY                     MOVE IN HIAR BYTE
.CNTP    SVC   4                                 ISSUE GETMAIN SVC
         MEXIT
.RROUT   AIF   ('&A' NE '').ERROR9
         AIF   ('&LV'(1,1) EQ '(').ISARGA
         AIF   ('&SP' EQ '').CTUA
         AIF   ('&SP'(1,1) EQ '(').ISARGB
         CNOP  0,4
&NAME    BAL   1,*+8                             BRANCH AROUND SP+LV
         DC    AL1(&SP)                          SUBPOOL VALUE
         DC    AL3(&LV)                          LENGTH
         L     0,0(1,0)                          LOAD SP AND LV
         AIF   ('&HIARCHY' EQ '1').CONTHH
         SVC   10                                ISSUE GETMAIN SVC
         MEXIT
.CTUA    AIF   (T'&LV EQ 'N').CONTCC
.CTUAA   CNOP  0,4
&NAME    BAL   1,*+8                             BRANCH AROUND LENGTH
         DC    A(&LV)                            LENGTH
         L     0,0(1,0)                          LOAD LENGTH
         AIF   ('&HIARCHY' EQ '1').CONTHH
         SVC   10                                ISSUE GETMAIN SVC
         MEXIT
.CONTCC  AIF   (&LV GT 4095).CTUAA
&NAME    LA    0,&LV.(0,0)                       LOAD LENGTH
         AIF   ('&HIARCHY' EQ '1').CONTHH
         BAL   1,*+4                             INDICATE GETMAIN
         SVC   10                                ISSUE GETMAIN SVC
         MEXIT
.ISARGB  AIF   (T'&LV EQ 'N').CONTEE
.CONTFF  CNOP  0,4
&NAME    BAL   1,*+8                             BRANCH AROUND LENGTH
         DC    A(&LV)                            LENGTH
         LR    0,&SP(1)                          PICK UP SUBPOOL
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         O     0,0(1,0)                          PACK SP AND LV
         AIF   ('&HIARCHY' EQ '1').CONTHH
         SVC   10                                ISSUE GETMAIN SVC
         MEXIT
.CONTEE  AIF   (&LV GT 4095).CONTFF
&NAME    LR    0,&SP(1)                          PICK UP SUBPOOL
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         LA    1,&LV.(0,0)                       LOAD LENGTH
         OR    0,1                               PACK SP AND LV
         AIF   ('&HIARCHY' EQ '1').CONTHH
         BAL   1,*+4                             INDICATE GETMAIN
         SVC   10                                ISSUE GETMAIN SVC
         MEXIT
.ISARGA  AIF   ('&LV(1)' EQ '0').ZEROUT
         AIF   ('&SP' EQ '').CTUB
         AIF   ('&SP'(1,1) EQ '(').ISARGC
&NAME    LA    0,&SP.(0,0)                       PICK UP SUBPOOL
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         OR    0,&LV(1)                          PACK SP AND LV
         AIF   ('&HIARCHY' EQ '1').CONTHH
         BAL   1,*+4                             INDICATE GETMAIN
         SVC   10                                ISSUE GETMAIN SVC
         MEXIT
.CTUB    ANOP
&NAME    LR    0,&LV(1)                          LOAD LENGTH
         AIF   ('&HIARCHY' EQ '1').CONTHH
         BAL  1,*+4                              INDICATE GETMAIN
         SVC   10                                ISSUE GETMAIN SVC
         MEXIT
.CONTHH  CNOP  0,4
         L     1,*+8                             PUT NEG. LCS IN R1
         BC    15,*+8
         DC    XL4'80D3C3E3'
         SVC   10
         MEXIT
.ISARGC  ANOP
&NAME    LR    0,&SP(1)                          PICK UP SUBPOOL
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE
         OR    0,&LV(1)                          PACK SP AND LV
         AIF   ('&HIARCHY' EQ '1').CONTHH
         BAL   1,*+4                             INDICATE GETMAIN
         SVC   10                                ISSUE GETMAIN SVC
         MEXIT
.ZEROUT  ANOP
         AIF   ('&HIARCHY' EQ '1').HIAREG
&NAME    BAL   1,*+4                             INDICATE GETMAIN
         SVC   10                                ISSUE GETMAIN SVC
         AGO   .CONTGG
.HIAREG  ANOP
         CNOP  0,4
&NAME    L     1,*+8                             PUT NEG. LCS IN R1
         BC    15,*+8
         DC    XL4'80D3C3E3'
         SVC   10
.CONTGG  AIF   ('&SP' EQ '').EXIT
         IHBERMAC 92
.EXIT    MEXIT
.ERROR1  IHBERMAC 17
         MEXIT
.ERROR2  IHBERMAC 35,,&MF
         MEXIT
.ERROR3  IHBERMAC 69
         MEXIT
.ERROR4  IHBERMAC 89
         MEXIT
.ERROR5  IHBERMAC 91
         MEXIT
.ERROR6  IHBERMAC 90
         MEXIT
.ERROR7  IHBERMAC 45,,&MODE
         MEXIT
.ERROR8  IHBERMAC 01
         MEXIT
.ERROR9  IHBERMAC 93
         MEXIT                                                BC A45446
.ERROR10 IHBERMAC 46,,&SP,1                                   BC A45446
         MEND
