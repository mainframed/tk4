         MACRO
&NAME    LOGSEG &PAR1
         AIF   ('&PAR1' NE '').GO
         MNOTE 12,'***  IHB001  DCB OPERAND REQ''D-NOT SPECIFIED'
         AGO   .END
.GO      ANOP
&NAME    LA    14,4(6)             ADDRESS FIRST BYTE TO LOG
         LH    15,4(14)            LOAD MESSAGE SIZE
         LA    15,4(15)            ADD FOUR
         STH   15,0(14)            STORE IN FIRST HALFWORD TO LOG
         LR    0,14 LOAD ADDRESS OF FIRST BYTE TO LOG IN REGISTER 0
         AIF   ('&PAR1'(1,1) EQ '(').REG
         L     1,=A(&PAR1)             LOAD THE DCB ADDRESS
         AGO   .ADR
.REG     AIF   ('&PAR1' EQ '(1)').ADR
         LR    1,&PAR1(1)              DCB ADDRESS TO REGISTER 1
.ADR     L     15,48(1)                LOAD ADDRESS OF OUTPUT ROUTINE
         BALR  14,15                   TO OUTPUT ROUTINE
.END     MEND
