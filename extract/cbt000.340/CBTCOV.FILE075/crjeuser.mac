         MACRO
&USERNAM CRJEUSER &OPERAND
         GBLB  &GNOGO,&ORG
         GBLC  &GUSER
         LCLA  &N,&LNOGO,&INDEX,&MAX,&M,&MAXG,&MAXP
         LCLB  &SKIP
         LCLC  &USER,&CHAR1,&CHAR2,&PASS
.*       PARAMETER VALIDIZATION
         AIF   (N'&SYSLIST NE 0).COUNT
         MNOTE 1,'WARNING - NO USERIDS ARE ENTERED INTO THE SYSTEM DUE C
                  TO THIS MACRO EXPANSION'
         MEXIT
.*       CHECK FOR PAIRING OF USERIDS AND PASSWORDS
.COUNT   ANOP
&N       SETA  N'&SYSLIST
         AIF   ((&N/2)*2 EQ &N).BEGIN
         MNOTE 8,'ODD NUMBER OF OPERANDS, USERID''S AND PASSWORD''S    C
                     CANNOT BE PAIRED'
&GNOGO   SETB  1
         MEXIT
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF USERID
.BEGIN   ANOP
&INDEX   SETA  &INDEX+1
         AIF   (&INDEX GT &N).CHKEND
         AIF   ('&SYSLIST(&INDEX)' NE '').CHKUSR1
         AIF   ('&SYSLIST(&INDEX+1)' EQ '').SETUP
         MNOTE 4,'NO USERID FOR PASSWORD &SYSLIST(&INDEX+1)'
&USER    SETC  '@'
&LNOGO   SETA  &LNOGO+1
         AGO   .BEGIN1
.SETUP   ANOP
&INDEX   SETA  &INDEX+1
         AGO   .BEGIN
.CHKUSR1 ANOP
&USER    SETC  '&SYSLIST(&INDEX)'
         AIF   (K'&SYSLIST(&INDEX) LE 7).CHKUSR2
         MNOTE 4,'USERID &USER EXCEEDS SEVEN CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
&MAX     SETA  7
         AGO   .CHKUSR3
.CHKUSR2 ANOP
&MAX     SETA  K'&SYSLIST(&INDEX)
.CHKUSR3 ANOP
&CHAR1   SETC  '&SYSLIST(&INDEX)'(1,1)
         AIF   ('&CHAR1' GE 'A' AND '&CHAR1' LE 'Z').CHKUSR4
         MNOTE 4,'NON-ALPHABETIC FIRST CHARACTER ON USERID &USER'
&LNOGO   SETA  &LNOGO+1
.CHKUSR4 ANOP
&M       SETA  1
.CHKUSR5 ANOP
&M       SETA  &M+1
         AIF   (&M GT &MAX).BEGIN1
&CHAR2   SETC  '&SYSLIST(&INDEX)'(&M,1)
         AIF   ('&CHAR2' GE 'A' AND '&CHAR2' LE 'Z').CHKUSR5
         AIF   ('&CHAR2' GE '0' AND '&CHAR2' LE '9').CHKUSR5
         AIF   ('&CHAR2' EQ '$').CHKUSR5
         AIF   ('&CHAR2' EQ '#').CHKUSR5
         AIF   ('&CHAR2' EQ '@').CHKUSR5
         MNOTE 4,'USERID &USER CONTAINS INVALID CHARACTER(S)'
&LNOGO   SETA  &LNOGO+1
.*       CHECK FOR EXISTENCE AND CORRECT SPECIFICATION OF PASSWORD
.BEGIN1  ANOP
&INDEX   SETA  &INDEX+1
         AIF   (&INDEX GT &N).NOPASS
         AIF   ('&SYSLIST(&INDEX)' NE '').CHKPAS1
.NOPASS  MNOTE 4,'NO PASSWORD FOR USERID &SYSLIST(&INDEX-1)'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKCOM
.CHKPAS1 ANOP
&PASS    SETC  '&SYSLIST(&INDEX)'
         AIF   (K'&SYSLIST(&INDEX) LE 8).CHKPAS2
         MNOTE 4,'PASSWORD &PASS EXCEEDS EIGHT CHARACTERS IN LENGTH'
&LNOGO   SETA  &LNOGO+1
&MAXP    SETA  8
         AGO   .CHKPAS3
.CHKPAS2 ANOP
&MAXP    SETA  K'&SYSLIST(&INDEX)
.CHKPAS3 ANOP
&CHAR1   SETC  '&SYSLIST(&INDEX)'(1,1)
         AIF   ('&CHAR1' GE 'A' AND '&CHAR1' LE 'Z').CHKPAS4
         MNOTE 4,'NON-ALPHABETIC FIRST CHARACTER ON PASSWORD &PASS'
&LNOGO   SETA  &LNOGO+1
.CHKPAS4 ANOP
&M       SETA  1
.CHKPAS5 ANOP
&M       SETA  &M+1
         AIF   (&M GT &MAXP).CHKCOM
&CHAR2   SETC  '&SYSLIST(&INDEX)'(&M,1)
         AIF   ('&CHAR2' GE 'A' AND '&CHAR2' LE 'Z').CHKPAS5
         AIF   ('&CHAR2' GE '0' AND '&CHAR2' LE '9').CHKPAS5
         AIF   ('&CHAR2' EQ '$').CHKPAS5
         AIF   ('&CHAR2' EQ '#').CHKPAS5
         AIF   ('&CHAR2' EQ '@').CHKPAS5
         MNOTE 4,'PASSWORD &PASS CONTAINS INVALID CHARACTER(S)'
&LNOGO   SETA  &LNOGO+1
         AGO   .BEGIN
.*       CHECK FOR UNIQUENESS AND ALPHABETICAL ORDER OF USERIDS
.CHKCOM  AIF   ('&USER' EQ '@').BEGIN
         AIF   ('&GUSER' EQ '').CHKPNT1
         AIF   ('&GUSER' NE '&USER').CHKCOM2
         MNOTE 4,'USERID &USER IS A DUPLICATE OF A PREVIOUS USERID'
&LNOGO   SETA  &LNOGO+1
         AGO   .BEGIN
.CHKCOM2 ANOP
&M       SETA  0
.CHKCOM3 ANOP
&M       SETA  &M+1
         AIF   (&M LE &MAX).CHKCOM4
.ERROR   MNOTE 4,'USERID &USER IS NOT IN ALPHABETICAL ORDER'
&LNOGO   SETA  &LNOGO+1
         AGO   .CHKPNT
.CHKCOM4 AIF   (&M GT &MAXG).CHKPNT
         AIF   ('&GUSER'(&M,1) LT '&USER'(&M,1)).CHKPNT
         AIF   ('&GUSER'(&M,1) EQ '&USER'(&M,1)).CHKCOM3
         AGO   .ERROR
.CHKPNT1 AIF   ('&USER' EQ '').BEGIN
.*       GENERATION OF USER VERIFICATION RECORD
.CHKPNT  ANOP
&GUSER   SETC  '&USER'
&MAXG    SETA  &MAX
         AIF   (&LNOGO NE 0 OR &GNOGO).BEGIN
         AIF   (&SKIP).GENER1
         SPACE 1
IHKUSR   CSECT
&SKIP    SETB  1
.GENER1  ANOP
*
*        USER TABLE ENTRY
*
         AIF   (NOT &ORG).GENER
         ORG   *-1
&ORG     SETB  0
.GENER   ANOP
         DC    CL8'&USER' .     USERID
         DC    CL8'&PASS' .     PASSWORD
         DC    7D'0' .             PADDING
         DC    CL8'&USER' .     USERID
         AGO   .BEGIN
.*       CHECK MACRO TERMINATION CONDITIONS
.CHKEND  AIF   (&LNOGO EQ 0 AND NOT &GNOGO).SETEND
         AIF   (&LNOGO EQ 0 AND &GNOGO).ERROR2
         AIF   (&LNOGO NE 0 AND NOT &GNOGO).ERROR3
.ERROR1  MNOTE 8,'&LNOGO TERMINATING ERROR(S) INCURRED, NO GENERATION'
         MEXIT
.ERROR2  MNOTE 8,'NO TERMINATING ERRORS INCURRED, NO GENERATION DUE TO C
                  PREVIOUS ERRORS'
         MEXIT
.ERROR3  MNOTE 8,'&LNOGO TERMINATING ERROR(S) INCURRED, GENERATION     C
                       TERMINATED'
&GNOGO   SETB  1
         MEXIT
.SETEND  ANOP
&ORG     SETB  1
         DC    X'FF' .             TERMINATOR
         MEND
