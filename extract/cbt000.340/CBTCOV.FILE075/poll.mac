         MACRO
&NAME    POLL  &ENTRY,&AUTOPOL=
         LCLA  &CTR
         LCLA  &COUNT
         LCLA  &VALUE
         LCLC  &TERM
.* 440000,601000-606000                                          A29210
         AIF   ('&AUTOPOL' NE '').AP
&CTR     SETA  513
         AIF   ('&NAME' NE '').OK
         MNOTE 12,'***  IHB001  LABEL OPERAND REQ''D NOT SPECIFIED'
         MEXIT
.OK      AIF   ('&ENTRY' EQ '').GEN
         AIF   ('&ENTRY'(1,1) NE '(').ANSWER
&CTR     SETA  (N'&ENTRY+1)*512+1
         AGO   .GEN
.ANSWER  ANOP
&CTR     SETA  (((K'&ENTRY+1)/2+4)/2*512)+1
.GEN     ANOP
&NAME    DC    H'&CTR'                 LIST SIZE AND STATUS
         AIF   ('&ENTRY' EQ '').END
         AIF   ('&ENTRY'(1,1) NE '(').GO
&CTR     SETA  N'&ENTRY
.LOOP    ANOP
&CTR     SETA  &CTR-1
&TERM    SETC  '&ENTRY(N'&ENTRY-&CTR)'
         DC    AL2(&TERM-TERMTBL)
         AIF   (&CTR GT 0).LOOP
.END     DC    H'0'                    END OF LIST
         MEXIT
.GO      DC    X'00'
         DC    X'&ENTRY'
         MEXIT
.AP      AIF   ('&AUTOPOL' EQ '1').T1
         AIF   ('&AUTOPOL' EQ '2').T2
         MNOTE 12,'***  IHB090 INVALID AUTOPOL VALUE'
         MEXIT
.T1      AIF   (N'&ENTRY LE 123).GOTO
.T2      AIF   (N'&ENTRY LE 82).GOTO
         MNOTE 12,'***  IHB091 MORE THAN 123-1030''S OR 82 NON 1030''S'
         MEXIT
.GOTO    ANOP
&COUNT   SETA  N'&ENTRY
         DS    0H
&VALUE   SETA  ((&COUNT*(&AUTOPOL+1)+8)*256+(&COUNT*2)*256+1)
&NAME    DC    AL2(&VALUE) LIST SIZE AND STATUS
&VALUE   SETA  (&COUNT*256+&COUNT)
         DC    AL2(&VALUE) TOTAL ENTRIES, ACTIVE ENTRIES
&VALUE   SETA  (81+(&AUTOPOL-1)*32)
         DC    AL1(&VALUE) CONTAINS WIDTH
&CTR     SETA  N'&ENTRY
.LOOPA   ANOP
&CTR     SETA  &CTR-1
&TERM    SETC  '&ENTRY(N'&ENTRY-&CTR)'
         DC    AL2(&TERM-TERMTBL)
         AIF   ('&AUTOPOL' EQ '1').NZ
         DC    AL1(0)
.NZ      AIF   (&CTR GT 0).LOOPA
         DC    X'FE' SCAN STOP BYTE
&TERM    SETC  '&NAME'
         DC    AL2(*-&TERM) OFFSET TO BEGINNING OF LIST
&CTR     SETA  N'&ENTRY
.LOOPB   ANOP
&CTR     SETA  &CTR-1
&TERM    SETC  '&ENTRY(N'&ENTRY-&CTR)'
         DC    AL2(&TERM-TERMTBL)                                A29210
         AIF   (&CTR GT 0).LOOPB
         MEND
