         MACRO
&NAME    CIRB  &EP=,&KEY=PP,&MODE=PP,&STAB=,&SVAREA=NO,&WKAREA=0,&RETIQ*
               E=YES                                               1505
         LCLB  &B(6)                                               1505
         LCLC  &D                                                  MO19
.*008600-099200                                                    MO19
         AIF   ('&EP' EQ '').E1
&NAME    IHBINNRA ,&EP
&B(1)    SETB  ('&KEY' EQ 'PP')
         AIF   (NOT &B(1) AND '&KEY' NE 'SUPR').E2
&B(2)    SETB  ('&MODE' EQ 'PP')
         AIF   (NOT &B(2) AND '&MODE' NE 'SUPR').E3
&B(3)    SETB  ('&SVAREA' EQ 'YES')
         AIF   (NOT &B(3) AND '&SVAREA' NE 'NO').E4
&B(6)    SETB  ('&RETIQE' EQ 'NO')                                 1505
         AIF   (NOT &B(6) AND '&RETIQE' NE 'YES').E6               1505
         AIF   ('&STAB' EQ '').CONTX
.CONTA   AIF   (K'&STAB GT 3).CONTB
&B(4)    SETB  ('&STAB' EQ 'RE')
&B(5)    SETB  ('&STAB' EQ 'DYN')
         AGO   .THRU
.CONTB   AIF   (K'&STAB GT 7).CONTC
&B(4)    SETB  ('&STAB' EQ '(RE)' OR '&STAB' EQ '(,RE)' OR '&STAB' EQ 'X
               (RE,)')
&B(5)    SETB  ('&STAB' EQ '(DYN)' OR '&STAB' EQ '(,DYN)' OR '&STAB' EQX
                '(DYN,)')
         AGO   .THRU
.CONTC   AIF   (K'&STAB NE 8).THRU
&B(4)    SETB  ('&STAB' EQ '(RE,DYN)' OR '&STAB' EQ '(DYN,RE)')
&B(5)    SETB  (&B(4))
.THRU    AIF   (&B(4)+&B(5) EQ 0).E5
.CONTX   AIF   ('&WKAREA'(1,1) EQ '(').REG
         CNOP  0,4
         B     *+8                               BRANCH AROUND CONST.
         DC    BL2'01000&B(1)&B(2)&B(3).000&B(4)&B(6).0&B(5).0'    1505
         DC    AL2(&WKAREA)                      WORK AREA SIZE
         L     1,*-4                             LOAD REG 1 WITH CONST.
         AGO   .END
.REG     B     *+6                               BRANCH AROUND FLAGS
         DC    BL2'01000&B(1)&B(2)&B(3).000&B(4)&B(6).0&B(5).0'    1505
         LH    1,*-2                             LOAD REG 1 WITH FLAGS
         SLL   1,16(0)                           SHIFT TO LEFT HALF
         OR    1,&WKAREA(1)                      PACK IN WORK AREA SIZE
.END     SVC   43                                ISSUE CIRB SVC
         MEXIT
.E1      IHBERMAC 27,408
         MEXIT
.E2      ANOP                                                      MO19
&D       SETC  'KEY=&KEY'                                          MO19
         AGO   .OUT                                                MO19
.E3      ANOP                                                      MO19
&D       SETC  'MODE=&MODE'                                        MO19
         AGO   .OUT                                                MO19
.E4      ANOP                                                      MO19
&D       SETC  'SVAREA=&SVAREA'                                    MO19
         AGO   .OUT                                                MO19
.E5      ANOP                                                      MO19
&D       SETC  'STAB=&STAB'                                        MO19
         AGO   .OUT                                                MO19
.E6      ANOP                                                      MO19
&D       SETC  'RETIQE=&RETIQE'                                    MO19
.OUT     IHBERMAC 54,408,&D                                        MO19
         MEND
