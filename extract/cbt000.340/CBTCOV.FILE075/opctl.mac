         MACRO
&NAME    OPCTL &CTLMSG=,&TERM=,&ALTERM=,&INTRCPT=
         GBLB  &MULT,&RHDR
         GBLC  &TEXT
         LCLA  &L,&ALT
         LCLB  &NOGO
         AIF   ('&NAME' NE '').GO
         MNOTE 12,'***  IHB001  OPCTL NAME OPERAND REQ''D-NOT SPECIFIEDX
               '
&NOGO    SETB  1
.GO      AIF   (NOT &MULT).OK
         MNOTE 4,'***  IHB067  OPCTL MACRO PREVIOUSLY USED'
&NOGO    SETB  1
.OK      AIF   (&RHDR).KEY1
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW RCVHDR MACROX
               '
&NOGO    SETB  1
.KEY1    ANOP
&MULT    SETB  1
         AIF   ('&CTLMSG' NE '').NB1
         MNOTE 12,'***  IHB001  MESSAGE NAME OPERAND REQ''D-NOT SPECIFIX
               ED'
&NOGO    SETB  1
         AGO   .KEY2
.NB1     AIF   (K'&CTLMSG LE 8).KEY2
         MNOTE 12,'***  IHB002  INVALID MESSAGE NAME OPERAND SPECIFIED'
&NOGO    SETB  1
.KEY2    AIF   ('&TERM' NE '').NB2
         MNOTE 12,'***  IHB001  TERMINAL OPERAND REQ''D-NOT SPECIFIED'
&NOGO    SETB  1
         AGO   .KEY3
.NB2     AIF   (K'&TERM LE 8).KEY3
         MNOTE 12,'***  IHB002  INVALID TERMINAL OPERAND SPECIFIED'
&NOGO    SETB  1
.KEY3    ANOP
&ALT     SETA  0
         AIF   ('&ALTERM' EQ '').KEY4
         AIF   (K'&ALTERM LE 8).SET01
         MNOTE 12,'***  IHB002  INVALID ALT. TERMINAL OPERAND SPECIFIEDX
               '
&NOGO    SETB  1
         AGO   .KEY4
.SET01   ANOP
&ALT     SETA  01
.KEY4    AIF   ('&INTRCPT' EQ '').ERRCHK
         AIF   ('&INTRCPT' EQ 'YES' OR '&INTRCPT' EQ 'NO').OK1
         MNOTE 12,'***  IHB002  INVALID INTERCPT OPERAND SPECIFIED'
&NOGO    SETB  1
         AGO   .ERRCHK
.OK1     AIF   ('&INTRCPT' EQ 'NO').ERRCHK
         AIF   ('&ALTERM' EQ '').SET02
&ALT     SETA  03
         AGO   .ERRCHK
.SET02   ANOP
&ALT     SETA  02
.ERRCHK  AIF   (&NOGO).EXIT
&L       SETA  K'&CTLMSG
         CNOP  0,4
&NAME    BAL   1,IHB&SYSNDX.A LOAD PARAMETER LIST ADDRESS IN REGISTER 1
         DC    V(IECKOPAW)
         DC    AL1(&L)             LENGTH OF CONTROL MESSAGE IDENTIFIER
         DC    AL1(&ALT) '00' - NEITHER ALTERM NOR INTERCPT SPECIFIED
*                        '01' - ALTERM SPECIFIED BUT NOT INTERCPT
*                        '02' - INTERCPT SPECIFIED BUT NOT ALTERM
*                        '03' - ALTERM AND INTERCPT BOTH SPECIFIED
         DC    AL2(&TERM-TERMTBL)  OFFSET TO CONTROL TERMINAL
         AIF   ('&ALTERM' EQ '').NO4
         DC    AL2(&ALTERM-TERMTBL) OFFSET TO ALTERNATE CTL TERMINAL
.NO4     ANOP
         AIF   ('&INTRCPT' EQ 'NO' OR '&INTRCPT' EQ '').NO5
         DC    AL2(INTERCPT-IECKTERM) OFFSET TO INTERCEPT FIELD
.NO5     ANOP
         DC    C'&CTLMSG'          CONTROL MESSAGE NAME IDENTIFIER
IHB&SYSNDX.A CLC   10(2,6),6(1) IS MESSAGE FROM A CONTROL TERMINAL
         AIF   ('&ALTERM' EQ '').NO1
         BE    *+14                BRANCH IF YES FOR LINKAGE
         CLC   10(2,6),8(1)        IS MESSAGE FROM ALTERNATE TERMINAL
.NO1     ANOP
         BNE   IHB&SYSNDX+6 BRANCH IF NO TO NEXT MACRO
.NO2     ANOP
         L     15,=V(IECKOCTL)     LOAD ADDRESS OF OPCTL ROUTINE
IHB&SYSNDX BALR 14,15              LINK TO OPERATOR CONTROL ROUTINE
         B     &TEXT               GO TO ENDRCV MACRO EXPANSION
.EXIT    MEND
