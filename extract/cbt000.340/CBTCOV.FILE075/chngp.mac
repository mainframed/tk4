         MACRO
&NAME    CHNGP   &DCB,&RLN,&PAR3
         GBLB  &IEDQE7
         LCLA  &IX
.* 070000-075000,724000-736000                                    20002
&NAME    DS    0H
         AIF   (&IEDQE7 EQ 1).TCAM                                20002
         AIF   ('&DCB' NE '' AND '&RLN' NE '' AND '&PAR3' NE '').OK
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      AIF   ('&DCB'(1,1) EQ '(' OR K'&DCB LT 9).OK2
         MNOTE 12,'***  IHB002  INVALID TERMNAME OPERAND SPECIFIED'
         MEXIT
.OK2     ANOP
         AIF   ('&RLN' EQ '(0)').SHIFT
         AIF   ('&RLN'(1,1) NE '(').LA
         LR    0,&RLN(1)               REL. LINE NUMBER TO REGISTER 0
         AGO   .SHIFT
.LA      LA    0,&RLN                  REL. LINE NUMBER TO REGISTER 0
.SHIFT   SLL   0,24                    POSITION RELATIVE LINE NUMBER
         AIF   ('&DCB'(1,1) EQ '(').NOLOC
&IX      SETA  12
         AIF   ('&PAR3'(1,1) EQ '(').X
&IX      SETA  &IX+2
.X       BAL   1,*+12                  SET TERMNAME ADDRESS
         DC    CL8'&DCB'               TERMINAL NAME
         L     15,=V(IECKDCBL)         ADDRESS OF DCB LOCATE ROUTINE
         BALR  14,15                   LINK TO DCB LOCATE ROUTINE
         LTR   15,15                   TEST THE RETURN CODE
         BC    7,*+&IX                 BRANCH ON ERROR-INVALID TERMNAME
         AGO   .AFT
.NOLOC   LA    1,0(&DCB(1))            DCB ADDRESS TO REGISTER ONE
         ALR   0,1                     COMBINE DCB ADDRESS AND REL LINE
.AFT     AIF   ('&PAR3'(1,1) EQ '(').LR
         LA    1,&PAR3                 WORKAREA ADDRESS TO REGISTER 1
         AGO   .LOAD
.LR      LR    1,&PAR3(1)              WORKAREA ADDRESS TO REGISTER 1
.LOAD    L     15,=V(IECKCHPL)         ADDRESS OF CHANGE POLL LIST RT.
         BALR  14,15                   LINK TO CHANGE POLL LIST ROUTINE
         MEXIT
.TCAM    ANOP
         MNOTE 0,'*** IHB301 QSTART SPECIFIED - NO EXPANSION'
         CNOP  2,4 .                                              20002
         MEND
