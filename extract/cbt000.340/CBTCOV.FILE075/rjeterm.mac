         MACRO
&NAME    RJETERM &TYPE=2780,&PUNCH=YES,&PRTSZ=120,               A29501X
               &FEATURE=NONE,&PACK=NO,&ID=                       A29501
         GBLA  &IHKTMNB                                            000A
         GBLB  &IHKSWT                                             000A
         GBLB &IHKTRB                                              000C
         GBLB  &IHKUP                                              000C
         GBLB  &IHKPU                                              000C
         GBLB  &IHKENT                                             000C
         LCLB  &SW(8)                                              000A
         LCLC  &CHAR                                               000A
         LCLA  &NUMB,&NUMBR                                        000D
.*27830050-9500                                                    000A
.*1210014000,126000-140000,784000,798000-805000                    000C
.* 732000                                                        A28213
.* 188000-196000                                                 A29493
.* 008000,012000,226000,236000,248000                            A29501
.* 662000                                                        A29510
.*C371000                                                        A40162
.*A636500                                                        A40162
.*C 098000,102000,110000                                         A43947
.*C 647000-650000                                                A61477
.*
         AIF   ('&NAME' NE '').NOK                                 000A
         MNOTE 12,'NAME FIELD BLANK. NAME IS REQUIRED'             000A
&SW(1)   SETB  1                                                   000A
.NOK     ANOP                                                      000A
         AIF   ('&PUNCH' EQ 'YES' OR '&PUNCH' EQ 'NO').PCHOK       000A
         MNOTE 8,'PUNCH=&PUNCH INCORRECTLY SPECIFIED. DEFAULT ASSUMED'
.*                                                                 000A
&SW(3)   SETB  1                                                   000A
.PCHOK   ANOP                                                      000A
&SW(4)   SETB  ('&TYPE' EQ '2780')                                 000A
         AIF   ((&SW(4)  OR '&TYPE' EQ '1130' OR '&TYPE' EQ            X
               '2020' OR '&TYPE' EQ '2770' OR '&TYPE' EQ 'SYS3' OR     X
               '&TYPE' EQ 'INTMX') AND '&ID' NE '').MPTLN        A43947
         AIF   ('&TYPE' EQ 'CPU' OR '&TYPE' EQ '1130' OR '&TYPE'       Z
               EQ '2020' OR '&TYPE' EQ 'SYS3').ACPU              A43947
         AIF   ('&TYPE' EQ '2770').CKPRT                           000D
         AIF   ('&TYPE' EQ '2780').CKPRT                           000A
         MNOTE 8,'INVALID TYPE OPERAND=&TYPE.2780 ASSUMED'         000A
         AGO   .CKPRT                                              000A
.MPTLN   ANOP                                                      000A
.CNTOK   ANOP                                                      000A
         AIF   (&SW(4)).CK2780                                     000A
&CHAR    SETC  '&ID.2D'                                            000A
         AGO   .CKPCH                                              000A
.CK2780  ANOP                                                      000A
&CHAR    SETC  '&ID.F32D'                                          000A
         AGO   .CKPCH                                              000A
.ACPU    ANOP                                                      000A
         AIF   ('&ID' EQ '').CKPCH                                 000A
         MNOTE 12,'ID SPECIFIED FOR TYPE=CPU INVALID'              000A
&SW(1)   SETB  1                                                   000A
.CKPCH   ANOP                                                      000A
.CKPRT   ANOP                                                      000A
         AIF   ('&PRTSZ' EQ '120' OR '&PRTSZ' EQ '132' OR '&PRTSZ' EQ  X
               '144').PRTOK                                        000A
         MNOTE 8,'INVALID PRTSZ. DEFAULT=120 ASSUMED'              000A
&SW(2)   SETB  1                                                   000A
.PRTOK   ANOP                                                      000A
         AIF   (&SW(1)).END                                        000A
         AIF   ('&FEATURE' EQ '2780MR' OR '&FEATURE' EQ 'NONE'         X
               OR '&FEATURE' EQ '2770EB' OR '&FEATURE'                 X
               EQ '').SETMR                                      A29501
         MNOTE 8,'INVALID FEATURE OPERAND NONE ASSUMED'          A29501
         AGO   .TPACK                                              000C
.SETMR   ANOP                                                      000C
         AIF   ('&FEATURE' EQ 'NONE' OR '&FEATURE' EQ '').TPACK  A29501
&SW(5)   SETB  1                                                   000C
.TPACK   ANOP                                                      000C
         AIF   ('&PACK' EQ 'YES' OR '&PACK' EQ 'NO'OR                  X
               '&PACK' EQ '').APAC                                 000C
         MNOTE 8,'INVALID PACK OPERAND PACK=NO ASSUMED'            000C
         AGO   .PTOK                                               000C
.APAC    ANOP                                                      000C
        AIF   ('&PACK' EQ 'NO' OR '&PACK' EQ '').PTOK              000C
         AIF   (&SW(4)).PINV                                       000C
&SW(6)   SETB  1                                                   000C
         AGO   .PTOK                                               000C
.PINV    ANOP                                                      000C
         MNOTE 8,'INVALID MACHINE SPECIFIED FOR PACK'              000C
.PTOK    ANOP                                                      000C
IHKKEN   CSECT                                                     000A
&IHKTMNB SETA  &IHKTMNB+1                                          000A
         AIF   (&IHKSWT).SKIP                                      000A
IHKCDTNB DS    0F                                                  000A
         DC    HL2'&IHKTMNB'       NO OF TERMINALS SUPPORTED. WILL BE
*                                  UPDATED BY SUCCEEDING RJETERM MACROS
.*   ABOVE TWO CARDS CHANGED                                       000A
         ENTRY IHKCDTNB                                            000A
*********************************************************************
*                                                                    *
*                                                                    *
*         THE TERMINAL DIRECTORY                                     *
*        THIS DIRECTORY IS TO MAINTAIN A LIST OF POTENTIAL           *
*   REMOTE TERMINALS AVAILABLE TO THE RJE SYSTEM.                    *
*                                                                    *
*                                                                    *
*********************************************************************
*                                                                    *
.*   ABOVE  CARDS ADDED                                            000A
         AGO   .ARND                                               000A
.SKIP    ANOP                                                      000A
         ORG   *-2                                                 000A
         DC    HL2'&IHKTMNB'                                       000A
.ARND    ANOP                                                      000A
IHKWELL  CSECT                                                     000A
         AIF   (&IHKSWT).BY1                                       000A
IHKCDTLN DC    H'80'   NO OF BYTES PER ENTRY IN TERMINAL DIRECT  A40162
.*                                                                 000A
IHKCDTDR DS    0F                                                  000A
         ENTRY IHKCDTDR,IHKCDTLN                                   000A
&IHKSWT  SETB  1                                                   000A
.BY1     ANOP                                                      000A
&NAME    DC    CL8'&NAME' .        TERMINAL NAME                   000A
         DC    3XL1'00' .          USER IDENTIFICATION             000A
         DC    XL1'00' .           STATUS BYTE                     000A
         AIF   (&SW(5)).SEMR                                       000C
         AIF   (&SW(6)).SEPAC                                      000C
         DC    XL1'00' .           TDIRRB                          000A
         AGO   .CONGO                                              000C
.SEMR    ANOP                                                      000C
         DC    X'04'               TDIRRB                          000C
         AGO   .CONGO                                              000C
.SEPAC   ANOP                                                      000C
         DC    X'08'               TDIRRB                          000C
         AGO   .CONGO                                              000C
.CONGO   ANOP                                                      000C
         DC    3XL1'00' .          TTR OF THIS ENTRY IN DISK COPY  000A
         DC    F'0' .              FIRST TERMINAL QEB              000A
         DC    F'0' .              LAST TERMINAL QEB               000A
         DC    F'0' .              LAST MESSAGE QEB                000A
         DC    CL4' ' .            PRINTER FORM NUMBER             000A
         DC    F'0' .              STOP ACK ECB                    000A
         DC    XL2'00' .           TDIR SWITCHES                   000A
         AIF   (&SW(4)).T2780                                      000A
         AIF   ('&TYPE' EQ '2770').T2780                           000D
         DC    X'40' .        LINE ANALYSIS HARDWARE CONFIGURATION 000A
         AGO   .NEXT                                               000A
.T2780   AIF   ('&PUNCH' EQ 'NO').PCHNO                            000A
         DC    X'00' .        LINE ANALYSIS HARDWARE CONFIGURATION 000A
         AGO   .NEXT                                               000A
.PCHNO   DC    X'20' .        LINE ANALYSIS HARDWARE CONFIGURATION 000A
.NEXT    AIF   ('&PRTSZ' NE '120' AND (NOT &SW(2))).T132           000A
         DC    XL1'78' .           PRINTER SIZE,120                000A
         AGO   .FIN                                                000A
.T132    AIF   ('&PRTSZ' NE '132').T144                            000A
         DC    XL1'84' .           PRINTER SIZE,132                000A
         AGO   .FIN                                                000A
.T144    DC    XL1'90' .           PRINTER SIZE,144                000A
.FIN     ANOP                                                      000A
         AIF   ('&ID' NE '').A1                                    000A
         DC    4F'0' .             RESERVE FOR DFTRMLST            000A
         AGO   .B1                                                 000A
.A1      ANOP                                                      000A
&NUMB    SETA  K'&ID                                               000D
&NUMBR   SETA  10                                                  000D
         AIF   ('&NUMB' GE '&NUMBR').MNTE                          000D
         DC    0F'0'                                               000A
         AIF   ('&TYPE' EQ '1130' OR '&TYPE' EQ '2020').E1130      000C
         AIF   ('&TYPE' EQ '2770').T2770                           000D
IHKK&SYSNDX DFTRMLST AUTOWLST,&CHAR,373737                         000C
         DC    0F'0'                                               000A
         AGO   .B1                                                 000A
.T2770   ANOP                                                      000D
IHKK&SYSNDX   DFTRMLST  AUTOWLST,&CHAR,37373737                    000D
         AGO   .B1                                                 000D
.E1130   ANOP                                                      000A
IHKK&SYSNDX DFTRMLST AUTOWLST,&CHAR,3737                           000A
         DC    F'00'                                               000C
         DC    0F'0'                                               000A
.B1      ANOP                                                      000A
         DC    AL4(0) .            ADDR OF LCB FOR THIS TERMINAL   000A
         DC    8XL1'00'                                            000A
         AIF   ('&ID' NE '').EN1                                   000C
         DC    F'00'                                               000D
         DC    H'00'                                               000D
         AGO   .SKIPA                                              000D
         DC    2F'00'                                              000C
         AGO   .TRIN                                               000C
.EN1     ANOP                                                      000C
IHKE&SYSNDX DFTRMLST OPENLST,&CHAR                                 000A
         AIF   ('&TYPE' EQ '2770').SKIPA                           000D
         DC    H'00'                                               000D
.SKIPA   ANOP                                                      000D
         AIF   ('&TYPE' NE '2770').STRIN                           000D
         DS    0H                                                  000D
         DC    X'00'                                               000D
         AIF   ('&FEATURE' EQ '2770EB').SETT                       000D
         DC    X'01'                                               000D
         AGO   .TRIN                                               000D
.SETT    ANOP                                                      000D
         DC    X'03'                                               000D
         AGO   .TRIN                                               000D
.STRIN   ANOP                                                      000D
         DC    H'00'                                               000D
.TRIN    ANOP                                                      000C
         DC    X'00' .             SAVE AREA FOR VOL SEQ NUM     A40162
         DC    3X'00' .            NOT USED                      A40162
         AIF   (&SW(4)).TRAN                                       000C
         AIF   ('&TYPE' EQ '2780' OR '&TYPE' EQ '2770').TRAN     A32856
         AGO   .IHKPK                                              000C
.TRAN    ANOP                                                      000C
         AIF   (&IHKTRB).IHKPK                                   A61477
         ENTRY IHKTRTAB                                          A61477
&IHKTRB  SETB  1                                                   000C
IHKTRTAB CSECT                                                     000C
*IHKTRTAB                     ***DATA LINK ZAP TRANSLATE TABLE     000C
         DC    X'004040400440060708090A0B0C0D0E0F'               A29510
         DC    X'401112131415161718191A1B1C1D1E40'                 000C
         DC    X'202122232425404028292A2B2C402E2F'                 000C
         DC    X'303140333435364038393A3B3C403E3F'                 000C
         DC    X'404142434445464748494A4B4C4D4E4F'                 000C
         DC    X'505152535455565758595A5B5C5D5E5F'                 000C
         DC    X'606162636465666768696A6B6C6D6E6F'                 000C
         DC    X'407172737475767778797A7B7C7D7E7F'               A28213
         DC    X'808182838485868788898A8B8C8D8E8F'                 000C
         DC    X'909192939495969798999A9B9C9D9E9F'                 000C
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'                 000C
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                 000C
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'                 000C
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'                 000C
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                 000C
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                 000C
.IHKPK   ANOP                                                      000C
IHKTRTAB CSECT                                                     000C
IHKAPACK CSECT                                                     000C
*******************************************************************000C
*        THIS ROUTINE GENERATES THE V-CONS NECESSARY TO MAKE THE   000C
*        PACK ROUTINES RESIDENT IF SPECIFIED.  IF NOT IT GENERATES 000C
*        AN ENTRY POINT TO RESOLVE THE EXTERNAL REFERENCES.        000C
*******************************************************************000C
         AIF   (&IHKENT).IHKNT                                     000C
&IHKENT  SETB  1                                                   000C
         ENTRY IHKADPCK                                            000C
         ENTRY IHKADUPK                                            000C
.IHKNT   ANOP                                                      000C
         AIF   (&SW(6)).SPAC                                       000C
         AIF   (&IHKPU).END                                        000C
&IHKPU   SETB  1                                                   000C
IHKADPCK EQU   *                                                   000C
IHKADUPK EQU   *+4                                                 000C
         AGO   .END                                                000C
.SPAC    ANOP                                                      000C
         AIF   (&IHKPU).IHKP                                       000C
&IHKPU   SETB  1                                                   000C
IHKADPCK EQU   *                                                   000C
IHKADUPK EQU   *+4                                                 000C
.IHKP    ANOP                                                      000C
         AIF   (&IHKUP).END                                        000C
&IHKUP   SETB  1                                                   000C
         DC    V(IHKCHPCK)                                         000C
         DC    V(IHKCHUPK)                                         000C
         AGO   .END                                                000D
.MNTE    ANOP                                                      000D
         MNOTE  8,'ADDRESSING CHARACTERS EXCEED K ASSEMBLY             X
               TERMINATED'                                         000D
.END     MEND
