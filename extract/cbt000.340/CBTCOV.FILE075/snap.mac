         MACRO
&NAME    SNAP  &DCB=,&TCB=,&SDATA=,&PDATA=,&STORAGE=,&LIST=,&ID=,&MF=
.*00130290,0404                                                    5139
.*3530063600                                                       2607
         LCLC  &C1,&C2,&GNAME
         LCLB  &B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7,&B8,&B9,&B10,&B11,&B12,&X
               B13,&B14,&B15,&B16
         LCLA   &FLAGS1,&FLAGS2,&CTR1,&CTR2,&DISP
&B0      SETB  1
&B1      SETB  ('&TCB' NE '')
         AIF   ('&SDATA' EQ '').ABC7
         AIF    ('&SDATA'(1,1) EQ '(').ABC2
&B2      SETB  ('&SDATA' EQ 'ALL' OR '&SDATA' EQ 'CB')
&B3      SETB  ('&SDATA' EQ 'ALL' OR '&SDATA' EQ 'TRT')
&B4      SETB  ('&SDATA' EQ 'ALL' OR '&SDATA' EQ 'NUC')
&B7      SETB  ('&SDATA' EQ 'ALL' OR '&SDATA' EQ 'Q')
&CTR1    SETA  &B2+&B3+&B4+&B7
         AIF   ('&CTR1' EQ '0').MSG2
         AGO   .ABC7
.ABC2    ANOP
&CTR1    SETA  N'&SDATA
&CTR2    SETA  0
.LOOPA   ANOP
&CTR2    SETA  &CTR2+1
&C1      SETC  '&SDATA(&CTR2)'
         AIF   ('&C1' EQ 'TRT').ABC4
         AIF   ('&C1' EQ 'NUC').ABC5
         AIF   ('&C1' EQ 'CB').ABC6
         AIF   ('&C1' EQ 'Q').ABC67
         AIF   ('&C1' EQ 'ALL').SETALL
         AGO   .MSG2
.ABC3    ANOP
         AIF   ('&CTR2' NE '&CTR1').LOOPA
         AGO   .ABC7
.ABC4    ANOP
&B3      SETB  1
         AGO   .ABC3
.ABC5    ANOP
&B4      SETB  1
         AGO   .ABC3
.ABC6    ANOP
&B2      SETB  1
         AGO   .ABC3
.ABC67   ANOP
&B7      SETB  1
         AGO   .ABC3
.SETALL  ANOP
&B2      SETB  1
&B3      SETB  1
&B4      SETB  1
&B7      SETB  1
.ABC7    ANOP
&B5      SETB  ('&STORAGE' NE '')
&B16     SETB  ('&LIST' NE '')
         AIF   ('&B5' EQ '1' AND '&B16' EQ '1').MSG4
         AIF   ('&B5' EQ '0').ABC7B
&CTR1    SETA  N'&STORAGE
         AIF   ('&CTR1' LT '2').MSG4
.ABC7A   ANOP
&CTR1    SETA  &CTR1-2
         AIF   ('&CTR1' EQ '0').ABC7C
         AIF   ('&CTR1' LT '1').MSG4                               0880
         AGO   .ABC7A
.ABC7B   ANOP
         AIF   ('&B16' EQ '0').ABC7C
         AIF   (N'&LIST NE 1).MSG4
.ABC7C   ANOP
&B6      SETB  ('&ID' NE '')
         AIF   ('&PDATA' EQ '').ABC18
         AIF   ('&PDATA'(1,1) EQ '(').ABC8
&B8 SETB ('&PDATA' EQ 'SAH' OR '&PDATA' EQ 'SA' OR '&PDATA' EQ 'ALL')
&B9      SETB  ('&PDATA' EQ 'SAH')
&B10      SETB  ('&PDATA' EQ 'REGS' OR '&PDATA' EQ 'ALL')
&B11 SETB ('&PDATA' EQ 'LPA' OR '&PDATA' EQ 'ALLPA' OR '&PDATA' EQ 'ALLX
               ')
&B12 SETB ('&PDATA' EQ 'JPA' OR '&PDATA' EQ 'ALLPA' OR '&PDATA' EQ 'ALLX
               ')
.PDATA1  ANOP
&B13     SETB  ('&PDATA' EQ 'PSW' OR '&PDATA' EQ 'ALL')
&B14     SETB  ('&PDATA' EQ 'SPLS' OR '&PDATA' EQ 'ALL')
&CTR1    SETA  &B8+&B9+&B10+&B11+&B12+&B13+&B14
         AIF   ('&CTR1' EQ '0').MSG3
&B15     SETB  0
         AGO   .ABC18
.ABC8    ANOP
&CTR1    SETA  N'&PDATA
&CTR2    SETA  0
.LOOPB   ANOP
&CTR2    SETA  &CTR2+1
&C1      SETC  '&PDATA(&CTR2)'
         AIF   ('&C1' EQ 'SA').ABC10
         AIF   ('&C1' EQ 'SAH').ABC11
         AIF   ('&C1' EQ 'REGS').ABC12
         AIF   ('&C1' EQ 'LPA').ABC13
         AIF   ('&C1' EQ 'JPA').ABC14
         AIF   ('&C1' EQ 'ALLPA').ABC15
         AIF   ('&C1' EQ 'PSW').ABC16
         AIF   ('&C1' EQ 'SPLS').ABC17
         AIF   ('&C1' EQ 'ALL').ABC17A
         AGO   .MSG3
.ABC9    ANOP
         AIF   ('&CTR2' NE '&CTR1').LOOPB
         AGO   .ABC18
.ABC10    ANOP
&B8      SETB  1
         AGO   .ABC9
.ABC11   ANOP
&B8      SETB    1
&B9      SETB  1
         AGO    .ABC9
.ABC12    ANOP
&B10     SETB  1
         AGO   .ABC9
.ABC13   ANOP
&B11     SETB  1
         AGO   .ABC9
.ABC14   ANOP
&B12     SETB   1
         AGO   .ABC9
.ABC15    ANOP
&B11     SETB  1
&B12     SETB  1
         AGO   .ABC9
.ABC16    ANOP
&B13     SETB  1
         AGO   .ABC9
.ABC17    ANOP
&B14     SETB   1
         AGO   .ABC9
.ABC17A  ANOP
&B8      SETB  1
&B10     SETB  1
&B11     SETB  1
&B12     SETB  1
&B13     SETB  1
&B14     SETB  1
.ABC18    ANOP
&FLAGS1  SETA  128*&B0+64*&B1+32*&B2+16*&B3+8*&B4+4*&B5+4*&B16+2*&B6+1*X
               &B7
&FLAGS2  SETA  128*&B8+64*&B9+32*&B10+16*&B11+8*&B12+4*&B13+2*&B14+1*&BX
               15
         AIF   ('&DCB' EQ '' AND ('&MF' EQ '')).MSG1
         AIF   ('&MF' EQ '').STYPE
         AIF   ('&MF' EQ 'L').LTYPE
         AIF   ('&MF'(1,3) NE '(E,').MSG5
         AIF ('&NAME' EQ '').NONM
&NAME    DS    0H                                                  0918
.NONM    ANOP
         AIF   ('&MF(2)'(1,1) EQ '(').REG                         28406
         LA    1,&MF(2)                                            0918
         AGO   .CONT0
.REG     ANOP
         AIF   ('&MF(2)' EQ '(1)').CONT0                         A28406
&C1      SETC  '&MF(2)' (2,K'&MF(2)-2)                           A28406
         LR    1,&C1         GET ADDR OF PAR LIST
.CONT0   ANOP
         AIF   ('&DCB' EQ '').NODCB
         AIF   ('&DCB'(1,1) EQ '(').REGDCB
         LA    0,&DCB        GET DCB ADDRESS
         ST    0,4(0,1)      STORE INTO PARAMETER LIST
         AGO    .NODCB
.REGDCB  ANOP
&C1      SETC  '&DCB'(2,K'&DCB-2)  STRIP PARENS                  A28406
         ST    &C1,4(0,1)          STORE DCB ADDRESS             A28406
.NODCB   ANOP
         AIF   (('&FLAGS1' EQ '128') AND ('&FLAGS2' EQ '0')).NOID 60518
         AIF   ((('&FLAGS1' EQ '194') AND ('&FLAGS2' EQ '0'))  OR      X
               (('&FLAGS1' EQ '130') AND ('&FLAGS2' EQ '0'))).TCBID
         AIF   (('&FLAGS1' EQ '192') AND ('&FLAGS2' EQ '0')).TCBID2
         MVI   2(1),&FLAGS1       SET OPTION FLAGS
         MVI   3(1),&FLAGS2       SET OPTION FLAGS
         AGO   .SKAL1
.TCBID   ANOP
         OI    2(1),&FLAGS1       SET OPTION FLAGS
.TCBID1  ANOP
         AIF   ('&TCB' NE '''S''').SKAL1
         NI    2(1),X'BF'     SPECIFY CURRENT TCB
         AGO   .CTCB
.SKAL1   ANOP
         AIF   ('&B1' EQ '0').CTCB
         AIF   ('&TCB' EQ '''S''').TCBID1                     AB A39593
         AIF   ('&TCB'(1,1) EQ '(').REGTCB
         L     0,&TCB        GET TCB ADDRESS
         ST    0,8(0,1)      STORE INTO PARAMETER LIST
         AGO   .CTCB
.TCBID2  ANOP
         AIF   ('&TCB' EQ '''S''').TCBID1
         OI    2(1),&FLAGS1     SET OPTION FLAGS
         AGO   .SKAL1
.REGTCB    ANOP
&C1      SETC  '&TCB'(2,K'&TCB-2)  STRIP PARENS                  A28406
         ST    &C1,8(0,1)         STORE TCB ADDRESS              A24806
.CTCB    ANOP
         AIF   ('&B5' EQ '0').NOSHOT
&CTR1    SETA  N'&STORAGE
         LA    0,16(0,1)           SET UP SNAPSHOT LIST ADDRESS
         ST    0,12(0,1)
.DPA1    ANOP
&CTR2    SETA   0
&DISP    SETA  12
.LOOP1   ANOP
&CTR2    SETA  &CTR2+1
&DISP    SETA  &DISP+4
&C1      SETC  '&STORAGE(&CTR2)'
         AIF   ('&STORAGE(&CTR2)'(1,1) EQ '(').STOR1
         AIF   ('&MF' EQ '').AIF1
         LA    0,&STORAGE(&CTR2)   GET STARTING/ENDING ADDRESS     5139
         ST    0,&DISP.(0,1)
         AGO   .AIF1
.STOR1   ANOP
&C1      SETC  '&STORAGE(&CTR2)'(2,K'&STORAGE(&CTR2)-2)         A28406
         ST    &C1,&DISP.(0,1)          GET STARTING/ENDING ADDRESS
.AIF1    ANOP
         AIF   ('&CTR2' NE '&CTR1').LOOP1
         MVI   &DISP.(1),X'80'       SET END OF LIST INDICATOR
         AIF   ('&MF' EQ '').HOCH
.NOSHOT  ANOP
         AIF   ('&B16' EQ '0').IDPREP
         AIF   ('&LIST'(1,1) EQ '(').LL1
         LA    0,&LIST       GET ADDRESS OF SNAPSHOT LIST
         ST    0,12(0,1)     STORE IN PARAMETER LIST
         AGO   .IDPREP
.LL1     ANOP
&C1      SETC  '&LIST'(2,K'&LIST-2)                              A28406
         ST  &LIST,12(0,1)        STORE SNAPSHOT LIST ADDR IN PARM LIST
.IDPREP  ANOP
         AIF   ('&B6' EQ '0').NOID
         AIF   ('&ID'(1,1) EQ '(').IDPREP1
         MVI   0(1),&ID            GET ID NUMBER
         AGO    .NOID
.IDPREP1  ANOP
&C1      SETC  '&ID'(2,K'&ID-2)                                  A28406
         STC   &ID,0(1)         GET ID NUMBER
.NOID    ANOP
         SVC   51
         MEXIT
.STYPE   ANOP
&GNAME   SETC  'IHB'.'&SYSNDX'
         CNOP  0,4
&NAME    BAL   1,&GNAME            BRANCH AROUND PARAM LIST
.LSTYPE  ANOP
         AIF   ('&B6' EQ '0').NOIDS
         AIF   ('&ID'(1,1) EQ '(').NOIDS
         DC    AL1(&ID)            ID NUMBER
         AGO   .CONT2
.NOIDS   ANOP
         DC    AL1(0)           ID NUMBER
.CONT2   ANOP
         DC    AL1(0)
         DC    AL1(&FLAGS1)        OPTION FLAGS
         DC    AL1(&FLAGS2)        OPTION FLAGS
         AIF    ('&DCB' EQ '').DCBA0
         AIF   ('&DCB'(1,1) EQ '(').DCBA0                          9943
         DC    A(&DCB)             DCB ADDRESS
         AGO   .DCBA1
.DCBA0   ANOP
         DC    A(0)          DCB ADDRESS
.DCBA1    ANOP
         DC    A(0)                TCB ADDRESS
         AIF   ('&B5' EQ '0' AND '&B16' EQ '0').SS3
         AIF   ('&B5' EQ '1').SS1
         AIF   ('&LIST'(1,1) EQ '(').SS3
         DC    A(&LIST)      ADDRESS OF SNAPSHOT LIST
         AGO   .CONT8
.SS1     ANOP
&CTR1    SETA  N'&STORAGE
         AGO   .SS2
.SS3     ANOP
         DC    A(0)                ADDRESS OF SNAP-SHOT LIST
         AGO   .CONT8
.SS2     ANOP
         DC    A(*+4)              ADDRESS OF SNAP-SHOT LIST
&CTR2    SETA  0
.LOOP3   ANOP
&CTR2    SETA  &CTR2+1
         AIF   ('&STORAGE(&CTR2)' EQ '').STYPE3
         AIF   ('&STORAGE(&CTR2)'(1,1) EQ '(').STYPE2
         DC    A(&STORAGE(&CTR2))     STARTING/ENDING ADDRESS
.STYPE1  ANOP
         AIF   ('&CTR2' NE '&CTR1').LOOP3
.CONT8   ANOP
         AIF   ('&MF' EQ 'L').EXIT
&GNAME   DS    0H
         AIF   ('&B6' EQ '0').HOCH0
         AIF   ('&ID'(1,1) NE '(').HOCH0
&C1      SETC  '&ID'(2,K'&ID-2)                                 A28406
         STC   &C1,0(1)         GET ID NUMBER                   A28406
.HOCH0    ANOP
         AIF   ('&B5' EQ '0').HOCH1
         AGO   .DPA1
.HOCH    ANOP
         AIF   ('&B1' EQ '0').YYY                                  9943
         AIF   ('&TCB'(1,1) EQ '(').XXX
         L     0,&TCB        GET TCB ADDRESS
         ST    0,8(0,1)      STORE IN PARAMETER LIST
         AGO   .YYY                                                9943
.HOCH1   ANOP
         AIF   ('&B16' EQ '0').HOCH
         AIF   ('&LIST'(1,1) EQ '(').XXX2
         AIF   ('&MF' EQ '').HOCH
         LA    0,&LIST             GET ADDRESS OF SNAPSHOT LIST
         ST    0,12(0,1)           STORE INTO PARAM LIST
         AGO   .HOCH
.XXX2    ANOP
&C1      SETC '&LIST'(2,K'&LIST-2)                               A28406
         ST    &C1,12(0,1)       STORE SNAPSHOT ADDRESS INTO PARAM
         AGO   .HOCH
.STYPE2  ANOP
         AIF   ('&MF' EQ 'L').MSG4
         DC    A(0)          STARTING/ENDING ADDRESS
         AGO   .STYPE1
.STYPE3   ANOP
         AIF   ('&MF' EQ '').MSG4
         DC    A(0)         STARTING/ENDING ADDRESS
          AGO   .STYPE1
.XXX     ANOP
&C1      SETC  '&TCB'(2,K'&TCB-2)                                A28406
         ST    &C1,8(0,1)         STORE ADDRESS OF TCB INTO PARAM LIST
.YYY     AIF   ('&DCB'(1,1) NE '(').SVC51                          9943
&C1      SETC  '&DCB'(2,K'&DCB-2)                                A28406
         ST    &C1,4(0,1)          STORE DCB ADDR IN PARM LIST    9943
.SVC51   ANOP
         SVC   51
         AGO   .EXIT1
.EXIT    ANOP
         AIF   ('&STORAGE' EQ '').EXIT1
         ORG   *-4
         DC    X'80'
         ORG   *+3
.EXIT1   ANOP
         MEXIT
.LTYPE   ANOP
         AIF   ('&TCB' NE '').MSG6                                 2607
         AIF   ('&STORAGE' EQ '').LTYPE1
         AIF   ('&STORAGE'(2,1) EQ '(').MSG4
         AGO   .LTYPE2
.LTYPE1  ANOP
         AIF   ('&LIST' EQ '').LTYPE2
         AIF   ('&LIST'(1,1) EQ '(').MSG4
.LTYPE2  ANOP
&NAME    DS    0F
         AGO   .LSTYPE
.MSG1    IHBERMAC  6
         MEXIT
.MSG2    ANOP
&B2      SETB  1
&B3      SETB  0
&B4      SETB  0
&B7      SETB  0
         IHBERMAC  183,&SDATA,CB
         AGO   .ABC7
.MSG4    IHBERMAC  189
         MEXIT
.MSG3    ANOP
&B8      SETB  1
&B9      SETB    0
&B10     SETB  1
&B11     SETB  1
&B12     SETB  1
&B13     SETB  1
&B14     SETB  1
         IHBERMAC  183,&PDATA,ALL
         AGO   .ABC18
.MSG5    IHBERMAC  35,,&MF
         MEXIT
.MSG6    IHBERMAC   197                                            2607
         MEND
