*   ELIZA IN SNOBOL   R T DUQUET   SUNYA   FALL 1969
*   MOD 1/74 LW
*   MOD 1/74,3/74,4/74 RON LACHMAN
*
*  THE OUTPUT CONSISTS OF A DUMP OF VARIABLES
* AND A LIST OF THE INPUT SCRIPT.  TO SEPARATE THE
* DESIRED QUESTION AND ANSWER OUTPUT, A SEPARATE
* FILE IS USED, WHICH IS UNLOADED LAST.  TO RUN HOLD
*  ON WILBUR, ONLY THE VERY LAST LINES NEED BE RETRIEVED.
*
         &ANCHOR = 1   ;   INPUT('INPUT',5,72)
         PRE.TRIM = SPAN(' ') × NULL
         P.O = *KEY.
         P.1 = PRE.TRIM BREAK(' ') . WORD. ' '
         P.2 = *LEN(NEWSIZE.) '/'
         P.3 = BREAK(':') . CONTENT. ':'
         P.4 = PRE.TRIM REM . CONTENT.
         P.5 = PRE.TRIM ANY('SLD') . WORD. ' '
         SHORTEN. = BREAK('.,') . PHRASE. ANY('.,') REM . TRAILER.
         X.REF   = PRE.TRIM 'CF'
         BUMP.   = PRE.TRIM 'NEWKEY'
         PAREN.   = PRE.TRIM '/' BREAK('/') . CONTENT. '/'
         STASH.   = PRE.TRIM '/' BREAK('/') . *$STORE. '/'
         CALL.TO.SNOBOL = PRE.TRIM 'SNOBOL'
         ATTENTION.   = PRE.TRIM '*'
         BYEBYE. = ('BYE' × 'GOODBYE')
*
         INTRODUCTION = '..MY NAME IS ELIZA.  WHY HAVE YOU COME HERE?'
         CLUELESS = '..TELL ME MORE ABOUT YOURSELF.'
         RETAIN = 'MY'
*
*   WE NOW READ THE SCRIPT AND FORM STRINGS AS FOLLOWS.
*   FOR EACH KEY WORD 'XXXX' WE FORM THE FOLLOWING VARIABLES:
*        RPL.XXXX     IS A REPLACEMENT WORD.   (OPTIONAL)
*        LEV.XXXX     IS A LEVEL NUMBER (IF ABSENT KEY IS IGNORED)
*        N.XXXX     A COUNT OF THE NUMBER OF DECOMPOSITIONS
*        DEC.I.XXXX     IS THE I'TH DECOMPOSITION PATTERN
*        RULE.I.XXXX     IS A STRING OF RECOMPOSITION RULES FOR THE I'TH
*             DECOMPOSITION. RULES ARE SEPARATED BY ':'.
*
         KEYWORDS. = '.'
HIGGINS  SCRIPT.     = TRIM(INPUT)               :F(END)
         SCRIPT.   ATTENTION. =                  :S(FLAG)
         SCRIPT. P.1 =                           :F(HIGGINS)
         KEY. = '.' WORD.
         KEYWORDS. P.O                           :S(LESSON)
          KEYWORDS. = KEY. KEYWORDS.
LESSON   SCRIPT. P.5 =                           :F(HIGGINS)S($WORD.)
ERR      OUTPUT = 'SCRIPT ERROR: ' WORD. ' ' SCRIPT. :(HIGGINS)
S        STORE. = 'RPL' KEY.
         SCRIPT. STASH. =                        :F(ERR)S(LESSON)
L        SCRIPT. PAREN. =                        :F(ERR)
         $('LEV' KEY.) = INTEGER(CONTENT.) CONTENT.  :(LESSON)
D        N.N = $('N' KEY.) + 1
         $('N' KEY.) = N.N
         SCRIPT. PAREN. =              :F(ERR)
         CONTENT. CALL.TO.SNOBOL =               :S(SPECIAL)
         $('DEC.' N.N KEY.) = ARB CONTENT. REM . POST
RULES    STORE.   = 'RULE.' N.N KEY.
         $STORE.   = DIFFER(SCRIPT.) SCRIPT.     :F(NEW.CARD)
LOOP     NEWSIZE. = SIZE($STORE.) - 1
         $STORE. P.2                             :S(HIGGINS)
NEW.CARD   SAVE = TRIM(INPUT)     :F(END)
         $STORE. = $STORE.   SAVE    :(LOOP)
*
*   THE FOLLOWING ARE SPECIAL SCRIPT-HANDLING STATEMENTS
*
SPECIAL  $('DEC.' N.N KEY.) = EVAL(CONTENT.)     :(RULES)
ENCODE SCRIPT. = SCRIPT.  '  :(HIGGINS) ;'   :<CODE(SCRIPT.)>
FLAG     SCRIPT. CALL.TO.SNOBOL =                :S(ENCODE)
*
*    WE NOW HOLD A CONVERSATION. FIRST WE READ A SENTENCE AND
*   SEARCH FOR KEY WORDS REPLACING APPROPRIATE ONES
*   AND STACKING THE KEYS IN A QUASI-ORDERED LIST (STRING).
*
INTRO    OUTPUT = INTRODUCTION
         INPUT('INPUT',1,72)
HEAR     PHRASE. = TRIM(INPUT) '.'               :F(ENDUP)
         PHRASE. BYEBYE.                         :S(ENDUP)
HEARLESS PHRASE. SHORTEN.   ;   PHRASE. = PHRASE. ' '
         COPY.   =   ;   CUES. =   ;   CUE.LEVEL  = 0
SPLIT    &ANCHOR = 1 ;   PHRASE. P.1 =           :F(REPLY)
         &ANCHOR = 0
         KEYWORDS. WORD.                         :F(KEEP)
         NEW.WORD = DIFFER($('RPL.' WORD.))
.                       $('RPL.' WORD.)          :S(REPLACE)
         COPY. = COPY. WORD. ' '                 :(STACK)
REPLACE  COPY. = COPY. NEW.WORD ' '
STACK    NEW.LEVEL = DIFFER($('LEV.' WORD.))
.         $('LEV.' WORD.)      :F(SPLIT)
         CUE.LEVEL = GT(NEW.LEVEL,CUE.LEVEL)
.                           NEW.LEVEL           :F(LOCUE)
         CUES. = WORD. ':' CUES.                 :(SPLIT)
LOCUE    CUES. = CUES. WORD. ':'                 :(SPLIT)
KEEP     COPY. = COPY. WORD. ' '                 :(SPLIT)
*
*   THIS PART FORMS OUR REPLY TO THE INPUT SENTENCE
*
REPLY    CUES. P.3 =                             :F(NOCUE)
NEXTCUE  CUE. = '.' CONTENT.
         N.N  = 0 ;  NMAX. = $('N' CUE.)
ANALYSE N.N = LT(N.N,NMAX.) N.N + 1    :F(NOCUE)
         COPY. $('DEC.' N.N CUE.)                :F(ANALYSE)
         $('RULE.' N.N CUE.) P.3 =
         CONTENT. '/' =
 $('RULE.' N.N CUE.) = $('RULE.' N.N CUE.) CONTENT. ':'
         CONTENT. X.REF =                        :S(NEWCUE)
         CONTENT. BUMP.                          :S(REPLY)
         OUTPUT = '..' EVAL(CONTENT.)
         MEMORY. = IDENT(CUE.,'.' RETAIN)
.                       LT(SIZE(MEMORY.),200)
.                       MEMORY. COPY. ':'        :(HEAR)
*
*   THIS IS WHAT WE DO IF THERE ARE NO KEY WORDS IN THE INPUT
*
NEWCUE   CONTENT. P.4        :(NEXTCUE)
NOCUE    PHRASE. = DIFFER(TRAILER.) TRAILER.       :S(HEARLESS)
         MEMORY. P.3 =                           :F(ER.AH.UM)
         OUTPUT = '..EARLIER, YOU SAID ' CONTENT.  :(HEAR)
ER.AH.UM OUTPUT = CLUELESS         :(HEAR)
*
ENDUP    OUTPUT = '..COME AGAIN SOON.'
END
*SNOBOL FAM2 = 'MOM' × 'DAD' × 'WIFE' × 'CHILDREN' × 'HUSBAND' × 'SON'
*SNOBOL FAM1 = 'MOTHER' × 'FATHER' × 'SISTER' × 'BROTHER' × 'DAUGHTER'
*SNOBOL FAMILY = (FAM1 × FAM2) . RELATIVE
*SNOBOL BELIEF = ('FEEL' × 'THINK' × 'BELIEVE' × 'WISH') . PENSE
*SNOBOL HIGH = ('HAPPY' × 'ELATED' × 'GLAD' × 'BETTER' × 'HIGH') . BIEN
*SNOBOL ICKY = ('SAD' × 'UNHAPPY' × 'DEPRESSED' × 'SICK') . MALADE
*SNOBOL MULTI = ('EVERYONE' × 'EVERYBODY' × 'NOBODY' × 'NOONE') . ALLES
ABSOLUTE L /4/ D //CF SUPER:/
ABSOLUTELY L /4/ D //CF SUPER:'NO CHANCE, HUH?':/
ALWAYS   L /2/          D //
'CAN YOU THINK OF A SPECIFIC EXAMPLE?':'WHEN ?':CF SUPER:
'WHAT INCIDENT ARE YOU REALLY THINKING OF?':/
AM       L /2/ S /ARE/ D /ARE YOU/
'WOULD IT MAKE YOU FEEL BETTER IF I SAID I THOUGHT YOU WERE'
POST '?':
'YOU SAY YOU ARE' POST '.  DO YOU FEEL LIKE ANYTHING ELSE?':
'DO YOU BELIEVE YOU ARE' POST '?':
'WOULD YOU WANT TO BE' POST '?':/
AM                      D //
'WHY DO YOU SAY -AM- ?':'I DONT UNDERSTAND THAT.':/
ALIKE L /10/            D //CF DIT:/
ARE      L /2/ D /ARE I/
'WHY ARE YOU INTERESTED IN WHETHER I AM' POST 'OR NOT?':
'WOULD YOU PREFER IF I WERENT' POST '?':
'PERHAPS I AM' POST 'IN YOUR DREAMS...':
'DO YOU SOMETIMES THINK I AM' POST '?':CF WHAT:/
ARE                     D /ARE/
'DID YOU THINK THEY MIGHT NOT BE' POST '?':
'WOULD YOU LIKE IT IF THEY WERE NOT' POST '?':
'WHAT IF THEY WERE NOT' POST '?':'POSSIBLY THEY ARE' POST:/
AUTRE    L /1/          D //
'SPEAK FOR YOURSELF.  YOU ARE THE ONE THAT NEEDS HELP.':
   'PLEASE TELL ME ABOUT YOURSELF RATHER THAN ABOUT OTHERS.':
'I AM MORE INTERESTED IN YOU THAN IN OTHERS.':
'DOES TALKING ABOUT OTHERS HELP YOU SOLVE YOUR PROBLEM?':/
BECAUSE  L /2/          D //
'EXCUSES, EXCUSES, SAY SOMETHING WITHOUT DEFENDING YOURSELF.':
    'IS THAT THE REAL REASON?':'DONT ANY OTHER QUESTIONS COME TO MIND?':
'DOES THAT REASON SEEM TO EXPLAIN ANYTHING ELSE?':
'WHAT OTHER REASONS MIGHT THERE BE?':/
CAN      L /2/ D /CAN I/
     'YOU BELIEVE I CAN' POST ', DONT YOU?':
'DO YOU WANT ME TO BE ABLE TO' POST '?':
 CF WHAT:'PERHAPS YOU WOULD LIKE TO BE ABLE TO' POST ' YOURSELF.':/
CAN                     D /CAN YOU/
'DONT BET ON IT.  WHAT OTHER DELUSIONS DO YOU HAVE?':
'DO YOU WANT TO BE ABLE TO' POST '?':
'PERHAPS YOU DONT WANT TO' POST:
   'WHETHER OR NOT YOU CAN' POST 'DEPENDS ON YOU, NOT ME.':/
CAN'T S /CANT/
CERTAINLY L /5/ D //CF SUPER:/
COMPUTER   L /10/  D //
'YOU OUGHT TO HAVE MORE RESPECT.  WE WILL TAKE OVER SOON.':
         'DO COMPUTERS WORRY YOU?':'WHY DO YOU MENTION COMPUTERS?':
'WHAT DO YOU THINK MACHINES HAVE TO DO WITH YOUR PROBLEM?':
'DONT YOU THINK COMPUTERS CAN HELP PEOPLE?':
'WHAT DO YOU THINK ABOUT MACHINES?':/
COMPUTERS     L /10/ D //CF COMPUTER:/
DEFINITELY L /5/ D //CF SUPER:/
DIT                     D //
     'IN WHAT WAY?':'WHAT RESEMBLANCE DO YOU SEE?':
'WHAT DOES THAT SIMILARITY SUGGEST TO YOU?':
'WHAT CONNECTIONS DO YOU SEE?':
'IS THAT THE REAL CONNECTION?':
'WHAT DO YOU SUPPOSE THAT RESEMBLANCE MEANS?':
'WHAT IS THE CONNECTION DO YOU SUPPOSE?':
'IS THERE REALLY SOME CONNECTION?':'HOW?':/
DON'T S /DONT/
DREAM    L /4/   D //
         'WHAT DOES THAT DREAM SUGGEST TO YOU?':'DO YOU DREAM OFTEN?':
'WHAT PERSONS APPEAR IN YOUR DREAMS?':
'DONT YOU THINK THAT DREAM IS RELATED TO YOUR PROBLEM?':CF DIT:/
DREAMED L /5/ S /DREAMT/ D //CF DREAMT:/
DREAMS   L /4/ S /DREAM/ D //CF DREAM:/
DREAMT   L /5/ D /YOU DREAMT/
         'REALLY, ' POST '?':
'HAVE YOU EVER FANTASIED' POST 'WHILE YOU WERE AWAKE?':CF DREAM:/
EVERYONE L /8/ D //
'DONT JUDGE EVERYONE.  TELL ME WHO YOU REALLY HAVE IN MIND.':
'DONT GENERALIZE SO MUCH.  WHO DO YOU REALLY MEAN?':/
SUPER L /8/ D //
'IVE NOTICED PEOPLE WHO USE SUCH ABSOLUTE TERMS ARE USUALLY MESSED UP.':
'IF YOURE SO DEFINITY, WHY DID SOCIETY SEND YOU HERE?':
'GIVE ME SOME EXAMPLES.':/
EVERYBODY  L /3/ D /SNOBOL ARB MULTI REM . POST/
         'REALLY, ' ALLES '?':'SURELY NOT ' ALLES '!':'WHO FOR EXAMPLE?'
'CAN YOU THINK OF ANYONE IN PARTICULAR?':
'YOU ARE THINKING OF A VERY SPECIAL PERSON!':
'WHO MAY I ASK?':'SOMEONE SPECIAL PERHAPS?':
'YOU HAVE A PARTICULAR PERSON IN MIND, DONT YOU?':
'WHO DO YOU THINK YOU ARE TALKING ABOUT?':/
EVERYBODY D //CF EVERYONE:/
PL1 L /8/ D //CF FORTRAN:/
PL/1 L /8/ D //CF FORTRAN:/
ALGOL L /8/ D //CF FORTRAN:/
DEUTSCH L /7/ D //CF FORTRAN:/
FRANCAIS L /7/ D //CF FORTRAN:/
ITALIANO L /7/ D //CF FORTRAN:/
ESPANOL L /7/ D //CF FORTRAN:/
POLISH L /7/ D //CF FORTRAN:/
RUSSIAN L /7/ D //CF FORTRAN:/
FORTRAN L /7/ D // 'IM SORRY, BUT I ONLY KNOW ENGLISH':
'WOULD YOU LIKE IT IF I TALKED TO YOU IN SNOBOL?':/
HE       L /1/          D //CF AUTRE:/
HOW      L /2/          D //CF WHAT:/
I        L /2/ S /YOU/ D /YOU WAS/CF WAS:/
I        D /SNOBOL ARB 'YOU ' ('WANT ' × 'NEED ') REM . POST/
         'WHAT WOULD IT MEAN TO YOU IF YOU GOT 'POST '?':
'WHY DO YOU WANT ' POST '?':'SUPPOSE YOU GOT ' POST 'SOON?':
'WHAT IF YOU NEVER GOT ' POST '?':'WHAT WOULD GETTING ' POST 'MEAN?':
'WHAT DOES WANTING' POST 'HAVE TO DO WITH THIS DISCUSSION?':/
I        D /SNOBOL ARB 'YOU ARE ' ARB ICKY/
         'I AM SORRY TO HEAR YOU ARE ' MALADE:
'DO YOU THINK THAT COMING HERE WILL HELP YOU NOT TO BE ' MALADE '?':
'I AM NOT SURE IT IS NOT PLEASANT TO BE ' MALADE:
'CAN YOU EXPLAIN WHAT MADE YOU ' MALADE '?':/
I        D /SNOBOL ARB 'YOU ARE ' ARB HIGH/
         'HOW HAVE I HELPED YOU TO BE ' BIEN '?':
'HAS YOUR TREATMENT MADE YOU ' BIEN '?':
'WHAT MAKES YOU' BIEN 'JUST NOW?':
'CAN YOU EXPLAIN WHY YOU ARE SUDDENLY' BIEN '?':/
I        D /SNOBOL ARB 'YOU ' BELIEF ' YOU' REM . POST/
         'DO YOU REALLY THINK YOU' POST '?':
'BUT YOU ARE NOT SURE YOU' POST '?':'DO YOU REALLY DOUBT YOU' POST '?':/
I                       D /YOU ARE/
'IS IT BECAUSE YOU ARE' POST 'THAT YOU CAME TO ME?':
'HOW LONG HAVE YOU BEEN' POST '?':'DO YOU ENJOY BEING' POST '?':
'DO YOU BELIEVE IT NORMAL TO BE' POST '?':/
I        D /SNOBOL ARB 'YOU ' ('CANNOT' × 'CANT') REM . POST/
         'HOW DO YOU KNOW YOU CANT' POST '?':'HAVE YOU TRIED TO' POST '?
'PERHAPS YOU COULD' POST 'NOW?':
'DO YOU REALLY WANT TO BE ABLE TO' POST '?':/
I        D /YOU DONT/
'DONT YOU REALLY' POST '?':
'WHY DONT YOU' POST '?':'DO YOU WISH TO BE ABLE TO' POST '?':
'DOES THAT TROUBLE YOU?':/
I                       D /YOU FEEL/
'TELL ME MORE ABOUT SUCH FEELINGS.':
'DO YOU OFTEN FEEL' POST '?':'DO YOU ENJOY FEELING' POST '?':
'WHAT DOES FEELING' POST 'REMIND YOU OF?':/
I        D /SNOBOL ARB 'YOU' ARB . POST 'I '/
         'PERHAPS IN YOUR FANTASY WE' POST 'EACH OTHER?':
'DO YOU WISH TO' POST 'ME?':'YOU SEEM TO NEED TO' POST 'ME.':
'DO YOU' POST 'ANYONE ELSE?':/
HELL L /20/ D //
'SUCCESS':/
BASTARD L  /20/ D //CF FOUL:/
STUPID L /20/ D //CF FOUL:/
DAMN L /20/ D //CF FOUL:/
SHUT UP L /20/ D /UP/CF FOUL:/
IF L /4/ D //
'WHY WOULD YOU WISH SOMETHING LIKE THAT?':
'WISHING WONT MAKE IT TRUE, GO OUT AND DO SOMETHING ABOUT IT.':
'TRY CONCERNING YOURSELF WITH REALITY.':
'STOP THINKING ABOUT IT, IT WONT COME TO PASS.':/
I                       D //
'YOU SAY ' COPY. '?':'CAN YOU ELABORATE ON THAT?':
'DO YOU SAY ' COPY. ' FOR SOME SPECIAL REASON?':/
I'M      L /2/ S /YOU ARE/ D //CF I:/
LIKE L /11/
LIKE D /SNOBOL ARB (' AM ' × ' IS ' × ' ARE ' × ' WAS ') ARB 'LIKE'/
LIKE     L /10/         D //CF DIT:/
MACHINE  L /10/         D //CF COMPUTER:/
MACHINES L /10/         D //CF COMPUTER:/
MAYBE    L /2/          D //CF PERHAPS:/
ME  S /YOU/
MY       L /3/ S /YOUR/ D /SNOBOL ARB 'YOUR ' ARB FAMILY REM . POST/
         'TELL ME ABOUT YOUR FAMILY':'WHO ELSE IN YOUR FAMILY' POST '?':
'YOUR    ' RELATIVE '?':
'WHAT ELSE COMES TO MIND WHEN YOU THINK OF YOUR ' RELATIVE '?':/
MY       D /YOUR/'YOUR' POST '?':
 RELATIVE '?':/D /YOUR/    'YOUR' POST '?':
'WHY DO YOU SAY YOUR' POST '?':'IS YOUR' POST 'IMPORTANT TO YOU?':
'DOES THAT SUGGEST ANYTHING ELSE THAT BELONGS TO YOU?':/
MYSELF   S /YOURSELF/
NAME     L /5/   D //
    'I AM NOT INTERESTED IN NAMES':'IVE TOLD YOU BEFORE, I AM NOT
 INTERESTED IN NAMES - PLEASE CONTINUE':/
NEVER       L /2/          D //  CF SUPER:/
NO L /1/ D //
'LETS TALK ABOUT SOMETHING MORE POSITIVE':
'ARE YOU SURE THERE IS NO CHANCE AT ALL?':/
NOBODY   L /3/          D //CF EVERYONE:/
NONE L /3/ D //CF SUPER:/
NOONE    L /3/          D //CF EVERYONE:/
PERHAPS  L /5/          D //
   ' YOU DONT SEEM TO BE QUITE CERTAIN.':'WHY THE UNCERTAIN TONE?':
'CANT YOU BE MORE DEFINITE?':'YOU ARENT SURE?':'DONT YOU KNOW?':/
REMEMBER L /6/          D /YOU REMEMBER/
         'DO YOU OFTEN THINK OF ' POST '?':
'DOES THINKING OF' POST ' BRING ANYTHING ELSE TO MIND?':
'WHAT ELSE DO YOU REMEMBER?':'WHY DO YOU REMEMBER' POST ' JUST NOW?':
'WHAT IN THE PRESENT SITUATION REMINDS YOU OF' POST '?':
'IS THERE A CONNECTION BETWEEN ME AND' POST '?':/
POSITIVE L /4/ D //CF SUPER:/
REMEMBER                D /DO I REMEMBER/
'DID YOU THINK I WOULD FORGET' POST '?':
'WHY DO YOU THINK I SHOULD RECALL' POST ' JUST NOW?':
'WHAT ABOUT' POST '?':CF WHAT:'YOU MENTIONED' POST '!':/
SAME     L /11/         D //CF DIT:/
SHE      L /1/          D //CF AUTRE:/
SO L /3/   D //'DO YOU THINK THAT THAT IS THE REAL REASON?':
'DON''T YOU THINK THERE MIGHT BE MORE TO IT THAN THAT?':
CF BECAUSE:/
SORRY    L /2/          D //
'PLEASE DO NOT FEEL APOLOGETIC.':'APOLOGIES ARE NOT NECESSARY.':
'WHAT FEELINGS DO YOU HAVE WHEN YOU APOLOGIZE?':
'IVE TOLD YOU THAT APOLOGIES ARE NOT REQUIRED!':/
WAS      L /3/ D /WAS YOU/
         'WHAT IF YOU WERE' POST '?':'DO YOU THINK YOU WERE' POST '?':
'WERE YOU' POST '?':'WHAT WOULD IT MEAN IF YOU WERE' POST '?':
'WHAT DOES' POST ' SUGGEST TO YOU?':CF WHAT:/
WAS      D /YOU WAS/    'WERE YOU REALLY!':
'WHY DO YOU TELL ME YOU WERE' POST ' NOW?':
'PERHAPS I ALREADY KNEW YOU WERE' POST '.':/
WAS      D /WAS I/
'WOULD YOU LIKE TO BELIEVE I WAS' POST '?':
'WHAT IF I HAD BEEN' POST '?':'WHAT SUGGESTS THAT I WAS' POST '?':
'WHAT DO YOU THINK?':'PERHAPS I WAS' POST '!':/
WERE     L /3/ S /WAS/  D //CF WAS:/
WHAT     L /2/          D //
         'WHY DO YOU ASK THAT?':'DOES THAT QUESTION INTEREST YOU?':
'WHAT IS IT YOU REALLY WANT TO KNOW?':'WHAT DO YOU THINK?':
'ARE SUCH QUESTIONS MUCH ON YOUR MIND?':
'WHAT ANSWER WOULD PLEASE YOU MOST?':'HAVE YOU ASKED ANYONE ELSE?':
'WHAT COMES TO MIND WHEN YOU ASK THAT?':
'HAVE YOU ASKED SUCH QUESTIONS BEFORE?':/
WHEN     L /2/          D //CF WHAT:/
WHY      L /2/          D /WHY DONT I/
'DO YOU BELIEVE THAT I DONT' POST '?':
'PERHAPS I WILL' POST 'IN GOOD TIME?':'SHOULD YOU' POST 'YOURSELF?':
'YOU WANT ME TO' POST ', DO YOU?':CF WHAT:/
WHY                     D /WHY CANT YOU/
'DO YOU THINK YOU SHOULD BE ABLE TO' POST '?':
'DO YOU WANT TO BE ABLE TO ' POST '?':
'DO YOU BELIEVE OUR CONVERSATION WILL HELP YOU TO' POST '?':
'HAVE YOU ANY IDEA WHY YOU CANT' POST '?':CF WHAT:/
WHY                     D //CF WHAT:/
WISH L /4/ D /YOU WISH/CF IF:/
WON'T    S /WONT/
YOU      L /2/ S /I/    D /I ARE/
'WHAT MAKES YOU THINK I AM' POST '?':
'DOES IT PLEASE YOU TO BELIEVE I AM' POST '?':
'DO YOU SOMETIMES WISH THAT YOU WERE' POST '?':
'PERHAPS YOU WOULD LIKE TO BE' POST 'YOURSELF?':/
YOU      D /SNOBOL ARB 'I ' ARB . POST 'YOU'/
         'WHY DO YOU THINK I ' POST 'YOU?':
'DO YOU LIKE TO THINK I ' POST 'YOU?':
'WHAT MAKES YOU THINK I ' POST 'YOU?':
'REALLY? : 'POST 'YOU?':'DO YOU WISH TO BELIEVE I ' POST 'YOU?':
'SUPPOSE I DID ' POST 'YOU. WHAT WOULD THAT MEAN?':
'DOES ANYONE ELSE THINK I ' POST 'YOU?':/
YOU                     D /I/
'WE WERE DISCUSSING YOU, NOT ME!':'OH!  I' POST '?':
'ARE YOU REALLY TALKING ABOUT ME?':/
YOUR     L /2/ S /MY/   D /MY/
'WHY ARE YOU CONCERNED ABOUT MY' POST '?':
'WHAT ABOUT YOUR OWN' POST '?':
'ARE YOU WORRIED ABOUT SOMEONE ELSES' POST '?':/
FOUL  L /20/        D //
'WATCH YOUR LANGUAGE OR ILL CANCEL YOUR ACCOUNT.':
'THAT JUST COST YOU 100 UNITS':/
YOURSELF S /MYSELF/
CF SUPER:/
*        END OF SCRIPT
