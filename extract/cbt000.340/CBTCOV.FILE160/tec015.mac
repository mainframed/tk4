THMK     TITLE 'THIMK GAME'
* LOU WARSHAWSKY AND RON LACHMAN
         PRINT NOGEN
         MACRO
&NAME    RAND
&NAME    L     R1,JSEED
         SR    R0,R0
         M     R0,PRIME
         D     R0,MAXIMUM
         ST    R0,JSEED
         N     R0,=F'7'
         LR    R1,R0
         MEND
         MACRO
&NAME    $PUT  &TEXT,&LEN
         LCLA  &SIZE
         LCLC  &BUFF
         AIF   ('&TEXT' NE '').NOSKIP
.*   LINE FEED ONLY
&SIZE    SETA  1
&BUFF    SETC  '=C'' '''
         AGO   .GO
.NOSKIP  ANOP
&BUFF    SETC  '&TEXT'
         AIF   ('&LEN' EQ '').NORM
.*   LENGTH HAS BEEN SPECIFIED
&SIZE    SETA  &LEN
         AGO   .GO
.*   NORMAL FORM
.NORM    ANOP
&SIZE    SETA  L'&TEXT
.GO      ANOP
&NAME    LA    R0,&SIZE            BUFFER SIZE IN R0
         LA    R1,&BUFF            BUFFER ADDR IN R1
         TPUT  (1),(0),R
         MEND
THIMK    CSECT
         REGISTER
         STM   R14,R12,12(R13)     SAVE REGISTERS
         B     16(R15)             BRANCH AROUND IDENTIFIER
         DC    CL8'THIMK'          IDENTIFIER
         LR    R11,R15             1ST BASE REGISTER R11
         LA    R12,2048(R11)
         LA    R12,2048(R12)       2ND BASE REGISTER R12
         USING THIMK,R11,R12
         ST    R13,SAVEAREA+4
         LA    R10,SAVEAREA
         ST    R10,8(R13)
         LR    R13,R10
*
*   MAIN PROGRAM
*
         TIME
         ST    R0,JSEED
         MVI   JSEED+1,X'0C'
         CP    JSEED(2),=PL2'120'
         BNL   POSTNOON
         MVC   GREETING+5(8),MORNING
         B     GREET
POSTNOON CP    JSEED(2),=PL2'170'
         BH    EVE
         MVC   GREETING+5(10),AFTERN
         B     GREET
EVE      MVC   GREETING+5(8),EVENING
GREET    $PUT  GREETING
         $PUT  THISIS
         TPUT  QRULES,33,ASIS
         TGET  INPUT,120
         TRT   INPUT,LOOKN
         BC    6,START
         $PUT
         $PUT  RULES01
         $PUT  RULES02
         $PUT  RULES03
         $PUT  RULES04
         $PUT  RULES05
         $PUT  RULES06
         $PUT  RULES07
         $PUT  RULES08
         $PUT  RULES09
         $PUT  RULES10
         $PUT  RULES11
         $PUT  RULES12
         $PUT  RULES13
         $PUT  RULES14
         $PUT  RULES15
START    TIME  TU
         ST    R0,JSEED
         OI    JSEED+3,X'01'
*
*   START THE GAME
*
NEWGAME  MVC   BOARD(6),NEWBOARD   RESET GAME BOARD
         $PUT
         TPUT  QFIRST,39,ASIS
         TGET  INPUT,120
         TRT   INPUT,LOOKN         DOES HE WANT TO GO FIRST?
         BC    8,UMOVE             YES:GOTO UMOVE ROUTINE
*
*   MACHINE GOES FIRST
*
MFIRST   RAND
         SLA   R1,1                MULTIPLY RANDOM # BY 2
         LA    R10,OPENERS(R1)     ADDR OF OPENING MOVE IN R10
         LH    R9,0(R10)           ROW DISPLACEMENT + ZONE IN R9
         LR    R8,R9               MOVE IN R8
         SRL   R9,4                SHIFT # OUT OF R9
         N     R9,=F'7'            REMOVE ZONE
         N     R8,=F'7'            # TO BE REMOVED IN R8
         LH    R7,BOARD(R9)        OLD BOARD VALUE IN R7
         SR    R7,R8               SUBTRACT MOVE FROM ROW
         STH   R7,BOARD(R9)        STORE NEW BOARD VALUE
         SRA   R9,1                DIVIDE ROW DISPLACEMENT BY 2
         LA    R9,C'A'(R9)         CONVERT TO CHARACTER (A,B,C)
         STC   R9,MTAKES+26        STORE ROW ID IN OUTPUT LINE
         LA    R8,C'0'(R8)         CONVERT # REMOVED TO CHARACTER
         STC   R8,MTAKES+15        STORE # REMOVED IN OUTPUT LINE
         $PUT  MTAKES              PRINT MOVE MESSAGE
*
*   PRINT BOARD
*
UMOVE    MVC   PRNTLINE+3(13),PRNTLINE+2
         MVI   PRNTLINE,C'A'       ROW ID
         LH    R2,BOARD            BOARD VALUE IN R2
         BAL   R6,EXPAND
         MVC   PRNTLINE+3(13),PRNTLINE+2
         MVI   PRNTLINE,C'B'       ROW ID
         LH    R2,BOARD+2          BOARD VALUE IN R2
         BAL   R6,EXPAND
         MVC   PRNTLINE+3(13),PRNTLINE+2
         MVI   PRNTLINE,C'C'       ROW ID
         LH    R2,BOARD+4          BOARD VALUE IN R2
         LA    R6,HUMAN            SET DIFFERENT RETURN POINT
EXPAND   SLA   R2,1                MULTIPLY ROW VALUE BY 2
         LA    R5,PRNTLINE+1
         AR    R5,R2               LOOP COMPARAND
         LA    R4,2                LOOP INCREMENT
         LA    R3,PRNTLINE+1       LOOP INDEX
PRNTLOOP BXH   R3,R4,PRNTPUT
         MVI   0(R3),C'I'          MOVE I INTO LINE
         B     PRNTLOOP
PRNTPUT  $PUT  PRNTLINE
         BR    R6                  RETURN
PRNTLINE DC    CL16' '
*
*   GET MOVE FROM PLAYER
*
HUMAN    SR    R2,R2               CLEAR R2
         TPUT  PROMPT,12,ASIS      PROMPT FOR MOVE
         TGET  INPUT,120
         TRT   INPUT,LOOKABC       SEARCH FOR ROW ID
         LR    R4,R2               ROW ID IN R4
         SR    R2,R2               CLEAR R2
         TRT   INPUT,LOOKNUM       SEARCH FOR NUMBER
         LR    R5,R2               NUMBER IN R5
         LTR   R4,R4               ROW ID OK?
         BP    NUMCK               YES:CHECK FOR VALID NUMBER
ROWQUIZ  TPUT  QROW,11,ASIS        NO:PROMPT FOR ROW
         TGET  INPUT,120
         SR    R2,R2               CLEAR R2
         TRT   INPUT,LOOKABC       ROW ID OK NOW?
         BC    8,ROWQUIZ           NO:ASK AGAIN
         LR    R4,R2               YES:ROW ID IN R4
NUMCK    LTR   R5,R5               NUMBER OK?
         BP    NUMOK               YES:CONTINUE
NUMQUIZ  TPUT  QNUM,10,ASIS        NO:PROMPT FOR NUMBER
         TGET  INPUT,120
         SR    R2,R2               CLEAR R2
         TRT   INPUT,LOOKNUM       NUMBER OK NOW?
         BC    8,NUMQUIZ           NO:ASK AGAIN
         LR    R5,R2               YES:NUMBER IN R5
NUMOK    N     R4,=F'7'            REMOVE ZONE FROM ROW ID
         C     R4,=F'7'            FORFEIT?
         BE    FORFEIT             YES:ULOSE
         N     R5,=F'7'            NO:REMOVE ZONE FROM NUMBER
         LTR   R5,R5               FORFEIT?
         BZ    FORFEIT             YES:ULOSE
         CH    R5,BOARD(R4)        NO:NUMBER TOO HIGH?
         BNH   AOK                 NO:CONTINUE
         $PUT  TOOMANY             YES:GIVE ERROR MESSAGE, AND
         B     HUMAN               START OVER
AOK      LH    R6,BOARD(R4)        LOAD OLD ROW VALUE IN R4
         SR    R6,R5               SUBTRACT MOVE
         STH   R6,BOARD(R4)        STORE NEW BOARD VALUE
         B     OFFENSE
FORFEIT  LA    R9,MOVETBLE
         B     ULOSE
LOOKABC  DC    XL93'FF',XL39'F0F2F4',XL64'F0F2F4',XL60'00'
LOOKNUM  DC    XL93'F0',XL155'F1F2F3F4F5F6F7',XL8'00'
PROMPT   DC    C'YOUR MOVE - '
QROW     DC    C'WHICH ROW? '
QNUM     DC    C'HOW MANY? '
TOOMANY  DC    C'THAT''S TOO MANY.'
*
*   CHECK FOR WINS, AND MAKE MOVE
*
OFFENSE  LH    R9,BOARD            ROW A VALUE IN R9
         MH    R9,=H'6'            MULTIPLY BY 6
         AH    R9,BOARD+2          ADD ROW B VALUE
         SLA   R9,2                MULTIPY BY 4
         AH    R9,BOARD+4          ADD ROW C VALUE
         SLA   R9,1                MULTIPY BY 2
         LA    R9,MOVETBLE(R9)     ADDR OF MOVE IN R9
         TM    0(R9),X'04'         UWIN?
         BC    5,UWIN              YES:GOTO UWIN ROUTINE
         TM    0(R9),X'08'         NO:ULOSE?
         BC    5,ULOSE             YES:GOTO ULOSE ROUTINE
         LH    R8,0(R9)            ROW DISPLACEMENT + ZONE IN R8
         LR    R7,R8               MOVE IN R7
         SRL   R8,4                SHIFT # OUT OF R8
         N     R8,=F'7'            REMOVE ZONE
         N     R7,=F'7'            # TO BE REMOVED IN R7
         LH    R6,BOARD(R8)        OLD BOARD VALUE IN R6
         SR    R6,R7               SUBTRACT MOVE FROM ROW
         STH   R6,BOARD(R8)        STORE NEW ROW VALUE
         SRA   R8,1                DIVIDE ROW DISPLACEMENT BY 2
         LA    R8,C'A'(R8)         CONVERT TO CHARACTER (A,B,C)
         STC   R8,MTAKES+26        STORE ROW ID IN OUTPUT LINE
         LA    R7,C'0'(R7)         CONVERT # REMOVED TO CHARACTER
         STC   R7,MTAKES+15        STORE # REMOVED IN OUTPUT LINE
*
*   CHECK FOR A CHANGE IN GAME STATUS, AND
*    CHOOSE AN APPROPRIATE COMMENT
*
         CLC   0(1,R9),0(R10)
         BE    NOCHANGE
         LR    R10,R9
         TM    0(R9),X'01'         DEFENSIVE POSITION?
         BC    8,NODEF             NO:CHECK OTHER POSSIBILITIES
         RAND
         SLA   R1,5                MULTIPLY RANDOM # BY 32
         LA    R1,MHATES(R1)       ADDR OF COMMENT IN R1
         LA    R0,32
         TPUT  (1),(0),R
         B     NOCOMNT
NODEF    RAND
         SLA   R1,5                MULTIPLY RANDOM # BY 32
         LA    R1,MLIKES(R1)       ADDR OF COMMENT IN R1
         LA    R0,32
         TPUT  (1),(0),R
NOCOMNT  $PUT  MTAKES
         B     UMOVE
NOCHANGE $PUT  MTAKES
         CLC   0(2,R9),=X'0222'    TO BE?
         BNE   UMOVE               NO:FORGET THE COMMENTS
         CLI   TOBEFLG,X'00'       YES:HAS TOBE BEEN DONE YET?
         BNE   PREDONE             YES:GIVE THE NEXT COMMENT, IF ANY
         RAND
         SRA   R1,2                NO:SHOULD WE DO IT?
         BNZ   UMOVE               NAW, MAYBE LATER
         $PUT  TOBETXT1            YEAH! DO IT NOW!
         $PUT
         $PUT  TOBETXT2
         $PUT  TOBETXT3
         $PUT  TOBETXT4
         $PUT
         $PUT  TOBETXT5
         MVI   TOBEFLG,X'01'
         B     UMOVE
PREDONE  CLI   TOBEFLG,X'01'       WAS THE TOBE THING JUST DONE?
         BNE   UMOVE               NO:FORGET THE COMMENTS
         $PUT  TOBETXT1            YES:GIVE THE OTHER MESSAGE
         $PUT  TOBETXT6
         MVI   TOBEFLG,X'FF'       MAKE SURE IT ISN'T DONE AGAIN
         B     UMOVE
*
*   END OF GAME ROUTINES
*
UWIN     LH    R5,UWIN#            # OF WINS IN R5
         LA    R5,1(R5)            ADD 1 TO R5
         STH   R5,UWIN#            STORE NEW # OF WINS
         $PUT  UWINTXT
         B     ENDGAME
UWIN#    DC    H'0'
UWINTXT  DC    C'YOU WIN.'
ULOSE    LH    R5,ULOSE#           # OF LOSSES IN R5
         LA    R5,1(R5)            ADD 1 T0 R5
         STH   R5,ULOSE#           STORE NEW # OF LOSSES
         C     R9,=A(MOVETBLE)     DID HE LOSE WITH 0 ON BOARD?
         BE    ULOSE0              YES:PRINT SHORT TEXT
         LH    R5,0(R9)            NO:GET ROW LEFT WITH ZONE
         SRL   R5,4                SHIFT # OUT OF R5
         N     R5,=F'7'            REMOVE ZONE
         SRA   R5,1                DIVIDE ROW DISPLACEMENT BY 2
         LA    R5,C'A'(R5)         CONVERT TO CHARACTER (A,B,C)
         STC   R5,LULOSE+28        STORE ROW ID IN OUTPUT LINE
         $PUT  LULOSE
         B     ENDGAME
ULOSE#   DC    H'0'
LULOSE   DC    C'YOU LOSE WITH 1 LEFT IN ROW  .'
SULOSE   DC    C'YOU LOSE.'
ULOSE0   $PUT  SULOSE
ENDGAME  $PUT
         LR    R10,R9              LOAD LAST MOVE IN R10
         TPUT  Q1MOTIME,37,ASIS
         TGET  INPUT,120
         TRT   INPUT,LOOKN         DOES HE WANT TO PLAY ANOTHER GAME?
         BC    6,ALLDONE           NO:PRINT SCORE
         CLC   ULOSE#,=H'0'        HAS HE LOST ONE YET?
         BNE   NEWGAME             YES:ASK HIM IF HE WANTS TO GO FIRST
         $PUT  MGO1ST              NO:TELL HIM MACHINE WILL GO FIRST
         MVC   BOARD(6),NEWBOARD   RESET BOARD
         B     MFIRST
Q1MOTIME DC    C'WOULD YOU LIKE TO PLAY ANOTHER GAME? '
MGO1ST   DC    C'SINCE YOU ARE DOING SO WELL, I''LL GO FIRST.'
ALLDONE  LH    R2,UWIN#            # OF WINS IN R2
         LH    R3,ULOSE#           # OF LOSSES IN R3
         AR    R3,R2               TOTAL # OF GAMES IN R3
         MH    R2,=H'10'
         MH    R3,=H'10'
         CVD   R2,PNUM
         ED    SCORE+12(3),PNUM+6
         CVD   R3,PNUM
         ED    SCORE+28(3),PNUM+6
         $PUT
         $PUT  SCORE,32
         $PUT  THANX               BYE BYE
         L     R13,4(R13)
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SR    R15,R15             SET RETURN CODE
         BR    R14                 EXIT
PNUM     DS    D
JSEED    DS    F
SAVEAREA DS    18F
PRIME    DC    F'16807'
MAXIMUM  DC    X'7FFFFFFF'
BOARD    DS    3H
NEWBOARD DC    H'7',H'5',H'3'
         LTORG
SCORE    DC    C'YOU HAVE WON ',X'2120',C' GAMES OUT OF ',X'2120',C'.'
THANX    DC    C'COME AGAIN SOON.'
INPUT    DS    CL120
LOOKN    DC    XL150'FF',XL64'FF',XL42'00'
MTAKES   DC    C' MACHINE TAKES   FROM ROW  .'
QFIRST   DC    C'WOULD YOU LIKE TO MAKE THE FIRST MOVE? '
*   MOVE TABLE
         DS    0H
MOVETBLE DC    X'0A00',X'0541',X'0A41',X'0A42'   00X
         DC    X'0521',X'0A41',X'0A20',X'0A20'   01X
         DC    X'0A21',X'0A40',X'0121',X'0241'   02X
         DC    X'0A22',X'0A40',X'0221',X'0141'   03X
         DC    X'0A23',X'0A40',X'0222',X'0221'   04X
         DC    X'0A24',X'0A40',X'0223',X'0222'   05X
         DC    X'0501',X'0A01',X'0A00',X'0A00'   10X
         DC    X'0A21',X'0141',X'0241',X'0242'   11X
         DC    X'0A00',X'0221',X'0201',X'0141'   12X
         DC    X'0A00',X'0222',X'0121',X'0221'   13X
         DC    X'0A00',X'0223',X'0221',X'0222'   14X
         DC    X'0A00',X'0224',X'0222',X'0223'   15X
         DC    X'0A01',X'0A40',X'0101',X'0241'   20X
         DC    X'0A20',X'0201',X'0221',X'0141'   21X
         DC    X'0121',X'0221',X'0242',X'0221'   22X
         DC    X'0221',X'0222',X'0241',X'0222'   23X
         DC    X'0222',X'0221',X'0224',X'0223'   24X
         DC    X'0223',X'0222',X'0225',X'0224'   25X
         DC    X'0A02',X'0A40',X'0201',X'0101'   30X
         DC    X'0A03',X'0202',X'0101',X'0241'   31X
         DC    X'0201',X'0101',X'0241',X'0242'   32X
         DC    X'0101',X'0221',X'0222',X'0243'   33X
         DC    X'0221',X'0222',X'0223',X'0224'   34X
         DC    X'0222',X'0223',X'0224',X'0225'   35X
         DC    X'0A03',X'0A40',X'0202',X'0201'   40X
         DC    X'0A04',X'0203',X'0201',X'0202'   41X
         DC    X'0202',X'0201',X'0204',X'0203'   42X
         DC    X'0201',X'0202',X'0203',X'0204'   43X
         DC    X'0101',X'0121',X'0242',X'0243'   44X
         DC    X'0221',X'0121',X'0241',X'0242'   45X
         DC    X'0A04',X'0A40',X'0203',X'0202'   50X
         DC    X'0A05',X'0204',X'0202',X'0203'   51X
         DC    X'0203',X'0202',X'0205',X'0204'   52X
         DC    X'0202',X'0203',X'0204',X'0205'   53X
         DC    X'0201',X'0101',X'0241',X'0242'   54X
         DC    X'0121',X'0221',X'0242',X'0243'   55X
         DC    X'0A05',X'0A40',X'0204',X'0203'   60X
         DC    X'0A06',X'0205',X'0203',X'0204'   61X
         DC    X'0204',X'0203',X'0206',X'0205'   62X
         DC    X'0203',X'0204',X'0205',X'0206'   63X
         DC    X'0202',X'0201',X'0141',X'0241'   64X
         DC    X'0201',X'0202',X'0221',X'0141'   65X
         DC    X'0A06',X'0A40',X'0205',X'0204'   70X
         DC    X'0A07',X'0206',X'0204',X'0205'   71X
         DC    X'0205',X'0204',X'0207',X'0206'   72X
         DC    X'0204',X'0205',X'0206',X'0207'   73X
         DC    X'0203',X'0201',X'0201',X'0141'   74X
         DC    X'0202',X'0203',X'0101',X'0201'   75X
OPENERS  DC    X'0201',X'0221',X'0241',X'0201'
         DC    X'0203',X'0242',X'0221',X'0241'
MLIKES   DC    CL32'AH-HA!'
         DC    CL32'TOUCHE!'
         DC    CL32'NOW I HAVE YOU!'
         DC    CL32'YOU MIGHT AS WELL GIVE UP NOW.'
         DC    CL32'YOU''LL BE SORRY FOR THAT MOVE.'
         DC    CL32'YOU DUMMY.'
         DC    CL32'YOU''RE A DUAL INLINE PACKAGE.'
         DC    CL32'YOU FOOL.'
MHATES   DC    CL32'HMMM...'
         DC    CL32'@#$*?Ö%!!!'
         DC    CL32'YOU''RE GETTING TOO GOOD FOR ME.'
         DC    CL32'DOT GAMBIT.'
         DC    CL32'GOOD MOVE, I REGRET TO SAY.'
         DC    CL32'STINKER!'
         DC    CL32'I''M GOING TO GET YOU FOR THIS.'
         DC    CL32'YOU ACE WHOLE.'
TOBETXT1 DC    C'2-B, EH?'
TOBETXT2 DC    C'TO BE, OR NOT TO BE.  THAT IS THE QUESTION.'
TOBETXT3 DC    C'WHETHER ''TIS NOBLER IN THE MIND TO SUFFER THE'
TOBETXT4 DC    C'SLINGS AND ARROWS OF OUTRAGEOUS FORTUNE...'
TOBETXT5 DC    C'I ALWAYS WANTED TO BE AN ACTOR.'
TOBETXT6 DC    C'LET''S NOT GO THROUGH THAT AGAIN!'
TOBEFLG  DC    X'00'
GREETING DC    CL15'GOOD'
MORNING  DC    C'MORNING.'
AFTERN   DC    C'AFTERNOON.'
EVENING  DC    C'EVENING.'
THISIS   DC    C'THIS IS THIMK.'
QRULES   DC    C'WOULD YOU LIKE TO SEE THE RULES? '
RULES01  DC    C'  THIMK IS A FORM OF NIM, A GAME THAT HAS BEEN PLAYED'
RULES02  DC    C'FOR THOUSANDS OF YEARS.  THE GAME WAS ORIGINALLY'
RULES03  DC    C'PLAYED WITH PILES OF STICKS OR STONES, AND EACH'
RULES04  DC    C'PLAYER, IN TURN, WOULD TAKE AWAY A NUMBER OF STICKS,'
RULES05  DC    C'UNTIL THE PILES WERE EMPTY.  THE PLAYER WHO TOOK'
RULES06  DC    C'THE LAST STICK WAS THE LOSER.'
RULES07  DC    C'  IN OUR GAME, A STICK IS REPRESENTED BY THE'
RULES08  DC    C'LETTER ''I''.  THERE ARE THREE ROWS OF STICKS,'
RULES09  DC    C'IDENTIFIED BY THE LETTERS A, B, AND C.  ROW A WILL'
RULES10  DC    C'START WITH SEVEN STICKS, ROW B WITH FIVE, AND'
RULES11  DC    C'ROW C WITH THREE.'
RULES12  DC    C'  TO MAKE A MOVE, ENTER THE LETTER OF THE ROW (A-C)'
RULES13  DC    C'AND THE NUMBER YOU WISH TO REMOVE (1-7), IN ANY'
RULES14  DC    C'ORDER.  IF YOU WISH TO FORFEIT A GAME, ENTER AN'
RULES15  DC    C'ASTERISK (*).'
         DC    C'    ASPAC THIMK - LOU WARSHAWSKY AND RON LACHMAN  '
         END   THIMK
