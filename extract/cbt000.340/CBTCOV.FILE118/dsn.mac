*                                                                    *
*       THIS COMMAND PROCESSOR REQUEST A DATASET AND THEN SEARCHES   *
*       THE QCB CHAIN TO FIND ALL ASIDS THAT ARE ALLOCATED TO IT.    *
*       IT THEN SEARCHES THE ASIDS TO FIND THE ACTUAL JOBNAME OR     *
*       USERID ASSOCIATED IT.                                        *
*                                                                    *
*       WRITTEN BY: RICHARD G NIKULA                                 *
*                   WASHINGTON STATE DEPT. OF TRANSPORTATION         *
*                                                                    *
DSNFIND ENTER (14,12),12,(,72)
        LR    R10,R1
        USING CPPL,R10             CPPL ADDRESSABILITY
        USING IKJPARMD,R11         PCL  ADDRESSABILITY
*    STUFF FOR PARSE ****
        MVC   PPLUPT,CPPLUPT
        MVC   PPLECT,CPPLECT
        MVC   PPLCBUF,CPPLCBUF
        LA    R1,PPLUPT
        LINK  EP=IKJPARS           GO DO IT
        LTR   R15,R15              ALL GO OK???
        BNZ   PARSERR              NOPE....
        L     R11,ANS              WHERE IS PARSE DSECT??
        L     R9,DSN               ADDRESS OF DATASET NAME
        LH    R10,DSN+4            GET THE LENGTH
        LA    R2,16
        L     R2,0(R2)             --> CVT
        L     R3,640(R2)           --> FIRST QCB
        XR    R5,R5                CLEAR REG FOR IC IN FINDEM
LOOPTOP LTR   R3,R3                END OF CHAIN YET??
        BZ    NOFIND
        CLC   16(8,R3),=CL8'SYSDSN'
        BE    ENDFIND
        L     R3,0(R3)             --> NEXT MAJOR
        B     LOOPTOP
ENDFIND EQU   *
        L     R3,8(R3)             ---> FIRST MINOR
FINDEM  EQU   *
        LTR   R3,R3                END OF MINOR CHAIN???
        BZ    NOTALC
        IC    R5,16(R3)            VARIABLE LENGTH
        CR    R5,R10               LENGTH OF DATASET SAME AS INPUT
        BNZ   NEXMINR              NO, THEN CAN'T MATCH CAN IT !
        BCTR  R5,0                 WHY ???   YOU ASK?????
        EX    R5,CLCDSN            TRY TO COMPARE THEM..
        BZ    FOUNDSN              FOUND THE ONE WE WERE LOOKING FOR
NEXMINR L     R3,0(R3)             ----> NEXT MINOR (AND REPEAT)
        B     FINDEM
CLCDSN  CLC   0(0,R9),20(R3)       ** EXECUTED** COMPARE DSNAMES
FOUNDSN L     R6,8(R3)             ----> FIRST QEL
        LA    R4,TABLE
NEXTQEL EQU   *
        MVC   0(2,R4),14(R6)       ASID
        TM    12(R6),X'80'
        BO    SHRD
        MVI   2(R4),X'00'          WASN'T SHARED, SHOW EXCLUSIVE
SHRD    L     R6,0(R6)             ----> NEXT QEL
        LTR   R6,R6                END OF QELS??
        LA    R4,3(R4)
        BNZ   NEXTQEL
ALLDONE LA    R4,TABLE
        CLI   2(R4),X'FF'          IS FIRST ONE SHARING DSN ??
        BE    PUTHEAD
        MVC   HEADER+18(3),=C'OLD'
PUTHEAD TPUT HEADER,L'HEADER       PUT THE HEADER MESSAGE
        LA    R10,LINE             SET UP POINTER
        L     R3,556(R2)           ---> ASVT
ASLOOP  LA    R7,520(R3)           ---> ENTRY ADDS(MINUS 4)
        L     R5,516(R3)           ---> # OF ASCBS
CHKASID LA    R7,4(R7)             ENTRY + 4
        TM    0(R7),X'80'          IN USE?????
        BO    SKIPCHK              NO...DON'T LOOK AT IT THEN
*   ASVT-->ASCB-->CSCB
*   BOY WHAT FUN!!!!!!!!!
        L     R6,0(R7)             R6 ----> ASCB
        CLC   0(2,R4),36(R6)       ASIDS MATCH???
        BE    CSCBCK1              YUP...GO FIND CSCB
SKIPCHK BCT   R5,CHKASID           GO FOR NEXT ASID
        B     NEXTASID             JOB MUST HAVE ENDED JUST NOW
CSCBCK1 L     R5,56(R6)            ----> POSSIBLE CSCB
        LTR   R5,R5                TEST FOR POSITIVE
        BNP   CSCBCK2              NO --> TRY NEXT ONE
        TM    28(R5),X'03'         TEST INDICATOR FLAGS
        BM    GOTCSCB              GO TO IT
CSCBCK2 L     R5,172(R6)           TRY NEXT ONE
        LTR   R5,R5                ONE MORE TIME
        BNP   CSCBCK3
        S     R5,=F'8'             ADJUST POINTER BACK SOME
        B     GOTCSCB
CSCBCK3 L     R5,176(R6)           ONE LAST TRY
        LTR   R5,R5                CROSS YOUR FINGERS
        BNP   NEXTASID             OH WELL...TRY NEXT ASID
        S     R4,=F'16'            GOT TO ADJUST THIS ONE TOO
GOTCSCB TM    28(R5),X'03'         TEST INDICATOR FLAG
        BM    GOOD                 JOB OR TSO
        BZ    UNKN                 COULD BE ANYONE
        MVC   0(4,R10),=CL8'INIT'  IT'S AN INITIATOR
        B     DISPLAY
UNKN    MVC   5(8,R10),=CL8'SYSTEM'
        B     DISPLAY
GOOD    MVC   5(8,R10),8(R5)       MOVE IN JOB NAME
        TM    28(R5),X'02'         IS IT TSO OR JOB
        BZ    TSOSES
        MVC   0(3,R10),=C'JOB'
        B     DISPLAY
TSOSES  MVC   0(4,R10),=C'USER'
DISPLAY MVI   4(R10),C'-'          ISN'T THIS PRETTY
        LA    R10,18(R10)          INCREMENT POINTER REGISTER
        AP    COUNT,=PL1'1'        FOUR PER LINES FOR ALLOCATIONS
        CP    COUNT,=PL1'4'        IS PRETTIER
        BNE   NEXTASID
        TPUT  LINE,L'LINE
        LA    R10,LINE             RESET POINTER
        ZAP   COUNT,=PL1'0'
        MVI   LINE,C' '
        MVC   LINE+1(L'LINE-1),LINE        CLEAR THE LIN
NEXTASID LA    R4,3(R4)
        CLC   0(3,R4),=3X'FF'      FOXESS MEANS END
        BE    LASTLN
        CLI   WFLAG,C'Y'           HAS WAIT MESSAGE BEEN WRITTEN
        BE    ASLOOP
        CLI   TABLE+2,X'00'        WAS IT AN  EXCLUSIVE DATASET?
        BE    PUTWTG               YES, THEN ALL LOWER TASKS WAITING
        CLI   2(R4),X'00'           IS THIS AN EXCLUSIVE REQUEST?
        BNE   ASLOOP               NO, SO FAR EVERYTHING SHARING IT
PUTWTG  TPUT  LINE,L'LINE
        MVI   WFLAG,C'Y'           SHOW THAT NO NEED TO CHECK WAITING
        LA    R10,LINE             RESET POINTER
        ZAP   COUNT,=PL1'0'
        MVI   LINE,C' '
        MVC   LINE+1(L'LINE-1),LINE        CLEAR THE LIN
        TPUT  WAITING,L'WAITING    TELL THEM THE REST DON'T HAVE IT
        B     ASLOOP
LASTLN  TPUT  LINE,L'LINE
BYEBYE  LEAVE EQ,CC=0
NOFIND  TPUT MESSAGE,L'MESSAGE
        B    RET04
NOTALC  TPUT MESS2,L'MESS2
        B    BYEBYE
PARSERR TPUT MESS3,L'MESS3
        B    RET04
RET04   LEAVE CC=4
        LTORG
WFLAG   DC   C'N'
LINE    DC   CL76' '
COUNT   DC   PL2'0'
WAITING DC   C'FOLLOWING ARE WAITING FOR DATASET:'
HEADER  DC   C'DATASET ALLOCATED SHR BY:'
MESSAGE DC   C'SYSTEM ERROR, REENTER COMMAND'
MESS2   DC   C'DATA SET NOT ALLOCATED'
MESS3   DC   C'PARSE ERROR, REENTER COMMAND'
PPLUPT  DC   A(0)
PPLECT  DC   A(0)
PPLECB  DC   A(PPLECBS)
PPLPCL  DC   A(PARM)
PPLANS  DC   A(ANS)
PPLCBUF DC   A(0)
PPLUWA  DC   A(0)
PPLECBS DC   A(0)
ANS     DC   A(0)
TABLE   DC   3000X'FF'          ENOUGH FOR 1000 ALLOCATIONS
PARM    IKJPARM
DSN     IKJPOSIT DSNAME,PROMPT='DATA SET NAME',USID
        IKJENDP
        IKJCPPL
        END
