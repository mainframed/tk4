*                                                                    *
*         THIS COMMAND PROCESSOR RETURNS THE NAME OF THE EDIT        *
*         DATASET CURRENTLY BEING EDITTED AND WHETHER THE CURRENT    *
*         COPY HAS BEEN MODIFIED SINCE IT WAS LAST SAVED             *
*                                                                    *
*                                                                    *
*         WRITTEN: RICHARD G NIKULA                                  *
*                  WASHINGTON STATE DEPT. OF TRANSPORTATION          *
*                                                                    *
*                                                                    *
DSNAME ENTER  (14,12),12,(,WORKLEN)
         LR   R5,R1                 GET THE EDIT COMM TABLE
         USING OUTAREA,R13
         MVC  LINE+44(11),=CL11' '  PUT BLANKS
         MVC  LINE(44),240(R5)      GET THE DSNAME
         LH   R7,238(R5)            GET THE LENGTH
         LA   R8,LINE               START OF DSNAME
         AR   R8,R7                 GET THE DISPLACEMENT TO END OF NAME
         CLC  284(8,R5),=CL8' '     IS THERE A MEMBER NAME
         BE   NOMEM                 NO THEN SKIP THE MEMBER PART
         MVI  0(R8),C'('            PUT OPENING PAREN
         LA   R8,1(R8)              NEXT CHAR
         MVC  0(8,R8),284(R5)       GET THE MEMBER NAME
         LA   R4,8                  MAX LETTERS IN MEMBER NAME
RPAREN   EQU *
         LA   R8,1(R8)              NEXT CHAR
         BCTR R4,0                  DECREMENT COUNT
         LTR  R4,R4                 HAVE WE RUN OUT OF CHARACTERS?
         BZ   PUTPAR                YES THEN GO PUT A PARENTHESIS
         CLI  0(R8),C' '            IS IT BLANK?
         BNE  RPAREN                NO THEN KEEP LOOKING
PUTPAR   EQU  *
         MVI  0(R8),C')'            CLOSE PARENS
NOMEM    EQU  *
         TM   125(R5),X'80'         IS  ALTER BIT SET
         BZ   NOTNEW NO
         MVI  1(R8),C'*'            FLAG IT AS NEVER SAVED
NOTNEW   EQU  *
         LA   R8,LINE               WHERES OUR OUTPUT LINE?
         TPUT (R8),55               PUT IT OUT
         LEAVE  EQ,CC=0             RETURN TO EDIT
      SPACE 3
OUTAREA  DSECT
         DS    18F
LINE     DS    CL55
WORKLEN  EQU *-OUTAREA
      END
