         MACRO
&LABEL   OUTPUT &LIST,&EXIT=BALR,&UCMID=
         AIF   ('&LIST'(1,1) NE '(').NOREG
&LABEL   LR    1,&LIST             LOAD ADDRESS OUTPUT LIST
         AGO   .CONT1
.NOREG   ANOP
&LABEL   L     1,&LIST             LOAD ADDRESS OUTPUT LIST
.CONT1   ANOP
         AIF   (T'&UCMID EQ 'O').CONT2
         AIF   ('&UCMID'(1,1) EQ '(').UCMREG
         ICM   REG1,8,&UCMID       LOAD UCM ENTRY ID
         AGO   .CONT2
.UCMREG  SLL   &UCMID(1),24        SHIFT UCM ENTRY ID TO TOP BYTE
         OR    REG1,&UCMID(1)      LOAD UCM ID INTO PARAMETER REG
.CONT2   CNOP  0,4                 FULLWORD ALIGNMENT
         BAL   15,*+8              BRANCH ROUND ADCON
         DC    V(AUXCMM00)         ADDRESS OUTPUT PROCESSOR
         L     15,0(15)            LOAD ADDRESS PROCESSOR
         AIF   ('&EXIT' EQ 'BALR').BALR
         AIF   ('&EXIT' EQ 'XCTL').XCTL
         MNOTE 4,'INVALID EXIT TYPE - &EXIT, BALR ASSUMED'
.BALR    BALR  14,15               LINK TO OUTPUT ROUTINE
         MEXIT
.XCTL    L     14,4(13)            ADDRESS PREVIOUS SAVE AREA
         ST    1,24(14)            STORE ADDRESS OUTPUT LIST IN S/A
         ZRETURN RC=(15)           PASS CONTROL
         ORG   *-2
         BR    15                  BRANCH TO NEXT ROUTINE
         MEND
