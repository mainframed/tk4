***********************************************************************
*                                                                     *
*  THIS MODULE (IN CONJUNCTION WITH 'SSIUPDAT') CREATES 80 BYTE       *
*  RECORDS OF DUMMY SETSSI INFO FOR INPUT TO L.E. WHEN CREATING       *
*  APPLICATION LOAD LIBRARY MODULES ON DISK.    JCL FOR CREATION OF   *
*  THE SSI LIBRARY BY THIS MODULE IS:-                                *
*                                                                     *
*    //CREATE EXEC PGM=SSICREAT                                       *
*    //SSIFILE DD DSN=SYS1.SSILIB,DISP=(,CATLG),UNIT=...,VOL=SER=..., *
*    //SPACE=(TRK,(1,,1)),DCB=(BLKSIZE=80,LRECL=80,RECFM=F,DSORG=PO)  *
*                                                                     *
*    SSICREAT NEED ONLY BE RUN ONCE FOR INITIAL LIBRARY CREATION      *
*                                                                     *
*  OF THE SETSSI INFO CREATED THE FIRST 6 DIGITS REPRESENT THE DATE   *
*  IN  DDMMYY  FORM, THE LAST 2 DIGITS BEING A CODE REPRESENTING THE  *
*  LANGUAGE AND LEVEL OF LANGUAGE THE PROGRAM HAS BEEN COMPILED UNDER.*
*                                                                     *
*                                                                     *
* CONTACT:- RAY BARBER,ROWNTREE-MACKINTOSH(YORK), TEL 0904-53071 X476 *
*                                                                     *
***********************************************************************
         SPACE 3
SSICREAT CSECT
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         LR    11,13
         LA    13,SAVE1
         ST    13,8(11)
         ST    11,4(13)
*
         SPACE 2
         TIME  DEC
         SPACE 2
         ST    1,DUBWD+4
         TM    DUBWD+5,X'01'
         BO    *+16
         TM    DUBWD+5,X'12'
         BM    *+8
         MVI   YEARTAB+4,29
         MVC   YEAR-1(3),EDMASK
         ED    YEAR-1(3),DUBWD+5        SET UP YEAR
         XC    DUBWD(6),DUBWD          CLEAR ALL BUT JULIAN DATE
         SR    0,0                     CLEAR
         CVB   1,DUBWD                 CONVERT JULIAN DATE TO BINARY
         LA    4,YEARTAB-4             PREPARE TO SCAN CONV TABLE
PRIDATE  SR    1,0
         LA    4,4(4)
         IC    0,0(4)
         CR    0,1
         BL    PRIDATE
         CVD   1,DUBWD
         MVO   DUBWD(2),DUBWD+6(2)     SHIFT FOR EDIT
         MVC   DAY-1(3),EDMASK         SET UP DAY MASK
         ED    DAY-1(3),DUBWD          SET UP DAY
         MVC   MONTH(2),2(4)           SET UP MONTH
         CLI   DAY,X'40'                IS DAY NO LT 10
         BNE   *+8
         MVI   DAY,X'F0'              YES, SET TO ZERO IN TENS DAY POSN
*
         MVC   CONVDATE(6),DAY          MOVE CONVERTED DATE FOR OUTPUT
         SPACE 3
         OPEN  (PDSDCB,(OUTPUT))
         SPACE 2
         MVI   DATELANG,C'A'           ASSEMBLER F LEVEL
         WRITE DECB1,SF,PDSDCB,RECORD,'S'   *
         CHECK DECB1                   *
         NOTE  PDSDCB                  *
         ST    1,ASSEMBLR+8            *
         STOW  PDSDCB,ASSEMBLR,A       *
         SPACE 2
         MVI   DATELANG,C'B'           AFORTRAN G LEVEL
         WRITE DECB2,SF,PDSDCB,RECORD,'S'
         CHECK DECB2                   **
         NOTE  PDSDCB                  **
         ST    1,FORTRAN+8             **
         STOW  PDSDCB,FORTRAN,A        **
         SPACE 2
         MVI   DATELANG,C'C'            PL/1 VERS 5.2 B LEVEL
         WRITE DECB3,SF,PDSDCB,RECORD,'S'  ***
         CHECK DECB3                   ***
         NOTE  PDSDCB                  ***
         ST    1,PL1+8                 ***
         STOW  PDSDCB,PL1,A            ***
         SPACE 3
         MVI   DATELANG,C'D'            RPG E LEVEL
         WRITE DECB4,SF,PDSDCB,RECORD,'S' ****
         CHECK DECB4                    ' ****
         NOTE  PDSDCB                   ' ****
         ST    1,RPGE+8                 ' ****
         STOW  PDSDCB,RPGE,A            ' ****
         SPACE 3
         MVC   DATELANG(2),=C'C2'       PL/1 OPTIMIZER
         WRITE DECB5,SF,PDSDCB,RECORD,'S' *****
         CHECK DECB5                    ' *****
         NOTE  PDSDCB                   ' *****
         ST    1,PL1OPTIM+8             ' *****
         STOW  PDSDCB,PL1OPTIM,A         *****
         SPACE 3
         CLOSE (PDSDCB)
         SPACE 3
         WTO   'SSILIB CREATED SUCCESSFULLY'
         L     13,4(13)
         RETURN (14,12),RC=0
*
*        ERROR ROUTINE
*
ERROR    WTO   'SSILIB ERROR ON CREATION - HELP !!'
         ABEND 1212,DUMP
         SPACE 3
PDSDCB   DCB   DDNAME=SSIFILE,DSORG=PO,SYNAD=ERROR,MACRF=(W),LRECL=80
RECORD   DS    0CL80
SSI      DC    C' SETSSI '
CONVDATE DS    CL6
DATELANG DC    CL2' 1'
SPARE    DC    CL64' '
DIRENTRY DS    0CL36
ASSEMBLR DC    CL12'ASSMBLRF    '
FORTRAN  DC    CL12'FORTRANG    '
PL1      DC    CL12'PL1V52B     '
RPGE     DC   CL12'RPGE        '
PL1OPTIM DC    CL12'PL1OPTIM    '
DATE     DS    0CL7
FILLER   DS    C
DAY      DS    CL2
MONTH    DS    CL2
YEAR     DS    CL2
DUBWD    DS    D
EDMASK   DC    X'402120'
SAVE1    DS    18F
ALIGN0   DS    0F
YEARTAB  DC    AL1(31),C'001'
         DC    AL1(28),C'002'
         DC    AL1(31),C'003'
         DC    AL1(30),C'004'
         DC    AL1(31),C'005'
         DC    AL1(30),C'006'
         DC    AL1(31),C'007'
         DC    AL1(31),C'008'
         DC    AL1(30),C'009'
         DC    AL1(31),C'010'
         DC    AL1(30),C'011'
         DC    AL1(31),C'012'
         LTORG
         END
