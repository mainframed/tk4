./ ADD   LIST=ALL,NAME=VTOCLIST
./ NUMBER  INCR=100,NEW1=100
         TITLE 'VTOCLIST - CONTROL CSECT FOR YEBDMAP'
VTOCLIST CSECT
         BEGIN (SAVE,,SAVE)
         YEBEQU
F1BASE   EQU   R9
         USING F1DSCB,F1BASE
F3BASE   EQU   R10
         USING F3DSCB,F3BASE
F4BASE   EQU   R9
         USING F4DSCB,F4BASE
F5BASE   EQU   R10
         USING F5DSCB,F5BASE
         EJECT
         MVC   PAGECT,=H'0'        SET PAGE COUNT TO ZERO
         MVC   LINECT,=H'999'      SET LINE COUNT TO 999
         MVC   AHEADING,=F'0'
         TIME
         SRL   R0,4
         ST    R0,PTIME          STORE TIME
         MVC   TIME,MASK1         MOVE MASK IN
         ED    TIME,PTIME        EDIT TIME
         ST    R1,PDATE
         UNPK  TRWORK,PDATE+2(8)
         TR    TRWORK,XLATE-240
         MVC   DAYNO,TRWORK
         CALL  XDATEDIT,(CVDATE)  CONVERT DATE
         MVC   DATE,CVDATE        MOVE DATE
         CALL  OPENPRT             OPEN SYSPRINT
         SR    R0,R0
         ST    R0,RETCODE
         STH   R0,VOLSDONE
         STC   R0,OPTIONS
         MVC   VOLCT,=H'0'         CLAER VOL COUNT TO ZERO
         L     R1,SAVE+4           LOAD ADDRESS OF HSA
         L     R1,24(R1)           LOAD ADDRESS OF ADDRESS LIST
         L     R2,0(R1)            R2 -> PARM FIELD
         CALL  VTOCPARM,((R2),PCWA)  READ PARM FIELD
         LH    R0,VOLCT            R0 = VOLUME COUNT
         LTR R0,R0                 TEST FOR ZERO
         BNZ   VOLSIN              IF NOT ZERO LEAVE ALONE
         MVC   VOLCT,=H'1'         OTHER WISE SET TO 1
VOLSIN   EQU   *
         TM    OPTIONS,OPPUNCH     PUNCHED OUTPUT REQUIRED?
         BZ    READVTOC              NO - CONTINUE
         CALL  OPENPCH             YES - OPEN SYSPUNCH
         SPACE 5
*              START TO READ VTOC
         SPACE
READVTOC SR    R0,R0               CLEAR R0
         STH   R0,PAGECT
         ST    R0,AHEADING
         MVC   LINECT,=H'999'
         LA    R1,=44X'FF'         R1 -> 44 X'FF'
         ST    R1,PF1DSCB
         LA    R1,ALOWEXT
         ST    R1,ALOWEXT
         STH   R0,FREECYLS
         STH   R0,AVTRAKS
         STH   R0,AVEXTS
         STH   R0,DSCOUNT
         LH    R0,VOLSDONE
         CH    R0,VOLCT
         BNL   ENDRUN
         AH    R0,=H'1'
         STH   R0,VOLSDONE
         BAL   R14,SETRIGHT
         MVC   DDVOLNO,DECUNPK+10  NEW DD CARD VOLUME NUMBER
         MVC   DCBDDNAM,VOLUMEXX   MOVE IN DDNAME
         DEVTYPE DCBDDNAM,DEVAREA  GET DEVICE TYPE
         LTR   R15,R15
         BNE   NODD
         CLI   DEVAREA+2,X'20'     DIRECT ACCESS DEVICE?
         BNE   NOTDA               NO
         MVC   DXXXX,DEVAREA+3     YES - MOVE DEVICE CODE
         LA    R2,D2301            R2 -> FIRST DEVICE GROUP
         USING DEVDET,R2
COMPDEV  CLC   DXXXX,DEVCODE       SEARCH FOR A MATCHING CODE
         BE    DEVFOUND                 BRANCH WHEN FOUND
         LA    R2,NEXTDEV          R2 -> NEXT DEVICE GROUP
         B     COMPDEV             COMPARE NEXT DEVICE
         SPACE 3
*              CHECK DEVICE TYPE
         SPACE
DEVFOUND CLC   DEVNAME,UNKNOWN     KNOWN DEVICE TYPE?
         BE    UNKDEV              NO - BRANCH
         ST    R2,ADEVDATA         STORE ADDRESS OF DEVICE STATISTICS
         RDJFCB (VTOC)
         MVC   TITLE,TITLE-1       CLEAR TITLE TO SPACES
         LA    R1,JFCB
         USING JFCB,R1
         MVC   VOLSER#,JFCBVOLS    MOVE VOLSER
         MVC   JFCBDSNM,F4ID       FILL DSNAME WITH X'04' F4DSCB ID
         DROP  R1
         OPEN  (VTOC,INPUT),TYPE=J
         TM    DCBOFLGS,X'10'      TEST FOR SUCCESFUL OPEN
         BZ    OPENFAIL            BRANCH IF NOT
         SPACE 3
*              SET UP VOLUME CARD
         SPACE
         SPACE
SETCARD  TM    OPTIONS,OPPUNCH     PUNCH REQUIRED?
         BZ    READF4              NO - READ FORMAT 4
         MVI   PUNCARD,SPACE       CLEAR PUNCH CARD
         MVC   PUNCARD+1(79),PUNCARD    TO SPACES
         MVC   PUNVSN,VOLSER#      VOLUME SERIAL
         MVC   PUNDATE,DATE        DATE OF RUN
         MVI   PUNCTYPE,C'1'       INDICATE VOLUME CARD
         PERFORM PUNCH
         SPACE 3
*              READ FORMAT 4 DSCB
         SPACE
READF4   MVC   VOLUME,VOLSER#      MOVE VOLSER TO PRINT TITLE
         MVC   TITLE(46),TABLE     MOVE IN TITLE
         MVC   ANEXT(28),VOLLABXT  RESET VOLUME LABEL EXTENT
         LA    R2,ANEXT            R2 -> VOLUME LABEL EXTENTS
         PERFORM SAVEXT            SAVE DETAILS OF EXTENTS
         ALLOCATE 148              ALLOCATE 148 BYTES FOR DSCB
         ST    R3,AF4DSCB          SAVE ADDRESS OF FORMAT 4 DSCB
         LR    F4BASE,R3           -> FORMAT 4 DSCB
         USING F4DSCB,F4BASE
         PERFORM READDSCB          READ FIRST DSCB
         PERFORM ABEND             END OF DATA FOUND
         CLI   DS4IDEMT,C'4'       SHOULD BE FORMAT 4 DSCB
         BNE   NOT4                NO - THEN SOMETHING IS WRONG
         MVC   ASCCHH(8),DS4LEXT   VTOC EXTENTS
         LA    R2,DS4IND           R2 -> EXTENT TYPE AND INDICATOR
         PERFORM CALTREXT          FIND NUMBER OF TRACKS IN VTOC
         STH   R0,ANOTK            STORE NUMBER OF TRACKS
         STH   R0,VTOCTRAK         STORE NUMBER OF TRACKS
         MVC   ASABS(4),FIRSTABS   STARTING AND ENDING ABSTRAKS
         MVC   AEXTSEQ,DS4EXSEQ    EXTENT SEQUENCE NUMBER
         LA    R0,=CL44'VOLUME TABLE OF CONTENTS'
         ST    R0,ADSNAME          STORE ADDRESS
         LA    R2,ANEXT            R2 -> ADDRESS OF NEXT EXTENT
         PERFORM SAVEXT            SAVE DETAILS OF VTOC EXTENT
         SPACE 3
*              READ FORMAT 5 DSCB
         SPACE
         ALLOCATE 152              ALLOCATE SPACE FOR FORMAT 5 DSCB
         ST    R3,AF5DSCB          SAVE ADDRESS OF FORMAT 5 DSCB
         LR    F5BASE,R3           -> FORMAT 5 DSCB
         USING F5DSCB,F5BASE
         PERFORM READDSCB          READ NEXT DSCB
         PERFORM ABEND             END OF FILE
         B     A00320              NORMAL EXIT
A00304   ALLOCATE 152
         ST    R3,DS5ANEXT         ADDRESS OF NEXT FORMAT 5 DSCB
         LA    R2,DS5PTRDS         R2 -> CCHH OF NEXT F5 DSCB
         LR    R10,R3              R10 -> LATEST F5 DSCB
         PERFORM OBNEXT            READ NEXT F5 DSCB
A00320   CLI   DS5FMTID,C'5'       FORMAT 5 DSCB?
         BNE   BADDSCB             NO? THEN THERE IS SOMETHING WRONG
         PERFORM FORMAT5           PROCESS F5 DSCB
         CLC   DS5PTRDS,=XL5'0'    ANOTHER DSCB?
         BNE   A00304              YES? THEN READ IT IN
         SR    R0,R0               CLEAR R0
         ST    R0,DS5ANEXT         NO MORE FORMAT 5 DSCB'S
         SPACE 5
*              READ FORMAT 1 DSCB'S & CHAIN IN ALPHABETIC ORDER
         SPACE
A00336   EQU   *
READF1   ALLOCATE 160              ALLOCATE SPACE FOR FORMAT 1 DSCB
         LR    R9,R3               R9 -> NEW DSCB
         USING F1DSCB,R9
A00340   PERFORM READDSCB          READ NEXT DSCB
         B     ENDVTOC             END OF FILE
A00348   CLI   DS1FMTID,C'1'       FORMAT 1 DSCB?
         BNE   A00340              NO? THEN READ NEXT DSCB
         LH    R1,DSCOUNT           R1 = DATA SET COUNT
         LA    R1,1(R1)            ADD 1
         STH   R1,DSCOUNT           DSCOUNT = NEW DATA SET COUNT
         L     R2,PF1DSCB          R2 -> FIRST FORMAT 1DSCB
         L     R3,AXF1DSCB         R3 -> XF1DSCB
A00364   CLC   DS1DSNAM,0(R2)      DATA SET NAME = ALL X'FF'
         BL    A00378              NO? THEN CONTINUE
         LR    R3,R2
         L     R2,152(R2)
         B     A00364
A00378   ST    R2,DS1ANEXT         R2 -> NEXT FORMAT 1 DSCB
         ST    R9,152(R3)          POINTER TO THIS DSCB
         SR    R0,R0               CLEAR R0
         ST    R0,DS1AF3          SET ADDRESS OF F3DSCB TO ZERO
         CLC   DS1PTRDS,=XL5'0'    FORMAT 2 OR FORMAT 3 DSCB?
         BE    A00336              NO? THEN READ NEXT DSCB
         SPACE 5
*              READ FORMAT 3 DSCB
         SPACE
         ALLOCATE 152
         LR    R10,R3              R10 ->
         USING F3DSCB,R10
         LA    R2,DS1PTRDS
A00400   PERFORM OBNEXT            READ NEXT DSCB
         CLI   DS3FMTID,C'3'       FORMAT 3 DSCB?
         BE    A00450              YES?
         CLC   DS3PTRDS,=XL5'0'    NO?    POINTER = ZERO?
         BE    A00336
         LA    R2,DS3PTRDS         NO?
         B     A00400
A00450   ST    R10,DS1AF3          SAVE ADDRESS OF FORMAT 3 DSCB
         B     A00336              READ NEXT DSCB
         SPACE 5
*              END OF VTOC
         SPACE
ENDVTOC  CLOSE VTOC
         L     R9,AF4DSCB
         USING F4DSCB,R9
         MVC   PRLINE+1(19),=C'DEVICE DESCRIPTION:'
         MVC   PRLINE+22(5),=C'TYPE='
         L     R2,ADEVDATA         R2 -> NAME OF DEVICE
         MVC   PRLINE+27(15),1(R2) MOVE DEVICE NAME
         MVC   PRLINE+45(7),=C'NOCYLS=' NUMBER OF CYLINDERS
         LA    R2,DS4DEVSZ         R2 -> NO OF CYLS
         PERFORM CONVDEC2
         MVC   PRLINE+52(5),DECLADJ
         MVC   PRLINE+57(9),=C'TRKS/CYL=' TRACKS PER CYL
         LA    R2,DS4DEVSZ+2       R2 -> NO OF TRKS/CYL
         PERFORM CONVDEC2
         MVC   PRLINE+66(4),DECLADJ
         MVC   PRLINE+73(8),=C'TRKSIZE=' TRACK SIZE
         LA    R2,DS4DEVTK         R2 -> TRACK LENGTH
         PERFORM CONVDEC2
         MVC   PRLINE+81(5),DECLADJ
         MVC   PRLINE+88(9),=C'DSCB/TRK='  DSCB/TRK
         LA    R2,DS4DEVDT         R2 -> N0 OF DSCBS/TRK
         PERFORM CONVDEC1
         MVC   PRLINE+97(3),DECLADJ
         MVC   PRLINE+102(8),=C'PDS/TRK=' DIRECTORY BLOCKS PER TRACK
         LA    R2,DS4DEVDB
         PERFORM CONVDEC1
         MVC   PRLINE+110(3),DECLADJ
         MVI   PRCTL,C'0'          WRITE AFTER 2
         MVC   LINES,=H'2'         LINES = 2
         PERFORM PRINT
         SPACE 3
*              PUT VTOC DESCRIPTION
         SPACE
         MVC   PRLINE+1(17),=C'VTOC DESCRIPTION:'  VTOC DESCRIPTION
         MVC   PRLINE+20(10),=C'NUM DSCBS='
         SR    R0,R0               CLEAR R0
         IC    R0,DS4DEVDT         R0 = NO OF DSCBS/TRK
         MH    R0,VTOCTRAK         MULTIPLY BY NO OF TRACKS IN VTOC
         PERFORM SETRIGHT
         MVC   PRLINE+30(5),DECLADJ
         MVC   PRLINE+37(12),=C'AVAIL DSCBS=' AVAILABLE DSCBS
         LA    R2,DS4DSREC         NO OF AVAILABLE DSCBS
         PERFORM CONVDEC2
         MVC   PRLINE+49(5),DECLADJ
         MVC   PRLINE+55(12),=C'VTOC EXTENT='   VTOC EXTENT
         MVI   PRLINE+71,C'.'
         MVI   PRLINE+76,C'-'
         MVI   PRLINE+81,C'.'
         UNPK  TRWORK,DS4LEXT(8)   LOW EXTENT
         TR    TRWORK,XLATE-240    TRANSLATE TO PRINT
         MVC   PRLINE+67(4),TRWORK STARTING CYLINDER
         MVC   PRLINE+72(4),TRWORK+4  AND TRACK
         UNPK  TRWORK,DS4HEXT(8)   HIGH EXTENT
         TR    TRWORK,XLATE-240
         MVC   PRLINE+77(4),TRWORK ENDING CYLINDER
         MVC   PRLINE+82(4),TRWORK+4  AND TRACK
         MVC   PRLINE+88(7),=C'NUMALT='
         LA    R2,DS4NOATK         R2 -> NO OF ALT TRACKS
         PERFORM CONVDEC2
         MVC   PRLINE+95(5),DECLADJ
         MVC   PRLINE+102(13),=C'NEXTALT=    .'
         UNPK  TRWORK,DS4CCHH(8)   CCHH OF NEXT ALTERNATE TRACK
         TR    TRWORK,XLATE-240
         MVC   PRLINE+110(4),TRWORK  NEXT ALTERNATE CYLINDER
         MVC   PRLINE+115(4),TRWORK+4   AND TRACK
         MVI   PRCTL,C'0'          WRITE AFTER 2
         MVC   LINES,=H'2'         LINES = 2
         PERFORM PRINT
         SPACE 3
*              LIST AVAILABLE SPACE
         SPACE
         MVC   PRLINE+1(16),=C'AVAILABLE SPACE:'
         LH    R0,AVTRAKS          NO OF AVAILABLE TRACKS
         PERFORM SETRIGHT
         MVC   PRLINE+18(5),DECRADJ+7
         MVC   PRLINE+24(9),=C'TRACKS IN'
         LH    R0,AVEXTS           R0 = NO OF AVAILABLE EXTENTS
         PERFORM SETRIGHT
         MVC   PRLINE+34(4),DECRADJ+8
         MVC   PRLINE+39(18),=C'EXTENTS, INCLUDING'
         LH    R0,FREECYLS         R0 = NO OF FULL CYLINDERS
         PERFORM SETRIGHT
         MVC   PRLINE+58(4),DECRADJ+8
         MVC   PRLINE+63(15),=C'FULL CYLINDERS.'
         MVI   PRCTL,C'0'          WRITE AFTER 2
         MVC   LINES,=H'2'         LINES = 2
         PERFORM PRINT
         SPACE 3
*              DUMP FORMAT 4 AND FORMAT 5 DSCB'S
         SPACE
         TM    OPTIONS,OPDUMP           DUMP REQUIRED
         BZ    A00584
         LR    R2,R9               R2 -> FORMAT 4 DSCB
         LA    R3,DS4FDAD          R3 -> CCHHR OF FORMAT 4 DSCB
         PERFORM HEXDUMP
         L     R10,AF5DSCB
         USING F5DSCB,R10
A00564   LTR   R10,R10             FORMAT 5 DSCB?
         BZ    A00570              NO
         LR    R2,R10
         LA    R3,DS5FDAD          R3 -> CCHHR OF FORMAT 5 DSCB
         PERFORM HEXDUMP
         L     R10,DS5ANEXT        R10 -> NEXT FORMAT 5 DSCB
         B     A00564
         SPACE 3
*              PUT FIRST HEADING
         SPACE
A00570   MVI   PRCTL,C'0'          WRITE AFTER 2
         MVC   LINES,=H'2'         LINES = 2
         PERFORM PRINT
A00584   L     R9,AXF1DSCB         R9 -> XF1DSCB
         LA    R1,DSHEAD           R1 -> DSHEAD
         ST    R1,AHEADING           AHEADING = A(DSHEAD)
         MVC   PRSTART(140),DSHEAD PUT FIRST DATA SET HEADER
         PERFORM PRINT
         MVC   PRSTART(140),DSUNLINE  AND UNDERLINE
         PERFORM PRINT
         PERFORM PRINT             PUT BLANK LINE
         OI    OPBYTE1,SETBLANK    CLEAR BLANK LINE AT TOP OF PAGES
         SPACE 5
*              PROCESS FORMAT 1 DSCB
         SPACE
         USING F1DSCB,F1BASE
         USING F3DSCB,F3BASE
FORMAT1  L     F1BASE,DS1ANEXT     R9 -> NEXT FORMAT 1 DSCB
         CLC   DS1DSNAM,=44X'FF'   DSNAME = ALL X'FF'?
         BE    ENDLIST             YES - END OF VTOC
         L     F3BASE,DS1AF3      R10 -> FORMAT 3 DSCB OR ZEROS
         ST    F1BASE,AF1DSCB      SAVE ADDRESS OF FORMAT 1 DSCB
         ST    F3BASE,AF3DSCB      SAVE ADDRESS OF FORMAT 3 DSCB
         PERFORM CALCEXT           CALCULATE DATA SET EXTENTS
         MVC   PRLINE+1(44),DS1DSNAM  DATA SET NAME
         MVC   PRLINE+46(6),DS1DSSN   VOLSER
         LA    R2,DS1VOLSQ
         PERFORM CONVDEC2
         MVC   PRLINE+53(4),DECUNPK+8
         LA    R2,DS1CREDT         R2 -> CREATION DATE
         LA    R3,PRLINE+59
         PERFORM DATECONV
         LA    R2,DS1EXPDT         R2 -> EXPIRY DATE
         LA    R3,PRLINE+68
         PERFORM DATECONV
         SPACE 3
*              DATA SET ORGANISATION
         SPACE
         LA    R2,PRLINE+78
         TM    DS1DSORG,X'80'      INDEXED SEQUENTIAL ?
         BZ    A00634              NO?
         MVC   0(2,R2),=C'IS'
         LA    R2,2(R2)
         B     UNMOVE
A00634   TM    DS1DSORG,X'40'      SEQUENTIAL?
         BZ    A00646              NO?
         MVC   0(2,R2),=C'PS'
         LA    R2,2(R2)
         B     UNMOVE
A00646   TM    DS1DSORG,X'20'      DIRECT ACCESS?
         BZ    A00658              NO?
         MVC   0(2,R2),=C'DA'
         LA    R2,2(R2)
         B     UNMOVE
A00658   TM    DS1DSORG,X'02'      PARTITIONED?
         BZ    UNMOVE              NO?
         MVC   0(2,R2),=C'PO'
         LA    R2,2(R2)
UNMOVE   TM    DS1DSORG,X'01'      UNMOVEABLE?
         BZ    A00676              NO?
         MVI   0(R2),C'U'
A00676   UNPK  TRWORK,DS1OPTCD(8)  OPTIONS
         TR    TRWORK,XLATE-240
         MVC   PRLINE+104(2),TRWORK
         SPACE 3
*              RECORD FORMAT
         SPACE
         LA    R2,PRLINE+82
         TM    DS1RECFM,X'C0'      UNDEFINED?
         BZ    A00680              NO?
         MVI   0(R2),C'U'
         BO    A00680              IF NOT MIXED THEN BRANCH
         TM    DS1RECFM,X'80'      FIXED?
         MVI   0(R2),C'F'
         BO    A00680              OR
         MVI   0(R2),C'V'          VARIABLE?
A00680   LA    R2,1(R2)
         TM    DS1RECFM,X'20'
         BZ    A00690              NO?
         MVI   0(R2),C'T'
         LA    R2,1(R2)
A00690   TM    DS1RECFM,X'10'      BLOCKED?
         BZ    A00700              NO?
         MVI   0(R2),C'B'
         LA    R2,1(R2)
A00700   TM    DS1RECFM,X'08'      STANDARD OR SPANNED?
         BZ    A00710              NO?
         MVI   0(R2),C'S'
         LA    R2,1(R2)
A00710   TM    DS1RECFM,X'06'      PRINT CONTROL CHARACTERS?
         BNM   A00720              NO?
         TM    DS1RECFM,X'04'      ASCII CHARACTERS?
         MVI   0(R2),C'A'
         BO    A00720              OR
         MVI   0(R2),C'M'          MACHINE CHARACTERS?
A00720   LA    R2,DS1BLKL          R2 -> BLOCK LENGTH
         PERFORM   CONVDEC2
         MVC   PRLINE+88(5),DECRADJ+7
         LA    R2,DS1LRECL         R2 -> LOGICAL RECORD LENGTH
         PERFORM   CONVDEC2
         MVC   PRLINE+94(5),DECRADJ+7
         LA    R2,DS1KEYL          R2 -> KEY LENGTH
         PERFORM   CONVDEC1
         MVC   PRLINE+100(3),DECRADJ+9
         SPACE 3
*              ALLOCATIONS
         SPACE
         CLI   DS1NOEPV,X'00'      NO OF EXTENTS = 0?
         BNE   TRKAL               NO - CONTINUE
         MVC   PRLINE+107(15),=C' **DUMMY DSCB**'
         B     SCALO
TRKAL    EQU   *
         L     R0,TRAKSAL          R0 = TRACKS ALLOCATED
         PERFORM   SETRIGHT
         MVC   PRLINE+108(5),DECRADJ+7
         CLC   DS1LSTAR,=XL3'0'
         BE    A00740
         LH    R0,DS1LSTAR
         AH    R0,=H'1'
         PERFORM   SETRIGHT
         MVC   PRLINE+114(5),DECRADJ+7
A00740   LA    R2,DS1NOEPV
         PERFORM   CONVDEC1
         MVC   PRLINE+120(2),DECRADJ+10
SCALO    EQU   *
         LA    R2,DS1SCALO+1       R2 -> SECONDARY ALLOCATION
         PERFORM   CONVDEC3
         MVC   PRLINE+123(5),DECRADJ+7
         TM    DS1SCALO,X'C0'      ALLOCATION IN CYLINDERS?
         BNO   A00778              NO? TRY ABSTRAK
         MVI   PRLINE+129,C'C'
         B     A00798
A00778   BNZ   A00784              NOT ABSTRAK? TRY BLOCKS
         MVI   PRLINE+129,C'A'
         B     A00798
A00784   TM    DS1SCALO,X'80'      BLOCKS?
         BO    A00794              NO? MUST BE TRACKS THEN
         MVI   PRLINE+129,C'B'
         B     A00798
A00794   MVI   PRLINE+129,C'T'
A00798   MVI   PRCTL,C' '          WRITE AFTER 1
*              IF DATA SET IS PARTITIONED AND GREATER THAN 'PERCENT'  *
*              FULL THEN PAD DSNAME WITH ASTERISKS                    *
*              'PERCENT' DEFAULTS TO  80%                             *
         TM    DS1DSORG,X'02'      PARTITIONED ?                    MKB
         BZ    A00904              NO - SKIP                        MKB
         SR    R2,R2
         LH    R3,DS1LSTAR              NO OF TRACKS USED
         CLC   DS1LSTAR,=XL3'0'         ANY TRACKS USED
         BE    A00904                   NO
         LA    R3,1(R3)                 ROUND TO WHOLE TRACKS
         MH    R3,=H'100'               TRACKS USED * 100
         L     R4,TRAKSAL               NO OF TRACKS ALLOCATED
         DR    R2,R4                    CALC % USED
         CH    R3,PERCENT               WARNING REQUIRED
         BL    A00904                   (3
         LA    R1,PRLINE+44             YES - ADDR END OF DSN
         LA    R2,44                    LENGTH OF DSN
A00900   CLI   0(R1),C' '               SPACE FOR *?
         BNE   A00902                   NO - LAST CHAR OF DSN FOUND
         MVI   0(R1),C'*'               PAD WITH *
         BCTR  R1,R0
         BCT   R2,A00900
A00902   MVI   1(R1),C' '               INSERT SPACE BEFORE *
A00904  PERFORM PRINT                   PRINT DATA SET LINE
         SPACE 4
*              DUMP FORMAT 1 AND FORMAT 3 DSCB'S
         SPACE
         TM    OPTIONS,OPDUMP      DUMP REQUIRED?
         BZ    TESTEXT             NO
         LA    R2,F1DSCB           R2 -> FORMAT 1 DSCB
         ST    R2,AF1DSCB          SAVE ADDRESS
         LA    R3,DS1FDAD          R3 -> FULL D-A ADDRESS
         PERFORM HEXDUMP
         LTR   R10,R10             FORMAT 3 DSCB?
         BZ    NOF3                NO
         LA    R2,F3DSCB           R2 -> FORMAT 3 DSCB
         ST    R2,AF3DSCB          SAVE ADDRESS
         LA    R3,DS3FDAD          R3 -> FULL D-A ADDRESS
         PERFORM HEXDUMP
NOF3     MVC   LINES,=H'1'         WRITE AFTER SPACING 1
         PERFORM PRINT             PRINT BLANK LINE
         SPACE 3
*              CHECK IF EXTENT LISTING REQUIRED
         SPACE
TESTEXT  TM    OPTIONS,OPEXT       EXTENT LISTING REQUIRED?
         BZ    SETDSCD             NO - BYPASS LISTING
         CALL  VTOCEXT,PCWA        LINK TO EXTENT LISTER
         SPACE 3
*              SET UP DATA SET CARD
         SPACE
SETDSCD  TM    OPTIONS,OPPUNCH     PUNCH REQUIRED?
         BZ    LISTPDS             NO - BYPASS PUNCH
         MVI   PUNCARD,SPACE       CLEAR PUNCH CARD
         MVC   PUNCARD+1(79),PUNCARD    TO SPACES
         MVC   PUNVSN,VOLSER#      VOLUME SERIAL
         MVC   PUNDATE,DATE        DATE OF RUN
         MVC   PUNDSN,DS1DSNAM     DATA SET NAME
         CLC   DS1LRECL,=H'80'     LRECL = 80?
         BNE   TRYLOAD             NO - SEE IF IT IS A LOAD LIBRARY
         TM    DS1RECFM,X'80'      IS IT ALSO FIXED LENGTH?
         BZ    TRYLOAD             NO - CAN'T BE CARD IMAGES
         TM    DS1RECFM,X'C0'      BUT IT COULD BE UNDEFINED
         BO    TRYLOAD             SO RULE THAT OUT
         MVI   PUNTYPE,C'C'        YES - THEN IT IS CARD IMAGES
         B     PUTDSCD             OK TO PUT OUT CARD
TRYLOAD  CLI   DS1RECFM,X'C0'      RECFM = U?
         BNE   NOTYPE              NEITHER CARD OR LOAD
         TM    DS1DSORG,X'02'      PARTITIONED?
         BZ    NOTYPE              NO - CANT BE LOAD LIBRARY
         LA    R2,DEVCONS          R1 -> DEVICE CONSTANTS
         USING DEVDET,R2
COMPBLK  CLC   DS1BLKL,DEVMXREC    IS BLKSIZE SAME AS ANY MAX REC?
         BE    SETLOAD             YES - IT'S A LOAD LIBRARY
         LA    R2,NEXTDEV          TRY NEXT DEVICE
         CLI   DEVCODE,0           END OF TABLE?
         BE    TRYONEK             MAY BE 1K
         B     COMPBLK             TRY AGAIN
TRYONEK  CLC   DS1BLKL,=H'1024'    COULD BE 1K?
         BNE   NOTYPE              NO - NOT A LOAD LIBRARY
SETLOAD  MVI   PUNTYPE,C'L'        YES - IT IS A LOAD LIBRARY
         B     PUTDSCD
NOTYPE   MVI   PUNTYPE,C' '        CLEAR PUNTYPE
PUTDSCD  EQU   *
         MVI   PUNCTYPE,C'2'       INDICATE DATA SET
         PERFORM PUNCH             PUNCH OUT CARD
         SPACE 3
*              CALL PDS LISTER
         SPACE
         CLI   OPBYTE2,OPPUNCH     DECK OPTION ONLY ?               MKB
         BE    TESTPO              YES - SEE IF PARTITIONED         MKB
LISTPDS  TM    OPTIONS,OPPDS       PDSLISTING REQUIRED?
         BZ    USAGE                    NO
TESTPO   DS    0H                                                   MKB
         TM    DS1DSORG,X'02'      PARTITIONED?
         BZ    USAGE                    NO
         CLI   DS1NOEPV,X'00'      NO OF EXTENTS = ZERO?
         BZ    USAGE                    YES
         CALL  VTOCPDS,PCWA
         SPACE 4
*              DISC USAGE
         SPACE
USAGE    EQU   *
         TM    OPTIONS,OPUSAGE          DISC USAGE REQUIRED?
         BZ    FORMAT1                  NO - PROCESS NEXT F1DSCB
         MVI   PUNCARD,SPACE            CLEAR PUNCH CARD
         MVC   PUNCARD+1(79),PUNCARD         TO SPACES
         MVC   USEVSN,VOLSER#           VOLUME SERIAL
         MVC   USEDSN,DS1DSNAM          DATA SET NAME
         MVC   USEDATE,DATE             TODAYS DATE
         MVI   PUNCTYPE,C'9'            ID CHARACTER
         CALL  VTOCUSE,(PCWA,PUNCARD)   USAGE ROUTINE
         B     FORMAT1
         SPACE 4
*              END OF VTOC SUMMARY
         SPACE
*              END OF VTOC SUMMARY
         SPACE
ENDLIST  MVC   PRLINE+49(14),=C'<<<<< END VTOC'
         LH    R0,DSCOUNT          R0 = TOTAL NUMBER OF DATA SETS
         PERFORM SETRIGHT          CONVERT TO DECIMAL
         MVC   PRLINE+64(4),DECRADJ+8 NUMBER OF DATA SETS
         MVC   PRLINE+69(15),=C'DATA SETS >>>>>'
         MVI   PRCTL,C'0'          WRITE AFTER 2
         MVC   LINES,=H'2'         LINES = 2
         PERFORM PRINT
         SPACE 4
*              CHECK IF CATALOGUE LISTING REQUIRED
         SPACE
         TM    OPTIONS,OPCATLG          CATLG LISTING REQUIRED?
         BZ    PTAMAP                   NO
         CALL  VTOCCAT,(PCWA,PF1DSCB)   YES
         SPACE 4
*              PRINT TRACK ALLOCATION MAP
         SPACE
PTAMAP   MVC   LINECT,=H'999'
         LA    R0,TRALHEAD         R0=
         ST    R0,AHEADING
         MVC   TITLE,TITLE-1       CLEAR TITLE TO SPACES
         MVC   TRVOLUME,VOLSER#    MOVE VOLSER TO TITLE
         MVC   TROWNID,OWNERID     OWNER ID
         MVC   TITLE(49),TRALLOC   MOVE IN TITLE
         L     R9,ALOWEXT          R9 -> ALOWEXT
         USING DSEXT,R9
         L     R8,ADEVDATA         R8 -> DEVICE STATISTICS
         USING DEVDET,R8           OVERLAY MASK
         L     R4,=F'-1'
         SR    R5,R5               CLEAR R5
A00856   CLC   EXTSCCHH,=F'-1'     LAST EXTENT PROCESSED?
         BE    A00940              YES?
         CLC   EXTEABS,DEVTOTAL    IS ENDING ABSTRAK LESS THAN TOTAL?
         BNL   A00905              NO - INVALID EXTENT
         CLC   EXTSABS,EXTEABS     IS START HIGHER THAN END?
         BH    A00905              YES - INVALID EXTENT
         LH    R0,EXTSABS          NO - R0 = STARTING ABSTRAK
         SR    R0,R4               SUBTRACT PREVIOUS ENDING ABSTRAK
         S     R0,=F'1'            PLUS 1
         BZ    A00894              RESULT SHOULD BE ZERO
         BP    A00910              SOME TRACKS MISSING?
         MVC   PRLINE+19(7),=C'OVERLAP' THERE MUST BE AN OVERLAP
         CH    R4,EXTEABS          R4 < ENDING ABSTRAK
         BNL   A00898              NO - THEN BYPASS LOAD
A00894   LH    R4,EXTEABS         R4 = ENDING ABSTRAK
A00898   LH    R0,EXTNOTRK         R0 = NUMBER OF TRACKS IN EXTENT
         AR    R5,R0
         PERFORM   SETRIGHT
         MVC   PRLINE+50(5),DECRADJ+7
         B     A00915
A00905   MVC   PRLINE+12(14),=C'INVALID EXTENT'
         B     A00915
A00910   PERFORM   SETRIGHT
         MVC   PRLINE+12(14),=C'TRACKS MISSING'
         MVC   PRLINE+6(5),DECRADJ+7
         MVI   PRCTL,C' '          WRITE AFTER 1
         MVC   LINES,=H'1'
         PERFORM PRINT
         B     A00894
A00915   UNPK  TRWORK,EXTSCCHH(8)  UNPACK FOR TRANSLATE
         TR    TRWORK,XLATE-240
         MVC   PRLINE+28(4),TRWORK
         MVI   PRLINE+32,X'4B'
         MVC   PRLINE+33(4),TRWORK+4
         UNPK  TRWORK,EXTECCHH(8)  UNPACK FOR TRANSLATION
         TR    TRWORK,XLATE-240
         MVC   PRLINE+39(4),TRWORK
         MVI   PRLINE+43,C'.'
         MVC   PRLINE+44(4),TRWORK+4
         TM    EXTTYPND,X'FF'      TEST FOR NO EXTENT NUMBER
         BO    A00926
         SR    R0,R0
         IC    R0,EXTTYPND         R0 = EXTENT NUMBER
         AH    R0,=H'1'
         PERFORM   SETRIGHT
         MVC   PRLINE+57(2),DECRADJ+10
A00926   L     R1,EXTADSN          R1 -> DATA SET NAME, ETC.
         MVC   PRLINE+62(44),0(R1)
         MVI   PRCTL,C' '          WRITE AFTER 1
         MVC   LINES,=H'1'         LINES = 1
         PERFORM PRINT
         L     R9,EXTANEXT         R9 -> NEXT EXTENT BLOCK
         B     A00856
A00940   LH    R0,DEVTOTAL
         SR    R0,R4
         S     R0,=F'1'
         BNP   A00966
         PERFORM   SETRIGHT
         MVC   PRLINE+12(14),=C'TRACKS MISSING'
         MVC   PRLINE+6(5),DECRADJ+7
         MVI   PRCTL,C' '          WRITE AFTER 1
         MVC   LINES,=H'1'         LINES = 1
         PERFORM PRINT
A00966   EQU   *
         LR    R0,R5
         PERFORM   SETRIGHT
         MVC   PRLINE+50(5),DECRADJ+7
         MVC   PRLINE+57(20),=C'TRACKS ACCOUNTED FOR'
         MVI   PRCTL,C'0'          WRITE AFTER 2
         MVC   LINES,=H'2'         LINES = 2
         PERFORM PRINT
         MVC   PRLINE+52(28),=C'<<<<< END OF TRACK MAP >>>>>'
         MVI   PRCTL,C'0'          WRITE AFTER 2
         MVC   LINES,=H'2'         LINES = 2
         PERFORM PRINT
         PERFORM FREECORE          FREE ALL GOTTEN CORE
         B     READVTOC              PROCESS NEXT VOLUME
         DROP  R8
         DROP  R9
         SPACE 3
*              DEVICE ERROR MESSAGES - RETURN CODE = 4
         SPACE
NODD     MVC   PRLINE+41(19),=C'CANNOT FIND DD CARD'
         B     PUTERR4
NOTDA    MVC   PRLINE+41(27),=C'DEVICE IS NOT DIRECT ACCESS'
         B     PUTERR4
UNKDEV   MVC   PRLINE+41(19),=C'UNKNOWN DEVICE TYPE'
         B     PUTERR4
OPENFAIL MVC   PRLINE+41(27),=C'ATTEMPT TO OPEN VTOC FAILED'
         B     PUTERR4
PUTERR4  MVC   PRLINE+10(28),=C'ERROR CONCERNING ''VOLUME01'':'
         MVC   PRLINE+28(8),VOLUMEXX
         MVI   PRCTL,C' '          WRITE AFTER 1
         MVC   LINES,=H'1'         LINES = 1
         PERFORM PRINT
         LA    R0,4                RETURN CODE FOR THIS GROUP
         C     R0,RETCODE          LESS THAN HIGHEST RETURN CODE?
         BL    READVTOC              YES - PROCESS NEXT VOLUME
         ST    R0,RETCODE          NO - HIGHEST RETURN CODE = 4
         B     READVTOC              PROCESS NEXT VOLUME
         SPACE 3
*              DEVICE ERROR MESSAGES - RETURN CODE =8
         SPACE
NOT4     MVC   PRLINE+41(23),=C'FIRST DSCB NOT FORMAT 4'
         LA    R2,9
         B     PUTERR8
BADDSCB  MVC   PRLINE+41(33),=C'BAD DSCB, SHOULD HAVE BEEN TYPE 5'
         LA    R2,10
         B     PUTERR8
PUTERR8  MVC   PRLINE+10(28),=C'ERROR CONCERNING ''VOLUME01'':'
         MVC   PRLINE+28(8),VOLUMEXX
         MVI   PRCTL,C' '          WRITE AFTER 1
         MVC   LINES,=H'1'         LINES = 1
         PERFORM PRINT
         LA    R3,140(R2)          R3 ->
         PERFORM HEXDUMP
         CLOSE VTOC
         LA    R0,8                RETURN CODE FOR THIS GROUP
         C     R0,RETCODE          LESS THAN EXISTING RETURN CODE?
         BL    READVTOC              YES - PROCESS NEXT VOLUME
         ST    R0,RETCODE          NO - RETURN CODE = 8
         B     READVTOC              PROCESS NEXT VOLUME
         SPACE 5
* READ DSCB ROUTINE
READDSCB ST    R14,RSAVE14         SAVE R14
         READ  ECB,SF,VTOC,(R3)
         CHECK ECB
         USING F1DSCB,R3
         MVC   DS1FDAD,DCBFDAD     MOVE FULL D-A ADDRESS
         L     R14,RSAVE14
         LA    R14,4(R14)          NORMAL RETURN
         EXIT
EOVTOC   L     R14,RSAVE14         END OF DATA ADDRESS
         EXIT
RSAVE14  DC    F'0'
         SPACE 5
* OBTAIN CONTINUATION DSCB'S
OBNEXT   ST    R14,OBTSAVE         SAVE R14
         MVC   DSCCHHR,0(R2)       CCHHR OF DSCB
         ST    R3,CONTDSCB+12      ADDRESS OF RECEIVING FIELD
         OBTAIN CONTDSCB
         LTR   R15,R15             SUCCESSFUL OBTAIN?
         BZ    SETFDAD             YES - CONTINUE
         PERFORM ABEND             NO - ABEND
SETFDAD  LA    R1,VTOC
         MVC   140(3,R3),5(R1)     SET FULL D-A ADDRESS
         MVC   143(5,R3),5(R2)     CCHHR
         L     R14,OBTSAVE         RESTORE R14
         EXIT
CONTDSCB CAMLST SEEK,DSCCHHR,VOLSER#,0
OBTSAVE  DC    F'0'                R14 SAVE AREA
DSCCHHR  DC    XL5'0'              CCHHR OF DSCB
         SPACE 5
*   CALCULATE DETAILS OF DATA SET EXTENTS
         SPACE
         USING F1DSCB,R9
         USING F3DSCB,R10
CALCEXT  ST    R14,CSAVE14
         SR    R5,R5
         ST    R5,TRAKSAL
         IC    R5,DS1NOEPV
         LTR   R5,R5
         BCR   8,R14
         SR    R3,R3               CLEAR R3
         SR    R4,R4               CLEAR R4
GETEX    EX    R0,LOADAEXT(R3)     POINT TO EACH EXTENT
         PERFORM CALTREXT
         AR    R4,R0
         MVC   ASCCHH(8),2(R2)     STARTING AND ENDING CCHH
         STH   R0,ANOTK            NUMBER OF TRACKS IN EXTENT
         MVC   AEXTSEQ,1(R2)       EXTENT SEQUENCE NUMBER
         MVC   ASABS(4),FIRSTABS      STARTING AND ENDING ABSTRAKS
         LA    R0,F1DSCB        R0 -> FORMAT 1 DSCB
         ST    R0,ADSNAME          ADSNAME -> DATA SET NAME
         LA    R2,ANEXT            R2 -> NEXT EXTENT
         PERFORM SAVEXT
         SR    R0,R0               CLEAR R0
         IC    R0,DS1NOEPV         R0 = NO OF EXTENTS
         CR    R0,R5               FIRST EXTENT?
         BNE   NOTIF1              NO - BYPASS MOVE
         MVC   DS1AEXT,ADEXT     YES - SAVE ADDRESS OF FIRST EXTENT
         MVC   PREVEXT,ADEXT       SAVE ADDRESS OF THIS BLOCK
         B     NEXTEXT
NOTIF1   L     R15,PREVEXT         R15 -> PREVIOUS EXTENT BLOCK
         USING DSEXT,R15
         MVC   EXTDNEXT,ADEXT      STORE ADDRESS IN PREVIOUS BLOCK
         MVC   PREVEXT,ADEXT       SAVE ADDRESS OF THIS BLOCK
NEXTEXT  EQU   *
         LA    R3,4(R3)
         BCT   R5,GETEX
         ST    R4,TRAKSAL
         L     R14,CSAVE14
         EXIT
         DROP  R15
CSAVE14  DC    F'0'
TRAKSAL  DC    F'0'
PREVEXT  DC    F'0'                ADDRESS OF PREVIOUS DS EXTENT
LOADAEXT LA    R2,DS1EXT1
         LA    R2,DS1EXT2
         LA    R2,DS1EXT3
         LA    R2,DS3EXT4
         LA    R2,DS3EXT5
         LA    R2,DS3EXT6
         LA    R2,DS3EXT7
         LA    R2,DS3EXT8
         LA    R2,DS3EXT9
         LA    R2,DS3EXT10
         LA    R2,DS3EXT11
         LA    R2,DS3EXT12
         LA    R2,DS3EXT13
         LA    R2,DS3EXT14
         LA    R2,DS3EXT15
         LA    R2,DS3EXT16
         SPACE 3
*              CALCULATE TRACKS PER EXTENT
         SPACE
*              ON ENTRY R2 -> DSCB EXTENT FIELD
         SPACE
CALTREXT L     R8,ADEVDATA         R8 -> DEVICE STATISTICS
         USING DEVDET,R8           OVERLAY MASK
         CLI   0(R2),X'00'         NO EXTENT?
         BCR   8,R14               RETURN IF EQUAL
         LA    R2,2(R2)
         BAL   R15,CALABS          CALCULATE FIRST ABSTRK
         STH   R0,FIRSTABS         SAVE FIRST ARSTRK
         LA    R2,4(R2)
         BAL   R15,CALABS          CALCULATE LAST ABSTRK
         STH   R0,LASTABS          SAVE LAST ABSTRK
         LH    R1,FIRSTABS         LOAD FIRST ABSTRAK
         SR    R0,R1               FIND NO OF TRACKS - 1
         AH    R0,=H'1'            R0 = NO OF TRACKS
         S     R2,=F'6'            SET R2 DOWN BY 6
         EXIT
* CALCULATE ABSOLUTE TRACK NO.
CALABS   SR    R0,R0               CLEAR R0
         CLI   DXXXX,X'05'        DEVICE TYPE= 2321?
         BNE   NOT2321
         IC    R0,0(R2)            INSERT BIN NO
         MH    R0,DEVBIN
         SR    R1,R1               CLEAR R1
         IC    R1,1(R2)            INSERT CYL NO
         MH    R1,DEVTK                MULTIPLY BY NO OF TRKSYCYL
         AR    R0,R1               ADD TO BIN NO
         SR    R1,R1               CLEAR R1
         IC    R1,2(R2)            INSERT FIRST HEAD NO
         MH    R1,DEVSTRIP         MULTIPLY BY ?
         AR    R0,R1               ADD TO CYL NO
         SR    R1,R1               CLEAR R1
         IC    R1,3(R2)            INSERT HEAD NO
         MH    R1,DEVHTRK          MULTIPLY BY NO OF HEADS/TRK
         AR    R0,R1               ADD TO HEAD NO
         BR    R15
         SPACE
NOT2321  MVC   CCHHWORK,0(R2)      MOVE CCHH
         LH    R0,CCWORK           R0= CYL NO
         MH    R0,DEVTK            MULTIPLY BY TRKS/CYL
         SR    R1,R1               CLEAR R1
         LH    R1,HHWORK           R1 = HEAD NO
         MH    R1,DEVHTRK          MULTIPLY BY HDS/TRK
         AR    R0,R1               R0 = ABSTRAK
         BR    R15                 EXIT
         SPACE
CCHHWORK DS    0F
CCWORK   DC    H'0'                CYL NO
HHWORK   DC    H'0'                HEAD NO
FIRSTABS DC    H'0'                FIRST ABSTRK
LASTABS  DC    H'0'                LAST ABSTRK
         SPACE 5                     PROCESS FORMAT 5 DSCB
*  PROCESS FORMAT 5 DSCB
         USING F5DSCB,R10
FORMAT5  ST    R14,FSAVE14
         L     R8,ADEVDATA         R8 -> DEVICE STATISTICS
         USING DEVDET,R8           OVERLAY MASK
         LA    R2,DS5AVEXT         R2 -> CCHH OF FIRST AVAILABLE EXTENT
         LA    R3,8                R3 = NO OF EXTENTS
FIRST8   PERFORM   CALCEXT5
         LA    R2,5(R2)            R2 -> NEXT EXTENT
         BCT   R3,FIRST8
         LA    R2,DS5MAVET
         LA    R3,18
NEXT18   PERFORM   CALCEXT5
         LA    R2,5(R2)
         BCT   R3,NEXT18
         L     R14,FSAVE14
         EXIT
CALCEXT5 ST    R14,GSAVE14         SAVE R14
         STM   R2,R5,GSAVE25       SAVE R2 THRU R5
         CLC   0(5,R2),=XL5'0'     = 0?
         BCR   8,R14               YES? THEN EXIT
         LH    R1,AVEXTS           NO OF EXTENTS
         LA    R1,1(R1)            PLUS 1
         STH   R1,AVEXTS           NEW OF EXTENTS
         MVC   FREEABS(4),0(R2)    DETAILS OF FREE EXTENT
         LH    R3,FREEABS          R3 = STARTING ABSTRAK
         LH    R4,FREECYLX         R4 = NO OF FULL CYLS IN THIS EXTENT
         SR    R5,R5               CLEAR R5
         IC    R5,4(R2)            R5= NO OF TRACKS
         STH   R3,FIRSTABS         ABSTRAK
         LH    R0,FREECYLS         NO OF FULL CYLINDERS AVAILABLE
         AR    R0,R4
         STH   R0,FREECYLS         NEW NO OF FULL CYLINDERS
         LR    R0,R4               R0 = NO OF CYLINDERS
         MH    R0,DEVTKCYL         X NO OF TRACKS PER CYLINDER
         AR    R0,R5               PLUS NO OF TRACKS IN EXTENT
         STH   R0,ANOTK            NO OF TRACKS IN EXTENT
         LR    R1,R0
         AH    R1,AVTRAKS          TOTAL NO OF TRACKS AVAILABLE
         STH   R1,AVTRAKS          NEW TOTAL
         AR    R0,R3               R0 = NO OF TRACKS + ABSTRAK
         BCTR  R0,R0               LESS 1
         STH   R0,LASTABS          R0 = LAST ABSTRAK OF EXTENT
         LH    R1,FIRSTABS         R1 = STARTING ABSTRAK
         PERFORM   CALCCCHH
         MVC   ASCCHH,ABSCCHH      STARTING CCHH
         LH    R1,LASTABS          R1 = LAST ABSTRAK
         PERFORM CALCCCHH
         MVC   AECCHH,ABSCCHH      ENDING CCHH
         MVC   ASABS(4),FIRSTABS   STARTING AND ENDING ABSTRAKS
         MVI   AEXTSEQ,X'FF'       INDICATE NO EXTENT NUMBER
         LA    R0,=CL44'   AVAILABLE'
         ST    R0,ADSNAME
         LA    R2,ANEXT            R2 -> NEXT EXTENT
         PERFORM   SAVEXT
         LM    R2,R5,GSAVE25       RESTORE R2 THRU R5
         L     R14,GSAVE14         RESTORE R14
         EXIT
FSAVE14  DC    F'0'
GSAVE14  DC    F'0'
GSAVE25  DC    4F'0'
         SPACE 5
* CALCULATE CCHH FROM ABSTRAK
         SPACE
*              ON ENTRY R1 = ABSTRAK
         SPACE
CALCCCHH SR    R0,R0               CLEAR R0
         ST    R0,ABSCCHH          ABSCCHH= ZERO
         L     R8,ADEVDATA         R8 -> DEVICE STATISTICS
         USING DEVDET,R8           OVERL1Y MASK
         CLI   DXXXX,X'05'        DEVICE TYPE= 2321?
         BNE   NOT2321A
         LH    R15,DEVBIN
         LTR   R15,R15
         BZ    A01020
         DR    R0,R15              APPLICABLE
         STC   R1,ABSCCHH             ONLY
         LR    R1,R0                   TO
         SR    R0,R0                     2321
A01020   LH    R15,DEVTK
         LTR   R15,R15
         BZ    A01030
         DR    R0,R15              R0/R1 = STARTING ABSTRAK
         STC   R1,ABSCCHH+1         NO OF CYL
         LR    R1,R0               NO OF TRACK
         SR    R0,R0               CLEAR R0
A01030   LH    R15,DEVSTRIP
         LTR   R15,R15
         BZ    A01050
         DR    R0,R15              APPLICABLE
         STC   R1,ABSCCHH+2           ONLY
         STC   R0,ABSCCHH+3            TO 2321
         EXIT
A01050   STC   R1,ABSCCHH+3         TRACK NO
         EXIT
NOT2321A LH    R15,DEVTK           R15 = TRKS/CYL
         LTR   R15,R15             TEST FOR ZERO I.E. 2301
         BZ    C2301
         DR    R0,R15              R0/R1 = ABSTRAK
         STH   R1,ABSCCHH          NO OF CYL
         STH   R0,ABSCCHH+2         NO OF TRACK
         EXIT
C2301    STH   R1,ABSCCHH+2         NO OF TRACK
         EXIT
ABSCCHH  DC    F'0'
         SPACE 5
*   SAVE DETAILS OF EXTENTS
         SPACE
SAVEXT   ST    R14,ESAVE14         SAVE R14
         STM   R3,R5,ESAVE35       SAVE R3 -R5
         ALLOCATE 28               ON RETURN R3 -> NEW CORE
         USING DSEXT,R3
         MVC   DSEXT1,0(R2)        MOVE EXTENT DETAILS TO NEW CORE
         L     R2,ALOWEXT          R2-> LOWEST EXTENT
         LA    R4,ALOWEXT          R4-> NEXT LOWEST EXTENT
COMPCCHH CLC   EXTSCCHH,OLDSCCHH(R2)    IF THIS EXTENT IS LOWER THAN
         BL    LINKCCHH                PREVIOUS RELINK THE CHAIN
         LR    R4,R2               R4 -> LOWEST EXTENT
         L     R2,0(R2)            R2 -> NEXT LOWEST EXTENT
         B     COMPCCHH            TRY AGAIN
LINKCCHH ST    R2,EXTANEXT STORE ADDRESS OF NEXT EXTENT
         ST    R3,0(R4)            STORE THIS ADDRESS IN PREVIOUS EXT
         ST    R3,ADEXT            SAVE ADDRESS OF EXTENT
         LM    R3,R5,ESAVE35       RESTORE R3 - R5
         L     R14,ESAVE14         RESTORE R14
         EXIT
ESAVE35  DC    3F'0'               R3 - R5 SAVE
ESAVE14  DC    F'0'                R14 SAVE
ADEXT    DC    F'0'                ADDRESS OF CURRENT EXTENT BLOCK
         SPACE 3
*              EXTENT BLOCK CONSTRUCTION AREA
         SPACE
ALOWEXT  DC    A(ALOWEXT)          ADDRESS OF LOWEST EXTENT
HIGHEXT  DC    F'-1'               COMPARE HIGH CCHH
ANEXT    DC    F'0'                ADDRESS OF NEXT EXTENT
ASCCHH   DC    F'0'                STARTING CCHH OF VOLUME LABEL
AECCHH   DC    F'0'                ENDING CCHH OF VOLUME LABEL
ANOTK    DC    H'1'                NO OF TRACKS
AEXTSEQ  DC    X'FF'               EXTENT SEQUENCE NUMBER
         DC    X'00'
ADSNAME  DC    A(NVOLLAB)          ADDRESS OF 'VOLUME LABEL'
ASABS    DC    H'0'                STARING ABSTRAK OF VOLUME LABEL
AEABS    DC    H'0'                ENDING ABSTRAK OF VOLUME LABEL
ADSEXT   DC    F'0'                ADDRESS OF NEXT DATA SET EXTENT
OLDSCCHH EQU   ASCCHH-ANEXT
         SPACE 3
*              VOLUME LABEL EXTENT
         SPACE
VOLLABXT DS    0CL28
         DC    F'0'                ADDRESS OF NEXT EXTENT
         DC    F'0'                STARTING CCHH OF VOLUME LABEL
         DC    F'0'                ENDING CCHH OF VOLUME LABEL
         DC    H'1'                NUMBER OF TRACKS
         DC    X'FF'               INDICATE ONLY EXTENT
         DC    X'00'               RESERVED
         DC    A(NVOLLAB)          ADDRESS OF NAME 'VOLUME LABEL'
         DC    H'0'                STARTING ABSTRAK OF VOLUME LABEL
         DC    H'0'                ENDING ABSTRAK OF VOLUME LABEL
         DC    F'0'                ADDRESS OF NEXT DATA SET EXTENT
         SPACE 4
DSAVE14  DC    F'0'
SAVE     DC    18F'0'                   SAVE AREA
         SPACE 3
DSEXT    MIPDSEXT
         SPACE 3
VTOCLIST CSECT
         SPACE
* CONVERT TO DECIMAL ROUTINE
         SPACE
CONVDEC1 LA    R3,1
         B     CONVDEC4
CONVDEC2 LA    R3,2
         B     CONVDEC4
CONVDEC3 LA    R3,3
CONVDEC4 SR    R0,R0
MOVEBIN  SLL   R0,8
         IC    R0,0(R2)
         LA    R2,1(R2)
         BCT   R3,MOVEBIN
SETRIGHT LPR   R0,R0
         CVD   R0,PNUM             PNUM = PACKED DECIMAL
         UNPK  DECUNPK,PNUM        UNPACK
         OI    DECUNPK+11,X'F0'    SET SIGN
         MVC   DECRADJ,MASK12      MOVE IN MASK
         ED    DECRADJ,PNUM+2      EDIT THE FIELD
         MVC   DECLADJ(11),DECRADJ+1 MOVE 11 CHARACTERS
SETLEFT  CLI   DECLADJ,X'40'       FIRST CHARACTER = SPACE?
         BCR   7,R14               NO - THEN EXIT
         MVC   DECLADJ(11),DECLADJ+1 MOVE 1 TO THE LEFT
         B     SETLEFT             TEST AGAIN
         SPACE
PNUM     DC    D'0'                PACKED NUMBER
MASK12   DC    X'40',9X'20',X'2120'     EDITING MASK
DECLADJ  DC    CL12' '             DECIMAL LEFT  ADJUSTED WITH SPACES
DECRADJ  DC    CL12' '             DECIMAL RIGHT ADJUSTED WITH SPACES
DECUNPK  DC    CL12' '             DECIMAL RIGHT ADJUSTED - LEAD ZEROS
         SPACE 5
*   DATE CONVERT ROUTINE
         SPACE
DATECONV ST    R14,DSAVE14
         SR    R0,R0
         IC    R0,0(R2)
         MH    R0,=H'1000'
         MVC   CVDATE(2),1(R2)
         AH    R0,CVDATE
         CVD   R0,CVDATE
         CALL  XDATEDIT,(CVDATE)   CONVERT DATE
         MVC   0(8,R3),CVDATE
         L     R14,DSAVE14
         BR    R14
*
         SPACE 5
* DUMP ROUTINE
         SPACE
HEXDUMP  ST    R14,HSAVE14         SAVE R14
         MVC   PRLINE+8(17),=C'BLK 1234.5678.90:'
         UNPK  TRWORK,DSEXT1+3(8)
         TR    TRWORK,XLATE-240
         MVC   PRLINE+12(4),TRWORK
         MVC   PRLINE+17(4),TRWORK+4
         MVC   PRLINE+22(2),TRWORK+8
         LA    R3,0(R2)
         LA    R4,PRLINE+35
         LA    R5,44
         PERFORM   DPFORMAT
         MVI   PRCTL,C'0'          WRITE AFTER BLANK LINE
         MVC   LINES,=H'4'         LINE COUNT FOR GROUP
         PERFORM PRINT
         MVC   PRLINE+8(13),=C'FORMAT N DSCB'
         MVC   PRLINE+15(1),44(R2)
         OI    PRLINE+15,X'F0'     CLEAR SIGN
         LA    R3,44(R2)
         LA    R4,PRLINE+26
         LA    R5,48
         PERFORM DPFORMAT
         MVI   PRCTL,C' '          WRITE AFTER SPACING 1
         MVC   LINES,=H'0'         NO LINE COUNT
         PERFORM PRINT
         LA    R3,92(R2)
         LA    R4,PRLINE+26
         LA    R5,48
         PERFORM   DPFORMAT
         MVI   PRCTL,C' '          WRITE AFTER SPACING 1
         MVC   LINES,=H'0'         NO LINE COUNT
         PERFORM PRINT
         L     R14,HSAVE14
         EXIT
HSAVE14  DC    F'0'
         SPACE 5
DPFORMAT LTR   R5,R5
         BCR   13,R14
         B     A01150
A01144   BCT   R1,A01154
         MVI   0(R4),C' '
         LA    R4,1(R4)
A01150   LA    R1,4
A01154   UNPK  TRWORK(3),DSEXT1(2)
         TR    TRWORK(2),XLATE-240
         MVC   0(2,R4),TRWORK
         LA    R3,1(R3)
         LA    R4,2(R4)
         BCT   R5,A01144
         EXIT
         SPACE 5
* ROUTINE TO GET AND ALLOCATE NEW CORE
ALLOCORE A     R4,=F'3'            ROUND R4 UP ...
         N     R4,=F'-4'           ... TO MULTIPLE OF 4
SHRECORE L     R0,AVCORE           R0 = AMOUNT OF SPARE CORE ALLOCATED
         SR    R0,R4               LESS AMOUNT REQUIRED
         BM    GETCORE             NOT ENOUGH? THEN GET SOME MORE
         ST    R0,AVCORE           STORE AMOUNT LEFT
         L     R3,COREAD           R3 -> NEW CORE
         LR    R0,R3
         AR    R0,R4               PLUS AMOUNT TAKEN
         ST    R0,COREAD           R0 -> SPARE CORE
         EXIT
         SPACE
GETCORE  GETMAIN R,LV=2048
         L     R3,LAST2K           R3 -> AFIRST2K
         ST    R1,0(R3)            AFIRST2K = A(NEW CORE)
         ST    R1,LAST2K           LAST2K = A(NEW CORE)
         SR    R0,R0               CLEAR R0
         ST    R0,0(R1)            CLEAR FIRST WORD
         LA    R1,4(R1)            R1 -> NEW CORE +4
         ST    R1,COREAD
         MVC   AVCORE,CORESIZE     AVCORE = CORE AVAILABLE = 2044
         B     SHRECORE
         SPACE 2
* FREEMAIN ROUTINE
         SPACE
FREECORE L     R2,AFIRST2K           R2 -> LAST 2K OF CORE OR ZEROS
FREELOOP LTR   R2,R2
         BZ    CLEARADS            IF ZERO NO CORE TO FREE
         LR    R1,R2               R1 -> CORE TO BE FREED
         L     R2,0(R2)            R2 -> NEXT 2K TO BE FREED
         FREEMAIN R,LV=2048,A=(1)
         B     FREELOOP
CLEARADS SR    R0,R0               CLEAR R0
         ST    R0,AVCORE           CLEAR AVCORE
         ST    R0,AFIRST2K           CLEAR AFIRST2K
         LA    R0,AFIRST2K           R0 -> AFIRST2K
         ST    R0,LAST2K           LAST2K -> AFIRST2K
         EXIT
AVCORE   DC    F'0'                AMOUNT OF CORE AVAILABLE
COREAD   DC    F'0'                ADDRESS OF SPARE CORE
LAST2K   DC    A(AFIRST2K)
AFIRST2K   DC    F'0'
CORESIZE DC    F'2044'
          SPACE 3
*              PRINT ROUTINE
         SPACE
PRINT    ST    R14,PSAVE14         SAVE RETURN ADDRESS
         CALL  VTOCPRT,PCWA
         L     R14,PSAVE14         RESTORE RETURN ADDRESS
         EXIT
PSAVE14  DC    F'0'                R14 SAVE AREA
         SPACE 3
*              PUNCH ROUTINE
         SPACE
PUNCH    ST    R14,QSAVE14         SAVE R14
         CALL  VTOCPCH,PCWA
         L     R14,QSAVE14         RESTORE R14
         EXIT
QSAVE14  DC    F'0'                R14 SAVE AREA
         SPACE 3
*              TITLES
         SPACE
AHEADING DC    A(0)                ADDRESS OF LIST HEADING
PAGECT   DC    H'0'                PAGE COUNT
         DC    X'40'
TOPLINE  DC    C'1'                SKIP TO CHANNEL 1
         DC    CL43'STOCK EXCHANGE COMPUTER CENTRE (IBM)'
TITLE    DC    CL52'DIRECT ACCESS VOLUME DIRECTORY LISTER'
DATE     DC    C'03/15/84'
         DC    2C' '
         DC    C'DAY='
DAYNO    DC    C'999'
TIME     DC    C'  23:59'
         DC    5C' '
         DC    CL5'PAGE'
PAGENO   DC    C'999'
PRSTART  DC    A(0)
LINES    DC    H'0'                NUMBER OF LINES IN THIS GROUP
         DC    X'40'               INCLUDING SPACING
PRLINE   DS    0CL133              PRINT LINE
PRCTL    DC    C' '                SPACE 1 LINE
         DC    CL132' '
LINECT   DC    H'999'              LINE COUNT
LINELMT  DC    H'57'               LINE LIMIT
TABLE    DC    CL29'TABLE OF CONTENTS FOR VOLUME'
VOLUME   DC    C'XXXXXX'
         DC    C' '
OWNERID  DC    CL10' '             OWNERID FROM VOLUME LABEL
DSHEAD   DC    A(DSUNLINE)         ADDRESS OF UNDERLINE
         DC    H'3'                NUMBER OF LINES IN THIS GROUP
         DC    X'40'               SPACE
         DC    C'-'                WRITE AFTER 3
         DC    CL19' '
         DC    CL26'DSNAME'
         DC    CL7'SERIAL'
         DC    CL7'SEQ'
         DC    CL9'CREDT'
         DC    CL9'EXPDT'
         DC    CL4'DSO'
         DC    CL6'RECFM'
         DC    CL6'BLKSZ'
         DC    CL6'LRECL'
         DC    CL4'KEY'
         DC    CL4'OP'
         DC    CL6'TRKAL'
         DC    CL6'TRKUS'
         DC    CL3'EX'
         DC    CL6'SCALO'          SECONDARY ALLOCATION
         DC    CL4'T'
DSUNLINE DC    A(0)                NO FURTHER LINES IN THE GROUP
         DC    H'0'                NUMBER OF LINES = ZERO
         DC    X'40'               SPACE
         DC    C'+'                NO LINES TO BE SPACED
         DC    132C'_'
TRALLOC  DC    CL32'TRACK ALLOCATION MAP FOR VOLUME'
TRVOLUME DC    C'XXXXXX'
         DC    C' '
TROWNID  DC    CL10' '             OWNER ID
TRALHEAD DC    A(TRALUL)           ADDRESS OF NEXT LINE
         DC    H'3'                NUMBER OF LINES TO BE SPACED
         DC    X'40'               SPACE
         DC    C'-'                WRITE AFTER 3
         DC    CL27' '
         DC    CL12'FIRST TRK'
         DC    CL10'LAST TRK'
         DC    CL7'#TRKS'
         DC    CL5'EXT'
         DC    CL71'DSNAME OR USAGE'
TRALUL   DC    A(0)                NO FURTHER LINES
         DC    H'0'                NO LINES TO BE SPACED
         DC    X'40'               SPACE
         DC    C'+'                WRITE WITHOUT SPACEING
         DC    CL27' '
         DC    78C'_'
         DC    CL27' '
         SPACE 3
*              END OF PROCESSING
         SPACE
ENDRUN   CALL  CLOSEPRT
         TM    OPTIONS,OPPUNCH+OPUSAGE  PUNCH IN OPERATION?
         BZ    NOCALL                   NO - BYPASS CLOSE
         CALL  CLOSEPCH            CLOSE SYSPUNCH
NOCALL   L     R15,RETCODE         R15 = RETURN CODE
         GOBACK RC=(15)
         SPACE 3
*              ABNORMAL TERMINATION
         SPACE
ABEND    ABEND 100,DUMP
         SPACE 3
VTOC     DCB   KEYLEN=44,                                              C
               DSORG=PS,                                               C
               EODAD=EOVTOC,                                           C
               RECFM=F,                                                C
               EXLST=JFCBAD,                                           C
               DDNAME=VOLUME01,                                        C
               MACRF=R,                                                C
               BLKSIZE=96
         ORG   VTOC
         DS    CL5
DCBFDAD  DS    CL8                 FULL D-A ADDRESS
         DS    CL27
DCBDDNAM DS    CL8                 DDNAME
DCBOFLGS DS    CL1                 OPEN FLAGS
         ORG
JFCBAD   DC    X'87'
         DC    AL3(JFCB)
CVDATE   DS    0D
         DC    F'0'
PDATE   DC    F'0'
PTIME   DC    F'0'
DEVAREA  DC    2F'0'               DEVICE TYPE
PF1DSCB   DC    A(0)                ADDRESS OF FIRST FORMAT 1 DSCB
XF1DSCB   EQU   PF1DSCB-152
FREEABS  DC    H'0'                ABSTRAK ADDRESS OF FREE AREA
FREECYLX DC    H'0'                NO OF FULL CYLS IN THIS EXTENT
RETCODE  DC    F'0'                RETURN CODE
VOLSDONE DC    H'0'
DSCOUNT  DC    H'0'                DATA SET COUNT
FREECYLS DC    H'0'                NO OF FULL CYLS AVAILABLE
AVTRAKS  DC    H'0'                NO OF AVAILABLE TRACKS
AVEXTS   DC    H'0'                NO OF AVAILABLE EXTENTS
VTOCTRAK DC    H'0'
TRWORK   DC    CL15' '
XLATE    DC    C'0123456789ABCDEF'
ADEVDATA DC    A(0)                ADDRESS OF DEVICE STATISTICS
SPACE    EQU   X'40'
         SPACE 3
DEVDET   DSECT                     OVERLAY MASK FOR DEVICE CONSTANTS
         SPACE
DEVCODE  DS    X                   UCB TYPE DEVICE CODE
DEVNAME  DS    CL15                DEVICE DESCRIPTION
DEVBIN   DS    H                   ONLY USED FOR 2321
DEVTK    DS    H                   NUMBER OF TRACKS PER CYL
DEVSTRIP DS    H                   ONLY USED FOR 2321
DEVHTRK  DS    H                   HEADS PER TRACK
DEVTKCYL DS    H                   NUMBER OF TRACKS PER CYL
DEVTOTAL DS    H                   TOTAL NUMBER OF ALLOCATABLE TRACKS
DEVMXREC DS    H                   MAXIMUM RECORD SIZE
NEXTDEV  EQU   *
         SPACE 3
VTOCLIST CSECT
         SPACE
DEVCONS  DS    0H                  DEVICE CONSTANT TABLE
         SPACE
D2301    DC    X'02'
         DC    CL15'2301 DRUM'
         DC    H'0'
         DC    H'0'
         DC    H'0'
         DC    H'1'
         DC    H'8'
         DC    H'200'
         DC    H'20483'
D2302    DC    X'04'
         DC    CL15'2302 DISK FILE'
         DC    H'0'
         DC    H'46'
         DC    H'0'
         DC    H'1'
         DC    H'46'
         DC    H'11500'
         DC    H'4984'
D2303    DC    X'03'
         DC    CL15'2303 DRUM'
         DC    H'0'
         DC    H'10'
         DC    H'0'
         DC    H'1'
         DC    H'10'
         DC    H'800'
         DC    H'4892'
D2305#1  DC    X'06'
         DC    CL15'2305-1 F-H DISK'
         DC    H'0'
         DC    H'8'
         DC    H'0'
         DC    H'1'
         DC    H'8'
         DC    H'384'
         DC    H'14138'
D2305#2  DC    X'07'
         DC    CL15'2305-2 F-H DISK'
         DC    H'0'
         DC    H'8'
         DC    H'0'
         DC    H'1'
         DC    H'8'
         DC    H'768'
         DC    H'14660'
D2311    DC    X'01'
         DC    CL15'2311 DISK PACK'
         DC    H'0'
         DC    H'10'
         DC    H'0'
         DC    H'1'
         DC    H'10'
         DC    H'2000'
         DC    H'3625'
D2314    DC    X'08'
         DC    CL15'2314 DISK PACK'
         DC    H'0'
         DC    H'20'
         DC    H'0'
         DC    H'1'
         DC    H'20'
         DC    H'4000'
         DC    H'7294'
D2321    DC    X'05'
         DC    CL15'2321 DATA CELL'
         DC    H'1000'
         DC    H'100'
         DC    H'20'
         DC    H'1'
         DC    H'20'
         DC    H'20000'
         DC    H'2000'
D3330    DC    X'09'
         DC    CL15'3330 DISK PACK'
         DC    H'0'
         DC    H'19'               TRKS/CYL
         DC    H'0'
         DC    H'1'                HEADS/TRK
         DC    H'19'               TRKS/CYL
         DC    H'7676'             TOTAL NO OF TRACKS ON VOLUME
         DC    H'13030'
D3330#11 DC    X'99' ************* CHANGE WHEN KNOWN ******************
         DC    CL15'3330-11 DISK'
         DC    H'0'
         DC    H'19'
         DC    H'0'
         DC    H'1'
         DC    H'19'
         DC    H'15352'
         DC    H'13030'
D3340#35 DC    X'99' ************* CHANGE WHEN KNOWN ******************
         DC    CL15'3340-35 DISK'  WINCHESTER'
         DC    7H'0'
D3340#70 DC    X'99' ************* CHAMGE WHEN KNOWN ******************
         DC    CL15'3340-70 DISK'
         DC    7H'0'
         SPACE 3
DXXXX    DC    X'00'               COMPARE DEVICE CODE
UNKNOWN  DC    CL7'UNKNOWN'
SPACES   DC    CL8' '
NVOLLAB  DC    CL44'VOLUME LABEL'
AXF1DSCB DC    A(XF1DSCB)
MASK1    DC    X'402120207A2020'
         SPACE 3
PUNCARD  MIPCARD TYPE=OLD
         ORG   PUNCARD
*              VOLUME LABEL
VOLLAB   DS    0CL80               VOLUME LABEL
         DS    CL41
VOLOWNER DS    CL10                OWNER ID
         DS    CL29
         ORG   PUNCARD
*              DISC USAGE
USECARD  DS    0CL80
USEDSN   DS    CL44                     DATA SET NAME
USETRUSE DS    F                        TRACKS USED
USETRALL DS    F                        TRACKS ALLOCATED
USEVSN   DS    CL6                      VOLUME SERIAL
USEDATE  DS    CL8                      TODAYS DAT
         DS    CL14
         ORG
         SPACE 3
         PRINT GEN
*              PROGRAM CONTROL WORK AREA
PCWA     MIPPCWA
         SPACE 5
*              JOB FILE CONTROL BLOCK
         SPACE
JFCBPFX  DC    F'4'
F4ID     EQU   JFCBPFX+3           FORMAT 4 DSCB ID =X'04'
JFCB     DS    CL176               JFCB WORK AREA
         ORG   JFCB
JFCBDSNM DS    CL44                DATA SET NAME
         DS    CL74
JFCBVOLS DS    CL30                UP TO FIVE VOLUME SERIALS
         SPACE 3
*              FORMAT 1 DSCB
         SPACE
F1DSCB   MIPF1
         SPACE 3
*              FORMAT 3 DSCB
         SPACE
F3DSCB   F3DSCB
DS3FDAD  DS    CL8                 FULLD-A ADDRESS OF DSCB
         SPACE 3
*              FORMAT 4 DSCB
         SPACE
F4DSCB   F4DSCB
DS4FDAD  DS    CL8                 FULLD-A ADDRESS
         SPACE 3
*              FORMAT 5 DSCB
         SPACE 1
F5DSCB   F5DSCB
DS5FDAD  DS    CL8                 FULL D-A ADDRESS
DS5ANEXT DS    A                   ADDRESS OF NEXT FORMAT 5 DSCB
         END
./ ADD   LIST=ALL,NAME=VTOCPARM
./ NUMBER  INCR=100,NEW1=100
         TITLE 'VTOCPARM - PARM PROCESS ROUTINE OF YEBDMAP'
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*   VTOCPARM   WILL READ THE PARM FIELD PASSED BY VTOCLIST AND SETS   *
*        ON THE APPROPRIATE FLAGS IN THE PCWA.                        *
*                                                                     *
*   ON ENTRY   R1 POINTS TO TWO ADDRESSES                             *
*              1) THE ADDRESS OF THE PARM FIELD                       *
*              2) THE ADDRESS OF THE PCWA                             *
*                                                                     *
** ALTERATION 1 BY S.A.LYNCH FEB 1975                                **
** TO ALLOW FOR NEGATIVE OPTIONS AND ABBREVIATIONS                   **
**  E.G. PARM=NODECK , PARM=NDK                                      **
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         MACRO                                                    MSAL1
&L       VTCSET &LAB
&L       LA    R8,1(,R8)           CHAR COUNT OF OPTION IN R8
         AR    R2,R8               R2 -> NEXT WORD
         SR    R3,R8
         CLI   PARMCHAR,COMMA      NEXT CHAR A COMMA ?
         BE    &LAB                OR
         LTR   R3,R3               END OF PARM?
         BZ    &LAB                OR
         B     BADPARM             NOT
         MEND
         SPACE  3
VTOCPARM CSECT
         BEGIN
         YEBEQU
         EJECT
*              SET UP ADDRESSES
         SPACE
         L     R2,0(R1)            LOAD PARM ADDRESS
         LH    R3,0(R2)            LOAD PARMLENGTH
         LA    R2,2(R2)            LOAD PARM FIELD ADDRESS
         L     R11,4(R1)           R11 -> PCWA
         USING PCWA,R11
         L     R10,APRLINE         R10 -> PRLINE
         USING PRFIELD,R10
         SPACE 3
*              CHECK PARM FIELD
         SPACE
         MVC   PRLINE+10(21),=C'EXEC PARAMETER FIELD:'
         CH    R3,=H'99'           CHECK PARM LENGTH
         BL    A00010              BRANCH IF OK
         MVC   PRLINE+30(29),=C'  IS TOO LONG AND WAS IGNORED'
         SR    R3,R3
         B     A00050
A00010   LTR   R1,R3
         BZ    A00050
         BCTR  R1,R0
         STC   R1,A00030+1
A00030   MVC   PRLINE+33(1),0(R2)
A00050   EQU   *
A00070   MVI   PRLINE,C'-'         SPACE 3 LINES THEN WRITE
         MVC   LINES,=H'3'         SPACE 3 LINES
         PERFORM PRINT          BRANCH TO PRINT HEADERS
         USING PARM,R2
         MVI   OPBYTE1,0           SET OPBYTE1 TO DEFAULTS
         MVI   OPBYTE2,0           SET OPBYTE2 TO DEFAULTS
NEXTWORD LTR   R3,R3               TEST R3
         BZ    ENDPARM             IF ZERO THEN END OF PARM
         LR    R15,R2              SAVE R2
         LA    R7,PARMTAB          PNTR TO PARAM TABLE             SAL1
         CLI   PARMCHAR,COMMA                                      SAL1
         BE    LBCOMA                                             SAL1
BAKPARM  XR    R8,R8               CLEAR R8                        SAL
         LH    R8,0(R7)            LENTH-1 IN R8                   SAL1
         EX    R8,TEST             TEST FOR PARAM IN TABLE        SAL1
         BE    FOUND               BRANCH IF PARAM FOUND           SAL1
         LA    R7,5(R7)            ADJUST PNTR                     SAL1
         AR    R7,R8               PNTR TO NEXT TABLE ENTRY        SAL1
         LA    R0,1                                                SAL1
         NR    R0,R7              TEST IF R7 AT HALFWORD ADDR      SAL1
         BE    HAFWD              IF SO                            SAL1
         LA    R7,1(,R7)          INCR ADDR BY 1                   SAL1
HAFWD    CLC   0(2,R7),=X'FFFF'    TEST FOR END OF TABLE           SAL1
         BNE   BAKPARM                                             SAL1
         B     BADPARM                                             SAL1
LBCOMA   LA    R2,1(,R2)                                           SAL1
         BCTR  R3,0                REDUCE LENGTH BY 1
         B     NEXTWORD
         SPACE  3
FOUND    LH    R1,2(,R7)           INDEX IN R7                     SAL1
         M     R0,=F'04'           MULT INDEX FOR FULLWD COUNT     SAL1
         LA    R7,SUBTAB           SBRTN TABLE PNTR                SAL1
         AR    R7,R1               PNTR TO RELV ENTRY              SAL1
         L     R1,0(,R7)           PNTR TO SBRTN IN R1             SAL1
         BR    R1                  BRANCH TO RELV RTN              SAL1
         SPACE 3
*              VOLUME COUNT
         SPACE
VOLS     LA    R2,5(R2)            R2 -> VOLUME COUNT
         SH    R3,=H'5'            REDUCE LENGTH BY 5
         BZ    ENDVOL              NO COUNT
         SR    R0,R0               CLEAR R0
         SR    R1,R1               CLEAR R1
TESTVOL  CLI   PARMCHAR,COMMA      IS IT A COMMA?
         BE    ENDVOL              YES
         TM    PARMCHAR,X'F0'      TEST FOR CHAR > X'EF'
         BNO   BADPARM             NO THEN USE DEFAULT
         IC    R0,PARMCHAR         R0 = DIGIT
         N     R0,=F'15'           CLEAR SIGN BITS
         CH    R0,=H'10'           DECIMAL NUMBER?
         BNL   BADPARM             NO - USE DEFAULT
         MH    R1,=H'10'           MULTIPLY PREVIOUS BY 10
         AR    R1,R0               ADD ON NEW
         LA    R2,1(R2)            R2 -> NEXT BYTE
         BCT   R3,TESTVOL          GO ROUND AGAIN
ENDVOL   STH   R1,VOLCT            SAVE VOLUME COUNT
         B     NEXTWORD            GO ROUND AGAIN
         SPACE 3
*              DUMP OPTION
         SPACE
DUMP     VTCSET   SETDUMP                                          SAL1
SETDUMP  OI    OPBYTE2,OPDUMP      SET FLAG
         B     NEXTWORD            GO ROUND AGAIN
         SPACE  3
*              NO DUMP OPTION
         SPACE  1
NDMP     VTCSET  CLDUMP                                            SAL1
CLDUMP   NI    OPBYTE2,255-OPDUMP  CLEAR FLAG                      SAL1
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE 3
*              PDS LISTING OPTION
         SPACE
PDS      VTCSET   SETPDS                                           SAL1
SETPDS   OI    OPBYTE2,OPPDS       SET FLAG
         B     NEXTWORD            GO ROUND AGAIN
         SPACE  3
*              NO PDS LISTING OPTION
         SPACE  1
NPDS     VTCSET CLPDS                                              SAL1
CLPDS    NI    OPBYTE2,255-OPPDS   CLEAR FLAG                      SAL1
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE 3
*              CATALOG FLAGGING OPTION
         SPACE
CATLG    VTCSET   SETCATLG                                         SAL1
SETCATLG OI    OPBYTE2,OPCATLG
         B     NEXTWORD            GO ROUND AGAIN
         SPACE  3
*              NO CATALOG FLAGGING OPTION
         SPACE  1
NCTLG    VTCSET CLCTLG                                             SAL1
CLCTLG   NI    OPBYTE2,255-OPCATLG CLEAR FLAG                      SAL1
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE 3
*              DATA SET EXTENT OPTION
         SPACE
EXTENTS  VTCSET   SETEXT                                           SAL1
SETEXT   OI    OPBYTE2,OPEXT       SET FLAG
         B     NEXTWORD            GO ROUND AGAIN
         SPACE  3
*              NO DATA SET EXTENT OPTION
         SPACE  1
NEXTNT   VTCSET CLEXT                                              SAL1
CLEXT    NI    OPBYTE2,255-OPEXT   CLEAR FLAG                      SAL1
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE 3
*              PUNCHED OUTPUT OPTION
         SPACE
PUNCH    VTCSET   SETPUNCH                                         SAL1
SETPUNCH OI    OPBYTE2,OPPUNCH     SET FLAG
         B     NEXTWORD            GO ROUND AGAIN
         SPACE  3
*              NO PUNCHED OUTPUT OPTION
         SPACE  1
NPUNCH   VTCSET CLPUNCH                                            SAL1
CLPUNCH  NI    OPBYTE2,255-OPPUNCH CLEAR FLAG                      SAL1
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE 3
*              TERMINAL OUTPUT OPTION
         SPACE
TERM     VTCSET   SETTERM                                          SAL1
SETTERM  OI    OPBYTE2,OPTERM      SET FLAG
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE  3
*              NO TERMINAL OUTPUT OPTION                           SAL1
         SPACE  1
NTERM    VTCSET CLTERM                                             SAL1
CLTERM   NI    OPBYTE2,255-OPTERM  CLEAR FLAG                      SAL1
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE 4
*              VALUE FOR PERCENTAGE FILLED WARNING
         SPACE
PRCENT   LA    R2,5(R2)                 R2 -> %AGE VALUE
         SH    R3,=H'5'                 COUNT LESS 5
         BZ    ENDPARM                  END OF PARM - 80% DEFAULT
         CLI   PARMCHAR,COMMA           VALUE PRESENT?
         BE    NEXTWORD                 NO - 80% DEFAULT
         LA    R4,2                     NO OF %AGE DIGITS
         SR    R1,R1                    CLEAR R1 FOR DYNAMIC LENGTH
         LR    R5,R2                    SAVE START OF %AGE VALUE
NUM      TM    0(R2),X'F0'              NUMERIC?
         BNO   BADPARM                  NO
         LA    R2,1(R2)                 ADDRESS NEXT CHAR IN PARM FIELD
         BCTR  R3,R0                    COUNT LESS 1
         LTR   R3,R3                    END OF PARM?
         BZ    INSRT                    YES
         CLI   0(R2),COMMA              END OF %AGE VALUE?
         BE    INSRT                    YES
         LA    R1,1(R1)                 LENGTH OF %AGE
         BCT   R4,NUM
         B     BADPARM                  %AGE VALUE TOO LONG
INSRT    EX    R1,PCK                   PACK %AGE VALUE
         CVB   R5,DBWD
         STH   R5,PERCENT               STORE %AGE VALUE
         B     NEXTWORD
         SPACE
PCK      PACK  DBWD,0(1,R5)
DBWD     DS    D
         SPACE 4
*              FULL PDS LISTING OPTION
         SPACE
FPDS     VTCSET   SETFPDS                                          SAL1
SETFPDS  OI    OPBYTE2,FULLPDS+OPPDS    SET FLAGS
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE  3
*              NO FULL PDS LISTING OPTION
         SPACE  1
NFPDS    VTCSET CLFPDS                                             SAL1
CLFPDS   NI    OPBYTE2,255-FULLPDS         CLEAR FULLPDS BIT       SAL1
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE 4
*              DISC USAGE OPTION
         SPACE
USAGE    VTCSET   SETUSAGE                                         SAL1
SETUSAGE OI    OPBYTE2,OPUSAGE
         B     NEXTWORD                 GO ROUND AGAIN
         SPACE 4
*              ERROR IN PARMFIELD
         SPACE
*              ERROR IN PARMFIELD
         SPACE
BADPARM  MVC   PARMWORD,SPACES     CLEAR PARMWORD
         MVC   PRLINE+10(50),=C'''        '' IS AN ILLEGAL PARAMETER AN*
               D WAS IGNORED'
         LR    R2,R15              RESTORE R2
         LA    R1,8                MAX LENGTH OF ERROR
SEECOMMA CLI   PARMCHAR,COMMA      REACHED A COMMA?
         BE    SETERR              YES
         LA    R2,1(R2)            NO R2 -> NEXT BYTE
         BCT   R1,SEECOMMA
SETERR   LR    R14,R2              R14 -> NEXT COMMA
         SR    R14,R15             R14 = LENGTH
         SR    R3,R14              COUNT LESS LENGTH OF ERROR
         BCTR  R14,0               LENGTH LESS 1
         EX    R14,MOVERR          MOVE BAD WORD
         MVC   PRLINE+11(8),PARMWORD
         MVC   LINES,=H'3'         SET LINES TO 3
         MVI   PRLINE,C'-'         SPACE 3 LINES THEN WRITE
         PERFORM PRINT
         LA    R2,1(R2)            R2 -> NEXT BYTE
         BCTR  R3,0                PARM COUNT - 1
         LTR   R3,R3               TEST R3 FOR MORE DATA
         BP    NEXTWORD            IF THERE IS, GO ROUND AGAIN
         SPACE  3
*              NO DISC USAGE OPTION
         SPACE  1
NUSGE    VTCSET CLUSG                                              SAL1
CLUSG    NI    OPBYTE2,255-OPUSAGE CLEAR FLAG                      SAL1
         B     NEXTWORD            GO ROUND AGAIN                  SAL1
         SPACE 3
*              MAP OPTION
         SPACE  1
MAP      B     NEXTWORD            DUMMY RTN                       SAL1
         SPACE  3
*              NO MAP OPTION
         SPACE  1
NMAP     B     NEXTWORD            DUMMY RTN                       SAL1
         SPACE  3
*              END OF PARM
         SPACE
ENDPARM  EQU   *
         CLC   VOLCT,=H'0'         VOLUME COUNT = ZERO?
         BNE   A00180              NO - CONTINUE
         MVC   VOLCT,=H'1'         YES - SET VOLUME COUNT TO 1
A00180   LH    R0,VOLCT
         PERFORM CONVERT
         MVC   PRLINE+10(3),DECRADJ+1   NUMBER OF VOLUMES
         MVC   PRLINE+14(24),=C'VOLUME(S) WILL BE LISTED'
         MVC   LINES,=H'3'         SET LINES TO 3
         MVI   PRLINE,C'-'         SPACE 3 LINES THEN WRITE
         PERFORM PRINT
         SPACE 3
GOBACK   GOBACK
         SPACE 5
*        PRINT ROUTINE
         SPACE
PRINT    ST    R14,R14SAVE         SAVE R14
         LA    R1,PCWA             R1 -> PCWA
         CALL  VTOCPRT,((R1))      PRINT LINE
         L     R14,R14SAVE         RESTORE R14
         EXIT
         SPACE 5
*              CONVERT TO DECIMAL ROUTINE
         SPACE
CONVERT  LPR   R0,R0
         CVD   R0,PNUM             PNUM = PACKED DECIMAL
         MVC   DECRADJ,MASK1       MOVE IN MASK
         ED    DECRADJ,PNUM+6      EDIT
         EXIT
         SPACE 5
TEST     CLC   PARMCHAR(0),4(R7)   TEST FOR PARM IN TABLE          SAL1
PNUM     DC    D'0'                PACKED NUMBER
MASK1    DC    X'40202120'         EDITING MASK
DECRADJ  DC    CL4' '              DECIMAL NUMBER
         SPACE
PARMWORD DC    CL8' '              PARM WORD RETURN AREA
R14SAVE  DS    F                   R14 SAVE AREA
SPACES   DC    CL8' '
MOVERR   MVC   PARMWORD,0(R15)     MOVE WORD IN ERROR
COMMA    EQU   C','                COMMA
         SPACE 3
*              PARM FIELD
         SPACE
         DS    0H
PARMTAB  DC    H'04',H'00',C'VOLS='                                SAL1
         DC    H'04',H'03',C'CATLG'
         DC    H'06',H'13',C'NOCATLG'
         DC    H'03',H'09',C'%AGE'
         DC    H'04',H'08',C'USAGE'
         DC    H'06',H'18',C'NOUSAGE'
         DC    H'03',H'01',C'DUMP'
         DC    H'05',H'11',C'NODUMP'
         DC    H'02',H'02',C'PDS'
         DC    H'04',H'12',C'NOPDS'
         DC    H'06',H'07',C'FULLPDS'
         DC    H'08',H'17',C'NOFULLPDS'
         DC    H'06',H'04',C'EXTENTS'
         DC    H'08',H'14',C'NOEXTENTS'
         DC    H'01',H'00',C'V='
         DC    H'00',H'03',C'C'
         DC    H'01',H'13',C'NC'
         DC    H'01',H'09',C'%='
         DC    H'00',H'08',C'U'
         DC    H'01',H'18',C'NU'
         DC    H'01',H'01',C'DP'
         DC    H'02',H'11',C'NDP'
         DC    H'00',H'02',C'P'
         DC    H'01',H'12',C'NP'
         DC    H'01',H'07',C'FP'
         DC    H'02',H'17',C'NFP'
         DC    H'00',H'04',C'E'
         DC    H'01',H'14',C'NE'
         DC    H'02',H'10',C'MAP'
         DC    H'04',H'19',C'NOMAP'
         DC    H'00',H'10',C'M'
         DC    H'01',H'19',C'NM'
         DC    H'03',H'06',C'TERM'
         DC    H'03',H'05',C'DECK'
         DC    H'00',H'06',C'T'
         DC    H'01',H'05',C'DK'
         DC    H'05',H'16',C'NOTERM'
         DC    H'05',H'15',C'NODECK'
         DC    H'01',H'16',C'NT'
         DC    H'02',H'15',C'NDK'
         DS    0H
         DC    X'FFFF'
SUBTAB   DC    A(VOLS)                                             SAL1
         DC    A(DUMP)
         DC    A(PDS)
         DC    A(CATLG)
         DC    A(EXTENTS)
         DC    A(PUNCH)
         DC    A(TERM)
         DC    A(FPDS)
         DC    A(USAGE)
         DC    A(PRCENT)
         DC    A(MAP)
         DC    A(NDMP)
         DC    A(NPDS)
         DC    A(NCTLG)
         DC    A(NEXTNT)
         DC    A(NPUNCH)
         DC    A(NTERM)
         DC    A(NFPDS)
         DC    A(NUSGE)
PARM     DSECT                                                     SAL1
PLENGTH  DS    0H
PARMCHAR DS    100C
         SPACE 5
*              PRINT LINE
         SPACE
PRFIELD DSECT
         DS    F
LINES    DS    H
SPACE    DC    X'40'
PRLINE   DS    CL133
         SPACE 5
*              PROGRAM CONTROL WORK AREA
         SPACE
PCWA     MIPPCWA DSECT
         END
./ ADD   LIST=ALL,NAME=VTOCUSE
./ NUMBER  INCR=100,NEW1=100
VTOCUSE  CSECT
         SPACE
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
*                                                                     *
*    VTOCUSE   WILL IF REQUIRED PRODUCE A PUNCHED CARD,RECORD ID = 9  *
*              GIVING                                                 *
*                                                                     *
*                        DSNAME                                       *
*                        TOTAL TRACKS ALLOCATED                       *
*                        TOTAL TRACKS IN USE                          *
*                        VOLUME SERIAL NUMBER                         *
*                                                                     *
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         SPACE 4
         BEGIN
         SPACE 4
         YEBEQU
         SPACE 4
         L     R10,4(R1)                R10 -> CARD AREA
         L     R11,0(R1)                R11 -> PCWA
         USING PCWA,R11
         SPACE 4
*              1ST ENTRY OPEN SYSPUNCH IF NOT IN USE
         SPACE
         BC    0,OPENED                 NO BRANCH 1ST TIME THRO
         OI    *-3,X'F0'                SET TO UNCONDITIONAL BRANCH
         TM    OPTIONS,OPPUNCH          SYSPUNCH ALREADY IN USE
         BO    OPENED                   YES
         CALL  OPENPCH                  NO - OPEN SYSPUNCH
         EJECT
OPENED   L     R8,AF1DSCB               R8 -> FORMAT 1 DSCB
         USING F1DSCB,R8
         CLI   DS1NOEPV,0               ANY EXTENTS?
         BE    GOBACK                   NO - TAKE NO ACTION
         L     R7,DS1AEXT               R7 -> FIRST EXTENT BLOCK
         USING DSEXT,R7
         LTR   R7,R7                    SHOULD POINT TO NEXT EXTENT
         BZ    GOBACK                   RETURN IF ZERO
         LH    R6,DS1LSTAR              POINTER TO LAST WRITTEN BLOCK
         LA    R6,1(R6)                 ROUND UP TO WHOLE TRACKS
         ST    R6,44(R10)               STORE TRACKS USED
         SR    R5,R5                    CLEAR R5 -> 0
NEXTEXT  EQU   *
         AH    R5,EXTNOTRK              INCR NO OF TRACKS ALLOCTED
         L     R7,EXTDNEXT              ADDRESS NEXT BLOCK
         LTR   R7,R7                    ANOTHER BLOCK EXIST?
         BNZ   NEXTEXT                  YES
         ST    R5,48(R10)               LOAD TRACKS ALLOCATED
         CALL  VTOCPCH,((R11))          PUNCH CARD
         SPACE 3
GOBACK   GOBACK RC=0                    RETURN
         EJECT
DSEXT    MIPDSEXT
         SPACE 4
PCWA     MIPPCWA DSECT
         SPACE 4
F1DSCB   MIPF1
         END
./ ADD   LIST=ALL,NAME=VTOCCAT
./ NUMBER  INCR=100,NEW1=100
VTOCCAT  CSECT
         SPACE 4
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
*                                                                     *
*        VTOCCAT    WILL CHECK IF DATA SETS ARE CATALOGUED AND IF SO  *
*                   PRINTS THEM                                       *
*                                                                     *
*                   2 PARAMETERS ARE PASSED: ADDRESS OF PCWA          *
*                                            ADDRESS OF 1ST DSCB      *
*                                                                     *
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         SPACE 4
         BEGIN (SAVE)
         SPACE
         YEBEQU
         SPACE 4
         LM    R10,R11,0(R1)            ADDRESS PARAMETERS
         USING PCWA,R10                 R10 -> PCWA
         L     R11,0(R11)               R11 -> 1ST FORMAT 4 DSCB
         LR    R2,R11
         USING F1DSCB,R11
         EJECT
*              SET UP HEADER LINE - INITIALISE PRINT LINE
         SPACE
         L     R9,ATOPLINE              ADDRESS PAGE HEADER LINE
         USING TOPLINE,R9
         LA    R8,HEADING               ADDRESS NEXT HEADING
         ST    R8,ANEXT                 STORE IN PAGE HEADER LINE
         MVC   THEAD,=C'CATALOGUED DATA SETS FOR VOLUME '
         MVC   TVOL,VOLSER#             VOLUME SERIAL
         SPACE
         L     R9,APRLINE               ADDRESS PRINT LINE
         USING PRFIELD,R9
         SR    R8,R8
         ST    R8,ACLINE                NO FURTHER PRINT LINES
         MVC   LINECT,=H'999'
         MVI   PRCTL,C' '               SPACE TO NEXT LINE
         MVC   PRLINE+1(132),PRLINE     CLEAR PRINT LINE
         SPACE
         MVC   VOLSERNO,VOLSER#
         SR    R5,R5                    CATALOGUE COUNT
         EJECT
*              RUN THROUGH DSCB'S CHECKING FOR CATALOGED DATA SETS
         SPACE
CHKDSCB  CLC   DS1DSNAM,HEXFS           ANY MORE DSCBS?
         BE    TRAIL                    NO
         MVC   DSN,DS1DSNAM
*              CHECK IF DATA SET CATALOGUED ON SYSTEM CATALOGUE
         LOCATE LIST1
         LTR   R15,R15                  FOUND IN SYSTEM CATALOGUE?
         BZ    FOUND                    YES
         SPACE
         LOCATE LIST2
         LTR   R15,R15                  FOUND IN PRIVATE CATALOGUE?
         BZ    FOUND
         OI    152(R11),X'80'      NOT FOUND - TURN ON HIGH ORDER BIT
         SPACE
NEXTDSCB L     R11,152(R11)             ADDR NEXT DSCB
         B     CHKDSCB
FOUND    LA    R5,1(R5)                 DATA SET COUNT
         NI    152(R11),X'7F'      FOUND - TURN OFF HIGH ORDER BIT
         MVC   LINES,=H'1'
         MVC   PRLINE+11(44),DS1DSNAM
         PERFORM PRINT                  PRINT LINE
         B     NEXTDSCB
         SPACE 4
TRAIL    MVC   PRLINE+40(37),=C'<<<<<      DATA SETS CATALOGUED >>>>>'
         MVC   PRLINE+45(5),=X'2020202120'   EDIT PATTERN
         CVD   R5,DBWD                  NO OF DATA SETS CATALOGUED
         ED    PRLINE+45(5),DBWD+5
         PERFORM PRINT
         SPACE 2
***      NOW LIST UNCATALOGED DATASETS                              ***
         SPACE 2
         BAL   R3,UNCATHDG         WRITE UNCAT HEADINGS
         LR    R11,R2              R11 -> 1ST FMT 1 DSCB            MKB
         SR    R5,R5               CLEAR R5 FOR COUNT               MKB
NEXT1    L     R2,152(0,R11)       R2 -> NEXT DSCB                  MKB
         LTR   R2,R2               HIGH ORDER BIT ON ?              MKB
         BM    UNCAT               YES - NOT CATALOGED              MKB
NEXT2    LR    R11,R2              NO - CATALOGED - R11 -> NEXT DSCB KB
         CLC   DS1DSNAM,HEXFS      ANYMORE DSCBS ?                  MKB
         BE    TRAIL2              NO                               MKB
         B          NEXT1          YES - PRAY CONTINUE              MKB
         SPACE
UNCAT    DS    0H                                                   MKB
         MVC   PRLINE+11(44),DS1DSNAM  MOVE IN PRINTED DSNAME       MKB
         LA    R5,1(0,R5)          ADD 1 TO COUNT                   MKB
         MVC   LINES,=C'1'         PRINT AFTER 1                    MKB
         PERFORM PRINT                                              MKB
         B     NEXT2               TRY NEXT DSCB                    MKB
TRAIL2   DS    0H                                                   MKB
         MVC   PRLINE+40(37),=C'<<<<<      DATA SETS NOT CATLGD >>>>>'
         MVC   PRLINE+45(5),=X'2020202120'                          MKB
         CVD   R5,DBWD                                              MKB
         ED    PRLINE+45(5),DBWD+5 COUNT OF DATA SETS               MKB
         PERFORM PRINT                                              MKB
         SPACE
         GOBACK RC=0
         SPACE 4
*              PRINT LINE
         SPACE
PRINT    ST    R14,SAVE14
         CALL  VTOCPRT,((R10))
         L     R14,SAVE14
         BR    R14
SAVE14   DS    F
         SPACE 2
UNCATHDG DS    0H                  HEADINGS FOR UNCATALOGED DATA SETS
         L     R9,ATOPLINE         R9 -> PAGE HEADER LINE
         LA    R8,UHEAD            R8 -> NEXT HEADING
         ST    R8,0(0,R9)          CHAIN UP                         MKB
         MVC   51(32,R9),=C'LIST OF UNCATALOGED DATA SETS   '
         L     R9,APRLINE          R9 -> PRINTLINE
         LA    R8,0                TERMINATE
         ST    R8,0(0,R9)           CHAIN
         MVC   LINECT,=H'999'      LINE COUNT = 999 ...
         MVI   7(R9),C' '          CLEAR PRINT LINE
         MVC   8(132,R9),7(R9)
         BR    R3                  RETURN TO CALLER
*              LIST OF CATALOGUED DATA SETS HEADINGS
         SPACE
HEADING  DC    A(UNLINE)           ADDRESS OF UNDERLINE
         DC    H'3'                NO OF LINES IN THIS GROUP
         DC    X'40'
         DC    C'-'                WRITE AFTER 3
         DC    CL10' '
         DC    CL122'CATALOGUED DATA SETS'
UNLINE   DC    A(0)                NO FURTHER LINES TO PRINT
         DC    H'0'                NO OF LINES
         DC    X'40'
         DC    C'+'                SPACE NO LINES
         DC    CL10' '
         DC    20C'_'
         DC    CL102' '
         SPACE 2
UTOP     DC    C'        ',C'UNCATALOGED DATA SETS',110C' '  PAGEHDGG
UHEAD    DC    A(LINEUN)           ADDRESS OF UNDERLINE
         DC    H'3'                # OF LINES THIS GROUP
         DC    X'40'
         DC    C'-'                WRITE AFTER 3
         DC    10C' '
         DC    C' DATA SETS NOT CATALOGED',110C' '
LINEUN   DC    A(0)                NO MORE TO PRINT
         DC    H'0'
         DC    X'40'
         DC    C'+'                NO SPACE
         DC    10C' '
         DC    C' ',23C'_',110C' '
         SPACE 2
*              LOCATE & CAMLST WORK AREAS
         SPACE
LIST1    CAMLST NAME,DSN,,RTRNFLD
LIST2    CAMLST NAME,DSN,VOLSERNO,RTRNFLD
         SPACE
DSN      DS    CL44                DATA SET NAME
VOLSERNO DS    CL6                 VOLUME SERIAL
RTRNFLD  DS    0D                  CAMLIST
         DS    CL265
         SPACE 4
*              WORK AREAS
         SPACE
SAVE     DS    9D
DBWD     DS    D
HEXFS    DC    44X'FF'
         EJECT
*              PROGRAM CONTROL WORK AREA
         SPACE
PCWA     MIPPCWA DSECT
         SPACE 4
*              PAGE HEADING FOR CATALOGED DATA SET LIST
         SPACE
TOPLINE  DSECT
ANEXT    DS    F
         DS    CL47
THEAD    DS    CL32
TVOL     DS    CL6
         SPACE 4
*              PRINT LINE
         SPACE
PRFIELD  DSECT
ACLINE   DS    A                        ADDRESS NEXT LINE
LINES    DS    H                        NO OF LINES
         DS    C
PRCTL    EQU   *                        CONTROL CHARACTER
PRLINE   DS    CL133
LINECT   DS    H                        LINE COUNT
LINELMT  DS    H                        LINES/PAGE
         SPACE 4
*              FORMAT 1 DSCB
         SPACE
F1DSCB   MIPF1
         END
./ ADD   LIST=ALL,NAME=VTOCEXT
./ NUMBER  INCR=100,NEW1=100
         TITLE 'VTOCEXT - DATA SET EXTENT LISTER'
VTOCEXT  CSECT
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*   VTOCEXT    WILL IF REQUIRED PRODUCE A LIST OF EXTENTS FOR EACH    *
*        DATA SET LISTED BY VTOCLIST                                  *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         BEGIN
         SPACE 3
         YEBEQU
         SPACE 3
         L     R11,0(R1)           R11 -> PCWA
         USING PCWA,R11
         L     R10,APRLINE         R10 -> PRINT LINE
         USING PRLINE,R10
         EJECT
*              INITIALISE FIELDS
         SPACE
         L     R8,AF1DSCB          R8 -> FORMAT 1 DSCB
         USING F1DSCB,R8
         CLI   DS1NOEPV,0          ANY EXTENTS?
         BE    GOBACK              NO - TAKE NO ACTION
         MVI   COLUMN,ONE          SET TO PRINT IN COLUMN 1
         L     R8,DS1AEXT          R8 -> FIRST EXTENT BLOCK
         USING DSEXT,R8
         LTR   R8,R8               SHOULD POINT TO NEXT EXTENT
         BZ    GOBACK              RETURN IF ZERO
         SPACE 3
*              SET UP HEADINGS
         SPACE
         MVC   PRFLD1,TITLES       MOVE TITLES TO COLUMN 1
         MVC   PRFLD2,TITLES            AND TO COLUMN 2
         MVC   LINES,=H'2'         SPACE 2 LINES
         MVI   PRCTL,C'0'
         PERFORM PRINT             PUT HEADINGS
         MVC   PRFLD1,ULINE        MOVE UNDERLINES TO COLUMN 1
         MVC   PRFLD2,ULINE             AND COLUMN 2
         MVC   LINES,=H'0'         SPACE NO LINES
         MVI   PRCTL,C'+'
         PERFORM PRINT
         SPACE 3
*              SET UP PRINT FIELD
         SPACE
SETEXT   SR    R0,R0               CLEAR R0
         IC    R0,EXTSEQ           R0 = SEQUENCE NUMBER
         AH    R0,=H'1'            PLUS 1
         CVD   R0,DNUM             IN DECIMAL
         MVC   EXTNO,MASK1         MOVE IN MASK
         ED    EXTNO,DNUM+6        EDIT EXTENT NUMBER
         UNPK  TRFIELD,EXTSCCHH(5) STARTING CCHH
         TR    TRFIELD,TRABLE      TRANSLATE TO PRINT
         MVC   FROMCC,TRCC         MOVE IN CC
         MVC   FROMHH,TRHH              AND HH
         UNPK  TRFIELD,EXTECCHH(5) ENDING CCHH
         TR    TRFIELD,TRABLE      TRANSLATE TO PRINT
         MVC   TOCC,TRCC           MOVE IN CC
         MVC   TOHH,TRHH                AND HH
         SPACE 3
*              NOW TRACKS
         SPACE
         LH    R0,EXTSABS          R0 = STARTING ABSTRAK NUMBER
         CVD   R0,DNUM             IN DECIMAL
         MVC   FROMABS,MASK2       MOVE IN MASK
         ED    FROMABS,DNUM+5      THEN EDIT
         LH    R0,EXTEABS          R0 = ENDING ABSTRAK NUMBER
         CVD   R0,DNUM             IN DECIMAL
         MVC   TOABS,MASK2         MOVE IN MASK
         ED    TOABS,DNUM+5        THEN EDIT
         LH    R0,EXTNOTRK         R0 = NUMBER OF TRACKS IN EXTENT
         CVD   R0,DNUM             IN DECIMAL
         MVC   NOTRKS,MASK2        MOVE IN MASK
         ED    NOTRKS,DNUM+5       THEN EDIT
         SPACE 3
*              MOVE TO PRINT LINE
         SPACE
         CLI   COLUMN,ONE          FIRST PRINT COLUMN
         BNE   COLUMN2             NO - MUST BE SECOND
         MVC   PRFLD1,LINE         MOVE TO COLUMN 1
         MVI   COLUMN,TWO          NEXT LINE TO COLUMN 2
         L     R8,EXTDNEXT         R8 -> NEXT BLOCK
         LTR   R8,R8               OR ZERO?
         BNZ   SETEXT              NO - PROCESS THIS BLOCK
         PERFORM PRINT             YES - PRINT LINE
         B     ENDPRINT            GO TO FINISH PRINTING
COLUMN2  MVC   PRFLD2,LINE         MOVE TO COLUMN 2
         PERFORM PRINT             PUT THE LINE
         MVI   COLUMN,ONE          NEXT LINE TO COLUMN 1
         L     R8,EXTDNEXT         R8 -> NEXT BLOCK
         LTR   R8,R8               OR ZERO?
         BNZ   SETEXT              NO - PROCESS THE BLOCK
         SPACE 3
*              TIDY UP PRINT
         SPACE
ENDPRINT PERFORM PRINT             PUT BLANK LINE
GOBACK   GOBACK RC=0               AND RETURN
         SPACE 3
*              PRINT ROUTINE
         SPACE
PRINT    ST    R14,PRINTST         SAVE R14
         LA    R1,PCWA             R1 -> PCWA
         CALL VTOCPRT,((R1))       BRANCH TO PRINT ROUTINE
         L     R14,PRINTST         RESTORE R14
         EXIT                      RETURN
         SPACE 3
DNUM     DC    D'0'
TRLS     DC    C'0123456789ABCDEF' TRANSLATE TABLE
TRABLE   EQU   TRLS-240            TRANSLATE TABLE ORIGIN
PRINTST  DC    F'0'
MASK1    DC    X'40202120'
MASK2    DC    X'402020202120'
TRFIELD  DC    CL9' '
         ORG   TRFIELD
TRCC     DS    CL4
TRHH     DS    CL4
         ORG
COLUMN   DC    X'00'
ONE      EQU   X'00'
TWO      EQU   X'FF'
         SPACE 3
TITLES   DC    CL4' '
         DC    C'EXT'
         DC    CL4' '
         DC    C'FROM'
         DC    CL6' '
         DC    C'TO'
         DC    CL7' '
         DC    C'FROM'
         DC    CL3' '
         DC    C'TO'
         DC    CL5' '
         DC    C'TRKS'
         DC    CL18' '
         SPACE
ULINE    DC    CL4' '
         DC    3C'_'
         DC    CL4' '
         DC    4C'_'
         DC    CL6' '
         DC    2C'_'
         DC    CL7' '
         DC    4C'_'
         DC    CL3' '
         DC    2C'_'
         DC    CL5' '
         DC    4C'_'
         DC    CL18' '
         SPACE 3
LINE     DC    CL2' '
EXTNO    DC    XL4'40202120'
         DC    CL2' '
FROMCC   DC    CL4'0000'
         DC    CL1'.'
FROMHH   DC    CL4'0000'
         DC    CL1' '
TOCC     DC    CL4'0000'
         DC    CL1'.'
TOHH     DC    CL4'0000'
         DC    CL1' '
FROMABS  DC    XL6'402020202120'
TOABS    DC    XL6'402020202120'
         DC    CL1' '
NOTRKS   DC    XL6'402020202120'
         DC    CL18' '
         SPACE 3
DSEXT    MIPDSEXT
         SPACE 3
PRLINE   DSECT
PRANEXT  DC    F'0'
LINES    DC    0H'0'
PRLINES  DC    H'0'
SPACE    DC    X'40'
PRCTL    DC    CL1' '
PRFLD1   DC    CL66' '             COLUMN 1
PRFLD2   DC    CL66' '             COLUMN 2
         SPACE 3
PCWA     MIPPCWA DSECT
         SPACE 3
F1DSCB   MIPF1
         END
./ ADD   LIST=ALL,NAME=VTOCPDS
./ NUMBER  INCR=100,NEW1=100
         TITLE 'VTOCPDS - PDS LISTING SUBROUTINE'
VTOCPDS  CSECT
         SPACE
*              ON ENTRY R1 -> THE ADDRESS OF THE PCWA
         SPACE
** ALTERATION 2 BY S.A.LYNCH FEB 1975                                **
** TO PRODUCE CONTINUATION HEADINGS WHEN PRINTING MEMBERS OF A       **
** PARTITIONED DATA SET USING PDS OPTION                             **
         SPACE 3
         BEGIN
         YEBEQU
         EJECT
**             UPDATED FEB 1975 BY MKB TO STOP LOOP IN PRINTING     **
**             DIRECTORY ENTRIES WITH MORE THAN 44 BYTES OF USER    **
**             DATA. NO DEDUCTION WAS BEING RECORDED IN FIELD       **
**             LENTRY. ADDITIONAL STH INSTRUCTION ADDED IN          **
**             ROUTINE 'PUTLINE'. PROBLEM DETECTED WHEN PROCESSING  **
**             SMP CDS FILE - SMP STRIKES AGAIN                     **
         SPACE 4
*              PROVIDE ADDRESSABILITY TO PCWA
         SPACE
         L     R11,0(R1)           R11 -> PCWA
         USING PCWA,R11
         L     R10,APRLINE         R10 -> PRINT LINE
         USING PRLINE,R10
         L     R9,APUNCARD         R9 -> CARD IMAGE
         USING PUNCARD,R9
         SPACE 3
         CLI   OPBYTE2,OPPUNCH     DECK OPTION ONLY ?               MKB
         BE    NOLIST              YES - NO HEADINGS                MKB
         SPACE 2
*              PUT HEADINGS
         SPACE
         TM    OPBYTE2,FULLPDS     FULL LISTING REQUIRED?
         BZ    SHTHEAD             NO - PUT SHORT HEADINGS
         MVC   PRFLDA,HEADING      MOVE HEADING
         MVC   PRTEXT+20(9),=C'USER DATA'
         LA    R1,HEADS2          LONG HDR WHEN NEW PAGE           SAL2
         B     PUTHEAD
SHTHEAD  MVI   HEADING+19,C' '     NO TTR COUNT
         MVC   PRFLD1,HEADING      MOVE
         MVC   PRFLD2,HEADING          IN
         MVC   PRFLD3,HEADING              FOUR
         MVC   PRFLD4,HEADING                  HEADINGS
         LA    R1,HEADS1          SHORT HDRS WHEN NEW PAGE         SAL2
PUTHEAD  MVI   PRCTL,C'0'          SKIP 2 THEN WRITE
         MVC   LINES,=H'2'         NUMBER OF LINES = 2
         L     R2,ATOPLINE        SET UP PNTRS TO REQ HDRS         SAL2
         MVC   HDSTOR,0(R2)       STORE PREV HDR PNTRS             SAL2
         LA    R0,PDHDR           PNTR TO 2ND HDR                  SAL2
         ST    R0,0(,R2)          STORE PNTR IN CHAIN              SAL2
         ST    R1,PDHDR           CHAIN COMPLETED                  SAL2
         MVC   PDHDR+8(44),DMYDSN  CLEAR DSNAM SPACE               SAL2
         L    R3,AF1DSCB          BASE FOR FORMAT 1 DSCB           SAL2
         USING F1DSCB,R3                                           SAL2
         MVC   PDHDR+8(44),DS1DSNAM  DSNAM IN HDR 2                SAL2
         PERFORM PRINT
         TM    OPBYTE2,FULLPDS     FULL LISTING?
         BZ    SHTUNDER            NO - PUT SHORT UNDERLINES
         MVC   PRFLDA,ULINE        MOVE IN UNDERLINE
         MVC   PRTEXT+20(9),=9C'_'
         B     PUTUNDER
SHTUNDER MVI   ULINE+19,C' '       NO TTR COUNT
         MVC   PRFLD1,ULINE        MOVE
         MVC   PRFLD2,ULINE           IN
         MVC   PRFLD3,ULINE              FOUR
         MVC   PRFLD4,ULINE                 UNDERLINES
PUTUNDER MVI   PRCTL,C'+'          NO SPACE
         MVC   LINES,=H'0'         NO LINES
         PERFORM PRINT
         SPACE 3
*              OBTAIN DATA SET NAME
         SPACE
NOLIST   DS    0H                                                   MKB
         MVC   DCBDDNAM,VOLUMEXX   MOVE IN DD NAME
         L     R2,AJFCB            R2 -> JFCB
         L     R3,AF1DSCB          R3 -> FORMAT 1 DSCB
         USING F1DSCB,R3
         MVC   0(44,R2),DS1DSNAM   MOVE DATA SET NAME TO JFCB
         DROP  R3
         SPACE 3
**             PREPARE FIELDS
         SPACE
OPENDCB  EQU   *
         LA    R1,AJFCB            R1 -> ADDRESS OF JFCB
         ST    R1,AENTRY           USE AENTRY AS WORK FIELD
         MVC   DCBEXLST,AENTRY+1   MOVE ADDRESS TO EXIT LIST
         OPEN  (DIRECTRY,INPUT),TYPE=J     OPEN DIRECTORY
         SR    R0,R0               CLEAR R0
         STH   R0,PDSBKCT          BLOCK COUNT = ZERO
         STH   R0,PDSBKUS          NUMBER OF BLOCKS USED = ZERO
         STH   R0,PDSMBCT          MEMBER COUNT = ZERO
         STH   R0,PDSDACT          AMOUNT OF DATA LEFT IN BLOCK = ZERO
         STH   R0,FLDCT            FIELD COUNT = ZERO
A00A74   EQU   *
READTEST LH    R0,PDSDACT          R0 = LENGTH OF DATA REMAINING
         LTR   R0,R0
         BP    A00AD8              STILL ENTRIES TO BE PROCESSED
         SPACE 3
*              READ NEW DIRECTORY BLOCK
         SPACE
DIREAD   READ DIRECB,SF,DIRECTRY,DIRBLOK
         CHECK DIRECB              CHECK FOR COMPLETION
         LA    R8,DIRBLOK          R8 -> DIRECTORY BLOCK
         LH    R1,PDSBKCT          R1 = OLD BLOCK COUNT
         LA    R1,1(R1)            PLUS 1
         STH   R1,PDSBKCT          STORE NEW BLOCK COUNT
         CLC   OLDKEY,NEWKEY       IS MEMBER NAME SAME AS PREVIOUS?
         BNE   MOVEKEY             NO -THEN CONTINUE
         B     DIREAD              YES - READ NEXT BLOCK
MOVEKEY  MVC   OLDKEY,NEWKEY       MOVE IN NEW KEY
         LH    R0,8(R8)            R0 = LENGTH OF DATA
         SH    R0,=H'2'            LESS 2
         STH   R0,PDSDACT          STORE LENGTH OF DATA REMAINING
         LA    R1,10(R8)           R1 -> FIRST ENTRY
         ST    R1,AENTRY           STORE ADDRESS OF NEXT ENTRY
         B     READTEST
         SPACE 3
*              PROCESS DIRECTORY BLOCK
         SPACE
A00AD8   EQU   *
MEMBCT   L     R8,AENTRY           R8 -> MEMBER ENTRY
         USING MEMNAME,R8
         IC    R2,MEMIND           R2 = INDICATORS
         N     R2,=F'31'           R2 = NO OF HALFWORDS OF USERDATA
         SLL   R2,1                R2 = NO OF BYTES
         LR    R1,R8               R1 -> ENTRY
         AR    R1,R2
         LA    R1,12(R1)           R1 -> NEXT ENTRY
         ST    R1,AENTRY           SAVE ADDRESS OF NEXT ENTRY
         LH    R1,PDSDACT          R1 = LENGTH OF DATA REMAINING
         SR    R1,R2               R1 =
         SH    R1,=H'12'           LESS 12
         STH   R1,PDSDACT          STORE NEW LENGTH
         STH   R2,LENTRY
         CLC   MEMNAME,=8X'FF'     MEMBER NAME = ALL X'FF'?
         BNE   MEMBSET
         MVC   PDSBKUS,PDSBKCT     BLOCKS USED = BLOCK COUNT
         B     READTEST
         SPACE 3
*              SET UP PRINT AND PUNCH FIELDS
         SPACE
MEMBSET  MVC   PRWORK,SPACE        CLEAR WORK FIELD
         LH    R1,PDSMBCT          R1 = MEMBER COUNT
         LA    R1,1(R1)            PLUS 1
         STH   R1,PDSMBCT          STORE NEW MEMBER COUNT
         MVC   PRMEMBER,MEMNAME    MOVE NAME TO PRINT
         MVC   PUNMEM,MEMNAME         AND PUNCH FIELDS
         TM    MEMIND,ALIAS        ALIAS NAME?
         BZ    A00B34              NO?
         MVI   PRALIAS,C'A'        YES
         MVI   PUNALIAS,C'A'
A00B34   EQU   *
SETPRINT UNPK  TTRWORK(7),MEMTTRP(4)    TTR
         TR    TTRWORK,TRABLE-240  TRANSLATE TO PRINTABLE CHARS
         MVC   PRTT,TTRWORK        SET UP TRACK NUMBER
         MVI   PRDOT,C'.'
         MVC   PRR,TTRWORK+4       SET UP RECORD NUMBER
         TM    OPBYTE2,FULLPDS     FULL LISTING?
         BZ    SETPUNCH            NO - BYPASS TTR COUNT
         IC    R0,MEMIND           R0 = INDICATORS
         SRL   R0,5
         N     R0,=F'3'            CLEAR ALL BUT LAST TWO BITS
         STC   R0,PRNOTTR          NUMBER OF TTR'S IN USER DATA
         OI    PRNOTTR,X'F0'       SET SIGN
         SPACE 3
*              SET UP PUNCH FIELDS
         SPACE
SETPUNCH EQU   *
         LH    R1,LENTRY           R1 = ENTRY LENGTH
         LTR   R1,R1               ANY USER DATA?
         BZ    NOTSSI              IF ZERO THEN NO SSI PRESENT
         CLI   PUNTYPE,C'L'        LOAD LIBRARY?
         BE    LOADLIB
         B     SETSSI              NO - THEN SET SSI
         SPACE 3
*        LOAD LIBRARY
         SPACE
LOADLIB  SH    R1,=H'21'           LESS MINIMUM LENGTH OF USER DATA
         BZ    NOTSSI              IF ZERO THEN NO SSI
         TM    MEMATTR,X'04'       SCATTER LOADED?
         BZ    TESTAL              NO
         SH    R1,=H'8'            YES - SUBTRACT SCATTER LENGTH
         BZ    NOTSSI              IF ZERO NO SSI
TESTAL   TM    MEMIND,ALIAS        ALIAS?
         BZ    SETSSI              NO
         TM    MEMATTR,X'C0'       YES - AND RENT AND/OR REUS?
         BZ    SETSSI              NO
         SH    R1,=H'11'           YES - SUBTRACT LENGTH
         BZ    NOTSSI
         SPACE 3
*              SET SSI INFORMATION
         SPACE
SETSSI   SRL   R1,1                SWITCH OFF '1' BIT TO TAKE ACCOUNT
         SLL   R1,1                    OF POSSIBLE SLACK BYTE
         CH    R1,=H'4'            SHOULD BE FOUR BYTES LEFT
         BNE   NOTSSI
         L     R3,AENTRY           R3 -> NEXT ENTRY
         SH    R3,=H'4'            R3 -> SSI
         UNPK  TTRWORK,0(5,R3)     UNPACK SSI
         TR    TTRWORK,TRABLE-240  TRANSLATE TO CHARACTERS
         MVC   PUNSSI,TTRWORK      MOVE TO PUNCH FIELD
         MVC   PRSSI,TTRWORK       MOVE TO PRINT FIELD
         B     PUNCHCD
NOTSSI   MVC   PUNSSI,SPACES       CLEAR SSI FIELD
PUNCHCD  EQU   *
         TM    OPTIONS,OPPUNCH     PUNCH REQUIRED?
         BZ    TESTFUL             NO - BYPASS PUNCH
         MVI   PUNCTYPE,C'3'       INDICATE MEMBER CARD
         PERFORM PUNCH            PUNCH CARD
         CLI   OPBYTE2,OPPUNCH     DECK OPTION ONLY ?               MKB
         BE    READTEST            YES - DO NOT PRINT               MKB
TESTFUL  EQU   *
         TM    OPTIONS,FULLPDS     FULL LISTING REQUIRED?
         BZ    SHORTPDS            NO
         SPACE 3
*              SET UP FULL PDS LISTING
         SPACE
         MVC   PRFLDA,PRWORK       MOVE TO PRINT FIELD
         LH    R5,LENTRY           R5 = LENGTH OF USER DATA
         CH    R5,=H'4'            IS IT ONLY SSI?
         BNH   TESTONE             YES - DON'T BOTHER TO DUMP
         CH    R5,=H'44'           NUMBER OF BYTES > 44?
         BNH   SETDUMP              NO - ONLY ONE LINE TO BE PRINTED
         LA    R2,2
         AH    R2,LINECT           ADD CURRENT LINE COUNT
         CH    R2,LINELMT          IS IT > LINE LIMIT?
         BNH   NORESET             NO - THEN CONTINUE
         MVC   LINECT,=H'999'      YES - SET LINE COUNT TO 999
NORESET  EQU   *
         LA    R5,44
SETDUMP  LA    R3,MEMTTRT          R3 -> FIRST BYTE TO BE PRINTED
CONVEX   EQU   *
         LA    R4,PRTEXT           R4 -> DUMP AREA
         PERFORM HEXDUMP
TESTONE  EQU   *
         MVI   PRCTL,C' '          SPACE 1 THEN WRITE
         TS    LINE1               TEST IF THIS IS THE FIRST LINE
         BNZ   PUTLINE             BRANCH IF NOT - ELSE
         MVI   PRCTL,C'0'          SPACE 2 THEN WRITE
         MVC   LINES,=H'2'         ADD 2 TO LINE COUNT
PUTLINE  EQU   *
         PERFORM PRINT
         LH    R5,LENTRY           R5 = LENGTH OF USERDATA
         CH    R5,=H'44'           OVER 44 BYTES?
         BNH   READTEST            NO - GET NEXT ENTRY
         SH    R5,=H'44'           NUMBER OF BYTES REMAINING
         STH    R5,LENTRY         STORE BACK LENGTH OF USER DATA MKB75
*                                 TO BE PRINTED IN SECOND LINE   MKB75
         LA    R3,MEMNAME+56       R3 -> FIRST BYTE ON SECOND LINE
         B     CONVEX              PRINT NEXT 44 BYTES
         SPACE 3
*              SET UP SHORT PDS LIST
         SPACE
SHORTPDS LH    R8,FLDCT            R8 FIELD INDICATOR
         B     MOVEPRT(R8)         BRANCH TO APPROPRIATE MOVE
MOVEPRT  B     MOVE1               BRANCH TO FIRST MOVE
         B     MOVE2               BRANCH TO SECOND MOVE
         B     MOVE3               BRANCH TO THIRD MOVE
         B     MOVE4               BRANCH TO FOURTH MOVE
MOVE1    MVC   PRFLD1,PRWORK       MOVE FIRST FIELD
         B     SETUP               SET UP INDICATORS
MOVE2    MVC   PRFLD2,PRWORK       MOVE TO SECOND FIELD
         B     SETUP               SET UP INDICATORS
MOVE3    MVC   PRFLD3,PRWORK       MOVE TO THIRD FIELD
SETUP    LA    R8,4(R8)            R8 = INDEX TO NEXT FIELD
         STH   R8,FLDCT            SAVE INDICATOR
         B     READTEST            GO TO READ NEXT ENTRY
MOVE4    MVC   PRFLD4,PRWORK       MOVE TO FOURTH FIELD
         TS    LINE1               FIRST LINE?
         BNZ   NEWLINE             NO - BRANCH
         MVI   PRCTL,C'0'          SPACE 2 THEN WRITE
         MVC   LINES,=H'2'         2 LINES
NEWLINE  PERFORM PRINT             PUT LINE
         MVC   FLDCT,=H'0'         RESET INDICATOR TO ZERO
         B     READTEST            GO TO READ NEXT ENTRY
         SPACE 3
*              END OF DIRECTORY
         SPACE
DIREND   CLOSE DIRECTRY
         CLI   OPBYTE2,OPPUNCH     DECK OPTION ONLY ?               MKB
         BE    XIT                 YES                              MKB
         LH    R8,FLDCT            R8 = FIELD COUNT
         LTR   R8,R8               R8 = ZERO?
         BZ    LASTLINE            YES
         TS    LINE1               CHECK THAT A LINE HAS BEEN PRINTED
         BNZ   NOTFIRST            IF SO THE NO PROBLEM
         MVI   PRCTL,C'0'          ELSE SPACE 2
         MVC   LINES,=H'2'            BEFORE WRITING
NOTFIRST EQU   *
         PERFORM PRINT             NO - PRINT UNFINISHED LINE
LASTLINE EQU   *
         LH    R0,PDSMBCT          R0 = NO OF MEMBERS
         PERFORM CONVERT           CONVERT NUMBER OF MEMBERS
         MVC   MEMNO,DECRADJ+8
         LH    R0,PDSBKUS          R0 = NO OF BLOCKS USED
         PERFORM CONVERT           CONVERT NUMBER OF BLOCKS USED
         MVC   DIRUSE,DECRADJ+8
         LH    R0,PDSBKCT          R0 = NO OF BLOCKS ALLOCATED
         PERFORM CONVERT           CONVERT TOTAL NUMBER OF BLOCKS
         MVC   DIRALL,DECRADJ+8
         MVC   PRFLD1+4(84),DIRCON END OF LIST SUMMARY
         MVI   PRCTL,C'0'          SPACE 2 THEN WRITE
         MVC   LINES,=H'2'         ADD 2 TO LINE COUNT
         PERFORM PRINT
         PERFORM PRINT
         MVI   LINE1,OFF           SWITCH LINE1 OFF
         L     R2,ATOPLINE        SET UP PNTRS TO REQ HDRS         SAL2
         MVC   0(4,R2),HDSTOR     RESTORING HDR PNTR               SAL2
XIT      DS    0H                                                   MKB
         GOBACK
         SPACE 3
*              CONVERT TO DECIMAL ROUTINE
         SPACE
CONVERT  LPR   R0,R0
         CVD   R0,PNUM             PNUM = PACKED DECIMAL
         MVC   DECRADJ,MASK1       MOVE IN MASK
         ED    DECRADJ,PNUM+2      EDIT
         EXIT
PNUM     DC    D'0'                PACKED NUMBER
MASK1    DC    X'40',9X'20',X'2120'   EDITING MASK
DECRADJ  DC    CL12' '             RIGHT ADJUSTED WITH SPACES
         SPACE 3
*              CONVERT TO DUMP FORMAT
         SPACE
HEXDUMP  LTR   R5,R5               TEST NUMBER OF BYTES TO BE PRINTED
         BP    SETCOUNT            BRANCH IF POSITIVE
         EXIT                      ELSE EXIT
INSPACE  BCT   R1,EDIT             NO OF BYTES LEFT IN WORD
         MVI   0(R4),C' '          INSERT SPACE
         LA    R4,1(R4)            R4 -> NEXT PRINT CHARACTER
SETCOUNT LA    R1,4                SET WORD COUNT
EDIT     UNPK  TTRWORK(3),0(2,R3)  TRANSLATE BYTE
         TR    TTRWORK(2),TRABLE-240    TO PRINTABLE CHARACTER
         MVC   0(2,R4),TTRWORK     MOVE TO PRINT FIELD
         LA    R3,1(R3)            R3 -> NEXT BYTE
         LA    R4,2(R4)            R4 -> NEXT TWO PRINT CHARACTERS
         BCT   R5,INSPACE          R5 = NO OF BYTES LEFT
         EXIT
         SPACE 3
*        PRINT ROUTINE
         SPACE
PRINT    ST    R14,R14SAVE         SAVE R14
         LA    R1,PCWA             R1 -> PCWA
         CALL  VTOCPRT,((R1))      PRINT LINE
         L     R14,R14SAVE         RESTORE R14
         EXIT
         SPACE
R14SAVE  DS    F                   R14 SAVE AREA
         SPACE 3
*              PUNCH ROUTINE
         SPACE
PUNCH    ST    R14,R14SAVE         SAVE R14
         LA    R1,PCWA             R1 -> PCWA
         CALL  VTOCPCH,((R1))      PUNCH CARD
         L     R14,R14SAVE         RESTORE R14
         EXIT
         SPACE 3
*              SYNAD ROUTINE FOR ERROR READING DIRECTORY
         SPACE
DIRERR   MVI   PRCTL,C'0'          SPACE 2
         MVC   LINES,=H'2'            THEN WRITE
         MVC   PRFLDA(51),READERR  MOVE ERROR MESSAGE
         PERFORM PRINT             PRINT IT OUT
         PERFORM PRINT             FOLLOWED BY A SPACE
         CLOSE DIRECTRY
         L     R2,ATOPLINE        SET UP PNTRS TO REQ HDRS         SAL2
         MVC   0(4,R2),HDSTOR     RESTORING HDR PNTR               SAL2
         XR    R6,R6              CLEAR REGS                       SAL2
         GOBACK
         SPACE 3
HDSTOR   DS    F                  STORE FOR HDR PNTR
READERR  DC    C'ERROR READING DIRECTORY, CONTINUE WITH NEXT DATASET'
DIRCON   DC    CL19'DIRECTORY CONTAINS'
MEMNO    DC    CL4'0000'
MEMUSE   DC    CL18' MEMBERS AND USES'
DIRUSE   DC    CL4'0000'
OFTHE    DC    CL8' OF THE'
DIRALL   DC    CL4'0000'
DIRBLO   DC    CL27' DIRECTORY BLOCKS ALLOCATED'
LINE1    DC    X'00'               FIRST LINE SWITCH
ON       EQU   X'FF'
OFF      EQU   X'00'
AENTRY   DC    A(0)                ADDRESS OF NEXT MEMBER ENTRY
LENTRY   DC    H'0'                LENGTH OF CURRENT ENTRY
PDSDACT  DC    H'0'                LENGTH OF DATA REMAINING
PDSBKCT  DC    H'0'                DIRECTORY BLOCK COUNT
PDSBKUS  DC    H'0'                DIRECTORY BLOCKS USED
PDSMBCT  DC    H'0'                MEMBER COUNT
FLDCT    DC    H'0'
OLDKEY   DC    CL8' '              DIRECTORY BLOCK KEY
         LTORG
TTRWORK  DS    CL9
TRABLE   DC    C'0123456789ABCDEF' TRANSLATE TABLE
HEADS1   DC    A(ULINS1)          ADDR OF UNDERLINE
         DC    H'3'               NO OF LINES IN THIS GROUP
         DC    X'40'              SPACE LINES IN THIS GROUP
         DC    C'-'               WRITE AFTER 3
         DC     CL132' MEMBER      TTR       SSI         MEMBER      TT*
               R       SSI         MEMBER      TTR       SSI         ME*
               MBER      TTR       SSI    '
HEADS2   DC    A(ULINS2)          ADDR OF UNDERLINE
         DC    H'3'               NO OF LINES IN THIS GROUP
             DC    X'40'              SPACE LINES IN THIS GROUP
         DC    C'-'               WRITE AFTER 3
         DC     CL132'  MEMBER     TTR     T SSI                       *
                  USER DATA '
ULINS1   DC    A(0)               NO FURTHER LINES
         DC    H'0'               NO OF LINES 0NES
         DC    X'40'              SPACE
         DC    C'+'              0 LINES TO BE SPACED
         DC    CL133' ______      ___       ___         ______      ___*
                      ___         ______      ___       ___         ___*
               ___      ___       ___ '
ULINS2   DC    A(0)               NO FURTHER LINES
         DC    H'0'               NO OF LINES 0NES
         DC    X'40'              SPACE
         DC    C'+'              0 LINES TO BE SPACED
         DC    CL132'  ______     ___       ___                        *
                 _________ '
HEADING  DC    CL8' MEMBER '
         DC    CL3' '
         DC    CL7'  TTR'
         DC    CL3' T'
         DC    CL8'  SSI'
         DC    CL1' '
ULINE    DC    CL8' ______'
         DC    CL3' '
         DC    CL7'  ___'
         DC    CL3' _'
         DC    CL8'  ___'
         DC    CL1' '
         SPACE
SPACE    DC    CL1' '
PRWORK   DS    0CL30
PRMEMBER DC    CL8' '              MEMBER NAME
         DC    CL1' '
PRALIAS  DC    CL1' '              ALIAS = 'A'
         DC    CL1' '
PRTT     DC    CL4' '              TRACK NUMBER
PRDOT    DC    CL1'.'              TTR DIVIDER
PRR      DC    CL2' '              RECORD NUMBER
         DC    CL1' '
PRNOTTR  DC    CL1' '              NUMBER OF TTR'S IN USER DATA
         DC    CL1' '
PRSSI    DC    CL8' '              SSI
         DC    CL1' '
DMYDSN   DC    CL44' '           SPACES TO CLEAR DSN
DIRECTRY DCB   DDNAME=VOLUME01,                                        *
               MACRF=R,                                                *
               RECFM=F,                                                *
               DSORG=PS,                                               *
               BLKSIZE=256,                                            *
               KEYLEN=8,                                               *
               SYNAD=DIRERR,                                           *
               EODAD=DIREND
         ORG   DIRECTRY
         DS    CL37
DCBEXLST DS    CL3                 EXIT LIST ADDRESS
DCBDDNAM DS    CL8                 DD NAME
         ORG
SPACES   DC    CL8' '
FWORK    DS    F
         SPACE 3
PDHDR    DC    A(0)               2ND HDR FOR PDS
         DC    H'4'               NO OF LINES
         DC    X'40'              SPACES
         DC    C' '               CONTROL CHAR
         DC    CL46' '            DSNAM
         DC    CL86'(CONTINUED) '
*              PDS DIRECTORY BLOCK
         SPACE
DIRBLOK  DS    CL264               DIRECTORY BLOCK
         ORG   DIRBLOK
NEWKEY   DS    CL8                 NAME OF LAST ENTRY IN BLOCK
MEMNAME  DS    CL8                 MEMBER OR ALIAS NAME
MEMTTRP  DS    XL3                 RELATIVE ADDRSS OF FIRST BLOCK
MEMIND   DS    B                   INDICATORS
ALIAS    EQU   B'10000000'         ALIAS INDICATORS
NOTTR    EQU   B'01100000'         NO OF TTRS IN USER DATA
LENTH    EQU   B'00011111'         LENGTH OF USER DATA IN HALFWORDS
MEMTTRT  DS    XL3                 RELATIVE ADDRESS OF FIRST TEXT BLOCK
         DC    X'00'
MEMTTRST DS    XL3                 RELATIVE ADDRESS OF NOTE LIST OR
*                                  SCAT/TRANS TABLE
MEMNLST  DS    FL1                 NUMBER LIST ENTRIES
MEMATTR  DS    BL2                 MODULE ATTRIBUTES
MEMSTOR  DS    FL3                 MAIN STORAGE NEEDED FOR MODULE
MEMLFTXT DS    FL2                 LENGTH OF FIRST TEXT BLOCK
MEMEPAD  DS    FL3                 ENTRY POINT ADDRESS
MEMFTORG DS    FL3                 FIRST TEXT BLOCK ORIGIN
         ORG
         SPACE 3
*              PRINT LINE
         SPACE
PRLINE   DSECT                     PRINT LINE
PRANEXT  DS    A
LINES    DS    H
PRSPACE  DC    X'40'
PRCTL    DS    CL1                 CONTROL CHARACTER
PRFLD1   DS    CL30
         DS    CL4
PRFLD2   DS    CL30
         DS    CL4
PRFLD3   DS    CL30
         DS    CL4
PRFLD4   DS    CL30
         ORG   PRFLD1
         DS    CL2
PRFLDA   DS    CL30
         DS    CL1
PRTEXT   DS    CL99                TEXT FIELD
         ORG
LINECT   DS    H                   LINE COUNT
LINELMT  DS    H                   MAXIMUM NO OF LINES PER PAGE = 57
         SPACE 3
PUNCARD  MIPCARD DSECT,TYPE=OLD
         SPACE 3
*              PROGRAM CONTROL WORK AREA
         SPACE
PCWA     MIPPCWA DSECT
         SPACE 3
*              FORMAT 1 DSCB
         SPACE
F1DSCB   F1DSCB
         SPACE 3
*              FORMAT 4 DSCB
         SPACE
F4DSCB   F4DSCB
         END
./ ADD   LIST=ALL,NAME=VTOCPRT
./ NUMBER  INCR=100,NEW1=100
         TITLE 'VTOCPRT - PRINT ROUTINE OF VTOCLIST'
VTOCPRT  CSECT
         ENTRY OPENPRT,CLOSEPRT
         BEGIN (SAVE)
         YEBEQU
         SPACE 3
*              PROVIDE ADDRESSABILITY FOR PCWA
         SPACE
         L     R11,0(R1)           R11 -> PCWA
         USING PCWA,R11
         L     R10,ATOPLINE        R10 -> TOPLINE-7
         USING TOPLINE,R10
         L     R9,APRLINE          R9 -> PRLINE-7
         USING PRFIELD,R9
         EJECT
*              CHECK LINE COUNT
         SPACE
         TM    FLAGS,GROUPFLG      PART OF A GROUP?
         BO    GROUP               YES
         LH    R1,LINES            R1 = LINE COUNT
         CLI   PRCTL,C' '          SPACE 1 LINE
         BE    SPACE1              OR
         CLI   PRCTL,C'0'          SPACE 2 LINES
         BE    SPACE2              OR
         CLI   PRCTL,C'-'          SPACE 3 LINES
         BE    SPACE3              OR
         CLI   PRCTL,C'+'          NO SPACE
         BE    SPACE0              OR
         MVI   PRCTL,C' '          DEFAULT TO 1
         B     SPACE1
SPACE0   MVC   LINES,=H'0'         SET SPACE COUNT TO ZERO
         B     CHKLINE             CARRY ON
SPACE1   LA    R0,1                SPACE 1
         B     CHKGROUP            OR
SPACE2   LA    R0,2                SPACE 2
         B     CHKGROUP            OR
SPACE3   LA    R0,3                SPACE 3
CHKGROUP CR    R1,R0               LINES LESS THAN SHOULD BE?
         BNL   SETGROUP            NO - CARRY ON
         STH   R0,LINES            YES - STORE MINIMUM
         B     CHKLINE             CARRY ON
SETGROUP SR    R1,R0               ANY LEFT OVER?
         BZ    CHKLINE             NO - NOT GROUP
         OI    FLAGS,GROUPFLG      ELSE ITS THE START OF A GROUP
         STH   R1,GROUPCT          NUMBER OF LINES LEFT IN GROUP
         B     CHKLINE             CARRY ON
         SPACE 3
*              CONTINUATION OF A GROUP
         SPACE
GROUP    CLI   PRCTL,C'+'          NO SPACE
         BE    PUTLINE             NO SPACING -SHOULD BE O K
         LH    R1,GROUPCT          NUMBER OF LINES LEFT IN GROUP
         MVI   PRCTL,C' '          ONLY SINGLE SPACING IN A GROUP
         BCT   R1,GROUPA           ANOTHER LINE
         NI    FLAGS,GROUPOFF      IF ZERO CLEAR GROUP FLAG
GROUPA   STH   R1,GROUPCT          NUMBER OF LINES LEFT
         B     PUTLINE
         SPACE 3
*              CHECK FOR SPACE ON THE PAGE
         SPACE
CHKLINE  EQU   *
         LH    R8,LINECT           R8 = LINE COUNT
         AH    R8,LINES            R8 = LINE COUNT AFTER THIS GROUP
         CH    R8,LINELMT          HIGHER THAN LINE LIMIT
         BL    STORCNT             NO - SAVE COUNT AND PUT LINE
         SR    R8,R8               CLEAR R8
         SPACE 3
*              CONVERT PAGE NUMBER
         SPACE
         LH    R1,PAGECT           R1 = PAGE COUNT
         LA    R1,1(R1)            PLUS 1
         STH   R1,PAGECT           SAVE NEW PAGE COUNT
         CVD   R1,PNUM             CONVERT TO DECIMAL
         MVC   PAGENO,=X'202120'   MOVE IN MASK
         ED    PAGENO-1(4),PNUM+6  EDIT PAGE NUMBER
         PUT   SYSPRINT,HEADER     PUT TOP LINE
PUTHEAD  L     R10,ANEXT           R10 -> NEXT HEADER
         LTR   R10,R10             OR = ZEROS
         BZ    TOPSPACE            CHECK IF SPACE NEEDED
         PUT   SYSPRINT,HEADER     PUT HEADING
         AH    R8,LINES            ADD ON NUMBER OF LINES
         B     PUTHEAD             PUT NEXT HEADER
         SPACE 3
*              INSERT BLANK LINE IF NECESSARY
         SPACE
TOPSPACE TM    OPBYTE1,SETBLANK    BUT ONLY IF FLAG IS ON
         BZ    STORCNT             IF NOT ON THEN BYPASS
         LH    R2,GROUPCT          R2 = GROUP COUNT
         CLI   PRCTL,C'+'          NO SPACE
         BE    STORCNT             OR
         CLI   PRCTL,C'0'          SPACE 2
         BE    STORCNT             OR
         CLI   PRCTL,C'-'          SPACE 3
         BE    TOPS3               OR
         CLI   PRCTL,C' '          SPACE 1
         LA    R1,1(R1)            ADD 1 TO SPACE COUNT
         LA    R2,1(R2)            ADD 1 TO GROUP COUNT
         B     TOPSREST            RESET
TOPS3    BCTR  R1,0                SPACE COUNT LESS 1
         BCTR  R2,0                GROUP COUNT LESS 1
TOPSREST STH   R1,LINES            SAVE SPACE COUNT
         STH   R2,GROUPCT          SAVE GROUP COUNT
         MVI   PRCTL,C'0'          SET CONTROL TO SPACE 2
         SPACE 3
*              PUT PRINT LINE
         SPACE
STORCNT  STH   R8,LINECT           SAVE LINE COUNT
PUTLINE  PUT   SYSPRINT,PRLINE     PUT PRINT LINE
         MVC   PRLINE,SPACE        CLEAR PRINT LINE
         MVC   LINES,=H'1'         RESET LINES TO 1
         B     GOBACK
         SPACE 3
*              OPEN SYSPRINT
         SPACE
OPENPRT  BEGIN (SAVE)
         GETMAIN R,LV=176          GET CORE FOR JFCB
         ST    R1,JFCBAD           STORE ADDRESS
         MVI   JFCBAD,X'87'        INDICATE JFCB
         RDJFCB SYSPRINT           READ JFCB
         L     R1,JFCBAD           R1 -> JFCB
         CLC   102(2,R1),=H'0'     BLOCK SIZE SPECIFIED?
         BNE   @10                 BRANCH IF NOT ZERO
         MVC   102(2,R1),=H'1330'  ELSE SET BLOCK SIZE
@10      OPEN  (SYSPRINT,OUTPUT),TYPE=J
         L     R1,JFCBAD           R1 -> CORE TO BE FREED
         FREEMAIN R,LV=176,A=(R1)  FREE CORE
         B     GOBACK
         SPACE 3
*              CLOSE SYSPRINT
         SPACE
CLOSEPRT BEGIN (SAVE)
         CLOSE SYSPRINT
         B     GOBACK
         SPACE 3
GOBACK   GOBACK
         SPACE 3
PNUM     DC    D'0'                WORK AREA
SAVE     DC    18F'0'
GROUPCT  DC    H'0'                GROUP LINE COUNT
FLAGS    DC    X'00'               FLAGS
GROUPFLG EQU   X'80'               GROUP ON
GROUPOFF EQU   X'7F'               GROUP OFF
         DS    0F
JFCBAD   DC    X'87'
         DC    AL3(0)              ADDRESS OF JFCB
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBA,LRECL=133,           *
               MACRF=PM,EXLST=JFCBAD
         SPACE 3
*              PROGRAM CONTROL WORK AREA
         SPACE
PCWA     MIPPCWA DSECT
         SPACE 3
TOPLINE  DSECT
ANEXT    DS    F
PAGECT   DS    H                   CURRENT PAGE NUMBER
         DS    C
HEADER   DS    CL130
PAGENO   DS    CL3
         SPACE 3
*              PRINT LINE
         SPACE
PRFIELD  DSECT
ACLINE   DS    A                   ADDRESS OF NEXT LINE
LINES    DS    H                   NUMBER OF LINES
SPACE    DS    C                   SPACE
PRCTL    EQU   *
PRLINE   DS    CL133
LINECT   DS    H                   LINE COUNT
LINELMT  DS    H                   MAXIMUM NUMBER OF LINES ON A PAGE
         END
./ ADD   LIST=ALL,NAME=VTOCPCH
./ NUMBER  INCR=100,NEW1=100
         TITLE 'VTOCPCH - PUNCH ROUTINE OF VTOCLIST'
VTOCPCH  CSECT
         ENTRY OPENPCH,CLOSEPCH
         BEGIN (SAVE)
         YEBEQU
         EJECT
*              PROVIDE ADDRESSABILITY TO PCWA
         SPACE
         L     R11,0(R1)           R11 -> PCWA
         USING PCWA,R11
         L     R10,APUNCARD        R10 -> PUNCH CARD IMAGE
         USING PUNCARD,R10
         SPACE 3
*              PUT CARD IMAGE
         SPACE
         PUT SYSPUNCH,PUNCARD      PUNCH CARD IMAGE
         B     GOBACK
         SPACE 3
*              OPEN SYSPUNCH
         SPACE
OPENPCH  BEGIN (SAVE)
         GETMAIN R,LV=176          GET CORE FOR JFCB
         ST    R1,JFCBAD           STORE ADDRESS
         MVI   JFCBAD,X'87'        INDICATE JFCB
         RDJFCB SYSPUNCH           READ JFCB
         L     R1,JFCBAD           R1 -> JFCB
         CLC   102(2,R1),=H'0'     BLOCK SIZE SPECIFIED?
         BNE   @1                  BRANCH IF NOT ZERO
         MVC   102(2,R1),=H'1680'  ELSE SET BLOCKSIZE
@1       OPEN  (SYSPUNCH,OUTPUT),TYPE=J
         L     R1,JFCBAD           R1 -> CORE TO BE FREED
         FREEMAIN R,LV=176,A=(R1)  FREE CORE
         TM    SYSPUNCH+48,X'10'   TEST FOR SUCCESSFUL OPEN
         BO    GOBACK              RETURN IF OK
         NI    OPTIONS,X'EF'       SWITCH OFF PUNCH OPTION
         L     R9,APRLINE          R9 -> PRINT LINE - 7
         USING PRFIELD,R9
         MVC   PRLINE+10(58),=C'UNSUCCESSFUL OPEN FOR SYSPUNCH, NO PUNC*
               HED OUTPUT PRODUCED'
         MVI   PRLINE,C'-'         SPACE 3 THEN WRITE
         MVC   LINES,=H'3'         ADD 3 TO LINE COUNT
         LA    R1,PCWA
         CALL  VTOCPRT,((R1))      PRINT LINE
         B     GOBACK
         SPACE 3
*              CLOSE SYSPUNCH
         SPACE
CLOSEPCH BEGIN (SAVE)
         CLOSE SYSPUNCH
         B     GOBACK
         SPACE 3
GOBACK   GOBACK
         SPACE 3
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,RECFM=FB,LRECL=80,             *
               MACRF=PM,EXLST=JFCBAD
JFCBAD   DC    X'87'
         DC    AL3(0)              JFCB ADDRESS
SAVE     DC    18F'0'              REGISTER SAVE AREA
         SPACE 3
PRFIELD  DSECT
         DS    F
LINES    DS    H
SPACE    DC    X'40'
PRLINE   DS    CL133
         SPACE
         SPACE 3
PUNCARD  DSECT
         DS    CL80                PUNCH CARD IMAGE
         SPACE 3
PCWA     MIPPCWA DSECT
         END
./ ADD   LIST=ALL,NAME=ALLOCATE
./ NUMBER  INCR=100,NEW1=100
         MACRO
&MNAME   ALLOCATE &L
&MNAME   LA    0,&L
.*       GETMAIN R,LV=(0)
         GETMAIN R,LV=(0)
         LR    3,1
         MEND
./ ADD   LIST=ALL,NAME=BEGIN
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    BEGIN &SAVE,&BASE=12,&ID=*
.*
.* THE BEGIN MACRO PROVIDES THE FOLLOWING FACILITIES....
.*   1.  REGISTER SAVING
.*
.*       THE SAVE MACRO IS USED TO SAVE REGISTERS 14,12 IN THE SAVE
.*       AREA ADDRESSED BY REG 13.
.*   2.  IDENTIFIER.
.*
.*       YOU MAY PROVIDE ANY IDENTIFIER (TO HELP FIND THE ENTRY POINT
.*       IN A DUMP) BY CODING 'ID=STRING OF UP TO 70 CHARS' - THE
.*       DEFAULT ID IS THE NAME OF THE MACRO OR THE CSECT NAME UNLESS
.*       YOU CANCEL THE PARAMETER BY CODING 'ID='
.*   3.  DEFINING BASE REGISTERS.
.*       YOU MAY PREVENT ANY BASE REGISTER INITIALIZATION BY CODING
.*       'BASE=NONE', OR SET UP ONE OR MORE BASE REGISTERS BY CODING
.*       'BASE=(10,11,12...)'.  IF THE BASE PARAMETER IS OMITTED REG 12
.*       IS INITIALIZED AS A BASE BY DEFAULT.  NOTE THAT YOU CANNOT USE
.*       SYMBOLIC NAMES FOR THE BASE REGISTERS OTHER THAN THE FIRST.
.*       E.G. BASE=(R10,11,12) IS VALID BUT 'BASE=(R10,R11,R12) IS NOT
.*   4.  SAVE AREA PROVISION.
.*       THIS IS DEFINED BY THE FIRST POSITIONAL PARAMETER.  BY DEFAULT
.*       A 72 BYTE SAVE AREA IS GENERATED IN THE MACRO EXPANSION.
.*       YOU MAY ALSO CODE ONE OF.....
.*       A)  'NOSAVE' CAUSES NO SAVE AREA TO BE GENERATED AND LEAVES
.*             REG 13 AS AT ENTRY. UNLESS NOSAVE IS CODED REG 13 IS
.*             ALWAYS SET TO POINT TO THE NEW SAVE AREA.
.*       B)    IF YOU DEFINE THE SAVE AREA IN YOUR PROGRAM YOU CODE THE
.*             SAVE AREA NAME E.G. 'BEGIN SAVE'.
.*             BY CODING 'BEGIN (SAVE,,SAVE)' YOU WILL GENERATE A
.*             STATEMENT 'USING SAVE,13' TO BE GENERATED AND THIS CAN
.*             BE A USEFUL WAY OF CREATING A SECOND BASE REGISTER FOR
.*       WHAT COMES AFTER THE SAVE AREA IN YOUR PROGRAM.
.*       C)    IF YOU WISH TO ACQUIRE CORE DYNAMICALLY FOR THE SAVE
.*             AREA BEGIN WILL ISSUE A GETMAIN FOR YOU IF YOU CODE
.*             'BEGIN DYNAMIC'.  BY DEFAULT A 72 BYTE AREA WILL BE
.*             ACQUIRED AND REG 13 WILL BE SET TO POINT TO IT.
.*             IF YOU WISH TO ACQUIRE A LARGER AREA FOR OTHER PURPOSES
.*             IN ADDITION TO THE SAVE AREA YOU CODE THE TOTAL LENGTH
.*             AND DSECT NAME FOR THE AREA E.G. ........
.*             'BEGIN (DYNAMIC,240,DYNAR)'.  THIS EXAMPLE WILL RESULT
.*             IN A GETMAIN FOR 240 BYTES FROM SUBPOOL 0, REG 13 WILL
.*             BE SET TO THE ACQUIRED ADDRESS, AND A 'USING DYNAR,13'
.*             WILL BE GENERATED.
.*
.*       FORMAT OF MACRO....
.* NAME  BEGIN NOSAVE                            OR        (POS PARM 1)
.*             (SAVEAREA ADDRESS,,DSECT NAME)    OR
.*             (DYNAMIC,LENGTH,DSECT NAME)
.*             ,BASE=NONE                        OR        (KEYWORD 1)
.*             ,BASE=(REGISTER LIST)
.*             ,ID='CHARACTER STRING'                      (KEYWORD 2)
.*
         LCLA  &A(15),&A0,&A1,&R1,&R2
         LCLC  &GNAME,&C1,&C(5)
&NAME    SAVE  (14,12),T,&ID       SAVE REGISTERS.
         AIF   ('&BASE' EQ ''  OR '&BASE'EQ 'NONE').C10
         BALR  &BASE(1),0          SET FIRST (OR ONLY) BASE REGISTER.
         AIF   (N'&BASE EQ 1).C1
&A0      SETA  1
&A1      SETA  2
.C18     AIF   (K'&BASE LE (&A1+8)).C2
&C(&A0)  SETC  '&BASE'(&A1,8)
&A0      SETA  &A0+1
&A1      SETA  &A1+8
         AGO   .C18
.C2      ANOP
&C(&A0)  SETC  '&BASE'(&A1,K'&BASE-&A1)
         USING *,&C(1)&C(2)&C(3)&C(4)&C(5)   DEFINE BASE REGISTER(S).
&GNAME   SETC  'IHB'.'&SYSNDX'
&C1      SETC  '&GNAME'.'A'
&GNAME   EQU   *                   DEFINE BASE REGISTER ORIGIN.
&A0      SETA  2
.C4      AIF   (&A0 GT N'&BASE).C3
&A1      SETA  &BASE(&A0)
&A(&A1)  SETA  4096*(&A0-1)
&A0      SETA  &A0+1
         AGO   .C4
.C1      USING *,&BASE(1)
         AGO   .C10
.C3      ANOP
&A0      SETA  0
&R1      SETA  1
.C11     ANOP
&R2      SETA  &R1+1
         AIF   (&A(&R1) EQ 0).C5
.C7      AIF   (&A(&R2) EQ 0).C6
&R2      SETA  &R2+1
         AIF   (&R2 LT 16).C7
.C6      AIF   (&R1 EQ (&R2-1)).C8
&A1      SETA  &R2-1
         LM    &R1,&A1,&GNAME.A+&A0     INITIALISE BASE REGISTER(S).
         AGO   .C9
.C8      L     &R1,&GNAME.A+&A0    INITIALISE BASE REGISTER(S).
.C9      ANOP
&A0      SETA  &A0+4*(&R2-&R1)
.C5      AIF   (&R2 GE 16).C10
&R1      SETA  &R2
         AGO   .C11
.C10     AIF   ('&SAVE(1)' EQ '').C12
         AIF   ('&SAVE' EQ 'NOSAVE').C13
         AIF   ('&SAVE(1)' EQ 'DYNAMIC').C14
         LA    15,&SAVE(1)         SET GR15 -> NEW SAVE AREA.
         AGO   .C15
.C12     CNOP  0,4                 FORCE ALIGNMENT ON WORD BOUNDARY.
         BAL   15,*+76             SET GR15-> NEW SAVE AREA.
         DC    18F'0'              NEW SAVE AREA.
         AGO   .C15
.C14     AIF   ('&SAVE(2)' EQ '').C20
         LA    0,&SAVE(2)(0,0)    SET GR0 = LENGTH OF DYNAMIC AREA.
         AGO   .C19
.C20     LA    0,72(0,0)           SET GR0 = LENGTH OF DYNAMIC AREA.
.C19     GETMAIN R,LV=(0)          OBTAIN DYNAMIC NEW SAVE AREA.
         LR    15,1                SET GR15 -> NEW SAVE AREA.
         LM    0,1,20(13)          RESTORE PARAMETER REGISTERS.
.C15     ST    15,8(13,0)          STORE NSA ADDR. IN LSA.
         ST    13,4(15,0)          STORE LSA ADDR.IN NSA.
         LR    13,15               SET GR13 -> NEW SAVE AREA.
         AIF   ('&SAVE(3)' EQ '').C13
         USING &SAVE(3),13         DEFINE GR13 AS BASE REGISTER.
.C13     AIF   (N'&BASE LE 1).EXIT
         BC    15,&GNAME.B         BRANCH ROUND BASE REGISTER VALUE(S).
&A0      SETA  1
.C17     AIF   (&A(&A0) EQ 0).C16
&C1      DC    A(&GNAME+&A(&A0))   BASE REGISTER VALUE.
&C1      SETC  ''
.C16     ANOP
&A0      SETA  &A0+1
         AIF   (&A0 LE 15).C17
&GNAME.B EQU   *
 AIF (&A(13)NE 0 OR &A(15)NE 0 OR(&A(1)NE 0 AND'&SAVE'EQ'DYNAMIC')).E1
.EXIT    MEXIT
.E1      MNOTE 12,'***  IHBNNN  INVALID REGISTER SPECIFIED'
         MEND
./ ADD   LIST=ALL,NAME=EXIT
./ NUMBER  INCR=100,NEW1=100
         MACRO
         EXIT
         BR    14
         MEND
./ ADD   LIST=ALL,NAME=GOBACK
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    GOBACK     &SAVE,&RC=0
.*
.* THE GOBACK MACRO PROVIDES..........
.*   1.  RESETTING OF REG 13.
.*       REG 13 WILL BE RESET ACCORDING TO THE HSA POINTER IN THE
.*       CURRENT SAVE AREA UNLESS YOU CODE 'GOBACK NOSAVE' (IMPLYING
.*       THE YOU ISSUED 'BEGIN NOSAVE')
.*   2.   FREEMAIN OF DYNAMIC SAVE AREA.
.*       IF YOU CODE 'GOBACK (DYNAMIC,LENGTH)' A FREEMAIN WILL BE
.*       ISSUED FOR THE LENGTH YOU SPECIFY FROM THE ADDRESS GIVEN BY
.*       REG 13 (BEFORE RESETTING).
.*   3.  RETURN CODE.
.*       YOU MAY EITHER CODE 'RC=NUMBER' OR PUT THE NUMBER IN REG 15
.*       AND CODE 'RC=(15)'.  BY DEFAULT REG 15 IS SET TO ZERO.
.*   4.        RESTORING REGISTERS.
.*       GOBACK ISSUES 'RETURN (14,12),T,RC=(15)' AFTER OPERATIONS
.*       DESCRIBED IN 2 AND 3 UNLESS YOU CODE 'NOSAVE'.
.*
         AIF   ('&SAVE' EQ 'NOSAVE').C1
         AIF   ('&SAVE(1)' EQ 'DYNAMIC').C2
&NAME    L     13,4(13,0)          SET GR13 -> PREVIOUS SAVE AREA.
.C3      AIF   ('&RC' EQ '' OR '&RC' EQ '(15)').C3A
         AIF   ('&RC'(1,1) NE '(').C3A
         LR    15,&RC(1)           SET GR15 = RETURN CODE.
.C1B     RETURN     (14,12),T,RC=(15)
         MEXIT
.C3A     RETURN     (14,12),T,RC=&RC
         MEXIT
.C1      AIF   ('&RC' EQ '' OR '&RC' EQ '(15)').C1A
         AIF   ('&RC'(1,1) NE '(').C1A
&NAME    LR    15,&RC(1)           SET GR15 = RETURN CODE.
         AGO   .C1B
.C1A     ANOP
&NAME    RETURN     (14,12),T,RC=&RC
         MEXIT
.C2      ANOP
&NAME    LR    1,13                SET GR1 -> CURRENT SAVE AREA.
         L     13,4(13,0)          SET GR13 -> PREVIOUS SAVE AREA.
         AIF   ('&SAVE(2)' NE '').C4
         LA    0,72(0,0)           SET GR0 = LENGTH OF DYNAMIC AREA.
         AGO   .C5
.C4      LA    0,&SAVE(2)(0,0)     SET GR0 = LENGTH OF DYNAMIC AREA.
.C5      SVC   10                  ISSUE FREEMAIN SVC.
         AGO   .C3
         MEND
./ ADD   LIST=ALL,NAME=MIPPCWA
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    MIPPCWA &DSECT
         SPACE
         AIF   ('&DSECT' EQ 'DSECT').DSECT
         DS    0F
&NAME    EQU   *                   PROGRAM CONTROL WORK AREA
         AGO   .CONT
.DSECT   ANOP
&NAME    DSECT                     PROGRAM CONTROL WORK AREA
.CONT    ANOP
         SPACE
VOLSER#  DC    CL6' '              CURRENT VOLUME BEING LISTED
VOLCT    DC    H'0'                NUMBER OF VOLUMES TO BE LISTED
AF1DSCB  DC    A(0)                ADDRESS OF FORMAT 1 DSCB
AF3DSCB  DC    A(0)                ADDRESS OF FORMAT 3 DSCB
AF4DSCB  DC    A(0)                ADDRESS OF FORMAT 4 DSCB
AF5DSCB  DC    A(0)                ADDRESS OF FORMAT 5 DSCB
OPBYTE1  EQU   *                   FLAG BYTE
SETBLANK EQU   X'80'               START SPACES AT TOP OF PAGE
*        EQU   X'40'
*        EQU   X'20'
*        EQU   X'10'
*        EQU   X'08'
*        EQU   X'04'
*        EQU   X'02'
*        EQU   X'01'
         DC    A(0)
OPBYTE2  EQU   *                   OPTION BYTE
OPTIONS  EQU   *                   OPTION BYTE
OPDUMP   EQU   X'01'               DUMP OPTION
OPPDS    EQU   X'02'               PDS LISTING REQUIRED
OPCATLG  EQU   X'04'               CATALOG FLAGGING REQUIRED
OPPUNCH  EQU   X'08'               PUNCHED OUTPUT REQUIRED
OPEXT    EQU   X'10'               DATA SET EXTENTS REQUIRED
FULLPDS  EQU   X'20'               FULL PDS LISTING REQUIRED
OPTERM   EQU   X'40'               TERMINAL OUTPUT REQUIRED
OPUSAGE  EQU   X'80'                    DISC USAGE REQUIRED
         DC    A(0)                RESERVED
         AIF   ('&DSECT' EQ 'DSECT').DSECT2
APUNCARD DC    A(PUNCARD)          ADDRESS OF PUNCH CARD IMAGE
APRLINE  DC    A(PRSTART)          ADDRESS OF PRINT LINE
ATOPLINE DC    A(AHEADING)         ADDRESS OF FIRST HEADING
AJFCB    DC    X'87'               INDICATE JFCB
         DC    AL3(JFCB)           ADDRESS OF JFCB
VOLUMEXX DC    CL6'VOLUME'         CURRENT DDNAME
DDVOLNO  DC    CL2'00'             INCLUDING DD VOLUME NUMBER
PERCENT  DC    H'80'                    DEFAULT %AGE FILLED VALUE
         AGO   .OPT
.DSECT2  ANOP
APUNCARD DS    A                   ADDRESS OF PUNCH CARD IMAGE
APRLINE  DS    A                   ADDRESS OF PRINT LINE
ATOPLINE DS    A                   ADDRESS OF FIRST HEADING
AJFCB    DS    A                   ADDRESS OF JFCB
VOLUMEXX DS    CL8                 CURRENT DD NAME
PERCENT  DS    H                        %AGE FILLED VALUE
.OPT     ANOP
         MEND
./ ADD   LIST=ALL,NAME=PERFORM
./ NUMBER  INCR=100,NEW1=100
         MACRO
&LBL     PERFORM &LAB
         AIF   (K'&LAB GT 8).ERR1
         AIF   (N'&LAB GT 1).ERR2
         AIF   ('&LAB'(1,1) GT 'Z').ERR1
         AIF   ('&LAB'(1,1) LT 'A').NAT
         AGO   .BUILD
.NAT     AIF   ('&LAB'(1,1) EQ '$').BUILD
         AIF   ('&LAB'(1,1) EQ '#').BUILD
         AIF   ('&LAB'(1,1) NE '@').ERR1
.BUILD   ANOP
&LBL     BAL   14,&LAB
         MEXIT
.ERR1    MNOTE 'INVALID LABEL'
         MEXIT
.ERR2    MNOTE 'TOO MANY PARAMETERS SUPPLIED'
         MEND
./ ADD   LIST=ALL,NAME=YEBEQU
./ NUMBER  INCR=100,NEW1=100
         MACRO
         YEBEQU
         LCLA  &A
&A       SETA  0
.LOOP    ANOP
R&A      EQU   &A
&A       SETA  &A+1
         AIF   (&A LT 16).LOOP
         MEND
./ ADD   LIST=ALL,NAME=F1DSCB
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    F1DSCB &ORG
         SPACE
         AIF   (T'&ORG EQ 'O').DSECT
         AIF   ('&ORG' EQ 'DSECT').DSECT
         ORG   &ORG
&NAME    EQU   *                   FORMAT 1 DSCB
         AGO   .CONT
.DSECT   ANOP
&NAME    DSECT                     FORMAT 1 DSCB
.CONT    ANOP
         SPACE
DS1DSNAM DS    CL44                DATA SET NAME
DS1FMTID DS    CL1                 FORMAT IDENTIFIER = X'F1'
DS1DSSN  DS    CL6                 VOLUME SERIAL NUMBER
DS1VOLSQ DS    CL2                 VOLUME SEQUENCE NUMBER
DS1CREDT DS    XL3                 CREATION DATE
DS1EXPDT DS    XL3                 EXPIRATION DATE
DS1NOEPV DS    XL1                 NO OF EXTENTS
DS1NOBDB DS    XL1                 NO OF BYTES USED IN LAST PDS BLK
         DS    CL1                 RESERVED
DS1SYSCD DS    CL13                PROGRAMMING SYSTEM CODE ID
         DS    CL7                 RESERVED
DS1DSORG DS    XL2                 DATASET ORGANISATION
DS1RECFM DS    XL1                 RECORD FORMAT
DS1OPTCD DS    XL1                 OPTION CODES
DS1BLKL  DS    XL2                 BLOCK LENGTH
DS1LRECL DS    XL2                 LOGICAL RECORD LENGTH
DS1KEYL  DS    XL1                 KEYLENGTH
DS1REKP  DS    XL2                 RELATIVE KEY POSITION
DS1DSIND DS    XL1                 DATA SET INDICATORS
DS1SCALO DS    XL4                 SECONDARY ALLOCATION
DS1LSTAR DS    XL3                 POINTER TO LAST WRITTEN BLOCK
DS1TRBAL DS    XL2                 LL PART OF DISK ADDRESS
         DS    CL2                 RESERVED
DS1EXT1  DS    0XL10               FIRST EXTENT DESCRIPTION
         DS    XL1                 EXTENT TYPE INDICATOR
         DS    XL1                 EXTENT SEQUENCE NO (M)
         DS    XL4                 LOWER LIMIT OF EXTENT(CCHH)
         DS    XL4                 UPPER LIMIT OF EXTENT(CCHH)
DS1EXT2  DS    XL10                SECOND EXTENT DESCRIPTION
DS1EXT3  DS    XL10                THIRD  EXTENT DESCRIPTION
DS1PTRDS DS    XL5                 CCHHR OF F2DSCB OR F3DSCB
         MEND
./ ADD   LIST=ALL,NAME=F3DSCB
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    F3DSCB &ORG
         SPACE
         AIF   (T'&ORG EQ 'O').DSECT
         AIF   ('&ORG' EQ 'DSECT').DSECT
         ORG   &ORG
&NAME    EQU   *                   FORMAT 3 DSCB
         AGO   .CONT
.DSECT   ANOP
&NAME    DSECT                     FORMAT 3 DSCB
.CONT    ANOP
         SPACE
DS3KEYID DS    XL4                 KEY IDENTIFIER = 4X'03'
DS3EXT4  DS    XL10                EXTENT DESCRIPTION
DS3EXT5  DS    XL10                EXTENT DESCRIPTION
DS3EXT6  DS    XL10                EXTENT DESCRIPTION
DS3EXT7  DS    XL10                EXTENT DESCRIPTION
DS3FMTID DS    CL1                 FORMAT IDENTIFIER = X'F3'
DS3EXT8  DS    XL10                EXTENT DESCRIPTION
DS3EXT9  DS    XL10                EXTENT DESCRIPTION
DS3EXT10 DS    XL10                EXTENT DESCRIPTION
DS3EXT11 DS    XL10                EXTENT DESCRIPTION
DS3EXT12 DS    XL10                EXTENT DESCRIPTION
DS3EXT13 DS    XL10                EXTENT DESCRIPTION
DS3EXT14 DS    XL10                EXTENT DESCRIPTION
DS3EXT15 DS    XL10                EXTENT DESCRIPTION
DS3EXT16 DS    XL10                EXTENT DESCRIPTION
DS3PTRDS DS    CL5                 RESERVED
         MEND
./ ADD   LIST=ALL,NAME=F4DSCB
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    F4DSCB &ORG
         SPACE
         AIF   (T'&ORG EQ 'O').DSECT
         AIF   ('&ORG' EQ 'DSECT').DSECT
         ORG   &ORG
&NAME    EQU   *                   FORMAT 4 DSCB
         AGO   .CONT
.DSECT   ANOP
&NAME    DSECT                     FORMAT 4 DSCB
.CONT    ANOP
         SPACE
DS4KEYID DS    XL44                KEY IDENTIFIER = 44X'04'
DS4IDEMT DS    CL1                 FORMAT IDENTIFIER =X'F4'
DS4HPCHR DS    CL5                 HIGHEST DISK ADDRESS OF F1DSCB
DS4DSREC DS    XL2                 NO OF AVAIL DSCBS IN VTOC
DS4CCHH  DS    XL4                 CCHH OF NEXTALT
DS4NOATK DS    XL2                 NO OF ALT TRACKS
DS4CTOCI DS    XL1                 VTOC INDICATORS
DS4NOEXT DS    XL1                 VTOC CONSTANT = X'01'
         DS    CL2                 RESERVED
DS4DEVSZ DS    XL4                 NO OF LOGICAL CYLINDERS OR TRACKS
DS4DEVTK DS    XL2                 DEVICE TRACK LENGTH
DS4DEVI  DS    XL1                 CONSTANT FOR KEYED BLOCK
DS4DEVL  DS    CL1                 CONSTANT FOR LAST BLOCK
DS4DEVK  DS    XL1                 CONSTANT FOR NO KEY IN BLOCK
DS4DEVFG DS    XL1                 FLAG BYTE
DS4DEVTL DS    XL2                 DEVICE TOLERANCE
DS4DEVDT DS    XL1                 DSCBS/TRK
DS4DEVDB DS    XL1                 DIRECTORY BLOCKS/TRK
         DS    CL24                RESERVED
DS4F6PTR DS    CL5                 D/A ADDRESS OF 1ST F6DSCB
DS4VTOCE DS    0CL10               EXTENT DESCRIPTION OF VTOC
DS4IND   DS    CL1
DS4EXSEQ DS    CL1
DS4LEXT  DS    CL4
DS4HEXT  DS    CL4
         DS    CL25                RESERVED
         MEND
./ ADD   LIST=ALL,NAME=F5DSCB
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    F5DSCB &ORG
         SPACE
         AIF   (T'&ORG EQ 'O').DSECT
         AIF   ('&ORG' EQ 'DSECT').DSECT
         ORG   &ORG
&NAME    EQU   *                   FORMAT 5 DSCB
         AGO   .CONT
.DSECT   ANOP
&NAME    DSECT                     FORMAT 5 DSCB
.CONT    ANOP
         SPACE
DS5KEYID DS    XL4                 KEY IDENTIFIER = 4X'05'
DS5AVEXT DS    8CL5                8AVAILABLE EXTENTS
DS5FMTID DS    CL1                 FORMAT IDENTIFIER = X'F5'
DS5MAVET DS    18CL5               18 AVAILABLE EXTENTS
DS5PTRDS DS    CL5                 D/A ADDRESS OF NEXT F5DSCB
         MEND
./ ADD   LIST=ALL,NAME=F6DSCB
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    F6DSCB &ORG
         SPACE
         AIF   (T'&ORG EQ 'O').DSECT
         AIF   ('&ORG' EQ 'DSECT').DSECT
         ORG   &ORG
&NAME    EQU   *                   FORMAT 6 DSCB
         AGO   .CONT
.DSECT   ANOP
&NAME    DSECT                     FORMAT 6 DSCB
.CONT    ANOP
         SPACE
DS6KEYID DS    CL4                 KEY ID = 4X'06'
DS6AVEXT DS    8CL5                SHARED EXTENTS
DS6FMTID DS    CL1                 FORMAT IDENTIFIER
DS6MAVET DS    18CL5
DS6PTRDS DS    CL5                 D/A ADDRES OF NEXT F6DSCB
         MEND
./ ADD   LIST=ALL,NAME=MIPCARD
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    MIPCARD &DSECT,&TYPE=NEW
         AIF   ('&DSECT' EQ 'DSECT').DSECT
         DS    0F
&NAME    DS    0CL80               PUNCH CARD IMAGE
         AGO   .CARD
.DSECT   ANOP
&NAME    DSECT                     PUNCH CARD IMAGE
.CARD    AIF   ('&TYPE' EQ 'OLD').OLD
         SPACE
*              VOLUME CARD
PUNVSN   DS    CL6                 VOLUME SERIAL NUMBER
PUNDATE  DS    CL8                 DATE OF RUN
PUNDESC  DS    CL15                VOLUME DESCRIPTION
PUNDEVC  DS    CL8                 DEVICE CODE
PUNOWNID DS    CL10                OWNER ID
         DS    CL32
PUNCTYPE DS    CL1                 INDICATOR = 1 FOR VOLUME
         SPACE
*              DATA SET CARD
         SPACE
         ORG   PUNDESC             BEFORE THIS SAME AS VOLUME CARD
PUNDSN   DS    CL44                DATA SET NAME
PUNTYPE  DS    CL1                 LIBRARY TYPE
PUNCREDT DS    CL5                 CREATION DATE
PUNEXPDT DS    CL5                 EXPIRY DATE
         SPACE
*              MEMBER CARD
         SPACE
         ORG   PUNCREDT            BEFORE THIS SAME AS DATA SET CARD
PUNMEM   DS    CL8                 MEMBER NAME
PUNALIAS DS    CL1                 ALIAS
PUNSSI   DS    CL8                 SSI
         ORG   PUNCARD+80
         SPACE
         MEXIT
.OLD     ANOP
         SPACE
PUNMEM   DS    CL8                 MEMBER NAME
PUNALIAS DS    CL1                 ALIAS
PUNTYPE  DS    CL1                 LIBRARY TYPE
PUNVSN   DS    CL6                 VOLUME SERIAL
PUNDATE  DS    CL8                 DATE OF RUN
PUNDSN   DS    CL44                DATA SET NAME
PUNSSI   DS    CL8                 SSI
         DS    CL3                 RESERVED
PUNCTYPE DS    CL1                 INDICATOR FOR CARD TYPE
         SPACE
         MEND
./ ADD   LIST=ALL,NAME=MIPDSEXT
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    MIPDSEXT
         SPACE
*              OVERLAY MASK FOR EXTENT BLOCK
&NAME    DSECT
         SPACE
DSEXT1   DS    CL28
         SPACE
         ORG   DSEXT1
EXTANEXT DC    A(0)                ADDRESS OF NEXT SEQUENTIAL EXTENT
EXTSCCHH DC    F'0'                STARTING CCHH
EXTECCHH DC    F'0'                ENDING CCHH
EXTNOTRK DC    H'0'                NUMBER OF TRACKS IN EXTENT
EXTSEQ   EQU   *                   EXTENT SEQUENCE NUMBER
EXTTYPND DC    X'0'                OR   EXTENT TYPE INDICATOR
         DC    X'00'               RESERVED
EXTADSN  DC    A(0)                ADDRESS OF DATA SET NAME
EXTSABS  DC    H'0'                STARTING ABSTRAK
EXTEABS  DC    H'0'                ENDING ABSTRAK
EXTDNEXT DC    A(0)                ADDRESS OF NEXT EXTENT FOR DATA SET
         MEND
./ ADD   LIST=ALL,NAME=MIPF1
./ NUMBER  INCR=100,NEW1=100
         MACRO
&NAME    MIPF1 &ORG
&NAME    F1DSCB &ORG
         SPACE
*              END OF FORMAT 1 DSCB
         SPACE
DS1FDAD  DS    CL8                 FULL D-A ADDRESS OF DSCB
DS1AF3   DS    A                   ADDRESS OF FORMAT 3 DSCB
DS1ANEXT DS    A                   ADDRESS OF NEXT FORMAT 1 DSCB
DS1AEXT  DS    A                   ADDRESS OF FIRST EXTENT BLOCK
         MEND
./ ADD   LIST=ALL,NAME=XDATEDIT
ESD      
TXT 
TXT 
TXT 
TXT 
TXT 
END 
