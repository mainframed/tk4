         TITLE     'CLEARUCBS - INSPECT AND RESET CORRUPT UCBS'
********** REQUIRES MACRO #START   *************
********* AT CAV WE USE A MODIFICATION OF THE HASP SVC 254 TO ENTER THE
********* SUPERVISOR STATE. THIS MODULE WILL HAVE TO BE CHANGED TO
********* REFLECT YOUR OWN INSTALLATIONS METHOD OF GETTING THERE.
*
*
* THIS MODULE UNSTICKS (FREES) DEVICES DUE TO BUM ALLOCATION BY THE
* SYSTEM.
*
* IT SCANS THROUGN THE UCBS TO DETERMINE IF EACH UCB
* 1-IS A DIRECT ACCESS DEVICE, 2-IS ALLOCATED, 3-HAS ZERO NUMBER OF
* USERS, 4IS ONLINE, 5-IS NOT CHANGING STATUS, 6-IS NOT PERMANENTLY
* RESIDENT.
*
* ONLY IF ALL OF THE ABOVE CONDITIONS ARE MET, THEN THE DEVICE
* ALLOCATED BIT IN THE UCB IS TURNED OFF, THUS FREEING THE DEVICE AND
* ALLOWING OTHER VOLUMES TO BE MOUNTED.
*
* SAMPLE JCL -
* //STEPNAME EXEC PGM=CLEARUCB
* (NO OTHER JCL REQUIRED)
*
CLEARUCB CSECT
         #START    *
         L         R4,16               * LOAD CVT ADDR
         L         R5,0(R4)            * TCB WORDS
         L         R5,4(R5)            * ADDR CURRENT TCB
         L         R4,40(R4)           * ADDR IECILK2
UCBLOOP  LH        R3,0(R4)            * PICK UP UCB ADDR
         LTR       R3,R3               * ANY ZERO UCB ADDRESSES ...
         BZ        LOOPCTL             * ARE IGNORED
         CH        R3,=X'FFFF'         * REACHED END?
         BE        EXIT                * YES - ALL DONE
         CLI       2(R3),X'FF'         * IS THIS A UCB
         BE        GOTUCB              * YES - WE ARE SAFE
         ABEND     999,DUMP            * ELSE CRASH OUT
         EJECT
GOTUCB   AP        UCBCNT,=P'1'        * COUNT THE UCBS
         CLI       18(R3),X'20'        * DIRECT ACCESS DEVICE
         BNE       LOOPCTL             * NO - WE DON'T WANT IT
         TM        3(R3),B'00001000'   * ALLOCATED ?
         BZ        LOOPCTL             * NO - NOT INTERESTED
         CLI       56(R3),0            * NO OF USERS ZERO?
         BNE       LOOPCTL             * NO - CAN'T FREE IT
         TM        3(R3),B'10000000'   * DEVICE ONLINE?
         BZ        LOOPCTL             * STILL NOT INTERESTED
         TM        3(R3),B'11000000'   * CHANGING STATUS?
         BO        LOOPCTL             * YES - TOUCH IT NOT
         TM        3(R3),B'00000100'   * DO WE RESIDE PERMANENTLY?
         BO        LOOPCTL             * CONTINUE SO TO DO!
         SPACE     3
*** NOW WE KNOW IT NEEDS FIXING
         L         R1,=C'CAVS'         * GRAB SVC STATE
         SVC       254                 * VIA HASP SVC
         LPSW      PSWKEY0             * SET KEY TO ZERO
ANDINSTR EQU       *
         NI        3(R3),B'11110111'   * SWITCH OFF ALLOCATED BIT
         MVZ       PSWKEYT+1(1),28(R5) * RESET PROT-KEY FROM TCB
         LPSW      PSWKEYT             * RETURN TO PROB STATE
OPMSG    AP        UCBCHG,=P'1'        * COUNT THE UCBS CHANGED
         MVC       WTOUNIT(3),13(R3)   * MOVE UNIT NAME
         WTO       MF=(E,WTOMSG)       * WRITE TO CONSOLE
         SPACE     3
LOOPCTL  LA        R4,2(R4)            * BUMP UCB POINTER
         B         UCBLOOP             * SHOOT BACK TO TOP OF LOOP
EXIT     ED        UCBMSG1,UCBCNT      * SET UCB COUNT
         ED        UCBMSG2,UCBCHG      * SET CHANGED UCB COUNT
         WTO       MF=(E,UCBMSG)       * WRITE THE UCB INFO
         L         R13,4(R13)          * RESET R13
         LM        R14,R12,12(R13)     * RESTORE REGS
         SR        R15,R15
         BR        R14
         SPACE      3
         DS        0D                  * PSWS ARE DOUBLEWORD ALIGNED
PSWKEY0  DC        X'FF040000'         * SVC STATE - KEY ZERO
         DC        B'10000000',AL3(ANDINSTR) ILC=2, CC=0
PSWKEYT  DC        X'FF050000'         * PROGM, KEY TO BE SUPPLIED
         DC        B'10000000',AL3(OPMSG)
WTOMSG   DC        Y(WTOEND-*,0)
         DC        C'CLEARUCBS - UCB FOR '
WTOUNIT  DC        C'999'
         DC        C' HAS BEEN RESET'
WTOEND   EQU       *
UCBCNT   DC        PL3'0'
UCBCHG   DC        PL3'0'
UCBMSG   DC        Y(UCBMSGND-*,0)
         DC        C'CLEARUCBS -'
UCBMSG2  DC        XL6'402020202120'
         DC        C' UCBS CHANGED OUT OF'
UCBMSG1  DC        XL6'402020202120'
UCBMSGND EQU       *
         LTORG
         END
