*          DATA SET SOF152YYYY AT LEVEL 022 AS OF 23/01/85
         TITLE '* * * * * * *       O S / D I T T O     * * * * * * *'
         SPACE 20
***********************************************************************
*                                                                     *
*             5798-ARD  COPYRIGHT IBM CORPORATION  1973               *
*     REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE FORM NO. 120-2083     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
* * * * * * * * * * *       O S / D I T T O       * * * * * * * * * * *
*                                                                     *
*                                                                     *
*        XXX       LIST DITTO FUNCTIONS                               *
*        CC        CARD TO CARD                                       *
*        CCS       CARD TO CARD WITH SEQ. NUMBERS AND DECK NAME       *
*        CP        CARD TO PRINTER IN CHARACTER FORMAT                *
*        CD        CARD TO PRINTER IN CHAR. AND HEX DUMP FORMAT       *
*        CT        CARD TO TAPE BLOCKED TO 400                        *
*        CTS       CARD TO TAPE WITH SEQ. NUMBERS AND DECKNAME        *
*        CCL       CANCEL CARD INPUT (FLUSH TO EOF)                   *
*        TC        TAPE TO CARD BLOCKED OR UNBLOCKED                  *
*        TP        TAPE TO PRINTER UNBLOCKED IN CHAR. FORMAT          *
*        TPD       TAPE TO PRINTER DEBLOCKED IN CHAR. FORMAT          *
*        TD        TAPE TO PRINTER UNBLOCKED IN CHAR. AND HEX DUMP    *
*        TDD       TAPE TO PRINTER DEBLOCKED IN CHAR. AND HEX DUMP    *
*        TPV       TAPE TO PRINTER VARIABLE RECORDS IN CHAR. FORMAT   *
*        TDV       TAPE TO PRINTER VARIABLE RECORDS IN CHAR. AND HEX  *
*        TFA       THIS FUNCTION IS NOT AVAILABLE IN OS/DITTO         *
*        TFD       THIS FUNCTION IS NOT AVAILABLE IN OS/DITTO         *
*        TRS       TAPE RECORD SCAN                                   *
*        TRL       TAPE RECORD LOAD                                   *
*        INT       INTIALIZE TAPE                                     *
*        TT        TAPE TO TAPE  (01 TO 99) FILES                     *
*        TTR       TAPE TO TAPE REBLOCKED                             *
*        WTM       WRITE TAPE MARK                                    *
*        REW       REWIND TAPE                                        *
*        RUN       REWIND AND UNLOAD TAPE ( VIA CLOSE MACRO )         *
*        FSR       FORWARD SPACE RECORD                               *
*        BSR       BACK SPACE RECORD                                  *
*        FSF       FORWARD SPACE FILE                                 *
*        BSF       BACK SPACE FILE                                    *
*        ERT       ERASE TAPE, 3420 ONLY-DATA SECURITY ERASE VOLUME   *
*        DSE       DISPLAY DATA SET EXTENT, USE FOR VALID CCCHH       *
*        DD        DISK TO PRINTER IN CHAR. AND HEX FORMAT UNBLOCKED  *
*        SD        DISK TO PRINTER - SPLIT CYLINDER                   *
*        DDD       DISK TO PRINTER IN CHAR. AND HEX FORMAT DEBLOCKED  *
*        SDD       DISK TO PRINTER DEBLOCKED - SPLIT CYLINDER         *
*        DRL       DISK RECORD LOAD                                   *
*        DRS       DISK RECORD SCAN                                   *
*        SRS       DISK RECORD SCAN - SPLIT CYLINDER                  *
*        EOF       WRITE DISK END OF FILE RECORD                      *
*        DID       ALTER DISK IDENTIFICATION VOLUME LABEL (OFFLINE)   *
*        EOJ       END OF JOB                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
**************************     SOURCE BOOK ABSTRACT     ***************
**************************       PROGRAM ABSTRACT       ***************
*          TITLE  - OSDITTO
*          AUTHOR - I.B.M.
*          DATE   - 1973.
*          FUNCTION
*                 MULTIFUNCTION MODULE FOR CARDS,TAPES AND DISKS
*                 SEE OPTIONS ABOVE.
*          SAMPLE OF USE
*                 CAN BE INVOKED THROUGH THE CONSOLE OR BY CARD INPUT
*          PARAMETERS
*                 SEE SEPARATE DOCUMENTATION DESCRIBING THE USE OF
*                 DITTO THROUGH THE CONSOLE OR CARDS.
*          INSTALLATION MACROS INVOKED
*                 #WAYIN (DUMMY SECTION AT END OF MODULE).
*          ADDITIONAL REMARKS
*                 MODULE IS NOT WRITTEN TO STANDARDS.
*                 CARE MUST BE TAKEN IF AMMENDING MODULE BECAUSE
*                 4 BASE REGISTERS ARE ALREADY BEING USED AND
*                 MODULE HAS ALMOST USED UP ALL IT'S ADDRESSABILITY.
*          AMMENDMENT HISTORY
*                 0.1 - JUNE 1980 - BY A.LAMB.
*                 $$AUTH AMMENDED TO CALL DITTOVER EVERY TIME A DITTO
*                 FUNCTION IS BEING USED SO THAT AN SMF RECORD IS
*                 WRITTEN AND DITTO USE CAN BE MONITORED.
*                 1 BYTE INDICATOR ADDED TO FUNCTION VALIDATION
*                 TABLE TO DISTINGUISH FUNCTION TYPE.
*
*                 ***   JAN. 1985  BY JOHN MCGHEE. ***
*                       FROM PANVALET LEVEL 16.
*                 1. MODIFIED TO WORK IN MVS/XA. CHANGES ARE
*                    COMPATIBLE WITH MVS-370.
*                      A) SPLEVEL MACRO ADDED FOR 370 EXPANSIONS
*                      B) 3 BYTE UCB ADDRESSES HANDLED AT
*                         INITIALIZATION ROUTINE.
*                 2. 3380 SUPPORT ADDED TO DISK CHARACTERISTICS TABLE.
*                    2311 AND 2314 SUPPORT REMOVED - THIS MODULE IS
*                    BIG ENOUGH.
*                 3. A.LAMB'S AUTHORIZATION ROUTINE REMOVED SINCE
*                    NOBODY SEEMS TO WANT IT.
*                 4. NBUFPAGS AND DSL/DTPIO CALCULATION CHANGED
*                    TO GIVE A BUFFER BIG ENOUGH FOR A FULL
*                    3380 TRACK (47,476).
*                 5. DEFAULT TAPE MODE SET MADE D0 = 9 TRK 6250 BPI
*
*          PROGRAM DOCUMENTATION
*                 SEE SEPARATE DEPARTMENT MANUAL CALLED 'DITTO'
***********************************************************************
         EJECT
***********************************************************************
**********              SAVE REGISTERS                      ***********
**********              SET UP BASE REGISTERS               ***********
**********              BRANCH TO INIALIZATION ROUTINE      ***********
***********************************************************************
         SPACE 1
OSDITTO  CSECT
         SPLEVEL SET=1                ENSURE 370 STYLE MACROS
         USING *,RF
         SAVE  (14,12)
         LR    R2,RD
         DROP  RF
         BALR  R6,R0
         USING *,R6,RD,RE,RF           SET
         L     R7,BUMP                 UP
         LR    RD,R6                   THE
         AR    RD,R7                   BASE
         LR    RE,RD                   REGISTERS
         AR    RE,R7
         LR    RF,RE
         AR    RF,R7
         B     INITIALZ            GO DO INIALIZATION IN IO AREA
COPYRT   DC    C'5798-ARD COPYRIGHT IBM CORPORATION 1973'
         DC    C'05-01-74 VERS.1 MOD.3' MAINT. LEVEL
NBUFPAGS EQU   12                  THIS EQU CONTROLS THE SIZE OF THE
*                                  DISK/TAPE I/O BUFFER AND CCW BYTE
*                                  COUNT IN 4K INCREMENTS. IT IS SIG-
*                                  NIFICANT FOR TWO REASONS:
*                                      1. REGION/PARTITION SIZE IN MFT
*                                         AND MVT. ALSO VS/1 AND VS/2
*                                         IF OS/DITTO IS RUN V=R.
*                                      2. CCW BYTE COUNT IS USED BY IOS
*                                         TO DETERMINE NO. OF SHORT-
*                                         TERM PAGE FIXES FOR I/O.
*                                  A VALUE OF 7 IN THE EQU WILL ALLOC.
*                                  14K WHICH IS LARGE ENOUGH FOR FULL
*                                  TRK 3330 BLKSIZES. IT MAY BE REDUCED
*                                  IF SMALLER BUFFER IS DESIRED.
         DS    0F
SAVEAREA DC    3F'0'                   FORWARD BACKWARD PTRS
         DC    15F'0'                  SAVE R14 TO R12 - CALLED PGM
SAVREGS  DC    14F'0'                  SAVE R1 TO R14, DITTO TAPERR RTN
SV13TO15 DC    3F'0'               SAVE BASE REGS, LM AFTER EACH MACRO
BUMP     DC    F'4096'
         DC    F'0'                MAKE BUMP 2 WORDS
SAV      EQU   BUMP                USE BUMP AS SAV
SAVRCSZ  EQU   SAVREGS             SAVE RECSIZE VALUE AT SAVREGS
         EJECT
***********************************************************************
*********      CHECK FOR INTERRUPT OF A FUNCTION HERE    **************
***********************************************************************
         SPACE 2
CHKIRPT  EQU   *
         MVI   IRPTECB,X'00'           RESET WTOR ECB FOR FUNC IRPT
         OC    IRPT(4),BLANKS          SET INTERUPT REPLY TO UPPER CASE
         CLC   IRPT(4),IRPTDC          IRPT A FUNCTION ?
         BE    INTRP                   RESTORE IRPT & ASK FOR NEW FUNC
         CLC   IRPT(3),EOJDC           GO TO EOJ ?
         BE    EOJ
         MVC   CONIO(8),INVLDMSG       'INVALID '
         MVC   CONIO+8(12),IRPTMSG     'REPLY: IRPT '
         MVC   CONIO+20(6),=C'OCCURS'
         MVI   WCNCCW+7,X'1A'      26 CHAR. MSG,
         BAL   R8,WCON2                FALL THRU TO INTRP AFTER WCON2
         EJECT
***********************************************************************
**********    WTOR INTERRUPT ROUTINE & CONSOLE INPUT        ***********
***********************************************************************
         SPACE 1
INTRP    EQU   *
         MVI   IRPTECB,X'00'           RESET WTOR ECB FOR FUNC IRPT
         MVC   IRPT(4),BLANKS          BLANK OUT WTOR REPLY AREA
         WTOR  '***** OS/DITTO ACTIVE *****',IRPT,4,IRPTECB,           X
               DESC=6,ROUTCDE=2
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         MVC   CONIO(57),IRPTMSG
         MVI   WCNCCW+7,X'39'
         BAL   R8,WCON2
         MVC   CONIO(30),ENTERMSG  'ENTER: XXX . . .
         MVI   WCNCCW+7,X'1E'
         BAL   R8,WCON2
CONSOLE  EQU   *
         MVC   SCALE(20),BLANKS    BLANK SCALE AREA
         MVI   SCANVSW+1,X'00'     SET SW TO NOP AFTER EACH FUNCTION
         LA    R8,SHCCW                RESET SEEK CCW
         ST    R8,DSKCCWA              IN DISK IOB
         LA    R8,DTPIO            RESET IO POINTER IN
         ST    R8,RWCCW            DISK CCW.
         ST    R8,OTTPCCW          RESET IO ADDRESS IN OUT TAPE CCW
         MVI   BLCT,X'FF'          RESET
         MVI   BLCT+1,X'FF'        SIMULATED END OF FILE SW.
         MVC   INTPCCW+6(2),DSL+2  RESET TAPE LNGTH
         MVC   OTTPCCW+6(2),DSL+2  IN TAPE CCWS.
         MVI   LISTSW,0            PREPARE LISTSW FOR ZERO PROPOGATION
         MVC   LISTSW+1(LNOFMOVE),LISTSW   SET SW'S & W/A'S TO X'00'
         MVI   OTTPCCW,X'01'       RESET OUT TAPE TO WRITE.
         MVI   INTPCCW,X'02'       RESET IN-TAPE TO READ
         MVI   INTPERSW,X'00'      TURN OFF OPER ERROR HANDLING-INTAPE
         MVC   HH2,HH1+7           SET HEADING FORMAT.
         MVC   ADRSLO(215),BLANKS  BLANK OUT HEADING BUFFERS
         CLI   CNTRLSW,X'FF'       DO WE READ CONTROL CARDS ?
         BE    CNTRLCD             YES,  GO TO CONTROL CARD ROUTINE.
         MVI   CDEOFSW+1,X'F0'     DO NOT CHK FOR /+ DELIM. IN CONSL OP
         MVI   INTPERSW,X'02'      NO, SET TAPEIN FOR OPER ERR HANDLING
         MVC   CONIO(6),DITTODC        'DITTO '
         MVC   CONIO+6(9),FUNCMSG      'FUNCTION '
         MVI   CONIO+15,C'?'           '?'
         MVI   WCNCCW+7,X'10'
         MVI   RCNCCW+7,X'40'      SET FOR REPLY
         MVI   P,X'00'             SET PARAM COUNTER = ZERO
         LA    R2,P1               INDEX TO P1
         LA    R5,5                NUMBER OF P SAVE AREAS
         BAL   R8,WRTCON
         LA    R1,CONIO+2
         MVC   FUNCOD,CONIO        SAVE FUNCTION CODE
         CLI   FUNCOD+2,C','       COMMA AFTER FUNCTION ?
         BNE   CONSL1              NO, GO CHECK 3RD POSITION
         MVI   FUNCOD+2,C' '       YES, OVERLAY WITH BLANK
         B     CONSL2              GO SCAN PARAMETERS
CONSL1   LA    R1,CONIO+3
         CLI   CONIO+3,C','        COMMA AFTER 3RD POSITION ?
         BNE   CONDONE             NO, NO PARAMETERS - WE ARE DONE
CONSL2   MVC   P1(50),BLANKS       BLANK PARAM SAVE AREAS
SCNMOV   LA    R1,1(R1)            BUMP INPUT PAST COMMA
         LA    R4,11               MAX. LENGTH OF PARAM SAVE AREA + 1
         LR    R3,R1               SAVE ADDRESS OF INPUT
SCNMOV1  CLI   0(R1),C','          COMMA IS END OF PARAMETER ?
         BE    SCNMOV2             YES
         CLI   0(R1),C' '          BLANK IS END OF PARAMETER ?
         BE    SCNMOV2             YES
         LA    R1,1(R1)            NO, BUMP TO NEXT INPUT CHARACTER
         BCT   R4,SCNMOV1            AND GO CHECK AGAIN
         B     CONDONE             INVALID LENGTH, EXIT
SCNMOV2  IC    R7,P                ADD 1 TO
         LA    R7,1(R7)             VALID PARAMETER
         STC   R7,P                  COUNTER
         LA    R7,11               LENGTH OF P1 + 1 IN REG 7
         SR    R7,R4               LESS REG 4 = LENGTH OF INPUT PARAM
         LTR   R7,R7               WAS PARAM OMITTED ?  (R7 = 0)
         BZ    SCNMOV3             YES, BYPASS DATA MOVE
         BCTR  R7,R0               DECREMENT LNTH FOR MOVE
         EX    R7,SCNMOVE          MOVE DATA
SCNMOV3  LA    R2,10(R2)           BUMP TO NEXT PARAMETER SAVE AREA
         BCTR  R5,R0               DECREMENT SAVE AREA COUNTER
         LTR   R5,R5               ARE WE OUT OF SAVE AREAS ?
         BZ    CONDONE             YES, EXIT
         CLI   0(R1),C' '             MORE INPUT PARAMETERS ?
         BNE   SCNMOV              YES, GO SCAN NEXT PARAM
CONDONE  BAL   R8,LOOKUP           GO LOOK UP FUNCTION
         MVC   CONIO(8),INVLDMSG       'INVALID '
         MVC   CONIO+8(8),FUNCMSG      'FUNCTION'
         MVI   WCNCCW+7,X'10'
         BAL   R8,WCON2
         B     CONSOLE             INVALID FUNCTION , RETURN TO CONSOLE
SCNMOVE  MVC   0(1,R2),0(R3)       VARIABLE MOVE MODIFIED BY EXECUTE
P        DC    X'00'               COUNTER = NUMBER OF ACTIVE SAVE AREA
P1       DS    CL10
P2       DS    CL10                PARAMETER
P3       DS    CL10                 SAVE
P4       DS    CL10                  AREAS
P5       DS    CL10
         EJECT
***********************************************************************
**********                                                   **********
**********   AUTHORISATION ROUTINE FOR ALL DITTO FUNCTIONS   **********
**********   MODULE  DITTOVER  IS  CALLED  WHICH  COLLECTS   **********
**********     1 USER'S NAME                                 **********
**********     2 AUTHORISER'S NAME                           **********
**********     3 DEPT. OF AUTHORISER                         **********
**********     4 TAPES AND DISKS VOLSERS                     **********
**********     5 FUNCTIONS USED                              **********
**********   THEN WRITES AN SMF RECORD FOR EACH FUNCTION     **********
**********   ALSO TESTS FOR NON ZERO RETURN CODE FROM        **********
**********   DITTOVER - IF NON ZERO DITTO ENDS               **********
**********   DUE TO DISK VOLSER NOT BEING FOUND              **********
**********                                                   **********
***********************************************************************
*                REMOVED BY J.MCGHEE  JAN. 1985.                      *
***********************************************************************
         SPACE
*        CNOP 2,4                       BOUNDARY ALIGNMENT
*$$AUTH   EQU   *
*        MVC   $$TYPE,0(R5)             LOAD FUNCTION & TYPE PARM
*        BAL   R1,$$CALL                BRANCH ROUND CONSTANTS
*$$PARM   DC    A(CONIO,$$TYPE,IRPTECB)
*$$VERA   DC    V(DITTOVER)
*$$CALL   EQU   *
*        L     RF,$$VERA
*        BALR  RE,RF                    GO RECORD DITTO USE
*        LTR   RF,RF                    SET CONDITION CODE
*        LM    RD,RF,SV13TO15           RESTORE BASE REGS AFTER CALL
*        BNZ   EOJ                      BAD RETURN CODE...DITTO ENDS
*        B     4(R5)                    ZERO...GO GET DITTO FUNCTION
         EJECT
***********************************************************************
**********               CONTROL CARD INPUT                 ***********
***********************************************************************
         SPACE 2
CNTRLCD  BAL   R8,DHDG
         BAL   R8,OVFLO            SKIP TO CHAN 1
         BAL   R8,CDRD             READ A CARD
         CLI   EOF,X'FF'           /*, /+, OR HDWR END OF FILE ?
         BE    EOJ                 YES, EXIT
         MVI   PL,X'50'            SET PRINT LNTH TO 80
         BAL   R8,PRNT             PRINT THE CONTROL CARD.
         CLC   CONIO(8),CTL        CC 1-8 = '$$DITTO '     ?
         BE    CTLOK               YES
         CLI   CONIO,C' '          IS $$DITTO VALIDLY OMMITTED
         BE    CTLOK00             YES
         B     BADCTL              ISSUE BAD CONTROL CARD MSG
CTLOK00  EQU   *
         LA    RB,CONIO+1          YES, SET UP FOR CONTROL CARD SCAN
         LA    R5,71               AND SCAN LENGTH
         B     IDLOOP
         LA     R5,71              AND SCAN LENGTH
CTLOK    LA    RB,CONIO+8          YES, SET UP FOR CONTROL CARD SCAN
         LA    R5,64               SCAN TO 72 INCLUSIVE. 73-80 = SEQ NO
IDLOOP   CLI   0(RB),C' '          FUNCTION CODE ?
         BNE   HOLDID              YES, GO SAVE IT
         LA    RB,1(RB)            BUMP SCAN POINTER
         BCT   R5,IDLOOP           GO LOOK AGAN
         B     BADCTL              NO ID
HOLDID   MVC   FUNCOD,0(RB)        SAVE FUNCTION CODE
         LA    RB,3(RB)            BUMP PAST FUNC. CODE
         BCTR  R5,R0               DECREMENT
         BCTR  R5,R0                COUNTER
         BCTR  R5,R0
PARMLOP  CLI   0(RB),C' '          PARAMETERS GIVEN ?
         BNE   CKBG                YES
         LA    RB,1(RB)            NO, BUMP POINTER
         BCT   R5,PARMLOP           AND LOOK AGAIN
         B     CHKID               NO PARAMETERS, EXIT
CKBG     CLC   BG,0(RB)            PARAM = BEGIN ?
         BNE   CKEN                NO, BRANCH.
         LA    RB,6(RB)
         MVC   ADRSLO,0(RB)        YES, SAVE IT.
         LA    RB,5(RB)
         OI    CT,X'02'            SET BEGIN SW ON.
         B     BUMPIT
CKEN     CLC   EN,0(RB)            PARAM = END ?
         BNE   CKIN                NO, BRANCH
         LA    RB,4(RB)
         MVC   ADRSUP,0(RB)        YES, SAVE IT.
         LA    RB,5(RB)
         OI    CT,X'01'            SET END SW ON.
         B     BUMPIT
CKIN     CLC   IN,0(RB)            PARAM = INPUT ?
         BNE   CKOT                NO, BRANCH.
         LA    RB,6(RB)            INCR PAST INPUT=
         LA    R1,8                SET SCAN LENGTH
         BAL   R8,DDNMSCN          LOOK FOR TAPEIN, DISKIN, OR TAPEOUT
         MVC   INDDSAV,DDWK1       SAVE DDNAME
         BAL   R8,CKTPINDD         IS OPEN REQ'D ?  VALIDATE DDNAME
         OI    CT,X'80'            SET INPUT SW ON.
         B     BUMPIT
CKOT     CLC   OT,0(RB)            PARAM = OUTPUT ?
         BNE   CKNB                NO, BRANCH.
         LA    RB,7(RB)
         LA    R1,8
         BAL   R8,DDNMSCN          LOOK FOR TAPEOUT OR TAPEIN
         MVC   OUTDDSAV,DDWK1      SAVE DDNAME
         BAL   R8,CKTPOTDD         OPEN REQ'D ? VALIDATE DDNAME
         OI    CT,X'40'            SET OUTPUT SW ON.
         B     BUMPIT
CKNB     CLC   NB,0(RB)            PARAM = NBLKS ?
         BNE   CKRS                NO, BRANCH.
         LA    RB,6(RB)
         LA    R1,4                MAX. LNGTH OF NBLKS VALUE
         BAL   R8,RJUST            GO RT ADJUST
         MVC   HNB,0(RC)           VALUE IN HNB
         LA    R1,HNB              PRIME R1 FOR TSTNUM1
         LA    RC,4(R0)            SET RC TO MAX SIZE OF 4
         BAL   R8,TSTNUM1          TEST FOR NUMERIC.
         B     BADCTL
         MVC   HOLD+4(4),HNB       CHANGED TO HNB FOR NBLKS
         BAL   R8,BINCVT           CONVERT TO BINARY
         STH   R1,BLCT             AND STORE.
         LTR   R1,R1               IS IT ZERO ?
         BZ    BADCTL              YES, INVALID
         OI    CT,X'20'            SET NBLKS SW ON.
         B     BUMPIT
CKRS     CLC   RS,0(RB)            PARAM = RECSIZE ?
         BNE   CKDT                NO, BRANCH.
         LA    RB,8(RB)
         LA    R1,5                MAX. LNGTH OF RECSIZE VALUE
         BAL   R8,RJUST            GO RT ADJUST
         MVC   HRS,0(RC)           VALUE IN HRS
         OI    CT,X'10'            SET RECSIZE SW ON.
         B     BUMPIT
CKDT     CLC   DT,0(RB)            PARAM = DECKTYPE ?
         BNE   CKBF                NO, BRANCH.
         LA    RB,9(RB)
         MVC   DECKTYPE,0(RB)      YES, SAVE IT.
         LA    RB,3(RB)
         OI    CT,X'08'            SET DECKTYPE SW ON.
         B     BUMPIT
CKBF     CLC   BF,0(RB)            PARAM = BLKFACTOR ?
         BNE   CKDN                NO, BRANCH.
         LA    RB,10(RB)
         LA    R1,3                MAX. LNGTH OF BLKFACTOR
         BAL   R8,RJUST            GO RT ADJUST
         MVC   HBF,0(RC)           VALUE IN HBF
         OI    CT,X'04'            SET BLKFACTOR SW ON.
         B     BUMPIT
CKDN     CLC   DN,0(RB)            PARAM = DECKNAME ?
         BNE   BUMPIT              NO, BRANCH.
         LA    RB,9(RB)
         LR    R3,RB               YES, SET UP TO SAVE VARIABLE
         SR    RC,RC               LENGTH PARAMETER
         CLI   0(RB),C','          COMMA = END OF VALUE ?
         BE    BUMPIT              YES, GO MOVE IT
         CLI   0(RB),C' '          BLANK = END OF VALUE ?
         BE    BUMPIT              YES, GO MOVE IT
DNLOOP   LA    RB,1(RB)            BUMP PAST DATA ELEMENT
         CLI   0(RB),C' '          IS NEXT POS. BLANK ?
         BE    FILLIT              YES, GO MOVE DATA
         CLI   0(RB),C','          IS NEXT POS. A COMMA ?
         BE    FILLIT              YES, GO MOVE DATA
         LA    RC,1(RC)            NO, ADD 1 TO COUNTER
         B     DNLOOP              AND CHECK AGAIN
FILLIT   LA    R1,7(R0)            MAX FIELD LNGTH MINUS 1
         CR    RC,R1               USER GIVE MORE THAN 8 POS. ?
         BH    BADCTL              YES, BAD FORMAT
         STC   RC,*+5              NO, STORE LNGTH MINUS 1
         MVC   DECKNAME,0(R3)      IN MOVE INSTRUCTION
BUMPIT   CLI   0(RB),C' '          CHECK FOR BLANK
         BE    CHKID               YES, END OF PARAMETERS, EXIT
         CLI   0(RB),C','          OR COMMA .
         BNE   BADCTL              NO, BAD FORMAT
         LA    RB,1(RB)            YES, BUMP PAST COMMA
         B     CKBG                AND LOOP FOR NEXT PARAMETER
RJUST    MVC   WK2,0(RB)           VARIABLE LENGTH VALUE IN WK2
         LR    R0,R1               SAVE MAX. LNGTH OF VALUE
         LA    RC,WK2+1            WORK REGISTER
RJUST1   CLI   0(RC),C' '          END OF PARAMETER ?
         BE    RTMOVE              YES
         CLI   0(RC),C','          COMMA ?
         BE    RTMOVE              YES
         LA    RB,1(RB)            BUMP CONTROL CARD SCAN POINTER
         LA    RC,1(RC)            BUMP WORK POINTER
         BCT   R1,RJUST1           LOOP AND LOOK AGAIN
RTMOVE   SR    RC,R0               12 NOW POINTS TO BEGIN OF RT ADJ VAL
         LA    RB,1(RB)            BUMP CONTROL CARD SCAN TO DELIMITER
         BR    R8                  RETURN
DDNMSCN  MVC   DDWK1,BLANKS        CLEAR WORK AREA
         MVC   DDWK1,0(RB)         MOVE IN VAR LEN DDNAME
         LR    R0,R1               SAVE MAX VALUE OF 8
         LA    RC,DDWK1+1          PRIME SCAN PTR TO 2ND CHAR
DDNMSCN1 CLI   0(RC),C' '          BLANK ?
         BE    DDSCNDON
         CLI   0(RC),C','          COMMA ?
         BE    DDSCNDON
         LA    RB,1(RB)            BUMP CTL CRD SCAN PTR
         LA    RC,1(RC)            BUMP DDNAME  SCAN PTR
         BCT   R1,DDNMSCN1         LOOP CTL
DDSCNDON LA    RB,1(RB)            BUMP CTL CRD SCAN TO DELIMITER
         MVC   0(7,RC),BLANKS      SET RT PORTION OF DDNM TO BLANKS
         BR    R8
CHKID    BAL   R8,LOOKUP           GO LOOKUP FUNCTION
BADCTL   MVC   CONIO(80),BLANKS    RETURN HERE IF INVALID
         MVC   CONIO(42),CTLERROR  'CONTROL CARD ERROR, . . .
         BAL   R8,PRNT
         MVI   ABENDSW+1,X'F0'     SET RETURN SW TO XXXRET
         B     LSTFUNCT            GO LIST DOCUMENTATION FOR PGMR
XXXRET   EQU   *
         MVI   HIORDBIT,X'00'      SET DUMP BIT OFF
**************************** HAND CODED ABEND MACRO *******************
ABEND    DS    0H
         CNOP  0,4
         B     LOADCODE
HIORDBIT DC    AL1(128)            DUMP BIT SET UNTIL BADCTL CARD
UCODE    DC    AL3(2048)           U2048 TILL SET BY OTHER THAN BADCTL
*                                  U2048 IS CONTROL CARD ERROR
LOADCODE L     R1,HIORDBIT         LOAD CODES INTO R1
         SVC   13                  ABEND SVC
**************************** END OF THE ABEND MACRO *******************
U200     DC    AL3(2049)           INADEQUZTE BUFFER
U300     DC    AL3(2050)           UNUSUAL TAPE I/O ERROR
U400     DC    AL3(2051)           UNUSUAL DISK I/O ERROR
U500     DC    AL3(2052)           CURR. INPUT BLK , 1ST IN TTR RTN
*                                  OR I/P BLK NOT MULTIPLE OF RECSIZE.
WK1      DC    CL6'000000'             HI ORDER 0'S FOR RT. JUSTIFY
WK2      DS    CL6                     LOW ORDER WORK AREA
         EJECT
***********************************************************************
**********         VALIDATE AND TRANSLATE TAPE MODE SET     ***********
***********************************************************************
         SPACE 1
CHKMOD   LA    R3,MSICCW           SET IN-TAPE IOB TO
         ST    R3,INTPCCWA         CHAIN TO MODE SET CCW.
         LA    R3,MSOCCW           SET OUT-TAPE IOB TO
         ST    R3,OTTPCCWA         CHAIN TO MODE SET CCW.
         OI    INTPIOB,X'40'           FORCE COMM. CHAIN BIT ON
         OI    OTTPIOB,X'40'           FORCE COMM. CHAIN BIT ON
         LA    R3,MODTBL           SET UP FOR
         LA    R9,MODTBL2          TABLE SEARCH.
         CLC   CONIO+3(2),ZEROS    WAS MODE SET ZEROS ?
         BNE   NOTZERO             NO, BRANCH.
         MVC   CONIO+3(2),MODTBL   YES, SET MODE = C0.
NOTZERO  CLC   CONIO+3(2),BLANKS   MODE SET BLANKS ?
         BNE   MODLOOP             NO, BRANCH.
         MVC   CONIO+3(2),MODTBL   YES, SET MODE = C0
MODLOOP  CLC   CONIO+3(2),0(R3)    OPERATOR MODE SET MATCH TABLE ?
         BE    GOODMOD             YES, GO SET MODE.
         CLI   0(R3),X'FF'         NO, ARE WE AT END OF TABLE ?
         BE    BADMOD              YES, BAD MODE GIVEN.
         LA    R3,2(R3)            NO, BUMP TABLE
         LA    R9,1(R9)            LOOKUP.
         B     MODLOOP
GOODMOD  MVI   CONIO+5,X'09'       SET 9 TRK SW.
         TM    0(R9),X'C0'         IS MODE FOR 9 TRK ?
         BO    4(R8)               YES, BRANCH.
         MVI   CONIO+5,X'07'       NO, SET 7 TRK SW.
         B     4(R8)               RETURN.
BADMOD   MVC   CONIO(8),INVLDMSG       'INVALID '
         MVC   CONIO+8(8),=C'MODE SET'
         MVI   WCNCCW+7,X'10'
         B     WCON2
MODTBL   DC    CL36'D01020283038506068707890A0A8B0B8C8C0'
         DC    X'FF'
MODTBL2  DC       X'D313232B333B53636B737B93A3ABB3BBCBC3'
         EJECT
***********************************************************************
**********               VERTICAL HEX ROUTINE               ***********
***********************************************************************
         SPACE 1
HEXDP    MVC   HEXIO+20(100),0(RA)     MOVE DATA TO HEX AREA.
         SR    R8,R8               BUMP PRINT LENGTH
         IC    R8,PRLTH            BY
         LA    R8,32(R8)           32 POSITIONS
         STC   R8,PL               FOR HEX HEADINGS.
         MVC   CONIO(7),BLANKS
         MVI   INPUT,X'FF'         SET IGNORE OVERFLOW SW ON.
         MVC   CONIO(26),HDG1      SET UP FIRST
         MVC   HEXIO+14(6),DET1    HEADING.
         MVI   CC,X'09'            SINGLE SPACE.
         BAL   R8,EDIT             EDIT AND PRINT.
         MVC   HEXIO+20(1),0(RA)       MOVE
         PACK  HEXIO+20(0),HEXIO+20(0) ZONES AND
         MVO   HEXIO+21(16),0(16,RA)   WITH OFFSET TO
         MVO   HEXIO+36(16),15(16,RA)  NUMERIC POSITIONS.
         MVO   HEXIO+51(16),30(16,RA)
         MVO   HEXIO+66(16),45(16,RA)
         MVO   HEXIO+81(16),60(16,RA)
         MVO   HEXIO+96(16),75(16,RA)
         MVO   HEXIO+111(10),90(10,RA)
         NC    HEXIO+20(100),SET   AND OFF ZONE S.
         TR    HEXIO+20(100),HEXTB TRANSLATE TO HEX CHAR.
         MVC   CONIO(26),HDG2      SET UP ZONE
         MVC   HEXIO+14(6),DET2    HEADINGS.
         BAL   R8,PRNT             PRINT ZONES.
         MVC   HEXIO+20(100),0(RA) REFILL HEX AREA.
         NC    HEXIO+20(100),SET   AND OFF ZONES.
         TR    HEXIO+20(100),HEXTB TRANSLATE TO HEX CHAR.
         MVC   CONIO(26),HDG3      SET UP NUMERIC
         MVC   HEXIO+14(6),DET3    HEADINGS.
         BAL   R8,PRNT             PRINT NUMERIC LINE.
         MVC   CONIO(33),BLANKS
         MVC   HEXIO(120),SCALE    MOVE SCALE TO PRINT AREA.
         MVI   CC,X'11'            SET TO DOUBLE SPACE.
         MVI   INPUT,X'00'         TURN OFF IGNORE OVERFLOW SW.
         BAL   R8,PRNT             PRINT SCALE.
         BR    R9                  RETURN.
PRLTH    DC    CL1' '
HEXTB    DC    CL16'0123456789ABCDEF'
SET      DC    X'0F'                   SET IS PROPOGATED
         DS    CL99                    HERE AT INITIALIZE TIME
SCALE    DC    CL20' '
         DC    CL50'1...5...10...15...20...25...30...35...40...45...50'
         DC    CL50'...55...60...65...70...75...80...85...90...95.....'
TRTBL    DC    CL50' '
         DC    CL30'                        Ö.<(+×'
         DC    X'50'
         DC    CL20'         !$*);^-/   '
         DC    CL24'      ,%_>?          :#@'
         DC    X'7D'
         DC    CL25'="                       '
         DC    CL50'                                          ABCDEFGH'
         DC    CL50'I       JKLMNOPQR        STUVWXYZ      0123456789 '
         DC    CL5' '
         EJECT
***********************************************************************
**********         NUMERIC FUNCTIONS FOR CONTROL DATA       ***********
***********************************************************************
         SPACE 1
TSTNUMV  LR    R0,RC               RIGHT JUSTIFY NUMERIC
         BCTR  R0,R0                   CONSOLE REPLY.
         MVC   CONIO+50(5),ZEROS
         MVC   CONIO+55(10),CONIO
         LA    R1,CONIO+54
         AR    R1,RC
RTJUST   CLI   0(R1),C' '
         BNE   MOVBAC
         BCTR  R1,R0
         BCT   R0,RTJUST
MOVBAC   SR    R1,RC
         MVC   CONIO(10),1(R1)
TSTNUM   LR    R1,RB               TEST FOR NUMERIC.
TSTNUM1  TM    0(R1),X'F0'
         BNO   NUMBAD
         LA    R1,1(R1)
         BCT   RC,TSTNUM1
         B     4(R8)
NUMBAD   CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    BADCTL
         MVI   P,0
         B     0(R8)
BINCVT   PACK  HOLD,HOLD           CONVERT HOLD TO BINARY
         CVB   R1,HOLD             RESULTS IN REG 1.
         MVI   HOLD,0              CLEAR HOLD TO X'00'
         MVC   HOLD+1(7),HOLD
         BR    R8
DECCVT   CVD   R7,HOLD             CONVERT BINARY NUMBER
         UNPK  HOLDSEQ,HOLD        IN REG 7
         MVZ   HOLDSEQ+7(1),ON     TO DECIMAL.
         MVI   HOLD,0              CLEAR HOLD TO X'00'
         MVC   HOLD+1(7),HOLD
         CLI   ESW,X'00'           DO WE SUPPRESS LEADING ZEROS ?
         MVI   ESW,X'00'
         BNE   0(R8)               NO, RETURN.
         LA    R1,HOLDSEQ
         LA    R0,7                REPLACE WITH
DECLOP   CLI   0(R1),C'0'          BLANKS.
         BNE   0(R8)
         MVI   0(R1),C' '
         LA    R1,1(R1)
         BCT   R0,DECLOP
         BR    R8
         EJECT
***********************************************************************
**********         DEBLOCK  RECORDS FOR PRINTING OR DUMP    ***********
***********************************************************************
         SPACE 1
DEBLOK   LA    R4,100(R0)          REC LNTH TO BE PRINTED IS 100
         SR    R7,R7
DB1      MVI   CC,X'09'
         CR    R3,R4               IS RECORD LNTH GREATER THAN 100 ?
         BNH   LSBLK               NO, SET UP FOR LAST BLOCK.
         SR    R3,R4
DB2      CLI   DPSW,X'FF'          DUMP IN HEX ?
         BNE   LST                 NO, GO TO LIST
         CLI   LISTSW,X'FF'        DP,DPD,SP,SPD ?
         BE    DISKLST             YES
         STC   R4,PRLTH            YES, SET
         BAL   R8,DECCVT           UP TO
         MVC   SCALE+12(8),HOLDSEQ
         BAL   R9,HEXDP            HEX PRINT 100 POS.
         MVC   HDG1(78),BLANKS
         AR    RA,R4               BUMP I/O POINTER BY 100.
         LA    R7,10(R7)
         B     DB1
DISKLST  MVC   HDG1(8),HDG3        REC NUMBER
         MVC   HDG2,BLANKS         OMIT 'HEAD' TITLE
LST      LR    R1,R4
         LA    R1,32(R1)           BUMP PRINT LENGTH
         STC   R1,PL               BY 32 POS ( FOR HDGS)
         MVC   CONIO(32),HDG1      MOVE HDG1 TO PRINT AREA
         MVC   CONIO+32(100),0(RA)
         AR    RA,R4               BUMP I/O AREA POINTER BY 100.
         BAL   R8,EDIT             EDIT AND PRINT.
         MVC   HDG1(78),BLANKS
         B     DB1
LSBLK    LTR   R3,R3               ARE WE DONE ?
         BZ    0(RB)               YES, RETURN.
         LR    R4,R3               NO, SET PRINT LNGTH TO REMAINING
         SR    R3,R3               RECORD LENGTH.
         B     DB2
         EJECT
***********************************************************************
**********         EDIT AND CONVERT DISK ADDRESS TO BINARY   **********
***********************************************************************
         SPACE 1
EDTADR   LA    RC,5(R0)
         BAL   R8,TSTNUM           IS ADDRESS NUMERIC ?
         B     EDTBAD              NO, BRANCH.
         PACK  HOLD,0(3,RB)        NO, CONVERT CYLINDER NUMBERS
         CVB   R0,HOLD             TO BINARY.
         MVI   HOLD,0              CLEAR HOLD TO X'00'
         MVC   HOLD+1(7),HOLD
         PACK  HOLD,3(2,RB)        YES, CONVERT
         CVB   R1,HOLD             HEAD NUMBER TO BINARY.
         MVI   HOLD,0              CLEAR HOLD TO X'00'
         MVC   HOLD+1(7),HOLD
         CH    R1,UPPERH               HEAD ADDRESS VALID ?
         BH    EDTBAD                  NO, BAD ADDRESS
RELTRKSW BC    0,VALCCC                FALL THRU FOR REL. TRK
         L     R8,DKDEBADR             GET DEB ADRR
         AH    R0,6(R8)                ADD EXTENT BEGIN TO REL CCC
         AH    R1,8(R8)                ADD EXTENT BEGIN TO REL HH
         CH    R1,UPPERH               REL HH G.T. HEAD MAX ?
         BNH   VALCCC                  NO, ASSUME CYL BOUNDARY
         SH    R1,UPPERH               YES, DECREMENT HH BY HEAD MAX
         BCTR  R1,0                    DECR HH BY 1 - HD 0 SIGNIFICANT
         AH    R0,=H'1'                BUMP CCC BY 1
VALCCC   EQU   *
         CH    R0,UPPERC               CYL ADDR VALID ?
         BH    EDTBAD                  NO
         B     4(R9)               RETURN, CYL IN R0 , HEAD IN R1
EDTBAD   CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    BADCTL
         MVI   P,0
         B     0(R9)
BEGHD    DS    CL1
ENDHD    DS    CL1
         SPACE 2
***********************************************************************
*******************   TAPE CHARACTERISTICS ( TRACK AND       **********
*******************   DENSITY ) ARE PLACED IN THE HEADING    **********
*******************   BY THIS ROUTINE                        **********
***********************************************************************
         SPACE 1
TAPECHAR TM    0(R8),X'C0'         IS IT 9 TRACK ?
         BNO   MOVSEV8             NO, GO CHECK 7 TRACK.
         MVI   HH2+31,C'9'         SET HDG TO 9 TRACK
         MVC   MOVSEVCC+1(1),INTPCCW   SAVE CCW COMMAND
         MVI   INTPCCW,X'02'           READ WILL SET DRIVE MODE
         BAL   R8,INTP                 ISSUE READ
         MVI   INTPCCW,X'27'           BSR TO ORIG. POSIT.
         BAL   R8,INTP                 ISSUE BSR
         MVI   INTPCCW,X'04'           SET CCW FOR SENSE CMD
         BAL   R8,INTP                 ISSUE SENSE
MOVSEVCC MVI   INTPCCW,0               RESTORE CCW
         TM    DTPIO+5,X'40'       NEW SUBSYSTEM - 3803-2/3420-4,6,8 ?
         BO    CHK3420             YES
         TM    DTPIO+3,X'04'       IS  DRIVE PHASE ENCODED ?
         BNO   0(R9)               NO, LEAVE AS 800
HDG1600  MVC   HH2+40(2),=C'16'    YES, SET 1600
         BR    R9
CHK3420  TM    DTPIO+6,X'10'       800 OR 6250 ?
         BNO   HDG1600             NO, THEREFORE ITS 1600
CHK6250  TM    DTPIO+6,X'08'       MODEL 4,6, OR 8 ?
         BNO   0(R9)               NO, LEAVE AS 800
         MVC   HH2+40(3),=C'625'   YES, SET TO 6250
         BR    R9                  HEADING INFOR COMPLETE, RETURN
MOVSEV8  MVI   HH2+31,C'7'         SET HDG FOR 7 TRACK
         TM    0(R8),X'80'         IS IT 800 ?
         BNO   MOVSEV5             NO, BRANCH?
         MVI   HH2+41,C'8'         SET DENSITY TO 800
         BR    R9                  HEADING INFOR COMPLETE, RETURN
MOVSEV5  TM    0(R8),X'40'         IS IT 556 ?
         BNO   MOVSEV2             NO, BRANCH
         MVC   HH2+41(3),=C'556'   SET TO 556
         BR    R9                  HEADING INFOR COMPLETE, RETURN
MOVSEV2  MVI   HH2+41,C'2'         SET TO 200
         BR    R9                  HEADING INFOR COMPLETE, RETURN
         EJECT
***********************************************************************
******************   TAPE TRACK IS CHECKED BY THIS ROUTINE   **********
******************   TO INSURE THAT OPERATOR GAVE MODE       **********
******************   COMPATIBLE WITH DEVICE ALLOCATED        **********
***********************************************************************
         SPACE 1
TAPETRK  EQU   *
         TM    1(R3),X'80'         IS IT 7 TRK TAPE ?
         BZ    CHKTP1              NO, BRANCH.
         CLI   TRK7OR9,X'07'       YES, WAS 7 TRK MODE SET GIVEN ?
         BNE   BADTPTK9            NO, ISSUE 'UNIT IS NOT 9 TRK' MSG.
         B     4(R9)               YES, RETURN    + 4
CHKTP1   CLI   TRK7OR9,X'09'       WAS 9 TRK MODE SET GIVEN ?
         BNE   BADTPTK7            NO, ISSUE 'UNIT IS NOT 7 TRK' MSG
         B     4(R9)               YES, RETURN    + 4
BADTPTK7 MVC   CONIO(17),=C'UNIT IS NOT 9 TRK'
         MVI   CONIO+12,C'7'
         B     BADTRK79
BADTPTK9 MVC   CONIO(17),=C'UNIT IS NOT 9 TRK'
BADTRK79 MVI   WCNCCW+7,X'11'
         LR    R8,R9               PRIME R8 FOR RETURN + 0
         MVI   P,0                 TURN OFF STRING PARAM.
         B     WCON2               GO TO WTO TRN, THEN REVERT TO TUTOR.
         SPACE 2
***********************************************************************
**********          DISK CARACTERISTICS ARE DETER-          ***********
**********          MINED BY THIS ROUTINE AND               ***********
**********          PLACED IN THE HEADING FOR SYPRINT       ***********
***********************************************************************
         SPACE 1
DISKCHAR EQU   *
         MVI   DEVTYP,C'0'         RESET DEVICE TYPE.
         LA    RB,DISKTABL         LOAD DISK SUPPORT TABLE
DSKCHK   CLI   0(RB),X'FF'         END OF TABLE ?
         BE    BADUCBDK            YES, BR INTO BADUCB CODE
         CLC   DSKTYPE(1),4(RB)    UCB    DEVICE TYPE CODE MATCH TABLE?
         BE    DSKHIT              YES,
         LA    RB,8(RB)            NO, BUMP TO NEXT ENTRY
         B     DSKCHK                ENTRY & CHECK AGAIN
DSKHIT   MVI   DEVTYP,C'D'         SET DEVICE TYPE CODE FOR DISK
         MVC   GATRK+10(4),0(RB)   SET DEVICE NAME IN
         MVC   BPTRK+10(4),0(RB)     HEADINGS
         MVC   ENDHD,7(RB)         HEX VALUE - UPPER HEAD
         MVC   UPPERC(2),5(RB)         END CYL. ADDRESS - HEX
         MVC   UPPERH+1(1),7(RB)       END HD . ADDRESS - HEX
         BR    R9
         EJECT
***********************************************************************
*********           EVALUATE DDNAMES, CHECK FOR OPEN STATUS     *******
*********           PERFORM OPEN IF NECESSARY                   *******
***********************************************************************
         SPACE 1
OPENCRD  EQU   *
         OPEN  (CRDDCB,(INPUT))
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         MVI   CDINOPSW,X'FF'      SET OPEN SW FOR CARD IN
         BR    R9
CKTPINDD CLC   INDDSAV,TPINDD      TAPEIN  ?
         BNE   CKINFMOT                NO, CHECK INPUT FROM TAPEOUT DD
         CLI   TPINOPSW,X'FF'          OPEN ?
         BE    TPINDONE
OPINFROT EQU   *
         OPEN  (INTPDCB,(INPUT,DISP))
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         L     R1,INTPDCB+44           PTR TO DEB AFTER OPEN
         L     R2,32(R1)               PTR TO UCB AFTER OPEN
         ST    R2,ITMODE           STORE MODE SET FROM DEB, HI ORD BYTE
         MVC   ITUCB(3),13(R2)         GET UCB UNITNAME
         DEVTYPE TPINDD,ITDEVTYP    OBTAIN UCB INFORMATION
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         CLI   ITCLASS,X'80'       TAPE ?
         BE    TPINOK              YES
         MVC   CONIO(23),DVTYPMSG  NO
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    TELPGMR             YES
         B     TELOPER             NO
TPINOK   EQU   *
         MVI   TPINOPSW,X'FF'
TPINDONE EQU   *
         BR    R8                      RETURN
CKINFMOT CLC   INDDSAV,TPOTDD          INPUT OPER FROM TAPEOUT DD ?
         BNE   CKDKINDD            NO, GO TO DSKIN
         MVI   INFMOTSW,X'FF'          SET IN FROM OUT SW
         CLI   TPOTOPSW,X'FF'          IN TAPEOUT DD OPEN ?
         BE    TPINDONE                YES
         B     OPOTFRIN                NO
CKDKINDD CLC   INDDSAV,DSKINDD     DISKIN ?
         BNE   BADCTL              NO, INVALID CTL CRD
         CLI   DKINOPSW,X'FF'      OPEN ?
         BE    DKINDONE            YES
         OPEN  (DSKDCB,(INOUT))
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
CKDSKDEV L     R1,DSKDCB+44            PTR TO DEB AFTER OPEN
         L     R2,32(R1)               PTR TO UCB AFTER OPEN
         MVC   EXTNTCNT(1),16(R1)  SAVE EXTENT CNT FIELD OF DEB
         LA    R1,32(R1)           BUMP R1 TO DASD SECTION OF DEB
         ST    R1,DKDEBADR             SAVE DEB XTNT PTR
         MVC   DKUCB(3),13(R2)         GET UCB UNITNAME
         DEVTYPE DSKINDD,DKDEVTYP    OBTAIN UCB INFORMATION
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         CLI   DSKCLASS,X'20'      IS DISKIN DASD DEVICE ?
         BE    DASD                YES
         MVC   CONIO(23),DVTYPMSG  NO, MOVE IN MSG
         MVC   CONIO(8),DSKINDD    MOVE IN DDNAME
         MVC   CONIO+12(4),=C'DASD'
TELUSER  CLI   CNTRLSW,X'FF'       ARE WE IN CONTROL CARD MODE ?
         BNE   TELOPER             NO
TELPGMR  MVI   PL,X'17'
         MVI   CC,X'19'            SET CTL CAR
         BAL   R8,PRNT             PRINT MSG TO PGMR
         B     EOJ                 QUIT
TELOPER  EQU   *
         MVI   WCNCCW+7,X'17'
         BAL   R8,WCON2            TELL OPERATOR
         B     CONSOLE             GET ANOTHER FUNC IF DESIRED
DASD     EQU   *
         MVI   DKINOPSW,X'FF'
DKINDONE BR    R8
CKTPOTDD CLC   OUTDDSAV,TPOTDD     TPAEOUT ?
         BNE   CKTPOTIN            NO,  CHK FOR TAPEIN
         CLI   TPOTOPSW,X'FF'      OPEN ?
         BE    TPOTDONE            YES
OPOTFRIN EQU   *
         OPEN  (OTTPDCB,(OUTPUT,DISP)) THIS WILL REQ RING ON TAPEOUT
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         L     R1,OTTPDCB+44           PTR TO DEB AFTER OPEN
         L     R2,32(R1)               PTR TO UCB AFTER OPEN
         ST    R2,OTMODE           STORE MODE SET FROM DEB, HI ORD BYTE
         MVC   OTUCB(3),13(R2)         GET UCB UNITNAME
         DEVTYPE TPOTDD,OTDEVTYP    OBTAIN UCB INFORMATION
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         CLI   OTCLASS,X'80'       TAPE ?
         BE    TPOTOK              YES
         MVC   CONIO(23),DVTYPMSG  NO
         MVC   CONIO(8),TPOTDD     MOVE IN DDNAME
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    TELPGMR             YES
         B     TELOPER             NO
TPOTOK   EQU   *
         MVI   TPOTOPSW,X'FF'
TPOTDONE BR    R8
CKTPOTIN CLC   OUTDDSAV,TPINDD     OUTPUT OPER ON TAPEIN ?
         BNE   BADCTL              NO
         MVI   OTONINSW,X'FF'      YES, SET SW
         CLI   TPINOPSW,X'FF'      OPEN ALREADY ?
         BE    TPOTINDN            YES
         B     OPINFROT            NO OPEN INTP FOR OUTPUT OPER
TPOTINDN EQU   *
         BR    R8
         EJECT
***********************************************************************
**********            DISK CHARACTERISTICS TABLE            ***********
**********     THE FOLLOWING TABLE PROVIDES THE CAPABILITY  ***********
**********     TO SUPPORT EXISTING & NEW DASD DEVICES.      ***********
**********     TABLE ELEMENTS ARE USED TO ESTABLISH         ***********
**********     UPPER SEEK LIMIT EDITING, AND FOR PRNT HDGS. ***********
***********************************************************************
         SPACE 1
DISKTABL DS    0CL1
*DSKDEV1  DS    0CL8                2311 ELEMENTS
*        DC    CL4'2311'                 DEVICE NAME
*        DC    XL1'01'                   UCB DEVICE TYPE
*        DC    AL2(202)                  MAXIMUM CYLINDER NUMBER
*        DC    AL1(09)                   MAXIMUM HEAD NUMBER
*DSKDEV2  DS    0CL8                2314 ELEMENTS
*        DC    CL4'2314'                 DEVICE NAME
*        DC    XL1'08'                   UCB DEVICE TYPE
*        DC    AL2(202)                  MAXIMUM CYLINDER NUMBER
*        DC    AL1(19)                   MAXIMUM HEAD NUMBER
DSKDEV3  DS    0CL8                3330 ELEMENTS
         DC    CL4'3330'                 DEVICE NAME
         DC    XL1'09'                   UCB DEVICE TYPE
         DC    AL2(410)                  MAXIMUM CYLINDER NUMBER
         DC    AL1(18)                   MAXIMUM HEAD NUMBER
DSKDEV4  DS    0CL8                3330 - MODEL 11 ELEMENTS
         DC    CL4'3330'                 DEVICE NAME
         DC    XL1'0D'                   UCB DEVICE TYPE
         DC    AL2(814)                  MAXIMUM CYLINDER NUMBER
         DC    AL1(18)                   MAXIMUM HEAD NUMBER
DSKDEV5  DS    0CL8                3340 - MODEL 35 & 70 ELEMENTS
         DC    CL4'3340'                 DEVICE NAME
         DC    XL1'0A'                   UCB DEVICE TYPE
         DC    AL2(697)                  MAXIMUM CYLINDER NUMBER
         DC    AL1(11)                   MAXIMUM HEAD NUMBER
DSKDEV7  DS    0CL8                2305 - MODEL 1  ELEMENTS
         DC    CL4'2305'                 DEVICE NAME
         DC    XL1'06'                   UCB DEVICE TYPE
         DC    AL2(048)                  MAXIMUM CYLINDER NUMBER
         DC    AL1(07)                   MAXIMUM HEAD NUMBER
DSKDEV8  DS    0CL8                2305 - MODEL 2  ELEMENTS
         DC    CL4'2305'                 DEVICE NAME
         DC    XL1'07'                   UCB DEVICE TYPE
         DC    AL2(96)                   MAXIMUM CYLINDER NUMBER
         DC    AL1(07)                   MAXIMUM HEAD NUMBER
DSKDEV9  DS    0CL8                3350 - ELEMENTS
         DC    CL4'3350'                 DEVICE NAME
         DC    XL1'0B'                   UCB DEVICE TYPE
         DC    AL2(559)                  MAXIMUM CYLINDER NUMBER
         DC    AL1(29)                   MAXIMUM HEAD NUMBER
DSKDEVA  DS    0CL8                3380 - ELEMENTS
         DC    CL4'3380'                 DEVICE NAME
         DC    XL1'0E'                   UCB DEVICE TYPE
         DC    AL2(886)                  MAXIMUM CYLINDER NUMBER
         DC    AL1(14)                   MAXIMUM HEAD NUMBER
         DC    X'FF'               END OF TABLE
         EJECT
***********************************************************************
**********         OPERATOR I/O DEVICE ALLOCATION ROUTINE   ***********
***********************************************************************
         SPACE 1
ITMSG0   MVI   P,0
ITMSG    EQU   *
         MVC   CONIO(18),SYSITMSG  GET IN-TAPE ADDRESS.
         MVI   WCNCCW+7,X'12'      AND MODE SET FROM CONSOLE
         MVI   RCNCCW+7,X'05'
         BAL   R8,WRTCON
         BAL   R8,CHKMOD           VALIDATE MODE SET GIVEN.
         B     ITMSG0              RETURN HERE IF INVALID
         MVC   TRK7OR9(1),CONIO+5      SAVE 7 OR 9 TRK INDICATOR
         MVC   MSICCW(1),0(R9)     PLACE MODE SET IN MODE SET CCW
         MVC   HH2(48),DEVH
ITOPENCK CLI   TPINOPSW,X'FF'          TAPEIN  OPEN ?
         BE    ITUCBEQ                 YES
         CLI   TIALLOSW,X'FF'      TAPEIN  ALLOCATED ?
         BNE   ITOTOPCK            NO, CHECK TAPEOUT
         CLC   CONIO(3),TIALLUCB   OPER. UCB = UCB ALLOCATED TO TAPEIN?
         BNE   ITOTOPCK            NO, CHECK TAPEOUT
         MVC   INDDSAV,TPINDD      PREPARE FOR OPEN
         BAL   R8,CKTPINDD         GO FORCE TAPEIN OPEN
         B     ITOPENCK            CHECK TAPEIN AGAIN
ITOTOPCK CLI   TPOTOPSW,X'FF'          TAPEOUT OPEN ?
         BE    ITOTUCB             YES, OPER UCB=OTUCB? NO, BAD UCB
         CLI   TOALLOSW,X'FF'      TAPEOUT ALLOCATED ?
         BNE   BADUCB              NO, ISSUE INVALID CUU MSG
         CLC   CONIO(3),TOALLUCB   OPER. UCB = UCB ALLOC. TO TAPEOUT ?
         BNE   BADUCB              NO, ISSUE INVALID CUU MSG
         MVC   OUTDDSAV,TPOTDD     PREPARE FOR OPEN
         BAL   R8,CKTPOTDD         GO FORCE TAPEOUT OPEN
         B     ITOTOPCK            CHECK TAPEOUT AGAIN
ITUCBEQ  CLC   CONIO(3),ITUCB          DOES OPER UCB = ITUCB ?
         LA    R3,ITDEVTYP         PROVIDE PTR TO ITTRK INDICATOR
         BE    ITOK                    YES
         B     ITOTOPCK            NO, CHECK TAPEOUT UCB
ITOTUCB  CLC   CONIO(3),OTUCB          YES, DOES OPER UCB = OTUCB ?
         BNE   BADUCB              NO
ITOTOK   MVI   INFMOTSW,X'FF'          USE TAPEOUT FOR INPUT OPER
         MVC   OTMODE(1),MSICCW        STORE OPER MODE IN OTMODE
         LA    R3,OTDEVTYP         PROVIDE PTR TO OTTRK INDICATOR
ITOK     EQU   *
         BAL   R9,TAPETRK          GO EDIT MODE GIVEN BY OPERATOR
         B     ITMSG0              RETURN HERE IF INVALID
         MVC   ITMODE(1),MSICCW    PUT OPER. MODE IN ITMODE AFTER OPEN
         B     0(R7)
BADUCB   MVC   CONIO(13),BADCUUMM              NO, GIVE  BAD UCB MSG
         MVI   WCNCCW+7,X'0D'
         B     WCON2
OTMSG0   MVI   P,0
OTMSG    EQU   *
         MVC   CONIO(19),SYSOTMSG  GET OUT-TAPE ADDRESS.
         MVI   WCNCCW+7,X'13'      AND MODE SET FROM SYSLOG.
         MVI   RCNCCW+7,X'05'
         BAL   R8,WRTCON
         BAL   R8,CHKMOD           VALIDATE MODE SET.
         B     OTMSG0              RETURN HERE IF INVALID
         MVC   TRK7OR9(1),CONIO+5      SAVE 7 OR 9 TRK INDICATOR
         MVC   MSOCCW(1),0(R9)     PLACE MODE SET IN MODE SET CCW
OTOPENCK CLI   TPOTOPSW,X'FF'          TAPEOUT OPEN ?
         BE    OTUCBEQ                 YES
         CLI   TOALLOSW,X'FF'      TAPEOUT ALLOCATED ?
         BNE   OTITOPCK            NO, CHECK TAPEIN
         CLC   CONIO(3),TOALLUCB   OPER. UCB = UCB ALLOC. TO TAPEOUT ?
         BNE   OTITOPCK            NO, CHECK TAPEIN
         MVC   OUTDDSAV,TPOTDD     PREPARE FOR OPEN
         BAL   R8,CKTPOTDD         GO FORCE TAPEOUT OPEN
         B     OTOPENCK            CHECK TAPEOUT AGAIN
OTITOPCK CLI   TPINOPSW,X'FF'          TAPEIN  OPEN ?
         BE    OTITUCB             YES, OPER UCB=ITUCB? NO, OPN TAPEOUT
         CLI   TIALLOSW,X'FF'      TAPEIN  ALLOCATED ?
         BNE   BADUCB              NO, ISSUE INVALID CUU MSG
         CLC   CONIO(3),TIALLUCB   OPER. UCB = UCB ALLOCATED TO TAPEIN?
         BNE   BADUCB              NO, ISSUE INVALID CUU MSG
         MVC   INDDSAV,TPINDD      PREPARE FOR OPEN
         BAL   R8,CKTPINDD         GO FORCE TAPEIN OPEN
         B     OTITOPCK            CHECK TAPEIN AGAIN
OTUCBEQ  CLC   CONIO(3),OTUCB          DOES OPER UCB = OTUCB ?
         LA    R3,OTDEVTYP         PROVIDE PTR TO OTTRK INDICATOR
         BE    OTOK                    YES
         B     OTITOPCK            NO, CHECK TAPEIN UCB
OTITUCB  EQU   *
         CLC   CONIO(3),ITUCB          YES, DOES OPER UCB = ITUCB ?
         BNE   BADUCB
OTITOK   MVI   OTONINSW,X'FF'          USE TAPEIN FOR OUTPUT OPERATION
         LA    R3,ITDEVTYP         PROVIDE PTR TO ITTRK INDICATOR
OTOK     EQU   *
         BAL   R9,TAPETRK          GO EDIT MODE GIVEN BY OPERATOR
         B     OTMSG0              RETURN HERE IF INVALID
         MVC   OTMODE(1),MSOCCW    PUT OPER. MODE IN OTMODE AFTER OPEN
         B     0(R7)
IDMSG0   MVI   P,0
IDMSG    MVC   CONIO(10),SYSDSK    GET DISK ADDRESS AND VALIDATE UCB
         MVI   WCNCCW+7,X'0A'
         MVI   RCNCCW+7,X'03'
         BAL   R8,WRTCON
IDOPENCK CLI   DKINOPSW,X'FF'      IS DISKIN OPEN ?
         BE    IDUCBEQ             YES, COMPARE TO OPERATOR CUU
         MVC   INDDSAV,DSKINDD     NO, PREPARE FOR OPEN
         BAL   R8,CKDKINDD         GO OPEN DISKIN
         B     IDOPENCK            CHK AGAIN, IF BAD OPEN, WON'T RETURN
IDUCBEQ  CLC   CONIO(3),DKUCB      OPER CUU = UCB ?
         BE    IDOK                YES
BADUCBDK MVC   CONIO(11),BADCUUMM  NO, PREPARE BAD CUU MSG
         MVI   WCNCCW+7,X'0B'
         BAL   R8,WCON2            ISSUE MSG, RETURN
         B     IDMSG0              GIVE OPER ANOTHER CHANCE
IDOK     EQU   *
         BAL   R9,DISKCHAR         GO GET DISK DEVICE CHARACTERISTICS
         B     0(R7)               RETURN TO FUNCTION
         EJECT
***********************************************************************
**********     DATA SET EXTENT CONSOLE DISPLAY ROUTINE.     ***********
***********************************************************************
         SPACE 1
DISKDSE  EQU   *
         CLI   CNTRLSW,X'FF'       VALID FOR CONSOLE ONLY
         BE    BADCTL
         BAL   R7,IDMSG            GET CUU
         MVC   CONIO(34),ENTCCCHH  ASK FOR ABSOL DISK ADDRESSES
         MVC   CONIO+34(19),DASHBEG    ' - BEGIN=CCCHH,END='
         L     R9,DKDEBADR         GET PTR TO DEB
         LA    R2,2(R0)            SET LOOP CTL TO 2
         LA    R1,CONIO+43         ADDRESS BEGIN CCCHH AREA OF MSG
DEBLOOP  EQU   *
         SR    R7,R7               CLEAR
         IC    R7,9(R9)            GET HEAD
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   3(2,R1),HOLDSEQ+6   MOVE HH TO MSG
         LH    R7,6(R9)            GET CYLINDER
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   0(3,R1),HOLDSEQ+5   MOVE CCC TO MSG
         LA    R9,4(R9)            BUMP R9 TO END CCCHH
         LA    R1,CONIO+53         ADDRESS END   CCCHH AREA OF MSG
         BCT   R2,DEBLOOP          FORMAT END 2ND TIME THRU
DISKDSE1 MVI   WCNCCW+7,X'3A'
         BAL   R8,WCON2            'ENTER CCCHH WITHIN DATA SET EX...
         B     CONSOLE
         EJECT
***********************************************************************
**********               DISK  DEBLOCK  ROUTINE             ***********
***********************************************************************
         SPACE 1
DSKDEBL  MVI   RWCCW,X'1E'         SET TO READ COUNT, KEY, DATA.
         MVC   RWCCW+6(2),DSL+2    SET MAX. RECORD LNTH.
DSKDEBL1 EQU   *                   DECREMENT RECORD #(BR HERE FROM EOF)
         IC    R7,LOWLIM+6             TO SEARCH FOR
         BCTR  R7,R0                   R-1 RECORD.
         STC   R7,LOWLIM+6
         MVI   SHCCW,X'31'
         BAL   R8,DISK             GET RECORD.
         LA    R7,1(R7)            INCR. R BACK UP TO PREVIOUS VALUE
         STC   R7,LOWLIM+6         ST R BACK IN IOB
         TM    DSKS1,X'08'         NO RECORD FOUND ?
         BO    0(R4)               YES, RETURN.
         CLC   DTPIO(5),LOWLIM+2   WAS RECORD R0 ?
         BL    0(R4)               YES, TREAT AS NO REC.
         CLI   EOF,X'10'           IS FUNCTION WRITE EOF ?
         BE    DRCDEBL             YES, BRANCH.
         CLC   DSKCSW+5(2),DSL     IS RESIDUAL CT ZERO ??
         BE    WRGLNTC             YES, I/O AREA TOO SMALL.
DRCDEBL  MVI   DPSW,X'FF'          SET HEX DUMP SW ON.
         LA    RA,DTPIO+8
         STM   R3,R4,SAV
         SR    R7,R7
         IC    R7,DTPIO+4          GET RECORD # FROM COUNT FIELD.
         BAL   R8,DECCVT           CONVERT TO DECIMAL
         MVC   HDG1(8),DCYL
         MVC   HDG2(8),DHED        MOVE REC # IN HEADING.
         MVC   HDG3(8),DREC
         MVC   HDG3+5(3),HOLDSEQ+5
         IC    R7,DTPIO+5          GET KEY LNTH FROM COUNT.
         LR    R2,R7               KEY LNTH RETURNED IN R2.
         LTR   R7,R7               IS KEY LNTH ZERO ?
         BZ    NOKEY               YES, BRANCH.
         BAL   R8,DECCVT           NO, PUT KEY
         MVC   KEYH+5(5),HOLDSEQ+3  LNTH IN HEADING.
         MVC   HDG1+13(10),KEYH
         LR    R3,R7
         BAL   RB,DEBLOK           GO PRINT KEY RECORD.
         SR    R7,R7
NOKEY    IC    R7,DTPIO+6          GET DATA LNTH FROM COUNT FIELD.
         SLL   R7,8(R0)
         IC    R7,DTPIO+7
         LR    R5,R7               RETURN DATA LNTH IN R5.
         LTR   R7,R7               IS DATA LNTH ZERO ?
         BZ    ENDFL               YES, GO PRINT EOF RECORD.
         BAL   R8,DECCVT           NO, PUT LNTH IN HEADING.
         MVC   DATH+5(5),HOLDSEQ+3
         MVC   HDG1+13(10),DATH
         CLI   DBLSW,X'00'         IS DATA TO BE DEBLOCKED ?
         BE    DEBLK2              NO, BRANCH.
DEBLK1   C     R7,SAVRCSZ          IS DATA LNTH GREATER THAN LOG. LNTH?
         BNH   DEBLK2              NO, BRANCH.
         L     R3,SAVRCSZ          YES, GO
         SR    R7,R3               DEBLOCK FIRST LOGICAL PORTION.
         ST    R7,SAVREGS+4
         BAL   RB,DEBLOK
         L     R7,SAVREGS+4        DECREMENT DATA LNTH AND LOOP
         B     DEBLK1              UNTIL FINISHED.
DEBLK2   LR    R3,R7               LOAD LNTH IN R3.
         BAL   RB,DEBLOK           GO PRINT DATA RECORD.
         MVI   CC,X'13'
DEBLK3   BAL   R8,PRNT
         LM    R3,R4,SAV
         B     4(R4)               RETURN.
ENDFL    MVC   CONIO(26),HDG1      *****    EOF ROUTINE    *****
         MVC   DATH+5(4),BLANKS
         MVI   DATH+8,C'0'         DATA LNTH = 0.
         MVC   CONIO+13(9),DATH
         MVC   CONIO+22(20),BLANKS
         MVC   CONIO+32(19),ALTERMSG
         MVC   CONIO+32(7),=C'E-O-F  '
         MVI   CC,X'09'
         MVI   PL,X'33'
         MVI   INPUT,X'FF'
         BAL   R8,PRNT
         MVC   CONIO+13(50),BLANKS
         MVC   CONIO(26),HDG2
         BAL   R8,PRNT
         MVC   CONIO(26),HDG3
         MVI   CC,X'19'
         MVI   INPUT,X'00'
         B     DEBLK3
         EJECT
***********************************************************************
**********               ALTER RECORD FROM CONSOLE          ***********
***********************************************************************
         SPACE 1
CHNG     MVI   WCNCCW+7,X'22'      REG 9 = BEGIN OF RECORD AT ENTER
         MVI   RCNCCW+7,X'02'      REG 10 = END OF RECORD AT ENTER
         MVC   DECKNAME(5),ALTRMSG
         MVC   CONIO(34),PRTIO     # TYPE BYTES TO BE CHANGED
         BAL   R8,WRTCONP          GET REPLY
         LA    RC,2(R0)
         LA    RB,CONIO
         BAL   R8,TSTNUMV          IS REPLY NUMERIC ?
         B     CHNG
         MVC   HOLD+6(2),CONIO
         BAL   R8,BINCVT           CONVERT TO BINARY, RESULTS IN R1.
         MVC   ALTCT+6(2),CONIO    STORE DECIMAL VALUE.
         LA    R8,35(R0)
         CR    R1,R8               AMOUNT OF CHANGE = 1-35 ?
         BH    CHNG                NO, RESTART.
         LTR   R1,R1               REPLY 0 ?
         BZ    CHNG                YES, RESTART.
         LR    R3,R1               NO,  SAVE AMOUNT IN R3.
GETLTH   MVC   CONIO(29),SDPMSG    GET STARTING
         MVI   WCNCCW+7,X'1D'      POSITION OF CHANGE.
         MVI   RCNCCW+7,X'05'      ALLOW 5 DIGIT MAXIMUM
         BAL   R8,WRTCONP
         LA    RB,CONIO
         LA    RC,5(R0)            ALLOW 5 DIGIT MAXIMUM
         BAL   R8,TSTNUMV          IS REPLY NUMERIC ?
         B     GETLTH
         MVC   HOLD+3(5),CONIO     ALLOW 5 DIGIT MAXIMUM
         BAL   R8,BINCVT           CONVERT TO BINARY.
         LTR   R1,R1               REPLY GREATER THAN 0000 ?
         BZ    GETLTH              NO, RESTART.
         LA    R8,1(R0)
         SR    R1,R8
         LR    R0,R1
         LR    R8,R9               RECORD LNTH IN R8.
         AR    R8,R1               ADD STARTING ALTER POSITION.
         AR    R8,R3               ADD # BYTES OF CHANGE.
         CR    R8,RA               IS TOTAL GREATER THAN REC LNTH ?
         BNH   LNTHOK              NO, LENGTH OK.
         MVC   CONIO(28),LNGTHMSG      'LENGTH EXCEEDS END OF RECORD'
         MVI   WCNCCW+7,X'1C'
         BAL   R8,WCON2
         B     CHNG
LNTHOK   MVC   CONIO(26),ALTRMSG   LOG MSSG,
         MVC   CONIO(5),DECKNAME   ENTER OR ALTER
         MVI   WCNCCW+7,X'1A'
         MVI   RCNCCW+7,X'01'
         BAL   R8,WRTCONP
         LA    RC,CONIO
         CLI   CONIO,C'C'          IS ALTER IN CHAR. MODE ?
         BE    CHARIN              YES, BRANCH.
         CLI   CONIO,C'H'          IS ALTER IN HEX MODE ?
         BNE   LNTHOK              NO, INVALID REPLY, RESTART.
HEXIN    MVC   CONIO(31),ALTCT     MOVE SCALE TO SYSLOG.
         MVI   WCNCCW+7,X'1F'
         MVI   CONIO+16,C'2'
         LR    R7,R3
         AR    R7,R3
         BAL   R8,WCON2
         MVC   CONIO(75),SCALE+20
         STC   R7,WCNCCW+7
         STC   R7,RCNCCW+7
         BAL   R8,WRTCONP
HLOP     TM    0(RC),C'0'          HEX CHAR = 0-9 ?
         BO    HEND                YES, BRANCH.
         TM    0(RC),X'C0'         HEX CHAR = A-F ?
         BNO   BDHEX               NO, BAD HEX CHAR.
         NI    0(RC),X'0F'
         CLI   0(RC),X'06'
         BH    BDHEX
         TR    0(1,RC),ADTBL-1     SHIFT ZONE TO FORM 0X.
HEND     LA    RC,1(RC)
         BCT   R7,HLOP
         PACK  CONIO+80(8),CONIO(15)   PACK DATA
         PACK  CONIO+87(8),CONIO+14(15)    2 HEX POS. PER BYTE.
         PACK  CONIO+94(8),CONIO+28(15)
         PACK  CONIO+101(8),CONIO+42(15)
         PACK  CONIO+108(8),CONIO+56(15)
         LA    RC,CONIO+80
         B     MOVLOPS
CHARIN   MVC   CONIO(31),ALTCT     GET ALTER
         MVI   WCNCCW+7,X'1F'      DATA IN
         MVI   CONIO+16,C'1'       CHARACTER
         BAL   R8,WCON2            FORM FROM
         MVC   CONIO(75),SCALE+20  CONSOLE
         STC   R3,WCNCCW+7
         STC   R3,RCNCCW+7
         BAL   R8,WRTCONP
MOVLOPS  LR    R8,R9
         AR    R8,R0
MOVLOP   MVC   0(1,R8),0(RC)
         LA    R8,1(R8)            MOVE NEW DATA
         LA    RC,1(RC)            1 BYTE AT A TIME
         BCT   R3,MOVLOP           TO RECORD AND OVERLAY.
         B     0(R4)               RETURN FOR OTHER FUNCTIONS.
BDHEX    MVC   CONIO(8),INVLDMSG       'INVALID '
         MVC   CONIO+8(8),=C'HEX CHAR'
         MVI   WCNCCW+7,X'10'
         BAL   R8,WCON2
         LA    RC,CONIO
         B     HEXIN
ADTBL    DC    X'0A0B0C0D0E0F'
         EJECT
***********************************************************************
**********               TAPE ERROR CORRECTION              ***********
***********************************************************************
         SPACE 1
TPERR    MVC   CONIO(20),TAPERR    'INPUT TAPE I/O ERROR'
         CLI   INTPERSW,X'02'          DO WE GIVE ERRORS TO OPER ?
         BE    TPERR1                  YES, GO TO OPER ERR RTN
         MVI   PL,X'24'                PRIME PRINT LENTGH
         MVI   CC,X'19'                PRIME CAR CTL
         MVC   CONIO+20(7),COMMABLK    ', BLOCK'
         MVC   CONIO+27(9),=C' BYPASSED'
         BAL   R8,PRNT                 PRINT ERROR MSG TO PGMR
         B     RTTPER                  GO TO BYPASS RTN
TPERR1   MVI   WCNCCW+7,X'14'
         BAL   R8,WCON2            WRITE TAPE ERROR MSG.
TPE      MVC   CONIO(50),TPERRMSG
         MVI   WCNCCW+7,X'32'      WRITE OPERATOR OPTION MSG
         MVI   RCNCCW+7,X'01'
         BAL   R8,WRTCONP
         CLI   CONIO,C'B'          REPLY = BYPASS ?
         BE    RTTPER              YES, GO READ NEXT TAPE RECORD
         CLI   CONIO,C'I'          REPLY = IGNORE ?
         BE    TAPOK               YES, EXIT & PROCESS RECORD AS READ
         CLI   CONIO,C'C'          REPLY = CORRECTION BY OPERATOR ?
         BNE   TPE                 NO, BAD REPLY
         MVI   REPEAT+1,X'F0'      YES,
TPERLP   BAL   R8,DHDG              PRINT HEADING
         BAL   R8,OVFLO
         L     R5,DSL
         S     R5,TPLNTH           LESS RESIDUAL COUNT = LNTH READ
         CLC   INTPCSW+5(2),DSL        RECORD EXCEED BUFFER ?
         BE    WRGLNTC             YES, GO TO EXCEED LNTH MSG
         LR    R7,R5
         BAL   R8,DECCVT           LNTH IN HEADING
         MVC   HDG1(7),TPERRMSG+22   ' ERROR '
         MVC   HDG1+7(6),BADLTH+1      'BLOCK '
         MVC   HDG1+13(10),DATB
         MVC   HDG1+18(5),HOLDSEQ+3
         LR    R3,R5
         LA    RA,DTPIO
         LR    R2,R5
         MVI   DPSW,X'FF'          SET HEX PRINT SWITCH ON
         BAL   RB,DEBLOK           GO PRINT RECORD
REPEAT   BC    15,CHNGIT
REPEAT1  MVC   CONIO(26),CHNGMSG   'CHANGES COMPLETE ?' MSG
         MVI   WCNCCW+7,X'1A'
         MVI   RCNCCW+7,X'01'
         BAL   R8,WRTCONP
         CLI   CONIO,C'Y'          YES,
         BE    TAPOK               RETURN TO ORINAL FUNCTION
         CLI   CONIO,C'N'          NO,
         BNE   REPEAT1             PREPARE TO ACCEPT USER CORRECTION
CHNGIT   MVI   REPEAT+1,X'00'
         MVC   CONIO(26),CHNGMSG   'CHANGE TO LENGTH ?' MSG
         MVC   CONIO+6(10),TOLNGTH     'TO LENGTH'
         MVI   WCNCCW+7,X'1A'
         MVI   RCNCCW+7,X'01'
         BAL   R8,WRTCONP
         CLI   CONIO,C'N'          NO,
         BE    OKLNTH                  LENGTH OK, BRANCH
         CLI   CONIO,C'Y'          YES,
         BNE   CHNGIT
NEWLNTH  MVC   CONIO(16),RECSIZ    'ENTER NEW REC. LNTH' MSG
         MVC   CONIO(7),DESIRED
         MVI   WCNCCW+7,X'10'
         MVI   RCNCCW+7,X'05'
         BAL   R8,WRTCONP
         LA    RC,5(R0)
         LA    RB,CONIO
         BAL   R8,TSTNUMV          REPLY NUMERIC ?
         B     NEWLNTH             NO
         MVC   HOLD+3(5),CONIO     YES,
         BAL   R8,BINCVT           CONVERT TO BINARY
         CR    R1,R5               NEW LNTH SHORTER ?
         BNH   NOBLK               YES, LNTH IN REGISTER 1
         C     R1,DSL              NEW LNTH FIT IN BUFFER ?
         BH    WRGLNTC             NO, GO TO BADLNTH MSG.
         LA    RA,DTPIO
         AR    RA,R5
         LR    R8,R1               PAD OUT NEW EXTENDED LNTH WITH BLANK
         SR    R8,R5
PADLOOP  MVI   0(RA),C' '
         LA    RA,1(RA)
         BCT   R8,PADLOOP
NOBLK    ST    R1,SAVREGS+16       STORE NEW LNTH
         L     R5,DSL
         SR    R5,R1
         STH   R5,INTPCSW+5            SET BYTE CNT
         STH   R5,TPLNTH+2             STORE NEW RES. CNT.
         B     TPERLP              GO PRINT & SEE IF DONE
OKLNTH   MVI   PRTIO,C'#'          '# BYTES TO BE CHANGED' MSG
         MVC   PRTIO+1(28),BYTMSG
         MVC   PRTIO+29(10),BLANKS
         LA    R9,DTPIO
         LR    RA,R9
         AR    RA,R5
         BAL   R4,CHNG             GO TO ALTER REC ROUTINE FOR CHANGES
         B     TPERLP              GO PRINT & SEE IF DONE
TAPOK    LM    R1,RE,SAVREGS       EXIT TO PROCESS CORRECTED RECORD
         B     4(R1)                 WITH ORIGINAL FUNCTION
RTTPER   LM    R1,RE,SAVREGS       EXIT TO READ NEXT RECORD AND THEN
         B     0(R1)                 TO ORIGINAL FUNCTION
         EJECT
***********************************************************************
**********         VALIDATE FUNCTION CODE AND GO RECORD DITTO    ******
**********         USE, RETURN AND BRANCH TO APPROPRIATE         ******
**********         FUNCTION.                                     ******
***********************************************************************
         SPACE 1
LOOKUP   LA    R5,FUNCT            FUNCTION TABLE POINTER IN R5.
         CLI   FUNCOD+2,C' '           BLANK - ONLY 2 CHAR IN FUNCTION
         BNE   LKLOOP                  NO, SKIP NEXT INSTR.
         MVI   FUNCOD+2,C'U'       OVERLAY POS 3 OP CODE IF BLANK
LKLOOP   CLC   FUNCOD,0(R5)        OP CODE MATCH FUNCTION TABLE ?
         BE    4(R5)               GO TO APPROPRIATE FUNCTION
*        BE    $$AUTH              GO CALL DITTOVER THEN BRANCH TO
*                                  APPROPRIATE FUNCTION.
*                                  PARAMETERS ARE TAKEN OFF R5
*                                  AS 3 BYTE DITTO FUNCTION CODE AND
*                                  1 BYTE DITTO FUNCTION INDICATOR.
         LA    R5,8(R5)            BUMP INDEX
         CLI   0(R5),X'FF'         END OF TABLE ?
         BE    0(R8)               YES, RETURN
         B     LKLOOP              NO, LOOP FOR NEXT LOOK.
FUNCT    DC    C'CCUO'
         B     CDCD                CARD - CARD
         DC    C'CPUO'
         B     CDPR                CARD - PRINTER
         DC    C'CTUT'
         B     CDTP                CARD - TAPE
         DC    C'CTST'
         B     CDTPSQ              CARD - TAPE WITH RESEQUENCING
         DC    C'TCUT'
         B     TPCD                TAPE - CARD
         DC    C'TPUT'
         B     TPPRU               TAPE - LIST PRINT UNBLOCKED.
         DC    C'TPDT'
         B     TPPRR               TAPE - LIST PRINT REBLOCKED.
         DC    C'TDUT'
         B     TPDPU               TAPE - PRINT HEX UNBLOCKED.
         DC    C'TDDT'
         B     TPDPR               TAPE - PRINT HEX REBLOCKED.
         DC    C'TPVT'
         B     TPPRR               TAPE - LIST VARIABLE RECORDS
         DC    C'TDVT'
         B     TPDPR               TAPE - HEX  VARIABLE RECORDS
TTDC     DC    C'TTU2'             USE ALSO FOR TT = IF NEEDED
         B     TPTP                TAPE - TAPE.
         DC    C'WTMT'
         B     WTMK                WRITE TAPE MARK.
         DC    C'FSFT'
         B     FSPF                FORWARD SPACE FILE.
BSFDC    DC    C'BSFT'                 USE FOR BSR OR BSF MSG ALSO
         B     BSPF                BACK SPACE FILE.
         DC    C'FSRT'
         B     FSPR                FORWARD SPACE RECORD.
         DC    C'BSRT'
         B     BSPR                BACK SPACE RECORD.
         DC    C'REWT'
         B     REWTP               REWIND TAPE.
         DC    C'DSED'
         B     DISKDSE             DISPLAY DATA SET EXTENT ON CONSOLE
         DC    C'DDUD'
         B     DISKDP              DISK PRINT UNBLOCKED.
         DC    C'DRLD'
         B     DISKRLD             DISK RECORD LOAD.
         DC    C'DIDD'
         B     DSID                CHANGE DISK VOL  LABEL
EOJDC    DC    C'EOJO'             USE FOR EOJ CHECK IN CHKIRPT
         B     EOJ                 EOJ.
         DC    C'CCSO'
         B     CDCDS               CARD - CARD WITH SEQ. #'S.
         DC    C'RUNT'
         B     RUNTP               REWIND AND UNLOAD TAPE.
         DC    C'CDUO'
         B     CDDP                CARD - PRINTER IN HEX.
         DC    C'CCLO'
         B     CANC                CANCEL INPUT CARD DATA.
         DC    C'INTT'
         B     INITP               INITIALIZE TAPE.
         DC    C'ERTT'
         B     ERASETP             ERASE TAPE RECORDS
TRLDC    DC    C'TRL2'             USE ALSO FOR TRL = IF NEEDED
         B     TPRLD               TAPE RECORD LOAD.
         DC    C'TRST'
         B     TRSCAN              TAPE RECORD SCAN.
         DC    C'DRSD'
         B     DRSCAN              DISK RECORD SCAN.
         DC    C'DDDD'
         B     DISKDR              DISK - PRINT REBLOCKED.
         DC    C'EOFD'
         B     WRTEOF              WRITE DISK EOF RECORD.
         DC    C'SDUD'
         B     SPLITDD             DISK PRINT - SPLIT CYLINDER.
         DC    C'SDDD'
         B     SPLITDR             DISK PRINT REBLOCKED - SPLIT CYL.
         DC    C'DPUD'
         B     DISKPRT             DP
         DC    C'DPDD'
         B     DISKPRTD            DPD
         DC    C'SPUD'
         B     SPLITPRT            SP
         DC    C'SPDD'
         B     SPLITPRD            SPD
         DC    C'SRSD'
         B     SPLITRS             DISK RECORD SCAN - SPLIT CYL.
         DC    C'XXXO'
         B     LSTFUNCT            LIST DITTO FUNCTIONS
         DC    C'TTR2'
         B     TPTPR               TAPE - TAPE REBLOCKED
         DC    X'FF'
FUNCOD   DS    CL3
         EJECT
***********************************************************************
*                                                                     *
*                        OS/DITTO UTILITY ROUTINES                    *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
**********               LIST OS/DITTO FUNCTIONS               ********
***********************************************************************
         SPACE 1
LSTFUNCT EQU   *                   ENTRY - XXX FUNCTION
         BAL   R8,DHDG
         BAL   R8,OVFLO            SKIP TO CHANNEL 1
         MVI   PL,X'84'
         LOAD  EPLOC=LOADMODL          BRING IN XXX RTN
         LR    RC,R0               ENTRY POINT OF XXX FROM OS
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         LA    R7,CC               ADDRESS OF CARRIAGE CONTROL POS.
         LA    R9,PRNT             ADDRESS OF PRINT AREA
         LA    RB,CONIO
         MVI   INPUT,X'FF'
         BALR  RA,RC               BR TO XXX RETURN ON RA TO DELETE
         DELETE EPLOC=LOADMODL         DELETE XXX RTN AFTER USE
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
ABENDSW  BC    0,XXXRET            IF ABENDING U100,RETURN TO ABEND
         B     CONSOLE             GET NEXT FUNCTION
         DS    0F
LOADMODL DC    CL8'DITTOXXX'           XXX RTN NAME
         EJECT
***********************************************************************
**********      CARD TO CARD WITH OR WITHOUT SEQ.  NUM.     ***********
***********************************************************************
         SPACE 1
CDCD     EQU   *
         MVI   SEQSW+1,X'F0'       SW TO BYPASS RE-SEQUENCING
         MVI   TAPSW+1,0
         B     CDCD1
CDCDS    EQU   *
         MVI   TAPSW+1,X'00'       INACTIVATE TAPE OUTPUT
TAPSQ    MVI   SEQSW+1,X'00'       SW TO CAUSE RE-SEQUENCING
         LA    R7,1010             PAGE & LINE # FOR RPG & COBOL DECKS
         LA    RA,HOLDSEQ
         LA    R2,CONIO
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BNE   GETDT               NO, GO GET FROM CONSOLE
         LA    R9,CDCD1
         MVC   CONIO(3),DECKTYPE
         B     CHK1
GETDT    MVC   CONIO(6),XXXDASH        'XXX - '
         MVC   CONIO+6(8),DT           'DECKTYPE'
         MVI   WCNCCW+7,X'0E'
         MVI   RCNCCW+7,X'03'
         LA    R9,GETDK
         BAL   R8,WRTCON
CHK1     CLC   CONIO(3),=C'BAL'    DECKTYPE = BAL ?
         BNE   CHK2                NO,
         LA    R7,10               YES, SET 1ST CARD # = 0001
         LA    R3,75(R2)           SEQ. IN CC 76
         MVI   ALT2+1,X'04'        SEQ. LNGTH = 5
         LA    R5,72(R2)           NAME IN CC 73
         MVI   ALT1+1,X'02'        NAME LNGTH = 3
         MVI   DCKMSG+10,C'3'
         MVI   GETDK+11,X'03'
         LA    RA,3(RA)
         B     0(R9)
CHK2     CLC   CONIO(3),=C'RPG'    DECKTYPE = RPG ?
         BNE   CHK3                NO,
         LR    R3,R2               YES, SEQ IN CC 01
         MVI   ALT2+1,X'04'        SEQ LNGTH = 5
         LA    R5,74(R2)           NAME IN CC 75
         MVI   ALT1+1,X'05'        NAME LNGTH =6
         MVI   DCKMSG+10,C'6'
         MVI   GETDK+11,X'06'
         LA    RA,3(RA)
         B     0(R9)
CHK3     CLC   CONIO(3),=C'COB'    DECKTYPE = COBOL ?
         BNE   BADDT               NO, BAD FORMAT
         LR    R3,R2               SEQ IN CC 01
         MVI   ALT2+1,X'05'        SEQ LNGTH = 6
         LA    R5,72(R2)           NAME IN CC 73
         MVI   ALT1+1,X'07'        NAME LNGTH = 8
         LA    RA,2(RA)
         MVI   DCKMSG+10,C'8'
         MVI   GETDK+11,X'08'
         B     0(R9)
GETDK    MVC   CONIO(18),DCKMSG    CONSOLE MSG
         MVI   WCNCCW+7,X'12'      'DECK NAME X CHARS.'
         MVI   RCNCCW+7,X'08'
         BAL   R8,WRTCON
         MVC   DECKNAME,CONIO      SAVE OPERATOR REPLY
CDCD1    SR    R4,R4               ZERO CARD COUNTER FOR LOGOUT MSG
         MVC   TPDKMV+1(1),ALT1+1  LNTH FOR DECKNAME MOVE
         MVC   TPSQMV+1(1),ALT2+1  LNTH FOR SEQUENCE MOVE
         ST    R7,TPSQNUM          SAVE SEQ. NUMBER
         ST    R5,DECKADR          SAVE POINTER TO DECKNAME CC.
         ST    R3,SEQADR           SAVE POINTER TO SEQUENCE CC.
TAPSW    NOP   TAPSQ1              EXIT IF CTS FUNCTION
CDCD2    BAL   R8,CDRD             READ CARD
         CLI   EOF,X'FF'           /*, /+, OR HDWR END OF FILE ?
         BE    CCEND7
SEQSW    BC    15,PNCHIT           BRANCH IF 80/80 ONLY
ALT1     MVC   0(8,R5),DECKNAME    MOVE IN DECKNAME
         MVI   ESW,X'FF'           DO NOT SUPPRESS LEAD ZEROS
         BAL   R8,DECCVT           CONVERT REG 7 TO EBCDIC
ALT2     MVC   0(4,R3),0(RA)       MOVE SEQ NUMBER
         LA    R7,10(R7)           BUMP SEQ COUNTER
PNCHIT   BAL   R8,CDPN             GO PUNCH IT
         LA    R4,1(R4)            BUMP CARD COUNTER
         B     CDCD2               LOOP AND DO IT AGAIN
BADDT    CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    BADCTL              YES, PRNT MSG AND ABEND
         MVI   P,X'00'
         B     CDCDS               NO, RESTART
CCEND7   LR    R7,R4
CCEND    EQU   *
         MVC   CONIO(80),BLANKS    FORCE OUT LAST CARD
         BAL   R8,CDPN
         B     IOCNT               GO LOG-OUT CARD COUNT
         EJECT
***********************************************************************
**********         CARD TO PRINTER   LIST OR HEX            ***********
***********************************************************************
         SPACE 1
CDPR     EQU   *                   ENTRY FOR LIST ONLY
         BAL   R8,DHDG             GO PRINT HEADER
         BAL   R8,OVFLO
         SR    R7,R7               CARD COUNTER = 0
         MVI   PL,X'50'            PRNT LNGTH = 80
CDPR1    BAL   R8,CDRD             READ CARD
         CLI   EOF,X'FF'           /*, /+, OR HDWR END OF FILE ?
         BE    IOCNT               YES, GO LOG CARD COUNT
         MVI   CC,X'09'            SINGLE SPACE PRINT
         BAL   R8,EDIT             EDIT AND PRINT.
         LA    R7,1(R7)            BUMP COUNTER
         B     CDPR1               LOOP AND DO IT AGAIN
CDDP     EQU   *                   ENTRY - FOR HEX DUMP
         BAL   R8,DHDG             GO PRINT HEADER
         BAL   R8,OVFLO
         SR    R7,R7               ZERO RECORD COUNTER
         MVC   HDG1(10),DREC       SET UP HEADINGS
         MVC   HDG1+13(7),DATH
         MVI   HDG1+18,C'8'
         MVI   HDG1+19,C'0'
         LA    RA,DTPIO
CDDP1    BAL   R8,CDRD             READ CARD
         CLI   EOF,X'FF'           /*, /+, OR HDWR END OF FILE ?
         BE    IOCNT               YES, GO LOG COUNT
         MVC   DTPIO(80),CONIO     MOVE TO HEX DUMP AREA
         LA    R7,1(R7)            BUMP CARD COUNTER
         BAL   R8,DECCVT           CONVERT REC # TO EBCDIC, REG 7
         MVC   HDG1+5(5),HOLDSEQ+3
         MVI   PRLTH,X'50'
         BAL   R9,HEXDP            GO HEX DUMP
         B     CDDP1
         EJECT
***********************************************************************
**********          CARD TO TAPE - BLOCKED 1 TO 400         ***********
**********      WITH OR WITHOUT SEQ. NUMBERS & DECKNAME     ***********
***********************************************************************
         SPACE 1
CDTPSQ   EQU   *                   ENTRY - WITH SEQ NOS
         MVI   TAPSW+1,X'F0'       ACTIVATE RETURN IN CCS FUNCTION
         MVI   TPSEQSW+1,X'00'     ENABLE RE-SEQUENCING
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    CTCOM               YES,
         TM    CT,X'4C'            NO, ALL PARAMS GIVEN ?
         BNO   BADCTL              NO, BAD CTL CARD
         B     CTCOM1
CDTP     EQU   *                   ENTRY - BLOCKED TO 400
         MVI   TPSEQSW+1,X'F0'     BYPASS SEQUENCING
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    CTCOM               YES
         TM    CT,X'44'            NO, ALL PARAMS GIVEN ?
         BNO   BADCTL              NO - BAD CTL CARD
         B     CTCOM1
CTCOM    BAL   R7,OTMSG            GET OUTPUT TAPE
CTCOM1   EQU   *
         MVC   CONIO(3),HBF
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    CTRCOM              YES, BYPASS CONSOLE REQUEST
CDTPR1   MVC   CONIO(9),BF             'BLKFACTOR'
         MVI   WCNCCW+7,X'09'
         MVI   RCNCCW+7,X'03'
         BAL   R8,WRTCON
CTRCOM   LA    RC,3(R0)
         LA    RB,CONIO
         BAL   R8,TSTNUMV          BLK FACTOR NUMERIC ?
         B     CDTPR1              NO,
         CLC   CONIO(3),ZEROS      GREATER THAN ZERO ?
         BH    BFOK                YES
         CLI   CNTRLSW,X'FF'       NO, EXIT.  CONTROL CARDS ?
         BE    BADCTL              OR    OPERATOR
         MVI   P,X'00'             ERROR, IGNORE STRING PARAMS
         B     CDTPR1              RETRY
BFOK     MVC   HOLD+5(3),CONIO
         BAL   R8,BINCVT           CONVERT BLK FACTOR
         STH   R1,HBF              TO BINARY AND SAVE
         CLI   TPSEQSW+1,X'00'     ARE WE RE-SEQUENCING
         BE    TAPSQ               YES, GO GET PARAMS FROM CCS
TAPSQ1   SR    RC,RC               CLEAR CARD COUNTER
         SR    R4,R4               CLEAR BUFFER LNTH COUNTER
         LA    R5,DTPIO            TAPE OUTPUT AREA
         LH    R2,HBF              BLK FACTOR
         MH    R2,EIGHTY           MULTIPLY FOR BLOCK LENGTH
         ST    R2,INEND            SAVE BLOCK SIZE
         STH   R2,OTTPCCW+6        STORE IN CCW
         C     R2,DSL              IS BUFFER TO BIG FOR CORE ?
         BNH   MORCD               NO,
         MVC   CONIO(6),SYSOTMSG+8 YES, ISSUE MSG TO OPERATOR
         B     WRGLNTCA            'OUTPUT BLOCK EXCEEDS BUFFER, . . .
MORCD    BAL   R8,CDRD             READ CARD
         CLI   EOF,X'FF'           /*, /+, OR HDWR END OF FILE ?
         BE    LASTOT              YES, EXIT
TPSEQSW  NOP   TPSQBPAS            BRANCH IF NOT RE-SEQUENCING
         L     R1,DECKADR          INPUT ADDRESS FOR DECKNAME
TPDKMV   MVC   0(1,R1),DECKNAME    MOVE DECKNAME TO CARD
         L     R7,TPSQNUM          LOAD SEQ NUMBER
         MVI   ESW,X'FF'           DO NOT SUPPRESS LEADING ZEROS
         BAL   R8,DECCVT           CONVERT TO DECIMAL
         L     R1,SEQADR
TPSQMV   MVC   0(1,R1),0(RA)       MOVE SEQ # TO CARD
         LA    R7,10(R7)           BUMP SEQ #
         ST    R7,TPSQNUM           AND SAVE IT
TPSQBPAS MVC   0(80,R5),CONIO      MOVE TO TAPE BUFFER
         LA    RC,1(RC)            BUMP CARD COUNTER
         LA    R5,80(R5)           BUMP BUFFER POINTER
         LA    R4,80(R4)           KEEP TRACK OF LNGTH
         C     R4,INEND            BUFFER FULL ?
         BL    MORCD               NO,
         LA    R5,DTPIO            YES, RESET POINTERS
         SR    R4,R4
         BAL   R8,TPOT             WRITE TAPE BLOCK
         TM    OTEOR,1             END OF REEL ?
         BZ    MORCD               NO
         BAL   R9,NEWTAPOT         YES, GET NEW TAPE
         B     MORCD
LASTOT   LTR   R4,R4               LAST BLOCK A SHORT BLOCK ?
         BZ    CDTPEND             NO,
         STH   R4,OTTPCCW+6        YES, UPDATE CCW LNGTH
         BAL   R8,TPOT             GO WRITE SHORT BLOCK
CDTPEND  BAL   RA,LTMKR            WRITE TAPE MARK
         LR    R7,RC
         B     IOCNT               GO LOG CARD COUNT
TPSQNUM  DS    1F
DECKADR  DS    1F
SEQADR   DS    1F
         EJECT
***********************************************************************
**********         TAPE TO CARD - BLOCKED OR UNBLOCKED      ***********
***********************************************************************
         SPACE 1
TPCD     EQU   *
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    TC1                 YES,
         TM    CT,X'80'            VALID CONTROL CARD ?
         BNO   BADCTL              NO
         MVI   CT,X'00'
         B     TCCOM
TC1      BAL   R7,ITMSG            GET INPUT TAPE
TCCOM    EQU   *
         SR    R7,R7               ZERO CARD COUNTER
MORTP    BAL   R8,INTP             READ TAPE
         CLI   TMSW,X'FF'          EOF ?
         BE    CCEND               YES, WE ARE DONE
         SR    R2,R2               ZERO BLK LNGTH COUNTER
         LA    R4,DTPIO            BUFFER ADDRESS
         L     R5,DSL              CALCULATE BLOCK LNGTH
         S     R5,TPLNTH           LESS RESIDUAL COUNT = LNTH READ
         CLC   INTPCSW+5(2),DSL        BUFFER TOO SMALL ?
         BE    WRGLNTC             YES
PUNCHMOR CH    R5,=X'0051'         IS TAPE 81 CHAR BLK ?
         BNE   MOVPNCH             NO,
         LA    R2,1(R2)            YES, MUST HAVE SS CODE, BUMP PAST
         LA    R4,1(R4)
MOVPNCH  MVC   CONIO(80),0(R4)     MOVE TO PUNCH AREA
         LA    R2,80(R2)
         LA    R4,80(R4)           BUMP BUFFER POINTER
         BAL   R8,CDPN             GO PUNCH IT
         LA    R7,1(R7)            ADD 1 TO CARD COUNTER
         CR    R2,R5               END OF BUFFER ?
         BL    PUNCHMOR            NO,
         BE    MORTP               YES, GO GET TAPE BLK
         MVC   CONIO(59),BLANKS    TAPE NOT MULTIPLE OF 80,
         MVC   CONIO(19),TLMSG     ISSUE MSG AND EXIT
         B     WRGLNTC1            BYPASS BADLTH MSG
WRGLNTC  MVC   CONIO(6),SYSITMSG+8     'INPUT '
WRGLNTCA MVC   CONIO+6(53),BADLTH      ' BLOCK EXCEEDS BUFFER, TO ...
         MVC   UCODE(3),U200       MOVE  IN 200 FOR INADEQ. BUFFER
WRGLNTC1 CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    WMSGP
         MVI   WCNCCW+7,X'3B'
         LA    R8,CONSOLE
         B     WCON2
WMSGP    MVI   PL,X'3B'
         BAL   R8,OVFLO
         BAL   R8,PRNT
         B     ABEND               BR TO ABEND ( HAND CODED )
         EJECT
***********************************************************************
**********     TAPE TO PRINTER - LIST OR HEX, BLK OR UNBLK  ***********
***********************************************************************
         SPACE 1
TPPRU    EQU   *                  ENTRY - LIST UNBLOCKED
         MVI   RSZSW5+1,X'F0'
         MVI   DPSW,X'00'
         B     TP0
TPPRR    EQU   *                  ENTRY - DEBLOCK AND LIST
         MVI   RSZSW5+1,X'00'
         MVI   DPSW,X'00'
         B     TP0
TPDPU    EQU   *                  ENTRY - UNBLOCKED LIST & HEX
         MVI   RSZSW5+1,X'F0'
         MVI   DPSW,X'FF'
         B     TP0
TPDPR    EQU   *                  ENTRY - DEBLOCK LIST & HEX
         MVI   RSZSW5+1,X'00'
         MVI   DPSW,X'FF'
TP0      SR    R2,R2
         MVC   HH2(48),DEVH        BUILD HEADINGS
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    TP1
         TM    CT,X'80'            VALID CONTROL CARD ?
         BNO   BADCTL              NO
         B     RSZSW0
TP1      BAL   R7,ITMSG            GET INPUT TAPE
RSZSW0   EQU   *
         MVC   HH2+8(3),ITUCB          MOVE UCB TO DEVICE FIELD
         LA    R8,ITMODE           PICK UP TAPEIN  MODE SET FROM DEB
         CLI   INFMOTSW,X'FF'          INPUT FROM TAPEOUT DD ?
         BNE   RSZSW                   NO
         MVC   HH2+8(3),OTUCB          MOVE UCB TO DEVICE FIELD
         MVC   HH2+13(8),TPOTDD        MOVE TAPEOUT TO HEADING
         LA    R8,OTMODE           PICK UP TAPEOUT MODE SET FROM DEB
RSZSW    MVI   TPLPSW+1,X'F0'
         LA    R1,18               SET TABLE LENGTH
         LA    R4,MODTBL2          ADDRESS OF 2ND HALF OF TABLE
         LA    R7,MODTBL
PRNTSS   CLC   0(1,R8),0(R4)       LOOP THRU MODTBL
         BE    GOODSS              UNTIL MATCH IS FOUND
         LA    R7,2(R7)            EQUAL TO EXIST.
         LA    R4,1(R4)            MODE SET GIVEN.
         BCT   R1,PRNTSS
         LA    R7,BLANKS
GOODSS   MVC   HH2+27(2),0(R7)     MOVE DECIMAL MODE SET TO HEADING.
         BAL   R9,TAPECHAR         GO EVAL. MODE & PUT TRK & DEN IN HDG
TRSRETSW NOP   TRSRETRN                RETURN TO TRS
RSZSW5   BC    0,TPCOM             BRANCH IF NOT DEBLOCKING
         MVI   TPLPSW+1,X'00'
RSZSWDK  EQU   *                       BR HERE FOR DISK INPUT RECSIZE
         CLI   FUNCOD+2,C'V'       IS THIS VARIABLE LNGTH TAPE ?
         BE    TPCOM               YES
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    TP3
TP2      MVC   CONIO(16),RECSIZ    CONSOLE MSG 'RECSIZE ?'
         MVI   WCNCCW+7,X'10'
         MVI   RCNCCW+7,X'05'
         BAL   R8,WRTCON
         LA    RC,5(R0)
         LA    RB,CONIO
         BAL   R8,TSTNUMV          RECSIZE NUMERIC ?
         B     TP2                 NO,
         B     TP4                 YES
TP3      TM    CT,X'10'
         BZ    BADCTL
         MVC   CONIO(5),HRS
TP4      LA    RC,5(R0)
         CLC   CONIO(5),ZEROS      GREATER THAN ZERO ?
         BNE   GRTRZERO            YES,
         MVI   CONIO,C'X'
GRTRZERO LA    RB,CONIO
         BAL   R8,TSTNUM
         B     TP2
         MVC   HOLD+3(5),CONIO
         BAL   R8,BINCVT
         CLI   DBLSW,X'FF'         IS DISK DUMP USING THIS CODE ?
         BE    DISKDR1             YES, RETURN
         LR    R7,R1               LOGICAL REC LNGTH
         ST    R1,INEND            SAVE LOG. REC. SIZE TO LOAD RC LATER
         BAL   R8,DECCVT
         MVC   DATH+5(5),HOLDSEQ+3     PLACE IN HEADING
TPCOM    CLI   P,0                 OPERATOR LIMIT # PRINT BLOCKS ?
         BE    TPCOM1              NO
         BAL   R8,WRTCON           YES, GO GET IT
         LA    RC,4
         LA    RB,CONIO
         BAL   R8,TSTNUMV          IS IT NUMERIC ?
         B     TPCOM1              NO,  IGNORE
         MVC   HOLD+4(4),CONIO
         BAL   R8,BINCVT           CONVERT TO BINARY
         STH   R1,BLCT             SAVE IT
         MVI   CT,X'20'            TURN ON LIMIT PRINT SWITCH
TPCOM1   BAL   R8,DHDG
         BAL   R8,OVFLO
         MVC   CONIO,HH1
         MVI   PL,X'84'
         MVI   CC,X'19'
         CLI   RSZSW5+1,X'00'     ARE WE DEBLOCKING ?
         BE    PRTLTR              YES, PRINT HEADING LATER
         BAL   R8,PRNT             NO, PRINT NOW
PRTLTR   SR    R2,R2
         ST    R2,SAV              SET BLOCK COUNT = ZERO
         BAL   R8,INTP             READ TAPE
         CLI   TMSW,X'FF'          LEADING TAPE MARK ?
         BNE   TPLOOP2             NO, BRANCH AROUND RE-READ
TPMKPRNT EQU   *
         MVI   CC,X'13'            SPACE IMMEDIATE
         BAL   R8,PRNT
         MVC   CONIO(10),TAPMRK
         MVC   CONIO+10(2),HH1+1
         MVC   CONIO+12(120),CONIO+10
         MVI   PL,X'84'
         MVI   CC,X'19'            PRINT AND DOUBLE SPACE
         BAL   R8,PRNT             PRINT 'TAPE MARK'
TPMKDONE B     CONSOLE             TREAT   LEADING T.M. AS EOF IN OS
TPLOOP   EQU   *
         BAL   R8,INTP             READ TAPE
         CLI   TMSW,X'FF'          TAPE MARK ?
         BE    TPMKPRNT            YES, GO PRNT IT THEN EXIT
TPLOOP2  LA    RA,DTPIO            NO, CONTINUE
         L     R5,DSL
         S     R5,TPLNTH           LESS RESIDUAL COUNT = LNTH READ
         CLC   INTPCSW+5(2),DSL    DOES RES CNT = 0 ?
         BE    WRGLNTC             YES, ISSUE BLOCK EXCEEDS BUFFER MSG
         LR    R7,R5               BLOCK LNGTH IN REG 7
         BAL   R8,DECCVT           CONVERT TO EBCDIC
         MVC   HDG1(10),TBLK       PLACE IN HEADINGS
         MVC   HDG1+13(10),DATH
         MVC   HDG1+18(5),HOLDSEQ+3
         L     R7,SAV
         LA    R7,1(R7)            BUMP BLOCK COUNT
         ST    R7,SAV
         BAL   R8,DECCVT
         MVC   HDG1+5(5),HOLDSEQ+3 BLOCK NUMBER IN HEADING
TPLPSW   BC    15,TDU              NOP IF DEBLOCKING
         MVI   CC,X'19'
         MVI   PL,X'84'
         MVC   CONIO,HH1           SET UP HEADING
         MVC   CONIO+63(23),HDG1
         BAL   R8,PRNT             PRINT HEADING
         CLI   FUNCOD+2,C'V'       IS THIS VARIABLE LNGTH TAPE ?
         BNE   TDRLOP1             NO
         LA    RA,4(RA)            YES, BUMP PAST BLOCK LNGTH FIELD
         LA    R1,4
         SR    R5,R1               DECREMENT BLOCK LNGTH BY BLOCK FIELD
TDRLOP   CLI   FUNCOD+2,C'V'       IS THIS VARIABLE LNGTH TAPE ?
         BNE   TDRLOP1             NO,
         SR    RC,RC               YES, GET VARIABLE
         IC    RC,0(RA)              RECORD LNGTH
         SLL   RC,8                  FROM LOGICAL LNGTH FIELD
         IC    RC,1(RA)
         LA    RA,4(RA)            BUMP BUFFER POINTER PAST LNGTH FIELD
         LA    R1,4                4 IN REG 1 - LNTH OF REC LNTH FIELD
         SR    RC,R1               RC=ACTUAL USER DATA LNTH
         ST    RC,INEND            ST RC LOG. RECSIZE FOR LOAD NEXT PAR
         SR    R5,R1               DECREMENT BLOCK LNTH REMAINING
TDRLOP1  EQU   *
         L     RC,INEND            LOAD RC WITH LOG. RECSIZE
         LR    R3,RC               LOGICAL RECORD LENGTH
         MVC   HDG1(10),DREC
         MVC   HDG1+13(10),DATH
         LA    R2,1(R2)            BUMP RECORD NUMBER
         LR    R7,R2
         BAL   R8,DECCVT
         MVC   HDG1+5(5),HOLDSEQ+3 RECORD NUMBER IN HEADING
         LR    R7,R3               LOGICAL RECORD LENGTH
         BAL   R8,DECCVT           CONVERT TO EBDIC
         MVC   HDG1+18(5),HOLDSEQ+3  AND PLACE IN DATA LNGTH HEADING
         CR    R5,RC               FULL LOGICAL REC REMAINING ?
         BNL   FULBLK              YES
         LR    R3,R5               NO, UPDATE LNGTH FOR PRINT
         LR    R5,RC
         LR    R7,R3
         BAL   R8,DECCVT           CONVERT NEW LNGTH TO EBDIC
         MVC   HDG1+18(5),HOLDSEQ+3    AND PLACE IN HEADING
FULBLK   BAL   RB,DEBLOK           GO PRINT IT
         SR    R5,RC               SUBTRACT FROM BUFFER LNGTH
         BNZ   TDRLOP              LOOP IF BUFFER NOT EMPTY
         MVI   CC,X'13'            SET UP TO SPACE 2, NO PRNT
         BAL   R8,PRNT             DO IT
         B     TPLOOPCT            GO LOAD BUFFER
TDU      LR    R3,R5               BLOCK LN. IN REG 3
         BAL   RB,DEBLOK           GO PRINT BLOCK
         LA    R1,100(R0)
         CR    R5,R1               PRINT MORE THAN 1 LINE ?
         BNH   TPLOOPCT            NO,
         MVI   CC,X'13'            YES,
         BAL   R8,PRNT             PRINT ADD'NL LINE
TPLOOPCT TM    CT,X'20'            DO WE LIMIT # BLKS PRINTED ?
         BZ    TPLOOP              NO
         LH    R1,LOWLIM           GET BLK COUNT
         LA    R1,1(R1)            BUMP
         STH   R1,LOWLIM               BLOCK COUNT BY 1
         CLC   BLCT,LOWLIM         REACH LIMIT ?
         BE    CONSOLE             YES, WE ARE DONE
         B     TPLOOP              DOUBLE SPACE &  READ AGAIN
         EJECT
***********************************************************************
**********                    TAPE TO TAPE                  ***********
***********************************************************************
         SPACE 1
TPTP     EQU   *
         MVI   TRLSW+1,X'00'       NOP TAPE RECORD LOAD EXIT
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    TPTP1
         TM    CT,X'C0'
         BNO   BADCTL
         TM    CT,X'20'                OPTIONAL NBLKS GIVEN ?
         BNO   NONBLKS                 NO
         MVI   NBLKSW+1,X'00'          YES, SET SWITCH
NONBLKS  EQU   *                       BR HERE IF PGMR OMITS NBLKS
         MVI   CT,X'00'
         LA    R3,1
         B     FILELP
TPTP1    BAL   R7,ITMSG            GET INPUT TAPE
         BAL   R7,OTMSG            GET OUTPUT TAPE
FILECT   MVC   CONIO(7),FILNUM     GET NUMBER OF FILES
         MVI   WCNCCW+7,X'07'
         MVI   RCNCCW+7,X'02'
         BAL   R8,WRTCON
         LA    RC,2(R0)
         LA    RB,CONIO
         BAL   R8,TSTNUMV          NUMBER OF FILES NUMERIC ?
         B     FILECT              NO
         MVC   HOLD+6(2),CONIO
         BAL   R8,BINCVT           CONVERT TO BINARY
         LR    R3,R1               REG 3 HAS # OF FILES
         LTR   R3,R3               # FILES = ZERO ?
         BNZ   FILELP              NO
         MVI   P,X'00'             TURN OFF STRING PARAM INDICATOR
         B     FILECT              GO GET # FILES AGAIN
FILELP   SR    R7,R7               CLEAR BLOCK COUNTER
TTCOM    BAL   R8,INTP             GET INPUT TAPE BLOCK
         CLI   TMSW,X'FF'          TAPE MARK ?
         BNE   TTMORE              NO
         BAL   RA,LTMKR            GO WRITE OUTPUT T.M.
         MVC   CONIO+8(21),BLKTMSG     ' BLKS AND T.M. COPIED'
NBLKSMSG EQU   *                       BR HERE FOR NBLKS MSQ W/O T.M.
         MVI   ESW,X'00'
         BAL   R8,DECCVT
         MVC   CONIO(8),HOLDSEQ        MOVE # BLKS TO MSG
         LA    R4,29
         BAL   R9,IOCNT1           GO LOG RECORD COUNT
         BCT   R3,FILELP           LOOP UNTIL ALL FILES COPIED
         B     CONSOLE
TTMORE   LA    R7,1(R7)            BUMP BLOCK COUNT
         L     R5,DSL              CALCULATE TAPE
         S     R5,TPLNTH           LESS RESIDUAL COUNT = LNTH READ
         CLC   INTPCSW+5(2),DSL    EXCEED BUFFER ?
         BE    WRGLNTC             YES
         STH   R5,OTTPCCW+6        NO, STORE IN OUTPUT CCW
         BAL   R8,TPOT             WRITE TAPE
         TM    OTEOR,1             END OF REEL ?
         BZ    TRLSW               NO,
         BAL   R9,NEWTAPOT         YES, GO GET ANOTHER TAPE MOUNTED
TRLSW    BC    0,TRLRT             'TAPE RECORD LOAD' ROUTINE EXIT
NBLKSW   BC    15,TTCOM            LOOP AND READ AGAIN, UNLESS NBLKS
         STH   R7,LOWLIM               STORE PRESENT BLK CNT
         CLC   BLCT,LOWLIM             COMPARE TO NBLKS
         BNE   TTCOM                   CONTINUE COPY
         MVI   NBLKSW+1,X'F0'          FALL THRU, RESET SW TO NORM
         MVC   CONIO+8(21),BLKTMSG     ' BLKS AND T.M. COPIED'
         MVC   CONIO+13(4),=C', NO'    ' BLKS, NO T.M. COPIED'
         B     NBLKSMSG                GO PRINT MSG
         EJECT
***********************************************************************
**********               INITIALIZE  TAPE (VOL1XXXXXX)      ***********
***********************************************************************
         SPACE 1
INITP    EQU   *
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    BADCTL              YES, NOT ALLOWED - EXIT
         BAL   R7,OTMSG            GET OUTPUT TAPE ADDRESS
         MVC   CONIO(15),NVOLMSG
         MVC   CONIO+14(6),XX      'ENTER NEW VOL XXXXXX'
         MVI   WCNCCW+7,X'14'
         MVI   RCNCCW+7,X'06'
         BAL   R8,WRTCON
         MVC   DTPIO(80),BLANKS
         MVC   DTPIO(4),=C'VOL1'       BUILD NEW VOL LABEL
         MVC   DTPIO+4(6),CONIO
         MVC   CONIO(11),NVOLMSG+6 'NEW VOL IS '
         MVC   CONIO+11(6),DTPIO+4 'XXXXXX'
         LA    R8,DTPIO
         MVC   41(10,R8),USERNAME
         MVC   OTTPCCW+6(2),EIGHTY
         BAL   R8,TPOT             WRITE VOL1 LABEL
         MVC   DTPIO(61),BLANKS
         MVC   DTPIO(4),=C'HDR1'
         BAL   R8,TPOT             WRITE HDR1 LABEL
         MVI   WCNCCW+7,X'11'
         BAL   R8,WCON2
         B     LTMK
         EJECT
***********************************************************************
**********              TAPE TO TAPE REBLOCKED              ***********
***********************************************************************
         SPACE 1
TPTPR    EQU   *
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    TTR1                YES
         TM    CT,X'D4'            VALID PARAMETERS ?
         BNO   BADCTL              NO
         MVC   CONIO(5),HRS        RECSIZE PARAMETER
         B     TTR3
TTR1     BAL   R7,ITMSG            GET INPUT TAPE FROM CONSOLE
         BAL   R7,OTMSG            GET OTPUT TAPE FROM CONSOLE
TTR2     MVC   CONIO(16),RECSIZ    CONSOLE MSG 'RECSIZE ?'
         MVI   WCNCCW+7,16
         MVI   RCNCCW+7,5
         BAL   R8,WRTCON           GET RECSIZE FROM CONSOLE
TTR3     LA    RC,5
         LA    RB,CONIO
         BAL   R8,TSTNUMV          RECSIZE NUMERIC ?
         B     TTR2                NO
         LA    R4,TTR2             YES,
         MVC   HOLD+3(5),CONIO
         BAL   R8,BINCVT           CONVERT TO BINARY IN REG 1
         LTR   R1,R1               IS IT ZERO ?
         BZ    TTBAD               YES
         LR    R2,R1               SAVE RECSIZ
         MVC   CONIO(3),HBF        CONTROL CARD BLKFACTOR
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    TTR5                YES
TTR4     MVC   CONIO(4),=C'NEW '
         MVC   CONIO+4(9),BF           'BLKFACTOR'
         MVI   WCNCCW+7,X'0D'
         MVI   RCNCCW+7,X'03'
         BAL   R8,WRTCON           GO GET BLK FACTOR
TTR5     LA    RC,3
         LA    RB,CONIO
         BAL   R8,TSTNUMV          BLK FACTOR NUMERIC ?
         B     TTR4                NO
         LA    R4,TTR4             YES
         MVC   HOLD+5(3),CONIO     BLKFACTOR TO HOLD
         BAL   R8,BINCVT           CONVERT TO BINARY IN REG 1
         LTR   R1,R1               IS IT ZERO ?
         BZ    TTBAD               YES
         LR    R3,R1               SAVE BLKFACTOR
TTR6     SR    R7,R7               CLEAR RECORD COUNTER
         BAL   R8,INTP             GET FIRST BLOCK
         CLI   TMSW,X'FF'          TAPE MARK ?
         BNE   TTR7                NO
         BAL   R9,TPMKMSG          YES, GO LOG IT
         B     CONSOLE             DONE
TTR7     CLC   INTPCSW+5(2),DSL    RESIDUAL COUNT ZERO ?
         BE    WRGLNTC             YES, BUFFER TO SMALL
         L     R5,DSL              CCW READ LNTH   INITIALLY
         S     R5,TPLNTH           LESS RESIDUAL COUNT = LNTH READ
         MVC   FRSBLKCT,INTPCSW+5  SAVE, CHK CONSISTENT MAX BLKSZ, I/P
         LA    R9,DTPIO            ADDRESS OF BUFFER POOL
         AR    R9,R5               PLUS INPUT LNTH = ADDRESS OF OUTPUT
         ST    R9,OTTPCCW            BUFFER IN OTTPCCW
         MVI   OTTPCCW,1           RESET OTTPCCW TO WRITE
         LR    R9,R2               RECSIZE
         MR    R8,R3               X'S BLKFACTOR = OUTPUT BLK LNTH
         STH   R9,OTTPCCW+6        STORE IN OUTPUT CCW
         L     R1,OTTPCCW          OUTPUT BUFFER ADDRESS
         AR    R1,R9               PLUS OUTPUT LNTH
         LA    R1,0(R1)            DROP HIGH ORDER BYTE
         ST    R1,OTEND            GIVES ADDRESS OF BUFFER END +1
         LA    R9,0(R9)            DROP SIGN BIT OF OUTPUT BLK LNTH
         AR    R5,R9               ADD TO INPUT LNTH = TOTAL BUFFER
         C     R5,DSL              I/P + O/P GREATER THAN BUFFER SIZE ?
         MVC   CONIO(6),SYSOTMSG+8     'OUTPUT'-SET UP MSG IF BR TAKEN
         BH    WRGLNTCA            YES, EXIT
         MVI   MOVREC2+1,X'F0'     BYPASS SHORT MOVE
         SR    RC,RC               CLEAR COUNTER
         LA    RA,256              MAX LNTH OF MVC
TTR8     SR    R2,RA               DECREMENT RECSIZE UNTIL MINUS
         LTR   R2,R2               TEST IT
         BM    TTR9                BRANCH IF MINUS
         BZ    TTR10               BRANCH IF ZERO
         LA    RC,1(RC)            ELSE BUMP COUNTER
         B     TTR8                 AND DO AGAIN
TTR9     MVI   MOVREC2+1,0         ENABLE SHORT MOVE CODE
         AR    R2,RA               MAKE IT PLUS
         BCTR  R2,R0               DECREMENT BY 1 FOR SHORT
         STC   R2,TTRSHORT          MOVE FOR LOGICAL REC.
TTR10    STH   RC,MOVECT           SAVE # OF 256 LNTH MOVES PER LOG REC
         L     R3,OTTPCCW          ADDRESS OF OUTPUT BUFFER
         LA    R3,0(R3)            DROP HIGH ORDER BYTE
         B     TTR13
TTR12    BAL   R8,INTP             GET INPUT RECORD
         CLI   TMSW,X'FF'          TAPE MARK ?
         BE    TTRLAST             YES
         CLC   FRSBLKCT,INTPCSW+5  CURR. I/P BLK > 1ST I/P BLK ?
         BH    WRGLNREC            YES
TTR13    L     R5,DSL              IN-TAPE READ LNTH
         S     R5,TPLNTH           LESS RESIDUAL COUNT = LNTH READ
         LA    R1,DTPIO            BEGIN OF INPUT BUFFER
         AR    R1,R5               ADD LNTH OF INPUT BLOCK
         ST    R1,INEND            EQUALS ADDRESS+1 OF BLOCK END
         LA    R2,DTPIO            ADDRESS INPUT BUFFER
TTR14    LA    R7,1(R7)            BUMP LOGICAL REC COUNT
         LH    R1,MOVECT           NUMBER OF MOVES PER LOG REC
         LTR   R1,R1               DO WE NEED FULL MOVES ?
         BZ    MOVREC2             NO, JUST SHORT MOVE
MOVREC   MVC   0(256,R3),0(R2)     LOGICAL REC FROM IN TO OUT BUFFER
         LA    R2,256(R2)          BUMP INPUT BUFFER ADDRESS
         LA    R3,256(R3)          BUMP OUTPUT BUFFER ADDRESS
         BCT   R1,MOVREC
MOVREC2  B     TTR15               BRANCH IF ALL OF RECORD MOVED
         SR    R1,R1               CLEAR REGISTER
         IC    R1,TTRSHORT         LNTH-1 OF REMAINING PART OF REC
         EX    R1,MOVREC3          MOVE IT
         LA    R1,1(R1)            BUMP LNTH-1 TO LNTH
         AR    R2,R1               ADD IT TO INPUT BUFFER
         AR    R3,R1               ADD IT TO OUTPUT BUFFER
TTR15    C     R3,OTEND            IS OUTPUT BUFFER FULL ?
         BL    TTR16               NO
         BAL   R8,TPOT             YES, EMPTY IT
         TM    OTEOR,1             IS THIS END OF REEL ?
         BZ    NOTERR              NO
         BAL   R9,NEWTAPOT         YES, GO GET ANOTHER TAPE MOUNTED
NOTERR   L     R3,OTTPCCW          RESET POINTER TO BEGIN OF BUFFER
         LA    R3,0(R3)            DROP HIGH ORDER BYTE
TTR16    C     R2,INEND            IS INPUT BUFFER EMPTY ?
         BL    TTR14               NO, GO MOVE NEXT RECORD
         BE    TTR12               YES, GO READ NEXT TAPE BLOCK
WRGLNREC MVC   CONIO(6),SYSITMSG+8     'INPUT '
WRGLNRC1 MVC   CONIO+6(15),BADLTH      ' BLOCK EXCEEDS '
         MVC   CONIO+21(38),BLOCKMSG   'FIRST BLOCK OR NOT MULTIPLE...
         MVC   UCODE(3),U500       SET U500 ABEND FOR THIS COND.
         B     WRGLNTC1
TTBAD    CLI   CNTRLSW,X'FF'       BAD DATA, IS THIS CONTROL CARDS ?
         BE    BADCTL              YES
         MVI   P,0                 NO
         BR    R4                  TURN OFF STRING PARAMETER & RETRY
TTRLAST  L     R1,OTTPCCW          ADDRESS OF OUTPUT BUFFER
         SR    R3,R1               RESULTS EQUAL ACTIVE BUFFER LNTH
         LA    R3,0(R3)            DROP HIGH-ORDER BYTE
         LTR   R3,R3               IS BUFFER EMPTY ?
         BZ    TTRDONE             YES
         STH   R3,OTTPCCW+6        NO, LNTH FOR LAST WRITE
         BAL   R8,TPOT             EMPTY BUFFER
TTRDONE  BAL   R9,TPMKMSG          GO LOG REC COUNT & WRITE T.M.
         B     CONSOLE             RETURN TO DITTO FUNCTION
TPMKMSG  BAL   RA,LTMKR            WRITE OUTPUT TAPE MARK
         MVC   CONIO+8(16),LGRCDMSG    ' LOGICAL RECORDS'
         MVC   CONIO+24(16),TMCOPIED   ' AND T. M. COPIED'
         MVI   ESW,X'00'           ALLOW LEADING ZEROS
         BAL   R8,DECCVT           CONVERT REG 7 TO DECIMAL
         MVC   CONIO(8),HOLDSEQ    RESULTS TO PRINT AREA
         LA    R4,40
         B     IOCNT1              GO LOG RECORD COUNT
TTRSHORT DS    CL1
FRSBLKCT DS    CL2                 RESID. CNT OF 1ST I/P BLK IN TTR RTN
MOVECT   DS    H
INEND    DS    F
OTEND    DS    F
MOVREC3  MVC   0(1,R3),0(R2)       MODIFIED BY EXECUTE
         EJECT
***********************************************************************
**********              TAPE RECORD LOAD                    ***********
***********************************************************************
         SPACE 2
TPRLD    EQU   *                   ENTRY - TRL FUNCTION
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    BADCTL              YES, INVALID FUNCTION
         BAL   R8,DHDG             GO PRINT HEADER
         BAL   R7,ITMSG            GET INPUT TAPE ADDRESS
         BAL   R7,OTMSG            GET OUTPUT TAPE ADDRESS
         SR    R7,R7               CLEAR BLOCK COUNTER
HOWFAR   MVC   CONIO(6),BLKMSG
         MVI   WCNCCW+7,X'06'
         MVI   RCNCCW+7,X'04'
         BAL   R8,WRTCON           NBLKS MSG TO SYSLOG
         LA    RC,4(R0)
         LA    RB,CONIO
         BAL   R8,TSTNUMV          IS IT NUMERIC ?
         B     HOWFAR              NO
         MVC   HOLD+4(4),CONIO     YES, CONVERT TO
         BAL   R8,BINCVT           BINARY
         LR    R2,R1
         CLC   CONIO(4),ZEROS      IS IT ZERO ?
         BE    HOWFAR              YES, RESTART
WHICHWAY MVC   CONIO(24),DIRECT
         MVI   WCNCCW+7,X'18'
         MVI   RCNCCW+7,X'01'
         BAL   R8,WRTCON           'DIRECTION F OR B ?' MSG TO SYSLOG
         CLI   CONIO,C'B'          COPY BACKWARD ?
         BE    COPYB               YES
         CLI   CONIO,C'F'          COPY FORWARD ?
         BE    COPYF               YES
         MVI   P,0                 NO, TURN OF STRING PARAMS
         B     WHICHWAY            LOOP & ASK AGAIN
COPYF    MVI   INTPCCW,X'02'       SET CCWS TO COPY
         MVI   OTTPCCW,X'01'       FORWARD
         MVI   TRLSW+1,X'F0'       SET 'TT' EXIT SW ON
         LA    R3,1(R0)            # FILES = 1
FLOOP    B     TTCOM               EXIT TO TAPE TO TAPE
TRLRT    BCT   R2,FLOOP            AND LOOP UNTIL DONE
         MVI   OTTPCCW,X'27'
         BAL   R8,OVFLO
         BAL   R8,TPOT             BACKSPACE OUTPUT TAPE IN CASE WE
         MVI   OTTPCCW,X'01'       ALTER RECORD IN CORE AND REWRITE
         LR    R3,R5
         MVI   DPSW,X'FF'
         LA    RA,DTPIO            I/O AREA
         BAL   R8,DECCVT
         MVC   TBLK+5(5),HOLDSEQ+3
         MVC   HDG1(10),TBLK
         ST    R7,SAV              SAVE COUNT OF BLOCKS
         LR    R7,R5
         BAL   R8,DECCVT
         MVC   HDG1+13(10),DATB        SET UP PRINT HDG.
         MVC   HDG1+18(5),HOLDSEQ+3
         BAL   RB,DEBLOK           GO HEX PRINT TAPE RECORD
RIGHTREC MVC   CONIO(11),DESIRED
         MVC   CONIO+11(10),CHNGMSG+16
         MVI   WCNCCW+7,X'15'
         MVI   RCNCCW+7,X'01'
         BAL   R8,WRTCONP
         L     R7,SAV
         CLI   CONIO,C'N'
         BE    TRLRET              ANSWER NO, GO COPY AGAIN
         CLI   CONIO,C'Y'
         BNE   RIGHTREC
         LA    R1,TRLRET           ANSWER YES, SAVE REGS
         STM   R1,RE,SAVREGS       AND EXIT TO TAPE ERROR ROUTINE
         B     CHNGIT              TO ALTER RECORD
TRLRET   NOP   TRLRET1
TRLRET1  STH   R5,OTTPCCW+6
         BAL   R8,TPOT             WRITE UPDATED RECORD ON OUTPUT TAPE
         B     HOWFAR              LOOP AND ASK IF DONE
COPYB    MVI   INTPCCW,X'27'
         MVI   OTTPCCW,X'27'       SET UP TO BACKSPACE BOTH TAPES
         SR    R7,R2               DECREMENT BLK COUNTER BY # OF BLOCKS
BLOOP    BAL   R8,INTP
         BAL   R8,TPOT
         BCT   R2,BLOOP            LOOP UNTIL BACKSPACED DESIRED AMOUNT
         LA    R2,1(R0)
         B     COPYF               GO PRINT LAST RECORD
         EJECT
***********************************************************************
**********                   TAPE RECORD SCAN               ***********
***********************************************************************
         SPACE 1
TRSCAN   EQU   *                   ENTRY - TRS FUNCTION
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    BADCTL              VALID FUNCTION FOR CONSOLE ONLY
         BAL   R8,DHDG             GO PRINT HEADER
         MVI   TRSRETSW+1,X'F0'        SET SW FOR RETURN
         B     TP0                     GO USE TP CODE
TRSRETRN MVI   TRSRETSW+1,X'00'        RESET TRSRETSW TO NOP
         L     R2,DSL              MAX REC LNTH FOR VARIABLE RECORD
TRSDRS   MVC   CONIO(16),RECSIZ
         MVI   WCNCCW+7,X'10'
         MVI   RCNCCW+7,X'05'
         BAL   R8,WRTCON           MSG ' LOGICAL REC LENGTH ?'
         LA    R0,4                LNTH OF COUNT FIELD =4, VAR. RECDS
         CLI   CONIO,C'V'          RECSIZE=V ? (VARIABLE RECORDS)
         BE    TRSVAR              YES
         LA    RB,CONIO
         LA    RC,5(R0)
         BAL   R8,TSTNUMV          IS LOG. REC LN. NUMERIC ?
         B     TRSDRS              NO, RESTART
         MVC   HOLD+3(5),CONIO
         BAL   R8,BINCVT           YES, CONVERT TO BINARY
         LR    R2,R1               REG 2 HAS LOG. RE. LENGTH
         LA    R0,0                LNTH OF COUNT FIELD =0, FIXED LNTH
TRSVAR   STH   R0,HBF              STORE LNGTH OF COUNT FIELD
SCANLTH  MVC   CONIO(25),LNTHMSG   'LNTH SCAN ARGUMENT (1-35)'
         MVI   WCNCCW+7,X'19'
         LA    RB,CONIO
         MVI   RCNCCW+7,X'02'
         BAL   R8,WRTCON           MSG 'LENGTH OF SCAN ARGUMENT ?'
         MVI   P,0
         LA    RC,2(R0)
         BAL   R8,TSTNUMV          SCAN LNGTH NUMERIC ?
         B     SCANLTH             NO
         MVC   ALTCT+6(2),CONIO
         MVC   HDG1(13),SCANMSG2+1     'SCAN ARGUMENT'
         MVC   HDG1+14(7),DATH     SET UP PRINT HEADINGS
         MVC   HDG1+19(2),CONIO
         MVC   HOLD+6(2),CONIO
         BAL   R8,BINCVT
         LA    R8,35(R0)
         CR    R1,R8               SCAN LENGTH EXCEED 35 ?
         BH    SCANLTH             YES, RESTART
         LTR   R1,R1               SCAN LN. = 0 ?
         BZ    SCANLTH             YES, RESTART
         LR    R3,R1
         LR    R5,R3               REG 5 HAS SCAN LENGTH
SCANST   MVC   CONIO(29),SDPMSG
         MVI   WCNCCW+7,X'1D'
         MVI   RCNCCW+7,X'05'
         BAL   R8,WRTCON           MSG 'START POS. OF SCAN FIELD ?'
         LA    RC,5(R0)
         LA    RB,CONIO
         BAL   R8,TSTNUMV          IS IT NUMERIC
         B     SCANST              NO, RESTART
         MVC   HOLD+3(5),CONIO
         BAL   R8,BINCVT           CONVERT TO BINARY
         LTR   R1,R1               IS IT ZERO ?
         BZ    SCANST              YES, RESTART
         LR    R4,R1               REG 4 HAS SCAN DISPLACEMENT
         SR    R0,R0
         LA    R9,PRTIO
         BCTR  R4,0                DECREMENT SCAN DISPLACEMENT BY 1
         LR    RA,R4
         CLI   TYP,X'01'           IS THIS DISK SCAN FOR EOF ?
         BE    SCANOK              YES, BYPASS LENGTH CHECK
         AR    R4,R5               ADD SCAN LN. TO SCAN DISPLACEMENT
         CR    R2,R4               TOTAL EXCEED REC LN. ?
         BNL   SCANOK              NO
         MVC   CONIO(28),LNGTHMSG  'LENGTH EXCEEDS END OF RECORD'
         MVI   WCNCCW+7,X'1C'
         BAL   R8,WCON2            YES, ISSUE MSG AND RESTART
         CLI   TYP,X'02'           IS THIS DISK SCAN ON KEY ?
         BE    SCANLTH             YES, RESTART WITHOUT REC LN.
         B     TRSDRS              ELSE RESTART FOR BOTH
SCANOK   MVC   DECKNAME(5),ALTCT       CHANGE MSG TO 'ENTER ...'
         BAL   R4,LNTHOK           GO GET SCAN ARGUMENT DATA
         STC   R5,PRLTH
         CLI   TYP,X'00'           IS DISK SCAN USING CODING ?
         BNE   DRS1                YES, THEY EXIT HERE
         MVC   DTPIO(35),PRTIO     SAVE SCAN ARG
         BAL   R8,OVFLO
         BCTR  R5,0                MODIFY COMPARE LN WITH SCAN LN - 1
         STC   R5,SCANCHK+1
         LR    R5,RA               SAVE REG 10
         LA    RA,DTPIO
         BAL   R9,HEXDP            GO HEX DUMP SCAN ARG.
         LR    RA,R5               REG 10 = SCAN DISPLACEMENT
         MVC   HDG1(35),DTPIO          SAVE SCAN ARG
         SR    R7,R7               ZERO BLOCK COUNTER
SCANIN   BAL   R8,INTP             READ TAPE
         CLI   TMSW,X'FF'          EOF ?
         BNE   SCANIN2             NO, BRANCH & CONTINUE
         MVC   CONIO(14),=C'NO MATCH FOUND'
         MVI   WCNCCW+7,X'0E'
         BAL   R8,WCON2            PRNT 'NO MATCH FOUND'
         B     CONSOLE             WE ARE DONE
SCANIN2  LA    R7,1(R7)            BUMP BLOCK COUNT
         SR    RC,RC               CLEAR LRECL CTR
         SR    R4,R4
         L     R5,DSL
         S     R5,TPLNTH           LESS RESIDUAL COUNT = LNTH READ
         CLC   INTPCSW+5(2),DSL    BLOCK EXCEED BUFFER ?
         BE    WRGLNTC             YES, EXIT
         LA    R1,DTPIO            POINT TO BEGIN OF BUFFER
         AH    R1,HBF              HBF=LNTH OF COUNT FIELD
         AH    R4,HBF                 =4 FOR VARIABLE, =0 FOR FIXED LN
SCANLOOP LH    R8,HBF
         LA    RC,1(RC)            BUMP  LRECL CTR
         LTR   R8,R8               HBF = 0 ?
         BZ    SCANLOP1            YES, THIS IS FIXED LNTH RECORDS
         MVI   SCANVSW+1,X'F0'     SET SW TO BR IN FIXED LENGTH SCAN
         SR    R2,R2               NO, UPDATE R2 WITH LOGICAL LNTH
         IC    R2,0(R1)             OF VARIABLE RECORD
         SLL   R2,8
         IC    R2,1(R1)
         AR    R4,R2               CNT BUF PTR, R4=(LOG.DATA+4+4)
         LA    R0,4                DECREMENT R2 TO
         SR    R2,R0               DATA PORTION OF VAR. BLK. FORMAT
         LA    R1,4(R1)            BUMP PAST COUNT FIELD
SCANLOP1 LR    R3,R1               POINT TO BEGIN OF LOGICAL RECORD
         AR    R3,RA               ADD SCAN DISPLACEMENT - 1
SCANCHK  CLC   0(1,R3),HDG1        MATCH ?
         BE    SCANHIT             YES,
         AR    R1,R2               NO, BUMP TO NEXT LOGICAL RECORD
SCANVSW  BC    0,SCNBUFCK          BR IF SCAN IS VAR.BLK.
         AR    R4,R2               DO ONLY IN FIXED LENGTH SCAN
SCNBUFCK CR    R5,R4               ARE WE AT END OF BUFFER ?
         BH    SCANLOOP            NO, LOOP & CHECK NEXT LOG. REC.
         B     SCANIN              YES, GO GET NEW BLOCK
SCANHIT  MVI   DPSW,X'FF'          SET HEX DUMP SWITCH ON
         MVC   HDG1(35),BLANKS         CLEAR ARG FROM SAVE AREA
         LR    R9,R1                   SAVE R1 (PTR TO LREC IN DTPIO)
         BAL   R8,DECCVT           CONVERT BLK # TO EBCDIC
         MVC   HDG1(10),TBLK       SET UP HEADINGS
         MVC   HDG1+5(5),HOLDSEQ+3
         LR    R7,R5               BLOCK LENGTH
         BAL   R8,DECCVT           CONVERT TO EBCDIC
         MVC   HDG1+13(10),DATH
         MVC   HDG1+18(5),HOLDSEQ+3
         MVC   CONIO,HH1           HDG TO PRINT AREA
         MVC   CONIO+63(23),HDG1       BLK CT & LN
         MVI   PL,132                  PRT LN
         BAL   R8,PRNT             PRINT HEADING
         LR    R7,RC               RECORD #
         MVC   HDG1(10),DREC           HDG
         BAL   R8,DECCVT           CNVRT TO DEC
         MVC   HDG1+5(5),HOLDSEQ+3     REC # IN HDG
         LR    R7,R2               LRECL
         BAL   R8,DECCVT               CNVRT TO DEC
         MVC   HDG1+18(5),HOLDSEQ+3    LRECL IN HDG
         LR    R3,R2                   LRECL FOR DE BLOCK & PRT
         LR    RA,R9                   POINT TO LREC IN DTPIO
         BAL   RB,DEBLOK               GO PRT
         B     CONSOLE                 ALL DONE
         EJECT
***********************************************************************
**********         TAPE CONTROL AND MISC. FUNCTIONS         ***********
***********************************************************************
         SPACE 1
IOCNT    MVI   ESW,X'00'           BEGIN 'LOG CARD COUNT MSG' ROUTINE
         BAL   R8,DECCVT           REG 7 CONTAINS COUNT
         MVC   CONIO(8),HOLDSEQ
         MVC   CONIO+8(10),=C' I/O CARDS'
         LA    R4,18
         LA    R9,CONSOLE          RETURN ADDRESS
IOCNT1   CLI   CONIO,C' '
         BNE   IOCNT2
         MVC   CONIO(40),CONIO+1   LEFT JUSTIFY MSG
         BCT   R4,IOCNT1
IOCNT2   CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    IOCNTLST            YES, LOG ON SYSPRINT
         STC   R4,WCNCCW+7         NO, LOG ON CONSOLE
         BAL   R8,WCON2
         BR    R9                  RETURN
IOCNTLST BAL   R8,OVFLO
         STC   R4,PL
         BAL   R8,PRNT
         BR    R9                  END 'LOG CARD COUNT MSG'
NEWTAPOT EQU   *
         MVC   CONIO(27),NWTPMSG
         MVI   WCNCCW+7,X'1B'
         BAL   R8,WCON2            'MOUNT NEW OUTPUT TAPE...'
         OI    OTTPDCB+48,X'80'    SET OFLGS BIT TO WTM ON EOV MACRO
         EOV   OTTPDCB             ISSUE EOV TO REWIND & UNLOAD
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         BR    R9                  END MULTIPLE TAPE OUTPUT
DHDG     NOPR  R8                  PRINT  OS/DITTO HEADING
         OI    *-1,X'F0'           1ST TIME ONLY
         ST    R8,SAVREGS+32       SAVE RETURN REGISTER
         BAL   R8,OVFLO
         MVI   CC,X'1B'
         MVI   CONIO,C' '
         MVC   CONIO+1(131),CONIO
         MVI   PL,X'84'            TRIPLE SPACE
         BAL   R8,PRNT             4
         BAL   R8,PRNT             TIMES
         BAL   R8,PRNT
         BAL   R8,PRNT
         MVI   CC,X'09'
LL1      MVC   D1((6*L+6*S)-7),D1-1    PRINT LINE 1, -7 ADJUSTS FOR OS
         MVC   OO1(L),OO1-1
         MVI   DD1,C'D'
         MVC   DD2(L-2),DD1
         MVI   O2,C'O'
         MVC   O3(L-3),O2
         MVI   S2,C'S'
         MVC   S3(L-3),S2
         MVC   OO1(L),O1
         MVI   I1,C'I'
         MVC   I2(L-1),I1
         BCR   0,R1
         OI    *-1,X'F0'
         MVI   T1,C'T'
         MVC   T2(L-1),T1
         MVC   TT1(L),T1
         MVC   X+L-01(2),=C'* '
         BAL   R8,PRNT
LL2      MVC   DD2(L-1),DD1         PRINT LINE 2
         MVI   O1,C'O'
         MVC   O2(L-1),O1
         MVI   S1,C'S'
         MVC   S2(L-1),S1
         MVI   I1,C'I'
         MVC   I2(L-1),I1
         MVC   OO1(L),O1
         BCR   0,R1
         OI    *-1,X'F0'
         MVC   X+L-02(2),=C'* '
         BAL   R8,PRNT
         MVC   DD3(L-4),BLANKS
         MVC   O3(L-4),BLANKS
         MVC   S3(L-4),BLANKS
         MVC   OO1(L),O1
         MVC   I1(L/2-1),BLANKS
         MVC   I1+L/2+1(L),BLANKS
         MVC   T1+L/2+1(L),BLANKS
         MVC   TT1(L),T1
         MVC   X+L-03(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 3
         MVC   S3+1(L),S3
         MVC   X+L-04(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 4
         MVC   X+L-05(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 5
         MVC   S2(L-2),S1
         MVC   X+L-06(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 6
         MVI   S1,C' '
         MVI   S1+L-1,C'S'
         MVC   X+L-07(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 7
         MVC   S2(L-3),S1
         MVC   X+L-08(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 8
         MVC   X+L-09(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 9
         MVI   S1,C'S'
         MVI   S2,C'S'
         MVC   X+L-10(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 10
         BAL   R1,LL2
         MVC   X+L-11(2),=C'* '
         BAL   R8,PRNT             PRINT LINE 11
         BAL   R1,LL1
         MVC   X+L-12(2),=C'* '
         MVI   CC,X'19'
         BAL   R8,PRNT             PRINT LINE 12
         MVC   D1(124),D1-1
         MVC   D1+41(39),COPYRT
         BAL   R8,PRNT
         MVC   D1(124),D1-1
         MVC   D1+48(5),=C'DATE='
         MVC   D1+53(6),DATESAVE   GET DATE FROM TIME MACRO SAVE AREA
         MVC   D1+60(12),COPYRT+48     'OS/DITTO-00?'     MAINT. VER.
         BAL   R8,PRNT             PRINT IT
         L     R8,SAVREGS+32       LOAD RETURN REGISTER
         BR    R8                  RETURN
LTMK     LA    RA,CONSOLE          WRITE LAST TAPE MARK ROUTINE
LTMKR    MVI   OTTPCCW,X'1F'
         BAL   R8,TPOT
         MVI   OTTPCCW,X'01'
         BR    RA
REWTP    EQU   *                       ENTRY - TO REWIND TAPE
         MVI   OTTPCCW,X'07'
         B     TPCT
RUNTP    EQU   *                       REWIND & UNLOAD VIA CLOSE MACRO/
         CLI   CNTRLSW,X'00'           CONSOLE OPERATION ?
         BE    TPCT1RN                 YES
         TM    CT,X'40'                NO, OUTPUT TAPE GIVEN ?
         BNO   BADCTL
         B     GOTTCTRN
TPCT1RN  BAL   R7,OTMSG
GOTTCTRN CLI   OTONINSW,X'FF'          CLOSE TAPEIN ?
         BE    SETCLSIT                YES GO SET CLOSE TAPEIN SW
SETCLSOT MVI   CLOSOTSW+1,X'00'        SET SW TO FALL THRU AFTER CLOSE
         B     CLOSOTTP
SETCLSIT MVI   CLOSITSW+1,X'00'        SET SW TO FALL THRU AFTER CLOSE
         B     CLOSINTP
ERASETP  EQU   *                   ENTRY - 3420 ERASE TAPE
         MVI   OTTPCCW+7,1
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    BADCTL              YES, INVALID
         BAL   R7,OTMSG            GET TAPE ADDRESS
ERT1     LA    R1,ERTCCW
         ST    R1,OTTPCCWA
         MVC   ERTCCW(1),MSOCCW    GET MODE SET
         BAL   R8,TPOT             ERASE TAPE
         B     CONSOLE             RETURN
WTMK     EQU   *                   ENTRY - WRITE TAPE MARK
         MVI   OTTPCCW,X'1F'
         B     TPCT
BSPF     EQU   *                   ENTRY - BACK SPACE TAPE FILE
         MVI   OTTPCCW,X'2F'
         B     NFILMSG
FSPF     EQU   *                   ENTRY - FORWARD SPACE TAPE FILE
         MVI   OTTPCCW,X'3F'
NFILMSG  CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    TPCT                YES
         MVC   DECKNAME,FILNUM     # FILES TO MESSAGE AREA
         B     NTAPMSG
FSPR     EQU   *                   ENTRY - FORWARD SPACE TAPE RECORD
         MVI   OTTPCCW,X'37'
         B     NBLKMSG
BSPR     EQU   *                   ENTRY - BACK SPACE TAPE RECORD
         MVI   OTTPCCW,X'27'
NBLKMSG  CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    TCNTSAV             YES,
         MVC   DECKNAME,BLANKS     CLEAR POSITION  7
         MVC   DECKNAME(6),BLKMSG  # BLKS TO MESSAGE AREA
NTAPMSG  BAL   R7,OTMSG            GET OUTPUT TAPE
NBLKMSG1 MVC   CONIO(7),DECKNAME   CONSOLE MESSAGE
         MVI   WCNCCW+7,X'07'
         MVI   RCNCCW+7,X'04'
         BAL   R8,WRTCON           WRITE ON SYSLOG
         LA    RC,4(R0)
         LA    RB,CONIO
         BAL   R8,TSTNUMV          WAS IT NUMERIC ?
         B     NBLKMSG1
         B     TCNTCOM             YES,
TCNTSAV  MVC   CONIO(4),HNB        GET CONTROL CARD NBLOCKS
         TM    CT,X'20'            WAS IT GIVEN ?
         BZ    TPCT                NO, DEFAULT IS 1
         TM    CT,X'60'            OUTPUT TAPE GIVEN ?
         BNO   BADCTL              NO, BAD , BAD
TCNTCOM  LA    RC,4(R0)
         CLC   CONIO(4),ZEROS      WAS IT ZERO ?
         BNE   NOZER
         MVI   CONIO,C'X'          YES, MAKE IT BAD
NOZER    LA    RB,CONIO
         BAL   R8,TSTNUM           IS IT NUMERIC ?
         B     NBLKMSG1
         MVC   HOLD+4(4),CONIO     YES, CONVERT TO BINARY
         BAL   R8,BINCVT           REG 5 = # BLKS
         LR    R5,R1
         MVI   OTTPCCW+7,X'01'     REC LN = 1
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    GOTTCT              YES
         B     TPCTO               BRANCH AROUND BLKS=1
TPCT     LA    R5,1(R0)            SET DEFAULT = 1
TPCTO    MVI   OTTPCCW+7,X'01'     REC LN = 1
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    TPCT1               YES,
         LA    R8,OTTPCCW          NO, SET CCW'S TO BYPASS MODE SET
         ST    R8,OTTPCCWA
         TM    CT,X'40'            OUTPUT TAPE GIVEN ?
         BNO   BADCTL              NO, EXIT
         B     GOTTCT              YES
TPCT1    BAL   R7,OTMSG            GET CONSOLE OUTPUT TAPE
GOTTCT   EQU   *
         SR    R7,R7
TPCPLP   BAL   R8,TPOT             GO PERFORM TAPE OPERATION
         BCT   R5,TPCPLP           NOP WHEN DONE
         B     CONSOLE
CANC     EQU   *                   ENTRY - CANCEL CARD INPUT FUNCTION
         CLI   CNTRLSW,X'FF'       VALID FOR CONSOLE ONLY
         BE    BADCTL
FLUSHLP  CLI   EOF,X'FF'           /*, /+, OR HDWR END OF FILE ?
         BE    CONSOLE             YES, DONE
         BAL   R8,CDRD             NO, FLUSH RDR
         B     FLUSHLP             UNTIL EOF
         EJECT
***********************************************************************
**********               CHANGE DISK VOLUME NUMBER          ***********
***********************************************************************
         SPACE 1
DSID     EQU   *                   ENTRY - DID FUNCTION
         CLI   CNTRLSW,X'FF'       VALID FOR CONSOLE ONLY
         BE    BADCTL
         LINK  EP=DITTODID
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER MACRO
         B     CONSOLE             RETURN
NOID     MVC   CONIO(20),NOREC
         MVI   WCNCCW+7,X'14'
         BAL   R8,WCON2            LOG 'NO REC FOUND' MSG
         B     CONSOLE
         EJECT
***********************************************************************
**********                    DISK RECORD LOAD              ***********
***********************************************************************
         SPACE 1
DISKRLD  CLI   CNTRLSW,X'FF'       VALID FOR CONSOLE MODE ONLY
         BE    BADCTL
         BAL   R8,DHDG             GO PRINT HEADER
         BAL   R7,IDMSG            GET DISK UNIT ADDRESS
         MVC   DEVH+8(3),CONIO
         MVC   HH2(19),DEVH
         MVC   HH2+13(8),DSKINDD   PLACE DDNAME IN HDG
DRECAD   MVC   CONIO(23),RECADDRS
         MVI   WCNCCW+7,X'17'
         MVI   RCNCCW+7,X'0A'
         BAL   R8,WRTCON           GET ADDRESS 'CCC-HH-RRR'
         MVI   P,0
         LA    RB,CONIO+7
         LA    RC,3(R0)
         BAL   R8,TSTNUM
         B     DRECAD
         MVC   HOLD+5(3),CONIO+7
         BAL   R8,BINCVT
         LTR   R1,R1               REC # = 0 ?
         BZ    DRECAD              YES, INVALID
         LA    R8,255(R0)
         CR    R1,R8               REC # GREATER THAN 255 ?
         BH    DRECAD              YES, INVALID
         STC   R1,LOWLIM+6         STORE RRR IN SEEK ADDRESS
         MVC   CONIO+3(2),CONIO+4
         LA    RB,CONIO            GO VALIDATE CCCHH
         BAL   R9,EDTADR
         B     DRECAD              INVALID, RESTART
         STH   R0,LOWLIM+2         CCC IN SEEK ADDRESS
         STC   R1,LOWLIM+5         HH IN SEEK ADDRESS
         MVI   DSKDRLSW+1,X'F0'    SET RETURN SW TO A BRANCH
         B     FOUNDHA             GO USE DEC CNVRT CODE IN FOUNDHA
DRLRET   EQU   *
         MVI   DSKDRLSW+1,X'00'    SET RETURN SW TO A NOP
         MVC   DCYL+5(3),HOLDSEQ+5 MOVE CCC TO HDG
         MVC   HH2+41(2),BLANKS        BLANK OUT HH MOVED IN BY FOUNDHA
         MVC   CONIO,HH1
         BAL   R8,OVFLO
         MVI   PL,X'84'
         BAL   R8,PRNT             PRINT HDG
         MVI   CC,X'13'
         BAL   R8,PRNT
         BAL   R4,DSKDEBL          GET DISK REC AND PRINT IT
         B     NOID
         CLI   EOF,X'10'           IS CODE BEING USED BY 'WRITE EOF' ?
         BE    WRTEOFR             YES, THEY EXIT HERE
ALTKEY   LA    R9,DTPIO+8
         LTR   R2,R2               KEY LENGTH = 0 ?
         BZ    ALTDAT              YES,
         LTR   R5,R5               IS DATA LENGTH = 0 ?
         BZ    KEYONLY             YES, REC HAS KEY ONLY
         MVC   CONIO(5),ALTRMSG
         MVC   CONIO+5(18),SCANMSG+4
         MVI   WCNCCW+7,X'17'
         MVI   RCNCCW+7,X'01'
         BAL   R8,WRTCONP
         CLI   CONIO,C'D'          CHANGE DATA ?
         BE    ALTDAT              YES,
         CLI   CONIO,C'K'          MODIFY KEY ?
         BNE   ALTKEY              NO, RESTART
KEYONLY  MVC   PRTIO(5),LBKEY          MSG '# KEY BYTES TO BE CHANGED ?
         MVC   PRTIO+5(28),BYTMSG
         MVC   PRTIO+33(10),BLANKS
         LR    RA,R9               REG 9 = BEGIN OF KEY
         AR    RA,R2               REG 10 = END OF KEY
         BAL   R4,CHNG             GO ALTER KEY IN CORE
         B     DRLPRT              GO PRINT ALTERED RECORD
ALTDAT   LTR   R5,R5               IS DATA LENGTH = 0 ?
         BZ    CONSOLE             YES, MUST BE AN EOF RECORD - INVALID
         AR    R9,R2
         LR    RA,R9               REG 9 = BEGIN OF DATA
         AR    RA,R5               REG 10 = END OF DATE
         MVC   PRTIO(6),LBDATA
         MVC   PRTIO+6(28),BYTMSG
         BAL   R4,CHNG             GO ALTER DATA REC IN CORE
DRLPRT   BAL   R8,OVFLO
         MVC   CONIO(27),ALTERMSG
         MVI   PL,X'1B'
         BAL   R8,PRNT             PRNT ALTERED RECORD
         BAL   R4,DRCDEBL
         B     DRCMSG
DRCMSG   MVC   CONIO(26),CHNGMSG
         MVI   WCNCCW+7,X'1A'
         MVI   RCNCCW+7,X'01'
         BAL   R8,WRTCON           MSG 'CHANGES COMPLETE, Y OR N ?
         CLI   CONIO,C'Y'
         BE    WRTDRC              YES, GO REWRITE DISK REC
         CLI   CONIO,C'N'
         BNE   DRCMSG              NEITHER, RE-ASK QUESTION
         B     ALTKEY              NO, GO ALTER SOME MORE
WRTDRC   EQU   *
         LA    R8,DTPIO+8
         ST    R8,RWCCW
         MVI   RWCCW,X'0D'         SET CCW TO WRITE K & D
         BAL   R8,DISK             GO DO IT
         TM    DSKS1,X'08'         SUCCESSFUL ?  OR NO RECORD FOUND ?
         BO    NOID                NO, BAD DISK
         B     CONSOLE             YES, WE ARE DONE
         EJECT
***********************************************************************
***********             DISK DUMP  BLK OR UNBLK             ***********
***********************************************************************
         SPACE 1
DISKPRT  EQU   *                   ENTRY - DP FUNCTION
         MVI   LISTSW,X'FF'
         B     DISKDP
DISKPRTD EQU   *                   ENTRY - DPD FUNCTION
         MVI   LISTSW,X'FF'
         B     DISKDR
SPLITPRD EQU   *                   ENTRY - SPD FUNCTION
         MVI   LISTSW,X'FF'
         B     SPLITDR
SPLITPRT EQU   *                   ENTRY - SP FUNCTION
         MVI   LISTSW,X'FF'
SPLITDD  EQU   *                   ENTRY - SPLIT CYL. UNBLK
         MVI   EOF,X'01'
         B     DISKDP
SPLITDR  EQU   *                   ENTRY - SPLIT CYL. DEBLOCK
         MVI   EOF,X'01'
DISKDR   EQU   *                   ENTRY - FULL CYL. DEBLOCK
         MVI   DBLSW,X'FF'
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    DISKDP
         TM    CT,X'93'
         BO    DISKDP
         MVI   CT,X'00'
DISKDP   EQU   *                   ENTRY - DD FUNCTION
         MVI   DRSSW,X'00'         TURN OFF DISK REC SCAN EXIT
DISKDP1  MVC   HH2(22),DEVH        ENTRY FOR DRS, ENTRY ABOVE FOR DDU
         MVC   HH2+22(24),DSKH
         BAL   R8,DHDG
         MVC   HH2+13(8),DSKINDD   MOVE DDNAME TO HDG
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    DSKCON
         TM    CT,X'83'
         BNO   BADCTL
         BAL   R9,DISKCHAR         GO GET DISK CHAR. & PLACE IN HDG
         MVC   CONIO(5),ADRSLO
         B     DSPR1
DSKCON   BAL   R7,IDMSG            GET DISK UNIT ADDRESS
ADDBG    MVC   CONIO(13),BEGINMSG      'CCCHHH - BEGIN'
         MVI   WCNCCW+7,X'0D'
         MVI   RCNCCW+7,X'05'
         BAL   R8,WRTCON           GET BEGIN ADDRESS - CCCHH
DSPR1    LA    RB,CONIO
         MVC   HH2+8(3),DKUCB      MOVE UCB TO HDG
         BAL   R9,EDTADR           GO VALIDATE CCCHH
         B     ADDBG
         STH   R0,LOWLIM+2         CCC IN SEEK ADDRESS
         MVI   BEGHD,X'00'
         CLI   EOF,X'01'           SPLIT CYL. ?
         BNE   DSPR1A              NO, LEAVE TOP TRK = 0
         STC   R1,BEGHD            YES, SET TOP TRK = HH
DSPR1A   STC   R1,LOWLIM+5         HH IN SEEKADDRESS
         MVC   CONIO(5),ADRSUP
         CLI   CNTRLSW,X'FF'       CONTROL CARDS ?
         BE    DSPR2
ADDEN    MVC   CONIO(11),BEGINMSG      'CCCHH - BEG'
         MVC   CONIO+8(3),EN           MOVE 'END' OVER 'BEG'
         MVI   WCNCCW+7,X'0B'
         MVI   RCNCCW+7,X'05'
         BAL   R8,WRTCON           GET END ADDRESS - CCCHH
DSPR2    BAL   R9,EDTADR           GO VALIDATE IT
         B     ADDEN
         STH   R0,UPPLIM+2         STORE CCC
         STC   R1,UPPLIM+5         STORE HH
         CLI   EOF,X'01'           SPLIT CYL ?
         BNE   DRSSW1              NO, LEAVE BOTTOM TRACK ALONE
         STC   R1,ENDHD            YES, SET BOTTOM TRK = HH
DRSSW    EQU   *+1                 BRANCH CTL SWITCH
DRSSW1   NOP   DRSCAN1             EXIT. IF DRS
         BAL   R8,OVFLO
         CLI   DBLSW,X'FF'         DO WE DEBLOCK INTO LOG LNGTH ?
         BNE   RO                  NO
         MVI   CT,X'10'            YES, GO USE 'TP' CODE TO GET LOG LN.
         B     RSZSWDK             RETURN VIA B TO DISKDR1
DISKDR1  ST    R1,SAVRCSZ          LOG REC LNGTH RETURNED IN REG 1,SAVE
RO       CLC   UPPLIM,LOWLIM       ARE WE DONE ?
         BL    CONSOLE             YES
         LA    R8,SRCHA            NO, SET TO READ R0 REC
         ST    R8,DSKCCWA
         BAL   R8,DISK             GO READ R0
         TM    DSKS1,X'08'         NO RECORD FOUND ?
         BZ    FOUNDHA             BRANCH, WE FOUND IT
         MVC   CONIO(17),=C'NO HOME ADDRS REC'
         MVI   WCNCCW+7,X'11'
         BAL   R8,WCON2
         B     CONSOLE
FOUNDHA  SR    R7,R7
         IC    R7,LOWLIM+5         PHYSICAL HEAD #
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   HH2+41(2),HOLDSEQ+6
         MVC   DHED+6(2),HOLDSEQ+6
         LH    R7,LOWLIM+2         PHYSICAL CYL #
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
DSKDRLSW BC    0,DRLRET            NOP TIL SET BY DRL FUNCTION
         MVC   HH2+31(3),HOLDSEQ+5
         MVC   DCYL+5(3),HOLDSEQ+5
         MVC   HH2+52(65),BLANKS
         LA    R8,SHCCW            RESET IOB TO
         ST    R8,DSKCCWA              READ RN RECORD
         MVC   RWCCW+6(2),DSL+2
         MVC   LLIMSAVE,LOWLIM     SAVE DISK ADDRESS
         TM    DTPIO,X'03'         DEFECTIVE ALTERNATE TRACK ?
         BNO   TRDP                NO,
         MVC   HH2+46(30),GATRK    DEFECTIVE ALTERNATE TRACK
         MVC   HH2+46(9),BPTRK     YES, BUILD HDG.
         MVI   ALTSW,X'FF'
         B     PRTRO               GO PRNT HDG
TRDP     TM    DTPIO,X'02'         DEFECTIVE PRIMARY TRACK ?
         BNO   TROA                NO,
         MVC   HH2+46(66),BPTRK    DEFECTIVE PRIMARY TRACK
         SR    R7,R7               YES,
         IC    R7,DTPIO+8          GET HH OF ALTERNATE TRACK
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   HH2+110(2),HOLDSEQ+6
         IC    R7,DTPIO+5          GET CCC OF ALTERNATE TRACK
         SLL   R7,8
         IC    R7,DTPIO+6
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   HH2+99(3),HOLDSEQ+5
         B     PRTRO               GO PRINT R0 REC
TROA     TM    DTPIO,X'01'         OPERATIVE ALTERNATE TRACK ?
         BNO   TROP                NO,
         MVC   HH2+46(69),GATRK    OPERATIVE ALTERNATE TRACK
         SR    R7,R7               YES,
         IC    R7,DTPIO+8          GET HH OF ORIGINAL TRACK
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   HH2+113(2),HOLDSEQ+6
         MVC   DHED+6(2),HOLDSEQ+6
         IC    R7,DTPIO+5          GET CCC OF ORIGINAL TRACK
         SLL   R7,8
         IC    R7,DTPIO+6
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   HH2+102(3),HOLDSEQ+5
         MVC   DCYL+5(3),HOLDSEQ+5
         MVC   LOWLIM+2(4),DTPIO+5 RESET SEEK ADDRESS TO ORIGINAL TRACK
         B     PRTRO               GO PRINT R0
TROP     MVC   HH2+46(28),BPTRK    OPERATIVE PRIMARY TRACK
         MVC   HH2+46(9),GATRK     MUST BE OPERATIVE PRIMARY TRACK
PRTRO    MVI   PL,X'84'
         MVI   CC,X'19'
         MVC   CONIO(132),HH1
         BAL   R8,PRNT             PRINT HEADING USING R0 DATA
         SR    R3,R3               CLEAR RRR COUNTER
         CLI   ALTSW,X'FF'         ARE WE ON BAD ALT. TRK ?
         BE    BUMPTK              YES, NO DATA HERE - EXIT
RN       LA    R3,1(R3)            BUMP TO NEXT COUNT ID
         STC   R3,LOWLIM+6         STORE ID RRR IN SEEK ADDRESS
         BAL   R4,DSKDEBL          GO GET REC & PRINT IT
         B     BUMPTK              NO MORE RECS ON TRACK - EXIT
         CLI   TYP,X'00'           DISK REC SCAN USING THIS CODE ?
         BNE   DRS3                YES, THEY EXIT HERE
         CLI   DSKEOFSW,X'FF'      EOF ON DISK? SET ONLY IN CTL CD MODE
         BNE   RNCONT              NO, KEEP GOING
         B     CONSOLE             YES, WE'RE DONE WHEN EOF & CNTRL CDS
RNCONT   EQU   *
         B     RN                  LOOP AND GET NEXT REC
BUMPTK   MVC   LOWLIM,LLIMSAVE     RESET SEEK ADDRESS IN CASE
         MVI   ALTSW,X'00'           WE WERE READING AN ALT TRACK
         CLC   LOWLIM+5(1),ENDHD       LAST TRACK THIS CYL ?
         BL    BUMPHD              NO, GO BUMP HEAD
         LH    R3,LOWLIM+2         YES, BUMP CYL BY 1
         LA    R3,1(R3)
         STH   R3,LOWLIM+2
         MVC   LOWLIM+5(1),BEGHD       RESET SEEK ADDRESS TO UPPER TRAC
         B     RO                  GO READ R0 REC
BUMPHD   IC    R3,LOWLIM+5         BUMP TRACK BY 1
         LA    R3,1(R3)
         STC   R3,LOWLIM+5
         B     RO                  GO READ R0
         EJECT
***********************************************************************
**********               DISK  RECORD  SCAN                 ***********
***********************************************************************
         SPACE 1
SPLITRS  EQU   *                   ENTRY - SPLIT REC SCAN
         MVI   EOF,X'01'
DRSCAN   EQU   *                   ENTRY - FULL CYL SCAN
         CLI   CNTRLSW,X'FF'
         BE    BADCTL              INVALID FUNCTION FOR CONTROL CARDS
         MVI   DRSSW,X'F0'         TURN ON DRS SW FOR DDU CODING
         B     DISKDP1             USE DD CODE FOR CUU & CCCHH-CCCHH
DRSCAN1  MVI   WCNCCW+7,X'1F'      RETURN HERE FROM DD CODE
         MVI   RCNCCW+7,X'01'
         MVC   CONIO(31),SCANMSG
         BAL   R8,WRTCON           MSG 'SCAN KEY, DATA, OR EOF ?'
         MVI   TYP,X'01'           SCAN EOF SWITCH
         CLI   CONIO,C'E'          SCAN EOF ?
         BE    DRS2                YES, NO MORE INPUT NEEDED
         MVI   TYP,X'02'           SCAN KEY SWITCH
         LA    R2,255(R0)          MAX KEY LNGTH
         CLI   CONIO,C'K'          SCAN KEY ?
         BE    SCANLTH             YES, USE TRS CODING
         MVI   TYP,X'03'           SCAN DATA SWITCH
         CLI   CONIO,C'D'          SCAN DATA ?
         BE    TRSDRS              YES, USE TRS CODING
         MVI   P,0
         B     DRSCAN1
DRS1     ST    R2,SAVREGS+8        RETURN HERE FROM TRS CODING
         ST    R5,SAVREGS
         ST    RA,SAVREGS+4
DRS2     MVI   WCNCCW+7,X'0A'
         MVC   CONIO(10),RCDMSG    'RECORDS AT'
         BAL   R8,WCON2
         MVC   CONIO(10),RECADDRS  'CCC-HH-RRR'
         BAL   R8,WCON2            LOG CONSOLE HEADING
         LA    R1,1(R0)
         L     R4,SAVREGS
         SR    R4,R1
         STC   R4,KEYLNTH+1        MODIFY LENGTH OF COMPARES WITH
         STC   R4,DATLNTH+1            USER'S SCAN LENGTH
         B     RO                  EXIT TO DD CODE -RETURN AT DRS3
DRS3     CLI   TYP,X'02'           SCAN ON KEY ?
         BE    CKKEY               YES
         BH    CKDAT               03 = SCAN ON DATA
         LTR   R5,R5               ELSE SCAN ON EOF
         BZ    HITIT               BRANCH IF DATA LN = 0
         B     RN                  ELSE RETURN AND READ NEXT REC
CKKEY    LTR   R2,R2               KEY LN = 0 ?
         BZ    RN                  YES, GO READ NEXT REC
         L     R0,SAVREGS+4        SCAN DISPLACEMENT
         L     R1,SAVREGS          SCAN LNGTH
         AR    R1,R0               ADD THEM
         CR    R2,R1               ARE WE WITHIN THIS KEY LNGTH ?
         BL    RN                  NO, GO READ NEXT REC
         LA    R1,DTPIO+8          YES, POINT TO KEY IN BUFFER
         AR    R1,R0               BUMP BY USER'S DISPLACEMENT
KEYLNTH  CLC   PRTIO,0(R1)         SCAN MATCH ?
         BE    HITIT               YES
         B     RN                  NO, GO READ NEXT REC
CKDAT    LA    R1,DTPIO+8          POINT TO KEY IN BUFFER
         AR    R1,R2               ADD KEY LNGTH
         L     R2,SAVREGS+4        ADD SCAN DISPLACEMENT
         AR    R1,R2
         L     R4,SAVREGS+8        USER'S LOGICAL REC LNGTH
         LR    R0,R4
DATLOOP  CR    R0,R5               ARE WE WITHIN LOGICAL DATA REC
         BH    RN                  NO, GO READ NEXT REC
DATLNTH  CLC   PRTIO,0(R1)         SCAN MATCH ?
         BE    HITIT               YES
         AR    R1,R4               NO, BUMP TO NEXT
         AR    R0,R4                   LOGICAL REC
         B     DATLOOP             AND CHECK AGAIN
HITIT    SR    R7,R7
         MVC   CONIO(10),RECADDRS      'CCC-HH-RRR' FORMAT DISPLAY AREA
         IC    R7,DTPIO+3          GET HH OF THIS RECORD
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   CONIO+4(2),HOLDSEQ+6    PLACE IN CONSOLEAREA
         IC    R7,DTPIO+4          GET RRR - THIS RECORD
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   CONIO+7(3),HOLDSEQ+5
         LH    R7,DTPIO            GET CCC OF THIS RECORD
         MVI   ESW,X'FF'
         BAL   R8,DECCVT
         MVC   CONIO(3),HOLDSEQ+5
         MVI   WCNCCW+7,X'0A'
         BAL   R8,WCON2            LOG 'CCC-HH-RRR' ON CONSOLE
         B     RN                  GO GET NEXT REC.
         EJECT
***********************************************************************
**********                   WRITE EOF RECORD               ***********
***********************************************************************
         SPACE 1
WRTEOF   EQU   *                   ENTRY - WRITE EOF RECORD
         MVI   EOF,X'10'           SET WRITE EOF SWITCH ON
         B     DISKRLD             DRL CODING WILL GET RECORD
WRTEOFR  MVI   DTPIO+5,X'00'
         MVC   DTPIO+6(2),DTPIO+5      SET K & D LN = 0 IN COUNT FIELD
         MVI   RWCCW+6,X'00'
         MVI   RWCCW+7,X'08'       SET CCW LENGTH = 8 (COUNT ONLY)
         MVI   RWCCW,X'1D'         SET CCW = WRITE COUNT, KEY & DATA
         BAL   R4,DSKDEBL1         GO REWRITE DISK REC
         B     NOID                NOT SUCCESSFUL - EXIT TO MSG
         B     CONSOLE             SUCCESS - WE ARE DONE
         EJECT
***********************************************************************
**********                EOJ    ( CLOSE FILES )            ***********
***********************************************************************
         SPACE 1
EOJ      EQU   *                   ENTRY - EOJ FUNCTION
CLOSINTP CLI   TPINOPSW,X'FF'      INTP OPEN ?
         BNE   CLOSOTTP            NO
         CLOSE (INTPDCB,DISP)
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
CLOSITSW BC    15,CLOSOTTP         BR TILL RUN FUNC FOR TAPEIN
         MVI   CLOSITSW+1,X'F0'    RESTORE SW TO A BR
         MVI   TPINOPSW,X'00'      RESET TAPEIN OPEN SW TO CLOSED
         B     CONSOLE             GET NEXT FUNCTION
CLOSOTTP CLI   TPOTOPSW,X'FF'      OTTP OPEN ?
         BNE   CLOSDKIN            NO
         CLOSE (OTTPDCB,DISP)
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
CLOSOTSW BC    15,CLOSDKIN         BR TILL RUN FUNC FOR TAPEOUT
         MVI   CLOSOTSW+1,X'F0'    RESTORE SW TO A BR
         MVI   TPOTOPSW,X'00'      RESET TAPEOUT OPEN SW TO CLOSED
         B     CONSOLE             GET NEXT FUNCTION
CLOSDKIN CLI   DKINOPSW,X'FF'      DKIN OPEN ?
         BNE   CLOSCDIN            NO
         CLOSE DSKDCB
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
CLOSDKSW BC    15,CLOSCDIN         BR TIL NOPD BY OPEN NOT DASD RTN
         MVI   CLOSDKSW+1,X'F0'    RESTORE SW TO A BR
         B     CONSOLE             GET ANOTHER FUNC AFTER NOT DASD RTN
CLOSCDIN CLI   CDINOPSW,X'FF'      CDIN OPEN ?
         BNE   CLOSCDOT            NO
         CLOSE CRDDCB
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
CLOSCDSW BC    15,CLOSCDOT             BR TILL RDR HDWR EOF IN CNSL MOD
         MVI   CLOSCDSW+1,X'F0'        RESTORE SW TO A BR
         MVI   CDINOPSW,X'00'          RESET OP SW TO OFF
         BR    R8                      RETURN TO FUNC THAT BR TO CDRD
CLOSCDOT CLI   CDOTOPSW,X'FF'      CDOT OPEN ?
         BNE   CLOSPRTR            NO
         CLOSE PCHDCB
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
CLOSPRTR CLOSE PRTRDCB
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         LA    RD,SAVEAREA             PRIME   R13 BEFORE RETURN
         L     RD,4(RD)
         RETURN (14,12),RC=0
         EJECT
***********************************************************************
**********                                                       ******
********** I/O ROUTINES ( QSAM FOR U/R - EXCP FOR TAPE & DISK )  ******
**********                                                       ******
***********************************************************************
         SPACE 1
***********************************************************************
**********           CONSOLE - WRITE & READ REPLY  (WTOR)   ***********
***********************************************************************
WRTCONP  MVI   P,0                 TURN OFF SW - DONT USE STRING PARAMS
WRTCON   CLI   P,0                 USING STRING PARAMS
         BE    WRTCON1             NO,
         MVC   CONIO(10),P1        YES, MOVE PARAM INTO READ AREA
         MVC   P1(40),P2           MOVE PARAMS UP ONE
         IC    R0,P                DECREMENT
         BCTR  R0,R0                PARAMETER COUNTER
         STC   R0,P
         OC    CONIO(10),BLANKS
         BR    R8                  RETURN
WRTCON1  EQU   *
         TM    IRPTECB,X'40'           HAS WTOR REPLY BEEN MADE ?
         BO    CHKIRPT
         MVC   CONIOMSG(60),CONIO      PRIME MSG AREA
         ST    R0,SAVECCWA         SAVE R0 BEFORE WTOR
         MVC   CONIO(80),BLANKS        CLEAR REPLY AREA
         MVI   CONIOECB,X'00'          CLEAR ECB
         LH    R1,MSGLNGTH             ADD 4 TO MSGLNGTH
         L     R0,WTORDESC                 ( LOAD DESC & ROUTCDE . . .
         ST    R0,CONIOMSG(R1)             USE R1 AS BASE REG BEFORE+4)
         LA    R1,4(R1)                TO ALLOW FOR MSGLNGTH ADCON,FLAG
         STH   R1,MSGLNGTH             STORE RESULT BACK
***************  COMMON WTOR ROUTINE  *********************************
WTOR     WTOR  '60 CHAR.S-012345678901234567890123456789012345678901234X
               56789',CONIO,80,CONIOECB,DESC=6,ROUTCDE=2
WTORDESC EQU   *-6                 USE TO ADDRESS DESC & ROUTCDE
REPLNGTH EQU   WTOR+4              REPLY LENGTH
MSGLNGTH EQU   WTOR+12             MSG   LENGTH
CONIOMSG EQU   WTOR+16             MSG AREA
***********************************************************************
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         WAIT  ECB=CONIOECB            WAIT FOR REPLY
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         L     R0,SAVECCWA         RESOTRE R0 AFTER WTOR
         OC    CONIO(80),BLANKS        FORCES  UPPER CASE IN REPLY AREA
         MVI   P,X'00'             SET PARAM STRING COUNTER = 0
         BR    R8
         EJECT
***********************************************************************
**********               CONSOLE - WRITE ONLY  (WTO)        ***********
***********************************************************************
WCON2    EQU   *
         TM    IRPTECB,X'40'           HAS WTOR REPLY BEEN MADE ?
         BO    CHKIRPT
         ST    R0,SAVECCWA         SAVE R0 BEFORE WTOR
         MVC   WTOCONIO(60),CONIO      PRIME MSG AREA
         LH    R1,MSGLNGTH             ADD 4 TO MSGLNGTH
         L     R0,WTODESCR                 ( LOAD DESC & ROUTCDE ...
         ST    R0,WTOCONIO(R1)             USE R1 AS BASE REG BEFORE+4)
         LA    R1,4(R1)                TO ALLOW FOR MSGLNGTH ADCON,FLAG
         STH   R1,WTOLNGTH             ST RESULT IN WTO LVE MSG INTACT
         MVC   WTOCONIO,CONIO          PRIME WTO MSG AREA
***************  COMMON WTO  ROUTINE  *********************************
WTO      WTO   '60 CHAR.S 012345678901234567890123456789012345678901234X
               56789',DESC=6,ROUTCDE=2
WTODESCR EQU   *-6                 USE TO ADDRESS DESC & ROUTCDE
WTOLNGTH EQU   WTO+4               MSGLNGTH
WTOCONIO EQU   WTO+8               MSG AREA
***********************************************************************
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         L     R0,SAVECCWA         RESTORE R0 AFTER WTO
         BR    R8                      RETURN
         EJECT
***********************************************************************
**********                   CARD READING    (QSAM)         ***********
***********************************************************************
CDRD     EQU   *
         TM    IRPTECB,X'40'           HAS WTOR REPLY BEEN MADE ?
         BO    CHKIRPT
         CLI   CDINOPSW,X'FF'          OPEN ?
         BE    GETCRDSW
         BAL   R9,OPENCRD
GETCRDSW BC    0,CDEOFSW1          SET TO BR IF CTL CD MODE & NO /+,EOJ
         LA    RD,SAVEAREA             PRIME R13 BEFORE BALR MACRO
         GET   CRDDCB,CONIO
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
CDEOFSW  BC    0,CDRET                 NOP IF CTL MODE, BR IF CONSOLE
         CLC   CONIO(2),=C'/+'         END OF DATA CARDS IN CTL MODE ?
         BNE   CDRET                   NO
CDEOFSW1 MVI   EOF,X'FF'               SET EOF SW ON
CDRET    BR    R8                      RETURN
EODADCK  EQU   *
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         CLI   CNTRLSW,X'FF'       ARE WE IN CTL CRD MODE & EOD HIT ?
         BNE   EODADCK1            NO, SKIP SW SET TO AVOID LOOP, WHEN
         MVI   GETCRDSW+1,X'F0'    YES.    NO /+ OR $$DITTO EOJ SUBMT'D
EODADCK1 MVI   EOF,X'FF'           BR HERE TO SKIP SW SET IN OPR MODE
         MVI   CLOSCDSW+1,X'00'        SET SW TO FALL THRU AFTER CLOSW
         B     CLOSCDIN                GO CLOSE AT HDWR EOF
         EJECT
***********************************************************************
**********                   CARD PUNCHING   (QSAM)         ***********
***********************************************************************
CDPN     EQU   *
         TM    IRPTECB,X'40'           HAS WTOR REPLY BEEN MADE ?
         BO    CHKIRPT
         CLI   CDOTOPSW,X'FF'          OPEN ?
         BE    PUTCRD
         OPEN  (PCHDCB,(OUTPUT))
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         MVI   CDOTOPSW,X'FF'
PUTCRD   EQU   *
         LA    RD,SAVEAREA             PRIME R13 BEFORE BALR MACRO
         PUT   PCHDCB,CONIOI
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         BR    R8
         EJECT
***********************************************************************
**********           PRINTING & CARRIAGE CONTROL  (QSAM)    ***********
***********************************************************************
EDIT     TR    CONIO(132),TRTBL    TRANSLATE UNPRINTABLE CHAR FOR MAX
PRNT     EQU   *
         TM    IRPTECB,X'40'           HAS WTOR REPLY BEEN MADE ?
         BO    CHKIRPT
         CLI   TYP,X'00'           PRNT SPPED.
         BNE   0(R8)               EXIT IF SUPPRESS PRINTING
         SR    R1,R1
         IC    R1,CC                   CCW OP CODE
         SRL   R1,3(R0)                TRANSLATE TO # OF PRINTLINES
         AH    R1,LINCT                ADD   TO COUNTER
         STH   R1,LINCT
         IC    R1,PL                   GET PRINT LENGTH
         BCTR  R1,R0               DECREMENT R1 BY 1
         STC   R1,PR1+1                STORE COUNT LENGTH IN MOVE
         MVI   PRTIO,C' '
         MVC   PRTIO+1(131),PRTIO      CLEAR PRINTLINE
         CLI   INPUT,X'FF'             SUPPRESS SKIP TO CHAN 1
         BE    PR1
         CLI   LINCT+1,X'3A'           AT END OF PAGE ? (58 LINES/PAGE)
         BNH   PR1                     NO
         MVI   LINCT+1,X'01'           YES, RESET LINCT
         CLI   CC,X'8B'                IS THIS SKIP TO CHAN 1 ?
         BE    PR1BYPAS
         CLI   CC,X'13'                IF SPACE IMMED. BYPASS DATA MOVE
         MVI   CC,X'89'                SET TO WRITE & SKIP TO CHAN 1
         BE    PR1BYPAS
PR1      MVC   PRTIO,CONIO         MOVE DATA TO BUFFER
PR1BYPAS EQU   *
         LA    RD,SAVEAREA             PRIME R13 BEFORE BALR MACRO
         PUT   PRTRDCB,CC
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
PRNTEXIT BR    R8                      RETURN
         OI    PRNTEXIT+1,X'F0'
         MVI   CC,X'11'                DOUBLE SPACING
         BR    R8                      RETURN
OVFLO    EQU   *
         MVI   CC,X'8B'                SKIP TO CH 1
         NI    PRNTEXIT+1,X'0F'
         MVI   LINCT+1,X'AA'       FORCE LINE COUNT TO BE RESET
         B     PRNT
         EJECT
***********************************************************************
**********                   OUTPUT TAPE   (EXCP)           ***********
***********************************************************************
TPOT     EQU   *
         TM    IRPTECB,X'40'           HAS WTOR REPLY BEEN MADE ?
         BO    CHKIRPT
         ST    R4,SAVREGS+24       SAVE R4 BEFORE USE AS DSCT REG
         CLI   OTONINSW,X'FF'          OUTPUT OPER ON INPUT DCB ?
         BE    TPOTONIN                YES
         LA    R4,OTECBIOB         SET R4 WITH ECB/IOB OF TAPEOUT
         USING DSCTAPE,R4          SET ADDRESSABILITY REL. TO DSECT
         B     TPOTNORM            GO USE TAPEOUT ECB/IOB
TPOTONIN EQU   *
         LA    R4,ITECBIOB         SET R4 WITH ECB/IOB OF TAPEIN
         MVC   SAVECCWA,INTPCCWA       SAVE INTPCCWA
         MVC   INTPCCWA,OTTPCCWA
TPOTNORM LA    R1,DSCTIOB              PRIME R1 FOR EXCP
         MVI   DSCTECB,X'00'           CLEARECB
         EXCP  (R1)
         WAIT  ECB=DSCTECB
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         NI    OTEOR,0                 RESET EOR SWITCH
         CLI   OTONINSW,X'FF'          OUTPUT OPER ON INPUT DCB ?
         BNE   SKOTONIN
         MVC   INTPCCWA,SAVECCWA       RESTORE INTPCCWA
SKOTONIN EQU   *
         MVC   DSCTIOB(2),IOBFLG12     RESTORE IOB AFTER EXCP
         CLI   DSCTECB,X'7F'           CHECK FOR NORMAL COMPLETION
         BE    TPOTRET                 YES, RETURN
         CLI   DSCTECB,X'44'           PREVIOUS CCW HAD PERMANENT ERR ?
         BE    TPOTRTRY                YES, GO CHECK FOR TM OR LOAD PT
         CLI   DSCTECB,X'41'           HAS CURRENT CCW HAD PERM. ERROR
         BNE   ABEND300                ADD MSG LATER
         TM    DSCTCSW+3,X'01'         EOR ON WRITE ?
         BNO   ABEND300                UNUSUAL TAPE ERROR
         OI    OTEOR,1                 SET EOR FOR OT
TPOTRET  EQU   *                   COMMON RETURN TO ALLOW R4 RESTORE
         L     R4,SAVREGS+24       RESTORE R4 AFTER USE AS DSCT REG
         BR    R8
TPOTRTRY TM    DSCTCSW+3,X'01'         TM ON FSR, BSR ?
         BO    TMBSRFSR                YES, ISSUE MSG, RETRY CCW
         TM    DSCTCSW+3,X'02'     BSR OR BSF HIT LOAD PT ?
         BO    LPBSRBSF                YES, ISSUE MSG, RETURN TO FUNC
         DROP  R4                  DROP R4 ADDRESSING AT THIS POINT
ABEND300 EQU   *
         MVC   UCODE(3),U300       MOVE IN 300 - UNUSUAL TAPE I/O ERROR
         B     ABEND               BR TO ABEND ( HAND CODED )
TMBSRFSR EQU   *                       ISSUE MSG
         LA    R1,TPOT             SET R1 TO RE-ISSUE INTERCEPTED EXCP
         MVC   CONIO(10),TAPMRK    MOVE IN 'TAPE MARK '
         MVC   CONIO+10(36),BSRMSG     PRIME BSR OR FSR MSG
         B     CKIFCNSL            CHECK FOR CONSOLE OR CONTROL MODE
LPBSRBSF EQU   *                       ISSUE MSG
         LR    R1,R8               SET R1 TO RETURN TO FUNC AFTER MSG
         SR    R5,R5               SET NBLKS CT TO 0 FOR FUNCTION END
         LA    R5,1(R5)            SET R5 NBLKS TO 1 FOR BCT LOOP
         MVC   CONIO(10),=C'LOAD POINT'
         MVC   CONIO+10(36),BSRMSG     PRIME BSR OR BSF MSG
         MVC   CONIO+23(3),BSFDC       MOVE IN BSF
         MVC   CONIO+37(9),COMPLDC     'COMPLETE'
CKIFCNSL ST    R8,SAVREGS          SAVE R8 BEFORE BAL
         L     R4,SAVREGS+24       RESTORE R4 AFTER USE AS DSCT REG
         ST    R1,SAVREGS+4        SAVE R1 BEFORE MSG
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    OPERMSG             YES
PGMRMSG  EQU   *                   NO
         MVI   PL,X'2E'            SET PRINT LENGTH
         MVI   CC,X'19'            SET CAR CTL
         BAL   R8,PRNT             PRINT MSG TO PGMR
         L     R8,SAVREGS          RESTORE R8 AFTER BAL
         L     R1,SAVREGS+4        RESTORE R1 AFTER MSG
         BR    R1                  RETURN TO FUNCTION OR TPOT
OPERMSG  MVI   WCNCCW+7,X'2E'      SET MSG LENGTH
         BAL   R8,WCON2            PRINT MSG TO OPER
         L     R8,SAVREGS          RESTORE R8 AFTER BAL
         L     R1,SAVREGS+4        RESTORE R1 AFTER MSG
         BR    R1                  RETURN TO FUNCTION OR TPOT
         EJECT
***********************************************************************
**********                    INPUT TAPE   (EXCP)          ************
***********************************************************************
INTP     EQU   *
         TM    IRPTECB,X'40'           HAS WTOR REPLY BEEN MADE ?
         BO    CHKIRPT
         ST    R4,SAVREGS+24       SAVE R4 BEFORE USE AS DSCT REG
         CLI   INFMOTSW,X'FF'          INPUT OPER FROM TAPEOUT  DD ?
         BE    INTPFMOT                YES
         LA    R4,ITECBIOB         SET R4 WITH ECB/IOB OF TAPEIN
         USING DSCTAPE,R4          SET ADDRESSABILITY REL. TO DSECT
         B     INTPNORM            GO USE TAPEIN FOR I/P
INTPFMOT EQU   *
         LA    R4,OTECBIOB         SET R4 WITH ECB/IOB OF TAPEOUT
         MVC   SAVECCWA,OTTPCCWA       SAVE OTTPCCWA
         MVC   OTTPCCWA,INTPCCWA       PRIME OTTPCCWA FOR INPUT OPER
INTPNORM LA    R1,DSCTIOB              PTR TO IOB
         MVI   DSCTECB,X'00'           CLEAR ECB
         MVI   TMSW,X'00'              CLEAR TM SWITCH
         EXCP  (R1)
         WAIT  ECB=DSCTECB             WAIT ON ECB
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         MVC   TPLNTH+2(2),DSCTCSW+5 SAVE RESIDUAL COUNT
         CLI   INFMOTSW,X'FF'
         BNE   SKINFMOT
         MVC   INTPCSW+5(2),OTTPCSW+5  MOVE RESIDUAL CNT TO INTPCSW
         MVC   OTTPCCWA,SAVECCWA       RESTORE OTTPCCWA
SKINFMOT EQU   *
         MVC   DSCTIOB(2),IOBFLG12     RESTORE IOB AFTER EXCP
         CLI   DSCTECB,X'7F'           CHK FOR NORMAL COMPL
         BE    INTPRET                 YES, RETURN
         CLI   DSCTECB,X'41'       CHECK FOR PERM. I/O ERROR
         BE    TSTFRTM             GO TEST FOR TAPE MARK
         CLI   DSCTECB,X'44'       DID PREVIOUS CCW HAVE PERM. ERROR?
         BE    INTPNORM            RETRY INTERCEPTED EXCP
         B     ABEND300            OTHERWISE, UNUSUAL COND, ABEND
TSTFRTM  TM    DSCTCSW+3,X'01'         EOF ?
         BNO   INTPERR                 NO , CONTINUE INTP
         DROP  R4                  DROP R4 ADDRESSING AT THIS POINT
SETTMSW  MVI   TMSW,X'FF'              SET TAPE MARK SWITCH
INTPRET  EQU   *                   COMMON RETURN TO ALLOW RESTORE OF R4
         L     R4,SAVREGS+24       RESTORE R4 AFTER USE AS DSCT REG
         BR    R8                      RETURN TO FUNCTION
INTPERR  LA    R1,INTPRT           YES, SET RETURN ADDRESS
         L     R4,SAVREGS+24       RESTORE R4 AFTER USE AS DSCT REG
         STM   R1,RE,SAVREGS           SAVE REGS
         B     TPERR               GO HANDLE ERROR
INTPRT   B     INTP                RETURN HERE IF RECORD BYPASSED
         BR    R8                  RETURN HERE IF RECORD IGNORED
         DS    0F
TPLNTH   DC    F'0'                SAVE RESIDUAL CNT
         EJECT
***********************************************************************
**********             DISK - READ OR WRITE  (EXCP)         ***********
***********************************************************************
         SPACE 1
DISK     EQU   *
         TM    IRPTECB,X'40'           HAS WTOR REPLY BEEN MADE ?
         BO    CHKIRPT
         LA    R1,DSKIOB
         MVI   DSKECB,X'00'            CLEARECB
         MVC   DSKCC(5),LOWLIM+2   PRIME IOB DSK ADRRESS - CCHHR
         EXCP  (R1)
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         WAIT  ECB=DSKECB
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         CLI   DSKECB,X'7F'        OK ?
         BE    DSKRET              YES, RETURN
         CLI   DSKECB,X'41'        PERMANENT ERROR POSTED ?
         BE    CHK41               MAY BE NO REC FND OR EOE, EOF
         CLI   DSKECB,X'42'        DSK ADRESS OUT OF DEB LIMIT?
         BE    CHKEOE              ELSE ABEND - UNUSUAL I/O ERROR
         B     ABEND400
CHK41    TM    DSKCSW+3,X'02'      UNIT CHK ? MAY BE NO REC FND
         BO    CHKNOREC
         TM    DSKCSW+3,X'01'      UNIT EXCEPTION ?  MAY BE EOF OR EOE
         BO    SETDKEOF            YES MAY WANT TO SET EOF SW
         B     ABEND400
CHKEOE   EQU   *                   NO CHECK EOE  ABEND FOR NOW
         MVC   CONIO(7),NWTPMSG
         MVC   CONIO+7(15),DSMSG
         MVC   CONIO+22(24),DSEMSG     ', NEXT IS AVAIL. USE DSE'
         SR    R0,R0               CLEAR
         SR    R1,R1               CLEAR
         IC    R0,EXTNTCNT         GET NO OF EXTENTS
         IC    R1,DSKM             PICK UP CURR. XTNT NO.
         LA    R1,1(R1)            BUMP BY 1
         CLR   R0,R1               ARE WE DONE ?
         BE    EOENOMOR            YES
         STC   R1,DSKM             PUT UPDATED M BACK - MAKE AVAIL.
         L     R1,DKDEBADR         GET DASD DEB SECTION PTR
         LA    R1,16(R1)           UPDATE TO NEXT SECTION
         ST    R1,DKDEBADR         PUT BACK - MAKE AVAIL.
         B     EOERET              RETURN, NOW THAT EOE INCREMENTED
EOENOMOR EQU   *
         MVC   CONIO+24(7),=C'NO MORE'
EOERET   EQU   *
         LA    R1,CONSOLE
         B     CKIFCNSL            GO WRT MSG TO OPER/PGMR
CHKNOREC TM    DSKS1,X'08'         NO REC FOUND POSTED IN SENSE BYTE1 ?
         BO    DSKRET              YES, RETURN - DISK FUNC WILL HANDLE
ABEND400 EQU   *
         MVC   UCODE(3),U400       MOVE IN 400 - UNUSUAL DISK I/O ERROR
         B     ABEND               BR TO ABEND ( HAND CODED )
SETDKEOF EQU   *
         CLI   CNTRLSW,X'00'       CONSOLE OPERATION ?
         BE    DSKRET              DO NOT SET DSK EOF IN CONSOLE MODE
         MVI   DSKEOFSW,X'FF'      SET TO ON COND.
DSKRET   EQU   *
         MVC   DSKFLG1(2),IOBFLG42     SET IOB TO NO REL. CCW & COM. CH
         BR    R8                  RETURN
         EJECT
***********************************************************************
**********               CHANNEL COMMAND WORDS              ***********
***********************************************************************
         SPACE 1
         CNOP  0,8
RCNCCW   EQU   REPLNGTH-7              ADJUST FOR OS
WCNCCW   EQU   MSGLNGTH-6              ADJUST FOR OS  HAND-CODED WTOR
MSICCW   CCW   X'00',DTPIO,X'40',1     MODE SET    INPUT TAPE CCW
INTPCCW  CCW   X'02',DTPIO,X'20',1     READ TAPE CCW
ERTCCW   CCW   X'03',*,X'40',1         ERT TAPE CCW LIST  - NOP CTL CCW
         CCW   X'1F',*,X'40',1         WTM CONTROL CCW
         CCW   X'17',*,X'40',1         ERG CONTROL CCW
         CCW   X'97',*,X'40',1         ERASE LONG CONTROL CCW-3420 ONLY
         CCW   X'07',*,X'20',1         REW CONTROL CCW
MSOCCW   CCW   X'00',DTPIO,X'40',1     MODE SET   OUTPUT TAPE CCW
OTTPCCW  CCW   X'01',DTPIO,X'20',1     WRITE TAPE CCW (SET SLI BIT )
SHCCW    CCW   X'31',DSKCC,X'40',5     SEARCH ID EQ DISK CCW
TIC      CCW   X'08',*-8,X'00',1       TIC CCW
RWCCW    CCW   X'1E',DTPIO,X'20',4001  READ/WRITE DISK CCW
SRCHA    CCW   X'39',DSKCC,X'40',4     SEARCH HA EQ DISK CCW
TIC2     CCW   X'08',*-8,X'00',1       TIC CCW
HACCW    CCW   X'1A',DTPIO,X'40',5     READ HA CCW
ROCCW    CCW   X'16',DTPIO+5,X'00',16  READ R0 CCW
         EJECT
***********************************************************************
**********                   CONSTANTS                      ***********
***********************************************************************
         SPACE 1
DSL      DC    AL4(NBUFPAGS*4096)      LIMIT TO ACTUAL NO. OF PAGES
*                                      NEEDED TO READ INSTALLATION
*                                      BLKSIZES. THE RIGHT HW (DSL+2)
*                                      IS PLACED IN CCW BYTE COUNT
*                                      FIELD DURING DISK/TAPE I/O.
EIGHTY   DC    X'0050'
LINCT    DC    X'0000'
BLCT     DC    CL2' '
HBF      DC    X'000000'
ON       DC    X'FF'
DCYL     DC    CL8'CYL  XXX'
DHED     DC    CL8'HEAD  XX'
DREC     DC    CL10'REC  XXXXX'
LBKEY    DC    C'# '               USE 'KEY' OF NEXT
KEYH     DC    CL10'KEY  XXXXX'
LBDATA   DC    C'# '               USE 'DATA' OF NEXT
DATB     DC    CL10'DATA XXXXX'
DATH     DC    CL10'DATA XXXXX'
COMMABLK DC    C', '               USED IN MSG
TBLK     DC    CL10'BLOCKXXXXX'
IN       DC    CL6'INPUT='
OT       DC    CL7'OUTPUT='
ENTCCCHH DC    C'ENTER CCCHH WITHIN '        'DATA SET ...
DSMSG    DC    C'DATA SET EXTENT'
BEGINMSG DC    C'CCCHH'            ' - BEGIN' USED FROM NEXT MSG
DASHBEG  DC    C' - '              'BEGIN=CCCHHH,END=' USED FROM NEXT
BG       DC    CL6'BEGIN='
         DC    C'CCCHH,'           FILL AND COMMA
EN       DC    CL4'END='
NB       DC    CL6'NBLKS='
BLOCKMSG DC    C'FIRST BLOCK OR NOT MULTIPLE OF '    USE 'RECSIZE' NXT
RS       DC    CL8'RECSIZE='
BF       DC    CL10'BLKFACTOR='
DT       DC    CL9'DECKTYPE='
DN       DC    CL9'DECKNAME='
ZEROS    DC    CL5'00000'
ENTERMSG DC    C'ENTER: '              USE 'XXX - ' OF NEXT MSG
XXXDASH  DC    C'XXX - '               USE 'TO LIST FUNCTIONS' OF NEXT
         DC    C'TO LIST FUNCTIONS'
BSRMSG   DC    C' - ON BSR OR FSR, '   FUNCMSG WILL BE USED FROM NEXTDC
FUNCMSG  DC    C'FUNCTION '        USE 'CONTINUES' OF NXT MSG
CONTMSG  DC    C'CONTINUES'
DVTYPMSG DS    0CL1                USE 'TAPEIN NOT TAPE DEVICE'
TPINDD   DC    CL8'TAPEIN  '           DDNAMES TO BE SUPPLIED ON CTL CD
         DC    C'NOT TAPE '
DEVH     DC    CL22'DEVICE  XXX  TAPEIN   '
         DC    CL26'MODE XX  X TRACK   800 BPI'
BYTMSG   DC    CL28' BYTES TO BE CHANGED, (1-35)'
SDPMSG   DC    CL29'STARTING DATA POSITION IN REC'
DSKH     DC    CL24'CYLINDER XXX, HEAD  X,  '
BPTRK    DC    CL48'DEFECTIVE 231X PRIMARY TRACK, ALTERNATE TRACK IS'
         DC    CL18' CYL XXX, TRACK  X'
GATRK    DC    CL50'OPERATIVE 231X ALTERNATE TRACK, DEFECTIVE TRACK WA'
         DC    CL19'S CYL XXX, TRACK  X'
DET1     DC    CL6'CHAR  '
DET2     DC    CL6'ZONE  '
DET3     DC    CL6'NUMR  '
CTL      DC    CL8'$$DITTO '
DCKMSG   DC    CL18'DECK NAME X CHARS.'
INVLDMSG DS    0CL8                USE 'INVALID ' OF NEXT MSG
BADCUUMM DC    C'INVALID '         'CUUMM' OF NEXT MSG IS USED ALSO
SYSOTMSG DC    C'CUUMM - OUTPUT '      'TAPE' OF NEXT
TAPMRK   DC    CL10'TAPE MARK '
SYSITMSG DC    C'CUUMM - '            USE 'INPUT TAPE' OF NEXT MSG
TAPERR   DC    C'INPUT TAPE I/O ERROR'
SYSDSK   DC    CL10'CUU - DISK'
BLKMSG   DC    C'#'                USE ' BLKS' OF NEXT MSG
BLKTMSG  DC    C' BLKS'            USE ' AND T.M. COPIED' OF NEXT MSG
TMCOPIED DC    C' AND T.M. COPIED'
RECADDRS DC    CL23'CCC-HH-RRR  REC ADDRESS'
SCANMSG  DC    CL31'SCAN KEY - K, DATA - D, EOF - E'
DESIRED  DC    CL11'DESIRED REC'
DIRECT   DC    CL24'COPY FWD OR BACK, F OR B'
FILNUM   DC    CL7'# FILES'
TPERRMSG DC    CL50'BYPASS REC - B, IGNORE ERROR - I, USER CORRECT - C'
NOREC    DC    CL20'NO DISK RECORD FOUND'
NVOLMSG  DC    CL17'ENTER NEW VOL IS '
TOLNGTH  DC    C' TO '             USE 'LENGTH' OF NEXT MSG
LNGTHMSG DC    C'LENGTH EXCEEDS END OF'  USE' BLOCK'  OF NEXT MSG
BADLTH   DC    C' BLOCK EXCEEDS BUFFER, TO INCREASE SIZE - '
         DC    C'RE-ASSEMBLE'
LGRCDMSG DC    C' LOGICAL '        USE 'RECORDS' OF NEXT MSG
RCDMSG   DC    C'RECORDS AT'
ALTERMSG DC    C'ALTERED DISK RECORD IN CORE'
ALTRMSG  DC    CL26'ALTER IN HEX - H, CHAR - C'
ALTCT    DC    CL31'ENTER XX BYTES, X CHAR PER BYTE'
RECSIZ   DC    C'LOGICAL REC '     USE 'LNTH' OF NEXT MSG
LNTHMSG  DC    C'LNTH'             USE 'SCAN ARGUMENT (1-35)' OF NEXT
SCANMSG2 DC    C' SCAN ARGUMENT (1-35)'    ' SCAN' PORTION USED
NWTPMSG  DC    C'END OF REEL, MOUNT NEW '    USE 'TAPE' OF NEXT MSG
TLMSG    DC    CL19'TAPE NOT CARD IMAGE'
CHNGMSG  DC    C'CHANGES '         USE  ' COMPLETE ' OF NEXT MSG
COMPLDC  DC    C'COMPLETE '        USE '?, Y OR N' OF NEXT MSG
YORNMSG  DC    C'?, Y OR N'
XX       DC    CL6'XXXXXX'
INTPERSW DC    X'00'                   CONTROLS OPER INTP ERROR RTN
IOBFLG12 DC    X'0200'             USE TO RESET IOB FLG 1&2 AFTER ERROR
IOBFLG42 DC    X'4200'             USE TO SET DKIOB  TO COMM CHAINING
TPINOPSW DC    X'00'                   OPEN SWITCHES FOR OPEN CONTROL
TPOTOPSW DC    X'00'                   OPEN SWITCHES FOR OPEN CONTROL
CDINOPSW DC    X'00'                   OPEN SWITCHES FOR OPEN CONTROL
CDOTOPSW DC    X'00'                   OPEN SWITCHES FOR OPEN CONTROL
DKINOPSW DC    X'00'                   OPEN SWITCHES FOR OPEN CONTROL
TPOTDD   DC    CL8'TAPEOUT '           DDNAMES TO BE SUPPLIED ON CTL CD
DSKINDD  DC    CL8'DISKIN  '           DDNAMES TO BE SUPPLIED ON CTL CD
IRPTMSG  DC    C'REPLY: '
IRPTDC   DC    C'IRPT'
         DC    C' - TO INTERRUPT A FUNCTION, EOJ - '
         DC    C'TO END '          USE 'DITTO' OF NEXT MSG
DITTODC  DC    C'DITTO '
CTLERROR DC    C'CONTROL CARD ERROR, '
XXXMSG   DC    C'REFER TO ''XXX'' LISTING'
DSEMSG   DC    C', NEXT IS AVAIL. '
USEDSE   DC    C'USE DSE'          BLANKED OUT IN CTL CRD MODE
         DS    0H
UPPERC   DC    AL2(202)                INITIALLY 2311/2314
UPPERH   DC    AL2(09)                 INITIALLY 2311
USERNAME DC    CL10'IBM  S/370'    USED IN INT FUNCTION
CNTRLSW  DC    X'FF'               SW FOR CONTOL CARD MODE-SET ON NOW
DPSW     DC    X'00'               HEX DUMP SW
EXTNTCNT DC    X'0'                SAVE DEB EXTENT CNT HERE
TMSW     DC    X'00'               TAPE MARK SW SET WHEN T.M. READ
TRK7OR9  DC    X'00'               X'07' = 7-TRK, X'09' = 9-TRK
         DS    0F
CONIOECB DC    F'0'                    WTOR ECB FOR REPLY
IRPT     DC    CL4' '
IRPTECB  DC    F'0'
DKDEBADR DC    F'0'                SAVE DEB PTR
DATESAVE DS    CL6                 MACHINE DATE FOR TITLE PAGE YY.DDD
H16      DC    H'16'                   HALFWORD OF 16 TO INCR DEB
H8       DC    H'8'                    HALF WORD OF 8 TO INCR CCW LNTH
******************* DEVTYP MACRO INFOR. FOR TAPEIN,TAPEOUT, DISKIN ****
         DS    0F
TIALLOSW DC    X'00'               SWITH=X'FF' WHEN TAPEIN  ALLOCATED
TIALLUCB DC    C'***'              TAPEIN  UCB FROM TIOT
TOALLOSW DC    X'00'               SWITH=X'FF' WHEN TAPEOUT ALLOCATED
TOALLUCB DC    C'***'              TAPEOUT UCB FROM TIOT
ITMODE   DC    C' '
ITUCB    DC    CL3' '                  UCB SAVE
ITDEVTYP DS    0F
         DC    CL1' '
ITTRK    DC    CL1' '      00-9TRK,20-9TRK(D.D.),80-7TRK,C0-7TRK(D.C.)
ITCLASS  DC    CL1' '
IT3420   DC    CL1' '      03-3420
ITBLKSIZ DC    F'0'                    MAX BLKSIZE FOR DEV PLACED HERE
         DS    0F
OTMODE   DC    C' '
OTUCB    DC    CL3' '                  UCB SAVE
OTDEVTYP DS    0F
         DC    CL1' '
OTTRK    DC    CL1' '      00-9TRK,20-9TRK(D.D.),80-7TRK,C0-7TRK(D.C.)
OTCLASS  DC    CL1' '
OT3420   DC    CL1' '      03-3420
OTBLKSIZ DC    F'0'                    MAX BLKSIZE FOR DEV PLACED HERE
         DS    0F
         DC    C' '
DKUCB    DC    CL3' '                  UCB SAVE
DKDEVTYP DS    0F
DSKSTAT  DC    CL1' '
         DC    CL1' '
DSKCLASS DC    CL1' '
DSKTYPE  DC    CL1' '
DKBLKSIZ DC    F'0'                    MAX BLKSIZE FOR DEV PLACED HERE
$$TYPE   DC    CL4'    '
         EJECT
***********************************************************************
         LTORG                     FORCE LITERALS TO ORIGIN HERE
***********************************************************************
         EJECT
***********************************************************************
**********                   QSAM DCB'S                     ***********
***********************************************************************
         SPACE 1
PRTRDCB  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FM,LRECL=133,   X
               BLKSIZE=133
         EJECT
CRDDCB   DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=EODADCK
         EJECT
PCHDCB   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPUNCH,                      X
               RECFM=FA,BLKSIZE=81
         EJECT
***********************************************************************
**********                   EXCP DCB'S                     ***********
***********************************************************************
         SPACE 1
DSKDCB   DCB   MACRF=E,DDNAME=DISKIN,DSORG=PS,DEVD=DA
         EJECT
INTPDCB DCB DDNAME=TAPEIN,DSORG=PS,MACRF=E,DEVD=TA
         EJECT
OTTPDCB  DCB   DDNAME=TAPEOUT,MACRF=E,DSORG=PS,DEVD=TA
         EJECT
***********************************************************************
ITECBIOB DS    0F                  TAPEIN  ECB & IOB
INTPECB  DC    F'0'                    ECB
INTPIOB  DS    0F
         DC    X'0200'                 NO OTHER RELATED CCW'S
INTPSENS DC    H'00'
         DC    X'7F'
         DC    AL3(INTPECB)
INTPFLG  DC    X'00'
INTPCSW  DC    XL7'00'
INTPCCWA DC    A(INTPCCW)              CCW IS 16 BYTES FROM IOB
INTPDCBA DC    A(INTPDCB)
         DC    F'0'
         DC    H'0'                ZERO WHEN NOT USED BY EXCP PGMR
         DC    H'0'
***********************************************************************
OTECBIOB DS    0F                  TAPEOUT ECB & IOB
OTTPECB  DC    F'0'
OTTPIOB  DS    0F
         DC    X'0200'
OTTPSENS DC    H'00'
         DC    X'7F'
         DC    AL3(OTTPECB)
OTTPFLG  DC    X'00'
OTTPCSW  DC    XL7'00'
OTTPCCWA DC    A(OTTPCCW)
OTTPDCBA DC    A(OTTPDCB)
         DC    F'0'
         DC    H'0'                ZERO WHEN NOT USED BY EXCP PGMR
         DC    H'0'
***********************************************************************
DSKIOBAR DS    0F                  DISKIN ECB AND IOB
DSKECB   DC    F'0'
DSKIOB   DS    0F
DSKFLG1  DC    X'42'               NO RELATED CCW'S & COMM. CHAINING
DSKFLG2  DC    X'00'
DSKS0    DC    X'00'
DSKS1    DC    X'00'
         DC    X'00'
DSKECBA  DC    AL3(DSKECB)
DSKERFLG DC    X'00'
DSKCSW   DC    XL7'00'
DSKCCWA  EQU   *
DSKSIOCC DC    X'00'
         DC    AL3(SHCCW)
         DC    X'00'
DSKDCBA  DC    AL3(DSKDCB)
         DC    2F'0'
DSKM     DC    X'00'
DSKBB    DC    XL2'00'
DSKCC    DC    XL2'00'
DSKHH    DC    XL2'00'
DSKR     DC    X'00'
         EJECT
***********************************************************************
**********                 STORAGE AND EQUATES              ***********
***********************************************************************
         SPACE 1
HH1      DC    CL8'* * * * '       LEADING *'S IN HEADING
HH2      DS    CL117               HEADING BODY
HH3      DC    CL7'* * * *'        TRAILING *'S IN HEADING
BLANKS   DC    CL80' '             UTILITY BLANKS FOR CLEARING CRD AREA
ADRSLO   DS    CL5                 LOWER DISK ADDRESS
ADRSUP   DS    CL5                 UPPER DISK ADDRESS
INPUT    DS    CL1                 OVERFLOW SW
HNB      DS    CL4                 HOLD NBLKS
HRS      DS    CL5                 HOLD RECSIZE
HDG1     DS    CL26                HEADING BUFFER
HDG2     DS    CL26                HEADING BUFFER
HDG3     DS    CL26                HEADING BUFFER
DECKTYPE DS    CL3                 HOLD DECKTYPE
DECKNAME DS    CL8                 HOLD DECKNAME
CC       DS    CL1                 CARRIAGE CONTROL CHARACTER
PRTIO    DS    CL132               PRINT BUFFER
CP       DS    CL1                     ALLOW FOR PRINT CTL ON 3525
CONIO    DS    CL132               COMMON BUFFER FOR CONSOLE,CARD, ETC.
CONIOI   EQU   CONIO-1                 ALLOW FOR PRINT CTL ON 3525
HEXIO    EQU   CONIO+12            BEGIN HEX
DEVTYP   DS    CL1                 DEVICE TYPE INDICATOR SET BY LUBPUB
LISTSW   DS    CL1                 DP,DPD,SP,SPD LIST CTL
MVBEGIN  EQU   *                   BEGIN ZERO CLEAR
HOLD     DS    1D                  PACK AREA
HOLDSEQ  DS    1D                  PACK AREA
LOWLIM   DS    CL8                 BEGIN= LOWER CCCHH, ALSO BLKCNT LIM
UPPLIM   DS    CL6                 END= UPPER CCCHH
LLIMSAVE DS    CL8                 LOWLIM SAVE AREA
DBLSW    DS    CL1                 DE-BLOCK SW
ALTSW    DS    CL1                 ALTERNATE TRK SW
CT       DS    CL1                 PARAMTRS. FOUND MASK, BIT0=INPUT,ETC
EOF      DS    CL1                 CARD EOF SW
TYP      DS    CL1                 TYPE OF DISK SCAN
PL       DS    CL1                 PRINT LENGTH
ESW      DS    CL1                 LEAD ZERO SW ( SUPPRESS )
OTEOR    DS    CL1                 OUTPUT TAPE END OF REEL SW
OTONINSW DS    CL1                 SW FOR O/P OPER. ON TAPEIN  DD
INFMOTSW DS    CL1                 SW FOR I/P OPER. ON TAPEOUT DD
DSKEOFSW DS    CL1                 SET TO X'FF' ON EOF READ (CTL.CRD.)
MVEND    EQU   *                   END   ZERO CLEAR
LNOFMOVE EQU   MVEND-MVBEGIN       LENGTH OF MOVE TO ZERO SWITCHES
OUTDDSAV DS    CL8
         DS    CL1
INDDSAV  DS    CL8
         DS    CL1
DDWK1    DS    CL9
DDWK2    DS    CL8
DTPIO    DS    0F                  DISK/TAPE IO AREA
         DS    CL(NBUFPAGS*4096)       LIMIT TO ACTUAL NO. OF PAGES
*                                      NEEDED TO READ TYPICAL INSTAL-
*                                      LATION BLKSIZES.
L        EQU   12                  LETTER WIDTH
S        EQU   2                   SIZE OF BLOCKED LETTERS
D1       EQU   CONIO+3             BEGIN 'D'
D2       EQU   D1+1                'D' CONT.
D3       EQU   D2+1                'D' CONT. NOT PRINTED IN OS/DITTO
O1       EQU   D1+L/2+S/2-1            MOVE O OVER 1/2 'D' WIDTH - 1
O2       EQU   O1+1                'O' CONT.
O3       EQU   O2+1                'O' CONT.
S1       EQU   O1+L+S              'S' POSITION
S2       EQU   S1+1                'S' CONT.
S3       EQU   S2+1                'S' CONT.
X        EQU   S1+L+S              SLASH POSITION
DD1      EQU   X+L+S               'D' IN DITTO
DD2      EQU   DD1+1               'D' CONT.
DD3      EQU   DD2+1               'D' CONT.
I1       EQU   DD1+L+S             'I' IN DITTO
I2       EQU   I1+1                'I' CONT.
T1       EQU   I1+L+S              'T' IN DITTO
T2       EQU   T1+1                'T' CONT.
TT1      EQU   T1+L+S              'T' ( 2ND ) IN DITTO
OO1      EQU   TT1+L+S             LAST 'O' IN DITTO
R0       EQU   0                   REGISTER NOTATION
R1       EQU   1                   REGISTER NOTATION
R2       EQU   2                   REGISTER NOTATION
R3       EQU   3                   REGISTER NOTATION
R4       EQU   4                   REGISTER NOTATION
R5       EQU   5                   REGISTER NOTATION
R6       EQU   6                   REGISTER NOTATION
R7       EQU   7                   REGISTER NOTATION
R8       EQU   8                   REGISTER NOTATION
R9       EQU   9                   REGISTER NOTATION
RA       EQU   10                  REGISTER NOTATION
RB       EQU   11                  REGISTER NOTATION
RC       EQU   12                  REGISTER NOTATION
RD       EQU   13                  REGISTER NOTATION
RE       EQU   14                  REGISTER NOTATION
RF       EQU   15                  REGISTER NOTATION
SAVECCWA EQU   DSKECB              USE DSK ECB FOR TEMP. STORAGE
         ORG   ADRSLO              ORIGIN THERE TO USE FOR INITIALIZING
*                                   CODE.
         EJECT
***********************************************************************
**********         GET PARM FIELD                           ***********
**********         ADJUST SAVE AREA POINTERS                ***********
**********         GET DATE, OPEN SYSPRINT                  ***********
**********         CHECK PARM FIELD                         ***********
**********         DETERMINE MODE OF OPERATION, SET         ***********
**********         TAPE ALLOCATION SWITCHES, AND ISSUE      ***********
**********         SYSIN OPEN IF IN CONTROL CARD MODE       ***********
***********************************************************************
         SPACE 1
INITIALZ ST    R1,PARMPTR
         LA    R3,SAVEAREA
         ST    R3,8(R2)                PTR TO MY SAVE AREA
         ST    R2,4(R3)                PTR TO PREVIOUS SAVE AREA
         STM   RD,RF,SV13TO15      SAVE BASE REGS ONCE, LM AFTER MACRO
         TIME  DEC                     ISSUE MACRO TO OBTAIN DATE
         L     RF,SV13TO15+8           RESTORE R15 BEFORE NEXT INSTR.
         ST    R1,DATEPKED             STORE DATE
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
         UNPK  DATUPKD(8),DATEPKED     UNPK IN TO SAVE AREA
         MVC   DATESAVE(2),DATUPKD+3 GET DATE FROM TIME MACRO WORK AREA
         MVI   DATESAVE+2,C'.'     INSERT PERIOD
         MVC   DATESAVE+3(3),DATUPKD+5 GET DATE FROM TIME MACRO WORK AR
         OPEN  (PRTRDCB,(OUTPUT))
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER EACH  MACRO
RELOCAT  MVC   SET+1(99),SET
         L     R8,PARMPTR
         L     R8,0(R8)            PTR TO LENGTH FIELD
BGRD     CLC   2(9,R8),JOBSTRM     CONTROL CARD MODE ?
         BNE   BYPASCRD                NO, BYPASS OPEN CARD
         BAL   R9,OPENCRD              YES, OPEN SYSIN
         MVC   USEDSE(7),BLANKS    BLANK OUT DSE PORTION OF MSG
         B     CONSOLE                 GO INITIALIZE FOR CONTROL CARD
BYPASCRD MVI   CNTRLSW,X'00'           NO, TURN OFF CONTROL CARD SW.
         MVI   RELTRKSW+1,X'F0'    REL TRK RTNE SKIPPED IN OPER MODE
TIOTLOOK EQU   *
         EXTRACT TIOTADDR,FIELDS=(TIOT)    GET TIOT POINTER FROM TCB
         LM    RD,RF,SV13TO15      RESTORE BASE REGS AFTER MACRO
         CLC   TIOTADDR,ZEROWORD       DID EXTRACT FILL IN FIELD ?
         BNE   LOOKINIT            IF EXTRACT OK - USE, ELSE SIMULATE
*              SIMULATE EXTRACT MACRO
         L     R3,16               CVT PTR
         L     R2,0(R3)            PTR TO TCB PTR
         L     R3,4(R2)            CURRENT TCB PTR
         L     R2,12(R3)           TIOTADDR
         ST    R2,TIOTADDR         STORE TIOT ADDR
*                                  END OF EXTRACT
LOOKINIT EQU   *                   INITIALIZE FOR TIOT SCAN
         L     R8,TIOTADDR         PRIME R8 WITH TIOT PTR
         LA    R8,24(R8)           BUMP TO 1ST DD ENTRY
LOOKTI   CLC   4(8,R8),TPINDD      DDNAME IN TIOT ENTRY = TAPEIN ?
         BNE   LOOKTO              NO, LOOK FOR TAPEOUT
         MVI   TIALLOSW,X'FF'      JES, SET TAPEIN ALLOCATIONS SWITCH
         SR    R9,R9
         ICM   R9,7,17(R8)         GET UCB ADDRESS
         MVC   TIALLUCB(3),13(R9)  SAVE UCB CUU
         B     LOOKBUMP            INCRMENT POUNTERS
LOOKTO   CLC   4(8,R8),TPOTDD      DDNAME IN TIOT ENTRY = TAPEOUT ?
         BNE   LOOKBUMP            NO, BUMP TO NEXT ENTRY
         MVI   TOALLOSW,X'FF'      YES, SET TAPEOUT ALLOCATED SWITCH
         SR    R9,R9
         ICM   R9,7,17(R8)         GET UCB ADDRESS
         MVC   TOALLUCB(3),13(R9)  SAVE UCB CUU
LOOKBUMP SR    R9,R9               CLEAR R9
         IC    R9,0(R8)            PICK UP TIOT ENTRY LENGTH
         AR    R8,R9               BUMP TO NEXT ENTRY ADDRESS
         CLI   0(R8),X'00'         END OF TIOT ?
         BNE   LOOKTI              NO, CONTINUE LOOKING. YES, FALL THRU
GOINTRP  B     INTRP                   GO INTIALIZE IRPT FUNCTION
         DS    0F
TIOTADDR DC    F'0'                PTR TO TIOT, USE TO GET TAPE UCB'S
ZEROWORD DC    F'0'                IF NOT EQ. TO TIOTADDR, EXTRACT OK
PARMPTR  DC    F'0'
DATEPKED DS    F
DATUPKD  DS    2F
JOBSTRM  DC    CL9'JOBSTREAM'
         EJECT
***********************************************************************
DSCTAPE  DSECT                     DUMMY SECTION FOR TAPE ECB & IOB
         DS    0F
DSCTECB  DS    F
DSCTIOB  DS    0F
         DS    H
DSCTSENS DS    H
         DS    CL1
         DS    CL3
DSCTFLG  DS    CL1
DSCTCSW  DS    CL7
DSCTCCWA DS    F
DSCTDCBA DS    F
         DS    F
         DS    H
         DS    H
OSDITTO  CSECT                     END DUMMY SECTION FOR ECB/IOB, TAPE
         #WAYIN GPM=YES
         #SPGSECT
         DC    CL24'P!A!N022SOF152YYYY230185'
         END   OSDITTO
