* THIS IS THE MET OFFICE TSO JCL CHECKER. IT IS NOT COMPREHENSIVE
* BUT CHECKS ONLY KEYWORDS AND A FEW SUBPARAMETERS OF THEM AS WELL
* AS SUCH THINGS AS BALANCED PARANTHESES. IT EXPECTS A
* JOB CARD OF THE FORM //JOBNAME JOB (ACC1,ACC2),NAME,PARAMETERS
* AND IT HAS NOT BEEN TESTED WITH OTHER TYPES. HOWEVER ANY
* CHANGES NECSSARY SHOULD BE MINOR. IT ALSO PRODUCES AN ERROR
* MESSAGE IF UNIT=3330 OR UNIT=2400-3 IS CODED BUT THIS IS TRIVIAL TO
* REMOVE IF REQUIRED. IT IS NECESSARY TO TELL TSO THAT
* A SCANNER FOR CNTL DATA SETS EXISTS.  THE JCL CHECKER MODULE
* RESIDES ON A LNKLST LIBRARY AND IS ASSUMED TO BE NAMED
* JCLCHECK. THE FOLLOWING ZAP IS NECESSARY :-
*       VER 025E 0000,0000,0000,0832
*       REP 025E D1C3,D3C3,C8C5,0872    JCLCHECK
*       VER 0274 0000,0000
*       REP 0274 0000,0002
* THIS ZAP IS THE SAME IN MVS 3.7 AND 3.8.
* INFORMATION ABOUT SCAN CHECKERS IS IN EDIT LOGIC GY28-6773
JCLCHECK CSECT
         CHAIN ,,*,BASE=(13,12)
         L     3,4(1)         LOAD ADDRESS OF COMMUNICATIONS AREA
* TEST FIRST 4 BITS OF THE AREA
* A VALUE OF 1X1X INDICATES A FINAL ENTRY SO RETURN IMMEDIATELY
* A VALUE OF 0XXX INDICATES AN INITIAL ENTRY SO SET UP WORKAREAS
* OTHERWISE START SYNTAX CHECKING
         TM    0(3),X'A0'
         BO    END
         NI    0(3),X'80'
         TM    0(3),X'80'
         BO    BUFF
STWKA     L     4,=A(WORKAREA)
         ST    4,0(3)         STORE WORKAREA ADDRESS
         B     END1
END      L     8,CONTFLAG     IS A CONTINUATION CARD EXPECTED
         C     8,=F'1'
         BNE   END1
         MVC   ERROR+50(15),CONTEX
         TPUT  ERROR,80
END1      L     13,4(13)
         LM    14,12,12(13)
         SR    15,15          SET CONDITION CODE TO ZERO
         BR    14
BUFF     MVC   CARDCNT+3(1),0(1)      MOVE NUMBER OF RECORDS
         MVC   BUFFADD+1(3),1(1)   STORE ADDRESS OF NEXT BUFFER
         L     6,CARDCNT
         C     6,=F'0'        IS NUMBER OF CARDS IN BUFFER ZERO
         BE    ENDJCL         IF SO GET NEXT BUFFER
         A     1,=F'4'        POINT TO START OF RECORD
         ST    1,REG1ST       KEEP ADDRESS OF START OF RECORD
         L     8,CONTFLAG     TEST IF THIS IS A CONTINUATION CARD
         C     8,=F'1'
         BNE   REC
* THIS IS A CONTINUATION CARD
         L     10,REG10ST     RESTORE REGISTER 10
         SR    8,8
         ST    8,CONTFLAG     RESET FLAG
         B     CONT6
* START RECORD SCAN
REC      LR    11,1
* TEST FOR //
         CLC   0(2,1),=C'//'
         BNE   CARD
* MOVE LINE NUMBER TO ERROR MESSAGE OUTPUT BUFFER
         MVC   ERROR+5(8),72(1)
         A     1,=F'2'
* TEST FOR //*
         CLC   0(1,1),=C'*'
         BE    CARD
* TEST FOR A NAME
         CLC   0(1,1),=C' '
         BE    OPER
         L     10,=A(OPER2)
* BRANCH TO NAME TESTING ROUTINE
         MVC   NNALPH+46(15),=C'                  '
         MVC   NAMLEN+46(15),=C'                  '
         B     NAME
* TEST FOR A VALID OPERATION
OPER2    S     1,=F'1'        POINT TO LAST CHARACTER
OPER     LR    3,1
         SR    3,11           REG3 CONTAINS DISTANCE ALONG THIS CARD
OPER1     A     1,=F'1'
         A     3,=F'1'        MOVE ALONG CARD
         C     3,=F'72'       HAS IT REACHED THE LINE NUMBER
         BE    CARD           IF SO THIS IS A // CARD
         CLC   0(1,1),=C' '
         BE    OPER1
         CLC   0(3,1),=C'JOB'
         BE    JOB
         CLC   0(4,1),=C'EXEC'
         BE    EXEC
         CLC   0(2,1),=C'DD'
         BE    DD
         CLC   0(4,1),=C'PROC'
         BE    CARD
         CLC   0(4,1),=C'PEND'
         BE    CARD
* UNIDENTIFIABLE OPERATION FOUND
         MVC   ERROR+15(50),NONOPER
         TPUT   ERROR,80
         B     CARD
* JOB CARD IDENTIFIED SO FIND AND SKIP ACCOUNTING FIELD AND NAME
JOB      A     1,=F'3'
         CLC   0(1,1),=C' '
         BE    JOB1
* NO SPACE AFTER JOB
         MVC   ERROR+15(50),JOBSP
         TPUT   ERROR,80
         B     CARD
JOB1     LR    3,1
         SR    3,11           REG3 CONTAINS DISTANCE ALONG CARD
JOB20     A     1,=F'1'
         A     3,=F'1'        MOVE ALONG CARD
         C     3,=F'72'       HAS IT REACHED LINE NUMBER
         BE CONTCRD           IF SO CONTINUATION CARD EXPECTED
         CLC   0(1,1),=C' '
         BE    JOB20
         SR    2,2
* TEST FOR COMMAS BETWEEN FIELDS
JOB2     CLC   0(1,1),=C','
         BNE   JOB3
         C     2,=F'1'
         BE    JOB4
         A     2,=F'1'
JOB3     CLC   0(1,1),=C'('
         BNE   JOB6
* BRACKET FOUND SO TEST FOR BALANCING
         L     10,=A(JOB5)
         B     BRKTEST
JOB6     CLC   0(1,1),=C' '
         BNE   JOB5
* ILLEGAL SPACE FOUND
         MVC   ERROR+15(50),SPACE
         TPUT   ERROR,80
         B     CARD
JOB5     A     1,=F'1'
         B     JOB2
* TEST FOR VALID KEYWORD
JOB4     A     1,=F'1'
         CLC   0(1,1),=C' '
         BNE   JOB12
* CONTINUATION CARD EXPECTED
         L     10,=A(JOB12)
         B     CONTCRD
JOB12    L     4,=F'4'
         CLC   0(4,1),=C'PRTY'
         BE    JOB9
         CLC   0(4,1),=C'TIME'
         BE    JOB9
         CLC   0(4,1),=C'COND'
         BE    JOB9
         CLC   0(4,1),=C'USER'
         BE    JOB9
         L     4,=F'5'
         CLC   0(5,1),=C'CLASS'
         BE    JOB9
         CLC   0(5,1),=C'GROUP'
         BE    JOB9
         L     4,=F'2'
         CLC   0(2,1),=C'RD'
         BE    JOB9
         L     4,=F'6'
         CLC   0(6,1),=C'NOTIFY'
         BE    JOB9
         CLC   0(6,1),=C'REGION'
         BE    JOB19
         CLC   0(6,1),=C'TYPRUN'
         BE    JOB9
         L     4,=F'7'
         CLC   0(7,1),=C'ADDRSPC'
         BE    JOB9
         CLC   0(7,1),=C'RESTART'
         BE    JOB9
         CLC   0(7,1),=C'PERFORM'
         BE    JOB9
         L     4,=F'8'
         CLC   0(8,1),=C'MSGLEVEL'
         BE    JOB9
         CLC   0(8,1),=C'MSGCLASS'
         BE    JOB9
         CLC   0(8,1),=C'PASSWORD'
         BE    JOB9
* UNIDENTIFIABLE KEYWORD
         MVC   ERROR+15(50),UNKEY
         TPUT   ERROR,80
         B     CARD
JOB9     AR    1,4
JOB14    CLC   0(1,1),=C'.'
         BNE   JOB13
* NAME FOUND AFTER KEYWORD
         A     1,=F'1'
         L     10,=A(JOB21)
         MVC   NNALPH+46(15),=C'(AFTER PARM.)  '
         MVC   NAMLEN+46(15),=C'(AFTER PARM.)  '
         B     NAME
JOB21    S     1,=F'1'        POINT TO LAST CHARACTER
JOB13    CLC   0(1,1),=C'='
         BE    JOB10
* NO = AFTER KEYWORD
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
* RUN ALONG KEYWORD INFO CHECKING FOR BRACKETS
JOB10    A     1,=F'1'
         CLC   0(1,1),=C'('
         BE    JOB11
         CLC   0(1,1),=C' '
* END OF CARD FOUND
         BE    CARD
         CLC   0(1,1),=C','
         BNE   JOB10
* TEST FOR ANOTHER KEYWORD OR IF A CONTINUATION CARD IS EXPECTED
         A     1,=F'1'
         CLC   0(1,1),=C' '
         BNE   JOB12
JOB18    L     10,=A(JOB12)
         B     CONTCRD
JOB11    L     10,=A(JOB10)
         B     BRKTEST
JOB19    L     10,=A(JOB10)
         B     REGION
* PROCESS EXEC CARD
EXEC     A     1,=F'4'
         CLC   0(1,1),=C' '
         BE    EXEC1
* NO SPACE AFTER EXEC
         MVC   ERROR+15(50),EXECSP
         TPUT   ERROR,80
         B     CARD
EXEC1    LR    3,1
         SR    3,11           REG3 CONTAINS DISTANCE ALONG CARD
EXEC12    A     1,=F'1'
         A     3,=F'1'        MOVE ALONG CARD
         C     3,=F'72'       HAS IT REACHED LINE NUMBER
         BE    CONTCRD        IF SO CONTINUATION CARD EXPECTED
         CLC   0(1,1),=C' '
         BE    EXEC12
* TEST FOR KEYWORDS
         L     4,=F'3'
         CLC   0(3,1),=C'PGM'
         BE    EXEC2
         L     4,=F'4'
         CLC   0(4,1),=C'PROC'
         BE    EXEC2
* TEST NAME OF INLINE PROCEDURE
         L     10,=A(EXEC22)
         MVC   NNALPH+46(15),=C'(INLINE PROC)  '
         MVC   NAMLEN+46(15),=C'(INLINE PROC)  '
         B     NAME
EXEC2    AR    1,4
         CLC   0(1,1),=C'='
         BE    EXEC4
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
* TEST NAME OF PGM OR PROC
EXEC4    A     1,=F'1'
         L     10,=A(EXEC3)
         MVC   NNALPH+46(15),=C'(PGM OR PROC)  '
         MVC   NAMLEN+46(15),=C'(PGM OR PROC)  '
         B     NAME
* TEST FOR EXEC PARAMETERS
EXEC3    S     1,=F'1'        POINT TO LAST CHARACTER
         CLC   0(1,1),=C' '
* TEST FOR END OF CARD
         BE    CARD
         A     1,=F'1'
         CLC   0(1,1),=C' '
* TEST FOR CONTINUATION CARD
         BNE   EXEC5
EXEC6    L     10,=A(EXEC5)
         B     CONTCRD
EXEC5    L     4,=F'2'
         CLC   0(2,1),=C'RD'
         BE    EXEC7
         L     4,=F'4'
         CLC   0(4,1),=C'ACCT'
         BE    EXEC7
         CLC   0(4,1),=C'COND'
         BE    EXEC7
         CLC   0(4,1),=C'PARM'
         BE    EXEC7
         CLC   0(4,1),=C'TIME'
         BE    EXEC7
         L     4,=F'5'
         CLC   0(5,1),=C'DPRTY'
         BE    EXEC7
         L     4,=F'6'
         CLC   0(6,1),=C'REGION'
         BE    EXEC17
         L     4,=F'7'
         CLC   0(7,1),=C'ADDRSPC'
         BE    EXEC7
         CLC   0(7,1),=C'PERFORM'
         BE    EXEC7
         L     4,=F'8'
         CLC   0(8,1),=C'DYNAMNBR'
         BE    EXEC7
* UNIDENTIFIABLE KEYWORD FOUND
         MVC   ERROR+15(50),UNKEY
         TPUT   ERROR,80
         B     CARD
EXEC7    AR    1,4
EXEC8    CLC   0(1,1),=C'.'
         BNE   EXEC9
* NAME FOUND AFTER KEYWORD
         A     1,=F'1'
         L     10,=A(EXEC25)
         MVC   NNALPH+46(15),=C'(AFTER PARM.)  '
         MVC   NAMLEN+49(15),=C'(AFTER PARM.)  '
         B     NAME
EXEC25   S     1,=F'1'        POINT TO LAST CHARACTER
EXEC9    CLC   0(1,1),=C'='
         BE    EXEC10
* NO = AFTER KEYWORD
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
* RUN ALONG KEYWORD INFO CHECKING FOR BRACKETS
EXEC10   A     1,=F'1'
         CLC   0(1,1),=C'('
         BE    EXEC11
         CLC   0(1,1),=C''''
         BE    EXEC14
         CLC   0(1,1),=C' '
* END OF CARD FOUND
         BE    CARD
         CLC   0(1,1),=C','
         BNE   EXEC10
* TEST FOR ANOTHER KEYWORD OR IF A CONTINUATION CARD IS EXPECTED
EXEC15   A     1,=F'1'
         CLC   0(1,1),=C' '
         BNE   EXEC5
EXEC13   L     10,=A(EXEC5)
         B     CONTCRD
EXEC11   L     10,=A(EXEC10)
         B     BRKTEST
EXEC17   L     10,=A(EXEC10)
         B     REGION
EXEC14   SR    7,7
EXEC21   A     1,=F'1'
EXEC19   CLC   0(1,1),=C' '
         BNE   EXEC18
         LR    6,1
         S     6,=F'1'        GO BACK ONE SPACE
         CLC   0(1,6),=C','   WAS LAST CHARACTER A COMMA
         BE    EXEC20
* UNBALANCED APOSTROPHES
         MVC   ERROR+15(50),UNAPS
         TPUT  ERROR,80
         B     CARD
*CONTINUATION CARD EXPECTED
EXEC20   ST    7,REG7ST       KEEP REGISTER 7
         L     10,=A(EXEC19)
         B     CONTCRD
EXEC18   CLC   0(1,1),=C''''
         BE    EXEC10         LOOK FOR ANOTHER KEYWORD
         A     7,=F'1'
         C     7,=F'100'
         BNH   EXEC21
*TOO MANY CHARACTERS IN PARM FIELD
         MVC   ERROR+15(50),TOOMC
         TPUT  ERROR,80
         B     CARD
* RUN ALONG INLINE PROCEDURE INFO TESTING FOR CONTINUATION CARD
EXEC22   CLC   0(1,1),=C' '
         BE    EXEC23
         CLC   0(1,1),=C'('
         BE    EXEC24
         A     1,=F'1'
         B     EXEC22
EXEC23   S     1,=F'1'
         CLC   0(1,1),=C','      WAS LAST CHARACTER A COMMA
         BNE   CARD           IF NO END OF CARD
         L     10,=A(EXEC22)  IF YES CONTINUATION EXPECTED
         B     CONTCRD
* TEST BRACKETS
EXEC24   L     10,=A(EXEC22)
         B     BRKTEST
* PROCESS DD CARD
DD       A     1,=F'2'        SKIP PAST DD
DD16     CLC   0(1,1),=C' '
         BE    DD1
* NO SPACE AFTER DD
         MVC   ERROR+15(50),DDSP
         TPUT   ERROR,80
         B     CARD
DD1      LR    3,1
         SR    3,11           REG3 CONTAINS DISTANCE ALONG CARD
DD10      A     1,=F'1'
         A     3,=F'1'        MOVE ALONG CARD
         C     3,=F'72'       HAS IT REACHED THE LINE NUMBER
         BE    CARD           IF SO GET ANOTHER CARD
         CLC   0(1,1),=C' '
         BE    DD10
* SET FLAG  FOR MINOR KEYWORD
         SR    9,9
         ST    9,MINFLAG
* TEST FOR MAJOR KEYWORDS
DD8      L     4,=F'1'
         CLC   0(1,1),=C'*'
         BE    DD2
         L     4,=F'4'
         CLC   0(4,1),=C'DATA'
         BE    DD2
         L     4,=F'5'
         CLC   0(5,1),=C'DUMMY'
         BE    DD2
         CLC   0(5,1),=C'DYNAM'
         BE    DD2
         B     DD3
* MAJOR KEYWORD FOUND SO TEST IF PRECEDED BY MINOR KEYWORD
DD2      AR    1,4            SKIP OVER KEYWORD
* TEST IF COMMA OR BLANK AFTER KEYWORD
         CLC   0(1,1),=C','
         BE    DD11
         CLC   0(1,1),=C' '
         BE    DD11
* ILLEGAL KEYWORD
         MVC   ERROR+15(50),UNKEY
         TPUT  ERROR,80
         B     CARD
DD11     L     9,MINFLAG
         A     1,=F'1'        SKIP PAST BLANK OR COMMA
         C     9,=F'0'
         BE    DD3
         MVC   ERROR+15(50),MAJMIN
         ST    1,REG1
         TPUT   ERROR,80
         L     1,REG1
         B     DD6
* TEST FOR MINOR KEYWORDS
DD3      L     4,=F'6'
         CLC   0(1,1),=C' '   IF BLANK THEN END OF CARD OR CONT
         BNE   DD12
         S     1,=F'1'        POINT TO LAST CHARACTER
         L     10,=A(DD8)     LOAD RETURN ADDRESS
         CLC   0(1,1),=C','   IS IT A COMMA
         BE    CONTCRD        CONTINUATION IF YES
         B     CARD           END OF CARD IF NO
DD12     CLC   0(6,1),=C'DSNAME'
         BE    DSN
         CLC   0(6,1),=C'VOLUME'
         BE    DD4
         L     4,=F'3'
         CLC   0(3,1),=C'DSN'
         BE    DSN
         CLC   0(3,1),=C'VOL'
         BE    DD4
         CLC   0(3,1),=C'AFF'
         BE    AFF
         CLC   0(3,1),=C'DCB'
         BE    DD4
         CLC   0(3,1),=C'DLM'
         BE    DLM
         CLC   0(3,1),=C'FCB'
         BE    DD4
         CLC   0(3,1),=C'SEP'
         BE    SEP
         CLC   0(3,1),=C'UCS'
         BE    DD4
         CLC   0(3,1),=C'AMP'
         BE    DD4
         L     4,=F'4'
         CLC   0(4,1),=C'DEST'
         BE    DD4
         CLC   0(4,1),=C'DISP'
         BE    DISP
         CLC   0(4,1),=C'HOLD'
         BE    HOLD
         CLC   0(4,1),=C'TERM'
         BE    DD4
         CLC   0(4,1),=C'UNIT'
         BE    UNIT
         CLC   0(4,1),=C'DSID'
         BE    DD4
         CLC   0(4,1),=C'FREE'
         BE    DD4
         L     4,=F'5'
         CLC   0(5,1),=C'LABEL'
         BE    DD4
         CLC   0(5,1),=C'QNAME'
         BE    QNAME
         CLC   0(5,1),=C'SPACE'
         BE    SPACEF
         CLC   0(5,1),=C'SPLIT'
         BE    DD4
         CLC   0(5,1),=C'BURST'
         BE    DD4
         CLC   0(5,1),=C'CHARS'
         BE    DD4
         CLC   0(5,1),=C'CHKPT'
         BE    DD4
         CLC   0(5,1),=C'FLASH'
         BE    DD4
         CLC   0(5,1),=C'MSVGP'
         BE    DD4
         L     4,=F'6'
         CLC   0(6,1),=C'COPIES'
         BE    DD4
         CLC   0(6,1),=C'DDNAME'
         BE    DSN
         CLC   0(6,1),=C'OUTLIM'
         BE    DD4
         CLC   0(6,1),=C'SYSOUT'
         BE    DD4
         CLC   0(6,1),=C'MODIFY'
         BE    DD4
         CLC   0(6,1),=C'SUBSYS'
         BE    DD4
         L     4,=F'7'
         CLC   0(7,1),=C'PROTECT'
         BE    DD4
         L     4,=F'8'
         CLC   0(8,1),=C'SUBALLOC'
         BE    DD4
* UNIDENTIFIABLE KEYWORD FOUND
         MVC   ERROR+15(50),UNKEY
         TPUT   ERROR,80
         B     CARD
* TEST DDNAME OR DSNAME
DSN      AR    1,4
* SET MINOR KEYWORD FLAG
         L     9,=F'1'
         ST    9,MINFLAG
         CLC   0(1,1),=C'='
         BE    DSN1
* NO = AFTER KEYWORD
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
DSN1     A     1,=F'1'
         L     10,=A(DD8)
         MVC   NNALPH+46(15),=C'(DDNAME OR DSN)'
         MVC   NAMLEN+46(15),=C'(DDNAME OR DSN)'
         B     NAME
* MINOR KEYWORD FOUND SO SET FLAG
DD4      L     9,=F'1'
         ST    9,MINFLAG
         AR    1,4
         CLC   0(1,1),=C'='
         BE    DD6
* NO = AFTER KEYWORD
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
* RUN ALONG KEYWORD INFO CHECKING FOR BRACKETS
DD6      A     1,=F'1'
DD5      CLC   0(1,1),=C'('
         BE    DD7
         CLC   0(1,1),=C' '
* END OF CARD FOUND
         BE    CARD
         CLC   0(1,1),=C','
         BNE   DD6
* TEST FOR ANOTHER PARAMETER OR A CONTINUATION CHARACTER
         A     1,=F'1'
         CLC   0(1,1),=C' '
         BNE   DD8
DD9      L     10,=A(DD8)
         B     CONTCRD
DD7      L     10,=A(DD6)
         B     BRKTEST
* CHECK THE NAME AFTER AN AFF KEYWORD
AFF      A     1,=F'3'
* SET MINOR KEYWORD FLAG
         L     9,=F'1'
         ST    9,MINFLAG
         CLC   0(1,1),=C'='
         BE    AFF1
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
AFF1     A     1,=F'1'
         MVC   NNALPH+46(15),=C'(AFTER AFF)    '
         MVC   NAMLEN+46(15),=C'(AFTER AFF)    '
         L     10,=A(DD8)
         B     NAME
* CHECK DISP KEYWORD
DISP     A     1,=F'4'
* SET MINOR KEYWORD FLAG
         L     9,=F'1'
         ST    9,MINFLAG
         CLC   0(1,1),=C'='
         BE    DISP1
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
DISP1    A     1,=F'1'
         CLC   0(1,1),=C'('
         BE    DISP2
* ONLY ONE PARAMETER EXPECTED
         CLC   0(3,1),=C'NEW'
         BE    DISP3
         CLC   0(3,1),=C'OLD'
         BE    DISP3
         CLC   0(3,1),=C'SHR'
         BE    DISP3
         CLC   0(3,1),=C'MOD'
         BE    DISP3
* UNIDENTIFIABLE PARAMETER
         MVC   ERROR+15(50),DISPAR1
         TPUT   ERROR,80
         B     CARD
DISP3    A     1,=F'3'
         B     DD5
* MORE THAN ONE PARAMETER EXPECTED
DISP2    A     1,=F'1'
         L     4,=F'1'
         CLC   0(1,1),=C','
         BE    DISP4
         L     4,=F'4'
         CLC   0(3,1),=C'NEW'
         BE    DISP4
         CLC   0(3,1),=C'OLD'
         BE    DISP4
         CLC   0(3,1),=C'SHR'
         BE    DISP4
         CLC   0(3,1),=C'MOD'
         BE    DISP4
*        UNIDENTIFIABLE PARAMETER
         MVC   ERROR+15(50),DISPAR1
         TPUT   ERROR,80
         B     CARD
DISP4    AR    1,4
         L     4,=F'1'
         CLC   0(1,1),=C','
         BE    DISP5
         L     4,=F'5'
         CLC   0(4,1),=C'KEEP'
         BE    DISP5
         CLC   0(4,1),=C'PASS'
         BE    DISP5
         L     4,=F'6'
         CLC   0(5,1),=C'CATLG'
         BE    DISP5
         L     4,=F'7'
         CLC   0(6,1),=C'DELETE'
         BE    DISP5
         L     4,=F'8'
         CLC   0(7,1),=C'UNCATLG'
         BE    DISP5
* UNIDENTIFIABLE PARAMETER
         MVC   ERROR+15(50),DISPAR2
         TPUT   ERROR,80
         B     CARD
DISP5    AR    1,4
         S     1,=F'1'        GO BACK TO CHECK BRACKET
         CLC   0(1,1),=C')'
         BE    DISP8
         A     1,=F'1'
         L     4,=F'3'
         CLC   0(4,1),=C'KEEP'
         BE    DISP7
         L     4,=F'4'
         CLC   0(5,1),=C'CATLG'
         BE    DISP7
         L     4,=F'5'
         CLC   0(6,1),=C'DELETE'
         BE    DISP7
         L     4,=F'6'
         CLC   0(7,1),=C'UNCATLG'
         BE    DISP7
* UNIDENTIFIABLE PARAMETER
         MVC   ERROR+15(50),DISPAR3
         TPUT   ERROR,80
         B     CARD
DISP7    AR    1,4
DISP6    A     1,=F'1'
         CLC   0(1,1),=C')'
         BE    DISP8
* UNBALANCED PARANTHESES
         MVC   ERROR+15(50),DISPBRK
         TPUT   ERROR,80
         B     CARD
DISP8    A     1,=F'1'
         B     DD5
* TEST THE DLM PARAMETER
* SET MINOR KEYWORD FLAG
DLM      L     9,=F'1'
         ST    9,MINFLAG
         A     1,=F'3'
         CLC   0(1,1),=C'='
         BE    DLM1
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
DLM1     A     1,=F'1'
         CLC   0(1,1),=C''''
         BE    DLM1
         A     1,=F'1'
DLM2     A     1,=F'1'
         CLC   0(1,1),=C''''
         BE    DLM2
         CLC   0(1,1),=C' '
         BE    DD6
         CLC   0(1,1),=C','
         BE    DD6
* DELIMETER IS NOT TWO CHARACTERS
         MVC   ERROR+15(50),DLMCHAR
         TPUT   ERROR,80
         B     CARD
* TEST THE HOLD PARAMETER
* SET MINOR KEYWORD FLAG
HOLD     L     9,=F'1'
         ST    9,MINFLAG
         A     1,=F'4'
         CLC   0(1,1),=C'='
         BE    HOLD1
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
HOLD1    L     4,=F'2'
         CLC   0(2,1),=C'NO'
         BE    HOLD2
         L     4,=F'3'
         CLC   0(3,1),=C'YES'
         BE    HOLD2
* UNIDENTIFIABLE PARAMETER
         MVC   ERROR+15(50),HOLDPARM
         TPUT   ERROR,80
         B     CARD
HOLD2    AR    1,4
         B     DD6
* TEST THE QNAME PARAMETER
* SET MINOR KEYWORD FLAG
QNAME    L     9,=F'1'
         ST    9,MINFLAG
         A     1,=F'5'
         CLC   0(1,1),=C'='
         BE    QNAME1
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
QNAME1   MVC   NNALPH+46(15),=C'(AFTER QNAME=) '
         MVC   NAMLEN+46(15),=C'(AFTER QNAME=) '
         L     10,=A(DD8)
         B     NAME
* TEST THE SEP PARAMETER
* SET MINOR KEYWORD FLAG
SEP      L     9,=F'1'
         ST    9,MINFLAG
         SR    5,5
         A     1,=F'3'
         CLC   0(1,1),=C'='
         BE    SEP1
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
SEP1     A     1,=F'1'
         CLC   0(1,1),=C'('
         BNE   SEP6
         A     1,=F'1'        SKIP PAST BRACKET
         A     5,=F'1'        INDICATE BRACKET ROUND NAMES
SEP6     MVC   NNALPH+46(15),=C'(AFTER SEP)    '
         MVC   NAMLEN+46(15),=C'(AFTER SEP)    '
         SR    9,9
SEP2     L     10,=A(SEP3)
         B     NAME
SEP3     A     9,=F'1'
         C     9,=F'7'
         BNH   SEP4
* TOO MANY NAMES IN SEP PARAMETER
         MVC   ERROR+15(50),NAMSEP
         TPUT   ERROR,80
         B     CARD
SEP4     C     5,=F'0'        TEST IF CLOSE BRACKET EXPECTED
         BE    DD8            IF NOT FIND NEXT KEYWORD
         S     1,=F'2'        POINT TO EXPECTED BRACKET
         CLC   0(1,1),=C')'
         BE    DD6
* ANOTHER NAME PRESENT
SEP5     A     1,=F'2'
         B     SEP2
* TEST THE UNIT PARAMETER
* SET MINOR KEYWORD FLAG
UNIT     L     9,=F'1'
         ST    9,MINFLAG
         A     1,=F'4'
         CLC   0(1,1),=C'='
         BE    UNIT1
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
UNIT1    A     1,=F'1'
         CLC   0(1,1),=C'('
         BNE   UNIT2
         L     10,=A(DD6)
         B     BRKTEST        CHECK IF PARENTHESES BALANCED
UNIT2    CLC   0(5,1),=C'TAPE9'
         BNE   UNIT3
         MVC   ERROR+15(50),TAPE9
         ST    1,REG1
         TPUT   ERROR,80
         L     1,REG1
         B     DD6
UNIT3    CLC   0(4,1),=C'3330'
         BNE   DD6
         MVC   ERROR+15(50),DISK
         ST    1,REG1
         TPUT   ERROR,80
         L     1,REG1
         B     DD6
* TEST THE SPACE PARAMETER
* SET MINOR KEYWORD FLAG
SPACEF    L     9,=F'1'
         ST    9,MINFLAG
         A     1,=F'5'
         CLC   0(1,1),=C'='
         BE    SPACE1
         MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         MVC   NAMLEN+46(15),=C'                  '
         B     CARD
SPACE1   A     1,=F'1'        SKIP PAST =
         CLC   0(1,1),=C','   HAS SPACE= BEEN CODED
         BE    DD5            LOOK FOR NEW KEYWORD
         CLC   0(1,1),=C' '   IS THIS THE END OF THE CARD
         BE    CARD
         L     10,=A(DD6)
         B     BRKTEST
* THIS SECTION TESTS THE LENGTH OF A NAME AND ITS FIRST CHARACTER
NAME     L     2,=A(ALPHA)
         A     2,=F'25'
         SR    4,4
NAME1    CLC   0(1,1),0(2)
* BRANCH IF FIRST CHARACTER IS ALPHABETIC
         BE    NAME2
         S     2,=F'1'
         C     2,=A(ALPHA)
         BNL   NAME1
         CLC   0(1,1),=C'*'
         BE    NAME2
         CLC   0(1,1),=C'&&'
         BE    NAME2
         CLC   0(1,1),=C'#'
         BE    NAME2
         CLC   0(1,1),=C'$'
         BE    NAME2
* FIRST CHARACTER NOT ALPHABETIC OR * OR & OR # OR $
         MVC   ERROR+15(65),NNALPH
         TPUT   ERROR,80
         MVC   ERROR+61(18),=C'                  '
         B     CARD
NAME2    L     3,=F'1'
         AR    1,3
NAME3    CLC   0(1,1),=C' '
* IF EQUAL END OF NAME
         BE    NAME9
         CLC   0(1,1),=C','
         BE    NAME9
         CLC   0(1,1),=C'='
         BE    NAME9
         CLC   0(1,1),=C')'
         BE    NAME14
         CLC   0(1,1),=C'.'
* IF EQUAL START OF NEW NAME
         BE   NAME10
         CLC   0(1,1),=C'('
* IF EQUAL START NEW NAME BUT DO NOT CHECK FIRST CHARACTER
* AS IT COULD BE A DATA GENERATION GROUP NUMBER
         BE   NAME11
         A     3,=F'1'
         A     1,=F'1'
         C     3,=F'8'
         BNH   NAME3
* NAME TOO LONG
         MVC   ERROR+15(65),NAMLEN
         TPUT   ERROR,80
         MVC   ERROR+61(18),=C'                  '
         B     CARD
NAME14   S     4,=F'1'        CLOSE BRACKET FOUND
         A     1,=F'1'        POINT TO CHARACTER AFTER BRACKET
         CLC   0(1,1),=C' '   IS IT BLANK
         BE    NAME9
         CLC   0(1,1),=C','   IS IT A COMMA
         BE    NAME9
         MVC   ERROR+15(50),AFTNAM
         TPUT  ERROR,80
         B     CARD
NAME9    A     1,=F'1'        POINT TO NEXT CHARACTER
         C     4,=F'0'        TEST FOR BALANCED PARANTHESES
         BNH  NAME12
* UNBALANCED PARENTHESES
NAME13   MVC   ERROR+15(50),BRK
         TPUT   ERROR,80
         B     CARD
NAME12   BR    10
NAME11   A     4,=F'1'        OPEN BRACKET INDICATOR SET
         A     1,=F'1'        JUMP PAST BRACKET AND FIRST CHARACTER
         B     NAME2
NAME10   A     1,=F'1'        JUMP PAST .
         C     4,=F'0'        CHECK FOR UNBALANCED BRACKETS
         BH    NAME13         IF NO ERROR MESSAGE
         B     NAME
* THIS SECTION TESTS THE REGION PARAMETER
REGION   ST    10,REG10ST     STORE RETURN ADDRESS
         A     1,=F'5'
REGION1  A     1,=F'1'
         CLC   0(1,1),=C'='
         BNE   REGION6
REGION9  A     1,=F'1'
         CLC   0(1,1),=C'0'
         BNE   REGION2
         MVC   ERROR+15(50),REG0
         TPUT   ERROR,80
         B     CARD
REGION2  A     1,=F'1'
         CLC   0(1,1),=C'K'
         BE    REGION4
         CLC   0(1,1),=C','
         BE    REGION5
         CLC   0(1,1),=C' '
         BNE   REGION2
REGION5  MVC   ERROR+15(50),REG
         TPUT   ERROR,80
         B     CARD
REGION6  CLC   0(1,1),=C'.'   IS THERE A NAME AFTER REGION
         BNE   REGION7
         A     1,=F'1'        POINT TO FIRST CHARACTER OF NAME
         ST    10,REG10ST     STORE RETURN ADDRESS
         L     10,=A(REGION8)
         MVC   NNALPH+46(15),=C'AFTER REGION   '
         MVC   NAMLEN+46(15),=C'AFTER REGION   '
         B     NAME
REGION8  S     1,=F'1'        POINT TO LAST CHARACTER
         CLC   0(1,1),=C'='   IS IT AN EQUALS
         BE    REGION9
REGION7  MVC   ERROR+15(50),ILLKEY
         TPUT   ERROR,80
         B     CARD
REGION4  L     10,REG10ST     RELOAD RETURN ADDRESS
         BR    10             RETURN
* TEST FOR BALANCED PARENTHESES
BRKTEST  LR    9,11
         ST    10,REGST       KEEP RETURN ADDRESS
         SR    7,7
BRKTEST1 CLC   0(1,1),=C' '
         BNE   BRKTEST5
* SPACE AFTER OPEN BRACKET FOUND
         L     10,=A(BRKTEST1)
         ST    7,REG7ST       KEEP REGISTER 7
         LR    6,1
         S     6,=F'1'        MOVE BACK ONE SPACE
         CLC   0(1,6),=C','   WAS LAST CHARACTER A COMMA
         BE    CONTCRD        CONTINUATION CARD FOUND
         CLC   0(1,6),=C')'   WAS LAST CHARACTER A BRACKET
         BE    BRKTEST6       UNMATCHED PARENTHESES
         MVC   ERROR+15(50),SPACE
         TPUT  ERROR,80
         B     CARD
BRKTEST5 CLC   0(1,1),=C'('
         BNE   BRKTEST2
         A     7,=F'1'
         B     BRKTEST3
BRKTEST2 CLC   0(1,1),=C')'
         BNE   BRKTEST3
         S     7,=F'1'
         C     7,=F'0'
         BE    BRKTEST4
BRKTEST3 A     1,=F'1'
         B     BRKTEST1
BRKTEST6 MVC   ERROR+15(50),BRK
         TPUT   ERROR,80
         B     CARD
BRKTEST4 L     10,REGST       RESTORE RETURN ADDRESS
         BR    10
* TEST FOR A CORRECT CONTINUATION CARD
CONTCRD  L     2,CARDCNT      LOAD NUMBER OF RECORDS LEFT
         ST    10,REG10ST     STORE RETURN ADDRESS
         S     2,=F'1'
         ST    2,CARDCNT      REDUCE THIS BY ONE
         C     2,=F'0'
         BNH   CONT4          BRANCH IF NO CARDS LEFT
         L     1,REG1ST       LOAD START OF THIS CARD
         A     1,=F'80'       POINT TO NEXT CARD
         ST    1,REG1ST
         B     CONT6
CONT4    L     1,BUFFADD      LOAD ADDRESS OF NEXT BUFFER
         C     1,=F'0'
         BNE   CONT7          BUFFER AVAILABLE
* NO MORE BUFFERS SO INDICATE THAT A CONTINUATION
* CARD IS NEEDED BEFORE RETURNING
         ST    10,REG10ST     KEEP REG 10
         L     8,=F'1'
         ST    8,CONTFLAG      SET THE FLAG
         B     ENDJCL         GET ANOTHER BUFFER IF AVAILABLE
CONT7    MVC   CARDCNT+3(1),0(1)  LOAD NUMBER OF CARDS IN THIS BUFFER
         MVC   BUFFADD+1(3),1(1)  LOAD ADDRESS OF NEXT BUFFER
         A     1,=F'4'        POINT TO START OF FIRST RECORD
         ST    1,REG1ST
CONT6    LR    11,1
* PUT LINE NUMBER IN ERROR BUFFER
         MVC   ERROR+5(8),72(1)
         CLC   0(2,1),=C'//'
         BE    CONT1
* NO // ON CONTINUATION CARD
CONT5   MVC   ERROR+15(50),CONTSL
         TPUT   ERROR,80
         B     CARD
CONT1    L     9,=F'14'
         A     1,=F'2'
         CLC   0(1,1),=C' '   IS THERE A BLANK IN COLUMN 3
         BNE   CONT8          CONTINUATION CARD STARTS IN COLUMN 3
CONT2    CLC   0(1,1),=C' '
         BNE   CONT3
         A     1,=F'1'
         BCT   9,CONT2
* CARD STARTS AFTER COLUMN 16
CONT8    MVC   ERROR+15(50),CTOOSP
         TPUT   ERROR,80
         B     CARD
CONT3    L     10,REG10ST     RESTORE RETURN ADDRESS
         L     7,REG7ST       RESTORE REGISTER 7
         BR    10
* POINT TO NEXT RECORD
CARD     L     2,CARDCNT
         S     2,=F'1'        REDUCE COUNT BY ONE
         ST    2,CARDCNT
         C     2,=F'0'
         BNH   ENDJCL         NO RECORDSLEFT
         L     1,REG1ST
         A     1,=F'80'       POINT TO NEXT RECORD
         ST    1,REG1ST
         B     REC            SCAN THIS RECORD
ENDJCL   L     1,BUFFADD      LOAD ADDRESS OF NEXT BUFFER
         SR    6,6
         ST    6,CARDCNT      ZERO NUMBER OF CARDS IN BUFFER
         C     1,=F'0'
         BE    BACK           NO MORE BUFFERS
         B     BUFF           EXTRACT ADDRESSES FROM THIS BUFFER
BACK     L     13,4(13)
         LM    14,12,12(13)
         SR    15,15          SET CONDITION CODE TO ZERO
         BR    14             RETURN
         LTORG
REG1     DS    F
REG1ST   DS    F
REG7ST   DS    F
WORKAREA DC    4F'0'
REGST    DS    F
REG10ST  DS    F
CONTFLAG DC    F'0'
CARDCNT  DC    F'0'
BUFFADD  DC    F'0'
ALPHA    DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
NNALPH   DC    C'FIRST CHARACTER OF NAME NOT ALPHA OR *,#,$,&&     '
         DC    17C' '
ERROR    DC    80C' '
NAMLEN   DC    C'NAME HAS MORE THAN EIGHT CHARACTERS               '
         DC    17C' '
NONOPER  DC    C'NONVALID OPERATION CODE                           '
JOBSP    DC    C'JOB MUST BE FOLLOWED BY AT LEAST ONE SPACE        '
BRK      DC    C'NON MATCHING PARENTHESES                          '
SPACE    DC    C'ILLEGAL SPACE OR PARENTHESES NOT BALANCED         '
CONTSL   DC    C'MISSING // ON CONTINUATION CARD                   '
CTOOSP   DC    C'CONT CARD DOES NOT START BETWEEN COLS 4 AND 16    '
UNKEY    DC    C'UNIDENTIFIABLE KEYWORD FOUND                      '
ILLKEY   DC    C'NO = FOLLOWING A KEYWORD                          '
EXECSP   DC    C'EXEC MUST BE FOLLOWED BY AT LEAST ONE BLANK       '
DDSP     DC    C'DD MUST BE FOLLOWED BY AT LEAST ONE BLANK         '
MINFLAG  DC    F'0'
MAJMIN   DC    C'MAJOR KEYWORD PRECEDED BY MINOR KEYWORD           '
REG0     DC    C'REGION SIZE CODED AS ZERO                         '
REG      DC    C'REGION HAS K MISSING                              '
DISPAR1  DC    C'FIRST SUBPARAMETER OF DISP IS UNIDENTIFIABLE      '
DISPAR2  DC    C'SECOND SUBPARAMETER OF DISP IS UNIDENTIFIABLE     '
DISPAR3  DC    C'THIRD SUBPARAMETER OF DISP IS UNIDENTIFIABLE      '
DISPBRK  DC    C'UNBALANCED PARANTHESES IN DISP FIELD              '
DLMCHAR  DC    C'DELIMITER MUST BE TWO CHARACTERS                  '
DISK     DC    C'CODE DISK NOT 3330                                '
TAPE9    DC    C'CODE T0800,T1600,T6250 NOT TAPE9                  '
NAMSEP   DC    C'TOO MANY DDNAMES AFTER SEP KEYWORD                '
HOLDPARM DC    C'HOLD PARAMETER MUST BE EITHER YES OR NO           '
CONTEX   DC    C'CONTINUATION CARD EXPECTED BUT NO CARDS LEFT      '
TOOMC    DC    C'TOO MANY CHARACTERS IN PARM FIELD                 '
UNAPS    DC    C'ILLEGAL SPACE OR UNBALANCED APOSTROPHES           '
AFTNAM   DC    C'NAME MUST BE FOLLOWED BY BLANK, COMMA OR EQUALS   '
         END
