         TITLE 'FLASH2 MODULE 1'
         SPACE 2
         MACRO
&NAME    CONTROL
         LCLA  &N
         LCLC  &A
&NAME    DS    0A .                    CONTROL TABLE.
.LOOP    ANOP
&N       SETA  &N+1
&A       SETC  '01234567'(&N,1)
         DC    F'-1',X'15&A',XL3'0',XL6'0',C'?'
         AIF   ('&A' NE '7').LOOP
CONTABNA DC    X'FF'                   CONTROL TABLE END MARKER.
         MEND
         SPACE 3
         REGS  ,
         EJECT ,
CTRLBAR  EQU   R11
UCBBAR   EQU   R10
PARMBAR  EQU   R9
         SPACE 3
CONTROL  DSECT ,                       CONTROL TABLE ENTRY DSECT.
         USING *,CTRLBAR
CONFLAG  DS    0C                      CONTROL FLAGS.
CONLOC   EQU   0                       'UCB LOCATED'
CONPRT   EQU   X'80'                   PRINT REQUESTED.
CONNOPRT EQU   X'08'                   UNABLE TO PRINT.
CONSCR   EQU   X'40'                   SCRATCH TEMPORARY DATA SETS.
CONCAT   EQU   X'04'                   FLAG CATALOGUED DATA SETS.
CONEND   EQU   255                     END OF TABLE FLAG
CONUCBA  DS    A                       UCB ADDRESS.
CONUNITX DS    XL2                     UNIT IN HEX (0CUA)
CONVTOCA DS    XL3                     VTOC ADDRESS (TTR)
CONPACK  DS    CL6                     PACK IDENT..
CONMOD   DS    C                       MODULE LETTER.
CONENDA  EQU   *                       ENTRY END.
         SPACE 3
UCB      DSECT ,
         USING *,UCBBAR
         ORG   UCB+4
UCBUNITX DS    XL2                     UNIT IN HEX (XCUA).
         ORG   UCB+13
UCBUNITC DS    CL3                     UNIT IN CHARACTER.
         ORG   UCB+24
UCBSENSE DS    A                       A(ADDITIONAL SENSE INFORMATION).
UCBVOLI  DS    CL6                     VOLUME SERIAL NUMBER.
         ORG   UCB+36
UCBVTOC  DS    CL3                     VTOC ADDRESS.
         ORG   UCB+X'40'
UCBENDA  EQU   *                       UCB END ADDRESS.
         SPACE 3
PARM     DSECT ,
         USING *,PARMBAR
PARMLEN  DS    H                       PARM LENGTH FIELD.
PARMOP1  DS    X,X                     OPERAND 1.
PARML1   EQU   *-PARMOP1               LENGTH (MAX) IF OP2 OMITTED.
PARMOP2  DS    XL6                     OPERAND 2 ('ALL',XXXXXX OR NULL)
PARML2   EQU   *-3-PARMOP1             LENGTH IF OP2 = 'ALL'.
PARML3   EQU   *-PARMOP1               LENGTH IF OP2 = PACK IDENT..
         SPACE 3
         TITLE 'FLASH2 - A - VALIDATE PARM'
FLASH201 CSECT                         PRIMARY ENTRY POINT.
         ENTER 'FLASH201'
         SPACE 3
         L     PARMBAR,0(R1)           ADDRESS PARM AREA.
         NC    PARMLEN,PARMLEN         IF PARM SUPPLIED,
         BNZ   A100                    THEN BRANCH.
         WTO   'FLASH - PARM OMITTED - RUN TERMINATED',ROUTCDE=(11)
         B     D040                    EXIT.
A100     EQU   *
         CLI   PARMOP1,PARMNMAP        IF 'NO MAP' REQUESTED,
         BE    A300                    THEN BRANCH.
         CLI   PARMOP1,PARMMAP         IF 'MAP' NOT REQUESTED,
         BNE   A200                    THEN BRANCH.
         MVI   PARMOPT,PARMMAP         SET 'MAP REQUESTED' OPTION.
         B     A300
A200     EQU   *
         CLI   PARMOP1,C'S'            IF SCRATCH NOT REQUESTED,
         BNE   A250                    THEN BRANCH.
         MVI   SCRCAT,CONSCR           INDICATE SCRATCH TEMP DATA SETS.
         B     A300
A250     EQU   *
         CLI   PARMOP1,C'C'            IF CATALOGUED DATA SETS NOT TO
         BNE   A260                    BE FLAGGED , THEN BRANCH.
         MVI   SCRCAT,CONCAT           INDICATE CATALOG FLAGGING.
         B     A300
A260     EQU   *
         WTO   'FLASH - PARM OPERAND 1 INVALID - N SUBSTITUTED',       *
               ROUTCDE=(11)
A300     EQU   *
         CLI   PARMLEN+1,PARML1        IF OPERAND 2 OMITTED,
         BNH   A600                    THEN BRANCH.
         CLI   PARMLEN+1,PARML2        IF LENGTH OF OPERAND 2 NOT = 3,
         BNE   A400                    THEN BRANCH.
         CLC   =C'ALL',PARMOP2         IF 'ALL' NOT REQUESTED,
         BNE   A410                    THEN BRANCH.
         MVI   PARMPACK,PARMALL        INDICATE ALL PACKS TO BE LISTED.
         B     A600                    EXIT.
A400     EQU   *
         CLI   PARMLEN+1,PARML3        IF LENGTH OF OPERAND 2 IS 6.
         BE    A500                    THEN BRANCH.
A410     EQU   *
         WTO   'FLASH - PARM OPERAND 2 INVALID - NULLIFIED',ROUTCDE=11
         B     A600                    EXIT.
A500     EQU   *
         MVC   PARMPACK,PARMOP2        SAVE PACK IDENT..
A600     EQU   *
         TITLE 'FLASH2 - B - CREATE CONTROL TABLE'
         LA    CTRLBAR,CONTABLE        ADDRESS FIRST CONTROL ENTRY.
         LA    R1,MFTTEMP              R1 -> MFT UCB LOOK-UP ROUTINE.
         LA    R2,32                   R2 -> SVC OPSW.
         TM    1(R2),8                 IF IN BC MODE,
         BZ    B010                    THEN BRANCH.
         L     R1,16                   R1 -> CVT.
         L     R1,124(R1)              R1 -> IECIXAVL.
         L     R1,64(R1)               R1 -> UCB LOOK-UP ROUTINE.
B010     EQU   *
         STM   R14,R12,12(R13)         SAVE REGS..
         LH    R6,CONUNITX             R6 = CUA.
         LR    R15,R1                  R15 -> UCB LOOK-UP ROUTINE.
         BALR  R14,R15                 CALL UCB LOOK-UP.
         LTR   R15,R15                 TEST RETURN CODE.
         BNZ   B015                    BRANCH IF UCB NOT FOUND.
         ST    R7,CONUCBA              UCB ADDRESS INTO TABLE.
B015     EQU   *
         LM    R0,R12,20(R13)          RESTORE REGS 0 THRU 12.
         BNZ   B500                    BRANCH IF UCB NOT FOUND.
         MVI   CONFLAG,CONEND          DE-LIMIT TABLE.
         L     UCBBAR,CONUCBA          MAKE UCB ADDRESSABLE.
         MVC   UCBWORK,UCBUNITX        MOVE UNIT FROM UCB TO WORK AREA.
         NI    UCBWORK,15              TURN OFF FLAGS.
         CLC   UCBWORK,CONUNITX        IF LOCATED UCB DOES NOT MATCH
         BNE   B500                    TABLE ENTRY , THEN BRANCH.
         MVI   CONFLAG,CONLOC          INDICATE UCB LOCATED.
         CLI   PARMPACK,PARMALL        IF ALL PACKS TO BE LISTED,
         BNE   B021                    THEN
         OI    CONFLAG,CONPRT               TURN ON PRINT FLAG.
         OC    CONFLAG,SCRCAT          SET SCRATCH OR CATALOG FLAG.
B021     EQU   *
         MVC   CONPACK,UCBVOLI         MOVE PACK ID TO TABLE.
         NC    CONPACK,CONPACK         IF NO VOLUME MOUNTED,
         BNZ   B023                    THEN
         OI    CONFLAG,CONNOPRT             INDICATE UNABLE TO PRINT.
         B     B025
B023     EQU   *
         TM    PARMPACK,PARMALL        IF SELECTED PACK NOT TO BE
         BNM   B025                    LISTED , THEN BRANCH.
         CLC   CONPACK,PARMPACK        IF VOL MATCHES SELECTED PACK,
         BNE   B025                    THEN
         OI    CONFLAG,CONPRT               TURN ON PRINT FLAG,
         ST    CTRLBAR,PARMCTRL        AND SAVE ENTRY ADDRESS.
         OC    CONFLAG,SCRCAT          SET SCRATCH OR CATALOG FLAG.
B025     EQU   *
         MVC   CONVTOCA,UCBVTOC        VTOC ADDRESS TO TABLE.
         NC    CONVTOCA,CONVTOCA       IF VTOC ADDRESS NOT ZERO,
         BNZ   B027                    THEN BRANCH.
         OI    CONFLAG,CONNOPRT        INDICATE UNABLE TO PRINT.
B027     EQU   *
         L     R14,UCBSENSE            R14 -> ADDITIONAL SENSE INFO'N..
         LTR   R14,R14                 IF R14 ZERO,
         BZ    B029                    THEN CANNOT IDENTIFY MODULE.
         CLI   4(R14),0                IF MODULE NOT IDENTIFIABLE,
         BE    B029                    THEN BRANCH.
         MVC   CONMOD,4(R14)           MOVE PHYSICAL ADDRESS TO TABLE,
         NI    CONMOD,7                AND
         TR    CONMOD,=C'ABCDEFGH'         TRANSLATE.
B029     EQU   *
         TM    CONUNITX+1,7            IF END OF TABLE NOT REACHED,
         LA    CTRLBAR,CONENDA         THEN ADVANCE TO NEXT ENTRY,
         BNO   B010                    AND LOOP.
B500     EQU   *
         CLI   CONTABLE,CONEND         IF AT LEAST 1 UCB LOCATED,
         BNE   B510                    THEN BRANCH.
         WTO   'FLASH UNABLE TO LOCATE FIRST 3330 UCB',ROUTCDE=(11)
         WTO   'PLEASE PASS DUMP TO SYSTEM PROGRAMMER',ROUTCDE=(11)
         ABEND 1,DUMP
B510     EQU   *
         TITLE 'FLASH2 - C - CONTROL TABLE SCAN - LISTVTOC'
         LA    CTRLBAR,CONTABLE        ADDRESS FIRST CONTROL ENTRY.
C010     EQU   *
         TM    CONFLAG,CONNOPRT        IF VTOC LISTING NOT POSSIBLE,
         BO    C020                    THEN BRANCH.
         TM    CONFLAG,CONPRT          IF VTOC LISTING NOT REQUESTED,
         BNO   C020                    THEN BRANCH.
         CLC   CONPACK,=C'CEPACK'      DO NOT ATTEMPT A FLASH OF
         BE    C020                    A CE PACK.
         ICM   CTRLBAR,8,PARMPACK
         CALL  FLASH202                LIST VTOC.
C020     EQU   *
         LA    CTRLBAR,CONENDA         ADVANCE TO NEXT CONTROL ENTRY.
         CLI   CONUCBA,CONEND          IF END OF TABLE NOT REACHED,
         BNE   C010                    THEN LOOP.
         LA    CTRLBAR,CONTABNA        REQUEST PRINT FILE CLOSURE.
         CALL  FLASH202                CLOSE PRINT FILE.
         TM    PARMPACK,PARMALL        IF NOT SINGLE PACK REQUEST,
         BNM   C040                    THEN BRANCH.
         L     CTRLBAR,PARMCTRL        P/U CTRL ENTRY FOR SELECTED PACK
         LTR   CTRLBAR,CTRLBAR         IF PACK NOT MOUNTED,
         BZ    C030                    THEN BRANCH.
         CLI   CONFLAG,CONNOPRT        IF PRINT WAS POSSIBLE,
         BNE   C040                    THEN BRANCH.
C030     EQU   *
         MVC   WTOCPACK,PARMPACK       PACK TO MESSAGE.
WTOC     WTO   'FLASH UNABLE TO LIST SELECTED PACK ******',ROUTCDE=(11)
         ORG   WTOC+43
WTOCPACK DS    CL6
         ORG   ,
C040     EQU   *
         TITLE 'FLASH2 - D - CONTROL TABLE SCAN - MAP'
         CLI   PARMOPT,PARMMAP         IF MAP NOT REQUESTED,
         BNE   D040                    THEN BRANCH.
         LA    CTRLBAR,CONTABLE        ADDRESS FIRST CONTROL ENTRY.
D010     EQU   *
         NC    CONPACK,CONPACK         IF NO PACK ON THIS UNIT,
         BZ    D030                    THEN BRANCH.
         MVC   WTOPACK,CONPACK         PACK ID TO MESSAGE.
         MVC   WTOMOD,CONMOD           MODULE ID TO MESSAGE.
         L     UCBBAR,CONUCBA          MAKE UCB ADDRESSABLE.
         MVC   WTOUNIT,UCBUNITC        UNIT TO MESSAGE.
WTO      WTO   'MODULE X=XXX=XXXXXX',ROUTCDE=(2,11)
         ORG   WTO+15
WTOMOD   DS    C,C                     MODULE.
WTOUNIT  DS    CL3,C                   UNIT.
WTOPACK  DS    CL6                     PACK.
         ORG   ,
D030     EQU   *
         LA    CTRLBAR,CONENDA         ADVANCE TO NEXT CONTROL ENTRY.
         CLI   CONUCBA,CONEND          IF END OF TABLE NOT REACHED,
         BNE   D010                    THEN LOOP.
D040     EQU   *
         SPACE 3
         LEAVE ,                       RETURN TO OPERATING SYSTEM.
         TITLE 'MFTTEMP - TEMPORARY MFT UCB LOOK-UP ROUTINE'
MFTTEMP  EQU   *
         LA    R15,4                   RC = 0 = 'UCB NOT FOUND'.
         L     UCBBAR,MFTUCBA          PICK-UP LAST LOCATED UCB.
         LTR   UCBBAR,UCBBAR           IF AT LEAST ONE UCB LOCATED,
         LA    UCBBAR,UCBENDA          THEN ADVANCE TO NEXT UCB,
         BNZ   MFT020                  AND BRANCH.
         L     R7,16                   R7 -> CVT.
         L     R7,40(R7)               R7 -> UCB LOOK-UP TABLE.
MFT010 EQU     *
         LH    UCBBAR,0(R7)            PICK-UP ADDRESS FROM TABLE
         LA    R7,2(R7)                ADVANCE TO NEXT LOOK-UP ENTRY.
         LTR   UCBBAR,UCBBAR           IF UCB ADDRESS
         BZ    MFT010                  = 0 , THEN LOOP.
         BM    MFT030                  -VE , THEN EXIT (END OF TABLE)
         MVC   MFTWORK,UCBUNITX        MOVE UNIT TO WORK AREA,
         NI    MFTWORK,15              AND TURN OFF FLAGS.
         CH    R6,MFTWORK              IF UNIT DOES NOT MATCH SEARCH
         BNE   MFT010                  ARGUMENT , THEN BRANCH.
MFT020   EQU   *
         SR    R15,R15                 RC = 0 = UCB FOUND.
         LR    R7,UCBBAR               R7 -> UCB.
MFT030   EQU   *
         ST    UCBBAR,MFTUCBA          SAVE ADDRESS OF LOCATED UCB.
         BR    R14                     RETURN.
MFTUCBA  DC    A(0)                    UCB ADDRESS SAVE AREA.
MFTWORK  DC    H'0'
         TITLE 'FLASH2 - LITERALS / CONSTANTS / WORK-AREAS'
         LTORG ,
         SPACE 2
CONTABLE CONTROL ,
         SPACE 2
PARMOPT  DC    C'N'                    PARM 'MAP' OPTION.
PARMMAP  EQU   C'Y'                    MODULE MAP.
PARMNMAP EQU   C'N'                    NO MODULE MAP.
         SPACE
PARMPACK DC    XL6'0'                  PARM PACK SELECTION.
PARMALL  EQU   255                     ALL PACKS TO BE LISTED.
UCBWORK  DC    XL2'0'                  WORK AREA FOR UCB UNIT CHECKING.
SCRCAT   DC    X'0'                    SCRATCH / CATALOG FLAG INDICATOR
PARMCTRL DC    A(0)                    CTRL ENTRY ADDR FOR SINGLE PACK.
         END   FLASH201
