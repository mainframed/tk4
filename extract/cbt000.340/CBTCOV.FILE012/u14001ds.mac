SRE02    START
         TITLE 'RAAJ8SRE  M02 - SELECTIVE RECORD EDIT - PARAMETER VET &C
               & TABLE CONSTRUCTION MODULE (C.J.N.)'
         INMD
         LR    3,1
         USING QDUMMY,3
         L     4,QPARAM
         USING SPARAM,4
         L     RTP,QRTP
         L     LINE,QLINE
         CLI   SINDIC,X'C0'            2ND.OR SUBSEQ.ENTRY TO MODULE ?
         BNE   Z02A                     BR.IF NOT
         MVI   Z02C+1,X'F0'              OTHERWISE SET BR.NOT TO
*                                         VET OR REPRINT F/C PARAM(S).
*
***      CONTROL ROUTINE
*
Z02A     LR    GRA,RTP
         LA    GRB,79
         BAL   RETURNB,TEST1           TEST RTP TABLE FOR PARAM
         B     Z02G                     EXIT FOR NO PARAM FOUND
         CLC   0(7,1),CREC
         BNE   Z02B
         BAL   RETURNA,VETR1           VET RECERD PARAM.(PART 1)
         B     Z02G                     BR.TO AVOID PRINTING
Z02B     CLC   0(4,1),=C'END '         END PARAM.
         BE    Z03A
         CLC   0(2,1),=C'/*'           /*
         BE    Z03A
Z02C     BC    0,Z02E                  INVALID PARAM.
         CLC   0(8,1),CCONT
         BNE   Z02D
         BAL   RETURNA,VETC            VET CONTROL PARAM.
         B     Z02E                     ERROR EXIT
         B     Z02F                     NORMAL EXIT
Z02D     CLC   0(5,1),CFILE
         BNE   Z02E
         BAL   RETURNA,VETF            VET FILE PARAM.
         B     Z02F                     NORMAL EXIT
*
Z02E     MVC   2(18,LINE),CINVAL
Z02F     MVC   20(80,LINE),0(RTP)      PARAM.TO O/P AREA
         BAL   RETURNB,PRINT           O/X PARAM.TO FCP MODULE
Z02G     BC    0,Z03M                  BR.IF PARAM.I/P VIA CONSOLE
         LA    RTP,80(RTP)              OTHERWISE INC.
         ST    RTP,QRTP                  TO NEXT RTP(TABLE)
         B     Z02A                       AND BR.BACK TO VET IT
*
*
Z03A     CLI   Z02G+1,X'F0'            PARAMS FROM CONSOLE ?
         BNE   Z03B                     BR.IF NOT
         MVI   Z02G+1,X'00'              ELSE SET DUMMY TEST TO NO BR.
         OI    SINDIC,X'E0'               AND INDICATE ALL DETS.KNOWN.
Z03B     CLI   SINDIC,X'E0'            ALL PARAMS.PRESENT AND CORRECT ?
         BE    Z04A                     BR.IF YES TO VET RECORD (PT.2)
*
Z03C     LA    GRA,MESS1+35            )
         TM    SINDIC,X'80'            )
         BZ    Z03D                    )
         TM    SINDIC,X'08'            )
         BZ    Z03E                    )
Z03D     NI    SINDIC,X'77'            )
         MVC   1(4,GRA),CFILE          )
         MVI   5(GRA),C','             )DETERMINE WHICH
         LA    GRA,5(GRA)              ) PARAMS.NEED TO
Z03E     TM    SINDIC,X'40'            )  BE I/P VIA CONSOLE
         BZ    Z03F                    )   AND SET OF RELEVANT
         TM    SINDIC,X'04'            )    'INCORRECT'INDICATORS
         BZ    Z03G                    )
Z03F     NI    SINDIC,X'BB'            )
         MVC   1(7,GRA),CCONT          )
         MVI   8(GRA),C','             )
         LA    GRA,8(GRA)              )
Z03G     TM    SINDIC,X'20'            )
         BZ    Z03H                    )
         TM    SINDIC,X'02'            )
         BZ    Z03J                    )
Z03H     NI    SINDIC,X'DD'            )
Z03HA    MVC   1(6,GRA),CREC           )
         LA    GRA,7(GRA)              )
Z03J     MVI   0(GRA),C')'             )
Z03K     WTOR  MF=(E,MESS1)            ) REQUEST PARAMS.
         WAIT  ECB=ECB1                )  VIA CONSOLE
         NI    ECB1,X'00'
         MVI   MESS1+36,X'40'          AND CLEAR
         MVC   MESS1+37(19),MESS1+36     CONSOLE O/P AREA
         CLI   ANS,C'7'                ANY MORE PARAMS.
         BE    Z03L
         MVI   Z04D+1,C'E'              IF NOT,INDICATE END
         B     Z04C                      AND BR.TO EXIT MODULE
Z03L     MVI   Z02G+1,X'F0'            SET DUMMY TEST TO BRANCH
         LA    RTP,CPARAM              SET RTP REG = RECEIVING AREA
Z03M     MVI   CPARAM,X'40'            CLEAR
         MVC   CPARAM+1(79),CPARAM      RECEIVING AREA
         WTOR  MF=(E,MESS2)
         WAIT  ECB=ECB2
         NI    ECB2,X'00'
         DOMD  SRE06,MESS2
         LA    RTP,CPARAM
         B     Z02A
*
*
Z04A     EQU   *
*
         BAL   RETURNA,VETR2           VET RECORD PARAM - PT2
         B     Z04B                        ERROR EXIT
         B     Z04C                        NORMAL EXIT
Z04B     LA    GRA,MESS1+35            ERROR - GRA = 1ST.PART OF MESS.
         B     Z03H                     AND BR.TO REQUEST RECORD PARAM.
Z04C     L     GRA,QINDIC
Z04D     MVI   2(GRA),C'C'             SET ON 'COMPLETE' INDIC
Z04E     B     Z04F
         MVC   QRTP(4),RECRTP          STORE ADDR.OF NEXT
         MVI   Z04E+1,X'F0'             RECORD PARAM.WHERE APPROPRIATE
         MVI   Z02C+1,X'00'
Z04F     OUTMD
*
*
*
         EJECT
*
***      VETC - ROUTINE TO VET CONTROL PARAMETER
*
VETC     TM    SINDIC,X'40'            CONTROL PARAM.ALREADY READ ?
         BO    0(RETURNA)               EXIT ROUTINE IF YES
         OI    SINDIC,X'40'            INDICATE CONTROL PARAM READ
*
         LA    GRA,8(1)
         LA    GRB,79(RTP)
         SR    GRB,GRA
         BAL   RETURNB,TEST1           ACCESS 1ST.CONTROL PARAM.
         B     4(RETURNA)              EXIT ROUTINE IF NONE FOUND
         B     Z10B                      OTHERWISE BR.TO VET PARAM.
*
Z10A     BAL   RETURNB,TEST4           ANY MORE ?
         B     4(RETURNA)               NO - NORMAL EXIT
         B     Z10C                     NO - ERROR EXIT
Z10B     CLC   0(7,1),=C'FIELD=('       YES
         BE    Z11A                    BR.TO VET FIELD
         CLC   0(5,1),=C'KEY=('
         BE    Z12A                    BR.TO VET KEY
         CLC   0(6,1),=C'SEARCH'
         BE    Z13A                    BR.TO VET SEARCH
         CLC   0(5,1),=C'BLOCK'
         BE    Z14A                    BR.TO VET BLOCK
*
*                         ERROR ROUTINE
Z10C     OI    SINDIC,X'04'            SET ON INCORRECT INDICATOR
         MVI   SFORMAT,C'C'            )
         MVI   SCLTH,X'00'             )STANDARDIZE
         MVC   SCDISP(2),XEFFS         ) SELECTION
         MVI   SSEARCH,X'00'           )  PARAMETER
         MVI   SKEY,X'00'              )   TABLE
         MVC   SKDISP(2),XEFFS         )
         BR    RETURNA
*
*                         VET FIELD
Z11A     CLC   SCDISP(2),XEFFS
         BNE   Z10C                    ERROR - FIELD ALREADY READ
         LA    GRA,7(1)
         LA    GRB,4
         BAL   RETURNB,TEST2           VET DISPLACEMENT
         B     Z10C                      ERROR - TOO LONG
         LR    GRB,1
         SR    GRB,GRA
         BAL   RETURNB,TEST3
         B     Z10C                      ERROR - NON-NUMERIC
         STC   GRB,Z11B+1
         OI    Z11B+1,X'70'
Z11B     PACK  DWORD1(8),0(1,GRA)
         CVB   GRC,DWORD1
         STH   GRC,SCDISP              STORE DISPLACEMENT
         LA    GRA,2(GRB,GRA)           AND INC.GRA TO LENGTH
*
         LA    GRB,2                   VET LENGTH
         BAL   RETURNB,TEST2
         B     Z10C                      ERROR - TOO LONG
         LR    GRB,1
         SR    GRB,GRA
         BAL   RETURNB,TEST3
         B     Z10C                      ERROR - NON-NUMERIC
         STC   GRB,Z11C+1
         OI    Z11C+1,X'70'
Z11C     PACK  DWORD1(8),0(1,GRA)
         CVB   GRC,DWORD1
         BCTR  GRC,0
         STC   GRC,SCLTH               STORE LENGTH
         LA    GRA,2(GRB,GRA)           AND INC.GRA TO FORMAT
*
         CLI   1(GRA),C')'             VET FORMAT
         BNE   Z10C                      ERROR - INVALID PARAM.
         MVI   TRTAB+195,X'13'
         MVI   TRTAB+215,X'0C'
         MVI   TRTAB+231,X'0B'
         TRT   0(1,GRA),TRTAB
         MVC   TRTAB+195(37),TRTAB
         BC    8,Z10C                    ERROR - INVALID FORMAT
         STC   2,Z11D+1
Z11D     CLI   SCLTH,X'00'
         BH    Z10C                      ERROR - FIELD LTH.TOO LONG
         MVC   SFORMAT(1),0(GRA)       STORE FORMAT
         LA    1,2(GRA)
         B     Z10A
*
*                         VET KEY
Z12A     CLC   SKDISP(2),XEFFS
         BNE   Z10C                    ERROR - KEY ALREADY READ
         LA    GRA,5(1)
         LA    GRB,4
         BAL   RETURNB,TEST2           VET DISPLACEMENT
         B     Z10C                      ERROR - TOO LONG
         LR    GRB,1
         SR    GRB,GRA
         BAL   RETURNB,TEST3
         B     Z10C                      ERROR - NON-NUMERIC
         STC   GRB,Z12B+1
         OI    Z12B+1,X'70'
Z12B     PACK  DWORD1(8),0(1,GRA)
         CVB   GRC,DWORD1
         STH   GRC,SKDISP              STORE DISPLACEMENT
         LA    GRC,2(GRB,GRA)          INC R1 TO KEY
         CLI   1(GRC),C')'
         BNE   Z10C                    ERROR - INVALID PARAM
         MVC   SKEY(1),0(GRC)          STORE KEY
         LA    1,2(GRC)
         B     Z10A
*
*                         VET SEARCH
Z13A     CLI   SSEARCH,X'00'
         BNE   Z10C                    ERROR - SEARCH ALREADY READ
         MVI   SSEARCH,C'S'            INDICATE SEARCH IN PARAM.TABLE
         LA    1,6(1)
         B     Z10A
*
*                       VET BLOCK
Z14A     CLI   SSEARCH,X'00'
         BNE   Z10C                    ERROR - BLOCK ALREADY READ
         MVI   SSEARCH,C'B'            IND.BLOCK EDIT IN PARAM.TAB.
         MVI   SFORMAT,C'P'            SET UP COMPARE
         MVI   SCLTH,X'02'               CONTROL
         MVC   SCDISP,=X'0000'         PREVENT FIELD BEING PROCESSED
         LA    1,5(1)
         B     Z10A
         EJECT
*
***      VETF - ROUTINE TO VET FILE PARAMETER
*
VETF     TM    SINDIC,X'80'            FILE  PARAM.ALREADY READ ?
         BO    4(RETURNA)              EXIT ROUTINE IF YES
         OI    SINDIC,X'80'            INDICATE FILE  PARAM.READ
*
         LA    GRA,5(1)
         LA    GRB,79(RTP)
         SR    GRB,GRA
         BAL   RETURNB,TEST1           ACCESS 1ST.FILE PARAM
         B     0(RETURNA)              FXIT ROUTINE IF NONE FOUND
         B     Z20B                     OTHERWISE BR.TO VET PARAM.
*
Z20A     BAL   RETURNB,TEST4           ANY MORE ?
         B     Z20C                     NO - NORMAL EXIT
         B     Z20F                     NO - ERROR EXIT
Z20B     CLC   0(5,1),=C'TYPE='         YES
         BE    Z21A                    BR.TO VET TYPE
         CLC   0(7,1),=C'LENGTH='
         BE    Z22A                    BR.TO VET LENGTH
         B     Z20F                    ERROR - INVALID PARAM.
*
*                         NORMAL EXIT
Z20C     CLI   Z21A+1,X'F0'
         BNE   Z20F                    ERROR - NO TYPE
         CLC   SRLTH(2),=X'0000'
         BE    Z20D
         CLI   STYPE,C'F'
         BE    Z20G
         B     Z20F                    ERROR - TYPE U OR V WITH LENGTH
Z20D     CLI   STYPE,C'F'              IF TYPE F AND NO LENGTH - ERROR
         BNE   Z20G
*
*                         ERROR EXIT
Z20F     OI    SINDIC,X'08'            SET ON INCORRECT INDICATOR
         MVI   STYPE,C'U'               AND STANDARDISE
         XC    SRLTH(2),SRLTH            SELECTION PARAMETER TABLE
         LA    RETURNA,4(RETURNA)
*
Z20G     MVI   Z21A+1,X'00'
         MVI   Z22A+1,X'00'
Z20H     BR    RETURNA                 RETURN TO CONTROL ROUTINE
*
*                         VET TYPE
Z21A     BC    0,Z20F                  ERROR - TYPE ALREADY READ
         MVI   Z21A+1,X'F0'            INDICATE TYPE READ
         LA    GRA,5(1)
         MVI   TRTAB+198,C'F'
         MVI   TRTAB+228,C'U'
         MVI   TRTAB+229,C'V'
         MVI   TRTAB+201,C'I'          ISAM
         TRT   0(1,GRA),TRTAB
         MVC   TRTAB+198(32),TRTAB
         BC    8,Z20F                  ERROR - INVALID TYPE
         STC   2,STYPE                 STORE TYPE
         LA    1,1(GRA)
         B     Z20A
*                         VET LENGTH
*
Z22A     BC    0,Z20F                  ERROR - LENGTH ALREADY READ
         MVI   Z22A+1,X'F0'            INDICATE LENGTH READ
         LA    GRA,7(1)
         LA    GRB,5
         BAL   RETURNB,TEST3
         B     Z22B
         B     Z20F                    ERROR - TOO LONG
Z22B     BAL   RETURNB,TEST4
         B     Z22C
         B     Z20F                    ERROR - NON-NUMERIC
         BCTR  1,0
Z22C     LR    GRB,1
         SR    GRB,GRA
         BCTR  GRB,0
         STC   GRB,Z22D+1
         OI    Z22D+1,X'70'
Z22D     PACK  DWORD1(8),0(1,GRA)
         CVB   GRC,DWORD1
         STH   GRC,SRLTH               STORE LENGTH
         B     Z20A
*
         EJECT
*
***      VETR1 - ROUTINE TO VET RECORD PARAMETER (PART 1)
*
VETR1    TM    SINDIC,X'20'            RECORD PARAM.ALREADY READ ?
         BCR   1,RETURNA                EXIT ROUTINE IF YES
         OI    SINDIC,X'20'            INDICATE RECORD PARAM READ
*
         CLI   Z02G+1,X'F0'            PARAMS.VIA CONSOLE ?
         BE    Z30A
         LA    GRA,80(RTP)             IF NOT STORE ADDRESS OF
         ST    GRA,RECRTP               NEXT VALID PARAM IN RTP TABLE
         MVI   Z04E+1,X'00'
*
Z30A     MVC   RECSTO,0(RTP)           STORE PARAM.
         MVI   Z40AA+1,X'F0'           SPECIAL INDIC FOR S/RTN VETR2
         BR    RETURNA
*
         EJECT
*
***      VETR2 - ROUTINE TO VET RECORD PARAMETER (PART2)
*
VETR2    MVC   SLOW(40),TRTAB          SET PARAM.TABLE LIMITS TO ZEROS
Z40AA    BC    0,Z40AB                 BR.EXCEPT FOR NO CONSOLE PARAM.
Z40AAA   MVI   SHIGH,X'FF'               OTHERWISE SET HIGH
         MVC   SHIGH+1(19),SHIGH          LIMIT TO HEX.'FF'S
         CLI   SFORMAT,C'P'       IS IT A PACKED COMPARE ? (BLOCK EDIT)
         BNE   4(RETURNA)              NO
         OI    SLOW+2,X'0F'            YES SET UP SIGN
         ZAP   SHIGH(3),=P'99999'          ON BOTH LIMITS
*
         B     4(RETURNA)                  AND EXIT ROUTINE (NORMAL)
*
Z40AB    MVI   Z40AA+1,X'00'
         LA    RTP,RECSTO
         MVC   20(80,LINE),0(RTP)      PARAM.TO O/X AREA
         LA    GRA,RECSTO              LOCATE
         LA    GRB,79                   1ST.VALID
         BAL   RETURNB,TEST1             DIGIT IN FIELD
         B     Z42A                    DUMMY ERROR
         LA    GRA,7(1)                GRA = 1ST.BYTE PAST 'RECORD '
         LA    GRB,RECSTO+79
         SR    GRB,GRA
         BAL   RETURNB,TEST1
         B     Z40AC               NO ENTRIES ON RECORD PARAM .
*
         MVI   Z40C+1,X'00'             IN PARAM TABLE
         MVI   Z40E+1,X'00'              SET OFF LIMITS READ
         B     Z40B                       BRANCHES,AND BR.TO VET
Z40AC    BAL   RETURNB,PRINT           IF NO ENTRIES ON RECORD PARAM
         B     Z40AAA                   PRINT PARAM & GO TO SET UP
*                                       DEFAULTS.
Z40A     BAL   RETURNB,TEST4           ANY MORE ?
         B     Z41A                     NO - NORMAL EXIT
         B     Z42A                     NO - ERROR EXIT
Z40B     CLC   0(5,1),=C'LOW=('         YES - TEST IF LOW LIMIT
         BNE   Z40D                            BR.IF NOT TO TEST IF HI.
Z40C     BC    0,Z42A                  ERROR - LOW ALREADY READ
         MVI   Z40C+1,X'F0'            INDICATE LOW READ
         LA    GRA,5(1)                GRA = 1ST.BYTE OF LIMIT
         LA    GRC,SLOW                STORE ADDR.OF PARAM.TABLE
         ST    GRC,DWORD1               LOWER LIMIT IN DWORD1(4)
         B     Z43A                      AND BR.TO VET LIMIT
Z40D     CLC   0(6,1),=C'HIGH=('
         BNE   Z42A                    ERROR - INVALID PARAM.
Z40E     BC    0,Z42A                  ERROR - HIGH ALREADY READ
         MVI   Z40E+1,X'F0'            INDICATE HIGH READ
         LA    GRA,6(1)                GRA = 1ST.BYTE OF LIMIT
         LA    GRC,SHIGH               STORE ADDR.OF PARAM.TABLE
         ST    GRC,DWORD1               HIGHER LIMIT IN DWORD1(4)
         B     Z43A                      AND BR.TO VET LIMIT
*
Z40F     L     GRC,DWORD1
         MVC   0(20,GRC),0(1)          STORE CONVERTED LIMIT
         LA    1,2(GRB,GRA)             INC.R1 TO NEXT
         B     Z40A                      AND BR.BACK.TO TEST
*
*                         NORMAL EXIT
Z41A     CLI   Z40C+1,X'F0'            LOW READ ?
         BE    Z41B                     BR.IF YES
         MVC   SLOW(20),SHIGH            OTHERWISE MOVE HIGH TO LOW
         B     Z41G                       AND BR.TO O/P PARAM.TO PRT.
Z41B     CLI   Z40E+1,X'F0'            HIGH READ ?
         BE    Z41C                     BR.IF YES
         MVC   SHIGH(20),SLOW            OTHERWISE MOVE LOW TO HIGH
         B     Z41G                       AND BR.TO O/P PARAM.TO PRT.
Z41C     CLI   SFORMAT,C'P'            )
         BNE   Z41E                    )
         IC    GRC,SCLTH               )
         SLL   GRC,4                   )
         XR    GRB,GRB                 )
         IC    GRB,SCLTH               )
         AR    GRC,GRB                 )CF.LIMITS -
         STC   GRC,Z41D+1              ) LOW V HIGH
Z41D     CP    SLOW(1),SHIGH(1)        )  ERROR IF LOW GREATER THAN HI.
         BH    Z42A                    )   OTHERWISE BR.TO O/P PARAM.
         B     Z41G                    )
*                                      )
Z41E     MVC   Z41F+1(1),SCLTH         )
Z41F     CLC   SLOW(1),SHIGH           )
         BH    Z42A                    )
Z41G     LA    RETURNA,4(RETURNA)      )
         B     Z42B                    )
*
*
Z42A     MVC   2(18,LINE),CINVAL
Z42B     BAL   RETURNB,PRINT
         MVI   Z62D+1,X'F0'            )
         MVI   Z62A+1,X'F0'            )RESET
         MVI   Z43G+1,X'00'            ) ALL BRANCHES
         MVI   Z43E+1,X'00'            )
         BR    RETURNA                 EXIT ROUTINE
*
*                         VET LIMIT (HIGH OR LOW)
Z43A     MVI   TRTAB+93,X'FF'
         MVI   TRTAB+77,X'01'
         CLI   SFORMAT,C'P'
         BH    Z43B
         BL    Z43C
         MVI   Z43D+1,X'19'            SET UP CONDS.FOR
         MVI   Z62D+1,X'00'             VETTING PACKED LIMIT
         B     Z43D
Z43B     MVI   Z43D+1,X'18'            SET UP CONDS.
         MVI   Z62A+1,X'00'             FOR VETTING
         MVI   Z43G+1,X'F0'              HEX.LIMIT
         B     Z43D
Z43C     MVI   Z43D+1,X'14'            SET UP CONDS.FOR
         MVI   Z43E+1,X'F0'             VETTING CHAR.LIMIT
*
Z43D     TRT   0(1,GRA),TRTAB
         MVI   TRTAB+93,X'00'
         MVI   TRTAB+77,X'00'
         BC    8,Z42A                  ERROR - GREATER THAN ALLOWED LTD
         N     2,FNO0F
         BCT   2,Z43DA
         B     Z42A                    ERROR - ')' NOT ON END OF LIMIT
Z43DA    LR    GRB,1
         SR    GRB,GRA                 GRB = LTH.OF LIMIT FIELD
Z43E     BC    0,Z43R                  BR.FOR CHAR. LIMIT
Z43F     BAL   RETURNB,TEST3
         B     Z42A                    ERROR - INVALID BYTE IN LIMIT
Z43G     BC    0,Z43P                  BR.FOR HEX.LIMIT
*                                       ELSE PACK LIMIT TO PARAM TABLE
Z43H     CH    GRB,FNO0F+2             CF LIMIT LTH.V 16
         BNH   Z43L                     BR.IF L/E FOR ONE-STAGE PACK
Z43HA    LR    GRC,GRB                   )
         SH    GRC,FNO0F+2               )PACK LAST
         STC   GRC,Z43J+5                ) 16 DIGITS
Z43J     PACK  GASH(13),0(16,GRA)        )
         BCTR  GRC,0
         STC   GRC,Z43K+1                PACK
         OI    Z43K+1,X'40'             REMAINING
Z43K     PACK  TRTAB(5),0(1,GRA)         DIGITS
         MVN   TRTAB+4(1),GASH+4          AND COLLATE
         MVC   GASH(5),TRTAB               TO ONE
         MVC   TRTAB(5),TRTAB+5             FIGURE
Z43KC    LA    1,GASH+12               SET R1
         XR    GRC,GRC                  TO START
         IC    GRC,SCLTH                 OF FIELD
         SR    1,GRC                      TO MOVE
         B     Z40F                        AND BR.TO MOVE
Z43L     IC    GRC,SCLTH               )
         SLL   GRC,4                   )ONE-STEP
         AR    GRC,GRB                 ) PACK
         STC   GRC,Z43M+1              )
Z43M     PACK  GASH(1),0(1,GRA)        )
Z43N     LA    1,GASH                  SET R1 = START OF FIELD
         B     Z40F                     TO MOVE AND BR.TO MOVE
*
Z43P     STC   GRB,Z43Q+1                        )
         TM    Z43Q+1,X'01'                      )
         BZ    Z42A                              )TRANSLATE
         MVC   TRTAB+193(6),=X'FAFBFCFDFEFF'     ) A-F FOR
         MVC   TRTAB+240(10),=C'0123456789'      )  PACKING
Z43Q     TR    0(1,GRA),TRTAB                    )
         MVC   TRTAB+193(59),TRTAB               )
         LR    GRC,GRA                 )
         LA    2,1(GRB)                )
         SRL   2,1                     )
         LA    1,GASH                  )
Z43QA    MVC   0(1,1),0(GRC)           )CONVERT LIMIT
         MVO   0(1,1),0(1,1)           ) TO HEXADECIMAL
         MVN   0(1,1),1(GRC)           )
         LA    GRC,2(GRC)              )
         LA    1,1(1)                  )
         BCT   2,Z43QA                 )
         B     Z43N
*
Z43R     MVI   Z43E+1,X'00'            CHARACTER LIMIT :
         BCTR  GRB,0
         STC   GRB,Z43S+1
Z43S     CLI   SCLTH,X'00'              CF.FIELD LTHS - SPEC.V PARAM.
         BH    Z43T                      BR.IF PARAM L TO ZERO-FILL
         LR    1,GRA                      OTHERWISE SET R1 = START OF
         B     Z40F                        FIELD AND BR.TO MOVE
Z43T     XR    GRC,GRC
         XC    GASH(20),GASH
         IC    GRC,SCLTH
         LR    2,GRB
         SR    GRC,2
         LA    1,GASH
         AR    1,GRC
         STC   2,Z43U+1
Z43U     MVC   0(1,1),0(GRA)
         B     Z43N
*
         EJECT
*
***      S/RTN PRINT - O/P PARAMETER TO FCP MODULE FOR PRINTING
*
PRINT    MVI   0(LINE),X'01'           CC TO O/P AREA
         L     GRC,QINDIC
         MVI   1(GRC),C'P'             INDICATE PRINT
         ST    1,DWORD1+4
         LR    1,3                     SET R1 FOR FCP MODULE
         DOMD  SRE00                   BR.TO FCP MODULE TO PRINT
         L     1,DWORD1+4
         L     GRC,QCOUNT
         LH    GRA,0(GRC)
         BCTR  GRA,0                   SUBTRACT 1 FROM LINE COUNT
         STH   GRA,0(GRC)
         ST    GRC,QCOUNT
         BR    RETURNB
*
*
         EJECT
*
***      S/RTN TEST1 - TEST FIELD OF SPECIFIED LTH.FOR NON-SPACE
*
TEST1    MVI   TRTAB+65,X'FF'          GRA = START OF FIELD
         MVC   TRTAB+66(190),TRTAB+65
         MVC   TRTAB(64),TRTAB+66      GRB = LTH OF (FIELD - 1)
         STC   GRB,Z60A+1
Z60A     TRT   0(1,GRA),TRTAB
         MVI   TRTAB,X'00'
         MVC   TRTAB+1(255),TRTAB
         BCR   8,RETURNB               ERROR EXIT - FIELD ALL SPACES
         B     4(RETURNB)              NORMAL EXIT
*
         SPACE 4
*
***      S/RTN TEST2 - TEST FOR COMMA.
*
TEST2    MVI   TRTAB+107,X'FF'         GRA = START OF FIELD
         STC   GRB,Z61A+1
Z61A     TRT   0(1,GRA),TRTAB          GRB = LTH.OF (FIELD - 1)
         MVI   TRTAB+107,X'00'
         BCR   8,RETURNB               ERROR EXIT - NO COMMA FOUND
         B     4(RETURNB)              NORMAL EXIT
*
         EJECT
*
***      S/RTN TEST3 - TEST FOR NUMERIC ONLY OR NUMERIC AND A-F ONLY
*
TEST3    MVI   TRTAB,X'FF'
         MVC   TRTAB+1(239),TRTAB
         MVC   TRTAB+250(5),TRTAB
         MVI   TRTAB+208,X'01'
         MVC   TRTAB+209(9),TRTAB+208
Z62A     B     Z62B                    BR.FOR NUMERIC ONLY (INC.NEG.)
         MVC   TRTAB+193(6),TRTAB+240   ELSE NUMERIC AND A-F ONLY
         MVI   Z62A+1,X'F0'
Z62B     BCTR  GRB,0                   GRA = START OF FIELD
         STC   GRB,Z62C+1
Z62C     TRT   0(1,GRA),TRTAB          GRB = LTH.OF FIELD
         MVI   TRTAB,X'00'
         MVC   TRTAB+1(255),TRTAB
*
         BC    8,4(RETURNB)            NORMAL EXIT - NO INVALID CHARS.
         BC    2,Z62D                  BR.IF LAST CHAR.INVALID
Z62CA    BR    RETURNB                  OTHERWISE ERROR EXIT
Z62D     B     Z62CA                   ERROR IF NOT LIMIT TO BE PACKED
         N     2,FNO0F                  YES - LAST CHAR.MINUS NO.?
         BCTR  2,RETURNB                 NO - ERROR EXIT
         B     4(RETURNB)                YES - NORMAL EXIT
*
         SPACE 4
*
***      S/RTN TEST4 - TEST IF ANOTHER VALID FIELD IN PARAM
*
TEST4    CLI   0(1),X'40'              R1+0 = SPACE ?
         BE    0(RETURNB)               YES - VALID - END OF PARAM.DETS
         CLI   0(1),C','               R1+0 = COMMA ?
         BNE   4(RETURNB)               NO  - INVALID PARAM.
         LA    1,1(1)                    OTHERWISE INC.R1 TO NEXT FIELD
         B     8(RETURNB)                 IN PARAM.AND BR.TO VET IT
*
         LTORG
*
         EJECT
*
***            STORAGE AND CONSTANTS
*
DWORD1   DS    D
ECB1     DC    F'0'
ECB2     DC    F'0'
RECRTP   DS    F
FNO0F    DC    F'15'
TRTAB    DC    256X'00'
CCONT    DC    C'CONTROL '
CFILE    DC    C'FILE '
CREC     DC    C'RECORD '
CINVAL   DC    C'INVALID PARAMETER:'
MESS1    WTOR  'IMSG  J8S1 TYPE PARAMS.(                    ',ANS,1,ECBC
               1,MF=L
MESS2    WTOR  'PLEASE CONTINUE',CPARAM,80,ECB2,MF=L
ANS      DS    C
CPARAM   DC    CL80' '
XEFFS    DC    X'FFFF'
RECSTO   DS    CL80
GASH     DS    21C
*
         EJECT
*
***            DUMMY SECTION FOR INTER-MODULE PARAMETERS
*
QDUMMY   DSECT
QPARAM   DS    F            ADDRESS OF SELECTION PARAMS
QINDIC   DS    F                       INDICATOR BYTES
         DS    2F
QLINE    DS    F                       OUTPUT AREA
QCOUNT   DS    F                       LINE COUNT
         DS    2F
QRTP     DS    F                       RUN-TIME PARAMETER
*
***            DUMMY SECTION FOR SELECTION PARAMETER TABLE
*
SPARAM   DSECT
STYPE    DS    C                       FILE TYPE (V,F,OR U)
SINDIC   DS    C                       INDICATOR BYTE
SRLTH    DS    H                       REC.LTH.(TYPE F ONLY)
SFORMAT  DS    C                       CONTROL FORMAT (X,C,OR P)
SCLTH    DS    C                       CONTROL LTH.(M/C LTH.)
SCDISP   DS    H                       CONTROL DISPLACEMENT
SSEARCH  DS    C                       SEARCH(S) OR BLOCK (B)
SKEY     DS    C                       KEY (IF SPECIFIED)
SKDISP   DS    H                       KEY DISP.(FFFF IF NOT)
SLOW     DS    20C                     LOW CONTROL
SHIGH    DS    20C                     HIGH CONTROL
SRE02    CSECT
*
         EJECT
*
***            REGISTER NAMES
*
RTP      EQU   5
LINE     EQU   6
RETURNA  EQU   7
RETURNB  EQU   8
GRA      EQU   9
GRB      EQU   14
GRC      EQU   15
*
         END
