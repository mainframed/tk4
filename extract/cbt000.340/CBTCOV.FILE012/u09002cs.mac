         TITLE 'ZSEPARAT: INVOKE PAGE SEPARATOR ROUTINE'
         PRINT NOGEN
         SPACE 3
* TO ENTER:
*        CALL ZSEPARAT,(NAME,LEN,DCB),VL
*        WHERE:-
*             NAME IS ADDR OF CHARS TO BE PRINTED IN BLOCK FORM
*             LEN  IS LENGTH OF NAME (F/WORD IN BINARY)
*             DCB  IS DCB OF FILE TO WHICH PAGE SEPARATOR DIEECTED
         EJECT
ZSEPARAT CSECT
         INIT
         USING IHADCB,R5
         LM    R3,R5,0(R1)
         MVC   P,P-1  CLEAR PRINT LINE
         TM    DCBRECFM,ASA   ASA CONTROL CHARACTERS REQUIRED ?
         BO    ASACC YES
         MVI   P,X'8B'  NO, MCH IMMEDIATE SKIP TO 1
         BAL   R14,PR
         MVI   P,X'1B'   IMMEDIATE SPACE 3
         BAL   R14,PR
         MVI   P,X'09'  PRINT & SPACE 1
         B     IN
         SPACE
ASACC    MVI   P,C'1'  ASA SKIP TO 1
         BAL   R14,PR
         MVI   P,C'-'   ASA SPACE 3 BEFORE PRINTING
         SPACE
IN       LA    R9,1  INITIALIZE LINE COUNT
         B     FIRST
NAMELOOP LA    R9,1(R9)  INVREMENT LINE COUNR
         MVC   P,P-1
         TM    DCBRECFM,ASA
         BO    FIRST
         MVI   P,X'09'
FIRST    ST    R9,LINO
         CALL  IEFSD095,((R3),LINO,P+1,(R4)),VL
         BAL   R14,PR
         C     R9,=F'12'  HAVE ALL LINES BEEN PRINTED
         BL    NAMELOOP  NO
         SPACE
         EXIT      YES
         SPACE
         DS    F
PR       ST    R14,*-4
         PUT   (R5),P
         L     R14,PR-4
         BR    R14
         SPACE 3
ASA      EQU   B'00000100' INDICATES THAT ASA CONTROL CHARACTERS REQD.
LINO     DS    F
         DC    C' '
P        DS    CL133
         LTORG
         DCBD  DEVD=DA,DSORG=PS
         END
