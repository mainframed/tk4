         MACRO
&TAG     TIOT
&TAG     DS    0H
         L     1,16          CVT ADDRESS
         L     1,0(,1)       TCB/ASCB WORDS ADDRESS
         L     1,12(,1)      OUR ASCB ADDRESS
         L     1,28(,1)      OUR TCB ADDRESS
         L     1,12(,1)      OUT TIOT ADDRESS
         MEND
SU33SPL  ENTER 13,12,ENT=SPLITTER,VERS=2.0
         PARMSCAN (SPLIT=0,ACT=0,SOUT=,FORM=,REPRINT=)
         STC   0,PARMFLG
         TM    PARMFLG,REPRINT
         BNO   *+8
         OI    FLAG,REPMARK
         TM    PARMFLG,ACT
         BNO   DOSOUT
         L     9,=A(KEY2)
         MVC   JCARD+16(3),5(9)
DOSOUT   TM    PARMFLG,SOUT
         BNO   DOFORM
         L     9,=A(KEY3)
         MVC   CLASS(1),0(9)
DOFORM   TM    PARMFLG,FORM
         BNO   DOSPLT
         L     9,=A(KEY4)
         MVC   FORMTYPE+1(1),CLASS
         MVC   FORMTYPE+4(4),0(9)
         MVC   CLASS(19),FORMTYPE
DOSPLT   TM    PARMFLG,SPLIT
         BZ    DFLT
         L     9,=A(KEY1)
         PACK  WD(8),0(8,9)
         CVB   9,WD
         B     TIOTSCAN
DFLT     L     9,=F'500'
TIOTSCAN TIOT
         LA    2,24(,1)
         LA    7,DCBTAB
         OPEN  (PRTER,OUTPUT)
         IFOPEN PRTER,OK=PRTOK
DDMISS   ABEND 999
PRTOK    EQU   *
         BAL   14,HEADERS
TIOTLOOP CLI   0(2),X'14'
         BNE   UPTIOT
         CLC   4(4,2),=C'MINI'
         BNE   UPTIOT
         CLC   10(2,2),=C'  '
         BNE   UPTIOT
         CLI   8(2),C'0'
         BL    UPTIOT
         CLI   8(2),C'9'
         BH    UPTIOT
         CLI   9(2),C'0'
         BL    UPTIOT
         CLI   9(2),C'9'
         BH    UPTIOT
         MVC   DCB+40(8),4(2)
         RDJFCB (DCB)
         CLI   JFCB+87,X'48'
         BNE   BUMDISP
         ENQ   (QNAME,JFCB,E,44,SYSTEM),RET=USE
         LTR   15,15
         BZ    OKENQ
         MVC   MINWTO+16(6),DCB+40
MINWTO   WTO   'SU3309  DDNAME FILE ALREADY IN USE - DD CARD IGNORED'
         B     UPTIOT
OKENQ    EQU   *
         MVC   4(44,7),JFCB         SAVE DSNAME FOR RESTART ENQ
         GETMAIN R,LV=96
         ST    1,0(,7)
         LA    7,48(,7)
         MVC   0(96,1),DCB
UPTIOT   LA    2,20(,2)
         CLI   0(2),X'00'
         BNE   TIOTLOOP
         C     7,=A(DCBTAB)
         BE    NODDS
         MVC   0(4,7),=F'-1'
         LA    7,DCBTAB
EOFLOOP  L     8,0(,7)
         OPEN  ((8),OUTPUT)
         CLOSE ((8))
         LA    7,48(,7)
         CLC   0(4,7),=F'-1'
         BNE   EOFLOOP
         TM    FLAG,REPMARK
         BO    POSITION
         OPEN  (CFILE,(OUTPUT))
         IFOPEN CFILE,NOT=DDMISS
         OPEN  (FILE)
         IFOPEN (FILE),NOT=DDMISS
         TIOT
         AH    1,FILE+40
         L     1,16(,1)
         MVC   VOLNO(6),28(1)
         GET   FILE,INAREA
         L     15,RECCNT
         LA    15,1(,15)
         ST    15,RECCNT
         CLI   INAREA,C'1'
         BNE   TRYM
         MVI   CCCOMP+1,C'1'
         MVI   CCTYPE,C'A'
         B     DONCCC
TRYM     CLI   INAREA,X'89'
         BE    GOTM1
         CLI   INAREA,X'8B'
         BNE   BUMFILE
GOTM1    MVC   CCCOMP+1(1),INAREA
         MVI   CCTYPE,C'M'
DONCCC   TIOT
         LA    1,0(,1)
         CLI   4(1),C' '
         BNE   GOT5
         LA    14,4(,1)
LOOPC    CLI   0(14),C' '
         BNE   GOTLEN
         BCT   14,LOOPC
GOTLEN   SR    14,1
         STC   14,*+5
         MVC   WORK(1),0(1)
         LA    15,WORK+1(14)
         MVC   0(3,15),=C'001'
         S     15,=A(WORK)
         A     15,=A(JCARD+2)
         ST    15,JSAVE
         MVC   JCARD+2(8),WORK
         B     OPENCHK
GOT5     MVC   JCARD+2(5),0(1)
         MVC   JSAVE,=A(JCARD+7)
OPENCHK  OPEN  (CHKDCB,(OUTPUT))
RESTIN   BAL   14,SU3302
         SR    3,3
         SR    4,4
         SR    5,5
         SR    6,6
*
         LA    7,DCBTAB
MAINLOOP L     8,0(,7)
         OPEN  ((8))
         GET   (8)
         B     OPWAIT
OPEOF    CLOSE ((8),REREAD)
         NI    OPWTSW+1,X'0F'
         OPEN  ((8),OUTPUT)
LOOPA    TM    FLAG,SPLMARK
         BO    NOCHK
CCCOMP   CLI   INAREA,C'1'
         BNE   NOCHK
         AR    5,3
         L     15,LINS
         AR    15,3
         ST    15,LINS
         SR    3,3
         LA    4,1(,4)
         CR    4,9
KILLSW1  BE    FINOP
NOCHK    LA    3,1(,3)
         NI    FLAG,255-SPLMARK
         CH    3,=H'128'
         BH    BUMDOC
         PUT   (8),INAREA
         L     1,ECBS
         TM    0(1),X'40'
         BO    GOTREP
GETIN    GET   FILE,INAREA
         L     15,RECCNT
         LA    15,1(,15)
         ST    15,RECCNT
         B     LOOPA
         EJECT
FINOP    CLOSE ((8))
         AR    6,4
         ST    4,DOCS
         SR    4,4
         RDJFCB ((8))
         MVC   DSN1(44),JFCB
         MVC   DSN2(44),JFCB
         OPEN  (RDR,OUTPUT)
         LA    10,9
         LA    11,JCARD
LOOPB    PUT   RDR,(11)
         LA    11,80(,11)
         BCT   10,LOOPB
         CLOSE (RDR,REREAD)
         MVC   PLINE+2(131),PLINE+1
         L     15,JSAVE
         MVC   NUMB,0(15)
         MVC   JNAME,JCARD+2
         L     15,DOCS
         CVD   15,WD
         MVC   NODOC,EDMSK+4
         ED    NODOC-1(6),WD+5
         CVD   6,WD
         MVC   NODOCTOT,EDMSK+2
         ED    NODOCTOT-1(8),WD+4
         L     15,LINS
         CVD   15,WD
         MVC   NOLINS,EDMSK+2
         ED    NOLINS-1(8),WD+4
         MVC   LINS(4),=F'0'
         CVD   5,WD
         MVC   NOLINTOT,EDMSK
         ED    NOLINTOT-1(10),WD+3
         PUT   PRTER,PLINE
         TM    FLAG,REPMARK
         BO    FINFIN
         MVC   CREC(8),JCARD+2
         MVC   CREC+8(6),VOLNO
         MVC   CREC+14(4),REPCNT
         MVC   REPCNT,RECCNT
         MVC   CREC+18(1),CCCOMP+1
         STCM  9,15,CREC+19
         PUT   CFILE,CREC
         L     15,LINCNT
         LA    15,2(,15)
         ST    15,LINCNT
         CH    15,=H'60'
         BL    *+8
         BAL   14,HEADERS
         L     10,SPLNO
         LA    10,1(,10)
         ST    10,SPLNO
         CVD   10,WD
         OI    WD+7,X'0F'
         L     15,JSAVE
         UNPK  0(3,15),WD+6(2)
         CHKPT CHKDCB,JCARD+2,8
         CH    15,=H'4'
         BNE   CHKOK
         ST    7,SAVEREG
         LA    7,DCBTAB+4
RESTENQ  ENQ   (QNAME,(7),E,44,SYSTEM),RET=USE
         LTR   15,15
         BZ    NEXTDSN
         SH    7,=H'4'
         L     15,0(,7)
         TIOT
         AH    1,40(,15)
         MVC   SU11+16(6),4(1)
SU11     WTO   'SU3311  DDNAME FILE NOT AVAILABLE - RESTART IMPOSSIBLE'
         LEAVE 0
NEXTDSN  LA    7,44(,7)
         CLC   0(4,7),=F'-1'
         LA    7,4(,7)
         BNE   RESTENQ
         L     7,SAVEREG
         BAL   14,SU3302
         BAL   14,HEADERS
CHKOK    EQU   *
         TM    FLAG,EOFMARK
         BO    FINFIN
         OI    FLAG,SPLMARK
         LA    7,48(,7)
         CLC   0(4,7),=F'-1'
         BNE   MAINLOOP
         LA    7,DCBTAB
         B     MAINLOOP
         EJECT
INEOF    OI    FLAG,EOFMARK
         CLOSE (FILE)
         B     FINOP
         SPACE 10
OPWAIT   CLOSE ((8),REREAD)
OPWTSW   NOP   *+12
         ST    7,REGS
         OI    *-7,X'F0'
         LA    7,48(,7)
         CLC   0(4,7),=F'-1'
         BNE   *+8
         LA    7,DCBTAB
         C     7,REGS
         BNE   MAINLOOP
WAITIT   NI    OPWTSW+1,X'0F'
         WTO   'SU3302  WAITING FOR AN OUTPUT FILE'
         STIMER WAIT,DINTVL=SECS30
         LA    7,DCBTAB
         B     MAINLOOP
         SPACE 10
POSITION EQU   *
         OPEN  (CNTL)
         IFOPEN CNTL,NOT=DDMISS
         L     9,=A(KEY5)
         SR    8,8
GETC     GET   CNTL,CREC
         CLC   VOLNO,CREC+8
         BE    *+14
         MVC   VOLNO,CREC+8
         LA    8,1(,8)
         CLC   CREC(8),0(9)
         BNE   GETC
         CLOSE (CNTL,REREAD)
         RDJFCB (FILE)
         STH   8,JFCB+70
         MVC   CCCOMP+1(1),CREC+18
         CLI   CREC+18,C'1'
         BNE   USEM
         MVI   CCTYPE,C'A'
         B     *+8
USEM     MVI   CCTYPE,C'M'
         OPEN  (FILE),TYPE=J
         ICM   8,15,CREC+14
         ICM   9,15,CREC+19
GETF     GET   FILE,INAREA
         BCT   8,GETF
         MVC   JCARD+2(8),CREC
         B     RESTIN
         SPACE 10
CEOF     EQU   *
         WTO   'SU3312  REPRINT JOBNAME NOT FOUND IN CONTROL FILE'
         LEAVE
         SPACE 10
NODDS    WTO   'SU3304  NO MINI FILE DD CARDS!',ROUTCDE=2,DESC=6
         ABEND 888
         SPACE 10
SU3302   EQU   *
         ST    14,SAVEREG
         STM   3,4,REGS
         MVC   REPLY2(6),=CL6' '
         EXTRACT WORKA,FIELDS=COMM
         L     3,WORKA
         MVC   ECBS+1(3),1(3)
         LA    3,4(,3)
         ST    3,AACIB
CLEARCIB ICM   4,15,0(3)
         BZ    CIBCLEAR
         QEDIT ORIGIN=(3),BLOCK=(4)
         B     CLEARCIB
CIBCLEAR QEDIT ORIGIN=(3),CIBCTR=1
         LM    3,4,REGS
         L     14,SAVEREG
         BR    14
         SPACE 10
GOTREP   EQU   *
         STM   2,4,REGS
         L     2,AACIB
         L     4,0(,2)
         LH    3,14(,4)
         BCTR  3,0
         EX    3,MOVEREP
         QEDIT ORIGIN=(2),BLOCK=(4)
         LM    2,4,REGS
         OC    REPLY2(6),=CL6' '
         CLC   REPLY2(5),=C'KILL '
         BE    CMDKILL
         CLC   REPLY2(4),=C'END '
         BE    CMDEND
         CLC   REPLY2(5),=C'HOLD '
         BE    CMDHOLD
         CLC   REPLY2(3),=C'GO '
         BNE   BUMREPLY
         BAL   14,SU3302
         B     GETIN
BUMREPLY WTO   'SU3310  INVALID COMMAND',ROUTCDE=2,DESC=6
         BAL   14,SU3302
         B     GETIN
         SPACE 10
CMDKILL  MVI   KILLSW1+1,X'F0'
         OI    FLAG,EOFMARK
         B     GETIN
         SPACE 10
CMDEND   OI    FLAG,EOFMARK
         B     GETIN
         SPACE 10
CMDHOLD  BAL   14,SU3302
SETTIM   XC    STIMECB,STIMECB
         STIMER REAL,TIMEXIT,DINTVL=MINS2
         WAIT  1,ECBLIST=ECBS
         L     1,ECBS
         TM    0(1),X'40'         WAS IT A MODIFY COMMAND?
         BZ    SETTIM             NO
         TTIMER CANCEL
         B     GOTREP
         SPACE 5
         PUSH  USING
         DROP
         USING *,15
TIMEXIT  STM   14,12,12(13)
         BALR  12,0
         DROP  15
         USING *,12
         POST  STIMECB
         LM    14,12,12(13)
         BR    14
         DROP
         POP   USING
         EJECT
EOVEXIT  EQU   *
         LR    2,1
         TIOT
         AH    1,40(,2)
         L     2,16(,1)
         MVC   VOLNO(6),28(2)
         MVC   RECCNT(4),=F'0'
         BR    14
         SPACE 10
BUMDISP  MVC   BDWTO+20(2),DCB+44
BDWTO    WTO   'SU3305  MINIXX DD CARD HAS INVALID DISP PARAMETER',    X
               ROUTCDE=2,DESC=6
         B     UPTIOT
         SPACE 10
BUMFILE  WTO   'SU3306  INPUT FILE DOES NOT HAVE VALID CONTROL CHARACTEX
               RS',ROUTCDE=2,DESC=6
         ABEND 777
         SPACE 10
BUMDOC   WTO   'SU3307  DOCUMENT FOUND WITH MORE THAN 128 LINES',      X
               ROUTCDE=2,DESC=6
         ABEND 666
         SPACE 10
HEADERS  EQU   *
         ST    14,SAVEREG
         AP    PAGNO,=PL1'1'
         UNPK  PAGE(3),PAGNO
         OI    PAGE+2,X'F0'
         PUT   PRTER,HEAD1
         PUT   PRTER,HEAD2
         PUT   PRTER,HEAD3
         MVC   LINCNT,=F'5'
         L     14,SAVEREG
         BR    14
         SPACE 10
FINFIN   PUT   PRTER,TAIL
         LEAVE 0
         SPACE 5
MOVEREP  MVC   REPLY2(1),16(4)
         SPACE 5
PATCH    DC    64S(*)
         EJECT
         LTORG
QNAME    DC    CL8'SPLITTER'
         SPACE 5
         PRINT NOGEN
CHKDCB   DCB   DDNAME=CHKPT,DSORG=PS,MACRF=W
FILE     DCB   DDNAME=INTAPE,DSORG=PS,MACRF=GM,EODAD=INEOF,EXLST=FILEX
DCB      DCB   DDNAME=MINIXX,DSORG=PS,MACRF=(GL,PM),EODAD=OPEOF,       X
               EXLST=JFCBA
RDR      DCB   DDNAME=IEFRDER,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,     X
               BLKSIZE=400
PRTER    SYSPRINT
CFILE    DCB   DDNAME=CONTROL,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=23
CNTL     DCB   DDNAME=CONTROL,DSORG=PS,MACRF=GM,EODAD=CEOF
         SPACE 10
         DS    0D
FILEX    DC    X'06',AL3(EOVEXIT)
JFCBA    DC    X'87',AL3(*+3)
JFCB     DS    44F
INAREA   DS    CL133
STIMECB  DS    F
ECBS     DC    A(0)               MODIFY ECB ADDRESS
         DC    X'80',AL3(STIMECB)
SAVEREG  DS    F
REPLY    DS    CL6
REPLY2   DS    CL6
WORK     DC    CL8' '
FLAG1    EQU   *
FLAG     DC    X'80'
SPLMARK  EQU   128
EOFMARK  EQU   64
REPMARK  EQU   32
         DS    0F
SPLNO    DC    F'1'
         DS    0D
SECS30   DC    C'00003000'
MINS2    DC    C'00020000'
EDMSK    DC    X'202020202020202120'
         DS    0D
WD       DS    D
FORMTYPE DC    C'(X,,FORM),OUTLIM=0,'
PARMFLG  DC    X'00'
         DS    0F
WORKA    DS    F
AACIB    DC    A(0)
REGS     DS    3F
JSAVE    DS    F
REPCNT   DC    F'1'
RECCNT   DC    F'0'
LINCNT   DC    F'0'
DOCS     DC    F'0'
LINS     DC    F'0'
PAGNO    DC    PL2'0'
HEAD1    DC    CL133'1   SU33SPL                       PRINT FILE SPLITX
               TING PROGRAM REPORT'
         ORG   *-10
         DC    C'PAGE '
PAGE     DC    CL5' '
HEAD2    DC    CL133'-SPLIT       JOBNAME        # DOCS        TOTAL   X
                    # LINES        TOTAL'
HEAD3    DC    CL133'   #                                     TO DATE  X
                                  TO DATE'
PLINE    DS    0CL133
         DC    CL2'0 '
NUMB     DS    CL3
         DS    CL8
JNAME    DS    CL8
         DS    CL8
NODOC    DS    CL5
         DS    CL6
NODOCTOT DS    CL7
         DS    CL8
NOLINS   DS    CL7
         DS    CL5
NOLINTOT DS    CL9
         DS    CL81
TAIL     DC    CL133'0   ***   E N D   O F   R E P O R T   ***'
CREC     DS    CL23
VOLNO    DS    CL6
         SPACE 10
JCARD    EQU   *
         DC    CL80'//     001 JOB (065,OPS,000),OP,CLASS=W,MSGCLASS=Z'
         DC    CL80'//STEPA EXEC PGM=IEBGENER'
D1       DC    CL80'//SYSUT1 DD DISP=SHR,DSN='
D4       DC    CL80'//SYSUT2 DD SYSOUT=H,OUTLIM=0, '
D3       DC    CL80'//  DCB=(RECFM=FBX,LRECL=133,BLKSIZE=3990) '
         DC    CL80'//STEPB EXEC PGM=EOF'
         DC    CL80'//SYSPRINT DD SYSOUT=Z'
D2       DC    CL80'//DDA DD DISP=SHR,DSN='
         DC    CL80'//'
DSN1     EQU   D1+25
DSN2     EQU   D2+22
CCTYPE   EQU   D3+17
CLASS    EQU   D4+19
         SPACE 5
DCBTAB   DS    1200F
         END
         SPACE 5
DCBTAB   DS    1200F
