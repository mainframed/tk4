         MACRO
         UOPT
         LCLA  &A
IHCUOPT  CSECT
         DC    A(95,0)
&A       SETA  1
.LOOP    ANOP
         DC    X'640A0012',V(ERRMSG)
&A       SETA  &A+1
         AIF   (&A LT 96).LOOP
         MEND
SU53CAL  ENTER 13,12,ENT=(CALC,DESKCAL),EXTENT=LABC,SA=256,            X
               AUTH=YES,VERS=MVS1.0
         ENTRY DDSW1,PRINT,PLINE
         STM   12,13,BASESAVE
         L     15,=V(IBCOM#)
         BAL   14,64(0,15)
         SPIE  CALSPIE,((1,15))
         LTR   1,1
         BZ    *+8
         ST    1,APICA
         OPEN  (INPUT)
         TM    INPUT+48,X'10'
         BZ    LABC
         OPEN  (PRINT,(OUTPUT))
         TM    PRINT+48,X'10'
         BO    OKDDS
         WTP   'NO SYSPRINT DD CARD - RUN ABANDONED'
         B     EXIT
OKDDS    OI    DDSW1+1,X'F0'
         OI    DDSW2+1,X'F0'
         PUT   PRINT,HEAD
LABC     EQU   *
         MVC   MASSGN,=F'0'
         INIT  REPLY,LV=81,FILL=40
         MVC   SENDBACK+37(12),=CL12' '
         MVC   ANSWER(22),=CL80' '
         SR    2,2
         LA    3,REPLY
         LA    10,RPN
DDSW1    NOP   READCARD
         XC    REPECB,REPECB
         WTOR  'SSU1601R  ENTER COMMAND',REPLY,80,REPECB
         WAIT  ECB=REPECB
         B     SCANIPT
READCARD GET   INPUT,REPLY
SCANIPT  EQU   *
         OC    REPLY,=CL80' '
         LA    1,REPLY
         LA    2,80
         MVC   PLINE+1(80),REPLY
BLNKSCAN CLI   0(1),C';'
         BE    ITSTHERE
         CLI   0(1),C' '
         BE    *+12
         LA    1,1(0,1)
         BCT   2,BLNKSCAN
         MVI  0(1),C';'
ITSTHERE EQU   *
         S     1,=A(REPLY)
         EX    1,TRT1
         BZ    *+10
         LR    3,1
         B     INVCOM
         CLC   REPLY(4),=C'HELP'
         BE    HELP
         CLC   REPLY(4),=C'LIST'
         BE    LIST
         CLC   REPLY(4),=C'TRUE'
         BE    TRUE
         CLC   REPLY(3),=C'END'
         BE    EXIT
         CLC   REPLY(4),=C'DUMP'
         BE    CRASH
         CLC   REPLY(4),=C'SNAP'
         BE    SNAP
         BAL   14,INIT
         CLI   REPLY+3,C'='
         BNE   SCANDEL
         CLI   REPLY,C'M'
         BNE   INVCOM
         CLI   REPLY+1,C'0'
         BL    INVCOM
         CLI   REPLY+1,C'9'
         BH    INVCOM
         CLI   REPLY+2,C'0'
         BL    INVCOM
         CLI   REPLY+2,C'9'
         BH    INVCOM
         PACK  WD(8),REPLY+1(2)
         CVB   3,WD
         MH    3,=H'9'
         LA    3,M00(3)
         ST    3,MASSGN
         MVC   SENDBACK+37(12),SAVMSG
         MVC   SENDBACK+47(2),REPLY+1
         MVI   REPLY+3,C'('
         LA    3,REPLY+4
         B     LABD
SCANDEL  LA    3,REPLY
LABD     EQU   *
         SR    1,1
         SR    2,2
         TRT   0(80,3),TRTTAB
SFUNBACK EQU   *
         LR    11,1
         LR    4,1
         SR    4,3
         BP    TRYCON
         BM    LOGERR
         CLI   0(3),C'E'
         BNE   NSFUN
         CLC   0(4,3),=C'ERFC'
         BNE   *+12
         LA    1,4(0,3)
         B     FUNCT
         CLC   0(3,3),=C'ERF'
         BE    SFUNCT
         CLC   0(3,3),=C'EXP'
         BE    SFUNCT
         CLI   1(3),C'A'
         BNL   INVCOM
         IC    2,1(0,3)
         IC    2,TRTTAB(2)
         LA    11,1(0,3)
         B     ECONST
SFUNCT   LA    1,3(0,3)
FUNCT    LA    2,7
         B     SFUNBACK
NSFUN    EQU   *
         BCTR  3,0
         CLI   0(3),C')'
         LA    3,1(0,3)
         BE    LABB
         CLI   0(3),C'+'
         BNE   *+12
         LA    3,1(0,3)
         B     LABD
         CLI   0(3),C'-'
         BNE   TRYPAR
         BCTR  3,0
         CLI   0(3),C'('
         LA    3,1(0,3)
         BNE   INVCOM
         LA    2,4
         B     LABG
TRYPAR   CLI   0(3),C'('
         BE    LABG
         CLI   0(3),C')'
         BE    LABG
         B     INVCOM
TRYCON   CLI   0(3),C'0'
         BL    SAVED
CONBACK  BCTR  4,0
         EX    4,MOVE1
         CLI   0(11),C'E'
         BE    GETEXP
         LR    5,3
         LA    6,1(0,4)
LOOPA    CLI   0(5),C'.'
         BE    PUTEXP
         LA    5,1(0,5)
         BCT   6,LOOPA
         EX    4,PACK1
         LA    5,1(0,4)
         CH    5,NODIGS
         BNH   *+8
         STH   5,NODIGS
         CVB  15,WD
         ST    15,1(0,10)
         MVC   5(4,10),=F'0'
         MVI   0(10),X'80'
RPENTUP  LA    10,9(0,10)
         B     LABB
PUTEXP   LA    5,HOLDNO+2(4)
         MVC   0(4,5),=C'E+00'
         LA    4,6(0,4)
         B     CONVFP
GETEXP   LA    5,1(0,11)
         LA    6,HOLDNO+2(4)
         MVI   0(6),C'E'
         LA    6,1(0,6)
         CLI   0(5),C'0'
         BNL   NOSIGN
         MVC   0(1,6),0(5)
         LA    5,1(0,5)
         B     *+8
NOSIGN   MVI   0(6),C'+'
         LA    6,1(0,6)
         CLI   0(5),C'0'
         BNL   *+10
GETOUT   LR    3,5
         B     INVCOM
         CLI   0(5),C'9'
         BH    GETOUT
         MVC   0(1,6),0(5)
         LA    6,1(0,6)
         LA    5,1(0,5)
         CLI   0(5),C'0'
         BNL   TWODIGS
         BCTR  6,0
         MVC   1(1,6),0(6)
         MVI   0(6),C'0'
         LA    4,6(0,4)
         LR    1,5
         LR    11,5
         B     CONVFP
TWODIGS  CLI   0(5),C'9'
         BH    GETOUT
         MVC   0(1,6),0(5)
         LA    1,1(0,5)
         LA    4,6(0,4)
         LA    11,1(0,5)
CONVFP   STC   4,TLON
         LA    5,HOLDNO+1
LOOPB    CLI   0(5),C'.'
         BE    *+16
         LA    5,1(0,5)
         BCT   4,LOOPB
         B     INVCOM
         LA    5,1(0,5)
         SR    6,6
LOOPC    CLI   0(5),C'E'
         BE    FINDECS
         LA    5,1(0,5)
         LA    6,1(0,6)
         B     LOOPC
FINDECS  STC   6,DIM
         STM   0,5,SAVEREGS
         LA    3,HOLDNO
         L     15,=V(ILBOEFL0)
         BALR  14,15
DIM      DC    X'00'
TLON     DC    X'00'
         DC    X'0100'
         STD   0,1(0,10)
         MVI   0(10),X'40'
         LM    0,5,SAVEREGS
         SR    2,2
         IC    2,DIM
         CH    2,NODECS
         BNH   *+8
         STH   2,NODECS
         SR    14,14
         IC    14,TLON
         SH    14,=H'6'
         SR    14,2
         CH    14,NODIGS
         BNH   *+8
         STH   14,NODIGS
         IC    2,0(0,11)
         IC    2,TRTTAB(2)
         B     RPENTUP
SAVED    CLI   0(3),C'M'
         BNE   TRYFUNC
         CLI   1(3),C'0'
         BL    INVCOM
         CLI   1(3),C'9'
         BH    INVCOM
         CLI   2(3),C'0'
         BL    INVCOM
         CLI   2(3),C'9'
         BH    INVCOM
         PACK  WD(8),1(2,3)
         CVB   5,WD
         MH    5,=H'9'
         LA    5,M00(5)
         CLI   0(5),0
         BE    NOTINIT
         MVC   0(9,10),0(5)
         B     RPENTUP
TRYFUNC  CLC   0(2,3),=C'PI'
         BE    PICONST
         CH    4,=H'1'
         BNE   NOTCONST
         CLI   0(3),C'Y'
         BE    EULER
         CLI   0(3),C'O'
         BE    GOLDEN
NOTCONST EQU   *
         L     5,AFUNS
LOOPD    CH    4,0(0,5)
         BNE   UPTAB
         BCTR  4,0
         EX    4,COMP1
         BE    GOTFUN
         LA    4,1(0,4)
UPTAB    LA    5,14(0,5)
         CLI   0(5),X'FF'
         BE    INVCOM
         B     LOOPD
GOTFUN   $STACK WORK,LPAR
         MVC   WA(4),=X'0F0B0021'
         MVC   WA+4(4),10(5)
         $STACK WORK,WA
         LA    3,1(0,11)
         B     LABD
PICONST  MVC   1(8,10),PI
SCONST   MVI   0(10),X'40'
         B     RPENTUP
ECONST   MVC   1(8,10),E
         B     SCONST
EULER    MVC   1(8,10),Y
         B     SCONST
GOLDEN   MVC   1(8,10),O
         B     SCONST
LABB     EQU   *
         CLC   0(2,11),=C'**'
         BNE   LABG
         LA    2,5
LABG     LR    5,2
         BCTR  5,0
         SLL   5,1
         LH    6,WTTAB(5)
         SRDL  6,8
LABE     $STACK WORK,WA,OP=EXAM
         CH    15,=H'4'
         BE    MTSTACK
         BH    LOGERR
         EX    6,COMP2
         BL    OKDEL
         $STACK WORK,WA,OP=DELETE
         MVC   0(9,10),WA+3
         LA    10,9(0,10)
         B     LABE
OKDEL    CLI   0(11),C';'
         BE    BUMPARS
         CLI   0(11),C')'
         BNE   LABF
         CLI   WA+4,C'('
         BNE   BUMPARS
         $STACK WORK,WA,OP=DELETE
         LA    3,1(0,11)
         B     LABD
LABF     SLDL  6,8
         STH   6,WA
         MVC   WA+2(2),=X'0001'
         CH    2,=H'4'
         BNE   TRYPOW
         CLI   0(11),C'-'
         BNE   NORMAL
         MVC   WA+4(8),=CL8'NEG'
         OI    WA+3,X'20'
STACKIT  $STACK WORK,WA
         LA    3,1(0,11)
         B     LABD
TRYPOW   CLC   0(2,11),=C'**'
         BNE   NORMAL
         MVC   WA+4(8),=CL8'POW'
         OI    WA+3,X'20'
         LA    11,1(0,11)
         B     STACKIT
NORMAL   EQU   *
         LA    14,1
         CLI   0(11),C'+'
         BE    DONE
         LA    14,1(0,14)
         CLI   0(11),C'-'
         BE    DONE
         LA    14,1(0,14)
         CLI   0(11),C'*'
         BE    DONE
         LA    14,1(0,14)
         CLI   0(11),C'/'
         BE    DONE
         IC    14,0(0,11)
DONE     EQU   *
         STC   14,WA+4
         MVC   WA+5(7),=CL8' '
         B     STACKIT
MTSTACK  CLI   0(11),C';'
         BNE   LABF
LABA     MVI   0(10),X'FF'
         LA    10,RPN
         $STACK WORK,OP=FREE
LABH     CLI   0(10),X'FF'
         BE    LABJ
         TM    0(10),X'01'
         BO    OPERATOR
         $STACK WORK2,(10)
LABI     LA    10,9(0,10)
         B     LABH
OPERATOR TM    0(10),X'20'
         BZ    TWOOPS
         $STACK WORK2,WA,OP=DELETE
         TM    WA,X'80'
         BZ    *+12
         L     0,WA+1
         B     *+8
         LD    0,WA+1
         CLC   1(3,10),=C'NEG'
         BNE   FUNCTION
         TM    WA,X'80'
         BO    FIXNEG
         LCDR  0,0
         STD   0,WA+1
RESTACK  $STACK WORK2,WA
         B     LABI
FUNCTION CLC   1(3,10),=C'POW'
         BE    TWOOPS
         L     15,1(0,10)
         TM    WA,X'80'
         BZ    *+12
         LA    9,WA+1
         BAL   14,FLOAT
         CALL  (15),(WA+1),VL
         STD   0,WA+1
         MVI   WA,X'40'
         B     RESTACK
FIXNEG   LCR   0,0
         ST    0,WA+1
         B     RESTACK
FLOAT    LD    0,SCALED0
         STD   0,WD
         ICM   0,15,0(9)
         BM    NEGNO
         ST    0,WD+4
         AD    0,WD
FLOUT    STD   0,0(0,9)
         BR    14
NEGNO    LPR   0,0
         ST    0,WD+4
         SD    0,WD
         B     FLOUT
TWOOPS   CLC   1(3,10),=C'POW'
         BE    GETSEC
         $STACK WORK2,WA,OP=DELETE
GETSEC   $STACK WORK2,WB,OP=DELETE
         SR    8,8
         IC    8,1(0,10)
         BCTR  8,0
         SLL   8,2
         CLC   1(3,10),=C'POW'
         BE    POWERISE
         CLC   WA(1),WB
         BE    SAME
         TM    WA,X'80'
         BZ    FLOTB
         LA    9,WA+1
         BAL   14,FLOAT
         B     OPIT
FLOTB    LA    9,WB+1
         BAL   14,FLOAT
OPIT     LD    0,WB+1
         LA    9,INSTSFP(8)
         EX    0,0(0,9)
OPBACK   STD   0,WA+1
         MVI   WA,X'40'
         B     RESTACK
POWERISE TM    WA,X'80'
         BZ    BREAL
         TM    WB,X'80'
         BO    INTINT
         CALL  FDXPI#,(WB+1,WA+1),VL
         B     OPBACK
INTINT   TM    WA+1,X'80'
         BZ    BOTINT
         LA    9,WB+1
         BAL   14,FLOAT
         MVI   WB,X'40'
         B     POWERISE
BOTINT   CALL  FIXPI#,(WB+1,WA+1),VL
         ST    0,WA+1
         MVI   WA,X'80'
         B     RESTACK
BREAL    TM    WB,X'40'
         BO    OKREAL
         LA    9,WB+1
         BAL   14,FLOAT
OKREAL   CALL  FDXPD#,(WB+1,WA+1),VL
         B     OPBACK
LABJ     $STACK WORK2,WA,OP=DELETE
         TM    WA,X'80'
         BO    FIXANS
         SR    1,1
         ICM   1,3,NODECS
         BNZ   *+8
         LA    1,1(0,1)
         LA    1,1(0,1)
         STC   1,DECS
         AH    1,NODIGS
         LA    1,7(0,1)
         CH    1,=H'22'
         BNH   *+8
         LA    1,22
         STC   1,TLEN
         SH    1,=H'7'
         SLL   1,3
         LA    1,POW10TAB(1)
         MVC   NOMDIGS(8),0(1)
CONVERT  L     15,=V(ILBOTEF3)
         LD    0,WA+1
         CNOP  2,8
         BALR  14,15
DECS     DC    X'00'
TLEN     DC    X'000500'
NOMDIGS  DC    XL8'00'
         LA    1,120(0,13)
LOWVLOOP CLI   0(1),0
         BE    *+12
         LA    1,1(0,1)
         B     LOWVLOOP
         MVI   0(1),C'.'
         SR    2,2
         SR    3,3
         SR    4,4
         LA    1,121(0,13)
LOOPXX   CLI   0(1),C'.'
         BE    GOTPRE
         LA    2,1(0,2)
         LA    1,1(0,1)
         B     LOOPXX
GOTPRE   LR    5,1
         LA    1,1(0,1)
LOOPYY   CLI   0(1),C'E'
         BE    GOTPOST
         LA    3,1(0,3)
         LA    1,1(0,1)
         B     LOOPYY
GOTPOST  LR    6,1
         PACK  WD(8),2(2,1)
         CVB   4,WD
         BCTR  2,0
         BCTR  3,0
         CH    4,=H'0'
         BE    EXPZERO
         CLI   1(6),C'-'
         BE    LEFT
         CH    3,=H'1'
         BNH   EXPZERO
LOOPZZ   MVC   0(1,5),1(5)
         MVI   1(5),C'.'
         LA    5,1(0,5)
         SH    4,=H'1'
         BNP   EXPZERO
         BCT   3,LOOPZZ
         B     EXPZERO
LEFT     CH    2,=H'1'
         BNH   EXPZERO
LOOPZZZ  BCTR  5,0
         MVC   1(1,5),0(5)
         MVI   0(5),C'.'
         SH    4,=H'1'
         BNP   EXPZERO
         BCT   2,LOOPZZZ
EXPZERO  CVD   4,WD
         OI    WD+7,X'0F'
         UNPK  WD(3),WD+6(2)
         MVC   2(2,6),WD+1
         CLC   2(2,6),=C'00'
         BNE   *+10
         MVC   0(4,6),=CL4' '
         CLI   121(13),C'0'
         BNE   RESULT
         LA    2,121(0,13)
STOPLOOP CLI   1(2),C'.'
         BE    RESULT
         CLI   0(2),C'0'
         BNE   RESULT
         MVI   0(2),C' '
         LA    2,1(0,2)
         B     STOPLOOP
RESULT   EQU   *
TSW      NOP   RES2
         TM    WA,X'80'
         BO    RES2
         BCTR  6,0
         SR    1,1
         IC    1,0(0,6)
         CH    1,=H'245'          DECIMAL 5
         BNL   *+12
         MVI   0(6),C' '
         B     RES2
         LA    2,120(0,13)
         SR    3,3
RNDLP    BCTR  6,0
         CR    6,2
         BNH   CARRY
         IC    1,0(0,6)
         CH    1,=H'240'
         BL    REPLACE
         LTR   3,3
         BNZ   REPLACE
         LA    1,1(0,1)
         CH    1,=H'250'
         BNL   OVERTOP
         LA    3,1
REPLACE  STC   1,1(0,6)
         CLI   1(6),C' '
         BE    CARRY
         B     RNDLP
OVERTOP  EQU   *
         MVI   1(6),C'0'
         B     RNDLP
CARRY    LTR   3,3
         BZ    *+12
         MVI   1(6),C' '
         B     RES2
         MVI   1(6),C'1'
RES2     ICM   1,15,MASSGN
         BZ    *+10
         MVC   0(9,1),WA
         SR    1,1
         IC    1,TLEN
         BCTR  1,0
         EX    1,MOVER
         NI    TSW+1,X'0F'
DDSW2    NOP   PRTOP
         WTO   ,MF=(E,SENDBACK)
         B     LABC
PRTOP    EQU   *
         MVC   PLINE+81(52),SENDBACK+4
PRTIT    EQU   *
         PUT   PRINT,PLINE
         INIT  PLINE+1,LV=132,FILL=40
         B     LABC
FIXANS   L     1,WA+1
         CVD   1,WD
         MVC   120(22,13),EDMSK
         LA    1,135(0,13)
         EDMK  120(22,13),WD
         BNM   *+10
         BCTR  1,0
         MVI   0(1),C'-'
         MVI   TLEN,16
         B     RESULT
HELP     EQU   *
         TM    DDSW1+1,X'F0'
         BZ    HWTO
         GOTO  SU53HPR,(PRINT)
         B     LABC
HWTO     GOTO  SU53HOP
         B     LABC
LIST     EQU   *
         LA    2,100
         LA    3,M00
         SR    4,4
LISTLP   CLI   0(3),X'00'
         BE    UPMEMS
         TM    0(3),X'80'
         BO   FIXLIST
         LD    0,1(0,3)
         L     15,=V(ILBOTEF3)
         CNOP  2,8
         BALR  14,15
         DC    X'0F160500'
         DC    DE16'1.0'
         MVI   122(13),C'.'
LISTOUT  CVD   4,WD
         OI    WD+7,X'0F'
         UNPK  WD(3),WD+6(2)
         TM    DDSW1+1,X'F0'
         BO    PRTLIST
         MVC   LISTWTO+9(2),WD+1
         MVC   LISTWTO+14(22),120(13)
LISTWTO  WTO   'M   =                        '
UPMEMS   LA    3,9(0,3)
         LA    4,1(0,4)
         BCT   2,LISTLP
         B     LABC
PRTLIST  MVC   PLINE+1(5),=C'M   ='
         MVC   PLINE+2(2),WD+1
         MVC   PLINE+7(22),120(13)
         PUT   PRINT,PLINE
         INIT  PLINE+1,LV=30,FILL=40
         B     UPMEMS
FIXLIST  L     14,1(0,3)
         CVD   14,WD
         MVC   120(22,13),EDMSK
         LA    1,135(0,13)
         EDMK  120(22,13),WD
         BNM   LISTOUT
         BCTR  1,0
         MVI   0(1),C'-'
         B     LISTOUT
TRUE     TM    WA,X'80'
         BO    FIXANS
         MVC   DECS(2),=X'0816'
         MVC   NOMDIGS(8),POW10TAB+128
         OI    TSW+1,X'F0'
         B     CONVERT
SNAP     EQU  *
         NOP  SOPEN
         OI    *-3,X'F0'
         OPEN  (SNAPDCB,(OUTPUT))
SOPEN    SNAP  DCB=SNAPDCB,SDATA=(CB),PDATA=(REGS),STORAGE=(DESKCAL,EODX
               C)
         LTR  15,15
         BNZ   CRASH
         B     LABC
INIT     EQU   *
         ST    14,0(0,13)
         $STACK WORK,OP=FREE
         $STACK WORK2,OP=FREE
         INIT  RPN,LV=720
         MVC   NODIGS(4),=F'0'
         L     14,0(0,13)
         BR    14
NOTINIT  MVC   NIMSG+08(3),0(3)
         TM    DDSW1+1,X'F0'
         BZ    NIMSG
         MVC   PLINE+81(28),NIMSG+8
         B     PRTIT
NIMSG    WTO   '    HAS NOT BEEN INITIALISED'
         B     LABC
INVCOM   S     3,=A(REPLY)
         LA    3,1(0,3)
         CVD   3,WD
         OI    WD+7,X'0F'
         UNPK  INVMSG+39(3),WD+6(2)
         TM    DDSW1+1,X'F0'
         BZ    INVMSG
         MVC   PLINE+81(34),INVMSG+8
         B     PRTIT
INVMSG   WTO   'INVALID INPUT - SCAN POINTER = XXX'
         B     LABC
LOGERR   WTP   'PROGRAM LOGIC ERROR - BYE-BYE!'
CRASH    ABEND 1,DUMP,STEP
BUMPARS  TM    DDSW1+1,X'F0'
         BZ    WTOIT
         MVC   PLINE+81(22),WTOIT+8
         B     PRTIT
WTOIT    WTO   'UNBALANCED PARENTHESES'
         B     LABC
SAME     TM    WA,X'80'
         BZ    OPIT
         L     0,WB+1
         CLI   1(10),X'02'
         BNH   FOPIT
         LR    1,0
         SR    0,0
FOPIT    LA    9,INSTSFX(8)
         EX    0,0(0,9)
         CLI   1(10),X'02'
         BH    REG1
         ST    0,WA+1
         B     RESTACK
REG1     ST    1,WA+1
         B     RESTACK
EXIT     ICM   1,15,APICA
         BZ    LEAVE
         SPIE  ,MF=(E,(1))
LEAVE    LEAVE
         CNOP  2,4
MOVE1    MVC   HOLDNO+1(1),0(3)
MOVER    MVC   ANSWER(1),120(13)
PACK1    PACK  WD(8),HOLDNO+1(1)
COMP1    CLC   0(1,3),2(5)
COMP2    CLI   WA+1,X'00'
TRT1     TRT   REPLY(1),TRTTAB2
INSTSFP  EQU   *
         AD    0,WA+1
         SD    0,WA+1
         MD    0,WA+1
         DD    0,WA+1
INSTSFX  A     0,WA+1
         S     0,WA+1
         M     0,WA+1
         D     0,WA+1
SENDBACK WTO   'RESULT IS                                           ',MX
               F=L
ANSWER   EQU   SENDBACK+14
         LTORG
WORK     $QCB  ACCESS=STACK,LEN=12
WORK2    $QCB  ACCESS=STACK,LEN=9
         DC    C'('
REPLY    DC    CL80' '
         DC    C' '
SNAPDCB  DCB   DDNAME=SNAPDUMP,DSORG=PS,MACRF=W,RECFM=VBA,LRECL=125,   X
               BLKSIZE=882
INPUT    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,RECFM=FB,LRECL=80,       X
               EODAD=EXIT
PRINT    SYSPRINT
REPECB   DS    F
RPN      DS    80XL9
WD       DS    D
MASSGN   DC    F'0'
APICA    DC    F'0'
NODIGS   DC    H'0'
NODECS   DC    H'0'
TRTTAB   EQU   *
         DC    XL77'00',X'0703',XL13'00'
         DC    X'040201000304',XL99'00'
         DC    X'FC',XL58'00'
TRTTAB2  DC    75X'FF',X'00FF0000',13X'FF',X'000000FF0000'
         DC    28X'FF',X'00',66X'FF',9X'00',7X'FF',9X'00'
         DC    8X'FF',8X'00',6X'FF',10X'00',6X'FF'
SAVEREGS DS    6F
         ENTRY BASESAVE
BASESAVE DS    2F
HOLDNO   DC    CL30'+'
HEAD     DC    CL133'1        CALCULATIONS AND RESULTS'
PLINE    DC    CL133'0'
FUNTAB   EQU   *
         DC    H'3',CL8'LOG',V(DLOG)
         DC    H'3',CL8'EXP',V(DEXP)
         DC    H'3',CL8'SIN',V(DSIN)
         DC    H'3',CL8'COS',V(DCOS)
         DC    H'3',CL8'TAN',V(DTAN)
         DC    H'3',CL8'ERF',V(DERF)
         DC    H'4',CL8'SQRT',V(DSQRT)
         DC    H'4',CL8'ATAN',V(DATAN)
         DC    H'4',CL8'SINH',V(DSINH)
         DC    H'4',CL8'COSH',V(DCOSH)
         DC    H'4',CL8'TANH',V(DTANH)
         DC    H'4',CL8'ERFC',V(DERFC)
         DC    H'5',CL8'LOG10',V(DLOG10)
         DC    H'5',CL8'ARSIN',V(DARSIN)
         DC    H'5',CL8'ARCOS',V(DARCOS)
         DC    H'5',CL8'COTAN',V(DCOTAN)
         DC    H'5',CL8'GAMMA',V(DGAMMA)
         DC    H'5',CL8'LGAMA',V(DLGAMA)
         DC    H'6',CL8'LGAMMA',V(DLGAMA)
         DC    X'FF'              END OF TTABLE
         DS    0D
AFUNS    DC    A(FUNTAB)
LPAR     DC    X'11010001',CL8'('
WA       DC    XL12'00'
WB       DC    XL12'00'
EDMSK    DC    X'40',13X'20',X'2120',CL6' '
WTTAB    DC    X'0200'
         DC    X'0300'
         DC    X'0C0C'
         DC    X'0D0D'
         DC    X'0E0E'
         DC    X'0F0B'
         DC    X'1101'
SAVMSG   DC    C'SAVED IN MXX'
SCALED0  DC    X'4E00000000000000'
PI       DC    D'3.14159265358979323846'
E        DC    D'2.71828182845904523536'
Y        DC    D'0.57721566490153286060'     GAMMA (EULERS CONSTANT'
O        DC    D'1.61803398874989484820'     PHI (GOLDEN RATIO)
POW10TAB DC    D'1.0'
         DC    DE1'1.0'
         DC    DE2'1.0'
         DC    DE3'1.0'
         DC    DE4'1.0'
         DC    DE5'1.0'
         DC    DE6'1.0'
         DC    DE7'1.0'
         DC    DE8'1.0'
         DC    DE9'1.0'
         DC    DE10'1.0'
         DC    DE11'1.0'
         DC    DE12'1.0'
         DC    DE13'1.0'
         DC    DE14'1.0'
         DC    DE15'1.0'
         DC    DE16'1.0'
M00      DC    100XL9'00'
EODC     EQU   *
         DROP
CALSPIE  CSECT
         USING *,15
         STM   0,15,CSSAVE
         CNOP  0,4
         BAL   13,*+76
         USING *,13
         DROP  15
         DS    18F
         SR    2,2
         LR    11,1
         IC    2,7(0,11)
         BCTR  2,0
         SLL   2,5
         LA    3,MSGS(2)
         MVC   CSWTO+8(32),0(3)
         L     4,=V(DDSW1)
         TM    1(4),X'F0'
         BZ    CSWTO
         L     4,=V(PRINT)
         L     5,=V(PLINE)
         MVC   81(32,5),CSWTO+8
         PUT   (4),(5)
         B     DUNWRIT
CSWTO    WTO   '                                 ',ROUTCDE=(2,11)
DUNWRIT  EQU   *
         L     2,=V(LABC)
         STCM  2,7,9(11)
         L     14,=V(BASESAVE)
         MVC   CSSAVE+48(8),0(14)
         LM    0,15,CSSAVE
         BR    14
         LTORG
CSSAVE   DS    16F
MSGS     EQU   *
         DC    CL32'OPERATION EXCEPTION'
         DC    CL32'PRIVILEGED OPERATION EXCEPTION'
         DC    CL32'EXECUTE EXCEPTION'
         DC    CL32'PROTECTION EXCEPTION'
         DC    CL32'ADDRESSING EXCEPTION'
         DC    CL32'SPECIFICATION EXCEPTION'
         DC    CL32'DATA EXCEPTION'
         DC    CL32'FIXED-POINT OVERFLOW EXCEPTION'
         DC    CL32'FIXED-POINT DIVIDE EXCEPTION'
         DC    CL32'DECIMAL OVERFLOW EXCEPTION'
         DC    CL32'DECIMAL DIVIDE EXCEPTION'
         DC    CL32'EXPONENT OVERFLOW EXCEPTION'
         DC    CL32'EXPONENT UNDERFLOW EXCEPTION'
         DC    CL32'SIGNIFICANCE EXCEPTION'
         DC    CL32'FLOATING-POINT DIVIDE EXCEPTION'
         UOPT
         DROP
ERRMSG   ENTER
         SH    1,=H'4'
         MVI   3(11),0
         L     2,0(0,1)
         L     3,0(0,2)
         LA    2,4(0,2)
         BCTR  3,0
         CH    3,=H'121'
         BL    *+8
         LA    3,120
         EX    3,ERRMVE
         L     3,=V(DDSW1)
         TM    1(3),X'F0'
         BZ    ERRWTO
         L     3,=V(PRINT)
         L     4,=V(PLINE)
         MVC   1(121,4),ERRWTO+8
         PUT   (3),(4)
         B     WRITTEN
ERRWTO   WTO   '                                                       X
                                                                       X
                         '
WRITTEN  LEAVE
ERRMVE   MVC   ERRWTO+8(1),0(2)
         LTORG
         END
