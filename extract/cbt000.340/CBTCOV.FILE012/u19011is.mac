B15      TITLE 'HBS - SUBROUTINE 15'
B1501    CSECT
         ENTRY B150100
B150100  EQU   *
         PRINT NOGEN
*    HBS STANDARD SUBROUTINE 15 PERFORMS A TABLE LOOK-UP OPERATION
*        EITHER TO A) VERIFY THAT A FIELD IS VALID BY REFERENCE TO
*                     A TABLE OF VALID ENTRIES.
*        AND,IF REQUIRED B) EXTRACT FROM THE TABLE INFORMATION RELATING
*                     TO THIS PARTICULAR ENTRY.
*              LINK TO THE SUBROUTINE BY -
*                      CALL B1501,(P1,P2,P3,P4,P5,P6,P7)
*        WHERE P1 IS THE NAME OF THE FIELD CONTAINING THE ARGUMENT.
*              P2 IS THE NAME OF THE TABLE.
*              P3 IS THE LENGTH OF THE TABLE ENTRY.
*              P4 IS THE LENGTH OF THE ITEM FOLLOWING THE TABLE ENTRY.
*                 (OPTIONAL-MAY BE ZERO)
*              P5 IS THE NUMBER OF ENTRIES IN THE TABLE.
*              P6 IS THE NAME OF THE FIELD TO CONTAIN THE RESULT OF
*                 THE SEARCH. (OPTIONAL-MAY BE ZERO, LENGTH MUST BE
*                 EQUAL TO P4)
*              P7 IS THE NAME OF THE ERROR ROUTINE IF NO ENTRY IS FOUND
*
*                  ALL PARAMETERS MUST BE PRESENT.
*
         ISEQ  73,80
         SAVE  (14,12)             SAVE REGISTERS
         BALR  5,0                 ASSIGN BASE REGISTER
         USING *,5
         LM    6,12,0(1)           LOAD PARAMETERS
         LR    3,8                 LOAD REG 3 WITH P3
         AR    3,9                 ADD P4 TO P3 IN REG 3 (INDEX)
         LR    4,3                 LOAD REG 4 WITH RESULT
         STH   10,SHW02            STORE P5
         MH    4,SHW02             MULTIPLY REG4 BY P5 (LENGTH OF TBLE)
         AR    4,7                 ADD ADDRESS OF TABLE TO GET LIMIT
         LR    2,7                 LOAD ADDRESS OF TABLE
         BCTR  8,0                 DECREMENT REG 8 BY ONE
B1504    EX    8,B1505             EXECUTE COMPARE
         BE    B1510               IF ENTRY MATCHES, GO TO B1510
         BNE   B1508               OTHERWISE GO TO B1508
B1505    CLC   0(0,6),0(2)         COMPARE ARGUMENT WITH TABLE ENTRY
B1508    AR    2,3                 INCREMENT ADDRESS OF TABLE
         CR    2,4                 Q-WAS THAT LAST ENTRY
         BE    B1512               A-YES, GO TO ERROR ROUTINE
         B     B1504               GO TO TEST NEXT ENTRY
B1510    LA    8,1(8)              INCREMENT REG 8 TO CORRECT VALUE
         LTR   9,9                 Q-IS P4 ZERO
         BZ    B1521               A-YES, RETURN TO MAIN PROGRAM
         AR    2,8                 SELECT TABLE ENTRY
         BCTR  9,0                 DECREMENT REG 9 BY ONE
         STC   9,B1511+1           STORE LENGTH (FOR MVC)
B1511    MVC   0(0,11),0(2)        MOVE ENTRY TO SAVE AREA
         B     B1521               GO TO RETURN TO MAIN PROGRAM
B1512    ST    12,12(13)           LOAD ERROR ROUTINE
B1521    RETURN (14,12)            RE-LOAD REGISTERS
*
*        AREAS FOLLOW
*
SHW02    DS    H                   WORK AREA FOR P5
         END
