         TITLE 'SYS1.MAN DUMP PROGRAM'
********** REQUIRES MACRO #START   *************
* THIS MODULE DUMPS THW SMF SYS1.MAN FILES SAFELY.
* FUNCTIONS ARE -
*  1. CHECK THAT NOT RECORDING ON SYS1.MAN FILE TO BE DUMPED.
   2. CHECK THAT SYS1.MAN TO BE DUMPED IS NOT EMPTY BEFORE SCREWING UP
*       OUTPUT FILE.
*  3. DE-(OR IS IT UN-) SPAN THE SMF RECORDS FOR LATER COBOL PGS.
*  4. GENERATE THE SMF RECORDS TYPES 2 & 3 (HDR &TRLR).
*  5. AFTER DUMPING & AFTER SURE ALL IS OK THE SYS1MAN FILE IS CUT DOWN
*       TO JUST ONE RECORD AND THEN THE IBM PROGRAM IFASMFDP IS LINKED
*       TO TO REINITIALIZE THE SYS1.MAN FILE AND DO WHAT HAS TO BE DONE
*       TO SMF.
* SAMPLE JCL -
* //STEPNAME EXEC PGM=DUMPSMF,PARM=X
* //DUMPIN DD DSN=SYS1.MANX,
* // DCB=(RECFM=VBS,LRECL=1000,BLKSIZE=1004),
* // DISP=OLD
* //TAPEOUT DD DSN=SMFDUMP.SMFTUE,
* // UNIT=TAPE,VOL=SER=SMFTUE,
* // LABEL=RETPD=5,DCB=BLKSIZE=1004,DISP=(NEW,KEEP)
* //DUMPOUT DD DUMMY
* //SYSPRINT DD DUMMY
DUMPSMF  START
*      AUTHOR - A.G.SIBBALD - C.A.V. LIMITED
         #START *
* USE OF REGS
* R2 NOT USED
* R3 NOT USED
* R4 NOT USED
* R5 NOT USED
* R6 NOT USED
* R7 NOT USED
* R8 NOT USED
* R9 NOT USED
* R10 NOT USED
* R11 SMCA ADDRESS
* R12 EXEC PARM ADDRESS
         TITLE 'EDIT EXEC PARMS'
         L     R12,0(R1)  PICK UP ADDRESS OF EXEC PARMS
         LH    R11,0(R12) GET LEN OF PARMS
         CH    R11,=H'1' CHECK LENGTH
         BL    GA10 ERROR - INVALID EXEC PARMS
         CLI   2(R12),C'X' TEST LIBRARY NAME QUALIFIER
         BE    BA10 OR - ITS AN X
         CLI   2(R12),C'Y' TEST OTHER POSSIBLE VALUE
         BNE   GA10 ERROR - INVALID EXEC PARMS
         TITLE 'DETERMINE IF SYS1.MAN BEING USED'
BA10     L     R11,16 PICK UP CVT ADDRESS
         L     R11,196(R11) PICK UP SMCA ADDRESS
         LTR   R11,R11 TEST SMF PRESENT
         BZ    CA10 NO SMF ON THIS SYSTEM
         TM    104(R11),X'60' TEST SMCASWA
         BNZ   CA10 NOT RECORDING
         TM    1(R11),X'C0' TEST SMCAMISC
         BZ    CA10 NOT RECORDING
         CLC   35(1,R11),2(R12) TEST SMCAXORY
         BE    GA60 ERROR - RECORDING ON DATA SET TO BE DUMPED
         TITLE 'TEST IF SYS1.MAN FILE EMPTY'
         OPEN  (DUMPIN,(INPUT)) OPEN SYS1.MAN FILE
         TM    DUMPIN+48,X'10' TEST OPEN
         BZ    GA20 ERROR - OPEN DIDNT WORK
         GET   DUMPIN,SMFREC READ 1ST SMF RECORD - EODAD IS ERROR
         GET   DUMPIN,SMFREC READ 2ND SMF RECORD - EODAD IS ERROR
         CLOSE (DUMPIN) START FROM BEGINNING AGAIN
         MVC   DUMPIN+33(3),=AL3(EA10) SET NEW EODAD
         TITLE 'INITIALIZE THE FILES'
CA10     OPEN  (DUMPIN,(INPUT)) OPEN SYS1.MAN FILE
         TM    DUMPIN+48,X'10' TEST OPEN
         BZ    GA20 ERROR
         OPEN (TAPEOUT,(OUTPUT)) OPEN THE OUTPUT FILE
         TM    TAPEOUT+48,X'10' TEST OPEN
         BZ    GA30 ERROR
         TIME  BIN
         STM   R0,R1,SPECRECF SET TIME & DATE IN SPECIAL RECORD
         MVI   SPECRECT,2 MAKE IT A DUMP HEADER
         PUT   TAPEOUT,SPECREC WRITE THE DUMP HEADER
         TITLE 'COPY SYS1.MAN FILE'
DA10     GET   DUMPIN,SMFREC READ SYS1.MAN RECORD
         AP    RECCNT,=P'1' COUNT THE RECORDS
         PUT   TAPEOUT,SMFREC WRITE THE OUTPUT
         B     DA10 GO PROCESS NEXT RECORD
         TITLE 'WRITE SMF TERMINATION RECORDS AND CLOSE FILES'
EA10     TIME  BIN
         STM   R0,R1,SPECRECF SET TIME & DATE IN SPECIAL RECORD
         MVI   SPECRECT,3 MAKE IT A DUMP TRAILER
         PUT   TAPEOUT,SPECREC WRITE THE DUMP TRAILER
         AP    RECCNT,=P'1' COUNT IT IN
         CLOSE (DUMPIN) CLOSE THE SYS1.MAN FILE
         CLOSE (TAPEOUT) CLOSE THE OUTPUT FILE
         FREEPOOL TAPEOUT RELEASE THE BUFFERS
         ED    WTORECNT,RECCNT SET RECORD COUNT IN WTOR RECORD
         LA    R1,WTOREC SET PARM REG
         SVC   35 ISSUE WTO SVC
         TITLE 'TIDY UP THE SYS1.MAN FILE'
         CP    RECCNT,=P'3' TEST THE RECORD COUNT
         BL    GA50 ERROR - SYS1.MAN FILE EMPTY
         OPEN  (DUMPIN,(OUTPUT)) OPEN SYS1.MAN AS OUTPUT NOW
         TM    DUMPIN+48,X'10' DID OPEN WORK
         BZ    GA20 ERROR
         MVI   SPECRECT,255 SET AS DUMMY RECORD TYPE
         PUT   DUMPIN,SPECREC WRITE THE DUMMY RECORD
         CLOSE (DUMPIN) TRUNCATE THE FILE TO 1 RECORD
         FREEPOOL DUMPIN RELEASE THE BUFFERS
         LINK  EP=IFASMFDP GO DO THE HOUSEKEEPING REQD
         LTR   R15,R15 TEST RETURN CODE
         BNP   FA10 ZERO RC OR NOT SET
         CH    R15,=H'4096' TEST VS MAX POSS VALUE
         BL    GA40 RETURN CODE INDICATES ERROR
FA10     L     R13,4(R13) PREPARE TO RETURN
         RETURN (14,12),RC=0
         TITLE 'ERROR CONDITIONS'
GA10     WTO   'DUMPSMF-01 INVALID EXEC PARM - MUST OR X OR Y'
         B     GB10
GA20     WTO   'DUMPSMF-02 DD STATEMENT FOR DUMPIN MISSING'
         B    GB10
GA30     WTO   'DUMPSMF-03 DD STATEMENT FOR TAPEOUT MISSING'
         B     GB10
GA40     WTO   'DUMPSMF-04 IFASMFDP NON-ZERO RETURN CODE'
         B     GB10
GA50     MVC   GA50A+27(1),2(R12) SET X OR Y IN MESSAGE
GA50A    WTO   'DUMPSMF-05 SYS1.MAN* IS EMPTY'
         B     GB10
GA60     MVC   GA60A+37(1),2(R12) SET X OR Y IN MESSAGE
GA60A    WTO   'DUMPSMF-06 CANT DUMP SYS1.MAN* SINCE SMF IS USING IT'
         B     GB10
GB10     ABEND 4095
         TITLE 'CONSTANTS AND AREAS'
         DC    0F'0',H'0' ENSURE ALIGNMENT
SPECREC  DC    AL2(18,0),X'00'
SPECRECT DC    X'00'
SPECRECF DC    XL8'00'
         DC    CL4'DUMP'
RECCNT   DC    PL4'1'
WTOREC   DC    AL2(WTOREND-*,0),C'DUMPSMF-00 NO OF SMF RECORDS'
WTORECNT DC    X'4020202020202120'
WTOREND  EQU   *
         LTORG
DUMPIN   DCB   DSORG=PS,MACRF=(GM,PM),DDNAME=DUMPIN,EODAD=GA50
TAPEOUT  DCB   DSORG=PS,MACRF=PM,DDNAME=TAPEOUT,                       C
               LRECL=1000,RECFM=VB
SMFREC   DS    0D,CL1000
         END
