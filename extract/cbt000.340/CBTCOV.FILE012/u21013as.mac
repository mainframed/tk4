DFRE     TITLE 'ZAJDFRE - CALCULATE FREE SPACE ON A DISK'
*        THIS ROUTINE IS TO BE CALLED FROM PL/I BY THE STATEMENT      *
*                                                                     *
*              CALL ZAJDFRE(VOL,CYL,TRK);                             *
*                                                                     *
*          WHERE VOL, CHAR(6), IS THE VOLUME SERIAL OF THE DISK, AND  *
*          CYL AND TRK, BOTH FIXED BIN(15), ARE VARIABLES INTO WHICH  *
*          THE NUMBERS OF FREE CYLINDERS AND TRACKS RESPECTIVELY ARE  *
*          TO BE STORED.                                              *
*                                                                     *
*          FOR DETAILS OF DSCB LAYOUTS SEE THE SYSTEM CONTROL BLOCKS  *
*          MANUAL, C28-6628.                                          *
*                                                                     *
*        THIS VERSION OF THE ROUTINE IS FOR USE WITH THE PL/I
*          OPTIMISING COMPILER
*
**    AUTHORS - ICI (MOND DIVISION)
ZAJDFRE  PLISAV
*                        PROCESS PARAMETERS                           *
         LM    2,4,0(1)
         L     2,0(,2)
         MVC   VOL(6),0(2)         COPY VOLUME SERIAL
         CLC   VOL(3),=C'PTR'      VOLUME SERIAL PROVIDED ?
         BNE   PROCESS             BRANCH IF SO.
*                                                                     *
*                        FIRST FIND THE VOLUME SERIAL                 *
*                                                                     *
         MVC   PTRNAM(6),VOL       COPY POINTER DATASET NAME.
         LOCATE GETVOL             GET VOLUME BLOCK.
         LTR   15,15               FOUND OK ?
         BNZ   ERROR               BRANCH IF NOT.
         MVC   VOL(6),AREA+6       COPY REAL VOLUME SERIAL.
*                        INITIALISE COUNTERS
PROCESS  EQU   *
         SR    5,5                 NUMBER OF FULL CYLINDERS
         SR    6,6                 NUMBER OF ODD TRACKS
*                        FIND FIRST FORMAT 5 DSCB
         OBTAIN FMT4
         CLI   AREA,X'F4'          HAVE WE FOUND FMT 4 DSCB ?
         BNE   ERROR               BRANCH IF NOT
         MVC   CCHHR(4),AREA+63    CCHH OF START OF VTOC
         MVI   CCHHR+4,2           FIRST FORMAT 5 IS RECORD 2
         TM    AREA+14,X'80'       DO FORMAT 5 DSCBS EXIST ?
         LA    15,12
         BO    ERROR               BRANCH IF NOT.
*
*                        PROCESS EACH FORMAT 5 DSCB
*
LOOPA    OBTAIN FMT5
         LTR   15,15
         BNZ   ERROR
         CLI   AREA+44,X'F5'       IS THIS A FORMAT 5 ?
         LA    15,8
         BNE   ERROR               BRANCH IF NOT
         LA    7,8
         LA    8,AREA+4
LOOPB    EQU   *
         MVC   HALFWD,2(8)         NO. OF UNUSED CYLS IN BINARY
         AH    5,HALFWD            ACCUM NO. OF UNUSED CYLS
         IC    15,4(,8)
         AR    6,15
         LA    8,5(,8)
         BCT   7,LOOPB
*
         LA    7,18
         LA    8,AREA+45
LOOPC    EQU   *
         MVC   HALFWD,2(8)         NO. OF UNUSED CYLS IN BINARY
         AH    5,HALFWD            ACCUM NO. OF UNUSED CYLS
         IC    15,4(,8)
         AR    6,15
         LA    8,5(,8)
         BCT   7,LOOPC
*
         MVC   CCHHR(5),AREA+135
         CLC   CCHHR(5),=XL5'00'   ANOTHER FORMAT 5 ?
         BNE   LOOPA               BRANCH IF SO.
*
BACK     STH   5,0(,3)             STORE
         STH   6,0(,4)               RESULTS
         L     13,4(,13)
         RETURN (14,11)
*
ERROR    SRL   15,2
         IC    14,CODES-1(15)
         STC   14,ERCOD
         PLIMSG ERR
         B     BACK
         EJECT
*              CONSTANTS AND WORK AREAS                               *
*                                                                     *
AREA     DS    90F
FMT4     CAMLST SEARCH,FOURS,VOL,AREA
FMT5     CAMLST SEEK,CCHHR,VOL,AREA
GETVOL   CAMLST NAME,PTRNAM,,AREA
FOURS    DC    44X'04'
PTRNAM   DC    CL44' '
ERR      DC    A(ERRMES),AL2(29),AL2(0)
ERRMES   DC    C'ERROR CODE '
ERCOD    DC    C' '
         DC    C' ON VOLUME '
VOL      DC    CL6'      '
CODES    DC    C'1234'
CCHHR    DS    5C
HALFWD   DS    H                   HALFWORD WORK AREA
*
S        DSECT
SAV      DS    26F                 SAVE AREA
         END
