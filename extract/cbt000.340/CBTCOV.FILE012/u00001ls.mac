TSGVHEC  CSECT
*
*  EVERY TIME A NEW JOB IS ENCOUNTERED IN THE INPUT STREAM THIS CSECT
*  UPDATES (IN THE 'UNIQUE NAME' FIELD OF THE INTERPRETER WORK AREA)
*  THE DATE AND TIME USED TO CREATE SYSTEM GENERATED DSNAMES.
*  WITHOUT THIS FIX THE DATE AND TIME USED FOR ALL TEMP DSNAMES ARE
*  THOSE OBTAINED AT TIME OF START RDR.  WITH THIS ADDITION THEY BECOME
*  THE DATE AND TIME OF READING EACH INDIVIDUAL JOB.
*
*  THIS ADDITION BENEFITS USERS OF A RESIDENT READER, OR A TRANSIENT
*  READER THAT DOES NOT END (BECAUSE OF ROLL-OUT, OR BECAUSE EOF IS NOT
*  SET), IN TWO WAYS.
*        1  IT ELIMINATES DUPLICATE NAMES WHEN THE SAME JOB (WITH JCL
*           USING DSN=&NAME) IS RE-READ BY THE SAME READER BEFORE THE
*           PREVIOUS TEMP DATA SETS HAVE BEEN SCRATCHED.
*        2  IT ALLOWS USERS OF SYSSCRAT TO SPECIFY A TIME DEADLINE
*           FOR AUTOMATIC SCRATCHING OF TEMP DATA SETS IN THE KNOWLEDGE
*           THAT NO DATA SETS BELONGING TO JOBS READ SINCE THAT TIME
*           WILL BE SCRATCHED.
*
*  TO INCORPORATE THIS CSECT INTO YOUR READER/INTERPRETER FIRST
*  COMPILE AND LINK EDIT THIS CSECT INTO A LIBRARY, SAY DSN=NEWMOD AND
*  MEMBERNAME=TSGVHEC, THEN USE THE FOLLOWING LINKEDIT....
*  //LINKRDR  EXEC  PGM=IEWL,PARM='XREF,LIST,NCAL,LET'
*  //SYSPRINT  DD  ...
*  //SYSUT1  DD  ...
*  //NEW  DD  DSN=NEWMOD,...
*  //SYSLMOD  DD  DSN=SYS1.LINKLIB,DISP=SHR
*  //SYSLIN  DD  *
*        INCLUDE  NEW(TSGVHEC)
*        CHANGE  IEFVHEC(TSGVHEC)
*        INCLUDE  SYSLMOD(IEFVHA)      (THIS DOUBLE INCLUDE IS
*        INCLUDE  SYSLMOD(IEFVHA)   NECESSARY OR IEFVHEC WILL BE LOST)
*        ALIAS  NEWVHCB                (OR IEFVHCB)
*        ENTRY  IEFVHA
*        NAME  NEWVHA(R)               (OR IEFVHA)
*  /*
*
*  WHEN YOU ARE SATISFIED THAT THE ABOVE LINKEDIT IS SUCCESSFUL BRING
*  THE NEW READER/INTERPRETER INTO OPERATION BY THE FOLLOWING RENAME..
*        ...MEMBER=IEFVHA,NEWNAME=OLDVHA
*        ...MEMBER=IEFVHCB,NEWNAME=OLDVHCB
*        ...MEMBER=NEWVHA,NEWNAME=IEFVHA
*        ...MEMBER=NEWVHCB,NEWNAME=IEFVHCB
*
*  THE ABOVE PROCEDURE SHOULD PREVENT LOSS OF YOUR READER, BUT THE
*  SYSTEM IS VERY EXPOSED AND YOU SHOULD NOT BEGIN WITHOUT HAVING
*  A BACK-UP OF LINKLIB AVAILABLE IN A FORM THAT DOES NOT REQUIRE
*  YOU TO START A READER ON THE AFFECTED SYSTEM.
*
*
         BALR  11,0
         USING *,11
         STM   0,1,TSAVE
         TIME
         SRL   0,4
         STM   0,1,TIME
         OI    TIME+3,15
         OI    DATE+3,15
         UNPK  X'37'(5,12),DATE+1(3)
         UNPK  X'3E'(6,12),TIME(4)
         L     15,VCON
         LM    0,1,TSAVE
         BR    15
VCON     DC    V(IEFVHEC)
TSAVE    DC    2F'0'
TIME     DC    F'0'
DATE     DC    F'0'
         END
