GRIPE    CSECT
**********************************************************************
*     GRIPE IS USED TO INDICATE WHEN USERS ARE NOT SATIAFIED WITH    *
*      THE RESPONSE TIME ON A TERMINAL.                              *
*     GRIPE WRITES AN SMF RECORD WITH DATE ,TIME AND USERID.         *
*     IT HAS TO RUN IN PROTECT KEY 0 AND MUST BE HELD IN AN          *
*      AUTHORISED LIBRARY.                                           *
*                                                                    *
*                                                                    *
*    WRITTEN BY JESS HAND           MET.O.12B          15/07/83      *
**********************************************************************
          EQUREG R
          DS    0H
          STM   14,12,12(13)      SAVE REGISTERS
          LR    R2,R13
          BALR  R13,0
          USING *,13              ADDRESSABILITY
          GETMAIN R,LV=108
          LR    R4,R1             DSECT ADDRESSING
          USING SMF131,R4
          ST    13,8(R2)          L S A ADDRESS INTO H S A
          ST    R2,SAVE+4         H S A ADDRESS INTO L S A
          L     R6,16             ADDRESS OF CVT
          L     R6,0(R6)          POINTER TO ADDRESS OF ACTIVE TCB
          L     R6,0(R6)          ADDRESS OF TCB
          L     R6,12(R6)         ADDRESS OF TIOT
          MVC   18(8,R4),0(R6)    MOVE USERID TO SMF RECORD
*
*      TIME OF DAY IN HUNDRETHS OF SECONDS SINCE MIDNIGHT.
*
          TIME  BIN
          ST    R0,WORK
          MVC   6(4,R4),WORK      TIME TO SMF RECORD
*
*     DATE IN FORM 00YYDDDF (PACKED DECIMAL)
*
          ST    R1,WORK
          MVC   10(4,R4),WORK      DATE TO SMF RECORD
*
*     MOVE TO SMF RECORD X'001A'(RECORD LENGTH),X'000002'(SEGMENT DESC
*      AND SYSID),X'83'(RECORD TYPE).
*
          MVC   0(6,R4),=X'001A00000283'
*
*    MOVE TO SMF RECORD C'M381'(SYSTEM IDENTIFYER)
*
          MVC   14(4,R4),=C'M381'
*
*    THIS PUTS THE SMF RECORD TO THE SMF DATASET.
*
*    THIS CODING SETS UP THE AUTHORISATION USING SVC241
*    AND THEN WRITES THE SMF RECORD TO THE SMF DATASET.
*
*   NO ATTNS WILL BE PROCESSED AND AN ESTAE ENVIROMENT IS ESTABLISHED
*
          STAX  DEFER=YES               DEFER ATTN INTERUPTS
          LA    R3,40                   ERROR CODE IN REG 3
          LTR   R15,R15                 CHECK RETURN CODE
          BNZ   RETURN
*    ESTAE ENVIROMENT.
          ESTAE ESTAEX                  ESTABLISH ESTAE ENVIROMENT
          LA    R3,44                   ERROR CODE IN REG 3
          LTR   R15,R15                 CHECK RETURN CODE
          BNZ   STAXOFF
*      SET AUTHORISATION ON
          LA    R0,1                    SET UP AUTHORISATION.
          SVC   241
*
*    WRITE SMF 131 RECORD TO DATASET.
*
          SMFWTM (R4)                  WRITE TO SMF DATASET.
*
*   RESET AUTHORISATION TO OFF
*
          LR    R3,R15                 SAVE RETURN CODE
          XR    R0,R0                  SET AUTHORISATION OFF
          SVC   241                    AUTHORISATION OFF
          ESTAE 0                      END OF ESTAE ENVIROMENT
*   END OF ESTAE ENVIROMENT
*   ALLOW ATTN INTERUPTS AGAIN
STAXOFF   STAX  DEFER=NO               ALLOW ATTN INTERUPTS
RETURN    L     R13,SAVE+4             RESTORE R13
          FREEMAIN R,LV=108,A=(4)      FREE WORK AREA
          LR    R15,R3                 RESET RETURN CODE
          L     R14,12(R13)            RESTORE R14
          LM    0,12,20(13)            RESTORE REGISTERS
          BR    R14                    RETURN
*
*    ESTAE EXIT ROUTINE
*
ESTAEX    DS    0H
          XR    R0,R0                  SET AUTHORISATION OFF
          SVC   241                    AUTHORISATION OFF
          XR    R15,R15
          BR    R14                    CONTINUE WITH ABEND.
*
*
*
SMF131    DSECT
SMFREC    DS    CL26
WORK      DS    F
SAVE      DS    9D
          END
