*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSU39CMD.
SSU39CMD ENTER ENT=COMMAND
         L     2,0(0,1)                PICK UP PARM ADDRESS
         LH    3,0(0,2)                PICK UP LINGTH
         LTR   3,3                     ANY PARM?
         BZ    EXIT                    NO - EXIT
         SVC   255                     PUT IN PROT KEY ZERO
         SR    4,4                     SET UP COUNTER
         LA    5,2(2)                  ADDR. OF  START OF PARM FIELD
         LA    2,2(2)                  BUMP TO START
LOOPA    CLI   0(2),C'/'               IS IT A SLASH?
         BE    GOT1                    YES - GO PROCESS IT
         LA    4,1(4)                  BUMP COUNTER
         LA    2,1(2)                  BUMP TO NEXT BYTE
         BCT   3,LOOPA                 GO BACK
*
* AT THIS POINT WE ARE PROCESSING THE LAST OR ONLY PARM
*   REG 4 CONTAINS THE LENGTH OF HHE COMMAND AND
*   REG 5 POINTS TO THE START OF THE COMMAND
*
DORTN    LA    4,4(4)                  INCRE BY LEN OF C/BLOCK
         STH   4,CB1                   STORE LEN FOR MGCR
         SH    4,=H'5'                 REDUCE FOR MOVE
         STC   4,*+5                   INSERT LEN IN MVC
         MVC   CB1+4(1),0(5)           MOVE CMD INTO C/BLOCK
         LA    1,CB1                   ADDRESS OF C/B
         CLI   1(5),X'E0'          IS IT SPECIAL AUTHORISATION?
         BNE   *+12
         MVI   1(5),C' '
         MVI   CB1+5,C' '              BLAMK OYT CODE
         CLI   1(5),C' '
         BE    ABBREV
         LA    6,TAB1
LOOPB    LH    7,0(0,6)
         EX    7,COMP1
         BE    ISSUE
         LA    6,10(0,6)
         CLI   0(6),X'FF'
         BE    LOAD
         B     LOOPB
ABBREV   LA    6,TAB2
LOOPC    CLC   0(2,5),0(6)
         BE    ISSUE
         LA    6,2(0,6)
         CLI   0(6),X'FF'
         BNE   LOOPC
LOAD     L     0,AUTH
ISSUE    SVC   34
MSW      NOPR  12                      BRANCH IF MULTI CMDS
EXIT2    SVC   255                     OUT OF PROT KEY ZERO
EXIT     LEAVE 0
GOT1     LTR   4,4                     ANY COMMAND?
         BZ    IDIOT                   NO - TRY NEXT ONE
         OI    MSW+1,X'F0'             TURN SWITCH ON
         BAL   12,DORTN                GO ISSUE COMMAND
IDIOT    LA    5,1(2)                  POINT REG 5 AT NEXT PARM FLD
         LA    2,1(2)                  GO PAST SLASH
         SR    4,4                     RESET COUNTER
         NI    MSW+1,X'0F'             TURN SWITCH OFF
         BCT   3,LOOPA                 GO LOOJ FOR NEXT SLASH
*
*  IF WE COME THRU HERE THEN HE FINISHED WITH A SLASH SO EXIT
*
         B     EXIT2                   GO EXIT
         LTORG
AUTH     DS    0F
         DC    X'0000E000'         GROUPS 1,2 & 3
CB1      DS    0F
         DC    H'7'
         DC    H'0'
         DC    CL100'D T'
COMP1    CLC   0(1,5),2(6)
TAB1     DS    0H
         DC    H'5',CL8'BRDCST'
         DC    H'6',CL8'CONTROL'
         DC    H'6',CL8'DISPLAY'
         DC    H'2',CL8'LOG'
         DC    H'4',CL8'REPLY'
         DC    H'6',CL8'MONITOR'
         DC    H'2',CL8'MSG'
         DC    H'3',CL8'SHOW'
         DC    H'5',CL8'STOPMN'
         DC    X'FF'
TAB2     DC    C'K '
         DC    C'D '
         DC    C'L '
         DC    C'MN'
         DC    C'MR'
         DC    C'R '
         DC    C'PM'
         DC    X'FF'
         END
