         MACRO
&LOC     PRINTOUT
.*       CODED BY JOHN R. EHRMAN (SLAC) 26 MAY 1967
         LCLA    &N,&P,&Q
         LCLC    &TYPE,&NAME
&N       SETA    N'&SYSLIST            NUMBER OF OPERANDS
         SPACE
&LOC     DS      0H
         CNOP    2,4                   ALIGN FOR SUBROUTINE JUMP
         STM     14,15,*+10             SAVE LINKING REGISTERS
         L       15,*+18               LOAD BRANCH ADDRESS
         BALR    14,15                  JUMP TO PRINT ROUTINE
         DC      2F'0'                  SAVE R14 AND R15
         DC      F'&N'                 NUMBER OF ITEMS
         AIF     ('&LOC' EQ '').C
         DC      X'80',VL3(JREPRNTO)   ENTRY ADDRESS + NAME FLAG
         DC      CL8'&LOC'             MACRO-INSTRUCTION NAME
         AGO     .B
.C       DC      X'00',VL3(JREPRNTO)   ENTRY ADDRESS
.B       ANOP
&P       SETA    &P+1
         AIF     (&P GT &N).D
         AIF     (K'&SYSLIST(&P) LE 8).H
  MNOTE '***''&SYSLIST(&P)'' IS TOO LONG A NAME, 1ST 8 CHARS USED.'
.H       ANOP
&NAME    SETC    '&SYSLIST(&P)'
&TYPE    SETC    T'&SYSLIST(&P)
         AIF     ('&TYPE' NE 'N').EB   OK IF NOT SELF-DEFINING
&Q       SETA    &SYSLIST(&P)          CHANGE TO DECIMAL REP FOR NAMING
&NAME    SETC    '&Q'                  CHANGE BACK TO CHARACTERS
         AGO     .F
.EB      AIF    ('&TYPE' EQ 'O' OR '&TYPE' EQ 'U' OR '&TYPE' EQ 'J').EA
.E       AIF     ('&TYPE' EQ 'C' OR '&TYPE' EQ 'X' OR '&TYPE' EQ 'R').J
         AIF     ('&TYPE' EQ 'B' OR '&TYPE' EQ 'G' OR '&TYPE' EQ 'K').J
         AIF     ('&TYPE' EQ 'I' OR '&TYPE' EQ 'Z' OR '&TYPE' EQ 'P').J
.F       DC      CL8'&NAME',CL1'&TYPE',AL3(&NAME)  NAME,TYPE,LOC
         AGO     .B
.EA      DC      CL8'&NAME. ',CL1'&TYPE',AL3(0)  NAME,TYPE
         AGO     .B
.J       ANOP                          PROCESS TYPES WITH LENGTHS
&Q       SETA    L'&SYSLIST(&P)        GET LENGTH
     DC  CL8'&NAME',CL1'&TYPE',AL3(&NAME),F'&Q'  NAME,TYPE,LOC,LEN
         AGO     .B
.D       LM      14,15,0(14)            RESTORE LINK REGISTERS
         SPACE
.END     MEND
