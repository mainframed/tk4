Z32PRINT SETUP 13,12,EQUATE=YES
         EJECT
*
*  SWITCH SETTINGS
*
*        B0 ON  =>  NOT FIRST TIME THRO'
*        B1 ON  =>  END OF CARD
*        B2 ON  =>  SELECTION REQ'D
*        B3 ON  =>  MORE THAN 1 MEMBER PER PAGE
*        B4 ON  =>  1 READ ALREADY CARRIED OUT
*        B5 ON  =>  END OF MEMBER REACHED
*        B6 ON  =>  4 READS COMPLETED
*        B7 ON  =>  SELECTED MEMBER NOT FOUND
*
B0       EQU   X'80'
B1       EQU   X'40'
B2       EQU   X'20'
B3       EQU   X'10'
B4       EQU   X'08'
B5       EQU   X'04'
B6       EQU   X'02'
B7       EQU   X'01'
         EJECT
         L     R4,0(R1)
         CLC   0(2,R4),=H'0'
         BE    NOSKIP
         CLC   2(6,R4),=C'NOSKIP'
         BNE   *+8
NOSKIP   OI    SWITCH,X'10'        SET NOSKIP  SWITCH
         B     PARMEND
         CLC   2(4,R4),=C'SKIP'
         BE    PARMEND
         B     INVPARM
PARMEND  EQU   *
*
         TIME  DEC
         ST    1,PDATE
         UNPK  DATE(5),PDATE+1(3)
         ST    0,PTIME
         UNPK  TIME(5),PTIME(3)
         MVI   TIME+4,C' '
         EJECT
         OPEN  (IN,(INPUT))
         TM    IN+48,X'10'
         BZ    ABEND
*
         OPEN  (PDCB,(INPUT))
         TM    PDCB+48,X'10'
         BZ    ABEND
*
         RDJFCB PDCB
         MVC   DSNAME(25),JFCB     ASSUME MAX 25 CHAR DSNAME
         ENQ   (QNAME,RNAME,E,44,SYSTEM)
         LH    R6,PDCB+82          PDS  LRECL
         CH    R6,=H'120'          COMPARE WITH 120
         BL    *+8                 <120
         LH    R6,=H'120'          ELSE SET TO 120
         CVD   R6,DRECL            CONVERT LRECL(<120) TO DECIMAL
         LA    R6,1(R6)
         STH   R6,OUT+82           SET OUTPUT LRECL TO PDS LRECL+1
*
         OPEN  (OUT,(OUTPUT))
         TM    OUT+48,X'10'
         BZ    ABEND
*
         EJECT
         LH    R0,PDCB+62     LOAD BLKSIZE
         SLL   R0,2           4 * BLKSIZE
         BAL   1,*+4
         SVC   10             ISSUE GETMAIN
         LR    5,R1           R5 PTS TO INPUT AREA
         ST    R5,STORADDR
         OPEN  (CARD,(INPUT))
         TM    CARD+48,X'10'
         BZ    GETIN
         PUT   OUT,CTLCARDS
         MVI   PFCC,C'-'
*
         GETMAIN R,LV=808     GET STORAGE FOR SELECT LIST
         OI    SWITCH,X'20'
         LR    R10,R1
         ST    R10,LISTADDR
         EJECT
******************************
*  ANALYSIS OF SELECT CARDS  *
******************************
GETCARD  GET   CARD
         MVC   OUTLINE(80),0(R1)
         BAL   11,PUTMESG
         MVI   PFCC,C' '
         NI    SWITCH,X'BF'        SET OFF EOC SWITCH
         LR    R9,R1
LABTEST  LA    R1,9
LOOP1    CLI   0(R9),C' '          CHECK FOR LABEL
         BE    NOLAB
         LA    R9,1(R9)
         BCT   R1,LOOP1
         B     INVLABEL
*
NOLAB    LA    R9,1(R9)
         LA    R1,50
LOOP2    CLI   0(R9),C' '          SEARCH FOR NEXT INFO. ON CARD
         BNE   CHECKF
         LA    R9,1(R9)
         BCT   R1,LOOP2
         B     INVCARD
*
CHECKF   CLC   0(7,R9),=C'SELECT '      CHECK FUNCTION
         BE    SEL1
         CLC   0(2,R9),=C'S '
         BE    SEL2
         B     INVFUNCT
*
         EJECT
SEL1     LA    R9,5(R9)                 INCREMENT INPUT PTR.
SEL2     LA    R9,2(R9)
*
         LA    R1,50
LOOP3    CLI   0(R9),C' '               SEARCH FOR NEXT INFO. ON CARD
         BNE   CHECKM
         LA    R9,1(R9)
         BCT   1,LOOP3
         B     INVCARD
*
CHECKM   CLC   0(7,R9),=C'MEMBER='      CHECK PARAMETER
         BE    M1
         CLC   0(2,R9),=C'M='           CHECK PARAMETER
         BE    M2
         B     INVCARD
*
         EJECT
M1       LA    R9,5(R9)            INCREMENT INPUT PTR.
M2       LA    R9,2(R9)
*
         CLI   0(R9),C'('
         BNE   ONEMEM
         LA    R9,1(R9)
TRT      TRT   0(9,R9),TRTABLE
         BZ    INVNAME
         CLI   0(R1),C','         MORE MEMBERS ON THIS CARD?
         BE    CONT1               YES!
         CLI   0(R1),C')'          TEST FOR END OF CARD
         BE    SETSW40             IF SO,SET EOC SWITCH
         TM    SWITCH,X'40'
         BZ    INVCARD
         CLI   0(R1),C' '
         BNE   INVNAME
SETSW40  OI    SWITCH,X'40'
CONT1    SR    R1,R9
         LTR   R1,R1
         BZ    INVNAME
         BCTR  1,0
         MVC   0(8,R10),BLANK      BLANK NEXT LIST ENTRY
         EX    1,MOVELIST          MOVE MEMBER NAME INTO LIST
         LA    R10,8(R10)          INCREMENT LIST POINTER
         TM    SWITCH,X'40'        TEST FOR END OF CARD
         BO    GETCARD             YES.
         AR    R9,R1
         LA    R9,2(R9)            RESET R9 TO PT. TO NEXT MEMBER
         B     TRT
*
ONEMEM   OI    SWITCH,X'40'
         B     TRT
*
         EJECT
ENDOFILE MVC   0(8,R10),=8X'FF'    SET END OF SELECT LIST MARKER
         L     R10,LISTADDR        RESET LIST PTR.
FINDNAME CLC   0(8,R10),=8X'FF'    TEST FOR END OF LIST
         BE    EODIR
         MVC   MEMNAME(8),0(R10)
         FIND  PDCB,(R10),D
         LTR   15,15
         BNZ   NOTFOUND
         MVI   0(R10),X'00'  MEMBER SUCCESSFULLY FOUND
         B     READ1
*
         EJECT
GETIN    EQU   *
*********************
*  READ  DIRECTORY  *
*********************
GET      GET   IN
         LR    R3,R1
         LH    R4,0(1)        R4 HAS LENGTH OF BLOCK
         SH    R4,=H'2'
         STH   R4,BLKCNT      STORE BLKLEN
         LA    R3,2(R3)       PTS TO FIRST ENTRY
MOVENAME MVC   MEMNAME(8),0(R3)
         CLC   MEMNAME(8),=8X'FF'  END OF DIRECTORY?
         BE    EODIR
         TM    11(R3),X'80'        ALIAS?
         BZ    *+10
         MVC   MEMNAME+9(7),=C'(ALIAS)'
         MVC   TTRK(3),8(R3)
*
         FIND  PDCB,TTRK,C
         LTR   15,15
         BNZ   ABEND
*
         EJECT
READ1    L     R5,STORADDR
         READ  DECB,SF,,(5),MF=E
         CHECK DECB
         NI    SWITCH,X'FD'        SET OFF 4 READS COMPLETE SWITCH
         LH    R2,PDCB+62     LOAD BLKSIZE
         L     R7,PDCB+68     LOAD IOB PREFIX ADDR
         SH    R2,22(R7)      SUBTRACT RESIDUAL CNT FROM BLKSIZE
READ2    AR    R5,R2               INCREMENT IOAREA PTR
         READ  DECB,SF,,(5),MF=E
         CHECK DECB
         LH    R6,PDCB+62
         L     R7,PDCB+68
         SH    R6,22(R7)
         AR    R2,R6               INCREMENT DATA LENGTH
READ3    AR    R5,R6
         READ  DECB,SF,,(5),MF=E
         CHECK DECB
         LH    R6,PDCB+62
         L     R7,PDCB+68
         SH    R6,22(R7)
         AR    R2,R6               INCREMENT DATA LENGTH
READ4    AR    R5,R6
         READ  DECB,SF,,(5),MF=E
         CHECK DECB
         LH    R6,PDCB+62
         L     R7,PDCB+68
         SH    R6,22(R7)
         AR    R2,R6
         OI    SWITCH,X'02'        SET ON 4 READS COMPLEE SWITCH
         TM    SWITCH,X'08'
         BO    PRINT
NEWPAGE  AP    PAGE,=P'1'
         ZAP   LINES,=P'55'
         OI    SWITCH,X'80'       SET SECOND TIME THRO' SWITCH
         MVC   VARINFO(23),HEADINFO
         MVI   TITLE,C'1'
         BAL   11,PRINTITL
         OI    SWITCH,X'08'
         B     PRINT
         EJECT
PRINT    L     R5,STORADDR
         LH    R6,PDCB+82     LOAD LRECL
         LR    R7,R6
         CH    R7,=H'120'          COMPARE WITH 120
         BL    *+8
         LH    R7,=H'120'          IF HIGH,SET R7=120
         BCTR  R7,0
EXPRIN   EX    R7,MOVEPRIN         MOVE  INTO PRINLINE
         BAL   11,PUT
         AR    R5,R6               INCREMENT I/O PTR BY LRECL
         SR    R2,R6               DECREMENT COUNT BY LRECL
         BZ    ENDPRIN             IF ZERO,END PRINT
         CR    R2,R6
         BL    ABEND
         B     EXPRIN
         EJECT
ENDPRIN  ZAP   COUNT,=P'0'
         TM    SWITCH,X'04'
         BZ    READ1
         MVI   PRINLINE,C' '
         MVI   PRINLINE+1,C'*'
         MVC   PRINLINE+2(119),PRINLINE+1
         LA    0,PRINLINE
         LA    1,OUT
         L     15,48(1)
         BALR  14,15               WRITE LINE OF *'S
         MVI   PRINLINE+1,C' '
         MVC   PRINLINE+2(119),PRINLINE+1
         NI    SWITCH,X'F3'        SET OFF  5,6
         TM    SWITCH,X'20'
         BZ    *+12
         LA    R10,8(R10)
         B     FINDNAME
*
         LH    R5,10(R3)
         N     R5,=F'31'           LENGTH OF USER DATA IN NEXT ENTRY
         AR    R5,R5
         LA    R5,12(R5)
         LH    R6,BLKCNT
         SR    R6,R5
         BNZ   INCR
         B     GET
*
INCR     STH   R6,BLKCNT
         AR    R3,R5
         B     MOVENAME
*
         EJECT
PRINTITL PUT   OUT,TITLE
         MVI   PRINLINE,C'-'       SET SKIP 3 LINES
         MVC   MEMNAME+9(9),MEMNAME+8
         BR    11
PUT      SP    LINES,=P'1'
         BZ    SETLINES
PUTPRIN  PUT   OUT,PRINLINE
         MVI   PRINLINE,C' '
         BR    11
SETLINES ZAP   LINES,=P'55'
         MVC   VARINFO(23),HEADINFO
         MVI   TITLE,C'1'
         MVC   MEMNAME+9(8),=C'(CONT''D)'
         PUT   OUT,TITLE
*
         MVC   MEMNAME+9(9),MEMNAME+8
         MVI   PRINLINE,C'-'       SET SKIP 3 LINES
         B     PUTPRIN
*
*
EOMEM    OI    SWITCH,X'04'        SET EOMEM SWITCH
         TM    SWITCH,X'02'
         BO    ENDPRIN
         TM    SWITCH,X'08'
         BO    PRINT
         TM    SWITCH,X'80'       TEST SECOND TIME
         BZ    NEWPAGE
         SP    LINES,=P'7'         SUBTRACT NO REQ'D FOR TITLE
         CVD   R2,DCOUNT           R2 CONTAINS LENGTH OF INPUT
         DP    DCOUNT(8),DRECL+6(2)    00000000XXXCXXXC
         ZAP   COUNT(3),DCOUNT+3(3)
         AP    COUNT,=P'1'         ADD 1 TO GIVE NO OF OUTPUT RECORDS
         CP    LINES,COUNT
         BL    NEWPAGE
         MVI   TITLE,C'-'
         MVI   VARINFO,C' '
         MVC   VARINFO+1(22),VARINFO
         BAL   11,PRINTITL
         B     PRINT
*
         EJECT
EODIR    CLOSE PDCB
         CLOSE IN
         TM    SWITCH,B7      ANY MEMBERS NOT FOUND ?
         BZ    CLOSEOUT
         PUT   OUT,ERRORMSG  PRINT HEADING
         L     R10,LISTADDR
CLI00    CLI   0(R10),X'00'  MEMBER SELECTED?
         BNE   CHECKEND      END OF LIST?
         LA    R10,8(R10)
         B     CLI00
*
CHECKEND CLI   0(R10),X'FF'
         BE    CLOSEOUT
         MVC   Z3200A+20(8),0(R10)
         PUT   OUT,Z3200A
         MVI   Z3200A,C' '
         LA    R10,8(R10)
         B     CLI00
ERRORMSG DC    C'1     Z32PRINT.  ERRORS ENCOUNTERED DURING PROCESSING.*
               '
         DC    66C' '
CTLCARDS DC    C'1     Z32PRINT.  LISTING OF CONTROL CARDS'
         DC    80C' '
Z3200A   DC    C'-   Z3200A   MEMBER          NOT FOUND IN LIBRARY'
         DC    31C' '
Z3200B   DC    C'    Z3200B   FORMAT INVALID - REST OF CARD IGNORED'
         DC    30C' '
Z3200C   DC    C'    Z3200C   FUNCTION INVALID - CARD IGNORED      '
         DC    30C' '
Z3200D   DC    C'    Z3200D   LABEL INVALID - CARD IGNORED         '
         DC    30C' '
Z3200E   DC    C'    Z3200E   MEMBER NAME INVALID                  '
         DC    30C' '
         DC    66C' '
CLOSEOUT CLOSE OUT
         L     13,4(13)
         LM    14,12,12(13)
         SR    15,15
         BR    14
*
ABEND    ABEND 1,DUMP
*
         EJECT
NAME     DC    8C' '
BLKCNT   DC    H'0'
PFCC     DC    C'-'
OUTLINE  DS    CL120
PUTMESG  PUT   OUT,PFCC
         BR    11
PRINLINE DC    121C' '
TITLE    DC    C'1 '
DSNAME   DC    27C' '         25 CHARACTER DSNAME
MEMNAME  DC    8C' '
         DC    10C' '         SPACE FOR ALIAS
VARINFO  DC    34C' '
*
*
HEADINFO DC    C'  DATE='                7
DATE     DS    CL5                       5
         DC    C'  TIME='                7
TIME     DS    CL4                       4
         DC    C'  PAGE='                7
PAGE     DC    PL3'1'                    3
         DC    C' '                      1
*
TTRK     DC    F'0'
STORADDR DS    F
LISTADDR DS    F
PDATE    DS    F
PTIME    DS    F
DRECL    DS    D
DCOUNT   DS    D
*
SWITCH   DC    X'00'
COUNT    DC    PL3'0'
LINES    DC    PL3'57'
BLANK    DC    8C' '
*
QNAME    DC    CL8'SYSIEWL '
JFCB     DC    44F'0'
EXLST    DC    X'07'
         DC    AL3(JFCB)
RNAME    EQU   JFCB
*
MOVELIST MVC   0(0,R10),0(R9)
MOVEPRIN MVC   PRINLINE+1(0),0(R5)
*
         READ  DECB,SF,PDCB,IOAREA,'S',MF=L
IOAREA   EQU   *
         EJECT
         PRINT ON,NOGEN
CARD     DCB   DDNAME=SYSIN,MACRF=(GL),DSORG=PS,EODAD=ENDOFILE
IN       DCB   DDNAME=IN,DSORG=PS,MACRF=(GL),RECFM=U,BLKSIZE=256,      *
               EODAD=EODIR
OUT      DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBA
PDCB     DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=(R),                       *
               SYNAD=ABEND,                                            *
               EXLST=EXLST,                                            *
               EODAD=EOMEM
*
TRTABLE  DC    64X'00'
         DC    C' '
         DC    12X'00',C'(',15X'00',C')',13X'00',C','
         DC    148X'00'
         EJECT
INVCARD  MVC   OUTLINE(80),Z3200B
         BAL   11,PUTMESG
         B     GETCARD
INVPARM  ABEND 2,DUMP
INVFUNCT MVC   OUTLINE(80),Z3200C
         BAL   11,PUTMESG
         B     GETCARD
INVLABEL MVC   OUTLINE(80),Z3200D
         BAL   11,PUTMESG
         B     GETCARD
INVNAME  MVC   OUTLINE(80),Z3200E
         BAL   11,PUTMESG
         B     GETCARD
NOTFOUND OI    SWITCH,B7      SHOW MEMBER NOT FOUND
         LA    R10,8(R10)    INCREMENT LIST PTR.
         B     FINDNAME
         END
