TAPESET  CSECT
         SPACE
*  APPLICATIONS USING TAPE GENERATION DATA GROUPS IN WHICH THE SAME SET
*  OF TAPES ARE RE-CYCLED PRESENT A SET UP PROBLEM.  TAPESET AIMS TO
*  SOLVE THIS PROBLEM.
         SPACE
*  THE PROBLEM OCCURS WHEN JCL ASSUMES AN INPUT FILE (GEN 0) AND AN
*  OUTPUT FILE (GEN +1, OVER-WRITING E.G. GEN -5).  THIS WILL ONLY WORK
*  PROPERLY WHEN AT LEST SIX GENERATIONS EXIST.  YOU CAN GET ROUND IT
*  BY ALTERING THE JCL FOR THE FIRST SIX RUNS, OR YOU CAN USE THIS
*  PROGRAM BEFORE THE FIRST RUN TO CALL FOR SIX SCRATCH TAPES AND TO..
*      1.  WRITE THE CORRECT LABELS ON EACH TAPE BY OPEN/CLOSE FOR
*          OUTPUT.
*      2.  CATALOGUE EACH 'NULL' GENERATION AS IT IS CREATED.
         SPACE
*  AFTER RUNNING TAPESET THE JCL FOR NORMAL RUNS WILL WORK WITHOUT
*  ALTERATION - E.G. GEN 0 WILL BE A 'NULL' INPUT AND GEN -5 WILL EXIST
*  FOR OVER-WRITING.
         SPACE
*  BEFORE RUNNING TAPESET YOU MUST CREATE THE NECESSARY CATALOGUE INDEX
*  BY BLDG, AND SET UP THE DUMMY DSCB.
         SPACE
*  YOU MUST ALSO PREPARE A CARD FOR EACH GDG TO BE INITIALIZED TO TELL
*  TAPESET THE DDNAME (GIVING THE DD CARD DEFINING THE UNIT TYPE AND
*  TAPE DENSITY), THE IDENTIFIER FOR THE LABEL THE OPERATOR MUST PUT ON
*  EACH TAPE AS IT IS CREATED, THE NAME OF THE GDG (YOU GIVE AN
*  ABSOLUTE GENERATION NAME E.G. MCU.SMFDATA.G0001V00 (THIS ALLOWS YOU
*  TO START AT OTHER THAN GEN 1 IN EXCEPTIONAL CASES) AND THE NUMBER OF
*  GENERATIONS TO BE CREATED (STARTING WITH THE GEN NUMBER JUST GIVEN).
*  AS EACH TAPE IS CREATED THE OPERATOR WILL GET A CONSOLE MESSAGE 'JOB
*  TAPESET - NOW PUT LABEL 'LABEL ID' ON TAPE 'SERIAL' ON 'CUU''.  THE
*  SAME MESSAGE WILL BE REPEATED FOR EACH GENERATION WITH THE SAME
*  'LABEL ID' BUT WITH DIFFERENT VOL SERIALS.  IT IS ASSUMED THAT THE
*  OPERATOR HAS BEEN GIVEN A BUNCH OF PRE-WRITTEN LABELS AND TOLD TO
*  USE ONE OF THEM FOR EACH TAPE HAVING A PARTICULAR 'LABEL ID'.
         SPACE
*  THE CARD FORMAT IS AS FOR A STRING OF POSITIONAL PARAMETERS FOR A
*  MACRO.  IT MUST START IN COL 1, AND THE FIELDS MUST BE SEPARATED BY
*  COMMAS.  THERE IS A MAXIMUM LENGTH FOR EACH FIELD SHOWN IN BRACKETS
*  BELOW.
*  1.  DDNAME (8)  POINTS TO THE DD STATEMENT DEFINING TAPE UNIT TYPE,
*        AND OPTIONALLY GIVES DCB FOR LABEL.
*  2.  LABEL NAME (10)  THE OPERATOR WILL BE ASKED BY WTO TO FIX THE
*        LABEL HAVING THIS 'ID' TO THE TAPE USED.
*  3.  DSNAME (44)  THIS DSNAME WILL BE WRITTEN INTO THE TAPE LABEL.
*        FOR GEN DATA GROUPS SEE NEXT FIELD.
*  4  GENERATION COUNT (3)  THIS DETERMINES THE NUMBER OF TAPES
*           THAT WILL BE LABELLED WITH SUCCESSIVE GENERATION NUMBERS
*        STARTING WITH THE ONE GIVEN IN THE DSNAME FIELD.  EACH
*        GENERATION WILLBE CATALOGUED AS IT IS CREATED.
*
*  EXAMPLE OF JCL.......
*  //SETUP    EXEC PGM=TAPESET
*  //Y        DD   UNIT='2400-2,,DEFER),DSN=Y,DISP=(,KEEP),DCB=(DEN=2,
*  //SYSPRINT DD   SYSOUT=A
*  //* FOLLOWING STATEMENT ALLOCATES A 9 TRK DRIVE ON WHICH ONE OR MORE
*  //* GDG'S CAN BE CREATED. A DSNAME IS GIVEN ONLY TO ALLOW DISP=KEEP
*  //* TO ENSURE THAT EACH TAPE IS UNLOADED.  YOU MUST GIVE DENSITY IF
*  //* APPROPRIATE, BUT DCB INFO WILL BE PICKED UP FROM THE DUMMY DSCB
*  //* FOR EACH GENERATION.
*  //X        DD   UNIT=(2400,,DEFER),DSN=X,DISP=(,KEEP),DCB=DEN=2
*  //* THE FOLLOWING STATEMENT ALLOCATES A 7 TRK DRIVE.  YOU MUST GIVE
*  //* DENSITY AND TRTCH AS THESE ARE NOT AVAILABLE IN THE DUMMY DSCB.
*  //              TRTCH=C)
*  //SYSIN    DD   *
*  X,TAPE ABC,ABC.G0001V00,4
*  X,TAPE DEF,DEF.G0001V00,4
*  Y,TAPE XYZ,XYZ.G0001V00,10
*  /*
*  //
*
         SPACE 3
         BEGIN
         L     1,16
         L     1,0(1)
         L     1,4(1)
         L     1,12(1)
         ST    1,TIOTAD
         OPEN  (IN,,PRINT,OUTPUT)
GET      GET   IN
         MVC   DDN(62),SPACES
         LR    7,1
         MVC   CARDIM+1(80),0(7)
         LA    4,CARDIM
         BAL   5,PRINTT
         LR    8,7
         TRT   0(9,7),COMTAB
         BC    8,CARDERR
         LR    9,7
         LA    7,1(1)
         SR    1,9
         S     1,ONE
         BM    CARDERR
         EX    1,MVDDN
         TRT   0(11,7),COMTAB
         BC    8,CARDERR
         LR    9,7
         LA    7,1(1)
         SR    1,9
         S     1,ONE
         BM    CARDERR
         EX    1,MVLABEL
         TRT   0(45,7),COMTAB
         BC    8,CARDERR
         LR    9,7
         LA    7,1(1)
         SR    1,9
         S     1,ONE
         BM    CARDERR
         EX    1,MVDSN
         LA   15,3
         L    1,FOES
PICKNUM  IC   1,0(7)
         CLI  1(7),64
         BE   NUMGOT
         SLL  1,8
         LA   7,1(7)
         BCT  15,PICKNUM
         B    CARDERR
NUMGOT   ST   1,FUM
         MVZ  BUM,FUM
         XC   BUM,FOES
         BNZ  CARDERR
         PACK NUMBER,FUM
         MVC   XYZ+40(8),DDN
RDJ      RDJFCB  (XYZ)
         OI    JFCB+52,8
         MVC   JFCB(44),DSN
         NC    JFCB+102(2),JFCB+102
         BNZ   *+14
         MVC   JFCB+102(4),DUMBLK
         MVI   JFCB+100,128
         OPEN  (XYZ,OUTPUT),TYPE=J
         TM    XYZ+48,16
         BZ    OPERR
         LH    7,XYZ+40
         A     7,TIOTAD
         L     7,16(7)
         MVC   LCUU,13(7)
         MVC   UTYPE,16(7)
         MVC   VOLSER,28(7)
         CLOSE (XYZ)
         OI    BNC+1,240
         CP    NUMBER,ONEP
         BE    ONEDONE
         NI    BNC+1,15
          LA   7,DSN+43
          LA   8,44
CLI7      CLI  0(7),64
          BNE  GFND
          BCTR 7,0
          BCT  8,CLI7
          B    DSNERR
GFND      S    7,EIGHT
          CLC  0(2,7),DOTG
         BNE   DSNERR
          CLI  6(7),C'V'
          BNE  DSNERR
         PACK  GENNO,2(4,7)
         LA    10,2(7)
ONEDONE  MVC   LSER,VOLSER
         MVC   LLAB,LABEL
         MVC   LDSN,DSN
         CATALOG  CAMLST
          STM  0,1,SAVER
         LTR   15,15
         BNZ   CATERR
         L     1,TIOTAD
         MVC   WTO+8(8),0(1)
         MVC   WTO+42(10),LABEL
         MVC   WTO+62(6),VOLSER
         MVC   WTO+72(3),LCUU
WTO      WTO   '12345678 TAPESET - NOW PUT LABEL ''123456789A'' ON TAPE*
                123456 ON CUU',ROUTCDE=(3,11)
         LA    4,LINE
         BAL   5,PRINTT
BNC      B     NEXTC
         AP    GENNO,ONEP
         SP    NUMBER,ONEP
         UNPK  GENNUM,GENNO
         OI    GENNUM+3,240
         MVC   0(4,10),GENNUM
         B     RDJ
EOC      CLOSE (IN,,PRINT)
         GOBACK
         SPACE
*        PRINT ROUTINE ASSUMES USER HAS PUT ADDRESS OF LINE TO BE
*        PRINTED IN REG 4 AND HAS ISSUED A 'BAL 5,PRINTT'.
         SPACE
PRINTT   SP    LCNT,TWO
         BP    PLINE
         PUT   PRINT,NP
         ZAP   LCNT,MAXCNT
PLINE    PUT   PRINT,(4)
         BR    5
CARDERR  MVC   LCARD,0(8)
         LA    4,REJL
         BAL   5,PRINTT
NEXTC    PUT   PRINT,ONEUP
         SP    LCNT,ONEP
         B     GET
OPERR    MVC   REJOPEN+9(19),NOTOPEN
         MVC   OPERDSN,DSN
         LA    4,REJOPEN
         BAL   5,PRINTT
         B     NEXTC
DSNERR   MVC   REJOPEN+9(19),NOTGDG
         B     OPERR+6
CATERR   MVC   LCATF,DSN
         LA    4,CATFL
         BAL   5,PRINTT
          LM   0,1,SAVER
         CVD   0,DW
         UNPK  DW(4),DW+6(2)
         OI    DW+3,240
         MVC   CERMSG+51(2),DW+2
         CVD   1,DW
         UNPK  DW(4),DW+6(2)
         OI    DW+3,240
         MVC   CERMSG+63(2),DW+2
         CVD   15,DW
         UNPK DW(4),DW+6(2)
         OI    DW+3,240
         MVC   CERMSG+39(2),DW+2
         LA    4,CERMSG
         BAL   5,PRINTT
         B     NEXTC
IN       DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),LRECL=80,EODAD=EOC
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBM,          *
               LRECL=111,BLKSIZE=1110
XYZ      DCB   DDNAME=XYZ,DSORG=PS,MACRF=(E),EXLST=XLIST
DW       DC    D'0'
ONE      DC    F'1'
TIOTAD   DC    F'0'
EIGHT     DC   F'8'
SAVER     DC   2F'0'
FUM      DC   F'0'
BUM      DC   F'0'
FOES     DC   C'0000'
XLIST    DC    X'87',AL3(JFCB)
CAMLST   CAMLST  CAT,DSN,,VOLLIST
VOLLIST  DC    H'1'
UTYPE    DC    XL4'00'
VOLSER   DC    CL6' ',H'1'
JFCB     DC    CL176' '
COMTAB   DC    256X'00'
         ORG   COMTAB+C','
         DC    X'FF'
         ORG
NP       DC    X'8B'
ONEUP    DC    X'0B'
CERMSG   DC    X'11',CL110'CONTENTS OF REGISTERS WERE - REG 15 = XX, REG
               G 0 = XX, REG 1 = XX  (SEE SYSTEM PROGS GUIDE, FIG CV2)'
CARDIM   DC    X'11',CL110' '
LINE     DC    X'11',C'TAPE '
LSER     DC    CL6' ',C' ON '
LCUU     DC    CL3' ',C' HAS BEEN LABELLED '
LLAB     DC    CL10' ',C' AND CATALOGUED AS '
LDSN     DC    CL44' '
REJOPEN  DC    X'11',C'FAILURE IN ATTEMPT TO OPEN '
OPERDSN  DC    CL44' ',CL39' '
REJL     DC    X'11',C'FOLLOWING CARD IMAGE REJECTED '
LCARD    DC    CL80' '
CATFL    DC    X'11',C'FAILURE IN ATTEMPT TO CATALOGUE '
LCATF    DC    CL44' '
SPACES   DC    CL62' '
NOTGDG   DC    C'DSNAME NOT FOR GDG '
NOTOPEN  DC    C'IN ATTEMPT TO OPEN '
DDN      DC    CL8' '
LABEL    DC    CL10' '
DSN      DC    CL44' '
MVDDN    MVC   DDN(1),0(9)
MVLABEL  MVC   LABEL(1),0(9)
MVDSN    MVC   DSN(1),0(9)
DOTG     DC    C'.G'
V00      DC    C'V00'
DUMBLK   DC    X'00500050'
GENNUM   DC    CL4' '
GENNO    DC    PL3'0'
NUMBER   DC   PL3'1'
ONEP     DC    P'1'
TWO      DC    P'2'
LCNT     DC    PL2'0'
MAXCNT   DC    P'56'
         END
