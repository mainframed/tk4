RTPDMPDS CSECT
*
*  THIS PROGRAM ISSUES A BLDL TO THE DATA SET SPECIFIED BY THE DD CARD
*  WITH DDNAME 'D' TO FIND THE MEMBERS SPECIFIED IN THE PARM FIELD OF
*  THE EXEC CARD
*  THE START AND END DISK ADDRESSES OF THE MEMBER ARE STORED IN A
*  CONTROL CARD IMAGE THAT IS WRITTEN TO THE DATA SET SPECIFIED BY DD
*  CARD 'SYSLIN'.
*  THE PROGRAM THEN LINKS TO IEHDASDR, PASSING SYSLIN AS AN OVERRIDE
*  FOR SYSIN.
*  FOR EXAMPLE, TO OBTAIN A FULL IEHDASDR DISK PRINT OF MEMBER 'MEMB'
*  FROM LOAD MODULE LIBRARY 'LIB' WITHOUT PREVIOUSLY KNOWING THE DISK
*  ADDRESS....
*        //PRINT  EXEC  PGM=RTPDMPDS,PARM=MEMB
*        //SYSPRINT  DD  SYSOUT=A
*        //D       DD  DSN=LIB,DISP=SHR
*        //SYSLIN  DD  SPACE=(TRK,1),UNIT=SYSDA
*
*  NOTE THAT THE PROGRAM CAN ALSO BE USED FOR THE DIRECTORY OF THE
*  LIBRARY OR FOR PRINTING A SEQUENTIAL DATA SET BY OMITTING THE PARM
*  IN THIS CASE THE BLDL WILL FAIL AND THE PROGRAM WILL DEFAULT TO THE
*  START ADDRESS OF THE DATA SET SPECIFIED, AND WILL OBTAIN THE END
*  ADDRESS IN ITS USUAL WAY BY SCANNING THE DATA SET FROM THAT POINT
*  UNTIL IT FINDS AN EOF.
*
*        BEGIN  BASE=3
         STM   14,12,12(13)
         BALR  3,0
         USING *,3
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         LR    4,13
         L     2,0(1)
         LH    1,0(2)
         LTR   1,1
         BNZ   *+12
         LA    1,1
         LA    2,LIST+4
         BCTR  1,0
         EX    1,MOVE
         OPEN  (D,,LIN,OUTPUT)
         TM    DSORG,X'02'
         BZ    SEQ
         CLI   LIST+4,C' '
         BE    SEQ
         BLDL  D,LIST
         MVI   LIST+15,0
         L     0,LIST+12
RPT      L     1,D+44
         LA    2,MBB
         L     15,16
         L     15,28(15)
         BALR  14,15
         LR    13,4
         BC    0,TIME2
         OI    *-3,X'F0'
         B     HP
SEQ      L     1,D+44
         MVC   MBB+3(4),38(1)
*        HEXPRINT  MBB+3,BEGIN,4
         CNOP  0,4
HP       BAL   1,*+36
         DC    A(BEGIN)
         DC    A(MBB+3)
         DC    H'4'
         DC    AL1(0)
         DC    AL1(0)
         DC    CL16'0123456789ABCDEF',V(TSGHEXPT)
         L     15,28(1)
         BALR  14,15
TSGHEXPT CSECT
         STM   0,7,20(13)
         LM    4,5,0(1)
         LH    0,8(1)
         SR    3,3
         SR    6,6
         SR    7,7
         IC    6,10(1)
         IC    7,11(1)
         IC    3,0(5)
         SLDL  2,28
         SRL   3,28
         IC    2,12(1,2)
         IC    3,12(1,3)
         STC   2,0(4)
         STC   3,1(4)
         LA    4,2(4)
         LA    5,1(5)
         BCT   6,72(15)
         AR    4,7
         IC    6,10(1)
         BCT   0,26(15)
         LM    0,7,20(13)
         BR    14
RTPDMPDS CSECT
         EXCP  IOB
         WAIT  1,ECB=ECB
*TIME2   HEXPRINT MBB+3,END,4
         CNOP  0,4
TIME2    BAL   1,*+36
         DC    A(END)
         DC    A(MBB+3)
         DC    H'4'
         DC    AL1(0)
         DC    AL1(0)
         DC    CL16'0123456789ABCDEF',V(TSGHEXPT)
         L     15,28(1)
         BALR  14,15
         PUT   LIN,CARD
         CLOSE (D,,LIN)
         LINK  EP=IEHDASDR,MF=(E,PARMS)
*        GOBACK
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
MOVE     MVC   LIST+4(0),2(2)
LIST     DC    0F'0',H'1,12',CL8' ',A(256)
D        DCB   DDNAME=D,DSORG=PO,MACRF=R,EXLST=DEXIT
LIN      DCB   DDNAME=SYSLIN,DSORG=PS,MACRF=PM,LRECL=80,RECFM=F,       *
               BLKSIZE=80
ECB      DC    F'0'
IOB      DC    X'42000000',A(ECB,0,0,CCW,D,0,0)
MBB      DC    XL8'00'
DEXIT    DCBEXITS OPEN
         MVC   DSORG,198(15)
         BR    14
DSORG    DC    X'00'
CCW      CCW   X'31',MBB+3,X'40',5
         CCW   X'08',CCW,0,0
CCWZ     CCW   X'92',MBB+3,X'40',8
         CCW   X'0E',*,X'70',1
         CCW   X'08',CCWZ,0,0
CARD     DC    C' DUMP FROMDD=D,TODD=SYSPRINT,BEGIN='
BEGIN    DC    8C'0',C',END='
END      DC    8C'0',CL24' '
PARMS    DC    A(PARMS+8),X'80',AL3(PARMS+10)
         DC    H'0,40',32X'00',CL8'SYSLIN'
         END
