*          DATA SET UTLPRMS    AT LEVEL 014 AS OF 19/02/76
         TITLE 'UTLPROGM/UTLPRMS - MESSAGE HANDLER'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  MDSL  EXTENTION OF IEHPROGM GDG HANDLING FACILITIES                *
*  SOFTWARE CODE - UTL                 * SYSTEMS PROGRAMMING GROUP    *
*  PROGRAM ID    - UTLPROGM            * MOBIL DATA SERVICES LTD      *
*  MODULE ID     - UTLPRMS             * BECKET HOUSE, VESTRY ROAD    *
*  DATE WRITTEN  - JAN 1975            * SEVENOAKS, KENT              *
*  AUTHOR        - ANDREW STRACHAN     * TN14 5EJ                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
UTLPRMS  CSECT
         BEGIN
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  THIS MODULE CREATES AND PRINTS MESSAGES USING 'MSGNR' & 'RETCODE'  *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LR    R3,R1                   * SET BASE REGISTER
         USING OPTIONS,R3              * +
         CLC   MSGNR,=X'FFFF'          * Q/ TIME TO CLOSE
         BE    MSCLS880                * Y. GO DO CLOSE
         CLI   PRNTOPEN,X'00'          * HAS THE FILE BEEN OPENED
         BNE   MSOVR050                * GO DO PAGE OVERFLOW
         OPEN  (PRNTDCB,(OUTPUT))      * OPEN THE FILE
         TM    PRNTDCB+X'30',X'10'     * Q/ OPENED OK
         BO    MSOPN040                * Y. GO SET FLAG
         MVC   RETJOB,=H'16'           * SET RETURN CODE
         MVI   OPCODE,X'FF'            * FLAG END OF JOB
         B     MSFIN900                * GO DO FINISH
MSOPN040 EQU   *                       * DO SET FLAG
         MVI   PRNTOPEN,X'01'          * FLAG IT AS OPEN
MSOVR050 EQU   *                       * DO PAGE OVERFLOW
         LH    R15,LINECNT             * Q/ LINES MORE THAN 55
         CH    R15,=H'55'              * +
         BL    MSDET200                * N. GO DO DETAIL LINE
         CLI   HL1TIME,X'21'           * Q/ HAS TIME BEEN STORED
         BNE   MSHDR100                * Y. GO DO HEADER LINE
         TIME  DEC                     * FIND CURRENT DATE & TIME
         STM   R0,R1,TIMEWORK          * MOVE THEM TO PRINT LINE
         ED    HL1TIME,TIMEWORK        * +
         ED    HL1DATE,DATEWORK+1      * +
MSHDR100 EQU   *                       * DO HEADER
         MVC   PRNTAREA,HL1LINE        * MOVE IN HEADER
         PUT   PRNTDCB,PRNTAREA        * PRINT LINE
         MVI   DL1LINE,C'-'            * MOVE SKIP TO ASA
         MVC   LINECNT,=H'0'           * RESE LINE COUNT
MSDET200 EQU   *                       * DO DETAIL
         LH    R7,MSGNR                * Q/ MESSAGE MORE THAN 9
         CH    R7,=H'9'                * +
         BH    MSBLA220                * Y. GO BUILD
         MVI   PRNTAREA,C'-'           * MOVE SKIP TO ASA
         L     R15,ACARD               * POINT TO CARD AREA
         MVC   PRNTAREA+1(80),0(R15)   * MOVE CARD IMAGE TO PRINT LINE
         MVI   PRNTAREA+81,SP          * BLANK OUT REST
         MVC   PRNTAREA+82(L'PRNTAREA-82),PRNTAREA+81
         PUT   PRNTDCB,PRNTAREA        * PRINT LINE
         MVI   DL1LINE,SP              * BLANK OUT SKIP CHAR
         LH    R15,LINECNT             * ADD 1 TO LINE
         LA    R15,3(R15)              * +
         STH   R15,LINECNT             * +
         LH    R7,MSGNR                * Q/ MESSAGE ZERO
         CH    R7,=H'0'                * +
         BNE   MSRET210                * N. GO DO RETURN CODE UPDATE
         MVC   RETCODE,=H'0'           * MAKE RETURN ZERO
         B     MSFIN900                * GO DO FINISH
MSRET210 EQU   *                       * DO UPDATE RETURN CODE
         LH    R15,RETJOB              * DO RETURN CODE
         CH    R15,=H'4'               * Q/ RET LESS THAN 4
         BNL   MSBLA220                * N. GO DO BUILD
         MVC   RETJOB,=H'4'            * SET RET CODE TO 4
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  BUILD MESSAGE ID                                                   *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
MSBLA220 EQU   *                       * DO BUILD OF MESSAGE
         MVC   PRNTAREA,DL1LINE        * SET UP DETAIL LINE
         LA    R10,PRNTAREA+13         * POINT TO NEXT PART
         LH    R7,MSGNR                * CONVERT MESSAGE NR TO PACKED
         CVD   R7,MSGWORK              * +
         OI    MSGWORK+L'MSGWORK-1,X'0F' FORCE SIGN
         UNPK  0(3,R10),MSGWORK        * UNPACK INTO PRINT LINE
         LA    R10,3(R10)              * POINT TO NEXT PART
         LH    R7,RETCODE              * Q/ RETURN ZERO
         CH    R7,=H'0'                * +
         BE    MSBLB240                * Y. GO DO BUILD
         MVI   0(R10),C'-'             * MOVE HYPHEN TO LINE
         LA    R10,1(R10)              * SKIP OVER IT
         CVD   R7,RETWORK              * CONVERT TO PACKED
         OI    RETWORK+L'RETWORK-1,X'0F' FORCE SIGN
         UNPK  0(2,R10),RETWORK        * UNPACK INTO PRINT LINE
         LA    R10,2(R10)              * SKIP OVER IT
MSBLB240 EQU   *                       * DO BUILD NAME
         LA    R10,1(R10)              * LEAVE A SPACE
         LH    R7,MSGNR                * Q/ MESSAGE LESS THAN 10
         CH    R7,=H'10'               * +
         BNL   MSBLR250                * N. GO DO RETURN CODE UPDATE
         MVC   PRNTAREA+101(9),=C'* ERROR *'
         MVC   RETCODE,=H'4'           * MAKE RETURN NON-ZERO
         B     MSBLW300                * GO DO WORDS
MSBLR250 EQU   *                       * DO UPDATE RETURN CODE
         LH    R15,RETCODE             * Q/ RETCODE = ZERO
         CH    R15,=H'0'               * +
         BE    MSBLC260                * Y. GO DO BUILD NAME
         MVC   PRNTAREA+101(9),=C'* ERROR *'
         LH    R15,RETJOB              * DO RETURN CODE UPDATE
         CH    R15,=H'8'               * Q/ RETURN LESS THAN 8
         BNL   MSBLC260                * N. GO DO BUILD
         MVC   RETJOB,=H'8'            * SET RETURN TO 8
MSBLC260 EQU   *                       * DO BUILD OF NAME
         LH    R7,MSGNR                * Q/ MESSAGE LESS THAN 16
         CH    R7,=H'16'               * +
         BL    MSBLD280                * Y. GO DO BUILD DSN
         LH    R8,LENINDEX             * LOAD UP LENGTH
         BCTR  R8,0                    * SUBTRACT 1 FOR MVC
         EX    R8,MVCINDEX             * MOVE IN INDEX
         AR    R10,R8                  * POINT TO NEXT PLACE + 1
         LA    R10,2(R10)              * +
         B     MSBLW300                * GO DO BUILD WORKDS
MSBLD280 EQU   *                       * DO BUILD DSN
         LH    R8,LENINDEX             * LOAD UP LENGTH
         LA    R8,8(R8)                * ADD 9 FOR G00V00 - 1 FOR MVC
         EX    R8,MVCDSN               * MOVE IN DSN
         AR    R10,R8                  * POINT TO NEXT PLACE + 1
         LA    R10,2(R10)              * +
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  MOVE CORRECT LITERAL INTO MESSAGE                                  *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
MSBLW300 EQU   *                       * DO BUILD WORDS
         LH    R15,MSGNR               *
         MH    R15,=H'4'               *
         LA    R14,MSBRA320            *
         AR    R14,R15                 *
         BR    R14                     *
MSBRA320 EQU   *                       *
         B     MSPRT800                *
         B     MSBRB401                *
         B     MSBRB402                *
         B     MSBRB403                *
         B     MSBRB404                *
         B     MSBRB405                *
         B     MSBRB406                *
         B     MSBRB407                *
         B     MSBRB408                *
         B     MSPRT800                *
         B     MSBRB410                *
         B     MSBRB411                *
         B     MSBRB412                *
         B     MSBRB413                *
         B     MSBRB414                *
         B     MSBRB415                *
         B     MSBRB416                *
         B     MSBRB417                *
         B     MSBRB418                *
         B     MSBRB419                *
         B     MSBRB420                *
         B     MSBRB421                *
         B     MSBRB422                *
MSBRB401 EQU   *                       *
         LA    R8,MSG01                *
         LA    R9,L'MSG01              *
         B     MSBRC450                *
MSBRB402 EQU   *                       *
         LA    R8,MSG02                *
         LA    R9,L'MSG02              *
         B     MSBRC450                *
MSBRB403 EQU   *                       *
         LA    R8,MSG03                *
         LA    R9,L'MSG03              *
         B     MSBRC450                *
MSBRB404 EQU   *                       *
         LA    R8,MSG04                *
         LA    R9,L'MSG04              *
         B     MSBRC450                *
MSBRB405 EQU   *                       *
         LA    R8,MSG05                *
         LA    R9,L'MSG05              *
         B     MSBRC450                *
MSBRB406 EQU   *                       *
         LA    R8,MSG06                *
         LA    R9,L'MSG06              *
         B     MSBRC450                *
MSBRB407 EQU   *                       *
         LA    R8,MSG07                *
         LA    R9,L'MSG07              *
         B     MSBRC450                *
MSBRB408 EQU   *                       *
         LA    R8,MSG08                *
         LA    R9,L'MSG08              *
         B     MSBRC450                *
MSBRB410 EQU   *                       *
         LA    R8,MSG10                *
         LA    R9,L'MSG10              *
         B     MSBRC450                *
MSBRB411 EQU   *                       *
         LA    R8,MSG11                *
         LA    R9,L'MSG11              *
         B     MSBRC450                *
MSBRB412 EQU   *                       *
         LA    R8,MSG12                *
         LA    R9,L'MSG12              *
         B     MSBRC450                *
MSBRB413 EQU   *                       *
         LA    R8,MSG13                *
         LA    R9,L'MSG13              *
         B     MSBRC450                *
MSBRB414 EQU   *                       *
         LA    R8,MSG14                *
         LA    R9,L'MSG14              *
         B     MSBRC450                *
MSBRB415 EQU   *                       *
         LA    R8,MSG15                *
         LA    R9,L'MSG15              *
         B     MSBRC450                *
MSBRB416 EQU   *                       *
         LA    R8,MSG16                *
         LA    R9,L'MSG16              *
         B     MSBRC450                *
MSBRB417 EQU   *                       *
         LA    R8,MSG17                *
         LA    R9,L'MSG17              *
         B     MSBRC450                *
MSBRB418 EQU   *                       *
         LA    R8,MSG18                *
         LA    R9,L'MSG18              *
         B     MSBRC450                *
MSBRB419 EQU   *                       *
         LA    R8,MSG19                *
         LA    R9,L'MSG19              *
         B     MSBRC450                *
MSBRB420 EQU   *                       *
         LA    R8,MSG20                *
         LA    R9,L'MSG20              *
         B     MSBRC450                *
MSBRB421 EQU   *                       *
         LA    R8,MSG21                *
         LA    R9,L'MSG21              *
         B     MSBRC450                *
MSBRB422 EQU   *                       *
         LA    R8,MSG22                *
         LA    R9,L'MSG22              *
         B     MSBRC450                *
MSBRC450 EQU   *                       *
         BCTR  R9,0                    * SUBTRACT 1 FOR MVC
         EX    R9,MVCWORDS             * MOVE IN WORDS
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  PRINT THE MESSAGE.                                                 *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
MSPRT800 EQU   *                       * DO PRINT
         LH    R15,LINECNT             * ADD 1 TO LINE
         LA    R15,1(R15)              * +
         STH   R15,LINECNT             * +
         PUT   PRNTDCB,PRNTAREA        * PRINT LINE
         MVI   DL1LINE,SP              * RESET SKIP CHAR
         B     MSFIN900                * GO DO FINISH
MSCLS880 EQU   *                       * DO CLOSE
         CLOSE PRNTDCB                 * CLOSE THE FILE
MSFIN900 EQU   *                       * DO FINISH
         EXIT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  WORK AREAS AND EXECUTED INSTRUCTIONS USED BY THIS MODULE           *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         PRINT NOGEN
TIMEWORK DS    F                       * WORK AREA FOR TIME
DATEWORK DS    F                       * WORK AREA FOR DATE
MSGWORK  DS    D                       * WORK AREA FOR MESSAGE NR
RETWORK  DS    D                       * WORK AREA FOR RETURN
LINECNT  DC    H'99'                   * LINE COUNT
PRNTOPEN DC    X'00'                   * FLAG FOR OPEN
SP       EQU   C' '
MVCINDEX MVC   0(0,R10),INDEX          * MOVE INDEX TO LINE
MVCDSN   MVC   0(0,R10),SAVDSN         * MOVE DSNAME TO LINE
MVCWORDS MVC   0(0,R10),0(R8)
MSG01    DC    C'OPERATION MUST NOT START IN COLUMN 1'
MSG02    DC    C'OPERATION NOT RECOGNIZED'
MSG03    DC    C'OPERAND NOT FOUND'
MSG04    DC    C'OPERAND NOT RECOGNIZED'
MSG05    DC    C'ENTRIES MUST BE 1 TO 255'
MSG06    DC    C'ENTRIES OPERAND NOT USED FOR SCRAP'
MSG07    DC    C'DLTX OPERAND NOT USED FOR REPLACE OR EXTEND'
MSG08    DC    C'INDEX NAME MUST BE 1 TO 35 LONG'
MSG10    DC    C'SCRATCHED'
MSG11    DC    C'NOT SCRATCHED'
MSG12    DC    C'UNCATALOGUED'
MSG13    DC    C'NOT UNCATALOGED'
MSG14    DC    C'RE-CATALOGUED'
MSG15    DC    C'NOT RE-CATALOGED'
MSG16    DC    C'DELETED'
MSG17    DC    C'NOT DELETED'
MSG18    DC    C'RE-BUILT'
MSG19    DC    C'NOT RE-BUILT'
MSG20    DC    C'NOT FOUND ON CATALOG'
MSG21    DC    C'ATTEMPTING TO EXTEND OR REDUCE INCORRECTLY'
MSG22    DC    C' '
HL1LINE  DC    CL121'1UTLPROGM  -  IEHPROGM EXTENTION FOR GDG HANDLING'
         ORG   HL1LINE+100             *
HL1TIME  DC    X'21204B20204B2020'     * EDIT WORD FOR HH.MM.SS
         DS    CL2                     *
HL1DATE  DC    X'21204B202020'         * EDIT WORD FOR YY.DDD
         ORG                           *
DL1LINE  DC    CL121'         #UTL'    * DETAIL LINE
PRNTAREA DS    CL121                   * PRINT OUTPUT AREA
PRNTDCB  DCB   DDNAME=SYSPRINT,RECFM=FBA,LRECL=121,                    C
               MACRF=PM,DSORG=PS
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* THE COMMON AREA USED BY ALL MODULES.                                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
OPTIONS  DSECT                         * THE AREA MAPPED IS IN UTLPROGM
INDEX    DS    CL44                    * INDEX.NAME
SAVDSN   DS    CL44                    * WORK AREA FOR DSN FOR CAMLST
LENINDEX DS    H                       * INDEX.NAME LENGTH
OPCODE   DS    XL8                     * OPERATION
SWDLTX   DS    CL1                     * DELETE INDEX SWITCH
SWEMDEL  DS    H                       * EMPTY/DELETE SWITCH
EMPTY    EQU   2                       * EMPTY
DELETE   EQU   1                       * DELETE
RETCODE  DS    H                       * RETURN CODE FROM MACROS
RETJOB   DS    H                       * HIGHEST RETURN CODE
MSGNR    DS    H                       * MESSAGE NUMBER
OLDREAL  DS    H                       * ACTUAL NUMBER OF GENERATIONS
OLDMAX   DS    H                       * PREVIOUS MAXIMUM
NEWMAX   DS    H                       * MAXIUMUM DESIRED
ACARD    DS    A                       * ADDR OF CARD AREA
UTLPRMS  CSECT
         END
