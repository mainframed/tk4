         TITLE 'BINARY TO DISPLAY FORMAT HEX CONVERSION'
B1801    CSECT
         ENTRY B180100
B180100  EQU   *
         PRINT NOGEN
*
* THIS SUBROUTINE WILL TRANSLATE ANY FIELD UP TO 66 BYTES LONG INTO
* HEXADECIMAL DISPLAY FORMAT. THE OUTPUT WILL BE TWICE THE LENGTH OF
* THE INPUT. IF INPUT LENGTH IS GREATER THAN 66 THEN66 BYTES ONLY WILL
* BE TRANSLATED.
*
* LINK TO SUBROUTINE BY  CALL B1801,(P1,P2,P3)
* WHERE
*   P1 IS ADDRESS OF INPUT
*   P2 IS LENGTH OF INPUT
*   P3 IS ADDRESS OF RESULT (ASSUMED TWICE LENGTH OF INPUT)
*
         ISEQ  73,80
         SAVE  (14,12)                  SAVE REGISTERS
         BALR  5,0                      LOAD SUBROUTINE BASE
         USING *,5
         LM    6,8,0(1)                 LOAD PARAMETER ADDRESSES INTO
*                                       REGS. 6,7 AND 8
         LR    9,7                      SAVE INPUT LENGTH
         AR    9,9                      DOUBLE LENGTH AND
         BCTR  9,0                      SUBTRACT 1
         LR    3,8                      SAVE ADDRESS OF RESULT
         C     7,=F'67'            Q. LENGTH LESS THAN 67
         BL    B1802               YES, OKAY
         LA    7,66                NO, LENGTH OF 66 ASSUMED
B1802    CH    7,=H'8'                Q.IS LENGTH LESS THAN 8
         BL    B1803                    YES
         SH    7,=H'7'                  SUBTRACT 7 FROM LENGTH
         MVC   SCW008(7),0(6)           GET 7 BYTES OF INPUT
         LA    6,7(6)                   UPDATE INPUT POINTER
         UNPK  SCW015,SCW008(8)
         MVC   0(14,8),SCW015           MOVE UNPACKED INPUT TO RESULT
         LA    8,14(8)                  UPDATE RESULT POINTER
         B     B1802
*
* NO.OF BYTES TO BE CONVERTED EQ 7 OR LESS - LENGTH IN R7
*
B1803    MVC   SCW008(7),CXL007         MOVE HEX ZEROES TO WORK FIELD
         LA    12,SCW008+7              COMPUTE ADDRESS IN
         SR    12,7                     WORK FIELD FOR RESIDUAL INPUT
         LR    2,7                      GET LENGTH OF RESIDUAL INPUT
         BCTR  2,0                      SUBTRACT 1
         STC   2,B1804+1                INSERT LENGTH IN MOVE INSTRUC.
B1804    MVC   0(1,12),0(6)             MOVE RESIDUAL INPUT TO WORKFLD
         UNPK  SCW015,SCW008(8)
         LA    12,SCW015+14             COMPUTE ADDRESS
         LR    2,7                      OF RESULT
         AR    2,2
         SR    12,2
         BCTR  2,0                      SUBTRACT 1 FROM LENGTH OF
         STC   2,B1805+1                RESULT AND STORE IN MVC
B1805    MVC   0(1,8),0(12)             MOVE RESULT
         STC   9,B1806+1                AMEND TRANSLATE INSTRUCTION
B1806    TR    0(1,3),CCT016-240        TRANSLATE RESULT TO DISPLAY
         RETURN (14,12)                 EXIT
*
** CONSTANTS AND STORAGE AREAS
         DS    CL150
SCW008   DS    CL7
         DC    X'FF'
SCW015   DS    CL15
CXL007   DC    X'00000000000000'
CCT016   DC    C'0123456789ABCDEF'
         LTORG
         END
