ZZAAAE   BEGIN BR=(12),SAVE=SAVEAREA
         LM    6,9,0(1)
*
** 1ST TIME THROUGH OPEN REPORT
*
BA02     EQU   *
         BC    0,BA04
         OPEN  (P02,(OUTPUT))
         OPEN  (D01,(OUTPUT))
         OI    BA02+1,X'F0'
*
** CHECK IF CLOSE OPTION
*
BA04     EQU   *
         CLI   0(8),X'FF'
         BE    BA80
         MVI   CCW144,C' '         CLEAR GENERATED
         MVC   CCW144+1(143),CCW144
         MVI   CXI001,X'00'
         CLC   CCW008,0(8)         Q- NEW MEMBER
         BE    BA06                A- NO,DONT RESTART COUNT
         ZAP   CPW005,=P'0'        A- YES RESTART COUNT
BA06     EQU   *
         MVC   CCW008,0(8)
         AP    CPW005,=P'10'       INCREASE RECORD COUNT
*
** SET UP LOOP POINTERS
*
         LR    1,7
         LR    2,1
         LA    14,CCW144
         LR    3,6
         LA    4,71
*
** LOOP THROUGH TABLE ON 1ST COMPARE
*
BA10     EQU   *
         SR    5,5
         IC    5,0(,3)             GET LENGTH OF COMPARE FIELD
         CLI   0(3),X'FF'          Q- ANY MORE ENTRIES
         BE    BA20                A- NO,MOVE DOWN RECORD
         EX    5,EX01              Q- KEY = DATA
         BE    BA12                A- YES,GO SHUFFLE
         LA    3,73(,3)            A- NO,POINT TO NEXT KEY
         CLI   0(9),X'08'
         BNE   BA10
         LA    3,73(,3)
         B     BA10                GO PROCESS THIS KEY
BA12     EQU   *
         MVI   CXI001,X'FF'
         SR    4,5                 REDUCE LENGTH TO COMPARE
         BCTR  4,0                 NEXT TIME
         CLI   0(9),X'04'
         BNH   BA30
         SR    2,1
         BZ    BA14
         BCTR  2,0
         EX    2,EX02
         LA    2,1(,2)
BA14     EQU   *
         AR    14,2
         LA    2,1(5,2)
         AR    2,1
         IC    5,73(,3)
         EX    5,EX03
         LA    14,1(5,14)
BA16     EQU   *
         LR    1,2
         LR    3,6
         B     BA10
BA20     EQU   *
         LA    2,1(,2)
         LR    3,6
         LTR   4,4                 Q- END OF CARD                    01
         BZ    BA30                A- YES,NO MORE TO MOVE            01
         BCT   4,BA10
         CLI   0(9),X'08'
         BNE   BA30
         SR    2,1
         BCTR  2,0
         EX    2,EX02
*
** FIND IF EQUAL KEY FOUND
*
BA30     CLI   CXI001,X'00'        Q- EQUAL KEY FOUND
         BE    BA90                A- NO,RETURN
         BAL   10,B5001
         CLI   0(9),X'04'          Q- PUNCH OPTION
         BNH   BA90                A- NO,GO EXIT
         BAL   10,B5101
         B     BA90
*
** CLOSE
*
BA80     CLOSE D01
         CLOSE P02
*
** EXIT
*
BA90     EQU   *
         L     13,4(,13)
         LM    14,12,12(13)
         BR    14
*
** B5001 PRINT KEY FOUND ON RECORD OR REPORT MEMBER NAME
*
B5001    EQU   *
         BC    0,B5003
         MVI   CCO133,X'89'
         BAL   11,B6001
         MVC   CCO133+60(17),=C'PDS UPDATE REPORT'
         BAL   11,B6001
         OI    B5001+1,X'F0'
B5003    EQU   *
         CLC   CCW008B,0(8)        Q- NEW MEMBER
         BE    B5005
         MVI   CCO133,X'11'
         BAL   11,B6001
         MVC   CCO133+2(13),=C'MEMBER NAME ='
         MVC   CCO133+16(8),0(8)
         MVC   CCW008B,0(8)        SAVE NEW MEMBER NAME
         BAL   11,B6001
B5005    CLI   0(9),X'00'
         BER   10
         MVC   CCO133+10(80),0(7)
         BAL   11,B6001
         BR    10
*
** SUBROUTINE  B5101 - PRINT CHANGE CARD AND WRITE IT OUT
*
B5101    EQU   *
         MVC   CCO133+3(3),=C'NEW'
         CLI   CCW144+71,C' '
         BE    B5111
         LA    3,CCW144+70
B5103    EQU   *
         CLI   0(3),C','
         BE    B5105
         BCT   3,B5103
B5105    EQU   *
         LA    3,1(,3)
         LA    4,CCW144
         SR    3,4
         BCTR  3,0
         EX    3,EX04
         LA    4,1(3,4)
         CLC   CCW144(2),=C'//'
         BNE   B5107
         MVC   CCW080B(2),=C'//'
B5107    EQU   *
         MVC   CCW080B+15(55),0(4)
         PACK  SPW005,72(8,7)
         CP    SPW005,=P'0'        Q- SEQ NO BLANK
         BNE   B5109               A- NO,CONTINUE
         ZAP   SPW005,CPW005       A- YES,SET NEW VALUE
B5109    EQU   *
         MVC   SCW001,CCW080A+70   SAVE BYTE 71
         MVC   CCW080A+70(10),CXE010
         ED    CCW080A+70(10),SPW005
         MVC   CCW080A+70(1),SCW001  RESTORE BYTE 71
         MVI   CCW080A+71,C'*'
         AP    SPW005,=P'1'
         MVC   CCW080B+70(10),CXE010
         ED    CCW080B+70(10),SPW005
         MVC   CCO133+10(80),CCW080A
         MVC   CCW080B+71(1),71(7)
         BAL   11,B6001
         MVC   CCO133+3(3),=C'NEW'
         MVC   CCO133+10(80),CCW080B
         BAL   11,B6001
         LA    3,CCW080A
         BAL   11,B6101
         LA    3,CCW080B
         BAL   11,B6101
         B     B5113
B5111    EQU   *
         MVC   CCW144+72(8),72(7)  GET SEQ NO IF ANY
         CLI   CCW144+72,C' '      Q- SEQ NO PRESENT
         BNE   B5112               A- YES,DO NOT INSERT
         MVC   SCW001,CCW144+70    SAVE BYTE 71
         MVC   CCW144+70(10),CXE010  SET MASK
         ED    CCW144+70(10),CPW005  SET SEQ NO
         MVC   CCW144+70(1),SCW001 RESTORE BYTE 72
B5112    MVC   CCW144+71(1),71(7)  RESTORE CONTINUATION
         MVC   CCO133+10(80),CCW144
         BAL   11,B6001
         LA    3,CCW144
         BAL   11,B6101
B5113    EQU   *
         MVI   CCW144,C' '
         MVI   CCW080A,C' '
         MVI   CCW080B,C' '
         MVC   CCW080A+1(79),CCW080A
         MVC   CCW080B+1(79),CCW080B
         BR    10
*
** SUBROUTINE B6001 - PRINT PDS UPDATE REPORT
*
B6001    EQU   *
         CP    CPW002,=P'3'
         BH    B6003
         ZAP   CPW002,=P'50'
         NI    B5001+1,X'0F'
B6003    EQU   *
         PUT   P02,CCO133
         MVI   CCO133,X'09'
         MVI   CCO133+1,C' '
         MVC   CCO133+2(131),CCO133+1
         SP    CPW002,=P'1'
         BR    11
*
** SUBROUTINE  B6101  - WRITE OUT RECORD
*
B6101    EQU   *
         CLC   CCW008A,0(8)
         BE    B6103
         MVC   CCW033+24(8),0(8)
         MVC   CCW008A,0(8)
         PUT   D01,CCW033
         CLC   72(8,7),=8C' '      Q- SEQ NO BLANK
         BNE   B6103
         PUT   D01,CCW025
         PUT   D01,CCW033
B6103    EQU   *
         PUT   D01,(3)
         BR    11
         EJECT 1
*
** EXECUTED INSTRUCTIONS
*
EX01     CLC   1(0,3),0(2)         EQUAL KEYS
EX02     MVC   0(0,14),0(1)        MOVE UNCHANGED DATA DOWN
EX03     MVC   0(0,14),74(3)       MOVE CHANGE VALUE
EX04     MVC   CCW080A(0),CCW144
*
** CONSTANTS,STORAGE AREAS AND LITERALS
*
D01      DCB   DDNAME=D01,,MACRF=PM,DSORG=PS
SAVEAREA  DS   18F
CCO133    DC   CL133' '
CCW008    DC   CL8' '
CXE010   DC    X'40212020202020202020'
CCW008A  DC    CL8' '
CCW008B  DC    CL8' '
CCW025   DC    CL80'./ NUMBER NEW1=10,INCR=10'
CCW033   DC    CL80'./ CHANGE LIST=ALL,NAME='
CCW144    DC   CL144' '
CXI001    DC   X'00'
CCW080A   DC   CL80' '
CCW080B   DC   CL80' '
SPW005    DS   PL5
CPW002    DC   PL2'0'
SCW001   DC    CL1' '              BYTE 71 SAVE AREA
CPW005   DC    PL5'0'                   RECORD COUNT
P02      DCB   DDNAME=P02,MACRF=PM,DSORG=PS
         LTORG
         END
