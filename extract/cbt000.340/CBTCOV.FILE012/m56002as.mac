         MACRO -- $MDLENTH -- COMPUTE DECIMAL LENGTH
&NAME    $MDLENTH &HEADER=L,&DOC=NO
         LCLC  &L,&N
&L       SETC  '$&HEADER'
&N       SETC  '$DLENGTH'
         AIF   ('&NAME' EQ '').NONAME
&N       SETC  '&NAME'
.NONAME  ANOP
         TITLE '&SYSECT CONTROL SECTION LENGTH'
         SPACE 10
         AIF   ('&DOC' NE 'YES').NODOC
         SPACE 1
*        THIS MACRO COMPUTES THE LENGTH OF THE CURRENT CSECT IN DECIMAL
         SPACE 1
*        CORRECT FORM IS:-
*NAME    $MDLENTH HEADER=< L × X > CHARACTER STRING UP TO SEVEN BYTES
*                                  IN LENGTH TO BE USED AS THE START OF
*                                  THE GENERATED NAMES.  DEFAULT IS 'L'
*              <DOC= NO × YES>     REQUEST FOR DOCUMENTATION
         SPACE 2
.NODOC   ANOP
         DS    0D                  FILL OUT CONTROL SECTION LENGTH
&L       EQU   *-&SYSECT
&L.1     EQU   &L/100000*100000              COMPUTE
&L.2     EQU   (&L-&L.1)/10000*10000           INTERMEDIATE
&L.3     EQU   (&L-&L.1-&L.2)/1000*1000          LENGTH
&L.4     EQU   (&L-&L.1-&L.2-&L.3)/100*100         VALUES
&L.5     EQU   (&L-&L.1-&L.2-&L.3-&L.4)/10*10
&L.6     EQU   &L-&L.1-&L.2-&L.3-&L.4-&L.5
         SPACE 5
*                                  DECIMAL LENGTH
         SPACE 3
&N       EQU   &L.1/100000*1048576+&L.2/10000*65536+&L.3/1000*4096+&L.4C
               /100*256+&L.5/10*16+&L.6
         SPACE 5
         MEND
