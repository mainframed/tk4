AMM.     TITLE 'RT5013M - SECOND LOAD OF TESTING MONITOR PROGRAM - A.M'
RT5013M  CSECT
***********************************************************************
*                                                                     *
*  THIS PROGRAM IS THE SECOND LOAD OF THE TESTING MONITOR PROGRAM. IT *
* RECEIVES CONTROL FROM THE FIRST LOAD VIA XCTL. THE PROGRAM LINKS TO *
* THE COMPILER, LINKAGE EDITOR OR PROGRAM LOAD AS SET UP BY THE FIRST *
* LOAD                                                                *
*                                                                     *
***********************************************************************
         SPACE 1
* REGISTER USAGE.
R0       EQU   0                   WORK REGISTER.
R1       EQU   1                   WORK REGISTER / PARAMETER REGISTER.
R2       EQU   2                   WORK REGISTER.
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
RBASE    EQU   12                  BASE REGISTER.
R13      EQU   13                  SAVE AREA POINTER.
R14      EQU   14                  RETURN ADDRESS.
R15      EQU   15                  ENTRY POINT ADDRESS/ RETURN CODE.
         EJECT
         USING DYNAR,R13           )
         BALR  RBASE,R0            ) INITIALISE AND DEFINE BASE REGS.
         USING *,RBASE             )
         SPACE 1
***********************************************************************
*                                                                     *
*        SET UP TIME LIMIT.                                           *
*                                                                     *
***********************************************************************
         SPACE 1
         L     R0,DYNTIME          SET GR0 = TIME.
         LTR   R0,R0               TEST FOR ZERO TIME.
         BZ    LINKCOMP            IF NONE THEN SKIP STIMER.
         STIMER REAL,LINKEX,BINTVL=DYNTIME   SET TIME LIMIT.
         SPACE 1
***********************************************************************
*                                                                     *
*        LINK TO COMPILER.                                            *
*                                                                     *
***********************************************************************
         SPACE 1
LINKCOMP CLI   DYNOPT,C'3'         DOES OPTION INCLUDE A COMPILE ?
         BH    LINKLKED            IF NOT THEN GO TO TEST LKED.
         LA    R1,DYNPARMC         SET GR1 -> COMPILER PARAMETER PTR.
         LA    R2,DYNBLDLC         SET GR2 -> BUILD LIST ENTRY.
         BAL   R9,LINKSUB          LINK TO LINK TO COMPILER.
         CLI   DYNOPT,C'1'         IS OPTION COMPLETE ?
         BE    RETURN              IF SO THEN RETURN.
         SPACE 1
***********************************************************************
*                                                                     *
*        LINK TO LINKAGE EDITOR.                                      *
*                                                                     *
***********************************************************************
         SPACE 1
LINKLKED MVI   DYNDDN+49,C'B'      SET 'SYSPRNTB' DDNAME IN OVER-RIDES.
         CLI   DYNOPT,C'4'         DOES OPTION INCLUDE A LINK-EDIT.
         BH    LINKGO              IF NOT THEN GO TO TEST GO.
         LA    R1,DYNPARML         SET GR1 -> LINK-EDIT PARAMETER PTR.
         LA    R2,DYNBLDLL         SET GR2 -> BUILD LIST ENTRY.
         BAL   R9,LINKSUB          LINK TO LINK TO LINKAGE EDITOR.
         CLI   DYNOPT,C'2'         IS OPTION COMPLETE ?
         BE    RETURN              IF SO THEN RETURN.
         SPACE 1
***********************************************************************
*                                                                     *
*        LINK TO GO STEP.                                             *
*                                                                     *
***********************************************************************
         SPACE 1
LINKGO   LINK  EPLOC=DYNNAME,SF=(E,DYNLINK),MF=(E,DYNPARMG),DCB=DYNDCB
         SPACE 1
***********************************************************************
*                                                                     *
*        RELEASE DYNAMIC AREAS.                                       *
*                                                                     *
***********************************************************************
         SPACE 1
RETURN   LR    R10,R15             SAVE RETURN CODE.
         TTIMER CANCEL             CANCEL REMAINING TIME INTERVAL
         LA    R3,2                SET GR3 = 2.
         L     R1,DYNPARMC         SET GR1 -> COMPILER PARAMETER LIST.
         LTR   R1,R1               TEST FOR LIST PRESENT.
         BZ    RETURNA             IF NOT THEN NO FREE.
         LH    R2,0(R1)            SET GR2 = PARAMETER LENGTH.
         LA    R0,4(R2)            SET GR0 = TOTAL LENGTH.
         SR    R1,R3               DECREMENT GR1 FOR FREEMAIN.
         FREEMAIN R,LV=(0),A=(1)   RELEASE DYNAMIC AREA.
RETURNA  L     R1,DYNPARMC+4       SET GR1 -> DD-NAME OVER-RIDE LIST.
*        LTR   R1,R1               TEST FOR OVER-RIDES PRESENT.
*        BZ    RETURNB             IF NOT THEN NO FREE.
*        LH    R2,0(R1)            SET GR2 = PARAMETER LENGTH.
*        LA    R0,4(R2)            SET GR0 = TOTAL LENGTH.
         SR    R1,R3               DECREMENT GR1 FOR FREEMAIN.
*        FREEMAIN R,LV=(0),A=(1)   RELEASE DYNAMIC AREA.
RETURNB  L     R1,DYNPARML         SET GR1 -> LINK-EDITOR PARAMETER LST
         LTR   R1,R1               TEST FOR LIST PRESENT.
         BZ    RETURNC             IF NOT THEN NO FREE.
         LH    R2,0(R1)            SET GR2 = PARAMETER LENGTH.
         LA    R0,4(R2)            SET GR0 = TOTAL LENGTH.
         SR    R1,R3               DECREMENT GR1 FOR FREEMAIN.
         FREEMAIN R,LV=(0),A=(1)   RELEASE DYNAMIC AREA.
RETURNC  L     R1,DYNPARMG         SET GR1 -> GO PARAMETER LIST.
         LA    R1,0(R1)            CLEAR HI-ORDER BYTE.
         LTR   R1,R1               TEST FOR LIST PRESENT.
         BZ    RETURND             IF NOT THEN NO FREE.
         LH    R2,0(R1)            SET GR2 = PARAMETER LENGTH.
         SR    R1,R3               DECREMENT GR1 FOR FREEMAIN.
         LA    R0,4(R2)            SET GR0 = TOTAL LENGTH.
         FREEMAIN R,LV=(0),A=(1)   RELEASE DYNAMIC AREA.
RETURND  L     R1,CLOSEL           )
         ALR   R1,R13              ) SET UP AND STORE L-FORM CLOSE.
         ST    R1,DYNPARMG         )
         CLOSE MF=(E,DYNPARMG)     CLOSE SYSLMOD DATA SET.
         LR    R15,R10             SET GR15 = RETURN CODE.
*        GOBACK (DYNAMIC,DYNL),RC=(15)  RETURN.
         LR    1,13
         L     13,4(13)
         LA    0,DYNL
         SVC   10
         L     14,12(13)
         LM    0,12,20(13)
         MVI   12(13),255
         BR    14
         SPACE 1
***********************************************************************
*                                                                     *
*        LINK SUB-ROUTINE.                                            *
*                                                                     *
***********************************************************************
         SPACE 1
LINKSUB  LINK  EPLOC=(R2),SF=(E,DYNLINK),MF=(E,(1))    LINK TO STEP.
         C     R15,DYNCOND         TEST RETURN CODE.
         BNL   RETURN              IF HIGH THEN RETURN.
         BR    R9                  RETURN.
         SPACE 1
***********************************************************************
*                                                                     *
*        TIMER COMPLETION EXIT ROUTINE.                               *
*                                                                     *
***********************************************************************
         SPACE 1
LINKEX   LA    R1,X'80'            SET DUMP CODE
         SLL   R1,12               SHIFT LEFT.
         LA    R1,X'322'(R1)       SET ABEND CODE.
         SLL   R1,12               SHIFT LEFT.
         SVC   13                  ABEND.
         SPACE 1
CLOSEL   CLOSE (DYNDCB-DYNAR),MF=L
         TITLE 'RT5013M - DYNAMIC SAVE AREA FORMAT. - A.MORRISH.'
DYNAR    DSECT
         DS    18F                 SAVE AREA.
DYNDCB   DCB   DDNAME=SYSLMOD,MACRF=E,EXLST=0,DSORG=PO
DYNLINK  LINK  DE=0,SF=L
DYNPARMC DC    A(0),X'80',AL3(DYNDDN)
DYNPARML DC    A(0),X'80',AL3(DYNDDN)
DYNPARMG DC    X'80',AL3(0)
DYNCOND  DC    F'4'                CONDITION FOR TERMINATION.
DYNTIME  DC    F'0'                TIME LIMIT.
DYNNAME  DC    CL8'TEMPNAME'
DYNDDN   DC    H'88',5XL8'0',C'SYSPRNTASYSLIN  ',3XL8'0',C'SYSLIN  '
DYNBLDLC DC    CL8'COMPILER'
DYNBLDLL DC    CL8'LINKEDIT'
DYNOPT   DC    CL1'1'              OPTION CODE
DYNL     EQU   *-DYNAR             DYNAMIC AREA LENGTH
         END   RT5013M
