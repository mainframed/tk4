*        GENERAL DESCRIPTION
*        THE UT34 UTILITY PROGRAM INVOKES THE IBM UTILITY IEBUPDTE
*        AFTER OBTAINING EXCLUSIVE CONTROL OF THE DATA SET WHICH IS TO
*        BE UPDATED. THIS ALLOWS DISP=SHR TO BE USED ON THE DATA SET
*        THAT IS BEING UPDATED AND THUS PREVENTS ALLOCATION DELAYS.
*        DATA SET REQUIREMENTS
*        THE DATASETS REQUIRED AND THE PARAMETERS ARE EXACTLY AS
*        DESCRIBED IN THE IBM PUBLICATION C28-6586 UTILITIES MANUAL
*        NOTES
*        SHOULD THE UPDATE PROGRAM IEBUPDTE END ABNORMALLY THE UTILITY
*        PROGRAM UT34 WILL ABEND WITH   USER CODE 444
         TITLE 'INVOKE IBM UTILITY IEBUPDTE'
UT34     CSECT
         IN    12,PARM=YES
         L     2,0(1)              ADDRESS OF PARM LIST
         L     4,16                CVT
         L     4,0(4)              MAGIC WORD
         L     4,4(4)              TCB
         L     4,12(4)             TIOT
         SR    5,5
         LA    4,24(4)             FIRST DDNAME
A10      EQU   *
         CLC   0(4,4),=F'0'        END OF TIOT
         BE    A20
         CLC   4(8,4),=C'SYSUT1  '
         BNE   A15                 TEST SYSUT2
         OI    IND,1               SET SYSUT1 FOUND IND
         B     A17                 GO TEST NEXT ENTRY
A15      EQU   *
         CLC   4(8,4),=C'SYSUT2  '
         BNE   A17
         OI    IND,2               SET SYSUT2 FOUND IND
A17      EQU   *
         IC    5,0(4)
         LA    4,0(5,4)
         B     A10                 GO LOOK AT NEXT TIOT ENTRY
A20      EQU   *
         CLI   IND,3
         BE    A25                 ENQ ON SYSUT2
         CLI   IND,2
         BE    A25                 ENQ ON SYSUT2
         CLI   IND,1
         BE    A23                 ENQ ON SYSUT1
         WTO   'UT34 SYSUT1 AND SYSUT2 DD CARDS MISSING',ROUTCDE=11
         ABEND 444
A23      EQU   *
         MVC   SYSUTS,=C'SYSUT1  '
A25      EQU   *
         CALL  ZZENQDEQ,(SYSUTS,ENQNAME)
         LINK  EP=IEBUPDTE,PARAM=((2)),VL=1
         LR    6,15           SAVE REG 15
         LA    3,0
         CALL  ZZENQDEQ,((3),ENQNAME)
         LTR   6,6
         BZ    C01
         WTO   'UT34 UPDATE IS UNSUCCESSFUL',ROUTCDE=11
         ABEND 444
C01      EQU   *
         OUT   0
ENQNAME  DS    CL44
SYSUTS   DC    C'SYSUT2  '
         CNOP  2,4                 ALIGN TO HALFWORD WHICH NOT FULL
LVALS    DC    H'0'
IND      DC    X'00'
         LTORG
         END
