         TITLE  'TRACER - THIS MODULE FORMATS A SPY PARTITION TRACE'
* MACRO
         MACRO
&NAME    CUREG
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUREG MACRO:16/01/75'
         POP   PRINT
         ACTR  50
         LCLA  &X,&Y,&Z
*    GENERAL REGISTERS
.LPR     ANOP
R&X      EQU   &X
&X       SETA  &X+1
         AIF   (&X LT 16).LPR
*    CONTROL REGISTERS
.LPC     ANOP
C&Y      EQU  &Y
&Y       SETA  &Y+1
         AIF   (&Y LT 16).LPC
*    FLOATING POINT REGISTERS
.LPF     ANOP
F&Z      EQU   &Z
&Z       SETA  &Z+2
         AIF   (&Z LT 8).LPF
         MEND
         MACRO
&NAME    CUENTER &BASE=(R12),&RECURSE=,&RENT=,&FIRST=,&CSECT=,&PARM=
         LCLA  &AL0,&AL1,&AL2,&AL9,&AL12
         ACTR  45                                              DEC 75
&AL2     SETA  &SYSNDX
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUENTER MACRO:16/06/77'
         POP   PRINT
         LCLC  &CL0,&CL1,&CL2,&CL3,&CL12,&CL13,&CL50
         LCLC  &CL51,&CL52,&CL53
         GBLB  &BG15
         AIF   ('&NAME' EQ '').B98
         ENTRY &NAME
.B98     ANOP
&CL0     SETC  '12'
         AIF   ('&BASE'(1,1) EQ '(').B10  *VALIDATE &BASE PARAMETER
&CL0     SETC  '&BASE'
         AIF   ('&CL0' EQ 'NONE').B11
         AIF   ('&CL0'(1,1) NE 'R').B4B
&AL9     SETA  K'&CL0-1
&CL0     SETC  '&CL0'(2,&AL9)
.B4B     AIF   ((&CL0 GT 1) AND (&CL0 LT 13)).B11
.BERA    MNOTE 9,'&&BASES WRONG,MACRO ABANDONED'
         MEXIT
.B10     ANOP
&CL0     SETC  '&BASE(1)'
&AL0     SETA  1                        COUNT ALONG TO-BE-SCANNED BASES
.B13     ANOP
&AL1     SETA  1                       *COUNT ALONG PREVIOUSLY SCANNED
&CL12    SETC  '&BASE(&AL0)'
         AIF ('&CL12'(1,1) NE 'R').B12A
&AL9     SETA  K'&CL12-1
&CL12    SETC  '&CL12'(2,&AL9)
.B12A    ANOP
&AL12    SETA  &CL12
.B12     AIF    ((&AL12 LT 2) OR (&AL12 GT 12)).BERA
.B15     AIF   (&AL1 EQ &AL0).B14      *J OUT IF REACHED LATEST
&CL13    SETC  '&BASE(&AL1)'
         AIF   ('&CL13'(1,1) NE 'R').B15A
&AL9     SETA  K'&CL13-1
&CL13    SETC  '&CL13'(2,&AL9)
.B15A    AIF   (&CL12 EQ &CL13).BERA *ERROR IF DUPLICATED
&AL1     SETA  &AL1+1                  *STEP ALREADY SCANNED PTR
         AGO   .B15
.B14     ANOP
&AL0     SETA  &AL0+1                  *STEP SCAN POINTER
         AIF   ('&BASE(&AL0)' NE '').B13 *J IF NOT NULL
&AL9     SETA  N'&BASE
         AIF   (&AL0 LT &AL9).BERA *ERROR IF NULL ELEMENT
.B11    ANOP
.*   START GENERATING PROGRAM
         DS    0H
         BALR  R15,0
         USING *,R15
&CL1     SETC  '&CSECT'
         AIF   ('&CL1' NE '').B30
&CL1     SETC  '*'
.B30     ANOP
&NAME    SAVE  (14,12),,&CL1
.B32     ANOP
.B90A    BAL   &CL0,OCSVA&AL2
         DROP  R15
         USING *,&CL0
OCSVD&AL2 EQU  *         HOLD IMMEDIATELY AFTER USING STATEMENT
         AIF   (&BG15).NODATIM
         CUREG                          REGISTER MNEMNONICS
&CL51    SETC  '&NAME'
         AIF   (K'&CL51 LE 8).DT1
&CL51    SETC  '&CL51'(1,8)
         AGO   .DT2
.DT1     ANOP
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '&SYSECT'
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '????????'
.DT2     ANOP
&CL52    SETC  '&SYSDATE'(4,2)'.'.'&SYSDATE'(1,2)'.'.'&SYSDATE'(7,2)
&CL53    SETC  '&SYSTIME'(1,2)':'.'&SYSTIME'(4,2)':00'
CUVRSION DC    C'****VERSION/',CL8'&CL51',C'/&CL52./&CL53'
&BG15    SETB  (1)                                             DEC 75
.NODATIM ANOP
OCSAR&AL2 DS    18F
OCSVA&AL2 EQU  *
         AIF   ('&BASE' EQ '').B89
&AL9     SETA  N'&BASE
         AIF   (&AL9 LE 1).B89
&AL1     SETA  &AL9
&AL0     SETA  2
.B88     ANOP
&CL2     SETC  '&BASE(&AL0)'
&CL3     SETC  '&BASE(&AL0-1)'
         LA    &CL2,1
         LA    &CL2,4095(&CL2,&CL3)
         USING OCSVD&AL2+4096*(&AL0-1),&CL2
         AIF   (&AL0 EQ &AL1).B87
&AL0     SETA  &AL0+1
         AGO   .B88
.B89     ANOP
.B87     ANOP
         ST    R13,OCSAR&AL2+4
         LR    R15,R13
         LA    R13,OCSAR&AL2
         ST    R13,8(R15)
         MEXIT
.MEND    MEND
* MACRO
         MACRO
&NAME    CUEXIT   &RC=(15)
         GBLB  &BG15                                 DEC 75
         LCLA  &B,&W
         LCLC  &Z
&NAME    DS 0H
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUEXIT MACRO:16/06/77'
         POP   PRINT
         L      R13,4(13)
&Z         SETC  '&RC'
.B94     AIF   ('&Z'(1,2) NE '(R').B84
&B       SETA  K'&Z-2
&Z        SETC  '&Z'(1,1).'&Z'(3,&B)
.B84     ANOP
         AIF   ('&Z' EQ '(15)').B60
         AIF   ('&Z'(1,1) EQ '(').B61
         RETURN (14,12),RC=&Z
         AGO   .B62
.B61     ANOP
&W       SETA  K'&Z-2
&Z       SETC  '&Z'(2,&W)
         LR    R15,&Z
.B60     RETURN (14,12),RC=(15)
.B62     ANOP
         MEND
TRACER   CUENTER
         LA     5,AREA+10
         OPEN  (INMOVE,,OUTMOVE,OUTPUT)
READ     GET   INMOVE,AREA
         MVC   HEADJBN,AREA
         BAL    2,PUTOUT
HERE     XC    BUFFER,BUFFER
         CLC   0(4,5),=X'FFFFFFFF'
         BE    FIN
         LA    6,BUFFER
         LA    7,94
         MVC   OUTNF,4(5)
TRCLOOP  MVO   0(2,6),0(1,5)
         MVC   1(1,6),0(5)
         NI    1(6),X'0F'
         LA    6,2(6)
         LA    5,1(5)
         BCT   7,TRCLOOP
         TR    BUFFER,TABLE
         MVI   OUTCTL,C'0'
         MVC   OUTPT,=C'PSW'
         MVC   OUTTT,=C'TYPE'
         MVC   OUTEPT,=C'EP'
         MVC   OUTNT,=C'NAME'
         MVC   OUTADT,=C'ADDRESS'
         MVC   OUTNFH,BUFFER+8
         MVC   OUTADR,BUFFER
         MVC   OUTEPF,BUFFER+32
         MVC   OUTPSW1,BUFFER+40
         MVC   OUTPSW2,BUFFER+48
         MVC   OUTTTP,=C'PRB '
         CLI  BUFFER+28,C'0'
         BE    TYPDUN
         MVC   OUTTTP,=C'SVRB'
         CLI  BUFFER+28,C'C'
         BNL   TYPDUN
         MVC   OUTTTP,BUFFER+28
TYPDUN   MVC   OUTTTH,BUFFER+28
         BAL   2,PUTOUT
         MVC   OUTRT,=C'R0-7 '
         MVI   OUTCTL,C'0'
         LA    10,OUTREG
         LA    11,BUFFER+104
         LA    9,8
BACK1    MVC   0(8,10),0(11)
         LA    10,9(,10)
         LA    11,8(,11)
         BCT   9,BACK1
         BAL   2,PUTOUT
         MVC   OUTRT,=C'R8-15'
         LA    10,OUTREG
         LA    9,2
BACK2    MVC   0(8,10),0(11)
         LA    10,9(,10)
         LA    11,8(,11)
         BCT   9,BACK2
         LA    11,BUFFER+56
         LA    9,6
BACK3    MVC   0(8,10),0(11)
         LA    10,9(,10)
         LA    11,8(,11)
         BCT   9,BACK3
         BAL   2,PUTOUT
         B     HERE
FIN      CLOSE (INMOVE,,OUTMOVE,)
         CUEXIT RC=0
PUTOUT   CP    LINE,=P'52'
         BL    NOHEAD
         PUT   OUTMOVE,HEADER
         SP    LINE,LINE
         MVI   OUTCTL,C'0'
NOHEAD   PUT    OUTMOVE,OUTAR
         CLI   OUTCTL,C'0'
         BNE   NOTDOUB
         AP    LINE,=P'1'
NOTDOUB  MVI  OUTAR,C' '
         MVC   OUTAR+1(L'OUTAR-1),OUTAR
         AP   LINE,=P'1'
         BR    2
         LTORG
LINE     DC   P'55'
BUFFER   DC    CL188' '
INMOVE   DCB   DDNAME=INMOVE,DSORG=PS,MACRF=GM,EODAD=FIN
OUTMOVE  DCB   DDNAME=OUTMOVE,DSORG=PS,MACRF=PM
HEADER   DC    CL20'1'
         DC    CL113'    ********   S P Y    T R A C E     F A C I L I C
               T Y     ********'
         ORG   HEADER+90
         DC    C'JOBNAME='
HEADJBN  DS    CL8
         ORG
TABLE     DC    C'0123456789ABCDEF'
OUTAR     DC   CL133'0 '
          ORG  OUTAR
OUTCTL    DS    C
          DS   CL3
OUTPT     DS   CL3
          DS   C
OUTPSW1  DS    CL8
         DS    C
OUTPSW2  DS    CL8
         DS    CL2
OUTTT    DS    CL4
         DS    C
OUTTTP   DS    CL4
         DS    C
OUTTTH   DS    CL4
         DS    C
OUTEPT   DS    CL2
         DS    C
OUTEPF   DS    CL8
         DS    C
OUTNT    DS    CL4
         DS    C
OUTNF    DS    CL8
         DS    C
OUTNFH   DS    CL16
         DS    C
OUTADT   DS    CL7
         DS    C
OUTADR   DS    CL8
         ORG   OUTTT
OUTRT    DS    CL5
         DS    C
OUTREG   DS    CL8
         ORG
AREA     DC    19000C' '
         END
