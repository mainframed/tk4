*
READVTOC STARTER
         RDJFCB (VTOCIN,,VTOCOUT,(UPDAT))
         MVI   JFCBIN,X'04'
         MVC   JFCBIN+1(43),JFCBIN
         MVI   JFCBOUT,X'04'
         MVC   JFCBOUT+1(43),JFCBOUT
         OPEN  (VTOCIN,,VTOCOUT,(UPDAT)),TYPE=J
*
*   HOUSEKEEPING
*
         XR    2,2       R2 - COUNT OF DSCBS COPIED
         OPEN  (PRINT,(OUTPUT))         OPEN PRINTDCB
*
*  READ LOOP TO FIND 1ST F0 DSCB ON OUTPUT VTOC
*
READLOOP READ  DECB2,SF,MF=E
         CHECK DECB2               CHECK READ
         CLI   DSCBTYPE,X'00'      IS IT F0 DSCB ?
         BNE   READLOOP            NO, READAGAIN
*
*  FIND DUMMY DSCBS ON INPUT VTOC
*
FINDUMMY READ  DECB1,SF,MF=E
         CHECK DECB1               CHECK READ
         CLI   DSCBTYPE,X'F1'      IS IT F1 DSCB?
         BNE   FINDUMMY            NO, READAGAIN
         CLC   FRSTEXTN,=10X'00'   IS IT A DUMMY DSCB (TRK,0) ?
         BNE   FINDUMMY            NO, READAGAIN
         LA    2,1(2)              INCREMENT DUMMY DSCB COUNTER
         WRITE DECB2,SF,MF=E       COPY ACROSS DUMMY DSCB
         CHECK DECB2               CHECK WRITE
         MVC   PRDSN,VTOCREC            MOVE DSN INTO PRINTLINE
         PUT   PRINT,PLINE              PRINT DSN
         MVC   CCHHR,VTOCOUT+8     STORE CCHHR OF RECORD WRITTEN
         B     READLOOP            GO & FIND NEXT DUMMY DSCB
*
*   END OF FILE ROUTINE FOR INPUT VTOC
*
EOFIN    EQU   *
         CLOSE (VTOCIN,)
         CLOSE (VTOCOUT,REREAD),TYPE=T
         READ  DECB2,SF,MF=E       READ F4 OF OUTPUT VTOC
         CHECK DECB2               CHECK READ
         MVC   VTOCREC+45(5),CCHHR                    RECORD
         LH    3,VTOCREC+50        UPDATE
         SR    3,2                        F0 DSCB
         STH   3,VTOCREC+50                       COUNT
         WRITE DECB2,SF,MF=E       REWRITE F4 DSCB
         CHECK DECB2               CHECK WRITE
         CLOSE (VTOCOUT)
         CLOSE PRINT                    CLOSE PRINT DCB
         STOPPER
SYN      EQU   *
         SYNADAF ACSMETH=BSAM
         LR    4,1
         ABEND 10,DUMP,STEP
VTOCIN   DCB   DDNAME=VTOCIN,DSORG=PS,MACRF=R,RECFM=F,BLKSIZE=96,      *
               KEYLEN=44,EXLST=LIST1,SYNAD=SYN,EODAD=EOFIN
VTOCOUT  DCB   DDNAME=VTOCOUT,DSORG=PS,MACRF=(R),BLKSIZE=96,RECFM=F,   X
               KEYLEN=44,EXLST=LIST2 SYNAD=SYN,EODAD=EOFOUT
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=45,   *
               BLKSIZE=2250
MFREAD2  READ  DECB2,SF,VTOCOUT,VTOCREC,'S',MF=L
VTOCREC  DS    0CL140
KEY      DS    CL44
DSCBTYPE DS    XL1
         DS    CL60
FRSTEXTN DS    CL10
         DS    CL25
MFREAD1  READ DECB1,SF,VTOCIN,VTOCREC,'S',MF=L
         DS    0F
LIST1    DC    X'87'
         DC    AL3(JFCBIN)
LIST2    DC    X'87'
         DC    AL3(JFCBOUT)
JFCBIN   DS    CL176
JFCBOUT  DS    CL176
         DS    0F
CCHHR    DS    0CL5
CC       DS    CL2
HH       DS    CL2
R        DS    CL1
PLINE    DS    0CL45
         DC    C' '                     SINGLE SPACE ASA CONTROL CHAR.
PRDSN    DS    CL44
         LTORG
         END
