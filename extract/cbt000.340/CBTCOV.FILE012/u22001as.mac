         TITLE 'SMF PRINT UTILITY PROGRAM'
*        THIS PROGRAM PRINTS SMF RECORDS IN A FANCY FORMAT
*        RATHER THAN USE IEBPTPCH
*
*        SMF TYPE #4 & #5 WILL BE PREFIXED WITH DATE,JOBNAME AND TIME
*        PLUS , AS ALL THE OTHERS  WILL BE PREFIXED WITH
*        TYPE AND LENGTH
*
*        THE JCL REQUIRED TO RUN IS :-
*        //SYSPRINT DD SYSOUT=A,DCB=(LRECL=133,BLKSIZE=
*        //INPUT DD DSN=SYS1.MANX,DISP=SHR
*
*        THIS CODE WAS KNOCKED UP VERY QUICKLY SO I APPLOLGIZE FOR IT'S
*        POOR QUALITY,BUT THE PROGRAM DOES WORK AMD CAN BE MODIFED
*        FOR USER RECORDS .
*
*        SHOULD YOU HAVE ANY QUIERIES CONTACT M.J.MOORE  TEL 01-405-9282
*
SMFUTIL  CSECT
R0       EQU   0
R1       EQU   1                   IBM CONVENTION
R2       EQU   2                   RECORD DSECT
R3       EQU   3                   RECORD LENGTH
R4       EQU   4                   PRINT CONVERTING
R5       EQU   5                   POINTING TO THE WORKFILE
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9                   LINE COUNT
R10      EQU   10                  PRINT REG
R11      EQU   11                  LINK  REG
R12      EQU   12
R13      EQU   13                  IBM CONVENTION
R14      EQU   14                  IBM CONVENTION
R15      EQU   15
         BEGIN ,BASE=(12)
         MVC   RECOUNT,=X'00000F'  ZEROISE RECORD COUNT
         MVC   PCOUNT,=X'000F'     ZEROISE PAGE COUNT
         OPEN  (SMF,(INPUT),PRINT,(OUTPUT))
         TM    SMF+48,X'10'        CHECK INPUT
         BNO   BADINPUT
         TM    PRINT+48,X'10'      CHECK OUTPUT
         BNO   BADPRINT
         CALL  DATE01,(RUNDATE),VL GET TODAYS DATE
         BAL   R10,HEADING         BRANCH OUT TO OUTPUT HEADING
         EJECT
GETONE   GET   SMF                 GET A RECORD
         LR    R2,R1               SAVE RECORD
         USING RECORD,R2           -> TO RECORD
         AP    RECOUNT,COUNT       ADD ONE TO RECORD COUNT
         CLC   TYPE,=X'0004'        IS IT TYPE FOUR
         BE    PREPRINT            YES
         CLC   TYPE,=X'0005'        IS IT TYPE FIVE
         BE    PREPRINT            YES
         CLC   TYPE,=X'0082'        IS IT TYPE ONE THRIRTY
         BNE   PRINTME             NO SO JSUT PRINT IT OUT
         EJECT
PREPRINT L     R1,STARTIME         LOAD UP TIME
         BAL   R10,TIMECNVT        GO AND CONVERT IT
         MVC   LTIME(8),TIM        NOW MOVE IT INTO THE LINE
         EDIT  LDATE-1,STARTDAT+1,'X0.000'      EDIT DATE
         MVC   LJOBNAME,JOBNAME    YES WELL PRINT JOBNAME
PRINTME  LH    R4,TYPE       THIS IS THE TYPE
         CVD   R4,DUBL             CONVERT IT
         MVC   LTYPE(4),=X'40202020'   MOVE IN MASK
         ED    LTYPE(4),DUBL+6     AND NOW THEW TYPE
         LH    R4,LENGTH           AND SO FOR THE LENGTH
         CVD   R4,DUBL             YES CONVERT IT
         MVC   LENGH(4),=X'40202020'   MOVE IN EDIT MASK
         ED    LENGH(4),DUBL+6     AND NOW THE LENGTH ITSELF
         LH    R3,LENGTH           LOAD INTO REG 3 RECORD LENGTH
         SH    R3,=H'4'            KNOCK OFF THE LENTH OF THE LENGTH
         LA    R2,4(R2)            KNOCK OFF THE LEGTH OR ELSE TROUBLE
PRNTMORE LA    R5,WORKFIL          LOAD UP OUR WORK AREA
         UNPK  0(15,5),0(8,2)      START TO UN PACK IT ALL
         UNPK  14(15,5),7(8,2)     AND SO ON FOR THE NEXT FEW CARDS
         UNPK  28(15,5),14(8,2)
         UNPK  42(15,5),21(8,2)
         UNPK  56(15,5),28(8,2)
         UNPK  70(11,5),35(6,2)
         TR    0(80,5),TRTABLE-240
         CH    R3,=H'40'           IS LINE TO BE PRINTED GRATER THAN 40
         BH    OUT                 Y%S
         AR    R3,R3               DOUBLE IT
         BCTR  R3,0                DECREMENT ONE FOR THE MOVE
         EX    R3,LASTLINE         AND FILE IT INTO THE LINE
         LA    R0,LINE             LOAD UP
         BAL   R10,PUTPRINT        AND PRINT
         B     GETONE              KNOW GO AND GET ANOTHER RECORD
OUT      MVC   LREC(80),WORKFIL    NOW MOVE IT INTO THE PRINT LINE
         LA    R0,LINE             GO AND
         BAL   R10,PUTPRINT
         SH    R3,=H'40'           SUBTRACT 80 BYTES
         LA    R2,40(R2)           OR -> REG 2 80 BYTES FURTHER ON
         B     PRNTMORE            MORE DATA TO PRINT
LASTLINE MVC   LREC(0),0(R5)       MOVE IN REMAINDING BITS FOR PRINTING
         EJECT
BADINPUT WTO   'UNABLE TO OPEN INPUT'
         ABEND 4,DUMP
BADPRINT WTO   'UNABLE TO OPEN OUTPUT'
         ABEND 8,DUMP
         EJECT
END      LA    R0,BLANK
         PUT   PRINT,(0)
         LA    R0,SCALE
         PUT   PRINT,(0)
         LA    R0,=X'8B'           LINE UP PAGE
         BAL   R10,PUTPRINT
         MVC   NO(6),=X'402021212121'   MOVE IN MASK
         ED    NO(6),RECOUNT       NOW MOVE IN RECS PROCESSED
         LA    R0,HEAD3            PRINT LAST PAGE
         BAL   R10,PUTPRINT
         CLOSE (PRINT,,SMF)        CLOSE FILES
         GOBACK
         EJECT
* ROUTINE TO CONVERT BINARY TIME LOADED INTO REG1 INTO A PRINTABLE FOM
TIMECNVT SLR   R0,R0               CLEAR REG
         D     R0,F6000            DIVIDE BY 6000 CONVERT TO MINS
         AH    R0,H50              ADD ROUNDING FACTOR
         CVD   R0,DUBL             CONVERT INTO DOUBLE WORD
         EDIT  SECS,DUBL+5,'X.00.00'
         SLR   R0,R0               CLEAR REG YET AGAIN
         D     R0,F60              DIVIDE MINS BY 60 TO GIVE HRS
         CVD   R0,DUBL
         EDIT  MINS,DUBL+6,'X.00'
         CVD   R1,DUBL
         UNPK  HRS(3),DUBL+6(2)    MOVE IN HRS
         OI    HRS+2,X'F0'         SET SIGN FOR PRINTING
         BR    R10                 BRANCH BACK
         EJECT
PUTPRINT PUT   PRINT,(0)           OUT IT GOES
         MVI   LINE+1,C' '         BLANK OUT PRINT LINE
         MVC   LINE+2(131),LINE+1  CONTINUE TO DO SO
         BCTR  R9,R10              GO BACK OR PRINT HEADING
         LA    R0,BLANK            LOAD IN A BLANK LINE
         PUT   PRINT,(0)           AND PRINT IT
         LA    R0,SCALE            JUST TO LOOK PRETTY PUT IN A SCALE
         PUT   PRINT,(0)           PRINT IT ALSO
HEADING  AP    PCOUNT,COUNT        ADD ONE TO PAGE COUNT
         LA    R0,=X'8B'           SKIP TO CHAN ONE
         PUT   PRINT,(0)           OUT IT GOES
         MVC   PAGE(4),=X'40202121' MOVE IN MASK
         ED    PAGE(4),PCOUNT      EDIT IN PAGE COUNT
         MVC   DATE(17),DAYDATE    MOVE IN TODAYS DATE
         LA    R0,HEAD1
         PUT   PRINT,(0)           OUT IT GOES
         LA    R0,HEAD2
         PUT   PRINT,(0)           OUT IT GOES
         LA    R9,56               SET LINE COUNT
         BR    R10
OBLK     LA    R6,PRINT
         CLC   62(2,R6),=X'0000'   IS BLKSIZE FILLED IN ?
         BNE   FILLED              YES
         MVC   62(2,R6),=X'0085'   NO
FILLED   BR    R14                 GOBACK
INRECFM  LA    R6,SMF
         CLC   36(1,R6),=X'58'     IS RECFM=VBS
         BNE   FORGETIT            NO
         OI    32(R6),X'60'        YES THEN SET BUFTK=A
FORGETIT BR    R14
         EJECT
SMF      DCB   DDNAME=INPUT,                                           X
               MACRF=GL,                                               X
               EXLST=EXIT2,                                            X
               EODAD=END,                                              X
               DSORG=PS
         EJECT
PRINT    DCB   DDNAME=SYSPRINT,                                        X
               MACRF=PM,                                               X
               DSORG=PS,                                               X
               RECFM=FBSM,                                             X
               EXLST=EXIT1,                                            X
               OPTCD=C,                                                X
               LRECL=133
         EJECT
RECORD   DSECT
LENGTH   DS    CL4                 ACCES METH LENGTH
TYPE     DS    CL2                 SMF REC TYPE
         DS    CL12
JOBNAME  DS    CL8                 SPEAKS FOR SELF
STARTIME DS    CL4                 RDR INIT TIME 1/100TH SECS
STARTDAT DS    CL4                 RDR INIT DATE 00YYDDDF
SMFUTIL  CSECT
         EJECT
HEAD1    DS    0CL133
         DC    X'11'
         DC    CL50' '
         DC    CL17'SMF PRINT UTILITY'
         DC    CL23' '
DATE     DC    CL17' '
         DC    CL17' '
PAGE     DC    CL4' '
         DC    CL4' '
HEAD2    DS    0CL133
         DC    X'11'
         DC    CL40'  TYPE  DATE     TIME      JOBNAME     L'
         DC    CL6'ENGTH '
         DC    CL34' '
         DC    CL20'S M F    R E C O R D'
         DC    CL34' '
HEAD3    DS    0CL133
         DC    X'09'
         DC    CL46'END OF REPORT TOTAL NUMBER OF RECORDS READ IS '
NO       DC    CL6' '
         DC    CL80' '
LINE     DS    0CL133
         DC    X'09'
         DC    CL1' '
LTYPE    DC    CL4' '
         DC    CL2' '
LDATE    DC    CL6' '
         DC    CL2' '
LTIME    DC    CL8' '
         DC    CL4' '
LJOBNAME DC    CL8' '
         DC    CL4' '
LENGH    DC    CL4' '
         DC    CL6' '
LREC     DC    CL80' '
         DC    CL3' '
BLANK    DS    0CL133
         DC    X'09'
         DC    CL132' '
SCALE    DS    0CL133
         DC    X'09'
         DC    CL49' '
         DC    CL40'         5        10        15        20'
         DC    CL40'        25        30        35        40'
         DC    CL3' '
         EJECT
HRS      DS    C
TIM      DS    0CL8
MINS     DS    CL3
SECS     DS    CL8
WORKFIL  DS    CL81
RUNDATE  DS    0CL50               DATE01 REQUIRES THIS
         DS    CL24
DAYDATE  DS    CL17                TODAY'S DATE
         DS    CL9
RECOUNT  DS    PL3                 RECORD COUNT
PCOUNT   DS    PL2                 PAGE COUNT
DUBL     DS    D
COUNT    DC    PL1'1'              COUNT DIGIT
F60      DC    F'60'
F6000    DC    F'6000'
EXIT1    DC    X'85',AL3(OBLK)
EXIT2    DC    X'85',AL3(INRECFM)
H50      DC    H'50'
TRTABLE  DC    C'0123456789ABCDEF'
         LTORG
         END
