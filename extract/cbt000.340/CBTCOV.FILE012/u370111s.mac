         MACRO
         WSFREGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
         MACRO
&NAME    EDIT  &TO,&FROM,&EDWD
         LCLA  &CNT1,&CNT2,&CNT3,&EDMK
         LCLC  &LITWK,&EDCD,&LIT1,&LIT2,&LIT3,&LIT4,&LIT5,&NO,&LIT6
         AIF   (T'&EDWD NE 'O').A1      IS EDIT WORD OMITTED
         MNOTE 1,'NO EDIT FORMAT GIVEN'
         MEXIT
.A1      AIF   ('&EDWD'(1,1) EQ '''').A2
.A15     MNOTE 1,'EDIT FORMAT NOT WITHIN QUOTS'
         MEXIT
.A2      AIF   ('&EDWD'(K'&EDWD,1) EQ '''').A3
         AGO   .A15
.A3      ANOP
&CNT1    SETA  K'&EDWD-2
&CNT2    SETA  1
         AIF   ('&EDWD'(2,1) EQ 'X').A4
         AIF   ('&EDWD'(2,1) EQ '*').A6
         MNOTE 1,'INPROPER FILL CHARARTER - USE X OR *'
         MEXIT
.A4      ANOP
&LITWK   SETC  '40'
         AGO   .A7
.A6      ANOP
&LITWK   SETC  '5C' *
&CNT2    SETA  &CNT2+1
.A7      ANOP
&CNT2    SETA  &CNT2+1
&EDCD    SETC  '&EDWD'(&CNT2+4*&CNT3,1)
         AIF   ('&EDCD' EQ '''').C91
         AIF   ('&EDCD' EQ 'X').A8
         AIF   ('&EDCD' EQ ',').A9
         AIF   ('&EDCD' EQ '0').B1
         AIF   ('&EDCD' EQ '.').B2
         AIF   ('&EDCD' EQ ' ').B3
         AIF   ('&EDCD' EQ 'C').B4
         AIF   ('&EDCD' EQ 'R').B5
         AIF   ('&EDCD' EQ '-').B6
         AIF   ('&EDCD' EQ '$').B7
         AIF   ('&EDCD' EQ '*').B8
         AIF   ('&EDCD' EQ '/').B9
         MNOTE 1,'IMPROPER EDIT CHARACTER X,0. CR-$*/'
         MEXIT
.A8      ANOP
&LITWK   SETC  '&LITWK'.'20' DIGIT SELECTOR
         AGO   .C9
.A9      ANOP
&LITWK   SETC  '&LITWK'.'6B'  COMMA
         AGO   .C9
.B1      ANOP
&LITWK   SETC  '&LITWK'.'21'  SIGNIFICANT STARTER
         AGO   .C9
.B2      ANOP
&LITWK   SETC  '&LITWK'.'4B'  PERIOD
         AGO   .C9
.B3      ANOP
&LITWK   SETC  '&LITWK'.'40'  BLANK
         AGO   .C9
.B4      ANOP
&LITWK   SETC  '&LITWK'.'C3'  C
         AGO   .C9
.B5      ANOP
&LITWK   SETC  '&LITWK'.'D9'  R
         AGO   .C9
.B6      ANOP
&LITWK   SETC  '&LITWK'.'60'  -
         AGO   .C9
.B7      ANOP
&EDMK    SETA  &CNT2+4*&CNT3
         AGO   .B1
.B8      ANOP
&LITWK   SETC  '&LITWK'.'5C'  *
         AGO   .C9
.B9      ANOP
&LITWK   SETC  '&LITWK'.'61'  /
.C9      AIF   (&CNT2 NE 3).A7
.C91     AIF   (&CNT3 EQ 0).D1
         AIF   (&CNT3 EQ 1).D2
         AIF   (&CNT3 EQ 2).D3
         AIF   (&CNT3 EQ 3).D4
         AIF   (&CNT3 EQ 5).D6
         AIF   (&CNT3 EQ 4).D5
         MNOTE 1,'EDIT WORD TOO LARGE TO HANDLE'
.D1      ANOP
&LIT1    SETC '&LITWK'
         AGO   .D9
.D2      ANOP
&LIT2    SETC '&LITWK'
         AGO   .D9
.D3      ANOP
&LIT3    SETC '&LITWK'
         AGO   .D9
.D4      ANOP
&LIT4    SETC '&LITWK'
         AGO   .D9
.D5      ANOP
&LIT5    SETC '&LITWK'
         AGO   .D9
.D6      ANOP
&LIT6    SETC  '&LITWK'
.D9      AIF   ('&EDCD' EQ '''').E1
&LITWK   SETC '&NO'
&CNT3    SETA  &CNT3+1
&CNT2    SETA 0-1
         AGO   .A7
.E1      AIF   ('&TO'(K'&TO,1) EQ ')').E2
&CNT3    SETA  &CNT3*4+&CNT2-1
         AIF   ('&EDWD'(2,1) NE '*').E1A
&CNT3    SETA  &CNT3-1
.E1A     ANOP
&LITWK   SETC  '('.'&CNT3'.')'
&NAME    MVC   &TO&LITWK,=X'&LIT1&LIT2&LIT3&LIT4&LIT5&LIT6'
         AIF   (&EDMK NE 0).F1
         ED    &TO&LITWK,&FROM
         AGO   .E4
.E2      ANOP
&NAME    MVC   &TO,=X'&LIT1&LIT2&LIT3&LIT4&LIT5&LIT6'
         AIF   (&EDMK NE 0).F2
.E3      ED    &TO,&FROM
         AGO   .E4
.F1      LA    1,&TO+&EDMK
         EDMK  &TO&LITWK,&FROM
.F15     BCTR  1,0
         MVI   0(1),C'$'
         AGO   .E4
.F2      EDMK  &TO,&FROM
         AGO   .F15
.E4      ANOP
         MEND
         TITLE 'SYSMS02 - DISPLAY PARTITIONS'
         ISEQ  73,80
         SPACE
***********************************************************************
*                                                                     *
*        THIS MODULE IS PART OF WSCC MASTER SCHEDULER.                *
*                                                                     *
*                                                                     *
*        IT PERFORMS A DISPLAY OF ALL PARTITIONS.                     *
*                                                                     *
*                                                                     *
*        REGISTER USAGE.                                              *
*                                                                     *
*               R0 - SYSTEM/WORK                                      *
*               R1 - SYSTEM/WORK                                      *
*               R2 - WORK                                             *
*               R3 - WORK                                             *
*               R4 - CONSOLE ID                                       *
*               R5 - CURRENT PIB -> OR TIOT ->                        *
*               R6 -                                                  *
*               R7 - CURRENT TCB ->                                   *
*               R8 - CURRENT PART. NO.                                *
*               R9 - NO OF TCBS                                       *
*              R10 - A(TCB LIST)                                      *
*              R11 - A(WTO WORK AREA)                                 *
*              R12 - BASE                                             *
*              R13 -                                                  *
*              R14 - SYSTEM/WORK                                      *
*              R15 - SYSTEM/WORK                                      *
*                                                                     *
*                                                                     *
*        WRITTEN BY CHRIS HUTCHINS, OCTOBER 1973                      *
*                                                                     *
*        WEST SUSSEX COUNTY COUNCIL SYSTEMS PROGRAMMING GROUP.        *
*                                                                     *
***********************************************************************
         EJECT
SYSMS02  CSECT
         BALR  R12,0
         USING *,R12
         B     *+12
         DC    CL8'SYSMS02'
         SPACE
         WSFREGS
CVT      EQU   16
         EJECT
         SLR   R4,R4               CLEAR R4
         L     R10,CVT             CVT ->
         L     R1,0(R10)           TCB NEW/OLD ->
         L     R1,4(R1)            TCB ->
         L     R1,0(R1)            SVRB ->
         IC    R4,96+X'28'(R1)     LOAD CONSOLE ID FROM EX SAVE AREA
         LR    R0,R4               LOAD INTO R0
*
*        ISSUE 'HEADING' WTO
*
         WTO   'PART SIZE CLASS INIT JOB NAME STP NAME',               -
               DESC=(5),MCSFLAG=(REG0,RESP)
*
*        GET WORK AREA FOR WTO ETC
*
         GETMAIN R,LV=WTOWLTH,SP=255
         LR    R11,R1              SAVE ADDRESS IN R11
         USING WTO,R11
         EJECT
*
*        GET TCB POINTERS
*
         SLR   R9,R9               CLEAR R9
         IC    R9,X'F8'(R10)       LOAD NO. OF PP TCBS
         L     R10,X'F4'(R10)      TCB LIST -> (P0 TCB)
         LA    R10,0(R10)          CLEAR HIGH ORDER BYTE
         SLR   R8,R8               SET PART. NO. (P0)
         EJECT
*
*        FORMAT PARTITION DETAILS
*
PARTFORM L     R7,0(R10)           LOAD CURRENT TCB ->
         MVC   WTO(WTOLTH),KWTO    INITIALISE
*        PARTITION NO.
         CVD   R8,DWRK
         EDIT  WTOPART-1,DWRK+6,'X0X'
         MVI   WTOPART-1,X'00'     RESET BYTE OVERWRITTEN BY EDIT
*        PARTITION SIZE
         L     R1,24(R7)           LOAD BBX ->
         LM    R0,R1,4(R1)         LOAD START OF END ADDRESSES
         SR    R1,R0               CALCULATE SIZE
         SRA   R1,10               DIVIDE BY 1024 FOR 1K BLOCKS
         CVD   R1,DWRK
         EDIT  WTOSIZE-1,DWRK+6,'X0X'
         LTR   R1,R1               ZERO PARTITION?
         BZ    PARTEND
*        PARTITION CLASS
PARTCLAS L     R5,124(R7)          LOAD PIB ->
         TM    124(R7),B'11000000' PP PARTITION
         BO    PP                  YES, THEN BRANCH
         TM    124(R7),B'01000000' RDR PARTITION?
         BZ    QWTR                NO, THEN IS IT WTR
         MVC   WTOCLASS,=C'RDR'
         B     JOBNAMEX
QWTR     TM    124(R7),B'10000000' WTR PARTITION?
         BZ    SYSTASK             NO, THEN IT IS A SYS. TASK
         MVC   WTOCLASS,=C'WTR'
         B     JOBNAMEX
SYSTASK  EQU   *
         MVC   WTOCLASS,=C'SYS'
         B     JOBNAMEX
*
*        THE JOB CLASSES ARE IN BYTES 25-27 AND ARE IN DESCENDING
*        ORDER.  THEY ARE IN BINARY ASCENDING ORDER, 28 EQUATING TO A,
*        29 EQUATING TO B, AND SO ON UPTO 36 WHICH EQUATES TO O.
*
PP       LA    R2,WTOCLASS         POINT TO OUTPUT BUFFER
         LA    R1,27(R5)           POINT HIGHEST PRTY
         LA    R3,3                LOAD LOOP COUNTER
         SLR   R0,R0               CLEAR R0
PPLOOP   CLI   0(R1),X'00'         CLASS ZERO?
         BZ    INIT                YES, THEN NO MORE CLASSES
         IC    R0,0(R1)            LOAD CLASS CODE
         SH    R0,=X'0028'         DECREMENT
         STC   R0,0(R2)            STORE
         TR    0(1,R2),CLASSTAB          & TRANSLATE
         LA    R2,1(R2)
         BCTR  R1,0
         BCT   R3,PPLOOP
*        INITIATOR DETAILS
INIT     TM    12(R5),B'00000100'  DEFINITION IN PROGRESS?
         BZ    PSTOP               NO, THEN IS INITIATOR STOPPED
         MVC   WTOINIT,=C'DEFN'
         B     JOBNAME
PSTOP    TM    12(R5),B'10000000'  INITIATOR STOPPED?
         BO    PACTIVE             NO, THEN IS PARTITION ACTIVE
         MVC   WTOINIT,=C'STOP'
         B     JOBNAME
PACTIVE  TM    12(R5),B'00000001'  PARTITION ACTIVE?
         BZ    PWAIT               NO, THEN PARTITION MUST BE WAITING
ACTV     MVC   WTOINIT,=C'ACTV'
         B     JOBNAME
PWAIT    MVC   WTOINIT,=C'WAIT'
*        JOB & STEP NAME
JOBNAME  EQU   *
JOBNAMEX EQU   JOBNAME
         TM    173(R7),X'40'       TCB NON DISPATCHABLE?
         BZ    JOBCONT             NO, THEN CONTINUE
         MVC   WTOJOB(L'NONDIS),NONDIS
         B     PARTEND
JOBCONT  L     R5,12(R7)           LOAD TIOT ->
         LTR   R5,R5               ZERO?
         BNZ   TIOTMOVE            NO, THEN MOVE IN INFO
         CLC   WTOINIT,=C'ACTV'    PP RUNNING?
         BE    JSMVC
         OC    0(4,R7),0(R7)       RB?
         BZ    PARTEND
JSMVC    MVC   WTOJOB(L'JS),JS     YES, THEN JOB SCHEDULER ACTIVE
         B     PARTEND
TIOTMOVE CLI   0(R5),C'A'
         BL    JSMVC
         CLI   0(R5),C'Z'
         BH    JSMVC
         MVC   WTOJOB,0(R5)        MOVE JOB NAME
         MVC   WTOSTP,8(R5)        MOVE STEP NAME
         EJECT
*
*        WRITE OUT PARTITION DETAILS
*
PARTEND  LR    R0,R4               LOAD CONSOLE ID
         WTO   MF=(E,(R11))
         SPACE 5
*
*        UPDATE POINTERS AND PROCESS NEXT PARTITION
*
         LA    R10,4(R10)          POINT TO NEXT TCB IN LIST
         LA    R8,1(R8)            INCREMENT TO NEXT PARTITION NO.
         BCT   R9,PARTFORM
         SPACE 10
*
*        CLEAR UP & EXIT
*
         FREEMAIN R,LV=WTOWLTH,A=(R11),SP=255
         BR    R14                 EXIT
         EJECT
*
*        CONSTANTS
*
KWTO     WTO   ' NN  NNNK                             ',               -
               DESC=(5),MCSFLAG=(REG0,RESP),                           -
               MF=L
CLASSTAB DC    C'ABCDEFGHIJKLMNO'
JS       DC    C'JOB SCHEDULER'
NONDIS   DC    C'NON DISPATCHABLE'
         LTORG
         EJECT
*
*        WTO DSECT & WORK AREAS
*
WTO      DSECT
         DS    4C                  LENGTH & FLAGS
WTOPART  DS    CL3
         DS    2C
WTOSIZE  DS    CL3
         DS    3C
WTOCLASS DS    CL3
         DS    2C
WTOINIT  DS    CL4
         DS    C
WTOJOB   DS    CL8
         DS    C
WTOSTP   DS    CL8
WTOMCS   DS    XL4
WTOLTH   EQU   *-WTO
*        WORK AREAS
DWRK     DS    D
WTOWLTH  EQU   *-WTO
         END
