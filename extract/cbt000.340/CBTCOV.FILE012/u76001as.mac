*          DATA SET FICHE15B   AT LEVEL 006 AS OF 21/03/79
FICHE15  ZENT
         L     R2,0(R1)                PICK UP ADDRESS OF RECORD
         EJECT
* FICHE15
*        THIS PROGRAM, TOGETHER WITH FICHE35, ARE SORT EXITS WHICH
*        ENABLE A WRITER-TO-TAPE OUTPUT FILE TO BE SORTED INTO JOB
*        NAME ORDER.
*        A REQUIREMENT IS THAT THE OUTPUT FROM THE WRITER HAS JOB
*        SEPARATOR PAGE(S) WITH THE JOB NAME IN THE STANDARD IBM
*        POSITION AND FORMAT, AND A DOUBLE ROW OF *S AS DELIMITERS.
*
*        EXAMPLE OF FILE:-
*              *****
*              *****
*              HEADER PAGE 1
*                 "
*                 "
*              *****
*              *****
*              HEADER PAGE 2 (OPTIONAL)
*                 "
*                 "
*              *****
*              *****
*              DATA PAGES
*                 "
*                 "
*                 "
*              *****
*              *****
*              TRAILER PAGE (OPTIONAL)
*                 "
*                 "
*              *****
*              *****
*
*        METHOD
*            1 READ FILE TIL TWO ROWS OF *S
*            2 READ 1ST LINE AFTER , EXTRACT 'JOB NAME'
*            3 READ NEXT LINE , EXTRACT 'JOB NAME'
*            4 IF TWO JOB-NAMES = , THEN ITS SEP PAGE - GO TO 6
*            5 ITS DATA , SO ADD JOBNAME TO PRINT LINES AND OUTPUT
*               TO SORT . REPEAT TIL TWO ROWS OF *S - THEN GO TO 2.
*            6 ITS SEP PAGE . IF JOBNAME = PREVIOUS JOB NAME THROW
*               AWAY LINES TIL TWO *S AS ITS 2ND HEADER OR TRAILER.
*                IF NOT EQUAL ITS NEW JOB SO OUTPUT HEADER PAGE TIL TWO
*                  ROWS OF *S
*
*              IT SHOULD WORK FOR ANY NO OF HEADER AND TRAILER PAGES.
*
* FICHE35
*        THIS JUST STRIPS OF JOBNAME FROM PRINT LINES.
         EJECT
         LTR   R2,R2                   ? EOF
         BZ    EOF                     YES
         SPACE 3
         L     R4,NEXT                 GO TO ROUTINE TO DEAL WITH
         BR    R4                       EXPECTED RECORD
         SPACE 3
FIRST    DS    0H                      ADD Z1 RECORD TO START OF FILE
         LA    R15,12
         LA    R1,Z1REC
         MVC   NEXT,=A(AST)
         B     EXIT
         SPACE 3
AST      DS    0H
         CLC   1(132,R2),ASTLINE       IS THIS AN ASTERISK LINE ?
         BNE   HED                     NO, GO SEE IF ITS A HEADER PAGE
         ZAP   LINES,MAXLINES
         LA    R15,4
         MVC   NEXT,=A(AST)
         B     EXIT
         SPACE 3
HED      DS    0H
         BAL   R10,EXTRACT             EXTRACT JOB NAME FROM 1ST LINE
         MVC   JOB1,JOBNAME            STORE JOB NAME
         MVC   STORLINE+8(133),0(R2)    & LINE
         LA    R15,4
         MVC   NEXT,=A(HEDA)
         B     EXIT
         SPACE 3
HEDA     DS    0H
         BAL   R10,EXTRACT             EXTRACT JOBNAME FROM 2ND LINE
         CLC   JOB1,JOBNAME            ? EQUAL TO 1ST LINE
         BNE   STORED                  NO, THEREFORE DATA
*                                      YES, ITS A SEP PAGE
         SPACE 3
SEP      DS    0H
         CLC   JOBNAME,LASTJOB         ? SAME JOBNAME AS PREVIOUS SEP
         BE    SAMESEP                 SAME, REJECT IT
         MVC   LASTJOB,JOBNAME         NEW ONE, WANT IT
         B     STORED
SAMESEP  DS    0H
         LA    R15,4
         MVC   NEXT,=A(SEEKAST)
         B     EXIT
         SPACE 3
SEEKAST  DS    0H                      REJECT UNTIL NEXT ASTERISK LINE
         CLC   1(132,R2),ASTLINE
         BE    AST
         LA    R15,4
         MVC   NEXT,=A(SEEKAST)
         B     EXIT
         SPACE 3
STORED   DS    0H
         MVC   STORLINE(8),LASTJOB     OUTPUT STORED LINE
         LA    R15,12
         LA    R1,STORLINE
         MVC   NEXT,=A(REQD)
         B     EXIT
         SPACE 3
REQD     DS    0H                      OUTPUT TIL NEXT ASTERISK LINE
         CLC   1(132,R2),ASTLINE
         BE    AST
         MVC   LINE(8),LASTJOB
         MVC   LINE+8(133),0(R2)
         BAL   R10,CHECK60
         LA    R15,0
         LA    R1,LINE
         MVC   NEXT,=A(REQD)
         B     EXIT
         SPACE 3
EOF      DS    0H
         LA    R15,8
         B     EXIT
         SPACE 3
EXIT     DS    0H                      EXIT FROM PROGRAM
         L     R13,4(R13)
         LM    R2,R12,28(R13)
         L     R0,20(R13)
         L     R14,12(R13)
         BR    R14
         SPACE 3
EXTRACT  DS    0H                      EXTRACT JOBNAME FROM BLOCKS
         LA    R7,JOBNAME
         LA    R5,8
         LA    R9,10(R2)
LOOP3    LA    R8,12
         LR    R6,R9
LOOP1    CLI   0(R6),C' '
         BNE   LOOP2
         LA    R6,1(R6)
         BCT   R8,LOOP1
LOOP2    MVC   0(1,R7),0(R6)
LOOP9    LA    R7,1(R7)
         LA    R9,14(R9)
         BCT   R5,LOOP3
         BR    R10
         SPACE 3
CHECK60  DS    0H                      CHECK ONLY 60 LINES PER PAGE
         CLI   LINE+8,C'1'             ? HOF
         BNE   NOTHOF
         ZAP   LINES,MAXLINES
         B     DOWN1
NOTHOF   CP    LINES,=P'0'             ? 60 ALREADY
         BNL   NOT60
         MVI   LINE+8,C'1'             MOVE IN HOF
         ZAP   LINES,MAXLINES
         B     DOWN1
NOT60    CLI   LINE+8,C'-'
         BE    DOWN3
         CLI   LINE+8,C'0'
         BE    DOWN2
         MVI   LINE+8,C' '
         B     DOWN1
DOWN3    SP    LINES,=P'1'             DECREMENT LINE COUNT
DOWN2    SP    LINES,=P'1'
DOWN1    SP    LINES,=P'1'
         BR    R10
         EJECT
NEXT     DC    A(FIRST)
JOBNAME  DC    CL8' '
JOB1     DC    CL8' '
LASTJOB  DC    CL8' '
STORLINE DS    CL141
LINE     DS    CL141
Z1REC    DC    XL8'00',CL133'Z1 STD1S110  PNJCL  Y'
ASTLINE  DC    120C'*',12C' '
LINES    DC    P'60'
MAXLINES DC    P'60'
         END
