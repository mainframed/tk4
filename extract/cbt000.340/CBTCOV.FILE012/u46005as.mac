         TITLE 'INSPECTION OF LIBRARIES- (COMPRPDS), ROOT SEGMENT- JAN X
               71- K.TAYFOOR'
SPS01COM CSECT
         SAVE  (14,12)
         BALR  11,0               **
         USING *,11                *
         ST    13,SAVEA1+4         *   S A V E    R E G I S T E R S
         LR    15,13               *
         LA    13,SAVEA1           *
         ST    13,8(15)           **
*
         OPEN  (PRINTER,(OUTPUT))
         MVI   PRTLINE,X'8B'
         PUT   PRINTER,PRTLINE
         MVC   PRTLINE(2),=X'1140'     CONTROL CHARACTER & SPACE
         MVC   PRTLINE+2(119),PRTLINE+1    CLEAR PRINT LINE
         MVC   PRTLINE+20(56),=C'*****  OUTPUT OF THE LIBRARIES INSPECTX
               ION PROGRAM  *****'
         LINK  EP=SPM09DAT,PARAM=(PRTLINE+104)
         PUT   PRINTER,PRTLINE
         CALL  SPS01CRD,(COMMAREA)   CONTROL CARD ANALYZER
         CLC   CODE,=F'4'
         BH    EXIT
         TM    WORD,X'05'      COMPARE DIRECTORY OR ALL MEMBERS?
         BZ    A1              NO
         CALL  SPS01DIR,(COMMAREA)   YES, COMPARE DIRECTORIES
         CLC   CODE,=F'4'
         BH    EXIT
         TM    WORD,X'06'
         BZ    EXIT
A1       CALL  SPS01FND,(COMMAREA)     FIND AND COMPARE MEMBERS
EXIT     MVI   PRTLINE,X'11'       SPACE 3 LINES
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(119),PRTLINE+1    CLEAR PRINT LINE
         MVC   PRTLINE+1(35),=C'END OF LIBRARIES INSPECTION PROGRAM'
         PUT   PRINTER,PRTLINE
         MVI   PRTLINE,X'11'
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(110),PRTLINE+1
         MVC   PRTLINE+1(14),=C'RETURN CODE IS'
         L     3,CODE
         CVD   3,WKAREA
         UNPK  PRTLINE+17(8),WKAREA+4(4)
         OI    PRTLINE+24,X'F0'
         PUT   PRINTER,PRTLINE
         CLOSE (PRINTER)
         L     13,4(13)
         RETURN (14,12)
SAVEA1   DC    18F'0'
WKAREA   DC    D'0'
COMMAREA DS    0D
CODE     DC    F'0'
PRTLINE  DC    121C' '
         DS    0D
TABLE    DC    320C' ',16C'*'
WORD     DC    F'0'
PRINTER  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),                    X
               LRECL=121,BLKSIZE=121,RECFM=FM
*
*
         LTORG
         END
         TITLE 'INSPECTION OF LIBRARIE- (CARD),CONTROL CARD ANALYZER-  X
               JAN 71- K. TAYFOOR'
SPS01CRD CSECT
         SAVE  (14,12)
         BALR  11,0
         USING *,11
         L     9,0(1)
         USING COMMAREA,9
         ST    13,SAVEB1+4
         LR    15,13
         LA    13,SAVEB1
         ST    13,8(15)
         LA    5,TABLE
         LR    6,5
         LA    7,CARD+71
         OPEN  (CARDIN)
         MVI   PRTLINE,X'11'
L1       GET   CARDIN,CARD
         MVI   PRTLINE+1,C' '          CLEAR PRINT LINE
         MVC   PRTLINE+2(119),PRTLINE+1
*
         MVC   PRTLINE+1(80),CARD
         PUT   PRINTER,PRTLINE
*
*
         LA    3,CARD
         SR    4,4
         LA    4,65(4)
*
*
A1       CLC   0(7,3),=C'COMPARE'      SCAN FOR 'COMPARE'
         BE    A2                      FOUND
         LA    3,1(3)
         BCT   4,A1
         B     ERROR
*
A2       S     4,=F'7'
         LA    3,7(3)
A3       CLI   0(3),C' '               SCAN FOR 'PDS' OR 'MEMBER(S)'
         BNE   A4                      FOUND
         LA    3,1(3)
         BCT   4,A3
         B     ERROR
*
*
*
A4       CLC   0(3,3),=C'PDS'          PDS ?
         BNE   A6                      NO
         OI    WORD,X'01'              YES
         B     L1
*
*
*
A6       CLC   0(6,3),=C'MEMBER'
         BNE   ERROR
         TM    WORD,X'02'          IS THIS THE 1ST MEMBER CARD?
         BO    ERROR               NO
         LA    3,6(3)              YES
         S     4,=F'6'
         OI    WORD,X'02'
*
         CLI   0(3),C'S'
         BNE   A8
         LA    3,1(3)
         BCTR  4,0
*
*
A8       CLI   0(3),C'='
         BNE   ERROR
         LA    3,1(3)
         BCTR  4,0
*
*
         CLC   0(3,3),=C'ALL'
         BNE   A10
         OI    WORD,X'04'
         B     L1
*
*
*
A10      CLI   0(3),C'('
         BNE   A10A                                                   *
         LA    3,1(3)
         LA    4,CARD+70
         SR    4,3
         B     AA
A10A     CLC   8(8,3),=8C' '                                          *
         BNE   ERROR                                                  *
         CLI   0(3),C'A'                                              *
         BL    ERROR                                                  *
         CLI   0(3),C'Z'                                              *
         BH    ERROR                                                  *
         MVC   0(8,5),0(3)     MOVE NAME INTO TABLE                   *
         LA    5,8(5)                                                 *
         B     A12A                                                   *
*
A12      CLI   0(3),C')'
         BNE   A14
A12A     MVC   0(8,5),=8C'*'       TABLE END MARKER                   *
         OI    WORD,X'40'              SET IND
         B     L1
*
*
*
A14      CLI   0(3),C','
         BNE   ERROR
         LA    3,1(3)
         BCTR  4,0
*
         CLI   0(3),C' '
         BNE   AA
*
         CLI   CARD+71,C' '
         BE    ERROR
*
         GET   CARDIN,CARD             FIELD IS CONTINUED ON SUBSEQUENT
         MVC   PRTLINE+1(80),CARD      CARD
         PUT   PRINTER,PRTLINE
*
*
         LA    3,CARD
         LA    4,CARD+70
         SR    4,3
A15      CLI   0(3),C' '
         BNE   AA
         LA    3,1(3)
         BCT   4,A15
         B     ERROR
*
*
***MOVE MEMBER NAME INTO TABLE *****
*
AA       CLC   0(8,5),=8C'*'           IS THIS END OF TABLE
         BNE   A16                     NO
         MVC   0(8,5),=8C'$'           YES,OBTAIN A NEW TABLE
*
*
*
         GETMAIN R,LV=336
         ST    1,12(5)
         LR    5,1
         MVI   0(5),C' '           CLEAR NEW TABLE
         MVC   1(160,5),0(5)
         MVC   160(160,5),0(5)
         MVC   320(16,5),=16C'*'
*
*
*
A16      LR    6,5
         LA    8,9
*
*
         CLI   0(3),C'A'
         BL    ERROR
         CLI   0(3),C'Z'
         BH    ERROR
*
         MVC   0(1,6),0(3)
         LA    3,1(3)
         BCTR  4,0
         LA    6,1(6)
         LA    5,8(5)
*
*
A18      CLI   0(3),C')'           END OF LIST OF NAMES
         BE    A20                 YES
         CLI   0(3),C','           END OF NAME
         BE    A20                 YES
         CLI   0(3),C' '
         BE    ERROR
         MVC   0(1,6),0(3)
         LA    3,1(3)
         LA    6,1(6)
         BCT   8,A18                NAME  MUST BE LESS THAN 8 CHARAC.
         B     ERROR
*
*
*
A20      CR    3,7                 C.C.72
         BNL   ERROR
         B     A12                 RETURN TO MAIN LINE
******
*
*
ERROR    MVC   CODE,=F'8'
         MVI   PRTLINE,X'11'
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(119),PRTLINE+1
         MVC   PRTLINE+1(23),=C'INVALID CONTROL CARD(S)'
         PUT   PRINTER,PRTLINE
*
A22      GET   CARDIN,CARD
         MVC   PRTLINE+1(80),CARD
         PUT   PRINTER,PRTLINE
         MVI   PRTLINE,X'11'
         B     A22
*
*
EODAD1   TM    WORD,X'FF'
         BM    A23
         MVC   CODE,=F'12'
A23      TM    WORD,X'02'
         BZ    A24
         TM    WORD,X'44'
         BM    A24
         BO    A24
         MVC   CODE,=F'12'
*
*
A24      CLOSE (CARDIN)
         L     13,4(13)
         RETURN (14,12)
*
SAVEB1   DC    18F'0'
CARD     DC    80C' '
*
*
*
CARDIN   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),                       X
               RECFM=F,BLKSIZE=80,LRECL=80,EODAD=EODAD1
*
         LTORG
COMMAREA DSECT
         DS    0D
CODE     DS    F
PRTLINE  DS    CL121
         DS    0D
TABLE    DS    CL336
WORD     DC    F'0'
PRINTER  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM)
*
*
*
         END
         TITLE 'INSPECTION OF LIBRARIES- (DIRECTRY),,COMPARE DIRECTORIEX
               S- JAN 71- K.TAYFOOR'
SPS01DIR CSECT
         SAVE  (14,12)
         BALR  11,0
         USING *,11
         L     8,0(1)
         USING COMMAREA,8
         LR    15,13
         ST    13,SAVEC1+4
         LA    13,SAVEC1
         ST    13,8(15)
*
*
*
         MVI   EXL,X'87'
         OPEN  (DS1,,DS2)          USIN QSAM DCB
         OI    SW,X'18'            SET IND 08 & 10
         RDJFCB (DS1,(INPUT))
*
         MVI   PRTLINE,X'8B'           CLEAR RRINT LINE AND SKIP
         PUT   PRINTER,PRTLINE
         MVI   PRTLINE,X'11'
         MVI   PRTLINE+1,C' '          TO CHANEL 1
         MVC   PRTLINE+2(119),PRTLINE+1
*
*
*
         MVC   PRTLINE+1(106),=C'COMPARISON OF DIRECTORIES ON (        X
                        -VOL=      ) AND (                 -VOL=      )X
                AT         '
*
         MVC   VOL1(6),JFCB+118
         MVC   PRTLINE+53(6),JFCB+118    VOL. SER. NO.                *
         MVC   PRTLINE+31(17),JFCB         DSNAME
         RDJFCB (DS2,(INPUT))
         MVC   VOL2(6),JFCB+118
         MVC   PRTLINE+88(6),JFCB+118    VOL. SER. NO.                *
         MVC   PRTLINE+66(17),JFCB         DSNAME
         LA    2,PRTLINE+99
         LINK  EP=SPM09DAT,PARAM=((2))    DATE
         PUT   PRINTER,PRTLINE
         GET   DS1,BLK1
         GET   DS2,BLK2
*
         LA    3,BLK1+2
         LA    4,BLK2+2
         LA    5,BLK1
         AH    5,BLK1                  ADDS OF UNUSED AREA IN  BLK1
         LA    6,BLK2
         AH    6,BLK2                  ADDS OF UNUSED AREA IN  BLK2
*
*
*
L1       CLC   0(8,3),0(4)
         BNE   L2
         OI    SW,X'01'
         BAL   12,X
         BAL   12,Y
         B     L1
*
L2       NI    SW,X'FE'
         LA    12,A6
A5A      TM    SW,X'80'
         BO    A5B
         OI    SW,X'80'
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(110),PRTLINE+1    CLEAR PRINT LINE
*
         MVC   PRTLINE+1(56),=C'THE FOLLOWING MEMBERS ARE NOT COMMON   X
               TO BOTH DATA SETS'
*
         PUT   PRINTER,PRTLINE
*
*
         MVC   PRTLINE+1(63),=C'   NAME OF MEMBER                      X
                          DISK IDENTITY'
         PUT   PRINTER,PRTLINE
*
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(110),PRTLINE+1    CLEAR PRINT LINE
*
         PUT   PRINTER,PRTLINE
A5B      BR    12
A6       CLC   0(8,3),0(4)
         BNL   L3
*
         MVC   PRTLINE+3(8),0(3)
         MVC   PRTLINE+50(6),VOL1
         PUT   PRINTER,PRTLINE
*
         BAL   12,X
         B     L1
*
*
L3       MVC   PRTLINE+3(8),0(4)
         MVC   PRTLINE+50(6),VOL2
         PUT   PRINTER,PRTLINE
*
         BAL   12,Y
         B     L1
*
*
*
EOD1     TM    SW,X'10'
         BZ    ERROR
         OI    SW,X'40'
         TM    SW,X'20'
         BO    EXIT
*
         TM    SW,X'01'
         BZ    A7B
         BAL   12,Y
A7B      BAL   12,A5A
A8       MVC   PRTLINE+3(8),0(4)       BUILD PRINT LINE FOR SYSUT2
         MVC   PRTLINE+50(6),VOL2
         PUT   PRINTER,PRTLINE
*
         BAL   12,Y
         B     A8
*
*
*
EOD2     TM    SW,X'08'
         BZ    ERROR
         OI    SW,X'20'
         TM    SW,X'40'
         BO    EXIT
         TM    SW,X'01'
         BZ    A8B
         BAL   12,X
A8B      BAL   12,A5A
A10      MVC   PRTLINE+3(8),0(3)       BUID PRINT LINE FOR SYSUT1
         MVC   PRTLINE+50(6),VOL1
         PUT   PRINTER,PRTLINE
*
         BAL   12,X
         B     A10
******   POINT  REG 3 TO NEXT ENTRY IN 1ST DIRECTORY ******
X        LA    3,11(3)
         IC    7,0(3)
         N     7,=F'31'
         AR    7,7
         AR    3,7
         LA    3,1(3)
         CR    3,5
         BNL   X4
         CLC   0(8,3),=8X'FF'
         BE    EOD1
         CLI   0(3),C'A'
         BL    X4
         CLI   0(3),C'Z'
         BNH   X6
*
*
X4       GET   DS1,BLK1
         CLC   BLK1(2),=2X'00'
         BE    EOD1
         LA    3,BLK1+2
         LA    5,BLK1
         AH    5,BLK1
X6       BR    12
******    POINT REG 4 TO NEXT ENTRY IN 2ND DIRECTORY ****
Y        LA    4,11(4)
         IC    7,0(4)
         N     7,=F'31'
         AR    7,7
         AR    4,7
         LA    4,1(4)
         CR    4,6
         BNL   Y4
         CLC   0(8,4),=8X'FF'
         BE    EOD2
         CLI   0(4),C'A'
         BL    Y4
         CLI   0(4),C'Z'
         BNH   Y6
*
*
Y4       GET   DS2,BLK2
         CLC   BLK2(2),=2X'00'
         BE    EOD2
         LA    4,BLK2+2
         LA    6,BLK2
         AH    6,BLK2
Y6       BR    12
*
ERROR    MVC   CODE,=F'9'
         MVI   PRTLINE+1,C' '              CLEAR PRINT LINE
         MVC   PRTLINE+2(110),PRTLINE+1
*
         MVC   PRTLINE+1(45),=C'******** ERROR ** PDS DIRECTORY MODULE X
               ******'
*
         PUT   PRINTER,PRTLINE
*
EXIT     CLOSE (DS1,,DS2)
         TM    SW,X'80'
         BO    A9
         MVI   PRTLINE,X'11'
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(110),PRTLINE+1
         MVC   PRTLINE+1(25),=C'DIRECTORIES ARE IDENTICAL'
         PUT   PRINTER,PRTLINE
A9       L     13,4(13)
         RETURN (14,12)
JFCB     DC    178X'00'
EXL      DC    A(JFCB)
SW       DC    X'00'
VOL1     DC    6C'0'
VOL2     DC    6C'0'
*
*
DS1      DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GM),EODAD=EOD1,EXLST=EXL, X
               RECFM=F,BLKSIZE=256,LRECL=256
*
DS2      DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(GM),EODAD=EOD2,EXLST=EXL, X
               RECFM=F,BLKSIZE=256,LRECL=256
*
BLK1     DC    256X'00'
BLK2     DC    256X'00'
SAVEC1   DC    18F'0'
         LTORG
COMMAREA DSECT
         DS    0D
CODE     DS    F
PRTLINE  DS    CL121
         DS    0D
TABLE    DS    CL336
WORD     DS    F
PRINTER  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM)
*
*
*
         END
         TITLE 'INSPECTION OF LIBRARIES- (FINDMEMB), FIND MEMBERS- JAN X
               71- K. TAYFOOR'
SPS01FND CSECT
         SAVE  (14,12)
         BALR  11,0
         USING *,11
         L     8,0(1)
         USING COMMAREA,8
         ST    13,SAVEC2+4
         LR    15,13
         LA    13,SAVEC2
         ST    13,8(15)
         SR    7,7
         MVI   PRTLINE,X'1B'
         PUT   PRINTER,PRTLINE
         MVI   PRTLINE,X'11'
*
         OPEN  (DS1,,DS2)
         TM    WORD,X'04'              COMPARE ALL MEMBERS?
         BZ    LL                  NO
*****  COMPARE  ALL MEMBERS  ******
         OPEN  (DS1SEQ)
*
AL1      GET   DS1SEQ,BLK1
         CLC   BLK1(2),=X'0000'
         BE    EOD1
         LA    3,BLK1+2            3 POINTS TO 1ST DIRECTORY ENTRY
         LA    5,BLK1
         AH    5,BLK1        REG 5 POINTS TO UNUSED AREA IN DIRECTORY
*
*
*
*
*
AL2      MVC   MEMBER(8),0(3)
         TM    11(3),X'80'         IS THIS ENTRY FOR ALIAS?
         BO    AL3             YES, SKIP IT.
*                                  NO
         FIND  DS2,MEMBER,D        FIND MEMBER ON DS2
         C     15,=F'8'
         BL    AL2A
         BAL   7,ERROR2
*
AL2A     C    15,=F'4'   RETURN ADDS. FROM ERROR ROUTINE
         BL    AL4                 MEMBER FOUND
*
AL3      LA    3,11(3)             MEMBER  NOT FOUND IN SYSUT2
         IC    7,0(3)              3 POINTS TO NEXT ENTRY
         N     7,=F'31'                 00
         AR    7,7
         AR    3,7
         LA    3,1(3)
         CR    3,5                                                    *
         BNL   AL1                                                    *
         CLC   0(8,3),=8X'FF'
         BE    EOD1                                                   *
         CLI   0(3),C'A'
         BL    AL1
         CLI   0(3),C'Z'
         BH    AL1
         B     AL2                                                    *
*                                                                     *
*                                                                     *
*
*
AL4      FIND  DS1,MEMBER,D            MEMBER FOUND IN SYSUT2
         C     15,=F'8'                IS IT IN SYSUT1?
         BL    AL5                     YES
         BAL   7,ERROR1                NO, ERROR
*
         B     AL3                     SKIP THIS MEMBER
AL5      CALL  SPS01MEM,((8),MEMBER)   COMPARE TWO MEMBERS
         B     AL3                     GET NEXT ENTRY
*
*
SYNAD1   MVI   PRTLINE,X'11'
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(118),PRTLINE+1    CLEAR PRINT LINE
*
*
*
EOD1     CLOSE (DS1,,DS2,,DS1SEQ)
         L     13,4(13)
         RETURN (14,12)
LL       LA    3,TABLE
*
ML1      CLC   0(8,3),=8C'*'       TABLE END MARKER?
         BE    ML2                 YES
*
         CLC   0(8,3),=8C'$'       LAST ENTRY IN THIS TABLE?
         BNE   MA1                 NO
         L     3,12(3)             YES,3 POINTS TO NEXT TABLE
*
MA1      MVC   MEMBER(8),0(3)
         FIND  DS1,MEMBER,D
         C     15,=F'8'
         BL    MA2
         BAL   7,ERROR1
*
         LA    3,8(3)
         B     ML1
*
*
MA2      C     15,=F'4'
         BL    MA3
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(110),PRTLINE+1
         MVC   PRTLINE+2(110),PRTLINE+1
         MVC   PRTLINE+1(61),=C'**** ERROR **** MEMBER          NOT FOUX
               ND ON SYSUT1 - IGNORED'
         MVC   PRTLINE+24(8),MEMBER
         PUT   PRINTER,PRTLINE
         MVC   CODE,=F'4'
*
*
         LA    3,8(3)
         B     ML1
*
*
MA3      FIND  DS2,MEMBER,D
         C     15,=F'8'
         BL    MA4
         BAL   7,ERROR2
*
         LA    3,8(3)
         B     ML1
*
MA4      C     15,=F'4'
         BL    MA5
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(110),PRTLINE+1
         MVC   PRTLINE+1(61),=C'**** ERROR **** MEMBER          NOT FOUX
               ND ON SYSUT2 - IGNORED'
         MVC   PRTLINE+24(8),MEMBER
         PUT   PRINTER,PRTLINE
         MVC   CODE,=F'4'
*
*
         LA    3,8(3)
         B     ML1
*
*
MA5      CALL  SPS01MEM,((8),MEMBER)   COMPARE TWO MEMBERS
         LA    3,8(3)
         B     ML1
*
ML2      B     SYNAD1                  END OF TABLE
ERROR1   MVI   PRTLINE,X'13'
         MVI   PRTLINE+1,C' '
         MVC   PRTLINE+2(118),PRTLINE+1    CLEAR PRINT LINE
         MVC   PRTLINE+1(110),=C'**** ERROR ***    PERMANENT I/O ERROR X
               FOUND DURING DIRECTORY SEARCH FOR          ON SYSUT1-THIX
               S MEMBER IGNORED'
         MVC   PRTLINE+72(8),MEMBER
*
         PUT   PRINTER,PRTLINE
         MVC   CODE,=F'12'
         BR    7
*
*
ERROR2   MVI   PRTLINE,X'11'
         MVC   PRTLINE+1(110),=C'**** ERROR ***     PERMANENT I/O ERRORX
               FOUND DURING DIRECTORY SEARCH FOR          ON SYSUT2-THIX
               S MEMBER IGNORED'
         MVC   PRTLINE+72(8),MEMBER
         PUT   PRINTER,PRTLINE
         MVC   CODE,=F'12'
         BR    7
*
*
         DS    0D
MEMBER   DC    CL8' '
SAVEC2   DC    18F'0'
BLK1     DC    256C' '
*
DS1      DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=(R)
*
DS2      DCB   DDNAME=SYSUT2,DSORG=PO,MACRF=(R)
*
DS1SEQ   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GM),BLKSIZE=256,          X
               LRECL=256,EODAD=EOD1,RECFM=F
*
*
*
         LTORG
COMMAREA DSECT
         DS    0D
CODE     DS    F
PRTLINE  DS    CL121
         DS    0D
TABLE    DS    CL336
WORD     DS    F
PRINTER  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM)
*
*
         END
         TITLE 'INSPECTION OF LIBRARIES- (COMPMEMB), COMPARE MEMBERS- JX
               AN 71- K. TAYFOOR'
SPS01MEM CSECT
         SAVE  (14,12)
         BALR  11,0
         USING *,11
         ST    13,SAVE+4
         LR    15,13
         LA    13,SAVE
         ST    13,8(15)
*
*
         SR    9,9                                                    *
         L     8,0(1)
         USING COMMAREA,8
         L     1,4(1)
         MVC   MEMBER,0(1)             MEMBER NAME PASSED FROM CALLING
*
*                                      MODULE
         XC    IND,IND             CLEAR IND
         MVI   EX1,X'87'
         RDJFCB (DS1,(INPUT))          READ JFCB FOR DS1
         MVC   JFCB+44(8),MEMBER       MODIFY JFCB (INCLUDE MEMBER NAME
         OI    JFCB+52,X'08'
         OI    JFCB+86,X'01'
*
         OPEN  (DS1),TYPE=J
         MVI   EX2,X'87'
*
         RDJFCB (DS2,(INPUT))          READ JFCB FOR DS2
         MVC   JFCB+44(8),MEMBER       MODIFY JFCB
         OI    JFCB+52,X'08'
         OI    JFCB+86,X'01'
*
         OPEN  (DS2),TYPE=J
*
A1       GET   DS1
         LR    3,1
*
         TM    0(3),X'40'      IS THIS A SYM RECORD
         BO    A1              YES, GET NEXT RECORD
*
*
         TM    0(3),X'20'      IS THIS A CESD RECORD?
         BZ    ERROR           NO, ERROR
*
         LH    12,DS1+82               LRECL
*
         STC   12,A2+1     INSERT CORRECT LENGTH IN THE FOLLOWING INST.
A2       MVC   CESD(1),8(3)    MOVE ESD DATA INTO CESD IN CORE
*
A3       GET   DS1
         LR    3,1
         MVC   IND,0(3)
         TM    IND,X'01'           IS THIS A CONTROL RECORD?
         BZ    A3                  NO, GET NEXT RECORD
*
*
L10      TM    IND,X'02'           IS THIS A CONTROL AND RLD RECORD?
         BO    A6                  YES
*
*                                  NO,THIS IS A CONTROL RECORD
         MVC   WKAREA(2),4(3)      COUNT (OF CONTROL DATA)
         LH    12,WKAREA
         STC   12,A4+1             INSERT CORRECT LENGTH IN THE
A4       MVC   CNTRL(1),16(3)      FOLLOWING INSTRUCTION AND MOVE
*                                  CONTROL DATA INTO CORE
         LA    3,CNTRL
         AR    12,3
         MVC   0(4,12),=4X'99'     END MARKER OF CONTROL DATA IN CORE
         B     A8
*
*
A6       MVC   WKAREA(2),4(3)      COUNT (OF CONTROL INFORMATION)
         LH    12,WKAREA
         MVC   WKAREA(2),6(3)      COUNT (OF RLD INFORMATION)
*
         LA    3,16(3)           * REG.3 WILL POINT TO CONTROL INFORM.
         AH    3,WKAREA          *
         STC   12,A7+1             INSERT CORRECT LENGTH IN INSTRUCTION
A7       MVC   CNTRL(1),0(3)
*
         LA    3,CNTRL
         AR    12,3
         MVC   0(4,12),=4X'99'     END MARKER OF CONTOL DATA IN CORE
*
** LENGTH OF TXT RECORD OR LENGTH OF CONTROL SECTION FOLLOWING ***
A8       SR    5,5
         LA    12,CNTRL+2
A9       CLC   0(2,12),=X'9999'    END MARKER ?
         BE    A10                 YES
*                                  NO
         AH    5,0(12)     REG.5= TOTAL LENGTH OF TXT UP TO THE FOLLOW.
         LA    12,4(12)    RECORD (NOT A TXT RECORD)
         B     A9
*
A10      LR    6,5
*
         GET   DS1             THIS MUST BE A TXT RECORD
         LR    3,1
*
A12      GET   DS2             GET RECORD FROM 2ND MEMBER
         LR    4,1
         TM    0(4),X'01'      IS THIS A CONTROL OR CNTRL/RLD RECORD?
         BZ    A12             NO, GET NEXT RECORD
*
*
A14      GET   DS2             THIS MUST BE A TXT RECORD
         LR    4,1
*
         CLC   DS1+82(2),DS2+82    EQUAL LRECL ?
         BE    A16
         MVI   PRTLINE,X'11'       NO, ERROR
         MVC   PRTLINE+1(120),=120C' '
         MVC   PRTLINE+1(76),=C'*** ERROR *** MEMBERS (        ) HAVE  X
               UNEQUAL LRECL - WILL SKIP THIS MEMBER'
         MVC   PRTLINE+24(8),MEMBER
         MVC   CODE(4),=F'4'
         PUT   PRINTER,PRTLINE
         MVC   PRTLINE+1(120),=120C' '     CLEAR PRTLINE
         B     END
*
** COMPARE TXT1 WITH TXT2
A16      LH    12,DS1+82       LRECL
         LR    2,12
*
A17      CLC   0(1,3),0(4)   COMPARE  TXT1  , TXT2
         BNE   A18
         LA    3,1(3)
         LA    4,1(4)
         BCT   2,A17
         B     A30         IDENTICAL
*
A18      SR    12,2                                                   *
*                                                                     *
*                  LOCATION= REG 6 - REG 5 + REG 12                   *
A20      LR    7,6
         SR    7,5
         AR    7,12        RELATIVE LOCATION IN REG. 7                *
*
** LOCATE  CONTROL SECTION FROM CNTRL RECORD **
*
         LA    10,CNTRL
A22      SH    7,2(10)
         C     7,=F'0'
         BNH   A26
*
         LA    10,4(10)
         CLC   0(2,10),=X'9999'        END MARKER?
         BE    ERROR
         B     A22
*
*
A26      AH    7,2(10)         RELATIVE LOCATION FROM CSECT
         CLC   0(2,10),ID                                             *
         BNE   A26A                                                   *
         AR    7,9                                                    *
** WHICH CSECT ? ***
*
A26A     LH    10,0(10)    CSECT ENTRY NO. IN CESD                    *
         SLA   10,4            10=10 X 16
*                              REG.10 POINTS TO CSECT SYMBOL
         S     10,=F'16'
         LA    12,CESD                                                *
         AR    10,12                                                  *
         MVC   PRTLINE+1(106),=C'***ERROR***  MEMBERS (        ) ARE NOX
               T IDENTICAL** EXAMINE LOCATION (      ) RELATIVE TO CSECX
               T (        )'
         MVC   PRTLINE+23(8),MEMBER    MEMBER NAME
         MVC   PRTLINE+98(8),0(10)     CSECT SYMBOL
         ST    7,WKAREA                    *                          *
         UNPK  WKAREA(8),WKAREA(5)         *                          *
*                                          *  CONVERT  DISPLAC. TO    *
         L     1,=F'8'                     *                          *
         LA    12,WKAREA-1                 *  HEXADECIMAL             *
         SR    2,2                                                    *
Z10      LA    12,1(12)                    *                          *
         CLI   0(12),X'F9'                 *                          *
         BNH   Z20                         *                          *
         IC    2,0(12)                                                *
         S     2,=F'57'
         STC   2,0(12)                                                *
Z20      BCT   1,Z10                       *                          *
         MVC   PRTLINE+71(6),WKAREA+1                                 *
         PUT   PRINTER,PRTLINE
         MVC   PRTLINE+1(120),=120C' '
         MVC   CODE,=F'4'
         B     END
******
*
*
A30      SH    5,DS1+82
         C     5,=F'5'         IS THIS THE LAST TXT RECORD?
         BL    A32             YES
*
         GET   DS1             NO, GET NEXT TXT RECORD
         LR    3,1
         B     A14             GO ON COMPARING TXT RECORDS
A32      LA    12,CNTRL            REG 12 = CURRENT ENTRY IN CNTRL REC*
A32A     CLC   2(2,12),=X'9999'  * REG 9 = LENGTH OF TEXT COMPARED    *
         BE    A32C              * SO FAR WHICH BELONG TO THE LAST    *
         CLC   ID(2),0(12)       * CSECT EXAMINED.                    *
         BE    A32B                                                   *
         SR    9,9                                                    *
         MVC   ID(2),0(12)                                            *
A32B     MVC   WKAREA(2),2(12)                                        *
         AH    9,WKAREA                                               *
         LA    12,4(12)                                               *
         B     A32A                                                   *
A32C     TM    IND,X'08'   IS THIS EOM (THE LAST TEXT RECORD)         *
         BO    EXIT            YES
*
L20      GET   DS1             NO
         LR    3,1
         MVC   IND,0(3)
*
         TM    IND,X'08'       IS EOM IND ON ?
         BO    A36             YES
*
         TM    IND,X'01'       NO, IS CNTRL RECORD IND ON?
         BO    L10             YES
         GET   DS2
         LR    4,1
         B     L20             NO, GET NEXT RECORD
*
A36      TM    IND,X'01'       IS THIS A CONTROL RECORD?
         BO    L10
         B     CHECK           NO, THEN IT MUST BE A RLD RECORD. BRANCH
*                              TO CHECK IF MORE RECORDS ON DS2
*
EOD1     NOP   ERROR
ERROR    MVC   PRTLINE+1(120),=120C' '
         MVC   PRTLINE+1(77),=C'*** ERROR *** MEMBERS (        ) ARE NOX
               T LOAD MODULES - WILL SKIP THIS MEMBER'
         MVC   PRTLINE+24(8),MEMBER
         PUT   PRINTER,PRTLINE
         MVC   CODE,=F'4'
         B     END
*
** NORMAL EXIT **
EXIT     MVC   PRTLINE+1(120),=120C' '
         MVC   PRTLINE+1(32),=C'MEMBERS (        ) ARE IDENTICAL'
         MVC   PRTLINE+10(8),MEMBER
         PUT   PRINTER,PRTLINE
*
*
END      MVC   PRTLINE+1(120),=120C' '
         CLOSE (DS1,,DS2)
         L     13,4(13)
         RETURN (14,12)
CHECK    GET   DS2
         CLC   IND,0(1)    IS DS2 LONGER THAN DS1
         BE    EXIT        NO
*                          YES,ERROR
EOD2     MVC   PRTLINE+1(120),=120C' '
         MVC   PRTLINE+1(58),=C'*** ERROR ***  MEMBERS(        ) ARE OFX
               DIFFERENT LENGTHS '
         MVC   PRTLINE+24(8),MEMBER
         PUT   PRINTER,PRTLINE
         B     END
*
*
         DS    0D
CESD     DC    256C'*'
CNTRL    DC    300C' '
EX1      DC    A(JFCB)
EX2      DC    A(JFCB)
JFCB     DC    200X'00'
MEMBER   DC    CL8' '
IND      DC    X'00'
ID       DC    X'0000'                                                *
WKAREA   DC    D'0'
DS1      DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),RECFM=U,EODAD=EOD1,   X
               EXLST=EX1
DS2      DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(GL),RECFM=U,EODAD=EOD2,   X
               EXLST=EX2
SAVE     DC    18F'0'
         LTORG
COMMAREA DSECT
         DS    0D
CODE     DS    F
PRTLINE  DS    CL121
         DS    0D
TABLE    DS    CL336
WORD     DS    F
PRINTER  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM)
*
*
         END
