*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSM43SOR.
SSM43SOR ENTER ENT=CORESORT
         LM    11,12,0(1)
         LM    0,3,0(12)
         BCTR  2,0                 SET KEYLEN
         BCTR  3,0                 SET KEYDISP
         SR    11,1                DISPLACEMENT FOR COMPARES
         MVI   TEST+1,X'D0'        ASSUME ASCENDING
         NC    COMP,=X'FF00F000F000'    RESET COMPARE INSTRUCTION
         STH   3,SETC+2
         STH   3,SETC+4
         STC   2,SETC+1
         OC    COMP,SETC
         CLI   16(12),C'D'
         BNE   *+8
         MVI   TEST+1,X'B0'        SET DESCENDING
         LH    3,=H'-1'
         AR    3,1                 LENGTH FOR MOVE
         BM    ERREXIT             INVALID LENGTH
         LR    2,1
         STH   2,RECLEN
         LR    8,0                 N
         LR    7,8                 M=N
L10      CH    7,=H'2'             ANY TO DO?
         BL    EXIT                NO - FINISHED
         SRL   7,2               *
         SLL   7,1                *M=2*(M/4)+1
         LA    7,1(0,7)          *
         LR    9,7
         MH    9,RECLEN
         LR    10,8               * LAST = N-M
         SR    10,7               *
         LR    4,2
L40      LR    5,4                 J=I
L20      LR    6,5                *
         AR    6,9
         LA    14,0(5,11)
         LA    15,0(6,11)
COMP     CLC   0(1,14),0(15)       COMPARE INST
TEST     BNH   L30                 ASSUMR ASCENDING ORDER
         EX    3,SHIFT1          *
         EX    3,SHIFT2           *EXCHANGE RECORDS
         EX    3,SHIFT1          *
         SR    5,9
         BP    L20
L30      AR    4,2
         BCT   10,L40
         B     L10                 NEXT ROUND
EXIT     LEAVE 0
ERREXIT  ABEND 16,DUMP
         LTORG
SETC     DC    3H'0'
RECLEN   DC    H'0'
SHIFT1   XC    0(1,14),0(15)
SHIFT2   XC    0(1,15),0(14)
         END
