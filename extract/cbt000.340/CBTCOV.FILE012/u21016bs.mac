LVOL     TITLE 'VOLUME TABLE FOR ZAJLIST - ZAJLVOL'
*        THIS IS THE VOLUME SET TABLE FOR ZAJLIST, USED IN ZAJL2 TO
*          EXPAND REQUESTS FOR FREE SPACE LISTINGS. IT IS DECLARED
*          THERE AS A STRUCTURE BASED ON THE EXTERNAL POINTER ZAJLPTR,
*          WHOSE DEFINITION FOLLOWS AS A SEPARATE CSECT. TO CHANGE THE
*          TABLE IT IS SIMPLY NECESSARY TO REASSEMBLE THESE TWO CSECTS
*          AND RELINK ZAJLIST INCLUDING THEM.  ALTERNATIVELY,
*          IMASPZAP MAY BE USED TO CHANGE INDIVIDUAL ENTRIES.
*
**    AUTHORS - ICI (MOND DIVISION)
ZAJLVOL  CSECT
NSETS    DC    H'2'                NUMBER OF SETS
NINSET   DC    H'3'                NUMBER IN EACH SET
*               NOTE THAT THERE MUST BE THE SAME NUMBER OF ENTRIES IN
*                 EACH SET.  HOWEVER, TRAILING ENTRIES MAY BE BLANK.
SET1     DC    C'SYSRES',C'MD0023',C'MD0024',C'MD0025'
SET2     DC    C'ONLINE',C'MD2020',C'      ',C'      '
ZAJLPTR  CSECT
         DC    A(ZAJLVOL)
         END
L3       TITLE 'ZAJL3 - EXTRACT INFORMATION FROM AN INDEX'
**       DESCRIPTION OF NODE TABLE ENTRY.
*
NODE     DSECT
NDN      DS    44C                 INDEX NAME
NDNLN    DS    H                   LENGTH OF NAME
NDVOL    DS    6C                  VOLUME SERIAL
NDTTR    DS    3C                  INDEX ADDRESS
NDFLG    DS    C                   FLAGS
*
S        DSECT
SAV      DS    26F                 SAVE AREA
*
ZAJL3    PLISAV
         SR    5,5                 FOR OPTIMISER
         LM    6,7,0(1)            PARAMETER ADDRESSES
*              PROCESS PARAMETERS
         L     8,0(6)              ADDRESS OF FIRST NODE
         LH    9,0(7)              CURRENT NODE NUMBER
         BCTR  9,0
         MH    9,=H'56'            CALCULATE ADDRESS OF
         AR    9,8                   CURRENT ENTRY.
         USING NODE,9
         MVC   INDEX(44),NDN
         MVC   VOL(6),NDVOL
         LOCATE FIRST              GET FIRST BLOCK OF INDEX
         C     15,=F'12'           INDEX FOUND ?
         BE    INDOK               BRANCH IF SO.
*
**       ERROR - INDEX BLOCK NOT FOUND
*
BLKER    SRL   15,2
         IC    14,CODES(15)
         STC   14,ERCOD
         PLIMSG ERR
         B     RET
*
**       BLOCK IS OK - EXAMINE IT
*
INDOK    EQU   *
         MVC   VOL(6),AVOL         COPY VOLUME SERIAL
         LA    8,AREA+20           FIRST USEFUL ENTRY IN BLOCK
*
**       EXAMINE NEXT ENTRY
*
LOOP     EQU   *
         CLC   0(8,8),=8X'FF'      LINK ENTRY ?
         BE    LINK                BRANCH IF SO
*
**       BUILD UP TABLES
*
         ST    8,PARM
         LA    1,PARML
         L     15,=V(ZAJL5)
         BALR  14,15
         SR    1,1                 POINT
         IC    1,11(,8)             AT
         SLL   1,1                   NEXT
         LA    8,12(1,8)              ENTRY
         B     LOOP
*
**       DEAL WITH LINK ENTRY
*
LINK     CLC   8(3,8),=X'000000'   ANOTHER BLOCK ?
         BE    RET                 BRANCH IF NOT
         MVC   TTR(3),8(8)         GET NEXT
         LOCATE CHAIN                BLOCK
         LTR   15,15               OK ?
         BNZ   BLKER               BRANCH IF NOT
         LA    8,AREA+2            FIRST ENTRY IN EXTENSION BLOCK.
         B     LOOP
*
**       RETURN TO CALLER
*
RET      L     13,4(,13)
         RETURN (14,12)
*
**       WORK AREAS
*
PARM     DS    F
PARML    DC    A(PARM)
FIRST    CAMLST NAME,INDEX,,AREA
CHAIN    CAMLST BLOCK,TTR,VOL,AREA
ERR      DC    A(ERRMES),AL2(66),AL2(0)
ERRMES   DC    C'ERROR CODE '
ERCOD    DC    C' '
         DC    C' ON INDEX '
INDEX    DC    CL44' '
CODES    DC    C'01234567'
TTR      DC    XL3'00'
VOL      DC    6X'00'
AREA     DS    34D
AVOL     EQU   AREA+259            VOL RETURNED BY LOCATE
*
         END
