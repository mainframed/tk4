BLEAT    START 0
*    THIS PROGRAM WILL GIVE A TAPE ERROR ANALYSIS BY 25 FEET
*    LENGTHS ON SL TAPES. THE TAPE IS WRITTEN FOR THE FULL
*    REEL AND THE UCB CHECKED AFTER EACH 25 FEET FOR ERRORS
*    THE TAPE IS THEN READ FOR THE FULL LENGTH & THE SAME
*    PROCEDURE USED.
*    TWO POINTS.... 1 ANY DATA ALREADY ON THE TAPE WILL BE
*    DESTROYED. 2 THE TAPE UNIT SHOULD BE CLEANED BEFORE USE
*    B Y
*    L ENGTH
*    E RROR
*    A NALYSIS FOR
*    T APES
*    POSSIBLE JCL  // EXEC PGM=BLEAT
*                  //STEPLIB DD DSN=USERLIB,DISP=SHR
*                  //SYSPRINT DD SYSOUT=A
*                  //TAPE DD DSN=&&TEST,UNIT=2400
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         DS    0H
         STM   14,12,12(13)
         BALR  R12,0
         USING *,12
         ST    R5,SAVEAREA
         LA    R5,SAVEAREA
         ST    R13,SAVEAREA+4
         ST    R5,8(0,13)
         LR    R13,R5
         L     R5,SAVEAREA
         B     SAVEAREA+80
CISSAVE  EQU   *
SAVEAREA DS    0F
         DC    C'    HIGHLOW R14 R15 R0  R1  R2  R3  '
         DC    C'R4  R5  R6  R7  R8  R9  R10 R11 R12 '
         DC    CL8'BLEAT'
         OPEN  (TAPE,(OUTIN),PRINT,(OUTPUT))
         L     R2,TAPE+44              DEB POINTER
         L     R2,32(R2)               UCB POINTER
         LA    R2,0(0,R2)
         MVC   VOLSER(6),28(R2)        RETAIN VOLUME SERIAL NUMBER
         L     R3,48(R2)               UCB EXTN POINTER
         LA    R3,12(0,R3)             TEMP READ COUNT
         LR    R9,R3
         LH    R8,=H'70'               FOR LATER PRINTING
         LH    R5,=H'65'
WRITER   XC    ECB,ECB
         EXCP  IOB
         WAIT  ECB=ECB
         CLI   ECB,X'7F'
         BE    GOOD
         BAL   R10,BAD
GOOD     BCT   R5,WRITER
         B     ACCUM1
BAD      LA    R4,IOB
         CLI   12(R4),X'0D'
         BE    WRITEND
         CLI   READSW,X'FF'
         BE    GOOD
         L     R6,CHANPROG
         LA    R1,ERGAP
         ST    R1,CHANPROG
         XC    ECB,ECB
         EXCP  IOB
         WAIT  ECB=ECB
         ST    R6,CHANPROG
         BR    R10
ACCUM1   LA    R5,8
         LA    R4,TRE
ACCUM2   CH    R5,=H'6'                UP TO START IOS ?
         BE    SIONLY
         CH    R5,=H'2'
         BNH   CLNERG
         SR    R2,R2
         IC    R2,0(R3)                NEW TOTAL FROM UCB EXTN
         CH    R2,0(0,R4)              COMPARE OLD TOTAL
         BH    LINEOUT
ACCUM3   LA    R4,2(0,R4)              NEXT HALF-WORD ACCUMULATOR
         LA    R3,1(0,R3)              NEXT UCB EXTN. TOTAL
         BCT   R5,ACCUM2
         LTR   R8,R8
         BM    NEWHDG
NEWSUB   MVI   SUBHDG+1,X'00'
         AP    FEET(4),=P'25'
         AP    LENGTH(2),=P'+1'
         CLI   ENDSW,X'FF'
         BE    LAST2
         LH    R5,=H'65'
         LR    R3,R9
         CLI   READSW,X'FF'
         BE    ENDOV
         B     WRITER
NEWHDG   MVI   PRINTRTN+1,X'00'
         B     NEWSUB
CLNERG   LA    R3,1(0,R3)
         B     SIONLY
LINEOUT  LR    R6,R5
         BCTR  R6,0
         MH    R6,=H'10'
         LA    R6,ERROR(R6)            FOR CORRECT ERROR TYPE
         MVC   LINE+4(10),0(R6)
         SH    R2,0(0,R4)
         LR    R7,R2
         CH    R7,=H'100'
         BH    SHOCKER
         LA    R6,LINE+25
ASTERISK MVI   0(R6),X'5C'             ONE ASTERISK
         LA    R6,1(0,R6)
         BCT   R7,ASTERISK
ADDNEW   AH    R2,0(0,R4)              ADD OLD TOTAL
         STH   R2,0(0,R4)              STORE NEW ACCUMULATION
         BAL   R10,PRINTRTN
         B     ACCUM3
SIONLY   LR    R6,R5
         BCTR  R6,0
         MH    R6,=H'10'
         LA    R6,ERROR(R6)
         LH    R2,0(R3)                NEW TOTAL
         SH    R2,0(0,R4)              SUBTRACT OLD TOTAL
         LA    R3,1(0,R3)
         LTR   R2,R2
         BZ    NONPRTN
         CH    R2,=H'65'
         BE    NONPRTN
         CVD   R2,DWD
         MVC   LINE+15(8),EDITWD
         ED    LINE+15(8),DWD+4
         MVC   LINE+4(10),0(R6)
         B     ADDNEW
SHOCKER  CVD   R2,DWD
         MVC   LINE+15(8),EDITWD
         ED    LINE+15(8),DWD+4
         B     ADDNEW
NONPRTN  AH    R2,0(0,R4)
         STH   R2,0(0,R4)
         B     ACCUM3
PRINTRTN BC    0,SUBHDG
         MVI   PRINTRTN+1,X'F0'
         LH    R8,=H'70'
         PUT   PRINT,HDR1              SKIP TO CHAN ONE
         PACK  PAGER(2),HDR2+101(2)
         AP    PAGER(2),=P'+1'
         UNPK  HDR2+101(2),PAGER(2)
         OI    HDR2+102,X'F0'
         MVC   HDR2+86(6),VOLSER
         PUT   PRINT,HDR2              MAIN HEADING
         PUT   PRINT,BLINE
         PUT   PRINT,HDR3              ERROR COUNTS
         SH    R8,=H'8'
SUBHDG   BC    0,LINER
         MVI   SUBHDG+1,X'F0'
         UNPK  HDR5+2(4),FEET+1(3)
         OI    HDR5+5,X'F0'
         UNPK  HDR4+9(2),LENGTH(2)
         OI    HDR4+10,X'F0'
         PUT   PRINT,BLINE
         PUT   PRINT,HDR4
         PUT   PRINT,HDR5
         SH    R8,=H'6'
LINER    PUT   PRINT,LINE
         MVI   LINE+1,X'40'
         MVC   LINE+2(131),LINE+1
         BCTR  R8,0
         BR    R10
WRITEND  MVI   READSW,X'FF'
         B     ACCUM1
ENDOV    BC    0,LAST1
         MVI   ENDOV+1,X'F0'
         NI    TAPE+44,X'3F'
         SP    LENGTH,LENGTH
         AP    LENGTH(2),=P'+1'
         SP    FEET,FEET
         LA    R1,WTM
         ST    R1,CHANPROG
         XC    ECB,ECB
         EXCP  IOB
         WAIT  ECB=ECB
         NI    TAPE+44,X'3F'
         LA    R1,REW
         ST    R1,CHANPROG
         XC    ECB,ECB
         EXCP  IOB
         WAIT  ECB=ECB
         LA    R1,FSF
         ST    R1,CHANPROG
         XC    ECB,ECB
         EXCP  IOB
         WAIT  ECB=ECB
         LA    R1,READ
         ST    R1,CHANPROG
         MVI   READSW,X'00'
         MVI   PRINTRTN+1,X'00'
         MVI   SUBHDG+1,X'00'
         B     WRITER
LAST1    MVI   ENDSW,X'FF'
         B     ACCUM1
LAST2    PUT   PRINT,TOTLINE
         LA    R4,TRE
         LA    R5,LINE+29
NEXTOT   LH    R2,0(0,R4)
         MVC   0(8,R5),EDITWD
         CVD   R2,DWD
         ED    0(8,R5),DWD+4
         LA    R4,2(0,R4)
         LA    R5,12(0,R5)
         C     R5,=A(LINE+124)
         BL    NEXTOT
         PUT   PRINT,LINE
         CLOSE (TAPE,,PRINT)
         L     13,CISSAVE+4
         LM    14,12,12(13)
         BR    14
VOLSER   DC    6C'0'
PAGER    DS    CL3
LENGTH   DC    PL2'+1'
NLENGTH  DC    PL2'0'
ERROR    DS    0H
         DC    CL10'CLNR ACTNS'
         DC    CL10'ERASE GAPS'
         DC    CL10'NOISE RECS'
         DC    CL10'PERM WRITE'
         DC    CL10'PERM  READ'
         DC    CL10'START  IOS'
         DC    CL10'TEMP WRITE'
         DC    CL10'TEMP  READ'
HDR1     DC    X'8B'                    SKIP TO CHAN 1
HDR2     DC    X'11'
         DC    30C' '
         DC    C'BY LENGTH TAPE ERROR ANALYSIS FOR VOLUME SERIAL'
         DC    C' NUMBER          PAGE  00'
         DC    30C' '
HDR3     DC    X'09'
         DC    C'NUMBER OF ERRORS        1   5    10   15   20   25'
         DC    C'   30   35   40   45   50   55   60   65   '
         DC    C'70   75   80   85   90   95   100      '
HDR4     DC    X'09'
         DC    C'LENGTH  00       '
         DC    110X'6D'
         DC    5C' '
HDR5     DC    X'09'
         DC    C' 0000 FEET'
         DC    122C' '
LINE     DC    X'09'
         DC    132C' '
BLINE    DC    X'19'
         DC    132C' '
EDITWD   DC    X'4020202020202120'
DWD      DS    D
ENDSW    DC    X'00'
READSW   DC    X'00'
FEET     DC    PL4'0'
TRE      DC    2D'0'
TOTLINE  DC    X'11'
         DC    19C' '
         DC    CL8'TOTALS *'
         DC   CL48'TEMP  READ *TEMP WRITE *START  IOS *PERM  READ *'
         DC   CL46'PERM WRITE *NOISE RECS *ERASE GAPS *CLNR ACTNS'
         DC    11C' '
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,            *
               RECFM=FM,BLKSIZE=133
TAPE     DCB   DDNAME=TAPE,MACRF=(E),DSORG=PS,IOBAD=IOB,DEVD=TA,       *
               RECFM=F
IOB      DS    0D
         DC    X'42000000'
         DC    A(ECB)
         DC    D'0'
CHANPROG DC    A(WRITE)
         DC    A(TAPE)
         DC    F'0'
         DC    H'1',H'0'
ECB      DC    F'0'
WRITE    CCW   X'C3',WRITE,X'40',1
         CCW   X'01',AREA,X'20',6400
WTM      CCW   X'1F',*,X'40',1         WRITE TAPE MARK
         CCW   X'03',*,X'20',1         NOP
REW      CCW   X'07',*,X'20',1         REWIND
FSF      CCW   X'3F',*,X'20',1         FORWARD SP FILE
ERGAP    CCW   X'27',*,X'40',1         BACK SPACE BLOCK
         CCW   X'17',*,X'20',1         ERGAP
READ     CCW   X'02',AREA,X'20',6400
         LTORG
AREA     DC    100CL64'WRITTEN BY TAPE ERROR ANALYSIS PROGRAM'
         END
