         MACRO
&NAME    LEAVE &REGS,&SAVE=,&TEMP=14,&STORE=0,&CODE=
         LCLA  &COUNT,&LO,&HI
         LCLC  &REG
&NAME    DS    0H
         AIF   ('&SAVE' EQ 'DYN').FREE
         L     13,4(0,13)              LOAD ADDRESS OF CALLERS SAVEAREA
         AGO   .CKCODE
.FREE    ANOP
         LR    &TEMP,1                 SAVE REGISTER 1
         LR    1,13                    LOAD ADDRESS OF SAVE AREA
         L     13,4(0,13)              LOAD ADDRESS OF CALLERS SAVEAREA
&COUNT   SETA  72+&STORE
         LA    0,&COUNT                LOAD COUNT OF DYNAMIC AREA
*        FREEMAIN R,LV=(0),A=(1)       FREE DYNAMIC AREA
         FREEMAIN R,LV=(0),A=(1)
         LR    1,&TEMP                 RESTORE REGISTER 1
.CKCODE  ANOP
         AIF   ('&CODE' EQ '').SETZERO
         AIF   ('&CODE'(1,1) EQ '(').LOADREG
         LA    15,&CODE                LOAD RETURN CODE
         AGO   .RESTORE
.LOADREG ANOP
&REG     SETC  '&CODE'(2,K'&CODE-2)
         LR    15,&REG                 LOAD RETURN CODE
         AGO   .RESTORE
.SETZERO ANOP
         SR    15,15                   ZERO RETURN CODE
.RESTORE ANOP
         AIF   ('&REGS' EQ '').RESTALL
         AIF   (N'&REGS NE 1).SETREGS
         L     &REGS,20+4*&REGS.(0,13) RESTORE REGISTER
         AGO   .LOADRET
.SETREGS ANOP
&LO      SETA  &REGS(1)
&HI      SETA  &REGS(2)
         LM    &LO,&HI,20+4*&LO.(13)   RESTORE REGISTERS
         AGO   .LOADRET
.RESTALL ANOP
         LM    2,12,28(13)             RESTORE REGISTERS
.LOADRET ANOP
         L     14,12(0,13)             LOAD RETURN ADDRESS
         BR    14                      RETURN TO CALLER
         MEND
