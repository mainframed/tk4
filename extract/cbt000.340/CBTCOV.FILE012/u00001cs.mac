         TITLE 'SMFSTS : FOR FINDING THE STATUS OF THE SMF DATA SETS'
SMFSTS   CSECT
***********************************************************************
*                                                                     *
*    S   M   F   S   T   S                                            *
*                            - STATUS OF SMF DATA SETS                *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*  TO OBTAIN PRINT OUT OF RELEVANT AREAS OF CORE (EG PRIME DCB )      *
*  MUST SUPPLY , ON EXEC CARD                                         *
*                                       PARM.GO=ALL                   *
*  OTHERWISE THE PGM WILL JUST OUTPUT STATUS MESSAGES TO THE CONSOLE  *
*  GIVING THE STATUS OF THE SMF  DATA SETS SYS1.MANX & SYS1.MANY      *
*                                                                     *
*        PROGRAM ASSUMES THAT BOTH DATA SETS (SYS1.MANX & SYS1.MANY)
*        ARE ON THE SAME DEVICE TYPE.  THEY ARE ALSO ASSUMEDTOBE
*        OF ONLY ONE EXTENT ONLY ,AND TO BE ON DISC  ----
*        HOWEVER THESE LAST TWO ASSUMPTIONS CAN BE OVERCOME BY
*        ADJUSTING THE CODE (LABEL : TOTRACK  , WHEN DATA SET IS
*        CLOSED )  AND CHANGING THE TRKSPCYL CONSTANT ,WHICH IS
*        USED IN CONVERTING FROM CCHH TO TRACKS .
*                                                                     *
***********************************************************************
         BEGIN
         SPACE
***R E G I S T E R      U S A G E                                     *
REG0     EQU   0              MACROS
REG1     EQU   1              MACROS,LINK FOR OUTPUT OF MESSAGES
REG2     EQU   2              CONVERSION ROUTINE
REG3     EQU   3              OUTPUT OF SMF BUFFERS
REG4     EQU   4
REG5     EQU   5
REG6     EQU   6
REG7     EQU   7              PRIME DEB
REG8     EQU   8              PRIME DCB
REG9     EQU   9              SMCA POINTER
REG10    EQU   10             PARAMETER FIELD
REG11    EQU   11
REG12    EQU   12             BASE
         EJECT
***********************************************************************
*  WAS THE PARAMETER  'ALL'  SPECIFIED  (EXEC CARD )                  *
*  POINT REG 10 TO PARM FIELD( FROM REG1) FOR TESTING LATER           *
*                                                                     *
         LR    10,1
         SPACE
***********************************************************************
*  ACCESS THE SMF CONTROL BLOCK   (THE SMCA)                          *
*  TEST FOR ERROR CONDITIONS  - SMF NOT SPECIFIED AT IPL              *
*                             - SMF RECORDS LOST (BOTH DATA SETS FULL,*
*                                           CLOSED OR UNAVAILABLE )   *
*                                                                     *
FINDSMCA L     REG1,16
         L     REG9,196(REG1)           REG9 -> SMCA
SMFATIPL TM    1(REG9),X'C0'            WAS SMF SPECIFIED AT IPL
         BZ    NOTSPEC                  NO
LOSTREC  TM    104(REG9),X'40'          BOTH DATA SETS FULL  ?
         BNO   PRIMARY                  NO , SO NO RECORDS LOST
         SPACE
WASLOST  LA    REG1,MSGLOST             YES, ERROR MSG THEN CONTINUE
         WTO   MF=(E,(1))
         B     PRIMARY
         SPACE
NOTSPEC  LA    REG1,MSGNOSMF            MESSAGE THEN TERMINATE
         WTO   MF=(E,(1))
         B     FINISH
         EJECT
***********************************************************************
*  PRIMARY DATA SET     -FIND IDENTIFIER FROM SMCA                    *
*                       -TEST THAT IT IS OPEN (ERROR IF NOT )         *
*                       -COMPILE STATUS MESSAGE FROM DCB,DEB          *
*                       -MESSAGE TO OPERATOR                          *
*                                                                     *
PRIMARY  MVC   MSGDS+28(1),35(REG9)     PUT IDENTIFIER INTO MSG
         L     REG8,36(REG9)            REG8 -> PRIME DCB
         TM    48(REG8),16              IS DATA SET (PRIME) OPEN
         BO    INUSE                    YES
         SPACE
*PRIMARY DATA SET CLOSED.               NOT OPEN
         MVC   MSGPCLS+49(1),35(REG9)   PUT IDENTIFIER INTO MSG
         LA    REG1,MSGPCLS
         WTO   MF=(E,(1))
         B     FINISH
         SPACE
INUSE    MVC   MSGINUSE+28(1),35(9)
         LA    1,MSGINUSE
         WTO   MF=(E,(1))
         L     7,44(8)
         L     1,32(7)
         TM    18(1),32
         BZ    NOTDISK
         SPACE
***********************************************************************
*  USE DCB AND DEB TO FIND NUMBER OF TRACKS USED, AND TOTAL NUMBER OF *
*  TRACKS (ASSUME ONE EXTENT ONLY ) IN DATA SET . USE RESIDENT ROUTINE*
*  FOR CONVERTING TO TRACKS (CVT +32).                                *
*                                                                     *
         XC    TRACKS,TRACKS
         MVC   TRACKS+6(2),46(REG7)     TOTAL NUMBER TRACKS
         L     REG4,TRACKS+4
         CVD   REG4,TRACKZ
         UNPK  TRACKS,TRACKZ
         OI    TRACKS+7,X'F0'
         MVC   MSGDS+55(3),TRACKS+5     PUT INTO MSG (TOTAL TRKS)
USEDTRKS XC    TRACKS,TRACKS
         MVC   TRACKS,5(REG8)           DCB FDAD
         LA    REG2,TRACKS
         LR    1,7
         L     15,16                    CVT
         L     15,32(15)                CONVERSION ROUTINE IN CVT
         STM   9,13,XSAVE
         CNOP  2,4
         BALR  14,15                    CONVERT TO TTR0
         LM    9,13,8(14)               REG14 POINTS HERE
         B     AFTSAVE
XSAVE    DC    5F'0'
AFTSAVE  SRL   REG0,16                  REG0 CONTAINED TTR0
         LR    1,0
         LA    1,1(1)
         CVD   1,TRACKZ
         UNPK  TRACKS,TRACKZ
         OI    TRACKS+7,240
         MVC   MSGDS+34(3),TRACKS+5
         SPACE
***********************************************************************
*  NOW OUTPUT THE MESSAGE,OF THE STATUS OF THE PRIMARY DATA SET,      *
*                         TO THE OPERATOR AND SYSPRINT                *
*                                                                     *
MSGPRIME LA    REG1,MSGDS
         WTO   MF=(E,(1))               PRIMARY MESSAGE
         EJECT
***********************************************************************
*  ACCESS THE IDENTIFIER OF THE ALTERNATE DATA SET FROM THE SMCA .    *
*  CHECK THAT THERE HAS NOT BEEN A DATA SET SWITCH DURING RUN OF THIS *
*  PGM BY CHECKING THAT THE PRIME AND ALTERNATE ID'S ARE NOT SAME.    *
*  IF HAS BEEN SWITCH THEN PROGRAM MUST RERUN - SO MESSAGE AND THEN   *
*  BRANCH TO LABEL             'PRIMARY' TO REPEAT CALCULATIONS       *
*                                                                     *
         CLC   MSGDS+28(1),51(REG9)          COMMPARE ID'S
         BNE   ALTNATE                       DIFFERENT
SWITCHED LA    REG1,MSGSWIT
         WTO   MF=(E,(1))                    ERROR MESSAGE
         B     PRIMARY                       RESTART
         SPACE
         SPACE
***********************************************************************
*  ALTERNATE DATA SET   -PUT IDENTIFIER INTO MESSAGE AND CAMLIST      *
*                       -MOVE VOL/SER FROM SMCA INTO OBTAIN CAMLIST   *
*                       -OBTAIN THE DSCB OF ALTERNATE DATA SET        *
*                       -COMPILE STATUS MESSAGE FROM DSCB             *
*                       -MESSAGE TO OPERATOR                          *
*                                                                     *
ALTNATE  MVC   MSGDS+28(1),51(REG9)          ID INTO MS
         MVC   DSXORY+8(1),51(REG9)          ID INTO CAMLIST
         MVC   VOLNUM(6),40(REG9)            VOL/SER INTO CAMLIST
         OBTAIN  OBTXORY                OBTAIN DSCB OF ALTNATE DATA SET
         LTR   15,15
         BNZ   ERRORO
         EJECT
***********************************************************************
*  ACCESS DSCB (NOW IN WORKAREA) TO CALCULATE NUMBER OF TRACKS (1ST   *
*  EXTENT ONLY IS CONSIDERED) INTO DATA SET, AND NUMBER OF TRACKS USED*
*                                                                     *
TRKSUSED L     1,16
         L     2,40(1)                  UCB LOOK-UP TABLE ADDRESS
SRCHUCBS LH    3,0(2)
         LTR   3,3
         BC    4,NOTFND
         BC    2,UCBFND
NEXTUCB  LA    2,2(2)
         B     SRCHUCBS
NOTFND   WTO   '*** SMFSTATUS : NO DISK UCB HAS VOL SERIAL OF ALTERNATE$
                DATA SET',ROUTCDE=(2,11)
         B     FINISH
UCBFND   TM    18(3),32
         BZ    NEXTUCB
         CLC   28(6,3),VOLNUM
         BNE   NEXTUCB
*  FIND DEV CHARACTERISTICS (TRACKS PER CYLINDER) FOR SMF DISK.
         L     1,64(1)
         IC    2,19(3)
         LA    3,15
         NR    2,3
         IC    2,0(1,2)
         AR    1,2
*   PICK UP TRACKS PER CYLINDER.
         MVC   TRKSPCYL,2(1)
         LH    REG4,WORKAREA+54         LAST TRK USED
         LTR   REG4,REG4
         BZ    EMPTY                    DONT PUT 1TRK USED IF EMPTY
         LA    REG4,1(REG4)             NO. OF TRACKS USED
EMPTY    CVD   REG4,TRACKZ
         UNPK  TRACKS,TRACKZ
         OI    TRACKS+7,X'F0'
         MVC   MSGDS+34(3),TRACKS+5     PUT INTO MSG (TRACKS USED)
         SPACE
TOTTRACK XC    TRACKS,TRACKS
         MVC   TRACKS+6(2),WORKAREA+67  LAST CC OF 1ST EXTENT
         L     REG4,TRACKS+4
         MH    REG4,TRKSPCYL            CONVERT TO TRACKS.
         LA    REG4,1(REG4)
         MVC   TRACKS+6(2),WORKAREA+69  LAST HH OF 1ST EXTENT
         A     REG4,TRACKS+4            LAST TRACK OF 1ST EXTENT
         MVC   TRACKS+6(2),WORKAREA+63  FIRST CC OF 1ST EXTENT
         L     REG6,TRACKS+4
         MH    REG6,TRKSPCYL            CONVERT TO TRACKS
         MVC   TRACKS+6(2),WORKAREA+65  FIRST HH OF 1ST EXTENT
         A     REG6,TRACKS+4
         SR    REG4,REG6                TOTAL NO. TRKS 1ST EXTENT
         CVD   REG4,TRACKZ
         UNPK  TRACKS,TRACKZ
         OI    TRACKS+7,X'F0'
         MVC   MSGDS+55(3),TRACKS+5     PUT INTO MSG
         SPACE
MSGALT   LA    REG1,MSGDS               ALTERNATE MESSAGE
         WTO   MF=(E,(1))
         B     TESTALL
         SPACE
ERRORO   LA    REG1,MSGOBTER
         WTO   MF=(E,(1))
         B     FINISH
NOTDISK  WTO   '*** SMFSTATUS : PRIMARY DATA SET NOT ON DISK.  NO MORE *
               TO BE SAID',ROUTCDE=(2,11)
         B     FINISH
         TITLE 'SMFSTS : PRINTOUT OF USEFUL AREAS OF CORE FOR SMF '
***********************************************************************
*  STUDY THE INPUT PARAMETER FIELD (POINTED TO BY REG10)              *
*  IF 'ALL' HAS BEEN PASSED THEN REQUIRE PRINTING OF RELEVANT AREAS OF*
*  CORE,  OTHERWISE,THIS IS NOT WANTED , SO TERMINATE                 *
*                                                                     *
TESTALL  L     REG2,0(REG10)            REG2 -> PARAMS
         LH    REG1,0(REG2)
         LTR   REG1,REG1
         BZ    FINISH                   NO INPUT PARAMETERS
*                                        SO TERMINATE JOB
         CLC   2(3,REG2),=C'ALL'        IS PARAMETER  'ALL'
         BE    PRINTIT                  GO ON TO PRINT AS REQUIRED
         SPACE
ERRORP   LA    REG1,MSGERR
         WTO   MF=(E,(1))
         B     FINISH
         SPACE
PRINTIT  OPEN  (PRINT,OUTPUT)
         SPACE
TIMDAT   TIME  DEC                      R0->TIME  ; R1 -> DATE
         ST    REG0,TIMEWORK            TIME
         MVI   TIMEWORK+3,15
         MVO   TIMEWORK(4),TIMEWORK(3)
         UNPK  TIMEWORK,TIMEWORK(4)
         MVC   PAGEHEAD+40(2),TIMEWORK+2     HH
         MVC   PAGEHEAD+43(2),TIMEWORK+4     MM
         MVC   PAGEHEAD+46(2),TIMEWORK+6     SS
         ST    REG1,TIMEWORK            DATE
         OI    TIMEWORK+3,X'0F'
         UNPK  TIMEWORK,TIMEWORK(4)
         MVC   PAGEHEAD+33(2),TIMEWORK+3     YY
         MVC   PAGEHEAD+29(3),TIMEWORK+5     DDD
         EJECT
HEAD1    PUT   PRINT,NP                      NEW PAGE
         PUT   PRINT,ASTERIX                 ***
         MVC   LINE+20(50),PAGEHEAD
         MVI   LINE,X'11'
         PUT   PRINT,LINE                    HEADING - TIME & DATE
         PUT   PRINT,ASTERIX                 ***
         SPACE
         PUT   PRINT,SMCA               TITLE   -SMCA
         LA    REG0,120(REG9)           REG9 -> SMCA
         EJECT
         PRINCORE  (9),,PRINT
         EJECT
PRINTDCB PUT   PRINT,PDCB               TITLE - PRIMARY DCB
         LA    REG0,96(REG8)            REG8 -> DCB
         PRINCORE  (8),,PRINT
         SPACE
PRINDEB  PUT   PRINT,PDEB
         SPACE
         LA    REG0,48(REG7)            REG( -> DEB OF PRIME
         PRINCORE   (7),,PRINT
         EJECT
PRINALT  PUT   PRINT,ADCB               ALTERNATE DCB
         L     REG1,52(REG9)            REG9 -> SMCA
         LA    REG0,96(REG1)
         PRINCORE  ,,PRINT
         SPACE
BUFFS    PUT   PRINT,NP                 NEW PAGE
         PUT   PRINT,SBUFA              FIRST HALF SMF BUFFER
         L     REG1,20(REG9)            REG9 -> SMCA
         L     REG2,12(REG9)            LENGTH HALF BUFFER
         LA    REG3,0(REG2,REG1)        END OF BUFFER
         PRINCORE  ,(3),PRINT
         SPACE
         PUT   PRINT,NP
         PUT   PRINT,SBUFB
         LA    REG0,0(REG2,REG3)        END OF 2ND BUFFER
         PRINCORE  (3),,PRINT
         EJECT
***********************************************************************
CLSFILE  CLOSE (PRINT)                  CLOSE FILE
         SPACE
FINISH   GOBACK                 ********END OF PROGRAM.********
***********************************************************************
         TITLE 'MESSAGES'
MSGINUSE WTO   '*** SMFSTATUS : SYS1.MANA IS CURRENTLY IN USE',MF=L,   *
               ROUTCDE=(2,11)
         SPACE
MSGNOSMF WTO   '*** SMFSTATUS : NO SMF RECORDING SPECIFIED AT IPL ',   *
               MF=L,ROUTCDE=(2,11)
         SPACE
MSGLOST  WTO   '*** SMFSTATUS : SMF RECORDS LOST DUE TO SYS1.MANX && Y *
               FULL OR UNAVAILABLE ',MF=L,ROUTCDE=(2,11)
         SPACE
MSGDS    WTO   '*** SMFSTATUS : SYS1.MAN  HAS     TRKS USED OUT OF     *
               TRKS ',MF=L,ROUTCDE=(2,11)
         SPACE
MSGSWIT  WTO   '*** SMFSTATUS : SMF HAS SWITCHED DATA SETS DURING RUN O*
               F SMFSTS , SO RUN IS BEING REPEATED',MF=L,ROUTCDE=(2,11)
         SPACE
MSGPCLS  WTO   '*** SMFSTATUS : SMF PRIMARY DATA SET ,SYS1.MAN  , IS CL*
               OSED ',MF=L,ROUTCDE=(2,11)
         SPACE
MSGERR   WTO   '*** SMFSTATUS : ERROR IN INPUT PARAMETER .RERUN WITH PA*
               RAMETER''ALL''IF PRINTOUT REQUIRED',MF=L,ROUTCDE=(2,11)
         SPACE
MSGOBTER WTO   '*** SMFSTATUS : ERROR IN ACCESS OF ALTERNATE DATA SET',*
               MF=L,ROUTCDE=(2,11)
         TITLE 'CAMLIST   AND   DECLARATIONS  '
OBTXORY  CAMLST  SEARCH,DSXORY,VOLNUM,WORKAREA           *** OBTAIN ***
DSXORY   DC    CL44'SYS1.MAN '
VOLNUM   DC    CL6'0'
WORKAREA DS    0D
         DS    350C
         SPACE
         SPACE
TIMEWORK DS    D                        WORKAREA
TRACKS   DS    D                        WORKAREA FOR TRACK CALCNS
TRACKZ   DS    D
TRKSPCYL DC    H'20'                    NO. OF TRACKS PER CYLINDER
*                             SET HERE FOR DATA SETS ON 2314
         SPACE
         SPACE
*  PRINT  OUT  TITLES  ETC
         SPACE
LINE     DC    CL133' '
ASTERIX  DC    X'11',132C'*'
PAGEHEAD DC    CL50'S M F   S T A T U S      ON          AT           '
NP       DC    X'8B'                    NEW PAGE
SMCA     DC    X'11',CL132'CONTENTS OF SMCA '
PDCB     DC    X'11',CL132'CONTENTS OF PRIMARY DCB '
PDEB     DC    X'11',CL132'CONTENTS OF PRIMARY DEB '
ADCB     DC    X'11',CL132'CONTENTS OF ALTERNATE DCB '
SBUFA    DC    X'11',CL132'CONTENTS OF SMF BUFFER  - FIRST HALF '
SBUFB    DC    X'11',CL132'CONTENTS OF SMF BUFFER  - SECOND HALF '
         TITLE  ' D C B   FOR   P R I N T    F I L E  '
PRINT    DCB   DDNAME=SYSPRINT,                                        *
               DSORG=PS,                                               *
               MACRF=(PM),                                             *
               RECFM=FBM,                                              *
               LRECL=133,                                              *
               BLKSIZE=1995
               END
