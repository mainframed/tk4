         MACRO
         FMTENT &TITLE,&LENGTH,&CONVERT=YES,&NL=NO,&PRINT=YES
         LCLA  &WORKA              USED TO HOLD LENGTH PARAMETER
         LCLA  &WORKB              USED TO HOLD LENGTH OF &&TITLE
         LCLA  &WORKC              USED TO CALCULATE FLAG VALUE
.*
.*       FIRST ESTABLISH THE FLAG VALUE AND SET IT
.*
         AIF   ('&CONVERT' NE 'YES').X2 NO CONVERSION OF DATA
&WORKC   SETA  &WORKC+128
.X2      AIF   (T'&TITLE NE 'O').X3  NO LABEL FIELD
&WORKC   SETA  &WORKC+64
.X3      AIF   ('&NL' NE 'YES').X4  NO NEW LINE NEEDED
&WORKC   SETA  &WORKC+32
.X4      AIF   ('&PRINT' NE 'NO').X5  THIS FIELD IS NON-PRINT
&WORKC   SETA  &WORKC+16
.X5      ANOP
         DC    AL1(&WORKC)         PROCESSING FLAGS
.*
.*
.*       NEXT  TEST THE LENGTH AND, IF OMMITTED, SUPPLY A DEFAULT.
.*
         AIF   (T'&LENGTH NE 'O').NUMTEST
&WORKA   SETA  4                   SET DEFAULT LENGTH OF FOUR
         AGO   .DSCTTST            TEST FOR DSECT WANTED
.NUMTEST ANOP  TEST THE VALIDITY OF OPERAND
         AIF   (T'&LENGTH NE 'N').ERROR1 INVALID INPUT PARAMETER
&WORKA   SETA  &LENGTH
         AIF   (&WORKA GT 24 OR &WORKA LT 0).ERROR1 INVALID PARAMETER
         AGO   .DSCTTST            TEST FOR DSECT WANTED
.*
.*
.DSCTTST ANOP
.*
.*       TEST TO SEE IF A DSECT IS WANTED.
.*
         AIF   ('&TITLE' EQ '$DSECT').DSECT YES. GO DO IT
.*
.*DEFINE A NORMAL TABLE ENTRY
.*
         AIF   (T'&TITLE NE 'O').TSTQUT
         DC    AL1(0)              NO LABEL FOR THIS FIELD
         DC    CL1' '              NUL VALUE FOR LABEL
         AGO   .FLDLEN             GO FORM FIELD LENGTH
.TSTQUT  ANOP
         AIF   ('&TITLE'(1,1) EQ '''').INQUOTE    YES. DIFFERENT INST
&WORKB   SETA  K'&TITLE
         DC    AL1(&WORKB-1)       LENGTH OF TEXT
         DC    C'&TITLE'
         AGO   .FLDLEN             GO FORM FIELD LENGTH
.INQUOTE ANOP
&WORKB   SETA  K'&TITLE-2
         DC    AL1(&WORKB-1)       LENGTH OF TEXT
         DC    C&TITLE
.FLDLEN  ANOP
         DC    AL1(&WORKA-1)       EXECUTABLE LENGTH OF FIELD
.*
.*
         MEXIT
.DSECT   ANOP
.*
.*       FORM A DSECT OF THE TABLE ENTRY
.*
FMTTABLE DSECT
FMTFLAG  DS    X                   PROCESSING OPTION FLAGS
FMTCNVRT EQU   X'80'               DATA IS TO BE CONVERTED
FMTNOLAB EQU   X'40'               DATA IS TO HAVE NO LABEL
FMTNLINE EQU   X'20'               THIS FIELD TO BEGIN A NEW LINE
FMTNOPRT EQU   X'10'               THIS FIELD IS NOT TO BE PRINTED
FMTLBLEN DS    AL1                 EXECUTABLE LENGTH OF FIELD TITLE
FMTTITLE DS    CL1                 TITLE FOR THE OUTPUT FIELD
FMTFDLEN DS    AL1                 LENGTH OF THIS FIELD
FMTENLEN EQU   *-FMTTABLE          LENGTH OF A TABLE ENTRY (BASIC)
         MEXIT
.*
.*       ERROR MESSAGES
.*
ERROR1   MNOTE 8,'INVALID LENGTH PARAMETER - &LENGTH'
         MEND
