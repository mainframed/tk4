PUNCHCAT CSECT
         SPACE 3
*  THE PUNCHCAT PROGRAM WILL CREATE A DECK OF CARDS FOR IEHPROGM TO
*  REMOVE EVERY ENTRY FROM A CATALOGUE AND THEN RECREATE IT (OPTIONALLY
*  SWOPPING ALL OCCURRENCES OF UP TO 5 OLD VOLUME SERIALS FOR NEW
*  VOLUME SERIALS).  YUO MAY MAKE OTHER CHANGES MANUALLY BEFORE
*  SUBMITTING THE CARDS TO IEHPROGM.
         SPACE
*  IT WAS WRITTEN TO CORRECT THE CATALOGUE AFTER A DISK TO DISK DUMP
*  OF ONE SYSRES VOLUME TO A NEW PACK WITH A DIFFERENT SERIAL.  THE OLD
*  SYSTEM CONSISTED OF 2 2314 PACKS (080319,080320), AND THIS WAS
*  COPIED TO VOLUMES 080405,080407, SO ALL SYSTEM DATA SETS HAD TO BE
*  UNCATALOGUED AND RECATALOGUED WITH THE NEW SERIALS.
         SPACE
*  FOR CONVENIENCE OF WRITING THE INPUT IS A SEQUENTIAL DATA SET, I.E.
*  A LIST OF THE CATALOGUE PRODUCED BY THE RTPRINCA PROGRAM.  AN
*  APPROPRIATE PAIR OF IEHPROGM ENTRIES (E.G. UNCATLG/CATLG, DLTX/BLDX,
*  DLTX/BLDG, RELEASE/CONNECT, DLTA/BLDA) IS CREATED FROM EACH INPUT
*  ENTRY, AND THE PROGRAM LINKS TO TSGLSORT (TO TAKE ADVANTAGE OF ITS
*  AUTOMATIC CORESIZE PARAMETER GENERATION FOR SORT) BEFORE PUNCHING
*  AND LISTING THE CARDS IN THE CORRECT SEQUENCE FOR IEHPROGM.
*  THE CARDS ARE NUMBERED AND YOU CAN EASILY ALTER OR REMOVE ANY CARDS
*  BY REFERENCE TO THE IDENTICALLY NUMBERED LISTING WITHOUT HAVING TO
*  INTERPRET THE CARDS.
         SPACE
*  THE PROGRAM REQUIRES A PARM LIST IN THE FORM .....
*  'CVOL=TYPE=SERIAL,OLDVOL/NEWVOL,OLDVOL/NEWVOL' TO GIVE IT THE CVOL
PARAMETER TO BE INCLUDED IN EACH CARD, AND TO CREATE THE 'SWOP LIST' OF
*  UP TO 5 PAIRS OF VOL SERIALS.   EXAMPLE.......
*  PARM='CVOL=2314=080405,080319/080405,080320/080407'
*  NOTE THAT THE MINIMUM PARM IS 'CVOL=TYPE=SERIAL'.
         SPACE
*  WARNING.  IF THE NUMBER OF VOL SERIALS CATALOGUED FOR ANY ONE DSNAME
*  WILL NOT FIT ON ONE LINE OF RTPRINCA OUTPUT (I.E. EXCEEDS 6 FOR
*  DISK VOLUMES, LESS FOR TAPE, OR IF DIFFERENT VOL TYPES ARE CATLGD)
*  THIS PROGRAM WILL NOT GIVE CORRECT OUTPUT - IT WILL INSTEAD PLACE A
*  MESSAGE 'TOO MANY VOLUMES - DO IT YOURSELF' IN THE OUTPUT CARD.
         SPACE  3
*   EXAMPLE OF JCL...........
*   //A        EXEC PGM=RTPRINCA
*   //* CREATE LIST OF CATALOGUE AS DISK SEQUENTIAL DATA SET
*   //SYSCAT   DD   DSN=SYSCTLG,DISP=SHR,VOL=REF=SYS1.SVCLIB
*   //SYSPRINT DD   DSN=CATPRINT,DISP=(,PASS),UNIT=SYSDA,
*   //              DCB=BLKSIZE=1463,SPACE=(CYL,(2,1))
*   //B        EXEC PGM=IEBGENER
*   //* PRINT LIST OF CATALOGUE FOR CHECK
*   //SYSPRINT DD   SYSOUT=A
*   //SYSUT1   DD   DSN=CATPRINT,DISP=(SHR,PASS),
*   //              DCB=(RECFM=FB,LRECL=133,BLKSIZE=1463)
*   //SYSUT2   DD   SYSOUT=A,DCB=(RECFM=FBM,LRECL=133,BLKSIZE=1463)
*   //SYSIN    DD   DUMMY
*   //C        EXEC PGM=PUNCHCAT
*   //* EXECUTE PUNCHCAT, WHICH LINKS TO TSGLSORT
*   //STEPLIB  DD   DSN=TSG.SORTLIB,DISP=SHR
*   //SYSOUT   DD   SYSOUT=A
*   //SORTLIB  DD   DSN=SYS1.SORTLIB,DISP=SHR
*   //SORTIN   DD   UNIT=SYSDA,SPACE=(CYL,(2,1)),
*   //              DCB=(RECFM=FB,LRECL=80,BLKSIZE=800)
*   //SORTOUT  DD   UNIT=SYSDA,SPACE=(CYL,(2,1)),
*   //              DCB=(RECFM=FB,LRECL=80,BLKSIZE=800)
*   //SORTWK01 DD   SPACE=(CYL,2,,CONTIG),UNIT=SYSDA
*   //SORTWK02 DD   SPACE=(CYL,2,,CONTIG),UNIT=SYSDA
*   //SORTWK03 DD   SPACE=(CYL,2,,CONTIG),UNIT=SYSDA
*   //SYSIN    DD   *
*     SORT  FIELDS=(76,5,BI,D),SIZE=E500
*     MODS  E15=(E15,300,STEPLIB,N),E35=(E35,300,STEPLIB,N)
*     END
*   /*
*   //SYSCATP  DD   DSN=CATPRINT,DISP=(SHR,PASS)
*   //SYSPRINT DD   SYSOUT=A
*   //SYSPUNCH DD   UNIT=00D
*   //
         SPACE  3
         BEGIN
         L     1,0(1)
         LH    2,0(1)
         LA    3,16
         CR    2,3
         BL    BADPARM
         MVC   CVOL,2(1)
         MVI   CVOL+16,C','
         SR    2,3
         BZ    ENDPARM
         CLI   18(1),C','
         BNE   BADPARM
         BCTR  2,0
         LA    1,19(1)
         LA    3,14
         LA    4,SWOPLIST
         LA    5,5
VOLSWOLP CLI   6(1),C'/'
         BNE   BADPARM
         MVC   0(6,4),0(1)
         MVC   6(6,4),7(1)
         SR    2,3
         BNP   ENDPARM
         CLI   13(1),C','
         BNE   BADPARM
         LA    1,14(1)
         LA    4,12(4)
         BCT   5,VOLSWOLP
         SPACE  2
BADPARM  WTO   'UNACCEPTABLE PARM FOR PUNCHCAT',ROUTCDE=(2,11)
         B     GOBACK
         SPACE  2
ENDPARM  OPEN  (CATLIST,,SORTIN,OUTPUT)
         USING CATLINE,9
         SPACE  2
GETA     GET   CATLIST
         LR    9,1
         CLI   MCODE,9
         BNE   GETA
         CLC   ENDMARK,LNAME
         BE    ENDCAT
         CLI   LNAME,64
         BE    DIY
         SPACE  2
         MVC   GAP,SPACES
         MVI   SUFF,1
         AP    SEQ,ONE
         MVC   SEQB,TYPE
         MVC   CONTN,SPACES
         MVI   SUFF,0
         CLC   LTYPE,SPACES
         BE    DSNAME
         CLI   LTYPE,C'C'
         BE    CVOLPTR
         CLI   LTYPE,C'I'
         BE    INDEXPTR
         CLI   LTYPE,C'G'
         BE    GDG
         SPACE  2
ALIAS    MVC   VERB,DLTA
         MVI   SEQB,8
         PUT   SORTIN,CARDA
         MVC   VERB,BLDA
         MVI   SEQB,2
         MVC   GAP(6),INDEX
         LA    6,LVTYPE
         LA    7,GAP+6
ALMOV    MVC   0(1,7),0(6)
         CLI   0(7),64
         BE    ALCOM
         LA    6,1(6)
         LA    7,1(7)
         B     ALMOV
ALCOM    MVI   0(7),C','
         PUT   SORTIN,CARDA
         MVC   CONTN(6),ALIS
         LA    6,LNAME
         LA    7,CONTN+6
         BAL   8,ALMOVE
         B     PUTAL
         SPACE  2
ALMOVE   MVC   0(1,7),0(6)
         CLI   0(7),64
         BE    ALMOVED
         LA    6,1(6)
         LA    7,1(7)
         B     ALMOVE
ALMOVED  BCTR  7,0
         CLI   0(7),C'.'
         BCR   7,8
         MVI   0(7),64
         BR    8
         SPACE  2
PUTAL    MVI   TYPE,8
         PUT   SORTIN,CARD
         MVI   TYPE,2
         PUT   SORTIN,CARD
         B     GETA
         SPACE  2
DSNAME   MVC   VERB,UNCATLG
         MVI   SEQB,9
         PUT   SORTIN,CARDA
         MVC   VERB,CATLG
         MVI   SEQB,1
         MVC   GAP(4),VOLIS
         LA    6,LVTYPE
         LA    7,GAP+4
VTM      MVC   0(1,7),0(6)
         LA    6,1(6)
         LA    7,1(7)
         CLI   0(6),64
         BNE   VTM
         MVI   0(7),C'='
         MVI   1(7),C'('
         LA    7,2(7)
         LA    8,5
         LA    6,LVS
         CLI   LVTYPE+1,C'4'
         BE    TAPEVOLS
         LA    3,SWOPLIST
VSWOPLP  CLI   0(3),0
         BE    NOSWOP
         CLC   0(6,3),0(6)
         BE    SWOP
         LA    3,12(3)
         B     VSWOPLP
SWOP     MVC   0(6,6),6(3)
NOSWOP   MVC   0(6,7),0(6)
         LA    6,7(6)
         CLI   0(6),64
         BE    ENDDVOLS
         BCT   8,*+14
         MVC   7(9,7),MV
         B     ENDDPUT
         MVI   6(7),C','
         LA    7,7(7)
         B     VSWOPLP-4
ENDDVOLS MVI   6(7),C')'
         MVI   7(7),C','
         SPACE  2
ENDDPUT  PUT   SORTIN,CARDA
         MVC   CONTN(7),DSN
         MVC   CONTN+7(44),LNAME
         MVI   TYPE,9
         PUT   SORTIN,CARD
         MVI   TYPE,1
         PUT   SORTIN,CARD
DSNBR    BC    0,GDGG
         B     GETA
         SPACE  2
TAPEVOLS LA    8,3
         MVC   0(6,7),0(6)
         MVI   6(7),C','
         MVC   7(2,7),FS1
         CLI   6(6),C'('
         BNE   *+14
         MVC   7(2,7),7(6)
         LA    6,4(6)
         LA    6,7(6)
         MVI   9(7),C','
         LA    7,10(7)
         CLI   0(6),64
         BE    ENDTVOLS
         BCT   8,TAPEVOLS+4
         MVC   0(9,7),MV
         B     ENDDPUT
         SPACE  2
ENDTVOLS BCTR  7,0
         MVI   0(7),C')'
         MVI   1(7),C','
         B     ENDDPUT
         SPACE 2
CVOLPTR  MVC   VERB,RELEASE
         MVI   SEQB,6
         PUT   SORTIN,CARDA
         MVC   VERB,CONNECT
         MVI   SEQB,5
         MVC   GAP(4),VOLIS
         MVC   GAP+4(4),LVTYPE
         MVI   GAP+8,C'='
         MVC   GAP+9(6),LVS
         MVI   GAP+15,C','
         PUT   SORTIN,CARDA
         MVC   CONTN(6),INDEX
         MVC   CONTN+6(44),LNAME
         MVI   TYPE,6
         PUT   SORTIN,CARD
         MVI   TYPE,5
         PUT   SORTIN,CARD
         B     GETA
         SPACE  2
INDEXPTR MVC   VERB,DLTX
         MVI   SEQB,7
         PUT   SORTIN,CARDA
         MVC   VERB,BLDX
         MVI   SEQB,4
         PUT   SORTIN,CARDA
         MVC   TYPES,INDT
         SPACE  2
INDCON   MVC   CONTN(6),INDEX
         LA    7,CONTN+6
         LA    6,LNAME
MVIND    MVC   0(1,7),0(6)
         LA    7,1(7)
         LA    6,1(6)
         CLI   0(6),64
         BNE   MVIND
         BCTR  6,0
         BCTR  7,0
         CLI   0(6),C'.'
         BNE   *+8
         MVI   0(7),64
         MVC   TYPE,TYPES
         PUT   SORTIN,CARD
         MVC   TYPE,TYPES+1
         PUT   SORTIN,CARD
         B     GETA
         SPACE  2
GDG      CLC   LVS,SPACES
         BE    GDGG
         OI    DSNBR+1,240
         B     DSNAME
GDGG     NI    DSNBR+1,15
         MVC   GAP,SPACES
         MVC   CONTN,SPACES
         LA    1,LNAME+43
CLIC     CLI   0(1),64
         BNE   *+8
         BCT   1,CLIC
         CLI   0(1),C'.'
         BE    ELIMG
         LA    15,8
         SR    1,15
         CLC   0(2,1),DOTG
         BNE   *+10
ELIMG    MVC   0(9,1),SPACES
         MVC   VERB,DLTX
         MVI   SEQB,7
         PUT   SORTIN,CARDA
         MVC   VERB,BLDG
         MVI   SEQB,3
         MVC   GAP(19),ENTDEL
         MVC   GAP+8(3),LTYPE+4
         OC    GAP+8(3),ZEROS
         CLI   LTYPE+8,C'D'
         BE    *+10
         MVC   GAP+12(7),EMPTY
         PUT   SORTIN,CARDA
         MVC   TYPES,BDGT
         B     INDCON
         SPACE  2
DIY      MVC   GAP(29),TMVMSG
         PUT   SORTIN,CARDA
         B     GETA
         SPACE  2
ENDCAT   CLOSE (CATLIST,,SORTIN)
         SPACE  2
         LINK  EP=TSGLSORT
         SPACE  2
         OPEN  (SORTOUT,,LIST,OUTPUT,PUNCH,OUTPUT)
         SPACE  2
GETB     GET   SORTOUT,CARD
         MVC   CARD+74(6),EDM
         AP    OUTCNT,ONE
         ED    CARD+74(6),OUTCNT
         PUT   PUNCH,CARD
         SP    LCNT,ONE
         BP    PLINE
         PUT   LIST,NP
         ZAP   LCNT,MAXCNT
PLINE    PUT   LIST,LINE
         B     GETB
         SPACE  2
EOCARDS  CLOSE (SORTOUT,,LIST,,PUNCH)
GOBACK   GOBACK
         EJECT
CATLIST  DCB   DDNAME=SYSCATP,DSORG=PS,RECFM=FB,LRECL=133,MACRF=(GL),  *
               EODAD=ENDCAT
SORTIN   DCB   DDNAME=SORTIN,DSORG=PS,RECFM=FB,LRECL=80,BLKSIZE=800,   *
               MACRF=(PM)
SORTOUT  DCB   DDNAME=SORTOUT,DSORG=PS,RECFM=FB,LRECL=80,BLKSIZE=800,  *
               EODAD=EOCARDS,MACRF=(GM)
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBM,LRECL=81,BLKSIZE=810,*
               MACRF=(PM)
PUNCH    DCB   DDNAME=SYSPUNCH,DSORG=PS,RECFM=FB,LRECL=80,BLKSIZE=800, *
               MACRF=(PM)
         EJECT
FS1      DC    C'01'
MV       DC    C'MORE VOLS'
ENTDEL   DC    C'ENTRIES=000,DELETE,'
ZEROS    DC    C'000'
EMPTY    DC    C'EMPTY, '
TYPES    DC    XL2'00'
INDT     DC    X'0704'
BDGT     DC    X'0703'
DOTG     DC    C'.G'
TMVMSG   DC    C'TOO MANY VOLS. DO IT YOURSELF'
EDM      DC    X'402120202020'
OUTCNT   DC    PL3'0'
ONE      DC    P'1'
LCNT     DC    PL2'0'
MAXCNT   DC    P'58'
NP       DC    X'8B'
SWOPLIST DC    5XL12'00',X'00'
LINE     DC    X'09'
CARD     DS    0CL80
         DC    CL15' '
CONTN    DC    CL60' '
TYPE     DS    C
SEQ      DC    PL3'0'
SUFF     DS    C
SPACES   DC    CL60' '
ENDMARK  DC    C'NUMBER OF BLOCKS'
CARDA    DS    0CL80
         DC    C' '
VERB     DS    CL8
CVOL     DS    CL17
GAP      DS    CL45
         DC    CL4'*'
SEQB     DS    CL5
DLTA     DC    CL8'DLTA'
BLDA     DC    CL8'BLDA'
UNCATLG  DC    CL8'UNCATLG'
CATLG    DC    CL8'CATLG'
CONNECT  DC    CL8'CONNECT'
RELEASE  DC    CL8'RELEASE'
BLDX     DC    CL8'BLDX'
DLTX     DC    CL8'DLTX'
BLDG     DC    CL8'BLDG'
ALIS     DC    C'ALIAS='
VOLIS    DC    C'VOL='
DSN      DC    C'DSNAME='
INDEX    DC    C'INDEX='
         EJECT
CATLINE  DSECT
MCODE    DS    C,CL4
LNAME    DS    CL44,CL2
LTYPE    DS    CL10,CL2
LVTYPE   DS    CL6,CL2
LVS      DS    CL62
         END
