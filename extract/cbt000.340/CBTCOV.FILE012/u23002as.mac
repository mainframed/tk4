   TITLE 'CAS038 - ADD/DELETE DUMMY VSAM KSDS RECORDS'
*
*  C A S 0 3 8  - B A L
*
*  THIS PROGRAM WILL ADD A DUMMY RECORD TO ANY VSAM KSDS, AND CAN
*  ALSO BE USED TO SUBSEQUENTLY DELETE THAT RECORD.
*
*  THIS PROGRAM IS USEFUL FOR ALLOWING SYSTEMS SUCH AS C.I.C.S TO
*  OPEN A VSAM KSDS THAT HAS NOT BEEN LOADED. THE FILE CAN THEN
*  BE CREATED ONLINE
*
*  TO EXECUTE:-  //CAS038 EXEC PGM=CAS038,PARM=X
*                //VSAMF  DD   DSN=USER.VSAM.KSDS,DISP=SHR
*
*                WHERE THE PARM VALUE CAN BE:
*                A - ADD A DUMMY RECORD
*                D - DELETE THE DUMMY RECORD, PREVIOUSLY CREATED BY
*                    THE ADD FUNCTION
*
*                USER ABEND CODES:
*                4 - INVALID PARAMETER
*                8 - INTERNAL PROGRAM ABEND
*
         EJECT
CAS038   PVGCSECT 12
*
         L     2,0(1)                  ADDR OF PARM AREA
         LA    2,2(2)                  SKIP OVER 2 BYTE LENGTH
         CLI   0(2),C'A'               ADD FUNCTION ?
         BE    A010
         CLI   0(2),C'D'               DELETE FUNCTION ?
         BE    D010
         ABEND 4,DUMP,STEP
*
* PROCESS ADD FUNCTION
*
A010     EQU   *
         GENCB BLK=ACB,AM=VSAM,DDNAME=VSAMF,LENGTH=96,WAREA=GENACB,    X
               MACRF=(KEY,SEQ,OUT,RST)
         LTR   15,15
         BZ    A015
         MVC   ABCODE,=C'AB05'
         B     ABEND
*
A015     EQU   *
         OPEN  GENACB
         LTR   15,15
         BZ    A020
         MVC   ABCODE,=C'AB10'
         B     ABEND
*
A020     EQU   *
         SHOWCB ACB=GENACB,AREA=SHOW,LENGTH=4,FIELDS=(LRECL)
         LTR   15,15
         BZ    A030
         MVC   ABCODE,=C'AB15'
         B     ABEND
*
A030     EQU   *
         GETMAIN R,LV=20480
         LTR   15,15
         BZ    A040
         MVC   ABCODE,=C'AB20'
         B     ABEND
*
A040     EQU   *
         ST    1,STORADDR              ADDR OF GETMAIN AREA
         LR    8,1                     SETUP ADDR FOR MVCL
         L     9,=F'20480'             LENGTH OF MVCL TARGET
         LA    2,0                     REG 2 NOT REQD ON AN INIT
         L     3,FILL                  FILL CHAR
         MVCL  8,2                     INIT RECORD AREA
*
A050     EQU   *
         L     5,STORADDR
         L     6,LRECL
         GENCB BLK=RPL,ACB=GENACB,AM=VSAM,AREA=(5),AREALEN=20480,      X
               RECLEN=(6),LENGTH=256,WAREA=GENRPL,                     X
               OPTCD=(KEY,SEQ,MVE)
         LTR   15,15
         BZ    A060
         MVC   ABCODE,=C'AB25'
         B     ABEND
*
A060     EQU   *
         PUT   RPL=GENRPL
         LTR   15,15
         BZ    E010
         MVC   ABCODE,=C'AB30'
         B     ABEND
*                                                                   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                   *
D010     EQU   *
         GENCB BLK=ACB,AM=VSAM,DDNAME=VSAMF,LENGTH=96,WAREA=GENACB,    X
               MACRF=(KEY,DIR,OUT)
         LTR   15,15
         BZ    D015
         MVC   ABCODE,=C'AB50'
         B     ABEND
*
D015     EQU   *
         OPEN  GENACB
         LTR   15,15
         BZ    D020
         MVC   ABCODE,=C'AB55'
         B     ABEND
*
D020     EQU   *
         SHOWCB ACB=GENACB,AREA=SHOW,LENGTH=4,FIELDS=(LRECL)
         LTR   15,15
         BZ    D030
         MVC   ABCODE,=C'AB60'
         B     ABEND
*
D030     EQU   *
         GETMAIN R,LV=20480
         LTR   15,15
         BZ    D040
         MVC   ABCODE,=C'AB65'
         B     ABEND
*
D040     EQU   *
         ST    1,STORADDR              ADDR OF GETMAIN AREA
         LR    8,1                     SETUP ADDR FOR MVCL
         L     9,=F'20480'             LENGTH OF MVCL TARGET
         LA    2,0                     REG 2 NOT REQD ON AN INIT
         L     3,FILL                  FILL CHAR
         MVCL  8,2                     INIT RECORD AREA
*
D050     EQU   *
         L     5,STORADDR
         L     6,LRECL
         GENCB BLK=RPL,ACB=GENACB,AM=VSAM,AREA=(5),AREALEN=20480,      X
               RECLEN=(6),LENGTH=256,WAREA=GENRPL,ARG=(5),             X
               OPTCD=(KEY,DIR,UPD,MVE)
         LTR   15,15
         BZ    D060
         MVC   ABCODE,=C'AB70'
         B     ABEND
*
D060     EQU   *
         GET   RPL=GENRPL
         LTR   15,15
         BZ    D070
         MVC   ABCODE,=C'AB75'
         B     ABEND
*
D070     EQU   *
         ERASE RPL=GENRPL
         LTR   15,15
         BZ    E010
         MVC   ABCODE,=C'AB75'
         B     ABEND
*
E010     EQU   *
         L     13,4(13)
         RETURN (14,12),RC=0
*
ABEND    EQU   *
         ABEND 8,DUMP,STEP
*
         DS    0F
FILL     DC    XL4'FF000000'           FILL CHAR FOR RECORD
SHOW     DS    0F
LRECL    DS    CL4
STORADDR DS    F
ABCODE   DS    CL4
GENACB   DS    CL96
GENRPL   DS    CL96
         END
