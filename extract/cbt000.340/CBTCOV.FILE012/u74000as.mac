ZZCDLIB  CSECT
         TITLE 'ZZCDLIB - COMPRESS CARD IMAGES INTO LIBRARY FORMAT'
         PRINT NOGEN
*
* THIS PROGRAM COMPRESSES PROGRAM SOURCE CODE FROM CARD IMAGES INTO A
* MORE COMPACT FORMAT BY ABBREVIATING BLANK STRINGS AND REMOVING
* SEQUENCE NUMBERS. THE OUTPUT IS WRITTEN TO A PDS. ZZLIBCD IS USED TO
* READ THE PDS AND OUTPUT CARD IMAGES.
*
         EQUATER
         SAVE  (14,12)
         BALR  R11,0
         USING *,R11
         LA    R12,SAVE
         ST    R13,4(R12)
         ST    R12,8(R13)
         LR    R13,R12
         OPEN  (CARDIN,(INPUT),LIBOUT,(OUTPUT))
         LH    R6,BUFLEN
         GET   CARDIN                   READ CONTROL CARD
         LR    R3,R1
         CLC   0(6,R1),=C'000000'       Q. COBOL PROGRAM
         BNE   L002
         MVI   COBSW,X'FF'
         MVC   CARDLEN,=H'74'           INCREASE CARD LENGTH
         MVC   PROGNAME,12(R1)
         LA    R3,6(R3)
         B     L006
L002     ZZLBL
         CLC   0(7,R1),=C'NAME = '      SCAN FOR MODULE NAME
         BE    L004
         LA    R1,1(R1)
         B     L002
L004     ZZLBL
         MVC   PROGNAME,7(R1)           STORE MODULE NAME
L006     ZZLBL
         CLC   0(17,R1),=C'SOURCE VERSION = '
         BE    L008
         LA    R1,1(R1)
         B     L006
L008     ZZLBL
         PACK SSI(5),17(9,R1)           SSI TO PDS DIRECTORY DATA
         XR    R2,R2
         XR    R5,R5                    ZERO BLANK COUNT
         GETBUF LIBOUT,(R4)
         ST    R4,STARTBUF
         AH    R4,BUFLEN                ADD BUFFER LENGTH
         ST    R4,ENDBUF
         L     R4,STARTBUF
         CLI   COBSW,X'FF'              Q. COBOL PROGRAM
         BNE   L010
         MVC   0(2,R4),=C'00'           INDICATE COBOL IN OUTPUT
         LA    R4,2(R4)
L010     ZZLBL
         CH    R2,CARDLEN               Q. END OF CARD
         BNE   L020
         GET   CARDIN                   READ NEXT CARD IMAGE
         XR    R2,R2                    ZERO CARD SCAN COUNT
         LR    R3,R1                    SET CARD INDEX
         CLI   COBSW,X'FF'              Q. COBOL PROGRAM
         BNE   L020
         LA    R3,6(R3)                 BYPASS SEQUENCE NO.S
L020     ZZLBL
         CLC   0(3,R3),=C'   '          Q. 3 CONSECUTIVE BLANKS
         BE    L030
         MVC   0(1,R4),0(R3)            MOVE CHARACTER FROM CARD IMAGE *
                                          TO OUTPUT
         LA    R4,1(R4)                 INDEX OUTPUT BUFFER
         LA    R3,1(R3)                 INDEX INPUT CARD IMAGE
         LA    R2,1(R2)                 CARD SCAN COUNT
         C     R4,ENDBUF                Q. OUTPUT BUFFER FULL
         BNE   L010
         BAL   R10,OUTPUT
         B     L010
         SPACE 3
L030     ZZLBL
         CLI   0(R3),C' '               Q. NEXT INPUT CHARACTER BLANK
         BNE   L040
         LA    R5,1(R5)                 ADD TO BLANK COUNT
         LA    R3,1(R3)                 INDEX CARD
         LA    R2,1(R2)                 CARD SCAN COUNT
         CH    R5,=H'255'               Q. LONGEST POSSIBLE STRING
         BE    L040
         CH    R2,CARDLEN               Q. END OF CARD IMAGE
         BNE   L030
         GET   CARDIN
         XR    R2,R2                    ZERO CARD SCAN COUNT
         LR    R3,R1                    SET CARD INDEX
         CLI   COBSW,X'FF'              Q. COBOL PROGRAM
         BNE   L030
         LA    R3,6(R3)                 BYPASS SEQUENCE NO.S
         B     L030
         SPACE 3
L040     ZZLBL
         MVI   0(R4),X'DF'              BLANK STRING DELIMITER
         LA    R4,1(R4)                 INDEX OUTPUT BUFFER
         C     R4,ENDBUF                Q. OUTPUT BUFFER FULL
         BNE   L050
         BAL   R10,OUTPUT
L050     ZZLBL
         STC   R5,0(R4)                 BLANK COUNT TO OUTPUT BUFFER
         LA    R4,1(R4)                 INDEX OUTPUT BUFFER
         XR    R5,R5                    ZERO BLANK COUNT
         C     R4,ENDBUF
         BNE   L010
         BAL   R10,OUTPUT
         B     L010
         SPACE 5
EOF      ZZLBL
         LTR   R5,R5                    Q. IS BLANK STRING OUTSTANDING
         BZ    E020
         MVI   0(R4),X'DF'              BLANK STRING DELIMITER
         LA    R4,1(R4)                 INDEX OUTPUT BUFFER
         C     R4,ENDBUF                Q. OUTPUT BUFFER FULL
         BNE   E010
         BAL   R10,OUTPUT
E010     ZZLBL
         STC   R5,0(R4)                 BLANK COUNT TO OUTPUT BUFFER
         LA    R4,1(R4)                 INDEX OUTPUT BUFFER
         SPACE
E020     ZZLBL
         LR    R6,R4
         S     R6,STARTBUF              LENGTH OF SHORT BLOCK
         LTR   R6,R6                    Q. NEW BUFFER STARTED
         BZ    E040
E030     ZZLBL
         BAL   R10,OUTPUT
E040     ZZLBL
         FREEBUF LIBOUT,(R4)
         STOW    LIBOUT,PROGNAME,R
         CLOSE (CARDIN,,LIBOUT)
         L     R13,4(R13)
         RETURN (14,12),RC=0
         SPACE 5
*
*        OUTPUT A COMPRESSED BLOCK TO THE LIBRARY
*
OUTPUT   ZZLBL
         L     R4,STARTBUF
         WRITE CHK1,SF,LIBOUT,(R4),(R6)
         CHECK CHK1
         FREEBUF LIBOUT,(R4)
         GETBUF LIBOUT,(R4)
         ST    R4,STARTBUF
         AH    R4,BUFLEN
         ST    R4,ENDBUF
         L     R4,STARTBUF
         BR    R10
         SPACE 5
CARDIN   DCB   DDNAME=CARDIN,                                          *
               DSORG=PS,                                               *
               EODAD=EOF,                                              *
               LRECL=80,                                               *
               MACRF=GL,                                               *
               RECFM=FB
LIBOUT   DCB   BLKSIZE=13030,                                          *
               DDNAME=LIBOUT,                                          *
               DSORG=PO,                                               *
               MACRF=W,                                                *
               RECFM=UT,                                               *
               BUFNO=3
BUFLEN   DC    H'13030'                 LENGTH OF OUTPUT BUFFER
SAVE     DS    18F
STARTBUF DS    F                        START OF CURRENT BUFFER
ENDBUF   DS    F                        END OF CURRENT BUFFER
CARDLEN  DC    H'72'                    CARD LENGTH - CHANGED FOR COBOL
COBSW    DC    X'00'                    COBOL SWITCH
* PDS DIRECTORY DATA
PROGNAME DC    CL8' '
         DC    XL3'00'                  TTR
         DC    X'02'                    HALFWORDS OF USER DATA
SSI      DS    XL4
FILLER   DS    X
         END
