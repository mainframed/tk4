S60029G  CSECT
         ENTRY PGENPRTG
         PRINT NOGEN
*
*      PGENPRTG. UTILITY PRINT MODULE.
*
*
*        02      OP-REC.
*         03     OP-CONT         PIC X.
*         03     OP-DATA         PIC X(132).
*
*          CALL 'PGENPRTG' USING OP-REC.
*
*
*                  X'40' = WRITE AFTER 1 SPACE.
*                  X'F0' = WRITE AFTER 2 SPACES.
*                  X'60' = WRITE AFTER 3 SPACES.
*                  X'F1' = WRITE AFTER SKIP TO CHANNEL 1.
*                  X'F2' = WRITE AFTER SKIP TO CHANNEL 2.
*                  ETC.
*
*        ON RETURN OP-CONT WILL CONTAIN '1' IF MORE THAN 55 LINES
*        HAVE BEEN PRINTED SINCE THE LAST SKIP TO CHANNEL 1, ELSE IT
*        WILL CONTAIN ZERO. OP-DATA WILL CONTAIN SPACES.
         EJECT
PGENPRTG EQU   *
         MBEGIN
         L     R2,0(R1)                 R2 = INPUTDATA ADDRESS.
         CLI   0(R2),X'FF'              TEST IF 'CLOSE' REQUIRED
         BNE   NOTCLSE                  NO, GO SEE WHAT IS
         CLI   OCIND,C'O'               TEST IF FILE OPEN
         BNE   EXIT1                    NO, EXIT FROM ROUTINE
         CLOSE PFILE                    CLOSE THE PRINT FILE
         MVI   OCIND,C'C'               MOVE 'CLOSED' INDICATOR.
         B     EXIT1                    EXIT FROM ROUTINE
EXIT     EQU   *
         MVC   1(132,R2),SPACES         CLEAR PRINT LINE
EXIT1    EQU   *
         MFINISH
NOTCLSE  EQU   *
         CLI   OCIND,C'O'               TEST IF FILE OPEN
         BE    OPENED                   BRANCH IF YES
         OPEN  (PFILE,(OUTPUT))         OPEN THE FILE
         MVI   OCIND,C'O'               SET 'OPENED' INDICATOR
         ZAP   LCOUNT,P0                ZEROISE LINE COUNT
         CLI   0(R2),C'1'               TEST IF 1ST REQUEST IS CHAN 1
         BE    OPENED                   IF YES BRANCH
         MVI   SPACES,C'1'              ASSUME SKIP TO CHAN 1
         PUT   PFILE,SPACES             DO IT
         MVI   SPACES,C' '              RESTORE SPACES CONSTANT
OPENED   EQU   *
         CLI   0(R2),X'40'              TEST IF SPACE 1
         BE    SKIP40                   YES, BRANCH
         CLI   0(R2),X'F0'              TEST IF SPACE 2
         BE    SKIPF0                   YES, BRANCH
         CLI   0(R2),X'60'              TEST IF SPACE 3
         BE    SKIP60                   YES, BRANCH
         CLI   0(R2),X'F1'              TEST IF CHAN 1 SKIP
         BE    SKIPF1                   YES, BRANCH
*        ASSUME 1-SPACE FOR SKIPS TO CHANNELS OTHER THAN 1
SKIP40   EQU   *
         AP    LCOUNT,P1                ADD 1 TO LINE-COUNT
         B     PRINT                    GO PRINT LINE
SKIPF0   EQU   *
         AP    LCOUNT,P2                ADD 2 TO LINE-COUNT
         B     PRINT                    GO PRINT THE LINE
SKIP60   EQU   *
         AP    LCOUNT,P3                ADD 3 TO LINE COUNT
         B     PRINT                    GO PRINT THE LINE
SKIPF1   EQU   *
         ZAP   LCOUNT,P0                CLEAR LINE COUNT
PRINT    EQU   *
         PUT   PFILE,(R2)               OUTPUT PRINT LINE
         MVI   0(R2),C'0'               SET CTLCHR AS ZERO
         CP    LCOUNT,P56               TEST IF 55 LINES PRINTED YET
         BL    EXIT                     NO, LEAVE ZERO
         MVI   0(R2),C'1'               YES, MOVE '1'
         B     EXIT                     GO EXIT FROM ROUTINE
         EJECT
PFILE    DCB   BLKSIZE=133,DDNAME=PGENPRTG,DEVD=DA,DSORG=PS,           *
               LRECL=133,MACRF=(PM),RECFM=FA
*
*
R1       EQU   1
R2       EQU   2
P0       DC    P'0'
P1       DC    P'1'
P2       DC    P'2'
P3       DC    P'3'
P56      DC    P'56'
LCOUNT   DC    PL4'0'
*
OCIND    DC    C'C'
SPACES   DC    CL133' '
         LTORG
         END
