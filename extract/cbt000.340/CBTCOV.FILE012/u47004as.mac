         TITLE 'GN046 - FILE MATCH UTILITY'
*****************************************************************
*                                                               *
*   OBJECT:      TO MATCH TWO SEQUENTIAL DATASETS ON SPECIFIED  *
*  --------  SEQUENCE KEYS AND COMPARE MATCHED RECORDS ON UP TO *
*            FOUR FURTHER SPECIFIED FIELDS.                     *
*                                                               *
*.  PARAMETER:   PARM='RKPA,KEYL,RKPB,RFPA,FL,RFPB,----'        *
*   ----------                                                  *
*                                                               *
*   NOTES:       RKPA SPECIFIES KEY POSITION ON FILEA
*   -----
*                RKPB SPECIFIES KEY POSITION ON FILEB
*
*                KEYL SPECIFIES THE KEYLENGTH OF BOTH FILES
*
*                RFPA SPECIFIES A FIELD ON FILEA TO BE COMPARED WITH
*
*                RFPB WHICH IS A FIELD ON FILEB
*
*                FL SPECIFIES THE FIELD LENGTH
*
*                UP TO FOUR FIELDS CAN BE COMPARED.
*
*   INPUT        1. FILEA -SYSUT1
*   ABEND CODES:                                                *
*   ------------                                                *
*                                                               *
*        U0004  PARAMETER MISSING                               *
*        U0008   PARAMETER INVALID                              *
*        U0012   SYSUT1 DD STATEMENT MISSING                    *
*        U0016   SYSUT2 DD STATEMENT MISSING                    *
*        U0020   SYSUT3 DD STATEMENT MISSING                    *
*        U0024   SYSUT4 DD STATEMENT MISSING                    *
*        U0028   SYSUT5 DD STATEMENT MISSING                    *
*        U0032   SYSUT6 DD STATEMENT MISSING                    *
*        U0036   SYSPRINT DD STATEMENT MISSING                  *
*        U0040   IOERROR ON SYSUT1                              *
*        U0044   IOERROR ON SYSUT2                              *
*        U0048   IOERROR ON SYSUT3                              *
*        U0052   IOERROR ON SYSUT4                              *
*        U0056   IOERROR ON SYSUT5                              *
*        U0060   IOERROR ON SYSUT6                              *
*        U0064   SYSUT3 LRECL > SYSUT1 LRECL                    *
*        U0068   SYSUT4 LRECL > SYSUT2 LRECL                    *
*        U0072   RKPA OR RFPA + KEYL > SYSUT1 LRECL             *
*        U0076   RKPB OR RFPB + KEYL > SYSUT2 LRECL             *
*        U0080   FIELD LENGTH > 256                             *
*        U0084   SEQUENCE ERROR ON SYSUT1                       *
*        U0088   SEQUENCE ERROR ON SYSUT2                       *
*                                                               *
*   -----        2. FILEB -SYSUT2
*                3. PARAMETER INFORMATION
*
*   OUTPUT       1. FILEC -SYSUT3    -ON A BUT NOT ON B
*   ------       2. FILED -SYSUT4    -ON B BUT NOT ON A
*                3. FILEE -SYSUT5    -FROM A UNEQUAL FIELD COMPARISON
*                4. FILEF -SYSUT6    -FROM B UNEQUAL FIELD COMPARISON
*                5. MESSAGES -SYSPRINT
*
*   SAMPLE JCL   //   EXEC PGM=GN046,PARM=.................
*   ----------
*                //SYSPRINT DD SYSOUT=A
*
*                //SYSUT1   DD    -INPUT FILEA
*
*                //SYSUT2   DD    -INPUT FILEB
*
*                //SYSUT3   DD    -OUTPUT FILEC(OPTIONAL)
*
*                //SYSUT4   DD    -OUTPUT FILED(OPTIONAL)
*
*                //SYSUT5   DD    -OUTPUT FILEE(OPTIONAL)
*
*                //SYSUT6   DD    -OUTPUT FILEF(OPTIONAL)
*
*****************************************************************
         SPACE 2
GN046    BEGIN BASEREG=(12)
         SPACE 1
         L     2,0(1)                  STORE ADDRESS OF PARM LIST
         LA    10,0                     RIO INDEX REG FOR PARMFLDS
         LH    3,0(2)                   R3 = PARMLENGTH
         LA    4,2(2)                   R4 POINTS TO PARM INFO
         LTR   3,3                      CHECK PARM CODED
         BNZ   CONTPRM                  CONTINUE IF CODED
         ABEND 4                        NOT CODED
CONTPRM  EQU   *
         LTR   5,4                      R5 POINTS TO START PARM ADD
         BCTR  3,0                      R3 = PARM LENGTH -1
         AR    5,3                      R5 POINTS TO PARM END ADD
         SPACE 2
PARMLOOP EQU   *
         CH    10,=H'60'                PARM OVERSIZE ?
         BE    ABEND8                   YES
         EX    3,TRTPARM                TRT 0(0,4),TRTTAB   SCAN FOR ,
         BZ    LASTPARM                 LAST PARM FIELD
         LR    7,1                     R7 POINTS TO COMMA
         SR    7,4                      R7 = FIELD LENGTH
         SR    3,7                      R3 = LENGTH REMAINDER PARM
         CH    3,=H'1'                  CHECK LENGTH REMAINDER
         BL    ABEND8
         LTR   7,7                      TEST FOR ZERO
         BZ    INCINDX                  YES
         BCTR  7,0                     R7 = FIELD LENGTH -1
PACKFLD  EX    7,PACKPARM               PACK  PACKAREA,0(0,4)
         CVB   9,PACKAREA               CONVERT TO BINARY
         ST    9,PARMFLDS(10)          STORE IN TABLE
INCINDX  AH    10,=H'4'                 INCREMENT TABLE INDEX
         LA    4,1(1)                   R4 = NEXT PARM FIELD
         BCTR  3,0                      R3 = REMAINDER LENGTH -1
BRBACK   B     PARMLOOP
*
*        THIS BRANCH IS CHANGED TO NOP ON LAST TIME THRU
*
         SPACE 1
CHKPARM  EQU   *
         LA    9,PARMFLDS
         CLC   8(4,9),HVAL             RKPB COMPLETED?
         BNE   GETLRECL                YES
         MVC   8(4,9),0(9)             NO
GETLRECL EQU   *
         CL    10,=F'8'                 PARAMETER UNDERSIZE ?
         BL    ABEND8                   YES
         RDJFCB SYSUT3                  READ JFCB
         MVC   S3LRECL,S1JFCB+104       STORE SYSUT3 LRECL
         RDJFCB SYSUT4                  READ JFCB
         MVC   S4LRECL,S1JFCB+104       STORE SYSUT4 LRECL
         OPEN  (SYSUT1,(INPUT))
         TM    SYSUT1+48,X'10'          SUCCESS ?
         BO    OPENSYS2                 YES
         ABEND 12                       NO
OPENSYS2 OPEN  (SYSUT2,(INPUT))
         TM    SYSUT2+48,X'10'          SUCCESS ?
         BO    MAINPROC                YES
         ABEND 16                       NO
READS    BAL   11,GETSYS1               READ SYSUT1
         MVC   S1LRECL,SYSUT1+82        STORE LRECL SYSUT1
         BAL   11,GETSYS2               READ SYSUT2
         MVC   S2LRECL,SYSUT2+82        STORE LRECL SYSUT2
         CLC   S3LRECL,S1LRECL          CHECK SYSUT3 LRECL
         BNH   COMPL2                   OK
         ABEND 64                       TOO GREAT
COMPL2   CLC   S4LRECL,S2LRECL          CHECK SYSUT4 LRECL
         BNH   COMPL3                   OK
         ABEND 68                       TOO GREAT
COMPL3   EQU   *
         LA    9,PARMFLDS
LOOP     EQU   *
         CL    9,LASTADD                LAST PARMFIELD VALIDATED ?
         BH    MAINLOOP                YES
         CLC   8(4,9),HVAL              RKPB OR PB COMPLETED?
         BNE   LOOP1                   YES
         MVC   8(4,9),0(9)             MOVE RKPA TO PB
LOOP1    CLC   4(4,9),HVAL             LENGTH COMPLETED ?
         BNE   LOOP2                    YES
         MVC   4(4,9),=F'1'             DEFAULT LENGTH = 1
LOOP2    EQU   *
         CLC   4(4,9),=F'256'           LENGTH > 256 ?
         BNH   LOOP3                    NO
         ABEND 80,DUMP                 YES
LOOP3    EQU   *
         LA    8,0                      ZEROISE REG 8
         A     8,0(9)                   ADD RKPA TO R8
         A     8,4(9)                   ADD LENGTH TO R8
         CL    8,S1LRECL                COMPARE TOTAL TO LRECL
         BNH   LOOP4
         ABEND 72,DUMP
LOOP4    EQU   *
         LA    8,0                      ZEROISE R8
         A     8,4(9)                   ADD LENGTH TO R8
         A     8,8(9)                   ADD RKPB TO R8
         CH    8,S2LRECL               COMPARE TOTAL TO LRECL
         BNH   LOOP5                    OK
         ABEND 76,DUMP
LOOP5    LA    9,12(9)                  INCREMENT PARAM ADDRESS
         B     LOOP
         SPACE 2
MAINPROC LA    9,PARMFLDS
         L     0,4(9)                   LOAD KEY LENGTH FOR GETMAIN
         ST    0,LENSTOR               STORE KEYLENGTH
         GETMAIN R,LV=(0)               OBTAIN STORAGE AREAS
         ST    1,SYS1STOR                    FOR SEQUENCE
         GETMAIN R,LV=(0)                    CHECKING OF
         ST    1,SYS2STOR                    INPUT FILES
         OPEN  (SYSUT3,(OUTPUT),SYSUT4,(OUTPUT))
         TM    SYSUT3+48,X'10'          OPEN SUCCESSFUL?
         BO    SYS4OPEN                 YES
         ABEND 20                       NO
SYS4OPEN EQU   *
         TM    SYSUT4+48,X'10'          OPEN SUCCESSFUL?
         BO    OPENSYS5                 YES
         ABEND 24                       NO
OPENSYS5 EQU   *
         OPEN  (SYSUT5,(OUTPUT),SYSUT6,(OUTPUT))
         TM    SYSUT5+48,X'10'          OPEN SUCCESSFUL?
         BO    SYS6OPEN                 YES
         ABEND 28                       NO
SYS6OPEN EQU   *
         TM    SYSUT6+48,X'10'          OPEN SUCCESSFUL?
         BO    READS                    YES
         ABEND 32                       NO
         SPACE 1
MAINLOOP EQU   *
         L     1,SYS1STOR              LOAD STORED KEY SYSUT1
         L     2,LENSTOR               LOAD LENGTH STORED KEY
         BCTR  2,0
         EX    2,COMPHVAL              TEST FOR EOF
         BNE   COMKEYS1                 NO,BRANCH
         L     1,SYS2STOR              LOAD ADD STORED SYS2 KEY
         L     2,LENSTOR               LENGTH
         BCTR  2,0                       NGTH
         EX    2,COMPHVAL              TEST FOR EOF
         BE    CONTPRNT                 YES  BRANCH
COMKEYS1 EQU   *
         L     5,SYS1STOR              R5 = STORED SYS1 KEY
         L     6,LENSTOR
         BCTR  6,0
         L     7,SYS2STOR
         EX    6,COMPAR                CONPARE KEYS
         BH    PUTSYS4                 SYSUT1 HIGH
         BL    PUTSYS3                 SYSUT2 HIGH
COMKEYS2 EQU   *
         LA    9,12(9)                  INCREMENT FIELD POINTERS
         CL    9,LASTADD                ANY MORE FIELDS ?
         BH    GETMORIN                 NO
         BAL   8,SETADDR
         EX    6,COMPAR
         BNE   PUTSYS56
         B     COMKEYS2
GETMORIN BAL   11,GETSYS1
         BAL   11,GETSYS2
         B     MAINLOOP
         SPACE 2
LASTPARM EQU   *
         NI    BRBACK+1,X'0F'           NOP BRANCH AT BRBACK
         LR    7,3                      R7 = REMAINDER LENGTH-1
         LA    2,PARMFLDS(10)
         ST    2,LASTADD
         B     PACKFLD
         SPACE 2
*****    EXECUTED INSTRUCTIONS                         ******
         SPACE 1
HVMOVE   MVC   0(0,1),HVAL             LOAD HIGH VALUES
COMPHVAL CLC   0(0,1),HVAL             TEST FOR HIGH VALUES
SEQCHK1  CLC   0(0,5),0(1)             SEQUENCE CHECK SYSUT1
SEQCHK2  CLC   0(0,7),0(2)             SEQUENCE CHECK SYSUT12
COMPAR   CLC   0(0,5),0(7)              COMPARE SYSUT1 TO SYSUT2 FIELD
TRTPARM  TRT   0(0,4),TRTTAB            SCAN FOR COMMA
PACKPARM PACK  PACKAREA,0(0,4)          PACK PARM FIELD
KEYMOVE1 MVC   0(0,1),0(5)             MOVE THIS KEY TO LAST
KEYMOVE2 MVC   0(0,2),0(7)             MOVE THIS KEY TO LAST
ABEND8   ABEND 8,DUMP                  PARAMETER ERROR
         SPACE 1
GETSYS1  EQU   *
         GET   SYSUT1                   READ SYSUT1
         LR    3,1                      R3 POINTS TO RECORD
         L     2,SYS1CNT                INCREMENT
         AH    2,ONE                    RECORD
         ST    2,SYS1CNT                COUNT
         TM    IOERR,X'01'              IOERROR ?
         BZ    TESTSEQ1                 NO
         ABEND 44                       YES
TESTSEQ1 EQU   *
         LA    9,PARMFLDS
         BAL   8,SETADDR      SET UP ADDRESS OF KEY
         L     1,SYS1STOR              LOAD ADD  STORED KEY
         EX    6,SEQCHK1      COMPARE WITH LAST KEY
         BH    MOVEKEY1                 THIS > LAST
         BE    SYS1EQUL       DUPLICATE KEY
         ABEND 84             SEQUENCE ERROR
SYS1EQUL L     2,DUP1CNT
         AH    2,ONE
         ST    2,DUP1CNT
         BR    11
MOVEKEY1 EX    6,KEYMOVE1
         BR    11
         SPACE 1
GETSYS2  EQU   *
         GET   SYSUT2                   READ
         L     2,SYS2CNT               INCREMENT
         AH    2,ONE                    RECORD
         ST    2,SYS2CNT                COUNT
         LR    4,1                      R4 POINTS TO RECORD
         TM    IOERR,X'02'              IOERROR ?
         BZ    TESTSEQ2                 NO
         ABEND 44                       YES
TESTSEQ2 LA    9,PARMFLDS
         BAL   8,SETADDR                SET UP KEY ADDRESS & LENGTH
         L     2,SYS2STOR              LOAD ADD  STORED KEY
         EX    6,SEQCHK2                COMPARE AGAINST LAST KEY
         BH    MOVEKEY2                 OK   THIS > LAST
         BE    SYS2EQUL                 DUPLICATE KEY
         ABEND 88                       SEQUENCE ERROR
SYS2EQUL L     2,DUP2CNT                ADD  ONE
         AH    2,ONE                       TO
         ST    2,DUP2CNT                DUPLICATE COUNT
         BR    11
MOVEKEY2 EX    6,KEYMOVE2               MOVE THIS KEY TO LAST KEY
         BR    11
         SPACE 1
SETADDR  EQU   *
         LM    5,7,0(9)                PKPA,KEYL,RKPB = R5,R6,R7
         AR    5,3                      R5= SYSUT1 FIELD
         AR    7,4                      R7 = SYSUT2 FIELD
         BCTR  6,0                      SUBTRACT 1 FROM LENGTH
         BR    8
         SPACE 1
         SPACE 1
PUTSYS3  EQU   *
         L     2,SYS3CNT
         AH    2,ONE                    ADD U TO CNT
         ST    2,SYS3CNT
         PUT   SYSUT3,0(3)              WRITE SYSUT3
         TM    IOERR,X'04'             IOERR ?
         BZ    WRITE3OK                 NO
         ABEND 48                       YES
WRITE3OK BAL   11,GETSYS1              READ SYSUT1
         B     MAINLOOP
         SPACE 1
PUTSYS4  EQU   *
         L     2,SYS4CNT               ADD 1
         AH    2,ONE                    TO
         ST    2,SYS4CNT               ACOUNT
         PUT   SYSUT4,0(4)              WRITE SYSUT4
         TM    IOERR,X'08'              IOERROR ?
         BZ    WRITE40K                 NO
         ABEND 52                       YES
WRITE40K BAL   11,GETSYS2               READ SYSUT2
         B     MAINLOOP
         SPACE 1
PUTSYS56 EQU   *
         L     2,SYS5CNT
         AH    2,ONE                    ADD TO RECORD COUNT
         ST    2,SYS5CNT
         PUT   SYSUT5,0(3)              WRITE SYSUT5
         TM    IOERR,X'10'
         BZ    WRITE5OK
         ABEND 56
WRITE5OK EQU   *
         PUT   SYSUT6,0(4)              WRITE SYSUT6
         TM    IOERR,X'20'
         BZ    WRITE6OK
         ABEND 60
WRITE6OK EQU   *
         BAL   11,GETSYS1               READ  SYSUT1
         BAL   11,GETSYS2               READ  SYSUT2
         B     MAINLOOP
         SPACE 1
EOFSYS1  EQU   *
         L     1,SYS1STOR
         L     2,LENSTOR
         BCTR  2,0
         EX    2,HVMOVE                SET KEY TO HIGH VALUES
         BR    11
         SPACE 1
EOFSYS2  EQU   *
         L     1,SYS2STOR
         L     2,LENSTOR
         BCTR  2,0
         EX    2,HVMOVE
         BR    11
         SPACE 2
SYS1ERR  OI    IOERR,X'01'
         BR    14
SYS2ERR  OI    IOERR,X'02'
         BR    14
SYS3ERR  OI    IOERR,X'04'
         BR    14
SYS4ERR  OI    IOERR,X'08'
         BR    14
SYS5ERR  OI    IOERR,X'10'
         BR    14
SYS6ERR  OI    IOERR,X'20'
         BR    14
         SPACE 1
CONTPRNT EQU   *
         CLOSE (SYSUT1)
         CLOSE (SYSUT2)
         CLOSE (SYSUT3)
         CLOSE (SYSUT4)
         CLOSE (SYSUT5)
         CLOSE (SYSUT6)
         OPEN  (SYSPRINT,(OUTPUT))
         TM    SYSPRINT+48,X'10'
         BO    PUTCONTS
         ABEND 36
PUTCONTS EQU   *
         PUT   SYSPRINT,PLINE1
         MVC   PNAME,=C'SYSUT1'
         L     2,SYS1CNT
         CVD   2,DWORD
         MVC   PCNT,PATRN
         ED    PCNT,DWORD+4
         PUT   SYSPRINT,PLINE2
         MVC   PNAME,=C'SYSUT2'
         L     2,SYS2CNT
         CVD   2,DWORD
         MVC   PCNT,PATRN
         ED    PCNT,DWORD+4
         PUT   SYSPRINT,PLINE2
         MVC   PNAME,=C'SYSUT3'
         L     2,SYS3CNT
         CVD   2,DWORD
         MVC   PCNT,PATRN
         ED    PCNT,DWORD+4
         PUT   SYSPRINT,PLINE2
         MVC   PNAME,=C'SYSUT4'
         L     2,SYS4CNT
         CVD   2,DWORD
         MVC   PCNT,PATRN
         ED    PCNT,DWORD+4
         PUT   SYSPRINT,PLINE2
         MVC   PNAME,=C'SYSUT5'
         L     2,SYS5CNT
         CVD   2,DWORD
         MVC   PCNT,PATRN
         ED    PCNT,DWORD+4
         PUT   SYSPRINT,PLINE2
         MVC   PNAME,=C'SYSUT6'
         PUT   SYSPRINT,PLINE2
         MVC   PNAME(7),=C'DUPCNT1'
         L     2,DUP1CNT
         CVD   2,DWORD
         MVC   PCNT,PATRN
         ED    PCNT,DWORD+4
         PUT   SYSPRINT,PLINE2
         MVC   PNAME(7),=C'DUPCNT2'
         L     2,DUP2CNT
         CVD   2,DWORD
         MVC   PCNT,PATRN
         ED    PCNT,DWORD+4
         PUT   SYSPRINT,PLINE2
         CLOSE (SYSPRINT)
         LA    15,0
         GOBACK RC=(15)
PLINE1   DS    0CL133
         DC    C'1  GN046      DDNAME     RECORD COUNT '
         DC    CL95' '
PLINE2   DS    0CL133
         DC    CL14' '
PNAME    DC    CL6' '
         DC    CL7' '
PCNT     DC    CL8' '
         DC    CL98' '
PARMADD  DC    F'0'
LASTADD  DC    F'0'
PARMFLDS DS    0F
         DC    15F'-1'
TRTTAB   DS    0F
         DC    XL256'00'
         ORG   TRTTAB+X'6B'
         DC    X'01'
         ORG
PACKAREA DC    D'0'
EXLIST1  DC    X'87',AL3(S1JFCB)
S1JFCB   DC    XL176'00'
S1LRECL  DC    H'0'
S2LRECL  DC    H'0'
S3LRECL  DC    H'0'
S4LRECL  DC    H'0'
ONE      DC    H'1'
SYS1CNT  DC    F'0'
SYS2CNT  DC    F'0'
SYS3CNT  DC    F'0'
SYS4CNT  DC    F'0'
SYS5CNT  DC    F'0'
SYS6CNT  DC    F'0'
HVAL     DC    256X'FF'
PATRN    DC    X'4020202020202120'
IOERR    DC    X'00'
SYS1STOR DC    F'0'
SYS2STOR DC    F'0'
LENSTOR  DC    F'0'
DUP1CNT  DC    F'0'
DUP2CNT  DC    F'0'
DWORD    DC    D'0'
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EXLST=EXLIST1,          *
               EODAD=EOFSYS1,SYNAD=SYS1ERR
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=GL,EXLST=EXLIST1,          *
               EODAD=EOFSYS2,SYNAD=SYS2ERR
SYSUT3   DCB   DDNAME=SYSUT3,DSORG=PS,MACRF=PM,EXLST=EXLIST1,          *
               SYNAD=SYS3ERR
SYSUT4   DCB   DDNAME=SYSUT4,DSORG=PS,MACRF=PM,EXLST=EXLIST1,          *
               SYNAD=SYS4ERR
SYSUT5   DCB   DDNAME=SYSUT5,DSORG=PS,MACRF=PM,SYNAD=SYS5ERR
SYSUT6   DCB   DDNAME=SYSUT6,DSORG=PS,MACRF=PM,SYNAD=SYS6ERR
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM
         LTORG
         END
