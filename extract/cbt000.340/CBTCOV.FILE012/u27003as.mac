SY1      CSECT
*    USE:
*      SY1 CONTROLS REGULAR HOUSEKEEPING AS FOLLOWS
*
*
*        IF PERIOD + STORED DATE > CURRENT DATE THEN HOUSEKEEPING
*        IS TO BE PERFORMED. A RETURN CODE IS ISSUED, WHICH SUBSEQUENT
*        JOBSTEPS TEST, SO THAT THE HOUSEKEEPING GOES AHEAD.
*        ANOTHER PROGRAM,SY4(ALSO IN THE GUIDE LIBARY), PLACED AT THE
*        END OF THE HOUSEKEEPING STEPS PICKS UP THE STORED DATE
*        AND PERIOD, ADDS THEM TOGETHER AND WRITES THE TOTAL AS THE
*        NEW STORED DATE. THIS INDICATES THAT THE HOUSEKEEPING WAS
*        COMPLETED AND IS NOT TO BE RUN AGAIN UNTIL THE PERIOD HAS
*        ELAPSED AGAIN. IF THE HOUSEKEEPING FAILED THE STORED DATE
*        WOULD BE UNCHANGED AND THE NEXT TIME THE JOB WAS RUN AGAIN
*        THE HOUSEKEEPING WOULD START AGAIN.     THIS ENSURES THAT
*        IF OPERATIONS FORGET TO RUN THE HOUSEKEEPING THEN WHATEVER
*        WAS MEANT TO BE DONE ON THAT DAY WILL BE DONE ON THE FIRST
*        OCCASION THAT THEY RUN IT SUBSEQUENTLY.
*
*    THE JCL IS AS FOLLOWS:
*        //    EXEC PGM=SY1,
*        //    PARM='PERIOD,CODE1,MES1,CODE2,MES2'
*        //CHDATE DD DISK FILE FOR STORED DATE
*
*        WHERE PERIOD    IS NUMBER OF DAYS TO BE ADDED TO STORED DATE
*                          FOR COMPARISON WITH CURRENT DATE
*              CODE1     IS RETURN CODE IF CURRENT DATE^>STORED
*                          DATE + PERIOD
*              MES1      IS MESSAGE WRITTEN TO OPERATOR WHEN CODE1
*                          RETURNED.
*              CODE2     IS CODE RETURNED WHEN CURRENT DATE>= STORED
*                          DATE + PERIOD
*              MES2      IS MESSAGE WRITTEN TO OPERATOR WHEN CODE2
*                          ISSUED.
*

*    DEPARTMENT OF POSTS AND TELEGRAPHS
*      COMPUTER CENTRE
*    DUBLIN 14
*
*    PHONE 983577
*
************************************************************
*                           SY1                            *
* THIS PROGRAM READS A DATE FILE DDNAME= DATEFL WHICH IT   *
* UPDATES WITH THE CURRENT DATE IF THE PERIOD+ DATE IS GT  *
* THE CURRENT DATE FROM TIME MACRO TH                      *
* AND ISSUES A RETURN CODE.                                *
* IT ACCEPTS PARAMETERS FROM THE EXEC CARD IN THE FORMAT   *
* 'PERIOD,CODE1,MESSAGE1,CODE2,MESSAGE2'.THESE ARE         *
* POSITIONAL PARAMETERS,ALL EXECPT PERIOD MAY BE OMITTED   *
* BY CODING A COMMA IF ANY PARAMETERS FOLLOW.              *
* IF PERIOD IS OMITTED THE PROGRAM ABENDS WITH CODE 90     *
* IF PERIOD IS NOT NUMERIC THE PROGRAM ABENDS WITH CODE 91 *
* IF CODE1 OR CODE2 ARE OMITTED ZERO IS ASSUMED.           *
* IF CODE1 IS NOT NUMERIC PROGRAM ABENDS WITH CODE 92      *
* IF CODE2 IS NOT NUMERIC PROGRAM ABENDS WITH CODE 93      *
* IF MESSAGE1 OR MESSAGE2 OMITTED BLANK ASSUMED.           *
*                       ************                       *
*                   PROGRAM NARRATIVE                      *
* PICK UP PARAMETER FROM EXEC CARD                         *
* CHECK THAT PERIOD IS PRESENT AND NUMERIC.                *
* PICK UP PERIOD IN PACKED FORMAT                          *
* GET  CURRENT DATE FROM TIME MACRO                        *
* READ DATE FILE (DDNAME=DATEFL)                           *
* STORE PERIOD IN PACKED FORMAT IN BYTES 5&6 OF DATE RECORD*
* FOR USE BY SY4 IF ACTION1 IS TAKEN IN THIS PROG.         *
* ADD DATE ON DATE FILE TO PERIOD                          *
* IF RESULT ^> CURRENT DATE THEN UPDATE DATE FILE WITH     *
* RESULT,WRITE MESSAGE1 TO OPERATOR AND ISSUE RETURN CODE  *
* OF CODE1.                                                *
*                           ELSE DO NOT UPDATE DATE FILE,  *
* WRITE MESSAGE2 TO OPERATOR,AND ISSUE RETURN CODE OF CODE2*
*                   ********************                   *
* WRITTEN BY P.MAGUIRE                                     *
*              FEBRUARY 1974                               *
************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2    LENGTH OF PARM FIELD FROM EXEC STATEMENT
R3       EQU   3    ADRESS OF START OF PARM FIELD
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
R12      EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         SAVE  (14,12),,*
         BALR  R12,0
         USING *,R12
         LR    RB,RD
         LA    RD,SAVER
         ST    RB,4(RD)
         ST    RD,8(RB)
         L     R1,0(0,R1)     PICK UP PARAMETER FROM EXEC STMT
         MVC   HWORD,0(R1)    PICK UP LENGTH OF PARM FIELD
         LH    R2,HWORD       PUT LENGTH OF PARM FIELD IN R2
         SH    R2,ONE         AND REDUCE BY ONE FOR EXECUTE INSTRUCTION
         EX    R2,PUPRM       MOVE PARM FIELD INTO PARMS
         LA    R2,PARMS       POINT REGISTERS 2
         LR    R3,R2                           AND 3 TO PARMS
         LA    R5,FIRST       FOR BCR INSTRUCTION
* SCAN PARMS AND PICK UP SEPERATE ITEMS
*
SCAN     CLC   0(1,R3),DLM    LOOK FOR DELIMITER
         BCR   8,R5           IF FOUND BRANCH TO ADDRESS IN R5
         LA    R3,1(R3)       POINT R3 TO NEXT BYTE
         B     SCAN           LOOP AROUND
*
FIRST    EQU   *         R3 MUST HOLD ITS VALUE AS IT POINTS TO THE
         LR    R4,R3     LAST BYTE OF PARMS LOOKED AT.
         SR    R4,R2     PUT LENGTH OF PERIOD IN R4
         SH    R4,ONE    REDUCE BY ONE FOR EX.INSTRUCTION
         LTR   R4,R4
         BC   10,GO
         MVC   ENDWTO-49(37),=C'SY1 PERIOD PARAMETER MISSING ABEND 90'
         WTO   MF=(E,LIST1)
         ABEND 90
GO       EQU   *
         EX    R4,NUMER
         BZ    NEXTAD1
         MVC   ENDWTO-49(37),=C'SY1 PERIOD PARAMETER INVALID ABEND 91'
         WTO   MF=(E,LIST1)
         ABEND 91
NEXTAD1   EQU  *
         EX    R4,PUPRD  PICK UP PERIOD IN PACKED FORMAT
         TIME
         ST    1,SYSDTE  STORE IN FULLWORD SYSDTE
         OPEN  (CHDATE,(UPDAT))
         GET   CHDATE    READ RECORD FROM DATE FILE
         LR    R8,R1
         USING INDATE,R8
         MVC   PEROUT,PERIOD+6  MAKE PERIOD AVAILABLE FOR SY4
         AP    PERIOD,DTEDTE  ADD TO PERIOD FROM PARMS
         CLC   DATEYR,SYSYR
         BNL   SAMEYR
         MVO   YEAR,DATEYR
         AP    DATEYR(3),=P'1000' ADD 1 TO YEAR
         DP    YEAR,FOUR
         CP    YEAR+2(1),=P'0'
         BNE   NOTLEAP
         SP    DTEDTE,=P'366'
         B     NEXT
NOTLEAP  EQU   *
         SP    DTEDTE,=P'365'
NEXT     EQU   *
SAMEYR   EQU   *
         CP    PERIOD+6(2),SYSDY
         BH    ACTION2        IF RESULT GREATER IGNORE CODE1 & MSG1
         LA    R5,SECND       FOR BCR INSTRUCTION
         LA    R3,1(R3)       POINT R3 TO NEXT BYTE OF PARMS
         LR    R2,R3
         B     SCAN
SECND    EQU   *
         LR    R4,R3
         SR    R4,R2     PUT LENGTH OF CODE1 IN R4
         SH    R4,ONE    REDUCE BY ONE FOR EX.INSTRUCTION
         LTR   R4,R4
         BC    4,NOEX1
         EX    R4,NUMER
         BZ    NEXTAD2
         MVC   ENDWTO-49(36),=C'SY1 CODE1 PARAMETER INVALID ABEND 92'
         WTO   MF=(E,LIST1)
         ABEND 92
NEXTAD2   EQU  *
         EX    4,PUCDE   PICK UP CODE1 IN PACKED FORMAT
NOEX1    EQU   *
         LA    R5,THIRD  FOR BCR INSTRUCTION
         LA    R3,1(R3)  POINT R3 TO NEXT BYTE OF PARMS
         LR    R2,R3
         B     SCAN
THIRD    EQU   *
         LR    R4,R3
         SR    R4,R2     PUT LENGTH OF MESSAGE IN R4
         SH    R4,ONE    REDUCE BY ONE FOR EX.INSTRUCTION
         LTR   R4,R4
         BC     4,GOBACK
         EX    4,COMP    CHECK FOR BLANK MESSAGE
         BE    GOBACK    IF BLANK IGNORE
         EX    4,PUMSG
         WTO   MF=(E,LIST1)
         B     GOBACK
ACTION2  EQU   *
* AT THIS POINT CODE1 & MESSAGE1 CAN BE IGNORED
         LA    R5,SKIP23
         LA    R3,1(R3)       POINT R3 TO NEXT BYTE OF PARMS
         LA    R6,2           FOR USE IN BCT INSTRUCTION
         B     SCAN
SKIP23   EQU   *
         LA    R3,1(R3)       POINT R3 TO NEXT BYTE OF PARMS
         BCT   R6,SCAN
         LR    R2,R3
         LA    R5,FORTH
         B     SCAN
FORTH    EQU   *
         LR    R4,R3
         SR    R4,R2     PUT LENGTH OF CODE2 IN R4
         SH    R4,ONE    REDUCE BY ONE FOR EX.INSTRUCTION
         LTR   R4,R4
         BC    4,NOEX2
         EX    R4,NUMER
         BZ    NEXTAD3
         MVC   ENDWTO-49(36),=C'SY1 CODE2 PARAMETER INVALID ABEND 93'
         WTO   MF=(E,LIST1)
         ABEND 93
NEXTAD3   EQU  *
         EX    R4,PUCDE
NOEX2    EQU   *
         LA    R5,FIFTH
         LA    R3,1(R3)
         LR    R2,R3
         B     SCAN
FIFTH    EQU   *
         LR    R4,R3
         SR    R4,R2     PUT LENGTH OF MESSAGE IN R4
         SH    R4,ONE    REDUCE BY ONE FOR EX.INSTRUCTION
         LTR   R4,R4
         BC    4,GOBACK
         EX    R4,COMP   CHECK FOR BLANK MESSAGE
         BE    GOBACK    IF BLANK IGNORE
         EX    R4,PUMSG       PICK UP
         WTO   MF=(E,LIST1)           AND WRITE MESSAGE TO OPERATOR
GOBACK   EQU   *
         PUTX  CHDATE
         CLOSE (CHDATE)
         CVB   RF,CODE   PUT CODE IN BINARY INTO RF
         L     RD,4(RD)
         RETURN (14,12),RC=(15)
LIST1    WTO   '                                                 ',MF=L
ENDWTO   EQU   *
DLM      DC    CL1','
SAVER    DS    9D
HWORD    DS    H
PARMS    DC    101X'6B'
ONE      DC    H'1'
PERIOD   DC    D'0'
CODE     DC    X'000000000000000C'
SYSDTE   DS    0F
         DS    CL1
SYSYR    DS    CL1
SYSDY    DS    CL2
TRTAB    DS    0CL256
         DC    240X'FF'
         DC     10X'00'
         DC      6X'FF'
BLANKS   DC   50CL1' '
FOUR     DC    PL1'4'
YEAR     DC    X'00000C'
NUMER    TRT   0(0,R2),TRTAB
PUPRM    MVC   PARMS(0),2(R1)
PUPRD    PACK  PERIOD,0(0,R2)
PUCDE    PACK  CODE,0(0,R2)
PUMSG    MVC   ENDWTO-49(0),0(R2)
COMP     CLC   BLANKS(0),0(R2)
CHDATE   DCB   DSORG=PS,DDNAME=DATEFL,MACRF=(GL,PL)
INDATE   DSECT
         DS    CL1
DATEYR   DS    CL1
DTEDTE   DS    CL2
PEROUT   DS    CL2
         DS    CL74
         END
