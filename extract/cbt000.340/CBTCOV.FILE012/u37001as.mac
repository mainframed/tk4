         MACRO
&NA      BEGIN &BASE,&SAVADR=,&ID=YES,&REGS=YES
         GBLA  &CHK
         LCLC  &BS1,&BS2,&BS3
         USING *,15
         AIF   ('&REGS' EQ 'NO').NOREGS
         AIF   (&CHK EQ 1).NOREGS
*  REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FP0      EQU   0
FP2      EQU   2
FP4      EQU   4
FP6      EQU   6
&CHK     SETA  1
.*  SET UP CSECT NAME AS AN 8-BYTE CHARACTER CONSTANT IF SPECIFIED
.NOREGS  AIF   ('&ID' EQ 'NO').NOID
         AIF   ('&SYSECT' EQ '').NOID
         B     *+12
         DC    CL8'&SYSECT'
.NOID    ANOP
&NA      STM   14,12,12(13)
         AIF   ('&BASE' NE  '').BSPEC
&BS1     SETC  '12'                DEFAULT BASE = 12
         CNOP  0,4
         BAL   &BS1,*+76
         USING *,&BS1
         AGO   .SAVE
.*  BASE SPECIFIED. SET UP APPROPRIATE USING STATEMENT, ALLOWING UP TO
.*     THREE REGISTERS
.BSPEC   ANOP
&BS1     SETC  '&BASE(1)'
&BS2     SETC  '&BASE(2)'
&BS3     SETC  '&BASE(3)'
         AIF   ('&BS2' EQ  '').ONLY1
         AIF   ('&BS2' EQ '&BS1' OR '&BS1' EQ '&BS3').FAIL
         AIF   ('&BS2' EQ '&BS3').FAIL
         AGO   .OVER1
.*  ALL SPECIFIED BASE REGISTERS ARE UNIQUE
.ONLY1   CNOP  0,4
         BAL   &BS1,*+76
         USING *,&BS1
         AGO   .SAVE
.*  MORE THAN ONE BASE REGISTER SPECIFIED
.OVER1   CNOP  0,4
         BAL   &BS1,*+76
         AIF   ('&BS3' NE  '').ALL3 THREE BASE REGISTERS SPECIFIED
         USING *,&BS1,&BS2
         AGO   .SAVE
.ALL3    USING *,&BS1,&BS2,&BS3
.SAVE    ANOP
&SAVADR  DC    18F'0'
         AIF   ('&BS2' NE  '').SETB2
         AGO   .STORE
.SETB2   LR    &BS2,&BS1           SET 2ND BASE =
         AH    &BS2,IND&SYSNDX        1ST BASE + 4096
         AIF   ('&BS3' NE  '').SETB3
         AGO   .INC4K
.SETB3   LR    &BS3,&BS2           SET 3RD BASE =
         AH    &BS3,IND&SYSNDX        2ND BASE + 4096
.INC4K   B     *+6
IND&SYSNDX  DC H'4096'
.*  COMPLETE THE ESQUENCE
.STORE   ST    &BS1,8(13)
         ST    13,4(&BS1)
         LR    13,&BS1
         MEXIT
.*  ERROR NOTES
.FAIL    MNOTE 12,'BASE REGS DO NOT HAVE UNIQUE NAMES. GENERATION '
         MNOTE *,'TERMINATED'
         MEXIT
         MEND
         MACRO
&NAME    FINISH &RC=Z
&NAME    L     13,4(13)
         AIF   ('&RC' EQ '(15)').REG15
         AIF   ('&RC'(1,1) EQ '(').REGERR
         AIF   ('&RC' EQ 'Z').NORC
         LA    15,&RC
.REG15   L     14,12(13)
         LM    0,12,20(13)
         AGO   .OUT
.NORC    LM    14,12,12(13)
.OUT     BR    14
         MEXIT
.REGERR  MNOTE *,RETURN CODE REGISTER MUST BE 15. NO R.C. RETURNED
         AGO   .NORC
         MEND
         TITLE 'DISMEMBR - SCRATCH MEMBERS FROM PDS'
         ISEQ  73,80
*  THIS PROGRAM IS TO SCRATCH MEMBERS FROM A PARTITIONED DATA SET.
*   THE DATA SET IS SPECIFIED IN THE DD CARD 'LIBRARY' AND THE NAMES
*  OF THE MEMBERS TO BE SCRATCHED ARE INPUTTED THRU THE DATA SET
*  'SYSIN'  ON CARDS IN COLS 1 - 8 , A NEW CARD FOR EACH MEMBER. A
*  LISTING IS PRODUCED ON THE DATA SET 'SYSPRINT'.
*    EXAMPLE :-
*
*     //       JOB
*     //JOBLIB DD DSN=LIBRARY.IBMPROGS,DISP=SHR
*     //S1     EXEC PGM=DISMEMBR
*     //SYSPRINT DD SYSOUT=A
*     //LIBRARY  DD DSN=LIBRARY.TEST,DISP=OLD
*     //SYSIN    DD *
*     MEMBERA
*     PROGA
*     MYPROG
*     /*
         EJECT
DISMEMBR CSECT
         BEGIN
         OPEN  (SYSIN,,SYSPRINT,OUTPUT,LIBRARY,OUTPUT)
         TM    SYSIN+48,X'10' IS A DD CARD PRESENT FOR SYSIN ?
         BZ    NODD1          NO, THEN BRANCH
         TM    SYSPRINT+48,X'10'   IS THERE A SYSPRINT DD CARD ?
         BZ    NODD2               NO,THEN BRANCH
         TM    LIBRARY+48,X'10'    IS THERE A LIBRARY DD CARD ?
         BZ    NODD3               NO,THEN BRANCH
         PUT   SYSPRINT,HEADING
         PUT   SYSPRINT,PRINTLIN
GETCARD  GET   SYSIN               GET MEMBER NAME
         MVC   PRINTLIN(8),0(R1)   MOVE MEMBER NAME TO PRINT AREA
         STOW  LIBRARY,PRINTLIN,D  DELETE MEMBER
         LTR   R15,R15             WAS THE DELETE SUCESSFUL ?
         BZ    PRINTMEM            YES, THEN BRANCH
         CH    R15,=H'16'          WAS THERE AN I/O ERROR ?
         BNE   MEMMISS             NO, THEN BRANCH
         MVC   PRINTLIN+9(L'IOERR),IOERR     I/O ERROR
         B     PRINTIT
MEMMISS  MVC   PRINTLIN+9(L'MIERR),MIERR     MEMBER MISSING
         B     PRINTIT
PRINTMEM MVC   PRINTLIN+9(L'DEL),DEL         MEMBER DELETED
PRINTIT  PUT   SYSPRINT,PRINTLIN
         MVI   PRINTLIN,X'40'
         MVC   PRINTLIN+1(L'PRINTLIN-1),PRINTLIN  CLEAR PRINT LINE
         B     GETCARD
NODD1    WTO   'SYSIN DD CARD MISSING'
         B     EOJ
NODD2    WTO   'SYSPRINT DD CARD MISSING'
         B     EOJ
NODD3    WTO   'LIBRARY DD CARD MISSING'
EOJ      WTO   'RUN ABANDONED'
         FINISH RC=8
ENDCARDS PUT   SYSPRINT,PRINTLIN
         MVC   PRINTLIN(L'ENDMESS),ENDMESS
         PUT   SYSPRINT,PRINTLIN
         CLOSE (SYSIN,,SYSPRINT,,LIBRARY)
         FINISH RC=0
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),RECFM=FB,LRECL=80,     *
               OPTCD=C,BUFNO=41,EODAD=ENDCARDS
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=38,  *
               BLKSIZE=1140
LIBRARY  DCB   DDNAME=LIBRARY,DSORG=PO,MACRF=(W)
IOERR    DC    C'NOT DELETED I/O ERROR'
MIERR    DC    C'NOT DELETED MEMBER NOT FOUND'
DEL      DC    C'DELETED'
HEADING  DC    C' MEMBERS DELETED                      '
ENDMESS  DC    C' DISMEMBR ENDED - CHOW '
PRINTLIN DC    CL38' '
         END
