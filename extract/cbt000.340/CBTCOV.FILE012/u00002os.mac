TSGVIPL  CSECT
*
*  THIS CSECT IS USED IN AN MVT SYSTEM (WITH SMF) AT THE POINT WHERE
*  NIP LINKS TO IEEVIPL FOR MASTER SCHEDULER INITIALIZATION.
*  IT ACTS AS A 'SMARTNIP' ON DIRECT ACCESS DEVICES ONLY, VARYING
*  OFFLINE ANY DISK UCBS FOR WHICH NIP HAS NOT INSERTED A VOLUME SER.
*  SMF INITIALIZATION PUTS THE SYSTEM INTO A WAIT STATE WHEN READING
*  THE VOLUME LABELS OF ALL D.A. DEVICES IF IT FINDS ONE MARKED ONLINE
*  BUT WITH NO PACK ON IT.  AS THE SYSTEM WILL NOT PROCESS VARY
*  COMMANDS UNTIL AFTER SMF INITIALIZATION THIS CSECT IS THE ONLY
*  ALTERNATIVE TO REMOVING THE ONLINE BIT MANUALLY FOR :RIVES WHICH
*  ARE NORMALLY IN USE BUT HAPPEN TO HAVE NO PACK CURRENTLY ON THEM.
*  (ADMITTEDLY THEY COULD BE ZAPPED OFFLINE, BUT THIS WOULD MEAN THE
*  OPERATOR ALWAYS HAD TO VARY THEM ONLINE WHEN REQUIRED)
*
*
*  TO CAUSE MVT NIP TO USE THIS ROUTINE........
*      1. RECORD TSGVIPL IN LINKLIB.
*      2. FIND THE LABEL 'SKEDEXIT' IN IEAANIP0 ASSEMBLY AND ZAP THE
*         'IEEVIPL' USED IN THE LINK MACRO THERE, E.G....
*          NAME  IEANUC01  IEAANIP0
*          VERIFY  3A2C  C9C5C5E5C9D7D3  IEEVIPL
*          REP     3A2C  E3E2C7          TSG
*
         USING *,15
         STM   0,15,SAVE
         BALR  8,0
         USING *,8
         L     1,16
         L     2,40(1)       ADDRESS OF UCB LOOK-UP TABLE
UCBLOOP  LH    3,0(2)
         LTR   3,3
         BM    END
         BZ    NEXTUCB
         CLI   18(3),32      IS IT DA
         BNE   NEXTUCB
         CLI   28(3),0       HAS IT A VOL SER
         BNE   NEXTUCB
         NI    3(3),127      PUT IT OFFLINE IF IT IS A DISK DEVICE
*                            WITHOUT A VOL SER IN ITS UCB.
NEXTUCB  LA    2,2(2)
         B     UCBLOOP
END      DS    0H
         CNOP  0,4
         BAL   15,*+20
         DC    A(*+8)
         DC    A(0)
         DC    CL8'IEEVIPL'
         SVC   6             LINK TO IEEVIPL AS NIP WOULD HAVE DONE
         LM    0,15,SAVE
         BR    14
SAVE     DC    16F'0'
         END
