*        GENERAL DESCRIPTION
*        THE ZZHEX SUBROUTINE WILL CONVERT CHARACTERS INTO A
*        HEXADECIMAL FORM.
*        E.G. A BECOMES C1
*        CALLING PROCEDURE
*                   CALL 'ZZHEX' USING WA-CHARS WB-OPAREA WC-INLEN.
*        WHERE
*             WA-CHARS IS THE NAME OF THE  AREA  CONTAINING  THE
*                      CHARACTERS TO BE CONVERTED.
*             WB-OPAREA IS THE NAME OF THE AREA WHERE THE CONVERTED
*                      CHARACTERS ARE TO BE PLACED. IT MUST BE AT LEAST
*                      TWICE THE LENGTH SPECIFIED IN WC-INLEN.
*             WC-INLEN IS A FOUR BYTE BINARY FIELD ALIGNED TO A FULL
*                      WORD SPECIFYING HOW MANY CHARACTERS ARE TO BE
*                      CONVERTED.
*        EXAMPLE
*        IN THIS EXAMPLE THE CONTENTS OF WA-CHARS ARE CONVERTED TO
*        HEXADECIMAL.
*             05 WA-CHARS     PICTURE X(8)  VALUE 'DUNLOP  '.
*             05 WB-OPAREA    PICTURE X(16).
*             05 WC-INLEN     PICTURE S9(9) COMP SYNC VALUE +8.
ZZHEX    CSECT
         IN    12,PARM=YES
         LM    2,4,0(1)
*                                  REG 2 ADDRESS OF CHARACTERS INPUT
*                                  REG 3 ADDRESS OF OUTPUT AREA
*                                  REG 4 ADDRESS OF I/P LENGTH BINARY
         L     4,0(4)              LENGTH OF INPUT AREA
         LA    6,240               PUT HEX F0 IN REG 6
         LA    7,250               PUT HEX FA IN REG 7
         LA    8,57                PUT HEX 39 IN REG 8
         SR    5,5                 CLEAR REG 5
LOOP     EQU   *
         IC    5,0(2)              ONE CHARACTER FOR CONVERSION
         SRL   5,4                 SHIFT OFF BOTTOM 4 BITS
         OR    5,6                 PUT F IN FRONT
         CR    5,7                 TEST IF FA THRU FF
         BL    *+6                 F0 THRU F9
         SR    5,8                 MAKE FA=C1 & FF=C6 ETC
         STC   5,0(3)              STORE TOP PART CONVERTED
         IC    5,0(2)
         OR    5,6                 PUT F BEFORE LOW 4 BITS
         CR    5,7
         BL    *+6
         SR    5,8
         STC   5,1(3)              STORE LOW PART CONVERTED
         LA    3,2(3)
         LA    2,1(2)
         BCT   4,LOOP
         OUT
         END
