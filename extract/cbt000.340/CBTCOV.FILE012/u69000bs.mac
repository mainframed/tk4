SYSPARM  TITLE '*SYSPARM* SUBROUTINE TO CREATE DESCRIPTORS FOR OS PARM *
               STRING'
* SYSPARM ANBEGIN
SYSPARM  CSECT
         B     28(0,15)
         DC    AL1(22)
         DC    CL8'SYSPARM-'
         DC    CL8'11/15/77'
         DC    CL6'-12.38'
         STM   14,12,12(13)
         LR    14,13
         USING SYSPARM,15
         LA    13,SA0001
         DROP  15
         USING SA0001,13
         ST    13,8(14)
         ST    14,4(13)
         B     SA0001+72
SA0001   DS    18F
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
***********************************************************************
***********************************************************************
**                                                                   **
**                                                                   **
**   SYSPARM  SUBROUTINE                                             **
**   -------  ----------                                             **
**                                                                   **
**                                                                   **
**   THIS IS USED TO SPLIT THE OS PARM STRING INTO ELEMENTS          **
**   IDENTIFIED BY DESCRIPTORS.                                      **
**                                                                   **
**   EACH DESCRIPTOR IS 4 BYTES LONG --                              **
**                                                                   **
**             1 BYTE OF LENGTH-1 (FOR EXECUTE). (CAN BE 255 4 NULL. **
**             3 BYTE POINTER                                        **
**                                                                   **
**   *ENTRY - R1 POINTS TO PARM STRING POINTER.                      **
**   *                                                               **
**   *RETURN- R1 POINTS TO 1ST DESCRIPTOR                            **
**                                                                   **
**                                                                   **
**   SYSPARM CAN BE CALLED BY SIMPLY CODING, ON ENTRY TO THE MAIN    **
**   MODULE:                                                         **
**                                                                   **
**       CALL  SYSPARM                                               **
**                                                                   **
**                                                                   **
***********************************************************************
***********************************************************************
         EJECT
         SPLIT ON
         L     R2,0(R1)                 A(PARM-LENGTH)
         LH    R3,0(R2)                 PARM LENGTH
         LA    R4,2(R2,R3)              A(END PARMS+1)
         LA    R2,2(R2)                 A(PARM-STRING-BYTE 1)
         GETMAIN R,LV=408               GETMAIN FOR 100 DESCRIPTORS
         L     R15,4(R13)
         ST    R1,24(R15)               SET R1 UP ON RETURN
         LR    R5,R1
         MVC   0(4,R5),=X'FF000000'     INIT ALL DESCRIPTORS TO...
         MVC   4(256,R5),0(R5)          .... X'FF000000'
         MVC   260(408-260,R5),256(R5)
         LR    R7,R2                    A(STRING-BYTE 1)
         LH    R6,=Y(-1)                FOR CTR-1
         DO  WHILE=(CR,R2,LT,R4)
           IF  CLI,0(R2),EQ,C','
           THEN
             ST    R7,0(R5)             STORE STRING ADDRESS
             STC   R6,0(R5)              AND LENGTH-1
             LA    R5,4(R5)             A(NEXT DESCRIPTOR)
             LH    R6,=Y(-1)            RESET LENGTH CTR
             LA    R2,1(R2)             BUMP PTR
             LR    R7,R2                A(STRING)
           ELSE
             LA    R2,1(R2)             BUMP CTR
             LA    R6,1(R6)             BUMP PTR
           ENDIF
         ENDDO
         IF  LTR,R3,R3,NZ
         THEN
           ST    R7,0(R5)
           STC    R6,0(R5)
         ENDIF
         L     R13,4(R13)
         RETURN (14,12),RC=0
         END
