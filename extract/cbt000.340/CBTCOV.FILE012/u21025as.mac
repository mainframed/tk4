         TITLE 'ROUTINE TO CLOSE AND FREE BUFFERS FOR D-A DATA SETS USEX
               D BY FORTRAN MAIN PROGRAM'
**    AUTHORS - ICI (MOND DIVISION)
ZFREDA   CSECT
         SAVE  (14,12),,*
         BALR  11,0
         USING *,11
*      SAVE AREA CHAINING PROVIDED FOR BDAM CHECK MODULE IGG019LI
         ST    13,SAVE+4
         LR    15,13
         LA    13,SAVE
         ST    13,8(15)
         XR    6,6                 CLEAR INDEX REG. 6
         LR    9,1                 SAVE POINTER TO PARAMETER LIST
         B     LOCATE
LOOP     LA    6,4(6)              +4 TO INDEX REG. 6
         LR    1,9                 RESTORE PARAMETER LIST POINTER
LOCATE   L     2,0(6,1)            LOCATE NEXT PARAM.
         MVC   FILE,0(2)           STORE VALUE
         L     3,FILE              R3 = PARAMETER VALUE
         LTR   3,3                 VALID DS REF.NO. ?
         BC    13,QEND             NO  -  IGNORE THIS PARAM.
         L     10,=V(IHCUATBL)     R10 = ADDRESS OF UNIT ASSIGNT. TABLE
         BCTR  3,0                 PARAM. VALUE MINUS 1
         SLA   3,4                 X 16
         CH    3,2(10)             CHECK  V  INSTALLATION MAX. NO.
         BNL    QEND               TOO HIGH -  IGNORE THIS PARAM.
         LA    3,8(3)              R3 + 8  =  OFFSET OF 'UBLOCK' ENTRY
         AR    10,3                R10 = UBLOCK ADDRESS
         TM    3(10),X'01'         IS FILE OPERATIVE ?
         BO    QEND                NO.   IGNORE THIS PARAM.
         TM    15(10),X'01'        DIRECT ACCESS FILE ?
         BO    QEND                NO - IGNORE THIS PARAMETER
         L    4,0(10)              YES.  R4 = UNIT BLOCK ADDRESS
         TM    1(4),X'10'          CHECK 'STATUSU' = OPEN FOR BDAM  ?
         BZ    QEND                NO.   IGNORE THIS PARAM.
         L     5,8(4)              R5 = ADDRESS OF CURRENT DECB
         BAL   8,CHECK             TEST FOR I/O COMPLETE
         TM    1(4),X'20'          2 BUFFERS IN USE ?  (STATUSU)
         BZ    FREEBUF             NO.   GO TO CLOSE ETC.
         L     5,16(4)             R5 = STATUSB & NXTBUF
         MVC   16(4,4),8(4)        EXCHANGE CURBUF & NXTBUF
         ST    5,8(4)
         BAL   8,CHECK             TEST FOR I/O COMPLETE ON 2ND BUFFER
*                  RELEASE BUFFER STORAGE & CLOSE FILE
FREEBUF  DS    0H
         LA    4,80(4)             YES.   R4 = DCB ADDRESS
         CLOSE ((4))
         FREEPOOL ((4))
*                  RELEASE UNIT BLOCK STORAGE
         L     1,0(10)             R1 = ADDRESS OF UNIT BLOCK
         NI    1(1),X'6F'          CLEAR 'OPEN' BITS IN 'STATUSU'
         LA    0,184               R0 = LENGTH OF UNIT BLOCK
         FREEMAIN R,LV=(0),A=(1)
         OI    3(10),X'01'         SET 'FILE INOPERATIVE' IN U-A TABLE
QEND     LR    1,9                 LOCATE PARAMETER LIST
         AR    1,6                 LOCATE LAST PARAM. USED
         TM    0(1),X'80'          IS IT THE LAST PARAM. PASSED ?
         BZ    LOOP                NO.  TRY NEXT DATA SET
EXIT     DS    0H                  RETURN TO FORTRAN MODULE
         L     13,4(13)
         RETURN (14,12),T,RC=0
*                  TEST FOR COMPLETION OF I/O
CHECK    TM    8(4),X'20'          INSPECT 'STATUSA' FOR 'CHECK' ISSUED
         BCR   1,8                 YES.   EXIT
         CHECK (5)
         OI    8(4),X'20'          INDICATE 'CHECK' ISSUED IN 'STATUSA'
         BR    8                   EXIT
FILE     DS    F
SAVE     DS    18F          SAVE AREA FOR BDAM CHECK MODULE USE.
         LTORG
         END
