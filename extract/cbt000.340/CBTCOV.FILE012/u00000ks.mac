PRINFILE CSECT
*
*  THIS PROGRAM IS DESIGNED TO PRINT ALL OR ANY SELECTED PART OF A DATA
*  SET (SEQUENTIAL, ISAM, BDAM) IN ONE OF TWO FORMATS CONTROLLED BY
*  THE PARM ON THE EXEC CARD.
*        1  CHARACTER FORMAT - 120 CHARACTERS PER LINE, WITH ALL
*           'UNPRINTABLE' CHARACTERS TRANSLATED TO SPACES.
*        2  'HEXEDIC' FORMAT - 40 CHARACTERS PER LINE IN BOTH HEX (FOUR
*           COLUMNS OF TEN CHARS) AND EBCDIC (WITH 'UNPRINTABLE' CHARS
*           TRANSLATED TO SPACES).
*  THE DEFAULT FORMAT IS 'HEXEDIC'.  TO OBTAIN CHARACTER FORMAT THE
*  FIRST CHARACTER OF THE PARM FIELD MUST BE A 'C'.
*  THE DEFAULT FOR RECORDS TO BE SELECTED IS THE FIRST 1000 RECORDS
*  (OR TO EOF, IF EARLIER).  TO SELECT DIFFERENTLY YOU ENTER EITHER
*  ONE OR TWO PARAMETERS IN THE PARM FIELD THUS (M AND N ARE RECORD
*  NUMBERS, 'C' AND 'ALL' ARE ACTUAL CHARACTERS)........
*
*  PARM=M      PRINT THE FIRST M RECORDS IN HEXEDIC.
*  PARM='C,M'  PRINT THE FIRST M RECORDS IN CHARACTER
*  PARM='M,N'  PRINT FROM RECORD M TO RECORD N IN HEXEDIC
*  PARM='C,M,N'  PRINT FROM M TO N IN CHARACTER
*  PARM=ALL    PRINT THE WHOLE FILE (WHICH MAY BE A CONCATENATION OF
*              SEVERAL FILES) IN HEXEDIC
*  PARM='C,ALL'  PRINT THE WHOLE FILE IN CHARACTER
*  PARM='M,ALL'  PRINT FROM RECORD M TO EOF IN HEXEDIC
*  PARM='C,M,ALL'  PRINT FROM RECORD M TO EOF IN CHARACTER
*
*  DEBLOCKING OF THE INPUT IS IN ACCORDANCE WITH WHATEVER DCB INFO IS
*  MADE AVAILABLE VIA LABELS OR THE PRINT DD CARD DEFINING THE INPUT
*  A STANDARD LABEL, CATALOGUED FILE WOULD BE CORRECTLY DEBLOCKED IN
*  RESPONSE TO ......
*    //PRINT  DD  DSN=FILEX,DISP=SHR
*  YOU MUST PROVIDE THE INFORMATION FOR FILES WITH LESS SELF-IDENTIFIC
*  -ATION.  IF YOU HAVE A TAPE OF COMPLETELY UNKNOWN DATA CONTENT (YOU
*  MUST KNOW, OR FIND BY TRIAL AND ERROR, THE TRACK, DENSITY, AND MODE
*  OF THE TAPE) YOU CAN USE THE FOLLOWING JCL TO PRINT THE WHOLE OF THE
*  FIRST DATA FILE REGARDLESS OF WHETHER IT IS SL OR NL (BEGINNING WITH
*  OR WITHOUT A TAPE-MARK).....
*    //PRINT  DD  DSN=X,UNIT=(2400,,DEFER),DISP=(SHR,PASS),
*    //           VOL=SER=TAPEX,LABEL=(,BLP),DCB=(RECFM=U,
*    //           BLKSIZE=10000)
*    //        DD DSN=Y,UNIT=AFF=PRINT,VOL=REF=*.PRINT,
*    //           DCB=(*.PRINT),LABEL=(2,NL),DISP=(SHR,KEEP)
*
*  OUTPUT IS DEFINED BY A 'PRINTER DD' CARD AND USES QSAM IF NOT DIRECT
*  TO A PRINTER, OR AN EXCP CHAINED PROGRAM IF DIRECT TO A PRINTER.
*
*  THE DSCB OF A DISK DATA SET IS ALWAYS PRINTED (DATA PART OF F1) AT
*  THE HEAD OF THE LISTING.  FOR TAPE OR CARDS THE DSCB IS A DUMMY
*  WITH ONLY THE LRECL, RECFM, BLKSIZE FIELDS BEING VALID.
*
*  SAMPLE JCL TO PRINT RECORDS 100 TO 150 OF AN ISAM FILE IN HEXEDIC
*    //LIST  EXEC  PGM=PRINFILE,PARM='100,150'
*    //PRINT  DD  DSN=ISAMFILE,DISP=SHR,VOL=SER=VOL123,UNIT=2314,
*    //           DCB=DSORG=IS
*    //PRINTER  DD  SYSOUT=A
*
*  SAMPLE JCL TO PRINT AN OBJECT DECK OF CARDS....
*    //LISTCARD EXEC PGM=PRINFILE,PARM=ALL
*    //PRINT  DD  *
*    ...  OBJECT  DECK  ......
*    /*
*    //PRINTER  DD  SYSOUT=A
*
*
*        BEGIN  BASE=(11,12)
*  EXPANSION OF RESPOND TSG MACRO
         STM   14,12,12(13)
         BALR  11,0
         USING *,11,12
IHB1     EQU   *
         L     12,IHB1A
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         B     IHB1B
IHB1A    DC    A(IHB1+4096)
IHB1B    EQU   *
*  END OF MACRO  EXPANSION
         LR    10,1
         L     2,16                          CVT ADDRESS
         L     2,0(2)                        TCB DOUBLE-WORD ADDRESS
         L     2,4(2)                        CURRENT TCB ADDRESS
         L     2,12(2)                       TIOT ADDRESS
         LA    2,24(2)                       FIRST DDNAME
         SR    3,3
         STM   2,3,REGS
FINDTIOE CLI   0(2),X'00'
         BE    DDNF
         CLC   4(8,2),=C'PRINTER '
         BE    DDFOUND
         IC    3,0(2)
         AR    2,3
         B     FINDTIOE
DDNF     ABEND 8
DDFOUND  OPEN  (OUT,(OUTPUT))
         OI    S1+1,X'F0'
         OI    S2+1,X'F0'
         OI    NOMORE+1,X'F0'
         PUT   OUT,C11
         PUT   OUT,C12
         LM    2,3,REGS
FINDUCB  CLI   0(2),X'00'
         BE    NOTFOUND
         CLC   4(8,2),=C'PRINT   '
         BE    UCBFOUND
         IC    3,0(2)
         AR    2,3
         B     FINDUCB
NOTFOUND PUT   OUT,ERRM
         B     ENDA
UCBFOUND L     2,16(2)
         CLI   18(2),X'20'
         BNE   SEQ
         RDJFCB (DISK)
         OI    TAPESW+1,X'F0'
         OBTAIN  DSCBCAM
         B     *+4(15)
         B     FOUNDLAB
         B     NOVOL
         B     NODSCB
         B     NOREAD
FOUNDLAB TM    LABEL+38,X'80'
         BO    IS
SEQ      OPEN  (DISK)
SETHEAD  LA    4,LINE1
         LA    2,LABEL
         LA    3,96
TAPESW   BC    0,START
         MVC   LABEL+38(2),DISK+26    DSORG
         MVC   LABEL+42(2),DISK+62   BLKSIZE
         MVC   LABEL+44(2),DISK+82   LRECL
         MVC   LABEL+40(1),DISK+36   RECFM
         B     START
         EJECT
*
*        ALLOWABLE PARM FIELD ENTRIES ARE........
*              C                   IF C IS FIRST PARAMETER THEN PRINT
*                                  IN CHARACTER FORM ONLY - 120 CH/LINE
*              ALL                 PRINT TO EOF
*              N                   PRINT START TO RECORD N.
*              M,N                 PRINT FROM RECORD M TO N.
*              M,ALL               PRINT FROM RECORD M TO EOF.
*
SETPARM  L     10,0(10)
         LH    3,0(10)
         LTR   3,3
         BC    8,START
         LR    4,3
         LA    5,2(10)
         CLI   0(5),C'C'
         BNE   CLIA
         MVI   MOVETR+3,X'00'
         MVI   TRANS+3,X'00'
         MVC   SULA,SULB
         MVC   SULC,SULD
         MVC   LINE12(132),CLINE12
         MVC   HEXTOEB-6(4),NOHEX
         MVC   STEPDOWN+4(4),LA2
         L     1,NEXTLINE
         MVC   0(132,1),LINE12
         LA    1,266(1)
         ST    1,NEXTLINE
         LA    5,2(5)
         SH    4,=H'2'
         LTR   4,4
         BC    12,START
CLIA     CLI   0(5),C','
         BE    CLIB
         MVC   AX,AX+1
         MVC   AA,0(5)
         LA    5,1(5)
         BCT   4,CLIA
         MVC   AX,AX+1
         B     CLID
CLIB     LA    5,1(5)
         MVC   AX,AX+1
         BCT   4,CLIC
         B     CLID
CLIC     MVC   B,B+1
         MVC   BB,0(5)
         LA    5,1(5)
         BCT   4,CLIC
         MVC   B,B+1
CLID     CLC   AX+5(3),=C'ALL'
         BNE   *+14
CLIE     MVC   COUNTREC+6(4),BRECNO
         B     START
         MVZ   C,AX
         CLC   C,D
         BNE   INCPARM
         PACK  DISPLAY,AX
         CVB   2,DISPLAY
         BCTR  2,0
         ZAP   THOUS,DISPLAY
         AP    THOUS,=P'1000'
         CLC   B,D
         BNE   *+14
         ZAP   THOUS,DISPLAY
         B     START
         CLC   B+5(3),=C'ALL'
         BNE   *+14
         MVC   COUNTREC+6(4),BRECNO
         B     SKIP-10
         MVZ   C,B
         CLC   C,D
         BNE   INCPARM
         PACK  DISPLAY,B
         ZAP   THOUS,DISPLAY
         LTR   2,2
         BC    12,START
         L     4,NEXTLINE
SKIP     GET   DISK
         AP    RNUMBER,=P'1'
         BCT   2,SKIP
START    L     4,NEXTLINE
         SR    5,5
         L     6,=F'240'
         L     7,=F'250'
         L     8,=F'57'
         L     9,=F'15'
         LA 1,133(4)
         ST    1,NEXTLINE
BR1      B     PRESET
RECORD    GET  DISK
         LR    2,1
         LH    3,DISK+82
COUNTREC AP    RNUMBER,=P'1'
         CP    RNUMBER,THOUS
         BNH   RECNO
         MVC   0(132,4),LINE11
         MVC   0(34,4),ENDMSG2
         LA    4,133(4)
         B     ENDOUT
RECNO    MVC   0(6,4),EDMASK
         ED    0(6,4),RNUMBER+1
PRESET   LA     4,8(4)
SETUPL   LR    15,3
SULA     S     15,=F'40'
          ST   15,REMCNT
         BC    4,*+8
SULC     L     3,=F'40'
         MVC   0(124,4),LINE11
         BCTR  3,0
         EX    3,MOVETR
         EX    3,TRANS
         LA    3,1(3)
         SP    LNCNT,LNCNT
HEXTOEB  IC    5,0(2)
         SRL   5,4
         OR    5,6
         CR    5,7
         BL    *+6
         SR    5,8
         STC   5,0(4)
         IC    5,0(2)
         OR    5,6
         CR    5,7
         BL    *+6
         SR    5,8
         STC   5,1(4)
         LA    4,2(4)
         AP    LNCNT,=P'1'
         CP    LNCNT,=P'10'
         BL    *+14
         LA    4,1(4)
         SP    LNCNT,=P'10'
STEPDOWN LA    2,1(2)
         BCT   3,HEXTOEB
         L     4,NEXTLINE
         C     4,=A(LINE11)
         BL    NEWLINE
S1       BC    0,WRITE1
         EXCP  CHURNOUT
         WAIT  ECB=A
         CLI   A,X'7F'
         BNE   ABEND
         NI    A,X'3F'
BACK     LA    4,LINE1
         AP    PCNT,=P'1'
         CP    PCNT,=P'6'
         BL    NEWLINE
         SP    PCNT,=P'6'
S2       BC    0,NP2
         EXCP  NEWPAGE
         WAIT  ECB=A
         CLI   A,X'7F'
         BNE   ABEND
         NI    A,X'3F'
NEWLINE  LA    15,133(4)
         ST    15,NEXTLINE
         L     3,REMCNT
         LTR   3,3
ENDDSCB  BC    12,POSTDSCB
         BC    12,RECORD
         MVC   0(8,4),LINE11
         LA    4,8(4)
         B     SETUPL
MOVETR   MVC   84(1,4),0(2)
TRANS    TR    84(1,4),CHARTAB
POSTDSCB NI    ENDDSCB+1,X'0F'
         NI    BR1+1,X'0F'
         B     SETPARM
NEXTLINE DC    A(LINE1)
REMCNT   DC    F'0'
A        DC    F'0'
EOF      MVC   0(132,4),LINE11
         MVC   0(17,4),ENDMSG1
         LA    4,133(4)
ENDOUT   C     4,=A(LINE11)
         BE    NOMORE
         MVC   0(132,4),LINE11
         LA    4,133(4)
         B     ENDOUT
NOMORE   BC    0,WRITE2
         EXCP  CHURNOUT
         WAIT  ECB=A
         CLI   A,X'7F'
         BNE   ABEND
CLOSE    CLOSE (DISK)
         FREEPOOL  DISK
ENDA     CLOSE (PRINTER)
*ENDIT   GOBACK
*  EXPANSION OF RESPOND TSG MACRO
ENDIT    L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
*  END OF MACRO EXPANSION
INCPARM  PUT   OUT,INCORREC
         B     CLOSE
INCORREC DC    X'19'
         DC    CL132'PARM FIELD CONTAINS INVALID NON-NUMERIC CHARACTER'
AX       DC    C'00000000'
AA       DC    C'0'
B        DC    C'00000000'
BB       DC    C'0'
C        DC    C'00000000'
D        DC    C'00000000'
         RETURN (14,12)
ABEND    ABEND 4,DUMP
DISK     DCB   DSORG=PS,MACRF=(GL),DDNAME=PRINT,EODAD=EOF,EROPT=ACC,   X
               EXLST=X
PRINTER  DCB   MACRF=(E),DDNAME=PRINTER,DEVD=DA
OUT      DCB   DSORG=PS,MACRF=(PM),DDNAME=PRINTER,RECFM=FM,            X
               LRECL=133,BLKSIZE=133
CCW1     CCW   9,LINE1,X'60',132
         CCW   9,LINE2,X'60',132
         CCW   9,LINE3,X'60',132
         CCW   9,LINE4,X'60',132
         CCW   9,LINE5,X'60',132
         CCW   9,LINE6,X'60',132
         CCW   9,LINE7,X'60',132
         CCW   9,LINE8,X'60',132
         CCW   9,LINE9,X'60',132
         CCW   9,LINE10,X'20',132
CCW2     CCW   139,LINE11,X'60',132
         CCW   17,LINE12,X'20',132
CHURNOUT DC    X'40'
         DC    4X'00'
         DC    AL3(A)
         DC    2F'0'
         DC    X'00'
         DC    AL3(CCW1)
         DC    X'00'
         DC    AL3(PRINTER)
         DC    2F'0'
         DC    2F'0'
NEWPAGE  DC    X'40'
         DC    4X'00'
         DC    AL3(A)
         DC    2F'0'
         DC    X'00'
         DC    AL3(CCW2)
         DC    X'00'
         DC    AL3(PRINTER)
         DC    2F'0'
         DC    2F'0'
C1       DC    X'09'
LINE1    DC    CL132' DSCB '
         DC    X'09'
LINE2    DC    CL132' '
         DC    X'09'
LINE3    DC    CL132' '
         DC    X'09'
LINE4    DC    CL132' '
         DC    X'09'
LINE5    DC    CL132' '
         DC    X'09'
LINE6    DC    CL132' '
         DC    X'09'
LINE7    DC    CL132' '
         DC    X'09'
LINE8    DC    CL132' '
         DC    X'09'
LINE9    DC    CL132' '
         DC    X'09'
LINE10   DC    CL132' '
C11      DC    X'89'
LINE11   DC    CL132' '
C12      DC    X'11'
LINE12   DC    C'RECORD  01020304050607080910 '
         DC    C'11121314151617181920 21222324252627282930 '
         DC    C'31323334353637383940 '
         DC    C'1234567890123456789012345678901234567890'
CHARTAB   DC   75C' '
          DC   C'.<(+×&&'
          DC   9C' '
          DC   C'!$*);^-/'
          DC   9C' '
          DC   C',%_>'
          DC   11C' '
          DC   C':#@''='
          DC   66C' '
          DC   C'ABCDEFGHI'
          DC   7C' '
          DC   C'JKLMNOPQR'
          DC   8C' '
          DC   C'STUVWXYZ'
          DC   6C' '
          DC   C'0123456789'
          DC   6C' '
DISPLAY  DS    D
          DC   C' '
LNCNT    DC    PL2'0'
PCNT     DC    P'0'
RNUMBER  DC    PL4'0'
EDMASK   DC    X'402020202021'
THOUS    DC    PL4'1000'
ENDMSG1  DC    C'END OF INPUT FILE'
ENDMSG2  DC    C'TERMINATED AFTER REQUESTED RECORDS'
PACK     PACK  DISPLAY,2(1,10)
PACK1    PACK  DISPLAY,2(1,5)
PACK2    PACK  THOUS,2(1,10)
NOVOL    MVC   ERRMSG+45(13),=C'VOLUME NOT ON'
         B     PUTERRM
NODSCB   MVC   ERRMSG+45(13),=C'OF READ ERROR'
         B     PUTERRM
NOREAD   MVC   ERRMSG+45(13),=C'NOT IN VTOC  '
PUTERRM  PUT   OUT,ERRMSG
         L     13,4(13)
         RETURN (14,12)
IS       MVC   SKIP(4),BSKIP
         MVC   RECORD(4),BRECORD
         MVC   CLOSE(4),BCLOSE
         MVC   CLOSE2(4),BCLOSE2
         OPEN  (ISAM)
         B     SETHEAD
BSKIP    B     ISSKIP
ISSKIP   GET   ISAM
         AP    RNUMBER,=P'1'
         BCT   2,ISSKIP
         B     START
BRECORD  B     ISGET
ISGET    GET   ISAM
         LR    2,1
         LH    3,ISAM+82
         B     COUNTREC
BCLOSE   B     ISCLOSE
ISCLOSE  CLOSE (ISAM,,PRINTER)
         FREEPOOL  ISAM
         B     ENDIT
BCLOSE2  B     ISCLOSE2
ISCLOSE2 CLOSE (ISAM,,OUT)
         FREEPOOL ISAM
         FREEPOOL OUT
         B     ENDIT
WRITE1   LA    3,10
         LA    10,C1
P1       PUT   OUT,(10)
         LA    10,133(10)
         BCT   3,P1
S3       BC    0,CLOSE2
         B     BACK
WRITE2   OI    S3+1,X'F0'
         B     WRITE1
NP2      PUT   OUT,C11
         PUT   OUT,C12
         B     NEWLINE
CLOSE2   CLOSE (DISK,,OUT)
         FREEPOOL DISK
         FREEPOOL OUT
         B     ENDIT
ISAM     DCB   DSORG=IS,MACRF=(GL),DDNAME=PRINT,EODAD=EOF
         DS    0F
X        DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
DSCBCAM  CAMLST SEARCH,JFCB,JFCB+118,LABEL
REGS     DC    2F'0'
BRECNO   B     RECNO
ERRM     DC    X'19'
         DC    CL132'NO DD CARD WITH DDNAME PRINT (FOR INPUT FILE'
ERRMSG   DC    X'19'
         DC    CL132'PRINFILE GIVES UP. CANNOT READ DSCB BECAUSE 1234567
               789ABCD '
SULB     S     15,=F'120'
SULD     L     3,=F'120'
LA2      LA    2,120(2)
NOHEX    B     STEPDOWN+4
CLINE12  DC    C'RECORD  1   5   10   15   20   25   30   35   40   45'
         DC    C'   50   55   60   65   70   75   80   85   90   95'
         DC    C'  100  105  110  115  120    '
         LTORG
LABEL    DS    0D
         DC    350C' '
         END
