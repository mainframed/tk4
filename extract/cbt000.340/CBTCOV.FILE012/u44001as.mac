         TITLE 'ISCOPY - SELECTIVE ISAM PRINT/COPY UTILITY'
*       *********************************************************
*       *                                                       *
*       *                     ISCOPY  V 1.0                     *
*       *                                                       *
*       *      B.T.C. LONDON                P.E.S  AUG 72       *
*       *                                                       *
*       *********************************************************
*       *                                                       *
*       *      ISCOPY IS A UTILITY PROGRAM DESIGNED TO          *
*       *      OPERATE ON INDEXED SEQUENTIAL FILES.             *
*       *      PROCESSING IS CONTROLLED BY MEANS OF FREE FORMAT *
*       *      PARAMETER CARDS. PARAMETERS ARE DEFINED BY       *
*       *      KEYWORDS FOLLOWED BY THEIR OPERANDS AND MUST BE  *
*       *      SEPARATED BY COMMAS. A NEW CARD MAY BE STARTED   *
*       *      AFTER A COMMA AND COMMENTS MAY BE WRITTEN        *
*       *      FOLLOWING A BLANK.                               *
*       *                                                       *
*       *      SUPPORTED KEYWORDS ARE :-                        *
*       *           INPUT=    THE NAME OF THE DD CARD           *
*       *                     SPECIFYING THE INPUT I.S. FILE.   *
*       *                     DEFAULT IS SYSUT1.                *
*       *           OUTPUT=   THE NAME OF THE DD CARD           *
*       *                     SPECIFYING THE OPTIONAL OUTPUT    *
*       *                     FILE (COPY OPERATION).            *
*       *                     THIS FILE MUST BE ACCESSABLE BY   *
*       *                     EITHER QSAM OR QISAM.             *
*       *           PRINT=    ON (DEFAULT)                      *
*       *                     OFF                               *
*       *                     CONV CONVERT TO HEXADECIMAL       *
*       *           KEY1=     A STRING DEFINING THE RECORD WITH *
*       *                     WHICH PROCESSING IS TO COMMENCE,  *
*       *                     OR THE LITERAL STRING FIRST.      *
*       *           KEY2=     A STRING DEFINING THE RECORD      *
*       *                     AFTER WHICH PROCESSING IS TO      *
*       *                     CEASE, OR THE LITERAL STRING LAST.*
*       *           COUNT=    THE NUMBER OF RECORDS TO BE       *
*       *                     PROCESSED.                        *
*       *           TYPE=     A CODE SPECIFYING THE KEY         *
*       *                     CONVERSION REQUIRED.              *
*       *                     C  (DEFAULT) NO CONVERSION        *
*       *                     X  CONVERT TO HEXADECIMAL         *
*       *                     N  TREAT AS NUMERIC  (RIGHT       *
*       *                     JUSTIFY AND PAD WITH ZEROES)      *
*       *                     P  CONVERT TO PACKED FORMAT       *
*       *                                                       *
*       *      IF A KEY STRING IS SHORTER THAN THE KEY FIELD ON *
*       *      THE I.S. FILE, THEN THE ACTION TAKEN DEPENDS ON  *
*       *      THE TYPE OF FIELD :-                             *
*       *      C OR X : THE FIELDS ARE COMPARED ONE BYTE AT A   *
*       *               TIME, STARTING WITH THE LEFTMOST BYTE   *
*       *               UNTIL ALL THE STRING BYTES HAVE BEEN    *
*       *               EXAMINED. UNUSED FILE KEY BYTES ARE     *
*       *               IGNORED.                                *
*       *********************************************************
         EJECT
*       *********************************************************
*       *                                                       *
*       *      N OR P : THE STRING IS EXPANDED TO THE SIZE OF   *
*       *               THE FILE KEY AND THE FIELDS ARE         *
*       *               COMPARED.                               *
*       *      NOTE  STRINGS CONTAINING EMBEDDED BLANKS OR      *
*       *            SPECIFYING KEYS CONTAINING THE CHARACTERS  *
*       *            FIRST OR LAST SHOULD BE ENCLOSED IN        *
*       *            APOSTROPHES. STRINGS CONTAINING EMBEDDED   *
*       *            APOSTROPHES SHOULD BE SPECIFIED IN         *
*       *            HEXADECIMAL.                               *
*       *                                                       *
*       *      THE RECORD WHOSE KEY IS EQUAL TO THAT SPECIFIED  *
*       *      IN THE KEY1 OPERAND IS DETERMINED, AND THAT AND  *
*       *      SUBSEQUENT RECORDS ARE OBTAINED.                 *
*       *                                                       *
*       *      IF PRINT ON HAS BEEN SPECIFIED, THE SELECTED     *
*       *      RECORDS ARE PRINTED IN HEXADECIMAL WITH THE      *
*       *      CHARACTER EQUIVALENT OF THE CODE BELOW.          *
*       *      IF AN OUTPUT DATA SET HAS BEEN SPECIFIED, THE    *
*       *      SELECTED RECORDS ARE WITTEN TO THIS FILE.        *
*       *                                                       *
*       *      PROCESSING IS TERMINATED WHEN A RECORD IS        *
*       *      ENCOUNTERED WHOSE KEY IS GREATER OR EQUAL        *
*       *      TO THAT SPECIFIED BY THE KEY2 OPERAND, OR        *
*       *      WHEN THE NUMBER OF RECORDS SPECIFIED BY THE COUNT*
*       *      OPERAND HAVE BEEN PROCESSED.                     *
*       *                                                       *
*       *********************************************************
         SPACE 3
*              V 1.0 3/3/73  DCB BUILDER USES GETMAIN INSTEAD OF LOAD
         EJECT
         MACRO
&LABEL   ERRTAB &NUM                   BUILD ERROR MESSAGE BRANCH TABLE
         LCLA  &N
         LCLC  &SEQ
.A0      ANOP
&N       SETA  &N+1
         AIF   (&N GT &NUM).A3
         AIF   (&N GE 10).A1
&SEQ     SETC  '0&N'
         AGO   .A2
.A1      ANOP
&SEQ     SETC  '&N'
.A2      ANOP
ERR0&SEQ   LA    R1,MSG0&SEQ
         B     &LABEL
         AGO   .A0
.A3      MEND
         SPACE 5
         MACRO
         ERRMSG &LIT                   SET UP ERROR MESSAGE LITERALS
         GBLA  &N
         LCLC  &SEQ
&N       SETA  &N+1
.A0      ANOP
         AIF   (&N GE 10).A1
&SEQ     SETC  '0&N'
         AGO   .A2
.A1      ANOP
&SEQ     SETC  '&N'
.A2      ANOP
MSG0&SEQ   DC    C'&SEQ'
         DC    AL1(L'MSGL0&SEQ)
MSGL0&SEQ  DC    C&LIT
         MEND
         EJECT
*                      REGISTER USAGE
*
*
R0       EQU   0                   TEMP
R1       EQU   1                   TEMP
R2       EQU   2                   CURRENT INPUT POSITION
R3       EQU   3                   TEMP
R4       EQU   4                   TO ADDRESSES
R5       EQU   5                   FROM ADDRESSES
R6       EQU   6                   LENGTH MODIFIERS
R7       EQU   7                   SV CONTROL BLOCK ADD
R8       EQU   8                   SV CONTROL BLOCK ADD
R9       EQU   9                   DCB BASE
R10      EQU   10                  SECONDARY PROGRAM BASE
R11      EQU   11                  INTERNAL RETURN ADD
R12      EQU   12                  PRIMARY PROGRAM BASE
R13      EQU   13                  SAVE AREA ADD
R14      EQU   14                  EXTERNAL RETURN ADD
R15      EQU   15                  E.P. ADD
         PRINT ON,NOGEN
         SPACE 10
ISCOPY   CSECT
         STM   R14,R12,12(R13)
         BALR  R12,0
         USING BASE,R12,R10
BASE     L     R10,BASE2ADD
         ST    R13,SAVEAREA+4
         LR    R11,R13
         LA    R13,SAVEAREA
         ST    R13,8(R11)
         OPEN  (PARAMDCB,(INPUT),PRTDCB,(OUTPUT))
         BAL   R11,CLR001              PRINT TITLE
         B     ISP001
*
BASE2ADD DC    A(BASE+4096)
         SPACE 5
ISP001   BAL   R11,PRM001              GET A PARAM CARD
         BAL   R11,OPN001              OPEN INPUT AND BUILD OUTPUT DCB
         BAL   R11,KEY001              VALIDATE KEY FIELDS
         BAL   R11,STL001              SETL AND OPEN OUTPUT DCB
         BAL   R11,RDI001              READ ISAM LOOP
         BAL   R11,ENP001              END OF PASS ROUTINE
         B     ISP001                  LOOP
         SPACE 5
ISPEOF   EQU   *                       END OF INPUT FILE
         MVC   PRMSGLIT(12),=C'END OF INPUT'
         MVC   PRMSGNO2,=C'80'
         BAL   R11,PRE001
         CLOSE (PRTDCB,,PARAMDCB)
         LA    R9,INDCB
         BAL   R11,CLO001              CLOSE IF OPEN
         BAL   R11,CLL001              CLOSE ANY OUTPUT DCBS
         L     R13,4(R13)
         L     R14,12(R13)
         L     R15,RETCD
         LM    R0,R12,20(R13)
         BR    R14
         EJECT
PRM001   EQU   *                       READ AND INTERPRET PARAMS
         ST    R11,PRM11SAV
         MVI   MODESW,X'10'            SHOW INTERPRET PHASE
         MVI   TYPESW,X'00'
         MVI   DDSW,X'00'
         MVC   HLINLEN,=H'132'         DEFAULT PRINT LINE LENGTH
PRM002   READ  PRMDECB,SF,PARAMDCB,CARDAREA  OBTAIN PARAM CARD
         CHECK PRMDECB
         MVC   PRTCARD(80),CARDAREA
         BAL   R11,PRT001
         LA    R2,CARDAREA
         LA    R1,CARDAREA+72
         ST    R1,ENDCARD
PRM003   CLI   0(R2),C' '              Q. START OF PARAMS
         BNE   PRM004                  BRANCH IF YES
PRM0031  LA    R2,1(R2)                BUMP
         C     R2,ENDCARD              Q. END OF CARD
         BNL   PRM002                  READ NEW CARD IF YES
         B     PRM003                  ELSE LOOP
*
PRM004   NI    TYPESW,X'FE'            SET OFF QUOTE FLAG
         CLC   0(5,R2),=C'KEY1='
         BE    PRM0051
         CLC   0(5,R2),=C'KEY2='
         BE    PRM0052
         CLC   0(6,R2),=C'COUNT='
         BE    PRM006
         CLC   0(5,R2),=C'TYPE='
         BE    PRM007
         CLC   0(6,R2),=C'INPUT='
         BE    PRM0081
         CLC   0(7,R2),=C'OUTPUT='
         BE    PRM0082
         CLC   0(6,R2),=C'PRINT='
         BE    PRM0085
         B     ERR001                  ERROR UNSUPPORTED KEYWORD
*
PRM0051  TM    MODESW,X'80'            Q. DUPLICATE PARAM       KEY1
         BNZ   ERR002                  ERROR IF YES
         OI    MODESW,X'80'
         LA    R7,KEYDATA1
         CLC   5(5,R2),=C'FIRST'
         BNE   PRM0053
         OI    TYPESW,X'80'
         LA    R2,10(R2)
         B     PRM009
*
PRM0052  TM    MODESW,X'60'            Q. DUPLICATE PARAM     KEY2
         BNZ   ERR002                  ERROR IF YES
         OI    MODESW,X'40'
         LA    R7,KEYDATA2
         CLC   5(4,R2),=C'LAST'
         BNE   PRM0053
         OI    TYPESW,X'40'
         LA    R2,9(R2)
         B     PRM009
*
PRM0053  LA    R2,5(R2)                CURRENT ADDRESS          (KEYS)
         CLI   0(R2),C''''             Q. IS KEY IN QUOTES
         BNE   PRM00531                BRANCH IF NO
         OI    TYPESW,X'01'            ELSE SHOW IT
         LA    R2,1(R2)                AND BUMP
PRM00531 BAL   R11,LPM001              OBTAIN LENGTH OF PARM
         LR    R8,R7                   CONTROL BLOCK ADD
         LA    R1,1(R6)                LENGTH OF AREA
         BAL   R11,GTM001              GETMAIN
         EX    R6,KEYMOVE              MOVE KEY
         B     PRM009
*
PRM006   TM    MODESW,X'60'            Q. DUPLICATE PARAM       COUNT
         BNZ   ERR002                  ERROR IF YES
         OI    MODESW,X'20'
         LA    R2,6(R2)                CURRENT ADD
         BAL   R11,LPM001              OBTAIN LENGTH OF PARAM
         CH    R6,=H'15'               MAX LENGTH
         BNL   ERR003                  ERROR IF TOO LONG
         LR    R0,R4                   SET UP SV
         LA    R1,1(R6)
         STM   R0,R1,TKEYADD
         EX    R6,CNTPACK              PACK
         LA    R7,TKEYDATA
         BAL   R11,NUM001              Q. IS ENTRY NUMERIC
         B     PRM009
*
PRM007   TM    MODESW,X'0F'            Q.  DUPLICATE PARAM      TYPE
         BNZ   ERR002                  ERROR IF YES
         CLI   5(R2),C'N'              NUMERIC
         BE    PRM00711
         CLI   5(R2),C'P'              PACKED
         BE    PRM00712
         CLI   5(R2),C'X'              HEX
         BE    PRM00713
         CLI   5(R2),C'C'              CHARACTER (DEFAULT)
         BE    PRM00714
         B     ERR004                  ERROR
PRM00711 OI    MODESW,X'04'            NUMERIC
         B     PRM0072
PRM00712 OI    MODESW,X'08'            PACKED
         B     PRM0072
PRM00713 OI    MODESW,X'02'            HEX
         B     PRM0072
PRM00714 OI    MODESW,X'01'            CHAR
         B     PRM0072
PRM0072  LA    R2,6(R2)
         B     PRM009
*
PRM0081  TM    DDSW,X'80'              Q. DUPLICATE PARAM       INPUT
         BNZ   ERR002                  ERROR IF YES
         OI    DDSW,X'80'
         LA    R2,6(R2)                CURRENT ADD
         LA    R5,INDDNAM
         B     PRM0083
*
PRM0082  TM    DDSW,X'40'              Q. DUPLICATE PARAM       OUTPUT
         BNZ   ERR002                  ERROR IF YES
         OI    DDSW,X'40'
         LA    R2,7(R2)                CURRENT ADD
         LA    R5,OUTDDNAM
         B     PRM0083
*
PRM0083  BAL   R11,LPM001              GET LENGTH OF DD NAME PARAM
         CH    R6,=H'7'                Q. INVALID LENGTH
         BH    ERR005                  ERROR IF YES
         MVC   0(8,R5),=CL8' '
         EX    R6,KEYMOVE              MOVE DD NAME
         B     PRM009
*
PRM0085  TM    DDSW,X'20'              Q. DUPLICATE PARAM       PRINT
         BNZ   ERR002                  ERROR IF YES
         OI    DDSW,X'20'
         CLC   6(2,R2),=C'ON'
         BE    PRM00852
         CLC   6(3,R2),=C'OFF'
         BE    PRM00851
         CLC   6(4,R2),=C'CONV'
         BNE   ERR006                  ERROR
         OI    DDSW,X'02'              SHOW PRINT CONVERSION
         MVC   HLINLEN,=H'66'          PRINT LINE LENGTH FOR CONV
         LA    R2,2(R2)
         B     PRM00852
PRM00851 OI    DDSW,X'01'              SHOW PRINT OFF
         LA    R2,1(R2)
PRM00852 LA    R2,8(R2)
         B     PRM009
*
PRM009   CLI   0(R2),C' '              Q. END OF PARMS
         BE    PRM0091                 BRANCH IF YES
         CLI   0(R2),C','              Q. VALID DELIMITER
         BNE   ERR016                  ERROR IF NO
         CLI   1(R2),C' '              Q. END OF CARD
         BE    PRM002                  IF SO GET NEW CARD
         B     PRM0031                 ELSE LOOP
PRM0091  TM    TYPESW,X'08'            Q. INTERPRET ERROR
         BO    ERP003                  ERROR HANDLER IF YES
         NI    MODESW,X'EF'            END OF INTERPRET PHASE
         L     R11,PRM11SAV
         BR    R11
         EJECT
OPN001   EQU   *                       OPEN INPUT AND BUILD OUTPUT DCB
         USING IHADCB,R9
         ST    R11,OPN11SAV
         TM    DDSW,X'80'              Q. INPUT DDNAME SPECIFIED
         BO    OPN002                  BRANCH IF YES
         MVC   INDDNAM,=CL8'SYSUT1'    USE DEFAULT IF NO
OPN002   LA    R9,INDCB
         CLC   CURINDD,INDDNAM         Q. CONTINUATION OF DATA SET
         BE    OPN003                  BRANCH IF YES
         BAL   R11,CLO001              ELSE CLOSE IF OPEN
         MVC   INDCB(L'INDCBDL),INDCBD AND RESET IT
         MVC   DCBDDNAM,INDDNAM        SET UP DDNAME
         DEVTYPE INDDNAM,DWORK         Q. HAS DD CARD BEEN PROVIDED
         LTR   R15,R15
         BNZ   ERR023                  ERROR IF NO
         LA    R5,INDDNAM
         BAL   R11,LSS001              Q. IS INPUT DD ON OUTPUT LIST
         BNE   OPN0021                 BRANCH IF NO
         TM    DCBOFLGS,X'10'          Q. IS DCB OPEN
         BNO   OPN0021                 BRANCH IF NO
         CLOSE ((R9),REREAD)           ELSE CLOSE IT
OPN0021  MVC   CURINDD,INDCB+40
         OPEN  (INDCB,(INPUT))
         XR    R1,R1
         IC    R1,INDCB+16             KEY LENGTH
         STH   R1,KEYLEN               SET UP KEY LENGTH
*
OPN003   TM    DDSW,X'40'              Q. OUTPUT DD SPECIFIED
         BNO   OPN009                  BRANCH IF NO
         CLC   INDDNAM,OUTDDNAM        Q. SAME DD SPECD FOR BOTH
         BE    ERR024                  ERROR IF YES
         L     R9,OUTDCBAD             CURRENT OUTPUT DCB
         LTR   R9,R9                   Q. VALID ADDRESS
         BZ    OPN004                  BRANCH IF UNUSED
         CLC   CUROUTDD,OUTDDNAM       Q. CONTINUATION OF DATA SET
         BE    OPN007                  BRANCH IF YES
OPN004   LA    R5,OUTDDNAM
         BAL   R11,LSS001              Q. IS THERE A DCB FOR THIS DD
         BE    OPN007                  IF SO ITS ADD IS IN R9
         BAL   R11,RJF001              READ JFCB
         OI    JFCBMASK+4,X'80'        WRITE JFCB BACK TO QUEUE
         TM    JFCDSORG,X'80'          Q. IS DSORG I.S.
         BO    OPN005                  BRANCH IF YES
         XR    R2,R2                   SHOW PS DCB REQUIRED
         B     OPN006
OPN005   LA    R2,4                    SHOW IS DCB REQUIRED
OPN006   BAL   R11,DCB001              BUILD DCB
         LR    R9,R1                   SAVE DCB ADD
         BAL   R11,SDB001              SET UP DCB PARMS
         L     R3,DCBLSTAD             LOAD START OF DCB LIST
         GETMAIN EU,LV=16,A=DCBLSTAD   GET CORE FOR DCBLIST ENTRY
         L     R2,DCBLSTAD             NEW ENTRY ADD
         USING DDCBLST,R2
         ST    R3,DLSTCH               MAINTAIN CHAIN
         ST    R9,DLSTDCB              DCB ADD
         MVC   DLSTDD,OUTDDNAM         DDNAME
         B     OPN008
OPN007   TM    DCBOFLGS,X'10'          Q. IS EXISTING DCB OPEN
         BO    OPN008                  BRANCH IF YES
         BAL   R11,RJF001              READ JFCB
         NI    JFCBIND2,X'3F'          SET DISP TO MOD
         OI    JFCBIND2,X'80'
OPN008   ST    R9,OUTDCBAD             SET UP CURRENT DCB ADD
OPN009   L     R11,OPN11SAV
         DROP  R2
         BR    R11
         DROP  R9
         EJECT
KEY001   EQU   *                       VALIDATE AND CONVERT KEYS
         ST    R11,KEY11SAV
         TM    MODESW,X'80'            Q. IS KEY1 PARAM PRESENT
         BZ    ERR007                  ERROR IF MISSING
         TM    MODESW,X'60'            Q. IS THERE A KEY2 OR COUNT
         BZ    ERR008                  ERROR IF NOT
         TM    TYPESW,X'80'            Q. KEY1 = FIRST
         BO    KEY0011                 BRANCH ROUND CONVERSION IF YES
         LA    R7,KEYDATA1
         BAL   R11,CNV001              CONVERT KEY1
         TM    MODESW,X'0C'            Q. PACKED OR NUMERIC
         BNZ   KEY0011                 BRANCH IF YES
         L     R0,KEYACT1              LENGTH OF KEY1
         CH    R0,KEYLEN               Q. FULL KEY GIVEN
         BE    KEY0011                 BRANCH IF YES
         OI    TYPESW,X'20'            ELSE SHOW KEY CLASS
         LA    R8,TKEYDATA             TEMPORARY CONTROL BLOCK
         LH    R1,KEYLEN               KEY LENGTH ON FILE
         BAL   R11,GTM001              GETMAIN
         MVI   0(R5),X'00'             SET AREA TO BINARY ZERO
         LH    R1,KEYLEN
         BCTR  R1,0
         BCTR  R1,0
         EX    R1,CLRINST
         L     R4,KEYADD1
         L     R6,KEYACT1
         BCTR  R6,0
         EX    R6,KEYMOVE              LEFT ADJUST KEY IN NEW AREA
         LA    R8,KEYDATA1
         BAL   R11,FRM001              FREE OLD AREA
         MVC   KEYDATA1(8),TKEYDATA    RESET CB EXCEPT FOR KEY LEN
KEY0011  TM    TYPESW,X'40'            Q. IS KEY2 = LAST
         BO    KEY002                  BRANCH ROUND KEY2 PROC IF YES
         TM    MODESW,X'20'            Q. IS COUNT SPECIFIED
         BO    KEY002                  BYPASS KEY2 PROCESSING IF YES
         LA    R7,KEYDATA2
         BAL   R11,CNV001              CONVERT KEY2
         TM    MODESW,X'0C'            Q. PACKED OR NUMERIC
         BNZ   KEY0012                 BRANCH IF YES
         L     R0,KEYACT2              LENGTH OF KEY2
         CH    R0,KEYLEN               Q. FULL KEY GIVEN
         BE    KEY0012                 BRANCH IF FULL KEY SPECD
         OI    TYPESW,X'10'            ELSE SHOW KEY CLASS
KEY0012  TM    TYPESW,X'C0'            Q. EITHER KEY NOT LITERAL
         BNZ   KEY002                  BRANCH IF SO
         LA    R7,KEYDATA1
         LA    R8,KEYDATA2
         BAL   R11,CLK001              COMPARE SPECIFIED KEYS
         BH    ERR009                  ERROR IF KEY1 GT KEY2
KEY002   TM    DDSW,X'01'              Q. PRINT SUPPRESSED AND NO COPY
         BZ    KEY0021                  (OK - PRINT ON)
         TM    DDSW,X'40'
         BZ    ERR010                  ERROR IF NEITHER
KEY0021  L     R11,KEY11SAV
         BR    R11
         EJECT
STL001   EQU   *                       SETL AND OPEN OUTPUT FILE
         USING IHADCB,R9
         TM    TYPESW,X'80'            Q. FIRST SPECIFIED
         BO    STL002                  BRANCH IF YES
         TM    TYPESW,X'20'            Q. KEY CLASS SPECIFIED
         BO    STL003                  BRANCH IF YES
         LA    R1,128                  ELSE USE KEY
         B     STL004
STL002   LA    R1,16                   SHOW START OF FILE
         B     STL004
STL003   LA    R1,192                  SHOW KEY CLASS
         B     STL004
STL004   L     R0,KEYADD1              KEY ADD
         SLL   R1,24
         OR    R0,R1                   SET UP SETL PARAM
         LA    R9,INDCB
         L     R15,DCBSETL             PROCESSING MODULE ADD
         LA    R1,INDCB
         OI    TYPESW,X'04'            SHOW SETL BEING PROCESSED
         BALR  R14,R15                 SETL
         NI    TYPESW,X'FB'            RESET FLAG
         OI    TYPESW,X'02'            SHOW SETL OUTSTANDING
         TM    DDSW,X'40'              Q. IS THERE AN OUTPUT FILE
         BNO   STL005                  BRANCH IF NO
         L     R9,OUTDCBAD             ELSE GET DCB ADD
         TM    DCBOFLGS,X'10'          Q. IS DCB ALREADY OPEN
         BO    STL005                  BRANCH IF YES
         MVC   CUROUTDD,DCBDDNAM
         OPEN  ((R9),(OUTPUT)),TYPE=J
STL005   BR    R11
         DROP  R9
         EJECT
RDI001   EQU   *                       ISAM READ LOOP
         USING DKEYDATA,R7
         ST    R11,RDI11SAV
         LH    R0,KEYLEN
         ST    R0,TKEYACT              SET UP LENGTH PART OF FILE SV
         LA    R7,TKEYDATA
         LA    R8,KEYDATA2
         L     R9,OUTDCBAD             OUTPUT DCB
RDI0010  GET   INDCB
         LR    R4,R1                   SAVE ADD OF RECORD
         AH    R1,INDCB+60             PLUS REL KEY POS
         ST    R1,DKEYADD              SET UP ADD PART OF FILE SV
         TM    TYPESW,X'10'            Q. IS KEY2 KEY CLASS
         BNO   RDI0011                 BRANCH IF NO
         BAL   R11,CLK001              ELSE COMPARE KEYS
         BH    RDI0022                 END IF END OF CLASS
RDI0011  BAL   R11,PRC001              PROCESS THE ISAM RECORD
         TM    TYPESW,X'40'            Q. LAST SPECIFIED
         BO    RDI0010                 LOOP IF SO
         TM    MODESW,X'20'            Q. WAS COUNT SPECIFIED
         BNO   RDI0012                 BRANCH IF NO
         SP    COUNT,=P'1'             DECREMENT
         BZ    RDI002                  END WHEN ZERO
         B     RDI0010                 ELSE LOOP
RDI0012  TM    TYPESW,X'10'            Q. IS KEY2 KEY CLASS
         BO    RDI0010                 LOOP IF YES
         BAL   R11,CLK001              COMPARE KEYS
         BE    RDI002                  END IF EQUAL
         BH    RDI0021                 END ON OVERRUN
         B     RDI0010                 ELSE LOOP
*
*
RDI002   MVC   PRMSGLIT+11(16),=C'ON EQUAL COMPARE'
         MVC   PRMSGNO2,=C'82'
         B     RDI004
RDI0021  MVC   PRMSGLIT+11(35),=C'AS KEY HIGHER THAN KEY2 ENCOUNTERED'
         MVC   PRMSGNO2,=C'85'
         B     RDI004
RDI0022  MVC   PRMSGLIT+11(15),=C'AT END OF CLASS'
         MVC   PRMSGNO2,=C'83'
         B     RDI004
RDI003   MVC   PRMSGLIT+11(14),=C'AT END OF FILE'
         MVC   PRMSGNO2,=C'81'
         B     RDI004
RDI004   MVC   PRMSGLIT(10),=C'PASS ENDED'
         MVC   PRMSGNO2,=C'00'
         BAL   R11,PRE001
         L     R11,RDI11SAV
         BR    R11
         DROP  R7
         EJECT
ENP001   EQU   *                       END OF PASS ROUTINE
         ST    R11,ENP11SAV
         TM    DDSW,X'01'              Q. IS PRINTING SUPPRESSED
         BO    ENP0011                 BRANCH IF YES
         MVC   HLCNT,=H'60'            ELSE SHEET EJECT
         BAL   R11,CLR001
ENP0011  TM    TYPESW,X'02'            Q. IS A SETL OUTSTANDING
         BNO   ENP002                  BRANCH IF NOT
         ESETL INDCB
ENP002   LA    R8,KEYDATA1
         BAL   R11,FRM001              FREMAIN KEY1 AREA IF USED
         LA    R8,KEYDATA2
         BAL   R11,FRM001              FREEMAIN KEY2 AREA IF USED
         L     R11,ENP11SAV
         BR    R11
         EJECT
CNV001   EQU   *                       CONVERT AND VALIDATE KEY FIELDS
         ST    R11,CNV11SAV
         USING DKEYDATA,R7
         TM    MODESW,X'0C'            Q. IS NUMERIC TEST REQUIRED
         BZ    CNV0011                 BRANCH IF NO
         LM    R0,R1,DKEYADD           KEY AREA AND LENGTH
         BAL   R11,NUM001              TEST
*
CNV0011  TM    MODESW,X'04'            Q. NUMERIC
         BZ    CNV002                  BRANCH IF NO
         L     R0,DKEYACT
         CH    R0,KEYLEN               Q. FULL KEY GIVEN
         BE    CNV007                  END IF YES
         BH    ERR003                  ERROR IF LONGER
         LA    R8,TKEYDATA             TEMPORARY CONTROL BLOCK
         LH    R1,KEYLEN               KEY LENGTH ON FILE
         BAL   R11,GTM001              GETMAIN
         MVI   0(R5),C'0'
         LH    R6,KEYLEN               LENGTH OF NEW AREA
         BCTR  R6,0
         BCTR  R6,0
         EX    R6,CLRINST              SET NEW AREA TO ZERO
         LH    R5,KEYLEN               LENGTH OF NEW KEY AREA
         S     R5,DKEYACT              LESS LENGTH OF OLD KEY AREA
         A     R5,TKEYADD              PLUS NEW KEY ADD GIVES OFFSET
         L     R4,DKEYADD              OLD KEY ADD
         L     R6,DKEYACT              OLD KEY LENGTH
         BCTR  R6,0
         EX    R6,KEYMOVE              MOVE IT
         LR    R8,R7
         BAL   R11,FRM001              FREE OLD AREA
         MVC   DKEYDATA(12),TKEYDATA   RESET CB
         B     CNV007
*
CNV002   TM    MODESW,X'08'            Q. PACKED
         BZ    CNV003                  BRANCH IF NO
         LA    R1,16                   MAX PACKED FIELD LENGTH
         L     R6,DKEYACT              KEY LENGTH
         CH    R1,KEYLEN               Q. IS FILE KEY PACKABLE
         BL    ERR011                  ERROR IF TOO LONG
         LH    R1,KEYLEN
         SLL   R1,1
         BCTR  R1,0                    MAX LENGTH BEFORE PACK = 2N-1
         CR    R1,R6                   Q. IS KEY PACKABLE
         BL    ERR012                  ERROR IF TOO LONG
         LH    R1,KEYLEN               LENGTH OF FIELD REQUIRED
         LA    R8,TKEYDATA             TEMPORARY CONTROL BLOCK
         BAL   R11,GTM001              GETMAIN
         L     R4,DKEYADD
         LH    R1,KEYLEN
         BCTR  R1,0                    LENGTH MODIFIER FOR FIELD
         SLL   R1,4
         OR    R6,R1                   COMBINE WITH
         BCTR  R6,0                    LENGTH MODIFIER FOR KEY
         EX    R6,KEYPACK              PACK
         LR    R8,R7
         BAL   R11,FRM001              FREE OLD AREA
         MVC   DKEYDATA(12),TKEYDATA   RESET CB
         B     CNV007
*
CNV003   TM    MODESW,X'02'            Q.  HEX
         BNO   CNV007                  BRANCH IF NO (CHAR)
         LM    R5,R6,DKEYADD           HEX TRANSLATE KEY FIELD
         BCTR  R6,0
         EX    R6,EXTRAN               TRANSLATE
         LR    R3,R5                   OUTPUT ADDRESS
         LA    R1,1(R5,R6)             END OF FIELD ADDRESS
CNV0031  TM    0(R5),X'F0'
         BO    ERR013                  ERROR  INVALID CHAR
         IC    R4,0(R5)
         LA    R5,1(R5)                BUMP INPUT
         CR    R5,R1                   Q. END OF KEY
         BE    ERR013                  ERROR ODD NO OF BYTES
         TM    0(R5),X'F0'
         BO    ERR013                  ERROR  INVALID CHAR
         IC    R0,0(R5)
         SLL   R4,4
         OR    R4,R0
         STC   R4,0(R3)
         LA    R3,1(R3)                BUMP
         LA    R5,1(R5)                BUMP INPUT
         CR    R5,R1                   Q. END OF KEY
         BE    CNV0032                 BRANCH IF YES
         B     CNV0031                 LOOP
CNV0032  S     R3,DKEYADD              COMPUTE NEW KEY LENGTH
         ST    R3,DKEYACT              AND MODIFY CB FIELD
         B     CNV007
*
CNV007   L     R1,DKEYACT              ACTUAL LENGTH OF KEY
         CH    R1,KEYLEN               COMPARE WITH FILE KEY LENGTH
         BH    ERR003                  ERROR IF LONGER
         L     R11,CNV11SAV
         BR    R11
         DROP  R7
         SPACE 5
DCB001   EQU   *                       BUILD A DCB (IS OR PS)
         B     *+4(R2)
         B     DCB002                  PS
         B     DCB003                  IS
DCB002   LA    R2,L'PSDCBL             PS DCB LENGTH
         LA    R3,PSDCB                ADDRESS
         B     DCB004
DCB003   LA    R2,L'ISDCBL             IS DCB LENGTH
         LA    R3,ISDCB                ADDRESS
         B     DCB004
DCB004   GETMAIN R,LV=(2)
         BCTR  R2,0
         EX    R2,DCBMOVE              MOVE SKELETON DCB
         BR    R11
         SPACE 5
SDB001   EQU   *                       SET UP AN OUTPUT DCB
         USING IHADCB,R9
         OC    JFCBLKSI,JFCBLKSI       Q. HAS PARM BEEN SPECIFIED
         BNZ   SDB0011                 BRANCH IF YES
         MVC   DCBBLKSI,INDCB+62       ELSE USE INDCB PARAM
SDB0011  OC    JFCLRECL,JFCLRECL
         BNZ   SDB0012
         MVC   DCBLRECL,INDCB+82
SDB0012  OC    JFCRECFM,JFCRECFM
         BNZ   SDB0013
         MVC   DCBRECFM,INDCB+36
SDB0013  LA    R1,EXLIST
         O     R1,DCBEXLST             RETAIN TOP BYTE
         ST    R1,DCBEXLST
         MVC   DCBDDNAM,OUTDDNAM
         TM    DCBDSORG,X'80'          Q. INDEXED SEQUENTIAL
         BNO   SDB003                  BRANCH IF NO
         OC    JFCKEYLE,JFCKEYLE
         BNZ   SDB0021
         MVC   DCBKEYLE,INDCB+16
SDB0021  OC    JFCRKP,JFCRKP
         BNZ   SDB0022
         MVC   DCBRKP,INDCB+60
SDB0022  LA    R1,SYN001
         B     SDB004
SDB003   LA    R1,SYN002
SDB004   O     R1,DCBSYNAD             RETAIN TOP BYTE
         ST    R1,DCBSYNAD
         BR    R11
         DROP  R9
         SPACE 5
CLK001   EQU   *                       COMPARE KEYS - CB ADDS IN R7 R8
         L     R4,4(R7)                KEY ADDRESSES
         L     R5,4(R8)
         L     R6,8(R7)                KEY LENGTH
         TM    MODESW,X'08'            Q. PACKED
         BO    CLK002                  BRANCH IF YES
         C     R6,8(R8)                USE
         BL    CLK0011                     SHORTER KEY
         L     R6,8(R8)                                LENGTH
CLK0011  BCTR  R6,0                    EX LENGTH
         EX    R6,COMPLKEY             COMPARE
         BR    R11
CLK002   BCTR  R6,0                    EX LENGTH - PACKED
         SLL   R6,4
         O     R6,8(R8)                COMBINE LENGTHS
         BCTR  R6,0
         EX    R6,COMPPKEY             COMPARE
         BR    R11
         SPACE 5
PRC001   EQU   *                       PROCESS AN ISAM RECORD
         ST    R11,PRC11SAV
         USING DKEYDATA,R7
         TM    DDSW,X'40'              Q. OUTPUT DD SPECIFIED
         BNO   PRC002                  BRANCH IF NO
         LR    R0,R4                   RECORD ADDRESS
         PUT   (R9)
PRC002   TM    DDSW,X'01'              Q. IS PRINTING SUPPRESSED
         BO    PRC003                  BRANCH IF YES
         BAL   R11,FMT001              FORMAT AND PRINT
PRC003   L     R11,PRC11SAV
         BR    R11
         DROP  R7
         EJECT
LPM001   EQU   *                       OBTAIN LENGTH OF PARAM FIELDS
         LR    R4,R2                   SAVE CURRENT ADDRESS
LPM002   C     R2,ENDCARD              Q. END OF CARD
         BE    ERR015                  ERROR IF YES
         TM    TYPESW,X'01'            Q. IN QUOTES
         BNO   LPM0021                 BRANCH IF NO
         CLI   0(R2),C''''             Q. END OF PARM
         BE    LPM003                  BRANCH IF YES
         B     LPM0022
LPM0021  CLI   0(R2),C','              Q. ELSE END OF PARMS
         BE    LPM003                  YES
         CLI   0(R2),C' '
         BE    LPM003                  YES
LPM0022  LA    R2,1(R2)                ELSE BUMP
         B     LPM002                  LOOP
LPM003   LR    R6,R2
         SR    R6,R4                   LENGTH OF PARAM
         BZ    ERR016                  ERROR IF ZERO LENGTH
         BCTR  R6,0                    EXECUTABLE LENGTH
         TM    TYPESW,X'01'            Q. IN QUOTES
         BCR   14,R11                  BRANCH IF NOT
         LA    R2,1(R2)
         CLI   0(R2),C' '              Q. VALID
         BCR   8,R11                   YES
         CLI   0(R2),C','              YES
         BCR   8,R11
         B     ERR016                  ERROR IF NOT , OR SPACE
         SPACE 5
NUM001   EQU   *                       NUMERIC TEST
         USING DKEYDATA,R7
         LM    R4,R5,DKEYADD           PICK UP SV
         LA    R5,0(R4,R5)             END ADD +1
         BCTR  R5,0
NUM002   TM    0(R4),X'F0'             Q. ZONE OK
         BNO   ERR017                  ERROR IF NOT
         CR    R4,R5                   Q. END OF FIELD
         BCR   8,R11                   BRANCH IF YES
         LA    R4,1(R4)                BUMP
         B     NUM002
         DROP  R7
         SPACE 5
RJF001   EQU   *                       READ JFCB
         MVI   JFCBDSNM,C' '
         MVC   RJFCBDCB+40(8),OUTDDNAM SET UP DD NAME
         RDJFCB RJFCBDCB
         CLI   JFCBDSNM,C' '           Q. SUCESSFUL
         BE    ERR023                  ERROR NO DD CARD
         BR    R11
         SPACE 5
LSS001   EQU   *                       SEARCH DCB LIST FOR DD ENTRY
         L     R1,DCBLSTAD             START OF LIST
         USING DDCBLST,R1
LSS002   LTR   R1,R1                   Q. END OF LIST
         BZ    LSS003                  BRANCH IF YES
         CLC   DLSTDD,0(R5)            Q. IS THIS CORRECT ENTRY
         BE    LSS004                  BRANCH IF YES
         L     R1,DLSTCH               CHAIN DOWN LIST
         B     LSS002                  LOOP
LSS003   CLI   *,X'00'                 SET CC TO NE
         B     LSS005
LSS004   L     R9,DLSTDCB              LOAD DCB ADD
         DROP  R1
LSS005   BR    R11
         SPACE 5
GTM001   EQU   *                       GETMAIN - CONTROL BLOCK IN R8
         USING DKEYDATA,R8
         ST    R1,DKEYACT              STORE KEY LENGTH
         LA    R0,7(R1)                MAKE MULTIPLE OF 8
         SRL   R0,3
         SLL   R0,3
         ST    R0,DKEYLEN              STORE KEY AREA LENGTH
         GETMAIN R,LV=(0)
         ST    R1,DKEYADD              STORE ADDRESS
         LR    R5,R1                   AND SET REG FOR RETURN
         DROP  R8
         BR    R11
         SPACE 5
FRM001   EQU   *                       FREEMAIN
         USING DKEYDATA,R8
         LM    R0,R1,DKEYDATA
         LTR   R0,R0                   Q. HAS GETMAIN BEEN ISSUED
         BCR   8,R11                   RETURN IF NO
         FREEMAIN R,LV=(0),A=(1)
         XC    DKEYLEN,DKEYLEN         SHOW FREEMAIN
         BR    R11
         DROP  R8
         SPACE 5
CLL001   EQU   *                       CLOSE DCBS IN DCBLIST
         ST    R11,CLL11SAV
         L     R2,DCBLSTAD             START OF LIST
         USING DDCBLST,R2
CLL002   LTR   R2,R2                   Q. END OF LIST
         BE    CLL003                  BRANCH IF YES
         L     R9,DLSTDCB              DCB ADD
         BAL   R11,CLO001              CLOSE IF OPEN
         L     R2,DLSTCH               PICK UP CHAIN
         B     CLL002                  AND LOOP
CLL003   L     R11,CLL11SAV
         DROP  R2
         BR    R11
         SPACE 5
CLO001   EQU   *                       CLOSE DATA SET IF OPEN
         USING IHADCB,R9
         TM    DCBOFLGS,X'10'          Q. IS DCB OPEN
         BNO   CLO002                  BYPASS CLOSE IF NO
         CLOSE ((R9))
CLO002   BR    R11
         DROP  R9
         SPACE 5
SYN001   EQU   *                            ISAM ERROR HANDLER
         LR    R5,R1                   ERROR DCB ADD
         USING IHADCB,R5
         TM    DCBEXCD1,X'80'          Q. RECORD NOT FOUND (SETL)
         BO    SYN0011                 BRANCH IF YES
         TM    DCBEXCD2,X'80'          Q. SEQUENCE CHECK
         BO    ERR018                  ERROR
         TM    DCBEXCD2,X'40'          Q. DUPLICATE RECORD
         BO    ERR019                  ERROR
         SYNADAF ACSMETH=QISAM
         MVC   PRMSGLIT(4),=C'ISAM'
         BAL   R11,SYN003
         TM    TYPESW,X'04'            Q. ERROR DURING SETL
         BO    SYN0011                 BRANCH IF YES
         B     RDI0010                 ELSE CONTINUE WITH GET LOOP
SYN0011  OI    TYPESW,X'02'            FORCE ESETL
         TM    DCBEXCD1,X'80'          Q. NOT ON FILE
         BO    ERR020                  ERROR HANDLER
         TM    DCBEXCD1,X'10'          Q. INVALID REQUEST
         BO    ERR021                  ERROR HANDLER
         B     ERR022                  ERROR - ASSUME I-O ERROR
         DROP  R5
         SPACE 3
SYN002   EQU   *                       QSAM ERROR ANALYSIS ROUTINE
         STM   R13,R14,SYN13SAV
         SYNADAF ACSMETH=QSAM
         MVC   PRMSGLIT(4),=C'QSAM'
         BAL   R11,SYN003
         LM    R13,R14,SYN13SAV
         BR    R14
         SPACE 3
SYN003   EQU   *                       COMMON I-O ERROR HANDLER
         ST    R11,SYN11SAV
         MVI   RETCD+3,X'04'           SHOW ERROR ENCOUNTERED
         MVC   PRMSGNO2,=C'90'
         MVC   PRMSGLIT+5(19),=C'PROCESSING ERROR -'
         MVC   PRMSGLIT+24(60),68(R1)    SYNADAF MESSAGE
         BAL   R11,PRE001
         SYNADRLS ,
         SP    ERRCNT,=P'1'            Q. ERROR LIMIT REACHED
         BNZ   SYN0031                 BRANCH IF NO
         MVC   PRMSGNO2,=C'91'
         MVC   PRMSGLIT(24),=C'I-O ERROR LIMIT EXCEEDED'
         BAL   R11,PRE001
         CLOSE PRTDCB
         ABEND 999,DUMP,STEP
SYN0031  L     R11,SYN11SAV
         BR    R11
         SPACE 5
ERP001   ERRTAB  24                    ERROR MESSAGE BRANCH TABLE
         SPACE 5
ERP001   EQU   *
         MVI   RETCD+3,X'08'           SHOW ERROR ENCOUNTERED
         USING DERRMSG,R1
         MVC   PRERRFLG,=C'*** ERROR ***'
         MVC   PRMSGNO2,DERRNO         ERROR NUMBER
         IC    R6,DERRLEN
         BCTR  R6,0                    LENGTH OF ERROR MESSAGE
         EX    R6,MSGMOVE              MOVE ERROR MESSAGE
         DROP  R1
         BAL   R11,PRE001
         TM    MODESW,X'10'            Q. IN INTERPRET PHASE
         BNO   ERP003                  BRANCH IF NOT
         OI    TYPESW,X'08'            SHOW ERROR
ERP002   CLI   0(R2),C' '              Q. END OF CARD
         BE    ERP003                  END PHASE IF YES
         CLI   0(R2),C','              Q. END OF THIS PARAM
         BE    PRM009                  CONTINUE WITH OTHER PARAMS
         LA    R2,1(R2)                ELSE LOOP TO FIND DELIM
         B     ERP002
ERP003   NI    MODESW,X'EF'            RESET INTERPRET PHASE FLAG
         BAL   R11,ENP001              END PASS
         B     ISP001                  CONTINUE WITH NEXT CARD
         EJECT
FMT001   EQU   *                       FORMAT PRINT LINE
         ST    R11,FMT11SAV
         LH    R3,INDCB+82             INPUT RECORD LENGTH
         LH    R6,HLINLEN              SET UP DEFAULT LINE LENGTH
FMT002   SH    R3,HLINLEN              LESS MAX LINE LENGTH
         BNP   FMT003
         BAL   R11,CVX001              CONVERT AND PRINT
         LH    R1,HLINLEN              LENGTH
         AR    R4,R1                   BUMP INPUT ADD
         B     FMT002                  LOOP
FMT003   BZ    FMT004                  BRANCH IF COMPLETE
         AH    R3,HLINLEN              REMAINING LENGTH
         BZ    FMT004                  BRANCH IF NO MORE
         LR    R6,R3
         BAL   R11,CVX001              CONVERT AND PRINT
FMT004   L     R11,FMT11SAV
         BR    R11
         SPACE 5
CVX001   EQU   *                       CONV TO HEX, FORMAT AND PRINT
         ST    R11,CVX11SAV
         LA    R2,0(R4,R6)             END OF LINE ADD
         LA    R5,PRTAREA+1
         BCTR  R6,0
         TM    DDSW,X'02'              Q. IS CONVERSION REQUIRED
         BO    CVX002                  BRANCH IF YES
         EX    R6,KEYMOVE              MOVE TO PRINT LINE
         B     CVX003
CVX002   BAL   R11,TRN001              TRANSLATE TO HEX
         BAL   R11,PRT001              PRINT
         LA    R5,PRTAREA+1            OUTPUT ADD
         BAL   R11,SPC001              FORMAT
CVX003   TR    PRTAREA+1(132),TRTABPR  TRANSLATE UNPRINTABLE CHARS
         BAL   R11,PRT001              PRINT
         LA    R6,1(R6)                RESET
         L     R11,CVX11SAV
         BR    R11
         SPACE 3
SPC001   EQU   *                       FORMAT
         LR    R1,R4                   INPUT ADD
SPC002   MVC   0(1,R5),0(R1)           MOVE CHAR
         LA    R5,2(R5)                BUMP OUTPUT
         LA    R1,1(R1)                BUMP INPUT
         CR    R1,R2                   Q. END OF LINE
         BNE   SPC002                  LOOP IF NO
         BR    R11
         SPACE 5
TRN001   EQU   *                       TRANSLATE TO HEX
         LR    R1,R4
TRN002   UNPK  0(1,R5),0(1,R1)         LEFT HAND DIGIT
         OI    0(R5),X'F0'
         MVC   1(1,R5),0(R1)           RIGHT HAND DIGIT
         OI    1(R5),X'F0'
         LA    R5,2(R5)                BUMP OUTPUT
         LA    R1,1(R1)                BUMP INPUT
         CR    R1,R2                   Q. END OF LINE
         BNE   TRN002                  LOOP IF NOT
         LA    R1,1(R6,R6)
         EX    R1,EXTRANX              TRANSLATE
         BR    R11
         SPACE 5
PRE001   EQU   *                       FORMAT ERROR MESSAGE AND PRINT
         MVC   PRMSGNO1,=C'ISCOPY'
         MVI   PRMSGNO3,C'-'
         B     PRT001
         SPACE 5
PRT001   EQU   *                       PRINT AND CLEAR
         PUT   PRTDCB,PRTAREA
         B     CLR001
         SPACE 3
CLR001   EQU   *                       CLEAR PRINT AREA AND SHEET EJECT
         MVI   PRTAREA,C' '
         MVC   PRTAREA+1(L'PRTAREA-1),PRTAREA
         CLC   HLCNT,=H'60'            Q. END OF PAGE
         BL    CLR002                  BRANCH IF NO
         AP    P4PCNT,=P'1'            BUMP PAGE NO
         XC    HLCNT,HLCNT             RESET LINE COUNT
         MVI   PCARCTL,C'1'            EJECT
         MVC   PPTITLE(L'LPTITLE),LPTITLE
         MVC   PPAGEED,LPAGEED
         ED    PPAGNO,P4PCNT           PAGE NO
         ST    R11,CLR11SAV
         BAL   R11,PRT001              PRINT TITLE AND EJECT
         L     R11,CLR11SAV
         B     PRT001                  PRINT BLANK LINE
CLR002   LH    R1,HLCNT                LINE COUNT
         LA    R1,1(R1)                BUMP
         STH   R1,HLCNT
         BR    R11
         EJECT
KEYMOVE  MVC   0(0,R5),0(R4)
KEYPACK  PACK  0(0,R5),0(0,R4)
CLRINST  MVC   1(0,R5),0(R5)
CNTPACK  PACK  COUNT,0(0,R4)
COMPPKEY CP    0(0,R4),0(0,R5)
COMPLKEY CLC   0(0,R4),0(R5)
DCBMOVE  MVC   0(0,R1),0(R3)
EXTRAN   TR    0(0,R5),TRTAB
EXTRANX  TR    PRTAREA+1(0),TRTABHEX
         USING DERRMSG,R1
MSGMOVE  MVC   PRMSGLIT(0),DERRLIT
         DROP  R1
         SPACE 5
KEY11SAV DS    F
OPN11SAV DS    F
CLL11SAV DS    F
PRM11SAV DS    F
CNV11SAV DS    F
CLR11SAV DS    F
FMT11SAV DS    F
CVX11SAV DS    F
RDI11SAV DS    F
ENP11SAV DS    F
PRC11SAV DS    F
SYN11SAV DS    F
SYN13SAV DS    F
SYN14SAV DS    F
         SPACE 5
COUNT    DS    PL8
INDDNAM  DC    CL8' '
OUTDDNAM DC    CL8' '
CURINDD  DC    CL8' '
CUROUTDD DC    CL8' '
DWORK    DS    2F
DCBLSTAD DC    F'0'
OUTDCBAD DC    F'0'
EXLIST   DS    0F
         DC    X'87'
         DC    AL3(INFMJFCB)
ENDCARD  DS    F
RETCD    DC    F'0'
HLINLEN  DS    H
KEYLEN   DS    H
HLCNT    DC    H'60'                   LINE COUNT
         SPACE 5
KEYDATA1 DS    0F                      STORAGE AREA FOR KEY1 INFO
KEYLEN1  DC    F'0'
KEYADD1  DC    F'0'
KEYACT1  DC    F'0'
*
KEYDATA2 DS    0F                      STORAGE AREA FOR KEY2 INFO
KEYLEN2  DC    F'0'
KEYADD2  DC    F'0'
KEYACT2  DC    F'0'
*
TKEYDATA DS    0F                      TEMPORARY STORAGE AREA
TKEYLEN  DC    F'0'
TKEYADD  DC    F'0'
TKEYACT  DC    F'0'
*
DKEYDATA DSECT
DKEYLEN  DS    F                       LENGTH OF KEY AREA
DKEYADD  DS    F                       ADDRESS OF KEY AREA
DKEYACT  DS    F                       ACTUAL LENGTH OF KEY
         SPACE 5
DDCBLST  DSECT                         DCB LIST ENTRY
DLSTCH   DS    F                       CHAINING FIELD
DLSTDCB  DS    F                       DCB ADD
DLSTDD   DS    CL8                     DDNAME
         SPACE 5
ISCOPY   CSECT
MODESW   DS    X                       SWITCH SETTINGS
*
*              1.......                KEY1 PRESENT
*              .1......                KEY2 PRESENT
*              ..1.....                COUNT PRESENT
*              ...1....                INTERPRET PHASE
*              ....1...                TYPE = PACKED
*              .....1..                TYPE = NUMERIC
*              ......1.                TYPE = HEX
*              .......1                TYPE = CHARACTER
*
TYPESW   DS    X
*
*              1.......                KEY1 = FIRST
*              .1......                KEY2 = LAST
*              ..1.....                KEY1 IS KEY CLASS
*              ...1....                KEY2 IS KEY CLASS
*              ....1...                ERROR OUTSTANDING
*              .....1..
*              ......1.                SETL OUTSTANDING
*              .......1                CURRENT KEY IN QUOTES
*
DDSW     DS    X
*
*              1.......                INPUT SPECIFIED
*              .1......                OUTPUT SPECIFIED
*              ..1.....                PRINT COMMAND PRESENT
*              ...1....                ISAM ERROR BEING PROCESSED
*              ....1...
*              .....1..
*              ......1.                PRINT - CONVERSION TO HEX
*              .......1                PRINTING SUPPRESSED
         EJECT
ERRCNT   DC    PL2'10'
P4PCNT   DC    PL2'0'                  PAGE COUNT
LPTITLE  DC    C'ISCOPY - SELECTIVE PRINT OF INDEXED SEQUENTIAL FILES'
         SPACE 3
LPAGEED  DC    X'D7C1C7C540202021'
PRTAREA  DS    CL133                   PRINT AREA
         ORG   PRTAREA
PCARCTL  DS    C
         DS    CL30
PPTITLE  DS    CL90
PPAGEED  DS    0CL8
         DS    CL4
PPAGNO   DS    CL4
         ORG   PRTAREA
         DS    CL30
PRTCARD  DS    CL80
         ORG   PRTAREA
         DS    CL10
PRERRFLG DS    CL13
         DS    C
PRMSGNO1 DS    CL6
PRMSGNO2 DS    CL2
        DS     C
PRMSGNO3 DS    C
         DS    C
PRMSGLIT DS    C
         ORG
         SPACE 10
ERRMSG   EQU   *                       ERROR MESSAGES
         ERRMSG 'UNSUPPORTED KEYWORD'
         ERRMSG 'CONFLICTING OR DUPLICATE PARAMETER'
         ERRMSG 'PARAMETER EXCEEDS MAXIMUM LENGTH'
         ERRMSG 'INVALID TYPE CODE'
         ERRMSG 'INVALID DD NAME'
         ERRMSG 'INVALID PRINT OPTION'
         ERRMSG 'KEY1 PARAMETER MUST BE PRESENT'
         ERRMSG 'KEY2 OR COUNT MUST BE PRESENT'
         ERRMSG 'KEY1 GREATER THAN KEY2'
         ERRMSG 'EITHER PRINT OR COPY MUST BE SPECIFIED'
         ERRMSG 'KEY FIELD ON FILE TOO LARGE FOR PACKED COMPARISON'
         ERRMSG 'KEY FIELD TO BE PACKED EXCEEDS 16 DIGITS'
         ERRMSG 'FORMAT INVALID FOR HEXADECIMAL REPRESENTATION'
         ERRMSG 'KEY LENGTH EXCEEDS THAT OF FILE'
         ERRMSG 'INVALID CONTINUATION'
         ERRMSG 'INVALID PARAMETER'
         ERRMSG 'NON-NUMERIC FIELD'
         ERRMSG 'SEQUENCE ERROR ON OUTPUT FILE'
         ERRMSG 'DUPLICATE RECORD ON OUTPUT FILE'
         ERRMSG 'RECORD DEFINED BY KEY1 NOT ON FILE'
         ERRMSG 'INVALID SETL REQUEST'
         ERRMSG 'I-O ERROR ON ISAM FILE'
         ERRMSG 'DD CARD OMITTED'
         ERRMSG 'SAME DD NAME SPECIFIED FOR INPUT AND OUTPUT'
         SPACE 3
DERRMSG  DSECT
DERRNO   DS    CL2
DERRLEN  DS    X
DERRLIT  DS    C
ISCOPY   CSECT
         SPACE 5
CARDAREA DS    CL80
TRTAB    DC    193X'FF'
         DC    X'0A0B0C0D0E0F'
         DC    41X'FF'
         DC    X'00010203040506070809'
         DC    6X'FF'
*
*
TRTABPR  EQU   *
         DC    16C'.'
         DC    16C'.'
         DC    16C'.'
         DC    16C'.'
         DC    C' .........Ö.<(+×'
         DC    C'&&..........$*);^'
         DC    C'-/.........,%_>.'
         DC    C'..........:#@''="'
         DC    16C'.'
         DC    16C'.'
         DC    16C'.'
         DC    16C'.'
         DC    C'.ABCDEFGHI......'
         DC    C'.JKLMNOPQR......'
         DC    C'..STUVWXYZ......'
         DC    C'0123456789......'
*
         ORG   *-240
TRTABHEX DS    240X
         DC    C'0123456789ABCDEF'
*
SAVEAREA DS    18F
         SPACE 10
PARAMDCB DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(R),EODAD=ISPEOF
PRTDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,BLKSIZE=532,*
               LRECL=133
INDCBD   DCB   DSORG=IS,MACRF=(GL,SK),SYNAD=SYN001,EODAD=RDI003
INDCBDND EQU   *
INDCBDL  DS    0CL(INDCBDND-INDCBD)
INDCB    DCB   DSORG=IS,MACRF=(GL,SK),SYNAD=SYN001,EODAD=RDI003
RJFCBDCB DCB   DSORG=PS,MACRF=(R),EXLST=EXLIST
ISDCB    DCB   DSORG=IS,MACRF=(PM),SYNAD=0
ISDCBND  EQU   *
ISDCBL   DS    0CL(ISDCBND-ISDCB)
PSDCB    DCB   DSORG=PS,MACRF=(PM),SYNAD=0
PSDCBND  EQU   *
PSDCBL   DS    0CL(PSDCBND-PSDCB)
         PRINT ON,GEN
         EJECT
         IEFJFCBN ,
         EJECT
         DCBD  DSORG=IS
ISCOPY   CSECT
         PRINT ON,NOGEN
         LTORG
         END
