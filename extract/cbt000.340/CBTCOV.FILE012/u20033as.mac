         TITLE 'HASMDRIV - ASSEMBLER F DRIVER FOR HASP ASSEMBLY'
HASMDRIV CSECT
*
         REQU
*
         SAVE  (14,12)
         BALR  R2,0
         USING *,R2
         LA    R14,SAVER
         ST    R13,SAVER+4             STANDARD
         ST    R14,8(R13)                  LINKAGE
         LR    R13,R14                         CONVENTIONS
*
         L     R1,0(R1)                R1 -> PARM FIELD
         LH    R10,0(R1)               R10 CONTAINS LENGTH OF PARM FLD
         LA    R11,2(R1)               R11 -> PARM FIELD
*
         CH    R10,=H'3'               LENGTH < 3???
         BL    ERR1                    YES...ERROR
         CLC   =C'ALL',0(R11)          DOES HE WANT THE WHOLE THING???
         BNE   NOTALL                  NOPE...BRANCH AROUND
         LA    R10,ALLISTL             YEP...GET LENGTH OF LIST
         LA    R11,ALLIST              POINT TO DUMMY PARM FIELD
NOTALL   LA    R9,0(R10,R11)           R9 -> END OF PARM FLD + 1
*
LOOP1    LA    R4,3                    INITIALIZE FOR BCT
         LA    R8,1(R11)               ON YOUR MARK, GET SET...
*
LOOP2    LA    R8,1(R8)                POINT TO NEXT CHAR
         CLR   R8,R9                   END OF PARM FIELD???
         BNL   FOUNDEOJ                YES...JUMP OUT
         CLI   0(R8),C','              END OF OPERAND???
         BE    FOUNDEND                YES...JUMP OUT
         BCT   R4,LOOP2                MORE TO SCAN??? - GO BACK
         B     ERR2                    OPERAND TOO LONG - ERROR
*
FOUNDEOJ OI    SW1,EOJ                 INDICATE WHOLE PARM FLD SCANNED
FOUNDEND LA    R5,4                    LOAD FOR SUBTRACT
         SR    R5,R4                   GET LENGTH FOR EX
         MVC   MEMBER+4(4),=CL4' '     CLEAR LAST 4 BYTES OF NAME
         EX    R5,MOVEX                MOVE IN MODULE NAME
         LA    R11,1(R8)               BUMP UP OPERAND POINTER
*
         RDJFCB (SYSIN,,SYSGO)
         MVC   SYSINJ+JFCBELNM(8),MEMBER MOVE MEMBER NAME TO JFCB
         MVC   SYSGOJ+JFCBELNM(8),MEMBER MOVE MEMBER NAME TO JFCB
         OI    SYSINJ+JFCBMASK+4,X'80' FORCE JFCB REWRITE
         OI    SYSGOJ+JFCBMASK+4,X'80' FORCE JFCB REWRITE
         OPEN  (SYSIN,(INPUT),SYSGO,(OUTPUT)),TYPE=J
         CLOSE (SYSIN,,SYSGO)
         FREEPOOL SYSIN
         FREEPOOL SYSGO
*
         LINK  EP=IEUASM,PARAM=(ASMOPTS),VL=1
*
         C     R15,RCODE               IS RC THE HIGHEST YET???
         BNH   NOTHIGH                 NOPE...BRANCH AROUND
         ST    R15,RCODE               SAVE NEW HIGH RC
NOTHIGH  TM    SW1,EOJ                 ARE WE FINISHED???
         BNO   LOOP1                   NO....LOOP BACK
RETURN   L     R13,SAVER+4             RESTORE R13
         L     R15,RCODE               LOAD HIGHEST RC FROM ASM
         RETURN (14,12),RC=(15)
*
ERR1     WTO   'HASMDRIV - PARM FIELD MISSING OR TOO SHORT',           *
               ROUTCDE=(11)
         MVC   RCODE(4),=A(20)
         B     RETURN
*
ERR2     WTO   'HASMDRIV - PARM FIELD FORMAT ERROR',                   *
               ROUTCDE=(11)
         MVC   RCODE(4),=A(24)
         B     RETURN
         EJECT
MOVEX    MVC   MEMBER+4(*-*),0(R11)
*
SAVER    DC    18F'0'
*
RCODE    DC    F'0'
*
MEMBER   DC    CL8'HASP'
*
SYSINX   DC    AL1(128+7),AL3(SYSINJ)
SYSGOX   DC    AL1(128+7),AL3(SYSGOJ)
SYSINJ   DC    CL176' '
SYSGOJ   DC    CL176' '
JFCBELNM EQU   44
JFCBMASK EQU   72
*
ASMOPTS  DS    0H
         DC    AL2(ASMOPTSN-ASMOPTS-2)
         DC    C'LOAD,NODECK'
ASMOPTSN EQU   *
*
ALLIST   DC    C'ACCT,BR1,COMM,CON,INIT,MISC,NUC,PRPU,RDR,RTAM,WTR,XEQ'
ALLISTL  EQU   *-ALLIST
*
SW1      DC    X'00'
EOJ      EQU   X'80'
         SPACE 2
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EXLST=SYSINX
         SPACE 2
SYSGO    DCB   DDNAME=SYSGO,DSORG=PS,MACRF=PM,EXLST=SYSGOX
         SPACE 2
         END
