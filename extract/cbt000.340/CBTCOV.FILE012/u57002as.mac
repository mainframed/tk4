XXX      CSECT
*
*    PASSOFF SETS PASSWORD BITS OFF ON VTOC OF SPECIFIED VOLUME
*
*    JCL- SYSVTOC FOR THE VOLUME AS WITH IEH UTILITIES
*
*    MUST BE AUTHORISED
*
R1       EQU   1
R15      EQU   15
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         LR    11,13
         LA    13,SAVE
         ST     11,SAVE+4
         ST    13,8(11)
         RDJFCB (IN)
         MVC   SERIAL,JFCB+118
         MVC   JFCB(44),FORMAT4
         OPEN  (IN),TYPE=J
         RDJFCB (OUT)
         MVC   JFCB(44),FORMAT4
         OPEN  (OUT,(OUTPUT)),TYPE=J
         ENQ   (SYSVTOC,SERIAL,E,6,SYSTEM)
GET      READ  ECBA,SF,IN,DSCB,'S'
         CHECK ECBA
         CLI   DSCB+44,X'F1'
         BNE   GET
         NI    DSCB+93,X'EB'
         XDAP  ECBX,WI,OUT,DSCB,140,,IN+5
         WAIT  ECB=ECBX
         CLI   ECBX,X'7F'
         BE    GET
         ABEND 4,DUMP
EOF      DEQ   (SYSVTOC,SERIAL,6,SYSTEM)
         CLOSE (IN,,OUT)
         L     13,4(13)
         RETURN (14,12),RC=0
*
         DS    0D
SAVE     DS    18F
IN       DCB   DDNAME=SYSVTOC,LRECL=96,KEYLEN=44,BLKSIZE=96,           C
               EXLST=XLIST,EODAD=EOF,MACRF=(R),RECFM=F,DSORG=PS
OUT      DCB   DDNAME=SYSVTOC,MACRF=(E),EXLST=XLIST,DSORG=DA
         DS    0F
XLIST    DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
DSCB     DC    CL140' '
SYSVTOC  DC    C'SYSVTOC'
SERIAL   DC    CL6' '
FORMAT4  DC    44X'04'
         END
