************************************************************************
*
*                  P M 1 2 - ASSEMBLER PROGRAM
*
************************************************************************
*    UPON INITIALISATION, THE PROGRAM WILL ISSUE THE FOLLOWING MESSAGE:-
*            PM12 - REPLY INTERVAL COUNT 'MSS-NNN' OR 'END'
*    MSS - THE INTERVAL IN MINUTES AND SECONDS BETWEEN SAMPLES.
*    NNN - THE NUMBER OF SAMPLES REQUIRED FOR THIS RUN (DEFAULT=50).
*    ONCE THE SPECIFIED NUMBER OF SAMPLES HAS BEEN OBTAINED, THE MESSAGE
*            REPEATED.
************************************************************************
PM12     TITLE 'TRACE PAGE UTILISATION'
PM12     BEGIN
*
RECSIZE  EQU   *
         L     R02,16          CVT POINTER
         L     R03,312(R02)     HIGHEST REAL ADDRESS
         AH    R03,=H'1'
         SRL   R03,7           PLACE RSPT LENGTH IN
         STH   R03,PAGECCW+6   CCW
*
         AH    R03,=H'8'
         STH   R03,PAGEOUT+62  INITIALISE LRECL & BLKSIZE
         STH   R03,PAGEOUT+82
*
         RDJFCB PAGEOUT
         MVI   JFCB+100,X'80'
         STH   R03,JFCB+102
         STH   R03,JFCB+104
         L     R04,348(R02)     PSIA ADDRESS
         L     R05,4(R04)       RSPT ADDRESS
*
         ST    R05,PAGECCW     SET ADDRESS OF RSPT INTO
         MVI   PAGECCW,X'01'  WRITE CCW & RESET COMMAND
         OPEN  (PAGEOUT,(OUTPUT)),TYPE=J
*
WTOR     EQU   *
         MVC   REPLY,BLANKS
         XC    ECB1,ECB1
*
         WTOR  'PM12 - REPLY INTERVAL-COUNT ''MSS-NNN'' OR ''END''',   C
               REPLY,7,ECB1
         WAIT  ECB=ECB1
         OC    REPLY,BLANKS
*
         CLC   =C'END',REPLY
         BE    BACK
         CLI   REPLY+3,C' '
         BE    EDITL
         CLI   REPLY+3,C'-'
         BNE   WTOR
         XC    DWORD,DWORD
         PACK  DWORD+6(2),REPLY+4(3)
         CVB   R07,DWORD
         ST    R07,COUNT
*
EDITL    EQU   *
         CLI   REPLY,X'F0'
         BL    WTOR
         CLI   REPLY+1,X'F0'
         BL    WTOR
         CLI   REPLY+2,X'F0'
         BL    WTOR
         CLC   =C'000',REPLY
         BH    WTOR
*
*    SET  UP  REGISTERS  FOR  MAIN LOOP.
*    INITIAL  CODE  RESTRICTS RUN TIME BY BCT USING COUNT (DEFAULT=50)
*    LATER  VERSION  WILL SET COUNT=0 AND TEST FOR STOP/MODIFY
*
         L     R07,COUNT           TIMES THROUGH LOOP
         MVC   INTERVAL+3(3),REPLY SAMPLING INTERVAL FOR STIMER
*
LOOP     EQU   *
         TIME  DEC
         STM   R00,R01,TIME
         XC    ECB2,ECB2
*
         EXCP  IOB
         WAIT  ECB=ECB2
         CLI   ECB2,X'7F'
         BNE   ABEND
*
         STIMER WAIT,DINTVL=INTERVAL
*
*   INSERT  TEST FOR STOP/MODIFY IN HERE
*
         BCT   R07,LOOP
         B     WTOR
*
*
ABEND    EQU   *
         ABEND 16,DUMP
*
BACK     EQU   *
         CLOSE PAGEOUT
*
         BACK  RC=0
ECB1     DC    F'0'
ECB2     DC    F'0'
*
*
BLANKS   DC    CL16' '
COUNT    DC    F'50'
INTERVAL DC    CL8'00000000'
TIME     DC    F'0'
DATE     DC    F'0'
REPLY    DC    CL10' '
PAGEOUT  DCB   DDNAME=PAGEOUT,MACRF=(E),DSORG=PS,IOBAD=IOB,RECFM=F,    C
               DEVD=DA,EXLST=EXLST
DWORD    DC    D'0'
PAGE     DC    F'0'
CCWS     DS    0D
DATECCW  CCW   1,TIME,X'A0',X'8'
PAGECCW  CCW   1,PAGE,X'20',X'0'   ADDR+ LENGTH INSERTEDDYNY
IOB      DS    0D
         DC    X'8000'
         DC    H'0'
         DC    A(ECB2)
IOBSTAT  DC    2F'0'
         DC    A(CCWS)
         DC    A(PAGEOUT)
         DC    F'0'
         DC    H'1'
         DC    H'0'
JFCB     DC    CL176' '
EXLST    DS    0F
         DC    X'87'
         DC    AL3(JFCB)
         LTORG
         END
************************************************************************
*
*                  P M 1 5 - ASSEMBLER PROGRAM
*
************************************************************************
*    PARM INFORMATION CAN BE SUPPLIED TO LIMIT THE REPORT PRODUCED.
*    STARTT=HHMMSS (DEFAULT=000000) - START TIME
*    STARTD=YYDDD  (DEFAULT=00000)  - START DATE
*    STOPT=HHMMSS  (DEFAULT=235959) - STOP TIME
*    STOPD=YYDDD   (DEFAULT=99355)  - STOP DATE
*    IF NO PARM IS SPECIFIED, ALL THE PM12 OUTPUT IS ANALYSED AND REPORT
************************************************************************
PM15     TITLE 'PRINT PAGE UTILISATION REPORT'
PM15     BEGIN
         CALL  GETPARM,(PARM1,PARM2,PARM3,PARM4),VL
         PACK  DWORD(4),STARTT(7)
         MVC   STT(3),DWORD
         PACK  DWORD(4),STOPT(7)
         MVC   SPT(3),DWORD
         PACK  STD(3),STARTD(5)
         PACK  SPD(3),STOPD(5)
         OPEN  (SYSUT1,,SYSPRINT,(OUTPUT))
         LH    R02,SYSUT1+62
*
*
         SH    R02,=H'8'       LRECL = 16 PAGES + 8
         SRL   R02,4           CALCULATE NUMBER OF PAGES
         ST    R02,PAGES
*
         CVD   R02,DWORD
         MVE   PAGEDIT,DWORD+5,MASK3
*
         LR    R03,R02
         SLL   R02,1           CALCULATE M/C SIZE
         CVD   R02,DWORD
         MVE   MCEDIT,DWORD+5,MASK1
*
         GET   SYSUT1              USE 1ST RECORD TO GIVE
         LR    R03,R01             DATE
*
         UNPK  DATED(5),5(3,R03)
*
         CALL  GREGDATE,(DATED,DATEG)
         MVC   DATEG+6(2),DATEG+4
         MVC   DATEG+4(1),DATEG+3
         MVC   DATEG+3(1),DATEG+2
         MVI   DATEG+2,C'/'
         MVI   DATEG+5,C'/'
*
         LA    R05,8(R03)
         SR    R06,R06
*
LOOP0    EQU   *
         CLI   9(R05),X'00'        DETERMINE SIZE
         BNE   NUCEND              OF NUCLEUS
         LA    R06,1(R06)
         LA    R05,16(R05)
         B     LOOP0
*
NUCEND   EQU   *
         CVD   R06,DWORD
         MVE   NUCSIZE,DWORD+6,MASK4
         PUT   SYSPRINT,HEADING1
         PUT   SYSPRINT,HEADING2
         LH    R09,LINECT       ENTRIES/PAGE
*
INITLOOP EQU   *
         CLC   STD,5(R03)
         BL    MAINLOOP
         BH    GETONE
         CLC   STT,0(R03)
         BL    MAINLOOP
*
GETONE   EQU   *
         GET   SYSUT1              TEST IF CURRENT
         LR    R03,R01             RECORD'S DATE
         B     INITLOOP            & TIME FALL
*
MAINLOOP EQU   *
         CLC   SPD,5(R03)          WITHIN SPECIFIED
         BL    EODAD               LIMITS.
         BH    OK
         CLC   SPT,0(R03)
         BL    EODAD
*
OK       EQU   *
*
         LA    R04,TABLE1
         L     R05,PAGES
         SR    R06,R06
         LA    R08,8(R03)
*
LOOP1    EQU   *
         IC    R06,9(R08)
         SLL   R06,2
         LA    R07,0(R04,R06)
         AP    0(4,R07),=P'1'
         LA    R08,16(R08)
         BCT   R05,LOOP1
*
*   TABLE 1  NOW  CONTAINS  TOTALS FOR THIS SNAPSHOT
*
         LA    R05,9
         LA    R06,TABLE1+4        IGNORE NUCLEUS
*                                  ACCUMULATE SYSTEM TOTAL - 11 TCBS
*
*
*
*
LOOP2    EQU   *
         AP    TABLE1+40(4),0(4,R06)
         LA    R06,4(R06)
         BCT   R05,LOOP2
*
         LA    R04,TABLE1
         LA    R05,TABLE4          MINIMA
         LA    R06,TABLE5          MAXIMA
         LA    R07,TABLE6          TOTALS
         LA    R08,32
*
LOOP15   EQU   *
         AP    0(8,R07),0(4,R04)   UPDATE MINIMA
         CP    0(4,R06),0(4,R04)   MAXIMA
         BNL   NOTLOW              AND TOTAL
         ZAP   0(4,R06),0(4,R04)   TABLES
*
NOTLOW   EQU   *
         CP    0(4,R05),0(4,R04)
         BNH   NOTHIGH
         ZAP   0(4,R05),0(4,R04)
*
NOTHIGH  EQU   *
         LA    R04,4(R04)
         LA    R05,4(R05)
         LA    R06,4(R06)
         LA    R07,8(R07)
         BCT   R08,LOOP15
*
*   TOTALS  FOR  SYSTEM + 12 PARTNS ARE IN TABLE1+40 - TABLE1 + 88
*
         MVI   PRINT+1,C' '        CLEAR PRINT LINE
         MVC   PRINT+2(131),PRINT+1
*
         UNPK  TIME,0(4,R03)
         MVC   HOUR,TIME           EDIT TIME INTO
         MVI   SEP1,C':'           PRINT LINE
         MVC   MIN,TIME+2
         MVI   SEP2,C':'
         MVC   SEC,TIME+4
         LA    R07,TABLE1+40
         BAL   R10,SUBR01
         AP    EVENTCT,=P'1'
         MVI   ASCII,C' '
         GET   SYSUT1
         LR    R03,R01
         MVC   TABLE1(128),TABLE2  CLEAR COUNTERS
         BCT   R09,MAINLOOP        60 ENTRIES PER PAGE
         PUT   SYSPRINT,HEADING1   REPEAT HEADINGS
         PUT   SYSPRINT,HEADING2
         MVI   ASCII,C'0'
         LH    R09,LINECT
         B     MAINLOOP
*
EODAD    EQU   *
*                   FORM   TABLE 3
         LA    R04,TABLE3
         LA    R05,TABLE6
         LA    R06,32
*
LOOP7    EQU   *
         DP    0(8,R05),EVENTCT
         MVC   0(4,R04),1(R05)
*
         LA    R04,4(R04)
         LA    R05,8(R05)
         BCT   R06,LOOP7
*
         MVI   PRINT+1,C' '        CLEAR PRINT LINE
         MVC   PRINT+2(130),PRINT+1
*
         CH    R09,=H'10'          ROOM LEFT ON PAGE
         BNL   ROOMLEFT
         MVI   ASCII,C'1'          NO - NEW PAGE
*
ROOMLEFT EQU   *
         OI    ASCII,C'0'          AT LEAST DOUBLE SPACE
*
         MVC   HOUR(8),=CL8'AVERAGES'
         LA    R07,TABLE3+40
         BAL   R10,SUBR01
*
         MVC   HOUR(8),=CL8'MINIMA'
         LA    R07,TABLE4+40
         BAL   R10,SUBR01
*
         MVC   HOUR(8),=CL8'MAXIMA'
         LA    R07,TABLE5+40
         BAL   R10,SUBR01
*
         MVI   PRINT+1,C' '
         MVC   PRINT+2(130),PRINT+1
         MVC   PRINT+2(14),=C'EVENTS SAMPLED'
         MVE   PRINT+16(6),EVENTCT,MASK3
         PUT   SYSPRINT,PRINT
*
         CLOSE (SYSUT1,,SYSPRINT)
         BACK  RC=15
*
SUBR01   EQU   *                   SUBROUTINE TO EDIT
         LA    R05,17              VALUES INTO PRINT LINE
         LA    R06,EDITAREA        FROM ANY TABLE OF 4 BYTE
*                                  PACKED FIELDS.
*                                  R07 IS ASSUMED TO POINT
LOOP3    EQU   *                   TO THE FIRST ENTRY IN
         MVC   0(7,R06),MASK2      THE TABLE AND THE
         ED    0(6,R06),1(R07)     INITIAL TITLE HAS ALREADY
         LA    R06,7(R06)          BEEN INSERTED
         LA    R07,4(R07)
         BCT   R05,LOOP3
*
*
         PUT   SYSPRINT,PRINT
         BR    R10
*
*
         EJECT
*
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,RECFM=F,MACRF=GL,EODAD=EODAD
*
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBA,MACRF=PM,LRECL=133
*
         LTORG
         EJECT
PRINT    DS    0CL133
ASCII    DC    C'0'
         DC    C' '
HOUR     DC    CL2'HH'
SEP1     DC    C':'
MIN      DC    CL2'MM'
SEP2     DC    C':'
SEC      DC    CL2'SS'
         DC    C' '
EDITAREA DC    CL119' '
         DC    CL3' '
*
MASK1    DC    X'402020212020D2'
MASK2    DC    X'40202020212040'
MASK3    DC    XL6'402020202120'
MASK4    DC    XL4'40202120'
*
*
*
*
*
*
*
DWORD    DC    D'0'
PAGES    DC    F'0'
TABLE1   DC    32PL4'0'
TABLE2   DC    32PL4'0'
TABLE3   DC    32PL4'0'                 AVERAGE SIZE
TABLE4   DC    32PL4'5000'       MINIMA
TABLE5   DC    32PL4'0'                 MAXIMA
TABLE6   DC    32PL8'0'                 TOTAL
LINECT   DC    H'60' *************************************************
*
EVENTCT  DC    PL3'0'                   NUMBER OF SAMPLES TAKEN
DATED    DC    CL5' '
*
*
HEADING1 DS    0CL133
         DC    X'F1'
TITLE    DC    CL50'PM15 - PAGEMAP - REAL STORAGE UTILISATION SUMMARY '
         DC    CL5' '
         DC    CL5'DATE '
DATEG    DC    CL8'DDMMYY  '
         DC    CL5' '
         DC    C'M/C SIZE ='
MCEDIT   DC    CL6' '
         DC    C'K ('
PAGEDIT  DC    CL6'MASK3 '
         DC    C' PAGES)    NUCLEUS -'
NUCSIZE  DC    CL4'MSK4'
         DC    C' PAGES'
         DC    CL4' '
HEADING2 DS    0CL133
         DC    X'F040'
         DC    CL9'  TIME   '
         DC    C' '
         DC    CL7'SYSTEM'
         DC    CL7'   P0'
         DC    CL7'   P1'
         DC    CL7'   P2'
         DC    CL7'   P3'
         DC    CL7'   P4'
         DC    CL7'   P5'
         DC    CL7'   P6'
         DC    CL7'   P7'
         DC    CL7'   P8'
         DC    CL7'   P9'
         DC    CL7'  P10'
         DC    CL7'  P11'
         DC    CL7'  P12'
         DC    CL7'  P13'
         DC    CL7'  P14'
         DC    CL7'  P15'
         DC    CL3' '
*
*
TIME     DC    CL7' '
PARM1    DS    0CL13
         DC    CL6'STARTT'
P1LEN    DC    C'='
STARTT   DC    CL6'000000'
         DC    C','
PARM2    DS    0CL12
         DC    CL6'STARTD'
P2LEN    DC    C'='
STARTD   DC    CL5'00000'
         DC    C','
PARM3    DS    0CL12
         DC    CL5'STOPT'
P3LEN    DC    C'='
STOPT    DC    CL6'235959'
         DC    C','
PARM4    DS    0CL11
         DC    CL5'STOPD'
P4LEN    DC    C'='
STOPD    DC    CL5'99355'
         DC    C','
*
*
STD      DC    XL3'000000'    INITIAL DEFAULTS.
STT      DC    XL3'000000'    RESET WHEN PARM
SPD      DC    XL3'FFFFFF'    CHECKING  ADDED
SPT      DC    XL3'FFFFFF'
*
         END
************************************************************************
*
*                  B E G I N - ASSEMBLER MACRO
*
************************************************************************
         MACRO
&NAME    BEGIN &BASE=1,&PL1=NO,&ENTRY=,&REGS=R01
         GBLC  &REG00,&REG01            REFERENCED IN 'BACK' MACRO
         GBLB  &FIRST                   FOR REGISTER EQUATES
         LCLC  &DATE,&ENT
         LCLA  &AWORK1,&AWORK2,&AWORK3
&NAME    CSECT
         AIF   (K'&ENTRY EQ 0).GENREGS
         ENTRY &ENTRY
&ENTRY   DS    0H
         AIF   (K'&ENTRY NE 8).GENREGS
&ENT     SETC  '&ENTRY'(1,4).'&ENTRY'(6,3)
         ENTRY &ENT
&ENT     DS    0H
.GENREGS AIF   (&FIRST).NORGEN
&FIRST   SETB  1
         AIF   ('&REGS' NE 'R01').GENREG1
R00      EQU   0                        ) I/0 AND
R01      EQU   1                        ) PARAMETERS
R02      EQU   2
R03      EQU   3
R04      EQU   4
R05      EQU   5
R06      EQU   6
R07      EQU   7
R08      EQU   8
R09      EQU   9
&REG00   SETC  'R00'
&REG01   SETC  'R01'
         AGO   .GENREG2
.GENREG1 ANOP
R0       EQU   0                        ) I/0 AND
R1       EQU   1                        ) PARAMETERS
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
&REG00   SETC  'R0'
&REG01   SETC  'R1'
.GENREG2 ANOP
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13                       POINTER TO REG SAVE AREA
R14      EQU   14                       RETURN ADDR
R15      EQU   15                       BRANCHING ADDR
.NORGEN  ANOP
         CNOP  2,4
         BALR  R15,&REG00
         USING *,R15
         STM   R14,R12,12(R13)          SAVE REGS
         ST    R13,*+44      CHAIN TO S/A
         LR    R14,R13
         LA    R13,*+34      POINT AT S/A
         ST    R13,8(R14)               CHAIN THIS S/A TO LAST
         CNOP  2,4
         B     *+98                     GO TO SET BASE REGS
         DC    CL8'&NAME. '             CSECT NAME
&DATE    SETC  '&SYSDATE'(4,3).'&SYSDATE'(1,3).'&SYSDATE'(7,2)
         DC    CL9'&DATE'               CURRENT DATE
         DC    CL5'&SYSTIME'            CURRENT TIME
         DC    18F'0'                   SAVE AREA
         DROP  R15
         AIF   ('&BASE'(1,1) NE '(').BASE05
.* GENERATE USING'S FOR EACH BASE SPECIFIED
&AWORK1  SETA  0
&AWORK2  SETA  0
&AWORK3  SETA  1
         BALR  &BASE(&AWORK3),&REG00    SET UP BASE REGISTER
.ALOOP   ANOP
         USING *-&AWORK1+&AWORK2,&BASE(&AWORK3)
&AWORK3  SETA  &AWORK3+1
         AIF   (K'&BASE(&AWORK3) EQ 0).AEND  EXIT IF END OF SUB-LIST
&AWORK1  SETA  &AWORK1+8
&AWORK2  SETA  &AWORK2+4096
         LA    &BASE(&AWORK3),2048(&BASE(&AWORK3-1)) SET UP NEXT
         LA    &BASE(&AWORK3),2048(&BASE(&AWORK3))     BASE REGISTER
         AGO   .ALOOP
.BASE05  ANOP
         BALR  R12,&REG00               SET UP BASE REGISTER
         USING *,R12
         AIF   ('&BASE' EQ '1').AEND
         LA    R11,2048(R12)            SET UP NEXT
         LA    R11,2048(R11)              BASE REGISTER
         USING *-8+4096,R11
         AIF   ('&BASE' EQ '2').AEND
         LA    R10,2048(R11)            SET UP NEXT
         LA    R10,2048(R10)              BASE REGISTER
         USING *-16+8192,R10
.AEND    ANOP
         MEND
************************************************************************
*
*                  B A C K - ASSEMBLER MACRO
*
************************************************************************
         MACRO
&NAME    BACK  &RC=0,&SORTEX=NO
         GBLC  &REG00,&REG01            SET IN 'BEGIN' MACRO
&NAME    DS    0H
         LA    R15,0&RC
         L     R13,4(R13)               GET ADDR OF LAST S/A
         L     R14,12(R13)              GET RETURN ADDR
         AIF   ('&SORTEX' EQ 'NO').ABACK20
         AIF   ('&SORTEX' EQ 'YES').ABACK15
         MNOTE 0,'INVALID SORTEX SPECIFICATION - NO ASSUMED'
         AGO   .ABACK20
.ABACK15 ANOP
         ST    &REG01,24(R13)
.ABACK20 ANOP
         LM    &REG00,R12,20(R13)       RESTORE REGS
         BR    R14                      RETURN
         MEND
************************************************************************
*
*                  P S 1 9 - GREGDATE - ASSEMBLER SUBROUTINE
*
************************************************************************
PS19     TITLE 'GREGDATE - CONVERT DATE FROM JULIAN(YYDDD) TO GREGORIAN-
               (DDMMYY)'
PS19     BEGIN ENTRY=GREGDATE
         LM    R02,R03,0(R01)           R02 ---> INPUT - YYDDD  5 BYTES
*                                       R03 --> OUTPUT - DDMMYY 6 BYTES
         PACK  WORKAR(3),0(2,R02)       PACK YEAR
         PACK  WORKAR+3(2),2(3,R02)        AND DAYS
         DP    WORKAR(3),P4             DIVIDE IT BY 4
         LA    R04,DATAB2               ASSUME LEAP
         CP    WORKAR+2(1),P0           IS IT?
         BE    SLOOPY                   YES, LET'S GO ---------------->
         LA    R04,DATAB1               NO, USE T'OTHER ONE           ×
*                                                                     ×
SLOOPY   EQU   *                                                      ×
         CP    WORKAR+3(2),0(2,R04)     IS IT WITHIN THIS MONTH? <-----
         BNH   EUREKA                   YES, OFT WE GO                ×
         LA    R04,4(R04)               NO, TRY NEXT DOOR             ×
         B     SLOOPY                   ROUND AGAIN ------------------>
*
EUREKA   EQU   *
         BCTR  R04,0                    REDUCE REG 4 BY ONE......
         BCTR  R04,0                                    TWO......
         BCTR  R04,0                                    THREE....
         BCTR  R04,0                                    FOUR.....
         SP    WORKAR+3(2),0(2,R04)     FIND DAYS LEFT
         MVC   2(2,R03),2(R04)          MOVE ACROSS MONTH FROM TABLE
         MVC   4(2,R03),0(R02)             & YEAR FROM INPUT
         UNPK  0(2,R03),WORKAR+3(2)     THEN MOVE DAYS
         OI    1(R03),X'F0'                & FORCE F-ZONE
*
         BACK
*
         EJECT
*
*  DAY TABLES
*
         SPACE 3
*  TABLE 1 - NON-LEAP YEARS
         SPACE 1
DATAB1   DC    P'00',C'01',P'31',C'02',P'59',C'03',P'90',C'04',P'120',C-
               '05',P'151',C'06',P'181',C'07',P'212',C'08',P'243',C'09'-
               ,P'273',C'10',P'304',C'11',P'334',C'12',P'999'
         SPACE 3
*  TABLE 2 - LEAP YEARS
         SPACE 1
DATAB2   DC    P'00',C'01',P'31',C'02',P'60',C'03',P'91',C'04',P'121',C-
               '05',P'152',C'06',P'182',C'07',P'213',C'08',P'244',C'09'-
               ,P'274',C'10',P'305',C'11',P'335',C'12',P'999'
*
WORKAR   DS    CL8
P4       DC    P'4'
P0       DC    P'0'
         END
************************************************************************
*
*                  P S 5 1 - GETPARM - ASSEMBLER SUBROUTINE
*
************************************************************************
PS51     TITLE 'GETPARM - GET PARM INFORMATION'
*              THIS MODULE WILL OBTAIN PARM INFORMATION SUPPLIED IN THE
* EXEC JCL CARD TO THE PROGRAM. PARM INFORMATION CAN BE EITHER
* POSITIONAL OR KEYWORD.
*        ONE PARAMETER ONLY IS REQUIRED IN THE FOLLOWING FORMAT-
*              KEYWORD=DEFAULT,    WHERE 'KEYWORD' IS EITHER THE
*                                      REQUIRED KEYWORD OR
*                                      &PNN   - WHERE NN=POSITIONAL
*                                      PARM INFORMATION
*
*              THE '=' MUST BE PRESENT TO INDICATE THE LENGTH OF THE
*              KEYWORD AND IS USED TO DETERMINE THE VALIDITY OF THE
*              KEYWORD. THE ',' SHOULD BE PLACED IN THE POSITION 1 BYTE
*              FOLLOWING THE MAXIMUM LENGTH OF THE EXPECTED PARM
*              INFORMATION.
*
*              THE PARM INFORMATION IS LOCATED BY SCANNING BACK UP
*        THE SAVEAREA CHAIN. FOR KEYWORD PARAMETERS THE PARM AREA IS
*        CHECKED FOR THE PRESENCE OF THAT KEYWORD. FOR POSITIONAL
*        PARAMETERS NN-1 COMMAS ARE BYPASSED TO GET THE REQUIRED
*        PARAMETER - IT IS THEN CHECKED TO ENSURE THAT IT IS NOT A
*        KEYWORD PARAMETER.
*              IF THE REQUIRED PARM INFORMATION IS NOT AVAILABLE
*        THE '=' SIGN IS REPLACED BY BINARY ZEROS - THEREFORE ALLOWING
*        BOTH ASSEMBLER AND COBOL PROGRAMS TO CHECK THE RESULT, NO
*        OTHER PROCESSING TAKES PLACE. IT IS POSIBLE TO INSERT A
*        DEFAULT VALUE BETWEEN THE '=' AND ',' WHICH WILL STILL BE
*        PRESENT IF NOT REPLACED BY PARM INFORMATION.
*              THE NUMBER OF BYTES OF PARM INFORMATION PASSED BACK
*        REPLACES THE '=' SIGN - ALLOWING VARIABLE LENGTH DATA TOK
*        BE ACCESSED
*              WHEN THE REQUIRED PARM INFORMATION IS FOUND THE LENGTH
*        OF THE INFORMATION IS COMPARED WITH THE EXPECTED MAXIMUM
*        LENGTH, IF GREATER THE JOB IS ABORTED.
*              IF THE KEYWORD= CONTAINS BLANKS OR COMMAS THE JOB IS
*        ABORTED.
*
         EJECT
PS51     BEGIN ENTRY=GETPARM
         LR    R11,R01
*
GTPM0    EQU   *
         L     R03,0(R11)          GET KEYWORD ADDRESS
         XC    TRTAB,TRTAB
         LR    R04,R03
         MVI   TRTAB+C' ',8        INITIALISE TRANSLATE TABLE
         MVI   TRTAB+C'=',12       WITH BRANCH INDEX
         MVI   TRTAB+C',',4        VALUES
*
GTPM1    EQU   *
         SR    R02,R02
         TRT   0(100,R04),TRTAB
         BZ    INVKEY              TRANSLATE COMPLETE-INVALID FORM
         B     *(R02)              BRANCH TO REQUIRED CHARACTER
         B     GTPM2               PROCESSOR 1-COMMA FOUND
         B     INVKEY              2-BLANK PRESENT BEFORE '='
         BC    2,INVKEY            3- '=' FOUND (IN LAST POS-ERROR)
         MVI   TRTAB+C'=',0        RESET TRANSLATE TABLE TO
         MVI   TRTAB+C' ',0        ALLOW '=' AND BLANKS
         LA    R04,1(R01)          INCREMENT PASSED '='
         SR    R01,R03             GET LENGTH OF KEYWORD
         BZ    INVKEY              IF ZERO - ERROR
         STC   R01,GTPM5+1         STORE LTH IN COMPARE INSTRUCTION
         LA    R01,1(R01)          GET TH INCLUDING '='
         STH   R01,KEYLTH          STORE LENGTH
         B     GTPM1
*
GTPM2    EQU   *
         CLI   TRTAB+C'=',0        '=' ALREADY FOUND
         BNE   INVKEY              NO - ERROR
         SR    R01,R04             GET MAXIMUM EXPECTED LENGTH
         STH   R01,MAXLTH          AND STORE
*
*        LINK DOWN SA CHAIN
*
         LR    R04,R03
         LR    R02,R13
*
GTPM3    EQU   *
         LM    R02,R03,4(R02)      LOAD SAVE AREA POINTERS
         LTR   R02,R02             LAST SA IN CHAIN
         BNZ   GTPM3               NO
         L     R02,4(R03)          YES RELOAD ADDRESS OF LAST SA
         LR    R03,R04
         L     R02,24(R02)         GET REG 1 CONTENTS
         L     R02,0(R02)          GET 1ST ADDR IN LIST (PARMINF)
         LA    R04,2(R02)          BYPASS LENGTH FIELD
         LH    R06,0(R02)          GET LENGTH
         LTR   R06,R06             ANY PARM INFORMATION
         BZ    NOPARM              NO
         CLC   0(2,R03),=C'&&P'    IS IT A POSITIONAL PARAMETER
         BE    GTPM8               YES
*
GTPM4    EQU   *
         CH    R06,KEYLTH          COMPARE KEYLTH AND PARM LENGTH
         BNH   NOPARM              INSUFFICIENT FOR KEY
*
GTPM5    EQU   *
         CLC   0(0,R03),0(R04)     PLUGGED - KEY FOUND
         BE    GTPM14              YES
*
GTPM6    EQU   *
         BCTR  R06,0
         STC   R06,*+5
         TRT   0(0,R04),TRTAB      LOOK FOR NEXT COMMA
         BC    10,NOPARM           NO MORE PRESENT
*
GTPM7    EQU   *
         SR    R01,R04             GET LENGTH OF LAST PARAM
         LA    R04,1(R04,R01)      INCREMENT PASSED IT
         SR    R06,R01             REDUCE PARM COUNT
         B     GTPM4               RETRY
*
GTPM8    EQU   *
         LH    R07,KEYLTH          GET KEY LENGTH
         LA    R07,X'6C'(R07)      CLC MACHINE LTH FOR PACK INST.
         STC   R07,GTPM9+1         SET UP MACHINE LTHS FOR PACK
*
GTPM9    EQU   *
         PACK  W8,2(0,R03)         PACK NN
         CVB   R07,W8              CONVERT POSITION NO
         MVI   TRTAB+C'=',8        SET UP TRANSLATE TABLE
*
GTPM10   EQU   *
         BCTR  R06,0
         SR    R02,R02
         STC   R06,*+5
         TRT   0(,R04),TRTAB       LOOK FOR END OF PARAMETER
         B     *+4(R02)            BRANCH TO REQUIRED ROUTINE
         B     GTPM11              END OF TRANSLATE
         B     GTPM12              COMMA
         B     NOPARM              EQ SIGN - KEYWORD -NOT POSITION
*
GTPM11   EQU   *
         BCT   R07,NOPARM          THIS PARAMETER - NO
         B     GTPM16              YES - IS LAST ONE PRESENT
*
GTPM12   EQU   *
         BCT   R07,GTPM13          THIS PARAMETER - NO LOOK AT NEXT
         B     GTPM15              YES
*
GTPM13   EQU   *
         SR    R01,R04             GET LENGTH OF LAST PARAMETER
         LA    R04,1(R01,R04)      INCREMENT TO NEXT
         SR    R06,R01             DECREMENT PARM COUNT
         BNP   NOPARM
         B     GTPM10              RETRY
*
GTPM14   EQU   *
         BCTR  R06,0               FIND LENGTH OF KEYWORD PARM.
         AH    R04,KEYLTH          INCREMENT PASSED KEY
         SH    R06,KEYLTH          DECREMENT PARM COUNT
         STC   R06,*+5
         TRT   0(0,R04),TRTAB
         BZ    GTPM16              LAST PARAMETER IN PARM
*
GTPM15   EQU   *
         SR    R01,R04             GET PARAMETER LTH
         BNP   NOPARM
         BCTR  R01,0
         LR    R06,R01
*
GTPM16   EQU   *
         CH    R06,MAXLTH          PARM LTH TOO LONG
         BNL   INVLTH              YES
*
GTPM17   EQU   *
         AH    R03,KEYLTH          INCREMENT PASSED PARAMETER KEYWD
         STC   R06,*+5
         MVC   0(0,R03),0(R04)     MOVE PARM INFORMATION TO PARAM
         BCTR  R03,0               GET BACK TO '='
         LA    R06,1(R06)          GET ACTUAL NO BYTES PASSED BACK
         STC   R06,0(R03)          STORE LENGTH IN PLACE OF '='
*
GTPM18   EQU   *
         TM    0(R11),X'80'        LAST INPUT PARAMETER
         LA    R11,4(R11)          INCREMENT TO NEXT PARAMETER
         BZ    GTPM0               NOT LAST
         BACK
*
NOPARM   EQU   *
         AH    R03,KEYLTH          REQUIRED PARM NOT FOUND
         BCTR  R03,0
         MVI   0(R03),0            INDICATE BY SETTING '=' TO ZERO
         B     GTPM18
*
INVKEY   EQU   *
         LA    R03,MESS1
         B     ABORT
*
INVLTH   EQU   *
         LA    R03,MESS2
         LH    R06,KEYLTH          SET UP
         BCTR  R06,0               MESSAGE
         BCTR  R06,0               TO INCLUDE
         CLM   R06,1,=X'07'        PROGRAM
         BNH   INVLTH1             KEYWORD
         IC    R06,=X'07'
*
INVLTH1  EQU   *
         STC   R06,*+5
         MVC   MESS2+39(0),0(R03)  SET UP KEYWORD
         CLI   MESS2+39,C'&&'      POSITIONAL KEYWORD
         BNE   ABORT               NO
         MVC   MESS2+39(2),MESS2+38 YES - REMOVE &&P
*
ABORT    EQU   *
         CALL  OPERATOR,((R03),CANCEL),VL
         EJECT
*
KEYLTH   DC    H'0'
MAXLTH   DC    H'0'
TRTAB    DC    XL256'0'
CANCEL   DC    C'CANCEL'
W8       DC    D'0'
MESS1    DC    C'51P   I INVALID PROGRAM PARAMETER SUPPLIED TO GETPARM'
MESS2    DC    C'54P   I PARM INFORMATION FOR PARAMETER          TOO LOX
               NG'
         END
************************************************************************
*
*                  P S 8 5 - OPERATOR - ASSEMBLER SUBROUTINE
*
************************************************************************
PS85     TITLE 'OPERATOR - PASS MESSAGE TO OPERATOR'
PS85     BEGIN ENTRY=OPERATOR
         EJECT
***
*        OPERATOR                       POSSIBLE FORMATS OF ENTRY
*                                       DEPENDANT ON NO OF PARAMS INPUT
*        PARAMS     MSG                 ACTION
*
*          1        99P123I   ETC       PRINT MSG & RETURN
*
*          1        99P123D   ETC       PRINT MSG
*                                       GET REPLY (USE INTERNAL AREA)
*                                                 (USE STD REPLY LEN)
*                                       PERMISSABLE REPLY :-
*                                                 'CANCEL' FOR ABEND
*                                                 'IGNORE' FOR EXIT
*
*          2     99P123I   ETC         PRINT MSG
*                                      IF 2ND PARAM = 'CANCEL', ABEND
*
*          2        99P123D   ETC       PRINT MSG
*                                       GET REPLY (USE STD REPLY LEN)
*                                       RETURN REPLY TO USER
*
*          3        99P123D   ETC       PRINT MSG
*                                       GET REPLY (USE USER LENGTH)
*                                       RETURN REPLY TO USER
*
*          *   ANY REPLY OF CANCEL WILL GIVE ABEND
***
         EJECT
         USING DTIOT,R01           FOR EXTRACT MACRO
         USING DPARM1,R03
         USING DPARM2,R04
         USING DPARM3,R05
*
STDLEN   EQU   6
*
MAXLEN   EQU   99                  LONGEST MESSAGE
*
MINLEN   EQU   5
*
ENDMK    EQU   X'80'               TEST FOR VL BIT
         SPACE 2
         LM    R03,R05,0(R01)      LOAD PARAMETERS
         LR    R06,R01             SAVE A(PARAM ALIST)
         SR    R07,R07             SET PARAM COUNT TO ZERO
*
CNTPARAM EQU   *
         TM    0(R06),ENDMK        TEST FOR LAST PARAM VL BIT
         LA    R07,1(0,R07)        ADD ONE
         LA    R06,4(0,R06)        SET R06 -> NEXT PARAM ADDR
         BNO   CNTPARAM            LOOP IF END NOT FOUND
         STC   R07,PARAMCNT        SAVE NO OF PARAMS
         SPACE 2
*
MSGSETUP EQU   *
         PACK  DW,MESSL            CONVERT MESSAGE LENGTH
         CVB   R08,DW              TO BINARY
         LTR   R08,R08             MESSAGE LENGTH NEG OR ZERO ?
         BNP   EXIT                YES, -> EXIT
         CH    R08,=Y(MAXLEN)      L'MESSAGE > MAXIMUM ALLOWED ?
         BNH   *+8
         LA    R08,MAXLEN          SET L'MESSAGE TO MAXIMUM LENGTH
         CH    R08,=Y(MINLEN)
         BNL   *+8
         LA    R08,MINLEN
         LA    R08,L'STEPNAME+6(R08) STEP UP MSG LENGTH
*              BY LENGTH OF ADDED STEPNAME
*              BY 2 FOR THE 2 SPACES AROUND THE ADDED STEPNAME
*              BY 4 BECAUSE WTOR EXPAN REQUIRES LENGTH TO BE 4 HIGH
         STH   R08,LWTORMSG        STORE LENGTH IN WTOR EXPANSION
         SPACE 2
         MVC   AWTORHDR,MESSHDR    INSERT MSG HDR INTO BUFFER
         MVI   WTORSP1,C' '
         EXTRACT ATIOT,FIELDS=(TIOT) GET TIOT
         L     R01,ATIOT           R01 -> TIOT
         MVC   AWTORSTP,STEPNAME   GET STEP NAME FROM TIOT FOR MSG
         MVI   WTORSP2,C' '
         SH    R08,=Y(L'MESSHDR+L'STEPNAME+6) = L' REST OF MSG TEXT
         BCTR  R08,0               GET MC LENGTH
         EX    R08,MVCWTOR         MOVE REST OF MSG INTO BUFFER
         EJECT
         OI    WTOSW,X'F0'         SET WTO BR TO NO REPLY
         CLI   MESSCODE,C'D'       MSG CODE = REPLY REQUD ?
         BE    REPLYREQ            NO, PRINT MSG AND EXIT
         LA    R01,WTO
         B     WTORSVC             GO EXEC MODIFIED WTO(R)
         SPACE 2
*
REPLYREQ EQU   *
         NI    WTOSW,X'0F'         SET BR TO WTOR NOT WTO
         CLI   PARAMCNT,X'02'      WERE TWO PARAMS SUPPLIED
         BL    INTREPLY            NO, IE NO REPLY AREA OR LENGTH
         BE    SETSTDLN            YES, IE REPLY AREA, BUT NO LEN
*
REPLEN   EQU   *                   PROCESS SUPPLIED REPLY LENGTH
         ZAP   DW,REPLYL           CONVERT REPLY LENGTH
         CVB   R08,DW
         CH    R08,=Y(MAXLEN)      REPLY LEN : MAXIMUM ?
         BNH   *+8                 NO, SKIP OVER
*
SETSTDLN EQU   *
         LA    R08,STDLEN          SET REPLY TO STD LEN (NOT MAX)
         ST    R04,AWTOREP         SET A(WTOR REPLY) IN WTOR EXPAN
         STC   R08,LWTOREP         INSERT LENGTH IN WTOR EXPANSION
         BCTR  R08,0               R08 = MC ( REPLY LENGTH )
         XC    WTORECB,WTORECB     SET WTORECB TO INCOMPLETE
         STC   R08,*+5
         MVC   0(0,R04),IGNORE
         SPACE 2
WTOR     WTOR  '                                                       -
                                                                    ', X
               0,108,WTORECB
*                                      L'WTOR LITERAL = MAXLEN+L'STEP+2
*                                      = 107 SO USED 108 !
         ORG   WTOR
         DS    CL4
LWTOREP  DS    0C                      L'WTOR REPLY
AWTOREP  DS    CL4                     A(WTOR REPLY)
         DS    CL4
WTO      EQU   *                       REFERENCED BY WTO
LWTORMSG DS    CL2
         DS    CL2
AWTORMSG DS    0CL108                  WTOR TXT MSG AREA
AWTORHDR DS    CL5                     WTO
WTORSP1  DS    C                       SPACE BEFORE STEPNAME
AWTORSTP DS    CL6                     STEPNAME INSERTED HERE
WTORSP2  DS    C                       SPACE AFTER STEONA,E
AWTOREST DS    CL95
         ORG
         SPACE 2
WTORSVC  EQU   *-2                     REFERENCED BY WTO
WTOBR    NOP   CHEKCAN                 BR IF WTO NOT WTOR
WTOSW    EQU   WTOBR+1                 MODIFIED INSTRUCTION
         WAIT  ECB=WTORECB
         SPACE 2
         LA    R01,1(0,R08)              SET R01 = REPLY LENGTH
         LR    R02,R04                   SET R02 -> REPLY
UPCASE   EQU   *
         OI    0(R02),X'40'             FORCE UPPER CASE
         LA    R02,1(0,R02)             SET R02 -> NEXT REPLY CHAR
         BCT   R01,UPCASE               LOOP TILL ALL REPLY IS UC
         CH    R08,=Y(STDLEN-1)         MESSAGE TOO SMALL TO BE CANCEL?
         BL    GIVREPLY                 YES, RETURN REPLY
ENDREQUD EQU   *
         CLC   WTOREPLY(L'CANCEL),CANCEL     REPLY = CANCEL ?
         BE    ABEND                         YES, ABEND
         CLI   PARAMCNT,X'01'                ONLY ONE PARAM GIVEN ?
         BNE   GIVREPLY                      NO, RETURN REPLY
         CLC   WTOREPLY(L'IGNORE),IGNORE     REPLY = ONLY OTHER VALID
         BE    EXIT                          REPLY (=IGNORE). YES, EXIT
         MVC   LWTORMSG,=Y(L'RETRY+L'STEPNAME+2)  NO, SETUP RETY MSG
         MVC   AWTOREST(L'RETRY),RETRY USING MSG HDR ONLY
         MVC   WTOREPLY(L'IGNORE),IGNORE     CLEAR REPLY AREA
         B     WTOR                          AND REQUEST RETRY
         SPACE 2
CHEKCAN  EQU   *
         CLI   PARAMCNT,X'01'          IS THIS THE SPECIAL CANCEL ?
         BNH   EXIT                    NO, EXIT
         CLC   USERCAN,CANCEL          USER SPECIFIED CANCEL ?
         BE    ABEND                   YES, GO ABEND
         B     EXIT                    CANCEL NOT SPECIFIED
         EJECT
INTREPLY EQU   *
         LA    R04,INTAREA              SET A(REPLY) TO INTERNAL AREA
         B     SETSTDLN                 SET DEFAULT REPLY LENGTH
ABEND    ABEND 99,DUMP
GIVREPLY EQU   *
EXIT     BACK                                RETURN TO CALLER
         EJECT
DW       DS    D                        WORKAREA
         LTORG
         DS    0F
WTORECB  DC    X'80000000'             WTOR ECB SET ON
ATIOT    DS    F
INTAREA  DS    CL6                     USE IF NO SER AREA SUPPLIED
MVCWTOR  MVC   AWTOREST(0),MESSTXT     MOVE MSG TEXT TO MSG AREA
CANCEL   DC    CL6'CANCEL'
IGNORE   DC    CL93' '                       SPACE = END OF BLOCK
RETRY    DC    CL6'RETRY'
PARAMCNT DS    C
         EJECT
DTIOT    DSECT
JOBNAME  DS    CL8
STEPNAME DS    CL6                     ONLY FIRST SIX BYTES USED
         SPACE 1
DPARM1   DSECT
MESS     DS    0CL82
MESSL    DS    CL2
MESSHDR  DS    0CL5
         DS    CL4
MESSCODE DS    C
MESSTXT  DS    CL94
         SPACE 1
DPARM2   DSECT
USERAREA DS    0CL80
USERCAN  DS    0CL6                    USER'S PRESET DEFAULT REPLY
WTOREPLY DS    CL80
         SPACE 1
DPARM3   DSECT
REPLYL   DS    PL2
         END
************************************************************************
*
*                            END OF CONTRIBUTION
*
************************************************************************
