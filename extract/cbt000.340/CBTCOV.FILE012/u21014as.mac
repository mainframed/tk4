DSCB     TITLE 'ZAJDSCB - PL/I SUBROUTINE TO READ A DSCB '
*
*        THIS ROUTINE READS THE DATA PORTION OF A FORMAT 1 DSCB,
*          GIVEN THE DATASET NAME, OR THE WHOLE OF A DSCB, GIVEN THE
*          ADDRESS IN CCHHR FORM.  FOR FURTHER DETAILS SEE THE
*          FOLLOWING MANUALS :-
*
*          C28-6628 SYSTEM CONTROL BLOCKS
*          C28-6550 SYSTEM PROGRAMMERS GUIDE
*
*        THE CALLING SEQUENCE IS
*              CALL ZAJDSCB(VOL,NAME,AREA1);
*          FOR THE FIRST FUNCTION, OR
*              CALL ZAJDSCB(VOL,CCHHR,AREA2);
*          FOR THE SECOND, WHERE THE ARGUMENT ATTRIBUTES ARE:-
*              VOL  CHAR(6),            VOLUME SERIAL NUMBER
*              NAME CHAR(44),
*              AREA1 CHAR(96),
*              CCHHR CHAR(5),
*              AREA2 CHAR(140)
*
*        THIS VERSION OF THE ROUTINE IS FOR USE WITH THE PL/I
*          OPTIMISING COMPILER.
*
**    AUTHORS - ICI (MOND DIVISION)
ZAJDSCB  PLISAV
*                        PROCESS PARAMETERS
         LM    2,4,0(1)            LOAD PARAMETER ADDRESSES.
         L     2,0(,2)
         MVC   VOL(6),0(2)         COPY VOLUME SERIAL.
         L     4,0(,4)             POINT AT RESULT AREA.
         CLC   4(2,3),=H'5'        CCHHR TYPE CALL ?
         BE    PROCCHR             BRANCH IF SO.
*                        PROCESS NAME TYPE CALL
         L     3,0(,3)
         MVC   DSN(44),0(3)        COPY DATASET NAME.
         OBTAIN FMT1
         LTR   15,15               ERROR ?
         BZ    FMTOK               BRANCH IF NOT.
         MVC   0(96,4),BLANKS      RETURN BLANK VALUE.
         MH    15,=H'5'
         LA    15,MSGS-20(15)
         MVC   0(20,4),0(15)
         B     BACK
*
FMTOK    MVC   0(96,4),AREA
         B     BACK
*
PROCCHR  L     3,0(,3)
         MVC   CCHHR(5),0(3)       COPY ADDRESS.
         OBTAIN OTHER
         LTR   15,15               ERROR ?
         BZ    OTHOK               BRANCH IF NOT.
         MVC   0(140,4),BLANKS     RETURN BLANK VALUE.
         B     BACK
*
OTHOK    MVC   0(140,4),AREA
*
BACK     L     13,4(,13)
         RETURN (14,12)
*
FMT1     CAMLST SEARCH,DSN,VOL,AREA
OTHER    CAMLST SEEK,CCHHR,VOL,AREA
VOL      DS    6C
DSN      DS    44C
CCHHR    DS    5C
AREA     DS    90F
BLANKS   DC    CL140' '
MSGS     DC    CL20'VOLUME NOT MOUNTED'
         DC    CL20'NOT FOUND ON VOLUME'
         DC    CL20'I/O ERROR IN VTOC'
S        DSECT
SAV      DS    26F                 SAVE AREA
         END
