E35      CSECT
*
*   EXIT ROUTINE USED BY TSGLSORT TO DETERMINE RETURN CODE FOR SORT
*
         USING *,15
         STM   1,4,24(13)
         LR    3,1
         BC    0,COUNT
         OI    *-3,240
*  THE FOLLOWING FIRST TIME ONLY ROUTINE FINDS THE ADDRESS OF THE
*  CONTROL INDICATORS AND COUNTERS IN TSGLSORT CSECT AND SAVES THE
*  ADDRESS IN COUNTAD FOR ALL SUBSEQUENT ENTRIES.
*  FOR MFT SYSTEMS THE CHAIN OF ACTIVE RBS IS USED TO FIND THE TSGLSORT
*  RB WHICH IMMEDIATELY PRECEDES THE CSECT.
*  FOR MVT SYSTEMS THE JOB STEP CDE CHAIN IS USED TO FIND THE CDE FOR
*  TSGLSORT FROM WHICH THE START OF CSECT (ENTRY POINT) IS FOUND.
         L     1,16
         L     3,0(1)
         L     3,4(3)              REG 3 -> TCB
         CLI   116(1),32
         BL    MVT
         LA    1,0(3)
         L     2,0(1)
TESTRB   CLC   0(8,2),=CL8'TSGLSORT'
         BE    ALLSET
         L     2,28(2)
         LA    2,0(2)
         CR    1,2
         BNE   TESTRB
ABEND    ABEND 35,DUMP
*  IF WE CANNOT FIND THE RB WE ABEND WITH A CODE TO DISTINGUISH
*   WHICH OF E15 OR E35 FAILED TO FIND IT.
*
*   IF WE FIND THE RB WE SET THE INDICATOR IN TSGLSORT TO SHOW WE HAVE
*   REACHED THIS STAGE.
*   WE ALSO SAVE THE ADDRESS OF THE RECORD COUNT FIELD IN TSGLSORT.
*   FIELDS IN TSGLSORT ARE FOUND BY DISPLACEMENT FROM THE RB.
*
ALLSET   MVI   117(2),255
         LA    2,124(2)
         ST    2,COUNTAD
EXIT     L     4,0(3)
         LTR   4,4
         LA    1,8
         BZ    BACK
         SR    1,1
BACK     LR    15,1
         LM    1,4,24(13)
         L     1,0(1)
         BR    14
*
*   ADD 1 TO THE RECORD COUNT IN TSGLSORT.  THIS IS NOT DONE FOR THE
*   FIRST ENTRY TO BALANCE OUT THE FACT THAT IT WILL BE DONE FOR THE
*   LAST ENTRY AT EOF.
COUNT    L     2,COUNTAD
         L     4,0(2)
         LA    4,1(4)
         ST    4,0(2)
         B     EXIT
         SPACE
MVT      L     1,44(3)             CDE POINTER FROM TCB
NEWCDE   LA    1,0(1)
         LTR   1,1
         BZ    ABEND               TSGLSORT CDE NOT PRESENT
         LR    2,1
         CLC   8(8,1),=CL8'TSGLSORT'
         L     1,0(1)
         BNE   NEWCDE
         L     2,16(2)             GET CDE ENTRY POINT
         LA    2,0(2)              BACK OFF 32 BYTES TO....
         LA    1,32                 ...SIMULATE POSITIONING OF RB
         SR    2,1
         B     ALLSET
COUNTAD  DC    F'0'
         END
