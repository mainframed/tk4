./ ADD NAME=SPROTECT
         MACRO
&NAME    ENTER &DSECT,&BASE=,&PARM=,&ID=,&SAVE=
.* THE ENTER MACRO IS USED WHERE A CSECT IS ENTERED (REGISTER 15 MUST
.* POINT TO THE BEGINING OF THE ENTER MACRO).IT DOES THE FOLLOWING -
.*  1. SAVES REGISTERS.
.*  2. SETS UP BASE REGISTER(S) AS FOLLOWS -
.*     (A) REG 13 SET UP AS A BASE REGISTER POINTING TO THE BEGINING
.*         OF THE SAVE AREA (SEE 3 BELOW). THE ONLY TIME THIS IS
.*         NOT DONE IS IF SAVE IS NOT SPECIFIED, DSECT IS NOT
.*         SPECIFIED, AND BASE IS SPECIFIED.
.*     (B) UNLESS NO GETMAIN IS SPECIFIED, AND NO SAVE AREA IS
.*         SPECIFIED, A BASE REGISTER IS SET UP IN ADDITION TO REG 13.
.*  3. DOES ONE OF THE FOLLOWING -
.*     (A) SETS UP A SAVE AREA.
.*     (B) USES A SAVE AREA WHOSE ADDRESS IS PROVIDED.
.*     (C) GETMAINS AN AREA WHOSE FIRST (OR ONLY) 18 WORDS ARE THE
.*        SAVE AREA. THIS FORMAT WOULD BE USED IN A RE-ENTRANT PROGRAM.
.*  4. CHAINS THE SAVE AREAS.
.*  5. OPTIONALLY SAVES REGISTER 1 IN ANOTHER REGISTER.
.*  6. OPTIONALLY GENERATES A CSECT CARD.
.*       FORMAT OF MACRO -
.* NAME  ENTER (DSECT,LV),BASE=R1,PARM=R2,ID=ID,SAVE=SAVEAREA
.*       WHERE -
.*  NAME  IS THE NAME USED ON A GENERATED CSECT CARD. IF OMITTED, NO
.*             CSECT CARD IS GENERATED.
.*  (DSECT,LV) SPECIFIES THE NAME AND LENGTH (IN BYTES) OF A DSECT FOR
.*             THE AREA TO BE GETMAINED; A USING DSECT,13 IS GENERATED
.*             FOR A GETMAINED AREA.IF ONLY DSECT IS SPECIFIED,LV
.*             DEFAULTS TO 72 BYTES. (DSECT,LV) CANNOT BE SPECIFIED
.*             WITH SAVE.
.*  BASE  SPECIFIES THE BASE REGISTER SET UP.DEFAULT OF BASE=12 UNLESS
.*             NEITHER (DSECT,LV) OR SAVE ARE SPECIFIED. IN THIS CASE
.*             REG 13 ACTS AS THE BASE REGISTER BECAUSE A SAVE AREA
.*             IS SET UP IS AT THE BEGINING OF THE PROGRAM. THE
.*             REGISTER SPECIFIED MUST BE IN THE RANGE 2 TO 12.
.*  PARM  SPECIFIES THE REGISTER USED TO SAVE THE CONTENTS OF REGISTER
.*             1.NO SAVING DONE IF PARM NOT SPECIFIED,BUT REGISTER 1
.*             WILL REMAIN UNCHANGED IF THE GETMAIN OPTION IS NOT USED.
.*             THE REGISTER MUST BE IN THE RANGE 2 TO 12.
.*  ID    SPECIFIES AN IDENTIFIER OPERAND IDENTICAL TO THAT FOR THE
.*             IBM-SUPPLIED SAVE MACRO.
.*  SAVE  SPECIFIES THE ADDRESS OF A SAVE AREA. IT CANNOT BE SPECIFIED
.*             WITH (DSECT,LV) AS THE SAVE AREA IS GETMAINED IN THIS
.*             CASE. IF NEITHER SAVE OR (DSECT,LV) IS SPECIFIED THEN
.*             A SAVE AREA IS SET UP AT THE BEGINING OF THE PROGRAM.
.*             NOTE THAT IF SAVE IS SPECIFIED THE AREA MUST BE
.*             WITHIN 4095 BYTES OF THE BEGINING OF THE CSECT.
         AIF   ('&NAME' EQ '').NONAME
&NAME    CSECT
.NONAME  USING *,15
         SAVE  (14,12),T,&ID
         AIF   ('&BASE' EQ '').TGET
         BALR  &BASE,0
         USING *,&BASE
         AGO   .TPARM
.TGET    AIF   ('&SAVE' NE '').SETBASE
         AIF   ('&DSECT' EQ '').TPARM
.SETBASE BALR  12,0
         USING *,12
.TPARM   AIF   ('&PARM' EQ '').NOPARM
         LR    &PARM,1
.NOPARM  AIF   ('&DSECT' EQ '').NOGET
         DROP  15
         AIF   ('&SAVE' NE '').ERR
         AIF   ('&DSECT(2)' EQ '').NOLV
         GETMAIN R,LV=&DSECT(2)
         AGO   .LV
.NOLV    GETMAIN R,LV=72
.LV      ST    13,4(,1)
         ST    1,8(,13)
         LR    13,1
         USING &DSECT(1),13
         MEXIT
.NOGET   AIF   ('&SAVE' NE '').SAVE
         CNOP  0,4
         BAL   15,*+76
         DROP  15
S&SYSNDX DC    18F'0'
         AGO   .NOSAVE
.SAVE    LA    15,&SAVE
         DROP  15
.NOSAVE  ST    13,4(,15)
         ST    15,8(,13)
         LR    13,15
         AIF   ('&SAVE' NE '').ENDX
         AIF   ('&BASE' NE '').END
         USING S&SYSNDX,13
.END     MEXIT
.ENDX    USING &SAVE,13
         MEXIT
.ERR     MNOTE 12,'**** DSECT AND SAVE CANNOT BE SPECIFIED TOGETHER IN X
               ENTER MACRO'
         MEND
         MACRO
&NAME    EXIT  &DSECT,&T,&RC=0
.* THE EXIT MACRO IS USED TO RETURN FROM A CSECT.IT DOES THE FOLLOWING-
.*  1. RESTORES THE SAVE REGISTER.
.*  2. OPTIONALLY FREEMAINS THE AREA POINTED TO BY REG 13.
.*  3. SETS A RETURN CODE,IF REQUIRED.
.*  4. OPTIONALLY SETS THE X'FF' INDICATOR IN THE SAVE AREA TO SAY THAT
.*     CONTROL HAS BEEN RETURNED TO THE CALLING ROUTINE.
.*       FORMAT OF MACRO -
.* NAME  EXIT  (R,LV),&T,RC=NN
.*       WHERE -
.* (R,LV) SPECIFIES THAT A FREEMAIN IS REQUIRED.R IS CODED AS SHOWN,
.*       AND LV IS THE NUMBER OF BYTES TO BE FREEMAINED.IF ONLY R IS
.*       SPECIFIED,LV DEFAULTS TO 72 BYTES.IF NOTHING IS SPECIFIED,
.*       NO FREEMAIN IS DONE.
.* T      SPECIFIES THAT THE SAVE AREA IS TO BE FLAGGED WITH X'FF' TO
.*       INDICATE RETURN OF CONTROL TO THE CALLING PROGRAM. FLAGGING
.*       IS DONE IF T CODED - OTHERWISE OMIT THIS OPERAND.
.* RC     SPECIFIES ONE OF -
.*         (A) RC=(15)  WHICH MEANS THAT A RETURN CODE IS ALREADY
.*             LOADED INTO REG 15.
.*         (B) RC=(R)  WHERE R IS A REGISTER THAT THE RETURN CODE
.*             HAS BEEN LOADED INTO.
.*         (C) RC=VALUE  WHERE VALUE IS THE RETURN CODE TO BE SET.
.*             THE MAXIMUM RETURN CODE POSSIBLE IS 4095.
.*        NOTE THAT RC=0 IS THE DEFAULT.
         AIF   ('&DSECT(1)' EQ 'R').C2
&NAME    L     13,4(,13)
.C3      AIF   ('&RC'(1,1) NE '(').C3A
         AIF   ('&RC' EQ '(15)').C3A
         LR    15,&RC(1)
         RETURN (14,12),&T.,RC=(15)
         MEXIT
.C3A     RETURN (14,12),&T.,RC=&RC
         MEXIT
.C2      ANOP
&NAME    LR    1,13
         LR    12,15
         L     13,4(,13)
         AIF   ('&DSECT(2)' NE '').C4
         FREEMAIN R,LV=72,A=(1)
         AGO   .C5
.C4      FREEMAIN R,LV=&DSECT(2),A=(1)
.C5      LR    15,12
         AGO   .C3
         MEND
         TITLE 'SPROTECT - TAPE SECURITY PROTECTION'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  SPROTECT IS USED TO ALTER THE VALUE OF THE DATA SET SECURITY BYTE  *
*  WHICH IS HELD IN THE HDR1 RECORD OF A TAPE LABEL. THIS BYTE WILL   *
*  CONTAIN ONE OF THE FOLLOWING THREE VALUES:-                        *
*    C'0' - NO PASSWORD PROTECTION PRESENT                            *
*    C'1' - READ/WRITE PASSWORD PROTECTION PRESENT                    *
*    C'3' - WRITE-ONLY PASSWORD PROTECTION PRESENT                    *
*                                                                     *
*  JCL REQUIRED:-                                                     *
*      //SPRT  EXEC  PGM=SPROTECT,PARM=........                       *
*      //TAPE  DD    DSN=MY.GDG(-2),DISP=OLD      (FOR EXAMPLE)       *
*      //SYSUDUMP DD SYSOUT=A                                         *
*                                                                     *
*  THE PARM FIELD CAN HAVE THE FOLLOWING VALUES:-                     *
*    1)  OMITTED  : REMOVE ALL SECURITY PROTECTION                    *
*    2)  SPROTECT : ADD READ/WRITE SECURITY PROTECTION                *
*    3)  WPROTECT : ADD WRITE-ONLY SECURITY PROTECTION                *
*                                                                     *
*  RETURN CODES:-                                                     *
*    00 - NORMAL COMPLETION                                           *
*    08 - INVALID PARM FIELD                                          *
*    12 - UNABLE TO OPEN DDNAME TAPE AND/OR DEVICE IS NOT A TAPE      *
*    16 - NO HDR1 RECORD FOUND ON TAPE                                *
*                                                                     *
*  ABEND CODES:-                                                      *
*    U0001 - CHANNEL PROGRAM ERROR (CONTACT SYSPROGS)                 *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         REGS
         EJECT
SPROTECT ENTER ID=*
         LA    R11,8          SET BAD RETURN CODE
         LA    R10,240        ASSUME NO SECURITY (C'0' TO R10)
         L     R1,0(R1)       ADDRESS PARM FIELD
         LH    R2,0(R1)       GET PARAMETER LENGTH
         LTR   R2,R2          PARAMETER PRESENT?
         BZ    OPENTAPE       NO, SKIP
         LA    R10,241        ASSUME READ-WRITE SECURITY (C'1' TO R10)
         CLC   2(8,R1),SPRT   PARM=SPROTECT?
         BE    OPENTAPE       YES, SKIP
         LA    R10,243        ASSUME WRITE-ONLY SECURITY (C'3' TO R10)
         CLC   2(8,R1),WPRT   PARM=WPROTECT?
         BNE   EXIT           NO, IMMEDIATE ERROR EXIT
OPENTAPE EQU   *
         LA    R11,12         SET BAD RETURN CODE
         OPEN  (DCB3420,(INOUT))   OPEN TAPE
         TM    DCB3420+48,X'10'    OPEN SUCCESSFUL
         BZ    EXIT           NO, IMMEDIATE ERROR EXIT
         L     R2,DCB3420+44  GET DEB ADDRESS
         L     R2,32(R2)      GET UCB ADDRESS
         CLI   18(R2),X'80'   TEST UCBTYP FOR TAPE
         BNE   CLOSE          ERROR IF NOT TAPE
         LA    R11,16         SET BAD RETURN CODE
         LA    R2,CCWREW      ADDRESS REWIND CCW
         STCM  R2,7,CCW3420   SAVE ADDRESS IN IOB
         BAL   R8,EXCP        GO REWIND TAPE
         LA    R3,LABEL       ADDRESS OUTPUT AREA
GETDATA  EQU   *
         STCM  R3,7,CCWREAD+1 SAVE INPUT AREA ADDRESS IN CCW
         LA    R2,CCWREAD     ADDRESS READ CCW
         STCM  R2,7,CCW3420   SAVE ADDRESS IN IOB
         BAL   R8,EXCP        GO READ ONE BLOCK OF DATA
         CLC   LABEL(4),VOL1  VOL1 LABEL PRESENT?
         BNE   CLOSE          NO, IMMEDIATE ERROR EXIT
         LA    R3,80(R3)      ADDRESS NEXT INPUT AREA
         B     GETDATA        GO READ NEXT BLOCK
TAPEMARK EQU   *
         CLC   LABEL+80(4),HDR1    HDR1 LABEL PRESENT?
         BNE   CLOSE          NO, IMMEDIATE ERROR EXIT
         LA    R11,0          SET GOOD RETURN CODE
         STC   R10,LABEL+133  SET DATA SET SECURITY BYTE IN HDR1
         LA    R2,CCWREW      ADDRESS REWIND CCW
         STCM  R2,7,CCW3420   SAVE ADDRESS IN IOB
         BAL   R8,EXCP        GO REWIND TAPE
         LA    R4,LABEL       ADDRESS INPUT AREA
PUTDATA  EQU   *
         CR    R3,R4          END OF INPUT DATA?
         BE    CLOSE          YES, TAPE LABEL COMPLETELY REWRITTEN
         STCM  R4,7,CCWRITE+1 SET OUTPUT AREA ADDRESS IN CCW
         LA    R2,CCWRITE     ADDRESS WRITE CCW
         STCM  R2,7,CCW3420   SAVE ADDRESS IN IOB
         BAL   R8,EXCP        GO WRITE A BLOCK OF DATA
         LA    R4,80(R4)      ADDRESS NEXT OUTPUT AREA
         B     PUTDATA        GO OUTPUT NEXT BLOCK
CLOSE    EQU   *
         CLOSE DCB3420        CLOSE TAPE
EXIT     EQU   *
         LR    R15,R11        RECOVER RETURN CODE
         EXIT  RC=(15)        RETURN TO O/S
         SPACE 6
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                         INPUT/OUTPUT BLOCK                          *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
IOB3420  DS    0F
         DC    X'02'          IOB UNRELATED FLAG (NON-SEQUENTIAL)
         DC    4X'00'         IOBFLAG2/IOBSENS0/IOBSENS1/IOBECBCC
         DC    AL3(ECB3420)   ECB ADDRESS
         DC    2F'0'          CSW DETAILS
         DC    X'00'          SIO CONDITION CODE
CCW3420  DC    AL3(0)         CCW ADDRESS
         DC    X'00'          IOBFLAG4
         DC    AL3(DCB3420)   DCB ADDRESS
         DC    2F'0'          NOT USED
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                           EXCP SUBROUTINE                           *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
EXCP     EQU   *
         EXCP  IOB3420        PERFORM I/O OPERATION
         WAIT  ECB=ECB3420    WAIT FOR I/O COMPLETION
         XC    ECB3420,ECB3420     CLEAR ECB
         CLI   IOB3420+4,X'7F'     SUCCESSFUL COMPLETION?
         BER   R8             YES, RETURN TO CALLER
         TM    IOB3420+12,X'0D'    TEST UNIT STATUS IN CSW
         BO    TAPEMARK       CE + DE + UE INDICATES TAPEMARK
         TM    IOB3420+3,X'02'     FILE PROTECTION?
         BO    EXCP           YES, GO RETRY COMMAND
         ABEND 1,DUMP         CHANNEL PROGRAM ERROR
         SPACE 6
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                        CHANNEL COMMAND WORDS                        *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
CCWREW   CCW   X'07',X'000000',X'20',1  REWIND TAPE
CCWREAD  CCW   X'02',X'000000',X'20',80 READ TAPE RECORD
CCWRITE  CCW   X'01',X'000000',X'20',80 WRITE TAPE RECORD
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                     VARIABLES,CONSTANTS AND DCBS                    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ECB3420  DC    F'0'
HDR1     DC    C'HDR1'
VOL1     DC    C'VOL1'
SPRT     DC    C'SPROTECT'
WPRT     DC    C'WPROTECT'
LABEL    DC    880X'00'       BUFFER AREA FOR MAXIMUM OF 11 LABELS
*                      THIS CORRESPONDS TO VOL1, HDR1-2 AND UHL1-8
DCB3420  DCB   DSORG=PS,DDNAME=TAPE,MACRF=(E),IOBAD=IOB3420
         END
