***********************************************************************
*
*     PROGRAM   GN006
*     -------
*
*     OBJECT    TO PRINT AN INDEXED SEQUENTIAL FILE IN HEXADECIMAL/
*     ------
*               CHARACTER FORMAT. THE RECORDS ARE PRINTED IN LINES
*
*               OF 100 BYTES. THE KEY IS PRINTED SEPARATELY ON THE
*
*               LEFT HAND SIDE OF THE LINE AND THE RECORD NUMBER
*
*               AND LENGTH ON THE RIGHT HAND SIDE OF THE SAME LINE
*
*               UNPRINTABLE AND SPECIAL CHARACTERS ARE PRINTED IN
*
*               HEX ON CONSECUTIVE LINES
*
*     PARAMETER PARM='X,Y'
*     ---------
*               WHERE X IS NO. OF RECORDS TO BE PRINTED AND Y IS NO.
*
*               OF RECORDS TO BE IGNORED BEFORE PRINTING. WHEN NO
*
*               PARAMETER IS SPECIFIED PROGRAM WILL PRINT 50 RECS.
*
*               IF PARM='0' IS SPECIFIED THE ENTIRE FILE WILL BE
*
*               PRINTED. THE MAXIMUM LENGTH OF X AND Y IS 6 BYTES.
*
*     MESSAGES  GN006 UNABLE TO OPEN SYSPRINT
*     --------
*               GN006 UNABLE TO OPEN DUMP
*
*               GN006 NO RECORDS ON DUMP DATASET
*
*               GN006 PARAMETER EXCEEDS MAXIMUM LENGTH
*
*               GN006 IGNORE COUNT EXCEEDS FILE SIZE
*
*               GN006 RECORD COUNT EXHAUSTED
*
*               GN006  ***END OF PROGRAM***
*
*     SAMPLE JCL //   EXEC PGM=GN006,PARM='XXXXXX,YYYYYY'
*     ----------
*                //SYSPRINT DD SYSOUT=A
*
*                //DUMP     DD  -DEFINITION OF FILE TO BE PRINTED-
*
***********************************************************************
GN006    CSECT
         SAVE  (14,12),,*
         LR    10,15
         USING GN006,10
         ST    13,SAVE+4
         LA    14,SAVE
         ST    14,8(13)
         LR    13,14
         ST    1,PARMAD
*
*                             OPEN FILES
*
         OPEN  (DUMP,(INPUT),SYSPRINT,(OUTPUT))
         TM    SYSPRINT+48,X'10'
         BO    PRNTOK
         LA    1,MSG1
         SVC   35
         MVI   RETCODE,C'8'
         B     ABEOJ
PRNTOK   EQU   *
         TM    DUMP+48,X'10'
         BO    DMPOK
         MVC   MSG1+29(8),=C'DUMP    '
         LH    3,MSG1
         SH    3,=H'5'
         STH   3,MSG1+2
         LA    3,MSG1+2
         B     ERRTN
DMPOK    EQU   *
         MVC   LINCNT,=P'70'
         LA    5,0
         LA    4,0
         LH    4,DUMP+82
         GETMAIN R,LV=(4)
         ST    1,TLINE
         GETMAIN R,LV=(4)
         ST    1,BLINE
*
*                             PARAMETER CHECK
*
         L     1,PARMAD
         L     2,0(1)
         LH    4,0(2)
         LA    8,2(2)        R8 POINTS TO PARAM INFO
         LA    1,1(2)        R1 POINTS TO 1 BEFORE
         LA    9,50      PARM NOT REQUESTED - DEFAULT 50
         LTR   4,4       TEST LENGTH OF PARM
         BZ    INIT
PARMRQ   EQU   *
         LA    3,13
         CR    4,3           PARM MAX LENGTH 13
         BNH   PAROK
         LA    3,MSG2
         B     ERRTN
PAROK    EQU   *
         LR    7,1
         AR    7,4           R7 POINTS TO LAST BYTE PARM
         BCTR  4,0
LOOP     LA    2,0
         EX    4,TRANSFER     TRT 1,(0,1),TRANTAB
         BC    8,ENDSCAN
         LR    4,1
         SR    4,8           LENGTH FIRST PARAMETER TO R4
         BZ    GETPAR2
         BCTR  4,0           REDUCE R4 BY 1
         EX    4,PACKEX      PACK  PKPRM,0(0,8)
         CVB   9,PKPRM       R9=PARAMETER 1 VALUE
GETPAR2  LA    8,1(1)        R8 POINTS TO PARAMETER 2
         LR    4,7
         SR    4,1
         BCTR  4,0           R4 CONTAINS LENGTH PARAM2 LESS 1
         OI    PARM2SWT,X'01'   INDICATE THERE IS A PARAM2
         B     LOOP
ENDSCAN  EX    4,PACKEX          PACK PARAM1 OR PARAM2
         TM    PARM2SWT,X'01'
         BZ    NOPARM2
         CVB   8,PKPRM       PARAM2 COUNT TO R8
         ST    8,PARM2       SAVE COUNT IN PARM2
         B     *+8
NOPARM2  CVB   9,PKPRM       PARAM1 COUNT TO R9
         LTR   9,9
         BZ    INIT           BRANCH IF PARAMETER 1 0 OR NOT SPECIFIED
         A     9,PARM2       R9=PARAMETER1 + PARAMETER2 COUNTS
*
*
*
INIT     EQU   *
         TM    DUMP+36,X'40'       IS INPUT FILE VARIABLE
         BO    VARBL
UORF     EQU   *
         LH    8,DUMP+82           IF FIXED - TAKE LRECL
VARBL    EQU   *
         LA    7,0
         IC    7,DUMP+16     NOTE KEYLEN
         LH    6,DUMP+60     NOTE RKP
*
*
*
GETNXT   EQU   *
         NI    RECLN,X'7F'         SWITCH RECLN OFF.
         LTR   9,9                 IF PARM = 0 THEN
         BZ    READR               GO TO READ - OTHERWISE -
         CR    9,5                 CHECK REC COUNT.
         BNE   READR
         LA    3,MSG4
         B     ERRTN
READR    GET   DUMP                READ NEXT REC.
         A     5,=F'1'
         C     5,PARM2       TEST CURRENT READ COUNT 8 IGNORE COUNT
         BNH   INIT          IGNORE IF READ COUNT < OR = IGNORE COUNT
         TM    DUMP+36,X'40'       IS RECFM V
         BNO   ROUN
         LH    8,0(1)              IF V - STORE REC LENGTH
         S     8,=F'4'
         LA    1,4(1)                     AND UPDATE BUFFER PTR
ROUN     EQU   *
         LR    2,1
         NI    RIND,X'7F'
         L     4,TLINE
         LA    11,256
ENCORE   EQU   *
         LR    3,8
COMP     EQU   *
         CR    3,11
         BNH   MVRC
         MVC   0(256,4),0(2)
         SR    3,11
         AR    2,11
         AR    4,11
         B     COMP
PACKEX   PACK  PKPRM,0(0,8)
TRANSFER TRT   1(0,1),TRANTAB
MOVEX    MVC   0(1,4),0(2)
MVRC     EQU   *
         SH    3,=H'1'
         EX    3,MOVEX
         TM   RIND,X'80'
         BO    TRNSLT
         L     4,BLINE
         L     2,TLINE
         OI   RIND,X'80'
         B     ENCORE
*
*                             TRANSLATE
*
TRNSLT   EQU   *
         NI    RIND,X'7F'
         LR    3,8
         L     4,TLINE
         LA    12,TABLE1
TRAN     EQU   *
         CR    3,11
         BNH   TRANOUT
         TR    0(256,4),0(12)
         SR    3,11
         AR    4,11
         B     TRAN
TRANEX   TR    0(1,4),0(12)
TRANOUT  EQU   *
         SH    3,=H'1'
         EX    3,TRANEX
         TM    RIND,X'80'
         BO    OUT
         L     4,BLINE
         LA    12,TABLE2
         LR    3,8
         OI    RIND,X'80'
         B     TRAN
OUT      NI    RIND,X'7F'
         L     11,TLINE
         L     3,BLINE
         AR    11,6
         AR    3,6
         LA    4,100
         ST    8,LRECL
         OI    RIND,X'01'
         BAL   8,BACK
         B     PUTRECED
BACK     EQU   *
         OI    RIND,X'01'
         BAL    6,PUTOUT
         TM    RIND,X'80'
         BO    BACK1
         MVC   3(3,12),=C'KEY'
         OI    RIND,X'80'
BACK1    EQU   *
         CR    7,4
         BNH   KEYEND
         MVC   10(100,12),0(11)
         TRT   0(100,3),TABLE3
         BZ    BACKED
         SP    LINCNT,=P'2'
         BAL   6,PUTOUT
         AP    LINCNT,=P'2'
         MVC   10(100,12),0(3)
BACKED   EQU   *
         SR    7,4
         AR    3,4
         AR    11,4
         B     BACK
TRTEX    TRT   0(1,3),TABLE3
MOVEDEX  MVC   10(1,12),0(11)
KEYEND   EQU   *
         S     7,=F'1'
         EX    7,MOVEDEX
         EX    7,TRTEX
         BZ    PUTREC
         SP    LINCNT,=P'2'
         BAL   6,PUTOUT
         AP    LINCNT,=P'2'
         LR    11,3
         EX    7,MOVEDEX
PUTREC   BR    8
PUTOUT   EQU   *
         PUT   SYSPRINT
         MVC   0(2,1),=C'0 '
         MVC   2(131,1),1(1)
         CP    LINCNT,=P'58'
         BH    NEWPAGE
NOTNEWPG LR    12,1
PUT1     EQU   *
         TM    RIND,X'01'
         BNO   MVCBLK
         AP    LINCNT,=P'2'
         NI    RIND,X'FE'
         B     TESTFRT
MVCBLK   EQU   *
         AP    LINCNT,=P'1'
         MVI   0(1),X'40'
         B     TESTFRT
NEWPAGE  EQU   *
         AP    PCNT,=P'01'
         MVC   PAGECNT,PATTERN
         ED    PAGECNT,PCNT
         MVI   0(1),C'1'
         MVC   1(130,1),HEADING
         ZAP   LINCNT,=P'00'
         B     PUTOUT
TESTFRT  EQU   *
         TM    RECLN,X'80'
         BO    TEST2
TEST1    EQU   *
         ST    5,REGST
         CVD   5,CONV
         UNPK  116(6,1),CONV
         OI    121(1),X'F0'
         L     5,LRECL
         CVD   5,CONV
         UNPK  125(6,1),CONV
         OI    130(1),X'F0'
         L     5,REGST
         OI    RECLN,X'80'
TEST2    BR    6
PUTRECED EQU   *
         L     11,TLINE
         L     3,BLINE
         OI    RIND,X'01'
         L     7,LRECL
         BAL   8,BACK
         B     INIT
MVMSG    MVC   4(0,1),2(3)
ERRTN    EQU   *
         PUT   SYSPRINT
         MVC   0(2,1),ASA
         MVC   2(131,1),1(1)
         LH    6,0(3)
         EX    6,MVMSG
         MVC   ASA,=C'0 '
ABEOJ    EQU   *
         CLOSE (DUMP,,SYSPRINT)
         L     13,4(13)
         LH    15,RETCODE
         RETURN (14,12),,RC=(15)
FINITO   EQU   *
         LA    4,0
         CLR   5,4
         BE    ROUND
         C     5,PARM2       TEST IF IGNORE COUNT EXCEEDS FILE SIZE
         BNH   BIGIGN        BRANCH IF TOO BIG IGNORE
         LA    3,MSG3
         B     ERRTN
ROUND    EQU    *
         LA    3,MSG5
         B     ERRTN
         DS    0D
BIGIGN   EQU   *
         LA    3,MSG6
         B     ERRTN
CONV     DC    PL8'0'
ASA      DC    C'1 '
RETCODE  DS    H
REGST    DS    F
TLINE    DS    F
BLINE    DS    F
SAVE     DS    18F
PCNT     DC    PL3'0'
         DS    0H
MSG2     DC    AL2(END2-*-3)
         DC    C'   GN006  PARAMETER EXCEEDS MAXIMIUM LENGTH'
END2     EQU   *
         DS    0H
MSG3     DC    AL2(END3-*-3)
         DC    C'   GN006       ***  END OF PROGRAM  *** '
END3     EQU   *
         DS    0H
MSG1     DC    AL2(END1-*)
         DC    AL2(0)
         DC    C'   GN006  UNABLE TO OPEN SYSPRINT '
END1     EQU   *
         DS     0H
MSG4     DC    AL2(END4-*-3)
         DC    C'   GN006     RECORD COUNT EXHAUSTED'
END4     EQU    *
         DS     0H
MSG5     DC    AL2(END5-*-3)
         DC    C'   GN006      NO RECORDS ON DUMP DATASET '
END5     EQU    *
         DS    0H
MSG6     DC    AL2(END6-*-3)
         DC    C'   GN006 IGNORE COUNT EXCEEDS FILE SIZE'
END6     EQU   *
RIND     DC    X'00'
RECLN    DC    X'00'
LRECL    DC    F'0'
PARMAD   DC    F'0'
         DS    0D
PKPRM    DC    D'0'
LINCNT   DC    P'00'
HEADING  DS    0CL130
         DC    C'CHARACTER/HEXADECIMAL PRINT -          ISAM UTILITY PR*
               INT                                            PAGE '
PAGECNT  DC    CL6'0'
         DC    C'    RECNO   RECLEN'
PATTERN  DC    X'402020202020'
PARM2    DC    F'0'
PARM2SWT DC    X'00'
TRANTAB  DS    0F
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    X'00000000000000000000000100000000'  , AT 6B
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DC    16X'00'
         DS    0F
TABLE1   DC    16X'F0'
         DC    16X'F1'
         DC    16X'F2'
         DC    16X'F3'
         DC    X'40'
         DC    15X'F4'
         DC    16X'F5'
         DC    16X'F6'
         DC    16X'F7'
         DC    16X'F8'
         DC    16X'F9'
         DC    16X'C1'
         DC    16X'C2'
         DC    X'C3'
         DC    X'C1C2C3C4C5C6C7C8C9'
         DC    6X'C3'
         DC    X'C4D1D2D3D4D5D6D7D8D9'
         DC    6X'C4'
         DC    X'C5C5E2E3E4E5E6E7E8E9'
         DC    6X'C5'
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         DC    6X'C6'
         DS    0F
TABLE2   DC   4X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         DC    X'40F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         DC   4X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         DC    X'F0404040404040404040C1C2C3C4C5C6'
         DC    X'F0404040404040404040C1C2C3C4C5C6'
         DC    X'F0F14040404040404040C1C2C3C4C5C6'
         DC    X'40404040404040404040C1C2C3C4C5C6'
         DS    0F
TABLE3   DC    256X'00'
         ORG   TABLE3+193
         DC    9X'01'
         ORG   TABLE3+209
         DC    9X'01'
         ORG   TABLE3+226
         DC    8X'01'
         ORG   TABLE3+240
         DC    10X'01'
         ORG   TABLE3+256
SYSPRINT DCB   MACRF=PL,DSORG=PS,DDNAME=SYSPRINT,RECFM=FBA,LRECL=133,  *
               BLKSIZE=1330
DUMP     DCB   MACRF=(GL),DSORG=IS,DDNAME=DUMP,EODAD=FINITO
         LTORG
         END
