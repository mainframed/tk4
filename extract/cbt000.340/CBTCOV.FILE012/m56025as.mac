         MACRO
&LABEL   PGMIN &SAVEFLD=,&USING=,&EQUATES=
         LCLA  &N,&S
         LCLC  &EQU,&CSECT,&SAVE,&R1,&R2
.*
.*
         AIF   ('&LABEL' EQ '').NONAME
&LABEL    DS    0H
.NONAME  ANOP
&EQU     SETC  '&EQUATES'
         AIF   ('&EQU' EQ 'NO').C2
         AIF   ('&EQU' EQ 'YES').C1
         AIF   ('&EQU' EQ '').C1
         MNOTE 4,'INVALID EQUATES OPERAND - IGNORED'
         AGO   .C2
.C1      ANOP
         SPACE 1
*        REGISTER EQUATES
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 5
.C2      ANOP
&CSECT   SETC  '&SYSECT'
         USING &CSECT,15
         STM   14,12,12(13)
&SAVE    SETC  '&SAVEFLD'
         AIF   ('&SAVE' NE '').C3
&SAVE    SETC  'SAVEFLD'
.C3      BAL   12,&SAVE+72
&SAVE    DS    18F
         ST    12,8(13)
         ST    13,4(12)
         LR    13,12
&N       SETA  N'&USING
         AIF   (&N GT 0).C4
         LA    12,2048(12)
         LA    12,2048(12)
         DROP  15
         USING &SAVE,13,12
         MEXIT
.C4      ANOP
&S       SETA  &S+1
&R1      SETC  '&USING(&S)'
&R2      SETC  '12'
         LR    &R1,12
.C5      ANOP
&S       SETA  &S+1
         AIF   (&S GT &N).C6
&R1      SETC  '&USING(&S)'
         LA    &R1,2048(&R2)
         LA    &R1,2048(&R1)
&R2      SETC  '&R1'
         AGO   .C5
.C6      AIF   (&N GT 1).C7
         USING &SAVE,&USING(1)
         MEXIT
.C7      AIF   (&N GT 2).C8
         USING &SAVE,&USING(1),&USING(2)
         MEXIT
.C8      AIF   (&N GT 3).C9
         USING &SAVE,&USING(1),&USING(2),&USING(3)
         MEXIT
.C9      AIF   (&N EQ 4).C10
         MNOTE 16,'MORE THAN FOUR BASE REGISTERS SPECIFIED'
         MEXIT
.C10     USING &SAVE,&USING(1),&USING(2),&USING(3),&USING(4)
         MEND
