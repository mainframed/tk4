         MACRO
&LABEL   ZRETURN &RC=0
.*
.*  &&RC=          NUMBER TO BE RETURNED TO CALLING CSECT, OR REG 15
.*                 SPECIFIED FOR INDICATING PRESET CODE  ALREADY
.*
         GBLA  &SIZE         PASSED FROM GETMAIN IN ZCSECT FOR FREEMAIN
         GBLC  &NAME(75)               ARRAY HOLDING &SYSECT AND &SIZE
         GBLA  &INDX                   LENGTH OF ARRAY
.*
         LCLA  &B                      INDEX FOR ARRAY
         LCLC  &WORKC                  WORK AREA SIZE
.*
.* ****  RESTORE SIZE TO CORRECT CSECT VALUE
.*
&B       SETA  1
.SEARCH  AIF   ('&NAME(&B)' EQ '&SYSECT').SETSIZE
&B       SETA  &B+3
         AIF   ('&B' GE '&INDX').SETA0
         AGO   .SEARCH
.SETSIZE ANOP
&B       SETA  &B+1
&SIZE    SETA  &NAME(&B)
&WORKC   SETC  '&NAME(&B+1)'
         AGO   .RESTORE
.SETA0   ANOP
&SIZE    SETA  0
.*
.* ****  RESTORE REGISTERS AND FREE SAVEAREA(S) IF NECESSARY
.*
.RESTORE ANOP
&LABEL   L     13,4(0,13)
         AIF   (&SIZE EQ 0).RETURN
         L     1,8(13)
         LR    REG11,REG15             SAVE RETURN CODE REGISTER
         FREEMAIN R,SP=13,LV=&SIZE+&WORKC,A=(1)
         LR    REG15,REG11             RESTORE RETURN CODE
.RETURN  ANOP
         RETURN (14,12),T,RC=&RC
         MEND
