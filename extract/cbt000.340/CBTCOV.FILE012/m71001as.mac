         MACRO
&N       HEXPRINT &R1,&R2
         LCLC  &REG1,&REG2
         GBLA  &X
* ******************************************************************* *
*                                                                     *
*                         H E X P R I N T                             *
*                                                                     *
*        CONVERT FIELD FROM HEX TO PRINTABLE HEX EQUIVALENT           *
*                                                                     *
         AIF   ('&N' EQ '').GO
&N       DS    0H
.GO      ANOP
         AIF   ('&X' GT '1').GO2
&X       SETA  1
.GO2     AIF   (K'&R1 NE 0).REG1OK
         MNOTE 8,'***** NO REGISTERS SPECIFIED ***********************'
         AGO   .END
.REG1OK  ANOP
         AIF   ('&R1'(1,1) NE 'R').NUMB
&REG1    SETC  '&R1'(2,2)
         AGO   .COMP
.NUMB    ANOP
&REG1    SETC  '&R1'
.COMP    ANOP
         AIF   ('&REG1' GT '15').E1
&REG1    SETC  '&R1'
         AIF   (K'&R2 NE 0).REG2OK
         MNOTE 8,'***** NO SECOND REGISTER SPECIFIED *****************'
         AGO   .END
.REG2OK  ANOP
         AIF   ('&R2'(1,1) NE 'R').NUMB2
&REG2    SETC  '&R2'(2,2)
         AGO   .COMP2
.NUMB2   ANOP
&REG2    SETC  '&R2'
.COMP2   ANOP
         AIF   ('&REG2' GT '15').E1
&REG2    SETC  '&R2'
         AIF   ('&REG1' EQ '&REG2').E2
.CONT    ANOP
         B     $HEXCL&X           SKIP WORKAREA                       *
*        H E X P R I N T     W O R K A R E A                          *
$HEXSV&X DS    F                 SAVE AREA FOR REGISTER WITH NUMBER   *
         DS    CL1
&X       SETA  &X+1
$HEXSV&X DS    CL9               TEMPORARY AREA FOR CONVERT           *
&X       SETA  &X-1
         DS    0H                                                     *
*                                                                     *
$HEXCL&X ST    &REG1,$HEXSV&X    SAVE NUMBER                          *
         LA    &REG1,$HEXSV&X    GET ADDRESS OF WHERE IT IS           *
&X       SETA  &X+1
         UNPK  $HEXSV&X+0(9),0(5,&REG1)   CONVERT TO PRINTABLE
         NC    $HEXSV&X+0(8),=8X'0F'
         TR    $HEXSV&X+0(8),=CL16'0123456789ABCDEF'
         MVC   0(8,&REG2),$HEXSV&X   PUT ANSWER IN PASSED FIELD       *
&X       SETA  &X-1
         L     &REG1,$HEXSV&X      RESTORE REGSITER                   *
&X       SETA  &X+2
*                                                                K.F. *
* ******************************************************************* *
         MEXIT
.E2      MNOTE 8,'DUPLICATE REGISTER SPECIFICATION'
         AGO   .END
.E1      MNOTE 8,'IMPROPER REGISTER SPECIFIED, NO STATEMENTS GENERATED'
.END     ANOP
         MEND
