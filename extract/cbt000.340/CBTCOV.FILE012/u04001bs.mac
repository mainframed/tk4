         EJECT
TRACKIT  CSECT
*        BEGIN
*       THE NEXT 10 STATEMENTS ARE THE EXPANSION OF BOOTS BEGIN MACRO
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         B     *+76
         DC    18F'0'  SAVE AREA FOR THIS PROGRAM
         LA    15,*-72
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
*
         USING SYMBOLS,7
         LA    7,CONSTS
         L     3,0(1)              LOCATE PARAMETERS
         LH    5,0(3)              COUNT TO R5
         LTR   5,5
         BC    8,ERR1              ERROR IF NO  PARM
         LA    3,0(3)              REMOVE HIGH ORDER BIT
         LR    6,3
SCAN     CLI   2(6),C','           SEARCH FOR COMMA
         BE    FOUND
         LA    6,1(6)              STEP UP POINTER
         BCT   5,SCAN              REDUCE COUNT
         LA    5,1(5)
FOUND    SR    6,3                 NUMBER OF CHARACTERS TO R6
         LTR   6,6
         BC    8,ERR2              ERROR IF COMMA FIRST CHARACTER
         CH    6,EIGHT
         BH    ERR2                ERROR IF MORE THAN 8 CHARACTERS
         BCTR  5,0                 SUBTRACT 1 FROM COUNT FOR COMMA
         LA    10,3(3,6)           REG 10 TO POINT TO REMAINING PARM
         BCTR  6,0                 REDUCE REG6 FOR EXECUTE
         EX    6,MOVE1
         STH   5,0(3)              REMAINING COUNT TO COUNT FIELD
         LTR   5,5
         BC    8,CARRYON
         BCTR  5,0                 REDUCE REG5 FOR EXECUTE
         EX    5,MOVE2
CARRYON  EQU   *
         LA    15,SUBRTN1
         SVC   253                 USER TYPE 1 SVC
         SPACE 2
*  SUBRTN1 IS NOW ENTERED IN SUPERVISOR STATE
*  RETURN FROM SUBRTN1 IN PROBLEM PROGRAM STATE IS TO NEXT INSTRUCTION
         CLI   SW1,X'FF'
         BE    ERR3
         SPACE 2
         LINK  EPLOC=PGNAMEX
*  RETURN FROM LINKED TO PROGRAM
         ST    15,SAVE15           SAVE LINKEE'S RETURN CODE
         SPACE  2
*  ON RETURN FROM LINKEE A CHECK IS MADE TO SEE IF ABNORMAL TERMINATION
*  OF A SUBTASK HAS CAUSED COLLECTION OF STATISTICS TO BE PREMATURELY
*  TERMINATED
         SPACE  2
         L     5,100              SVCFLIH ADDRESS TO R5
         CLC   0(2,5),MASK2       IS START OF FLIH NORMAL OS
         BNE   CONT5              PROCEED TO RESTORE SVCFLIH AND SVCTAB
         WTO   'TRACKIT STATISTICS INCOMPLETE - MONITORING TERMINATED WX
               HEN SUB-TASK ABENDED',ROUTCDE=(2,11)
         B     CONT6
CONT5    LA    15,SUBRTN3
         SVC   253
         SPACE 2
*  SUBRTN3 IS NOW ENTERED IN SUPERVISOR STATE.
*  RETURN FROM SUBRTN3 IN PROBLEM PROGRAM STATE IS TO NEXT INSRUCTION.
*  THE REMAINDER OF THIS SECTION EDITS AND OUTPUTS THE ACCUMULATED     C
               STATISTICS.
         SPACE 2
CONT6    MVC   MSG1+16(8),PGNAME  MOVE IN PROGRAM NAME
         L     4,16               CVT ADDR TO R4
         L     4,0(4)             TCB POINTER ADDR TO R4
         L     4,4(4)             TCB ADDR TO R4
         L     4,24(4)            MSS BOUNDARY BOX ADDR TO R4
         L     4,8(4)             HIGH BOUNDARY ADDR TO R4
         S     4,MYSIZE           CALCULATE EFFECTIVE PARTITION SIZE
         ST    4,WORK
         BAL   5,CALC
         MVC   MSG1+36(6),RESULT+2   MOVE EFFECTIVE PARTITION TO MSG
         S     4,MINTOT           CALCULATE MAX SIZE OF PROGRAM
         ST    4,WORK
         BAL   5,CALC
         MVC   MSG1+64(6),RESULT+2     MOVE MAX SIZE INTO MESSAGE
         MVC   WORK,MINTOT
         BAL   5,CALC
         MVC   MSG2+40(6),RESULT+2     MOVE MIN TOTAL STORAGE TO MSG
         MVC   WORK,MINLGE
         BAL   5,CALC
         MVC   MSG3+40(6),RESULT+2     MOVE MIN LARGEST AREA TO MSG
MSG1     WTO   'PROGRAM          RUNNING IN        BYTES REACHED MAX OFC
                       BYTES',ROUTCDE=(2,11)
MSG2     WTO   'MINIMUM TOTAL STORAGE AVAILABLE        BYTES',         C
               ROUTCDE=(2,11)
MSG3     WTO   'MINIMUM LARGEST AREA AVAILABLE         BYTES',         C
               ROUTCDE=(2,11)
ENDIT    LA    13,TRACKIT+12
         L     13,4(13)
         L     15,SAVE15
         RETURN (14,12),RC=(15)
MOVE1    MVC   PGNAME(1),2(3)
MOVE2    MVC   2(1,3),0(10)
         SPACE 2
*  SUBROUTINE TO EDIT BINARY VALUES TO HEX CHARACTERS FOLLOWS
         SPACE 1
CALC     UNPK  RESULT,WORK(5)
         TR    RESULT(8),TABLE-240
         BR    5
         SPACE 4
*  ERROR EXITS
         SPACE 1
ERR1     WTO   '** TRACKIT  - PARM FIELD ABSENT FROM EXEC STATEMENT',  C
               ROUTCDE=(2,11)
         B     ERREND
ERR2     WTO   '** TRACKIT - ERROR IN PARM FIELD',ROUTCDE=(2,11)
         B     ERREND
ERR3     WTO   '** TRACKIT UNABLE TO OPERATE - FLIH HAS BEEN MODIFIED  C
               BY ANOTHER USER',ROUTCDE=(2,11)
ERREND   L     13,4(13)
         L     15,SIXTEEN
         RETURN (14,12),RC=(15)
         EJECT
*   THE FOLLOWING ROUTINE IS ENTERED AS A PREFIX TO SVCFLIH TO TRAP
*   ABENDS IN THE PROGRAM BEING MONITORED. IN SUCH CASES THE SVC NEW
*   PSW AND THE SVC TABLE ARE RESTORED TO NORMAL.
         SPACE 4
         DS    0D
SUBRTN4  EQU   *
         BCR   0,0
STM      DS     CL4          FLIH REG STORAGE INSTRUCTION INSERTED HERE
         BALR  12,0
         USING *,12
         CLI   35(0),X'0D'    WAS SVC13 ISSUED
         BNE    QUIT
         L     7,ADCON4       SET UP DSECT ADDRESSING
         L     4,TCBPTR       ADDRESS OF NEW/OLD BOX TO R4
TCBOTC   EQU   132                TCBOTC DISPLACEMENT
TCBFLGS  EQU   29                 TCBFLGS DISPLACEMENT
         L     4,4(4)             TCB  ADDRESS TO R4
         TM    TCBFLGS(4),X'40'   TEST NORMAL COMPLETION
         BO    QUIT               BRANCH IF NORMAL COMPLETION
LOOP3    CLC   TCBOTC(4,4),ZEROS    IS THIS TCB FOR ORIGINAL TASK
         BE    FOUNDTCB           BRANCH IF YES
         L     4,TCBOTC(4)        CHAIN TO ORIGINATING TASK
         B     LOOP3
FOUNDTCB EQU   *
         C     4,TCBID       WAS SVC13 ISSUED BY TRACKIT PARTITION
         BNE   QUIT
         L     8,ADCON4A      SET UP FOR BRANCH
         BALR  14,8           TO RESTORE SVC NEW PSW AND SVC TABLE
QUIT     LM    0,15,0(0)      REG STORE ADDR INSERTED BY SET UP MODULE
EXIT     BC    15,0(0)        SVCFLIH ADDR+4 INSERTED BY SET UP MODULE
ADCON4   DC    A(CONSTS)
ADCON4A  DC    A(SUBRTN3A)
         EJECT
*  THE FOLLOWING ROUTINE IS ENTERED VIA SVC 253 AND EXECUTES IN
*       SUPERVISOR STATE AS A TYPE 1 SVC.
*  ITS FUNCTION IS TO CHANGE ADDRESS POINTERS IN THE SVC TABLE WITHIN
*  THE NUCLEUS TO TRAP GETMAIN, FREEMAIN, AND REGMAIN SVCS.
         SPACE 4
         USING *,15
SUBRTN1  L     7,ADCON1      SET UP DSECT ADDRESSING
         L     5,100               SVCFLIH ADDRESS TO R5
         CLC   0(2,5),MASK2        IS START OF FLIH AS EXPECTED
         BE    PROCEED
         MVI   SW1,X'FF'           SET SWITCH AND EXIT
         B     EXIT2
PROCEED  L     6,ADCON6            ADDRESS OF SUBRTN4 TO R6
STM4     EQU   STM-SUBRTN4
         MVC   STM4(4,6),0(5)      COPY FLIH REG STORAGE INSTRUCTION
*                                  TO SUBRTN4
QUIT4    EQU   QUIT-SUBRTN4
         MVC   QUIT4+2(2,6),2(5)   COPY REG STORAGE ADDR TO LM SUBRTN4
         MVC   FLIHADD,100         SAVE WORD 2 OF NEW PSW
         LA    5,4(5)              POINT R5 TO 2ND INSTUCTION OF FLIH
EXIT4    EQU   EXIT-SUBRTN4
         STH   5,EXIT4+2(6)        SET UP FOR EXIT FROM SUBRTN4
LOOP2    CLC   0(2,5),MASK         SEARCH FOR LM89 INSTUCTION
         BE    HIT
         LA    5,2(5)
         B     LOOP2
HIT      LH    5,2(5)              ADDR OF SVCTAB ADDRESSES TO R5
         MVC   ADSTORE(8),0(5)
*                        R3 POINTS TO CVT WHEN SUBRTN1 IS ENTERED
         L     3,0(3)              ADDR OF NEW/OLD BOX TO R3
         ST    3,TCBPTR            STORE ADDR OF NEW/OLD BOX
*                        R4 POINTS TO TCB WHEN SUBRTN1 IS ENTERED
         LA    4,0(4)         CLEAR HIGH ORDER BYTE OF TCB POINTER
         ST    4,TCBID        STORE TRACKIT TCB ADDRESS
         LM    8,9,ADSTORE    SVCTAB ADDR TO R8, PREFIX TAB ADDR TO R9
         LA    10,4
         LA    6,ASVC4
         BAL   11,FIX
         LA    10,5
         LA    6,ASVC5
         BAL   11,FIX
         LA    10,10
         LA    6,ASVC10
         BAL   11,FIX
         MVC   100(4,0),ADCON6  SUBSTITUTE ADDR OF SUBRTN4 IN SVC NPSW
EXIT2    BR    14            EXIT
FIX      IC    10,0(9,10)    INDEXED PREFIX TABLE ENTRY TO R10
         SLL   10,2          MULTIPLY ENTRY BY 4
         ALR   10,8          ADDR OF SVC TABLE ENTRY TO R10
         XC    0(4,6),0(10)
         XC    0(4,10),0(6)
         XC    0(4,6),0(10)
         BR    11
ADCON1   DC    A(CONSTS)
ADCON6   DC    A(SUBRTN4)     ADDRESS OF SUBRTN4
         EJECT
*  THE FOLLOWING  ROUTINE IS ENTERED WHENEVER SVC4, SVC5, OR SVC10 IS
*  ISSUED BY ANY PARTITION.
*  AFTER LOGGING CHANGES IN MAIN STORAGE USAGE IN PARTITION UNDER
*  REVIEW THE INSTRUCTION SEQUENCE IS ROUTED TO THE APPROPRIATE SVC
         SPACE 4
         DS    0D
SUBRTN2  BALR  12,0
         USING *,12
         L     7,ADCON2            SET UP R7 FOR DSECT ADDRESSING
*                  R4 POINTS TO TCB WHEN ROUNTINE IS ENTERED
         LA    6,0(4)             TCB ADDRESS TO R6
LOOP4    CLC   TCBOTC(4,6),ZEROS    IS THIS TCB FOR ORIGINATING TASK
         BE    GETON               BRANCH IF YES
         L     6,TCBOTC(6)         CHAIN TO ORIGINATING TCB
         B     LOOP4
GETON    EQU   *
         C     6,TCBID             AM I INTERESTED IN THIS PARTITION
         BNE   GETOUT              IF NOT OMIT FQE SEARCH
       L       6,24(6)             BBOX ADDR TO R6
         XC    LARGE(8),LARGE     CLEAR LARGE AND TOTAL
LOOP     CLC   0(4,6),ZEROS       IS THERE ANOTHER FQE
         BE    OUT
         L     6,0(6)             ADDR OF NEXT FQE TO R6
         L     2,TOTAL            ADD SIZE OF
         A     2,4(6)             FREE AREA
         ST    2,TOTAL            TO TOTAL
         CLC   LARGE,4(6)         IS THIS LARGEST AREA SO FAR
         BNL   LOOP
         MVC   LARGE,4(6)         IF SO STORE IT
         B     LOOP
OUT      CLC   TOTAL,MINTOT       IS AVAILABLE STORAGE A NEW MINIMUM
         BNL   CONT3
         MVC   MINTOT,TOTAL       STORE NEW MINIMUM
CONT3    CLC   LARGE,MINLGE       IS LARGEST FREE AREA
         BNL   GETOUT             A NEW MINIMUM
         MVC   MINLGE,LARGE       STORE NEW MINIMUM
GETOUT   CLI   35(0),X'04'
         BNE   CONT1
         L     6,ASVC4
         B     CONT4
CONT1    CLI   35(0),X'05'
         BNE   CONT2
         L     6,ASVC5
         B     CONT4
CONT2    L     6,ASVC10
CONT4    BR    6
ADCON2   DC    A(CONSTS)
         EJECT
*  THE FOLLOWING ROUTINE IS ENTERED VIA SVC 253 AND EXECUTES IN
*       SUPERVISOR STATE AS A TYPE 1 SVC.
* ITS FUNCTION IS TO RESTORE THE CORRECT POINTERS IN THE SVC TABLE
*          BEFORE THE PROGRAM TERMINATES.
         SPACE 4
         USING *,15
SUBRTN3  L     7,ADCON3           SET UP R7 FOR DSECT ADDRESSING
SUBRTN3A LM    8,9,ADSTORE    SVCTAB ADDR TO R8, PREFIX TAB ADDR TO R9
         L     2,FIXCON
         LA    10,4
         LA    6,ASVC4
         BALR  11,2          SWOP BACK ENTRIES FOR SVC 4
         LA    10,5
         LA    6,ASVC5
         BALR  11,2                SWOP BACK ENTRIES FOR SVC5
         LA    10,10
         LA    6,ASVC10
         BALR  11,2          SWOP BACK ENTRIES FOR SVC 10
         MVC   100(4,0),FLIHADD    RESTORE SVC NEW PSW TO NORMAL
         BR    14            GET OUT OF SUPERVISOR STATE
ADCON3   DC    A(CONSTS)
         EJECT
*   THE FOLLOWING CONSTANTS ARE USED IN ALL ROUTINES AND ARE
*   ADDRESSED THROUGH A DSECT (EXCEPT FOR TABLE)
         SPACE 4
CONSTS   DS    0F
ADSTOREX DS    2F
ASVC4X   DC    A(SUBRTN2)
ASVC5X   DC    A(SUBRTN2)
ASVC10X  DC    A(SUBRTN2)
SAVE15X  DS    F
FIXCONX  DC    A(FIX)
LARGEX   DS    F
TOTALX   DS    F
ZEROSX   DC    F'0'
MINTOTX  DC    X'FFFFFFFF'
MINLGEX  DC    X'FFFFFFFF'
MYSIZEX  DC    A(ENDADDRX)
WORKX    DS    F
         DC    C'0'
RESULTX  DS    CL9
EIGHTX   DC    H'8'
SIXTEENX DC    F'8'
MASKX    DC    X'9889'
PGNAMEX  DC    CL8' '
TCBIDX   DS    F
FLIHADDX DS    F
TCBPTRX  DS    F
MASK2X   DC    X'900F'
SW1X     DC    X'00'
TABLE    DC    C'0123456789ABCDEF'
ENDADDRX DS    0D
         EJECT
*   THE FOLLOWING DUMMY SECTION ENABLES ALL ROUTINES TO ADDRESS
*   CONSTANTS THROUGH REGISTER 7.
         SPACE 4
SYMBOLS  DSECT
ADSTORE  DS    2F
ASVC4    DS    F
ASVC5    DS    F
ASVC10   DS    F
SAVE15   DS    F
FIXCON   DS    F
LARGE    DS    F
TOTAL    DS    F
ZEROS    DS    F
MINTOT   DS    F
MINLGE   DS    F
MYSIZE   DS    F
WORK     DS    F
         DS    C
RESULT   DS    CL9
EIGHT    DS    H
SIXTEEN  DS    F
MASK     DS    CL2
PGNAME   DS    CL8
TCBID    DS    F
FLIHADD  DS    F
TCBPTR   DS    F
MASK2    DS    CL2
SW1      DS    C
         DS    CL16
ENDADDR  DS    0D
         END
