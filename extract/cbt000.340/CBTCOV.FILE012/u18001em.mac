         TITLE   'SPY55 - DISK FREE SPACE DISPLAY - REL 1.0'
******************************************************************
* SPY55 WILL SHOW DETAILS OF FREE SPACE ON ALL ONLINE DISKS.     *
******************************************************************
* MACRO
         MACRO
&NAME    CUREG
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUREG MACRO:16/01/75'
         POP   PRINT
         ACTR  50
         LCLA  &X,&Y,&Z
*    GENERAL REGISTERS
.LPR     ANOP
R&X      EQU   &X
&X       SETA  &X+1
         AIF   (&X LT 16).LPR
*    CONTROL REGISTERS
.LPC     ANOP
C&Y      EQU  &Y
&Y       SETA  &Y+1
         AIF   (&Y LT 16).LPC
*    FLOATING POINT REGISTERS
.LPF     ANOP
F&Z      EQU   &Z
&Z       SETA  &Z+2
         AIF   (&Z LT 8).LPF
         MEND
         MACRO
&NAME    CUENTER &BASE=(R12),&RECURSE=,&RENT=,&FIRST=,&CSECT=,&PARM=
         LCLA  &AL0,&AL1,&AL2,&AL9,&AL12
         ACTR  45                                              DEC 75
&AL2     SETA  &SYSNDX
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUENTER MACRO:16/06/77'
         POP   PRINT
         LCLC  &CL0,&CL1,&CL2,&CL3,&CL12,&CL13,&CL50
         LCLC  &CL51,&CL52,&CL53
         GBLB  &BG15
         AIF   ('&NAME' EQ '').B98
         ENTRY &NAME
.B98     ANOP
&CL0     SETC  '12'
         AIF   ('&BASE'(1,1) EQ '(').B10  *VALIDATE &BASE PARAMETER
&CL0     SETC  '&BASE'
         AIF   ('&CL0' EQ 'NONE').B11
         AIF   ('&CL0'(1,1) NE 'R').B4B
&AL9     SETA  K'&CL0-1
&CL0     SETC  '&CL0'(2,&AL9)
.B4B     AIF   ((&CL0 GT 1) AND (&CL0 LT 13)).B11
.BERA    MNOTE 9,'&&BASES WRONG,MACRO ABANDONED'
         MEXIT
.B10     ANOP
&CL0     SETC  '&BASE(1)'
&AL0     SETA  1                        COUNT ALONG TO-BE-SCANNED BASES
.B13     ANOP
&AL1     SETA  1                       *COUNT ALONG PREVIOUSLY SCANNED
&CL12    SETC  '&BASE(&AL0)'
         AIF ('&CL12'(1,1) NE 'R').B12A
&AL9     SETA  K'&CL12-1
&CL12    SETC  '&CL12'(2,&AL9)
.B12A    ANOP
&AL12    SETA  &CL12
.B12     AIF    ((&AL12 LT 2) OR (&AL12 GT 12)).BERA
.B15     AIF   (&AL1 EQ &AL0).B14      *J OUT IF REACHED LATEST
&CL13    SETC  '&BASE(&AL1)'
         AIF   ('&CL13'(1,1) NE 'R').B15A
&AL9     SETA  K'&CL13-1
&CL13    SETC  '&CL13'(2,&AL9)
.B15A    AIF   (&CL12 EQ &CL13).BERA *ERROR IF DUPLICATED
&AL1     SETA  &AL1+1                  *STEP ALREADY SCANNED PTR
         AGO   .B15
.B14     ANOP
&AL0     SETA  &AL0+1                  *STEP SCAN POINTER
         AIF   ('&BASE(&AL0)' NE '').B13 *J IF NOT NULL
&AL9     SETA  N'&BASE
         AIF   (&AL0 LT &AL9).BERA *ERROR IF NULL ELEMENT
.B11    ANOP
.*   START GENERATING PROGRAM
         DS    0H
         BALR  R15,0
         USING *,R15
&CL1     SETC  '&CSECT'
         AIF   ('&CL1' NE '').B30
&CL1     SETC  '*'
.B30     ANOP
&NAME    SAVE  (14,12),,&CL1
.B32     ANOP
.B90A    BAL   &CL0,OCSVA&AL2
         DROP  R15
         USING *,&CL0
OCSVD&AL2 EQU  *         HOLD IMMEDIATELY AFTER USING STATEMENT
         AIF   (&BG15).NODATIM
         CUREG                          REGISTER MNEMNONICS
&CL51    SETC  '&NAME'
         AIF   (K'&CL51 LE 8).DT1
&CL51    SETC  '&CL51'(1,8)
         AGO   .DT2
.DT1     ANOP
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '&SYSECT'
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '????????'
.DT2     ANOP
&CL52    SETC  '&SYSDATE'(4,2)'.'.'&SYSDATE'(1,2)'.'.'&SYSDATE'(7,2)
&CL53    SETC  '&SYSTIME'(1,2)':'.'&SYSTIME'(4,2)':00'
CUVRSION DC    C'****VERSION/',CL8'&CL51',C'/&CL52./&CL53'
&BG15    SETB  (1)                                             DEC 75
.NODATIM ANOP
OCSAR&AL2 DS    18F
OCSVA&AL2 EQU  *
         AIF   ('&BASE' EQ '').B89
&AL9     SETA  N'&BASE
         AIF   (&AL9 LE 1).B89
&AL1     SETA  &AL9
&AL0     SETA  2
.B88     ANOP
&CL2     SETC  '&BASE(&AL0)'
&CL3     SETC  '&BASE(&AL0-1)'
         LA    &CL2,1
         LA    &CL2,4095(&CL2,&CL3)
         USING OCSVD&AL2+4096*(&AL0-1),&CL2
         AIF   (&AL0 EQ &AL1).B87
&AL0     SETA  &AL0+1
         AGO   .B88
.B89     ANOP
.B87     ANOP
         ST    R13,OCSAR&AL2+4
         LR    R15,R13
         LA    R13,OCSAR&AL2
         ST    R13,8(R15)
         MEXIT
.MEND    MEND
* MACRO
         MACRO
&NAME    CUEXIT   &RC=(15)
         GBLB  &BG15                                 DEC 75
         LCLA  &B,&W
         LCLC  &Z
&NAME    DS 0H
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUEXIT MACRO:16/06/77'
         POP   PRINT
         L      R13,4(13)
&Z         SETC  '&RC'
.B94     AIF   ('&Z'(1,2) NE '(R').B84
&B       SETA  K'&Z-2
&Z        SETC  '&Z'(1,1).'&Z'(3,&B)
.B84     ANOP
         AIF   ('&Z' EQ '(15)').B60
         AIF   ('&Z'(1,1) EQ '(').B61
         RETURN (14,12),RC=&Z
         AGO   .B62
.B61     ANOP
&W       SETA  K'&Z-2
&Z       SETC  '&Z'(2,&W)
         LR    R15,&Z
.B60     RETURN (14,12),RC=(15)
.B62     ANOP
         MEND
SPY55    CUENTER BASE=3                STANDARD CUENTER BASE 3
         L     1,0(1)                  LOAD IN PARM
         CLI   2(1),C' '               PARM EQUAL TO BLANK
         BE    NOPARM                  DO ALL ONLINE DISKS
NOTBAD   NI    SWITCH+1,X'00'          SET NOP SWITCH
         MVC   DISK(6),2(1)            MOVE VOL SER OF DISK
NOPARM   LA    1,128(1)                GET ECB ADDRESS
         POST  (1)                     LET COMMAND MODULE CONTINUE
         L     11,16(0,0)              GET CVT FROM ABSLOC 16
         L     11,40(11)               GET UCB ADDRESS LIST PORTION
HOLY     LH    12,0(11)                IS THIS ADDRESS
         CH    12,=H'0'                             EQUAL TO ZERO
         BNE   NOHOLES                 IF SO GET NEXT
UPTAB    LA    11,2(11)                              UCB ADDRESS
         B     HOLY                    BRANCH BACK FOR NEXT UCB ADDRESS
NOHOLES  CLI   18(12),X'20'            UCBTYPE EQUAL TO DISK?
         BNE   UPTAB                   IF NOT TRY ANOTHER
WT       WTO  '**   DISK  FREE  SPACE  STATUS  REPORT   **'
WTO      WTO  '**  VOLSER  DEV   CYLS     TRKS     MAX  **'
CHAIN1   CLI   2(12),X'FF'             IS THIS A UCB
         BE    GOODMAN                 IF YES NO PROBLEMS
         B     FIN                     OTHERWISE GET OUT
GOODMAN  CLI   18(12),X'20'            STILL A DISK?
         BE    ONYOUGO                 YES KEEP GOING
         B     AGAIN                   TRY TRY TRY AGAIN
ONYOUGO  CLC   28(6,12),=X'000000000000'    VOL SER NO IN UCB
         BE    AGAIN                    TRY TRY AGAIN
         MVC   LARGEST,=XL2'0000'       ZEROIZE LARGEST FIELD
         MVI   DIVID+3,X'1E'            MOVE 30 TO DIVISOR FOR 3350
         CLI   19(12),X'0B'             IS THIS 3350?
         BE    D3350                    IF SO BRANCH
         MVI   DIVID+3,X'13'       *****OTHERWISE MOVE 19 FOR 3330*****
D3350    MVC   FULL+2(2),36(12)         MOVE RELATIVE ADDRESS OF VTOC
         L     4,FULL                   LOAD RELATIVE ADDRESS
         SRDA  4,32(0)                  SHIFT ALONG TO CLEAR UP
         D     4,DIVID                  DIVIDE BY NO OF TRKS IN CYL
         STC   4,CCHHR+3                STORE RMAINDER IN CCHHR
         ST    5,FULL                   STORE CYL NO IN CCHHR
         MVC   CCHHR(2),FULL+2          COMME CA
         MVI   CCHHR+4,X'02'            MOVE IN R ADDRESS FOR FMT 5
         MVC   VOLNUM(6),28(12)         MOVE VOL SER NUM ACCROSS
SWITCH   B     CARRYON                  DEPENDING WHETHER 1 DISK OR ALL
         CLC   VOLNUM,DISK              IS THIS THE DISK WE WANT?
         BNE   AGAIN                    IF NOT TRY AGAIN
CARRYON  XR    8,8                      CLEAR OUT 8 FOR LUCK
         XR    10,10                    AND 10
READFMT5 OBTAIN INDGX                   ISSUE READ
OPENOK   EQU   *
         CLI   DS5FMTID,C'5'            CHECK IT IS A FMT5
         BE    FMT5GOOD                 ITS A SURE THING
         ABEND 5,DUMP                   ELSE WE CRASH...
FMT5GOOD LA    R5,DS5AVEXT              ADDRESS OF EXTENT DESCRIPTION
         LA    R6,5                     LENGTH OF DESCRIPTION
         LA    R7,DS5AVEXT+35           LAST EXTENT IN KEY AREA
         LA    R9,PART2                 POINTER FOR SWITCH
LOOP1    MVC   DWORK(5),0(R5)           ALIGN THE DESCRIPTOR FIELD
         AH    R8,DWORK+2               ADD UP THE NUMBER OF CYLS
         CLC   DWORK+2(2),LARGEST       IS THIS THE LARGEST FREE AREA
         BL    NOGOOD1                  AFRAID NOT
         MVC   LARGEST,DWORK+2          MOVE INTO LARGEST
NOGOOD1  MVC   IRWORK+1(1),DWORK+4      NOW ADD TRACK
         AH    R10,IRWORK               INTO TOTAL
         BXLE  R5,R6,LOOP1              POINT TO NEXT EXTENT DESCRIPTN
         BR    R9                       SWITCH DEPENDING ON SECTION
PART2    LA    R5,DS5MAVET              RESET BXLE REGISTERS
         LA    R7,DS5MAVET+85           FOR SECOND PART OF DSCB
         BAL   R9,LOOP1                 AND PERFORM CALC FOR REMAINDER
         CLC   DS5PTRDS(4),=X'00000000' END OF FORMAT 5 CHAIN?
         BE    FMT5END                  IF YES GET OUT
         MVC   CCHHR(5),DS5PTRDS        POINT FDAD TO NEXT FORMAT 5
         B     READFMT5                 AND READ IT
FMT5END  EQU       *
         EJECT
         CVD   R8,DWORK                 CONVERT # CYLS TO DECIMAL
         MVC   WTO1+25(4),=X'40202120'  SET UP FOR WTO
         MVC   WTO1+43(4),=X'40202120'   AND UNCLE TOM COBOL
         MVC   WTO1+32(6),=X'402020202120'    AND ALL
         ED    WTO1+25(4),DWORK+6        START THE EDITS
         CVD   10,DWORK                  POP THEM OUT
         ED    WTO1+32(6),DWORK+5        AS THE ACTRESS SAID
BYP      MVC   WTO1+20(3),13(12)         TO THE BISHOP
         LH    10,LARGEST                CONVERT LARGEST EXTENT
         CVD   10,DWORK                  POP IT OUT AS WELL
         ED    WTO1+43(4),DWORK+6        ON TO WTO LINE
         MVC   WTO1+12(6),VOLNUM         MOVE VOL SER NO
WTO1     WTO  '**                                       **'
         MVI  FOUND,X'FF'                IF SINGLE DISK MARK AS FOUND
         CLI  SWITCH+1,X'00'             IS IT SINGLE DISK
         BE   FIN1                       IF SO GET OUT
         B    AGAIN                      TRY TRY TRY
ENDUP    EQU  *                          IF NO PARM, OR PARM=0
KH10     DC        H'10'                 CONSTANT
NULL     DC        H'0'                  PARAMETER LIST FOR IEHLIST
         SPACE     2
         PRINT     NOGEN
         SPACE     2
DWORK    DC        D'0'                * DOUBLE-WRD WORK-AREA
FDAD     DC        D'0'                * FULL DISC ADDRESS
         LTORG
         DC        0F'0'               * ALIGNMENT ONLY
WTOMSG   DC        AL2(WTOEND-*,0)     * LENGTH DESCRIPTOR WORD
         DC        C'AVAILABLE SPACE ON '
WTOSER   DC        CL6' ',C' ='
WTO#CYL  DC        CL4' '
         DC        C' CYL'
WTOEND   EQU       *
         DC      CL4'DSCB'
         DS        0F
VTOCBUFF DS        0CL140              * BUFFER FOR VTOC RECORDS
DS5KEYID DS        X'05050505'         * KEY IDENTIFICATION
DS5AVEXT DS        XL5                 * AN AVAILABLE EXTENT
DS5EXTAV DS        XL35                * SEVEN MORE AVAILABLE EXTENTS
DS5FMTID DS        C'5'                * FORMAT IDENTIFIER
DS5MAVET DS        XL90                * 18 MORE AVAILABLE EXTENTS
DS5PTRDS DS        XL5                 * CCHHR OF NEXT FORMAT 5
AGAIN     LA    12,64(12)
          B     CHAIN1
FIN      CLI   FOUND,X'FF'
         BE    FIN1
         MVC   WTO9+12(6),DISK
WTO9     WTO   '**           NOT ONLINE TO THIS MACHINE **'
FIN1     CUEXIT RC=0
INDGX    CAMLST   SEEK,CCHHR,VOLNUM,VTOCBUFF
           DC   CL6' CCHHR'
CCHHR     DC     XL5'0000000000'
VOLNUM   DC   CL6'SPOOL1'
DOUBLE    DC    D'0'
LENGTH    DC    CL6' '
HALF     DC     H'0'
HALF1    DC    H'0'
LOW     DC   F'0'
HIGH     DC    F'0'
STR      DC    H'0'
ENDER    DC    C' '
          DS   0H
UCBHALF   DC  X'0E4C'
STOP     DC    C' '
STORE    DS   F
FULL    DC   F'0'
DIVID   DC   F'0'
IRWORK    DC   H'0'
LARGEST  DC   H'0'
DISK    DC   CL6' '
FOUND   DC   X'00'
         END
