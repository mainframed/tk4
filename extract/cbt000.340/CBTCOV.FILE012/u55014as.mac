*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSU62ESV.
SSU62ESV ENTER ENT=TAPERRS
         L     2,0(0,1)
         LR    10,2
         LH    3,0(0,2)
         LTR   3,3                 ANY PARM?
         BZ    NOPARM              NO - DO WHOLE TAPE
         LA    2,2(0,2)            UP TO START OF PARM
         CH    3,=H'8'             ONLY 1 DATE?
         BL    INVPARM
         BE    OKLEN
         CH    3,=H'17'
         BE    OKLEN
INVPARM  WTO   'INVALID PARM FIELD - ALL RECORDS PROCESSED',ROUTCDE=11
         B     NOPARM
OKLEN    CLI   2(2),C'/'
         BNE   INVPARM
         CLI   5(2),C'/'
         BNE   INVPARM
         STH   3,PLEN
         LA    4,3
LOOPA    LA    3,2
COMPA    CLI   0(2),C'0'
         BL    INVPARM
         CLI   0(2),C'9'
         BH    INVPARM
         LA    2,1(0,2)
         BCT   3,COMPA
         LA    2,1(0,2)
         BCT   4,LOOPA
         NOP   OKPARM
         OI    *-3,X'F0'
         CLC   PLEN,=H'8'
         BE    OKPARM
         CLI   0(2),C','
         BNE   INVPARM
         LA    2,1(0,2)
         LA    4,3
         B     LOOPA
OKPARM   LA    10,2(0,10)
         GOTO  CONVERT,((10),STDATE),,LOAD=NO
         LA    10,9(0,10)
         GOTO  CONVERT,((10),ENDATE),,LOAD=NO
NOPARM   OPEN  (INFIL,,PRTER,(OUTPUT))
         BAL   12,HEADING
         GETMAIN R,LV=190008
         LR    10,1
         LR    9,10
         A     9,=F'190000'
         ST    10,TABAD
         SR    8,8                 RECORD COUNTER
         INIT  (9)
READ     GET   INFIL
         LR    2,1
         CLI   5(2),X'15'
         BNE   READ
         CLC   10(4,2),STDATE
         BL    READ
         CLC   10(4,2),ENDATE
         BH    READ
         MVC   0(8,10),20(2)
         MVC   8(11,10),32(2)
         LA    10,19(0,10)
         LA    8,1(0,8)
         CR    9,10
         BH    READ
         LA    5,10(0,2)
         GOTO  DATE3,((5),WD)
         MVC   MSGA+53(8),WD
MSGA     WTO   'INCORE BUFFER OVERFLOWED - RESTART DATE IS - DD/MM/YY',X
               ROUTCDE=11
EOFIN    CLOSEF (INFIL)
         ST    8,RECNUM
         L     5,TABAD
         GOTO  SORTMOD,((5),RECNUM)
         LA    2,ACCUM
         L     10,TABAD
         SR    7,7
         MVC   CURVOL(6),0(10)
LOOPB    SR    3,3
         SR    4,4
         IC    4,8(0,10)
         BAL   12,ACM
         IC    4,9(0,10)
         BAL   12,ACM
         ICM   4,3,10(10)
         BAL   12,ACM
         SR    4,4
         IC    4,12(0,10)
         BAL   12,ACM
         IC    4,13(0,10)
         BAL   12,ACM
         IC    4,14(0,10)
         BAL   12,ACM
         ICM   4,3,15(10)
         BAL   12,ACM
         ICM   4,3,17(10)
         BAL   12,ACM
         LA    10,19(0,10)
         LA    7,1(0,7)
         CLC   0(6,10),CURVOL
         BNE   PRTVOL
         BCT   8,LOOPB
         B     EOREP1
ACM      L     5,0(3,2)
         AR    5,4
         ST    5,0(3,2)
         LA    3,4(0,3)
         BR    12
PRTVOL   MVC   VOLNO(6),CURVOL
         CVD   7,WD
         MVC   MNTS(8),EDMSK
         ED    MNTS(8),WD+4
         SR    7,7
         LA    3,8
         SR    4,4
         LA    6,TREAD
LOOPC    L     5,0(4,2)
         CVD   5,WD
         MVC   0(8,6),EDMSK
         ED    0(8,6),WD+4
         ST    7,0(4,2)
         LA    4,4(0,4)
         LA    6,12(0,6)
         BCT   3,LOOPC
         PUT   PRTER,PRTA
         INIT  VOLNO,LV=132,FILL=40
         L     3,LINES
         LA    3,2(0,3)
         ST    3,LINES
         CH    3,=H'60'
         BL    *+8
         BAL   12,HEADING
         MVC   CURVOL(6),0(10)
         BCT   8,LOOPB
EOREP1   BAL   12,HEADING2
         MVC   NUMREC,RECNUM
         L     10,TABAD
         GOTO  SORTMOD,((10),NUMREC)
         LA    2,ACCUM
         MVC   0(32,2),=8F'0'
         SR    7,7
         MVC   CURUNIT(2),6(10)
         L     8,NUMREC
LOOPD    SR    3,3
         SR    4,4
         IC    4,8(0,10)
         BAL   12,ACM
         IC    4,9(0,10)
         BAL   12,ACM
         ICM   4,3,10(10)
         BAL   12,ACM
         SR    4,4
         IC    4,12(0,10)
         BAL   12,ACM
         IC    4,13(0,10)
         BAL   12,ACM
         IC    4,14(0,10)
         BAL   12,ACM
         ICM   4,3,15(10)
         BAL   12,ACM
         ICM   4,3,17(10)
         BAL   12,ACM
         LA    10,19(0,10)
         LA    7,1(0,7)
         CLC   6(2,10),CURUNIT
         BNE   PRTUNIT
         BCT   8,LOOPD
         B     EOJOB
PRTUNIT  UNPK  WD(5),CURUNIT(3)
         TR    WD(4),TRTAB-240
         MVC   UNIT(3),WD+1
         CVD   7,WD
         MVC   MNTS(8),EDMSK
         ED    MNTS(8),WD+4
         SR    7,7
         LA    3,8
         SR    4,4
         LA    6,TREAD
LOOPE    L     5,0(4,2)
         CVD   5,WD
         MVC   0(8,6),EDMSK
         ED    0(8,6),WD+4
         ST    7,0(4,2)
         LA    4,4(0,4)
         LA    6,12(0,6)
         BCT   3,LOOPE
         PUT   PRTER,PRTA
         INIT  VOLNO,LV=132,FILL=40
         L     3,LINES
         LA    3,2(0,3)
         ST    3,LINES
         CH    3,=H'60'
         BL    *+8
         BAL   12,HEADING2
         MVC   CURUNIT(2),6(10)
         BCT   8,LOOPD
EOJOB    CLOSE (PRTER)
         L     10,TABAD
         FREEMAIN R,LV=190008,A=(10)
         LEAVE
HEADING  PUT   PRTER,HEAD1
HLOOP    PUT   PRTER,HEAD2
         PUT   PRTER,ULINE
         MVC   LINES,=F'4'
         BR    12
HEADING2 PUT   PRTER,HEAD3
         MVC   HEAD2+1(12),=CL12'UNIT ADDR.'
         B     HLOOP
         LTORG
WD       DC    D'0'
ACCUM    DC    8F'0'
LINES    DC    F'0'
TABAD    DS    F
CURVOL   DC    CL6' '
CURUNIT  DC    XL3'00'
TRTAB    DC    C'0123456789ABCDEF'
PLEN     DC    H'0'
INFIL    DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,BFTEK=A,EODAD=EOFIN
PRTER    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133
STDATE   DC    F'15'
ENDATE   DC    X'0099365F'
RECNUM   DS    F
         DC    F'19'
         DC    F'6'
         DC    F'1'
NUMREC   DC    F'0'
         DC    F'19'
         DC    F'2'
         DC    F'7'
         DC    C'A'
HEAD1    DC    CL133'1        TAPE VOLUME ERROR STATISTICS'
PRTA     DC    C'0'
VOLNO    DC    CL12' '
MNTS     DC    CL12' '
TREAD    DC    CL108' '
UNIT     EQU   VOLNO+3
HEAD2    DC    C'0'
         DC    CL12'VOL.SER.NO'
         DC    CL12'   MOUNTS'
         DC    CL12'TEMP.READ'
         DC    CL12'TEMP.WRITE'
         DC    CL12'START I/O''S'
         DC    CL12'PERM.READ'
         DC    CL12'PERM.WRITE'
         DC    CL12'NOISE BLKS'
         DC    CL12'ERASE GAPS'
         DC    CL24'CLEANER ACTIONS'
ULINE    DC    C' ',132C'-'
EDMSK    DC    X'4020202020202120'
HEAD3    DC    CL133'1              ERROR STATISTICS BY UNIT'
CONVERT  ENTER
         LM    2,3,0(1)
         PACK  WD2,6(2,2)
         CVB   4,WD2
         SLL   2,30
         LTR   2,2
         BNZ   *+8
         OI    CSW+1,X'F0'
         PACK  WD2,3(2,2)
         CVB   4,WD2
CSW      NOP   *+12
         LA    5,MONTAB-2
         B     *+8
         LA    5,MONTABL-2
         SLL   4,1
         LH    4,0(4,5)
         PACK  WD2,0(2,2)
         CVB   6,WD2
         AR    4,6
         CVD   4,WD2
         MVC   2(2,3),WD2+6
         PACK  WD2,6(2,2)
         SR    4,4
         ICM   4,3,WD2+6
         SRL   4,4
         STCM  4,3,0(3)
         LEAVE
         LTORG
WD2      DS    D
MONTAB   DC    H'0'
         DC    H'31'
         DC    H'59'
         DC    H'90'
         DC    H'120'
         DC    H'151'
         DC    H'181'
         DC    H'212'
         DC    H'243'
         DC    H'273'
         DC    H'304'
         DC    H'334'
MONTABL  DC    H'0'
         DC    H'31'
         DC    H'60'
         DC    H'91'
         DC    H'121'
         DC    H'152'
         DC    H'182'
         DC    H'213'
         DC    H'244'
         DC    H'274'
         DC    H'305'
         DC    H'335'
         END
