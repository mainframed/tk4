PDSREAD  CSECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*        THIS MODULE WILL READ ANY SPECIFIED MEMBER OF A PDS.         *
*                                                                     *
*        IT IS DESIGNED TO BE CALLED FROM EITHER BAL OR PL1 OPT.      *
*                                                                     *
*        IT HAS BEEN TESTED SUCESSFULLY USING MFT 21.7-PL1 OPT 2.1    *
*                                                                     *
*        2 PARAMETERS ARE PASSED                                      *
*          THE FIRST IS THE ADDR OF AN 8 BYTE FIELD CONTAINING        *
*          THE REQUIRED MEMBER NAME                                   *
*          THE SECOND IS THE ADDR OF AN AREA OF CORE SUFFICIENTLY     *
           LARGE TO CONTAIN UP TO N RECORDS (IE N TIMES 80)           *
*                                                                     *
            N IS CURRENTLY TESTED TO BE 30. CHANGE CARD NOS 190       *
*            AND 220 IF THIS LIMIT IS TO CHANGED                      *
*                                                                     *
*          IF LESS THAN THE LIMIT(N) RECORDS ARE READ THEN A DUMMY    *
*          RECORD CONSISTING OF 80*9'S IS RETURNED AS THE LAST MEMBER *
*                                                                     *
*        CURRENTLY IF A MEMBER IS NOT PRESENT THEN AN ABEND OCCURS    *
         NOTE THE DSN OF THE JOBDET DD STATEMENT MUST INCLUDE A       *
*        DUMMY MEMBER NAME                                            *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         USING *,12
         SAVE  (14,12),,*
         LR    12,15               ESTABLISH BASE
         LR    11,13               PERFORM
         LA    13,SAVE                     SAVE
         ST    11,4(13)                        AREA
         ST    13,8(11)                            CHAINING
         LM    2,3,0(1)            SET UP PARM PTRS
         L     2,0(2)                              MEMBER
         L     3,0(3)                              ARRAY
         RDJFCB (PDS1)             GET JFCB
         MVC   JFCB+44(8),0(2) SLOT MEMBER NAME INTO JFCB ELEMENT FLD
         OPEN  (PDS1),TYPE=J
         SR    5,5                 CLEAR REC COUNT
GETIT    GET   PDS1,(3)            GET NEXT RECORD INTO ARRAY
         LA    3,80(3)             BUMP ARRAY PTR
         LA    5,1(5)                            & COUNT
*        THE FOLLOWING CARD SHOULD BE CHANGED IF THE LIMIT            *
*        OF RECORDS TOBE READ CHANGES                                 *
         CH    5,=H'30'            IS THIS 30TH
         BL    GETIT               NO
         B     CLOS                OOPS! MORE THAN 30 FORGET REST!
*        THE FOLLOWING CARD SHOULD BE CHANGED IF THE LIMIT            *
*        OF RECORDS TOBE READ CHANGES                                 *
EOFPDS   CH    5,=H'30'            DID WE GET 30?
         BNL   CLOS                YES,SO CLOSE FILE
         MVC   0(80,3),NINES     ELSE SIGNAL END OF MEMBER
CLOS     CLOSE (PDS1)
         L     13,4(13)            CHAIN UP
         RETURN (14,12)        AND BO BACK
         DS    0F
         DC    X'00'
         ORG   *-1
SAVE      DS    20F   SAVE AREA
PDSEXT   DC    X'87',AL3(JFCB)
JFCB     DS    44F                 JFCB AREA
NINES    DC    80C'9'
PDS1     DCB   DDNAME=JOBDET,EODAD=EOFPDS,EXLST=PDSEXT,MACRF=GM,       C
               SYNAD=PDSSYN,DSORG=PS
PDSSYN   SYNAD
         LTORG
         END
