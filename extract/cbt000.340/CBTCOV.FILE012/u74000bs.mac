*    NAME = ZZLIBCD  , SOURCE VERSION = 63381550 (03 DEC 76  AT 15.50)
         TITLE 'ZZLIBCD - EXPAND LIBRARY FORMAT RECORDS TO CARD IMAGES'
ZZLIBCD  CSECT
         PRINT NOGEN
*
* THIS PROGRAM EXPANDS PROGRAM SOURCE CODE THAT HAS BEEN COMPRESSED BY
* ZZCDLIB (BY THE REMOVAL OF BLANK STRINGS AND DROPPING SEQUENCE NO.S)
* BACK INTO CARD IMAGES.
*
         EQUATER
         SAVE  (14,12)
         BALR  R11,0
         USING *,R11
         LA    R12,SAVE
         ST    R13,4(R12)
         ST    R12,8(R13)
         LR    R13,R12
         OPEN  (LIBIN,(INPUT),CARDOUT,(OUTPUT))
         LA    R7,LIBIN
         USING IHADCB,R7
         TM    DCBOFLGS,X'10'           Q. FILE OPEN FAILURE
         BZ    ERR1
         BAL   R12,READ1
         CLC   0(2,R3),=C'00'           Q. COBOL PROGRAM
         BNE   L005
         LA    R3,2(R3)
         LA    R4,73                    CARD SCAN COUNT, COBOL
         LA    R5,COBREC                SET OUTPUT RECORD INDEX
         MVI   COBSW,X'FF'              SET COBOL SWITCH
         B     L030
L005     ZZLBL
         LA    R4,71                    SET CARD SCAN COUNT (MACH. LEN)
         LA    R5,OUTREC                SET OUTPUT RECORD INDEX
         B     L030
         SPACE 3
L010     ZZLBL
         BAL   R12,READ
L030     ZZLBL
         XR    R2,R2
         EX    R4,TRT                   SCAN FOR BLANK STRING OR END OF*
                                          RECORD
         B     L040(R2)
L040     ZZLBL
         B     L050                     NO BLANK STRING
         B     L060                     PROBABLY END OF INPUT RECORD
         B     L080                     BLANK STRING
         SPACE 3
L050     ZZLBL
         EX    R4,MVC                   INPUT BUFFER TO OUTPUT RECORD
         LA    R3,1(R3,R4)              INDEX INPUT
         BAL   R12,OUTPUT               OUTPUT CARD IMAGE
         B     L030
         SPACE 3
L060     ZZLBL
         CR    R1,R3                    Q. ANY CHARS. BEFORE X'FF'
         BE    L070
         LR    R2,R1
         SR    R2,R3                    R2 = LENGTH OF CHARACTER STRING
         EX    R2,MVC                   MOVE TO OUTPUT, 1 BYTE TOO LONG
         SR    R4,R2                    CARD SCAN COUNT
         AR    R5,R2                    INDEX OUTPUT
         MVI   0(R5),C' '               BLANK OUT EXTRA BYTE MOVED
         AR    R3,R2                    INDEX INPUT
L070     ZZLBL
         C     R3,ENDBUF                Q. END OF INPUT BUFFER
         BE    L010
         MVI   0(R5),X'FF'
         LA    R5,1(R5)                 INDEX OUTPUT
         LA    R3,1(R3)                 INDEX INPUT
         BCTR  R4,0                     CARD SCAN COUNT
         B     L030
         SPACE 3
L080     ZZLBL
         CR    R1,R3                    Q. ANY CHARS BEFORE BLANK      *
                                         STRING
         BE    L090
         LR    R2,R1
         SR    R2,R3                    R2 = LENGTH OF CHARACTER STRING
         EX    R2,MVC                   MOVE TO OUTPUT, 1 BYTE TOO LONG
         SR    R4,R2                    CARD SCAN COUNT
         AR    R5,R2                    INDEX OUTPUT
         MVI   0(R5),C' '               BLANK OUT EXTRA BYTE MOVED
         AR    R3,R2                    INDEX INPUT
L090     ZZLBL
         LA    R3,1(R3)                 INDEX INPUT
         C     R3,ENDBUF                Q. END OF INPUT BUFFER
         BNE   L100
         BAL   R12,READ                 READ NEXT INPUT BLOCK
L100     ZZLBL
         XR    R6,R6
         IC    R6,0(R3)                 LENGTH OF BLANK STRING
         LA    R3,1(R3)                 INDEX INPUT
L105     ZZLBL
         LA    R4,1(R4)                 SET SCAN COUNT TO ACTUAL LENGTH
         CR    R6,R4                    Q. ENOUGH BLANKS TO FILL RECORD
         BH    L120
         AR    R5,R6                    INDEX OUTPUT
         SR    R4,R6                    CARD SCAN COUNT
         BZ    L110
         BCTR  R4,0                     RESET SCAN COUNT TO MACH. LNTH.
         B     L030
L110     ZZLBL
         BAL   R12,OUTPUT               OUTPUT CARD IMAGE
         B     L030
         SPACE
L120     ZZLBL
         SR    R6,R4
         BAL   R12,OUTPUT
         B     L105
         SPACE 3
EOF      ZZLBL
         L     R3,STARTBUF
         FREEBUF LIBIN,(R3)
         CLOSE (LIBIN,,CARDOUT)
         XR    R15,R15
RETURN   ZZLBL
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         SPACE 5
*
* SUBROUTINE TO OUTPUT CARD IMAGES.
* N.B. ON END OF FILE, THIS SUBROUTINE DOES NOT RETURN TO CALLING POINT
OUTPUT   ZZLBL
         CLI   COBSW,X'FF'              Q. COBOL PROGRAM
         BNE   O010
         UNPK  COBSEQ,SEQUENCE          PUT BACK COBOL SEQUENCE NO.
         OI    COBSEQ+5,C'0'
         AP    SEQUENCE,=P'10'
         B     O020
O010     ZZLBL
         UNPK  OUTSEQ,SEQUENCE          PUT BACK SEQUENCE NUMBER
         OI    OUTSEQ+7,C'0'
         AP    SEQUENCE,=P'100'
O020     ZZLBL
         PUT   CARDOUT,OUTREC
         MVI   OUTREC,C' '
         MVC   OUTREC+1(79),OUTREC      CLEAR OUTPUT RECORD AREA
         CLI   COBSW,X'FF'
         BNE   O030
         LA    R4,73
         LA    R5,COBREC                SET COBOL OUTPUT RECORD INDEX
         B     O040
O030     ZZLBL
         LA    R4,71                    SET CARD SCAN COUNT (MACH. LEN)
         LA    R5,OUTREC                SET OUTPUT RECORD INDEX
O040     ZZLBL
         CLI   0(R3),X'00'              Q. END OF FILE
         BE    EOF
         BR    R12
         SPACE 5
*
* SUBROUTINE TO READ INPUT BLOCKS
*
READ     ZZLBL
         L     R3,STARTBUF
         FREEBUF LIBIN,(R3)
READ1    ZZLBL
         GETBUF LIBIN,(R3)
         ST    R3,STARTBUF
         READ  CK1,SF,LIBIN,(R3),'S'    READ INPUT LIBRARY BLOCK
         CHECK CK1
         LH    R1,DCBLRECL
         AR    R3,R1
         ST    R3,ENDBUF
         MVI   0(R3),X'FF'
         L     R3,STARTBUF
         BR    R12
         SPACE 3
ERR1     ZZLBL
         WTO   'PROBABLY MEMBER NOT FOUND',ROUTCDE=11
         LA    R15,4
         B     RETURN
         SPACE 3
TRT      TRT   0(0,R3),SCANTAB
SCANTAB  DC    13XL16'00'
         DC    XL15'00',X'08'           X'DF' DENOTES BLANK STRING
         DC    XL16'00'
         DC    XL15'00',X'04'           X'FF' IS PUT AT END OF RECORD
SEQUENCE DC    PL5'0'
MVC      MVC   0(0,R5),0(R3)            INPUT BUFFER TO OUTPUT RECORD
         DS    0H
LIST     DC    X'0112'
PROGNAME DS    CL8                      PROGRAM NAME
         DS    XL10
SAVE     DS    18F
STARTBUF DS    F                        START OF CURRENT INPUT BUFFER
OUTREC   DC    CL72' '
OUTSEQ   DC    CL8' '
         ORG   OUTREC
COBSEQ   DS    CL6
COBREC   DS    CL74
ENDBUF   DS    F
COBSW    DC    X'00'
LIBIN    DCB   DDNAME=LIBIN,                                           *
               BUFNO=3,                                                *
               DSORG=PS,                                               *
               EODAD=EOF,                                              *
               MACRF=R,                                                *
               BUFL=13031               1 MORE THAN BLOCK SIZE
CARDOUT  DCB   DDNAME=CARDOUT,                                         *
               DSORG=PS,                                               *
               LRECL=80,                                               *
               MACRF=PM,                                               *
               RECFM=FB
         DCBD  DSORG=PO
         END
