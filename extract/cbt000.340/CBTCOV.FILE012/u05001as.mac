TSGADEL CSECT
**********************************************************************
*                                                                    *
*    BLAKELANDS COMPUTER SERVICES LTD (VW) .                         *
*        TSGADEL :-                                                  *
* THIS PROGRAM READS THE VTOC OF A DISK DUMPS AND SCRATCHES ALL      *
* DATASETS THAT HAVEN'T BEEN OPENED DURING THE PRECEDING NUMBER OF   *
* DAYS SPECIFIED BY PARAMETER                                        *
* THE PROGRAM HAS THE FACILITY TO RUN IN SIMULATED MODE BY USE OF    *
* THE SECOND PARAMETER WHICH MUST BE ENTERED AS 'Y' TO RUN IN SCRATCH*
* MODE                                                               *
* THUS THE PARAMETERS MAY BE ENTERED IN 4 WAYS:-                     *
* BLANK - DEFAULTS TO 031N IE 31 DAYS + SIMULATED RUN                *
* Y - SCRATCH MODE DEFAULT 031 DAYS                                  *
* 999Y - DAYS = 999 + SCRATCH                                        *
* 999  - DAYS = 999 SIMULATE RUN                                     *
* IF THE 'DAYS' PART OF THE PARAMETER IS WRONG IN ANY WAY THE RUN    *
* DOES NOT FAIL BUT DEFAULT VALUES OF '031' + 'N' ARE TAKEN          *
* AND THE RUN CONTINUES.                                             *
*        THE FOLLOWING ZAP IS NEEDED TO PUT OPEN DATE IN VTOC        *
*                      NAME IFG0196W                                 *
*                      VER 028E 9140,40B0,4780,330A                  *
*                      VER 03A0 0000,0000                            *
*                      REP 028E 9640,40B0,47F0,339E                  *
*                      REP 03A0 58F0,0010,D203,4012,F038,47F0,3294   *
**********************************************************************
         SAVE  (14,12)                 SAVES
         BALR  12,0
         USING *,12                    AND
         LR    11,13
         LA    13,SAVEAREA             ASSOCIATED
         ST    13,8(11)
         ST    11,4(13)                THINGS
         L     2,0(1)                  PARM ADD IN R2
         LH    3,0(2)
         LTR   3,3
         BZ    TIME1                   NO PARAMETERS USE DEFAULTS
         LA    2,2(2)                  POINTS TO BEG OF PARM
         CH    3,=H'3'                 IS PARM 3 DIGITS LONG
         BE    T1                      EQUAL TO 3
         BH    T1                      GREATER THAN 3
         BL    T2                      LESS THAN
T1       EQU   *
         CLI   0(2),X'F0'
         BL    TIME1
         CLI   0(2),X'F9'
         BH    TIME1
         CLI   1(2),X'F0'
         BL    TIME1
         CLI   1(2),X'F9'
         BH    TIME1
         CLI   2(2),X'F0'
         BL    TIME1
         CLI   2(2),X'F9'
         BH    TIME1
         CH    3,=H'3'
         BE    T3
         CLI   3(2),C'Y'
         BNE   T3
         MVI   SHALLWE,C'Y'
         B     T3
T2       EQU   *
         CLI   0(2),C'Y'
         BNE   T3
         MVI   SHALLWE,C'Y'
         B     TIME1
T3       EQU   *
         PACK  DAYS,0(3,2)             PACK PARAMETER
TIME1    EQU   *
         TIME  DEC
         ST    1,MACDATE                    STORE TIME
         CP    MACDATE+2(2),DAYS       CAN I SUBTRACT?
         BNH   NEWYEAR                 NO
         SP    MACDATE+2(2),DAYS       SUBTRACT FOR COMPARES
         MVC   WORKS,MACDATE+1
         BAL   10,ROUTINE
         MVC   EDITDATE,EDDLES+2
         B     DATRTN
NEWYEAR  EQU   *
         AP    MACDATE+2(2),=P'365'    NEWYEAR
         SP    MACDATE+1(3),=P'1000'        PROC
         SP    MACDATE+2(2),DAYS
         MVC   WORKS,MACDATE+1
         BAL   10,ROUTINE
         MVC   EDITDATE,EDDLES+2
DATRTN   EQU   *                       SET
         LINK  EP=DATRTN,PARAM=(COD,DATEL),VL=1
**********************************************************************
*                                                                    *
*         DATRTN USED ABOVE WILL NEED TO BE ALTERED TO THE           *
*         STANDARD JULIAN DATE ROUTINE OF THE INSTALATION            *
*                                                                    *
**********************************************************************
         OPEN  (PRINTER,(OUTPUT))
***********************************************************************
*                                                                     *
* THIS CODE FOLLOWING ADDRESSES TIOT, AND OBTAINS FIRST DDNAME        *
*                                                                     *
***********************************************************************
DDNAME1  EQU   *
         EXTRACT TIOTAD,'S',FIELDS=(TIOT)
         L     2,TIOTAD                REG 2 HAS TIOT ADDRESS
         LA    2,24(2)                 DD ENTRY
         B     DDNAME2
DDNAMEX  EQU   *
         MVI   SWITCH,C'*'
         CLOSE SYSIN
         CLOSE DISKRQD
         BAL   10,DELMEMB             SCRATCH + LIST MEMBERS
         ZAP   LINECNT,=PL2'61'
         SP    PAGECNT,PAGECNT
         MVI   OUTS,C' '
         MVC   OUTS+1(132),OUTS
         MVC   OUTS+1(41),=C'THE DATA SETS SHOWN HAVE NOT BEEN OPENED '
         MVC   OUTS+42(6),=C'SINCE '
         MVC   OUTS+48(8),EDITDATE
         PUT   PRINTER,OUTS
         MVI   OUTS,C' '
         MVC   OUTS+1(132),OUTS
         MVC OUTS+1(42),=C'THEY HAVE BEEN DUMPED TO TAPE,UNCATALOGUED'
         MVC   OUTS+43(14),=C' AND SCRATCHED'
         PUT   PRINTER,OUTS
         MVI   OUTS,C' '
         MVC   OUTS+1(132),OUTS
DDNAME2  EQU   *
**********************************************************************
*                                                                    *
* THIS CODE SETS UP DCB AND READS JFCB                               *
*                                                                    *
**********************************************************************
         LA    2,20(2)                 NEXT DD ENTRY
         CLI   0(2),X'00'              IS LAST DDNAME?
         BE    ENDUP                   YES
         CLC   4(5,2),=C'VWSYS'
         BNE   DDNAME2
         MVC   VOL1(6),4(2)
         LA    3,DISKRQD
         MVC   40(8,3),4(2)            MOVE IN DDNAME TO DCB
         RDJFCB DISKRQD
         MVI   WORKAREA,X'04'
         MVC   WORKAREA+1(43),WORKAREA CHANGE DSNAME
         MVC   PACK,WORKAREA+118
         OPEN  (DISKRQD,(INPUT)),TYPE=J
         OPEN  (SYSIN,(OUTPUT))
**********************************************************************
*                                                                    *
* READ AND PRINT CODE                                                *
*                                                                    *
**********************************************************************
READS    EQU   *
         READ  DECB1,SF,DISKRQD,DISKREC,'S'
         CHECK DECB1
         LA    3,DISKREC
         CLI   44(3),X'F1'             IS FORMAT ONE?
         BNE   READS
         CLC   0(4,3),=C'SYS1'
         BE    READS
         CLC   0(7,3),=C'SYSCTLG'
         BE    READS
         CLI   59(3),X'00'
         BE    READS
         CLC   62(3,3),=C'IBM'         HAS THIS BEEN OPENED?
         BE    READS
         CP    63(3,3),MACDATE+1(3)    WHEN OPENED?
         BH    READS
         MVC   WORKS,63(3)
         BAL   10,ROUTINE
         B     FDRSETUP
ROUTINE  EQU   *
         MVC   YEAR+2(1),WORKS
         SRP   YEAR,64-1,0
         CP    WORKS+1(2),=P'60'
         BE    LEAPYEAR
ROUTINE2 EQU   *
         LA    9,TABLE
         LA    8,0
DATELOOP EQU   *
         CP    2(2,9),WORKS+1(2)
         BC    10,MDAY
         LA    9,2(9)
         LA    8,1(8)
         C     8,=F'12'
         BNL   MESSAGE
         B     DATELOOP
MDAY     EQU   *
         CP    0(2,9),WORKS+1(2)
         BNE   *+16
         LA    7,2
         SR    9,7
         LA    10,1
         SR    8,10
         LA    8,1(8)
         CVD   8,MONTH
         SRP   MONTH,1,0
         SP    WORKS+1(2),0(2,9)
         MVC   DAY,WORKS+1
         MVO   YEAR(3),MONTH+6(1)
         SRP   DAY,1,0
         MVO   YEAR(2),DAY(1)
         MVC   EDDLES,PATTLES
         ED    EDDLES,YEAR
         BR    10
FDRSETUP EQU   *
         MVI   FILOUT,C' '
         MVC   FILOUT+1(43),FILOUT     SPACE FILL FILE NAME
         MVC   FILOUT(44),0(3)         MOVE FILE NAME OUT
         PUT   SYSIN,FDROUT            WRITE SYSIN RECORD
         B     READS
**********************************************************************
*                                                                    *
*        FDRSETUP WRITES THE SYSIN RECORDS FOR FDRDSF TO DUMP        *
*        THE FILES OVER THE SO MANY DAYS WITHOUT BEING OPENED        *
*                                                                    *
**********************************************************************
DELMEMB  EQU   *
         CLI   SHALLWE,C'Y'            SIMULATE RUN?
         BNE   DMNO                    NO
         LINK  EP=FDRDSF               CALL FAST DUMP RESTORE
DMNO     EQU   *
         OPEN  (SYSIN,(INPUT))
DM1      EQU   *
         GET   SYSIN,FDROUT
         CLI   SHALLWE,C'Y'            SIMULATE RUN ?
         BNE   DMNO1
*        CATALOG CAMI
DMNO1    EQU   *
**********************************************************************
*        THE UNCATALOG HAS BEEN OMITTED FOR REASONS OF LIBRARY       *
*        SECURITY                                                    *
**********************************************************************
         LTR   15,15
         BZ    DM2
         MVI   UNMOG,C'N'
         B     DM3
DM2      EQU   *
         MVI   UNMOG,C'Y'
DM3      EQU   *
         CLI   SHALLWE,C'Y'            SIMULATE RUN ?
         BNE   DM4
         SCRATCH SC1
DM4      EQU   *
         CP    LINECNT,=P'57'
         BH    HEADS
         MVI   OUTS,C' '
PRINTS2  EQU   *
         MVC   OUTS+18(44),FILOUT
         MVC   OUTS+65(1),UNMOG
         PUT   PRINTER,OUTS
         AP    LINECNT,=P'1'
         MVI   OUTS,C' '
         MVC   OUTS+1(132),OUTS
         B     DM1
DM999    EQU   *
         CLOSE SYSIN
         BR    10
MESSAGE  EQU   *
         ABEND 4,DUMP
LEAPYEAR EQU   *
         MVC   YEAR1,YEAR
         DP    YEAR1,=P'4'
         CP    YEAR1+3(1),=P'0'
         BNE   ROUTINE2
         MVC   EDDLES+7(3),=X'202020'
         ED    EDDLES+7(3),YEAR+2
         MVC   EDDLES+2(5),=C'29/02/'
         BR    10
HEADS    EQU   *
         CLI   SWITCH,C'*'
         BE    HEADS1
         MVI   OUTS,C' '
         MVC   OUTS+1(132),OUTS
         MVC   OUTS+1(41),=C'THE DATA SETS SHOWN HAVE NOT BEEN OPENED '
         MVC   OUTS+42(6),=C'SINCE '
         MVC   OUTS+48(8),EDITDATE
         PUT   PRINTER,OUTS
         MVC OUTS+1(42),=C'THEY HAVE BEEN DUMPED TO TAPE,UNCATALOGUED'
         MVC   OUTS+43(14),=C' AND SCRATCHED'
         PUT   PRINTER,OUTS
         MVI   OUTS,C' '
         MVC   OUTS+1(132),OUTS
HEADS1   EQU   *
         MVI   SWITCH,C' '
         AP    PAGECNT,=P'1'
         MVI   OUTS,C'1'               SET
         MVC   OUTS+1(8),=C'TSGADEL '
         MVC   OUTS+19(19),=C'BLAKELANDS C.S. LTD'
         MVC   OUTS+49(19),=C'AUTOMATIC ARCHIVING'
         MVC   OUTS+68(20),=C' AND DELETION SYSTEM'
         MVC   OUTS+97(8),VOL1
         MVC   OUTS+107(8),DATEL       MOVE TODAYS DATE IN
         MVC   OUTS+124(4),=C'PAGE'
         MVC   EDS2,PATTER
         ED    EDS2,PAGECNT
         MVC   OUTS+129(3),EDS2+1
         PUT   PRINTER,OUTS
         MVI   OUTS,C' '
         MVC   OUTS+1(132),OUTS
HEADS2   EQU   *
         MVI   OUTS,C'0'
         MVC   OUTS+30(13),=C'DATA SET NAME'
         MVC   OUTS+59(16),=C'UNCATALOGUED    '
         PUT   PRINTER,OUTS
         ZAP   LINECNT,CONST
         MVI   OUTS,C'0'
         MVI   OUTS+1,C' '
         MVC   OUTS+2(131),OUTS+1
         B     PRINTS2
ENDUP    EQU   *
         CLOSE PRINTER
         L     13,4(13)
         RETURN (14,12)
SHALLWE  DC    CL1'N'                  DEFAULT TO SIMULATE RUN
DAYS     DC    PL2'31'
         DS    0F
MACDATE  DS    PL4
COD      DC    C'T'
DATEL    DS    CL8
SAVEAREA DS    18F
EDITDATE DS    CL8
WORKS    DS    CL3
         PRINT NOGEN
PRINTER  DCB   DDNAME=PRINTER,MACRF=PM,DSORG=PS,BLKSIZE=133,           X
               LRECL=133,RECFM=FBA
DISKRQD  DCB   DDNAME=XXXXXXXX,MACRF=R,RECFM=F,DSORG=PS,EXLST=EX1,     X
               EODAD=DDNAMEX,KEYLEN=44,BLKSIZE=96
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GM,PM),DSORG=PS,RECFM=FB,LRECL=80,  *
               BLKSIZE=4800,EODAD=DM999
         PRINT GEN
EX1      DS    0F
         DC    X'07'
         DC    AL3(WORKAREA)
WORKAREA DS    CL176
DISKREC  DS    CL140
LINECNT  DC    PL2'61'
PAGECNT  DC    PL2'0'
OUTS     DC    CL133' '
EDS      DS    CL6
PACK     DS    CL6
EDDLES   DS    CL10
PATTLES  DC    X'40212020612020612020'
PATTER   DC    X'40202020'
CONST    DC    PL2'5'
EDS2     DS    CL4
TABLE    DC    PL2'00'                 BEGINNING
         DC    PL2'31'                 JAMUARY 1
         DC    PL2'59'                 FEBRUARY 2
         DC    PL2'90'                 MARCH 3
         DC    PL2'120'                APRIL 4
         DC    PL2'151'                MAY 5
         DC    PL2'181'                JUNE 6
         DC    PL2'212'                JULY 7
         DC    PL2'243'                AUGUST 8
         DC    PL2'273'                SEPTEMBER 9
         DC    PL2'304'                OCTOBER 10
         DC    PL2'334'                NOVEMBER 11
         DC    PL2'365'                DECEMBER 12
MONTH    DS    D
DAY      DS    H
YEAR     DC    PL4'0'
YEAR1    DC    PL4'0'
ASTERISK DC C'ASTERISKS INDICATE A DATASET NOT OPENED SINCE 08/02/78'
         DS    0D
TEMPDAT  DC    PL8'0'
CREATDAT DC PL8'0'
SWITCH   DC C'*'
FDROUT   DS    0CL80
         DC    CL10' DUMP DSN='
FILOUT   DC    CL44' '
         DC    CL26' '
TIOTAD   DC    F'0'                    TIOT ADDRESS
UNMOG    DC    CL1'N'
VOLLIST  DC    H'1'
         DC    X'3050200B'             3350 TYPE CODE
VOL1     DC    CL6'VWSYS7'
SSC      DC    H'0'
CAMI     CAMLST UNCAT,FILOUT
SC1      CAMLST SCRATCH,FILOUT,,VOLLIST,,OVRD
         END
