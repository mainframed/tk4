         MACRO
&NAME    SUMARIZE &CFIELDS=,&SFIELDS=,&OVFLOW=
         LCLA  &GENCTR,&DISP,&LEN,&LLEN
         AIF   (N'&CFIELDS GT 192).TOOMANY
         AIF   ('&CFIELDS' EQ '').NOCNTRL
&GENCTR  SETA  1
&NAME    L     2,0(1) REG 2 CONTAINS ADDRESS OF RECORD LEAVING MERGE
         LA    2,0(2) ZERO OUT HIGH ORDER BITS
         L     3,4(1) REG 3 CONTAINS ADDRESS OF RECORD IN OUTPUT AREA
         LA    3,0(3) ZERO OUT HIGH ORDER BITS
         LR    5,1   SAVE PARAMETER LIST ADDRESS
         LTR   3,3 FIRST RECORD
         BZ    RT&SYSNDX.00 YES - NO ACTION
         LTR   2,2 LAST RECORD
         BZ    RT&SYSNDX.08 YES - DO NOT RETURN
.GENAGEN ANOP
&DISP    SETA  &CFIELDS(&GENCTR)-1
&LEN     SETA  &CFIELDS(&GENCTR+1)
         AIF   ('&CFIELDS(&GENCTR+2)' EQ 'P').GENPACK
         AIF   ('&CFIELDS(&GENCTR+2)' NE 'C').TYPER
         AIF   (&LEN GT 256).BIGFLD
                CLC   &DISP.(&LEN,2),&DISP.(3) ARE CONTROL FIELDS EQUAL
.BRANCH  ANOP
         BNE   RT&SYSNDX.00 NO - NO ACTION
&GENCTR  SETA  &GENCTR+3
         AIF   (&GENCTR GT N'&CFIELDS).GENSUM
         AGO   .GENAGEN
.GENPACK ANOP
         AIF   (&LEN GT 16).BIGPACK
                CP    &DISP.(&LEN,2),&DISP.(&LEN,3) ARE CONTROL FIELDS *
               EQUAL
         AGO   .BRANCH
.GENSUM  ANOP
         AIF   ('&SFIELDS' EQ '').NULL
&GENCTR  SETA  1
.GENAGIN ANOP
&DISP    SETA  &SFIELDS(&GENCTR)-1
&LEN     SETA  &SFIELDS(&GENCTR+1)
         AIF   ('&SFIELDS(&GENCTR+2)' EQ 'Z').GENZSU
         AIF   ('&SFIELDS(&GENCTR+2)' EQ 'B').CHKLEN
         AIF   ('&SFIELDS(&GENCTR+2)' NE 'P').WRNGTYP
         AIF   (&LEN GT 16).BIGPACK
         ZAP   SU&SYSNDX.ME(&LEN),&DISP.(&LEN,2)
         AP    SU&SYSNDX.ME(&LEN),&DISP.(&LEN,3)
         AIF   ('&OVFLOW' NE '').GENPFLO
.MPBACK  MVC   &DISP.(&LEN,3),SU&SYSNDX.ME
.B1      ANOP
&GENCTR  SETA  &GENCTR+3
         AIF   (&GENCTR GT N'&SFIELDS).GENDONE
         AGO   .GENAGIN
.CHKLEN  ANOP
         AIF   (&LEN EQ 2).HWORD
         AIF   (&LEN NE 4).ERLEN
         MVC   SU&SYSNDX.ME(4),&DISP.(2)
         L     4,SU&SYSNDX.ME
         MVC   SU&SYSNDX.ME(4),&DISP.(3)
         A     4,SU&SYSNDX.ME
         AIF   ('&OVFLOW' NE '').GENFFLO
.MFBACK  ST    4,SU&SYSNDX.ME
         MVC   &DISP.(4,3),SU&SYSNDX.ME
         AGO   .B1
.HWORD   ANOP
         MVC   SU&SYSNDX.ME(2),&DISP.(2)
         LH    4,SU&SYSNDX.ME
         MVC   SU&SYSNDX.ME(2),&DISP.(3)
         AH    4,SU&SYSNDX.ME
         AIF   ('&OVFLOW' NE '').GENHFLO
.MHBACK  STH   4,SU&SYSNDX.ME
         MVC   &DISP.(2,3),SU&SYSNDX.ME
         AGO   .B1
.GENZSU  ANOP
         AIF   (&LEN GT 16).BIGZD
&LLEN    SETA  &LEN/2+1
         PACK  SU&SYSNDX.ME(&LLEN),&DISP.(&LEN,2)
         PACK  SU&SYSNDX.OU(&LLEN),&DISP.(&LEN,3)
         AP    SU&SYSNDX.ME(&LLEN),SU&SYSNDX.OU(&LLEN)
         AIF   ('&OVFLOW' NE '').GENZFLO
.MZBACK  UNPK  &DISP.(&LEN,3),SU&SYSNDX.ME(&LLEN)
         AGO   .B1
.GENPFLO AIF   ('&OVFLOW' EQ '''S''').GENBRSP
         AIF   ('&OVFLOW' EQ '''A''').GENBRAP
         LA    6,&DISP
         LA    7,*+8
         BO    &OVFLOW
         AGO   .MPBACK
.GENFFLO AIF   ('&OVFLOW' EQ '''S''').GENBRSF
         AIF   ('&OVFLOW' EQ '''A''').GENBRAF
         LA    6,&DISP
         LA    7,*+8
         BO    &OVFLOW
         AGO   .MFBACK
.GENHFLO AIF   ('&OVFLOW' EQ '''S''').GENBRSH
         AIF   ('&OVFLOW' EQ '''A''').GENBRAH
         LA    6,&DISP
         LA    7,*+8
         BO    &OVFLOW
         AGO   .MHBACK
.GENZFLO AIF   ('&OVFLOW' EQ '''S''').GENBRSZ
         AIF   ('&OVFLOW' EQ '''A''').GENBRAZ
         LA    6,&DISP
         AIF   ((&LEN/2)*2-&LEN EQ 0).CHKFLOW
         LA    7,*+8
         BO    &OVFLOW
.MZX     ANOP
         AGO   .MZBACK
.CHKFLOW ANOP
         LA    7,*+12
         TM    SU&SYSNDX.ME,X'F0'
         BC    7,&OVFLOW
         AGO   .MZX
.GENBRSP ANOP
         BO    OFLO&SYSNDX
         MVC   &DISP.(&LEN,2),=PL&LEN'0'
         AGO   .MPBACK
.GENBRAP ANOP
         BO    OFLO&SYSNDX
         AGO   .MPBACK
.GENBRSF ANOP
         BO    OFLO&SYSNDX
         MVC   &DISP.(4,2),=F'0'
         AGO   .MFBACK
.GENBRAF ANOP
         BO    OFLO&SYSNDX
         AGO   .MFBACK
.GENBRSH ANOP
         BO    OFLO&SYSNDX
         MVC   &DISP.(2,2),=H'0'
         AGO   .MHBACK
.GENBRAH ANOP
         BO    OFLO&SYSNDX
         AGO   .MHBACK
.GENBRSZ ANOP
         AIF   ((&LEN/2)*2-&LEN EQ 0).GENOCHK
         BO    OFLO&SYSNDX
.MZZ     ANOP
         MVC   &DISP.(&LEN,2),=ZL&LEN'0'
         AGO   .MZBACK
.GENOCHK ANOP
         TM    SU&SYSNDX.ME,X'F0'
         BC    7,OFLO&SYSNDX
         AGO   .MZZ
.GENBRAZ ANOP
         AIF   ((&LEN/2)*2-&LEN EQ 0).CHKOFLO
         BO    OFLO&SYSNDX
.MZY     ANOP
         AGO   .MZBACK
.CHKOFLO ANOP
         TM    SU&SYSNDX.ME,X'F0'
         BC    7,OFLO&SYSNDX
         AGO   .MZY
.ERLEN MNOTE 8,'BINARY DATA FIELD LENGTH NOT 2 OR 4.  GENERATION TERMIN*
               ATED.'
         MEXIT
.BIGZD   MNOTE 8,'ZONED DECIMAL FIELD EXCEEDS 16 DIGITS.  GENERATION TE*
               RMINATED.'
         MEXIT
.WRNGTYP MNOTE 8,'SUMMARY FIELD TYPE NOT ''P'', ''Z'', OR ''B''.  GENER*
               ATION TERMINATED.'
         MEXIT
.TOOMANY MNOTE 8,'TOO MANY CONTROL FIELDS.  GENERATION TERMINATED.'
         MEXIT
.TYPER   MNOTE 8,'CONTROL FIELD TYPE NOT ''C'' OR ''P''.  GENERATION TE*
               RMINATED.'
         MEXIT
.BIGFLD  MNOTE 8,'CHARACTER TYPE FIELD GREATER THAN 256 CHARACTERS.  GE*
               NERATION TERMINATED.'
         MEXIT
.BIGPACK MNOTE 8,'PACKED DECIMAL FIELD GREATER THAN 31 DIGITS AND SIGN.*
                 GENERATION TERMINATED'
         MEXIT
.NOCNTRL MNOTE 8,'''CFIELDS'' OPERAND IS MISSING.  GENERATION TERMINATE*
               D.'
         MEXIT
.NULL    ANOP
         LA    15,4                EQUAL COMPARE
         B     SU&SYSNDX.ND
RT&SYSNDX.00 LA    15,0            UNEQUAL COMPARE
         B     SU&SYSNDX.ND
RT&SYSNDX.08 LA    15,8 DO NOT RETURN
SU&SYSNDX.ND EQU   *
         EJECT
         MEXIT
.GENDONE LA    15,4 DELETE RECORD
         B     SU&SYSNDX.ND
RT&SYSNDX.00 LA    15,0 NO ACTION
         LR    1,2
         B     SU&SYSNDX.ND
RT&SYSNDX.08 LA    15,8 DO NOT RETURN
         B     SU&SYSNDX.ND
         AIF   ('&OVFLOW' EQ '''S''').CONTA
         AIF   ('&OVFLOW' EQ '''A''').CONTA
         AGO   .CONT
.CONTA   ANOP
OFLO&SYSNDX EQU   *
         AIF   ('&OVFLOW' EQ '''S''').GENNOAC
         WTO   'ARITHMETIC OVERFLOW OCCURRED.  ABEND/DUMP REQUESTED.'
         ABEND 300,DUMP
         AGO   .CONT
.GENNOAC B RT&SYSNDX.00
.CONT    ANOP
         DS    0F
SU&SYSNDX.ME DS    CL9
SU&SYSNDX.OU DS    CL9
         LTORG
         DS    0H
SU&SYSNDX.ND EQU   *
         EJECT
         MEXIT
         MEND
