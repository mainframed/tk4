CALN     TITLE 'UTILITY PROGRAM TO INVOKE UTILITIES EASILY'
**********************************************************************
**                                                                  **
**  THIS IS A UTILITY PROGRAM WHICH MAKES THE INVOKATION OF IBM     **
**  UTILITY PRORGAMS USABLE EASILY BY CATALOGUED PROCEDURES.        **
**  THIS MEANS THAT ONE CARD CAN BE USED TO INVOKE SUCH THINGS AS   **
**  LISTVTOC, LISTPDS, SCRATCH, CATALOG, COMPRESS.... ETC           **
**  THE DATA CARD FOR THESE PROGRAMS ARE PASSED TO THIS PROGRAM     **
**  IN THE PARM FIELD AND ARE WRITTEN TO SYSIN AND THE THE UTILITY  **
**  ROUTINE IS LOADED AND EXECUTED.                                 **
**  THE FORMAT OF THE PARAMETER TO THIS PROGRAM IS THE SAME AS THE  **
**  UTILITY CONTROL CARD EXCEPT THAT IT MUST HAVE A LABEL IN COL 1  **
**  WHICH MUST BE FOLLOWED BY AT LEAST ONE BLANK. THIS LABEL        **
**  SHOULD BE THE NAME OF THE REQUIRED PROGRAM IE IEHLIST           **
**  EG:                                                             **
**  EXEC PGM=UTILITY,PARM='IEHLIST  LISTVTOC VOL=2314=SYSRES'       **
**  THIS ALONG WITH THE USUAL DD CARDS SHOULD GIVE A LISTVTOC.      **
**  ONLY ONE CONTROL CARD MAY BE SPECIFIED IN THE PARM FIELD        **
**                                                                  **
**********************************************************************
         PRINT NOGEN
CALLIN   START 0
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         ST    13,SAVE+4
         LA    13,SAVE
         L     1,0(1)
         LH    2,0(1)
         LA    1,2(1)
         LA    3,0
LOOP     CLI   0(1),X'40'
         BE    OUT
         IC    0,0(1)
         STC   0,PROGRAM(3)
         LA    3,1(3)
         LA    1,1(1)
         BCTR  2,0
         B     LOOP
OUT      BCTR  2,0
         STC   2,MVC2+1
MVC2     MVC   OUTCARD(0),0(1)
         OPEN  (SYSIN,OUTPUT)
         PUT   SYSIN,OUTCARD
         CLOSE SYSIN
         LOAD  EPLOC=PROGRAM
         LR    15,0
         LA    1,PLIST
         BALR  14,15
         L     13,SAVE+4
         LM    0,12,20(13)
         L     14,12(13)
         BR    14
PLIST    DS    0F
         DC    X'80'
         DC    AL3(RAVE)
RAVE     DC    H'0'
SAVE     DC    18F'0'
PROGRAM  DC    8C' '
OUTCARD  DC    80C' '
         LTORG
SYSIN    DCB   DSORG=PS,RECFM=FB,MACRF=PM,BLKSIZE=80,LRECL=80,         *
               DDNAME=SYSIN
         END
