*          DATA SET PVGSMPPR   AT LEVEL 017 AS OF 25/03/83
         TITLE 'PVGSMPPR - PRINT SMPCDS DIRECTORY SUMMARY'
PVGSMPPR CSECT
         B     76(15)        BRANCH OVER SAVE AREA AND CONSTANTS
         DC    18F'0'        SAVE AREA
         STM   14,12,12(13)  SAVE 14-12 IN CALLING SAVE AREA
         ST    13,8(15)      SAVE 13 IN OUR SAVE+4
         LA    11,4(15)      LOAD ADDR OF OUR SAVE AREA
         ST    11,8(13)      STORE IN CALLING SAVE AREA
         LR    13,11         POINT 13 AT OUR SAVE AREA
         LR    2,15
         USING 2,PVGSMPPR
         PRINT NOGEN
         OPEN  (SMPCDS,,PRINTA,OUTPUT)
         PUT   PRINTA,HEADA
         PUT   PRINTA,HEADB
         PUT   PRINTA,CTLLIN
         LA    6,11                    11 MEMBERS PER PRINT LINE
         LA    7,LINE
READ     EQU   *
         READ  READDECB,SF,SMPCDS,BUFFER
         CHECK READDECB
         LA    3,BUFFER+2              POINT AT FIRST ENTRY
         CLI   EOFSW,255               DID WE HAVE EOF IN LAST BLOCK
         BE    EOF
         LA    4,7                     7 MEMBERS PER BLOCK
LOOP     EQU   *
         SR    5,5
         MVC   WORK,1(3)
         ICM   5,7,WORK                INSERT 1ST 3 BYTES
         SRL   5,2
         STCM  5,4,KROW                STORE SHIFTED 1ST BYTE
         ICM   5,4,ZERO                ZEROISE BYTE IN REG
         SRL   5,2
         STCM  5,2,KROW+1
         ICM   5,2,ZERO
         SRL   5,2
         STCM  5,1,KROW+2
         MVC   KROW+3(1),WORK+2        MOVE IN 4TH SET OF 6 BITS
         NI    KROW+3,X'3F'            ZEROISE LEFT 2 BITS
         ICM   5,7,WORK+3              INSERT 2ND 3 BYTES
         SRL   5,2
         STCM  5,4,KROW+4              STORE SHIFTED 5TH BYTE
         ICM   5,4,ZERO                ZEROISE BYTE IN REG
         SRL   5,2
         STCM  5,2,KROW+5
         ICM   5,2,ZERO
         SRL   5,2
         STCM  5,1,KROW+6
         MVC   KROW+7(1),WORK+5        MOVE IN 8TH SET OF 6 BITS
         NI    KROW+7,X'3F'            ZEROISE LEFT 2 BITS
         TR    KROW,TABLE              TRANSLATE
         MVC   3(8,7),KROW             MOVE TRANSLATED NAME TO PRINT
         MVC   0(1,7),0(3)             MOVE IN TYPE
         CLC   3(8,7),EOFMEM           IS IT EOF
         BNE   NOTALL9S
         MVI   EOFSW,255
NOTALL9S EQU   *
         LA    7,12(7)                 POINT TO NEXT PRINT POSITION
         LA    3,36(3)                 POINT TO NEXT MEMBER IN BUFFER
         BCT   6,CHECK4
         PUT   PRINTA,CTLLIN
         MVC   LINE,CTLLIN
         LA    6,11
         LA    7,LINE
CHECK4   EQU   *
         BCT   4,LOOP
         B     READ
EOF      EQU   *
         PUT   PRINTA,CTLLIN
         CLOSE (SMPCDS,,PRINTA)
         L     13,4(13)
         RETURN (14,12),RC=0
HEADA    DS    0CL133
         DC    C'1'
         DC    CL44'PVGSMPPR - DIRECTORY PRINT OF SMPCDS'
         DC    CL44' '
         DC    CL44' '
HEADB    DS    0CL133
         DC    C' '
         DC    CL44'A(ASSEM) D(LMOD) G(MACRO) J(MOD) M(SOURCE)'
         DC    CL44' P(DLIB) Z(SMPCDS EOF) 3(SYSMOD)'
         DC    CL44' '
SMPCDS   DCB   DDNAME=SMPCDS,DSORG=PO,EODAD=EOF,MACRF=R,BLKSIZE=256
PRINTA   DCB   DDNAME=PRINTA,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,  CCC
               BLKSIZE=2660
EOFSW    DC    X'0'                    END OF FILE SWITCH
EOFMEM   DC    C'99999999'             END OF FILE MEMBER
ZERO     DC    X'0'
WORK     DC    CL8' '
KROW     DC    CL8' '
TABLE    DC    X'0',X'40'
         DC    26X'0'
         DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'
CTLLIN   DC    C' '
LINE     DC    CL132' '
BUFFER   DS    4096C
         END   PVGSMPPR
