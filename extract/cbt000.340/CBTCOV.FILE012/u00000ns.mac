PTEMP    CSECT
*
*
*  THIS PROGRAM PREPARES A SCRATCH CARD IMAGE FOR EVERY DATA SET
*  ON EACH OF UP TO 20 DISK PACKS NAMED BY A DD CARD.
*  THE CARD IMAGES ARE BOTH PUNCHED AND PRINTED AFTER SORTING INTO
*  SEQUENCE OF CREATION DATE.
*  IF THERE IS NO PARM ON THE EXEC CARD ONLY TEMPORARY DATA SETS
*  (INCLUDING **SYSUT TYPE) ARE DEALT WITH.  PARM=ALL PRODUCES OUTPUT
*  FOR ALL DATA SETS.
*  CARDS AND LISTING ARE SERIALLY NUMBERED SO THAT SCRATCH CARDS CAN
*  BE SELECTED WITHOUT INTERPRETING THE CARDS.  NOTE THAT EACH SCRATCH
*  STATEMENT OCCUPIES TWO CARDS.
*  THE PROGRAM IS CURRENTLY LIMITED TO 2311, 2314, AND 2302 PACKS, BUT
*  THE 'SCANTIOT' SECTION COULD EASILY BE MODIFIED FOR OTHER TYPES.
*  THE JCL REQUIRED TO RUN THIS PROGRAM, WHICH INVOKES SORT, MUST
*  INCLUDE.....
*  //SCRATCH EXEC PGM=PUNCHSCR(,PARM=ALL)
*  //SYSOUT  DD  SYSOUT=...
*  //SORTLIB  DD  DSN=SYS1.SORTLIB,DISP=SHR
*  //TIN  DD  SPACE=(CYL,(2,1)),UNIT=SYSDA
*  //SORTIN  DD  VOL=REF=*.TIN,DSN=*.TIN,DISP=(SHR,DELETE)
*  //SORTOUT  DD  SPACE=(CYL,(2,1)),UNIT=SYSDA,DCB=(RECFM=FB,
*                 LRECL=66,BLKSIZE=660)
*  //TOUT  DD  VOL=REF=*.SORTOUT,DSN=*.SORTOUT,DISP=(SHR,DELETE)
*  //SORTWK01  DD  SPACE=(CYL,1,,CONTIG),UNIT=SYSDA
*  ...OTHER SORTWK CARDS AS REQUIRED.......
*  //PUNCH  DD  SYSOUT=...
*  //PRINT  DD  SYSOUT=...
*  ...FOLLOWED BY UP TO 20 DD CARDS, HAVING ANY DDNAME BEGINNING WITH
*  'D', TO IDENTIFY THE PACKS TO BE SEARCHED E.G....
*  //DA  DD  VOL=SER=123,UNIT=2314,DISP=SHR
*
*
*        BEGIN
*  EXPANSION OF RESPOND TSG MACRO
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
*  END OF MACRO  EXPANSION
         L     1,0(1)
         LH    2,0(1)
         LTR   2,2
         BC    8,FINDTIOT
         CLC   2(3,1),ALL
         BNE   FINDTIOT
         NI    PPA+1,15
         NI    PPB+1,15
FINDTIOT L     1,16          CVT ADDRESS
         L     1,0(1)        TCB POINTER
         L     2,0(1)        TCB ADDRESS
         L     2,12(2)       TIOT ADDRESS
         LA    2,24(2)       FIRST DDNAME
         LA    3,DDNT        DDNAME TABLE
         SR    4,4
         LA    5,20
         IC    4,0(2)
SCANTIOT LTR   4,4
         BZ    EOT
         CLI   4(2),C'D'
         BNE   STEPON
         L     6,16(2)
         MVC   0(8,3),4(2)
         MVC   8(4,3),C2302
         CLI   18(6),X'20'
         BNE   STEPON
         CLI   19(6),X'04'
         BE    STEPON-6
         MVC   8(4,3),C2314
         CLI   19(6),X'08'
         BE    STEPON-6
         MVC   8(4,3),C2311
         CLI   19(6),X'01'
         BNE   STEPON
         LA    3,12(3)
         BCTR  5,0
STEPON   IC    4,0(2)
         AR    2,4
         LA    5,1(5)
         BCT   5,SCANTIOT
         WTO   'OVER 20 DDCARDS FOR RTPSCRAT  - EXCESS CARDS IGNORED'
EOT      LA    3,DDNT-12
         OPEN  (TIN,(OUTPUT))
NEWDISK  LA    3,12(3)
         CLI   0(3),X'00'
         BE    SORT
         MVC   DISK+40(8),0(3)
         MVC   TINTYPE,8(3)
         RDJFCB (DISK)
         MVC   JFCB(44),F4
         MVC   TINV,JFCB+118
         OPEN  (DISK),TYPE=J
         SR    4,4
         SR    5,5
READ     READ  A,SF,DISK,DSCB,'S'
         CHECK A
         CLI   KEY,X'07'
         MVI   AORB,C'B'
         BL    READ
         MVC   TINN,KEY
         CLC   TINN(3),SYS
         BNE   NOTSYS
         CLC   TINN+8(2),T
         BNE   NOTSYS
         CLI   TINN+18,C'F'
         BE    *+12
         CLI   TINN+18,C'V'
         BNE   NOTSYS
*
*  THE ABOVE TESTS ASSUME THAT SYSTEM GENERATED NAMES WILL HAVE
*        'SYS' IN POSITIONS 1 - 3
*        '.T' IN POSITIONS 7 - 8
*        'F' OR 'V' IN POSITION 19
*  OTHER TESTS CAN BE ADDED IF OTHER FORMATS ARE USED
*
         MVI   AORB,C'A'
         MVC   TIND,TINN+3
         MVC   TINT,TINN+10
         B     PUTTIN
NOTSYS   CLC   TINN(7),SYSUT
         BNE   *+8
         MVI   AORB,C'A'
         IC    4,DSCB+53
         CVD   4,DW
         UNPK  TIND(2),DW
         OI    TIND+1,X'F0'
         LH    5,DSCB+54
         CVD   5,DW
         UNPK  TIND+2(3),DW
         OI    TIND+4,X'F0'
         MVC   TINT,SPACES
PUTTIN   PUT   TIN,TINREC
         B     READ
EOF      CLOSE (DISK)
         B     NEWDISK
SORT     CLOSE (TIN)
         LA    1,PLIST
         LINK  EP=SORT
         OPEN  (TOUT,,PRINT,(OUTPUT),PUNCH,(OUTPUT))
         LA    10,20
         PUT   PRINT,NEWPAGE
GET      GET   TOUT,TINREC
         MVC   VOLTYPE,TINTYPE
         MVC   VOLSER,TINV
         MVC   CDATE,TIND
         MVC   CTIME,TINT
         MVC   CSEQ1,EDMASK
         ED    CSEQ1,CNO
         AP    CNO,P1
         MVC   CSEQ2,EDMASK
         ED    CSEQ2,CNO
         MVC   CDSN,TINN
         PUT   PRINT,LINE1
         CLI   AORB,C'B'
PPA      BE    PA
         PUT   PUNCH,CARD1
         AP    CNO,P1
PA       PUT   PRINT,LINE2
         CLI   AORB,C'B'
PPB      BE    PBCT
         PUT   PUNCH,CARD2
PBCT     BCT   10,GET
         PUT   PRINT,NEWPAGE
         LA    10,20
         B     GET
EOFT     CLOSE (TOUT,,PRINT,,PUNCH)
*        GOBACK
*  EXPANSION OF RESPOND TSG MACRO
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
*  END OF MACRO EXPANSION
DISK     DCB   DDNAME=DISK,LRECL=96,KEYLEN=44,BLKSIZE=96,EXLST=XLIST,  *
               EODAD=EOF,MACRF=(R),RECFM=F,DSORG=PS
PUNCH    DCB   DDNAME=PUNCH,MACRF=(PM),DSORG=PS,RECFM=FB,LRECL=80,     *
               BLKSIZE=800
TIN      DCB   DSORG=PS,DDNAME=TIN,RECFM=FB,LRECL=66,BLKSIZE=660,      *
               MACRF=(PM)
TOUT     DCB   DDNAME=TOUT,DSORG=PS,LRECL=66,BLKSIZE=660,RECFM=FB,     *
               MACRF=(GM),EODAD=EOFT
PRINT    DCB   DDNAME=PRINT,MACRF=(PM),DSORG=PS,RECFM=FBM,LRECL=81,    *
               BLKSIZE=810
DDNT     DC    20XL12'00'
         DC    X'00'
         DS    0F
XLIST    DC    X'87'
         DC    AL3(JFCB)
JFCB     DC    CL176' '
F4       DC    44X'04'
DSCB     DS    0CL140
KEY      DC    CL44' '
DATA     DC    CL96' '
DW       DC    D'0'
PLIST    DC    X'80'
         DC    AL3(PARM)
*  PARM LIST FOR SORT CONSISTS OF...
*        LENGTH OF PARM LIST (HALFWORD NOT ON FULLWORD BOUND)
*        ADDRESS OF START OF SORT PARAMETER
*        ADDRESS OF END OF SORT PARAMETER
*        ADDRESS OF START OF RECORD PARAMETER
*        ADDRESS OF END OF RECORD PARAMETER
*        ADDRESS OF E15 EXIT
*        ADDRESS OF E35 EXIT
*        OPTIONAL DDNAMES
*        CORE SIZE
*        TAPE TECHNIQUES
         CNOP  2,4
PARM     DC    H'32'
         DC    A(SS)
         DC    A(SS+35)
         DC    A(RS)
         DC    A(RS+25)
         DC    2F'0'
         DC    C'SORT'
         DC    F'45056'
         DC    F'0'
         DC    X'FF000000'
SS       DC    C' SORT FIELDS=(1,18,BI,A),SIZE=E500  '
RS       DC    C' RECORD TYPE=F,LENGTH=66  '
NEWPAGE  DC    X'8B'
LINE1    DS    0CL81
         DC    X'09'
CARD1    DS    0CL80
         DC    C' SCRATCH VOL='
VOLTYPE  DC    CL4' '
         DC    C'='
VOLSER   DC    CL6' '
         DC    CL6','
         DC    C'DATE = '
CDATE    DC    CL5' '
         DC    C'  TIME = '
CTIME    DC    CL6' '
         DC    CL14' '
         DC    CL5'*'
CSEQ1    DC    CL4' '
LINE2    DS    0CL81
         DC    X'11'
CARD2    DS    0CL80
         DC    CL15' '
         DC    C'DSNAME='
CDSN     DC    CL44' '
         DC    CL10' '
CSEQ2    DC    CL4' '
TINREC   DS    0CL66
AORB     DC    C' '
TIND     DC    CL5' '
TINT     DC    CL6' '
TINV     DC    CL6' '
TINN     DC    CL44' '
TINTYPE  DC    CL4' '
SYS      DC    C'SYS'
T        DC    C'.T'
SPACES   DC    CL6' '
C2302    DC    C'2302'
C2314    DC    C'2314'
C2311    DC    C'2311'
P1       DC    P'1'
CNO      DC    PL2'1'
EDMASK   DC    X'40202120'
ALL      DC    C'ALL'
SYSUT    DC    C'**SYSUT'
         END
