*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSM42TAG.
         TITLE 'SSM42TAG - CHAINED BUFFER SORT'
         REQU
*
*        THIS MODULE IS FOR SORTING A SET OF DATA ITEMS      IN
*      BUFFERS WHICH HAVE A LINK ADDRESS IN THE FIRST FOUR BYTES OF
*      EACH BUFFER TO THE NEXT.ALL BUFFERS MUST BE ON FULL-WORD
*      BOUNDARIES AND ONLY ADDRESS INFORMATION SHOULD BE IN THE LINK
*      POINTERS AS IN THE SORT THESE FIELDS ARE CHANGED SO THAT AT
*      THE END THE BUFFERS ARE CHAINED IN THE CORRECT ORDER.
*
*      THE INPUT PARAMETERS ARE  1) ADDRESS OF BEGINING OF CHAIN
*                                2) ADDRESS OF CONTROL INFORMATION
*                  CONTROL INFORMATION (IN TWO AJACENT FULL-WORDS)
*                  1) RELATIVE DISPLACEMENT OF FIELD START IN BUFFER
*                                          INCLUDING LINK FIELD
*                  2) KEY-FIELD LENGTH    (-VE FOR DECENDING SORT)
*
*          IE  IF FIELD  IS JUST AFTER BUFFER LINK AND 8 BYTES LONG
*                          DC  F'4,8' IS THE CONTROL DATA
*
*
*        ON  EACH PASS  THE HIGHEST PROCEEDS TO THE END WHERE IT IS
*        RECHAINED TO THE HIGH Q, AND THE LOWEST IS ALSO FOUND THIS
*        IS THEN MOVED TO THE HEAD OF THE Q AND THE Q HEAD  POINTER
*        ALTERED TO POINT TO IT. THUS IN EACH PASS THE NUMBER OF
*        BUFFERS TO CHECK REDUCE BY TWO EACH PASS
*
*
*        DSECTS USED IN BASING WORK-LINKS
*
$LOW     DSECT
         USING *,R2
$LOWLINK DS    A
         USING *,R14
$QLINKS  DS    A
$Q       DSECT
         USING *,R10
$QLINK   DS    A
*
$R1      DSECT
         USING *,R11
$R1LINK  DS    A
         USING *,R6
$R1FIELD DS    C
*
$R2      DSECT
         USING *,R12
$R2LINK  DS    A
         USING *,R7
$R2FIELD DS    C
ASCEND  EQU  64
DESCEND  EQU  32
SSM42TAG ENTER ENT=TAGSORT
         L     R2,4(,R1)               LOCATE CONTROLS
         LM    R3,R4,0(R2)             PICK UP CONTROL VALUES
         LA    R5,ASCEND               ASCENDING ASSUMED
         LTR   R4,R4                   OK
         BP    TAG010                  YES
         BZ    TAGFAIL1                ERROR ZERO KEY
         LPR   R4,R4                   RESET KEY POSITVE.
         LA    R5,DESCEND              AND MARK ORDER
TAG010   BCTR  R4,0                    RESET FOR EXECUTED COMPARE
         SR    R9,R9                   SORTED Q POINTER
         LR    R14,R1
TAG020   LR    R11,R14                 LOCATE LOW CHAIN POINTER
         LR    R2,R14                  SET LOW RECORD POINTER
         SR    R8,R8                   CLEAR  SWOP MARKER
TAG030   LR    R10,R11
         L     R11,$QLINK              LOCATE  FIRST BUFFER
         LTR   R11,R11                 CHECK
         BZ    TAGFAIL2                NO LOGIC OR CHAIN FAULT
         L     R12,$R1LINK             LOCATE  NEXT BUFFER
         LTR   R12,R12                 CHECK
         BZ    TAG040                  CHAIN END
         LA     R6,$R1LINK(R3)         LOCATE KEY1
         LA     R7,$R2LINK(R3)         LOCATE KEY2
         EX    R4,TAGCOMP              COMPARE
         BE    TAG030                  SAME IGNORE
         EX    R5,TAGJUMP              TEST RESULT WILL GO TO TAG030
         LA    R8,1                    SET SWOP MARK
         ST    R12,$QLINK
         MVC   $R1LINK,$R2LINK
         ST    R11,$R2LINK
         LR    R11,R12               RESET  NEXT  Q POINTER
         LR    R6,R7
         B     TAG060
*
TAG040   LTR   R8,R8                   DID A SWOP OCCOUR
         BZ    TAG050                  NO
         ST    R9,$R1LINK              NOW REVISE END OF CHAIN
         L     R9,$QLINK               MOVING TO SORTED CHAIN
         XC    $QLINK,$QLINK           AND FIX CHAIN END
*        SHIFT  LOWEST  TO  INITIAL CHAIN
         L     R11,$LOWLINK            LOCATE LOW BUFFER
         MVC   $LOWLINK,$R1LINK        REMOVE FROM CURRENT POSN
         MVC   $R1LINK,$QLINKS         SWITCH POINTERS
         ST    R11,$QLINKS             LOWEST NOW CORRECT
         LR    R14,R11                 POINT TO CURRENT LOW
         L     R7,$R1LINK
         LTR   R7,R7               HAS LINK END BEEN REACHED
         BNZ   TAG020                  NO
TAG050   ST    R9,$R1LINK              RECONECT CHAIN
         LEAVE
TAG060   L     R7,$LOWLINK             LOCATE CURRENT PASS LOWEST
         LA    R7,$R2FIELD(R3)         THUS RECORD FIELD
         EX    R4,TAGCOMP              GOT IT
         EX    R5,TAGJUMP2             JUMPS TO TAG070 IF SUCCESFULL
         B     TAG030                  IGNORE
*
*        FIND LOWEST RECORD
*
TAG070   LR    R2,R10                  LOW LINK
         B     TAG030
TAGFAIL1 ABEND 391,DUMP
TAGFAIL2 ABEND 392,DUMP
TAGCOMP  CLC   $R1FIELD,$R2FIELD       COMPARISON
TAGJUMP  NOP   TAG060
TAGJUMP2 NOP   TAG070
         LTORG
         END
