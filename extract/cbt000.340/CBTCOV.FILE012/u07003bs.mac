         TITLE '  OSAID - O. S. CONVENTIONS                          '
OSAID    CSECT
         SAVE  (14,12),,*
         BALR  R12,R0
         USING *,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         TITLE '  OSAID - OPEN FILES AND GET A RECORD                '
         LA    R11,CARD
         OPEN  (IN,,OUT,(OUTPUT))
R201     GET   IN,CARD
         TITLE '  OSAID - INTERROGATE CARD IMAGE                     '
         CLC   CARD(2),=C'//'           JCL CARD
         BNE   MISJCLPR      NO   CTL CARD SEARCH
*                 YES - SEARCH FOR PGM NAME OR SYSPUNCH
         CLC   CARD(10),=C'//SYSPUNCH'  //SYSPUNCH CARD ?
         BE    SYSPUNCH
         LA    R10,5(0,R11)
         LA    R9,62(0,R11)
LOOP2901 EQU   *
         CLC   0(9,R10),=C'EXEC ASMS'
         BE    ASM
         CLC   0(10,R10),=C'EXEC LINKS'
         BE    LINK
         CLC   0(10,R10),=C'PGM=IEBCOP'
         BE    COPY
         CLC   0(4,R10),=C'PGM='        OTHER PROGRAM NAME ?
         BE    ZEROSWIT                 YES
         LA    R10,1(0,R10)
         CR    R9,R10
         BE    R201
         B     LOOP2901
ZEROSWIT EQU   *
         MVC   ASMSWIT(3),=X'000000'    ZERO OUT ALL SWITCHES
         B     R201
MISJCLPR EQU   *
         CLC   CARD(2),=C'/*'
         BE    R201
         CLI   LKEDSWIT,X'FF'           NO - IS IT LKED CTL CARD
         BE    LKEDRTNE                 YES
         CLI   COPYSWIT,X'FF'           NO - IS IT COPY CONTROL CARD
         BE    COPYRTNE
         B     R201
         TITLE '  OSAID - SWITCH CONTROL FOR ASMB,LKED OR COPY       '
ASM      EQU   *
         MVI   ASMSWIT,X'FF'
         MVI   LKEDSWIT,X'00'
         MVI   COPYSWIT,X'00'
         LA    R10,9(,R10)
ASM1     CLC   0(4,R10),=C'MOD='        LOOK FOR MODULE NAME
         BE    ASM2
         LA    R10,1(,R10)              KEEP LOOKING
         B     ASM1
ASM2     LA    R4,2(,R10)               LOOK FOR , DELIMITER
ASM3     CLI   0(R4),C','
         BE    ASM4
         LA    R4,1(,R4)                KEEP LOOKING
         B     ASM3
ASM4     LA   R10,4(,R10)               CALC LENGTH OF MEMBER NAME
         SR    R4,R10
         BCTR  R4,R0
         STC   R4,*+5
         MVC   OUTAREA1(0),0(R10)       MOVE MEMBER NAME
         LA    R6,R201                  SET RETURN ADDRESS
         B     OUTPUT                   WRITE OUTPUT RECORD
         B     R201
COPY     EQU   *
         MVI   COPYSWIT,X'FF'
         MVI   LKEDSWIT,X'00'
         MVI   ASMSWIT,X'00'
         B     R201
LINK     EQU   *
         MVI   LKEDSWIT,X'FF'
         MVI   COPYSWIT,X'00'
         MVI   ASMSWIT,X'00'
         B     R201
         TITLE '  OSAID - SYSPUNCH CARD PROCESSING - ASMB            '
SYSPUNCH EQU   *
         CLI   ASMSWIT,X'FF'
         BNE   R201
         MVI   CONTSWIT,X'00'           BLANK OUT CONTSWIT
         CLI   CARD+72,X'40'            CONT CARD ?
         BNE   CONT                     YES
         LA    R10,15(R11)
         LA    R9,56(R10)
LOOPCONT CLC   0(2,R10),=C', '          CONT CARD ?
         BE    CONT                     YES
         LA    R10,1(R10)
         CR    R9,R10
         BE    DSNFIND
         B     LOOPCONT
CONT     EQU   *
         OI    CONTSWIT,X'FF'
DSNFIND  EQU   *
         LA    R10,15(R11)
         LA    R9,49(R10)
LOOPDSN  CLC   0(3,R10),=C'DSN'
         BE    SEEKLEFT
         LA    R10,1(R10)
         CR    R9,R10
         BNE   LOOPDSN
         CLI   CONTSWIT,X'FF'           CONT FLAGGED
         BNE   ODDITY                   NO - STRANGE
         GET   IN,CARD                  GET A CONT CARD
         CLI   CARD+72,X'40'            MORE CONT ?
         BNE   CONT                     YES
         LA    R10,4(R11)               START OF CONT PROCESSING
         LA    R9,60(R10)               END OF CONT PROCESSING
         B     LOOPCONT
SEEKLEFT EQU   *
         LA    R10,3(R10)               SKIP PAST DSN
         LA    R9,71(R11)               END OF PROCESSING MARKER
COMPLEFT CLI   0(R10),C'('              FIND LEFT PARENTHESIS
         BE    SEEKRITE
         LA    R10,1(R10)
         CR    R9,R10
         BNE   COMPLEFT
         B     ODDITY+4
SEEKRITE EQU   *
         LA    R2,1(R10)                R2 POINTS TO FIRST CHARACTER
         LA    R10,2(R10)               SKIP PAST ( AND FIRST CHAR
COMPRITE CLI   0(R10),C')'
         BE    GOTBOTH
         LA    R10,1(R10)
         CR    R9,R10
         BE    ODDITY+8            NO RIGHT PARENT
         B     COMPRITE
GOTBOTH  EQU   *
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         B     R201
MOVE     MVC   OUTAREA1(1),0(R3)
         TITLE '  OSAID - LKED ROUTINES - CONTROL WORD SCAN          '
LKEDRTNE EQU   *
         LA    R10,1(R11)               START OF PROCESSING
         LA    R9,61(R10)               END OF PROCESSING
CONTLKED CLC   0(8,R10),=C'INCLUDE '
         BE    ITSINCLU                 YES
         CLC   0(5,R10),=C'NAME '
         BE    ITSNAME                  YES
         CLC   0(6,R10),=C'ALIAS '
         BE    ITSALIAS                 YES
         LA    R10,1(R10)               NO - IT MUST BE CHANGE OR
         CR    R9,R10                   OVERLAY - NOT NEEDED
         BE    R201                     GET A NEW CARD
         B     CONTLKED
ITSINCLU EQU   *                        INCLUDE CARD
         LA    R10,9(R10)               START OF PROCESSING
         LA    R9,CARD+69               END OF PROCESSING
         B     PROCINCL
         TITLE '  OSAID - LKED ROUTINES - NAME CARD PROCESSING       '
ITSNAME  EQU   *                        NAME CARD
         LA    R10,5(R10)               START OF PROCESSING
         LA    R9,CARD+71               END OF PROCESSING
LOOPTM   TM    0(R10),X'C0'             ALPHABETIC CHARACTER ?
         BO    SEEKR                    YES
         LA    R10,1(R10)               INCREMENT PTR
         CR    R9,R10                   END OF CARD
         BE    ODDITY+12                YES - STRANGE
         B     LOOPTM                   NO - LOOP
SEEKR    EQU   *
         LR    R2,R10                   R2 POINTS TO THE 1ST CHARACTER
         LA    R10,1(R10)
COMPBLNK CLI   0(R10),X'40'             SEARCH FOR BLANK
         BE    GOTNAME                  FOUND IT    R10 -> BLANK
         LA    R10,1(R10)               STEP UP CARD
         CR    R9,R10                   END OF CARD ?
         BE    ODDITY+16                YES - STRANGE
         B     COMPBLNK                 NO - LOOP
GOTNAME  EQU   *
         S    R10,=F'3'       ALLOW  FOR '(R)'
         CLC   0(3,R10),=C'(R)'           IS 'R' THERE ?
         BNE   MISSROUT
         LA    R6,RETURN
         B     WTRTNE
MISSROUT EQU   *
         LA    R10,3(R10)         DO NOT ALLOW FOR 'R'
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
RETURN   EQU   *
         B     R201                     & GET NEXT RECORD
         TITLE '  OSAID - LKED ROUTINES - INCLUDE CARD PROCESSING    '
PROCINCL EQU   *
         CLI   0(R10),C'('
         BE    FOUNDLFT
         LA    R10,1(R10)
         CR    R9,R10
         BE    ODDITY+20
         B     PROCINCL
FOUNDLFT EQU   *
         LA    R2,1(R10)                R2 POINTS TO CHARACTER 1
         LR    R3,R2                    SAVE R2
         LA    R10,2(R10)               START SEARCH FOR COMMA
COMPCOMA CLI   0(R10),C','
         BE    FOUNDCOM                 YES
         LA    R10,1(R10)               NO - INCREASE POINTER
         CR    R9,R10                   END OF CARD
         BE    NOCOMMA                  YES - NO COMMA
         B     COMPCOMA                 NO - LOOP
FOUNDCOM EQU   *
         LA    R8,1(R10)                R8 POINTS TO 1ST CHAR OF NEXT
         S     R10,=F'1'
         SR    R10,R2                   LENGTH FIELD
         EX   R10,MOVE
         LR    R3,R8      SET R3 TO POINT TO START OF NEXT ENTRY
         MVC   OUTAREA2,CARD+72         SEQUENCE NUMBER
         PUT   OUT,OUTAREA1
         XC    OUTAREA1,OUTAREA1        CLEAR BOTH
         XC    OUTAREA2,OUTAREA2        OUTAREAS
         LR    R2,R8                    RESTORE R2
         LA    R10,1(R8)                SET R10 TO POINT TO NEXT CHAR
         B     COMPCOMA
NOCOMMA  EQU   *
         LA    R10,1(R2)                RESET TO SEEK FOR )
FINDRITE CLI   0(R10),C')'
         BE    GOTRYTP
         LA    R10,1(R10)               NO
         CR    R9,R10                   END OF CARD ?
         BE    ODDITY+24                YES - STRANGE
         B     FINDRITE
GOTRYTP  EQU   *
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         B     R201
         TITLE '  OSAID - LKED ROUTINES - ALIAS CARD PROCESSING      '
ITSALIAS EQU   *                        ALIAS PROCESSING
         LA    R10,6(R10)               START OF PROCESSING
         LA    R9,CARD+71                 END  F PROCESSING
TESTIT   TM    0(R10),X'C0'             ALPHABETIC ?
         BO    FINDCHAR                 YES
         LA    R10,1(R10)               NO
         CR    R9,R10                   END OF PROCESSING
         BE    ODDITY+28                YES - ODD
         B     TESTIT                   NO - LOOP
FINDCHAR EQU   *
         LR    R2,R10                   SAVE START OF FIELD
         LA    R10,1(R10)               SKIP UP
SEEKCOMA CLI   0(R10),C','              COMMA ?
         BE    COMA                     YES
         LA    R10,1(R10)               SKIP UP
         CR    R9,R10                   END - OF - CARD ?
         BE    NOCOMA                   YES - NO COMMA
         B     SEEKCOMA                 NO - LOOP ROUND
COMA     EQU   *
         LR    R8,R10
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         LA    R2,1(R8)
         LA    R10,2(R2)
         B     SEEKCOMA
NOCOMA   EQU   *
         LA    R10,1(R2)
BLANK    CLI   0(R10),X'40'
         BE    BLANKET
         LA    R10,1(R10)
         CR    R9,R10
         BE    ODDITY+32
         B     BLANK
BLANKET  EQU   *
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         B     R201
         TITLE '  OSAID - COPY ROUTINE PROCESSING - NORMAL COPY      '
COPYRTNE EQU   *
         LA    R10,CARD+1
         LA    R9,CARD+53
LOOP962  EQU   *
         CLC   0(5,R10),=C'COPY '
         BE    R201
         LA    R10,1(R10)
         CR    R9,R10
         BE    CONTINUE
         B     LOOP962
CONTINUE EQU   *
         LA    R10,CARD+5
         LA    R9,CARD+71               LAST POSSIBLE LOCN OF DELIMITER
LOOP963  EQU   *
         CLI   0(R10),C'='         = SIGN ?
         BE    YESEQ               YES
         LA    R10,1(R10)          STEP UP CARD
         CR    R9,R10              END OF CARD ?
         BE ODDITY+36              YES - STRANGE
         B     LOOP963
YESEQ    EQU   *
         CLI   1(R10),C'('         MORE THAN 1 ?
         BE    FOUND103            YES
         TM    1(R10),X'C0'        ALPHAMERIC ?
         BNO   ODDITY+52           NO - STRANGE
         LA    R2,1(R10)           R2 POINTS TO 1ST CHAR
         LA    R10,2(R10)          R10 POINTS TO 2ND CHAR
LOOP1009 EQU   *
         CLI   0(R10),X'40'        END OF NAME ?
         BE    MEMBONLY            YES
         LA    R10,1(R10)          STEP UP CARD
         CR    R9,R10              END OF CARD ?
         BE    ODDITY+56           YES - STRANGE
         B     LOOP1009            NO - LOOP ROUND
MEMBONLY EQU   *
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         B     R201
FOUND103 EQU   *
         CLC   0(3,R10),=C'=(('    RENAME ?
         BE    RENAME
         LA    R2,2(R10)                SKIP PAST =(
         LA    R10,3(R10)               R10 POINTS TO START
LOOP964  CLI   0(R10),X'6B'             COMMA ?
         BE    GOTCOMMA                 YES
         LA    R10,1(R10)               NO
         CR    R9,R10                   END OF CARD ?
         BE    RIGHTP                   YES - GO TO RIGHT PAREN SEARCH
         B     LOOP964                  LOOP
GOTCOMMA EQU   *
         LA    R8,1(R10)                R8 POINTS TO NEXT
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         LR    R2,R8                    RESTORE R2 WITH NEW VALUE
         LA    R10,1(R8)                R10 POINTS AT NEW START
         B     LOOP964                  TRY AGAIN
RIGHTP   EQU   *
         LA    R10,1(R2)                R10 POINTS TO 1ST CHARACTER
LOOP965  CLI   0(R10),C')'         RIGHT PARENTHESIS ?
         BE    GOTRTP                   GOT IT
         LA    R10,1(R10)               NO - STEP UP BY ONE
         CR    R9,R10                   END OF CARD
         MVI   ASMSWIT,X'00'
         BE    ODDITY+44                YES - STRANGE
         B     LOOP965                  NO
GOTRTP   EQU   *
         LA    R8,1(R10)                POINT R8 AT NEXT CHAR
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         CLI   0(R8),X'40'              END OF CARD  ?
         BE    R201                     YES
         CLI   0(R8),C')'          END OF RENAME ?
         BE    R201                     YES
         CLI   0(R8),C','               ANOTHER MEMBER ?
         BNE   ODDITY+48
         CLI   1(R8),C'('
         BNE   MISSINST
         LA    R8,1(R8)
MISSINST EQU   *
         LA    R10,1(R8)
         LR    R2,R10
         LA    R9,CARD+71
         B     LOOP965
         TITLE '  OSAID - COPY ROUTINE PROCESSING - RENAME COPY      '
RENAME   EQU   *
         LA    R2,3(R10)           R2  POINTS TO 1ST CHAR
         LA    R10,1(R2)           R10 POINTS TO 2ND CHAR
LOOP1012 EQU   *
         CLI   0(R10),C','         COMMA ?
         BE    FIND1012            YES
         LA    R10,1(R10)          STEP UP CARD
         CR    R9,R10              END OF LIMITS ?
         BE    ODDITY+60           YES
         B     LOOP1012
FIND1012 EQU   *
         LA    R8,1(R10)           R8 POINTS TO 1ST CHAR OF NEW
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         LR    R2,R8               RELOAD R2
         LA    R10,1(R2)           R10 POINTS TO 2ND CHAR OF NEW
LOOP1013 EQU   *
         CLI   0(R10),C')'         END OF RENAME PAIR
         BE    FOUN1013            YES
         LA    R10,1(R10)          STEP UP CARD
         CR    R9,R10              END OF CARD ?
         BE    ODDITY+64           YES - STRANGE
         B     LOOP1013            LOOP
FOUN1013 EQU   *
         LA    R8,1(R10)           R8 POINTS TO CHAR AFTER )
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
*
         CLI   0(R8),C')'          END OF CARD ? ( TWO )) TOGETHER )
         BE    R201                YES
         CLI   0(R8),C','          MORE MEMBERS ?
         BNE   ODDITY+68           NO - ODD
         CLI   1(R8),C'('          ANOTHER RENAME PAIR ?
         BNE   SKIP0201            NO - SKIP
         LA    R2,2(R8)            R2 POINTS TO 1ST CHAR OF OLD
         LA    R10,1(R2)           R10 POINTS TO 2ND CHAR OF OLD
         B     LOOP1012            TRY RENAME AGAIN
SKIP0201 EQU   *
         TM    1(R8),X'C0'         ALPHAMERIC 1ST CHAR ?
         BNO   ODDITY+72           NO - STRANGE
         LA    R2,1(R8)            R2 POINTS TO 1ST CHAR
         LA    R10,1(R2)           R10 POINTS TO 2ND CHAR
LOOP5903 CLI   0(R10),C','         END OF MEMBER ?
         BE    FOUND902            YES
         LA    R10,1(R10)          NO STEP UP CARD
         CR    R9,R10              END OF CARD ;
         BE    SKIP202             YES
         B     LOOP5903            NO - LOOP ROUND
FOUND902 EQU   *
         LA    R8,1(R10)           R8 POINTS TO NEXT CHAR
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         LA    R10,1(R8)           R10 POINTS TO 2ND CHAR OF NEXT
         LR    R2,R8               R2 POINTS TO 1ST CHAR OF NEXT
         B     LOOP5903
SKIP202  EQU   *
         LA    R10,1(R2)           R10 POINTS TO 2ND CHAR OF NEXT
LOOP5904 EQU   *
         CLI   0(R10),C')'         END OF CARD ?
         BE    ENDCARD1            YES
         LA    R10,1(R10)          NO
         CR    R9,R10              END OF CARD ?
         BE    ODDITY+76           YES - STRANGE
         B     LOOP5904            NO - LOOP ROUND
ENDCARD1 EQU   *
         BAL   R6,WTRTNE  GO TO WRITE ROUTINE
         B     R201                GO READ A NEW CARD
         TITLE '  OSAID - WRITE ROUTINE'
WTRTNE   EQU   *
         S    R10,=F'1'       REDUCE FOR EXECUTE INST
         LR    R3,R2               SAVE FIRST CHAR LOCATION
         SR    R10,R2              LENGTH FOR EXECUTE INSTRUCTION
         EX   R10,MOVE
OUTPUT   MVC   OUTAREA2(8),CARD+72 MOVE CARD NUMBER
         MVC   OUTAREA2+8(4),=4C' '     ENSURE BLANKS
         PUT   OUT,OUTAREA1
         MVI   OUTAREA1,C' '            BLANK OUT OUTPUT AREAS
         MVC   OUTAREA1+1(7),OUTAREA1
         BR    R6
         TITLE '  OSAID - END OF FILE ROUTINE                        '
EOF      EQU   *
         CLOSE (IN,,OUT)
         L    R13,4(,R13)
         RETURN (14,12),RC=0
         TITLE '  OSAID - ABEND PROCESSING - ABEND BRANCH TABLE      '
ODDITY   EQU   *
         B     ABEND190
         B     ABEND191
         B     ABEND192
         B     ABEND193
         B     ABEND194
         B     ABEND195
         B     ABEND196
         B     ABEND197
         B     ABEND198
         B     ABEND199
         B     ABEND200
         B     ABEND201
         B     ABEND202
         B     ABEND203
         B     ABEND204
         B     ABEND205
         B     ABEND206
         B     ABEND207
         B     ABEND208
         B     ABEND209
         TITLE '  OSAID - ABEND PROCESSING - ABENDS                  '
ABEND190 ABEND 190,DUMP
ABEND191 ABEND 191,DUMP
ABEND192 ABEND 192,DUMP
ABEND193 ABEND 193,DUMP
ABEND194 ABEND 194,DUMP
ABEND195 ABEND 195,DUMP
ABEND196 ABEND 196,DUMP
ABEND197 ABEND 197,DUMP
ABEND198 ABEND 198,DUMP
ABEND199 ABEND 199,DUMP
ABEND200 ABEND 200,DUMP
ABEND201 ABEND 201,DUMP
ABEND202 ABEND 202,DUMP
ABEND203 EQU   *
         ABEND 203,DUMP
ABEND204 EQU   *
         ABEND 204,DUMP
ABEND205 EQU   *
         ABEND 205,DUMP
ABEND206 EQU   *
         ABEND 206,DUMP
ABEND207 EQU   *
         ABEND 207,DUMP
ABEND208 EQU   *
         ABEND 208,DUMP
ABEND209 EQU   *
         ABEND 209,DUMP
         TITLE '  OSAID - CONSTANTS, DCBS ETC                        '
SAVE     DC    18A(0)
CARD     DS    CL80
OUTAREA1 DC    CL8'        '
OUTAREA2 DC    CL8' '
         DC    4C' '
ASMSWIT  DC    X'00'
LKEDSWIT DC    X'00'
COPYSWIT DC    X'00'
IN       DCB   DDNAME=SYSIN,MACRF=GM,EODAD=EOF,DSORG=PS
OUT      DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=20,MACRF=PM,RECFM=FB,    *
               BLKSIZE=1600
CONTSWIT DC    X'00'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
         END
