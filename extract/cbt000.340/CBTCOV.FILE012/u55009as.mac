*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSU13TPL.
TAPLAB   CSECT
         SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         LA    9,SAVE
         ST    13,4(9)
         ST    9,8(13)
         LR    13,9
         OPEN  (PRINT,(OUTPUT),TAPE,(INPUT))
         BAL   2,SKIPTO1
         READ  TAPONE,SF,TAPE,VOL1AR,'S'
         CHECK TAPONE
         CLC   VOL1AR(4),=C'VOL1'
         BNE   TRYANSI
ANSIN    MVC   PRTAR+26(80),VOL1AR
         BAL   2,PUTRTN
         READ  TAPTWO,SF,TAPE,HDR1AR,'S'
         CHECK TAPTWO
         CLI   AFLG,X'FF'
         BNE   TRYHD
         XLATE HDR1AR,80
TRYHD    CLC   HDR1AR(4),=C'HDR1'
         BNE   TRYHDR0
         CLC   HDR1AR+4(76),=76C'0'
         BE    IEHINIT
         MVC   PRTAR+26(80),HDR1AR
         BAL   2,PUTRTN
A        READ  TAPEND,SF,TAPE,HDR2AR,'S'
         CHECK TAPEND
         CLI   AFLG,X'FF'
         BNE   TRYHD2
         XLATE HDR2AR,80
TRYHD2   CLC   HDR2AR(4),=C'HDR2'
         BNE   NOHDR2
         MVC   PRTAR+26(80),HDR2AR
         BAL   2,PUTRTN
         B     USER
NOLAB    MVI   PRTA,X'09'
         MVC   PRTAR+41(50),ASTERIX
         BAL   2,PUTRTN
         MVC   PRTAR+41(50),MSG1
         BAL   2,PUTRTN
         MVC   PRTAR+41(50),ASTERIX
         BAL   2,PUTRTN
         B     EOJ
TRYANSI  CLC   VOL1AR(4),=X'564F4C31'
         BNE   NOLAB
         XLATE VOL1AR,80
         MVI   AFLG,X'FF'
         B     ANSIN
TRYHDR0  CLC   HDR1AR(4),=C'HDR0'
         BE    DEBINIT
         MVC   PRTAR+20(22),MSG2
         BAL   2,PUTRTN
         B     A
IEHINIT  MVI   PRTA,X'19'
         BAL   2,PUTRTN
         MVC   PRTAR+26(80),ASTERIX
         BAL   2,PUTRTN
         MVC   PRTAR+26(80),MSG4
         MVC   PRTAR+98(6),VOL1AR+4
         BAL   2,PUTRTN
         MVC   PRTAR+26(80),ASTERIX
         BAL   2,PUTRTN
         B     EOJ
NOHDR2   MVC   PRTAR+20(22),MSG5
         BAL   2,PUTRTN
         B     VOLEX
DEBINIT  MVI   PRTA,X'19'
         BAL   2,PUTRTN
         MVC   PRTAR+27(78),ASTERIX
         BAL   2,PUTRTN
         MVC   PRTAR+27(78),MSG3
         MVC   PRTAR+97(6),VOL1AR+4
         BAL   2,PUTRTN
         MVC   PRTAR+27(78),ASTERIX
         BAL   2,PUTRTN
         B     EOJ
PUTRTN   PUT   PRINT,PRTA
         MVI   PRTA,X'09'
         MVI   PRTAR,C' '
         MVC   PRTAR+1(131),PRTAR
         BR    2
SKIPTO1  MVI   PRTA,X'8B'
         MVI   PRTAR,C' '
         MVC   PRTAR+1(131),PRTAR
         PUT   PRINT,PRTA
         MVI   PRTA,X'09'
         BR    2
EOJ      CLOSE (PRINT,,TAPE)
         L     13,SAVE+4
         RETURN (14,12),,RC=0
VOLEX    EQU   *
         MVI   PRTA,X'19'
         BAL   2,PUTRTN
         CLI   AFLG,X'FF'
         BNE   IBMSTD
         MVC   PRTAR+56(20),=C'***  A  N  S  I  ***'
         MVI   PRTA,X'19'
         BAL   2,PUTRTN
IBMSTD   MVC   PRTAR+41(50),ASTERIX
         BAL   2,PUTRTN
         MVC   MSGA+31(6),VOL1AR+4
         MVC   PRTAR+41(50),MSGA
         BAL   2,PUTRTN
         MVC   MSGB+31(17),HDR1AR+4
         MVC   PRTAR+41(50),MSGB
         BAL   2,PUTRTN
         MVC   MSGC+31(1),HDR2AR+4
         CLI   HDR2AR+38,C'B'
         BNE   ISITS
         MVI   MSGC+32,C'B'
         B     RECMOVE
ISITS    CLI   HDR2AR+38,C'S'
         BNE   ISITR
         MVI   MSGC+32,C'S'
         B     RECMOVE
ISITR    CLI   HDR2AR+38,C'R'
         BNE   RECMOVE
         MVC   MSGC+32(2),=C'BS'
RECMOVE  EQU   *
         MVC   PRTAR+41(50),MSGC
         BAL   2,PUTRTN
         MVC   MSGD+31(5),HDR2AR+10
         MVC   PRTAR+41(50),MSGD
         BAL   2,PUTRTN
         MVC   MSGE+31(5),HDR2AR+5
         MVC   PRTAR+41(50),MSGE
         BAL   2,PUTRTN
         CLI   HDR2AR+15,C'0'
         BNE   TRY1
         MVC   MSGF+31(3),=C'200'
         B     NEXT
TRY1     CLI   HDR2AR+15,C'1'
         BNE   TRY2
         MVC   MSGF+31(3),=C'556'
         B     NEXT
TRY2     CLI   HDR2AR+15,C'2'
         BNE   TRY3
         MVC   MSGF+31(3),=C'800'
         B     NEXT
TRY3     CLI   HDR2AR+15,C'3'
         BNE   UNKNOWN
         MVC   MSGF+31(4),=C'1600'
         B     NEXT
UNKNOWN  MVC   MSGF+31(10),=C'UNKNOWN   '
NEXT     MVC   PRTAR+41(50),MSGF
         BAL   2,PUTRTN
         MVC   MSGG+31(2),HDR1AR+42
         MVC   MSGG+34(3),HDR1AR+44
         MVC   PRTAR+41(50),MSGG
         BAL   2,PUTRTN
         MVC   MSGH+31(2),HDR1AR+48
         MVC   MSGH+34(3),HDR1AR+50
         MVC   PRTAR+41(50),MSGH
         BAL   2,PUTRTN
         CLI   AFLG,X'FF'
         BNE   *+8
         MVI   *+5,X'40'
         CLI   HDR1AR+53,C'0'
         BE    PUTIT
         MVC   MSGI+31(3),=C'YES'
PUTIT    MVC   PRTAR+41(50),MSGI
         BAL   2,PUTRTN            /
         MVC   MSGJ+31(2),HDR2AR+34
         MVC   PRTAR+41(50),MSGJ
         BAL   2,PUTRTN
         MVC   MSGK+31(8),HDR2AR+17
         MVC   PRTAR+41(50),MSGK
         BAL   2,PUTRTN
         MVC   MSGL+31(8),HDR2AR+26
         MVC   PRTAR+41(50),MSGL
         BAL   2,PUTRTN
         MVC   MSGM+31(4),HDR1AR+27
         MVC   PRTAR+41(50),MSGM
         BAL   2,PUTRTN
         CLI   AFLG,X'FF'
         BNE   *+14
         MVC   MSGN+31(14),VOL1AR+37
         B     *+10
         MVC   MSGN+31(10),VOL1AR+41
         MVC   PRTAR+41(50),MSGN
         BAL   2,PUTRTN
         MVC   MSGO+31(6),HDR1AR+54
         MVC   PRTAR+41(50),MSGO
         BAL   2,PUTRTN
         MVC   PRTAR+41(50),ASTERIX
         BAL   2,PUTRTN
         B     EOJ
TMARK    MVC   PRTAR+20(29),MSG6
         BAL   2,PUTRTN
         B     EOJ
USER     MVC   TAPE+33(3),=AL3(VOLEX)
B        READ  UHL,SF,TAPE,UHLAR,'S'
         CHECK UHL
         CLI   AFLG,X'FF'
         BNE   GOPRT9
         XLATE UHLAR,80
GOPRT9   MVC   PRTAR+26(80),UHLAR
         BAL   2,PUTRTN
         B     B
*
*   CONSTANTS AND LITERALS
*
SAVE     DS    18F
VOL1AR   DC    CL80' '
HDR1AR   DC    CL80' '
HDR2AR   DC    CL80' '
UHLAR    DC    CL80' '
AFLG     DC    X'00'
PRTA     DC    XL1'00'
PRTAR    DC    CL132' '
ASTERIX  DC    80C'*'
MSG1     DC    C'*        NON-STANDARD OR NO LABEL ON TAPE        *'
MSG2     DC    C'*** NO HDR1 RECORD ***'
MSG3     DC    C'* TAPE HAS JUST BEEN INITIALLISED BY OSDEBE - VOLUME SX
               ERIAL NUMBER IS        *'
MSG4     DC    C'* TAPE HAS JUST BEEN INITIALLISED BY IEHINITT - VOLUMEX
                SERIAL NUMBER IS        *'
MSG5     DC    C'*** NO HDR2 RECORD ***'
MSG6     DC    C'***  END OF FILE ON TAPE  ***'
MSGA     DC    C'* VOLUME SERIAL NUMBER                           *'
MSGB     DC    C'* DATA-SET NAME                                  *'
MSGC     DC    C'* RECORD FORMAT                                  *'
MSGD     DC    C'* RECORD LENGTH                                  *'
MSGE     DC    C'* BLOCK SIZE                                     *'
MSGF     DC    C'* RECORDING DENSITY                 BPI          *'
MSGG     DC    C'* CREATION DATE                  /               *'
MSGH     DC    C'* EXPIRY DATE                    /               *'
MSGI     DC    C'* PASSWORD PROTECTED?          NO                *'
MSGJ     DC    C'* RECORDING TECHNIQUE                            *'
MSGK     DC    C'* CREATION JOB NAME                              *'
MSGL     DC    C'* CREATION STEP NAME                             *'
MSGM     DC    C'* VOLUME SEQUENCE NUMBER                         *'
MSGN     DC    C'* OWNERS NAME                                    *'
MSGO     DC    C'* BLOCK COUNT                                    *'
PRINT    DCB   DDNAME=SYSPRINT,LRECL=133,RECFM=FM,BLKSIZE=133,MACRF=PM,X
               DSORG=PS
TAPE     DCB   DDNAME=SYSUT1,LRECL=80,BLKSIZE=80,RECFM=F,MACRF=R,DSORG=X
               PS,DEVD=TA,SYNAD=SYNEX,EODAD=TMARK
SYNEX    EQU   *
         USING *,15
         LR    8,0
         L     7,16(8)
         TM    13(7),X'40'
         BO    NOLAB
         BR    14
         END   TAPLAB
