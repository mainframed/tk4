CTCH  TITLE 'CATCH: ROUTINE TO MONITOR MODULES LOADED INTO CORE '
CATCH    CSECT
         STM   14,12,12(13)
         BALR  11,0
         USING *,11
         USING FNCHTRP,12
         CNOP  0,4
         L     12,*+8         ADDRESS OF FNCHTRP
         B     *+8
         DC    A(FNCHTRP)
         LA    9,SAVE
         ST    9,8(0,13)
         ST    13,4(0,9)
         LR    13,9
         OPEN  (CATCHOUT,OUTPUT)
         L     0,=F'6600'     2*BLKSIZE
         GETMAIN R,LV=(0)
         ST    1,BUF1         A(BUF1)
         ST    1,CURR         CURRENT POS
         A     1,=F'3290'     A(LAST REC IN BUF1)
         ST    1,ENPB
         ST    1,BUF1E
         A     1,=F'10'       A(BUF2)
         ST    1,BUF2
         A     1,=F'3290'     A(LAST REC IN BUF2)
         ST    1,BUF2E
         LA    15,SYNCH       ADDR OF SYNCH EXIT RTN
         SVC   12             ISSUE SYNCH SVC
         L     8,16
         L     8,132(8)       PGM FETCH RTN
*NOTE  IF PCI FETCH IS IN THE SYSTEM, CHANGE NEXT INSTRUCTION TO
*        SH    8,H54
         SH    8,H30                       -30
         ST    8,FINCH        SAVE ADDR
         MVC   CODESV,0(8)    SAVE FINCH CODE
         MVC   0(12,8),CODE
         WTO   'CATCH3 ANALYSIS NOW STARTED'
         WTOR  'TO STOP CATCH3 REPLY NOMAN AT ANY TIME',               *
               REPLY,5,WECB
         TIME  DEC            OBTAIN START TIME
         ST    0,STRT         SAVE IT
         L     5,BUF1         R5-> FIRST REC TO BE WRITTEN
         B     WAIT           NOTHING TO DO NOW BUT WAIT
         CNOP  0,4
         DROP  12
         USING FNCHTRP,15
FNCHTRP  SSM   ZERO           FINCH EXIT RTN-NO INTERRUPTS
         STM   0,14,SAVE15    SAVE REGS
         B     CONT           BRANCH ROUND SAVE AREA
SAVE15   DS    15F            SAVE AREA FOR EXIT RTN
ZERO     DC    H'0'
CONT     LR    12,15          SET UP BASE REG
         DROP  15
         USING FNCHTRP,12
         L     8,CURR         ADDR NEXT REC
         C     4,TCB          IS IT OUR REQUEST ?
         BE    L278
         TM    BECB,64        IS BECB POSTED
         BNO   L116           -NO FILL BUFFER
         CLC   CURR,ENPB
         BL    L116           -RESET SO FILL BUFF
         AP    NUM,=PL1'1'    COUNT AS LOST
         B     L274
L116     MVC   0(1,8),BECB
         L     6,8(1)
         MVC   1(8,8),0(10)   PGM NAME FROM RB
         L     7,16           CVT
         CLC   85(3,7),1(1)   IS IT FROM SVCLIB ?
         BNE   NOTSVC
         MVI   9(8),3         SET SVC IND
         MVC   WRK,10(10)     XRBSTAB BYTE 1
         NI    WRK,240
         CLI   WRK,192        TYPE 3 OR 4 BEING LOADED
         BE    L186
         CLI   0(6),C'I'
         BNE   L186
         MVC   1(8,8),0(6)    MOVE MODULE NAME
         B     L206
L186     MVC   1(3,8),=C'IGC' FILL IN NAME FOR SVC
         OI    4(8),240
         B     L206
NOTSVC   MVC   9(1,8),12(6)   MOVE IN LIBRARY IND
L206     LA    8,10(8)        UP POINTER
         C     8,ENPB         BUFF FULL
         BNH   L274
         POST  BECB,0         POST BUFFER FULL ECB
         TM    BUFSW,1        BUF1 OR BUF2
         BO    BUF2W
         MVI   BUFSW,1
         MVC   CURR,BUF2      SWITCH ADDR.
         MVC   ENPB,BUF2E
         B     L278
BUF2W    MVI   BUFSW,0
         MVC   CURR,BUF1
         MVC   ENPB,BUF1E
         B     L278
L274     ST    8,CURR
L278     L     15,FINCH       FINCH ADDR
         LM    0,14,12(12)    RESTORE REGS
         LR    2,4            EXECUTE REMOVED FINCH CODE
         LA    4,4(15)
         LR    5,1
         LM    7,9,0(5)
         LA    13,1
         SSM   5(15)          RESTORE SYSTEM MASK
         B     14(15)         RETURN TO FINCH
CHAT     OC    REP,=CL5' '    CONVERT TO UPPER CASE
         CLC   REP,=C'NOMAN'  CHECK FOR END REPLY
         BE    ENDJ           END OF JOB
         XC    WECB,WECB      CLEAR ECB
         WTOR  'CATCH3: INVALID REPLY. REPLY ''NOMAN'' TO STOP CATCH3',*
               REPLY,5,WECB
         MVI   BECB,0         CLEAR ECB
WAIT     WAIT  1,ECBLIST=ECBL SIT BACK WITH FEET UP
         TM    WECB,64        IS IT OPERATOR CHATTING ?
         BO    CHAT           -YES
         BAL   8,WRTN         WRITE BUFFER
         LA    8,0
         ST    8,BECB
         B     WAIT           GO BACK TO SLEEP
WRTN     WRITE DECB,SF,CATCHOUT,(5),'S'
         CHECK DECB           WAIT FOR COMPLETION
         TM    BUFSW,1        WHICH BUFF WAS WRIT
         BO    BUF2N          BUF1 WRIT-BUF2 NEXT
         L     5,BUF1
         BR    8              RETURN
BUF2N    L     5,BUF2
         BR    8              RETURN
SYNAD    WTO   'CATCH3 TERMINATED: BSAM WRITE ERROR'
         B     ENDJ
ENDJ     L     6,FINCH        RESTORE FINCH CODE
         MVC   0(14,6),CODESV
         TIME  DEC            TIME OF END OF ANALYSIS
         ST    0,ENDT
         ST    1,DATE         DATE OF ANALYSIS
         WTO   'CATCH3 ANALYSIS NOW STOPPED'
         L     7,ENPB
         L     6,CURR
         TM    BECB,64        REC TO BE WRIT ?
         BZ    *+8            -NO
         BAL   8,WRTN
         CR    6,7            LAST REC ?
         BL    L658           -YES
         MVI   0(6),X'88'     INDICATE TIME IN NEXT REC
         BAL   8,WRTN         WRITE REC
         LR    6,5
L658     MVC   0(16,6),EOJ    MOVE IN DATE & TIMES
         BAL   8,WRTN         WRITE LAST REC
         CLOSE CATCHOUT
         L     13,SAVE+4
         RETURN (14,12)
SYNCH    ST    3,SV3          SAVE R3
         L     3,16           CVT
H54      DC    H'54'
         L     3,0(3)         CVTTCB
         L     3,4(3)         OUR TCB
         ST    3,TCB
         L     3,0(3)         RB ADDR
         L     3,28(0,3)      RBLINK
         MVI   17(3),4        SET OPSW TO PK 0 & SUPV STATUS
         L     3,SV3          RESTORE R3
         BR    14             RETURN
SAVE     DS    18F            SAVE AREA
H30      DC    H'30'
FINCH    DC    A(0)           ADDR(FINCH RTN)
CODE     L     15,8(0,15)     CODE INSERTED INTO FINCH
         BR    15              -------//----
         DC    A(FNCHTRP)
CODESV   DC    CL14' '        AREA TO SAVE FINCH CODE
DUM      DS    CL5
EOJ      DC    X'99'          EOJ IND IN ENTRY
STRT     DC    F'0'           START TIME
ENDT     DC    F'0'           END   TIME
DATE     DC    F'0'           DATE OF RUN
NUM      DC    PL3'0'         NO. OF ENTRIES LOST
CURR     DC    F'0'           POSN OF CURRENT ENTRY
ENPB     DC    F'0'           A(LAST ENTRY IN CURRENT BUFFER)
BUF1     DC    F'0'           A(BUF1)
BUF1E    DC    F'0'           A(LAST ENTRY IN BUF1)
BUF2     DC    F'0'           A(BUF2)
BUF2E    DC    F'0'           A(LAST ENTRY IN BUF2)
TCB      DC    F'0'           A(OUT TCB)
ECBL     DC    A(BECB),X'80',AL3(WECB)  ECBLIST
WECB     DC    F'0'           WTOR ECB
BECB     DC    F'0'           BUF WRITE ECB
BUFSW    DC    X'00'          IND  0=BUF1,1=BUF2
WRK      DC    X'00'          SPARE
REPLY    EQU   *
REP      DC    CL5' '         REPLY AREA
SV3      DC    F'0'           AREA TO SAVE R3
CATCHOUT DCB   DSORG=PS,MACRF=(R,W),SYNAD=SYNAD,BLKSIZE=3300,RECFM=F,  *
               DDNAME=CATCHOUT
         LTORG
         END
