*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSM18SOR.
Y        DSECT
         USING *,10
REC1     DS    C                        NAME 1ST SORT REC
X        DSECT
         USING *,11
REC2     DS    C                        NAME 2ND SORT REC
*
*   CORE SORT  MODULE  HAS TWO PARAMETERS
*                        ADDRESS OF CORE TABLE
*                        AND ADDRESS OF CONTROL WORDS
*              CONTROL WORDS   IN FULL WORDS BINARY NOS
*                   1)   ENTRY COUNT
*                   2)   RECORD LENGTH
*                   3)   KEY LENGTH
*                   4)   START POSITION OF KEY
*        SORT ORDER ASSUMED ACCENDING  UNLESS  THE NEXT CHARACTER IS
*              A  'D'
SSM18SOR CSECT
SORTMOD  EQU   *
         ENTRY SORTMOD
         USING *,15
         SAVE  (14,12),,SORTMOD         SAVE 14 - 12
         ST    13,SAVE13                SAVE R13
         LM    12,13,0(1)               PICK UP PARM ADDRX
         LM    0,3,0(13)                PICK UP CONTROL FIELDS
         CH    0,=H'2'                  ANY RECORDS TO SORT
         BL    SORT005                 NON   EXIT
         BCTR  3,0                      SET KEY DISP
         BCTR  2,0                      SET KEY LENGTH
         NC    COMPINST,=X'FF00F000F000' RESET COMPARE
         MVI   TEST1+1,X'D0'              AND BRANCH INSTRUCTION
         STH   3,COMPSET+2              **
         STH   3,COMPSET+4              * SET UP COMPARAND
         STC   2,COMPSET+1              *            INSTRUCTION
         OC    COMPINST,COMPSET         **
         CLI   16(13),C'D'              DECENDING ORDER
         BNE   SORT001                  NO.
         MVI   TEST1+1,X'B0'            SET DECENDING ORDER TEST
SORT001  LH    4,=H'-1'
         AR    4,1                      CALCULATE EX MOVE LENGTH
         BM    SORT006                  NO  INPUT COUNT OR IT'S WRONG
         LR    2,1                      SAVE RECORD LENGTH
         MR    0,0                      CALCULATE TABLE LENGTH
         LA    9,0(1,12)                R9 NOW POINTS TO END OF TABLE
*
         L     6,0(0,13)                CALCULATE
         SLL   6,1                           NO OF COMPARES PER PASS
SORT002  LR    10,12                    REC1 START
         LA    11,0(2,12)               REC2 START
         LR    3,9                      TABLE END
         SR    5,5                      CLEAR SWITCHED REC IND.
         LR    7,6                      SET LOOP COUNT
         SH    6,=H'2'                    REDUCE COMPARE COUNT
         CR    9,11                    NEAR END
         BNE   SORT003                 NO
         AR    3,2                     SHIFT END
SORT003  BCT   7,COMPINST               CHECK ON COMPARE COUNT
         B     SORT006                  ERROR IN LOGIC
COMPINST CLC   REC1,REC2                TEST
TEST1    BNH   SORT004                  NO SWITCHING
         EX    4,SHIFT1                 *
         EX    4,SHIFT2                 * SWITCH RECORDS
         EX    4,SHIFT1                 *
         LA    5,1                      SET INDICATOR
SORT004  AR    10,2                     CHANGE
         AR    11,2                            RECORD POINTERS
         CR    11,3                     UP TO END
         BNE   SORT003                  NO CHECK NEXT PAIR
         LR    3,12                     RESET END POINT
         LCR   2,2                      INVERT SIGN OF DIRECTION
         BM    SORT004                 RESORT BACKWARDS
         AR    12,2      --             UP START LIMIT
         SR    9,2                      REDUCE END LIMIT
         LTR   5,5                      ANY SWITCH
         BZ    SORT005                  NO  EXIT
         CR    12,9                     PAST END
         BL    SORT002
*
*  SORT PHASE  COMPLETE EXIT
*
SORT005  L     13,SAVE13                PICK UP OLD 13
         RETURN (14,12),RC=0            AND RETURN TO CALLER
SORT006  ABEND 7,DUMP                   EXECEED LOGIG WRONG
         LTORG
SAVE13   DS    F                        SAVE AREA FOR R 13
COMPSET  DC    3H'0'                    HOLDS SKELETON DISPS.
SHIFT1   XC    REC1,REC2                USED FOR SWITCHING
SHIFT2   XC    REC2,REC1                         TWO RECORDS
         END
