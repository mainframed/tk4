C      IDENTIFICATION DIVISION.
C      PROGRAM-ID. SSU45PLT.
C     PROGRAM TO TEST LP GRAPH SUBROUTINES
C
      DIMENSION SUBT(136)
      DIMENSION IPAGE(1326),TITLES(44),MODE(6),XYREF(4),X(200),Y(200)
      EQUIVALENCE  (N,NPTS)
      DATA IHHIST/'H   '/,IVHIST/'V   '/
    7 READ (1,100,END=99) IPRECL,TITLES
C     IPRECL = 0 IF I/P FROM CARDS ELSE ASSUME PRE-CALC IN BINARY/FP
  100 FORMAT (I1,T7,15A4/T7,15A4/T7,7A4,T40,7A4)
    6 IF(IPRECL.NE.0)GO TO 8
      READ(1,101)IFIRST,NPTS,M1,M2,ILAST,IPLOT,IHIST
  101 FORMAT(I1,T7,I3,T20,I2,2X,I2,T40,I1,T47,A4,A1)
    9 IPLOT= IPLOT - 1077952512
      JHIST=0
      IF(IHIST.EQ.IHHIST)JHIST=-1
      IF(IHIST.EQ.IVHIST)JHIST=1
C     TEST FOR OUTPUT REQD. ON NEW PAGE.
      IF ( IFIRST )   3,3,1
    1 MODE(1) = M1
      MODE(2) = M2
      IFLAG = 0
      GO TO 4
    3 IF ( NPTS )   99,99,2
    2 IFLAG = -1
    4 IF(IPRECL)11,10,11
   10 READ(1,102)  (X(I),Y(I),I = 1,NPTS )
  102 FORMAT (T7,E9.2,2X,E9.2,2X,E9.2,2X,E9.2,2X,E9.2,2X,E9.2 )
   12 CALL XYLP(IPAGE,X,Y,N,MODE,XYREF,IPLOT,JHIST,IFLAG)
      IF ( ILAST )   5,6,5
    5 CALL PLOTLP(TITLES,IPAGE,MODE,XYREF )
      IF (ILAST-2)7,97,99
C     READ 8 CARDS FOR SUB-TITLES
   97 READ (1,104,END=99)SUBT
      J=1
      DO 98 I=1,4
      K=J+33
      WRITE (3,301)(SUBT(L),L=J,K)
   98 J=K+1
      GO TO 7
   99 CALL EXIT
  104 FORMAT(20A4/14A4)
  301 FORMAT(33A4,A1)
    8 READ(1,103)IFIRST,NPTS,M1,M2,ILAST,IPLOT,IHIST
  103 FORMAT (20A4)
      GO TO 9
   11 READ (1,103) (X(I),Y(I),I=1,NPTS)
      GO TO 12
      END
      SUBROUTINE XYLP(IPAGE,X,Y,N,MODE,XYREF,NCHAR,JHIST,IFLAG)
      DIMENSION IPAGE(1),X(1),Y(1),MODE(6),XYREF(4)
C
C     XYLP  PROVIDES A CRUDE GRAPHICAL REPRESENTATION OF A FUNCTION BY
C     SETTING APPROPRIATE CHARACTERS IN AN ARRAY FOR PRINTING.
C
C     DESCRIPTION OF PARAMETERS--
C     IPAGE             1326 WORD ARRAY OF CHARACTERS FOR OUTPUT.
C     X,Y               2 N-WORD ARRAYS OF CO-ORDS. OF POINTS.
C     N                 NUMBER OF POINTS TO BE PLOTTED.
C     MODE              MODE(1) IS 0 FOR NORMAL SCALE,1 FOR LOG.,X-AXIS.
C                       MODE(2) IS 0 FOR NORMAL SCALE,1 FOR LOG.,Y-AXIS.
C                       MODE(3) IS THE ROW NUMBER OF X-AXIS (1-51).
C                       MODE(4) IS THE COL.NUMBER OF Y-AXIS (1-101).
C                       MODE(5) IS NO. OF SPACES IN X DIR FOR X 10 (LOG
C                       MODE(6)                     Y
C     XYREF             IF MODE(1)=0,XYREF(1) IS STEP VALUE REPRESENTED
C                       BY 1 SPACE IN X DIRECTION,OTHERWISE LOWEST X VAL
C                       IF MODE(2)=0,XYREF(2) IS STEP VALUE REPRESENTED
C                       BY 1 SPACE IN Y DIRECTION,OTHERWISE LOWEST Y VAL
C                       XYREF(3) IS FACTOR FOR 10 SP IN X DIR (LOG)
C                       XYREF(4) IS FACTOR FOR 10       Y
C     NCHAR             BINARY VALUE OF EBCDIC CHARACTER FOR PLOTTING.
C     JHIST             -1 IF HORIZ. SHADING IE BAR CHART
C                       +1 IF VERT   SHADING IE HISTOGRAM
C                       0  IF POINT PLOT ONLY
C     IFLAG             ON INPUT-- 0 IF AXES NOT CALC.,ELSE -1.
C                       ON OUTPUT- NO. OF POINTS PLACED,OR -1 IF ERROR.
C
C
C
C
      DIMENSION NRC(2),NRCLIM(4),XYPMAX(2),XYNMIN(2),TEST(2)
      DIMENSION JTAB(9)
      EQUIVALENCE (NY,NRC(1)) , (NX,NRC(2))
      DATA JTAB/1,3*2,5*5/
      IF (N)   99,99,1
C     NOW SET UP LIMIT VALUES FOR ROW & COLUMN NUMBERS.
    1 NRCLIM(1) = 1
      NRCLIM(2) = 1
      NRCLIM(3) = 51
      NRCLIM(4) = 101
      IF (IFLAG)   25,26,26
C     AXES ALREADY PLACED SO FILL IN POINTS.
   25 IFLAG = 0
      XSTEP = XYREF(1)
      YSTEP = XYREF(2)
C     LOOP FOR POINTS.
      DO 19 I = 1,N
      IF ( MODE(1) )   50,50,51
   51 NX=MODE(4)+NINT(XYREF(3)*ALOG10(X(I)/XSTEP))
      GO TO 52
C     NORMAL MODE X-AXIS.
   50 NX = MODE(4) + NINT( X(I)/XSTEP )
   52 IF ( MODE( 2 ) )   54,54,53
   53 NY=MODE(3)-NINT(XYREF(4)*ALOG10(Y(I)/YSTEP))
      GO TO 55
C
C     NOW CHECK FOR POINT IN RANGE.
C
   54 NY = MODE( 3 ) - NINT(Y( I )/YSTEP )
   55 DO 20 J = 1,2
      M = NRC( J )
      IF ( M - NRCLIM( J ) )   19,21,21
   21 IF ( M - NRCLIM( J + 2 ) )   20,20,19
   20 CONTINUE
C     PUT POINT IN PLACE.
      CALL SETCH( IPAGE, NX + ( NY - 1 )*104, NCHAR )
      IFLAG = IFLAG + 1
C     TEST IF BAR CHART/HISTOGRAM REQUIRED
      IF(JHIST)60,19,61
   60 IF(NX-MODE(4))63,19,64
C     POINT TO LEFT OF Y-AXIS
   63 IRC=MODE(4)+104*(NY-1)
   69 NX=NX+104*(NY-1)+1
      NY=1
C     NOW LOOP AND SET VERT OR HORIZ SET OF POINTS
   65 IF(NX.GT.IRC)GO TO 19
      CALL SETCH(IPAGE,NX,NCHAR)
      NX=NX+NY
      GO TO 65
C     POINT TO RIGHT OF Y-AXIS
   64 IRC=NX-1+104*(NY-1)
      NX=MODE(4)-1
      GO TO 69
   61 IF(NY-MODE(3))66,19,67
C     POINT ABOVE X-AXIS
   66 IRC=NX+104*(MODE(3)-1)
   68 NX=104*NY+NX
      NY=104
      GO TO 65
C     POINT LIES BELOW X-AXIS
   67 IRC=NX+104*(NY-2)
      NY=MODE(3)-1
      GO TO 68
   19 CONTINUE
   99 RETURN
   26 SMALL=-1.E76
C
C     NOW CALCULATE LARGEST +VE AND SMALLEST -VE X&Y VALUES.
C
      DO 2 I = 1,2
      XYPMAX( I ) = SMALL
    2 XYNMIN(I)=-SMALL
      DO 4 I = 1,N
      TEST( 1 ) = X( I )
      TEST( 2 ) = Y( I )
      DO 4 J = 1,2
      T = TEST( J )
      IF ( T - XYPMAX( J ) )   5,4,7
    7 XYPMAX( J ) = T
    5 IF ( T - XYNMIN(J ) )   8,4,4
    8 XYNMIN( J ) = T
    4 CONTINUE
C
C     NOW CALCULATE SCALING FOR X&Y AXES.
C     ALLOWABLE SCALING FOR NORMAL MODE IS ONE SPACE REPRESENTING
C     I=10**J WHERE 10 .LE. I .LE. 95 AND I=0 (MOD 5)
C     FOR LOG. MODE A MULTIPLE OR DIVISOR OF 10 SPACES REPRESENT
C     A FACTOR OF A POWER OF 10
C
      DO 9 I = 1,2
      IRC = 5 - I
      IF ( MODE( I ) )   10,10,11
C     NORMAL MODE.
   10 U = (XYPMAX(I)-XYNMIN(I))/( NRCLIM(IRC)-NRCLIM(IRC-2) )
      V=U
      IF(V.LT.1.E-75)V=XYPMAX(I)
      IF(V.LT.1.E-75)V=10.
      V=ALOG10(V)
      J = V
      IF ( V )   12,13,13
   12 J = J - 1
   13 V=10.0**(J-1)
      U = U/V
      K=U+.91
      K=5*((K+4)/5)
   15 XYREF( I ) = K*V
      GO TO 9
C     LOGARITHMIC MODE.
   11 U=XYNMIN(I)
      IF(U.LE.1.E-75)GO TO 99
      U=ALOG10(U)
      IF(U.LT.0.0)U=U-.99
      J=U+.005
   18 U=10.0**J
   17 XYREF(I)=U
      J=ALOG10(XYPMAX(I))+.9-J
      IF(J.LE.0)J=1
      J=(NRCLIM(IRC)-NRCLIM(IRC-2))/J
      IF(J.LT.1)J=1
      IF(J.GT.10)J=10*(J/10)
      IF(J.LT.10)J=JTAB(J)
      MODE(I+4)=J
      XYREF(I+2)=J
    9 CONTINUE
C
C     NOW CALCULATE POSITION OF AXES.
C
      IF ( MODE( 1 ) )   28,28,29
C     NORMAL MODE X-AXIS.
   28 MODE(4) =
     1     NINT((-NRCLIM(4)*XYNMIN(1)+XYPMAX(1))/(XYPMAX(1)-XYNMIN(1)))
   37 IF ( MODE( 2 ) )   30,30,31
C     NORMAL MODE Y-AXIS.
   30 MODE(3) = NINT( NRCLIM(3) + 1
     1   -(  -NRCLIM(3)*XYNMIN(2)+XYPMAX(2))/(XYPMAX(2)-XYNMIN(2)) )
C
C     TEST FOR AXES IN RANGE.
   38 CONTINUE
      IHMODE = MODE(3)
      JHMODE=MODE(4)
      DO 32 I = 1,2
      M = MODE( I + 2 )
      IF ( M - NRCLIM(I) )   33,34,34
   34 IF ( M - NRCLIM(I+2) )   32,32,35
   32 CONTINUE
      GO TO 36
   29 MODE(4) = 1
      GO TO 37
   31 MODE(3) = 51
      GO TO 38
   33 MODE(I+2) = NRCLIM(I)
      GO TO 34
   35 MODE(I+2) = NRCLIM(I+2)
      GO TO 32
C     SET PAGE BLANK INITIALLY.
   36 DO 39 I = 1,1326
   39 IPAGE( I ) = 1077952576
C     NOW SET '-' FOR HORIZONTAL AXIS,'1' FOR VERTICAL AXIS.
      M = 104*( MODE(3) - 1 )
      DO 40 J = 1,101
   40 CALL SETCH(IPAGE, M+J, 96 )
      M = MODE(4) - 104
      DO 41 I = 104,5304,104
   41 CALL SETCH(IPAGE, M+I, 79)
C     NOW SET EVERY 10TH POSITION ON EACH AXIS TO '+'.
      K = MOD( MODE(4), 10 )
      IF ( K )   44,44,45
   44 K = 10
   45 I = 104*( MODE(3) - 1 )
      L = NRCLIM(4)
      DO 42 J = K,L,10
   42 CALL SETCH(IPAGE, J+I, 78 )
      K = MOD( MODE(3), 10 )
      IF ( K )   46,46,47
   46 K = 10
   47 J = MODE(4) - 104
      L = NRCLIM(3)
      DO 43 I = K,L,10
   43 CALL SETCH(IPAGE, 104*I+J, 78 )
      MODE(3)=IHMODE
      MODE(4)=JHMODE
      GO TO 25
      END
      SUBROUTINE PLOTLP (TITLES,IPAGE,MODE,XYREF )
      DIMENSION TITLES(44),IPAGE(1),MODE(6),XYREF(4),LABELX(33),
     1         LABELY(2),LABTAB(102)
C     PRINT A GRAPH WITH LABELLING.
C
C
      WRITE(3,300) TITLES
  300 FORMAT('1'//' ',30A4/'0',6X,'HORIZONTAL X-AXIS REPRESENTS ',7A4,
     1                       '  ,  VERTICAL Y-AXIS REPRESENTS  ',7A4)
      WRITE(3,301)
  301 FORMAT(7X,'SCALE ON X-AXIS IS             , SCALE ON Y-AXIS IS')
      IF ( MODE(1) )   1,2,1
    2 WRITE (3,302)
  302 FORMAT('+',T27,'LINEAR')
      GO TO 3
    1 WRITE(3,303)
  303 FORMAT('+',T27,'LOGARITHMIC' )
    3 IF ( MODE(2) )   4,5,4
    5 WRITE(3,304)
  304 FORMAT('+',T60,'LINEAR')
      GO TO 6
    4 WRITE(3,305)
  305 FORMAT('+',T60,'LOGARITHMIC' )
C
C     NOW CALCULATE LOWEST ROW NUMBER FOR LABELLING Y-AXIS.
C
    6 NLINE = 0
      CALL YLAB(1,NLINE,MODE,XYREF,LABELY )
      WRITE(3,308)
  308 FORMAT('0')
      M=-1
      DO 8 I=1,51
      CALL YLAB( I, NLINE, MODE, XYREF, LABELY )
      M=M+2
      LABTAB(M)=LABELY(1)
    8 LABTAB(M+1)=LABELY(2)
      CALL RELAB(LABTAB,1,8,408)
      M=-1
      DO 7 I=1,51
      M=M+2
      LABELY(1)=LABTAB(M)
      LABELY(2)=LABTAB(M+1)
      L = 26*I
      K = L - 25
    7 WRITE(3,306) LABELY, (IPAGE(J), J = K,L )
  306 FORMAT(7X,27A4,A1 )
C
C     LABEL X-AXIS.
C
      CALL XLAB(MODE, XYREF, LABELX )
      WRITE(3,307) LABELX
  307 FORMAT(' ',33A4)
      RETURN
      END
      SUBROUTINE XLAB(MODE,XYREF,LABELX)
C
C     FILL LINE LABELX FOR LABELLING THE X-AXIS.
C
C
C
      DIMENSION MODE(6),XYREF(4),LABELX(33)
      MINC=10
      IF(MODE(1).NE.0.AND.MODE(5).GT.10)MINC=MODE(5)
      DO 1 I=1,33
    1 LABELX(I) = 1077952576
      M    = MOD (MODE(4),10)
      IF (M)   2,2,3
    2 M    = 10
    3 DO 4 NCOL=M,101,MINC
      IF (MODE(1))   5,5,6
C     NORMAL MODE X-AXIS.
    5 X = (NCOL - MODE(4) )*XYREF(1)
      GO TO 7
C     LOG. MODE X-AXIS
    6 X=XYREF(1)*10.0**((NCOL-MODE(4))/XYREF(3))
    7 IF (X) 8,9,10
    9 IFCTR = 0
      IEXPT = 0
      GO TO 13
    8 X = -X
      CALL SETCH(LABELX,NCOL+10,96 )
C     NOW CALCULATE CHARACTER FORM OF X.
   10 Z = ALOG10(X)
      IEXPT = Z
      IF (IEXPT)   11,12,12
   11 IEXPT = IEXPT - 1
   12 Z = 100.*10.0**(Z-IEXPT)
      IFCTR = NINT(Z)
      IEXPT = IEXPT-2
      IF (IFCTR-1000 )   13,14,14
   14 IFCTR = IFCTR/10
      IEXPT = IEXPT +1
   13 J = IFCTR/100
      CALL SETCH(LABELX,NCOL+11, 240+J )
      J = IFCTR-100*J
      K = J/10
      J = J-10*K
      CALL SETCH(LABELX,NCOL+12, 240+K )
      CALL SETCH(LABELX,NCOL+13, 240+J )
      CALL SETCH( LABELX, NCOL+14, 197 )
      IF (IEXPT)   15,16,16
   15 CALL SETCH(LABELX,NCOL+15, 96 )
      IEXPT = -IEXPT
      GO TO 17
   16 CALL SETCH(LABELX,NCOL+15, 78 )
   17 J = IEXPT/10
      K = IEXPT - 10*J
      CALL SETCH (LABELX,NCOL+16, 240+J )
      CALL SETCH (LABELX,NCOL+17, 240+K )
    4 CONTINUE
      CALL RELAB(LABELX,M+10,10,132)
      RETURN
      END
      SUBROUTINE  YLAB(I,NLINE,MODE,XYREF,LABELY)
      DIMENSION MODE(6),XYREF(4),LABELY(2)
C
C     IF CURRENT LINE IS AT CORRECT DISTANCE FROM X-AXIS,
C     CALCULATE 8 CHARACTER LABELY,OTHERWISE SET LABELY TO BLANKS.
C
C     IF NLINE=0 FOR FIRST ENTRY,SET NLINE TO FIRST LINE TO BE LABELLED.
C
C
C
      IF (NLINE) 1,1,2
    1 MINC=10
      IF(MODE(2).NE.0)MINC=MODE(6)
      NLINE=MINC
      IF(NLINE.LT.10)NLINE=10
      NLINE=MOD(MODE(3),NLINE)
      IF (NLINE) 3,3,99
    3 NLINE = 10
      GO TO 99
    2 IF (I - NLINE)   98,4,98
   98 LABELY(1) = 1077952576
      LABELY(2) = 1077952576
   99 RETURN
    4 J = MODE(3) - NLINE
      IF (MODE(2))   5,5,6
    5 Y = J * XYREF(2)
      GO TO 7
    6 Y=XYREF(2)*XYREF(4)**(J/10)
    7 NLINE=NLINE+MINC
      IF (Y) 8,9,10
    8 CALL SETCH (LABELY,1,96)
      Y = -Y
      GO TO 11
   10 CALL SETCH (LABELY,1,64)
   11 Z = ALOG10(Y)
      IEXPT = Z
      IF (IEXPT) 12,13,13
   12 IEXPT = IEXPT - 1
   13 IFCTR = NINT( 100.*10.0**(Z-IEXPT))
      IEXPT = IEXPT-2
      IF (IFCTR - 1000 )   14,15,15
   15 IFCTR = IFCTR/10
      IEXPT = IEXPT + 1
   14 J = IFCTR/100
      CALL SETCH( LABELY, 2, 240+J )
      J = IFCTR-100*J
      K = J/10
      J = J-10*K
      CALL SETCH( LABELY, 3, 240+K )
      CALL SETCH( LABELY, 4, 240+J )
      CALL SETCH (LABELY,5,197)
      IF (IEXPT)   16,17,17
   16 CALL SETCH (LABELY,6,96)
      IEXPT = -IEXPT
      GO TO 18
   17 CALL SETCH (LABELY,6,78)
   18 J = IEXPT/10
      K = IEXPT - 10*J
      CALL SETCH (LABELY,7,240+J)
      CALL SETCH (LABELY,8,240+K)
      GO TO 99
    9 IFCTR = 0
      IEXPT = 0
      GO TO 14
      END
      FUNCTION NINT(X)
C     NEAREST INTEGER TO THE REAL VARIABLE X.
C
C
      IF (X)   1,2,3
    1 Y = X- .5
      GO TO 99
    3 Y = X + .5
      GO TO 99
    2 Y = X
   99 NINT = Y
      RETURN
      END
