S60022   CSECT
         ENTRY MABEND
*
*         OS ROUTINE - PRINTS ABEND MESSAGE AND RETURN CODE ON SYSLOG,
*         THEN RETURNS TO SUPERVISOR WITH OR WITHOUT DUMP.
*
*
*
*        PARAMETER INPUT AS FOLLOWS -
*
*          02      WSS-PARAM.
*           03     WSS-PROGNAME    PIC X(8) VALUE 'PROGNAME'.
*           03     WSS-RETCODE     PIC 9(4) VALUE ZERO.
*           03     WSS-DUMP        PIC X.
*           03     WSS-MESSAGE     PIC X(50).
*
         EJECT
         PRINT NOGEN
MABEND   EQU   *
         MBEGIN
         L     1,0(1)                   POINT TO RETURN CODE
         PACK  DWORK,8(4,1)             PACK RETURN CODE
         CVB   2,DWORK                  CHANGE TO BIARY IN R2
         ST    2,MRETCODE               STORE FOR OPERATING SYSTEM
         MVC   DWORK(1),12(1)           SAVE 'DUMP' INDICATOR
         MVC   MESS1(8),0(1)            MOVE PROGNAME
         MVC   MESS1+9(4),8(1)          MOVE RETURN CODE
         MVC   MESS1+15(50),13(1)       MOVE MESSAGE
         MVC   WTOLIST+4(65),MESS1      STORE MESSAGE TO PRINT
         LA    1,WTOLIST                POINT TO CONSOLE MESSAGE
         WTO   MF=(E,(1))
         CLI   DWORK,C'D'               TEST IF DUMP REQUIRED
         BNE   ENDJOB                   NO, FINISH NORMALLY
         ABEND (2),DUMP,STEP
ENDJOB   EQU   *
         L     13,MSAVEA+4              13 = CALLING PROGS SAVEA
         L     13,4(13)                 AND AGAIN....
         ST     13,MSAVEA+4             ENSURE EXIT TO SUPERVISOR
         MFINISH
MESS1    DC    C'XXXXXXXX.9999. '
         DC    C'XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX'
WTOLIST  WTO   'XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
               XXXXXXXXXX',ROUTCDE=11,DESC=4,MF=L
DWORK    DS    D
         LTORG
         END
