         MACRO
&NA      BEGIN &BASE,&SAVADR=,&ID=YES,&REGS=YES
         GBLA  &CHK
         LCLC  &BS1,&BS2,&BS3
         USING *,15
         AIF   ('&REGS' EQ 'NO').NOREGS
         AIF   (&CHK EQ 1).NOREGS
*  REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FP0      EQU   0
FP2      EQU   2
FP4      EQU   4
FP6      EQU   6
&CHK     SETA  1
.*  SET UP CSECT NAME AS AN 8-BYTE CHARACTER CONSTANT IF SPECIFIED
.NOREGS  AIF   ('&ID' EQ 'NO').NOID
         AIF   ('&SYSECT' EQ '').NOID
         B     *+12
         DC    CL8'&SYSECT'
.NOID    ANOP
&NA      STM   14,12,12(13)
         AIF   ('&BASE' NE  '').BSPEC
&BS1     SETC  '12'                DEFAULT BASE = 12
         CNOP  0,4
         BAL   &BS1,*+76
         USING *,&BS1
         AGO   .SAVE
.*  BASE SPECIFIED. SET UP APPROPRIATE USING STATEMENT, ALLOWING UP TO
.*     THREE REGISTERS
.BSPEC   ANOP
&BS1     SETC  '&BASE(1)'
&BS2     SETC  '&BASE(2)'
&BS3     SETC  '&BASE(3)'
         AIF   ('&BS2' EQ  '').ONLY1
         AIF   ('&BS2' EQ '&BS1' OR '&BS1' EQ '&BS3').FAIL
         AIF   ('&BS2' EQ '&BS3').FAIL
         AGO   .OVER1
.*  ALL SPECIFIED BASE REGISTERS ARE UNIQUE
.ONLY1   CNOP  0,4
         BAL   &BS1,*+76
         USING *,&BS1
         AGO   .SAVE
.*  MORE THAN ONE BASE REGISTER SPECIFIED
.OVER1   CNOP  0,4
         BAL   &BS1,*+76
         AIF   ('&BS3' NE  '').ALL3 THREE BASE REGISTERS SPECIFIED
         USING *,&BS1,&BS2
         AGO   .SAVE
.ALL3    USING *,&BS1,&BS2,&BS3
.SAVE    ANOP
&SAVADR  DC    18F'0'
         AIF   ('&BS2' NE  '').SETB2
         AGO   .STORE
.SETB2   LR    &BS2,&BS1           SET 2ND BASE =
         AH    &BS2,IND&SYSNDX        1ST BASE + 4096
         AIF   ('&BS3' NE  '').SETB3
         AGO   .INC4K
.SETB3   LR    &BS3,&BS2           SET 3RD BASE =
         AH    &BS3,IND&SYSNDX        2ND BASE + 4096
.INC4K   B     *+6
IND&SYSNDX  DC H'4096'
.*  COMPLETE THE ESQUENCE
.STORE   ST    &BS1,8(13)
         ST    13,4(&BS1)
         LR    13,&BS1
         MEXIT
.*  ERROR NOTES
.FAIL    MNOTE 12,'BASE REGS DO NOT HAVE UNIQUE NAMES. GENERATION '
         MNOTE *,'TERMINATED'
         MEXIT
         MEND
         MACRO
&NAME    FINISH &RC=Z
&NAME    L     13,4(13)
         AIF   ('&RC' EQ '(15)').REG15
         AIF   ('&RC'(1,1) EQ '(').REGERR
         AIF   ('&RC' EQ 'Z').NORC
         LA    15,&RC
.REG15   L     14,12(13)
         LM    0,12,20(13)
         AGO   .OUT
.NORC    LM    14,12,12(13)
.OUT     BR    14
         MEXIT
.REGERR  MNOTE *,RETURN CODE REGISTER MUST BE 15. NO R.C. RETURNED
         AGO   .NORC
         MEND
         TITLE 'ROUTINE TO UNCATALOG SPECIFIED FILES'
SYSUNCAT CSECT
         BEGIN
*
*    TWO FILES:-   (1) CARD FILE OF DSNAMES
*                  (2) PRINT FILE
*
         OPEN  (SYSIN,(INPUT),SYSPRINT,(OUTPUT))
*  IF SYSIN'S NOT THERE THEY MAY WANT TO TYPE IT IN FROM THE CONSOLE
         SR    R4,R4
         LA    R2,SYSIN
         USING IHADCB,R2
         TM    DCBOFLGS,X'10' NO SYIN DD CARD ?
         BO    *+8
         LA    R4,8(R4)   SET RETURN CODE = 8
         LA    R2,SYSPRINT
         TM    DCBOFLGS,X'10' NO SYSPRINT DD CORD?
         BO    *+8
         LA    R4,4(R4)       + RETURN CODE 4
*   ERROR MESSAGE NEEDED
         B     *+4(R4)
         B     BOTHERE1
         B     NOSYSPRT
         B     NOSYSIN
         B     NEITHERE
*
*  DSNAMES ON CARDS
*
BOTHERE1 LA    R4,STORE
         LA    R5,500  MAX NO OF DSN'S
BOTHERE  GET   SYSIN READ A CARD
         LR    R3,R1
         CLI   0(R1),C' '
         BE    BOTHERE
         TRT   0(45,R3),TABLE LOOK FOR A COMMA
         BZ    NOPURGE NO PURGE REQUIRED
         CLC   1(5,R1),=C'PURGE' SCRATCH WANTED ?
         BNE   BADCARD
         MVC   0(6,R1),=CL6' '
         MVI   DSNAME+44,C'P' INDICATE SCRATCH
         B     *+8
NOPURGE  MVI   DSNAME+44,C' ' NO PURGE
         MVC   DSNAME,0(R3)
LOCATE   LOCATE INDEX
         LTR   R15,R15
BR0      BZ    BUMP      *****
          OC    RC,=F'4'
         CH    R15,=H'8'
         BE    NOTFOUND
         CH    R15,=H'12'
         BE    INVALID
         CH    R15,=H'16'
         BE    INVALID
         CH    R15,=H'20'
         BE    INVALID
*  SOMETHING STRANGENHAS HAPPENED
         ABEND 1,DUMP
BUMP     MVC   0(45,R4),DSNAME   STORE DSN
         LA    R4,45(R4) BUMP ADDRESS
         BCT   R5,BOTHERE AND BRANCH BACK IF STORE NOT GONE
         ABEND 2,DUMP
*  UNCATALOG SPECIFIED DATA SET
UNCAT    LA    R4,STORE
         LNR   R5,R5
         LA    R5,500(R5) R5 = NO OF DSN'S = (500 - (R5))
         LTR   5,5
         BZ    NOSYSPRT
UNCAT1   MVC   DSNAME(45),0(R4)  MOVE NAME IN TO WORK AREA
         CLI   DSNAME+44,C'P' PURGE ?
         BNE   NOPG      NO
         LOCATE INDEX GET VOLUME LIST BLOCK AGAIN
NOPG     EQU   *
UNCAT2   CATALOG REMOVE
         LTR   R15,R15
         BE    OK
*    VERY ODD
         ABEND 2,DUMP
OK       MVC   MESSNAME,DSNAME
         MVC   MESSNO,=H'266'  OK MESSAGE
         BAL   R10,ERRMESS
         CLI   DSNAME+44,C'P' PURGE ?
         BNE   SCCONT NO
         TM    AREA+4,X'80' ARE WE TRYING TO DELETE A TAPE D.S. ?
         BO    SCCONT    YES
         SR    R0,R0 INDICATE NO UCB
         SCRATCH VOLUMES
         MVC   SCDSN,DSNAME
         LTR   R15,R15
         BZ    SCGOOD
         OC    RC,=F'4'
         MVC   SCDESC,SCNF
         PUT   SYSPRINT,SCMESS
         B     SCCONT
SCGOOD   MVC   SCDESC,SCOK
         PUT   SYSPRINT,SCMESS
SCCONT   EQU   *
         LA    R4,45(R4) BUMP ADDRESS
         BCT   R5,UNCAT1 IF THERE IS ONE
BR1      B     END       *****
*  DSN WAS INVALID OR INDEX TROUBLE WAS ENCOUNTERED
INVALID  MVC   MESSNAMX,DSNAME
         MVC   MESSNO,=H'133'
         BAL   R10,ERRMESS
BR2      B     BOTHERE   *****
*  DSNAME NOT IN CATALOG
NOTFOUND MVC   MESSNAMY,DSNAME
         MVC   MESSNO,=H'0'
         BAL   R10,ERRMESS
BR3      B     BOTHERE   *****
*  WRITE A PRINT LINE
ERRMESS  LA    R8,MESSAGES
         AH    R8,MESSNO
         PUT   SYSPRINT,(R8)
         MVC   WTOMESS2,0(R8)      AMDT 14/8/72
         LA    1,WTOMESS           AT REQUEST BOB CHRISTIE
         SVC   35
         BR    R10
BADCARD  MVC   BC1,0(R3)
         PUT   SYSPRINT,BCARD
BR4      B     BOTHERE
*
*  NO SYSPRINT DD CARD
*
NOSYSPRT FINISH RC=4
*  NO DD CARDS
NEITHERE FINISH RC=4
*  SYSIN MISSING - TRY THE CONSOLE
NOSYSIN  WTOR  'SYSBACK - DSNAMES FROM CONSOLE ? - YES/NO',            C
               REPLY,3,ECB
         WAIT  ECB=ECB
         NI    ECB,X'00'
         CLC   REPLY,=C'YES'
         BNE   NOSYSPRT
WTOR     MVC   DSNAME(50),=CL50' '  CLEAR DSNAME
         WTOR  'SYSBACK - ENTER DSNAME',DSNAME,50,ECB
         WAIT  ECB=ECB
         NI    ECB,X'00'
TRT2     TRT   DSNAME,TABLE        LOOK FOR COMMA        JAS / 161172
         BZ    SETP+4              NO PURGE REQ'D        JAS / 161170
         CLC   1(5,R1),=C'PURGE'   SCRATCH WANTED ?      JAS / 161170
         BE    SETP-6               YES                  JAS / 161170
*                                   ERROR                JAS / 161172
         MVC   DSNAME(50),=CL50' '  CLEAR DSNAME         JAS / 161172
         WTOR  'SYSBACK - REPLY ERR, ENTER AGAIN',       JAS / 161172  -
               DSNAME,50,ECB                             JAS / 161172
         WAIT  ECB=ECB                                   JAS / 161172
         NI    ECB,X'00'                                 JAS / 161172
         CLI   DSNAME,C'/'    OPERATOR GIVING UP ?       JAS / 161172
         BE    CLOSE           YES                       JAS / 161172
         B     TRT2            NO                        JAS / 161172
*                                                        JAS / 161172
         MVC   0(6,R1),=CL6' '   REMOVE PURGE            JAS / 161172
SETP     MVI   DSNAME+44,C'P'     & INDIC SCRATCH        JAS / 161172
*
*  INSTRUCTIONS MARKED THUS :- *****
*  ARE ALTERED IF JOB RUN FROM CONSOLR
*
         MVC   BR0+2(2),SCON2
         MVC   BR1+2(2),SCON
         MVC   BR2+2(2),SCON
         MVC   BR3+2(2),SCON
         MVC   BR4+2(2),SCON
         LA    R5,1 COUNT OF 1
         CLI   DSNAME,C'/'
         BNE   LOCATE
*  CONSOLE COMMUNICATION FINISHED
CLOSE    CLOSE SYSPRINT                                  JAS / 161172
         FINISH RC=0
END      CLOSE (SYSIN,,SYSPRINT)
         L    15,RC
         FINISH RC=(15)
ECB      DC    F'0'
         DS    0D
INDEX    CAMLST NAME,DSNAME,,AREA
         DS    0D
REMOVE   CAMLST UNCAT,DSNAME
         DS    0D
VOLUMES  CAMLST SCRATCH,DSNAME,,AREA,,OVRD
DSNAME   DC    CL44' '
         DC    C' '
         DC    CL5' '
AREA     DS    0D
         DS    CL265
SYSIN DCB DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=UNCAT,RECFM=FB,         C
               LRECL=80
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=133,  C
               BLKSIZE=798
SCON     DC    S(WTOR)
MESSAGES EQU   *
MESS1    DC    CL10'0'
MESSNAMY DS    CL44
         DC    CL79' - NOT FOUND IN CATALOG'
         DC    CL10'0'
MESSNAMX DS    CL44
         DC    CL79' - INVALID NAME OR INDEX STRUCTURE'
         DC    CL10'0'
MESSNAME DS    CL44
         DC    CL83' - UNCATALOGED'
SCMESS   DC    CL10'0'
SCDSN    DS    CL44
SCDESC   DS    CL79
SCOK     DC    CL79'DELETED'
SCNF     DC    CL79'EITHER DATA SET DOES NOT EXIST OR VOLUME COULD NOT C
               BE MOUNTED'
BCARD    DC    CL10'0'
BC1      DS    CL80
         DC    CL43' THIS CARD INVALID'
WTOMESS  DC    H'124'
         DC    H'0'
WTOMESS2 DS    CL120
REPLY    DC    CL3' '
MESSNO   DC    H'0'
SCON2    DC    S(UNCAT2)
TABLE    DC    107X'00'
         DC    X'FF' COMMA
         DC    148X'00'
RC       DC    F'0'
         LTORG
STORE    DC    500CL45' '
         DCBD  DSORG=PS
         END
