* THIS PROGRAM PRODUCED BY BRITISH STEEL CORPORATION CORBY
* REFER ANY PROBLEMS TO MALCOLM SISMEY TEL 053-66-2121 EXT 729
SYUNIT   CSECT
         SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         LR    11,13
         CNOP  0,4
         BAL   13,*+76
         DC    18F'0'
         ST    13,8(11)
         ST    11,4(13)
         SPACE 2
**** USE OF REGISTERS ****
*
*    REG 3 STORES ADDRESS OF UCBBYTE CONTROL FIELD WORKAREA
*    REG 4 USED FOR DSECT
*    REG 5 CHECKS NOT , 3 CHARACTERS BETWEEN COMMAS
*    REG 6 CONTAINS HIGH ADDRESS E.G. START ADDR ON REPLY FIELD.
*    REG 7 CONTAINS LOW ADDRESS - POSITION OF SEPERATING ,
*    REG 8 HOLDS ADDRESS OF UCB TO BE UPDATED
*    REG 9 COUNTS NO OF CHARACTERS FOR EXECUTE INSTRUCTION
*    REG 10 USED TO MOVE ALONG REPLY FIELD
*    REG 11 USED FOR UCB POINTER
*
****
         LA    2,0
         L     3,0(1)
         XC    0(1,3),0(3)    ZEROISE BYTE
         USING SYMBOLS,4
         LA    4,CONSTS
         LA    8,0
         L     9,16           LOAD CVT ADDRESS
         L     11,40(9)       ADDRESS OF UCB LIST
         EJECT
****
*   FOLLOWING ROUTINE OVERRIDES UCB VOL SERIAL NO SO THAT
*   ALL PACKS CAN BE CHECKED IF REQUIRED
****
         SPACE 2
FILLIN   WTOR  '***SYIDCHEK  REPLY PACK NUMBERS',REP1FLD,31,REP1ECB
         WAIT  ECB=REP1ECB
         B     REP1ECB+4
REP1FLD  DC    CL31' '   REPLY 'XXX,XXX,XXX, ETC  EG 'X27,X19,X21'
         DC    CL1' '
REP1ECB  DC    F'0'
         XC    REP1ECB,REP1ECB
         LA    10,REP1FLD
         SR    1,1
         TRT   0(31,10),TABLECH    CHECK FOR FIRST BLANK
         BC    8,COMMA             REPLY ALL 15
         MVI   0(1),X'6B'
         B     TESTX
COMMA    MVI   REP1FLD+31,X'6B'
TESTX    LA    9,0                 CHECK XXX INPUT
HOLY     LH    8,0(11)             LOAD UCB ADDRESS
         CH    8,=H'0'             CHECK FOR HOLES IN TABLE
         BNE   NOHOLES
UPTAB    LA    11,2(11)
         B     HOLY
NOHOLES  CH    8,UCBEND            END OF TABLE
         BE    REPEXIT             YES END OF TABLE SEARCH
         CLI   18(8),X'20'         DIRECT ACCESS
         BNE   UPTAB               NO LOOP AROUND
         CLC   13(3,8),=C'230'          14-9-72   2314 ADDRESS
         BL    UPTAB
         CLC   13(3,8),=C'237'          14-9-72   RANGE
         BH    UPTAB               AT PRESENT
ITNIP    B     NIPIT
         IC    2,TMBIT+1
         SRL   2,1
         STC   2,TMBIT+1
NIPIT    MVI   ITNIP+1,X'00'
         CLI   0(10),C'X'     FIRST CHARACTER MUST BE X
         BNE   REP4ERR        ERROR IN REPLY IF NOT X OR SPACE OR COMMA
         LA    5,3
         LR    6,10           STORE START ADDRESS OF REPLY
LOOPCHK  LA    10,1(10)
         CLI   0(10),X'6B'    CHECK FOR DIVIDING COMMA
         BE    MOVECHS
         CLI   0(10),X'F0'    * CHECK               *****
         BL    REPT2ERR       *       FOR               *
         CLI   0(10),X'F9'    *           NUMERIC       *
         BH    REPT2ERR       *****               INPUT *
         A     9,=F'1'
         BCT   5,LOOPCHK
         B     REPT2ERR
MOVECHS  LTR   9,9
         BC    8,REPT2ERR
         C     9,=F'5'
         BH    REPT2ERR
         LR    7,10           STORE COMMA POSITION
         LR    10,6           REVERT TO INITIAL POSITION
         LA    15,EXECMOVE
         STM   8,10,DREG8
         SVC   253
TMBIT    OI    0(3),X'08'
         LR    10,7           RESET TO COMMA POSITION
RESETALL LA    10,1(10)       RESET REG 10 TO NEXT XXX REPLY
         LA    11,2(11)       UP POINTER TO UCB IN UCB LIST
         B     TESTX
REPT2ERR WTO   '**SYIDCHEK MSG1 - INVALID REPLY'
         LA    15,4           **ERROR**
         B     FILLIN
REPEXIT  LA    15,0
         MVI   REP1FLD,X'40'
         MVC   REP1FLD+1(30),REP1FLD
         MVI   ITNIP+1,X'F0'            RESET SWITCH
         MVI   TMBIT+1,X'08'            RESET SWITCH
WHOOSH   L     13,4(13)
         RETURN (14,12),,RC=(15)
REP4ERR  CLI   0(10),X'6B'
         BNE   REP3ERR
         B     RESETALL
REP3ERR  CLI   0(10),X'40'
         BNE             REPT2ERR
         B     REPEXIT
TABLECH  DC    64X'00'
         DC    X'40'
         DC    191X'00'
UCBEND   DS    0H
         DC    X'FFFF'
         LTORG
         USING *,15
EXECMOVE L     4,CONSTX
         LM    8,10,DREG8
         MVI   28(8),X'40'
         MVC   29(5,8),28(8)
         EX    9,MOVEUCB      MOVE XXX INTO UCB VOL SERIAL NUMBER
         BR    14
MOVEUCB  MVC   28(0,8),0(10)
         SPACE 3
CONSTX   DC       A(CONSTS)
CONSTS   DS    0F
REG8     DC    F'0'
REG9     DC    F'0'
REG10    DC    F'0'
SYMBOLS  DSECT
DREG8    DS    F
DREG9    DS    F
DREG10   DS    F
SYUNIT   CSECT
         END
