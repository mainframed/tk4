         TITLE 'VARIABLE BLOCKED SPANNED OUTPUT ROUTINE'
         PRINT ON,NOGEN
OUTPUT   CSECT
         EXTRN IN
         ENTRY OUT
         ST    14,12(13)
         STM   7,12,48(13)
         BALR  12,0
         USING *,12
         LR    11,13
         LA    13,SAVEAREA
         ST    11,4(13)
         ST    13,8(11)
         LR    8,1
         L     7,INADDR
         TM    SWITCH,X'80'
         BO    ROUND
         OPEN  (OUT,(OUTPUT))
         OI    SWITCH,X'80'
         LA    1,OUT
         L     15,48(1)
         BALR  14,15
         ST    1,OUTADDR
ROUND    LH    10,12(8)
         LA    10,31(10)
         SRL   10,5
         L     11,8(8)
         L     9,OUTADDR
MOVE     MVC   0(32,9),0(11)
         LA    9,32(9)
         LA    11,32(11)
         BCT   10,MOVE
         LA    1,OUT
         L     15,48(1)
         BALR  14,15
         ST    1,OUTADDR
         L     13,SAVEAREA+4
         LM    7,12,48(13)
         L     14,12(13)
         LA    15,0
         BR    14
OUTADDR  DC    F'0'
INADDR   DC    X'80',AL3(IN)
         PRINT ON,NOGEN
OUT      DCB   DDNAME=SYSUT2,MACRF=(PL),DSORG=PS,EXLST=EXLST
EXLST    DC    X'85',AL3(SETDCB)
SETDCB   CLI   OUT+36,X'00'        IS RECFM = 0 ?
         BNE   TESTSPAN
         MVC   OUT+36(1),36(7)     SET OUTPUT = INPUT RECFM
         OI    OUT+32,X'60'        SET BFTEK = A
         B     *+16
TESTSPAN TM    OUT+36,X'08'        TEST FOR SPANNED FORMAT
         BZ    *+8
         OI    OUT+32,X'60'        SET BFTEK = A
         CLC   OUT+62(2),=H'0'     IS BLKSIZE = 0 ?
         BNE   BR14
         CLC   OUT+82(2),=H'0'     IS LRECL = 0 ?
         BNE   BR14
         MVC   OUT+62(2),62(7)     SET OUTPUT = INPUT BLKSIZE
         MVC   OUT+82(2),62(7)     SET OUTPUT LRECL = INPUT BLKSIZE
BR14     BR    14
SWITCH   DC    X'00'
SAVEAREA DC    18F'0'
         END
