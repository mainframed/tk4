SAFEDUMP CSECT
*
*  THIS PROGRAM WILL......
*        1  DUMP THE DISK PACK SPECIFIED BY DDNAME DISK TO THE TAPE
*           SPECIFIED BY DDNAME TAPE (WHICH MUST BE A GEN DAT GROUP)
*        2  READ THE VTOC OF THE DISK AND WILL AUTOMATICALLY LINK TO
*           'SUPRSHUFL' FOR EACH PDS THAT IS USING MORE THAN XX PERCENT
*           OF THE SPACE ALLOCATED TO IT (ALL EXTENTS).
*           THE VALUE OF XX IS GIVEN BY PARM (DEFAULT IS 80).
*        3  IN THE EVENT OF ANY ABEND (NORMALLY AN ERROR FOUND BY
*           'SUPRSHUFL') WILL AUTOMATICALLY RESTORE THE DISK FROM THE
*           TAPE.
*           AN ABEND WITHIN SUPRSHFL WILL GIVE A SNAP DUMP (IF THE
*           SNAPOUT DD IS PRESENT).  FIND THE REASON FOR THE DUMP FROM
*           THE FIELDS WITHIN THE PROGRAM FOLLOWING THE CONSTANTS
*           'ABEND COMPLETION CODE','ABEND PSW', AND 'REGS 0-15 AT
*           ABEND'.
*        4  ONLY IF THE END OF VTOC IS REACHED WITHOUT ERROR IS THE
*           TAPE CATALOGUED AS THE +1 GENERATION OF ITS GROUP.
*
*  SAMPLE JCL REQUIRED....
*        //BACKUP EXEC PGM=SAFEDUMP    (,PARM=60)
*        //SYSPRINT DD SYSOUT=A
*        //SNAPOUT DD SYSOUT=A
*        //TAPE DD DSN=E.BACKUP.VOLXX(-3),DISP=SHR
*        //DISK DD VOL=....,UNIT=....,DISP=SHR
*        //SYSLIB DD VOL=....,UNIT=....,DISP=SHR
*        //* DISK AND SYSLIB SPECIFY THE SAME VOLUME.
*        //*  ONE IS USED BY IEHDASDR AND THE OTHER BY SUPRSHUFL.
*        //SYSIN  DD  SPACE=(TRK,1),UNIT=SYSDA
*        //*  SYSIN IS A TEMPORARY DISK DATA SET TO WHICH IEHDASDR
*        //*  DUMP OR RESTORE STATEMENTS ARE WRITTEN
*        //
         SPACE
*  THIS VERSION WILL WORK UNDER MFT OR MVT.
         SPACE
*  SUPRSHFL EXECUTES SEVERAL GETMAINS WITHOUT FREEMAINS, AND REPEATED
*  USE WILL CAUSE AN EVENTUAL 80A.  AS THE SOURCE CODE OF SUPRSHFL IS
*  NOT AVAILABLE THIS PROBLEM HAS BEEN RESOLVED BY........
*  FOR MFT - THIS PROGRAM SAVES THE FQE CHAIN BEFORE LOADING AND
*  ENTERING SUPRSHFL, AND RESTORES THE SAME CHAIN AFTER RETURNING FROM
*  SUPRSHFL.
*  FOR MVT - AN INTERMEDIATE MODULE (CALLED SUPRSHFL) IS USED WHICH
*  LOADS THE GENUINE SUPRSHFL (CALLED MVTSHUFL) AND PLACES 10 PATCHES
*  IN IT BEFORE ENTERING IT FOR EXECUTION.  THE PATCHES CAUSE EACH
*  GET/FREEMAIN TO BE FOR SUBPOOL 1, AND 'SUPRSHFL' ISSUES A FREEMAIN
*  FOR THE WHOLE OF SUBPOOL 1 ON RETURN FROM 'MVTSHUFL'.
         SPACE
         SPACE
*  THIS VERSION ALSO PROTECTS PDS'S FROM ATTEMPTS AT CONCURRENT UPDATES
*  WHILE COMPRESSION IS TAKING PLACE.  THIS IS DONE BY AN ENQ ON MAJOR
*  NAME 'RTPUPDTE' AND MINOR NAME '(DSN OF PDS WITHOUT TERMINAL BLANKS)
*  THIS ONLY GIVES PROTECTION IF, AS IN RESPOND SYSTEMS, OTHER PROGRAMS
*  THAT MAY UPDATE THE PDS (IEWL,IEBUPDTE,IEBCOPY, AND LOCAL PROGRAMS)
*  ALSO ISSUE THE SAME ENQ.
         SPACE
         SPACE
*        ***  INSTALLATION NOTES  ***
*
*  MFT USERS.
*  LINKEDIT 'SUPRSHFL' INTO YOUR SYSTEM UNDER THE NAME 'SUPRSHFL'.
*  'LOADSHUF' IS NOT REQUIRED.
*
*  MVT USERS.
*  LINKEDIT 'SUPRSHFL' INTO YOUR SYSTEM UNDER THE NAME 'MVTSHFL'.
*  COMPILE AND LINKEDIT 'LOADSHUF' INTO YOUR SYSTEM UNDER THE NAME
*  'SUPRSHFL'.
         SPACE
         SPACE
*  THIS VERSION OF SAFEDUMP HAS TWO CHANGES MADE IN MARCH 1971....
*  1.  FOR MVT ONLY THE RETRY ROUTINE ISSUES A DEQ FROM RTPUPDTE TO
*      AVOID A D03 ABEND BECAUSE OF THE ENQ ISSUED IN 'LOADSHUF'.
*  2.  ALL DSNAMES BEGINNING 'SYS1.' ARE AUTOMATICALLY EXCLUDED FROM
*      COMPRESSION BY SUPRSHFL.  THIS AVOIDS E.G. COMPRESSION OF THE
*      'LIVE' SVCLIB.
         SPACE
         SPACE
         BEGIN FIRSTSAV,BASE=(12,11)
BASE     LR    10,1
         RDJFCB  (LIB)
         OI    LJFCB+76,128
         MVC   SAVEJ,LJFCB
         OPEN  (PRINT,OUTPUT)
         LR    1,10
         SPACE
*  TEST FOR PARM.  IF ONE IS NOT GIVEN, OR IF IT IS GREATER THAN 100,
*  A DEFAULT OF 80 PERCENT FULL IS USED AS THE LEVEL AT WHICH SUPRSHUFL
*  WILL BE INVOKED TO CONDENSE A PDS.
         SPACE
         L     1,0(1)
         LH    2,0(1)
         LTR   2,2
         BZ    NOPARMG
         SR    3,3
         SLL   3,8
         IC    3,2(1)
         LA    1,1(1)
         BCT   2,*-12
         ST    3,FW
         PACK  DW,FW
         CVB   3,DW
         C     3,CENT
         BH    NOPARMG
         ED    ED1,DW+6
         PUT   PRINT,LINEPG
         BAL   14,TPAGE
         ST    3,PCENT
         B     CLOSEP
NOPARMG  PUT   PRINT,LINENP
         BAL   14,TPAGE
CLOSEP   CLOSE (PRINT)
         RDJFCB  (TAPE)
         OI    JFCB+76,128
         MVC   ERR+49(44),JFCB
         MVC   ERRMSG1+1(85),ERR+8
         LA    2,JFCB
         LA    3,44
         LA    4,GIN
CLCG     CLC   0(2,2),DOTG
         BNE   *+14
         CLC   6(2,2),V0
         BE    GFND
         MVC   0(1,4),0(2)
         LA    2,1(2)
         LA    4,1(4)
         BCT   3,CLCG
         MVC   ERRMSG2+24(44),RA
         B     ERR
FIRSTSAV DC    18F'0'
         SPACE
*   GIN = GEN.IND(+1)
*   SGIN = GEN.IND
*   SGENNO = GENERATION NUMBER (ONLY) OF OLD GEMERATION
         SPACE
GFND     MVC   SGIN,GIN
         MVC   SGENNO,1(2)
         MVC   0(4,4),PLUSONE
         LOCATE  ACAM
         LTR   15,15
         MVC   ERRMSG2+24(44),RB
         BC    7,ERR
         MVC   JFCB(44),GIN
         OPEN  (TAPE,(OUTPUT)),TYPE=J
         CLOSE (TAPE,REREAD)
         OPEN  (SYSIN,OUTPUT)
         PUT   SYSIN,DUMPCARD
         CLOSE (SYSIN)
         LINK  EP=IEHDASDR,PARAM=(NOPARM),VL=1
         LR    10,15
         OPEN  (PRINT,OUTPUT)
         LR    15,10
         LTR   15,15
         MVC   ERRMSG2+24(44),RC
         BC    7,ERR
         SPACE
*  HERE WE READ THE WHOLE GENERATION INDEX AND SEARCH FOR THE VOLUME
*  LIST OF THE OLD GENERATION OF 'TAPE' WHICH IS BEING OVER-WRITTEN.
*  WE CANNOT DO A DIRECT CATLG LOOK UP BECAUSE WE DO NOT KNOW THE
*  RELATIVE GEN NO (THE JFCB CONTAINS THE ABSOLUTE DSNAME WHEN WE
*  READ IT)
         SPACE
         LOCATE  BCAM
         MVC   ERRMSG2+24(44),RD
         C     15,TWELVE
         BC    7,ERR
         SR    4,4
         LA    2,LOCAREA+20
COMPGEN  CLC   0(8,2),FFS
         BE    NEXTBLK
         XC    1(4,2),FFS
         CLC   0(8,2),SGENNO
         BE    GENFND
         IC    4,11(2)
         SLL   4,1
         LA    2,12(4,2)
         B     COMPGEN
NEXTBLK  OC    8(3,2),8(2)
         MVC   ERRMSG2+24(44),RE
         BC    8,ERR
         MVC   BLK,8(2)
         LOCATE  CCAM
         LTR   15,15
         MVC   ERRMSG2+24(44),RF
         BC    7,ERR
         LA    2,LOCAREA+2
         B     COMPGEN
GENFND   MVC   VOLLIST,12(2)
         MVC   ENDWTO+28(44),GIN
ENDWTO   WTO   'DUMP SUCCESSFUL FOR                                    *
                               '
         LA    6,LINE2+22
         LA    7,VOLLIST
         LH    5,VOLLIST
         C     5,TWELVE
         BNH   *+8
         L     5,TWELVE
         MVC   LINE1+1(44),ENDWTO+8
         PUT   PRINT,LINE1
         BAL   14,TPAGE
LOOP     LA    6,7(6)
         MVC   0(6,6),6(7)
         MVI   6(6),C','
         LA    7,12(7)
         BCT   5,LOOP
         MVI   6(6),64
         STAE  RETRY,CT,XCTL=NO
         STM   11,12,SAVEBASE
         RDJFCB  (DISK)
         OI    JFCB+52,8
         MVC   JFCB(44),F4
         OPEN  (DISK),TYPE=J
READV    READ  A,SF,DISK,DSCB,'S'
         CHECK A
         LA    3,DSCB
         USING DSCBD,3
B4       BC    0,AFT4
         CLI   DFMT,C'4'
         BNE   AFT4
         MVC   TPC+2(2),DTPC TRACKS PER CUL
         OI    B4+1,240
AFT4     CLI   DFMT,C'1'
         BNE   READV
         TM    DDSORG,2
         BZ    READV
         CLC   DDSN(5),SYSTEM
         BE    READV
         SR    10,10
       MVC       EXTS(30),DEXT
       LA        4,EXTS
         LA    5,3
         BAL   14,SUMEXT
         NC    DNEXT,DNEXT
         BZ    CALCOMP
         OBTAIN  F3
         LTR   15,15
         BZ    GOODOB
         MVC   BADOBNM(44),DDSN
         PUT   PRINT,BADOB
         BAL   14,TPAGE
         B     RESTORE
GOODOB   LA    3,OBTAR
         USING DSCBD,3
         LA    4,DF3EXTA
         LA    5,4
         BAL   14,SUMEXT
         LTR   5,5
         BZ    CALCOMP
       MVC       EXTS,DF3EXTB
       LA        4,EXTS
         LA    5,9
         BAL   14,SUMEXT
CALCOMP  LA    3,DSCB
         LH    8,DLTT
         LA    9,100
         MR    8,8
         LTR   10,10
         BP    *+10
         SR    9,9
         B     *+14
         DR    8,10
         C     9,PCENT
         BH    COMPRESS
         MVC   NCOMDSN(44),DDSN
         CVD   9,DW
         MVC   NONCOMP+27(4),PCMASK
         ED    NONCOMP+27(4),DW+6
         MVI   NONCOMP+28,C'('
         PUT   PRINT,NONCOMP
         BAL   14,TPAGE
         B     READV
COMPRESS DS    0H
         MVC   LJFCB,SAVEJ
         MVC   LJFCB(44),DDSN
         OPEN  (LIB),TYPE=J
         CLOSE (LIB)
         CVD   9,DW
         MVC   CSMSG+27(4),PCMASK
         ED    CSMSG+27(4),DW+6
         MVI   CSMSG+28,C'('
         MVC   CSDSN(44),DDSN
         PUT   PRINT,CSMSG
         BAL   14,TPAGE
         SPMODE  PROB,0
         L     1,16
         TM    116(1),16     TEST FOR MFT OR MVT
         BZ    *+12
         OI    GOMVT+1,240
         OI    MVTBR+1,240
         L     1,0(1)
         L     1,4(1)
         SPACE
GOMVT    BC    0,LOADIT
         SPACE
         LA    1,24(1)
         ST    1,ADMSS
         MVC   STORMSS,0(1)
         L     15,0(1)
         LA    14,FQESTORE
MVFQES   MVC   0(8,14),0(15)
         LA    14,8(14)
         C     14,ENDSTORE
         BH    FQEXIT
         L     15,0(15)
         LTR   15,15
         BC    7,MVFQES
         ST    15,0(14)
         LA    8,44
         LA    9,LJFCB+43
CLIL     CLI   0(9),64
         BNE   ENQSETUP
         BCTR  9,0
         BCT   8,CLIL
         ABEND 4,DUMP
ENQSETUP STC   8,ENQ+5
         STC   8,DEQ+5
ENQ      ENQ   (RTPUPDTE,LJFCB,E,44,SYSTEM)
LOADIT   EQU   *
         SPACE
*  HERE WE GO TO SUPRSHFL (OR POSSIBLY - IN MVT - TO 'LOADSHUF' FOUND
*  UNDER THE NAME OF SUPRSHFL).
         SPACE
         LOAD  EP=SUPRSHFL
         LR    15,0
         LA    1,ADNOPARM
         BALR  14,15
         SPACE
*  ON RETURN DELETE ALL TRACE OF SUPRSHFL AND LOSE ALL ITS CORE.
         SPACE
         DELETE  EP=SUPRSHFL
         SPACE
*  GET PROTECT KEY ZERO AGAIN IN CASE IT HAS BEEN LOST IN MVT LOADSHUF
         SPACE
         SPMODE  PROB,0
MVTBR    BC    0,ALLBACK
DEQ      DEQ   (RTPUPDTE,LJFCB,44,SYSTEM)
         L     1,ADMSS
         MVC   0(4,1),STORMSS
         L     15,0(1)
         LA    14,FQESTORE
MBFQES   MVC   0(8,15),0(14)
         L     15,0(14)
         LA    14,8(14)
         LTR   15,15
         BC    7,MBFQES
ALLBACK  EQU   *
         SPMODE PROB,*
         MVC   COMPCOMP+26(44),DDSN
         PUT   PRINT,COMPCOMP
         BAL   14,TPAGE
         B     READV
EOVTOC   PUT   PRINT,ENDMSGA
         BAL   14,TPAGE
         CATALOG  DCAM
         CVD   15,DW
         LTR   15,15
         BZ    GOODBYE
ERRGO    PUT   PRINT,ERRMSG1
         BAL   14,TPAGE
         PUT   PRINT,ERRMSG2
         BAL   14,TPAGE
         UNPK  ERRMSG3+21(3),DW+6(2)
         OI    ERRMSG3+23,240
         PUT   PRINT,ERRMSG3
         BAL   14,TPAGE
         B     END
ERR      WTO   'DUMP UNSUCCESSFUL TO TAPE DD WITH DSNAME               *
                                                 '
         CVD   15,DW
         B     ERRGO
SUMEXT   NC    0(10,4),0(4)
         BZ    SUMEXIT
         SR    6,6
         LH    7,2(4)
         LTR   7,7
         BZ    *+8
         M     6,TPC
         AH    7,4(4)
         SR    8,8
         LH    9,6(4)
         LTR   9,9
         BZ    *+8
         M     8,TPC
         AH    9,8(4)
         LA    9,1(9)
         SR    9,7
         AR    10,9
         LA    4,10(4)
         BCT   5,SUMEXT
SUMEXIT  BR    14
GOODBYE  PUT   PRINT,ENDMSGB
         PUT   PRINT,LINE2
         WTO   'SUPER-SHUFFLE SUCCESFUL.TAPES CATALOGUED.'
END      CLOSE (PRINT,,DISK)
         GOBACK
TPAGE    SP    LCNT,ONE
         BCR   2,14
         PUT PRINT,NPAGE
         ZAP   LCNT,MAXCNT
         BR    14
         EJECT
         USING *,15
RESTORE  LM    11,12,SAVEBASE
         DROP  15
         LA    13,FIRSTSAV
         STAE  0
         PUT   PRINT,ABMSG
         BAL   14,TPAGE
         MVC   ABWTO+58(44),GIN
ABWTO    WTO   'SUPER-SHUFFLE HAS ABENDED.WILL ATTEMPT TO RESTORE      *
                                                     '
         OPEN  (SYSIN,OUTPUT)
         PUT   SYSIN,RESTCARD
         CLOSE (SYSIN)
         LINK  EP=IEHDASDR,PARAM=NOPARM,VL=1
         LTR   15,15
         BZ    RECOVERD
         WTO   'RESTORE OF TAPE FAILED AFTER SHUFFLE ABENDED. **TAKE UR*
               GENT ACTION**'
         PUT   PRINT,FAILURE
         B     BADEND
RECOVERD PUT   PRINT,RECOVEM
         MVC   RECVWTO+42(44),GIN
RECVWTO  WTO   'RESTORE OF TAPE WAS SUCCESFUL FOR                      *
                                     '
BADEND   CLOSE (DISK,,PRINT)
         GOBACK
         EJECT
         USING *,15
RETRY    MVC   ACCDE,4(1)
         MVC   APSW(8),8(1)
         MVC   APCPSW(8),40
         MVC   AREGS(64),24(1)
         BALR  10,0
         USING *,10
         STM   13,14,ABSAVE
         LA    13,ABSAVE
         TSGSNAP  SDATA=(CB),PDATA=(ALL)
         CLOSE  (TSGSNAPA)
         L     1,16
         TM    116(1),16
         BZ    ENDRETRY      NOT MVT
         LA    8,44
         LA    9,LJFCB+43
CIRL     CLI   0(9),64
         BNE   RENQSETP
         BCTR  9,0
         BCT   8,CIRL
RENQSETP STC   8,RDQ+5
RDQ      DEQ   (RTPUPDTE,LJFCB,44,SYSTEM)
ENDRETRY EQU   *
         LM    13,14,ABSAVE
         L     0,ADREST
         LA    15,4
         BR    14
         DROP  15,10
FQEXIT   STAE  0
         ABEND 99,DUMP
ADREST   DC    A(RESTORE)
ADNOPARM DC    A(NOPARM)
ENDSTORE DC    A(ENDFQES)
ADMSS    DC    F'0'
STORMSS  DC    F'0'
FQESTORE DC    20D'0'
ENDFQES  DC    F'0'
         DC    C'ABEND COMPLETION CODE '
ACCDE    DC    F'0'
         DC    C'ABEND PSW...'
APSW     DC    2F'0'
         DC    C'PROG CHK PSW'
APCPSW   DC    2F'0'
         DC    C'REGS 0 - 15 AT ABEND'
AREGS    DC    16F'0'
         EJECT
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=(E),EXLST=XLIST
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBA,          *
               LRECL=121,BLKSIZE=121
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(PM),RECFM=F,LRECL=80,      *
               BLKSIZE=80
LIB      DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=(E),EXLST=LLIST
DISK     DCB   DDNAME=DISK,DSORG=PS,LRECL=96,BLKSIZE=96,RECFM=F,       *
               KEYLEN=44,EXLST=DLIST,EODAD=EOVTOC,MACRF=(R)
         EJECT
DW       DC    D'0'
ABSAVE   DC    18F'0'
TCBAD    DC    F'0'
TWELVE   DC    F'12'
FTEEN    DC    F'0'
TPC      DC    F'20'
SAVEBASE DC   2F'0'
FW       DC    F'0'
CENT     DC    F'100'
PCENT    DC    F'80'
         CNOP  2,4
NOPARM   DC    H'0'
XLIST    DC    X'87'
         DC    AL3(JFCB)
DLIST    DC    X'87',AL3(JFCB)
LLIST    DC    X'87',AL3(LJFCB)
BLK      DC    XL3'00'
         DS    0H
VOLLIST  DC    XL64'00'
COPYNAME DC    CL44' '
RTPUPDTE DC    C'RTPUPDTE'
DOTG     DC    C'.G'
V0       DC    C'V0'
GIN      DC    CL44' '
JFCB     DC    CL176' '
LJFCB    DC    CL176' '
SAVEJ    DC    CL176' '
PLUSONE  DC    C'(+1)'
SGIN     DC    CL44' '
SGENNO   DC    CL8' '
F4       DC    44X'04'
       DS        0H
DSCB     DS    0CL140
KEY      DC    CL44' '
DATA     DC    CL96' '
       DS        0H
EXTS   DC        XL90'00'
FFS      DC    8X'FF'
PCMASK   DC    X'40202120'
LCNT     DC    P'35'
MAXCNT   DC    P'35'
ONE      DC    P'1'
PKF      DC    X'00'
SYSTEM   DC    C'SYS1.'
         EJECT
FAILURE  DC    CL121'0RESTORE OF TAPE FAILED.  **TAKE URGENT ACTION**'
RECOVEM  DC    CL121'0RESTORE OF TAPE WAS SUCCESSFUL. INVESTIGATE CAUSE*
                OF SHUFFLE FAILURE'
ABMSG    DC    CL121'0SUPER-SHUFFLE HAS ABENDED. WILL ATTEMPT TO RESTOR*
               E TAPE TO DISK.'
BADOB    DC    C'0ERROR IN OBTAINING FORMAT 3 DSCB FOR '
BADOBNM  DC    CL83' '
NONCOMP  DC    C'0COMPRESSION NOT NECESSARY (XX  PERCENT FULL) FOR '
NCOMDSN  DC    CL83' '
COMPCOMP DC    CL121' COMPRESSION COMPLETE FOR '
ENDMSGA  DC    CL121'0END OF VTOC. NO MORE LIBRARIES TO COMPRESS. WILL *
               TRY TO CATLG TAPE(S).'
ENDMSGB  DC    CL121' TAPE(S) SUCCESSFULLY CATALOGUED '
LINEPG   DC    C'1PARM GIVEN FOR SAFEDUMP IS'
ED1      DC    X'40202120'
         DC    CL90' PERCENT FULL (FOR COMPRESSION TO OCCUR)'
LINENP   DC    CL121'1NO PARM (OR INVALID PARM) FOR SAFEDUMP.  DEFAULT *
               OF 80 PERCENT USED FOR COMPRESSION'
DUMPCARD DC    CL80' DUMP  FROMDD=DISK,TODD=TAPE '
RESTCARD DC    CL80' RESTORE TODD=DISK,FROMDD=TAPE,PURGE=YES '
CSMSG    DC    C'0COMPRESSION NOW STARTING  (XX  PERCENT FULL) FOR '
CSDSN    DC    CL83' '
NPAGE    DC    CL121'1'
LINE1    DC    C'1'
         DC    CL120' '
LINE2    DC    C' '
         DC    CL120'NEW GENERATION CATALOGED ON '
RA       DC    CL44'TAPE DD NOT FOR A GEN. DATA GROUP'
RB       DC    CL44'ERROR IN LOCATING +1 GENERATION'
RC       DC    CL44'ERROR IN IEHDASDR'
RD       DC    CL44'ERROR IN LOCATING GENERATION INDEX'
RE       DC    CL44'OLD GENERATION NOT FOUND IN GENERATION INDEX'
RF       DC    CL44'ERROR IN LOCATING OLD GENERATION'
RG       DC    CL44'ERROR IN CATALOGING NEW GENERATION'
ERRMSG1  DC    C'1'
         DC    CL120' '
ERRMSG2  DC    C' '
         DC    CL120'REASON FOR FAILURE WAS '
ERRMSG3  DC    C' '
         DC    CL120'VALUE OF REG 15 WAS '
         SPACE
ACAM     CAMLST  NAME,GIN,,LOCAREA
BCAM     CAMLST  NAME,SGIN,,LOCAREA
CCAM     CAMLST  BLOCK,BLK,,LOCAREA
DCAM     CAMLST  CAT,GIN,,VOLLIST
F3       CAMLST  SEEK,DSCB+135,JFCB+118,OBTAR
         DS    0D
LOCAREA  DS    CL272
OBTAR    DS    0D
         DS    350C
LAST     DC    C'LAST'
         EJECT
DSCBD    DSECT
DDSN     DS    CL44
DFMT     DS    C
         DS    CL15
DLDB     DS    C
         DS    CL21
DDSORG   DS    CL2
DRECFM   DS    C
         DS    CL13
DLTT     DS    CL2
         DS    CL5
DEXT     DS    CL30
DNEXT    DS    CL5
         ORG   DDSN
DF3      DS    CL4
DF3EXTA  DS    CL40
         DS    C
DF3EXTB  DS    CL90
         ORG   DDSN
DF4      DS    CL44
         DS    CL20
DTPC     DS    CL2
         END
