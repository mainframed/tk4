*
* Z817TEST IS USED TO TEST A FLAG DSCB (OF THE FORM ZSYS1.Z817SET.ID)
*          SET BY A PREVIOUS JOB.
*
*          IF THE FLAG EXISTS IT IS SCHEDULED FOR  DELETION AT JOB
*          TERMINATION (BY USING A 'PASS' JFCB ) AND THE STEP
*          TERMINATES NORMALLY.
*
*          IF THE FLAG DOES NOT EXIST  THE STEP WILL ABNORMALLY
*          TERMINATE WITH USER CODE 1XX  (XX = RETURN CODE FROM
*          OBTAIN MACRO).
*
**   AUTHORS - I.C.I. (MOND DIVISION)
*
         TITLE '*** Z817TEST ***'
Z817TEST START
         SAVE  (14,12),,*
         LR    12,15          BASE
         USING Z817TEST,12
         CNOP  0,4
         BAL   15,*+76
         DC    18A(0)         SAVE
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15          CHAIN
         SPACE 3
*        BUILD FLAG NAME FROM PARM INFO
         L     9,0(1)    REG9 -> PARM FIELD
         SR    8,8
         AH    8,0(9)    REG8 = LENGTH OF PARM
         BNP   ERR01
         BCTR  8,0
         EX    8,MOVE
         SPACE
*        GET VOL SER OF VTOC CONTAINING FLAG DSCB
         RDJFCB (TEST)
         CLI   PASSJFCB,0
         BE    ERR02
         MVC   VOLSER,JFCBVOLS
         SPACE
*        SEARCH VTOC FOR FLAG DSCB
         OBTAIN FLAGDSCB
         LTR   15,15
         BNZ   ERR03
         SPACE
*        SCRATCH PASS DATA SET
         DEVTYPE DDNAME,DEVINFO
         MVC   DEVCODE,DEVINFO
         SR    0,0
         SCRATCH PASSDSCB
         LTR   15,15
         BNZ   ERR04
         SPACE
*        'RENAME' PASS JFCB TO FLAG JFCB
         MVC   PASSJFCB(44),FLAGDSN
         OPEN  (TEST),TYPE=J
         CLOSE TEST
         SPACE
RETURN   EQU   *
         L     13,4(13)
         RETURN (14,12),,RC=0
ERR01    EQU   *    NO PARM FIELD SPECIFIED
         ABEND  10,DUMP
ERR02    EQU   *    NO PASS JFCB FOR THIS STEP
         ABEND  20,DUMP
ERR03    EQU   *    OBTAIN FOR FLAG DSCB FAILED
         LA    1,100
         AR    1,15
         ABEND (1),DUMP
ERR04    EQU   *    SCRATCH FOR PASS DSCB FAILED
         LA    1,200
         AR    1,15
         ABEND (1),DUMP
         SPACE 3
*        DATA
MOVE     MVC   FLAGDSN(1),2(9)
FLAGDSCB CAMLST SEARCH,FLAGDSN,VOLSER,WK1
WK1      DS    0D
         DS    148C
PASSDSCB CAMLST SCRATCH,PASSJFCB,,VOLIST
VOLIST   DC    H'1'
DEVCODE  DC    XL4'0'
VOLSER   DC    CL6' ',H'0'
PASSJFCB DS    0D
         DC    XL118'0'
JFCBVOLS DC    XL58'0'
DDNAME   DC    CL8'TEST'
DEVINFO  DC    D'0'
TESTEXIT DS    0F
         DC    X'87',AL3(PASSJFCB)
TEST     DCB    DDNAME=TEST,DSORG=PS,MACRF=(E),EXLST=TESTEXIT
FLAGDSN  DC    CL100' '
         LTORG
         END
