MEMBER NAME  GOBACK
         MACRO
&NAME    GOBACK     &SAVE,&RC=0
         AIF   ('&SAVE' EQ 'NOSAVE').C1
         AIF   ('&SAVE(1)' EQ 'DYNAMIC').C2
&NAME    L     13,4(13,0)          SET GR13 -> PREVIOUS SAVE AREA.
.C3      AIF   ('&RC' EQ '' OR '&RC' EQ '(15)').C3A
         AIF   ('&RC'(1,1) NE '(').C3A
         LR    15,&RC(1)           SET GR15 = RETURN CODE.
.C1B     RETURN     (14,12),T,RC=(15)
         MEXIT
.C3A     RETURN     (14,12),T,RC=&RC
         MEXIT
.C1      AIF   ('&RC' EQ '' OR '&RC' EQ '(15)').C1A
         AIF   ('&RC'(1,1) NE '(').C1A
&NAME    LR    15,&RC(1)           SET GR15 = RETURN CODE.
         AGO   .C1B
.C1A     ANOP
&NAME    RETURN     (14,12),T,RC=&RC
         MEXIT
.C2      ANOP
&NAME    LR    1,13                SET GR1 -> CURRENT SAVE AREA.
         L     13,4(13,0)          SET GR13 -> PREVIOUS SAVE AREA.
         AIF   ('&SAVE(2)' NE '').C4
         LA    0,72(0,0)           SET GR0 = LENGTH OF DYNAMIC AREA.
         AGO   .C5
.C4      LA    0,&SAVE(2)(0,0)     SET GR0 = LENGTH OF DYNAMIC AREA.
.C5      SVC   10                  ISSUE FREEMAIN SVC.
         AGO   .C3
         MEND
