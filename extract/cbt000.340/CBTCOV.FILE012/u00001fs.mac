TSGBCOPY CSECT
*
*  THIS PROGRAM GIVES THE USER OF IEBCOPY PROTECTION AGAINST MULTIPLE
*  CONCURRENT UPDATING OF THE SAME LIBRARY IF HE IS PREPARED TO USE THE
*  DDNAME 'SYSUT2' TO DEFINE THE LIBRARY BEING UPDATED.
*
*  THE PROGRAM FINDS THE DSNAME OF THE LIBRARY DEFINED BY SYSUT2 AND
*  ISSUES AN ENQ FOR EXCLUSIVE USE WITH THE MAJOR NAME 'RTPUPDTE' AND
*  USES THE DSNAME AS THE MINOR NAME.
*
*  THIS METHOD GIVES COMMON PROTECTION FOR USERS OF RESPOND VERSIONS OF
*  IEWL, IEBUPDTE, TSGUPDAT, AND IEBCOPY.
*
*  IT IS NOT POSSIBLE TO PROTECT AGAINST USE OF IEBCOPY IF LIBRARIES
*  ARE DEFINED BY OTHER DDNAMES THAN SYSUT2 ( AND THE REL 19 VERSION
*  ALLOWS ANY NUMBER OF LIBRARIES TO BE DEFINED USING ANY COLLECTION
*  OF DDNAMES ).  SINCE THE PROTECTION CANNOT BE ENFORCED THIS PROGRAM
*  LINKS TO IEBCOPY WITHOUT ISSUING AN ENQ IF IT DOES NOT FIND DDNAME
*  SYSUT2.
*
*         BEGIN
*  EXPANSION OF THE RESPOND TSG BEGIN MACRO FOLLOWS
          STM  14,12,12(13)
          BALR 12,0
          USING     *,12
          CNOP 0,4
          BAL  15,*+76
          DC   18F'0'
          ST   15,8(13)
          ST   13,4(15)
          LR   13,15
*  END OF EXPANSION OF BEGIN
         LR    10,1
         L     2,16          CVT
         L     2,0(2)        TCB DW
         L     2,4(2)        TCB
         L     3,12(2)       TIOT
         LA    3,24(3)
SRCHTIOT SR    6,6
LOOPT    IC    6,0(3)
         LTR   6,6
         BZ    GOLINK
         CLC   4(8,3),SYSUT2
         BE    FOUND
         AR    3,6
         B     LOOPT
FOUND    LH    9,18(3)       TEST FOR DUMMY
         LTR   9,9
         BZ    GOLINK
         RDJFCB  (DCB)
         LA    7,44
         LA    8,JFCB+43
*  ESTABLISH LENGTH OF DSNAME EXCLUDING TERMINAL SPACES
CLI      CLI   0(8),64
         BNE   ENQSETUP
         BCTR  8,0
         BCT   7,CLI
         ABEND  4,DUMP       DSNAME IS ALL SPACES
ENQSETUP STC   7,ENQ+5       PUT LENGTH IN MACRO
         STC   7,DEQ+5
ENQ      ENQ   (RTPUPDTE,JFCB,E,44,SYSTEM)
LINK     LR    1,10          RESTORE PARM ADDRESS
         LINK  EP=IEBCOPY
         ST    15,FIFTEEN
NODEQ    BC    0,L15
DEQ      DEQ   (RTPUPDTE,JFCB,44,SYSTEM)
L15      L     15,FIFTEEN
*         GOBACK  RC=(15)
*  EXPANSION OF RESPOND TSG GOBACK MACRO FOLLOWS
GOBACK    L    13,4(13)
          L    14,12(13)
          LM   0,12,20(13)
          MVI  12(13),255
          BR   14
*  END OF EXPANSION OF GOBACK
GOLINK   OI    NODEQ+1,240
         B     LINK
DCB      DCB   MACRF=(E),DDNAME=SYSUT2,EXLST=XLIST
XLIST    DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
RTPUPDTE DC    C'RTPUPDTE'
FIFTEEN  DC    F'0'
SYSUT2   DC    CL8'SYSUT2'
         END
