*
Z18COPY  SETUP 13,EQUATE=YES
*
         L     R3,0(R1)       R3 PTS. TO PARM FIELD
         RDJFCB SYSUT1
         MVC   DSNAME(44),JFCB
         MVC   INDSN(14),JFCB
         RDJFCB SYSUT2
         MVC   OUTDSN(14),JFCB
OPENCOPY OPEN  (COPYIN,(OUTPUT))
         TIME  DEC
         ST    1,PDATE
         UNPK  DATE(5),PDATE+1(3)
         ST    0,PTIME
         UNPK  TIME(5),PTIME(3)
         MVI   TIME+4,C' '
         EJECT
         TM    SWITCH,X'20'
         BO    COMPRESS
*
         CLC   0(2,R3),=H'0'
         BE    CARD1          STRAIGHT COPY
         CLC   0(2,R3),=H'1'
         BNE   CLCCOMPR
         CLI   2(R3),C'R'     CHECK FOR REPLACE
         BE    CARD2          REPLACEMENT COPY
         CLI   2(R3),C'C'     CHECK FOR COMPRESS
         BE    COMPRESS
CLCCOMPR CLC   2(8,R3),=C'COMPRESS'     CHECK FOR COMPRESS
         BE    COMPRESS
         LH    R4,0(R3)            LOAD LENGTH
         CLC   2(2,R3),=C'R,'      TEST FOR REPLACE
         BNE   COPY
         MVC   OUTLINE(30),=C'  COPY I=((SYSUT1,R)),O=SYSUT2'
         BAL   11,PUT
         LA    R3,2(R3)
         BCTR  R4,0
         BCTR  R4,0                DECREASE BY 2
LTR44    LTR   R4,R4               ONLY  ,R SO INVALID
         BZ    INVPARM
         BCTR  R4,0                DECREASE BY 1 TO GET EXECUTE LENGTH
         EX    R4,MOVEPARM         MOVE PARM INTO PARMAREA
         LA    1,PARMAREA          PT  R1 TO CARD IMAGE
         OI    SWITCH,X'40'        SET PARM SWITCH
         B     PARMLAB
COPY     MVC   OUTLINE(30),=C'  COPY I=SYSUT1,O=SYSUT2      '
         BAL   11,PUT
         B     LTR44
*
COMPRESS MVC   OUTLINE(30),=C'  COPY I=SYSUT1,O=SYSUT1      '
         MVC   OUTDSN(14),INDSN         SET OUTDSN
         MVC   JFCB(44),DSNAME          SET JFCB FOR ENQ
         BAL   11,PUT
         B     CLCOPYIN                 CLOSE INPUT FILE THEN LINK
         EJECT
CARD1    MVC   OUTLINE(30),=C'  COPY I=SYSUT1,O=SYSUT2      '
         BAL   11,PUT
         B     OPEN
CARD2    MVC   OUTLINE(30),=C'  COPY I=((SYSUT1,R)),O=SYSUT2'
         BAL   11,PUT
         B     OPEN
*
OPEN     TM    SWITCH,X'80'
         BO    GETCARD
         OPEN  (SYSIN,(INPUT))
         TM    SYSIN+48,X'10'
         BZ    CLCOPYIN
GETCARD  GET   SYSIN
PARMLAB  LR    R3,R1
         LA    R4,9
LOOP1    CLI   0(R3),C' '
         BE    NOLAB
         LA    R3,1(R3)
         BCT   4,LOOP1
NOLAB    LA    R4,70
         LA    R3,1(R3)
LOOP2    CLI   0(R3),C' '
         BNE   FUNCTEST
         LA    R3,1(R3)
         BCT   R4,LOOP2
MOVECARD MVC   OUTLINE(80),0(R1)
         BAL   11,PUT
         TM    SWITCH,X'40'
         BO    CLCOPYIN
         B     GETCARD
FUNCTEST CLC   0(6,R3),=C'SELECT'
         BE    MOVECARD
         CLC   0(2,R3),=C'S '
         BE    MOVECARD
         B     INVCARD
*
         EJECT
EOSYSIN  CLOSE SYSIN
CLCOPYIN CLOSE COPYIN
         ENQ   (QNAME,RNAME,E,44,SYSTEM)
         LINK  EP=IEBCOPY,PARAM=(PARMADDR,DDADDR),VL=1
         ST    15,CONCODE
         DEQ   (QNAME,RNAME,44,SYSTEM)
         L     15,CONCODE
         LTR   15,15
         BNZ   END
         OPEN  (PRINT,(INPUT))
GETPRINT GET   PRINT
         CLC   1(7,1),=C'IEB154I'   CHECK SUCCESSFUL?
         BE    CLOSE
         CLC   1(7,1),=C'IEB155I'  CHECK SUCCESSFUL
         BNE   GETPRINT
*
CLOSE    CLOSE PRINT
         LA    15,0
END      L   13,4(13)
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
EOPRINT  CLOSE PRINT
         L     13,4(13)
         LM    14,12,12(13)
         LA    15,4
         BR    14
         EJECT
EXLST    DC    X'07'
         DC    AL3(JFCB)
JFCB     DC    44F'0'
CONCODE  DS    F
PDATE    DS    F
PTIME    DS    F
QNAME    DC    C'SYSIEWL '
RNAME    EQU   JFCB
DSNAME   DS    CL44
PARMADDR DC    H'0'
DDADDR   DC    H'40'
         DC    32C'0'
         DC    C'COPYIN  '    ALTERNATE NAME FOR IEBCOPY INPUT
PARMAREA DC    80C' '              CARD IMAGE FIELD
OUTLINE  DC    30C' '         30
         DC    C'  I='        34
INDSN    DS    CL14           49   ASSUME MAX 14 CHAR DSN
         DC    C' O='         52
OUTDSN   DS    CL14           66   ASSUME MAX 14 CHAR DSN
         DC    2C' '          67
TIME     DS    CL4            73
         DC    4C' '          75
DATE     DS    CL5            80
SWITCH   DC    X'00'
ABEND    ABEND 1,DUMP
INVCARD  ABEND 3,DUMP
MOVEPARM MVC   PARMAREA(0),2(R3)
INVPARM  ABEND 2,DUMP
PUT      PUT   COPYIN,OUTLINE      WRITE IEBCOPY INPUT FILE
         BR    11                  RETURN
         PRINT ON,NOGEN
PRINT    DCB   DDNAME=SYSPRINT,EODAD=EOPRINT,MACRF=(GL),DSORG=PS
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=(R),EXLST=EXLST
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PO,MACRF=(R),EXLST=EXLST
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),EODAD=EOSYSIN
COPYIN   DCB   DDNAME=COPYIN,DSORG=PS,MACRF=(PM)
         END
CLCOPYIN CLOSE COPYIN
LINK     ENQ   (QNAME,RNAME,E,44,SYSTEM)
