         TITLE 'JOB RE-RUNABILITY ROUTINE  -  MGP014. (PDS VERSION).'
MGP014  START
*
**    AUTHORS - ICI (MOND DIVISION)
**
***
****                         PRODUCE SCRATCH & UNCATLG CARDS
*****                        FOR INPUT TO THE SCRATCH PROCEDURE
******                       FOR GENERATION DATA SETS.
*******                 E.G. INPUT FORMAT   AG.PPRI(-9) TO
********                                    AG.PPRI(+9)
********                     CONVERTED TO   AG.PPRI.G0001V00  TO
*******                                     AG.PPRI.G0019V00
******                       OUTPUT WILL BE AN UNCATLG CARD AND,
*****                        IF THE VOLUME IS DIRECT-ACCESS,
****                         A SCRATCH CARD FOR 3330 OR 2314.
***
**       MGP014 IS BASED ON HGP014 BUT USES A PDS MEMBER AS THE
**        OUTPUT DATA SET TO RECEIVE THE IEHPROGM CARD IMAGES.
**       THE MEMBER IS UPDATED INPLACE USING OPEN - UPDAT AND BSAM
**       READ/WRITE BLOCK.  THE PDS MEMBER MUST EXIST BEFORE FIRST
**        ACCESS BY THE ROUTINE.  CARD IMAGES ARE OUTPUT WITH SEQUENCE
**        NUMBERS STARTING AT 00000001 AND INCREMENTING BY 1 IN CC73-80
*
         SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         ST    13,SAVE+4
         LR    15,13
         LA    13,SAVE
         ST    13,8(15)
         B     A1
SAVE     DS    18F
A1       OPEN  (PRINT,(OUTPUT))
         OPEN  (DISKE,(UPDAT))
         OPEN  (CARDS,(INPUT))
         PUT   PRINT,MSG1
         USING IHADCB,10
         LA    10,DISKE
         MVC   BLKSIZE,DCBBLKSI
*
**
*
A2       GET   CARDS,INAREA       READ CONTROL CARD
A21      B     FIRST
         MVC   MSG3(1),BLANK
         MVC   MSG3+1(132),MSG3
         NI    NEWGEN,X'00'        SET NEW GDG INDIC. OFF
         MVC   DSET(40),INAREA    MOVE DSNAME FOR LOCATE MACRO
         LA    3,DSET             MOVE START ADDRESS TO R3
*
         TRT   0(40,3),TABLE      SEARCH FOR (
         BC    8,ERROR1           NO ( FOUND
         ST    1,GENLOC      SAVE POINTER FOR NEW GENERATION USE
         MVC   0(4,1),CONST       MOVE (0) FOR 1ST LOCATE
         LOCATE THENAME
         LTR   15,15              IF LOCATE OK  R15  WILL
         BZ    A5                  CONTAIN ZEROES
         CH    15,=H'16'
         BH    A3                  SERIOUS ERROR
         CH    15,=H'4'
         BE    A3                  SYSCTLG NOT AVAILABLE
*     DSNAME(0) NOT FOUND  -  ASSUME NEW GDG.
         MVC   DSET(40),INAREA
         L     1,GENLOC
         CLI   1(1),C'+'
         BE    ACCNGDG
         MVC   MSG3+1(80),INAREA
         MVC   MSG3+85(36),=C'NEW GDG ASSUMED BUT NOT + GENERATION'
         PUT   PRINT,MSG3
         ABEND 666
ACCNGDG  DS    0H
         MVC   MSG3(25),MSG2
         MVC   MSG3+25(44),DSET
         MVC   MSG3+69(23),=C'  -  NEW GDG ASSUMED.  '
         PUT   PRINT,MSG3
         MVC   MSG3+1(132),MSG3
         OI    NEWGEN,X'FF'        SET NEW GDG INDICATOR
         B     A5             GO TO CHECK FOR VOL=SER/REF SPECIFIED
A3       CVD   15,REG15
         UNPK  MSG2+93(8),REG15   FROM LOCATE MACRO
         OI    MSG2+100,X'F0'
         MVC   MSG2+25(44),DSET   DATA SET NAME NOT FOUND
         PUT   PRINT,MSG2
         ABEND 888
*
FIRST    CLC   INAREA+76(4),SPACE+2
         BNE   SECND
         MVC   INAREA+76(4),NORT
         B     THIRD
SECND    MVC   X4(4),INAREA+76    MOVE TO FULLWORD
         L     0,X4               TO REG 0
         N     0,KF0              CHECK FOR CORRECT ZONES
         C     0,KF0
         BNE   ERROR2
         L     0,X4               RELOAD IF ZONES OK
         AL    0,KF15
         N     0,KF10             CHECK FOR VALID NUMERIC BITS
         BNZ   ERROR2
         B     THIRD
ERROR2   EQU   *
         MVC   MSG6+62(4),INAREA+76
         PUT   PRINT,MSG6
         B     FIRST+10
X4       DC    F'0'
KF0      DC    X'F0F0F0F0'
KF15     DC    X'15151516'
KF10     DC    X'10101010'
MSG6     DS    0D
         DC    CL133' COND.CODE IN 77-80 OF 1ST CONTROL CARD NOT NUMERIC
               C OR SPACES(    ).REPLACED BY 0000'
THIRD    PACK  REG15+4(4),INAREA+76(4)     SET UP RETURN CODE
         MVI   A21+1,X'00'         NO-OP 1ST TIME BRANCH
         B     A21+4
*
A8       MVC   DSET(40),INAREA       RESTORE D/SET NAME TO INPUT
         TM    NEWGEN,X'FF'        NEW GDG ?
         BZ    A8A                 NO.ALLOW MISSING VOL=SER/REF
         L     1,GENLOC            YES - PICK UP REL GDG LOC'N IN DSET
         MVC   GOOVOO+5(1),2(1)    STORE GENERATION VALUE FROM INPUT
         MVC   0(9,1),GOOVOO       SET ABSOLUTE GENERATION NO IN DSET
*     CHECK FOR EXISTENCE OF GENERATION INDEX BY CATALOG/UNCATALOGUE
         CATALOG NEWGDG
         LTR   15,15               SUCCESSFUL ?
         BNZ   CATFAIL             NO
         CATALOG REMOVE
         LTR   15,15               OK ?
         BZ    A8B                 YES- BYPASS LOCATE ON (+1) ETC.
         MVC   MSG2+1(6),=C'UNCATL'
         B     A3                  GO TO ABEND 888
CATFAIL  MVC   MSG2+1(6),=C' CATLG'
         B     A3                  GO TO ABEND 888
A8A      LOCATE THENAME              FORMAT AND LOCATE IN CATLG
         LTR   15,15
         BNZ   A3                    LOCATE FAILED
A8B      MVC   OUTAREA(1),BLANK
         MVC   OUTAREA+1(79),OUTAREA
         MVC   OUTAREA(18),UNCAT     CREATE UNCATALOGUE RECORD
         MVC   OUTAREA+18(40),DSET
         AP    CARDIDNT,=P'1'
         UNPK  OUTAREA+72(8),CARDIDNT
         OI    OUTAREA+79,X'F0'
         MVC   MSG3+1(80),INAREA         PRINT CONTROL
         PUT   PRINT,MSG3                  CARD
         MVC   MSG3+1(80),OUTAREA        PRINT UNCATALOGUE
         PUT   PRINT,MSG3                  RECORD
*
*
*
*   UPDATE INPLACE (READ/WRITE BLOCK) ON PDS MEMBER
*
A9       CLC   CURBLKSZ,=H'0'      BLOCK READ BUT NOT YET WRITTEN ?
         BNE   LOCLREC             YES - AVOID READ
         READ  UPDECB,SF,DISKE,UPDAR,'S',MF=E
         CHECK UPDECB
         MVI   UPDIND,X'0F'        INDICATE BLOCK READ BUT NOT WRITTEN
         MVC   UPDARPTR,=A(UPDAR)    PTR TO CURRENT REC IN BLOCK
         LH    5,BLKSIZE           MAX. BLKSIZE
         L     6,UPDECB+16         IOB ADDRESS
         SH    5,14(6)             R5=BLK LENGTH OF CURRENT BLOCK
         STH   5,CURBLKSZ
LOCLREC  L     4,UPDARPTR
         LH    5,CURBLKSZ
         MVC   0(80,4),OUTAREA   MOVE RECORD TO U/D AREA CURRENT REC
         LA    4,80(4)             POINT TO NEXT LOCATION IN BLOCK
         ST    4,UPDARPTR          SAVE POINTER
         SH    5,=H'80'            REDUCE CURRENT AVAILABLE SPACE
         STH   5,CURBLKSZ          SAVE VALUE
         BP    A10                 NOT YET EMPTY
         WRITE UPDECB,SF,DISKE,UPDAR,'S',MF=E
         CHECK UPDECB
         MVI   UPDIND,X'F0'        INDICATE BLOCK WRITTEN BACK TO DISK
         B     A10
*
UPEND    DS    0H
         TM    CRDEOF,X'FF'     ENTRY FROM CARD EOF ROUTINE ?
         BC    1,CLOSUP            YES - RETURN IMMEDIATELY
         MVC   MSG3+1(45),=C'EOF ON PDS MEMBER BEFORE CARD INPUT COMPLET
               TE '
         MVC   MSG3+46(87),MSG3+45
         PUT   PRINT,MSG3
         ABEND 777
*
A10      DS    0H
         CLC   VOLSER,SPACE         IS THIS A DISK DATA SET
         BE    A2                   IF NOT READ NEXT INPUT CARD
         MVC   OUTAREA(1),BLANK
         MVC   OUTAREA+1(79),OUTAREA
         MVC   OUTAREA(20),SCRTCHA   CREATE SCRATCH CARD
         CLC   LOCAREA+2(4),PSEUDO+2        IS IT A 3330 ?
         BE    *+10
         MVC   OUTAREA+15(4),=C'2314'
         MVC   OUTAREA+20(6),VOLSER     FOR DISK
         MVC   OUTAREA+26(8),SCRTCHB             DATA
         MVC   OUTAREA+34(40),DSET                    SET
         AP    CARDIDNT,=P'1'
         UNPK  OUTAREA+72(8),CARDIDNT
         OI    OUTAREA+79,X'F0'
         MVC   VOLSER,SPACE
         MVC   MSG3+1(80),OUTAREA     PRINT SCRATCH
         PUT   PRINT,MSG3               RECORD
         B     A9                  OUTPUT CARD IMAGE TO DISK
*
*     PROCESS VOL=SER/VOL=REF FIELDS
A5       CLC   INAREA+40(8),CONSTA     IS THERE A VOL SER NO
         BNE   A6                         IN THE CONTROL CARD
         MVC   VOLSER(6),INAREA+48        IF YES STORE IT
         B     A8
A6       CLC   INAREA+40(8),CONSTB     IS THERE A VOL REF IN
         BE    A7                         THE CONTROL CARD
         TM    NEWGEN,X'FF'        NEW GDG ?
         BZ    A6A                 NO - ACCEPTABLE OMMISSION
A6ERR    MVC   MSG3+1(80),INAREA
         MVC   MSG3+85(26),=C'VOL=REF MISSING OR INVALID'
         PUT   PRINT,MSG3
         ABEND 555
A6A      TM    LOCAREA+4,X'80'     DEVTYPE = TAPE ?
         BO    A8                  YES (I.E. NEW GDG)
         MVC   VOLSER(6),LOCAREA+6     ELSE STORE VOLSER EX CATLG
         B     A8
A7       MVC   DSET(28),INAREA+48      OBTAIN VOL SER NO FROM
         LOCATE THENAME                CATALOGUE FOR DATA SET
         LTR   15,15                   USED IN VOL=REF PARAMETER
         BZ    A6A                 FOUND OK
         TM    NEWGEN,X'FF'        NEW GDG ?
         BO    A6ERR               YES  - UNACCEPTABLE ERROR
         MVC   MSG3+1(80),INAREA
         MVC   MSG3+85(27),=C'VOL=REF NOT FOUND - IGNORED'
         PUT   PRINT,MSG3
         MVC   MSG3+1(132),MSG3
         B     A8
*
*
ERROR1   MVC   MSG3+1(44),DSET        DATA SET NAME WITHOUT L/H
         PUT   PRINT,MSG3         BRACKET (
         PUT   PRINT,MSG4
         ABEND 999
*
*
LSTCARD  DS    0H                  END OF JOB ROUTINE
*
*          TEST FOR END OF CARDS BEFORE END OF PDS MEMBER
         OI    CRDEOF,X'FF'        SET CARD EOF INDICATOR
         CLI   UPDIND,X'0F'        PARTIAL UPDATE OUTSTANDING ?
         BE    LSTUPD              YES
         CLI   UPDIND,X'00'        OUTPUT D/SET ACCESSED YET ?
         BNE   ENDUPRD             YES
         MVI   MSG3,X'40'
         MVC   MSG3+1(132),MSG3
         MVC   MSG3+1(39),=C'NO CARD INPUT - OUTPUT DATA SET CLEARED'
         PUT   PRINT,MSG3
ENDUPRD  READ  UPDECB,SF,DISKE,UPDAR,'S',MF=E
         CHECK UPDECB
         MVC   UPDARPTR,=A(UPDAR)    PTR TO CURRENT REC IN BLOCK
         LH    5,BLKSIZE           MAX. BLKSIZE
         L     6,UPDECB+16         IOB ADDRESS
         SH    5,14(6)             R5=BLK LENGTH OF CURRENT BLOCK
         STH   5,CURBLKSZ
ENDLOOP  L     4,UPDARPTR
         MVI   0(4),X'40'
         MVC   1(71,4),0(4)        SET UP NULL RECORD
         AP    CARDIDNT,=P'1'
         UNPK  72(8,4),CARDIDNT
         OI    79(4),X'F0'
         LA    4,80(4)             POINT TO NEXT LOCATION IN BLOCK
         ST    4,UPDARPTR          SAVE POINTER
         SH    5,=H'80'            REDUCE CURRENT AVAILABLE SPACE
         BP    ENDLOOP             NOT YET EMPTY
         WRITE UPDECB,SF,DISKE,UPDAR,'S',MF=E
         CHECK UPDECB
         B    ENDUPRD
LSTUPD   LH    5,CURBLKSZ
         B     ENDLOOP
CLOSUP   DS    0H
         PUT   PRINT,MSG5
         CLOSE (CARDS)
         CLOSE (DISKE)
         CLOSE (PRINT)
OUT      L     13,SAVE+4
         CVB   15,REG15
         RETURN (14,12),,RC=(15)
*
         READ  UPDECB,SF,DISKE,UPDAR,'S',MF=L
         DCBD  DSORG=PS,DEVD=DA
MGP014   CSECT
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FA,             X
               LRECL=133,BLKSIZE=133,BFTEK=S
*
DISKE    DCB   DDNAME=OUTPUT,DSORG=PS,EODAD=UPEND,MACRF=(R,W)
*
CARDS    DCB   DSORG=PS,MACRF=GM,DDNAME=INPUT,EODAD=LSTCARD
*
MSG1     DS    0D
         DC    CL133' ***  MGP014- PDS VERSION CATALOGUE/VTOC HOUSEKEEPX
               ING UTILITY.    STARTED'
MSG2     DS    0D
         DC    CL25' LOCATE UNSUCCESSFUL FOR '
         DS    CL44
         DC    CL24'REG.15 HAS COND.CODE OF '
         DC    CL8' '
         DC    CL32' '
MSG3     DS    0D
         DS    CL133
MSG4     DS    0D
         DC    CL133' L/H BRACKET NOT FOUND IN D/SET NAME'
MSG5     DS    0D
         DC    CL133' ***  MGP014  ENDED'
NEWGDG   CAMLST CAT,DSET,,PSEUDO
REMOVE   CAMLST UNCAT,DSET
THENAME  CAMLST NAME,DSET,,LOCAREA
DSET     DC    CL44' '
*   VOLUME LIST FOR CATALOG ROUTINE IF NEW GDG ASSUMED
PSEUDO   DC    H'1'
         DC    X'30502009'
         DC    CL6'DISKVL'
         DC    H'0'
LOCAREA  DS    0D
         DS    265C
OUTAREA  DS    CL80                    CARD IMAGE OUTPUT
INAREA   DS    CL80                    CARD INPUT
TABLE    DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'00000000000000000000000000000000'
         DC    X'00010000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
         DC    X'0000000000000000000000000000000000000000'
*
         LTORG
*
**
*
CONST    DC    C'(0) '            MEANING CURRENT GENERATION
BLKSIZE  DS    H
CURBLKSZ DC    H'0'
CARDIDNT DC    PL2'0'
UPDARPTR DS    F
REG15    DS    0D
         DC    PL8'0'
SPACE    DC    CL6'      '
NORT     DC    CL4'0000'
UNCAT    DC    CL18'  UNCATLG  DSNAME='
VOLSER   DC    CL6'      '        STORE FOR VOL.SER.NO IF DISK
SCRTCHA  DC    CL20'  SCRATCH  VOL=3330='   USED TO CREATE
SCRTCHB  DC    CL08',DSNAME='                   SCRATCH RECORD
CONSTA   DC    CL8'VOL=SER='                TO CHECK CONTROL CARD
CONSTB   DC    CL8'VOL=REF='
CRDEOF   DC    X'00'        'END OF CARD INPUT' INDICATOR
UPDIND   DC    X'00'        'STATE OF UPDATE' INDICATOR   -  MEANINGS
*              X'00'-NOT READ YET/X'0F'-READ BUT NOT YET WRITTEN/
*              X'F0'-READ AND WRITTEN
BLANK    DC    CL1' '
NEWGEN   DC    X'00'
GOOVOO   DC    C'.G0000V00'
GENLOC   DS    F
UPDAR    DS    39CL80
         END
