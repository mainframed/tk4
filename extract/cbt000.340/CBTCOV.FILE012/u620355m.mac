./       ADD   SEQFLD=774,LIST=ALL,NAME=U620355M
         MACRO
&SAVEC   SAVEC &NOCSECT,&N=1,&B=,&REQ=
         LCLA  &A1,&A2,&A3,&A4
         LCLC  &C1(12),&C2(12)
         AIF   ('&N' GT '0').SAVEC
         MNOTE 'NO BASE REGISTERS REQUESTED'
         MEXIT
.SAVEC   AIF   ('&N' LT '13').SAVEC1
         MNOTE 'MORE THAN 12 BASE REGISTERS REQUESTED'
         MEXIT
.SAVEC1  AIF   (N'&B LT 13).SAVEC2
         MNOTE 'MORE THAN 12 BASE REGISTERS SPECIFIED'
         MEXIT
.SAVEC2  AIF   ('&NOCSECT' EQ '').SAVEC3
&SAVEC   DS    0H
         AGO   .SAVEC4
.SAVEC3  ANOP
&SAVEC   CSECT
.SAVEC4  AIF   ('&REQ' EQ 'NO').SAVEC5
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.SAVEC5  ANOP
&A1      SETA  1
&A2      SETA  13
&C1(1)   SETC  '13'
.SAVEC6  AIF   (&A1 EQ &N).SAVEC7
&A1      SETA  &A1+1
&A2      SETA  &A2-1
&A3      SETA  &A3+1
&C1(&A1) SETC  '&A2'
&C2(&A1) SETC  ',&A2'
         AIF   ('&B(&A3)' EQ '').SAVEC6
&C1(&A1) SETC  '&B(&A3)'
&C2(&A1) SETC  ',&B(&A3)'
         AGO   .SAVEC6
.SAVEC7  CNOP  0,4
         B     32(0,15)
         DC    XL1'1B'
         AIF   ('&SAVEC' EQ '').SAVEC7A
         DC    CL11'&SAVEC'
         AGO   .SAVEC7B
.SAVEC7A DC    CL11' '
.SAVEC7B DC    CL16' &SYSTIME &SYSDATE '
         STM   14,12,12(13)
         LR    14,13
         BALR  15,0
         BAL   13,84(15)
         USING *,13&C2(2)&C2(3)&C2(4)&C2(5)&C2(6)&C2(7)&C2(8)&C2(9)&C2(C
               10)&C2(11)&C2(12)
         DC    18F'0'
         AIF   ('&SAVEC' EQ '').SAVEC7C
         DC    CL8'&SAVEC'
         AGO   .SAVEC7D
.SAVEC7C DC    CL8' '
.SAVEC7D ST    13,8(14)
         ST    14,4(13)
&A1      SETA  1
.SAVEC8  AIF   (&A1 EQ &N).SAVEC9
&A1      SETA  &A1+1
&A4      SETA  &A4+1
         LA    &C1(&A1),4095(&C1(&A4))
         LA    &C1(&A1),1(&C1(&A1))
         AGO   .SAVEC8
.SAVEC9  MEND
