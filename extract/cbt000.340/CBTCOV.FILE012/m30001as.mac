         MACRO
         SAVEAREA   &POOL=5
         SPACE 3
         LTORG
SAVEAREA CSECT
         ENTRY SAVEUP,SAVEDOWN
SAVEDOWN EQU   *
         USING *,15
         USING SAVDSECT,13
         CLC   SAVARAD,=F'0'  IS A NEW SAVEAREA AVAILABLE
         BE    SAVERROR       .... NO
         MVC   SAVD13B,SAVARAD     ESTABLISH FORWARD SAVEAREA CHAINING
         ST    13,SAVFULLW         STORE CURRENT SAVEAREA ADDRESS
         L     13,SAVARAD          POINT 13 TO NEW SAVEAREA
         MVC   SAVD13A,SAVFULLW    ESTABLISH BACKWARD SAVEAREA CHAINING
         SP    SAVESIZE,=P'1'      REDUCE SAVEAREA COUNT BY 1
         BNP   SAVEMPTY            .... BRANCH IF NOW EMPTY
         LA    13,72(13)           POINT TO NEXT AREA ON POOL
SAVEHW72 EQU   *-2                 PREVIOUS HALFWORD IS CONSTANT H'72'
         ST    13,SAVARAD          STORE AS POINTER TO NEXT SAVEAREA
         SH    13,SAVEHW72         RESET TO POINT TO NEWEST  SAVEAREA
         BR    14             RETURN ON REGISTER 14
SAVEMPTY XC    SAVARAD,SAVARAD     CLEAR NEXT SAVEAREA POINTER
         BR    14             RETURN ON REGISTER 14
         DROP  15
         SPACE 3
SAVEUP   EQU   *
         USING *,15
         ST    13,SAVARAD          STORE CURRENT VALUE OF 13
         AP    SAVESIZE,=P'1'      INCREMENT COUNT OF SAVEAREAS
         L     13,SAVD13A          LOAD PREVIOUS SAVEAREA ADDRESS
         LM    14,12,SAVD14        RESTORE CALLING MODULE REGISTERS
         BR    14                  RETURN
         DROP  15
         SPACE 3
SAVERROR EQU   *         ENTERED IF REQUEST MADE AND NO SAVEAREAS LEFT
         BALR  12,0      ESTABLISH BASE REGISTER THAT MAY BE USED WITH
         USING *,12           OS SYSTEM MACROS.
         WTO   'SAVEDOWN CAUSED JOB TERMINATION - REQUEST MADE FOR A SAX
               VEAREA WHEN NO AREA AVAILABLE.',ROUTCDE=11
         ABEND 0,DUMP
         SPACE 3
         LTORG
         SPACE 3
SAVESIZE DC    P'&POOL'            NUMBER OF SAVEAREAS ON THE POOL
SAVARAD  DC    A(SAVEPOOL)         ADDRESS OF NEXT SAVEAREA
SAVFULLW DS    F                   WORK AREA
SAVEPOOL DS    &POOL.CL72          SAVEAREA POOL
         SPACE 3
SAVDSECT DSECT
SAVDPL1  DS    F                   WORD 1    USED BY PL1
SAVD13A  DS    F                   WORD 2    POINTER TO UPPER SAVEAREA
SAVD13B  DS    F                   WORD 3    POINTER TO LOWER SAVEAREA
SAVD14   DS    F                   WORD 4    STORE REGISTER 14
SAVD15   DS    F                   WORD 5    STORE REGISTER 15
SAVD0    DS    F                   WORD 6    STORE REGISTER 0
SAVD1    DS    F                   WORD 7    STORE REGISTER 1
SAVD2    DS    F                   WORD 8    STORE REGISTER 2
SAVD3    DS    F                   WORD 9    STORE REGISTER 3
SAVD4    DS    F                   WORD 10   STORE REGISTER 4
SAVD5    DS    F                   WORD 11   STORE REGISTER 5
SAVD6    DS    F                   WORD 12   STORE REGISTER 6
SAVD7    DS    F                   WORD 13   STORE REGISTER 7
SAVD8    DS    F                   WORD 14   STORE REGISTER 8
SAVD9    DS    F                   WORD 15   STORE REGISTER 9
SAVD10   DS    F                   WORD 16   STORE REGISTER 10
SAVD11   DS    F                   WORD 17   STORE REGISTER 11
SAVD12   DS    F                   WORD 18   STORE REGISTER 12
         SPACE 3
SAVEAREA CSECT
         DROP  12,13
&SYSECT  CSECT
         SPACE 3
         MEND
