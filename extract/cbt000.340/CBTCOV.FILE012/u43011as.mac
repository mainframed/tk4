*        GENERAL DESCRIPTION
*        THE ZZENQDEQ SUBROUTINE IS USED TO OBTAIN EXCLUSIVE CONTROL OF
*        A DATA SET BY ENQUEUING ON THE DATA SET NAME. THE SUBROUTINE
*        CANNOT BE CALLED BY A COBOL PROGRAM.
*        THE SUBROUTINE MUST BE CALLED TWICE IF IT IS USED. THE FIRST
*        TIME TO ENQ ON THE DATA SET THE SECOND TIME TO DEQ.
*        CALLING PROCEDURE
*        TO ENQ
*              LA    4,DDNAME
*              CALL  ZZENQDEQ,((4),ENQNAME)
*        DDNAME  DS CL8            MUST CONTAIN THE DDNAME OF THE FILE
*        ENQNAME DS CL44           USED BY THE ZZENQDEQ SUBROUTINE
*        TO DEQ
*              LA    4,0
*              CALL  ZZENQDEQ,((4),ENQNAME)
ZZENQDEQ CSECT
         IN    12,PARM=YES
         LM    2,3,0(1)
         LTR   2,2
         BZ    B01                 DEQ
         SPACE 5
**       READ JFCB TO GET DATA SET NAME
         SPACE 2
A01      EQU   *
         USING IHADCB,4            ADDRESSING FOR DCBD
         LA    4,IN
         MVC   DCBDDNAM,0(2)       STACK IN DDNAME
         RDJFCB (IN)
         CLC   JFCB(3),=C'SYS'     TEST SYSTEM NAME
         BNE   A10
         MVC   JFCB(3),=C'DYD'     REPLACE BY DYD
A10      EQU   *
         MVC   0(44,3),JFCB        STORE NAME IN AREA PASSED
         SPACE 5
**       ENQ   ON DATASET NAME
         SPACE 2
         ENQ   ((3),(3),E,44,SYSTEM)
         B     C01                 EXIT
         SPACE 5
**DEQ   ON ATASET NAME
         SPACE 2
B01      EQU   *
         DEQ   ((3),(3),44,SYSTEM)
         SPACE 5
**       EXIT ROUTINE
         SPACE 2
C01      EQU   *
         OUT   0
IN       DCB   MACRF=GL,DSORG=PS,EXLST=X
         DS    0F
X        DC    X'87',AL3(JFCB)
JFCB     DS    CL176
         LTORG
         PRINT NOGEN
         DCBD  DSORG=PS
         END
