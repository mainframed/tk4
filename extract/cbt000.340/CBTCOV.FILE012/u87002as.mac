+ THIS PROGRAM IS VERY USEFUL FOR BATCH JOBS ACCESSING VSAM FILES
+ DEFINED WITH SHAREOPTIONS(2). IN OUR CICS SYSTEM SUCH FILES ARE
+ ALLOCATED DISP=SHR AND OPEN FOR OUTPUT. TO PROCESS IN BATCH THE
+ OPERATOR SHOULD CLOSE THE FILE USING 'CEMT S DA(....) CLO' - IF HE
+ FORGETS THE BATCH PROGRAM WILL FALL OVER. THIS PROGRAM SITS ON THE
+ FRONT OF SUCH JOBS TO ENSURE THAT THE FILE HAS BEEN CLOSED.
+
NCZ93101 TITLE 'CHECK TO SEE IF VSAM DATASET IS AVAILABLE              *
               - PROGRAM DOCUMENTATION'
*****                                                             *****
*                                                                     *
* ID:          NCZ93101 - VSAM DATASET AVAILABILITY CHECK             *
*                                                                     *
* NCZ93101.1 PROGRAM DESCRIPTION                                      *
*                                                                     *
* UP TO 9 DD STATEMENTS VSAM1-VSAM9 ARE CHECKED TO SEE IF THE VSAM    *
* DATASET IS AVAILABLE FOR INPUT/OUTPUT. IF THE FILE IS NOT AVAILABLE *
* A MESSAGE IS WRITTEN TO THE OPERATOR ASKING HIM TO EITHER TO RETRY  *
* OR END THE RUN.                                                     *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93101.2 USER INSTRUCTIONS                                        *
*                                                                     *
* REQUIRED JCL -                                                      *
* //STEPNAME EXEC PGM=NCZ93101                                        *
* //STEPLIB  DD  DISP=SHR,DSN=MAC1.LOAD                               *
* //VSAMX    DD  DISP=SHR,DSN=VSAM.DATASET                            *
*  -UP TO 9 DD STATEMENTS - X=1-9                                     *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93101.3 MESSAGES                                                 *
*                                                                     *
* MC3011D - ASK OP WHAT TO DO IF A FILE IS NOT AVAILABLE              *
*           (SEE MAC1.INFO(MESSAGES))                                 *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93101.4 ABEND CODES                                              *
*                                                                     *
* 2000 - RUN ABORTED DUE TO OPERATOR REQUEST (OR MODCB FAILED)        *
*                                                                     *
*****                                                             *****
         EJECT
*****                                                             *****
*                                                                     *
* NCZ93101.5 IMPLEMENTATION METHOD                                    *
*                                                                     *
* NORMAL ASSEMBLY AND LINK-EDIT                                       *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93101.6 AMENDMENT HISTORY                                        *
*                                                                     *
* MAINTAIN THE RECORD BELOW.                                          *
*                                                                     *
* CHANGE HISTORY:                                                     *
* DATE      INITS VERSION COMMENTS                                    *
*                                                                     *
* 17 DEC 81 EMM   V01     CREATED                                     *
* 19 AUG 82 EMM   V02     USE MODCB TO ALTER ACB FOR DDNAME           *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93101.7 MODULARISATION DETAILS                                   *
*                                                                     *
* NO UNUSUAL STRUCTURE                                                *
* USES VSAM MACROS - OPEN, CLOSE, ACB, MODCB                          *
* NOTE THAT THE ACB IS ALTERED TO SPECIFY THE DDNAME                  *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* ID:          NCZ93101 - VSAM DATASET AVAILABILITY CHECK             *
*                                                                     *
* RETURN CODES:RC = 0  ALWAYS                                         *
*                                                                     *
* REG USAGE:   R0 -                                                   *
*              R1 -                                                   *
*              R2 -                                                   *
*              R3 -                                                   *
*              R4 -                                                   *
*              R5 -                                                   *
*              R6 -                                                   *
*              R7 -                                                   *
*              R8 -                                                   *
*              R9                                                     *
*              R10 -                                                  *
*              R11 -                                                  *
*              R12 - BASE REGISTER                                    *
*              R13 -                                                  *
*              R14 -                                                  *
*              R15 - RETURN CODES FROM MACROS, EP ADDRESS             *
*                                                                     *
*****                                                             *****
         EJECT
         PRINT NOGEN
NCZ93101 MSETUP 12
         ZAP   CT,=P'0'
CONT     CP    CT,=P'9'               CHECK MAX OF 9 FILES
         BE    END
         AP    CT,=P'1'
         OI    CT,15
         UNPK  DIGIT,CT
*                                     PUT DDNAME INTO ACB
         MODCB ACB=ACB,DDNAME=(*,DDN)
         LTR   R15,R15
         BNZ   AB                     ABEND IF MODCB FAILS
RETRY    OPEN  (ACB)
         C     R15,=F'8'              WAS OPEN OK
         BL    OK                     - YES
         SHOWCB ACB=ACB,AREA=ERROR,LENGTH=4,FIELDS=(ERROR)
         CLC   ERROR,=F'128'          IS DD MISSING
         BE    CONT                   - YES, IGNORE
         CLC   ERROR,=F'148'          IS DSN NONVSAM
         BE    NOTVSAM                - YES
         CLC   ERROR,=F'188'
         BE    NOTVSAM                - YES
         CLC   ERROR,=F'168'          IS FILE AVAILABLE
         BNE   AB
         MVC   WTOR1+24(8),DDN
ASKOP    XC    RECB,RECB
WTOR1    WTOR  'MC3011D: --------: FILE NOT AVAILABLE - REPLY ''R'' TO *
               RETRY OR ''E'' TO END',REPLY,1,RECB
         WAIT  ECB=RECB,LONG=YES
         CLI   REPLY,C'R'
         BE    RETRY
         CLI   REPLY,C'E'
         BNE   ASKOP
         B     AB
NOTVSAM  MVC   WTO2+16(8),DDN
WTO2     WTO   'MC3012I: --------: DATASET IS NONVSAM AND HAS BEEN IGNO*
               RED'
         B     CONT
OK       CLOSE  (ACB)
         B     CONT
END      MCLEAR RC=0
*
AB       ABEND 2000
*
         LTORG
ACB      ACB   AM=VSAM,DDNAME=VSAM1,MACRF=(IN,OUT)
RECB     DC    F'0'
ERROR    DC    F'0'
DDN      DC    CL8'VSAM1'
DIGIT    EQU   DDN+4,1
CT       DC    P'0'
REPLY    DC    C' '
         END   NCZ93101
