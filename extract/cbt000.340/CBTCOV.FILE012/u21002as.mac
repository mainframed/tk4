CALCF5   CSECT
*   AUTHORS - I.B.M. (?)  SUBMITTED BY I.C.I. (MOND DIVISION)
*
*  BEFORE RUNNING THIS PROGRAM ENSURE THAT...
*        1.  THE NUMBER OF SEPARATE EXTENTS ON THE VOLUME WHOSE SPACE
*              IS TO BE RE-CALCULATED DOES NOT EXCEED 256 - IF IT DOES
*              YOU MUST SCRATCH SOME DATA SETS BEFORE RUNNING CALCF5.
*        2.  THE NUMBER OF FREE AREAS (AFTER SCRATCHING DONE FOR 1)
*              DOES NOT EXCEED 26 - IF IT DOES YOU MUST ALLOCATE THE
*              SPACE USING E.G..........
*        //MOPUP DD DSN=MPPUP,DISP=(,KEEP),VOL=...,UNIT=...,
*        //    SPACE=(TRK,1,,ALX)
*              NOTE THAT ALX GETS YOU THE FIVE LARGEST AREAS, AND YOU
*              CAN MOP UP FIVE FREE AREAS PER DD CARD OF THIS FORM
*        3.  THE VOLUME WHOSE FREE SPACE IS TO BE RE-CALCULATED IS A
*              2311, 2314, OR 2302.  THE CONSTANTS FOR OTHER TYPES ARE
*              NOT INCLUDED IN THIS VERSION.
*
*  TO RUN THIS PROGRAM ONLY ONE DD CARD IS REQUIRED TO DEFINE THE
*  VOLUME (DDNAME IS SYSVTOC)  E.G........
*        //SYSVTOC DD VOL=...,UNIT=...,DISP=SHR
*
*  THIS PROGRAM WILL......
*        1.  OPEN THE VTOC ON THE VOLUME SPECIFIED.
*        2.  ENQ ON THIS VTOC TO PREVENT O.S. FROM ALTERING IT.
*        3.  BUILD A LIST OF ALL THE ALLOCATED AREAS DESCRIBED IN THE
*              EXTENT FIELDS OF F1, F3, AND F4 DSCBS.
*        4.  SORT THE LIST BY DISK ADDRESS.
*        5.  CREATE A LIST OF THE CORRESPONDING FREE AREAS.
*        6.  CREATE ONE (ONLY) F5 DSCB AND WRITE IT BACK TO THE DISK
*              ADDRESS OF THE FIRST (OR ONLY) F5 FOUND WHEN READING.
*        7.  DEQ FROM THE VTOC.
*
*  IN TWO CASES WHERE THIS PROGRAM WAS USED IT WAS ALSO NECESSARY TO
*  WRITE A SIMPLE PROGRAM TO OVERWRITE ONE OR MORE DSCBS WITH ZEROS
*  THIS CAN BE DONE BY XDAP WI AS USED IN THE F5 WRITE BACK IN THIS
*  PROGRAM.  THE CASES WERE....
*        1.  AN F3 DSCB (DESCRIBING 100 CYLINDERS) HAD BEEN LEFT ON
*              DISK WITHOUT AN F1 TO OWN IT.
*        2.  MULTIPLE F5 DSCBS EXISTED CONTAINING RUBBISH.  ALL BUT
*              THE FIRST WERE REMOVED AFTER CALCF5.
*
*        BEGIN BASE=(11,12)
*  THIS IS THE EXPANSION OF THE RESPOND TSG BEGIN MACRO
         STM   14,12,12(13)
         BALR  11,0
         USING *,11,12
IHB1     EQU   *
         L     12,IHB1A
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         B     IHB1B
IHB1A    DC    A(IHB1+4096)
IHB1B    DS    0H
         RDJFCB  (IN)
         MVC   SERIAL,JFCB+118
         MVC   JFCB(44),FORMAT4
         OPEN  (IN),TYPE=J
         L     2,IN+44         REG2 POINTS DEB
         LH    2,34(2)         REG2 POINTS TO UCB
         CLC   18(2,2),D3330
         BNE   TEST2314
         MVC   HIGH,HIGH3330
         MVC   FORTYSIX,FS3330
         MVI   NF5CLI+1,X'12'
         MVI   CALCLOWT+3,X'12'
         B     RDJ
TEST2314 EQU   *
         CLC   18(2,2),D2314
         BNE   TEST2302
         MVC   FORTYSIX,FS2314
         MVI   NF5CLI+1,X'13'
         MVI   CALCLOWT+3,X'13'
         B     RDJ
TEST2302 CLC   18(2,2),D2302
         BNE   TEST2311
         MVC   HIGH,HIGH2302
         MVC   FORTYSIX,FS2302
         MVI   NF5CLI+1,X'2D'
         MVI   CALCLOWT+3,X'2D'
         B     RDJ
TEST2311 CLC   18(2,2),D2311
         BE    RDJ
NOTREC   WTO   'DEVICE FOR CALCF5 IS NOT 2311/2314/2302'
         ABEND 12,DUMP
RDJ      EQU   *
         RDJFCB  (OUT)
         MVC   JFCB(44),FORMAT4
         OPEN  (OUT,(OUTPUT)),TYPE=J
         ENQ  (SYSVTOC,SERIAL,E,6,SYSTEM)
         LA    10,EXTLIST
GET      READ  ECBA,SF,IN,DSCB,'S'
         CHECK ECBA
         CLI   ID,C'1'
         BE    F1
         CLI   ID,C'3'
         BE    F3
         CLI   ID,C'4'
         BE    F1
         CLI   ID,C'5'
         BE    F5FND
NEXTREC  C     10,LIMIT
         BL    GET
         ABEND 2,DUMP
F1       LA    1,F1EXT
         LA    2,3
F1L      CLC   0(8,1),ZERO
         BE    NEXTREC
         MVC   0(8,10),0(1)
         LA    10,8(10)
         LA    1,10(1)
         BCT   2,F1L
         B     NEXTREC
F3       LA    1,F3EXT1
         LA    2,4
F3L1     CLC   0(8,1),ZERO
         BE    NEXTREC
         MVC   0(8,10),0(1)
         LA    10,8(10)
         LA    1,10(1)
         BCT   2,F3L1
         LA    1,F3EXT2
         LA    2,9
F3L2     CLC   0(8,1),ZERO
         BE    NEXTREC
         MVC   0(8,10),0(1)
         LA    10,8(10)
         LA    1,10(1)
         BCT   2,F3L2
         B     NEXTREC
F5FND    MVC   F5DISKAD,IN+5
         MVC   F5FND(4),BNEXTREC
BNEXTREC B     NEXTREC
EOF      MVC   S1(8),HIGH
         LA    10,EXTLIST
NEXTE    LA    1,S1
         LA    2,S2
SORT     CLC   0(8,10),ZERO
         BE    SORTED
         CLC   0(8,10),0(1)
         BL    ALOW
AHIGH    MVC   0(8,2),0(1)
         LA    2,8(2)
         LA    1,8(1)
         B     SORT
ALOW     MVC   0(8,2),0(10)
         LA    10,8(10)
MOVES1   LA    2,8(2)
         MVC   0(8,2),0(1)
         LA    1,8(1)
         CLC   0(8,1),ZERO
         BNE   MOVES1
MOVEBACK MVC   S1(256),S2
         MVC   S1+256(256),S2+256
         MVC   S1+512(256),S2+512
         MVC   S1+768(256),S2+768
         MVC   S1+1024(256),S2+1024
         MVC   S1+1280(256),S2+1280
         MVC   S1+1536(256),S2+1536
         MVC   S1+1792(256),S2+1792
         B     NEXTE
SORTED   LA    10,ZERO+4
         LA    2,8
         LA    3,F5L1
NEXTF5   LH    9,4(10)
         LH    8,0(10)
         SR    9,8
         BZ    SAMECYL
         BCTR  9,0
         SR    8,8
NF5CLI   CLI   3(10),X'09'
         BE    TESTHITT
CALCLOWT LA    8,9
         SH    8,2(10)
TESTHITT CLI   7(10),X'00'
         BE    TESTFORE
CALCHITT AH    8,6(10)
TESTFORE LTR   9,9
         BC    2,ENTERF5
         LTR   8,8
         BC    2,ENTERF5
         LA    10,8(10)
         B     SAMEF5
STEPONF5 LA    10,8(10)
         BCT   2,SAMEF5
         LA    2,18
         LA    3,F5L2
         MVC   *-8(4),BABEND
SAMEF5   CLC   0(4,10),HIGH
         BNE   NEXTF5
WRITEF5  XDAP  ECBX,WI,OUT,F5,140,,F5DISKAD
         WAIT  ECB=ECBX
         CLI   ECBX,X'7F'
         BE    *+12
         MVI   ABEND4+7,X'08'
         B     ABEND4
         DEQ  (SYSVTOC,SERIAL,6,SYSTEM)
         CLOSE (IN,,OUT)
*        GOBACK
*  THIS IS THE EXPANSION OF THE RESPOND TSG GOBACK MACRO
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
BABEND   B     ABEND4
ABEND4   ABEND 4,DUMP
SAMECYL  LH    8,6(10)
         SH    8,2(10)
         BCTR  8,0
         LTR   8,8
         BC    10,TESTFORE
         SR    8,8
         B     TESTFORE
ENTERF5  LH    7,0(10)
         MH    7,FORTYSIX
         AH    7,2(10)
         LA    7,1(7)
         STH   7,HWA
         STH   9,HWB
         MVC   0(4,3),HWA
         STC   8,4(3)
         LA    3,5(3)
         B     STEPONF5
IN       DCB   DDNAME=SYSVTOC,LRECL=96,KEYLEN=44,BLKSIZE=96,           *
               EXLST=XLIST,EODAD=EOF,MACRF=(R),RECFM=F,DSORG=PS
OUT      DCB   DDNAME=SYSVTOC,MACRF=(E),EXLST=XLIST,DSORG=DA
         DS    0F
XLIST    DC    X'87'
         DC    AL3(JFCB)
JFCB     DC    CL176' '
FORTYSIX DC    H'10'
HWA      DC    H'0'
HWB      DC    H'0'
F5DISKAD DC    D'0'
DSCB     DS    0CL140
KID      DS    CL4
         DS    CL2
F3EXT1   DS    CL38
ID       DS    C
         DS    CL2
F3EXT2   DS    CL60
F1EXT    DS    CL28
         DS    CL5
         DS    0F
ZERO     DC    XL8'00'
S1       DC   256XL8'00'
S2       DC   256XL8'00'
EXTLIST  DC   256XL8'00'
ENDEXTL  DS    0C
LIMIT    DC    A(ENDEXTL)
HIGH     DC    X'00C8000000C80000'
SYSVTOC  DC    C'SYSVTOC'
SERIAL   DC    CL6' '
FORMAT4  DC    44X'04'
F5       DS    0CL140
         DC    4X'05'
F5L1     DC    X'0001'
         DC    XL38'00'
F5ID     DC    C'5'
F5L2     DC    XL95'00'
FS2314   DC    H'20'
FS2302   DC    H'46'
FS3330   DC    H'19'
HIGH2302 DC    X'00F6000000F60000'
HIGH3330 DC    X'0194000001940000'
D2314    DC    X'2008'
D2311    DC    X'2001'
D2302    DC    X'2004'
D3330    DC    X'2009'
         END
