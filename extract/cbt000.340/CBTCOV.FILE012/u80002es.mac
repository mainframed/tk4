*********PKV1FRMT*****************************************
FORMAT   CSECT
         COPY  REG
         KSAVE 12,*
         PRINT NOGEN
         OPEN  (TERMIDS,,DATA,OUTPUT)    OPEN DATASETS
NEXTERM  EQU   *
         GET   TERMIDS,CARD              GET INPUT CARD FOR NEXT TERM
         MVC   TERM(4),CARD              MOVE TERMID TO OUTPUT RECORD
         MVC   LIMIT,CARD+5              SAVE NO OF RECS FOR THIS TERM
         MVC   TENTHOU(5),=CL5'00000'    INIT COUNT TO ZERO (IN CHAR)
         SR    R2,R2                     INITIALISE
         IC    R2,=XL1'F0'                  COUNT
         LR    R3,R2                           REGISTERS
         LR    R4,R2                              TO
         LR    R5,R2                                 CHARACTER
         LR    R6,R2                                    ZERO
         PACK  DWORD+5(3),LIMIT(5)
         CVB   R7,DWORD
         BCTR  R7,0
         ST    R7,JOBDATA+19
         PUT   DATA,RECORD               PUT CONTROL RECORD
         MVC   JOBDATA+19(4),JOBDATA+25  BLANK OUT JOBDATA FIELD AGAIN
ADDONE   EQU   *
         LA    R2,1(,R2)                 INCREMENT UNITS
         STC   R2,UNITS                     DIGIT OF COUNT
         C     R2,=XL4'000000FA'         UNITS > 9 ??
         BL    OK                        NO
         MVI   UNITS,X'F0'               YES - SET UNITS
         IC    R2,UNITS                     DIGIT TO ZERO
         LA    R3,1(,R3)                 INCREMENT TENS
         STC   R3,TENS                      DIGIT OF COUNT
         C     R3,=XL4'000000FA'         TENS > 9 ??
         BL    OK                        NO
         MVI   TENS,X'F0'                YES - SET TENS
         IC    R3,TENS                      DIGIT TO ZERO
         LA    R4,1(,R4)                 INCREMENT HUNDREDS
         STC   R4,HUNDS                     DIGIT OF COUNT
         C     R4,=XL4'000000FA'         HUNDREDS > 9 ??
         BL    OK                        NO
         MVI   HUNDS,X'F0'               YES - SET HUNDREDS
         IC    R4,HUNDS                     DIGIT TO ZERO
         LA    R5,1(,R5)                 INCREMENT THOUSANDS
         STC   R5,THOU                      DIGIT OF COUNT
         C     R5,=XL4'000000FA'         THOUSANDS > 9 ??
         BL    OK                        NO
         MVI   THOU,X'F0'                YES - SET THOUSANDS
         IC    R5,THOU                       DIGIT TO ZERO
         LA    R6,1(,R6)                 INCREMENT TEN-THOUSANDS
         STC   R6,TENTHOU                   DIGIT OF COUNT
OK       EQU   *
         PUT   DATA,RECORD               PUT OUT RECORD
         CLC   TENTHOU(5),LIMIT          REACHED LIMIT ??
         BL    ADDONE                    NO - GO DO NEXT RECORD
         B     NEXTERM                   YES - GO DO NEXT TERMINAL
         SPACE
ENDJOB   EQU   *                         END OF INPUT DATA = EOJ
         CLOSE (TERMIDS,,DATA)           CLOSE FILES
         KRETN 0                         RETURN
         SPACE
RECORD   DS    0CL142
TERM     DC    CL4'TRZ1'
TENTHOU  DC    CL1'0'
THOU     DC    CL1'0'
HUNDS    DC    CL1'0'
TENS     DC    CL1'0'
UNITS    DC    CL1'0'
JOBDATA  DC    CL133' '
CARD     DC    CL80' '
LIMIT    DS    CL5
DWORD    DC    D'0'
         SPACE
DATA     DCB   DSORG=PS,DDNAME=DATA,MACRF=(PM),RECFM=FB,               *
               LRECL=142,BLKSIZE=1420
TERMIDS  DCB   DSORG=PS,DDNAME=INPUT,MACRF=(GM),RECFM=FB,              *
               LRECL=80,EODAD=ENDJOB
*SNAP DCB DDNAME=SNAP,DSORG=PS,RECFM=VBA,MACRF=(W),
*               LRECL=125,BLKSIZE=1632
         END   FORMAT
