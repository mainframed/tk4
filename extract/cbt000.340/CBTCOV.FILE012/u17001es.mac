S60027   CSECT
         ENTRY MVSCHECK
*
*        ANALYZE VSAM RETURN CODE FROM ASSEMBLER PROGRAM.
*
*        PARAMETER AREA...              F     R15 CONTENTS
*                                       F     A(RPL) OR A(ACB)
*                                       CL8   MACRO TYPE (O/C/G/P)
*                                               PLUS PROGRAM LABEL
*                                       CL2   R15 CONTENTS (NN)       *
*                                       CL4   RETURN CODE (NNNN)      *
*
*                                       (* - DENOTES RETURNED BY THIS
*                                           SUBROUTINE)
*
         PRINT NOGEN
         EJECT
MVSCHECK EQU   *
         MBEGIN
         L     R3,0(R1)                 R3 = A(PARAM LIST)
         L     R1,0(R3)                 R1 = R15 R/C
         CVD   R1,DWORK                 CONVERT TO DECIMAL
         UNPK  WORK3,DWORK+6(2)         UNPACK TO WORK3
         OC    WORK3,NUMERIC
         MVC   16(2,R3),WORK3+1         STORE FOR PROGRAM
         L     R1,4(R3)                 PUT R1 = RPL OR ACB
         CLI   8(R3),C'O'               TEST IF 'OPEN' (ACB)
         BE    VSOC                     YES, BRANCH
         CLI   8(R3),C'C'               TEST IF 'CLOSE' (ACB)
         BNE   VSGP                     NO, BRANCH
VSOC     EQU   *
         SHOWCB ACB=(R1),AREA=FDBK,LENGTH=4,FIELDS=(ERROR)
         B     SHOWN                    ACB ERRORS NOW DISPLAYED
VSGP     EQU   *
         SHOWCB RPL=(R1),AREA=FDBK,LENGTH=4,FIELDS=(FDBK)
SHOWN    EQU   *
         L     R1,FDBK                  R1 = ERROR CODE
         CVD   R1,DWORK                 CONVERT TO DECIMAL
         UNPK  DWORK(5),DWORK+5(3)      UNPACK
         OC    DWORK(5),NUMERIC         ENSURE NUMERIC
         MVC   18(4,R3),DWORK+1         MOVE TO PROGRAM
         MFINISH
*
         EJECT
R1       EQU   1
R3       EQU   3
DWORK    DS    D
FDBK     DS    F
NUMERIC  DC    5X'F0'
WORK3    DS    CL3
*
*
         LTORG
*
*
         END
