./     ADD     LIST=ALL,LEVEL=01,NAME=SPGMCRO
./     NUMBER  NEW1=10,INCR=10
         EJECT
*
*                                  THIS MACRO PRODUCES A BCTR TABLE
*                                  OF THE ABEND CODES PRODUCED BY THIS
*                                  MODULE.  20 VALUES IS MAXIMUM.
*
         MACRO
         SPGCODES &RANGE=,&VALUES=,&DUMP=YES,&ABREG=2,&LABEL=ABENDCOD
         LCLA  &ABENDCD                 UPPER ABEND CODE VALUE
         LCLA  &INDEX
.*                                      CHECK RANGE OF VALUES
         AIF   (N'&RANGE NE 2).MNOTE
         AIF   ((&RANGE(2)-&RANGE(1)) GT 20).MNOTE
.*                                      CHECK NUMBER OF VALUES
         AIF   (&VALUES LT 1 OR &VALUES GT 20).MNOTE
&INDEX   SETA  &VALUES
&ABENDCD SETA  &RANGE(2)                SET UPPER CODE VALUE
.*                                      CONSTRUCT TABLE
.TABLOOP ANOP
ABEND&INDEX BCTR  R&ABREG,0
&INDEX   SETA  &INDEX-1
         AIF   (&INDEX NE 1).TABLOOP
ABEND1   EQU   *
         WTO   'SPG011I SPGSHARE CATASTROPHE',ROUTCDE=(1,11)
         AIF   ('&DUMP' NE 'YES').NODUMP
         LA    R1,X'800' .              INDICATE
         SLL   R1,20 .                  DUMP
         OR    R1,R2 .                  REQUIRED
         SVC   13 .                     TERMINATE
         AGO   .INIT
.NODUMP  LR    R1,R2 .                  SET R1 FOR ABEND
         SVC   13 .                     TERMINATE
.INIT    ANOP
&LABEL   LA    R&ABREG,&ABENDCD
         MEXIT
.MNOTE   MNOTE 4,'INVALID RANGE OR VALUE SPECIFICATION'
         MEND
