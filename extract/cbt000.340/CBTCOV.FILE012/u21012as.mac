CTLG     TITLE 'ROUTINE TO CATLG LATEST MEMBER OF A GDG WITH OPTIONAL RX
               ENAME FACILITY'
         ISEQ  77,80
*        TO BE CALLED FROM A PL/1 (OPT.COMPILER) PROGRAM
*          BY THE STATEMENT   -
*                                                                     *
*              CALL ZAJCTLG(DSNAME,VOLUME);                           *
*              OR                                                     *
*              CALL ZAJCTLG(DSNAME,VOLUME,TYPE);                      *
*              OR
*              CALL ZAJCTLG(DSNAME,VOLUME,TYPE,OLDNAME)
*                                                                     *
*          WHERE DSNAME IS A CHARACTER STRING CONTAINING THE GDG      *
*            INDEX NAME AND VOLUME IS A CHARACTER STRING OF LENGTH    *
*            6 CONTAINING THE REQUIRED VOLUME SERIAL NUMBER. THE      *
*            NEXT FREE GENERATION (I.E. +1) OF THE GDG IS CATALOGUED  *
*            AS EXISTING ON THE SPECIFIED (TAPE) VOLUME.              *
*            OR 2314/3330 IF SO SPECIFIED IN THE TYPE PARAMETER.
*          OPTIONALLY, A DISK DATA SET CAN BE RENAMED FROM THE OLDNAME
*            (CHARACTER STRING) TO THAT SPECIFIED IN THE FIRST
*            PARAMETER (PLUS GENERATION NO.). WHEN USING THIS OPTION,
*            'TYPE' OF 3330 (OR 2314 AS APPLICABLE) MUST BE SPECIFIED.
*                                                                     *
**    AUTHORS - ICI (MOND DIVISION)
ZAJCTLG  PLISAV SAVLEN=SLEN
         LM    2,3,0(1)            LOAD PARAMETER ADDRESSES.
         LR    4,1                 SAVE PARAMETER LIST ADDRESS
*              DEAL WITH DSNAME PARAMETER.
         L     5,0(,2)             DSNAME ADDRESS.
         LH    6,4(2)              DSNAME LENGTH
         BCTR  6,0                 PREPARE FOR EXECUTE.
         MVC   DSNAME(44),=CL44' ' BLANK OUT AREA.
         EX    6,MOV1              COPY IN DSNAME.
         LA    6,DSNAME+1(6)       ADD RELATIVE
         MVC   0(4,6),=C'(+1)'       GENERATION NUMBER.
*              DEAL WITH VOLUME PARAMETER.
         L     5,0(,3)             VOLUME ADDRESS
         MVC   VOLUME(6),0(5)      COPY IN VOLUME.
*              OBTAIN ABSOLUTE GENERATION NAME.
         MVC   CAML(16),ABSGEN
         LA    1,DSNAME
         ST    1,CAML+4
         LA    1,AREA
         ST    1,CAML+12
         LOCATE CAML
         LTR   3,15                CHECK RETURN CODE
         BZ    LOCOK               BRANCH IF OK.
         MVC   MSG+11(7),=C' LOCATE'
         B     ABEND
*              PREPARE PARAMETERS FOR CATALOG.
LOCOK    EQU   *
         MVC   VOLIST(6),=X'000130008001'
         MVC   SEQNO(2),=H'1'
         LR    5,4                 SAVE PARM LIST POINTER
*                   CHECK FOR VOLUME TYPE
         TM    4(4),X'80'          IS TYPE SPECIFIED ?
         BO    TYPOK               BRANCH IF NOT
         L     4,8(,4)             POINT AT TYPE SDV
         L     4,0(,4)             POINT AT TYPE
         CLC   0(4,4),=C'2400'
         BE    TRYREN              TAPE IS DEFAULT
         CLC   0(4,4),=C'2314'
         BNE   NOT2314
         MVC   VOLIST+2(4),=X'30C02008'
TYPDA    MVC   SEQNO(2),=H'0'      SEQUENCE ALWAYS 0 FOR DA.
         B     TRYREN
NOT2314  EQU   *
         CLC   0(4,4),=C'3330'
         BNE   NOT3330
         MVC   VOLIST+2(4),=X'30502009'
         B     TYPDA
NOT3330  EQU   *
*        (OTHER DEVICE TYPES MAY BE ADDED HERE)
*                   UNRECOGNISED VOLUME TYPE
         MVC   M1+35(4),0(4)       COPY INCORRECT TYPE
         PLIMSG MD1
ERMES    EQU   *
         MVC   46(2,12),=H'16'     SET RETURN CODE
         B     BACK
*                  PROCESS RENAME OPTION
TRYREN   TM    8(5),X'80'          IS RENAME REQUIRED ?
         BO    TYPOK               NO -  BRANCH
         L     7,12(5)
         L     5,0(7)              OLDNAME ADDRESS POINTER
         LH    6,4(7)              OLDNAME LENGTH
         BCTR  6,0                 -1
         MVC   OLDNAME(44),=CL44' '    CLEAR AREA
         EX    6,MOV2              COPY IN OLDNAME
         MVC   CAML(16),RENAME
         LA    1,OLDNAME
         ST    1,CAML+4            OLDNAME
         LA    1,DSNAME
         ST    1,CAML+8            NEWNAME
         LA    1,VOLIST
         ST    1,CAML+12
         RENAME CAML
         LTR   3,15                CHECK RETURN CODE
         BZ    TYPOK               BRANCH IF OK.
         MVC   MSG+11(7),=C' LOCATE'
         MVC   M2+35(44),0(5)      OLDNAME TO ERROR MESSAGE
         PLIMSG MD3
         B     ABEND
*              CATALOG THE NEW GENERATION.
TYPOK    EQU   *
         MVC   CAML(16),NEWGEN
         LA    1,DSNAME
         ST    1,CAML+4
         LA    1,VOLIST
         ST    1,CAML+12
         CATALOG CAML
         LTR   3,15                CHECK RETURN CODE
         BZ    BACK                BRANCH IF OK.
         MVC   MSG+11(7),=C'CATALOG'
*              ERROR IN LOCATE OR CATALOG - PRODUCE MESSAGE
ABEND    EQU   *
         SRL   3,2                 DIVIDE BY 4
         IC    3,CODES-1(3)
         STC   3,MSG+33
         PLIMSG MD2
         B     ERMES
*              JOB DONE - TIDY UP AND RETURN.
BACK     EQU   *
         L     13,SAV+4
         RETURN (14,12)
*
MOV1     MVC   DSNAME(0),0(5)
MOV2     MVC   OLDNAME(0),0(5)
DUMMY    EQU   *                   FOR USE IN CAMLST MACROS.
ABSGEN   CAMLST NAME,DUMMY,,DUMMY  SHOULD BE NAME,DSNAME,,AREA
NEWGEN   CAMLST CAT,DUMMY,,DUMMY   SHOULD BE CAT,DSNAME,,VOLIST
RENAME   CAMLST RENAME,DUMMY,DUMMY,DUMMY  (RENAME,OLD-,NEW-,VOLIST)
MD1      DC    A(M1),AL2(40),AL2(0)
MD2      DC    A(MSG),AL2(34),AL2(0)
MD3      DC    A(M2),AL2(80),AL2(0)
CODES    DC    C'12345678'
MSG      DC    C' ZAJCTLG - XXXXXXX FAILED - CODE X'
M1       DC    C' ZAJCTLG - VOLUME TYPE NOT KNOWN - XXXX '
M2       DC    C' ZAJCTLG - OLD DSNAME NOT FOUND - '
         DC    44C'*'
         DC    C' '
*              DESCRIPTION OF WORK AREA.
D        DSECT
SAV      DS    25F
DSNAME   DS    44C
CAML     DS    4F
AREA     DS    70F
VOLIST   DS    14C
VOLUME   EQU   VOLIST+6
SEQNO    EQU   VOLIST+12
OLDNAME  DS    44C
         DS    0D
SEND     EQU   *
SLEN     EQU   SEND-SAV
*
         END
