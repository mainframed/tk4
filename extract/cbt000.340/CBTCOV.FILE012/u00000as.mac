PRINTEX  CSECT
*
*  THE RTPREXPT PROGRAM PROVIDES A MEANS OF LISTING ANY MULTIFILE TAPE
*  OF 80 BYTE RECORDS.  IT IS DESIGNED TO HELP IN THE PREPARATION OF
*  RESPOND EXPORT TAPES, WHICH HAVE AN UNBLOCKED FORST FILE (CONTENTS
*  DESCRIPTION) FOLLOWED BY ANY NUMBER OF CARD IMAGE FILES BLOCKED
*  80/3200, BUT IT COULD BE EASILY MODIFIED FOR A LONGER MAX BLKSIZE.
*
*  IF A PARM IS OMITTED FROM THE EXEC CARD THE WHOLE TAPE IS LISTED,
*  STOPPING ONLY AT A DOUBLE TAPE MARK.  FOR A QUICK CONFIRMATION OF
*  THE TAPE CONTENTS A LIMIT TO THE NUMBER OF BLOCKS TO BE PRINTED
*  FROM EACH FILE CAN BE GIVEN, E.G. PARM=2 WILL PRINT ONLY THE FIRST
*  2 BLOCKS FROM EACH FILE.
*
*  SAMPLE JCL....
*        //PRINTEX  EXEC  PGM=RTPREXPT,PARM=3
*        //SYSPRINT  DD  SYSOUT=A
*        //TAPE  DD  VOL=SER=EXPORT,LABEL=(,NL),UNIT=2400
*
*        BEGIN
*  EXPANSION RESPOND TSG BEGIN MACRO FOLLOWS
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'                  NEW SAVE AREA
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         SR    10,10
         L     1,0(1)
         LH    2,0(1)
         LTR   2,2
         BZ    NOPARM
         BCTR  2,0
         EX    2,PACKPARM
         CVB   10,DW
NOPARM   ST    10,SAVEPARM
         RDJFCB  (TAPE)
         OI    JFCB+52,8
         MVI   JFCB+69,1
         OPEN  (LIST,(OUTPUT))
         MVC   REELNO,JFCB+118
         OPEN  (TAPE),TYPE=J
         MVI   TAPE+44,12
NEWFILE  SP    BLOCKNO,BLOCKNO
         AP    FILENO,ONE
         MVC   SFNO,MASK3
         ED    SFNO,FILENO
         MVC   TFNO,STARTF+10
         LA    8,STARTF
         BAL   9,PRINT
         MVI   DTM,0
READ     EXCP  TIOB
         WAIT  ECB=TECB
         TM    TIOB+12,1
         BO    EOF
         MVI   DTM,255
         AP    BLOCKNO,ONE
         MVC   LINE+1(6),BLOCKL
         MVC   LBN,MASK5
         ED    LBN,BLOCKNO
         CLI   TECB,127
         BE    GOODBLK
         MVC   CARDIM,ERROR
         LA    8,LINE
         BAL   9,PRINT
         MVC   LINE+1(11),SFNO+4
         XC    TIOB(4),TIOB
         NI    TAPE+44,63
GOODBLK  L     7,FULLEN
         SH    7,TIOB+14
         LA    6,DATA
NEXTREC  MVC   CARDIM,0(6)
         LA    8,LINE
         BAL   9,PRINT
         MVC   LINE+1(11),SFNO+4
         LA    6,80(6)
         LA    1,80
         SR    7,1
         BP    NEXTREC
         LA    8,UP1
         BAL   9,PRINT
         BCT   10,READ
CLOSE    LA    1,FSF
         ST    1,TIOB+16
         EXCP  TIOB
         WAIT  ECB=TECB
         LA    1,RCCW
         ST    1,TIOB+16
         XC    TIOB(4),TIOB
         NI    TAPE+44,63
         L     10,SAVEPARM
         LA    8,UP1
         BAL   9,PRINT
         B     NEWFILE
EOF      CLI   DTM,0
         BNE   CLOSE
         MVC   STARTF+1(20),EOT
         LA    8,STARTF
         BAL   9,PRINT
         CLOSE (TAPE,,LIST)
*        GOBACK
*  EXPANSION OF RESPOND TSG GOBACK MACRO FOLLOWS
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
PACKPARM PACK  DW,2(0,1)
PRINT    SP    LCNT,ONE
         BP    PLINE
         PUT   LIST,NP
         PUT   LIST,TITLE
         ZAP   LCNT,MAXCNT
PLINE    PUT   LIST,(8)
         BR    9
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBM,          *
               LRECL=94,BLKSIZE=1410
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=(E),DEVD=TA,EXLST=XLIST,     *
               EODAD=EOF
XLIST    DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
DW       DC    D'0'
RCCW     CCW   2,DATA,X'20',3200
FSF      CCW   63,*,32,1
TIOB     DC    F'0'
         DC    A(TECB)
         DC    2F'0'
         DC    A(RCCW)
         DC    A(TAPE)
         DC    2F'0'
TECB     DC    F'0'
SAVEPARM DC    F'0'
FULLEN   DC    F'3200'
NP       DC    X'8B'
MAXCNT   DC    P'58'
LCNT     DC    PL2'0'
EOT      DC    CL20'END OF TAPE LISTING'
TITLE    DC    X'19'
         DC    CL18' '
         DC    C'  RESPOND TSG LISTING OF EXPORT TAPE NO '
REELNO   DC    C'123445'
         DC    CL7' '
TFNO     DC    CL12' '
MASK3    DC    X'40202120'
MASK5    DC    X'402020202120'
UP1      DC    X'0B'
STARTF   DC    X'11'
         DC    C'START OF FILE NO'
SFNO     DC    XL4'00'
         DC    CL73' '
LINE     DC    X'09'
         DC    C'BLOCK'
LBN      DC    XL6'00'
         DC    CL2' '
CARDIM   DC    CL80' '
BLOCKNO  DC    PL3'0'
ONE      DC    P'1'
FILENO   DC    PL2'0'
DTM      DC    X'00'
ERROR    DC    CL80'*** READ ERROR ***'
BLOCKL   DC    CL11'BLOCK'
DATA     DS    CL3200
         END
