         MACRO
&NAME    $QCB  &ACCESS=QUEUE,&TYPE=F,&LEN=
         LCLB  &A(6)
         LCLA  &B
&NAME    DS    0F
&A(1)    SETB  ('&ACCESS' EQ 'STACK')
&A(2)    SETB  ('&ACCESS' EQ 'QUEUE')
&A(3)    SETB  ('&ACCESS' EQ 'DEQUE')
&A(4)    SETB  ('&ACCESS' EQ 'CIRQUE')
&A(5)    SETB  ('&ACCESS' EQ 'TREE')
&A(6)    SETB  ('&ACCESS' EQ 'TABLE')
&B       SETA  &A(1)+2*&A(2)+3*&A(3)+4*&A(4)+5*&A(5)+6*&A(6)
         DC    AL1(&B)             LIST TYPE
         AIF   ('&TYPE' EQ 'F').F
         DC    X'80'               VARIABLE LENGTH ELEMENTS
.Y       DC    H'0'                LENGTH OF ELEMENT
         AGO   .X
.F       DC    X'00'               FIXED LENGTH ELEMENTS
         AIF   (T'&LEN EQ 'O').Y
&B       SETA  &LEN
         DC    H'&B'               ELEMENT LENGTH
.X       ANOP
         DC    A(0)                FIRST BUFFER
         DC    A(0)                HEAD OF LIST
         DC    A(0)                TAIL OF LIST
         DC    A(0)                CURRENT ELEMENT
         DC    A(0)                HEAD OF FREELIST
         MEND
