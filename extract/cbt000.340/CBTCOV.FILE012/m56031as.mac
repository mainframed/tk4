         MACRO
&LOC     READCARD  &REG,&UNIT=5,&BUFFNAM=READCARB,&END=0 DEFAULT SPECS
.*          JOHN R. EHRMAN (SLAC) 13 DECEMBER 1968 (VERSION 2)
         GBLB  &READCAR                INDICATES CARD BUFFER DEFINITION
         LCLA  &A
         AIF   (T'&REG EQ 'N').S       OK IF SELF-DEFINING
.REGERR1 MNOTE 4,'*** REGISTER VALUE &REG IS NOT SELF-DEFINING.'
         MEXIT
.S       AIF   (&REG LT 16).T          OK IF LEGAL REGISTER NUMBER
.REGERR2 MNOTE 4,'*** REGISTER VALUE SPECIFIED IS NOT IN 0-15.'
         MEXIT
.T       SPACE
         AIF   ('&LOC' EQ '').A
&LOC     DS    0H
.*       DEFINE BUFFER ONLY IF DEFAULT NAME IS TO BE USED
.A       AIF   (&READCAR OR ('&BUFFNAM' NE 'READCARB')).C
         CNOP  0,4
         B     *+84                    JUMP OVER CARD BUFFER
READCARB DS    CL80  80 CHAR BUFFER ON FULLWORD BOUNDARY
&READCAR SETB  1                       NO MORE BUFFERS
.C       CNOP  2,4
         STM   14,15,*+22              SAVE LINKING REGISTERS
         L     15,*+14                 LOAD BRANCH ADDRESS
         BALR  14,15                   JUMP TO READ ROUTINE
         AIF   (T'&UNIT NE 'N').B      JUMP IF UNIT NOT SELFDEFINING
&A       SETA  &UNIT                   TO AVOID TROUBLE IN DC INSTN
         DC    F'&A'                   UNIT NUMBER
         AGO   .D
.B       DC    X'01',AL3(&UNIT)        INDIRECT UNIT SPECIFICATION
.D       DC    A(&BUFFNAM)             POINTER TO CARD BUFFER
         DC    X'1',VL3(JREREADC)      ENDFILE FLAG, ADDRESS OF READER
         DC    2F'0'
         LM    14,15,12(14)            RESTORE LINKING REGISTERS
         LA    &REG,&BUFFNAM           POINT TO BUFFER
         NOP   &END                    ENDFILE BRANCH, SET TO BC 15
         SPACE
         MEND
