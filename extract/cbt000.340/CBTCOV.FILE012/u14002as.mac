SMDDA   TITLE 'DASD DEVICE PERFORMANCE ANALYSIS'
***********************************************************************
*                                                                     *
*        VERSION 2.1 (OCT83)                                          *
*                                                                     *
* SMDDA DASD DEVICE ANALYSIS FROM RMF TYPE 74 RECORDS.                *
*                                                                     *
*       ALL THE TYPE 74 RECORDS ARE TOTALLED FOR THE DAYTIME (0800    *
*       TO 1700) FOR ALL DAYS IN THE INPUT.                           *
*                                                                     *
*       STATISTICS ARE ACCUMULATED IN STORAGE FOR UP TO 100 VOLUME    *
*       & DEVICE COMBINATIONS FOR 2 SYSTEMS.                          *
*                                                                     *
*       A REPORT WITH ONE LINE FOR EACH VOLUME SERIAL NUMBER WITH     *
*       EACH DEVICE ADDRESS IS PRINTED.                               *
*                                                                     *
*       INPUT IS A DATASET CONTAINING RMF OR CMF TYPE 74 RECORDS      *
*       FOR THE PERIOD OF INTEREST.  THE DATA CAN BE UNSEQUENCED.     *
*       IF THE EXTRACTOR VERSION  (SMF74MFV & -RV1)  IN ANY RECORD    *
*       DOES NOT MATCH THAT OF THE FIRST TYPE 74 RECORD READ THEN     *
*       THIS IS INDICATED ON THE HEADING BY AN *.                     *
*                                                                     *
*       IF PARM DATA IS USED, IT WILL BE PRINTED VERBATIM ON A LINE   *
*       AT THE HEAD OF EACH REPORT PAGE.                              *
*                                                                     *
*       MAPPING MACRO ERBSMFR IS USED, THEREFORE SYS1.RMFMAC01 MUST   *
*       BE ADDED TO THE SYSLIB CONCATENATION FOR ASSEMBLY.            *
*                                                                     *
*       IN ADDITION TO STANDARD OS MACROS, THE FOLLOWING MACROS ARE   *
*       USED AND ARE INCLUDED INLINE AFTER THIS DOCUMENTATION:        *
*       ENTER   LEAVE   MSORT   EDIT    REGEQU                        *
*       FPLD    FPST    FPDEF                                         *
*                                                                     *
*       UTDTC (EMEB SERVICE ROUTINE) IS USED FOR DATE CONVERSION.     *
*                                                                     *
*       FLOATING-POINT INSTRUCTIONS ARE USED, THEREFORE THIS PROGRAM  *
*       IS ONLY USABLE ON A PROCESSOR WITH THIS FEATURE.              *
*                                                                     *
*       WRITTEN BY     DAVE MILLS                                     *
*                      EAST MIDLANDS ELECTRICITY                      *
*                      398 COPPICE ROAD                               *
*                      ARNOLD                                         *
*                      NOTTINGHAM   NG5 7HX      (0602) 269711 X2007  *
*       ALL RIGHTS RESERVED:   THIS PROGRAM IS PROVIDED FOR THE USE   *
*       OF GUIDE MEMBER INSTALLATIONS ON AN AS IS BASIS WITHOUT ANY   *
*       WARRANTY.  NO RESPONSIBILITY IS IMPLIED FOR THE ACCURACY OF   *
*       THE OUTPUT OF THE PROGRAM.                                    *
*                                                                     *
**************************** CONTINUED ********************************
         EJECT
***********************************************************************
*                                                                     *
*        SUGGESTED JOB CONTROL FOR EXECUTION                          *
*        -----------------------------------                          *
*                                                                     *
*        //GO      EXEC PGM=SMDDA,PARM='OPERATIONS MANAGER'           *
*        //STEPLIB   DD DSN= (LIBRARY CONTAINING SMDDA PROGRAM)       *
*        //SYSUDUMP  DD SYSOUT=*                                      *
*        //SYSPRINT  DD SYSOUT=*            (REPORT OUTPUT)           *
*        //SYSUT1    DD DSN= (DATASET CONTAINING RMF/CMF RECORDS)     *
*                                                                     *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
*        CALCULATION METHODS                                          *
*        -------------------                                          *
*                                                                     *
*        THE CALCULATIONS TO ARRIVE AT AVERAGE I/O TIMES ARE BASED ON *
*        PRINCIPLES DISCUSSED IN THE IBM PUBLICATION "AN MVS TUNING   *
*        PERSPECTIVE" (GG22-9023-04).                                 *
*                                                                     *
*        THE DATA IS OBTAINED FROM RMF OR CMF TYPE 74 RECORDS.  THE   *
*        MEASUREMENTS USED ARE AS FOLLOWS: (BY SMF NAME)              *
*                                                                     *
*        SMF74INT  -  INTERVAL DURATION                               *
*             SAM  -  # SAMPLES IN THIS INTERVAL                      *
*             ACT  -  # SAMPLES WHEN DEVICE WAS BUSY                  *
*             CNT  -  # REQUESTS SERVICED BY THIS DEVICE              *
*             CUB  -  # SAMPLES WHEN CU WAS BUSY                      *
*             QUE  -  # REQUESTS QUEUED FOR DEVICE                    *
*             RDL  -  # SAMPLES WHEN DEVICE RESERVED BY ANOTHER CPU   *
*             RSV  -  # SAMPLES WHEN DEVICE RESERVED BY THIS CPU      *
*                                                                     *
**************************** CONTINUED ********************************
         EJECT
***********************************************************************
*                                                                     *
*        FORMULAE  (SUBROUTINE "CALCULAT"):                           *
*        --------                                                     *
*                                                                     *
*        DEVICE ACTIVITY COUNT, SELF SCALING BAR GRAPH REPRESENTING   *
*        THE TOTAL OF *CNT* FOR THE DEVICE.                           *
*                                                                     *
*        FOR "% OF I/O TIME QUEUED ON ..." CALCULATIONS:              *
*        % Q TIME = 100 * AVERAGE TIME QUEUED / AVERAGE I/O TIME      *
*                                                                     *
*        DEV ACTIVITY RATE = CNT / INT      (AS CALCULATED BY RMFPOST)*
*                                                                     *
*        % CU DELAY = 100*CUB/SAM           (AS CALCULATED BY RMFPOST)*
*        AVG CU Q TIME = % CU DELAY / DEV ACTIVITY RATE               *
*                      = CUB/SAM * INT/CNT      (DELAY DUE TO CU BUSY)*
*        :. % CUQ  = (CUB*100)/(QUE+ACT)                              *
*                                                                     *
*        % RES DELAY = 100*RDL/SAM          (AS CALCULATED BY RMFPOST)*
*        AVG RES DELAY TIME = % RESV DELAY / 100 * DEV ACTIVITY RATE  *
*                           = RDL/SAM * INT/CNT (DELAY DUE TO RESERVE)*
*        :. % RESQ = (RDL*100)/(QUE+ACT)                              *
*                                                                     *
*        % DEV BUSY = 100*ACT/SAM           (AS CALCULATED BY RMFPOST)*
*        AVG DEVICE SERVICE TIME = % DEV BUSY / 100* DEV ACTIVITY RATE*
*                                = ACT * INT/(CNT*SAM)                *
*                                                                     *
*        AVG Q LENGTH = QUE/SAM             (AS CALCULATED BY RMFPOST)*
*        AVG Q TIME = AVG Q LENGTH / DEV ACTIVITY RATE                *
*                   = QUE/SAM * INT/CNT                               *
*                            (DELAY DUE TO DEV BUSY, CU BUSY, RESERVE)*
*                                                                     *
*        AVG DEV Q TIME = AVG Q TIME - AVG CU Q TIME                  *
*            ---                     - AVG RES DELAY TIME             *
*                       = (QUE-CUB-RDL) * INT/(CNT*SAM)               *
*                                             (DELAY DUE TO DEV BUSY) *
*        :. % DEVQ = ((QUE-CUB-RDL)*100)/(QUE+ACT)                    *
*                                                                     *
*        AVG QUEUING TIME = AVG Q TIME                                *
*                         = QUE * INT/(CNT*SAM)                       *
*                                                                     *
*        AVG I/O SERVICE TIME = AVG Q TIME + AVG DEV SERVICE TIME     *
*                             = (QUE+ACT) * INT/(CNT*SAM)             *
*                                                                     *
*        EFFICIENCY IS THE RATIO OF DEVICE SERVICE TIME TO THE TOTAL  *
*        I/O SERVICE TIME (ZERO AND 100% ARE NOT PRINTED):            *
*        EFFICIENCY = 100 * AVG DEV SERVICE TIME / AVG I/O TIME       *
*                   = 100 * ACT / (QUE+ACT)                           *
*                                                                     *
***********************************************************************
         EJECT
*---------------------------------------------------------------------*
*  FLOATING-POINT CONVERSION MACROS                                   *
*---------------------------------------------------------------------*
         MACRO
&TAG     FPLD  &FPR,&GPR
.* TO LOAD A FLOATING-POINT REG (LONG) FROM A GENERAL-PURPOSE REGISTER
.* REF: SYSTEM/370 PRINCIPLES OF OPERATION, PAGE A-38
.* NOTE: THE FINAL INSTRUCTION SETS THE CONDITION CODE.
         GBLB  &FPDEF
&FPDEF   SETB  1
&TAG     ST    &GPR,RTEM+4
         XC    RTEM+4(4),R231+4
         MVC   RTEM(4),R231
         LD    &FPR,RTEM
         SD    &FPR,R231
         MEND
         SPACE
         MACRO
&TAG     FPST  &GPR,&FPR,&OVRFLW
.* TO LOAD A GENERAL-PURPOSE REGISTER FROM A FLOATING-POINT REGISTER
.* REF: SYSTEM/370 PRINCIPLES OF OPERATION, PAGE A-38,A-39
.* NOTE: THE GENERATED CODE BUGGERS UP THE FLOATING-POINT REGISTER.
         GBLB  &FPDEF
&FPDEF   SETB  1
&TAG     SD    &FPR,R231R
         BC    11,&OVRFLW
         AW    &FPR,R232
         BC    4,&OVRFLW
         STD   &FPR,RTEM
         XI    RTEM+4,X'80'
         L     &GPR,RTEM+4
         MEND
         SPACE
         MACRO
         FPDEF
.* TO DEFINE STORAGE AND CONSTANTS FOR THE FP MACROS.
         GBLB  &FPDEF,&FPDEF1
         AIF   (&FPDEF1).A
&FPDEF1  SETB  1
         AIF   (NOT &FPDEF).A
* STORAGE AND CONSTANTS FOR THE FLOATING-POINT MACROS
RTEM     DC    D'0'
R231     DC    X'4E',X'00000080000000'  D'2**31'
R232     DC    X'4E',X'00000100000000'  D'2**32'
R231R    DC    X'4F',X'00000008000000'  D'2**31'
.A       ANOP
         MEND
         SPACE 2
*---------------------------------------------------------------------*
*    OTHER STANDARD EMEB MACROS                                       *
*---------------------------------------------------------------------*
         PRINT OFF
         MACRO
&NAME    ENTER &LABEL,&REGS,&SAVE=,&BASE=12,&STORE=0
.*
.*  25/4/82  REGS,TEMP REMOVED, 14-12 SAVED                         MJH
.*  20/2/84  TIDYING UP, USER MUST ENSURE R15 VALUE                 DEM
.*
         LCLA  &CHARS,&COUNT,&DISP,&DISPX,&BASENDX
         LCLC  &SNAPID,&REG
         LCLB  &NOSNAP,&INTSAVE,&DYNSAVE
.*
&NAME    DS    0F                                                FEB 84
.* - - - - - - - - - - - - - - - - - -
&INTSAVE SETB  ('&SAVE' EQ '')         1=INTERNAL SAVE AREA
&DYNSAVE SETB  ('&SAVE' EQ 'DYN')      1=GETMAINED SAVE AREA
.* - - - - - - - - - - - - - - - - - -
         AIF   ('&SYSECT' EQ '').CHKLBL
&SNAPID  SETC  '''&SYSECT'''
.CHKLBL  ANOP
         AIF   (T'&LABEL EQ 'O').CHKSNAP
&SNAPID  SETC  '&LABEL'
.CHKSNAP ANOP
&NOSNAP  SETB  ('&SNAPID' EQ '')       1=SNAPID NOT WANTED
         AIF   (&NOSNAP).SETDISP
&CHARS   SETA  K'&SNAPID-2
&COUNT   SETA  2
.TSTCHAR ANOP
         AIF   ('&SNAPID'(&COUNT,1) EQ '&&'(1,1)).DECR
         AIF   ('&SNAPID'(&COUNT,1) NE '''').INCR
.DECR    ANOP
&CHARS   SETA  &CHARS-1
&COUNT   SETA  &COUNT+1
.INCR    ANOP
&COUNT   SETA  &COUNT+1
         AIF   (&COUNT LT K'&SNAPID).TSTCHAR
&COUNT   SETA  (1+&CHARS+3)/4*4        (COUNT,SNAPID,DS-0F)
.SETDISP ANOP
         AIF   (&NOSNAP AND NOT &INTSAVE).STORE
&DISP    SETA  &INTSAVE*72+&COUNT+4
.*                           THIS MUST BE ON WORD-BOUNDARY
         B     &DISP.(0,15)
         AIF   (&NOSNAP).SETSAVE
         DC    AL1(&CHARS),C&SNAPID
         DS    0F
.* - - - - - - - - - - - - - - - - - -
         AIF   (NOT &INTSAVE).STORE
.SETSAVE ANOP
         DC    18A(0)
.* - - - - - - - - - - - - - - - - - -
.STORE   ANOP
         AIF   ('&REGS' EQ '').SAVEALL
         MNOTE 0,'REGISTER RANGE IGNORED, REGS 14,12 SAVED'
.SAVEALL ANOP
         STM   14,12,12(13)
.* - - - - - - - - - - - - - - - - - -
.SETBASE ANOP
.*                           CHECK BASE REGISTERS ARE VALID
&BASENDX SETA  1
.BASELP0 ANOP
&REG     SETC  '&BASE(&BASENDX)'
         AIF   ('&REG'(1,1) NE 'R').NUMBASE
&REG     SETC  '&REG'(2,K'&REG-1)
.NUMBASE ANOP
         AIF   ('&REG' EQ '13' OR '&REG' EQ '14').INVBASE
         AIF   (NOT &DYNSAVE).NXTBASE
         AIF   ('&REG' NE '0' AND '&REG' NE '1').NXTBASE
.INVBASE ANOP
         MNOTE 4,'BASE REGISTER &BASE(&BASENDX) COULD CAUSE ERRORS'
.NXTBASE ANOP
&BASENDX SETA  &BASENDX+1
         AIF   (&BASENDX LE N'&BASE).BASELP0
.*                          ENSURE BASE REG IS AT DOUBLE-WORD
         CNOP  6,8
&REG     SETC  '&BASE(1)'
         BALR  &REG,0
         USING *,&REG
         AIF   (N'&BASE EQ 1).ONEBASE
&BASENDX SETA  1
&DISP    SETA  N'&BASE*4
.BASELP1 ANOP
&REG     SETC  '&BASE(&BASENDX+1)'
         L     &REG,*+&DISP
&DISPX   SETA  &BASENDX*4
         USING *-&DISPX+4096*&BASENDX,&REG
&BASENDX SETA  &BASENDX+1
         AIF   (&BASENDX LT N'&BASE).BASELP1
         B     *+&DISP
&BASENDX SETA  1
.BASELP2 ANOP
         DC    A(*-&DISP+4096*&BASENDX)
&DISP    SETA  &DISP+4
&BASENDX SETA  &BASENDX+1
         AIF   (&BASENDX LT N'&BASE).BASELP2
.ONEBASE ANOP
.* - - - - - - - - - - - - - - - - - -
         LR    14,13
         AIF   (&INTSAVE).INTSAVE
         AIF   (&DYNSAVE).DYNSAVE
         LA    13,&SAVE
         AGO   .CHAIN
.DYNSAVE ANOP
         LA    0,72+&STORE
*        GETMAIN R,LV=(0)
         GETMAIN R,LV=(0)
         LR    13,1
         LM    0,1,20(14)
         AGO   .CHAIN
.INTSAVE ANOP
&DISP    SETA  &COUNT+4
         LA    13,&DISP.(0,15)
.CHAIN   ANOP
         ST    14,4(0,13)
         ST    13,8(0,14)
         MEND
         MACRO
&NAME    LEAVE &REGS,&SAVE=,&STORE=0,&CODE=
.*
.*  25/4/82  REGS,TEMP REMOVED,0 AND 1 NOW PASSED UNCHANGED       M.J.H
.*           RETURN CODE CORRECTED
.*
         LCLC  &REG
.*
         AIF   (T'&REGS EQ 'O').START
         MNOTE 0,'REGISTER RANGE IGNORED, REGS 2-14 RESTORED'
.START   ANOP
&NAME    DS    0H
         AIF   ('&CODE' EQ '').SETZERO
         AIF   ('&CODE'(1,1) EQ '(').LOADREG
         LA    15,&CODE                LOAD RETURN CODE
         AGO   .CODEND
.LOADREG ANOP
&REG     SETC  '&CODE'(2,K'&CODE-2)
         AIF   ('&REG' EQ '15').CODEND
         LR    15,&REG                 LOAD RETURN CODE
         AGO   .CODEND
.SETZERO ANOP
         SLR   15,15                   ZEROISE RETURN CODE
.CODEND  ANOP
         AIF   ('&SAVE' EQ 'DYN').DYNCODE
         L     13,4(0,13)              LOAD ADDRESS OF CALLERS SAVEAREA
         XC    8(4,13),8(13)           CLEAR LSA CHAIN
         LM    2,12,28(13)             RESTORE REGISTERS 2-12
         L     14,12(0,13)             LOAD RETURN ADDRESS
         BR    14                      RETURN TO CALLER
         MEXIT
.DYNCODE ANOP
         LR    2,13                    LOAD ADDRESS OF SAVE AREA
         L     13,4(0,13)              LOAD ADDRESS OF CALLERS SAVEAREA
         STM   15,1,16(13)             REGS 15,0,1 IN CALLERS SAVEAREA
         XC    8(4,13),8(13)           CLEAR LSA CHAIN
         LA    0,72+&STORE             LOAD COUNT OF DYNAMIC AREA
*        FREEMAIN R,LV=(0),A=(2)       FREE DYNAMIC AREA
         FREEMAIN R,LV=(0),A=(2)
         LM    14,12,12(13)            RESTORE REGISTERS
         BR    14                      RETURN TO CALLER
         MEND
         MACRO
&NAME    MSORT &TAB,&KEYLEN,&LEVLEN,&END,&ORDER,&SAVE
         LCLC  &CG0
         LCLC  &CG1
         LCLC  &CG2
         LCLC  &CG3
         LCLC  &CG4
         AIF   ('&TAB' NE '').A
         MNOTE 12,'TABLE NAME OMITTED - GENERATION TERMINATED'
         MEXIT
.A       ANOP
         AIF   ('&LEVLEN' NE '').B
         AIF   ('&KEYLEN' NE '').BA
         MNOTE 4,'KEY AND ENTRY LENGTHS OMITTED - L''&TAB ASSUMED'
&CG1     SETC  'L''&TAB'
         AGO   .BB
.B       ANOP
&CG1     SETC  '&LEVLEN'
         AIF   ('&KEYLEN' NE '').BC
         MNOTE *,'KEY LENGTH OMITTED - &CG1 ASSUMED'
         AGO   .BB
.BA      ANOP
&CG1     SETC  '&KEYLEN'
         MNOTE *,'ENTRY LENGTH OMITTED - &CG1 ASSUMED'
.BB      ANOP
&CG0     SETC  '&CG1'
         AGO   .C
.BC      ANOP
&CG0     SETC  '&KEYLEN'
.C       ANOP
&CG2     SETC  'C''&END'''
         AIF   ('&END' NE '').D
&CG2     SETC  'X''FF'''
         MNOTE *,'TABLE END PARAMETER OMITTED - X''FF'' ASSUMED'
.D       ANOP
&CG3     SETC  '&ORDER'
         AIF   ('&ORDER' EQ 'A').F
         AIF   ('&ORDER' EQ 'D').F
&CG3     SETC  'A'
         AIF   ('&ORDER' NE '').E
         MNOTE *,'SEQUENCE OMITTED - ASCENDING ASSUMED'
         AGO   .F
.E       MNOTE 4,'SEQUENCE INVALID - ASCENDING ASSUMED'
.F       ANOP
&CG4     SETC  'C&SYSNDX'
         AIF   ('&SAVE' EQ '').H
&CG4     SETC  '&SAVE'
.H       ANOP
&NAME    STM   1,2,&CG4
         LA    1,&TAB
         B     *+8
A&SYSNDX LA    1,&CG1.(1)
         CLI   0(1),&CG2
         BE    D&SYSNDX
         LR    2,1
B&SYSNDX LA    2,&CG1.(2)
         CLI   0(2),&CG2
         BE    A&SYSNDX
         CLC   0(&CG0,2),0(1)
         AIF   ('&CG3' NE 'A').I
         BNL   B&SYSNDX
         AGO   .J
.I       ANOP
         BNH   B&SYSNDX
.J       ANOP
         XC    0(&CG1,1),0(2)
         XC    0(&CG1,2),0(1)
         XC    0(&CG1,1),0(2)
         B     B&SYSNDX
         AIF   ('&SAVE' NE '').K
C&SYSNDX DS    2F
.K       ANOP
D&SYSNDX LM    1,2,&CG4
         MEND
         MACRO
&NAME    EDIT  &TO,&FROM,&EDWD
         LCLA  &CNT1,&CNT2
         LCLA  &D
         LCLB  &EDMK
         LCLC  &LTH
         LCLC  &M(24)
         LCLC  &EDCH
         LCLC  &SD
.*
         AIF   (T'&EDWD NE 'O').A1      IS EDIT WORD OMITTED
         MNOTE 4,'NO EDIT FORMAT GIVEN'
         MEXIT
.A1      AIF   ('&EDWD'(1,1) EQ '''').A2
.A15     MNOTE 4,'EDIT FORMAT NOT WITHIN QUOTES'
         MEXIT
.A2      AIF   ('&EDWD'(K'&EDWD,1) NE '''').A15
.*
&CNT1    SETA  2         START POSITION IN &EDWD
&CNT2    SETA  1         START POSITION IN &M
         AIF   ('&EDWD'(2,1) EQ '*').A6
&M(1) SETC  '40'    FILL CHAR IS BLANK
&CNT2    SETA  &CNT2+1
.*
.A6      ANOP
         AIF   ('&EDWD'(&CNT1,1) EQ '''').D1
         AIF   ('&EDWD'(&CNT1,1) EQ 'X').A8
         AIF   ('&EDWD'(&CNT1,1) EQ '0').A81
         AIF   ('&EDWD'(&CNT1,1) EQ ',').A9
         AIF   ('&EDWD'(&CNT1,1) EQ ':').B1
         AIF   ('&EDWD'(&CNT1,1) EQ '.').B2
         AIF   ('&EDWD'(&CNT1,1) EQ ' ').B3
         AIF   ('&EDWD'(&CNT1,1) EQ 'C').B4
         AIF   ('&EDWD'(&CNT1,1) EQ 'R').B5
         AIF   ('&EDWD'(&CNT1,1) EQ '-').B6
         AIF   ('&EDWD'(&CNT1,1) EQ '$').B7
         AIF   ('&EDWD'(&CNT1,1) EQ '*').B8
         AIF   ('&EDWD'(&CNT1,1) EQ '/').B9
&EDCH    SETC  '&EDWD'(&CNT1,1)
         MNOTE 4,'IMPROPER EDIT CHARACTER - &EDCH'
         MEXIT
.*
.A8      AIF   ('&EDWD'(&CNT1+1,1) EQ 'X').A89   IS NEXT A 'X' ?
         AIF   ('&EDWD'(&CNT1+1,1) EQ '0').A88    OR A '0' ?
         AIF   ('&EDWD'(&CNT1+1,1) EQ '$').A88     OR A '$'    ?
         AIF   ('&EDWD'(&CNT1+1,1) EQ '''').A89      OR THE END ?
         AIF   ('&EDWD'(&CNT1+2,1) EQ '0').A88     IF NOT TEST NEXT
         AGO   .A89                              ELSE USE DS
.A81     AIF   (&CNT1 EQ 2).A88        '0' IN FIRST POSITION
         AGO   .A89                    ELSE USE DIGIT SELECT
.A88     ANOP
&M(&CNT2)   SETC  '21'  START SIGNIFICANCE
         AGO   .C9
.A89      ANOP
&M(&CNT2)   SETC  '20'  DIGIT SELECT
         AGO   .C9
.A9      ANOP
&M(&CNT2)   SETC  '6B'  COMMA
         AGO   .C9
.B1      ANOP
&M(&CNT2)   SETC  '7A'  COLON
         AGO   .C9
.B2      ANOP
&M(&CNT2)   SETC  '4B'  PERIOD
         AGO   .C9
.B3      ANOP
&M(&CNT2)   SETC  '40'  BLANK
         AGO   .C9
.B4      ANOP
&M(&CNT2)   SETC  'C3'  C
         AGO   .C9
.B5      ANOP
&M(&CNT2)   SETC  'D9'  R
         AGO   .C9
.B6      ANOP
&M(&CNT2)   SETC  '60'  -
         AGO   .C9
.B7      ANOP
&EDMK    SETB  1        $ - SET ON EDMK OPTION
&D       SETA  &CNT2-1  SET DISP OF SIGNIF SELECT
&SD      SETC  '&D'
         AGO   .A81      AND TREAT AS IF '0'
.B8      ANOP
&M(&CNT2)   SETC  '5C'  *
         AGO   .C9
.B9      ANOP
&M(&CNT2)   SETC  '61'  /
.*
.C9      ANOP
&CNT1    SETA  &CNT1+1
&CNT2    SETA  &CNT2+1
         AIF   (&CNT2 LE 24).A6
         MNOTE 4,'EDIT WORD TOO LARGE TO HANDLE'
.*
.D1      ANOP
&CNT2    SETA  &CNT2-1
&LTH     SETC  ''
         AIF   ('&TO'(K'&TO,1) EQ ')').E2
&LTH     SETC  '('.'&CNT2'.')'
.E2      ANOP
&NAME    MVC   &TO&LTH,=X'&M(1)&M(2)&M(3)&M(4)&M(5)&M(6)&M(7)&M(8)&M(9)X
               &M(10)&M(11)&M(12)&M(13)&M(14)&M(15)&M(16)&M(17)&M(18)&MX
               (19)&M(20)&M(21)&M(22)&M(23)&M(24)'
         AIF   (&EDMK).F1
         ED    &TO&LTH,&FROM
         AGO   .E4
.F1      LA    1,&TO+&SD
         EDMK  &TO&LTH,&FROM
         BCTR  1,0
         MVI   0(1),C'$'
.E4      ANOP
         MEND
         MACRO
         REGEQU
         PUSH  PRINT
         AIF   ('&SYSPARM' NE 'SUP').ACC01
         PRINT OFF,NOGEN
.ACC01   ANOP
*
***      SYMBOLIC REGISTER EQUATES
*
         SPACE 2
* GENERAL PURPOSE REGISTERS
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
* FLOATING POINT REGISTERS
         SPACE
F0       EQU   0
F2       EQU   2
F4       EQU   4
F6       EQU   6
         SPACE
* EXTENDED CONTROL REGISTERS
         SPACE
C0       EQU   0
C1       EQU   1
C2       EQU   2
C3       EQU   3
C4       EQU   4
C5       EQU   5
C6       EQU   6
C7       EQU   7
C8       EQU   8
C9       EQU   9
C10      EQU   10
C11      EQU   11
C12      EQU   12
C13      EQU   13
C14      EQU   14
C15      EQU   15
         POP   PRINT
         MEND
*---------------------------------------------------------------------*
         PRINT ON
         EJECT
*---------------------------------------------------------------------*
*  DSECTS     DSECTS     DSECTS     DSECTS     DSECTS     DSECTS      *
*---------------------------------------------------------------------*
         PRINT GEN
SMFREC   DSECT
         ERBSMFR 74          MAPPING FOR SMF TYPE 74 RECORD
         SPACE
         PRINT NOGEN
         DCBD  DSORG=PS,DEVD=DA
         PRINT GEN
         EJECT
*---------------------------------------------------------------------*
*  0. THIS IS THE PROGRAM ENTRY POINT, ESTABLISH A SAVE AREA AND      *
*     OBTAIN ADDRESSIBILITY.  SET THE SYSTEM MASK SO THAT INTERRUPTS  *
*     OCCUR TO AVOID CALCULATION ERRORS DUE TO OVERFLOWS ETC.         *
*---------------------------------------------------------------------*
SMDDA    CSECT
         ENTER 'SMDDA',BASE=(11,12)
         SPACE
         L     R1,0(R1)                POINT AT PARM DATA
         OC    0(2,R1),0(R1)           IS DATA COUNT ZERO ?
         BZ    SETMASK                 YES, SKIP PARM PROCESSING
         SLR   R2,R2                   GET
         ICM   R2,B'0011',0(R1)         PARM DATA LENGTH (MAX 100)
         BCTR  R2,0                     LESS 1 (FOR MVC)
         EX    R2,MOVEPARM
         MVI   H0,C'1'                 INDICATE DATA ON HEADING PREFIX
         B     SETMASK
         SPACE
MOVEPARM MVC   H0L(0),2(R1)            PARM DATA TO HEADING PREFIX LINE
         SPACE
SETMASK  DS    0H
         ICM   R1,B'1000',=B'00001110' ALLOW ALL INTERRUPTS
         SPM   R1                   1   EXCEPT SIGNIFICANCE.
         EJECT
*---------------------------------------------------------------------*
*  1. INITIATE PROCESSING BY OPENING THE INPUT SMF DATASET AND THE    *
*     OUTPUT REPORT DATASET.                                          *
*---------------------------------------------------------------------*
         OPEN  (SYSUT1,INPUT)
         L     R1,=A(SYSUT1)
         USING IHADCB,R1
         TM    DCBOFLGS,DCBOFOPN       OPENED ?
         BO    OPEN1                   YES, CONTINUE
         LA    R3,MSG6                 NO, ERROR MESSAGE
         B     GENERROR
         SPACE
OPEN1    DS    0H
         OPEN  (SYSPRINT,OUTPUT)
         L     R1,=A(SYSPRINT)
         TM    DCBOFLGS,DCBOFOPN       OPENED ?
         BO    OPEN2                   YES, CONTINUE
         LA    R3,MSG7                 NO, ERROR MESSAGE
         B     GENERROR
         SPACE
OPEN2    DS    0H
         EJECT
*---------------------------------------------------------------------*
*  2. READ THE SMF RECORDS, SELECTING THE REQUIRED RECORDS:           *
*          RECORD TYPE = 74                                           *
*          TIME GE 0800                                               *
*          TIME LT 1700                                               *
*          DEVICE CLASS = DASD                                        *
*---------------------------------------------------------------------*
READREC  DS    0H
         L     R1,=A(SYSUT1)
         GET   (1)                     READ A RECORD
         LR    R10,R1                  @ RECORD
         USING SMFREC,R10              TELL ASSEMBLER
         CLI   SMF74RTY,74             IS IT TYPE 74 ?
         BNE   READREC                 NO, DONT WANT IT
         CLC   SMF74IST,=P'0080000'    CF INT START TIME V 0800
         BL    READREC                 LT, DON'T WANT IT
         CLC   SMF74IST,=P'0170000'    CF INT START TIME V 1700
         BNL   READREC                 GE, DON'T WANT IT
         TM    SMF74SUB+1,X'20'        IS IT A DASD RECORD ?
         BNO   READREC                 NO, DONT WANT IT
         SPACE
         OC    VERSION,VERSION         HAVE WE GOT RMF/CMF VERSION ?
         BNZ   TESTVERS                YES, GO COMPARE V THIS REC
         MVC   VERSION,SMF74MFV        SAVE VERSION/RV1 FOR NEXT RECS
TESTVERS DS    0H
         CLC   VERSION,SMF74MFV        SAME VERSION OF RMF/CMF ?
         BE    VERSOK                  YES, CONTINUE
         MVI   VERIND,C'*'             NO, POSSIBLE DOUBLE SAMPLING
VERSOK   DS    0H
         SPACE
         AP    RECNUM,=P'1'            ADD 1 TO RECORDS READ
         MVC   LASTD,SMF74DAT          SAVE LAST DATE
         OC    FIRDTEST,FIRDTEST       HAVE WE GOT THE FIRST DATE ?
         BNZ   NFIRST                  YES, CONTINUE
         MVC   FIRSTD,SMF74DAT         NO, SAVE THIS AS FIRST DATE
NFIRST   DS    0H
         EJECT
*---------------------------------------------------------------------*
*  3. SAVE THE COMMON DATA FROM THIS RECORD.                          *
*---------------------------------------------------------------------*
         MVC   WACSID,SMF74SID         SAVE SYSTEM ID
         MVC   WACSAM,SMF74SAM         SAVE NUMBER OF SAMPLES
         SPACE
         OC    LEFTSID,LEFTSID         SID FOUND FOR THE LEFT ?
         BNZ   COMMTL                  YES, GO SEE IF SAME
         MVC   LEFTSID,WACSID          STORE SID FOR THE LEFT
         B     COMMSL                  GO TOTAL SAMPLES FOR LEFT
COMMTL   DS    0H
         CLC   LEFTSID,WACSID          SAME AS ALREADY GOT ?
         BNE   COMMNL                  NO, GO TRY RIGHT SIDE
COMMSL   DS    0H
         OC    SMF74NXT,SMF74NXT       LAST REC FOR THIS INTERVAL ?
         BNZ   COMM1                   NO, SKIP
         L     R0,TOTSAMA              YES,
         A     R0,WACSAM                ACCUMULATE TOTAL
         ST    R0,TOTSAMA               NUMBER OF SAMPLES ON LEFT
         B     COMM1
         SPACE
COMMNL   DS    0H
         OC    RGHTSID,RGHTSID         SID FOUND FOR THE RIGHT ?
         BNZ   COMMTR                  YES, GO SEE IF SAME
         MVC   RGHTSID,WACSID          STORE SID FOR THE RIGHT
         B     COMMSR                  GO TOTAL SAMPLES FOR RIGHT
COMMTR   DS    0H
         CLC   RGHTSID,WACSID          SAME AS ALREADY GOT ?
         BNE   COMMER                  NO, TOO MANY SID'S
COMMSR   DS    0H
         OC    SMF74NXT,SMF74NXT       LAST REC FOR THIS INTERVAL ?
         BNZ   COMM1                   NO, SKIP
         L     R0,TOTSAMB              YES,
         A     R0,WACSAM                ACCUMULATE TOTAL
         ST    R0,TOTSAMB               NUMBER OF SAMPLES ON LEFT
         B     COMM1
         SPACE
COMMER   DS    0H
         LA    R3,MSG4                 NEITHER, TOO MANY SID'S
         B     GENERROR                  GO TO ABEND
         SPACE
COMM1    DS    0H
         MVC   WINT,SMF74INT           INT DURATION (MMSSTTTF)
         ZAP   WMINS,=P'0'
         MVC   WMINM,WINTM             SEPARATE THE MINUTES
         XC    WINTM,WINTM             EXCLUDE MINS
         MP    WMINS,=P'6000'          CONVERT MINS TO MSECS
         AP    WINT,WMINS              ADD BACK INTO INT DURATION
         CVB   R0,WINTD                INT DURATION IN BINARY
         ST    R0,WACINT               SAVE INT DURATION IN MSECS
         SPACE
         MVC   WHALF,SMF74SIZ          GET SIZE OF CCDS
         LH    R9,WHALF                ACCUMULATE IT
         MVC   WHALF,SMF74SDC          GET SIZE OF DCDS
         AH    R9,WHALF                ACCUMULATE IT
         MVC   WHALF,SMF74SDD          GET SIZE OF DDS
         LH    R7,WHALF                SIZE OF EACH DDS
         MVC   WHALF,SMF74DEV          GET # DEVICE DATA SECTIONS
         LH    R8,WHALF                # DDS'S TO BE PROCESSED
         LA    R9,18(R9,R10)           @ 1ST DEVICE DATA SECTION
         USING SMF74ADD,R9
         EJECT
*---------------------------------------------------------------------*
*  4. PROCESS ONE DEVICE DATA SECTION, SAVING THE DATA IN A SECTION   *
*     OF THE ACCUMULATION AREA FOR THIS SYSTEM/ DEVICE/ VOLUME        *
*     COMBINATION.                                                    *
*---------------------------------------------------------------------*
PROCDDS  DS    0H
         LTR   R8,R8                   ANY DDS'S REMAINING ?
         BNP   READREC                 NO, GO READ NEXT RECORD
         SPACE
         CLC   =XL(L'SMF74SER)'00',SMF74SER  ALL ZERO VOLSER ?
         BE    DDSDONE                 YES, IGNORE
         CLC   =CL(L'SMF74SER)' ',SMF74SER  BLANK VOLSER ?
         BE    DDSDONE                 YES, IGNORE THIS DDS
         MVO   WHALF,SMF74ADD          SHUFFLE UP THE UNIT NUMBER
         MVC   WACUNT,WHALF            SAVE UNIT NUMBER
         MVC   WACSER,SMF74SER         SAVE VOLSER
         SPACE
* SEARCH THE ACCUMULATION AREA FOR THIS DEVICE/VOLSER/SID ...
         LA    R4,ACCSEGM              @ FIRST SEGMENT
         USING ACCSEGM,R4
SEARCH   DS    0H
         CLI   ACCIDT,ACCIFI           END OF SEGMENTS ?
         BNE   CONTS                   NO, CONTINUE
         LA    R3,MSG2                 YES, POINT AT ERROR MESSAGE
         B     GENERROR                 AND GO TO ERROR ROUTINE
         SPACE
CONTS    DS    0H
         CLI   ACCIDT,ACCIUN           EMPTY SEGMENT ?
         BE    SEGNEW                  YES, USE IT
         CLC   ACCKEY,WACKEY           THIS SEGMENT SAME AS RECORD ?
         BE    SEGUSE                  MATCH, USE THIS ONE
         LA    R4,ACCSEGL(,R4)         ELSE TRY NEXT SEGMENT
         B     SEARCH
         SPACE
SEGNEW   DS    0H
         MVC   ACCKEY,WACKEY           STORE UNIT/VOLSER/SYSID
         MVC   ACCADD,SMF74ADD         STORE FULL DEVICE ADDRESS (1ST)
SEGUSE   DS    0H
         OC    ACCCNF,SMF74CNF         STORE WORST CONFIGURATION FLAGS
         OC    SMF74RSV,SMF74RSV       HAS THIS SYSTEM DONE RESERVE ?
         BZ    SEGU1                   NO, SKIP
         OI    ACCMIS,ACCRSV           YES, SET MISC INDICATOR
SEGU1    DS    0H
         L     R0,WACINT
         A     R0,ACCINT
         ST    R0,ACCINT               UPDATE INTERVAL DURATION
         L     R0,WACSAM
         A     R0,ACCSAM
         ST    R0,ACCSAM               UPDATE NUMBER OF SAMPLES
         MVC   WWORD,SMF74CNT
         L     R0,WWORD
         A     R0,ACCCNT
         ST    R0,ACCCNT               UPDATE NUMBER OF REQUESTS
         C     R0,MAXCNT               IS COUNT NOW < CURRENT MAX ?
         BNH   SEGU2                   NO, SKIP
         ST    R0,MAXCNT               YES, UPDATE MAX NUMBER OF REQS
SEGU2    DS    0H
         MVC   WWORD,SMF74QUE
         L     R0,WWORD
         A     R0,ACCQUE
         ST    R0,ACCQUE               UPDATE NUMBER OF ENQUEUED REQ
         MVC   WWORD,SMF74CUB
         L     R0,WWORD
         A     R0,ACCCUB
         ST    R0,ACCCUB               UPDATE # SAMPLES WHEN CU BUSY
         MVC   WWORD,SMF74RDL
         L     R0,WWORD
         A     R0,ACCRDL
         ST    R0,ACCRDL               UPDATE # SAMPLES WHEN SHARED
         MVC   WWORD,SMF74ACT
         L     R0,WWORD
         A     R0,ACCACT
         ST    R0,ACCACT               UPDATE # SAMPLES WHEN DEV BUSY
         SPACE
         DROP  R4
DDSDONE  DS    0H
         AR    R9,R7                   POSSIBLE NEXT DDS
         BCTR  R8,0                    DECR COUNT OF DDS'S
         B     PROCDDS                 GO TO PROCESS IT
         EJECT
*---------------------------------------------------------------------*
*  5. WHEN ALL THE DATA HAS BEEN ACCUMULATED,  SORT THE ACCUMULATION  *
*     TABLE INTO DEVICE ADDRESS AND SYSTEM SEQUENCE.                  *
*---------------------------------------------------------------------*
EODADDR  DS    0H            EODAD FOR SYSUT1
         CLOSE SYSUT1
         SPACE
         MSORT ACCAREA,L'ACCKEY,ACCSEGL,,A
         EJECT
*---------------------------------------------------------------------*
*  6. SET UP VARIABLES IN THE HEADINGS.                               *
*---------------------------------------------------------------------*
* CONVERT FIRST AND LAST DATES TO DD MMM YY FORMAT IN 1ST HEADING.
         OC    FIRDTEST,FIRDTEST       HAVE WE GOT THE FIRST DATE ?
         BZ    NODATES
         LA    R1,UTDTCP1              )
         L     R15,=V(UTDTC)           × CONVERT 00YYDDDF (PD+) TO
         BALR  R14,R15                 × DD MMM YY (CHAR) AND PLACE
         LA    R1,UTDTCP2              × INTO THE HEADING:  FIRSTD TO
         L     R15,=V(UTDTC)           × H1F,  LASTD TO H1T
         BALR  R14,R15                 )
NODATES  DS    0H
         SPACE
         CLC   LEFTSID,RGHTSID         ENSURE THAT THE LOWEST
         BNH   NOSWAP                   SID IS THE
         XC    LEFTSID,RGHTSID          ONE ON
         XC    RGHTSID,LEFTSID          THE LEFT SIDE
         XC    LEFTSID,RGHTSID          OF THE REPORT.
         XC    TOTSAMA,TOTSAMB          (ALSO SWAP
         XC    TOTSAMB,TOTSAMA           SAMPLE COUNTS
         XC    TOTSAMA,TOTSAMB           TO MATCH)
NOSWAP   DS    0H
         SPACE
         MVC   H2A,LEFTSID             SYSTEM ID
         MVC   H2B,RGHTSID             SYSTEM ID
         L     R0,TOTSAMA              TOTAL SAMPLES ON LEFT
         CVD   R0,DWORD                 TO DECIMAL
         EDIT  H2SA,DWORD+4,'XXXXXX0'  EDIT TOTAL SAMPLES
         L     R0,TOTSAMB              TOTAL SAMPLES ON RIGHT
         CVD   R0,DWORD                 TO DECIMAL
         EDIT  H2SB,DWORD+4,'XXXXXX0'  EDIT TOTAL SAMPLES
         SPACE
         MVC   H2V,VERMFV              RMF/CMF VERSION
         UNPK  H2C(L'H2C+1),VERRV1(L'VERRV1+1)  CMF IDENTIFIER
         MVI   H2C+L'H2C,C' '            × BLANK EXTRA BYTE
         NI    H2C,B'00001111'           × ENSURE ZONE '0'
         AIF   (L'H2C LT 2).VER1
         MVZ   H2C+1(L'H2C-1),H2C        × IN ALL CHARS
.VER1    ANOP
         TR    H2C,=C'0123456789ABCDEF'  × MAKE PROPER CHARS
         MVC   H2I,VERIND              * IF POSSIBLE DOUBLE SAMPLING
         SPACE
         B     REPORT
         SPACE
UTDTCP1  DC    A(DTCC,FIRSTD,H1F,ZERO)    PARAMETERS FOR UTDTC
         ORG   *-4
         DC    X'80'                      VL BIT
         ORG
UTDTCP2  DC    A(DTCC,LASTD,H1T,ZERO)
         ORG   *-4
         DC    X'80'                      VL BIT
         ORG
DTCC     DC    C'D',C' ',C'J',C'F',C'    '  UTDTC COMMAND BLOCK
ZERO     DC    F'0'           DUMMY FOR UTDTC
         EJECT
*---------------------------------------------------------------------*
*  7. ACCESS THE ACCUMULATION AREA, SEGMENT BY SEGMENT, PRINTING      *
*     THE REPORT AS WE GO.                                            *
*---------------------------------------------------------------------*
REPORT   DS    0H
         SPACE
*   ---------- CALCULATE THE SCALE OF THE ACTIVITY GRAPH ----------
         SLR   R0,R0
         L     R1,MAXCNT               GET MAX ACTIVITY COUNT
         AH    R1,=AL2(L'DACGR-1)      (ROUND UP)
         D     R0,=A(L'DACGR)          = TOKEN SIZE IN GRAPH
         C     R1,=F'1000'             < 1000 ?
         BL    REP1                    YES, LEAVE AS IS
         SLR   R0,R0                   NO,
         D     R0,=F'1000'              ROUND UP
         SLR   R0,R0                    TO
         AH    R1,=H'1'                 NEXT
         M     R0,=F'1000'              FULL 1000
REP1     DS    0H
         ST    R1,DACGS                SAVE DEVICE ACTIVITY GRAPH SCALE
         SRA   R1,1                    GET HALF SCALE
         ST    R1,DACGT                SAVE IT FOR ROUNDING
         SPACE
*   ---------- INITIALISE THE CONTROL KEYS ----------
         XC    WACKEY,WACKEY           ZERO 'LAST' UNIT/VOLSER/SYSID
         LA    R4,ACCSEGM              @ FIRST SEGMENT
         USING ACCSEGM,R4
LOCATE   DS    0H
         CLI   ACCIDT,ACCIFI           END OF SEGMENTS ?
         BE    REPDONE                  -OR-
         CLI   ACCIDT,ACCIUN           EMPTY SEGMENT ?
         BE    NEXTSEG                 YES, SKIP OVER IT
         SPACE
         CLC   ACCUNT,WACUNT           SAME UNIT AS LAST TIME ?
         BNE   NEWLINE1                NO, GO START A NEW LINE
         CLC   ACCSER,WACSER           SAME VOLSER AS LAST TIME ?
         BNE   NEWLINE2                NO, GO START A NEW LINE
         CLC   ACCSID,WACSID           SAME SID AS LAST TIME ?
         BNE   SAMELINE                NO, PUT OTHER SYS IN THIS LINE
         LA    R3,MSG3                 YES, PROGRAM LOGIC ERROR
         B     GENERROR                GO TO ABEND
         SPACE
NEWLINE1 DS    0H            START NEW LINE, NEW UNIT
         BAL   R14,PUT                 PRINT PREV (IF ANY) LINE
         XC    WACUNT,ACCUNT           IS IT IN SAME STRING AS LAST ?
         TM    WACUNT,B'11111000'       (SAME 1'S ->0, NE BITS ->1)
         MVC   WACUNT,ACCUNT           SAVE THIS UNIT NUMBER
         BZ    NEWL1                   SAME STRING, SINGLE SPACE
         MVI   LCC,C'0'                DIFFERENT STRING, DOUBLE SPACE
         B     NEWL1                   GO TO PROCESS VOLSER
         SPACE
NEWLINE2 DS    0H            START NEW LINE, SAME UNIT, NEW VOLSER
         BAL   R14,PUT                 PRINT PREV (IF ANY) LINE
NEWL1    DS    0H
         MVC   WACSER,ACCSER           SAVE THIS VOL SERIAL
         MVC   LV,ACCSER               VOL SERIAL TO LINE
         SPACE
SAMELINE DS    0H
         MVC   WACSID,ACCSID           SAVE THIS SYSTEM ID
         SPACE
         TM    ACCCNF,ACCVAC           VARIED DURING AN INTERVAL ?
         BZ    SAMEL1                  NO, SKIP
         MVI   LI,C'*'                 YES, INDICATE ON REPORT
SAMEL1   DS    0H
         SPACE
         CLC   LEFTSID,WACSID          IS THIS FOR THE LEFT ?
         BE    LEFTSIDE                YES, GO DO LEFT SIDE
         CLC   RGHTSID,WACSID          IS THIS FOR THE RIGHT SIDE ?
         BE    RGHTSIDE                YES, GO DO RIGHT SIDE
         LA    R3,MSG8                 NEITHER, INTERNAL ERROR
         B     GENERROR                  GO TO ABEND
         SPACE
LEFTSIDE DS    0H            EDIT DATA ONTO LEFT SIDE
         UNPK  LDA,ACCADD              DEVICE ADDRESS
         NI    LDA,B'00001111'         × ENSURE ZONE '0'
         AIF   (L'LDA LT 2).LEFTS1
         MVZ   LDA+1(L'LDA-1),LDA      × IN ALL CHARS
.LEFTS1  ANOP
         TR    LDA,=C'0123456789ABCDEF' × MAKE PROPER CHARS
         BAL   R14,CALCULAT            GO DO THE CALCULATIONS
         MVC   LAA,INVTAB              INVERT THE DEVICE ACTIVITY
         TR    LAA,DACGR                GRAPH ON THE LEFT
         EDIT  L0A,QPDEV,'XXX'         EDIT DEV Q %
         EDIT  L1A,QPCU,'XXX'          EDIT CU Q %
         EDIT  L2A,QPRES,'XXX'         EDIT RES Q %
         EDIT  L3A,AQT,'X0.XXX'        EDIT AVG Q TIME
         EDIT  L4A,DST,'X0.XXX'        EDIT DEV SERV TIME
         EDIT  L5A,AIT,'X0.XXX'        EDIT AVG I/O TIME
         CP    EFF,=P'100'             IS EFFICIENCY = 100 % ?
         BE    LEFTS2                  YES, LEAVE IT BLANK
         EDIT  L6A,EFF,'XXX'           EDIT EFFICIENCY %
LEFTS2   DS    0H
         B     NEXTSEG                 GO TO GET THE NEXT SEGMENT
         SPACE
RGHTSIDE DS    0H            EDIT DATA ONTO RIGHT SIDE
         UNPK  LDB,ACCADD              DEVICE ADDRESS
         NI    LDB,B'00001111'         × ENSURE ZONE '0'
         AIF   (L'LDB LT 2).RGHTS1
         MVZ   LDB+1(L'LDB-1),LDB      × IN ALL CHARS
.RGHTS1  ANOP
         TR    LDB,=C'0123456789ABCDEF' × MAKE PROPER CHARS
         BAL   R14,CALCULAT            GO DO THE CALCULATIONS
         CP    EFF,=P'100'             IS EFFICIENCY = 100 % ?
         BE    RGHTS2                  YES, LEAVE IT BLANK
         EDIT  L6B,EFF,'XXX'           EDIT EFFICIENCY %
RGHTS2   DS    0H
         EDIT  L5B,AIT,'X0.XXX'        EDIT AVG I/O TIME
         EDIT  L4B,DST,'X0.XXX'        EDIT DEV SERV TIME
         EDIT  L3B,AQT,'X0.XXX'        EDIT AVG Q TIME
         EDIT  L2B,QPRES,'XXX'         EDIT RES Q %
         EDIT  L1B,QPCU,'XXX'          EDIT CU Q %
         EDIT  L0B,QPDEV,'XXX'         EDIT DEV Q %
         MVC   LAB,DACGR               PUT IN DEVICE ACT GRAPH
         SPACE 2
NEXTSEG  DS    0H
         LA    R4,ACCSEGL(,R4)         ELSE TRY NEXT SEGMENT
         B     LOCATE
         DROP  R4
         EJECT
*---------------------------------------------------------------------*
*  8. THE REPORT IS FINISHED, PRINT LAST LINE AND SUMMARY MESSAGE.    *
*     THEN TERMINATE THE RUN.                                         *
*---------------------------------------------------------------------*
         SPACE
REPDONE  DS    0H
         BAL   R14,PUT                 PRINT FINAL LINE (IF ANY)
         CP    RECNUM,=P'0'            NUMBER OF RECORDS = ZERO ?
         BNE   FINEDIT
         MVC   LINE(L'MSG5),MSG5       NO RECORDS SELECTED MSG
         MVC   RETCODE,=H'8'           SET BAD RETURN CODE
         B     FINPUT                  GO TO PRINT IT AND END
         SPACE
FINEDIT  DS    0H
         L     R0,DACGS                CONVERT GRAPH SCALE
         CVD   R0,DWORD                 TO DECIMAL
         EDIT  MSG0R,DWORD+3,'XXX,XXX,XX$' EDIT GRAPH SCALE
         MVI   0(R1),C' '              BLANK OUT THE $
         LR    R2,R1                   MVCL SOURCE ADDR
         LA    R3,MSG0+L'MSG0
         SR    R3,R1                   MVCL SOURCE LENGTH
         ICM   R3,B'1000',=C' '        FILL CHARACTER
         LA    R0,MSG0R                MVCL RESULT ADDRESS
         LA    R1,MSG0+L'MSG0-MSG0R    MVCL RESULT LENGTH
         MVCL  R0,R2                   MOVE GRAPH SCALE TO LEFT
         EDIT  MSG0C,RECNUM,'XXXX$'    EDIT SELECTED REC COUNT
         MVI   0(R1),C' '              BLANK OUT THE $
         LR    R2,R1                   MVCL SOURCE ADDRESS
         LA    R3,MSG0+L'MSG0
         SR    R3,R1                   MVCL SOURCE LENGTH
         ICM   R3,B'1000',=C' '        FILL CHARACTER
         LA    R0,MSG0C                MVCL RESULT ADDRESS
         LA    R1,MSG0+L'MSG0-MSG0C    MVCL RESULT LENGTH
         MVCL  R0,R2                   MOVE REC COUNT TO LEFT
         MVC   LINE(L'MSG0),MSG0       FINAL MESSAGE TO LINE
FINPUT   DS    0H
         MVI   LCC,C'0'                ENSURE BLANK LINE
         BAL   R14,PUT                 PRINT FINAL LINE
         SPACE 2
         CLOSE SYSPRINT
         SPACE
         LH    R15,RETCODE
         LEAVE CODE=(15)
         SPACE
RETCODE  DC    H'0'
         EJECT
*---------------------------------------------------------------------*
*     GENERAL ERROR ROUTINE ...                                       *
*     PARAMETERS:  REG 3  CONTAINS @ MESSAGE (ID AND TEXT ONLY)       *
*     THE MESSAGE IS PRINTED.                                         *
*     IF THE MESSAGE ID HAS A SEVERITY OF 'E' OR WORSE;               *
*     AN ABEND CODE IS DEDUCED FROM THE MESSAGE IDENTIFIER,           *
*     GENERAL REGISTERS ARE SAVED IN AN AREA IDENTIFIABLE IN A DUMP,  *
*     AND ABEND IS INVOKED                                            *
*---------------------------------------------------------------------*
GENERROR DS    0H
         STM   R14,R1,GENSAVE          SAVE SYSTEM REGISTERS
         BAL   R14,PUT                 PRINT PREV LINE IF ANY
         SLR   R0,R0
         IC    R0,0(R3)                GET MSG LENGTH
         BCTR  R0,0                    - 1
         EX    R0,MOVEMSG              MESSAGE ONTO THE LINE
         BAL   R14,PUT                 SEND MESSAGE
         CLI   8(R3),C'E'              ERROR ?
         BE    GENAB                   YES, ABEND
         CLI   8(R3),C'D'              DISASTER ?
         BE    GENAB                   YES, ABEND
         MVC   RETCODE,=H'4'           ELSE, SET POOR RETURN CODE
         LM    R14,R1,GENSAVE           AND
         BR    R14                      RETURN
GENAB    DS    0H
         PACK  GENDW,6(2,R3)           GET MESSAGE NUMBER
         CVB   R1,GENDW                IN BINARY = ABEND CODE
         ABEND (1),DUMP                *** ABEND ***
         SPACE
MOVEMSG  MVC   LINE(0),1(R3)           MOVE MESSAGE ONTO THE LINE
GENDW    DC    D'0'                    ABEND CODE (PACKED)
         SPACE
         DC    C'  R14 R15 R0 R1 '     LOCATOR
GENSAVE  DC    4A(0)                   SAVE AREA
         DC    4C' '                   SEPARATOR
         EJECT
*---------------------------------------------------------------------*
*     SUBROUTINE TO PERFORM THE REQUIRED CALCULATIONS                 *
*     ON ENTRY - R4 CONTAINS THE BASE FOR THE ACCUMULATOR SEGMENT     *
*     THE RESULTS ARE PLACED IN DATA STORAGE AREAS (PACKED DECIMAL)   *
*---------------------------------------------------------------------*
         USING ACCSEGM,R4
CALCULAT DS    0H
*     -------- DEVICE ACTIVITY GRAPH ---------
         MVI   DACGR,C' '              CLEAR THE GRAPH
         MVC   DACGR+1(L'DACGR-1),DACGR  TO SPACES
         L     R1,ACCCNT               GET COUNT
         SLR   R0,R0                   IN E-O PAIR
         D     R0,DACGS                R1= NUMBER OF TOKENS
         C     R0,DACGT                C REMAINDER WITH HALF
         BNL   CALCG1                  GE, ALLOW AN EXTRA ASTERISK
         SH    R1,=H'1'                LT, REDUCE FOR EXECUTE
         BM    CALCG2                  IF NOW -VE, SKIP
CALCG1   DS    0H
         EX    R1,MOVEAST              MOVE REQUIRED NUMBER OF *'S
CALCG2   DS    0H
         C     R0,DACGT                REMAINDER >= HALF A TOKEN
         BNL   CALCG3                  YES, LEAVE AS IS
         C     R0,=F'1000'             REMAINDER < 1K ?
         BL    CALCG3                  YES, LEAVE AS IS
         LA    R1,DACGR+1(R1)          POINT AT CHAR AFTER LAST *
         MVI   0(R1),C''''             ADD A PART INDICATOR
CALCG3   DS    0H
         SPACE
*    --------- PERCENTAGES ------------
CALCPC   DS    0H
         ZAP   QPDEV,=P'0'             SET ALL PERCENTS
         ZAP   QPCU,=P'0'               TO ZERO
         ZAP   QPRES,=P'0'
         ZAP   EFF,=P'0'
         L     R3,ACCQUE               R3 = QUE
         A     R3,ACCACT                    + ACT
         LTR   R5,R3                   IS DIVISOR = ZERO ?
         BZ    CALCTM                  YES, ALL %'S MUST BE ZERO
         SRA   R5,1                    R5 = ROUNDING FACTOR
         SPACE
*              DEV Q % = ((QUE-CUB-RDL)*100)/(QUE+ACT)
         SLR   R0,R0
         L     R1,ACCQUE               R0/1 = QUE
         S     R1,ACCCUB                      - CUB
         S     R1,ACCRDL                      - RDL
         M     R0,=F'100'
         AR    R1,R5
         DR    R0,R3
         CVD   R1,DWORD
         CP    DWORD,=P'100'           RESULT > 100 ?
         BNH   CALC1R
         ZAP   DWORD,=P'999'           YES, INDICATE ERROR
CALC1R   ZAP   QPDEV,DWORD             STORE RESULT
*              CU  Q % = (CUB*100)/(QUE+ACT)
         SLR   R0,R0
         L     R1,ACCCUB
         M     R0,=F'100'
         AR    R1,R5
         DR    R0,R3
         CVD   R1,DWORD
         CP    DWORD,=P'100'           RESULT > 100 ?
         BNH   CALC2R
         ZAP   DWORD,=P'999'           YES, INDICATE ERROR
CALC2R   ZAP   QPCU,DWORD              STORE RESULT
*              RES Q % = (RDL*100)/(QUE+ACT)
         SLR   R0,R0
         L     R1,ACCRDL
         M     R0,=F'100'
         AR    R1,R5
         DR    R0,R3
         CVD   R1,DWORD
         CP    DWORD,=P'100'           RESULT > 100 ?
         BNH   CALC3R
         ZAP   DWORD,=P'999'           YES, INDICATE ERROR
CALC3R   ZAP   QPRES,DWORD             STORE RESULT
*              EFFIC % = (ACT*100)/(QUE+ACT)
         SLR   R0,R0
         L     R1,ACCACT
         M     R0,=F'100'
         AR    R1,R5
         DR    R0,R3
         CVD   R1,DWORD
         CP    DWORD,=P'100'           RESULT > 100 ?
         BNH   CALC4R
         ZAP   DWORD,=P'999'           YES, INDICATE ERROR
CALC4R   ZAP   EFF,DWORD               STORE RESULT
         SPACE
CALCTM   DS    0H
*    --------- AVERAGE TIME PER I/O REQ ------------
         ZAP   DST,=P'0'               PRESET ALL TIME
         ZAP   AQT,=P'0'                RESULTS TO ZERO.
         ZAP   AIT,=P'0'
*              DEV SERV TIME = (INT/(SAM*CNT))*(ACT)
CALC5    DS    0H
         L     R0,ACCSAM
         FPLD  F0,R0                   F0 = SAM
         BZ    CALCDN                  IF ZERO, ALL TIMES MUST BE ZERO
         L     R0,ACCCNT
         FPLD  F2,R0                   F2 = CNT
         BZ    CALCDN                  IF ZERO, ALL TIMES MUST BE ZERO
         MDR   F0,F2                   F0 = SAM * CNT
         HDR   F2,F0                   F2 = ROUNDING FACTOR (F0/2)
         SPACE
         L     R0,ACCINT
         FPLD  F6,R0                   F6 = INT (FOR ALL CALCS)
         BZ    CALCDN                  IF ZERO, ALL TIMES MUST BE ZERO
         SPACE
         L     R0,ACCACT
         FPLD  F4,R0                   F4 = ACT
         STD   F4,FDACT                (ALSO SAVED FOR LATER)
         BZ    CALC6                   IF ZERO, LEAVE DST = ZERO
         MDR   F4,F6                   F4 = ACT * INT
         ADR   F4,F2                        + ROUNDING FACTOR
         DDR   F4,F0                        / SAM * CNT
         FPST  R0,F4,CALC5O
         CVD   R0,DWORD
         CP    DWORD,=P'100000'        RESULT >= 100 SECS ?
         BL    CALC5R
CALC5O   ZAP   DWORD,=P'99000'         YES, INDICATE ERROR
CALC5R   ZAP   DST,DWORD               STORE RESULT
*              AVG Q TIME = (INT/(SAM*CNT)*QUE
CALC6    DS    0H
         L     R0,ACCQUE               R0 = QUE
         FPLD  F4,R0
         STD   F4,FDTOT                (ALSO SAVED FOR LATER)
         BZ    CALC7                   IF ZERO, LEAVE AQT = ZERO
         MDR   F4,F6                   F4 = QUE * INT
         ADR   F4,F2                        + ROUNDING FACTOR
         DDR   F4,F0                        / SAM * CNT
         FPST  R0,F4,CALC6O
         CVD   R0,DWORD
         CP    DWORD,=P'100000'        RESULT >= 100 SECS ?
         BL    CALC6R
CALC6O   ZAP   DWORD,=P'99000'         YES, INDICATE ERROR
CALC6R   ZAP   AQT,DWORD               STORE RESULT
*              AVG I/O TIME = (INT/(SAM*CNT)*(QUE+ACT)
CALC7    DS    0H
         LD    F4,FDTOT                F4 = QUE
         AD    F4,FDACT                     + ACT
         BZ    CALCDN                  IF ZERO, LEAVE AIT = ZERO
         MDR   F4,F6                   F4 = (QUE+ACT) * INT
         ADR   F4,F2                        + ROUNDING FACTOR
         DDR   F4,F0                        / SAM * CNT
         FPST  R0,F4,CALC7O
         CVD   R0,DWORD
         CP    DWORD,=P'100000'        RESULT >= 100 SECS ?
         BL    CALC7R
CALC7O   ZAP   DWORD,=P'99000'         YES, INDICATE ERROR
CALC7R   ZAP   AIT,DWORD               STORE RESULT
         SPACE
CALCDN   DS    0H
         DROP  R4
         SPACE
         BR    R14
         SPACE
MOVEAST  MVC   DACGR(0),=20C'*' MOVE REQ NUMBER OF *'S TO GRAPH
         SPACE
         FPDEF
FDACT    DC    D'0'
FDTOT    DC    D'0'
         EJECT
*---------------------------------------------------------------------*
*     SUBROUTINE TO PRINT THE LINES OF THE REPORT                     *
*---------------------------------------------------------------------*
PUT      DS    0H
         ST    R14,PUTSAVE
         CLI   LINE,C' '               IS THE LINE
         BNE   PUT1
         CLC   LINE+1(L'LINE-1),LINE     ALL BLANK ?
         BER   R14                     YES, IGNORE IT
PUT1     DS    0H
         MVI   PUTC,X'0C'              PRESET ZERO LINES
         TM    LCC,B'00001111'         IF CC NOT X0 (+)
         BNZ   PUT2                     USE ZERO LINES
         MVZ   PUTC,LCC                FOR _ 0 - CONVERT TO
         NI    PUTC,B'00111111'         NUMBER OF LINES USED
         XI    PUTC,B'00010000'           (1 2 3)
PUT2     DS    0H
         L     R2,=A(SYSPRINT)         @ DCB FOR PUTS
         CP    LINECT,PUTC             ENOUGH LINES ON CURRENT PAGE ?
         BNL   PUT3                    YES, GO PUT IT
         CLI   H0,C' '                 IS THERE PARM DATA ON PREFIX ?
         BE    PUTNP                   NO, DON'T PRINT PREFIX LINE
         MVI   H1,C' '                 YES, SINGLE SPACE HEADING LINE 1
         PUT   (2),H0                       PRINT HEADING PREFIX
PUTNP    DS    0H
         AP    PAGNUM,=P'1'
         EDIT  H1P,PAGNUM,'XX0'        PAGE NUMBER
         PUT   (2),H1                  PUT HEADING LINE 1
         PUT   (2),H2                  PUT HEADING LINE 2
         MVC   H21A,H2A                SETUP OVERPRINT FOR HEADING
         MVC   H21B,H2B                 LINE 2 (SYSTEM IDS)
         PUT   (2),H21                 OVERPRINT HEADING LINE 2
         PUT   (2),H21                  AND AGAIN
         PUT   (2),H3                  PUT HEADING LINE 3
         PUT   (2),H4                  PUT HEADING LINE 4
         PUT   (2),H5                  PUT HEADING LINE 5
         ZAP   LINECT,=P'53'           SET NEW LINE COUNT
         MVI   LCC,C'0'                ENSURE SPACE BEFORE FIRST LINE
         SPACE
PUT3     DS    0H
         PUT   (2),LINEREC             PUT THE LINE
         SP    LINECT,PUTC             DECREMENT LINE COUNT
         MVI   LCC,C' '                SET SINGLE SPACING
         MVC   LINE,LCC                BLANK OUT THE LINE
         L     R14,PUTSAVE
         BR    R14
         SPACE
PUTSAVE  DC    A(0)                    STORE FOR RETURN ADDRESS
PAGNUM   DC    PL2'0'                  CURRENT PAGE NUMBER
PUTC     DC    P'0'                    NUMBER OF LINES USED
         EJECT
*---------------------------------------------------------------------*
*     DATA STORAGE AND WORKING AREAS                                  *
*---------------------------------------------------------------------*
         SPACE
         LTORG
         SPACE
WACUNT   DC    X'00'         UNIT NUMBER    ×
WACSER   DC    CL6' '        VOLSER         ×
WACSID   DC    CL4' '        SYSTEM ID      V
WACKEY   EQU   WACUNT,*-WACUNT        IDENTIFYING KEY
         SPACE
WACINT   DC    F'0'          DURATION OF INTERVAL IN MSECS
*                                      MAX 4,294,967,295 ...
WACSAM   DC    F'0'          NUMBER OF SAMPLES
TOTSAMA  DC    F'0'          TOTAL NUMBER OF SAMPLES (LEFT)
TOTSAMB  DC    F'0'          TOTAL NUMBER OF SAMPLES (RIGHT)
MAXCNT   DC    F'0'          MAXIMUM NUMBER OF REQUESTS
         SPACE
VERSION  DC    XL(L'SMF74MFV+L'SMF74RV1)'0' RMF/CMF VERSION
VERMFV   EQU   VERSION,L'SMF74MFV      VERSION IN EBCDIC
VERRV1   EQU   VERSION+L'SMF74MFV,L'SMF74RV1  CMF VERSION IN HEX
VERIND   DC    C' '          * MEANS POSSIBLE DOUBLE SAMPLING
         SPACE
WINTD    DC    D'0'
WINT     EQU   WINTD+4,4     MMSSTTTF  DURATION FROM RECORD
WINTM    EQU   WINTD+4,1     MM        MINUTES
         SPACE
WMINS    DC    PL5'0'        000000000F  FOR MM IN MSECS
WMINM    EQU   WMINS+3,1           MM    MINUTES
         SPACE
RECNUM   DC    PL3'0'        NUMBER OF RECORDS SELECTED
FIRSTD   DC    PL4'0'        00YYDDDF EARLIEST DATE SELECTED
FIRDTEST EQU   FIRSTD,2      XXXX      TO TEST FOR NO DATE
LASTD    DC    PL4'0'        00YYDDDF LATEST DATE SELECTED
         SPACE
LEFTSID  DS    CL4                     SYSTEM ID ON LEFT
         ORG   LEFTSID
         DC    (L'LEFTSID)X'00'         INITIALIZE TO ZEROS
         ORG
RGHTSID  DS    CL4                     SYSTEM ID ON RIGHT
         ORG   RGHTSID
         DC    (L'RGHTSID)X'00'         INITIALIZE TO ZEROS
         ORG
         SPACE
DWORD    DC    D'0'
         ORG   DWORD
WWORD    DC    F'0'
WHALF    DC    H'0'
         ORG
         EJECT
*---------------------------------------------------------------------*
*  DEFINE DATA FOR THE REPORT                                         *
*---------------------------------------------------------------------*
LINEREC  DC    CL133' '      REPORT RECORD AREA
LCC      EQU   LINEREC,1               CONTROL CHAR (ASA)
LINE     EQU   LINEREC+1,132           DATA LINE
LINECT   DC    PL2'0'        LINES REMAINING ON PAGE
         SPACE
H0       DC    C' '                    SET TO '1' WHEN PARM DATA
H0L      DC    CL(L'LINEREC)' '        PRINTED WHEN PARM DATA ONLY
         SPACE
H1       DC    C'1'
H1L      DC    C'SMDDA - DASD PERFORMANCE ANALYSIS                     X
               TIMES:  0800-1700   DATES:  DD MMM YY  TO  DD MMM YY    X
                             PAGE 999'
H1F      EQU   H1L+82,9      DD MMM YY    FROM DATE
H1T      EQU   H1L+97,9      DD MMM YY    TO DATE
H1P      EQU   H1L+128,4     _999         PAGE NUMBER
         SPACE
H2       DC    C'0'
H2L      DC    C'                         AAAA _XXXXXXX SAMPLES        X
                    EXTRACTOR XX XXXX *         _XXXXXXX SAMPLES  BBBB X
                                     '
H2A      EQU   H2L+25,4
H2SA     EQU   H2L+30,8
H2V      EQU   H2L+69,2
H2C      EQU   H2L+72,4
H2I      EQU   H2L+77,1
H2SB     EQU   H2L+87,8
H2B      EQU   H2L+105,4
H21      DC    C'+'                    (OVERPRINT FOR HEADING 2)
H21L     DC    C'                         AAAA                         X
                                                                  BBBB X
                                     '
H21A     EQU   H21L+H2A-H2L,L'H2A
H21B     EQU   H21L+H2B-H2L,L'H2B
         SPACE
H3       DC    C' '
         DC    C'EFFIC- AVG    DEV    AVG    % I/O TIME             DEVX
               ICE       VOL       DEVICE               % I/O TIME   AVX
               G    DEV    AVG EFFIC-'
H4       DC    C' '
         DC    C'IENCY  I/O    SERV  QUEUE  -QUEUED ON-           ACTIVX
               ITY  DEV SERIAL DEV ACTIVITY             -QUEUED ON- QUEX
               UE   SERV   I/O IENCY '
H5       DC    C' '
         DC    C'   %   TIME   TIME   TIME  RSV CU  DEV              COX
               UNT  ADR        ADR COUNT                DEV CU  RSV  TIX
               ME   TIME   TIME   %  '
*               ×__XX__X.XXX__X.XXX__X.XXX__XX__XX__XX ****************
*        LINE+  ×0   0      1      1      2   2   3    3
*               ×0   4      1      8      5   9   3    8
*              ****+XXX*VVVVVV XXX+*******************=_XX__XX__XX__X.X
*        LINE+      5   6      7   7                  9   9   0   0
*                   9   3      0   4                  3   7   1   5
*              XX__X.XXX__X.XXX__XX  ×
*        LINE+   1      1      2     ×
*                2      9      6     ×
         SPACE
* DEFINE FIELDS ON THE REPORT DATA LINE
L6A      EQU   LINE+00,4     _%%%     EFFICIENCY
L5A      EQU   LINE+04,7     _99.999  AVG I/O TIME
L4A      EQU   LINE+11,7     _99.999  DEV SER TIME
L3A      EQU   LINE+18,7     _99.999  AVG QUEUE TIME        A-SYSTEM
L2A      EQU   LINE+25,4     _%%%     RES Q %
L1A      EQU   LINE+29,4     _%%%     CU Q %
L0A      EQU   LINE+33,4     _%%%     DEV Q %
LAA      EQU   LINE+38,20    *        DEVICE ACTIVITY COUNT GRAPH
LDA      EQU   LINE+59,3     XXX      DEVICE ADDRESS
LI       EQU   LINE+62,1     *        INDICATOR (VARIED DURING INT)
LV       EQU   LINE+63,6     XXXXXX   VOL SERIAL
LDB      EQU   LINE+70,3     XXX      DEVICE ADDRESS
LAB      EQU   LINE+74,20    *        DEVICE ACTIVITY COUNT GRAPH
L0B      EQU   LINE+93,4     _%%%     DEV Q %
L1B      EQU   LINE+97,4     _%%%     CU Q %
L2B      EQU   LINE+101,4    _%%%     RES Q %
L3B      EQU   LINE+105,7    _99.999  AVG QUEUE TIME        B-SYSTEM
L4B      EQU   LINE+112,7    _99.999  DEV SER TIME
L5B      EQU   LINE+119,7    _99.999  AVG I/O TIME
L6B      EQU   LINE+126,4    _%%%     EFFICIENCY
LRA      EQU   LINE+58,1     +        INDICATOR (RESERVE DONE)
LRB      EQU   LINE+73,1     +        INDICATOR (RESERVE DONE)
         SPACE
INVTAB   DC    (L'LAA)AL1(INVTAB+L'LAA-1-*) FOR INVERTING GRAPH
         SPACE 2
*   ---------- DATA USED WHEN CALCULATING FOR THE REPORT ----------
DACGR    DC    CL(L'LAA)' '  DEVICE ACT GRAPH
QPDEV    DC    PL2'0'        DEV Q %
QPCU     DC    PL2'0'        CU Q %
QPRES    DC    PL2'0'        RES Q%%
AQT      DC    PL3'0'        AVG Q TIME
DST      DC    PL3'0'        DEV SERV TIME
AIT      DC    PL3'0'        AVG I/O TIME
EFF      DC    PL2'0'        EFFICIENCY %
         SPACE
DACGS    DC    F'0'          REQUESTS PER TOKEN
DACGT    DC    F'0'          REQUESTS PER HALF TOKEN
*   ----------                                           ----------
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                          MESSAGES ISSUED                            *
*                                                                     *
*---------------------------------------------------------------------*
MSG0     DC    C'SMDDA000I  END OF RUN, XXXXX RECORDS SELECTED, DEVICE X
               ACTIVITY COUNT SCALE; * = XXX,XXX,XXX REQUESTS'
MSG0C    EQU   MSG0+22,6
MSG0R    EQU   MSG0+79,12
         SPACE
MSG2     DC    C' SMDDA002E  NOT ENOUGH ACCUMULATION SPACE'
         ORG   MSG2
         DC    AL1(L'MSG2-1)
         ORG
MSG3     DC    C' SMDDA003E  PROGRAM LOGIC ERROR, DUPLICATE SID'
         ORG   MSG3
         DC    AL1(L'MSG3-1)
         ORG
MSG4     DC    C' SMDDA004E  INPUT SELECTED FOR MORE THAN 2 SYSTEMS'
         ORG   MSG4
         DC    AL1(L'MSG4-1)
         ORG
MSG5     DC    C' SMDDA005I  END OF RUN, *NO* RECORDS SELECTED'
         ORG   MSG5
         DC    AL1(L'MSG5-1)
         ORG
MSG6     DC    C' SMDDA006D  UNABLE TO OPEN INPUT DATASET'
         ORG   MSG6
         DC    AL1(L'MSG6-1)
         ORG
MSG7     DC    C' SMDDA007D  UNABLE TO OPEN REPORT DATASET'
         ORG   MSG7
         DC    AL1(L'MSG7-1)
         ORG
MSG8     DC    C' SMDDA008E  PROGRAM LOGIC ERROR, SID NOT STORED'
         ORG   MSG8
         DC    AL1(L'MSG8-1)
         ORG
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  DEFINE A TABLE FOR ACCUMULATING THE DATA FROM THE SMF RECORDS      *
*                                                                     *
*        NOTE: THE MAXIMUM VALUE IN A WORD IS 4,294,967,295           *
*---------------------------------------------------------------------*
         SPACE
ACCAREA  DS    0F
         SPACE
ACCSEGM  DS    0F
ACCIDT   DC    X'00'         SEGMENT IDENTITY -
ACCIUN   EQU   X'00'          UNUSED
ACCIFI   EQU   X'FF'          END MARKER
         ORG   ACCSEGM
ACCUNT   DC    X'00'         UNIT NUMBER  ×
ACCSER   DC    CL6' '        VOLSER       ×
ACCSID   DC    CL4' '        SYSTEM ID    V
ACCKEY   EQU   ACCSEGM,*-ACCSEGM  DEFINE SEGMENT KEY
ACCADD   DC    X'0000'       DEVICE ADDRESS (...F)
ACCCNF   DC    X'00'         DEVICE INDICATOR
ACCPAR   EQU   SMF74PAR       PARTIAL STATUS DURATION REPORT
ACCMXB   EQU   SMF74MXB       MULTIPLE EXP DEV, BASE EXPOSURE
ACCVAC   EQU   SMF74VAC       DEV VARIED DURING INTERVAL
ACCSTA   EQU   SMF74STA       CURRENTLY ONLINE
ACCMIS   DC    X'00'         MISCELLANEOUS INDICATORS
ACCRSV   EQU   B'10000000'    DEVICE RESERVED
ACCINT   DC    F'0'          DURATION OF INTERVAL IN MSECS
ACCSAM   DC    F'0'          NUMBER OF SAMPLES
ACCCNT   DC    F'0'          # REQUESTS SERVICED
ACCQUE   DC    F'0'          # REQUESTS ENQUEUED
ACCACT   DC    F'0'          # SAMPLES WHEN DEV BUSY & CU NOT BUSY
ACCCUB   DC    F'0'          # SAMPLES WHEN CONTROL UNIT BUSY
ACCRDL   DC    F'0'          # SAMPLES INDICATING DELAY DUE TO RESERVE
ACCSEGL  EQU   *-ACCSEGM     LENGTH OF DEV SEGMENT
         SPACE
         ORG   ACCSEGM
         DC    (200*ACCSEGL)X'00' SPACE FOR 200 SEGMENTS
         DC    X'FFFF'       ACCUMULATOR END MARKER
         EJECT
*---------------------------------------------------------------------*
*  DEFINE THE DATA MANAGEMENT CONTROL BLOCKS                          *
*---------------------------------------------------------------------*
         SPACE
MAXREC   EQU   32756         ABSOLUTE MAXIMUM SMF RECORD
SMFBLK   EQU   6233          1/2 TRACK 3330, 1/3 TRACK 3350
         SPACE
         PRINT NOGEN
SYSUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=SYSUT1,EODAD=EODADDR,          X
               RECFM=VBS,BFTEK=A,LRECL=MAXREC
         SPACE
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FA,LRECL=133,BLKSIZE=133,BUFNO=1
         SPACE 4
         REGEQU
         SPACE 5
         END
