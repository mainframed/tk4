000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID. YEBBLOCK.
000030 REMARKS.
000040
000050         A PROGRAM TO GENERATE BLOCK CHARACTERS
000060         INCLUDING THE DATE         ----
000070         TO BE USED AT THE FRONT OF LARGE BLOCKS OF PRINTOUT
000080
000090 ENVIRONMENT DIVISION.
000100 CONFIGURATION SECTION.
000110 SOURCE-COMPUTER. IBM-360-H50.
000120 OBJECT-COMPUTER. IBM-360-H50.
000130 SPECIAL-NAMES.
000140     C01 IS TO-NEXT-PAGE.
000150 INPUT-OUTPUT SECTION.
000160 FILE-CONTROL.
000170     SELECT PRINTFILE ASSIGN TO UT-S-SYSPRINT.
000180*
000190 DATA DIVISION.
000200 FILE SECTION.
000210 FD  PRINTFILE
000220     RECORD CONTAINS 132 CHARACTERS
000230     LABEL RECORDS ARE OMITTED
000240     RECORDING MODE IS F.
000250 01  ODD-PRINTFIELD.
000260     03  FILLER          PIC X(3).
000270     03  ODD-PRINTLINE   PIC X(126).
000280     03  FILLER          PIC X(3).
000290 01  EVEN-PRINTFIELD.
000300     03  FILLER          PIC X(10).
000310     03  EVEN-PRINTLINE  PIC X(112).
000320     03  FILLER          PIC X(10).
000330*
000340 WORKING-STORAGE SECTION.
000350 01  PRINTLINE           PIC X(126).
000352 01  FILLER REDEFINES PRINTLINE.
000354     03  BLOCK-CHAR      PIC X(14)
000356         OCCURS 9
000358         INDEXED BY C.
000360*
000370 01  KEYWORD-FIELD.
000380     03  KEYWORD         VALUE SPACES.
000390         05  DAY         PIC 99.
000400         05  FILLER      PIC X.
000410         05  ALPHA-MONTH PIC AAA.
000420         05  FILLER      PIC X.
000430         05  YEAR        PIC 99.
000440 01  KEYWORD-R REDEFINES KEYWORD-FIELD.
000450     03  KEYWORD-BYTE    PIC X       OCCURS 9.
000460*        INDEXED BY K
000470 01  STORAGE-FIELD.
000480     03  WORD-STORAGE.
000490         05  FIRST-WORD  PIC X(9)    VALUE SPACES.
000500         05  SECOND-WORD PIC X(9)    VALUE SPACES.
000510         05  THIRD-WORD  PIC X(9)    VALUE SPACES.
000520         05  FOURTH-WORD PIC X(9)    VALUE SPACES.
000530     03  WORD-TABLE REDEFINES WORD-STORAGE.
000540         05  WORD        OCCURS 4.
000550             07  LETTER  PIC X       OCCURS 9.
000560*
000570     03  LINE-STORAGE.
000580         05  FIRST-LINE  PIC 99      VALUE ZERO.
000590         05  SECOND-LINE PIC 99      VALUE ZERO.
000600         05  THIRD-LINE  PIC 99      VALUE ZERO.
000610         05  FOURTH-LINE PIC 99      VALUE ZERO.
000620     03  LINE-TABLE REDEFINES LINE-STORAGE.
000630         05  LINE-FIELD  OCCURS 4.
000640             07  LINE-BYTE           PIC 9       OCCURS 2.
000650     03  LINE-NUMBERS REDEFINES LINE-STORAGE.
000660         05 LINE-NO      PIC 99      OCCURS 4.
000670 01  FIELD-LENGTHS.
000680     03  FIELD-ONE       PIC 9       COMP-3      VALUE ZERO.
000690     03  FIELD-TWO       PIC 9       COMP-3      VALUE ZERO.
000700     03  FIELD-THREE     PIC 9       COMP-3      VALUE ZERO.
000710     03  FIELD-FOUR      PIC 9       COMP-3      VALUE ZERO.
000720 01  FIELD-TABLE REDEFINES FIELD-LENGTHS.
000730     03  FIELD-LENGTH    PIC 9       COMP-3      OCCURS 4.
000740*        INDEXED BY W
000750         88  EVEN-LENGTH VALUES 2, 4, 6, 8.
000760         88  ODD-LENGTH  VALUES 1, 3, 5, 7, 9.
000770 01  WORD-IND            PIC X       VALUE 'N'.
000780     88  WORD-PROCESSING VALUE 'W'.
000790     88  NORMAL-MODE     VALUE 'N'.
000800 01  LINE-ADVANCE        PIC 99      COMP-3.
000810 01  LIMITS-STORAGE.
000820     03  ONE-WORD-LIMITS.
000830         05  FILLER      PIC 9(6)    VALUE 014925.
000840         05  FILLER      PIC 9(6)    VALUE 000000.
000850         05  FILLER      PIC 9(6)    VALUE 000000.
000860         05  FILLER      PIC 9(6)    VALUE 000000.
000870     03  TWO-WORD-LIMITS.
000880         05  FILLER      PIC 9(6)    VALUE 013313.
000890         05  FILLER      PIC 9(6)    VALUE 174937.
000900         05  FILLER      PIC 9(6)    VALUE 000000.
000910         05  FILLER      PIC 9(6)    VALUE 000000.
000920     03  THREE-WORD-LIMITS.
000930         05  FILLER      PIC 9(6)    VALUE 011707.
000940         05  FILLER      PIC 9(6)    VALUE 173225.
000950         05  FILLER      PIC 9(6)    VALUE 334943.
000960         05  FILLER      PIC 9(6)    VALUE 000000.
000970     03  FOUR-WORD-LIMITS.
000980         05  FILLER      PIC 9(6)    VALUE 010101.
000990         05  FILLER      PIC 9(6)    VALUE 171717.
001000         05  FILLER      PIC 9(6)    VALUE 333333.
001010         05  FILLER      PIC 9(6)    VALUE 494949.
001020 01  LIMITS-TABLE REDEFINES LIMITS-STORAGE.
001030     03  WORD-LIMITS     PIC X(24)   OCCURS 4.
001040*        INDEXED BY W.
001050 01  LINE-LIMITS.
001060     03  LIMITS-FIELD    OCCURS 4.
001070*        INDEXED BY W
001080         05  LOWER-LIMIT PIC 99.
001090         05  UPPER-LIMIT PIC 99.
001100         05  AVERAGE-LIMIT           PIC 99.
001110 01  COMPUTATIONAL-FIELDS            COMP-3.
001120     03  P               PIC 99      VALUE 1.
001130     03  K               PIC 99      VALUE 1.
001140     03  L               PIC 99      VALUE 1.
001150     03  W               PIC 99      VALUE 1.
001160     03  B               PIC 99      VALUE 1.
001170     03  PREVIOUS-LINE-NO            PIC S99     VALUE -15.
001180     03  CURRENT-LINE-NO             PIC S99     VALUE ZERO.
001185     03  NO-OF-WORDS     PIC 99.
001190*
001200 01  BYTE-TALLY          PIC 9(9)    COMP        VALUE 9.
001210 01  BLOCK-LINE-NO       PIC 9(9)    COMP        VALUE 1.
001230*
001240 01  TODAYS-DATE.
001250     03  MONTH           PIC 99.
001260     03  FILLER          PIC X.
001270     03  DAY             PIC 99.
001280     03  FILLER          PIC X.
001290     03  YEAR            PIC 99.
001300 01  MONTH-NAME-FIELD    PIC X(36)   VALUE
001310     'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.
001320 01  EACH-MONTH REDEFINES MONTH-NAME-FIELD.
001330     03  MONTH-NAME      PIC X(3)    OCCURS 12.
001340 01  RET-CODE            PIC S9999   VALUE ZERO  COMP.
001350 01  FIRST-LINE          PIC 99.
001360*
001370 LINKAGE SECTION.
001380*
001390 01  PARM.
001400     03  PARM-LENGTH     PIC 9(4)    COMP.
001410     03  PARM-FIELD.
001420         05  PARM-BYTE   PIC X       OCCURS 19.
001430*
001440 PROCEDURE DIVISION USING PARM.
001450*
001460 STARTUP.
001470     OPEN OUTPUT PRINTFILE.
001480     MOVE ZERO TO RETURN-CODE.
001490     MOVE SPACES TO ODD-PRINTFIELD.
001500     WRITE ODD-PRINTFIELD BEFORE ADVANCING TO-NEXT-PAGE.
001510*
001520 SET-PARM.
001530     MOVE SPACES TO KEYWORD.
001540 READ-PARM.
001550     IF PARM-BYTE (P) = ')' GO TO SECOND-DIGIT.
001560     IF      P > PARM-LENGTH
001580         GO TO INIT-LINE-UP.
001590     IF PARM-BYTE (P) = '(' ADD 1 TO P THEN GO TO OPEN-WORD.
001600     IF PARM-BYTE (P) = ',' GO TO KEYWORD-END.
001610     GO TO MOVE-WORD.
001620 OPEN-WORD.
001630     MOVE 'W' TO WORD-IND.
001640 MOVE-WORD.
001650     MOVE PARM-BYTE (P) TO LETTER (W L).
001660     ADD 1 TO FIELD-LENGTH (W).
001665     IF P = PARM-LENGTH GO TO INIT-LINE-UP.
001668     IF PARM-BYTE (P) = '.' GO TO KEYWORD-END.
001670     ADD 1 TO P.
001680     ADD 1 TO L.
001690     GO TO READ-PARM.
001700 KEYWORD-END.
001710     IF WORD-PROCESSING GO TO SET-LINE-COUNT.
001720     ADD 1 TO P.
001730     ADD 1 TO W.
001740     IF W > 4 GO TO WORD-NO-ERROR.
001750     MOVE 1 TO L.
001760     GO TO READ-PARM.
001770 SET-LINE-COUNT.
001780     ADD 1 TO P.
001790     IF PARM-BYTE (P) NOT = ')' GO TO SET-LINE-COUNT.
001800 SECOND-DIGIT.
001810     SUBTRACT 1 FROM P.
001820     IF      PARM-BYTE (P) NOT NUMERIC
001830         OR  PARM-BYTE (P) = ','
001840         MOVE ZERO TO LINE-BYTE (W 2)
001850         THEN GO TO FIRST-DIGIT      ELSE
001860     MOVE PARM-BYTE (P) TO LINE-BYTE (W 2).
001870     SUBTRACT 1 FROM P.
001880 FIRST-DIGIT.
001890     IF      PARM-BYTE (P) NOT NUMERIC
001900         OR  PARM-BYTE (P) = ','
001910         MOVE ZERO TO LINE-BYTE (W 1)
001920         THEN GO TO RESET-LINE-COUNT
001930         ELSE MOVE PARM-BYTE (P) TO LINE-BYTE (W 1).
001940 RESET-LINE-COUNT.
001950     ADD 1 TO P.
001960     IF PARM-BYTE (P) NOT = ')' GO TO RESET-LINE-COUNT.
001970     MOVE 'N' TO WORD-IND.
001980     ADD 1 TO P.
001990     GO TO READ-PARM.
002000 INIT-LINE-UP.
002010     MOVE WORD-LIMITS (W) TO LINE-LIMITS.
002015     MOVE W TO NO-OF-WORDS.
002020     MOVE 1 TO B, L, W, BLOCK-LINE-NO.
002030 LINE-UP.
002040     MOVE FIELD-LENGTH (W) TO TALLY.
002050     COMPUTE K = (9 - TALLY) / 2 + 1.
002060     MOVE SPACES TO KEYWORD.
002070     IF LETTER (W 1) = '$' GO TO SET-DATE.
002080 SET-WORD.
002090     MOVE LETTER (W L) TO KEYWORD-BYTE (K).
002095     IF KEYWORD-BYTE (K) = '.' GO TO COMPUTE-LINE-ADVANCE.
002100     ADD 1 TO L.
002110     ADD 1 TO K.
002120     IF K < 10 GO TO SET-WORD.
002130     GO TO COMPUTE-LINE-ADVANCE.
002140 SET-DATE.
002150     MOVE CURRENT-DATE TO TODAYS-DATE.
002160     MOVE CORRESPONDING TODAYS-DATE TO KEYWORD.
002170     MOVE MONTH-NAME (MONTH) TO ALPHA-MONTH.
002180     MOVE 1 TO BLOCK-LINE-NO.
002190 COMPUTE-LINE-ADVANCE.
002200     IF      LINE-NO (W) NOT NUMERIC
002210         OR  LINE-NO (W) < LOWER-LIMIT (W)
002220         OR  LINE-NO (W) > UPPER-LIMIT (W)
002230         MOVE AVERAGE-LIMIT (W) TO LINE-NO (W).
002240 ADJUST-LINE-NO.
002250     IF LINE-NO (W) < (PREVIOUS-LINE-NO + 16)
002260         ADD 1 TO LINE-NO (W) THEN GO TO ADJUST-LINE-NO.
002270     MOVE LINE-NO (W) TO PREVIOUS-LINE-NO.
002280     COMPUTE LINE-ADVANCE = LINE-NO (W) - CURRENT-LINE-NO.
002290     MOVE LINE-NO (W) TO CURRENT-LINE-NO.
002300*
002310 WRITE-LINE.
002320     MOVE SPACES TO PRINTLINE.
002330     ENTER ASSEMBLER IEFSD095.
002340*
002350*        'IEFSD095' IS THE SUBROUTINE FOR
002360*        CREATING BLOCK LETTERS 12 X 12
002370*
002380     CALL 'IEFSD095' USING KEYWORD  BLOCK-LINE-NO PRINTLINE
002390         BYTE-TALLY.
002400     IF KEYWORD-BYTE (K) = '.'
002410         AND BLOCK-LINE-NO > 9
002420         SET C TO K
002430         MOVE '    XXXX' TO BLOCK-CHAR (C).
002440     IF  EVEN-LENGTH (W) MOVE PRINTLINE TO EVEN-PRINTLINE.
002450     IF   ODD-LENGTH (W) MOVE PRINTLINE TO  ODD-PRINTLINE.
002460     WRITE ODD-PRINTFIELD AFTER LINE-ADVANCE.
002470     MOVE 1 TO LINE-ADVANCE.
002480     ADD 1 TO BLOCK-LINE-NO, CURRENT-LINE-NO.
002490     IF BLOCK-LINE-NO < 13 GO TO WRITE-LINE.
002500     MOVE ZERO TO RETURN-CODE.
002510 RESET-LINE.
002520     ADD 1 TO W.
002530     IF W > NO-OF-WORDS GO TO END-RUN.
002540     MOVE 1 TO L, BLOCK-LINE-NO.
002550     GO TO LINE-UP.
002560 WORD-NO-ERROR.
002570     DISPLAY  'YEB010I NUMBER OF WORDS TO BE PRINTED EXCEEDS 4'.
002580     MOVE 4 TO RETURN-CODE.
002590     GO TO INIT-LINE-UP.
002600 END-RUN.
002610*
002620     MOVE SPACES TO ODD-PRINTFIELD.
002630     WRITE ODD-PRINTFIELD BEFORE ADVANCING TO-NEXT-PAGE.
002640*
002650 END-OF-PRINT.
002660*
002670     IF RETURN-CODE NOT = ZERO
002680     MOVE RETURN-CODE TO RET-CODE    THEN
002690     DISPLAY 'YEB007I HIGHEST RETURN CODE IS   ' RET-CODE.
002700*
002710     CLOSE PRINTFILE.
002720     STOP RUN.
