         TITLE 'J8SRE -- MOD 07 -- SELECTIVE RECORD EDIT -- ISAM MODULEX
               '
         DCBD  DSORG=(IS),DEVD=(DA)
PRAMDUM  DSECT
*
**  INTER-MODULE PARAMETERS
*
         DS    F
PINDIC   DS    F
PIOREG   DS    F
PRLTH    DS    F
         DS    6F
PTINFO   DS    F
         EJECT
DMJFCB   DSECT
*
**  JFCB FIELDS
*
JFCBDSNM DS    CL44   DATASET NAME
         DS    28C
JFCBMASK DS    CL8    DATA MGMT. MASK
         DS    38C
JFCBVOLS DS    CL30   VOL SERIAL NUMBERS (FIRST 5)
SRE07    CSECT
         INMD
         USING PRAMDUM,6
         LR    6,1
         USING IHADCB,3
         L     3,=A(INPF)
         L     1,PINDIC
         LA    1,1(1)
         CLI   0(1),C'C'               CLOSE ?
         BE    Z08A
         CLI   0(1),C'G'               GET ?
         BE    Z06A
         CLI   0(1),C'O'               OPEN ?
         BE    Z04A
         CLI   0(1),C'R'               REOPEN ?
         BNE   Z10A
*
* REOPEN INPUT (TO START AGAIN)
*
Z02A     EQU   *
         CLOSE (INPF,REREAD)
         OPEN  (INPF,INPUT)
         B     Z10A
*
* OPEN INPUT ISAM DATASET
*
Z04A     OPEN  (INPF,INPUT)
         B     Z10A
*
* GET A RECORD FROM ISAM DATASET
*
Z06A     EQU   *
         GET   INPF                    GET RECORD
         ST    1,PIOREG                STORE ADDR. OF RECORD
         L     9,PRLTH
         MVC   2(2,9),DCBLRECL         STORE LENGTH OF RECORD
         L     1,PINDIC
         MVC   3(1,1),ERRFLAG          RECORD ERROR CONDITION
         MVI   ERRFLAG,X'00'            IF ANY
         B     Z10A
*
* SYNAD ROUTINE
*
INERR    BALR  15,0
         USING *,15
         MVI   ERRFLAG,C'P'
         BR    14
ERRFLAG  DC    X'00'
         DROP  15
         DS    0H
*
* CLOSE INPUT DATASET
*
Z08A     EQU   *
         CLOSE (INPF)
         B     Z10A
*
* END OF INPUT
*
ENDUP    L     1,PINDIC
         MVI   1(1),C'E'
*
* EXIT
*
Z10A     OUTMD
         EJECT
*
* STORAGE & CONSTANTS
*
EXTAB1   DS    0F
         DC    X'87'
         DC    AL3(CBLK1)
CBLK1    DS    44F
*
* ISAM DATASET CONTROL BLOCK
*
INPF     DCB   DDNAME=INPF,MACRF=(GL),DSORG=IS,EODAD=ENDUP,            X
               EXLST=EXTAB1,SYNAD=INERR
         END
