SU25EOF  ENTER 12,11,ENT=EOF,VERS=2.1
         ST    1,PARMPTR           SAVE PARM POINTER
         OPEN  (PRTER,(OUTPUT))
         TM    PRTER+48,X'10'
         BO    *+10
         MVC   RETCODE(4),=F'4'
         LA    0,HEADER
         BAL   14,PUTLINE          GO WRITE HEADER
         L     1,PARMPTR
         L     1,0(,1)             GET PARM FIELD POINTER
         LH    2,0(1)
         LTR   2,2                 ANY PARM FIELD?
         BZ    NOPARM              NO
         BCTR  2,0                 REDUCE FOR MOVE
         EX    2,MOVEPARM          MOVE PARM FIELD TO MESSAGE
         LA    3,19(2)             TOTAL LENGTH OF MESSAGE
         STCM  3,3,PARMMSG          STORE LENGTH IN MESSAGE HEADER
         LA    0,PARMMSG
         BAL   14,PUTLINE
         LA    1,PARMMSG+18        POINT AT PARM FIELD
PARMSCAN CLC   0(5,1),=C'EOFDD'
         BE    GOTEOFDD
         CLC   0(4,1),=C'SNAP'
         BNE   INVPARM             GO WRITE INVALID PARM MSG
         OI    FLAGS,X'10'         SET SNAP FLAG ON
         LA    1,4(1)              PAST PARM
         BC    15,UPPARM           ROUND AGAIN
GOTEOFDD OI    FLAGS,X'20'         SET EOFDD FLAG ON
         LA    1,5(1)              PAST PARM
UPPARM   CLI   0(1),X'40'          SPACE?
         BE    PARMEND             YES - FINISHED
         CLI   0(1),C','           COMMA?
         BNE   INVDEL              NO - WRITE ERROR MSG
         LA    1,1(1)              PAST DELIMITER
         B     PARMSCAN            ROUND AGAIN
PARMEND  TM    FLAGS,X'10'         SNAP SPECIFIED?
         BZ    NOPARM              NO - DO NOT OPEN IT
         OPEN  (SNAPDCB,(OUTPUT))
         TM    SNAPDCB+48,X'10'    OPEN OK?
         BZ    NOSNAPS             NO - WRITE MESSAGE
NOPARM   EQU   *
         TIOT
         LR    10,1                GET TIOT ADDRESS
         LA    10,24(10)           UP TO FIRST ENTRY
TIOTLOOP CLI   0(10),X'00'         END OF TIOT?
         BE    ALLDONE             YES - FINISHED
         MVC   DDNAME(8),4(10)     MOVE DDNAME TO MESSAGE
         LA    0,DDMSG
         BAL   14,PUTLINE          GO WRITE MESSAGE
         CLI   4(10),X'40'         START WITH BLANK?
         BE    CONCAT              YES - MUST BE CONCATENATION
         CLC   4(3,10),=C'SYS'     START WITH 'SYS'
         BE    DONTDO              YES - DO NOT HANDLE
         CLC   4(8,10),=CL8'STEPLIB'
         BE    DONTDO
         CLC   4(8,10),=CL8'JOBLIB'
         BE    DONTDO
         CLC   4(8,10),=CL8'PGM=*.DD'
         BE    DONTDO
         CLC   4(8,10),=CL8'STEPCAT'
         BE    DONTDO
         CLC   4(8,10),=CL8'JOBCAT'
         BE    DONTDO
         TM    FLAGS,X'20'         EOFDD CHECK?
         BZ    NOEOFCK             NO
         CLC   4(3,10),=C'EOF'     START WITH EOF?
         BNE   DONTDO              NO - DO NOT DO IT
NOEOFCK  NI    FLAGS,X'BF'         TURN OFF ERROR BIT
         CLI   0(10),X'14'         LENGTH OF TIOT ENTRY OK?
         BNE   INVLEN              NO - ERROR MESSAGE
         L     9,16(10)            GET UCB ADDRESS
         LA    9,0(9)
         LTR   9,9                 ANY THERE?
         BZ    INVLEN              NO - TIOT ERROR
         DEVTYPE DDNAME,DEVTYPE,DEVTAB,RPS
         SLL   15,24
         SRL   15,24
         LTR   15,15               RETURN CODE OK?
         BNZ   DTYPERR             NO - WRITE MESSAGE
         MVC   DCB(52),DUMMYDCB    MOVE DUMMY DCB
         MVC   DCB+40(8),4(10)     MOVE DDNAME TO DCB
         CLI   UCBTYP+2,X'80'      MAG TAPE?
         BE    MAGTAPE             YES - GO
         CLI   UCBTYP+2,X'20'      DASD?
         BNE   INVDEV              NO - INVALID DEVICE TYPE
         RDJFCB (DCB)
         OI    JFCB+52,X'08'       DO NOT WRITE
         NI    JFCB+76,X'7F'             JFCB BACK
         MVC   VOLSERNO(6),28(9)   MOVE VOLSERNO FROM UCB
         OBTAIN BYNAME             READ DSCB FOR DATASET
         SLL   15,24
         SRL   15,24
         LTR   15,15               RETURN CODE OK?
         BNZ   INVOBT              NO - GO WRITE MESSAGE
         TM    DSCB1+38,X'A0'      ISAM OR DA?
         BNZ   INVORG              YES - INVALID
         TM    DSCB1+39,X'08'      VSAM FILE?
         BO    INVORG              YES INVALID
         TM    DSCB1+38,X'02'      PARTITIONED?
         BNO   SEQFIL              NO - DO SEQUENTIAL
         MVI   DCB+26,X'02'        MAKE DCB PO DSORG
         OPEN  (DCB,(OUTPUT)),TYPE=J
         TM    DCB+48,X'10'        OPEN OK?
         BZ    OPENNOT             NO - ISSUE MESSAGE
         L     7,DCB+44            LOAD DEB ADDRESS
         MVC   IOB(40),DUMMYIOB    MOVE DUMMY IOB
         MVC   SEARCHID(4),38(7)   MOVE CCHH START FROM DEB TO IOB
         MVC   BYNAME+4(4),=A(FMT4NAME)   POINT TO FORMAT 4 DSNAME
         OBTAIN BYNAME
         MVC   BYNAME+4(4),=A(JFCB)  RESTORE OBTAIN PARAMETER LIST
POLOOP1  EXCP  IOB
         WAIT  1,ECB=IOECB
         MVC   DCB+5(8),IOB+32     MOVE SEEK ADDRESS TO DCB
         CLI   IOECB,X'7F'         I/O COMPLETED OK?
         BNE   IOERROR             NO
POLOOP2  MVC   IOB+17(3),=AL3(RDCOUNT)  MOVE CCW ADDRESS TO IOB
         XC    IOECB,IOECB         CLEAR ECB
         EXCP  IOB
         WAIT  1,ECB=IOECB
         CLI   IOECB,X'7F'         I/O COMPLETED OK?
         BNE   POIOCHK             NO - CHECK CODE
         CLC   COUNT+6(2),=X'0000' EOF RECORD READ?
         BE    EOFPO               YES - OK
         CLC   COUNT+5(3),=X'080100'  VALID KEY AND DATA LENGTHS?
         BNE   INVDIR              NO - ISSUE MESSAGE
         CLI   IOB+39,X'00'        READING RECORD 0?
         BE    R0READ              YES
         CLI   COUNT+4,X'01'       JUST REWAD RECORD 1?
         BE    UPTRK               UPDATE TRACK NUMBER
R0READ   MVC   SEARCHID(5),COUNT   MOVE CCHHR FROM COUNT FIELD TO IOB
         MVC   IOB+17(3),=AL3(KEYDATA)  MOVE CCW ADDRESS
         B     POLOOP1             ROUND AGAIN
POIOCHK  TM    IOB+12,X'02'        UNIT CHECK?
         BZ    IOERROR             NO MUST BE AN ERROR
         TM    IOB+3,X'08'         NO RECORD FOUND?
         BZ    IOERROR             NO MUST BE AN ERROR
         CLC   IOB+39(1),DSCB1+31  FINISHED A TRACK?
         BL    IOERROR             NO - IT'S AN ERROR
UPTRK    SR    1,1
         ICM   1,3,SEARCHID+2      PICK UP HEAD NUMBER
         LA    1,1(1)              ADD ONE
         CH    1,TRKCYL            END OF CYLINDER?
         BNL   UPCYL               YES - UPDATE IT
         SLL   1,8                 SHIFT TO GIVE RECORD 0
         STCM  1,7,SEARCHID+2      STORE IN SEEK ADDRESS
         NI    DCB+44,X'3F'        TURN OFF DCB ERROR FLAGS
         B     POLOOP2             ROUND AGAIN
UPCYL    SR    1,1
         ICM   1,3,SEARCHID        PICK UP CYL NUMBER
         LA    1,1(1)              ADD ONE
         SLL   1,16                SHIFT TO GIVE HEAD ZERO
         STCM  1,15,SEARCHID       STORE IN SEEK ADDRESS
         MVI   IOB+39,X'00'        RECORD ZERO NEXT
         NI    DCB+44,X'3F'        TURN OFF DCB ERROR FLAGS
         B     POLOOP2             ROUND AGAIN
EOFPO    TM    FLAGS,X'80'         ANY ERRORS?
         BO    *+10                YES
         MVC   DCB+8(5),COUNT      MOVE CCHHR TO DCB
         SR    6,6
         IC    6,DCB+12            PICK UP RECORD NUMBER
         LA    5,264               KEY PLUS DATA LENGTH
         TM    OHFLAG,X'01'        ANY TOLERANCE FACTOR?
         BZ    NOTOL               NO
         LH    4,TOLFACT           PICK UP TOLERANCE FACTOR
         MR    4,4                 MULTIPLY BLK LEN BY TOLERANCE
         SRDL  4,9                 SHIFT INTO REG 5
NOTOL    SR    4,4
         IC    4,OHEAD1            GET BLOCK OVERHEAD
         AR    5,4                 ADD TO ADJUSTED LENGTH
         MR    4,6                 TIMES RECORDS / TRACK
         LH    4,TRKLEN            GET TOTAL TRACK LENGTH
         SR    4,5                 LESS AMOUNT WRITTEN
         STH   4,DCB+18            STORE TRACK BALANCE IN DCB
         B     CLOSE               GO CLOSE THE FILE
SEQFIL   OPEN  (DCB,(OUTPUT)),TYPE=J
         TM    DCB+48,X'10'        OPEN OK?
         BZ    OPENNOT             NO, ISSUE MESSAGE
         L     7,DCB+44            GET DEB ADDRESS
         XC    DCB+5(8),DCB+5      CLEAR MBBCCHHR
         MVC   DCB+6(6),36(7)      MOVE BBCCHH OF D/S START FROM DEB
         MVC   DCB+18(2),TRKLEN    MOVE TRK LEN TO DCB (MFT COMPAT)
         B     CLOSE               GO CLOSE THE DATASET
MAGTAPE  TM    UCBTYP+1,X'C4'      7 TRACK TAPE?
         BNZ   INVDEV              YES - CANT DO
         OPEN  (DCB,(OUTPUT))
         TM    DCB+48,X'10'        OPEN OK?
         BZ    OPENNOT             NO, ISSUE MESSAGE
CLOSE    CLOSE (DCB)
         LA    0,COMPMSG           POINT TO COMPLETE MESSAGE
         TM    FLAGS,X'80'         ANY ERRORS?
         BZ    *+8                 NO, OK
         LA    0,TERMMSG           POINT TO TERMINATED MESSAGE
         BAL   14,PUTLINE          GO WRITE MESSAGE
         NI    FLAGS,X'7F'         TURN OFF ERROR FLAG
UPTIOT   SR    3,3
         IC    3,0(10)             GET LENGTH OF TIOT ENTRY
         AR    10,3                UP TO NEXT ENTRY
         B     TIOTLOOP            ROUND FOR NEXT DD CARD
INVLEN   LA    0,TIOTMSG           POINT TO TIOT ERROR MESSAGE
         BAL   14,PUTLINE          GO PUT IT OUT
         BAL   1,SNAPTST           CHECK FOR SNAP DUMP
         MVC   RETCODE(4),=F'8'    SET RETURN CODE
         B     UPTIOT              TRY AGAIN
DONTDO   OI    FLAGS,X'40'         TURN FLAG ON
         LA    0,NOTMSG            POINT TO NOT OURS MESSAGE
         BAL   14,PUTLINE          GO WRITE IT
         B     UPTIOT              TRY AGAIN
DTYPERR  CVD   15,WD               CONVERT RETURN CODE FROM DEVTYPE
         UNPK  WD(4),WD+6(3)       MAKE IT PRINTABLE
         MVC   DTYPRC(2),WD+1      MOVE TO MESSAGE
         LA    0,DTYPMSG           POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         BAL   1,SNAPTST           CHECK FOR SNAP
         MVC   RETCODE(4),=F'8'
         B     UPTIOT              ROUND AGAIN
INVDEV   LA    0,DEVMSG            POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         BAL   1,SNAPTST           CHECK FOR SNAP
         MVC   RETCODE(4),=F'8'
         B     UPTIOT
INVOBT   CVD   15,WD               GET RC
         UNPK  WD(4),WD+6(3)          IN PRINTABLE
         MVC   OBTRC(2),WD+1              FORM INTO MESSAGE
         LA    0,OBTMSG            POINT TO MSG
         BAL   14,PUTLINE          GO PRINT IT
         BAL   1,SNAPTST           CHECK FOR SNAP
         MVC   RETCODE(4),=F'8'
         B     UPTIOT
INVORG   LA    0,ORGMSG            POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         BAL   1,SNAPTST           CHECK FOR SNAP
         MVC   RETCODE(4),=F'8'
         B     UPTIOT
OPENNOT  LA    0,UNOPENMS          POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         BAL   1,SNAPTST           CHECK FOR SNAP
         MVC   RETCODE(4),=F'8'
         B     UPTIOT              ROUND AGAIN
IOERROR  SYNADAF ACSMETH=EXCP,PARM1=IOB
         MVC   8(41,1),SYNMSG      MOVE MESSAGE TO BUFFER
         LA    0,4(1)              POINT TO LENGTH FIELD
         PUT   PRTER,(0)           WRITE SYNAD MESSAGE
         BAL   1,SNAPTST           CHECK FOR SNAP
         SYNADRLS
         MVC   RETCODE(4),=F'8'
         NI    DCB+44,X'3F'        TURN OFF DCB ERROR FLAGS
         OI    FLAGS,X'80'         SET ERROR FLAG
         B     EOFPO               FINISH WITH THIS DATASET
CONCAT   TM    FLAGS,X'40'         DOING THIS ONE ANYWAY?
         BO    DONTDO+4            NO - OK ISSUE MESSAGE
         LA    0,CATMSG            YES - TSK, TSK
         BAL   14,PUTLINE          GO PRINT MESSAGE
         BAL   1,SNAPTST           CHECK FOR SNAP
         MVC   RETCODE(4),=F'8'
         B     UPTIOT              ROUND AGAIN
INVDIR   LA    0,DIRMSG            POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         BAL   1,SNAPTST           CHECK FOR SNAP
         MVC   RETCODE(4),=F'8'
         OI    FLAGS,X'80'         SET ERROR FLAG
         B     EOFPO               FINISH THIS DATASET
NOSNAPS  LA    0,SNAPMSG           POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         MVC   RETCODE(4),=F'4'
         B     NOPARM              GO BACK
INVPARM  LA    0,PARMERR           POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         MVC   RETCODE(4),=F'16'
         B     ALLDONE             RETURN
INVDEL   LA    0,DLIMMSG           POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         MVC   RETCODE(4),=F'16'
         B     ALLDONE             RETURN
SNAPTST  TM    SNAPDCB+48,X'10'    IS SNAP DCB OPEN
         BZR   1                   YES - OK
         STM   0,15,SNAPSAVE       SAVE ALL REGS
         SR    2,2
         IC    2,SNAPID            GET SNAP ID
         SNAP  DCB=SNAPDCB,ID=(2),SDATA=(CB),PDATA=(PSW,REGS,SA,JPA)
         CVD   2,WD               CONVERT SNAP ID
         UNPK  WORK(5),WD+6(3)          TO PRINT ON
         MVC   SNID(3),WORK                  MESSAGE DATASET
         LA    0,SNIDMSG           POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         LA    2,1(2)              INCREMENT ID
         STC   2,SNAPID            SAVE FOR NEXT TIME
         LM    0,15,SNAPSAVE       RESTORE REGS
         BR    1                   RETURN
PUTLINE  TM    PRTER+48,X'10'      SYSPRINT OPEN?
         BZR   14                  NO - RETURN
         LR    5,14                SAVE RETURN REG
         PUT   PRTER,(0)           WRITE MESSAGE
         BR    5                   RETURN
ALLDONE  L     1,RETCODE           GET LAST RETURN CODE
         CVD   1,WD                CONVERT IT TO
         UNPK  WD(4),WD+6(3)            PRINTABLE
         MVC   RC(2),WD                    CHARS AND PUT IN MESSAGE
         LA    0,ENDMSG            POINT TO MESSAGE
         BAL   14,PUTLINE          GO PRINT IT
         TM    PRTER+48,X'10'      IS PRINTER OPEN?
         BZ    CLSNAP              NO - CLOSE SNAP D/S
         CLOSE (PRTER)
CLSNAP   TM    SNAPDCB+48,X'10'    IS SYSSNAP OPEN?
         BZ    RET                 NO - ENDED
         CLOSE (SNAPDCB)
RET      EQU   *
         L     15,RETCODE          GET RETURN CODE
         LEAVE (15)                RETURN
         SPACE 5
MOVEPARM MVC   PARMMSG+18(1),2(1)  MOVE PARM FIELD (EXECUTED)
         SPACE 5
         LTORG
         EJECT
         PRINT NOGEN
DUMMYDCB DCB   DDNAME=XXXXXXXX,DSORG=PS,MACRF=E,EXLST=JFCBA
PRTER    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=VBA,LRECL=137,  X
               BLKSIZE=1012
SNAPDCB  DCB   DDNAME=SYSSNAP,DSORG=PS,MACRF=W,RECFM=VBA,LRECL=125,    X
               BLKSIZE=882
         PRINT GEN
         DS    0F
BYNAME   CAMLST SEARCH,JFCB,VOLSERNO,DSCB1
         DS    0F
DUMMYIOB DC    X'427C0000'
         DC    A(IOECB)
         DS    2F
         DC    A(WRKDATA)
         DC    A(DCB)
         DS    3F
         DC    F'1'                START AT RECORD ONE
DIREND   DC    8X'FF'              KEY OF BLOCK
         DC    H'14'               LENGTH OF DATA
         DC    8X'FF'              EOF MARKER
ZEROES   DC    XL132'00'           REST OF DIR BLOCK + ZERO BLOCK
         DC    XL132'00'
         DS    0F
DCB      DS    XL52                WORKING DCB (MOVED FROM DUMMYDCB)
         DS    0F
IOECB    DC    F'0'
IOB      DS    XL40
IOBSEEK  EQU   IOB+32
SEARCHID EQU   IOB+35
JFCBA    DC    X'87',AL3(JFCB)
JFCB     DS    44F
PARMPTR  DS    F
         DS    0D
DSCB1    DS    XL148
VOLSERNO DS    CL6
FMT4NAME DC    44X'04'
FLAGS    DC    X'00'
DEVTYPE  DS    0F
UCBTYP   DS    F
MAXBLK   DS    F
TOTCYLS  DS    H
TRKCYL   DS    H
TRKLEN   DS    H
OHEAD1   DS    X
OHEAD2   DS    X
OHEAD3   DS    X
OHFLAG   DS    X
TOLFACT  DS    H
SECTORS  DS    F
         DS    0D
WD       DS    D
WORK     DS    D
RETCODE  DC    F'0'
SNAPID   DC    X'01'
         DS    0F
COUNT    DS    XL8
SNAPSAVE DS    16F
         SPACE 10
         DS    0D
WRKDATA  CCW   X'31',SEARCHID,X'40',5
         TIC   *-8
         CCW   X'0D',DIREND,X'00',264
         SPACE 5
RDCOUNT  CCW   X'31',SEARCHID,X'40',5
         TIC   *-8
         CCW   X'12',COUNT,X'00',8
         SPACE 5
KEYDATA  CCW   X'31',SEARCHID,X'40',5
         TIC   *-8
         CCW   X'0D',ZEROES,X'00',264
         EJECT
HEADER   DC    AL2(HEND-*),AL2(0)
         DC    C'1        EOF --- DATASET CLEARING PROGRAM'
HEND     EQU   *
         SPACE 5
ENDMSG   DC    AL2(EEND-*),AL2(0)
         DC    C'-EOF - EXECUTION COMPLETE - RETURN CODE IS '
RC       DC    C'  '
EEND     EQU   *
         SPACE 5
DDMSG    DC    AL2(DEND-*),AL2(0)
         DC    C'-DDNAME '
DDNAME   DC    CL8' '
DEND     EQU   *
         SPACE 5
COMPMSG  DC    AL2(CEND-*),AL2(0)
         DC    C'     PROCESSING COMPLETE'
CEND     EQU   *
         SPACE 5
TIOTMSG  DC    AL2(TEND-*),AL2(0)
         DC    C'     NON-STANDARD TIOT ENTRY'
TEND     EQU   *
         SPACE 5
NOTMSG   DC    AL2(NEND-*),AL2(0)
         DC    C'     EOF DOES NOT PROCESS THIS DDNAME'
NEND     EQU   *
         SPACE 5
DTYPMSG  DC    AL2(DTEND-*),AL2(0)
         DC    C'     DEVTYPE PRODUCED RETURN CODE '
DTYPRC   DC    C'  '
DTEND    EQU   *
         SPACE 5
DEVMSG   DC    AL2(DVEND-*),AL2(0)
         DC    C'     INVALID DEVICE TYPE - ONLY DASD AND 9 TRACK TAPE X
               ALLOWED'
DVEND    EQU   *
         SPACE 5
OBTMSG   DC    AL2(OEND-*),AL2(0)
         DC    C'     OBTAIN PRODUCED RETURN CODE '
OBTRC    DC    C'  '
OEND     EQU   *
         SPACE 5
ORGMSG   DC    AL2(OREND-*),AL2(0)
         DC    C'     INVALID DSORG - ONLY SEQUENTIAL AND PARTITIONED AX
               LLOWED'
OREND    EQU   *
         SPACE 5
UNOPENMS DC    AL2(UEND-*),AL2(0)
         DC    C'     OPEN FAILED'
UEND     EQU   *
         SPACE 5
CATMSG   DC    AL2(CTEND-*),AL2(0)
         DC    C'     CONCATENATED DATASETS ARE NOT PROCESSED'
CTEND    EQU   *
         SPACE 5
DIRMSG   DC    AL2(DIEND-*),AL2(0)
         DC    C'     INVALID BLOCK FOUND IN DIRECTORY'
DIEND    EQU   *
         SPACE 5
TERMMSG  DC    AL2(TMEND-*),AL2(0)
         DC    C'     PROCESSING TERMINATED'
TMEND    EQU   *
         SPACE 5
SNAPMSG  DC    AL2(SEND-*),AL2(0)
         DC    C'-OPEN FAILED FOR SYSSNAP - NO SNAP DUMPS WILL BE PROVIX
               DED'
SEND     EQU   *
         SPACE 5
SNIDMSG  DC    AL2(SIEND-*),AL2(0)
         DC    C'     SNAP DUMP TAKEN - ID '
SNID     DC    C'   '
SIEND    EQU   *
         SPACE 5
PARMERR  DC    AL2(PEND-*),AL2(0)
         DC    C'-INVALID OPTION IN PARM FIELD'
PEND     EQU   *
         SPACE 5
DLIMMSG  DC    AL2(DLEND-*),AL2(0)
         DC    C'-INVALID DELIMITER IN PARM FIELD'
DLEND    EQU   *
         SPACE 5
PARMMSG  DC    AL2(PMEND-*),AL2(0)
         DC    C'-PARM FIELD : '
         DC    CL110' '            SPACE FOR PARM FIELD
PMEND    EQU   *
         SPACE 5
SYNMSG   DC    C'     I/O ERROR WHILE PROCESSING DIRECTORY '
         SPACE 5
         END
