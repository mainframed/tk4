         TITLE 'HASPUNDP - REMOVE DUPLICATE HASP MODIFICATIONS'
UNDUPE   CSECT
*
         REQU
*
         SAVE  (14,12)
         BALR  R2,0
         USING *,R2
         LA    R12,SAVER
         ST    R13,SAVER+4             STANDARD
         ST    R12,8(R13)                  LINKAGE
         LR    R13,R12                         CONVENTIONS
*
         OPEN  (INFILE,(INPUT),OUTFILE,(OUTPUT),MESSAGE,(OUTPUT))
*
         GET   INFILE,OLDCARD          GET 1ST CARD
*
         PUT   MESSAGE,HEADER          OUTPUT HEADING
         PUT   MESSAGE,BLANKS          LEAVE A BLANK LINE
*
READ     GET   INFILE,NEWCARD          GET NEXT CARD
         CLC   NEWCARD+72(8),OLDCARD+72  DIFFERENT SEQUENCE NUMBER?
         BH    WRITE                   YES...OUTPUT THE PREVIOUS CARD
         CLC   NEWCARD+67(4),OLDCARD+67  IF NEW CARD IS OF A LOWER
         MVC   BLANKS+10(80),NEWCARD        MOD LEVEL THEN REJECT
         BNH   REJECT                       IT AND PRINT ON MSG DATA
         MVC   BLANKS+10(80),OLDCARD        SET. OTHERWISE SAVE THE
*                                           NEW CARD AND REJECT AND
         MVC   OLDCARD,NEWCARD              PRINT OUT THE OLD
*                                           CARD
REJECT   PUT   MESSAGE,BLANKS          PRINT REJECTED CARD
         B     READ                    GO GET ANOTHER CARD
WRITE    PUT   OUTFILE,OLDCARD         OUTPUT A VALID UPDATE CARD
         MVC   OLDCARD,NEWCARD         SAVE NEW CARD FOR VERIFICATION
         B     READ                    GO GET ANOTHER CARD
*
EOF      PUT   OUTFILE,OLDCARD         OUTPUT FINAL CARD
         CLOSE (INFILE,,OUTFILE,,MESSAGE)
*
         L     R13,SAVER+4             RESTORE R13
         SR    R15,R15                 RETURN CODE = 0
         RETURN (14,12),RC=(15)
         EJECT
SAVER    DC    18F'0'                  STANDARD O/S SAVE AREA
*
OLDCARD  DC    CL80' '
NEWCARD  DC    CL80' '
HEADER   DS    0CL133
         DC    CL40'1',C'THE FOLLOWING CARDS HAVE BEEN REJECTED AS DUPL*
               ICATES',CL41' '
BLANKS   DC    CL133' '
*
INFILE   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GM,EODAD=EOF
         SPACE 2
OUTFILE  DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,      *
               BLKSIZE=5600
         SPACE 2
MESSAGE  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FA,LRECL=133,   *
               BLKSIZE=133
         SPACE 2
         END
