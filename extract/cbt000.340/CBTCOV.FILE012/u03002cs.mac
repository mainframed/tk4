*          DATA SET SOF154YYYY AT LEVEL 006 AS OF 23/01/85
         TITLE '* * * * * *    DID FUNCTION FOR VS1/VS2  * * * * * *'
***********************************************************************
**********               DID LOAD MODULE LINKED             ***********
**********                TO FOR DID FUNCTION               ***********
***********************************************************************
**********   AMENDED -                                      ***********
**********           JAN 1985, BY JOHN MCGHEE               ***********
**********           FROM PANVALET LEVEL 2.                 ***********
**********           SPLEVEL MACRO ADDED.                   ***********
**********           UCB SEARCH CHANGED TO USE IOSUCBSC     ***********
**********           ROUTINE FOR XA COMPATIBILITY,          ***********
***********************************************************************
         SPACE 2
         ISEQ  75,80
DITTODID START
         SPLEVEL SET=1             ENSURE 370 STYLE MACROS
         SAVE  (14,12)
         LR    R2,RD
         BALR  RB,R0
         USING *,RB
         B     AROUND              BR BY COPYRIGHT
COPYRT   DC    C'5798-ARD COPYRIGHT IBM CORPORATION 1973'
         DC    C'12-25-72 OS/DITTO-000'
AROUND   EQU   *
         LA    R3,SAVEAREA
         ST    R3,8(R2)            PTR TO MY SA
         ST    R2,4(R3)            PTR TO PREVIOUS SA
         LR    RD,R3               POINT REG 13 TO SAVAREA
*                                  - REQUIRED BY IOSUCBSC, AND
*                                    SEEMS LIKE A GOOD IDEA ANYWAY.
FRSTWTOR EQU   *
         MVI   CUUECB,X'00'
         WTOR  'REPLY: CUUXXXXXX - TO SPECIFY UNIT AND VOLSER, OR END -X
                TO END DID',UCBNAME,9,CUUECB,DESC=6,ROUTCDE=2
         WAIT ECB=CUUECB
         OC    UCBNAME(9),=C'         '    FORCE CAPS ON REPLY
         CLC   UCBNAME,=C'END'
         BE    RETURN
UCBFIND  EQU   *
         L     R9,CVTPTR           GET CVT POINTER
         USING CVT,R9              MAP UCB
NEEDUCB  EQU   *
         XC    UCBWORKA,UCBWORKA   ZERO IOSUCBSC WORK AREA
STARTEST EQU   *
         LA    R1,UCBPARML         POINT TO IOSUCBSC PARM LIST
         L     RF,CVTUCBSC         PICK UP SCAN ROUTINE'S ADDRESS
         BALR  RE,RF               SCAN FOR NEXT UCB (LIMITED TO DASD)
         LTR   RF,RF               NON ZERO R15 MEANS NO MORE UCBS
         BNZ   UCBEND
         L     R1,UCBADDR          ADDR OF UCB
         CLC   UCBNAME,13(R1)      DESIRED UCB ?
         BNE   STARTEST            NO, GO LOOK FOR MORE ENTRIES
         DROP  R9                  UNMAP CVT/RELEASE R9
         LR    R9,R1               ADDR OF UCB
         B     LABELIT
UCBEND   EQU   *
         MVC   WTO1+8(3),UCBNAME
WTO1     WTO   'CUU NOT DASD OR NOT IN SYSTEM',DESC=6,ROUTCDE=2
         B     FRSTWTOR
         EJECT
***********************************************************************
*********  LABEL ROUTINE: READ VOL LABEL, ENTER NEW SER,    ***********
*********  WRITE OUT NEW LABEL ON VOLUME, POST UCB.         ***********
***********************************************************************
         SPACE 1
LABELIT  EQU   *
************************  OFFLINE CHECKING BEFORE LABELING  ***********
         TM    3(R9),X'80'         ONLINE ?
         BO    ONLINERR
         TM    6(R9),X'40'         NOT READY ?
         BO    NOTRDY
         LA    R1,RDAREA           ADDR OF AREA FOR LIST
         ST    R9,0(R1)            ST UCB PTR IN LIST
         MVI   0(R1),X'8F'         BUILD DEB INDICATOR
         L     R8,RDDCBAD          PTR TO DCB
         ST    R8,4(R1)            ST DCB PTR IN LIST
         MVI   4(R1),X'88'         SET LAST ENTRY INDICATOR
         SVC   82                  CONSTRUCT DEB
         LTR   RF,RF               DEB SUCCESSFULLY BUILT ?
         BNZ   ONLINERR            NO, ERROR MSG
READ     EQU   *
         LA    R5,RDAREA
         ST    R5,RDWRTCCW         PRIME CCW
         MVI   RDWRTCCW,6          READ OP CODE
         MVI   RDWRTCCW+4,X'20'
         EXCP  RDIOB               READ VOL LABEL
         BAL   R4,WAITD
         MVC   OLDSER(6),RDAREA+4      SAVE OLD SER
NOSET    MVC   RDAREA+4(6),VOLSER  MOVE IN NEW SER
WRITEOUT MVI   RDWRTCCW,5          WRT OPCODE
         MVI   RDWRTCCW+4,X'60'
         EXCP  RDIOB               WRITE VOL LABEL
         BAL   R4,WAITD
OFFLNPS  EQU   *                   OFFLINE POST OF UCB
         LA    R1,RDAREA           AREA FOR LIST
         LA    R2,VOLSER           PTR TO NEW VOLSER
         ST    R9,0(R1)            UCB PTR TO LIST
         MVI   0(R1),X'88'         SET SPECIAL POST INDIC.
         ST    R2,4(R1)            PTR TO NEW VOLSER
         SR    R2,R2               CLEAR
         ST    R2,8(R1)            ZERO VTOC PTR
         MVC   12(4,R1),RDDCB+44   DEB PTR TO LIST
         MVI   12(R1),X'80'        SET LAST ENTRY
         SVC   82                  POST UCB WITH NEW VOLSER
         MVC   WTO2+16(6),OLDSER       MOVE OLD SER
         MVC   WTO2+32(6),VOLSER       MOVE NEW SER
WTO2     WTO   'OLD SER=XXXXXX, NEW SER=XXXXXX - DID COMPLETE: ISSUE MOX
               UNT, VARY ONLINE',DESC=6,ROUTCDE=2
         B     FRSTWTOR
         EJECT
***********************************************************************
**********         ERROR MESSAGES AND RETURN ROUTINE.       ***********
***********************************************************************
         SPACE 1
NOTRDY   EQU   *
         MVC   NOTRDY1+8(3),UCBNAME
NOTRDY1  WTO   'CUU NOT READY, CYCLE UP (OFFLINE) BEFORE DID',         X
               DESC=6,ROUTCDE=2
         B     FRSTWTOR
ONLINERR EQU   *
         MVC   ONLINER1+8(3),UCBNAME
ONLINER1 WTO   'CUU ONLINE, VARY OFFLINE BEFORE DID',DESC=6,ROUTCDE=2
         B     FRSTWTOR
IOERR    EQU   *
         WTO   'DISK I/O ERROR',DESC=6,ROUTCDE=2
RETURN   EQU   *
         LA    RD,SAVEAREA         PRIME RD BEFORE RETURN
         L     RD,4(RD)
         RETURN (14,12),RC=0
         EJECT
***********************************************************************
**********            WAIT ROUTINE FOR DISK I/O             ***********
***********************************************************************
         SPACE 1
WAITD    TM    DISKECB,X'40'       COMPL ?
         BO    TESTD
         WAIT  ECB=DISKECB
TESTD    CLI   DISKECB,X'7F'       OK ?
         BNE   IOERR               NO
         MVI   DISKECB,0
         BR    R4                  YES, RETURN
         EJECT
***********************************************************************
********* DCB, CCW'S, STORAGE, ECB'S, IOB, CONSTANTS, EQU'S ***********
***********************************************************************
         SPACE 1
RDDCB    DCB   DSORG=PS,MACRF=(E),DEVD=DA,EOEA=P8,SIOA=P7,PGFX=YES
         DS    0D                  ALLIGN
CCWLIST  CCW   X'31',RDSEEK+3,X'60',5   SRCH FOR REC 3
         CCW   8,CCWLIST,0,0           REPEAT UNTIL FOUND
RDWRTCCW CCW   0,0,X'20',80            R/W DATA
CCWLIST1 CCW   X'31',RDSEEK+3,X'60',5  SRCH FOR REC 3
         CCW   X'08',CCWLIST1,0,0      REPEAT UNITL DONE
         CCW   X'06',0,X'30',80        READ CHECK
         EJECT
UCBWORKA DS    0D,CL100            D/WORD ALIGNED WORKAREA FOR IOSUCBSC
RDAREA   DS    CL80
DISKECB  DC    F'0'                WAIT/COMPLETE BITS
RDIOB    DS    0F
RDFLAGS  DC    XL2'2000'
RDSENSE  DC    H'0'
RDECBAD  DC    A(DISKECB)
RDSTATUS DC    2F'0'
RDCCWAD  DC    A(CCWLIST)
RDDCBAD  DC    A(RDDCB)
RDRESAD  DC    F'0'
RDBLKCT  DC    H'1'
RDERROR  DC    H'0'
RDSEEK   DC    1F'0'               MBBC
         DC    X'00000003'         CHHR
         DS    0F
CUUECB   DC    F'0'
SAVEAREA DS    18F
UCBPARML DS    0F                  3 WORD IOSUCBSC PARAMETER LIST
         DC    A(UCBWORKA)
         DC    A(DASDCLAS)
         DC    X'80',AL3(UCBADDR)
UCBADDR  DS    F                   UCB ADDR RETURNED FROM IOSUCBSC
UCBNAME  DS    CL3                 UCB FROM OPERTAOR
VOLSER   DC    CL6'XXXXXX'         XXXXXXX IS DEFAULT SER
OLDSER   DC    CL6'OLDSER'         SAVE OLD SER HERE, OLDSER IS DEFAULT
DASDCLAS DC   XL1'20'              TO LIMIT UCB SCAN  TO DASD
*
         CVT  DSECT=YES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
DITTODID CSECT
         #WAYIN GPM=YES
         #SPGSECT
         DC    CL24'P!A!N006SOF154YYYY230185'
         END   DITTODID
