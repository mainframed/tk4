         TITLE 'S0ALIBRY EXIT TO DESCRIBE MEMBERS'
********                                                       ********
*                                                                     *
*        AN EXIT TO UTILITY S0ALIBRY TO MATCH CARD INPUT              *
*        WITH LIBRARY MEMBER NAMES AND PRINT THE RELEVANT             *
*        MEMBER DESCRIPTION FROM THE CARD, WHERE PRESENT.             *
*                                                                     *
********                                                       ********
         EJECT
S0ALIBXT LSAVE EQUATES=YES
         SPACE
         L     R1,0(R1)           * PICK UP A(PARM LIST)
         LR    R2,R1              * SAVE PARM POINTER
         CLC   0(4,R2),ZEROS      LAST ENTRY ?
         BNE   CONTINU            * NO
         CLOSE CARDS              * CLOSE FILE
         B     RETURN
         SPACE 2
CONTINU  EQU   *
         TM    CARDS+48,X'10'     * 1ST ENTRY ?
         BO    OPENED             * NO
         OPEN  (CARDS,(INPUT))    * OPEN FILE
         SPACE
OPENED   EQU   *
         L     R3,0(R2)           * A(MEMBER NAME)
TEST     EQU   *
         CLC   0(8,R3),INAREA     * MEMBER V. CARD
         BL    RETURN             * NO CARD FOR MEMBER
         BE    THISMEM
         GET   CARDS,INAREA       * GET CARD
         B     TEST               * AND TRY AGAIN
         SPACE
THISMEM  EQU   *
         L     R4,4(R2)           * A(PRINT POSN)
         L     R5,8(R2)           * POSITIONS AVAILABLE
         CH    R5,H71             * TOO LARGE ?
         BNH   AROUND             * NO
         LH    R5,H71             * USE MAX VALUE
AROUND   EQU   *
         BCTR  R5,R0              * MINUS 1
         EX    R5,MOVE            * MOVE LITERAL
         B     RETURN             * AND RETURN
         SPACE
EOCARDS  EQU   *
         MVC   INAREA(8),C9       * SET HIGH VALUES AT EOF
RETURN   EQU   *
         LRETN (14,12)
         SPACE 3
H71      DC    H'71'
ZEROS    DC    4X'00'
C9       DC    8C'9'
INAREA   DC    80C' '
MOVE     MVC   0(0,R4),INAREA+9
CARDS    DCB   DSORG=PS,MACRF=(GM),DDNAME=SYSIN,EODAD=EOCARDS
         SPACE
         END
