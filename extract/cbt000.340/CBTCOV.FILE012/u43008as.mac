*        GENERAL DESCRIPTION
*        A ROUTINE FOR OUTPUTTING BLOCK LETTERS FOR PRINTING PURPOSES
*        THUS ALLOWING EASY IDENTIFICATION OF SECTIONS OF PRINTOUT.
*           EXAMPLE:
*                    THE LETTER 'R' WILL BE SET UP FOR PRINTING AS,
*                        RRRRRRRRRRR
*                        RRRRRRRRRRRR
*                        RR        RR
*                        RR        RR
*                        RR        RR
*                        RRRRRRRRRRRR
*                        RRRRRRRRRRR
*                        RR    RR
*                        RR     RR
*                        RR      RR
*                        RR       RR
*                        RR        RR
*        CALLING PROCEDURE
*                      CALL 'ZZBLOCK' USING ZZBLOCK-NAME ZZBLOCK-PRINT
*          WHERE
*                 ZZBLOCK-NAME IS THE NAME TO BE PRINTED IN BLOCKS AND
*                 ZZBLOCK-PRINT IS THE AREA FROM WHICH THE BLOCK
*                 LETTERS MAY BE PRINTED.
*                      05 ZZBLOCK-NAME  PICTURE X(9).
*                      05 ZZBLOCK-PRINT PICTURE X(1310).
*        NOTES
*        1)   EACH LETTER WILL BE BLOCKED 12 COLS. LONG BY 12 LINES
*             HIGH.
*        2)   THE AREA ZZBLOCK-PRINT MUST BE LARGE ENOUGH TO HOLD ALL
*             THE BLOCK LETTERS REQUIRED, AND IS CALCULATED AS FOLLOWS,
*              ZZBLOCK-PRINT AREA = (NO. OF LETTERS * 14 - 2)*12
*        3)   EMBEDDED SPACES ARE NOT ALLOWED.
*        EXAMPLE
*        IT IS REQUIRED TO PRINT 'FACT11' IN BLOCK CAPITALS.
*        WORKING STORAGE WILL CONTAIN,
*                     02 ZZBLOCK-NAME   PICTURE X(9) VALUE 'FACT11'.
*                     02 ZZBLOCK-PRINT.
*                       05 ZZBLOCK-LINE OCCURS 12 PICTURE X(82).
*        IF NOW THE 'WRITE SYSOUT FROM ZZBLOCK-LINE(N)' STATEMENT IS
*        GIVEN 12 TIMES FOR N = 1 THRU 12 ,THE WORD 'FACT11' WILL BE
*        PRINTED IN BLOCK CAPITALS 82 COLS. LONG AND 12 LINES DEEP.
*        A LINKEDIT INCLUDE CARD FOR IEFSD095 MUST BE IN AT LINKEDIT
*        TIME. ROUTINE CAN BE FOUND IN SYS1.CI505 ON THE DLIB01 PACK.
ZZBLOCK  CSECT
         IN    12,PARM=YES
         L     2,0(1)              ADDRESS OF AREA
         ST    2,LETTERS
         L     3,4(1)              ADDRESS OF WHERE TO PUT IT
         ST    3,ADDR
         LR    4,2
         LA    5,9                 MAX OF 9 LETTERS TO A PRINT LINE
         SR    6,6
LOOP    EQU   *
         CLI   0(4),X'40'
         BE    CLEAR               GO CLEAR AREA
         LA    6,1(6)              COUNT LETTERS
         LA    4,1(4)
         BCT   5,LOOP
CLEAR    EQU   *                   CALC LENGTH TO BE CLEARED
         ST    6,COUNT             NUMBER OF LETTERS
         MH    6,=H'14'
         S     6,=F'2'             LENGTH FOR CLEARING
         ST    6,LINELEN           LENGTH OF A LINE
         MH    6,=H'12'
         LR    7,6
SPACES   S     7,=F'256'
         BM    LESS256
         MVI   0(3),X'40'
         MVC   1(255,3),0(3)
         LA    3,256(3)
         LTR   7,7
         BZ    CALL
         B     SPACES
MOVE     MVC   1(1,3),0(3)
LESS256  EQU   *
         MVI   0(3),X'40'
         A     7,=F'254'
         BM    CALL
         EX    7,MOVE              MOVE VARIABE NUMBER OF SPACES
CALL     EQU   *
         LA    3,12
LOOP2    EQU   *
         L     1,LINE
         LA    1,1(1)
         ST    1,LINE
         LA    1,LIST
         CALL  IEFSD095
         L     2,ADDR
         A     2,LINELEN
         ST    2,ADDR
         BCT   3,LOOP2
         XC    LINE,LINE
         OUT
         DS    0F
LIST     DS    0CL16
LETTERS  DS    F
         DC    A(LINE)
ADDR     DS    F
         DC    A(COUNT)
LINELEN  DC    F'0'
LINE     DC    F'0'
COUNT    DC    F'0'
         LTORG
         END
