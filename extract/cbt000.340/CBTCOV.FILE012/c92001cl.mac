PROC 0
/***********************************************************************
/* DESCRIPTION      - INVOKE TSO COMMANDS.
/*
/* AUTHOR           - PAUL FOX
/*
/* HELP PANELS      -
/*
/* MESSAGE MEMBERS  - VVT10
/*
/* SKELETON MEMBERS -
/*
/* PANELS USED      -
/*
/* CLISTS USED      -
/*
/* TABLES USED      -
/*
/* MODIFICATIONS    - 22/10/84 - NEW
/*
/***********************************************************************
CONTROL NOFLUSH NOMSG /*CONLIST LIST

/*IF &SYSUID = TSO076 THEN +
/*    CONTROL CONLIST LIST

  ISPEXEC VGET (VCMD1 VCMD2 VCMD3 VCMD4 VCMD5 VCMD6 VCMD7 +
      VCMD8 VCMD9 VCMD10 VCMD11 VCMD12 VCMD13 VCMD14 VCMD15 +
      ) PROFILE

  SET &MESSAGE =
  SET &CURSOR =
LOOP: +
  ISPEXEC CONTROL DISPLAY REFRESH
  ISPEXEC DISPLAY PANEL(V00PT1) MSG(&MESSAGE) CURSOR(&CURSOR)
  SET &OLDCC = &LASTCC
  SET &MESSAGE =
  SET &CURSOR =
  ISPEXEC VPUT (VCMD1 VCMD2 VCMD3 VCMD4 VCMD5 VCMD6 VCMD7 +
      VCMD8 VCMD9 VCMD10 VCMD11 VCMD12 VCMD13 VCMD14 VCMD15 +
      ) PROFILE
  IF &OLDCC = 8 THEN +
      EXIT CODE(8)

  ERROR DO
      SET &ERRCC = &LASTCC
      SET &MESSAGE = VVT101A
      SET &S&COUNT = &STR(*ER)
      SET &CURSOR = S&COUNT
      SET &ERRORS = Y
      RETURN
  END
  SET &COUNT = 1
  SET &ERRORS = N
  DO WHILE &COUNT <=15 AND &ERRORS = N
      SET &SEL = &&S&COUNT
      IF  &SUBSTR(1,&SEL ) = S +
      OR  &SUBSTR(1,&SEL ) = H THEN DO
          IF  &SUBSTR(1,&SEL ) = H THEN DO
              SET &CMD = &STR(&&VHELP&COUNT)
              IF  &SUBSTR(1,&CMD ) = &STR(%) THEN +
                  SET &CMD = +
                      &STR(HELP &SUBSTR(2:&LENGTH(&STR(&CMD )),&CMD ))
              ELSE +
                  SET &CMD = &STR(HELP &CMD)
          END
          ELSE +
              SET &CMD = &STR(&&VCMD&COUNT)
          SET &S&COUNT =
          IF  &STR(&CMD) ^= THEN DO
              CONTROL MSG PROMPT
              SET &OLDSCAN = &SYSSCAN
              SET &SYSSCAN = 2
              &CMD
              SET &SYSSCAN = &OLDSCAN
              SET &CURSOR = S&COUNT
              CONTROL NOMSG NOPROMPT
          END
      END
  SET &COUNT = &COUNT + 1
  END
  ERROR OFF
  GOTO LOOP
