         TITLE 'MDUMP-MTRACE-MSNAP FORTRAN AIDS-VERSION 4.0'
MDUMP    CSECT
*                   M D U M P  VERSION 4.0   M.BRYAN   OS 20.6
         ENTRY MSNAP,MTRACE
         STM   14,12,12(13)
         LR    3,13
         LR    8,15
         USING MDUMP,8
         LA    13,MRBDSA
         ST    3,4(,13)
         ST    13,8(,3)
*
         SR    5,5
         LTR   1,1
         BC    13,NOPARMS
         L     1,0(,1)
         L     5,0(,1)
         ST    5,ND      SETS ND=0 AND RND=REQUIRED NO OF DUMPS
NOPARMS  BAL   2,LINK2   FIXES  IHC207-225
         LA    3,FLD2
         BAL   2,LINK1             231-239
         LA    3,FLD3
         BAL   2,LINK1             241-301
         LA    11,2
         ST    11,ZERO
         LA    3,FLD4
         BAL   2,LINK1   SPECIAL FIX FOR IHC217
*
         MVC   EXIT2(8),EXIT  THAT SETS UP EXIT2
*
         MVC   MDUMP(4),EXIT+12
         LTR   5,5
         BZ    NOOPEN
         OPEN  (SNAPDCB,(OUTPUT))
         TM    SNAPDCB+48,X'10'    IF FILE OPEN
         BO    EXIT                RETURN NORMALLY
NOOPEN   SR    2,2
         ST    2,ND           CANCEL SNAP REQUEST
         B     EXIT
         DS    0F
LINK1    LM    11,12,0(3)
         STM   11,12,ERSTRT
LINK2    LA    1,PARAMS
EXIT2    L     15,ERRSET
         BALR  14,15
         BR    2
         B     4(,14)
DWRK1    DS    0D
ERSTRT   DC    F'207'
EREND    DC    F'225'
DWRK2    DS    0D
FLD2     DC    F'231',F'239'
FLD3     DC    F'241',F'301'
FLD4     DC    F'217',F'0'
ERRSET   DC    V(ERRSET)
PARAMS   DC    A(ERSTRT)
         DC    A(ZERO)
         DC    A(ZERO)
         DC    A(ZERO)
         DC    A(TRAP)
         DC    X'80',AL3(EREND)
ZERO     DC    F'0'
TRAP     DC    A(MSNAP)
MRBDSA   DC    18F'0'
ND       DC    H'0'
RND      DC    H'0'
         TITLE 'MTRACE-PROVIDE TRACEBACK WHEN ABENDING'
         CNOP  0,4
MTRACE   STM   14,12,12(13)
         BAL   8,12(15)
         DC    A(MDUMP)
         L     8,0(,8)
         LR    3,13
         LA    13,MRBDSA
         ST    3,4(,13)
         ST    13,8(,3)
CHANGER  LA    2,MTRACE ** THIS INSTRUCTION CHANGED FOR STAE ENTRY
         MVC   CHANGER(4),GOTOSTAE
         STAE  (2),XCTL=YES
         B     EXIT
GOTOSTAE B     STAERTN
STAERTN  L     13,76(,1) RESTORE 13
         SR    1,1
         L     15,ERRTRA
         BALR  14,15
         LA    13,MRBDSA
         B     EXIT
ERRTRA   DC    V(ERRTRA)
         TITLE 'MSNAP-PROGRAMMER SNAP AND IHC TRAP'
         CNOP  0,4
MSNAP    STM   14,12,12(13)
         BAL   8,12(15)
         DC    A(MDUMP)
         L     8,0(,8)
         LR    3,13
         LA    13,MRBDSA
         ST    3,4(,13)
         ST    13,8(,3)
*
         LTR   1,1  IF NOPARMS THEN R1=0 SO MUST BE TRUE MSNAP
         BNZ   IHCEROR
         MVC   DUMPSNAP(4),SNAP
         MVC   REASON(21),REASON2
         LA    5,EXIT
         B     SNAPX
*
IHCEROR  L     4,4(,1)
         LA    5,EXIT2
         L     4,0(,4)
         CVD   4,DWRK1
         UNPK  DWRK2(7),DWRK1(8)
         OI DWRK2+6,X'F0'
         MVC   REASON+17(3),DWRK2+4
         TITLE 'SNAP ACTION ROUTINE'
* SERVICES  TRAPPED IHC ERRORS WHERE ND<RND
*           PROGRAMMER REQUESTED DUMP
SNAPX    LH    2,ND
         LH    3,RND
         LA    2,1(,2)
         CR    2,3
         BNL   ABEND
         TM   SNAPDCB+48,X'10'
         BNO   ABEND
         STH   2,ND
         CVD   2,DWRK1
         UNPK  DWRK2(7),DWRK1(8)
         OI    DWRK2+6,X'F0'
         MVC   SNAPCD(2),DWRK2+5
         LA    1,WTOP
         SVC   35
         STC   2,SNAPPS
         LA    1,SNAPPS
         SVC   51
         MVC   REASON(21),REASON1
         MVC   DUMPSNAP(4),DUMP
         BR    5
ABEND    MVC   DMSG(17),DMSG2      ABEND 17 EXIT
         LA    1,WTOP
         SVC   35
         STAE  0
         ABEND 17,DUMP
EXIT     L     13,4(0,13)
         LM    14,12,12(13)
         MVI   12(13),X'FF'
         SR    15,15
         BR    14             <- EXIT+12
WTOP     DS    0F
         DC    AL2(DC-*) MSG LENGTH
         DC    X'8000'
         DC    C' M'
DUMPSNAP DC    C'DUMP CALLED ON '
REASON   DC    C'FORTRAN ERROR IHC***I ('
DMSG     DC    C'  SNAP  ID = '
SNAPCD   DC    C'**) '
DC       DC    X'00000020'
DMSG2    DC    C'ABDUMP USER=0017)'   17
REASON2  DC    C'PROGRAMMERS  REQUEST '  21
REASON1  DC    C'FORTRAN ERROR IHC***I'  21
SNAP     DC    C'SNAP'
DUMP     DC    C'DUMP'
         CNOP  0,4
SNAPPS   DC    X'0000A388'
         DC    A(SNAPDCB)
         DC    A(0),X'80',AL3(0)
SNAPDCB  DCB   DDNAME=MDUMPOUT,DSORG=PS,RECFM=VBA,MACRF=(W),           *
               BLKSIZE=882,LRECL=125
         END
