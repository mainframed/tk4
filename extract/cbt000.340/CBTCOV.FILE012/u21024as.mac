         TITLE 'ZFLUSH - CAUSES A JOB TO FLUSH IF RESTARTED'
ZFLUSH   START
**    AUTHORS - ICI (MOND DIVISION)
         PREPARE
*              READ JFCB & BUILD VOLUME LIST
         RDJFCB (FLUSH)
         MVC   VOLSER,JFCBVOL
         DEVTYPE DDNAME,DEVINFO
         MVC   DEVCODE,DEVINFO
*              BUILD UNIQUE DSNAME FROM JOBNAME
         EXTRACT TIOTADDR,FIELDS=(TIOT)
         L     2,TIOTADDR
         MVC   NEWDSN+16(8),0(2)
*              RENAME DATA SET
         SR    0,0
         RENAME OLDDSCB
         LTR    15,15
         BZ    NORMAL         OLDDSN HAS BEEN RENAMED
         CLI   SEQNUM+1,3
         BE    RESTART        NEWDSN ALREADY EXISTS
         WTO   'ZFLUSH - UNABLE TO SET UP VTOC ENTRY'
         B     RETURN   BAD RETURN FROM RENAME   FAIL SAFE
*
NORMAL   EQU   * WRITE BACK JFCB TO JOBQE & RETURN
         MVC   OLDDSN,NEWDSN
         OPEN  (FLUSH),TYPE=J
         CLOSE FLUSH
RETURN   EQU   *
         EXIT
*
RESTART  EQU   *  DELETE NEWDSCB & ABEND 222
         SR    0,0
         STH   0,SEQNUM
         SCRATCH NEWDSCB
         ABEND 222
*
DCBEXIT  DS    0F
         DC    X'87',AL3(JFCB)
JFCB     DS    0D
OLDDSN   DS    CL44
         DS    CL74
JFCBVOL  DS    CL6
         DS    CL52
*
VOLIST   DC    H'1'     ONE VOLUME
DEVCODE  DS    XL4
VOLSER   DS    CL6
SEQNUM   DC    H'0'
*
DEVINFO  DC    D'0'
DDNAME   DC    CL8'FLUSH'
NEWDSN   DC    CL44'ZFLUSH.FLAGDSCB.'
TIOTADDR DC    A(0)
*
OLDDSCB  CAMLST RENAME,OLDDSN,NEWDSN,VOLIST
NEWDSCB  CAMLST SCRATCH,NEWDSN,,VOLIST
FLUSH    DCB   DDNAME=FLUSH,MACRF=(E),DSORG=DA,EXLST=DCBEXIT
         END
