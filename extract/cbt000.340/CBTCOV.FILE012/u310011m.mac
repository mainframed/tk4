         MACRO
&NAME    ENTER &LABEL,&REGS,&SAVE=,&BASE=12,&TEMP=14,&STORE=0
         LCLA  &CHARS,&COUNT,&DISP,&HI,&LO,&BASENO,&BASENDX
         LCLC  &BASEREG
         CNOP  0,4
         USING *,15
&NAME    DS    0F
         AIF   ('&LABEL' EQ '' AND '&SAVE' NE '').STORE
         AIF   ('&LABEL' EQ '').SETDISP
&CHARS   SETA  K'&LABEL-2
&COUNT   SETA  2
.TSTCHAR ANOP
         AIF   ('&LABEL'(&COUNT,1) EQ '&&'(1,1)).DECR
         AIF   ('&LABEL'(&COUNT,1) NE '''').INCR
.DECR    ANOP
&CHARS   SETA  &CHARS-1
&COUNT   SETA  &COUNT+1
.INCR    ANOP
&COUNT   SETA  &COUNT+1
         AIF   (&COUNT GE K'&LABEL).TSTCHAR
&COUNT   SETA  (&CHARS+4)/4*4
.SETDISP ANOP
         AIF   ('&SAVE' NE '').NOSAVE
&DISP    SETA  72
.NOSAVE  ANOP
&DISP    SETA  &DISP+&COUNT+4
         B     *+&DISP                 BRANCH OVER INITIAL AREAS
         AIF   ('&LABEL' EQ '').SETSAVE
         DC    AL1(&CHARS)             NO OF CHARS IN IDENTIFIER
         DC    C&LABEL                 IDENTIFIER
         AIF   ('&SAVE' NE '').STORE
.SETSAVE ANOP
         DC    18A(0)                  INTERNAL SAVE AREA
.STORE   ANOP
         AIF   ('&REGS' EQ '').SAVEALL
         AIF   (N'&REGS NE 1).SETREGS
         ST    &REGS,20+4*&REGS.(0,13) SAVE REGISTER
         AGO   .SETBASE
.SETREGS ANOP
&LO      SETA  &REGS(1)
&HI      SETA  &REGS(2)
         AIF   (&LO LE 2).ONESAVE
         STM   14,1,12(13)             SAVE SYSTEM REGISTERS
         STM   &LO,&HI,20+4*&LO.(13)   SAVE OPTIONAL REGISTERS
         AGO   .SETBASE
.ONESAVE ANOP
         STM   14,&HI,12(13)           SAVE REGISTERS
         AGO   .SETBASE
.SAVEALL ANOP
         STM   14,12,12(13)            SAVE REGISTERS
.SETBASE ANOP
         AIF   (N'&BASE EQ 1).ONEBASE
         CNOP  2,4                     ENSURE MIDDLE-WORD ALIGNMENT
&BASEREG SETC  '&BASE(1)'
         BALR  &BASEREG,0              ESTABLISH ADDRESSABILITY
         USING *,&BASEREG
&BASENO  SETA  N'&BASE
&BASENDX SETA  2
.BASELP1 ANOP
&BASEREG SETC  '&BASE(&BASENDX)'
&DISP    SETA  &BASENO*4
         L     &BASEREG,*+&DISP        LOAD BASE REGISTER
&BASENDX SETA &BASENDX-1
&DISP    SETA &BASENDX*4
         USING *-&DISP+4096*&BASENDX,&BASEREG
&BASENDX SETA &BASENDX+1
&DISP    SETA &BASENO*4
         AIF   (&BASENDX EQ &BASENO).BASEXIT
&BASENDX SETA  &BASENDX+1
         AGO   .BASELP1
.BASEXIT ANOP
         B     *+&DISP                 BRANCH ROUND BASE ADDRESSES
&BASENDX SETA  1
.BASELP2 ANOP
         AIF   (&BASENDX EQ &BASENO).CKSAVE
         DC    A(*-&DISP+4096*&BASENDX) BASE ADDRESS
&DISP    SETA  &DISP+4
&BASENDX SETA  &BASENDX+1
         AGO   .BASELP2
.ONEBASE ANOP
         BALR  &BASE,0                 ESTABLISH ADDRESSABILITY
         USING *,&BASE
.CKSAVE  ANOP
         LR    &TEMP,13                SAVE CALLERS SAVE ADDRESS
         AIF   ('&SAVE' EQ 'DYN').DYNSAVE
         AIF   ('&SAVE' EQ '').INTSAVE
         LA    13,&SAVE                LOAD ADDRESS OF NEW SAVE AREA
         AGO   .CHAIN
.DYNSAVE ANOP
&COUNT   SETA  72+&STORE
         LA    0,&COUNT                LOAD COUNT OF DYNAMIC AREA
*        GETMAIN R,LV=(0)              AQUIRE DYNAMIC AREA
         GETMAIN R,LV=(0)
         LR    13,1                    LOAD ADDRESS OF NEW SAVE AREA
         LM    0,1,20(&TEMP)           RESTORE REGISTERS
         AGO   .CHAIN
.INTSAVE ANOP
         LA    13,4+&COUNT.(0,15)      LOAD ADDRESS OF NEW SAVE AREA
.CHAIN   ANOP
         ST    &TEMP,4(0,13)           FORWARD CHAIN
         ST    13,8(0,&TEMP)           BACKWARD CHAIN
         MEND
