UNCATALL CSECT
*
*  RTPUNCAL IS DESIGNED TO HELP USERS OF LARGE GENERATION DATA GROUPS
*  IN TWO WAYS...
*
*  1 - IT WILL 'EMPTY' A GDG INDEX ON REQUEST WITHOUT WAITING FOR IT TO
*  FILL UP TO A PRESCRIBED NUMBER OF ENTRIES.   THIS PERMITS E.G. A
*  VARIABLE NUMBER OF TAPES TO BE ACCUMULATED DURING A MONTH, FOR THE
*  USER THEN TO TAKE ADVANTAGE OF THE ABILITY TO REFER TO THE WHOLE SET
*  OF TAPES BY ONE DD CARD FOR THE GDG, AND THEN TO EMPTY THE GDG INDEX
*  AND START THE ACCUMULATION AGAIN FOR THE NEXT MONTH.
*
*  2 - IT CAN SAVE A GREAT DEAL OF PAINSTAKING KEY-PUNCHING WHEN THE
*  ENTRIES OF A LARGE GDG HAVE TO BE CORRECTED FOR AN ERROR AFFECTING
*  RELATIVELY OLD MEMBERS.  INSTEAD OF HAVING TO PUNCH CARDS TO
*  UNCATLG AND THEN CATLG EACH GENERATION AFFECTED THIS PROGRAM CAN
*  BE RUN AND WILL...
*        A.  UNCATLG THE GENERATIONS AUTOMATICALLY.
*        B.  PUNCH CARDS TO CATLG THE MEMBERS, PUNCHING THE DSNAME AND
*              THE VOLUME SERIAL NUMBERS ON SEPARATE CONTINUATION CARDS
*              FOR EASE OF 'SHUFFLING' THE VOLUMES.
*        C.  IT ALSO PRINTS A REFERENCE LISTING OF THE CARDS THAT HAVE
*              BEEN PUNCHED.
*
*  THE JCL REQUIRED IS (EXAMPLE)...
*        //UNDO EXEC PGM=RTPUNCAL
*        //SYSPRINT DD SYSOUT=...
*        //SYSPUNCH DD SYSOUT=...  (OR UNIT=00D)
*        //SYSIN  DD  *
*          UNCATALL  GEN.DATA.GROUP(0)
*        /*
*
*  NOTE.
*        1  MULTIPLE UNCATALL STATEMENTS CAN BE PROCESSED IN ONE RUN.
*        2  THE FORMAT OF THE UNCATALL CARD IS AS SHOWN, GIVING THE
*              NAME OF THE GDG AND THE RELATIVE GENERATION NUMBER AT
*              WHICH THE UNCATLG PROCESS IS TO START.
*              IF, FOR A GDG OF 10 MEMBERS, AN UNCATALL G.D.G(-3) CARD
*              WAS USED GENERATIONS 0,1, AND 2 WOULD REMAIN, AND
*              GENERATIONS 4-10 WOULD BE UNCATLGED.
*        3  SYSPRINT HAS PRE-CODED RECFM=FBM,LRECL=81,BLKSIZE=810
*        4  SYSPUNCH HAS PRE-CODED RECFM=FB,LRECL=80,BLKSIZE=800
*        5  GDG INDEXES ON A SEPARATE CONTROL VOLUME WILL BE PROCESSED
*              NORMALLY PROVIDED THAT THE INDEX IS ACCESSIBLE THROUGH
*              A NORMAL 'CONNECT' STATEMENT.
*
*
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         OPEN  (SYSIN,,LIST,(OUTPUT),PUNCH,(OUTPUT))
         PUT   LIST,NP
         PUT   LIST,TITLE
GETCARD  GET   SYSIN,CARD
         LA    6,CARD-1
         TRT   1(80,6),NBT
         BC    8,GETCARD
         LR    4,1
         CLC   0(8,4),=C'UNCATALL'
         BE    CARDOK
         MVI   SCRSW,0
         CLC   0(8,4),=C'SCRUNTAL'
         BNE   SYNTAX
         MVI   SCRSW,255
CARDOK   DS    0H
         LA    6,8(4)
         TRT   1(70,6),NBT
         BC    8,GETCARD
         LR    4,1
FIND     MVC   GDGNAME,0(4)
         LOCATE  CAMLSTA
         ST    15,LOC15
         LTR   15,15
         BNZ   LOCERR
         MVC   CARDOUT+14(56),GDGNAME
         CATALOG  CAMLSTB
         ST    15,CAT15
         LTR   15,15
         BNZ   CATERR
         SP    LINECNT,=P'4'
         BP    PUTL
         ZAP   LINECNT,=P'55'
         PUT   LIST,NP
         PUT   LIST,TITLE
PUTL     TRT   CARDOUT+14(56),BT
         MVI   0(1),C','
         MVI   CARDOUT+71,C'*'
         PUT   LIST,LINE
         PUT   PUNCH,CARDOUT
         MVI   CARD2+19,64
         MVC   CARD2+20(52),CARD2+19
         LH    10,LOCA                 NO. OF VOLS
         LA    9,LOCA+2
         LA    8,CARD2+19
         LA    7,CARD2+26
         CLI   2(9),128
         BNE   TDISK
         TM    1(9),32
         BNO   *+14
         MVC   0(7,8),=C'2400-4='
         B     ENDTAP
         TM    1(9),128
         BNO   *+14
         MVC   0(7,8),=C'2400-2='
         B     ENDTAP
         TM    0(9),4
         BNO   *+14
         MVC   0(7,8),=C'2400-3='
         B     ENDTAP
         LA    7,CARD2+24
         MVC   0(5,8),=C'2400='
         B     ENDTAP
TDISK    LA    7,CARD2+24
         CLI   2(9),32
         BNE   ENDT
         MVI   CARD2+23,C'='
         SR    6,6
         IC    6,3(9)
         LA    1,DISKTAB-4
         SLL   6,2
         AR    1,6
         MVC   0(4,8),0(1)
ENDT     LH    14,LOCA
         MVI   MVSW,0
         C     14,ONE
         BE    ENTERDV
         MVI   0(7),C'('
         LA    7,1(7)
         MVI   MVSW,255
ENTERDV  MVC   0(6,7),4(9)
         LA    7,6(7)
         CLI   MVSW,0
         BE    EXITV
         BCT   14,*+8
         B     ENDV
         MVI   0(7),C','
         LA    7,1(7)
         LA    9,12(9)
         C     7,=A(CARD2+71)
         BL    ENTERDV
         B     EXITV
ENDV     CLI   MVSW,0
         BE    EXITV
         MVI   0(7),C')'
         B     EXITV
ENDTAP   LH    14,LOCA
         C     14,ONE
         BE    TAPESV
         MVI   0(7),C'('
         LA    7,1(7)
NEXTV    MVC   0(6,7),4(9)
         MVI   6(7),C','
         LH    1,10(9)
         CVD   1,DW
         CP    DW,=P'10'
         BL    UNPKMV
         UNPK  UK,DW+6(2)
         MVC   7(2,7),UK+1
         LA    7,1(7)
         B     *+10
UNPKMV   UNPK  7(1,7),DW+7(1)
         OI    7(7),240
         BCT   14,*+12
         MVI   8(7),C')'
         B     EXITV
         MVI   8(7),C','
         LA    7,9(7)
         LA    9,12(9)
         C     7,=A(CARD2+71)
         BL    NEXTV
         B     EXITV
TAPESV   MVC   0(6,7),4(9)
         CLI   11(9),1
         BNH   EXITV
         MVI   0(7),C'('
         MVC   1(6,7),4(9)
         MVI   7(7),C','
         LH    1,10(9)
         CVD   1,DW
         CP    DW,=P'10'
         BL    UNPKSV
         UNPK  UK,DW+6(2)
         MVC   8(2,7),UK+1
         LA    7,1(7)
         B     *+10
UNPKSV   UNPK  8(1,7),DW+7(1)
         OI    8(7),240
         MVI   9(7),C')'
EXITV    MVI   LINE2,25
         CLI   SCRSW,0
         BE    *+8
         MVI   LINE2,17
         PUT   LIST,LINE2
         PUT   PUNCH,CARD2
         CLI   SCRSW,0
         BE    NORMUNC
         SR    0,0
         SCRATCH  CAMLSTC
         MVC   SLINE+1(54),NORMS
         LH    2,LOCA
         LA    3,LOCA+2
SCVOLP   MVC   SLINE+12(6),4(3)
         CLI   11(3),0
         BE    PUTSLINE
         MVC   SLINE+19(27),VNM
         CLI   11(3),5
         BNL   PUTSLINE
         MVC   SLINE+31(15),NODSCB
         CLI   11(3),1
         BE    PUTSLINE
         MVC   SLINE+31(15),ERRIO
PUTSLINE PUT   LIST,SLINE
         SP    LINECNT,=P'3'
         LA    3,12(3)
         BCT   2,SCVOLP
         PUT   LIST,ONEUP
NORMUNC  DS    0H
         B     FIND
         EJECT
EOF      DS    0H
         CLOSE (SYSIN,,LIST,,PUNCH)
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
LOCERR   B     LOCERR(15)
         B     NOVOL
         B     WRONGNAM
         B     GETCARD
         B     WRONGNAM
         B     SYNTAX
         B     IOERR
NOVOL    PUT   LIST,VOLNOTON
         B     GETCARD
WRONGNAM PUT   LIST,NAMERR
         B     GETCARD
SYNTAX   PUT   LIST,SYNTERR
         B     GETCARD
CATERR   B     CATERR(15)
         B     NOVOL
         B     WRONGNAM
         B     GETCARD
         B     WRONGNAM
         B     WRONGNAM
         B     WRONGNAM
         B     IOERR
IOERR    PUT   LIST,IOERMSG
         B     GETCARD
         EJECT
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),LRECL=80,EODAD=EOF
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),LRECL=81,RECFM=FBM, *
               BLKSIZE=810
PUNCH    DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=(PM),LRECL=80,RECFM=FB,  *
               BLKSIZE=800
         EJECT
CAMLSTA  CAMLST  NAME,GDGNAME,,LOCA
CAMLSTB  CAMLST  UNCAT,GDGNAME
CAMLSTC  CAMLST  SCRATCH,GDGNAME,,LOCA,,OVRD
DW       DC    D'0'
LOCA     DS    CL272
LOC15    DC    F'0'
CAT15    DC    F'0'
ONE      DC    F'1'
         EJECT
NBT      DC    193X'00'
         DC    9X'FF'
         DC    7X'00'
         DC    9X'FF'
         DC    8X'00'
         DC    8X'FF'
         DC    6X'00'
         DC    10X'FF'
         DC    6X'00'
BT       DC    64X'00'
         DC    X'FF'
         DC    195X'00'
         DC    CL4' '
CARD     DC    CL80' '
GDGNAME  DC    CL64' '
DISKTAB  DC    C'2311'
         DC    C'2301'
         DC    C'2302'
         DC    C'2302'
         DC    C'2321'
         DC    CL8' '
         DC    C'2314'
UK       DC    CL3' '
LINECNT  DC    PL2'55'
SCRSW    DC    X'00'
MVSW     DC    X'00'
NP       DC    X'8B'
ONEUP    DC    X'0B'
         EJECT
TITLE    DC    X'19'
         DC    CL26' '
         DC    CL54'RESPOND TSG UNCATALL UTILITY'
LINE2    DC    X'19'
CARD2    DC    CL15' '
         DC    CL65'VOL=1234= '
VOLNOTON DC    X'19'
         DC    CL80'*** REQUIRED VOLUME NOT MOUNTED OR DOES NOT CONTAIN*
               SYSCTLG'
NAMERR   DC    X'19'
         DC    CL80'*** NO MORE DATA SETS FOR THIS REQUEST ( OR REQUEST*
                WAS WRONGLY SET UP)'
SYNTERR  DC    X'19'
         DC    CL80'*** YOU CAN''T SPELL'
IOERMSG  DC    X'19'
         DC    CL80'*** I/O ERROR IN CATALOGUE OPERATION'
LINE     DC    X'09'
CARDOUT  DC    CL80' CATLG DSNAME= '
SLINE    DC    X'11'
         DC    CL80' '
NORMS    DC    CL54'SCRATCH ON 123456 WAS SUCCESSFUL'
VNM      DC    CL27'HAD ERROR - VOL NOT MOUNTED'
NODSCB   DC    CL15'NO DSCB'
ERRIO    DC    CL15'I/O ERROR'
         END
