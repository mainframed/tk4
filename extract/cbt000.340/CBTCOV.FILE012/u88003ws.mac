         TITLE   'RARRETN - BUILD FDRDSF JCL FOR RETURN'
         REGISTER
*
* REGISTERS USED OVER WHOLE PROGRAM
*  R 1=BASE2    R12=BASE   R13=SAVE CHAIN
*
         SUBR  RARRETN
*
         L     R11,BASE2
         USING RARRETN+4096,R11
         MVI   RC,0              DEFAULT RETURN CODE
         EJECT
*
* OPEN SYSPRINT FOR MESSAGES
*
         OPEN  (DCBSYSP,(OUTPUT))
         USING IHADCB,R5           ADDRESS DCB
         LA    R5,DCBSYSP
         TM    DCBOFLGS,DCBOFOPN
         BO    SYSPOK
         DROP  R5
         WTO   'RARRE02I UNABLE TO OPEN DATASET - DDNAME=SYSPRINT',    +
               ROUTCDE=1,DESC=3
         B     FAIL
SYSPOK   EQU   *
         OI    FLAG2,FLGSYSP       SET FLAG FOR OPEN
         EJECT
*
*        LOCK  QUEUE AND OPEN IT
*
         ENQ   (QNAME,RNAME,E,,SYSTEM)   EXCLUSIVE CONTROL OF Q
         OI    FLAG2,FLGENQ               MARK AS ENQUED
         OPEN  (DCBQUEUE,(INOUT))
         LA    R5,DCBQUEUE
         BAL   R4,TESTOPEN         LOOK TO SEE IF OPEN
         OI    FLAG2,FLGQUEUE      SET FLAG FOR OPEN
         EJECT
*
* READ DATA FROM 'ARCHIVE.QUEUE' INTO MEMORY.
*   R5=BUFFER CHAIN POINTER   R6=CURRENT DATA ARE POINTER
*
PROCESS  EQU   *
         LA    R5,CHAIN
STARTP   EQU   *
         GETMAIN R,LV=BLKSIZE+4     GET ENOUGH MEMORY TO READ A BLOCK
         LA    R6,4(R1)          ADDRESS OF BUFFER.
         ST    R1,0(R5)          CHAIN IT OFF THE REST
         ZR    R5
         ST    R5,0(R1)          KILL END OF CHAIN
         LR    R5,R1
         READ  DECBQUE,SF,DCBQUEUE,(6)
         CHECK DECBQUE
         TM    BLKSIZE-1(R6),BLKEND QUICK CHECK FOR QUEUE VALIDITY
         BZ    CORRUPTQ
         TM    BLKSIZE-1(R6),QUEEND LAST BLOCK ?
         BZ    STARTP               YES !
         EJECT
**********************************************************************
*                                                                    *
*       CREATE NEW Q CONTAINING ONLY REQUESTS FOR OTHER PROCESS      *
*       ALSO REMOVES SCRATCHED REQUESTS                              *
*         R2=NEW QUEUE BUFFER ADDRESS R5=CURRENT BUFFER POINTER      *
*         R6=CURRENT RECORD POINTER   R4=NEW RECORD POINTER          *
*         R7=COUNT OF RETURN RECORDS                                 *
**********************************************************************
NEWQ     EQU   *
         BAL   R14,GETRECOR        GET FIRST RECORD OFF OLD Q
*                                  NOTE FIRST RECORD IS A DUMMY
         ZR    R7                  ZERO COUNT OF RETURN REQUESTS
         POINT DCBQUEUE,BLOCKADR   POINT TO START OF DS READY FOR WRT
         GETMAIN R,LV=BLKSIZE      GET A BUFFER FOR NEW QUEUE
         ST    R1,NEWQBUFF         SAVE ADDRESS
         LR    R2,R1               SAVE START OF BUFFER ADDRESS
         B     ZEROQUE
*
* SEARCH QUEUE PICKING OUT THE ARCHIVE REQUESTS AND WRITE THEM BACK
*
QENQUIRE EQU   *
         BAL   R14,GETRECOR        GET ANOTHER REQUEST
         USING QUEUEMAP,R6         MAP OLD COPY OF QUEUE
         TM    QACTION,BADBITS     ANY BITS SET THAT SHOULD NOT BE SET
         BNZ   CORRUPTQ            MAKE SURE REQUEST GETS NO FURTHER
         TM    QACTION,FLAGDEL     IS IT A SCRATCHED RECORD ?
         BO    LASTREQ             IF SO IGNORE IT
         TM    QACTION,FLAGARC     IS IT RET. REQUEST
         BO    ARCHIVDS            SAVE AWAY ARCHIVE REQUESTS
         TM    QACTION,FLAGRET     ANY ARCS PRESENT
         BZ    CORRUPTQ            WHAT ON EARTH IS IT THEN
         INCR  R7
         OI    FLAG1,FLGJCL        INDICATE THERE IS SOME WORK TO DO
LASTREQ  EQU   *
         TM    QEND,FLAGEND        LAST RECORD ?
         BZ    QENQUIRE
         B     WRITNEWQ            WRITE NEW Q
         DROP  R6
         EJECT
*
* HERE TO INSERT A NEW RECORD IN THE OUTPUT BUFFER
*
ARCHIVDS EQU   *
         USING QUEUEMAP,R4
         NI    QEND,X'FF'-FLAGEND  TURN OFF MARKER FOR LAST RECORD
         TM    QEND,BLKEND         IS BUFFER FULL ?
         BZ    MVTOBUF             NO SO TRANSFER RECORD
         NI    QEND,X'FF'-QUEEND   TURN OFF END OF QUEUE MARKER
WRITNEWQ EQU   *
         WRITE DECBQUE,SF,DCBQUEUE,(2),MF=E
         CHECK DECBQUE
         TM    BLKSIZE-1(R2),QUEEND WAS THIS THE LAST BLOCK OF QUEUE
         BO    FREEQ               YES SO CLOSE & FREE Q
ZEROQUE  EQU   *
         ZR    R9                  PADD,SOURCE LENGTH FOR MVCL
         LA    R3,BLKSIZE          DESTINATION LENGTH FOR MVCL
         MVCL  R2,R8               ZERO THE BLOCK
         L     R2,NEWQBUFF         RECOVER BLOCK ADDRESS
         MVI   BLKSIZE-1(R2),BLKEND+QUEEND MARK THE END OF THE QUEUE
         LR    R4,R2               POINT TO FIRST RECORD
         B     ADDQ02
MVTOBUF  EQU   *
         INCR  R4,127              POINT TO NEXT EMPTY SLOT
ADDQ02   EQU   *
         MVC   0(RECSIZE,R4),0(R6) MOVE REQUEST TO NEW QUEUE
         OI    QEND,FLAGEND        CURRENT RECORD LAST
         B     LASTREQ             AND GO BACK FOR ANOTHER
         DROP  R4
         EJECT
*
*        CLOSE  AND UNLOCK THE QUEUE
*
FREEQ    EQU   *
         ST    R7,NUMRETN          SAVE NUMBER OF RETURN REQUESTS
         FREEMAIN R,LV=BLKSIZE,A=(2) FREE UP BUFFER AGAIN
         ZERO  NEWQBUFF,4          MARK AS FREED
         CLOSE (DCBQUEUE,FREE)
         NI    FLAG2,X'FF'-FLGQUEUE MARK AS CLOSED NOW
         DEQ   (QNAME,RNAME,,SYSTEM)
         NI    FLAG2,X'FF'-FLGENQ  MARK AS DEQUEUED
         EJECT
**********************************************************************
*        IF THERE IS ANYTHING TO PROCESS THEN OPEN TRACE FILE
**********************************************************************
*
         TM    FLAG1,FLGJCL
         BO    RETNS
         PUT   DCBSYSP,MSG51       NO REQUESTS TO PROCESS
         MVI   RC,4                SET RETURN CODE
         B     FINISH
RETNS    EQU   *
         OPEN  (ACBTRACE)
         LTR   R15,R15
         BNZ   ERRORVOC
         OI    FLAG2,FLGTRACE      SET FLAG FOR OPEN
         OPEN  (DCBFDR,(OUTPUT)) OPEN FDR/JCL FILE
         LA    R5,DCBFDR         POINT AT DCB TO BE TESTED
         BAL   R4,TESTOPEN       LOOK TO SEE IF OPEN
         OI    FLAG2,FLGFDR      SET FLAG FOR OPEN
         EJECT
*
* NOW SCAN THE QUEUE FOR A NEW DATASET TO PROCESS
*    R5=CURRENT BLOCK POINTER   R6=CURRENT RECORD POINTER
*
DISKJCL  EQU   *
         NI    FLAG1,X'FF'-FIRSTI  SET FIRST TIME FLAG
QPROCESS EQU   *
         BAL   R14,GETRECOR        GET A RECORD
         USING QUEUEMAP,R6
         TM    QACTION,FLAGDEL     REQUEST DELETED
         BO    QLOOP               YES
         TM    QACTION,FLAGRET     IS IT RETURN REQUEST
         BO    XPROCESS            NO
QLOOP    EQU   *
         TM    QEND,FLAGEND        Q ALL DONE
         BZ    QPROCESS
         B     ALLDONE
         EJECT
*
*        PRINT OUT IDENTIFING LINES TO SYSPRINT TO AID DEBUGGING
*
* R5=QUEUE BLOCK POINTER, R6=QUEUE RECORD POINTER
*
XPROCESS EQU   *
         TM    FLAG1,FLGHEAD       PRINT LISTING HEADER
         BO    HEADDONE
         OI    FLAG1,FLGHEAD
         PUT   DCBSYSP,MSG05
         PUT   DCBSYSP,MSG09
HEADDONE EQU   *
         LA    R8,FDR2             POINT TO FDR2 CONTROL RECORD
         USING FDRCNTL,R8
         MVC   FDR2DSK,QVOLSER     VOLUME TO RETURN TO
         MVC   FDR2USR,QUSERID     REQUESTING USER
         MVC   FDR2TPE,QTAPE       FROM TAPE
         MVC   FDR2LBL,QTLBL                AT LABEL
         MVC   FDR2DSN,QDSN        SET UP FDR CARD FOR OLD DSN
         MVI   FDR2ID,C'2'         INSERT CARD ID
         MVC   KEYDSN,QDSN         FOR TRACE FILE SEARCH
         MVI   FDR2DSN+44,C' '     OVERWRITE PREVIOUS POSSIBLE COMMA
         MVI   FDR3,C'*'           COMMENT OUT NEW NAME
         TM    QACTION,FLAGNNME    IS IT NEW NAME
         BZ    FDRSYSP
         MVI   FDR3,C' '           THEN REMOVE COMMENT AGAIN
         LA    R1,FDR2DSN+43       POINT TO END OF DSN
FIDSNEND CLI   0(R1),C' '          SCAN BACK LOOKING FOR NON SPACE
         BNE   COMMA
         BCT   R1,FIDSNEND
COMMA    MVI   1(R1),C','          INSERT A COMMA ON TO FDR2 CARD
FDRSYSP  EQU   *
         LA    R8,FDR3             POINT TO FDR3 CONTROL RECORD
         MVC   FDR3DSN,QNEWDSN     SET UP NEW DSN IN FDR CARD
         MVC   FDR3KEY,QKEY        ARCHIVE KEY
         MVI   FDR3ID,C'3'         INSERT CARD ID
         DROP  R8
         PUT   DCBSYSP,FDR2       FDR CARD TO SYSPRINT
         PUT   DCBSYSP,FDR3
         EJECT
*
*        READ  TRACE FILE & FIND INFO ON FILE NEEDED FOR RESTORE
*
* NOTE: TRACE RECORD MUST BE PRESENT BECAUSE IT WAS VERIFIED AT QUEUE
*       ENTRY TIME. BUT WE CHECK EVERY FIELD JUST TO MAKE SURE.
*
* R9=TRACE RECORD POINTER
*
         NI    FLAG3,X'FF'-FLAGSEQ SWITCH SEQUENTIAL OFF
GETT     EQU   *
         BAL   R14,GETRACE
         L     R9,TRCBUFFP         POINT TO TRACE RECORD
         USING TRACEMAP,R9
         CLC   TRACEDAT,QDATE      IS IT RIGHT DATE
         BNE   GETTN               IF NOT GET THE NEXT
         CLC   TRACEKEY,QKEY       IS IT RIGHT KEY
         BNE   GETTN               IF NOT GET THE NEXT
         CLC   TRACETPE,QTAPE      IS IT RIGHT TAPE
         BNE   GETTN               IF NOT GET THE NEXT
         CLC   TRACELBL,QTLBL      IS IT RIGHT LABEL
         BE    PRCESRET            IF SO THEN PROCESS THE RETURN
GETTN    EQU   *
         CLI   FDBK+3,8            DUPLICATE RECORD ?
         BE    GETT                NO SO WE MUST HAVE AN ERROR HERE
NOTRACE  EQU   *                   COME HERE IF RECORD NOT FOUND
         PUT   DCBSYSP,MSG02
         MVI   RC,12               SET RETURN CODE
         LA    R1,QDSN             SET UP PARM LIST FOR SEND
         ST    R1,SENDPARM         BECAUSE UNABLE TO FIND TRACE RECORD
         LA    R1,QUSERID
         ST    R1,SENDPARM+4
         LA    R1,NTRCMSG
         ST    R1,SENDPARM+8
         LA    R1,NTRCMSGL
         ST    R1,SENDPARM+12
         LA    R1,SENDPARM
         BAL   R14,SENDMESG
         B     GETNEXT
         EJECT
PRCESRET EQU   *
         MVC   VOLSER,QVOLSER      GET DEVICE CHARACTERISTICS FOR
         BAL   R14,DEVCHAR         RETURN VOLUME
         CLC   TRACEDVC,DISKTYPE   DO THE DEVICE TYPES MATCH
         BE    DISKOK
         PUT   DCBSYSP,MSG53
         MVI   RC,8                SET RETURN CODE USER ERROR
         LA    R1,QDSN             SET UP PARM LIST FOR SEND
         ST    R1,SENDPARM         BECAUSE INVALID DISK TYPE
         LA    R1,QUSERID
         ST    R1,SENDPARM+4
         LA    R1,DTYPMSG
         ST    R1,SENDPARM+8
         LA    R1,DTYPMSGL
         ST    R1,SENDPARM+12
         LA    R1,SENDPARM
         BAL   R14,SENDMESG
         B     GETNEXT
DISKOK   EQU   *
         LA    R1,TRACEDS1         POINT TO DSCB1 STORED IN TRACE
         USING IECSDSL1+44,R1      ONLY GET LAST 96 BYTES OF DSCB1
         DROP  R1
         BAL   R14,CHKSPACE        NOW CALC TOTAL SPACE REQUIRED
         ST    R1,TRACEDSZ         SAVE SIZE FOR LATER
         MVC   DSNAMEL,QDSN
         TM    QACTION,FLAGNNME    IS IT NEW NAME
         BZ    NOTNEWN
         MVC   DSNAMEL,QNEWDSN
NOTNEWN  EQU   *
         EJECT
*
* THERE ARE LOTS OF POSSIBLITIES INVOLVING DUPLICATE DATASETS,
* UNCATALOGUED DATASETS AND INCORRECT CATALOG ENTRIES WHICH ARE NOT
* CHECKED HERE. WE LEAVE IT TO FDR/JCL TO SPOT THE PROBLEMS. WE
* WILL ALLOCATE(& CATALOG) A DATASET IF IT DOES NOT EXIST ON THE VOLUME
* REQUIRED (THIS WILL FAIL IF IT EXISTS ANY WHERE ELSE, IT WILL GIVE A
* WARNING IF IT IS ALREADY CATLOGUED). IF THE DATASET DOES EXIST ON THE
* REQUIRED VOLUME AND IT IS BIG ENOUGH THEN WE WILL USE IT(IGNORING ANY
* CATALOG ENTRY) OTHERWISE WE WILL DELETE IT FIRST(IGNORING ANY CATALOG
* ENTRY) AND REALLOCATE IT WITHOUT RECATALOGING (THUS ASSUMING THAT IT
* IS ALREADY CATALOGUED IF REQUIRED. THIS WILL FAIL IF IT EXISTS
* ANY WHERE ELSE)
*
         MVC   VOLID,QVOLSER       GET DISK VOLSER
         OBTAIN DSCB1OB
         LTR   R15,R15
         BNZ   ALLOC               IF NOT THEN ALLOCATE/CATALOG ONE
         LA    R8,DSCB1
         USING IECSDSL1+44,R8      ONLY FIRST 96 FOR DSCB1
         CLI   DS1NOEPV,X'03'      IF <= 3 ONLY DSCB1 NEEDED
         BNH   CLCAVAIL
         MVC   CCHHR,DS1PTRDS      POINT TO NEXT DSCB
         OBTAIN DSCB3OB
         LTR   R15,R15
         BZ    CLCAVAIL            IF THIS FAILS THE VTOC IS CORRUPT
         MVI   DS1NOEPV,X'03'      ONLY 3 EXTENTS FOUND IGNORE REST
CLCAVAIL EQU   *
         DROP  R8
         LA    R1,DSCB1            POINT TO 236 BYTES CONTAINING DSCBS
         BAL   R14,CHKSPACE
         C     R1,TRACEDSZ         IS AVAIL SPACE ENOUGH
         BNL   JCLWRITE            YES THEN DONT NEED ALLOCATION
         OI    FLAG1,FLAGDELT      SET FLAG TO GEN. JCL FOR DELETE
ALLOC    EQU   *
         OI    FLAG1,FLAGALOC      SET FLAG TO GEN. JCL FOR ALLOCATE
         MVC   JCL22DSN,DSNAMEL
         MVC   JCL21VOL,QVOLSER
         L     R1,TRACEDSZ
         CVD   R1,PDSIZE
         UNPK  JCL25TRK,PDSIZE+5(3) INSERT REQUIRED SIZE INTO SPACE
         OI    JCL25TRK+5,X'F0'    CONVERT LAST DIGIT TO CHARACTER
         EJECT
*
* NOW WRITE OUT THE JCL FOR A JOB TO DELETE THE DATASET
*
JCLWRITE EQU   *
         MVZ   JCL2Q,HASHZ
         MVN   JCL2Q,HASHN
         MVC   JCL3USER,QUSERID   DIRECT PRINTOUT TO REQUESTOR
         ZR    R4                 UPDATE THE JOB NAME INDEX
         IC    R4,JCL1INDX        TO CYCLE ROUND 1-9,0-9,0-9 ...
         INCR  R4
         STC   R4,JCL1INDX        PUT IT BACK
         CLI   JCL1INDX,X'FA'     REACHED THE TOP YET ?
         BNE   INDEXOK
         MVI   JCL1INDX,C'0'      THEN GO BACK TO THE START
INDEXOK  EQU   *
         LA    R4,JCL1            JOBCARD,CONT,CONT,MAIN
         LA    R7,4               NUMBER OF CARDS
         BAL   R8,PUTFDR
         TM    FLAG1,FLAGDELT     DO WE NEED TO DELETE DATASET FIRST
         BZ    NODELETE
         LA    R4,JCL20           EXEC,DD,DISP,DSN
         LA    R7,4
         BAL   R8,PUTFDR
NODELETE EQU   *
         EJECT
*
* NOW RUN FDR TO RETURN THE DATASET
*
         MVC   JCL5VOL,QVOLSER     GET DISK VOLSER
         MVC   JCL6VOL,TRACETPE    GET TAPE VOL
         MVC   JCL7LBL,TRACELBL    GET TAPE LABEL
         MVC   JCL7DSN1,TRACELBL   BUILD TAPE DSN
         MVC   JCL7DSN2,TRACELBL
         LA    R4,FDR1
         USING FDRCNTL,R4
         MVI   FDR1ID,C'1'        INSERT CARD ID
         DROP  R4
         LA    R4,JCL4            EXEC,STEPLIB,DISK1,TAPE1,CONT,
         LA    R7,7               SYSPRINT,CONT
         BAL   R8,PUTFDR
         PUT   DCBFDR,JCL21       TARGET
         TM    FLAG1,FLAGALOC     DO WE NEED TO ALLOCATE DATASET
         BZ    ALCOLD             IF NOT THEN JUST DISP=OLD
         TM    FLAG1,FLAGDELT     HAVE WE PREVIOUSLY DELETED IT
         BZ    ALCCATLG           IF NOT THEN CATALOG IT AS WELL
         PUT   DCBFDR,JCL24       DISP=(NEW,KEEP,KEEP)
         B     ALCSPACE
ALCOLD   EQU   *
         PUT   DCBFDR,JCL24A      DISP=OLD
         B     ALCNOSPC
ALCCATLG EQU   *
         PUT   DCBFDR,JCL23       DISP=(NEW,CATLG,CATLG)
ALCSPACE EQU   *
         PUT   DCBFDR,JCL25       SPACE
ALCNOSPC EQU   *
         PUT   DCBFDR,JCL22       DSN
         LA    R4,JCL11           SYSIN,RESTORE,SELECT,CONT,TERMINATOR
         LA    R7,5
         BAL   R8,PUTFDR
         EJECT
*
*        NOW WRITE JCL FOR TRACE/ANALYSIS AFTER RUN OF FDR
*
         PUT   DCBFDR,JCL12       EXEC
*        PUT   DCBFDR,JCL12A ******* STEPLIB FOR TESTING ONLY ********
         LA    R4,JCL13           FDRLOG,SYSPRINT
         LA    R7,2
         BAL   R8,PUTFDR
GETNEXT  EQU   *
         OI    QACTION,FLAGDEL     FOR COMPLETENESS
         B     QLOOP               LOOK FOR NEXT RETURN REQUEST
*
ALLDONE  EQU   *
         CLOSE (DCBFDR)            CLOSE FDR/JCL FILE
         NI    FLAG2,X'FF'-FLGFDR  MARK AS CLOSED
         CLOSE (ACBTRACE)          CLOSE TRACE FILE
         LTR   R15,R15
         BNZ   ERRORVOC
         NI    FLAG2,X'FF'-FLGTRACE MARK AS CLOSED
         PUT   DCBSYSP,MSG07       MARK CONVERSION COMPLETE
         EJECT
*
* FREE ALL MEMORY USED FOR QUEUE
*
FINISH   EQU   *
         L     R5,CHAIN            POINT TO START OF CHAIN
FREEG1   EQU   *
         LTR   R1,R5               ZERO = END OF CHAIN
         BZ    ENDFREE             YES ITS ALL DONE
         L     R5,0(R1)            GET ADDRESS NEXT BUFFER
         FREEMAIN RU,LV=BLKSIZE+4,A=(1)
         B     FREEG1
ENDFREE  EQU   *
         ZERO  CHAIN,4
         CLOSE (DCBSYSP)           CLOSE MESSAGES FILE
         NI    FLAG2,X'FF'-FLGSYSP MARK AS CLOSED
         B     THEEND
         EJECT
*
* HERE IF QUEUE IS CORRUPT
*
CORRUPTQ EQU   *                   CORRUPTED FLAG
         PUT   DCBSYSP,MSG06       TOO CORRUPT TO CONTINUE
*
* HERE FOR ALL FAILURES
*
* FREE NEW QUEUE BUFFER AND CLOSE QUEUE
*
FAIL     EQU   *
         MVI   RC,16              SET RETURN CODE
         LT    R2,NEWQBUFF        GET ADDRESS OF NEW QUEUE BUFFER
         BZ    NONFREE
         FREEMAIN RU,LV=BLKSIZE,A=(2) FREE UP BUFFER AGAIN
NONFREE  EQU   *
         TM    FLAG2,FLGQUEUE     IS QUEUE STILL OPEN
         BZ    QNOTOPN
         CLOSE (DCBQUEUE,FREE)
QNOTOPN  EQU   *
         EJECT
*
* NOW WE MUST RELEASE LOCK ON QUEUE FILE
*
         TM    FLAG2,FLGENQ        IS QUEUE ENQUEUED STILL
         BZ    FAILDQ
         DEQ   (QNAME,RNAME,,SYSTEM)
FAILDQ   EQU   *
         EJECT
*
* REWIND AND CLOSE JCL FILE, AND CLOSE TRACE FILE
         TM    FLAG2,FLGFDR
         BZ    CLOSE3
* WE MUST REWIND DCBFDR HERE TO STOP JCL FROM BEING PROCESSED
         CLOSE (DCBFDR)
CLOSE3   EQU   *
         TM    FLAG2,FLGTRACE
         BZ    CLOSE4
         CLOSE (ACBTRACE)
CLOSE4   EQU   *
*
* FREEMAIN QUEUE STORAGE
*
         L     R5,CHAIN            POINT TO START OF CHAIN
FAILG1   EQU   *
         LTR   R1,R5               ZERO = END OF CHAIN
         BZ    FAILG2              YES ITS ALL DONE
         L     R5,0(R1)            GET ADDRESS OF NEXT BUFFER
         FREEMAIN RU,LV=BLKSIZE+4,A=(1)
         B     FAILG1
FAILG2   EQU   *
         EJECT
*
* FINALLY CLOSE MESSAGES FILE IF REQUIRED
*
         TM    FLAG2,FLGSYSP
         BZ    CLOSE1
         CLOSE (DCBSYSP)
CLOSE1   EQU   *
*
THEEND   EQU   *
         ZR    R10
         IC    R10,RC            COLLECT RETURN CODE
         CH    R10,=H'8'         ARE THERE ANY ERROR MESSAGES
         BL    NOWTO
         WTO   'RARRE99I **********************************************+
               ****************',ROUTCDE=1,DESC=3
WTOAGAIN EQU   *
         WTO   'RARRE99I IMPORTANT MESSAGES ON SYSPRINT - PLEASE SAVE O+
               UTPUT AND TAKE REQUIRED ACTION',ROUTCDE=1,DESC=7
         WTO   'RARRE99I **********************************************+
               ****************',ROUTCDE=1,DESC=7
         MVI   WTORECB,0          INITIALIZE ECB
         WTOR  'RARRE99I REPLY C TO CONTINUE OR R TO RESHOW MESSAGE',  +
               WTOREPLY,1,WTORECB,ROUTCDE=1
         WAIT  ECB=WTORECB,RELATED=WTOR
         CLI   WTOREPLY,C'C'      ARE WE TO CONTINUE
         BNE   WTOAGAIN           IF NOT REPEAT MESSAGE
NOWTO    EQU   *
         GOODBY RC=(10)
         EJECT
*
* SUBROUTINE TO TEST A DCB FOR OPEN AND ISSUE A MESSAGE IF NOT
* USES R0,R1
*
TESTOPEN EQU   *
         USING IHADCB,R5
         TM    DCBOFLGS,DCBOFOPN   OPENED ?
         BOR   R4                  YES
         MVC   MSG02DD,DCBDDNAM    YES SO WRITE A MESSAGE
         DROP  R5
         PUT   DCBSYSP,MSG02
         B     FAIL
         EJECT
*  ______________________
* × SUBROUTINE GETRECORD.×
*  ----------------------
* GET A RECORD FROM THE BUFFERS.
* RETURNS R6=POINTER TO NEW RECORD
*     R5 (BLOCKPOINTER) MUST BE PRESERVED BETWEEN CALLS
* USES R8
*
GETRECOR EQU   *
         ST    R14,SAVEDR14     SAVE RETURN ADDRESS.
         TM    FLAG1,FIRSTI     FIRST TIME ?
         BZ    GET001           NO, GET AN ORDINARY ADDRESS.
         LR    R8,R6            POINT TO PREVIOUS RECORD
         USING QUEUEMAP,R8
         INCR  R6,127           INCREMENT POINTER.
         TM    QEND,BLKEND      WAS IT THE LAST IN THE BLOCK
         BZ    GET002           IF NOT RETURN POINTER
         TM    QEND,QUEEND      IF SO HAVE WE RUN OFF END
         BO    CORRUPTQ
         DROP  R8
         B     GET001A
GET001   EQU   *
         LA    R5,CHAIN         YES, GET BEGINNING OF CHAIN.
         OI    FLAG1,FIRSTI     SET 'FIRST-TIME-DONE' FLAG.
GET001A  EQU   *
         L     R5,0(R5)         GET ADDRESS OF NEXT BIT.
         LA    R6,4(R5)         POINTS TO FIRST RECORD.
GET002   EQU   *
         L     R14,SAVEDR14     RESTORE RETURN ADDRESS.
         BR    R14
         EJECT
*
* SUBROUTINE PUTFDR TO WRITE A NUMBER OF RECORDS TO FDR/JCL FILE
* ENTRY WITH R4-> FIRST RECORD
*            R7 = NUMBER OF RECORDS
*
PUTFDR   EQU   *
         PUT   DCBFDR,(4)
         INCR  R4,80
         BCT   R7,PUTFDR
         BR    R8
*
*  ___________________
* × SUBROUTINE BINDEC ×
*  -------------------
*
*       ON ENTRY R1 CONTAINS NUMBER TO BE CONVERTED
*       ON EXIT R1 CONTAINS ADDRESS OF CONVERTED NUMBER
*
BINDEC   EQU   *
         CVD   R1,CPACK           CONVERT TO DECIMAL
         UNPK  CUNPK,CPACK        NOW TO CHARACTERS
         OI    CUNPK+15,X'F0'     CHANGE LAST DIGIT TO CHARACTER
         LA    R1,CUNPK           POINT TO NEW STRING
         BR    R14
         EJECT
*        _______________________
*        × SUBROUTINE SENDMESG ×
*        -----------------------
*
* A ROUTINE TO SEND A MESSAGE TO THE REQUESTOR USING OUR OWN SVC
*  ON ENTRY
*        R1 POINTS TO PARM LIST AS FOLLOWS
*          0 ->DSN                 DATASET NAME
*          4 ->USERID              REQUESTOR
*          8 ->MISSMSG             INFO MESSAGE
*         12 ->MISSMSGL            INFO MESSAGE LENGTH
*  RESTORES ALL REGISTERS BEFORE RETURN
*
SENDMESG EQU   *
         STM   R1,R5,GSAVE
         L     R2,0(R1)            POINT TO DSN
         CLI   0(R1),C' '          DON'T PROCESS IF BLANK
         BER   R14
         LA    R3,43(R2)           POINT TO END OF DSN
         LA    R4,44               MAX POSS LENGTH
SEND01   EQU   *
         CLI   0(R3),C' '          FIND LENGTH OF DSN
         BNE   SEND02
         DECR  R4
         BCT   R3,SEND01
SEND02   EQU   *
         BLANK SENDMSG+6,75        BLANK TARGET FOR MEESSAGE
         MVC   SENDMSG+6(44),0(R2) & MOVE IT
         LA    R5,SENDMSG+6(R4)    POINT TO END OF DSN
         MVI   0(R5),C'"'          INSERT TRAILING QUOTE
         L     R3,12(R1)           POINT TO LENGTH
         LH    R2,0(R3)            & LOAD IT
         DECR  R2                  FOR EXECUTE
         L     R3,8(R1)            POINT TO MESSAGE
         EX    R2,MOVESEND         MOVE MESSAGE TO TARGET
         LA    R4,8(R2,R4)         GET FINAL MEESSAGE LENGTH
         STC   R4,SENDMSG          & INSERT IT
         L     R2,4(R1)            POINT TO USERID
         MVC   SENDUSER,0(R2)      MVE IT
         LA    R1,SENDPTR
         SVC   241                 ISSUE NOTIFY SVC
         LM    R1,R5,GSAVE
         BR    R14
MOVESEND MVC   2(0,R5),0(R3)       MOVE MESSAGE
         EJECT
*
* _____________________
*× SUBROUTINE CHKSPACE ×
* ---------------------
*        CALCULATE SPACE ALLOCATED
*        R1 POINTS TO 236 BYTES CONTAINING DSCBS
* RETURN SPACE IN TRACKS IN R1
* ALL REGISTERS RESTORED ON RETURN
*
CHKSPACE EQU   *
         STM   R2,R9,GSAVE
         ZR    R9                  TOTALLING REG
         USING IECSDSL1+44,R1      MAP DSCB1
         ZR    R3
         IC    R3,DS1NOEPV         GET NO OF EXTENTS
         LA    R2,3                NO OF EXTENTS AT THIS LOCATION
         LA    R4,DS1EXT1          POINT TO EXTENT INFO
         CR    R3,R2               HAVE WE AS MANY AS THIS
         BH    CALC1               MORE THAN 3
         LR    R2,R3               IF NOT THEN LOOK AT LESS
CALC1    BAL   R7,CALCEXT          CALC THESE EXTENTS
         SH    R3,=H'3'            KNOCK 3 OFF THE RUNNING COUNT
         BNP   FINCALC             ALL DONE ?
         DROP  R1
         LA    R1,96(R1)           LOOK AT DSCB FMT3
         USING IECSDSL3,R1        MAP THIS DSCB
         LA    R2,4                NO EXTENTS AT THIS LOCATION
         LA    R4,DS3EXTNT         POINT TO EXTENT INFO
         CR    R3,R2               HAVE WE AS MANY AS THIS
         BH    CALC2               MORE THAN 4?
         LR    R2,R3               IF NOT THEN LOOK AT LESS
CALC2    BAL   R7,CALCEXT          CALC THESE EXTENTS
         SH    R3,=H'4'            KNOCK 4 OFF THE RUNNING COUNT
         BNP   FINCALC             ALL DONE ?
         LR    R2,R3               THIS MANY LEFT (MAX 9)
         LA    R4,DS3ADEXT         POINT TO EXTENT INFO
         DROP  R1
         BAL   R7,CALCEXT
FINCALC  EQU   *
         LR    R1,R9               RETURN SPACE IN R1
         LM    R2,R9,GSAVE
         BR    R14                 RETURN
         EJECT
*
* SUBROUTINE TO
*        CALCULATE SPACE FOR EACH  EXTENT
* ON ENTRY R2=NO OF EXTENT DESCRIPTIONS AT THIS POINT
*          R4-> THE FIRST EXTENT DESCRIPTION
*          R9= RUNNING TOTAL OF TRACKS FOR THIS DS
* USES R5,R6
*
CALCEXT  EQU   *
         ZR    R5
         ICM   R5,3,6(R4)        ENDING CYL ADDR
         ZR    R6
         ICM   R6,3,2(R4)         STARTING CYL ADDR
         SR    R5,R6
         MH    R5,TRKPCYL        DIFFERENCE IN TRACKS
         ICM   R6,3,8(R4)         ENDING TRACK ADDR
         AR    R5,R6
         ICM   R6,3,4(R4)         STARTING TRACK ADDR
         SR    R5,R6
         LA    R9,1(R9,R5)      TOTAL TRACKS
         INCR  R4,10               POINT TO NEXT EXTENT
         BCT   R2,CALCEXT     CALCULATE TRACKS FOR NEXT EXTENT
         BR    R7
         EJECT
*
* SUBROUTINE GETRACE
* GET A RECORD FORM THE TRACE
*
GETRACE  EQU   *
         ST    R14,SAVEDR14             SAVE RETURN ADDRESS.
         TM    FLAG3,FLAGSEQ            SEQUENTIAL SEARCH ?
         BO    GETT01                   IF SO SKIP POINT
         POINT RPL=RPLTRACE
         LTR   R15,R15
         BNZ   GETTEOF
         OI    FLAG3,FLAGSEQ            GET NEXT ONE SEQUENTIALLY
GETT01   EQU   *
         GET   RPL=RPLTRACE        GET A RECORD
         LTR   R15,R15
         BNZ   GETTEOF
         PRINT NOGEN
         SHOWCB RPL=RPLTRACE,AREA=FDBK,LENGTH=4,FIELDS=FDBK
         PRINT GEN
         LTR   R15,R15
         BNZ   ERRORVS
         L     R14,SAVEDR14
         BR    R14
         EJECT
GETTEOF  EQU   *
         ST    R15,SAVEDR14        SAVE RETURN CODE TEMPORARILY
         PRINT NOGEN
         SHOWCB RPL=RPLTRACE,AREA=FDBK,LENGTH=4,FIELDS=FDBK
         PRINT GEN
         LTR   R15,R15
         BNZ   ERRORVS
         ZR    R1
         IC    R1,FDBK+3           GET FEEDBACK CODE
         L     R15,SAVEDR14        RECOVER RETURN CODE
         CH    R15,=H'8'
         BNE   GETVSERR
         CH    R1,=H'4'            END OF FILE ?
         BE    NOTRACE
         CH    R1,=H'16'           RECORD NOT FOUND ?
         BE    NOTRACE
GETVSERR EQU   *
         LR    R1,R15              CONVERT RC TO DECIMAL
         BAL   R14,BINDEC
         MVC   MSG04RC,13(R1)      INSERT INTO MESSAGE
         ZR    R1
         IC    R1,FDBK+3           GET FEED BACK CODE
         BAL   R14,BINDEC          CONVERT TO DECIMAL
         MVC   MSG04FDB,13(R1)     INSERT INTO MESSAGE
         PUT   DCBSYSP,MSG04
         B     FAIL
         EJECT
*        ----------------------
*        × SUBROUTINE DEVCHAR ×
*        ----------------------
*
*        GET   DEVICE CHARACTERISTICS
*
DEVCHAR  EQU   *
         LA    R1,RBPTR
         DYNALLOC
* PROBLEM HERE WHAT DO WE DO WITH MOUNTABLE PACKS ?
* WE MAY REQUIRE MORE VOLUMES THAN THERE ARE AVAILABLE DRIVES
* FOR THE MOMENT WE WILL IGNORE THOSE REQUESTS
         LTR   R15,R15
         BNZ   DEVERR
         DEVTYPE DDDISK,DISKCHAR,DEVTAB,RPS
         BR    R14
*
* ISSUE DYNAMIC ALLOCATION ERROR MESSAGE
*
DEVERR   EQU   *
         STM   R2,R5,GSAVE
         UNPK  CUNPK(5),ERROR(3)   CONVERT ERROR CODE TO CHARS
         TR    CUNPK(4),HEXTAB-C'0' NOW TO PROPER EBCDIC
         MVC   MSG01ERR,CUNPK      MOVE TO MESSAGE
         UNPK  CUNPK(5),INFO(3)    CONVERT INFO CODE TO CHARS
         TR    CUNPK(4),HEXTAB-C'0'
         MVC   MSG01INF,CUNPK
         LR    R1,R15              GET RETURN CODE
         BAL   R14,BINDEC          TO DECIMAL
         MVC   MSG01RC,14(R1)      MOVE TO MESSAGE
         PUT   DCBSYSP,MSG01
         PUT   DCBSYSP,MSG08
         MVI   RC,12               SET RETURN CODE
         LM    R2,R5,GSAVE
         USING QUEUEMAP,R6
         LA    R1,QDSN             SET UP PARM LIST FOR SEND
         ST    R1,SENDPARM         BECAUSE UNABLE TO ALLOCATE VOLUME
         LA    R1,QUSERID
         ST    R1,SENDPARM+4
         LA    R1,UALCMSG
         ST    R1,SENDPARM+8
         LA    R1,UALCMSGL
         ST    R1,SENDPARM+12
         LA    R1,SENDPARM
         BAL   R14,SENDMESG
         DROP  R6
         B     GETNEXT             AND TRY FOR NEXT
         EJECT
*
*        HERE FOR VSAM FAILURES IN OPEN/CLOSE
*
ERRORVOC EQU   *
         ST    R15,SAVEDR14      SAVE RETURN CODE
         PRINT NOGEN
         SHOWCB ACB=ACBTRACE,AREA=FDBK,LENGTH=4,FIELDS=ERROR
         PRINT GEN
         LTR   R15,R15
         BNZ   ERRORVS
         L     R15,SAVEDR14      RESTORE RETURN CODE
         L     R0,FDBK           GET ERROR CODE
*
* HERE FOR VSAM FAILURES IN SHOWCB (AND OPEN/CLOSE EVENTUALLY)
*      ERROR CODE IN R0, RETURN CODE IN R15
*
ERRORVS  EQU   *
         LR    R1,R15            GET RETURN CODE
         BAL   R14,BINDEC        CONVERT TO DECIMAL
         MVC   MSG03RC,13(R1)    INSERT INTO MESSAGE
         LR    R1,R0             GET ERROR CODE
         BAL   R14,BINDEC        CONVERT TO DECIMAL
         MVC   MSG03ERR,13(R1)   INSERT INTO MESSAGE
         PUT   DCBSYSP,MSG03
         B     FAIL
         EJECT
         LTORG
         DROP  R12,R11
         EJECT
*
* DATA AREAS - BASE AND FLAGS
*
BASE2    DC    A(RARRETN+4096)
*
FLAG1    DC    X'00'
FLGHEAD  EQU   X'80'               WRITE HEADINGS ON SYSP
FLGJCL   EQU   X'40'               ON TO GENERATE JCL FOR FDR
FIRSTI   EQU   X'20'               FIRST TIME ON INPUT FLAG.
FLAGALOC EQU   X'02'               TO GEN ALLOC JCL
FLAGDELT EQU   X'01'               TO GEN DELETE JCL
*
FLAG2    DC    X'00'
FLGSYSP  EQU   X'80'               SYSPRINT OPEN
FLGFDR   EQU   X'40'               FDR/JCL FILE OPEN
FLGTRACE EQU   X'10'               TRACE FILE OPEN
FLGQUEUE EQU   X'04'               QUEUE FILE OPEN
FLGENQ   EQU   X'02'               QUEUE ENQUEUED
*
FLAG3    DC    X'00'
FLAGSEQ  EQU   X'01'               READ TRACE SEQUENTIALLY
*
RC       DC    X'00'               RETURN CODE
* VALUES       0 - NORMAL COMPLETION
*              4 - NO REQUESTS TO PROCESS
*              8 - AT LEAST ONE USER ERROR DETECTED
*             12 - AT LEAST ONE REQUEST FAILED
*             16 - SERIOUS ERROR ALL REQUESTS ABANDONED
         EJECT
*
*        MESSAGES
*
* COLS 1'ST LINE   0----+----1----+----2----+----3----+----4----+----51
*      2'ND   52--+----6----+----7----+----80
MSG01    DC    CL80'RARRE01I ERROR DURING DYNAMIC ALLOCATION OF DISK RC+
               =00, ERROR=0000, INFO=0000'
MSG01RC  EQU   MSG01+52,2
MSG01ERR EQU   MSG01+62,4
MSG01INF EQU   MSG01+73,4
MSG02    DC    CL80'RARRE02I UNABLE TO OPEN DATASET - DDNAME=ABCDEFGH'
MSG02DD  EQU   MSG02+41,8
MSG03    DC    CL80'RARRE03I VSAM ERROR R15 = 000 R0 = 000'
MSG03RC  EQU   MSG03+26,3
MSG03ERR EQU   MSG03+35,3
MSG04    DC    CL80'RARRE04I VSAM ERROR R15 = 000 FDBK = 000'
MSG04RC  EQU   MSG04+26,3
MSG04FDB EQU   MSG04+37,3
MSG05    DC    CL80'RARRE05I FDR CONTROL CARDS'
MSG06    DC    CL80'RARRE06I CORRUPT ENTRY ON QUEUE PROCESSING ENDS'
MSG07    DC    CL80'RARRE07I CONVERSION COMPLETE.'
MSG08    DC    CL80'RARRE08I - REQUEST IGNORED'
MSG09    DC    CL80'       DATASETNAME                                 +
                 DISK  USER   TAPE  LABEL'
MSG51    DC    CL80'RARRE51I NO RETURN REQUESTS TO PROCESS'
MSG52    DC    CL80'RARRE52I TRACE RECORD NOT FOUND TO MATCH QUEUE ENTR+
               Y - REQUEST IGNORED'
MSG53    DC    CL80'RARRE53I DEVICE TYPE INCOMPATIBLE WITH DATASET ORIG+
               IN - REQUEST IGNORED'
*
UALCMSGL DC    AL2(UALCLGTH)
UALCMSG  DC    C'ALLOCATION ERROR'
UALCLGTH EQU   *-UALCMSG
*
NTRCMSGL DC    AL2(NTRCLGTH)
NTRCMSG  DC    C'TRACE RECORD MISSING'
NTRCLGTH EQU   *-NTRCMSG
*
DTYPMSGL DC    AL2(DTYPLGTH)
DTYPMSG  DC    C'INCOMPATIBLE DEVICE TYPES'
DTYPLGTH EQU   *-DTYPMSG
         EJECT
*
*       CARD IMAGES FOR FDR JCL ETC
* NOTE: THE ORDER OF THESE STATEMENTS IS IMPORTANT
*
         PRINT OFF
HASHZ    DC    XL8'C0C0C0C0C0C0C0C0'
         PRINT ON
* COLS 1'ST LINE   0----+----1----+----2----+----3----+----4----+----51
*      2'ND   52--+----6----+----7----+----80
* JOBCARD GROUP
JCL1     DC    CL80'//JOBNAME0 JOB ,ARCHIVE, **ARCHIVE GENERATED JOB'
JCL1INDX EQU   JCL1+9
JCL2     DC    CL80'//        MSGLEVEL=(1,1),PASSWORD=????????,'
JCL2Q    EQU   JCL2+34,8
JCL3     DC    CL80'//        USER=USERIDX,MSGCLASS=A'
JCL3A    DC    CL80'//*MAIN USER=USERIDX'
JCL3USER EQU   JCL3A+13,7
* DELETE GROUP
JCL20    DC    CL80'//NULL    EXEC PGM=IEFBR14  ** DELETE DATASET'
JCL21    DC    CL80'//TARGET  DD VOL=SER=VOLSER,UNIT=SYSDA,'
JCL21VOL EQU   JCL21+21,6
JCL21A   DC    CL80'//         DISP=(OLD,DELETE,DELETE),'
JCL22    DC    CL80'//         DSN='
JCL22DSN EQU   JCL22+15,44
* FDR GROUP 1
JCL4     DC    CL80'//RETURN EXEC PGM=FDRDSF'
JCL4S    DC    CL80'//STEPLIB DD DSN=SYS2.FDRCPK,DISP=SHR'
JCL5     DC    CL80'//DISK1   DD UNIT=SYSDA,DISP=SHR,VOL=SER=VOLSER'
JCL5VOL  EQU   JCL5+41,6
JCL6     DC    CL80'//TAPE1   DD UNIT=TAPE62,DISP=OLD,VOL=SER=VOLSER,'
JCL6VOL  EQU   JCL6+42,6
JCL7     DC    CL80'//           LABEL=(0000,SL),DSN=ARC0000.AAA0000'
JCL7LBL  EQU   JCL7+20,4
JCL7DSN1 EQU   JCL7+36,4
JCL7DSN2 EQU   JCL7+44,4
JCL9     DC    CL80'//SYSPRINT DD DISP=(MOD,PASS),UNIT=VIO,DSN=&&PRT,'
JCL10    DC    CL80'//         DCB=(LRECL=121,BLKSIZE=6050,RECFM=FBA)'
* FOR ALLOCATE NEW DATASET
JCL23    DC    CL80'//         DISP=(NEW,CATLG,CATLG),'
* FOR RE-ALLOCATE OLD DATASET USING EXISTING CATALOG ENTRY (IF ANY)
JCL24    DC    CL80'//         DISP=(NEW,KEEP,KEEP),'
* FOR USING EXISTING DATASET
JCL24A   DC    CL80'//         DISP=OLD,'
* NOTE SECONDARY ALLOCATION/DIRECTORY SPACE OVERWRITTEN BY FDR
JCL25    DC    CL80'//         SPACE=(TRK,(000000,10)),'
JCL25TRK EQU   JCL25+23,6
* FDR GROUP 2
JCL11    DC    CL80'//SYSIN DD *'
* NOTE THE FORMAT OF THE FDR CARDS MUST BE REFELECTED BY RARTRACE CODE
FDR1     DC    CL80'  RESTORE TYPE=DSF'
FDR2     DC    CL80' S DSN='
FDR3     DC    CL80'* NEWNAME='
FDR4     DC    CL80'/*'
*
JCL12    DC    CL80'//ANALYSIS EXEC PGM=RARTRACE,COND=EVEN'
* NOTE STEPLIB *************** TESTING ONLY *************************
* JCL12A DC    CL80'//STEPLIB DD DSN=A012006.ARCHIVE.LOAD,DISP=SHR'
* ANALYSIS GROUP
JCL13    DC    CL80'//FDRLOG     DD DISP=OLD,DSN=&&PRT'
JCL15    DC    CL80'//SYSPRINT   DD SYSOUT=*'
         PRINT OFF
HASHN    DC    XL8'0101010101010101'
         PRINT ON
         EJECT
*
*        DYNAMIC ALLOCATION PARAMETERS
*
         DS    0F
RBPTR    DC    X'80',AL3(RB)
RB       DC    X'14'
DYNVERB  DC    X'01'               ALLOC ON DSNAME
DYNFLG1  DC    X'00'
DYNFLG2  DC    X'00'
ERROR    DC    H'0'
INFO     DC    H'0'
LISTPTR  DC    A(DYNLIST)
         DC    2F'0'
DYNLIST EQU    *
TP55     DC    A(TU55)
TP4      DC    A(TU4)
TP7      DC    A(TU7)
TPA      DC    A(TUA)
TP10     DC    A(TU10)
TP15     DC    X'80',AL3(TU15)
*
TU55     DC    X'0055',X'0001'     DDNAME RETURN
         DC    X'0008'
DDDISK   DS    CL8
TU4      DC    X'0004',X'0001',X'0001'
         DC    X'04'               NEW
TU7      DC    X'0007',X'0000'
TUA      DC    X'000A',X'0001',X'0003'
         DC    X'000000'
TU10     DC    X'0010',X'0001',X'0006'
VOLSER   DS    CL6
TU15     DC    X'0015',X'0001',X'0005',C'SYSDA'
         EJECT
*
*        GENERAL WORK AREAS AND DATA
*
QNAME    DC    CL8'ARCHIVE'
RNAME    DC    CL8'QUEUE   '
CHAIN    DC    A(0)      BEGINNING OF THE INPUT CHAIN.
NEWQBUFF DC    A(0)      ADDRESS OF BUFFER FOR NEW QUEUE
SAVEDR14 DS    F         SAVED RETURN ADDRESS.
GSAVE    DS    16F          GENERAL SAVE AREA
SENDPARM DS    4A           PARAMETERS FOR SENMESG SUBROUTINE
SENDUSER DS    CL7          USERID FOR SVC SEND
SENDMSG  DS    0CL81        MESSAGE WORK AREA FOR SVC
         DS    X            LENGTH OF MESSAGE FOR SVC
         DC    CL80'RTN "'  MESSAGE FOR SVC
SENDPTR  DC    A(SENDUSER)  POINT TO USERID FOR SVC
         DC    A(SENDMSG)   POINT TO MESSAGE
BLOCKADR DC    F'1'         POINTS TO FIRST BLOCK.
NUMRETN  DS    F            NUMBER OF RETURNS TO PROCESS
TRCBUFFP DS    F            POINTER TO TRACE RECORD BUFFER
HEXTAB   DC    C'0123456789ABCDEF'
DSCB1    DS    XL96
DSCB3    DS    XL140
DSCB1OB  CAMLST SEARCH,DSNAMEL,VOLID,DSCB1
DSCB3OB  CAMLST SEEK,CCHHR,VOLID,DSCB3
CCHHR    DS    5C              CYL/TRACK/RECORD INFO.
DSNAMEL  DC    CL44' '
VOLID    DC    CL6' '
*
DISKCHAR DS    0F               6 WORDS FOR DEVCHAR
DISKTYPE DS    F         UCB TYPE
MAXBLOCK DS    F         MAX BLOCK SIZE
NUMCYLS  DS    H         NUMBER OF CYLINDERS ON VOLUME
TRKPCYL  DS    H         TRACKS PER CYLINDER
BYTSPTRK DS    H         BYTES PER TRACK
         DS    H         OTHER DATA
         DS    2F                  ETC.
*
CPACK    DS    D
CUNPK    DS    CL16
*
PDSIZE   DS    D
TRACEDSZ DS    F         SAVE ORIGINAL SIZE OF DS
FDBK     DS    F         FEEDBACK FROM VSAM
WTORECB  DS    F                  ECB FOR WTO REPLY
WTOREPLY DS    C                  MESSAGE FROM WTOR
KEYDSN   DS    CL44      KEY FOR SEARCH OF TRACE FILE
         EJECT
*
* THE DCB'S, ACB'S AND RPL'S
*
         PRINT  NOGEN
DCBQUEUE DCB   DDNAME=QUEUE,DSORG=PS,MACRF=(RP,WP),RECFM=F,            *
               LRECL=BLKSIZE,BLKSIZE=BLKSIZE,EODAD=CORRUPTQ
DCBFDR   DCB   DDNAME=FDRJCL,DSORG=PS,MACRF=(PM),RECFM=FB,             *
               LRECL=80,BLKSIZE=3120
DCBSYSP  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FB,           *
               EROPT=ACC,LRECL=80,BLKSIZE=3120
ACBTRACE ACB   DDNAME=TRACE
RPLTRACE RPL   ACB=ACBTRACE,AREALEN=321,RECLEN=321,ARG=KEYDSN,         *
               OPTCD=LOC,AREA=TRCBUFFP
         PRINT GEN
         EJECT
*
*        MAP QUEUE TRACE & GET FLAGS
*
         PRINT  GEN
         RARMACRO
         PRINT NOGEN
         IECSDSL1 (1,3)            MAP DSCB1 & DSCB3
         DCBD    DSORG=PS,DEVD=DA
         END
