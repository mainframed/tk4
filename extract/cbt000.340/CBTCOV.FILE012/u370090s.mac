         TITLE 'RECORD COMPRESS/EXPAND SUB-ROUTINE'
         ISEQ  73,80
WSQUASH  CSECT
         SPACE 2
***********************************************************************
         SPACE 3
*   RRRRRR     EEEEEEE     CCCCC      OOOOO     RRRRRR     DDDDD      *
*   R     R    E          C     C    O     O    R     R    D    D     *
*   R     R    E          C          O     O    R     R    D     D    *
*   R     R    E          C          O     O    R     R    D     D    *
*   RRRRRR     EEEE       C          O     O    RRRRRR     D     D    *
*   RRR        EEEE       C          O     O    RRR        D     D    *
*   R  R       E          C          O     O    R  R       D     D    *
*   R   R      E          C          O     O    R   R      D     D    *
*   R    R     E          C     C    O     O    R    R     D    D     *
*   R     R    EEEEEEE     CCCCC      OOOOO     R     R    DDDDD      *
         SPACE 3
*       CCC    OO    M   M   PPP    RRR    EEEEE    SSS     SSS       *
*      C      O  O   MM MM   P  P   R  R   E       S   S   S   S      *
*     C       O  O   M M M   P  P   R  R   E       S       S          *
*     C       O  O   M M M   PPP    RRR    EEE      SSS     SSS       *
*     C       O  O   M   M   P      RR     E           S       S      *
*      C      O  O   M   M   P      R R    E       S   S   S   S      *
*       CCC    OO    M   M   P      R  R   EEEEE    SSS     SSS       *
         SPACE 4
*             RRR    OO   U  U  TTTTT  III  N   N  EEEEE              *
*             R  R  O  O  U  U    T     I   NN  N  E                  *
*             RRR   O  O  U  U    T     I   N N N  EEE                *
*             R R   O  O  U  U    T     I   N  NN  E                  *
*             R  R   OO    UU     T    III  N   N  EEEEE              *
         SPACE 3
***********************************************************************
         EJECT
*  THIS SUB-ROUTINE OPERATES THE TECHNIQUE DEVISED BY MIKE STONE AT
*        WEST SUSSEX COUNTY COUNCIL, FOR ELIMINATION OF SOME REDUNDANT
*        BYTES IN RECORDS, IN ORDER TO SAVE SPACE ON DISCS, TAPES, ETC.
         SPACE 2
*  IT HAS THREE ENTRY POINTS, AS FOLLOWS:
*
*   1.   "WSQUASH1", TO EXPAND A RECORD FROM ITS COMPRESSED FORM INTO
*              THE USER'S WORK AREA
*   2.   "WSQUASH2", TO COMPRESS A RECORD FROM THE USER'S AREA, READY
*              TO WRITE TO AN OUTPUT FILE
*   3.   "WSQUASH3", AS FOR "2", BUT WITH SPECIAL PROVISION TO TRY
*              AND MAKE THE RECORD FOR OUTPUT THE SAME LENGTH AS WHEN
*              IT WAS EXPANDED (FOR "UPDATE-IN-PLACE" PROCESSING)
         SPACE 3
*  THE TECHNIQUE OF SQUASHING IS TO ELIMINATE THE AREAS WHERE TWO OR
*        MORE CONSECUTIVE BYTES ARE FOUND TO CONTAIN EITHER
*        X'40' (BLANK) OR X'00' (NORMALLY, THE LEADING DIGITS OF A
*        PACKED DECIMAL FIELD).
*        FOR CONSECUTIVE BLANKS, IT PUTS INTO THE FIRST BYTE A SINGLE
*        CHARACTER, OF THE FORM X'B?', WHERE "?" IS *TWO LESS* (IN HEX)
*        THEN THE NUMBER OF BYTES SQUASHED OUT.  (THE MAX. FOR ANY ONE
*        STRING OF BLANKS IS THEREFORE 17, WHICH CAN BE REDUCED TO THE
*        SINGLE BYTE X'BF'.)
*        FOR CONSECUTIVE ZEROS, IT PUTS IN THE SINGLE CHARACTER X'A?',
*        WHERE "?" IS *8 MORE* (IN HEX) THAN THE NUMBER OF BYTES
*        SQUASHED OUT.  (THE MAX. FOR ANY ONE STRING OF ZEROS IS THUS 7
*        AND THIS IS REDUCED TO X'AF'.)
*
*  CLEARLY, HEX CODES 'AA'-'BF' INCLUSIVE MUST NEVER APPEAR AS GENUINE
*        DATE IN THE USER'S RECORDS, SINCE THEY ARE USED IN THE
*        EXPANSION ACTIVITY OF THIS ROUTINE TO PROPAGATE BLANKS OR
*        ZEROS.  HEX 'A0' LIKEWISE MUST NOT OCCUR - SEE NEXT PARA.
*
*  PADDING.
*     WHEN A CALL IS MADE TO COMPRESS DATA, THE USER MAY REQUEST THAT
*        A GIVEN NUMBER OF "PAD" CHARACTERS BE APPENDED TO THE RECORD.
*        THIS IS TO ALLOW FOR HIS RECORDS TO GROW IN SIGNIFICANT DATA
*        DURING BISAM/QISAM UPDATES-IN-PLACE, WITHOUT FORCING THEM
*        INTO AN OVERFLOW AREA BECAUSE THEIR LENGTH IS NOW DIFFERENT.
*        THE "PAD" CHARACTER USED IS X'A0';  THESE ARE NEVER PUT INTO
*        A USER'S AREA ON EXPANSION OF A RECORD; THEY ARE GENERATED
*        AS APPROPRIATE (SEE ABOVE, DIFFERENCE BETWEEN ENTRY 2 AND 3)
*        ON COMPRESSION.
         EJECT
*  PARAMETERS REQUIRED ARE:-
*    (A) ALL CASES:
*              1. POINTER TO RECORD FOR ACTION
*              2. POINTER TO AREA FOR RECEIVING THE RESULTANT DATA
*           ***NOTE. BOTH THESE POINTERS MUST BE TO A FULL-WORD BOUNDRY
*              FOR THE R.D.W. OF A VARIABLE-LENGTH RECORD.  FOR PARM.1,
*              THE INCOMING RECORD LENGTH MUST ACTUALLY BE IN THE
*              R.D.W.; FOR PARM.2, THIS ROUTINE WILL INSERT THE NEW
*              RECORD LENGTH INTO THE R.D.W. SLOT.
*     (B) FOR ENTRY 2 & ENTRY 3:
*              3. POINTER TO A HALF-WORD CONTAINING NUMBER OF "PAD"
*                 CHARACTERS TO BE APPENDED (FOR ENTRY 3 THIS MUST BE
*                 SUPPLIED, BUT WILL ONLY BE USED IF THE RECORD IS,
*                 AFTER SQUASHING BUT BEFORE PADDING, ALREADY LONGER
*                 THAN WHEN IT WAS FIRST READ.  OTHERWISE IT IS PADDED
*                 MERELY ENOUGH TO TAKE IT UP TO ITS ORIGINAL LENGTH.)
         SPACE 4
*   REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         ENTRY WSQUASH1,WSQUASH2,WSQUASH3
         SPACE
*  NOTE: IF A USER ISSUES A CALL TO "WSQUASH", IT OPERATES AS IF A
*              CALL TO "WSQUASH1"
         SPACE 2
*  ENTRY 1 - TO EXPAND A RECORD
*
         USING *,R15
WSQUASH1 NI    TWOWAY+1,X'00'      SET TWO-WAY BRANCH OFF FOR EXPAND
         MVI   TABLE,X'00'         SET TABLE NOT TO FIND ANY X'00' OR
         MVI   TABLE+64,X'00'       X'40' CHARACTERS IN SCAN
         B     LINKAGE
*  CONSTANT TO IDENTIFY THIS MODULE IN A DUMP
         DC    CL8'WSQUASH'
         SPACE 2
*  ENTRY 2 - TO COMPRESS (& OPTIONAL PAD) IRRESPECTIVE OF PREVIOUS
*              INPUT RECORD LENGTH
*
         USING *,R15
WSQUASH2 MVI   QFORCLEN+1,X'F0'    SET BR. TO OMIT SAME LENTH ATTEMPT
         BAL   R15,WSQUASH3+4
         SPACE 2
*  ENTRY 3 - COMPRESS, TRY TO FORCE LENGTH=LAST INPUT
*
         USING *,R15
WSQUASH3 MVI   QFORCLEN+1,X'00'    CLEAR BR., FOR PAD TO SAME LENTH
         OI    TWOWAY+1,X'F0'      SET TWO-WAY BRANCH ON FOR COMPRESS
         MVI   TABLE,X'A9'         SET TABLE TO FIND X'00' & X'40'
         MVI   TABLE+64,X'AF'
         SPACE
*******************************
* NOTE TO MEDDLING PROGRAMMERS:  **  B E W A R E **
*******************************
*    THE "BM" INSTRN. JUST BELOW TESTS THE CONDITION CODE SET BY EITHER
*        THE "NI" OR THE "OI" JUST ABOVE  ...... SO WATCH IT!
         SPACE 2
LINKAGE  STM   R14,R12,12(R13)
         BALR  R12,0
         USING *,R12
*
*  PICK UP PARAMETERS
*
         LM    R5,R7,0(R1)
         LR    R10,R6              HOLD OUTPUT AREA POINTER IN R10
         LH    R4,0(R5)            RECORD LENGTH OF INPUT
*                                  Q EXPAND OR COMPRESS - TEST C.C.
         BM    *+8                   FROM "NI" OR "OI" ABOVE
         STH   R4,LRECL            EXPAND - HOLD INCOMING LRECL
         SH    R4,H4               CALC. ACTUAL DATA LENGTH
         LA    R8,3(R5,R4)         SET R8 TO END OF INPUT
         LA    R5,4(R5)            POINT TO
         LA    R6,4(R6)              ACTUAL DATA LOCNS.
         SR    R2,R2
         SR    R9,R9
         SR    R1,R1
         EJECT
*  ALL PREPARED FOR LOOP           R4 HAS DATA LENGTH
*                                  R5 HAS INPUT DATA POINTER
*                                  R6 HAS OUTPUT AREA POINTER
*                                  R7 HAS (IF COMPRESS CALLED) POINTER
*                                    TO HALF-WORD "PAD" REQUIREMENT
*                                  R8 HAS END-OF-INPUT POINTER
*                                  R9 IS EMPTY
*                                  R2 IS EMPTY
*                                  R1 IS EMPTY
*                                  R10=R6, FOR INSERT OF NEW LRECL
         SPACE 3
*  LOOP STARTS HERE
NEXTSCAN BCTR  R4,0                REDUCE OVERALL LENGTH BY 1
         LA    R3,255              THEN PREPARE LENGTH OF NEXT CHUNK
         CR    R3,R4
         BL    DOTRT                - EITHER 256, OR REMAINING LENGTH
         LR    R3,R4                        IF LESS
DOTRT    EX    R3,TRANSTES         LOOK FOR SIGNIFICANT CHARACTER
         BZ    MOVELOT             IF COND.CODE = 0, FULL LENGTH HAS
*                                   BEEN SCANNED WITHOUT FINDING WHAT
*                                     WE WANT
         SPACE 2
*  HERE THE ROUTINE DIVERGES FOR EXPANSION OR COMPRESSION
*
TWOWAY   NOP   COMPRESS            SET TO BR. IF COMPRESSION CALLED
         EJECT
*  EXPANSION SECTION MAIN ACTION
*
EXPAND   LR    R3,R1               SAVE POINTER TO CODE BYTE IN RECORD
         SR    R1,R5               GET LENGTH UP TO IT
         EX    R1,MOVE              AND MOVE UNCHANGED SECTION
         AR    R6,R1                 INCREMENT O/P AREA PAST THIS
         CLI   0(R3),X'A0'         Q PAD CHARACTER FOUND
         BE    WAYOUT               IF SO, FINISHED EXPANSION
         LR    R1,R2               R1, HAS COUNT(-1) OF POSNS. TO FILL
         MVI   0(R6),X'40'         PREPARE TO FILL WITH BLANKS
         TM    0(R3),X'10'         Q BLANKS TO FILL - 'A?' OR 'B?'
         BO    FILLBLNK
         MVI   0(R6),X'00'          NO, FILL WITH ZEROS
FILLBLNK SR    R3,R5               DEVELOP INCREMENT UP INPUT AREA
         EX    R1,MOVEPAD      (PLACES 1 TOO MANY, BUT POINTER UPDATE
         B     UPIOS                     CORRECTS IT FOR NEXT CHAR.)
         EJECT
*  COMPRESS SECTION MAIN ACTION
*
COMPRESS CR    R1,R8              Q WAS THE CHAR. FOUND BY "TRT" THE
         BNL   MOVELOT             LAST BYTE OF THE RECORD - IF SO STOP
         MVC   CSTRING+1(1),0(R1)  PUT THE CHAR. INTO "CLI" BELOW
         LR    R3,R1               SAVE POINTER TO PLACE FOUND
         CLC   0(1,R3),1(R3)        AND SEE IF THERE IS AT LEAST ONE
         BNE   RECYCLE              FURTHER BYTE WITH THE SAME IN IT
*
*  THERE ARE AT LEAST 2 BLANKS OR 2 ZEROS TOGETHER.  R2 HOLDS THE
*        PRELIMINARY CODE VALUE OF X'A9' FOR ZEROS, OR X'AF' FOR
*        BLANKS - WHICH WILL IMMEDIATELY HAVE X'01' ADDED AS AN
*        ADEQUATE INDICATION OF THE FIRST 2 FOUND.
*
*        THE RESULT OF THE FOLLOWING SMALL LOOP WILL BE THAT R2 WILL
*              HAVE FROM X'AA'-'AF' FOR 2-7 BYTES OF X'00'
*               AND FROM X'B0'-'BF' FOR 2-17 BYTES OF X'40'
*
         IC    R9,TABLE2-X'A9'(R2) GET A MAX.COUNT IN R9 - 6 FOR ZEROS
*                                                         16 FOR BLANKS
SQUEEZE  LA    R2,1(R2)            COUNT THE STRING LENGTH
         LA    R3,1(R3)            UP TO NEXT BYTE INPUT STRING
CSTRING  CLI   1(R3),X'00'         Q SAME AGAIN ("IMMEDIATE" CHAR.
         BNE   PUTSQIZ                                 ×INSERTED ABOVE)
         CR    R3,R8               Q UP TO END OF INPUT
         BE    PUTSQIZ              YES
         BCT   R9,SQUEEZE
PUTSQIZ  STC   R2,0(R1)            REPLACE 1ST 00 OR 40 BY RESULT BYTE
*
*   NOW PREPARE TO MOVE A CHUNK OF THE INPUT RECORD TO THE OUTPUT AREA
*
RECYCLE  SR    R1,R5               LENGTH OF MOVE
         SR    R3,R5               LENGTH OF INPUT DEALT WITH
*    AND MOVE IT
DOMOVE   EX    R1,MOVE
         EJECT
*  EXPAND AND COMPRESS COME TOGETHER AGAIN HERE
*  MOVE UP THE INPUT AND OUTPUT AREAS
*
UPIOS    LA    R5,1(R3,R5)
         LA    R6,1(R1,R6)
         SR    R4,R3               Q HAVE WE FINISHED THE FULL LENGTH
         BNZ   NEXTSCAN
*
WAYOUT   LR    R8,R6               YES; CALCULATE NEW O/P LRECL
         SR    R8,R10
         TM    TWOWAY+1,X'F0'      Q EXPAND OR COMPRESS
         BNO   PUTNEWL
         MVI   0(R6),X'A0'         ATTCH 1 PAD CHAR. IN CASE REQD.BELOW
QFORCLEN BC    0,FULLPAD            COMPRESS - BR.IF NO TRY TO FORCE
*                                      SAME LENGTH RECORD
         SH    R8,LRECL              ELSE, HOW DOES NEWLY CALCULATED
         BP    FULLPAD                 LRECL CF.WITH ORIGINAL?
*                                   NEW IS LONG, GIVE FULL PADDING
         BZ    NOPADS                SAME AS IT WAS, NO PAD TO DO
         LPR   R8,R8                 NEW SHORTER, PAD UP TO OLD LENGTH
         B     PUTPADS
*
FULLPAD  LH    R8,0(R7)            PICK UP USER NO.OF PADS REQD.
         LTR   R8,R8               Q NONE WANTED
         BZ    NOPADS
PUTPADS  EX    R8,MOVEPAD     (PUTS 2 TOO MANY, BUT FINAL LRECL IS OK)
NOPADS   AR    R8,R6               UP R8 TO END O/P AREA + PADDING
         LA    R8,1(R8)            ENSURE LRECL WILL BE EVEN
         SRL   R8,1
         SLL   R8,1
         SR    R8,R10               & CALC. REVISED LRECL OF O/P
PUTNEWL  STH   R8,0(R10)           REVISED LENGTH TO OUTPUT AREA
         XC    2(2,R10),2(R10)     ZEROISE LOWER HALF OF RDW
         LM    R14,R12,12(R13)     ***  EXIT   **
         SR    R15,R15
         BR    R14
         SPACE 2
*   INSTRUCTIONS OUTSIDE MAIN CODE
MOVELOT  LR    R1,R3               COPY LENGTH INTO R1
         B     DOMOVE
         SPACE 3
*   CONSTANTS AND AREAS
TABLE2   DC    X'06000004000010'   ) NOTE: ONLY FIRST & LAST BYTES THIS
H4       EQU   TABLE2+2            )  TABLE EXTRACTED DIRECTLY,
LRECL    EQU   TABLE2+4            )  THUS EQUATES USE SPARE BYTES
TABLE    DC    X'A9',63X'00',X'AF',95X'00'
         DC    X'FF',9X'00'        FOR X'A0'
         DC    X'010203040506'     FOR X'AA'-'AF'
         DC    X'0102030405060708090A0B0C0D0E0F10'  FOR 'B0'-'BF'
         DC    64X'00'             REMAINDER OF TABLE
*   EXECUTED INSTRUCTIONS
TRANSTES TRT   0(0,R5),TABLE
MOVE     MVC   0(0,R6),0(R5)
MOVEPAD  MVC   1(0,R6),0(R6)
         END
