         TITLE 'MAGNETIC TAPE ANALYSIS PROGRAM - MODULE MAGANAL'
********** REQUIRES MACRO #START   *************
*
*
* THIS MODULE MAY BE USED TO TEST MAGNETIC TAPE REELS OR DRIVES.
* ALL I/O ERRORS ENCOUNTERED ARE COUNTED AND DISPLAYED ON THE REPORT.
*
* BEFORE THE MODULE MAY BE USED, A MODULE 'IGG019Z9' IS REQUIRED IN
* SYS1.SVCLIB. THE CODING FOR THIS MODULE IS -
* IGG019Z9 START
*          L  10,36(4) PICK UP EXLST ADDRESS FROM DCB
*          L  11,0(10) PICK UP ERROR COUNT FROM EXLST
*          LA 11,1(11) INCREMENT BY UNITY
*          ST 11,0(10) SET TO NEW VALUE
*          BR 14 RETURN
*          END
*
*
* TO INVOKE (A SPECIFIC UNIT ADDRESS (EG 281) MAY REPLACE 'TAPE') -
* //STEPNAME EXEC PGM=MAGANAL
* //SYSPRINT DD SYSOUT=A
* //SYSUT1 DD VOL=SER=DUMMY,DISP=OLD,UNIT=(TAPE,,DEFER)
*
*
* METHOD OF OPERATION -
* 1. ISSUE WTOR 'STATE VOL SER TO BE ANALYZED'
* 2. IF REPLY IS 'END' GO TO 10. ELSE THIS IS THE VOL SER OF REEL
* 3. ISSUE MOUNT VIA OS (IEC101A) FOR THIS REEL.
* 4. OPEN REEL IGNORING ALL LABELS (WHICH WILL BE WIPED OUT).
* 5. WRITE A TAPE MARK AT START OF TAPE (STOPS READBACK).
* 6. WRITE FOOT-LONG RECORDS (PGM IS FOR 1600 BPI - BUT EASILY CHANGED)
*      UNTIL REFLECTIVE MARKER HIT. ALL I/O ERRORS ARE INDICATED ON THE
*      REPORT AS THEY HAPPEN.
* 7. READ TAPE BACKWARDS UNTIL EOF AGAIN INDICATING I/O ERRORS AS THEY
*      OCCUR (THIS PHASE GIVES BUM RESULTS AT THE MOMENT).
* 8. UNLOAD THE TAPE VIA OS (IEC202E).
* 9 GO BACK TO 1.
* 10. END EXECUTION.
*
MAGANAL  START
* AUTHOR - A.G.SIBBALD   CAV LTD., GILLINGHAM, KENT.
         #START
         TITLE 'MODULE INITIALIZATION'
         OPEN  (SYSPRINT,(OUTPUT))
         TM    SYSPRINT+48,X'10'
         BZ    HA10 ERROR - OPEN DIDNT WORK
         RDJFCB (SYSUT1,(INPUT))
         CLI   JFCBAREA,0 HAS JFCB BEEN READ
         BE    HA20 ERROR - JFCB NOT READ
         NI    JFCBAREA+66,X'A0' TURN OFF LABEL BITS
         OI    JFCBAREA+66,X'10' BLP
         MVC   JFCBAREA(44),=CL44'TAPE.TO.BE.ANALYZED'
         GETMAIN VC,LA=CORELEN,A=COREADD
         LTR   R15,R15 TEST RETURN CODE
         BNZ   HA30 ERROR - CANT GET CORE
         MVC   WRITECHP+1(3),COREADD+1 SET ADDRESS TO WRITE FROM
         MVC   WRITECHP+6(2),CORELEN+2 SET LENGTH OF WRITE
         L     R15,COREADD PICK UP ADD OF BUFFER
         AH    R15,CORELEN+2 POINT PAST BUFFER
         BCTR  R15,0 POINT AT LAST BYTE
         ST    R15,DOUBLEWD GET ALIGNED
         MVC   READCHP+1(3),DOUBLEWD+1 SET READ BACKWARDS CCW
         MVC   READCHP+6(2),CORELEN+2 SET LENGTH OF READ
         TITLE 'OBTAIN OPERATOR INTERVENTION'
AA10     XC    REPLYECB,REPLYECB
         MVC   REPLY,=CL6' '
         WTOR  'STATE VOL SER TO BE ANALYZED',REPLY,6,REPLYECB
         WAIT  ECB=REPLYECB
         CLC   REPLY,=CL6'END'
         BE    GA10
         CLI   REPLY+1,C'A'
         BL    AA10
         CLC   REPLY,=CL6'OK'
         BE    AA10
         TITLE 'START NEXT VOLUME'
* ******** OUTPUT
         SPACE 2
         MVC   JFCBAREA+118(6),REPLY SET VOL SER NO REQD
         MVC   H1VOL,REPLY SET VOL SER IN H1
         MVI   JFCBAREA+69,1
         OPEN  (SYSUT1,(OUTPUT)),TYPE=J
         TM    SYSUT1+48,X'10'
         BZ    HA20 OPEN DIDNT WORK
         OI    SYSUT1+48,X'80' REQUEST A TAPE MARK
         CLOSE (SYSUT1,LEAVE)
         MVI   JFCBAREA+69,2
         NI    SYSUT1+48,X'7F'
         OPEN  (SYSUT1,(OUTIN)),TYPE=J
         TM    SYSUT1+48,X'10'
         BZ    HA20 ERROR - OPEN DIDNT WORK
         LA    R5,1 GOING UP THE TAPE
         SR    R6,R6 CURRENT POSN ON TAPE (FEET)
         LA    R9,WRITEIOB POINT AT OUTPUT IOB
         XC    TPLEN,TPLEN RESET LENGTH OF TAPE
         SR    R4,R4 INDICATE OUTPUT
         MVC   H1DIRN,=CL6'OUTPUT'
         B     BA10 GO START PROCESSING
         SPACE 2
* ******** INPUT
         SPACE 2
AB10     LA    R4,4  INDICATE INPUT
         LA    R9,READIOB
         MVC   H1DIRN,=CL6' INPUT'
         LH    R5,=H'-1' GOING DOWN THE TAPE
         B     BA10 GO START PROCESSING
         TITLE 'START PROCESSING'
BA10     SR    R10,R10 ZERO THE COUNTS
         SR    R11,R11
         SR    R7,R7 RESET TOTAL VOLUME FIGURES
         SR    R8,R8
         L     R12,BLKCNT
         MVI   CA10+1,0 RESET NOP
         STH   R6,TPLEN SAVE LENGTH OF TAPE
         NI    SYSUT1+48,X'33' TURN OFF UNWANTED BITS
         ST    R9,SYSUTECB ENSURE ALIGNMENT
         TITLE 'DO SOME I/O'
CA10     NOP   CA20 BRANCH IF NO OUTSTANDING I/O
         BAL   R2,CB10
CA20     MVI   CA10+1,0 RESET TO NOP
         WAIT  ECB=SYSUTECB
         A     R10,EXITLIST COUNT THE TEMP ERRORS
         CLI   SYSUTECB,X'7F' TEST ECB COMPLETION CODE
         BE    CA30 A PERMANENT ERROR HAS NOT OCCURRED
         TM    12(R9),X'01' TEST UNIT EXCEPTION
         BO    FA10 END OF REEL
         LA    R11,1(R11) COUNT THE PERM ERRORS
CA30     AR    R6,R5 KEEP RUNNING TOTAL OF ALL I/OS
         BCT   R12,CA10 COUNT NO OF I/OS
         BAL   R2,CB10
         MVI   CA10+1,X'F0' ALLOW I/O
         BAL   R12,DA10 GO WRITE THE LINE
         L     R12,BLKCNT RESET FOR NEXT INTERVAL
         B     CA10
         SPACE 2
* ******** DO SOME I/O
         SPACE 2
CB10     XC    SYSUTECB,SYSUTECB RESET THE ECB
         XC    EXITLIST(4),EXITLIST RESET ERROR COUNT
         EXCP  (9) PROCESS A BLOCK
         BR    R2 RETURN
         TITLE 'BUILD A LINE'
DA10     LA    R1,L1BLOBS
         LTR   R11,R11
         BNZ   DA20
         LTR   R10,R10
         BNZ   DA30
         BR    R12 RETURN
DA20     LR    R15,R11
         MVI   DB20+1,C'P'
         BAL   R14,DB10
DA30     LTR   R15,R10
         BNP   DA40
         MVI   DB20+1,C'T'
         BAL   R14,DB10
DA40     MVC   L1INTVL,=X'402020202120'
         CVD   R6,DOUBLEWD CONVERT POSN ON TAPE
         ED    L1INTVL,DOUBLEWD+5
         AR    R7,R10 ACC TOTAL VOLUME FIGURES
         AR    R8,R11
         SR    R10,R10
         SR    R11,R11
         B     EA10 GO PRINT LINE
         EJECT
DB10     LA    R0,L1BLOBS+99
         SR    R0,R15
         BNP   DB40 LINE FULL
         CR    R15,R0
         BNH   DB20
         LR    R15,R0
DB20     MVI   0(R1),C'$'
         SH    R15,=H'2'
         BM    DB30 FINISHED
         EX    R15,DB50 MOVE REST IN
         LA    R1,2(R15,R1)
         B     DB40 GO RETURN
DB30     LA    R1,1(R1)
DB40     BR    R14
DB50     MVC   1(0,R1),0(R1)
         TITLE 'PRINT A LINE'
EA10     SP    LINECNT,=P'1'
         BP    EA20
         AP    PAGECNT,=P'1'
         MVC   H1PAGE,=X'402020202120'
         ED    H1PAGE,PAGECNT
         PUT   SYSPRINT,H1
         PUT   SYSPRINT,H2
         ZAP   LINECNT,=P'55'
         MVI   L1,C'0'
EA20     PUT   SYSPRINT,L1
         MVI   L1,C' '
         MVC   L1+1(132),L1
         BR    R12 RETURN
         TITLE 'END OF VOLUME'
FA10     BAL   R12,DA10 GO WRITE THE LINE
         CVD   R7,DOUBLEWD
         MVC   L2TEMP,=X'402020202120'
         ED    L2TEMP,DOUBLEWD+5
         CVD   R8,DOUBLEWD
         MVC   L2PERM,=X'402020202120'
         ED    L2PERM,DOUBLEWD+5
         AH    R6,TPLEN CALC ACTUAL LEN OF TAPE
         CVD   R6,DOUBLEWD
         MVC   L2LEN,=X'402020202120'
         ED    L2LEN,DOUBLEWD+5
         MVC   L1(133),L2
         BAL   R12,EA10 GO WRITE THE LINE
         SP    LINECNT,LINECNT
         B     *+4(R4) TEST IF INPUT OR OUTPUT
         B     AB10 GO DO INPUT ANALYSIS
         CLOSE (SYSUT1)
         B     AA10
         TITLE 'END OF PROCESSING'
GA10     CLOSE (SYSPRINT)
         FREEPOOL SYSPRINT
         FREEMAIN V,A=COREADD
GA20     L     R13,4(R13) PREPARE TO RETURN
         RETURN (14,12)
         TITLE 'ERROR CONDITIONS'
HA10     WTO   'MAGANAL-01 SYSPRINT CANNOT BE OPENED'
         B     GA20
HA20     WTO   'MAGANAL-02 SYSUT1 CANNOT BE PROCESSED'
         B     GA20
HA30     WTO   'MAGANAL-03 INSUFFICIENT CORE'
         B     GA20
         TITLE 'CONSTANTS AND AREAS'
DOUBLEWD DC    D'0'
WRITECHP DC    0D'0',B'00000001',3X'00',B'00100000',3X'00'
READCHP  DC    0D'0',B'00001100',7X'00'
WRITEIOB DC    0F'0',X'02',4X'00',AL3(SYSUTECB),9X'00',AL3(WRITECHP)
         DC    AL4(SYSUT1),XL6'01',8X'00'
READIOB  DC    0F'0',X'02',4X'00',AL3(SYSUTECB),9X'00',AL3(READCHP)
         DC    AL4(SYSUT1),4X'00',H'-1',8X'00'
REPLYECB DC    F'0'
SYSUTECB DC    F'0'
BLKCNT   DC    F'1'
CORELEN  DC    2A(18560)
COREADD  DC    A(0,0)
TPLEN    DC    H'0'
PAGECNT  DC    PL3'0'
LINECNT  DC    PL2'0'
REPLY    DC    CL6' '
H1       DC    CL10'1'
H1DIRN   DC    CL6' ',CL20' ANALYSIS OF VOLUME '
H1VOL    DC    CL6' '
         DC    CL72' '
         DC    CL4'PAGE'
H1PAGE   DC    CL6' '
         DC    CL10' '
H2       DC    CL13'0 POSN (FEET)'
         DC    CL20' '
         DC    CL100'ERRORS'
L1       DC    CL3' '
L1INTVL  DC    CL6' '
         DC    CL4' '
         DC    CL20' '
L1BLOBS  DC    CL100' '
L2       DC    CL22'0 THIS TAPE IS APPROX.'
L2LEN    DC    CL6' ',CL11' FEET LONG.'
L2TEMP   DC    CL6' ',CL25' TEMP ERRORS ENCOUNTERED.'
L2PERM   DC    CL6' ',CL25' PERM ERRORS ENCOUNTERED.'
         DC    CL35' '
JFCBAREA DC    44F'0'
EXITLIST DC    F'0',X'87',AL3(JFCBAREA)
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=(E),EODAD=FA10,XENDA=Z9,            C
               EXLST=EXITLIST,DSORG=PS,IOBAD=WRITEIOB,DEVD=TA
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,                      C
               RECFM=FBA,LRECL=133,BLKSIZE=133
         END
