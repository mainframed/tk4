*        GENERAL DESCRIPTION
*        THE UT31 UTILITY PROGRAM INVOKES THE LINKAGE EDITOR PROGRAM
*        AFTER ENQUEUING ON THE SYSLMOD DATA SET. THIS MEANS THAT WHILE
*        THIS PROGRAM IS EXECUTING EXCLUSIVE CONTROL IS RETAINED OF THE
*        DATA SET SPECIFIED ON THE SYSLMOD DD CARD, WHATEVER THE
*        DISPOSITION SPECIFIED FOR THE DATA SET. THEREFORE DISP=SHR CAN
*        BE USED. THIS PREVENTS DELAYS IN ALLOCATION.
*        DATA SET REQUIREMENTS
*        THE DATA SETS REQUIRED AND THE PARAMETERS ARE EXACTLY AS
*        DESCRIBED IN THE IBM PUBLICATION C28-6538 LINKAGE EDITOR AN&
*        LOADER PROGRAM.
*        NOTES
*        NO EXAMPLES ARE GIVEN BECAUSE THE PROGRAM IS USUALLY EXECUTED
*        AS PART OF A CATALOGUED PROCEDURE, WHERE THE PROGRAM NAME UT31
*        IS SUBSTITUTED FOR IEWL.
         TITLE 'INVOKE THE LINKAGE EDITOR'
UT31     CSECT
         IN    12,PARM=YES
         L     2,0(1)              ADDRESS OF PARM LIST
         L     4,16                CVT
         L     4,0(4)              MAGIC WORD
         L     4,4(4)              TCB
         L     4,12(4)             TIOT
         LA    5,0                 CLEAR REG 5
         LA    4,24(4)             FIRST DDNAME
A10      EQU   *
         CLC   0(4,4),=F'0'
         BE    A20                 END OF TIOT
         CLC   4(8,4),=C'SYSLMOD '
         BE    A30                 SYSLMOD FOUND
         IC    5,0(4)
         LA    4,0(5,4)
         B     A10                 GO LOOK AT NEXT ENTRY
A20      EQU   *
         WTO   'UT31 SYSLMOD DD CARD MISSING',ROUTCDE=11
         ABEND 444
A30      EQU   *
         LA    4,4(4)              POINT TO DDNAME
         CALL  ZZENQDEQ,((4),ENQNAME) ENQ ON SYLMOD DSNAME
         LINK  EP=IEWL,PARAM=((2)),VL=1
         ST    15,SAVE15           SAVE LINKAGE EDITOR RETURN CODE
         LA    4,0
         CALL  ZZENQDEQ,((4),ENQNAME)  DEQ
         L     15,SAVE15        RESTORE LINKAGE EDITOR RETURN CODE
         L     13,4(13)
         MVI   16(13),X'FF'
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
SAVE15   DS    F
ENQNAME  DS    CL44
         LTORG
         END
