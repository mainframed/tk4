000000 IDENTIFICATION DIVISION.
000010 PROGRAM-ID.    YEBJOBCD.
000020 ENVIRONMENT DIVISION.
000030 CONFIGURATION SECTION.
000040 SOURCE-COMPUTER. IBM-360-H50.
000050 OBJECT-COMPUTER. IBM-360-H50.
000060 INPUT-OUTPUT SECTION.
000070 FILE-CONTROL.
000080     SELECT INPUT-STREAM  ASSIGN TO UT-S-HASPIN.
000090     SELECT OUTPUT-STREAM ASSIGN TO UT-S-HASPOUT.
000100 DATA DIVISION.
000110 FILE SECTION.
000120 FD  INPUT-STREAM
000130     RECORD CONTAINS 80 CHARACTERS
000140         BLOCK CONTAINS 0 CHARACTERS
000150     LABEL RECORDS ARE OMITTED
000160     RECORDING MODE IS F.
000170 01  INPUT-CARD.
000180     03  FIRST-FOUR      PIC X(4).
000190     03  DATA-FIELD.
000200         05  1-DIGIT     PIC 9.
000210         05  COL-6       PIC X.
000220         05  COL-7       PIC X.
000230         05  COL-8       PIC X.
000240         05  COL-9       PIC X.
000250         05  COL-10      PIC X.
000260         05  FILLER      PIC XX.
000270         05  FILLER-1    PIC X(8).
000280         05  COL-21      PIC X.
000290         05  COL-22      PIC X.
000300         05  COL-23      PIC X.
000310         05  COL-24      PIC X.
000320         05  FILLER-2    PIC X(47).
000330     03  FILLER          PIC X.
000340     03  SEQFLD          PIC 9(8).
000350     66  FIRST-EIGHT RENAMES FIRST-FOUR THRU COL-8.
000360     66  2-DIGITS RENAMES 1-DIGIT THRU COL-6.
000370     66  3-DIGITS RENAMES 1-DIGIT THRU COL-7.
000380     66  EXEC-A   RENAMES COL-7   THRU COL-21.
000390     66  EXEC-B   RENAMES COL-8   THRU COL-22.
000400     66  EXEC-C   RENAMES COL-9   THRU COL-23.
000410     66  EXEC-D   RENAMES COL-10  THRU COL-24.
000420     66  DD-FIELD RENAMES FILLER-1 THRU FILLER-2.
000430 FD  OUTPUT-STREAM
000440     RECORD CONTAINS 80 CHARACTERS
000450     LABEL RECORDS ARE OMITTED
000460     RECORDING MODE IS F.
000470 01  OUTPUT-CARD         PIC X(80).
000480 WORKING-STORAGE SECTION.
000490 01  JOB-CARD.
000500     03  FILLER          PIC X(8)    VALUE '//YGB2SG'.
000510     03  JOB-NO          PIC 99.
000520     03  FILLER          PIC X(22)   VALUE
000530         ' JOB (9001,880,15,10),'.
000540     03  FILLER          PIC X       VALUE QUOTE.
000550     03  RELEASE-NO      PIC X(8).
000560     03  FILLER          PIC X(8)    VALUE ' STEP SG'.
000570     03  STEP-NO         PIC 99.
000580     03  FILLER          PIC X       VALUE QUOTE.
000590     03  FILLER          PIC X(7)    VALUE ',CLASS='.
000600     03  JOB-CLASS       PIC X.
000610     03  FILLER          PIC X(12)   VALUE SPACES.
000620     03  JOBSEQ          PIC 9(8).
000630 01  PARM-STORE.
000640     03  RELEASE-NO-IN   PIC X(8)    VALUE SPACES.
000650     03  ASEM-CLASS      PIC X       VALUE 'V'.
000660     03  LINK-CLASS      PIC X       VALUE 'W'.
000670     03  COPY-CLASS      PIC X       VALUE 'X'.
000680     03  REST-CLASS      PIC X       VALUE 'Y'.
000690 01  PARM-TABLE REDEFINES PARM-STORE.
000700     03  STORE-BYTE      PIC X       OCCURS 12   INDEXED BY S.
000710 01  COMP-STORE.
000720     03  COMP-STORE-3.
000730       04  JOB-NO-S      PIC 999.
000740       04  FILLER REDEFINES JOB-NO-S.
000750         05  H-TEN       PIC 99.
000760         05  FILLER REDEFINES H-TEN.
000770           06  HUNDRED   PIC X.
000780           06  TENS      PIC X.
000790         05  UNITS       PIC X.
000800     03  EXEC-STORE.
000810       04  EXEC-PGM      PIC X(9).
000820       04  PROG-ID       PIC X(6).
000830     66  TENS-AND-UNITS RENAMES TENS THRU UNITS.
000840 01  LETTER.
000850     03  FILLER          PIC X.
000860     03  100-LETTER      PIC A.
000870 01  LETTER-COMP REDEFINES LETTER.
000880     03  COMP-LETTER     PIC 9(4)    COMP.
000890 01  SEQ                 PIC 9(8)    COMP-3      VALUE ZERO.
000900 LINKAGE SECTION.
000910 01  PARM.
000920     03  PARM-LENGTH     PIC 9(4)    COMP.
000930     03  PARM-FIELD.
000940         05  PARM-BYTE   PIC X       OCCURS 16   INDEXED BY P.
000950 PROCEDURE DIVISION USING PARM.
000960     OPEN INPUT INPUT-STREAM.
000970     OPEN OUTPUT OUTPUT-STREAM.
000980     SET P TO 1.
000990     SET S TO 1.
001000 READ-PARM.
001010     IF PARM-BYTE (P) = ',' GO TO NEW-FIELD.
001020     IF P > PARM-LENGTH GO TO PARM-END.
001030     MOVE PARM-BYTE (P) TO STORE-BYTE (S).
001040     SET P UP BY 1.
001050     IF S < 8 SET S UP BY 1.
001060     GO TO READ-PARM.
001070 NEW-FIELD.
001080     IF S < 9 SET S TO 9 ELSE SET S UP BY 1.
001090     SET P UP BY 1.
001100     GO TO READ-PARM.
001110 PARM-END.
001120     MOVE RELEASE-NO-IN TO RELEASE-NO.
001130 READ-CARD.
001140     READ INPUT-STREAM AT END GO TO END-OF-INPUT.
001150     IF FIRST-FOUR NOT = '//SG' GO TO DD-CARD.
001160     MOVE ZERO TO JOB-NO-S.
001170     EXAMINE DATA-FIELD TALLYING UNTIL FIRST SPACE.
001180     IF TALLY = 1 MOVE 1-DIGIT TO UNITS.
001190     IF TALLY = 2 MOVE 2-DIGITS TO TENS-AND-UNITS.
001200     IF TALLY = 3 MOVE 3-DIGITS TO JOB-NO-S.
001210     EXAMINE DATA-FIELD TALLYING UNTIL FIRST 'E'.
001220     IF TALLY = 2 MOVE EXEC-A TO EXEC-STORE.
001230     IF TALLY = 3 MOVE EXEC-B TO EXEC-STORE.
001240     IF TALLY = 4 MOVE EXEC-C TO EXEC-STORE.
001250     IF TALLY = 5 MOVE EXEC-D TO EXEC-STORE.
001260     IF EXEC-PGM NOT = 'EXEC PGM=' GO TO WRITE-CARD.
001270     IF PROG-ID = 'ASMBLR' MOVE ASEM-CLASS TO JOB-CLASS
001280     ELSE
001290     IF PROG-ID = 'IEBCOP' MOVE COPY-CLASS TO JOB-CLASS
001300     ELSE
001310     IF PROG-ID = 'IEWL,C' OR 'IEWL,P'
001320         MOVE LINK-CLASS TO JOB-CLASS
001330         ELSE MOVE REST-CLASS TO JOB-CLASS.
001340     IF JOB-NO-S < 100 GO TO WRITE-JOB-CARD.
001350         IF JOB-NO-S >  99 AND < 190 MOVE 183 TO COMP-LETTER.
001360         IF JOB-NO-S > 189 AND < 280 MOVE 190 TO COMP-LETTER.
001370         IF JOB-NO-S > 279 AND < 360 MOVE 198 TO COMP-LETTER.
001380         ADD H-TEN TO COMP-LETTER.
001390     MOVE 100-LETTER TO TENS.
001400 WRITE-JOB-CARD.
001410         MOVE JOB-NO-S TO JOB-NO, STEP-NO.
001420     ADD 10 TO SEQ.
001430     MOVE SEQ TO JOBSEQ.
001440     WRITE OUTPUT-CARD FROM JOB-CARD.
001450     GO TO WRITE-CARD.
001460 DD-CARD.
001470     IF (FIRST-EIGHT = '//SYSUT3' OR '//SYSUT4')
001480         AND PROG-ID = 'IEBCOP'
001490         MOVE 'UNIT=WORK,SPACE=(CYL,(1,1))' TO DD-FIELD.
001500 WRITE-CARD.
001510     ADD 10 TO SEQ.
001520     MOVE SEQ TO SEQFLD.
001530     WRITE OUTPUT-CARD FROM INPUT-CARD.
001540     GO TO READ-CARD.
001550 END-OF-INPUT.
001560     MOVE '/*EOF' TO OUTPUT-CARD.
001570     WRITE OUTPUT-CARD.
001580     CLOSE INPUT-STREAM.
001590     CLOSE OUTPUT-STREAM.
001600     STOP RUN.
