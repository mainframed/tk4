***  FUNCTION - THIS PROGRAM PRODUCES A REPORT ON DISK ARM ACTIVITY ON
***  ALL D/A DEVICES MONITORED BY THE HOOVER 'TRACE' PROGRAM.
**  RECORD TYPES 3,4 AND 9 ARE INSPECTED.
**  ANY OTHER RECORD TYPE IS IGNORED.
**  CONTROL CARDS SPECIFY THE DRIVE ADDRESSES FOR WHICH A REPORT IS
**  REQUIRED.  THE FORMAT OF THE CONTROL CARD IS:-
***      POS   LENGTH                  FIELD
***       1       3                    EBCDIC UNIT ADDR
***       5       2                    'V=' VOL SER KEYWORD
***       7       6                    EITHER : 6 CHAR VOL SER NO
***                                      OR - 'IGNORE' - MEANING DO
***                                          NOT PRINT VOL SER NO
***                                    OR - 'FINDIT' - MEANING GET VOL
***                                          SER NO OF PACK CURRENTLY
***                                          ON THIS DRIVE ADDR
***       14      2                    'D=' DEVICE TYPE KEYWORD
***       16      4                    DEVICE TYPE E.G. 2314
***       21      1                    REPORT TYPE - 1 OR 2 - IF BLANK
***                                          OR INVALID THEN REPORT
***                                          TYPE 1 IS GIVEN AS DEFAULT
**
**       VERSION 1 APL 1972
**       G.R. FREETH.    HOOVER(UK) LTD.
         TITLE 'MAIN ENTRY'
SEEK     CSECT
         HSAVE
         OPEN  (OUT,OUTPUT,CTL,,SUMMDCB,OUTPUT)
         MVC   REP1COD1(8),NOP1        SAVE CODING
         MVC   REP1COD2(28),NOP2       SAVE CODING
         PUT   SUMMDCB,SUMMHD1
         PUT   SUMMDCB,SUMMHD2
         MVC   SUMMAREA,SUMMAREA-1
         MVI   SUMMAREA,C'-'
 TITLE 'HOUSE-KEEPING'
NEXT1    GET   CTL,SELECT              READ SELECTION RECORD
         BAL   LINKREG,INIT            INIT REQUEST TYPE ETC
         OPEN  (IN,)                    OPEN I/P
         ZAP   LCT,=P'60'              SET LINE COUNT
         GETMAIN EC,LV=1500,A=WORK      RESERVE CORE
         GETMAIN VC,LA=RANGE,A=ANSWER   GET CORE FOR TABLE
         FREEMAIN E,LV=1500,A=WORK     FREE RESERVED CORE
         L     TABEND,ANSWER            INIT
         S     TABEND,EIGHT            TABLE
         A     TABEND,ANSWER+4          ADDRESSES
         L     TABLAST,ANSWER
         MVC   0(8,TABLAST),MODEL
         XC    TOTAL,TOTAL             ZEROISE TOTAL
         L     INDEX,EIGHT              SET BRANCH INDEX
         SR    OVERFLOW,OVERFLOW
         BAL   LINKREG,READ             GET ELEMENT FROM RECORD TYPE 4
         USING ENTRY,TABSTRT
  TITLE 'READ INPUT FILE AND BUILD IN-CORE TABLE'
MAINLOOP BAL   LINKREG,GET             GET ELEMENT FROM RECORD TYPE 4
LOOP1    CH    THIS,FROM                TEST CYL ADDRESSES
         BE    LAB1
NOP1     CH    THIS,TO
          BE   LAB2
TRYNEXT  BXLE  TABSTRT,INDEX,LOOP1      TRY NEXT TABLE ENTRY
         CR    TABSTRT,TABEND          END OF TABLE ?
         BNH   NEWENTRY                NO - ENTER NEW ONE
         A     OVERFLOW,ONE            INCREMENT OVERFLOW
         B     MAINLOOP                 LOOP
LAB1     CH    NEXT,TO                 COMPARE CYL ADDRESSES
         BE    INCR
         B     TRYNEXT
LAB2     CH    NEXT,FROM
         BE    INCR
          B    TRYNEXT
INCR     L     WORKREG,COUNT
         A     WORKREG,ONE
         ST    WORKREG,COUNT
         L     1,TOTAL
         A     1,ONE
         ST    1,TOTAL
         B     MAINLOOP
NEWENTRY STH   THIS,FROM
         STH   NEXT,TO
         MVC   COUNT,ONE
         L     1,TOTAL
         A     1,ONE
         ST    1,TOTAL
         LR    TABLAST,TABSTRT
         B     MAINLOOP
  TITLE 'SORT TABLE AND PRODUCE CYL TO CYL REPORT'
EOINP    ST    OVERFLOW,OFLOSTOR
         CLOSE (IN,REREAD)
         FREEPOOL IN
         CLC   TOTAL,ZERO
         BZ    NOTHING
         BAL   LINKREG,SORT
         L     TABSTRT,ANSWER
PRTLOOP  CR    TABSTRT,TABLAST
         BH    EOTABLE
**       FROM CYL ADDR TO PRTAR
         LH    WORKREG,FROM
         CVD   WORKREG,DWORD
         MVC   PRTAR+PFROM(8),MASK
         ED    PRTAR+PFROM(8),DWORD+4
**       TO CYL ADDR TO PRTAR
         LH    WORKREG,TO
         CVD   WORKREG,DWORD
         MVC   PRTAR+PTO(8),MASK
         ED    PRTAR+PTO(8),DWORD+4
**       NO OF SEEKS TO PRTAR
         L     WORKREG,COUNT
         LTR   WORKREG,WORKREG
         BZ    GOTMODEL
         CVD   WORKREG,DWORD
         MVC   PRTAR+PCOUNT(8),MASK
         ED    PRTAR+PCOUNT(8),DWORD+4
**       %AGE OF TOTAL SEEKS TO PRTAR
         SRDA  WORKREG,32
         M     WORKREG,TENTHOU
         D     WORKREG,TOTAL
         CVD   OVERFLOW,DWORD
         MVC   PRTAR+PCENT(9),PCENTMSK
         ED    PRTAR+PCENT(9),DWORD+4
**       NO OF CYLS TRAVELLED TO PRTAR
         BAL   LINKREG,CYLS
         CVD   WORKREG,DWORD
         MVC   PRTAR+PCYLNO(8),MASK
         ED    PRTAR+PCYLNO(8),DWORD+4
**       SEEK TIME TO PRTAR
         BAL   LINKREG,SEEKTIME
**       TOTAL CYLS MOVED TO PRTAR
         SRDA  WORKREG,32
         M     WORKREG,COUNT
         CVD   OVERFLOW,DWORD
         MVC   PRTAR+PTOTCYL(8),MASK
         ED    PRTAR+PTOTCYL(8),DWORD+4
**        INCR TOTAL CYLS
         A     OVERFLOW,TOTCYLS
         ST    OVERFLOW,TOTCYLS
**       TEST PRT OVERFLOW
         CP    LCT,P50
         BL     NOHEAD
PRTFIRST B     NOHD0
         PUT   OUT,HD0
NOHD0    PUT   OUT,HD1
         MVI   PRTFIRST+1,X'00'
         SP    LCT,LCT
         MVI   PRTAR,C'-'
NOHEAD   PUT   OUT,PRTAR
         AP    LCT,PONE
         MVC   PRTAR,PRTAR-1
GOTMODEL LA    TABSTRT,8(TABSTRT)
         B     PRTLOOP
**       FORCE LOWER CYL ADDR FIRST
CYLS     CLC   FROM(2),TO
         BL    REVERSE
         LH    WORKREG,FROM
         SH    WORKREG,TO
         BR    LINKREG
REVERSE  LH    WORKREG,TO
         SH    WORKREG,FROM
         BR    LINKREG
  TITLE 'PRODUCE TOTAL STATISTICS'
**       END OF TABLE - NOW COMPUTE TOTALS
**       TOTAL SEEKS
EOTABLE  L     1,TOTAL
         CVD   1,DWORD
         MVC   PRTAR+PCOUNT(8),MASK
         ED    PRTAR+PCOUNT(8),DWORD+4
         MVC   SUMMAREA+PTOTSEEK(8),PRTAR+PCOUNT
**       TOTAL CYLS MOVED
         L     WORKREG,TOTCYLS
         CVD   WORKREG,DWORD
         MVC   PRTAR+PTOTCYL(8),MASK
         ED    PRTAR+PTOTCYL(8),DWORD+4
         MVC   PRTAR(L'TOTS),TOTS
**       TOTAL SEEK TIME
         L     WORKREG,TOTIME
         CVD   WORKREG,DWORD
         MVC   PRTAR+PTIME(10),MASKX
         ED    PRTAR+PTIME(10),DWORD+3
         MVC   SUMMAREA+PSEEKTM(10),PRTAR+PTIME
         PUT   OUT,PRTAR
         MVC   PRTAR,PRTAR-1
**       TOTAL EXCPS
         L     WORKREG,EXCPS
         LTR   WORKREG,WORKREG
         BZ    NOTHING
         CVD   WORKREG,DWORD
         MVC   PRTAR+PCOUNT(8),MASK
         ED    PRTAR+PCOUNT(8),DWORD+4
         MVC   SUMMAREA+PTOTEXCP(8),PRTAR+PCOUNT
         MVC   PRTAR(L'EXCPMSG),EXCPMSG
         BAL   LINKREG,PRT
**       ACTIVITY %AGE
         L     OVERFLOW,TOTAL
         M     WORKREG,TENTHOU
         D     WORKREG,EXCPS
         CVD   OVERFLOW,DWORD
         MVC   PRTAR+PCOUNT-1(9),PCENTMSK
         ED    PRTAR+PCOUNT-1(9),DWORD+4
         MVC   SUMMAREA+PACTV(9),PRTAR+PCOUNT-1
         MVC   PRTAR(L'ACTVMSG),ACTVMSG
         MVI   PRTAR+PCOUNT+9,C'%'
         BAL   LINKREG,PRT
**       AVERAGE EXCP RATE
         CLC   TIME,ZERO
         BE    NORT9
         L     WORKREG,EXCPS
         SRDA  WORKREG,32
         M     WORKREG,HUNDRED
         D     WORKREG,TIME
         CVD   OVERFLOW,DWORD
         MVC   PRTAR+PCOUNT-1(9),PCENTMSK
         ED    PRTAR+PCOUNT-1(9),DWORD+4
         MVC   SUMMAREA+PEXCPRAT(9),PRTAR+PCOUNT-1
NORT9    EQU   *
         MVC   PRTAR(L'AVEXCP),AVEXCP
         BAL   LINKREG,PRT
**       AVERAGE SEEK DISTANCE
         L     WORKREG,TOTCYLS
         SRDA  WORKREG,32
         M     WORKREG,HUNDRED
         D     WORKREG,TOTAL
         CVD   OVERFLOW,DWORD
         MVC   PRTAR+PCOUNT-1(9),PCENTMSK
         ED    PRTAR+PCOUNT-1(9),DWORD+4
         MVC   SUMMAREA+PAVSEEK(9),PRTAR+PCOUNT-1
         MVC   PRTAR(L'AVSEEK),AVSEEK
         MVC   PRTAR+PCOUNT+9(4),=C'CYLS'
         BAL   LINKREG,PRT
         MVC   PRTAR,PRTAR-1
NOTHING  EQU   *
         FREEMAIN V,A=ANSWER
         PUT   SUMMDCB,SUMMAREA
         MVC   SUMMAREA,SUMMAREA-1
         L     WORKREG,OFLOSTOR
         LTR   WORKREG,WORKREG
         BZ    NEXT1
         CVD   WORKREG,DWORD
         MVC   PRTAR+PCOUNT(8),MASK
         ED    PRTAR+PCOUNT(8),DWORD+4
         MVC   PRTAR(L'OFLOMSG),OFLOMSG
         PUT   OUT,PRTAR
         MVC   PRTAR,PRTAR-1
         B     NEXT1
  TITLE 'PRINT SUBROUTINE FOR TOTALS'
**       PRINT SUBROUTINE FOR TOTALS
PRT      AP     LCT,=P'2'
         CP    LCT,P50
         BL    PRTXYZ
         SP    LCT,LCT
         PUT   OUT,CONTHD1
PRTXYZ   PUT   OUT,PRTAR
         MVC   PRTAR,PRTAR-1
         BR    LINKREG
CONTHD1  DC    CL133'1   TOTALS CONTINUED../'
  TITLE 'SORT SUBROUTINE'
**       SORT INTO ASCENDING CYL FROM ADDR
SORT     STM   0,15,SORTSAVE
         LA    TABLAST,8(TABLAST)
         L     TABSTRT,ANSWER
NOP2     EQU   *
SRTLAB0  CLC   FROM,TO
         BL    SRTLAB1
         LH    WORKREG,FROM
         MVC   FROM,TO
         STH   WORKREG,TO
SRTLAB1  BXLE  TABSTRT,INDEX,SRTLAB0
         L     15,ANSWER
STARTI   CR    15,TABLAST
         BNL   FINSORT
         LA    14,8(15)
STARTJ   CR    14,TABLAST
         BNL   FINJ
         CLC   0(4,15),0(14)
         BNH   INCRSORT
         MVC   SORTEMP(8),0(14)
         MVC   0(8,14),0(15)
         MVC   0(8,15),SORTEMP
INCRSORT LA    14,8(14)
         B     STARTJ
FINJ     LA    15,8(15)
         B     STARTI
FINSORT  LM    0,15,SORTSAVE
         BR    LINKREG
SORTEMP  DS    D
SORTSAVE DS    16F
  TITLE 'SEEK TIME CALCULATION SUBROUTINE'
**       CALCULATE SEEK TIME
SEEKTIME STM   0,15,SEEKSAVE
         LM    14,15,SEEKTABL          GET TABLE ADDRESSES
         LTR   15,15                   NO SEEK TIME CALCS ?
         BCR   8,LINKREG               NO - RETURN
SEEKLP1  CH    WORKREG,0(14)
         BNH   GOTTIME
         LA    14,4(14)
         CR    14,15
         BL     SEEKLP1
GOTTIME  LH    WORKREG,2(14)
         SRDA  WORKREG,32
         M     WORKREG,COUNT
         CVD   OVERFLOW,DWORD
         MVC   PRTAR+PTIME(10),MASKX
         ED    PRTAR+PTIME(10),DWORD+3
         MVC   PRTAR+PTIME+11(2),=C'MS'
         A     OVERFLOW,TOTIME
         ST    OVERFLOW,TOTIME
         LM    0,15,SEEKSAVE
         BR    LINKREG
TOTIME   DS    F
SEEKSAVE DS    16F
MASKX    DC    X'40202020202020202120'
         TITLE 'INIT - GET SELECTION TYPE ETC'
INIT     XC    TOTCYLS,TOTCYLS          ZEROISE
         XC    TOTIME,TOTIME            ZEROISE
         XC    TIME,TIME                ZEROISE
         XC    EXCPS,EXCPS              ZEROISE
         XC    SEEKTABL(8),SEEKTABL    FORCE NO SEEK TIME CALCS
          MVI  PRTFIRST+1,X'F0'
         MVI   HD0,C'0'                FORCE SPACE
         MVI   HD0+PSELECT,C' '
         MVC   HD0+PSELECT+1(L'HD0-PSELECT-1),HD0+PSELECT
         MVC   PRTAR,PRTAR-1
         MVI   PRTAR,C'1'
         MVC   PRTAR+2(13),=C'CONTROL CARD='
         MVC   PRTAR+PSELECT(80),SELECT
         PUT   OUT,PRTAR               PRINT SELECTION REQUESTED
         MVC   PRTAR,PRTAR-1
         MVC   HD0+PSELECT(3),SELECT   INIT DRIVE ADDR
         MVC   SUMMAREA+PADDR(3),SELECT
*        GET VOL SER NUMBER
         CLC   SELVID,VOLID            V= SPECIFIED ?
         BNE   NOVOL                   NO - DO NOT PRINT NUMBER
         CLC   SELVSN,IGNORE           IGNORE SPECIFIED ?
         BE    NOVOL                   YES - SKIP
         CLC   SELVSN,FINDIT           FINDIT SPECIFIED ?
         BNE   VOLGIVEN                NO - ASSUME IN CTL CARD
         FINDUCB DEVADDR=SELECT,TO=(WORKREG),VALID=YES
         BE    VOLFOUND
         MVC   HD0+PSELECT+5(5),=C'INVAL'
         B     INITL1
VOLFOUND MVC   HD0+PSELECT+5(6),28(WORKREG)
         B     INITL1
VOLGIVEN MVC   HD0+PSELECT+5(6),SELVSN
         B     INITL1
NOVOL    MVC   HD0+PSELECT+5(6),=C'NO VOL'
INITL1   EQU   *                       END OF VOLSER PROCESSING
         MVC   SUMMAREA+PVOLSER(6),HD0+PSELECT+5
*        GET DEVICE TYPE
         CLC   SELDID,DEVID            D= SPACIFIED ?
         BE    INITL2                  YES - BRANCH
NODEV    MVC   HD0+PSELECT+15(12),=C'NO DEV GIVEN'
         B     INITL3                  GO GET REPORT TYPE
INITL2   EQU   *
         LA    WORKREG,SEEKTABS
GETDEV   CLI   0(WORKREG),C'*'         END OF TABLE ?
         BE    NODEVTAB                YES - NO SUCH TABLE FOR DEV
         CLC   0(4,WORKREG),SELDTP     GOT DEVICE TYPE ?
         BE    GOTDEV
         LA    WORKREG,12(WORKREG)     GET NEXT ENTRY
         B     GETDEV
NODEVTAB MVC   HD0+PSELECT+25(12),=C'NO DEV TABLE'
         B     INITL3
GOTDEV   MVC   SEEKTABL(8),4(WORKREG)
         MVC   HD0+PSELECT+25(9),=C'DEV TYPE ='
         MVC   HD0+PSELECT+34(4),SELDTP
         MVC   SUMMAREA+PDTYPE(4),SELDTP
INITL3   EQU   *
*        GET REPORT TYPE
         CLC   SELRID,REPID            R= SPECIFIED ?
         BNE   SETREP                   O - DEFAULT REPORT1
         CLI   SELRNO,C'1'             REPORT 1 ?
         BE    SETREP1
         CLI   SELRNO,C'2'             REPORT 2 ?
         BE    SETREP2
SETREP   MVC   HD0+PSELECT+54(9),=C'DEFAULTED'
         MVI   SELRNO,C'1'             FORCE REPORT1
SETREP1  MVC   NOP1(8),REP1COD1
         MVC   NOP2(28),REP1COD2
         B     INITL4
SETREP2  MVC   NOP1(8),REP2CODE
         MVC   NOP2(28),REP2CODE
INITL4   MVC   HD0+PSELECT+40(11),=C'REPORT TYPE'
         MVC   HD0+PSELECT+52(1),SELRNO
         PUT   OUT,HD0
         MVI   HD0,C'1'
         BR    LINKREG
  TITLE 'END OF JOB ROUTINE'
**       END OF JOB
EOJ       CLOSE (OUT,,CTL,,SUMMDCB,)
         FREEPOOL SUMMDCB
         FREEPOOL OUT
         FREEPOOL CTL
         L     13,HSAVE+4
         RETURN (14,12),RC=0
  TITLE 'READ SUBROUTINE'
GET      L     TABSTRT,ANSWER
         LR     THIS,NEXT
         L     WORKREG,ELEMENT
         C     WORKREG,EMPTY
         BE    READ
         LA    WORKREG,2(WORKREG)
GETIN    ST     WORKREG,ELEMENT
          LH   NEXT,INAREA(WORKREG)
         BR    LINKREG
READ     GET   IN,INAREA
         CLC   INAREA(4),SELECT-1
         BNE   TRY3
         LA    WORKREG,4
         B     GETIN
TRY3     CLI   INAREA,C'3'
         BNE   TRY9
         CLC   INAREA+4(3),SELECT
         BNE   READ
         MVC   EXCPS(4),INAREA+12
         B     READ
TRY9     CLI   INAREA,C'9'
         BNE   READ
         L     WORKREG,TIME
         A     WORKREG,INAREA+12
         ST    WORKREG,TIME
         B     READ
  TITLE 'CONSTANTS ETC'
         PRINT NOGEN
TABSTRT  EQU   2
INDEX    EQU   4
TABLAST  EQU   5
WORKREG  EQU   6
WORKREG2 EQU   1
LINKREG  EQU   3
OVERFLOW EQU   7
TABEND   EQU   8
THIS     EQU   9
NEXT     EQU   10
DWORD    DS    D
TOTAL    DS    F
TIME     DS    F
HUNDRED  DC    F'100'
ZERO     DC    F'0'
EXCPS    DS    F
ONE      DC    F'1'
ANSWER   DS    2F
RANGE     DC   F'10000',F'100000'
WORK     DS    F
EIGHT    DC    F'8'
MODEL    DC    H'0'
         DC    H'1'
         DC    F'0'
ELEMENT  DS    F
TOTCYLS  DS    F
EMPTY    DC    F'18'
OFLOSTOR DS     F
TENTHOU  DC    F'10000'
INAREA   DS    CL20
MASK     DC    X'4020202020202120'
PCENTMSK DC    X'4020202020214B2020'
P50      DC    P'50'
PONE     DC    P'1'
LCT      DC    P'60'
OFLOMSG  DC    C'-** OVERFLOW **'
TOTS      DC   C'-TOTALS'
EXCPMSG  DC    C'0TOTAL EXCPS TO THIS DEVICE'
ACTVMSG  DC    C'0ACTIVITY PERCENTAGE'
AVEXCP   DC    C'0AVERAGE EXCP RATE/SEC'
AVSEEK   DC    C'0AVERAGE SEEK'
         DC    C' '
PRTAR    DC    CL133' '
PFROM    EQU   10
PTO      EQU   20
PCOUNT   EQU   30
PCENT    EQU   40
PCYLNO   EQU   52
PTOTCYL  EQU   70
PTIME    EQU   84
PSELECT  EQU   15
HD0      DC    CL133'1 DRIVE ADDR='
HD1      DC    CL133'-              FROM       TO      COUNT        %  *
                  NO. CYLINDERS     CYLS MOVED      SEEK TIME'
SEEKTABL DS    2F
SEEKTABS DS    0F
         DC    C'2314',A(TAB2314S),A(TAB2314E)
         DC    C'660 ',A(TAB660S),A(TAB660E)
         DC    C'3330',A(TAB3330S),A(TAB3330E)
         DC    C'*   ',A(0),A(0)
         DC    C'5'
SELECT   DS    CL80
         ORG   SELECT
SELUNIT  DS    CL3                     UNIT ADDR
         DS    C
SELVID   DS    CL2                     V=
SELVSN   DS    CL6                     VOL SER NO
         DS    C
SELDID   DS    CL2                     D=
SELDTP   DS    CL4                     DEVICE TYPE
         DS    C
SELRID   DS    CL2                     R=
SELRNO   DS    C                       REPORT TYPE
         ORG
VOLID    DC    C'V='
DEVID    DC    C'D='
REPID    DC    C'R='
REPORT1  DC    C'1'
REPORT2  DC    C'2'
IGNORE   DC    C'IGNORE'
FINDIT   DC    C'FINDIT'
SUMMHD1  DC    CL133'1    DRIVE    DEVICE    VOLUME      TOTAL      TOT*
               AL    AVERAGE SEEK     SEEK TIME     ACTIVITY     EXCP R*
               ATE'
SUMMHD2  DC    CL133'     ADDR.     TYPE     SERIAL      EXCPS      SEE*
               KS        CYLS            MS             %         PER S*
               EC'
         DC    C' '
SUMMAREA DS     CL133
PADDR    EQU   6
PDTYPE   EQU   15
PVOLSER  EQU   24
PTOTEXCP EQU   33
PTOTSEEK EQU   44
PAVSEEK  EQU   56
PSEEKTM  EQU   72
PACTV    EQU   85
PEXCPRAT EQU   98
REP1COD1 DS    CL8
REP1COD2 DS    CL28
REP2CODE DS    0H
A        NOP   A
         NOP   A
         NOP   A
         NOP   A
         NOP   A
         NOP   A
         NOP   A
         LTORG
IN       DCB   DDNAME=IN,DSORG=PS,MACRF=GM,EODAD=EOINP
OUT      DCB   DDNAME=OUT,DSORG=PS,MACRF=PM
CTL      DCB   DDNAME=CTL,DSORG=PS,MACRF=GM,EODAD=EOJ
SUMMDCB  DCB   DDNAME=SUMMARY,DSORG=PS,MACRF=PM
TAB2314S DS    0F
TAB660S  EQU   *
         DC    H'0',H'25'
         DC    H'1',H'25'
         DC    H'2',H'30'
         DC    H'3',H'35'
         DC    H'4',H'40'
         DC    H'5',H'40'
         DC    H'10',H'45'
         DC    H'15',H'55'
         DC    H'20',H'60'
         DC    H'25',H'65'
         DC    H'40',H'70'
         DC    H'60',H'72'
         DC    H'80',H'75'
         DC    H'100',H'85'
         DC    H'120',H'95'
         DC    H'140',H'105'
         DC    H'160',H'115'
         DC    H'180',H'125'
         DC    H'200',H'133'
TAB2314E EQU   *
TAB660E  EQU   *
TAB3330S EQU   *
         DC    H'1',H'8'
         DC    H'2',H'9'
         DC    H'4',H'10'
         DC    H'6',H'11'
         DC    H'9',H'12'
         DC    H'12',H'13'
         DC    H'16',H'14'
         DC    H'20',H'15'
         DC    H'25',H'16'
         DC    H'30',H'17'
         DC    H'36',H'18'
         DC    H'42',H'19'
         DC    H'49',H'20'
         DC    H'56',H'21'
         DC    H'64',H'22'
         DC    H'72',H'23'
         DC    H'81',H'24'
         DC    H'90',H'25'
         DC    H'100',H'26'
         DC    H'110',H'27'
         DC    H'121',H'28'
         DC    H'132',H'29'
         DC    H'144',H'30'
         DC    H'156',H'31'
         DC    H'169',H'32'
         DC    H'182',H'33'
         DC    H'196',H'34'
         DC    H'210',H'35'
         DC    H'225',H'36'
         DC    H'240',H'37'
         DC    H'256',H'38'
         DC    H'272',H'39'
         DC    H'289',H'40'
         DC    H'306',H'41'
         DC    H'324',H'42'
         DC    H'342',H'43'
         DC    H'361',H'44'
         DC    H'380',H'45'
         DC    H'400',H'46'
         DC    H'404',H'47'
TAB3330E EQU   *
ENTRY    DSECT
FROM     DS    H
TO       DS    H
COUNT    DS    F
         END
