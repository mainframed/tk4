* THIS PROGRAM PRODUCED BY BRITISH STEEL CORPORATION CORBY
* REFER ANY PROBLEMS TO MALCOLM SISMEY TEL 053-66-2121 EXT 729
         TITLE 'SYIDTABLE  LISTER PROGRAM NUMBER TWO'
SYIDLST2 BEGIN
         EJECT
*     THIS PROGRAM READS A SORTED FILE OF RECORDS FORMAT
*     XXNNNNNN  WHERE XX IS THE JOB LOG CODE  AND YYYYYY IS THE
*     VOLUME SERIAL NUMBER.  RECORDS ARE SORTED INTO ASCENDING SEQUENCE
*     THIS PROGRAM PRINTS A LIST OF JOB CODES AND  THE PACKS ASSOCIATED
*     WITH EACH CODE.
*
*
*
         EJECT
         OPEN  (INFILE,INPUT,OUTFILE,OUTPUT)
         MVI   OUTAREA,X'8B'       SKIP TO
         PUT   OUTFILE,OUTAREA     CH   ONE
         MVC   OUTAREA(133),OUTAREA-1   BLANK OUT AREA
         MVC   OUTAREA+30(43),HEAD1     HEADING
         MVI   OUTAREA,X'19'       WRITE SPACE 3
         PUT   OUTFILE,OUTAREA          PUT OUT
         MVC   OUTAREA(133),OUTAREA-1   BLANK OUT
         MVI   OUTAREA,X'09'       WRITE SPACE 1
         LA    2,OUTAREA+1         AREA START
         LA    3,OUTAREA+124       AREA END
READ     GET   INFILE              READ A RECORD
         LR    4,1                 STORE ADDR OF INPUT
         CLC   OLDLOG,0(4)         HAS LOG NO CHANGED
         BE    SAMELOG             NO- PUT OUT
         PUT   OUTFILE,OUTAREA     YES START NEW LINE
         MVC   OUTAREA(133),OUTAREA-1   BLANK OUT
         MVI   OUTAREA,X'0B'       SPACE 1 IMMEDIATELY
         PUT   OUTFILE,OUTAREA     PUT OUT
         LA    2,OUTAREA+1         AREA START
         MVI   OUTAREA,X'09'       WRITE & SPACE 1
         MVC   OLDLOG,0(4)         MOVE NEW LOG TO OLD LOG
         MVC   0(2,2),0(4)         MOVE OUT LOG CODE TO PRINTLINE
         LA    2,3(2)              STEP UP R2
         B     SAMELOG             TREAT AS NORMAL
*
SAMELOG  MVC   0(6,2),2(4)         MOVE OUT VOLUME SERIAL NO.
         LA    2,7(2)              STEP UP R2
         CR    3,2                 IS AT END OF LINE
         BNH   NEWLINE             YES SET UP NEW ONE
         B     READ                NO GET NEXT RECORD
*
NEWLINE  PUT   OUTFILE,OUTAREA     PUT OUT LINE
         MVC   OUTAREA(133),OUTAREA-1   BLANK OUT
         MVI   OUTAREA,X'09'       WRITE AND SPACE1
         LA    2,OUTAREA+1         AREA START
         MVC   0(2,2),OLDLOG       IN LOG CODE
         LA    2,3(2)              STEP UP
         B     READ                GET NEXT CARD
*
ENDIN    PUT   OUTFILE,OUTAREA     PUT OUT
         MVC   OUTAREA(133),OUTAREA-1   BLANK
         MVI   OUTAREA,X'89'       SKIP TO CH 1 & WRITE
         MVC   OUTAREA+20(23),ENDM      END MESSAGE
         PUT   OUTFILE,OUTAREA          PUT OUT
         CLOSE (INFILE,,OUTFILE)        CLOSE
         L     13,4(13)                 GO
         RETURN  (14,12)                BACK
*        CONSTANTS ETC.
INFILE   DCB   DDNAME=IN,DEVD=DA,MACRF=GL,LRECL=18,EODAD=ENDIN,        C
               DSORG=PS
OUTFILE  DCB   DDNAME=PRINT,DEVD=DA,MACRF=PM,LRECL=133,                C
               DSORG=PS
         DC    CL1' '              BLANK
OUTAREA  DS    CL133               OUTPUT AREA
HEAD1    DC    CL43'LISTING OF JOB CODES AND APPROPRIATE PACKS'
OLDLOG   DC    CL2'  '             LAST LOG CODE
ENDM     DC    CL23'END OF LOG CODE LISTING'
         END   SYIDLST2
