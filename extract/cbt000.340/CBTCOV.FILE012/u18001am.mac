        TITLE   'REPORT - THIS MODULE DISPLAYS ACTIVE/Q/REQUESTS '
* MACRO
         MACRO
&NAME    CUREG
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUREG MACRO:16/01/75'
         POP   PRINT
         ACTR  50
         LCLA  &X,&Y,&Z
*    GENERAL REGISTERS
.LPR     ANOP
R&X      EQU   &X
&X       SETA  &X+1
         AIF   (&X LT 16).LPR
*    CONTROL REGISTERS
.LPC     ANOP
C&Y      EQU  &Y
&Y       SETA  &Y+1
         AIF   (&Y LT 16).LPC
*    FLOATING POINT REGISTERS
.LPF     ANOP
F&Z      EQU   &Z
&Z       SETA  &Z+2
         AIF   (&Z LT 8).LPF
         MEND
         MACRO
&NAME    CUENTER &BASE=(R12),&RECURSE=,&RENT=,&FIRST=,&CSECT=,&PARM=
         LCLA  &AL0,&AL1,&AL2,&AL9,&AL12
         ACTR  45                                              DEC 75
&AL2     SETA  &SYSNDX
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUENTER MACRO:16/06/77'
         POP   PRINT
         LCLC  &CL0,&CL1,&CL2,&CL3,&CL12,&CL13,&CL50
         LCLC  &CL51,&CL52,&CL53
         GBLB  &BG15
         AIF   ('&NAME' EQ '').B98
         ENTRY &NAME
.B98     ANOP
&CL0     SETC  '12'
         AIF   ('&BASE'(1,1) EQ '(').B10  *VALIDATE &BASE PARAMETER
&CL0     SETC  '&BASE'
         AIF   ('&CL0' EQ 'NONE').B11
         AIF   ('&CL0'(1,1) NE 'R').B4B
&AL9     SETA  K'&CL0-1
&CL0     SETC  '&CL0'(2,&AL9)
.B4B     AIF   ((&CL0 GT 1) AND (&CL0 LT 13)).B11
.BERA    MNOTE 9,'&&BASES WRONG,MACRO ABANDONED'
         MEXIT
.B10     ANOP
&CL0     SETC  '&BASE(1)'
&AL0     SETA  1                        COUNT ALONG TO-BE-SCANNED BASES
.B13     ANOP
&AL1     SETA  1                       *COUNT ALONG PREVIOUSLY SCANNED
&CL12    SETC  '&BASE(&AL0)'
         AIF ('&CL12'(1,1) NE 'R').B12A
&AL9     SETA  K'&CL12-1
&CL12    SETC  '&CL12'(2,&AL9)
.B12A    ANOP
&AL12    SETA  &CL12
.B12     AIF    ((&AL12 LT 2) OR (&AL12 GT 12)).BERA
.B15     AIF   (&AL1 EQ &AL0).B14      *J OUT IF REACHED LATEST
&CL13    SETC  '&BASE(&AL1)'
         AIF   ('&CL13'(1,1) NE 'R').B15A
&AL9     SETA  K'&CL13-1
&CL13    SETC  '&CL13'(2,&AL9)
.B15A    AIF   (&CL12 EQ &CL13).BERA *ERROR IF DUPLICATED
&AL1     SETA  &AL1+1                  *STEP ALREADY SCANNED PTR
         AGO   .B15
.B14     ANOP
&AL0     SETA  &AL0+1                  *STEP SCAN POINTER
         AIF   ('&BASE(&AL0)' NE '').B13 *J IF NOT NULL
&AL9     SETA  N'&BASE
         AIF   (&AL0 LT &AL9).BERA *ERROR IF NULL ELEMENT
.B11    ANOP
.*   START GENERATING PROGRAM
         DS    0H
         BALR  R15,0
         USING *,R15
&CL1     SETC  '&CSECT'
         AIF   ('&CL1' NE '').B30
&CL1     SETC  '*'
.B30     ANOP
&NAME    SAVE  (14,12),,&CL1
.B32     ANOP
.B90A    BAL   &CL0,OCSVA&AL2
         DROP  R15
         USING *,&CL0
OCSVD&AL2 EQU  *         HOLD IMMEDIATELY AFTER USING STATEMENT
         AIF   (&BG15).NODATIM
         CUREG                          REGISTER MNEMNONICS
&CL51    SETC  '&NAME'
         AIF   (K'&CL51 LE 8).DT1
&CL51    SETC  '&CL51'(1,8)
         AGO   .DT2
.DT1     ANOP
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '&SYSECT'
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '????????'
.DT2     ANOP
&CL52    SETC  '&SYSDATE'(4,2)'.'.'&SYSDATE'(1,2)'.'.'&SYSDATE'(7,2)
&CL53    SETC  '&SYSTIME'(1,2)':'.'&SYSTIME'(4,2)':00'
CUVRSION DC    C'****VERSION/',CL8'&CL51',C'/&CL52./&CL53'
&BG15    SETB  (1)                                             DEC 75
.NODATIM ANOP
OCSAR&AL2 DS    18F
OCSVA&AL2 EQU  *
         AIF   ('&BASE' EQ '').B89
&AL9     SETA  N'&BASE
         AIF   (&AL9 LE 1).B89
&AL1     SETA  &AL9
&AL0     SETA  2
.B88     ANOP
&CL2     SETC  '&BASE(&AL0)'
&CL3     SETC  '&BASE(&AL0-1)'
         LA    &CL2,1
         LA    &CL2,4095(&CL2,&CL3)
         USING OCSVD&AL2+4096*(&AL0-1),&CL2
         AIF   (&AL0 EQ &AL1).B87
&AL0     SETA  &AL0+1
         AGO   .B88
.B89     ANOP
.B87     ANOP
         ST    R13,OCSAR&AL2+4
         LR    R15,R13
         LA    R13,OCSAR&AL2
         ST    R13,8(R15)
         MEXIT
.MEND    MEND
* MACRO
         MACRO
&NAME    CUEXIT   &RC=(15)
         GBLB  &BG15                                 DEC 75
         LCLA  &B,&W
         LCLC  &Z
&NAME    DS 0H
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUEXIT MACRO:16/06/77'
         POP   PRINT
         L      R13,4(13)
&Z         SETC  '&RC'
.B94     AIF   ('&Z'(1,2) NE '(R').B84
&B       SETA  K'&Z-2
&Z        SETC  '&Z'(1,1).'&Z'(3,&B)
.B84     ANOP
         AIF   ('&Z' EQ '(15)').B60
         AIF   ('&Z'(1,1) EQ '(').B61
         RETURN (14,12),RC=&Z
         AGO   .B62
.B61     ANOP
&W       SETA  K'&Z-2
&Z       SETC  '&Z'(2,&W)
         LR    R15,&Z
.B60     RETURN (14,12),RC=(15)
.B62     ANOP
         MEND
REPORT   CUENTER
         ST    1,SAVE1
         LINEDIT TEXT='LINK OS/VS1 14A 14A RR OSFR',DISP=CPCOMM,DOT=NO
         CNOP 4,8
         BAL  1,ACCESS
         DC   CL8'ACCESS'
         DC   CL8'14A'
         DC   CL8'G'
         DC   8X'FF'
ACCESS   SVC  202
         DC   AL4(*+4)
         CNOP 4,8
         BAL  1,FILEDEF
         DC   CL8'FILEDEF'
         DC   CL8'IANFILE'
         DC   CL8'DISK'
         DC   CL8'OP'
         DC   CL8'SPY'
         DC   CL8'G4'
         DC   8X'FF'
FILEDEF  SVC  202
         DC   AL4(*+4)
         DISPW *,CANCEL=YES,BYTES=1
         OPEN  (IANFILE,(INPUT))
         GET   IANFILE,INAREA
         MVC   TIME(5),INAREA
         MVC   HEAD1+55(5),TIME
         MVC  HEAD1A+55(5),TIME
         MVC   HEAD2+55(5),TIME
         L     2,SAVE1
        CLI    8(2),X'FF'
        BE    WTO
         CLI   8(2),C'R'
         BNE   NOTDR
         OI    BRANCH+1,X'F0'
         B     DR
NOTDR    CLI   8(2),C'N'
         BNE   NOTDN
         CLC  8(2,2),=C'N='
         BNE   DN
         CLI  11(2),C' '
         BNE  NOTDN
         MVC  CLASS,10(2)
         NI    BRAN1+1,X'00'
         B    DN
NOTDN    EQU   *
         CLI   8(2),C'A'
         BNE   NOTDA
         OI    BRAN2+1,X'F0'
NOTDA    EQU    *
WTO      WTO '*******************************************************',C
               ROUTCDE=11
HEAD1    WTO '**  A C T I V E   D I S P L A Y   R E P O R T        **',C
               ROUTCDE=11
WTO0     WTO '** PART JOBNAME  STEPNAME    PART JOBNAME  STEPNAME  **',C
               ROUTCDE=11
        LA   6,INAREA+5
LOOP0   LA   7,2
        LA    8,WTO1+12
LOOP1    MVC   0(21,8),0(6)
         LA   8,26(8)
         LA    6,21(6)
         CLC   0(3,6),=C'999'
         BE   WTO1
          BCT   7,LOOP1
WTO1     WTO '**                                                   **',C
               ROUTCDE=11
         MVC   WTO1+12(48),WTO1+11
         CLI   0(6),C'P'
         BE    LOOP0
DR       LA    6,INAREA+5
DRLOOP   CLC   0(3,6),=C'WTO'
         BE    DREND
         LA    6,21(6)
         B     DRLOOP
DREND    LA    6,4(6)
         WTO '*******************************************************',C
               ROUTCDE=11
BRAN2     NOP   CLOSE
HEAD1A   WTO '**  O U T S T A N D I N G  R E Q U E S T S           **',C
               ROUTCDE=11
DRLOOP1  CLC  0(3,6),=C'END'
         BE   DN1
         MVC  DRWTO+12(40),0(6)
         LA   6,40(6)
         OI   DN1+1,X'F0'
DRWTO    WTO '**                                                   **',C
               ROUTCDE=11
         B    DRLOOP1
DN1      NOP   DN
         WTO '**  NO OUTSTANDING REQUESTS                          **',C
               ROUTCDE=11
DN       WTO '*******************************************************',C
               ROUTCDE=11
BRANCH    NOP   CLOSE
HEAD2    WTO '**  S Y S J O B Q E   S T A T U S  R E P O R T       **',C
               ROUTCDE=11
         WTO '**  JOBNAME  CL TYPE  PR      JOBNAME  CL TYPE   PR  **',C
               ROUTCDE=11
         GET   IANFILE,INAREA
         LA   6,INAREA
LOOP3    LA   7,2
         LA   8,WTO2+12
LOOP4      EQU  *
BRAN1      B    ONA
         CLC   1(3,6),=C'   '
          BE   WTO2
          CLC   0(1,6),CLASS
          BE    ONA
           LA    6,11(6)
          B     LOOP4
ONA       OI    TRAPPER+1,X'F0'
ON       MVC  10(1,8),0(6)
          MVC   12(6,8),=C'      '
          MVC   0(8,8),1(6)
          CLI  9(6),C'I'
          BNE   NOTINP
         MVC   12(5,8),=C'INPUT'
         B     INPUT
NOTINP   CLI  9(6),C'O'
        BNE    INPUT
           MVC  12(6,8),=C'OUTPUT'
INPUT     MVC   19(1,8),10(6)
           LA   6,11(6)
ONABIT    LA    8,26(8)
          BCT   7,LOOP4
WTO2     WTO '**                                                   **',C
               ROUTCDE=11
         MVI   WTO2+12,C' '
         MVC   WTO2+13(48),WTO2+12
           CLC   1(3,6),=C'   '
          BNE   LOOP3
TRAPPER    NOP  TRAPFIN
         WTO '** QUEUE EMPTY                                       **',C
               ROUTCDE=11
TRAPFIN  WTO '*******************************************************',C
               ROUTCDE=11
CLOSE    CLOSE  (IANFILE,)
           CNOP  4,8
           BAL  1,RELEASE
           DC   CL8'RELEASE'
           DC   CL8'G'
           DC   CL8'('
           DC   CL8'DET'
           DC   8X'FF'
RELEASE   SVC  202
           DC   AL4(*+4)
         CUEXIT  RC=0
IANFILE   DCB   DDNAME=IANFILE,DSORG=PS,MACRF=GM
CLASS     DC   CL1' '
TIME    DC   CL5' '
SAVE1    DC   F'0'
         LTORG
INAREA   DC   2500C' '
         END
