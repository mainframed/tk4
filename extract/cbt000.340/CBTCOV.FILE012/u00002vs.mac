AMM.     TITLE 'RT5035M - GENERAL PURPOSE XCTL ROUTINE - A.MORRISH.'
RT5035M  CSECT
***********************************************************************
*                                                                     *
*  THIS PROGRAM IS PROVIDED TO ALLOW A MULTIPLICITY OF PROGRAMS TO BE *
* EXECUTED AT A COST OF ONE ENTRY IN IEEVLNKT.                        *
*                                                                     *
*  THE PROGRAM TRANSFERS CONTROL TO THE PROGRAM SPECIFIED IN THE      *
* PARAMETER LIST. THE REMAINDER OF THE LIST (IF ANY) IS PASSED TO THE *
* PROGRAM TO WHICH CONTROL IS TRANSFERRED.                            *
*                                                                     *
***********************************************************************
         SPACE 3                                                      *
* REGISTER USAGE.
R0       EQU   0                   WORK REGISTER.
R1       EQU   1                   PARAMETER REGISTER.
R2       EQU   2                   ADDRESS OF PARAMETER LIST.
R3       EQU   3                   WORK REGISTER.
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  BASE REGISTER.
         EJECT                                                        *
         USING *,R15               DEFINE BASE REGISTER.
         SPACE 1                                                      *
*                                                                     *
*        FIND PROGRAM NAME AND SET UP XCTL.                           *
*                                                                     *
         SPACE 1                                                      *
         L     R2,0(R1)            SET GR2 -> PARAMETER LIST.
         LH    R3,0(R2)            SET GR3 = LENGTH OF LIST.
         LTR   R3,R3               TEST SIGN OF GR3.
         BNP   BR14                IF NOT PLUS THEN GO TO EXIT.
         LA    R0,8                SET GR0 = 8.
         CLR   R3,R0               COMPARE FOR ADDITIONAL PARAMETERS.
         BNH   *+6                 IF NOT GREATER THEN LEAVE.
         LR    R3,R0               SET GR3 = 8.
         BCTR  R3,R0               DECREMENT GR3 FOR EXECUTE.
         EX    R3,MOVEPROG         MOVE PROGRAM NAME TO XCTL.
         SPACE 1                                                      *
*                                                                     *
*        MOVE PARAMETER LIST TO ELIDE PROGRAM NAME.                   *
*                                                                     *
         SPACE 1                                                      *
         LH    R3,0(R2)            SET GR3 = LENGTH OF LIST.
         SR    R3,R0               DECREMENT GR3 BY 8.
         BNM   *+6                 BRANCH AROUND IF VALID.
         SR    R3,R3               SET GR3 = 0 AND CONDITION CODE = 0.
         STH   R3,0(R2)            STORE DECREMENTED LIST LENGTH.
         BZ    XCTL                IF NO PARAMETERS THEN GO TO EXIT.
         BCTR  R3,R0               DECREMENT GR3 FOR EXECUTE.
         EX    R3,MOVEPARM         SET UP PARAMETER LIST FOR PROGRAM.
         SPACE 1                                                      *
*                                                                     *
*        TRANSFER CONTROL TO REQUIRED PROGRAM.                        *
*                                                                     *
         SPACE 1                                                      *
XCTL     XCTL  SF=(E,XCTLL),MF=(E,(1))  TRANSFER CONTROL.
         SPACE 1                                                      *
*                                                                     *
*        RETURN TO INVOKER IF INVALID PARAMETER LIST.                 *
*                                                                     *
         SPACE 1                                                      *
BR14     SR    R15,R15             SET  GR15 = 0.
         BR    R14                 RETURN.
         SPACE 1                                                      *
*                                                                     *
*        MODEL STATEMENTS                                             *
*                                                                     *
         SPACE 1                                                      *
MOVEPROG MVC   PROGNM(0),2(R2)     MOVE NAME TO PROGRAM AREA.
MOVEPARM MVC   2(0,R2),10(R2)      ELIDE PROGRAM NAME.
         SPACE 1                                                      *
*                                                                     *
*        CONSTANTS AND WORK AREAS.                                    *
*                                                                     *
         SPACE 1                                                      *
XCTLL    XCTL  EPLOC=PROGNM,SF=L
PROGNM   DC    CL8' '
         END   RT5035M
