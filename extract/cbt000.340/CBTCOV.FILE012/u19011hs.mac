*
         TITLE 'D0701    CARD ERROR REPORT PRINTING                   '
D0701    CSECT
         ENTRY D070101             INSERT MODULE NUMBER CHANGING
D070101  EQU   *                   PROGRAMMER CODE TO V.M. LEVEL
         PRINT NOGEN
         COPY  REGEQU
         EJECT
***********************************************************************
*                                                                     *
*    MODULE D0701     PROGRAMMER R.A.WHITELEY       ASSIGNED 27/06/72 *
*                                                                     *
*                           MODULE FUNCTIONS                          *
*                           ----------------                          *
*        SUBROUTINE IS USED TO PRODUCE AN ERROR REPORT FOR EDITTED    *
*  CARDS. ERRONEOUS CHARACTERS ARE UNDERLINED, AND A SERIES OF        *
*  MESSAGES MAY BE PRINTED. THE MESSAGES REQUIRED ARE PASSED AT P3,   *
*  AN '*' IS USED AS DELIMETER, AND END OF MESSAGES IS INDICATED      *
*  BY '*' FOLLOWED BY X'FF'.                                          *
*        USE B3401, P03.                                              *
*        MESSAGES FOR ONE CARD ARE NOT SPLIT BETWEEN PAGES.           *
*                                                                     *
*                                                                     *
*                                                                     *
         SPACE 2
*                             PARAMETERS                              *
*                             ----------                              *
*  1.   I      80 BYTE CARD IMAGE AREA.                               *
*                                                                     *
*  2.   I      80 BYTE UNDERLINE AREA.                                *
*                                                                     *
*  3.   I      MESSAGE TABLE. (EACH MESSAGE HAS MAX.LENGTH 31 BYTES)  *
*                             (C'*'  DENOTES END OF MESSAGE)          *
*                             (X'FF' DENOTES END OF TABLE)            *
*  4.   I      HEADINGS FOR REPORT.                                   *
*                                                                     *
*                           SPECIAL NOTES                             *
*                           -------------                             *
*  THE HEADINGS AT P4 MUST BE DELIMITED BY C'Z' & MUST NOT CONTAIN    *
*  MORE THAN 8X133 BYTES (SEE B3401).                                 *
*  TO FINISH REPORT (CLOSE PRINTER) X'FF' SHOULD BE PASSED AT P3.     *
*                                                                     *
*                                                                     *
***********************************************************************
*
**  AMENDMENT TO MODULE D0701    BY PAUL ANTON DANIELSON
*
**  ASSIGNED 12/01/73, EFFECTIVE 18/01/73 AMENDMENT IS 0.1
*
**  DETAILS OF AMENDMENT CHANGE METHOD OF LINKING TO B3401 TO 'LATCH'
*   -------------------- AND UTILISE NEW LINE COUNT FACILITY TO TEST
*                        LINE COUNT.
*
*
*
*
*
*
***********************************************************************
         EJECT
         ISEQ  73,80
         MODIN
*
** CHECK FOR REPORT FINISHED (PRINTER TO BE CLOSED)
*  ------------------------------------------------
         CLI   0(R8),X'FF'         Q. PRINTER TO BE CLOSED
         BE    BA40                A. YES - CLOSE IT
*
** INITIALIZE
*  ----------
         MVI   CCO133,C'0'         MISS A LINE BEFORE PRINTING
         LR    R3,R8               SAVE ADDRESS OF MESSAGE TABLE
         BAL   R10,B5201           FORCE NEW PAGE IF REQ'D
         LR    R3,R8               SAVE ADDRESS OF MESSAGE TABLE
         SPACE 2
***********************************************************************
*  SET UP 1ST LINE (CARD IMAGE) & PRINT                               *
***********************************************************************
         SPACE
         MVC   CCO080,0(R6)        CARD IMAGE TO PRINT-LINE
         BAL   R10,B5001           MESSAGE TO PRINT-LINE
         BAL   R10,B5101           PRINT IT
         SPACE 3
***********************************************************************
*  SET UP 2ND LINE (UNDER-LINE) & PRINT                               *
***********************************************************************
         SPACE
         MVC   CCO080,0(R7)        UNDER-LINE AREA TO PRINT LINE
         BAL   R10,B5001           MESSAGE TO PRINT-LINE
         BAL   R10,B5101           PRINT IT
         SPACE 3
***********************************************************************
*  SET UP SUBSEQUENT LINES (IF REQUIRED) & PRINT                      *
***********************************************************************
         SPACE
BA08     CLI   0(R3),X'FF'         Q. ANY FURTHER MESSAGES
         BE    BA50                A. NO - EXIT
         BAL   R10,B5002           MESSAGE TO PRINT LINE
         BAL   R10,B5101           PRINT IT
         B     BA08                CHECK FOR FURTHER MESSAGES
         SPACE 3
*
** FINISH REPORT
*  -------------
BA40     MVI   CCO133,X'FF'        INDICATE &
         BAL   R10,B5101           - FINISH REPORT
         SPACE
BA50     MODOUT
         EJECT
***********************************************************************
* B5001  SELECT NEXT MESSAGE & SET UP IN PRINT-LINE                   *
***********************************************************************
         SPACE
B5001    CLI   0(R3),X'FF'         Q. END OF MESSAGE TABLE
         BER   R10                 A. YES - RETURN
B5002    XR    R1,R1               CLEAR R1
         TRT   0(32,R3),CXT256     TEST 32 BYTES OF MESSAGE FOR *
         BZ    B5050               * NOT FOUND - ERROR
         SR    R1,R3               CALC. LENGTH OF MESSAGE
         BCTR  R1,R0               LENGTH FOR EXECUTED MOVE INSTRUCTION
         EX    R1,MOVE             MOVE MESSAGE TO PRINT LINE
         LA    R3,2(R1,R3)         POINT TO FIELD AFTER MESSAGE & *
         BR    R10                 RETURN
*
B5050    MVC   CCO031,=C'****** NO ASTERISK FOUND ******'
         MVI   0(R3),X'FF'         FORCE END OF TABLE
         BR    R10                 RETURN
*
MOVE     MVC   CCO031(0),0(R3)     MOVE MESSAGE TO PRINT LINE
         SPACE 3
***********************************************************************
* B5101  - PRINT A LINE ON REPORT                                     *
***********************************************************************
         SPACE
B5101    LATCH B3401,((R9),CCO133,CCX001,CFD004)  PRINT A LINE      0.1
         MVI   CCO133,C' '         NEXT TIME PRINT ON NEXT LINE
         MVI   CCX001,C'0'         DO NOT FORCE NEW PAGE
         BR    R10                 RETURN
         EJECT
***********************************************************************
* B5201  - DETERMINE WHERE TO PRINT MESSAGE                           *
***********************************************************************
         SPACE
B5201    EQU   *                                                    0.1
         ZAP   SDW008,CFD004(2)    B3401 LINE COUNT TO WORK AREA    0.1
         CVB   R14,SDW008          - & THEN TO REGISTER
         LH    R4,CFD004+2         MAX. NO. OF LINES PER PAGE       0.1
         SR    R4,R14              NO.OF LINES LEFT ON THE PAGE TO REG.
         LR    R12,R4              - (TWICE)
*
** ARE 3 LINES LEFT ON THE PAGE - IF NOT FORCE NEW PAGE & EXIT
*  -----------------------------------------------------------
         SH    R12,=H'3'           Q. 3 LINES LEFT
         BM    B5210               A. NO - FORCE NEW PAGE
*
** SEARCH FOR ASTERISKS & REDUCE LINE COUNT BY 1 FOR EACH &
** FORCE NEW PAGE IF ZERO.
** NOTE. THIS RTN ALLOWS FOR BLANK LINE PRIOR TO CARD IMAGE
*  --------------------------------------------------------
B5204    CLI   0(R3),X'FF'         Q. END OF MESSAGE TABLE
         BER   R10                 A. YES - EXIT
         XR    R1,R1               CLEAR REGISTER 1 FOR TRT
         TRT   0(32,R3),CXT256     Q. * IN NEXT 32 BYTES
         BZR   R10                 A. NO - ERROR - EXIT
         LA    R3,1(R1)            A. YES - POINT TO NEXT MESSAGE
         BCT   R4,B5204            IF STILL ROOM ON PAGE - LOOP
*
** FORCE NEW-PAGE
*  --------------
B5210    MVI   CCX001,C'1'         FORCE NEW PAGE
         BR    R10                 RETURN
         EJECT
***********************************************************************
* CONSTANTS, STORAGE AREAS & LITERAL POOL                             *
***********************************************************************
         SPACE
SDW008   DS    D                   WORK AREA FOR B3401 LINE COUNT
CFD004   DC    0F'0',PL2'50',H'50' LINE COUNT        - P4 FOR B3401 0.1
CCX001   DC    C'0'                NORMAL SHEET NO.  - P3 FOR B3401
CCO133   DS    0CL133              PRINT LINE        - P2 FOR B3401
         DC    CL11' '             -
CCO080   DC    CL80' '             -CARD IMAGE
         DC    CL9' '              -
CCO031   DC    CL31' '             -MESSAGE
         DC    CL2' '              -
CXT256   DS    0CL256              TABLE FOR TRANSLATE & TEST
         DC    92X'00'
         DC    C'*'
         DC    163X'00'
*
         LTORG
         END
