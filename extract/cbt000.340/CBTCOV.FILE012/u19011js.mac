         TITLE 'B340113  SUBROUTINE TO PRINT REPORTS.                 '
B3401    CSECT
         ENTRY B340103             INSERT MODULE NUMBER CHANGING
B340103  EQU   *                   PROGRAMMER CODE TO V.M. LEVEL
         PRINT NOGEN
         COPY  REGEQU
         ENTRY CPD002
         SPACE 3
***********************************************************************
*                                                                     *
*   PROGRAM B3401     PROGRAMMER D.S.THOMSON        ASSIGNED 17/11/70 *
*                                                                     *
*                           MODULE FUNCTIONS                          *
*                           ----------------                          *
*  SUBROUTINE FOR PRODUCING PRINTED REPORTS. PUTS OUT DETAIL LINES    *
*  AND ALSO HANDLES HEADING ROUTINES, PAGE OVERFLOW, SHEET COUNTING,  *
*  AND DETERMINING AND EDITING DATE, WHERE DATE POSITION IS BLANK     *
*                                                                     *
         SPACE 2
*                             PARAMETERS                              *
*                             ----------                              *
*  1. ADDRESS OF HEADINGS, UP TO 8 LINES. ANY ASA CC'S MAY BE USED    *
*     BUT THAT FOR FIRST LINE MUST BE 1 . POSITION 92-100 OF LINE 2   *
*     MUST BE LEFT BLANK FOR INSERTION OF SHEET NO. IF POSITION       *
*     111-129 IS BLANK CURRENT DATE WILL BE INSERTED.                 *
*                                                                     *
*  2. ADDRESS OF PRINT-LINE (WITH ASA CC). FF IN BYTE 0 CAUSES        *
*     MODULE TO CLOSE PRINTER/PRINT-TAPE.                             *
*                                                                     *
*  3. ADDRESS OF 1-BYTE INDICATOR.                                    *
*          0 - NORMAL LINE-COUNTING,SHEET-NUMBERING AND PAGE OVERFLO  *
*              ROUTINES APPLY.                                        *
*          1 - SKIP TO NEW PAGE CONTINUE NORMAL SHEET-NUMBERING.      *
*          2 - AS 1 BUT RESTART SHEET-NUMBERING AT 1 ON NEW PAGE.     *
*                                                                     *
*  4. ADDRESS OF FULLWORD CONTAINING MAX LINES PER PAGE EXCLUDING     *
*     HEADINGS (USUALLY 55).                                          *
*                                                                     *
*                                                                     *
*
**  AMENDMENT TO MODULE B3401    BY D S THOMSON
*
**  ASSIGNED 02/07/71, EFFECTIVE 02/07/71 AMENDMENT IS 0.1
*
**  DETAILS OF AMENDMENT   MODULE NOW CHECKS FOR EOF BEFORE ANYTHING
*   --------------------   ELSE TO AVOID SPECIFICATION EXCEPTION IF
*                          OTHER PARAMETERS NOT CORRECTLY SET UP
*
***********************************************************************
*
**  AMENDMENT TO MODULE B3401    BY N.G.WADDINGTON
*
**  ASSIGNED 18/12/72, EFFECTIVE 31/12/72 AMENDMENT IS 0.2
*
**  DETAILS OF AMENDMENT MODULE AMENDED TO MAKE THE LINE COUNT
*   -------------------- ACCESSIBLE BY OTHER MODULES WHICH USE THE
*                        MACRO LATCH WHEN USING SUBROUTINE B3401.
*
***********************************************************************
*
**  AMENDMENT TO MODULE B3401    BY K.S.PEEL
*
**  ASSIGNED 05/02/74, EFFECTIVE 01/03/74 AMENDMENT IS 0.3
*
**  DETAILS OF AMENDMENT TO INSERT A REFERENCE,IN THE HEADINGS,OF THE
*   -------------------- PROGRAM NAME AND OF THE DD NAME OF THE PRINT
*                        DATA SET USING THE SUBROUTINE
***********************************************************************
         EJECT
         ISEQ  73,80
         MODIN
***********************************************************************
*   TEST FOR END OF RUN AND CLOSE PRINTER/PRINT TAPE IF NECESSARY     *
***********************************************************************
*
         CLI   0(R7),X'FF'         Q. END OF RUN
         BNE   BA04                A. NO, CARRY ON
         MVI   SCO133,X'FF'
         LATCH RG0203,(SCO133)                                      LAT
*
BA03     EQU   *
         ZAP   0(2,R9),CPD002      RETURN PRESENT LINE COUNT        0.2
         MODOUT
*
*   SAVE MAX LINES FIRST TIME THROUGH
*   ---------------------------------
BA04     CLI   CCX001,C'0'         Q. FIRST TIME THRU
         BNE   BA05                A. NO CARRY ON
         LH    R3,2(R9)            SAVE MAX LINES                   0.2
         CVD   R3,SPD008           PACK MAX LINES
         MVI   CCX001,C'1'         RESET SWITCH
*
*   CHECK PARAMETER 3
*   -----------------
*
BA05     CLI   0(R8),C'0'          Q. PARAMETER 3 = 0
         BE    BA06                A-YES,GO CHECK LINE COUNT
*
         ZAP   CPD002(2),=P'0'     A-NO,ZEROISE LINE COUNT
         CLI   0(R8),C'1'          Q-PARAMETER 3 = 1
         BE    BA06                A-YES,GO UPDATE SHEET NO
         ZAP   CPD002A,=P'0'       A-NO,RESTART SHEET NUMBERING
*
***********************************************************************
*   MAIN ROUTINE TO BRANCH TO HEADINGS ROUTINE WHEN NECESSARY AND     *
*   UPDATE SHEET NO, ELSE PUT OUT DETAIL LINE AND UPDATE LINE COUNT   *
***********************************************************************
*
*   CHECK LINE-COUNT
*   ----------------
*
BA06     CP    CPD002(2),=P'0'     Q-HEADINGS REQUIRED
         BE    BB03                A-YES,GO TO HEADINGS ROUTINE
*
BA10     ST    R7,SFD004           PRINT ONE
         LA    R1,SFD004                LINE OF
         LATCH RG0203                        DETAILS                LAT
         BAL   R10,BC02            GO UPDATE LINE COUNT
         CP    SPD008,CPD002       Q-PAGE OVERFLOW
         BH    BA03                A-NO,GOBACK
*
         CP    CPD002A,=P'999'     Q-SHEET NO = 999
         BNE   BA12                A-NO,CARRY ON
*
         ZAP   CPD002A,=P'0'       A-YES,RESTART SHEET NUMBERING
BA12     ZAP   CPD002,=P'0'        ZEROISE LINE COUNT
         B     BA03                GOBACK
*
***********************************************************************
*   ROUTINE TO PRINT HEADINGS UP TO MAXIMUM OF EIGHT LINES            *
***********************************************************************
*
BB03     AP    CPD002A,=P'1'       ADD 1 TO SHEET NO
         CLI   CCX001A,C'0'        Q-FIRST TIME THROUGH
         BNE   BB05                A-NO,CARRY ON
         LATCH B1201,(18,SCD018)   A-YES,GET DATE
         CLC   1(9,R6),=CL18' '    CHECK AREA TO CONTAIN PROGRAM &  0.3
         BNE   BB0405              DDNAME,IF NOT BLANK DO NOT INSERT0.3
*                                  IT                               0.3
*                                                                   0.3
** OBTAIN PROGRAM NAME FROM PROGRAM REQUEST BLOCK                   0.3
*  ----------------------------------------------                   0.3
         L     R1,16               GET CVT POINTER                  0.3
         L     R1,0(R1)            GET ADDRESS OF TCB WORDS         0.3
         L     R1,4(R1)            GET ADDRESS OF CURRENT TCB       0.3
         L     R1,0(R1)            GET ADDRESS OF FIRST ACTIVE RB   0.3
         MVC   1(5,R6),0(R1)       MOVE TO HEADING                  0.3
         LA    R12,5               COUNT FOR LENGTH OF PROGRAM NAME 0.3
         LA    R14,1(R6)           POINT TO PROGRAM NAME            0.3
BB0305   CLI   0(R14),X'40'        Q-END OF PROGRAM NAME            0.3
         BE    BB04                A-YES,GO PRINT DD NAME           0.3
         LA    R14,1(R14)          POINT TO NEXT CHARACTER          0.3
         BCT   R12,BB0305          PRINT DD NAME IF END OF PROG.NAME0.3
BB04     MVC   0(4,R14),CCD004     PRINT FILE TYPE TO PRINT LINE    0.3
BB0405   MVI   CCX001A,C'1'        RESET SWITCH
*
*   PRINT FIRST HEADINGS LINE
*   -------------------------
*
BB05     MVC   SCO133,0(R6)
         LATCH RG0203,(SCO133)                                      LAT
*
*   CHECK FOR SECOND LINE PRESENT
*   -----------------------------
*
         CLI   133(R6),C'Z'        Q-IS THERE ANOTHER HEADINGS LINE
         BNE   BB08                A-YES,GO FIND IT
         MVI   SCO133,C' '         A-NO,SET UP
         MVC   SCO133+1(132),SCO133     DUMMY
         B     BB10                CARRY ON
BB08     MVC   SCO133,133(R6)      MOVE SECOND LINE TO STORAGE
*
*   SET UP SHEET NO AND DATE IF NECESSARY
*   ------------------------------------
*
BB10     MVC   SCO133+97(4),CXE004 EDIT SHEET
         ED    SCO133+97(4),CPD002A     NUMBER
         MVC   SCO133+92(5),=C'SHEET'
*
         CLC   SCO133+111(18),=CL18' '  Q. DATE PRESENT
         BNE   BB14                A-YES,BYPASS DATE INSERTION
*
         MVC   SCO133+111(18),SCD018    INSERT DATE
BB14     LATCH RG0203,(SCO133)     PRINT SECOND HEADINGS LINE       LAT
         CLI   133(R6),C'Z'        Q. WAS THERE A SECOND LINE
         BE    BA10                A. NO, GOBACK
*
*
*   ROUTINE TO DEAL WITH FURTHER HEADING LINES
*   ------------------------------------------
*
         ZAP   CPD001,=P'2'        NOTE 2 LINES PRINTED
         LA    R6,266(R6)          POINT R6 TO NEXT HEADINGS LINE
BB20     CLI   0(R6),C'Z'          Q-IS THERE ANOTHER HEADINGS LINE
         BE    BA10                A-NO,GOBACK
*
         AP    CPD001,=P'1'        UPDATE HEADINGS COUNTER
         CP    CPD001,=P'8'        Q-MORE THAN 8 HEADINGS
         BH    BA10                A-YES,GOBACK
*
         MVC   SCO133,0(R6)        PRINT ONE LINE
         LATCH RG0203,(SCO133)          OF HEADINGS                 LAT
         LA    R6,133(R6)          POINT R6 TO NEXT HEADINGS LINE
         B     BB20                GO LOOK AT NEXT LINE
*
***********************************************************************
*   ROUTINE TO UPDATE LINE COUNT BY INTERPRETING ASA CONTROL CHAR     *
***********************************************************************
*
BC02     CLI   0(R7),C' '          Q-ASA CC = SPACE
         BNE   BC04                A-NO,CARRY ON
*
         AP    CPD002,=P'1'        A-YES,ADD 1 TO LINE-COUNT
         BR    R10                      AND GOBACK
*
BC04     CLI   0(R7),C'0'          Q-ASA CC = 0
         BNE   BC06                A-NO,CARRY ON
*
         AP    CPD002,=P'2'        A-YES,ADD 2 TO LINE COUNT
         BR    R10                      AND GOBACK
*
BC06     CLI   0(R7),C'-'          Q-ASA CC = -
         BNE   BC08                A-NO,MUST BE INVALID
         AP    CPD002,=P'3'        A-YES,ADD 3 TO LINE COUNT
         BR    R10                      AND GOBACK
*
BC08     WTO   'B3401T INVALID ASA CC'                               OS
         ABEND 92,DUMP                                               OS
*
***********************************************************************
*   CONSTANTS STORAGE AND WORK AREAS                                  *
***********************************************************************
*
SFD004   DS    F                   POINTER TO PRINT AREA
SCO133   DS    CL133               PRINT LINE
CCX001A  DC    C'0'                FIRST TIME SWITCH FOR HEADINGS
CPD002A  DC    PL2'0'              SHEET NUMBER
CCX001   DC    C'0'                FIRST TIME SWITCH FOR MAIN RTN
SCD018   DS    CL18                DATE
CXE004   DC    X'40202020'         EDIT MASK FOR SHEET NO
CPD001   DC    PL1'2'              HEADINGS COUNTER
SCD001   DS    CL1                 DUMP FOR ASA CC
         DS    0D
SPD008   DS    PL8                 DUMP FOR MAX LINES
CPD002   DC    PL2'0'              LINE COUNTER
CCD004   DC    CL4'/P03'           PRINT FILE TYPE                  0.3
*
         LTORG
         END
