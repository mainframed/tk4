         TITLE 'ZPL1LINK: AN INTERFACE RTN FOR OTHERS CALLING PL1'
*      ZPL1LINK IS AN INTERFACE MODULE WHICH ALLOWS PL1 TO BE CALLED
*
* FROM ASSEMBLER AND PASS A PARAMETER LIST. THE ROUTINE IS USED IN THE
*
* FOLLOWING WAY:-
*
*       CALL ZPL1LINK,(SUB,A,B,C,D),VL
*
* WHERE A, B, C, & D ARE PARAMETERS TO BE PASSED TO THE PL1 MODULE
*
* NAMED SUB. IT IS A REQUIREMENT OF ZPL1LINK THAT SUB IS DECLARED IN
*
* THE ASSEMBLER CALLING MODULE USING AN EXTRN STATEMENT.
*
         EJECT
ZPL1LINK CSECT
         ENTRY  ENVSET
IHEQLSA  DXD   18F   AT LEAST FOR
         INIT  11
         SPACE 2
         SPACE 2
         LR    R3,R1  SAVE PARAMETER LIST ADR.
         LA    R1,4(R3)      RE-ADJUST REG 1 TO POINT TO BEGINNING
*                            OF PROPER PARAMETER LIST
         SPACE
* FIRST TIME THROUGH RTN
         CLI   FIRSTSW,ON
         BNE   INITED IF SECOND OR SUBSEQ. TIME THRU
         CALL  IHESAPB
ENVSET   L     R4,0(R12)  OBTAIN & SAVE PICA OF CALLING MODULE
         ORG   *-2
         DC    QL2(IHEQLSA)
         MVC   LASTPICA,12(R4)
         ST    R12,SAVE12  SAVE PRV POINTER
         L     R15,=V(IHESADA)  GO GET DSA ADDR IN R13
         LA    R0,100    IHESADA REQUIRES REGISTER 0 TO CONTAIN THE    X
                         LENGTH OF THE DSA.
         BALR  R14,R15
         ST    R13,SAVE13
         SPACE
* PL1 ENVIRONMENT SET
INITED   LM    R12,R13,SAVE12  RELOAD PRV & DSA POINTERS
         L     R4,0(R3)  LOAD PL1 SUB EP ADDR.
          CLI   FIRSTSW,OFF  1ST. TIME THRU X?
         BE    SPIE  NO, GO ISSUE SPIE
         MVI   FIRSTSW,OFF ELSE, UNSET FIRST TIME THRU SW & DONT SPIE
         B     DONTSPIE   BECAUSE IHESAPD JUST DID
SPIE     L    R4,=V(IHEERRA)   INTERRUPT ADR
         SPIE  (R4),(1,15)
         SPACE
DONTSPIE LA    R1,4(R3)  LOAD ADDR. OF PARAMETER LIST
         L     R15,0(R3)  LOAD ADDR OF SUBRTN E.P.
         BALR  R14,R15   & GO TO PL1 SUBRTN
         SPACE 3
* RESET SPIE TO RETURN & RETURN TO CALLING NODULE
         L     R1,LASTPICA
         SPIE  MF=(E,(R1))
         LA    R13,C0001  RESTORE PTR TO CURRENT SAVE AREA
         EXIT
         SPACE 3
ON       EQU   1
OFF      EQU   0
LASTPICA DS    F   STORE FOR LAST SPIE
SAVE12   DS    F   SAVE FOR PRV POINTER
SAVE13   DS    F    DSA POINTER
FIRSTSW DC    AL1(ON)  TO INDICATE FIRST TIME THROUGH ROUTINE
         EJECT
IHEMAIN  CSECT
         DC    V(ENVSET)
         END
