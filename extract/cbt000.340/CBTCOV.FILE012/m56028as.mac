         MACRO
&HERE    PRINTLIN  &LOC,&LEN=133,&UNIT=6
.*  THIS MACRO WAS CODED BY JOHN R. EHRMAN, SLAC COMP GROUP, 11/1/67
.*  AT EXECUTION TIME THE PRESENCE OF THE EXTERNAL ENTRY POINT MCRPRTLN
.*  IS REQUIRED.
         LCLB  &LENB,&LENF,&UNITB,&UNITF,&LOCB
&LOCB    SETB  ('&LOC'(1,1) EQ '(')    INDICATE INDIRECT LOCATION SPEC
&LENF    SETB  (T'&LEN NE 'N')    TEST FOR REMOTELY SPECIFIED LENGTH
&LENB    SETB  (&LENF)       SET ADCON PRESENCE BIT ACCORDINGLY
         AIF   (&LENF).A     SKIP IF REMOTE SPECIFICATION
         AIF   (&LEN EQ 133).A    SKIP IF STANDARD LENGTH
&LENB    SETB  1             INDICATE ADCON PRESENCE
.A       ANOP
&UNITF   SETB  (T'&UNIT NE 'N')   TEST FOR REMOTELY SPECIFIED LENGTH
&UNITB   SETB  (&UNITF)
         AIF   (&UNITF).B    SKIP IF REMOTE UNIT
         AIF   (&UNIT EQ 6).B     SKIP IF STANDARD UNIT NUMBER
&UNITB   SETB  1             INDICATE UNIT ADCON PRESENCE
.B       AIF   ('&HERE' EQ '').C   TEST LOCATION FIELD SYMBOL
&HERE    DS    0H            DEFINE LOCATION
.C       CNOP  2,4           ALIGN
         STM   14,15,*+10    SAVE R14, R15
         L     15,*+14       LOAD BRANCH ADDRESS
         BALR  14,15         BRANCH TO PRINT ROUTINE
         DC    2F'0'         SAVE AREA FOR R14,R15
         DC    V(MCRPRTLN)   PRINT ROUTINE ADDRESS
         DC    B'&LOCB&LENB&UNITB',AL3(&LOC)     FLAGS, LOCATION
         AIF   (NOT &UNITB).D     SKIP IF NO UNIT ADCON
         DC    B'&UNITF',AL3(&UNIT)    UNIT SPECIFICATION
.D       AIF   (NOT &LENB).E SKIP IF NO LENGTH ADCON
         DC    B'&LENF.0000000',AL3(&LEN)   LENGTH SPECIFICATION
.E       LM    14,15,0(14)   RESTORE REGISTERS
         SPACE
         MEND
