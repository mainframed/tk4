GRIPEAN  CSECT
**********************************************************************
*      GRIPEAN IS THE ANALYSIS PROGRAM FOR GRIPE.                    *
*       GRIPE WRITES SMF TYPE131 RECORD AND GRIPEAN READS THEM       *
*       CHECKS THE DATE AND TIME AND DRAWS A HISTOGRAM OF            *
*       USERIDS AGAINST TIME.                                        *
*                                                                    *
*    WRITTEN BY JESS HAND           MET.O.12B        30/11/83        *
*    UPDATED  02/04/84  JESS HAND  RETURN ZERO COMPLETION CODE       *
*    UPDATED  07.08/84  L CHAMBERS "NO 131 RECS IN DS" FLAG (ONEREC) *
*                                  "DS EMPTY FLAG" (RECFLAG)         *
*                                                                    *
**********************************************************************
         EQUREG R
         CHAIN
         L     R2,0(R1)
         OPEN  (LIST,(OUTPUT))        OPEN OUTPUT DATASET.
         CLC   0(2,R2),=H'0'          ANY PARAMETERS?
         BE    OPEN                   NO.GO OPEN DATASET.
         MVI   PARAM,X'0F'            PARAMETER FLAG
         MVI   INCR+3,X'01'           DEFAULT INCREMENT OF 1 MIN.
         CLC   0(2,R2),=H'15'         PARM LENGTH OF 15.? NO INCREMENT?
         BL    PARMERR                TOO SHORT. ERROR
         BE    INCD
         PACK  DWRD,18(2,R2)          SPECIFIED INCREMENT.
         CVB   R6,DWRD
         ST    R6,INCR                STORE SPECIFIED INCREMENT.
INCD     LA    R2,2(R2)               SKIP PARAMETER LENGTH
         PACK  DATE,0(5,R2)           DATE OF INTENSIVE ANALYSIS
         CLC   6(4,R2),11(R2)         SECOND TIME LATER?
         BNL   PARMERR                NO.  ERROR
         PACK  DWRD,6(2,R2)           START OF INTENSIVE ANALYSIS.(HRS)
         XR    R6,R6                  CLEAR R6 FOR MULTIPLY
         CVB   R7,DWRD
         M     R6,=F'60'              HOURS TO MINUTES
         PACK  DWRD,8(2,R2)           START OF INTENSIVE ANALYSIS.MINS
         CVB   R6,DWRD                MINS TO BINARY
         AR    R6,R7                  MINUTES SINCE 0000Z.
         ST    R6,DAYSTART            START IN BINARY MINS SINCE 0000Z
         XR    R6,R6                  CLEAR R6 FOR MULTIPLY
         PACK  DWRD,11(2,R2)          END OF INTENSIVE ANALYSIS.(HRS)
         CVB   R7,DWRD
         M     R6,=F'60'              HOURS TO MINS
         PACK  DWRD,13(2,R2)         END OF INTENSIVE ANALYSIS IN MINS
         CVB   R6,DWRD
         AR    R6,R7                  MINUTES SINCE 0000Z.
         ST    R6,DAYEND              END IN MINS SINCE 0000Z.
*
OPEN     OPEN  (SMFDCB,(INPUT))       OPEN DATASET
         GET   SMFDCB                 READ FIRST SMF RECORD
         LR    R2,R1                  ADDRESS OF SMF RECORD
         MVI   RECFLAG,X'FF'          SHOW DS NOT EMPTY
         XR    R3,R3                  CLEAR USERID COUNT
         CLI   PARAM,X'0F'            PARAMETERS?
         BE    NEWDAY                 YES. USE PARAMETER DATE
         MVC   DATE(4),10(R2)         GET DATE.
         MVC   DWRD+4,DATE            STORE DATE FOR LEAP YEAR CHECK.
         DP    DWRD,=P'1000'        REMOVE DAYS.
         DP    DWRD(5),=P'4'           DIVIDE YEAR BY 4
         BNZ   NEWDAY                 NON LEAP YEAR.
         AP    YEAREND,ONED           LEAP YEAR. 366 DAYS A YEAR.
         B     NEWDAY
*      INCREMENT DATE BY 1. TO CHECK EACH DAY.
NXTDAY   AP    DATE,ONED              ADD ONE TO DATE.
         CP    DATE+2(2),YEAREND      END OF YEAR.
         BNH   NEWDAY
         AP    DATE,=P'1000'          ADD ONE TO YEAR.
         MVC   DATE+2(2),=X'001F'     SET TO DAY ONE.
NEWDAY   MVC   START,DAYSTART         RESET START OF TIME PERIOD TIME
         L     R6,START
         A     R6,INCR                INCREMENT FOR END OF PERIOD.
         ST    R6,END                 RESET END OF TIME PERIOD TIME
PARMDATE MVC   DWRD,ZERO              CLEAR DOUBLEWORD WORK AREA
         LH    R6,DATE                EXTRACT YEAR FROM DATE.
         SLL   R6,4
         ST    R6,DWRD+4
         OI    DWRD+7,X'0F'           PUT IN SIGN BITS
         CVB   R6,DWRD
         A     R6,=F'1900'
         ST    R6,YEAR
         MVC   DWRD,ZERO              CLEAR WORK AREA.
         MVC   DWRD+6(2),DATE+2       YEAR DAY.
         CVB   R6,DWRD
         ST    R6,DAYS
         CALL  DATE23,(DAYS,YEAR,DAYS,MONTH,YEAR)  DATE TO DD/MM/YYYY
         L     R6,YEAR
         CVD   R6,DWRD
         OI    DWRD+7,X'0F'           CORRECT SIGN TO NUMBER.
         UNPK  LINE+20(5),DWRD+5(3)   YEAR TO LINE
         MVI   LINE+20,C'/'
         L     R6,MONTH
         CVD   R6,DWRD
         OI    DWRD+7,X'0F'           CORRECT SIGN TO NUMBER.
         UNPK  LINE+17(3),DWRD+6(2)   MONTH TO LINE
         MVI   LINE+17,C'/'
         L     R6,DAYS
         CVD   R6,DWRD
         OI    DWRD+7,X'0F'           CORRECT SIGN TO NUMBER.
         UNPK  LINE+14(3),DWRD+6(2)   DAYS TO LINE.
         MVI   LINE+14,C' '
         MVC   LINE+30(26),=C'ANALYSIS OF GRIPE COMMANDS'
         MVI   LINE,X'F1'             PAGE THROW.
         BAL   R11,PUTLINE            WRITE OUT LINE.
         TM    FLAG,X'0F'             END OF DATASET REACHED.
         BO    NOREC                  YES. NO ENTRIES.
         OI    FLAG,X'F0'             SET "NO RECORD TODAY" FLAG.
         CLI   5(R2),X'83'            HAVE WE A RECORD TYPE 131?
         BE    DAT                    YES. SKIP READ.
*
*
*   ALL SUBSEQUENT READS OF SMF RECORDS ARE DONE HERE.
*   THE TIME IS EXTRACTED AND CHECK FOR THE TEN MINUTE TIME PERIOD
*   AND THE USERID INSERTED ON THE APPROPRIATE LINE.
*   THE RECORDS ARE IN CHRONOLOGICAL ORDER.
*
NEXTREAD GET   SMFDCB
         LR    R2,R1                   ADDRESS OF SMF RECORD.
         CLI   5(R2),X'83'             RECORD TYPE 131?
         BNE   NEXTREAD                NO. READ NEXT RECORD.
DAT      MVI   ONEREC,X'FF'            SHOW DS NOT EMPTY
         CP    DATE,10(4,R2)           CHECK DATE FOR NEXT DAY.
         BL    ENDDAY                  NEXT DAY.FINISH TODAY FIRST.
         BH    NEXTREAD                TOO EARLY. GET NEXT RECORD.
         NI    FLAG,X'0F'              WE HAVE RECORDS FOR TODAY
TIM      MVC   TIME(4),6(R2)
         L     R7,TIME
         XR    R6,R6                   CLEAR R6 FOR DIVIDE.
         D     R6,=F'6000'             TIME TO MINUTES SINCE MIDNIGHT.
         C     R7,START                BEFORE 0800.
         BL    NEXTREAD                YES. READ NEXT RECORD.
TIMEP    C     R7,END                  AFTER END OF THIS 10 MIN SLOT
         BH    NEXTIME                 YES.OUTPUT THIS LINE.
*     WE ARE IN THE RIGHT TIME PERIOD.
*     PUT USERID ON LINE.
*
         LA    R3,1(R3)                UPDATE USERID COUNTER.
         C     R3,=F'19'               END OF LINE
         BH    NEXTREAD                YES. NO MORE USERIDS TO LINE
         MVC   0(6,R8),18(R2)          USERID FROM SMF RECORD.
         LA    R8,6(R8)                UPDATE LINE POINTER.
         B     NEXTREAD                GO GET NEXT RECORD.
*
*    WE HAVE FINISHED WITH THIS TIME SLOT.
*    OUTPUT LINE AND GET NEXT TIME SLOT.
*
NEXTIME  LA    R10,TIMEP
         LA    R9,NEXTREAD             POINTER FOR BRANCH IF END OF DAY
LINEOUT  XR    R4,R4                   CLEAR R4 FOR DIVIDE
         L     R5,END                  END TIME OF PERIOD.
         D     R4,=F'60'               HOURS(R5) AND MINS(R4)
         CVD   R4,DWRD
         OI    DWRD+7,X'0F'           CORRECT SIGN TO NUMBER.
         UNPK  LINE+9(3),DWRD+6(2)     MINS TO LINE.
         CVD   R5,DWRD
         OI    DWRD+7,X'0F'           CORRECT SIGN TO NUMBER.
         UNPK  LINE+7(3),DWRD+6(2)     HOURS TO LINE
         MVI   LINE+7,C'-'
         XR    R4,R4                   CLEAR R6 FOR DIVIDE
         L     R5,START                START TIME OF TIME PERIOD
         D     R4,=F'60'               HOURS(R5) AND MINS(R4)
         CVD   R4,DWRD
         OI    DWRD+7,X'0F'           CORRECT SIGN TO NUMBER.
         UNPK  LINE+4(3),DWRD+6(2)     MINS TO LINE
         CVD   R5,DWRD
         OI    DWRD+7,X'0F'           CORRECT SIGN TO NUMBER.
         UNPK  LINE+2(3),DWRD+6(2)     HOURS TO LINE
         MVI   LINE+2,C' '             BLANK FIRST HOUR FIGURE
         MVI   LINE+13,C')'            EDGE OF GRAPH
         BAL   R11,PUTLINE             OUTPUT LINE
*
*    UPDATE START AND END TIME OF 10 MIN TIME SLOT
*
         L     R4,END                  END OF TIME SLOT
         A     R4,INCR                 15 MIN INCREASE
         ST    R4,END
         CLC   END,DAYEND              END OF DAY REACHED.
         BHR   R9
         L     R4,START                START OF TIME SLOT
         A     R4,INCR
         ST    R4,START
         BR    R10
*
*   NEXT RECORD IS FOR TOMORROW. FINISH OFF TODAYS GRAPH.
*
ENDDAY   CLI   PARAM,X'0F'            PARAMETERS?
         BE    ENDSMF
         TM    FLAG,X'F0'             NO RECORDS FOR THIS DAY?
         BO    NOREC                  NO RECORDS.
         LA    R10,LINEOUT            LOOP ROUND TIL END OF DAY REACHED
         LA    R9,NXTDAY              THEN SET UP FOR NEW DAY.
         B     LINEOUT
*
*     NO SMF TYPE 131 RECORDS FOR THIS DAY.
*     MESSAGE TO INDICATE THIS STATE.
*
NOREC    MVC   LINE+2(23),=C'NO ENTRIES FOR THIS DAY'
         BAL   R11,PUT
         TM    FLAG,X'0F'           END OF DATASET REACHED.
         BNO   NXTDAY NO
         B     NOW
*
*     END OF SMF RECORDS REACHED.
*     IF DATA-SET EMPTY  PRINT OUT MESSAGE THEN END.
*     IF NO 131 RECS IN DATA SET THEN PRINT NORECS MESSAGE
*     FOR EACH DAY THEN END.
*     IF AT LEAST ONE 131 REC IN DATA-SET FINISH OFF
*     CURRENT GRAPH AND PRINT OUT NORECS MESSAGE FOR ANY REMAINING
*     DAYS UP TO TODAY THEN END.
*
ENDSMF   CLI   RECFLAG,X'00'          CHECK EMPTY DATA-SET FLAG
         BE    NOREC2
         CLI   ONEREC,X'00'           CHECK ANY 131 RECS IN DS
         BNE   REC131
         MVC   LINE+2(23),=C'NO ENTRIES FOR THIS DAY'
         BAL   R11,PUT
         B     FINI
REC131   LA    R10,LINEOUT            LOOP TIL END OF TODAY.
         LA    R9,FINI                THEN FINISH.
         B     LINEOUT                OUTPUT LINE AND FINISH DAY.
*
*     DATA-SET EMPTY - PRINT MESSAGE AND FINISH
*
*
NOREC2   MVC   LINE+2(37),=C' DATA SET EMPTY,NO RECORDS TO ANALYSE'
         BAL   R11,PUT      BRANCH TO OUPUT LINE THEN FINISH
         B     FINISH
*
*     ERROR IN PARAMATERS. FORMAT SHUOLD BE YYDDD/HHMM YYDDD/HHMM
*
PARMERR  MVC   LINE+5(21),0(R2)       PARAMETERS INTO LINE.
         MVC   LINE+40(19),=C'ERROR IN PARAMETERS'
         BAL   R11,PUTLINE
*
*    WE HAVE REACHED THE END OF THE DATASET.
*    NOW LOOP THRO' NO ENTRIES LOOP TIL YESTERDAY.
*
FINI     CLI   PARAM,X'0F'             PARAMETERS ENTERED?
         BE    FINISH                  YES. ALL DONE.
*   GET TODAYS DATE/TIME
         TIME
         ST    R1,TODAY                TODAYS DATE.
         SP    TODAY,ONED              SUBTRACT ONE FOR YESTERDAY.
         OI    FLAG,X'0F'              SET END OF DATASET FLAG.
NOW      CP    DATE,TODAY              NOT YET REACHED TODAY.
         BL    NXTDAY                  NO.
*
*    CLOSE DATASET AND RETURN
*
FINISH   CLOSE SMFDCB
         CLOSE LIST
         RETURN13 RC=0
*
*   ROUTINE TO OUTPUT LINE TO LISTING,CLEAR LINE,AND RESET POINTER.
*
PUTLINE  C     R3,ZERO               NO USERIDS IN THIS LINE.
         BE    PUT
         CVD   R3,DWRD               )
         OI    DWRD+7,X'0F'          )  NUMBER OF USERIDS WHO HAVE
         UNPK  LINE+129(3),DWRD+6(2) )  GRIPED IN TIME PERIOD TO LINE.
         MVI   LINE+129,C' '         )
PUT      PUT   LIST,LINE
         MVI   LINE,C' '
         MVC   LINE+1(132),LINE        CLEAR LINE.
         LA    R8,LINE+14              RESET POINTER
         XR    R3,R3                   CLEAR USERID COUNTER
         BR    R11
*
*    DCBS AND STORAGE.
*
LIST     DCB   DDNAME=LIST,MACRF=PM,BLKSIZE=133,LRECL=133,RECFM=FBA,   *
               DSORG=PS
SMFDCB   DCB   DDNAME=SMF,MACRF=GL,BFTEK=A,RECFM=VBS,                  *
               SYNAD=FINI,EODAD=ENDSMF,DSORG=PS
*
ZERO     DC    D'0'
DWRD     DC    D'0'
TODAY    DC    F'0'
ONED     DC    P'1'        DECIMAL ONE
YEAREND  DC    P'365'
HOURS    DC    F'0'
MINS     DC    F'0'
DATE     DC    F'0'
TIME     DC    F'0'
DAYS     DC    F'0'
MONTH    DC    F'0'
YEAR     DC    F'0'
START    DC    F'0'
END      DC    F'0'
DAYSTART DC    F'480'
DAYEND   DC    F'1050'
INCR     DC    F'15'
PARAM    DC    X'00'           PARAMETERS.
FLAG     DC    X'F0'          "NO RECORDS TODAY" FLAG
RECFLAG  DC    X'00'          "DATA-SET EMPTY" FLAG
ONEREC   DC    X'00'          "NO 131 RECS IN DS" FLAG
SMFREC   DS    CL26
LINE     DC    CL133' '
         END
