OVLTRACE CSECT
*        THIS CSECT IS A FRONT END TO IEWSZOVR, THE RESIDENT OVERLAY
*              SUPERVISOR, IT RECORDS THE LOADING OF SEGMENTS.
*              FOR EACH CALL WHICH ISSUES AN SVC45 AN 8 BYTE RECORD
*              IS WRITTEN TO THE FILE TSGTRACE(DDNAME=OVTRACE).
*              THE RECORDS ARE AS FOLLOWS:
*                  BYTE 1 LENGTH 1:
*                  FIRST BYTE IS THE LOW ORDER BYTE OF THE SEGTAB
*                                      ENTRY FOR THE CALLED SEGMENT
*                  BYTE 2,3,4 GIVE THE ADDRESS IN REGISTER 14
*                  BYTE 5 GIVES THE TARGET SEGMENT NUBER
*                  BYTE 6,7,8 GIVE THE TARGET ADDRESS
*
*              R1 -> ENTAB ENTRY
*              R2 -> +=SEGWT, -=CALL/BRANCH
*              R6 -> REGISTER SAVE AREA  (NOT USED HERE)
*              R8 -> CVT
*              R9 -> REQUESTED SEGMENT
*              R10 = 0
*              R12 -> SEGTAB
*
*  TO LINKEDIT AND EXECUTE THE PROGRAM TO BE TRACEDJCL SIMIALR TO THE
*  FOLLOWING EXAMPLE IS REQUIRED....
*        //LKED.IEW DD DSN=SYS1.LINKLIB,DISP=SHR
*        //LKED.SYSLMOD DD DSN=&&STEP,DISP=(,PASS),...
*        //LKED.SYSLIN  DD  *
*          INCLUDE  IEW(IEWSZOVR)
*          NAME  IEWSZOVR(R)
*        /*
*        //B  EXEC  PGM=OVERLAY    (THE PROGRAM TO BE TRACED)
*        //STEPLIB  DD  DSN=&&STEP,DISP=(SHR,PASS)  (CONTAINING OUR
*                                      MODIFIED IEWSZOVR)
*
         BALR  10,0                    ESTABLISH BASE
         USING *,10
         STM   13,1,FIRSTSAV
         LA    13,HERESAVE
         TM    TSGTRACE+48,X'10'       TSGTRACE OPEN?
         BO    TAKEREAD                YES
         OPEN (TSGTRACE,(OUTPUT))      NO
TAKEREAD EQU   *
         PUT   TSGTRACE                GET RECORD SPACE
         LR    0,1                     SAVE BUFFER POINTER
         L     1,0(8,0)                -> NEXT & CURRENT
         L     1,4(1)                  -> TCB
         L     1,0(1)                  -> RB(IEWSZOVR)
         L     1,28(1)                 -> SVRB
         LM    14,15,88(1)             RESTORE PROGS R14 & 15
         LR    1,0                     RESTORE BUFFER POINTER
         SR    14,12                   OFFSET (14)
         ST    14,0(1)                 STORE PROG R14
         L     15,4(15)                LOAD TARGET ADDRESS FROM ENTAB
         SR    15,12                   OFFSET (15)
         ST    15,4(1)                 STORE
         STC   9,4(1)                  STORE SEGMENT REQUEST
         LA    14,0(9,9)
         AR    14,14                   R14 = 4 X SEGNO
         MVC   0(1,1),23(14)           SEGMENT STATUS -> BUFFER
         LM    13,1,FIRSTSAV
         L     15,=V(IEWSXOVR)         -> RESIDENT OVLY SUPERVISOR
         SR    10,10                   RESTORE R10
         BR    15                      GO TO OVLY SUPERVISOR
         PRINT NOGEN
TSGTRACE DCB   BFALN=F,RECFM=FB,DDNAME=OVTRACE,DSORG=PS,MACRF=PL,      ,
               BLKSIZE=200,LRECL=8
         PRINT GEN
FIRSTSAV DC    5F'0'
HERESAVE DC    18F'0'
         END
