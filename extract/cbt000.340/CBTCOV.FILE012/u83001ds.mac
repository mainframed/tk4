ZZAAAD   BEGIN BR=(12),SAVE=SAVEAREA
         LM    6,7,0(1)
*
** 1ST TIME THROUGH OPEN FOR INPUT
*
BA02     EQU   *
         BC    0,BA04
         OI    BA02+1,X'F0'
         OPEN (PDSDATA,(INPUT))
         LA    2,PDSDATA
         USING IHADCB,2
         LH    3,DCBBLKSI
         STH   3,0(,6)
         LH    3,DCBLRECL
         STH   3,2(,6)
         B     BA90
*
** AFTER 1ST TIME THROUGH CHECK IF CLOSE REQUIRED
*
BA04     EQU   *
         CLI   3(7),X'FF'
         BE    BA80
         CLC   CCW008,0(7)
         BE    BA10
         L     3,8(,7)
         ST    3,SFD004
         MVC   CCW008,0(7)
         POINT PDSDATA,SFD004
BA10     EQU   *
         BAL   10,B5001            READ A RECORD
         B     BA90                GO   EXIT
BA80     EQU   *
         CLOSE PDSDATA
BA90     EQU   *
         L     13,4(,13)
         LM    14,12,12(13)
         BR    14
         EJECT
*
** SUBROUTINE  B5001 - READ A BLOCK AND RETURN RECORD ADDRESS
*
B5001    EQU   *
         LM    3,5,CFD004A
         CR    4,5
         BL    B5003
         XC    DECB1,DECB1
         READ  DECB1,SF,PDSDATA,CXWXXX,'S'
         CHECK DECB1
         L     14,DECB1+16
         LA    2,PDSDATA
         USING IHADCB,2
         LH    15,DCBBLKSI
         SH    15,14(,14)
         LA    14,CXWXXX
         LA    14,0(14,15)
         ST    14,CFD004C
         LR    5,14
         LA    4,CXWXXX
B5003    EQU   *
         ST    4,0(,6)
         AH    4,=H'80'
         STM   3,5,CFD004A
         BR    10
B5009    EQU   *
         XC    0(4,6),0(6)
         LA    4,CXWXXX
         AH    4,=Y(12960)
         STM   3,5,CFD004A
         BR    10
         SPACE 4
*
** CONSTANTS,STORAGE AREAS AND LITERALS
*
PDSDATA  DCB   DDNAME=PDSDATA,MACRF=RP,EODAD=B5009,DSORG=PS
SAVEAREA DS    18F
CFD004A  DC    A(CXWXXX)
CFD004B  DC    A(CXWXXX)
CFD004C  DC    A(CXWXXX)
SFD004   DS    F
CCW008   DC    CL8' '              MEMBER NAME SAVE AREA
         LTORG
CXWXXX   DS    12960XL1            RECORD AREA
         DCBD  DSORG=PO,DEVD=DA
         END
