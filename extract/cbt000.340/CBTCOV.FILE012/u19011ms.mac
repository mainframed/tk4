         TITLE ' SUBRTNE 12 - OBTAINS TODAYS DATE IN 3 DIFFER. FORMS  '
B1201    CSECT
         ENTRY B1201O2             INSERT MODULE NUMBER CHANGING
B1201O2  EQU   *                   PROGRAMMER CODE TO V.M. LEVEL
         PRINT NOGEN
         COPY  REGEQU
         SPACE
         SPACE
         SPACE
***********************************************************************
*                                                                     *
*  MODULE B1201X                                                      *
*  PROGRAMMER                         DATE ASSIGNED  16/04/70         *
*                                                                     *
*  MODULE FUNCTIONS  OBTAINS DATE USING TIME MACRO AND TRANSLATES     *
*  ----------------  INTO 1 OF 3 FORMS -                              *
*                    1. EDITED DATE IN 8 BYTES - DD.MM.YY             *
*                    2. EDIT DATE IN 18 BYTES DDMONTHYYYY             *
*                    3. PACKED DATE YYDDD SIGN - AS IN H.B.S. YEAR    *
*                    WHERE 1 FEB = 001 DAYS                           *
*                     ALL LEAP YEARS ARE ALLOWED FOR.                 *
*                                                                     *
*                                                                     *
*                                                                     *
*  PARAMETERS  NO. I/O                DESCRIPTION                     *
*  ----------  --- ---                -----------                     *
*                                                                     *
*              1.  I    LENGTH OF OUTPUT FIELD - 8 FOR DD.MM.YY,      *
*                       18 FOR DDMONTHYYYY AND 3 FOR PACKED DATE.     *
*              2.  O    OUTPUT FIELD.                                 *
*                                                                     *
*              3.       NONE                                          *
*                                                                     *
*              4.       NONE                                          *
*                                                                     *
*              5.       NONE                                          *
*                                                                     *
*                                                                     *
*  SPECIAL NOTES                                                      *
*  -------------                                                      *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*     AMENDMENT TO MODULE B1201    AMENDED BY  P.A.D.  19/05/71   O.S.*
*                                                                     *
*     MODULE  READS CONTROL CARD CONTAINING DATE IN THE FORMAT        *
*     YYDDD . THIS DATE REPLACES THE MACHINE DATE AND IS PASSED       *
*     BACK TO THE CALLING MODULE. IF NO CONTROL CARD IS SUBMITTED     *
*     OR DUMMY CODED IN DD CARD, THEN THE DATE IS OBTAINED USING      *
*     THE TIME MACRO IN THE NORMAL MANNER.                            *
*                                                                     *
***********************************************************************
*
**  AMENDMENT TO MODULE B1201    BY S WELLOCK
*
**  ASSIGNED 30/11/72, EFFECTIVE 04/12/72 AMENDMENT IS 0.2
*
**  DETAILS OF AMENDMENT MODULE AMENDED TO ALLOW FOR LEAP YEAR WHEN
*   -------------------- ACCEPTING A DATE FROM A TIME CARD. PREVIOUSLY
*                        JAN DATES IN A YEAR FOLLOWING A LEAP YEAR
*                        WERE INCORRECT (1 DAY LOW)
*
***********************************************************************
         ISEQ  73,80
         EJECT
         MODIN           SAVE, LOAD BASE, LOAD PARAM REGS,
*                        POINT TO SAVE AREA (DEFINED IN MACRO)
*                                                                    OS
BA01     BC    0,BA02              NOTE... 1ST TIME THROUGH ONLY     OS
         OI    BA01+1,X'F0'        ------- RESET TO BC 15,BA02       OS
         OPEN  (TIME)                                                OS
         GET   TIME,SCW080         READ CONTROL CARD                 OS
BA0101   CLOSE (TIME)                                                OS
BA02     BC    0,BA05              NOTE... SET TO BC 15 IF MACHINE   OS
*                                  ------- TIME & DATE REQUIRED      OS
BA0201   EQU   *                                                     OS
         BC    0,BA06              NOTE...  1ST TIME THROUGH ONLY SETOS
         OI    BA0201+1,X'F0'      -------  TO BC 15,BA06.           OS
*                                                                    OS
         CLI   SCW080,C'0'         Q.VALID CONTROL CARD              OS
         BE    BA04                A.YES                             OS
*                                                                    OS
**       INVALID CONTROL CARD                                        OS
*        --------------------                                        OS
         WTO   'B1201T - INVALID CONTROL CARD'                       OS
         ABEND 92,DUMP                                               OS
*                                                                    OS
**       EODAD ON TIME - MEANS NO CONTROL CARD                       OS
*        -------------------------------------                       OS
BA03     EQU   *                                                     OS
         OI    BA02+1,X'F0'        SET BC INST. TO USE MACHINE VALUESOS
         B     BA0101              GO CLOSE FILE                     OS
*                                                                    OS
BA04     PACK  SFD004,SCW080+1(5)  GET DATE IN CORRECT FORMAT        OS
         B     BA06                     GO PROCESS AS NORMAL         OS
*                                                                    OS
BA05     EQU   * NORMAL BRNCH POINT TO PREVIOUS VERSION OF B1201     OS
         TIME
         ST    R1,SFD004
BA06     EQU   * BRANCH POINT USED WHEN NONSTANDARD DATE IS REQUIRED OS
         LA    R1,SFD004
         C     R6,=F'3'       Q. - INPUT PARAM = 3 PACKED DATE
         BE    B1202          YES.
         C     R6,=F'8'       Q  - INPUT PARAM = 8  DD.MM.YY REQ'D
         BE    B1205          YES.
         C     R6,=F'18'      Q-   INPUT PARAM = 18 DD.MONTH.19YR REQ'D
         BE    B1211          YES.
         ABEND 92,DUMP
*
***********************************************************************
*                                                                     *
*  DATE IN PACKED FORM - YYDDS.                                       *
*  ----------------------------                                       *
*  TEST DAYS.  - IF LESS THAN 31 THEN H.B.S YEAR IS PREVIOUS ONE      *
*  ---------   - IF GREATER THAN 31 THEN ADJUST SO START OF YEAR      *
*                IS 1ST FEB.                                          *
*                                                                     *
***********************************************************************
*
B1202    ZAP   SPW003,1(3,R1)      STORE DATE FROM R1
         CP    SPW003+1(2),=P'32'  Q. DAYS LESS THAN 32.
         BNL   B1203               NO-H.B.S YEAR IS SAME.
         SP    SPW003,=P'666'      YES- SUBTRACT 666 FOR HBS YR
         BAL   R10,B1250           GO CHECK FOR LEAP YEAR           0.2
         CP    CCT024+2(2),=PL2'29'     Q. LEAP YR FOUND            0.2
         BNE   B1204                    A. NO, OK                   0.2
         AP    SPW003,=P'1'             ALLOW FOR 29TH FEB.         0.2
         B     B1204               SET UP OUTPUT P2
B1203    SP    SPW003,=P'31'       NO - ADJUST DAYS SO  START OF YEAR
*                                       IS 1ST FEB
B1204    MVC   0(3,R7),SPW003      MOVE DATE TO PARAM 2.
         B     B1299               RETURN TO CALLING ROUTINE.
*
***********************************************************************
*                                                                     *
*  DATE IN THE FORM DD.MM.YY.                                         *
*  --------------------------                                         *
*                                                                     *
***********************************************************************
*
*
B1205    ZAP   SPW003,1(3,R1)           STORE DATE
         UNPK  CCW080+6(5),SPW003(3)    UNPACK YEAR
         OI    CCW080+10,X'F0'
         BAL   R10,B1250                GO TO CHECK FOR LEAP YEAR
         ZAP   CPC002,=P'1'             SET MONTH = 1
         LA    R2,CCT024                SET ADDRESS OF DAYS IN MONTHS.
*
B1206    CP    SPW003+1(2),0(2,R2)      Q - DAYS GREATER THAN DAYS
         BH    B1207                         IN NEXT MONTH
         UNPK  CCW080+3(2),CPC002       NO -  UNPACK MONTH
         OI    CCW080+4,X'F0'
         UNPK  CCW080(2),SPW003+1(2)          UNPACK DAYS.
         OI    CCW080+1,X'F0'
         ZAP   CCW004,=P'0'
         B     B1208               YES -
B1207    SP    SPW003(3),0(2,R2)        SUBT MONTH DAYS FROM DATE
         AP    CPC002,=P'1'             ADD 1 TO MONTH NO
         LA    R2,2(R2)
         B     B1206
*
**  REMOVE LEADING ZEROS FROM DAYS AND MONTH.
**  -----------------------------------------
*
B1208    CLI   CCW080,C'0'              Q. 1ST DIGIT OF DAY ZERO
         BNE   B1209
         MVI   CCW080,C' '              YES. SET TO SPACE.
B1209    CLI   CCW080+3,C'0'            Q. 1ST DIGIT OF MONTH ZERO.
         BNE   B1210
         MVI   CCW080+3,C' '            YES. SET TO SPACE.
B1210    MVC   0(8,R7),CCW080           MOVE DATE TO PARAM 2.
         B     B1299
*
***********************************************************************
*                                                                     *
* DATE IN THE FORM  DD. MONTH. 19YR                                   *
*                                                                     *
***********************************************************************
*
B1211    ZAP   SPW003,1(3,R1)           STORE DATE
         UNPK  SCW018+15(5),SPW003(3)   UNPACK YEAR INTO SCW018
         BAL   R10,B1250                TEST FOR LEAP YEAR
         ZAP   CPC002,=P'1'             SET MONTH = 1
         LA    R2,CCT024                ADDRESS OF DAYS IN MONTHS TABLE
         MVI   SCW018+17,C' '           CLEAR LAST POS'N OF SCW018
B1212    CP    SPW003+1(2),0(2,R2)      Q. DAYS GREATER THAN CURR MONTH
         BH    B1213                    YES. DATE IS NOT IN THIS MONTH
         UNPK  SCW018(2),SPW003+1(2)    NO. UNPACK DAYS INTO SCW018
         OI    SCW018+1,X'F0'               FORCE ZONE = F
         B     B1214
B1213    SP    SPW003(3),0(2,R2)
         AP    CPC002,=P'1'             INCREASE MONTH BY 1
         LA    R2,2(R2)
         B     B1212
*
B1214    CLI   SCW018,C'0'              Q - 1ST DIGIT OF DAY = 0
         BNE   B1215
         MVI   SCW018,C' '              YES - REPLACE WITH SPACE.
*
B1215    LA    R4,CXT013                ADDRESS OF MONTH LENGTHS.
         LA    R2,CCT074                ADDRESS OF MONTH NAMES.
*
B1216    MVC   CFW004+3(1),0(R4)        STORE LENGTH OF MONTH.
B1217    SP    CPC002,=P'1'             SUBT 1 FROM MONTH CTR
         BZ    B1218                    Q - MONTH CTR ZERO.
         A     R2,CFW004                NO - ADJUST R2 FOR NEXT MONTH.
         LA    R4,1(R4)                      INCREASE R4 FOR NEXT MNTH.
         B     B1216
*
B1218    SR    3,3                      CLEAR R3.
         IC    3,0(R4)                  INSERT LENGTH IN R3
         SH    3,=H'1'                  SUBT 1 FOR INSTRUCTION LENGTH
         EX    3,B1220                  EXECUTE MOVE.
         LA    9,SCW018+5
         AR    9,3                      FIND POSITION OF YEAR.
         MVC   0(2,R9),=C'19'           MOVE IN CENTURY
         MVC   2(2,R9),SCW018+15        MOVE IN YEAR
         MVC   4(8,R9),=C'        '     CLEAR REST
*
         MVI   0(R7),C' '
         MVC   1(17,R7),SCW018
         B     B1299
*
B1220    MVC   SCW018+3(1),0(R2)
B1299    ZAP   CCT024+2(2),=PL2'28'
         MODOUT
*
**   CHECK FOR LEAP YEAR.
**   --------------------
*
B1250    MVO   CCW004(4),SPW003(1)      SET UP YEAR  AS  YYS
         OI    CCW004+3,X'0F'
         DP    CCW004(4),=P'4'          DIVIDE BY 4
         CP    CCW004+3(1),=P'0'        Q. REMAINDER = 4
         BNE   B1251                    NO.
         ZAP   CCT024+2(2),=PL2'29'     YES - SET FEB CONSTANT = 29.
B1251    BR    R10
*
**   CONSTANTS  AND STORAGE FOLLOW.
**   ------------------------------
*
SPW003   DS    CL3            PACKED DATE  YYDDDS
CCW080   DC    C'  .  .     ' UNPACKED DATE  DD.MM.YY
CPC002   DC    PL2'0'         MONTH NO
CCT024   DC    X'031C028C031C030C031C030C031C031C030C031C030C031C'
CCW004   DC    XL4'00'
SCW018   DC    CL26' '
CCT074   DC    C'JANUARYFEBRUARYMARCHAPRILMAYJUNEJULYAUGUSTSEPTEMBER'
         DC    C'OCTOBERNOVEMBERDECEMBER'
CXT013   DC    X'070805050304040609070808'
CFW004   DC    F'0'
SFD004   DS    F
*
SCW080   DS    CL80                CONROL CARD AREA                  OS
*                                                                    OS
**       DCB FOR TIME CONTROL CARD                                   OS
*        -------------------------                                   OS
TIME     DCB   DSORG=PS,DDNAME=TIME,MACRF=(GM),LRECL=80,BUFNO=1,     OSX
               EODAD=BA03,BLKSIZE=80,RECFM=F                         OS
         LTORG
         END
