         MACRO
         WSFREGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
         MACRO
&SYMBOL  COMMAND &XCTLNAM,&COMM1,&COMM2,&DATA
         GBLA  &TOT
         LCLC  &L
.*
.*       THIS MACRO IS USED EXCLUSIVELY WITH THE WSCC MASTER
.*       SCHEDULER ROUTER MODULE 'SYSMS01'.
.*       IT IS USED TO SET UP THE COMMAND NAME CONSTANTS.
.*
         AIF   ('&XCTLNAM' EQ 'TOTAL' AND '&COMM1' EQ '').TOTAL
         AIF   ('&XCTLNAM' EQ '' OR '&COMM1' EQ '').ERROR1
&L       SETC  'L'''
&TOT     SETA  &TOT+1
         DC    CL8'&XCTLNAM'
         AIF   ('&DATA' NE '').DATA
         DC    X'00' .             USER DATA
         AGO   .RES
.DATA    DC    XL1'&DATA' .           USER DATA
.RES     DC    X'00'
         DC    AL1(&L.IHB&SYSNDX.P)
IHB&SYSNDX.P DC C'&COMM1'
         AIF   ('&COMM2' EQ '').NOSEC
         DC    AL1(&L.IHB&SYSNDX.S)
IHB&SYSNDX.S DC C'&COMM2'
         MEXIT
.NOSEC   DC    AL1(0) .            NO SECONDARY COMMAND
         MEXIT
.TOTAL   ANOP
&SYMBOL  DC    H'&TOT'
         MEXIT
.ERROR1  MNOTE 8,'ERROR - XCTL-NAME AND/OR COMMAND1 MISSING'
.END     MEND
         TITLE 'SYSMS01 - WSCC MASTER SCHEDULER - ROUTER MODULE'
         ISEQ  73,80
         SPACE 2
***********************************************************************
*                                                                     *
*        WSCC MASTER SCHEDULER.                                       *
*                                                                     *
*        THIS IS THE FIRST MODULE OF WEST SUSSEX COUNTY COUNCIL'S     *
*                                                                     *
*        MASTER SCHEDULER AND PERFORM COMMAND ROUTINE.  IF A NONE     *
*                                                                     *
*        WSCC COMMAND IS ENCOUNTERED, CONTROL IS PASSED TO IGC0403D.  *
*                                                                     *
*                                                                     *
*        TO INSTALL THIS ROUTER MODULE THE FOLLOWING ZAP TO 21.6 IS   *
*                                                                     *
*        NECESSARY:                                                   *
*                                                                     *
*         NAME IGC5403D                                               *
*         VER 023E  C9C7C3F0F4F0F3C4         IGC0403D                 *
*         REP 023E  E2F8E2D4E2F0F140         SYSMS01                  *
*                                                                     *
*                                                                     *
*        WRITTEN BY CHRIS HUTCHINS, OCTOBER 1973,                     *
*                                                                     *
*        WEST SUSSEX COUNTY COUNCIL SYSTEMS PROGRAMMING GROUP.        *
*                                                                     *
***********************************************************************
         EJECT
SYSMS01  CSECT
         SPACE 1
         WSFREGS
BASER    EQU   3
COMMR    EQU   12
WORKLTH  EQU   36
         SPACE 1
         BALR  BASER,0
         USING SYSMS01+2,BASER
         B     *+12
         DC    CL8'SYSMS01'
         LR    COMMR,R1            LOAD COMMAND ->
         L     R0,FREELTH          LOAD LENGTH
         BAL   R1,*+4              INDICATE GETMAIN
         SVC   10                  ISSUE GETMAIN
         ST    R2,0(R1)            SAVE
         STM   R4,R11,4(R1)             REGISTERS
         LR    R11,R1              SAVE ->
         LR    R4,R3               SAVE R3
         BAL   R6,GET              GET COMMAND INFO.
         LR    R3,R4               RESTORE R3
*
*        ON RETURN:
*
*              R7  CONTAINS PRIME LENGTH
*              R8  CONTAINS PRIME ADDRESS
*              R9  CONTAINS SECONDARY LENGTH
*              R10 CONTAINS SECONDARY ADDRESS
*
         LTR   R7,R7               COMMAND ENTERED?
         BZ    EXIT                NO, THEN EXIT
         SPACE 5
         LH    R6,COMMANDS         LOAD OFFSET OF NUMBER OF COMMANDS
         LH    R6,COMMANDS(R6)     LOAD NUMBER OF COMMANDS
         LA    R5,COMMANDS+2       LOAD ADDRESS OF FIRST COMMAND ENTRY
*
*        SCAN FOR COMMAND MATCH
*
SCAN     CLM   R7,B'0001',10(R5)   PRIME LENGTHS EQUAL?
         BNE   NEXT                NO, THEN TRY NEXT ENTRY
         LR    R1,R7               LOAD LENGTH INTO R1
         BCTR  R1,0                DECREMENT FOR EXECUTE
         EX    R1,CLCP             CLC  0(0,R8),11(R5)
         BNE   NEXT                BRANCH IF COMMANDS NOT EQUAL
         LA    R2,11(R7,R5)        POINT TO SECONDARY COMMAND IN TABLE
         CLM   R9,B'0001',0(R2)    SECONDARY LENGTHS EQUAL?
         BNE   NEXT                NO, THEN TRY NEXT ENTRY
         LR    R1,R9               LOAD LENGTH INTO R1
         BCTR  R1,0                DECREMENT FOR EXECUTE
         EX    R1,CLCS             CLC 0(0,R10),1(R2)
         BE    FOUND               BRANCH IF COMMANDS EQUAL
         SPACE 3
*
*        UPDATE TO NEXT COMMAND TABLE ENTRY
*
NEXT     SLR   R1,R1               CLEAR R1
         LA    R5,10(R5)           ADD STANDARD LENGTH
         LA    R2,2
SECADD   IC    R1,0(R5)            LOAD LENGTH
         AR    R5,R1               ADD TO TOTAL
         LA    R5,1(R5)            ADD LENGTH FIELD LENGTH
         BCT   R2,SECADD
         BCT   R6,SCAN
         SPACE 4
*
*        NONE WSCC COMMAND - XCTL TO IGC0403D
*
EXIT     LA    R5,IGC0403D
         EJECT
*
*        EXIT TO WSCC MODULE IF COMMAND MATCH FOUND ELSE EXIT TO
*        NEXT SVC 34 MODULE
*
FOUND    EQU   *
*        POINT TO EXTENDED SAVE AREA
         L     R4,16
         L     R4,0(R4)
         L     R4,4(R4)
         L     R4,0(R4)
         LA    R4,96(R4)
*        BUILD XCTL TABLE
         LA    R1,8(R4)            ADDRESS OF
         ST    R1,0(R4)                       XCTL N5AME
         XC    4(4,R4),4(R4)       DCB PTR (0)
         MVC   8(8,R4),0(R5)       XCTL NAME
         ICM   COMMR,B'1000',8(R5)      INSERT USER DATA
*        FREE CORE
         LA    R1,0(R11)           CLEAR HIGH ORDER BYTE AND LOAD R1
         L     R0,FREELTH
         DROP  BASER
         LR    BASER,R4
         L     R2,0(R1)            RESTORE
         LM    R4,R11,4(R1)                REGS
         SVC   10                  ISSUE FREEMAIN SVC
         LR    R15,BASER           MOVE XCTL ->
         LR    R1,COMMR            MOVE COMMAND ->
         SVC   7                   ISSUE XCTL SVC
         EJECT
*
*        SCAN COMMAND ROUTINE
*
GET      BALR  R13,0
         USING *,R13
         SLR   R7,R7               CLEAR
         SLR   R8,R8                     ALL
         SLR   R9,R9                         OUTPUT
         SLR   R10,R10                              REGISTERS
         LA    R2,1                LOAD INCREMENT
         LA    R1,4(COMMR)         LOAD COMMAND ADDRESS
         LR    R3,R1               LOAD INTO R3
         AH    R3,0(COMMR)         ADD DATA LENGTH
         SH    R3,KH5              R3 NOW POINTS TO LAST BYTE OF BUFFER
LOOP1    CLI   0(R1),C' '          BEGINNING OF COMMAND?
         BNE   FIRSTCAR            YES, THEN PROCESS PRIMARY
         BXLE  R1,R2,LOOP1
         BR    R6                  RETURN IF NO COMMAND
FIRSTCAR LR    R8,R1               LOAD PRIMARY ADDRESS
LOOP2    CLI   0(R1),C' '          END OF COMMAND?
         BE    PRIMEND             YES, THEN PROCESS SECONDARY
         CLI   0(R1),X'00'
         BE    PRIMEND
         BXLE  R1,R2,LOOP2
         LR    R7,R1               CALCULATE PRIMARY
         SR    R7,R8                                LENGTH
         BR    R6                  RETURN, NO COMMAND
PRIMEND  LR    R7,R1               CALCULATE PRIMARY
         SR    R7,R8                                 LENGTH
LOOP3    CLI   0(R1),C' '          BEGINNING OF SECONDARY COMMAND?
         BNE   SECSTART            YES, THEN PROCESS SECONDARY
         BXLE  R1,R2,LOOP3
         BR    R6                  RETURN, NO SECONDARY COMMAND
SECSTART LR    R10,R1              LOAD SECONDARY ADDRESS
LOOP4    CLI   0(R1),C' '          END OF COMMAND?
         BE    SECEND
         CLI   0(R1),X'00'
         BE    SECEND
         BXLE  R1,R2,LOOP4
SECEND   LR    R9,R1               CALCULATE SECONDARY
         SR    R9,R10                                  LENGTH
         BR    R6
         DROP  R13
         EJECT
*
*        EXECUTED INSTRUCTIONS
*
CLCP     CLC   0(0,R8),11(R5)
CLCS     CLC   0(0,R10),1(R2)
         EJECT
*
*        CONSTANTS
*
FREELTH  DS    0F
         DC    AL1(255)            SUBPOOL
         DC    AL3(WORKLTH)        LENGTH
KH5      DC    H'5'
IGC0403D DC    CL8'IGC0403D'
         EJECT
*
*        COMMAND TABLE
*
*        THIS TABLE DEFINES THE WEST SUSSEX COUNTY COUNCIL COMMANDS.
*        EACH COMMAND IS DEFINED BY THE USE OF THE 'COMMAND' MACRO.
*        IT IS CODED AS FOLLOWS:
*
*        COMMAND XCTL-MODULE-NAME,COMMAND1,COMMAND2,USER-DATA
*
*        WHERE:
*
*           XCTL-MODULE-NAME IS THE NAME OF THE MODULE TO BE XCTL'ED
*                            TO WHEN A COMMAND MATCH IS FOUND.
*           COMMAND1         IS THE PRIMARY COMMAND, E.G. D
*           COMMAND2         IS THE SECONDARY COMMAND, E.G. PART
*           USER-DATA        IS TWO HEX DIGITS OF USER DATA TO BE
*                            PLACED IN THE HIGH-ORDER BYTE OF R1 WHEN
*                            XCTL'ING. THE DEFAULT IS 00.
*
*           NOTE: COMMAND2 & USER-DATA ARE OPTIONAL.
*
*        EXAMPLE:
*
*
*        FOR COMMAND 'D PART' WITH NO USER-DATA THE FOLLOWING WOULD BE
*        CODED:
*
*        COMMAND SYSMS02,D,PART
*
         SPACE 4
COMMANDS DC    AL2(COMMTOTS-*)     OFFSET  OF NO. OF ENTRIES
         SPACE 1
         COMMAND SYSMS02,D,PART    'D PART'
         COMMAND SYSMS02,D,P
*
*
*
*
*
*
*
*
*
*        FURTHER COMMANDS MAY BE PLACED ERE
*
COMMTOTS COMMAND TOTAL
         EJECT
         END
