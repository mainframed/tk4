AASLIB   CSECT
         SPACE  3
*  THIS PROGRAM IS INTENDED TO CHECK ANY PDS (SOURCE OR LOAD MODULE)
*  FOR I/O ERRORS OR OTHER FORMS OF CORRUPTION.
         SPACE
*  IT FIRST READS THE DIRECTORY AND SORTS IT TO TTR SEQUENCE (THERE IS
*  A LIMIT OF 1000 MEMBERS, AND AN ABEND USER 1 WILL OCCUR IF THIS IS
*  EXCEEDED, BUT THIS LIMIT CAN BE RAISED BY ALTERING THE VALUE OF THE
*  CONSTANT LABELLED 'SIXK' BY 16 BYTES PER MEMBER)
         SPACE
*  IT THEN READS EVERY PHYSICAL RECORD OF EACH MEMBER (ALIASES ARE
*  IGNORED) FROM THE START TTR TO AN EOF RECORD.  ERROR MESSAGES ARE
*  PRINTED FOR....
*        1  TEMPORARY I/O ERRORS DETECTED BEFORE O.S. ERROR RECOVERY.
*        2  PERMANENT I/O ERRORS.
*        3  NON-CONSECUTIVE RECORD ADDRESSES (TRACK OR RECORD NUMBER
*           IN THE COUNT FIELD READ IS NOT THE ONE WHICH SHOULD FOLLOW
*           THE ADDRESS SEARCHED ON.).
*        4  LENGTH GIVEN IN COUNT FIELD DISAGREES WITH NUMBER OF BYTES
*           READ (PROBABLY AN OVERFLOW RECORD)
*        5  MEMBER BRIDGES EXTENTS.
*        6  START ADDRESS OF MEMBER WITHIN AREA OF PREVIOUS MEMBER.
*        7  MEMBER GOES TO END OF LAST EXTENT WITHOUT FINDING AN EOF.
         SPACE
*  FOR ALL MEMBERS THE NAME, NUMBER OF BLOCKS, AND START/END DISK
*  ADDRESSES ARE PRINTED.
         SPACE
*  AN OPTION ALLOWS YOU TO COMPARE THE MEMBERS, AND THE NUMBER OF
*  BLOCKS IN EACH MEMBER, BEFORE AND AFTER SOME OTHER OPERATION (SUCH
*  AS A COMPRESS IN PLACE).  FOR THIS YOU PUT 'PARM=CREATE', AND
*  INCLUDE A 'CHECK' DD STATEMENT TO CREATE A SEQUENTIAL DATA SET OF
*  320 BYTE RECORDS (1 FOR EACH 20 MEMBERS), IN THE 'BEFORE' RUN.  IN
*  THE 'AFTER' RUN YOU PUT 'PARM=VERIFY' AND INCLUDE A 'CHECK' DD TO
*  READ BACK THE CHECKLIST.  YOU WILL THEN GET PRINTED MESSAGES AT THE
*  END OF THE NORMAL LISTING FOR ANY DISCREPANCIES IN THE MEMBERS OR
*  THE NUMBER OF BLOCKS IN MEMBERS.
         SPACE
*  EXAMPLE OF JCL....
*        //LIBCHECK  EXEC PGM=COMPLIB  (,PARM=CREATE)
*                                      (,PARM=VERIFY)
*        //SYSPRINT  DD   SYSOUT=A
*        //SYSLIB    DD   DEFINES PDS TO BE CHECKED.
*        //CHECK     DD   DSN=CHECK,DISP=(,KEEP),VOL=REF=XYZ,
*        //               SPACE=(TRK,(5,2))
*      OR  //CHECK   DD   DSN=CHECK,DISP=(SHR,DELETE),VOL=REF=XYZ
         SPACE  3
         BEGIN BASE=(11,12)
         L     1,0(1)
         LH    2,0(1)
         LTR   2,2
         BZ    NOPARM
         MVC   PARM,2(1)
         CLI   PARM,C'C'
         BE    NOPARM
         CLI   PARM,C'V'
         BE    NOPARM
         WTO   'INVALID PARM FOR COMPLIB - NOT ''CREATE'' OR ''VERIFY'''
               ',ROUTCDE=(2,11)
         ABEND 1
NOPARM   DS    0H
         OPEN  (LIB,,EXCP,,LIST,OUTPUT)
         TIME  DEC
         STM   0,1,CONTREC+24
         ED    TYD,CONTREC+29
         ED    THM,CONTREC+24
         L     0,SIXK
         GETMAIN R,LV=(0)
         ST    1,GOTADD
         MVC   8(4,1),ENDMARK
         ST    1,HIDIR
         A     1,SIXK
         ST    1,ENDOFGOT
         L     1,EXCP+44
         SR    2,2
         SR    15,15
         IC    15,16(1)
         LA    1,32(1)
         B     *+8
         LA    1,16(1)
         AH    2,14(1)
         BCT   15,*-8
         SLL   2,16
         ST    2,LIBEND
READ     READ  ECBA,SF,LIB,DBLK,256
         CHECK ECBA
         LA    2,DBLK
         LH    3,DBLK
         LTR   3,3
         BZ    EODIR
         AR    2,3
         LA    3,DBLK+2
ENTRY    CLC   0(8,3),FFS
         BE    EODIR
         MVI   LL,31
         NC    LL,11(3)
         SR    1,1
         IC    1,LL
         SLL   1,1
         STC   1,LL
         TM    11(3),128
         BO    STEPON                  IGNORE ALIAS
         L     9,GOTADD
ENCOMP   CLC   8(4,3),8(9)
         BNH   INSERT
         LA    9,16(9)
         B     ENCOMP
INSERT   L     8,HIDIR
         C     8,ENDOFGOT
         BNL   ABSIZE
         LR    7,8
         LA    8,16(8)
         ST    8,HIDIR
NEXTIN   MVC   0(16,8),0(7)
         XC    12(4,8),12(8)
         S     8,TWELVE
         S     7,TWELVE
         CR    7,9
         BNL   NEXTIN
         MVC   0(12,8),0(3)
STEPON   A     3,LLA
         LA    3,12(3)
         CR    2,3
         BH    ENTRY
         B     READ
ABSIZE   ABEND 1,DUMP
EODIR    SPMODE  PROB,0
         L     1,EXCP+44
         L     1,28(1)
         LA    2,APPEND
         ST    2,12(1)
         ST    2,16(1)
         LA    2,EOEA
         ST    2,0(1)
         SPMODE  PROB,*
         L     10,GOTADD
         B     MAINLOOP
         DROP  12
         USING *,15
APPEND   CLI   TWICE,0
         BCR   7,14                    PERM ERROR
         TM    IOB+12,1
         BZ    *+12
         OI    MEOM+1,240
         B     APPEX
         TM    IOB+3,8                 NO REC FOUND
         BO    TRYNTRK
         SRDL  10,63
         IC    10,DISKAD+7
         IC    11,DBLK+4
         CR    10,11
         BL    TRYSEQ
TRYNTRK  OI    MNT+1,240
         B     APPEX
TRYSEQ   BCTR  11,0
         CR    10,11
         BE    TRYLEN
         OI    INVSEQ,255
         B     APPEX
TRYLEN   LH    10,IOB+14
         L     11,L2314
         SR    11,10
         ST    11,ACTDLEN
TRYERR   TM    IOB+0,4
         BCR   8,14
         OI    MTEMPR+1,240
         B     APPEX+4
APPEX    NI    IOB,X'FB'
         MVC   TEMPCSW,IOB+8
         MVC   TEMPSENS,22(7)          IOS R7 -> UCB
         NI    IOB+12,X'FE'            REMOVE UE
         OI    TWICE,255
         BR    14
         USING *,15
EOEA     STM   9,2,EOESAVE
         L     0,TT
         LR    1,3
         LA    2,DISKAD
         L     15,16
         L     15,28(15)
         CNOP  2,4
         BALR  14,15
         LM    9,2,8(14)
         B     EOERET
EOESAVE  DC    10F'0'
EOERET   MVC   48(8,7),DISKAD
         XC    PREVEND,PREVEND
         OI    MEOE+1,240
         B     8(14)                   RETRY SEEK IN NEW EXTENT
         DROP  15
         USING IHB0001+4096,12
PRINT    SP    LCNT,ONE
         BP    PLINE
         ZAP   LCNT,MAXCNT
         PUT   LIST,NP
         PUT   LIST,TITLE
PLINE    PUT   LIST,(5)
         CLI   0(5),11
         BCR   12,6
         SP    LCNT,ONE
         BR    6
SEQERR   LA    5,SEQERRM
         MVC   4(8,5),LMN
         UNPK  25(11,5),DISKAD+3(6)
         MVI   35(5),64
         TR    25(10,5),TRTAB-240
         UNPK  43(11,5),DBLK(6)
         MVI   53(5),64
         TR    43(10,5),TRTAB-240
         LA    6,NEXTRK
         B     PRINT
EOLERR   MVC   EOLMSG+4(8),LMN
         LA    5,EOLMSG
         LA    6,EOM
         B     PRINT
NOTM80   LA    5,INVLENG
         MVC   11(8,5),LMN
         UNPK  37(11,5),DBLK(6)
         MVI   47(5),64
         TR    37(10,5),TRTAB-240
         MVC   63(6,5),MASK5
         L     1,ACTDLEN
         CVD   1,DTA
         ED    63(6,5),DTA+5
         BAL   6,PRINT
         B     NORMCONT
LENGERR  LA    5,LENGTHER
         MVC   4(8,5),LMN
         UNPK  30(11,5),DBLK(6)
         MVI   40(5),C','
         TR    30(10,5),TRTAB-240
         MVC   52(6,5),MASK5
         CVD   2,DTA
         ED    52(6,5),DTA+5
         LH    2,DBLK+6
         CVD   2,DTA
         MVC   65(6,5),MASK5
         ED    65(6,5),DTA+5
         BAL   6,PRINT
         B     NORMCONT
TOOLONG  NI    MTOOL+1,15
         LA    5,TOOLMSG
         MVC   4(8,5),LMN
         UNPK  30(11,5),DBLK(6)
         MVI   40(5),64
         TR    30(10,5),TRTAB-240
         MVC   50(6,5),MASK5
         L     2,ACTDLEN
         CVD   2,DTA
         ED    50(6,5),DTA+5
         BAL   6,PRINT
         B     MTOOL+4
TEMPERR  LA    5,TEMSG
         NI    MTEMPR+1,15
         LA    6,MTEMPR+4
         MVC   74(4,5),TEMP
PENT     MVC   4(8,5),LMN
         UNPK  20(11,5),DBLK(6)
         MVI   30(5),C','
         TR    20(10,5),TRTAB-240
         UNPK  39(5,5),TEMPCSW+4(3)
         MVI   43(5),C','
         TR    39(4,5),TRTAB-240
         UNPK  51(13,5),TEMPSENS(7)
         MVI   63(5),64
         TR    51(12,5),TRTAB-240
         MVC   66(6,5),MASK5
         LH    2,TEMPCSW+6
         CVD   2,DTA
         ED    66(6,5),DTA+5
         L     1,TEMPCSW
         S     1,EIGHT
         BP    MOVINOP
         MVC   64(2,5),63(5)
         B     MSLASH
MOVINOP  UNPK  64(3,5),0(2,1)
         TR    64(2,5),TRTAB-240
MSLASH   MVI   66(5),C'/'
         B     PRINT
BRIDGEXT LA    5,BXTM
         MVC   4(8,5),LMN
         BAL   6,PRINT
         NI    MEOE+1,15
         B     MNORM
PERMERR  LA    5,TEMSG
         MVC   74(4,5),PERM
         LA    6,NORMCONT
         XC    IOB(4),IOB
         MVI   IOB,X'42'
         XC    IOB+30(2),IOB+30
         NI    EXCP+44,63
         MVC   TEMPCSW,IOB+8
         L     1,EXCP+44
         L     1,32(1)
         MVC   TEMPSENS,22(1)
         B     PENT
CLOSEALL LA    5,ONEUP
         BAL   6,PRINT
         LA    5,FINMSG
         MVC   29(6,5),MASK5
         ED    29(6,5),TBLKCNT
         LH    1,LIBEND
         CVD   1,DTA
         MVC   71(6,5),MASK5
         ED    71(6,5),DTA+5
         BAL   6,PRINT
         CLOSE (LIB,,EXCP)
         CLI   PARM,C'N'
         BE    FINALE
         CLI   PARM,C'C'
         BNE   VERIFY
*  CREATE OPTION - WRITE OUT DIRECTORY AS NOW HELD IN CORE (I.E. .....
*  MEMBER NAME/TTR/BLOCK COUNT IN TTR SEQUENCE)
         OPEN  (CHECK,OUTPUT)
         L     9,GOTADD
PUT      PUT   CHECK,(9)
         CLC   8(4,9),ENDMARK
         BE    CLOSECHK
         LA    9,16(9)
         B     PUT
CLOSECHK CLOSE (CHECK)
FINALE   CLOSE (LIST)
         SR    15,15
         CLI   ERRET,0
         BE    GOB
         LA    15,20
GOB      GOBACK  RC=(15)
*  VERIFY OPTION - READ BACK PREVIOUS CHECK LIST AND FIND EACH
*  PREVIOUS MEMBER IN NEW LIST IN CORE.  ISSUE AN ERROR MESSAGE AND SET
*  ERRET IF OLD AND NEW BLOCK COUNTS DISAGREE, OR IF MEMBER IS ON ONE
*  LIST BUT NOT THE OTHER.
VERIFY   OPEN  (CHECK)
GET      GET   CHECK,CHK
         L     9,GOTADD
         CLC   CHK+8(4),ENDMARK
         BE    EOCHK
COMPARE  CLC   8(4,9),ENDMARK
         BE    NOTFND
         CLC   0(8,9),CHK
         BE    FOUND
         LA    9,16(9)
         B     COMPARE
FOUND    XC    0(8,9),0(9)             REMOVE TALLYING ENTRIES.
         MVC   VMN,CHK
         TM    14(9),255
         BZ    DISCREP
         TM    CHK+14,255
         BZ    DISCREP
         CP    12(3,9),CHK+12(3)
         BE    GET
DISCREP  OI    14(9),15
         OI    CHK+14,15
         UNPK  NBLKS,12(3,9)
         UNPK  OBLKS,CHK+12(3)
         LA    5,EMA
         BAL   6,PRINT
         B     ERRGET
NOTFND   MVC   ENMC,CHK
         LA    5,EMC
         BAL   6,PRINT
ERRGET   MVI   ERRET,255
         B     GET
EOCHK    L     9,GOTADD
NCHK     CLC   8(4,9),ENDMARK
         BE    CLOSECHK
         NC    0(8,9),0(9)
         BZ    ZEROK
         MVC   ENMB,0(9)
         LA    5,EMB
         BAL   6,PRINT
         MVI   ERRET,255
ZEROK    LA    9,16(9)
         B     NCHK
MAINLOOP CLC   8(4,10),ENDMARK
         BE    CLOSEALL
         MVC   LMN,0(10)
         MVC   TT(3),8(10)
         SR    1,1
         IC    1,10(10)
         BCTR  1,0
         LTR   1,1
         BM    *+8
         STC   1,TT+2
         STM   9,14,MCSAVE
         L     0,TT
         L     1,EXCP+44
         LA    2,DISKAD
         L     15,16
         L     15,28(15)
         CNOP  2,4
         BALR  14,15
         LM    9,14,8(14)
         B     MCRET
MCSAVE   DC    6F'0'
MCRET    CLC   DISKAD+3(5),PREVEND
         BH    MCALCST
         LA    5,MOVLAP
         BAL   6,PRINT
MCALCST  MVC   PREVEND,DISKAD+3
         LH    1,PREVEND
         CVD   1,DTA
         MVC   LSC,MASK3
         ED    LSC,DTA+6
         LH    1,PREVEND+2
         CVD   1,DTA
         MVC   CPD,MASK3
         ED    CPD,DTA+6
         MVC   LSH,CPD+2
         SR    1,1
         IC    1,PREVEND+4
         LA    1,1(1)
         CVD   1,DTA
         MVC   CPD,MASK3
         ED    CPD,DTA+6
         MVC   LSR,CPD+2
         LA    5,ONEUP
         BAL   6,PRINT
MEXCP    MVI   INVSEQ,0
         NI    EXCP+44,63
         MVC   IOB(5),NEWFLGS
         XC    DBLK(8),DBLK
         EXCP  IOB
         WAIT  ECB=ECB
         MVI   TWICE,0
MNT      BC    0,NEXTRK
MEOM     BC    0,EOM
MTOOL    BC    0,TOOLONG
         CLI   ECB,127
         BNE   PERMERR
MTEMPR   BC    0,TEMPERR
MEOE     BC    0,BRIDGEXT
MNORM    CLI   INVSEQ,0
         BNE   SEQERR
         AP    MBLKCNT,ONE
         AP    TBLKCNT,ONE
MNCONT   L     2,L2314
         SH    2,IOB+14
         CH    2,DBLK+6
         BC    7,LENGERR
NORMCONT IC    3,DISKAD+7
         LA    3,1(3)
         STC   3,DISKAD+7
         B     MEXCP
NEXTRK   NI    MNT+1,15
         MVI   DISKAD+7,0
         LH    1,TT
         LA    1,1(1)
         CH    1,LIBEND
         BNL   EOLERR
         STH   1,TT
         SR    1,1
         IC    1,DISKAD+6
         LA    1,1(1)
         STC   1,DISKAD+6
         CH    1,CYLEND
         BL    MEXCP
         MVI   DISKAD+6,0
         IC    1,DISKAD+4
         LA    1,1(1)
         STC   1,DISKAD+4
         B     MEXCP
EOM      NI    MEOM+1,15
         MVC   PREVEND,DISKAD+3
         LH    1,PREVEND
         CVD   1,DTA
         MVC   LEC,MASK3
         ED    LEC,DTA+6
         LH    1,PREVEND+2
         CVD   1,DTA
         MVC   CPD,MASK3
         ED    CPD,DTA+6
         MVC   LEH,CPD+2
         SR    1,1
         IC    1,PREVEND+4
         LA    1,1(1)
         CVD   1,DTA
         MVC   CPD,MASK3
         ED    CPD,DTA+6
         MVC   LER,CPD+2
         MVC   LNB,MASK3
         ED    LNB,MBLKCNT+1
         MVC   12(3,10),MBLKCNT
         SP    MBLKCNT,MBLKCNT
         LA    5,LINEA
         BAL   6,PRINT
         LA    10,16(10)
         B     MAINLOOP
EOFIN    EQU   *
NEWFLGS  DC    X'420000007F'
TEMPSENS DC    XL6'00'
TRTAB    DC    C'0123456789ABCDEF'
TEMP     DC    C'TEMP'
PERM     DC    C'PERM'
INVSEQ   DC    X'00'
TWICE    DC    X'00'
LCNT     DC    PL2'0'
MAXCNT   DC    PL2'58'
ONE      DC    P'1'
TWO      DC    P'2'
ONEUP    DC    X'0B'
NP       DC    X'8B'
LINEA    DC    X'09'
LMN      DC    CL8' '
LNC      DC    CL6' '
         DC    CL6' '
LNB      DC    CL4' '
         DC    C' BLOCKS FROM CYL'
LSC      DC    CL4' '
         DC    C' HD '
LSH      DC    CL2' '
         DC    C' R '
LSR      DC    CL2' '
         DC    C'  TO  CYL'
LEC      DC    CL4' '
         DC    C' HD '
LEH      DC    CL2' '
         DC    C' R '
LER      DC    CL2' '
MASK5    DC    X'402020202120'
MASK3    DC    X'40202120'
TITLE    DC    X'11'
         DC    CL7' '
         DC    C'RESPOND TSG LIBRARY CHECK (COMPLIB) PROGM RUN AT'
THM      DC    X'4021204B2020'
         DC    C' ON'
TYD      DC    X'4021204B202020'
         DC    CL8' '
INVLENG  DC    X'09'
         DC    CL79'** MEMBER 12345678. RECORD AT CCHHR 123456789A HAS *
               DATA LENGTH 12345'
LENGTHER DC    X'09'
         DC    CL79'** 12345678. RECORD AT CCHHR 123456789A, CSW LENGTH*
                12345, COUNT 12345'
TOOLMSG  DC    X'09'
         DC    CL79'** 12345678. RECORD AT CCHHR 123456789A LENGTH IS 1*
               2345 (OVER 1040)'
TEMSG    DC    X'09'
         DC    CL79'** 12345678. CCHHR 123456789A, STATUS 1234, SENSE 1*
               23456123456 XX/12345 (TEMP)'
BXTM     DC    X'09'
         DC    CL79'** 12345678 BRIDGES EXTENTS.'
FINMSG   DC    X'09'
         DC    CL79'END OF RUN.  TOTALS - BLOCKS 12345  ...........  TR*
               ACKS IN ALL EXTENTS 12345 '
SEQERRM  DC    X'09'
         DC    CL79'** 12345678.  SEARCH ON 123456789A  FOUND 123456789*
               A '
EOLMSG   DC    X'09'
         DC    CL79'** 12345678 ATTEMPTS TO RUN OFF END OF LIBRARY'
MBLKCNT  DC    PL3'0'
TBLKCNT  DC    PL3'0'
MRECCNT  DC    PL3'0'
TRECCNT  DC    PL3'0'
MOVLAP   DC    X'09'
         DC    CL79'*** START ADDRESS OF NEXT MEMBER OVERLAPS END ADDRE*
               SS OF THIS MEMBER'
SPMEM    DC    CL8' '
SPCARDS  DC    CL6' '
SPSC     DC    CL4' '
         DC    C' '
SPSHR    DC    CL4' '
SPEC     DC    CL4' '
         DC    C' '
SPEHR    DC    CL4' '
PARM     DC    C'N'
ERRET    DC    X'00'
CHK      DC    CL16' '
EMA      DC    X'11',C'MEMBER '
VMN      DC    CL8' ',C' *** (NOW HAS '
NBLKS    DC    CL5' ',C' BLOCKS, CHECKLIST HAD '
OBLKS    DC    CL5' ',CL18' '
EMB      DC    X'11',C'MEMBER '
ENMB     DC    CL8' ',C' NEW (NOW IN LIBRARY, BUT NOT IN CHECKLIST)'
         DC    CL21' '
EMC      DC    X'11',C'MEMBER '
ENMC     DC    CL8' ',C' OLD (NOT IN LIBRARY, BUT WAS IN CHECKLIST)'
         DC    CL21' '
LIB      DCB   DDNAME=SYSLIB,DSORG=PS,MACRF=(R),RECFM=F,               *
               BLKSIZE=256,EODAD=EODIR
EXCP     DCB   DDNAME=SYSLIB,DSORG=DA,MACRF=(E),EODAD=EOFIN
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBM,          *
               LRECL=80,BLKSIZE=1600
CHECK    DCB   DDNAME=CHECK,DSORG=PS,MACRF=(GM,PM),RECFM=FB,LRECL=16,  *
               BLKSIZE=320,EODAD=EOCHK
DTA      DC    D'0'
DTB      DC    D'0'
DDN      DC    D'0'
IOB      DC    X'42000000'
         DC    A(ECB)
         DC    2F'0'
         DC    A(CP)
         DC    A(EXCP)
         DC    2F'0'
DISKAD   DC    2F'0'
CP       CCW   49,DISKAD+3,96,5
         CCW   8,*-8,96,1
         CCW   30,DBLK,32,7500
LIBEND   DC    F'0'
ACTDLEN  DC    F'0'
EIGHT    DC    F'8'
TEMPCSW  DC    XL8'00'
CPD      DC    F'0'
ECB      DC    F'0'
TWELVE   DC    F'16'
SIXK     DC    F'16384'
GOTADD   DC    F'0'
HIDIR    DC    F'0'
ENDOFGOT DC    F'0'
L2314    DC    F'7492'
FFS      DC    8X'FF'
LLA      DC    XL3'00'
LL       DC    X'00'
ENDMARK  DC    X'7FFFFFFF'
TT       DC    F'0'
CONTREC  DC    CL32'*** WTAAS.SOURCE.LISTCR'
CYLEND   DC    H'20'
         DS    0H
PREVEND  DC    XL5'00'
FINEOF   DC    AL3(EOFIN)
DBLK     DS    CL7500
         END
