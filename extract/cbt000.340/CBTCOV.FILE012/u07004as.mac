//*
//*                SORTS OUT JCL ERRORS AND ABENDING JOBS FROM
//*
//*                TYPE 5 RECORDS
//*
//*                AUTHOR  *** MKB *** BRITISH RAIL
//*
         TITLE ' SMF JOB COMPLETION REPORT'
************************************************************************
*                        PURPOSE OF PROGRAM                           *
*                                                                     *
         SPACE
*        TO READ THROUGH THE SMF DATA SET AND TEST THE JOB            *
*        RECORDS FOR AN ABEND CODE. IF ONE IS FOUND THEN THE          *
*        APPROPRIATE BITS ARE TESTED TO DETERMINE WHETHER OR          *
*        NOT A USER ABEND OCCURRED. IF A USER ABEND IS INDICATED      *
*        NO ACTION IS TAKEN. THE (ASSUMED) SYSTEM ABEND CODE IS       *
*        TRANSLATED TO EBCDIC AND WRITTEN TO THE OUTPUT FILE,         *
*        TOGETHER WITH THE JOBNAME AND THE TEXT 'ABENDED' AS IS       *
*        THE DATE                                                     *
*        THE JOB CPU TIME IS TESTED FOR ZERO - INDICATING JCL         *
*        ERROR AND IF FOUND THE JOBNAME AND THE TEXT '**JCL**'        *
*        ARE WRITTEN TO THE OUTPUT FILE ALONG WITH THE DATE           *
*                                                                     *
*        - INSTALLATION DEPENDENT SECTION                             *
*        AT BRB, THE 'PGMR NAME FIELD' IN SMF TYPE 5 RCDS IS          *
*        USED AS A JOB DESCRIPTION - E.G  'MKB SMF REPORT' -          *
*        THIS IS DISPLAYED ON THE OUTPUT FILE                         *
*        ALSO THE 'ROOM #' FIELD ON THE HASP ACCT CARD (FIELD 2       *
*        IN THE OS JOB CARD ACCT #) IS USED AS A PGMR ID CARD         *
*        - EG  //SMFREPT JOB (9000,889),'MKB SMF REPT',CLASS=E        *
*        THIS FIELD (889) IS ALSO MOVED TO THE OUTPUT FILE            *
*        - END OF INSTALLATION DEPENDENT SECTION                      *
*                                                                     *
*        - NOTE DCB INFO FOR INPUT AND OUTPUT FILES - VARIABLE        *
*        LENGTH RECORDS ARE ASSUMED AND THE RDW IS INCLUDED IN        *
*        DISPLACEMENT CALCULATIONS                                    *
*                                                                     *
*        IGBPTPCH IS USED TO LIST THE FILE OUTPUT BY THE PROGRAM      *
*        CTL STATEMENTS  PRINT MAXFLDS=1                              *
*        RECORD FIELD=(100)                                           *
*        TITLE 'DATE  PGMR REASON  TITLE  CMP'  (NOT TO SCALE)        *
************************************************************************
         EJECT
SMFREPT  CSECT
          STM   R14,R12,12(R13)
         LR    R12,R15
         USING SMFREPT,R12
         LR    R15,R13
         ST    R13,SAVE+4
         LA    R13,SAVE
         ST    R13,8(0,R15)
         B     MISSAVE
SAVE     DS    18F
MISSAVE EQU   *
         OPEN  (IN,,OUT,(OUTPUT))
LOOP     EQU   *
         XC    INAREA(255),INAREA
         GET   IN,INAREA
         CLI   INAREA+5,X'05'      JOB RECD ?
         BNE   LOOP      NO
         EJECT
JOBRECD  EQU   *
         CLC   INAREA+55(2),ZEROS
         BE    JOBFLUSH       YES TEST FOR FLUSH
         TM    INAREA+66,X'02'    ABEND ?
         BNO   LOOP     NO
         TM   INAREA+55,X'80'    USER ABEND ?
         BO    LOOP         YES
         MVC   HWORD(2),INAREA+55
         LH    R2,HWORD
         STC   R2,CCODE+2
         SRL   R2,4
         STC   R2,CCODE+1
         SRL   R2,4
         STC   R2,CCODE
         NC    CCODE(3),=X'0F0F0F'
         TR    CCODE(3),TABLE
         MVC   MSG(7),ABENDED
         B     MSGPRINT
JOBFLUSH EQU   *
         CLC   INAREA+117(3),ZEROS
         BNE   LOOP
         MVC   MSG(7),FLUSHED
         MVC   CCODE,BLANKS
MSGPRINT EQU   *
         XR    R9,R9
         IC    R9,INAREA+121
         LA    R8,INAREA+122(R9)
         MVC   PGMR#(3),1(R8)
         MVC   PNAME(20),INAREA+97
         MVC   JNAME(8),INAREA+18
         UNPK  WORK(5),INAREA+11(3)
         OI    WORK+4,X'F0'
         MVC   YR(2),WORK
         MVC   DAY(3),WORK+2
         PUT   OUT,OUTAREA
         B     LOOP
END      EQU   *
         CLOSE (IN,,OUT)
         L    R13,SAVE+4
         LM    R14,R12,12(R13)
         BR    R14
         PRINT NOGEN
IN       DCB   DDNAME=IN,DSORG=PS,MACRF=GM,EODAD=END
OUT      DCB   DDNAME=OUT,MACRF=PM,DSORG=PS,RECFM=FB,LRECL=100,        *
               BLKSIZE=1000
ZEROS    DC    F'0'
HWORD    DS    H
TABLE    DC    C'0123456789ABCDEF'
ABENDED  DC    C'ABENDED'
FLUSHED  DC    C'**JCL**'
WORK     DS    D
OUTAREA  DS    0CL100
         DC    C' '
         DC    CL4' '
YR       DS    CL2
         DC    C'.'
DAY      DS    CL3
         DC    CL4' '
JNAME    DS    CL8
         DC    CL4' '
MSG      DS    CL7
         DC    CL4' '
CCODE    DS    CL3
         DC    CL4' '
PNAME    DS    CL20
         DC    4C' '
PGMR#    DC    C'   '
         DC    53C' '
BLANKS   DC    C'   '
         LTORG
INAREA   DS    CL10000
R15      EQU   15
R14      EQU   14
R13      EQU   13
R12      EQU   12
R9       EQU   9
R8       EQU   8
R2       EQU   2
         END
//GO.IN  DD  DSN=YMF.SYSTEM.DATA(-1),DISP=SHR
//GO.OUT  DD  DSN=&&A,DISP=(,PASS),UNIT=WORK,SPACE=(CYL,(2,1))
//Z  EXEC  PGM=IEBPTPCH
//SYSPRINT  DD  SYSOUT=A
//SYSUT1  DD  DSN=&&A,DISP=(OLD,PASS)
//SYSUT2  DD  SYSOUT=A
 PRINT TYPORG=PS,MAXFLDS=4
 TITLE ITEM=('DATE      JOBNAME             CMP   PGMR',9)
 RECORD FIELD=(100)
