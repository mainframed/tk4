*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSM14BLK.
SSM14BLK ENTER 12,ENT=(BLOCKMOD,IEFSD095),RENT=YES,                    X
               XTRN=(INDEXTAB,PLINETAB,MAINTAB)
         LM    2,5,0(1)            GET PARM LIST
         L     3,0(0,3)
         L     5,0(0,5)
         LA    3,0(0,3)
         LA    5,0(0,5)
         CH    3,=H'12'            MAX LINE COUNT
         BH    ERR1
         BCTR  3,0
GODOIT   L     8,=A(INDEXTAB)
         L     9,=A(MAINTAB)
         L     10,=A(PLINETAB)
         SR    6,6
         SR    7,7
         IC    6,0(0,2)            PICK UP CHAR
         IC    7,0(6,8)            PICK UP INDEX
         CLI   0(2),129            LOWER CASE A
         BL    NOFOLD
         CLI   0(2),169            LOWER CASE Z
         BH    NOFOLD
         O     6,=F'64'
NOFOLD   MH    7,=H'12'
         ALR   7,3                 GET LINE #
         IC    7,0(7,9)            GET LINE FORMAT INDEX
         N     7,=F'255'
         SLL   7,1
         LA    7,0(7,10)           ADDR. OF LINE FORMAT
         LA    11,12               NO. TO DO
         SR    0,0
         ICM   1,3,0(7)            PICJ UP FORMAT
         SLL   1,20                SHIFT INTO HIGH BYTES
         SLDL  0,1                 SHIFT FIRST INTO REG 0
INPUT    LTR   0,0                 ANYTHING TO DO?
         BZ    *+10                NO - BRANCH ROUND
         STC   6,0(0,4)            PUT CHAR IN
         SR    0,0                 CLEAR REG
         LA    4,1(0,4)            UP O/P AREA
         SLDL  0,1                 SHIFT NEXT INTO REG 0
         BCT   11,INPUT            ROUND TIL WE FINISH
         LA    4,2(0,4)            GAP OF 2 CHARS
         LA    2,1(0,2)            NEXT CHAR
         BCT   5,GODOIT            ROUND FOR NEXT LETTER
         LEAVE
ERR1     LEAVE 16
         LTORG
         END
