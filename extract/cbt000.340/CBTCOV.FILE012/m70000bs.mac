         MACRO
         SETCHAN &CHAN,&POS,&LINES,&REPEAT
         GBLA  &FBYTE,&LBYTE,&MATRIX(255)
         LCLA  &CTR,&SUB
&SUB     SETA  1
         AIF   ('&POS(&SUB)' EQ '').INIT
 MNOTE '******* CHANNEL &CHAN SPECIFICATION FOLLOWS ******************'
.INIT    AIF   ('&POS(&SUB)' EQ '').SETNEXT
         AIF   ((&LINES-&POS(&SUB)) GE 0).POSOK
         MNOTE 16,'CHANNEL &CHAN POSITION &POS(&SUB) GREATER THAN LINESX
               (&LINES), POSITION IGNORED'
         AGO   .NEXTPOS
.POSOK   ANOP
         AIF   (&MATRIX(&POS(&SUB)) EQ 0).UNIQUE
         MNOTE 12,'POSITION &POS(&SUB) USED BY CHANNEL &MATRIX(&POS(&SUX
               B)), CHANNEL &CHAN USE IGNORED'
         AGO   .NEXTPOS
.UNIQUE  ANOP
&MATRIX(&POS(&SUB)) SETA &CHAN
&CTR     SETA 1
.CHLOOP  AIF   (&POS(&SUB) NE 1).CHLAST
&FBYTE   SETA  &CHAN
.CHLAST  AIF   ((&POS(&SUB)-&LINES) NE 0).SETCH
&LBYTE   SETA  &CHAN
.SETCH   ORG   FORM&CTR+&POS(&SUB)-1
          DC   AL1(&CHAN)
&CTR     SETA  &CTR+1
         AIF   ((&REPEAT-&CTR) GE 0).CHLOOP
.NEXTPOS ANOP
&SUB     SETA  &SUB+1
         AGO   .INIT
.SETNEXT ANOP
         MEND
