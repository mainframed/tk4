         TITLE 'SYANORAC - P.J.CHISLETT JULY 1973'
* MODULE DESCRIPTION.
*      SYANORAC IS A DATA MANAGEMENT MODULE. IT IS NORMALLY CALLED
*   BY A COBOL PROGRAM AND IS PASSED A SINGLE PARAMETER WHICH POINTS
*   TO AN AREA CONSISTING OF   1) A ONE BYTE FLAG
*                              2) A 44 BYTE DSNAME
*                              3) A 2 BYTE DDNAME SUFFIX
*                              4) A  STORAGE AREA FOR THE RECORD
*     THIS MODULE WILL ACCEPT ANY DSNAME AND DDNAME-SUFFIX AND WILL
*   PERFORM OPEN, READ AND CLOSE OPERATIONS AS APPROPRIATE.
*     IF THE DSNAME CANNOT BE LOCATED ON THE CATALOGUE, THE FLAG IS SET
*   TO 'M' AND CONTROL IS RETURNED TO THE CALLING PROGRAM.
*      IF EOF IS ENCOUNTERED BEFORE ANY RECORDS HAVE BEEN READ, I.E.
*   THE FILE IS EMPTY, THEN THE FILE IS CLOSED AND THE FLAG IS SET TO
*   'E' BEFORE RETURNING<
*      NORMAL EOF IS INDICATED BY SETTING THE FLAG TO 'C' BEFIRE
*   CLOSING THE FILE AND RETURNING.
         SPACE 3
*   REGISTER USAGE
*        R13 - R0   SYSTEM
*        R1         PARM LIST POINTER ON ENTRY TO MODULE
*        R2         POINTS TO FLAG
*        R3         POINTS TO DSNAME
*        R4         POINTS TO DDNAME SUFFIX
*        R5         POINTS TO RECORD AREA
*        R6
*        R7
*        R8
*        R9
*        R10
*        R11
*        R12        BASE REG
         SPACE 3
         SYMREGS
         SPACE 3
SYANORAC BEGIN
         L     R2,0(R1)            R2 POINTS TO FLAG
         LA    R3,1(R2)            R3 POINTS TO DSNAME
         LA    R4,44(R3)           R4 POINTS TO DDNAME SUFFIX
         LA    R5,2(R4)            R5 POINTS TO RECORD AREA
FTEST    B     FIRSTIME           NOP IF NOT FIRST TIME THRU
         CLI   0(R2),X'00'        DOES FILE NEED OPENING
         BE    GETREC             NO
         B     OPENPROC           YES
FIRSTIME NI    FTEST+1,X'00'      NOP FTEST BRANCH
OPENPROC MVC   INDSN,0(R3)         MOVE DSN TO CAMLIST FIELD
         LOCATE CAMLOC             LOCATE CATALOG VOL-LIST BLOCK
         LTR   R15,R15
         BZ    DEVTEST
         MVI   0(R2),C'M'
         B     GOBACK
CAMLOC   CAMLST NAME,INDSN,,VOLIST
INDSN    DC    CL44' '
VOLIST   DS    0D
         DS    265C
DEVTEST  CLI   VOLIST+4,X'80'      IS DSET ON TAPE
         BE    TAPEDDN             YES
         MVC   DDNAME(4),DISK
         B     FILLDDN
TAPEDDN  MVC   DDNAME(4),TAPE
FILLDDN  MVC   DDNAME+4(2),0(R4)   MOVE SUFFIX TO DDNAME
         MVC   INDCB+40(8),DDNAME  MOVE DDNAME TO DCB
         RDJFCB (INDCB)
MODJFCB  MVC   JFCB(44),0(R3)      MOVE DSN TO JFCB
         MVI   JFCB+52,X'88'       INDICATE CATALOGUED DATA SET
*                                  AND OPEN NOT TO WRITE BACK JFCB.
         MVC   JFCB+68(2),VOLIST+12 MOVE IN DATASET SEQNO (DA = 0).
         MVC   JFCB+117,X'01'      ONE VOL SERNO
         MVC   JFCB+118(6),VOLIST+6  MOVE IN VOL SERNO
         OPEN  (INDCB),TYPE=J
GETREC   GET   INDCB,(5)
         MVI   0(R2),X'00'        SET FLAG FOR SUCCESSFUL GET
GOBACK   L     R13,4(R13)
         RETURN (14,12)
SETFLAG CLI   0(R2),X'00'         HAVE ANY RECORDS BEEN READ
         BNE   NORECS
         MVI   0(R2),C'C'         SET FLAG FOR NORMAL CLOSE
         B     CLOSEFIL
NORECS   MVI   0(R2),C'E'         SET FLAG FOR CLOSE OF EMPTY FILE
CLOSEFIL CLOSE (INDCB)
         FREEPOOL INDCB
         B     GOBACK
INDCB    DCB   DSORG=PS,MACRF=(GM),EXLST=JFCBADD,EODAD=SETFLAG
JFCBADD  DS    0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    44F
TAPE     DC    CL4'TAPE'
DISK     DC    CL4'DISK'
DDNAME   DC    CL8' '
         END
