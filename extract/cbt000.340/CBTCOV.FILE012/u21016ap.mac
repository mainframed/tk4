* PROCESS  A,X,AG,LIST,MAP,OPT(TIME),STG;
 /*      SELECTIVE DATASET LISTING - ZAJLIST - T.J.BOURNE - 13/08/70 */
0ZAJLIST:PROC OPTIONS(MAIN) REORDER;
0/* PRINT FILE FOR COMMENTS OR ERRORS */
         DCL   SYSPRINT  FILE PRINT,
0/* PRINT FILE FOR DATASET LISTING    */
               SYSUT2    FILE PRINT,
0/* INPUT FILE FOR PARAMETER CARDS    */
               SYSIN     FILE INPUT;
-/*      ENTRY DECLARATIONS */
0        DCL (DATE,TIME) BUILTIN;
         DCL (ZAJL2,ZAJL3) ENTRY EXTERNAL;
         DCL ZAJL4 ENTRY(FIXED BIN(15)) EXTERNAL;
         DCL ZAJDFRE ENTRY(CHAR(6),FIXED BIN(15),FIXED BIN(15)) EXT;
-/* STRUCTURE TO HOLD TABLE OF NODES  */
         DCL   1    NODES(200) EXTERNAL,
 /*                   NODE NAME       */
                    2 NDN     CHAR(44),
 /*                   LENGTH OF NAME  */
                    2 NDNLN   FIXED BIN(15),
 /*                   VOLUME SERIAL   */
                    2 NDVOL   CHAR(6),
 /*                   INDEX ADDRESS   */
                    2 NDTTR   CHAR(3),
 /*                   FLAGS           */
                    2 NDFLG   BIT(8);
-/* STRUCTURE TO HOLD TABLE OF DATASETS */
         DCL   1    DSETS(600) EXTERNAL,
 /*                   FOR ALPHAMERIC SEQUENCING */
                    2 DSSEQ   FIXED BIN(16),
 /*                   DATASET NAME    */
                    2 DSN     CHAR(44),
 /*                   LENGTH OF NAME  */
                    2 DSNLN   FIXED BIN(15),
 /*                   VOLUME SERIAL   */
                    2 DSVOL   CHAR(6),
 /*                   DEVICE TYPE     */
                    2 DSTYP   CHAR(4);
 /*      HEADING FOR ALL OUTPUT */
0        DCL HEAD CHAR(31) STATIC INIT
                         ('ZAJLIST VERSION 1.3 (28/NOV/72)');
-/* TABLE OF DISKS FOR WHICH FREE SPACE LISTING IS REQUIRED    */
0        DCL   SPVOL(20) EXT CHAR(6) INIT((20)(6)' ');
-/* FORMATTED DATE AND TIME FOR HEADINGS */
0        DCL DT CHAR(24) STATIC INIT(' AS AT HH.MM ON DD/MM/YY');
-/* STRUCTURE TO HOLD COMMON COUNTERS */
         DCL   1    CTRS EXTERNAL,
 /*                   LAST ENTRY IN NODE TABLE       */
                    2 NND     FIXED BIN(15),
 /*                   LAST ENTRY IN DATASET TABLE    */
                    2 NDS     FIXED BIN(15),
 /*                   CURRENT NODE                   */
                    2 JND     FIXED BIN(15),
 /*                   FIRST DATASET ENTRY IN ALPHAMERIC SEQUENCE  */
                    2 ALPHA   FIXED BIN(15);
-/*      WORK AREA FOR FORMATTING TIME AND DATE */
0        DCL TEMP CHAR(6);
         DCL PG FIXED(3);          /* PAGE NUMBER */
-/*      INITIALISATION     */
0        OPEN  FILE(SYSPRINT) LINESIZE(120) PAGESIZE(60),
               FILE(SYSUT2) LINESIZE(132);
0        PUT EDIT (' ') (LINE(60),A(120));
         TEMP=TIME;
         SUBSTR(DT,8,2)=SUBSTR(TEMP,1,2);
         SUBSTR(DT,11,2)=SUBSTR(TEMP,3,2);
         TEMP=DATE;
         SUBSTR(DT,17,2)=SUBSTR(TEMP,5,2);
         SUBSTR(DT,20,2)=SUBSTR(TEMP,3,2);
         SUBSTR(DT,23,2)=SUBSTR(TEMP,1,2);
0        NND=0;
0        ON ENDPAGE(SYSPRINT)
           PUT PAGE EDIT(HEAD,' - ERROR LISTING',DT,' ')
                        (X(30),A,A,A,SKIP,A(120));
0        ON ENDPAGE(SYSUT2)
           BEGIN;
           PUT FILE(SYSUT2) PAGE EDIT
                        (HEAD,
                         ' - DATASET LISTING FOR INDEX ',
                         SUBSTR(NDN(1),1,NDNLN(1)),DT)
                        (X(5),A,A,A,A)
                        (' PAGE ',PG) (COLUMN(123),A,P'ZZ9')
                        ('DSNAME','VOLUME XTNTS NO. OF EMPTY  SEC QTY',
                         ' CREATED  EXPIRY DSORG RECFM BLKSZ LRECL ',
                         'KEYLEN RKP')
                        (SKIP(2),A(44),A,A,A)
                        ('TRACKS TRACKS','DATE',' ')
                        (SKIP,X(57),A,X(19),A,SKIP,A(132));
           PG=PG+1;
           END;
         ON ERROR SNAP CALL PLIDUMP('H');
-/*      INITIALISE FOR EACH INPUT CARD    */
0NEXTCARD:
         NND,NDS,ALPHA=0;
-/*      PROCESS NEXT PARAMETER CARD  */
0        CALL ZAJL2;
0/*      SEE IF TABLE IS EMPTY                  */
0        IF NND=0
           THEN GO TO TRYSPACE;
         PG=1;
0        JND=0;
         GO TO LASTTEST;
-/*      SELECT NEXT NODE                       */
0NEXTNODE:
         JND=JND+1;
-/*      BUILD TABLE FOR THIS NODE              */
         CALL ZAJL3(ADDR(NDN(1)),JND);
-/*      ANOTHER NODE ?     */
0LASTTEST:
0        IF JND<NND THEN GO TO NEXTNODE;
-/*      PRINT MESSAGE IF NO DATASETS FOR THIS INDEX      */
0        IF NDS=0
           THEN DO;
                PUT SKIP EDIT('NO ENTRIES IN INDEX ',NDN(1)) (A,A);
                GO TO NEXTCARD;
                END;
-/*      PRINT DATASET LISTING FOR THIS INDEX   */
0        SIGNAL ENDPAGE(SYSUT2);
         JDS=ALPHA;
         DO J=1 TO NDS;
           CALL ZAJL4(JDS);
           JDS=DSSEQ(JDS);
         END;
0        GO TO NEXTCARD;
-/* HANDLE REQUESTS FOR FREE SPACE LISTINGS     */
0TRYSPACE:
         IF SPVOL(1)='      ' THEN RETURN;
         PUT PAGE EDIT(HEAD,' - FREE SPACE LISTING',DT,' ')
                      (X(10),A,A,A,SKIP,A(120));
0        DO J=1 TO 20 WHILE (SPVOL(J)^='      ');
           CALL ZAJDFRE(SPVOL(J),JCYL,JTRK);
           PUT SKIP(2) EDIT(SPVOL(J),' HAS',JCYL,' FULL CYLINDERS AND',
                              JTRK,' ODD TRACKS NOT ALLOCATED')
                           (A,A,F(4),A,F(4),A);
         END;
         END;  /* OF ZAJLIST */
