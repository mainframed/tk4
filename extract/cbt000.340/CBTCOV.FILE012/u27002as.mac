SYEXPDTE START 0
*       *************************************************************
*       * THIS PROGRAM IS A MODIFICATION TO CHANGE1 DESCRIBED BELOW *
*       * THE PROGRAM NOW SAVES AND RETURNS REGISTERS CONVENTIONALLY*
*       * INSTEAD OF CHANGING ONLY ONE EXPIRY DATE IF PARM='CARDS'  *
*       * IS SPECIFIED ANY NUMBER OF DSCBS CAN BE CHANGED.          *
*       * IF PARM='YYDDD' IS SPECIFIED THE EXTRA CODING FOR CONTROL *
*       * CARDS IS BYPASSED.                                        *
*       *      DDNAME FOR CONTROL CARDS IS SYSIN.                   *
*       *      FORMAT OF CONTROL CARDS AS FOLLOWS:-                 *
*       *      KEYWORD DATE MUST APPEAR FIRST ON THE CARD,IT CAN    *
*       * START IN COLUMN1,AND MUST BE FOLLOWED BY AT LEAST ONE     *
*       * BLANK.THE OTHER THREE KEYWORDS WHICH ARE DDNAME,DSNAME,&  *
*       * EXPDT CAN APPEAR IN ANY ORDER FOLLOWED BY AN = SIGN AND   *
*       * THE RELEVANT DATA;EACH SEPERATE ENTRY SEPERATED BY A COMMA*
*       * E.G.                                                      *
*DATE DDNAME=DISKFILE,DSNAME=DATASET1,EXPDT=99365                   *
*       * CONTROL CARDS & MESSAGES ARE PRINTED ON SYSPRINT          *
*       *                                                           *
*       *************************************************************
*       ***************************************************************
*       *CHANGE1 WRITES AN EXPIRATION DATE INTO A DSCB.  THE DSCB IS  *
*       *READ AND WRITTEN WITH XDAP WITH IDENTIFICATION BY KEY.  THE  *
*       *FIRST TRACK ADDR IS PICKED UP FROM THE DEB ASSOCIATED WITH   *
*       *THE DASD DCB.  THE ADDR IS INCREMENTED UNTIL THE TRACK IS    *
*       *FOUND WHICH CONTAINS THE DSCB.  THE NEW EXPDT IS STORED, AND *
*       *THE DSCB IS WRITTEN BACK INTO ITS ORIGINAL POSITION.  WOW.   *
*       *CHANGE1 IS GOING TO TRY A DARING INNOVATION IN THAT NO       *
*       *REGISTERS WILL BE SAVED, AND CONTROL SHALL RETURN TO THE     *
*       *SUPERVISOR VIA AN SVC 3.  TERRIFIC.                          *
*       ***************************************************************
         XR    15,15
         SAVE  (14,12)
         BALR  2,0                 SET BASE REGISTER
         USING *,2
         LR    11,13              STANDARD
         LA    13,SAVE1             O.S.
         ST    11,4(13)           SAVE AREA
         ST    13,8(11)           CHAINING
         BC    15,*+10
         DC    C'CHANGE'
         L     1,0(0,1)            LOAD ADDR OF EXPDT FROM EXEC
         MVC   HWORD,0(1)          PICK UP LENGTH OF PARM FIELD
         CLC   HWORD,=H'1'
         BC    6,TEST5
         OI    SW2,X'40'
         MVC   SW1,2(1)
         BC    15,RDJFCB
TEST5    CLC   HWORD,=H'5'         CHECK LENGTH OF PARM FIELD
         BC    4,ABEND4            ERROR IF PARM LENGTH LT 5
OKCONV   MVC   EXPDT,2(1)          PICK UP NEW EXPDT
         CLC   EXPDT,=C'CARDS' ARE CONTROL CARDS BEING USED ?
         BNE   ONLY1
         OPEN  (CTLCRD)
         OPEN  (SYSPNT,(OUTPUT))
         PUT   SYSPNT,CLINE
         MVI   ASA,C'0'
CHKCRDS  EQU   *
         XC    SW3,SW3
         GET   CTLCRD,CARD   READ CONTROL CARD
         MVC   MLINE,CARD
         MVC   MDSN,BLANKS
         PUT   SYSPNT,CLINE
         LA    3,CARD        POINT REG.3 TO 1ST. COLUMN OF CARD
CHECK1   CLC   0(1,3),=C' '  IS COLUMN BLANK ?
         BNE   CHECK2        IF NOT BLANK CHECK KEYWORD
         LA    3,1(3)        POINT TO NEXT COLUMN
         B     CHECK1        BACK TO CHECK1
CHECK2   CLC   0(4,3),=C'DATE' IS 1ST. KEYWORD = 'DATE' ?
         BNE   ABEND9        INVALID 1ST. KEYWORD, ABEND
         LA    3,4(3)        POINT TO NEXT COLUMN AFTER DATE
CHECK3   CLC   0(1,3),=C' '  IS COLUMN BLANK ?
         BNE   CONTACT       IF NOT BLANK START SCAN FOR NEXT KEYWORD
         LA    3,1(3)        POINT REG.3 TO NEXT COLUMN
         B     CHECK3        BACK TO CHECK3
CONTACT  LR    4,3           POINT REG.4 TO START OF KEYWORD
         LA    3,1(3)        SCAN FOR
         CLC   0(1,3),=C' '   BLANK
         BE    LAST            OR
         CLC   0(1,3),=C','   COMMA
         BNE   CONTACT+2     MOVE ON TO NEXT COLUMN
*******************CHECK KEYWORD
         TM    SW3,X'E0'     FIRST KEYWORD ?
         BZ    TEST1
         LA    4,1(4)
TEST1    EQU   *
         CLC   0(6,4),=C'EXPDT='
         BNE   NEXT1
         MVC   EXPDT,6(4)    PICK UP NEW EXPIRY DATE
         OI    SW3,X'80'     INDICATE EXPIRY DATE PICKED UP
         B     CONTACT
NEXT1    EQU   *
         CLC   0(7,4),=C'DSNAME='
         BNE   NEXT2
         LR    5,3
         LA    4,7(4)        INCREMENT REG.4 TO ADDRESS OF DSNAME
         SR    5,4           PUT LENGTH FOR EX INSTRUCTION IN REG.5
         SH    5,ONE         REDUCE LENGTH BY 1 FOR EX INSTRUCTION
         MVC   DSNAME,BLANKS  BLANK OUT DSNAME FIELD
         EX    5,MVDSN
         OI    SW3,X'40'     INDICATE DSNAME PICKED OP
         B     CONTACT
NEXT2    EQU   *
         CLC   0(7,4),=C'DDNAME='
         BNE   ABEND9
         LR    5,3
         LA    4,7(4)        INCREMENT REG.4 TO ADDRESS OF DDNAME
         SR    5,4           PUT LENGTH FOR EX INSTRUCTION IN REG.5
         SH    5,ONE         REDUCE LENGTH BY 1 FOR EX INSTRUCTION
         MVC   DASD+40(8),BLANKS  BLANK OUT DDNAME FIELD
         EX    5,MVDDN
         OI    SW3,X'20'     INDICATE DDNAME PICKED UP
         B     CONTACT
LAST     EQU   *
         TM    SW3,X'10'
         BO    LAST1
         OI    SW3,X'10'
         LA    4,1(4)
         B     TEST1
LAST1    EQU   *
         TM    SW3,X'E0'     HAVE THREE KEYWORDS BEEN RECEIVED ?
         BNO   ABENDA        IF NOT ABEND
ONLY1    EQU   *
         NC    NONNUM,EXPDT        TEST FOR NONNUMERICS
         CLC   NONNUM,=X'F0F0F0F0F0'   CHECK THAT PARM ALL NUMBERS
         BC    6,ABEND5            ERROR IF PARM NOT ALL NUMBERS
         CLC   HWORD,=H'6'         ERROR IF PARM LENGTH GT 5
         BC    4,RDJFCB
         BC    2,ABEND4
         MVC   SW1,7(1)
RDJFCB   RDJFCB DASD               READ JFCB FOR VOLUME
         MVC   JFCB(44),Z4         MOVE DSNAME FOR VTOC TO DSCB
         OPEN  (DASD),TYPE=J
         TM    DASD+48,X'10'       TEST FOR SUCCESSFUL DASD OPEN
         BC    12,ABEND7           ERROR IF OPEN UNSUCCESSFUL
NOWWHAT1 XC    WORD,WORD
         MVC   WORD+1(3),DASD+45   PICK UP DEB ADDR
         L     6,WORD
         SH    6,=H'15'
         MVC   MBBCCHHR+1(7),0(6)       PICK UP DASD VTOC ADDR
         SH    6,=H'1'
         MVC   DEB(255),0(6)       MOVE DEB FROM SUPERVISOR AREA
         MVC   DEB+255(61),255(6)
         MVC   ADEBP,DASD+45       SAVE OLD DCB POINTER TO DEB
         MVC   DASD+45(3),ADEB     CHANGE DCB POINTER TO DEB
         MVI   DEB+48,X'00'        SET FILE MASK
         XDAP  ECBLAB,RI,DASD,DSCB,140,,MBBCCHHR
         WAIT  ,ECB=ECBLAB
         TM    ECBLAB,X'7F'        TEST SUCCESSFUL READ
         BC    12,ABEND8
READOK   MVC   DS4DEVSZ,DSCB+64    PICK UP TRKS/CYL FOR VOLUME
         MVC   DS4HPCHR,DSCB+45    PICK UP ADDR OF LAST ACTIVE F1 DSCB
         BC    15,REALREAD
INCRMNT  LH    6,HH          INCREMENT TRK ADDR
         LA    6,1(0,6)      IF TRK ADDR GT TRKS/CYL...
         STH   6,HH
         CLC   HH,DS4DEVSZ
         BC    12,REALRED
         XC    HH,HH         SET TRK ADDR TO ZERO...
         LH    6,CC          AND...
         LA    6,1(0,6)      INCREMENT CYL ADDR...
         STH   6,CC          BURMA SHAVE.
*
*
*
*
REALRED  CLC   MBBCCHH+3(4),DS4HPCHR   IF MBBCCHH GT HIGHEST F1
         BC    2,ABEND3                     DATASET NOT ON VOLUME
REALREAD XDAP  ECBIN,RK,DASD,DATA,96,(DSNAME,44),MBBCCHH
         WAIT  ECB=ECBIN
         TM    ECBIN,X'7F'         READ COMPLETE
         BC    1,GOODREAD
         TM    ECBIN,X'41'         KEY NOT FOUND ON THIS TRK
         BC    1,INCRMNT
         TM    ECBIN,X'42'         ATTEMPT TO READ BEYOND VTOC EXTENT
         BC    1,ABEND3
         BC    15,ABEND1           SOME OTHER DUMB PROBLEM
GOODREAD TM    SW2,X'40'
         BC    1,SECURE
         PACK  DTRACKS+6(2),EXPDT(2)   PICK UP EXPIRATION YEAR
         CVB   6,DTRACKS            CONVERT TO BINARY
         STC   6,DSCB+56           STORE YEAR
         PACK  DTRACKS+6(2),EXPDT+2(3) PICK UP EXPIRATION DAY
         CVB   6,DTRACKS           CONVERT TO BINARY
         STH   6,HWORD
         MVC   DSCB+57(2),HWORD    STORE DAY
         XC    HWORD,HWORD
         CLI   SW1,X'00'
         BC    8,UPDATE
SECURE   CLI   SW1,X'01'
         BC    6,OFF
         OI    DSCB+93,X'10'
         BC    15,UPDATE
OFF      CLI   SW1,X'02'
         BC    6,ABEND6
         NI    DSCB+93,X'EF'
UPDATE   XDAP  ECBLABEL,WK,DASD,DATA,96,(DSNAME,44),MBBCCHH
         WAIT  ,ECB=ECBLABEL
         TM    ECBLABEL,X'7F'      TEST SUCCESSFUL WRITE
         BC    12,ABEND2           UNSUCCESSFUL WRITE
         MVC   DASD+45(3),ADEBP    RESTORE OLD DEB POINTER TO DCB
         TM    SW3,X'E0'
         BZ    GOBACK
         CLOSE (DASD)
         MVC   MLINE+53(10),=C'          '
  MVC MLINE,=C'EXPIRY DATE HAS BEEN SUCCESSFULLY CHANGED FOR DSNAME='
         MVC   MLINE+60(40),DSNAME
         PUT   SYSPNT,CLINE
         B     CHKCRDS
EOF      CLOSE (CTLCRD)
         MVC   DSNAME,BLANKS
GOBACK   L     13,4(13)
         RETURN (14,12)
MVDSN    MVC   DSNAME(0),0(4)
MVDDN    MVC   DASD+40(0),0(4)
ABEND1   ABEND 1,DUMP,STEP
ABEND2   ABEND 2,DUMP,STEP
ABEND3   ABEND 3,DUMP,STEP
ABEND4   ABEND 4,DUMP,STEP
ABEND5   ABEND 5,DUMP,STEP
ABEND6   ABEND 6,DUMP,STEP
ABEND7   ABEND 7,DUMP,STEP
ABEND8   ABEND 8,DUMP,STEP
ABEND9   ABEND 9,DUMP,STEP
ABENDA   ABEND 10,DUMP,STEP
SW3      DC    X'00'
CARD     DS    CL80
BLANKS   DC    44CL1' '
CLINE    DS    0CL133
ASA      DC    CL1'1'
         DC    30CL1' '
MLINE    DC    CL80'SYEXPDTE OUTPUT FOR CONTROL CARDS AND MESSAGES'
MDSN     DS    CL22
ONE      DC    H'1'
SAVE1    DS    9D
         DS    0D
DTRACKS  DC    D'0'
DEB      DS    CL316
WORD     DS    F
LIST     DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
DSNAME   DS    CL44
DSCB     DS   0CL140
Z4       DC    11X'04040404'
DATA     DS    CL96
HWORD    DS    H
DS4DEVSZ DS    H
EXPDT    DS    CL5
MBBCCHHR DS    0CL8
MBBCCHH  DS    0CL7
MBB      DC    X'000000'
CC       DS    H
HH       DS    H
       DS    CL1
SW1      DC    X'00'
SW2      DC    X'00'
NONNUM   DC    X'F0F0F0F0F0'
DS4HPCHR DS    CL4
ADEB     DC    AL3(DEB+16)
ADEBP    DS    CL3
CTLCRD   DCB   DSORG=PS,DDNAME=SYSIN,MACRF=GM,EODAD=EOF,EROPT=ABE
SYSPNT   DCB DSORG=PS,DDNAME=SYSPRINT,MACRF=PM,RECFM=FA,BLKSIZE=133
DASD     DCB   DDNAME=SYSDISK,MACRF=(E),EXLST=LIST,EODAD=ABEND3
         END   SYEXPDTE


