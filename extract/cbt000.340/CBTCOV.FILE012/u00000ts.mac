MODLIST  CSECT
*
*
*  THIS VERSION OF RTPMLIST HAS BEEN REVISED TO WORK UNDER MVT AS WELL
*  AS MFT.  TO DO THIS THE PREVIOUS TECHNIQUE OF GETTING INTO SUPVSOR
*  MODE BY MEANS OF A SIO APPENDAGE HAS BEEN REPLACED BY USE OF THE
*  RESPOND SPMODE MACRO (WHICH USES SVC 54 AND REQUIRES ISAM TO BE
*  INCLUDED IN YOUR SYSGEN)
*  THIS VERSION HAS ALSO BEEN MODIFIED SO THAT IT DOES NOT INCREMENT
*  THE USE COUNT FOR EACH DISK PACK IT FINDS IN THE SYSTEM - THIS MEANS
*  THAT OPERATORS ARE NOT PREVENTEE FROM UNLOADING PACKS NOT
*  CURRENTLY IN USE BECAUSE RTPMLIST HAS 'ALLOCATED' THEM
*
*
*  SPECIAL WARNING TO ALL USERS OF RTPMLIST.....
*  IEHLIST HAS A FAULT WHICH WE HAVE APARED AND WHICH WILL BE FIXED
*  IN REL 20 - AT PRESENT IT WILL ALLOW E.G. SVCLIB TO BE SCRATCHED BY
*  USE OF A LISTPDS STATEMENT IF SVCLIB IS ON THE SAME PACK AS THE
*  INL OR INP DATA SETS (WHICH HAVE DISP=(,DELETE)).
*  TO FIX THIS PROBLEM ZAP IEHLIST IEHROOT TO SET ON THE 'DO NOT WRITE
*  BACK TO JOBQ' BIT IN THE MODLJFCB DEFINED IN THAT CSECT - I.E.  USE
*  A REP CARD TO PUT X'08' IN MODLJFCB + 52 IN IEHLIST IEHROOT.
*
*
*
*  THIS PROGRAM ADDS TO THE SERVICES OFFERED BY IEHPROGM AND IEHLIST
*  BY....
*        1.    ALLOWING CATALOGUE REFERENCES WHERE THE STANDARD
*              VOL=REF=ABC IS ALLOWED AS WELL AS VOL=2314=123456
*              CVOL=REF=XYZ IS ALLOWED AS WELL AS CVOL=2302=2302AB
*        2.    ALLOWING INPUT FOR BOTH UTILITIES IN ONE JOB STEP
*              THIS PROGRAM ANALYSES AND CONVERTS THE SYSIN STREAM
*              INTO TWO DATA SETS (ONE FOR IEHPROGM AND ONE FOR
*              IEHLIST) AND THEN LINKS FIRST TO IEHPROGM AND THEN TO
*              IEHLIST TO ALLOW THEM TO PROCESS THE CONVERTED INPUT.
*        3.    PERMITTING CONTINUATION CARDS TO BEGIN IN ANY COLUMN
*              FROM 1 TO 71.
*        4.    ELIMINATING THE NEED FOR THE USER TO SUPPLY DD CARDS
*              FOR EACH DEVICE ON WHICH HE WISHES IEHPROGM/LIST TO
*              OPERATE.  THIS PROGRAM WILL CREATE A TIOT ENTRY FOR
*              EVERY DIRECT ACCESS DEVICE WHICH IS ONLINE AND HAS A
*              VOLUME CURRENTLY ON IT.  THE USER NEED ONLY SUPPLY DD
*              CARDS FOR VOLUMES WHICH MAY NOT BE MOUNTED AT THE
*              START OF HIS JOB.
*  TO ALLOW EASIER USE OF THE 'LISTCAT' FUNCTION IT IS NOW PERMISSIBLE
*        5.    ALLOWING THE ABBREVIATION DSN= WHEREVER DSNAME= IS
*              NORMALLY REQUIRED.
*        6.    NOT REQUIRING A CONTINUATION MARK IN COL 72.  ONE IS
*              INSERTED AUTOMATICALLY IF THE LAST CHARACTER PUNCHED
*              IS A COMMA.
*        7.    ALLOWING A NEW PARAMETER UNIT= WHICH CAN BE USED IN
*              PLACE OF VOL=.  THIS IS INTENDED TO BE USED BY THE
*              OPERATOR WHEN HE HAS SPECIFIED PARM=CONSOLE AND IS
*              TYPING HIS REQUESTS THROUGH THE 1052 - HE MAY FIND IT
*              MORE CONVENIENT TO TYPE 'LISTVTOC  DUMP,UNIT=190' IN
*              PLACE OF 'LISTVTOC  DUMP,VOL=2311=123456' OR
*              'LISTVTOC  DUMP,VOL=REF=XYZ'.
*        8.    IF THE PROGRAM IS EXECUTED WITH 'PARM=CONSOLE' IT WILL
*              ISSUE A WTOR ('**SPEAK') REQUESTING THE OPERATOR TO
*              ENTERHIS REQUESTS.  HE TYPES THESE EXACTLY AS IF HE WERE
*              PUNCHING CARDS (EXCEPT THAT A NAME FIELD IS NOT ALLOWED
*              ON ANY STATEMENT, AND THE VERB MAY BEGIN AS THE FIRST
*              CHARACTER) AND THE PROGRAM WILL REISSUE THE **SPEAK WTOR
*              AFTER EACH EOB.  THE OPERATOR ENDS HIS INPUT WITH 'END'
*              IN PLACE OF A VERB AND THE PROGRAM WILL THEN PROCESS
*              HIS STATEMENTS AS IF THEY HAD BEEN READ FROM CARDS.
*        9.    TO OBTAIN A BETTER LISTING OF THE CATALOGUE THAN IS
*              GIVEN BY THE IEHLIST LISTCTLG FUNCTION A VERB 'LISTCAT'
*              CAN BE USED - THE ONLY OPERAND IS THE VOL SERIAL OF THE
*              PACK CONTAINING THE SYSCTLG TO BE LISTED E.G....
*              LISTCAT  080135
*  TO USE A CATLG REFERENCE,E.G....
*        'LISTCAT  VOL=REF=SYS1.SVCLIB'
*  WITH THIS IMPROVEMENT THE USER NO LONGER NEED KNOW THE ACTUAL VOL
*  SERIAL OF THE PACK ON WHICH THE CATALOGUE RESIDES.
*              WHEN LISTCAT IS USED THE PROGRAM WILL EXECUTE THE CATLG
*              LISTING IMMEDIATELY, AND IF USING THE CONSOLE VERSION
*              THERE WILL BE A PAUSE BEFORE THE NEXT **SPEAK WTOR
*
*  FURTHER IMPROVEMENTS INCLUDED IN THIS VERSION ARE:.........
*
*        1.    YOU CAN OMIT THE 'VOL' PARAMETER FOR LIST,SCRATCH, OR
*              RENAME OPERATIONS IF THE 'DSN' DATA SET IS CATALOGUED.
*              E.G. ' LISTPDS DSN=SYS1.SVCLIB ' WILL NOW GENERATE
*              ' LISTPDS DSNAME=SYS1.SVCLIB,VOL=TYPE=SERIAL '
*        2.    TO AVOID 413 ABENDS IN IEHLIST/IEHPROGM (CAUSED BY
*              REQUESTS FOR VOLUMES NOT CURRENTLY MOUNTED AND FOR WHICH
*              NO UNIT IS AVAILABLE BECAUSE NO CURRENT VOLUME IS
*              DEMOUNTABLE) ALL REQUESTS FOR VOLUMES NOT MOUNTED ARE
*              REJECTED.  ADD YOUR OWN DD CARDS TO GET PRIVATE PACKS
*              MOUNTED BY O.S. FOR THE RTPMLIST STEP.
*        3.    THE HEADING LINE FOR THE INPUT CONFIRMATION LISTING
*              (MODPRINT) NOW INCLUDES THE DATE AND TIME.
         SPACE  2
*  WARNING - 'LISTVTOC' OR 'LISTCTLG' STATEMENTS WITH NO OPERANDS
*            (IMPLYING SYSRES) MUST NOT HAVE NAME FIELDS OR THEY WILL
*            BE REJECTED WITH 'NO VOL SPECIFIED AND NO DSNAME FOR
*            CATLG LOOK-UP OF VOL'.  (SEE CHECK 3 STATEMENTS BEFORE
*            LABEL 'NOTLIST')
         SPACE  2
*  THE JCL REQUIRED FOR THIS PROGRAM IS......
*        //MODLIST EXEC    PGM=RTPMLIST(,PARM=CONSOLE)
*        //MODPRINT DD     SYSOUT=C
*        //SYSCATP  DD     SYSOUT=C,DCB=BLKSIZE=1463
*        //SYSPRINT DD     SYSOUT=C
*        //SYS      DD     VOL=REF=SYS1.SVCLIB,DISP=SHR
*        //SYSCAT   DD     DSN=SYSCTLG,DISP=SHR,VOL=REF=SYS1.SVCLIB
*        //INP      DD     SPACE=(TRK,(2,1)),UNIT=SYSDA
*        //INL      DD     SPACE=(TRK,(2,1)),UNIT=SYSDA
*  THE MODPRINT DCB SPECIFIES ALL DCB PARAMETERS (INCLUDING BLOCKING)
*  THE INP AND INL DATA SETS ARE THE TEMPORARY SYSIN STREAMS AND CAN
*  BE PLACED WHERE YOU LIKE WITH AS MUCH SPACE AS APPROPRIATE.
*  THE SYSCATP DD SPECIFIES THE LISTCAT SYSOUT AND REQUIRES A BLKSIZE.
*  THE SYS DD PROVIDES A MODEL USED WHEN BUILDING THE PSEUDO-TIOT OF
*  ALL AVAILABLE DEVICES.
*  THE SYSPRINT DD SPECIFIES THE NORMAL PRINT OF IEHPROGM/IEHLIST.
*  THE SYSCAT DD PROVIDES A TIOT ENTRY THAT CAN BE ALTERED TO POINT
*  TO ANY DEVICE REQUESTED IN A LISTCAT STATEMENT.
*
*  NOTE THAT VOLUMES FOR WHICH O.S. HAS ISSUED A KEEP MESSAGE WILL
*  HAVE HAD THEIR VOL SERIALS REMOVED FROM THE UCB
*
*  THIS PROGRAM DOES NOT ALLOW ONE PARAMETER TO BE SPLIT OVER TWO
*  CARDS, AND THE 'TRANSLATED VERSION' OF YOUR INPUT (E.G. AFTER
*  EXPANDING DSN= TO DSNAME= )MUST FIT BETWEEN COLUMNS 16 AND 71 OF A
*  CARD IMAGE GENERATED FROM YOUR CARD. FOR EXAMPLE A STATEMENT....
*  LISTPDS  DSN=(NAMELIST......)    IS ONLY ACCEPTABLE IF THE KEYWORD
*  AND THE WHOLE NAMELIST ARE ON ONE CARD AND THE TOTAL LENGTH OF THE
*  PARAMETER DOES NOT EXCEED 55 CHARACTERS.
*  CONFIRMATION OF ALL INPUT, AND ERROR MESSAGES FOR ERRORS FOUND BY
*  THIS PROGRAM, APPEAR IN A SEPARATE SYSPRINT LISTING DEFINED BY THE
*  MODPRINT DD CARD.
*
*  ERRORS DETECTED BY THIS PROGRAM ARE NOT PASSED ON TO THE STANDARD
*  UTILITIES, BUT FURTHER ERRORS MAY BE FOUND BY THEM IN THE CARDS
*  PASSED ON AND THE NORMAL SYSPRINT LISTING IS GIVEN
*
*  CARDS ARE PRESENTED TO THE STANDARD UTILITIES IN SEQUENCE OF
*  ORIGINAL INPUT AFTER SEPARATING THEM INTO TWO STREAMS, I.E. ALL
*  IEHPROGM CARDS ARE PROCESSED BEFORE ALL IEHLIST CARDS.
*
*  THE HIGHEST RETURN CODE FROM THE TWO UTILITIES IS USED AS THE
*  RETURN CODE FOR THIS PROGRAM.
*
*        BEGIN BASE=(11,12)
*  THIS IS THE EXPANSION OF THE RESPOND TSG BEGIN MACRO
         STM   14,12,12(13)
         BALR  11,0
         USING *,11,12
IHB1     EQU   *
         L     12,IHB1A
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         B     IHB1B
IHB1A    DC    A(IHB1+4096)
IHB1B    EQU   *
         LR    10,1
         LA    4,UCBADS
         LA    5,200(4)
         L     1,16
         CLI   116(1),32                TEST FOR MFT
         BNL   *+8
         OI    MVT,255
         L     2,40(1)
         ST    2,UCBLKPAD
         L     1,0(1)
         L     1,4(1)
         L     2,12(1)
         ST    2,ART               ADDRESS REAL TIOT
         LA    2,12(1)
         ST    2,ATCBTIOT
         L     1,16
         L     2,40(1)             UCB LOOK-UP TABLE
SRCHUCBS LH    3,0(2)
         LTR   3,3
         BC    4,ENDUCB
         BC    2,UCBFND
NEXTUCB  LA    2,2(2)
         B     SRCHUCBS
UCBFND   CLI   18(3),32
         BNE   NEXTUCB             NOT DISK
         TM    3(3),128
         BC    8,NEXTUCB           OFFLINE
         CLI   28(3),0
         BE    NEXTUCB             NO VOLUME MOUNTED
         STH   3,0(4)
         CR    4,5                 END OF LIST
         BE    NEXTUCB             LIST AREA FULL
         LA    4,2(4)
         B     NEXTUCB
ENDUCB   S     4,ADUCBADS          NO OF ENTRIES * 2
         LR    5,4
         SLA   5,2                 NO OF ENTRIES * 8
         AR    5,4                 NO OF ENTRIES * 10
         SLA   5,1                 NO OF ENTRIES * 20
         LA    5,20(5)
         L     2,ART
         LA    2,24(2)
         SR    3,3
RTLOOP   IC    3,0(2)
         LTR   3,3
         BZ    ENDRTIOT
         CLC   4(8,2),SYSNM
         BNE   *+10
         MVC   MODEL,0(2)
         CLC   4(8,2),SYSCATNM
         BNE   *+14
         LR    1,2
         S     1,ART
         ST    1,SYSCATDP
         CLC   4(8,2),SYSPRNM
         BNE   *+8
         ST    2,SYSPRAD
         AR    2,3
         B     RTLOOP
ENDRTIOT S     2,ART
         ST    2,LRT               LENGTH REAL TIOT
         AR    2,5
         ST    2,FREELEN
         NC    SEVEN,FREELEN
         BZ    NOROUND
         LA    15,8
         S     15,SEVEN
         AR    2,15
         ST    2,FREELEN
NOROUND  EQU   *
         SPMODE  PROB,0
         L     0,FREELEN
         CLI   MVT,255
         BE    MVTGET
         GETMAIN  R,LV=(0)
GOT      ST    1,APT                    ADDRESS OF PSEUDO-TIOT
         A     1,SYSCATDP
         ST    1,SYSCATDP
         B     MOVETIOT
         SPACE
*  FOR MVT THE PSEUDO-TIOT IS ACQUIRED IN SQS (SP 255)
*  THE FOLLOWING CODE IS A NON-PERMITTED FORM OF GETMAIN
*        GETMAIN  R,LV=(0),SP=255
         SPACE
MVTGET   ST    0,GETLIST
         MVI   GETLIST,255
         CNOP  0,4
         BAL   1,*+8
GETLIST  DC    F'0'
         L     0,0(1)
         SVC   10
         B     GOT
         SPACE
MOVETIOT STM   2,6,SIOSAVE
MOVEOFF  L     2,APT
         L     3,ART
         L     4,LRT
         LR    5,4
*  THIS CODE ALLOWS FOR THE REAL TIOT TO BE LONGER THAN 256 BYTES
         LA    6,256
BIGTIOT  SR    4,6
         BM    ENDBTIOT
         MVC   0(256,2),0(3)
         AR    2,6
         AR    3,6
         B     BIGTIOT
ENDBTIOT AR    4,6
         BCTR  4,0
         LTR   4,4
         BM    *+8           IF LRT IS A MULTIPLE OF 256
         EX    4,MVRT
         L     2,APT
         AR    2,5
         L     3,ADUCBADS
         SR    5,5
         SR    6,6
MTLOOP   LH    4,0(3)
         LTR   4,4
         BC    8,ENDMOVE
         TM    3(4),128
         BC    8,NEXT              NOW OFFLINE
         CLI   28(4),0
         BE    NEXT                NOW NO VOLUME
         STH   4,MODEL+18
         LA    6,1(6)
         CVD   6,DWA
         UNPK  DDNUMB,DWA+6(2)
         OI    DDNUMB+2,240
         MVC   MODEL+7(2),DDNUMB+1
         MVC   0(20,2),MODEL
*  THE ORIGINAL VERSION OF RTPMLIST PUT ON THE 'ALLOCATED' BIT IN EACH
*  DISK UCB, AND INCREMENTED THE USE COUNT IN EACH UCB BY ONE.  THIS
*  PROCEDURE INHIBITS UNLOAD COMMANDS ISSUED BY THE OPERATOR FOR PACKS
*  WHICH ARE PROBABLY NOT BEING REFERRED TO BY RTPMLIST AND SO THIS
*  CODE HAS BEEN REMOVED
         LA    2,20(2)
NEXT     LA    3,2(3)
         B     MTLOOP
ENDMOVE  MVI   0(2),0
         L     4,ATCBTIOT
         MVC   0(4,4),APT          CHANGE TIOT ADDRESS IN TCB
         LM    2,6,SIOSAVE
         SPMODE  PROB,*
OPEN     DS    0H
         OPEN  (INP,(OUTPUT),INL,(OUTPUT),PRINT,(OUTPUT))
         TIME
         ST    0,TIM
         UNPK  TIMP,TIM
         MVC   HHMM(2),TIMP+1
         MVC   HHMM+3(2),TIMP+3
         DATE  YYDDD,FORMAT='YY DDD   DD MMM YY',PREFORM=YES
         MVC   YYDDD+16(2),YYDDD
         L     1,0(10)
         LH    2,0(1)
         LTR   2,2
         BZ    NOTCONS
         CLI   2(1),195
         BE    CONSETUP
NOTCONS  EQU   *
         OPEN  (SYSIN)
NEWCARD  LA    4,CARD
         LA    5,OUT
         LA    6,CARD-1
         LA    8,CARD+71
         LA    9,OUT+71
         BAL   7,GETCARD
         MVI   VOLFND,0
         MVI   SAVEDSN+7,64
         LA    1,INL
         ST    1,FNM
         LR    14,4
         BAL   10,GETIN
         MVC   ERRMSG+52(25),EA
         CR    6,8
         BNL   ERROR
CONSOPT  BC    0,VERB
         CR    4,14
         BH    VERB
NAME     LR    3,6
         LA    3,1(3)
         SR    3,4
         LR    2,4
         BAL   10,PUTOUT
A        BAL   10,GETIN
         CR    4,8
         BL    VERB+4
         BAL   7,GETCARD
         B     A
VERB     LA    5,1(5)
         CLI   0(4),C'L'
         BE    *+12
         LA    1,INP
         ST    1,FNM
         LR    3,6
         LA    3,1(3)
         SR    3,4
         LR    2,4
         BAL   10,PUTOUT
         MVC   SV,0(4)
         CLC   0(7,4),LC
         BE    CATLIST
OPERAND  BAL   10,GETIN
         CR    4,8
         BL    ISITDSN
         B     DISPOSE
ISITDSN  CLC   0(4,4),DSN
         BNE   ISITUNIT
         S     4,THREE
         MVC   0(6,4),DSNAME
ISITUNIT CLC   0(5,4),UNIT
         BNE   ISITREF
         MVC   UNITNAME,5(4)
         S     4,SIX
         MVC   0(8,4),VOLREF
         MVC   4(3,4),UNITNAME
         L     10,UCBLKPAD
SRCHUCBA LH    1,0(10)
         LTR   1,1
         BC    4,ENDUCBSA
         BC    8,NEXTUCBA
         CLC   UNITNAME,13(1)
         BE    UCBFNDA
NEXTUCBA LA    10,2(10)
         B     SRCHUCBA
ENDUCBSA EQU   *
UCBFNDA  MVC   8(6,4),28(1)
         MVC   UM,4(4)
         MVC   UV,28(1)
         PUT   PRINT,UNITMSG
         SP    PCNTR,ONE
ISITREF  CLC   0(8,4),VOLREF
         BE    ISREF
         CLC   0(9,4),CVOLREF
         BNE   NOTREF
         LA    14,9(4)
         B     *+8
ISREF    LA    14,8(4)
         MVC   VTS+15(1),0(6)
         LR    3,6
         MVC   ERRMSG+52(25),EB
         SR    3,14
         BC    12,ERROR
         BCTR  3,0
         MVC   LOCNAME,SPACES
         MVI   LOCNMOVE+5,8
         CLI   0(4),C'V'
         BE    *+8
         MVI   LOCNMOVE+5,9
         EX    3,LOCNMOVE
         LOCATE  CAMLST
         LTR   15,15
         BZ    LALOC
         MVC   ERRMSG+52(25),ED
         C     15,FOUR
         BE    ERROR
         MVC   ERRMSG+52(25),EC
         C     15,TWELVE
         BE    ERROR
         MVC   ERRMSG+52(25),EF
         B     ERROR
LALOC    LA    14,LOCAREA+2
         MVC   ERRMSG+52(25),EH
         CLI   2(14),32
         BNE   ERROR
         SR    1,1
         IC    1,3(14)
         LA    2,DISKTABA-4
         SLL   1,2
         AR    2,1
         MVC   VTS+4(4),0(2)
         MVC   VTS+9(6),4(14)
         LA    2,VTS
         LA    3,16
         CLI   0(4),C'C'
         BNE   *+12
         LA    2,CVTS
         LA    3,17
         BAL   10,PUTOUT
NEXTFLDA CLI   0(6),64
         BE    DISPOSE
         CLI   1(6),64
         BNE   OPERAND
         MVC   ERRMSG+52(25),EG
         CLI   CONT,64
         BE    ERROR
         BAL   7,GETCARD
         B     OPERAND
NOTREF   LA    3,1(6)
         SR    3,4
         LR    2,4
         BAL   10,PUTOUT
         B     NEXTFLDA
DISPOSE  CLI   CONT,64
         BE    *+12
         BAL   7,GETCARD
         B     DISPOSE
         CLC   SV,SVA
         BNE   NOTLIST
         CLC   OUT+10(60),SPACES
         BE    DISPOUT       LISTVTOC OR LISTCTLG WITH NO OPERANDS
         B     CHKV
NOTLIST  CLC   SV,SVB
         BE    CHKV
         CLC   SV,SVC
         BNE   DISPOUT
CHKV     CLI   VOLFND,0
         BNE   DISPOUT
         MVC   LOCNAME,SAVEDSN+7
         MVI   0(4),64
         MVI   CONT,64
         MVI   0(6),64
         BCTR  5,0
         MVI   0(5),C','
         LA    5,1(5)
         LOCATE  CAMLST
         LTR   15,15
         BZ    LALOC
         MVC   ERRMSG+52(25),EI
         B     ERROR
DISPOUT  L     1,FNM
         BCTR  5,0
         MVI   0(5),64
         PUT   (1),OUT
NEXTCARD PUT   PRINT,ONEUP
         SP    PCNTR,ONE
         MVC   OUT,SPACES
         B     NEWCARD
EOF      CLOSE (INL,,INP,,PRINT)
         TM    GETCARD+1,240
         BO    CONSEND
         CLOSE (SYSIN)
         FREEPOOL  SYSIN
CONSEND  EQU   *
*
*  BOTH IEHPROGM (SCRATCH MEMBER FUNCTION) AND IEHLIST (LISTPDS) OPEN
*  A PDS BY OPEN TYPE=J USING THE FIRST DD CARD THEY FIND IN THE TIOT
*  WHICH IS ALLOCATED TO THE VOLUME ON WHICH THE PDS RESIDES.
*  BECAUSE THEY DO NOT PUT ON THE 'DO NOT WRITE BACK TO JOBQ' BIT IN
*  THE JFCB IT IS WRITTEN BACK WITH ITS DSNAME CHANGED TO THAT OF THE
*  PDS.  IF THE ORIGINAL JFCB WAS FOR A DATA SET THAT HAD DISP=DELETE
*  THEN THS PDS WILL BE DELETED AT STEP TERMINATION UNLESS IT IS DATE
*  PROTECTED.
*  THE FOLLOWING RDJFCB PRESERVES AN UNCORRUPT VERSION OF THE JFCBS
*  THAT COULD CAUSE THIS PROBLEM, AND ARFTER IEHPROGM AND IEHLIST
*  THESE ORIGINAL JFCBS ARE WRITTEN BACK TO THE JOBQ BY OPEN/CLOSE.
*  AN ASSOCIATED PROBLEM IS CURED AT THE SAME TIME - THE DSNAMES OF THE
*  SYSOUT DATA SETS COULD BE CORRUPTED BY HAVING THEIR NAMES CHANGED
*  SO THAT THEY COULD NOT BE PRINTED AFTER THE JOB.
*
         RDJFCB  (PRINT,,SLIST,,INP,,INL,,SYSIN)
         LINK  EP=IEHPROGM,PARAM=(OPT,DDN),VL=1
         ST    15,PROGRET
         MVC   NAMEIN,NAMEL
         LINK  EP=IEHLIST,PARAM=(OPT,DDN),VL=1
         ST    15,LISTRET
         SPACE
         SPMODE  PROB,0
REVERT   STM   2,4,SIOSAVE
REVOFF   L     2,SYSPRAD
         OI    3(2),128
         L     2,ART
         L     3,ATCBTIOT
         ST    2,0(3)
         L     1,APT
         L     0,FREELEN
         CLI   MVT,255
         BE    MVTFREE
         FREEMAIN  R,LV=(0),A=(1)
         B     GETOUT
*  FOLLOWING CODE IS A NON-PERMITTED FORM OF FREEMAIN
*        FREEMAIN  R,LV=(0),A=(1),SP=255
MVTFREE  ST    0,FREELIST
         MVI   FREELIST,255
         CNOP  0,4
         B     *+8
FREELIST DC    F'0'
         L     0,FREELIST
         LA    1,0(1)
         SVC   10
GETOUT   LM    2,4,SIOSAVE             **MVT**
         SPMODE  PROB,*
         SPACE
GOBACK   EQU   *
         OPEN  (PRINT,OUTPUT,SLIST,OUTPUT,INP,OUTPUT,INL,OUTPUT),TYPE=J
         OPEN  (SYSIN),TYPE=J
         PUT   PRINT,ENDMSG
         MVC   ENDMSGID+7(8),ENDMSGCA
         PUT   SLIST,ENDMSG
         CLOSE (PRINT,,SLIST,,INP,,INL,,SYSIN)
         L     15,LISTRET
         C     15,PROGRET
         BNL   *+8
         L     15,PROGRET
*        GOBACK  RC=(15)
*  THIS IS THE EXPANSION OF THE RESPOND TSG GOBACK MACRO
         L     13,4(13)
         L     14,12(13)
         LM    0,12,20(13)
         MVI   12(13),255
         BR    14
GETCARD  BC    0,CONSIN
         GET   SYSIN,CARD
         LA    1,71
         LA    14,CARD+70
TESTCONT CLI   0(14),64
         BNE   *+10
         BCTR  14,0
         BCT   1,TESTCONT
         CLI   0(14),C','
         BNE   GOTCARD
         MVI   CARD+71,C'*'
GOTCARD  EQU   *
         SP    PCNTR,ONE
         BP    PUTLINE
         PUT   PRINT,NP
         PUT   PRINT,TITLE
         ZAP   PCNTR,PLIMIT
PUTLINE  MVI   LINE,9
         PUT   PRINT,LINE
         MVC   CONT,CARD+71
         MVC   CARD+71(9),SPACES
         LA    6,CARD-1
         BR    7
CONSETUP WTO   'REPLY TO ''**SPEAK'' MESSAGES FROM RTPMLIST WITH INPUT *
               FOR IEHPROGM/IEHLIST, OR WITH ''END'''
         OI    GETCARD+1,240
         OI    CONSOPT+1,240
         B     NEWCARD
CONSIN   MVC   CARD,SPACES
         WTOR  '**SPEAK',CARD,71,ECBC
         WAIT  ECB=ECBC
         NI    ECBC,63
         OC    CARD,SPACES
         CLC   CARD(3),ENDC
         BE    EOF
         LA    1,CARD+70
         LA    6,72
         CLI   0(1),64
         BE    CONCLI
         CLI   0(1),C','
         BNE   CONERR
CONCLI   CLI   0(1),64
         BNE   CONLC
         BCTR  1,0
         BCT   6,CONCLI
CONERR   WTO   'REPLY IGNORED.  PLEASE SPEAK UP'
         B     CONSIN
CONLC    CLI   0(1),C','
         BNE   GOTCARD
         MVI   CARD+71,C'*'
         B     GOTCARD
ERROR    PUT   PRINT,ERRMSG
         SP    PCNTR,ONE
CLIERR   CLI   CONT,64
         BE    NEXTCARD
         BAL   7,GETCARD
         B     CLIERR
GETIN    TRT   1(80,6),NBT
         BC    7,SET4
         LA    4,80(6)
         LR    6,4
         BR    10
SET4     LR    4,1
         TRT   0(80,4),BCT
         BC    7,*+10
         LA    6,80(4)
         BR    10
         SR    15,15
SET4LP   LR    6,1
         STC   2,FUNCBYTE
         CLI   FUNCBYTE,3               OPEN BRACKETS
         BNE   *+8
         LA    15,1(15)
         CLI   FUNCBYTE,4               CLOSE BRACKETS
         BNE   *+6
         BCTR  15,0
         LTR   15,15
         BC    2,*+10
         CLI   FUNCBYTE,2
         BCR   13,10                    COMMA OR BLANK
         CLI   FUNCBYTE,2
         BE    ERRSPLIT      BLANK FOUND WITHIN BRACKETS
         TRT   1(80,6),BCT
         BC    7,SET4LP
         LA    6,80(6)
         BR    10
ERRSPLIT MVC   ERRMSG+52(25),EK
         B     ERROR
PUTOUT   LR    14,5
         AR    14,3
         CR    14,9
         BL    FITS
         MVI   0(9),C'*'
         L     1,FNM
         PUT   (1),OUT
         MVC   OUT,SPACES
         LA    5,OUT+15
FITS     BCTR  3,0
         EX    3,MVTO
         LA    5,1(3,5)
         CLC   SV,SVA
         BE    CHKDV
         CLC   SV,SVB
         BE    CHKDV
         CLC   SV,SVC
         BCR   7,10
CHKDV    CLC   SAVEDSN(7),0(2)
         BNE   CHKVR
         MVC   SAVEDSN+7(44),SPACES
         BCTR  3,0
         EX    3,MVTODSN
CHKVR    CLC   0(4,2),VOLREF
         BCR   7,10
         MVI   VCLC+3,8
         CLI   7(2),C'='
         BE    *+8
         MVI   VCLC+3,9
         MVI   VOLFND,255
         L     15,UCBLKPAD
VSRCHA   LH    1,0(15)
         LTR   1,1
         BC    4,ENDVSRCH
         BC    8,NEXTVS
         TM    18(1),32
         BZ    NEXTVS
VCLC     CLC   9(6,2),28(1)
         BCR   8,10
NEXTVS   LA    15,2(15)
         B     VSRCHA
ENDVSRCH MVC   ERRMSG+52(25),EJ
         B     ERROR
MVTODSN  MVC   SAVEDSN(1),0(2)
MVTO     MVC   0(1,5),0(2)
LOCNMOVE MVC   LOCNAME(1),8(4)
CATLIST  BAL   10,GETIN
         CLC   0(8,4),VOLREF
         BNE   LUCBLK
         MVC   LOCNAME,8(4)
         LOCATE  CAMLST
         LA    4,LOCAREA+6
LUCBLK   L     10,UCBLKPAD
SU       LH    1,0(10)
         LTR   1,1
         BC    4,EU
         BC    8,NU
         CLI   18(1),32
         BNE   NU            NOT DISK
         CLC   0(6,4),28(1)
         BNE   NU            NOT VOL SER REQUESTED
         L     15,SYSCATDP
         SPMODE  PROB,0
         MVC   18(2,15),0(10)               UCB ADDRESS TO TIOT
         SPMODE  PROB,*
         RDJFCB (SYSCATA)
         MVC   JFCB+118(6),0(4)
         CALL  PUNCHCAT,(JFCB)
         LTR   15,15
         BC    8,RETOK
         PUT   PRINT,CATERROR
         SP    PCNTR,ONE
RETOK    EQU   *
         B     NEXTCARD
NU       LA    10,2(10)
         B     SU
EU       PUT   PRINT,NVFC
         SP    PCNTR,ONE
         B     NEXTCARD
PRINT    DCB   DDNAME=MODPRINT,DSORG=PS,MACRF=(PM),RECFM=FBM,          *
               LRECL=81,BLKSIZE=1377,EXLST=XLISTC
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),RECFM=FB,              *
               LRECL=80,EODAD=EOF,EXLST=XLISTF
INP      DCB   DDNAME=INP,DSORG=PS,MACRF=(PM),RECFM=FB,                *
               LRECL=80,BLKSIZE=1440,EXLST=XLISTD
INL      DCB   DDNAME=INL,DSORG=PS,MACRF=(PM),RECFM=FB,                *
               LRECL=80,BLKSIZE=1440,EXLST=XLISTE
SLIST    DCB   DDNAME=SYSCATP,DSORG=PS,MACRF=(PM),LRECL=133,           *
               RECFM=FBM,EXLST=XLISTB
SYSCATA  DCB   DDNAME=SYSCAT,DSORG=DA,MACRF=(E),EXLST=XLISTA
XLISTA   DC    X'87',AL3(JFCB)
XLISTB   DC    X'87',AL3(JFCBB)
XLISTC   DC    X'87',AL3(JFCBC)
XLISTD   DC    X'87',AL3(JFCBD)
XLISTE   DC    X'87',AL3(JFCBE)
XLISTF   DC    X'87',AL3(JFCBF)
SIX      DC    F'6'
SYSCATDP DC    F'0'
UCBLKPAD DC    F'0'
TIM      DC    F'0'
SYSNM    DC    CL8'SYS'
SYSPRNM  DC    CL8'SYSPRINT'
SYSPRAD  DC    F'0'
ECBC     DC    F'0'
THREE    DC    F'3'
FNM      DC    F'0'
DISKTABA DC    C'2311'
         DC    C'2301'
         DC    C'2303'
         DC    C'2302'
         DC    C'2321'
         DC    CL8' '
         DC    C'2314'
MVRT     MVC   0(1,2),0(3)
MVT      DC    X'00'
VOLFND   DC    X'00'
DDNUMB   DC    CL3' '
SIOSAVE  DC    5F'0'
TIMP     DC    D'0'
DWA      DC    D'0'
ADUCBADS DC    A(UCBADS)
UCBADS   DC    102XL2'00'
APT      DC    F'0'
ART      DC    F'0'
LRT      DC    F'0'
ATCBTIOT DC    F'0'
SEVEN    DC    F'7'
FREELEN  DC    F'0'
SV       DC    CL4' '
SVA      DC    C'LIST'
SVB      DC    C'SCRA'
SVC      DC    C'RENA'
MODEL    DC    XL20'00'
DSN      DC    C'DSN='
DSNAME   DC    C'DSNAME'
ENDC     DC    C'END'
CVTS     DC    C'C'
VTS      DC    C'VOL=TYPE=SERIAL,'
CVOLREF  DC    C'CVOL=REF='
VOLREF   DC    C'VOL=REF='
ERRMSG   DC    X'09'
         DC    CL80'*** ABOVE CARD NOT PASSED TO IEHPROGM/LIST BECAUSE '
                '
EA       DC    CL25'IT IS BLANK'
EB       DC    CL25'REF IS MISSING'
EC       DC    CL25'REF IS NOT A DSNAME'
ED       DC    CL25'CVOL NOT MOUNTED'
EF       DC    CL25'OF ERROR IN CATLG LOOK-UP'
EG       DC    CL25'CONTINUATION MARK MISSING'
EH       DC    CL25'REF IS TO NON-DISK DSNAME'
EI       DC    C'NO VOL AND DSN NOT CATLGD'
EJ       DC    C'VOL NOT CURRENTLY MOUNTED'
EK       DC    CL25'OF INCOMPLETE PARAMETER'
PAD      DS    CL6
LINE     DC    X'09'
CARD     DC    CL80' '
SPACES   DC    CL80' '
OUT      DC    CL80' '
CATERROR DC    X'09'
         DC    CL80'NO DSNAME SYSCTLG, OR VOL NOT MOUNTED, OR I/O ERROR*
                IN READING SYSCTLG DSCB'
PCNTR    DC    PL2'0'
ONE      DC    P'1'
PLIMIT   DC    P'55'
NP       DC    X'8B'
TITLE    DC    X'19',CL15' ',C'INPUT TO RTPMLIST AT '
HHMM     DC    C'HH.MM ON '
YYDDD    DC    C'YY.DDD ( DD MMM YY )',CL15' '
ONEUP    DC    X'0B'
SAVEDSN  DC    CL50'DSNAME='
NBT      DC    XL193'00'
         DC    9X'FF'
         DC    XL7'00'
         DC    9X'FF'
         DC    XL8'00'
         DC    8X'FF'
         DC    XL6'00'
         DC    10X'FF'
         DC    XL6'00'
BCT      DC    XL64'00'
         DC    X'02'
         DC    XL12'00'
         DC    X'03'                    OPEN BRACKET
         DC    XL15'00'
         DC    X'04'                    CLOSE BRACKET
         DC    XL13'00'
         DC    X'01'
         DC    XL148'00'
CONT     DC    C' '
FUNCBYTE DC    X'00'
         CNOP  2,4
OPT      DC    H'0'
         CNOP  2,4
DDN      DC    H'40'
         DC    XL32'00'
NAMEIN   DC    CL8'INP'
NAMEL    DC    CL8'INL'
CAMLST   CAMLST  NAME,LOCNAME,,LOCAREA
LOCNAME  DC    CL44' '
LISTRET  DC    F'0'
PROGRET  DC    F'0'
TWELVE   DC    F'12'
FOUR     DC    F'4'
JFCB     DC    CL176' '
JFCBB    DC    CL176' '
JFCBC    DC    CL176' '
JFCBD    DC    CL176' '
JFCBE    DC    CL176' '
JFCBF    DC    CL176' '
SYSCATNM DC    CL8'SYSCAT'
LC       DC    C'LISTCAT'
UNIT     DC    C'UNIT='
UNITNAME DC    CL3' '
NVFC     DC    X'09'
         DC    CL80'VOLUME FOR LISTCAT CANNOT BE FOUND AMONG THOSE CURR*
               ENTLY MOUNTED'
UNITMSG  DC    X'09'
         DC    C'(NOTE THAT VOLUME ON '
UM       DC    CL3' '
         DC    C' IS '
UV       DC    CL6' '
         DC    CL46')'
ENDMSG   DC    X'09'
         DC    CL55' '
ENDMSGID DC    C'END OF MODPRINT OUTPUT'
         DC    CL55' '
ENDMSGCA DC    C'SYSCATLG'
         DS    0D
LOCAREA  DS    CL265
PUNCHCAT CSECT
*        BEGIN
*  THIS IS THE EXPANSION OF THE RESPOND TSG BEGIN MACRO
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         L     1,0(1)
         MVC   VOLNO,118(1)
         ST    1,XLIST
         MVI   XLIST,X'87'
         L     1,16
         L     1,28(1)
         ST    1,CONVERT     PCNVT ADDRESS FROM CVT
         SP    PCNTRA,PCNTRA
         MVC   NINC,NINCA
         MVC   NEMP,NINCA
         MVC   NPU,NINCA
         OBTAIN  CAMLSTA
         LTR   15,15
         BC    7,GOB
         MVC   CC(4),IN+63
         MVC   DISKAD+3(4),IN+63
         MVI   DISKAD+7,X'01'
         LA    2,DISKAD
         LA    3,IN+24
         LA    4,LNAME
         STM   3,4,DISKAD+8
         OPEN  (SYSCAT),TYPE=J
         OPEN  (LIST,(OUTPUT))
         TIME
         ST    0,TIME
         MVI   TIME+3,12
         ST    1,DATE
         ED    HDRTIME,TIME
         ED    HDRDATE,DATE+1
         MVC   HDRVOLNO,VOLNO
         ENQ   (QNAME,RNAME,E,7,SYSTEM)
READ     XDAP  INCB,RI,SYSCAT,NN,264,,(2)
         WAIT  ECB=INCB
         CLI   INCB,X'7F'
         BNE   READERR
FTB      B     SETEND
         NI    INCB,63
         LM    3,4,8(2)
         LA    15,LASTLINE
         SR    15,4
         BCTR  15,0
         EX    15,MSP
NEXTFLD  CLC   0(8,3),FFS
         BNE   TYPENTRY
         CLC   8(3,3),ZEROTTR
         BE    TESTEMPT
         S     2,SIXTEEN
         BAL   5,CALCTTR
         A     2,SIXTEEN
         LA    3,IN+2
         ST    3,8(2)
         B     READ
READERR  ABEND 80,DUMP
TESLEVEL C     2,ADISKAD
         BE    END
         S     2,SIXTEEN
         B     READ
END      DEQ   (QNAME,RNAME,7,SYSTEM)
         CLOSE (SYSCAT)
         OPEN  (BSAMCAT)
         LH    2,ENDTTR
         LA    2,1(2)
         SR    3,3
         IC    3,ENDTTR+2
         MR    2,3
         LM    4,6,ZREGS
READB    READ  DECB,SF,BSAMCAT,NN
         CHECK DECB
         LA    4,1(4)
         CLI   NN,0
         BNE   *+12
         LA    5,1(5)
         B     BCTA
         LH    7,IN
         LA    8,256
         SR    8,7
         AR    6,8
BCTA     BCT   3,READB
EOFA     CVD   4,DISKAD
         ED    NINC,DISKAD+5
         CVD   5,DISKAD
         ED    NEMP,DISKAD+5
         SRA   6,8
         CVD   6,DISKAD
         ED    NPU,DISKAD+5
         PUT   LIST,SPACEUP
         PUT   LIST,LASTLINE
         CLOSE (LIST,,BSAMCAT)
         FREEPOOL  LIST
         SR    15,15
*        GOBACK  RC=(15)
*  THIS IS THE EXPANSION OF THE RESPOND TSG GOBACK MACRO
GOB      L     13,4(13)
         L     14,12(13)
         LM    0,12,20(13)
         MVI   12(13),255
         BR    14
SETEND   MVC   ENDTTR,IN+14
         NI    FTB+1,15
         B     FTB+4
TYPENTRY CLI   11(3),0
         BE    IPE
         CLI   11(3),2
         BE    GDG
         MVI   INDSW,255
         CLI   11(3),3
         BE    OCVLPTR
         CLI   11(3),4
         BE    ALIAS
         CLI   11(3),5
         BE    CVOLPTR
         CLI   11(3),1
         BE    VOLCBPTR
         CLI   11(3),7
         BNL   DATASET
         SR    5,5
         IC    5,11(3)
         SLL   5,1
         AR    3,5
         B     NEXTFLD
IPE      BAL   5,MOVEN
         MVI   0(7),C'.'
         MVI   INDSW,0
         LA    7,1(7)
         LR    4,7
         BAL   5,CALCTTR
         LA    3,12(3)
         CLI   GSTRING,255
         BNE   *+16
         MVI   INDSW,1
         LA    3,4(3)
         MVI   GSTRING,0
         ST    3,8(2)
         LA    2,16(2)
         LA    3,IN+20
         STM   3,4,8(2)
         B     READ
GDG      MVI   GSTRING,255
         MVC   LTYPE,LTYPEGDG
         SR    8,8
         IC    8,13(3)
         CVD   8,DW
         ED    LTYPE+3(4),DW+6
         TM    12(3),2
         BNO   *+8
         MVI   LTYPE+8,C'D'
         TM    12(3),1
         BNO   *+8
         MVI   LTYPE+9,C'E'
         MVC   SLT,LTYPE
         B     IPE
DATASET  CLI   0(3),C'G'
         BNE   NGDG
           CLC      5(2,3),V0
         BNE   NGDG
         TM    1(3),240
         BO    NGDG
         XC    1(4,3),FFS
         MVC   LTYPE,SLT
         MVC   SLT,SPACESA
NGDG     BAL   5,MOVEN
         LA    9,14(3)
         BAL   5,FINDTYPE
         CLI   11(3),7
         BH    MULVOL
         MVC   LVS(6),18(3)
           CLC      24(2,3),HONE
           BNH      NODSEQ
           LH       1,24(3)
           CVD      1,DW
           MVC      SEQ,PMASK
           MVC      LVS+6(4),BRACKETS
           ED       SEQ,DW+6
           MVC      LVS+7(2),SEQ+2
NODSEQ     EQU      *
BLANKOUT LA    3,26(3)
         BAL   5,PRINTA
         B     NEXTFLD
FINDTYPE CLI   2(9),128
         BNE   TDISK
         TM    1(9),32
         BNO   *+12
         MVC   LVTYPE,T24004                800 DUAL DENSITY
         BR    5
         TM    1(9),128
         BNO   *+12
         MVC   LVTYPE,T24002                7 TRK
         BR    5
         TM    0(9),4
         BNO   *+12
         MVC   LVTYPE,T24003                1600 BPI
         BR    5
         MVC   LVTYPE,T2400
         BR    5
TDISK    CLI   2(9),32
         BNE   ENDT          BLANK - UNKNOWN DEVTYPE
         SR    6,6
         IC    6,3(9)
         LA    1,DISKTAB-4
         SLL   6,2
         AR    1,6
         MVC   LVTYPE(4),0(1)
ENDT     BR    5
PRINTA   SP    PCNTRA,ONEA
         BC    10,PUTLINEA
         PUT   LIST,NPA
         MVC   PCNTRA,PLIMITA
         MVC   HDRPNO,PMASK
         AP    PNO,ONEA
         ED    HDRPNO,PNO
         PUT   LIST,HDR
         PUT   LIST,TITLEA
PUTLINEA PUT   LIST,LINEA
         LA    15,LASTLINE
         SR    15,4
         BCTR  15,0
         EX    15,MSP
         BR    5
MSP      MVC   0(1,4),SPACESA
MOVEN    LR    6,3
         LR    7,4
         LA    8,8
MOVN     MVC   0(1,7),0(6)
         LA    6,1(6)
         LA    7,1(7)
         CLI   0(6),64
         BE    MOVEND
         BCT   8,MOVN
MOVEND   BR    5
CALCTTR  STM   2,14,REGS
         LA    8,REGS
         MVC   RAWTTR,8(3)
         L     0,RAWTTR
         L     1,SYSCAT+44
         LA    1,0(1)
         LA    2,16(2)
         L     15,CONVERT
         BALR  14,15
         LM    2,14,0(8)
         BR    5
MULVOL   OI    MPSW,255
         LA    6,LVS
         LA    7,14(3)
         MVC   LASTTYPE,14(3)
         SR    8,8
         IC    8,11(3)
         LA    3,12(3)
         SLA   8,1
         AR    3,8
         SRA   8,2
SETMSER  MVC   0(6,6),4(7)
           CLC      10(2,7),HONE
           BNH      NOMSEQ
           LH       1,10(7)
           CVD      1,DW
           MVC      SEQ,PMASK
           MVC      6(4,6),BRACKETS
           ED       SEQ,DW+6
           MVC      7(2,6),SEQ+2
           LA       6,4(6)
NOMSEQ     EQU      *
         SH    8,THREEA
         BC    8,MVPRNT
         LA    7,12(7)
         CLC   LASTTYPE,0(7)
         BE    NEXTMVOL
         MVC   LASTTYPE,0(7)
         BAL   5,PRINTA
         CLI   MPSW,255
         BNE   *+20
         MVC   STORNAME,LNAME
         MVC   LNAME,SPACESA
         MVI   MPSW,0
         LR    9,7
         BAL   5,FINDTYPE
         LA    6,LVS
         B     SETMSER
NEXTMVOL MVI   6(6),C','
         LA    6,7(6)
         B     SETMSER
MVPRNT   BAL   5,PRINTA
         CLI   MPSW,255
         BE    NEXTFLD
         MVC   LNAME,STORNAME
         B     NEXTFLD
TESTEMPT CLI   INDSW,255
         BE    TESLEVEL
         MVC   LTYPE,SLT
         CLI   INDSW,0
         BNE   *+10
         MVC   LTYPE,INDPTR
         BAL   5,PRINTA
         B     TESLEVEL
ALIAS    BAL   5,MOVEN
           MVC      LTYPE,ALIA
           MVC      LVTYPE(8),12(3)
         BAL   5,PRINTA
         LA    3,20(3)
         B     NEXTFLD
CVOLPTR  BAL   5,MOVEN
         MVC   LTYPE(8),CVOTR
         MVC   LVS(6),16(3)
         SR    5,5
         IC    5,15(3)
         LA    1,DISKTAB-4
         SLL   5,2
         AR    1,5
         MVC   LVTYPE(4),0(1)
         BAL   5,PRINTA
         LA    3,22(3)
         B     NEXTFLD
OCVLPTR  BAL   5,MOVEN
         MVC   LTYPE(8),CVOTR
         MVC   LVS(6),12(3)
         MVC   LVTYPE(4),NA
         BAL   5,PRINTA
         LA    3,18(3)
         B     NEXTFLD
NA       DC    CL4'N/A'
VOLCBPTR CLI   0(3),C'G'
         BNE   NGDGM
         CLC   5(2,3),V0
         BNE   NGDGM
         TM    1(3),240
         BO    NGDGM
         XC    1(4,3),FFS
         MVC   LTYPE,SLT
         MVC   SLT,SPACESA
NGDGM    BAL   5,MOVEN
         STM   0,15,VCBREGS
         OI    MPSW,255
NEXTVCB  LA    2,VDISKAD-16
         BAL   5,CALCTTR
         XDAP  VCB,RI,SYSCAT,VKEY,264,,VDISKAD
         WAIT  ECB=VCB
         CLI   VCB,127
         BNE   READERR
         LH    10,VCOUNT
         LA    9,VSERS
NEXTLINE BAL   5,FINDTYPE
         MVC   LASTTYPE,0(9)
           LA       11,6
         LA    6,LVS
SETVMS   MVC   0(6,6),4(9)
           CLC      10(2,9),HONE
           BNH      NOSEQ
           LH       1,10(9)
           CVD      1,DW
           MVC      SEQ,PMASK
           MVC      6(4,6),BRACKETS
           ED       SEQ,DW+6
           MVC      7(2,6),SEQ+2
           LA       6,4(6)
NOSEQ      EQU      *
         BCT   10,*+8
         B     VMPRNT
         LA    9,12(9)
         CLI   0(9),0
         BNE   FULLINE
         BAL   5,PRINTA
         CLI   MPSW,255
         BNE   *+20
         MVC   STORNAME,LNAME
         MVC   LNAME,SPACESA
         MVI   MPSW,0
         MVC   8(3,3),10(9)
         B     NEXTVCB
FULLINE  BCT   11,NOTFULL
DIFFTYPE BAL   5,PRINTA
         CLI   MPSW,255
         BNE   *+20
         MVC   STORNAME,LNAME
         MVC   LNAME,SPACESA
         MVI   MPSW,0
         B     NEXTLINE
NOTFULL  CLC   LASTTYPE,0(9)
         BNE   DIFFTYPE
         MVI   6(6),C','
         LA    6,7(6)
         B     SETVMS
VMPRNT   BAL   5,PRINTA
         LM    0,15,VCBREGS
         LA    3,14(3)
         CLI   MPSW,255
         BE    NEXTFLD
         MVC   LNAME,STORNAME
         B     NEXTFLD
SYSCAT   DCB   DDNAME=SYSCAT,MACRF=(E),DEVD=DA,DSORG=DA,EXLST=XLIST
BSAMCAT  DCB   DDNAME=SYSCAT,LRECL=256,KEYLEN=8,BLKSIZE=256,           *
               MACRF=(R),DSORG=PS,RECFM=F,EODAD=EOFA
LIST     DCB   DDNAME=SYSCATP,DSORG=PS,MACRF=(PM),LRECL=133,           *
               RECFM=FBM
CAMLSTA  CAMLST  SEARCH,SYSCAANM,VOLNO,IN
SYSCAANM DC    CL44'SYSCTLG'
VOLNO    DC    C'111111'
NINCA    DC    X'402020202120'
MPSW     DC    X'00'
INDSW    DC    X'FF'
PCNTRA   DC    PL2'0'
PLIMITA  DC    P'55'
V0         DC       C'V0'
TIME     DC    F'0'
DATE     DC    F'0'
ZREGS    DC    3F'0'
CONVERT  DC    F'0'
SIXTEEN  DC    F'16'
ADISKAD  DC    A(DISKAD)
RAWTTR   DC    XL3'00'
         DC    X'00'
REGS     DC    13F'0'
         DS    0F
XLIST    DC    X'87'
         DC    AL3(JFCBA)
JFCBA    DC    CL176' '
THREEA   DC    H'3'
C        DC    H'0'
T        DC    H'0'
CC       DC    H'0'
HH       DC    H'0'
TT       DC    H'0'
ENDTTR   DC    XL3'00'
FFS      DC    8X'FF'
HONE       DC       X'0001'
SEQ        DC       XL4'00'
BRACKETS   DC       C'(  )'
CVOTR    DC    C'CVOL PTR'
ALIA       DC       C' ALIAS FOR'
INDPTR   DC    CL10'INDEX PTR'
ONEA     DC    P'1'
T2400    DC    CL6'2400'
T24002   DC    CL6'2400-2'
T24003   DC    CL6'2400-3'
T24004   DC    CL6'2400-4'
SLT      DC    CL10' '
QNAME    DC    CL8'SYSCTLG'
RNAME    DC    CL8'SYSCTLG'
SPACEUP  DC    X'0B'
NPA      DC    X'8B'
HDR      DC    X'19'
           DC       CL4' '
           DC       CL40'RESPOND TSG UTILITY'
         DC    C'LIST OF SYSCTLG ON '
HDRVOLNO DC    C'123456'
         DC    C' AT'
HDRTIME  DC    X'4020214B2020'
         DC    C' ON'
HDRDATE  DC    X'4020214B202020'
         DC    CL36' '
         DC    C'PAGE'
HDRPNO   DC    X'40202021'
TITLEA   DC    X'11'
         DC    CL24' '
         DC    CL29'NAME'
         DC    CL8'TYPE'
         DC    CL9'VOL TYPE'
           DC       CL62'VOLUME SERIALS (FILE SEQ SHOWN IN BRACKETS IF *
               GREATER THEN 1)'
SPACESA  DC    CL132' '
LINEA    DC    X'09'
         DC    CL4' '
LNAME    DC    CL44' '
         DC    CL2' '
LTYPE    DC    CL10' '
         DC    CL2' '
LVTYPE   DC    CL6' '
         DC    CL2' '
LVS      DC    CL62' '
LASTLINE DC    X'09'
         DC    CL4' '
         DC    C'NUMBER OF BLOCKS IN CATLG ='
NINC     DC    X'402020202120'
         DC    CL4' '
         DC    C'NUMBER OF EMPTY BLOCKS ='
NEMP     DC    X'402020202120'
         DC    CL4' '
         DC    C'(SPACE IN PARTLY USED BLOCKS ='
NPU      DC    X'402020202120'
         DC    C' EMPTY BLOCKS) '
         DC    CL8' '
STORNAME DC    CL44' '
LASTTYPE DC    CL4' '
PNO      DC    PL2'0'
PMASK    DC    X'40202021'
LTYPEGDG DC    C'GDG'
         DC    X'40202120404040'
ZEROTTR  DC    X'000000'
GSTRING  DC    X'00'
DISKTAB  DC    C'2311'
         DC    C'2301'
         DC    C'2302'
         DC    C'2302'
         DC    C'2321'
         DC    CL8' '
         DC    C'2314'
         DS    0H
VKEY     DC    XL8'00'
VCOUNT   DC    XL2'00'
VSERS    DC    XL252'00'
DW       DC    D'0'
VDISKAD  DC    D'0'
VCBREGS  DC    16F'0'
DISKAD   DC    10D'0'
NN       DC    D'0'
IN       DS    350C
         END
