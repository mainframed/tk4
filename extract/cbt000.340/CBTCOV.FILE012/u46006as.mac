         LCLC  &MCSECT,&PREF
&MCSECT  SETC  'SPZ24LKD'
&PREF    SETC  '&MCSECT'(1,5)
         TITLE '&MCSECT ... CLEAR CORE BEFORE LINK-EDIT'
*
*        THIS  MODULE GETS THE BIGGEST POSSIBLE AREA WHICH IT ZEROISES
*              RELEASES IT AND TRANSFERS TO THE LINKAGE EDITOR
*
         SPACE 3
$CORE    DSECT
         USING *,R3
$COREBIT DS    256X                    CLEARED BLOCK
$CORENXT DS    X                       NEXT BIT
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
&MCSECT  CSECT
LINK     EQU   *
LIVELINK SAVE  (14,12),,*
         ENTRY LIVELINK,LINK
         BALR  R12,0                   BASE MODULE
         USING *,R12
         GETMAIN   ,MF=(E,LKDCORE)     TRY GETTING AS MUCH AS POSSIBLE
         LTR   R15,R15                 DID WE GET ANY
         BNZ   LKD040                  NO
         LM    R3,R4,AQUIRED           R3 = START , R4 = QUANITITY
         LA    R5,255                  USED IN CLEARANCE
LKD010   SH    R4,H256                 STEP DOWN
         BP    LKD020                  OK NOT FINISHED YET
         AR    R5,R4                   RESET EXECUTE LENGTH
LKD020   EX    R5,LKDCLEAR             CLEAR 256 BYTES
         LA    R3,$CORENXT             STEP ON
         LTR   R4,R4                   TO END
         BP    LKD010                  NO
         FREEMAIN   ,MF=(E,LKDCORE)    NOW RELEASE IT
         LA    R15,LISTXTCL            TO XTCL LIST
         LM    R0,R12,20(R13)          RESTORE REGISTERS
         XCTL  ,SF=(E,(15))            OUT TO LINKAGE EDITOR
*
*        NO CORE AVAILABLE
*
LKD040   WTO   '&PREF.-1I CANNOT AQUIRE ANY CORE',ROUTCDE=(2,11)
         RETURN (14,12),RC=4
*
*        CONTROL DATA
*
LKDCLEAR XC    $COREBIT,$COREBIT       CLEAR CORE BLOCK 'EXECUTED'
LKDCORE  GETMAIN   VC,LA=SIZES,A=AQUIRED,MF=L
H256     DC    H'256'                  DECREMENTAL COUNTER
SIZES    DC    A(50*1024,16*1024000)   MIN AND MAX CORE SIZES
AQUIRED  DC    2A(0)                   FILLED BY GETMAIN 1ST = ADDRESS
*                                                        2ND = LENGTH
LISTXTCL XCTL  EP=IEWL,SF=L            TRANSFER LIST
         END
