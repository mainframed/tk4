**       ISSUE VOLUME SERIAL NUMBER MESSAGES TO THE OPERATOR TOGETHER
**       WITH A USER COMMENT.
**       THE VOLUME SERIAL NUMBER MAY BE GIVEN EXPLICITLY OR VIA A
**       DATA SET NAME.
**       CARD FORMAT -
*              MESSAGE,VOLSER,VOLSER,DSNAME,DSNAME,....
**       INPUT - SYSIN DATA SET
**       OUTPUT - WTO
ZR73     CSECT
         HSAVE
         OPEN  (SYSIN,)
READ     GET   SYSIN,INAR              READ
         MVI   STEPNAME,C' '
         MVC   STEPNAME+1(L'STEPNAME-1),STEPNAME
         SR    A,A                     ZERO WTO COUNT
         LA    B,INAR                  INIT SCAN START
         LA    C,STEPNAME              INIT O/P START
LOOP1    CLI   0(B),C','               END ?
         BE    END1
         MVC   0(1,C),0(B)
         LA    A,1(A)
         LA    B,1(B)
         LA    C,1(C)
         B     LOOP1
END1     LA    D,DSN
         LA    A,2(A)
         LA    B,1(B)
         LA    C,2(C)
NEXT     MVC   DSN,DSN-1
LOOP2    CLI   0(B),C','
         BE    LOCATE
         CLI   0(B),C' '
         BE    LOCATE
         MVC   0(1,D),0(B)
         LA    B,1(B)
         LA    D,1(D)
         B     LOOP2
LOCATE   LA    15,43
         LA    14,DSN+43
ZIP      CLI   0(14),C'.'              PERIOD ?
         BE    DSET                    MUST BE A DATA SET NAME
         BCTR  14,0                    CHOP
         BCT   15,ZIP                  ZIP ROUND
VOL      EQU   *
         MVI   LOOKAREA+1,X'01'        SET VOL COUNT=1
         MVC   VOLSERS+4(6),DSN
         B     ENTER
DSET     LOCATE LOOKUP
         LTR   15,15
         BZ    ENTER                   OK - GO WTO
         MVC   VOLSERS+4(6),=C'NOTCAT'
         MVI   LOOKAREA+1,X'01'
ENTER    LA    VOLREG,VOLSERS
         SR    LOOPLIM,LOOPLIM
         IC    LOOPLIM,LOOKAREA+1
LOOKMOVE MVC   0(6,C),4(VOLREG)
         LA    A,7(A)
         LA    C,7(C)
         LA    VOLREG,12(VOLREG)
         BCT   LOOPLIM,LOOKMOVE
FIN      CLI   0(B),C' '
         BE    WTO
         B     END1
WTO      LA    A,4(A)
         STH   A,WTOLEN
         LA    1,WTOLEN
         SVC   35
         B     READ
EOJ      CLOSE (SYSIN,)
         L     13,HSAVE+4
         RETURN (14,12),RC=0
VOLREG   EQU   3
OUTVOL   EQU   4
LOOPLIM  EQU   5
A        EQU   6
B        EQU   7
C        EQU   8
D        EQU   9
LOOKUP   CAMLST NAME,DSN,VOLSER,LOOKAREA
VOLSER   DC    C'333006'
INAR     DS    CL80
         DC    C' '
DSN      DS    CL44
WTOLEN   DC    F'0'
STEPNAME DS    CL132
LOOKAREA DS    0D
         DS    265C
         ORG   LOOKAREA+2
VOLSERS  DS    CL240
         ORG
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=EOJ
         END
