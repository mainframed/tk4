         TITLE 'HASPSEL - SELECT HASP MODIFICATIONS FOR ASSEMBLY'
SELECT   CSECT
*
         REQU
*
         HSAVE
*
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT),SYSPRINT,(OUTPUT),SYSIN,*
               (INPUT))
*
         PUT   SYSPRINT,HEADER
*
         SR    R7,R7                   ZERO TO COUNT MODS
         LA    R9,BLDLIST+4            POINT TO 1ST ENTRY IN BLDL LIST
READC    GET   SYSIN,CARD              READ A CONTROL CARD
         LA    R8,CARD                 START SCAN IN COLUMN 1
INSPECT  CLI   0(R8),C' '              IS IT A BLANK
         BE    READC                   YES...GET ANOTHER CARD
         MVC   0(4,R9),0(R8)           MOVE MOD NAME INTO BLDL LIST
         LR    R5,R9                   POINT TO NEWEST BLDL ENTRY
         LA    R6,1(R7)                GET NUMBER OF ENTRIES SO FAR
LOOP1    SH    R6,=H'1'                DECREMENT COUNTER
         BZ    SORTEND                 JUMP OUT IF NO MORE ENTRIES
         SH    R5,=H'14'               BACK UP ONE ENTRY
         CLC   0(4,R5),14(R5)          COMPARE ENTRIES
         BNH   SORTEND                 FINISHED SORT - JUMP OUT
         XC    0(4,R5),14(R5)          SWITCH
         XC    14(4,R5),0(R5)              THE TWO
         XC    0(4,R5),14(R5)                 ENTRIES
         B     LOOP1                   LOOP BACK
SORTEND  LA    R7,1(R7)                INCREMENT ENTRY COUNTER
         LA    R8,5(R8)                POINT TO NEXT FIELD ON CARD
         C     R8,=A(CARD+72)          PAST COLUMN 71
         BNL   CCARDERR                YES...ILLEGAL CARD
         LA    R9,14(R9)               POINT TO NEXT SLOT IN BLDL LIST
         B     INSPECT                 GO CHECK FIELD
*
EOFSYSIN LTR   R7,R7                   ARE THERE NO MODS TO SELECT?
         BZ    NOMODS                  NO...ERROR
         STH   R7,BLDLIST              YES...STORE NUMBER IN BLDL LIST
*
         BLDL  SYSUT1,BLDLIST
*
         LTR   R15,R15                 BLDL OKAY?
         BNZ   BLDLERR                 NO...ERROR
         LA    R2,SYSUT1
         USING IHADCB,R2
         LH    R0,DCBBLKSI
         GETMAIN R,LV=(0)
         LR    R4,R1
         LA    R9,BLDLIST+4            POINT TO 1ST ENTRY IN BLDL LIST
*
FIND     FIND  SYSUT1,8(9),C
*
READBLK  READ  RDECB,SF,SYSUT1,(4),'S'
         CHECK RDECB
         L     R5,DCBIOBA              POINT TO IOB PREFIX
         LH    R3,DCBBLKSI             GET SYSUT1 BLOCKSIZE
         SH    R3,22(R5)               SUB RESIDUAL COUNT IN CSW
         LA    R2,SYSUT2                         TO GET NUM BYTES READ
         STH   R3,DCBBLKSI             STORE THIS FIGURE IN SYSUT2 DCB
         LA    R2,SYSUT1               RESET IHADCB ADDRESSABILITY
         WRITE WDECB,SF,SYSUT2,(4),'S'
         CHECK WDECB
         B     READBLK                 GET ANOTHER BLOCK
*
EOFUT1   MVC   MESSNAME(4),0(R9)       MOVE MOD NAME TO MESSAGE
         PUT   SYSPRINT,MESSAGE
         MVI   MESSAGE,C' '            RESET ASA CNTRL
         LA    R9,14(R9)               POINT TO NEXT ENTRY IN BLDL LIST
         BCT   R7,FIND                 LOOP BACK IF MORE TO GO
FINISHED SR    R6,R6                   ZERO RETURN CODE
         DROP  R2
         MVC   SYSUT2+DCBBLKSI-IHADCB(2),SYSUT1+DCBBLKSI-IHADCB
*
RETURN   CVD   R6,DWRD                 PACK THE RETURN CODE
         ED    RCODE(4),DWRD+6         PLACE RETURN CODE IN MESSAGE
         PUT   SYSPRINT,RMSG
         CLOSE (SYSUT1,,SYSUT2,,SYSPRINT,,SYSIN)
         LR   R15,R6                   GET RETURN CODE
         L     R13,HSAVE+4
         RETURN (14,12),RC=(15)
*
CCARDERR PUT   SYSPRINT,CARDMSG
         LA    R6,8
         B     RETURN
*
NOMODS   PUT   SYSPRINT,NOMSG
         LA    R6,8
         B     RETURN
*
BLDLERR  CH    R15,=H'8'               WERE ANY MODS MISSING?
         BNE   MISSING                 YES...GO AND LIST THEM
         PUT SYSPRINT,IOERRMSG         NO...I/O ERROR IN DIRECTORY
         LA    R6,20
         B     RETURN
MISSING  PUT   SYSPRINT,MISSMSG1
         LA    R9,BLDLIST+4            POINT TO 1ST ENTRY IN BLDL LIST
MISS1    CLI   10(R9),X'00'            WAS THIS MOD MISSING?
         BNE   MISS2                   NO
         MVC   MEMNAME(4),0(R9)        YES.MOVE NAME TO MESSAGE
         PUT   SYSPRINT,MISSMSG2
MISS2    LA    R9,14(R9)               POINT TO NEXT ENTRY IN BLDL LIST
         BCT   R7,MISS1
         LA    R6,12
         B     RETURN
         EJECT
CARD     DC    CL80' '
*
HEADER   DC    CL50'1',CL32'HASP MODIFICATION SELECT UTILITY',CL51' '
*
MESSAGE  DC    CL133'0 MEMBER XXXX HAS BEEN SUCCESSFULLY COPIED'
MESSNAME EQU   MESSAGE+9
*
RMSG     DC    CL133'0 END OF JOB - RETURN CODE XXXX'
         ORG   RMSG+27
RCODE    DC    X'40202120'
         ORG
*
CARDMSG  DC    CL133'0 CONTROL CARD ERROR'
*
NOMSG    DC    CL133'0 NO MOD SELECTION REQUESTED'
*
IOERRMSG DC    CL133'0 I/O ERROR WHILE READING DIRECTORY OF SYSUT1'
*
MISSMSG1 DC    CL133'0 THE FOLLOWING MEMBERS WERE NOT FOUND'
MISSMSG2 DC    CL133'  MEMBER XXXX COULD NOT BE FOUND'
MEMNAME  EQU   MISSMSG2+9
*
DWRD     DC    D'0'
         SPACE 2
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=R,EODAD=EOFUT1
         SPACE 2
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=W,RECFM=FB,LRECL=80
         SPACE 2
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FA,LRECL=133,   *
               BLKSIZE=133
         SPACE 2
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=EOFSYSIN
         SPACE 2
         LTORG
         SPACE 2
BLDLIST  DC    0H'0'
         DC    H'0',H'14'
         DC    2800C' '
         SPACE 2
         DCBD  DSORG=PO,DEVD=DA
         END
