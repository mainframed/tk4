         TITLE 'SPOOL PAPER TAPE TO DISK - PRINT THE RECORD'
         PRINT NOGEN
ZSPOOL4  INIT
         L     R7,=A(PRINTER2) PICK UP ADDRESS OF DCB FOR GOOD CARDS
         L     R8,=A(COMMON)
         USING AREA,R8
         USING IHADCB,R9
         EXTRN COMMON,TAPE,PRINTER2
         EJECT
         MVI   PRNTCNTL,C' '
         TM    SWITCH1,DISPOPEN IS THIS THE FIRST TIME FOR THIS CARD
         BO    A3 NO
         OI    SWITCH1,DISPOPEN
         LA    R2,TITLE PICK UP ADDRESS OF OUTPUT AREA
         LA    R1,JOBNAME PICK UP ADDRESS OF JOB NAME
A2       CLC   0(2,R1),=C'.T' IS THIS THE END OF THE JOB NAME
         BE    A3 YES
         MVC   0(1,R2),0(R1) MOVE ONE CHARACTER OF JOB NAME TO OUTPUT
         LA    R1,1(R1) INCREMENT TO NEXT POSITIONS
         LA    R2,1(R2)
         B     A2
A3       EQU   *
         CP    LINECOUN,=P'45'
         BNH   A1
         BAL   R9,OFLO
A1       LTR   R6,R6
         BNZ   A9
         PUT   (7),LINE5
         B     END
         SPACE 1
A9       PUT   (7),LINE3 PUT FIRST LINE OF HEADING TO RECORDS
         AP    LINECOUN,=P'2'
         LR    R10,R6 PICK UP ADDRESS OF INPUT AREA
         L     R9,=A(TAPE)
         LH    R3,DCBLRECL FIND LENGTH OF RECORD
         LA    R4,99 SET LINE MAXIMUM (100 - 1)
         LTR   R3,R3 IS RECORD LENGTH ZERO
         BZ    A5 YES
         BCTR  R3,0 DECREASE LENGTH BY 1
A6       CR    R3,R4 IS THERE A FULL LINE OF INFORMATION
         BH    A7 YES
         MVI   PRNTAREA+1,C'*'
         MVC   PRNTAREA+2(99),PRNTAREA+1
         LR    R4,R3
A7       EX    R4,A8 MOVE DETAILS TO OUTPUT
         LA    R10,100(R10) INCREMENT TO NEXT POSITION
         LA    R5,PRNTAREA
         PUT   (7),(5) PRINT A LINE
         AP    LINECOUN,=P'1'
         SH    R3,=H'100' DECREASE AMOUNT REMAINING
         BC    10,A6 GO AND PROCESS MORE
END      EXIT
A5       PUT   (7),LINE4
         B     END
A8       MVC   PRNTAREA+1(1),0(R10)
         EJECT
OFLO     AP    PAGECOUN,=P'1'
         MVC   PAGENO(4),=X'40202020'
         ED    PAGENO(4),PAGECOUN
         PUT   (R7),LINE1
         PUT   (R7),LINE2
         ZAP   LINECOUN,=P'5'
         BR    R9
         EJECT
LINE1    DC    C'1'
         DC    33C' '
         DC    C'CHECK LISTING FOR JOB - '
TITLE    DC    43C' '
         DC    CL20' '
         DC    C'PAGE'
PAGENO   DC    CL8' '
         SPACE 2
LINE2    DC    C'-'
         DC    C'1         11        21        31        41        51 '
         DC    C'       61        71        81        91     100'
         DC    CL32' '
         SPACE 2
LINE3    DC    C'0'
         DC    C'V---------V---------V---------V---------V---------V--'
         DC    C'-------V---------V---------V---------V--------V'
         DC    CL32' '
         SPACE 2
LINE4    DC    C' '
         DC    132C' '
         ORG   LINE4+1
         DC    C' *** ZERO LENGTH RECORD ***'
         ORG
         SPACE 2
LINE5    DC    C'-'
         DC    132C' '
         ORG   LINE5+1
         DC    C' E N D  O F  P A P E R  T A P E'
         ORG
         EJECT
DISPOPEN EQU   X'40'
         LTORG
PAGECOUN DC    PL2'0'
LINECOUN DC    PL2'60'
         EJECT
AREA     DSECT
PRNTAREA EQU   *
PRNTCNTL DS    CL1
         DS    CL132
SWITCH1  DS    XL1
SWITCH2  DS    XL1
JOBNAME  DS    CL44
         EJECT
         DCBD  DSORG=QS,DEVD=PT
         END
