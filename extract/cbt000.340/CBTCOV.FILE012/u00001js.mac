TSGMERGE CSECT
*
*  TSGMERGE IS A VERSION OF TSGLSORT (Q.V.) FOR USE IN A MERGE
*  APPLICATION.  DIFFERENCES ARE.......
*
*        1  BECAUSE A MERGE HAS NO SORTIN PHASE THE E15 EXIT IS NOT
*           REQUIRED AND RETURN CODES 4,8, AND 16 ARE NOT RELEVANT.
*        2  RET CODE 8 IS USED IN TSGMERGE TO MEAN THAT EOF WAS FOUND
*           WITH THE FIRST RECORD PRESENTED FOR OUTPUT.
*        3  RET CODE 16 FROM TSGMERGE MEANS THAT THE NUMBER OF OUTPUT
*           RECORDS WAS LESS THAN THE VALUE OF 'INCNT', WHICH IS ZERO
*           BY DEFAULT BUT MAY BE SET TO ANY VALUE BY A PARM ON THE
*           EXEC CARD, E.G...
*              //MERGE EXEC PGM=TSGMERGE,PARM=10000
*        4  IF THE USER IS ALREADY USING TSGLSORT HE MUST ARRANGE FOR
*           THE E35 ROUTINE FOR TSGMERGE TO BE IN A DIFFERENT LIBRARY
*           FROM THE E35 FOR TSGLSORT.  E.G. FOR TSGMERGE...
*              //STEPLIB  DD  DSN=TSG.MLIB,DISP=SHR
*                MODS  E35=(E35,160,STEPLIB,N)
*
*  ORIGINAL COMMENTS FOR TSGLSORT NOW FOLLOW....
*
*   THIS PROGRAM ALLOWS THE USER TO EXECUTE THE STANDARD IBM SORT
*   PROGRAM WITH TWO ADDED FEATURES....
*        1     A RETURN CODE IS GIVEN TO INDICATE THAT THE SORT WAS, OR
*              WAS NOT, SUCCESSFUL.  THE RETURN CODES ARE...
*
*                 0  SORT SUCCESSFULLY COMPLETED
*                 4  SORT FAILED BEFORE READING SORTIN
*                 8  SORTIN HAS NO RECORDS
*                12  SORT FAILED BEFORE WRITING SORTOUT
*                16  SORTOUT COUNT DOES NOT EQUAL SORTIN COUNT
*                20  LESS THAN 16K AVAILABLE FOR SORT
*
*        2     A 'CORE= ' PARAMETER IS AUTOMATICALLY GENERATED
*              ACCORDING TO THE CORE AVAILABLE.  THE VALUE USED IS THE
*              SIZE OF THE LARGEST FQE (BEFORE LINKING TO SORT) LESS AN
*              ARBITRARY 6K TO ALLOW FOR DATA MANAGEMENT FUNCTIONS.
*              THE RESULT MUST NOT BE LESS THAN 16K.
*
*   THE PROGRAM USES AN E15 AND E35 EXIT ROUTINE (SOURCE DECKS FOLLOW)
*   AND THE USER MUST SUPPLY A 'MODS' CARD TO TELL THE SORT TO USE THEM
*
*   BECAUSE THE EXIT ROUTINES MUST BE NAMED E15 AND E35 IF THE LINKEDIT
*   FUNCTION IS TO BE AVOIDED YOU ARE ADVISED TO PUT THIS PROGRAM AND
*   ITS EXIT ROUTINES INTO A SEPARATE PRIVATE LIBRARY TO PREVENT
*   CONFUSION WITH ANY OTHER ROUTINES HAVING THE SAME NAMES.
*
*   THE USER IS NOT ALLOWED TO PROVIDE A PARM FIELD OF HIS OWN ON THE
*   EXEC CARD, AND CANNOT USE THE 'ALTERNATIVE DDNAME' FEATURE
*   (NORMALLY AVAILABLE WHEN LINKING TO SORT).  HE CAN USE ANY EXIT
*   ROUTINES OF HIS OWN, AND CAN ADD CODE TO THE E15 AND E35 PROVIDED.
*
*   THE JCL REQUIRED IS AS FOLLOWS (EXAMPLE)...
*
*        //SORT EXEC PGM=TSGLSORT      *** SEE NOTE BELOW ***
*        //STEPLIB DD DSN=TSG.SORTLIB,DISP=SHR
*        //SYSOUT DD SYSOUT=...
*        //SORTLIB DD DSN=SYS1.SORTLIB,DISP=SHR
*        ........................................
*        //SORTIN,SORTOUT,SORTWK CARDS AS NORMAL
*        ........................................
*        //SYSIN  DD  *
*              SORT  ...NORMAL CONTROL CARD ...
*              RECORD  ...OPTIONAL - NORMAL IF USED...
*              MODS E15=(E15,160,STEPLIB,N),E35=(E35,160,STEPLIB,N)
*                  ...PLUS ADDITIONAL EXITS IF REQUIRED...
*              END
*        /*
*
*   *** NOTE *** IF YOU CHANGE THE NAME OF THIS PROGRAM YOU MUST ALSO
*   CHANGE THE LITERAL (=CL8'TSGLSORT') IN BOTH EXIT ROUTINES.
*
*   YOU MAY LINK TO TSGLSORT - NO PARAMETERS ARE REQUIRED.
*
*   YOU MAY OMIT THE MODS CARD - THIS WILL GIVE YOU THE AUTOMATIC
*   CALCULATION OF 'CORE= ', AND WILL EXECUTE SORT NORMALLY, BUT WILL
*   ALWAYS GIVE A RETURN CODE OF 4 (TO BE IGNORED).
*
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+88
         DC    18F'0'
*
*   THE FOLLOWING INDICATORS MUST REMAIN AT THIS DISPLACEMENT FROM THE
*   START OF THE PROGRAM TO ENABLE THE EXIT ROUTINES TO FIND THEM.
*
INDIC    DC    X'00'
OUTDIC   DC    X'00'
         DC    H'0'
INCNT    DC    F'0'
OUTCNT   DC    F'0'
*
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
*
         L     1,0(1)
         LH    2,0(1)
         LTR   2,2
         BZ    NOPARM
         BCTR  2,0
         EX    2,PACK
         CVB   2,DW
         ST    2,INCNT
NOPARM   EQU   *
*
*   NOW FIND THE LARGEST FQE AND GENERATE 'CORE=......'.
*   THIS IS PRESENTED TO SORT AS AN 'EXEC' TYPE PARAM RATHER THAN A
*   'LINK/ATTACH' PARAMETER LIST.  THE REASON FOR THIS IS TO ALLOW THE
*   USER THE FULL RANGE OF EXIT ROUTINES.
*
         L     1,16
         L     3,0(1)
         L     3,4(3)              REG 3 -> TCB
         CLI   116(1),32
         BL    MVT
         SPACE
*  DETERMINE AVAILABLE CORE IN MFT SYSTEMS FROM LARGEST FQE.
         SPACE
         L     2,24(3)             REG 2 -> TCBMSS
         L     2,0(2)
         MVC   LFQE,4(2)
         SR    3,3
FQELOOP  L     5,4(2)
         C     5,LFQE
         BNH   *+10
         MVC   LFQE,4(2)
         L     2,0(2)
         CR    3,2
         BNE   FQELOOP
         L     2,LFQE
CORESUM  EQU   *
         SH    2,=H'6144'
         CH    2,=H'16384'
         BL    NOCORE
         CVD   2,DW
         UNPK  CORESIZE,DW+4(4)
         MVI   CORESIZE,C'='
         OI    CORESIZE+6,240
*
*   NOW LINK TO SORT
*
         LINK  EP=IERRCO00,PARAM=(PARM),VL=1
*
*   ON RETURN - TEST INDICATORS AND SET UP RETURN CODE.
*
         LTR   15,15
         BNZ   GOBACK
         SR    1,1
         LA    15,12
         CLI   OUTDIC,0
         BE    GOBACK
         LA    15,8
         C     1,OUTCNT
         BE    GOBACK
         LA    15,16
         CLC   INCNT,OUTCNT
         BH    GOBACK
         SR    15,15
GOBACK   L     13,4(13)
         L     14,12(13)
*   LEAVE RETURN CODE IN REG 15
         LM    0,12,20(13)
         MVI   12(13),255
         BR    14
         SPACE
*  DETERMINE AVAILABLE CORE IN MVT SYSTEM FROM SUM OF FBQE SIZES.
         SPACE
MVT      L     1,152(3)            REG 1 -> DUMMY PQE-8
         L     2,8(1)              REG 2 -> FIRST PQE
         L     4,0(2)              REG 4 -> FIRST FBQE
         CR    4,2
         BE    NOCORE
         L     10,8(4)             SIZE FROM FIRST FBQE
NEXTFBQE L     4,0(4)
         CR    4,2
         BE    NEXTPQE
SUBSENT  A     10,8(4)
         B     NEXTFBQE
NEXTPQE  L     2,8(2)
         LTR   2,2
         BZ    ENDPQES
         L     4,0(2)
         B     SUBSENT
ENDPQES  LR    2,10
         B     CORESUM
         SPACE
DW       DC    D'0'
LFQE     DC    F'0'
PACK     PACK  DW,2(1,1)
         CNOP  2,4
PARM     DC    H'11'
         DC    C'CORE'
CORESIZE DC    C'=123456'
*
NOCORE   LA    15,20
         B     GOBACK
         END
