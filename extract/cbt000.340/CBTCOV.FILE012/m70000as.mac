         MACRO
&FCBNAME FCBDEF &DEFAULT=NO,&LINES=66,&REPEAT=1,&CH1=,&CH2=,&CH3=,     X
               &CH4=,&CH5=,&CH6=,&CH7=,&CH8=,&CH9=,&CH10=,&CH11=,      X
               &CH12=,&LPI=6
         GBLA  &FBYTE,&LBYTE,&MATRIX(255)
         LCLA  &CTR,&SETLPI
&FCBNAME START
         AIF   ('&DEFAULT' EQ 'NO').DEFNO
         AIF   ('&DEFAULT' EQ 'YES').DEFYES
         MNOTE 4,'DEFAULT MUST BE YES OR NO, NO ASSUMED'
.DEFNO   DC    X'00'
         AGO   .BUFLEN
.DEFYES  DC    X'80'
.BUFLEN  AIF   ((&LINES*&REPEAT) GT 255).TOOLONG
         DC    AL1(&LINES*&REPEAT)
         AGO   .DEFFCB
.TOOLONG MNOTE 16,'LENGTH GREATER THAN 255, ASSEMBLY IMPOSSIBLE'
         MEXIT
.DEFFCB  ANOP
FCB      DC    XL(&LINES*&REPEAT)'00'
&CTR     SETA  &REPEAT
.SETBUFS ANOP
FORM&CTR     EQU   FCB+((&CTR-1)*&LINES)
&CTR     SETA  &CTR-1
         AIF   (&CTR EQ 0).ALLDONE
         AGO   .SETBUFS
.ALLDONE ANOP
CH1      SETCHAN  1,&CH1,&LINES,&REPEAT
CH2      SETCHAN  2,&CH2,&LINES,&REPEAT
CH3      SETCHAN  3,&CH3,&LINES,&REPEAT
CH4      SETCHAN  4,&CH4,&LINES,&REPEAT
CH5      SETCHAN  5,&CH5,&LINES,&REPEAT
CH6      SETCHAN  6,&CH6,&LINES,&REPEAT
CH7      SETCHAN  7,&CH7,&LINES,&REPEAT
CH8      SETCHAN  8,&CH8,&LINES,&REPEAT
CH9      SETCHAN  9,&CH9,&LINES,&REPEAT
CH10     SETCHAN 10,&CH10,&LINES,&REPEAT
CH11     SETCHAN 11,&CH11,&LINES,&REPEAT
CH12     SETCHAN 12,&CH12,&LINES,&REPEAT
* ***** NOW SET UP FIRST AND LAST BYTES *******************************
&SETLPI  SETA  0
         AIF   (&LPI EQ 6).SETF
         AIF   (&LPI EQ 8).SET8
         MNOTE 4,'LPI MUST BE 6 OR 8, 6 ASSUMED'
         AGO   .SETF
.SET8    ANOP
&SETLPI  SETA  16
.SETF    ORG   FCB
         DC    AL1(&SETLPI+&FBYTE)
         ORG   FCB+(&LINES*&REPEAT)-1
         DC    AL1(16+&LBYTE)
         END
         MEND
