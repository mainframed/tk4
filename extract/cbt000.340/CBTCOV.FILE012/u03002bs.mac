*          DATA SET SOF155YYYY AT LEVEL 006 AS OF 23/01/85
         TITLE '* * * * * *    XXX FUNCTION FOR OS AND VS  * * * * *'
***********************************************************************
**********               XXX LOAD MODULE LOADED             ***********
**********          INTO PARTITION FOR XXX FUNCTION         ***********
***********************************************************************
**********   AMENDED -                                      ***********
**********           JAN. 1985 BY JOHN MCGHEE               ***********
**********           FROM PANVALET LEVEL 2.                 ***********
**********           VS1 REFERENCES TO PARTITION NUMBERS    ***********
**********           REMOVED.                               ***********
**********           NOTE ABOUT DEFAULT TAPE MODESET        ***********
**********           CHANGED TO SAY D0.                     ***********
***********************************************************************
         SPACE 2
         ISEQ  75,80
DITTOXXX START
         USING *,RC,R5,RA
         LR    R5,RC
         AH    R5,BUMP
         ST    RA,RETURN           SAVE RETURN ADDRESS
         LR    RA,R5
         AH    RA,BUMP
         USING CCX,R7
         USING PGX,RB
         B     AROUND              BR BY COPYRIGHT
COPYRT   DC    C'5798-ARD COPYRIGHT IBM CORPORATION 1973'
         DC    C'05-01-74 VERS.1 MOD.3'
AROUND   EQU   *
         SPACE 2
***********************************************************************
**********          PAGE 1 -  LIST OF FUNCTIONS WITH        ***********
**********            DESCRIPTIONS WITH * FRAMING           ***********
***********************************************************************
         SPACE 1
         MVI   BL,C' '
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVI   CC,W1               CARRIAGE CTL = WRITE & SPACE
         MVI   PG,C'*'
         MVC   PG+1(W),PG          *'S ACROSS PAGE
         MVC   PG+30(17),=C' O S / D I T T O '
         BALR  R8,R9               PRINT HEADING
         MVI   PG+1,C' '
         MVC   PG+2(W-2),PG+1      BLANK HEADING
         BALR  R8,R9               PRINT LINE 4
         LA    R2,TBL1             POINTER TO PRINT LITERALS
         LA    R3,NTBL1            # OF TABLE ENTRIES
         MVI   CC,W1               SET UP PRINT & SINGLE SPACE
MOVTBL1  MVC   OPCODE,0(R2)        FUNCTION CODE TO PRNT AREA
         MVC   COMMENT,3(R2)       DESCN. TO PRNT AREA
         BALR  R8,R9               PRINT LINE
         LA    R2,52(R2)           BUMP TO NEXT LITERAL
         BCT   R3,MOVTBL1          LOOP & PRNT AGAIN TO COMMENTS
         MVC   PG+2(W-2),PG+1      CLEAR EXCEPT *'S
         BALR  R8,R9               PRINT SPACE AFTER XXX
         MVC   OPCODE(66),COMNT1
         BALR  R8,R9               PRINT COMNT1
         MVC   OPCODE(66),COMNT1AA
         BALR  R8,R9               PRINT COMNT1AA
         MVC   OPCODE(66),COMNT1A
         BALR  R8,R9               PRINT COMNT1A
         MVC   OPCODE(66),COMNT2
         BALR  R8,R9               PRINT COMNT2
         MVC   OPCODE(66),COMNT2A
         BALR  R8,R9                   'FOR SPLIT CYL . . .
         MVC   OPCODE(66),COMNT3
         BALR  R8,R9               PRINT 'PUNCHED OUTPUT MAY BE ETC.
         MVC   OPCODE(66),COMNT4
         BALR  R8,R9               PRINT 'DCB=( . . .
         MVC   PG+2(W-2),PG+1
         BALR  R8,R9               PRINT SPACE LINE
         MVC   COMMENT(41),WHO     MOVE L.S.B... LINE
         BALR  R8,R9               PRINT IT
         MVC   PG+1(W),PG          PROPAGATE *'S
         MVI   CC,WC1              SET TO PRNT & SKIP TO NEW PG.
         BALR  R8,R9               PRINT LAST LINE PG1
         EJECT
***********************************************************************
**********        PAGE 2 - 'FUNCTION CONTROL CARD FORMAT '  ***********
***********************************************************************
         SPACE 1
         MVC   PG,PG-1             CLEAR PRINT AREA
         MVC   PG+0(19),=C'* * * * * * * * * *'
         MVC   PG+62(19),PG        BUILD HEADING
         MVC   PG+27(28),CNTRLHD
         MVI   CC,W3               SET TO PRINT & TRIPLE SPACE
         BALR  R8,R9               PRINT 'FUNCTION CARD...' HDG.
         MVC   PG,PG-1             CLEAR PRINT AREA
         MVC   PG+2(6),=C'CC 1-7'     COLUMN HEADING
         MVC   PG+13(8),=C'FUNCTION'  COLUMN HEADING
         MVC   PAR(10),=C'PARAMETERS'   'PARAMETERS' TO HEADING
         MVI   CC,W2               SET TO PRINT & DOUBLE SPACE
         BALR  R8,R9               PRINT COLUMN HEADINGS
         MVC   PG+1,PG             CLEAR PRINT AREA
         MVC   PG+2(7),=C'$$DITTO'
         MVI   CC,W1               SET TO PRINT & SINGLE SPACE
         LA    R2,TBL1             POINTER TO FUNCTION CODE LITERALS
         LA    R3,NTBL3            NUMBER OF TBL4 ENTRIES
         LA    R4,TBL4             POINTER TO PARAMETER SWITCHES
LOOP     LA    R8,PARAM-4          POINTER TO VECTOR TABLE
         SR    R0,R0               CLEAR REG 0
         LA    R1,PG+25            ADDRESS OF PARAMETER PRINT AREA
         MVC   PG+15(70),PG+14     CLEAR PRINT AREA EXCEPT $$DITTO
         MVC   PG+15(3),0(R2)      FUNCTION CODE TO PRINT AREA
         IC    R0,0(R4)            INDEX VALUE FROM TBL4
         LTR   R0,R0               IS IT ZERO?
         BZ    END                 YES, GO PRINT FUNCTION CODE ONLY
GO       SLL   R0,2                NO, DOUBLE INDEX VALUE
         AR    R8,R0               ADD TO VECTOR TABLE FOR ENTRY
         BALR  R0,R8               BRANCH TO VECTOR TABLE (PARAM+)
         SR    R0,R0               CLEAR REG 0
         LA    R8,PARAM-4          RESET POINTER TO VECTOR TABLE
         LA    R4,1(R4)            BUMP TO NEXT INDEX VALUE
         IC    R0,0(R4)            INDEX INTO REG 0
         LTR   R0,R0               IS IT ZERO?
         BNZ   GO                  NO, LOOP & ADD NEXT PARAMETER
         BALR  R8,R9               YES, GO PRINT LINE
END      LA    R4,1(R4)            BUMP TO INDEX FOR NEXT FUNCTION
         LA    R2,52(R2)           BUMP TO NEXT FUNCTION CODE
         MVC   PG+11(70),PG+10     CLEAR PRINT AREA
         BCT   R3,LOOP             LOOP UNTIL ALL FUNCTIONS LISTED
         MVI   CC,W2               SET TO PRINT & DOUBLE SPACE
         MVC   PG+1(20),PG         CLEAR $$DITTO
         BALR  R8,R9               DOUBLE SPACE
         MVC   TRK+1(9),=C'PARAMETERS'
         MVC   PAR+8(11),=C'DESCRIPTION'
         MVI   CC,W2               SET TO PRINT & DOUBLE SPACE
         BALR  R8,R9               PRINT LOWER HALF PAGE HDGS.
         MVI   CC,W1               SET TO PRINT & SINGLE SPACE
         LA    R2,M2               POINTER TO PARAMETER TABLE
         LA    R3,DESCN            POINTER TO DESCRIPTION TABLE
         LA    R4,9                NUMBER OF PARAMETERS
PARMLP   MVC   TRK(14),0(R2)       PARAMETER TO PRINT AREA
         LA    R2,15(R2)           BUMP TO NEXT PARAMETER
         MVC   PAR(33),0(R3)       DESCN. TO PRINT AREA
         LA    R3,33(R3)           BUMP TO NEXT DESCN.
         BALR  R8,R9               PRINT PARAM & DESCN.
         MVC   PG+1,PG             CLEAR PRINT AREA
         BCT   R4,PARMLP           LOOP UNTIL DONE
         MVI   CC,W2
         BALR  R8,R9               DOUBLE SPACE
         MVI   CC,W1               SET TO PRINT & SINGLE SPACE
         MVC   TRK(78),PARM1       'PARM NOTE' TO PRINT AREA
         BALR  R8,R9               PRINT 1ST LINE PARM NOTE
         MVC   TRK(78),DDNOTE      SUBMIT DD NOTE
         BALR  R8,R9               PRINT DD NOTE
         MVC   TRK(78),NOTE3       FILE PROTECT NOTE
         BALR  R8,R9               PRINT FILE PROTECT NOTE
         MVC   TRK(78),NOTE4       TAPE MARK NOTE
         BALR  R8,R9               PRINT TAPEMARK NOTE
         MVC   TRK(78),NOTE4A      SET UP DCB NOTE
         BALR  R8,R9               PRINT IT
         MVC   TRK(78),CCCHH1      'CCCHH IS ...
         BALR  R8,R9               PRINT IT
         MVC   TRK(78),CCCHH2      'E.G. BEGIN= . . .
         BALR  R8,R9               PRINT IT
         MVC   TRK(78),CCCHH3      ABSOL. NOTE
         BALR  R8,R9               PRINT IT HERE AS NO ROOM ON CONSOL.
         MVC   TRK(78),NBLKSNOT    'NBLKS NOTE' TO PRINT AREA-LINE 2
         MVI   CC,WC1              SET TO PRINT & SKIP TO NEW PAGE
         BALR  R8,R9               PRINT & SKIP TO NEW PAGE
         EJECT
***********************************************************************
**********        PAGE 3 - 'CONSOLE PROCEDURES'             ***********
***********************************************************************
         SPACE 1
         MVI   PG,C' '
         MVC   PG+1,PG             CLEAR PRINT AREA
         MVI   CC,W3               SET TO PRINT & TRIPLE SPACE
         MVC   BL+7(19),=C'* * * * * * * * * *'
         MVC   BL+104(19),BL+7
         MVC   BL+51(17),=C'OS/DITTO CONSOLE '
         MVC   BL+68(10),PROCED
         BALR  R8,R9               PRINT IT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVC   BL+7(116),PROC1
         MVI   CC,W1               SPACE 1 AFTER PRINT
         BALR  R8,R9               PRINT IT
         MVC   BL+7(116),PROC2
         BALR  R8,R9               PRINT IT
         MVC   BL+7(116),PROC3
         BALR  R8,R9               PRINT IT
         MVC   BL+7(116),PROC4
         BALR  R8,R9               PRINT IT
         MVC   BL+7(116),PROC5
         MVI   CC,W3               SPACE 3 AFTER PRINT
         BALR  R8,R9               PRINT IT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVC   BL+7(9),PROCED
         MVC   PG+5(18),SYMBOLPA
         MVC   PG+58(12),FUNCMSG
         MVI   CC,W1               SPACE 1 AFTER PRINT
         BALR  R8,R9               PRINT IT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVC   BL+7(4),NAME
         MVC   PG+5(14),DEFVALU
         MVC   PG+58(10),GDELINE
         MVI   CC,W2               SPACE 2 AFTER PRINT
         BALR  R8,R9               PRINT IT
         LA    R2,DITPROC
         LA    R3,DEFVAL
         LA    R4,GDELIN
         ST    R6,SAVR6            STORE R6 BEFORE BCT LOOP
         LA    R6,12               LOOP 12 TIMES
         MVC   BL+1(131),BL
         MVI   CC,W1
         MVC   BL+8(5),=C'DITTO'
MOVLOP1  EQU   *
         MVC   BL+13(3),0(R2)
         MVC   PG+6(30),0(R3)
         MVC   PG+59(33),0(R4)
         LA    R2,3(R2)
         LA    R3,30(R3)
         LA    R4,33(R4)
         ST    R6,SAVR6XXX         STORE R6 BEFORE BALR TO PRINT
         L     R6,SAVR6            RESTORE R6 BEFORE BALR TO PRINT
         BALR  R8,R9               PRINT IT
         L     R6,SAVR6XXX         RESTORE R6 AFTER  BALR TO PRINT
         BCT   R6,MOVLOP1
         L     R6,SAVR6            RESTORE R6 BEFORE BALR TO PRINT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVI   CC,W3               SPACE 3 AFTER PRINT
         BALR  R8,R9               PRINT IT
         MVC   PG+5(11),=C'UNIT RECORD'
         MVC   PG+17(19),SYMBOLPA
         MVC   PG+58(13),=C'TAPE AND DISK'
         MVC   PG+72(19),SYMBOLPA
         MVI   CC,W2               SPACE 2 AFTER PRINT
         BALR  R8,R9               PRINT IT
         LA    R2,UNTRCD
         LA    R3,TPNDSK
         LA    R4,6
         MVC   BL+1(131),BL
         MVI   CC,W1
MOVLOP2  MVC   PG+6(30),0(R2)
         MVC   PG+59(31),0(R3)
         LA    R2,30(R2)
         LA    R3,31(R3)
         BALR  R8,R9               PRINT IT
         BCT   R4,MOVLOP2
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVI   CC,W3               SPACE 3 AFTER PRINT
         BALR  R8,R9               PRINT IT
         MVC   PG+5(84),SAMPSTRT
         MVI   CC,W2
         BALR  R8,R9               PRINT
         MVC   BL+1(131),BL
         MVC   PG+5(7),=C'COMMAND'
         MVC   PG+58(11),=C'EXPLANATION'
         BALR  R8,R9               PRINT
         LA    R4,11
         LA    R2,SAMPCOMM
         LA    R3,COMEXPL
         MVC   BL+1(131),BL
         MVI   CC,W1
MOVLOP3  MVC   PG+6(44),0(R2)
         MVC   PG+59(41),0(R3)
         LA    R2,44(R2)
         LA    R3,41(R3)
         BALR  R8,R9               PRINT
         BCT   R4,MOVLOP3
         MVC   BL+1(131),BL
         MVI   CC,W1
         BALR  R8,R9               PRINT
         MVI   CC,W2
         MVC   BL+8(5),=C'NOTE.'
         BALR  R8,R9               PRINT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVI   CC,W1               SPACE 1 AFTER PRINT
         MVC   BL+8(121),CCINOTE
         BALR  R8,R9               PRINT
         MVC   BL+8(121),TCTLNOTE
         BALR  R8,R9               PRINT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVC   BL+8(68),DSKNOTE
         BALR  R8,R9               PRINT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVC   BL+8(50),XXXNOTE
         BALR  R8,R9               PRINT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVI   CC,WC1              PRINT & SKIP TO 1
         BALR  R8,R9               PRINT IT
         EJECT
***********************************************************************
**********        PAGE 4 - 'CONSOLE OPERATION'              ***********
***********************************************************************
         SPACE 1
         MVI   PG,C' '
         MVC   PG+1,PG             CLEAR PRINT AREA
         MVI   CC,W3               SET TO PRINT & TRIPLE SPACE
         MVC   BL+7(19),=C'* * * * * * * * * *'
         MVC   BL+104(19),BL+7
         MVC   BL+51(17),=C'OS/DITTO CONSOLE '
         MVC   BL+68(9),=C'OPERATION'
         BALR  R8,R9               PRINT PAGE 2 HDG. - CONSOLE OPER..
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVC   BL+7(115),OPERATOR  OPER. PRINT COMMENT LINE 1
         MVI   CC,W1               SPACE 1 AFTER PRINT
         BALR  R8,R9               PRINT IT
         MVC   BL+7(116),OPERATOR+116   2ND LINE OF OPER. COMMENT
         BALR  R8,R9               PRINT IT
         MVC   BL+7(116),TE7       SET UP WTOR NOTE
         MVI   CC,W2
         BALR  R8,R9               PRINT IT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         LA    R2,TBL1+3           POINTER TO FUNCTION DESCNS.
         LA    R3,NTBL1            # OF PRINT LINES
         LA    R4,STRING
         MVI   CC,W1
         MVC   PG+62(22),REQUR
         B     OPLOOP1
OPERLOOP MVI   CC,W1               SET TO PRINT & SINGLE SPACE
         MVC   PG+54(37),PG+53     CLEAR PRINT AREA
         LA    R1,PG+54
SAVCORE  CLI   0(R4),C' '          END OF LITERAL ?
         BE    BUMPBL              YES
         MVC   0(1,R1),0(R4)       MOVE DATA
         LA    R1,1(R1)            BUMP PRINT AREA
         LA    R4,1(R4)            BUMP PRINT LITERAL POINTER
         B     SAVCORE
BUMPBL   LA    R4,1(R4)            BUMP PAST BLANK
OPLOOP1  MVC   PG+1(49),0(R2)      DESCNS. TO PRINT AREA
         LA    R2,52(R2)           BUMP TO NEXT DESCN.
         BALR  R8,R9               PRINT LINE
         BCT   R3,OPERLOOP
         MVC   PG+1(95),PG         CLEAR PRINT AREA
         BALR  R8,R9               SINGLE SPACE
         LA    R2,M6+1             ADDRESS OF NBLKS=N..N
         LA    R3,DESCN1           ADDRESS OF NBLKS DESCN
         LA    R4,6                # OF PRINT LINES
         MVI   BL+66,C'*'
OPLOOP   MVI   CC,W1               SET TO SINGLE SPACE
         MVC   BL+07(14),00(R2)    LEFT PARAMETER TO PRINT AREA
         MVC   BL+27(33),00(R3)    LEFT DESCRIPTION TO PRINT AREA
         MVC   BL+74(14),15(R2)    RIGHT PARAMETER TO PRINT AREA
         MVC   BL+94(33),33(R3)    RIGHT DESCRIPTION TO PRINT AREA
         LA    R2,30(R2)           BUMP PARAMETER POINTER
         LA    R3,66(R3)           BUMP DESCRIPTION POINTER
         BALR  R8,R9               PRINT IT
         BCT   R4,OPLOOP
         MVC   BL+01(130),BL       CLEAR PRINT AREA
         MVI   CC,WC1              PRINT & SKIP TO 1
         BALR  R8,R9
         EJECT
***********************************************************************
**********        PAGE 5 - 'TAPE DENSITY & MODE VALUES'     ***********
***********************************************************************
         SPACE 1
         MVI   CC,W3               SET TO PRNT & TRIPLE SPACE
         MVC   PG+22(38),TAPHDG    'TAPE DENSITY...' HDG.
         MVC   PG+0(19),=C'* * * * * * * * * *'
         MVC   PG+62(19),PG
         BALR  R8,R9               PRINT IT - LINE 1
         MVC   PG,HDG              COL. HDGS. - 'TRACK DENSITY...'
         BALR  R8,R9               PRINT THE COLUMN HDGS.
         MVI   CC,W1               SET TO PRNT & SINGLE SPACE
         MVC   PG+1,PG             CLEAR PRNT AREA
         LA    R2,TBL2             POINTER TO PRINT LITERALS
         LA    R3,NTBL2            NUMBER OF LITERALS
MOVTBL2  MVC   DEN,0(R2)           DENSITY VALUE TO PRINT AREA
         MVC   PAR,3(R2)           PARITY VALUE TO PRINT AREA
         MVC   TRN,7(R2)           TRANSLATE VALUE TO PRINT AREA
         MVC   DCT,10(R2)          DATA CONVERT VALUE TO PRINT AREA
         MVC   MMS,13(R2)          MODE VALUE TO PRINT AREA
         MVC   TRK,15(R2)          TRACK VALUE TO PRINT AREA
         BALR  R8,R9               PRINT THE VALUES
         LA    R2,16(R2)           BUMP TO NEXT LITERAL
         BCT   R3,MOVTBL2          LOOP & PRINT ALL 7 TRACK
         MVC   PG+1,PG             CLEAR PRINT AREA
         BALR  R8,R9               PRINT BLANK LINE
         MVI   TRK,C'9'            9 TO TRACK
         MVC   DEN-1(4),=C' 800'   800 TO DENSITY
         MVC   PAR(14),=C'SINGLE DENSITY'
         MVC   MMS,=C'C0'          MODE VALUE TO PRINT AREA
         BALR  R8,R9               PRINT 9-800-SINGLE DENSITY
         MVC   DEN-1(2),=C'16'     1600 TO DENSITY
         BALR  R8,R9               PRINT 9-1600-SINGLE DENSITY
         MVC   DEN-1(3),=C'625'    6250 BPI
         MVI   MMS,C'D'            D0 MODE SET
         MVI   CC,W2               PRT & DOUBLE SPACE
         BALR  R8,R9               PRINT
         MVC   DEN-1(4),=C' 800'   800 TO DENSITY
         MVC   PAR(6),=C'DUAL  '   OVERLAY SINGLE WITH DUAL
         MVI   CC,W1               SET TO PRINT & SINGLE SPACE
         MVC   MMS,=C'C8'              C8
         BALR  R8,R9               PRINT 9-800-DUAL DENSITY
         MVC   DEN-1(2),=C'16'     1600 TO DENSITY
         MVI   MMS+1,C'0'          MODE TO C0
         BALR  R8,R9               PRINT 9-1600-DUAL DENSITY
         MVC   DEN-1(3),=C'625'    6250 BPI
         MVI   MMS,C'D'            D0
         MVI   CC,W3               PRT & TRIPLE SPACE
         BALR  R8,R9               PRINT
         MVC   PG+1,PG             CLEAR PRINT AREA
         MVC   TRK(63),DEFAULT     'NOTE. MM...' TO PRINT AREA
         BALR  R8,R9               PRINT NOTE & TRIPLE SPACE
         MVI   CC,W2               SET TO PRINT & DOUBLE SPACE
         MVC   PG(80),TE1          TAPE ERROR COMMENT LINE 1
         BALR  R8,R9               PRINT IT
         MVC   PG(80),TE2          TAPE ERROR COMMENT LINE 2
         BALR  R8,R9               PRINT IT
         MVC   PG(80),TE3          TAPE ERROR COMMENT LINE 3
         BALR  R8,R9               PRINT IT
         MVC   PG(80),TE4          TAPE ERROR COMMENT LINE 4
         BALR  R8,R9               PRINT IT
         MVC   PG(80),TE5          TAPE ERROR COMMENT LINE 5
         BALR  R8,R9               PRINT IT
         MVC   BL+1(131),BL        CLEAR PRINT AREA
         MVC   PG(28),TE6          TAPE ERROR COMMENT - LINE 6
         BALR  R8,R9               PRINT IT
         L     RA,RETURN
         BR    RA                  WE ARE DONE - RETURN TO DITTO
RETURN   DS    1F
SAVR6    DS    F                   SAVE R6 DURING PROCEDURE PAGE LOOP
SAVR6XXX DS    F                   SAVE R6 BEFORE BR TO PRINT
BUMP     DC    H'4096'             BASE REGISTER BUMP
PARAM    B     X1                  VECTOR TABLE
         B     X2                  ENTRY INTO THIS TABLE
         B     X3                  IS BASED ON AN INDEX
         B     X4                  VALUE OBTAINED FROM
         B     X5                  TBL4.
         B     X6                  EXIT IS TO A SPECIFIC
         B     X7                  MOVE STATEMENT FOR
         B     X8                  THE REQUIRED PARAMETER
         B     X9
         B     XA
         B     XB
         B     XC
X1       LR    R8,R0               NO PARAMETER REQUIRED
         BR    R8                  RETURN
X2       MVC   0(12,R1),M2         'INPUT = SYSNNN' TO PRINT AREA
         LA    R1,12(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
X3       MVC   0(14,R1),M3         'OUTPUT = ...' TO PRINT AREA
         LA    R1,14(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
X4       MVC   0(12,R1),M4         'BEGIN...' TO PRINT AREA
         LA    R1,12(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
X5       MVC   0(10,R1),M5         'END=...' TO PRINT AREA
         LA    R1,10(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
X6       MVC   0(11,R1),M6         'NBLKS=...' TO PRINT AREA
         LA    R1,11(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
X7       MVC   0(14,R1),M7         'RECSIZE=..#' TO PRINT AREA
         LA    R1,14(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
X8       MVC   0(14,R1),M8         'BLKFACTOR...' TO PRINT AREA
         LA    R1,14(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
X9       MVC   0(12,R1),M9+1       'DECKTYPE=...' TO PRINT AREA
         LA    R1,12(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
XA       MVC   0(15,R1),MA         'DECKNAME=...' TO PRINT AREA
         LA    R1,15(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
XB       MVC   0(13,R1),MB         'OUTPUT...' TO PRINT AREA
         LA    R1,13(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
XC       MVC   0(13,R1),M9         ',DECKTYPE=...' TO PRINT AREA
         LA    R1,13(R1)           BUMP PRINT AREA POINTER
         B     X1                  RETURN
         SPACE 2
***********************************************************************
**********             THIS TABLE DETERMINES THE LINE       ***********
**********          SEQUENCE AND PARAMETERS FOR THE         ***********
**********          FUNCTION CONTROL CARD PAGE.             ***********
**********             PARAMETERS ARE ASSEMBLED IN A        ***********
**********          STRING FORMAT IN THE PRINT AREA. THE    ***********
**********          HEX VALUES 02 THRU 0B CORRESPOND TO     ***********
**********          PARAMETERS LABELED M2 THRU MB.          ***********
***********************************************************************
         SPACE 1
TBL4     DC    X'00'     BYPASS FIRST ELEMENT IN TBL1
         DC    X'0100'     CC
         DC    X'090A00'   CCS
         DC    X'0100'     CP
         DC    X'0100'     CH
         DC    X'0B0800'   CT
         DC    X'0B080C0A00' CTS
         DC    X'00'               BYPASS CCL
         DC    X'00'       BYPASS PAGE 1 SPACE
         DC    X'00'       BYPASS *** TAPE FUNCTION COMMENT
         DC    X'0200'     TC
         DC    X'020600'   TP
         DC    X'02070600' TPR
         DC    X'020600'   TH
         DC    X'02070600' THR
         DC    X'020600'   TPV
         DC    X'020600'   TDV
         DC    X'00'       BYPASS TRS
         DC    X'00'       BYPASS TRL
         DC    X'00'       BYPASS INT
         DC    X'02030600' TT (WITH OPTIONAL NBLKS)
         DC    X'0203070800' TTR
         DC    X'0B00'     WTM
         DC    X'0B00'     REW
         DC    X'0B00'     RUN
         DC    X'0B0600'   FSR
         DC    X'0B0600'   BSR
         DC    X'0B00'     FSF
         DC    X'0B00'     BSF
         DC    X'00'       BYPASS ERT
         DC    X'00'       BYPASS SPACE LINE
         DC    X'00'       BYPASS *** DISK FUNCTION COMMENT
         DC    X'00'               BYPASS DSE
         DC    X'02040500'         DP
         DC    X'02040500'         DD
         DC    X'0204050700'       DPD
         DC    X'0204050700'       DDD
         DC    X'00'       BYPASS DRL
         DC    X'00'       BYPASS DRS
         DC    X'00'       BYPASS EOF
         DC    X'00'       BYPASS DID
         DC    X'00'       BYPASS SPACE LINE
         DC    X'0100'     XXX
         DC    X'0100'     EOJ
         SPACE 2
***********************************************************************
**********                     CONSTANTS                    ***********
***********************************************************************
         SPACE 1
TBL1     DC    C'               CARD FUNCTIONS                       '
         DC    C'CC CARD TO CARD                                     '
         DC    C'CCSCARD TO CARD WITH SEQ. NUMBERS AND DECK NAME     '
         DC    C'CP CARD TO PRINTER IN CHARACTER FORMAT              '
         DC    C'CD CARD TO PRINTER IN CHARACTER AND HEX DUMP FORMAT '
         DC    C'CT CARD TO TAPE BLOCKED 1 TO 400                    '
         DC    C'CTSCARD TO TAPE RESEQUENCED                         '
         DC    C'CCLCANCEL CARD INPUT (FLUSH TO EOF)                 '
         DC    C'                                                    '
         DC    C'               TAPE FUNCTIONS                       '
         DC    C'TC TAPE TO CARD BLOCKED OR UNBLOCKED                '
         DC    C'TP TAPE TO PRINTER UNBLOCKED IN CHAR. FORMAT        '
         DC    C'TPDTAPE TO PRINTER DEBLOCKED IN CHAR. FORMAT        '
         DC    C'TD TAPE TO PRINTER UNBLOCKED IN CHAR. AND HEX DUMP  '
         DC    C'TDDTAPE TO PRINTER DEBLOCKED IN CHAR. AND HEX DUMP  '
         DC    C'TPVTAPE TO PRINTER VARIABLE RECDS CHAR. FORMAT      '
         DC    C'TDVTAPE TO PRINTER VARIABLE RECDS CHAR. AND HEX DUMP'
         DC    C'TRSTAPE RECORD SCAN                                 '
         DC    C'TRLTAPE RECORD LOAD                                 '
         DC    C'INTINITIALIZE TAPE                                  '
         DC    C'TT TAPE TO TAPE  (01 TO 99) FILES                   '
         DC    C'TTRTAPE TO TAPE REBLOCKED                           '
         DC    C'WTMWRITE TAPE MARK                                  '
         DC    C'REWREWIND TAPE                                      '
         DC    C'RUNREWIND AND UNLOAD TAPE                           '
         DC    C'FSRFORWARD SPACE RECORD                             '
         DC    C'BSRBACK SPACE RECORD                                '
         DC    C'FSFFORWARD SPACE FILE                               '
         DC    C'BSFBACK SPACE FILE                                  '
         DC    C'ERTERASE TAPE  (DATA SECURITY ERASE - 3420 ONLY)    '
         DC    C'                                                    '
         DC    C'               DISK FUNCTIONS                       '
         DC    C'DSEDISPLAY DATA SET EXTENT ON CONSOLE, USE FOR CCCHH'
         DC    C'DP DISK TO PRINTER UNBLOCKED IN CHAR. FORMAT        '
         DC    C'DD DISK TO PRINTER UNBLOCKED IN CHAR. AND HEX DUMP  '
         DC    C'DPDDISK TO PRINTER DEBLOCKED IN CHAR. FORMAT        '
         DC    C'DDDDISK TO PRINTER DEBLOCKED IN CHAR. AND HEX DUMP  '
         DC    C'DRLDISK RECORD LOAD - KEY AND/OR DATA               '
         DC    C'DRSDISK RECORD SCAN - PARTIAL KEY OR DATA OR EOF    '
         DC    C'EOFWRITE DISK EOF RECORD                            '
         DC    C'DIDALTER DISK IDENTIFICATION VOLUME LABEL (OFFLINE) '
         DC    C'                                                    '
         DC    C'XXXLIST FUNCTIONS ON SYSPRINT                       '
         DC    C'EOJEND OF JOB                                       '
COMNT1   DC    C'CONTROL CARD OPERATION - /*'
         DC    C' IS SYSIN EOF FOR CONTROL '
         DC    C'CARDS '
         DC    X'50'                   &
         DC    C' DATA.'
COMNT1AA DC    C'                           '
         DC    C' IF /* IN DATA, //SYSIN DD DATA,DLM=AA.'
COMNT1A  DC    C'                         /+'
         DC    C' IS OS/DITTO EOF FOR DATA CARD INPUT.  '
COMNT2   DC    C'CONSOLE OPERATION - READER EOF KEY'
         DC    C' IS END OF FILE FOR CARD INPUT.  '
COMNT2A  DC    C'FOR SPLIT CYLINDER DISK FUNCTIONS:'
         DC    C' USE SP, SD, SPD, SDD, AND SRS.  '
COMNT3   DC    C'PUNCHED OUTPUT MAY BE INTERPRETE'
         DC    C'D BY USING //SYSPUNCH DD UNIT=00D,'
COMNT4   DC    C'DCB=(BLKSIZE=81,RECFM=FA,FUNC=I) -'
         DC    C' 3525 PRINT FEATURE REQUIRED.   '
TBL2     DC    C'200ODD OFFON 107'
         DC    C'200EVENOFFOFF207'
         DC    C'200EVENON OFF287'
         DC    C'200ODD OFFOFF307'
         DC    C'200ODD ON OFF387'
         DC    C'                '
         DC    C'556ODD OFFON 507'
         DC    C'556EVENOFFOFF607'
         DC    C'556EVENON OFF687'
         DC    C'556ODD OFFOFF707'
         DC    C'556ODD ON OFF787'
         DC    C'                '
         DC    C'800ODD OFFON 907'
         DC    C'800EVENOFFOFFA07'
         DC    C'800EVENON OFFA87'
         DC    C'800ODD OFFOFFB07'
         DC    C'800ODD ON OFFB87'
DESCN    DC    C'MUST BE TAPEIN,DISKIN, OR TAPEOUT'
         DC    C'MUST BE TAPEOUT OR TAPEIN        '
         DC    C'CCCHH REL. TO 1ST TRK OF DATA SET'
         DC    C'CCCHH REL. TO 1ST TRK OF DATA SET'
DESCN1   DC    C'NUMBER OF TAPE BLOCKS     1-9999 '
         DC    C'LOGICAL RECORD SIZE      1-99999 '
         DC    C'DECKTYPE (BAL, RPG, COB)         '
         DC    C'DECKNAME (0-3, 0-5, 0-8) CHAR.   '
         DC    C'OUTPUT BLOCKING FACTOR     1-999 '
         DC    C'LENGTH OF SCAN ARGUMENT (1-35)   '
         DC    C'COPY FORWARD OR BACK    (F OR B) '
         DC    C'NEW VOLUME SERIAL NUMBER  LNTH=6 '
         DC    C'NUMBER OF FILES         (1-99)   '
         DC    C'SCAN FOR (K=KEY, D=DATA, E=EOF)  '
         DC    C'OPTIONAL - (LIMITS PRINTING)     '
         DC    C'TRS VARIABLE LENGTH RECORDS ONLY '
M2       DC    C'INPUT=DDNAME  '
M3       DC    C',OUTPUT=DDNAME '
M4       DC    C',BEGIN=CCCHH   '
M5       DC    C',END=CCCHH     '
M6       DC    C',NBLKS=N..N    '
M7       DC    C',RECSIZE=N...N '
M9       DC    C',DECKTYPE=XXX  '
MA       DC    C',DECKNAME=X...X'
M8       DC    C',BLKFACTOR=N.N '
         DC    C',SCANLNTH=NN   '
         DC    C',COPYD=X       '
         DC    C',VOLSER=NNNNNN '
         DC    C',NFILES=NN     '
         DC    C',SCANTYPE=X    '
         DC    C',NRECS=N..N    '
         DC    C',RECSIZE=V     '
MB       EQU   M3+1
NTBL1    EQU   44                  # OF ELEMENTS IN TBL1
NTBL2    EQU   17
NTBL3    EQU   NTBL1
W1       EQU   09
W2       EQU   17
W3       EQU   25
WC1      EQU   137
W        EQU   80
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
DEFAULT  DC    C'NOTE.  MM PORTION OF CUUMM IS OPTIONAL - '
         DC    C'VALUE IS D0 IF OMITTED'
CNTRLHD  DC    C'FUNCTION CONTROL CARD FORMAT'
PARM1    DC    C'NOTE. SUBMIT (PARM=''JOBSTREAM'') ON EXEC CARD '
         DC    C'TO DENOTE CONTROL CARD OPERATION.'
DDNOTE   DC    C'      SUBMIT TAPEIN, TAPEOUT, '
         DC    X'50'                   &
         DC    C' DISKIN DD CARDS IF USED IN INPUT= OR OUTPUT=. '
NOTE3    DC    C'      TAPEOUT IS OPENED AS OUTPUT; THEREFORE, A FILE '
         DC    C'PROTECT RING IS REQUIRED.'
NOTE4    DC    C'      TWO TAPE MARKS ARE WRITTEN ON TAPEOUT BY A CLOS'
         DC    C'E IN FUNCTIONS RUN '
         DC    X'50'                   &
         DC    C' EOJ.'
NOTE4A   DC    C'      DCB=(DEN=X,TRTCH=XX) MAY BE SPECIFIED ON TAPEIN'
         DC    C' AND TAPEOUT DD CARDS.   '
CCCHH1   DC    C'      CCCHH IS RELATIVE TO FIRST CCCHH OF DATA SET EX'
         DC    C'TENT; E.G. BEGIN=00000,  '
CCCHH2   DC    C'            END=00418 WILL ACCESS ENTIRE 3330 FIVE CY'
         DC    C'LINDER 3330 DATA SET.    '
NBLKSNOT DC    C'      NBLKS=N..N PARAMETER IS OPTIONAL. LAST PARAMETE'
         DC    C'R MUST BE EOJ.           '
CCCHH3   DC    C'      FROM CONSOLE CCCHH IS ABSOLUTE. OPERATOR MUST U'
         DC    C'SE DSE FOR VALID CCCHH.  '
TAPHDG   DC    C'CONSOLE TAPE DENSITY '
         DC    X'50'
         DC    C' MODE VALUES  MM'
HDG      DC    C'     '
         DC    C'TRACK'
         DC    C'      '
         DC    C'DENSITY'
         DC    C'      '
         DC    C'PARITY'
         DC    C'      '
         DC    C'TRANSLATE'
         DC    C'      '
         DC    C'DATA CONVERT'
         DC    C'      '
         DC    C'MM'
         DC    C'      '
TE1      DC    C'A USER INPUT TAPE ERROR CORRECTION ROUTI'
         DC    C'NE IS AVAILABLE UNDER CONSOLE OPERATION.'
TE2      DC    C'CONTROL WILL BE PASSED TO THE ROUTINE FO'
         DC    C'R ALL INPUT TAPE DITTO FUNCTIONS IF  OS '
TE3      DC    C'ERROR RECOVERY IS UNSUCCESSFUL.  THE CON'
         DC    C'SOLE OPERATOR MAY EITHER IGNORE THE     '
TE4      DC    C'ERROR (ACCEPTING THE RECORD AS READ), BY'
         DC    C'PASS (DROP) THE ERROR RECORD, OR PERFORM'
TE5      DC    C'CONSOLE ALTERATION TO THE RECORD LENGTH '
         DC    C'AND/OR DATA, BEFORE RETURNING CONTROL TO'
TE6      DC    C'THE ORIGINAL DITTO FUNCTION.'
TE7      DC    C'FUNCTIONS MAY BE ENDED BY REPLYING ''IRPT'' TO THE 1S'
         DC    C'T WTOR MESSAGE. A REPLY OF ''EOJ'' WILL END THE FUNCT'
         DC    C'ION AND DITTO.'
WHO      DC    C'L. S. BRAMMER       IBM CORPORATION  1973'
OPERATOR DC    C'TO MINIMIZE CONSOLE MESSAGES, YOU MAY ENTER'  LINE 1A
         DC    C' WITH THE FUNCTION ALL OR PART OF THE  '
         DC    C'REQUIRED PARAMETERS.  A PARAMETER ERROR '  LINE 2A
         DC    C'WILL CAUSE DITTO TO REVERT TO THE '
         DC    C'TUTORIAL ROUTINES AND REQUEST REMAINING '  LINE 3A
         DC    C'PARAMETERS IN A SINGLE REQUEST MODE.    '  LINE 3B
REQUR    DC    C'PARAMETER REQUIREMENTS'
STRING   DC    C'CC '
         DC    C'CCS,DECKTYPE,DECKNAME '
         DC    C'CP '
         DC    C'CD '
         DC    C'CT,CUUMM,BLKFACTOR '
         DC    C'CTS,CUUMM,BLKFACTOR,DECKTYPE,DECKNAME '
         DC    C'CCL '
         DC    C' '
         DC    C' '
         DC    C'TC,CUUMM '
         DC    C'TP,CUUMM,NRECS '
         DC    C'TPD,CUUMM,RECSIZE,NRECS '
         DC    C'TD,CUUMM,NRECS '
         DC    C'TDD,CUUMM,RECSIZE,NRECS '
         DC    C'TPV,CUUMM,NRECS '
         DC    C'TDV,CUUMM,NRECS '
         DC    C'TRS,CUUMM,RECSIZE,SCANLNTH '
         DC    C'TRL,CUUMM,CUUMM,NBLKS,COPYD '
         DC    C'INT,CUUMM,VOLSER '
         DC    C'TT,CUUMM,CUUMM,NFILES '
         DC    C'TTR,CUUMM,CUUMM,RECSIZE,BLKFACTOR '
         DC    C'WTM,CUUMM '
         DC    C'REW,CUUMM '
         DC    C'RUN,CUUMM '
         DC    C'FSR,CUUMM,NBLKS '
         DC    C'BSR,CUUMM,NBLKS '
         DC    C'FSF,CUUMM,NFILES '
         DC    C'BSF,CUUMM,NFILES '
         DC    C'ERT,CUUMM '
         DC    C' '
         DC    C' '
         DC    C'DSE,CUU '
         DC    C'DP,CUU,CCHHH,CCCHH '
         DC    C'DD,CUU,CCHHH,CCCHH '
         DC    C'DPD,CUU,CCCHH,CCCHH,RECSIZE '
         DC    C'DDD,CUU,CCCHH,CCCHH,RECSIZE '
         DC    C'DRL,CUU,CCC-HH-RRR '
         DC    C'DRS,CUU,CCCHH,CCCHH,SCANTYPE '
         DC    C'EOF,CUU,CCC-HH-RRR '
         DC    C'DID '
         DC    C' '
         DC    C'XXX '
         DC    C'EOJ '
DITPROC  DC    C'CC '
         DC    C'CCI'
         DC    C'CP '
         DC    C'CT '
         DC    C'TC '
         DC    C'TCI'
         DC    C'TP '
         DC    C'TT '
         DC    C'TRL'
         DC    C'TRS'
         DC    C'DP '
         DC    C'DID'
DEFVAL   DC    C'RDR=00C,PUN=00D,CLS=          '
         DC    C'RDR=00C,PUN=00D               '
         DC    C'RDR=00C,PRT=,CLS=A            '
         DC    C'RDR=00C,OUT=TAPE              '
         DC    C'IN=TAPE,PUN=00D,CLS=          '
         DC    C'IN=TAPE,PUN=00D               '
         DC    C'IN=TAPE,PRT=,CLS=A            '
         DC    C'IN=TAPE,OUT=TAPE              '
         DC    C'IN=TAPE,OUT=TAPE,PRT=00E,CLS= '
         DC    C'IN=TAPE,PRT=00E,CLS=          '
         DC    C'IN=SYSDA,DSNM=,SER=,PRT=,CLS=A'
         DC    C'NONE                          '
GDELIN   DC    C'CC,CCS,CP,CD,CCL                 '
         DC    C'CC,CCS,CP,CD,CCL                 '
         DC    C'CP,CD,CCL                        '
         DC    C'CT,CTS,CCL                       '
         DC    C'TC,TP,TPD,TD,TDD                 '
         DC    C'TC,TP,TPD,TD,TDD                 '
         DC    C'TP,TPD,TD,TDD,TPV,TDV,TRS,INT,ERT'
         DC    C'TT,TTR                           '
         DC    C'TRL                              '
         DC    C'TRS                              '
         DC    C'DSE,DP,DD,DPD,DDD,DRL,DRS,SRS,EOF'
         DC    C'DID,XXX                          '
UNTRCD   DC    C'RDR=CUU   SELECT SYSIN UNIT   '
         DC    C'PUN=CUU   SELECT SYSPUNCH UNIT'
         DC    C'CLS=      SYSOUT CLASS=NULL   '
         DC    C'CLS=A     SYSOUT CLASS=A OR B '
         DC    C'PRT=      SYSPRINT UNIT=NULL  '
         DC    C'PRT=CUU   SELECT SYSPRINT UNIT'
TPNDSK   DC    C'IN=CUU    SELECT TAPEIN UNIT   '
         DC    C'IN=CUU    SELECT DISKIN UNIT   '
         DC    C'OUT=CUU   SELECT TAPEOUT UNIT  '
         DC    C'DSNM=     SPECIFY DISKIN DSN   '
         DC    C'SER=      PROVIDE DISKIN VOLUME'
         DC    C'          SERIAL NUMBER        '
PROC1    DC    C'TO INITIATE OS/DITTO FROM THE CONSOLE ENTER A START C'
         DC    C'OMMAND OF THE FORM; ''S DITTO---''.    REPLACE --- WI'
         DC    C'TH OS/DITTO '
PROC2    DC    C'PROCEDURE NAME SUFFIX; E.G. S DITTOTP    WILL ALLOCAT'
         DC    C'E ONE TAPE DRIVE AND SYSPRINT. THIS ALLOCATION WILL '
         DC    C'SUPPORT    '
PROC3    DC    C'DITTO FUNCTION TP AND ALL OTHER FUNCTIONS THAT REQUIR'
         DC    C'E NO MORE THAN ONE TAPE DRIVE AND SYSPRINT. TO SELECT'
         DC    C' INPUT     '
PROC4    DC    C'TAPE DRIVE AND PRINT ONLINE, ENTER OPTIONAL SYMBOLIC '
         DC    C'PARAMETERS; E.G. S DITTOTP,IN=281,PRT=00E,CLS= .    U'
         DC    C'SE THE     '
PROC5    DC    C'FOLLOWING PROCEDURE NAME, SYMBOLIC PARAMETER, AND FUN'
         DC    C'CTION USE GUIDELINES TO CHOOSE CORRECT PROCEDURE AND '
         DC    C'FUNCTION. '
PROCED   DC    C'PROCEDURES'
SYMBOLPA DC    C'SYMBOLIC PARAMETERS'
FUNCMSG  DC    C'FUNCTION USE'
NAME     DC    C'NAME'
DEFVALU  DC    C'DEFAULT VALUES'
GDELINE  DC    C'GUIDELINES'
SAMPSTRT DC    C'SAMPLE START COMMANDS USING OPTIONAL SYMBOLIC PARAMET'
         DC    C'ERS TO OVERRIDE DEFAULT VALUES.'
SAMPCOMM DC    C'S DITTOCC,RDR=00A,PUN=,CLS=B                '
         DC    C'S DITTOCCI,RDR=00A                          '
         DC    C'S DITTOCP,PRT=00F,CLS=                      '
         DC    C'S DITTOCT,OUT=TAPE7                         '
         DC    C'S DITTOTC,IN=285,PUN=,CLS=B                 '
         DC    C'S DITTOTCI,PUN=00B                          '
         DC    C'S DITTOTP,IN=287,PRT=00F,CLS=               '
         DC    C'S DITTOTT,IN=284,OUT=281                    '
         DC    C'S DITTOTRL,PRT=,CLS=A                       '
         DC    C'S DITTOTRS,IN=270,PRT=,CLS=A                '
         DC    C'S DITTODP,IN=151,DSNM=DATASET,SER=333001    '
COMEXPL  DC    C'SYSIN ON 00A, SYSPUNCH TO SYSOUT=B       '
         DC    C'SYSIN ON 00A                             '
         DC    C'SYSPRINT ON 00F                          '
         DC    C'TAPEOUT ON 7-TRACK  (NEED UNITNAME=TAPE7)'
         DC    C'TAPEIN ON 285, SYSPUNCH TO SYSOUT=B      '
         DC    C'SYSPUNCH ON 00B                          '
         DC    C'TAPEIN ON 287, SYSPRINT ON 00F           '
         DC    C'TAPEIN ON 284, TAPEOUT ON 281            '
         DC    C'SYSPRINT TO SYSOUT=A                     '
         DC    C'TAPEIN ON 270, SYSPRINT TO SYSOUT=A      '
         DC    C'DISKIN ON 151, DSN=DATASET,VOL=SER=333001'
CCINOTE  DC    C'DITTOCCI AND DITTOTCI INTERPRET CARD OUTPUT. 3525 FEA'
         DC    C'TURE AND OS JCL FUNC=I SUPPORT REQUIRED. SYSPUNCH MUS'
         DC    C'T BE ONLINE.   '
TCTLNOTE DC    C'TAPE CONTROL FUNCTIONS (INT,WTM,REW,RUN,FSR,BSR,FSF,B'
         DC    C'SF,ERT) ARE VALID WHEN TAPEIN OR TAPEOUT ALLOCATED BY'
         DC    C' PROCEDURE.    '
DSKNOTE  DC    C'DISKIN IS OPENED AS INOUT TO ALLOW DISK WRITE FUNCTIO'
         DC    C'NS DRL AND EOF.'
XXXNOTE  DC    C'XXX AND DID FUNCTIONS ARE VALID IN ALL PROCEDURES.'
PGX      DSECT
BL       DS    CL20
PG       DS    0CL81
         DS    CL7
TRK      DS    CL1
         DS    CL10
DEN      DS    CL3
         DS    CL9
PAR      DS    CL4
         DS    CL10
TRN      DS    CL3
         DS    CL14
DCT      DS    CL3
         DS    CL10
MMS      DS    CL2
         ORG   PG
         DS    CL10
OPCODE   DS    CL3
         DS    CL7
COMMENT  DS    CL49
         DS    CL4
DOLLAR   DS    CL1
CCX      DSECT
CC       DS    CL1
DITTOXXX CSECT
         #WAYIN GPM=YES
         #SPGSECT
         DC    CL24'P!A!N006SOF155YYYY230185'
         END
