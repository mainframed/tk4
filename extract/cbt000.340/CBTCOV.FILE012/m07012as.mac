         MACRO
         YEBSAVE &NAME
         LCLC  &C
         LCLA  &A,&B
&A       SETA  K'&NAME
         AIF   (&A EQ 0).NONAME
         AIF   (&A LE 4).NOTLONG
***                 O.S. SUPPORT GROUP SAVE AND EQUATE MACRO        ***
         SPACE 2
&B       SETA  &A-4
         STM   R14,R12,12(R13)         . SAVE REGISTERS
         LR    R12,R15                 . R12 -> ENTRY POINT
         USING &NAME.,R12
&C       SETC  '&NAME'(5,&B)
         ST    R13,SAVE&C+4            . SAVE OLD SAVE AREA ADDRESS
         LR    R15,R13                 . R15 -> OLD SAVE AREA
         LA    R13,SAVE&C              . R13 -> NEW SAVE AREA
         ST    R13,8(0,R15)            . CHAIN SAVE AREAS
         B     MISSAVE                 . BRANCH ROUND SAVE AREA
SAVE&C   DS    18F
         EJECT
***                 REGISTER EQUATES                                ***
         SPACE 2
         YEBEQU
         EJECT
MISSAVE  EQU   *
         MEXIT
.NONAME  ANOP
         MNOTE 8,'*** NO CSECT NAME SUPPLIED ***'
         MEXIT
.NOTLONG ANOP
         MNOTE 8,'*** CSECT NAME LESS THAN 5 CHARS ***'
         MEND
