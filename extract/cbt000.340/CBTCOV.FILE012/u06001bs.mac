 CATCHPR :PROC OPTIONS(MAIN) ;
          /*  PROGRAM TO PRODUCE QUICK SUMMARY OF A CATCH TAPE      */
          DCL 1 INREC,
                3 I1        BIT(8),
                3 MODULE    CHAR(9),
              1 OUTREC,
                3 I2          BIT(8),
                3 MODNAME     CHAR(9),
                3 F           FLOAT(16) INIT(0.),
              1 ORDER,
                3 COUNT     BIN FIXED(16),
                3 NAME      CHAR(9),
                3 GASH1     CHAR(5),
              1 INREC2,
                3 DAYTE     DEC FIXED(5,3),
                3 LOSTRECS  DEC FIXED(5,0),
                3 GASH      CHAR(4),
              START      CHAR(4) DEF MODULE POS(1),
              END        CHAR(4) DEF MODULE POS(5),
              DUMP FILE SEQUENTIAL BUFFERED,
              (SORTIN,SORTOUT) FILE RECORD,
              (MSEC,SECS,MINS,HRS) BIN FIXED(16),
              IPG        INIT(0),
              SF         CHAR(23)     INIT(' SORT FIELDS=(2,9,CH,A)'),
              SF1        CHAR(23) INIT(' SORT FIELDS=(1,4,FI,D)'),
              RF         CHAR(26) INIT(' RECORD TYPE=F,LENGTH=(18)' ),
              IHESARC    ENTRY (BIN FIXED(16)),
              IHESRTA    ENTRY (,,,BIN FIXED(16)),
              MAINSTORE  BIN FIXED(31) INIT(81920),
              LIBRARY    CHAR(7),
              RETCD      BIN FIXED(16),
               SELCD CHAR(80) STATIC INIT(' SELECT MEMBER='),
          CCD CHAR(80) STATIC INIT(' C I=I,O=O'),
     (TTRL,TTR,TTRS) BIN FIXED(31),
          (PUN1,PUN2,PUN3) FILE RECORD OUTPUT ENV(F (1280,80)),
          EXCD CHAR(80) STATIC INIT(' EXCLUDE MEMBER='),
              RTNNAME    CHAR(9),
              RTN        CHAR(8) DEF RTNNAME POS(1),
              IGC        CHAR(3) DEF RTNNAME POS(1),
             INL  CHAR(1) DEF RTNNAME,
              SVC        CHAR(3) DEF RTNNAME POS(6),
              LIB        CHAR(1) DEF RTNNAME POS(9),
              SVCNO      CHAR(3),
              OR         CHAR(1) DEF SVCNO POS(3),
              ALLSTARS   CHAR(43) INIT((43)'*'),
              SOMESTARS  CHAR(43) INIT
                        ('*          *        *          *          *'),
                              /* END OF DECLARATIONS  */  ;
          ON ERROR SNAP CALL IHEDUMP      ;
          TTRS,TTRL=99999999;
          OPEN FILE(DUMP),OUTPUT FILE(SORTIN) ;
 READ    :READ FILE(DUMP) INTO (INREC) ;
          IF I1 = '10011001'B THEN GO TO LASTREC ;
          IF I1 = '10001000'B THEN GO TO READ ;
          MODNAME = MODULE ;
          I2 = I1 ;
          WRITE FILE(SORTIN) FROM (OUTREC) ;
          GO TO READ ;
 LASTREC :
         MSEC,SECS,MSEC,MINS,HRS=0    ;
          READ FILE(DUMP) INTO (INREC2) ;
          CLOSE FILE(DUMP), FILE(SORTIN) ;
          CALL IHESRTA (SF, RF, MAINSTORE, RETCD) ;
          IF RETCD > 0 THEN GO TO SORTFAIL ;
          OPEN FILE(SORTOUT) ;
          ON ENDFILE(SORTOUT) GO TO FREQUENCY ;
          READ FILE(SORTOUT) INTO (OUTREC) ;
          OPEN OUTPUT FILE(SORTIN) ;
 CONT    :NAME = MODNAME ;
          COUNT = 1 ;
 NEXT    :READ FILE(SORTOUT) INTO (OUTREC) ;
          IF MODNAME = NAME THEN DO ;
                                      COUNT = COUNT + 1 ;
                                      GO TO NEXT ;
                                    END ;
          WRITE FILE(SORTIN) FROM (ORDER) ;
          GO TO CONT ;
 FREQUENCY:WRITE FILE(SORTIN) FROM (ORDER) ;
          CLOSE FILE(SORTIN),FILE(SORTOUT) ;
          CALL IHESRTA(SF1, RF, MAINSTORE,RETCD) ;
          IF RETCD > 0 THEN GO TO SORTFAIL ;
          OPEN FILE(SORTOUT) ;
          ON ENDFILE(SORTOUT) GO TO EOJ ;
          ON ENDPAGE(SYSPRINT) BEGIN ;
            IPG = IPG + 1 ;
            PUT PAGE EDIT( ALLSTARS,'PAGE',IPG,
                          '* CATCH SUMMARY OF LOADINGS   ',DAYTE,
                          '     *',
                           ALLSTARS,
                           ALLSTARS,
                          '* LOADINGS * SVC NO * NAME     * LIBRARY  *',
                           ALLSTARS)
                         ( X(30), A, X(40),   A,  F(3), SKIP,
                           X(30), A, F(7,3),         A, SKIP,
                           X(30), A, SKIP(2), X(30), A, SKIP,
                           X(30), A, SKIP,    X(30), A       ) ;
            LINES = 0 ;
          END ;
          SIGNAL ENDPAGE ;
 AGAIN   :READ FILE(SORTOUT) INTO (ORDER) ;
            LINES = LINES + 1 ;
            IF LINES > 23 THEN DO;
                                 PUT SKIP EDIT(SOMESTARS,ALLSTARS)
                                              (X(30),A,SKIP,X(30),A) ;
                                 SIGNAL ENDPAGE ;
                               END ;
          RTNNAME = NAME ;
            IF IGC = 'IGC' THEN DO ;
                                  SVCNO = SVC ;
                                  UNSPEC(OR)=UNSPEC(OR)×'11110000'B;
                                END ;
                    ELSE IF IGC='IGG' THEN SVCNO=SUBSTR(RTNNAME,4,3);
                    ELSE IF IGC='IEM' THEN SVCNO='PL1';
                    ELSE IF IGC='IER' THEN SVCNO='SRT';
                    ELSE IF IGC='IEF' THEN SVCNO='SCH';
                    ELSE IF IGC='IEE' THEN SVCNO='SCH';
                    ELSE IF IGC='IGE' THEN SVCNO='ERR';
                    ELSE IF IGC='IEU' THEN SVCNO='ASF';
                    ELSE IF IGC='IEY' THEN SVCNO='F4G';
                    ELSE IF IGC='IEB' THEN SVCNO='UTL';
                    ELSE IF IGC='IEH' THEN SVCNO='UTL';
                    ELSE IF IGC='IEW' THEN SVCNO='LNK';
                    ELSE IF IGC='IHE' THEN SVCNO='PL1';
                    ELSE IF IGC='IFC' THEN SVCNO='MCH';
                    ELSE IF IGC='DEV' THEN SVCNO='SCH';
                    ELSE IF IGC='IEK' THEN SVCNO='F4H';
                    ELSE IF IGC='GO ' THEN IF UNSPEC(LIB)&'00000001'B
                   THEN SVCNO='SCH';
                   ELSE SVCNO='EXC';
                           ELSE SVCNO = '' ;
            IF UNSPEC(LIB)&'00000010'B THEN LIBRARY = 'SVC' ;
              ELSE IF UNSPEC(LIB)&'00000001'B THEN LIBRARY='LINK' ;
              ELSE LIBRARY = 'PRIVATE' ;
          PUT SKIP EDIT(SOMESTARS,'*',COUNT,'  *  ',SVCNO,
                          '   * ',RTN, ' * ', LIBRARY ,'  *' )
                         (X(30), A, SKIP, X(30), A, F(8), 7 A ) ;
          IF LIBRARY='PRIVATE' THEN GO TO NOSEL;
          CALL BLDL(RTN,LIBRARY,TTR);
          IF TTR=0 THEN GO TO NOSEL;  SUBSTR (SELCD,16,8)=RTN;
          IF LIBRARY='LINK' THEN GO TO LNK;
          IF TTRS>TTR THEN WRITE FILE(PUN1) FROM(CCD);
          WRITE FILE(PUN1) FROM(SELCD);
          TTRS=TTR;
          GO TO NOSEL;
 LNK:     IF TTRL>TTR THEN WRITE FILE(PUN2) FROM(CCD);
          SUBSTR(EXCD,17,8)=RTN;
          WRITE FILE(PUN2) FROM(SELCD);
          WRITE FILE(PUN3) FROM(EXCD);
          TTRL=TTR;
 NOSEL    :;
          GO TO AGAIN ;
 EOJ     :CLOSE FILE(SORTOUT) ;
          PUT SKIP EDIT(SOMESTARS,ALLSTARS)(X(30),A,SKIP,X(30),A) ;
          PUT SKIP(3) EDIT('DURATION OF ANALYSIS =',HRS,' HRS',MINS,
                           ' MINS',SECS, '.', MSEC , ' SECS',ALLSTARS)
                          (X(28), A, F(3), A, F(3), A, F(3), A, F(2),
                                                    A, SKIP, X(30),A );
 RET     :RETURN ;
 SORTFAIL:PUT SKIP(3) EDIT('******* SORT FAILURE  --- RETURN CODE',
                              RETCD) ( A, F(3)) ;
          GO TO RET ;
          END ; /* CATCHPR */
