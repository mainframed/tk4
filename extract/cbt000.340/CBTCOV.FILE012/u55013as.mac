*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSU48XFR.
SSU48XFR ENTER ENT=SMFCOPY
         L     2,0(0,1)            GET PARM ADDR
         LH    3,0(0,2)            GET LENGTH
         LTR   3,3                 ANY PARM THERE?
         BZ    NOPARM              NO - OK
         CLC   2(4,2),=C'SORT'     IS IT OK?
         BNE   INVPARM             NO - TELL SOMEONE
         OI    SORTSW+1,X'F0'      TURN BRANCH ON
         CH    3,=H'6'
         BL    NOPARM
         CLI   6(2),C'='
         BE    OKEQU
         NI    SORTSW+1,X'0F'
         B     INVPARM
OKEQU    SH    3,=H'6'
         EX    3,PACKA
         CVB   3,WD
         ST    3,SORTSZ
         B     NOPARM
*
PACKA    PACK  WD(8),7(1,2)        EXECUTED INSTRUCTION
*
INVPARM  BCTR  3,0                 REDUCE COUNT FOR MOVE
         CH    3,=H'6'             MAXIMUM IS 6 BYTES
         BL    *+8
         LA    3,5                 PUT LENGTH OF 6
         STC   3,*+5               STORE LENGTH IN MVC
         MVC   MSG+13(1),2(2)      MOVE PARM TO MSG
MSG      WTO   'PARM=       IS INVALID - FILE ONLY COPIED',ROUTCDE=11
NOPARM   OPEN  (INFIL,,OUTFIL,(OUTPUT),PRTER,(OUTPUT))
         GETMAIN R,LV=2048         GET COUNTER AREA
         ST    1,IPCTRAD           SAVE ADDRESS
         LA    2,1024(0,1)            OF INPUT AND
         ST    2,OPCTRAD                   OUTPUT COUNTER AREAS
         INIT  (1),LV=2048
         SR    9,9                 CLEAR WORK REG
         L     10,IPCTRAD          ADDR. OF I/P COUNTERS
         L     11,OPCTRAD          ADDR. OF O/P COUNTERS
SORTSW   NOP   SORTLNK             SET FOR WEEKLY RUN
GETRECA  LA    15,E15
         BALR  14,15               GO GET A RECORD
         CH    15,=H'12'           ALL O.K?
         BNE   EOJ                 NO - MUST BE END-OF-FILE
         LR    2,1                 SAVE ADDR. OF RECORD
         IC    9,5(0,2)            GET RECORD TYPE
         SLL   9,2
         L     8,0(9,11)           LOAD COUNTER
         LA    8,1(0,8)            UPDATE IT
         ST    8,0(9,11)           RESTORE IT
         SR    9,9
         PUT   OUTFIL,(2)          WRITE THE RECORD
         B     GETRECA             GO GET ANOTHER
SORTLNK  LINK  EP=SORT,PARAM=SLIST,VL=1
EOJ      CLOSE (INFIL,,OUTFIL)
HEADING  PUT   PRTER,HEAD1
         PUT   PRTER,HEAD2
         PUT   PRTER,LINE
         MVC   LINES(4),=F'4'
         NOPR  12
         OI    *-1,X'F0'
         SR    2,2                 START AT RECTYPE 0
         SR    5,5
         SR    6,6                 TOTAL
         SR    7,7                    ACCUMULATORS
GETCOUNT L     3,0(5,10)           LOAD COUNT
         LTR   3,3                 ANY TO PRINT?
         BNZ   *+12                YES - OK
         OI    TESTX+1,X'F0'       TURN BRANCH ON
         B     OPTRY               GO TRY O/P COUNTER
         CVD   3,WD
         MVC   IPRECNT(8),EDIT
         ED    IPRECNT(8),WD+4
         AR    6,3                 ADD TO I/P TOTAL
OPTRY    L     3,0(5,11)           LOAD O/P COUNT
         LTR   3,3                 ANY THERE?
         BZ    TESTX               NO - PRINT THE LINE IF POSS.
         CVD   3,WD
         MVC   OPRECNT(8),EDIT
         ED    OPRECNT(8),WD+4
         AR    7,3                 ADD TO O/P TOTAL
TESTX    NOP   UPRTYP
PRTIT    CVD   2,WD
         MVC   IPRT(4),=X'40202120'
         ED    IPRT(4),WD+6
         PUT   PRTER,PRTA
         INIT  IPRT,LV=123,FILL=40
         L     4,LINES
         LA    4,2(0,4)
         ST    4,LINES
         CH    4,=H'60'
         BL    *+8
         BAL   12,HEADING
UPRTYP   LA    2,1(0,2)            UPDATE RECORD TYPE
         NI    TESTX+1,X'0F'       TURN SWITCH OFF AGAIN
         LR    5,2
         SLL   5,2
         CH    2,=H'256'
         BL    GETCOUNT
         PUT   PRTER,TOTLIN
         CVD   6,WD
         MVC   IPRECNT(8),EDIT
         ED    IPRECNT(8),WD+4
         CVD   7,WD
         MVC   OPRECNT(8),EDIT
         ED    OPRECNT(8),WD+4
         PUT   PRTER,PRTA
         L     1,IPCTRAD
         FREEMAIN R,LV=2048,A=(1)
         CLOSE (PRTER)
         LEAVE
         LTORG
         PUSH  USING
         DROP  13
         USING *,15
E15      SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         DROP  15
         LR    14,13
         BAL   13,*+76
         DS    18F
         ST    14,4(0,13)
         ST    13,8(0,14)
BONKIT   NOP   SECOND
         OI    *-3,X'F0'
GETIT    GET   INFIL
         LR    2,1
         ST    2,INNAD
         SR    3,3
         ICM   3,3,0(2)
         LA    4,4(0,2)            START OF FIRST RECORD
         LA    5,0(2,3)
         ST    5,BLKEND            ADDRESS OF END-OF-BLOCK
         CLI   2(4),0
         BNE   TESTSEG
TESTFLG  TM    FLAG,128
         BO    ERRPREV
RETA     ST    4,CURREC            SAVE THIS RECORD ADDRESS
         SR    9,9
         L     10,IPCTRAD
         IC    9,5(0,4)
         SLL   9,2
         L     11,0(9,10)
         LA    11,1(0,11)
         ST    11,0(9,10)
         CLI   5(4),X'04'
         BNE   GOBACK
         SR    9,9
         ICM   9,3,106(4)
         LA    10,107(0,4)
         AR    9,10
         TM    0(9),X'80'
         BNO   GOBACK
         XC    0(3,9),0(9)
GOBACK   EQU   *
         L     13,4(0,13)
         LM    14,0,12(13)
         LR    1,4
         LM    2,12,28(13)
         LA    15,12
         BR    14
SECOND   L     4,CURREC
         L     5,BLKEND
         L     2,INNAD
CALCIT   SR    6,6
         ICM   6,3,0(4)
         LA    4,0(6,4)
         CR    4,5
         BNL   GETIT
         CLI   2(4),0
         BE    TESTFLG
TESTSEG  CLI   2(4),1
         BNE   TRYFLG
         TM    FLAG,128
         BO    ERRPREV
         SR    9,9
         OI    FLAG,128
         LA    8,OUTAREA+4
MOVEIT   SR    7,7
         ICM   7,3,0(4)
         LA    4,4(0,4)
         SH    7,=H'4'
         MOVE  (8),(4),(7)
         AR    8,7
         AR    9,7
         SH    4,=H'4'
         B     CALCIT
TRYFLG   TM    FLAG,128
         BNO   ERRTHIS
         CLI   2(4),2
         BNE   MOVEIT
         NI    FLAG,X'7F'
         SR    7,7
         ICM   7,3,0(4)
         LA    4,4(0,4)
         SH    7,=H'4'
         MOVE  (8),(4),(7)
         AR    9,7
         LA    9,4(0,9)
         STH   9,OUTAREA
         MVC   OUTAREA+2(2),=H'0'
         SH    4,=H'4'
RETB     ST    4,CURREC
         LA    4,OUTAREA
         B     RETA+4
ERRPREV  LA    9,4(0,9)
         STH   9,OUTAREA
         MVC   OUTAREA+2(2),=H'0'
         MVC   OUTAREA+4(1),OUTAREA+5
         MVI   OUTAREA+5,X'FF'
         NI    FLAG,X'7F'
         B     RETB
ERRTHIS  MVC   2(2,4),=H'0'
         MVI   5(4),X'FF'
         B     RETA
EOFAD    TM    FLAG,128
         BNO   RETC
         LA    9,4(0,9)
         STH   9,OUTAREA
         MVC   OUTAREA+2(2),=H'0'
         MVC   OUTAREA+4(1),OUTAREA+5
         MVI   OUTAREA+5,X'FF'
         MVC   BONKIT+2(2),=S(RETC)
         B     RETB
RETC     LEAVE 8
         LTORG
INNAD    DS    F
BLKEND   DS    F
CURREC   DS    F
         DROP  12
         POP   USING
         PUSH  USING
         DROP  13
         USING *,15
E35      SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         DROP  15
         LR    14,13
         BAL   13,*+76
         DS    18F
         ST    14,4(0,13)
         ST    13,8(0,14)
         L     2,0(0,1)
         LTR   2,2
         BZ    RET8
         NOP   NFIRST
         OI    *-3,X'F0'
HOLDREC  MVC   COMPAR(18),0(2)
         SR    3,3
         IC    3,5(0,2)
         SLL   3,2
         L     4,OPCTRAD
         L     5,0(3,4)
         LA    5,1(0,5)
         ST    5,0(3,4)
         PUT   OUTFIL,(2)
RET4     LEAVE 4
NFIRST   CLC   0(18,2),COMPAR
         BE    RET4
         B     HOLDREC
RET8     LEAVE 8
         LTORG
COMPAR   DS    CL18
         DROP  12
         POP   USING
INFIL    DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,RECFM=U,LRECL=32767,    X
               BLKSIZE=32767,EODAD=EOFAD
OUTFIL   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM
PRTER    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133
IPCTRAD  DS    F
OPCTRAD  DS    F
         CNOP  2,4
SLIST    DC    H'28'
         DC    A(SREC)
         DC    A(SRECE)
         DC    A(RREC)
         DC    A(RRECE)
         DC    A(E15)
         DC    A(E35)
SORTSZ   DC    F'100000'
LINES    DS    F
HEAD1    DC    CL133'1          S.M.F. COPY UTILITY'
TOTLIN   DC    CL31'0'
         DC    CL31'----------'
         DC    CL71'----------'
HEAD2    DC    CL5'0'
         DC    CL12'RECORD TYPE'
         DC    CL10' '
         DC    CL20'INPUT RECORDS READ'
         DC    CL10' '
         DC    CL75'OUTPUT RECORDS WRITTEN'
LINE     DC    C' ',132C'-'
PRTA     DC    CL10'0'
IPRT     DC    CL22' '
IPRECNT  DC    CL31' '
OPRECNT  DC    CL69' '
WD       DS    D
EDIT     DC    XL8'4020202020202120'
FLAG     DC    X'00'
SREC     DC    C' SORT FIELDS=(11,4,PD,A,7,4,BI,A,6,1,BI,A)'
SRECE    DC    C' '
RREC     DC    C' RECORD TYPE=V,LENGTH=(1024,,,18,250)'
RRECE    DC    C' '
OUTAREA  DS    XL4096
         END
