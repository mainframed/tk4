         TITLE 'HASP HOTNEWS GENERATOR'
HOTNEWS  CSECT
*
         REQU
*
         SAVE  (14,12)
         BALR  R2,0                    ESTABLISH
         USING *,R2                        ADDRESSABILITY
         LA    R14,SAVER
         ST    R13,SAVER+4             STANDARD
         ST    R14,8(R13)                  LINKAGE
         LR    R13,R14                         CONVENTIONS
*
         L     R1,0(R1)                R1 -> PARM FIELD
         CLC   =H'2',0(R1)             LENGTH = 2???
         BNE   TESTING                 NO - ASSUME TEST
         CLC   =C'GO',2(R1)            PARM = 'GO'???
         BNE   TESTING                 NOPE - ASSUME TEST
         OI    SWITCH,GO               TURN ON GO SWITCH
         MVI   TESTING+8,X'0F'         SET SYSUT2 OPEN TYPE TO OUTPUT
         MVC   SYSUT1+DCBEODAD-IHADCB+1(3),=AL3(EOF)  CHANGE EODAD
*
TESTING  OPEN  (SYSUT1,(INPUT),SYSUT2,(INPUT),SYSPRINT,(OUTPUT))
*
         TM    SWITCH,GO               IS THIS THE REAL THING???
         BNO   READ                    NO...DON'T OUTPUT TITLE
         LH    R4,SYSUT2+DCBBLKSI-IHADCB GET HOTNEWS BLOCKSIZE
         GETMAIN R,LV=(4)
         LR    R5,R1                   SAVE BUFFER ADDRESS
         LA    R6,TLENGTH+6(R5)        POINT TO FIRST SLOT IN BUFFER
         MVC   4(TLENGTH+2,R5),TPREFIX MOVE TITLE TO BUFFER
         SPACE 1
**********************************************************************
*                                                                    *
*        NOTE : THE NEXT INSTRUCTION IS DEPENDANT ON THE LOCATION    *
*        OF SYS1.HOTNEWS ON THE FIRST SPOOL DISK                     *
*        THE LITERAL '=XL4' CONTAINS THE MTTR OF HOTNEWS AND         *
*        M U S T  BE CHANGED IF HOTNEWS IS REALLOCATED.              *
*        THE RECORD NUMBER 'R' MUST ALWAYS BE X'02'                  *
*                                      S P R SHANLY - DEC. 1972      *
*                                                                    *
**********************************************************************
         SPACE 1
         MVC   0(4,R5),=XL4'00004B02'  INITIALIZE NEXT TRK PTR
         SPACE 1
READ     GET   SYSUT1,LINE
         GET   SYSUT1,LINE+80
*
PUT      TM    SWITCH,GO               IS THIS THE REAL THING???
         BO    PUTA                    YES...GO GET 'EM
         MVI   LINECTL,C' '            SINGLE SPACE
         PUT   SYSPRINT,LINECTL
         B     READ                    LOOP BACK
*
PUTA     LA    R7,LINE+132             POINT TO END OF LINE + 1
         LA    R10,132                 MAXIMUM LENGTH OF LINE
NEXTCHAR BCTR  R7,0                    BACK UP 1 CHARACTER
         CLI   0(R7),C' '              IS IT A BLANK?
         BNE   NOTBLANK                NOPE...END OF SCAN
         BCT   R10,NEXTCHAR            LOOP BACK IF MORE TO GO
         LA    R10,1                   ALL BLANKS - FORCE COUNT OF 1
NOTBLANK LA    R7,1(R10)               SET R7 FOR MOVE
         STC   R10,LINEL               PLACE LENGTH IN RECORD
         LR    R8,R6                   GET PTR TO NEXT SLOT IN BUFFER
         LA    R8,2(R8,R10)            CALCULATE END OF RECORD
         SR    R8,R4                   SUBTRACT LENGTH OF BUFFER
         CR    R5,R8                   COMPARE WITH START OF BUFFER
         BNH   WRITEBUF                GO WRITE IT IF REC WON'T FIT
PUTB     EX    R7,MOVEX                MOVE RECORD INTO BUFFER
         LA    R6,2(R6,R10)            POINT TO NEXT SLOT IN BUFFER
         B     READ                    GO GET ANOTHER 2 CARDS
*
WRITEBUF MVI   0(R6),X'FF'             TERMINATE BUFFER
         PUT   SYSUT2,(5)
         SR    R11,R11                 ZERO FOR INSERT CHAR
         IC    R11,3(R5)               GET REC NO OF CHAIN ADDRESS
         LA    R11,1(R11)              INCREMENT
         STC   R11,3(R5)               STORE BACK IN CHAIN FIELD
         LA    R6,4(R5)                RESET PTR TO NEXT FREE SLOT
         B     PUTB                    RETURN TO CALLER
MOVEX    MVC   0(*-*,R6),LINEL         USED TO MOVE LINE TO BUFFER
*
EOF      MVI   0(R6),X'FF'             TERMINATE BUFFER
         XC    0(4,R5),0(R5)           ZERO CHAIN ADDRESS
         PUT   SYSUT2,(5)
CLOSE    EQU   *
         CLOSE (SYSUT1,,SYSUT2,,SYSPRINT)
*
         L     R13,SAVER+4             RESTORE REGISTER 13
         SR    R15,R15                 ZERO RETURN CODE
         RETURN (14,12),RC=(15)
         EJECT
SAVER    DC    18F'0'                  STANDARD O/S SAVE AREA
*
SWITCH   DC    X'00'                   SWITCH BYTE
GO       EQU   X'80'                   INDICATES PRODUCTION RUN
*
LINEL    DC    X'00'                   LINE LENGTH
LINECTL  DC    X'09'                   CARRIAGE CONTROL
LINE     DC    2CL80' '                CARD IMAGES
*
TPREFIX  DC    AL1(TLENGTH,25)
TITLE    DC    CL49' ',C'H A S P   B R O A D C A S T   L O G'
TLENGTH  EQU   *-TITLE
         SPACE 2
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GM,EODAD=CLOSE
         SPACE 2
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(GM,PM)
         SPACE 2
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FA,LRECL=133,   *
               BLKSIZE=133
         SPACE 2
         LTORG
         SPACE 2
         DCBD  DSORG=PS,DEVD=DA
         END
