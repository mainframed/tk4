XXX      CSECT
*
*    PASSON SETS PASSWORD BITS ON ON VTOC OF SPECIFIED VOLUME
*
*    JCL- SYSVTOC FOR THE VOLUME AS WITH IEH UTILITIES
*         SYSPRINT
*
*    MUST BE AUTHORISED
*
*     ABENDS   4095   ALLOCATION FAILED FOR PASSWORD DATA SET
*              4092   ERROR READING PASSWORD DATA SET
*              4      ERROR REWRITING VTOC
*
R1       EQU   1
R15      EQU   15
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         LR    11,13
         LA    13,SAVE
         ST     11,SAVE+4
         ST    13,8(11)
*
*    ALLOCATE PASSWORD DATA SET
*    OPEN IT AND VTOC
*
         LA    R1,S99RBPTR
         DYNALLOC
         LTR   R15,R15
         BZ    ALLOCOK
         MVI   S99RBPTR,0          ZERO VL BIT
         ST    R15,R15SAVE         SAVE RC
         LINK  EP=IKJEFF18,PARAM=(S99RBPTR,P2,P3,P4),VL=1
         LTR   R15,R15
         BZ    FINISH
         ABEND 4095,DUMP
ALLOCOK  OPEN  PASS
         OPEN (REPORT,OUTPUT)
         RDJFCB (IN)
         MVC   SERIAL,JFCB+118
         MVC   JFCB(44),FORMAT4
         OPEN  (IN),TYPE=J
         RDJFCB (OUT)
         MVC   JFCB(44),FORMAT4
         OPEN  (OUT,(OUTPUT)),TYPE=J
         L     R1,PASS+44
         MVC   SEEK1+1(6),36(R1)
         MVI   SEEK1+7,1
         MVI   IOBIN1,X'42'
         PUT   REPORT,LINE1
         ENQ   (SYSVTOC,SERIAL,E,6,SYSTEM)
*
**   READ PASSWORD DS AND VTOC TO FIND MATCH
**   UPDATE PW BITS IF NECESSARY
**   REPORT TO SYSPRINT
*
GET      READ  ECBA,SF,IN,DSCB,'S'
         CHECK ECBA
         CLI   DSCB+44,X'F1'
         BNE   GET
         MVC   DSN,DSCB            STORE DSN AS KEY
         TM    DSCB+83,X'08'       VSAM ?
         BO    VSAMDS              YES BRANCH
         MVI   ECBIN,X'00'         RESET ECB
         EXCP  IOBIN1               DO IO TO PASSWORD
         WAIT  ECB=ECBIN
         CLI   ECBIN,X'7F'         IO COMPLETE
         BE    NOERROR             YES  BRANCH
         CLI   ECBIN,X'42'         END OF EXTENT
         BE    GET                 YES, NO RECORD FOUND
         CLI   ECBIN,X'41'         PERM ERROR
         BNE   ABEND               YES  BRANCH
         TM    IOBIN1+12,X'01'      EOF
         BO    GET                 YES  NO PASSWORD FOUND
ABEND    ABEND 4092,DUMP
VSAMDS   EQU   *
         OI    DSCB+93,X'10'       PW READ&WRITE
         MVC   RECORD+44(8),=CL8'VSAM D.S'
         B     WRITE1
NOERROR  EQU   *
         TM    DSCB+93,X'14'       ANY PASSWORD BITS ON
         BNZ   WRITE2              YES  JUST REPORT
         OI    DSCB+93,X'14'       WRITE PROTECT  NOPWREAD
         MVI   PW+8,C' '           NO NOTE ON REPORT
         LA    R1,READTAB
READLOOP CLI   0(R1),C' '          END OF TABLE
         BE    WRITE1              YES  NOPWREAD
         CLC   0(12,R1),DSCB       DS IN READ PROTECT TABLE
         BE    READPROT            YES  GET READ PROTECT
         LA    R1,12(R1)
         B     READLOOP
READPROT NI    DSCB+93,255-X'04'   SET READ PROTECTION
         MVI   PW+8,C'*'           NOTE ON REPORT
WRITE1   EQU   *
         XDAP  ECBX,WI,OUT,DSCB,140,,IN+5
         WAIT  ECB=ECBX
         CLI   ECBX,X'7F'
         BE    WRITE2
         ABEND 4,DUMP
WRITE2   EQU   *
         MVC   PW,RECORD+44        STORE PASSWORD
         PUT   REPORT,LINE2
         B     GET
EOF      DEQ   (SYSVTOC,SERIAL,6,SYSTEM)
         CLOSE (IN,,OUT)
         CLOSE (PASS,,REPORT)
FINISH   EQU   *
         L     13,4(13)
         RETURN (14,12),RC=0
*
         DS    0D
SAVE     DS    18F
S99RBPTR DC    X'80'
         DC    AL3(S99RB)
P2       DC    A(R15SAVE)
P3       DC    F'0'
P4       DC    A(SWITCHES)
         SPACE 1
R15SAVE  DC    F'0'
S99RB    DC    X'14',X'01'    LENGTH,VERB(ALLOC)
         DC    X'F000'        FLAGS1
         DC    X'00000000'    ERROR CODES
         DC    A(TEXT)
         DC    F'0'
         DC   4X'00'          FLAG 2
TEXT     DC    A(TEXT1,TEXT2,TEXT3,TEXT4)
         DC    X'80'
         DC    AL3(TEXT5)
TEXT1    DC    X'0001',X'0001',X'0004',C'PASS'
TEXT2    DC    X'0002',X'0001',X'0008',C'PASSWORD'
TEXT3    DC    X'0004',X'0001',X'0001',X'08'
TEXT4    DC    X'0005',X'0001',X'0001',X'08'
         PRINT OFF
TEXT5    DC    X'0050',X'0001',X'0008',CL8'SILENCE'
         PRINT ON
SWITCHES DC    X'5033'
RECORD   DS    CL132
LINE1    DC    C'   SECURITY REPORT FOR VOLUME '
SERIAL   DC    C'      '
LINE2    DS    0CL80
DSN      DC    CL44' ',C'/'
PW       DC    CL8' ',CL27' '
HA       DC    5X'00'
SEARCH   CCW   X'1A',HA,X'40',5          SEARCH HOME ADDRESS
SEARCH1  CCW   X'92',COUNT,X'40',8       READ COUNT
         CCW   X'A9',DSN,X'60',44        SEARCH KEY EQ DSN
         CCW   X'08',SEARCH1,X'40',0     TIC TO READ COUNT
         CCW   X'1A',HA,X'40',5          START NEXT CCW AT TRACK START
SEARCH2  CCW   X'31',COUNT,X'40',5       SEARCH ID EQ
TIC      CCW   X'08',SEARCH2,X'40',0     TIC TO SEARCH
READKD   CCW   X'0E',RECORD,0,132        READ KEY & DATA
COUNT    DC    2F'0'
IOBIN1   DC    F'0',A(ECBIN),2F'0',A(SEARCH,PASS),4F'0'
SEEK1    EQU   IOBIN1+32
ECBIN    DC    F'0'
READTAB  EQU   *
         DC    CL12'PASSWORD'
         DC    CL12'SYS1.UADS'
         DC   6CL12' '
ENDTAB   DC    C' '
PASS     DCB   DDNAME=PASS,MACRF=E,DSORG=PS
REPORT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FB,             C
               LRECL=80,BLKSIZE=800
IN       DCB   DDNAME=SYSVTOC,LRECL=96,KEYLEN=44,BLKSIZE=96,           C
               EXLST=XLIST,EODAD=EOF,MACRF=(R),RECFM=F,DSORG=PS
OUT      DCB   DDNAME=SYSVTOC,MACRF=(E),EXLST=XLIST,DSORG=DA
         DS    0F
XLIST    DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
DSCB     DC    CL140' '
SYSVTOC  DC    C'SYSVTOC'
FORMAT4  DC    44X'04'
         END
