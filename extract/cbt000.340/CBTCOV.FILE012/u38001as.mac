IPLD     TITLE 'CHECK THAT IPL TIME AND DATE ARE WITHIN LIMITS'
**********************************************************************
**                                                                  **
** THIS PROGRAM WHEN EXECUTED AS PART OF THE HASP CATALOGUED        **
** PROCEDURE (OR AT SOME CONVENIENT TIME DURING IPL) CHECKS TO      **
** SEE IF THE DATE SET AT IPL IS GREATER THAN THE DATE SET AT THE   **
** LAST IPL. IF THE DATE IS GREATER THAN FOUR DAYS LATER OR IS      **
** BEFORE THEN A MESSAGE IS WRITTEN TO THE OPERATOR SAYING THAT     **
** THE DATE HAS BEEN ENTERED WRONGLY AND DISPLAYING THE DATE & TIME **
** AT THIS STAGE THE OPERATOR MAY RESET THE DATE & TIME AND REPLY   **
** RETRY AND IF ACCEPTABLE THE PROGRAM CONTINUES. IF THE DATE &     **
** TIME AS ENTERED ARE CORRECT THEN THE OPERATOR CAN REPLY GO       **
** AND THE PROGRAM WILL WRITE A MESSAGE ON THE CONSOLE AND          **
** CONTINUE, ACCEPTING THE 'INVALID' DATE.                          **
** NOTE THAT CODE MUST BE INSERTED AFTER LABEL 'ERROR' TO GET THE   **
** PROGRAMS PSW PROTECT KEY AS 0 FOR THE MGCR SVC 34 OTHERWISE      **
** THIS INSTRUCTION WILL ACT AS A NO-OP                             **
** SYS1.IPL.DATE & SYS1.IPL.TIME MUST BE CATALOGUED BEFORE THE      **
** FIRST RUN OF THIS PROGRAM                                        **
**                                                                  **
**********************************************************************
         PRINT NOGEN
IPLDATE  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         ENTRY IPLDATE
         STM   R14,R12,12(R13)
         BALR  R12,0
         USING *,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         SPACE 2
**
**    FIND TIME AND DATE NOW AND FIX UP NICELY
**
TRYAGAIN DS    0H                  MAY COME BACK HERE IF RETRY
         TIME  DEC                 GET TIME & DATE IN DECIMAL
         SRA   R0,4                REMOVE LAST HUNDRETH OF SEC
         STM   R0,R1,TODAY
         OI    TODAY+3,X'0F'       SET SIGN BIT IN TIME
         LA    R2,0                SET R2 TO 0
         ST    R2,DOUBLE           CLEAR DOUBLE
         ST    R1,DOUBLE+4         DECIMAL DATE
         OI    DOUBLE+7,X'0F'
         CVB   R0,DOUBLE
         ST    R0,TDATEB           BINARY DATE - WILL BE USEFUL
         UNPK  TDATEC(5),TDATED+1(3)
         OI    TDATEC+5,X'F0'
         UNPK  TTIMEC(6),TODAY(4)
         OI    TTIMEC+5,X'F0'
**
**   FIND LAST IPL DATE & TIME FROM CATALOGUE
**
GATE1    BC    0,ENDGT1            *** MODIFIED ***
         LOCATE INDATE             FIND OLD IPL DATE
         LTR   R15,R15
         BZ    NOERR1
         WTO   'TIP001E LOCATE ERROR ON SYS1.IPL.DATE'
         OC    VOLID1(6),=X'FFFFFFFFFFFF'
         B     ERROR
NOERR1   DS    0H
         OI    GATE1+1,X'F0'       *** MODIFY GATE1 ***
ENDGT1   DS    0H
         CLC   VOLID1(5),TDATEC       ARE DATES EQUAL ?
         BE    SAMEDAY             NO - BRANCH
         LA    R4,0
         ST    R4,DOUBLE
         PACK  DOUBLE,VOLID1(5)
         CVB   R5,DOUBLE
         LA    R4,0                R4/R5  OLD IPL DATE
         L     R7,TDATEB
         LA    R6,0                R6/R7  TODAY'S IPL DATE
         D     R4,=F'1000'         SPLIT YEAR & DAY
         D     R6,=F'1000'         SPLIT YEAR & DAY
         CR    R5,R7               COMPARE YEARS
         BH    ERROR               LAST IPL > THIS YEAR
         BNE   NEWYEAR
         SR    R6,R4               SUBTRACT DAYS
         BM    ERROR               LAST DAY > TODAY
         C     R6,=F'4'            ALLOW 4 DAYS LEEWAY
         BH    ERROR
         B     CONTINUE
**
**    PROCESS A NEW YEAR
**
NEWYEAR  DS    0H
         LA    R5,1(R5)            SHOULD BE THIS YEAR
         CR    R5,R7               SHOULD BE EQUAL NOW
         BNE   ERROR               MORE THAN ONE YEAR
         S     R4,=F'365'
         SR    R6,R4
         LPR   R6,R6
         C     R6,=F'10'           ALLOW 10 DAYS OVER CHRISTMAS
         BH    ERROR
         WTO   'TIP002I HAPPY NEW YEAR'
         WTO   'TIP002I HAPPY NEW YEAR'
         B     CONTINUE            ACCEPT A NEW YEAR
**
**    SAME DAY - CHECK TIME OF LAST IPL
**
SAMEDAY  DS    0H
GATE2    BC    0,ENDGT2            *** MODIFIED ***
         LOCATE INTIME
         LTR   R15,R15
         BZ    NOERR2
         WTO   'TIP003E LOCATE ERROR ON SYS1.IPL.TIME'
         OC    VOLID2(6),=X'FFFFFFFFFFFF'
NOERR2   DS    0H
         OI    GATE2+1,X'F0'
ENDGT2   DS    0H
         CLC   TTIMEC,VOLID2       CHECK TIMES
         BH    CONTINUE
**
**    ERROR PROCESSING
**
ERROR    DS    0H
         WTO   'TIP004E INVALID DATE OR TIME SET AT IPL'
**********************************************************************
** NOTE THAT CODE MUST BE INSERTED HER TO GET PROTECT KEY 0         **
** IF A DISPLAY OF THE CURRENT TIME & DATE ARE REQUIRED             **
**********************************************************************
         LA    R0,0
         LA    R1,DISTIME          DISPLAY TIME
         SVC   34
         WTO   'TIP005I IF DATE AND TIME ARE CORRECT TYPE ''GO'' TO OVE*
               RRIDE'
         WTO    'TIP006I IF DATE OR TIME ARE WRONG CORRECT THEM AND TYP*
               E ''RETRY'''
BADREPLY WTOR  'TIP007* TYPE ''GO'' OR ''RETRY''',REPLY,5,ECB
         WAIT  ECB=ECB
         LA    R1,0
         ST    R1,ECB
         OC    REPLY(5),=C'     '
         CLC   REPLY,=C'RETRY'
         BE    TRYAGAIN
         CLC   REPLY(2),=C'GO'
         BNE   BADREPLY
         WTO   'TIP008I ******************************************'
         WTO   'TIP008I * TIME AND/OR DATE CORRECTED BY OPERATOR *'
         WTO   'TIP008I ******************************************'
*
**  RECATALOGUE TIME & DATE
*
CONTINUE DS    0H
         CATALOG  RECTDATE
         LTR   R15,R15
         BZ    NCATER1
         WTO   'TIP010E CATALOG ERROR ON SYS1.IPL.DATE'
NCATER1  DS    0H
         CATALOG  RECTTIME
         LTR   R15,R15
         BZ    NCATER2
         WTO   'TIP011E CATALOG ERROR ON SYS1.IPL.TIME'
NCATER2  DS    0H
         L     R13,SAVE+4
         LM    R14,R12,12(R13)
         LA    R15,0
         BR    R14
         LTORG
         DS    0D
         DS    F
TODAY    DC    F'0'                TIME IN PACKED DECIMAL
TDATED   DC    F'0'                DATE IN PACKED DECIMAL
TDATEB   DC    F'0'                DATE IN BINARY
TVOLDATE DC    H'1'
         DC    X'30C02008'
TDATEC   DC    CL6'12345 '         DATE IN CHAR
         DC    H'0'
TVOLTIME DC    H'1'
         DC    X'30C02008'
TTIMEC   DC    CL6'987654'         TIME IN CHAR
         DC    H'0'
INDATE   CAMLST NAME,SYSDATE,,LOCAREA1
INTIME   CAMLST NAME,SYSTIME,,LOCAREA2
RECTDATE CAMLST RECAT,SYSDATE,,TVOLDATE
RECTTIME CAMLST RECAT,SYSTIME,,TVOLTIME
SYSDATE  DC    CL44'SYS1.IPL.DATE'
SYSTIME  DC    CL44'SYS1.IPL.TIME'
DOUBLE   DC    D'0'
LOCAREA1 DS    0D
         DC    265C'1'
VOLID1   EQU   LOCAREA1+6
LOCAREA2 DS    0D
         DC    265C'2'
VOLID2   EQU   LOCAREA2+6
         DS    0F
DISTIME  DC    AL2(8,0)
         DC    C'D T '
REPLY    DC    C'XXXXX'
ECB      DC    F'0'
SAVE     DC    18F'0'
         END
