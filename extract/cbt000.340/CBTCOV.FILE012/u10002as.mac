         TITLE 'VISUAL DISPLAY OF VTOCS'
* THIS PROGRAM PRODUCES A PICTORAL DISPLAY OF A VTOC OF A DIRECT
* ACCESS DEVICE (BUT NOT A DATA CELL).
*
* AT A GLANCE IT IS POSSIBLE TO SEE HOW EFFICIENTLY A DISK PACK IS
* BEING UTILIZED.
*
* SAMPLE JCL -
* //STEPNAME EXEC PGM=VTOCMAP
* //VTOCDD DD VOL=SER=DISKNO,UNIT=DISK,DISP=OLD
* //SYSPRINT DD SYSOUT=A
*
* ERROR MESSAGES INDICATING HOLES OR DUPLICATE REFERENCES ON THE PACK
* MAY BE GENERATED.
*
VTOCMAP  CSECT
         SAVE  (14,12)
         BALR  3,0
         USING *,3
         USING *+4000,12
         LA    12,4000(3)
         LA    4,SAVEAREA
         ST    13,SAVEAREA+4
         ST    4,8(13)
         LR    13,4
         TIME DEC
         ST    1,DATE
         ST    0,TIME
         MVI   TIME+3,X'0F'
         ED    EDDATE,DATE+1 SET DATE IN HDG1
         ED    EDTIME,TIME SET TIME IN HDG1
         MVI   EDTIME+9,C' '
         L     4,16                   POINTER TO CVT
         L     5,32(4)                POINTER TO IECPRLTV
         ST    5,IECPRLTV             STORE ADDR
         TITLE 'READ THE FORMAT 4 DSCB'
         RDJFCB (VTOC)
         MVC    HDG1+15(6),JFCB+118   STORE VOL SERIAL FOR PRINT
         MVI   JFCB,X'04'             SET UP VTOC DSN
         MVC    JFCB+1(43),JFCB      IN JFCB
         OPEN  (VTOC,(INPUT,DISP)),TYPE=J
         TM    VTOC+48,X'10' DID OPEN WORK
         BZ    ERROR1 ERROR - OPEN DIDNT
         L     4,VTOC+44              DEB POINTER
         ST    4,DEBADDR
         READ  DECB1,SF,VTOC,DSCBAREA
         CHECK DECB1
         CLI   DSCBAREA+44,C'4' TEST FOR FORMAT 4 DSCB
         BNE   ERROR3 ERROR - MUST HAVE FORMAT 4 DSCB HERE
         MVC   #CYL,DSCBAREA+62 NO OF CYLINDERS ON DEVICE
         MVC   #TRK,DSCBAREA+64 NO TRKS/CYL
         LH    4,#CYL PICK UP NO OF CYLS
         MH    4,#TRK CALC NO OF TRKS ON VOLUME
         ST    4,COREQ SET GETMAIN CORE REQUIREMENTS
         ST    4,COREQ+4
         GETMAIN VC,LA=COREQ,A=TABADD
         LTR   15,15 TEST RETURN CODE
         BNZ   ERROR5 ERROR - NOT ENOUGH CORE
         ST    4,TABLEN OVERRIDE GETMAIN LENGTH
         L     5,TABADD POINT AT START OF TABLE
CLEAR1   SH    4,=H'256' DEC BY LEN OF MAX XC
         BM    CLEAR2 NOT MAX MOVE
         XC    0(256,5),0(5) ZERO OUT 256 BYTES
         LA    5,256(5) POINT AT NEXT BYTE
         B     CLEAR1 GO PROCESS NEXT SEGMENT
CLEAR2   AH    4,=H'255' GET LEN LEFT LESS UNITY
         BM    CLEAR4 FINISHED
         EX    4,CLEAR3 ZERO OUT FINAL FRAGMENT
         B     CLEAR4 CONTINUE
CLEAR3   XC    0(0,5),0(5) ZERO OUT A FRAGMENT
CLEAR4   EQU   *
         MVI   PRINTCHR,C'V'           V FOR VTOC
         MVC   LOWLMT(8),DSCBAREA+107 STORE LOWER & UPPER LIMITS
         BAL   14,PROCEXT1            PROCESS EXTENT
         TITLE 'READ THE FORMAT 5 DSCBS'
         MVI   FIRSTIND,C'1'          FIRST FORMAT 5
READ5    READ  DECB2,SF,VTOC,DSCBAREA
         CHECK DECB2
         CLI   DSCBAREA+44,C'5' TEST FOR FORMAT 5 DSCB
         BNE   ERROR4 ERROR - MUST HAVE FORMAT 5 DSCB HERE
         MVI   PRINTCHR,C'.'           . FOR UNUSED SPACE
         MVC   DSCBAREA+44(90),DSCBAREA+45  MAKE EXTENTS CONTIGUOUS
         LA    8,26                   LOOP CONTROL
         LA    9,5                    INCREMENT
         LA    10,DSCBAREA+4           START OF FIRST EXTENT
         SPACE 2
***********************************************************************
         SPACE 2
LOOP5    CLC   0(2,10),=XL2'00'          IS THIS A ZERO EXTENT
         BE    NEXT5                  YES
         MVC   STARTTRK(2),0(10)      START TRK
         MVC   NOTRKS(2),2(10)        NO.CYLS
         MVC   HALF+1(1),4(10)        NO. TRKS
         MVI   HALF,X'00'
         LH    7,#TRK TRKS/CYL
         MH    7,NOTRKS               CYLS * #TRK = TRKS
         AH    7,HALF                 + ODD TRKS
         STH   7,NOTRKS
         BAL   14,PROCEXT2            PROCESS EXTENT
         AR    10,9                   POINT TO NEXT EXTENT
         BCT   8,LOOP5
NEXT5    CLC   DSCBAREA+135(5),=XL5'00' NON ZERO POINTER TO ANOTHER F5
         BNE   POINT5                 YES
         CLC   STADDR(4),=XL4'00'        IS THIS FIRST F5
         BE    READ                   YES
         POINT VTOC,STADDR            POINT TO REC FOLLOWING FIRST F5
         MVC   STADDR(4),=XL4'00'
         B     READ                   READ NEXT SEQ RECORD
         SPACE 2
***********************************************************************
         SPACE 2
POINT5   CLI   FIRSTIND,C'1'         FIRST FORMAT5 I.E. START OF CHAIN
         BNE   CONVERT5               NO
         NOTE VTOC                    FORMAT 5 ADDR
         LA    8,1
         AR    1,8                    Z=1 FOR REC FOLLOWING F5
         ST    1,STADDR               ADDR OF NEXT SEQ RECORD
         MVI   FIRSTIND,X'00'         RESET FIRST IND
         SPACE 2
***********************************************************************
         SPACE 2
CONVERT5 L     15,IECPRLTV            POINTER TO CONVERSION ROUTINE
         MVC    VTOC+8(5),DSCBAREA+135 DCBFDAD OF NEXT REC IN CHAIN
         LA    2,VTOC+5               DCBFDAD
         L     1,DEBADDR              POINTER TO VTOC DEB
         STM   9,13,SAVEREGS
         BALR  14,15                  CONVERT ADDR
         LM    9,13,SAVEREGS
         ST    0,PNTADDR
         POINT VTOC,PNTADDR           POINT TO NEXT FORMAT5
         B     READ5                  READ NEXT FORMAT5
         TITLE 'PROCESS THE FORMAT 1 DSCBS'
READ     READ  DECB3,SF,VTOC,DSCBAREA
         CHECK DECB3
         CLI   DSCBAREA+44,X'F1'
         BNE   READ                   NOT FORMAT1 READ NEXT SEQ RECORD
         SPACE 2
***********************************************************************
         SPACE 2
         NI    FLAGS,X'DF'             POSSIBLE FREE SPACE FLAG
         MVI   FIRSTIND,C'1'
         CLC   DSCBAREA(7),=C'SYSCTLG'
         BE    SYSDS
         CLC   DSCBAREA(5),=C'SYS1.'
         BE    SYSDS
         CLC   DSCBAREA(9),=C'AAAAAAAA.'
         BE    TEMPDS
         CLC   DSCBAREA(3),=C'SYS'
         BNE   DATADS
         CLC   DSCBAREA+8(2),=C'.T'
         BNE   DATADS
         CLI   DSCBAREA+18,C'P'
         BE    TEMPDS
         CLI   DSCBAREA+18,C'F'
         BE    TEMPDS
         CLI   DSCBAREA+18,C'V'
         BNE   DATADS
TEMPDS   MVI   PRINTCHR,C'T'
         B     PROCF1
SYSDS    MVI   PRINTCHR,C'S'
         B     PROCF1
DATADS   MVI   PRINTCHR,C'D'
         CLI   DSCBAREA+82,X'00'
         BE    FREESP                  DSORG HAS NOT BEEN SET UP
         TM    DSCBAREA+82,X'42'       40 = PS   02 = PO
         BZ    PROCF1                  NOT PS OR PO
         OI    FLAGS,X'20'             POSSIBLE FREE SPACE FLAG
         LA    5,1
         AH    5,DSCBAREA+98           TRACK OF LAST BLOCK WRITTEN
         STH   5,HIGH                  NO OF TRACKS USED
         SR    4,4
         STH   4,TOTTRKS               INITIALISE TOTAL TRACKS THIS DS
         CLC   DSCBAREA+98(3),=XL3'00' IS LAST BLOCK WRITTEN ZERO
         BNE   PROCF1                  NO
         NI    FLAGS,X'DF'             FILE HAS NOT BEEN OPENED
FREESP   MVI   PRINTCHR,C'F'           FREE SPACE ON USER DATA SET
         SPACE 2
***********************************************************************
         SPACE 2
PROCF1   LA    8,3                    LOOP CONTROL
         LA    9,10                   INCREMENT
         LA   10,DSCBAREA+105         START OF FIRST EXTENT
LOOP1    CLI   0(10),X'00'             ZERO EXTENT
         BE    TESTPTR1               YES
         MVC   LOWLMT(8),2(10)        STORE LOWER & UPPER LIMITS
         BAL   14,PROCEXT1            PROCESS EXTENTS
         AR    10,9                   POINT TO NEXT EXTENT
         BCT   8,LOOP1
TESTPTR1 CLC   DSCBAREA+135(5),=XL5'00' POINTER TO F2 OR F3
         BNE   POINT1                 YES
         CLC   STADDR(4),=XL4'00'       HAS F1 ADDR BEEN STORED
         BE    READ                   NO
         POINT VTOC,STADDR            POINT TO REC FOLLOWING LAST F1
         XC    STADDR,STADDR INDICATE POINTER USED
         B     READ
         SPACE 2
***********************************************************************
         SPACE 2
ENDVTOC  CLOSE (VTOC,DISP)
         B     C GO PRINT THE VTOC
         TITLE 'PROCESS THE FORMAT 2 AND 3 DSCBS'
POINT1   CLI   FIRSTIND,C'1'          IS THIS F1 I.E. START OF CHAIN
         BNE   CONVERT1               NO
         MVI   FIRSTIND,X'00'         RESET FIRST IND
         NOTE  VTOC                   FORMAT 1 ADDR
         LA    8,1
         AR    1,8                    Z=1 FOR REC FOLLOWING F1
         ST    1,STADDR               ADDR OF NEXT SEQ REC
         SPACE 2
***********************************************************************
         SPACE 2
CONVERT1 L     15,IECPRLTV            POINTER TO CONVERSION ROUTINE
         MVC   VTOC+8(5),DSCBAREA+135 DCBFDAD OF NEXT REC IN CHAIN
         LA    2,VTOC+5               DCBFDAD
         L     1,DEBADDR              POINTER TO VTOC DEB
         STM   9,13,SAVEREGS
         BALR  14,15                  CONVERT ADDR
         LM    9,13,SAVEREGS
         ST    0,PNTADDR
         SPACE 2
***********************************************************************
         SPACE 2
         POINT VTOC,PNTADDR           POINT TO F2 OR F3
         READ DECB4,SF,VTOC,DSCBAREA
         CHECK DECB4
         CLI   DSCBAREA+44,X'F2'      IS THIS A FORMAT 2
         BE    TESTPTR1               YES
         MVC   DSCBAREA+44(90),DSCBAREA+45 MAKE EXTENTS CONTIGUOUS
         LA    8,13                   LOOP CONTROL
         LA    9,10                   INCREMENT
         LA   10,DSCBAREA+4           START OF FIRST EXTENT
         B     LOOP1                  PROCESS EXTENTS
         TITLE 'PROCESS THE EXTENTS'
PROCEXT1 LH    4,#TRK TRKS/CYL
         MH    4,LOWCC
         AH    4,LOWHH
         STH   4,STARTTRK             LOWER LIMIT OF EXTENT IN TRACKS
         LH    4,#TRK TRKS/CYL
         MH    4,HYCC
         AH    4,HYHH                 UPPER LIMIT OF EXTENT IN TRACKS
         SH    4,STARTTRK
         AH    4,=H'1'
         STH   4,NOTRKS               NO OF TRACKS IN EXTENT
         TM    FLAGS,X'20'             DSORG=PS OR PO
         BZ    PROCEXT2                NOT A SEQUENTIAL DATA SET
         AH    4,TOTTRKS
         STH   4,TOTTRKS               NEW TOTAL TRACKS FOR THIS FILE
         SH    4,HIGH                  MINUS LAST BLOCK WRITTEN
         STH   4,FREETRKS              UNUSED TRACKS IN THIS EXTENT
         BL    PROCEXT2                LAST BLOCK > THIS EXTENT
         LH    5,NOTRKS
         SR    5,4
         STH   5,NOTRKS
         STH   5,USEDTRKS              USED TRACKS IN THIS EXTENT
         OI    FLAGS,X'10'             LAST BLOCK LIES IN THIS EXTENT
PROCEXT2 L     4,TABADD PICK UP ADDRESS OF START OF TABLE
         AH    4,STARTTRK
         SPACE 2
***********************************************************************
         SPACE 2
PROCEXT3 L     15,REFTRKS PICK UP NO TRKS REFERENCED SO FAR
         AH    15,NOTRKS INCLUDE THIS EXTENT
         ST    15,REFTRKS SET TO NEW VALUE
AGAIN    CLI   NOTRKS,X'00'           NOTRKS > 255
         BE    A                      NO
         LH    5,NOTRKS
         SH    5,=H'255'
         STH   5,NOTRKS               NOTRKS-255 (MAX)
         LA    5,254                  MAX-1
         B     B
A        LH    5,NOTRKS
         BCTR  5,0 DEC BY UNITY
         XC    NOTRKS,NOTRKS ZERO OUT
B        MVC   0(1,4),PRINTCHR
         LTR   5,5                    WAS THERE ONLY 1 TRACK
         BZ    RETURN                 YES
         BCTR  5,0 DEC BY UNITY
         EX    5,MOVE MOVE THE REST
         CLC   NOTRKS(2),=XL2'00'       ANY MORE
         BE    RETURN                 NO
         LA    4,255(4) UPDATE BASE
         B     AGAIN
         SPACE 2
***********************************************************************
         SPACE 2
RETURN   TM    FLAGS,X'10'             IS LAST BLOCK IN THIS EXTENT
         BZ    RET                     NO
         NI    FLAGS,X'CF'             TURN OFF BLK IN EXT AND ORG
         MVI   PRINTCHR,C'F'           FREE SPACE
         LH    5,FREETRKS              NO OF FREE TRACKS
         LTR   5,5                     ALL TRACKS ON THIS EXTENT USED?
         BZ    RET                     YES
         STH   5,NOTRKS                NO OF FREE TRACKS
         L     4,TABADD PICK UP ADDRESS OF START OF TABLE
         AH    4,STARTTRK
         AH    4,USEDTRKS              NEW START ADDRESS
         B     PROCEXT3 GO PROCESS THE FREE AREA
RET      BR    14                      RETURN
MOVE     MVC   1(1,4),0(4)            PROPOGATE PRINTCHR
         TITLE 'CONTROL THE LISTING'
C        OPEN  (PRINT,(OUTPUT,DISP))
         TM    PRINT+48,X'10' DID OPEN WORK
         BZ    ERROR2 ERROR - OPEN DIDNT
         L     4,TABADD PICK UP ADDRESS OF START OF TABLE
         ST    4,PRTADD SET PRINT RTN ADDRESS
         LH    8,#CYL
         A     4,TABLEN
         LH    15,#TRK
         BCTR  15,0
CA       SH    4,#TRK
         EX    15,CC
         BNE   CB
         SH    8,=H'1'
         BP    CA
         LA    8,1
CB       STH   8,#CYL
         SH    8,=H'100'
         STH   8,CYLEFT
         BNM   PPA
         MVC   INNERLP,#CYL
         B     PPA
CC       CLC   0(0,4),=256X'00'
G        LH    15,#CYL
         MH    15,#TRK
         C     15,REFTRKS COMPARE TO NO REFERENCED
         BE    GG ALL OK
         BL    GA
         PUT   PRINT,ERRLINE WRITE ERROR MESSAGE
         B     GG
GA       PUT   PRINT,ERRLINEA
GG       CLOSE (PRINT,DISP)
         FREEMAIN V,A=TABADD
         L     13,4(13)
         RETURN (14,12)
         TITLE 'DISPLAY THE VTOC'
PPA      BAL   2,PPC GO START NEW PAGE
         SPACE 2
***********************************************************************
         SPACE 2
D        BAL   2,PPB GO PRINT HEADINGS
         L     4,PRTADD PICK UP START ADD IN TABLE
         ST    4,WORD
         LA    5,PRNTAREA+16
         LH    7,#TRK MIDDLE LOOP CONTROL
         LH    8,INNERLP
         SR    10,10                  TRACK NO. =0 INITIALLY
         SPACE 2
***********************************************************************
         SPACE 2
E        MVC   0(1,5),0(4)            NEXT CHR
         AH    4,#TRK
         AH    5,=H'1'
         BCT   8,E
         CVD   10,BTRACK
         MVC   PRNTAREA+10(4),EDIT
         ED    PRNTAREA+10(4),BTRACK+6
         PUT   PRINT,PRNTAREA
         MVC   PRNTAREA+2(131),PRNTAREA+1
         AH    10,=H'1'               INCREMENT TRACK NO.
         L     4,WORD
         AH    4,=H'1'
         ST    4,WORD
         LA    5,PRNTAREA+16
         LH    8,INNERLP
         BCT   7,E
         SPACE 2
***********************************************************************
         SPACE 2
         LH    4,#TRK
         MH    4,=H'100'
         A     4,PRTADD
         ST    4,PRTADD SET START POSN IN TABLE
         LH    7,#TRK MIDDLE LOOP CONTROL
         LH    8,CYLEFT
         SH    8,=H'100'
         STH   8,CYLEFT
         BNM   F
         AH    8,=H'100'
         BNP   G
         STH   8,INNERLP
F        LH    15,#TRK
         CH    15,=H'22'
         BH    PPA
         XI    ALT,X'FF'
         BZ    PPA
         B     D
         TITLE 'PRINT SOME HEADINGS'
PPB      MVC   PRNTAREA+2(131),PRNTAREA+1 BLANK OUT THE PRINT AREA
         UNPK  HUNS,SEG
         AP    SEG,=P'1'
         OI    HUNS,C'0'
         MVC   HDG2+10(1),HUNS
         MVC   HDG2+17(1),HUNS
         MVC   PRNTAREA+56(20),HDG2
         MVI   PRNTAREA,X'60'
         PUT   PRINT,PRNTAREA
         MVC   HDG3(1),HUNS
         MVC   HDG3+10(1),HUNS
         MVC   HDG3+20(1),HUNS
         MVC   HDG3+30(1),HUNS
         MVC   HDG3+40(1),HUNS
         MVC   HDG3+50(1),HUNS
         MVC   HDG3+60(1),HUNS
         MVC   HDG3+70(1),HUNS
         MVC   HDG3+80(1),HUNS
         MVC   HDG3+90(1),HUNS
         MVC   HDG3+99(1),HUNS
         MVC   PRNTAREA+16(100),HDG3
         MVI   PRNTAREA,X'40'
         PUT   PRINT,PRNTAREA
         MVC   PRNTAREA+16(100),HDG4
         PUT   PRINT,PRNTAREA
         MVC   PRNTAREA+16(100),HDG5
         PUT   PRINT,PRNTAREA
         MVC   PRNTAREA+2(131),PRNTAREA+1
         BR    2 RETURN
         SPACE 2
***********************************************************************
         SPACE 2
PPC      MVI   PRNTAREA,X'F1'        HEAD OF FORM
         MVC   PRNTAREA+2(131),PRNTAREA+1
         MVC   PRNTAREA+46(47),HDG1
         PUT   PRINT,PRNTAREA
         MVC   PRNTAREA(2),=X'6040'
         MVC   PRNTAREA+2(131),PRNTAREA+1
         MVC   PRNTAREA+16(93),KEY1
         PUT   PRINT,PRNTAREA
         MVC   PRNTAREA+2(131),PRNTAREA+1
         MVI   PRNTAREA,X'40'
         MVC   PRNTAREA+21(70),KEY2
         PUT   PRINT,PRNTAREA
         BR    2 RETURN
         TITLE 'ERROR CONDITIONS'
ERROR1   WTO   'VTOCDD CANNOT BE OPENED'
         B     ABTERM GO ABEND
ERROR2   WTO   'SYSPRINT CANNOT BE OPENED'
         B     ABTERM GO ABEND
ERROR3   WTO   'FORMAT 4 DSCB MISPLACED'
         B     ABTERM GO ABEND
ERROR4   WTO   'FORMAT 5 DSCB MISPLACED'
         B     ABTERM GO ABEND
ERROR5   WTO   'INSUFFICIENT CORE'
         B     ABTERM GO ABEND
ABTERM   ABEND 4095
         TITLE 'CONSTANTS AND AREAS'
BTRACK   DC    D'0'
SAVEAREA DS    18F
JFCB     DS    44F
EXLIST   DC    0F'0',X'87',AL3(JFCB)
REFTRKS  DC    F'1'
IECPRLTV DS    1F
DEBADDR  DS    1F
DSCBAREA DS    35F
TIME     DS    1F
DATE     DS    1F
COREQ    DC    2F'0'
TABADD   DC    F'0'
TABLEN   DC    F'0'
PRTADD   DC    F'0'
LOWLMT   DS    0F
LOWCC    DS    CL2
LOWHH    DS    CL2
HYCC     DS    CL2
HYHH     DS    CL2
STADDR   DC    F'0'
PNTADDR  DC    F'0'
WORD     DS    1F
SAVEREGS DS    5F
STARTTRK DC    H'0'
NOTRKS   DC    H'0'
HALF     DC    H'0'
HIGH     DC    H'0'
TOTTRKS  DC    H'0'
FREETRKS DC    H'0'
USEDTRKS DC    H'0'
FLAGS    DC    X'00'
EDIT     DC    X'40202120'
HDG1     DC    C'MAP OF VTOC ON        DATE'
EDDATE   DC    X'402020202021'
         DC    C' TIME'
EDTIME   DC    X'4021204B20204B202020'
HDG2     DC    C'CYLINDERS 000 TO 099'
PRNTAREA DC    CL133' '
PRINTCHR DS    CL1
FIRSTIND DS    CL1
HDG3     DC    100C' '
HDG4     DC    C'0         1         2         3         4         5'
         DC    C'         6         7         8         9        9'
HDG5     DC    C'0         0         0         0         0         0'
         DC    C'         0         0         0         0        9'
KEY1     DC    C'KEY: V-SPACE OCCUPIED BY VTOC, .-FREE SPACE POINTED'
         DC    C' TO BY FORMAT 5 DSCBS, S-SYSTEM DATA SETS,'
KEY2     DC    C'T-TEMPORARY DATA SETS, D-USER DATA SETS'
         DC    C', F-FREE SPACE IN USER DATA SET'
ERRLINE  DC    CL133'0 ***** VOLUME CONTAINS UNREFERENCED TRACKS *****'
ERRLINEA DC    CL133'0 **** VOLUME CONTAINS DUPLICATED REFERENCES ****'
#CYL     DC    H'0'
#TRK     DC    H'0'
CYLEFT   DC    H'0'
INNERLP  DC    H'100'
ALT      DC    X'00'
SEG      DC    P'0'
HUNS     DC    C' '
         EJECT
VTOC     DCB   DSORG=PS,MACRF=RP,DDNAME=VTOCDD,BLKSIZE=96,KEYLEN=44,   C
               RECFM=FS,EXLST=EXLIST,EODAD=ENDVTOC
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FA,BLKSIZE=133, X
               LRECL=133
         END
