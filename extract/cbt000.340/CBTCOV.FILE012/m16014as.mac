         MACRO
&N       PARMSCAN  &LIST,&FLAGS=YES,&KEYS=YES,&FAIL=0
         COPY  HGLOBAL
         LCLA  &OPNO
         LCLC  &LAB
         LCLC  &FILLER,&ZEROS
         LCLA  &INPOS,&INTAB,&NO
         LCLA   &T,&L,&F,&IN
         LCLA  &KEYV(64),&FX,&ODISP
         LCLC  &FLAGV(64)
&ZEROS   SETC  '00000000'
.*
.*       EXEC PARM ANALYSIS SETUP
.*       FORMAT OF &LIST
.*             (OPTION1,KEY1=VALUE1,KEY2=OPTION2/OPTION3,..)
.*
&N       PASS  SM19PRM,(&BASIC.$PM)
&T       SETA  N'&LIST
         BASIC $PM
.LIMIT   AIF   (&T LE &L).TERM
&L       SETA  &L+1
&FILLER  SETC  ' '
&INTAB   SETA  K'&LIST(&L)
&LAB     SETC  ''
&FILLER  SETC  ' '
&INPOS   SETA  1
.CHARX   AIF   ('&LIST(&L)'(&INPOS,1) EQ '=').EQSET
         AIF   ('&LIST(&L)'(&INPOS,1) EQ '/').SLASHED
&LAB     SETC  '&LAB'.'&LIST(&L)'(&INPOS,1)
&INPOS   SETA  &INPOS+1
         AIF   (&INPOS LE &INTAB).CHARX
         AGO   .EQSET
.SLASHED AIF   (&INPOS EQ &INTAB).INVAL
         AIF   ('&FILLER' EQ ' ').INVAL
         AIF   ('&LAB' EQ '').INVAL
         AIF ('&LAB'(1,1) GE '0').INVAL
&FILLER  SETC  '/'
.EQSET   AIF   ('&LAB' EQ '').INVAL
         AIF   ('&FLAGS' NE 'YES').GOX
         AIF   (&FX GE 64).OFLOW
&FX      SETA  &FX+1
&FLAGV(&FX)    SETC '&LAB'
&F       SETA  &F*2
         AIF   ((&F LT 256) AND (&F NE 0)).GOX
&F       SETA  1
.GOX     DC    CL9'&FILLER&LAB'
&ODISP   SETA  &ODISP+9
         AIF   (&INPOS GT &INTAB).LIMX
&LAB     SETC  ''
&NO      SETA  &INTAB-&INPOS
&INPOS   SETA  &INPOS+1
         AIF   ('&FILLER' EQ '/').CHARX
&FILLER  SETC  '='
&IN      SETA  &INTAB-1
.TESTSH  AIF   ('&LIST(&L)'(&IN,1) EQ '/').CHARX
&IN      SETA  &IN-1
         AIF   (&IN GE &INPOS).TESTSH
&INTAB   SETA  &INPOS-1
.KEYTEST AIF  ('&KEYS' NE 'YES').GOKEY
&OPNO    SETA  &OPNO+1
         AIF   (&OPNO GT 64).OFLOW
&KEYV(&OPNO) SETA                      &ODISP+1
.GOKEY   AIF   (&NO EQ 0).GOX
&LAB     SETC  '&LIST(&L)'(&INPOS,&NO)
         AIF   ('&LAB'(1,1) LT '0').GOX
&NO      SETA  8-&NO
         AIF   (&NO EQ 0).GOX
&FILLER  SETC  '='.'&ZEROS'(1,&NO)
         AGO   .GOX
.LIMX    AIF   ('&FILLER' NE '/').LIMIT
&FILLER  SETC  '='
&LAB     SETC  ''
&NO      SETA  0
         AGO   .KEYTEST
.*       FLAG VALUE EXPANSION
.TERM    AIF   (&FX EQ 0).TKEY
*
*        FLAG BIT VALUES
*
.EXPNF   ANOP
&FLAGV(&FX)     EQU &F
&F       SETA  &F/2
&FX      SETA  &FX-1
         AIF   (&F NE 0).EXPNF
         AIF   (&FX EQ 0).TKEY
&F       SETA  128
         MNOTE &FAIL,'NEXT FLAG BYTE'
         AGO   .EXPNF
.*       KEY EXPANSIN
.TKEY    AIF   (&OPNO EQ 0).ZEND
*
*        KEY LOCATIONS
*
.EXPNK   ANOP
&FX      SETA  &FX+1
KEY&FX EQU   &BASIC.$PM+&KEYV(&FX)
         AIF   (&FX LT &OPNO).EXPNK
.ZEND    DC    X'FF' .                 TEMINATION CODE
&SYSECT  CSECT
 MEXIT
.INVAL   MNOTE 4,'&LIST(&L) - INVALID FORMAT'
         AGO .LIMIT
.OFLOW   MNOTE 4,'TOO MANY OPTIONS/KEYWORDS'
         MEND
