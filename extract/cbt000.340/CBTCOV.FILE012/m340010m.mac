./ ADD NAME=BEGIN
         MACRO
&NAME    BEGIN  &SAVE,&REGS=(14,12),&TCODE=,&BASE=12,&ID=*
         LCLA  &BASE2
         LCLC  &SAVE2
         AIF   ('&NAME' NE '').A1
SECT&SYSNDX CSECT DEFAULT CSECT NAME
         AGO   .A2
.A1      ANOP
&NAME    CSECT
.A2      ANOP
*        EQUREG
       EQUREG
         SAVE  &REGS,&TCODE,&ID
         AIF   (&BASE GE 3 AND &BASE LE 12).A3
         MNOTE 4,'INVALID BASE REGISTER . 12 SUBSTITUTED'
&BASE2   SETA  12
         AGO   .A4
.A3      ANOP
&BASE2   SETA  &BASE
.A4      BALR  &BASE2,0 ESTABLISH BASE REGISTER
         USING *,&BASE2
         AIF   ('&SAVE' NE '').A5
         CNOP  0,4
         B     *+76
SAVE&SYSNDX DC 18A(0)
&SAVE2   SETC  'SAVE&SYSNDX'
         AGO   .A6
.A5      ANOP
&SAVE2   SETC  '&SAVE'
.A6      ANOP
         ST    13,&SAVE2.+4 STORE CP'S SAA IN THIS HSA
         LR    15,13     REINDEX CP'S SAA
         LA    13,&SAVE2 INDEX THIS SA
         ST    13,8(,15)     STORE THIS SAA IN CP'S LSA
         MEND
./ ADD NAME=EQUREG
         MACRO
         EQUREG
         GBLB  &EQUREG
         AIF   (&EQUREG).ERROR
&EQUREG  SETB  1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEXIT
.ERROR   MNOTE *,'EQUREG MACRO ALREADY ISSUED'
         MEND
