ZZAAAA   BEGIN BR=(12),SAVE=SAVEAREA
*
** BUILD CONTROL CARD TABLE
*
         CALL  ZZAAAB,(CXT730,CXI001,CXT640)
*
** OPEN  PDS (DATA) RETURN BLOCKSIZE RECORD SIZE
*
         BAL   10,B5001
         CLC   SFD004+2(2),=XL2'0050'
         BE    BA16
         WTO   'ZZAAAA - RECORD LENGTH NOT = 80'
         B     BA90
*
** READ MEMBER NAMES FROM DIRECTORY AND BUILD A NAME TABLE
** WITH TTR POINTERS
*
BA16     EQU   *
         CALL  ZZAAAC,(CXTXXX)
*
** SET UP POINTERS FOR LOOP READING EACH MEMBER IN TURN
*
         LA    4,12                SET LENGTH
         LA    5,CXTXXX
         AH    5,=Y(13000)
         SR    5,4                 POINT TO LAST ENTRY
         LA    6,CXT640            POINT TO TABEL OF MEMBERS SELECTED
         LA    3,CXTXXX            POINT TO TABLE START
BA40     EQU   *
         CLI   CXT640,X'00'        Q- MEMBER SELECTION ACTIVE
         BE    BA20                A- NO,PROCESS ALL RECORDS
         CLI   0(6),X'FF'          Q- END OF TABLE
         BE    BA78                A- YES,GO CLOSE
BA42     EQU   *
         CLC   0(8,6),0(3)         Q- SELECT LOW = CURRENT MEMBER
         BE    BA20                A- YES,GO PROCESS
         BH    BA44                A- NO,SELECT  HIGH - IGNORE MEMBER
         CLC   8(8,6),0(3)         Q- SELECT HIGH = CURRENT MEMBER
         BNL   BA20                A- YES,OR HIGHER PROCESS THIS MEMBER
         LA    6,16(,6)            A- NO,POINT TO NEXT ENTRY
         B     BA40                CONTINUE CHECK
BA44     EQU   *
         LA    3,12(,3)            POINT TO NEXT MEMBER
         B     BA42                CHECK NEW CURRENT MEMBER
*
** READ A RECORD,IF A MEMBER NAME IS PRESENT
*
BA20     EQU   *
         CLI   0(3),X'FF'          Q-END OF MEMBER TABLE
         BE    BA80                A- YES,GO CLOSE FILES
BA22     BAL   10,B5001            A- NO,READ A RECORD
         BAL   10,B5101            GO COMPARE RECORD
         B     BA22                LOOP THROUGH MEMBER
BA30     EQU   *
         BXLE  3,4,BA40            Q- ANY MORE MEMBERS
         B     BA80                A- NO,END OF JOB
BA78     EQU   *
         LR    3,6                 POINT TO X'FF'
BA80     EQU   *
         BAL   10,B5101            GO CLOSE REPORT.
         MVI   SFD004+3,X'FF'
         BAL   10,B5001            CLOSE PDS.
BA90     EQU   *
         L     13,4(,13)           EXIT
         LM    14,12,12(13)
         BR    14
         EJECT
*
**  SUBROUTINE B5001 - READ A RECORD FROM PDS DATA
*                      R9  TO CONTAIN RECORD ADDRESS*1ST TIME THROUGH
*                          CONTAINS ZEROS AT EOF    *CONTAINS BLK/REC
*                      LINKAGE BY REGISTER 10       *LENGTH
*                      R3  POINTS TO MEMBER NAME  X'FF'S TO CLOSE
B5001    EQU   *
         CALL  ZZAAAD,(SFD004,(3)) READ A RECORD
         ICM   9,15,SFD004         Q- EOF A
         BZ    BA30                A- YES,GO TO EOF ROUTINE
         BR    10                  A- NO,RETURN
         SPACE 4
*
** SUBROUTINE  B5101 - COMPARE RECORD WITH TABLE OF KEYS
*                             R9  POINTS TO RECORD
*                             R3  POINTS TO MEMBER NAME
*                             LINKAGE VIA R10
*
B5101    EQU   *
         CALL  ZZAAAE,(CXT730,(9),(3),CXI001)
         BR    10
         SPACE 4
*
** CONSTANTS STORAGE AREAS AND LITERALS
*
SFD004   DS    F                   RECORD ADDRESS PDS DATA
SFD004A  DS    F                   BLOCKLENGTH  RECORD LENGTH
SAVEAREA DS    18F
CXI001   DC    X'00'               CONTROL CARD ERROR IND. X'00' NOERR
*                                                          X'FF' ERROR
CXT730   DC    1460X'FF'
CXT640   DC    X'00',639X'FF'
         LTORG
CXTXXX   DC    13000X'FF'
         END
