         TITLE  'SPY51 - CHANNEL/DEVICE MONITOR - REL 1.0'
******************************************************************
*** SPY51 WILL SHOW CHANNEL DEVICE ACTIVITY ON VS1 OVER A PERIOD *
* OF TWO MINUTES.                                                *
****************************************************************
* MACRO
         MACRO
&NAME    CUREG
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUREG MACRO:16/01/75'
         POP   PRINT
         ACTR  50
         LCLA  &X,&Y,&Z
*    GENERAL REGISTERS
.LPR     ANOP
R&X      EQU   &X
&X       SETA  &X+1
         AIF   (&X LT 16).LPR
*    CONTROL REGISTERS
.LPC     ANOP
C&Y      EQU  &Y
&Y       SETA  &Y+1
         AIF   (&Y LT 16).LPC
*    FLOATING POINT REGISTERS
.LPF     ANOP
F&Z      EQU   &Z
&Z       SETA  &Z+2
         AIF   (&Z LT 8).LPF
         MEND
         MACRO
&NAME    CUENTER &BASE=(R12),&RECURSE=,&RENT=,&FIRST=,&CSECT=,&PARM=
         LCLA  &AL0,&AL1,&AL2,&AL9,&AL12
         ACTR  45                                              DEC 75
&AL2     SETA  &SYSNDX
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUENTER MACRO:16/06/77'
         POP   PRINT
         LCLC  &CL0,&CL1,&CL2,&CL3,&CL12,&CL13,&CL50
         LCLC  &CL51,&CL52,&CL53
         GBLB  &BG15
         AIF   ('&NAME' EQ '').B98
         ENTRY &NAME
.B98     ANOP
&CL0     SETC  '12'
         AIF   ('&BASE'(1,1) EQ '(').B10  *VALIDATE &BASE PARAMETER
&CL0     SETC  '&BASE'
         AIF   ('&CL0' EQ 'NONE').B11
         AIF   ('&CL0'(1,1) NE 'R').B4B
&AL9     SETA  K'&CL0-1
&CL0     SETC  '&CL0'(2,&AL9)
.B4B     AIF   ((&CL0 GT 1) AND (&CL0 LT 13)).B11
.BERA    MNOTE 9,'&&BASES WRONG,MACRO ABANDONED'
         MEXIT
.B10     ANOP
&CL0     SETC  '&BASE(1)'
&AL0     SETA  1                        COUNT ALONG TO-BE-SCANNED BASES
.B13     ANOP
&AL1     SETA  1                       *COUNT ALONG PREVIOUSLY SCANNED
&CL12    SETC  '&BASE(&AL0)'
         AIF ('&CL12'(1,1) NE 'R').B12A
&AL9     SETA  K'&CL12-1
&CL12    SETC  '&CL12'(2,&AL9)
.B12A    ANOP
&AL12    SETA  &CL12
.B12     AIF    ((&AL12 LT 2) OR (&AL12 GT 12)).BERA
.B15     AIF   (&AL1 EQ &AL0).B14      *J OUT IF REACHED LATEST
&CL13    SETC  '&BASE(&AL1)'
         AIF   ('&CL13'(1,1) NE 'R').B15A
&AL9     SETA  K'&CL13-1
&CL13    SETC  '&CL13'(2,&AL9)
.B15A    AIF   (&CL12 EQ &CL13).BERA *ERROR IF DUPLICATED
&AL1     SETA  &AL1+1                  *STEP ALREADY SCANNED PTR
         AGO   .B15
.B14     ANOP
&AL0     SETA  &AL0+1                  *STEP SCAN POINTER
         AIF   ('&BASE(&AL0)' NE '').B13 *J IF NOT NULL
&AL9     SETA  N'&BASE
         AIF   (&AL0 LT &AL9).BERA *ERROR IF NULL ELEMENT
.B11    ANOP
.*   START GENERATING PROGRAM
         DS    0H
         BALR  R15,0
         USING *,R15
&CL1     SETC  '&CSECT'
         AIF   ('&CL1' NE '').B30
&CL1     SETC  '*'
.B30     ANOP
&NAME    SAVE  (14,12),,&CL1
.B32     ANOP
.B90A    BAL   &CL0,OCSVA&AL2
         DROP  R15
         USING *,&CL0
OCSVD&AL2 EQU  *         HOLD IMMEDIATELY AFTER USING STATEMENT
         AIF   (&BG15).NODATIM
         CUREG                          REGISTER MNEMNONICS
&CL51    SETC  '&NAME'
         AIF   (K'&CL51 LE 8).DT1
&CL51    SETC  '&CL51'(1,8)
         AGO   .DT2
.DT1     ANOP
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '&SYSECT'
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '????????'
.DT2     ANOP
&CL52    SETC  '&SYSDATE'(4,2)'.'.'&SYSDATE'(1,2)'.'.'&SYSDATE'(7,2)
&CL53    SETC  '&SYSTIME'(1,2)':'.'&SYSTIME'(4,2)':00'
CUVRSION DC    C'****VERSION/',CL8'&CL51',C'/&CL52./&CL53'
&BG15    SETB  (1)                                             DEC 75
.NODATIM ANOP
OCSAR&AL2 DS    18F
OCSVA&AL2 EQU  *
         AIF   ('&BASE' EQ '').B89
&AL9     SETA  N'&BASE
         AIF   (&AL9 LE 1).B89
&AL1     SETA  &AL9
&AL0     SETA  2
.B88     ANOP
&CL2     SETC  '&BASE(&AL0)'
&CL3     SETC  '&BASE(&AL0-1)'
         LA    &CL2,1
         LA    &CL2,4095(&CL2,&CL3)
         USING OCSVD&AL2+4096*(&AL0-1),&CL2
         AIF   (&AL0 EQ &AL1).B87
&AL0     SETA  &AL0+1
         AGO   .B88
.B89     ANOP
.B87     ANOP
         ST    R13,OCSAR&AL2+4
         LR    R15,R13
         LA    R13,OCSAR&AL2
         ST    R13,8(R15)
         MEXIT
.MEND    MEND
* MACRO
         MACRO
&NAME    CUEXIT   &RC=(15)
         GBLB  &BG15                                 DEC 75
         LCLA  &B,&W
         LCLC  &Z
&NAME    DS 0H
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUEXIT MACRO:16/06/77'
         POP   PRINT
         L      R13,4(13)
&Z         SETC  '&RC'
.B94     AIF   ('&Z'(1,2) NE '(R').B84
&B       SETA  K'&Z-2
&Z        SETC  '&Z'(1,1).'&Z'(3,&B)
.B84     ANOP
         AIF   ('&Z' EQ '(15)').B60
         AIF   ('&Z'(1,1) EQ '(').B61
         RETURN (14,12),RC=&Z
         AGO   .B62
.B61     ANOP
&W       SETA  K'&Z-2
&Z       SETC  '&Z'(2,&W)
         LR    R15,&Z
.B60     RETURN (14,12),RC=(15)
.B62     ANOP
         MEND
SPY51    CUENTER
         L     1,0(1)               GET PARM ADDRESS
         LA    1,128(1)             GET ECB ADDRESS
         POST  (1)                  LET COMMAND MODULE CONTINUE
         WTO   '** SPY UCB / CHANNEL UTILIZATION MONITOR **'
         L     5,NUMBER             LOAD NUMBER OF LOOPS ROUND
         L     11,16(0,0)           LOAD CVT FROM ABSLOC 16
         L     11,40(11)            GET UCB ADDRESS LIST PORTION
HOLY     LH    4,0(11)              IS THIS ADDRESS
         CH    4,=H'0'                        EQUAL TO ZERO
         BNE   NOHOLES              IF SO GET NEXT
UPTAB    LA    11,2(11)                        UCB ADDRESS
         B     HOLY                 BRANCH BACK FOR NEXT UCB
NOHOLES  CLI   18(4),X'20'          UCBTYPE = DISK?
         BNE   UPTAB                IF NOT LOOK AGAIN
         ST    4,STORE              STORE ADDRESS OF FIRST DISK UCB
         LA    6,AREA               LOAD STORAGE AREA
CHAIN1   CLI   2(4),X'FF'           END OF DISK UCBS?
         BE    GOODMAN              IF YES GET OUT FAST
         B     ESCAPE               NOW...
GOODMAN  CLI   18(4),X'20'          IF DISK KEEP GOING
         BE    ONYOUGO
         B     ESCAPE
ONYOUGO  CLC   28(6,4),=X'000000000000'   IS PACK MOUNTED ON DEVICE
         BE    AGAIN                      IF NOT MISS OUT THIS UCB
         ST    4,0(6)                     STORE UCB ADDRESS
         MVC   6(6,6),28(4)               MOVE VOL SER NO
         MVC   4(2,6),14(4)               MOVE DEVICE ADDRESS
         LA    6,20(6)                    UP STORAGE AREA ADDRESS
AGAIN     LA   4,64(4)                    UP UCB ADDRESS
         B   CHAIN1                       BRANCH BACK
ESCAPE   MVC   0(4,6),=X'FF00FF00'        SET MARKER AT END OF TABLE
ESCAPE1   LA    6,AREA                    RELOAD ADDRESS OF STORAGE AR
LOOPER   CLC   0(4,6),=X'FF00FF00'        ARE WE AT END
         BE   GETAWAY                     IF SO GET OUT
          L     4,0(6)                    LOAD UCB ADDRESS
         TM    6(4),X'80'                 IS DEVICE BUSY?
         BNO  NOTBUSY                     BRANCH IF NOT
         L     10,12(6)                   ADD 1
         LA    10,1(10)                        INTO
          ST   10,12(6)                            BUSY COUNTER
NOTBUSY   TM   6(4),X'08'                 IS CONTROL UNIT BUSY?
          BNO  NOTCTL                     BRANCH IF NOT
          L     10,16(6)                  ADD 1
          LA    10,1(10)                       TO
          ST   10,16(6)                          CONTROL BUSY COUNTER
NOTCTL   LA   6,20(6)                     GET NEXT UCB
         B     LOOPER                     BRANCH BACK
GETAWAY  EQU   *                          OUT WE COME
         MODESET MODE=SUP                 GET IN SUPERVISOR STATE
         TCH  X'0100'                     TEST CHANNEL 1
         BZ   NOTONE                      BRANCH IF NOT BUSY
         L    10,CHAN1                    ADD 1
         LA   10,1(10)                        TO
         ST   10,CHAN1                          CHANNEL 1 BUSY
NOTONE   TCH  X'0200'                     TRY THE SAME FO CHANNEL 2
         BZ   NOTTWO
         L    10,CHAN2
         LA   10,1(10)
         ST   10,CHAN2
NOTTWO   TCH   X'0000'                     TRY CHANNEL 0 FOR LUCK
         BZ   NOTZER
         L    10,CHAN0
         LA   10,1(10)
         ST   10,CHAN0
NOTZER   MODESET  KEY=NZERO,MODE=PROB
NOTTY    EQU  *
         STIMER  WAIT,BINTVL=BINTVL            WAIT FOR WHILE
         MVI   ECB1,X'00'                      RESET ECB
         BCT   5,ESCAPE1                       OFF WE GO AGAIN
         LA   6,AREA                           NOW TO PUT OUT INFO
         LA    7,WTOS                          LOAD WTO AREA
WTLOOP   CLC  0(4,6),=X'FF00FF00'              ARE WE AT END
         BE   WRITER                           WRITE OUT
         MVC  1(2,7),4(6)                      MOVE DEVICE TO WTO AREA
         MVC  6(6,7),6(6)                      MOVE VOL SER NO
         L    4,12(6)                          GET BUSY
         MH   4,=H'100'                          TOTAL
         SRDA   4,32(0)                             AS
         D    4,NUMBER                                PERCENTAGE
         CVD   5,DOUBLE                                  OF
         MVC   15(3,7),=X'202120'                          TOTAL
         ED    14(4,7),DOUBLE+6
         MVI   18(7),C'%'
         L    4,16(6)                   DO
         MH   4,=H'100'                   THE
         SRDA   4,32(0)                        SAME
         D    4,NUMBER                             FOR
         CVD    5,DOUBLE                              CONTROL
         MVC   25(3,7),=X'202120'                            UNIT
         ED    24(4,7),DOUBLE+6                                   BUSY
         MVI   28(7),C'%'
         LA    7,34(7)                 UP WTO ADDRESS AREA
         LA   6,20(6)                  UP UCB INFO AREA
         B    WTLOOP                   DO IT ALL AGAIN
WRITER   MVC   0(9,7),=C'CHANNEL 0'    MOVE CHANNEL 0 CONSTANT
         L    4,CHAN0                PUT
         MH   4,=H'100'                 OUT
         SRDA  4,32(0)                     CHANNEL
         D    4,NUMBER                            0
         CVD   5,DOUBLE                            BUSY
         MVC  15(3,7),=X'202120'                       PERCENT
         ED    14(4,7),DOUBLE+6
         MVI  18(7),C'%'
         LA    7,34(7)                  UP WTO AREA
         MVC   0(9,7),=C'CHANNEL 1'     DO SAME FOR CHANNEL 1
         L    4,CHAN1
         MH   4,=H'100'
         SRDA  4,32(0)
         D    4,NUMBER
         CVD   5,DOUBLE
         MVC  15(3,7),=X'202120'
         ED    14(4,7),DOUBLE+6
         MVI  18(7),C'%'
         LA    7,34(7)
         MVC   0(9,7),=C'CHANNEL 2'      AND CHANNEL 2
         L    4,CHAN2
         MH   4,=H'100'
         SRDA  4,32(0)
         D    4,NUMBER
         CVD   5,DOUBLE
         MVC  15(3,7),=X'202120'
         ED    14(4,7),DOUBLE+6
         MVI   18(7),C'%'
         LA    7,34(7)
         MVC  0(4,7),=C'****'
         LA   7,WTOA
LOOPX    CLC  0(4,7),=C'****'
         BE    ABEND
         MVC  PUTOUT+14(34),0(7)
         CLC  15(3,7),=C'  0'
         BE   ZERO1
PUTOUT   WTO   '**                                        **'
         CLC   0(3,7),=C'DEV'
         BE    ZERO1
         OI   FINISH+1,X'F0'
ZERO1    LA   7,34(7)
         B    LOOPX
ABEND    EQU   *
FINISH   NOP   FINT
         WTO  '**    NO DEVICES FOUND TO BE BUSY.        **'
FINT   CUEXIT  RC=0
NUMBER   DC   F'400'
BINTVL  DC   F'25'
ECB1     DC     F'0'
DOUBLE   DC   D'0'
FIN      EQU   *
CHAN1    DC   F'0'
CHAN2    DC   F'0'
CHAN0    DC   F'0'
STORE    DC   F'0'
AREA     DC  12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
         DC   12C' '
         DC   2F'0'
WTOA      DC    CL34'DEV   VOLSER   DEV BSY   CTL BSY'
WTOS     DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         DC    CL34'1                                '
         END
