         TITLE 'ABENDIT/SETRCD/JCLTEST'
*
* THIS PROGRAM ALLOWS THE TESTING AND SETTING OF RETURN CODES VIA JCL.
**** THIS PGM ESPECIALLY USEFUL WITH COND=ONLY,PARM='PAUSE,,JOB FAILED'
* TO EXECUTE CODE
*
* //STEPNAME EXEC PGM=ABENDIT,PARM='FIELD1,FIELD2,FIELD3'
* NO OTHER JCL IS REQUIRED.
*
*
* WHERE FIELD1 MAY BE EITHER 'PAUSE' OR OMMITTED.
*              IF PAUSE THEN THE PGM ISSUES A WTOR MACRO WITH FIELD3 AS
*              THE MESSAGE AND PADDED OUT TO 120 CHARS WITH *'S, AND
*              THEN THE PGM WAITS FOR THE REPLY 'OK'  FIELD2 IS ACTED
*              UPON NORMALLY
*              IF PAUSE OMMITTED THEN NO SPECIAL ACTION IS TAKEN.
*
*
*       FIELD2 CODE AS 'UNNNN' OR 'NNNN' (LEADING ZEROES NOT REQUIRED).
*              THIS IS THE LAST THING DONE BY THE PGM.
*              IF 'UNNNN' CODED AN ABEND OCCURRS (NO DUMP) WITH ABEND
*              CODE OF 'UNNNN'.
*              IF 'NNNN' CODED THEN 'NNNN' IS THE RETURN CODE SET BY
*              THIS PGM.
*              IF FIELD2 OMMITTED A RETURN CODE OF 0 IS SET.
*
*
*       FIELD3 IS THE MESSAGE FIELD.
*              IF PAUSE CODED (SEE FIELD1) THIS IS THE WTOR MESSAGE.
*              IF PAUSE NOT CODED THE THIS IS A WTO MESSAGE JUST BEFORE
*              CAUSING THE ABEND OR SETTING THE RETURN CODE.
*
*
*       DEFAULTS ARE NO MESSAGE, ZERO RETURN CODE AND NO PAUSE
*
*
*       EXAMPLES OF USE
*              PARM='PAUSE,4,RETCDE OF 4 AND PAUSE'
*              PARM='U16' ABEND 'U0016'
*              PARM=',,RET CDE 0 AND THIS MESSAGE'
*
ABENDIT  START
*      AUTHOR - A.G.SIBBALD - C.A.V. LIMITED
         SAVE  (14,12)
         BAL   R15,80(R15) BRANCH ROUND SAVE AREA & PICK UP ITS ADD
         USING *,13 BASE REGISTER
         DC    18F'0' SAVE AREA
         ST    R15,8(R13) SET NEW SAVE AREA ADD IN OLD
         ST    R13,4(R15) SET OLD SAVE AREA ADD IN NEW
         LR    R13,R15 SWITCH TO NEW SAVE AREA
         L     R3,0(R1) PICK UP EXEC PARM ADDRESS
         LH    R4,0(R3) PICK UP PARM LEN
         LA    R3,2(R3) INC TO DATA POSN
         LTR   R4,R4 TEST LEN
         BNP   FA10 PARMS OMITTED
         CH    R4,=H'2' TEST LEN
         BL    AA10 CANT BE /*
         CLC   0(2,R3),=C'/*'
         BE    FA10 PARMS OMITTED
* USE OF REGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3 CURRENT ADDRESS IN PARM AREA
R4       EQU   4 CURRENT LENGTH OF PARM AREA
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13 BASE REG
R14      EQU   14
R15      EQU   15
         TITLE 'EXTRACT PAUSE PARAMETER'
         CH    R4,=H'5' CHECK LENGTH
         BL    AA10 PAUSE DEFINITELY NOT PRESENT
         CLC   0(5,R3),=C'PAUSE' IS IT A PAUSE REQUEST
         BNE   AA10 PAUSE NOT PRESENT
         OI    CTLSW,X'01' REQUEST PAUSE
         LA    R3,5(R3) SKIP PAST PAUSE
         SH    R4,=H'5' DECREASE LENGTH
         BNP   EA10 ERROR - RUN OUT OF PARMS
         CLI   0(R3),C',' TEST FOR COMMA
         BNE   EA10 ERROR - PAUSE MUST BE FOLLOWED BY A COMMA
         LA    R3,1(R3) SKIP PAST COMMA
         BCT   R4,AA40 ADJUST LENGTH
         B     EA10 ERROR - PAUSE REQUIRES AT LEAST A MESSAGE
         TITLE 'DETERMINE PRESENCE OF LAST 2 PARMS'
AA10     CLI   0(R3),C',' TEST FOR COMMA
         BNE   BA10 MUST BE ABEND/RETURN CODE
         LA    R3,1(R3) SKIP PAST 1ST COMMA
AA30     BCT   R4,AA40 TEST LEN
         B     EA20 ERROR - TRAILING COMMA
AA40     CLI   0(R3),C','
         BNE   BA10 MUST HAVE ABEND/RETURN CODE
         LA    R3,1(R3) SKIP PAST 2ND COMMA
         BCT   R4,DA10 ADJUST LENGTH AND GO EXTRACT MESSAGE
         B     EA20 ERROR - TRAILING COMMA
         TITLE 'EXTRACT ABEND/RETURN CODE PARMS'
BA10     CLI   0(R3),C'U' TEST IF ABEND REQUIRED
         BNE   BA20 NO - MUST BE RETURN CODE
         OI    CTLSW,X'02' REQUEST ABEND
         LA    R3,1(R3) SKIP PAST U
         BCT   R4,BA20 ADJUST LEN
         B     EA20 ERROR - NO CODE
BA20     CLI   0(R3),C'0' TEST 1ST DIGIT
         BL    EA20 ERROR - MUST HAVE 1 DIGIT
         CLI   0(R3),C'9'
         BH    EA20 ERROR
         MVC   WORK4(1),0(R3) SET 1ST DIGIT
         LA    R3,1(R3)
         BCT   R4,BA30
         B     CA10 END OF PARMS
BA30     CLI   0(R3),C'0' TEST 2ND DIGIT
         BL    BA60 MUST BE COMMA
         CLI   0(R3),C'9'
         BH    EA20 ERROR
         MVC   WORK4+1(1),0(R3) SET 2ND DIGIT
         LA    R3,1(R3)
         BCT   R4,BA40
         B     CA10 END OF PARMS
BA40     CLI   0(R3),C'0' TEST 3RD DIGIT
         BL    BA60 MUST BE COMMA
         CLI   0(R3),C'9'
         BH    EA20 ERROR
         MVC   WORK4+2(1),0(R3) SET 3RD DIGIT
         LA    R3,1(R3)
         BCT   R4,BA50
         B     CA10 END OF PARMS
BA50     CLI   0(R3),C'0' TEST 4TH DIGIT
         BL    BA60
         CLI   0(R3),C'9'
         BH    EA20 ERROR
         MVC   WORK4+3(1),0(R3) SET 4TH DIGIT
         LA    R3,1(R3)
         BCT   R4,BA60 MUST HAVE COMMA
         B     CA10 GO CONVERT ABEND/RETURN CODE
BA60     CLI   0(R3),C','
         BNE   EA20 ERROR - MUST HAVE COMMA
         LA    R3,1(R3)
         BCT   R4,CA10 GO CONVERT ABEND/RETURN CODE IF OK
         B     EA20 ERROR - TRAILING COMMA
         TITLE 'CONVERT ABEND/RETURN CODE'
CA10     CLI   WORK4+3,C' ' IS CODE RIGHT ADJUSTED
         BNE   CA20 YES - GO CONVERT
         MVC   WORK4+3(1),WORK4+2 RIGHT ADJUST BY 1 POSITION
         MVC   WORK4+2(1),WORK4+1
         MVC   WORK4+1(1),WORK4
         MVI   WORK4,C'0' MOVE IN LEADING ZERO
         B     CA10 TRY AGAIN
CA20     PACK  DOUBLEWD,WORK4(4)
         CVB   R15,DOUBLEWD
         LTR   R15,R15 TEST CODE
         BNZ   CA30 NOT ZERO
         NI    CTLSW,X'FD' NO LONGER WANT ABEND IF CODE IS ZERO
         B     CA40
CA30     CH    R15,=H'4096' CHECK MAX VALUE OF CODE
         BL    CA40 CODE CANT BE ABOVE 4095
         LA    R15,4095 USE MAX VALUE
CA40     STH   R15,RCDE SET RETURN CODE
         B     DA10 GO EXTRACT MESSAGE
         TITLE 'EXTRACT MESSAGE FIELD'
DA10     LTR   R4,R4 TEST LEN
         BNP   FA10 MESSAGE NOT PRESENT
         CLI   0(R3),C'$' IS 1ST CHAR OF MESSAGE A WTL REQUEST
         BNE   DA15 WTO REQUEST
         OI    CTLSW,X'08' REQUEST WTL
         LA    R3,1(R3) SKIP PAST
         BCT   R4,DA20 ADJUST LEN
         B     EA20 ERROR - TRAILING $
DA15     OI    CTLSW,X'04' REQUEST WTO
DA20     CH    R4,=H'98' TEST MAX LEN OF MESSAGE
         BH    EA20 ERROR - IMPOSSIBLE LEN
         STH   R4,MSGLEN SAVE MESSAGE LEN
         BCTR  R4,0 DEC LEN FOR EX INSTN
         EX    R4,DA30 MOVE MESSAGE TO MESSAGE AREA
         B     FA10 GO ACT ON PARMS
DA30     MVC   MSGDATA(0),0(R3) MOVE MESSAGE TO WTO/WTL AREA
         TITLE 'ERROR CONDITIONS'
EA10     MVC   MSGDATA(15),=C'NO REASON GIVEN'
         MVC   MSGLEN,=H'15' SET MESSAGE LENGTH
         OI    CTLSW,X'04' REQUEST MESSAGE BE WRITTEN
         B     FA10 GO ACT ON THE ABOVE
EA20     MVC   MSGDATA(20),=C'UNDECIPHERABLE PARMS'
         MVC   MSGLEN,=H'20' SET MESSAGE LENGTH
         OI    CTLSW,X'06' REQUEST ABEND AND MESSAGE
         LA    R15,4095 GET MAX VALUE OF CODE
         STH   R15,RCDE SET TO MAX VALUE
         B     FA10 GO ACT ON THE ABOVE
         TITLE 'WTO OR WTL'
FA10     TM    CTLSW,X'01'
         BO    GA10 PAUSE REQUESTED
         TM    CTLSW,X'04'
         BO    FA20 WTO REQUESTED
         TM    CTLSW,X'08'
         BZ    HA10 MESSAGE NOT REQUIRED
         LH    R4,MSGLEN PICK UP LEN OF MESSAGE
         LA    R4,4(R4) INCLUDE WTL CONTROL BYTES
         STH   R4,MSGLEN SET TO NEW VALUE
         LA    R1,MSGWTL SET PARM REG
         SVC   36 ISSUE WTL SVC
         B     HA10 GO RETURN
FA20     LH    R4,MSGLEN PICK UP LEN OF MESSAGE
         LA    R4,4(R4) INCLUDE WTO CONTROL BYTES
         STH   R4,MSGLEN SET TO NEW VALUE
         LA    R1,MSGWTO SET PARM REG
         SVC   35 ISSUE WTO SVC
         B     HA10 GO RETURN
         TITLE 'PAUSE REQUESTED'
GA10     TM    CTLSW,X'0C'
         BZ    EA10 ERROR - NO PAUSE MESSAGE PRESENT
         MVC   PMSGDATA,MSGDATA SET PAUSE MESSAGE
GA20     XC    PAUSEECB,PAUSEECB ZERO OUT ECB
         MVC   REPLY(2),=CL2' '
         LA    R1,MSGPAUSE SET PARM REG
         SVC   35 ISSUE WTOR SVC
         WAIT  ECB=PAUSEECB
         CLC   REPLY(2),=C'OK' CORRECT REPLY
         BE    HA10 YES - CONTINUE
         B     GA20 NO - GO TRY AGAIN
         TITLE 'TERMINATE PROGRAM'
HA10     TM    CTLSW,X'02'
         BO    HA20 ABEND REQUESTED
         LH    R15,RCDE PICK UP RETURN CODE
         L     R13,4(R13) PREPARE TO RETURN
         RETURN (14,12),RC=(15)
HA20     LH    R1,RCDE PICK UP ABEND CODE IN PARM REG
         SVC   13 ISSUE ABEND SVC
         TITLE 'CONSTANTS AND AREAS'
DOUBLEWD DS    D
PAUSEECB DC    F'0'
WORK4    DC    CL4' '
RCDE     DC    H'0'
CTLSW    DC    X'00'
REPLY    DC    CL2' '
MSGWTO   DS    0F
MSGWTL   DS    0H
MSGLEN   DC    H'10',H'0'
MSGDATA  DC    100C'*'
MSGPAUSE DC    0F'0',X'02',AL3(REPLY),A(PAUSEECB),H'112',H'0'
         DC    C'REPLY OK TO THIS MESSAGE TO CONTINUE - '
PMSGDATA DC    CL100' '
         END
