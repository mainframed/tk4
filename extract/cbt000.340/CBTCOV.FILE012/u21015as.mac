         TITLE ' SUBRTN ZAJFEOV AND FORCES END OF VOLUME ON A FILE'
ZAJFEOV  CSECT
**    AUTHORS - ICI (MOND DIVISION)
         SAVE  (14,12),,*          SAVE REGISTERS
         USING *,11
         ST    13,SAVEFEOV+4       FORWARD AND BACKWARD SAVING
         LA    15,SAVEFEOV
         ST    15,8(13)
         LR    13,15
         SPACE 2
*        THE   NEXT RTN OBTAINS THE ADDRESS OF THE DCB FROM THE PARAM
*        PASSED FROM PL/1. THE CALLING INSTRUCTIONS FROM PL/1 ARE AS
*        FOLLOWS- CALL ZAJFEOV (FILENAME); -THE PARAMETER LIST ADDR IS
*        PASSED IN REG 1 AS NORMAL.THE FULLWORD IN THE PARAMETER LIST
*        CONTAINS THE ADDR OF THE DECLARE CONTROL BLOCK (DCLCB) FOR THE
*        FILE. THE 1ST HALFWORD IN THE DCLCB CONTAINS THE RELATIVE ADDR
*        OF THE PSEUDO REG FOR THE FILE,AND THE BASE ADDR OF THE PSEUDO
*        REG LIST IS CONTAINED IN REG 12. THE CONTENTS OF THE PSEUDO
*        REG FOR THE FILE IS THE ADDR OF THE FILE CONTROL BLOCK (FCB).
*        THE 1ST FULLWORD OF THE FCB CONTAIN THE ADDR OF THE DCB.
         SPACE 2
         L     3,0(1)              ADDR OF DCLCB IN REG 3
         LH    3,0(3)              RELATIVE ADDR OF PSEUDO REG
         AR    3,12                BASE ADDR OF PSEUDO REG LIST
         L     3,0(3)              ADDR OF FILE CONTROL BLOCK
         L     3,0(3)              ADDR OF DCB
         SPACE 2
*        NOW  FORCE END OF VOLUME FOR THE FILE USING FEOV MACRO
         SPACE 2
         FEOV  (3)
         SPACE 2
*        RETURN TO CALLING PROGRAM FIRST RESTORING REGISTERS
         L     13,SAVEFEOV+4
         RETURN (14,12),,RC=0
         SPACE 2
SAVEFEOV DS    9D                  SAVEAREA
         LTORG
         END
