PUNCHCAT CSECT
*  THIS PROGRAM PROVIDES AN IMPROVED LISTING OF A SYSCTLG IN THAT IT..
*        1     PRINTS ONE CONTINUOUS LISTING OF ALL ENTRIES IN
*              COLLATING SEQUENCE.
*        2     IDENTIFIES EACH TYPE OF ENTRY, GIVING THE ORIGINAL
*              BLDG SPECIFICATION FOR GEN DATA GROUPS.
*        3     DIFFERENTIATES DSNAMES ENDING GNNNNV00 FROM GEN DATA
*              GROUP ENTRIES (USUALLY THESE HAVE BEEN WRONGLY CATLGD)
*        4     PRINTS EMPTY INDEXES
*        5     IDENTIFIES VOLUME TYPES AS E.G. 2314 RATHER THAN
*              GIVING THE HEX DEVICE TYPE CODE.
*        6     INDICATES THE AMOUNT OF UNUSED SPACE REMAINING IN THE
*              CATALOGUE.
*        7     PROVIDES A SHORTER LISTING FOR MULTI-VOLUME DATA SETS
*              BY PLACING UP TO SIX VOLUMES ON ONE LINE.
*
*  THE FOLLOWING JCL IS REQUIRED FOR EXECUTION......
*
*  //SYSCAT DD DSN=SYSCTLG,DISP=SHR,VOL=VOLUME,UNIT=UNIT
*  //SYSPRINT DD SYSOUT=CLASS,DCB=BLKSIZE=1463
*
*  NOTE THAT LRECL=133,RECFM=FBM ARE CODED IN THE DCB FOR SYSPRINT BUT
*  THE BLKSIZE MUST BE GIVEN ON THE DD CARD.
*
*        BEGIN
*  EXPANSION OF RESPOND TSG MACRO FOLLOWS
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
*  END OF MACRO EXPANSION
         L     1,16
         L     1,28(1)
         ST    1,CONVERT     PCNVT ADDRESS FROM CVT
         LA    2,DISKAD
         LA    3,IN+24
         LA    4,LNAME
         STM   3,4,DISKAD+8
         OPEN  (SYSCAT,,LIST,(OUTPUT))
         TIME
         ST    0,TIME
         MVI   TIME+3,12
         ST    1,DATE
         ED    HDRTIME,TIME
         ED    HDRDATE,DATE+1
         L     1,16
         L     1,0(1)
         L     1,4(1)
         L     1,12(1)
         AH    1,SYSCAT+40
         L     1,16(1)
         MVC   HDRVOLNO,28(1)
         L     1,SYSCAT+44
         MVC   CC(4),38(1)
         MVC   DISKAD+3(4),38(1)
         MVI   DISKAD+7,X'01'
         ENQ   (QNAME,RNAME,E,7,SYSTEM)
READ     XDAP  INCB,RI,SYSCAT,NN,264,,(2)
         WAIT  ECB=INCB
         CLI   INCB,X'7F'
         BNE   READERR
FTB      B     SETEND
         NI    INCB,63
         LM    3,4,8(2)
         LA    15,LASTLINE
         SR    15,4
         BCTR  15,0
         EX    15,MSP
NEXTFLD  CLC   0(8,3),FFS
         BNE   TYPENTRY
         CLC   8(3,3),ZEROTTR
         BE    TESTEMPT
         S     2,SIXTEEN
         BAL   5,CALCTTR
         A     2,SIXTEEN
         LA    3,IN+2
         ST    3,8(2)
         B     READ
READERR  ABEND 80,DUMP
TESLEVEL C     2,ADISKAD
         BE    END
         S     2,SIXTEEN
         B     READ
END      DEQ   (QNAME,RNAME,7,SYSTEM)
         CLOSE (SYSCAT)
         OPEN  (BSAMCAT)
         LH    2,ENDTTR
         LA    2,1(2)
         SR    3,3
         IC    3,ENDTTR+2
         MR    2,3
         LM    4,6,ZREGS
READB    READ  DECB,SF,BSAMCAT,NN
         CHECK DECB
         LA    4,1(4)
         TM    NN,255
         BO    *+12
         LA    5,1(5)
         B     BCT
         LH    7,IN
         LA    8,256
         SR    8,7
         AR    6,8
BCT      BCT   3,READB
EOF      CVD   4,DISKAD
         ED    NINC,DISKAD+5
         CVD   5,DISKAD
         ED    NEMP,DISKAD+5
         SRA   6,8
         CVD   6,DISKAD
         ED    NPU,DISKAD+5
         PUT   LIST,SPACEUP
         PUT   LIST,LASTLINE
         CLOSE (LIST,,BSAMCAT)
*        GOBACK
*  EXPANSION OF RESPOND TSG MACRO
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
*  END OF MACRO EXPANSION
SETEND   MVC   ENDTTR,IN+14
         NI    FTB+1,15
         B     FTB+4
TYPENTRY CLI   11(3),0
         BE    IPE
         CLI   11(3),2
         BE    GDG
         MVI   INDSW,255
         CLI   11(3),3
         BE    OCVLPTR
         CLI   11(3),4
         BE    ALIAS
         CLI   11(3),5
         BE    CVOLPTR
         CLI   11(3),1
         BE    VOLCBPTR
         CLI   11(3),7
         BNL   DATASET
         SR    5,5
         IC    5,11(3)
         SLL   5,1
         AR    3,5
         B     NEXTFLD
IPE      BAL   5,MOVEN
         MVI   0(7),C'.'
         MVI   INDSW,0
         LA    7,1(7)
         LR    4,7
         BAL   5,CALCTTR
         LA    3,12(3)
         CLI   GSTRING,255
         BNE   *+16
         MVI   INDSW,1
         LA    3,4(3)
         MVI   GSTRING,0
         ST    3,8(2)
         LA    2,16(2)
         LA    3,IN+20
         STM   3,4,8(2)
         B     READ
GDG      MVI   GSTRING,255
         MVC   LTYPE,LTYPEGDG
         SR    8,8
         IC    8,13(3)
         CVD   8,DW
         ED    LTYPE+3(4),DW+6
         TM    12(3),2
         BNO   *+8
         MVI   LTYPE+8,C'D'
         TM    12(3),1
         BNO   *+8
         MVI   LTYPE+9,C'E'
         MVC   SLT,LTYPE
         B     IPE
DATASET  CLI   0(3),C'G'
         BNE   NGDG
           CLC      5(2,3),V0
         BNE   NGDG
         TM    1(3),240
         BO    NGDG
         XC    1(4,3),FFS
         MVC   LTYPE,SLT
         MVC   SLT,SPACES
NGDG     BAL   5,MOVEN
         LA    9,14(3)
         BAL   5,FINDTYPE
         CLI   11(3),7
         BH    MULVOL
         MVC   LVS(6),18(3)
           CLC      24(2,3),HONE
           BNH      NODSEQ
           LH       1,24(3)
           CVD      1,DW
           MVC      SEQ,PMASK
           MVC      LVS+6(4),BRACKETS
           ED       SEQ,DW+6
           MVC      LVS+7(2),SEQ+2
NODSEQ     EQU      *
BLANKOUT LA    3,26(3)
         BAL   5,PRINT
         B     NEXTFLD
FINDTYPE CLI   2(9),128
         BNE   TDISK
         TM    1(9),32
         BNO   *+12
         MVC   LVTYPE,T24004                800 DUAL DENSITY
         BR    5
         TM    1(9),128
         BNO   *+12
         MVC   LVTYPE,T24002                7 TRK
         BR    5
         TM    0(9),4
         BNO   *+12
         MVC   LVTYPE,T24003                1600 BPI
         BR    5
         MVC   LVTYPE,T2400
         BR    5
TDISK    CLI   2(9),32
         BNE   ENDT          BLANK - UNKNOWN DEVTYPE
         SR    6,6
         IC    6,3(9)
         LA    1,DISKTAB-4
         SLL   6,2
         AR    1,6
         MVC   LVTYPE(4),0(1)
ENDT     BR    5
PRINT    SP    PCNTR,ONE
         BC    10,PUTLINE
         PUT   LIST,NP
         MVC   PCNTR,PLIMIT
         MVC   HDRPNO,PMASK
         AP    PNO,ONE
         ED    HDRPNO,PNO
         PUT   LIST,HDR
         PUT   LIST,TITLE
PUTLINE  PUT   LIST,LINE
         LA    15,LASTLINE
         SR    15,4
         BCTR  15,0
         EX    15,MSP
         BR    5
MSP      MVC   0(1,4),SPACES
MOVEN    LR    6,3
         LR    7,4
         LA    8,8
MOVN     MVC   0(1,7),0(6)
         LA    6,1(6)
         LA    7,1(7)
         CLI   0(6),64
         BE    MOVEND
         BCT   8,MOVN
MOVEND   BR    5
CALCTTR  STM   2,14,REGS
         LA    8,REGS
         MVC   RAWTTR,8(3)
         L     0,RAWTTR
         L     1,SYSCAT+44
         LA    1,0(1)
         LA    2,16(2)
         L     15,CONVERT
         BALR  14,15
         LM    2,14,0(8)
         BR    5
MULVOL   OI    MPSW,255
         LA    6,LVS
         LA    7,14(3)
         MVC   LASTTYPE,14(3)
         SR    8,8
         IC    8,11(3)
         LA    3,12(3)
         SLA   8,1
         AR    3,8
         SRA   8,2
SETMSER  MVC   0(6,6),4(7)
           CLC      10(2,7),HONE
           BNH      NOMSEQ
           LH       1,10(7)
           CVD      1,DW
           MVC      SEQ,PMASK
           MVC      6(4,6),BRACKETS
           ED       SEQ,DW+6
           MVC      7(2,6),SEQ+2
           LA       6,4(6)
NOMSEQ     EQU      *
         SH    8,THREE
         BC    8,MVPRNT
         LA    7,12(7)
         CLC   LASTTYPE,0(7)
         BE    NEXTMVOL
         MVC   LASTTYPE,0(7)
         BAL   5,PRINT
         CLI   MPSW,255
         BNE   *+20
         MVC   STORNAME,LNAME
         MVC   LNAME,SPACES
         MVI   MPSW,0
         LR    9,7
         BAL   5,FINDTYPE
         LA    6,LVS
         B     SETMSER
NEXTMVOL MVI   6(6),C','
         LA    6,7(6)
         B     SETMSER
MVPRNT   BAL   5,PRINT
         CLI   MPSW,255
         BE    NEXTFLD
         MVC   LNAME,STORNAME
         B     NEXTFLD
TESTEMPT CLI   INDSW,255
         BE    TESLEVEL
         MVC   LTYPE,SLT
         CLI   INDSW,0
         BNE   *+10
         MVC   LTYPE,INDPTR
         BAL   5,PRINT
         B     TESLEVEL
ALIAS    BAL   5,MOVEN
           MVC      LTYPE,ALIA
           MVC      LVTYPE(8),12(3)
         BAL   5,PRINT
         LA    3,20(3)
         B     NEXTFLD
CVOLPTR  BAL   5,MOVEN
         MVC   LTYPE(8),CVOTR
         MVC   LVS(6),16(3)
         SR    5,5
         IC    5,15(3)
         LA    1,DISKTAB-4
         SLL   5,2
         AR    1,5
         MVC   LVTYPE(4),0(1)
         BAL   5,PRINT
         LA    3,22(3)
         B     NEXTFLD
OCVLPTR  BAL   5,MOVEN
         MVC   LTYPE(8),CVOTR
         MVC   LVS(6),12(3)
         MVC   LVTYPE(4),NA
         BAL   5,PRINT
         LA    3,18(3)
         B     NEXTFLD
NA       DC    CL4'N/A'
VOLCBPTR CLI   0(3),C'G'
         BNE   NGDGM
         CLC   5(2,3),V0
         BNE   NGDGM
         TM    1(3),240
         BO    NGDGM
         XC    1(4,3),FFS
         MVC   LTYPE,SLT
         MVC   SLT,SPACES
NGDGM    BAL   5,MOVEN
         STM   0,15,VCBREGS
         OI    MPSW,255
NEXTVCB  LA    2,VDISKAD-16
         BAL   5,CALCTTR
         XDAP  VCB,RI,SYSCAT,VKEY,264,,VDISKAD
         WAIT  ECB=VCB
         CLI   VCB,127
         BNE   READERR
         LH    10,VCOUNT
         LA    9,VSERS
NEXTLINE BAL   5,FINDTYPE
         MVC   LASTTYPE,0(9)
           LA       11,6
         LA    6,LVS
SETVMS   MVC   0(6,6),4(9)
           CLC      10(2,9),HONE
           BNH      NOSEQ
           LH       1,10(9)
           CVD      1,DW
           MVC      SEQ,PMASK
           MVC      6(4,6),BRACKETS
           ED       SEQ,DW+6
           MVC      7(2,6),SEQ+2
           LA       6,4(6)
NOSEQ      EQU      *
         BCT   10,*+8
         B     VMPRNT
         LA    9,12(9)
         CLI   0(9),0
         BNE   FULLINE
         BAL   5,PRINT
         CLI   MPSW,255
         BNE   *+20
         MVC   STORNAME,LNAME
         MVC   LNAME,SPACES
         MVI   MPSW,0
         MVC   8(3,3),10(9)
         B     NEXTVCB
FULLINE  BCT   11,NOTFULL
DIFFTYPE BAL   5,PRINT
         CLI   MPSW,255
         BNE   *+20
         MVC   STORNAME,LNAME
         MVC   LNAME,SPACES
         MVI   MPSW,0
         B     NEXTLINE
NOTFULL  CLC   LASTTYPE,0(9)
         BNE   DIFFTYPE
         MVI   6(6),C','
         LA    6,7(6)
         B     SETVMS
VMPRNT   BAL   5,PRINT
         LM    0,15,VCBREGS
         LA    3,14(3)
         CLI   MPSW,255
         BE    NEXTFLD
         MVC   LNAME,STORNAME
         B     NEXTFLD
SYSCAT   DCB   DDNAME=SYSCAT,MACRF=(E),DEVD=DA,DSORG=DA
BSAMCAT  DCB   DDNAME=SYSCAT,LRECL=256,KEYLEN=8,BLKSIZE=256,           *
               MACRF=(R),DSORG=PS,RECFM=F,EODAD=EOF
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),LRECL=133,RECFM=FBM
MPSW     DC    X'00'
INDSW    DC    X'FF'
PCNTR    DC    PL2'0'
PLIMIT   DC    P'55'
V0         DC       C'V0'
TIME     DC    F'0'
DATE     DC    F'0'
ZREGS    DC    3F'0'
CONVERT  DC    F'0'
SIXTEEN  DC    F'16'
ADISKAD  DC    A(DISKAD)
RAWTTR   DC    XL3'00'
         DC    X'00'
REGS     DC    13F'0'
THREE    DC    H'3'
C        DC    H'0'
T        DC    H'0'
CC       DC    H'0'
HH       DC    H'0'
TT       DC    H'0'
ENDTTR   DC    XL3'00'
FFS      DC    8X'FF'
HONE       DC       X'0001'
SEQ        DC       XL4'00'
BRACKETS   DC       C'(  )'
CVOTR    DC    C'CVOL PTR'
ALIA       DC       C' ALIAS FOR'
INDPTR   DC    CL10'INDEX PTR'
ONE      DC    P'1'
T2400    DC    CL6'2400'
T24002   DC    CL6'2400-2'
T24003   DC    CL6'2400-3'
T24004   DC    CL6'2400-4'
SLT      DC    CL10' '
QNAME    DC    CL8'SYSCTLG'
RNAME    DC    CL8'SYSCTLG'
SPACEUP  DC    X'0B'
NP       DC    X'8B'
HDR      DC    X'19'
           DC       CL4' '
           DC       CL40'RESPOND TSG UTILITY'
         DC    C'LIST OF SYSCTLG ON '
HDRVOLNO DC    C'123456'
         DC    C' AT'
HDRTIME  DC    X'4020214B2020'
         DC    C' ON'
HDRDATE  DC    X'4020214B202020'
         DC    CL36' '
         DC    C'PAGE'
HDRPNO   DC    X'40202021'
TITLE    DC    X'11'
         DC    CL24' '
         DC    CL29'NAME'
         DC    CL8'TYPE'
         DC    CL9'VOL TYPE'
           DC       CL62'VOLUME SERIALS (FILE SEQ SHOWN IN BRACKETS IF *
               GREATER THEN 1)'
SPACES   DC    CL132' '
LINE     DC    X'09'
         DC    CL4' '
LNAME    DC    CL44' '
         DC    CL2' '
LTYPE    DC    CL10' '
         DC    CL2' '
LVTYPE   DC    CL6' '
         DC    CL2' '
LVS      DC    CL62' '
LASTLINE DC    X'09'
         DC    CL4' '
         DC    C'NUMBER OF BLOCKS IN CATLG ='
NINC     DC    X'402020202120'
         DC    CL4' '
         DC    C'NUMBER OF EMPTY BLOCKS ='
NEMP     DC    X'402020202120'
         DC    CL4' '
         DC    C'(SPACE IN PARTLY USED BLOCKS ='
NPU      DC    X'402020202120'
         DC    C' EMPTY BLOCKS) '
         DC    CL8' '
STORNAME DC    CL44' '
LASTTYPE DC    CL4' '
PNO      DC    PL2'0'
PMASK    DC    X'40202021'
LTYPEGDG DC    C'GDG'
         DC    X'40202120404040'
ZEROTTR  DC    X'000000'
GSTRING  DC    X'00'
DISKTAB  DC    C'2311'
         DC    C'2301'
         DC    C'2302'
         DC    C'2302'
         DC    C'2321'
         DC    CL8' '
         DC    C'2314'
         DS    0H
VKEY     DC    XL8'00'
VCOUNT   DC    XL2'00'
VSERS    DC    XL252'00'
DW       DC    D'0'
VDISKAD  DC    D'0'
VCBREGS  DC    16F'0'
DISKAD   DC    10D'0'
NN       DC    D'0'
IN       DS    350C
         END
