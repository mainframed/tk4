          TITLE     '                             COMPARE / ASSCOMP'
*ASSCOMP - ASSEMBLER SUBROUTINE OF COBOL FILE COMPARE PROGRAM
*                                       ('COMPARE')
          SPACE     2
*RECEIVES A SINGLE ITEM PARAMETER LIST - 0(1) IS ADDRESS OF ALL THE
*     CONTROL INFORMATION NEEDED TO EXECUTE THIS MODULE. THIS IS
*     DESCRIBED BY THE DSECT  'CONTINFO'
          SPACE     2
*THIS MODULE IS NOT SERIALLY RE-USEABLE
          EJECT
CONTINFO  DSECT
IDENT     DS        9CL8      COBOL D-N IS IDENTIFIER (9)
LENGTH    DS        9H                     LENGTH (9)
BLKSIZE   DS        H                      BLKSIGE
IDSTART   DS        H                      IDENT-START-POSN
TBLSIZE   DS        H                      TABLE-SIZE
IDSIZE    DS        PL1                    IDENT-SIZE
FORMAT    DS        CL1                    RECORD-FORMAT
SELXIT    DS        CL1                    USER-SELECT-EXIT
COMPXIT   DS        CL1                    USER-COMPARE-EXIT
RANGE     DS        900H                   RANGE (9,100)
TEST      DS        900CL2                 TEST-TYPE (9,100) AND
*                                          FIELD-LENGTH (9,100)
*                                   (FIELD-LENGTH IS LNGTH - 1 OR LL)
          EJECT
ASSCOMP   CSECT
         SAVE  (14,12)
         LA    15,SAVE
         ST    15,8(0,13)
         ST    13,4(0,15)
         LR    13,15
         LR    12,15
         USING *,12
          L         2,0(1)
          USING     CONTINFO,2
          SPACE     2
*USE OF REGISTERS -
*   R0
*   R1    USED IN I-O OPERATIONS AND BY OTHER MACROS / PAR LST ADDRS
*   R2    BASE REGISTER FOR DATA PASSED FROM COBOL (DSECT CONTINFO)
*   R3    WORK AREA / RTNS 3,6 - K / RTNS 6,7 - I
*   R4    USED TO ADDRESS PRINT FILE RECORD
*   R5    WORK AREA / RTN 7 - J
*   R6    USED TO ADDRESS COMP1 FILE RECORD
*   R7    USED TO ADDRESS COMP2 FILE RECORD
*   R8    COUNT1  - COUNT OF RECS READ ON COMP1
*   R9    COUNT2  - COUNT OF RECS READ ON COMP2
*   R10   LENGTH OF RECORDS NOW BEING COMPARED
*   R11   COUNT3  - COUNT OF RECS COMPARED
*   R12   BASE REGISTER FOR INSTRUCTIONS + WORK AREAS
*   R13   SAVE AREA POINTER
*   R14   LINKAGE - RETURN ADDRESS / WORK
*   R15   LINKAGE - ENTRY ADDRESS / ENTRY TO R11 - A(REC TO PRINT OUT)
          SPACE     2
*ROUTINE 1 SETS LOGIC SWITCHES 1,4,5,6,7, AND SETS UP THE TEST FOR
*                                       IDENTITY, + CLEARS COUNTERS
RTN1      CLI       FORMAT,C'F'
          BE        R1A
          MVC       SW7+2(2),SR6VFM
          B         R1A
SR6VFM    DC        S(R6VFM)
R1A       CLI       SELXIT,C'S'
          BE        R1B
          MVC       SW4+2(2),SA
          B         R1B
SA        DC        S(A)
R1B       CP        IDSIZE,=PL1'0'
          BNE       R1C
          CLI       FORMAT,C'F'
          BE        R1D
          MVC       SW6+2(2),SR6VAR
          B         R1D
SR6VAR    DC        S(R6VAR)
SR6IDENT  DC        S(R6IDENT)
R1C       MVC       SW6+2(2),SR6IDENT
          SR        3,3
          IC        3,IDSIZE
          SRA       3,4(0)
          SH        3,=H'1'
          STC       3,R6IDTEST+1
          NI        IDSTART,X'0F'
          OI        IDSTART,X'60'
          MVC       R6IDTEST+4(2),IDSTART
R1D       SR        8,8
          SR        9,9
          SR        11,11
          EJECT
*ROUTINE 2 OPENS THE FILES, CHECKS THAT BLKSIZE, LRECL AND RECFM
*     CORRESPOND ON THE TWO FILES FOR COMPARISON TO INFORMATION
*     RECEIVED FROM THE COBOL PGM, AND WRITES HEADING ON PRINT FILE.
*NOTE THAT RECFM, LRECL AND BLKSIZE MUST BE ENTERED ON THE DSCB OR THE
*     DD CARD FOR THE TWO FILES TO BE COMPARED
RTN2      OPEN      (PRINT,(OUTPUT),COMP1,(INPUT),COMP2,(INPUT))
          PUT       PRINT
          LR        4,1
          MVC       0(1,4),SKIP1
          PUT       PRINT
          LR        4,1
          MVC       0(1,4),WRITE
          MVC       1(132,4),HDG
          TIME      DEC
          ST        1,FWORD
          ED        19(7,4),FWORD+1
          SRL       0,12(0)
          ST        0,FWORD
          OI        FWORD+3,X'0F'
          ED        28(7,4),FWORD+1
          PUT       PRINT
          LR        4,1
          MVC       0(1,4),SPACE1
          PUT       PRINT
          LR        4,1
          LA        3,COMP1
          USING     IHADCB,3
          CLI       FORMAT,C'F'
          BNE       R2A
          TM        DCBRECFM,X'80'
          BNO       R2C
          TM        DCBRECFM,X'40'
          BO        R2C
          LH        5,DCBLRECL
          CH        5,LENGTH
          BNE       R2E
          B         R2B
R2A       TM        DCBRECFM,X'80'
          BO        R2C
          TM        DCBRECFM,X'40'
          BNO       R2C
R2B       LH        5,DCBBLKSI
          CH        5,BLKSIZE
          BE        R2G
R2C       MVI       MSG10+29,C'1'
R2D       MVC       0(1,4),SPACE1
          PUT       PRINT
          LR        4,1
          MVC       0(1,4),WRITE
          MVC       1(132,4),MSG10
          PUT       PRINT
          LR        4,1
          B         RTN10
R2E       MVI       MSG11+31,C'1'
R2F       MVC       0(1,4),SPACE1
          PUT       PRINT
          LR        4,1
          MVC       0(1,4),WRITE
          MVC       1(132,4),MSG11
          PUT       PRINT
          LR        4,1
          B         RTN10
R2G       LA        3,COMP2
          USING     IHADCB,3
          CLI       FORMAT,C'F'
          BNE       R2H
          TM        DCBRECFM,X'80'
          BNO       R2K
          TM        DCBRECFM,X'40'
          BO        R2K
          LH        5,DCBLRECL
          CH        5,LENGTH
          BNE       R2L
          B         R2J
R2H       TM        DCBRECFM,X'80'
          BO        R2K
          TM        DCBRECFM,X'40'
          BNO       R2K
R2J       LH        5,DCBBLKSI
          CH        5,BLKSIZE
          BE        R2M
R2K       MVI       MSG10+29,C'2'
          B         R2D
R2L       MVI       MSG11+31,C'2'
          B         R2F
          DROP      3
R2M       EQU       *
          EJECT
*ROUTINE 3 READS THE TWO FILES AND HANDLES USER SELECTION WHEN REQU'D
RTN3      EQU       *
LOOP      CLI       EOF1,X'00'     START OF MAIN PROGRAM LOOP
          BE        LP3
          MVI       MSG6A+5,C'2'
          MVI       MSG6A+20,C'1'
LP1       MVC       1(39,4),MSG6A
          CVD       8,DWORD
          UNPK      MSGB+23(7),DWORD
          OI        MSGB+29,X'F0'
          CVD       9,DWORD
          UNPK      MSGB+43(7),DWORD
          OI        MSGB+49,X'F0'
          MVC       40(93,4),MSGB
          MVC       0(1,4),WRITE
          PUT       PRINT
          MVC       0(1,1),SPACE1
          PUT       PRINT
          LR        4,1
LP2       B         SW2
LP3       GET       COMP1
          LR        6,1
          AH        8,=H'1'
SW2       B         R3A
R3A       GET       COMP2
          LR        7,1
          AH        9,=H'1'
SW4       B         USER
R3B       MVC       SW2+2(2),SR3A
          B         USER
SR3A      DC        S(R3A)
REC1ADDR  DS        F
REC2ADDR  DS        F
KADDR     DC        A(K)
USER      ST        6,REC1ADDR
          ST        7,REC2ADDR
          LA        1,REC1ADDR
          CALL      USERS           (PARAM LIST - 0(1) RECORD 1
*                                                 4(1) RECORD 2
*                                                 8(1) K (RETURN BYTE))
          CLI       K,C' '
          BE        A                   A IS IN RTN 6
          CLI       K,C'1'
          BNL       R3C
          NI        K,X'0F'
          SR        3,3
          IC        3,K
          SH        3,=H'1'
          B         B                   B IS IN RTN 6
R3C       CLI       K,C'3'
          BE        LOOP
          CLI       K,C'2'
          BE        R3A
          CLI       K,C'4'
          BE        RTN10
          MVC       SW2+2(2),SR3B
          B    LP3
SR3B      DC        S(R3B)
          EJECT
*ROUTINE 6 DETERMINES WHICH OF THE 9 RANGE AND TEST TABLES WILL BE
*                   USED FOR THE TYPE OF RECORDS NOW IN INPUT.
RTN6      EQU       *
A         SR        3,3
SW6       B         R6FIX1
R6VAR     LR        5,3
          LH        10,0(6)
R6A       CH        10,LENGTH(5)
          BE        R6C
          AH        3,=H'1'
          AH        5,=H'2'
          CH        3,=H'9'
          BL        R6A
R6ERR1    MVC       1(39,4),MSG1A
R6ERR2    CVD       8,DWORD
          UNPK      MSGB+23(7),DWORD
          OI        MSGB+29,X'F0'
          CVD       9,DWORD
          UNPK      MSGB+43(7),DWORD
          OI        MSGB+49,X'F0'
          MVC       40(93,4),MSGB
          MVC       0(1,4),WRITE
          PUT       PRINT
          LR        4,1
          LR        15,6
          BAL       14,RTN11
          LR        15,7
          BAL       14,RTN11
          MVC       0(1,4),SPACE1
          PUT       PRINT
          LR        4,1
          B         LOOP
R6IDENT   LA        5,IDENT
R6B       CLC       0(8,5),=XL8'FEFEFEFEFEFEFEFE'
          BE        R6D
R6IDTEST  CLC       0(8,5),0(6)
          BE        R6D
          AH        3,=H'1'
          AH        5,=H'8'
          CH        3,=H'9'
          BL        R6B
          B         R6ERR1
R6C       CH        10,0(7)
          BE        R6FIX
          MVC       1(39,4),MSG3A
          B         R6ERR2
B         CH        3,=H'9'
          BNL       R6ERR1
R6D       EQU       *
SW7       B         R6FIX1
R6VFM     LR        5,3
          SLL       5,1(0)
          LH        10,0(6)
          CH        10,LENGTH(5)
          BE        R6C
          MVC       1(39,4),MSG2A
          B         R6ERR2
R6FIX     MH        3,=H'100'
          AH        11,=H'1'
          B         RTN7
R6FIX1    LH        10,LENGTH
          B         R6FIX
          EJECT
*ROUTINE 7 COMPARES EACH FIELD DEFINED FOR THIS RECORD TYPE IN THE
*         MANNER INDICATED IN TEST (I) -
*                   B  =  USE A CLC INSTRUCTION
*                   P  =  COMPARE PACKED
*                   N  =  NO COMPARISON
*               X,Y,Z  =  USER COMPARISON RTN - USERX, USERY OR USERZ
RTN7      SR        5,5
          SLL       3,1(0)
R7LOOP    IC        14,TEST(3)
          STC       14,FWORD
          CLI       FWORD,C'N'
          BE        R7A
          CLI       FWORD,C'B'
          BNE       R7F
          IC        14,TEST+1(3)
          STC       14,R7CHTEST+1
          STH        5,FWORD
          STH        5,FWORD+2
          OI        FWORD,X'60'
          OI        FWORD+2,X'70'
          MVC       R7CHTEST+2(4),FWORD
R7CHTEST  CLC       0(1,6),0(7)
          BE        R7A
R7ERR1    MVC       1(39,4),MSG5A
          LH        5,RANGE(3)
          CVD       5,DWORD
          UNPK      31(4,4),DWORD
          OI        34(4),X'F0'
          CVD       8,DWORD
          UNPK      MSGB+23(7),DWORD
          OI        MSGB+29,X'F0'
          CVD       9,DWORD
          UNPK      MSGB+43(7),DWORD
          OI        MSGB+49,X'F0'
          MVC       40(93,4),MSGB
          MVC       0(1,4),WRITE
          PUT       PRINT
          LR        4,1
          LR        15,6
          BAL       14,RTN11
          LR        15,7
          BAL       14,RTN11
          MVC       0(1,4),SPACE1
          PUT       PRINT
          LR        4,1
          B         R7E
R7F       CLI       FWORD,C'P'
          BNE       R7G
          IC        14,TEST+1(3)
          STC       14,R7PDTEST+1
          STH       5,FWORD
          STH       5,FWORD+2
          OI        FWORD,X'60'
          OI        FWORD+2,X'70'
          MVC       R7PDTEST+2(4),FWORD
R7PDTEST  CP        0(1,6),0(7)
          BNE       R7ERR1
R7A       LH        5,RANGE(3)
          B         R7E
R7G       LR        14,6
          AR        14,5
          ST        14,PARAM1
          LR        14,7
          AR        14,5
          ST        14,PARAM2
          SR        14,14
          IC        14,TEST+1(3)
          STH       14,LENHW
          LA        1,PARMLIST
          IC        14,TEST(3)
          SH        14,=H'231'
          MH        14,=H'4'
          L         15,BRNCHTBL(14)
          LA        14,R7B
          BR        15
          DS        0F
PARMLIST  EQU       *
PARAM1    DS        F
PARAM2    DS        F
PARAM3    DC        A(LENHW)
PARAM4    DC        A(K)
LENHW     DS        H
BRNCHTBL  DC        A(CALLX)
          DC        A(CALLY)
          DC        A(CALLZ)
CALLX     L         15,=V(USERX)
          BR        15
CALLY     L         15,=V(USERY)
          BR        15
CALLZ     L         15,=V(USERZ)
          BR        15
R7B       CLI       K,C'N'
          BNE       R7A
          B         R7ERR1
R7E       CR        5,10
          BE        LOOP
          AH        3,=H'2'
          B         R7LOOP
          EJECT
*ROUTINE 8 HANDLES THE END OF FILE CONDITION ON FILE 1
EOF1      DC        X'00'
SLASHAST  DC        C'/*'
RTN8      MVI       EOF1,X'FF'
          LA        6,SLASHAST
          B         SW2
          EJECT
*ROUTINE 9 HANDLES THE END OF FILE CONDITION ON FILE 2
RTN9      CLI       EOF1,X'00'
          BNE       RTN10
          MVI       MSG6A+5,C'1'
          MVI       MSG6A+20,C'2'
          MVC       LP2+2(2),SRTN10
          B         LP1
SRTN10    DC        S(RTN10)
          EJECT
*NORMAL END OF JOB ROUTINE (ROUTINE 10)
RTN10     MVC       0(1,4),SPACE1
          PUT       PRINT
          LR        4,1
          MVC       1(132,4),MSG7
          MVC       0(1,4),WRITE
          CVD       8,DWORD
          UNPK      29(7,4),DWORD
          OI        35(4),X'F0'
          CVD       9,DWORD
          UNPK      49(7,4),DWORD
          OI        55(4),X'F0'
          CVD       11,DWORD
          UNPK      68(7,4),DWORD
          OI        74(4),X'F0'
          CLOSE     (PRINT,,COMP1,,COMP2)
          L         13,SAVE+4
          RETURN    (14,12)
          EJECT
*ROUTINE 11 PRINTS AN IMAGE OF A RECORD WHOSE ADDR IS PASSED TO IT IN
*         R15.  REGISTER USAGE FOR THIS ROUTINE -
*                   R1   RUT ADDRESS FOR PRINT FILE
*                   R2   A(PRINTOUT), THEN A(PRINTOUT) + LENGTH
*                                       ALREADY PRINTED
*                   R3   BASE REG FOR RTN 11 DATA AREAS
*                   R4   WORK AREA
*                   R10  RECORD LENGTH
*                   R11  A(PRINTOUT) + ((R10 * 2)+6)
*                   R15  A(RECORD TO BE PRINTED)
RTN11     EQU       *+20
ST2       DS        F
ST3       DS        F
ST4       DS        F
ST11      DS        F
ST14      DS        F
          ST        2,ST2
          ST        3,ST3
          ST        4,ST4
          ST        11,ST11
          ST        14,ST14
          LR        1,4
          CLC       0(2,15),=CL2'/*'
          BE        R11G
          L         3,=A(RTN11DTA)
          USING     RTN11DTA,3
          LA        2,PRINTOUT
          LR        11,10
          SLL       11,1(0)
          AH        11,=H'6'
          A         11,=A(PRINTOUT)
          LR        4,15
          AR        4,10
R11A      CR        4,15
          BNH       R11B
          MVC       0(1,2),0(15)
          TR        0(1,2),TRTBL1
          MVC       1(1,2),0(15)
          TR        1(1,2),TRTBL2
          AH        2,=H'2'
          AH        15,=H'1'
          B         R11A
R11B      MVC       0(6,2),=CL6'***END'
          LA        2,PRINTOUT
          MVI       R11DB+1,X'00'
R11B1     LR        4,2
          AH        2,=H'132'
          CR        2,11
          BH        R11C
          MVC       1(132,1),0(4)
          B         R11D
R11C      MVC       1(132,1),=CL132' '
          SR        11,4
          SH        11,=H'1'
          STC       11,R11CMVC+1
          MVI       R11DB+1,X'F0'
R11CMVC   MVC       1(132,1),0(4)
R11D      MVC       0(1,1),WRITE
          PUT       PRINT
R11DB     NOP       R11E
          B         R11B1
R11E      L         2,ST2
          L         3,ST3
          L         11,ST11
          LR        4,1
          LH        15,ERRORS
          AH        15,=H'1'
          STH       15,ERRORS
          CH        15,=H'20'
          BH        R11F
          L         14,ST14
          BR        14
ERRORS    DC        H'0'
R11F      MVC       0(1,4),SPACE1
          PUT       PRINT
          LR        4,1
          MVC       0(1,4),WRITE
          MVC       1(132,4),MSG8
          PUT       PRINT
          LR        4,1
          B         RTN10
R11G      MVC       0(1,1),WRITE
          MVI       1(1),C' '
          MVC       2(131,1),1(1)
          MVC       1(2,1),=CL2'/*'
          PUT       PRINT
          B         R11E
          EJECT
*ROUTINE 12 IS SYNAD RTN FOR BOTH FILES
RTN12     SYNADAF   ACSMETH=QSAM
          ST        1,SAVE1
          MVC       0(1,4),WRITE
          MVC       1(132,4),MSG9
          ST        14,SAVE14
          PUT       PRINT
          LR        4,1
          MVC       0(1,4),WRITE
          MVC       1(128,4),0(1)
          MVC       129(4,4),=CL4'    '
          PUT       PRINT
          LR        4,1
          SYNADRLS
          L         14,SAVE14
          L         1,SAVE1
          BR        14
SAVE1     DS        F
SAVE14    DS        F
          EJECT
* WORK AREAS AND CONSTANTS
          SPACE     1
SAVE      DC        18F'0'
MSG1A     DC        CL39'NO COMPARE FORMAT SPECIFIED'
MSG2A     DC        CL39'RECORD LENGTH DIFFERS FROM FORMAT'
MSG3A     DC        CL39'RECORD LENGHS DIFFER'
MSG5A     DC        CL39'DIFFERENCE IN FIELD ENDING AT 9999'
MSG6A     DC        CL39'FILE 9 EXCEEDS FILE 9'
MSGB      DC        CL34'RECORD NO. ON FILE 1 = 9999999,   '
          DC        CL59'FILE 2 = 9999999'
MSG7      DC        CL39'COMPARE ENDED      FILE 1 = 9999999    '
          DC        CL93'FILE 2 = 9999999    COMPARE 9999999 RECORDS'
MSG8      DC        CL132'** ABNORMAL END    MORE THAN 10 ERRORS'
MSG9      DC        CL28'SYNAD EXIT                  '
          DC        CL104'BLOCK IN ERROR SKIPPED'
MSG10     DC        CL43'RECFM OR BLKSIZE ON FILE COMP9 DIFFERS FROM'
          DC        CL89'CONTROL CARD ENTRY    JOB TERMINATED'
MSG11     DC        CL45'LRECL ON FIXED FORMAT FILE COMP9 DIFFERS FROM'
          DC        CL87'CONTROL CARD ENTRY    JOB TERMINATED'
HDG       DC        CL19'         SYCOMPAR  '
          DC        XL15'212040202020404040202120402020'  DATE AT HDG+
          DC        CL98' '   19, TIME AT HDG+28 (EDIT WD AT 18, 27)
FWORD     DS        F
K         DS        CL1
DWORD     DS        D
          LTORG
          EJECT
*DCBS FOR FILES USED BY THIS MODULE - PRINT = PRINT FILE
*                                     COMP1 = FIRST FILE FOR COMPARISON
*                                     COMP2 = SECOND FILE FOR COMP"SON
          SPACE     5
PRINT     DCB       BLKSIZE=931,DDNAME=PRINT,DEVD=DA,DSORG=PS,         C
               LRECL=133,MACRF=(PL),RECFM=FBM
          SPACE     2
SPACE1    DC        BL1'00001011'  IMMEDIATE SINGLE SPACE   )CONTROL
WRITE     DC        BL1'00001001'  WRITE THEN SPACE 1       )CHS FOR
SKIP1     DC        BL1'10001011'  IMMEDIATE SKIP TO CH 1   )PRINT FL
          SPACE     5
COMP1     DCB       DDNAME=COMP1,DSORG=PS,EODAD=RTN8,EROPT=SKP,        C
               MACRF=(GL),SYNAD=RTN12
          SPACE     5
COMP2     DCB       DDNAME=COMP2,DSORG=PS,EODAD=RTN9,EROPT=SKP,        C
               MACRF=(GL),SYNAD=RTN12
          SPACE     5
          DCBD      DSORG=PS
          EJECT
* WORK AREAS FOR RTN 11
ASSCOMP   CSECT
RTN11DTA  EQU       *
TRTBL1    DC   CL50'00000000000000001111111111111111222222222222222233'
          DC   CL50'33333333333333444444444444444455555555555555556666'
          DC   CL50'66666666666677777777777777778888888888888888999999'
          DC   CL50'9999999999AAAAAAAAAAAAAAAABBBBBBBBBBBBBBBBCCCCCCCC'
     DC  CL56'CCCCCCCCDDDDDDDDDDDDDDDDEEEEEEEEEEEEEEEEFFFFFFFFFFFFFFFF'
TRTBL2    DC   CL50'0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF01'
          DC   CL50'23456789ABCDEF0123456789ABCDEF0123456789ABCDEF0123'
          DC   CL50'456789ABCDEF0123456789ABCDEF0123456789ABCDEF012345'
          DC   CL50'6789ABCDEF0123456789ABCDEF0123456789ABCDEF01234567'
     DC  CL56'89ABCDEF0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF'
PRINTOUT  DS        CL8200
          END
