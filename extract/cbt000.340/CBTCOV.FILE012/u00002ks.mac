*     THIS PROGRAM WAS UPDATED TO BE COMPATIBLE WITH MVT ON 7 JAN 1970
*
*    THIS PROGRAM WAS EXTENDED ON THE 26 MARCH. IT NOW WORKS WITH THE
*    LOADER, AND UNDER MFT OR MVT.
*
*    **** JOHN PARKER **** TSG FOLLOW-UP TO AP#1 **** TSG#1 ****
*
*    THIS IS A SUBPROGRAM TO BE CALLED BY A PL/1 PROGRAM BASICALLY AS A
*    DEBUGGING AID.  THE CALL CAN BE IN ONE OF THREE FORMS:-
*
*        CALL TSGLMIT(BIN(XXX,31,0));
*        CALL TSGPLOT(BIN(XXX,31,0));
*        CALL TSGTIME(BIN(XXX,31,0));
*
*     TSGLMIT PERMITS OPERATION FOR XXXX SECONDS TASK TIME AND THEN
*    LISTS THE NEXT 50 EXECUTED STATEMENTS.  THE PROGRAM IS TERMINATED
*    BY A PL/1 DUMP WITH AN ID=100.  XXXX IS DECIMAL,POSITIVE AND MUST
*    LIE BETWEEN 0 AND 1200 INCLUSIVE.
*
*     TSGPLOT LISTS THE NEXT XXXX STATEMENTS EXECUTED AND THEN PERMITS
*    THE RESUMPTION OF NORMAL PROCESSING.  XXXX IS DECIMAL,POSITIVE AND
*    MUST LIE BETWEEN 0 AND 50000 INCLUSIVE.
*
*    TSGTIME CAN BE USED TO SET AN EXTERNAL BIT SWITCH, CALLED TSGFLAG,
*    AFETR XXXX SECONDS TASK TIME. IF THE PROGRAMMER HAS NOT DEFINED
*    TSGFLAG AS EXTERNAL BIT(1), THEN TSGTIME ABENDS THE TASK WHEN THE
*    TIME EXPIRES.
*
*    CALLS TO TSGTIME OR TSGLMIT WHILE EITHER OF THESE FUNCTIONS ARE
*    ACTIVE, ARE IGNORED.
*
*    CALLS TO TSGPLOT WHILE PLOTTING IS IN PROGRESS RESET THE COUNTER.
*
*     THIS PROGRAM SHOULD BE ADDED TO THE SYSTEM BY THE FOLLOWING JCL
*    //A JOB
*    //B EXEC ASMFCL
*    //ASM.SYSIN DD *
*        THIS DECK IS INSERTED HERE.
*    //LKED.SYSLMOD DD DSN=SYS1.PL1LIB,DISP=SHR,VOL=,SPACE=
*    //LKED.SYSIN DD *
*        ALIAS TSGPLOT
*        ALIAS TSGTIME
*        NAME TSGLMIT
*    //
         EJECT
*
*              1   COMMON ENTRY SECTION.
*                  ---------------------
*
*                  R1    PRINT MESSAGE POINTER
*                  R2    PRINT MESSAGE LENGTH POINTER
*                  R9    WORK
*                  R10   BASE REGISTER
*                  R12   PL/1 PRV
*                  R13   EXTERNAL SAVE
*                  R14   SPECIAL WORK REGISTER
*                  R15   ENTRY POINT AND SPECIAL BASE REGISTER
*
*              1.1 HOUSEKEEPING ROUTINE.
*
         PUNCH ' LIBRARY *(TSGFLAG)'
TSGLMIT  CSECT
         ENTRY TSGPLOT,TSGTIME
         EXTRN TSGFLAG
*        FLAGS & SWITCHES
TSGLMITF EQU   X'80'                        TSGLMIT HAS BEEN CALLED
TSGPLOTF EQU   X'40'                        TSGPLOT WAS ENTRY POINT
TSGTIMEF EQU   X'20'                        TSGTIME HAS BEEN CALLED
CHAINED  EQU   X'10'                         STATEMENTS ARE CHAINED
LMITOVER EQU   X'08'                         LMIT TIME INTERVAL EXPIRED
ENTRYONE EQU   X'04'                         SET = '1' ON ANY ENTRY
ENTRYTWO EQU   X'02'                         SET IF ASSYNCH ENTRY OCCUR
*                                             WHILE ENTRY ONE IS SET
NOIRB    EQU   X'01'                         SET BEFORE SCHEDULING AN
*                                             BEEN HELD OFF
*                                             ASSYNCH ENTRY WHICH HAS
ALL      EQU   X'FF'                        ADDRESS ALL SWITCHES
         EJECT
         NOPR  0                            PRIMARY ENTRY (LMIT)
TSGPLOT  NOPR  0                            PRIMARY ENTRY (PLOT)
TSGTIME  NOPR  0                            PRIMARY ENTRY (TIME)
TIME     NOPR  0                            TIME EXPIRED ENTRY
INT      NOPR  0                            INTERRUPT PSEUDO ENTRY
DATUM    STM   14,11,12(13)                 SAVE REGISTERS NOT R12/13
         BALR  10,0                         ESTABLISH ADDRESSABILITY
         USING *,10                         ASSIGN BASE REGISTER
         TM    SWITCH,ENTRYONE               CHECK IF SECOND ASSYNCH.
         BO    RASSYNCH                       ENTRY, IF SO HOLD OFF
         OI    SWITCH,ENTRYONE               SET ENTERED SWITCH
         ST    13,SAVE+4                    FORWARD CHAIN
         LA    9,SAVE
         ST    9,8(13)                      BACKWARD CHAIN
         LR    13,9                         -> NEW SAVE AREA
*
*              1.2 ROUTING SECTION.
*
         N     15,CLEARTOP                  CLEAR FLAG BYTE ON EP
         L     9,ADATUM                     SET DATUM VALUE
         SR    9,15                         OBTAIN EP/DATUM DIFFERENCE
         S     9,TWO                        REDUCE
         BZ    INT1                         EP INT ? YES,BRANCH
         S     9,TWO                        REDUCE
         BZ    CHAIN3                       EP TIME? YES,BRANCH
         S     9,TWO                        REDUCE
         BZ    TSGT1                        EP TSGTIME? YES,BRANCH
         S     9,TWO                        REDUCE
         BZ    TSGP1                        EP TSGPLOT? YES,BRANCH
         B     TSGL1                        TSGLMIT EP, BRANCH
*
*              1.3 SET INDICATORS.
*
*              1.3.1 TSG LIMIT SECTION.
*
TSGL1    TM    SWITCH,TSGLMITF+TSGTIMEF     TIME OR LMIT ALREADY CALLED
         BNZ   TSGL2                        YES, BRANCH FOR ERROR
         OI    SWITCH,TSGLMITF              SET CALLED FLAG
         B     SETT1                        BRANCH TO TIMER SECTION
TSGL2    MVC   M3+11(4),LMESS               SET MESSAGE CODE 1ST ENTRY
         B     MERROR                       BRANCH TO ERROR PRINT SECT
*
*              1.3.2 TSG PLOT SECTION.
*
TSGP1    OI    SWITCH,TSGPLOTF              SET ENTRY FLAG
         B     SETT1                        BRANCH TO TIMER FOR ARB
*
*              1.3.3 TSG TIMER SECTION.
*
TSGT1    TM    SWITCH,TSGTIMEF+TSGLMITF     TIME OR LMIT ALREADY CALLED
         BNZ   TSGT2                        YES, BRANCH FOR ERROR
         OI    SWITCH,TSGTIMEF              SET CALLED FLAG
         L     9,=A(TSGFLAG)                FIND 'TIME UP' FLAG
         LTR   9,9                          IS THERE ONE ?
         BZ    SETT1                        NO, BRANCH TO CHAINING SECT
         L     9,0(9)                       YES:
         MVI   0(9),X'00'                   THEN TURN OFF AND
         B     SETT1                        BRANCH TO CHAINING SECTION
TSGT2    MVC   M3+11(4),TMESS               SET MESSAGE CODE 1ST ENTRY
         B     MERROR                       BRANCH TO ERROR PRINT SECT
*
*              1.4 REAL ENTRY SECTION FOR INTERRUPT ENTRY. THIS
*                  PROVIDES FOR AN INITIAL INTERRUPT CHECKING.
*
INTERPT  EQU   *
         USING *,15
         STM   13,14,SPECSAVE               SAVE R13-R14
         CLC   6(2,1),OPER                  TEST PIE FOR OP INTERRUPT
         BNE   INTRET                       NOT OP,BRANCH TO IHEERR
         TM    8(1),X'40'                   ILC 1 OR 3 1/2 WORDS?
         BO    INTRET                       YES, BRANCH TO IHEERR
         L     14,8(1)                      LOAD NEXT INST ADDRESS
         N     14,CLEARTOP                  CLEAR FLAG BYTE
         S     14,FOUR                      REDUCE TO ERROR ADDRESS
         CLI   0(14),X'9A'                  INVALID OP '9A'?
         BNE   INTRET                       NOT '9A', BRANCH TO IHEERR
         LM    13,14,SPECSAVE               RELOAD REGISTERS
         L     15,AINT                      LOAD PSEUDO ENTRY ADDRESS
         BR    15                           BRANCH TO PSEUDO ENTRY
INTRET   LA    15,RETURNE                   SET RETURN ROUTINE ADDR.
         BR    15                           GO TO IHERR VIA ROUTINE
         DROP  15
*
*              1.5 ERRONEOUS ENTRY, MESSAGE PRINT SECTION.
*
MERROR   TM    SWITCH,TSGLMITF              PREVIOUS CALL TO TSGLMIT ?
         BNO   ERR1                         NO
         MVC   M3+29(4),LMESS               SET MESSAGE CODE 2ND ENTRY
         B     ERR3                         OUTPUT MESSAGE
ERR1     TM    SWITCH,TSGTIMEF              PREVIOUS CALL TO TSGTIME ?
         BO    ERR2                         YES
         BAL   0,ABEND                      SHOULD NOT OCCUR
ERR2     MVC   M3+29(4),TMESS               SET MESSAGE CODE 2ND ENTRY
ERR3     LA    1,M3                         LOAD MESSAGE ADDRESS
         LA    2,LM3                        LOAD MESSAGE LENGTH ADDRESS
         L     15,VIHEPRTB                  LOAD PRINT ROUTINE ADDRESS
         BALR  14,15                        BRANCH TO PRINT
         B     RETURN                       RETURN TO PL/1 VIA ROUTINE
*
*              1.6 DEFAULT VALUES TAKEN FOR PARAMETER
*
DEFAULT  TM    SWITCH,TSGLMITF              LMIT ENTRY ?
         BZ    DEF1                         NO
         MVC   M5+3(15),LMESS               YES, INSERT 'LMIT 90 SEC..'
DEF1     TM    SWITCH,TSGTIMEF              TIME ENTRY ?
         BZ    DEF2                         NO
         MVC   M5+3(15),TMESS               YES, INSERT 'TIME 90 SEC..'
DEF2     TM    SWITCH,TSGPLOTF              PLOT ENTRY?
         BZ    DEF3                         NO
         MVC   M5+3(15),PMESS               YES, INSERT 'PLOT 50 ENT..'
DEF3     LA    1,M5
         LA    2,LM5
         L     15,VIHEPRTB                  CALL PRINT ROUTINE
         BR    15                           RETURN ON 14
         EJECT
*
*              2   SET TIMER SECTION.
*                  ------------------
*
*                  R1    PARAMETER POINTER
*                  R8    WORK
*                  R9    WORK
*                  R10   BASE REGISTER
*                  R12   PL/1 PRV
*                  R13   EXTERNAL SAVE
*
*              2.1 OBTAIN AND STORE THE RB ADDRESS.
*
SETT1    LA    9,16                         LOAD ADDRESS OF CVT POINTER
         L     9,0(9)                       LOAD ADDRESS OF CVT
         L     9,0(9)                       LOAD ADDRESS OF TCB WORDS
         L     9,4(9)                       LOAD ADDRESS OF TCB
         L     9,0(9)                       LOAD ADDRESS OF RB
         ST    9,ARB                        STORE A(RB) IN ARB
         TM    SWITCH,TSGPLOTF              TSGPLOT ENTRY ?
         BO    CHAIN1                       YES, SKIP TO NEXT SECT
*
*              2.2 RETRIEVE AND CHECK TIME PARAMETER.
*
         L     9,0(1)                       LOAD ADDRESS OF PARM
         CLI   0(1),X'80'                   TEST FOR LAST (ONLY) PARM
         BNE   SETT2                        NOT LAST,ARRANGE DEFAULT
         L     9,0(9)                       LOAD IN PARAMETER
         LTR   9,9                          TEST PARM
         BM    SETT2                        MINUS, ARRANGE DEFAULT
         C     9,TIMELIM                    COMPARE AGAINST LIMIT
         BH    SETT2                        HIGH, ARRANGE DEFAULT
         B     SETT3                        OK BRANCH TO SET TIMER
*
*              2.3 INVALID TIME PARAMETER, SET TO DEFAULT.
*
SETT2    L     9,TIMEDEF                    SET DEFAULT
         BAL   14,DEFAULT                   WARN DEFAULT TAKEN
*
*              2.4 SET UP TIMER FOR REQUIRED CENTISECONDS AND RETURN.
*
SETT3    M     8,HUNDRED                    CONVERT TO 1/100'S OF SEC.
         ST    9,TIMEPARM                   STORE TIME PARAMETER
         L     9,ATIME                      SET UP TIMER RETURN ADDRESS
         STIMER TASK,(9),BINTVL=TIMEPARM    SET TIME LIMIT
         B     RETURN                       RETURN TO PL/1 VIA ROUTINE
         EJECT
*
*              3   CHAINING SECTION.
*                  -----------------
*
*                  R1    PARAMETER POINTER
*                  R2    FUNCTION REGISTER (TRT INSTRUCTION)/WORK
*                  R7    REMAINING LOAD MODULE LENGTH
*                  R8    LOAD MODULE SEARCH POINTER
*                  R9    WORK
*                  R10   BASE REGISTER
*                  R12   PL/1 PRV
*                  R13   EXTERNAL SAVE
*                  R14   LINKAGE
*
*              3.1 TSGPLOT ENTRY. CHECK THAT TSGLMIT IS NOT TERMINATING
*                  THE PROGRAM, THEN RETRIEVE AND CHECK ENTRY COUNT
*                  PARAMETER.
*
CHAIN1   TM    SWITCH,LMITOVER               LMIT TERMINATING PROGRAM?
         BO    RETURN                       YES, IGNORE TSGPLOT CALL
         L     9,0(1)                       LOAD ADDRESS OF PARM
         CLI   0(1),X'80'                   TEST FOR LAST (ONLY) PARM
         BNE   CHAIN2                       NOT LAST, ARRANGE DEFAULT
         L     9,0(9)                       LOAD PARAMETER
         LTR   9,9                          TEST PARM
         BM    CHAIN2                       MINUS, ARRANGE DEFAULT
         C     9,ENTLIM                     CHECK AGAINST LIMIT
         BH    CHAIN2                       HIGH, ARRANGE DEFAULT
         ST    9,ENTPARM                    STORE ENTRY COUNT
         B     CHAIN2A
CHAIN2   MVC   ENTPARM,ENTDEF               SET PARM TO DEFAULT
         BAL   14,DEFAULT                   WARN DEFAULT TAKEN
*
*                  3.2 SET CHAINING SWITCH, TEST IF CHAINED
*
CHAIN2A  TM    SWITCH,CHAINED                ALREADY CHAINED ?
         BO    CHAIN7A                      YES, SKIP CHAINING
         B     CHAIN4                       NO, CHAIN
*
*              3.3 TSGLMIT AND TSGTIME ENTRIES. BRANCH APPROPRIATELY.
*
CHAIN3   TM    SWITCH,TSGLMITF              DID TSGLMIT CAUSE THIS?
         BNO   CHAIN20                      NO, SET TSGFLAG
         OI    SWITCH,LMITOVER               TERMINATION IN PROGRESS
         TM    SWITCH,CHAINED               CHAIN BUILT ALREADY ?
         BO    CHAIN7A                      YES, SKIP CHAINING
*
*              3.4 ARRANGE STATEMENT NUMBER CHAINING.
*
*              3.4.1 INITIALIZE LOAD MODULE SEARCH.
*
CHAIN4   L     8,ARB                        LOAD REQUEST BLOCK ADDRESS
         L     9,16                         -> CVT
         TM    116(9),MVT                   IS CONTROL PROG MVT ?
MVT      EQU   X'10'
         BZ    CHAIN4D                      NO
*        THIS IS THE MVT DEPENDANT CODE TO FIND THE ORIGIN OF THE   MVTX
               LOAD MODULE AND ITS LENGTH                           MVT
         L     8,12(8)                      8-> CDE                 MVT
         TM    0(8),X'04'                   MINOR CDE?              MVT
         BZ    CHAIN4A                      NO                      MVT
         L     8,20(8)                      YES, -> MAJOR CDE       MVT
CHAIN4A  L     7,20(8)                      -> EXTENT LIST          MVT
*   NEXT INSTRUCTION LOADS THE LENGTH OF THE LOAD MODULE, SCATTER LOAD X
               ONLY EXAMINES THE FIRST BLOCK.                       MVT
         L     2,8(7)                                               MVT
         LA    2,0(2)                       MODULE LENGTH IN R2     MVT
CHAIN4B  TM     8(7),X'80'                  ALLOW FOR A SCATTER     MVT
         BO    CHAIN4C                      EXTENT LIST             MVT
         LA    7,4(7)                                               MVT
         B     CHAIN4B                                              MVT
CHAIN4C  L     8,12(7)                      -> LOAD MODULE          MVT
         LR    7,2                          LENGTH NOW IN R7        MVT
         B     CHAIN4E                                              MVT
CHAIN4D  EQU   *                                                    MFT
*        THIS IS THE MFT DEPENDANT CODE TO FIND THE ORIGIN OF THE   MFT,
               LOAD MODULE AND ITS LENGTH.
         LH    7,8(8)                       GET LOAD MODULE LENGTH
         SLA   7,3                          CONVERT TO BYTES
         LA    8,32(8)                      POINT TO START OF MODULE
         S     7,THIRTY2                    ADJUST LENGTH REMAINING
CHAIN4E  EQU   *                            COMMON CODE AGAIN
         C     8,ADATUM                     CHECK LOAD MODULE INCLUDES
         BH    CHAIN4F                      NO         ×__ THIS CODE
         LA    9,0(7,8)                     CHECK UPPER BOUND
         C     9,ADATUM
         BNL   CHAIN4H                      O.K.
CHAIN4F  EQU   *                            THIS MUST BE THE LOADER
         L     9,16                         -> CVT
         L     9,0(9)                       -> NEXT & CURRENT
         L     9,4(9)                       -> TCB
         L     9,112(9)                     -> FIRST SAVE AREA
         L     8,16(9)                      E.P.A. LOADER
         CLC   5(8,8),=C'IEWLCTRL'          IS LOADER ?
         BNE   CHAIN4G                      NO, THEN WE ARE LOST
         L     9,8(9)                       -> PROGRAM FIRST SAVE AREA
         L     9,44+12+8(9)                 R11 OF LOADER -> CONTROL
         LM    8,9,0(9)                     LOADER CONTROL BLOCK
         LR    7,9
         C     8,ADATUM                     SAME CHECKS AS BEFORE
         BH    CHAIN4G                      TO VERIFY THAT
         LA    9,0(7,8)                     THIS MODULE LIES
         C     9,ADATUM                     WITHIN THE AREA
         BNL   CHAIN4H                      OF R8 -> R7+R8
CHAIN4G  EQU   *
*
*              IF WE GET TO HERE, WE ARE UNABLE TO DELIMIT THE LOAD    X
               MODULE. IF WE ARE IN TERMINATION WE ABEND, OTHERWISE WE X
               WRITE A MESSAGE TO THE PROGRAMMER, AND IGNORE THE CALL
*
         LA    1,M4                         PUT
         LA    2,LM4                        OUT
         L     15,VIHEPRTB                  MESSAGE
         BALR  14,15
         TM    SWITCH,LMITOVER              IN TERMINATION ?
         BNO   RETURN                       NO
         BAL   0,ABEND                      YES
*
CHAIN4H  EQU   *
         ST    8,ALAST                      INITIAL OFFSET
         ST    8,AFIRST                     DITTO, FOR UNPICKING LATER
*
*              3.4.2 SEARCH FOR 'MVI' INSTRUCTION, BY HALFWORDS.
*
CHAIN5   CLI   0(8),X'92'                   TEST FOR MVI
         BE    CHAIN8                       BRANCH IF ONE FOUND
CHAIN6   S     7,TWO                        IF NOT, ADJUST LENGTH
         LA    8,2(8)                       POINT TO NEXT HALFWORD
         LTR   7,7                          TEST FOR COMPLETION
         BP    CHAIN5                       IF NOT, CONTINUE SCAN
*
*              3.4.3 ENTIRE LOAD MODULE SEARCHED, ISSUE SPIE & RETURN.
*
CHAIN7   SPIE  INTERPT,((1,13),15)          ISSUE FOR PL/1 INTERRUPTS
         ST    1,APICA                      STORE OLD PICA ADDRESS
CHAIN7A  XC    ENTRIES,ENTRIES              ZERO ENTRY COUNT
         OI    SWITCH,CHAINED               ALL  DONE  FUN
         TM    SWITCH,LMITOVER              TERMINATION ?
         BNO   RETURN                       NO
         MVC   ENTPARM,ENTDEF               YES, SET COUNT (= DEFAULT)
         B     RETURN
*
*              3.4.4 MVI FOUND CHECK FOR VALIDITY AND CHAIN IN.
*
CHAIN8   CLC   OP1,2(8)                     CHECK FIRST OPERAND
         BNE   CHAIN6                       INVALID, CONTINUE SCAN
         CLC   LASTNO,1(8)                  CHECK IMMEDIATE DATA
         BE    CHAIN6                       INVALID, CONTINUE SCAN
CHAIN9   MVC   LASTNO,1(8)                  STORE NEW STAT NO.
         MVI   0(8),X'9A'                   MOVE INVALID OPERATION IN
         LR    9,8                          COPY POINTER
         S     9,ALAST                      CALCULATE OFFSET
         STH   9,2(8)                       STORE OFFSET
         ST    8,ALAST                      UPDATE LASTFIND ADDRESS
*
*              3.4.5 MVI FOUND. SEARCH FOR MATCHING INSTRUCTION
*                    IN INSTRUCTION LENGTHS.
*
CHAIN10  S     7,FOUR                       ADJUST LENGTH REMAINING
         LA    8,4(8)                       POINT TO NEXT POSITION
         LTR   7,7                          END OF MODULE?
         BP    CHAIN11                      NO, SEARCH ON
         BAL   14,UNCHAIN1                  YES, UNPICK ODD MVI
         B     CHAIN7                       BRANCH TO SET PIE
CHAIN11  SR    2,2                          CLEAR TRT FUNCTION REG
         TRT   0(1,8),TABLE                 TEST INSTRUCTION TYPE
         BZ    CHAIN16                      VALID/NON-SPECIAL
         CL    2,B                          TEST FOR BRANCH
         BE    CHAIN12                      YES
         CL    2,M                          TEST FOR MVI
         BE    CHAIN15                      YES
         BAL   14,UNCHAIN1                  INVALID OP,UNCHAIN LAST MVI
         B     CHAIN5                       CONTINUE SEARCH
*
*              3.4.6 BRANCH INSTRUCTION FOUND, ADJUST REMAINING LENGTH
*                    AND RESTART SEARCH FOR 'MVI'.
*
CHAIN12  TM    0(8),X'F0'                   TEST TO FIND BRANCH TYPE
         BM    CHAIN13                      RX,BRANCH TO RX SECTION
         S     7,TWO                        RR, ADJUST LENGTH LEFT
         LA    8,2(8)                       POINT TO NEXT INSTRUCTION
         B     CHAIN14                      BRANCH TO END TEST
CHAIN13  S     7,FOUR                       RX/RS TYPE, UPDATE LENGTH
         LA    8,4(8)                       POINT TO NEXT INSTRUCTION
CHAIN14  LTR   7,7                          END OF MODULE?
         BNP   CHAIN7                       YES, BRANCH TO SET PIE
         B     CHAIN5                       NO, SCAN ON
*
*              3.4.7 MVI INSTRUCTION FOUND, CHECK AS BEFORE. IF VALID
*                    UPDATE AS BEFORE.
*
CHAIN15  CLC   OP1,2(8)                     FIRST OPERAND OK?
         BNE   CHAIN10                      NO, CONTINUE SCAN
         CLC   LASTNO,1(8)                  YES, IMMEDIATE DATA OK?
         BE    CHAIN10                      NO, CONTINUE SCAN
         B     CHAIN9                       YES, CHAIN IN
*
*              3.4.8 VALID OPERATION FOUND. ADVANCE TO NEXT INSTRUCTION
*                    AND CONTINUE SEARCH FOR A BRANCH.
*
CHAIN16  CLI   0(8),X'C0'                   TEST FOR LENGTH
         BL    CHAIN17                      NOT SS, TRY AGAIN
         S     7,SIX                        SS, ADJUST LENGTH LEFT
         LA    8,6(8)                       POINT TO NEXT INSTRUCTION
         B     CHAIN19                      BRANCH TO END TEST
CHAIN17  CLI   0(8),X'40'                   RX/RS/SI TYPE ?
         BL    CHAIN18                      NO, BRANCH FOR RR TYPE
         S     7,FOUR                       RX ETC, UPDATE LENGTH LEFT
         LA    8,4(8)                       POINT TO NEXT INSTRUCTION
         B     CHAIN19                      BRANCH TO END TEST
CHAIN18  S     7,TWO                        RR TYPE, ADJUST LENGTH
         LA    8,2(8)                       POINT TO NEXT INSTRUCTION
CHAIN19  LTR   7,7                          END OF LOAD MODULE?
         BP    CHAIN11                      NO, SCAN ON
         BAL   14,UNCHAIN1                  YES, UNPICK ODD MVI
         B     CHAIN7                       BRANCH TO SET PIE
*
*              TSGTIME ONLY. SET TSGFLAG,OR ABEND
*
CHAIN20  L     9,=A(TSGFLAG)                LOCATE TIME UP FLAG
         LTR   9,9                          IF THIS DOES NOT
         BNZ   CHAIN20A                      EXIST, THEN
         ABEND 128,DUMP                      ABEND. OTHERWISE
CHAIN20A L     9,0(9)                        SET IT TO 'FF' TO
         MVI   0(9),X'FF'                    SHOW TIME UP.
         NI    SWITCH,ALL-TSGTIMEF
         B     RETURN                       RETURN TO PL/1 VIA ROUTINE
*
*              3.6 ROUTINE TO UNPICK INVALID LAST LINK IN CHAIN.
*
UNCHAIN1 L     9,ALAST                      LOAD LAST LINK ADDRESS
         MVI   0(9),X'92'                   REINSERT 'MVI' OPCODE
         LH    2,2(9)                       SAVE OFFSET
         N     2,HALFPOS                    UNSIGNED 16 BIT
         MVC   2(2,9),OP1                   REINSERT FIRST OPERAND
         SR    9,2                          CALCULATE NEW LASTFIND
         ST    9,ALAST                      STORE IT
         BR    14                           RETURN TO POINT OF CALL
         EJECT
*
*              4   ANALYZE INTERRUPT, OUTPUT MESSAGE IF APPROPRIATE.
*                  -------------------------------------------------
*
*                  R1    PARAMETER POINTER
*                  R2    IHEPRT LINKAGE
*                  R8    WORK
*                  R9    WORK
*                  R10   BASE REGISTER
*                  R12   PL/1 PRV
*                  R13   SAVE AREA POINTER USUALLY EXTERNAL AREA
*                  R14   ROUTINE LINKAGE
*                  R15   ROUTINE LINKAGE
*
*
*              4.1 REQUIRED INTERRUPT, UPDATE PL/1 SAVE AREA AND LOAD
*                  STATEMENT NUMBER FOR PRINTING. UPDATE AND CHECK
*                  ENTRY COUNT.
*
INT1     L     9,8(1)                       LOAD NEXT INSTR. ADDR.
         N     9,CLEARTOP                   CLEAR FLAG BYTE
         S     9,FOUR                       REDUCE TO REQ. INSTR ADDR
         L     7,SAVE+4                     7-> ESA
         MVC   99(1,7),1(9)                 STMT NO. TO ESA
         L     9,ENTRIES                    LOAD ENTRY COUNT
         LA    9,1(9)                       INCREMENT COUNT
         ST    9,ENTRIES                    STORE COUNT
         C     9,ENTPARM                    CHECK AGAINST ENTRY LIMIT
         BH    INT3                         HIGH, BRANCH TO END
         LH    9,98(7)                      STMT NO. -> R9
         CVD   9,DWORK                      CONVERT TO DECIMAL
         UNPK  M1,DWORK+5(3)                UNPACK TO O/P AREA
         OI    M1+4,X'F0'                   CLEAR UNWANTED SIGN
         C     7,LASTSAVE                   SAME PROC AS BEFORE ?
         BE    INT2                         YES, THEN MESSAGE M1 IS OK
*
*              4.1.1 FIND THE NAME OF THE CURRENT PROCEDURE
*
         LR    8,7                          POINT R8 AT ESA
INT1A    LR    9,8                          POINT R9 AT THIS SAVE
         L     8,4(9)                       POINT R8 AT LSA(9)
         TM    0(9),X'80'                   THIS SAVE = DSA ?
         BZ    INT1A                        NO, CHAIN BACK
         TM    0(9),X'10'                   THIS DSA FOR BEGIN ?
         BO    INT1A                        YES, CHAIN BACK
         CLI   0(8),X'25'                   SPECIAL CASE, CHECK FOR
         BE    INT1A                         ON UNIT. BRANCH IF SO.
         CLI   0(8),X'29'                   PRV VDA ?
         BNE   INT1A2                       NO, BUT IF IT WAS THEN
         L     8,4(8)                        CHAIN BACK TO ESA(MAIN)
         B     INT1A5
INT1A2   TM    0(8),X'80'                   TEST 8 POINTS TO DSA
         BO    INT1A5                       IF SO CONTINUE
         L     8,4(8)                       OTHERWISE FIND THE DSA
         B     INT1A2
INT1A5   L     8,16(8)                      EP OF THIS PROC -> R8
         MVC   PROC,5(8)                    MOVE IN PROC NAME
         IC    8,4(8)                       NAME LENGTH TO R8
         LA    8,6(8)                       ADJUST THE LENGTH OF
         STC   8,LM1+1                       MESSAGE 1
*
*              4.1.2 CHECK IF CURRENT BLOCK IS A BEGIN BLOCK.  IF IT IS
*                    THEN CHECK THAT IT IS CONTAINED IN THE CURRENT
*                    PROC BY COMPARING THE ADDRESSES OF STATIC
*
         MVI   M1+5,C' '               BLANK 'BLOCK NOT IN THIS PROC'
         TM    0(7),X'10'                   CURRENT DSA BEGIN?
         BZ    INT1B                        NOT BEGIN BLOCK
*    THIS CODE DOES NOT WORK: GOODNESS KNOWS WHY
         CLC   65(3,7),65(9)           COMPARE ADDRESSES OF STATIC FOR
         BE    INT1B                         CURRENT BLOCK AND CURRENT
         MVI   M1+5,C'*'                     PROC: IF DIFFERENT THEN
INT1B    EQU   *                             MARK 'PROC NOT IN BLOCK'
         ST    7,LASTSAVE                   AVOID REPETITION OF CODE
*
*              4.2 PRINT MESSAGE AND RETURN TO SUPERVISOR.
*
INT2     LA    1,M1                         POINT TO MESSAGE
         LA    2,LM1                        POINT TO LENGTH  OF MESSAGE
         L     15,VIHEPRTB                  LOAD PRINT ROUTINE ADDRESS
         BALR  14,15                        BRANCH TO PRINT ROUTINE
         B     RETURN                       RETURN TO OS VIA ROUTINE
*
*              4.3 REQUIRED STATEMENTS OUTPUT, PUT OUT AN END MESSAGE,
*                  DECIDE AND EXECUTE RETURN TO OS.
*
INT3     TM    SWITCH,LMITOVER               TERMINATION ?
         BNO   INT4                          NO
         MVC   M2+10(4),LMESS               INSERT LMIT MESSAGE
         B     INT5                         SKIP ROUND
INT4     MVC   M2+10(4),PMESS               INSERT PLOT MESSAGE
INT5     LA    1,M2                         POINT TO END MESSAGE
         LA    2,LM2                        POINT TO MESSAGE LENGTH
         L     15,VIHEPRTB                  LOAD PRINT ROUTINE ADDRESS
         BALR  14,15                        BRANCH TO PRINT
         BAL   14,INT7                      BRANCH TO UNPICK ROUTINE
         TM    SWITCH,LMITOVER               TERMINATION ?
         BNO   INT6                          NO
         LA    1,ADUMC                      LOAD ADDRESS OF PARMLIST
         L     13,SAVE+4                    POINT 13 AT LSA
         ST    1,24(13)                     ALTER SAVE AREA TO NEW R1
         MVC   12(4,13),VIHEDUMP            ALTER FOR BRANCH ADDRESS
         B     RETURN                       BRANCH VIA ROUTINE
INT6     L     1,APICA                      LOAD OLD PICA ADDRESS
         SPIE  ,MF=(E,(1))                  SET PIE EXIT ADDRESS IHEERR
         B     RETURN
*
*              4.4 ROUTINE TO UNPICK ENTIRE CHAIN.
*
INT7     L     9,ALAST                      LOAD ADDRESS OF CHAIN END
         NI    SWITCH,ALL-CHAINED           TURN OFF 'CHAINED' FLAG
INT8     C     9,AFIRST                     CHECK FOR END OF CHAIN
         BCR   13,14         (BNH)          END,RETURN TO CALL POINT
         CLI   0(9),X'9A'                   CHECK OPCODE
         BE    INT9                         OURS, UNPICK
         S     9,ERRHALF                    NOT OURS, WE MUST HAVE
         B     INT8                         OVERFLOWED, SO CORRECT R9
INT9     LH    8,2(9)                       PRESERVE OFFSET
         N     8,HALFPOS                    UNSIGNED 16 BIT
         MVC   2(2,9),OP1                   RESTORE FIRST OPERAND
         MVI   0(9),X'92'                   PUT BACK 'MVI' OPCODE
         SR    9,8                          CALCULATE NEXT ITEM ADDRESS
         B     INT8                         GO ROUND AGAIN
         EJECT
*
*              5   COMMON RETURN ROUTINES.
*                  -----------------------
*
*                  R10   BASE REGISTER
*                  R12   PL/1 PRV
*                  R13   SAVE AREA POINTER
*                  R14   RETURN LINK
*                  R15   TRANSFER LINK AND SPECIAL BASE REG
*
*              5.1 STANDARD RETURN ROUTINE.
*
RETURN   NI    SWITCH,ALL-TSGPLOTF-ENTRYONE-NOIRB
         L     13,SAVE+4                     LOAD EXTERNAL SAVE AREA
         TM    SWITCH,ENTRYTWO               ASSYNCH ENTRY HELD OFF?
         LM    14,11,12(13)
         BCR   14,14                         NO, RETURN
         BALR  15,0                          YES
         USING *,15                          ESTABLISH TEMPORARY BASE
         NI    SWITCH,ALL-ENTRYTWO           SINGLE THREAD AGAIN
         OI    SWITCH,NOIRB                  NO IRB THO' PROCESSING A
*                                             TIMER INTRERUPT
         L     15,ATIME                      SCHEDULE THE
         DROP  15                             TIMER INTERUPT PROCESSING
         BR    15                             THAT WAS HELD OFF
*
*              5.2 PASS CONTROL TO PL/1 ERROR HANDLER.
*
RETURNE  EQU   *
         USING *,15
         LM    13,14,SPECSAVE               RELOAD REGISTERS
         L     15,APICA                     LOAD OLD PICA ADDRESS
         L     15,0(15)                     LOAD IHEERR ADDRESS
         BR    15                           BRANCH TO IHEERR
         DROP  15
*
*              5.3 SET ENTRYTWO TO CAUSE THE PROCESSING HELD OFF HERE
*                  TO BE DONE SINGLE THREAD
*
RASSYNCH OI    SWITCH,ENTRYTWO
         LM    14,11,12(13)
         BR    14
*
*              5.3 ABEND WHEN UNEXPECTED ERROR IS DETECTED, R0 POINTS
*                  TO CODE FOLLOWING THE POINT WHERE THE ERROR WAS
*                  DETECTED
*
ABEND    ABEND 999,DUMP
         EJECT
*
*
*              6   DEFINE CONSTANTS AND STORAGE.
*                  -----------------------------
*
DWORK    DC    D'0'                         DOUBLE WORD WORK AREA
SAVE     DC    17F'0'                       MAJOR INTERNAL SAVE AREA
SPECSAVE DC    2F'0'                        STORAGE FOR R13,14
LASTSAVE DC    A(0)                         FOR OPTIMISATION IN 4.1
ENTPARM  DC    F'0'                         ENTRIES PARAMETER
ENTDEF   DC    F'50'                        ENTRIES DEFAULT
ENTLIM   DC    F'50000'                     LIMIT TO TSGPLOT PARM
ENTRIES  DC    F'0'                         COUNT OF ENTRIES TO MODULE
TIMEPARM DC    F'0'                         TIME PARAMETER
TIMEDEF  DC    F'90'                        TIME DEFAULT
TIMELIM  DC    F'1200'                      TIME LIMIT
ADATUM   DC    A(DATUM)                     DATUM POINT FOR EP'S
AINT     DC    A(INT)                       INT EP ADDRESS
ATIME    DC    A(TIME)                      TIME EP ADDRESS
ARB      DC    F'0'                         REQUEST BLOCK ADDRESS
APICA    DC    F'0'                         ADDRESS OF OLD PICA
AFIRST   DC    A(0)                         LOW CORE END OF STMT CHAIN
ALAST    DC    F'0'                         ADDRESS OF LAST LINK
ADUMC    DC    X'80'
         DC    AL3(DUMC)                    ADDRESS OF DUMPCODE
VIHEPRTB DC    V(IHEPRTB)                   ADDRESS OF IHEPRTB
VIHEDUMP DC    V(IHEDUMP)                   ADDRESS OF DUMP ROUTINE
B        DC    X'000000'
         DC    C'B'                         CONSTANT
M        DC    X'000000'
         DC    C'M'                         CONSTANT
TWO      DC    F'2'                         CONSTANT
FOUR     DC    F'4'                         CONSTANT
SIX      DC    F'6'                         CONSTANT
THIRTY2  DC    F'32'                        CONSTANT
HUNDRED  DC    F'100'                       CONSTANT
DUMC     DC    F'100'                       DUMPCODE
HALFPOS  DC    X'0000FFFF'                  -> UNSIGNED BIN(16)
ERRHALF  DC    X'00010000'                  IN CASE BIN(16) IS TOO SMAL
CLEARTOP DC    X'00FFFFFF'                  AND FIELD
OP1      DC    X'D063'                      CHECK FOR FIRST OPERAND
OPER     DC    H'1'                         CHECK FOR OPERATION INT.
LASTNO   DC    X'00'                        LAST STAT NO FOUND
SWITCH   DC    X'00'                        INDICATOR BYTE
LM1      DC    H'5'                         LENGTH OF MESSAGE 1
M1       DC    CL5' ',C' '                  STATEMENT NUMBER MESSAGE
PROC     DC    CL8' '                       STATEMENT PROC.  MESSAGE
LM2      DC    H'14'                        LENGTH OF MESSAGE TWO
M2       DC    C'END OF TSGXXXX'            END MESSAGE
LM3      DC    H'50'                        LENGTH OF ERROR MESSAGE
M3       DC    C'CALL TO TSGXXXX IGNORED - TSGXXXX CURRENTLY ACTIVE'
LMESS    DC    C'LMIT'                      INSERT FOR MESSAGE
         DC    C' 90 SECONDS'                    ''
PMESS    DC    C'PLOT'                      INSERT FOR MESSAGE
         DC    C' 50 ENTRIES'                    ''
TMESS    DC    C'TIME'                      INSERT FOR MESSAGE
         DC    C' 90 SECONDS'                    ''
LM4      DC    0H'0',AL2(L'M4)
M4       DC    C'NO STATEMENT TRACE, PROGRAM EXTENT NOT KNOWN'
M5       DC    C'TSGXXXXXXXXXXXXXXX (DEFAULT) TAKEN'
LM5      DC    0H'0',AL2(L'M5)
*
TABLE    DC    4C'I'                        TRANSLATE AND TEST TABLE
         DC    X'00'
         DC    3C'B'
         DC    3X'00'
         DC    5C'I'
         DC    53X'00'
         DC    3C'B'
         DC    5X'00'
         DC    C'I'
         DC    3X'00'
         DC    3C'I'
         DC    13X'00'
         DC    6C'I'
         DC    10X'00'
         DC    7C'I'
         DC    9X'00'
         DC    C'I'
         DC    4X'00'
         DC    2C'B'
         DC    10X'00'
         DC    C'M'
         DC    6X'00'
         DC    3C'I'
         DC    4X'00'
         DC    49C'I'
         DC    7X'00'
         DC    4C'I'
         DC    4X'00'
         DC    17C'I'
         DC    3X'00'
         DC    4C'I'
         DC    6X'00'
         DC    2C'I'
         END
