*
         TITLE 'XM210410  COMPARE RECORDS                             '
XM210410 CSECT
         ENTRY XM210400
XM210400 EQU   *
         PRINT NOGEN
         COPY  REGEQU
         EJECT
***********************************************************************
*                                                                     *
*    MODULE XM210410  PROGRAMMER R.A.WHITELEY       ASSIGNED 25/04/74 *
*                                                                     *
*                           MODULE FUNCTIONS                          *
*                           ----------------                          *
*  MODULE COMPARES RECORDS PASSED AT P1 & P2. ANY INEQUALITY IS       *
*   INDICATED IN THE APPROPRIATE BYTE OF THE UNDERLINE AREA BY        *
*   SETTING IT TO X'11'. THE UNDERLINE AREA WILL CONTAIN X'00' ON     *
*   ENTRY OR X'22' IF THE FIELD IS A KEY FIELD.                       *
*  IF THE RECORDS ARE UNEQUAL THEN CORRESP.BYTES IN U'LINE SHOULD BE  *
*   BE ORED TO X'44'(THIS MEANS PARTS OF RECORD NOT COMPARED WILL     *
*   CONTAIN X'00').                                                   *
*  IF THE COMPARE FIELD IS THE WRONG FORMAT THEN CORRESPONDING        *
*   BYTES IN U'LINE ARE SET TO X'88'.                                 *
*  THE INDICATOR AT P6 IS SET ACCORDING TO ACTION TAKEN               *
         SPACE 2
*                             PARAMETERS                              *
*                             ----------                              *
*  1. DATA SET 1 RECORD                 - LENGTH SEE P5     I         *
*                                                                     *
*  2. DATA SET 2 RECORD                 - LENGTH SEE P5     I         *
*                                                                     *
*  3. UNDERLINE AREA                    - LENGTH = MAX.OF   I/O       *
*                                                  P1 OR P2           *
*  4. COMPARE FIELD TABLE               - 11 X 6 BYTES      I         *
*        EACH ENTRY CONSISTS OF                                       *
*         1 BYTE FORMAT IND.  B=BINARY, P=PACKED, M=BITS, R = COMPARE *
*         1 BYTE MASK IF FORMAT IS M                  BINARY REMAINDR *
*         2 BYTE BINARY DISPLACEMENT OF FIELD WITHIN RECORD           *
*         2 BYTE BINARY LENGTH OF FIELD                               *
*           X'FF' DENOTES LAST ENTRY IN TABLE                         *
*                                                                     *
*  5. RECORD LENGTHS (DS1 & DS2)        - 2 HALFWORDS       I         *
*                                                                     *
*  6. ACTION INDICATOR                                                *
*        C'X' = COMPARE FIELD NOT PRESENT (IN BOTH RECORDS) O         *
*        C'W' = RECORDS UNEQUAL                             O         *
*        C' ' = RECORDS EQUAL                               I         *
*                                                                     *
*                                                                     *
*                           SPECIAL NOTES                             *
*                           -------------                             *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         ISEQ  73,80
         MODIN
*
** SET UP R11 TO POINT AT P6, THE ACTION INDICATOR
*  -----------------------------------------------
         L     R11,20(R1)          POINT R11 AT ACTION INDICATOR
         ST    R9,SFD004           SAVE COMPARE TABLE ADDRESS
         B     BA06                DO NOT POINT TO NEXT COMPARE FLD
*
** IF LAST ENTRY IN COMPARE TABLE (X'FF') THEN COMPARE IS FINISHED
*  ---------------------------------------------------------------
BA05     LA    R9,6(,R9)     POINT TO NEXT COMPARE FIELD
BA06     CLI   0(R9),X'FF'         Q. ANY ENTRIES IN THE TABLE
         BE    BA92                A. NO - CHECK FOR ERRORS
*
** EXAMINE FORMAT IND. IN THIS ENTRY & GO TO APPROPRIATE ROUTINE
*  -------------------------------------------------------------
         CLI   0(R9),C'R'          Q. REST OF RECORDS TO BE COMPARED(R)
         BE    BA40                A. YES - DO IT
         CLI   0(R9),C'M'          Q. BINARY COMPARE
         BL    BA60                A. YES - DO IT
*                                  Q. PACKED COMPARE
         BH    BA80                A. YES - DO IT
*                                  MUST BE BIT COMPARE
         EJECT
***********************************************************************
* ROUTINE TO COMPARE BITS (FORMAT IND. = M)                           *
***********************************************************************
         SPACE
*
** ENSURE FIELDS ARE PRESENT IN BOTH RECORDS
*  -----------------------------------------
BA20     LH    R4,2(,R9)           DISPLACEMENT TO R4
         CH    R4,0(,R10)          Q. FIELD PRESENT IN 1ST DSET RECORD
         BNL   BA91                A. NO - INDICATE ERROR
         CH    R4,2(,R10)          Q. FIELD PRESENT IN 2ND DSET RECORD
         BNL   BA91                A. NO - INDICATE ERROR
*
** COMPARE THE BYTE & UNDERLINE IF ANY INEQUALITY
*  ----------------------------------------------
BA23     LA    R14,0(R6,R4)        POINT R14 AT BYTE IN 1ST DS RECORD
         LA    R15,0(R7,R4)        POINT R15 AT BYTE IN 2ND DS RECORD
         MVC   SXD001,0(R14)       MOVE 1ST FIELD TO W/A
         XC    SXD001,0(R15)       DELETE ALL COMMON BITS (1ST/2ND FLD)
         NC    SXD001,1(R9)        DELETE ALL BITS NOT TO BE COMPARED
         BZ    BA05                IF BITS ZERO THEN ALL BITS TO BE
*                                  - COMPARED ARE EQUAL, SO GO &
*                                  - PROCESS NEXT COMPARE FIELD
*
** BYTE FIELD INEQUALITY UNDERLINE THE BYTE
*  ----------------------------------------
BA26     LA    R14,0(R8,R4)        POINT AT UNDERLINE BYTE
         OI    0(R14),X'11'        UNDERLINE THE BYTE
         MVI   0(R11),C'W'         INDICATE UNEQUAL BYTE
         B     BA05                GO PROCESS NEXT COMPARE FIELD
         EJECT
***********************************************************************
* ROUTINE TO COMPARE REST OF RECORDS (FORMAT IND. = R)                *
***********************************************************************
         SPACE
*
** SET UP REGISTERS WITH LENGTHS OF FIELD TO BE COMPARED &
** ENSURE FIELDS ARE PRESENT IN BOTH RECORDS
*  --------------------------------------------------------
BA40     LH    R3,0(,R10)          LENGTH OF DATA SET 1 RECORD
         LH    R15,2(,R10)         LENGTH OF DATA SET 2 RECORD
         LH    R4,2(,R9)           DISPLACEMENT OF FIELD IN REC.TO R4
         SR    R3,R4               Q. FIELD PRESENT IN RECORD 1
         BNP   BA91                A. NO - INDICATE ERROR
         SR    R15,R4              Q. FIELD PRESENT IN RECORD 2
         BNP   BA91                A. NO - INDICATE ERROR
*                                  LENGTH OF FIELD (DSET1 RECORD) TO R3
*                                  LENGTH OF FIELD (DSET2 REC.) TO R15
*
** SET UP REGISTERS WITH ADDRESSES OF FIELDS TO BE COMPARED
*  --------------------------------------------------------
         LA    R2,0(R4,R6)         POINT R2 AT FIELD IN DSET1 RECORD
         LA    R14,0(R4,R7)        POINT R14 AT FIELD IN DSET2 RECORD
*
** COMPARE FIELDS & INDICATE ANY INEQUALITY IN THE UNDERLINE AREA
*  --------------------------------------------------------------
         BAL   R12,B7101           COMPARE THIS FIELD
         B     BA05                GO PROCESS NEXT COMPARE FIELD
         EJECT
***********************************************************************
* ROUTINE TO COMPARE BINARY FIELD (FORMAT IND. = B)                   *
***********************************************************************
         SPACE
*
** SET UP REGISTERS WITH LENGTH OF FIELD TO BE COMPARED & ENSURE
** THE FIELD IS PRESENT IN BOTH RECORDS
*  -------------------------------------------------------------
BA60     LH    R3,4(,R9)           LENGTH OF FIELD TO R3
         LR    R15,R3              - & TO R15
         LH    R2,2(,R9)           DISPLACEMENT OF FIELD IN RECORD TO
         LR    R14,R2              - R2 & R14
         AR    R2,R3               DISP.OF END OF FIELD IN RECORD TO R2
         CH    R2,0(,R10)          Q. COMP.FIELD PRESENT IN DATA SET 1
*                                  - RECORD
         BH    BA91                A. NO - REPORT AS ERROR
         CH    R2,2(,R10)          Q. COMP.FIELD PRESENT IN DATA SET 2
*                                  - RECORD
         BH    BA91                A. NO - REPORT AS ERROR
*
** SET UP REGISTERS WITH ADDRESSES OF FIELDS TO BE COMPARED
*  --------------------------------------------------------
         LA    R2,0(R14,R6)        DISP.OF FIELD IN RECORD NOW IN R14
*                                  POINT R2 AT FIELD IN DATA SET 1 REC.
         AR    R14,R7              POINT R14 AT FIELD IN DATA SET 2 REC
*
** COMPARE FIELDS & INDICATE ANY INEQUALITY IN THE UNDERLINE AREA
*  --------------------------------------------------------------
         BAL   R12,B7101           COMPARE
         B     BA05                GO PROCESS NEXT COMPARE FIELD
         EJECT
***********************************************************************
* ROUTINE TO COMPARE PACKED FIELD (FORMAT IND. = P)                   *
***********************************************************************
         SPACE
*
** SET UP REGISTER WITH LENGTH OF FIELD TO BE COMPARED MINUS ONE &
** ENSURE THAT THE COMPARE FIELD IS PRESENT IN BOTH RECORDS
*  ---------------------------------------------------------------
BA80     LH    R3,4(,R9)           LENGTH OF FIELD TO R3
         LH    R4,2(,R9)           DISPLACEMENT OF FIELD IN RECORD
         LA    R15,0(R3,R4)        DISPLACEMENT OF END OF FIELD
         LH    R14,0(,R10)         LENGTH OF DATA SET 1 RECORD
         CR    R15,R14             Q. FIELD PRESENT IN DATA SET 1 REC.
         BH    BA91                A. NO - INDICATE ERROR
         LH    R14,2(,R10)         LENGTH OF DATA SET 2 RECORD
         CR    R15,R14             Q. FIELD PRESENT IN DATA SET 2 REC.
         BH    BA91                A. NO - INDICATE ERROR
         BCTR  R3,R0               LENGTH OF FIELD MINUS 1 TO R3
*
** ENSURE FIELDS ARE PACKED, IF NOT UNDERLINE THE ERROR
*  ----------------------------------------------------
         LA    R14,0(R6,R4)        POINT TO 1ST DATA SET REC.FIELD
         BAL   R12,B7201           Q. FIELD PACKED
         BNZ   BA87                A. NO - UNDERLINE INEQUALITY
         LR    R15,R14             SAVE FIELD ADDRESS
         LA    R14,0(R7,R4)        POINT TO 2ND DATA SET REC.FIELD
         BAL   R12,B7201           Q. FIELD PACKED
         BNZ   BA87                A. NO - UNDERLINE INEQUALITY
*
** CHECK FIELDS ARE EQUAL, IF NOT UNDERLINE THE ERROR
*  --------------------------------------------------
         LR    R12,R3              SET UP R3 WITH LENGTHS OF PACKED
         SLL   R3,4                - FIELDS FOR EXECUTED
         OR    R3,R12              - COMPARE
         EX    R3,EX01             Q. PACKED FIELDS EQUAL
*                                  ** CP  0(0,R14),0(0,R15) **
         BE    BA05                A. YES - PROCESS NEXT COMPARE FIELD
*
** UNDERLINE INEQUALITY ON PACKED FIELD
*  ------------------------------------
BA87     LH    R3,4(,R9)           LENGTH OF FIELD TO R3
         BCTR  R3,R0               - SUBTRACT 1 FOR EXECUTE INSTR.
         LA    R14,0(R8,R4)        POINT R14 AT UNDERLINE AREA
         EX    R3,EX02             UNDERLINE PACKED FIELD
         MVI   0(R11),C'W'         INDICATE RECORDS UNEQUAL
         B     BA05                GO PROCESS NEXT COMPARE FIELD
         EJECT
***********************************************************************
* IF INEQUALITY DETECTED - THEN SET UP UNDERLINE AREA TO INDICATE     *
* WHICH BYTES HAVE NOT BEEN COMPARED                                  *
***********************************************************************
         SPACE
BA91     MVI   0(R11),C'X'         INDICATE COMPARE FIELD NOT PRESENT
BA92     CLI   0(R11),C' '         Q. ANY INEQUALITY
         BE    BA99                A. NO - EXIT
*
** SET UP REGISTERS TO INDEX THRU' FIELDS COMPARED
*  -----------------------------------------------
         LA    R2,6                LENGTH OF COMPARE FIELD TO R2
         LR    R3,R9               POINT R3 TO LAST BYTE OF LAST
         BCTR  R3,R0               - COMPARE FIELD PROCESSED
         L     R9,SFD004           POINT R9 TO START OF COMPARE TABLE
*
** SET UP REGISTERS WITH LENGTH & DISPLACEMENT OF COMPARE FIELDS
*  -------------------------------------------------------------
BA93     LH    R14,2(,R9)          POINT R14 AT CORRESPONDING
         LA    R14,0(R14,R8)       - BYTE IN UNDERLINE AREA
         LH    R15,4(,R9)          INSERT LENGTH OF FIELD TO R15
         CLI   0(R9),C'R'          Q. REST OF RECORD TO BE COMPARED
         BNE   BA96                A. NO - UNDERLINE COMPARE FIELD
*
** CALCULATE LENGTH OF REST OF RECORD
*  ----------------------------------
         LH    R15,0(,R10)         LENGTH OF 1ST DATA SET REC. TO R15
         CH    R15,2(,R10)         Q. 2ND DSET LENGTH GREATER THAN 1ST
         BNL   BA95                A. NO - SET UP LEN.OF REST OF RECORD
         LH    R15,2(,R10)         INSERT 2ND DSET LENGTH TO R15
BA95     SH    R15,2(,R9)          - & FIND LENGTH OF COMPARE FIELD
*
** UNDERLINE COMPARE FIELD
*  ------------------------
BA96     OI    0(R14),X'44'        SET UNDERLINE BITS ON
         LA    R14,1(,R14)         POINT TO NEXT UNDERLINE POSITION
         BCT   R15,BA96            LOOP UNTIL FIELD UNDERLINED
         BXLE  R9,R2,BA93          LOOP UNTIL ALL COMPARE FIELDS DONE
*
BA99     MODOUT
         EJECT
***********************************************************************
* B7101 - COMPARE BINARY FIELDS & UNDERLINE BYTES IN ERROR      -R12- *
*          R2  CONTAINS ADDRESS OF FIELD 1                            *
*          R3  CONTAINS FIELD 1 LENGTH                                *
*          R14 CONTAINS ADDRESS OF FIELD 2                            *
*          R15 CONTAINS FIELD 2 LENGTH                                *
*         (DISPLACEMENT OF FIELD IN RECORD CAN BE DETERMINED SINCE    *
*          R6 & R7 POINT AT START OF RECORDS. R8 POINTS AT UNDERLINE.)*
***********************************************************************
         SPACE
*
** UNDERLINE EXTRA PART OF RECORD IF RECORDS OF UNEQUAL LENGTH
*  -----------------------------------------------------------
B7101    SR    R3,R15              SUBTRACT FIELD LENGTHS
         BZ    B7120               LENGTHS EQUAL - COMPARE RECORDS
         BP    B7110               RECORD DIFFERENCE IS +VE, CONTINUE
         AR    R15,R3              SMALLEST LENGTH TO R15
B7110    LPR   R5,R3               LENGTH DIFFERENCE TO R5
         MVI   0(R11),C'W'         INDICATE RECORDS UNEQUAL
         LA    R4,0(R8,R15)        POINT R4 AT UNDERLINE AREA
         AR    R4,R2               - CORRESPONDING TO START OF
         SR    R4,R6               - EXTRA PART OF RECORD
         L     R1,CFD004           SET UP R1 WITH PADDING CHARACTER
         MVCL  R4,R0               UNDERLINE EXCESS RECORD
B7120    LR    R3,R15              SMALLEST LENGTH TO R3
*
** COMPARE FIELDS
** IF UNEQUAL UNDERLINE THOSE BYTES
*  --------------------------------
B7125    CLCL  R2,R14              Q. FIELDS EQUAL
         BER   R12                 A. YES - RETURN
         MVI   0(R11),C'W'         INDICATE ERROR FOUND
         LR    R4,R2               LENGTH OF FIELD
         SR    R4,R6               - COMPARED TO R4
         AR    R4,R8               POINT R4 AT CORRECT UNDERLINE POS'N
         MVI   0(R4),X'11'         UNDERLINE BYTE IN ERROR
         BCTR  R3,R0               DECREMENT LENGTHS
         BCTR  R15,R0              - OF FIELDS BY 1
         LA    R2,1(,R2)           INDEX OVER
         LA    R14,1(,R14)         - BYTE IN ERROR
         B     B7125               CONTINUE COMPARING THE FIELD
         EJECT
***********************************************************************
* B7201 - CHECK FIELD IS VALID PACKED FORMAT                          *
*          R14 POINTS TO FIELD                                        *
*          R3  CONTAINS LENGTH MINUS 1 OF FIELD                       *
*         CONDITION CODE WILL BE ZERO IF FIELD IS PACKED              *
***********************************************************************
         SPACE
B7201    SR    R2,R2               CLEAR R2 TO ZERO
         EX    R3,EX03             MOVE FIELD TO W/A
*                                  ** MVC  SPD016(0),0(R14) **
         EX    R3,EX04             TRANSLATE THE FIELD (IN W/A)
*                                  ** TRT  SPD016(0),CXT256 **
*                                  IF FIELD IS PACKED CONDITION CODE
*                                  - WILL BE 2 & R2 WILL CONTAIN X'01'
         BC    13,B7210            RETURN IF CONDITION CODE NOT 2
         C     R2,=F'1'            Q. FIELD PACKED (I.E. R2 CONTAINS
*                                     X'01' - LAST FUNCTION BYTE)
         BR    R12                 A. NO - RETURN (WITH C.C = 0)
*                                  A. YES- RETURN (WITH C.C NOT 0)
B7210    CLI   *,X'00'             SET CONDITION CODE TO NOT ZERO
         BR    R12                 RETURN
         EJECT
***********************************************************************
* EXECUTED INSTRUCTIONS                                               *
***********************************************************************
         SPACE
EX01     CP    0(0,R14),0(0,R15)   Q. COMPARE FIELDS EQUAL
EX02     MVC   0(0,R14),=16X'88'   UNDERLINE PACKED ERROR
EX03     MVC   SPD016(0),0(R14)    MOVE 'PACKED' FIELD TO W/A
EX04     TRT   SPD016(0),CXT256    TRANSLATE 'PACKED' FIELD
         SPACE 3
***********************************************************************
* STORAGE AREAS, CONSTANTS & LITERAL POOL                             *
***********************************************************************
         SPACE
CFD004   DS    0F                  PADDING CHARACTER FOR PADDING
         DC    X'11000000'         - UNDERLINE AREA WHEN ONE RECORD
*                                  - LONGER THAN THE OTHER
SFD004   DS    F                   S/A FOR COMPARE TABLE ADDRESS
SPD016   DS    PL16                W/A FOR PACKED FIELD
SXD001   DS    X                   W/A FOR BIT COMPARE
*
** TRANSLATE TABLE (FOR CHECKING PACKED FIELD)
*  -------------------------------------------
CXT256   DS    0CL256              TABLE
         DC    10X'00000000000000000000010101010101'   00 TO 9F
         DC    96X'FF'                                 A0 ONWARDS
         LTORG
         END
