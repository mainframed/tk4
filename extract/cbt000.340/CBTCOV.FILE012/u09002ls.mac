ZDAY     TITLE '         DATE ROUTINE'
         SPACE 3
* 1. OBTAIN THE CURRENT DATE IN THE FORM:- DD.MM.YY
         SPACE
*        CALL  ZTODAY,(DATE),VL  WHERE 'DATE' IS AN 8-BYTE CHAR. FLD.
         SPACE 3
* 2. CONVERT A GIVEN DATE FROM THE FORM YYDDD TO DD.MM.YY
         SPACE
*        CALL  ZTODAY,(DATE,YD),VL  WHRRE 'DATE' IS AN 8-BYTE CHAR. FLD
*                                           'YD' IS A 5-BYTE CHAR FIELD
*                                                CONTAINING YYDDD
         SPACE 3
*        FOR  THE ORIGINAL USERS OF THE ROUTINE, A CHECK IS MADE TO SEE
*        IF A 'VL' SPECIFICATION IS MADE ON THE CALL. IF IT HASN'T
*        THEN THE CURRENT DATE IS RETURNED.
         SPACE
*           IF A DATE IS SUPPLIED BUT EITHER CONTAINS A CHARACTER
*        OUTSIDE THE RANGE 0-9, OR GIVES A DAY NO. OVER 365 FOR A
*        NON-LEAP YEAR, OR GIVES A DAY NO. OVER 366 FOR A LEAP YEAR
*        THEN THE RESULT FIELD IS FILLED WITH ASTERISKS.
         EJECT
ZTODAY   CSECT
         USING YD,R10  BASE FOR YYDDD
         USING DATE,R9  BASE FOR DD.MM.YY
         INIT
         SPACE
         LM    R9,R10,0(R1) R9=OUTPUT DATE, R10=OPTIONAL INPUT DATE
         SPACE
         LTR   R9,R9  SINGLE PARA?--CURRENT DATE REQUIRED
         BNL   USERDATE  NO,DATE SUPPLIED
         SPACE
* OBTAIN CURRENT DATE IN FORM 00 YY DD DS IN R1
CURRENT  EQU   *
         TIME
         B     CONVERT
         SPACE
* VALIDATE SECOND PARAMETER,RETURN *'S IN DD.MM.YY IF INVALID
USERDATE LTR   R10,R10  WAS VL SPECIFIED ON CALL?
         BNL   CURRENT NO,MUST BE ONE OF THE OLD USERS
         LA    R3,YDYEAR CHECK ALL 5 CHARS ARE IN RANGE 0-9
         LA    R4,1
         LA    R5,YDYEAR+4
VALIDATE CLI   0(R3),C'0'
         BL    INVALID
         CLI   0(R3),C'9'
         BH    INVALID
         BXLE  R3,R4,VALIDATE
         B     SETINR1 ALL CHARS VALID
INVALID  MVC   DATEDMY,=8C'*'  INDICATE INVALID DATE SUPPLIED
         B     RETURN
         SPACE
*        SET SUPPLIED D&TE IN R1 IN THE 'TIME' MACRO FORM00 YY DD DS
SETINR1  PACK  PWK,YDYEAR(5) PACK YYDDD
         L     R1,PWK  00 YY DD DS
         SPACE
*        CONVERT DATE FROM YYDDD TO DD.MM.YY
CONVERT  EQU   *
* REG1 RETURNED WITH DATE:- 00YYDDDS
         ST    R1,DATESAVE  SAVE DATE
         CP    DDD,=P'0'
         BE    INVALID
         MVI   FEB,28
         MVI   DOUBLE+7,15 SET SIGN
         MVO   DOUBLE+6(2),YY SET YEAR NO
         CVB   R2,DOUBLE YEAR IN BINARY
         STC   R2,DOUBLE+7 STORE CHARACTER
         TM    DOUBLE+7,X'03' LEAP YEAR
         BNZ   NOTLEAP IF NOT DIVISIBLE BY 4
         CP    DDD,=P'366'  POSSIBLE DAY NO FOR LEAP YEAR?
         BH    INVALID  NO
         MVI   FEB,29 NO. OF DAYS IN FEBRUARY FOR LEAP YEAR
         B     A1
NOTLEAP  CP    DDD,=P'365' POSSIBLE DAY NO. FOR A NON-LEAP YEAR?
         BH    INVALID NO
A1       MVC   DOUBLE+6(2),DDD SET DAY OF YEAR
         CVB   R2,DOUBLE DAY OF YEAR IN BINARY
         SPACE
         SR    R0,R0 ZERO REGISTER
         LA    R1,MONTH LOAD ADDRESS OF NO. OF DAYS IN MONTH TABLE
         SPACE
         CH    R2,0(R1) WITHIN MONTH?
         BNH   *+16 :YES
         SH    R2,0(R1)
         LA    R1,2(R1) NEXT ENTRY
         BCT   R0,*-16 LOOP
         BCTR  R0,R0 MONTH (-VE)
         LCR   R0,R0 MONTH (+VE)
         CVD   R0,DOUBLE MONTH IN DECIMAL
         OI    DOUBLE+7,15 DE-SIGN
         UNPK  AREA+3(2),DOUBLE MM
         CVD   R2,DOUBLE DAY OF MONTH IN DECIMAL
         OI    DOUBLE+7,15 DE-SIGN
         UNPK  AREA(2),DOUBLE DD
         UNPK  AREA+6(3),YY(2) YY
         MVC   0(8,R9),AREA MOVE TO OUTPUT AREA
         SPACE
RETURN   EQU   *
         EXIT
         EJECT
PWK      DS    F
DATESAVE DS    F
         ORG   DATESAVE+1
YY       DS    X
DDD      DS    XL2
*
DOUBLE   DS    D
         ORG   DOUBLE
         DC    8X'0'
*
AREA     DS    0CL8
         DC    C'  .  .   '
         SPACE
MONTH    DC    H'31,28,31,30,31,30,31,31,30,31,30,31'
         ORG   MONTH+3
FEB      DS    X
         ORG
         SPACE
YD       DSECT
YDYEAR   DS    CL2
YDDAY    DS    CL3
         SPACE
DATE     DSECT
DATEDMY  DS    CL8
