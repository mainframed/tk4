*        GENERAL DESCRIPTION
*        THE UT18 UTILITY READS THE VOLUME TABLE OF CONTENTS OF ANY
*        DISK AND PRODUCES A DATASET CONTAINING A HEADING RECORD,
*        A BLANK RECORD AND SCRATCH RECORDS FOR EACH NON-SYSTEM DATA
*        SET ON THE DISK.
*        DATA SET REQUIREMENTS
*        INPUT DATA SET:
*                       THIS IS THE VOLUME TABLE OF CONTENTS OF ANY
*                       DISK.
*        OUTPUT DATA SET:
*                       THIS IS A SEQUENTIAL FIXED BLOCKED FILE OF 80
*                       BYTE RECORDS.
*        CONTROL STATEMENTS
*        JOB CONTROL:
*        EXEC  PGM=UT18
*        IN    THIS STATEMENT DEFINES THE DISK OF WHICH THE VOLUME
*        DD    TABLE OF CONTENTS IS READ.
*        OUT   THIS STATEMENT DEFINES THE OUTPUT DATASET WHICH IS
*        DD    SEQUENTIAL.
SCRATCH  CSECT
         IN    12
         RDJFCB (IN)
         MVI   JFCB,X'04'
         MVC   JFCB+1(43),JFCB
         MVC   VOLUME,JFCB+118
         MVC   DISK,VOLUME
         OPEN  (IN,(INPUT)),TYPE=J
         OPEN  (OUT,(OUTPUT))
         PUT   OUT,BLANK
         PUT   OUT,HDG
         CLI   IN+17,X'29'         TEST 3330
         BNE   LOOP
         MVC   AREA+19(4),=C'3330'
LOOP     EQU   *
         READ  INDECB,SF,IN,INAREA,140
         CHECK INDECB
         CLI   INAREA+44,C'1'
         BNE   LOOP
         CLC    INAREA(3),=C'SYS'
         BE    LOOP
         MVC   DSNAME,INAREA
         PUT   OUT,AREA
         B     LOOP
FINISH   EQU   *
         CLOSE (IN)
         CLOSE (OUT)
         OUT
IN       DCB   MACRF=R,                                                X
               DSORG=PS,DDNAME=IN,EODAD=FINISH,EXLST=X,                X
               RECFM=F,LRECL=96,BLKSIZE=96,KEYLEN=44
OUT      DCB   MACRF=PM,DSORG=PS,DDNAME=OUT,RECFM=FB,LRECL=80
         DS    0F
X        DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
AREA     DS    0CL80
         DC    C' SCRATCH PURGE,VOL=2314='
VOLUME   DS    CL6
         DC    C',DSNAME='
DSNAME   DS    CL33
         DC    9C' '
HDG      DC    C'UT18 DATASETS ON DISK '
DISK     DS    CL6
BLANK    DC    28C' '
         DC    52C' '
         DS    0D
INAREA   DS    CL140
         END
