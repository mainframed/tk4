*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSM41MAC.
         LCLA  &BUFNO,&N99
         SPACE 2
&BUFNO   SETA  4000
&N99     SETA  &BUFNO+9
         TITLE 'SSM41MAC - PDS MEMBER-ALIAS-CORRELATOR'
$PDS     DSECT
$PDSREG  EQU   5
         USING  *,5
$PDSLINK DS    F
$PDSFILE DS    CL8
$PDSNAME DS    CL44
$PDSREQ  EQU   *-$PDS
*
*   THIS PROGRAM PRODUCES A LIST OF MEMBERS AND THEIR ALIASES IN
*      ALPHABETIC MEMBER ORDER.
*
*   THE MODULE GETS ON ENTRY POINTERS TO A CHAIN OF BUFFERS,
*    A PRINTERDCB AND NEXT PAGE NO TO BE USED.
*
*   EACH BUFFER CONTAINS A DDNAME AND DSNAME OF A PDS TO BE LISTED
*
*   THE PROGRAM HAS A CAPACITY OF 1000  NAMES IN AN IN CORE TABLE
*      ALIAS ARE CORRELATED BY MATCHING DISK ADDRESSES (TTR'S)
*
SSM41MAC ENTER ENT=PDSLIST,EXTENT=LPDCORE1
*
*      EXTRACT  PARM DATA
*
         LM    5,7,0(1)                 LOCATE PARM FIELDS
         LR    12,1           SAVE ENTRY POIMTER
         ZAP   PAGENUMB,0(2,7)         COLLECT PAQGE COUNTER
         GOTO  DATE2,(HDRDATE)     FILL IN DATE
*
         SR    2,2                      CLEAR R.C. REG
         MVI   LPDFLAG,0           CLEAR FLAG BYTE
*
*      GET CORE FOR TABLE - &BUFNO X 15 BYTES
*
         USING  TAB,8
         USING  MEMB,9
         CLC   =A(0),LPDTABLE           CORE AQUIRED
         BNE   LPD001                   YES
         GETMAIN  EC,MF=(E,LPDCORE1)     BUFFER
         LTR   15,15                     OK
         BNZ   LPDCFAIL                 NO GOOD REPORT
LPD001   MVC   LPDDFILE+40(8),$PDSFILE  SET DD NAME
         RDJFCB  MF=(E,LPDOPEN1)
LPD002   MVC   JFCB(44),$PDSNAME        SET DSNAME
         OPEN  MF=(E,LPDOPEN1),TYPE=J
         L     8,LPDTABLE               YES PICK UP START
         SR    10,10                    MEMBER COUNTER
         SR    11,11                    TOTAL ENTRY COUNT
         LA    3,&BUFNO-1               MAX ENTRY COUNT
*
*       READ  IN DIRECTORY BUILDING NAME TABLE
*
LPD003   GET   LPDDFILE                 GET BLOCK
         LA    9,2(,1)                  POINT TO FIRST ENTRY.
         LH    7,0(,1)
         LA    7,0(7,1)                 POINT R7 TO END OF BLOCK
*
LPD004   CLC   MEMBNAME,ALLONES         END ENTRY
         BE    LPD010                   YAS
         MVC   TABNAME(12),MEMBNAME     OK FILL ENTRY
         MVC   TABNUMB,=H'&N99'         SET HIGH MARK
         LA    11,1(,11)                ADD ONE TO NAME COUNT.
         NI    TABFLAG,ALIAS            IS ALIAS
         BNZ   LPD005                   YES
         LA    10,1(,10)                NO UP MEMBER COUNT
         STH   10,TABNUMB               STORE IT
LPD005   MVC   TABEXTRA(1),TABFLAG
         LA    8,TABNEXT           STEP TO NEXT TABLE PSN.
         BCT   3,LPD006                 FULL-UP
         B     LPDOVFLW                 YES REPORT
*
*        STEP UP DIRECTORY ENTRY
*
LPD006   SR    4,4
         IC    4,MEMBFLAG               EXTRACT COUNTS
         N     4,=F'31'                ONLY USER COUNT REQ
         SLL   4,1                      DOUBLE FOR USER COUNT
         LA    9,MEMBNEXT(4)            NOW POINT TO NEXT ENTRY
         CR    9,7                      AT END
         BL    LPD004                   NO CONTINUE WITH THIS BLOCK
         BE    LPD003                   NEW BLOCK
*  ERROR
*
LPD009   MVC   LPDCONTL(22),=C'- DIRECTORY ERROR SCAN'
         LA    2,8                     SET FAIL INDEX
         B     LPDXWRIT                 TO ERROR MSG RTN
*
*      LAST ENTRY REACHED  SORT INTO DISK ORDER
*
LPD010   MVC   TAB(TABSIZE),ALLONES     END MARKER
         ST    11,LPDNUMB1
         LA    1,LPDTABLE               CONTROL FOR SORT
         GOTO  CORESORT            SORT TABLE IN DISK ORDER
*
*        MOVE  MEMBER NUMBERS INTO ALIASES
*
         L     8,LPDTABLE               TABLE START
         ST    8,LPDAREA                SECOND SORT CONTROL
LPD012   CLC   TABLOCN,TABLOCN+TABSIZE  SAME DISK LOCN
         BNE   LPD013                   NO
         MVC   TABNUMB+TABSIZE,TABNUMB  YES COPY THROUGH NUMBER
LPD013   CLI   TABNAME,255         TABLE END?
         BE    LPD013A             CONTINUE
         LA    8,TABNEXT           GET NEXT ENTRY
         B     LPD012              ROUND AGAIN
*
*        MEMBER ORDER LIST
*
LPD013A  ST    11,LPDNUMB2         COMPLETE SECOND LIST
         LA    1,LPDAREA                SORT CONTROLS
         GOTO  CORESORT            SORT TABLE IN MEMBER ORDER
*
*      FILL IN REPORT  HEADER LAYOUT
*
LPD015   MVC   HDRDSN,JFCB              DSN TO HEADER
         MVC   HDRVOL,JFCB+118          AND VOL
         L     8,LPDTABLE              BASE CONTROL REGS
         L     9,LPDASTAR
*
*        PRODUCE HEADER
*
LPD016   AP    PAGENUMB,=P'1'           UP PAGE COUNTER
         UNPK  HDRPAGE(3),PAGENUMB
         OI    HDRPAGE+2,240            FIX ZONE
         ZAP   LINENUMB,=P'50'          FIX PAGE LENGTH
         PUT   (6),HDRLINE             PRINT HEADER
         OI    LPDCONTL,C'-'            SKIP CONTROL
*
*        PRODUCE DETAIL LINE
*
LPD017   TM    TABFLAG,255              TEST FLAG BYTE
         BO    LPD025                   LAST ENTRY
         BNZ   LPD020                   ALIAS ENTRY
         C     9,LPDASTAR               ANY ALIAS REPORT SET UP
         BNE   LPD019                   YES PRINT IT
LPD018   MVC   LPDNAME,TABNAME          SET UP ENTRY NAME
         LA    7,LPDTRACK
         BAL   3,LPDCONVT              CONVERT TRACK NO
         LA    8,TABNEXT
*
LPD019   BAL   3,LPDPRINT
         L     9,LPDASTAR
         SP    LINENUMB,=P'1'           REDUCE LINE COUNT
         BP    LPD017                   OK NEXT WILL FIT
         TM    TABFLAG,255              TEST FLAG
         BZ    LPD016                   NEW PAGE FIRST
         BO    LPD026                   ALL DONE
*
*        DEAL WITH ALIAS ENTRY
*
LPD020   CLC   TABNUMB,=H'&N99'         UNMATCHED ALIAS ENTRY
         BE    LPD022                    NO
LPD021   MVC   MEMBNAME,TABNAME         YES FILL UP ALIAS REPORT
         LA    9,MEMBLOCN+1             STEP POSN.
         LA    8,TABNEXT                NEXT ENTRY
         C     9,=A(LPDAEND)            ALIAS REPORT FULL
         BL    LPD017                   NO GO ON
         B     LPD019                   PRINT LINE
*
*        UN-MEMBERED ALIAS
*
LPD022   CLC   LPDHOLD,TABLOCN          PREVIOUS LINE SAME ALIAS
         BE    LPD021                   YES
         C     9,LPDASTAR               PREVIOUS LINE ALIASES SET UP
         BH    LPD019                   PRINT REPORT
         CP    LINENUMB,=P'1'           HONS PAGE SIZE
         BL    LPD016                   FULL UP
         TM    LPDFLAG,NOMEMB           SUBTITLE OUT
         BO    LPD023                   NOT FIRST
         CP    LINENUMB,=P'50'          FULL PAGE FREE
         BL    LPD016                   NO
         OI    LPDFLAG,NOMEMB           OK REPORT SUB HEADING
         LA    9,LPDNAME                REVISE ALIAS START
         ST    9,LPDASTAR                OUT PUT LINE POSN.
         MVC   LPDCONTL(L'LPDNOMEM),LPDNOMEM
         BAL   3,LPDPRINT               PRINT SUM TITLE
         SP    LINENUMB,=P'3'           REVISE PAGE HEADING
LPD023   MVC   LPDHOLD,TABLOCN          GET DISK LOCN
         LA    7,LPDNAME
         BAL   3,LPDCONVT               CONVERT TTR
         B     LPD021
*
*        TERMINATION ROUTINE
*
LPD025   C     9,LPDASTAR               ANY ALIASES
         BE    LPD026                   NO
         BAL   3,LPDPRINT              YES PRINT LINE
LPD026   SR    11,10                    CALULATE NO OF ALIASES
         CVD   11,LPDWORK
         UNPK  LPDNALTS,LPDWORK         INTO REPORT LINE
         OI    LPDNALTS+2,240           FIX ZONE
         CVD   10,LPDWORK
         UNPK  LPDNMAIN,LPDWORK         EXTRACT MEMBER COUNT
         OI    LPDNMAIN+3,240           FIX ZONE
         MVC   LPDCONTL(LPDPADD),LPDFINAL
         BAL   3,LPDPRINT               PRINT FINAL MSG
LPDCLOSE CLOSE  ,MF=(E,LPDOPEN1)        CLOSE DOWN
LPDFREE  L     5,$PDSLINK               NEXT BUFFER
         LTR   5,5                      IS ONE
         BZ    LPD027                   NO
         CLC   $PDSFILE,LPDDFILE+40    SANE DD NAME
         BE    LPD002                   YES JUST SET DSN
         B     LPD001                   READ JFCB FIRST
*
LPD027   LM    5,7,0(12)                RELOAD CONTROLS
         ZAP   0(2,7),PAGENUMB          RETURN CURRENT PAGE COUNTER
         LOSE  $PDS                     FREE OUT CHAIN
         L     13,4(,13)
         LR    15,2                     EXTRACT R.C.
         L     14,12(,13)
         LM    0,12,20(13)              RETURN REMAINING REGS
         BR    14                       WAY OUT
*
*        TRACK ADDRESS CONVERTION RTN.
*
         USING  TRACK,7
LPDCONVT LH    4,TABLOCN               EXTRACT TRACK NO
         CVD   4,LPDWORK
         UNPK  TRACKNUM,LPDWORK
         SR    4,4
         IC    4,TABLOCN+2             EXTRACT LINR REC NO
         CVD   4,LPDWORK
         UNPK  TRACKREC,LPDWORK
         OC    TRACKZON,=C'0-00'        FIX ZONES AND DASH
         BR    3                        RETURN
*
*        PRINT SUBROUTINR
*
LPDPRINT PUT   (6),LPDCONTL             PRINT LINE SET UP
*
         MVC   LPDCONTL,LPDCONTL-1
         BR    3                        RETURN
         EJECT
*
*        ERROR SECTION
*
LPDCFAIL MVC   LPDCONTL(27),=C'- UNABLE TO GET WORK TABLE.'
         LA    2,4
LPDXWRIT BAL   3,LPDPRINT          GO PRINT THE MESSAGE
         B     LPDERRXT(2)              WAY OUT CODE
LPDERRXT B     LPDABEND                 ABORT
         B     LPDCLOSE                WAY OUT
         MVC   LPDCONTL,LPDCONTL-1     CLEAR PRINT LINE
         B     LPDFREE                  CORE FREE FIRST
*
LPDOVFLW MVC   LPDCONTL(27),=C'- INCORE TABLE OVER FLOWED.'
         LA    2,8
         B     LPDXWRIT
*
LPDABEND OI    LPDOPEN1,X'80'           SET END ZONE
         CLOSE  ,MF=(E,LPDOPEN1)        CLOSE PRINTER
         ABEND  90,DUMP                 DUMP
*
LPDEOF   MVC   LPDCONTL+23(11),=C'* EOF HIT *'
         B     LPD009                   FINISH FAIURE
         EJECT
*
*        DATA  STATEMENTS
*
LPDOPEN1 OPEN  (LPDDFILE,(INPUT)),MF=L
LPDTABLE DC    A(0)                    ADDRESS FOR TABLE STORE
         DC    A(LPDNUMB1)
LPDAREA  DC    A(0,LPDNUMB2)
LPDASTAR DC    A(LPDAWORD)
LPDCORE1 GETMAIN  EC,LV=&BUFNO*TABSIZE,A=LPDTABLE,MF=L
PAGENUMB DC    PL2'1'                  PAGE COUNTER
LINENUMB DC    PL2'0',C' '
LPDCONTL DC    CL81' '                 PRINT LINE
         ORG   LPDCONTL+1
LPDNAME  DS    CL8,C
LPDTRACK DS    CL8
LPDAWORD DS    CL9
LPDNMAST DS    CL13
         ORG   LPDCONTL+80
LPDAEND  DS    C                       END OF LINE
         DC    CL52' '
HDRLINE  DC    C'1'                     PAGE HEADING
HDRDATE  DC    8C'X',C' VOL='
HDRVOL   DS    CL6
         DC    C' DSN='
HDRDSN   DS    CL44
         DC    C' PAGE '
HDRPAGE  DC    C'00',C'    '
         DC    CL52' '
LPDFINAL DC    C'-  DATA-SET HAS '
LPDNMAIN DC    C'0000',C' MEMBERS AND '
LPDNALTS DC    C'000',C' ALIASES '
LPDPADD  EQU   *-LPDFINAL
*
LPDFLAG  DC    X'00'                   FLAG BYTE
NOMEMB   EQU   8
LPDHOLD  DC    A(0)
LPDNUMB1 DC    A(0,15,4,9)
LPDNUMB2 DC    A(0,15,3,13)
ALLONES  DC    14X'FF'                      HIG VALUE TERMINATOR
LPDNOMEM DC    CL81'-        NO MEMBER NAME IS ASSCOCIATED WITH THE FOLL
               LOWING ALIASES '
LPDWORK  DS    D
         LTORG
         PRINT  NOGEN
LPDDFILE DCB   MACRF=GL,LRECL=256,BLKSIZE=256,RECFM=F,DSORG=PS,        X
               DDNAME=SYSUT1,EODAD=LPDEOF,EXLST=JREAD
JREAD    DC    X'87',AL3(JFCB)
JFCB     DS    44F                      SPACE FOR JFCB WHEN READ
         EJECT
*
*        FORMAT OF WORK AREAS
*
TAB      DSECT  ,                       TABLE ENTRY
TABNAME  DS    CL8
TABLOCN  DS    AL3
TABFLAG  DS    B
ALIAS    EQU   X'80'
TABNUMB  DS    H
TABEXTRA DS    B
TABNEXT  DS    C
TABSIZE  EQU   TABNEXT-TAB
*
MEMB     DSECT  ,                       DIRECTORY ENTRY
MEMBNAME DS    CL8
MEMBLOCN DS    AL3
MEMBFLAG DS    B                        CONTROL BYTE
MEMBNEXT DS    X
*
TRACK    DSECT  ,                       FORMAT OF TTR REPORT ELEMENT
TRACKNUM DS    0CL4,CL3
TRACKZON DS    0CL4,CL2
TRACKREC DS    CL2
*
         END
