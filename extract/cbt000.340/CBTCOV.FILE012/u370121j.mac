//CLISXREF JOB  ACCOUNT,PROGRAMMER,COND=(0,LT),MSGCLASS=T
//*
//*   SAMPLE JCL AND DATA FOR CLISXREF PROGRAM
//*
//  EXEC  ASMFCG
//ASM.SYSIN  DD DISP=SHR,DSN=GUIDE.GOODIES(U370120S)
//GO.SYSPRINT DD SYSOUT=T
//GO.SYSUDUMP  DD  SYSOUT=T
//GO.CLIST  DD DATA,DLM=$$
        /* DUMMY CLIST TO ILLUSTRATE DO LOOP PROCESSING */
 IF THEN DO
  LOOPA
  END
 IF THEN DO
  LOOPB
  END
 IF THEN DO 1
 LOOP1
  IF THEN DO 2
  LOOP2
   IF THEN DO 3
   LOOP3
    IF THEN DO 4
    LOOP4
     IF THEN DO 5
     LOOP5
      IF THEN DO 6
      LOOP6
       IF THEN DO 7
       LOOP7
        IF THEN DO 8
        LOOP8
         IF THEN DO 9
         LOOP9
                    IF THEN DO 10
                    LOOP10
                    LOOP10
                    LOOP10
                    LOOP10
                    LOOP10
                    LOOP10
                         IF THEN DO 11
                         LOOP11
                         LOOP11
                         LOOP11
                              IF THEN DO 12
                              LOOP12
                              LOOP12
                              LOOP12
                              LOOP12
                                   IF THEN DO 13
                                   LOOP13
                                   LOOP13
                                   LOOP13
                                   END  13
                             LOOP12
                             LOOP12
                             LOOP12
                             LOOP12
                             LOOP12
                             END  12
                        LOOP11
                        LOOP11
                        LOOP11
                        LOOP11
                        LOOP11
                        END  11
                   LOOP10
                   LOOP10
                   LOOP10
                   LOOP10
                   LOOP10
                   END  10
         END  9
        END  8
       END  7
      END  6
     END  5
    END  4
   END  3
  END  2
 END  1
 EDIT FRED
 FRED
 END SAVE
 IF THEN DO
  LOOPX
  END
 IF THEN DO
  LOOPY
  END
        /*  END OF DUMMY CLIST */
PROC 0
CONTROL NOFLUSH NOPROMPT NOLIST NOCONLIST NOSYMLIST NOMSG
ISPEXEC VGET (PRJ0,LIB0,TYP0) PROFILE   /* USER PROFILE VARIABLES */
SET &PROJ=&PRJ0                    /* SET DEFAULTS */
SET &LIBR=&LIB0
SET &TYPE=&TYP0
START: +
FREE FI(SYSORG)
FREE FI(CNTL)
FREE FI(SYSEXPDT)
FREE FI(CNTM)
FREE ATTRLIST(CTLA)
SET &FILE=&STR(&SUBSTR(1:2,&STR(&SYSTIME))) /* UNIQUE FILEID */
SET &FILE=&STR(&FILE.&SUBSTR(4:5,&STR(&SYSTIME)))
SET &FILE=&STR(&FILE.&SUBSTR(7:8,&STR(&SYSTIME)))
SET &JOBID=&STR(&SUBSTR(1:5,&STR(&SYSUID)))  /* JOB NAME PREFIX */
/**/
ISPEXEC DISPLAY PANEL(WSCCUTIL)    /* MAIN MENU PANEL */
SET &RC=&LASTCC
IF &RC EQ 8 THEN EXIT(0)       /* END KEY */
IF &RC NE 0 THEN EXIT(&RC)     /* NOT ENTER KEY */
SET &SEL=&WSCCSEL              /* GET PRIMARY OPTION FROM MENU */
IF &SEL EQ X THEN GOTO DOXPDT   /* IF "EXPIRY DATE", GO BELOW   */
IF &SEL EQ E THEN GOTO DONATE   /* IF "NATURAL RESTORE", GO BELOW */
IF &SEL EQ C THEN DO           /* IS IT THE "COMPRESS" OPTION? */
/**/
   ISPEXEC DISPLAY PANEL(WSCCUTIC)    /*"COMPRESS" PANEL */
   SET &RC=&LASTCC
   IF &RC EQ 8 THEN EXIT(0)       /* END KEY */
   IF &RC NE 0 THEN EXIT(&RC)     /* NOT ENTER KEY */
   END
/**/
MAINPART: +
SET &GRP=&STR(&SUBSTR(1:3,&STR(&SYSUID)))  /* 3 DIGIT GROUP ID */
ALCLOOP: +
ALLOC FI(SYSORG) DA('&PROJ..&LIBR..&TYPE') SHR
SET &RC=&LASTCC
IF &RC EQ 0 THEN GOTO CONT1 /* FILE ALLOCATED */
/*                              "ALLOCATION ERROR" */
ISPEXEC DISPLAY MSG(WSARC003)
SET &RC=&LASTCC
IF &RC EQ 8 THEN EXIT(0)       /* END KEY */
GOTO ALCLOOP
/*        SETUP SYSORG.C&FILE FILE */
CONT1: +
FREE FI(SYSORG)
ATTR CTLA BLKSIZE(80) OUTPUT RECFM(F) DSORG(PS)
ALLOC F(CNTL) DA(SYSORG.C&FILE) NEW SP(1) TRA USING(CTLA)
SET &RC=&LASTCC
IF &RC EQ 0 THEN GOTO OPNCNTL       /* FILE ALLOCATED */
/*                              "ALLOCATION FAILED" */
ISPEXEC DISPLAY MSG(WSARC001)
EXIT CODE(&RC)
OPNCNTL: +
OPENFILE CNTL OUTPUT
/**/
/*      COMPRESS REQUEST  */
/* CREATE JCL FOR SYSORG - F.D.R. BACKUP, IEBCOPY COMPRESS, RLSE */
SET &JOBID=&STR(&JOBID.ORG)
SET &CNTL=&STR(//&JOBID JOB ST000800,&SYSUID,MSGCLASS=T,)
PUTFILE CNTL
SET &CNTL=&STR(//  NOTIFY=&SYSUID)
PUTFILE CNTL
SET &CNTL=&STR(// EXEC SYSORG,WAYOUT=T,FILE='&PROJ..&LIBR..&TYPE')
PUTFILE CNTL
/***/
/**/
/*   NORMAL END OF ROUTINE */
/*   --------------------- */
CLOSE: CLOSFILE CNTL
FREE FILE(CNTL)
FREE ATTRLIST(CTLA)
/*   USER MUST FINALLY CONFIRM THE SUBMIT */
SUBLOOP: +
ISPEXEC DISPLAY PANEL(WSCCUTIS)
SET &RC=&LASTCC
IF &RC NE 0 THEN GOTO SUBBYP        /* NOT ENTER KEY */
SUBMIT SYSORG.C&FILE
SET &RC=&LASTCC                  /* SAVE RETURN CODE FROM SUBMIT*/
CHKSUB: +
IF &RC EQ 0 THEN GOTO SUB1       /* CHECK SUBMIT RETURN CODE */
/*                                "SUBMIT FAILED" */
ISPEXEC DISPLAY MSG(WSARC000)
SET &RC=&LASTCC
IF &RC EQ 8 THEN EXIT(0)       /* END KEY */
GOTO SUBLOOP                    /* TRY AGAIN */
SUB1: +
ISPEXEC DISPLAY MSG(WSARC006)
/*                               "JOB SUBMITTED" */
SUBBYP: +
DELETE SYSORG.C&FILE
GOTO START      /* LOOP TILL NO MORE REQUESTS*/
/**/
/**/
/*      EXPIRY DATE CHANGE OPTION SELECTED    */
/*      ----------------------------------    */
DOXPDT: +
SET &DSNAME=&STR('&PROJ..&LIBR..&TYPE')
EXPLOOP: +
ISPEXEC DISPLAY PANEL(WSCCUTIX)
SET &RC=&LASTCC
IF &RC EQ 8 THEN EXIT(0)       /* END KEY */
IF &RC NE 0 THEN EXIT(&RC)     /* NOT ENTER KEY */
SET &EXPDT=&YY.&DDD
EXPALLC: +
ALLOC FI(SYSEXPDT) DA(&DSNAME) VOLUME(&VOLSER) UNIT(&UNIT) SHR
SET &RC=&LASTCC
IF &RC EQ 0 THEN GOTO CONTX    /* FILE ALLOCATED */
/*                              "ALLOCATION ERROR" */
ISPEXEC DISPLAY MSG(WSUTI000)
SET &RC=&LASTCC
IF &RC EQ 8 THEN EXIT(0)       /* END KEY */
GOTO EXPALLC
/*        PASS PARAMS.TO PROGRAM   */
CONTX: +
SET &L=&LENGTH(&DSNAME)-1      /* REMOVE QUOTES FROM DSNAME */
SET &DSN=&STR(&SUBSTR(2:&L,&STR(&DSNAME)))
ISPEXEC SELECT PGM(UTIEXPDT) PARM(&DSN,&VOLSER,&EXPDT,SPF)
SET &RC=&LASTCC
FREE FI(SYSEXPDT)
ISPEXEC DISPLAY MSG(WSXPD00&RC) /* SHOW RESULT OF OPERATION */
SET &RC=&LASTCC
IF &RC EQ 8 THEN EXIT(0)       /* END KEY NOW? */
IF &RC NE 0 THEN EXIT(&RC)     /* NOT ENTER KEY */
GOTO EXPLOOP
EXIT(&RC)
/**/
/**/
/*      NATURAL SOURCE EMERGENCY RESTORE SELECTED      */
/*      -----------------------------------------      */
DONATE: +
ISPEXEC DISPLAY PANEL(WSCCUTIE)
SET &RC = &LASTCC
IF &RC EQ 8 THEN EXIT(0)          /* END KEY */
IF &RC NE 0 THEN EXIT(&RC)        /* NOT ENTER KEY */
ATTR CTLA BLKSIZE(80) OUTPUT RECFM(F) DSORG(PS)
ALLOC F(CNTM) DA(NATEREST.E&FILE) NEW SP(1) TRA USING(CTLA)
SET &RC=&LASTCC
IF &RC EQ 0 THEN GOTO OPNCNTM       /* FILE ALLOCATED */
/*                              "ALLOCATION FAILED" */
ISPEXEC DISPLAY MSG(WSARC001)
EXIT CODE(&RC)
OPNCNTM: +
OPENFILE CNTM OUTPUT
/**/
/*      NATEREST REQUEST  */
/*   CREATE JCL FOR NATEREST */
SET &JOBID=&STR(&JOBID.NAT)
SET &CNTM=&STR(//&JOBID JOB ST000818,&SYSUID,MSGCLASS=T,)
PUTFILE CNTM
SET &CNTM=&STR(//  NOTIFY=&SYSUID)
PUTFILE CNTM
SET &CNTM=&STR(// EXEC NATEREST,LIBR='&NATLIB',PROG='&NATPRG')
PUTFILE CNTM
/***/
/**/
/*   NORMAL END OF ROUTINE */
/*   --------------------- */
CLOSF: CLOSFILE CNTM
FREE FILE(CNTM)
FREE ATTRLIST(CTLA)
/*   USER MUST FINALLY CONFIRM THE SUBMIT */
SUBLOOQ: +
ISPEXEC DISPLAY PANEL(WSCCUTIS)
SET &RC=&LASTCC
IF &RC NE 0 THEN GOTO SUBBYQ        /* NOT ENTER KEY */
SUBMIT NATEREST.E&FILE
SET &RC=&LASTCC                  /* SAVE RETURN CODE FROM SUBMIT*/
CHKSUC: +
IF &RC EQ 0 THEN GOTO SUC1       /* CHECK SUBMIT RETURN CODE */
/*                                "SUBMIT FAILED" */
ISPEXEC DISPLAY MSG(WSARC000)
SET &RC=&LASTCC
IF &RC EQ 8 THEN EXIT(0)       /* END KEY */
GOTO SUBLOOQ                    /* TRY AGAIN */
SUC1: +
ISPEXEC DISPLAY MSG(WSARC006)
/*                               "JOB SUBMITTED" */
SUBBYQ: +
DELETE NATEREST.E&FILE
GOTO START      /* LOOP TILL NO MORE REQUESTS*/
$$
