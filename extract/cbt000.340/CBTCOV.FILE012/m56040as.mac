         MACRO
&NAME    TESTIOT &DDNAME=
         LCLC  &DDN
         AIF   ('&NAME' EQ '').NONAME
&NAME    EQU   *
.NONAME  ANOP
         CNOP  0,4
.DDN     BAL   1,*+20 ^            BRANCH AROUND LIST
         DC    A(*+12) ^           LIST ADDRESS
         DC    A(0) ^              TCB ADDRESS
         DC    AL1(2) ^            FIELD BYTE 1
         DC    AL3(0) ^            FIELD BYTE 2,3,4.
         DC    F'0' ^              ADDRESS TASK INPUT/OUTPUT TABLE
         SVC   40 ^                ISSUE EXTRACT SVC
         L     1,*-6 ^             LOAD ADDRESS OF TIOT
         XC    *-10(4),*-10 ^      ZEROISE ADDRESS TIOT AREA
         LA    1,24(1) ^           POINT R1 AT START OF FIRST DD ENTRY
         XR    15,15 ^             ZEROISE REG.15
         CLC   0(4,1),*-22 ^       TEST IF END OF TIOT
         BE    *+26 ^              YES - GO SET CONDITION CODE TO 4
         AIF   ('&DDNAME' EQ '').NODDN
&DDN     SETC  '&DDNAME'
         AIF   ('&DDNAME'(1,1) EQ '(').REG
         CLC   4(8,1),&DDN ^       TEST IF SPECIFIED DDN IS IN TIOT
         AGO   .NEXT
.NODDN   ANOP
&DDN     SETC  '(0)'
.REG     CLC   4(8,1),0&DDN ^      TEST IF SPECIFIED DDN IS IN TIOT
.NEXT    BE    *+20 ^              YES - CONDITION CODE IS ZERO
         IC    15,0(1) ^           LOAD LENGTH OF THIS DD ENTRY
         LA    1,0(15,1) ^         GET ADDRESS OF NEXT DD ENTRY
         B     *-30 ^              REPEAT TEST
         LA    15,4 ^              SET CONDITION CODE TO 4
         MEND
