         TITLE '  MEMBER ALIAS/RENAME/SCRATCH ROUTINE'
Z26MARS  SETUP 13,12,EQUATE=NO
*
*   SWITCH SETTINGS
*        B1 OFF => FIRST TIME THROUGH
*        B2 ON  => END OF CARD
*        B3 ON  => PARM FIELD INPUT
*        B4 ON  => ERROR DURING PROCESSING
*        B5 OFF => SCRATCH FUNCTION
*        B5 ON  => RENAME FUNCTION
*        B6 ON  => ALIAS FUNCTION
*        B7 ON  => PARM FIELD=MEMBER NAME
*        B8 ON  => DDNAME CARD READ
*        B9 OFF => FIRST CARD IS 'S M='
*
RCARD    EQU   7
RWK1     EQU   5
*
         SR    6,6
         LR    3,1
         OPEN  (IN,(INPUT))
         OPEN  (OUT,(OUTPUT))
         TM    OUT+48,X'10'
         BZ    OUTNO
         OPEN  (PDCB,(UPDAT))
         TM    PDCB+48,X'10'
         BO    *+8
         OI    SWITCH,X'80'
*
         TIME  DEC
         ST    1,PDATE
         UNPK  DATE(5),PDATE+1(3)
         ST    0,PTIME
         UNPK  TIME(5),PTIME(3)
         MVI   TIME+4,C' '
         LA    0,TITLE
         LA    1,OUT
         L     15,48(1)
         BALR  14,15
         BAL   11,PUT
         EJECT
*  TEST FOR MEMBER NAME SPECIFIED IN PARM FIELD
*
         L     4,0(3)
         CLC   0(2,4),=H'0'
         BE    GETCARD
         CLOSE PDCB
         OI    SWITCH,X'20'
         CLC   0(2,4),=H'8'
         BH    CARDIMAG            IF PARM > 8 CHARS,ASSUME CARD IMAGE
         LR    RCARD,4
         LA    RCARD,2(RCARD)
         LH    RWK1,0(4)
CHECKPRM CLI   0(RCARD),C' '       CHECK FOR SPACES IN PARM FIELD
         BE    CARDIMAG            IF SO,ASSUME CARD IMAGE
         LA    RCARD,1(RCARD)
         BCT   RWK1,CHECKPRM
         LH    3,0(4)
         BCTR  3,0
         EX    3,MOVE1
         OPEN  (PDCB,(UPDAT))
         TM    PDCB+48,X'10'
         OI    SWITCH,X'02'
         BZ    PDCBNO
         B     DELETE              IF NO SPACES,ASSUME MEMBER TO DELETE
*
CARDIMAG CLC   0(2,4),=H'80'       CHECK PARM < 80
         BH    INVPARM
         LH    3,0(4)
         BCTR  3,0
         EX    3,MOVEPARM          MOVE PARM INPUT INTO 80 BYTE FIELD
         LA    RCARD,PARMFLD
         B     PARMLAB
         EJECT
*
*   READ PARAMETER CARDS
*
GETCARD  NI    SWITCH,X'B1'
ROUND    GET   IN
         LR    RCARD,1
PARMLAB  LR    RWK1,RCARD
         LA    0,PRINLINE
         MVC   PRINLINE+1(80),0(RCARD)
         MVI   PRINLINE+81,C' '
         MVC   PRINLINE+82(51),PRINLINE+81
         MVI   PRINLINE,C'-'
         SP    LINES(2),=P'3'
         BAL   11,PUT
         MVI   PRINLINE,C' '
         MVC   PRINLINE+1(80),PRINLINE
*
*   TEST FOR LABEL
*
LABTEST  LA    1,9
LOOP4    CLI   0(RWK1),C' '
         BE    NOLAB
         LA    RWK1,1(RWK1)
         BCT   1,LOOP4
         B     INVLABEL
*
NOLAB    LA    RWK1,1(RWK1)
         LA    1,50
LOOP2    CLI   0(RWK1),C' '
         BNE   CHECKF
         LA    RWK1,1(RWK1)
         BCT   1,LOOP2
         B     INVCARD
         EJECT
*
*   CHECK FOR SCRATCH
*
CHECKF   CLC   0(8,RWK1),=C'SCRATCH '
         BE    S1
         CLC   0(5,RWK1),=C'SCRA '
         BE    S2
         CLC   0(2,RWK1),=C'S '
         BE    S3
         CLC   0(7,RWK1),=C'SELECT '
         BE    SELECT
*
*   CHECK FOR RENAME
*
         CLC   0(7,RWK1),=C'RENAME '
         BE    R1
         CLC   0(2,RWK1),=C'R '
         BE    R2
*
*   CHECK FOR ALIAS
*
         CLC   0(6,RWK1),=C'ALIAS '
         BE    A1
         CLC   0(2,RWK1),=C'A '
         BE    A2
*
*   CHECK FOR DDNAME
*
         CLC   0(7,RWK1),=C'DDNAME='
         BE    D1
*
         B     INVFUNCT
*
*   INCREMENT RWK1 APPROPRIATELY & SET SWITCH BITS
*
R1       LA    RWK1,5(RWK1)
R2       LA    RWK1,2(RWK1)
         OI    SWITCH,X'08'        SET SWITCH FOR RENAME
         B     SEARCHDD
*
A1       LA    RWK1,4(RWK1)
A2       LA    RWK1,2(RWK1)
         OI    SWITCH,X'04'        SET SWITCH FOR ALIAS
         B     SEARCHDD
*
S1       LA    RWK1,3(RWK1)
S2       LA    RWK1,3(RWK1)
S3       LA    RWK1,2(RWK1)
         B     SEARCHDD
*
SELECT   LA    RWK1,7(RWK1)
         B     SEARCHDD
*
D1       OI    SWITCH,X'81'        SET DDNAME & FIRST TIME SWITCHES
         MVC   DDNAME(8),7(RWK1)
         MVC   DDNAME2(8),BLANK
         B     OPENDDN
         EJECT
*
*   SEARCH FOR DDNAME
*
SEARCHDD LA    1,50
LOOP5    CLI   0(RWK1),C' '
         BNE   CHECKD
         LA    RWK1,1(RWK1)
         BCT   1,LOOP5
         B     INVCARD
*
*   ISOLATE & CHECK DDNAME
*
CHECKD   CLC   0(7,RWK1),=C'MEMBER='
         BE    M1
         CLC   0(2,RWK1),=C'M='
         BE    M2
         B     TRT
*
M1       LA    RWK1,5(RWK1)
M2       LA    RWK1,2(RWK1)
         TM    SWITCH,X'01'
         BO    SWTEST08
         TM    SWITCH,X'80'
         BO    INVCARD
         TM    SWITCH+1,X'80'
         BZ    RDJFCB
SWTEST08 OI    SWITCH+1,X'80'
         TM    SWITCH,X'08'
         BO    INVCARD
         CLI   0(RWK1),C'('
         BE    *+12
         OI    SWITCH,X'40'
         B     STRAN
         LA    RWK1,1(RWK1)
         B     STRAN
TRT      NI    SWITCH,X'FE'
         OI    SWITCH+1,X'80'
         OI    SWITCH,X'80'        SET SECOND TIME SWITCH
         TRT   0(9,RWK1),TRTABLE
         BZ    INVCARD
         CLI   0(1),C'('
         BNE   INVCARD
         SR    1,RWK1
         BCTR  1,0
         EX    1,MOVEDDN
         LR    6,1
OPENDDN  CLC   DDNAME(8),DDNAME2
         BNE   OPEN
         MVC   DDNAME(8),BLANK
         TM    SWITCH,X'01'
         BO    STRAN
         B     CONT2
RDJFCB   DEQ   (QNAME,RNAME,44,SYSTEM),RET=HAVE
         RDJFCB PDCB
         ENQ   (QNAME,RNAME,E,44,SYSTEM)
         MVC   PRINLINE+1(9),=C'DSNAME = '
         MVC   PRINLINE+10(44),JFCB
         MVI   PRINLINE+81,C' '
         MVC   PRINLINE+82(51),PRINLINE+81
         BAL   11,PUT
         MVI   PRINLINE,C' '
         MVC   PRINLINE+1(53),PRINLINE
         TM    SWITCH+1,X'80'
         BZ    SWTEST08
         TM    SWITCH,X'01'
         BO    GETCARD
CONT2    AR    RWK1,6
         LA    RWK1,2(RWK1)
         TM    SWITCH,X'08'
         BO    RTRAN
         TM    SWITCH,X'04'
         BO    RTRAN
         B     STRAN
OPEN     CLOSE PDCB
MOVE21   MVC   DDNAME2(8),DDNAME        REPLACE OLD DDNAME
         MVC   PDCB+40(8),DDNAME        RESET DCB DDNAME
         OPEN  (PDCB,(UPDAT))           OPEN NEW DATA SET
         MVC   DDNAME(8),BLANK     BLANK ORIGINAL DDNAME
         TM    PDCB+48,X'10'
         BZ    PDCBNO
         B     RDJFCB
         EJECT
*
*   ISOLATE MEMBER NAME FOR SCRATCH FUNCTION
*
STRAN    TRT   0(9,RWK1),TRTABLE
         BZ    INVNAME
         CLI   0(1),C','
         BE    CONT1
         CLI   0(1),C')'
         BE    SETSW40
         TM    SWITCH,X'40'
         BZ    INVNAME
         CLI   0(1),C' '
         BNE   INVNAME
SETSW40  OI    SWITCH,X'40'
CONT1    SR    1,RWK1
         LTR   1,1
         BZ    INVCARD
         BCTR  1,0
         EX    1,MOVEMEM
         BAL   11,CHECKMEM
         B     DELETE
         EJECT
*
*   CHECK FORMAT OF MEMBER NAME
*
CHECKMEM LA    1,1(1)
         CLI   0(RWK1),C'*'
         BE    LADDR
         CLI   0(RWK1),C'A'
         BL    INVNAME
         CLI   0(RWK1),C'Z'
         BH    INVNAME
LADDR    LA    RWK1,1(RWK1)
         BCTR  1,0
         LTR   1,1
         BZ    INCR2
LOOP7    CLI   0(RWK1),C'*'
         BE   BCT
         CLI   0(RWK1),C'A'
         BL    INVNAME
         CLI   0(RWK1),C'9'
         BH    INVNAME
BCT      LA    RWK1,1(RWK1)
         BCT   1,LOOP7
INCR2    LA    RWK1,1(RWK1)
         BR    11
         EJECT
*
*   ISOLATE MEMBER NAMES FOR RENAME/ALIAS FUNCTION
*
RTRAN    CLI   0(RWK1),C'('
         BNE   INVCARD
         LA    RWK1,1(RWK1)
         MVC   OLDNAME(8),BLANK
         MVC   NEWNAME(8),BLANK
         TRT   0(9,RWK1),TRTABLE
         BZ    INVNAME
         CLI   0(1),C','
         BNE   INVCARD
         SR    1,RWK1
         LTR   1,1
         BZ    INVCARD
         BCTR  1,0
         EX    1,MOVEONAM          MOVE OLD NAME
         BAL   11,CHECKMEM         CHECK FORMAT
         TRT   0(9,RWK1),TRTABLE
         CLI   0(1),C')'
         BNE   INVCARD
         SR    1,RWK1
         LTR   1,1
         BZ    INVCARD
         BCTR  1,0
         EX    1,MOVENNAM          MOVE NEW NAME/ALIAS
         BAL   11,CHECKMEM         CHECK FORMAT
         CLI   0(RWK1),C')'
         BE    ENDCARD
         CLI   0(RWK1),C','
         BNE   INVCARD
         CLI   1(RWK1),C'('
         BNE   INVCARD
         LA    RWK1,1(RWK1)
         B     *+12
ENDCARD  OI    SWITCH,X'40'        SET END OF CARD SWITCH
         LA    RWK1,1(RWK1)        POINT TO ',' OR ' '
         TM    SWITCH,X'08'
         BO    RENAME
         B     ALIAS
MOVEMEM  MVC   DIRENTRY(0),0(RWK1)
MOVEDDN  MVC   DDNAME(0),0(RWK1)
MEMBER   DC    CL8' '
TRTABLE  DC    64X'00'
         DC    C' '
         DC    12X'00'
         DC    C'('
         DC    15X'00'
         DC    C')'
         DC    13X'00'
         DC    C','
         DC    148X'00'
*
         DS    0H
INVNAME  MVC   PRINLINE+82(50),Z2612
         BAL   11,PUT
         OI    SWITCH,X'10'
         B     SWTEST20
INVCARD  MVC   PRINLINE+82(50),Z2610
         BAL   11,PUT
         OI    SWITCH,X'10'
         B     SWTEST20
INVLABEL MVC   PRINLINE+82(50),Z2609
         BAL   11,PUT
         OI    SWITCH,X'10'
         B     SWTEST20
INVPARM  MVC   PRINLINE+82(50),Z2615
         BAL   11,PUT
         B     CLOSE2
INVFUNCT MVC   PRINLINE+82(50),Z2611
         BAL   11,PUT
         OI    SWITCH,X'10'
         B     SWTEST20
NEWEXIST MVC   Z2605+20(8),NEWNAME
         MVC   PRINLINE+82(50),Z2605
         BAL   11,PUT
         OI    SWITCH,X'10'
         B     SWTEST
IOERR    MVC   PRINLINE+82(50),Z2607
         BAL   11,PUT
         WTO   'I/O ERROR IN LIBRARY - PLEASE RESTORE'
         B     SWTEST20
DELETE   BAL   11,CHECKLEN
         STOW  PDCB,DIRENTRY,D
         CH    15,=H'08'
         BE    NOTFOUND
         CH    15,=H'00'
         BNE   INVALID
OK       MVC   Z2601+20(8),DIRENTRY
         MVC   PRINLINE+82(50),Z2601
         LA    0,PRINLINE
         BAL   11,PUT
         B     SWTEST
         EJECT
NOTFOUND MVC   Z2602+20(8),DIRENTRY
         MVC   PRINLINE+82(50),Z2602
         LA    0,PRINLINE
         OI    SWITCH,X'10'
         BAL   11,PUT
         B     SWTEST
*
SWTEST   TM    SWITCH,X'02'
         BO    CLOSE1
         TM    SWITCH,X'40'
         BO    TESTCOMM
         TM    SWITCH,X'0C'
         BZ    STRAN
         B     RTRAN
TESTCOMM CLI   0(RWK1),C' '
         BE    SWTEST20
         CLI   0(RWK1),C','
         BNE   INVCARD
         NI    SWITCH,X'B0'
         LA    RWK1,1(RWK1)
         B     LABTEST
RENAME   BAL   11,CHECKLEN
         STOW  PDCB,OLDNAME,C
         MVC   DIRENTRY(8),OLDNAME
         CH    15,=H'4'
         BE    NEWEXIST
         CH    15,=H'8'
         BE    NOTFOUND
         CH    15,=H'16'
         BE    IOERR
         CH    15,=H'0'
         BNE   INVALID
         MVC   Z2603+20(8),OLDNAME
         MVC   Z2603+40(8),NEWNAME
         MVC   PRINLINE+82(50),Z2603
         BAL   11,PUT
         B     SWTEST
         EJECT
ALIAS    BAL   11,CHECKLEN
         MVC   ONAME(8),OLDNAME
         MVC   NNAME(8),NEWNAME
         MVC   DIRENTRY(8),OLDNAME
         BLDL  PDCB,LIST1
         CH    15,=H'4'
         BE    NOTFOUND
         CH    15,=H'8'
         BE    IOERR
         MVC   NTTR(3),OTTR
         MVC   NABIT(63),OABIT
         OI    NABIT,X'80'
         STOW  PDCB,LIST2,A
         CH    15,=H'4'
         BE    NEWEXIST
         CH    15,=H'8'
         BE    NOTFOUND
         CH    15,=H'12'
         BE    DIRFULL
         CH    15,=H'0'
         BNE   INVALID
         MVC   Z2604+20(8),OLDNAME
         MVC   Z2604+40(8),NEWNAME
         MVC   PRINLINE+82(50),Z2604
         BAL   11,PUT
         B     SWTEST
CHECKLEN SR    RWK1,RCARD
         CH    RWK1,=H'80'        CHECK END OF CARD NOT REACHED
         BH    INVPARM
         AR    RWK1,RCARD
         BR    11
*
SWTEST20 TM    SWITCH,X'20'       CHECK FOR PARM INPUT
         BO    CLOSE1
         B    GETCARD
         EJECT
DIRFULL  MVC   OLDNAME(8),BLANK
         MVC   NEWNAME(8),BLANK
         MVC   PRINLINE+82(50),Z2606
         BAL   11,PUT
         B     GETCARD
*
*
PUT      SP    LINES(2),=P'1'
         LA    0,PRINLINE
         MVC   DIRENTRY(8),BLANK
         CP    LINES(2),=P'0'
         BL    SKIP
PUT1     LA    1,OUT
         LA    0,PRINLINE
         L     15,48(1)
         BALR  14,15
         BR    11
SKIP     ZAP   LINES(2),=P'60'
         LA    1,OUT
         LA    0,TITLE
         L     15,48(1)
         BALR  14,15
         B     PUT1
FINAL    CLOSE IN
CLOSE1   TM    SWITCH,X'10'
         BO    CLOSE2
         CLOSE OUT
         CLOSE PDCB
         L     13,4(13)
         LM    14,12,12(13)
         LA    15,0
         BR    14
INVALID  MVC   PRINLINE+2(50),Z2608
         LA    0,PRINLINE
         BAL   11,PUT
CLOSE2   CLOSE OUT
ABEND    L     13,4(13)
         LM    14,12,12(13)
         LA    15,4
         BR    14
         EJECT
LIST1    DS    0H
NUMBER   DC    H'1'
LENGTH   DC    H'76'
ONAME    DC    CL8' '
OTTR     DS    CL3
OKBIT    DS    CL1
OZBIT    DS    CL1
OABIT    DS    BL1
OTTRN    DS    BL2
OUDLEN   DS    BL5
OUDATA   DS    CL62
*
LIST2    DS    0H
NNAME    DC    CL8' '
NTTR     DS    CL3
NABIT    DS    BL1
NTTRN    DS    BL2
NUDLEN   DS    BL5
NUDATA   DC    62C' '
*
OLDNAME  DC    CL8' '
NEWNAME  DC    CL8' '
MOVEONAM MVC   OLDNAME(0),0(RWK1)
MOVENNAM MVC   NEWNAME(0),0(RWK1)
         EJECT
         PRINT ON,NOGEN
OUT      DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,RECFM=FBA,          *
               LRECL=133
IN       DCB   DDNAME=SYSIN,MACRF=(GL),DSORG=PS,EODAD=FINAL
PDCB     DCB   DSORG=PO,MACRF=(R,W),DDNAME=SYSUT1,EXLST=EXLST
DDNAME   DC    CL8' '
DDNAME2  DC    CL8' '
EXLST    DC    X'07'
         DC    AL3(JFCB)
JFCB     DC    44F'0'
QNAME    DC    C'SYSIEWL '
RNAME    EQU   JFCB
BLANK    DC    8C' '
SWITCH   DC    X'0000'
S0       DC    F'0'
SAVEAREA DS   0F
DIRENTRY DC    8C' '
TITLE    DC    C'1    Z26MARS. T.S.G. MEMBER ALIAS/RENAME/SCRATCH ROUTI*
               NE'
         DC    5C' '
         DC    C'DATE='
DATE     DS    CL5
         DC    5C' '
         DC    C'TIME='
TIME     DS    CL4
         DC    50C' '
PDATE    DS    F
PTIME    DS    F
PRINLINE DC    133C' '
Z2601    DC    C'     Z260A   MEMBER          DELETED              '
Z2602    DC    C'     Z260B   MEMBER          NOT FOUND            '
Z2603    DC    C'     Z260C   MEMBER          RENAMED TO           '
Z2604    DC    C'     Z260D   MEMBER          ALIASED AS           '
Z2605    DC    C'     Z260E   MEMBER          ALREADY EXISTS       '
Z2606    DC    C'     Z260F   DIRECTORY FULL                       '
Z2607    DC    C'     Z260G   I/O ERROR IN DIRECTORY               '
Z2608    DC    C'     Z260H   ABNORMAL TERMINATION                 '
Z2609    DC    C'     Z260I   LABEL INVALID -CARD IGNORED          '
Z2610    DC    C'     Z260J   FORMAT INVALID - REST OF CARD IGNORED'
Z2611    DC    C'     Z260K   FUNCTION INVALID - CARD IGNORED      '
Z2612    DC    C'     Z260L   MEMBER/DD NAME INVALID               '
Z2615    DC    C'     Z260O   CARD FORMAT INVALID/PARM FLD TOO LING'
OUTNO    ABEND 996
MOVEPARM MVC   PARMFLD(0),2(4)
PARMFLD  DC    CL80' '
         LA    0,PRINLINE
         BAL   11,PUT
         B     CLOSE2
PDCBNO   MVC   PRINLINE+82(50),=C'     Z260M   PARTITIONED DATA SET CAN*
               NOT BE OPENED'
         LA    0,PRINLINE
         BAL   11,PUT
         MVC   DDNAME2(8),BLANK    BLANK INVALID DDNAME
         TM    SWITCH,X'20'
         BO    ABEND
         B     GETCARD
MOVE1    MVC   DIRENTRY(0),2(4)
LINES    DC    P'60'
         END
