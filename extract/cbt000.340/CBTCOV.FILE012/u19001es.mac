         MACRO
&NAME    BZR   &R
&NAME    BCR   8,&R                BR ON ZERO
         MEND
         MACRO
&NAME    BNZR  &R
&NAME    BCR   7,&R                BR ON NOT ZERO
         MEND
         MACRO
&NAME    BMR   &R
&NAME    BCR   4,&R                BR ON MINUS OR MIXED
         MEND
         MACRO
&NAME    BOR   &R
&NAME    BCR   1,&R                BR ON OVERFLOW OR IF ONES
         MEND
         MACRO
&NAME    BPR   &R
&NAME    BCR   2,&R                BR ON PLUS
         MEND
         MACRO
&NAME    BNER  &R
&NAME    BCR   7,&R                BR ON NOT EQUAL
         MEND
         MACRO
&NAME    BNLR  &R
&NAME    BCR   11,&R               BR ON NOT LOW
         MEND
         MACRO
&NAME    BNHR  &R
&NAME    BCR   13,&R               BR ON NOT HIGH
         MEND
         MACRO
&NAME    BER   &R
&NAME    BCR   8,&R                BR ON EQUAL
         MEND
         MACRO
&NAME    BLR   &R
&NAME    BCR   4,&R                BR ON LOW
         MEND
         MACRO
&NAME    BHR   &R
&NAME    BCR   2,&R                BR ON HIGH
         MEND
         MACRO
&NAME    BNPR  &R
&NAME    BCR   13,&R               BR ON NOT PLUS
         MEND
         MACRO
&NAME    BNMR  &R
&NAME    BCR   11,&R               BR ON NOT MINUS
         MEND
         MACRO
&NAME    BNOR  &R
&NAME    BCR   14,&R               BR IF NOT ONES
         MEND
         MACRO
&NAME    NUMBR
         B     F&SYSNDX+6
A&SYSNDX DC    X'FF00FFFFFFFFFF00FFFFFFFFFF00'
B&SYSNDX DC    X'F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0'
C&SYSNDX DC    XL10'00'
         DC    X'FAFBFCFDFEFF'
X&SYSNDX DC    F'0'                SAVE AREA REGISTER 2
D&SYSNDX NC    B&SYSNDX,0(14)
E&SYSNDX XC    B&SYSNDX,=20X'F0'
F&SYSNDX TRT   0(0,14),C&SYSNDX-240
&NAME    MVI   D&SYSNDX+1,X'00'    SET INSTR. LENGTHS TO ZERO
         MVI   E&SYSNDX+1,X'00'
         MVI   F&SYSNDX+1,X'00'
         BCTR  15,0
         ST    2,X&SYSNDX          SAVE REGISTER 2
         MVC   B&SYSNDX,=20X'F0'   SET MASK TO LOWEST VALID CHARACTER
         EX    15,D&SYSNDX         EXECUTE AND WITH DATA LENGTH IN R15
         EX    15,E&SYSNDX
         BM    G&SYSNDX            ERROR IF ANY BITS LEFT ON
         EX    15,F&SYSNDX         EXECUTE TRANSLATE AND TEST
         BC    8,H&SYSNDX          IF  OK  BYPASS ERROR ROUTINE
G&SYSNDX LR    14,15               ERROR ROUTINE SETS REGS.14 & 15 EQL.
H&SYSNDX L     2,X&SYSNDX          RESTORE REGISTER 2
         MEND
         MACRO
&NAME    PROGOUT  &RC
         LCLA  &A
&NAME    L     R13,4(R13)
         LM    R14,R12,12(R13)
         AIF   (K'&RC GT 0).G01
         MNOTE 0,'RC OMITTED RC=0 ASSUMED'
&A       SETA  0
         AGO   .G03
.G01     ANOP
&A       SETA  &RC
         AIF   (&A GT 15).ERROR
.G03     LA    R15,&A.(R0,R0)
         BR    R14
         MEXIT
.ERROR   MNOTE 8,'INVALID RETURN CODE'
         MEND
         MACRO
&NAME    PROGIN
         ENTRY SAVEAREA
&NAME    SAVE  (14,12)
         BALR  R5,R0
         USING *,R5
         CNOP  6,8
         ST    R13,*+22
         LR    R6,R13
         LA    R13,*+12
         ST    R13,8(R6)
         B     *+76
SAVEAREA DS    9D
         MEND
         MACRO
&NAME    MODOUT &R1,&R2,&R3,&R4,&R5,&R6
         LCLA  &A,&B
&NAME    L     R13,4(R13)
         AIF   (K'&R6 EQ 0).M01
         MNOTE 1,'NO MORE THAN 5 REGS CAN BE SPECIFIED- REST IGNORED'
.M01     AIF   (K'&R1 EQ 0).M09
&A       SETA  &R1
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AIF   (K'&R2 EQ 0).M09
&A       SETA  &R2
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AIF   (K'&R3 EQ 0).M09
&A       SETA  &R3
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AIF   (K'&R4 EQ 0).M09
&A       SETA  &R4
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AIF   (K'&R5 EQ 0).M09
&A       SETA  &R5
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AGO   .M09
.ERROR   MNOTE 1,'REGS 13,14 AND 15 CANNOT BE CHANGED- MACRO IGNORED'
         MEXIT
.M09     RETURN (14,12)
         MEND
         MACRO
&NAME    MODIN
         CNOP  4,8
&NAME    SAVE  (14,12)
         BALR  R5,R0
         USING *,R5
         ST    R13,*+26
         LR    R6,R13
         LA    R13,*+16
         ST    R13,8(R6)
         LM    R6,R10,0(R1)
         B     *+76
         DS    9D
         MEND
         MACRO
&NAME    LPYR  &INPUT,&NOLPYR
         AIF   ('&INPUT' NE ' ').NO
         MNOTE 'FIRST PARAMETER OMITTED'
         AGO   .N1
.NO      AIF   ('&NOLPYR' NE ' ').N2
         MNOTE 'SECOND PARAMETER OMITTED'
.N1      MNOTE 'GENERATION TERMINATED'
         MEXIT
.N2      ANOP
&NAME    TM    &INPUT,X'01'
         BO    &NOLPYR
         TM    &INPUT,X'12'
         BM    &NOLPYR
         MEND
         TITLE 'B340113  SUBROUTINE TO PRINT REPORTS.                 '
B3401    CSECT
         ENTRY B340102             INSERT MODULE NUMBER CHANGING
B340102  EQU   *                   PROGRAMMER CODE TO V.M. LEVEL
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         ENTRY CPD002
         SPACE 3
***********************************************************************
*                                                                     *
*   PROGRAM B3401     PROGRAMMER D.S.THOMSON        ASSIGNED 17/11/70 *
*                                                                     *
*                           MODULE FUNCTIONS                          *
*                           ----------------                          *
*  SUBROUTINE FOR PRODUCING PRINTED REPORTS. PUTS OUT DETAIL LINES    *
*  AND ALSO HANDLES HEADING ROUTINES, PAGE OVERFLOW, SHEET COUNTING,  *
*  AND DETERMINING AND EDITING DATE, WHERE DATE POSITION IS BLANK     *
*                                                                     *
         SPACE 2
*                             PARAMETERS                              *
*                             ----------                              *
*  1. ADDRESS OF HEADINGS, UP TO 8 LINES. ANY ASA CC'S MAY BE USED    *
*     BUT THAT FOR FIRST LINE MUST BE 1 . POSITION 92-100 OF LINE 2   *
*     MUST BE LEFT BLANK FOR INSERTION OF SHEET NO. IF POSITION       *
*     111-129 IS BLANK CURRENT DATE WILL BE INSERTED.                 *
*                                                                     *
*  2. ADDRESS OF PRINT-LINE (WITH ASA CC). FF IN BYTE 0 CAUSES        *
*     MODULE TO CLOSE PRINTER/PRINT-TAPE.                             *
*                                                                     *
*  3. ADDRESS OF 1-BYTE INDICATOR.                                    *
*          0 - NORMAL LINE-COUNTING,SHEET-NUMBERING AND PAGE OVERFLO  *
*              ROUTINES APPLY.                                        *
*          1 - SKIP TO NEW PAGE CONTINUE NORMAL SHEET-NUMBERING.      *
*          2 - AS 1 BUT RESTART SHEET-NUMBERING AT 1 ON NEW PAGE.     *
*                                                                     *
*  4. ADDRESS OF FULLWORD CONTAINING MAX LINES PER PAGE EXCLUDING     *
*     HEADINGS (USUALLY 55).                                          *
*                                                                     *
*                                                                     *
*
**  AMENDMENT TO MODULE B3401    BY D S THOMSON
*
**  ASSIGNED 02/07/71, EFFECTIVE 02/07/71 AMENDMENT IS 0.1
*
**  DETAILS OF AMENDMENT   MODULE NOW CHECKS FOR EOF BEFORE ANYTHING
*   --------------------   ELSE TO AVOID SPECIFICATION EXCEPTION IF
*                          OTHER PARAMETERS NOT CORRECTLY SET UP
*
***********************************************************************
*
**  AMENDMENT TO MODULE B3401    BY N.G.WADDINGTON
*
**  ASSIGNED 18/12/72, EFFECTIVE 31/12/72 AMENDMENT IS 0.2
*
**  DETAILS OF AMENDMENT MODULE AMENDED TO MAKE THE LINE COUNT
*   -------------------- ACCESSIBLE BY OTHER MODULES WHICH USE THE
*                        MACRO LATCH WHEN USING SUBROUTINE B3401.
*
***********************************************************************
         EJECT
         ISEQ  73,80
         MODIN
***********************************************************************
*   TEST FOR END OF RUN AND CLOSE PRINTER/PRINT TAPE IF NECESSARY     *
***********************************************************************
*
         CLI   0(R7),X'FF'         Q. END OF RUN
         BNE   BA04                A. NO, CARRY ON
         MVI   SCO133,X'FF'
         CALL  RG0203,(SCO133)
*
BA03     EQU   *
         ZAP   0(2,R9),CPD002      RETURN PRESENT LINE COUNT        0.2
         MODOUT
*
*   SAVE MAX LINES FIRST TIME THROUGH
*   ---------------------------------
BA04     CLI   CCX001,C'0'         Q. FIRST TIME THRU
         BNE   BA05                A. NO CARRY ON
         LH    R3,2(R9)            SAVE MAX LINES                   0.2
         CVD   R3,SPD008           PACK MAX LINES
         MVI   CCX001,C'1'         RESET SWITCH
*
*   CHECK PARAMETER 3
*   -----------------
*
BA05     CLI   0(R8),C'0'          Q. PARAMETER 3 = 0
         BE    BA06                A-YES,GO CHECK LINE COUNT
*
         ZAP   CPD002(2),=P'0'     A-NO,ZEROISE LINE COUNT
         CLI   0(R8),C'1'          Q-PARAMETER 3 = 1
         BE    BA06                A-YES,GO UPDATE SHEET NO
         ZAP   CPD002A,=P'0'       A-NO,RESTART SHEET NUMBERING
*
***********************************************************************
*   MAIN ROUTINE TO BRANCH TO HEADINGS ROUTINE WHEN NECESSARY AND     *
*   UPDATE SHEET NO, ELSE PUT OUT DETAIL LINE AND UPDATE LINE COUNT   *
***********************************************************************
*
*   CHECK LINE-COUNT
*   ----------------
*
BA06     CP    CPD002(2),=P'0'     Q-HEADINGS REQUIRED
         BE    BB03                A-YES,GO TO HEADINGS ROUTINE
*
BA10     ST    R7,SFD004           PRINT ONE
         LA    R1,SFD004                LINE OF
         CALL  RG0203                        DETAILS
         BAL   R10,BC02            GO UPDATE LINE COUNT
         CP    SPD008,CPD002       Q-PAGE OVERFLOW
         BH    BA03                A-NO,GOBACK
*
         CP    CPD002A,=P'999'     Q-SHEET NO = 999
         BNE   BA12                A-NO,CARRY ON
*
         ZAP   CPD002A,=P'0'       A-YES,RESTART SHEET NUMBERING
BA12     ZAP   CPD002,=P'0'        ZEROISE LINE COUNT
         B     BA03                GOBACK
*
***********************************************************************
*   ROUTINE TO PRINT HEADINGS UP TO MAXIMUM OF EIGHT LINES            *
***********************************************************************
*
BB03     AP    CPD002A,=P'1'       ADD 1 TO SHEET NO
         CLI   CCX001A,C'0'        Q-FIRST TIME THROUGH
         BNE   BB05                A-NO,CARRY ON
         CALL  B1201,(18,SCD018)   A-YES,GET DATE
         MVI   CCX001A,C'1'        RESET SWITCH
*
*   PRINT FIRST HEADINGS LINE
*   -------------------------
*
BB05     MVC   SCO133,0(R6)
         CALL  RG0203,(SCO133)
*
*   CHECK FOR SECOND LINE PRESENT
*   -----------------------------
*
         CLI   133(R6),C'Z'        Q-IS THERE ANOTHER HEADINGS LINE
         BNE   BB08                A-YES,GO FIND IT
         MVI   SCO133,C' '         A-NO,SET UP
         MVC   SCO133+1(132),SCO133     DUMMY
         B     BB10                CARRY ON
BB08     MVC   SCO133,133(R6)      MOVE SECOND LINE TO STORAGE
*
*   SET UP SHEET NO AND DATE IF NECESSARY
*   ------------------------------------
*
BB10     MVC   SCO133+97(4),CXE004 EDIT SHEET
         ED    SCO133+97(4),CPD002A     NUMBER
         MVC   SCO133+92(5),=C'SHEET'
*
         CLC   SCO133+111(18),=CL18' '  Q. DATE PRESENT
         BNE   BB14                A-YES,BYPASS DATE INSERTION
*
         MVC   SCO133+111(18),SCD018    INSERT DATE
BB14     CALL  RG0203,(SCO133)     PRINT SECOND HEADINGS LINE
         CLI   133(R6),C'Z'        Q. WAS THERE A SECOND LINE
         BE    BA10                A. NO, GOBACK
*
*
*   ROUTINE TO DEAL WITH FURTHER HEADING LINES
*   ------------------------------------------
*
         ZAP   CPD001,=P'2'        NOTE 2 LINES PRINTED
         LA    R6,266(R6)          POINT R6 TO NEXT HEADINGS LINE
BB20     CLI   0(R6),C'Z'          Q-IS THERE ANOTHER HEADINGS LINE
         BE    BA10                A-NO,GOBACK
*
         AP    CPD001,=P'1'        UPDATE HEADINGS COUNTER
         CP    CPD001,=P'8'        Q-MORE THAN 8 HEADINGS
         BH    BA10                A-YES,GOBACK
*
         MVC   SCO133,0(R6)        PRINT ONE LINE
         CALL  RG0203,(SCO133)          OF HEADINGS
         LA    R6,133(R6)          POINT R6 TO NEXT HEADINGS LINE
         B     BB20                GO LOOK AT NEXT LINE
*
***********************************************************************
*   ROUTINE TO UPDATE LINE COUNT BY INTERPRETING ASA CONTROL CHAR     *
***********************************************************************
*
BC02     CLI   0(R7),C' '          Q-ASA CC = SPACE
         BNE   BC04                A-NO,CARRY ON
*
         AP    CPD002,=P'1'        A-YES,ADD 1 TO LINE-COUNT
         BR    R10                      AND GOBACK
*
BC04     CLI   0(R7),C'0'          Q-ASA CC = 0
         BNE   BC06                A-NO,CARRY ON
*
         AP    CPD002,=P'2'        A-YES,ADD 2 TO LINE COUNT
         BR    R10                      AND GOBACK
*
BC06     CLI   0(R7),C'-'          Q-ASA CC = -
         BNE   BC08                A-NO,MUST BE INVALID
         AP    CPD002,=P'3'        A-YES,ADD 3 TO LINE COUNT
         BR    R10                      AND GOBACK
*
BC08     WTO   'B3401T INVALID ASA CC'                               OS
         ABEND 92,DUMP                                               OS
*
***********************************************************************
*   CONSTANTS STORAGE AND WORK AREAS                                  *
***********************************************************************
*
SFD004   DS    F                   POINTER TO PRINT AREA
SCO133   DS    CL133               PRINT LINE
CCX001A  DC    C'0'                FIRST TIME SWITCH FOR HEADINGS
CPD002A  DC    PL2'0'              SHEET NUMBER
CCX001   DC    C'0'                FIRST TIME SWITCH FOR MAIN RTN
SCD018   DS    CL18                DATE
CXE004   DC    X'40202020'         EDIT MASK FOR SHEET NO
CPD001   DC    PL1'2'              HEADINGS COUNTER
SCD001   DS    CL1                 DUMP FOR ASA CC
         DS    0D
SPD008   DS    PL8                 DUMP FOR MAX LINES
CPD002   DC    PL2'0'              LINE COUNTER
*
         LTORG
         END
