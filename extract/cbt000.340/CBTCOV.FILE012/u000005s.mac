RTPXDSCB CSECT
*
*  THIS PROGRAM WILL SCAN THE TIOT AND WILL, FOR ALL DDNAMES EXCEPT...
*        JOBLIB,STEPLIB,SYSPRINT,SYSABEND,SYSUDUMP, OR PGM=*.DD
*  ...READ THE JFCB AND OBTAIN THE ASSOCIATED DSCB.
*
*  IF THERE IS AN ENTRY IN THE JFCB FOR ANY OF THE FOLLOWING FIELDS...
*  RECFM,EXPDT,BLKSIZE,LRECL,DSORG,OPTCD, SPACE TYPE AND SECONDARY QTY
*  ...THE JFCB ENTRY WILL REPLACE THE DSCB ENTRY AND THE DSCB WILL
*  BE WRITTEN BACK TO THE VTOC.
*
*  IF A SYSPRINT CARD IS PROVIDED THE PROGRAM WILL PRINT THE OLD AND
*  NEW VERSIONS OF THE DSCB (IN THE DUMP FORMAT OF LISTVTOC) PLUS A
*  PLAIN MESSAGE STATING WHICH FIELDS HAVE BEEN ALTERED.
*
*  IF USED, SYSPRINT HAS RECFM FBM, LRECL 121, BLKSIZE 1452 CODED
*
*  AN EXAMPLE OF THE JCL REQUIRED TO CHANGE THE EXPIRY DATE FIELD OF
*  SYS1.LINKLIB, AND TO CORRECT THE BLOCK SIZE OF X.USERLIB..........
*        //XDSCB   EXEC    PGM=RTPXDSCB
*        //SYSPRINT  DD    SYSOUT=C
*        //FIRST   DD      DSN=SYS1.LINKLIB,DISP=SHR,LABEL=EXPDT=99365
*        //SECOND  DD      DSN=X.USERLIB,VOL=REF=....,DISP=SHR,
*        //                DCB=BLKSIZE=7280
*
*
*        BEGIN
*  THIS IS THE EXPANSION OF THE RESPOND TSG BEGIN MACRO
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'        SAVE AREA
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         OPEN  (PRINTER,(OUTPUT))
         TM    PRINTER+48,16
         BO    *+8
         MVI   PRSW,255
         TIME
         ST    0,TIME
         MVI   TIME+3,12
         ST    1,DATE
         ED    TIMEF,TIME
         ED    DATEF,DATE+1
         L     1,16          CVT
         L     1,0(1)        TCB DW
         L     1,4(1)        CURR TCB
         L     2,12(1)       TIOT
         LA    2,24(2)       FIRST DDNAME
         SR    3,3
SRCHTIOT IC    3,0(2)
         LTR   3,3
         BC    8,END
         CLC   4(8,2),JOBLIB
         BE    NEXTDDN
         CLC   4(8,2),STEPLIB
         BE    NEXTDDN
         CLC   4(8,2),SYSPRINT
         BE    NEXTDDN
         CLC   4(8,2),PGM
         BE NEXTDDN
         CLC   4(8,2),SYSABEND
         BE    NEXTDDN
         CLC   4(8,2),SYSUDUMP
         BE    NEXTDDN
         CLC   4(8,2),BLANKS
         BNE   PROCESS
NEXTDDN  AR    2,3
         B     SRCHTIOT
PROCESS  MVC   VTOC+40(8),4(2)
         MVC   DUMMY+40(8),4(2)
         RDJFCB  (DUMMY)
         MVC   REST,JFCB+44
         OBTAIN  CAMLST
         LTR   15,15
         BC    7,DSCBERR
         CLI   PRSW,0
         BNE   CHANGE
         MVC   LINE+46(19),OLDDSCB
         BAL   5,PRINT
         MVC   LINE(44),JDSN
         BAL   5,PRINT
         LA    4,LINE+10
*        HEXPRINT  DSCB,(4),55
*  THE FOLLOWING CODE IS THE EXPANSION OF THE RESPOND TSG HEXPRINT MACR
         CNOP  0,4
         BAL   1,*+36
         DC    A(0)
         DC    A(DSCB)
         DC    H'55'
         DC    AL1(0)
         DC    AL1(0)
         DC    CL16'0123456789ABCDEF',V(TSGHEXPT)
         ST    4,0(1)
         L     15,28(1)
         BALR  14,15
TSGHEXPT CSECT
         STM   0,7,20(13)
         LM    4,5,0(1)
         LH    0,8(1)
         SR    3,3
         SR    6,6
         SR    7,7
         IC    6,10(1)
         IC    7,11(1)
         IC    3,0(5)
         SLDL  2,28
         SRL   3,28
         IC    2,12(1,2)
         IC    3,12(1,3)
         STC   2,0(4)
         STC   3,1(4)
         LA    4,2(4)
         LA    5,1(5)
         BCT   6,72(15)
         AR    4,7
         IC    6,10(1)
         BCT   0,26(15)
         LM    0,7,20(13)
         BR    14
RTPXDSCB CSECT
         BAL   5,PRINT
         LA    6,DSCB+55
*        HEXPRINT  (6),(4),41
*  THIS IS A SECOND USE OF THE HEXPRINT MACRO
         CNOP  0,4
         BAL   1,*+36
         DC    A(0)
         DC    A(0)
         DC    H'41'
         DC    AL1(0)
         DC    AL1(0)
         DC    CL16'0123456789ABCDEF',V(TSGHEXPT)
         ST    4,0(1)
         ST    6,4(1)
         L     15,28(1)
         BALR  14,15
         BAL   5,PRINT
         BAL   5,PRINT
CHANGE   MVC   LINE(21),FCHA
         LA    4,LINE+21
         OC    JEXPDT,JEXPDT
         BC    8,*+20
         MVC   DEXPDT,JEXPDT
         MVC   0(6,4),EXPDT
         LA    4,6(4)
         OC    JDSORG,JDSORG
         BC    8,*+20
         MVC   DDSORG,JDSORG
         MVC   0(6,4),DSORG
         LA    4,6(4)
         OC    JRECFM,JRECFM
         BC    8,*+20
         MVC   DRECFM,JRECFM
         MVC   0(6,4),RECFM
         LA    4,6(4)
         OC    JOPTCD,JOPTCD
         BC    8,*+36
         CLI   JOPTCD,X'24'
         BNE   *+12
         OI    DSCB+93,16
         B     *+10
         MVC   DOPTCD,JOPTCD
         MVC   0(6,4),OPTCD
         LA    4,6(4)
         OC    JBLKSI,JBLKSI
         BC    8,*+20
         MVC   DBLKSI,JBLKSI
         MVC   0(8,4),BLKSIZE
         LA    4,8(4)
         OC    JLRECL,JLRECL
         BC    8,*+20
         MVC   DLRECL,JLRECL
         MVC   0(6,4),LRECL
         LA    4,6(4)
         OC    JSECQTY,JSECQTY
         BC    8,*+20
         MVC   DSECQTY,JSECQTY
         MVC   0(21,4),SECQTY
         LA    4,21(4)
         BAL   5,PRINT
         BAL   5,PRINT
         MVC   KEY,JDSN
         OPEN  (VTOC,(OUTPUT)),TYPE=J
*  THE DUMMY DCB IS OPENED AND CLOSED TO WROTE BACK THE CORRECT
*  DSNAME IN THE JFCB SO THAT THE END OF STEP DISPOSITION MESSAGES
*  DO NOT SHOW BLANK (REALLY 44 X'04') DSNAMES.
         OPEN  (DUMMY),TYPE=J
         CLOSE (DUMMY)
         MVC   BLKREF+3(5),DCCHHR
         XDAP  ECB,WK,VTOC,DSCB,96,(KEY,44),BLKREF
         WAIT  ECB=ECB
         CLI   ECB,127
         BNE   DSCBERR
         CLOSE (VTOC)
         MVC   LINE+46(19),NEWDSCB
         BAL   5,PRINT
         LA    4,LINE+10
*        HEXPRINT  DSCB,(4),55
*  HERE IS A THIRD USE OF THE HEXPRINT MACRO
         CNOP  0,4
         BAL   1,*+36
         DC    A(0)
         DC    A(DSCB)
         DC    H'55'
         DC    AL1(0)
         DC    AL1(0)
         DC    CL16'0123456789ABCDEF',V(TSGHEXPT)
         ST    4,0(1)
         L     15,28(1)
         BALR  14,15
         BAL   5,PRINT
         LA    6,DSCB+55
*        HEXPRINT  (6),(4),41
*  HERE IS A FOURTH USE OF THE RESPOND TSG HEXPRINT MACRO
         CNOP  0,4
         BAL   1,*+36
         DC    A(0)
         DC    A(0)
         DC    H'41'
         DC    AL1(0)
         DC    AL1(0)
         DC    CL16'0123456789ABCDEF',V(TSGHEXPT)
         ST    4,0(1)
         ST    6,4(1)
         L     15,28(1)
         BALR  14,15
         BAL   5,PRINT
         BAL   5,PRINT
         BAL   5,PRINT
         B     NEXTDDN
END      MVC   LINE+46(19),ENDMSG
         BAL   5,PRINT
         CLOSE (PRINTER)
*        GOBACK
*  THIS IS THE EXPANSION OF THE RESPOND TSG GOBACK MACRO
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),X'FF'
         LA    15,0
         BR    14
PRINT    CLI   PRSW,0
         BCR   7,5
         SP    PCNTR,ONE
         BC    2,PUTLINE
         PUT   PRINTER,NP
         ZAP   PCNTR,PLIMIT
         PUT   PRINTER,TITLE
PUTLINE  PUT   PRINTER,CC
         MVC   LINE,BLANKS
         BR    5
DSCBERR  L     1,ERRADTAB-4(15)
         MVC   ERRMA+26(16),0(1)
         MVC   LINE(42),ERRMA
         MVC   LINE+45(10),DDNIS
         MVC   LINE+55(8),4(2)
         BAL   5,PRINT
         SP    PCNTR,PCNTR
         B     NEXTDDN
DSCBWERR MVC   LINE(42),WERRM
         MVC   LINE+42(8),4(2)
         BAL   5,PRINT
         CLOSE (VTOC)
         SP    PCNTR,PCNTR
         B     NEXTDDN
PRINTER  DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBM,LRECL=121,           *
               BLKSIZE=1452,MACRF=(PM)
DUMMY    DCB   DDNAME=A,DSORG=DA,MACRF=(E),EXLST=XLIST
VTOC     DCB   MACRF=(E),DDNAME=A,DSORG=DA,EXLST=YLIST
XLIST    DC    X'87',AL3(JFCB)
YLIST    DC    X'87',AL3(DFCB)
DFCB     DC    44X'04'
REST     DC    CL132' '
JFCB     DS    0CL176
JDSN     DS    CL44
         DS    CL39
JEXPDT   DS    CL3
         DS    CL12
JDSORG   DS    C
         DS    C
JRECFM   DS    C
JOPTCD   DS    C
JBLKSI   DS    CL2
JLRECL   DS    CL2
         DS    CL12
JVOL     DS    CL6
         DS    CL24
         DS    CL7
JSECQTY  DS    CL4
         DS    CL17
ERRADTAB DC    A(M1,M2,M3,M4,M5)
TIME     DC    F'0'
DATE     DC    F'0'
CAMLST   CAMLST  SEARCH,JDSN,JVOL,DSCB
BLKREF   DC    D'0'
         DS    0D
DSCB     DS    0CL350
         DS    CL12
DEXPDT   DS    CL3
         DS    CL23
DDSORG   DS    C
         DS    C
DRECFM   DS    C
DOPTCD   DS    C
DBLKSI   DS    CL2
DLRECL   DS    CL2
         DS    CL4
DSECQTY  DS    CL4
         DS    CL42
DCCHHR   DS    CL5
         DS    CL249
FCHA     DC    C'FIELDS CHANGED ARE - '
EXPDT    DC    C'EXPDT '
DSORG    DC    C'DSORG '
RECFM    DC    C'RECFM '
OPTCD    DC    C'OPTCD '
BLKSIZE  DC    C'BLKSIZE '
LRECL    DC    C'LRECL '
SECQTY   DC    C'SECONDARY ALLOCATION '
KEY      DC    CL44' '
PGM      DC    CL8'PGM=*.DD'
SNAPOUT  DC CL8'SNAPOUT'
SYSABEND DC    CL8'SYSABEND'
SYSUDUMP DC    CL8'SYSUDUMP'
JOBLIB   DC    CL8'JOBLIB'
STEPLIB  DC    CL8'STEPLIB'
SYSPRINT DC    CL8'SYSPRINT'
OLDDSCB  DC    C'OLD VERSION OF DSCB'
NEWDSCB  DC    C'NEW VERSION OF DSCB'
CC       DC    X'09'
LINE     DC    CL120' '
BLANKS   DC    CL120' '
PRSW     DC    X'00'
NP       DC    X'8B'
PCNTR    DC    PL2'0'
PLIMIT   DC    P'60'
ENDMSG   DC    C'THE END OF RTPXDSCB'
ONE      DC    P'1'
TITLE    DC    X'11'
         DC    CL33' '
         DC    C'RESPOND TSG DSCB AMEND UTILITY RUN AT'
TIMEF    DC    X'4020214B202040'
         DC    C'ON'
DATEF    DC    X'4020214B202020'
         DC    CL34' '
ERRMA    DC    CL42'ERROR IN OBTAINING DSCB - '
M1       DC    CL16'VOL. NOT MOUNTED'
M2       DC    CL16'DSCB NOT FOUND'
M3       DC    CL16'I/O ERR IN VTOC'
M4       DC    CL16'S.N.O. - 16'
M5       DC    CL16'S.N.O. - 20'
DDNIS    DC    C'DDNAME IS '
WERRM    DC    C'I/O ERROR WHEN RE-WRITING DSCB FOR DDNAME '
         END
