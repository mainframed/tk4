         MACRO
         IEFJFCBN
INFMJFCB EQU   *
JFCBDSNM DS    CL44                    DATA SET NAME
         DS    28C
JFCBMASK DS    CL8                     DATA MANAGMENT MASK
         DS    14C
JFCDEN   DS    BL1                     TAPE DENSITY USED TO RECGN MT
         DS    3C
JFCDSORG DS    CL2
         DS    18C
JFCBVOLS DS    CL30                    VOLUME SERIAL NUMBERS(FIRST 5)
         MEND
SRE00    START
         TITLE 'J8SRE -- MOD 00 -- SELECTIVE RECORD EDIT - FCP MODULE'
         INMD
         USING PRAMDUM,6
         LR    6,1                     ADDR. INTER-MODULAR PARAMS.
         L     3,=A(INPF)              ADDR. INPUT DCB
         USING IHADCB,3
         L     1,PINDIC                ) ADDR.
         LA    1,1(1)                  ) INDICATOR
*
         CLI   0(1),C'G'               GET ?
         BL    Z11A                    LO - CLOSE
         BE    Z05A                    EQ - GET
         CLI   0(1),C'P'               PUT ?
         BE    Z06A                    EQ - PUT
         BL    Z00B
Z00A     CLI   0(1),C'S'
         BL    Z09A
         BE    Z11B
         BH    Z08C
*
**   OPEN ALL DATASETS
*
Z00B     EQU   *
         L     3,X'10'                 @ CVT
         L     3,0(3)                  @ TCB WORDS
         L     3,4(3)                  @ TCB (OURS)
         L     3,12(3)                 @ TIOT
         XR    10,10
LOOKFOR  CLC   28(8,3),=C'OUTF    '    SEARCH FOR OUTF DD CARD
         BE    FOUND
         IC    10,24(3)
         AR    3,10
         CLI   24(3),0
         BNE   LOOKFOR                 IF NO DD CARD FOUND ----
         OI    NOOUTF1+1,X'F0'         SET BRANCHES TO
         OI    NOOUTF2+1,X'F0'          NOT ACCESS OUTF DCB
         OI    NOOUTF3+1,X'F0'
FOUND    EQU   *
*
** ACCESS JOB FILE CONTROL BLOCKS
*
         USING DMJFCB,10
*
         RDJFCB MF=(E,EX1)             PULL JFCB'S IN
         LA    10,CBLK1
         LA    9,DDIN                  YES
         BAL   11,Z01B
*
Z01A     LA    10,CBLK2
         LA    9,DDOUT
         BAL   11,Z01B
         B     Z02A
*
** TO OBTAIN A TAPE SERIAL NO. (IF REQD.)
*
Z01B     CLC   JFCBVOLS(6),=C'RAA999'  DUMMY TSN ?
         BCR   7,11                    NO
         MVC   MESS1+17(5),0(9)
         WTOR  MF=(E,MESS1)                 ) ASK OPERATOR
         WAIT  ECB=ECB1                     ) FOR CORRECT
         NI    ECB1,X'00'                   )    TSN
         DOMD  SRE06,MESS1
         MVC   JFCBVOLS(6),TSNO        MOVE IT TO JFCB
*
         OI    JFCBMASK+4,X'80'        SET JFCB TO BE WRITTEN BACK
         BR    11
*
Z02A     OPEN  (PRINT,OUTPUT),TYPE=J
*
Z02B     OPEN  (INPF,INPUT),TYPE=J
         RDJFCB (INPF)                 (I HOPE) ACCESS INPUT JFCB
         LA    10,CBLK1
         L     11,PTINFO               CREATE HEADING CONTAINING
         MVC   0(44,11),JFCBDSNM       DATA SET NAME
         MVC   44(9,11),=C' VOL=SER='
         MVC   53(6,11),JFCBVOLS       SERIAL NO.
         TM    JFCDSORG,X'80'          ISAM DATASET ?
         BNO   Z02C
         MVC   38(6,11),=C'(ISAM)'
         L     1,PINDIC
         MVI   4(1),C'I'               ISAM INPUT
         CLOSE INPF
         LR    1,6
         DOMD  SRE07                   OPEN ISAM DATASET
Z02C     EQU   *
NOOUTF1  NOP   Z02D
         OPEN  (OUTF,(OUTPUT))
Z02D     EQU   *
         B     Z12A
*
** ROUTINE TO OBTAIN AN INPUT BLOCK
*
Z05A     L     1,PINDIC
         CLI   4(1),C'I'               ISAM INPUT ?
         BNE   Z05B
         LR    1,6                     YES -
         DOMD  SRE07                   ENTER ISAM MODULE
         B     Z12A                     TO GET INPUT
Z05B     GET   INPF
         ST    1,PIOREG                STORE ADDR. OF BLOCK
         L     9,PRLTH
         MVC   2(2,9),DCBLRECL         STORE BLOCK LTH
         LA    1,INPF
         USING IHADCB,1
         TM    DCBIFLGS,X'C0'          ERROR CONDITION ?
         L     1,PINDIC
         MVI   3(1),X'00'
         BZ    *+8
         MVI   3(1),C'P'               RECORD ERROR CONDITION
         DROP  1
         B     Z12A
         SPACE 3
*
** ROUTINE FOR OUTPUT
*
Z06A     L     9,PROUT
         MVC   Z08B+1(1),0(9)          STORE CTLCHAR
*
         TM    FLAG,X'01'              MAG. TAPE OUTPUT
         BZ    Z07A                    NO.
*
         MVO   STORE(2),0(1,9)         )
         TR    STORE(1),TABLE1         )  CONVERT CTLCHAR TO
         MVZ   0(1,9),STORE            )  A.I.S. STANDARD
         B     Z08A                    )
*
Z07A     TR    0(1,9),TABLE2           ) CONVERT TO I.B.M. STANDARD
*
Z08A     PUT   PRINT,(9)               OUTPUT LINE
*
Z08B     MVI   0(9),X'00'              RESTORE CTLCHAR.
         MVI   1(9),C' '               )  CLEAR
         MVC   2(159,9),1(9)           )  LINE
         B     Z12A
Z08C     L     9,PRECORD
NOOUTF2  NOP   Z08D
         PUT   OUTF,(9)
Z08D     EQU   *
         B     Z12A
         SPACE 3
*
** TO SET INPUT MEDIA TO BEGINNING OF DATA
*
Z09A     L     1,PINDIC
         CLI   4(1),C'I'               ISAM INPUT
         BNE   Z09B
         LR    1,6                     YES -
         DOMD  SRE07                   ENTER ISAM MODULE
         B     Z12A                     TO CLOSE & OPEN INPUT
*
Z09B     CLOSE (INPF,REREAD)
         OPEN  (INPF,(INPUT))
         B     Z12A
         SPACE 3
*
** TO CLOSE FILES
*
Z11A     CLOSE (PRINT)
         L     1,PINDIC
         CLI   4(1),C'I'               ISAM INPUT
         BNE   Z11AA
         LR    1,6
         DOMD  SRE07
         B     Z12A
*
Z11AA    CLOSE (INPF)
NOOUTF3  NOP   Z11AB
         CLOSE OUTF
Z11AB    EQU   *
         B     Z12A
         SPACE 3
*
** ROUTINE TO ACCESS PARAMETER CARDS
*
Z11B     OPEN  (PRAMS,(INPUT))
         LA    9,23                      MAX OF 23 CARDS INPUT
         LA    10,AREA                           ALLOWED
Z11C     GET   PRAMS,(10)
         LA    10,80(10)
         BCT   9,Z11C
         SH    10,=H'80'                      )
         MVC   0(6,10),=C' END  '             )
         WTO   'J8SA EXCESS PARAMS IGNORED'   )
*
Z11D     CLOSE PRAMS
         MVC   0(6,10),=C' END  '      MOVE AN END IN.(JUST IN CASE)
         MVC   PRTP(4),=A(AREA)
         B     Z12A
*
** END OF INPUT
*
ENDUP    L     1,PINDIC
         MVI   1(1),C'E'
*
*
*
Z12A     OUTMD
*
         EJECT
*
** STORAGE & CONSTANTS
*
EX1      RDJFCB  (INPF,,PRINT,(OUTPUT)),MF=L
*
ECB1     DC    F'0'
*
EXTAB1   DS    0F
         DC    X'87'
         DC    AL3(CBLK1)
CBLK1    DS    44F
EXTAB2   DS    0F
         DC    X'05'
         DC    AL3(PREXIT)             @ DCB EXIT ROUTINE
         DC    X'87'
         DC    AL3(CBLK2)
CBLK2    DS    44F
*
DDIN     DC    CL5'INPF'
DDOUT    DC    CL5'PRINT'
*
*        DCB EXIT ROUTINE FOR PRINT DCB
*
         USING *,15
PREXIT   LR    3,1
         MVC   DCBLRECL,=H'133'
         CLC   DCBBLKSI,=H'0'          OUTPUT BLKSIZE GIVEN ?
         BNE   Z14A
         MVC   DCBBLKSI,=H'1330'
Z14A     CLI   DCBDEVT,X'81'           TAPE OUTPUT ?
         BNE   Z14B                     NO-
         OI    FLAG,X'01'
         MVC   DCBBLKSI,=H'644'
         MVC   DCBLRECL,=H'161'
Z14B     BR    14
         LTORG
         DROP  15
*
FLAG     DC    X'00'
*
MESS1    WTOR  'J8S0       SEND TSN',TSNO,6,ECB1,MF=L
*
TSNO     DS    CL6
*
TABLE1   DC    X'80'
STORE    DC    X'00000090000000A0000000B00000F0'
*
*
TABLE2   DC    X'01091119'
         DC    60X'00'
         DC    16X'09'
         DC    49X'00'
         DC    X'899199A1A9B1B9C1C9D1D9E1'
         DC    51X'00'
*
** DATASET TO BE EDITED
*
INPF     DCB   DDNAME=INPF,MACRF=(GL),EODAD=ENDUP,                     X
               EROPT=ACC,EXLST=EXTAB1,                                 X
               RECFM=U,DSORG=PS
*
** OUTPUT MEDIUM
*
OUTF     DCB   DDNAME=OUTF,MACRF=(PM),DSORG=PS
PRINT    DCB   DDNAME=PRINT,MACRF=(PM),DSORG=PS,EXLST=EXTAB2,          X
               RECFM=FBM
*
** PARAMETERS INPUT
*
PRAMS    DCB   DDNAME=SYSIN,MACRF=(GM),DSORG=PS,EODAD=Z11D
*
AREA     DS    1840C
*
*
         DCBD  DSORG=(PS)
*
DMJFCB   DSECT
         IEFJFCBN
*
** INTER-MODULAR PARAMETERS
*
PRAMDUM  DSECT
PARAM    DS    F
PINDIC   DS    F
PIOREG   DS    F
PRLTH    DS    F
PROUT    DS    F
         DS    F
PRECORD  DS    F
         DS    F
PRTP     DS    F
         DS    F
PTINFO   DS    F
*
*              DUMMY SECTION FOR SELECTION PARAMETER TABLE
*
SPARAM   DSECT
STYPE    DS    C                       FILE TYPE (V,F,U OR I)
SINDIC   DS    C                       INDICATORS
STLTH    DS    H                       REC.LTH. (TYPE F ONLY)
SFORMAT  DS    C                       CONTROL FORMAT (X,C OR P)
SCLTH    DS    C                       CONTROL LTH. (M/C LTH)
SCDISP   DS    H                       CONTROL DISPLACEMENT
SSEARCH  DS    C                       SEARCH (S) OR BLOCK (B)
SKEY     DS    C                       KEY
SKDISP   DS    H                       KEY DISP. (FFFF IF NONE)
SLOW     DS    20C                     LOW CONTROL
SHIGH    DS    20C                     HIGH CONTROL
         END
