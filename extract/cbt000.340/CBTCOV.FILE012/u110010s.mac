//XM5000  JOB 1,CIE,MSGLEVEL=(1,1)
//  EXEC ASMFCL
//ASM.SYSIN DD *
XM5000   CSECT
***********************************************************************
*                                                                     *
*    THIS PROGRAM WILL SELECT TYPES   1,AND 12 RECORDS FROM SMF FILES *
*                                                                     *
*    THE RECORDS ARE REFORMATTED AND VARIOUS BINARY FIELDS            *
*                                                                     *
*         ARE CONVERTED TO DECIMAL AND PACKED DECIMAL                 *
*                                                                     *
*    THE RECORDS ARE STORED FOR ANALYSIS BY OTHER PROGRAM(5)          *
*                                                                     *
*    THE FORMAT OF THE OUTPUT RECORDS IS GIVEN LATER                  *
*                                                                     *
*    A CONTROL CARD IS USED TO SPECIFY THE PERIOD FOR WHICH RECORDS   *
*      ARE TO BE SELECTED                                             *
*    THE CARD FORMAT IS  CC 1-5   YYDDD   FIRST DAY OF PERIOD         *
*                           6-10  YYDDD   LAST  DAY OF PERIOD         *
*                           11   = Y      VS1 SWITCH                  *
*                                = N      MFT SWITCH                  *
***********************************************************************
           EJECT
***********************************************************************
*                                                                     *
*    THE  LAYOUT  OF  THE  WAIT  RECORD  (TYPES  1 &  12)             *
*                                                                     *
***********************************************************************
*      *           *      *     *      *      *    *                  *
* START*DESCRIPTION*LENGTH* NO  *ALPHA *PACKED*DEC *   REMARKS        *
*  POS.*           *      *BYTES*OR NUM*      *POS.*                  *
***********************************************************************
*      *           *      *     *      *      *    *                  *
*  00  * REC TYPE  *   2  *  2  *  N   *      *    * =X'FOF1'OR'F1F2' *
***********************************************************************
*  02  * TIME      *   9  *  5  *  N   *  P   *  2 *  IN SECONDS      *
***********************************************************************
*  07  * DATE      *   7  *  4  *  N   *  P   *    *   00YYDDD        *
***********************************************************************
*  11  * SYSTEM ID *   2  *  2  *  A/N *      *    *                  *
***********************************************************************
*  13  *MODEL ID   *   I  *  I  *  A/N *      *    *                  *
***********************************************************************
*  15  *WAIT TIME  *   9  *  5  *  N   *  P   *  2 *  IN SECONDS      *
***********************************************************************
*  20  *EXPIR. TIME*   9  *  5  *  N   *  P   *  2 *  IN SECONDS      *
***********************************************************************
*  25  * PAGE INS  *   9  *  5  *  N   *  P   *    *                  *
***********************************************************************
*  30  * PAGE OUTS *   9  *  5  *  N   *  P   *    *                  *
***********************************************************************
*  35  *PAGERECLAIM*   9  *  5  *  N   *  P   *    *                  *
***********************************************************************
           EJECT
**********************************************************************
*                                                                    *
*         REFER  ANY  QUERIES  TO                                    *
*                                                                    *
*                        W. O. FLYNN                                 *
*                                                                    *
*                        CORAS IOMPAIR EIREANN                       *
*                                                                    *
*                        COMPUTER SERVICES DEPT.                     *
*                                                                    *
*                        ORIEL ST.                                   *
*                                                                    *
*                        DUBLIN 1                                    *
*                                                                    *
*                                  742941  EX 133                    *
*                                                                    *
*                                                                    *
*                                                                    *
*                                                                    *
**********************************************************************
         EJECT
*****                                                             *****
*    SAVE REGISTERS  AND SET UP EQUATE VALUES                         *
*****                                                             *****
BEGIN    SAVE  (14,12)
         BALR  10,0
         USING *,10
FIRST    ST    13,SAVE+4
         LR    4,13
         LA    13,SAVE
         ST    13,8(4)
         B     SAVE+72
SAVE     DS    18F
*
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
*****                                                             *****
         EJECT
*****
*    OPEN  INPUT  AND  OUTPUT  FILES
*****
         OPEN  (CARDIN,,INREC,,OUTREC,(OUTPUT),OUTPRT,(OUTPUT))
*****
*         READ CONTROL CARD  AND PACK DATA
*****
         GET   CARDIN
         USING CARDREC,R2
         LR    R2,R1
         PACK  DATEFROM(4),FROMD(5)     PACK START OF PERIOD
         PACK  DATETO(4),TOD(5)         PACK END   OF PERIOD
         MVC   VS1SW,SW1
*****
         MVC   OUTPUT,OUTPUT-1          CLEAR OUTPUT FILE AREA
*****                                                             *****
*      READ THE SMF FILE                                              *
*      *  USE R3 AND RELATIVE ADDRESSING TO CHECK IF THE RECORD       *
*         IS EITHER  A TYPE  1 OR 12                                  *
*      *  IF IT IS CHECK THAT THE RECORD IS FOR THE REQUIRED PERIOD   *
*      *  IF THE RECORD DOES NOT SATISFY THE ABOVE CONDITIONS         *
*           GO BACK TO READ THE NEXT RECORD                           *
*                                                                     *
*****                                                             *****
READSMF  GET   INREC
         LR    R3,R1
         CLI   5(R3),X'01'         IS IT TYPE 1
         BE    DATECHK             GO CHECK DATE
         CLI   5(R3),X'0C'         IS IT TYPE 12
         BNE   READSMF             GO READ  ANOTHER  RECORD
*
*
*    CHECK THE DATE
DATECHK  CP    10(4,R3),DATEFROM        IS DATE LO
         BL    READSMF
         CP    10(4,R3),DATETO          IS DATE HI
         BH    READSMF
*****
         EJECT
*****                                                             *****
*         THIS ROUTINE  FORMATS THE RECORD FROM THE SMF RECORDS       *
*                                                                     *
*              IT MOVES THE RECORD TYPE INTO THE FIRST TWO BYTES      *
*              IT INCREMENTS THE RECORD COUNT BY TYPE                 *
*                                                                     *
*              THE OUTPUT RECORD IS CREATED IN A FIXED SIZE AREA      *
*                   CALLED - OUTPUT                                   *
*                                                                     *
*              R3  AND RELATIVE ADDRESSING IS USED FOR THE SMF RECORD *
*                                                                     *
*              R4  AND RELATIVE ADDRESSING IS USED FOR THE OUTPUT REC *
*                                                                     *
*              THE  VS/1 SWITCH IS EXAMINED AND IF = Y                *
*                        THE VS1  DATA IS  CONVERTED                  *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*****                                                             *****
         LA    R4,OUTPUT           SET UP ADDRESSABILITY OF OUTPUT
*
         CLI   5(R3),X'01'         IS IT TYPE 1
         BE    TYPE01
*                                  IT MUST BE TYPE 12
         MVC   0(2,R4),=C'12'      MOVE ID TO REC TYPE 12
         AP    COUNT12,=P'1'       INCREMENT COUNT
         B     REST
*
*
TYPE01   MVC   0(2,R4),=C'01'      MOVE ID TO REC TYPE 1
         AP    COUNT01,=P'1'       INCREMENT COUNT
*
*    MOVE AND CONVERT  REST OF RECORD
REST     MVC   FLWORD(4),CLEARFWD       CLEAR F/W AREA
         MVC   FLWORD(4),6(R3)          TIME TO F/W
         XR    R5,R5                    CLEAR R5
         L     R5,FLWORD                TIME TO R5
         CVD   R5,DBLWORD               TIME TO PD
         MVC   2(5,R4),DBLWORD+3        TIME TO OUTPUT RECORD
*
*
         MVC   7(8,R4),10(R3)      MOVE DATE, SYSTEM ID, AND MODEL-ID
*                                        TO  OUTPUT
*
         MVC   FLWORD(4),CLEARFWD       CLEAR F/W AREA
         MVC   FLWORD(4),18(R3)         WAIT TIME TO F/W
         XR    R5,R5                    CLEAR R5
         L     R5,FLWORD                WAIT TIME TO R5
         CVD   R5,DBLWORD               WAIT TIME TO PD
         MVC   15(5,R4),DBLWORD+3       TIME TO OUTPUT RECORD
*
***
         EJECT
***                                                                 ***
*      THIS  SECTION  APPLIES TO VS/1  TYPE  RECORDS  ONLY            *
***                                                                 ***
         CLI   VS1SW,C'Y'     IS IT A VS/1  RECORD
       BNE   WRITREC
*
*
*    IN THE FIRST RECORD AFTER AN IPL THE FIRST TWO BYTES
*      OF THE EXPIRATION TIME WORD IS = LAST TWO BYTES OF WAIT TIME
*      IF THIS IS SO MOVE THE TIME FIELD INTO THE EXPIRATION FIELD
*      AND CONTINUE PROCESSING AS NORMAL
         CLC   20(2,R3),22(R3)
         BNE   EXPIRT
         MVC   22(4,R3),6(R3)
*
EXPIRT   MVC   FLWORD(4),CLEARFWD       CLEAR F/W AREA
         MVC   FLWORD(4),22(R3)         EXPIRATION TIME TO F/W
         XR    R5,R5                    CLEAR R5
         L     R5,FLWORD                EXPIRATION TIME TO R5
         CVD   R5,DBLWORD                               TO PD
         MVC   20(5,R4),DBLWORD+3                       TO OUTPUT REC
*
         MVC   FLWORD(4),CLEARFWD       CLEAR F/W AREA
         MVC   FLWORD(4),26(R3)         PAGE-INS  TO F/W
         XR    R5,R5                    CLEAR R5
         L     R5,FLWORD                PAGE-INS TO  R5
         CVD   R5,DBLWORD                        TO  PD
         MVC   25(5,R4),DBLWORD+3                TO  OUTPUT RECORD
*
*
         MVC   FLWORD(4),CLEARFWD       CLEAR  F/W
         MVC   FLWORD(4),30(R3)         PAGE-OUTS TO F/W
         XR    R5,R5                    CLEAR R5
         L     R5,FLWORD                PAGE-OUTS TO R5
         CVD   R5,DBLWORD                         TO PD
         MVC   30(5,R4),DBLWORD+3                 TO OUTPUT RECORD
*
*
         MVC   FLWORD(4),CLEARFWD
         MVC   FLWORD(4),34(R3)
         XR    R5,R5
         L     R5,FLWORD
         CVD   R5,DBLWORD
         MVC   35(5,R4),DBLWORD+3
*
***                                                                 ***
*            END OF VS/1  DEPENDENT  DATA                             *
***                                                                 ***
         EJECT
WRITREC  PUT   OUTREC,OUTPUT
*
         MVC   OUTPUT,OUTPUT-1          CLEAR OUTPUT AREA FOR NEW REC
         B     READSMF                  GO BACK AND READ A NEW RECORD
*                                                                     *
*                                                                     *
*                                                                     *
*****                                                             *****
         EJECT
*****                                                             *****
*         THIS SECTION PRINTS THE RECORD COUNTS                       *
*                                                                     *
*                                                                     *
*                                                                     *
*****                                                             *****
ENDOFILE MVI   PRT,X'40'                CLEAR
         MVC   PRT+1(132),PRT                  PRINT AREA
         MVC   HEAD1+83(5),FROMD        DATES TO  HEAD
         MVC   HEAD1+94(5),TOD                        AND  PRINT
         PUT   OUTPRT,HEAD1
*
*           PRINT  RECORD  COUNTS
         MVC   PRT(25),=C'-  NO  OF  TYPE 1  RECS ='
         MVC   PRT+28(10),EDIT
         ED    PRT+28(10),COUNT01
         MVC   PRT+40(23),=C'NO  OF  TYPE 12  RECS ='
         MVC   PRT+65(10),EDIT
         ED    PRT+65(10),COUNT12
         PUT   OUTPRT,PRT
*
*
         MVI   PRT,X'40'
         MVC   PRT+1(132),PRT
         MVC   PRT(15),=C'-  END  OF  JOB'
         PUT   OUTPRT,PRT
*                                                                     *
*****                                                             *****
*                                                                     *
*         CLOSE  FILES                                                *
*                                                                     *
*****                                                             *****
         CLOSE (CARDIN,,INREC,,OUTREC,,OUTPRT)
*                                                                     *
*                                                                     *
*****                                                            ******
*****                                                             *****
*         RESTORE   REGS   AND  RETURN                                *
*****                                                             *****
         L     13,4(13)
         RETURN (14,12)
*****                                                             *****
         EJECT
*****                                                             *****
*              DCB'S                                                  *
*****                                                             *****
CARDIN   DCB   DSORG=PS,MACRF=(GL),DDNAME=DDCARD
INREC    DCB   DSORG=PS,MACRF=(GL),DDNAME=DISKIN,EODAD=ENDOFILE
OUTREC   DCB   DSORG=PS,MACRF=(PM),DDNAME=DISKOUT
OUTPRT   DCB   DSORG=PS,MACRF=(PM),DDNAME=DDPRINT
*****                                                             *****
*         CONSTANTS  AND  DATA  AREAS                                 *
*****                                                             *****
DATEFROM DC    PL4'0'
DATETO   DC    PL4'0'
VS1SW    DC    C' '
SPACE1   DC    C' '
OUTPUT   DS    CL50
*
COUNT01  DC    PL4'0'
COUNT12  DC    PL4'0'
*
         DS    D
FLWORD   DC    X'00000000'
CLEARFWD DC    X'00000000'
DBLWORD  DS    D
*
HEAD1    DC CL103'1    C.I.E  SELECTION  AND  FORMATTING  OF  SMF  TYPE*
                1 AND TYPE 12 RECORDS   FROM         TO          '
PRT      DS CL133
EDIT     DC X'40206B2020206B202120'
*****
*    DSECT FOR CONTROL CARD
*****
CARDREC  DSECT
FROMD    DS    CL5
TOD      DS    CL5
SW1      DS    C
*****
         END
/*
//LKED.SYSLMOD DD DSN=CIEFE.LIBRARY,UNIT=SYSDA,DISP=OLD,VOL=SER=CIESY0
//LKED.SYSIN DD *
    NAME  XM5000(R)
/*
