ZZAYAA   BEGIN
         B     BA00
PRNUMBS  DC    C'01'
         COPY  EQUATES
BA00     EQU   *
         L     R1,0(,R1)           POINT TO PARM DATA
         SR    R3,R3               CLEAR HIGH ORDER BYTES
         ICM   R3,3,0(R1)          Q- ANY PARM DATA
         BZ    ERROR1              A- NO,GO SET RETURN CODE
         BCTR  R3,R0               A- YES,SET LENGTH FOR MOVE
         EX    R3,EX01             MOVE TO CAMLST PARM
         LA    R4,3(R1,R3)         POINT TO END OF PARM
BA02     EQU   *
         CLI   0(R4),C'('          Q- START OF RELATIVE GEN NO
         BE    BA04                A- YES,GO PROCESS
         BCTR  R4,R0               POINT TO PREVIOUS BYTE
         BCT   R3,BA02             Q-START OF DATA
         B     ERROR1              A- YES,INVALID PARM
BA04     EQU   *
         EX    R3,EX02             MOVE DSN PREFIX TO CAMLST PARM
         LA    R4,1(,R3)           POINT PAST   -
         LA    R4,DSN2(R4)               PREFIX
         MVC   0(5,R4),=C'(+1) '   SET RELATIVE GEN NO
         LOCATE CAM1
         LTR   R15,R15             GET VOLUME LIST
         BNZ   ERROR
         LOCATE CAM2               GET NEW GENERATION NUMBER
         LTR   R15,R15
         BNZ   ERROR
         CATALOG CAM3              UPDATE CATALOG
         LTR   R15,R15
         BNZ   ERROR
         SR    R15,R15             SET ZERO RETURN CODE
         B     EXIT
ERROR1   EQU   *
         LA    R15,100             SET INVALID PARM RC
ERROR    EQU   *
EXIT     EQU   *
         L     R13,4(,R13)
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
         SPACE 5
*
** EXECUTED INSTRUCTIONS
*
EX01     MVC   DSN1(0),2(R1)
*
EX02     MVC   DSN2(0),2(R1)
*
** CAMLST ENTRIES
*
CAM1     CAMLST NAME,DSN1,,VOL1
CAM2     CAMLST NAME,DSN2,,VOL2
CAM3     CAMLST CAT,DSN2,,VOL1
*
** DATA AREAS
*
DSN1     DC    CL44' '
DSN2     DC    CL44' '
*
*
         DS    0D
VOL1     DS    CL265
         DS    0D
VOL2     DS    CL265
*
         LTORG
         END
