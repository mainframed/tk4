         MACRO
         MSG   &M
.* THIS MACRO WAS WRITTEN FOR GLCC'S VERSION OF SPZ03DAC, AND CAUSES
.* CHANGES TO BE MADE TO ROUTING CODES FOR THAT CENTRE.
         GBLC  &CENTRE
         GBLB  &FSTIME
         AIF   ('&CENTRE' NE 'GLCC').NOGLCC
         AIF   (&FSTIME).NOSUB
&FSTIME  SETB  1
         B     SP&SYSNDX.A
SP600    EQU   *
         LR    R6,R1
         CLC   13(5,R1),=C'REACT'
         BNE   SP601
         LR    R14,R1
         AH    R14,0(R1)
         MVC   2(2,R1),=X'8000'
         MVC   0(4,R14),=X'00009000'
         B     SP602
SP601    CLC   23(6,R1),=C'FAILED'
         BNE   SP602
         LR    R14,R1
         AH    R14,0(R1)
         MVC   2(2,R1),=X'8000'
         MVC   0(4,R14),=X'00008000'
SP602    SVC   35
         MVC   2(2,R6),=X'0200'
         BR    R7
SP&SYSNDX.A EQU *
.NOSUB   LA    R1,&M
         BAL   R7,SP600
         MEXIT
.NOGLCC  LA    R1,&M
         SVC   35
         MEND
         SPACE 5
         MACRO
         VERBLIST &VERB=,&ROUTE=
         GBLB  &USE
         LCLA &K
         AIF   (T'&VERB EQ 'O' OR T'&ROUTE EQ 'O').Z
&K       SETA  K'&VERB
         AIF   (K'&VERB GT 7).Y
         AIF   (NOT &USE).A
         ORG   *-1
         AGO   .B
.A       ANOP
VERBLIST DS    0F
.B       DC    AL1(&K),AL3(&ROUTE),CL8'&VERB',X'FF'
&USE     SETB  1
         MEXIT
.Z       MNOTE 4,'OPERAND OMITTED.'
         MEXIT
.Y       MNOTE 4,'VERB (&VERB) CONTAINS MORE THAN SEVEN CHARACTERS.'
         MEND
         SPACE 5
         GBLC  &CSECT,&MSG,&CENTRE
&CSECT   SETC  'SPZ03DAC'
&MSG     SETC  '&CSECT'(1,5)
&CENTRE  SETC  'GLCC'
         TITLE '&CSECT - DYNAMIC RECONFIGURATION PROGRAM. PROGRAMMER - ,
               DAVID COWEN.'
*
*
*        THIS FIRST SECTION IS FOR HOUSEKEEPING AND ENVIRONMENT
*        CHECKING. THE GETMAIN FOR A SAVE-AREA IS ESSENTIAL - THE SVC
*        USED CAUSES THE PSW TO BE STORED IN THE TASKS RB. THIS
*        PSW CAN SUBSEQUENTLY BE CHECKED TO ENSURE THAT THE TASK IS IN
*        PROTECT KEY ZERO
*
*
&CSECT   CSECT
         SAVE  (14,12),,*               HOUSEKEEPING
         LR    R11,R15                  *
         BALR  R12,R0                   *
         USING *,R12                    *
         GETMAIN R,LV=72                *
         ST    R13,4(,R1)               *
         ST    R1,8(,R13)               *
         LR    R13,R1                   *
         L     R10,CVTAD                CVT
         L     R11,0(,R10)             FIND TCB WORDS
         L     R10,CVTILK2(,R10)        UCB LOOK-UP TABLE
         L     R11,4(,R11)             FIND MY TCB
         SPACE 2
         TM    28(R11),ZEROPROT        HAS TASK ZERO PROTECT KEY
         BZ    SP001                    YES
         WTO   '&MSG.00I &CSECT NOT A SYSTEM TASK'
         B     SP102
SP001    OPEN  (PARMS)
SP002    GET   PARMS,CARDAREA
         MVC   DRAINROT,=A(SP002)
         MVC   REPLISTA(8),=A(REPLIST,REPLIST+20*4-4)
         LR    R2,R1                    RECORD ADDRESS
         LA    R3,80                    LOOP COUNTER
SP003    CLI   0(R2),C' '               SCAN FOR VERB
         BNE   SP005                    BRANCH IF FOUND
         LA    R2,1(,R2)
         BCT   R3,SP003
SP004    WTO   '&MSG.01I INVALID PARAMETER CARD - IGNORED'
         B     SP002
SP005    LA    R4,VERBLIST              SCAN VERB LIST TO FIND FUNCTION
         LA    R5,0                     *
SP006    CLI   0(R4),X'FF'              END OF LIST
         BE    SP004                    YES - WARN OPERATOR - BAD CARD
         IC    R5,0(,R4)                VERB LENGTH
         EX    R5,VERBCOMP              IS THIS CORRECT ENTRY
         BE    SP007                    YES
         LA    R4,12(,R4)               BUMP TO NEXT ENTRY
         B     SP006
SP007    LA    R5,1(,R5)                CORRECT COUNTERS
         MVC   VERB,4(R4)
         SR    R3,R5
         BNP   SP004                    RUN OUT OF CARD
         ALR   R2,R5
*
*        VERB HAS BEEN FOUND IN LIST - ADDRESS OF ACTION ROUTINE IS NOW
*        OBTAINED BEFORE CHECKING DEVICE LIST SYNTAX AND SEQUENCE
*
         L     R5,0(,R4)                ROUTINE ADDRESS
SP008    CLI   0(R2),C' '               SCAN FOR DEVICE LIST
         BNE   SP009                   FOUND
         LA    R2,1(,R2)
         BCT   R3,SP008
         B     SP004                    ERROR - NO DEVICE LIST
SP009    LR    R3,R2                   LEAVE R2 POINTING TO LIST
         TR    0(3,R3),TOEF
SP010    CLI   3(R3),C' '              END OF LIST
         BE    SP011                   YES
         LA    R4,4(,R3)               IS LIST IN ASCENDING SEQUENCE
         TR    0(3,R4),TOEF            *
         CLC   0(3,R3),0(R4)           *
         BNL   SP004                   NO - ERROR
         LR    R3,R4
         B     SP010
         EJECT
*
*
*        INPUT CARD CHECKING IS NOW COMPLETE. R2 POINTS TO A LIST OF
*        DEVICES WHOSE UCBS ARE TO BE MODIFIED. R5 CONTAINS ADDRESS OF
*        ROUTE TO BE TAKEN.
*        UCBS ARE NOW SCANNED - WHEN MATCH WITH CARD IS FOUND CONTROL
*        IS PASSED TO APPROPRIATE ACTION ROUTINE.
*
*
SP011    LR    R3,R10                   UCB TABLE
SP012    LH    R4,0(,R3)                UCB ADDRESS
         USING UCB,R4
         LA    R3,2(,R3)           TO NEXT ENTRY IN UCB TABLE
         LTR   R4,R4                    NULL ENTRY
         BZ    SP012                    YES
         BM    SP014                    LAST ENTRY - WARN OPERATOR
*        TEMPORY FIX TO HANDLE SUB UCB'S
         TR    0(3,R2),FROMEF
         CLC   UCBNAME,0(R2)
         BNE   SP012
         TR    0(3,R2),TOEF
         BR    R5
SP013    EQU   *
         CLI   3(R2),C','              ANY MORE
         BNE   SP017                    NO - GO TO CLEAN UP THIS CARD
         LA    R2,4(,R2)                TO NEXT ADDRESS
         B     SP011                    GET NEXT UCB
SP014    L     R8,REPEND                DRAIN CARD TO ERROR LIST
SP015    MVC   0(3,R8),0(R2)            *
         TR    0(3,R8),FROMEF          *
         SH    R8,=H'4'                *
         CLI   3(R2),C','              *
         BNE   SP016                   *
         LA    R2,4(,R2)               *
         B     SP015                   *
SP016    ST    R8,REPEND               *
         EJECT
SP017    L     R8,REPEND
         L     R9,=A(REPLIST+20*4-1)    FIND LENGTH OF ERROR LIST
         LA    R8,4(,R8)
         SR    R9,R8                    *
         BNP   SP017A
         EX    R9,MSGMOVE1              MOVE DEVICE LIST TO ERROR MSG
         LA    R9,35(,R9)               SET MESSAGE LENGTH
         STH   R9,MSGERR1               *
         MVC   MSGERR1+13(8),VERB
         MSG   MSGERR1
SP017A   LA    R8,REPLIST               FIND LENGTH OF GOOD LIST
         L     R9,REPSTART              *
         SR    R9,R8                    *
         BNP   SP018                    *
         EX    R9,MSGMOVE2              MOVE LIST TO MESSAGE
         LA    R9,35(,R9)               SET MESSAGE LENGTH
         STH   R9,MSGOK1                *
         MVC   MSGOK1+13(8),VERB
         MSG   MSGOK1
SP018    L     R11,DRAINROT             FIND ROUTE FOR DRAINING
         BR    R11
         EJECT
SP101    CLOSE (PARMS)
SP102    LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN R,LV=72,A=(1)
         RETURN (14,12),RC=0
         EJECT
*
*
*   *****  *****  *****  ***  *****  *   *
*   *   *  *        *     *   *   *  **  *
*   *****  *        *     *   *   *  * * *
*   *   *  *        *     *   *   *  *  **
*   *   *  *****    *    ***  *****  *   *
*
*   ****   *****  *   *  *****  ***  *   *  *****  *****
*   *   *  *   *  *   *    *     *   **  *  *      *
*   ****   *   *  *   *    *     *   * * *  ***    *****
*   *   *  *   *  *   *    *     *   *  **  *          *
*   *   *  *****   ***     *    ***  *   *  *****  *****
*
         EJECT
*
*   **         ****   *****  *****  *****  *****
*    *         *   *  *      *   *  *        *
*    *         *   *  ***    *****  *        *
*    *         *   *  *      *   *  *        *
*   ***  **    ****   *****  *   *  *****    *
*
*
*        DEACTIVATES DEVICES I.E. RENDERS THEM UNFIT FOR ALLOCATION,
*        BY MARKING THE UCBS 'ONLINE,ALLOCATED,PERMANENTLY RESIDENT'.
*        THE PRACTICAL EFFECT IS THAT NON-EXISTENT DEVICES ARE NEVER
*        CONSIDERED FOR ALLOCATION, AND NEVER NAMED IN ALLOCATION
*        RECOVERY MESSAGES.
*
*
         SPACE 5
         USING UCB,R4
SP501    MVC   DRAINROT,=A(SP503)
         TM    SRTESTAT,ONLINE          IS THIS DEVICE ONLINE
         BO    SP502                    YES - CANNOT BE DEACTIVATED
         MVI   SRTESTAT,ONLINE+ALOCATED+PERMRES   SET UCB FLAGS
         L     R8,REPSTART
         MVC   0(3,R8),UCBNAME
         LA    R8,4(,R8)
         ST    R8,REPSTART
         TM    UCBTBYT3,UCB3TAPE+UCB3DACC         D/A OT TAPE
         BZ    SP013                    NO - GO FOR NEXT DEVICE
*
*        IF D/A OR TAPE UNIT A DUMMY VOLSER IS INSERTED TO FACILITATE
*        INTERPRETATION OF DUMPS AND SUCH MESSAGES AS THOSE PRODUCED
*        BY DASTATUS AND PACKLOG.
*
         MVC   SRTEVOLI(3),=C'DUM'      SET UNIQUE SERIAL NUMBER
         MVC   SRTEVOLI+3(3),UCBNAME
         MVI   SRTESTAB,PRIVATE         SET UCB FLAG
         B     SP013                    GO FOR NEXT DEVICE
SP502    L     R8,REJREACT
         MVC   0(3,R8),UCBNAME
         LA    R8,4(,R8)
         ST    R8,REJREACT
         B     SP013
         SPACE 3
SP503    MVC   MSGERR2+13(8),VERB
         L     R9,=A(REACTRS)
         L     R8,REJREACT
         ST    R9,REJREACT
         SR    R8,R9
         BNP   SP002
         EX    R8,MSGMOVE3
         LA    R8,33(,R8)
         STH   R8,MSGERR2
         MSG   MSGERR2
         B     SP002
         EJECT
*
*   ***        *****  *****  *****  *****  *****
*     *        *   *  *      *   *  *        *
*    *         *****  ***    *****  *        *
*   *          *  *   *      *   *  *        *
*   ***  **    *   *  *****  *   *  *****    *
*
*
*        REACTIVATES DEVICES PREVIOUSLY MADE USELESS BY A DEACT
*        COMMAND.
*
*
         SPACE 5
         USING UCB,R4
SP511    MVC   DRAINROT,=A(SP503)
         TM    SRTESTAT,ONLINE+ALOCATED+PERMRES   DONE BY DEACT
         BNO   SP502
         TM    UCBTBYT3,UCB3TAPE+UCB3DACC      D/A OR TAPE
         BZ    SP512                    NO
         CLC   SRTEVOLI(3),=C'DUM'      DONE BY DEACT
         BNE   SP502                    NO - BRANCH
         CLC   SRTEVOLI+3(3),UCBNAME    DONE BY DEACT
         BNE   SP502                    NO - BRANCH
         XC    SRTEVOLI,SRTEVOLI        CLEAR VOLSER
         NI    SRTESTAB,PRIVATE         MAKE PUBLIC
SP512    MVI   SRTESTAT,ONLINE          MARK UCB ONLINE ONLY
         L     R8,REPSTART
         MVC   0(3,R8),UCBNAME
         LA    R8,4(,R8)
         ST    R8,REPSTART
         B     SP013
         EJECT
MSGMOVE1 MVC   MSGERR1+35(0),0(R8)
MSGMOVE2 MVC   MSGOK1+36(0),0(R8)
MSGMOVE3 MVC   MSGERR2+34(0),0(R9)
         SPACE 3
REJREACT DC    A(REACTRS)
REACTRS  DC    20C'   ,',C'   '
VERB     DS    CL8
PARMS    DCB   DDNAME=DDRDR1R,DSORG=PS,EODAD=SP101,MACRF=GM
VERBCOMP CLC   0(0,R2),4(R4)
         VERBLIST VERB=DEACT,ROUTE=SP501
         VERBLIST VERB=REACT,ROUTE=SP511
REPLIST  DC    20CL4'   ,'
MSGERR1  WTO   '&MSG.02I VERB----  NO UCBS FOR    ,   ,   ,   ,   ,   ,,
               XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX  ', ,
               MF=L,MCSFLAG=HRDCPY
         DS    CL4
MSGERR2  WTO   '&MSG.04I VERB----  FAILED FOR    ,   ,   ,   ,   ,   , ,
               XX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX  ,
               ',MF=L,MCSFLAG=HRDCPY
         DS    CL4
MSGOK1   WTO   '&MSG.03I VERB----  COMPLETE FOR    ,   ,   ,   ,   ,   ,
               ,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,XXX,  ',
               ,MF=L,MCSFLAG=HRDCPY
         DS    CL4
UCBSAVE  DS    H
DRAINROT DS    A
CARDAREA DS    CL80
REPLISTA DS    0A
REPSTART DS    A
REPEND   DS    A
         LTORG
TOEF     EQU   *-X'C0'
         DC    X'00FAFBFCFDFEFF'
         ORG   TOEF+C'0'
         DC    C'0123456789',X'FAFBFCFDFEFF'
FROMEF   EQU   *-C'A'
         DC    C'ABCDEF'
         ORG   FROMEF+C'0'
         DC    C'0123456789ABCDEF'
ZEROPROT EQU   X'F0'
ONLINE   EQU   X'80'
ALOCATED EQU   X'08'
PERMRES  EQU   X'04'
PRIVATE  EQU   X'10'
E        EQU   8
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
CVTAD    EQU   16
CVTILK2  EQU   40
UCB      DSECT
         DS    3X
SRTESTAT DS    X
         DS    9X
UCBNAME  DS    CL3
UCBTYP   DS    XL4
         DS    8X
SRTEVOLI DS    CL6
SRTESTAB DS    X
UCBTBYT3 EQU   UCBTYP+2
UCB3TAPE EQU   128
UCB3DACC EQU   32
         END
