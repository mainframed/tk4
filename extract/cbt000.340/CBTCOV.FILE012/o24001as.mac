*          DATA SET IEWL       AT LEVEL 002 AS OF 11/08/82
       TITLE   'PROG. TO INVOKE LINKAGE-EDITOR'
***********************************************************************
*
*          THIS PROGRAM WAS IN EXISTENCE IN JUNE 1977
*
*          IT IS DESIGNED TO PUT TIME AND DATE, AND LIBRARY USED,
*          ON THE LAST PAGE OF OUTPUT FROM THE LINKAGE EDITOR.
*          THE LINKAGE EDITOR ITSELF IS NOT CHANGED.
*
*          THIS PROGRAM SHOULD BE STORED AS 'IEWL' IN A LINKLIST
*          LIBRARY, AND THE ALIAS IEWL REMOVED FROM THE LINKAGE EDITOR
*          ITSELF. THE LINKAGE EDITOR MUST ALWAYS BE INVOKED AS IEWL
*          TO GET THE BENEFIT OF THIS WRAPPING. SMP COMPLIES - CHECK
*          YOUR JCL PROCEDURES TO SEE THAT THEY DO.
*
*          THIS PROGRAM WORKS AS FOLLOWS:
*
*        ON ENTRY, CHECK REGISTER 1 - IF THE DDNAME 'SYSLMOD' IS
*                    OVERRIDDEN, CHANGE THE NAME IN THE DCB.
*        LINK TO HEWL
*        ON RETURN, STORE RETURN CODE
*        RDJFCB TO OBTAIN LOAD LIBRARY NAME.
*        IF LOAD LIBRARY IS NOT A TEMP DATASET, THEN
*              RDJFCB FOR SYSPRINT
*              CHANGE DISP TO MOD ON SYSPRINT
*              OPEN TYPE=J SYSPRINT
*              ISSUE TIME MACRO
*              EDIT TIME INTO OUTPUT FORMAT
*              CONVERT DATE TO FORM TUESDAY 31 DECEMBER 1968 (THIS IS
*                            BASE DATE)
*              PRINT COMPLETION CODE, TIME AND DATE
*              PRINT DSNAME
*              PRINT VOLUME SERIAL NUMBER
*        CLOSE SYSPRINT
*        RETURN
*
***********************************************************************
*        REGISTER USAGE
*
R0       EQU   0             SVCS, DATE / TIME MANIPULATION
R1       EQU   1             SVCS, DATE / TIME MANIPULATION
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12            POINTER WHEN BUILDING PRINT LINE
R13      EQU   13            BASE AND SAVE AREA REGISTER
R14      EQU   14            GENERAL WORK REGISTER
R15      EQU   15
*
***********************************************************************
         EJECT
         USING *,R15
         B     START
         DC    AL1(8)
         DC    C'MONDIEWL &SYSDATE &SYSTIME'
START    DS    0F
         STM   R14,R12,12(R13)    SAVE ALL REGISTERS
         ST    R13,*+12           FOR CHAINING
         BAL   R13,*+76           NEW SAVE AREA / BASE
         USING *,R13
         DC    18F'0'
         L     R15,4(0,R13)       HIGHER SAVE AREA
         XC    8(4,R13),8(R13)    LOWER SAVE AREA POINTER
         ST    R13,8(,R15)        THIS IS 'LOWER SAVEA AREA' IN HIGHER
         PRINT NOGEN
         TM    0(1),X'80'         ONLY OPTIONS PRESENT?
         BO    XCTL               YES BRANCH
         L     RPARM,4(0,1)       LOAD ADDRESS OF DDNAME LIST
* LOOK FOR MODIFIED SYSLMOD ENTRY
         CLI   1(RPARM),X'18'     TABLE MUST BE AT LEAST 24 BYTES
         BL    XCTL               NOT THERE
         NC    18(8,RPARM),18(RPARM)   ZERO ENTRY?
         BZ    XCTL               YES NOT THERE
         MVC   SYSLMOD+40(8),18(RPARM) MOVE DDNAME TO DCB
*
XCTL     LINK  EP=HEWL
*
EXIT     DS    0H
         ST    15,CODE
         CVD   15,DWORD
         RDJFCB (SYSLMOD)
         TM    JFCB+87,X'01'           IS IT TEMPORARY ?
         BO    TEMP
         RDJFCB (SYSPRINT)
         NI    JFCB+87,X'3F'    CANCEL DISP FOR SYSPRINT.
         OI    JFCB+87,X'80'    DISP=MOD FOR SYSPRINT.
         OPEN  (SYSPRINT,OUTPUT),TYPE=J
         TIME
         ST    R0,DATE
         OI    DATE+3,X'0F'
         ED    TIMED(10),DATE
         MVI   TIMED,C' '
         MVI   TIMED+9,C' '
         ST    R1,DATE
* ROUTINE TO DETERMINE DAY OF WEEK
* BASE IS TUESDAY 31 DEC 68
* IF SATURDAY = 0, TUESDAY = 3
         ZAP   DUB3,=P'0'
         MVO   DUB3+6(2),DATE+1(1)
         CVB   R1,DUB3                  CURRENT YEAR
         STC   R1,YEAR
         SH    R1,=H'69'                # YEARS SINCE 1968
         LA    R15,3(R1)                ADD TO BASE
         SR    R0,R0
         D     R0,=F'4'                 # LEAP YEARS
         AR    R15,R1                   ADD TO BASE
         SR    R14,R14                  REMAINDER = 31/12 OF PREVIOUS
         D     R14,=F'7'                     YEAR.
         ZAP   DUB3,DATE+2(2)
         CVB   R1,DUB3
         AR    R14,R1                   ADD CURRENT DAY NO.
         SRDL  R14,32
         D     R14,=F'7'                REMAINDER = DAY OF WEEK NO.
         MH    R14,LENDAY
         LA    R14,DAYTAB(R14)
         LA    R11,10(R14)     SAVE ADDR. OF COMMENTS FOR DAY
         SR    R1,R1
         IC    R1,0(R14)
         LA    R12,ANSWER(R1)        R12 = POSITION IN ANSWER FIELD
         BCTR  R1,0
         EX    R1,MOVEDAY         MVC   ANSWER(0),1(R14)
* CALCULATE MONTH AND DATE
         TM    YEAR,3
         BNZ   *+10
         MVC   FEB,=P'29'
         LA    R14,MTHTAB
MLOOP    CP    DATE+2(2),0(2,R14)
         BNH   DATOK
         SP    DATE+2(2),0(2,R14)
         SR    R1,R1
         IC    R1,2(R14)
         LA    R14,3(R14,R1)
         B     MLOOP
DATOK    MVC   0(4,R12),=X'40202020'
         ED    0(4,R12),DATE+2
         MVI   0(R12),C','
         LA    R12,5(R12)
         IC    R1,2(R14)
         BCTR  R1,0
         EX    R1,MOVEMTH         MVC   0(0,R12),3(R14)
         LA    R12,1(R1,R12)
         MVC   1(2,R12),=C'19'
         UNPK  3(3,R12),DATE+1(2)
         MVC   OUTLINE+66(46),LINE4
         UNPK  DWORD(2),DWORD+6(2)
         OI    DWORD+1,X'F0'
         MVC   LINE2+41(2),DWORD
         MVC   OUTLINE(44),LINE2
         PUT   SYSPRINT,OUTLINE        PRINT COMPLETION CODE.
         MVI   OUTLINE,C' '
         MVC   OUTLINE+1(132),OUTLINE
         MVC   OUTLINE(32),LINE1       ..NO.
         MVC   OUTLINE+32(44),JFCB
         PUT   SYSPRINT,OUTLINE        PRINT DSNAME.
         MVI   OUTLINE,C' '
         MVC   OUTLINE+1(132),OUTLINE
         MVC   OUTLINE(32),LINE3
         MVC   OUTLINE+32(6),JFCB+118
         PUT   SYSPRINT,OUTLINE        PRINT VOLUME SERIAL.
         CLOSE (SYSPRINT)
TEMP     EQU   *
         L     15,CODE
         L     13,4(13)
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
         TITLE 'CONSTANTS, WORK AREAS, ETC'
DWORD    DC    D'0'
LMODXIT  DS    0F
         DC    X'87'
         DC    AL3(JFCB)
SYSPRXIT DS    0F
         DC    X'87'
         DC    AL3(JFCB)
CODE     DC    F'0'
LINE1    DC    C'0 *** SYSLMOD DATA SET NAME WAS '
LINE2    DC    C'+**** LINKAGE EDITOR COMPLETION CODE WAS   .'
LINE3    DC    C'  ***         VOLUME SERIAL WAS '
LINE4    DC    C'***** '
TIMED    DC    X'F021204B20204B202020'
         DC    C'  '
ANSWER   DC    CL28' '
         DC    CL1' '
JFCB     DC    CL176' '
OUTLINE  DC    CL133' '
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=FA,LRECL=121,   *
               EXLST=SYSPRXIT,                                         *
               BLKSIZE=121
SYSLMOD  DCB   DDNAME=SYSLMOD,DSORG=PS,MACRF=GL,EXLST=LMODXIT
MOVEDAY  MVC   ANSWER(0),1(R14)
MOVEMTH  MVC   0(0,R12),3(R14)
DUB3     DS    D
DATE     DS    F
YEAR     DS    C
LENDAY   DC    H'10'
* LAYOUT OF DAYTAB = 1 BYTE LENGTH OF DAY
*                    9 BYTES DAY
DAYTAB   DC    X'8',C'SATURDAY '
         DC    X'6',C'SUNDAY   '
         DC    X'6',C'MONDAY   '
         DC    X'7',C'TUESDAY  '
         DC    X'9',C'WEDNESDAY'
         DC    X'8',C'THURSDAY '
         DC    X'6',C'FRIDAY   '
MTHTAB   DC    P'31',X'7',C'JANUARY'
FEB      DC    P'28',X'8',C'FEBRUARY'
         DC    P'31',X'5',C'MARCH'
         DC    P'30',X'5',C'APRIL'
         DC    P'31',X'3',C'MAY'
         DC    P'30',X'4',C'JUNE'
         DC    P'31',X'4',C'JULY'
         DC    P'31',X'6',C'AUGUST'
         DC    P'30',X'9',C'SEPTEMBER'
         DC    P'31',X'7',C'OCTOBER'
         DC    P'30',X'8',C'NOVEMBER'
         DC    P'31',X'8',C'DECEMBER'
         LTORG
RPARM    EQU   5
         END
