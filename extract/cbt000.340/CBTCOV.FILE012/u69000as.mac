*      PROGM    SYSRESV
*      FUNCTION    ENABLE PROGMS THAT UPDATE FILES SIMULTANEOUSLY
*                  TO REN PARALEL WITH PROTECTION OF THE FILE
*                  TO RUN PARALEL WITH PROTECTION OF THE FILE
*
*
*            A RESERVE IS ISSUED ACROSS SYSTEMS B/4 PASSING CONTROL
*            TO THE PROGRAM UPDATING  ON RETURN A DEQUE ACROSS
*            SYSTEMS IS ISSUED
*    INPUT TO SYSRESV    PARM FILD FIRST PART IS UPDATE PROGM
*                        NAME THEN ANY PRAMATERS THEN PROGRAM
*                        NEEDS TO BE PASSED TO EXECUTE
*                        SYSUT2  DD CARD REFERS TO FILE BEING UPDATED
* SYSRESV ANBEGIN
SYSRESV  CSECT
         B     28(0,15)
         DC    AL1(22)
         DC    CL8'SYSRESV-'
         DC    CL8'11/15/77'
         DC    CL6'-12.23'
         STM   14,12,12(13)
         LR    14,13
         USING SYSRESV,15
         LA    13,SA0001
         DROP  15
         USING SA0001,13
         ST    13,8(14)
         ST    14,4(13)
         B     SA0001+72
SA0001   DS    18F
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
****  ANALYSE PARM STRING
         L     R6,0(R1)                 SAVE PTR TO PARMS
         CALL  SYSPARM
         OC    1(3,R1),1(R1)            Q/ANY PARMS
         BZ    NOPARM                   NO/ERROR
*              GET PROGRAM NAME
         CLI   0(R1),255                Q/ANY PGM SPECIFIED
         BE    EROR1                    NO/EEROR
         CLI   0(R1),7                  Q/PGM NM LEN GT 8
         BH    EROR2                    YES/ERROR
         XR    R3,R3
         L     R2,0(R1)                 A(PGM NM)
         IC    R3,0(R1)                 L'PGM NM-1
         EX    R3,EXMVCPGM
****
         SPACE 3
*        GET ANY ALTERNATE DDNAME
         CLI   4(R1),255                Q/ANY DD NM SUPPLIED
         BE    JPA1                     NO/ASSUME SYSUT2
         CLI   4(R1),7                  Q/DDNAME LEN GT 8
         BH    EROR4                    YES/ERROR
         L     R2,4(R1)                 A(DDNAME
         IC    R3,4(R1)                 L'DDNAME-1
         EX    R3,EXMVCDDN
         MVC   SYSUT2+40(8),DDN         CHANGE FROM SYSUT2 TO NEW 1
JPA1     EQU   *
         SPACE 2
*        SEE IF NON-SOURCE LIB UPDATE ALLOWED
         CLI   8(R1),255                Q/ANY 3RD PARM SUPPLED
         BE    JPB                      NO/JUMP
         L     R3,8(R1)                 A(PARM3)
         CLI   0(R3),C'N'               Q/ALLOW NON SOURCE-LIB UPDATE
         BNE   JPB                      NO/DON'T SET FLAG
         OI    SW,NOCHKDCB
JPB      EQU   *
         OC    13(3,R1),13(R1)          Q/ANY PARMS TO BE PASSED
         BZ    JPC
         L     R2,12(R1)                A(PARM4)
         LA    R2,0(R2)
         LH    R7,0(R6)                 L'OS PARMS
         LA    R8,2(R6,R7)              A(EOPARMS+1)
         CR    R8,R2                    Q/ANY PARMS TO BE PASSED
         BE    JPC                      NO/JUMP
         SR    R8,R2                    GET LENGTH OF RESR OF PARMS
         STH   R8,L                     STORE IN PARM AREA
         BCTR  R8,0
         EX    R8,EXMVCRST              MOVE REST OF PARMS TO 'PARMS'
         SPACE 2
JPC      EQU   *
*        UNLESS SPECIFICALLY REQUESTD, ENSURE THET DSCB LRECL = 80.
         TM    SW,NOCHKDCB              Q/NON SOURCE LIBRARY
         BO    JPD                      YES/BR
         OPEN  SYSUT2
         CLC   =Y(80),SYSUT2+82         Q/DCBLRECL = 80
         BNE   EROR5                    NO/ERROR
         CLOSE SYSUT2
JPD      EQU   *
         CLC   DDN(8),=CL8' '    DDNAME PROVIDED
         BNE   SYSLD     YES
         LA    R7,SYSUT2   ADDRES DCB
         B     SYS     GET DSNAME
SYSLD    LA    R7,SYSUT2      ADRES DCB
         MVC   40(8,R7),DDN    PLAC DDNAM PROVIDED
         EJECT
*      ISSUE SYSDSN TO GET DSNAME OF DSET TU BE UPDATED
SYS      SYSDSN (R7),DSN
         CLC   DSN(44),=CL44' '    DSN FOUNED
         BE    EROR3      NO SO EROR
         B     UCBSRCH
         EJECT
UCBSRCH  EQU   *
* FTIOT R1
         L     R1,16
         L     R1,0(,R1)
         L     R1,4(,R1)
         L     R1,12(,R1)
         L     R1,24(,R1)
TIOTSR   CLC   0(4,R1),=4X'0'      END OF TIOT
         BE    EROR3     YES SO EROR
         CLC   4(6,R1),=C'SYSUT2'    UCB FOR SYSUT2
         BE    UCBFND       YES
         LH    R9,SIV     SIZ OF DDN
         EX    R9,CDDN         CLC DDNM
         BE    UCBFND
         LA    R15,0      CLEAR
         IC    R15,0(R1)      LENTH OF ENTRY
         LA    R1,0(R15,R1)     UP TO NEXT
         B     TIOTSR       LOOK AGAIN
UCBFND   L     R2,16(R1)        UCB ADRES
         ST    R2,SAV     SAV ADRES UCB FOR RESERVS
         SPACE 3
*
         RESERVE (NAML,DSN,E,,SYSTEMS),UCB=SAV
         LOAD  EPLOC=NAM
         LA    R1,AL                    A(NEW PARMS)
         LR    R15,0
*
         BALR  R14,R15
         ST    R15,SAV      SAV R15 ON RETURN FROM PROGRM
         EJECT
*       ISSUE DEQ      RESTORE R15  AND EXIT VIA SVC 3
END      DEQ   (NAML,DSN,,SYSTEMS)
         L     R15,SAV
GOBACK   EQU   *
         L     R13,4(R13)
         RETURN  (14,12),RC=(15)
MVDD     MVC   DDN(0),0(R5)
MVPAR    MVC   2(0,R1),0(R5)
MVNAM    MVC   NAM(0),2(R1)
CDDN     CLC   4(0,R1),DDN
         EJECT
NOPARM   EQU   *
         WTO   ' NO PARM FIELD PROVIDED '
         B     ENDO
EROR3    EQU   *
         WTO   ' SYSUT2 DD STATEMENT MISSING '
         B     ENDO
EROR1    EQU   *
         WTO   ' NO PROGRAM NAME PROVIDED IN PARM '
         B     ENDO
EROR2    EQU   *
         WTO   ' PROGRAM NAME INVALID '
         B     ENDO
EROR4    EQU   *
         WTO   'DDNAME DOES NOT CONFORM TO STANDARD'
EROR5    EQU   *
         WTO   ' INVALID UPDATE ATTEMPT TO SOURCE LIBRARY'
         CLOSE SYSUT2
         B     ENDO
*
ENDO     LA    R15,20
         B     GOBACK
         LTORG
TRTB     DC    64X'00'
         DC    X'40'
         DC    191X'00'
NAM      DC    CL8' '
NAML     DC    CL8'SYSIEWLP'
FUL      DS    F
DDN      DC    CL8' '
DSN      DC    CL44' '
POG      DC    X'00'
SAV      DS    F
SIV      DS    H
HAL      DC    PL1'0'
SWT      DC    X'00'
SW       DC    X'00'
NOCHKDCB EQU   128
EXMVCPGM MVC   NAM(0),0(R2)
EXMVCDDN MVC   DDN(0),0(R2)
EXMVCRST MVC   PARMS(0),0(R2)
AL       DC    0F'0',X'80',AL3(L)
         DC    Y(0)
L        DC    Y(0)
PARMS    DC    CL100' '
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=GM,DSORG=PS
*
*
*     TEST PLAN  SF318          MOD LEV 01
*        PUT 3RD PARM IN STRING TO ALLOW OVERWRITING
*           OF NON-SOURCE LIBRARIES, THIS PARM MUST BE 'N'
*              TO  OVERWRITE THESE LIBRARIES. IT CAN ALSO BE 'Y'
*                  TO PREVENT OVERWRITING NON-SOURCE LIBS.
*              THE REASON FOR THIS MOD IS BECAUSE IEBUPDTE HAS
*              DCBLRECL = 80 WHICH OVERWRITES DSCB LRECL WHICH CAN
*              BE DISASTROUS
*
*
**** N.B.      NO MOD NUMBERS ON CARDS AS ORIGINAL SEQ NOT MULTIPLE OF
**    100
*
*
*
* VERSION 02
*        MOD SF445
*  A.NORDEN --  JULY 1977
*
*   CHANGE QNAME FROM 'UPDATE' TO 'SYSIEWLP' TO CORRESPOND TO LKED.
*
*  ALSO CHANGE SVC 3 TO L R13,4(R13) + RETURN MACRO TO ENABLE
*   INVOCATION OF SYSRESV BY LINK OR LOAD
*
*
         END
