 /* LIST SORTED BASIC DICTIONARY OF VS1 OPTIONAL MATERIAL    */
 LISTDIC: PROC OPTIONS(MAIN);
 DCL (SYSUT1 INPUT, PRINT OUTPUT
       ENV(F RECSIZE(121) CTLASA))
       FILE SEQL RECORD;                /* PRINTING 6 PER LINE        */
 DCL 1 DIC BASED(P),                    /* SORTED I/P RECORD          */
       2 NAME CHAR(9),                  /* INC LEADING SPACE          */
       2 VSN CHAR(7),                   /* DITTO                      */
       2 LAB CHAR(4);                   /* LABEL                      */
 DCL 1 HOF STATIC,                      /* PRINTER HEADING            */
       2 ASA CHAR(1) INIT('1'),
       2 X(6) CHAR(20) INIT(
   (6) (1)' MODULE VOLUME FILE ');
 DCL 1 PLINE STATIC,                    /* LINE OF 6 ENTRIES          */
       2 ASA CHAR(1) INIT('0'),         /* DOUBLE SPACED              */
       2 D(6) LIKE DIC;                 /* DICT ENTRY                 */
 DCL (I,J) FIXED BIN(15);               /* GENERAL COUNTS             */
0    ON ENDFILE(SYSUT1) GOTO Z;
0A:  WRITE FILE(PRINT) FROM(HOF);       /* PAGE HDR                   */
     DO I = 1 TO 30;                    /* 60 LINES PER PAGE          */
       DO J = 1 TO 6;                   /* 6 COLS PER LINE            */
         READ FILE(SYSUT1) SET(P);
         D(J) = DIC;                    /* STORE ITEM                 */
         END;
       WRITE FILE(PRINT) FROM(PLINE);
       END;                             /* OF PAGE LOOP               */
     GOTO A;                            /* TO PRINT NEXT PAGE         */
0Z:  IF J < 7 THEN DO;                  /* TEST IF PART FILLED LINE   */
       DO J = J TO 6;
         STRING(D(J))=(20)' ';          /* CLEAR REST OF LINE         */
         END;
       WRITE FILE(PRINT) FROM(PLINE);   /* LAST LINE                  */
       END;
     CLOSE FILE(PRINT),FILE(SYSUT1);
     END LISTDIC;
