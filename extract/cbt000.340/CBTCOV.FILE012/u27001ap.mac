 SBASA:
          PROC OPTIONS(MAIN);
 /*********************************************************************/
 /*                                                                   */
 /*                                                                   */
 /*                                                                   */
 /*    JCL REQUIRED TO EXECUTE PROGRAM IS AS FOLLOWS:                 */
 /*                                                                   */
 /*  //STEP     EXEC PGM=SAMPLE                                       */
 /*  //SYSPRINT   DD SYSOUT=A                                         */
 /*  //OUFILE     DD SYSOUT=A,DCB=(BLKSIZE=133,RECFM=FA)              */
 /*  //INFILE     DD *                                                */
 /*      HERE FOLLOW CARDS TO BE PRINTED                              */
 /*  /*                                                               */
 /*                                                                   */
 /*    THIS JCL APPLIES WHERE THE LINKAGE-EDITED PROGRAM HAS BEEN     */
 /*    PLACED IN SYS1.LINKLIB WITH MEMBER NAME "SAMPLE".              */
 /*                                                                   */
 /*                                                                   */
 /*                                                                   */
 /*    COMPILER :  PL/1 F LEVEL                                       */
 /*                                                                   */
 /*    FUNCTION :  CREATES LINES OF PRINT FROM INPUT DATA ON CARDS.   */
 /*                PRINTOUT CAN BE USED TO SHOW USERS WHAT PRODUCTION */
 /*                PRINTOUT WILL LOOK LIKE.                           */
 /*                                                                   */
 /*    INPUT:      NORMALLY INPUT CARDS ARE IN PAIRS:                 */
 /*                  1 TYPE '1' CARD                                  */
 /*                  1 TYPE '2' CARD                                  */
 /*                                                                   */
 /*                THESE CARDS HAVE THE FOLLOWING FORMATS:            */
 /*                                                                   */
 /*                CARD TYPE '1'                                      */
 /*                COL 1      ASA CONTROL CHARACTER                   */
 /*                COL 2 -72  DATA FOR PRINTING                       */
 /*                COL 72     '1'                                     */
 /*                COL 73-80  IGNORED                                 */
 /*                                                                   */
 /*                CARD TYPE '2'                                      */
 /*                COL 1 -62  DATA FOR PRINTING                       */
 /*                COL 63-71  IGNORED                                 */
 /*                COL 72     '2'                                     */
 /*                COL 73-78  IGNORED                                 */
 /*                                                                   */
 /*                DATA FROM CARD TYPE '1' IS PRINTED                 */
 /*                                        IN PRINT POSITIONS  1-70   */
 /*                DATA FROM CARD TYPE '2' IS PRINTED                 */
 /*                                        IN PRINT POSITIONS  71-132 */
 /*                                                                   */
 /*     DEFAULTS:  A SINGLE TYPE '1' CARD HAS PRINT POSITIONS 71-132  */
 /*                BLANKED OUT                                        */
 /*                                                                   */
 /*                A SINGE  TYPE '2' CARD TAKES PRINT POSITIONS       */
 /*                1-70 AND THE ASA CONTROL CHARACTER FROM THE LAST   */
 /*                TYPE '1' CARD PROCESSED.(IF NO TYPE '1' CARD HAS   */
 /*                BEEN READ COLS 1-70 ARE BLANK AND ASA CHAR IS ALSO */
 /*                A BLANK.)                                          */
 /*                                                                   */
 /*                THE FIRST LINE PRINTED WILL START ON A NEW PAGE    */
 /*                                                                   */
 /*                                                                   */
 /*    SUBMITTED BY:                                                  */
 /*                SYSTEMS PROGRAMMING                                */
 /*                COMPUTER UNIT                                      */
 /*                DEPT. OF POSTS & TELEGRAPHS                        */
 /*                DUNDRUM                                            */
 /*                DUBLIN 14                                          */
 /*                IRELAND                                            */
 /*    CONTACT:    D.R.HINDS   PHONE DUBLIN   983577                  */
 /*                                                                   */
 /*                                                                   */
 /*********************************************************************/
     /****                                                         ****/
     /*                                                               */
     /*   THIS PROGRAM CREATES LINES OF PRINT ON AN OUTPUT DATA SET,  */
     /*   USING PUNCHED CARDS AS INPUT. THE FACILITY IS PROVIDED TO   */
     /*   SPECIFY ASA CARRIAGE CONTROL CHARS IN THE OUTPUT LINES      */
     /*   PROGRAMMER     P. LOUGHRAN.   DATE:   JUNE 1972             */
     /*                                                               */
     /****                                                         ****/
     /*                                                               */
     /*   DEFINE FILES:-  INFILE:- INPUT FILE OF PUNCHED CARDS.       */
     /*                   OUFILE:- OUTPUT PRINT FILE.                 */
     /*                                                               */
     /****                                                         ****/
 DCL INFILE FILE RECORD INPUT;
 DCL OUFILE FILE RECORD OUTPUT;
     /****                                                         ****/
     /*                                                               */
     /*   DEFINE INPUT AND OUTPUT WORK AREAS                          */
     /*                                                               */
     /****                                                         ****/
 DCL 1 IN STATIC,             /* CARD INPUT STRUCTURE                 */
       2 CARRS CHAR(71),
       2 CCODE CHAR(1),
       2 CBLK  CHAR(8);
 DCL CLNEF CHAR(133) STATIC INIT('1');
 DCL 1 CLNEZ STATIC,
       2 CASA CHAR(11) INIT('-'),
       2 CEND CHAR(25) INIT('END OF DATA SET.'),
       2 ZREAD PIC'ZZZZ9',
       2 CCRDS CHAR(15) INIT(' CARDS READ.'),
       2 ZPRTD PIC'ZZZZ9',
       2 CLINES CHAR(19) INIT(' LINES PRINTED.'),
       2 ZUNREC PIC'ZZZZ9',
       2 CUNREC CHAR(48) INIT(' UNRECOGNISED CARD TYPES ASSUMED 1');
 DCL 1 CLNE STATIC,
       2 CASA1 CHAR(1) INIT(' '),
       2 CCD1  CHAR(70) INIT(' '),
       2 CCD2  CHAR(62) INIT(' ');
     /****                                                         ****/
     /*                                                               */
     /*   DECLARE BIT STRING VARIABLES                                */
     /*                                                               */
     /****                                                         ****/
 DCL B1   BIT(1) STATIC INIT('0'B);     /* TYPE 1 OR UNREC CARD READ  */
 DCL B2   BIT(1) STATIC INIT('0'B);     /* TYPE 1 OR UNREC CARD READ  */
     /****                                                         ****/
     /*                                                               */
     /*   DEFINE BINARY VARIABLES                                     */
     /*                                                               */
     /****                                                         ****/
 DCL ( NREAD,            /*  NO CARDS READ,                           */
       NPRTD,            /*  NO LINES PRINTED.                        */
       NUNREC            /*  NO UNRECOGNISED CODES.                   */
             ) BIN FIXED(15) STATIC INIT(0B);
     /****                                                         ****/
     /*                                                               */
     /*   DEFINE CHARACTER STRING VARIABLES                           */
     /*                                                               */
     /****                                                         ****/
 DCL CREC CHAR(5) DEF ZUNREC;
 DCL CASA2 CHAR(1) DEF CARRS;
 DCL CAR2  CHAR(70) DEF CARRS POS(2);
 DCL CAR3  CHAR(62) DEF CARRS;
          ON ENDFILE(INFILE) GO TO L1000;
          OPEN FILE(INFILE);
          OPEN FILE(OUFILE);
     /*   READ A RECORD                                               */
          READ FILE(INFILE) INTO(IN);
          NREAD = NREAD + 1B;      /* INCR  CARDS READ                */
          IF CCODE ^= '1'          /* CODE NOT EQUAL TO 1.?           */
          THEN IF CCODE ^= '2'     /* CODE NOT EQUAL TO 2.?           */
               THEN DO;            /* UNRECOGNISED CARD CODE          */
                      NUNREC = NUNREC + 1B;  /* INCR UNREC. CARDS     */
                      GO TO L0010;
                    END;
               ELSE DO;            /* TYPE 2. 1ST HALF REC = BLANK   */
                      CASA1 = '1';      /* 1ST. RECORD. SET ASA = 1   */
                      CCD2  = CAR3;     /* ASSIGN 2ND HALF OF PRINT   */
                      GO TO L0040;
                    END;
 L0010:   B2 = '1'B;                    /* CARD OF TYPE 1 OR UNREC RED*/
          CASA1 = '1';                  /*1ST. RECORD.  SET ASA = 1   */
 L0020:
          CCD1 = CAR2;
          READ FILE(INFILE) INTO(IN);
          NREAD = NREAD + 1B;
          B1 = '0'B;          /* RESET'TYPE 1 OR UNREC CARD READH FLAG*/
          IF CCODE ^= '2'     /* NOT EQUAL TO EXPECTED 2.             */
          THEN DO;
                 CCD2 = ' ' ; /* BLANK OUT 2ND HALF OF REC.          */
                 B1 = '1'B;   /* SET 'TYPE1 OR UNREC CARD READ.       */
                 IF CCODE ^='1'      /* NOT TYPE 1?                   */
                 THEN NUNREC = NUNREC + 1B;  /* INCR UNREC CARDS.     */
                 GO TO L0040;
               END;
 L0030:
          IF B2 = '0'B        /* NO RECORDS OF TYPE1 OR UNREC YET     */
          THEN CASA1 = ' ';   /* THEN SET ASA TO BLANK                */
          CCD2 = CAR3;
 L0040:
          WRITE FILE(OUFILE) FROM(CLNE);
          NPRTD = NPRTD + 1B;
          IF B1               /* TYPE1 OR UNREC STILL TO BE PRTD'ON?  */
          THEN DO;
                 CASA1 = CASA2;
                 GO TO L0020;
               END;
          READ FILE(INFILE) INTO(IN);
          NREAD = NREAD + 1B;
          IF CCODE = '1'
          THEN;
          ELSE IF CCODE = '2'
               THEN GO TO L0030;
               ELSE NUNREC = NUNREC + 1B;
          CASA1 = CASA2;
          B1 = '1'B;
          B2 = '1'B;     /* SET 'TYPE1 OR UNREC CARD READ'FLAG.       */
          GO TO L0020;
 L1000:
     /****                                                         ****/
     /*                                                               */
     /*   THE END OF CARD INPUT HAS BEEN REACHED. PRINT BALANCING PAGE*/
     /*                                                               */
     /****                                                         ****/
          IF B1
          THEN DO;
                 CASA1 = CASA2;
                 CCD1 = CAR2;
          CCD2 = ' ';
                 WRITE FILE(OUFILE) FROM(CLNE);
                 NPRTD = NPRTD + 1B;
               END;
          ZREAD = NREAD;
          ZPRTD = NPRTD;
          IF NUNREC = 0B
          THEN DO;
                 CREC = ' ';
                 CUNREC = ' ';
               END;
          ELSE ZUNREC = NUNREC;
          WRITE FILE(OUFILE) FROM(CLNEF);
          WRITE FILE(OUFILE) FROM(CLNEZ);
          CLOSE FILE(INFILE);
          CLOSE FILE(OUFILE);
          END SBASA;
