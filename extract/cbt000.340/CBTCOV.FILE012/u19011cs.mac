*
         TITLE 'XM210210  VALIDATE COMPARE CONTROL CARDS              '
XM210210 CSECT
         ENTRY XM210200
XM210200 EQU   *
         PRINT NOGEN
         COPY  REGEQU
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT2     EQU   X'20'
BIT3     EQU   X'10'
BIT4     EQU   X'08'
BIT5     EQU   X'04'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
BIT8     EQU   X'80'
BIT9     EQU   X'40'
BIT10    EQU   X'20'
BIT11    EQU   X'10'
BIT12    EQU   X'08'
BIT13    EQU   X'04'
BIT14    EQU   X'02'
BIT15    EQU   X'01'
         EJECT
***********************************************************************
*                                                                     *
*    MODULE XM210210  PROGRAMMER R.A.WHITELEY       ASSIGNED 25/04/74 *
*                                                                     *
*                           MODULE FUNCTIONS                          *
*                           ----------------                          *
*  MODULE VALIDATES CONTROL CARD INPUT WHICH CONSISTS OF THE SIX      *
*   FOLLOWING TYPES OF CONTROL CARD                                   *
*        A) FORMAT  (OR F)                                            *
*        B) KEY     (OR K)                                            *
*        C) COMPARE (OR C)                                            *
*        D) PRINT   (OR P)                                            *
*        E) TITLE   (OR T)                                            *
*        F) EXIT    (OR E)                                            *
*   THE 5 OUTPUT PARAMETERS ARE SET UP FROM THE DETAIL ON THESE       *
*   CARDS. A CARD VALIDATION REPORT IS PRODUCED  (XM21/P03)           *
*                                                                     *
         SPACE 2
*                             PARAMETERS                              *
*                             ----------                              *
*  1. INPUT RECORD FORMAT TABLE         - 11 BYTES        I/O         *
*        1ST BYTE IS FORMAT INDICATOR                                 *
*        C'S' = IBM SEQUENTIAL RECORD                                 *
*        C'H' = HBS FORMAT, IBM UNDEFINED LENGTH RECORDS BLOCKED      *
*                INTO HBS RECORDS WITH 1ST 2 BYTES AS LENGTH          *
*        C'I' = IBM INDEX SEQUENTIAL RECORDS                          *
*        NEXT 5 BYTES = NO.OF RECORDS (DSET1) TO CHECKPOINT           *
*        NEXT 5 BYTES = NO.OF RECORDS BETWEEN CHECKPOINTS             *
*                                                                     *
*  2. KEY FIELD TABLE                   - 11 X 6 BYTES    I/O         *
*        EACH ENTRY CONSISTS OF                                       *
*         1 BYTE FORMAT IND.  B=BINARY, P=PACKED.                     *
*         1 BYTE ASCENDING/DESCENDING ORDER OF KEYS, A OR D           *
*         2 BYTE BINARY DISPLACEMENT OF KEY WITHIN RECORD             *
*         2 BYTE BINARY LENGTH OF KEY MINUS ONE                       *
*           X'FF' DENOTES LAST ENTRY IN TABLE.                        *
*                                                                     *
*  3. COMPARE FIELD TABLE               - 11 X 6 BYTES    I/O         *
*        EACH ENTRY CONSISTS OF                                       *
*         1 BYTE FORMAT IND.  B=BINARY, P=PACKED, M=BITS, R=REST OF   *
*         1 BYTE MASK IF FORMAT IS M                        RECORD    *
*         2 BYTE BINARY DISPLACEMENT OF FIELD WITHIN RECORD           *
*         2 BYTE BINARY LENGTH OF FIELD MINUS ONE                     *
*           X'FF' DENOTES LAST ENTRY IN TABLE.                        *
*                                                                     *
*  4. PRINT INFORMATION TABLE           - 203 BYTES       I/O         *
*        (SEE DSECT DS1)                                              *
*                                                                     *
*  5. ERROR INDICATOR                   - 1 BYTE          I/O         *
*            C'V' = NO ERRORS,  C'7' = ERRORS                         *
*                                                                     *
*  6. EXIT ROUTINES                     - 16 BYTES        I/O         *
*         8 BYTE EXIT RTN NAME PRIOR TO COMPARE                       *
*         8 BYTE EXIT RTN NAME AFTER UNEQUAL COMPARE                  *
*                                                                     *
*                           SPECIAL NOTES                             *
*                           -------------                             *
*  USE D0701 TO PRODUCE CARD REPORT - EACH CARD SHOULD BE PRINTED     *
*  WHETHER VALID OR NOT.                                              *
*                                                                     *
*  REGISTER USAGE IS AS FOLLOWS                                       *
*        R0, R1, R2, R14, R15  IMMEDIATE.                             *
*        R13 BASE.                                                    *
*        R6 TO R10  PARAMETERS                                        *
*        R3   INDEX THRU' COMPARE KEY/TABLE P2 & P3                   *
*        R4   INDEX THRU' CARD ENTRY                                  *
*        R5   INDEX THRU' CARD                                        *
*        R11  SUBRTN LINKAGE REGISTER                                 *
*        R12  UNDERLINE INDEX REGISTER                                *
*                                                                     *
***********************************************************************
         EJECT
         USING DS1,R9
         ISEQ  73,80
         MODIN
***********************************************************************
*  INITIALISATION                                                     *
***********************************************************************
         LA    R12,CCWXXX          POINT AT MESSAGE AREA
         MVC   SFD004,20(R1)       SAVE P6 ADDRESS
         SPACE
***********************************************************************
*  READ A CONTROL CARD, VALIDATE CARD CODE & OPERATION                *
***********************************************************************
         SPACE
BA10     BAL   R11,B6001           READ A CONTROL CARD
*                                  Q. EOF
         BE    BA95                A. YES - FINISH REPORT
*                                  RESET ERROR INDICATOR
*
** CHECK CARD CODE & DELIMITER
*  ---------------------------
         CLC   SCI080(4),=C'XM21'  Q. VALID CARD CODE
         BE    BA13                A. YES - CONTINUE
         MVC   0(18,R12),=C'INVALID CARD CODE*'
         LA    R12,18(R12)         POINT AT AREA FOR NEXT MESSAGE
         MVC   CCW080(4),=75C'-'   UNDERLINE CARD CODE
         OI    SXW001B,BIT8        SET ERROR INDICATOR
         B     BA90                GO REPORT
BA13     CLI   SCI080+4,C' '       Q. VALID DELIMITER
         BE    BA15                A. YES - CONTINUE
         MVC   0(18,R12),=C'INVALID DELIMITER*'
         LA    R12,18(,R12)        POINT AT AREA FOR NEXT MSG.
         MVI   CCW080+4,C'-'       UNDERLINE DELIMITER
         OI    SXW001B,BIT9        SET ERROR INDICATOR
         B     BA90                GO REPORT
*
** CHECK OPERATION ON CONTROL CARD
*  -------------------------------
BA15     CLC   SCI080+5(8),=C'COMPARE='   Q. COMPARE CARD
         BE    BA50                       A. YES - PROCESS IT
         CLC   SCI080+5(4),=C'KEY='       Q. KEY CARD
         BE    BA30                       A. YES - PROCESS IT
         CLC   SCI080+5(6),=C'PRINT='     Q. PRINT CARD
         BE    BA70                       A. YES - PROCESS IT
         CLC   SCI080+5(6),=C'FORMAT='    Q. FORMAT CARD
         BE    BA20                       A. YES - PROCESS IT
         CLC   SCI080+5(6),=C'TITLE='     Q. TITLE CARD
         BE    BA80                       A. YES - PROCESS IT
         CLC   SCI080+5(5),=C'EXIT='      Q. EXIT CARD
         BE    BA86                       A. YES - PROCESS IT
         LA    R5,SCI080+7         POINT AT BYTE AFTER OPERATION
         CLC   SCI080+5(2),=C'C='  Q. COMPARE CARD
         BE    BA51                A. YES - PROCESS IT
         CLC   SCI080+5(2),=C'K='  Q. KEY CARD
         BE    BA31                A. YES - PROCESS IT
         CLC   SCI080+5(2),=C'P='  Q. PRINT CARD
         BE    BA71                A. YES - PROCESS IT
         CLC   SCI080+5(2),=C'F='  Q. FORMAT CARD
         BE    BA21                A. YES - PROCESS IT
         CLC   SCI080+5(2),=C'T='  Q. TITLE CARD
         BE    BA81                A. YES - PROCESS IT
         CLC   SCI080+5(2),=C'E='  Q. EXIT CARD
         BE    BA87                A. YES - PROCESS IT
         BAL   R11,B6301           INDICATE INVALID OPERATION
         B     BA90                GO REPORT
         EJECT
***********************************************************************
* ROUTINE TO PROCESS FORMAT CARD                                      *
***********************************************************************
         SPACE
*
** ENSURE FORMAT CARD CAN BE PROCESSED
*  -----------------------------------
BA20     LA    R5,SCI080+12        POINT R12 AT INPUT FORMAT FIELD
BA21     TM    SXW001A,BIT4+BIT5   Q. KEY OR COMPARE CONT.CARD EXPECTED
         BZ    BA22                A. NO - CONTINUE
         BAL   R11,B6401           INDICATE INVALID CONTINUATION CARD
BA22     TM    SXW001B,BIT9        Q. FORMAT CARD PROCESSED
         BNO   BA23                A. NO - CONTINUE
         BAL   R11,B6301           INDICATE INVALID OPERATION
         B     BA90                GO REPORT THIS CARD
BA23     OI    SXW001B,BIT9        INDICATE FORMAT CARD PROCESSED
*
** CHECK FORMAT
** (MAYBE IS, HBS OR IBM)
*  ----------------------
         CLI   0(R5),C'/'          Q. FORMAT PRESENT
         BE    BA2805              A. NO - CHECK NO.OF RECORDS TO CHKPT
         CLC   0(3,R5),=C'IBM'     Q. IBM RECORDS
         BE    BA27                A. YES - SET FORMAT IND.
         CLC   0(3,R5),=C'HBS'     Q. HBS BLOCKED RECORDS
         BE    BA26                A. YES - SET FORMAT IND.
         CLC   0(3,R5),=C'IS '     Q. INDEX SEQUENTIAL FILE
         BE    BA25                A. YES - SET FORMAT IND.
         BAL   R11,B6201           INDICATE INVALID ENTRY
         B     BA90                GO REPORT
BA25     MVI   0(R6),C'I'          SET IND.TO INDEX SEQUENTIAL RECORDS
         B     BA90                GO READ NEXT CARD
BA26     MVI   0(R6),C'H'          SET IND.TO HBS BLOCKING RECORDS
         B     BA28                GO CHECK
BA27     MVI   0(R6),C'S'          SET IND.TO IBM SEQUENTIAL RECORDS
*
** CHECK NO.OF RECORDS BEFORE CHECKPOINT
** (CAN TAKE ANY VALUE FROM 0 TO 999,999,999)
*  ------------------------------------------
BA28     LA    R5,3(,R5)           POINT OVER FORMAT
         CLI   0(R5),C' '          Q. CHECKPOINT ENTRY PRESENT
         BE    BA90                A. NO - GO REPORT THIS CARD
         CLI   0(R5),C'/'          Q. VALID DELIMITER
         BNE   BA29                A. NO - UNDERLINE THIS ENTRY
BA2805   LA    R5,1(,R5)           POINT R5 AT CARD ENTRY
         LR    R4,R5               POINT R4 AT CARD SUB-ENTRY
         LA    R3,SHD002           POINT R3 AT DUMMY AREA
         BAL   R11,B6101           EXTRACT NO.OF RECORDS BEFORE CHKPT.
         CR    R4,R5               Q. VALID EXTRACTION
         BE    BA29                A. NO - REPORT
         ZAP   1(5,R6),SDW008+3(5) INSERT NO.OF RECORDS TO CHECKPOINT
         ZAP   6(5,R6),1(5,R6)     INSERT NO.OF RECORDS BETWEEN CHKPTS.
         CLI   0(R4),C' '          Q. VALID DELIMITER
         BE    BA90                A. YES - GO REPORT
BA29     BAL   R11,B6201           INDICATE INVALID ENTRY
         B     BA90                GO REPORT
         EJECT
***********************************************************************
* ROUTINE TO PROCESS KEY CARD                                         *
***********************************************************************
         SPACE
*
** ENSURE KEY CARD CAN BE PROCESSED
*  --------------------------------
BA30     LA    R5,SCI080+9         POINT R12 AT 1ST ENTRY
BA31     TM    SXW001A,BIT5        Q. COMPARE CONT.CARD EXPECTED
         BNO   BA33                A. NO - CONTINUE
         BAL   R11,B6401           INDICATE INVALID CONTINUATION CARD
BA33     TM    SXW001B,BIT10       Q. KEY CARD PROCESSED
         BNO   BA35                A. NO - CONTINUE
         TM    SXW001A,BIT4        Q. CONTINUATION CARD ALLOWED
         BO    BA34                A. YES - CONTINUE
         BAL   R11,B6301           INDICATE INVALID OPERATION
         B     BA90                GO REPORT
BA34     NI    SXW001A,255-BIT4    SET OFF CONTINUATION CARD IND.
BA35     OI    SXW001B,BIT10       INDICATE KEY CARD PROCESSED
*
** CHECK KEY CARD - 'NONE' ENTRY
*  -----------------------------
         CLC   0(5,R5),=C'NONE '   Q. KEY CARD INDICATING NO KEYS
         BE    BA90                A. YES - IGNORE BUT GO REPORT
*
** CHECK KEY CARD - ENTRY PRESENT
*  ------------------------------
BA38     CLI   0(R5),C' '          Q. ENTRY PRESENT
         BNE   BA40                A. YES - CONTINUE
         OI    SXW001A,BIT4        INDICATE CONTINUATION CARD NEXT
         B     BA90                GO REPORT
BA40     SP    CPD002,=P'1'        Q. MORE THAN 10 ENTRIES PROCESSED
         BNM   BA42                A. NO - CONTINUE PROCESSING
         MVC   0(25,R12),=C'MORE THAN 10 KEY ENTRIES*'
         LA    R12,25(,R12)        POINT AT AREA FOR NEXT MESSAGE
         LA    R14,CCW080-SCI080+0(,R5)  INDICATE THE INVALID ENTRY
         MVI   0(R14),C'X'               - IN THE UNDERLINE
         OI    SXW001B,BIT15       SET ERR.IND.
         B     BA90                GO REPORT
*
** CHECK KEY CARD - DISPLACEMENT
*  -----------------------------
BA42     LA    R3,2(,R7)           POINT R3 AT AREA TO RECEIVE DISP.
         LR    R4,R5               POINT R4 AT THIS SUB ENTRY
         BAL   R11,B6101           EXTRACT DISPLACEMENT FROM CARD
         CR    R4,R5               Q. EXTRACTION OK
         BE    BA48                A. NO - ENTRY INVALID
         CP    SDW008,SXT005K      Q. DISPLACEMENT TOO LARGE
         BH    BA48                A. YES - ENTRY INVALID
         CLI   0(R4),C','          Q. VALID DELIMITER
         BNE   BA48                A. NO - ENTRY INVALID
         LA    R4,1(,R4)           POINT R4 TO NEXT FIELD
*
** CHECK KEY CARD - LENGTH
*  -----------------------
         LA    R3,4(,R7)           POINT R3 AT AREA TO RECEIVE LENGTH
         BAL   R11,B6101           EXTRACT LENGTH FROM CARD
         CR    R4,R5               Q. EXTRACTION OK
         BE    BA48                A. NO - ENTRY INVALID
         CP    SDW008,SXT005K      Q. LENGTH TOO LARGE
         BH    BA48                A. YES - ENTRY INVALID
         CLI   0(R4),C','          Q. VALID DELIMITER
         BNE   BA48                A. NO - ENTRY INVALID
         CLC   4(2,R7),=2X'00'     Q. ZERO LENGTH
         BE    BA48                A. YES - ENTRY INVALID
         LA    R4,1(,R4)           POINT TO NEXT FIELD
*
** CHECK KEY CARD - FORMAT
*  -----------------------
         CLC   0(2,R4),=C'B,'      Q. BINARY KEY FORMAT
         BE    BA46                A. YES - CONTINUE
         CLC   0(2,R4),=C'P,'      Q. PACKED KEY FORMAT
         BNE   BA46                A. NO  - ERROR
         CLC   4(2,R7),=H'16'      Q. LENGTH GREATER THAN 16
         BH    BA48                A. YES - ERROR
BA46     MVC   0(1,R7),0(R4)       INSERT FORMAT CHAR.TO KEY TABLE
         LA    R4,2(,R4)           POINT TO NEXT FIELD
*
** CHECK KEY CARD - KEY DIRECTION
*  ------------------------------
         CLI   0(R4),C'A'          Q. KEY DIRECTION ASCENDING
         BE    BA47                A. YES - CONTINUE
         CLI   0(R4),C'D'          Q. KEY DIRECTION DESCENDING
         BNE   BA48                A. NO - ENTRY INVALID
BA47     MVC   1(1,R7),0(R4)       INSERT KEY DIRECTION INTO KEY TABLE
         LA    R4,1(,R4)           POINT TO NEXT FIELD
*
** CHECK KEY CARD - ENTRY DELIMITER
*  --------------------------------
         CLI   0(R4),C' '          Q. LAST ENTRY ON CARD
         BNE   BA4705              A. NO - CHECK FURTHER
         LA    R7,6(,R7)           POINT TO NEXT ENTRY IN KEY TABLE
         B     BA90                GO REPORT CARD
BA4705   CLI   0(R4),C'/'          Q. ANOTHER ENTRY PRESENT
         BNE   BA48                A. NO  - ERROR
         LA    R7,6(,R7)           POINT TO NEXT ENTRY IN KEY TABLE
         LA    R5,1(,R4)           POINT R5 AT START OF NEXT ENTRY
         B     BA38                GO PROCESS THIS NEXT ENTRY
*
** INVALID KEY CARD ENTRY - UNDERLINE ALL THE ENTRY
*  ------------------------------------------------
BA48     BAL   R11,B6201           INDICATE INVALID CARD ENTRY
         CLM   R2,1,=C' '          Q. LAST ENTRY ON CARD
         BE    BA90                A. YES - GO REPORT
         B     BA38                GO PROCESS THIS NEXT ENTRY
         EJECT
***********************************************************************
* ROUTINE TO PROCESS COMPARE CARD                                     *
***********************************************************************
         SPACE
*
** ENSURE COMPARE CARD CAN BE PROCESSED
*  ------------------------------------
BA50     LA    R5,SCI080+13        POINT R12 AT 1ST ENTRY
BA51     TM    SXW001A,BIT4        Q. KEY CONT.CARD EXPECTED
         BNO   BA53                A. NO - CONTINUE
         BAL   R11,B6401           INDICATE INVALID CONTINUATION CARD
BA53     TM    SXW001B,BIT13       Q. COMPARE CARD PROCESSED
         BNO   BA55                A. NO  - CONTINUE
         TM    SXW001A,BIT5        Q. CONTINUATION CARD ALLOWED
         BO    BA54                A. YES - CONTINUE
         BAL   R11,B6301           INDICATE INVALID OPERATION
         B     BA90                GO REPORT
BA54     NI    SXW001A,255-BIT5    SET OFF CONTINUATION CARD IND.
BA55     OI    SXW001B,BIT13       INDICATE COMPARE CARD PROCESSED
*
** CHECK COMPARE CARD - 'ALL' ENTRY
*  --------------------------------
         CLC   0(4,R5),=C'ALL '    Q. COMPARE CARD FOR ALL RECORD
         BNE   BA58                A. NO - CONTINUE
         MVC   0(6,R8),=X'D90000000000' INSERT 'ALL' COMPARE VAL.TO TAB
         LA    R8,6(,R8)           POINT TO NEXT ENTRY IN TABLE
         B     BA90                GO REPORT CARD
*
** CHECK COMPARE CARD - ENTRY PRESENT
*  ----------------------------------
BA58     CLI   0(R5),C' '          Q. ENTRY PRESENT
         BNE   BA60                A. YES - CONTINUE
         OI    SXW001A,BIT5        INDICATE CONTINUATION CARD NEXT
         B     BA90                GO REPORT
BA60     SP    CPD002A,=P'1'       Q. MORE THAN 10 ENTRIES PROCESSED
         BNM   BA62                A. NO - CONTINUE PROCESSING
         MVC   0(29,R12),=C'MORE THAN 10 COMPARE ENTRIES*'
         LA    R12,29(R12)         POINT AT NEXT MESSAGE AREA
         LA    R14,CCW080-SCI080+0(,R5)  INDICATE THE INVALID ENTRY
         MVI   0(R14),C'X'               - IN THE UNDERLINE
         OI    SXW001B,BIT15       SET ERROR IND.
         B     BA90                GO REPORT
*
** CHECK COMPARE CARD - DISPLACEMENT
*  ---------------------------------
BA62     LA    R3,2(,R8)           POINT R3 AT AREA TO RECEIVE DISP.
         LR    R4,R5               POINT R4 AT THIS SUB ENTRY
         BAL   R11,B6101           EXTRACT DISPLACEMENT FROM CARD
         CR    R4,R5               Q. EXTRACTION OK
         BE    BA68                A. NO - ENTRY INVALID
         CP    SDW008,SXT005K      Q. DISPLACEMENT TOO LARGE
         BH    BA68                A. YES - ENTRY INVALID
         CLI   0(R4),C','          Q. VALID DELIMITER
         BNE   BA68                A. NO - ENTRY INVALID
         LA    R4,1(,R4)           POINT R4 TO NEXT FIELD
*
** CHECK COMPARE CARD - BIT COMPARE
*  --------------------------------
         CLC   2(2,R4),=C',M'      Q. BIT COMPARE
         BNE   BA64                A. NO - CONTINUE
         MVC   SHD002,0(R4)        INSERT MASK INTO W/A
         TR    SHD002,CXT256A      TRANSLATE THE BYTE MASK
*                                  - SO THAT C'Y' BECOMES X'0Y'
*                                  - OR X'FF' IF INVALID
         CLI   SHD002,X'0F'        Q. 1ST CHARACTER INVALID
         BH    BA68                A. YES - ERROR
         CLI   SHD002+1,X'0F'      Q. 2ND CHARACTER
         BH    BA68                A. YES - ERROR
         MVO   1(1,R8),SHD002(1)   MOVE 1ST HALF OF HEX BYTE
         MVN   1(1,R8),SHD002+1    MOVE 2ND HALF OF HEX BYTE
         MVI   0(R8),C'M'          INSERT BIT COMPARE INDICATOR
         MVC   4(2,R8),=H'1'       INSERT LENGTH OF 1 BYTE
         LA    R4,4(,R4)           POINT TO DELIMITER AT END OF ENTRY
         B     BA67                GO CHECK DELIMITER
*
** CHECK COMPARE CARD - FOR COMPARE ON REST OF RECORD
*  --------------------------------------------------
BA64     CLI   0(R4),C'R'          Q. COMPARE FOR REST OF RECORD
         BNE   BA65                A. NO - CONTINUE
         MVI   0(R8),C'R'          INSERT FORMAT IND.(R)
         LA    R4,1(,R4)     POINT R4 OVER FORMAT IND.
         B     BA67                GO CHECK DELIMITER
*
** CHECK COMPARE CARD - LENGTH
*  ---------------------------
BA65     LA    R3,4(,R8)           POINT R3 AT AREA TO RECEIVE LENGTH
         BAL   R11,B6101           EXTRACT LENGTH FROM CARD
         CR    R4,R5               Q. EXTRACTION OK
         BE    BA68                A. NO - ENTRY INVALID
         CLI   0(R4),C','          Q. VALID DELIMITER
         BNE   BA68                A. NO - ENTRY INVALID
         CP    SDW008,SXT005K      Q. LENGTH TOO LARGE
         BH    BA68                A. YES - ENTRY INVALID
         CLC   4(2,R8),=2X'00'     Q. ZERO LENGTH
         BE    BA68                A. YES - ENTRY INVALID
         LA    R4,1(,R4)           POINT TO NEXT FIELD
*
** CHECK COMPARE CARD - FORMAT
*  ---------------------------
         CLI   0(R4),C'B'          Q. BINARY COMPARE FORMAT
         BE    BA66                A. YES - CONTINUE
         CLI   0(R4),C'P'          Q. PACKED COMPARE FORMAT
         BNE   BA68                A. NO - ERROR
         CLC   4(2,R8),=H'16'      Q. LENGTH GREATER THAN 16
         BH    BA68                A. YES - ERROR
BA66     MVC   0(1,R8),0(R4)       INSERT FORMAT TO COMPARE TABLE
         LA    R4,1(,R4)           POINT R4 PAST FORMAT IND.
*
** CHECK COMPARE CARD - ENTRY DELIMITER
*  ------------------------------------
BA67     CLI   0(R4),C' '          Q. LAST ENTRY ON CARD
         BNE   BA6705              A. NO - CHECK FURTHER
         LA    R8,6(,R8)           POINT TO NEXT ENTRY IN COMPARE TABLE
         B     BA90                GO REPORT THE CARD
BA6705   CLI   0(R4),C'/'          Q. ANOTHER ENTRY PRESENT
         BNE   BA68                A. NO - ERROR
         LA    R5,1(,R4)           POINT AT START OF NEXT ENTRY
         LA    R8,6(,R8)           POINT TO NEXT ENTRY IN COMPARE TABLE
         B     BA58                GO PROCESS THIS NEXT ENTRY
*
** INVALID COMPARE CARD ENTRY - UNDERLINE ALL THE ENTRY
*  ----------------------------------------------------
BA68     BAL   R11,B6201           INDICATE INVALID CARD ENTRY
         CLM   R2,1,=C' '          Q. LAST ENTRY ON CARD
         BE    BA90                A. YES - GO REPORT
         B     BA58                GO PROCESS THIS NEXT ENTRY
         EJECT
***********************************************************************
* ROUTINE TO PROCESS PRINT CARD                                       *
***********************************************************************
         SPACE
*
** ENSURE PRINT CARD CAN BE PROCESSED
*  ----------------------------------
BA70     LA    R5,SCI080+11        POINT R5 AT PRINT INFORMATION
BA71     TM    SXW001A,BIT4+BIT5   Q. KEY OR COMPARE CONT.CARD EXPECTED
         BZ    BA72                A. NO - CONTINUE
         BAL   R11,B6401           INDICATE INVALID CONTINUATION CARD
BA72     TM    SXW001B,BIT11       Q. PRINT CARD PROCESSED
         BNO   BA73                A. NO - CONTINUE
         BAL   R11,B6301           INDICATE INVALID OPERATION
         B     BA90                GO REPORT THIS CARD
BA73     OI    SXW001B,BIT11       INDICATE PRINT CARD PROCESSED
*
** CHECK PRINT CARD - PRINT ENTRY
*  ------------------------------
         CLI   0(R5),C'C'          Q. PRINT IN CHARACTER FORMAT
         BE    BA75                A. YES - INSERT IN TABLE
         CLI   0(R5),C'X'          Q. PRINT IN HEX FORMAT
         BE    BA75                A. YES - INSERT IN TABLE
         CLI   0(R5),C'/'          Q. FORMAT IND. PRESENT
         BE    BA76                A. NO - CHECK NEXT FIELD
         BAL   R11,B6201           INDICATE INVALID ENTRY
         ICM   R2,1,=C' '          Q. LAST ENTRY ON CARD
         BNE   BA76                A. NO - CONTINUE
         B     BA90                A. YES - GO REPORT THE CARD
BA75     MVC   SXT001,0(R5)        INSERT PRINT FORMAT
         LA    R5,1(,R5)           POINT R5 AT NEXT ENTRY
         CLI   0(R5),C' '          Q. LAST ENTRY ON CARD
         BE    BA90                A. YES GO REPORT
         CLI   0(R5),C'/'          Q. VALID DELIMITER
         BE    BA76                A. YES - CONTINUE
         BAL   R11,B6201           INDICATE INVALID ENTRY
BA76     LA    R5,1(,R5)           POINT R5 AT NEXT ENTRY
*
** CHECK PRINT CARD - MAX.NO.OF RECORDS TO BE PRINTED
*  --------------------------------------------------
         LR    R4,R5               POINT R4 AT THIS ENTRY
         CLI   0(R5),C'/'          Q. THIS ENTRY PRESENT
         BE    BA7705              A. CHECK NEXT FIELD
         ZAP   SXT005D,=P'999999999'   INSERT MAX.NO.OF RECORDS
         ZAP   SXT005C,SXT005D          - TO BE PRINTED TO PRNT.INF.TAB
         CLC   0(4,R5),=C'ALL '    Q. ALL RECORDS TO BE PRINTED
         BE    BA90                A. YES - NO MORE ENTRIES, GO REPORT
         CLC   0(4,R5),=C'ALL/'    Q. ALL RECORDS TO BE PRINTED
         BNE   BA7605              A. NO - CHECK FURTHER
         LA    R5,4(,R5)           POINT R5 OVER THIS ENTRY
         B     BA78                GO CHECK NO.OF CONSECUTIVE ERRORS
BA7605   LA    R3,SHD002           POINT AT DUMMY HALFWORD
         LR    R4,R5               POINT AT NEXT ENTRY
         BAL   R11,B6101           EXTRACT MAX.NO.OF RECORDS
         CR    R4,R5               Q. EXTRACTION OK
         BNE   BA77                A. YES - CONTINUE
         BAL   R11,B6201           INDICATE INVALID ENTRY
         ICM   R2,1,=C' '          Q. LAST ENTRY ON CARD
         BNE   BA78                A. NO - CONTINUE
         B     BA90                A. YES - GO REPORT THE CARD
BA77     ZAP   SXT005D,SDW008+3(5)  INSERT VALUE INTO PRINT INF.TABLE
         ZAP   SXT005C,SXT005D      - (2 ENTRIES)
         CLI   0(R4),C' '          Q. ANY MORE ENTRIES
         BE    BA90                A. NO - GO REPORT
BA7705   LA    R5,1(,R4)           POINT R5 AT NEXT CARD ENTRY
*
** CHECK PRINT CARD - NO.OF CONSECUTIVE ERRORS BEFORE TERMINATION
*  --------------------------------------------------------------
BA78     LR    R4,R5               POINT AT NEXT ENTRY
         LA    R3,SHD002           POINT AT DUMMY AREA
         BAL   R11,B6101           EXTRACT NO.OF CONSECUTIVE ERRORS
         CR    R4,R5               Q. EXTRACTION OK
         BNE   BA79                A. YES - CONTINUE
         BAL   R11,B6201           INDICATE INVALID ENTRY
         B     BA90                GO REPORT
BA79     ZAP   SXT005H,SDW008+3(5) INSERT VALUE INTO PRINT INF. TABLE
         CLI   0(R4),C' '          Q. VALID DELIMITER
         BE    BA90                A. YES - OK,GO REPORT THE CARD
         BAL   R11,B6201           INDICATE INVALID ENTRY
         B     BA90                GO REPORT THE CARD
         EJECT
***********************************************************************
* ROUTINE TO PROCESS TITLE CARD                                       *
***********************************************************************
         SPACE
BA80     LA    R5,SCI080+11        POINT R5 AT TITLE
BA81     TM    SXW001A,BIT4+BIT5   Q. KEY OR COMPARE CONT.CARD EXPECTED
         BZ    BA83                A. NO - CONTINUE
         BAL   R11,B6401           INDICATE INVALID CONTINUATION CARD
BA83     TM    SXW001B,BIT12       Q. TITLE CARD PROCESSED
         BNO   BA85                A. NO - CONTINUE
         BAL   R11,B6301           INDICATE INVALID OPERATION
         B     BA90                GO REPORT THIS CARD
BA85     OI    SXW001B,BIT12       INDICATE TITLE CARD PROCESSED
         MVC   SXT060,0(R5)        MOVE 60 CHARACTER TITLE TO PNT TABLE
         B     BA90                GO REPORT CARD
         SPACE 3
***********************************************************************
* ROUTINE TO PROCESS EXIT CARD                                        *
***********************************************************************
         SPACE
*
** ENSURE NO CONTINUATION CARDS EXPECTED
*  -------------------------------------
BA86     LA    R5,SCI080+10        POINT R5 AT TITLE
BA87     TM    SXW001A,BIT4+BIT5   Q. KEY OR COMPARE CONT.CARD EXPECTED
         BZ    BA88                A. NO - CONTINUE
         BAL   R11,B6401           INDICATE INVALID CONTINUATION CARD
BA88     TM    SXW001B,BIT14       Q. EXIT CARD PROCESSED
         BNO   BA8805              A. NO - CONTINUE
         BAL   R11,B6301           INDICATE INVALID OPERATION
         B     BA90                GO REPORT THIS CARD
BA8805   OI    SXW001B,BIT14       INDICATE EXIT CARD PROCESSED
*
** INSERT EXIT NAMES INTO EXIT NAME TABLE
*  --------------------------------------
         L     R14,SFD004          PROVIDE ADDRESSABILITY FOR P6
         CLI   0(R5),C'/'          Q. 1ST EXIT RTN PRESENT
         BE    BA89                A. NO - EXTRACT 2ND NAME
         MVC   0(8,R14),0(R5)      MOVE 1ST EXIT RTN.NAME TO P6
         LA    R5,8(,R5)           POINT R5 AT END OF ENTRY
         CLI   0(R5),C' '          Q. END OF DATA FOR THIS CARD
         BE    BA90                A. YES - REPORT THIS CARD
         CLI   0(R5),C'/'          Q. VALID DELIMITER
         BNE   BA8905              A. NO - ERROR
BA89     MVC   8(8,R14),1(R5)      MOVE 2ND EXIT RTN NAME TO P6
         CLI   9(R5),C' '          Q. VALID DELIMITER
         BE    BA90                A. YES - REPORT CARD
BA8905   BAL   R11,B6201           INDICATE INVALID ENTRY
         B     BA90                GO REPORT THIS CARD
         EJECT
***********************************************************************
* REPORT THIS CARD                                                    *
***********************************************************************
         SPACE
BA90     TM    SXW001A,BIT0+BIT1+BIT2+BIT3+BIT6  Q. ANY ERRORS
         BZ    BA93                          A. NO -REPORT CARD
         OI    SXW001B,BIT8                  IND.ERROR IN CARD INPUT
         NI    SXW001A,255-BIT0-BIT1-BIT2-BIT3-BIT6 SET OFF CARD IND.
         B     BA94                          GO REPORT CARD ERRORS
BA93     MVC   0(2,R12),=C' *'     INSERT DUMMY MSG. INTO TABLE
         LA    R12,2(,R12)         POINT R12 PAST THIS ENTRY
BA94     BAL   R11,B6501           PRINT LINE ON REPORT
         B     BA10                GO READ NEXT CARD
         SPACE 3
***********************************************************************
* END OF CARD INPUT - FINISH THE REPORT                               *
***********************************************************************
         SPACE
*
** ENSURE NO CONTINUATION CARDS EXPECTED
*  -------------------------------------
BA95     TM    SXW001A,BIT4+BIT5   Q. CONTINUATION CARD EXPECTED
         BZ    BA96                A. NO - CONTINUE
         MVI   SCI080,C' '         CLEAR
         MVC   SCI080+1(79),SCI080 - CARD IMAGE
         BAL   R11,B6401           IND. INVALID CONTINUATION CARD
         BAL   R11,B6501           PRINT ON REPORT
*
** INDICATE END OF KEY FIELD TABLE & COMPARE FIELD TABLE
*  -----------------------------------------------------
BA96     MVI   0(R7),X'FF'         INSERT END MARKER
BA97     TM    SXW001B,BIT13       Q. COMPARE CARD PROCESSED
         BZ    BA98                A. NO - DO NOT INSERT END MARKER
         MVI   0(R8),X'FF'         INSERT END MARKER
BA98     EQU   *
         TM    SXW001B,BIT8        Q. ANY ERRORS
         BZ    BA99                A. NO - RETURN
         MVI   0(R10),C'7'         INDICATE ERRORS FOUND
*
BA99     MODOUT
         EJECT
***********************************************************************
* B6001 - READ A CONTROL CARD (USING B3201 VIA CONCARD)               *
***********************************************************************
         SPACE
B6001    LATCH B3201,(SCI080)      READ A CARD
         CLI   SCI080,X'00'        Q. END OF CARD INPUT
         BR    R11                 RETURN
         SPACE 3
***********************************************************************
* B6101 - ROUTINE TO EXTRACT VARIABLE LENGTH NUMERIC FIELD FROM       *
*         CARD INPUT (DELIMITER IS , BLANK OR / )                     *
*          I   R4  POINTS TO START OF FIELD                           *
*          O   R4  POINTS TO DELIMITER  (UNCHANGED IF NOT FOUND)      *
*          I   R3  POINTS AT HALFWORD TO CONTAIN BINARY RESULT        *
*                  OF EXTRACTION (SDW008 CONTAINS PACKED AMOUNT)      *
*          O   (R3) CONTAINS RESULT OF EXTRACTION                     *
***********************************************************************
         SPACE
*
** POINT R15 AT NEXT DELIMITER
** IF NOT FOUND WITHIN 10 BYTES THEN THE ENTRY IS INVALID
*  ------------------------------------------------------
B6101    TRT   0(10,R4),CXT256     Q. DELIMITER FOUND WITHIN 10 BYTES
         BZR   R11                 A. NO - RETURN
         LA    R15,0(,R1)          ADDRESS OF DELIMITER TO R15
         LR    R2,R15              SAVE ADDRESS OF DELIMITER
*
** ENSURE FIELD IS NUMERIC
** IF NOT THEN ENTRY IS INVALID
*  ----------------------------
         LR    R14,R4              POINT R14 AT FIELD TO BE CHECKED
         SR    R15,R14             LENGTH OF FIELD TO R15
         BZR   R11                 RETURN IF FIELD HAS ZERO LENGTH
         NUMBR                     Q. FIELD
         CR    R14,R15                - NUMERIC
         BER   R11                 A. YES - RETURN
*
** PACK FIELD FROM CARD INPUT, THEN CONVERT TO BINARY
*  --------------------------------------------------
         SR    R2,R4               LENGTH OF FIELD TO R2
         BCTR  R2,R0               LENGTH OF FIELD MINUS 1 TO R2
         LA    R15,112             INSERT LENGTH MINUS 1 OF SDW008 (7)
*                                  - INTO R15 FOR EXECUTE INSTRUCTION
         OR    R15,R2              SET UP R15 WITH LENGTHS FOR EX.INST.
         EX    R15,EX01            PACK FIELD INTO W/A
*                                  **  PACK  SDW008(0),0(0,R4)  **
         CVB   R15,SDW008          CONVERT TO BINARY
         STH   R15,0(,R3)          STORE RESULT IN OUTPUT AREA
         LA    R4,1(R4,R2)         POINT R4 AT DELIMITER
         BR    R11                 RETURN
         EJECT
***********************************************************************
* B6201 - INVALID CARD ENTRY, UNDERLINE ALL ENTRY                     *
*         I/O R5   POINTS TO CARD ENTRY                               *
*         I/O R12  POINTS TO MESSAGE AREA                             *
*          O  R2   BITS 24-31 CONTAIN DELIMITER                       *
***********************************************************************
         SPACE
B6201    MVI   CXT256+C',',X'00'   DELETE COMMA IN TRANSLATE TABLE
         SR    R1,R1               CLEAR R1
         TRT   0(75,R5),CXT256     POINT R1 AT NEXT DELIMITER
         SR    R1,R5               FIND LENGTH -1 OF THIS ENTRY
         LA    R14,CCW080-SCI080+0(,R5)  POINT TO UNDERLINE AREA
         EX    R1,EX02             UNDERLINE THIS ENTRY
*                                  **  MVC   0(0,R14),=75C'-'  **
         MVI   CXT256+C',',C','    REINSTATE COMMA IN TRANSLATE TABLE
         MVC   0(14,R12),=C'INVALID ENTRY*'
         LA    R12,14(,R12)        POINT AT NEXT MESSAGE AREA
         OI    SXW001A,BIT3        SET ERROR IND.
         LA    R5,1(R1,R5)         POINT TO NEXT CARD ENTRY
         BR    R11                 RETURN
         SPACE 2
***********************************************************************
* B6301 - INVALID OPERATION                                           *
***********************************************************************
         SPACE
B6301    MVC   0(18,R12),=C'INVALID OPERATION*'
         LA    R12,18(,R12)        POINT AT NEXT MESSAGE AREA
         OI    SXW001A,BIT2        SET ERROR IND.
         MVI   CCW080+5,C'X'       UNDERLINE OPERATION ERROR
         BR    R11                 RETURN
         SPACE 2
***********************************************************************
* B6401 - INVALID CONTINUATION CARD                                   *
***********************************************************************
         SPACE
B6401    MVC   0(27,R12),=C'CONTINUATION CARD EXPECTED*'
         LA    R12,27(,R12)        POINT AT NEXT MESSAGE AREA
         OI    SXW001A,BIT6        SET ERROR IND
         NI    SXW001A,255-BIT4-BIT5  SET OFF CONT.CARD INDS.
         BR    R11                 RETURN
         SPACE 2
***********************************************************************
* B6501 - PRINT VALIDATION REPORT (D0701 VIA P03)                     *
***********************************************************************
         SPACE
B6501    MVI   0(R12),X'FF'        INSERT END OF MESSAGE MARKER
         LATCH D0701,(SCI080,CCW080,CCWXXX,CCHXXX) PRINT ON REPORT
         LA    R12,CCWXXX          POINT TO START OF MESSAGE
         MVI   CCW080,C' '         CLEAR
         MVC   CCW080+1(79),CCW080 - UNDERLINE
         BR    R11                 RETURN
         EJECT
***********************************************************************
* EXECUTED INSTRUCTIONS                                               *
***********************************************************************
         SPACE
EX01     PACK  SDW008(0),0(0,R4)   PACK FIELD TO W/A
EX02     MVC   0(0,R14),=75C'-'    UNDERLINE INVALID ENTRY
         SPACE 3
***********************************************************************
* CONSTANTS, STORAGE AREAS & LITERAL POOL                             *
***********************************************************************
         SPACE
SDW008   DS    D                   W/A FOR CONVERT TO DECIMAL INSTR.
SFD004   DS    F                   SAVE AREA FOR P6 ADDRESS
SHD002   DS    H                   W/A FOR CHECKING BIT COMPARE MASK
*                                  - ALSO DUMMY HALFWORD FOR B6101
CCWXXX   DS    CL200               MESSAGE AREA (FOR D0701)
CPD002   DC    PL2'10'             NO.OF KEY ENTRIES ALLOWED
CPD002A  DC    PL2'10'             NO.OF COMPARE ENTRIES ALLOWED
CXT256   DS    0CL256              TRANSLATE TABLE
         DC    64X'00',C' '        - DELIMITERS
         DC    32X'00',C'/'        - TO MARK END OF CARD ENTRY
         DC    9X'00',C',',148X'00'
CXT256A  DS    0CL256              TRANSLATE TABLE FOR BYTE MASK
         DC    193X'FF'                 - INVALID
         DC    X'0A0B0C0D0E0F'          - HEX CHARACTERS
         DC    41X'FF'                  - TRANSLATED
         DC    X'00010203040506070809'  - AS X'FF'
         DC    6X'FF'
SCI081   DS    0CL81               CARD INPUT AREA
SCI080   DS    CL80                CARD IMAGE
SCI001   DC    C' '                DELIMITER
CCW080   DC    CL80' '             CARD UNDERLINE
CCHXXX   DS    0C
         DC    CL35'1'                                      - LINE 1
         DC    CL98'DATA SET COMPARE PROGRAM - CARD VALIDATION REPORT'
         DC    CL133' '                                     - LINE 2
         DC    CL31'0',CL75'CARD IMAGE',CL27'ERROR'         - LINE 3
         DC    C'Z'                                         - END
SXW002   DS    0XL2                ERROR INDICATORS
SXW001A  DS    X                   - ERRORS IN CARD
*                                    BIT 0  INVALID CARD CODE
*                                        1  INVALID DELIMITER
*                                        2  INVALID OPERATION
*                                        3  ERROR IN ENTRY
*                                        4  CONTINUATION OF KEY CARD
*                                        5  CONTINUATION OF COMPARE CRD
*                                        6  INVALID CONTINUATION CARD
*                                        7
SXW001B  DS    X                   - ERRORS IN TOTAL INPUT
*                                        8  ERROR IN CARD INPUT
*                                        9  FORMAT CARD PROCESSED
*                                        10 KEY CARD PROCESSED
*                                        11 PRINT CARD PROCESSED
*                                        12 TITLE CARD PROCESSED
*                                        13 COMPARE CARD PROCESSED
*                                        14 EXIT CARD PROCESSED
*                                        15 MORE THAN 10 KEY/COMP.ENTRS
         ORG   SXW002
         DC    2X'00'              - INITIALISED
         LTORG
         EJECT
DS1      DSECT
*
** PRINT INFORMATION TABLE
*  -----------------------
SXTXXX   DS    0XL203              PRINT INFORMATION TABLE
SXT002A  DS    XL2                 - DATA SET 1 RECORD LENGTH
SXT002B  DS    XL2                 - DATA SET 2 RECORD LENGTH
SXT005A  DS    PL5                 - DATA SET 1 RECORD NO.
SXT005B  DS    PL5                 - DATA SET 2 RECORD NO.
SXT005C  DS    PL5              ** - NO. UNEQUAL/EXTRA RECORDS TO
*                                    PRINT BEFORE PRINT TERMINATION
SXT005D  DS    PL5              ** - MAX.NO. TO PRINT BEFORE PRINT TERM
SXT005E  DS    PL5                 - EXTRA RECORDS ON DATA SET 1
SXT005F  DS    PL5                 - EXTRA RECORDS ON DATA SET 2
SXT005G  DS    PL5                 - NO.OF CONSECUTIVE UNEQUAL/EXTRA
*                                    RECORDS BEFORE TERMINATION
SXT005H  DS    PL5              ** - MAX.NO.OF CONSECUTIVE UNEQUAL/
*                                    EXTRA RECORDS BEFORE TERM.
SXT001   DS    C                ** - PRINT FORMAT IND. C OR X
SXT005J  DS    PL5                 - NO.OF UNEQUAL RECORDS
SXT044A  DS    CL44                - DATA SET 1 NAME
SXT044B  DS    CL44                - DATA SET 2 NAME
SXT060   DS    CL60             ** - REPORT TITLE
SXT005K  DS    CL5                 - MAX.LENGTH OF RECORD/UNDERLINE
*                                  ** = SET UP BY THIS MODULE
         END
