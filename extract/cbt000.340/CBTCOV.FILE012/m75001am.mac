         MACRO
&NAME    BEGIN &BASE=12,                                               C
               &SAVE=12,                                               C
               &RET=NORMAL,                                            C
               &EXIT=EXIT,                                             C
               &SAVAREA=,                                              C
               &RGENT=15
         GBLA  &GRGENT
         GBLA  &GTEST
         GBLA  &GSAVE
         GBLC  &GEXITNM
         LCLA  &A
         LCLC  &F
         LCLC  &G
         LCLC  &H
&GSAVE   SETA  &SAVE
&GEXITNM SETC  '&EXIT&SYSNDX'
&GRGENT  SETA  &RGENT
&GTEST   SETA  &GTEST+1
         AIF   ('&GTEST' GT '1').B1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
.B1      ANOP
         USING *,R&RGENT
         B     84(R&RGENT)
         AIF   ('&SYSECT' NE '').QA
         DC    CL8'&&PRIVATE'
         AGO   .QB
.QA      DC    CL8'&SYSECT'
.QB      ANOP
&SAVAREA DC    18F'0'
         STM   R14,R&SAVE,12(R13)
         LA    R14,&GEXITNM+18
         LA    R&RGENT,12(R&RGENT)
         ST    R13,4(R&RGENT)
         ST    R&RGENT,8(R13)
         LR    R13,R&RGENT
         DROP  R&RGENT
         AIF   ('&BASE'(1,1) EQ '(').ZB
         USING *-94,&BASE
         AGO   .ZZ
.ZB      AIF   (K'&BASE GT 10).Z1
&A       SETA  K'&BASE-2
&F       SETC  '&BASE'(2,&A)
         USING *-94,&F
         AGO   .ZZ
.Z1      AIF   (K'&BASE GT 18).Z2
&A       SETA  K'&BASE-10
&F       SETC  '&BASE'(2,8)
&G       SETC  '&BASE'(10,&A)
         USING *-94,&F.&G
         USING *-94,&F&G
         AGO   .ZZ
.Z2      ANOP
&A       SETA  K'&BASE-18
&F       SETC  '&BASE'(2,8)
&G       SETC  '&BASE'(10,8)
&H       SETC  '&BASE'(18,&A)
         USING *-94,&F&G&H
.ZZ      ANOP
         AIF   ('&BASE(1)' EQ '15' OR '&BASE(1)' EQ '13').B2
         LR    R&BASE(1),R13
.B2      ANOP
         AIF   ('&BASE(2)' EQ '').BCONT
         AIF   ('&BASE(2)' EQ '15').B3
         LR    R&BASE(2),R13
.B3      ANOP
         B     *+6
         DC    H'4096'
         AH    R&BASE(2),*-2
         AIF   ('&BASE(3)' EQ '').BCONT
         LR    R&BASE(3),R&BASE(2)
         AH    R&BASE(3),*-8
         AIF   ('&BASE(4)' EQ '').BCONT
         LR    R&BASE(4),R&BASE(3)
         AH    R&BASE(4),*-14
         AIF   ('&BASE(5)' EQ '').BCONT
         LR    R&BASE(5),R&BASE(4)
         AH    R&BASE(5),*-20
         AIF   ('&BASE(6)' EQ '').BCONT
         LR    R&BASE(6),R&BASE(5)
         AH    R&BASE(6),*-26
         AIF   ('&BASE(7)' EQ '').BCONT
         LR    R&BASE(7),R&BASE(6)
         AH    R&BASE(7),*-32
.BCONT   ANOP
         BR    R14
&GEXITNM EQU   *
         L     R13,4(R13)
         AIF   ('&RET' EQ 'NORMAL').B4
         A     R14,12(R13)
         AGO   .B5
.B4      L     R14,12(R13)
.B5      ANOP
         LM    R0,R&SAVE,20(R13)
         MVI   24(R13),X'FF'
         BR    R14
         EJECT
         MEXIT
.ERR1    ANOP
         MNOTE 8,'INVALID ''BASE'' PARAMETER'
         MEXIT
.ERR2    ANOP
         MNOTE 8,'INVALID ''SAVE'' PARAMETER'
         MEXIT
.ERR3    ANOP
         MNOTE 8,'INVALID  ''RET'' PARAMETER'
         MEXIT
.ERR4    ANOP
         MNOTE 8,'INVALID ''EXIT'' PARAMETER'
         MEXIT
.ERR5    ANOP
         MNOTE 8,'INVALID ''RGENT'' PARAMETER'
         EJECT
         MEND
