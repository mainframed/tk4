*
**   AUTHORS - I.C.I. (MOND DIVISION)
*
*              ********************************************************
*              *********  NOTES ON SUPERLIST UTILITY  *****************
*              ********************************************************
*              1)  DD CARDS REQUIRED                                  *
*              *   //SYSPRINT  DD  SYSOUT=A                           *
*              *   //DDN DD UNIT=DISK,DISP=OLD,VOLUME=SER=VOLSER      *
*              * UP TO NINE DD CARDS WITH DDNAMES                     *
*              *   DD1 THROUGH DD9 MAY BE USED.  A SEPARATE REPORT    *
*              *   WILL BE GENERATED FOR EACH VOLUME SPECIFIED.       *
*              *------------------------------------------------------*
*              2) PARM FIELD SCRATCH OPTION                           *
*              *   IF NO PARM IS PASSED TO THE PROGRAM, THE ENTIRE    *
*              *   VTOC WILL BE LISTED.  IF A PARM  IS PASSED, THE    *
*              *   CHARACTERS OF THE PARM WILL BE COMPARED WITH THE   *
*              *   FIRST GROUP OF THAT MANY CHARACTERS OF EACH        *
*              *   DSNAME ON EVERY VOLUME.  THOSE DSNAMES RESULTING   *
*              *   IN AN EQUAL COMPARE WILL BE SCRATCHED (EXCEPT      *
*              *    THE SYSOUT DATA SET), AND ALL WILL BE LISTED.     *
*              *------------------------------------------------------*
*              2A) PARM FIELD LIST OPTION                             *
*              *   IF NO PARM IS PASSED,THEN THE ENTIRE SUPRLIST IS   *
*              *   PRODUCED.IF A PARM OF '2' IS PASSED THEN ONLY      *
*              *   THE FORMAT 5 LISTING AND THE SPACE ACCOUNTING      *
*              *   SUMMARY IS PRODUCED.IF THE LIST OPTION IS USED     *
*              *   THEN THE SCRATCH OPTION IS IGNORED                 *
*              *------------------------------------------------------*
*              3) THE QUANTITIES ALLOC, USED, AND UNUSED ARE IN       *
*              *   TRACKS, AND INCLUDE ANY SECONDARY EXTENTS.  THE    *
*              *   QUANTITIES USED AND UNUSED APPLY ONLY TO           *
*              *   SEQUENTIAL OR PARTITIONED DATA SETS.  THE          *
*              *   QUANTITIES FIRST AND LAST REFER TO THE             *
*              *   RELATIVE (TO ZERO) BEGINNING AND ENDING TRACKS OF  *
*              *   ALL EXTENTS ON THE VOLUME.                         *
*              ********************************************************
*              ********************************************************
*              ********************************************************
       SPACE 2
SUPRLIST START 0
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         L     4,ADCON
         USING SUPRLIST+4095,4
         ST    13,SAVARA+4
         LA    13,SAVARA
         L     1,0(1)
         MVC   PARMSAV(42),0(1)
         CLI   PARMSAV+2,X'F2'         CHECK FOR NOPRINT PARM.
         BNE   *+16
         OI    READDSCB+1,X'F0'        IF PARM IS PRESENT,,,
         OI    MLOP+1,X'F0'            THEN SET THE BRANCH SWITCHES
         OI    RDBN+1,X'F0'
         LH    10,PARMSAV         PICK UP LENGTH
         BCTR  10,0
         STC   10,CMPR+1          SET IN COMPARE INSTRUCTION
        RDJFCB (PRTDCB)           READ SYSPRINT JFCB
         OPEN  (PRTDCB,OUTPUT),TYPE=J OPEN SYSOUT DCB
         USING IHADCB,DCB          TO INPUT DCB
         LA    DDCNT,9             WE HANDLE DD9 THROUGH DD1.
BIGLOOP  LA    DCB,GODCB           POINT DCB TO THE REAL DCB
         STC   DDCNT,DCBDDNAM+2    AND RECREATE
         OI    DCBDDNAM+2,C'0'     THE SAME DDNAME
         ST    DDCNT,SVCNT
         MVC   LINK,=XL5'00'            ZERO LINK FIELD FOR NEW DD.
        RDJFCB (GODCB)             NOW READ IN THE JFCB FOR DDN..
         LTR   15,15         DDNAME BLANK OR NO DD STMNT.     (REL.21)
         BNZ   TESTDD            INCREMENT LOOP INDEX
         MVC   EXAMES(6),JFVOLSER  MOVE VOLUME SER TO PRINT MESSAGE
         MVC   JFCB(44),VTOCNAME   INSERT DSNAME OF VTOC INTO JFCB
         OPEN (GODCB,INPUT),TYPE=J NOW OPEN DCB SO AS TO READ VTOC
         GETMAIN R,LV=2000        SAVE 2K OF CORE FOR SYSTEM
         LR    5,1
         GETMAIN VC,LA=ASK,A=RECEIVE  GET REMAINDER FOR ME
         LR    1,5
         FREEMAIN R,LV=2000,A=(1) GIVE SYSTEM CORE BACK
         L     5,RECEIVE          PICK UP ADDRESS OF GOTTEN CORE
         L     6,RECEIVE+4        LENGTH OF AREA
CK3330   CLI   GODCB+17,X'29'
         BNE   CK2311
         MVC   CPT,=H'19'
         MVC   EXAMED(4),=C'3330'
         MVC   DEVCODE,=X'30502009'
         B     GONOW
CK2311   CLI   GODCB+17,X'21'          2311?
         BNE   CK2314
OTHER    MVC   CPT,=H'10'
         MVC EXAMED(4),=C'2311'
         MVC   DEVCODE,=X'30002001'
         B     GONOW
CK2314   CLI   GODCB+17,X'28'          2314?
         BNE   CK2301
         MVC   CPT,=H'20'
         MVC EXAMED(4),=C'2314'
         MVC   DEVCODE,=X'30C02008'
         B     GONOW
CK2301   CLI   GODCB+17,X'22'          2301?
         BNE   CK2303
         MVC   CPT,=H'200'
         MVC EXAMED(4),=C'2301'
         MVC   DEVCODE,=X'30402002'
         B     GONOW
NOLST    MVI   LSTPARM,X'D5'
         BR   10
CK2303   CLI   GODCB+17,X'23'          2303?
         BNE   CK2302
         MVC   CPT,=H'10'
         MVC EXAMED(4),=C'2303'
         MVC   DEVCODE,=X'30002003'
         B     GONOW
CK2302   CLI   GODCB+17,X'24'          2302?
         BNE   CK2321
         MVC   CPT,=H'46'
         MVC EXAMED(4),=C'2302'
         MVC   DEVCODE,=X'30002004'
         B     GONOW
CK2321   CLI   GODCB+17,X'25'          2321?
         BNE   OTHER                   ASSUME 10 TRKS/CYL
         MVC   CPT,=H'20'
         MVC EXAMED(4),=C'2321'
         MVC   DEVCODE,=X'30002005'
GONOW    MVC   MSG(81),STRTMSX        PRINT UTILITY TITLE
         BAL   10,PUT
         MVC   MSG+55(26),EXAMESX+11
         BAL   10,PUT
         BAL   10,READ            READ FORMAT 4 DSCB
         B     FMT4
READ     READ  DECB,SF,GODCB,DSCB READ IN A DSCB
         CHECK DECB                ISSUE BSAM CHECK
         BR    10
RDLNK    CLC   DSCB+135(5),LINK   CHECK FOR LINK TO ITSELF
         BE    INVLK
         MVC   LINK,DSCB+135      SAVE LINK FIELD
         LA    1,OBTAIN           POINT TO PARM LIST
         SVC   27                 READ DSCB
         BR    10
PUT      CLI   LSTPARM,X'E8'           CHECK IF PRINT IS ON,OR OFF
         BE   GMGB
         CLC   MSG+1(24),=CL24'SPACE ACCOUNTING SUMMARY'
         BNE   GMGA                    IF SPACE ACCOUNTING SUMMARY,,,
         MVI   LSTPARM,X'E8'           REACHED SET PRINT ON
GMGB     PUT   PRTDCB,MSG
GMGA     MVI   MSG,X'40'               CLEAR OUTPUT AREA
         MVC   MSG+1(132),MSG
         BR    10                 RETURN
READDSCB BC    0,GOGO             IGNORE SCRATCH OPTION IF NOLIST SET.
         CLC   PARMSAV(2),=H'0'   CHECK FOR PARM
         BE    GOGO
CMPR     CLC   PARM,DSCB          COMPARE
         BNE   GOGO
         CLC   DSCB(44),PRTJFCB   CHECK FOR SYSPRINT DATA SET (MVT)
         BE    GOGO
         MVC   DEVSER,JFVOLSER    MOVE IN VOL SERIAL
         SR    0,0
         SCRATCH CAMIT            SCRATCH DSCB
         CLC   SEQ,=H'0'          SUCCESSFUL SCRATCH
         BNE   ERROR
         MVC   MSGNAM,DSCB
         MVI   MSG+1,C'*'
         MVC   RECFM(24),DELETED  MOVE IN DELETED MESSAGE
         BAL   10,PUT
         BAL   10,PUT             BLANK LINE
         B     RDB
GOGO     CLC   DSCB+135(5),=XL5'00' CHECK FOR LINK FIELD
         BE    RDB
         BAL   10,RDLNK           READ DSCB LINKED TO
         CLI   DSCBFMT,C'5'
         BE    TAG                TAG FOR HEADER
         CLI   DSCBFMT,C'2'       FORMAT 2 DSCB
         BE    FMT2
         CLI   DSCBFMT,C'3'       FORMAT 3 DSCB
         BE    FMT3
         CLI   DSCBFMT,C'6'
         BE    FMT6
         B     ERROR
RDB      CLI   DSCB,C'5'          CHECK FOR TAGGED FMT5
         BNE   GET
RDBN     LA    10,*+8
         BC    0,NOLST
         MVC   MSG,EXAMESX        PRINT HEADING
         BAL   10,PUT
GET      BAL   10,READ            READ DSCB
         CLI   DSCBFMT,C'6'       FORMAT 6 DSCB
         BE    FMT6
         CLI   DSCBFMT,C'1'        IS THIS A TYPE 1 DSCB
         BNE   READDSCB
         BAL   10,PUT             BLANK LINE
         MVC   MSG+1(132),PDLINE  SET UP SEPERATOR LINE
         BAL   10,PUT
         MVC   MSGNAM,DSCB
         CLI   EXT1,X'00'         XTENT TYPE
         BNE   NOMOD
         MVC   RECFM(22),MDLMSG   MODEL DSCB
         B     DATE
NOMOD    TM    DSCBORG,X'80'
         BNO   TM2
         MVC   MSGDSTY,=C'INDSEQ'
         B     DATES
TM2      TM    DSCBORG,X'40'
         BNO   TM3
         MVC   MSGDSTY,=C'SEQUEN'
         B     DATES
TM3      TM    DSCBORG,X'20'
         BNO   TM4
         MVC   MSGDSTY,=C'DIRECT'
         B     DATES
TM4      TM    DSCBORG,X'02'
         BNO   ORGERR
         MVC   MSGDSTY,=C'PARTND'
         B     DATES
ORGERR   MVC   MSGDSTY,=C'UNDEFD'
DATES    SR    10,10
         LA    2,RECFM
         TM    DSCBRCFM,X'C0'     UNDEF
         BNO   XX1
         MVC   0(1,2),=C'U'
         LA    2,1(2)
         B     XX3
XX1      TM    DSCBRCFM,X'80'         FIXED
         BNO   XX2
         MVC   0(1,2),=C'F'
         LA    2,1(2)
         B     XX3
XX2      MVC   0(1,2),=C'V'
         LA    2,1(2)
XX3      TM    DSCBRCFM,X'20'     TK OVLOW
         BNO   XX4
         MVC   0(1,2),=C'T'
         LA    2,1(2)
XX4      TM    DSCBRCFM,X'10'     BLOCKED
         BNO   XX5
         MVC   0(1,2),=C'B'
         LA    2,1(2)
XX5      TM    DSCBRCFM,X'08'     STANDARD
         BNO   XX6
         MVC   0(1,2),=C'S'
         LA    2,1(2)
XX6      TM    DSCBRCFM,X'04'     ASA  CC
         BNO   XX7
         MVC   0(1,2),=C'A'
         LA    2,1(2)
XX7      TM    DSCBRCFM,X'02'     MACHINE
         BNO   XX8
         MVC   0(1,2),=C'M'
         LA    2,1(2)
XX8      C     2,RECAD
         BNL   XX9
         MVI   0(2),X'40'
         LA    2,1(2)
         B     XX8
XX9      MVC   WK2,LRECLD
         L     10,WK4
         CVD   10,PACK
         MVC   LRECL,ED6
         ED    LRECL,PACK+5
         MVC   WK2,BLKLD
         L     10,WK4
         CVD   10,PACK
         MVC   BLKSZ,ED6
         ED    BLKSZ,PACK+5
DATE     SR    10,10
         IC    10,DSCBCDAT
         CVD   10,PACK
         UNPK  MSGCREA(2),PACK
         OI    MSGCREA+1,X'F0'
         MVI   MSGCREA+2,C'.'
         MVC   WK2,DSCBCDAT+1
         L     10,WK4
         CVD   10,PACK
         UNPK  MSGCREA+3(3),PACK
         OI    MSGCREA+5,X'F0'
         SR    10,10
         IC    10,DSCBEDAT
         CVD   10,PACK
         UNPK  MSGPURG(2),PACK
         OI    MSGPURG+1,X'F0'
         MVI   MSGPURG+2,C'.'
         MVC   WK2,DSCBEDAT+1
         L     10,WK4
         CVD   10,PACK
         UNPK  MSGPURG+3(3),PACK
         OI    MSGPURG+5,X'F0'
         CLI   EXT1,0             MODEL DSCB
         BNE   SER
         BAL   10,PUT             PRINT MODEL DSCB
         B     READDSCB
SER      MVC   SERIAL,DSCB+45     SERIAL NUMBER
CYLS     TM    SECAL,X'C0'
         BNO   BLOCKS
         MVC   TYPSEC,=C'CYLS  '
         B     WRIT
BLOCKS   TM    SECAL,X'40'
         BZ    TRKS
         MVC   TYPSEC,=C'BLOCKS'  TYPE OF SECONDARY ALLOCATION
         B     WRIT
TRKS     TM    SECAL,X'80'
         BZ    NONE
         MVC   TYPSEC,=C'TRACKS'
         B     WRIT
NONE     MVC   TYPSEC(6),=C'NONE  '
         B     WRITE
WRIT     LH    10,SECAL+2         SECONDARY ALLOCATION
         LTR   10,10
         BZ    NONE
         CVD   10,PACK
         MVC   NUMBER,ED6
         ED    NUMBER,PACK+5
WRITE    BAL   10,PUT             WRITE LINE
EXTNT    MVC   MSG(44),EXTNTMSG   HEADER
         BAL   10,PUT
         CLI   DSCBFMT,C'3'       FMT3
         BNE   FT1
         LA    8,DSCB+4
         B     NXT
FT1      LA    8,DSCB+105
         MVC   LASTF1(2),LAST
NXT      CLI   0(8),0             NO MORE EXTENTS
         BNZ   FT4
         CLI   DSCBFMT,C'4'
         BE    GET5
PTALOC   CLC   DSCB+135(5),=XL5'00' END OF DATA SET EXTENTS
         BNE   READDSCB
         L     9,ALCSAV           LOAD TOTAL ALLOCATED
         SR    10,10
         ST    10,ALCSAV          CLEAR FOR NEXT DATA SET
         L     10,ALLOC
         AR    10,9
         ST    10,ALLOC
         CVD   9,PACK
         MVC   MSGALLOC,ED6
         ED    MSGALLOC,PACK+5
         LH    8,LASTF1
         L     10,USED
         AR    10,8
         ST    10,USED
         CVD   8,PACK
         MVC   MSGUSED,ED6
         ED    MSGUSED,PACK+5
         SR    9,8
         L     10,UNUSED
         AR    10,9
         ST    10,UNUSED
         CVD   9,PACK
         MVC   MSGUNUS,ED6
         ED    MSGUNUS,PACK+5
         B     READDSCB
GET5     BAL   10,READ            GET FIRST FMT5
         XC    ALCSAV,ALCSAV
TAG      MVI   DSCB,C'5'          TAG FOR HEADING
         B     FMT5
FT4      CLI   DSCBFMT,C'4'       CHECK FOR VTOC
         BNE   DATA
         MVC   TYPE(4),=C'VTOC'
         B     NUMCT
DATA     TM    0(8),1             DATA
         BZ    OVFLW
         MVC   TYPE(4),=C'DATA'
         B     NUM
OVFLW    TM    0(8),2             OVERFLOW AREA
         BZ    INDEX
         MVC   TYPE(8),=C'OVERFLOW'
         B     NUM
INDEX    TM    0(8),4             INDEX AREA
         BZ    NUM
         MVC   TYPE(5),=C'INDEX'
NUM      CLI   0(8),X'80'
         BNE   CONTIG
         MVC   TYPE(9),=C'SPLIT CYL'
         B     NUMCT
CONTIG   CLI   0(8),X'81'
         BNE   NUMCT
         MVC   TYPE+5(6),=C'CONTIG'
NUMCT    SR    9,9
         IC    9,1(8)
         CVD   9,PACK
         UNPK  EXTENT(2),PACK     EXTENT NUMBER
         OI    EXTENT+1,X'F0'
         MVC   WK2,2(8)           FIRST TRACK
         LH    9,WK2
         MH    9,CPT
         MVC   WK2,4(8)
         AH    9,WK2
         CVD   9,PACK
         MVC   FIRST(6),ED6
         ED    FIRST(6),PACK+5
         MVC   WK2,6(8)           LAST TRACK
         LH    10,WK2
         MH    10,CPT
         MVC   WK2,8(8)
         AH    10,WK2
         CVD   10,PACK
         MVC   LSTK(6),ED6
         ED    LSTK(6),PACK+5
         SR    10,9               LENGTH IN TRACKS
         LA    10,1(10)
         L     9,ALCSAV           ACCUMULATE TRACKS ALLOCATED
         AR    9,10
         ST    9,ALCSAV
         CVD   10,PACK
         MVC   LENGTH(6),ED6
         ED    LENGTH(6),PACK+5
         BAL   10,SAVEXT          SAVE EXTENT
         BAL   10,PUT
         LA    8,10(8)            MOVE TO NEXT EXTENT
         CLI   0(8),C'3'
         BNE   *+8
         LA    8,1(8)             JUMP OVER ID
         B     NXT
SETUP    LTR   8,8                NO PRINT IF ZERO
         BZ    SETINC
         CVD   8,PACK
         MVC   AMT(8),ED8
         ED    AMT(8),PACK+4
         MVC   FIRST(50),0(7)     MOVE IN DESCRIPTION
         BAL   10,PUT
SETINC   LA    7,50(7)
         BR    9
FMT2     MVC   MSG(22),FORMAT2    DSNAME LINE
         BAL   10,PUT
         SR    8,8
         LA    7,ISAM1            POINT TO DESC LINE
         IC    8,DSCB+45          INDEX LEVELS
         BAL   9,SETUP
         IC    8,DSCB+46          TRKS OF MASTER INDEX
         BAL   9,SETUP
         IC    8,DSCB+52          TRKS CYL OVERFLOW
         BAL   9,SETUP
         MVC   WK2,DSCB+59        RECORDS TO BE DELETED
         LH    8,WK2
         BAL   9,SETUP
         MVC   WK3,DSCB+61        RANDOM REF TO OVERFLOW
         L     8,WK4
         BAL   9,SETUP
         LH    8,DSCB+64          CORE BYTES TO HOLD HIGHEST INDEX
         BAL   9,SETUP
         SR    8,8
         IC    8,DSCB+66          TRKS OF HIGHEST INDEX
         BAL   9,SETUP
         MVC   WK4,DSCB+67        RECORDS OF PRIME DATA
         L     8,WK4
         BAL   9,SETUP
         LH    8,DSCB+124         RCDS LEFT ON OVERFLOW TRACK
         BAL   9,SETUP
         LH    8,DSCB+126         TRKS LEFT IN OVERFLOW AREA
         BAL   9,SETUP
         LH    8,DSCB+128         RCDS IN OVERFLOW AREA
         BAL   9,SETUP
         LH    8,DSCB+130         CYLS OF OVERFLOW AREA
         BAL   9,SETUP
         B     PTALOC
FMT3     MVC   MSG(22),FORMAT3
         B     WRITE
FMT4     MVC   MSG(22),FORMAT4
         BAL   10,PUT             PRINT HEADER
         LH    8,DSCB+62          NO. OF LOGICAL CYLINDERS
         CVD   8,PACK
         MVC   NUMCYL(6),ED6
         ED    NUMCYL(6),PACK+5
         LH    8,DSCB+64          TRKS. PER CYLINDER
         CVD   8,PACK
         MVC   TRKCYL(6),ED6
         ED    TRKCYL(6),PACK+5
         LH    8,DSCB+66          TRK LENGTH
         CVD   8,PACK
         MVC   TRKLN(6),ED6
         ED    TRKLN(6),PACK+5
         SR    8,8
         IC    8,DSCB+74          FULL DSCBS PER TRACK
         CVD   8,PACK
         MVC   DBTRK(6),ED6
         ED    DBTRK(6),PACK+5
         IC    8,DSCB+75          FULL DIRECTORY BLOCKS PER TRACK
         CVD   8,PACK
         MVC   PDSTK(6),ED6
         ED    PDSTK(6),PACK+5
         LH    8,DSCB+56          NO. AVAILABLE ALT TRACKS
         CVD   8,PACK
         MVC   ALTTRK(6),ED6
         ED    ALTTRK(6),PACK+5
         LH    8,DSCB+50          NO. AVAILABLE FORMAT 0 DSCBS
         CVD   8,PACK
         MVC   AVLDSCB(6),ED6
         ED    AVLDSCB(6),PACK+5
         LH    8,DSCB+54          NEXT ALT TRK HH
         CVD   8,PACK
         MVC   NXTALT(6),ED6
         ED    NXTALT(6),PACK+5
         LH    8,DSCB+52          NEXT ALT TRK CC
         CVD   8,PACK
         MVC   NXTALTC(6),ED6
         ED    NXTALTC(6),PACK+5
         MVC   MSG,FMT41
         BAL   10,PUT
         MVC   MSG,FMT42
         B     WRITE
FMT6     MVC   MSG(22),FORMAT6
         BAL   10,PUT
         B     FMTP
FMT5     MVC   MSG(22),FORMAT5    DSNAME LINE
         BAL   10,PUT
         CLC   LINK,=XL5'00'      CHECK FOR FIRST FORMAT 5
         BNE   FMTP1
FMTP     MVC   MSG(44),EXTNTMSG   HEADER
         MVC   MSG+40(32),FREESP
         CLI   DSCBFMT,C'6'
         BNE   FMTPA
         MVC   MSG+40(10),=CL10' '
         MVC   MSG+64(9),=C'DATA SETS'
FMTPA    BAL   10,PUT
         SR    2,2
FMTP1    SR    7,7
         SR    11,11
         LA    8,DSCB+4
NXT5     MVC   WK2,0(8)           RELATIVE BEGINNING TRACK
         LH    9,WK2
         C     8,=A(DSCB+4)       ALWAYS ONE EXTENT
         BE    RLZ5
         LTR   9,9
         BZ    TOTN6              NO MORE EXTENTS
RLZ5     CVD   9,PACK
         MVC   FIRST(6),ED6
         ED    FIRST(6),PACK+5
         MVC   WK2,2(8)           NO. OF FULL CYLINDERS
         LH    10,WK2
         CVD   10,PACK
         MVC   FULLCYL(6),ED6
         ED    FULLCYL(6),PACK+5
         MH    10,CPT
         IC    11,4(8)            EXTRA TRACKS
         CVD   11,PACK
         MVC   EXTRK(6),ED6
         ED    EXTRK(6),PACK+5
         CLI   DSCBFMT,C'6'
         BE    *+6
         AR    10,11
         CVD   10,PACK
         MVC   LENGTH(6),ED6      LENGTH
         ED    LENGTH(6),PACK+5
         AR    2,10
         AR    9,10               LAST TRACK
         BCTR  9,0
         CVD   9,PACK
         MVC   LSTK(6),ED6
         ED    LSTK(6),PACK+5
         CVD   7,PACK             EXTENT NO.
         UNPK  EXTENT(2),PACK
         OI    EXTENT+1,X'F0'
         BAL   10,SAVEXT
         BAL   10,PUT
         LA    7,1(7)
         C     7,=F'8'            SKIP BYTE IN MIDDLE
         BNE   *+8
         LA    8,1(8)
         LA    8,5(8)
         C     7,=F'26'           END OF DSCB
         BNE   NXT5
         B     GOGO
TOTN6    CLI   DSCBFMT,C'6'
         BE    READDSCB
TOT5     CVD   2,PACK             TOTAL FREE SPACE
         MVC   LENGTH(6),ED6
         ED    LENGTH(6),PACK+5
         MVC   LENGTH+10(10),=C'TOTAL TRKS'
         BAL   10,PUT
         LA    10,*+8
MLOP     BC    0,NOLST       SWITCH FOR NOPRINT
         B     READDSCB
SAVEXT   MVC   0(2,5),EXTENT      MOVE EXTENT TO SAVE AREA
         MVC   2(5,5),FIRST+1
         MVC   7(5,5),LSTK+1
         MVC   12(4,5),LENGTH+2
         CLI   DSCBFMT,C'1'       OTHER THAN FMT 1
         BNE   NOTONE
         MVC   16(44,5),DSCB      MOVE IN DSNAME
         B     INDX
NOTONE   CLI   DSCBFMT,C'3'
         BNE   CK4
         LR    1,5
         S     1,=F'60'           GET DSNAME FROM PREVIOUS EXTENT
         MVC   16(44,5),16(1)
         B     INDX
CK4      CLI   DSCBFMT,C'4'
         BNE   CK5
         MVC   16(22,5),=CL22' *** VTOC'
         B     PAD
CK5      CLI   DSCBFMT,C'5'
         BNE   CK6
         MVC   16(22,5),=CL22' *** FREE SPACE'
         B     PAD
CK6      CLI   DSCBFMT,C'6'
         BNE   0(10)
         MVC   16(22,5),FORMAT6
PAD      MVC   38(22,5),=22C' '   PAD WITH BLANKS
INDX     LA    5,60(5)            INCREMENT AREA POINTER
         S     6,=F'60'           REDUCE LENGTH COUNT
         BP    0(10)              RETURN IF OK
         MVC   MSG(50),COREMSG    NOT ENOUGH CORE MSG
         BAL   10,PUT
         ABEND 999,DUMP
NOMORE   CLOSE (GODCB)             NO MORE DSCB'S
         BAL   10,PUT
         L     10,ALLOC
         CVD   10,PACK
         MVC   TOTALLOC,ED6
         ED    TOTALLOC,PACK+5
         L     10,USED
         CVD   10,PACK
         MVC   TOTUSED,ED6
         ED    TOTUSED,PACK+5
         L     10,UNUSED
         CVD   10,PACK
         MVC   TOTUNUS,ED6
         ED    TOTUNUS,PACK+5
         MVC   MSG,TOTLIN         PRINT TOTALS
         BAL   10,PUT
         SR    10,10              ZERO TOTALS
         ST    10,ALLOC
         ST    10,USED
         ST    10,UNUSED
         MVC   MSG(50),SUM        ACCOUNTING SUMMARY MSG
         MVC   MSG+25(30),EXAMESX+7
         BAL   10,PUT
         MVC   MSG(44),EXTNTMSG   HEADER
         MVC   MSG+40(6),DS
         BAL   10,PUT
         MVC   0(60,5),=60X'FF'   DUMMY FINAL ENTRY
         MVC   EXTOT,=PL4'0'      ZERO TOTAL COUNTER
         MVC   HIGH,=CL5'0'       SET HIGH POINTER TO ZERO
         MVC   LOWEST,=5C' '      SET COUNTER TO ZERO
RESTLP   L     5,RECEIVE          SET TO BEGINNING OF LIST
         MVC   WORK+2(5),=5X'FF'  MOVE IN HIGH COMPARE
LOOPCK   CLC   2(5,5),WORK+2      COMPARE FIRST TRACK OF EXTENT
         BL    REPLACE
INCLP    CLC   0(3,5),=3X'FF'     CHECK FOR DUMMY
         BE    EOLIST
         LA    5,60(5)            INCREMENT
         B     LOOPCK
REPLACE  CLC   2(5,5),LOWEST      HAS THIS EXTENT BEEN PRINTED
         BNH   INCLP
         MVC   WORK,0(5)          IF NO, MOVE TO WORK AREA
         B     INCLP
EOLIST   MVC   LOWEST,WORK+2      RESET LOWEST POINTER
         CLC   LOWEST,=5X'FF'     IS DUMMY LOWEST
         BE    FINISLP
         OC    HIGH,=5C'0'        GUARANTEE NUMERIC ZONES
         OC    LOWEST,=5C'0'
         PACK  HIGH,HIGH
         PACK  LOWEST,LOWEST
         AP    HIGH,=PL5'1'       MATCH LAST AND FIRST TRACK
         CP    HIGH,LOWEST
         BE    OKEXT              IF EQUAL, NO PROBLEM
         BL    GAP
         SP    HIGH,LOWEST        COMPUTE OVERLAP
         MVC   ABMES+24(7),=C'OVERLAP'
         B     ABAMT
GAP      SP    LOWEST,HIGH        COMPUTE GAP
         MVC   HIGH,LOWEST
         MVC   ABMES+24(7),=CL7'GAP'
ABAMT    MVC   MSG(50),ABMES
         MVC   LENGTH(6),ED6
         ED    LENGTH(6),HIGH+2   MOVE IN DIFFERENCE
         BAL   10,PUT
OKEXT    MVC   EXTENT(2),WORK     MOVE EXTENT TO OUTPUT AREA
         MVC   FIRST+1(5),WORK+2
         MVC   LSTK+1(5),WORK+7
         MVC   LENGTH+2(4),WORK+12
         OC    WORK+12(4),=C'0000' COMPLETE NUMERIC ZONES
         PACK  WORK+12(4),WORK+12(4)
         AP    EXTOT,WORK+12(4)   KEEP RUNNING TOTAL
         MVC   TYPE(44),WORK+16
         MVC   LOWEST,WORK+2
         MVC   HIGH,WORK+7        SAVE LAST TRACK
         BAL   10,PUT
         B     RESTLP
FINISLP  MVC   LENGTH-2(8),ED8
         ED    LENGTH-2(8),EXTOT  PRINT TOTAL
         MVC   LENGTH+10(10),=C'TOTAL TRKS'
         BAL   10,PUT
         FREEMAIN V,A=RECEIVE     GIVE UP AREA
TESTDD   L     7,SVCNT
         BCT   7,BIGLOOP
         MVC   MSG(26),ENDMSX     INDICATE UTILITY END
         BAL   10,PUT
         CLOSE (PRTDCB)            CLOSE SYSOUT DCB
         L     SAVE,4(SAVE)
        RETURN (14,12),RC=0        & RETURN TO OS..
         SPACE
ERROR    MVC   ERRMES(44),DSCB     GET DSNAME THAT CAUSED THE ERROR
         MVC   ERRCODE(1),VOLIST+13 GET ERROR CODE
         OI    ERRCODE,C'0'        TO EBCDIC
         MVC   MSG,ERRMESX        WRITE ERROR MESSAGE OM SYSOUT
         BAL   10,PUT
         B     READDSCB           GO TO NEXT DSCB
INVLK    MVC   MSG(70),LNKLOOP    BAD LINK FIELD
         BAL   10,PUT
         B     RDB
         EJECT
DCB      EQU   3                   *
DDCNT    EQU   7                   *
SAVE     EQU   13                  *
         SPACE
ADCON    DC    A(SUPRLIST+4095)
FORMAT2  DC    C' *** FORMAT 2 DSCB ***'
FORMAT3  DC    C' *** FORMAT 3 DSCB ***'
FORMAT4  DC    C' *** FORMAT 4 DSCB ***'
FORMAT5  DC    C' *** FORMAT 5 DSCB ***'
FORMAT6  DC    C' *** FORMAT 6 DSCB ***'
STRTMSX  DC    CL55'1',C'--- SUPER-LIST UTILITY ---'
FREESP   DC    C'FREE SPACE   FULL CYL   EXT TRKS'
EXTNTMSG DC    10C' ',C' EXTENT  FIRST  LAST  LENGTH  TYPE'
DELETED  DC    C'****** NOW DELETED *****'
MDLMSG   DC    C'* * * MODEL DSCB * * *'
LNKLOOP  DC    CL70'0********** INVALID LINK FIELD -- LINKS TO SAME DSCX
               B **********'
PDLINE   DC    66C'. '
EXAMESX  DC    CL133'0DSNAME ON VOLUME XXXXXX -- UNIT=XXXX         CREA*
               TED EXPIRES DSORG  RECFM LRECL BLKSIZE SERIAL SEC ALLOCA*
               TION ALLOC   USED  UNUSED'
FMT41  DC  CL133'0NUMBR OF CYL - XXXXX          TRACK LENGTH - XXXXX   *
                      FULL DSCBS PER TRACK - XXXXX          NMBR AVL FM*
               T 0 DSCBS - XXXXX'
FMT42  DC  CL133' TRKS PER CYL - XXXXX          AVL ALT TRKS - XXXXX   *
                      PDS DIR BLKS PER TRK - XXXXX          NEXT ALT TR*
               K (CC-HH) - XXXXX XXX'
ISAM1    DC    CL50' - INDEX LEVELS'
         DC    CL50' - TRKS OF MASTER INDEX'
         DC    CL50' - TRKS OF CYL OVERFLOW'
         DC    CL50' - RECORDS TO BE DELETED'
         DC    CL50' - RANDOM REF TO OVERFLW'
         DC    CL50' - BYTES TO HOLD HIGHEST INDEX'
         DC    CL50' - TRKS OF HIGHEST INDEX'
         DC    CL50' - RECORDS OF PRIME DATA'
         DC    CL50' - BYTES LEFT ON CURRENT OVERFLW TRK'
         DC    CL50' - TRKS LEFT IN OVERFLW AREA'
         DC    CL50' - RCDS IN OVERFLOW AREA'
         DC    CL50' - FULL CYLS OF OVERFLOW AREA'
NUMCYL   EQU   FMT41+15
TRKCYL   EQU   FMT42+15
TRKLN    EQU   FMT41+45
DBTRK    EQU   FMT41+83
PDSTK    EQU   FMT42+83
ALTTRK   EQU   FMT42+45
AVLDSCB  EQU   FMT41+121
NXTALTC  EQU   FMT42+121
NXTALT   EQU   FMT42+125
EXAMES   EQU   EXAMESX+18
EXAMED   EQU   EXAMESX+33
ENDMSX   DC        C'0END OF SUPER-LIST UTILITY'
ERRMESX  DC    CL133' ABNORMAL CONDITION (X). DATA SET--'
ERRMES   EQU   ERRMESX+35
ERRCODE  EQU   ERRMESX+21
         SPACE
CPT      DC    H'0'
SVCNT    DC    F'0'
ALCSAV   DC    F'0'
SYSJFCB  DS    0F
         DC    X'87',AL3(PRTJFCB)
PRTJFCB  DS    44F
MSG      DS    0CL133             OUTPUT LINE
         DC    CL2' '
MSGNAM   DS    CL44
         DC    CL1' '
MSGCREA  DC    CL6' '
         DC    CL2'  '
MSGPURG  DC    CL6' '
         DC    CL1' '
MSGDSTY  DC    CL6' '
         DC    CL2'  '
RECFM    DC    CL4'    '
LRECL    DC    CL6' '
BLKSZ    DC    CL6' '
         DC    CL3'   '
SERIAL   DC    CL6' '
         DC    CL1' '
NUMBER   DC    CL6' '
         DC    CL1' '
TYPSEC   DC    CL6' '
         DC    CL1' '
MSGALLOC DC    CL6' '
         DC    CL1' '
MSGUSED  DC    CL6' '
         DC    CL2'  '
MSGUNUS  DC    CL6' '
         DC    CL20' '
TOTLIN   DC    CL65'0  * * * TOTALS FOR THIS VOLUME * * *'
         DC    CL45' '
TOTALLOC DS    CL6
         DC    CL1' '
TOTUSED  DS    CL6
         DC    CL2'  '
TOTUNUS  DS    CL6
         DC    CL2' '
JFCB     DS    44F                 JFCB AREA
JFVOLSER EQU   JFCB+118
VTOCNAME DC    44X'04'             DSNAME OF A VTOC
         SPACE
VOLIST   DC    H'1'
DEVCODE  DS    CL4
DEVSER   DS    CL6
SEQ      DC    H'0'
DSCB     DS    40F                 DATA SET CONTROL BLOCK
DSCBFMT  EQU   DSCB+44
DSCBCDAT EQU   DSCB+53
DSCBEDAT EQU   DSCB+56
NOEXT    EQU   DSCB+59
DSCBORG  EQU   DSCB+82
DSCBRCFM EQU   DSCB+84
BLKLD    EQU   DSCB+86
LRECLD   EQU   DSCB+88
SECAL    EQU   DSCB+94
LAST     EQU   DSCB+98
EXT1     EQU   DSCB+105
JOBFILE  DS    0F                  DCB EXIT LIST
         DC    X'87',AL3(JFCB)
WK4      DS    0F
         DC    X'00'
WK3      DS    0CL3
         DC    X'00'
WK2      DS    0CL2
         DC    X'00'
WK1      DS    0CL1
         DC    X'00'
LINK     DC    XL5'00'
PACK     DS    D
LASTF1   DC    H'0'
PARMSAV  DC    42C' '
PARM     EQU   PARMSAV+2
LSTPARM  DC    C'Y'
ED6      DC    X'402020202120'
ED8      DC    X'4020202020202120'
SAVARA   DS    18F
RECAD    DC    A(RECFM+5)
FULLCYL  EQU   MSG+53
EXTRK    EQU   MSG+63
AMT      EQU   MSG+8
EXTENT   EQU   MSG+13
FIRST    EQU   MSG+17
LSTK     EQU   MSG+24
LENGTH   EQU   MSG+31
TYPE     EQU   MSG+40
ALLOC    DC    F'0'
USED     DC    F'0'
UNUSED   DC    F'0'
HIGH     DS    CL5
EXTOT    DC    PL4'0000'
ABMES    DC    CL50' *** ABNORMAL CONDITION               ***'
COREMSG  DC    CL50' PROGRAM NEEDS MORE CORE TO SORT EXTENTS'
SUM      DC    CL50'0SPACE ACCOUNTING SUMMARY'
DS       DC    C'DSNAME'
LOWEST   DC    C'00000'
WORK     DS    CL60
         DS    0F
OBTAIN   DC    X'C0800000'        SEEK CODE
         DC    A(DSCB+135)        CCHHR
         DC    A(JFVOLSER)        VOLUME SERIAL
         DC    A(DSCB)            DSCB AREA
         DS    0D
ASK      DC    F'60'
         DC    F'600000'
RECEIVE  DS    D
CAMIT   CAMLST SCRATCH,DSCB,,VOLIST,,OVRD
         SPACE 2
         LTORG
         SPACE  2
PRTDCB   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,EROPT=ACC,            X
               LRECL=133,BLKSIZE=133,RECFM=FA,EXLST=SYSJFCB
GODCB    DCB   DSORG=PS,MACRF=R,DDNAME=DD,DEVD=DA,KEYLEN=44,LRECL=96,  X
               BLKSIZE=96,RECFM=FS,EODAD=NOMORE,EXLST=JOBFILE
         DCBD  DSORG=PS,DEVD=DA
SUPRLIST CSECT
         SPACE
         END
