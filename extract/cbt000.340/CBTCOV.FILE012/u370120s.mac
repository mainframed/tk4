CLISXREF TITLE 'REQUIRED WSCC MACROS '
         MACRO
&NAME    BEGIN &BASE,&ENTRY,&BRANCH,&SAVADR=,&ID=YES,&REGS=YES,        -
               &WORKLTH=0,&WORKNME=,&RENT=NO
.*
.*       THIS IS A RE-WRITE OF THE EXSISTING 'BEGIN' MACRO AND
.*       A REPLACEMENT FOR THE 'MBEGIN' MACRO.  AN
.*       ADDITIONAL RE-ENTRANT FEATURE HAS ALSO BEEN ADDED.
.*
.*       WRITTEN BY CHRIS HUTCHINS, MAY 1974.
.*
.*       WEST SUSSEX COUNTY COUNCIL SYSTEMS PROGRAMMING GROUP.
.*
         GBLB  &CHK,&RSW
         GBLC  &SNAME
         LCLB  &ENTSW
         LCLC  &BS1,&BS2,&BS3,&BS4
         LCLC  &EN1,&EN2,&EN3,&EN4
         LCLC  &BR1,&BR2,&BR3,&BR4
         LCLC  &S
&S       SETC  '&SYSNDX'
         AIF   ('&REGS' EQ 'NO' OR &CHK).NOREGS
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
&CHK     SETB  1
.NOREGS  ANOP
         AIF   ('&NAME' EQ '').NAMESKP
&NAME    DS    0H
.NAMESKP AIF   ('&ENTRY' EQ '' OR '&ENTRY(2)' EQ '').U15
&EN1     SETC  '&ENTRY(1)'
&EN2     SETC  '&ENTRY(2)'
&EN3     SETC  '&ENTRY(3)'
&EN4     SETC  '&ENTRY(4)'
         AIF   ('&EN1' NE '&SYSECT').GENENTS 1ST "ENTRY" = CSECT NAME?
&EN1     SETC  '        '                     YES. BLANK NAME FOR NOPR
         AGO   .GENENT2                        & BYPASS "ENTRY" STMNT.
.GENENTS ENTRY &EN1
.GENENT2 ENTRY &EN2
         AIF   ('&EN3' EQ '').ENT2
         ENTRY &EN3
         AIF   ('&EN4' EQ '').ENT2
         ENTRY &EN4
.ENT2    ANOP
&EN1     NOPR  0
&EN2     NOPR  0
         AIF   ('&EN3' EQ '').NOPENT
&EN3     NOPR  0
         AIF   ('&EN4' EQ '').NOPENT
&EN4     NOPR  0
.NOPENT  ANOP
         AGO   .STM
.U15     USING *,15
.STM     STM   14,12,12(13)        SAVE REGISTERS
         AIF   ('&BASE(1)' NE '').BSPEC
&BS1     SETC  '12'
         AGO   .RENTCK
.BSPEC   ANOP
&BS1     SETC  '&BASE(1)'
.RENTCK  AIF   ('&RENT' EQ 'YES').RENTNT
         AIF   ('&RENT' EQ 'NO').NOERR3
         MNOTE 8,'RENT VALUE INVALID - RENT=NO ASSUMED'
.NOERR3  ANOP
&RSW     SETB  0
         AIF   ('&WORKLTH' EQ '' OR '&WORKLTH' EQ '0').NOERR1
         MNOTE 4,'WORKLTH INVALID WHEN RENT=NO - IGNORED'
.NOERR1  AIF   ('&WORKNME' EQ '').NOERR2
         MNOTE 4,'WORKNME INVALID WHEN RENT=NO - IGNORED'
.NOERR2  ANOP
         AIF   ('&ENTRY' EQ '' OR '&ENTRY(1)' EQ '').NOBALR
         BALR  &BS1,0
         USING *,&BS1
.NOBALR  AIF   ('&ID' EQ 'NO' OR '&SYSECT' EQ '').NOID1
         B     *+12
         DC    CL8'&SYSECT'
.NOID1   ANOP
         CNOP  0,4
         BAL   &BS1,IHB&S.B
IHB&S    EQU   *
         USING *,&BS1
&SAVADR  DC    18F'0'
IHB&S.B  ST    &BS1,8(13)          CHAIN
         ST    13,4(&BS1)                SAVE
         LR    13,&BS1                        AREA
         AGO   .MBASE
.*
.RENTNT  ANOP
&RSW     SETB  1
         AIF   ('&SAVADR' EQ '').NOSAVA
         MNOTE 4,'SAVADR INVALID WHEN RENT=YES - IGNORED'
.NOSAVA  ANOP
         BALR  &BS1,0
IHB&S    EQU   *
         USING *,&BS1
         AIF   ('&ID' EQ 'NO' OR '&SYSECT' EQ '').NOID2
         B     *+12
         DC    CL8'&SYSECT'
.NOID2   ANOP
         BAL   1,*+8
IHB&S.S  DC    AL4((((72+&WORKLTH)+7)/8)*8)
         ICM   0,B'1111',IHB&S.S
         SVC   10
&SNAME   SETC  'IHB&S.S'
         XC    0(72,1),0(1)        CLEAR SAVE AREA
         ST    1,8(13)             CHAIN
         ST    13,4(1)                   SAVE
         LR    13,1                           AREA
         L     1,4(13)             RESTORE
         L     1,24(1)                     REGISTER 1
         AIF   ('&WORKNME' EQ '').MBASE
         USING &WORKNME-72,13      USING FOR 'WORKNME' DSECT
.*
.MBASE   AIF   ('&BASE' EQ '' OR '&BASE(2)' EQ '').MENTS
&BS2     SETC  '&BASE(2)'
&BS3     SETC  '&BASE(3)'
&BS4     SETC  '&BASE(4)'
         AIF   ('&BS4' EQ '').BS3CK
         AIF   ('&BS4' EQ '&BS3' OR '&BS4' EQ '&BS2' OR '&BS4' EQ '&BS1-
               ').FAIL1
.BS3CK   AIF   ('&BS3' EQ '').BS2CK
         AIF   ('&BS3' EQ '&BS2' OR '&BS3' EQ '&BS1').FAIL1
.BS2CK   AIF   ('&BS2' EQ '').MENTS
         AIF   ('&BS2' EQ '&BS1').FAIL1
         AIF   ('&BS4' EQ '').BS3U
         USING IHB&S+4096,&BS2,&BS3,&BS4
         AGO   .BASET
.BS3U    AIF   ('&BS3' EQ '').BS2U
         USING IHB&S+4096,&BS2,&BS3
         AGO   .BASET
.BS2U    USING IHB&S+4096,&BS2
.BASET   LR    &BS2,&BS1
         AH    &BS2,IHB&S.K
         AIF   ('&BS3' EQ '').KSET
         LR    &BS3,&BS2
         AH    &BS3,IHB&S.K
         AIF   ('&BS4' EQ '').KSET
         LR    &BS4,&BS3
         AH    &BS4,IHB&S.K
.KSET    B     *+6
IHB&S.K  DC    H'4096'
.*
.MENTS   AIF   ('&BRANCH' EQ '').END
&BR1     SETC  '&BRANCH(1)'
&BR2     SETC  '&BRANCH(2)'
&BR3     SETC  '&BRANCH(3)'
&BR4     SETC  '&BRANCH(4)'
         AIF   ('&BR1' NE '' AND '&EN1' EQ '').FAIL2
         AIF   ('&BR2' NE '' AND '&EN2' EQ '').FAIL2
         AIF   ('&BR3' NE '' AND '&EN3' EQ '').FAIL2
         AIF   ('&BR4' NE '' AND '&EN4' EQ '').FAIL2
&ENTSW   SETB  1
         AIF   (NOT &RSW).BRTAB
         L     15,4(13)
         L     15,16(15)
.BRTAB   SLL   15,29               GET LAST 3 BITS OF ENTRY ADDRESS
         SRL   15,28               (IE 0 2 4 OR 6) X 2
         B     *+4(15)
         B     &BR1
         B     &BR2
         AIF   ('&BR3' EQ '').BRK
         B     &BR3
         AIF   ('&BR4' EQ '').BRK
         B     &BR4
.BRK     ANOP
         AGO   .END
.*
.*       ERROR MESSAGES
.*
.FAIL1   MNOTE 12,'DUPLICATE BASE REGISTERS SPECIFIED'
.FAIL999 MNOTE *,'MACRO GENERATION TERMINATED'
         MEXIT
.FAIL2   MNOTE 12,'BRANCH NUMBERS DO NOT AGREE WITH ENTRY NUMBERS'
         AGO   .FAIL999
.END     SPACE
         MEND
         EJECT
         MACRO
&NAME    FINISH &RC=0
         GBLB  &RSW
         GBLC  &SNAME
         AIF   (NOT &RSW).CONT
&NAME    LA    1,0(13)
         L     13,4(13)
         ICM   0,B'1111',&SNAME
         SVC   10
         AGO   .RCHK
.CONT    ANOP
&NAME    L     13,4(13)
.RCHK    AIF   ('&RC' EQ '(15)').REG15
         AIF   ('&RC'(1,1) EQ '(').REGLOAD
         AIF   ('&RC' EQ 'NO').NORC
         AIF   (T'&RC NE 'N').ERR
         AIF   ('&RC' EQ '0').SLR
         LA    15,&RC
         AGO   .REG15
.SLR     SLR   15,15 .             SET ZERO RETURN CODE
.REG15   L     14,12(13)
         LM    0,12,20(13)
         AGO   .OUT
.NORC    LM    14,12,12(13)
.OUT     BR    14
         MEXIT
.REGLOAD LR    15,&RC(1)
         AGO   .REG15
.ERR     MNOTE 8,'INVALID RETURN CODE VALUE - IGNORED'
         AGO   .NORC
         MEND
         EJECT
         MACRO
&NAME    DCBPFA &SYNAD=1,&BLKSIZE=665,&LRECL=133
         SPACE 2
&NAME    DCB   DDNAME=&NAME,SYNAD=&SYNAD,BLKSIZE=&BLKSIZE,             X
               LRECL=&LRECL,DSORG=PS,MACRF=PM,RECFM=FBA
         MEND
         SPACE 6
         MACRO
&NAME    DCBGF &SYNAD=1,&EODAD=,&LRECL=80,&BLKSIZE=0
         SPACE 2
&NAME    DCB   DDNAME=&NAME,SYNAD=&SYNAD,EODAD=&EODAD,                 X
               LRECL=&LRECL,BLKSIZE=&BLKSIZE,                          X
               DSORG=PS,MACRF=GL,RECFM=FB
         MEND
         EJECT
         MACRO
&NAME    PLINE &DCB,&AREA,&RETURN,&LEN=133
         LCLC  &B
         LCLA  &A
&A       SETA  &LEN-1
         AIF   (T'&RETURN  NE 'N').P0101
         AIF   ('&AREA' EQ '').LOCATE
         AIF   ('&AREA'(1,1)  NE  '(').GOON
&B       SETC  '0'
.GOON    ANOP
&NAME    PUT   &DCB,&AREA
         AGO   .CLEAR
.LOCATE  ANOP
&NAME    PUT   &DCB
         AGO   .OUT
.CLEAR   MVI   &B.&AREA,X'40'
         AIF   ('&B' EQ '0').REG
         MVC   &AREA+1(&A),&AREA
         AGO   .OUT
.REG     MVC   1(&A.,&AREA),&B.&AREA
.OUT     BR    &RETURN
         MEXIT
.P0101   MNOTE  '12,RETURN REGISTER INCORRECT'
         MEND
         TITLE 'WSCC CROSS-REFERENCE PROGRAM FOR CLISTS'
CLISXREF CSECT
         SPACE 3
*  SAMPLE J.C.L. FOR EXECUTION
*
*     //CLISTEST JOB  ST000802,STONE,MSGCLASS=T
*     //  EXEC  ASMFCG
*     //GO.SYSUDUMP DD SYSOUT=T
*     //GO.SYSPRINT DD SYSOUT=T
*     //GO.CLIST DD  DATA,DLM=$$
         SPACE 3
         BEGIN
         OPEN  (CLIST,,SYSPRINT,OUTPUT)
         SPACE
         GETMAIN R,LV=80016        ENOUGH FOR 5000 ENTRIES (+1)
*
         ST    R1,AREA
         ST    R1,NEXTTAB
         A     R1,F80000
         ST    R1,AREATOP
         SPACE 2
         GET   CLIST                FIRST RECORD NOW
         CLC   72(8,R1),SPACES      IS CLIST SEQUENCE-NUMBERED?
         BNE   NEXTGOT               YES, OK
         MVC   PAREA+1(46),ERRMESS1  NO; NOTHING WE CAN DO
         PUT   SYSPRINT,PAREA
         B     FINISH
         SPACE
NEXTGOT  LR    R11,R1
         LA    R10,72               INPUT LENGTH OF RECORD
         STH   R10,PASSEDL+2         & PLACE INTO PARAM.LIST
         MVC   SEQNUM,72(R11)       SAVE THE LINE'S SEQUENCE NUMBER
         ST    R11,PASSEDA          ADDR.1ST DATA BYTE
         STM   R10,R11,SAVEINLA     SAVE INPUT (TEXT) LENGTH & POINTER
         EJECT
*   THE PARAMETER LIST FOR PARSING IS IN THE FOLLOWING FORM:
*     R1 -->  5 CONTIGUOUS FULLWORDS
*       (A) THE ADDRESS OF ALL THE PASSED INFORMATION - SEE BELOW
*       (B) THE ADDRESS OF A HALF-WORD FOR RETURNING THE ELEMENT LENGTH
*       (C) THE ADDRESS OF AN AREA IN WHICH THE ELEMENT WILL BE PLACED
*       (D) THE ADDRESS OF A BYTE IN WHICH THE TERMINATOR WILL BE PUT
*       (E) THE ADDRESS OF A HALF-WORD WHICH WILL HAVE THE NO.OF CHARS.
*             DEALT WITH BY "WSPARSE"
*
*     THE PASSED INFORMATION:-
*       1. A FULLWORD, THE LENGTH OF THE (RESIDUAL) STRING
*       2. A FULLWORD, THE ADDRESS OF THE NEXT STRING BYTE
*       3. A FULLWORD, THE LENGTH OF "START EXCLUSIONS TABLE" - BELOW
*       4. A FULLWORD, THE ADDRESS OF    ---   "   ---
*       5. A FULLWORD, THE LENGTH OF "TERMINATORS TABLE" - BELOW
*       6. A FULLWORD, THE ADDRESS OF    ---   "   ---
*
*    THE TABLES PROVIDE INFORMATION ON CHARACTERS WHICH ARE TO BE
*      BYPASSED AS INSIGNIFICANT BEFORE START-OF-ELEMENT, AND THOSE
*      WHICH ARE TO BE RECOGNISED AS TERMINATING AN ELEMENT.
*       BYTE CONTENTS;
*        1ST: BIT FLAGS -  1... .... = SPACE CHARACTERS
*                          .1.. .... = ANY ALPHA CHARACTER
*                          ..1. .... = ANY NUMERIC CHARACTER
*        2ND AND SUBSEQUENT BYTES SHOULD CONTAIN THE ACTUAL CHARACTER
*          WHICH IS RELEVANT, FOR "BYPASS" OR "TERMINATE", PURPOSES -
*          ACCORDING TO WHICH TABLE THIS IS.
*
         CNOP  0,4
LOOP     CALL  WSPARSE,(PASSEDL,RETLEN,DATA,TERM,CHDONE)
         SPACE
         MVI   BYPTAB,X'80'      RESET BYP. FLAG CODE TO 'SPACES' ONLY
         MVI   BYPTAB+1,C' '      & WIPE ANY ":" PLACED IN
*
         CH    R15,H16           NONSENSE FOUND WHILE PARSING?
         BE    CRASH
         STH   R15,SAVERC        SAVE R.C. FROM "WSPARSE"
         SPACE
QANYDATA OC    RETLEN,RETLEN     ANY ELEMENT DATA FOUND?
         BNZ   TSTALT            YES, GO
         TM    COMSW,X'80'       FOUND / PREVIOUSLY?
         BZ    TSTSLASH          NO
         CLI   TERM,C'*'         START COMMENT STRING /*
         BE    DISPLAY           YES - PRINT REST
         MVI   COMSW,X'00'       NO - RESET / FOUND SW
         B     UPTEXT            CONTINUE
TSTSLASH CLI   TERM,C'/'
         BNE   UPTEXT            NO  - CONTINUE
         OI    COMSW,X'80'       YES - MAYBE START COMMENT STRING
         B     UPTEXT            CONTINUE
         SPACE
TSTALT   TM    CENDSW,X'01'      LOOKING FOR ALT. END SEQUENCE
         BO    YESEND2           YES - THEN PROCESS IT
         SPACE
*  SCAN LIST OF WORDS TO BE EXCLUDED FROM X-REF
*
         LA    R10,OUTWORDS
STOPLOOP LH    R1,0(R10)         LENGTH OF NEXT OUT-WORD
         LTR   R1,R1             ANY MORE OUT-WORDS?
         BZ    TAKEIT             NO, MUST BE VALID TEXT ELEMENT
         BCTR  R1,0               YES; LENGTH FOR EXECUTE
         EX    R1,TRYSTOPS       CLC  DATA(0),2(R10)
         BE    OUTFOUND          OUT-WORD MATCH, GO
STOPLP2  LA    R10,3(R1,R10)     MOVE UP OUT-WORD LIST
         B     STOPLOOP
*
*  OUT-WORD MATCH FOUND - BUT CHECK ELEMENT LENGTH IS OUT-WORD LENGTH
*            BEFORE DISCARDING THIS
*
OUTFOUND CLC   0(2,R10),RETLEN
         BNE   STOPLP2           IF NOT =, NOT A GENUINE OUT-WORD
         SPACE 3
*  FOUND A WORD TO BE EXCLUDED
*
         CLC   DATA(2),=C'DO'    HAVE WE A "DO" OR "END" TO HANDLE?
         BE    DODO
         TM    CENDSW,X'80'      ALT. END STRING FOUND
         BZ    OUTEND1           NO - THEN CHECK FOR END
         EX    R1,CLCEND         IS THIS THE END STATEMENT
         BE    YESEND            ALT. END STRING
         B     OUTSUB1           ELSE CHECK FOR SUBSTRING
OUTEND1  CLC   DATA(3),=C'END'   "END" ?
         BE    YESEND
OUTSUB1  CLC   DATA(6),CSUBSTR+2   HAVE WE FOUND "SUBSTR"?
         BE    SETBYPN              YES, SET TO BYPASS NUMERICS
         B     UPTEXT
DODO     LH    R15,DOCOUNT       COUNT CURRENT DO LOOPS
         LA    R15,1(R15)
         STH   R15,DOCOUNT
         L     R1,DOINDEX
         CH    R15,=H'10'        MAX NO DO LOOPS TO PRT
         BH    *+8               YES - DONT ADJUST POINTER
         SH    R1,H2             NEW "DO"; MOVE BACK IN 'DOPATERN'
         MVC   0(2,R1),=C'->'     & START NEW BRACKET
         OI    DOFLAG,X'80'
         B     DOENPOST
YESEND   DS    0H                END STRING PROCESSING
         TM    CENDSW,X'80'      FOUND AN ALTERNATE END STRING
         BO    YESENDX           YES - MUST BE GENUINE END
         CLC   RETLEN,=H'3'       MAKE SURE END
         BNE   YESENDX
         CLC   DATA(3),=C'END'
         BNE   YESENDX
         CLI   TERM,C'('         END( ?
         BNE   YESENDX           NOPE - NOT ALT. END STRING
         OI    CENDSW,X'01'      SET SW - FIND ALT. END STRING
         B     UPTEXT            GO FIND IT
YESEND2  DS    0H                RETURN FORM PARSE ALT. END STRING
         CLI   TERM,C')'
         BNE   YESEND3           WHOOPS
         LH    R1,RETLEN
         CH    R1,=H'4'          MAX END STRING LENGTH
         BH    YESEND3           DUFF
         LTR   R1,R1
         BZ    YESEND4           END()
         BCTR  R1,0
         EX    R1,ENDMOVE2       MVC ENDSTRG+2(0),DATA - NEW END IN TAB
         LA    R1,1(R1)          RESTORE LENGTH
         STCM  R1,B'0011',ENDSTRG  OUTWORDS TABLE
         OI    CENDSW,X'80'      FOUND ALT. END STRING
YESEND4  NI    CENDSW,255-X'01'  RESET SW
         B     UPTEXT
YESEND3  ABEND 1,DUMP            INVALID ALT END STRING
YESENDX  LH    R15,DOCOUNT       COUNT CURRENT DO LOOPS
         LTR   R15,R15           IS THIS END WITHIN DO LOOP
         BZ    UPTEXT            NO - THEN IGNORE IT
         BCTR  R15,0
         STH   R15,DOCOUNT       SAVE NEW COUNT
         L     R1,DOINDEX
         MVC   0(2,R1),=C'<-'    "END"; STOP CURRENT BRACKET
         CH    R15,=H'10'        MAX COUNT
         BNL   *+8               YES - DONT ADJUST POINTER
         LA    R1,2(R1)
         OI    DOFLAG,X'40'
DOENPOST ST    R1,DOINDEX
         B     UPTEXT
*
SETBYPN  OI    BYPTAB,X'20'      &SUBSTR FOUND - BYPASS NEXT NUMERICS
         MVI   BYPTAB+1,C':'      AND THE COLON
         B     UPTEXT
         SPACE 3
*  TEXT ELEMENT TO BE TAKEN FOR CROSS-REFERENCING
*
TAKEIT   DS    0H
         L     R5,NEXTTAB        NEXT AVAILABLE TABLE LOCN.
         MVC   0(8,R5),SPACES    PREPARE TO MOVE ELEMENT
         LH    R4,RETLEN         ELEMENT LENGTH
         CH    R4,=H'8'
         BNH   *+8
         LA    R4,8              MAX ELEMENT LENGTH FOR XREF
         BCTR  R4,0
         EX    R4,MOVEITEM       MVC   0(0,R5),DATA:  ELEMENT TO TABLE,
         MVC   8(8,R5),SEQNUM        & ITS SEQUENCE-NUMBER
         LA    R5,16(R5)         UP OUR TABLE AREA
         C     R5,AREATOP        STILL ROOM FOR MORE?
         BNH   MOREROOM
         MVC   PAREA(44),ERRMESS2 NO, GIVE UP READING INPUT
         PUT   SYSPRINT,PAREA
         B     NMERGE             GO DO SORT AS MUCH AS WE GOT
         SPACE
MOREROOM ST    R5,NEXTTAB
*
UPTEXT   L     R6,PASSEDL         PICK UP RESIDUAL LENGTH OF TEXT
         SH    R6,CHDONE           REDUCE BY CHARS.HANDLED
         ST    R6,PASSEDL          & PUT FOR NEXT USE
         L     R6,PASSEDA         PICK UP POINTER TO TEXT
         AH    R6,CHDONE           & MOVE UP BY CHARS.DONE
         ST    R6,PASSEDA
         MVI   DATA,C' '    CLEAR DATA
         MVC   DATA+1(127),DATA
         OC    SAVERC,SAVERC      DID "WSPARSE" FIND END-OF-TEXT?
         BZ    LOOP                NO, NEXT ELEMENT
*
*   PRINT ROUTINE FOR THE INPUT CLIST LINES
*
DISPLAY  CP    LINCNT,KP55
         BL    PLINE
         PUT   SYSPRINT,HEAD1
         PUT   SYSPRINT,SUBHEAD1
         MVI   PAREA,C'0'
         SP    LINCNT,LINCNT
*
PLINE    MVC   PAREA+84(8),SEQNUM SEQUENCE NO.TO PRINT
         LM    R10,R11,SAVEINLA  PICK UP INREC LENGTH & POINTER
NXFIT    CH    R10,H100         WILL (RESIDUE OF) DATA FIT PRINT LINE?
         BL    LINEFITS         ALWAYS TRUE WITH FIXED 80 BYTE RECS
         MVC   PAREA+12(100),0(R11) NO, PUT WHAT WE CAN
         MVC   PAREA+112(20),DOPATERN
         AP    LINCNT,KP1
         BAL   R3,PRINE
         BAL   R3,DOLTIDY
         SH    R10,H100
         LA    R11,100(R11)
         B     NXFIT
LINEFITS BCTR  R10,0
         EX    R10,MOVETEXT     MVC PAREA+9(0),0(R11)
         MVC   PAREA+112(20),DOPATERN
         AP    LINCNT,KP1
         BAL   R3,PRINE
         BAL   R3,DOLTIDY
         SPACE 4
GETNEXT  GET   CLIST              NEXT INPUT RECORD
         MVI   COMSW,X'00'        RESET COMMENT SW
         B     NEXTGOT
         SPACE 4
*   PREPARE FOR X-REF LISTING.  SORT THE TABLE ENTRIES
*
NMERGE   CLC   AREA,NEXTTAB       MAKE SURE WE HAVE SOME
         BE    FINISH
         ST    R5,TABLETOP        SET UP SORT PARMS.
         S     R5,AREA
         SRA   R5,4               CALC.NO.OF ENTRIES
         STH   R5,NOOFENTS
         MVC   SORT+24(4),AREA    START ADDR.OF TABLE
         CNOP  0,4
SORT     CALL  WSSORT,(CAN,H16,H16,0,NOOFENTS,H0,RET)
         SPACE
         CLI   RET,C'0'
         BE    PXREF
         ABEND 12,DUMP
         SPACE 4
*   ALL ENTRIES NOW SORTED - START X-REF LISTING
*
PXREF    BAL   R4,PSUBA
         SPACE
         L     R5,AREA
PLOOP    LA    R9,PAREA+12
         MVC   NAME(8),0(R5)      HOLD CURRENT SYMBOL
         MVC   PAREA+1(8),0(R5)
PLOOPA   MVC   0(8,R9),8(R5)      STMNT.NUMBER TO NEXT PRINT POSN.
         LA    R9,10(R9)          MOVE UP PRINT AREA
         C     R9,=A(PAREA+129)    STILL SPACE IN PRINT LINE?
         BL    SAMELINE             YES
         BAL   R4,PSUB              NO, GO PRINT
         LA    R9,PAREA+12
SAMELINE LA    R5,16(R5)          MOVE UP SORTED TABLE
         C     R5,TABLETOP
         BH    FINISH             END OF ALL SYMBOLS?
         CLC   0(8,R5),NAME        NO.  SAME SYMBOL AS PREVIOUS?
         BE    PLOOPA                    YES, LOOP IN THIS PRINT LINE
         CLI   PAREA+1,X'40'             NO. MAIN LINE JUST DONE?
         BNE   GOPUTP
         CLI   PAREA+12,X'40'              OR CONTN.LINE?
         BE    PLOOP
GOPUTP   BAL   R4,PSUB
         B     PLOOP
         SPACE 5
CRASH    ABEND 16,DUMP
         SPACE 5
FINISH   CLOSE (CLIST,,SYSPRINT)
         FINISH
         EJECT
*   SUBROUTINE FOR PRINTING LINES OF X-REF TABLE
*
PSUB     BAL   R3,PRINE
         AP    LINCNT,KP1
         CP    LINCNT,KP55
         BL    PSEXIT
PSUBA    SP    LINCNT,LINCNT
         PUT   SYSPRINT,HEAD1
         PUT   SYSPRINT,SUBHEAD2
         SPACE
         MVI   PAREA,C'0'
PSEXIT   BR    R4
         SPACE 4
PRINE    PLINE SYSPRINT,PAREA,3
         SPACE 4
DOLTIDY  L     R1,DOINDEX
         TM    DOFLAG,X'80'       "DO" BRACKET JUST STARTED?
         BNO   OFFDO
DOLVERT  MVC   0(2,R1),=C' ×'      YES; VERT.BAR TO PRINT POSNS.
         B     DOLBACK
OFFDO    LH    R15,DOCOUNT
         CH    R15,=H'10'        MAX COUNT
         BNL   DOLVERT           YES - REESTABLISH VERT BAR
         SH    R1,H2               NO, "END"; WIPE PRINT POSNS.
         MVC   0(2,R1),SPACES
DOLBACK  MVI   DOFLAG,X'00'
         BR    R3
         EJECT
*  DATA USED BY SUBROUTINE "WSPARSE"
*
RETLEN   DC    H'0'              LENGTH OF ELEMENT FOUND
DATA     DC    CL128' '          ELEMENT RETURNED
TERM     DC    C'*'              TERMINATOR FOUND
CHDONE   DC    H'0'              NO.OF CHARS.HANDLED BY PARSE S/R
         SPACE 2
PASSEDL  DC    F'0'              LENGTH OF TEXT STRING PASSED
PASSEDA  DC    A(0)              POINTER TO TEXT STRING PASSED
         DC    F'6'              LENGTH OF 'BYPASS' TABLE
         DC    A(BYPTAB)         POINTER TO 'BYPASS' TABLE
         DC    F'18'             LENGTH OF 'TERMINATOR' TABLE
         DC    A(TERMTAB)        POINTER TO 'TERMINATOR TABLE
         SPACE 3
BYPTAB   DC    X'80',C' &&(''"'  BYPASS TABLE - NOTE: INITIAL SPACE
*                                     LEFT FOR INSERTING ":" ON &SUBSTR
*
TERMTAB  DC    X'80',C'&&''(=),.":^×<>+-/*'  TERMINATOR TABLE
         EJECT
*   OTHER CONSTANTS AND AREAS
*
AREA     DC    A(0)
AREATOP  DC    A(0)
NEXTTAB  DC    A(0)
TABLETOP DC    A(0)
SAVEINLA DC    2A(0)
F80000   DC    F'80000'
SEQNUM   DC    CL8' '
NAME     DC    CL8' '
SAVERC   DC    H'0'
NOOFENTS DC    H'0'
H100     DC    H'100'
H16      DC    H'16'
H12      DC    H'12'
H2       DC    H'2'
H0       DC    H'0'
MOVEITEM MVC   0(0,R5),DATA
MOVETEXT MVC   PAREA+9(0),0(R11)
TRYSTOPS CLC   DATA(0),2(R10)
CLCEND   CLC   ENDSTRG+2(0),DATA
ENDMOVE2 MVC   ENDSTRG+2(0),DATA
CENDSW   DC    X'00'
COMSW    DC    X'00'
LINCNT   DC    PL2'55'
KP1      DC    P'1'
KP55     DC    P'55'
PAREA    DC    CL133' '
SPACES   DC    8C' '
ERRMESS1 DC    C'CLIST IS NOT SEQUENCE-NUMBERED.  ABANDONED RUN'
ERRMESS2 DC    C'-MORE THAN 5000 SYMBOLS.  INPUT STOPPED HERE'
HEAD1    DC    CL133'1        LISTING OF A CLIST, WITH CROSS-REFERENCE'
SUBHEAD1 DC    CL133'-'
         ORG   SUBHEAD1+19
         DC    C'STATEMENT BODY'
         ORG   SUBHEAD1+84
         DC    C'SEQ.NUM.'
         ORG
SUBHEAD2 DC    CL133'-  SYMBOL   REFERENCES'
CAN      DC    C'CAN'
RET      DC    X'00'
         SPACE 2
*  AREA FOR BRACKETTING "DO" - "END" CODE;  UP TO 10 X 2-BYTE
*     FIELDS, AT RIGHT OF PRINT AREA, SET & CLEARED AS WE ENCOUNTER
*     THE RELEVANT KEYWORDS
*
DOPATERN DC    10CL2' '
DOINDEX  DC    A(DOPATERN+20)
DOFLAG   DC    X'00'
DOCOUNT  DC    H'0'            COUNT CURRENT DO LOOPS
         EJECT
*   TABLE OF WORDS WHICH ARE TO BE IGNORED IF FOUND IN PARSING
*
OUTWORDS DC    YL2(2),C'EQ'
         DC    YL2(2),C'NE'
         DC    YL2(2),C'LT'
         DC    YL2(2),C'GT'
         DC    YL2(2),C'LE'
         DC    YL2(2),C'GE'
         DC    YL2(2),C'NG'
         DC    YL2(2),C'NL'
         DC    YL2(2),C'IF'
         DC    YL2(2),C'OR'
         DC    YL2(2),C'DO'
         DC    YL2(3),C'SET'
         DC    YL2(3),C'AND'
         DC    YL2(4),C'THEN'
         DC    YL2(4),C'GOTO'
         DC    YL2(8),C'DATATYPE'
         DC    YL2(4),C'EVAL'
         DC    YL2(6),C'LENGTH'
         DC    YL2(3),C'STR'
CSUBSTR  DC    YL2(6),C'SUBSTR'
ENDSTRG  DC    YL2(3),C'END'    *** MUST BE LAST ENTRY     ****
         DC    8X'00'           *** END OF STOPWORDS TABLE ****
         EJECT
         LTORG
SYSPRINT DCBPFA
CLIST    DCBGF EODAD=NMERGE,LRECL=80,BLKSIZE=3120
         TITLE 'WSCC SUBROUTINE TO PARSE ANY DATA STRING'
WSPARSE  CSECT
         SPACE 3
*   THIS SUBROUTINE WILL RETURN THE NEXT ELEMENT PARSED OUT OF ANY
*      DATA STRING PASSED TO IT.  THE USER PRESCRIBES, BY MEANS OF
*      TABLES, WHICH(IF ANY) CHARACTERS ARE TO BE BYPASSED BEFORE
*      THE START OF A SIGNIFICANT ELEMENT, AND WHICH CHARACTERS ARE
*      TO BE REGARDED AS TERMINATING AN ELEMENT.
*
*   THE PARAMETER LIST RECEIVED MUST BE IN THE FOLLOWING FORM:
*     R1 -->  5 CONTIGUOUS FULLWORDS
*       (A) THE ADDRESS OF ALL THE PASSED INFORMATION - SEE BELOW
*       (B) THE ADDRESS OF A HALF-WORD FOR RETURNING THE ELEMENT LENGTH
*       (C) THE ADDRESS OF AN AREA IN WHICH THE ELEMENT WILL BE PLACED
*       (D) THE ADDRESS OF A BYTE IN WHICH THE TERMINATOR WILL BE PUT
*       (E) THE ADDRESS OF A HALF-WORD FOR RETURNING THE NO.OF CHARS.
*              WE HAVE DEALT WITH (UP TO & INCLUDING THE TERMINATOR)
*
*     THE PASSED INFORMATION:-
*       1. A FULLWORD, THE LENGTH OF THE (RESIDUAL) STRING
*       2. A FULLWORD, THE ADDRESS OF THE NEXT STRING BYTE
*       3. A FULLWORD, THE LENGTH OF "START EXCLUSIONS TABLE" - BELOW
*       4. A FULLWORD, THE ADDRESS OF    ---   "   ---
*       5. A FULLWORD, THE LENGTH OF "TERMINATORS TABLE" - BELOW
*       6. A FULLWORD, THE ADDRESS OF    ---   "   ---
*
*    THE TABLES PROVIDE INFORMATION ON CHARACTERS WHICH ARE TO BE
*      BYPASSED AS INSIGNIFICANT BEFORE START-OF-ELEMENT, AND THOSE
*      WHICH ARE TO BE RECOGNISED AS TERMINATING AN ELEMENT.
*       BYTE CONTENTS;
*        1ST: BIT FLAGS -  1... .... = SPACE CHARACTERS
*                          .1.. .... = ANY ALPHA CHARACTER
*                          ..1. .... = ANY NUMERIC CHARACTER
*        2ND AND SUBSEQUENT BYTES SHOULD CONTAIN THE ACTUAL CHARACTER
*          WHICH IS RELEVANT, FOR "BYPASS" OR "TERMINATE", PURPOSES -
*          ACCORDING TO WHICH TABLE THIS IS.
         EJECT
         BEGIN
         LM    R1,R5,0(R1)         PICK UP PARMS
         STM   R2,R5,RETDATA       STORE RETURN INFORMATION PARAMS.
         LM    R4,R9,0(R1)         PICK UP ALL INPUT PARAMS.
         LTR   R4,R4               ENSURE STRING HAS LENGTH
         BZ    RET16
         STM   R6,R9,TABLES        HOLD TABLE INFO.
         SR    R2,R2               TO COUNT LENGTH OF ELEMENT
         SR    R3,R3               TO COUNT NO.OF TEXT CHARS.HANDLED
         SPACE 2
*  DEAL WITH ANY "BYPASS", BEFORE ELEMENT
*
         LTR   R6,R6               ANY CHARS. TO BYPASS?
         BZ    ELESTART             NO, GO START ELEMENT
BYPLOOP  LM    R6,R7,TABBYP         YES, PICK UP DECRIP. OF BYPASS TAB.
         TM    0(R7),X'80'         BYPASS SPACES?
         BZ    QBYPALPH             NO, GO
         CLI   0(R5),X'40'          YES. IS STRING CHAR.A SPACE?
         BE    BYPASS                       YES, BYPASS IT
QBYPALPH TM    0(R7),X'40'         BYPASS ALPHA?
         BZ    QBYPNUM              NO, GO
         CLI   0(R5),C'A'           YES. IS STRING CHAR.< "A"?
         BL    UPBYPLIS                     YES, TRY REST OF LIST
         CLI   0(R5),C'Z'                   NO; > "Z"
         BL    BYPASS                         YES, IT'S ALPHA
QBYPNUM  TM    0(R7),X'20'         BYPASS NUMERICS?
         BZ    UPBYPLIS             NO, GO
         TM    0(R5),X'F0'          YES. IS STRING CHAR.NUMERIC?
         BO    BYPASS                       YES, BYPASS IT
*
*   DEAL NOW WITH ANY OTHER CHARS.SPECIFIED IN "BYPASS" TABLE
*
UPBYPLIS BCTR  R6,0                REDUCE REMAINING LENGTH BYPASS LIST
         LTR   R6,R6               ANY MORE TO BE CONSIDERED?
         BZ    ELESTART             NO, GO TAKE ELEMENT
         LA    R7,1(R7)             YES, UP TO NEXT
         CLC   0(1,R5),0(R7)       BYPASS THIS CHAR.IN STRING?
         BNE   UPBYPLIS             NO; LOOP UP BYPASS TABLE
         SPACE 2
*  POSITIVE IDENTIFICATION OF A STRING CHARACTER TO BE BYPASSED
*
BYPASS   LA    R5,1(R5)            MOVE UP THE STRING,
         LA    R3,1(R3)             COUNT CHARS.BYPASSED,
         BCT   R4,BYPLOOP           & LOOP ROUND FOR MORE BYPASS TESTS
         B     ENDSTRNG             UNLESS STRING NOW EXHAUSTED
         EJECT
*  START OF ELEMENT FOUND
*
ELESTART ST    R5,ELEPNTR          HOLD --> START OF ELEMENT
ELELOOP  LM    R8,R9,TABTERM       PICK UP DESCRIP. OF TERMINATE TABLE
         TM    0(R9),X'80'         TERMINATE ON SPACE?
         BZ    QTERMALP             NO, GO
         CLI   0(R5),X'40'          YES. IS STRING CHAR.A SPACE?
         BE    TERMPARS                     YES, TERMINATE
QTERMALP TM    0(R9),X'40'         TERMINATE ON ANY ALPHA?
         BZ    QTERMNUM             NO, GO
         CLI   0(R5),C'A'           YES. IS STRING CHAR.< "A"?
         BL    UPTERLIS                     YES, TRY REST OF LIST
         CLI   0(R5),C'Z'                   NO; > "Z"
         BL    TERMPARS                       YES, IT'S ALPHA, TERMINAT
QTERMNUM TM    0(R9),X'20'         TERMINTAE ON ANY NUMERIC?
         BZ    UPTERLIS             NO, GO
         TM    0(R5),X'F0'          YES. IS STRING CHAR.NUMERIC?
         BO    TERMPARS                     YES, TERMINATE ELEMENT
*
*   DEAL NOW WITH ANY OTHER CHARS.SPECIFIED IN "TERMINATE" TABLE
*
UPTERLIS BCTR  R8,0                REDUCE REMAINING LENGTH TERMNATE LIS
         LTR   R8,R8               ANY MORE TO BE CONSIDERED?
         BZ    ELETAKE              NO, GO TAKE ELEMENT CHARACTER
         LA    R9,1(R9)             YES, UP TO NEXT
         CLC   0(1,R5),0(R9)       TERMINATE STRING WITH THIS CHAR.?
         BNE   UPTERLIS             NO; LOOP UP TERMINATE TABLE
         B     TERMPARS             YES, TERMINATOR FOUND
*
*   ELEMENT CHARACTER IS *NOT* A TERMINATOR
*
ELETAKE  LA    R2,1(R2)            COUNT CHARS. IN ELEMENT
         LA    R5,1(R5)            MOVE UP THE STRING
         BCT   R4,ELELOOP           COUNT ROUND TO NEXT IN STRING
         B     ENDSTRNG
         EJECT
*   TERMINATOR FOUND
*
TERMPARS SR    R15,R15
         LA    R3,1(R3)           COUNT IT AS STRING CHAR.HANDLED
         BCT   R4,TERMA            & COUNT ALONG TEXT ..
ENDSTRNG LA    R15,4                 UNLESS EXHAUSTED - SET R.C. = 4
         SPACE 2
*  COME HERE WITH TERM.FOUND, OR END-OF-TEXT WITHOUT TERMINATOR
*
TERMA    LM    R7,R10,RETDATA     PICK UP USER'S RETURN PARAMS.
         STH   R2,0(R7)           RETURN LENGTH OF ELEMENT
         AR    R3,R2              CALC.TOTAL NO.OF CHARS. HANDLED
         STH   R3,0(R10)           & RETURN THIS
         MVC   0(1,R9),0(R5)      PASS BACK THE TERMINATOR FOUND
         LTR   R2,R2              ANY ELEMENT DATA FOUND?
         BZ    FINPARS             NO, EXIT
         BCTR  R2,0                YES
         L     R5,ELEPNTR         PICK UP OUR START-OF-ELEMENT PNTR.
         EX    R2,MOVEELE         MVC 0(0,R8),0(R5) PASS BACK ELEMENT
         SPACE
FINPARS  FINISH RC=(R15)
         SPACE 3
*  ERROR - WE WERE PASSED A ZERO=LENGTH STRING
*
RET16    LA    R15,16
         B     FINPARS
         EJECT
*  WORK AREAS
*
RETDATA  DC    4A(0)             HOLD ADDRESSES OF RETURN LOCATIONS
TABLES   DS    0F
TABBYP   DC    2A(0)             DESCRIPTOR OF BYPASS TABLE
TABTERM  DC    2A(0)             DESCRIPTOR OF TERMINATE TABLE
ELEPNTR  DC    A(0)              OUR PNTR.TO START OF ELEMENT
MOVEELE  MVC   0(0,R8),0(R5)
         LTORG
         TITLE '               W.S.C.C. SORT SUBROUTINE'
***********************************************************************
*                                                                     *
*        THIS  SUBROUTINE  SORTS  A  TABLE  OF  FIXED-LENGTH          *
*                                                                     *
*              RECORDS  HELD  IN  CORE.                               *
*                                                                     *
*        THE RECORDS ARE SORTED IN ASCENDING OR DESCENDING KEY ORDER  *
*        ACCORDING TO THE KEY-TYPE PARAMETER HANDED ACROSS.           *
*        THE KEYS MAY BE IN CHARACTER OR PACKED DECIMAL FORMAT,       *
*        AND MAY APPEAR ANYWHERE WITHIN THE RECORD.                   *
*                                                                     *
*        THE PARAMETERS TO BE HANDED ACROSS ARE:-                     *
*                                                                     *
*           1. A 3-BYTE FIELD CONTAINING A DESCRIPTION OF THE         *
*              KEY-TYPE.  THE VALUES BEING:-                          *
*                   1ST BYTE   P - KEY IN PACKED DECIMAL NUMBER.      *
*                              C - KEY IN CHARACTER FORMAT.           *
*                   2ND BYTE   A - RECORDS TO BE SORTED IN ASCENDING  *
*                                                      KEY ORDER.     *
*                              D - RECORDS TO BE SORTED IN DESCENDING *
*                                                      KEY ORDER.     *
*                   3RD BYTE   D - CHECK FOR DUPLICATE KEYS.          *
*                                  ANY OTHER CHARACTER FOR NO CHECK.  *
*                                                                     *
*           2. A HALFWORD FIELD CONTAINING THE LENGTH OF THE RECORD   *
*              IN BINARY.                                             *
*                                                                     *
*           3. A HALFWORD FIELD CONTAINING THE LENGTH OF THE SORT KEY *
*              IN BINARY.                                             *
*                                                                     *
*           4. THE TABLE OF RECORDS TO BE SORTED.                     *
*                                                                     *
*           5. A HALFWORD FIELD CONTAINING THE NUMBER OF RECORDS IN   *
*              THE TABLE IN BINARY.                                   *
*                                                                     *
*           6. A HALFWORD FIELD CONTAINING THE RELATIVE POSITION OF   *
*              THE SORT KEY WITHIN EACH RECORD IN BINARY.             *
*                                                                     *
*           7. A SINGLE BYTE TO CONTAIN THE RETURN CODE.              *
*              THE VALUES BEING:-                                     *
*                 0 - SORT SUCCESSFUL                                 *
*                 1 - SORT SUCCESSFUL, DUPLICATE KEYS FOUND           *
*                 2 - INVALID KEYS FOUND, SORT INCOMPLETE(PACKED ONLY)*
*                 3 - INVALID PARAMETER - KEY DOES NOT FALL WITHIN    *
*                                         RECORD BOUNDRIES            *
*                 4 - INVALID PARAMETER - KEY GREATER THAN RECORD     *
*                                         LENGTH OR LESS THAN 1       *
*                 5 - INVALID PARAMETER - INVALID DESCRIPTION KEYS    *
*                 6 - INVALID PARAMETER - RECORD LENGTH GREATER THAN  *
*                                         256 OR LESS THAN 1          *
*                 7 - INVALID PARAMETER - NO OF RECORDS TO BE SORTED  *
*                                         LESS THAN 1.                *
*                                                                     *
***********************************************************************
         EJECT
WSSORT   CSECT
         SPACE
         BEGIN
         SPACE
         LR    R8,R1                        SAVE R1 IN R8
         L     R9,24(R8)                    LOAD ADDRESS OF PARM7
         L     R3,0(R8)                     LOAD ADDRESS OF PARM 1
         MVC   PACKSW,0(R3)                 SET PACK SWITCH
         MVC   DKEY,2(R3)
         CLI   0(R3),C'P'                   PACKED?
         BE    PACKED
         CLI   0(R3),C'C'                   CHARACTER?
         BE    CHARCTR
         B     ERROR5
         SPACE
SORTSEQ  CLI   1(R3),C'A'                   ASCENDING SORT?
         BE    ASCEND
         CLI   1(R3),C'D'                   DESCENDING SORT?
         BE    DESCEND
         B     ERROR5
         EJECT
FLDLGTH  L     R3,4(R8)                     LOAD ADDRESS OF PARM 2
         LH    R2,0(R3)                     LOAD PARM 2
         CH    R2,=H'256'                   REC. LENGTH > 256?
         BH    ERROR6
         CH    R2,=H'1'                                 < 1?
         BL    ERROR6
         BCTR  R2,0                         SUBTRACT 1  FOR LGTH. INSRT
         STC   R2,AGAIN+1                   PLUG IN MVC LENGTH
         STC   R2,AGAIN+7
         STC   R2,MOVELOW+1
         STC   R2,MOVELOW+7
         STC   R2,OUT-5
         STC   R2,OUT-11
         STC   R2,HIGHSEQ+9
         STC   R2,LOWEQ+9
         STC   R2,XOR1+1
         STC   R2,XOR1+7
         STC   R2,XOR1+13
         AH    R2,=H'1'                     RESTORE LENGTH
         SLA   R2,1                         MULTIPLY BY 2
         ST    R2,LENSAVE
         LR    R0,R2
         GETMAIN R,LV=(0)
         ST    R1,CORESAVE
         LR    R10,R1
         SRA   R2,1                         DIVIDE BY 2
         STC   R2,AGAIN+9                   PLUG IN HIGH STORE DISPLACE
         STC   R2,OUT-1
         STC   R2,XOR1+3
         STC   R2,XOR1+11
         STC   R2,XOR1+15
         STC   R2,HIGHSEQ+13
         EJECT
         L     R3,8(R8)                     LOAD ADDRESS OF PARM 3
         LH    R4,0(R3)
         LR    R15,R4                       SAVE R4 IN R15
         CR    R4,R2                        KEY > RECORD?
         BH    ERROR4
         CH    R4,=H'1'                     KEY < 1?
         BL    ERROR4
         BCTR  R4,0                         SUBTRACT 1 FROM R4
         CLI   PACKSW,C'P'                  PACK SWITCH ON?
         BNE   CLCHAR
         CH    R4,=H'15'                    VALID PACKED KEY?
         BH    ERROR4
         LR    R5,R15      FOR MVS, JAN.1983, SAVE R15 ROUND SPIE
         SPIE  ERROR2,(7)                   MASK OFF 0C7'S
         LR    R15,R5       SEE ABOVE - RESTORE IT
         ST    R1,OLDPICA
         LR    R5,R4                        SET UP & PLUG IN LENGTHS
         SLA   R5,4
         OR    R4,R5
CLCHAR   STC   R4,COMPARE1+1
         STC   R4,COMPARE2+1
         STC   R4,ONCEMORE+1
         EJECT
         L     R6,12(R8)                    LOAD ADDRESS OF TABLE
         ST    R6,TBLEADDR
         L     R3,16(R8)                    LOAD ADDRESS OF PARM 5
         LH    R14,0(R3)
         ST    R14,NORECS
         CH    R14,=H'1'                    NO OF RECS. < 1?
         BL    ERROR7
         LR    R7,R2                        CALCULATE SIZE OF TABLE
         MH    R7,0(R3)
         AR    R7,R6
         SR    R7,R2
         EJECT
         L     R3,20(R8)                    LOAD ADDRESS OF PARM 6
         LH    R1,0(R3)
         LTR   R1,R1
         BC    4,ERROR3
         LR    R11,R2                       VALID KEY OFFSET?
         SR    R11,R15
         CR    R1,R11
         BH    ERROR3
         EJECT
         LA    R8,COUNT                     SET UP COUNTS & ADDRESSES
         LA    R11,INNERLP
         LR    R15,R14
         EJECT
AGAIN    MVC   0(1,R10),0(6)                HOLD FIRST ENTRY
         MVC   0(1,R10),0(7)                HOLD LAST  ENTRY  B
         LA    R4,0(1,6)                    POINT TO KEY
         LR    5,4
         LR    R3,R4
         SPACE
         B     COMPARE1
INNERLP  AR    R3,R2
COMPARE1 CLC   0(1,R3),0(4)
BRANCH1  BL    SWOPMIN
COMPARE2 CLC   0(1,R3),0(R5)
BRANCH2  BH    SWOPHIGH
COUNT    BCTR  14,11
         B     MOVE
SWOPMIN  LR    R4,R3                        REPLACE LOWEST SOFAR
         BR    R8
SWOPHIGH LR    R5,R3                        REPLACE HIGHEST SO FAR
         BR    R8
         EJECT
*        MOVE HIGHEST & LOWEST RECORDS FOUND THIS PASS IF NECESSARY
         SPACE 2
MOVE     SR    R4,R1                        RESTORE TO RECORD ADDRESS
         SR    R5,R1
         CR    R7,R4                        LAST = LOWEST?
         BE    SWOPIT
         CR    R6,R5                        FIRST =HIGHEST?
         BNE   JUMP1
XOR1     XC    0(1,R10),0(R10)              SWOP HELD ENTRIES
         XC    0(1,R10),0(R10)
         XC    0(1,R10),0(R10)
HIGHSEQ  EX    0,MOVELOW
         EX    0,MOVELOW+6
         MVC   0(1,R7),0(R10)               B
         B     OUT
SWOPIT   CR    R6,R5                        FIRST = HIGHEST?
         BNE   SWOPEM
         EX    0,XOR1
         EX    0,XOR1+6
         EX    0,XOR1+12
         EX    0,SWOPIT-10
         EX    0,LOWEQ+8
         B     OUT
SWOPEM   EX    0,XOR1
         EX    0,XOR1+6
         EX    0,XOR1+12
LOWEQ    EX    0,MOVEHIGH
         EX    0,MOVEHIGH+6
         MVC   0(1,R6),0(R10)
         B     OUT
MHI      CR    R7,R5                        LAST = HIGHEST?
         BE    OUT
         B     MOVELOW
JUMP1    CR    R6,R4                        FIRST = LOWEST?
         BE    MHI
MOVELOW  MVC   0(1,R6),0(R4)                MOVE RECORDS
         MVC   0(1,R4),0(R10)
MOVEHIGH MVC   0(1,R7),0(R5)
         MVC   0(1,R5),0(R10)               B
         EJECT
OUT      AR    R6,R2                        RESET TABLE ETC. FOR
         SR    R7,R2                                    THE NEXT PASS
         LR    R14,R15
         BCTR  14,0
         BCTR  14,0
         LR    R15,R14
         CH    R15,=H'1'
         BH    AGAIN
         CLI   DKEY,C'D'
         BE    DUPS
         MVI   0(R9),C'0'                   SET RETURN CODE = 0
RETURN   BAL   R10,FREEMAIN
         CLI   PACKSW,C'P'
         BE    UNSPIE
         B     EOJ
         SPACE 3
ERROR2   MVC   9(3,1),ERROR2A               (ERROR2 = SPIE ERROR)
         BR    R14
ERROR2B  MVI   0(R9),C'2'                   SET RETURN CODE = 2
UNSPIE   L     1,OLDPICA                    RESET SPIE
         SPIE  MF=(E,(1))
EOJ      FINISH
         EJECT
ERROR5   MVI   0(R9),C'5'                   SET RETURN CODE = 5
         B     EOJ
         SPACE 1
ERROR6   MVI   0(R9),C'6'                   SET RETURN CODE = 6
         B     EOJ
         SPACE 1
ERROR7   MVI   0(R9),C'7'                   SET RETURN CODE = 7
         BAL   R10,FREEMAIN
         CLI   PACKSW,C'P'
         BE    UNSPIE
         B     EOJ
         SPACE 1
ERROR4   MVI   0(R9),C'4'                   SET RETURN CODE = 4
         BAL   R10,FREEMAIN
         B     EOJ
         SPACE 1
ERROR3   MVI   0(R9),C'3'                   SET RETURN CODE = 3
         BAL   R10,FREEMAIN
         CLI   PACKSW,C'P'
         BE    UNSPIE
         B     EOJ
         EJECT
*        SEARCH FOR DUPLICATE RECORDS
         SPACE 2
DUPS     L     R6,TBLEADDR                  LOAD ADDRESS OF TABLE
         L   R14,NORECS                     LOAD NO. OF RECORDS
         LA    R6,0(R6,R1)                  LOAD ADDRESS OF FIRST
         LA    R7,0(R6,R2)                            & SECOND KEY
ONCEMORE CLC   0(1,R6),0(R7)                COMPARE RECORDS
         BE    TERMSORT                     IF = TO BRANCH OUT
         AR    R6,R2                              ELSE NEXT RECORD
         AR    R7,R2
         BCTR  R14,0
         CH    R14,=H'1'          BRANCH OUT IF END OF TABLE
         BNH   RETURN-4
         B     ONCEMORE
TERMSORT MVI   0(R9),C'1'                   SET RETURN CODE = 1
         B     RETURN
         SPACE 4
FREEMAIN L     R1,CORESAVE
         L     R0,LENSAVE
         FREEMAIN R,LV=(0),A=(1)
         BR    R10
         EJECT
PACKED   MVI   COMPARE1,X'F9'               PLUG IN CP
         MVI   COMPARE2,X'F9'
         MVI   ONCEMORE,X'F9'
         B     SORTSEQ
         SPACE
CHARCTR  MVI   COMPARE1,X'D5'               PLUG IN CLC
         MVI   COMPARE2,X'D5'
         MVI   ONCEMORE,X'D5'
         B     SORTSEQ
         SPACE 2
ASCEND   MVI   BRANCH1+1,X'40'              PLUG IN ASCENDING BC
         MVI   BRANCH2+1,X'20'
         B     FLDLGTH
         SPACE
DESCEND  MVI   BRANCH1+1,X'20'              PLUG IN DESCENDING BC
         MVI   BRANCH2+1,X'40'
         B     FLDLGTH
         EJECT
OLDPICA  DS    F
CORESAVE DS    F
LENSAVE  DS    F
TBLEADDR DS    F
NORECS   DS    F
PACKSW   DS    CL1
ERROR2A  DC    AL3(ERROR2B)
DKEY     DS    CL1
         LTORG
         END
