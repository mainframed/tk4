SRE04    START
         TITLE 'RAAJ8SRE   M04  - SELECTIVE RECORD EDIT -  EDIT A RECORC
               D MODULE  D.E.MILLS'
         INMD
         LR    3,1
         USING QDUMMY,3
         L     4,QPARAM
         USING SPARAM,4
         L     OUTPUT,QLINE            LOAD ADDR OF OUTPUT AREA
         L     GRA,QCOUNT
         LH    LNCNT,0(GRA)            LOAD LINE COUNT
         L     INPUT,QRECORD           LOAD ADDR OF RECORD TO EDIT
         L     GRA,QINDIC              SET ON PUT INDICATOR
         MVI   1(GRA),C'W'
         DOMD  SRE00
         L     GRA,QINDIC
         MVI   1(GRA),C'P'              FOR FCP. MODULE
*
         CH    LNCNT,=H'7'             IF NOT 7 LINES LEFT
         BH    Z01A                     PRINT HEADINGS
         DOMD  SRE05
         L     GRA,QCOUNT
         LH    LNCNT,0(GRA)            LOAD LINE COUNT
Z01A     BAL   RETURN,BLOCK            OUTPUT BLOCK NUMBER LINE
         ZAP   PBYTENO,=P'0'           CLEAR BYTE NUMBER
         CLI   STYPE,C'V'              LOAD RECLTH
         BNE   Z01AA                    ACCORDING
         MVC   HWORD,0(INPUT)           TO FILE TYPE
         LH    RECLTH,HWORD
         B     Z01C
*
Z01AA    CLI   STYPE,C'F'
         BNE   Z01B
         LH    RECLTH,SRLTH
         B     Z01C
*
Z01B     L     GRA,QBLTH                TYPES U,I OR X(INV.BLOCK)
         L     RECLTH,0(GRA)
Z01C     NI    Z03A+1,X'0F'            SET PROG SWITCHES
         NI    Z04A+1,X'0F'             TO NOT BRANCH
*
* CODING TO EDIT CHARACTER LINE (50 BYTES)
*
Z02A     LA    LINE,14(0,OUTPUT)       LINE INDEX TO OUTPUT AREA+14
         LA    GRA,5
Z02B     MVC   CLINST+1(10),0(INPUT)   STORE 10 BYTES OF INPUT
         TR    CLINST+1(10),XNPTAB     REMOVE UNPRINTABLE CHARS
         MVC   0(20,LINE),XCTRTAB      MOVE TO 20 BYTES OF
         TR    0(20,LINE),CLINST        PRINT AREA
         LA    LINE,24(LINE)
         LA    INPUT,10(INPUT)
         BCT   GRA,Z02B                MOVE 5 BLOCKS OF 10 CHARS
         BAL   RETURN,SHORTEN          SHORTEN LINE IF REQD.
         CLC   15(115,OUTPUT),14(OUTPUT) ALL SPACES ?
         BE    Z03A                      YES - DO NOT PRINT
         MVI   0(OUTPUT),X'01'
         DOMD  SRE00                   CALL FCP TO PUT LINE
         SH    LNCNT,=H'1'             REDUCE LINE COUNT
*
* CODING TO EDIT HEXADECIMAL LINE
*
Z03A     BC    0,Z03B                  PUT 'BYTE'
         OI    Z03A+1,X'F0'             ON HEX LINE
         MVC   2(4,OUTPUT),=C'BYTE'     1ST TIME ONLY
Z03B     UNPK  8(4,OUTPUT),PBYTENO     EDIT BYTE NUMBER
         OI    11(OUTPUT),X'F0'
         SH    INPUT,=H'50'            GO BACK TO CORRECT PART.
         LA    LINE,14(OUTPUT)
         LA    GRA,5
Z03C     UNPK  0(11,LINE),0(6,INPUT)   UNPACK 10 BYTES
         UNPK  10(11,LINE),5(6,INPUT)   INTO 20 COLUMNS
         MVI   20(LINE),C' '
         TR    0(20,LINE),XHEXTAB-240  CHANGE FA-FF TO C1-C6
         LA    LINE,24(LINE)
         LA    INPUT,10(INPUT)
         BCT   GRA,Z03C                EDIT 5 BLOCKS OF 20 COLUMNS
         BAL   RETURN,SHORTEN          SHORTEN LINE IF NEC.
         MVI   0(OUTPUT),X'02'
         DOMD  SRE00                   OUTPUT LINE
         SH    LNCNT,=H'2'
*
* CONTINUE TO NEXT DOUBLE LINE
*
Z04A     BC    0,Z04B                  BR IF END REACHED IN SHORTEN
         AP    PBYTENO,=P'50'
         SH    RECLTH,=H'50'
         BC    NP,Z04B
         CH    LNCNT,=H'2'
         BNL   Z02A
         DOMD  SRE05                   CALL HEADING MODULE
         L     GRA,QCOUNT
         LH    LNCNT,0(GRA)            RELOAD LINE COUNT
         BAL   RETURN,BLOCK            OUTPUT BLOCK NUMBER
         NI    Z03A+1,X'0F'             (& BYTE ON 1ST LINE)
         B     Z02A                    RETURN TO DO REST OF RECORD
*
Z04B     L     GRA,QCOUNT              STORE UPDATED
         STH   LNCNT,0(GRA)             LINE COUNT
         OUTMD
         EJECT
*
* ROUTINE TO SHORTEN LINE IF NOT ALL FROM INPUT RECORD
*
SHORTEN  CH    RECLTH,=H'50'
         BNL   0(RETURN)
         OI    Z04A+1,X'F0'
         LR    GRB,RECLTH
         XR    GRA,GRA
         D     GRA,=F'10'              FORM NO OF BLOCKS ON LINE
         MH    GRB,=H'24'              MULT BLOCKS BY 24
         MH    GRA,=H'2'               MULT EXTRAS BY 2
         AR    GRA,GRB                 FORM ADDRESS OF 1ST
         LA    GRB,14(GRA,OUTPUT)       UNREQD. COLUMN
         LNR   GRA,GRA                 & LTH TO BE BLANKED
         AH    GRA,=H'116'              OUT
         STC   GRA,*+9
         MVI   0(GRB),C' '             CLEAR UNREQD.
         MVC   1(1,GRB),0(GRB)          COLUMNS
         BR    RETURN
*
* ROUTINE TO EDIT & PUT 'BLOCK XXXX' LINE
*
BLOCK    MVC   1(5,OUTPUT),=C'BLOCK'
         L     GRA,QBLNO
         UNPK  8(4,OUTPUT),0(3,GRA)
         OI    11(OUTPUT),X'F0'
         L     GRA,QINDIC
         CLI   3(GRA),C'P'
         BNE   Z05A
         MVC   23(7,OUTPUT),=C'(ERROR)'
Z05A     EQU   *
         CLI   STYPE,C'X'              INVALID BLOCK ?
         BNE   Z06A
         MVC   14(7,OUTPUT),=C'INVALID'
Z06A     MVI   0(OUTPUT),X'01'
         DOMD  SRE00
         SH    LNCNT,=H'1'
         BR    RETURN
         LTORG
         EJECT
*
* TRANSLATE TABLES
*
XNPTAB   DC    74X'40'                 TO REMOVE UNPRINTABLE CHARS.
         DC    C'Ö.<(+×&&'
         DC    9X'40'
         DC    C'!$*);^-/'
         DC    8X'40'
         DC    C'|,%_>?'
         DC    10X'40'
         DC    C':#@''="'
         DC    65X'40'
         DC    C'ABCDEFGHI'
         DC    7X'40'
         DC    C'JKLMNOPQR'
         DC    8X'40'
         DC    C'STUVWXYZ'
         DC    6X'40'
         DC    C'0123456789'
         DC    5X'40'
         DC    C'ÿ'
*
CLINST   DC    11C' '
*
XCTRTAB  DC    X'000100020003000400050006000700080009000A'
*
XHEXTAB  DC    C'0123456789ABCDEF'
*
*
HWORD    DC    H'0'
PBYTENO  DC    PL3'0'
         EJECT
*
* DUMMY SECTION FOR INTER MODULE PARAMS
*
QDUMMY   DSECT
QPARAM   DS    F            ADDRESS OF SELECTION PARAMS
QINDIC   DS    F                       INDICATOR BYTES
QINPUT   DS    F                       INPUT BLOCK (IOREG)
QBLTH    DS    F                       INPUT BLOCK LENGTH
QLINE    DS    F                       OUTPUT AREA
QCOUNT   DS    F                       LINE COUNT
QRECORD  DS    F                       RECORD TO BE EDITTED
QBLNO    DS    F                       INPUT BLOCK NO.
QRTP     DS    F                       RUN-TIME PARAMETER
*
* DUMMY SECTION FOR INPUT PARAMS
*
SPARAM   DSECT
STYPE    DS    C                       FILE TYPE (V,F,U)          C
         DS    C
SRLTH    DS    H                       REC.LENGTH (F ONLY)        B
SFORMAT  DS    C                       CONTROL FORMAT (X,C,P)     C
SCLTH    DS    C                       CONTROL LENGTH (M/C LTH)   B
SCDISP   DS    H                       CONTROL DISP.              B
SSEARCH  DS    C                       SEARCH (S)                 C
SKEY     DS    C                       KEY (IF SPECIFIED)         C
SKDISP   DS    H                       KEY DISP. (FFFF IF NOT)    B
SLOW     DS    20C                     LOW CONTROL
SHIGH    DS    20C                     HIGH CONTROL
SRE04    CSECT
RETURN   EQU   2                       RETURN FROM SUB-ROUTINES
LINE     EQU   5                       OUTPUT AREA INDEX
RECLTH   EQU   6                       RECORD LENGTH
INPUT    EQU   7                       INPUT RECORD
OUTPUT   EQU   8                       OUTPUT AREA
LNCNT    EQU   9                       LINE COUNT
GRA      EQU   14                      GENERAL REGISTER ) EVEN/ODD
GRB      EQU   15                      GENERAL REGISTER )  PAIR
NP       EQU   13                      BNP AFTER ARITHMETIC
         END
