         ISEQ  73,80
         TITLE '--------------  SYSRES STATS MONITOR -------------'
*        PARM
*              ALL FIELDS ARE OPTIONAL
*
*              PARM='VOLID,SAMPLES,RATE'
*
*        VOLID     NAME OF PACK TO BE SAMPLED  --  DEFAULT IS SYSRES
*        SAMPLES   NUMBER OF SAMPLES TO BE TAKEN  --  DEFAULT 3600
*        RATE  INTERVAL BETWEEN SAMPLES  --  DEFAULT .05 SECONDS
*
*              1 < SAMPLES < 32768
*              .02 < RATE < 9.99
*
*
SYSRES   PROLOG REG=YES
         L     R1,0(,R1)                R1 -> PARM LENGTH
         LA    R3,2(,R1)                R3 -> PARM
         LR    R4,R3
         AH    R4,0(,R1)                R4 -> END OF TEXT
         CR    R4,R3                    Q. ANY TEXT TO PROCESS
         LA    R15,PARMTBL                   FOR 0 LENGTH TEXT
         BE    ENDPARM                  A. NO - SKIP SCAN
         LH    R5,0(,R1)                R5 = LENGTH OF TEXT
         XC    TRT,TRT                  CLEAR TRT TABLE
         MVI   TRT+C'=',4               STOP ON = - CODE 4
         MVI   TRT+C',',8              STOP ON , - CODE 8
         SR    R2,R2                    RESULT FROM TRT INSTR'
         XC    0(8,R15),0(R15)          CLEAR FIRST ENTRY
PSCAN    LR    R1,R3                    FOR CC 0
         BCTR  R5,0                     ADJUST LENGTH
         LR    R0,R3                    START OF FIELD
         EX    R5,STRT                  SCAN FOR A DELIMITER
         BZ    STOPB1                   NOTHING HIT
         BNP   NOTEND                   NOT LAST BYTE
         OI    SWITCH,X'80'             DO NOT RESUME SCAN
NOTEND   LR    R14,R3
         LA    R3,1(,R1)                R3 -> RESUME SCAN POINT
         LR    R5,R4                    R5 -> END OF TEXT
         SR    R5,R3                    R5 = CONTINUE LENGTH
         SR    R0,R1
         LPR   R0,R0                    R0 = LENGTH OF FIELD
         B     *(R2)                    BRANCH ON TYPE OF STOP
         B     STOPA                    =
         B     STOPB                    ,
STOPA    ST    R14,0(,R15)              A(KEYWORD)
         STC   R0,0(,R15)               LENGTH OF KEYWORD
         B     RESCAN                   RESUME
STOPB1   OI    SWITCH,X'80'             DO NOT RESUME
         LA    R0,1(,R5)                LENGTH
         LR    R14,R3                   ADDRESS
STOPB    ST    R14,4(,R15)              A(DATA)
         STC   R0,4(,R15)               LENGTH OF DATA
         LA    R15,8(,R15)              NEXT ENTRY
         XC    0(8,R15),0(R15)          CLEAR IT
RESCAN   TM    SWITCH,X'80'             Q. ANY MORE TEXT
         BZ    PSCAN                    A. YES - CARRY ON
ENDPARM  LA    R14,PARMTBL
         SR    R15,R14
         SRL   R15,3                    NUMBER OF ENTRIES USED
         NI    SWITCH,X'7F'
         SR    R2,R2
*        SECOND VALUE IS NUMBER OF SAMPLES
*        THIRD VALUE IS NUMER OF 1/100 BETWEEN SAMPLES
         CLI   PARMTBL+12,0             Q. OVERRIDE
         BE    TRYRATE                  A. NO
         IC    R2,PARMTBL+12
         BCTR    R2,0
         L     R15,PARMTBL+12
         EX    R2,PSAMP
         CVB   R1,DWORD
         ST    R1,SAMPLES
TRYRATE  CLI   PARMTBL+20,0
         BE    NORATE
         IC    R2,PARMTBL+20
         BCTR  R2,0
         L     R15,PARMTBL+20
         EX    R2,PSAMP
         CVB   R1,DWORD
         ST    R1,RATE
NORATE   L     R1,SAMPLES
         CH    R1,=H'32767'             MAX NO' SAMPLES
         BNH   *+8
         LH    R1,=H'32767'
         CH    R1,=H'0'
         BNE   *+8
         LA    R1,1                     MINIUM
         ST    R1,SAMPLES
         L     R1,RATE
         CH    R1,=H'999'               9.99 SECS MAX
         BNH   *+8
         LH    R1,=H'999'
         CH    R1,=H'1'
         BH    *+8
         LA    R1,2
         ST    R1,RATE
         CLI   PARMTBL+4,0
         BE    SYSPACK
         MVC   VOLID,=CL6' '
         L     R1,PARMTBL+4
         SR    R2,R2
         IC    R2,PARMTBL+4
         BCTR  R2,0
         EX    R2,MOVE
         L     R1,16
         L     R1,40(,R1)               R1 -> UCB TABLE
         LA    R15,12                   RETURN CODE
SCAN     CLC   0(2,R1),=XL2'FFFF'       Q. END OF TABLE
         BE    EXIT                     A. YES - EXIT(12)
         CLC   0(2,R1),=XL2'0'          Q. VALID ENTRY
         BE    UPSCAN                   A. NO - TRY AGAIN
         LH    R3,0(,R1)                R3 -> UCB
         CLI   18(R3),X'20'             Q. DASD
         BNE   UPSCAN                   A. NO - FORGET IT
         CLC   28(6,R3),VOLID           Q. REQUIRED VOLUME
         BE    FOUND                    A. YES
UPSCAN   LA    R1,2(,R1)                NEXT SLOT
         B     SCAN
MOVE     MVC   VOLID(0),0(R1)
SYSPACK  EQU   *
         L     R2,16                   R2 -> CVT
         L    R3,48(,R2)                R3 -> SYSRES UCB
FOUND    EQU   *
         L     R4,SAMPLES               NUMBER OF SAMPLES
         LA    R5,STATS                 R5 -> STATS TABLE
         SR    R6,R6                    INDEX REGISTER
         SR    R9,R9                    BUSY COUNT
         SR    R7,R7                    OVERLOAD COUNT
         EJECT
SAMPLOOP STIMER WAIT,BINTVL=RATE
         TM    6(R3),X'88'              Q. OVERLOAD
         BZ    *+8
         LA    R7,1(,R7)                BUSY COUNT
         TM    6(R3),X'06'              Q. BUSY
         BZ    *+8                      A. NO
         LA    R9,1(,R9)                BUSY COUNT
         ICM   R6,3,51(R3)              CCCC FROM SEEK
         SLL   R6,1                     DOUBLE IT
         LH    R15,0(R6,R5)             2 BYTE ENTRY
         LA    R15,1(,R15)              ACCUM COUNT
         STH   R15,0(R6,R5)
         BCT   R4,SAMPLOOP              REPEAT SAMPLE
         EJECT
*              PRINT STATS
         OPEN  (SYSPRINT,OUTPUT)
         SR    R8,R8
         MH    R9,=H'1000'
         D     R8,SAMPLES               RATIO
         CVD   R9,DWORD                 REMAINDER - FOR PERCENT
         UNPK  HEAD1+20(3),DWORD+6(2)
         MVC   HEAD1+7(6),VOLID
         MVI   HEAD1+22,C'%'
         PUT   SYSPRINT,HEAD1
         SR    R6,R6
         MH    R7,=H'1000'
         D     R6,SAMPLES
         CVD   R7,DWORD
         UNPK  HEAD2A+14(3),DWORD+6(2)
         MVI   HEAD2A+16,C'%'
         PUT   SYSPRINT,HEAD2A
         L     R15,RATE
         CVD   R15,DWORD
         ED    HEAD2+9(5),DWORD+6
         PUT   SYSPRINT,HEAD2
         L     R15,SAMPLES
         CVD   R15,DWORD
         ED    HEAD3+4(7),DWORD+5
         PUT   SYSPRINT,HEAD3
         PUT   SYSPRINT,HEAD4
         LR    R6,R5                    STATS BASE
         LA    R4,800
PLOOP    CLC   0(2,R6),=XL2'0'
         BE    PUP
         LR    R15,R6
         SR    R15,R5
         SRL   R15,1                    CYLINDER NUMBER
         CVD   R15,DWORD
         MVC   LINE,LINEIM
         UNPK  LINE+5(3),DWORD+6(2)
         OI    LINE+7,240
         ST    R15,DWORD+4
         UNPK  LINE+8(5),DWORD+6(3)
         MVI   LINE+8,C'('
         MVI   LINE+12,C')'
         MVI   LINE+13,C' '
         TR    LINE+9(3),TAB-240
         LH    R15,0(,R6)
         CVD   R15,DWORD
         ED    LINE+14(7),DWORD+5
         PUT   SYSPRINT,LINE
PUP      LA    R6,2(,R6)
         BCT   R4,PLOOP
         CLOSE SYSPRINT
         LA    R15,0
EXIT     EPILOG RC=(15)
         EJECT
STRT     TRT   0(0,R3),TRT
PSAMP    PACK  DWORD(8),0(0,R15)
PARMTBL  DC    4D'0'
DWORD    DS    D
SAMPLES  DC    F'3600'
RATE     DC    F'05'
VOLID    DC    CL6'SYSRES'
HEAD1    DC    CL25'1      VOLVOL  BUSY   %'
HEAD2    DC    CL25' '
         ORG   HEAD2
         DC    C'0    RATE',X'40214B2020',C' SECS'
         ORG
HEAD2A   DC    CL25'0     LOADING XX%'
HEAD3    DC    CL25' '
         ORG   HEAD3
         DC    C'0   ',X'4020206B202120',C' SAMPLES'
         ORG
HEAD4    DC    CL25'0    CYLINDER   SEEKS'
LINE     DC    CL25' '
LINEIM   DC    CL25' '
         ORG   LINEIM
         DC    C'        (     ',X'4020206B202120'
         ORG
TAB      DC    C'0123456789ABCDEF'
SWITCH   DC    X'00'
TRT      DS    CL256
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,LRECL=25,RECFM=FBA,   $
               BLKSIZE=2500
         LTORG
STATS    DC    800H'0'                  1 1/2 WORD PER CYLINDER
         END   SYSRES
