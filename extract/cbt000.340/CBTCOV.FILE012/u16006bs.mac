$QCB     DSECT
$FORMAT  DS    X
$OPERATN DS    X
$LEN     DS    H
$ANCHOR  DS    F
$HEAD    DS    F
$TAIL    DS    F
$CURREL  DS    F
$AVAIL   DS    F
SM34Q01  ENTER 12,RENT=YES,VERS=MVS1.0,PUN=NO
         XC    0(4,13),0(13)
         LR    10,0
         LR    11,1
         USING $QCB,11
         ICM   2,15,$ANCHOR
         BP    *+8
         BAL   9,FIRSTBUF
COMPAD   CLC   0(4,2),=F'0'
         BE    GOTLAST
         L     2,0(0,2)
         B     COMPAD
GOTLAST  TM    $OPERATN,X'02'
         BO    FREEALL
         TM    $OPERATN,X'60'
         BZ    CHEKMT
         TM    $OPERATN,X'40'
         BO    ADD
CHEKMT   ICM   3,15,$HEAD
         BP    NOTMT
         LEAVE 4
NOTMT    TM    $OPERATN,X'60'
         BZ    EXAM
*
*   DELETE ELEMENT FROM STACK
*
         TM    $OPERATN,X'18'
         BZ    NOTBADD
INVOP    LEAVE 8
NOTBADD  L     4,0(0,3)
         ST    4,$HEAD
         LH    5,$LEN
         TM    $OPERATN,X'01'
         BO    NOPASS
         LA    3,4(0,3)
         MOVE  (10),(3),(5)
NOPASS   LA    5,4(0,5)
         LH    6,10(0,2)
         AR    6,5
         STH   6,10(0,2)
         LH    6,12(0,2)
         BCT   6,SOMEBUF
         ICM   7,15,4(2)
         BZ    LASTBUF
         MVC   0(4,7),=F'0'
FREEBUF  LH    7,8(0,2)
         LA    7,16(0,7)
         FREEMAIN R,LV=(7),A=(2)
OKEXIT   LEAVE 0
LASTBUF  MVC   $ANCHOR,=F'0'
         B     FREEBUF
SOMEBUF  STH   6,12(0,2)
         B     OKEXIT
*
*   JUST EXAMINE THE TOP ENTRY
*
EXAM     LH    5,$LEN
         LA    3,4(0,3)
         MOVE  (10),(3),(5)
         B     OKEXIT
*
*   ADD AN ELEMENT TO THE STACK
*
ADD      LH    5,$LEN
         LA    5,4(0,5)
         CH    5,10(0,2)
         BH    NEWBUF
         LH    6,10(0,2)
         SR    6,5
         STH   6,10(0,2)
         LH    6,12(0,2)
         LA    6,1(0,6)
         STH   6,12(0,2)
         ICM   3,15,$HEAD
         BP    OLDBUF
         LA    6,16(0,2)
         L     3,0(0,13)
         B     *+8
OLDBUF   LA    6,0(5,3)
         SH    5,=H'4'
         ST    6,$HEAD
         ST    3,0(0,6)
         LA    6,4(0,6)
         MOVE  (6),(10),(5)
         B     OKEXIT
*
*   GET A NEW BUFFER FOR THE ADD FUNCTION
*
NEWBUF   CLC   0(4,2),=F'0'
         BZ    GETIT
         L     2,0(0,2)
         B     ADD
GETIT    LH    7,8(0,2)
         LA    7,16(0,7)
         GETMAIN R,LV=(7)
         ST    1,0(0,2)
         XC    0(16,1),0(1)
         ST    2,4(0,1)
         MVC   8(2,1),8(2)
         MVC   10(2,1),8(1)
         LR    2,1
         MVC   0(4,13),$HEAD
         MVC   $HEAD,=F'0'
         B     ADD
*
*   GET THE FIRST BUFFER
*
FIRSTBUF LH    2,$LEN
         LA    2,4(0,2)
         SLL   2,6
         LA    2,16(0,2)
         C     2,=F'2048'
         BH    *+8
         LA    2,2048
         GETMAIN R,LV=(2)
         ST    1,$ANCHOR
         XC    0(16,1),0(1)
         SH    2,=H'16'
         STH   2,8(0,1)
         STH   2,10(0,1)
         LR    2,1
         BR    9
FREEALL  EQU   *
         L     3,4(0,2)
         LH    4,8(0,2)
         LA    4,16(0,4)
         FREEMAIN R,LV=(4),A=(2)
         LTR   2,3
         BNZ   FREEALL
         MVC   $ANCHOR,=F'0'
         MVC   $HEAD,=F'0'
         B     OKEXIT
         LTORG
         END
