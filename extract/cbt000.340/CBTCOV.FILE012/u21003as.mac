COPYIT   CSECT
         SPACE 3
**********************************************************************
*                                                                    *
* PROGRAM FUNCTION :                                                 *
*                     TO COPY A SEQUENTIAL G.D.G. DATA SET           *
*                     AND CREATE CONTROL STATEMENTS FOR              *
*                     SUBSEQUENT IEHPROGM PROCESSING.                *
* LIMITATIONS AND RESTRICTIONS :                                     *
*                     A) ONLY ONE FILE MAY BE COPIED AT A TIME       *
*                     B) LABEL PARAMETER IS REQUIRED                 *
*                     C) MAXIMUM OF NINE DATA SETS PER TAPE          *
*                                                                    *
**    AUTHORS - ICI (FIBRES DIVISION)   CONTACT ICI (MOND DIV.)
**********************************************************************
         SPACE 3
         ASSETUP 1
         B     BEGIN
         EJECT
DSO      DCB   DSORG=PS,MACRF=(GL),DDNAME=DSO,EODAD=EOF,EXLST=JFCB
DSN1     DCB   DSORG=PS,MACRF=(PM),DDNAME=DSN1,EXLST=JFCB
DSN2     DCB   DSORG=PS,MACRF=(PM),DDNAME=DSN2
DSW      DCB   DSORG=PS,MACRF=(PM),DDNAME=DSW
 EJECT
JFCB     DS    0F
         DC    X'07'
         DC    AL3(AREA)
AREA     DS    CL176
MESSAGE  DS    0CL133
         DC    X'09'
         DC    C'SUCCESSFUL EXECUTION'
         DC    CL112' '
CDIMAGE1 DS    0CL80
         DC    C' UNCATLG DSNAME='
         DC    CL64' '
CDIMAGE2 DS    0CL80
         DC    C' SCRATCH VOL=3330=      ,DSNAME='
         DC    CL48' '
CDIMAGE  DS    0CL80
         DC    C' CATLG VOL=2400=(      , ),DSNAME='
         DC    CL46' '
         DS    0F
         DC    C' '
         DS    0H
OPTLIST  DC    X'000A'
         DC    C'LINECNT=50'
DDNMELST DC    X'0010'
         DC    C'CATVOL  '
         DC    C'SCRVOL  '
         DC    X'80'
         DC    CL3' '
         EJECT
         CNOP  0,4
BEGIN    EQU   *
         L     R4,0(R1)
         OPEN  (DSO,,DSN1,(OUTPUT),DSN2,(OUTPUT),DSW,(OUTPUT))
         SPACE 2
**********************************************************************
*                            COPY THE DATA SET
         SPACE 2
READ     EQU   *
         GET   DSO
         LR    R0,R1
         PUT   DSN1
         B     READ
         SPACE 2
**********************************************************************
*                            CREATE UNCATLG & SCRATCH
*                            CARD IMAGES
         SPACE 2
EOF      EQU   *
         RDJFCB  DSO
         MVC   CDIMAGE1+16(44),AREA
         MVC   CDIMAGE2+32(44),AREA
         MVC   CDIMAGE2+18(6),AREA+118
         SPACE 2
**********************************************************************
*                            CREATE CATLG CARD IMAGE
         SPACE 2
         RDJFCB DSN1
         MVC   CDIMAGE+34(44),AREA
         MVC   CDIMAGE+17(6),AREA+118
         MVC   CDIMAGE+24(1),2(R4)
         SPACE 2
**********************************************************************
*                            WRITE IEHPROGM CONTROL CARD IMAGES
*                            TO TEMPORARY FILE
         SPACE 2
         PUT   DSN2,CDIMAGE1
         PUT   DSN2,CDIMAGE2
         PUT   DSN2,CDIMAGE
         CLOSE (DSO,,DSN1,,DSN2)
         SPACE 2
**********************************************************************
*                            LINK TO IEHPROGM
         SPACE 2
         LINK  EP=IEHPROGM,PARAM=(OPTLIST,DDNMELST),VL=1
         SPACE 2
**********************************************************************
*                            TEST RETURN CODE
*                            DUMP IF NON ZERO
         SPACE 2
         CH    R15,=X'0000'
         BE    END
         ABEND  3999,DUMP
         SPACE 2
**********************************************************************
*                            WRITE ' SUCCESSFUL COMPLETION ' MESSAGE
         SPACE 2
END      EQU   *
         PUT   DSW,=X'1B'
         PUT   DSW,MESSAGE
         CLOSE DSW
         ASSEND
         END   COPYIT
