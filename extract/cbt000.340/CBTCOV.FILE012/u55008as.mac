*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSU03TSC.
         GBLC  &SAVE
SSU03TSC ENTER ENT=TAPESCAN
         L     1,0(0,1)
         SR    2,2
         LH    2,0(0,1)
         LTR   2,2
         BZ    NOPARM
         CH    2,=H'5'
         BNE   INVPARM
         OI    CSW1+1,X'F0'
         OI    CSW2+1,X'F0'
         MVC   RECCNT+18(19),=CL132' '
         B     NOPARM
INVPARM  WTO   'INVALID PARM FIELD - FULL SCAN ASSUMED',ROUTCDE=(1,11)
NOPARM   EQU   *
         OPEN  (PRTER,(OUTPUT),TAPIN)
         PUT   PRTER,CHAN1
         LINK  EP=CALDTE,PARAM=DATEAREA
         MVC   PRTAR(132),HEAD1
         ED    PAGE(6),PGCNT
         BAL   12,PRINT
         LM    2,6,ZEROS
         BAL   12,IOIN
         CLI   INECB,X'7F'
         BE    OK
         TM    INSTAT+4,X'01'
         BO    TMARK
         B     ERROR
OK       CLC   WORK(4),=CL4'VOL1'
         BNE   TRYANS
ANSIN    BAL   12,LABEL
         MVC   PRTAR(80),WORK
         BAL   12,PRINT
         B     SCAN
NOLAB    MVC   PRTAR(132),NOLMSG
         BAL   12,PRINT
         B     CHECK
TRYANS   CLC   WORK(4),=X'564F4C31'
         BNE   NOLAB
         XLATE WORK,80
         NI    ASW+1,X'0F'
         MVC   LMSG+44(20),=C'***  A  N  S  I  ***'
         B     ANSIN
TMARK    MVC   PRTAR(132),=CL132'         TAPE  MARK'
         BAL   12,PRINT
         OI    SW1+1,X'F0'
         NI    TMARK2+1,X'0F'
         CLI   TFLAG,X'FF'
         BE    EOJ
         CLI   VFLAG,X'FF'
         BE    EOJ
         MVI   TFLAG,X'FF'
         B     SCAN
PRINT    PUT   PRTER,PRTM
         MVC   PRTAR(132),=CL132' '
         AP    COUNT(2),P2
         CP    COUNT(2),=PL2'60'
         BNL   *+6
         BR    12
         ZAP   COUNT(2),=PL2'2'
         PUT   PRTER,CHAN1
         MVC   PRTAR(132),HEAD1
         AP    PGCNT(3),=PL1'1'
         ED    PAGE(6),PGCNT
         PUT   PRTER,PRTM
         MVC   PRTAR(132),=CL132' '
         BR    12
LABEL    MVC   LMSG+32(6),WORK+4
         MVC   PRTAR(132),LMSG
         B     PRINT
SCAN     BAL   12,IOIN
         CLI   INECB,X'7F'
         BE    CHECK
         TM    INSTAT+4,X'01'
         BO    TMARK2
         B     ERROR
CHECK    MVI   TFLAG,X'00'
ASW      B     EBCDIC
         XLATE WORK,80
EBCDIC   CLC   WORK(3),=C'HDR'
         BNE   CHKEOF
PRT      MVC   PRTAR(80),WORK
         BAL   12,PRINT
         B     SCAN
CHKEOF   CLC   WORK(3),=C'EOF'
         BNE   CHKEOV
         B     PRT
CHKEOV   CLC   WORK(3),=C'EOV'
         BNE   *+12
         MVI   VFLAG,X'FF'
         B     PRT
         CLC   WORK(3),=C'UHL'
         BNE   CHKUTL
         B     PRT
CHKUTL   CLC   WORK(3),=C'UTL'
         BNE   *+8
         B     PRT
         LH    2,INSTAT+6
         LH    3,=H'32767'
         SR    3,2
         C     4,=F'0'
         BNE   COMP
         LR    4,3
BUMP     LA    5,1(5)
         B     SCAN
COMP     CR    3,4
CSW1     BE    BUMP
EDIT     CVD   5,DWK1
         MVC   PRTAR(37),RECCNT
         ED    PRTAR(8),DWK1+4
CSW2     NOP   *+14
         CVD   4,DWK1
         ED    PRTAR+29(8),DWK1+4
         BAL   12,PRINT
         LR    4,3
         SR    2,2
         SR    3,3
         SR    5,5
SW1      B     BUMP
TMARK2   NOP   TMARK
         C     5,=F'0'
         BE    TMARK
         NI    SW1+1,X'0F'
         OI    TMARK2+1,X'F0'
         SR    3,3
         B     EDIT
EOJ      CLOSE (PRTER,,TAPIN)
         L     13,4(13)
         RETURN (14,12),RC=0
ERROR    LTR   5,5
         BZ    NONE
         MVC   SW1(4),INST1
         B     EDIT
NONE     MVC   SW1(4),INST2
         LA    7,WORK
         LR    9,7
         LA    8,64
         AR    9,4
         SR    4,4
GETIT    UNPK  WORK2(15),0(8,7)
         UNPK  WORK2+14(15),7(8,7)
         UNPK  WORK2+28(15),14(8,7)
         UNPK  WORK2+42(15),21(8,7)
         UNPK  WORK2+56(9),28(5,7)
         TR    WORK2(64),TRTAB-240
         MVI   PRTM,X'09'
         MVI   P2,X'1C'
         MVC   PRTAR(64),WORK2
         MVC   PRTAR+70(32),0(7)
         BAL   12,PRINT
         BXLE  7,8,GETIT
         BAL   12,PRINT
         MVI   PRTM,X'11'
         MVI   P2,X'2C'
         UNPK  WORK2(9),INSTAT+4(5)
         TR    WORK2(8),TRTAB-240
         MVC   PRTAR+5(28),ERRMSG
         MVC   PRTAR+34(8),WORK2
         BAL   12,PRINT
         B     SCAN
IOIN     XC    INECB(4),INECB
         EXCP  INIOB
         WAIT  ECB=INECB
         BR    12
         LTORG
INECB    DC    F'0'
INIOB    DC    X'0200'
         DC    H'0'
         DC    X'7F'
         DC    AL3(INECB)
INSTAT   DC    2F'0'
         DC    A(RDCCW)
         DC    A(TAPIN)
         DC    F'0'
         DC    H'1'
         DC    H'0'
         DC    2F'0'
RDCCW    CCW   2,WORK,X'20',32767
TRTAB    DC    C'0123456789ABCDEF'
P2       DC    PL1'2'
WORK2    DC    XL65'00'
PRTER    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),LRECL=133,RECFM=FBM,X
               BLKSIZE=133
TAPIN    DCB   DDNAME=TAPE,DSORG=PS,MACRF=E
PRTM     DC    X'11'
PRTAR    DC    CL132' '
CHAN1    DC    X'8B'
HEAD1    DC    CL54' '
         DC    CL68'TAPE  SCANNING  UTILITY'
         DC    CL5'PAGE '
         DC    X'2020202020'
DATEAREA EQU   *-35
PAGE     EQU   PRTAR+126
PGCNT    DC    PL3'1'
NOLMSG   DC    CL132'         TAPE  IS  UNLABELLED'
TFLAG    DC    X'00'
VFLAG    DC    X'00'
LMSG     DC    CL132'         TAPE  IS  LABELLED  -'
ZEROS    DC    5F'0'
DWK1     DC    D'0'
RECCNT   DC    XL8'4020202020202020'
         DC    C'  BLOCKS  OF  LENGTH '
         DC    XL8'4020202020202020'
INST1    DC    X'47F0'
         DC    SL2(NONE-&SAVE.(13))
INST2    DC    X'47F0'
         DC    SL2(BUMP-&SAVE.(13))
ERRMSG   DC    C'I/O ERROR - CSW STATUS WAS -'
COUNT    DC    PL2'2'
WORK     DC    80XL250'00'
         DC    40XL250'00'
         DC    28XL100'00'
         END
