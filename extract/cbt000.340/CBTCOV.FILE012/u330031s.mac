PEACH2   CSECT
*   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *
*          THIS PROGRAM WILL PRINT ANY PHYSICAL SEQUENTIAL FILE
*     (CONTAINING F, V, OR U FORMAT RECORDS, EXCEPT VARIABLE SPANNEDD
*     RECORDS) IN A FORMAT THAT FACILITATES QUICK RECOGNITION OF
*     CHARACTER FIELDS IN A RECORD OF MIXED FIELD TYPES (CHARACTER,
*     DECIMAL, BINARY AND BIT STRING).
*          THE HEADING CONTAINS THE DSNAME OF THE DATA SET BEING
*     LISTED, THE VOLUME SERIAL NUMBER, THE RECORD FORMAT, THE LOGICAL
*     RECORD LENGTH, THE BLOCKSIZE AND THE DATE.
*          EACH RECORD IS PRINTED SEPARATELY WITH ITS LOGICAL RECORD
*     LENGTH, RECORD NUMBER AND BLOCK NUMBER.   EACH BYTE IS
*     REPRESENTED BY A CHARACTER ON EACH OF 3 LINES OF PRINT.
*     THE HEXADECIMAL ZONE REPRESENTATION IS ON LINE 1 AND THE NUMERIC
*     ON LINE 2, WHETHER OR NOT THE CHARACTER IS PRINTABLE.   IF THE
*     BYTE CONTAINS THEHEXADECIMAL REPRESENTATION OF ANY CHARACTER IN
*     THE 60 CHARACTER SET THE 3RD LINE CONTAINS THIS CHARACTER.
*     IF NOT, THE CHARACTER '.' IS PRINTED.
*          EACH LINE SET REPRESENTS ONE HUNDRED CHARACTERS.   A RECORD
*     IS SPREAD ACROSS AS MANY LINES AS NECESSARY.   THE END OF THE
*     RECORD IS MARKED BY AN '*', AND THE END OF THE BLOCK IS MARKED
*     BY '**', ON EACH OF THE THREE LINES.
*          A PARAMETER INDICATING THE NUMBER OF RECORDS TO BE PRINTED
*     BY THE PROGRAM MAY BE PASSED VIA THE EXEC CARD.   A PARAMETER '0'
*     INDICATES THE WHOLE FILE IS TO BE PRINTED
*          THE DDNAMES OF THE FILES USED BY THE PROGRAM ARE: -
*     INPUT - THE INPUT DATA SET TO BE PRINTED,
*     OUTPUT - THE OUTPUTN PRINTED DATA SET
*          A DEFAULT BLOCKSIZE OF 532 IS USED FO THE OUTPUT. THE LRECL
*     IS 133.
*          THE FOLLOWING USER ABENDS ARE ISSUED BY THE PROGRAM:
*     200 - PARM FIELD ON EXEC CARD TOO LONG (MAXIMUM OF 5 CHARACTERS
*     ALLOWED).
*     300 - PARM FIELD NOT NUMERIC.
*     400 - AN ATTEMPT HAS BEEN MADE TO PRINT SPANNED RECORDS.
*          THE JCL NECESSARY TO USE WITH THIS PROGRAM IS: -
*     //  EXEC PGM=     ,PARM='99999'
*     //INPUT DD    (AS SPECIFIED BY THE USER)
*     //OUTPUT DD SYSOUT=A
*   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *
         SAVE  (14,12)
*
         CNOP  2,4
         BALR  12,0
         USING *,12
         B     *+8
         DC    A(SAVEAREA+80)
         ST    13,SAVEAREA+4
         LR    11,13
         LA    13,SAVEAREA
         ST    13,8(11)
* CHECK PARM FIELD PRESENT
         L     11,0(1)
         LH    10,0(11)
         LTR   10,10
         BNZ   PARMFLD
* NO PARM FIELD=DEFAULT TO 50 RECS
         ZAP   NUMREQ(3),=P'50'
         B     OPENDCB
* CHECK PARM FIELD 5 CHARS OR LESS
PARMFLD  CH     10,=H'5'
         BNH   TEST1
         ABEND 200
* CHECK PARM NUMERIC
TEST1    BCTR  10,0
         EX    10,TRTEST
         BZ    TEST2
         ABEND 300
* CONVERT PARM FIELD TO BINARY AND EXAMINE FOR '0'
TEST2    O     10,=F'112'
         EX    10,PACKIT
OPENDCB  CVB  10,DWORK
       MVC   NUMERICS(10),=C'0123456789'
         LTR   10,10
         BNZ   *+8
         L    10,=F'999999'
* EXAMINE INFORMATION IN JFCB'S AND TAKE APPROPRIATE ACTION.
         RDJFCB (INDCB,,OUTDCB)
* CHECK OUTPUT BLKSIZE VALID AND DEFAULT TO 532 IF NECESSARY
         LH    8,OUTJFCB+102
         LTR    8,8
         BZ    DFLTBS
         SRDA  8,32
*
         USING IHADCB,11
         LA    11,OUTDCB
         LH    15,DCBLRECL
         DR    8,15
         LTR   8,8
         BZ    *+10
DFLTBS   MVC   OUTJFCB+102(2),=H'532'
* GET INPUT DSNAME AND VOL SER NO
         LA    11,INDCB
         MVC   DSNAME(44),INJFCB
*
* SPECIAL DATE ROUTINE USING PL/1 LIBRARY ROUTINE IHEOSD
         B     DATE
PL1DATE  DC    CL6' '
DOPE     DS    0F
         DC    A(PL1DATE)
         DC    AL2(L'PL1DATE)
         DC    AL2(L'PL1DATE)
PARM     DC    A(DOPE)
*
DATE     LA    1,PARM
         L     15,=V(IHEOSDA)
         BALR  14,15
*
         MVC   DAYID(2),PL1DATE+4
         MVC   DAYID+3(2),PL1DATE+2
         MVC   DAYID+6(2),PL1DATE
* CONSTRUCT 2ND HEADING LINE
         MVC   VOLSER(6),INJFCB+118
         OPEN  (INDCB,,OUTDCB,(OUTPUT)),TYPE=J
* LOCATE AND CONVERT LRECL AND BLKSIZE TO PRINT
         LH    4,DCBBLKSI
         CVD   4,DWORK
         MVC   BLKSIZE(6),EDITPTN
         ED    BLKSIZE(6),DWORK+5
         LH    4,DCBLRECL
         CVD   4,DWORK
         MVC   LRECL(6),EDITPTN
         ED    LRECL(6),DWORK+5
* INTERPRATE RECFM
         LA    5,RECFM+1
         TM    DCBRECFM,X'20'
         BZ    *+12
         MVI   0(5),C'T'
         LA    5,1(5)
         TM    DCBRECFM,X'10'
         BZ    *+12
         MVI   0(5),C'B'
         LA    5,1(5)
         TM    DCBRECFM,X'08'
         BZ    *+12
         MVI   0(5),C'S'
         LA    5,1(5)
         TM    DCBRECFM,X'04'
         BZ    *+12
         MVI   0(5),C'A'
         LA    5,1(5)
         TM    DCBRECFM,X'02'
         BZ    *+8
         MVI   0(5),C'M'
* IDENTIFY BASIC RECORD TYPE
         TM    DCBRECFM,X'C0'
         BO    UNDEF
         TM    DCBRECFM,X'80'
         BO    FIXED
         B     VAR
* PRINT HEADINGS
HEADS    PUT   OUTDCB,PRINT1
         PUT   OUTDCB,PRINT2
         MVI   PRINT2,C' '
         BR    6
         EJECT
* CONTROL ROUTINES FOR FIXED,VARIBLE AND UNDEFINED RECORDS
* UNDEFINED RECFM
UNDEF    MVI   RECFM,C'U'
         BAL   6,HEADS
         LA    4,0
UNEXT    BAL   6,READIT
         LR    3,5
         LA  6,UNEXT
         B     TRT
* FIXED RECFM
FIXED    MVI   RECFM,C'F'     FIXED
         BAL   6,HEADS
         LA    4,0
FNEXTB   BAL   6,READIT
FNEXTR   LH    3,DCBLRECL
         SR    5,3
         LTR   5,5
         BM    FNEXTB
         LA    6,FNEXTR
         B     TRT
* VARIABLE RECFM
VAR      MVI   RECFM,C'V'
         BAL   6,HEADS
         LA    4,0
         TM    DCBRECFM,X'08'
         BZ    VNEXTB
         ABEND 400
*
VNEXTB   BAL   6,READIT
         CH    5,0(8)         LEN READ IN = BLKSIZE
         BE    OK1
         ABEND 500,DUMP       NO
OK1      LA    8,4(8)         STEP PAST BLKLEN
         SH    5,=H'4'
VNEXTR   LTR   5,5            END OF BLK?
         BNP   VNEXTB         YES
         MVC   SAVEAREA+2(2),0(8)       GET CURRENT LRECL
         LH    3,SAVEAREA+2
         SR    5,3
         LA    8,4(8)
         SH    3,=H'4'
         LA    6,VNEXTR
         B     TRT
         EJECT
READIT   LTR   4,4
         BZ    READREC
         LA    7,1(7)
         IC    2,=C'*'
         STC   2,PRINT1(7)
         STC   2,PRINT2(7)
         STC   2,PRINT3(7)
READREC  READ  INDECB,SF,INDCB,AREA,'S'
*
         CHECK INDECB
         LA    8,AREA
         LA    4,1(4)
         L     7,INDECB+16    IOB PTR
         LH    9,14(7)        CCW RESIDUE
         L     7,8(7)    NEXT CCW ADDR
         LTR   7,7            OK?
         BNZ   OK2
         ABEND 600,DUMP       NO
*
OK2      LA    7,0(7)         CLEAR HIGH ORDER BYTES
         LA    1,4                     SCAN COUNT
CCWNEXT  SH    7,=H'8'                 BACKUP TO PREVIOUS CCW
         CLI   0(7),X'86'              READ DATA?
         BE    CCWOKAY                 BRANCH IF YES
         CLI   0(7),X'06'              READ DATA?
         BE    CCWOKAY                 BRANCH IF YES
         CLI   0(7),X'02'              READ DATA?
         BE    CCWOKAY                 BRANCH IF YES
         CLI   0(7),X'0C'              READ DATA?
         BE    CCWOKAY
         BCT   1,CCWNEXT               SKIP BACK ANOTHER CCW
         B     ABEND7                  BRANCH IF NO READDATA
CCWOKAY  LH    5,6(7)                  LOAD LENGTH HALFWORD
         SR    5,9
         BP    0(6)
ABEND7    ABEND 700,DUMP
         EJECT
* BUILD  PRINT LINES
* REG USAGE FOR THIS RTN
*  REG 2   -   TRT CHAR
*  REG 3   -   LRECL OF CURRENT RECORD
*  REG 4   -   RECORD COUNT
*  REG 5   -   BLK LENGTH
*  REG 6   -   RETURN REG FROM THIS RTN
*  REG 7   -   COUNT OF CHARS ON THIS LINE
*  REG 8   -   BLOCK ADDRESS
*  REG 9   -   LINK REG TO PRINT RTN
*  REG 10  -   RESIDUAL RECORD COUNT
TRT      CP    RECNO,=P'0'
         BE    BAL2+4
         BCT   10,*+8
         B     EOJ
BAL2     BAL   9,PUTPRNT
         AP    RECNO,=P'1'
         MVI   PRINT1,C'-'
         MVC   INFO(23),INFO-1
         MVC   INFO+9(6),EDITPTN
         ED    INFO+9(6),RECNO
         CVD   3,DWORK                  LRECL
         MVC   INFO(6),EDITPTN
         ED    INFO(6),DWORK+5
         CVD   4,DWORK                  BLK NO
         MVC   INFO+17(6),EDITPTN
         ED    INFO+17(6),DWORK+5
* CLEAR THE LINES
NEXTLINE MVC   PRINT3+1(132),PRINT3
         MVC   PRINT1+1(132),PRINT3
         MVC   PRINT2+1(110),PRINT3
*
         LA    7,1
NEXTCHAR TRT   0(1,8),TRTAB
         STC   2,PRINT3(7)
         UNPK  DWORK(1),0(1,8)
         NI    DWORK,X'0F'
         NI    0(8),X'0F'
         TRT   DWORK(1),MINTRT
         STC   2,PRINT1(7)
         TRT   0(1,8),MINTRT
         STC   2,PRINT2(7)
* END OF CHAR PROCESSING
         LA    8,1(8)
         BCT   3,CHK100
* END OF RECORD PROCESSING
         LA    7,1(7)
         IC    2,=C'*'
         STC   2,PRINT1(7)
         STC   2,PRINT2(7)
         STC   2,PRINT3(7)
         BR    6
* CHECK FOR END OF LINE
CHK100   LA    7,1(7)
         C     7,=F'101'
         BNE   NEXTCHAR
         BAL   9,PUTPRNT
         MVC   INFO(23),INFO-1
         MVI   PRINT1,C'0'
         B     NEXTLINE
*
PUTPRNT  PUT   OUTDCB,PRINT1
         PUT   OUTDCB,PRINT2
         PUT   OUTDCB,PRINT3
         BR    9
         EJECT
* END OF INPUT ROUTINE
EOJ      CP    RECNO,=P'0'
         BNH   *+8
         BAL   9,PUTPRNT
         MVI   PRINT1+1,C' '
         MVC   PRINT1+2(131),PRINT1+1
        MVC  PRINT1+14(27),=C'END OF HEXADECIMAL PRINT OF'
         MVC   DSNAME(44),INJFCB
         MVC   DSNAME+39(19),=C'RECORDS PROCESSED ='
         MVC   DSNAME+58(6),EDITPTN
         ED    DSNAME+58(6),RECNO
         PUT   OUTDCB,PRINT1
*
         CLOSE (INDCB,,OUTDCB)
         L     13,SAVEAREA+4
         RETURN (14,12),,RC=0
         EJECT
SAVEAREA DS    0D
         DC    X'80'
         DC    XL199'00'
*
TRTEST   TRT   2(1,11),TRTAB
PACKIT   PACK  DWORK(8),2(1,11)
DWORK    DC    D'0'
         ORG   DWORK+5
NUMREQ   DS    CL3
         PRINT NOGEN
INDCB    DCB   DDNAME=INPUT,DSORG=PS,EODAD=EOJ,MACRF=R,EXLST=INEX
OUTDCB   DCB   DDNAME=OUTPUT,DSORG=PS,LRECL=133,RECFM=FBA,MACRF=PM,    X
               EXLST=OUTEX
INEX     DS    0F
         DC    X'07'
         DC    AL3(INJFCB)
OUTEX    DC    X'07'
         DC    AL3(OUTJFCB)
INJFCB   DS    176C
OUTJFCB  DS    176C
PRINT3   DC    CL133' '
PRINT1   DS    0CL133
         DC    CL21'1'
         DC    CL21'HEXADECIMAL PRINT OF '
DSNAME   DC    CL44' '
         DC    CL24' '
DAYID    DC    CL8'  /  /  '
         DC    CL15' '
*
PRINT2   DS    0CL133
         DC    CL16'0  VOL SER NO = '
VOLSER   DC    CL6' '
         DC    CL5' '
         DC    CL8'RECFM = '
RECFM    DC    CL5' '
         DC    CL2' '
         DC    CL8'LRECL = '
         ORG   *-1
LRECL    DC    CL6' '
         DC    CL2' '
         DC    CL10'BLKSIZE = '
         ORG   *-1
BLKSIZE  DC    CL6' '
         DC    CL38' '
INFO     DC    CL23'REC LEN  REC NO  BLK NO'
*
RECNO    DC    PL3'0'
MINTRT   DC    CL16'0123456789ABCDEF'
EDITPTN  DC    XL6'402020202120'
         LTORG
TRTAB    DC    256C'.'
         ORG   TRTAB+C' '
         DC    C' '
         ORG   TRTAB+C'.'
         DC    C'.<(+×&&'
         ORG   TRTAB+C'$'
         DC    C'$*);^-/'
         ORG   TRTAB+C','
         DC    C',%_>?'
         ORG   TRTAB+C':'
         DC    C':#@''="'
         ORG   TRTAB+C'A'
         DC   C'ABCDEFGHI'
         ORG   TRTAB+C'J'
         DC   C'JKLMNOPQR'
         ORG   TRTAB+C'S'
         DC    C'STUVWXYZ'
         ORG   TRTAB+C'0'
NUMERICS DC    XL10'00'
         ORG   *+6
         DS    0F
AREA     DC    30000X'00'
         DCBD  DSORG=PS
         END
