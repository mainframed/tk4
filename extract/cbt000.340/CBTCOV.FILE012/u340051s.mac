WHOHAS   CSECT
*
*
*  THIS  VERSION HAS BEEN WRITTEN FOR MVS/SP RELEASE 1.3 AND HAS BEEN
*  CHANGED FROM USING THE OLD MAJOR/MINOR/QEL QCBS TO USING THE
*  GQSCAN MACRO . BECAUSE OF THE DIFFICULTY OF PROVIDING ALL MAJOR
*  QNAMES ,ONLY DSN AND SELECTIVE MAJOR NAMES CAN NOW BE DISPLAYED.
*
*      REWRITTEN  FEB 1982  MJC
*
*      REVISION  MAY 1982 JAC
*
*    REVISED TO SHOW THE I.D. OF THE SYSTEM UPON WHICH THE RESOURCE
*    "OWNED".  ALSO, CORRECTED PROBLEM OF  NOT ALIGNING CORRECTLY
*    ON SECOND AND SUBSEQUENT RIBE'S.  ALSO TO SHOW WHEN IT IS A
*    RESERVE OR A RESERVE CONVERSION.
*    ALSO "REFINED" TO SHOW WHEN THE REQUEST IS SATISFIED (HAS USE OF)
*    OR NO (AWAITING USE OF), WHETHER THE ENQ IS SYSTEMS OR NOT ,
*    AND TO SHOW THE UCB 'ADDRESS' ON A RESERVE.
*    ALSO, WHEN ALTERED TO ALLOW THE USER TO SPECIFY BOTH THE QNAME
*    AND THE RNAME, OR JUST THE RNAME (WITH DEFAULT QNAME SYSDSN)
*    AS WELL AS ALLOWING THE USER TO SPECIFY QNAME ONLY.
*    IN THE CASE OF SPECIFYING A QNAME OF SPFEDIT AND AN RNAME
*    A MEMBER MAY BE SPECIFIED (IF THE DATASET IS A PDS)
*
*    EXAMPLES OF USE:
*
*    WHOHAS Q,SPFEDIT         - GIVES ALL 'JOBS' ENQ'D WITH
*                               QNAME OF SPFEDIT
*    WHOHAS Q,SPFEDIT TSO.ZSYS.CLIST(PROFILE)
*                             - GIVES ALL 'JOBS' ENQ'D WITH QNAME
*                         SPFEDIT AND RNAME TSO.ZSYS.CLIST.. .PROFILE
*    WHOHAS TSO.ZSYS.CLIST    - GIVES ALL 'JOBS' ENQ'D WITH QNAME
*                               SYSDSN AND RNAME TSO.ZSYS.CLIST
*
*
*    CODE ADDED TO DEAL LIST OUT ALL RESOURCES WHICH HAVE SOMEONE
*    WAITING FOR THEM
*                                      JAN 83    JEFF CLAXTON
*
*   MINOR AMENDMENT TO MAKE 'WAITING' THE OVERALL PARM DEFAULT **PL**
*                                      JUNE 83   PHIL LEWIS
***********************************************************************
*
*    THE TABLE 'QTABLE' MAY NEED TO HAVE ENTRIES ADDED TO IT
*    DEPENDING ON PROGRAM PRODUCTS INSTALLED AND ALSO SUCH PRODUCTS
*    AS CMF, DATAMANAGER AND OTHERS.
*
*
*
*
***********************************************************************
         EJECT
         SAVE  (14,12),,*
         BALR  R12,0
         USING *,R12
         USING RIB,R9
         USING RIBE,R7
         ST    R13,SAVE+4
         LR    R11,R13
         LA    R13,SAVE
         ST    R13,8(R11)
*
* ANALYSE PARAMETERS
*
         L     R2,0(R1)           R2=PARMS
         SR    R7,R7              GET LENGTH OF BUFFER
         LH    R7,0(R2)
         SPACE
         USING PSA,R0
         L     R8,PSAAOLD         R8-> ASCB
         USING ASCB,R8
         L     R9,ASCBOUCB        R9-> OUCB
*        USING R9,OUCB
*        TM    OUCBYFL,OUCBLOG    LOGON CREATED USER??
         TM    18(R9),X'20'         "     "       "   ??
         BO    CP                 YES  SO IT'S A C.P.
         SPACE
STC      MVI   STCSW,X'FF'        OTHERWISE IT'S A STC - SHOW IT SO
         CH    R7,=H'0'
         BE    DEFPARM            NO PARM THEN ASSUME 'WAITING' **PL**
         LA    R2,2(R2)           ALIGN ON PARM
         B     GETNAME            AND GET THE DATA SET NAME
         SPACE
CP       CH    R7,=H'10'          IS THERE A PARM
         BNH   DEFPARM            NO  ASSUME 'WAITING'  **PL**
NOTSPF   SR    R3,R3              CLEAR WORK REG
         LH    R3,2(R2)           GET OFFSET IN BUFFER OF PARM
         LA    R2,4(R2)           JUMP ROUND BUFFER PREFIX
         LA    R2,0(R3,R2)        ALIGN ON PARMS
         LA    R3,4(R3)           GET LENGTH
         SR    R7,R3                OF  DATASET
         BZ    DEFPARM            TREAT AS WAITING     **PL**
GETNAME  BCTR  R7,0
         EX    R7,EXOC
         B     BEGINQ
EXOC     OC    0(1,R2),BLANKS     MAKE UPPER CASE
         SPACE 2
*
*  DETERMINE WHICH TYPE OF PROCESSING..DSN OR SELECTIVE QUEUE .
*
         SPACE 2
DEFPARM  LA    R11,QTABLE            POINT TO TABLE
         MVI   WAITSW,X'FF'          AND SHOW DEALING WITH WAIT
         B     SELECTQ
BEGINQ   CLC   0(2,R2),=C'Q,'         IS IT Q,
         BNE   RNAME2              ***JAC   NO  GET RNAME
         MVC   SYSDSN,BLANKS        * BLANK OUT QNAME
         BCTR  R7,0                 *
         LA    R4,2(R2)             * POINT TO START OF QNAME
QLOOP    CLI   0(R4),X'40'          * IS CHAR BLANK
         BE    ENDQ
         LA    R4,1(R4)               POINT TO NEXT
         BCT   R7,QLOOP               DECREASE REM LENGTH AND GO BACK
ENDQ     LR    R6,R4
         SR    R6,R2                  FIND LENGTH OF QNAME
         S     R6,=F'3'
         EX    R6,MOVEQCB             AND MOVE IT IN
         SPACE
         LTR   R7,R7                  IS THERE ANY MORE BUFFER
         BZ    SELECTQ
         SPACE
BLANK    CLI   0(R4),X'40'            SKIP BLANKS
         BNE   RNAME1                 NON-BLANK   SO RNAME
         LA    R4,1(R4)               POINT TO NEXT
         BCT   R7,BLANK               END OF PARM? NO TRY AGAIN
         B     SELECTQ                YES- GO TO SELECTQ
         SPACE
RNAME1   LR    R2,R4                   POINT TO CURRENT PART OF PARM
         BCTR  R7,0                    SET LENGTH CORRECTLY
RNAME2   LA    R4,1(R2)                POINT TO NEXT CHAR
         LTR   R7,R7                   IS COUNT 0 LENGTH TO END FROM R4
         BZ    ENDR                    YES SO END OF RNAME
RLOOP    CLI   0(R4),X'40'             IS IT BLANK
         BE    ENDR                    YES - DEAL WITH DATASET NAME
         CLI   0(R4),C'('              OR A LEFT BRACKET
         BE    ENDR                    DEAL WITH DATASET NAME
         LA    R4,1(R4)                POINT TO NEXT CHAR
         BCT   R7,RLOOP                IS MORE REMAINING,YES- LOOP BACK
ENDR     LR    R7,R4                   FIND LENGTH. END +1
         SR    R7,R2                   MINUS BEGINNING
         BCTR  R7,0                    DECREASE FOR EXECUTE
         EX    R7,MOVEDSN              SAVE DATASET NAME
         LA    R7,1(R7)                INCREMENT TO TRUE LENGTH
         SPACE
         CLC   SYSDSN,=CL8'SPFEDIT '   IS IT SPFEDIT?
         BNE   OUTMESS                 NO OUTPUT THE MESSAGE
         CLI   0(R4),C'('              IS IT A MEMBER?
         BNE   SPFLEN                  NO - JUST SET RNAME LEN
         BCT   R7,MEMB1                DECREASE LENGTH (FOR '(')
         B     SPFLEN
MEMB1    LA    R2,1(R4)                POINT TO FIRST CH. OF MEMBER
         LR    R4,R2
BRAKLOOP CLI   0(R4),C')'              SCAN FOR RIGHT BRACKET
         BE    ENDBRAK
         CLI   0(R4),X'40'             OR A BLANK
         BE    ENDBRAK
         LA    R4,1(R4)                POINT TO NEXT CH.
         BCT   R7,BRAKLOOP             OR FOR END OF BUFFER
ENDBRAK  LR    R6,R4                 * FIND LENGTH OF MEMBER NAME
         SR    R6,R2                 *
         BZ    SPFLEN                * NO MEMBER NAME SPECIFIED
         BCTR  R6,0                  * DECREASE LENGTH FOR EXECUTE
         EX    R6,MOVEMEMB           * AND MOVE MEMBER NAME IN
SPFLEN   LA    R7,52                ***JAC   AND SET RNAME LENGTH TO 52
*
* ISSUE  MSG INFORMING REQUESTOR OF THE DATASET NAME
*
OUTMESS  CLI   STCSW,X'FF'           IS IT A STC?
         BE    OUTMESS1              YES DO A WTO
         TPUT  WTORES,80             NO DO A TPUT
         B     GETRIB
OUTMESS1 WTO   MF=(E,RESWTO)
*
*  LOCATE MAJOR NAME SYSDSN CONTROL BLOCKS
*
GETRIB   GQSCAN AREA=(OURRIB,1024),SCOPE=ALL,TOKEN=TOKADDR,            X
               RESNAME=(SYSDSN,DSN,(7))
         SPACE 2
         STM   R0,R1,REG0         STORE REGS
         LTR   R15,R15            WAS REQUEST SUCCESSFUL
         BZ    FINDUSER           YES - GO AND GET THE NAMES OF USERS
         SPACE 2
         C     R15,=F'4'          WAS THERE ANYBODY USING RESOURCE?
         BE    NOUSERS            NO - EXIT WITH MSG
         SPACE 2
         C     R15,=F'8'          IS THERE MORE DATA TO COME?
         BNE   QCBERROR           NO - RESOURCE IS INCORRECT SO EXIT
         MVI   MORE,X'FF'         INDICATE MORE TO COME
         SPACE 2
FINDUSER LA    R9,OURRIB          ALIGN ON RIB(RESOURCE INFO BLOCK)
         BAL   R6,GETUSER         GO AND GET THE INFORMATION
         CLI   MORE,X'FF'         ANY MORE TO DISPLAY
         BNE   OUT                NO - THEN EXIT NORMALLY
         MVI   MORE,X'00'         TURN OFF SWITCH TO INDICATE MORE
         GQSCAN AREA=(OURRIB,1024),SCOPE=ALL,TOKEN=TOKADDR,            X
               RESNAME=(SYSDSN,DSN,(7))
         SPACE 2
         STM   R0,R1,REG0         STORE REGS
         LTR   R15,R15            WAS REQUEST SUCCESSFUL
         BZ    FINDUSER           YES - GO AND GET THE NAMES OF USERS
         SPACE 2
         C     R15,=F'4'          WAS THERE ANYBODY USING RESOURCE?
         BE    NOUSERS            NO - EXIT WITH MSG
         SPACE 2
         C     R15,=F'8'          IS THERE MORE DATA TO COME?
         BNE   QCBERROR           NO - RESOURCE IS INCORRECT SO EXIT
         MVI   MORE,X'FF'         INDICATE MORE TO COME
         B     FINDUSER
         EJECT
*
*  PROCESSING OF A PARTICULAR QCB NAME
*
SELECTQ  DS    0H
         CLI   WAITSW,X'FF'          ARE WE LISTING WAITING RESOURCES?
         BNE   MOVEDSN1              NO
         CLI   0(R11),X'FF'          AT END OF Q NAME LIST
         BE    NOWAIT                YES- MESSAGE IF NO-ONE WAITING
         MVC   SYSDSN,0(R11)         ELSE MOVE IN QNAME
         B     LOCQCBW               AND DO GQSCAN PROCESSING
MOVEDSN1 MVC   DSN(8),SYSDSN
QCBRES   CLI   STCSW,X'FF'           IS IT A STC?
         BE    QCBRES1               YES DO A WTO
         TPUT  WTORES,80             NO DO A TPUT
         B     QCBRES2
QCBRES1  WTO   MF=(E,RESWTO)
         SPACE 3
QCBRES2  CLI   WAITSW,X'FF'          SHOWING WAITING RESOURCES?
         BE    QCBUSER               SKIP BACK TO OUTPUT INFO.
LOCQCB   GQSCAN MF=(E,QNAMLIST)
         SPACE 2
         STM   R0,R1,REG0         STORE REGS
         LTR   R15,R15            WAS REQUEST SUCCESSFUL
         BZ    QCBUSER            YES - GO AND GET THE NAMES OF USERS
         SPACE 2
         C     R15,=F'4'          WAS THERE ANYBODY USING RESOURCE?
         BE    NOUSERS            NO - EXIT WITH MSG
         SPACE 2
         C     R15,=F'8'          IS THERE MORE DATA TO COME?
         BNE   QCBERROR           NO - RESOURCE IS INCORRECT SO EXIT
         MVI   MORE,X'FF'         INDICATE MORE TO COME
         B     QCBUSER
         SPACE 2
LOCQCBW  GQSCAN MF=(E,WAITLIST)
         SPACE 2
         STM   R0,R1,REG0         STORE REGS
         LTR   R15,R15            WAS REQUEST SUCCESSFUL
         BZ    MOVEDSN1           YES - OUTPUT QNAME AND USERS' NAMES
         SPACE 2
         C     R15,=F'4'          WAS THERE ANYBODY USING RESOURCE?
         BNE   RC8                YES THEN DEAL WITH THEM
         LA    R11,8(R11)         ELSE POINT TO NEXT Q NAME
         B     SELECTQ            AND DEAL WITH THAT QUEUE
         SPACE 2
RC8      C     R15,=F'8'          IS THERE MORE DATA TO COME?
         BNE   QCBERROR           NO - RESOURCE IS INCORRECT SO EXIT
         MVI   MORE,X'FF'         INDICATE MORE TO COME
         SPACE 2
QCBUSER  LA    R9,OURRIB          ADDRESS INFORMATION BLOCK
         BAL   R6,GETUSER         GO AND INTERROGATE CONTROL BLK
         CLI   MORE,X'FF'         ANY MORE?
         BE    CLRMORE            YES THEN CLEAR THE FLAG BEFORE RETRY
         CLI   WAITSW,X'FF'       ARE WE DEALING WITH 'WAITING'
         BNE   OUT                NO-THEN EXIT NORMALLY
         LA    R11,8(R11)         POINT TO NEXT Q-NAME
         B     SELECTQ            AND DEAL WITH THAT
CLRMORE  MVI   MORE,X'00'         CLEAR MORE FLAG
         CLI   WAITSW,X'FF'       DEALING WITH WAITING??
         BE    LOCQCBW1
         GQSCAN MF=(E,QNAMLIST)
         SPACE 2
         STM   R0,R1,REG0         STORE REGS
         LTR   R15,R15            WAS REQUEST SUCCESSFUL
         BZ    QCBUSER            YES - GO AND GET THE NAMES OF USERS
         SPACE 2
         C     R15,=F'4'          WAS THERE ANYBODY USING RESOURCE?
         BE    OUT                NO - EXIT
         SPACE 2
         C     R15,=F'8'          IS THERE MORE DATA TO COME?
         BNE   QCBERROR           NO - RESOURCE IS INCORRECT SO EXIT
         MVI   MORE,X'FF'         INDICATE MORE TO COME
         B     QCBUSER                                              SEB
         SPACE 2
LOCQCBW1 GQSCAN MF=(E,WAITLIST)
         SPACE 2
         STM   R0,R1,REG0         STORE REGS
         LTR   R15,R15            WAS REQUEST SUCCESSFUL
         BZ    QCBUSER            YES - GO AND GET THE NAMES OF USERS
         SPACE 2
         C     R15,=F'4'          WAS THERE ANYBODY USING RESOURCE?
         BNE   RC8A               YES THEN DEAL WITH THEM
         LA    R11,8(R11)         ELSE POINT TO NEXT Q NAME
         B     SELECTQ            AND DEAL WITH THAT QUEUE
         SPACE 2
RC8A     C     R15,=F'8'          IS THERE MORE DATA TO COME?
         BNE   QCBERROR           NO - RESOURCE IS INCORRECT SO EXIT
         MVI   MORE,X'FF'         INDICATE MORE TO COME
         B     QCBUSER            AND DEAL WITH IT
         SPACE 2
         EJECT
*
* INFORM USER  DATA SET NOT ENQUEUED
*
NOUSERS  CLI   STCSW,X'FF'                  IS IT AN STC
         BE    NOUSERS1
         TPUT  NOU,60                       NO DO A TPUT
         B     OUT
NOUSERS1 WTO   MF=(E,NOUWTO)                YES DO A WTO
         B     OUT
*
* INFORM USER  NO TASKS ARE WAITING FOR RESOURCES
*
NOWAIT   CLI   OUTSW,X'FF'                  HAVE WE OUTPUT ANYTHING
         BE    OUT                          NO  DONT OUTPUT A MESSAGE
         CLI   STCSW,X'FF'                  IS IT AN STC
         BE    NOWAIT1
         TPUT  NOW,60                       NO DO A TPUT
         B     OUT
NOWAIT1  WTO   MF=(E,NOWWTO)                YES DO A WTO
         B     OUT
*
* INVALID QCBS OR ERROR PROCESSING GQSCAN
*
QCBERROR CLI   STCSW,X'FF'                 IS IT A STC??
         BE    QCBEROR1
         TPUT  BADQCB,60                   NO  DO A TPUT
         B     OUT
QCBEROR1 WTO   MF=(E,QCBWTO)               YES  DO A WTO
         B     OUT
         EJECT
*
* EXTRACT THE REQUIRED INFORMATION FROM RIB AND INFORM REQUESTOR
*
GETUSER  EQU   *
         MVI   OUTSW,X'FF'        SHOW THAT WE'VE OUTPUT SOMETHING
         L     R2,REG1            R2= # OF RIB'S (FROM GQSCAN)
         L     R0,REG0            AND RESTORE REGISTER 0
         STM   R3,R7,USERSAVE     SAVE WORK REGS
         SR    R3,R3              ZEROISE WORK REGS
         SR    R4,R4              ZEROISE WORK REGS
         SR    R6,R6              ZEROISE WORK REGS
         STCM  R0,15,RIBL         STORE LENGTH OF RIB AND RIBE
         ICM   R3,3,RIBL          GET LENGTH OF FIXED RIB
         ICM   R4,3,RIBL+2        GET LENGTH OF FIXED RIBE
GETRIBN  L     R5,RIBNRIBE        NUMBER OF RIBES IN THIS AREA
         LA    R5,0(R5)
         ICM   R6,3,RIBVLEN       LENGTH OF VARIABLE RIB
         LA    R8,0(R3,R9)        ALIGN ON VARIABLE PART OF RIB
*        CLI   OPT,X'FF'          IS IT Q PROCESSING?
*        BNE   GETJOBNM           NO - SKIP TPUT
         BCTR  R6,0               SET UP EXECUTE
         EX    R6,MINMOVE         MOVE QNAME INTO TPUT
         LA    R6,1(R6)           RESTORE TRUE LENGTH
         CLI   STCSW,X'FF'        IS IT A STC
         BE    WTOMIN1
         TPUT  WTOMIN,80          DO THE TPUT
         B     GETJOBNM
WTOMIN1  WTO   MF=(E,MINWTO)      DO A WTO
GETJOBNM LA    R7,0(R6,R8)        ALIGN ON RIBE
         MVC   JOB(8),RIBEJBNM    MOVE OUT JOBNAME
         MVC   ENQTYPE(6),=C'SHARED'  DETERMINE TYPE OF REQUEST
         TM    RIBERFLG,RIBETYPE   IS IT SHARED
         BO    SCOPE                 YES- FIND OUT SCOPE
         MVC   ENQTYPE(6),=C'EXCL  ' SHOW EXCLUSIVE
SCOPE    MVC   S(1),BLANKS            ***JAC*** RESET TO SYSTEM
         TM    RIBSCOPE,RIBSYSS       *  FIND SCOPE
         BNO   STAT                   *  IF NOT SYSTEMS, SKIP
         MVC   S(1),=C'S'             *  SHOW SYSTEMS
STAT     MVC   STATUS(8),=CL8'     HAS' *  SHOW THAT HAS RESOURCE
         TM    RIBESFLG,RIBESTAT      *  IS IT WAITING FOR RESOURCE
         BO    WHICHSYS               *  NO - SKIP
         MVC   STATUS(8),=CL8'AWAITING' *  YES - SHOW AWAITING
WHICHSYS MVC   SYSTEM(6),RIBESYSN     *  AND WHICH SYSTEM
         TM    RIBERFLG,RIBERESC      *  RESERVE CONVERSION??
         BNO   RES                    *  NO,IS IT RESERVE??
         MVC   RESMESS(18),RESCON     *  SET MESSAGE FOR RES-CONV
         B     WTOUSER                *  AND TELL THE USER
RES      TM    RIBERFLG,RIBERESV      *  IS IT A RESERVE?
         BNO   WTOUSER                *  NO OUTPUT PLAIN MESSAGE
         MVC   RESMESS(8),RESCON      *  YES, SHOW IT IS SO
         L     R10,RIBEUCB            * AND FIND UCB ADDRESS
         USING UCBCMSEG,R10           *  AND GET UNIT NAME FROM UCB
         MVC   RESMESS+10(3),UCBNAME  *
*                                     ***JAC***
WTOUSER  CLI   STCSW,X'FF'              IS IT A STC
         BE    WTOUSER1
         TPUT  NAME,80                  NO DO THE TPUT
         B     WTOUSER2
WTOUSER1 WTO   MF=(E,NAMEWTO)           YES DO THE WTO
WTOUSER2 MVC   WTOMIN+22(44),BLANKS    BLANK OUT QNAME
         MVC   RESMESS(20),BLANKS      BLANK OUT RESERVE MESSAGE
         BCT   R5,NEXTRIBE        ANY MORE RIBES
         BCTR  R2,0               DECREASE COUNT OF RIB'S
         LTR   R2,R2              TEST IT
         BZ    GOBACK             AND IF ZERO RETURN
         LA    R9,0(R4,R7)        ALIGN ON NEXT RIB
         CLC   RIBQNAME,SYSDSN    IS IT OUR QNAME
         BE    GETRIBN            GO AND GET NEXT RIB ENTRIES
GOBACK   LM    R3,R7,USERSAVE
         BR    R6                 RETURN
NEXTRIBE LA    R8,0(R4,R7)        NEXT RIBE
         LA    R6,0               ***JAC***
         B     GETJOBNM
         EJECT
OUT      L     R13,SAVE+4
         RETURN (14,12),RC=0
         TITLE 'CONSTANTS AND VARIABLES ETC.....'
SAVE     DC    18F'0'
USERSAVE DC    5F'0'
REG0     DC    F'0'
REG1     DC    F'0'
MOVEDSN  MVC   DSN(0),0(R2)
MOVEMEMB MVC   MEMBER(0),0(R2)
MOVEQCB  MVC   SYSDSN(0),2(R2)
COMPDSN  CLC   DSN(0),20(R4)
MINMOVE  MVC   WTOMIN+22(0),0(R8)      MOVE OUT MINOR NAME
         SPACE 2
RESWTO   DS    0F
         DC    AL1(0)
         DC    AL1(88)                LENGTH
         DC    X'8000'                MCS FLAGS - CODES FOLLOW MESSAGE
WTORES   DC    CL80'WHOHAS........RESOURCE IS   '
         DC    X'4000'                ROUTING CODES
         DC    X'0000'                DESCRIPTOR CODES
         SPACE 2
MINWTO   DS    0F
         DC    AL1(0)
         DC    AL1(88)                LENGTH
         DC    X'8000'                MCS FLAGS - CODES FOLLOW MESSAGE
WTOMIN   DC    CL80'  MINOR RESOURCE IS  '
         DC    X'4000'                ROUTING CODES
         DC    X'0000'                DESCRIPTOR CODES
         SPACE 2
NAMEWTO  DS    0F
         DC    AL1(0)
         DC    AL1(88)                LENGTH
         DC    X'8000'                MCS FLAGS - CODES FOLLOW MESSAGE
NAME     DC    CL40'       XXXXXXX       HAS USE OF X '
         DC    CL40'  SYSTEM                          '
         DC    X'4000'                ROUTING CODES
         DC    X'0000'                DESCRIPTOR CODES
         SPACE 2
NOUWTO   DS    0F
         DC    AL1(0)
         DC    AL1(68)                LENGTH
         DC    X'8000'                MCS FLAGS - CODES FOLLOW MESSAGE
NOU      DC    CL60'WHOHAS.....NO  USERS FOUND WITH THIS RESOURCE'
         DC    X'4000'                ROUTING CODES
         DC    X'0000'                DESCRIPTOR CODES
         SPACE 2
NOWWTO   DS    0F
         DC    AL1(0)
         DC    AL1(68)                LENGTH
         DC    X'8000'                MCS FLAGS - CODES FOLLOW MESSAGE
NOW      DC    CL60'WHOHAS.....NO  USERS AWAITING A RESOURCE'
         DC    X'4000'                ROUTING CODES
         DC    X'0000'                DESCRIPTOR CODES
         SPACE 2
QCBWTO   DS    0F
         DC    AL1(0)
         DC    AL1(68)                LENGTH
         DC    X'8000'                MCS FLAGS - CODES FOLLOW MESSAGE
BADQCB   DC    CL60'WHOHAS.....ERROR PROCESSING ENQ/DEQ CONTROL BLK'
         DC    X'4000'                ROUTING CODES
         DC    X'0000'                DESCRIPTOR CODES
DSN      EQU   WTORES+27
MEMBER   EQU   WTORES+27+44
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RIBL     DC    F'0'
SYSDSN   DC    CL8'SYSDSN  '
RNAME    DC    CL44'         '
BLANKS   DC    CL80' '
RESCON   DC    CL18'RESERVE CONVERSION'
TOKADDR  DC    F'0'
MORE     DC    X'00'
OPT      DC    X'00'
STCSW    DC    X'00'                        SWITCH TO SHOW STARTED TASK
WAITSW   DC    X'00'               SWITCH TO SHOW DEALING WITH WAITING
OUTSW    DC    X'00'                SWITCH TO SHOW WE'VE DONE SOME O/P
JOB      EQU   NAME+7
STATUS   EQU   NAME+16
ENQTYPE  EQU   NAME+32
S        EQU   NAME+48
SYSTEM   EQU   NAME+50
RESMESS  EQU   NAME+60
QNAMLIST GQSCAN AREA=(OURRIB,1024),SCOPE=ALL,TOKEN=TOKADDR,            X
               RESNAME=(SYSDSN),MF=L
WAITLIST GQSCAN AREA=(OURRIB,1024),SCOPE=ALL,TOKEN=TOKADDR,            X
               RESNAME=(SYSDSN),WAITCNT=1,MF=L
QTABLE   DS    0D
         DC    CL8'SYSDSN  '
         DC    CL8'SPFDSN  '
         DC    CL8'SPFEDIT '
         DC    CL8'IKJTMPNM'
         DC    CL8'SYSIEWL '
         DC    CL8'SYSIAT  '
         DC    CL8'SYSIEA01'
         DC    CL8'SYSIEFSD'
         DC    CL8'SYSIGGV1'
         DC    CL8'SYSIGGV2'
         DC    CL8'SYSIKJBC'
         DC    CL8'SYSIKJUA'
         DC    CL8'SYSIEWLP'
         DC    CL8'SYSSMF01'
         DC    CL8'SYSVSAM '
         DC    CL8'SYSVTOC '
         DC    CL8'SYSZCAXW'
         DC    CL8'SYSZCMDS'
         DC    CL8'SYSZCOMM'
         DC    CL8'SYSZCSD '
         DC    CL8'SYSZEC16'
         DC    CL8'SYSZIGGI'
         DC    CL8'SYSZJWTP'
         DC    CL8'SYSZOPEN'
         DC    CL8'SYSZPCCB'
         DC    CL8'SYSZPGAD'
         DC    CL8'SYSZPSWD'
         DC    CL8'SYSZRBMF'
         DC    CL8'SYSZRPLW'
         DC    CL8'SYSZSIPS'
         DC    CL8'SYSZSMF1'
         DC    CL8'SYSZTIOT'
         DC    CL8'SYSZUSRL'
         DC    CL8'SYSZVARY'
         DC    CL8'SYSZVMV '
         DC    CL8'SYSZVOLS'
         DC    CL8'SYSZWTOR'
         DC    X'FF'
         LTORG
         DS    0D
OURRIB   DS    CL1024
         EJECT
         ISGRIB
         EJECT
         IEFUCBOB
         EJECT
         IHAASCB
         EJECT
         PRINT NOGEN
         EJECT
         IHAPSA
*        IRAOUCB
         END
