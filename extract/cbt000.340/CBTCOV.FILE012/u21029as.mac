*
* Z817SET IS USED TO WRITE A MSG(PASSED BY EXEC STMT PARM FIELD)
*         TO THE OPERATORS CONSOLE.
*
*         IF THE PARM FIELD IS OMITTED, OR 'NOMSG' APPEARS IN THE
*         PARM FIELD, THEN NO MESSAGE IS OUTPUT.
*
*         IF 'REPLY' APPEARS IN THE PARM FIELD THEN THE MESSAGE IS
*         OUTPUT AND REPLY 'OK' IS AWAITED.
*         (NB. THE PARM FIELD SHOULD BE OF THE FORM
*         'MESSAGE ,REPLY OK TO CONTINUE' ).
*
*   MAY BE USED IN CONJUNCTION WITH Z817TEST TO SET A 'FLAG' ENTRY
*     IN A SYSTEM VOLUME VTOC THUS PROVIDING JOB DEPENDENCY TESTING
*       FACILITIES
*
**   AUTHORS - I.C.I. (MOND DIVISION)
*
         TITLE '*** Z817SET ***'
Z817SET  START
         SAVE  (14,12),,*
         LR    12,15          BASE
         USING Z817SET,12
         CNOP  0,4
         BAL   15,*+76
         DC    18A(0)         SAVE
         ST    13,4(15)
         ST    15,8(13)
         LR    13,15          CHAIN
         SPACE 3
         L     9,0(1)    REG9 -> PARM FIELD
         ST    9,PARMADDR
         SR    8,8
         AH    8,0(9)    REG8 = LENGTH OF PARM FIELD
         BZ    NOMSG
* SCAN PARM FIELD FOR 'NOMSG' OR 'REPLY'
         S     8,=F'6'   REG8 = LENGTH OF SCAN
         BNP   SCAN02
SCAN01   EQU   *
         CLC   2(5,9),=C'NOMSG'
         BE    NOMSG
         CLC   2(5,9),=C'REPLY'
         BE    REPLY
         LA    9,1(9)
         BCT   8,SCAN01
SCAN02   EQU   *
         L     9,PARMADDR
         LH    8,0(9)
         BCTR  8,0
         EX    8,MOVE
         A     8,=F'5'
         STH   8,MESSAGE
SWITCH   BC    0,WTOR
WTO      EQU   *
         WTO   MF=(E,MESSAGE)
         B     NOMSG
WTOR     EQU   *
         WTOR  ,REP,2,ECB,MF=(E,MESSAGER)
         XC    ECB,ECB
         WAIT  ECB=ECB
         CLC   REP,=C'OK'
         BNE   WTOR
NOMSG    EQU   *
         L     13,4(13)
         RETURN (14,12),,RC=0
REPLY    EQU   *
         OI    SWITCH+1,X'F0'
         B     SCAN02
         SPACE 3
* DATA
MOVE     MVC   MESSAGE+4(1),2(9)
PARMADDR DC    F'0'
ECB      DC    F'0'
MESSAGER DC    2F'0'
MESSAGE  DC    26F'0'
REP      DC    CL2' '
         LTORG
         END
