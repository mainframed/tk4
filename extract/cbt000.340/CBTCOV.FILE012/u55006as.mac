*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. FASTSORT.
SORTMOD ENTER
         LM    11,12,0(1)          ADDR TABLE, ADDR CONTROL AREA
         USING SCONTROL,12
         LM    2,6,$SNUM
         ST    2,KNUM
         ST    4,KLEN
         STCM  6,8,KTYPE
*        NOW GET WORK AREA TO HOLD POINTERS
         LA    0,2(0,2)
         SLL   0,3                 8*N+16
         ST    0,WAMT
         GETMAIN EC,LV=(0),A=ADWTAB
         LTR   15,15
         BNZ   EC16                SORT FAILURE
         L     1,ADWTAB            ADDR OF WORK TABLE
         BCTR  5,0                 KEY DISPL
         LR    7,5
         ALR   5,11                1ST KEY ADDRESS
SMODA    ST    5,0(0,1)
         ALR   5,3                 ADDRESS OF NEXT KEY
         LA   1,4(0,1)
         BCT   2,SMODA
         GOTO  CSORTA,ADPARMK
         L     0,KNUM
         L     1,ADWTAB
         LR    3,7
         LR    7,0
         SLL   7,2
         ALR   7,1
         LR    8,7                 WORK AREA
         SR    10,10
         LR    4,10
SMODB    L     5,0(0,1)
         SLR   5,3                 REC ADDR
         SLR   5,11                DISPL
         D     4,$SRECL
         SLL   5,2                 REL REC NO. 0=1ST,4=2ND ETC
         ST    5,0(0,1)
         LA    1,4(0,1)
         ST    10,0(5,7)           PERM ARRAY
         LA    10,4(0,10)
         BCT   0,SMODB
*        NOW CARRY OUT INTERCHANGE
         SH    10,=H'4'            MAX DISP IN POINTER TABLE
         LR    9,0
SMODC    EQU   *
         L     6,0(8,9)            K=P(I)
         CLR   6,9
         BE    SMODD               RECORD IN CORRECT PLACE
         LR    1,9
         SRL   1,2
         MH    1,$SRECL+2
         ALR   1,11
         LR    7,1
SMODF    LR    2,6
         SRL   2,2
         MH    2,$SRECL+2
         ALR   2,11
         LH    0,$SRECL+2
         BCTR  0,0
         STC   0,LASTSWAP+1
         STC   0,LASTSWAP+7
         SRA   0,8
         BNH   LASTSWAP
SMODE    XC    0(256,1),0(2)
         XC    0(256,2),0(1)
         EX    0,*-12
         LA    1,256(0,1)
         LA    2,256(0,2)
         BCT   0,SMODE
LASTSWAP XC    0(0,1),0(2)
         XC    0(0,2),0(1)
         EX    0,*-12
         L     0,0(8,6)            L=P(K)
         ST    6,0(8,6)            P(K)=K
         LR    6,0                 K=L
         CLR   6,9
         BE    SMODD
         LR    1,7
         B     SMODF
SMODD    CLR   9,10
         BNL   SMODX
         LA    9,4(0,9)
         B     SMODC
SMODX    LM    0,1,WAMT
         FREEMAIN R,LV=(0),A=(1)
         SR    15,15
SORTOUT  LEAVE (15)
EC16     LA    15,16
         B     SORTOUT
WAMT     DS    F
ADPARMK  DS    0F
ADWTAB   DS    F
ADKAREA  DC    A(KPARM)
KPARM    DS    0F
KNUM     DS    F
KLEN     DS    F
KTYPE    DS    C
         LTORG
SCONTROL DSECT
$SNUM    DS    F
$SRECL   DS    F
$SKLEN   DS    F
$SKPOSN  DS    F
$SKTYPE  DS    C
         END
