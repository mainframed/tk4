*                  SYSGEN STAGE 1 OUTPUT COMPARISON
*                  --------------------------------
*
*     AUTHOR: PETE LAVERICK, EAGLE STAR INS.CO.LTD.,CHELTENHAM.
*
*     SPSG IS A PROGRAM TO COMPARE STAGE 1 OUTPUT OF TWO SYSTEM
*
*        GENERATIONS OF A SIMILAR TYPE  (I.E. NUCLEUS, I/O,
*
*        OR FULL GENERATION).  IT LISTS THE 'NEW' STAGE 1 OUTPUT
*
*        HIGHLIGHTING ANY DIFFERENCES.  ONLY THE CONTROL CARDS ARE
*
*        CHECKED AND STEPS CONTAINING IEHPROGM, IFCDIP00, AND IEHLIST
*
*        ARE IGNORED.
*
*
*
*     SPSG USES THREE FILES - OLD AND NEW, BOTH (FB 80 3200) ARE
*
*        THE TWO STAGE 1 OUTPUT FILES, AND PRINT (FMB 133 665) IS THE
*
*        SYSOUT FILE. THE OLD AND NEW DATASETS MAKE TWO PASSES,THE
*
*        FIRST TO MATCH UP JOB STEPS AND BUILD A TABLE OF STEPS, AND
*
*        THEN AGAIN TO COMPARE INDIVIDUAL CONTROL CARDS AND PRINT THE
*
*        NEW FILE.
*
*        AMENDED MAY '77 TO CATER FOR IMS STAGE 1 COMPARISON.   P.L.
*
*     SAMPLE JCL
*     ----------
*
*        //SPSG EXEC PGM=SPSG
*
*        //STEPLIB DD DSN=...
*
*        //OLD DD DSN=STAGE1.OUTPUT.LAST,DISP=OLD
*
*        //NEW DD DSN=STAGE1.OUTPUT.NEW,DISP=OLD
*
*        //PRINT DD SYSOUT=A
*
*        //SYSUDUMP DD SYSOUT=A
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE '      SPSG SYSGEN STAGE 1 OUTPUT COMPARISON'
SPSG     CSECT
R0       EQU   0            USE - CONVENTIONAL
R1       EQU   1            USE - CONVENTIONAL
R2       EQU   2            USE - GENERAL
R3       EQU   3            USE - START OF CURRENT ENTRY IN STEPTABLE
R4       EQU   4            USE - GENERAL
R5       EQU   5            USE - AVAILABLE
R6       EQU   6            USE - RETURN REGISTER IN BAL INSTRUCTIONS
R7       EQU   7            USE - AVAILABLE
R8       EQU   8            USE - AVAILABLE
R9       EQU   9            USE - AVAILABLE
R10      EQU   10           USE - GENERAL
R11      EQU   11           USE - GENERAL
R12      EQU   12           USE - SECOND BASE REGISTER
R13      EQU   13           USE - BASE REGISTER
R14      EQU   14           USE - CONVENTIONAL
R15      EQU   15           USE - CONVENTIONAL
         USING *,15
         STM   14,12,12(13)
         LR    14,13
         CNOP  0,4
         BAL   13,*+88
         USING *,13
         USING *+8192,12
         USING TBLENTRY,R3
         DS    18F
         DC    CL12'PROGRAM SPSG'
         ST    13,8(14)
         ST    14,4(13)
         LA    R12,4095(R13)
         LA    R12,4095(R12)
         LA    R12,2(R12)
         DROP  15
         LA    R3,STEPTBLE
         MVC   TENTRY,NULLNTRY       BLANK OUT FIRST TABLE ENTRY
#1       EQU   *
*        OPEN OLD & NEW FOR FIRST PASS
         OPEN  (OLD,(INPUT),NEW,(INPUT))
         SR    R4,R4                    ZEROISE CONT CARD COUNT
#2       EQU   *
*        READ NEW
         GET   NEW,NEWREC
#4       EQU   *
*        IS IT AN EXEC STATEMENT?
         LA    R10,NEWREC
         BAL   R6,#CHKEXEC             CHECK IF EXEC STATEMENT
         B     #4A                     NOT EXEC
         B     #5
#4A      CLI   NEWREC,C'/'
         BE    #2
         LA    R4,1(R4)
         B     #2
#5       EQU   *
*        FILL IN TABLE ENTRY
         B     #5A1                    FIRST TIME THRU
         STH   R4,TCONT                STORE CONTROL CARD COUNT
         SR    R4,R4                   ZEROISE COUNT
         LA    R3,32(R3)               INCREMENT POINTER
#5A1     MVI   #5+1,X'00'              RESET BRANCH STATEMENT
         MVC   TENTRY+32,NULLNTRY      BLANK OUT NEXT ENTRY IN TABLE
         LA    R10,6(R10)              GET PAST ' EXEC ' ON CARD
#5A      CLI   0(R10),X'40'
         BNE   #5B
         LA    R10,1(R10)              FIND FIRST CHAR OF PROC
         B     #5A
#5B      LR    R2,R3
         BAL   R6,#GETNAME
         LA    R2,TIDENT
         CLC   TEXEC,ASMS              IS IT AN ASMS STEP?
         BE    #5CASMS
         CLC   TEXEC,IFOX00
         BE    #5CIFOX
         CLC   TEXEC,IEWL
         BE    #5CIEWL
#5CREAD  GET   NEW,NEWREC              OTHERWISE READ THRU TO
         CLI   NEWREC,C'/'             FIRST CONTROL CARD?
         BNE   #5CREAD1
         LA    R10,NEWREC
         BAL   R6,#CHKEXEC
         B     #5CREAD                 NOT EXEC
         B     #5                      EXEC STATEMENT
#5CREAD1 LA    R4,1(R4)
         CLC   TEXEC,LINKS             IS STEP LINKS?
         BE    #5CLINK
         CLC   TEXEC,IEBCOPY           IS IT IEBCOPY?
         BE    #5COPY
         CLC   TEXEC,IEBUPDTE          IS IT IEBUPDTE
         BE    #5CUPDT
         B     #2                      READ NEXT CARD
#5CASMS  EQU   *
*        IDENTIFY ASMS STEP BY MOD
         LA    R10,1(R10)              SCAN ALONG EXEC CARD
         CLC   0(4,R10),MOD            LOOKING FOR MOD
         BNE   #5CASMS
         LA    R10,4(R10)
         BAL   R6,#GETNAME
         B     #2
#5CLINK  EQU   *
*        IDENTIFY LINKS STEP BY BYTES 10-25 OF FIRST CONTROL CARD
         MVC   TIDENT,NEWREC+10
         B     #2
#5COPY   EQU   *
*        IDENTIFY IEBCOPY STEP BY BYTES 17-32 OF THE SECOND CONT CARD
         GET   NEW,NEWREC
         LA    R4,1(R4)
         MVC   TIDENT,NEWREC+16
         B     #2
#5CUPDT  EQU   *
*        IDENTIFY IEBUPDTE STEP BY BYTES 13-28 OF FIRST CONTROL CARD
         MVC   TIDENT,NEWREC+12
         B     #2
#5CIFOX  EQU   *
*        IDENTIFY IFOX STEP BY MODULE NAME OF SYSGO
         LA    R2,SYSGO
         B     #NEWIDNT
#5CIEWL  EQU   *
*        IDENTIFY IEWL STEP BY SYSLIN MODULE NAME,OR FIRST CONT.CARD
         LA    R2,SYSLIN
         B     #NEWIDNT
#6       EQU   *
*        RESET TO START OF TABLE & READ THRU OLD FILE
         STH   R4,TCONT
         LA    R3,STEPTBLE
#6READ   EQU   *
         GET   OLD,OLDREC
         LA    R10,OLDREC
         BAL   R6,#CHKEXEC              CHECK IF EXEC STATEMENT
         B     #6READ                   NOT EXEC
#6BRANCH B     #9                       BRANCH FIRST TIME THRU
#7       EQU   *
*        NOW CHECK FOR STEP IN TABLE
         LR    R2,R3                   MAINTAIN CURRENT POSN IN TABLE
#7COMP   CLC   NAMEWA(24),0(R2)
         BE    #8A                     STEP FOUND
         LA    R2,32(R2)               NEXT TABLE ENTRY
         CLC   0(24,R2),NULLNTRY       END OF TABLE?
         BNE   #7COMP
#8       EQU   *
         LH    R2,TINSRT               NOTFOUND,SO INCREMENT
         LA    R2,1(R2)                MISSING STEP COUNT
         STH   R2,TINSRT               FOR THIS STEP
         B     #9
#8A      EQU   *
*        SET FOUND INDICATOR ON FOR THIS ENTRY IN TABLE
         LR    R3,R2                   UPDATE POSN IN TABLE
         MVI   TFOUND,C'1'
         LA    R3,32(R3)
#9       EQU   *
*        FIND NAME OF PROC
         BC    0,#12A                  BRANCH OUT HERE WHEN EOF
         NI    #6BRANCH+1,X'0F'
         LA    R10,6(R10)              GET PAST ' EXEC '
#9A      CLI   0(R10),X'40'            SCAN FOR FIRST NON-BLANK
         BNE   #9B                     AFTER EXEC
         LA    R10,1(R10)
         B     #9A
#9B      LA    R2,NAMEWA
         MVC   NAMEWA(24),SPACES
         BAL   R6,#GETNAME             MOVE NAME TO WORKAREA
         CLC   NAMEWA,ASMS             IS IT AN ASMS STEP?
         BE    #9ASMS
         CLC   NAMEWA,IFOX00
         BE    #9IFOX
         CLC   NAMEWA,IEWL
         BE    #9IEWL
         CLC   NAMEWA,LINKS
         BE    #9READ
         CLC   NAMEWA,IEBCOPY
         BE    #9READ
         CLC   NAMEWA,IEBUPDTE
         BNE   #6READ
#9READ   GET   OLD,OLDREC              OTHERWISE READ THRU
         CLI   OLDREC,C'/'
         BNE   #9READ1
         LA    R10,OLDREC
         BAL   R6,#CHKEXEC
         B     #9READ                  NOT EXEC
         B     #7
#9READ1  CLC   NAMEWA,LINKS            IS STEP LINKS?
         BE    #9LINKS
         CLC   NAMEWA,IEBCOPY          IS IT IEBCOPY?
         BE    #9COPY
         CLC   NAMEWA,IEBUPDTE         IS IT IEBUPDTE?
         BE    #9UPDTE
         B     #6READ
#9ASMS   EQU   *
*        IDENTIFY ASMS STEP BY MOD
         LA    R10,1(R10)              SCAN ALONG EXEC CARD
         CLC   0(4,R10),MOD            LOOKING FOR MOD
         BNE   #9ASMS
         LA    R10,4(R10)
         LA    R2,STEPDESC
         BAL   R6,#GETNAME             MOVE MODNAME IN
         B     #6READ
#9LINKS  EQU   *
         MVC   STEPDESC,OLDREC+10
         B     #6READ
#9COPY   EQU   *
         GET   OLD,OLDREC
         MVC   STEPDESC,OLDREC+16
         B     #6READ
#9UPDTE  EQU   *
         MVC   STEPDESC,OLDREC+12
         B     #6READ
#9IFOX   EQU   *
         LA    R2,SYSGO
         B     #OLDIDNT
#9IEWL   EQU   *
         LA    R2,SYSLIN
         B     #OLDIDNT
#12      EQU   *
*        CLOSE OLD & NEW
         OI    #9+1,X'F0'
         B     #7
#12A     EQU   *
         CLOSE (OLD,REWIND,NEW,REWIND)
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                           *
*        S E C O N D   P A S S   &   P R I N T              *
*                                                           *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
#13      EQU   *
*        OPEN OLD,NEW & PRINT AFTER CHANGING EODAD
         LA    R2,#38
         ST    R2,OLD+32               ALTER EODAD
         NI    OLD+32,X'00'
         LA    R2,#37
         ST    R2,NEW+32
         NI    NEW+32,X'00'
         OPEN  (OLD,(INPUT),NEW,(INPUT),PRINT,(OUTPUT))
         LA    R3,STEPTBLE-32
         MVI   PLINE,X'40'
         MVC   PLINE+1(131),PLINE
         BAL   R6,#PRINT
#13A     GET   OLD,OLDREC              LINE UP OLD ON FIRST EXEC STMT
#13B     LA    R10,OLDREC
         BAL   R6,#CHKEXEC
         B     #13A                    NOT EXEC STMT
#13BC    BC    0,#15
         OI    #13BC+1,X'F0'
#14      EQU   *
*        READ NEW
         GET   NEW,NEWREC
#15      EQU   *
*        IS IT AN EXEC STATEMENT
         LA    R10,NEWREC
         BAL   R6,#CHKEXEC
         B     #16                     NOT EXEC STMT
         B     #18                     IT IS AN EXEC STMT
#16      EQU   *
*        PRINT THE CARD
         MVC   PCARD,NEWREC            MOVE CARD TO PRINTLINE
         BAL   R6,#PRINT               LINK TO PRINT ROUTINE
         B     #14
#18      EQU   *
*        ANY MISSING STEPS?
         LA    R3,32(R3)
         CLC   TINSRT,H0               IS MISSING STEP COUNT ZERO
         BE    #20
#19      EQU   *
*        PRINT MESSAGE & FLUSH MISSING STEPS IN OLD JOBSTREAM
         LH    R2,TINSRT               LOAD MISSING STEP COUNT
         CVD   R2,DOUBLEWD             CONVERT THE MISSING STEP COUNT
         UNPK  STPMSGNO,DOUBLEWD       FOR PRINTING
         OI    STPMSGNO+1,X'F0'
         MVI   PCHNGE,C'*'
         BAL   R6,#PRINT
         MVC   PCARD,STPMSG
         MVI   PCHNGE,C'*'
         BAL   R6,#PRINT
         MVI   PCHNGE,C'*'
         BAL   R6,#PRINT               PUT BLANK LINES ROUND MESSAGE
         TM    EOFNEW,X'80'
         BO    #39
         LA    R2,1(R2)
         B     #19FLSH
#19FLUSH EQU   *                       FLUSH THRU REQ'D NO OF STEPS
         GET   OLD,OLDREC
#19FLSH  LA    R10,OLDREC
         BAL   R6,#CHKEXEC             EXEC STATEMENT?
         B     #19FLUSH                NO
         BCT   R2,#19FLUSH             YES
#20      EQU   *
*        IS THE STEP IEHPROGM,IFCDIP00, OR IEHLIST
         CLC   TEXEC,IEHPROGM
         BE    #21
         CLC   TEXEC,IEHLIST
         BE    #21
         CLC   TEXEC,IFCDIP00
         BNE   #22
#21      EQU   *
*        READ & PRINT THE STEP FROM THE NEW & FLUSH FROM OLD
         MVC   PCARD,NEWREC
         BAL   R6,#PRINT
         GET   NEW,NEWREC              GET NEXT CARD
         LA    R10,NEWREC
         BAL   R6,#CHKEXEC             IS IT AN EXEC STMT?
         B     #21                     NO,PRINT IT
         CLI   TFOUND,C'1'             IS STEP PRESENT IN OLD?
         BNE   #18
         LA    R4,1
         LA    R10,OLDREC             DO WE ALREADY HAVE AN EXEC STMT?
         BAL   R6,#CHKEXEC
         LA    R4,2                    NOT EXEC STATEMENT
#21GTOLD GET   OLD,OLDREC
         LA    R10,OLDREC
         BAL   R6,#CHKEXEC
         B     #21GTOLD                NOT EXEC STMT
         BCT   R4,#21GTOLD             FLUSH THRU TO NEXT EXEC
         B     #18                     NEXT EXEC STMT
#22      EQU   *
*        IS IT A NEW STEP?
         CLI   TFOUND,C'1'             CHECK OLD STEP PRESENT INDICATOR
         BE    #24
#23      EQU   *
*        PRINT ALL OF STEP,MARKING AS INSERTIONS
         MVC   PCARD,NEWREC
         MVI   PCHNGE,C'×'             INDICATE CHANGE
         BAL   R6,#PRINT
         GET   NEW,NEWREC              GET NEXT
         LA    R10,NEWREC
         BAL   R6,#CHKEXEC             CHECK IF NEW STEP
         B     #23                     NO, SO PRINT IT
         B     #18
#24      EQU   *
*        PROCESS JCL
         MVC   PCARD,NEWREC
         BAL   R6,#PRINT
         GET   NEW,NEWREC
         LA    R10,NEWREC
         BAL   R6,#CHKEXEC             NEXT EXEC WITH NO CONT CARDS?
         B     #24A                    NO
         B     #24OLD                  YES,THEN FLUSH OLD AS WELL
#24A     CLI   NEWREC,C'/'
         BE    #24
         LH    R4,TCONT                LOAD NO OF CONTROL CARDS
#24OLD   EQU   *                       FLUSH OLD JCL
         GET   OLD,OLDREC
         LA    R10,OLDREC
         BAL   R6,#CHKEXEC
         B     #24B
         B     #27
#24B     CLI   OLDREC,C'/'
         BE    #24OLD
#25      EQU   *
*        COMPARE CONTROL CARDS. IF A DIFFERENCE IS FOUND, BUILD A
*        TABLE OF ALL NEW CARDS, & CHECK EACH OLD CARD AGAINST IT
         CLC   NEWREC(72),OLDREC       JUST COMPARE THE FIRST 72 BYTES
         BNE   #28
         BCTR  R4,0                    REDUCE CONT CARD COUNT
#26      EQU   *
*        PRINT THE NEWREC, & READ ANOTHER NEW & OLD RECORD
         MVC   PCARD,NEWREC
         BAL   R6,#PRINT
         GET   NEW,NEWREC
         GET   OLD,OLDREC
#27      EQU   *
*        END OF EITHER SET OF CONTROL CARDS?
         CLI   OLDREC,C'/'
         BNE   #25
#27A     CLI   NEWREC,C'/'
         BE    #13B
#27PRINT EQU   *
*        PRINT REMAINING NEW CONTROL CARDS
         MVC   PCARD,NEWREC
         MVI   PCHNGE,C'×'
         BAL   R6,#PRINT
         GET   NEW,NEWREC
         B     #27A
#28      EQU   *
*        INEQUALITY FOUND,SO BUILD TABLE OF ALL NEW CONTROL CARDS
         LA    R0,1(R4)                NO OF CONT CARDS LEFT
         MH    R0,H80                 MULTIPLY BY 80 T0 GET NO OF BYTES
         ST    R0,GETMNWA              STORE NO OF BYTES FOR FREEMAIN
         GETMAIN R,LV=(0)
         ST    R1,GETMNAD              STORE THE ADDR OF THE TABLE
         LR    R4,R1
         MVC   0(80,R4),SPACES         BLANK OUT FIRST TABLE ENTRY
#28JCL   EQU   *
         CLI   NEWREC,C'/'             JCL?
         BE    #31
#29      EQU   *
*        STORE NEWREC
         MVC   80(80,R4),0(R4)         BLANK OUT NEXT ENTRY
         MVC   0(80,R4),NEWREC         STORE NEWREC
         LA    R4,80(R4)               MOVE ALONG TABLE
#30      EQU   *
*        READ  NEWREC
         GET   NEW,NEWREC
         B     #28JCL
#31      EQU   *
*        IS OLDREC IN NEWREC TABLE
         L     R4,GETMNAD
         LR    R2,R4                   MAINTAIN CURRENT POSN
#31A     CLC   0(80,R2),SPACES         END OF TABLE?
         BE    #33
         CLC   0(72,R2),OLDREC
         BE    #32
         LA    R2,80(R2)               GET NEXT ENTRY
         B     #31A
#32      EQU   *
*        PRINT NEWRECS FROM TABLE
         MVC   PCARD,0(R4)
         CR    R4,R2
         BNE   #32INSRT
         BAL   R6,#PRINT
         LA    R4,80(R4)
         B     #34
#32INSRT MVI   PCHNGE,C'×'             PRINT INSERTS
         BAL   R6,#PRINT
         LA    R4,80(R4)
         B     #32
#33      EQU   *
*        INDICATE DELETION
         MVI   PCHNGE,C'×'
         BAL   R6,#PRINT
#34      EQU   *
*        READ NEXT OLDREC
         GET   OLD,OLDREC
         LR    R2,R4                   GET CURRENT POSN IN TABLE
         CLI   OLDREC,C'/'             IS IT ANOTHER CONTROL CARD?
         BNE   #31A                    YES,GO TO CHECK NEWREC TABLE
#36      EQU   *
*        PRINT REMAINING ENTRIES IN TABLE AS INSERTIONS
         CLC   0(80,R4),SPACES         END OF TABLE?
         BNE   #36A
         L     R0,GETMNWA
         L     R1,GETMNAD
         FREEMAIN R,LV=(0),A=(1)
         B     #13B
#36A     EQU   *
         MVC   PCARD,0(R4)             MOVE CARD TO PRINTLINE
         MVI   PCHNGE,C'×'             INDICATE CHANGE
         BAL   R6,#PRINT               PRINT IT
         LA    R4,80(R4)               GET NEXT ENTRY
         B     #36
#37      EQU   *
*        CHECK IF ANY MORE OLD STEPS
         OI    EOFNEW,X'80'            SET END OF FILE FLAG FOR NEW
         LA    R3,32(R3)
         CLC   TINSRT,H0
         BNE   #19
#38      EQU   *
         TM    EOFNEW,X'80'
         BO    #39
#38A     EQU   *
         MVI   PCHNGE,C'×'
         MVC   PCARD,NEWREC
         BAL   R6,#PRINT
         GET   NEW,NEWREC
         B     #38A
#39      EQU   *
*        CLOSE FILES & RETURN
         BAL   R6,#PRINT
         MVC   PLINE(132),ENDMSG
         BAL   R6,#PRINT
         CLOSE (OLD,,NEW,,PRINT)
         L     13,4(13)
         LM    14,12,12(13)
         BR    14
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#CHKEXEC EQU   *
*        CHKEXEC CHECKS TO SEE IF R10 POINTS TO AN EXEC STATEMENT.
*        IF IT IS AN EXEC STATEMENT,IT RETURNS TO R6+4
         CLI   0(R10),C'/'
         BNER  R6
         LA    R11,15
#CHKLOOP CLC   0(6,R10),EXEC           LOOK FOR EXEC
         BE    4(R6)                   FOUND-RETURN TO CALL+4
         LA    R10,1(R10)
         BCT   R11,#CHKLOOP
         BR    R6                      NOT FOUND
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#GETNAME EQU   *
*        EXTRACT UP TO 8 BYTE NAME FROM STRING
*        R10 POINTS TO THE START OF NAME
*        R2  POINTS TO THE START OF THE OBJECT FIELD
         LA    R11,8
         CLC   0(4,R10),PGM            IS IT PGM=?
         BNE   #GETLOOP                NO
         LA    R10,4(R10)              GET PAST PGM=
#GETLOOP MVC   0(1,R2),0(R10)          MOVE ONE CHARACTER
         LA    R10,1(R10)
         LA    R2,1(R2)
         CLI   0(R10),C','             END OF NAME?
         BE    #GETEND
         CLI   0(R10),X'40'
         BE    #GETEND
         BCT   R11,#GETLOOP
#GETEND  BR    R6
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#PRINT   EQU   *
*        PRINT SUBROUTINE
         L     R10,LNECNT
         BCT   R10,#PRINT1              REDUCE LINECOUNT
         MVI   PCONT,X'89'              SKIP TO CHANNEL 1
         PUT   PRINT,PRTREC
         L     R10,PGECNT
         LA    R10,1(R10)               INCREMENT PAGE COUNT
         ST    R10,PGECNT
         CVD   R10,DOUBLEWD             UNPACK IT FOR PRINTING
         UNPK  PAGENO,DOUBLEWD
         OI    PAGENO+3,X'F0'
         MVI   PCONT,X'19'              SPACE THREE LINES
         MVC   PLINE(132),PHEAD
         L     R10,FIFTY9
#PRINT1  ST    R10,LNECNT
         PUT   PRINT,PRTREC
         MVI   PCONT,X'09'
         MVI   PLINE,X'40'               BLANK OUT PRINTLINE
         MVC   PLINE+1(131),PLINE
         BR    R6
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#NEWIDNT EQU   *
         GET   NEW,NEWREC
         LA    R10,NEWREC
         BAL   R6,#CHKEXEC
         B     #NEW1
         B     #5
#NEW1    CLC   NEWREC+2(6),0(R2)
         BNE   #NEWIDNT                NOT REQ'D JCL STATEMENT
         LA    R10,NEWREC+11
         LA    R2,60                   TO LIMIT BCT LOOP
#NEW2    CLI   0(R10),C'*'             IS IT A DD * STATEMENT
         BE    #NEW5
         CLC   0(4,R10),DSN            DSNAME FOUND?
         BE    #NEW3
         LA    R10,1(R10)
         BCT   R2,#NEW2
         B     #2
#NEW3    CLI   0(R10),C'('
         BE    #NEW4
         LA    R10,1(R10)
         BCT   R2,#NEW3
         B     #2                       NOT A PDS MEMBER
#NEW4    MVC   TIDENT(8),1(R10)
         B     #2
#NEW5    GET   NEW,NEWREC
         MVC   TIDENT,NEWREC+8
         LA    R4,1(R4)               INCR CONT CARD COUNT
         B     #2
* * * * * * * * * * * * * * * * * * * * * * * * * *
#OLDIDNT EQU   *
         GET   OLD,OLDREC
         LA    R10,OLDREC
         BAL   R6,#CHKEXEC
         B     #OLD1
         B     #7
#OLD1    CLC   OLDREC+2(6),0(R2)
         BNE   #OLDIDNT                NOT REQ'D JCL STATEMENT
         LA    R10,OLDREC+11
         LA    R2,60                  TO LIMIT BCT LOOP
#OLD2    CLI   0(R10),C'*'               IS IT A DD * STATEMENT
         BE    #OLD5
         CLC   0(4,R10),DSN             DSNAME FOUND?
         BE    #OLD3
         LA    R10,1(R10)
         BCT   R2,#OLD2
         B     #6READ
#OLD3    CLI   0(R10),C'('
         BE    #OLD4
         LA    R10,1(R10)
         BCT   R2,#OLD3
         B     #6READ                   NOT A PDS MEMBER
#OLD4    MVC   STEPDESC(8),1(R10)
         B     #6READ
#OLD5    GET   OLD,OLDREC
         MVC   STEPDESC,OLDREC+8
         B     #6READ
* * * * * * * * * * * * * * * * * * * * * * * * * * *
DOUBLEWD DS    D
NEWREC   DS    CL80                       NEW RECORD AREA
OLDREC   DS    CL80                       OLD RECORD AREA
IEHPROGM DC    C'IEHPROGM'
IEHLIST  DC    C'IEHLIST '
IFCDIP00 DC    C'IFCDIP00'
IEBUPDTE DC    C'IEBUPDTE'
IEBCOPY  DC    C'IEBCOPY '
ASMS     DC    C'ASMS    '
LINKS    DC    C'LINKS   '
IFOX00   DC    C'IFOX00  '
IEWL     DC    C'IEWL    '
GETMNWA  DC    F'0'
GETMNAD  DC    F'0'
LNECNT   DC    F'1'
PGECNT   DC    F'0'
FIFTY9   DC    F'59'
H0       DC    H'0'
H80      DC    H'80'
NAMEWA   DC    CL8' '
STEPDESC DC    CL16' '
STPMSG   DS    0CL80
         DC    C'THERE ARE '
STPMSGNO DC    C'00'
         DC    C' STEPS OMITTED AT THIS POINT'
         DC    CL40' '
PHEAD    DC    CL30' '
         DC    CL54'SYSGEN STAGE 1 OUTPUT COMPARISON'
         DC    C'PAGE '
PAGENO   DC    C'0000'
SPACES   DC    CL80' '
ENDMSG   DS    0CL132
         DC    CL16' '
         DC    C'*************************** END OF REPORT '
         DC    CL74'***************************'
NULLNTRY DC    CL24' '
         DC    4X'00'
         DC    C'0    '
PGM      DC    C'PGM='
MOD      DC    C'MOD='
SYSGO    DC    C'SYSGO '
SYSLIN   DC    C'SYSLIN'
DSN      DC    C'DSN='
EXEC     DC    C' EXEC '
PRTREC   DS    0CL133                  PRINT RECORD AREA
PCONT    DC    X'19'
PLINE    DS    CL10
PCHNGE   DS    CL1                     CHANGE INDICATOR
         DS    CL3
PCARD    DS    CL80                    CARD IMAGE
         DS    CL38
EOFNEW   DC    X'00'                     END OF FILE FLAG FOR NEW
OLD      DCB   DDNAME=OLD,DSORG=PS,MACRF=GM,EODAD=#12,                 C
               RECFM=FB,LRECL=80,BLKSIZE=3200
NEW      DCB   DDNAME=NEW,DSORG=PS,MACRF=GM,EODAD=#6,                  C
               RECFM=FB,LRECL=80,BLKSIZE=3200
PRINT    DCB   DDNAME=PRINT,DSORG=PS,MACRF=PM,                         C
               RECFM=FMB,LRECL=133,BLKSIZE=665
*        STEP TABLE - 200 ENTRIES OF 32 BYTES
*                   - 8 BYTES EXEC NAME
*                   - 2 * 8 BYTE IDENTIFIERS FOR STEP
*                   - 2 BYTE MISSING STEP COUNT
*                   - 2 BYTE CONT CARD COUNT
*                   - 1 BYTE FOUND ON OLD IND
*                   - 3 BYTE FILLER
         ORG   *+31
         ORG   SPSG+(*-SPSG)/32*32
STEPTBLE DS    200CL32
TBLENTRY DSECT
TENTRY   DS    0CL32
TEXEC    DS    CL8
TIDENT   DS    CL16
TINSRT   DS    H
TCONT    DS    H
TFOUND   DS    CL1
         DS    CL3
         END
