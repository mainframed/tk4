+ VERY PROUD OF THIS ONE. IT IS INVOKED FROM AN SPF PANEL. IT COULD ALSO
+ BE USED AS A PDS COMPRESS FRONT END TO BYPASS COMPRESSING DATASETS
+ WITH PLENTY OF FREE SPACE, IN DASD SPACE MANAGEMENT ROUTINES, IN LOGON
+ PROCS TO WARN USERS OF FULL LIBRARIES ETC.
+
+ CLIST USED IS AS FOLLOWS:
+
+----------------------------------------------------------------------
/* CALCULATE SPACE UTILISED IN PDS AS PERCENTAGE IN RETURN CODE       */
/* PANELS:                                                            */
/* VM7      - DESCRIBE FUNCTION, GET DSN AND DISPLAY RESULT           */
/* CHANGE HISTORY:                                                    */
/* 27 JUN 83 AM : CREATED                                             */
/*                                                                    */
CONTROL MAIN NOMSG
P01: +
ISPEXEC DISPLAY PANEL(VM7)
IF &LASTCC = 8 THEN EXIT
ISPEXEC VGET (Z)
SET &ERR = &Z
SET &MSG = &Z
ALLOC FI(SPUTIL) DS('&DSN') SHR
SET &CC = &LASTCC
IF &CC ^= 0 THEN +
   DO
   SET &ERR = DATASET IN USE OR NOT CATALOGUED
   GOTO P01
   END
CALL 'MAC1.LOAD(NCZ93208)'
SET &CC = &LASTCC
SET &MSG = DATASET IS &CC % FULL
IF &CC > 100 THEN +
   DO
   SET &MSG = FUNCTION TERMINATED ABNORMALLY COND CODE &CC
   SET &ERR = SPACE UTILISED NOT DETERMINED
   END
IF &CC = 104 THEN +
   DO
   SET &MSG = DATASET ORG IS NOT PO
   END
FREE FI(SPUTIL)
SET &CC = &LASTCC
IF &CC ^= 0 THEN +
   SET &ERR = DATASET NOT FREED
GOTO P01
+-----------------------------------------------------------------------
NCZ93208 TITLE 'CALCULATE DATASET SPACE UTILISATION                    *
               - PROGRAM DOCUMENTATION'
*****                                                             *****
*                                                                     *
* ID:          NCZ93208 - CALCULATE DATASET SPACE UTILISATION         *
*                                                                     *
* NCZ93208.1 PROGRAM DESCRIPTION                                      *
*                                                                     *
* THIS PROGRAM DETERMINES THE SPACE UTILISED IN A PARTITIONED         *
* DATASET, GIVEN AS A PERCENTAGE OF THE ALLOCATED SPACE.              *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93208.2 USER INSTRUCTIONS                                        *
*                                                                     *
* THE PROGRAM IS INVOKED BY SPF. IT MAY ALSO BE INVOKED FROM A TSO    *
* CLIST. NO PARAMETERS ARE REQUIRED BUT ALLOCATION MUST HAVE BEEN     *
* MADE OF THE PDS CONCERNED AS //SPUTIL.                              *
*                                                                     *
* THE PERCENTAGE OF ALLOCATED SPACE USED IS COMMUNICATED TO THE USER  *
* VIA RETURN CODE. RETURN CODES > 100 ARE USED TO INDICATE CONDITIONS *
* DETECTED WHEN THE PROGRAM CANNOT COMPLETE NORMALLY.                 *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93208.3 MESSAGES                                                 *
*                                                                     *
* NONE                                                                *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93208.4 ABEND CODES                                              *
*                                                                     *
* NONE                                                                *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93208.5 IMPLEMENTATION METHOD                                    *
*                                                                     *
* NORMAL ASSEMBLY & LINK-EDIT.                                        *
*                                                                     *
*****                                                             *****
         EJECT
*****                                                             *****
*                                                                     *
* NCZ93208.6 AMENDMENT HISTORY                                        *
*                                                                     *
* MAINTAIN THE RECORD BELOW.                                          *
*                                                                     *
* CHANGE HISTORY:                                                     *
* DATE      INITS VERSION COMMENTS                                    *
*                                                                     *
* 20 JUN 83  AM   V01     CREATED                                     *
*                                                                     *
* SUGGESTED ENHANCEMENTS:                                             *
*                                                                     *
* RECORD DETAILS BELOW.                                               *
*                                                                     *
* DD MMM YY WHO : COMMENTS                                            *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93208.7 MODULARISATION DETAILS                                   *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93208.8 PROGRAM LOGIC OVERVIEW                                   *
*                                                                     *
*****                                                             *****
         TITLE 'CALCULATE DATASET SPACE UTILISATION                    *
               - PROGRAM PROLOGUE'
*****                                                             *****
*                                                                     *
* ID:          NCZ93208 - CALCULATE DATASET SPACE UTILISATION         *
*                                                                     *
* ENTRY INTFCE: NORMAL OS.                                            *
*                                                                     *
* EXIT  INTFCE: NORMAL OS.                                            *
*                                                                     *
* RETURN CODES:RC < 101    THE RC IS % DATASET SPACE UTILISED         *
*                                                                     *
*              RC > 100    PROGRAM WAS TERMINATED DUE TO ERROR        *
*                                                                     *
*              RC = 101    CANNOT LOCATE TIOT ENTRY FOR DDNAME        *
*              RC = 102    DEVTYPE FAILED                             *
*              RC = 103    CANNOT OBTAIN DSCB F1                      *
*              RC = 104    DATASET IS NOT A PDS                       *
*              RC = 105    CANNOT OBTAIN DSCB F3                      *
*                                                                     *
* REG USAGE:   R0 -                                                   *
*              R1 -                                                   *
*              R2 -                                                   *
*              R3 -                                                   *
*              R4 - ADDR TIOT ENTRIES                                 *
*              R5 - WORK REG FOR TIOT SEARCH                          *
*              R6 - ADDR UCB                                          *
*              R7 - ADDR DSCB EXTENT DESCRIPTIONS                     *
*              R8 - WORK REG FOR SPACE CALCULATION                    *
*              R9 - WORK REG FOR SPACE CALCULATION                    *
*              R10 -                                                  *
*              R11 -                                                  *
*              R12 -                                                  *
*              R13 - PROGRAM BASE REGISTER                            *
*              R14 - WORK REG USED WITH R15 FOR RETURN CODE CALC      *
*              R15 - RETURN CODE                                      *
*                                                                     *
*****                                                             *****
         TITLE 'CALCULATE DATASET SPACE UTILISATION'
         PRINT NOGEN
NCZ93208 MSETUP 13
*
*                                      ADDRESS TIOT
*
         EXTRACT TIOTA,'S',FIELDS=TIOT,MF=(E,EXTRACT)
         L     R4,TIOTA
         LA    R4,TIOENTRY-TIOT(R4)
         USING TIOENTRY,R4
*
*                                      LOCATE TIOT ENTRY FOR DDNAME
*
         XR    R5,R5
TIOSRCH  EQU   *
         CLC   TIOEDDNM,DDNAME
         BE    ADDRUCB
         IC    R5,TIOELNGH
         LA    R4,0(R5,R4)
         LTR   R5,R5
         BP    TIOSRCH
         LA    R15,101                 SET ERROR RETURN CODE
         B     RETURN
*
*                                      ADDRESS UCB & EXTRACT VOL SER
*
ADDRUCB  EQU   *
         ICM   R6,B'0111',TIOEFSRT
         USING UCBCMSEG,R6
         MVC   VOLSER,UCBVOLI
*
*                                      DETERMINE TRK/CYL FOR DEVICE
*                                                        ( DEVTPC )
         MVC   DBWD,DDNAME
         DEVTYPE DBWD,DEVWKA,DEVTAB
         LTR   R15,R15
         BZ    READJFCB
         LA    R15,102                 SET ERROR RETURN CODE
         B     RETURN
         EJECT
*
*                                      GET DSNAME FOR DDNAME
*
READJFCB RDJFCB (SPUTIL)
         MVC   DSNAME,JFCBDSNM
*
*                                      OBTAIN DSCB FORMAT 1
*
OBTAINF1 EQU   *
         MVC   CAMLST,CAMSRCH
         LA    R15,DSNAME
         ST    R15,CAMDSN
         LA    R15,VOLSER
         ST    R15,CAMSER
         LA    R15,OBTWKA
         ST    R15,CAMWKA
         XR    R0,R0
         OBTAIN CAMLST
         LTR   R15,R15                 OBTAIN OKAY ?
         BZ    DSORG                   YES, GO CHECK FOR ORG PO
         LA    R15,103                 NO, SET ERROR RETURN CODE
         B     RETURN
DSORG    TM    DS1DSORG,DS1DSGPO       IS DATASET A PDS ?
         BO    OBTAINF3                YES, GO CHECK FOR DSCB F3
         LA    R15,104                 NO, SET ERROR RETURN CODE
         B     RETURN
*
*                                      OBTAIN DSCB FORMAT 3
*
OBTAINF3 CLI   DS1NOEPV,3              MORE THAN 3 EXTENTS ?
         BNH   EXTDESC                 NO, SO NO DSCB F3
         LA    R15,DS1PTRDS
         ST    R15,CAMDSN
         LA    R15,F3WKA
         ST    R15,CAMWKA
         MVC   CAMLST,CAMSEEK
         OBTAIN CAMLST
         LTR   R15,R15                 OBTAIN OKAY ?
         BZ    EXTDESC                 YES, GO ADDRESS EXTENT DESCS
         LA    R15,105                 NO, SET ERROR RETURN CODE
         B     RETURN
         EJECT
*
*                                      CALCULATE SPACE ALLOCATION
*                                      FROM DSCB EXTENT DESCRIPTIONS
*
EXTDESC  EQU   *
         XR    R15,R15
         ST    R15,SPALLOC
         ICM   R15,B'001',DS1NOEPV     EXTRACT NUMBER OF EXTENTS
         BZ    USECALC
         MVC   EXTENTS(30),DS1EXT1     MOVE EXTENT DESCRIPTIONS FROM
         MVC   EXTENTS+30(40),DS3EXTNT      DSCB1 & DSCB3 FOR EASIER
         MVC   EXTENTS+70(90),DS3ADEXT      EXAMINATION IN ONE BLOCK
         LA    R7,EXTENTS              POINT TO FIRST EXTENT DESC
EXTCALC  EQU   *
         USING EXTENT,R7               ADDR A 10 BYTE EXTENT DESC
         CLI   EXTTYPE,0
         BNH   EXTLOOP
         LH    R9,EXTLOCC
         MH    R9,DEVTPC               CONVERT START CYL TO TRKS
         AH    R9,EXTLOHH                  AND ADD START TRKS
         LH    R8,EXTHICC
         MH    R8,DEVTPC               CONVERT END CYL TO TRKS
         AH    R8,EXTHIHH                  AND ADD END TRKS
         BCTR  R9,0                    ADJUST TRKS RELATIVE TO ZERO
         SR    R8,R9                   DIFF IS SPACE ALLOCATION IN TRKS
         A     R8,SPALLOC              ACCUMULATE SPACE
         ST    R8,SPALLOC                  AND STORE
EXTLOOP  LA    R7,EXTNEXT              ADDRESS NEXT EXTENT DESCRIPTION
         BCT   R15,EXTCALC             BRANCH UNTIL ALL PROCESSED
*
*                                      CALCULATE SPACE UTILISED AND ITS
*                                      PERCENTAGE OF ALLOCATED SPACE
*
USECALC  EQU   *
         LH    R15,DS1LSTAR            LOAD LAST TRACK ADDRESS
         CLI   DS1LSTAR+2,0            ANY BLOCKS USED ON LAST TRK ?
         BNH   USEST
         LA    R15,1(R15)              YES, ADJUST TRKS AS REL TO ZERO
USEST    ST    R15,SPUSED
         L     R15,SPERCENT
         XR    R14,R14                 CALCULATE % TRACKS USED OF THOSE
         M     R14,SPUSED                  ALLOCATED WITH RESULT TO R15
         D     R14,SPALLOC
*
*                                      TERMINATE WITH RETURN CODE OF
*                                      PERCENTAGE ALLOCATED SPACE USED
*
RETURN   MCLEAR RC=(15)
         EJECT
TIOTA    DS    A
EXTRACT  EXTRACT TIOTA,FIELDS=TIOT,MF=L
DDNAME   DC    CL8'SPUTIL  '
VOLSER   DS    CL6
DBWD     DS    D
DEVWKA   DS    5F
DEVTPC   EQU   DEVWKA+10
SPUTIL   DCB   DDNAME=SPUTIL,DSORG=PO,MACRF=(E),EXLST=EXLST
EXLST    DC    X'87',AL3(JFCBAREA)
JFCBAREA DS    0D
         DC    176X'00'
         ORG   JFCBAREA
         IEFJFCBN
         ORG   ,
CAMSRCH  CAMLST SEARCH,*-*,*-*,*-*
CAMSEEK  CAMLST SEEK,*-*,*-*,*-*
DSNAME   DS    CL44
CAMLST   DS    F
CAMDSN   DS    A
CAMSER   DS    A
CAMWKA   DS    A
OBTWKA   DS    0D,XL140
F3WKA    DS    0D,XL140
         ORG   OBTWKA-44
         IECSDSL1 (1)
         ORG   F3WKA
         IECSDSL1 (3)
         ORG   ,
EXTENTS  DS    CL160
SPALLOC  DS    F
SPUSED   DS    F
SPERCENT DC    F'100'
         LTORG
TIOT     DSECT
         IEFTIOT1
         IEFUCBOB
EXTENT   DSECT                     ONE TEN BYTE EXTENT DESCRIPTION
EXTTYPE  DS    BL1
EXTSEQ   DS    XL1
EXTLOCC  DS    HL2
EXTLOHH  DS    HL2
EXTHICC  DS    HL2
EXTHIHH  DS    HL2
EXTNEXT  EQU   *
         END
