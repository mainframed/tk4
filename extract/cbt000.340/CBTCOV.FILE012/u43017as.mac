*        GENERAL DESCRIPTION
*        THE ZZREORG SUBROUTINE IS USED TO PRINT OUT ON THE CONSOLE AND
*        SYSTEM OUTPUT DEVICE THE REORGANIZATION CRITERIA PRODUCED BY
*        ANS COBOL WHEN AN INDEXED SEQUENTIAL FILE THAT IS ACCESSED
*        RANDOMLY IS CLOSED. THESE STATISTICS ARE ONLY PROVIDED IF THE
*        "APPLY REORG-CRITERIA" STATEMENT IS INCLUDED IN I-O-CONTROL.
*        CALLING PROCEDURE
*        I-O-CONTROL.
*            APPLY REORG-CRITERIA WAAG-REORG ISFILE.
*            CLOSE ISFILE.
*            CALL 'ZZREORG' USING ISFILE WAAG-REORG.
*        WHERE ISFILE IS THE NAME OF THE INDEXED SEQUENTIAL FILE AND
*        WAAG-REORG IS AN 8 BYTE AREA IN WORKING STORAGE ALIGNE& ON A
*        FULL WORD BOUNDARY.
*        NOTES
*        THE FILE MUST BE CLOSED BEFORE THE SUBROUTINE IS CALLED. THE
*        MESSAGE PRINTED GIVES:-
*                               JOBNAME
*                               STEPNAME
*                               DDNAME
*                               NUMBER OF FULL CYLINDER OVERFLOW AREAS
*                               NUMBER OF FREE TRACKS IN THE
*                                      INDEPENDENT OVERFLOW AREA
*                               ACCESSES TO OVERFLOW RECORDS OTHER THAN
*                                      THE FIRST IN THE CHAIN.
         TITLE 'ZZREORG PRINT REORGANISATION CRITERIA'
ZZREORG  CSECT
         IN    12,PARM=YES
         LM    2,3,0(1)            ADDRESS OF DCB AND STATS AREA
         L     2,8(2)
         EXTRACT TIOTADDR,FIELDS=TIOT
         L     4,TIOTADDR
         MVC   JOB,0(4)            JOBNAME
         MVC   STEP,8(4)           STEP NAME
         CLI   40(2),C'A'
         BL    D01                 ERROR
         CLI   40(2),C'I'
         BNH   C01                 VALID
         CLI   40(2),C'J'
         BL    D01                 ERROR
         CLI   40(2),C'R'
         BNH   C01                 VALID
         CLI   40(2),C'S'
         BL    D01                 ERROR
         CLI   40(2),C'Z'
         BH    D01                 ERROR
C01      EQU   *
         MVC   DDNAME,40(2)
         LH    5,0(3)              FULL CYLINDER OVFLOW AREAS
         CVD   5,DWORD
         ED    CYLOFLOW,DWORD+6
         LH    5,2(3)              TRACKS REMAINING IN INDEP OVFLOW
         CVD   5,DWORD
         ED    INDEP,DWORD+5
         L     5,4(3)              ACCESS TO OVERFLOW RECORDS
         CVD   5,DWORD
         ED    ACCESS,DWORD+4
         B     J01                 GO OUT PUT
D01      EQU   *
         MVC   MESSAGE+27(63),ERROR
J01      EQU   *
         LA    1,MESSAGE
         SVC   35
         OUT   0
DWORD    DS    D
TIOTADDR DS    F
         DS    0F
MESSAGE  EQU   *
         DC    AL2(MSG01-*)
         DC    X'8000'
JOB      DS    CL8
         DC    C' '
STEP     DS    CL8
         DC    C' '
DDNAME   DS    CL8
         DC    C' '
         DC    C'FULL CYL OFLOW'
CYLOFLOW DC    X'40202120'
         DC    C' '
         DC    C'FREE INDEP TRKS'
INDEP    DC    X'402020202120'
         DC    C' '
         DC    C'OFLOW ACCESSES'
ACCESS   DC    X'4020202020202120'
MSG01    EQU   *
         DC    X'0000'
         DC    X'4020'
ERROR    DC    CL63'FILE NOT CLOSED-NO STATISTICS AVAILABLE'
         LTORG
         END
