         EJECT
         MACRO
&NAME    GNHOF &EP=SVRTN *      ENTER ROUTINE IN SUPERVISOR STATE
         LA    15,SV&SYSNDX
         SVC   12
         L     15,=A(&EP)
         BALR  14,15
         LPSW  PSW&SYSNDX *     RESTART IN PROBLEM STATE
SV&SXSNDX ST   10,SAV&SYSNDX    SAVE
         L     10,16            CVT
         L     10,0(10)         TCB POINTER
         L     10,4(10)         TCB
         L     10,0(10)         HIGH PRB
         L     10,28(10)        NEXT LOWER PRB
         MVC   PSW&SYSNDX.(5),16(10) STORE FOR RESTART
         NI    17(10),X'0E'     SET KEY TO ZERO AND PROBLEM BIT OFF
         L     10,SAV&SYSNDX    RESTORE
         BR    14               RETURN
SAV&SYSNDX DS  F
PSW&SYSNDX DS  0D
         DS    CL5
         DC    AL3(*+3)
         MEND
         MACRO
&NAME    IN    &REG,&SAVE,&PARM=YES
         LCLC  &RG,&PM
         CNOP  0,4
&NAME    STM   14,12,12(13)
&RG      SETC  '2'
         AIF   (T'&REG EQ 'O').BASE
&RG       SETC  '&REG'
         AGO   .NXT
.BASE    ANOP
*              *** REG 2 ASSUMED BASE
.NXT     ANOP
         BALR  &RG,0
         USING *,&RG
         LR    1,13
         BAL   13,*+76
         AIF   (T'&SAVE NE 'O').BR
         DC    C'SAVE'
         AGO   .BRN
.BR      ANOP
&SAVE    DC    CL4'&SAVE'
.BRN     ANOP
         DC    C'HSA LSA RET EPA R0  R1  R2  R3  '
         DC    C'R4  R5  R6  R7  R8  R9  R10 R11 R12 '
         ST    1,4(13)
         ST    13,8(1)
&PM       SETC  '&PARM'
         AIF   ('&PM' EQ 'NO').END
         L     1,24(1) RELOAD PARAMETER REGISTER
         AIF   ('&PM' EQ 'YES').END
         MNOTE 'INVALID PARAMETER, YES ASSUMED'
.END     MEND
          MACRO
&NAME    OUT   &CCOD
&NAME    L     13,4(13)
         MVI   16(13),X'FF'
         AIF   ('&CCOD' EQ '(15)').BRAKT
         LM    14,12,12(13)
         AIF   (T'&CCOD EQ 'O').NOCODE
         LA    15,&CCOD
.NOCODE  ANOP
         BR    14
         MEXIT
.BRAKT   ANOP
         L     14,12(13)
         LM    0,12,20(13)
         AGO   .NOCODE
         MEND
RS00  TITLE   'RESCUE - 5TH DECEMBER 1969'
*
* THIS PROGRAM WILL PUT ANY DATASET ON A SYSOUT QUEUE.
*   SINCE THE ONLY WAY THE SYSTEM KNOWS THAT A PARTICULAR DS IS ON A
*   QUEUE IS BY DSNAME AND VOLUME, IF A SYSOUT=... CARD IS PROVIDED
*   FOR THIS PROGRAM, WE CREATE THE NECESSARY HOOKS. THE PROGRAM
*   MERELY SWITCHES THE DSNAMES OF THE TWO DATASETS, AFTER CHECKING
*   THAT THEY ARE ON THE SAME VOLUME.
*
*   THE REST OF THE CODE IS TO PROVIDE A BRIEF EXPLANATION TO THE
*   RECIPIENT OF THE OUTPUT.
*
RESCUE   CSECT
         IN    3,PARM=YES
         OPEN  (PRT,(OUTPUT))
         TM    PRT+48,X'10'   CHECK OPEN
         BNO   NOPRT           * NO
         NI    PUT+1,X'0F'    SWITCH ON PRINT
         MVC   A(35),L1
         BAL   14,PUT
NOPRT    EQU   *
         MVI   JFCB1,0
         MVI   JFCB2,0
         RDJFCB  SYSUT1
         MVC   A(16),L2
         MVC   A+16(44),JFCB1
         BAL   14,PUT
         RDJFCB  SYSUT2
         MVI   A+7,C'2'
         MVC   A+16(44),JFCB2
         BAL   12,PUTCLR
         L     1,C1
         CLI   JFCB1,0
        BE     ABEND
         L     1,C2
         CLI   JFCB2,0
        BE     ABEND
         CLC   JFCB1+118(6),JFCB2+118   SAME VOL SERIAL
         BNE   NOTEQ                     *
         MVC   VOL(6),JFCB1+118
         SR    0,0
         RENAME  TEMP
         L     1,C3
         LTR   15,15
         BNZ   ABEND
         MVC   VOL(6),JFCB2+118
         SR    0,0
         RENAME  UT2TOUT1
         L     1,C4
         LTR   15,15
         BNZ   ABEND
         MVC   VOL(6),JFCB1+118
         SR    0,0
         RENAME  TEMPTOT2
         L     1,C5
         LTR   15,15
         BNZ   ABEND
         MVC   A(19),L4
         BAL   12,PUTCLR
         MVI   A,C'-'
         BAL   14,PUT
         BAL   14,PUT
         LA    8,7
         LA    9,JFCB1+24
CLIDOT   CLI   0(9),C'.'
         BE    NMND
         LA    9,1(9)
         BCT   8,CLIDOT
NMND     LA    9,7
         SR    9,8
         EX    9,MOVE
         MVI   A,C'0'
         BAL   12,PUTCLR
         MVI   A,C'-'
         BAL   14,PUT
         BAL   14,PUT
         BAL   14,PUT
         MVC   A+40(52),L5
         MVI   A+40,C' '
         BAL   12,PUTCLR
         MVC   A+40(52),L6
         BAL   12,PUTCLR
         MVC   A+40(53),L7
         BAL   12,PUTCLR
         MVC   A+40(15),L8
         BAL   12,PUTCLR
         CLOSE PRT
         OUT     0
MOVE     MVC   A+56(1),JFCB1+23
         SPACE 3
PUT      NOPR  14
         LA    0,A
         LA    1,PRT
         L     15,48(0,1)
         BR    15
PUTCLR   BAL   14,PUT
         MVI   A,X'40'
         MVC   A+1(120),A
         BR    12
         SPACE 3
NOTEQ    MVC   A(24),L3
         BAL   14,PUT
         L     1,C6
         B     ABEND
ABEND    ST    1,ABSV
         CLOSE   PRT
         L     1,ABSV
         SVC   13
TEMP     CAMLST RENAME,JFCB1,TEMPNAM,VOLIST      1 TO TEMP
UT2TOUT1 CAMLST RENAME,JFCB2,JFCB1,VOLIST        2 TO 1
TEMPTOT2 CAMLST RENAME,TEMPNAM,JFCB2,VOLIST      TEMP TO 2
ABSV     DC    F'0'
JFCB1    DC    CL176' '
JFCB2    DC    CL176' '
VOLIST   DC    H'1'            1
VDEV     DC    X'30C02008'     3
VOL      DC    XL6'00'         2
TEMPNAM  DC    CL44'INTER.SYSOUT.NAME'
L1       DC    C'1 SYSOUT RESCUE PROGRAM - VERSION 1'
L2       DC    C'0 SYSUT1 NAME - '
L3       DC    C'0   - NOT ON SAME VOLUME'
L4       DC    C'0 RENAME SUCCESSFUL'
L5       DC    C'0 THIS PRINTER OUTPUT HAS BEEN PRODUCED BY A SPECIAL'
L6       DC    C'  PROGRAM, FOLLOWING MACHINE PROBLEMS. WE REGRET ANY'
L7       DC    C'  INCONVENIENCE AND ASK YOU TO CONTACT US IF YOU HAVE'
L8       DC    C'  ANY QUESTIONS'
A        DC    CL121' '
         PRINT NOGEN
SYSUT1   DCB   EXLST=XL1,DDNAME=SYSUT1,MACRF=(E)
SYSUT2   DCB   EXLST=XL2,DDNAME=SYSUT2,MACRF=(E)
PRT      DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=121,MACRF=(PM),RECFM=FBA
         DS    0F
C1       DC    X'80000001'
C2       DC    X'80000002'
C3       DC    X'80000003'
C4       DC    X'80000004'
C5       DC    X'80000005'
C6       DC    X'80000006'
XL1      DC    X'87',AL3(JFCB1)    MUST BE ALIGNED
XL2      DC    X'87',AL3(JFCB2)    MUST BE ALIGNED
         END
