FINDVOL  CSECT
*
*  THIS PROGRAM IS INTENDED TO ENABLE AN OPERATOR TO DISCOVER THE
*  VOLUME SERIAL NUMBER OF A DISK PACK AND OPTIONALLY TO CHANGE IT.
*  THE OPERATOR IS TOLD, BY A SERIES OF WTO MESSAGES, HOW TO OPERATE
*  THIS PROGRAM.
*  ANY NUMBER OF DISK PACKS, ON DRIVES THAT MAY BE ON OR OFF-LINE, CAN
*  BE IDENTIFIED AND CHANGED.
*  A COMPLETELY UNKNOWN VOLUME CAN BE PUT ON AN OFF-LINE DRIVE AND
*  IDENTIFIED, OR THE VOLUME CURRENTLY ON ANY ON-LINE DRIVE CAN BE
*  DISCOVERED.
*  THE OPERATOR ENTERS A UNIT ADDRESS, E.G. '190', AND THE PROGRAM
*  REPLIES 'PACK ON 190 ISVOL123'.  HE MAY THEN EITHER ENTER A
*  DIFFERENT ADDRESS (AND GET A SIMILAR REPLY) OR ENTER 'NEW', WHEN THE
*  PROGRAM SAYS 'GIVE NEW VOLUME SERIAL NUMBER OFR VOL123 ON 190'
*  (ALWAYS REFERRING TO THE LAST DRIVE IDENTIFIED).  THE OPERATOR CAN
*  NOW ENTER ANY 6 CHAR SERIAL, WHICH WILL BE WRITTEN INTO THE VOLUME
*  LABEL, OR 'CANCEL' (TO CANCEL THE CLIP OPTION, NOT THE PROGRAM)
*  NOTETHAT THE PROGRAM WILL REJECT UNIT ADDRESSES WHICH ARE NOT FOR
*  DISK UCBS IN THE CURRENT SYSTEM, BUT IF THE OPERATOR GIVES THE
*  ADDRESS OF A DISK DRIVE WHICH HAS NOT GOT A PACK READY ON IT AN
*  INTERVENTION REQUIRED SITUATION WILL ARISE.
*
*  JCL REQUIRED TO RUN THIS PROGRAM CONSISTS OF ONE DD CARD ONLY.....
*  //IDENTIFY  EXEC  PGM=FINDVOL
*  //SYS  DD  VOL=REF=SYS1.SVCLIB,SPACE=(TRK,2)
*  ....THE SYS DD MAY REFER TO ANY RESIDENT VOLUME.  IT IS REQUIRED
*  TO ALLOW AN OUTPUT DATA SET TO BE OPENED, THUS CREATING A DEB WITH
*  A FILE MASK ALLOWING WRITE OPERATIONS.
*
*
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         LA    10,PARMFLD
         OPEN  (SYS,(OUTPUT))
         SPMODE  PROB,0
         LA    2,SYS         A(DCB)
         L     3,44(2)       A(DEB)
         MVC   38(10,3),EXTENT
         LR    9,3
         SPMODE  PROB,*
         WTO   '*** DISKS FOR FINDVOL MAY BE ON OR OFFLINE'
         WTO   '*** VALID REPLIES FOR FINDVOL ***'
         WTO   'MSG A - ''XXX'' e.g. ''297'' TO DISPLAY VOL SERIAL NO O*
               N DRIVE XXX'
         WTO   'MSG A - ''new'' TO CLIP LAST VOL SERIAL DISPLAYED'
         WTO   'MSG A - ''end'' TO END PROGRAM'
         WTO   'MSG B - ''XXXXXX'' e.g. ''090123'' TO CLIP LAST VOLUME *
               DISPLAYED TO THIS SERIAL'
         WTO   'MSG B - ''cancel'' TO RETURN TO MSG A WITHOUT CLIPPING'
         B     WTOR
FINDUCB  LA    2,2(1)
         LA    0,3
         CLI   0(2),C'0'
         BNL   *+16
         IC    3,0(2)
         LA    3,9(3)
         STC   3,0(2)
         LA    2,1(2)
         BCT   0,*-24
         NC    ADDR,2(1)
         L     1,16
         L     2,36(1)       CVTILK1
         L     1,40(1)       CVTILK2
         SR    3,3
         IC    3,ADDR
         IC    3,0(2,3)
         AR    2,3
         IC    3,ADDR+1
         IC    3,0(2,3)
         AR    3,3
         IC    0,ADDR+2
         AR    0,0
         AR    1,3
         AR    1,0
         LH    1,0(1)
         CLC   STORUCB,13(1)
         BNE   NOUCB
         CLI   18(1),X'20'
         BNE   NOTDISK
         MVC   WTO+16(3),13(1)
         SPMODE  PROB,0
         STH   1,34(9)
         SPMODE  PROB,*
         EXCP  IOB
         WAIT  ECB=ECB
         CLI   ECB,X'7F'
         BNE   DISKERR
         MVC   WTO+23(6),VOLSER
WTO      WTO   'PACK ON XXX IS XXXXXX'
         NI    CLIP+1,X'0F'
WTOR     WTOR  '*** REPLY TO FINDVOL MSG A',REPLY,3,ECBW
         WAIT  ECB=ECBW
         NI    ECBW,X'3F'
         OC    REPLY,SPACES
         CLC   REPLY,END
         BE    GOBACK
CLIP     B     *+14
         CLC   REPLY,NEW
         BE    CHANGE
         MVC   2(3,10),REPLY
         LR    1,10
         MVC   STORUCB,REPLY
         MVC   ADDR,MASK
         B     FINDUCB
CHANGE   MVC   WTOR2+50(6),WTO+23
         MVC   WTOR2+60(3),WTO+16
WTOR2    WTOR  'GIVE NEW VOLUME SERIAL NUMBER FOR XXXXXX ON XXX - FINDV*
               OL MSG B',NEWVOL,6,ECBW
         WAIT  ECB=ECBW
         NI    ECBW,X'3F'
         OC    NEWVOL,SPACES
         CLC   NEWVOL,CANCEL
         BE    WTOR
         MVC   VOLSER,NEWVOL
         MVI   CP+16,5
         EXCP  IOB
         WAIT  ECB=ECB
         MVI   CP+16,6
         CLI   ECB,X'7F'
         BE    WTOR
         WTO   'DISK WRITE ERROR. VOLUME NOT CLIPPED AND MAY HAVE UNPRE*
               DICTABLE LABEL'
         B     WTOR
GOBACK   L     13,4(13)
         LM    14,12,12(13)
         BR    14
NOUCB    WTO   'FINDVOL CANNOT FIND UCB FOR ADDRESS SPECIFIED'
         B     WTOR
DISKERR  WTO   'FINDVOL HAS DISK ERROR ON READING VOLUME LABEL'
         B     WTOR
NOTDISK  WTO   'FINDVOL SAYS UNIT YOU SPECIFIED IS NOT A DISK'
         B     WTOR
PARMFLD  DC    H'3'
         DC    CL3' '
ECB      DC    F'0'
ECBW     DC    F'0'
IOB      DC    X'C2000000'
         DC    A(ECB)
         DC    2F'0'
         DC    A(CP)
         DC    A(SYS)
         DC    2F'0'
DISKAD   DC    X'0000000000000003'
CP       CCW   49,DISKAD+3,X'60',5
         CCW   8,CP,X'00',1
         CCW   6,VOL1,X'20',80
EXTENT   DC    X'00000000000000010002'
VOL1     DS    CL4
VOLSER   DS    CL6
         DS    CL70
REPLY    DC    C'END'
NEW      DC    C'NEW'
CANCEL   DC    C'CANCEL'
SPACES   DC    CL6' '
NEWVOL   DC    CL6' '
STORUCB  DC    CL3' '
END      DC    C'END'
MASK     DC    X'0F0F0F'
ADDR     DC    X'0F0F0F'
SYS      DCB   DDNAME=SYS,MACRF=(E)
         END
