PROC 0 EXITKEY() VGET(PROFILE) PANEL(V00P51) VGETVARS() DELETE +
    PROFVARS(DUMMY)
/***********************************************************************
/* DESCRIPTION      - DEFINE A VSAM DATASET
/*
/* AUTHOR           - PAUL FOX
/*
/* MODIFICATIONS    - 09/04/85 - IST VERSION.
/*
/***********************************************************************
CONTROL NOFLUSH NOMSG /*MSG CONLIST LIST

  ISPEXEC VGET (VSNAME VSMOD VSTYPE VSBUFSP VSATYP VSSHR +
      VSDCISZ VSDALLOC VSDPRIM VSDSEC VSCIFSP VSCAFSP VSALRL +
      VSMLRL VSKEYLEN VSKEYOFF VSDVOL VSIMBED VSREPL +
      VSIVOL &VGETVARS) &VGET

  ISPEXEC VGET (&PROFVARS) PROFILE

  SET &EXITCODE = 0
  SET &MESSAGE =
  SET &CURSOR =
  GOTO DISPLAY
LOOP: +
  IF  &EXITKEY = ENTER THEN DO
      GOTO FINISH
  END
DISPLAY: +
  ISPEXEC DISPLAY PANEL(&PANEL) MSG(&MESSAGE) CURSOR(&CURSOR)
  SET &OLDCC = &LASTCC
  SET &MESSAGE =
  SET &CURSOR =
  IF &OLDCC = 8 THEN DO
      SET &EXITCODE = 8
      GOTO FINISH
  END

  ISPEXEC CONTROL DISPLAY LINE START(15) /* SET LINE MODE

  IF  &DSN ^= THEN DO
      /* SET UP INDEX AND DATA DSN'S
      SET &DSNDATA = &SUBSTR(1:&LENGTH(&DSN)-1,&DSN).D'
      SET &DSNIND  = &SUBSTR(1:&LENGTH(&DSN)-1,&DSN).I'
  END

  IF  &VSTYPE = INDEXED +
  OR  &VSTYPE = THEN +
     SET &INDEX = INDEX(NAME(&DSNIND) &VSIMBED &VSREPL &IVOL)
  ELSE +
      SET &INDEX =

  IF  &DELETE = DELETE THEN DO
      /* DELETE THE CLUSTER BEFORE DEFINING
      DELETE &DSN CLUSTER
  END

  /* DEFINE THE CLUSTER
  CONTROL MSG /*LIST CONLIST
  DEFINE CLUSTER(NAME(&DSN) &MODEL &VSTYPE &BUFSP &ALLOCTYP &SHROPT +
      SPEED) +
      DATA(NAME(&DSNDATA) &DCISZ &DALLOC &FSP &RECSIZE &KEYS &DVOL) +
      &INDEX
  SET &OLDCC = &LASTCC
  CONTROL NOMSG /*NOLIST NOCONLIST
  IF  &OLDCC ^= 0 THEN DO
      SET &ERRCC = &OLDCC
      SET &ERRDSN = &DSN
      SET &MESSAGE = VV511A
      GOTO DISPLAY
  END

  SET &ERRDSN = &DSN
  SET &MESSAGE = VV511B

  GOTO LOOP

FINISH:+
  ISPEXEC VPUT (VSNAME VSMOD VSTYPE VSBUFSP VSATYP VSSHR +
      VSDCISZ VSDALLOC VSDPRIM VSDSEC VSCIFSP VSCAFSP VSALRL +
      VSMLRL VSKEYLEN VSKEYOFF VSDVOL VSIMBED VSREPL +
      VSIVOL &VGETVARS) &VGET
  ISPEXEC VPUT (&PROFVARS) PROFILE
  EXIT CODE(&EXITCODE)
