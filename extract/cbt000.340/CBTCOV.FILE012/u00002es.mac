RTPMCOPY CSECT
***********************************************************************
*                                                                     *
*             R  T  P  M  C  O  P  Y                                  *
*       M A I N T E N A N C E  P R O G R A M  F O R  T S G  T A P E S *
***********************************************************************
*       FUNCTION  - THIS PROGRAM IS INTENDED FOR CREATING AND UPDATING*
*                  TAPES FOR EXPORT TO NON-TSG PERSONNEL. THESE TAPES *
*                  CONTAIN VARIOUS UTILITY PROGRAMS ,MACROS ,ETC.
*                                                                     *
*                   AT THE SAME TIME AS IT CREATES A NEW TAPE, THE    *
*                   PROGRAM CAN MAKE UP TO FIVE COPIES OF THE NEW TAPE*
*                   ,TO CONFORM TO THE SPECIFICATIONS SUPPLIED BY THE *
*                   USER IN THE FORM OF DD CARDS.
*                                                                     *
*       TAPE FORMAT-THE TAPES ARE MULTI-FILE UNLABBLLED TAPES HAVING  *
*                   AN UNBLOCKED FIRST FILE,DESCRIBING THE CONTENTS,  *
*                   FOLLOWED BY N FURTHER FILES BLOCKED (80,3200)     *
*                                                                     *
*       CONTROL CARDS- THE CONTENTS OF THE NEW TAPE IS SPECIFIED,FILE *
*                   BY FILE, BY A SERIES OF CONTROL CARDS. EACH SUCH  *
*                   CONTROL CARD SPECIFIES THE SOURCE OF ITS CORRES-  *
*                   PONDING FILE.                                     *
*             FORMAT:  CC 1- 7   MUST  CONTAIN   '..FILE '            *
*                      CC 8- 9  CONTAIN THE NUMBER OF THE FILE ON THE *
*                               OUTPUT TAPE,RIGHT ADJUSTED WITH ZEROS,*
*                               TO A MAX. OF 99                       *
*                      CC10-15  SHOULD CONTAIN  ' FROM '              *
*                      CC16-23  SOURCE OF THE INPUT FILE. THERE ARE 3 *
*                               POSSIBLE SOURCES AND CARD FORMATS :-  *
*                             A)CARDS FOLLOWING THE CONTROL CARD      *
*                                FORMAT:  * IN C16                    *
*                             B)GIVEN FILE ON OLD EXPORT TAPE (IE. IN)*
*                                FORMAT: NUMBER OF THIS FILE ON IN -  *
*                                IN C16 OR CC16+17                    *
*                             C)GIVEN FILE AS SPECIFIED BY A DDNAME   *
*                                FORMAT: DDNAME STARTING IN C16 ,LEFT *
*                                ADJUSTED AND BLOCKED UP TO 8CHARS    *
*                                WITH BLANKS.                         *
*                      CC24-80  UNDEFINED. COULD BE USED FOR COMMENTS *
*                               AS WILL BE PRINTED ON SYSPRINT .      *
*                                                                     *
*       DDNAMES  - IN - RESERVED DDNAME FOR OLD EXPORT TAPR           *
*                  OUT1                                               *
*                  OUT2  RESERVED FOR THE NEW EXPORT TAPES CREATED BY *
*                  OUT3  PROGRAM. NAMES SHOULD BE ASSIGNED IN ORDER   *
*                  OUT4  IE. IF 3 PRODUCED THESE ASSUMED TO BE        *
*                  OUT5  OUT1,OUT2,OUT3.                              *
*                  OUT6                                               *
*                                                                     *
*                  ALL DD CARDS SHOULD SPECIFY 'KEEP' SO THAT TAPES   *
*                  ARE NOT UNLOADED DURING THE JOB.                   *
*                  BLOCKSIZE MUST BE SPECIFIED BY USER ON ALL INPUT DD*
*                  CARDS.
***********************************************************************
***********************************************************************
*    R T P M C O P Y   :  E X A M P L E                               *
*                                                                     *
***********************************************************************
*                                                                     *
*       REQUIREMENTS :  TO UPDATE THE EXISTING MULTI-FILE TAPE,ON     *
*              MAGNETIC TAPE NUMBER 123456  (9 TRACK), PRODUCING TWO  *
*              NEW TAPES : A) NINE TRACK  ,800 BPI                    *
*                          B) SEVEN TRACK ,800 BPI                    *
*                                                                     *
*       SOURCE OF FILES : THE LIST OF CONTENTS (FILE 1) MUST BE       *
*              REORGANISED  - INPUT FROM CARDS OF NEW LIST            *
*                         ONE NEW FILE FROM A LIBRARY                 *
*                         ONE NEW FILE FROM A SPECIFIC MAG TAPE 091234*
*                         OTHERS FROM AN EXISTING TAPE OR FROM CARDS  *
*                                                                     *
*       EXAMPLE OF CONTROL STREAM REQUIRED :                          *
*       //MAINTCPY  EXEC    PGM=RTPMCOPY                              *
*       //SYSPRINT  DD      SYSOUT=A                                  *
*       //IN        DD      DSN=OLDTAP,DISP=(SHR,KEEP),               *
*                           LABEL=(,NL),VOL=SER=123456,UNIT=T9        *
*       //OUT1      DD      DSN=NEW1,DISP=(,KEEP),LABEL=(,NL),UNIT=T9 *
*       //OUT2      DD      DSN=NEW2,DISP=(,KEEP),LABEL=(,NL),UNIT=T7,*
*                           DCB=(TRTCH=C,DEN=2)                       *
*       //X1        DD      DSN=TSG.SUBLIB(MEMBX),DISP=SHR            *
*       //X2        DD      DSN=STRANGER,DISP=(SHR,KEEP),UNIT=T9,     *
*                           VOL=SER=091234,LABEL=(3,SL)               *
*       //SYSIN     DD      *                                         *
*       ..FILE 01 FROM *        CONTENTS OF NEW TAPE                  *
*           SOURCE CARDS OF                                           *
*           NEW FILE 1
*       ..FILE 02 FROM 6        FROM OLD TAPE FILE 6                  *
*       ..FILE 03 FROM 02       FROM OLD TAPE FILE I                  *
*       ..FILE 04 FROM X1       FROM SPECIFIED DATA SET               *
*       ..FILE 05 FROM 07                                             *
*       ..FILE 06 FROM *        FROM FOLLOWING CARDS UP TO NEXT       *
*           SOURCE CARDS                     CONTROL CARD             *
*       ..FILE 07 FROM X2                                             *
*       ..FILE 08 FROM 2                                              *
*       /*                                                            *
*       //                                                            *
*                                                                     *
*       THE PROGRAM PRINTS OUT A MESSAGE TO THE OPERATOR:-            *
*       'GIVE REEL NUMBERFOR TAPE JUST MOUNTED ON (UNIT)'             *
*       IT USES THIS TO IDENTIFY THE TWO NEW TAPES AND MAKES A NOTE   *
*       OF (DDNAME ON REEL NUMBER) ON SYSPRINT.                       *
*                                                                     *
*       SYSPRINT ALSO CONTAINS A LISTING OF EACH CONTROL CARD PLUS A  *
*       SUCCESSFUL TRANSFER MESSAGE ,OR THE APPROPRIATE ERROR MESSAGE.*
*       A COUNT OF THE NUMBER OF FILES ON THE NEW TAPES  IS KEPT AND  *
*       THE JOB WILL ABEND AFTER AN ERROR                             *
***********************************************************************
         EJECT
***********************************************************************
*
*DOCUMENTATION - USE OF REGISTERS
*  R1   -SYSTEM USE IN GET LOCATE
*  R2   -USED BY TRANSLATE AND TEST
*  R3   -JFCB'S FOR OUTPUT TAPES -FOR LOOPING TO OPEN
*  R4   -COUNT OF OUTPUT TAPES
*  R5   -DCB'S OF OUTPUT TAPES IN TURN-FOR USE IN OPEN/CLOSE/PUT LOOPS
*  R6   -SYSPRINT LINE COUNT
*  R7   -BINARY FILE NUMBER   (1)INPUT FROM FILE X ON IN
*                            (2)OUTPUT TO FILE X  ON  OUT
*  R8   -POINTS TO LATEST CONTROL CARD IN BUFFER ,TO AVOID CORRUPTION
*  R9   -USED IN LOOPS REFFERRING TO OUYPUT TAPES AS A COUNT,IE ONLY
*        THOSE  FOR WHICH DD CARDS GIVEN.
*        NB. IF ONLY 2 TAPES THESE ASSUMED TO BE OUT1 ANDOUT2  ETC.
*  R10  -DCB'S FOR THE GET'S OF RECORDS
*        IE. POINTS TO THE DCB OF THE INPUT SOURCE SPECIFIED BY CONTROL
*        CARD
*  R11  -BASE REGISTER
*  R12  -BASE REGISTER
*  R......
***********************************************************************
         EJECT
*        BEGIN BASE=(12,11)
         STM   14,12,12(13)
         BALR  12,0
         USING *,12,11
IHB1     EQU   *
         L     11,IHB1A
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'                  NEW SAVE AREA
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         B     IHB1B
IHB1A    DC    A(IHB1+4096)
IHB1B    EQU   *
         OPEN  (SYSIN,,PRINT,(OUTPUT))
***********************************************************************
* SET UP SYSPRINT HEADINGS
***********************************************************************
         MVI   PRINTCC,X'8B'
         PUT   PRINT,PRINTCC            NEW PAGE
         MVC   LINE,SPACES
         MVC   LINE+40(49),HEADING
         MVI   PRINTCC,X'19'
         PUT   PRINT,PRINTCC            HEADING
         MVC   LINE,SPACES
         MVC   LINE+20(50),REEL
         MVI   PRINTCC,X'11'
         PUT   PRINT,PRINTCC            DDNAME/REEL NUMBER
         MVC   LINE,SPACES
         EJECT
*                                       READ IN ALL JFCB'S
     RDJFCB (IN,,OUT1,,OUT2,,OUT3,,OUT4,,OUT5,,OUT6)
*
*
         SR    6,6                      LINE COUNT FOR SYSPRINT
         EJECT
***********************************************************************
* THE FOLLOWING SECTION FINDS WHICH OUTPUT TAPES ARE WANTED -IE HAVE
*DDCARDS SUPPLIED ,AND OPENS FILE 1 ON EACH OF THESE
***********************************************************************
*
OUTTAPE  OPEN  (OUT1,(OUTPUT,LEAVE),                                   *
               OUT2,(OUTPUT,LEAVE),                                    *
               OUT3,(OUTPUT,LEAVE),                                    *
               OUT4,(OUTPUT,LEAVE),                                    *
               OUT5,(OUTPUT,LEAVE),                                    *
               OUT6,(OUTPUT,LEAVE))
         SR    4,4
         LA     5,OUT1                 R5-> DCB OF EACH OUTTAPE IN TURN
TESTDD   TM    48(5),16                 TEST FOR SUCCESSFUL OPEN
         BO    INUSE                    IE DD CARD SUPPLIED
         LTR   4,4                      R4 - COUNT OF OUTTAPES
         BNZ   FILE1                    ALL USED OUTTAPES FOUND
         MVC   LINE,SPACES
         MVC   LINE(20),ABNORM
         MVC   LINE+22(38),NODD         NO DD CARDS
         MVI   PRINTCC,X'11'
         PUT   PRINT,PRINTCC
         B     WINDUP                   ABANDON RUN
*
INUSE    LA    4,1(4)                   INCREASE TAPE COUNT (BIN)
         L     1,44(5)                  ACCESS UNIT
         L     1,32(1)                    NAME (3BYTES)
         MVI   REELNO+5,64             PUT BLANK INTO LAST POSITION
         XC    ECB1,ECB1                CLEAR
         MVC   ASKREEL+59(3),13(1)      PUT INTO THE MESSAGE TO OPERATR
ASKREEL  WTOR  'GIVE REEL NUMBER FOR TAPE JUST MOUNTED ON      ',      *
               REELNO,6,ECB1
         CVD   4,CNTDEC
         OI    CNTDEC+7,15             SIGN
         UNPK  LINE+53(1),CNTDEC       SET UP
         MVC   LINE+50(3),=CL3'OUT'       PRINT
         MVI   PRINTCC,X'09'              LINE
         WAIT  ECB=ECB1                 REPLY FROM OPERATOR
         MVC   LINE+62(6),REELNO
         PUT   PRINT,PRINTCC
         MVC   LINE,SPACES
         LA    5,96(5)                 POINT NEXT DCB
         C     4,=F'6'
         BL    TESTDD                   NOT ALL SIX DONE YET
FILE1    MVI   PRINTCC,X'8B'            SKIP NEW PAGE
         PUT   PRINT,PRINTCC
         MVI   PRINTCC,X'19'
         MVC   LINE(49),HEADING
         PUT   PRINT,PRINTCC            HEADING LINE
         MVC   LINE,SPACES
         LA    6,4(6)                  INITIALIZE LINE COUNT
        EJECT
***********************************************************************
*GET CONTROL CARD
***********************************************************************
         GET   SYSIN                    R1-> BUFFER CONT 1ST CARD
         LR    8,1
         CLC   0(9,1),=C'..FILE 01'
         BE    SETFILE1
         MVI   PRINTCC,X'11'
         MVC   LINE(20),ABNORM
         MVC   LINE+21(35),NOFILE1      NOT FOUND FILE1 CONTROL CARD
         PUT   PRINT,PRINTCC
         MVC   LINE,SPACES
         B     WINDUP
SETFILE1 OI    MKFILE1,X'01'            SET FILE1 MARKER
         B     COPYCC
         EJECT
*
MPROC    GET   SYSIN                    GET CARD
         LR    8,1                      SAVE R1
         CLC   0(6,8),=C'..FILE'
         BE    MPROC2
         MVI   PRINTCC,X'11'
         MVC   LINE(20),ABNORM
         MVC   LINE+21(36),NOCCD        NOT CONTROL CARD
         PUT   PRINT,PRINTCC
         MVC   LINE,SPACES
         B     WINDUP
*
**********************************************************************
*CHECK THAT THE FILE NUMBER FIELD OF CONTROL CARD IS NUMERIC
**********************************************************************
*
MPROC2   TRT   7(2,8),NUMTABLE
         BC    8,ORDER                  BRANCH IF IS NUMERIC
         MVI   PRINTCC,X'11'
         MVC   LINE(20),ABNORM          ABNORMAL END
         MVC   LINE+21(29),FILENO        ILLEGAL FILE NUMBER
         PUT   PRINT,PRINTCC
         MVC   LINE,SPACES
         B     WINDUP
*
ORDER    AP    CNTR,K1P
         PACK  CHARCNTR,7(2,8)          PACK FILE NUMBER
         CP    CNTR,CHARCNTR            CHECK IT IS IN STRICT SEQUENCE
         BE    COPYCC
         MVI   PRINTCC,X'11'
         MVC   LINE(20),ABNORM
         MVC   LINE+21(34),OUTORDER
         PUT   PRINT,PRINTCC            OUT OF ORDER
         MVC   LINE,SPACES
         B     WINDUP                   CLOSE FILES
        EJECT
**********************************************************************
*COPY OF CONTROL CARD TO SYSPRINT
**********************************************************************
COPYCC   MVI   PRINTCC,X'09'
         MVC   LINE+10(80),0(8)
         PUT   PRINT,PRINTCC            COPY OF CONTROL CARD
         MVC   LINE,SPACES
         LA    6,2(6)                   INCREASE LINE COUNT
         EJECT
***********************************************************************
*DETERMINE SOURCE OF INPUT FILE FROM CONTROL CARD & SET R10 TO POINT TO
*             REVEVANT  DCB
**********************************************************************
*
         NI    CARDINPT,X'00'          CLEAR MARKER
         TRT   15(1,8),NUMTABLE         TEST IF NUMERIC
         BC    8,FROMIN                 IF IS THEN INPUT IS FROM IN
         CLC   15(1,8),=C'*'            TEST FOR *
         BNE   FROMDD                   IF IS NOT THEN INPUT FROM DDNME
         OI    CARDINPT,X'01'           IF * THEN INPUT FROM CARDS
         LA    10,SYSIN
         B     OUTOPEN
FROMIN   LA    10,IN                   OBTAIN FROM OLD TAPE (IN)
         TRT   16(1,8),NUMTABLE         TEST IF CCD COL17 IS NUMERIC
         BC    8,TWOFIG                 BRANCH IF IS
         MVC   16(1,8),15(8)
         MVI   15(8),X'F0'              MAKE '1 ' INTO '01'
TWOFIG   PACK  UNPIN,15(2,8)
         CVB   7,UNPIN                  CONVERT FILE NO WITHIN IN
         STH   7,JFCBAREA+68            ADJUST JFCB OF IN (FILE SEQUEN)
         OPEN  (IN,(INPUT,LEAVE)),TYPE=J   OPEN FILE WITHIH 'IN'
         B     OUTOPEN
FROMDD   LA    10,FILEINPT              INPUT VIA DDCARD SPECIFICATION
         MVC   FILEINPT+21(3),=X'000001'  RESET BUF CONTROL BLOCK
         MVC   FILEINPT+62(2),=X'0000'   RESET BLOCKSIZE
         MVC   FILEINPT+40(8),15(8)     ADJUST DDNAME IN DCB FROM CCD
         OPEN  (FILEINPT,(INPUT,LEAVE))
*
OUTOPEN  PACK  UNPNEW,7(2,8)           OUTPUT FILE NUMBER
         CVB   7,UNPNEW
         LR    9,4                      COUNT FOR LOOP
         LA    5,OUT1                  R5->DCB
         LA    3,JFCBAR1               R3->JFCB
OUTLOOP  STH   7,68(3)                 ADJUST JFCB'S IN TURN
         OPEN  ((5),(OUTPUT,LEAVE)),TYPE=J   OPEN FILE ON OUTPUT
         LA    5,96(5)                  POINT NEXT  DCB
         LA    3,176(3)                POINT NEXT JFCB
         BCT   9,OUTLOOP
*
*                                                                     *
**********************************************************************
*THIS FOLLOWING LOOP GETS AND PUTS RECORDS UNTIL ALL OF A FILE HAS
* BEEN TRANSFERRED. NB CHECKS IN CARD INPUT FOR CONTROL CARDS
**********************************************************************
*
READRECD OI    EODSYSIN,X'01'          MARK TRANSFER
         GET   (10)                    R10-> RELEVANT DCB
         LR    2,1
         NI    EODSYSIN,X'00'          CLEAR MARKER
         TM    CARDINPT,X'01'
         BNO   NOTCD                    BRANCH IF NOT CARD INPUT
         CLC   0(6,1),=C'..FILE'        IS IT CONTROL CARD
         BNE   NOTCD                    NO,CONTINUE TRANSFER
         OI    EODSYSIN,X'01'          YES,SET MARKER,NOT END SYSIN
         LR    8,2
         B     SUCES                   COMPLETED FILE TRANSFER
NOTCD    LA    5,OUT1
         LR    9,4
PUTLOOP  PUT   (5),(2)
         LA    5,96(5)                  POINT NEXT DCB
         BCT   9,PUTLOOP
         B     READRECD
*
**********************************************************************
* HAVE OUTPUT TOTAL FILE TO ALL OUTPUT TAPES
***********************************************************************
*
SUCES    MVI   PRINTCC,X'11'
         MVC   LINE+30(28),SUCCESS
         PUT   PRINT,PRINTCC            SUCCESSFUL TRANSFER
         MVC   LINE,SPACES
         LA    6,3(6)                   INCR LINE COUNT
         C     6,=F'54'                 FULL PAGE
         BNH   CLOSEINP
*
        EJECT
***********************************************************************
*PAGE OVERFLOW ROUTINE
***********************************************************************
*
         MVI   PRINTCC,X'8B'
         PUT   PRINT,PRINTCC            NEW PAGE
         MVC   LINE,SPACES
         MVC   LINE+40(49),HEADING
         MVI   PRINTCC,X'19'
         PUT   PRINT,PRINTCC            HEADING LINE
         MVC   LINE,SPACES
         SR     6,6                    LINE COUNT RESET
        EJECT
***********************************************************************
*CLOSE INPUT AND OUTPUT FILES AND GO BACK TO CONSIDER NEXT FILE       *
***********************************************************************
*                                                                     *
CLOSEINP TM    CARDINPT,X'01'
         BO    CLOSEOUT                 DO NOT CLOSE SYSIN
         CLOSE ((10),LEAVE)             CLOSE INPUT FILES
         FREEPOOL (10)
CLOSEOUT LA    5,OUT1
         LR    9,4
CLSLOOP  CLOSE ((5),LEAVE)              CLOSE ALL OUTPUT FILES
         FREEPOOL (5)
         LA    5,96(5)
         BCT   9,CLSLOOP
*
BLSIZECH TM    MKFILE1,X'01'            IS THIS FILE 1
         BNO   REENTRY                  BRANCH ROUND IF NOT
         MVC   IN+62(2),NEWBLKSZ        IF IS THEN CHANGE ALL BLKSIZE
         LR    9,4                        OF IN & OUTPUT FILES
         MVC   IN+21(3),=X'000001'     ADJUST DCB
         LA     5,OUT1
BLZLOOP  MVC   62(2,5),NEWBLKSZ
         MVC   21(3,5),=X'000001'      ADJUST DCB
         LA    5,96(5)                  POINT NEXT DCB
         BCT   9,BLZLOOP
*
REENTRY  TM    EODSYSIN,X'01'          HAVE COMPLETED ALL TRANSFFERS?
         BNO   WINDUP                  END OF SYSIN FILE MET
         NI    EODSYSIN,X'00'          CLEAR MARKER
RREENTRY TM    CARDINPT,X'01'          CARD INPUT TEST
         BO    MPROC2                   HAVE ALREADY GOT THE NEXT
*                                       CONTROL CARD
         B     MPROC                    NONCARD RETURN TO GET NEXT
*                                       CONTROL CARD
         EJECT
***********************************************************************
*THE FOLLOWING SECTION DEALS WITH THE CLOSING OF THOSE FILES WHICH WERE
* OPEN THROUGHOUT THE JOB.
*THERE ARE TWO MEANS OF ENTRY
*  A)AFTER ANY ABNORMAL ENDING OF THE JOB - IN WHICH CASE THE NUMBER OF
*    FILES ON OUTPUT TAPES MESSAGE MAY BE INCORRECT
*  B)NORMAL END WHEN THERE ARE NO MORE CONTROL CARDS IN SYSIN -IE THE
*    EODAD EXIT
***********************************************************************
**
WINDUP   TM    EODSYSIN,X'01'          END OF TRANSFER OF FILE?
         BNO   GOWINDUP
         NI    EODSYSIN,X'00'          CLEAR MARKER
         B     SUCES
GOWINDUP MVI   PRINTCC,X'19'
         PUT   PRINT,PRINTCC            LEAVE 4 LINES BLANK
         MVC   LINE,SPACES
         MVC   LINE(35),NUMFILES
         UNPK  LINE+39(2),CHARCNTR       NUMBER OF LAST FILE OUTPUT
         OI    LINE+40,X'F0'           CORRECT FORMAT FOR PRINT
         PUT   PRINT,PRINTCC
         MVC   LINE,SPACES
*
         CLOSE (PRINT,,SYSIN)
*        GOBACK
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
********
         EJECT
***********************************************************************
*SYNAD ROUTINE -TRANSFER ERROR
*IE ERROR IN GET OR PUT OF A RECORD
***********************************************************************
*                                                                     *
TRANSERR MVI   PRINTCC,X'09'
         MVC   LINE(20),ABNORM
         MVC   LINE+22(22),NOTSUC
         PUT   PRINT,PRINTCC            UNSUCCESSFUL TRANSFER
         MVC   LINE,SPACES
         B     WINDUP
*                                                                     *
**
*SYNAD ROUTINE - ERROR ON SYSIN
*
SYSERRIN MVI   PRINTCC,X'09'
         MVC   LINE(20),ABNORM
         MVC   LINE+22(34),ERRSYSIN
         PUT   PRINT,PRINTCC            ERROR ON SYSIN
         MVC   LINE,SPACES
         B     WINDUP
**
***********************************************************************
         EJECT
***********************************************************************
**
*SYSPRINT MESSAGES
*
*
ABNORM   DC    CL20'ABNORMAL COMPLETION:'
ERRSYSIN DC    CL34'ERROR ON ACCESSING INFO FROM SYSIN'
FILENO   DC    CL29'ILLEGAL FORMAT IN FILE NUMBER'
HEADING  DC    CL49'RTPMCOPY: PREPARATION && UPDATING OF EXPORT TAPES '
NOCCD    DC    CL36'CONTROL CARD NOT FOUND WHEN EXPECTED'
NODD     DC    CL38'NO DD CARDS PROVIDED FOR OUTPUT TAPES '
NOFILE1  DC    CL35'CONTROL CARD  ..FILE 01  NOT FOUND '
NOTSUC   DC    CL22'UNSUCCESSFUL FILE TRANSFER '
NUMFILES DC    CL35'NUMBER FILES PUT ON OUTPUT TAPES = '
OUTORDER DC    CL34'FILES FOR OUTPUT ARE OUT OF ORDER '
REEL     DC    CL50'NEW EXPORT TAPES PRODUCED :  DDNAME ON REEL NUMBER'
SUCCESS  DC    CL28'SUCCESSFUL TRANSFER OF FILES'
*
**
         EJECT
* DECLARATIONS
PRINTCC  DC    X'00'
LINE     DC    CL132' '
SPACES   DC    CL132' '
*SET UP TABLE FOR TESTING FOR NUMERICS
NUMTABLE DC    256X'FF'
         ORG   NUMTABLE+X'F0'
         DC    10X'00'                  NUMERICS 0TO9
         ORG
*
NEWBLKSZ DC    H'3200'
REELNO   DC    CL6' '
ECB1     DC    F'0'
CNTDEC   DS    D                        COUNT OF TAPES IN DEC P OR UNP
MKFILE1  DC    X'00'                    MARKER
K1P      DC    X'1C'                    ONE,PACKED
CNTR     DC    PL2'1'
CHARCNTR DC    X'0000'                  CHARACTER FORM OF FILE NUMBER
CARDINPT DC    X'00'                    MARKER -CARD INPUT
EODSYSIN DC    X'00'                   EODAD SYSIN AT END FILE TRANSF
UNPIN    DC    D'0'                     FILE SEQUENCE OF FILE FROM IN
UNPNEW   DC    D'0'                     UNPACKED OUT FILE NO.
*
EXLIST   DS    0F                       IN
         DC    X'87'
         DC    AL3(JFCBAREA)
EXLIST1  DS    0F                       OUT1
         DC    X'87'
         DC    AL3(JFCBAR1)
EXLIST2  DS    0F                       OUT2
         DC    X'87'
         DC    AL3(JFCBAR2)
EXLIST3  DS    0F                       OUT3
         DC    X'87'
         DC    AL3(JFCBAR3)
EXLIST4  DS    0F                       OUT4
         DC    X'87'
         DC    AL3(JFCBAR4)
EXLIST5  DS    0F                       OUT5
         DC    X'87'
         DC    AL3(JFCBAR5)
EXLIST6  DS    0F                       OUT6
         DC    X'87'
         DC    AL3(JFCBAR6)
*
JFCBAREA DS    44F                      IN
JFCBAR1  DS    44F                      OUT1
JFCBAR2  DS    44F                      OUT2
JFCBAR3  DS    44F                      OUT3
JFCBAR4  DS    44F                      OUT4
JFCBAR5  DS    44F                      OUT5
JFCBAR6  DS    44F                      OUT6
*
*
*DCB'S
*
*
PRINT    DCB   DSORG=PS,DDNAME=SYSPRINT,MACRF=(PM),                    *
               RECFM=FBM,LRECL=133,BLKSIZE=1463
*
OUT1     DCB   DSORG=PS,DDNAME=OUT1,MACRF=(PM),EXLST=EXLIST1,          *
               RECFM=FB,LRECL=80,BLKSIZE=80,SYNAD=TRANSERR
OUT2     DCB   DSORG=PS,DDNAME=OUT2,MACRF=(PM),EXLST=EXLIST2,          *
               RECFM=FB,LRECL=80,BLKSIZE=80,SYNAD=TRANSERR
OUT3     DCB   DSORG=PS,DDNAME=OUT3,MACRF=(PM),EXLST=EXLIST3,          *
               RECFM=FB,LRECL=80,BLKSIZE=80,SYNAD=TRANSERR
OUT4     DCB   DSORG=PS,DDNAME=OUT4,MACRF=(PM),EXLST=EXLIST4,          *
               RECFM=FB,LRECL=80,BLKSIZE=80,SYNAD=TRANSERR
OUT5     DCB   DSORG=PS,DDNAME=OUT5,MACRF=(PM),EXLST=EXLIST5,          *
               RECFM=FB,LRECL=80,BLKSIZE=80,SYNAD=TRANSERR
OUT6     DCB   DSORG=PS,DDNAME=OUT6,MACRF=(PM),EXLST=EXLIST6,          *
               RECFM=FB,LRECL=80,BLKSIZE=80,SYNAD=TRANSERR
*
SYSIN    DCB   DSORG=PS,DDNAME=SYSIN,MACRF=(GL),EODAD=WINDUP,          *
               RECFM=FB,LRECL=80,SYNAD=SYSERRIN
*
***
* FILEINPT MUST BE USED FOR THE EXTRA FILES USED AS INPUT TO THE
*        TAPES. THE DDNAME APPROPRIATE TO THE FILE IN USE MUST BE
*        FILLED IN BEFORE OPENING THE FILE. THE BLKSIZE FOR EACH
*        FILE MUST BE SUPPLIED BY THE USER IN THE DDCARD.
*
FILEINPT DCB   DSORG=PS,MACRF=(GL),DDNAME=DUMMY,EODAD=SUCES,           *
               RECFM=FB,LRECL=80,SYNAD=TRANSERR
IN       DCB   DSORG=PS,DDNAME=IN,MACRF=(GL),EXLST=EXLIST,EODAD=SUCES, *
               RECFM=FB,LRECL=80,BLKSIZE=80,SYNAD=TRANSERR
*
****
         END
