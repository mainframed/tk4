SRE05    START
         TITLE 'RAAJ8SRE   M05  - SELECTIVE RECORD EDIT - OUTPUT PAGE HC
               EADING MODULE - D.E.MILLS'
         INMD
         LR    3,1
         USING QDUMMY,3
         L     OUTPUT,QLINE            ADDRESS OUTPUT AREA
         L     GRA,QINDIC              ADDRESS INDICATORS
         MVI   1(GRA),C'P'             SET PUT IND.FOR FCP MOD.
*
         MVI   0(OUTPUT),X'81'          AT H.O.F.
         DOMD  SRE00
*
* ON FIRST TIME THROUGH SET UP DATE & TIME
*
Z01A     BC    0,Z01B                  )
         TIME  DEC                     )   FIRST
         ST    1,FWORD                 )   TIME
         UNPK  CTIME(5),FWORD+1(3)     )   THROUGH
         MVC   CDATE(2),CTIME          )   SET
         MVC   CDATE+3(3),CTIME+2      )   UP
         ST    0,FWORD                 )   DATE
         OI    FWORD+3,X'0F'           )   AND
         UNPK  CTIME+2(7),FWORD(4)     )   TIME
         MVC   CTIME(2),CTIME+2        )
         MVI   CTIME+2,C':'            )
         MVC   CTIME+3(2),CTIME+4
         MVI   CTIME+5,C':'            )
         OI    Z01A+1,X'F0'
*
Z01B     L     GRA,QPGNO               INCREMENT
         AP    0(3,GRA),=P'1'           PAGE NUMBER
         MVC   CPGNO,=X'402020202120'
         ED    CPGNO,0(GRA)
         MVI   0(OUTPUT),X'02'
         MVC   5(18,OUTPUT),CDTATM
         MVC   56(21,OUTPUT),CPGHDG
         MVC   119(10,OUTPUT),CPAGE
         LR    1,3
         DOMD  SRE00                   PRINT HEADING
*
         L     GRA,QCOUNT              RESET
         MVC   0(2,GRA),=H'57'          LINECOUNT
*
         OUTMD
         EJECT
CDTATM   DS    0CL18
CDATE    DC    CL10'  /'
CTIME    DC    9C' '
FWORD    DS    F
CPGHDG   DC    C'SELECTIVE RECORD EDIT'
CPAGE    DC    C'PAGE'
CPGNO    DS    CL6
*
*
* DUMMY SECTION FOR INTER-MODULE PARAMS
*
QDUMMY   DSECT
QPARAM   DS    F            ADDRESS OF
QINDIC   DS    F                       INDICATOR BYTES
QINPUT   DS    F
QBLTH    DS    F
QLINE    DS    F                       OUTPUT AREA
QCOUNT   DS    F                       LINE COUNT
QRECORD  DS    F
QBLNO    DS    F
QRTP     DS    F
QPGNO    DS    F                       PAGE NUMBER
SRE05    CSECT
OUTPUT   EQU   6
GRA      EQU   14
         END
