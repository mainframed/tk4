SYSSCRAT CSECT
*
*  THIS PROGRAM.....
*        1.  ESTABLISHES A DATE/TIME TO ACT AS A DEADLINE.  THE DEFAULT
*              IS 12 HOURS BEFORE CURRENT BUT THIS CAN BE OVER-RIDDEN
*              BY THE OPERATOR, WHO RECEIVES A CONSOLE MESSAGE AT THE
*              START OF THE PROGRAM ASKING FOR PERMISSION TO SCRATCH
*              OR FOR A DIFFERENT TIME AND DATE.  UNLESS THE 'NOM'
*              PARAMETER APPEARS IN THE EXEC CARD PARM FIELD THIS
*              CONSOLE MESSAGE ALWAYS APPEARS AND THE OPERATOR MUST
*              REPLY 'Y' BEFORE PROCEESING STARTS.
*
*  NOTE THAT THE DATE AND TIME IN SYSTEM GENERATED NAMES IS THE
*  TIME AT WHICH THE READER JOB STARTED.  IF A RESIDENT READER HAS
*  BEEN RUNNING FOR OVER 12 HOURS DATA SETS BELONGING TO JOBS THAT HAVE
*  ONLY JUST BEEN READ IN WILL BE SCRATCHED BECAUSE THEY APPEAR TO
*  BE 12 HOURS OR MORE OLD.  EVEN IF THIS CAN APPLY THIS PROGRAM CAN
*  BE VERY USEFUL AFTER A COLD STARTBY GETTING RID OF ALL LEFT-OVER
*  DATA SETS UP TO,THE TIME OF SYSTEM FAILURE.
*
*  THE ABOVE PROBLEM CAN BE OVERCOME BY MODIFYING IEFVHA (THE RDR
*  INTERPRETER) WITH THE TSGVHEC CSECT INCLUDED AS A SEPARATE PROGRAM.
*  THIS CAUSES THE RDR INTERPRETER TO 'TIME STAMP' ALL TEMPORARY
*  DATA SET NAMES WITH THE TIME OF READING THAT JOB, NOT THE TIME OF
*  STARTING THE READER.
*
*        2.  SEARCHES THE VTOCS OF ALL DISK DEVICES SPECIFIED.  THE
*              DEFAULT IS ALL ONLINE DISK DRIVES WITH VOL SER NOS IN
*              THEIR UCBS, BUT A LIST OF DEVICE ADDRESSES CAN BE GIVEN
*              IN THE PARM FIELD OF THE EXEC CARD AND WILL RESTRICT
*              THE PROGRAM TO THE DEVICES LISTED.
*
*        4.  ALL DATA SETS WITH SYSTEM GENERATED NAMES WILL BE
*              COMPARED WITH THE DEADLINE. IF THE DATE/TIME IN THEM IS
*              IS EARLIER THAN THE DAEDLINE THEY WILL BE SCRATCHED,
*              EXCEPT WHEN A 'NOS' (NO SCRATCH) PARM WAS SPECIFIED -
*              WHEN THEY WILL BE LISTED BUT NOT SCRATCHED.
*
*        5.  ALL DATA SETS WITH NAMES BEGINNING '**SYSUT'WILL BE
*              SCRATCHED UNLESS EITHER A 'NOS' PARM WAS SPECIFIED OR AN
*              RB WITH NAME BEGINNING 'IEHM' CURRENTLY EXISTS.
*
*        6.  CONFIRMATION OF ALL ACTION TAKEN WILL BE PRINTED.  IF
*              SCRATCH GIVES AN UNUSUAL RETURN CODE A DESCRIPPION
*              WILL APPEAR IN THE RESULT COLUMN.
*        7.  WHENEVER THE NUMBER OF EMPTY DSCBS (INCLUDING THOSE MADE
*              BLANK BY SCRATCHING HERE) ENCOUNTERED BEFORE THE 'HIGH
*              WATER MARK' ADDRESS IS GREATER THAN 25 THE 'VTOCLEAN'
*              ROUTINE WILL BE ENTERED.  THIS COMPRESSES THE VTOC BY
*              MOVING THE F1 DSCBS FURTHEST FROM THE START OF THE VTOC
*              BACK TO FILL IN EMPTY DSCBS AT THE FRONT OF THE VTOC.
*              SINCE ONLY F1 DSCBS ARE MOVED THERE IS NO NEED TO CHANGE
*              ANY LINK FIELDS.  'VTOCLEAN' ALSO MOVES THE 'HIGH WATER
*              MARK' BACK TO THE CORRECT CURRENT ADDRESS BY UPDATING
*              THE F4 DSCB, AND THIS CAN REDUCE THE TIME FOR 'DUPLICATE
*              NAME SEARCH' VERY CONSIDERABLY EVERY TIME SPACE
*              ALLOCATION IS ENTERED.
*
*  TO RUN THIS PROGRAM TWO DD CARDS ARE REQUIRED.  DDNAME SYS IS USED
*  AS A MODEL TIOT ENTRY WHEN READING THE VTOCS - IT IS BEST DIRECTED
*  TO SYSRES.  DDNAME SYSPRINT DESCRIBES THE PRINT OUT AND HAS A
*  DEFAULT BLOCKSIZE OF 1463 (BUT THIS CAN BE OVERRIDDEN BY ANY
*  BLKSIZE GIVEN ON THE DD CARD IN MULTIPLES OF 133 THANKS TO THE
*  DCBEXITS MACRO)  JCL EXAMPLE.........
*        //SYS       DD  VOL=REF=SYS1.SVCLIB,DISP=SHR
*        //SYSPRINT  DD  SYSOUT=A
*
*  THE PARM FIELD OF THE EXEC CARD MAY BE OMITTED, OR
*  A PARAMETER LIST OF 3 CHARACTER ENTRIES, SEPARATED BY COMMAS, MAY
*  BE SUPPLIED AND WILL HAVE THE FOLLOWING EFFECT...
*
*   IF THE ENTRY IS NOM THE PROGRAM WILL ISSUE NO CONSOLE MESSAGE
*  REQUESTING THE OPERATOR'S APPROVAL FOR THE CALCULATED DATE AND TIME
*   TO BE USED FOR SCRATCHING, AND WILL ASSUME A REPLY OF 'Y'
*
*  IF THE ENTRY IS NOS THE PROGRAM WILL LIST ALL DSNAMES THAT IT
*   WOULD HAVE SCRATCHED, AND WILL SHOW 'LISTED' IN PLACE OF
*   'SCRATCHED' ON THE PRINT-OUT
*
*   ANY OTHER ENTRY IN THE PARM LIST WILL BE STORED ON THE ASSUMPTION
*   THAT IT IS A VALID DEVICE ADDRESS, AND IF THERE ARE ANY SUCH
*   ENTRIES THE PROGRAM WILL ONLY SCRATCH DSNAMES ON THOSE UNITS
*   WITH DEVICE ADDRESSES THAT APPEAR IN THE LIST (THUS SAVING TIME
*   BY NOT SEARCHING THE VTOCS OF DEVICES NOT IN THE LIST)
*
*   ANY COMBINATION OF OF THE ABOVE PARAMETERS CAN APPEAR IN ANY ORDER
*   IN A PARM LIS¯   ANY ERROR IN THE PARM LIST WILL CAUSE THE PROGRAM
*   TO END WITHOUT COMMENT.
*   ERRORS IN THE PARM LIST CAN OCCUR THROUGHTHE OMISSION OF COMMAS
*   CAUSING AN INAPPROPRIATE TOTAL LENGTH, OR BECAUSE MORE THEN 26
*   DEVICE ADDRESSES ARE LISTED.
*
*  EXAMPLES....
*        //A EXEC PGM=SYSSCRAT,PARM='290,291,292'
*        //B EXEC PGM=SYSSCRAT,PARM='NOM'
*        //C EXEC PGM=SYSSCRAT,PARM='130,NOS,NOM,131'
*
*
*  THIS VERSION OF SYSSCRAT HAS BEEN AMENDED TO DIFFERENTIATE BETWEEN
*  MFT AND MVT SYSTEMS.  WHEN TESTING WHETHER IEHMOVE IS PRESENT IN
*  THE SYSTEM BEFORE SCRATCHING '**SYSUT' TYPE NAMES IT DETERMINES IF
*  WE ARE OPERATING UNDER MVT - IF SO IT SEARCHES THE JOB STEP AND LINK
*  PACK CDES FOR THE NAME 'IEHM....'INSTEAD OF SEARCHING THE RB NAMES.
*
*  IT ALSO HAS AN SPMODE MACRO TO ALLOW UCB ADDRESSES TO BE STORED IN
*  THE TIOT.
*
*        BEGIN
*  THIS IS THE EXPANSION OF THE RESPOND TSG BEGIN MACRO
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         L     1,0(1)
         LH    2,0(1)
         LTR   2,2
         BC    8,ENDPARM
         CLI   1(1),3
         BL    GOBACK
         LA    1,1(1)
         LA    3,UNITS
         LA    4,UNITSEND
         LA    5,3
         LA    6,1
TNOS     CLC   1(3,1),NOS
         BNE   TNOM
         OI    NOSBR+1,240
         MVC   NORM,NOSRM
         B     STEPONP
TNOM     CLC   1(3,1),NOM
         BNE   TUNIT
         OI    ASKBR+1,240
         B     STEPONP
TUNIT    MVC   0(3,3),1(1)
         NI    TUNBR+1,15
         LA    3,3(3)
         CR    3,4
         BNL   GOBACK
STEPONP  LA    1,4(1)
         SR    2,5
         BC    8,ENDPARM
         BC    4,GOBACK
         CLI   0(1),C','
         BNE   GOBACK
         SR    2,6
         BC    2,TNOS
         B     GOBACK
ENDPARM  EQU   *
         OPEN  (LIST,(OUTPUT))
         L     1,16          CVT ADDR
         MVC   MCSIZE,164(1)                HIGHEST ADDR
         IC    15,116(1)
         STC   15,SYSTYPE    X'10' = MVT, X'20' = MFT.
         L     1,0(1)        TCB DW ADDR
         L     1,4(1)        TCB ADDR
         L     1,12(1)       TIOT ADDR
         LA    1,24(1)       FIRST DDNAME
         SR    2,2
CLCSYS   IC    2,0(1)
         LTR   2,2
         BC    8,NOSYSDD
         CLC   4(8,1),SYSNM
         BE    SYSFND
         AR    1,2
         B     CLCSYS
NOSYSDD  PUT   LIST,ERRMA
         B     END
SYSFND   ST    1,SYSDDAD
         TIME
         ST    0,TIMEP
         ST    1,DATEP
         OI    TIMEP+3,15
         MVC   TITLE+82(6),TMASK
         ED    TITLE+82(6),TIMEP
         MVC   TITLE+91(6),DMASK
         ED    TITLE+91(6),DATEP+1
         SP    TIMEP,TWELVEH
         BC    11,DATESET
         AP    TIMEP,TFOURH
         SP    DATEP,ONE
         CP    DATEP+2(2),LASTDAY
         BH    SETLAST
         CP    DATEP+2(2),ONE
         BNL   DATESET
         SP    DATEP,YEAR
SETLAST  MVC   DATEP+2(2),LASTDAY
DATESET  OI    DATEP+3,15
         UNPK  DATEF,DATEP+1(3)
         UNPK  TIMEF,TIMEP
ASKBR    BC    0,SETUP
ASK      MVC   ASA+77(2),TIMEF
         MVC   ASA+80(2),TIMEF+2
         MVC   ASA+86(5),DATEF
ASA      WTO   'REQUEST PERMISSION TO SCRATCH ALL TEMPORARY DATA SETS C*
               REATED BEFORE HH.MM ON YYDDD'
WTOR     WTOR  'REPLY ''Y'', OR ''N'', OR GIVE DATE AND TIME IN FORM ''*
               HHMM-YYDDD''',REPLY,10,ECBW
         WAIT  ECB=ECBW
         NI    ECBW,63
         OC    REPLY,SPACES
         CLI   REPLY,C'Y'
         BE    SETUP
         CLI   REPLY,C'N'
         BE    END
         MVZ   HZ,REPLY
         XC    HZ,ZEROS
         BC    7,REPEAT
         CLI   REPLY+4,C'-'
         BNE REPEAT
         MVZ   YZ,REPLY+5
         XC    YZ,ZEROS
         BC    7,REPEAT
         MVC   HHMMF,REPLY
         MVC   DATEF,REPLY+5
         B     ASK
REPEAT   WTO   'PLEASE REPEAT REPLY FOR SYSSCRAT CORRECTLY'
         MVC   REPLY,SPACES
         B     WTOR
END      CLOSE (LIST)
*        GOBACK
*  THIS IS THE EXPANSION OF THE RESPOND TSG GOBACK MACRO
GOBACK   L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
SETUP    L     1,16
         L     2,40(1)       UCB LOOK-UP TABLE
SRCHUCBS LH    3,0(2)
         LTR   3,3
         BC    4,END
         BC    2,UCBFND
NEXTUCB  LA    2,2(2)
         B     SRCHUCBS
UCBFND   TM    18(3),32
         BZ    NEXTUCB
         TM    3(3),128      OFFLINE
         BZ    NEXTUCB
         CLI   28(3),0       UCB VOL SERIAL
         BE    NEXTUCB
TUNBR    B     RDJ
         LA    6,UNITS
TESTUNIT CLC   13(3,3),0(6)
         BE    RDJ
         LA    6,3(6)
         CLI   0(6),64
         BNE   TESTUNIT
         B     NEXTUCB
RDJ      EQU   *
         RDJFCB  (DISK)
         MVC   JFCB+118(6),28(3)            VOL SER TO JFCB
         MVC   JFCB(44),F4
         SPMODE  PROB,0
         L     1,SYSDDAD
         STH   3,18(1)       UCB ADDR TO TIOT
         SPMODE  PROB,*
         OPEN  (DISK),TYPE=J
         MVC   UCBTYPE,16(3)
         MVC   UCBSER,28(3)
         XC    HICCHHR,HICCHHR
         L     1,DISK+44
         L     1,32(1)
         MVC   SVSLINE+26(6),28(1)
         MVC   SVSLINE+39(3),13(1)
         LA    9,SVSLINE
         BAL   10,PRINTX
READV    READ  A,SF,DISK,DSCB,'S'
         CHECK A
         CLI   KEY,4
         BE    SETHI
         CLC   DISK+8(5),HICCHHR
         BNE   *+10
         ZAP   FINALCNT,SPARECNT
         CLI   KEY,0
         BNE   *+10
         AP    SPARECNT,ONE
         CLI   KEY,7
         BL    READV
         CLC   KEY(7),IE
         BE    TFORIEHM
         CLC   KEY(3),SYS
         BNE   READV
         CLC   KEY+8(2),T
         BNE   READV
         CLI   KEY+18,C'F'
         BE    *+12
         CLI   KEY+18,C'V'
         BNE   READV
*
*  THE ABOVE TESTS ASSUME THAT SYSTEM GENERATED NAMES WILL HAVE
*        'SYS' IN POSITIONS 1 - 3
*        '.T' IN POSITIONS 7 - 8
*        'F' OR 'V' IN POSITION 19
*  OTHER TESTS CAN BE ADDED IF OTHER FORMATS ARE USED
*
         CLC   KEY+3(5),DATEF
         BH    READV
         BL    SCRT
         CLC   KEY+10(4),HHMMF
         BH    READV
SCRT     SR    15,15
NOSBR    BC    0,LISTSCR
         SR    0,0
         SCRATCH  CAMLST
         AP    SPARECNT,ONE
LISTSCR  MVC   DSN,KEY
         MVC   VOL,UCBSER
         MVC   RES,NORM
         LTR   15,15
         BC    8,PRINT
         SRL   15,3
         MVC   RES,VNM
         LTR   15,15
         BC    8,PRINT
         MVC   RES,NFND
         CLI   SEQNO+1,1
         BE    PRINT
         MVC   RES,NEXP
         CLI   SEQNO+1,3
         BE    PRINT
         MVC   RES,IOERR
         CLI   SEQNO+1,4
         BE    PRINT
         MVC   RES,SNO
PRINT    SP    PCNTR,ONE
         BC    2,PRINLINE
         PUT   LIST,NP
         PUT   LIST,TITLE
         PUT   LIST,HDR
         ZAP   PCNTR,PLIMIT
PRINLINE PUT   LIST,LINE
         MVI   SEQNO+1,0
         B     READV
SETHI    SP    FINALCNT,FINALCNT
         SP    SPARECNT,SPARECNT
         MVC   HICCHHR,DATA+1
         B     READV
EOF      CLOSE (DISK)
         CP    FINALCNT,TWENTY5
         BL    NEXTUCB
         UNPK  SVCLINE+33(11),HICCHHR(6)
         MVI   SVCLINE+43,64
         TR    SVCLINE+33(10),TRTAB-240
         LA    9,SVCLINE
         BAL   10,PRINTX
         CALL  VTOCLEAN,(PARAM)
         L     10,NEWHI
         UNPK  EVCLINE+31(11),0(6,10)
         MVI   EVCLINE+41,64
         TR    EVCLINE+31(10),TRTAB-240
         LA    9,EVCLINE
         BAL   10,PRINTX
         B     NEXTUCB
PRINTX   SP    PCNTR,ONE
         BP    PLINX
         PUT   LIST,NP
         PUT   LIST,TITLE
         PUT   LIST,HDR
         ZAP   PCNTR,PLIMIT
PLINX    PUT   LIST,(9)
         BR    10
TFORIEHM L     1,16
         L     1,160(1)      TOP TCB ADDR
         TM    SYSTYPE,16
         BO    MVTSRCH
NEWTCB   LA    9,20          TO STOP INDEFINITE LOOP ON BAD RB ADD.
         L     5,0(1)        RB ADDRESS IN REG 5
NEWRB    LA    5,0(5)
         ST    5,RBADD
         TM    RBADD+3,3
         BC    7,NEXTTCB
         LTR   5,5
         BC    8,NEXTTCB
         CR    5,1
         BE    NEXTTCB
         C     5,MCSIZE
         BNL   NEXTTCB
         CLC   0(4,5),IEHM
         BE    READV
         L     5,28(5)
         BCT   9,NEWRB
NEXTTCB  L     1,116(1)
         LTR   1,1
         BC    8,SCRT
         B     NEWTCB
MVTSRCH  L     5,44(1)       TCB + 44 IS CDE LINK FOR JOB STEP TCBS
NEWCDE   LA    5,0(5)
         LTR   5,5
         BZ    NEXTMTCB
         CLC   8(4,5),IEHM
         BE    READV         IGNORE SCRATCH IF 'IEHM....' FOUND
         L     5,0(5)
         B     NEWCDE
NEXTMTCB L     1,116(1)
         LA    1,0(1)
         LTR   1,1
         BZ    TESTLPA       NO MORE TCBS
         B     MVTSRCH
TESTLPA  L     1,16
         L     5,188(1)      LPA CDE QUEUE
LPSRCH   LA    5,0(5)
         LTR   5,5
         BZ    SCRT
         CLC   8(4,5),IEHM
         BE    READV
         L     5,0(5)
         B     LPSRCH
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBM,LRECL=133,*
               BLKSIZE=1463,EXLST=XLISTB
DISK     DCB   DDNAME=SYS,DSORG=PS,LRECL=96,BLKSIZE=96,KEYLEN=44,      *
               EXLST=XLIST,EODAD=EOF,MACRF=(R),RECFM=F
XLISTB   DCBEXITS
ERRMA    DC    X'19'
         DC    CL132'DDNAME SYS NOT FOUND IN TIOT - SYSSCRAT ABANDONED'
NP       DC    X'8B'
TITLE    DC    X'19'
         DC    CL35' '
         DC    CL97'DATA SETS SCRATCHED BY TSG SYSSCRAT UTILITY AT HH.M*
               M ON YYDDD'
HDR      DC    X'11'
         DC    CL39' '
         DC    CL33'DATA SET NAME'
         DC    CL12'VOLUME'
         DC    CL48'RESULT'
LINE     DC    X'11'
         DC    CL24' '
DSN      DC    CL44' '
         DC    CL4' '
VOL      DC    CL6' '
         DC    CL4' '
RES      DC    CL16' '
         DC    CL34' '
NORM     DC    CL16'SCRATCHED'
NFND     DC    CL16'NOT FOUND'
NEXP     DC    CL16'NOT EXPIRED'
IOERR    DC    CL16'I/O ERROR'
SNO      DC    CL16'SHOULD NOT OCCUR'
VNM      DC    CL16'VOL NOT MOUNTED'
SVSLINE  DC    X'11',CL132'START VTOC SEARCH - PACK 123456 DRIVE 123'
SVCLINE  DC    X'11',CL132'START VTOCLEAN - OLD HIGH CCHHR 123456789A'
EVCLINE  DC    X'11',CL132'END VTOCLEAN - NEW HIGH CCHHR 123456789A'
TRTAB    DC    C'0123456789ABCDEF'
TMASK    DC    X'4020214B2020'
DMASK    DC    X'402021202020'
ZEROS    DC    C'00000'
HZ       DC    C'0000'
YZ       DC    C'00000'
F4       DC    44X'04'
TIMEF    DS    0CL7
HHMMF    DS    CL4
         DS    CL3
TWELVEH  DC    P'1200000'
TFOURH   DC    P'2400000'
ONE      DC    P'1'
YEAR     DC    P'0001000'
LASTDAY  DC    X'365C'
DATEF    DS    CL5
REPLY    DC    CL10' '
SPACES   DC    CL44' '
SYSNM    DC    CL8'SYS'
IEHM     DC    C'IEHM'
JFCB     DC    CL176' '
DSCB     DS    0CL140
KEY      DC    CL44' '
DATA     DC    CL96' '
VOLLIST  DC    H'1'
UCBTYPE  DC    XL4'00'
UCBSER   DC    XL6'00'
SEQNO    DC    H'0'
SYS      DC    C'SYS'
IE       DC    C'**SYSUT'
T        DC    C'.T'
SYSTYPE  DC    X'00'
PCNTR    DC    PL2'0'
PLIMIT   DC    P'28'
HICCHHR  DC    XL5'00'
FINALCNT DC    PL2'0'
SPARECNT DC    PL2'0'
TWENTY5  DC    P'25'
         CNOP  2,4
PARAM    DC    H'8'
         DC    CL8'SYS'
NEWHI    DC    A(FILEADDR)
RBADD    DC    F'0'
MCSIZE   DC    F'0'
ECBW     DC    F'0'
TIMEP    DC    F'0'
DATEP    DC    F'0'
SYSDDAD  DC    F'0'
XLIST    DC    X'87'
         DC    AL3(JFCB)
CAMLST   CAMLST  SCRATCH,KEY,,VOLLIST
NOSRM    DC    CL16'LISTED'
NOS      DC    C'NOS'
NOM      DC    C'NOM'
UNITS    DC    CL80' '
UNITSEND EQU   *
VTOCLEAN CSECT
WR1      EQU   9                  WORK REGISTER USED TO HOLD A COUNT
*                                      TO CONTROL ENQ AND DEQ DURING
*                                      RESHUFFLE
         ENTRY FILEADDR
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         LTR   1,1                 PARAMETER PASSED?
         BZ    NOPARM              NO
         L     1,0(1)              POINT PARM
         LH    2,0(1)              LENGTH OF PARM
         LTR   2,2                 ZERO?
         BZ    NOPARM              YES, NO PARM EFFECTIVELY
         CH    2,=H'8'             DDNAME LIMITED TO 8 CHARS
         BNH   L01                 SO
         LA    2,8                 TRUNCATE
L01      MVC   DCBVTOC+X'28'(0),2(1)   *EXECUTED TO MOVE IN DDNAME
L02      MVC   DCBVTOC2+X'28'(0),2(1)  * PASSED AS PARM
         MVC   DCBVTOC+X'28'(8),=CL8' '   BLANK OUT DDNAME
         MVC   DCBVTOC2+X'28'(8),=CL8' '
         BCTR  2,0                 MACHINE LENGTH
         EX   2,L01                MOVE IN DDNAME
         EX   2,L02                MOVE IN DDNAME
NOPARM   EQU  *
*   OPEN 2 DCB'S, 1 SCANS FOR FORMAT 1'S AND REPLACES THEM WITH
*      FORMAT 0'S:  2 SCANS FOR FORMAT 0'S TO TAKE THE FORMAT 1'S
         RDJFCB  (DCBVTOC)         READ JFCB INTO CORE
         MVI   VCJFCB,4
         MVC   VCJFCB+1(43),VCJFCB
         OPEN  (DCBVTOC,(UPDAT),DCBVTOC2,(UPDAT)),TYPE=J
         MVC   RNAME,VCJFCB+118    MOVE VOL SER FOR ENQ
ENQ      LA    WR1,10              DEQ EVERY 10 CHANGES TO GIVE SOMEONE
*                                   ELSE A CHANCE
         ENQ   (QNAME,RNAME,E,6,SYSTEM)     ENQ VTOC DATA SET
*        SEARCH VTOC FOR FIRST FORMAT 0 OR LAST FORMAT 1 DSCB
READ     EQU   *
         READ  DECB1,SF,DCBVTOC,DSCBAREA,MF=E    READ DSCB
         CHECK DECB1               WAIT FOR I/O COMPLETION
         CLI   DSCBAREA+44,X'00'       IS THIS A FORMAT 0 ?
         BNE   L03                     NO, TRY FORMAT 1
L02A     EQU   *
         CLC   FILEADDO,DCBVTOC+8  CHECK IF WE HAVE GONE BEYOND OLD
*                                    HI WATER MARK
         BNH   ENDVTOC
         TS    FMTOIND                 FIRST FORMAT 0?
         BNZ   READ                    NO, CONTINUE SCAN FOR FORMAT 1'S
         NOTE  DCBVTOC                 GET ADDRESS AND POINT
         ST    1,WORKAREA              DCBVTOC2 TO SAVE
         POINT DCBVTOC2,WORKAREA       TIME
         MVC   FILEADDR,DCBVTOC+8      SAVE CCHHR OF FIRST FORMAT 0
         B     READ
L03      CLI   DSCBAREA+44,X'F1'       IS THIS A FORMAT 1 ?
         BE    SHUFFLE                 RE-POSITION IT
         CLI   DSCBAREA+44,X'F4'       IS THIS A FORMAT 4 ?
         BNE   READ                    NO, SCAN FOR FORMAT 1'S
         MVC   FILEADDO,DSCBAREA+45    SAVE CURRENT HI WATER
         MVC   FILEADDR,DSCBAREA+45     AND SETS A DEFAULT NEW HI WATER
*        THIS CODE SKIPS OVER THE DSCB RECORDS TIL IT REACHES A FORMAT 0
               IT COULD BE REPLACED BY A ' B READ ' IF IT EVER PLAYS UP
         XC    DECB1,DECB1             CLEAR ECB
         MVC   FILEADDR,DCBVTOC+8      SET START OF VTOC SEARCH
         EXCP  IOBEXCP                 SEARCH FOR A FORMAT 0
         WAIT  ECB=DECB1               WAIT ON ABOVE SEARCH
         CLI   DECB1,X'7F'             O.K.?
         BE    L02A                    YES, FIRST FORMAT 0 FOUND
         ABEND 1,DUMP                  NO, DUMP (EITHER I/O ERROR, OR NO
               FORMAT 0'S LEFT IN WTOC)
         B     READ
SHUFFLE  CLI   FMTOIND,X'00'       HAVE WE FOUND A FORMAT 0 YET ?
         BE    READ                NO: NO PACKING POSSIBLE YET
L04      READ  DECB2,SF,,DSCBAREB,MF=E USE DCBVTOC2 TO READ FORMAT 0
         CHECK DECB2
         CLI   DSCBAREB+44,X'00'   VERIFY FORMAT 0
         BNE   L04                 NO, TRY AGAIN
         WRITE DECB1,SF,DCBVTOC,DSCBAREB,MF=E    WRITE F0 OVER F1
         CHECK DECB1
         WRITE DECB2,SF,,DSCBAREA,MF=E USE DCBVTOC2 TO WRITE F1 OVER F0
         CHECK DECB2
         MVC   FILEADDR,DCBVTOC2+8 SAVE CURRENT CCHHR (HI WATER)
         BCT   WR1,READ            GO TO READ NEXT FORMAT 1
         B     READ          DEQUING MIGHT LEAVE US WITH A BAD HI WATER
         DEQ   (QNAME,RNAME,6,SYSTEM) OR DEQ & RE-ENQ ON VTOC TO
         B     ENQ                 GIVE SOMEONE ELSE A CHANCE
ENDVTOC  EQU   *
*        CLOSE-T VTOC TO REPOSITION TO BEGINNING TO READ FORMAT 4 DSCB
         CLOSE (DCBVTOC,REREAD),TYPE=T      REPOSITION DATA SET
*        READ FORMAT 4 DSCB
         READ  DECB1,SF,DCBVTOC,DSCBAREA,MF=E
         CHECK DECB1               WAIT FOR I/O COMPLETION
*        UPDATE THE DS4HPCHR FIELD
         MVC   DSCBAREA+45(5),FILEADDR     MOVE IN NEW FILE ADDRESS
*        WRITE BACK UPDATED FORMAT 4 DSCB
         WRITE DECB1,SF,DCBVTOC,DSCBAREA,MF=E
         CHECK DECB1               WAIT FOR I/O COMPLETION
         DEQ   (QNAME,RNAME,6,SYSTEM)       DEQ VTOC DATA SET
         MVI   FMTOIND,X'00'       FOR REUSABILITY
         CLOSE (DCBVTOC,,DCBVTOC2)
         L     13,4(13)            RESTORE SAVEAREA ADDRESS
         RETURN  (14,12)           RETURN TO CONTROL PROGRAM
*        LIST FORM OF BSAM READ/WRITE
         WRITE DECB1,SF,DCBVTOC,MF=L
         WRITE DECB2,SF,DCBVTOC2,MF=L
JFCBLIST DC    0F'0',X'87',AL3(JFCB)
DCBVTOC  DCB   DSORG=PS,MACRF=RP,DDNAME=VTOCDD,BLKSIZE=96,KEYLEN=44,   X
               RECFM=FS,EXLST=JFCBLIST,EODAD=ENDVTOC
DCBVTOC2 DCB   DSORG=PS,MACRF=RP,DDNAME=VTOCDD,BLKSIZE=96,KEYLEN=44,   X
               RECFM=FS,EXLST=JFCBLIST
QNAME    DC    CL8'SYSVTOC '
RNAME    DS    CL6
FMTOIND  DC    X'00'
HEXZEROS DC    X'00'
CCWEXCP  CCW   X'1A',FILEADDR,X'70',5  READ HOME ADDRESS,(SUPPRESSED)
READCONT CCW   X'92',DCBVTOC+8,X'60',5 READ COUNT (S.I.L.)
         CCW   X'A9',HEXZEROS,X'60',1  DOES KEY START '00'
         CCW   X'08',READCONT,X'40',1  NO, READ NEXT COUNT
         CCW   X'03',0,0,1             YES, THEN STOP
IOBEXCP  DC    0F'0',X'40',A(DECB1),2F'0',A(CCWEXCP),A(DCBVTOC),4F'0'
         ORG   *-5                          KEEP THE FIRST FORMAT 0
FILEADDR DS    CL5                          ADDRESS IN THE IOB ABOVE
FILEADDO DS    CL5
SAVEAREA DS    18F
DSCBAREA DS    35F
DSCBAREB DS    35F
VCJFCB   DS    44F
WORKAREA DS    F
         LTORG
         END
