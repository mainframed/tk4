//TZ074RNM JOB (3103,A123),RNM,CLASS=W
//*      THIS JOBSTREAM IS SET UP TO EXECUTE ON ANY VS1 SYSTEM.
//*      THE JCL DOES NOT REQUIRE ANY SPECIFIC CATALOGUED
//*      PROCEDURE,THE PROGRAM DOES NOT USE NON STANDARD MACROS.
//*      THE USER WILL PROBABLY REQUIRE TO MAKE MINOR JCL
//*      MODIFICATIONS TO CONFORM TO INSTALLATION STANDARDS.
//*      THE PROGRAM WILL EXECUTE IN THE MINIMUM VS1 PARTITION OF 64K.
//ASM     EXEC PGM=ASMBLR,PARM='LINECNT=75,DECK'
//SYSPRINT  DD SYSOUT=A
//SYSLIB    DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1    DD DSN=&&SYSUT1,SPACE=(CYL,(2,1)),UNIT=SYSDA
//SYSUT2    DD DSN=&&SYSUT2,SPACE=(CYL,(2,1)),UNIT=SYSDA
//SYSUT3    DD DSN=&&SYSUT3,SPACE=(CYL,(2,1)),UNIT=SYSDA
//SYSPUNCH  DD DSN=&&SYSLIN,DISP=(,PASS),SPACE=(CYL,(1,1)),
//             DCB=(BLKSIZE=400,LRECL=80,RECFM=FBS),UNIT=SYSDA
//SYSIN     DD *
         TITLE 'SYSCTLG PRINT PROGRAM'
CATLIST  CSECT
*        CATLIST  - A SYSCTLG PRINT PROGRAM
*     THIS PROGRAM HAS BEEN FULLY TESTED ON 3330'S,BUT AS IT USES BSAM
*     IT SHOULD WORK OK ON OTHER DASD TYPES WITHOUT MODIFICATION.
*
*     THE PROGRAM WILL CATER FOR UPTO TWENTY LEVELS OF INDEX(THE
*     THEORETICAL MAXIMUM IS TWENTYTWO..EG A.B.C.D.E.F.....ETC)
*
*     TO EXECUTE THE PROGRAM, YOU NEED TWO DD CARDS AND OPTIONALLY
*     AN EXECUTION TIME PARAMETER.
*
*     THE DD CARD REQUIREMENTS ARE:-
*        CATLG DDNAME DEFINING THE SYSCTLG DATA SET
*        PRINT DDNAME DEFINING THE SYSOUT DATA SET (LRECL 133 RECFM FA)
*
*     THE OPTIONAL EXECUTION PARAMETERS ARE:-
*        IF NO PARAMETERS ARE SPECIFIED THE FULL SYSCTLG IS PRINTED.
*        IF A SELECTED INDEX IS REQUIRED,CODE AN 8 CHARACTER INDEX
*        IF THE INDEX IS LT 8 CHARACTERS IT MUST BE PADDED WITH BLANKS.
*        TO PRINT OUT ONLY DATA RELATING TO GDGS,CODE PARM=GDGS.
*
*     IT IS POSSIBLE TO PROVIDE FURTHER EDITING OF DATA BY MAKING
*     APPROPRIATE MODIFICATIONS TO THE PARM VALIDATION AND TO THE MAIN
*     PROCESSING ROUTINE - CATLB
*
*     EACH ENTRY TYPE IN THE CATALOG IS PRINTED WITH A DESCRIPTION:-
*        EACH LEVEL OF INDEX HAS THE DESCRIPTION   -  'INDEX LEVEL'
*        EACH CONNECTED INDEX HAS THE DESCRIPTION   - 'CVOL POINTER'
*        EACH GENERATION DATA GROUP IS DESCRIBED    -  'GDG'
*
*     IN ADDITION THE FOLLOWING INFORMATION IS PRINTED:-
*        FOR EACH GDG,THE OPTIONS SELECTED WHEN THE INDEX WAS BUILT
*        ARE ALSO PRINTED.
*        EMPTY INDEXES (INCLUDING GDGS) ARE PRINTED.
*        IF THE INDEX IS AN ALIAS,BOTH THE ALIAS AND THE TRUE NAME
*        ARE PRINTED.
*        IF THE DEVICE TYPE IS MAGNETIC TAPE,THE DATA SET SEQUENCE
*        NUMBER IS PRINTED.
*        THE DEVICE TYPE IS PRINTED AS A UNIT NAME,THIS IS ACHIEVED
*        BY HAVING A DEVICE TABLE IN THE PROGRAM WHICH MAY BE TUNED
*        TO INDIVIDUAL REQUIREMENTS.
*
*        ADDRESS ANY QUERIES TO:-
*              R. N. MAUD
*              TECHNICAL SUPPORT GROUP
*              GROUP MANAGEMENT SERVICES
*              ROWNTREE MACKINTOSH LTD
*              HAXBY ROAD
*              YORK
*              TEL: 0904-53071....EXT: 2722
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         STM   R14,R12,12(R13)          SAVE REGS
         B     80(R15)                  CONTINUE
         SPACE
         USING *,R13
         SPACE
*        SAVE AREA FOLLOWS
         SPACE
         DC    A(0,0,CATLIST+8,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0)
         SPACE
         ST    R13,12(R15)              STORE ADDR OF OLD SAVE AREA
         MVC   8(4,R13),16(R15)         MOVE ADDR OF NEW SAVE AREA
         ST    R1,32(R15)               SAVE REG 1
         LM    R13,R12,16(R15)          LOAD REGS 13 - 12
         SPACE 2
         BAL   R10,CATLA                INITIALISATION
         SPACE 2
         B     CATLB                    MAIN PROCESSOR
         SPACE 2
CATL999  L     R13,4(R13)               LOAD ADDR OF OLD SAVE AREA
         L     R14,12(R13)              LOAD REG 14
         LM    R2,R12,28(R13)           LOAD REGS 2 - 12
         XR    R15,R15                  SET RETURN CODE
         BR    R14                      FINISH
         EJECT
ADDR01   DC    XL3'000001'              ABSOLUTE TRACK ADDRESS
ADDR01A  DC    XL1'00'                  INDICATOR-MUST FOLL. ADDR01
ENTRY    DC    XL1'00'                  CATLG ENTRY TYPE
GDGINF   DS    CL5
HWORD    DC    H'0'                     HALFWORD ALIGNED WORK AREA
LINKSVEA DS    F                        LINKAGE REGISTER
LINKSVEB DS    F                        LINKAGE REGISTER
LINKSVEC DS    F                        LINKAGE REGISTER
LINKSVED DS    F                        LINKAGE REGISTER
LINKSVEE DS    F                        LINKAGE REGISTER
LINKSVEF DS    F                        LINKAGE REGISTER
LINKSVEG DS    F                        LINKAGE REGISTER
LINKSVEH DS    F                        LINKAGE REGISTER
CATJFCB  DS    0F
         DC    X'87'
         DC    AL3(VCB)
INDEXADD DS    24D                      ADDRESS STORE
WORK     DS    D                        WORK AREA
DEVICES  DC    XL4'30502009',CL5'3330 '
         DC    XL4'3050200B',CL5'3350 '
         DC    XL4'32008003',CL5'TAPE6' 6250 BPI
         DC    XL4'32108003',CL5'TAPE5' 1600/6250 BPI
DEVICEND DC    XL4'00000000',CL5'*****'
NODE     DS    CL8                      INDEX TO BE PRINTED
PRINTIND DC    XL1'00'                  REPORT TYPE INDICATOR
VCBK     DS    CL8                      KEY AREA MUST PREFIX VCB FIELD
VCB      DS    CL256                    VOLUME CONTROL BLOCK
VOLINDXK DS    CL8                      KEY AREA MUST PREFIX VOLINDX
VOLINDX  DS    CL256                    INPUT AREA-VOLUME INDEX
GDGK     DS    CL8                      GDG INDEX KEY
GDG      DS    CL256                    BLOCK OF A GDG INDEX
FFFFS    DC    XL4'FFFFFFFF'
ZEROES   DC    D'0'                     BINARY ZEROES
         READ  CATDECB,SF,CATDCB,VOLINDXK,'S',MF=L
PAGECNT  DC    PL2'0'
LINECNT  DC    PL2'78'
LINE     DS    CL133
HEAD     DC    CL133'1 YY.DDD                TIME        HRS           *
                SYSCTLG  -                                             *
                    PAGE'
HEAD1    DC    CL133'0--------- DATA SET OR INDEX NAME ----------- DEVI*
               CE-LABEL  ---------- VOLUMES OR ALIAS NAME -------------*
                 - ENTRY TYPE IF NOT D/S -'
DATAPFIX DS    0F                       THIS FIELD IS A PREFIX TO THE
         DS    CL3                      DATA FIELD TO STOP BACK
         DC    CL1'.'                   BLANKING BEYOND LOWEST QUAL.
DATA     DS    0F                       PRINT DATA AREA
         DC    CL200' '
         EJECT
CATDCB   DCB   DDNAME=CATLG,MACRF=(RCP),DSORG=PS,LRECL=256,            *
               BLKSIZE=256,RECFM=F,KEYLEN=8,EXLST=CATJFCB
         EJECT
PRINT    DCB   DDNAME=PRINT,DSORG=PS,MACRF=(PM)
         EJECT
CATLA    L     R1,0(R1)                 OBTAIN PARAMETER ADDRESS
         CLC   0(2,R1),=X'0000'         IS A PARM SPECIFIED
         BE    CATLA010                 BRANCH IF NOT
         SPACE 2
         CLC   2(4,R1),=C'GDGS'
         BNE   CATLA005
         MVI   CATLF007+1,X'F0'
         LA    R1,4(R1)
CATLA005 CLC   2(3,R1),=C'ALL'          FULL CATALOG LISTING
         BE    CATLA010                 YES
         MVC   NODE,2(R1)               STORE INDEX REQUIRED
         OI    PRINTIND,X'80'           INDICATE TYPE OF REPORT
         SPACE 2
CATLA010 MVC   INDEXADD(8),ZEROES       BUILD UP AN ADDRESS LIST OF
         LA    R3,23                    THE MAXIMUM POSSIBLE LEVELS
         LA    R4,INDEXST               OF AN INDEX QUALIFICATION
         LA    R5,INDEXADD+8
         TIME  DEC                      OBTAIN DATE AND TIME
         SRL   R0,12                    AND EDIT TIME INTO PRINT
         ST    R0,WORK                  LINE HEADING
         NI    WORK+3,X'F0'
         OI    WORK+3,X'0F'
         MVC   HEAD+29(6),=X'2120204B2020'
         ED    HEAD+28(7),WORK+1
         ST    R1,WORK                  YEAR AND DAY
         MVC   HEAD+11(6),=X'21204B202020'
         ED    HEAD+10(7),WORK+1        EDIT DAY NUMBER TO PRINT
CATLA011 ST    R4,0(R5)
         BCT   R3,CATLA015
         B     CATLA020
CATLA015 LA    R5,8(R5)
         LA    R4,264(R4)
         B     CATLA011
         EJECT
CATLA020 OPEN  (CATDCB,,PRINT,(OUTPUT)) OPEN THE FILES
         POINT CATDCB,ADDR01            POSITION ACCESS ARM
         READ  CATDECB,SF,MF=E          FIRST BLOCK OF VOLUME INDEX
         CHECK CATDECB                  CHECK FOR SUCCESSFUL READ
         LA    R3,VOLINDX+24            R3 > DATA PORTION OF BLOCK
         RDJFCB (CATDCB)
         MVC   HEAD+63(6),VCB+118
         SPACE 2
         BR    R10
         EJECT
CATLB    NOP   CATLB                    MAIN PROCESSOR ROUTINE
CATLB020 CLI   0(R3),X'FF'              IS IT A LINK ENTRY
         BE    CATLB050                 YES - GET NEXT BLOCK OF VOL IND
         SPACE 2
         TM    PRINTIND,X'80'           SINGLE INDEX REPORT
         BZ    CATLB040                 NO
         SPACE 2
         CLC   NODE,0(R3)               IS THIS THE REQD INDEX
         BH    CATLB800
         BL    CATL999
         SPACE 2
CATLB040 MVI   DATA,C' '                BLANK OUT NAME
         MVC   DATA+1(43),DATA
         CLI   11(R3),X'00'             INDEX POINTER
         BE    CATLB100
         CLI   11(R3),X'01'             VOL CONTROL BLOCK POINTER
         BE    CATLB200
         CLI   11(R3),X'02'             GDG ENTRY
         BE    CATLB700
         CLI   11(R3),X'04'             ALIAS ENTRY
         BE    CATLB500
         CLI   11(R3),X'05'             NEW CVOL POINTER
         BE    CATLB400
         B     CATLB600                 DATA SET ENTRY
         SPACE 2
CATLB050 CLC   8(3,R3),ZEROES           END OF VOLUME INDEX
         BE    CATL999                  YES - END OF JOB
         SPACE 2
         MVC   ADDR01,8(R3)             ADDRESS OF NEXT BLOCK
         POINT CATDCB,ADDR01            POSITION ACCESS ARM
         READ  CATDECB,SF,,VOLINDXK,MF=E  READ A BLOCK OF VOLUME INDEX
         CHECK CATDECB                  CHECK FOR SUCCESSFUL READ
         SPACE 2
         LA    R3,VOLINDX+2             ADDRESS OF DATA PORTION
         B     CATLB020                 IDENTIFY NEXT ENTRY
         EJECT
CATLB100 MVC   ADDR01,08(R3)            ADDRESS OF FIRST BLOCK
         BAL   R10,CATLF                OBTAIN A FULL INDEX
         B     CATLB800
         SPACE 2
CATLB200 MVI   ENTRY,X'02'              02 = A SIMPLE DATA SET ENTRY
         MVC   DATA(8),0(R3)            DATA SET NAME
         LA    R2,DATA+44               R2 > DATA FIELD
         MVC   ADDR01,8(R3)             ADDRESS OF VOLUME CONTROL BLOCK
         BAL   R10,CATLE                OBTAIN VOLS FROM CONTROL BLOCK
         B     CATLB900
         SPACE 2
CATLB400 MVI   ENTRY,X'03'              04 = NEW CVOL POINTER
         MVC   DATA(8),0(R3)            INDEX NAME
         MVC   DATA+44(6),16(R3)        VOLUME POINTER
         MVC   DATA+52(4),12(R3)        DEVICE TYPE
         B     CATLB900
         SPACE 2
CATLB500 MVI   ENTRY,X'05'              05 = ALIAS ENTRY
         MVC   DATA(8),0(R3)            ALIAS NAME
         MVC   DATA+44(8),12(R3)        TRUE NAME
         B     CATLB900
         SPACE 2
CATLB600 MVI   ENTRY,X'02'              02 = SIMPLE DATA SET ENTRY
         MVC   DATA(8),0(R3)            DATA SET NAME
         LA    R2,DATA+44               R2 > DATA FIELD
         LA    R8,12(R3)                ADDRESS OF INPUT DATA
         BAL   R10,CATLG                MOVE VOLUME DATA
         B     CATLB900
         SPACE 2
CATLB700 MVI   ENTRY,X'01'              GDG ENTRY
         MVC   ADDR01,8(R3)             ADDRESS OF GDG INDEX
         LR    R7,R3
         LA    R5,DATA
         BAL   R10,CATLD                OBTAIN GDG DATA
         B     CATLB800
         SPACE 2
CATLB800 LA    R1,12                    CALCULATE THE OFFSET OF
         MVI   HWORD,X'00'
         MVC   HWORD+1(1),11(R3)        THE NEXT INDEX ENTRY AND
         LH    R5,HWORD                 STORE IN REG 5
         SLL   R5,1
         AR    R5,R1
         LA    R3,0(R5,R3)              ADDRESS NEXT VOL INDEX ENTRY
         B     CATLB020                 IDENTIFY NEXT ENTRY
         SPACE 2
CATLB900 BAL   R10,CATLC                PRINT ROUTINE
         B     CATLB800
         EJECT
CATLC    ST    R10,LINKSVEC             SAVE LINKAGE REGISTER
         CP    LINECNT,=P'75'           IS PAGE FULL
         BM    CATLC020                 NO
         SPACE 2
         ZAP   LINECNT,=P'0'
         AP    PAGECNT,=P'1'
         MVC   HEAD+117(3),=X'202020'
         ED    HEAD+116(4),PAGECNT
         PUT   PRINT,HEAD               PRINT HEADINGS
         PUT   PRINT,HEAD1
         MVI   LINE,C'0'                SET UP FIRST PRINT LINE AFTER
         MVI   LINE+1,C' '              HEADINGS ARE PRINTED
         MVC   LINE+2(131),LINE+1
         SPACE 2
CATLC020 CLI   ENTRY,X'01'              GDG ENTRY
         BNE   CATLC040                 NO
         MVC   LINE+108(6),=C'*  GDG'
         MVC   LINE+46(11),=C'ENTRIES    '
         TM    GDGINF+4,X'02'           DELETE OPTION SPECIFIED
         BZ    CATLC025                 NO
         MVC   LINE+57(7),=C'/DELETE'
CATLC025 MVC   LINE+54(3),GDGINF+1
         SR    R7,R7
         B     CATLC120
         SPACE 2
CATLC040 CLI   ENTRY,X'03'              CVOL POINTER
         BNE   CATLC060                 NO
         MVC   LINE+108(15),=C'*  CVOL POINTER'
         MVC   LINE+60(6),DATA+44       VOLUME POINTED TO
         L     R8,DATA+52               DEVICE TYE
         BAL   R10,CATLC800             OBTAIN DEVICE DEC
*        R8 POINTS TO DEVICE DESCRIPTIOM
         MVC   LINE+46(4),4(R8)
         SR    R7,R7
         B     CATLC120
         SPACE 2
CATLC060 CLI   ENTRY,X'05'              ALIAS
         BNE   CATLC070                 NO
         MVC   LINE+108(8),=C'*  ALIAS'
         MVC   LINE+46(12),=C'TRUE NAME IS'
         MVC   LINE+60(44),DATA+44      TRUE NAME
         SR    R7,R7
         B     CATLC120
CATLC070 CLI   ENTRY,X'00'              INDEX ENTRY
         BNE   CATLC080
         MVC   LINE+108(14),=C'*  INDEX LEVEL'
         SR    R7,R7
         B     CATLC120
         EJECT
CATLC080 NOP   CATLC080                 DATA SET
         L     R8,DATA+44               DEVICE TYPE
         BAL   R10,CATLC800
*        R8 POINTS TO DEVICE DESCRIPTION
         MVC   LINE+46(5),4(R8)         DEVICE
         LA    R8,DATA+54               ADDRESS OF VOLUMES
         LH    R7,DATA+52               VOLUME COUNT
         CLC   DATA+48(2),=X'0000'      LABEL
         BE    CATLC081                 NO
         MVC   LINE+54(3),DATA+48       LABEL TO PRINT LINE
CATLC081 LA    R11,6                    MAXIMUM VOLUMES IN A VOLUME
         LA    R1,LINE+60               ADDRESS IN PRINT LINE
CATLC082 MVC   0(6,R1),0(R8)            MOVE A VOLUME
         BCT   R11,CATLC083             BRANCH TILL A LINE IS FULL
         LA    R8,6(R8)
         BCTR  R7,0
         B     CATLC085                 PRINT A LINE
CATLC083 BCT   R7,CATLC084              BRANCH TILL ALL VOLS ARE USED
         B     CATLC085                 PRINT A LINE
CATLC084 LA    R8,6(R8)                 ADDRESS NEXT VOLUMES FIELD
         LA    R1,8(R1)
         B     CATLC082
CATLC085 CLI   ENTRY,X'02'              CONTINUATION OF VOLUMES
         BNE   CATLC140                 YES
         MVI   ENTRY,X'08'              INDICATE POSS. CONT.
CATLC120 MVC   LINE+1(44),DATA          NAME
CATLC140 PUT   PRINT,LINE
         MVI   LINE,C' '
         MVC   LINE+1(132),LINE
         LTR   R7,R7                    ALL VOLUMES GONE
         BNZ   CATLC081                 NO
         MVI   DATA+44,C' '             CLEAR ALL DATA BUT DSNAME
         MVC   DATA+45(154),DATA+44
         AP    LINECNT,=P'1'
CATLC160 L     R10,LINKSVEC             RESTORE LINKAGE REGISTER
         BR    R10
         SPACE 2
CATLC800 LA    R7,DEVICES               ADDRESS OF DEVICE TABLE
         LA    R1,(DEVICEND+9-DEVICES)/9  NO OF DEVICES IN TABLE
CATLC810 C     R8,0(R7)                 DO DEVICES MATCH
         BE    CATLC830                 YES
         BCT   R1,CATLC815              NO MORE DEVICES
         B     CATLC830
CATLC815 LA    R7,9(R7)                 ADDRESS NEXT DEVICE
         B     CATLC810
CATLC830 LR    R8,R7                    ADDRESS OF DEVICE DESC
CATLC840 BR    R10                      RETURN
         EJECT
CATLD    ST    R10,LINKSVED             SAVE LINKAGE REGISTER
         MVI   HWORD,X'00'              CONVERT MAXIMUM ENTRIES
         MVC   HWORD+1(1),13(R7)        FROM BINARY TO CHARACTER
         LH    R1,HWORD
         CVD   R1,WORK
         MVC   GDGINF(4),=X'40202020'
         ED    GDGINF(4),WORK+6         MAX ENTRIES AS 3 BYTE CHAR
         MVC   GDGINF+4(1),12(R7)       TYPE INDICATOR
         MVC   0(8,R5),0(R7)
         BAL   R10,CATLC                PRINT ROUTINE
         MVI   ENTRY,X'02'              DATA SET TYPE
CATLD010 LA    R5,1(R5)                 ADVANCE ONE QUALIFIER
         CLI   0(R5),C' '
         BNE   CATLD010
         MVI   0(R5),C'.'
         LA    R5,1(R5)
         BAL   R10,CATLH                PROCESS GDG INDEX
         BCTR  R5,0                     DROP BACK ONE QUALIFIER
         MVI   0(R5),C' '
         MVC   1(8,R5),0(R5)
CATLD020 BCTR  R5,0
         CLI   0(R5),C'.'
         BE    CATLD030
         MVI   0(R5),C' '
         B     CATLD020
CATLD030 LA    R5,1(R5)
         L     R10,LINKSVED             RESTORE LINKAGE REGISTER
         BR    R10
         EJECT
CATLE    ST    R10,LINKSVEE             SAVE LINKAGE REGISTER
         POINT CATDCB,ADDR01            POSITION ACCESS ARM
         READ  CATDECB,SF,,VCBK,MF=E    READ A VOLUME CONTROL BLOCK
         CHECK CATDECB                  CHECK FOR SUCCESSFUL READ
         LA    R8,VCB                   ADDRESS OF INPUT DATA
         BAL   R10,CATLG                MOVE VOLUME DATA
         L     R10,LINKSVEE             RESTORE LINKAGE REGISTER
         BR    R10
         EJECT
CATLF    ST    R10,LINKSVEF             SAVE LINKAGE REGISTER
         LA    R6,INDEXADD+8            ADDRESS OF ADDRESS LIST
         LA    R5,DATA                  PRINT DATA AREA
         MVC   0(8,R5),0(R3)            HIGHEST LEVEL INDEX
CATLF001 POINT CATDCB,ADDR01            FIRST BLOCK OF AN INDEX
CATLF002 LA    R5,1(R5)                 ADVANCE ONE QUALIFIER
         CLI   0(R5),C' '
         BNE   CATLF002
         MVI   ENTRY,X'00'              INDEX ENTRY
         BAL   R10,CATLC                PRINT THE INDEX
         MVI   0(R5),C'.'
         LA    R5,1(R5)
         L     R4,0(R6)                 LOAD AREA ADDRESS
         READ  CATDECB,SF,,(R4),MF=E    OBTAIN FIRST BLOCK OF INDEX
         CHECK CATDECB
         LA    R4,28(R4)                ADDRESS FIRST ENTRY
CATLF005 CLI   0(R4),X'FF'              IS IT A LINK ENTRY
         BE    CATLF010                 YES - GET NEXT BLOCK
         CLI   11(R4),X'00'             INDEX POINTER
         BE    CATLF030
         CLI   11(R4),X'02'             GDG ENTRY
         BE    CATLF050
CATLF007 NOP   CATLF080
         CLI   11(R4),X'01'             VOL CONTROL BLOCK POINTER
         BE    CATLF040
         B     CATLF060                 DATA SET ENTRY
CATLF010 CLC   8(3,R4),ZEROES           END OF THIS INDEX LEVEL
         BE    CATLF020                 YES
         MVC   ADDR01,8(R4)             ADDRESS NEXT BLOCK
         L     R4,0(R6)                 LOAD AREA ADDRESS
         POINT CATDCB,ADDR01
         READ  CATDECB,SF,,(R4),MF=E    READ NEXT BLOCK OF INDEX
         CHECK CATDECB
         LA    R4,10(R4)                ADDRESS FIRST ENTRY
         B     CATLF005
CATLF020 LA    R4,8
         SR    R6,R4                    BACK ONE UP CHAIN
         CLC   0(4,R6),ZEROES           TOP OF CHAIN
         BE    CATLF999                 YES
         BCTR  R5,0                     BLANK OUT LOWEST LEVEL NAME
         MVI   0(R5),C' '
         MVC   1(8,R5),0(R5)
CATLF025 BCTR  R5,0                     BLANK OUT LAST QUALIFIER
         CLI   0(R5),C'.'
         BE    CATLF026
         MVI   0(R5),C' '
         B     CATLF025
CATLF026 LA    R5,1(R5)
         L     R4,4(R6)                 > NEXT ENTRY IN RESTORED BLOCK
         B     CATLF005
CATLF030 MVC   0(8,R5),0(R4)
         MVC   ADDR01,8(R4)
         LA    R4,12(R4)                ADDRESS NEXT ENTRY
         ST    R4,4(R6)                 SAVE ADDRESS
         LA    R6,8(R6)
         B     CATLF001                 ADDRESS NEXT DATA AREA
CATLF040 MVI   ENTRY,X'02'
         MVC   0(8,R5),0(R4)            SIMPLE DATA SET NAME
         MVC   ADDR01,8(R4)
         BAL   R10,CATLE
         B     CATLF070
CATLF050 MVI   ENTRY,X'01'
         MVC   ADDR01,8(R4)
         LR    R7,R4
         BAL   R10,CATLD
         B     CATLF080
CATLF060 MVI   ENTRY,X'02'
         MVC   0(8,R5),0(R4)
         LA    R2,DATA+44
         LA    R8,12(R4)
         BAL   R10,CATLG
CATLF070 BAL   R10,CATLC                PRINT ROUTINE
CATLF080 LA    R1,12                    ADDRESS NEXT ENTRY
         MVI   HWORD,X'00'
         MVC   HWORD+1(1),11(R4)
         LH    R8,HWORD
         SLL   R8,1
         AR    R1,R8
         LA    R4,0(R1,R4)
         B     CATLF005                 PROCESS NEXT ENTRY
CATLF999 L     R10,LINKSVEF             RESTORE LINKAGE REGISTER
         BR    R10
         EJECT
CATLG    ST    R10,LINKSVEG             SAVE LINKAGE REGISTER
         MVC   0(4,R2),2(R8)            DEVICE TYPE
         MVC   8(2,R2),0(R8)            VOLUME COUNT
         MVC   4(2,R2),=X'0000'         LABEL FIELD
         CLC   12(2,R8),=X'0001'        ANY LABEL PRESENT
         BNH   CATLC005                 NO
         LH    R7,12(R8)
         CVD   R7,WORK
         MVC   WORK(4),=X'40202020'
         ED    WORK(4),WORK+6
         MVC   4(3,R2),WORK+1
CATLC005 LH    R7,8(R2)                 VOLUME COUNT
         LA    R1,10(R2)                VOLUME AREA
         LA    R8,2(R8)                 ADDRESS INPUT VOLUMES
CATLG010 MVC   0(6,R1),4(R8)            VOLUME
         LA    R8,12(R8)                ADDRESS NEXT VOLUME
         LA    R1,6(R1)
         BCT   R7,CATLG010              DROP THROUGH WHEN ZERO VOLUMES
         L     R10,LINKSVEG             RESTORE LINKAGE REGISTER
         BR    R10
         EJECT
CATLH    ST    R10,LINKSVEH             SAVE LINKAGE
         LA    R9,GDG+18                IGNORE INDEX CONTROL ENTRY
CATLH010 POINT CATDCB,ADDR01            POINT TO FIRST BLOCK OF GDG
         READ  CATDECB,SF,,GDGK,MF=E    READ A GDG INDEX BLOCK
         CHECK CATDECB
         LA    R9,2(R9)                 ADDRESS DATA AREA
CATLH020 CLI   0(R9),X'FF'              END OF A BLOCK
         BNE   CATLH030                 NO
         CLC   8(3,R9),ZEROES           END OF INDEX
         BE    CATLH999                 YES
         MVC   ADDR01,8(R9)             ADDRESS OF NEXT BLOCK
         LA    R9,GDG
         B     CATLH010
CATLH030 MVI   ENTRY,X'02'
         XC    1(4,R9),FFFFS            COMPLEMENT GEN. NO.
         MVC   0(8,R5),0(R9)            GENERATION NUMBER
         LA    R2,DATA+44               R2 > DATA FIELD
         CLI   11(R9),X'01'             VOL CONTROL BLOCK POINTER
         BE    CATLH050                 YES
         LA    R8,12(R9)                R4 > INPUT DATA
         BAL   R10,CATLG                MOVE VOLUME DATA
         B     CATLH070
CATLH050 MVC   ADDR01,8(R9)             ADDRESS OF VOLUME CONTROL BLOCK
         BAL   R10,CATLE                OBTAIN VOLUME DATA
CATLH070 BAL   R10,CATLC                PRINT
         LA    R1,12                    CALCULATE OFFSET OF NEXT
         MVI   HWORD,X'00'              ENTRY AND ADDRESS
         MVC   HWORD+1(1),11(R9)        NEXT ENTRY
         LH    R8,HWORD
         SLL   R8,1
         AR    R8,R1
         LA    R9,0(R8,R9)
         B     CATLH020
CATLH999 L     R10,LINKSVEH
ENDPROG  BR    R10
         LTORG
INDEXST  DS    21CL264                  INDEX STORE
         END   CATLIST
//LINK    EXEC PGM=IEWL,PARM='LET,LIST,XREF',COND=(8,LT,ASM)
//SYSPRINT  DD SYSOUT=A
//SYSLIN    DD DSN=&&SYSLIN,DISP=(OLD,DELETE)
//SYSLIB    DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1    DD DSN=&&SYSUT1,SPACE=(CYL,(5,2)),UNIT=SYSDA
//SYSLMOD   DD DSN=&&GOSET(GO),DISP=(,PASS),
//             SPACE=(CYL,(1,1,1)),UNIT=SYSDA
//GO      EXEC PGM=GO
//STEPLIB   DD DSN=&&GOSET,DISP=(OLD,PASS)
//SYSPRINT  DD SYSOUT=A
//CATLG     DD DSN=SYSCTLG,DISP=OLD,VOL=REF=SYS1.SVCLIB
//PRINT     DD  SYSOUT=A,DCB=(BLKSIZE=133,LRECL=133,RECFM=FA)
//
