         TITLE 'INFORM USERS OF WHAT OPTIONS ARE IN EFFECT'
ZSPOOL5  INIT
         EXTRN PRINTER2,DISK,COMMON,TAPE
         USING AREA,R11
         L     R11,=A(COMMON)
         USING IHADCB,R10
         EJECT
D1       L     R3,=A(PRINTER2) PICK UP ADDRESS OF DCB FOR GOOD CARDS
         LA    R8,WAREA0+21
         LA    R7,0 SET COUNT TO ZERO
         LA    R5,JOBNAME
DA       CLC   0(2,R5),=C'.T' IS THIS THE END
         BE    DB
         MVC   0(1,R8),0(R5)
         LA    R8,1(R8)
         LA    R7,1(R7)
         LA    R5,1(R5)
         B     DA
DB       LA    R7,25(R7)
         STH   R7,WLEN0
         CNOP  0,4
         BAL   R1,WGO0
WLEN0    DC    H'0'
         DC    H'0'
WAREA0   DC    C'MOUNT PAPER TAPE FOR           '
WGO0     SVC   35
         SPACE 1
         LA    R8,JOBNAME LOAD ADDRESS OS START OF JOB NAME
         SR    R7,R7
D3       CLI   0(R8),C'.' HAVE WE REACHED THE END
         BE    D2 YES
         LA    R7,1(R7) INCREMENT COUNT BY 1
         LA    R8,1(R8) INCREMENT TO NEXT POSITIOB
         B     D3
         SPACE 1
D2       ST    R7,DBLWD
         LA    R8,JOBNAME
         CALL  ZSEPARAT,((8),DBLWD,(3))
         EJECT
         MVI   PRNTAREA,C' '       ROLL A BLANK
         MVC   PRNTAREA+1(132),PRNTAREA   DOWN PRINT AREA
         MVC   PRNTAREA+51(14),=C'TODAYS DATE IS'
         MVC   PRNTAREA+77(12),=C'RUN TIMED AT'
         CALL  ZTODAY,(DATIME),VL  GET TODAYS DATE
         MVC   PRNTAREA+67(8),DATIME
         CALL  ZTIME,(DATIME),VL    GET CURRENT TIME
         MVC   PRNTAREA+91(8),DATIME   CURRENT TIME IN PRINT LINE
         MVI   PRNTCNTL,C'-'
         PUT   (3),PRNTAREA   PRINT THE HEADING LINE (IDENTIFICATION)
         MVI   PRNTAREA,C' ' ROLL A BLANK
         MVC   PRNTAREA+1(132),PRNTAREA   DOWN THE PRINT AREA
         MVC   PRNTAREA+1(80),CARDAREA
         MVI   PRNTCNTL,C'-'
         PUT   (3),PRNTAREA PRINT THE INPUT CARD
         LA    R8,WAREA1+13 PICK UP ADDRES0 OF DESCRIPTION PART OF AREA
         LA    R7,0 SET COUNT TO ZERO
         TM    EORSWTCH,EOR1 IS SWITCH U ON
         BNO   D4 NO
         MVC   0(2,R8),=C'1,' YES
         LA    R7,2(R7) INCREMEN0 COUNT
         LA    R8,2(R8) INCREMENT TO NEXT POSITION
D4       TM    EORSWTCH,EOR2 IS SWITCH 2 ON
         BNO   D5 NO
         MVC   0(2,R8),=C'2,' YES
         LA    R7,2(R7) INCREMEN0 COUNT
         LA    R8,2(R8) INCREMENT TO NEXT POSITION
D5       TM    EORSWTCH,EOR3 IS SWITCH 3 ON
         BNO   D6 NO
         MVC   0(2,R8),=C'3,' YES
         LA    R7,2(R7) INCREMEN0 COUNT
         LA    R8,2(R8) INCREMENT TO NEXT POSITION
D6       TM    EORSWTCH,EOR4 IS SWITCH 4 ON
         BNO   D7 NO
         MVC   0(2,R8),=C'4,' YES
         LA    R7,2(R7) INCREMEN0 COUNT
         LA    R8,2(R8) INCREMENT TO NEXT POSITION
D7       TM    EORSWTCH,EOR5 IS SWITCH 5 ON
         BNO   D8 NO
         MVC   0(2,R8),=C'5,' YES
         LA    R7,2(R7) INCREMEN0 COUNT
         LA    R8,2(R8) INCREMENT TO NEXT POSITION
D8       TM    EORSWTCH,EOR6 IS SWITCH 6 ON
         BNO   D9 NO
         MVC   0(2,R8),=C'6,' YES
         LA    R7,2(R7) INCREMEN0 COUNT
         LA    R8,2(R8) INCREMENT TO NEXT POSITION
D9       TM    EORSWTCH,EOR7 IS SWITCH 7 ON
         BNO   D10 NO
         MVC   0(2,R8),=C'7,' YES
         LA    R7,2(R7) INCREMEN0 COUNT
         LA    R8,2(R8) INCREMENT TO NEXT POSITION
D10      TM    EORSWTCH,EOR8 IS SWITCH 8 ON
         BNO   D11 NO
         MVC   0(2,R8),=C'8,' YES
         LA    R7,2(R7) INCREMEN0 COUNT
         LA    R8,2(R8) INCREMENT TO NEXT POSITION
         SPACE 1
D11      LTR   R7,R7 ARE THERE ANY SWITCHES TO BE SET
         BP    D12 YES
         MVC   0(4,R8),=C'NONE'
         LA    R7,5(R7) INCREMENT COUNT
         B     D13
         SPACE 1
D12      BCTR  R8,R0 DECREASE R8 BY 1
         MVI   0(R8),C' ' BLANK OUT THE LAST ','
D13      LA    R7,16(R7) ADD ON THE FIXED PART
         STH   R7,WLEN1 STORE LENGTH OF OUTPUT
         CNOP  0,4
         BAL   R1,WGO1
WLEN1    DC    H'0'
         DC    H'0'
WAREA1   DC    C'EOR SWITCHES                 '
WGO1     SVC   35
         MVI   PRNTAREA,C' '
         MVC   PRNTAREA+1(132),PRNTAREA
         MVC   PRNTAREA+1(29),WAREA1 MOVE SWITCHES TP PRINTER OUTPUT
         MVI   PRNTCNTL,C'0'
         PUT   (3),PRNTAREA
         EJECT
         LA    R8,WAREA2
         LA    R7,4
         MVC   0(9,R8),=C'PARITY - '
         TM    PARSWTCH,PARODD IS IT ODD PARITY
         BO    D14 YES
         TM    PARSWTCH,PAREVEN IS IT EVEN PARITY
         BO    D15 YES
         MVC   9(4,R8),=C'NONE' NO PARITY
         LA    R7,13(R7) INCREMENT COUNT
         LA    R8,13(R8) INCREMENT TO NEXT POSITION
         B     D16
D14      MVC   9(3,R8),=C'ODD'
         LA    R7,12(R7)
         LA    R8,12(R8)
         B     D16
D15      MVC   9(4,R8),=C'EVEN'
         LA    R7,13(R7)
         LA    R8,13(R8)
         SPACE   3
D16      MVC   0(12,R8),=C'   DELETE - '
         TM    DELSWTCH,DELYES
         BO    D17
         MVC   12(3,R8),=C'OFF'
         LA    R7,15(R7)
         LA    R8,15(R8)
         B     D18
D17      MVC   12(2,R8),=C'ON'
         LA    R7,14(R7)
         LA    R8,14(R8)
         SPACE 3
D18      MVC   0(16,R8),=C'   TRACK/SUPP - '
         TM    TSSWTCH,TSYES
         BO    D19
         MVC   16(3,R8),=C'OFF'
         LA    R7,19(R7)
         LA    R8,19(R8)
         B     D20
D19      MVC   16(2,R8),=C'ON'
         LA    R7,18(R7)
         LA    R8,18(R8)
         SPACE 3
D20      STH   R7,WLEN2
         CNOP  0,4
         BAL   R1,WGO2
WLEN2    DC    H'0'
         DC    H'0'
WAREA2   DC    47C' '
WGO2     SVC   35
         MVC   PRNTAREA+1(47),WAREA2 MOVE DCB INFORMATION TO PRINT AREA
         PUT   (3),PRNTAREA PRINT IT
         EJECT
         MVI   PRNTAREA+1,C' '
         MVC   PRNTAREA+2(131),PRNTAREA+1
         MVC   PRNTAREA+1(7),=C'CODE = '
         CLI   CODE,C' ' DID THE USER PASS A CODE
         BE    D21 NO
         MVC   PRNTAREA+8(1),CODE
         B     D22
D21      MVC   PRNTAREA+8(8),TRANMOD
         SPACE 3
D22      MVC   PRNTAREA+20(7),=C'RECFM ='
         L     R10,=A(DISK) LOAD ADDRESS OF DISK DCB
         TM    DCBRECFM,RECFMFB IS RECFM FIXED BLOCKED
         BO    D24 YES
         TM    DCBRECFM,RECFMF IS RECFM FIXED
         BO    D23 YES
         MVI   PRNTAREA+28,C'U'
         B     D25
D23      MVI   PRNTAREA+28,C'F'
         B     D25
D24      MVC   PRNTAREA+28(2),=C'FB'
         SPACE 3
D25      MVC   PRNTAREA+43(6),=X'402020202020' MOVE IN PATTERN
         LH    R7,DCBBLKSI PICK UP BLOCK SIZE
         CVD   R7,DBLWD
         ED    PRNTAREA+43(6),DBLWD+5 EDIT IN BLOCK SIZE
P        MVC   PRNTAREA+35(9),=C'BLKSIZE ='
         SPACE 3
         CLI   PRNTAREA+28,C'U' IS THE FILE UNDEFINED
         BE    D26 YES
         MVC   PRNTAREA+61(6),=X'402020202020' MOVE IN PATTERN
         LH    R7,DCBLRECL PICK UP LRECL
         CVD   R7,DBLWD
         ED    PRNTAREA+61(6),DBLWD+5
         MVC   PRNTAREA+55(7),=C'LRECL ='
         SPACE 3
D26      L     R10,=A(TAPE)
         TM    DCBEROPT,X'20'           IS EROPT = ABE
         BNO   D28                      NO
         MVC   PRNTAREA+80(3),=C'ABE'   YES
         B     D30
D28      TM    DCBEROPT,X'40'           IS EROPT = SKP
         BNO   D29                      NO
         MVC   PRNTAREA+80(3),=C'SKP'   YES
         B     D30
D29      MVC   PRNTAREA+80(3),=C'ACC'   MUST BE EROPT = ACC
D30      MVC   PRNTAREA+72(8),=C'EROPT = '
         SPACE 3
         PUT   (3),PRNTAREA
DC       MVC   WECB,=F'0' ZEROISE ECB
         MVC   WREPLY,=C'  ' BLANK OUT REPLY AREA
 WTOR  'REPLY ''GO'' WHEN READY OR ''END'' FOR NEXT CARD',WREPLY,3,WECB
         WAIT  ECB=WECB
         EJECT
         CLC   WREPLY,=C'END'
         BE    D27
         CLC   WREPLY,=X'859584' HAS THE OPERATOR REPLIED IN THE L/C
         BE    D27 YES
         LA    R15,0 SET A CONTINUE RETURN CODE
         B     END
D27      LA    R15,4 SET AN END RETURN CODE
END      EXIT
         EJECT
WREPLY   DS    CL3
WECB     DS    F
DBLWD    DS    D
DATIME   DS    CL8
         LTORG
         EJECT
*              COMMON AREA
AREA     DSECT
PRNTAREA EQU   *
PRNTCNTL DS    CL1
         DS    CL132
SWITCH1  DS    XL1
SWITCH2  DS    XL1
JOBNAME  DS    CL44
DISPSWT  DS    XL1
BLDLIST  DS    0F
         DC    H'1'
         DC    H'58'
TRANMOD  DS    CL8
         DS    XL50
EPLOC    DS    F
BLKSIZE  DS    H
EORSWTCH DS    CL1
PARSWTCH DS    CL1
DELSWTCH DS    CL1
TSSWTCH  DS    CL1
CODE     DS    CL1
CARDAREA DS    CL80
         EJECT
         DCBD  DSORG=QS,DEVD=DA
         EJECT
EOR1     EQU   X'80'
EOR2     EQU   X'40'
EOR3     EQU   X'20'
EOR4     EQU   X'10'
EOR5     EQU   X'08'
EOR6     EQU   X'04'
EOR7     EQU   X'02'
EOR8     EQU   X'01'
PARODD   EQU   X'80'
PAREVEN  EQU   X'40'
DELYES   EQU   X'80'
TSYES    EQU   X'80'
RECFMU   EQU   X'C0' UNDEFINED
RECFMF   EQU   X'88' FIXED1STANDARD
RECFMFB  EQU   X'98' FIXED/BLOCKED/STANDARD
         END
