         TITLE 'SPOOL PAPER TAPE TO DISK - I/O MODULE'
         PRINT NOGEN
ZSPOOL3  INIT  (12,11)
         EXTRN DISK,TAPE,COMMON
         ENTRY BUFIN,BUFOUT,EOF
         USING AREA,R10
         L     R10,=A(COMMON)
         L     R9,=A(DISK)
         L     R8,=A(TAPE)
         USING IHADCB,R7
         EJECT
*              AT THIS POINT THE NUMBER OF I/O BUFFERS IS KNOWN. THE
*              BUFFER AREA HAS TO BE FORMATTED
         SPACE 2
         LR    R7,R9 PICK UP THE ADDRESS OF THE DISK DCB
         MVC   NOBUFF+1(1),DCBBUFNO
         MVC   BUFFLENG,DCBBUFL
         L     R0,BUILD
         BUILD BUFOUT,(0)
         SPACE 1
         LR    R7,R8 PICK UP THE ADDRESS OF THE TAPE DCB
         MVC   NOBUFF+1(1),DCBBUFNO
         MVC   BUFFLENG,DCBBUFL
         L     R0,BUILD
         BUILD BUFIN,(0)
         EJECT
         OPEN  (TAPE,(INPUT))
         OPEN  (DISK,(OUTPUT))
         SPACE 3
         LA    R6,INAREA LOAD ADDRESS OF WORKAREA
         CLI   TRANMOD,C' '   WAS A TRANSLATION MODULE LOADED
         BE    A5             BRANCH IF NO
         MVC   DCBLCTBL,EPLOC PUT TRANSLATION MODULE ADDR IN DCB
A5       EQU   *
         LR    R7,R9 PICK UP ADDRESS OF DISK DCB
         TM    DCBRECFM,RECFMU IS OUTPUT UNDEFINED
         BO    A3 YES - DO NOT BOTHER TO BLANK OUT INPUT AREA
         LR    R7,R8 PICK UP ADDRESS OF TAPE DCB
         LR    R1,R6 FIND ADDRESS OF INPUT AREA
         AH    R1,DCBBLKSI ADD ON DESIRED LENGTH
         LR    R2,R6 LOAD ADDRESS OF INPUT AREA
A2       CR    R1,R2 HAVE WE REACHED THE END
         BE    A3 YES
         MVI   0(R2),C' ' BLANK THIS POSITION
         LA    R2,1(R2) INCREMENT TO NEXT POSITION
         B     A2
         SPACE 3
A3       GET   (8),(6) READ A RECORD FROM PAPER TAPE
         TM    DISPSWT,DISPYES DOES THE USER WANT THIS RECORD PRINTED
         BNO   A6 NO
         CALL  ZSPOOL4 PRINT THIS RECORD
A6       LR    R7,R8 PICK UP ADDRESS OF TAPE DCB
         CLC   DCBLRECL,=H'0' IS THE RECORD LENGTH ZERO
         BE    A5 YES - DO NOT WRITE THIS RECORD
         LR    R7,R9 PICK UP ADDRESS OF DISK DCB
         TM    DCBRECFM,RECFMU IS OUTPUT UNDEFINED
         BNO   A1 NO - DO NOT UPDATE DISK DCB
         LR    R7,R8 PICK UP ADDRESS OF TAPE DCB
         LH    R2,DCBLRECL FIND LENGTH OF BLOCK READ
         LR    R7,R9 PICK UP ADDRESS OF DISK DCB
         STH   R2,DCBLRECL UPDATE DISK DCB WITH LENGTH OF RECORD TO BE -
                            WRITTEN
A1       PUT   (9),(6) PUT RECORD TO DISK
         B     A5
         EJECT
EOF      CLOSE TAPE
         TM    DISPSWT,DISPYES DOES THE USER WANT THE TAPE PRINTING OUT
         BNO   A7 NO
         SR    R6,R6 SIGNAL END OF TAPE
         CALL  ZSPOOL4
A7       CLOSE DISK
         CLI   TRANMOD,C' ' DID THE PROGRAM LOAD A MODULE
         BE    END  NO
         DELETE DE=TRANMOD
END      EXIT
         EJECT
         LTORG
RECFMU   EQU   X'C0'
DISPYES  EQU   X'80'
         EJECT
BUILD    DS    0F
NOBUFF   DC    X'0000'
BUFFLENG DC    X'0000'
         SPACE 3
*              PAPER TAPE BUFFER AREA
BUFIN    DS    CL8
         DS    CL1024
         SPACE 3
*              DISK OUTPUT AREA
BUFOUT   DS    CL8
         DS    CL1024
         SPACE 3
*              WORK AREA
INAREA   DS    CL1024
         EJECT
*              COMMON AREA
AREA     DSECT
         DS    CL179
DISPSWT  DS    XL1
         DS    0F
         DS    CL4
TRANMOD  DS    CL8
         DC    50X'00'
EPLOC    DS    F
         EJECT
         DCBD  DSORG=QS,DEVD=(DA,PT)
         END
