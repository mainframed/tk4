*      IDENTIFICATION DIVISION.
*      PROGRAM-ID. SSU32AC3.
SSU32AC3 ENTER
         ENTRY DATE1
         L     1,0(1)
         LH    2,0(1)
         LR    11,1
         LOAD  EP=DATE3
         ST    0,DATENT
         LTR   2,2
         BZ    NOPARM
         LA    1,2(0,11)
         CH    2,H11
         BNE   INVPARM
         CLI   5(1),C','
         BNE   INVPARM
         LA    3,5
TESTA    TM    0(1),X'F0'
         BNO   INVPARM
         LA    1,1(1)
         BCT   3,TESTA
SW1      NOP   GOTPARM
         LA    3,5
         LA    1,1(1)
         OI    SW1+1,X'F0'
         B     TESTA
GOTPARM  SH    1,H11
         MVC   DATE1(5),0(1)
         MVC   DATE1+5(5),6(1)
NOPARM   SMFGET REC=(4,5,6),DATES=DATE1,FIELDS=(19,8,CH,A,31,4,PD,A,27,X
               4,BI,A)
         OPEN  (PRTER,(OUTPUT))
         TM    PRTER+48,X'10'
         BO    PRTOK
ABEND    ABEND 1001
PRTOK    PUT   PRTER,JOBHED
         PUT   PRTER,JLINE
         CLC   DATE1(10),=C'0000099999'
         BNE   *+14
         MVC   PGHEAD+90(36),=CL36' '
         B     PRTHED
         PACK  WORKF(4),DATE1(5)
         L     15,DATENT
         CALL  (15),(WORKF,INDATE)
         PACK  WORKF(4),DATE1+5(5)
         L     15,DATENT
         CALL  (15),(WORKF,OUTDATE)
PRTHED   MVC   PGHEAD+9(5),=X'2020202020'
         ED    PGHEAD+8(6),PAGENO
         AP    PAGENO(3),=PL1'1'
         PUT   PRTER,PGHEAD
         PUT   PRTER,PLINE
         PUT   PRTER,PHEAD1
         PUT   PRTER,PHEAD2
         PUT   PRTER,ULINE
         NOPR  9
         OI    *-1,X'F0'
         MVC   LINENO,=F'10'
         MVI   PGHEAD,C'1'
         OPEN (SMFIN)
         LA    2,AREA
         USING SMFRCD4,2
READ     GET   SMFIN,(2)
SW2      NOP   REST
         OI    *-3,X'F0'
         MVC   CURJOB,SMF4JBN
         B     WHATREC
REST     CLC   CURJOB,SMF4JBN
         BNE   ENDREC
WHATREC  CLI   SMF4RTY,4
         BE    REC4
         CLI   SMF4RTY,5
         BE    REC5
         DROP  2
         USING SMFRCD6,2
         MVC   WORKF,SMF6NLR
         L     3,LINEST
         AL    3,WORKF
         ST    3,LINEST
         L     3,LINTOT
         AL    3,WORKF
         ST    3,LINTOT
         B     READ
         DROP  2
         USING SMFRCD4,2
REC4     LH    3,SMF4LENN
         LA    4,SMF4LENN+2
         LA    3,0(3)
         SH    3,=H'2'
         LTR   3,3
         BNP   READ
         USING SMF4DEVC,4
TESTB    CLI   SMF4DEVC,X'80'
         BE    TAPE
         CLI   SMF4DEVC,X'20'
         BE    DISK
REDUCE   SH    3,=H'8'
         LTR   3,3
         BZ    READ
         LA    4,8(4)
         B     TESTB
TAPE     L     5,TAPDEV
         LA    5,1(5)
         ST    5,TAPDEV
         L     5,TAPEX
         AL    5,SMF4EXCP
         ST    5,TAPEX
         L     5,TETOT
         AL    5,SMF4EXCP
         ST    5,TETOT
         B     REDUCE
DISK     L     5,DISKDEV
         LA    5,1(5)
         ST    5,DISKDEV
         L     5,DISKEX
         AL    5,SMF4EXCP
         ST    5,DISKEX
         L     5,DETOT
         AL    5,SMF4EXCP
         ST    5,DETOT
         B     REDUCE
         DROP  2
         USING SMFRCD5,2
         DROP  4
REC5     LA    4,SMF5RSD
         L     15,DATENT
         CALL  (15),((4),JDATE)
         MVC   JNAME,SMF5JBN
         SR    5,5
         IC    5,SMF5NST
         CVD   5,WORKD
         OI    WORKD+7,X'0F'
         UNPK  JSTEP(2),WORKD+6(2)
         CLI   JSTEP,C'0'
         BNE   *+8
         MVI   JSTEP,C' '
         ICM   5,B'1111',SMF5NCI
         CVD   5,WORKD
         OI    WORKD+7,X'0F'
         MVC   JCARDS,=X'4020202020202120'
         ED    JCARDS,WORKD+4
         L     4,CRDTOT
         AR    4,5
         ST    4,CRDTOT
         LA    4,JCLASS
         LA    5,SMF5SMCI
         LA    6,TAB1
         LA    10,5
         LR    9,6
GOTEST   SR    7,7
         IC    7,=X'80'
TESTIT   TM    0(5),X'FF'
         BZ    NXTBYT
         EX    7,TEST5
         BZ    SHIFT
         MVC   0(1,4),0(6)
         LR    8,7
         AL    8,=F'-1'
         EX    8,AND5
         LA    4,1(4)
SHIFT    LA    6,1(6)
         SRL   7,1
         B     TESTIT
TEST5    TM    0(5),X'00'
AND5     NI    0(5),X'00'
NXTBYT   LA    5,1(5)
         LA    6,8(9)
         LA    9,8(9)
         BCT   10,GOTEST
         B     READ
ENDREC   NI    SW2+1,X'0F'
         MVC   PRTAR+3(8),JDATE
         MVC   PRTAR+20(8),JNAME
         MVC   PRTAR+37(2),JSTEP
         MVC   PRTAR+47(8),JCARDS
         L     2,LINEST
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+61(8),=X'4020202020202120'
         ED    PRTAR+61(8),WORKD+4
         MVC   LINEST,=F'0'
         MVC   PRTAR+76(12),JCLASS
         MVC   JCLASS(12),=CL12' '
         L     2,TAPDEV
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+89(4),=X'40202120'
         ED    PRTAR+89(4),WORKD+6
         MVI   PRTAR+94,C'/'
         MVC   TAPDEV,=F'0'
         L     2,TAPEX
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+95(8),=X'4020202020202120'
         ED    PRTAR+95(8),WORKD+4
         MVC   TAPEX,=F'0'
         L     2,DISKDEV
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+106(4),=X'40202120'
         ED    PRTAR+106(4),WORKD+6
         MVC   DISKDEV,=F'0'
         MVI   PRTAR+111,C'/'
         L     2,DISKEX
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+112(8),=X'4020202020202120'
         ED    PRTAR+112(8),WORKD+4
         MVC   DISKEX,=F'0'
         BAL   11,PRINT
         L     2,JTOT
         LA    2,1(0,2)
         ST    2,JTOT
         LA    2,AREA
EOFSW    NOPR  12
         B     SW2
INVPARM  WTO   '***  INVALID  PARM  FIELD  ***',ROUTCDE=11
         B     ABEND
PRINT    PUT   PRTER,PRTA
         L     2,LINENO
         LA    2,2(2)
         ST    2,LINENO
         MVC   PRTAR,=CL132' '
         C     2,=F'60'
         BLR   11
         BAL   9,PRTHED
         MVC   LINENO,=F'7'
         BR    11
EOF      OI    EOFSW+1,X'F0'
         BAL   12,ENDREC
         MVC   PRTAR(132),=132C'-'
         BAL   11,PRINT
         MVC   PRTAR(132),=CL132'    GRAND TOTALS -'
         L     2,CRDTOT
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+47(8),=X'4020202020202120'
         ED    PRTAR+47(8),WORKD+4
         L     2,LINTOT
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+61(8),=X'4020202020202120'
         ED    PRTAR+61(8),WORKD+4
         L     2,TETOT
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+95(8),=X'4020202020202120'
         ED    PRTAR+95(8),WORKD+4
         L     2,DETOT
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+112(8),=X'4020202020202120'
         ED    PRTAR+112(8),WORKD+4
         L     2,JTOT
         CVD   2,WORKD
         OI    WORKD+7,X'0F'
         MVC   PRTAR+20(8),=X'4020202020202120'
         ED    PRTAR+20(8),WORKD+4
         MVC   PRTAR+29(4),=C'JOBS'
         BAL   11,PRINT
        DELETE EP=DATE3
         CLOSE (SMFIN,,PRTER)
         L     13,4(13)
         RETURN (14,12),RC=0
         LTORG
WORKD    DC    D'0'
WORKF    DC    F'0'
LINENO   DS    F
CURJOB   DC    CL16' '
LINEST   DC    F'0'
TAPDEV   DC    F'0'
TAPEX    DC    F'0'
DISKDEV  DC    F'0'
DISKEX   DC    F'0'
LINTOT   DC    F'0'
TETOT    DC    F'0'
DETOT    DC    F'0'
CRDTOT   DC    F'0'
DATENT   DC    F'0'
JTOT     DC    F'0'
JNAME    DC    CL8' '
JCARDS   DC    CL8' '
JCLASS   DC    CL12' '
TAB1     DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'
JDATE    DC    CL8' '
DATE1    DC    C'0000099999'
H11      DC    H'11'
PRTA     DC    C'0'
PRTAR    DC    CL132' '
JSTEP    DC    CL2' '
JOBHED   DC    C'1',CL48' ',CL84'A B B E Y   D A T A   C E N T R E'
JLINE    DC    C' ',CL48' ',33C'*',CL51' '
PGHEAD   DC    C'0',CL132'   PAGE                                      X
                   PERIPHERAL  SERVICES  STATISTICS         REPORT PERIX
               OD - XX/YY/ZZ - XX/YY/ZZ'
PAGENO   DC    PL3'1'
PLINE    DC    CL50' ',32C'*',CL51' '
PHEAD1   DC   C'0',CL132'     DATE           JOB-NAME      # STEPS     X
                CARDS          LINES         SYSOUT            DATA SETX
               S / EXCP COUNTS'
PHEAD2   DC    C' ',CL47' ',CL85'READ          PRINTED        CLASSES  X
                     TAPES                  DISKS'
ULINE    DC    C'0',132C'-'
PRTER    DCB   DDNAME=SYSPRINT,RECFM=FA,LRECL=133,BLKSIZE=133,DSORG=PS,X
               MACRF=(PM)
SMFIN    DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=(GM),RECFM=VBS,LRECL=440, X
               EODAD=EOF
INDATE   EQU   PGHEAD+107
OUTDATE  EQU   PGHEAD+118
AREA     DC    2CL220' '
         DSECT
         IFASMFR (4,5,6)
         END
