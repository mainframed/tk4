*
         TITLE 'XM210529  PRINT COMPARISON REPORT                     '
XM210529 CSECT
         ENTRY XM210500
XM210500 EQU   *
         PRINT NOGEN
         COPY  REGEQU
         SPACE 3
***********************************************************************
*                                                                     *
*    MODULE XM210529  PROGRAMMER 29 A W BROWN       ASSIGNED 16/05/74 *
*                           MODULE FUNCTIONS                          *
*                           ----------------                          *
*  MODULE PRINTS THE COMPARISON REPORT. THIS INDICATES ANY INEQUALITY *
*   IN THE RECORDS BEING COMPARED. INEQUALITIES BETWEEN 2 RECORDS     *
*   ARE INDICATED BY AN ASTERISK UNDER UNEQUAL BYTES. PARTS OF RECORD *
*   NOT COMPARED ARE INDICATED BY A DASH, & KEYS BY A K. (THESE WILL  *
*   HAVE BEEN BUILT UP IN THE UNDERLINE AREA).                        *
*  A SUMMARY OF RECORDS COMPARED & ANY INEQUALITIES IS PRINTED.       *
*  EACH TIME THROUGH THE MODULE THE ACTION TO BE TAKEN IS DETERMINED  *
*   BY THE STATE OF THE INDICATOR AT P5.                              *
*                                                                     *
*                                                                     *
         SPACE 2
*                             PARAMETERS                              *
*                             ----------                              *
*  1. DATA SET 1 RECORD                 - LENGTH SEE P4     I         *
*                                                                     *
*  2. DATA SET 2 RECORD                 - LENGTH SEE P4     I         *
*                                                                     *
*  3. UNDERLINE                         - LENGTH = MAX OF   I         *
*                                                  P1 OR P2           *
*  4. PRINT INFORMATION TABLE           - 203 BYTES         I/O       *
*        (SEE DSECT DS1)                                              *
*                                                                     *
*  5. ACTION INDICATOR                  - 1 BYTE            I/O       *
*        C'A' = EXTRA RECORD ON DATA SET 1 - PRINT 1ST RECORD    I    *
*        C'L' = EXTRA RECORD ON DATA SET 2 - PRINT 2ND RECORD    I    *
*        C'W' = RECORDS UNEQUAL            - PRINT BOTH RECORDS  I    *
*        C'B' = KEY NOT PRESENT            - PRINT 1ST RECORD    I    *
*        C'M' = KEY NOT PRESENT            - PRINT 2ND RECORD    I    *
*        C'C' =     KEY FIELD NOT PACKED   - PRINT 1ST RECORD    I    *
*        C'N' =     KEY FIELD NOT PACKED   - PRINT 2ND RECORD    I    *
*        C'6' = XX CONSECUTIVE UNEQUAL RECORDS                   I    *
*        C'0' = END OF FILE ON BOTH DATA SETS - FINISH REPORT    I    *
*        C'4' = INVALID HBS BLOCKING                             I    *
*        C'5' = RECORD LENGTH TOO LARGE                          I    *
*        C'8' = KEYS INCONSISTENT                                I    *
*        C'X' = COMPARE FIELDS NOT PRESENT - PRINT BOTH RECORDS  I    *
*                                                                     *
*                           SPECIAL NOTES                             *
*                           -------------                             *
*  USE B3401 TO PRODUCE COMPARISON REPORT.                            *
*   UNDERLINE AREA WILL CONTAIN ON ENTRY                              *
*        X'00' & IS TO BE PRINTED AS  -                               *
*        X'22'                        K                               *
*        X'44'                        BLANK                           *
*        X'55'                        *                               *
*        X'66'                        K                               *
*        X'CC'                        P                               *
*        X'AA'                        F                               *
*                                                                     *
***********************************************************************
         EJECT
         ISEQ  73,80
         MODIN
         USING SXTXXX,R9           PROVIDE LABELS FOR PRINT INFO TABLE
*
** INDICATE NO OF CHARACTERS ON PRINT LINE
*  ----------------------------------------------------------
         LA    R2,60               ASSUME CHARACTER
         CLI   SXT001,C'X'         Q- HEXADECIMAL?
         BE    BA04                A- NO,CHARACTER
         AR    R2,R2               INCREASE COUNT TO 120
         MVC   CCW131,CCW131A      INSERT CHAR.PRINT POS.IND.TO HEADING
BA04     MVC   CCW133+29(60),SXT060  PUT TITLE IN LINE
*
** CHECK ACTION INDICATOR BRANCHING TO ROUTINE REQ'D
*  -------------------------------------------------
BA06     LATCH B1501,((R10),CCT065,1,3,13,CFD004A+1,*)
         L     R15,CFD004A         POINT TO ROUTINE REQ'D
         BR    R15                 AND GO TO IT
         EJECT
*
** ACTION IND. A - EXTRA RECORD ON D/S 1
*  -------------------------------------
BA12     AP    SXT005E,=PL1'1'     INCREASE EXTRA RECORDS
         SP    SXT005C,=PL1'1'     Q- PRINT REQ'D
         BM    BA87                A- NO,GO PRINE MSG
         MVI   CCO133,C'0'         SPACE ONE LINE
         MVC   CCO133+3(12),CCM012 PUT ERROR MSG IN PRINT LINE
         BAL   R12,B6001           AND PRINT
         BAL   R11,B5101           PRINT HEADING FOR D/S 1
         LR    R4,R6               POINT D/S 1 RECORD
         BAL   R11,B5001           PRINT RECORD
         B     BA90                GO EXIT
*
** ACTION IND. L - EXTRA RECORD ON D/S 2
*  -------------------------------------
BA18     AP    SXT005F,=PL1'1'     INCREASE EXTRA RECORDS
         SP    SXT005C,=PL1'1'     Q- PRINT REQ'D
         BM    BA87                A- NO,GO PRINT MSG
         MVI   CCO133,C'0'         SPACE ONE LINE
         MVC   CCO133+3(12),CCM012 PUT ERROR MSG IN PRINT LINE
         BAL   R12,B6001           AND PRINT
         BAL   R11,B5201           PRINT HEADINGS FOR D/S 2
         LR    R4,R7               POINT TO D/S 2 RECORD
         BAL   R11,B5001           PRINT RECORD
         B     BA90                GO EXIT
*
** ACTION INDICATOR 'W' UNEQUAL RECORDS
*  ------------------------------------
BA24     AP    SXT005J,=PL1'1'     INCREASE UNEQUAL RECORDS
         SP    SXT005C,=PL1'1'     Q- PRINT REQ'D
         BM    BA87                A- NO,GO PRINT MSG
         MVI   CCO133,C'0'         SPACE ONE LINE
         MVC   CCO133+3(15),CCM015 PUT ERROR MSG IN PRINT LINE
         BAL   R12,B6001           AND PRINT
         BAL   R11,B5101           PRINT HEADING FOR D/S1
         LR    R4,R6               POINT TO DS 1 RECORD
         BAL   R11,B5001           PRINT RECORD
         BAL   R11,B5201           PRINT D/S 2 HEADING
         LR    R4,R7               POINT TO DS 2 RECORD
         BAL   R11,B5001           PRINT RECORD
         B     BA90                GO EXIT
*
** ACTION INDICATOR 'B' KEY NOT PRESENT DATA SET 1
*  -----------------------------------------------
BA30     AP    SXT005E,=PL1'1'     INCREASE EXTRA RECORDS
         SP    SXT005C,=PL1'1'     Q- PRINT REQ'D
         BM    BA87                A- NO,GO PRINT MSG
         MVI   CCO133,C'0'         SPACE ONE LINE
         MVC   CCO133+3(15),CCM015A PUT ERROR MSG IN PRINT LINE
         BAL   R12,B6001           AND PRINT
         BAL   R11,B5101           PRINT HEADING FOR DS1
         LR    R4,R6               POINT TO DS 1 RECORD
         BAL   R11,B5001           PRINT RECORD
         B     BA90                GO EXIT
         EJECT
*
** ACTION INDICATOR 'M' KEY NOT PRESENT DATA SET 2
*  -----------------------------------------------
BA36     AP    SXT005F,=PL1'1'     INCREASE EXTRA RECORDS
         SP    SXT005C,=PL1'1'     Q- PRINT REQ'D
         BM    BA87                A- NO,GO PRINT MSG
         MVI   CCO133,C'0'         SPACE ONE LINE
         MVC   CCO133+3(15),CCM015A PUT ERROR MSG IN PRINT LINE
         BAL   R12,B6001           AND PRINT
         BAL   R11,B5201           PRINT HEADING FOR D/S 2
         LR    R4,R7               POINT TO D/S 2 RECORD
         BAL   R11,B5001           PRINT RECORD
         B     BA90                GO EXIT
*
** ACTION INDICATOR 'C'  KEY FIELD NOT PACKED DATA SET 1
*  -----------------------------------------------------
BA42     AP    SXT005E,=PL1'1'     INCREASE EXTRA RECORDS
         SP    SXT005C,=PL1'1'     Q-PRINT REQ'D
         BM    BA87                A- NO,GO PRINT MSG
         MVI   CCO133,C'0'         SPACE ONE LINE
         MVC   CCO133+3(14),CCM014 PUT ERROR MSG. IN PRINT LINE
         BAL   R12,B6001           AND PRINT
         BAL   R11,B5101           PRINT HEADING FOR D/S 1
         LR    R4,R6               POINT TO D/S 1 RECORD
         BAL   R11,B5001           AND PRINT
         B     BA90                GO EXIT
*
** ACTION INDICATOR 'N' KEY FIELD NOT PACKED D/S 2
*  -----------------------------------------------
BA48     AP    SXT005F,=PL1'1'     INCREASE EXTRA RECORDS
         SP    SXT005C,=PL1'1'     Q- PRINT REQ'D
         BM    BA87                A- NO,GO PRINT MSG
         MVI   CCO133,C'0'         SPACE ONE LINE
         MVC   CCO133+3(14),CCM014 PUT ERROR MSG IN PRINT LINE
         BAL   R12,B6001           AND PRINT
         BAL   R11,B5201           PRINT HEADING FOR D/S 2
         LR    R4,R7               POINT TO D/S 2 RECORD
         BAL   R11,B5001           AND  PRINT
         B     BA90                GO EXIT
*
** ACTION INDICATOR 'X' COMPARE FIELD NOT PRESENT
*  ----------------------------------------------
BA54     AP    SXT005J,=PL1'1'     INCREASE UNEQUAL RECORDS
         SP    SXT005C,=PL1'1'     Q- PRINT  REQ'D
         BM    BA87                A- NO,GO PRINT MSG
         MVI   CCO133,C'0'         SPACE ONE LINE
         MVC   CCO133+3(25),CCM025 MOVE MSG TO PRINT LINE
         BAL   R12,B6001           AND PRINT
         BAL   R11,B5101           PRINT D/S 1 HDR
         LR    R4,R6               POINT TO DS 1 RECORD
         BAL   R11,B5001           AND PRINT
         BAL   R11,B5201           PRINT D/S 2 HDR
         LR    R4,R7               POINT TO DS 2 RECORD
         BAL   R11,B5001           AND PRINT
         B     BA90                GO EXIT
         EJECT
*
** ACTION INDICATOR '6'  XX CONSECUTIVE UNEQUAL RECORDS
*  ----------------------------------------------------
BA60     MVI   CCO133,C'-'         SPACE ONE LINE
         MVC   CCO133+6(78),CCM078 PUT MSG IN PRINT LINE
         MVC   CCO133+37(12),CXE012 PUT PATTERN IN PRINT LINE
         ED    CCO133+37(12),SXT005H PUT NUMBER OF CONSECUTIVE UNEQUAL
         BAL   R12,B6001             EXTRA RECORDS IN LINE AND PRINT
*
** PUT COMPARE TOTALS IN PRINT LINE FOR TYPES '6' AND '0'
*  ------------------------------------------------------
BA66     MVI   CCO133,C'-'         SPACE ONE LINE
         MVC   CCO133+11(31),CCM031     PUT MSG IN PRINT LINE
         SP    SXT005A,SXT005E          CALCULATE NO OF RECORDS COMPARE
         MVC   CCO133+42(12),CXE012     PUT PATTERN IN MSG
         ED    CCO133+42(12),SXT005A    PUT NO IN PRINT LINE
         BAL   R12,B6001                AND PRINT
         MVC   CCO133+11(31),CCM031A    PUT RECORDS UNEQUAL IN PRINT L.
         MVC   CCO133+42(12),CXE012     PUT PATTERN IN PRINT LINE
         ED    CCO133+42(12),SXT005J    PUT NO. IN PRINT LINE
         BAL   R12,B6001                AND PRINT
         MVC   CCO133+11(31),CCM031B    PUT EXTRA RECORDS IN PRINT LINE
         MVC   CCO133+42(12),CXE012     PUT PATTERN IN PRINT LINE
         ED    CCO133+42(12),SXT005E    PUT NO. IN PRINT LINE
         BAL   R12,B6001                AND PRINT
         MVC   CCO133+11(31),CCM031C    PUT EXTRA RECORDS IN PRINT LINE
         MVC   CCO133+42(12),CXE012     PUT PATTERN IN PRINT LINE
         ED    CCO133+42(12),SXT005F    PUT NO IN PRINT LINE
         BAL   R12,B6001                AND PRINT
         B     BA88                     GO CLOSE PRINT FILE
*
** ACTION INDICATOR '4' - INVALID HBS BLOCKING
*  -------------------------------------------
BA72     MVC   CCO133+6(34),CCM034      PUT MSG IN PRINT LINE
         MVI   CCO133,C'0'         SPACE ONE LINE
         B     BA86                     GO PRINT
*
** ACTION INDICATOR '5' - RECORD LENGTH TOO LARGE
*  ----------------------------------------------
BA78     MVC   CCO133+6(37),CCM037      PUT MSG IN PRINT LINE
         MVI   CCO133,C'0'         SPACE OME LINE
         B     BA86                     GO PRINT
*
** ACTION INDICATOR  '8' KEYS INCONSISTENT
*  ---------------------------------------
BA84     MVC   CCO133+6(31),CCM031D     MOVE MSG TO PRINT LINE
         MVI   CCO133,C'0'         SPACE ONE LINE
BA86     BAL   R12,B6001                AND PRINT
         B     BA88                GO CLOSE FILES
*
** PRINT NO LONGER REQ'D ISSUE MSG AND BRANCH NEXT TIME
*  ----------------------------------------------------
BA87     BC    0,BA90              Q- 1ST TIME THROUGH
         OI    BA87+1,X'F0'        A-YES,BRANCH NEXT TIME
         MVI   CCO133,C'0'         SPACE 1 LINE
         MVC   CCO133+6(59),CCM059 MOVE MSG TO PRINT LINE
         MVC   CCO133+38(12),CXE012 PUT PATTERN IN PRINT LINE
         ED    CCO133+38(12),SXT005D PUT NO IN PRINT LINE
         BAL   R12,B6001           AND PRINT
         B     BA90                GO EXIT
*
** CLOSE FILES
*  -----------
BA88     MVI   CCO133,X'FF'        SET CLOSE INDICATOR
         BAL   R12,B6001           CLOSE FILE
*
** EXIT
*  ----
BA90     MODOUT
         EJECT
*
** SUBROUTINE B50- PRINT RECORD IN HEX OR CHARACTER AND UNDERLINE AREA
*  -------------------------------------------------------------------
B5001    MVC   CCO133+2(131),CCW131     PUT PRINT POS IND IN PRINT LINE
         ST    R8,SFD004A          SAVE UNDERLINE ADDRESS (R8)
         BAL   R12,B6001                AND PRINT
B5003    SR    R3,R2               Q- MORE THAN 1 PRINT LINE
         BM    B5004               A- NO- PRINT WHATS LEFT
         LR    R5,R2               SAVE LENGTH OF LINE
         BAL   R12,B6101           CONVERT TO PRINT FORMAT
         BAL   R12,B6001           AND PRINT
         ST    R4,SFD004           SAVE ADDRESS OF PRINT LINE
         LR    R4,R8               POINT TO UNDERLINE
         LR    R5,R2               SAVE LENGTH OF UNDERLINE
         BAL   R12,B6101           CONVERT TO PRINT FORMAT
         TR    CCO133+7(120),CCT256 TRANSLATE UNDERLINE
         BAL   R12,B6001           PRINT UNDERLINE
         L     R4,SFD004           POINT TO RECORD
         AR    R4,R2               POINT AT NEXT RECORD
         AR    R8,R2               POINT AT NEXT UNDERLINE
         B     B5003               GO PRINT NEXT LINE
*
B5004    AR    R3,R2               NO OF BYTES TO BE PRINTED
         BZ    B5010               RETURN IF ZERO
         LR    R5,R3               SAVE LENGTH OF LINE
         BAL   R12,B6101           CONVERT TO PRINT FORMAT
         BAL   R12,B6001           AND PRINT
         LR    R4,R8               POINT TO UNDERLINE
         LR    R5,R3               SAVE LENGTH OF UNDERLINE
         BAL   R12,B6101           CONVERT TO PRINT FORMAT
         TR    CCO133+7(120),CCT256 TRANSLATE UNDERLINE
         BAL   R12,B6001           PRINT UNDERLINE
B5010    L     R8,SFD004A          RESTORE UNDERLINE ADDRESS TO R8
         BR    R11                 RETURN
         EJECT
*
** SUBROUTINE  B51- PRINT TITLE FOR D/S ONE
*  ----------------------------------------
B5101    MVC   CCO133+5(123),CCM123  PUT ERROR TITLE IN PRINT LINE
         MVC   CCO133+37(12),CXE012  PUT PATERN IN PRINT LINE
         MVC   CCO133+58(7),CXE007   PUT PATTERN IN PRINT LINE
         MVC   CCO133+84(44),SXT044A PUT DSNAME  IN PRINT LINE
         ED    CCO133+37(12),SXT005A PUT RECORD NO IN PRINT LINE
         LH    R3,SXT002A            SAVE RECORD LENGTH
         CVD   R3,SPW008             CONVERT LENGTH TO PACKED
         ED    CCO133+58(7),SPW008+5 PUT RECORD LENGTH IN PRINT LINE
         MVI   CCO133+37,C'='      INSERT = OVER EDIT MASK
         MVI   CCO133+58,C'='      INSERT = OVER EDIT MASK
         BAL   R12,B6001             AND PRINT
         BR    R11                   RETURN
         EJECT
*
** SUBROUTINE  B52- PRINT TITLE FOR DS TWO
*  ---------------------------------------
B5201    MVC   CCO133+5(123),CCM123A    PUT ERROR TITLE IN PRINT LINE
         MVC   CCO133+37(12),CXE012     PUT PATTERN IN PRINT LINE
         MVC   CCO133+58(7),CXE007      PUT PATTERN IN PRINT LINE
         MVC   CCO133+84(44),SXT044B    PUT DSNAME IN PRINT LINE
         ED    CCO133+37(12),SXT005B    PUT RECORD NO. IN PRINT LINE
         LH    R3,SXT002B               SAVE RECORD LENGTH
         CVD   R3,SPW008                CONVERT LENGTH TO PACKED
         ED    CCO133+58(7),SPW008+5    PUT RECORD LENGTH IN PRINT LINE
         MVI   CCO133+37,C'='      INSERT = OVER EDIT MASK
         MVI   CCO133+58,C'='      INSERT = OVER EDIT MASK
         BAL   R12,B6001                AND PRINT
         BR    R11                      RETURN
         EJECT
*
** SUBROUTINE  B60- PRINT A LINE
*  -----------------------------
B6001    LATCH B3401,(CCO532,CCO133,CCD001,CFD004)     PRINT LINE
         MVI   CCO133,C' '         SPACE ONE LINE
         MVI   CCD001,C'0'         INDICATE NORMAL LINE COUNTING
         BR    R12                 RETURN
         EJECT
*
** SUBROUTINE  B61- CONVERT TO PRINT FORMAT IF REQ'D
*  -------------------------------------------------
B6101    CLI   SXT001,C'C'         Q- CHARACTER PRINT REQ'D
         BNE   B6102               A- NO,GO CONVERT TO PRINTABLE HEX
         BCTR  R5,0                SET LENGHT FOR MOVE
         EX    R5,B6201            PUT RECORD IN PRINT LINE
         BR    R12                 RETURN
B6102    LATCH B1801,((R4),(R5),CCO133+7) CONVERT TO PRINTABLE HEX
         BR    R12                 RETURN
         EJECT
*
** SUBROUTINE B62- EXECUTED MOVE TO PRINTLINE
*  ------------------------------------------
B6201    MVC   CCO133+7(0),0(R4)    PUT RECORD IN PRINT LINE
         EJECT
*
** CONSTANTS STORAGE AREAS AND LITERALS
*  ------------------------------------
SPW008   DS    D                   WORK AREA FOR LENGTH
SFD004   DS    F                   SAVE AREA FOR R4
SFD004A  DS    F                   SAVE AREA FOR R8 - UNDERLINE ADDR.
CFD004   DC    F'70'               LINE COUNT
CFD004A  DC    F'0'                ADDRESS OF ROUTINE FROM B1501
CXE012   DC    XL12'402020206B2020206B202120'
CXE007   DC    XL7'4020206B202020'
CCO133   DC    CL133' '            PRINT LINE
CCD001   DC    C'1'                PAGE INDICATOR FOR B3401
CCO532   DC    CL1'1'              1ST LINE OF HEADER
         DC    CL34' '
         DC    CL98'DATA  SET COMPARE PROGRAM - COMPARISON REPORT'
         DC    CL133' '            2ND LINE OF HEADER
CCW133   DC    CL133' '            BLANK LINE FOR TITLE
         DC    CL133'0'            TWO BLANK LINES
         DC    CL2' '              PRINT POS INDICATOR
CCW131   DC    CL131'     /0        /         /10       /         /20  X
                    /         /30       /         /40       /         /X
               50       /         /60'
         DC    C'Z'                END OF HEADINGS INDICATOR
CCM123   DC    CL123'1ST DATA SET RECORD.  RECORD NO.               LENX
               GTH          DATA SET NAME='
CCM123A  DC    CL123'2ND DATA SET RECORD.  RECORD NO.               LENX
               GTH          DATA SET NAME='
CCM012   DC    CL12'EXTRA RECORD'
CCM015   DC    CL15'UNEQUAL RECORDS'
CCM015A  DC    CL15'KEY NOT PRESENT'
CCM025   DC    CL25'COMPARE FIELD NOT PRESENT'
CCM014   DC    CL14'KEY NOT PACKED'
CCM031   DC    CL31'RECORDS COMPARED              ='
CCM031A  DC    CL31'RECORDS UNEQUAL               ='
CCM031B  DC    CL31'EXTRA RECORDS ON DATA SET 1   ='
CCM031C  DC    CL31'EXTRA RECORDS ON DATA SET 2   ='
CCM059   DC    CL59'****** PRINTING TERMINATED AFTER             RECORDX
               S ******'
CCM078   DC    CL78'****** COMPARE TERMINATED AFTER             CONSECUX
               TIVE UNEQUAL RECORDS ******'
CCM034   DC    CL34'****** INVALID HBS BLOCKING ******'
CCM037   DC    CL37'****** RECORD LENGTH TOO LARGE ******'
CCM031D  DC    CL31'****** KEYS INCONSISTENT ******'
CCW131A  DC    CL131'     /0        /         /20       /         /40  X
                    /         /60       /         /80       /         /X
               100      /         /120'
CCT065   DC    C'A',AL3(BA12)      TABLE
         DC    C'L',AL3(BA18)
         DC    C'W',AL3(BA24)      FOR
         DC    C'B',AL3(BA30)
         DC    C'M',AL3(BA36)      B1501
         DC    C'C',AL3(BA42)
         DC    C'N',AL3(BA48)      TO CHECK
         DC    C'X',AL3(BA54)
         DC    C'6',AL3(BA60)      ERROR
         DC    C'0',AL3(BA66)
         DC    C'4',AL3(BA72)      CODE
         DC    C'5',AL3(BA78)
         DC    C'8',AL3(BA84)
CCT256   DC    C'-'
         DC    CL33' '
         DC    C'K'                TABLE
         DC    CL33' '
         DC    C' '
         DC    CL16' '
         DC    C'*'                FOR
         DC    CL16' '
         DC    C'K'
         DC    CL67' '
         DC    C'F'                TRANSLATION
         DC    CL22' '
         DC    C'F'
         DC    CL1' '
         DC    CL1'P'              OF
         DC    CL8' '
         DC    C'P'
         DC    CL35' '
         DC    C'-'          UNDERLINE
         DC    CL1' '
         DC    C'K'
         DC    CL1' '
         DC    C' '                AREA
         DC    C'*'
         DC    C'K'
         DC    CL9' '
         LTORG
         EJECT
DS1      DSECT
*
** PRINT INFORMATION TABLE
*  -----------------------
SXTXXX   DS    0XL203              PRINT INFORMATION TABLE
SXT002A  DS    XL2                 - DATA SET 1 RECORD LENGTH
SXT002B  DS    XL2                 - DATA SET 2 RECORD LENGTH
SXT005A  DS    PL5                 - DATA SET 1 RECORD NO.
SXT005B  DS    PL5                 - DATA SET 2 RECORD NO.
SXT005C  DS    PL5              ** - NO. UNEQUAL/EXTRA RECORDS TO
*                                    PRINT BEFORE PRINT TERMINATION
SXT005D  DS    PL5                 - MAX.NO. TO PRINT BEFORE PRINT TERM
SXT005E  DS    PL5              ** - EXTRA RECORDS ON DATA SET 1
SXT005F  DS    PL5              ** - EXTRA RECORDS ON DATA SET 2
SXT005G  DS    PL5                 - NO.OF CONSECUTIVE UNEQUAL/EXTRA
*                                    RECORDS BEFORE TERMINATION
SXT005H  DS    PL5                 - MAX.NO. OF CONSECUTIVE UNEQUAL/
*                                    EXTRA RECORDS BEFORE TERM.
SXT001   DS    C                   - PRINT FORMAT IND. C OR X
SXT005J  DS    PL5              ** - NO.OF UNEQUAL RECORDS
SXT044A  DS    CL44                - DATA SET 1 NAME
SXT044B  DS    CL44                - DATA SET 2 NAME
SXT060   DS    CL60                - REPORT TITLE
*                                  ** = SET BY THIS MODULE
SXT005K  DS    PL5                 - MAX.LENGTH OF RECORD/UNDERLINE
XM210529 CSECT
         END
