PUNCHREP CSECT
*
*  THE PUNCHREP PROGRAM FULFILLS TWO NEEDS....
*      1 IT AIDS THE SELECTIVE MANIPULATION OF LIBRARIES BY PUNCHING
*        CARDS FOR EACH DIRECTORY ENTRY IN ONE OF VARIOUS FORMATS THAT
*        CAN BE SPECIFIED IN THE EXEC CARD PARM FIELD.  THE USER CAN
*        EXTRACT THOSE CARDS HE NEEDS AND USE THEM AS INPUT TO IEHMOVE,
*        IEBCOPY, IEBUPDTE, IEHPROGM, OR TSGUPDAT.  THE CARDS ARE
*        SEQUENCE NUMBERED AND A PRINTED LISTING IS PROVIDED SO THAT
*        THEY CAN BE SORTED OUT WITHOUT HAVING TO INTERPRET THEM.
*      2 IF THE 'TTRSEQ' OPTION IS USED THE OUTPUT WILL INDICATE THE
*        SEQUENCE IN WHICH MEMBERS WERE ADDED TO THE LIBRARY.  THE USER
*        CAN 'DATE STAMP' ADDITIONS TO THE LIBRARY BY PLACING DUMMY
*        MEMBERS IN THE LIBRARY AT SELECTED TIMES - THE DUMMY MEMBER
*        NAME WILL GIVE THE TIME AND DATE AT WHICH IT WAS ADDED, AND
*        SUBSEQUENT OUTPUT FROM PUNCHREP WILL SHOW THE NORMAL
*        MEMBERS IN RELATION TO THOSE TIMES.
*
*  THE JCL REQUIRED IS, E.G....
*        //LIST  EXEC  PGM=PUNCHREP,PARM='X,Y'
*        //SYSPRINT  DD  SYSOUT=A
*        //SYSPUNCH  DD  SYSOUT=P
*        //SYSLIB  DD  DSN=USERLIB,DISP=SHR
*
*  NOTES.
*        PARM FIELD.
*        THE FIRST PARAMETER ('X') MAY BE 'TTRSEQ' OR 'DIRSEQ'.
*        'TTRSEQ' IS THE DEFAULT AND SELECTS THE SORTING OPTION.
*        'DIRSEQ' CAUSES OUTPUT TO BE IN COLLATING SEQUENCE OF MEMBER
*        NAMES (AS IN LISTPDS).
*        THE SECOND PARAMETER ('Y') MAY BE....
*        'MEMBER'  (DEFAULT) OUTPUT WILL BE 'MEMBER NAME=' CARDS FOR
*                  INPUT TO IEBCOPY.
*        'SELECT'  OUTPUT WILL BE 'SELECT MEMBER=' CARDS FOR IEHMOVE.
*        'SCRATCH' OUTPUT WILL BE PAIRS OF CARDS FOR IEHPROGM IN THE
*                  FORMAT 'SCRATCH PURGE,VOL=XXXX=YYYYYY,MEMBER=     ,
*                  DSNAME=    '
*        './REPRO' OUTPUT WILL BE './  REPRO  LIST=ALL,NAME=  (,SSI= )'
*                  CARDS FOR IEBUPDTE.
*        '@%CHANGE'  OUTPUT WILL BE '@%  CHANGE  LIST,NAME=  (,SSI= )'
*                  CARDS FOR TSGUPDAT.
*        '@%SCRATCH' OUTPUT WILL BE '@%  SCRATCH  ....' CARDS FOR
*                  TSGUPDAT - NOTE THAT EACH CARD WILL HAVE ONE
*                  MEMBER NAME ONLY ON IT, ALTHOUGH THIS WILL CAUSE
*                  TSGUPDAT TO SKIP TO A NEW PAGE FOR EACH CARD, AS THE
*                  PURPOSE OF PUNCHREP IS TO ALLOW THE USER TO SELECT
*                  AND DISCARD THE OUTPUT IN ANY COMBINATION.
*
*  ALIAS ENTRIES FOUND IN THE DIRECTORY ARE TREATED AS FOLLOWS....
*        FOR 'MEMBER' AND 'SELECT' - NO CARD IS PUNCHED BUT A COMMENT
*        IS PRINTED SHOWING '...... IS AN ALIAS FOR ABOVE NAMED
*        MEMBER'.  SINCE ALIASES MAY NOT FOLLOW THE MEMBER IN
*        COLLATING SEQUENCE THESE COMMENTS SHOULD BE IGNORED UNLESS THE
*        TTRSEQ OPTION WAS USED.
*        FOR './REPRO' AND '@%CHANGE' - AN ALIAS CARD IN PROPER FORMAT
*        FOR IEBUPDTE OR TSGUPDAT WILL BE PUNCHED AND LISTED.
*        FOR 'SCRATCH' AND '@%SCRATCH' - THE ALIAS NAME WILL BE
*        INCLUDED IN A NORMAL OUTPUT CARD AS IF IT WERE A TRUE MEMBER.
*
*  SSI INFORMATION WILL BE APPENDED TO './REPRO' AND '@%CHANGE' CARDS
*        IF THE SSI INFORMATION EXISTS IN THE DIRECTORY AND MEETS THE
*        FOLLOWING RESPOND STANDARD....
*              SSI=AAAABBBC
*        AAAA MUST X'0020'
*        EACH B MUST BE IN THE RANGE 0 - 9
*        C MAY BE AN HEX DIGIT 0 - F
*
*  EXAMPLES OF PARM FIELDS....
*        1  PARM OMITTED GIVES MEMBER CARDS FOR IEBCOPY IN TTRSEQ.
*        2  PARM='DIRSEQ,SCRATCH' GIVES SCRATCH CARDS FOR IEHPROGM IN
*           COLLATING SEQUENCE.
*        3  PARM=SELECT GIVES SELECT CARDS FOR IEHMOVE IN TTRSEQ BY
*           DEFAULT.
*
*  THERE IS A LIMIT OF 1280 MEMBERS IN ANY ONE DIRECTORY FOR THIS
*  PROGRAM
*
*        BEGIN  BASE=(11,12)
         STM   14,12,12(13)
         BALR  11,0
         USING  *,11,12
IHB1     EQU   *
         L     12,IHB1A
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'                  NEW SAVE AREA
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         B     IHB1B
IHB1A    DC    A(IHB1+4096)
IHB1B    EQU   *
         L     1,0(1)
         LH    2,0(1)
         LTR   2,2
         BZ    NOPARM
         CLC   2(6,1),SEQ
         BE    SEQSPEC
         CLC   2(6,1),DSEQ
         BNE   TRYREP
         MVC   SEQ,DSEQ
SEQSPEC  LA    1,7(1)
         LA    3,7
         SR    2,3
         BC    4,TRYREP
         AR    2,3
         BC    8,NOPARM
TRYREP   CLC   2(7,1),RFORM
         BNE   *+14
         MVC   CFORM,RFORM
         B     NOPARM
         CLC   2(5,1),REPERR
         BNE   *+14
         MVC   CFORM,RFORM
         B     NOPARM
         CLC   2(6,1),SFORM
         BNE   *+14
         MVC   CFORM,SFORM
         B     NOPARM
         CLC   2(9,1),TSFORM
         BNE   *+14
         MVC   CFORM,TSFORM
         B     NOPARM
         CLC   2(8,1),TCFORM
         BNE   *+14
         MVC   CFORM,TCFORM
         B     NOPARM
         CLC   2(7,1),SCRFORM
         BNE   *+10
         MVC   CFORM,SCRFORM
NOPARM   RDJFCB  (LIST,,PUNCH,,LIB)
         NC    LJFCB+102(2),LJFCB+102
         BC    4,*+12
         LA    1,1620
         STH   1,LJFCB+102
         NC    PJFCB+102(2),PJFCB+102
         BC    4,*+12
         LA    1,1600
         STH   1,PJFCB+102
         OPEN  (LIST,(OUTPUT),PUNCH,(OUTPUT),LIB),TYPE=J
         MVC   LIBNAME,LIBJFCB
         OBTAIN  CAMLST
         LTR   15,15
         BNZ   ABOBERR
         TM    DSCB+40,X'C0'
         BO    *+8
         MVI   TYPLIB,255
         L     0,TWENTYK
         GETMAIN  R,LV=(0)
         ST    1,GOTADD
         MVC   8(4,1),ENDMARK
         ST    1,HIDIR
         A     1,TWENTYK
         ST    1,ENDOFGOT
READ     READ  ECBA,SF,LIB,DBLK,256
         CHECK ECBA
         LA    2,DBLK
         LH    3,DBLK
         LTR   3,3
STOPBR   BZ    EMPTYB
         L     1,FULL
         LA    1,1(1)
         ST    1,FULL
         AR    2,3
         LA    3,DBLK+2
ENTRY    CLC   0(8,3),FFS
         BNE   MVILL
         OI    STOPBR+1,240
         B     READ
MVILL    MVI   LL,31
         XC    SSIFLD,SSIFLD
         NC    LL,11(3)
         NI    11(3),128
         SR    1,1
         IC    1,LL
         SLL   1,1
         STC   1,LL
         C     1,FOUR
         BL    SOURSSI
         LA    15,12(1,3)
         S     15,FOUR
         CLI   TYPLIB,255
         BNE   *+14
         MVC   SSIFLD,12(3)
         B     SOURSSI
         MVC   SSIFLD,0(15)
SOURSSI  CLI   SEQ,C'D'
         BNE   ENCOMP-4
         L     9,HIDIR
         MVC   0(12,9),0(3)
         MVC   12(4,9),SSIFLD
         LA    9,16(9)
         C     9,ENDOFGOT
         BNL   ABSIZE
         ST    9,HIDIR
         B     STEPON
         L     9,GOTADD
ENCOMP   CLC   8(4,3),8(9)
         BNH   INSERT
         LA    9,16(9)
         B     ENCOMP
INSERT   L     8,HIDIR
         C     8,ENDOFGOT
         BNL   ABSIZE
         LR    7,8
         LA    8,16(8)
         ST    8,HIDIR
NEXTIN   MVC   0(16,8),0(7)
         S     8,SIXTEEN
         S     7,SIXTEEN
         CR    7,9
         BNL   NEXTIN
         MVC   0(12,8),0(3)
         MVC   12(4,8),SSIFLD
STEPON   A     3,LLA
         LA    3,12(3)
         CR    2,3
         BH    ENTRY
         B     READ
EMPTYB   L     1,EMPTY
         LA    1,1(1)
         ST    1,EMPTY
         B     READ
EODIR    L     9,GOTADD
         CLC   CFORM,RFORM
         BE    REP
         CLC   CFORM,SFORM
         BE    SEL
         CLC   CFORM,TSFORM
         BE    TSF
         CLC   CFORM,TCFORM
         BE    TCF
         CLC   CFORM,SCRFORM
         BE    SCR
         B     MEM
SEL      C     9,HIDIR
         BNL   ALLDONE
         TM    11(9),128
         BZ    TMEM
         MVC   ISANAL+2(8),0(9)
         MVC   CARD(39),ISANAL
         MVI   PSWTCH,0
         BAL   10,PRINT
         B     NMEM
TMEM     MVC   MCARD+17(8),0(9)
         MVI   PSWTCH,255
         MVC   CARD(25),MCARD
         BAL   10,PRINT
NMEM     LA    9,16(9)
         B     SEL
SCR      L     1,16
         L     1,0(1)
         L     1,4(1)
         L     1,12(1)              TIOT ADDRESS
         AH    1,LIB+40
         L     1,16(1)              UCB ADDRESS
         MVC   SCRCARD+24(6),28(1)  VOL SERIAL
         SR    10,10
         IC    10,19(1)
         LA    2,DISKTAB-4
         SLL   10,2
         AR    2,10
         MVC   SCRCARD+19(4),0(2)
         MVC   SCRCCARD+7(44),LIBJFCB
SCRLP    C     9,HIDIR
         BNL   ALLDONE
         MVC   SCRCARD+38(8),0(9)
         LA    10,SCRCARD+45
         MVI   1(10),64
         CLI   0(10),64
         BCTR  10,0
         BE    *-6
         MVI   2(10),C','
         MVC   CARD(72),SCRCARD
         MVI   PSWTCH,255
         BAL   10,PRINT
         MVC   CARD+15(51),SCRCCARD
         BAL   10,PRINT
         LA    9,16(9)
         B     SCRLP
PRINT    SP    LCNT,ONE
         BP    PLINE
         PUT   LIST,NP
         PUT   LIST,TITLE
         ZAP   LCNT,MAXCNT
PLINE    CLI   PSWTCH,0
         BE    NOSEQ
         AP    CSEQ,ONE
         MVC   CARD+74(6),SEQED
         ED    CARD+74(6),CSEQ
         PUT   PUNCH,CARD
NOSEQ    PUT   LIST,LINE
         MVC   CARD,SPACES
         BR    10
ABOBERR  PUT   LIST,NP
         PUT   LIST,TITLE
         MVC   CARD(57),OBERMSG
         PUT   LIST,LINE
         LA    15,16
*
*  IF THERE IS AN ERROR IN OBTAINING THE DSCB OF THE LIBRARY, OR IF
*  THE LIMIT OF 1280 MEMBERS IS EXCEEDED, THE PROGRAM ENDS WITH A
*  RETURN CODE OF 16
*
*        GOBACK  RC=(15)
         L     13,4(13)
         L     14,12(13)
         LM    0,12,20(13)
         MVI   12(13),255
         BR    14
ABSIZE   MVC   OBERMSG+27(30),ABSIZMSG
         B     ABOBERR
TSF      C     9,HIDIR
         BNL   ALLDONE
         MVC   TSCARD+13(8),0(9)
         MVC   CARD(21),TSCARD
         MVI   PSWTCH,255
         BAL   10,PRINT
         LA    9,16(9)
         B     TSF
REP      C     9,HIDIR
         BNL   ALLDONE
         TM    11(9),128
         BZ    TREP
         MVC   REPAL+16(8),0(9)
         MVC   CARD(24),REPAL
         B     REPPRNT+6
TREP     MVC   REPCARD+33(13),SPACES
         MVC   REPCARD+25(8),0(9)
         BAL   10,CHKSSI
         B     REPPRNT
         LA    5,SSIIS+5
         LA    6,12(9)
*        HEXPRINT  (6),(5),4
         CNOP  0,4
         BAL   1,*+36
         DC    A(0)
         DC    A(0)
         DC    H'4'
         DC    AL1(0)
         DC    AL1(0)
         DC    CL16'0123456789ABCDEF',V(TSGHEXPT)
         ST    5,0(1)
         ST    6,4(1)
         L     15,28(1)
         BALR  14,15
TSGHEXPT CSECT
         STM   0,7,20(13)
         LM    4,5,0(1)
         LH    0,8(1)
         SR    3,3
         SR    6,6
         SR    7,7
         IC    6,10(1)
         IC    7,11(1)
         IC    3,0(5)
         SLDL  2,28
         SRL   3,28
         IC    2,12(1,2)
         IC    3,12(1,3)
         STC   2,0(4)
         STC   3,1(4)
         LA    4,2(4)
         LA    5,1(5)
         BCT   6,72(15)
         AR    4,7
         IC    6,10(1)
         BCT   0,26(15)
         LM    0,7,20(13)
         BR    14
PUNCHREP CSECT
         LA    1,REPCARD+32
         CLI   0(1),64
         BCTR  1,0
         BE    *-6
         MVC   2(13,1),SSIIS
REPPRNT  MVC   CARD(46),REPCARD
         MVI   PSWTCH,255
         BAL   10,PRINT
         LA    9,16(9)
         B     REP
TCF      C     9,HIDIR
         BNL   ALLDONE
         TM    11(9),128
         BZ    TTCF
         MVC   TALC+11(8),0(9)
         MVC   CARD(19),TALC
         B     TCFPRNT+6
TTCF     MVC   TCCARD+30(13),SPACES
         MVC   TCCARD+22(8),0(9)
         BAL   10,CHKSSI
         B     TCFPRNT
         LA    5,SSIIS+5
         LA    6,12(9)
*        HEXPRINT  (6),(5),4
         CNOP  0,4
         BAL   1,*+36
         DC    A(0)
         DC    A(0)
         DC    H'4'
         DC    AL1(0)
         DC    AL1(0)
         DC    CL16'0123456789ABCDEF',V(TSGHEXPT)
         ST    5,0(1)
         ST    6,4(1)
         L     15,28(1)
         BALR  14,15
         LA    1,TCCARD+30
         CLI   0(1),64
         BCTR  1,0
         BE    *-6
         MVC   2(13,1),SSIIS
TCFPRNT  MVC   CARD(43),TCCARD
         MVI   PSWTCH,255
         BAL   10,PRINT
         LA    9,16(9)
         B     TCF
MEM      C     9,HIDIR
         BNL   ALLDONE
         TM    11(9),128
         BZ    TRUMEM
         MVC   ISANAL+2(8),0(9)
         MVC   CARD(39),ISANAL
         MVI   PSWTCH,0
         BAL   10,PRINT
         B     NEXMEM
TRUMEM   MVC   MEMNCARD+15(8),0(9)
         MVC   CARD(23),MEMNCARD
         MVI   PSWTCH,255
         BAL   10,PRINT
NEXMEM   LA    9,16(9)
         B     MEM
CHKSSI   NC    12(4,9),12(9)
         BCR   8,10
         CLC   12(2,9),TWOOH
         BCR   7,10
         LA    4,9
         LH    6,14(9)
         LA    7,3
NCHK     LA    5,15
         SRL   6,4
         NR    5,6
         CR    4,5
         BCR   4,10
         BCT   7,NCHK
         B     4(10)
ALLDONE  L     1,FULL
         CVD   1,DBLK
         ED    FINMSGF,DBLK+6
         L     1,EMPTY
         CVD   1,DBLK
         ED    FINMSGE,DBLK+6
         PUT   LIST,UP3
         PUT   LIST,FINMSG
         CLOSE (LIST,,LIB,,PUNCH)
*        GOBACK
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
CAMLST   CAMLST  SEARCH,LIBJFCB,LIBJFCB+118,DBLK
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),LRECL=81,           *
               RECFM=FBM,EXLST=LXLST
PUNCH    DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=(PM),LRECL=80,           *
               RECFM=FB,EXLST=PXLST
LIB      DCB   DDNAME=SYSLIB,DSORG=PS,MACRF=(R),RECFM=F,LRECL=256,     *
               BLKSIZE=256,EODAD=EODIR,EXLST=LIBXLST
LXLST    DC    X'87',AL3(LJFCB)
PXLST    DC    X'87',AL3(PJFCB)
LIBXLST  DC    X'87',AL3(LIBJFCB)
LJFCB    DC    XL176'00'
PJFCB    DC    XL176'00'
LIBJFCB  DC    XL176'00'
TWENTYK  DC    F'20480'
GOTADD   DC    F'0'
ENDOFGOT DC    F'0'
HIDIR    DC    F'0'
ENDMARK  DC    X'7FFFFF80'
FOUR     DC    F'4'
SIXTEEN  DC    F'16'
FULL     DC    F'0'
EMPTY    DC    F'0'
SSIFLD   DC    XL4'00'
         DS    0F
LLA      DC    XL3'00'
LL       DC    X'00'
SEQ      DC    C'TTRSEQ'
DSEQ     DC    C'DIRSEQ'
CFORM    DC    CL9'MEMBER'
REPERR   DC    C'REPRO'
RFORM    DC    CL9'./REPRO'
SFORM    DC    CL9'SELECT'
TSFORM   DC    CL9'@%SCRATCH'
TCFORM   DC    CL9'@%CHANGE'
SCRFORM  DC    CL9'SCRATCH'
TWOOH    DC    X'0020'
TYPLIB   DC    X'00'               00 = LOAD   FF = SOURCE
NP       DC    X'8B'
TITLE    DC    X'19',C'RESPOND TSG PUNCHREP UTILITY LISTING OF '
LIBNAME  DC    CL40' '
LCNT     DC    PL2'0'
MAXCNT   DC    PL2'55'
ONE      DC    P'1'
TWO      DC    P'2'
DISKTAB  DC    C'23112301230323022321'
         DC    CL8' '
         DC    C'2314'
*
*  PSWTCH  0 = PRINT WITH NO PUNCH OR SEQ NUMBER    FF = PRINT AND PUNC
*
PSWTCH   DC    X'00'
LINE     DC    X'09'
CARD     DC    CL80' '
SPACES   DC    CL80' '
CSEQ     DC    PL3'0'
SEQED    DC    X'402020202120'
ISANAL   DC    CL39'           IS AN ALIAS FOR ABOVE MEMBER'
MCARD    DC    CL25'  SELECT  MEMBER='
SCRCARD  DC    CL72' SCRATCH PURGE,VOL=1234=123456,MEMBER=             *
                                   *'
SCRCCARD DC    CL51'DSNAME= '
REPCARD  DC    CL46'./  REPRO  LIST=ALL,NAME=12345678'
SSIIS    DC    C',SSI=12345678'
REPAL    DC    C'./  ALIAS  NAME=12345678'
TSCARD   DC    CL71'@%  SCRATCH '
TALC     DC    C'@%  ALIAS  12345678'
TCCARD   DC    CL43'@%  CHANGE  LIST,NAME=12345678'
MEMNCARD DC    C'  MEMBER  NAME=12345678'
OBERMSG  DC    C'PUNCHREP FAILED BECAUSE OF ERROR IN OBTAINING SYSLIB D*
               SCB'
ABSIZMSG DC    CL30'MORE THAN 1280 MEMBERS IN LIB'
UP3      DC    X'1B'
FINMSG   DC    X'09'
         DC    C'DIRECTORY BLOCK TOTALS - FULL ='
FINMSGF  DC    X'40202120'
         DC    C'    EMPTY ='
FINMSGE  DC    X'40202120'
         DC    CL30' '
FFS      DC    8X'FF'
         DS    0D
DBLK     DS    CL256
         DS    CL96
DSCB     EQU   DBLK
         END
