ZSCRTMVE CSECT
         PRINT NOGEN
         SPACE 2
* THE PURPOSE OF THIS MODULE IS TO SCAN THE VTOCS OF SPECIFIED VOLUMES
* AND DELETE **SYSUT DATA SETS LEFT BY IEHMOVE
*
**   AUTHORS - I.C.I. (MOND DIVISION)
*
         SPACE 2
         SAVE  (14,12)                  SAVE SYSTEM REGISTERS
         SPACE 2
         BALR  12,0                     ESTABLISH
         USING *,12                               ADDRESSABILITY
         SPACE 2
         ST    13,ZSAVE+4               ESTABLISH
         LR    11,13                        SAVE
         LA    13,ZSAVE                        AREA
         ST    13,8(11)                          CHAINS
         SPACE 2
         L     3,0(1)              PICK UP PARM LIST
         LH    5,0(3)              PICK UP PARM LIST LENGTH
         SPACE 2
         OPEN  (ZPRINT,(OUTPUT))   OPEN OUTPUT FILE
         SPACE 2
         LTR   5,5                 IS LIST SUPPLIED
         BZ    ZLABEL27                 NO BRANCH
         SPACE 2
         LA    3,2(3)                   PICK UP PARM LIST ENTRY
         CLI   0(3),X'F0'               IS IT 0
         BE    ZLABEL27                 YES BRANCH
         CLI   0(3),X'F1'               IS IT 1
         BE    ZLABEL01                 YES BRANCH
         SPACE 2
         LA    2,ZVOLIST                PICK UP HEAD OF VOLUME LIST
         SPACE 2
ZLABEL29 CLI   0(2),X'4B'               IS SPACE FREE,IN LIST
         BE    ZLABEL28                 YES - BRANCH
         C     2,=A(ZDSN2)              LIST FULL
         BE    ZLABEL01                 YES - BRANCH
         LA    2,10(2)                  MOVE TO NEXT SLOT
         B     ZLABEL29
         SPACE 2
ZLABEL27 EQU   *
         LA    2,ZVOLIST                PICKUP HEAD OF VOLUME LIST
         SPACE 2
ZLABEL28 EQU   *
         OPEN  (ZCRDIN,(INPUT))         OPEN INPUT FILE
ZLABEL02 GET   ZCRDIN,ZWORK
         SPACE 2
         AP    ZCOUNT(2),=PL2'1'        INCREMENT DATA CARD COUNT
         CLI   ZWORK+6,C','             IS SYNTAX CORRECT
         BNE   ZLABEL03
         MVC   0(6,2),ZWORK             ENTER VOL-SER-ID IN VOLUME LIST
         CLI   ZWORK+7,X'F0'            IS DEVICE TYPE OK -TOO LOW
         BL    ZLABEL03                 YES-BRANCH
         CLI   ZWORK+7,X'F7'            IS DEVICE TYPE OK -TOO HIGH
         BC    10,ZLABEL03              YES-BRANCH
         SPACE 2
         NI    ZWORK+7,X'0F'            CONVERT DEVICE
         MVC   ZWORK+4(3),=X'000000'      IDENTIFIER TO
         L     3,ZWORK+4                    BINARY
         SLL   3,2                            AND LOAD APPROPRIATE
         LA    4,ZDEVTYPE                       DEVICE TYPE CODE
         AR    3,4                                INTO VOLUME
         MVC   6(4,2),0(3)                          LIST
         SPACE 2
ZLABEL04 LA    2,10(2)                  INCREMENT VOLUME LIST POINTER
         C     2,=A(ZEND)               RECYCLE IF LIST NOT FULL
         BNE   ZLABEL02
         SPACE 2
         B     ZLABEL01
ZLABEL03 MVC   ZOUTAREA(ZMARK3),ZCAPT01 LOAD ERROR MESSAGE
         ED    ZOUTAREA+ZMARK1(L'ZEDWRD),ZCOUNT   LOAD DATA CARD ID
         PUT   ZPRINT,ZOUTAREA                    PRINT MSG
         MVI   ZCAPT01,X'40'                      RESET
         MVI   ZOUTAREA,X'40'
         MVC   ZOUTAREA+1(L'ZOUTAREA-1),ZOUTAREA       BUFFER
         MVI   0(2),X'5C'                         INDICATE BYPASS
         B     ZLABEL04
         SPACE 2
ZLABEL00 CLOSE ZCRDIN              CLOSE INPUT FILE
         SPACE 2
ZLABEL01 EQU   *
         MVI   ZOUTAREA,X'F1'           SET ASA CHAR
         SPACE 2
         LA    4,ZVOLIST-10             R4 FORMS BASE FOR VOLUME
         SPACE 2
ZLABEL06 LA    2,ZDSN1-28               R2 FORMS BASE FOR DSN CYCLE
         LA    4,10(4)                  MOVE TO NEXT VOLUME
         CLI   0(4),X'4B'               END OF VOLUME CYCLE
         BE    ZLABEL22                 YES-GO TO EXIT
         SPACE 2
         CLI   0(4),X'5C'               VOLUME TO BE IGNORED
         BE    ZLABEL06                 YES - MOVE TO NEXT VOLUME
         SPACE 2
ZLABEL10 LA    2,28(2)                  MOVE TO NEXT DSN
         SPACE 2
         MVI   ZDSN0,X'40'
         MVC   ZDSN0+1(43),ZDSN0
         C     2,=A(ZDSN2)              END OF DSN CYCLE ?
         BE    ZLABEL06                 YES--MOVE TO NEXT VOLUME
         SPACE 2
         LA    3,5                     R3 FORMS BASE FOR LENGTH CYCLE
         SPACE 2
ZLABEL18 LA    3,2(3)                   MOVE TO NEXT LENGTH
         SPACE 2
         B     ZLABEL05
ZLABEL22 EQU   *                        EXIT SEQUENCE
         SPACE 2
         MVC   ZOUTAREA(L'ZCAPT02),ZCAPT02   LOAD TERMINAL CAPTION
         PUT   ZPRINT,ZOUTAREA          PRINT CAPTION
         CLOSE ZPRINT                   CLOSE PRINT FILE
         SPACE 2
         L     13,ZSAVE+4               RETURN
         RETURN (14,12),,RC=0                  TO SYSTEM
         SPACE 2
ZDSN1    DC    C'**SYSUT1.*.*.*.*.*.*.*.*.*.*'
         DC    C'**SYSUT2.*.*.*.*.*.*.*.*.*.*'
         DC    C'**SYSUT3.*.*.*.*.*.*.*.*.*.*'
ZDSN2    EQU   *
ZCAPT02  DC    C'0  UTILITY ENDS NORMALLY'
ZLABEL19 MVC   ZDSN0(0),0(2)
         SPACE 2
ZLABEL05 EQU   *
         SPACE 2
         SPACE 2
         EX    3,ZLABEL19               LOAD DSN
         MVC   ZVOLNUM(6),0(4)          LOAD VOLUME SERIAL
         SPACE 2
         OBTAIN ZOBTAIN                 LOCATE THE F1 DSCB IN VTOC
         SPACE 2
         B     ZLABEL07(15)             ROUTING SWITCH
         SPACE 2
         CNOP  0,4
ZLABEL07 B     ZLABEL08                 RC=0
         B     ZLABEL09                 RC=4
         B     ZLABEL21            RC=8
         B     ZLABEL11                 RC=12
         B     ZLABEL12                 RC=16
         B     ZLABEL13                 RC=20
         SPACE 2
         ABEND 100,DUMP
ZLABEL20 MVC   ZOUTAREA+ZCAPT04(0),ZDSN0         LOAD DSN
         SPACE 2
ZOBTAIN  CAMLST SEARCH,ZDSN0,ZVOLNUM,ZWORKAR
ZDSN0    DC    CL44'A'
ZVOLNUM  DC    CL6'A'
ZWORKAR  DS    0D
         DS    350C
         SPACE 2
ZLABEL08 SR    0,0
         MVC   ZVOLUME1(4),6(4)         LOAD DEVICE TYPE
         MVC   ZVOLUME2(6),0(4)         LOAD VOL-SER
         SCRATCH ZSCRATCH
         SPACE 2
         B     ZLABEL14(15)
         SPACE 2
ZLABEL14 B     ZLABEL15                 RC=0
         B     ZLABEL09                 RC=4
         B     ZLABEL17            RC=8
         ABEND 101,DUMP
ZSCRATCH CAMLST SCRATCH,ZDSN0,,ZVOLUME3,,OVRD
ZVOLUME3 DC    H'1'
ZVOLUME1 DC    X'00000000'
ZVOLUME2 DC    C'ABCDEF'
         DC    H'0'
         SPACE 2
ZLABEL15 MVC   ZOUTAREA(ZCAPT06),ZCAPT03     LOAD CAPTION
         MVI   ZCAPT03,X'40'                 LOAD ASA
         EX    3,ZLABEL20
         MVC   ZOUTAREA+ZCAPT05(6),0(4)           LOAD VOL-SER
         PUT   ZPRINT,ZOUTAREA
         MVI   ZOUTAREA,X'40'
         MVC   ZOUTAREA+1(ZCAPT06),ZOUTAREA
         SPACE 2
ZLABEL21 EQU   *
         C     3,=F'27'            END OF LENGTH CYCLE ?
         BE    ZLABEL10                      YES - MOVE TO NEXT DSN
         SPACE 2
         B     ZLABEL18                      NO - MOVE TO NEXT LENGTH
         SPACE 2
ZCAPT03  DC    C'1 '
ZCAPT04  EQU   *-ZCAPT03
         DC    30X'40'
         DC    C'HAS BEEN SCRATCHED FROM VOLUME '
ZCAPT05  EQU   *-ZCAPT03
         DC    6X'40'
ZCAPT06  EQU   *-ZCAPT03
ZCAPT07  DC    C'  VOLUME '
ZCAPT08  EQU   *-ZCAPT07
         DC    7X'40'
         DC    C'IS NOT MOUNTED,PROCESSING CONTINUED ON NEXT VOLUME'
ZCAPT09  EQU   *-ZCAPT07
         SPACE 2
ZCAPT10  DC    C'  OBTAIN MACRO ERROR CONDITION '
ZCAPT11  EQU   *-ZCAPT10
         DC    3X'40'
         DC    C'HAS BEEN ENCOUNTED ON VOLUME '
ZCAPT12  EQU   *-ZCAPT10
         DC    7X'40'
         DC    C'PROCESSING CONTINUES ON NEXT VOLUME'
ZCAPT13  EQU   *-ZCAPT10
ZCAPT14  DC    C'  WHILST SCRATCHING '
ZCAPT15  EQU   *-ZCAPT14
         DC    29X'40'
         DC    C'FROM'
ZCAPT16  EQU   *-ZCAPT14
         DC    7X'40'
         DC    C'A RETURN CODE OF '
ZCAPT17  EQU   *-ZCAPT14
         DC    C'X WAS ENCOUNTED'
ZCAPT18  EQU   *-ZCAPT14
ZCAPT19  DC    C'         PROCESSING CONTINUES AS IF SCRATCH HAD BEEN SU
               UCCESSFULL'
         SPACE 2
ZCODES   DC    X'000103040506'
         DC    X'F0F1F3F4F5F6'
         CNOP  0,4
ZLABEL09 EQU   *
         MVC   ZOUTAREA(ZCAPT09),ZCAPT07
         MVC   ZOUTAREA+ZCAPT08(6),ZVOLNUM
ZLABEL24 LA    10,ZLABEL06
ZLABEL23 EQU   *
         PUT   ZPRINT,ZOUTAREA
         MVI   ZOUTAREA,X'40'
         MVC   ZOUTAREA+1(L'ZOUTAREA-1),ZOUTAREA
         BR    10
ZLABEL11 EQU   *
         MVC   ZWORKAR(2),=X'F1F2'
ZLABEL16 MVC   ZOUTAREA(ZCAPT13),ZCAPT10
         MVC   ZOUTAREA+ZCAPT11(2),ZWORKAR
         MVC   ZOUTAREA+ZCAPT12(6),ZVOLNUM
         B     ZLABEL24
ZLABEL12 EQU   *
         MVC   ZWORKAR(2),=X'F1F6'
         B     ZLABEL16
ZLABEL13 EQU   *
         MVC   ZWORKAR(2),=X'F2F0'
         B     ZLABEL16
         SPACE 2
ZLABEL17 EQU   *
         ST    2,ZWORKAR+4
         LA    2,ZCODES
ZLABEL26 CLC   ZVOLUME2+7(1),0(2)
         BE    ZLABEL25
         LA    2,1(2)
         B     ZLABEL26
ZLABEL25 EQU   *
         MVC   ZOUTAREA(ZCAPT18),ZCAPT14
         MVC   ZOUTAREA+ZCAPT15(28),ZDSN0
         MVC   ZOUTAREA+ZCAPT16(6),ZVOLUME2
         MVC   ZOUTAREA+ZCAPT17(1),6(2)
         L     2,ZWORKAR+4
         PUT   ZPRINT,ZOUTAREA
         MVI   ZOUTAREA,X'40'
         MVC   ZOUTAREA+1(ZCAPT18-1),ZOUTAREA
         MVC   ZOUTAREA(L'ZCAPT19),ZCAPT19
         LA    10,ZLABEL21
         B     ZLABEL23
         SPACE 2
ZSAVE    DS    18F
ZVOLIST EQU *
         DC    C'ZD0001'
         DC    X'30502009'
         DC    C'ZD0002'
         DC    X'30502009'
         DC    C'MD2020'
         DC    X'30502009'
         DC    C'MD2021'
         DC    X'30502009'
         DC    10X'4B'
         DC    10X'4B'
         DC    10X'4B'
         DC    10X'4B'
         DC    10X'4B'
         DC    10X'4B'
ZEND     DC    10X'4B'
         DS    0F
ZWORK    DS    CL80
         CNOP  0,4
ZCOUNT   DC    PL2'0'
ZDEVTYPE DC    X'30002001'         2311
         DC    X'30C02008'         2314
         DC    X'30002005'         2321
         DC    X'30402002'         2301
         DC    X'30002004'         2302
         DC    X'30002003'         2303
         DC    X'30502009'
ZCAPT01  EQU   *
         DC    C'1 SYNTAX ERROR ON DATA CARD '
ZMARK1   EQU   *-ZCAPT01
ZEDWRD   DC    X'40202120'
ZMARK26  EQU   *
         DC    C' PROCESSING IGNORED ON THIS VOLUME'
ZMARK3   EQU   *-ZCAPT01
ZOUTAREA DC    CL121' '
ZCRDIN   DCB   DDNAME=SYSIN,MACRF=GM,DSORG=PS,BLKSIZE=80,              X
               RECFM=F,EODAD=ZLABEL00
ZPRINT   DCB   DDNAME=SYSPRINT,MACRF=PM,RECFM=FA,BLKSIZE=121,DSORG=PS
         SPACE 2
         END
