***********************************************************************
*                                                                     *
*  MODULE SSIUPDAT (IN CONJUNCTION WITH SSICREAT) UPDATES THE DATE    *
*  AREA (VIA TIME MACRO) IN THE DUMMY SETSSI CARD.  AS SET UP BELOW   *
*  THE JCL REQD IS:-                                                  *
*                                                                     *
*                                                                     *
*    //UPDAT EXEC PGM=SSIUPDAT                                        *
*    //ASSM DD DISP=SHR,DSN=SYS1.SSILIB(ASSMBLRF)     ASSEMBLER 'F'   *
*    //FORT DD DISP=SHR,DSN=SYS1.SSILIB(FORTRANG)     FORTRAN 'G'     *
*    //PLV1 DD DISP=SHR,DSN=SYS1.SSILIB(PL1V52B)      PL/1 VERS 5     *
*    //RPGE DD DISP=SHR,DSN=SYS1.SSILIB(RPGE)         RPG 'E'         *
*    //PLOP DD DISP=SHR,DSN=SYS1.SSILIB(PL1OPTIM)     PL/1 OPTIMISER  *
*                                                                     *
*                                                                     *
*  THE MEMBERS ARE UPDATED INPLACE.  THE MODULE/PROCEDURE CAN BE      *
*  STARTED VIA THE CONSOLE AT IPL TIME, OR IN OUR INSTALLATION IT IS  *
*  PART OF THE 'STRTHASP' PROCEDURE, IE:-                             *
*                                                                     *
*    JCL AS ABOVE PLUS:-                                              *
*    //HASP EXEC PGM=HASP,TIME=1439                                   *
*    //OLAYLIB DD DSN=....                                            *
*    // START INIT.P0,,,H                                             *
*                                                                     *
*  USE OF THE LIBRARY MEMBER WITHIN THE L.E. STEP:-                   *
*                                                                     *
*    //LKED EXEC  PGM=IEWL,....                                       *
*    //SYSPRINT                                                       *
*    //SYSUT1                                                         *
*    //SYSLIN DD DSN=SYS1.SSILIB(PL1V52B),DISP=SHR                    *
*    //       DD DSN=&&LOADSET,DISP=...                               *
*    //       DD DSN=SYS1.PL1LIB,DISP=SHR                             *
*                                                                     *
* CONTACT:- RAY BARBER,ROWNTREE-MACKINTOSH(YORK), TEL 0904-53071 X476 *
*                                                                     *
***********************************************************************
         SPACE 3
SSIUPDAT CSECT
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         LR    11,13
         LA    13,SAVE1
         ST    13,8(11)
         ST    11,4(13)
*
         SPACE 2
         TIME  DEC
         SPACE 2
         ST    1,DUBWD+4
         TM    DUBWD+5,X'01'
         BO    *+16
         TM    DUBWD+5,X'12'
         BM    *+8
         MVI   YEARTAB+4,29
         MVC   YEAR-1(3),EDMASK
         ED    YEAR-1(3),DUBWD+5        SET UP YEAR
         XC    DUBWD(6),DUBWD          CLEAR ALL BUT JULIAN DATE
         SR    0,0                     CLEAR
         CVB   1,DUBWD                 CONVERT JULIAN DATE TO BINARY
         LA    4,YEARTAB-4             PREPARE TO SCAN CONV TABLE
PRIDATE  SR    1,0
         LA    4,4(4)
         IC    0,0(4)
         CR    0,1
         BL    PRIDATE
         CVD   1,DUBWD
         MVO   DUBWD(2),DUBWD+6(2)     SHIFT FOR EDIT
         MVC   DAY-1(3),EDMASK         SET UP DAY MASK
         ED    DAY-1(3),DUBWD          SET UP DAY
         MVC   MONTH(2),2(4)           SET UP MONTH
         CLI   DAY,X'40'                IS DAY NO LT 10
         BNE   *+8
         MVI   DAY,X'F0'              YES, SET TO ZERO IN TENS DAY POSN
*
         MVC   CONVDATE(6),DAY          MOVE CONVERTED DATE FOR OUTPUT
         SPACE 3
*
*        ASSEMBLER
*
*
         OPEN  (PDSDCB,(UPDAT))         OPEN FOR UPDATE INPLACE
         READ  DECB1,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB1
         MVC   UPDATE+8(6),CONVDATE
         WRITE DECB1,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB1
         CLOSE (PDSDCB)
         SPACE 2
*
*        FORTRAN
*
         MVC   PDSDCB+40(4),=C'FORT'
         OPEN  (PDSDCB,(UPDAT))
         READ  DECB2,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB2
         MVC   UPDATE+8(6),CONVDATE
         WRITE DECB2,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB2
         CLOSE (PDSDCB)
         SPACE 3
*
*        PL/1
*
         MVC   PDSDCB+40(4),=C'PLV1'
         OPEN  (PDSDCB,(UPDAT))
         READ  DECB3,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB3
         MVC   UPDATE+8(6),CONVDATE
         WRITE DECB3,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB3
         CLOSE (PDSDCB)
         SPACE 3
*
*        RPG
*
         MVC   PDSDCB+40(4),=C'RPGE'
         OPEN  (PDSDCB,(UPDAT))
         READ  DECB4,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB4
         MVC   UPDATE+8(6),CONVDATE
         WRITE DECB4,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB4
         CLOSE (PDSDCB)
         SPACE 3
*
*        PL/1 OPTIMISER
*
         MVC   PDSDCB+40(4),=C'PLOP'
         OPEN  (PDSDCB,(UPDAT))
         READ  DECB5,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB5
         MVC   UPDATE+8(6),CONVDATE
         WRITE DECB5,SF,PDSDCB,UPDATE,'S',MF=E
         CHECK DECB5
         CLOSE (PDSDCB)
*
         SPACE 3
         WTO   'SSILIB UPDATED SUCCESSFULLY'
         L     13,4(13)
         RETURN (14,12),RC=0
*
*        ERROR ROUTINE
*
ERROR    WTO   'SSILIB ERROR ON UPDATING - HELP !!'
         ABEND 1212,DUMP
         SPACE 3
PDSDCB   DCB   DDNAME=ASSM,DSORG=PO,SYNAD=ERROR,MACRF=(R,W),           *
               EODAD=ERROR,LRECL=80
         READ  DECB1,SF,PDSDCB,UPDATE,'S',MF=L
         READ  DECB2,SF,PDSDCB,UPDATE,'S',MF=L
         READ  DECB3,SF,PDSDCB,UPDATE,'S',MF=L
         READ  DECB4,SF,PDSDCB,UPDATE,'S',MF=L
         READ  DECB5,SF,PDSDCB,UPDATE,'S',MF=L
UPDATE   DS    CL80
RECORD   DS    0CL80
SSI      DC    C' SETSSI '
CONVDATE DS    CL6
DATELANG DC    CL2' 1'
SPARE    DC    CL64' '
DIRENTRY DS    0CL36
ASSEMBLR DC    CL12'ASSMBLRF    '
FORTRAN  DC    CL12'FORTRANG    '
PL1      DC    CL12'PL1V52B     '
RPGE     DC    CL12'RPGE        '
PL1OPTIM DC    CL12'PL1OPTIM    '
DATE     DS    0CL7
FILLER   DS    C
DAY      DS    CL2
MONTH    DS    CL2
YEAR     DS    CL2
DUBWD    DS    D
EDMASK   DC    X'402120'
SAVE1    DS    18F
ALIGN0   DS    0F
YEARTAB  DC    AL1(31),C'001'
         DC    AL1(28),C'002'
         DC    AL1(31),C'003'
         DC    AL1(30),C'004'
         DC    AL1(31),C'005'
         DC    AL1(30),C'006'
         DC    AL1(31),C'007'
         DC    AL1(31),C'008'
         DC    AL1(30),C'009'
         DC    AL1(31),C'010'
         DC    AL1(30),C'011'
         DC    AL1(31),C'012'
         LTORG
         END
