 TITLE '**SKRATCH2 SUPER PUKKA P.D.S. SKRATCHER**'
SKRATCH2 CSECT
         STM   14,12,12(13)
         ST    15,8(0,13)
         ST    13,4(0,15)
         LR    13,15
         USING SKRATCH2,13
         L     1,0(0,1)
         LH    2,0(0,1)
         LTR   2,2
         BZ    NOIPX
         MVC   SYSUT1+40(8),=CL8' '
         BCTR  2,0
         EX    2,DDNMV
NOIPX    LA    1,2
         SVC   11
         ST    1,0(0,13)
         UNPK  12(3,13),1(2,13)
         MVC   YEER,12(13)
         SRL   1,16
         SLL   1,4
         XC    DWWD(4),DWWD
         ST    1,DWWD+4
         OI    DWWD+7,12
         CVB   4,DWWD
         SR    5,5
         SRDL  4,2
         LTR   5,5
         BNZ   NOLEAP
         MVI   MONTHS+3,X'9C'
NOLEAP   SR   5,5
NEXT     LA    4,MONTHS(5)
         SP    DATE+2(2),0(2,4)
         BC    13,STOP
         LA    5,2(0,5)
         B     NEXT
         SPACE
MONTHS   DC    PL2'31,28,31,30,31,30,31,31,30,31,30,31'
         SPACE
STOP     AP    DATE+2(2),0(2,4)
         OI    DATE+3,15
         UNPK  YEAR(3),DATE+2(2)
         MVC   DAY,YEAR+1
         SRA   5,1
         LA    5,1(0,5)
         CVD   5,DWWD
         OI    DWWD+7,15
         UNPK  YEAR(3),DWWD+6(2)
         MVC   MNTH,YEAR+1
         EJECT
         LA    7,1
         MVC   DDNM(8),SYSUT1+40
         OPEN  (SYSIN,,SYSUT2,,SYSPRINT,OUTPUT)
         GETMAIN R,LV=102400
         LR    2,1
         MVC   LPRT(4),SYSPRINT+48
         BAL   9,LINEPRNT
         GETMAIN R,LV=8192
         LR    3,1
         LR    4,3
GETCD    GET   SYSIN
         LA    15,80
CLA      CLI   0(1),64
         BNE   *+16
         LA    1,1(0,1)
         BCT   15,CLA
         B     ERRCD
         CLC   0(7,1),=C'SCRATCH'
         BNE   ERRCD
         LA    1,8(0,1)
         SH    15,=H'8'
         BNH   ERRCD
CLB      CLI   0(1),C','
         BNL   NMFD
         LA    1,1(0,1)
         BCT   15,CLB
         B     GETCD
NMFD     SR    14,14
         LR    8,1
CLC      CLI   1(1),C','
         BNH   ENNM
         LA    14,1(0,14)
         LA    1,1(0,1)
         BCT   15,CLC
ENNM     MVC   0(8,4),=CL8' '
         EX    14,NMVE
         LA    1,2(0,1)
         LA    4,8(0,4)
         SH    15,=H'2'
         BH    CLB
         B     GETCD
DDNMV    MVC   SYSUT1+40(1),2(1)
NMVE     MVC   0(1,4),0(8)
*
         EJECT
ENFSIN   SR    4,3
         BE    TYEUP
         SRL   4,3
         LR    5,3
         LR    6,4
         BCTR  6,0
         LTR   6,6
         BNH   NOSORT
INLUP    LR    8,6
         LA    9,8(0,5)
IMLUP    CLC   0(8,9),0(5)
         BC    11,NEXQ
         XC    0(8,9),0(5)
         XC    0(8,5),0(9)
         XC    0(8,9),0(5)
NEXQ     LA    9,8(0,9)
         BCT   8,IMLUP
         LA    5,8(0,5)
         BCT   6,INLUP
NOSORT   EQU   *
         SR    6,6
         LR    5,2
GETBL    LA    1,READX
         L     15,SYSUT1+48
         BALR  14,15
         L     15,SYSUT2+52
         BALR  14,15
         LA    1,JFCB
         LH    15,8(0,1)
         BCTR  15,0
         BCTR  15,0
         BCTR  15,0
         EX    15,DMVE
         LA    5,1(15,5)
         CLC   0(8,1),=8X'FF'
         BE    ENDIR
         B     GETBL
DMVE     MVC   0(1,5),10(1)
         EJECT
ENDIR    MVC   LINE,LINE-1
         CLOSE SYSUT2
         RDJFCB (SYSUT2)
         OI    JFCB+52,10
         OPEN  (SYSUT2,UPDAT),TYPE=J
         LA    1,READX
         L     15,SYSUT2+48
         BALR  14,15
         L     15,SYSUT2+52
         BALR  14,15
         LA    5,JFCB+10
         LA    11,2
         LR    8,3
         LR    12,2
         LR    10,4
TEST     MVC   LINE+1(8),0(8)
         CLC   0(8,8),0(12)
         BH    NOTREQ
         BL    NOTHER
         LA    8,8(0,8)
SKIP     IC    6,11(0,12)
         SLL   6,27
         SRL   6,26
         LA    12,12(6,12)
          MVC   LINE+11(L'DELMES),DELMES
         BAL   9,LINEPRNT
         BCT   10,TEST
         B     FILREST
NOTHER   MVC   LINE+11(L'NOTMES),NOTMES
         BAL   9,LINEPRNT
         LA    8,8(0,8)
         BCT   10,TEST
FILREST  MVC   MODX+2(2),SMODX
         B     NOTREQ
NOTREQ   IC    6,11(0,12)
         SLL   6,27
         SRL   6,26
         LA    6,12(0,6)
         LA    14,0(6,11)
         CH    14,=H'256'
         BH    NOROOM
         MVC   JFCB(8),0(12)
         BCTR  6,0
         EX    6,USDMV
         LA    11,1(6,11)
         CLC   0(8,12),=8X'FF'
         BE    ENDIRW
         LA    5,1(6,5)
         LA    12,1(6,12)
MODX     B     TEST+6
USDMV    MVC   0(1,5),0(12)
SMODX    DC    S(NOTREQ)
NOROOM   STH   11,JFCB+8
         L     15,SYSUT2+48
         LA    1,READX
         MVI   READX+5,32
         BALR  14,15
         L     15,SYSUT2+52
         BALR  14,15
FRED     NOP   TYEUP
         MVI   READX+5,128
         L     15,SYSUT2+48
         BALR  14,15
         L     15,SYSUT2+52
         BALR  14,15
         LA    11,2
         LA    5,JFCB+10
         MVC   JFCB(8),0(12)
         B     NOTREQ
ENDIRW   MVI   FRED+1,X'F0'
         B     NOROOM
TYEUP    EQU   *
         MVC   LINE,LINE-1
         MVC   LINE+1(L'ENJ),ENJ
         BAL   9,LINEPRNT
ENDOF    EQU   *
         CLOSE (SYSIN,,SYSPRINT,,SYSUT2,)
         L     13,4(0,13)
         RETURN (14,12),,RC=0
ENF      MVC   LINE(L'BADIR),BADIR
         BAL   9,LINEPRNT
         B     ENDOF
S2ERR    LA    3,LINE
         SYNADAF ACSMETH=BSAM
         MVC   13(78,3),50(1)
         SYNADRLS
         MVC   LINE(12),=C'-SYNAD EXIT.'
         BAL   9,LINEPRNT
         B     ENDOF
ERRCD    EQU   *
         MVC   LINE,LINE-1
         MVC   LINE+1(L'INV),INV
         MVC   LINE+1+L'INV(80),0(1)
         MVC   LINE+82+L'INV(8),=C'IGNORED.'
         BAL   9,LINEPRNT
         B     GETCD
LINEPRNT BCT   7,PUTLL
         AP    PAGNO,=PL2'1'
         MVC   DWWD(8),PATTN
         ED    DWWD(8),PAGNO
         MVC   HEAD+117(2),DWWD+6
         PUT   SYSPRINT,HEAD
         LA    7,40
PUTLL    LM    15,1,LPRT
         BALR  14,15
         BR    9
PAGNO    DC    PL4'0'
PATTN    DC    X'4020202020202120'
 EJECT
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=ENFSIN
*
 EJECT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,BLKSIZE=968,*
               LRECL=121
 EJECT
SYSUT1   DS    0F
SYSUT2   DCB   DSORG=PS,MACRF=(R,W),KEYLEN=8,DDNAME=SYSUT1,EXLST=JFCBL,*
               EODAD=ENF,                                              *
               SYNAD=S2ERR,BLKSIZE=256,LRECL=256,RECFM=F,NCP=1
 EJECT
LPRT     DC    F'0'
         DC    A(LINE)
         DC    A(SYSPRINT)
YEAR     DC    F'0'
DWWD     DC    D'0'
*
         READ  READX,SF,SYSUT2,JFCB,'S',MF=L
*
JFCBL    DC    X'87'
         DC    AL3(JFCB)
JFCB     DC    68F'0'
ENJ      DC    C'0NORMAL END OF JOB.'
BADIR    DC    C'DIRECTORY DOES NOT END WITH X''FFFFFFFF''. SCRATCH ATTE
               EMPT TERMINATED.'
INV      DC    C'INVALID CARD '
NOTMES   DC    C'NOT IN DIRECTORY.'
DELMES   DC    C'NOW DELETED.     '
HEAD     DC    CL120'1     SKRATCH'
         ORG   HEAD+27
         DC    C'**PUKKA SUPER FAST P.D.S. SKRATCHER MARK 2**'
         ORG   HEAD+88
         DC    C'DATE '
DAY      DC    C'XX'
         DC    C'/'
MNTH     DC    C'XX'
         DC    C'/'
YEER     DC    C'XX'
         ORG   HEAD+112
         DC    C'PAGE'
         ORG   HEAD+120
         DC    C' '
LINE     DC    CL120'0DATA SET CONTAINING MEMBERS TO BE SCRATCHED IS DEF
               FINED BY DDCARD BEARING DDNAME SYSUT1  .'
DDNM     EQU   LINE+81
DATE     EQU   SKRATCH2
         LTORG
         ORG   SKRATCH2+4092
         DC    F'0'
         END
