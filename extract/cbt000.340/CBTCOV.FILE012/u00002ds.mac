LINK     TITLE 'PL1LINK - PRIVATE MACRO DEFINITIONS'
         MACRO
         PL1REGS &LEVEL=,&EQUATES=
         GBLB  &PL1OPT,&PL1ASM
         LCLA  &LA1,&LA2,&LA3,&LA4,&LA5
         SPACE
         AIF   ('&LEVEL' EQ 'OPT').L1
         AIF   ('&LEVEL' EQ 'F').L2
         AIF   ('&LEVEL' EQ 'ASM').L5
         IHBERMAC 180,LEVEL,F
         AGO   .L2
.L5      ANOP
&PL1ASM  SETB  1
.L2      AIF   ('&EQUATES' EQ 'NO').L4
&LA1     SETA  2
&LA2     SETA  3
&LA3     SETA  4
&LA4     SETA  10
&LA5     SETA  11
*   REGISTER EQUATES FOR F COMPILER
         AGO  .L3
.L4      MEXIT
.L1      ANOP
&PL1OPT  SETB  1
         SPACE
         AIF   ('&EQUATES' EQ 'NO').L4       /
&LA1     SETA  10
&LA2     SETA  11
&LA3     SETA  4
&LA4     SETA  2
&LA5     SETA  3
*   REGISTER EQUATES FOR OPTIMISING COMPILER
.L3      SPACE
R0       EQU   0
R1       EQU   1
RA       EQU   1
RB       EQU   &LA1
RC       EQU   &LA2
RD       EQU   &LA3
RE       EQU   5
RF       EQU   6
RG       EQU   7
RH       EQU   8
RI       EQU   9
RJ       EQU   &LA4
RX       EQU   &LA5
WR       EQU   &LA5
PR       EQU   12
DR       EQU   13
LR       EQU   14
BR       EQU   15
         EJECT
         MEND
    SPACE 3
         MACRO
&NAME    PL1STRG &FROM,&LN,&LEVEL=
.* LEAVES REGISTER &LN CONTAINING LENGTH(STRING)
.*                 &FROM CONTAINING A(STRING)
         GBLB  &PL1OPT,&PL1ASM
         AIF   ('&LEVEL' EQ '').L0
         IHBERMAC 157,PL1STRG,LEVEL
.L0      AIF  (&PL1OPT).L1
         AIF   (&PL1ASM).L2
&NAME    LH    &LN,6(&FROM)            FIELD LENGTH
         L     &FROM,0(&FROM)          A(STRING)
         MEXIT
.L1      ANOP
&NAME    TM    6(&FROM),X'80'
         BZ    IHB&SYSNDX              FIXED STRING
         L     &FROM,0(&FROM)          VARYING STRING
         LH    &LN,0(&FROM)            FIELD LENGTH
         LA    &FROM,2(&FROM)          A(STRING)
         B     IHB&SYSNDX.A
IHB&SYSNDX LH  &LN,4(&FROM)            FIELD LENGTH
         L     &FROM,0(&FROM)          A(STRING)
IHB&SYSNDX.A EQU *
.L3      MEXIT
.L2      AIF   ('&NAME' EQ '').L3
&NAME    DS    0H
         MEND
         TITLE                            'PL1LINK - ROUTINE TO LINK TO'
                AN OS UTILITY OR OTHER LOAD MODULE FROM A PL/1 PROGRAM'
*   THIS ROUTINE IS CALLED BY A PL/1 PROGRAM, AND CONVERTS THE
*   PARAMETERS PASSED TO IT INTO THE FORM REQUIRED BY THE LINK MACRO.
*   THE PL/1 CALL IS:-
*             CALL PL1LINK(2-5 PARAMETERS);
*   THE PARAMETERS ARE:-
*   1) PROGRAM NAME - A CHARACTER STRING NOT EXCEEDING EIGHT CHARACTERS
*        AS CODED ON AN EXEC CARD. IT MAY BE PADDED ON THE RIGHT WITH
*        BLANKS IF DESIRED. THE PROGRAM MUST BE IN A LIBRARY  DEFINED
*        BY A JOBLIB OR STEPLIB UNLESS IT IS IN ONE OF THE LINKLIB
*        LIBRARIES.
*   2) RETURN CODE - FULL WORD FIXED BINARY. THE RETURN CODE FROM THE
*        PROGRAM INVOKED IS PASSED BACK TO THE PL/1 PROGRAM IN THIS
*        FIELD. IF IT IS OMITTED, THE PROGRAM WILL ABEND WITH A USER
*        CODE OF 16, AND A MESSAGE WILL APPEAR IN CORE, AND ALSO IN
*        REGISTERS 2-7.
*   3) PARAMETERS (OPTIONAL) - A CHARACTER STRING NOT EXCEEDING 100
*        CHARACTERS, CONTAINING THE PARAMETERS THAT WOULD BE PASSED
*        TO THE PROGRAM ON AN EXEC CARD. THIS PARAMETER MAY BE OMITTED
*        UNLESS IT IS DESIRED TO PASS PARAMETERS 4 OR 5 : IF THE FIRST
*        CHARACTER IS A BLANK, THE PARAMETER WILL BE IGNORED.
*   4) ALTERNATE DDNAMES (OPTIONAL - FOR IBM UTILITIES OR LINKAGE EDIT-
*        OR ONLY) - A STRUCTURE AS FOLLOWS:-
*        FOR UTILITIES:              FOR THE LINKAGE EDITOR:
*        DCL 1 DD NAMES STATIC,      DCL 1 DDNAMES STATIC,
*              2 SYSIN    CHAR(8),         2 SYSLIN   CHAR(8),
*              2 SYSPRINT CHAR(8),         2 MEMBER   CHAR(8),
*              2 SYSUT1   CHAR(8),         2 SYSLMOD  CHAR(8),
*              2 SYSUT2   CHAR(8),         2 SYSLIB   CHAR(8),
*              2 SYSUT3   CHAR(8),         2 SYSPRINT CHAR(8),
*              2 SYSUT4   CHAR(8);         2 SYSUT1   CHAR(8);
*        ALTERNATIVELY, A 48-CHARACTER STRING OF THE SAME PATTERN MAY
*        BE PASSED. FULLER DETAILS ARE IN APPENDIX B OF THE UTILITIES
*        MANUAL AND APPENDIX C OF THE LINKAGE EDITOR MANUAL, BUT NOTE
*        THAT IF IT IS DESIRED TO CHANGE ANY DDNAME, ALL 6 MUST BE
*        CODED. THOSE NOT BEING CHANGED MUST BE EITHER EXPLICITLY CODED
*        WITH THE DEFAULT NAME (E.G. SYSIN) OR A STRING OF BINARY ZEROS
*        (USING THE BUILT-IN FUNCTION LOW). THE PADDING ZEROS MENTIONED
*        IN THE MANUALS ARE SUPPLIED BY THIS ROUTINE, AND SHOULD NOT BE
*        CODED.
*        THIS PARAMETER MAY BE OMITTED UNLESS IT IS DESIRED TO PASS
*        PARAMETER 5 : IF THE FIRST CHARACTER IS A BLANK, THE PARAMETER
*        WILL BE IGNORED.
*   5) PAGE NUMBER (OPTIONAL - FOR IBM UTILITIES ONLY) - A NUMERIC
*        CHARACTER STRING OF LENGTH 4, CONTAINING THE NUMBER TO BE USED
*        ON THE FIRST PAGE OF THE UTILITY SYSPRINT OUTPUT. IF THIS
*        PARAMETER IS OMITTED, PAGE NUMBERING WILL START AT 1.
*
*        FOR THE OPTIMISING COMPILER, THE 'PL1REGS' MACRO AT THE
*        BEGINNING OF THE CSECT SHOULD BE RECODED :-
*                  PL1REGS LEVEL=OPT
*        AND THIS MODULE CAN BE LINKEDITED WITH AN ALIAS OF PLILINK.
*
*   THIS MODULE IS REENTERABLE BUT NOT REFRESHABLE
         EJECT
PL1LINK  CSECT
         ENTRY PLILINK
         SPACE 3
  PL1REGS LEVEL=F
PLILINK  SAVE  (14,12),,*
         LR    RJ,BR
         LR    RE,R1                   RE->CALLING PROG PARM LIST
         USING PL1LINK,RJ
         USING PARMIN,RE
         USING PARMOUT,DR
         GETMAIN R,LV=296
         ST    DR,4(R1)
         ST    R1,8(DR)
         LR    DR,R1
         SPACE
* INITIALISE SAVEAREA AND LINK PARAMETER LISTS
         SR    RA,RA                   RA=F'0'
         ST    RA,SAVE
         MVI   SAVE,X'80'              DSA FLAG FOR PL/1
         ST    RA,SAVE+8
         MVC   SAVE+12(60),SAVE+11
         ST    RA,OPTIONS-2
         MVC   OPTIONS+2(192),OPTIONS+1
         LA    RA,4
         ST    RA,HEADING-2
         L     RI,BLANKS               RI=X'40404040'
         ST    RI,PROGN
         ST    RI,PROGN+4
         LA    RA,OPTIONS
         ST    RA,AOPTIONS
         MVI   AOPTIONS,X'80'
         LA    RA,LENGTH
         ST    RA,ALENGTH
         MVI   ALENGTH,X'80'
         LA    RA,HEADING
         ST    RA,AHEADING
         MVI   AHEADING,X'80'
         SPACE
* SET UP LINK PARAMETER LISTS
         TM    PARM1,X'80'             PROGN ONLY PARM?
         BO    ABEND
         L     RC,PARM1                A(DV.UTILITY NAME)
         PL1STRG RC,RD
         C     RD,EIGHT
         BNH   VALID
         L     RD,EIGHT                TRUNCATE NAME
VALID    BCTR  RD,0
         EX    RD,MOVENAME
         L     RC,PARM3                A(DV.PARM)
         TM    PARM2,X'80'             RETURN CODE LAST PARM?
         BO    NOPARMS
         PL1STRG RC,RD
         LTR   RD,RD
         BZ    DDALT
         CLI   0(RC),C' '
         BE    DDALT
         STH   RD,OPTIONS
         BCTR  RD,0
         EX    RD,MOVEPARM
DDALT    EQU   *
         TM    PARM3,X'80'             PARAMETERS LAST PARM?
         BO    NOPARMS
         MVI   AOPTIONS,X'00'
         L     RC,PARM4                A(DV.ALTERNATE DDNAMES)
         L     RC,0(RC)                A(ALTERNATE DDNAMES)
         CLI   0(RC),C' '
         BE    PAGE
         CLC   PROGN(4),=C'IEWL'
         BE    LKED
         MVC   SYSIN(16),0(RC)         MOVE ALTERNATE
         MVC   SYSUT1(32),16(RC)          DD NAMES
         LA    RC,88                   FIELD LENGTH
STORLGTH STH   RC,LENGTH
PAGE     TM    PARM4,X'80'             ALTDD      LAST PARM?
         BO    NOPARMS
         MVI   ALENGTH,X'00'
         L     RC,PARM5                A(DV.PAGE NO)
         L     RC,0(RC)                A(PAGE NO)
         MVC   HEADING+2(4),0(RC)      MOVE PAGE NO
         SPACE
*  RECORD CURRENT TOP OF LOAD LIST
NOPARMS  EQU   *
         L     RC,16                   RC->CVT
         TM    116(RC),X'10'           MVT?
         BNO   NOTMVT
         MVI   MVTPATH+1,X'00'
NOTMVT   EQU *
         L     RC,0(RC)                RC->PTR->TCB
         L     RC,4(RC)                RC->TCB
         LA    RC,36(RC)               RC->TCBLLS FIELD
         L     RB,0(RC)                RB->TOP OF LOAD LIST
         SPACE
* REMOVE UTILITY FROM PL/1 ERROR-HANDLING ENVIRONMENT
         SPIE
         LR    RF,R1                   STORE A(PICA) IN RF
         SPACE
         LA    RD,PROGN
         LINK  EPLOC=(RD),MF=(E,AOPTIONS)
         L     RE,PARM2                A(RETURN CODE)
         ST    BR,0(RE)                STORE UTILITY RETURN CODE
         SPACE
* RESTORE PL/1 ERROR-HANDLING ENVIRONMENT
         SPIE  MF=(E,(RF))
         SPACE
* NOW DELETE ANY RBS ADDED TO LOAD LIST BY LINKED PROGRAM
LOOP     L     RD,0(RC)                RD->TOP OF LOAD LIST
         CR    RD,RB                   SAME AS PRE-LINK TOP?
         BE    EXIT                    YES
MVTPATH  B     *+8                     CHANGED TO NOP IF MVT
         L     RD,4(RD)                RD->CDE ENTRY(MVT ONLY)
         LA    R0,8(RD)
         DELETE EPLOC=(R0)
         B     LOOP
EXIT     EQU   *
         SPACE
* FREE PARAMETER AREA AND RETURN
         LR    R1,DR
         L     DR,SAVE+4
         FREEMAIN R,LV=296,A=(R1)
         RETURN (14,12),T
         SPACE
LKED     MVC   SYSLIN(32),0(RC)
         MVC   SYSPRINT(8),32(RC)
         MVC   SYSUT1(8),40(RC)
         LA    RC,64                   FIELD LENGTH
         B     STORLGTH
ABEND    LM    RB,RG,ERRMSG
         OR    RB,RI                   )
         OR    RC,RI                   )
         OR    RD,RI                   ) MAKE
         OR    RE,RI                   ) MESSAGE
         OR    RF,RI                   ) PRINTABLE
         OR    RG,RI                   )
         STM   RB,RG,ERRMSG            )
         ABEND 16,DUMP
MOVEPARM MVC   OPTIONS+2(0),0(RC)
EIGHT    DC    F'8'
ERRMSG   DC    X'9985A3A49995008396848500' 'RETURN CODE '
         DC    X'97819994009489A2A2899587' 'PARM MISSING'
BLANKS   DC    CL4'    '
MOVENAME MVC   PROGN(0),0(RC)
         LTORG
         EJECT
PARMIN   DSECT
PARM1    DS    A
PARM2    DS    A
PARM3    DS    A
PARM4    DS    A
PARM5    DS    A
         SPACE  2
PARMOUT  DSECT
SAVE     DS    18F
AOPTIONS DS    A                       ) LINK
ALENGTH  DS    A                       ) PARAMETER
AHEADING DS    A                       ) LIST
         DS    H
OPTIONS  DS    H
         DS    CL100
         DS    H
LENGTH   DS    H
SYSLIN   DS    2F
MEMBER   DS    2F
SYSLMOD  DS    2F
SYSLIB   DS    2F
SYSIN    DS    2F
SYSPRINT DS    2F
SYSPUNCH DS    2F
SYSUT1   DS    2F
SYSUT2   DS    2F
SYSUT3   DS    2F
SYSUT4   DS    2F
         DS    H
HEADING  DS    H
         DS    F
PROGN    DS    CL8
         END
