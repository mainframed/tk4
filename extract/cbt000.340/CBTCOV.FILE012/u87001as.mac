+ THIS FAIRLY TRIVIAL UTILITY WILL NOT EXECUTE UNDER MVS/XA DUE TO THE
+ MODE OF LOCATING UCB'S. THE MKB VTOC LIST UTILITY SEEMS TO BE
+ IN VERY COMMON USE IN VARIOUS FORMS.
+
+ WE USE IT AS A FLASH REPLACEMENT.
+
NCZ93003 TITLE 'ALLOCATE ALL ONLINE DASD (MKB VTOC LIST WRAPAROUND)    *
               - PROGRAM DOCUMENTATION'
*****                                                             *****
*                                                                     *
* ID:          NCZ93003 - ALLOCATE ALL ONLINE DASD VOLUMES            *
*                                                                     *
* NCZ93003.1 PROGRAM DESCRIPTION                                      *
*                                                                     *
* THIS PROGRAM SCANS UCB'S AND ALLOCATES ALL ONLINE DASD VOLUMES FOR  *
* INVOKING THE MKB VTOC LISTING PROGRAM.                              *
*                                                                     *
* EACH ONLINE DASD VOLUME IS ALLOCATED EQUIVALENT TO A JCL DD         *
* STATEMENT OF THE FORM:                                              *
* //VOLUME.. DD UNIT=3350,DISP=OLD,VOL=SER=......                     *
*                                                                     *
* SEE THE DOCUMENTATION FOR MKBVTOCL                                  *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93003.2 USER INSTRUCTIONS                                        *
*                                                                     *
* JCL:                                                                *
* ----                                                                *
*                                                                     *
* //S1      EXEC PGM=NCZ93003,PARM='.......'                          *
* //STEPLIB  DD  DISP=SHR,DSN=MAC1.LOAD     <--- FOR NCZ93003         *
* //         DD  DISP=SHR,DSN=MKB1.LOAD     <--- FOR MKB VTOC LISTER  *
* //SYSPRINT DD  SYSOUT=*                                             *
*                                                                     *
* THE PARM INFORMATION WILL BE PASSED TO THE VTOC LISTER PREFIXED BY A*
* 'VOLS=..' SUBPARAMETER.                                             *
*                                                                     *
* THE PROGRAM MAY BE EXECUTED AS A STARTED TASK CF. FLASH             *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93003.3 MESSAGES                                                 *
*                                                                     *
* NONE
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93003.4 ABEND CODES                                              *
*                                                                     *
* USER 0001  -  DYNAMIC ALLOCATION FAILED - ERROR AND INFO CODES IN R3*
*                                                                     *
*****                                                             *****
         EJECT
*****                                                             *****
*                                                                     *
* NCZ93003.5 IMPLEMENTATION METHOD                                    *
*                                                                     *
* MOVE TO MAC1.ASM AND PASMAL.                                        *
*                                                                     *
* IF THE VTOC LISTER IS EXECUTING AS AN AUTHORISED PROGRAM THEN THIS  *
* PROGRAM MUST BE LINK-EDITED WITH AC(1) INTO AN AUTHORISED LIBRARY.  *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93003.6 AMENDMENT HISTORY                                        *
*                                                                     *
* MAINTAIN THE RECORD BELOW.                                          *
*                                                                     *
* CHANGE HISTORY:                                                     *
* DATE      INITS VERSION COMMENTS                                    *
*                                                                     *
* 02 MAR 83 RH    V01     CREATED                                     *
*                                                                     *
* SUGGESTED ENHANCEMENTS:                                             *
*                                                                     *
* RECORD DETAILS BELOW.                                               *
*                                                                     *
* 02 MAR 83 RH  : THIS PROGRAM ASSUMES THAT THE DASD IS 3350 WITHOUT  *
*                 CHECKING                                            *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ93003.7 MODULARISATION DETAILS                                   *
*                                                                     *
* SCAN UCB CHAIN USING UCB LOOKUP TABLE.                              *
* ALLOCATE ANY ONLINE 3350 DEVICE.                                    *
* LINK TO MKB VTOC LIST WITH PARM.                                    *
*                                                                     *
* USES DYNALLOC, LINK.                                                *
*                                                                     *
*****                                                             *****
         TITLE 'ALLOCATE ALL ONLINE DASD (MKB VTOC LISTER WRAPAROUND)  *
               - PROGRAM PROLOGUE'
*****                                                             *****
*                                                                     *
* ID:          NCZ93003 - ALLOCATE ALL ONLINE DASD                    *
*                                                                     *
* ENTRY INTFCE: FROM OS WITH PROGRAM PARM.                            *
*                                                                     *
* EXIT  INTFCE: RETURN TO OS                                          *
*                                                                     *
* RETURN CODES: PASSED FROM VTOC LISTER                               *
*                                                                     *
*                                                                     *
*****                                                             *****
         TITLE 'ALLOCATE ALL ONLINE DASD (VTOC LIST WRAPAROUND)        *
               - PROGRAM CODE'
         PRINT NOGEN
NCZ93003 MSETUP 13
         ST    R1,OSPARMA          SAVE OS PARAMETER ADDRESS
         SPACE
         USING UCBCMSEG,R10
         USING UCBCMEXT,R9
         L     R11,16                  LOAD CVT ADDRESS
         L     R11,40(R11)             LOAD UCB LOOKUP ADDRESS
         SPACE
UCBLOOP  LH    R10,0(R11)              GET UCB ADDRESS
         CH    R10,=H'0'               VALID UCB ADDRESS?
         BE    NXTUCB                  - NO, IGNORE
         CH    R10,=H'-1'              END OF UCB'S
         BE    MOVEPARM                - YES, FINISHED UCB LOOKUP
         CLI   UCBDVCLS,UCB3DACC       Q - DIRECT ACCESS ?
         BNE   NXTUCB                  - NO, IGNORE
         TM    UCBSTAT,UCBONLI         Q - ONLINE ?
         BZ    NXTUCB                  - NO, IGNORE
         TM    UCBFLA,UCBNRY           Q - READY ?
         BO    NXTUCB                  - NO, IGNORE
         TM    UCBSTAT,UCBUNLD         Q - UNLOADING ?
         BO    NXTUCB                  - YES, IGNORE
         SPACE
* ALLOCATE THIS VOLUME
         MVC   DALVLSER+6(6),UCBVOLI   SET UP VOLUME SERIAL
         AP    VOL#P,=P'1'             INCREMENT VOLUME # FOR DDNAME
         UNPK  VOL#E,VOL#P
         OI    VOL#E+2,X'F0'
         MVC   DALDDNAM+12(2),VOL#E+1
         LA    R1,S99RBP1              POINT TO DYNAMIC ALLOC PARMS
         DYNALLOC
         LTR   R15,R15                 Q - OK ?
         BZ    NXTUCB                  Y
         L     R3,S99ER1
         ABEND 1,DUMP
         SPACE
NXTUCB   DS    0H
         LA    R11,2(R11)              POINT UP LOOKUP TABLE
         B     UCBLOOP
         EJECT
MOVEPARM DS    0H
         MVC   PARM+5(2),VOL#E+1   SET UP VOLUME COUNT FOR PARM
         L     R2,OSPARMA
         L     R2,0(R2)            -> OS PARM STRING
         LH    R3,0(R2)            BYTE COUNT
         LTR   R3,R3               Q - ANY PARM ?
         BZ    NOPARM              N
         BCTR  R3,0                Y GET M/C LENGTH
         EX    R3,EXMVC            MOVE OS PARM TO OUR PARM AREA
         LA    R3,1(R3)            RESTORE REAL LENGTH
         SPACE
NOPARM   EQU   *
         LA    R3,8(R3)            INCREMENT LENGTH FOR VOLUME COUNT
         STH   R3,PARML
         LA    R1,PARMLA
LINK     EQU   *
         LINK  EP=VSAMLIST         GO TO VTOC LISTER
         MCLEAR RC=(15)
         TITLE 'ALLOCATE ALL ONLINE DASD (MKB VTOC LIST WRAPAROUND)    *
                - STORAGE AND DSECTS'
         LTORG
         DS    0H
EXMVC    MVC   PARM+8(0),2(R2)
VOL#P    DC    PL2'0'              VOLUME NUMBER (PACKED)
VOL#E    DS    CL3                 VOLUME NUMBER (EBCDIC)
OSPARMA  DS    F                   -> OS PROGRAM PARM
PARMLA   DC    X'80',AL3(PARML)    -> OUR PARM
         DS    0H
PARML    DS    H                   PARM FOR MKBVTOCL
PARM     DC    CL100' '
         ORG   PARM
         DC    C'VOLS=..,'
         ORG
         DS    0F
S99RBP1  DC    X'80',AL3(S99RB1)   DYNAMIC ALLOCATE LIST
S99RB1   DC    X'14',X'01',X'0000' LENGTH,VERB(ALLOCATE)
S99ER1   DC    X'0000',X'0000'     ERROR CODE,INFO CODE
         DC    AL4(S99TUPL1)
         DC    XL8'00'
S99TUPL1 DC    AL4(DALDDNAM)
         DC    AL4(DALSTATS)
         DC    AL4(DALVLSER)
         DC    X'80',AL3(DALUNIT)
         SPACE
DALDDNAM DC    X'0001',X'0001',X'0008',C'VOLUME00'     DDNAME=VOLUME..
DALSTATS DC    X'0004',X'0001',X'0001',X'01'           DISP=OLD
DALVLSER DC    X'0010',X'0001',X'0006',C'......'       VOL=SER=......
DALUNIT  DC    X'0015',X'0001',X'0004',C'3350'         UNIT=3350
         PRINT NOGEN
         DSECT
         IEFUCBOB                      UCB MAPPING MACRO
         END   NCZ93003
