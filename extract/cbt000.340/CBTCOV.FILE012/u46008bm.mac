         MACRO
&SECT    SINIT &SAVAREA,&NAME,&BASE=12
         LCLC  &ENTRY
&ENTRY   SETC  '&NAME'
         AIF   (T'&NAME EQ 'O').M1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         ENTRY &ENTRY
         EJECT
&SECT    CSECT
         CNOP  0,4                     START ON A HALF WORD BOUNDARY
&ENTRY   STM   R14,R12,12(R13)         SAVE ALL REGISTERS EXCEPT 13
         BALR  &BASE,0                 SET UP THE BASE REGISTER
         USING *,&BASE
         ST    R13,&SAVAREA+4          STORE R13 INTO OWN SAVE AREA
         LA    R13,&SAVAREA            LOAD R13 WITH ADDR OF OWN SAVE
         AGO   .M2
.M1      ANOP
&SECT    START
         CNOP  0,4                     START ON A HALF WORD BOUNDARY
         STM   14,12,12(13)            SAVE ALL REGISTERS EXCEPT 13
         BALR  &BASE,0                 SET UP THE BASE REGISTER
         USING *,&BASE
         ST    13,&SAVAREA+4           STORE R13 INTO OWN SAVE AREA
         LA    13,&SAVAREA             LOAD R13 WITH ADDR OF OWN SAVE
.M2      ANOP
         MEND
