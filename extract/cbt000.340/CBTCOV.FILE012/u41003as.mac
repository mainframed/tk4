         MACRO
&NAME    BEGIN &BASE=12,&ID=*
         AIF   (&BASE GT 1 AND &BASE LT 13).A
         MNOTE 8,'INVALID BASE REGISTER GIVEN, GENERATION TERMINATED.'
         MEXIT
.A       ANOP
&NAME    SAVE  (14,12),,&ID
         BALR  &BASE,0             LOAD BASE REGISTER
         USING *,&BASE             ESTABLISH ADDRESSABILITY
         CNOP  0,4                 ALIGN ON FULLWORD BOUNDARY
         BAL   15,*+76             LOAD A(SAVEAREA) AND BRANCH AROUND
         DC    18F'0'              SAVEAREA
         ST    15,8(0,13)          CHAIN SAVEAREAS BACKWARDS
         ST    13,4(0,15)          CHAIN SAVEAREAS FOREWARDS
         LR    13,15               UPDATE CURRENT SAVEAREA POINTER
         MEND
         TITLE 'ZMAN - TEST FOR CURRENT SYS1.MAN DATASET'
         ISEQ  76,80
**
**  AUTHOR :  ALAN FULFORD   DATE SUBMITTED :  25/7/73
**  INSTALLATION :  ALLIED BREWERIES, BURTON   PHONE :  0283-66322  X 3
**
**
**  RC =  0    BOTH FULL,SMF NOT RECORDING
**        4    RECORDING ON Y, X NOT EMPTY
**        8    RECORDING ON X, Y NOT EMPTY
**       12    RECORDING ON Y, X EMPTY
**       16    RECORDING ON X, Y EMPTY
**
ZMAN01   CSECT
         BEGIN
         L     2,16                A(CVT)
         L     2,196(0,2)          A(SMCA)
         TM    104(2),X'40'        SMCASWA
         BZ    MAN02
         WTO   '**MAN01 BOTH DATASETS FULL. SMF NOT RECORDING'
         SR    3,3                 RC=0
         B     END
MAN02    MVC   MSG2+45(1),35(2)    SMCAXORY - PRIMARY
         MVC   MSG2+56(1),51(2)    SMCAXORY - ALTERNATE
         LA    3,4                 RC=4
         CLI   35(2),C'Y'          SMCAXORY - PRIMARY
         BE    *+8
         LA    3,4(0,3)            RC=8
         TM    46(2),X'10'         SMCASTA
         BNO   MSG2
         LA    3,8(0,3)            RC=12/16
         MVC   MSG2+61(9),=C'EMPTY    '
MSG2     WTO   '**MAN02 SMF NOW RECORDING ON SYS1.MAN , SYS1.MAN  IS NO*
               T EMPTY'
END      L     13,4(0,13)
         LR    15,3
         RETURN (14,12),RC=(15)
         END
