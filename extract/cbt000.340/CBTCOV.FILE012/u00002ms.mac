         TITLE     'TSGLINK - A UTILITY TO SAVE SCHEDULER TIME - TSGLSH*
               ED (PSEUDO SCHEDULER)'
TSGLSHED CSECT
*
*  FOR DESCRIPTION OF TSGLSHED SEE TSGLROOT AND SEPARATE DOCUMENTATION
*  FOR TSGLINK.
*
         BALR  11,0
         USING *,11
BASE     ST    12,TWELVE
         LA    12,2048(11)
         LA    12,2048(12)
         USING BASE+4096,12
         ST    14,RETURN
         L     4,0(1)              R4 ALWAYS POINTS TO TSGRLIST
         USING TSGRLIST,4
         MVC   PGMCARD,RPGMCARD
         L     1,FREEMAIN
         LTR   6,1
         BZ    TESTCDCT
         BP    LETGOPRM
         USING IERPARM,6
         L     2,E15ADD
         LTR   2,2
         BZ    TRYE35
         DELETE EPLOC=(2)
TRYE35   L     2,E35ADD
         LTR   2,2
         BZ    LETGOPRM
         DELETE EPLOC=(2)
LETGOPRM EQU   *
         LA    0,192
         FREEMAIN  R,LV=(0),A=(1)
         XC    FREEMAIN,FREEMAIN
TESTCDCT L     1,CDCTR
         CP    0(2,1),=P'1'        IS CDCTR ZERO - IF SO INITIALIZE.
         BNL   STRTSHED
         L     2,CDATA
         OPEN  ((2))               OPEN CDATA.
         L     1,CDCTR
         AP    0(2,1),=P'1'
         LR    1,2
         L     15,48(2)
         BALR  14,15               GET FIRST CARD
         ST    1,ABONE
         CLC   0(7,1),=C'TSGLINK'
         BNE   ABEX
         CLC   9(8,1),=CL8'PGM'
         BNE   ABEX
         MVC   PGMCARD,8(1)
         L     1,16
         L     1,0(1)
         L     1,4(1)
         L     1,12(1)
         LA    1,8(1)
         ST    1,STEPNAME
         LA    2,16(1)
         SR    3,3
INITTSGP IC    3,0(2)
         LTR   3,3
         BZ    STRTSHED
         CLC   4(8,2),=CL8'TSGLPRNT'
         BE    TSGPFND
         AR    2,3
         B     INITTSGP
         EJECT
TSGPFND  MVI       TSGLPRSW,255
STRTSHED L     2,CDCTR
         AP    2(2,2),=P'1'        STEP NUMBER.
         SPMODE  PROB,0
         L     1,STEPNAME
         MVC   0(6,1),=C'TSGLNK'       TIOT STEP NAME
         UNPK  6(2,1),2(2,2)
         OI    7(1),240
         SPMODE  PROB,*
         L     1,CDATA
         MVC   33(3,1),=AL3(EOCDATA)
         TM    TSGLPRSW,255            IS TSGLPRNT DD PRESENT
         BZ    SELECT
         TM    TSGLIST+48,16
         BO    LISTOPEN
         OPEN  (TSGLIST,(OUTPUT))
LISTOPEN SP    4(2,2),=P'5'
         BP    PLONE
         PUT   TSGLIST,NEWPAGE
         PUT   TSGLIST,LINEHDR
         MVC   4(2,2),=PL2'55'
PLONE    CP    0(2,2),=P'1'
         BE    PLTWO
         MVC   LINEONE+52(24),LINERET
         L         1,RETREG
         LH    1,0(1)
         CVD   1,DW
         UNPK      CCEDFLD,DW+5(3)
         MVC   LINEONE+73(4),CCEDFLD+1
         OI    LINEONE+76,240
PLTWO    TIME
         ST    0,TIME
         MVC   LINEONE+42(9),HMSMASK
         OI    TIME+3,15
         UNPK  DW,TIME
         MVC   LINEONE+43(2),DW+1
         MVC   LINEONE+46(2),DW+3
         MVC   LINEONE+49(2),DW+5
         L     1,CDCTR
         UNPK  LINEONE+7(2),2(2,2)
         OI    LINEONE+8,240
         MVC   LINEONE+31(8),PGMNAME
         PUT   TSGLIST,LINEONE
SELECT   LA    10,TSEGTAB
         LA    9,TYPETAB
         LA    8,4
TSELOOP  L     15,0(10)
         CLC   PGMTYPE,0(9)
         BCR   8,15
         LA    10,4(10)
         LA    9,1(9)
         BCT   8,TSELOOP
         B     ABEX
TYPETAB  DC    C'USPL'
TSEGTAB  DC    A(UTILITY)
         DC    A(SORT)
         DC    A(USERPROG)
         DC    A(ENDPROG)
         EJECT
ENDPROG  L     10,ENDSW
         NI    1(10),15
         MVC   RPGMNAME,=C'ENDOFRUN'
SHEDDOOR MVC   LINETHRE+29(8),RPGMNAME
         MVC   RPGMCARD,PGMCARD
         TM        TSGLPRSW,255
         BZ        SHEDRET
         L         2,CDCTR
         UNPK  LINETHRE+7(2),2(2,2)
         OI    LINETHRE+8,240
         MVC   LINETHRE+40(9),HMSMASK
         TIME
         ST    0,TIME
         OI    TIME+3,15
         UNPK  DW,TIME
         MVC   LINETHRE+41(2),DW+1
         MVC   LINETHRE+44(2),DW+3
         MVC   LINETHRE+47(2),DW+5
         PUT   TSGLIST,LINETHRE
         CLOSE (TSGLIST)
SHEDRET  L         14,RETURN
         L     12,TWELVE
         BR    14
EOCDATA  L         2,CDATA
         CLOSE ((2))
         LA    1,ENDCARD
         L     14,EOCRET
         BR    14
         EJECT
TSGRLIST DSECT
RETREG   DS    F
RPGMNAME DS    CL8
CDATA    DS    F
LINKP    DS    F
P1       DS    F
CDCTR    DS    F                       POINTS TO 0  CDCTR  PL2
*                                                2  STEPNO PL2
*                                                4  LINECTR PL2
STEPNAME DS    0F
TSGLPRSW DS    C
         DS    CL3
ENDSW    DS    F
RETCODES DS    F
OLDSNAME DC    C'SORT'
FREEMAIN DS    F
RPGMCARD DS    CL19
         SPACE     2
UTILPARM DSECT
         DS    H
P2       DC    H'72'
ALTNAMES DC    XL32'00'
SYSIN    DC    XL8'00'
SYSPRINT DC    XL8'00'
         DC    XL8'00'
SYSUT1   DC    XL8'00'
SYSUT2   DC    XL8'00'
         SPACE     2
IERPARM  DSECT
         DS    H
SORTPARM DC    H'32'
SORTSTRT DC    A(SORTCARD)
SORTEND  DC    A(ENDSCARD)
RECDSTRT DC    A(RECDCARD)
RECDEND  DC    A(ENDRCARD)
E15ADD   DS    F
E35ADD   DS    F
SORTNAME DC    C'SORT'
CORESIZE DS    F
SORTCARD DS    CL100
ENDSCARD DS    C
RECDCARD DS    CL50
ENDRCARD DS    C
         SPACE     2
TSGLSHED CSECT
DW       DC    D'0'
EOCRET   DC    F'0'
SUBRET   DC    F'0'
PGMCARD  DS    0CL19
PGMTYPE  DC    C'I'
CARDID   DC    CL10' '
PGMNAME  DC    CL8' '
         EJECT
ABEX     DS    0H
         L     10,CDCTR
         TM    TSGLIST+48,16
         BO    ABPRINT
         UNPK  ABWTO+93(3),0(2,10)
         OI    ABWTO+95,240
ABWTO    WTO   'WHEN ASKED GIVE REASON FOR CANCELLING JOB IN THIS PARTI*
               TION AS ''FORMAT ERROR IN CARD XXX'''
         CNOP  2,4
         L     1,*+6
         SVC   13
         DC    X'80122000'
ABPRINT  UNPK  ABLINE+35(3),0(2,10)
         OI    ABLINE+37,240
         L     3,ABONE
         PUT   TSGLIST,ABLINE
         MVC   ABLINE+1(76),0(3)
         PUT   TSGLIST,ABLINE
         CLOSE (TSGLIST)
         ABEND 1,DUMP
         EJECT
TSGLIST  DCB   DDNAME=TSGLPRNT,DSORG=PS,MACRF=(PM),LRECL=77,           *
               BLKSIZE=77,RECFM=FM
         EJECT
NEWPAGE  DC    X'8B'
LINEONE  DC    X'11'
         DC    CL76'TSGLNKSS START SCHEDULING FOR PGMNAMEX AT HH.MM.SS'
LINERET  DC    C'PREVIOUS RETURN CODE NNNN'
LINETWO  DC    X'19'
         DC    CL76'TSGLNKSS BYPASSED BECAUSE OF CONDITION CODES.'
LINETHRE DC    X'19'
         DC    CL76'TSGLNKSS START EXECUTION OF PGMNAMEX AT HH.MM.SS '
HMSMASK  DC    X'4021204B20204B2020'
CCMASK   DC    X'4021202020'
SS       DC    CL3' '
LINEHDR  DC    X'19'
         DC    CL22' '
         DC    CL54'RESPOND TSG PROGRAM MONITOR LOG'
CCEDFLD  DC    CL5' '
TIME     DC    F'0'
RETURN   DC    F'0'
TWELVE   DC    F'0'
ENDCARD  DC    C'TSGLINK LPGM       ENDOFRUN'
ABLINE   DC    X'11'
         DC    CL76'**** FORMAT ERROR IN TSGLINK CARD XXX. ERROR CARD I*
               MAGE FOLLOWS'
         EJECT
BYPASS   DS    0H
         L     10,RETCODES
         BCTR  10,0
         BCTR  10,0
         PACK  DW,27(2,3)
         CVB   9,DW
         SLA   9,1                     MULT BY 2. R9 + R10 -> RETCODE.
BYPNEXT  PACK  DW,19(4,3)
         CVB   8,DW                    R8 HAS TEST VALUE.
         CLC   24(2,3),=C'EQ'
         MVI   BYPBR+1,128             BRANCH IF EQUAL.
         BE    BYPTEST
         CLC   24(2,3),=C'GT'
         MVI   BYPBR+1,32              BRANCH IF TEST VALUE HIGH.
         BE    BYPTEST
         CLC   24(2,3),=C'LT'
         MVI   BYPBR+1,64              BRANCH IF TEST VALUE LOW.
         BE    BYPTEST
         CLC   24(2,3),=C'NE'
         MVI   BYPBR+1,112             BRANCH IF TEST VALUE NOT EQUAL.
         BNE   ABEX
BYPTEST  CH    8,0(9,10)
BYPBR    BC    0,DONOT
         L     15,SUBRET
         BR    15
DONOT    TM    TSGLIST+48,16
         BZ    BYPRD
         L     1,STEPNAME
         MVC   LINETWO+1(8),0(1)
         PUT   TSGLIST,LINETWO
BYPRD    LA    1,BYPRDRET
         ST    1,EOCRET
BYPRDREP L     1,CDATA
         L     15,48(1)
         BALR  14,15
         ST    1,ABONE
BYPRDRET L     2,CDCTR
         AP    0(2,2),=P'1'
         LR    3,1
         CLC   0(7,1),=C'TSGLINK'
         BNE   ABEX
         CLC   9(8,1),=CL8'PGM'
         BNE   BYPRDREP
         MVC   PGMCARD,8(1)
         MVC   RPGMCARD,PGMCARD
         L     10,RETREG
         LA    10,2(10)
         ST    10,RETREG
         MVC   RPGMNAME,19(1)
         B     STRTSHED
         EJECT
UTILITY  DS    0H
         MVC   4(8,4),PGMNAME
         GETMAIN  R,LV=192
         LA    6,0(1)
         USING  UTILPARM,6
         ST    6,FREEMAIN
         LA    2,P2
         L     3,16(4)                 A(LINKP)
         NI    4(3),127
         ST    2,8(3)
         OI    8(3),128
         LA    2,72
         STH   2,P2
         L     2,20(4)                 A(P1)
         XC    0(2,2),0(2)
         XC    ALTNAMES(72),ALTNAMES
         LA    1,GCURET
         ST    1,EOCRET
GETCARDU L     1,12(4)
         L     15,48(1)
         BALR  14,15
         ST    1,ABONE
GCURET   L     10,24(4)
         AP    0(2,10),=P'1'
         CLC   0(7,1),=C'TSGLINK'
         BNE   ERR
         LA    9,UKNTAB
         LA    8,8
UCLC     CLC   9(8,1),0(9)
         L     7,8(9)
         BCR   8,7
         LA    9,12(9)
         BCT   8,UCLC
         B     ABEX
USETCOND LR    3,1
         LA    1,GETCARDU
         ST    1,SUBRET
         B     BYPASS
UCHANGE  LR        3,1
         LA        2,GETCARDU
         ST        2,SUBRET
         B         CHANGE
USCAT    LR        3,1
         LA        2,GETCARDU
         ST        2,SUBRET
         B         SCATALOC
USET1    MVC   SYSUT1,19(1)
         B     GETCARDU
USET2    MVC   SYSUT2,19(1)
         B     GETCARDU
USETIN   MVC   SYSIN,19(1)
         B     GETCARDU
USETPR   MVC   SYSPRINT,19(1)
         B     GETCARDU
USETPARM LA    6,70(1)
         LA    7,52
PCL      CLI   0(6),64
         BNE   PLF
         BCTR  6,0
         BCT   7,PCL
ERR      B     ABEX
PLF      L     2,20(4)
         STH   7,0(2)
         BCTR  7,0
         EX    7,MP
         L     6,FREEMAIN
         B     GETCARDU
MP       MVC   2(1,2),19(1)
USETPGM  MVC   PGMCARD,8(1)
         B     SHEDDOOR
         EJECT
         DS    0F
UKNTAB   DC    CL8'SYSUT1'
         DC    A(USET1)
         DC    CL8'SYSUT2'
         DC    A(USET2)
         DC    CL8'SYSIN'
         DC    A(USETIN)
         DC    CL8'SYSPRINT'
         DC    A(USETPR)
         DC    CL8'PARM'
         DC    A(USETPARM)
         DC    CL8'PGM'
         DC    A(USETPGM)
         DC    CL8'BYPASS'
         DC    A(USETCOND)
         DC    CL8'CHANGEDD'
         DC    A(UCHANGE)
         DC    CL8'SCATALOC'
         DC    A(USCAT)
         EJECT
SORT     DS    0H
         MVC   RPGMNAME,=CL8'IERRCO00'
         GETMAIN R,LV=192
         LA    6,0(1)
         USING IERPARM,6
         ST    6,FREEMAIN
         L     2,LINKP
         LA    3,SORTPARM
         ST    3,4(2)
         OI    4(2),128
         LA    1,32
         STH   1,SORTPARM
         LA    1,SORTCARD
         ST    1,SORTSTRT
         LA    1,ENDSCARD
         ST    1,SORTEND
         LA    1,RECDCARD
         ST    1,RECDSTRT
         LA    1,ENDRCARD
         ST    1,RECDEND
         XC    E15ADD(8),E15ADD
         MVC   SORTNAME,=C'SORT'
         MVI   SORTCARD,64
         MVC   SORTCARD+1(151),SORTCARD
*  FIND AVAILABLE CORE AND SET CORESIZE PARAMETER FOR SORT.
         L     1,16
         L     9,0(1)        REG 1 -> CVT
         L     9,4(9)        REG 9 -> TCB
         CLI   116(1),32
         BL    MVTCORE
* FOR MFT SYSTEM CORE AVAILABLE TO SORT IS TAKEN FROM LARGEST FQE.
         L     2,24(9)             TCBMSS
         L     2,0(2)              FIRST FQE
         MVC   LFQE,4(2)
         SR    3,3
FQELOOP  L     5,4(2)
         C     5,LFQE
         BNH   *+10
         MVC   LFQE,4(2)
         L     2,0(2)
         CR    3,2
         BNE   FQELOOP
*  LAST FQE HAS ZERO LINK FIELD.
         L     2,LFQE
CORESUM  EQU   *
         SH    2,=H'6144'          ALLOW 8K ABOVE CORESIZE (6K PLUS
*                                  LENGTH OF TSGLSHED WHICH WILL BE
*                                  OVERLAID).
         CH    2,=H'16384'
         BNL   COREOK
NOCORE   EQU   *
         MVC   ABLINE+5(24),=C'LESS THAN 16K FOR SORT -'
         MVC   ABWTO+72(15),=C'80A FOR SORT ..'
         B     ABEX
* FOR MVT SYSTEM CORE AVAILABLE TO SORT IS TAKEN FROM SUM OF FBQES.
MVTCORE  L     1,152(9)      REG 1 -> DUMMY PQE-8
         L     2,8(1)        REG 2 -> FIRST PQE
         L     3,0(2)        REG 3 -> FIRST FBQE
         CR    3,2
         BE    NOCORE
         L     10,8(3)       SIZE FROM FIRST FBQE
NEXTFBQE L     3,0(3)
         CR    3,2
         BE    NEXTPQE
SUBSENT  A     10,8(3)
         B     NEXTFBQE
NEXTPQE  L     2,8(2)
         LTR   2,2
         BZ    ENDPQES
         L     3,0(2)
         B     SUBSENT
ENDPQES  LR    2,10
         B     CORESUM
COREOK   ST    2,CORESIZE
         LA    1,SRDRET
         ST    1,EOCRET
GETCARDS L     1,CDATA
         L     15,48(1)
         BALR  14,15
         ST    1,ABONE
SRDRET   L     10,CDCTR
         AP    0(2,10),=P'1'
         CLC   0(7,1),=C'TSGLINK'
         BNE   ABEX
SCONTBR  BC    0,SCONTCD
         LA    9,SKNTAB
         LA        8,9
SCLC     CLC   9(8,1),0(9)
         L     7,8(9)
         BCR   8,7
         LA    9,12(9)
         BCT   8,SCLC
         B     ABEX
         EJECT
SCHANGE  LR    3,1
         LA    2,GETCARDS
         ST    2,SUBRET
         B     CHANGE
SSCATA   LR    3,1
         LA    2,GETCARDS
         ST    2,SUBRET
         B     SCATALOC
SLOAD15  SR        2,2
         B         *+8
SLOAD35  LA        2,1
         MVC       SLEPLOC,19(1)
         LOAD      EPLOC=SLEPLOC
*  REDUCE CORESIZE BY AN ARBITRARY 1K TO ALLOW FOR EXIT ROUTINE SIZE.
         L     1,CORESIZE
         S     1,=F'1024'
         ST    1,CORESIZE
         OI    FREEMAIN,128
         LTR       2,2
         BNZ       *+12
         ST        0,E15ADD
         B         GETCARDS
         ST        0,E35ADD
         B         GETCARDS
SSETNAME MVC   SORTNAME,19(1)
         CLC   29(6,1),=C'CHANGE'
         BNE   GETCARDS
         SPMODE  PROB,0
         L     2,STEPNAME
         LA    2,16(2)
         SR    3,3
STIOTLP  IC    3,0(2)
         LTR   3,3
         BZ    STIOTEND
         CLC   4(4,2),OLDSNAME
         BNE   STNEXT
         CLC   8(2,2),=C'WK'
         BNE   STNEXT
         MVC   4(4,2),SORTNAME
STNEXT   AR    2,3
         B     STIOTLP
STIOTEND MVC   OLDSNAME,SORTNAME
         SPMODE  PROB,*
         B     GETCARDS
SSETSORT MVC   SORTCARD(63),8(1)
         CLI   71(1),64
         BE    GETCARDS
         OI    SCONTBR+1,240
         LA    8,ENDSCARD
         B     GETCARDS
SCONTCD  SR    7,7
SCCL     CLI   0(8),64
         BNE   SCCLF
         LA    7,1(7)
         BCT   8,SCCL
         B     ABEX
SCCLF    CH    7,=H'51'
         BNH   *+8
         LH    7,=H'51'
         EX    7,MSC
         CLI   71(1),64
         BNE   GETCARDS
         NI    SCONTBR+1,15
         B     GETCARDS
MSC      MVC   1(1,8),19(1)
SSETRECD MVC   RECDCARD,8(1)
         CLI   71(1),64
         BE    GETCARDS
         OI    SCONTBR+1,240
         LA    8,ENDRCARD
         B     GETCARDS
SSETCOND LR    3,1
         LA    10,GETCARDS
         ST    10,SUBRET
         B     BYPASS
SSETPGM  MVC   PGMCARD,8(1)
         B     SHEDDOOR
LFQE     DC    F'0'
SKNTAB   DC    CL8'SORT'
         DC    A(SSETSORT)
         DC    CL8'RECORD'
         DC    A(SSETRECD)
         DC    CL8'SORTNAME'
         DC    A(SSETNAME)
         DC    CL8'BYPASS'
         DC    A(SSETCOND)
         DC    CL8'PGM'
         DC    A(SSETPGM)
         DC    CL8'CHANGEDD'
         DC    A(SCHANGE)
         DC    CL8'SCATALOC'
         DC    A(SSCATA)
         DC        CL8'E15NAME'
         DC        A(SLOAD15)
         DC        CL8'E35NAME'
         DC        A(SLOAD35)
SLEPLOC  DC    CL8' '
         EJECT
USERPROG MVC   RPGMNAME,PGMNAME
         L     1,P1
         XC    0(2,1),0(1)
         L     2,LINKP
         ST    1,4(2)
         OI    4(2),128
         LA    1,GETURET
         ST    1,EOCRET
GETUCARD L     1,CDATA
         L     15,48(1)
         BALR  14,15
         ST    1,ABONE
GETURET  L     2,CDCTR
         AP    0(2,2),=P'1'
         CLC   0(7,1),=C'TSGLINK'
         BNE   ABEX
         LA    9,USEKNTAB
         LA    8,5
USECLC   CLC   9(8,1),0(9)
         L     7,8(9)
         BCR   8,7
         LA    9,12(9)
         BCT   8,USECLC
         B     ABEX
         DS    0F
USEKNTAB DC    CL8'PARM'
         DC    A(USERPARM)
         DC    CL8'BYPASS'
         DC    A(USERCOND)
         DC    CL8'CHANGEDD'
         DC    A(USERCHAN)
         DC    CL8'SCATALOC'
         DC    A(USERSCAT)
         DC    CL8'PGM'
         DC    A(USERPGM)
USERPARM LA    8,70(1)
         LA    7,52
UCL      CLI   0(8),64
         BNE   UCLF
         BCTR  8,0
         BCT   7,UCL
         B     ABEX
UCLF     L     9,P1
         STH   7,0(9)
         EX    7,UMP
         B     GETUCARD
UMP      MVC   2(1,9),19(1)
USERCOND LR    3,1
         LA    10,GETUCARD
         ST    10,SUBRET
         B     BYPASS
USERCHAN LR    3,1
         LA    2,GETUCARD
         ST    2,SUBRET
         B     CHANGE
USERSCAT LR    3,1
         LA    2,GETUCARD
         ST    2,SUBRET
         B     SCATALOC
USERPGM  MVC   PGMCARD,8(1)
         B     SHEDDOOR
CHANGE   SPMODE  PROB,0
         L     8,STEPNAME
         SR    9,9
         LA    8,16(8)
CHATIOTL IC    9,0(8)
         LTR   9,9
         BZ    CHARET
         MVC   CHARCOMP,4(8)
         CLC   CHARCOMP,19(3)
         BNE   *+10
         MVC   4(8,8),29(3)
         CLC   CHARCOMP,29(3)
         BNE   *+10
         MVC   4(8,8),19(3)
         AR    8,9
         B     CHATIOTL
CHARCOMP DC    CL8' '
CHARET   SPMODE  PROB,*
         L     10,SUBRET
         BR    10
         EJECT
SCATALOC ST    4,FOUR
         CLC   19(8,3),=CL8'ISINDEX'
         BE    DSCBISAM
         MVC   SCATADCB+40(8),29(3)
         RDJFCB  (SCATADCB)
         MVC   FUNCTION,19(3)
         MVC   OKDSN,JFCB
         BAL   10,SFNDUCB
         BAL   10,SBLDVOLS
         CLC      19(8,3),=CL8'SCRATCH'
         BE        SCRAT
         CLC      19(8,3),=CL8'CATLG'
         BE        SCATLG
         CLC      19(8,3),=CL8'UNCATLG'
         BE        SCUNCAT
         CLC      19(8,3),=CL8'RENAME'
         BE        SCREN
         CLC      19(8,3),=CL8'ALLOC'
         BE    ALLOC
SINVEX   B     SCARET
SCRAT    LH    0,SCUCBAD+2
         SCRATCH CAMLSTC
         SR    1,1
         B     CHECK
SCATLG   CATALOG CAMLSTA
         LA    1,1
         B     CHECK
SCUNCAT  CATALOG CAMLSTB
         LA    1,1
         B     CHECK
SCREN    MVC   OLDNAME,JFCB
         MVC   SCATADCB+40(8),39(3)
         RDJFCB (SCATADCB)
         LH    0,SCUCBAD+2
         RENAME CAMLSTD
         SR    1,1
         B     CHECK
         EJECT
SFNDUCB  L     8,STEPNAME
         LA        8,16(8)
         SR        9,9
SCATIOTL IC        9,0(8)
         LTR       9,9
         BZ    SINVEX
         CLC       4(8,8),29(3)
         BE        SCATIOTF
         AR        8,9
         B         SCATIOTL
SCARET   L         10,SUBRET
         BR        10
SCATIOTF MVC       SCUCBAD,16(8)
         BR    10
         SPACE 2
SBLDVOLS SR    9,9
         IC        9,JFCB+117
         STC       9,VOLLIST+1
         L     1,SCUCBAD
         MVC   OKUNIT,13(1)
         MVI   OKVOLS,64
         MVC   OKVOLS+1(36),OKVOLS
         LA    15,OKVOLS
         LA    2,JFCB+118
         LA        8,VOLTYPE
         LA    7,5
         CR    9,7
         BH    SINVEX
SVOLSET  MVC   0(4,8),16(1)
         MVC   4(6,8),0(2)
         MVC   0(6,15),0(2)
         C     9,=F'1'
         BNH   *+12
         MVI   7(15),C','
         LA    15,7(15)
         XC        10(2,8),10(8)
         TM    18(1),32
         BO        *+10
         MVC       10(2,8),JFCB+68
         LA    2,6(2)
         LA        8,12(8)
         BCT       9,SVOLSET
         BR    10
DSCBISAM LINK  EP=TSGLISAM,PARAM=(FOUR,LINEHDR,TSGLIST),VL=1
         B     SCARET
FOUR     DC    F'0'
ABONE    DC    F'0'
         EJECT
*
*  RETURN CODES FOR CATLG AND UNCATLG ARE....
*        4         VOL NOT MOUNTED OR HAS NO SYSCATLG
*        20        SYSCATLG FULL
*        28        I/O ERROR
*        8 16 24   WRONGLY SET-UP REQUEST
*
*  RETURN CODES FOR SCRATCH AND RENAME
*        4         VOLUME NOT MOUNTED
*        8         LAST BYTE OF INDIVIDUAL VOLLIST ENTRY SHOWS....
*          0       OK
*          1       DSCB NOT FOUND
*          3       DUPLICATE DSCB (RENAME)
*          4       I/O ERROR
*          5 6     CANNOT MOUNT OTHER VOLUMES
*
CHECK    LTR   15,15
         BZ    PUTCONF
         STC   15,SERRBY
         LTR   1,1
         BZ    CHKLONG
         CLI   SERRBY,4
         MVC   SCERRMSG+26(20),=CL20'CATLG NOT AVAILABLE'
         BE    PUTSCERR
         CLI   SERRBY,28
         MVC   SCERRMSG+26(20),=CL20'I/O ERROR'
         BE    PUTSCERR
         CLI   SERRBY,20
         MVC   SCERRMSG+26(20),=CL20'FULL SYSCATLG'
         BE    PUTSCERR
         MVC   SCERRMSG+26(20),=CL20'SYSCATLG CONFLICT'
PUTSCERR EQU   *
         TM    TSGLPRSW,255
         BZ    SCERRWTO
         PUT   TSGLIST,SERRLINE
         B     SINVEX
SCERRWTO MVC   SCWTO+34(20),SCERRMSG+26
         L     8,STEPNAME
         S     8,=F'8'
         MVC   SCWTO+63(8),0(8)
SCWTO    WTO   'SCATALOC FAILS BECAUSE OF 12345678901234567890 FOR JOB *
               12345678'
         B     SINVEX
CHKLONG  CLI   SERRBY,4
         MVC   SCERRMSG+26(20),=CL20'VOLUME NOT MOUNTED'
         BE    PUTSCERR
         LA    1,VOLSER
         LH    2,VOLLIST
         MVC   SCERRMSG+26(11),=CL11'VOL'
CHKLOOP  MVC   SCERRMSG+30(6),0(1)
         CLI   7(1),0
         BE    NEXTCHK
         CLI   7(1),4
         MVC   SCERRMSG+37(9),=CL9'I/O ERROR'
         BE    PUTSCERR
         MVC   SCERRMSG+37(9),=CL9'NOT MOUNT'
         BH    PUTSCERR
         CLI   7(1),1
         MVC   SCERRMSG+37(9),=CL9'NO DSCB'
         BE    PUTSCERR
         MVC   SCERRMSG+37(9),=CL9'DUP DSCB'
         B     PUTSCERR
NEXTCHK  LA    1,12(1)
         BCT   2,CHKLOOP
         MVC   SCERRMSG+26(20),=CL20'*** VOL LIST ERROR'
         B     PUTSCERR
SERRBY   DC    X'00'
SERRLINE DC    X'09'
SCERRMSG DC    CL76'SCATALOC FAILS BECAUSE OF '
OKMSGA   DC    X'09'
FUNCTION DC    CL7' '
         DC    C' SUCCESSFUL FOR '
OKDSN    DC    CL44' '
         DC    CL9' '
OKMSGB   DC    X'11'
         DC    CL19' '
         DC    C'ON UNIT '
OKUNIT   DC    CL3' '
         DC    C', VOL(S) '
OKVOLS   DC    CL37' '
PUTCONF  TM    TSGLPRSW,255
         BZ    SCARET
         L     2,CDCTR
         SP    4(2,2),=P'3'
         BP    PLONF
         PUT   TSGLIST,NEWPAGE
         PUT   TSGLIST,LINEHDR
         MVC   4(2,2),=PL2'55'
PLONF    PUT   TSGLIST,OKMSGA
         PUT   TSGLIST,OKMSGB
         B     SCARET
         EJECT
ALLOC    DS    0H
         LA    0,JFCB
         LA    1,SCUCBAD
         SVC   32
         LTR   15,15
         BZ    PLONF
         STC   15,SERRBY
         CLI   SERRBY,4
         MVC   SCALERMG+28(15),=CL15'DUPLICATE NAME'
         BE    SCALLER
         CLI   SERRBY,12
         MVC   SCALERMG+28(15),=CL15'I/O ERROR'
         BE    SCALLER
         MVC   SCALERMG+28(15),=CL15'INVALID REQUEST'
SCALLER  TM TSGLPRSW,255
         BZ    SCALWTO
         PUT   TSGLIST,SCALERLN
         B     SCARET
SCALWTO  MVC   SCWTO+34(15),SCALERMG+28
         MVC   SCWTO+49(5),=CL5' '
         B     SCERRWTO+6
SCALERLN DC    X'09'
SCALERMG DC    CL76'ALLOCATION FAILS BECAUSE OF '
         EJECT
CAMLSTA  CAMLST    CAT,JFCB,,VOLLIST
CAMLSTB  CAMLST    UNCAT,JFCB
CAMLSTC  CAMLST    SCRATCH,JFCB,,VOLLIST,,OVRD
CAMLSTD  CAMLST    RENAME,OLDNAME,JFCB,VOLLIST
VOLLIST  DC        H'0'
VOLTYPE  DC        CL4' '
VOLSER   DC        CL6' '
VOLSEQ   DC        H'0'
         DC    CL48' '                 FOR FOUR MORE VOLUMES
SCUCBAD  DC        F'0'
XLIST    DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
OLDNAME  DC        CL44' '
         EJECT
SCATADCB DCB   DDNAME=XYZ,MACRF=(E),EXLST=XLIST
         END
