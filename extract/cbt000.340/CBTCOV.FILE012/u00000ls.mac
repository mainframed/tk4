PROTSYS1 CSECT
*
*  THIS PROGRAM WILL SEARCH THE VTOC OF ONE DISK PACK AND WILL
*   INSERT AN EXPIRY DATE OF 99366 IN EVERY FORMAT 1 DSCB HAVING
*   A DSNAME BEGINNING WITH 'SYS1.'.
*  THE JCL REQUIRED TO EXECUTE THE PROGRAM CONSISTS OF AN EXEC CARD
*   PLUS ONE DD CAR IDENTIFYING THE DISK PACK TO BE SEARCHED, AND
*   HAVING THE DDNAME 'SYSVTOC'.  E.G.....
*        //PROTECT  EXEC   PGM=PROTSYS1
*        //SYSVTOC DD      DISP=SHR,VOL=SER=123456,UNIT=2314
*
*        BEGIN     EXPANSION OF RESPOND TSG BEGIN MACRO
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         RDJFCB  (IN)
         MVC   SERIAL,JFCB+118
         MVC   JFCB(44),FORMAT4
         OPEN  (IN),TYPE=J
         RDJFCB  (OUT)
         MVC   JFCB(44),FORMAT4
         OPEN  (OUT,(OUTPUT)),TYPE=J
         ENQ   (SYSVTOC,SERIAL,E,6,SYSTEM)
GET      READ  ECBA,SF,IN,DSCB,'S'
         CHECK ECBA
         CLC   DSCB(5),=C'SYS1.'
         BNE   GET
         MVC   DSCB+56(3),EXPDT
         XDAP  ECBX,WI,OUT,DSCB,140,,IN+5
         WAIT  ECB=ECBX
         CLI   ECBX,X'7F'
         BE    GET
         ABEND 4,DUMP
EOF      DEQ   (SYSVTOC,SERIAL,6,SYSTEM)
         CLOSE (IN,,OUT)
*        GOBACK    EXPANSION OF RESPOND TSG GOBACK MACRO
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
IN       DCB   DDNAME=SYSVTOC,LRECL=96,KEYLEN=44,BLKSIZE=96,           *
               EXLST=XLIST,EODAD=EOF,MACRF=(R),RECFM=F,DSORG=PS
OUT      DCB   DDNAME=SYSVTOC,MACRF=(E),EXLST=XLIST,DSORG=DA
         DS    0F
XLIST    DC    X'87'
         DC    AL3(JFCB)
JFCB     DC    CL176' '
DSCB     DC    CL140' '
EXPDT    DC    X'63016E'
SYSVTOC  DC    C'SYSVTOC'
SERIAL   DC    CL6' '
FORMAT4  DC    44X'04'
         END
