RIC1803D CSECT
         ISEQ  73,80
* ATTRIBUTES:  REENTERABLE; TRANSIENT
*
* FUNCTION:
*     THIS MODULE PROCESSES THE FOLLOWING COMMANDS
*         DISPLAY  S=XXXX
*         WHERE XXXX IS A CATALOGUED DATA SET
*     AND WRITES TO OPERATOR THE NUMBER OF TRACKS USED IN THE DATASET
*
* ENTRY - AT RIC1803D
*
* INPUT - AT ENTRY, GR6 POINTS TO 40 BYTES OF CORE HOLDING AS BELOW
*         BYTES  00-15  CALLING SEQUENCE TO XCTL
*         BYTES  16-23  VERB FROM COMMAND
*         BYTES  24-25  S=
*         BYTES  26-39  DATA SET NAME
*
* MESSAGES:
*       ' DATASET NOT FOUND '
*       ' GETMAIN UNSUCCESSFUL '
*       ' XXXX/YYYY TRKS USED XXXXXXXXXXXXXX '
*              WHERE XXXX IS THE NUMBER OF TRACKS COMPLETELY FILLED
*                AND YYYY IS THE TOTAL NUMBER OF TRACKS IN THE DATASET.
         EJECT
* REGISTER USAGE
* REG  0    PARM ADR?
* REG  1    PARM ADR?
* REG  2    LIST PASSED BY IGC0403D
* REG  3    ADDR OF AREA OBTAINED BY GETMAIN
* REG  4
* REG  5
* REG  6    ADDR OF S=
* REG  7
* REG  8
* REG  9
* REG  10
* REG  11
* REG  12   BASE REG
* REG  13
* REG  14   RETURN
* REG  15   ENTRY POINT
         EJECT
         BALR  12,0
         USING *,12
         B     START
         DC    C'RIC1803D'
START    EQU   *
         GETMAIN R,LV=OBLEN,SP=255
         LTR   15,15
         BP    NOSPACE
         LR    3,1
         USING OBTLST,3
         MVC   DSN(14),26(6)           *MOVE IN DSNAME
         MVI   DSN+14,C' '            *
         MVC   DSN+15(29),DSN+14      *
         MVC   CLIST(16),LOCLIST
         LA    15,DSN
         ST    15,CLIST+4
         LA    15,VOL
         ST    15,CLIST+8
         LA    15,WKAREA
         ST    15,CLIST+12
         LOCATE CLIST                 LOOK IN CATALOG
         LTR   15,15                  WAS IT FOUND
         BNZ   NOTFOUND
         MVC   VOL,WKAREA+6            MOVE IN VOL SER
         MVC   CLIST(4),OBLIST
         OBTAIN CLIST
         LTR   15,15                   WAS IT FOUND
         BNZ   NOTFOUND
         MVC   STORH,WKAREA+54          TRACKS USED
         MVC   0(4,6),MESLEN1
         MVC   8(16,6),ALFA1
         MVC   24(14,6),DSN
         XR    4,4
         IC    4,WKAREA+15             NO OF EXTENTS
         LA    5,WKAREA+63             ADDR OF EXTENT BNDRY
         L     7,16                    CVT
         L     7,40(7)                 ADDR OF UCB LIST
UCBADDR  LH    8,0(7)                  ADDR OF UCB
         LA    7,2(7)                  NEXT
         LTR   8,8
         BZ    UCBADDR
         BM    ENDADD
         CLI   18(8),X'20'             IS IT DASD
         BNE   UCBADDR
         CLC   VOL,28(8)
         BNE   UCBADDR
         CLI   19(8),X'09'
         BE    D3330
         CLI   19(8),8
         BE    D2314
         B     ENDADD
D2314    LA    7,20
         B     SPACELFT
D3330    LA    7,19
SPACELFT EQU   *
         SR    15,15                         CLEAR COUNT
         CLI   WKAREA+15,3                   < 3 EXTENTS ?
         BNH   LTHAN3                        IF SO TOTAL AT LTHAN3
         LH    4,=H'3'                       SET TO
         BAL   1,NEXTEXT                       PROCESS 1ST 3
         IC    4,WKAREA+15
         ST    15,STORF                      SAVE COUNT
         MVC   CLIST(4),SEEKLIST
         MVC   DSN(5),WKAREA+91           STORE FORMAT 3 SEEK ADDR
         OBTAIN CLIST                        SEEK FORMAT 3 DSCB
         LTR   15,15
         BNZ   ENDADD
         LA    5,WKAREA+6                    POINT TO NEXT EXT
         L     15,STORF                      GET COUNT
         SH    4,=H'3'                       EXTS REMAINING
         CH    4,=H'4'                       EXCEEDS 4 ?
         BNH   LTHAN3                        NO
         SH    4,=H'4'                       YES - PROCESS 1ST 4 AND
         STC   4,STORF                            BUMP UP
         LH    4,=H'4'
         BAL   1,NEXTEXT
         LA    5,1(5)
         IC    4,STORF
LTHAN3   BAL   1,NEXTEXT                PROCESS REMAINING EXTS
         MVC   8(16,6),=C'/0000 TRKS USED '  ALTER MESSAGE
         CVD   15,CLIST
         UNPK  9(4,6),CLIST(8)
         OI    12(6),C'0'
         B     ENDADD
NEXTEXT  MVC   XOL(4),0(5)
         LR    8,7                     *
         MH    8,XOL                   *
         BCTR  8,0                     *  SUBTRACT LOW GND
         SR    15,8                    *
         SH    15,XOL+2                *
         MVC   XOL(4),4(5)             *
         LA    5,10(5)                 NXT EXTENT
         LR    8,7                     *
         MH    8,XOL                   *  ADD HIGH END
         AR    15,8                    *
         AH    15,XOL+2                *
         BCT   4,NEXTEXT
         BR    1
ENDADD   EQU   *
         LH    15,STORH
         CVD   15,CLIST
         UNPK  4(4,6),CLIST(8)
         OI    7(6),C'0'
OUT1     LR    1,3
         FREEMAIN R,LV=OBLEN,A=(1),SP=255
OUT      WTO   MF=(E,(6))
         BR    14
NOSPACE  MVC   0(4,6),MESLEN2
         MVC   4(20,6),ALFA2
         B     OUT
NOTFOUND MVC   0(4,6),MESLEN3
         MVC   4(18,6),ALFA3
         B     OUT1
SEEKLIST CAMLST SEEK,0,0,0
         ORG   SEEKLIST+4
OBLIST   CAMLST SEARCH,0,0,0
         ORG   OBLIST+4
LOCLIST  CAMLST NAME,0,,0
MESLEN1  DC    AL2(38)
         DC    AL2(0)
ALFA1    DC    C' TRACKS USED IN '
MESLEN2  DC    AL2(24)
         DC    AL2(0)
ALFA2    DC    C'GETMAIN UNSUCCESSFUL'
MESLEN3  DC    AL2(22)
         DC    AL2(0)
ALFA3    DC    C'DATA SET NOT FOUND'
*        AREA FOR GETMAIN
OBTLST   DSECT
CLIST    CAMLST SEARCH,0,0,0
DSN      DS    CL44
VOL      DS    CL6
WKAREA   DS    0D
         DS    265C
STORF    DS    F
XOL      DS    F
STORH    DS    H
OBLEN    EQU   *-CLIST
         END
