IGC0Z03D CSECT
*
*  THIS MODULE IS EXECUTED IN RESPONSE TO AN OPERATOR COMMAND 'SWITSMF'
*  TO CAUSE SMF TO CLOSE THE CURRENT SYS1.MAN DATA SET (SO THAT IT CAN
*  BE DUMPED) AND TO OPEN THE ALTERNATE DATA SET.
*
*  BEFORE SWITCHING IT CHECKS....
*      1. THAT SMF RECORDING IS CURRENTLY ACTIVE.
*      2. THAT THE ALTERNATE IS EMPTY (OTHERWISE SMF WILL NOT OPEN IT)
*
*  IT THEN CAUSES A SWITCH IN THE SAME WAY AS Z EOD BY WRITING TO SMF
*  A 'HALT' RECORD. THIS ACHIEVES THE EFFECT OF EOD WITHOUT CLOSING
*  THE LOG OR WRITING A 'WAIT TIME' RECORD.
*
*  TO MAKE THIS COMMAND ACTIVE.....
*      1. RECORD IGC0Z03D IN SVCLIB.
*      2. ZAP IGC0403D TO CAUSE THE MP65 'QUIESCE' COMMAND TO BE RE-
*  PLACED BY 'SWITSMF' AND TO ALTER THE TABLE SELECTING NEXT XCTL TO
*  SELECT IGC0Z03D, E.G (FOR 19.1 MVT)........
*        NAME  IGC0403D  IEE0403D
*        VERIFY  02E1  D8E4C9C5E2C3C40D8  QUIESCE
*        REP     02E1  E2E6C9E3E2D4C4040  SWITSMF (NO ABBREVIATION)
*        VERIFY  036F  F0F8            08(03D)
*        REP     036F  F0E9            0Z(03D)
*
         BALR  3,0
         USING *,3
         SSM   ENABLE
         GETMAIN  R,LV=480,SP=255
         LR    10,1
         USING ZSECT,10
         MVC   ZOBCAM(4),OBFLAGS
         LA    1,ZDSN
         ST    1,ZDSNAD
         LA    1,ZVOL
         ST    1,ZVOLAD
         LA    1,ZWORK
         ST    1,ZWORKAD
         MVC   ZDSN,NAME
         L     1,16
         L     4,196(1)      REG 4 IS SMCA POINTER
         TM    1(4),X'C0'
         BZ    NOTREC
         IC    1,51(4)
         STC   1,ZDSN+8
         MVC   ZVOL,40(4)
         LA    1,ZOBCAM      OBTAIN DSCB OF ALTERNATE
         SVC   27
         LTR   15,15
         BNZ   ERROR
         CLC   ZWORK+54(3),ZEROTTR
         BNE   DUMPMSG
         NI    46(4),127     TURN OFF 'ALT FULL'
         NI    104(4),255-64 TURN OFF 'BOTH FULL'
         MVC   ZMSG(8),HALTREC
         LR    1,10
         SVC   83
         B     EXIT
ERROR    MVC   ZMSG,MSG
         LA    1,MTAB
         LA    14,12
         CR    15,14
         BNH   *+8
         LA    15,16
         L     1,0(1,15)
         MVC   ZMSG+41(15),0(1)
         LR    1,10
         WTO   MF=(E,(1))
EXIT     LR    1,10
         FREEMAIN  R,LV=480,A=(1),SP=255
         SVC   3
DUMPMSG  MVC   ZMSG,MSG2
         MVC   ZMSG+23(4),ZDSN+5
         LR    1,10
         WTO   MF=(E,(1))
         B     EXIT
NOTREC   MVC   ZMSG,MSG3
         LR    1,10
         WTO   MF=(E,(1))
         B     EXIT
MTAB     DC    A(0)
         DC    A(MA)
         DC    A(MB)
         DC    A(MC)
         DC    A(MD)
MA       DC CL15'VOL NOT MOUNTED'
MB       DC    CL15'DSCB NOT FOUND'
MC       DC    CL15'PERM I/O ERROR'
MD       DC    CL15'UNSPEC ERROR'
MSG      WTO   'CANNOT SWOP SMF DATA SETS BECAUSE OF VOL NOT MOUNTED', *
               MF=L,ROUTCDE=(2,11)
MSG2     WTO   'CANNOT SWOP SMF TO MANX BECAUSE IT IS NOT EMPTY',      *
               MF=L,ROUTCDE=(2,11)
MSG3     WTO   'NO SMF SWOP BECAUSE SMF IS NOT RECORDING',             *
               MF=L,ROUTCDE=(2,11)
ZEROTTR  DC    XL3'00'
ENABLE   DC    X'FF'
HALTREC  DC    F'0'
         DC    C'HALT'
NAME     DC    CL44'SYS1.MANX'
OBFLAGS  DC    X'C1000000'
ZSECT    DSECT
ZMSG     DS    CL64
ZOBCAM   DS    F
ZDSNAD   DS    F
ZVOLAD   DS    F
ZWORKAD  DS    F
ZWORK    DS    350C
ZDSN     DS    CL44
ZVOL     DS    CL6
         END
