IGC0803D CSECT
         ISEQ  73,80
         SPACE
* ATTRIBUTES: REENTERABLE; TRANSIENT
*
* FUNCTION:
*         THIS MODULE ALLOWS TWO ADDITIONAL OPERATOR COMMANDS TO BE
*    PROCESSED.
*         1)   DISPLAY DISPACE
*         2)   DISPLAY S=XXXX
*                        WHERE XXXX IS A CATALOGED DISK DATA SET.
*
*         THE FIRST, WHICH HAS AN ABBREVIATED FORM
*              D S
*    IS USED TO DISPLAY ON THE CONSOLE THE NUMBER OF UNUSED CYLINDERS
*    AND TRACKS ON EACH DISK VOLUME WHICH IS ONLINE AND IN THE READY
*    STATE.
*         2314 AND 3330 DISKS ARE SUPPORTED.
*
*         THE SECOND COMMAND WHICH HAS A CORRESPONDING ABBREVIATED FORM
*              D S=XXXX
*    IS USED TO DISPLAY THE NUMBER OF UNUSED TRACKS IN A CATALOGED
*    DATA SET. CONTROL IS PASSED FROM THIS MODULE TO RIC1803D WHEN THIS
*    COMMAND IS ENCOUNTERED.
*
*         FOR ALL OTHER COMMANDS, CONTROL IS PASSED TO RIC0803D.
*
* INSTALLATION PROCEDURE.
*         1)   USE IEHPROGM TO RENAME MEMBER IGC0803D OF SYS1.SVCLIB TO
*              RIC0803D.
*         2)   ASSEMBLE THIS NEW CSECT IGC0803D AND LINK EDIT INTO
*              SYS1.SVCLIB WITH NAME IGC0803D. 'RENT' OPTION SHOULD BE
*              INCLUDED IN THE LINKAGE EDITOR PARM VALUE.
*         3)   ASSEMBLE THE NEXT CSECT RIC1803D AND LINK EDIT INTO
*              SYS1.SVCLIB WITH NAME RIC1803D.
*
* ENTRY - THE ONLY ENTRY IS FROM IGC0403D
*
* INPUT - AT ENTRY, GR6 POINTS TO 40 BYTES OF CORE HOLDING THE
*              FOLLOWING INFORMATION
*              BYTES 00-15 CALLING SEQ USED TO XCTL TO IEC0103D
*              BYTES 16-23 VERB FROM OPERATOR COMMAND
*              BYTES 24-39 PARAMETER LIST FROM OPERATOR COMMAND
*
* MESSAGES:
*        DISPACE- 'SPACE ON XXXXXX: XXX CYLS, XXX TRKS.'
*                 'DSCB NOT FOUND - XXXXXX'
*                 'GETMAIN UNSUCCESSFUL'
         EJECT
* GENERAL REGISTER USEAGE
* REG    0     PARM ADR?
* REG    1     PARM ADR?
* REG    2     LIST PASSED BY IGC0403D
* REG    3
* REG    4
* REG    5     ADR OF AREA OBTAINED BY GETMAIN
* REG    6     ADR OF LIST IN FORMAT USED BY IGC0103D & 0203D
* REG    7
* REG    8
* REG    9
* REG    10    ADR OF UCB BEING CHECKED
* REG    11    ADR OF UCB POINTERS
* REG    12    BASE REG FOR THIS MODULE
* REG    13
* REG    14    RETURN
* REG    15    ENTRY POINT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
CVT      EQU   16
         BALR  12,0
         USING *,12
         B     START
         DC    C'IGC0803D'
START    L     R8,CVT              ADR OF CVT
         L     R11,40(R8)          ADR OF 2-BYTE UCB POINTERS
         L     R6,20(R2)
         SLL   R6,8
         SRL   R6,8
         USING PLIST,R6            INFORMATION FROM OPERATOR
         CLI   20(R2),X'68'        DISPLAY?
         BNE   OUT1
         SH    R6,H24
PARMCH   CLC   OPER(2),S           CHECK OPERAND - S?
         BE    PROCESS             YES - WE WANT THIS ONE
         CLC   OPER(8),SPACE       SPACE?
         BNE   CHACMES        NO - CHECK FOR OTHER DISPLAY COMMANDS.
PROCESS  EQU   *
         LR    R6,R2               ADR OF EXTENDED SAVE AREA
         LR    R3,R1                                             ***
         GETMAIN R,LV=377,SP=255
         LTR   R15,R15                                           ***
         BP    NOSPACE                                           ***
         LR    R5,R1                                             ***
         USING OBTLST,R5
         LR    R1,R3                                             ***
TESTEND  EQU   *
         CLC   0(2,R11),FS         LAST POINTER?
         BE    OUT2                YES - END
         CLC   0(2,R11),ZEROES5    ZERO POINTER?
         BNE   CHUCB               NO - GO & CHECK UCB
BUMP     LA    R11,2(R11)          BUMP ADR OF LIST
         B     TESTEND             TRY NEXT OME
*
CHUCB    EQU   *
         LA    R7,ABSTRAC
         LA    R8,VOLNO
         LA    R9,WKAREA
         STM   R7,R9,CLISTADR      STORE IN CAMLST
         L     R7,CAMFLAGS
         ST    R7,CLISTFLG         FULL-WORD OF FLAGS
         LH    R10,0(R11)          LOAD UCB ADR IN GR10
         USING UCBLIST,R10
         CLI   DUCBTYP3,X'20'      DASD?
         BNE   BUMP
         SR    R7,R7
         CLI   DUCBTYP4,X'09'      3330 DISK ?
         BE    CHREADY
         CLI   DUCBTYP4,X'08'      2314 DISK?
         BNE   BUMP
         LA    R7,20               NO OF TRACKS/CYL ON 2314
         B     *+8                                               ***
CHREADY  EQU   *
         LA    R7,19               NO OF TRKS/CYL ON 3330
         TM    DSTATUSA,X'80'      DEVICE ON-LINE?
         BZ    BUMP                NO - IGNORE
         TM    DUCBFL1,X'40'       DEVICE READY?
         BO    BUMP                NO - TRY NEXT UCB
* IF CONTROL REACHES THIS POINT, THE CURRENT UCB REFERS TO AN ON-LINE
*        DISK VOLUME IN THE READY STATE
         LH    R9,36(R10)          ADR OF VTOC IN TRACKS (NOT RECS)
         SR    R8,R8
         DR    R8,R7               CONVERT 'TT' TO 'CCHH'
         STH   R9,ABSTRAC          CC
         STH   R8,ABSTRAC+2        HH
         IC    R9,38(R10)
         LA    R9,1(R9)            ADD '1' TO REC # - GIVES FORMAT 5 CB
         STC   R9,ABSTRAC+4        R - POINTS TO FORMAT 5 DSCB
         MVC   VOLNO(6),28(R10)    VOLUME SER. NO.
         SR    R3,R3               CLEAR CYL COUNT               ***
         SR    R4,R4               CLEAR TRACK COUNT             ***
         ST    R3,0(R6)                                          ***
RDSCB    EQU   *                                                 ***
         OBTAIN CLIST
* 1ST FORMAT 5 DSCB IN WKAREA (1ST 140 BYTES)
         LTR   R15,R15             ANY RETURN CODE?
         BP    ERREND              YES - PRINT ERROR & RETURN
         LA    R2,EXTSTRT-5        ADR OF 1ST EXTENT
         LA    R8,8                COUNT OF EXTENTS BEFORE DSCB ID
         BAL   R9,SUMSPACE
         LA    R8,18               NEXT COUNT
         LA    R2,1(R2)            BUMP PAST DSCB ID
         BAL   R9,SUMSPACE
         CLC   0(5,R2),ZEROES5     LAST ONE WAS ZERO?
         BE    PRINTSP             YES - PRINT AVAILABLE SPACE
         CLC   5(5,R2),ZEROES5     ANY FURTHER FORMAT 5 DSCB'S?
         BE    PRINTSP             NO - PRINT SPACE ON VOLUME
         MVC   ABSTRAC(5),5(R2)    ABS ADR FOR NEXT DSCB
         B     RDSCB
PRINTSP  EQU   *
* OUTPUT SPACE FOR CURRENT VOLUME
         MVC   0(4,R6),MESLEN1
         MVC   4(9,R6),ALPHA1
         MVC   13(6,R6),28(R10)    SERIAL NUMBER
         MVC   19(21,R6),ALPHA2    ALPHA + 2 EDIT PATTERNS
         CVD   R3,CLISTFLG
         ED    20(5,R6),CLISTFLG+6 NO OF SPARE CYLS
         CVD   R4,CLISTFLG
         ED    30(5,R6),CLISTFLG+6 NO OF SPARE TRACKS
         WTO   MF=(E,(6))          WRITE LINE
         B     BUMP                UPDATE UCB POINTER & TRY NEXT UCB
*
SUMSPACE EQU   *
         LA    R2,5(R2)            UPDATE ADR OF EXTENT
         CLC   0(5,R2),ZEROES5     ZEROES?
         BE    PRINTSP             RETURN IF SO
         MVC   3(1,R6),4(R2)       SPARE TRACKS
         A     R4,0(R6)            NUMP NO OF TRACKS
         MVC   2(2,R6),2(R2)       SPARE CYCS
         A     R3,0(R6)            BUMP COUNT OF TRACKS
         MVI   2(R6),X'00'         DO NEXT EXTENT
         BCT   R8,SUMSPACE
         BR    R9                  RETURN AT END
*
ERREND   EQU   *
         MVC   0(4,R6),MESLEN2     LENGTH
         MVC   4(17,R6),ALPHA3     'DSCB NOT FOUND'
         MVC   21(6,R6),28(R10)    SERIAL NO
         WTO   MF=(E,(R6))
         B     BUMP                TRY NEXT VOLUME
*
* CALL IGC0203D IF NOT DISPLAY SPACE
OUT1     EQU   *
         MVC   8(8,R2),XLNAME      ENTER NAME RIC0803D
OUT3     EQU   *
         LA    R15,8(R2)
         ST    R15,0(R2)
         LR    R15,R2
         XCTL  SF=(E,(15))         PASS CONTROL TO IGC0203D
         SPACE 2
OUT2     EQU   *
         LR    R1,R5
         FREEMAIN R,LV=377,A=(1),SP=255
         BR    14
NOSPACE  EQU   *                   NO MAIN STORAGE FOUND BY GETMAIN
         MVC   0(4,R6),MESLEN3
         MVC   4(20,R6),ALPHA4     ERROR MESSAGE - NO CORE AVAILABLE.
         WTO   MF=(E,(6))
         BR    14
CHACMES  EQU   *
* THIS WOULD BE THE PLACE TO ADD NEW 'DISPLAY' COMMANDS
         CLC   OPER(2),SEQUAL
         BNE   OUT1
         MVC   8(8,R2),XLNAME2         GO AND PROCESS
         B     OUT3                    S= COMMAND
SEQUAL   DC    C'S='
XLNAME2  DC    C'RIC1803D'
         EJECT
* CONSTANTS
         DS    0D
S        DC    C'S '
SPACE    DC    C'DISPACE '
FS       DC    X'FFFF'
ZEROES5  DC    X'0000000000'
MESLEN1  DC    X'00280000'
MESLEN2  DC    X'001B0000'
MESLEN3  DC    X'00180000'
ALPHA1   DC    C'SPACE ON '
ALPHA2   DC    C':'
         DC    X'4020212022'
         DC    C'CYLS,'
         DC    X'4020212022'
         DC    C'TRKS.'
ALPHA3   DC    C'DSCB NOT FOUND - '
ALPHA4   DC    C'GETMAIN UNSUCCESSFUL'
XLNAME   DC    C'RIC0803D'
H24      DC    H'24'
CAMFLAGS DS    0F
         DC    AL1(192)            THREE BYTES OF FLAGS
         DC    AL1(128)            INDICATING THE FUNCTION
         DC    AL1(0)              TO BE PERFORMED
         DC    AL1(0)              NO OPTION 3
         EJECT
PLIST    DSECT
CALLSEQ  DS    4F
VERB     DS    2F
OPER     DS    4F
         SPACE 2
UCBLIST  DSECT
         ORG   *+3
DSTATUSA DS    CL3
DUCBFL1  DS    CL10
DUCBTYP1 DS    CL1
DUCBTYP2 DS    CL1
DUCBTYP3 DS    CL1
DUCBTYP4 DS    CL1
         SPACE 2
* AREA FROM GETMAIN
OBTLST   DSECT
CLIST    EQU   *
CLISTFLG DS    F
CLISTADR DS    F
CLISTVOL DS    F
CLISTWRK DS    F
DSCBADR  EQU   *
ABSTRAC  DS    CL5
VOLNO    DS    CL6
WKAREA   DS    0D
         DS    350C
         SPACE 2
DSCBF5   EQU   WKAREA
         ORG   WKAREA+4
EXTSTRT  DS    CL5
TOP      EQU   WKAREA+140
         END
