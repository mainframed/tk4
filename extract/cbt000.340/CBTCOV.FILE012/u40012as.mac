*   WRAP AROUND FOR IEBUPDTE TO ADD SSI INFORMATION
*
EDIT     CSECT
RCARD    EQU   3
ROUT     EQU   4
RWK1     EQU   5
RWK2     EQU   6
         COPY  UZSTART
*   SWITCH SETTINGS
*
*        B1 ON  => NAME STATEMENT FOUND ON CONTROL CARD
*        B2 ON  => ADD FUNCTION
*        B3 ON  => ENQ ISSUED
*        B4
*        B5 ON  => PASS PARM='NEW'
*        B6
*        B7
*        B8
*
*   OPEN INPUT PDS DCB
*
OPENPDCB OPEN  (PDCB,(INPUT))
         TM    PDCB+48,X'10'
         BO    OPENIN
         OI    SWITCH,X'08'   SET SWITCH TO PASS PARM='NEW'
*
OPENIN   OPEN  (IN,(UPDAT))
*
         TM    IN+48,X'10'
         BZ    FINAL
*
GETCARD  GET   IN
         NI    SWITCH,X'7F'
         LR    RCARD,1
         LR    RWK1,1
         EJECT
         CLC   0(2,RCARD),=C'./'
         BNE   GETCARD
         LA    RCARD,2(RCARD)
         CLI   0(RCARD),C' '
         BNE   GETCARD
*
         LA    1,40
LOOP1    LA    RCARD,1(RCARD)
         CLI   0(RCARD),C' '
         BNE   FUNCTEST
         BCT   1,LOOP1
         B     GETCARD
*
FUNCTEST CLC   0(7,RCARD),=C'CHANGE '
         BE    CHANGE
         CLC   0(4,RCARD),=C'ADD '
         BE    ADD
         CLC   0(5,RCARD),=C'REPL '
         BE    REPL
         CLC   0(2,RCARD),=C'N '
         BE    NUMBER
         B     GETCARD
         EJECT
CHANGE   LA    RCARD,7(RCARD)
         MVC   OUTLINE(10),=C'./ CHANGE '
         B     SEARCH
ADD      LA    RCARD,4(RCARD)
         MVC   OUTLINE(10),=C'./ ADD    '
         OI    SWITCH,X'40'
         B     SEARCH
REPL     LA    RCARD,5(RCARD)
         MVC   OUTLINE(10),=C'./ REPL   '
         B     SEARCH
NUMBER   MVC   OUTLINE(27),=C'./ NUMBER NEW1=100,INCR=100'
         B     PUTX
         EJECT
SEARCH   LA    1,40
LOOP2    CLI   0(RCARD),C' '
         BNE   CHECKPRM
         LA    RCARD,1(RCARD)
         BCT   1,LOOP2
         B     GETCARD
CHECKPRM LR    RWK2,RCARD
         CLC   0(6,RCARD),=C'TOTAL='
         BE    GETCARD
         CLC   0(5,RCARD),=C'NAME='
         BE    PDS
         CLC   0(4,RCARD),=C'SSI='
         BE    GETCARD
TRANSLTE TRT   0(20,RCARD),TRTABLE
         CLI   0(1),C','
         BE    TENDCARD
         CLI   0(1),C' '
         BE    ENDCARD
         B     GETCARD
         EJECT
PDS      OI    SWITCH,X'80'
         TM    SWITCH,X'20'        TEST FOR ENQ ISSUED
         BO    TESTSW40
*
* OPEN OUTPUT DCB AND ENQ ON DATA SET NAME
*
         OPEN  (PDCB2,(INPUT))
         TM    PDCB2+48,X'10'
         BZ    GETCARD
        RDJFCB PDCB2
         CLOSE PDCB2
         ENQ   (QNAME,RNAME,E,44,SYSTEM)
         OI    SWITCH,X'20'
R7       EQU   7
R8       EQU   8
TESTSW40 TRT   5(9,RCARD),TRTABLE
         BZ    TRANSLTE
         SR    1,RCARD
         LTR   1,1
         BZ    GETCARD
         SH    1,=H'6'
         EX    1,MOVENAME
         TM    SWITCH,X'40'
         BO    TRANSLTE
         BLDL  PDCB,LIST
         LTR   15,15
         BNZ   TRANSLTE
         LH    R7,LIST+16
         N     R7,=F'31'
         CH    R7,=H'2'
         BL    BLANKNAM
         MVC   DECFLD+1(1),LIST+21
         AP    DECFLD(3),=P'10'
         UNPK  VERNO(5),DECFLD(3)
         MVC   CDATE+6(2),VERNO+2
*
BLANKNAM MVC   NAME(8),BLANK
         B     TRANSLTE
*
         EJECT
LIST     DC    H'1'
         DC    H'58'
NAME     DC    8C' '
         DS    CL68
         DS    0D
DECFLD   DC    PL3'0'
         DS    F
VERNO    DS    CL5
BLANK    DC    8C' '
PDCB     DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=(R)
         EJECT
TENDCARD LR    RCARD,1
         LA    RCARD,1(RCARD)
         CLI   1(RCARD),C' '
         BNE   CHECKPRM+2
         CLI   71(RWK1),C' '
         BNE   CONTCARD
         B     GETCARD
*
ENDCARD  TM    SWITCH,X'80'
         BZ    GETCARD
         SR    1,RWK2
         CH    1,=H'48'
         BH    TOOLONG
         BCTR  1,0
         LA    ROUT,OUTLINE+10
         EX    1,MOVE
         AR    ROUT,1
         LA    ROUT,1(ROUT)
         TIME  DEC
         ST    1,PDATE
         UNPK  CDATE(5),PDATE+1(3)
         MVC   0(5,ROUT),=C',SSI='
         MVC   5(9,ROUT),CDATE
PUTX     MVC   0(80,RWK1),OUTLINE
         PUTX  IN
         MVI   OUTLINE,C' '
         MVC   OUTLINE+1(80),OUTLINE
CONTCARD B     GETCARD
         EJECT
FINAL    CLOSE IN
         CLOSE PDCB
LINK     BLDL  0,LIST1
         TM    SWITCH,X'08'
         BZ    OLD
         LA    1,ANEWPARM
         B     LINKDE
OLD      LA    1,ADPARM
LINKDE   EQU   *
         LINK  DE=MEMBER
         TM    SWITCH,X'20'
         BZ    END
         ST    15,CONCODE
         DEQ   (QNAME,RNAME,44,SYSTEM)
         L     15,CONCODE
END      L     13,4(13)
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
         EJECT
OUTLINE  DC    100C' '
IN       DCB   DSORG=PS,MACRF=(GL,PL),DDNAME=SYSIN,EODAD=FINAL
PDATE    DS    F
CONCODE  DS    F
CDATE    DS    CL5
         DC    C'000 '
LIST1    DC    H'1',H'58'
MEMBER   DC    C'IBMUPDTE'
         DS    CL50
TRTABLE  DC    64X'00'
         DC    C' '
         DC    42X'00'
         DC    C','
         DC    148X'00'
NEWPARM  DC    H'3'
         DC    C'NEW'
PARM     DC    H'0'
ADPARM   DS    0F
         DC    X'80'
         DC    AL3(PARM)
ANEWPARM DC    X'80'
         DC    AL3(NEWPARM)
*
QNAME    DC    C'SYSIEWL '
JFCB     DC    44F'0'
EXLST    DC    X'07'
         DC    AL3(JFCB)
RNAME    EQU   JFCB
PDCB2    DCB   DDNAME=SYSUT2,DSORG=PO,MACRF=(R),EXLST=EXLST
TOOLONG  B     GETCARD
SWITCH   DC    X'00'
MOVE     MVC   0(0,ROUT),0(RWK2)
MOVENAME MVC   NAME(0),5(RCARD)
         END
         ENQ   (QNAME,RNAME,E,44,SYSTEM),RET=HAVE
