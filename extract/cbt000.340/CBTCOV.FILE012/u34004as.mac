         TITLE 'SCY03: CATALOG FLIPPING UTILITY.        VERSION - 01'
***********************************************************************
*                                                                     *
*        SCY03: BACKWARD GENERATION DATA GROUP CYCLER.                *
*                                                                     *
*                        --------------------                         *
*                                                                     *
*        FUNCTION:                                                    *
*              TO CYCLE GENERATION DATA GROUPS WITHOUT REGARD TO      *
*        THE NUMBER OF VOLUMES ASSIGNED TO EACH CATALOGUED ENTRY.     *
*                                                                     *
*                        --------------------                         *
*                                                                     *
*        INPUT:                                                       *
*              THE PROGRAM IS DRIVEN BY A PARM FIELD DESCRIBING       *
*        THE NAMES OF THE GDG INDICES TO BE CYCLED. EACH NAME IS      *
*        SEPERATED FROM ITS PREDECESSOR BY A SLASH (/) OR A COMMA     *
*        (,).                                                         *
*        E.G.   //CYCLE EXEC PGM=SCY03,PARM='GDG1,GDG2/GDG3'          *
*                                                                     *
*                        --------------------                         *
*                                                                     *
*        OUTPUT:                                                      *
*              AN UPDATED CATALOGUE, WITH A DATA SET NAME OF ONE      *
*        LOWER  GENERATION NUMBER THAN THE PREVIOUS LOWEST, AND       *
*        WITH THE VOLUME SERIAL INFORMATION OF THE PREVIOUS           *
*        HIGHEST (WHICH IS DELETED) .                                 *
*                                                                     *
*                        --------------------                         *
*                                                                     *
*        EXITS:                                                       *
*              NO EXTERNAL CALLS.                                     *
*              1. EXIT AT END OF PROGRAM                              *
*                                                                     *
*                        --------------------                         *
*                                                                     *
*        TABLES/WORK AREAS:                                           *
*                                                                     *
*              TTABLE- A TRANSLATE TABLE TO EXTRACT AND VALIDATE      *
*                      DATA SET NAMES.                                *
*              GNM   - THE DATA SET NAME TO BE FLIPPED                *
*              SWTCH1- SWITCH FOR FIRST TIME THRO' FOR VCB BUILD      *
*              GXAREA- THE LOCATE RETURN DATA AREA                    *
*              SGXAR - VOLUME SERIAL LIST FOR CATALOG                 *
*                                                                     *
*                        --------------------                         *
*                                                                     *
*        ATTRIBUTES:                                                  *
*              SERIALLY REUSABLE                                      *
*                                                                     *
*                        --------------------                         *
*                                                                     *
*        NOTES:                                                       *
*             AUTHOR - J.W.ASHFORD.            DATE - SEP 1977.       *
*                      THE STOCK EXCHANGE,                            *
*                      LONDON,    E.C.2.                              *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
SWTCH    OPSYN MVI
TEST     OPSYN CLI
BON      OPSYN BE
BOFF     OPSYN BNE
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*        MACRO DEFINITIONS.                                           *
*        ----- ------------                                           *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
         MACRO
&NAME    SMWTO &MESSNO,&MESSMD,&MESS,&DSNF
.**********************************************************************
.*                                                                    *
.*                                                                    *
.*       SMWTO. SCY0X WRITE TO OPERATOR PARAMETER LIST MACRO          *
.*                                                                    *
.*       FUNCTION. TO CREATE A PARAMETER LIST READY FOR AN EXECUTION  *
.*                 OF A WTO MF=E , WITH A 44 CHAR SPACE FOR THE DATA  *
.*                 SET NAME , AND A MESSAGE MODE ADDRESS.             *
.*                                                                    *
.*       EXAMPLE..........                                            *
.*       MVC   DATADSN,=CL44'DSN1'                                    *
.*       WTO   MF=(E,DATA)                                            *
.*        .........                                                   *
.*        .........                                                   *
.*        .........                                                   *
.*        .........                                                   *
.*DATA   SMWTO 0302,U,ANYMESSAGE                                      *
.*                                                                    *
.*       GENERATES THE FOLLOWING MESSAGE AT THE OPERATOR CONSOLE      *
.*                                                                    *
.* SUM0302: U DSN1                                       ANYMESSAGE   *
.*                                                                    *
.**********************************************************************
         GBLC  &D,&E
&D       SETC  'DSN'
&E       SETC  'MDE'
&NAME    DS    0F
         DC    AL2(JWA&SYSNDX-*)
         DC    XL2'0'                     OPTION FLAGS
         DC    C'SUM&MESSNO.: '
&NAME&E  DC    C'&MESSMD '
         AIF   ( '&DSNF' EQ 'NODSN').NODSN
&NAME&D  DC    CL44' '
         DC    C' '
.NODSN   ANOP
         DC    C&MESS
JWA&SYSNDX EQU   *
         MEND
         EJECT
ON       EQU   X'F0'
OFF      EQU   X'00'
SCY03    BEGIN
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*        MAIN LOGIC SECTION.                                          *
*        ---- ----- --------                                          *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
*                                       *******************************
         TM    0(R1),X'80'              * TEST FOR VALID PARM         *
         BO    OKPARM                   *                             *
         WTO   MF=(E,MSG14)             * WRITE ILLEGAL PARM MESSAGE  *
         B     RETURN1                  *                             *
OKPARM   L     R3,0(R1)                 *  R3 HAS PARM                *
         SR    R6,R6                    * ZEROISE R6                  *
         LH    R6,0(R3)                 *  R6 HAS PARM LENGTH         *
         BCTR  R6,R0                    * SUBTRACT ONE FOR EXECUTES   *
         LA    R3,2(R3)                 *  R3 HAS START OP DSN'S      *
EXECTRAN TEST  LASTSWTC,ON              * LAST TIME THRO              *
         BON   NODLM                    * YES END                     *
         EX    R6,TRTFDLM               * SCAN FOR DELIMETER          *
         BNE   NOFLAG                   * FLAG THIS AS LAST TIME THRO *
         SWTCH LASTSWTC,ON              *                             *
         LA    R1,1(R6,R3)              * SIMULATE TRT ADDRESS        *
NOFLAG   SR    R1,R3                    * R1 HAS LENGTH               *
         ST    R1,ENDDSN                *  INTO ENDSN                 *
         SR    R6,R1                    * R6 HAS NEW REST OF PARM L   *
         BCTR  R6,R0                    * LENGTH - 1 FOR / OR COMMA   *
         BCTR  R1,R0                    * R1 HAS LENGTH-1             *
         MVC   GNM,GNM-1                * CLEAR DSNAME AREA           *
         EX    R1,MVTODSN               *  MOVE IN DSN                *
         LA    R3,2(R1,R3)              * R3 HAS START OF NEXT DSN    *
         LOCATE GINDEX                  * FIND DSN'S FIRST BLOCK IN CT*
         CLM   15,1,=X'0C'              *  CORRECT RC (SEE MSG 3)     *
         BNE   ERMSGP                   *   NO ILLEGAL DSN            *
         LA    R15,0                    * ZERO R15                    *
         CLI   GXAREA+21,X'0F'          * COMPLEMENTED 'F0' IN GEN NO *
         BH    ERMSGP                   *  FIELD,NO ERROR NOT GDG.    *
         MVC   HIGHX,GXAREA+20          *  SAVE THE  GXXXXVYY PART    *
         XC    HIGHX+1(4),=X'FFFFFFFF'  * DE-COMPLEMENT XXXX          *
         MVC   TEMPTTR,GXAREA+10        * SAVE LAST BLOCKS TTR        *
         CLI   GXAREA+31,X'01'          * BLOCK VCBPE OR DSPE         *
         BE    VCBPE                    *                             *
         MVC   SGXAR(62),GXAREA+32      * IF DSPE SAVE VOLS(< 5)      *
GETLOWST SWTCH SWTCH1,OFF               *  CLEAR  SWITCH              *
         MVC   TTR,TEMPTTR              * RETURN SAVED TTR            *
         LOCATE TTRLINK                 *  AND BRING IT INTO STORAGE  *
         LTR   R15,R15                  * LOCATE OK?                  *
         BNZ   ERMSGP                   *   NO  PRINT ERROR           *
SAMEBLK  LA    R7,GXAREA+2              * SCAN DOWN BLOCK FOR LAST ENT*
SRCHILE  SR    R8,R8                    *  ZERO R8                    *
         ICM   R8,1,11(R7)              *  INSERT BLOCK LENGTH        *
         AR    R8,R8                    *   DOUBLE IT                 *
         LR    R9,R7                    * SAVE THIS ENTRYS ADDRESS    *
         AR    R7,R8                    * R7 HAS NEXT ENTRYS ADDRESS  *
         LA    R7,12(R7)                *   NOW                       *
         CLC   0(8,R7),=8X'FF'          *   ILE?                      *
         BNE   SRCHILE                  *  NO INCREMENT AGAIN         *
         XC    1(4,R9),=4X'FF'          * DE-COMPLEMENT GNUMBER       *
         L     R7,ENDDSN                * FIND END OF DSNAME          *
         LA    R7,GNM(R7)               *      ADDRESS                *
         MVI   0(R7),C'.'               * MOVE IN SEPERATOR DOT       *
         MVC   1(8,R7),HIGHX            * MOVE HIGHEST GENERATION NO. *
         CATALOG UNCAT                  *  UNCATALOG IT               *
         LTR   R15,R15                  *  UNCATALOG OK?              *
         LA    R15,52(R15)              *  INCREMENT FOR ADDRESS TABLE*
         BNE   ERMSGP                   *   NO ERROR                  *
         PACK  DTARG,1(4,R9)            * SUBTRACT ONE FROM LOWEST    *
         SP    DTARG,=P'1'              *       GENERATION            *
         UNPK  1(4,R9),DTARG            *                             *
         OI    4(R9),X'F0'              * TURN SIGN TO RIGHT MODE FOR *
*                                       * EBCDIC TRANSLATION          *
         CLC   1(4,R9),=C'0000'         * ZERO GEN MADE               *
         BNE   MOVENDS                  *                             *
         MVC   MSG15DSN,GNM             * MOVE DSNAME                 *
         WTO   MF=(E,MSG15)             * WRITE MESSAGE               *
         B     EXECTRAN                 *                             *
MOVENDS  MVC   1(8,R7),0(R9)            *  MOVE TO DSNAME             *
         CATALOG CAT1                   *  CATALOG IT WITH HIGHEST    *
         LTR   R15,R15                  *     GENS VOLSER INFO        *
         LA    R15,52(R15)              *    IF CATALOG NOT OK GOTO   *
         BNE   ERMSGP                   *     ERRMSGP                 *
         B     EXECTRAN                 *  TRY FOR NEXT DSN           *
ERMSGP   MH    R15,=H'2'                * MULTIPLY BY TWO TO GET RIGHT*
         L     R1,WTOADS(R15)           *  OFFSET LOAD THE ADDRESSES  *
         LR    R3,R1                    * SAVE WTO ADDRESS            *
         L     R2,WTOADS+4(R15)         *                             *
         MVC   0(44,R2),GNM             *  AND MOVE DSN TO WTO        *
         WTO   MF=(E,(1))               *  WRITE MESSAGE              *
RETURN1  DS    0H                       *                             *
         CLI   13(R3),C'P'              * A PROGRAM ERROR             *
         BNE   NOABEND                  * DONT ABEND                  *
         ABEND 999,DUMP                 * ABEND IF NEEDED             *
NOABEND  L     R13,4(R13)               *    RETURN                   *
         RETURN (14,12),RC=(15)         *                             *
VCBPE    LA    R7,SGXAR                 * R7 HAS STRING ADDRESS       *
         MVC   TTR,GXAREA+28            *  MOVE VCB'S TTR             *
LOCTTR   LOCATE TTRLINK                 *  LOCATE FOR IT              *
         LTR   R15,R15                  *   LOCATE OK?                *
         BNE   ERMSGP                   *   NO ERROR                  *
         TEST  SWTCH1,ON                * FIRST TIME                  *
         BON   MOVE2                    *   YES                       *
         MVC   0(2,R7),GXAREA           * MOVE IN TOTAL NO OF VOLS    *
         LA    R7,2(R7)                 *  INCREMENT ADDRESS POINTER  *
         SWTCH SWTCH1,ON                *   TURN SWITCH ON            *
MOVE2    MVC   0(240,R7),GXAREA+2       *  MOVE VOLSER INFO           *
         LA    R7,240(R7)               *  INCREMENT ADDRESS POINTER  *
         ICM   R8,3,GXAREA              *  INSERT NO VOLSERS THIS BLK *
         CLM   R8,3,=XL2'14'            *    <20                      *
         BNH   GETLOWST                 *  NO MORE BLOCKS             *
         MVC   TTR,TTR1                 *  YES RETURN TO GETCOUST     *
         B     LOCTTR                   *                             *
NODLM    L     R13,4(R13)               *                             *
         RETURN (14,12),RC=0            *******************************
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*        ERROR MESSAGES SECTION.                                      *
*        ----- -------- --------                                      *
*                                                                     *
*        SMWTO IS ESSENTIALLY JUST A PREFORMATTED WTO MF=L            *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
MSGO     SMWTO 0300,U,'IS NOT A GDG DATA SET'
MSG1     SMWTO 0301,U,'CATALOGUE CONTROL VOLUME NOT MOUNTED'
MSG2     SMWTO 0302,U,'IS NOT CATALOGUED'
MSG3     SMWTO 0303,U,'DATA SET NAME NOT COMPLETE'
MSG4     SMWTO 0304,U,'CATALOGUE ENTRIES EXHAUSTED BEFORE LAST INDEX'
MSG5     SMWTO 0305,U,'SYNTAX ERROR'
MSG6     SMWTO 0306,S,'PERMANENT I/O ERROR'
MSG7     SMWTO 0307,PS,'TTR OUT OF SYSCTLG EXTENTS'
MSG8     SMWTO 0308,P,'INVALID WORK AREA POINTER'
MSG9     SMWTO 0309,P,'INVALID PARAMETER LIST'
MSG10    SMWTO 0310,P,'CATALOG STRUCTURE INCONSISTANT'
MSG11    SMWTO 0311,PS,'INDEX STRUCTURE NONEXISTANT'
MSG12    SMWTO 0312,S,'NO SPACE ON CATOLOG'
MSG13    SMWTO 0313,P,'ILLEGAL CATALOG ATTEMPT'
MSG14    SMWTO 0314,U,'NO/ILLEGAL PARM SUPPLIED',NODSN
MSG15    SMWTO 0315,U,'GDG ON LOWEST LIMIT'
WTOADS   DC    A(MSGO,MSGODSN)
         DC    A(MSG1,MSG1DSN)
         DC    A(MSG2,MSG2DSN)
         DC    A(MSG3,MSG3DSN)
         DC    A(MSG4,MSG4DSN)
         DC    A(MSG5,MSG5DSN)
         DC    A(MSG6,MSG6DSN)
         DC    A(MSG7,MSG7DSN)
         DC    A(MSG8,MSG8DSN)
         DC    A(0,0,0,0,0,0)
         DC    A(MSG9,MSG9DSN)
         DC    2A(0)
         DC    A(MSG1,MSG1DSN)
         DC    A(MSG10,MSG10DSN)
         DC    A(0,0)
         DC    A(MSG11,MSG11DSN)
         DC    A(MSG12,MSG12DSN)
         DC    A(MSG13,MSG13DSN)
         DC    A(MSG6,MSG6DSN)
         DC    A(0,0,0,0,0,0)
         DC    A(MSG9,MSG9DSN)
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*        DATA AREAS.                                                  *
*        ---- ------                                                  *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
TRTFDLM  TRT   0(0,R3),TRTABLE
SWTCH1   DC    X'00'
LASTSWTC DC    X'00'
TRTABLE  DC    256X'14'
         ORG   TRTABLE+C'@'
         DC    X'00'
         ORG   TRTABLE+C'Ö'
         DC    X'00'
         ORG   TRTABLE+C'#'
         DC    X'00'
         ORG   TRTABLE+C'A'
         DC    9X'00'
         ORG   TRTABLE+C'J'
         DC    9X'00'
         ORG   TRTABLE+C'S'
         DC    8X'00'
         ORG   TRTABLE+C'0'
         DC    10X'00'
         ORG   TRTABLE+C'.'
         DC    X'00'
         ORG   TRTABLE+X'C0'
         DC    X'00'
         ORG   TRTABLE+C','
         DC    X'04'
         ORG   TRTABLE+C'/'
         DC    X'04'
         ORG
ENDDSN   DC    A(0)
MVTODSN  MVC   GNM(1),0(R3)
         DC    C' '
GNM      DC    CL44' '
GXAREA   DS    0D
         DS    CL252
TTR1     DS    CL3
         DS    CL4
VOLSER   DS    CL6
TEMPTTR  DS    CL3
TTR      DS    CL3
HIGHX    DS    CL8
DTARG    DS    PL3
GINDEX   CAMLST NAME,GNM,,GXAREA
UNCAT    CAMLST UNCAT,GNM
CAT1     CAMLST CAT,GNM,,SGXAR
TTRLINK  CAMLST BLOCK,TTR,VOLSER,GXAREA
         LTORG
SGXAR    DS    CL3146
