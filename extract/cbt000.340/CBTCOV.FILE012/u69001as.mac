         TITLE 'CORE REDUCER'
SYSCORE START
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         LR    8,1                      SAVE R1
         L     1,0(1)                   ADDRESS OF PARM
         CLC   0(2,1),=2X'00'           ANY PASSED?
         BE    ERR1                     NO, ERROR
         LH    4,0(1)                   LENGTH OF PARM
         LA    3,2(1)                   ADDRESS PARM FIELD
         LR    5,3                      SAVE ADDR
LOOP     CLI   0(3),C','                END OF 1ST FIELD
         BE    FNDEND                   YES
         LA    3,1(3)                   UP TO NEXT CHAR
         BCT   4,LOOP
FNDEND   SR    3,5                      LENGHT OF 1ST FIELD
         LTR   3,3                      ZERO LENGTH?
         BZ    ERR1                     YES, ERROR
         CH    3,=H'8'                  GREATER THAN 8 CHARS
         BH    ERR2                     YES, ERROR
         BCTR  3,0                      SUBTRACT 1 FOR EX
         EX    3,MVCINS1                MOVE NAME
         LTR   4,4                      ANY MORE PARM?
         BZ    XCTL                     NO, XCTL
A05      BCT   4,A10                    SUBTRACT 1 FOR COMMA
         B     XCTL                     BRANCH IF NOW ZERO
A10      LA    3,4(3,1)                 UP PAST COMMA
         LR    5,3                      SAVE ADDRESS
KLOOP    CLI   0(3),C','                END OF FIELD
         BE    KEND                     YES, BRANCH
         TM    0(3),X'F0'               FIELD NUMERIC?
         BNO   ERR3                     NO, ERROR
         LA    3,1(3)
         BCT   4,KLOOP                  ROUND AGAIN
KEND     EQU   *
         SR    3,5                      FIND LENGTH
         LTR   3,3                      NO K SPECIFIED?
         BZ    XCTL05                   TRUE
         CH    3,=H'3'                  GREATER THAN 3 CHARS
         BH    ERR4                     YES, ERROR
         BCTR  3,0                      SUBTRACT 1 FOR EX
         EX    3,PACKINS                PACK VALUE
         CVB   7,DOUBLE                 CONVERT TO BINARY
         M     6,=F'1024'               GET NO. OF BYTES
         L     6,16                     CVT
         TM    116(6),X'22'             VS1?
         BNO   XCTL05                   NO, MFT - ALLOW WHOLE PARTITION
         L     6,0(6)                   TCB POINTER
         L     6,4(6)                   TCB
         L     6,24(6)                  BBX
         L     9,4(6)                   STARTING ADDR OF PARTITION
         L     10,8(6)                  ENDING ADDRESS OF PARTITION
         LA    9,0(9)
         LA    10,0(10)
         SR    10,9                     GET PARTITION SIZE
         SR    10,7                     SUBTRACT CORE REQUIRED FOR RUN
         BM    ERR4                     BRANCH PARTITION TOO SMALL
         BZ    XCTL05                   BRANCH PARTITION JUST RIGHT
         LR    0,10                     GETMAIN REMAINDER
         LR    6,1                      SAVE R1
         GETMAIN R,LV=(0)
         LR    1,6                      RESET R1
XCTL05   LTR   4,4                      PARMS TO PASS
         BZ    XCTL                     NO
         BCTR  4,0                      SUBTRACT 1 FOR COMMA
         LTR   4,4                      ANY PARMS TO PASS
         BZ    XCTL                     NO, BRANCH
         LA    3,2(3,5)                 UP PAST COMMA
         BCTR  4,0                      SUBTRACT 1 FOR EX
         EX    4,MVCINS2                MOVE PARM
         LA    4,1(4)                   UP 4
XCTL     STH   4,0(1)                   SET LENGTH OF PARM
         LA    0,NAME
         XR    1,1
         SVC   8                        LOAD
         LR    15,0
         LR    1,8                      SET R1
         BALR  14,15
         USING *,14
         CH    15,=H'20'
         BL    OK
         WTO   'REFER TO SOFTWARE DEPT',ROUTCDE=11
         ABEND 20,DUMP
OK       SVC   3
         DROP  14
ERR1     WTO   'NO MODULE NAME PASSED',ROUTCDE=11
         B     ERROR
ERR2     WTO   'MODULE NAME TOO LONG',ROUTCDE=11
         B     ERROR
ERR3     WTO   'CORE SIZE SPECIFIED IS NOT NUMERIC',ROUTCDE=11
         B     ERROR
ERR4     WTO   'CORE SIZE TOO LARGE',ROUTCDE=11
ERROR    LA    15,20
         SVC   3
DOUBLE   DS    D
NAME     DC    CL8' '
MVCINS1  MVC   NAME(0),0(5)
PACKINS  PACK  DOUBLE,0(0,5)
MVCINS2  MVC   2(0,1),0(3)
         LTORG
         END
