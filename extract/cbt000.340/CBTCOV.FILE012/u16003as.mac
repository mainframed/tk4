SU21GEN  ENTER ENT=IEBGENER,VERS=1.0
         TM    0(1),X'80'          ONLY 1 PARM?
         BO    NODDC               YES - DDNAMES OK
         L     2,4(,1)             PICK UP DDNAME LIST ADDR.
         LH    3,0(,2)             GET LENGTH
         CH    3,=H'32'
         BNH   NODDC
         LA    2,34(,2)            POINT TO SYSIN NAME
         SH    3,=H'32'            ADJUST COUNT
         CLC   0(8,2),=XL8'00'     CHANGE?
         BE    *+10                NO
         MVC   IN+40(8),0(2)       MOVE NEW NAME IN
         LA    2,24(,2)            UP TO SYSUT1
         SH    3,=H'24'
         BNP   NODDC
         CLC   0(8,2),=XL8'00'
         BE    *+10
         MVC   UT1+40(8),0(2)
         LA    2,8(,2)             UP TO SYSUT2
         SH    3,=H'8'
         BNP   NODDC
         CLC   0(8,2),=XL8'00'
         BE    NODDC
         MVC   UT2+40(8),0(2)
NODDC    RDJFCB (IN)
         LTR   15,15
         BNZ   WEDOIT
         CLC   INJFCB(44),=CL44'NULLFILE'
         BE    WEDOIT
BYEBYE   EQU   *
         LA    15,GOLIST
         L     13,4(0,13)
         L     14,12(0,13)
         LM    0,12,20(13)
         XCTL  MF=(E,(1)),SF=(E,(15))
WEDOIT   OPEN  (UT1)
F        LA    2,UT1
         OPEN  (UT2,(OUTPUT))
         TM    UT1+36,X'C0'
         BNO   *+8
         NI    SW1+1,X'0F'
         SR    2,2
READF    GET   UT1
         LA    2,1(,2)
         LR    0,1
SW1      B     *+10
         MVC   UT2+82(2),UT1+82
         PUT   UT2,(0)
         B     READF
EOFF     CLOSE (UT1)
         CLOSE (UT2)
EXIT     EQU   *
         RDJFCB (UT1,,UT2)
         TIOT
         CLC   16(8,1),=CL8' '
         BE    *+8
         LA   1,8(0,1)
         MVC   MSG2+15(8),8(1)
MSG2     WTP   'STEP - XXXXXXXX - REPORT'
         CVD   2,WD
         MVC   MSG+8(16),EDMSK
         ED    MSG+8(16),WD
         MVC   MSG+25(20),=CL20'RECORDS COPIED FROM'
         LA    3,MSG+45
         CLC   UT1JFCB+44(8),=CL8' '
         BE    DSET1
         TM    UT1JFCB+86,X'01'
         BZ    TRYGDG1
         MVC   0(7,3),=CL7'MEMBER'
         MVC   7(8,3),UT1JFCB+44
         MVC   16(3,3),=CL3'IN'
         LA    3,19(,3)
DSET1   EQU    *
        TM     UT1JFCB+52,X'20'
         BZ    NOTSIN
        MVC    0(18,3),=CL18'** SYSTEM INPUT **'
         B     BAL1
NOTSIN   MVC   0(8,3),=CL8'DATASET'
         MVC   8(44,3),UT1JFCB
BAL1    BAL    11,MSG
         MVC   MSG+8(107),=CL107' '
         MVC   MSG+42(2),=C'TO'
         LA    3,MSG+45
         CLC   UT2JFCB+44(8),=CL8' '
         BE    DSET2
         TM    UT2JFCB+86,X'01'
         BZ    TRYGDG2
         MVC   0(7,3),=CL7'MEMBER'
         MVC   7(8,3),UT2JFCB+44
         MVC   16(3,3),=CL3'IN'
         LA    3,19(,3)
DSET2   EQU    *
        TM     UT2JFCB+52,X'20'
         BZ    NOTSOUT
         MVC   0(19,3),=CL19'** SYSTEM OUTPUT **'
         B     BAL2
NOTSOUT MVC    0(8,3),=CL8'DATASET'
         MVC   8(44,3),UT2JFCB
BAL2     BAL   11,MSG
         LEAVE
TRYGDG1  TM    UT1JFCB+86,X'02'
         BZ    DSET1
         LA    4,UT1JFCB
         BAL   11,BLNKFND
         MVC   0(5,4),=C'(   )'
         MVC   1(3,4),UT1JFCB+44
         B     DSET1
TRYGDG2  TM    UT2JFCB+86,X'02'
         BZ    DSET2
         LA    4,UT2JFCB
         BAL   11,BLNKFND
         MVC   0(5,4),=C'(   )'
         MVC   1(3,4),UT2JFCB+44
         B     DSET2
BLNKFND  CLI   0(4),C' '
         BER   11
         LA    4,1(0,4)
         B     BLNKFND
MSG      WTP   '                                                       X
                                                                      '
         BR    11
         EJECT
         PUSH  USING
         DROP
         USING *,15
UT1DCBX  TM    36(1),X'FF'
         BNZ   *+8
         MVI   36(1),X'C0'
         CLC   62(2,1),=H'0'
         BNE   *+10
         MVC   62(2,1),=H'32760'
         CLC   82(2,1),=H'0'
         BNE   *+10
         MVC   82(2,1),=H'32760'
         BR    14
         DROP
         SPACE 5
IUL      BALR  12,0
         USING *,12
         LR    11,1
         LR    9,14
         GETMAIN R,LV=84
         CLC   ULAD(4),=F'0'
         BNE   GETADDR1
         ST    1,ULAD
BACK1    MVC   0(4,1),=F'0'
         L     10,0(,11)
         MVC   4(80,1),0(10)
         LR    14,9
         LA    15,4
         BR    14
GETADDR1 L     2,ULAD
LOOPIA   CLC   0(4,2),=F'0'
         BE    GOT1
         L     2,0(0,2)
         B     LOOPIA
GOT1     ST    1,0(0,2)
         B     BACK1
         EJECT
         DROP
         USING *,15
DCBEXIT  TM    36(1),X'FF'
         BNZ   *+10
         MVC   36(1,1),36(2)
         CLC   62(2,1),=H'0'
         BNE   *+10
         MVC   62(2,1),62(2)
         CLC   82(2,1),=H'0'
         BNE   *+10
         MVC   82(2,1),82(2)
         BR    14
         DROP
         SPACE 5
OUL      BALR  12,0
         USING *,12
         LR    9,14
         L     10,ULAD
         LTR   10,10
         BZ    FINIO
         L     1,0(0,1)
         MVC   0(80,1),4(10)
         MVC   ULAD(4),0(10)
         FREEMAIN R,LV=84,A=(10)
         LA    15,8
         LR    14,9
         BR    14
FINIO    SR    15,15
         LR    14,9
         BR    14
         DROP
         POP   USING
         EJECT
         LTORG
         DS    0D
WD       DS    D
ULAD     DC    F'0'
GOLIST   XCTL  EP=IBMGENER,SF=L
IN       DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL,EXLST=INJFCBA
UT1      DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,BFTEK=A,EODAD=EOFF,     X
               EXLST=UT1JFCBA
UT2      DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,EXLST=UT2JFCBA
INJFCBA  DC    X'87',AL3(INJFCB)
UT1JFCBA DC    X'07',AL3(UT1JFCB)
         DC    X'01',AL3(IUL)
         DC    X'03',AL3(IUL)
         DC    X'85',AL3(UT1DCBX)
UT2JFCBA DC    X'07',AL3(UT2JFCB)
         DC    X'02',AL3(OUL)
         DC    X'04',AL3(OUL)
         DC    X'85',AL3(DCBEXIT)
INJFCB   DS    44F
UT1JFCB  DS    44F
UT2JFCB  DS    44F
EDMSK    DC    X'40',13X'20',X'2120'
         END
