SRE03    START
         TITLE 'RAAJ8SRE   M03    SELECTIVE RECORD EDIT  - SELECT RECORC
               D MODULE  -  D.E.MILLS'
         INMD
         LR    3,1
         USING QDUMMY,3
         L     INPUT,QINPUT            LOAD INPUT INDEX
         L     BLTH,QBLTH              LOAD INPUT BLOCK LENGTH
         L     BLTH,0(BLTH)
         L     4,QPARAM                LOAD ADDR OF INPUT PARAMS
         USING SPARAM,4
         L     BLNOAD,QBLNO            LOAD ADDRESS OF BLOCK NO.
         CLI   STYPE,C'V'              INPUT VARIABLE ?
         BNE   Z02A                    NO - Z02A
         MVC   HWORD,0(INPUT)          YES - CHECK LOGICAL LENGTH
         CH    BLTH,HWORD                   = PHYSICAL LENGTH
         BNE   Z05CA
         LA    INPUT,4(INPUT)                & SKIP PAST BLOCK LENGTH
         SH    BLTH,=H'4'
Z02A     BC    0,Z03A
*
* CODING ENTERED FIRST TIME ONLY TO SET UP COMPARES
*
         CLC   SKDISP,=X'FFFF'         KEY SPECIFIED ?
         BE    Z02B                    NO - Z02B
         OC    KEYTEST+1(3),SKEY       MOVE KEY & DISP. TO TEST
         NI    Z03A+1,X'0F'            & MAKE KEY TEST OPERATIVE
Z02B     CLC   SCDISP,=X'FFFF'         CONTROL FIELD SPECIFIED ?
         BE    Z02CA                   NO - Z02CA
         NI    Z03B+1,X'0F'            YES - MAKE TESTS OPERATIVE
         OC    RECTEST+1(3),SCLTH      MOVE CONTROL LTH & DISP TO TEST
         CLI   SFORMAT,C'P'            PACKED DEC. CONTROL FIELD
         BNE   Z02BA                   NO - Z02BA
         MVI   RECTEST,X'F9'           CHANGE COMPARE TO CP
         MVO   RECTEST+1(1),RECTEST+1(1)  & SET UP MACHINE LENGTH
*
Z02BA    CLI   SSEARCH,C'B'            BLOCK EDIT ?
         BNE   Z02C                     NO - Z02C
         MVI   RECTEST+2,BLNOAD*16     CHANGE TEST TO COMPARE
         MVI   RECTEST+3,0               WITH BLOCK NUMBER
         OI    Z03A+1,X'F0'              & MAKE KEY TEST INOPERATIVE
*
Z02C     MVC   RECTESTA(4),RECTEST     DUP. IN OTHER TEST
*
Z02CA    OI    Z02A+1,X'F0'
*
*CODING TO DECIDE IF INPUT IS TO BE EDITED
*
Z03A     BC    15,Z03B                 SKIP KEY TEST
KEYTEST  CLI   0(INPUT),0              C. INPUT TO SPECIFIED KEY
         BNE   Z05A                    NE - NEXT RECORD
*
Z03B     BC    15,Z04A                 SKIP CONTROL TESTS
RECTEST  CLC   0(1,INPUT),SLOW         C. INPUT TO LOW CONTROL
         BE    Z04A                    EQ - EDIT
         BL    Z05A                    LO - NEXT RECORD
RECTESTA CLC   0(1,INPUT),SHIGH        C. INPUT TO HIGH CONTROL
         BNH   Z04A                    LO OR EQ - EDIT
         CLI   SSEARCH,C'S'            IF NOT SEARCH
         BE    Z05A                     SET ON FOUND INDIC
         L     GRB,QINDIC               & EXIT ROUTINE
         MVI   0(GRB),C'F'
         B     Z05D
*
* EDIT RECORD
*
Z04A     BAL   RETURN,TSTRL            CHECK RECORD LENGTH
         ST    INPUT,QRECORD           STORE ADDRESS OF RECORD
         LR    1,3
         DOMD  SRE04                   ENTER EDIT & PRINT MODULE
*
* ACCESS NEXT RECORD
*
Z05A     BAL   RETURN,TSTRL            CHECK RECORD LENGTH
         CLI   STYPE,C'V'              VARIABLE ?
         BNE   Z05B                    NO -
         MVC   HWORD,0(INPUT)          YES - STORE RECORD LENGTH
         B     Z05C
*
Z05B     CLI   STYPE,C'F'              FIXED ?
         BNE   Z05D                    NO - MUST BE UNDEF SO EXIT
         MVC   HWORD,SRLTH             YES - STORE RECORD LENGTH
Z05C     SH    BLTH,HWORD              SUB.RECLTH FROM BLOCK LENGTH
         BZ    Z05D                    BR OUT IF ZERO
         BM    Z05CA                   BR TO INV.BLOCK RTN IF _VE
         AH    INPUT,HWORD             ADD RECLTH TO INPUT INDEX
         B     Z03A
*
Z05CA    MVC   QRECORD,QINPUT
         MVC   Z05CB+1(1),STYPE        STORE FILE TYPE
         MVI   STYPE,C'X'              INVALID BLOCK TYPE
         LR    1,3
         DOMD  SRE04                   EDIT INVALID BLOCK
Z05CB    MVI   STYPE,C' '              RESTORE FILE TYPE
*
Z05D     OUTMD
         SPACE 5
TSTRL    CLI   STYPE,C'V'
         BNE   0(RETURN)
         MVC   HWORD,0(INPUT)          TEST VALIDITY OF RECORD
         LH    GRA,HWORD                LENGTH.
         LTR   GRA,GRA                 IF ZERO OR -VE
         BNP   Z05CA                     - INVALID BLOCK
         CR    GRA,BLTH                C. TO REMAINING BLOCK LENGTH
         BH    Z05CA                    HI- INVALID BLOCK
         BR    RETURN                   LO OR EQ- OK
         EJECT
HWORD    DS    H
*
* DUMMY SECTION FOR INTER MODULE PARAMS.
*
QDUMMY   DSECT
QPARAM   DS    F
QINDIC   DS    F
QINPUT   DS    F
QBLTH    DS    F
QLINE    DS    F
QCOUNT   DS    F
QRECORD  DS    F
QBLNO    DS    F
*
* DUMMY SECTION FOR INPUT PARAMS.
*
SPARAM   DSECT
STYPE    DS    C
         DS    C
SRLTH    DS    H
SFORMAT  DS    C
SCLTH    DS    C
SCDISP   DS    H
SSEARCH  DS    C
SKEY     DS    C
SKDISP   DS    H
SLOW     DS    20C
SHIGH    DS    20C
SRE03    CSECT
INPUT    EQU   6                       G.R. FOR INPUT INDEX
BLTH     EQU   7                       G.R. FOR INPUT BLOCK LENGTH
BLNOAD   EQU   8                       G.R.FOR ADDR OF BLOCK NO.
RETURN   EQU   14                      RETURN REG. FROM SUB-RTNS
GRA      EQU   2                       G.R. FOR MISC USE
GRB      EQU   15                      G.R. FOR MISC. USE
NP       EQU   13                      BNP AFTER ARITHMETIC.
         END
