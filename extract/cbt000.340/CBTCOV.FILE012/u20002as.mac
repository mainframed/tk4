ALLOCATE CSECT
*        TO MARK UNIT RECORD DEVICE UCBS AS ALLOCATED OR NOT ALLOCATED.
*        THIS PROGRAM IS INVOKED BY -
*
*              LINK EP=ALLOCATE,PARAM=(DD1,DD2...),VL=1,ID=N
*
*        WHERE ID = 0 TO DEALLOCATE, 4 TO ALLOCATE
*
*        RETURN CODES ARE -
*              0 = REQUESTED FUNCTION COMPLETE
*              4 = DDNAME NOT IN TIOT
*              8 = DEVICE FOUND NOT UNIT RECORD
*             12 = DEVICE ALREADY ALLOCATED (ID=4 ONLY)
*             16 = INVALID ID
*
*        ANY DDNAME THAT IS DUMMIED IS IGNORED
*        THIS ROUTINE USES SVC253 WHICH IS A BRANCH TO R1.
*
*
         HSAVE
         ST    1,PARM                  SAVE PARM ADDR
         STH   14,ID                   SAVE ID
         CLC   0(2,14),SVC3            WAS I LINKED TO ?
         BNE   NOTLINK                 NO - ASSUME CALL
         L     1,16                    -> CVT
         L     1,0(1)                  -> TCB DOUBLEWORDS
         L     1,4(1)                  -> MY TCB
         L     1,0(1)                  -> TOP RB
         L     1,28(1)                 BACK UP 1 RB
         L     1,20(1)                 -> RESUME PSW INSTR ADDRESS
         MVC   ID(2),2(1)              PICK OUT ID
NOTLINK  EQU   *
         CLC   ID,H0                   DEALLOCATE ?
         BE    LAB1
         CLC   ID,H4                   ALLOCATE ?
         BE    LAB2
         LA    15,16                   SET RETURN CODE
         B     RETURN
LAB1     MVC   INSTR,NI                SET NI INSTRUCTION
         MVI   SW+1,X'00'              FORCE NO BRANCH
         B     LAB3
LAB2     MVC   INSTR,OI                MOVE OI INSTRUCTION
         MVI   SW+1,X'10'              FORCE BR IF ONES
LAB3     EXTRACT TIOTADDR,FIELDS=(TIOT)
         LA    1,SVCRTN
         SVC   253
RETURN   L     13,HSAVE+4
         RETURN (14,12),RC=(15)
         TITLE 'SVC ROUTINE'
SVCRTN   BALR  12,0
         USING *,12
         XC    UCBTAB,UCBTAB           CLEAR UCB ADDRESSES
         LA    2,UCBTAB                INIT START ADDR
         L     3,PARM                  GET PARM ADDR
         SPACE 1
LOOP1    L     6,0(3)                  GET PTR TO DDN IN PARM
         L     4,TIOTADDR              GET TIOT START ADDR
         LA    4,24(4)                 BUMP PAST JOBNAME ETC
         SR    5,5                     PREPARE FOR IC
         SPACE 1
LOOP2    IC    5,0(4)                  GET LTH OF DD ENTRY
         CLC   0(4,4),END              TEST FOR END OF TIOT
         BE    ERROR1                  YES - NOT IN TIOT
         CLC   0(8,6),4(4)             CHECK DD NAMES
         BE    GOTUCB                  BRANCH IF MATCH
         LA    4,0(5,4)                POINT TO NEXT TIOT ENTRY
         B     LOOP2                   LOOP THROUCH TIOT
         SPACE 1
GOTUCB   LH    7,18(4)                 GET UCB ADDR FROM TIOT
         LTR   7,7                     CHECK IF ALLOCATED
         BZ    DUMMY                   DUMMY FILE IF ZERO
         CLI   18(7),X'08'             UNIT RECORD DEVICE ?
         BNE   ERROR2                  NO - THEN ERROR
         TM    3(7),X'08'              TEST IF ALLOCATED
*
*        THE NEXT INSTRUCTION IS NULLIFIED IF A DEALLOCATE REQUEST
*
SW       BO    ERROR3                  YES - ERROR
         STH   7,0(2)                  SAVE UCB ADDRESS
         LA    2,2(2)                  BUMP UCB TABLE POINTER
DUMMY    LTR   6,6                     CHECK END OF PARM LIST
         BM    ALLDONE                 BRANCH IF ALL DONE
         LA    3,4(3)                  POINT TO NEXT IN LIST
         B     LOOP1                   LOOP ROUND AGAIN
         TITLE 'ALLOCATE OR DEALLOCATE'
ALLDONE  LA    2,UCBTAB                GET ADDRESS OF UCB TABLE
LOOP3    CLC   0(2,2),END              END OF LIST
         BE    EXIT
         LH    3,0(2)                  PULL OUT UCB ADDRESS
INSTR    DS    CL4                     ALLOCATE/DEALLOCATE
         LA    2,2(2)                  BUMP TABLE POINTER
         B     LOOP3
EXIT     SR    15,15
         BR    14
ERROR1   LA    15,4                    DDNAME NOT IN TIOT
         BR    14
ERROR2   LA    15,8                    DEVICE NOT UNIT RECORD
         BR    14
ERROR3   LA    15,12                   DEVICE ALREADY ALLOCATED
         BR    14
         TITLE 'CONSTANTS ETC'
END      DC    F'0'
TIOTADDR DS    F                       TIOT ADDRESS
PARM     DS    F                       PARM ADDRESS
ID       DS    H
SVC3     DC    X'0A03'                 SVC3 FORMAT
H0       DC    H'0'
H4       DC    H'4'
OI       OI    3(3),X'08'              TURN ON ALLOCATE BIT
NI       NI    3(3),X'F7'              TURN OFF ALLOCATE BIT
UCBTAB   DS    CL20
         END
