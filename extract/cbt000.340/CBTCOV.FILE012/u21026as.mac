* NAME         ZR37   ALIAS  ZRELEASE
*
* VERSION      1
*
**   AUTHORS - I.C.I. (MOND DIVISION)
*
* STATUS       CHANGE LEVEL - 1.
*
* SIZE         READ-ONLY SECTION         -  228 BYTES
*              DYNAMIC WORKAREAS         -  112 BYTES
*
* FUNCTION     THIS PROGRAM RELEASES A JOB FROM THE JOB QUEUE.  THE
*              NAME OF THE JOB TO BE RELEASED IS PASSED TO THE PROGRAM
*              AS A PARAMETER.
*              ZR37 WILL DISPLAY A MESSAGE ON THE CONSOLE GIVING THE
*              NAME OF THE JOB BEING RELEASED & THE NAME OF THE JOB
*              RELEASING IT.
*
* ENTRY POINT  ZR37
*
* INPUT        REGISTER 1 POINTS TO THE ADDRESS OF A VARIABLE LENGTH
*              FIELD WHOSE 1ST TWO BYTES SPECIFY ITS LENGTH & THE
*              REMAINDER THE NAME OF THE JOB TO BE RELEASED.
*
* OUTPUT       THE ONLY OUTPUT OF ZR37 IS THE MESSAGE MENTIONED ABOVE.
*
* EXTERAL MODULES
*              NONE.
*
* EXIT         RETURN IS NORMAL, BY REGISTER 14, WITH A RETURN CODE OF
*              ZERO.
*
* WORKAREAS    THESE ARE OBTAINED FROM DYNAMIC STORAGE USING THE
*              GETMAIN MACRO INSTRUCTION.
*
* ATTRIBUTES   READ-ONLY, RE-ENTRANT.
*
* PRIVATE MACROS
*              NONE.
*
* ASSEMBLY REQUIREMENTS
*              ASSEMBLER WITH PSEUDO-REGISTER SUPPORT-
*
* NOTES        ZR37 RELEASES A JOB USING THE STANDARD COMMAND
*              PROCESSING SVC, SVC34.   REGISTER 1 POINTS TO CONTROL
*              BLOCK IN THE FORMAT
*
*                   BYTE  0 & 1    LENGTH OF BLOCK.
*                         2 & 3    MUST BE ZERO
*                         4 ONWARD COMMAND TO BE PROCESSED.
*
*              O/S REQUIRES THAT THE TASK ISSUING SVC 34 OPERATE IN
*              PROTECT KEY ZERO.   TO CHANGE THE PROTECT KEY TO ZERO,
*              ZR37 ISSUES THE SYNCH SVC, SVC12, THUS ADDING AN
*              EXTRA PRB TO THE CHAIN, & BRANCHING TO A ROUTINE WHICH
*              CHANGES THE PROTECT KEY OF THE RESUME PSW IN THE
*              PREVIOUS PRB & RETURNS VIA SVC3 TO THE INSTRUCTION
*              FOLLOWING THE SVC12.
*
         EJECT
* WORKAREAS OBTAINED FROM DYNAMIC STORAGE
*
DYNAMIC  DSECT
*
SAVE     DS    18F       REGISTER SAVE AREA.
*
CB       EQU   *         CONTROL BLOCK FOR WTO AND SVC 34.
LENGTH   DS    AL(2)
ZERO     DS    AL(2)
MSG      DS    CL36
*
*
         ORG   MSG       MASK FOR WTO LIST.
WTOCON1  DS    CL9
WTOJBNM1 DS    CL8
WTOCON2  DS    CL11
WTOJBNM2 DS    CL8
*
*
         ORG   MSG       MASK FOR SVC 34 LIST.
RELCON1  DS    CL8
RELJBNM  DS    CL8
*
*
ZR37     CSECT
*
*
*REGISTERS
*
CR       EQU   15   RETURN CODE REGISTER.
RR       EQU   14   RETURN REGISTER.
SR       EQU   13   SAVE AREA REGISTER.
BR       EQU   12   BASE REGISTER.
R0       EQU   0    REGISTER 0.
R1       EQU   1    REGISTER1.
WR       EQU   2    WORK REGISTER.
PR       EQU   3    PARAMETER REGISTER
TR       EQU   4    ADDRESS OF TCB.
DR       EQU   5    DYNAMIC STORAGE BASE REGISTER.
LR       EQU   6    LENGTH OF JOB NAME REGISTER.
*
*
         STM   RR,BR,12(SR)             SAVE REGISTERS.
         BALR  BR,0                     LOAD BASE REGISTER.
         USING *,BR                     ESTABLISH ADDRESSABILITY.
         LR    PR,R1                    SAVE REGISTER 1.
         L     R0,CXD                   GET DYNAMIC STORAGE LENGTH.
         GETMAIN R,LV=(0)               GET STORAGE.
         LA    DR,0(R1)                 GET
         ORG   *-2                      DYNAMIC STORAGE
         DC    QL2(DYNAMIC)             ADDRESS.
         USING DYNAMIC,DR               ESTABLISH ADDRESSABILITY.
         LA    WR,SAVE                  GET NEW SAVE AREA ADDRESS.
         ST    SR,4(WR)                 STORE OLD S.A. ADDRESS.
         ST    WR,8(SR)                 STORE NEW S.A.ADDRESS.
         LR    SR,WR                    UPDATE SAVE AREA REGISTER.
         L     WR,16                    GET CVT ADDRESS.
         L     WR,0(WR)                 GET NEW/OLD AREA ADDRESS.
         L     TR,4(WR)                 GET TCB ADDRESS.
         L     WR,12(TR)                GET TIOT ADDRESS.
         MVC   WTOJBNM1,0(WR)           OUR JOBNAME TO WTO BLOCK.
         MVC   WTOCON1,MSGID            MESSAGE TO WTO BLOCK
         MVC   WTOCON2,MSGCON           'RELEASING' TO WTO BLOCK.
         L     PR,0(PR)                 GET PARM VALUE ADDRESS.
         LH    LR,0(PR)                 GET LENGTH OF PARM FIELD.
         CH    LR,EIGHT                 IS LENGTH LESS THAN 8?
         BNH   *+8                      YES
         LH    LR,EIGHT                 NO. LOAD LENGTH OF 8.
         BCTR  LR,0                     GET OBJECT LENGTH.
         EX    LR,MVC1                  MOVE JOB NAME TO WTO BLOCK.
         LA    WR,33(LR)                GET LENGTH FOR WTO BLOCK.
         STH   WR,LENGTH                STORE LENGTH IN WTO BLOCK.
         MVC   ZERO,NOUGHT              WRITE IN ZERO FIELD.
         LA    R1,CB                    GET CONTROL BLOCK ADDRESS.
         SVC   35                       ISSUE WTO SVC.
         MVI   MSG,BLANK                BLANK IN 1ST BYTE.
         MVC   MSG+1(24),MSG            ROLL.
         MVC   RELCON1,RELEASE          MOVE 'RELEASE' TO SVC 34 BLOCK.
         EX    LR,MVC2                  MOVE IN JOB NAME.
         LA    WR,13(LR)                GET LENGTH OF COMMAND.
         STH   WR,LENGTH                STORE IN CONTROL BLOCK.
         L     WR,0(TR)                 GET 1ST RB ADDRESS.
         LA    CR,KEYSW                 LOAD ROUTINE ADDRESS.
         SVC   12                       ISSUE SYNCH SVC.
         XR    R0,R0                    SET PARM REG ZERO.
         LA    R1,CB                    GET CONTROL BLOCK ADDRESS.
         SVC   34                       ISSUE COMMAND PROCESS SVC.
         L     SR,4(SR)                 GET OLD S.A. ADDRESS.
         L     R0,CXD                   GET LENGTH OF DYNAMIC STORAGE
         LA    R1,DYNAMIC               GET DYN STORAGE ADDRESS.
         FREEMAIN R,LV=(0),A=(1)        RELEASE DYNAMIC STORAGE
         LM    RR,BR,12(SR)             RESTORE REGISTERS.
         XR    CR,CR                    SET RETURN CODE.
         BR    RR                       RETURN.
*
* THIS ROUTINE IS ENTERED AS A RESULT OF THE SYNCH SVC WHICH HAS ALSO
* BUILT ANOTHER PRB.
KEYSW    NI    17(WR),X'0F'             SET PROTECT KEY TO ZERO.
         SVC   3                        EXIT .
*
* THESE TWO INSTUCTIONS ARE REFERENCED BY EXECUTE INSTRUCTIONS.
MVC1     MVC   WTOJBNM2(0),2(PR)        THIS MOVES JOBNAME TO WTO BLOCK
MVC2     MVC   RELJBNM(0),2(PR)         JOBNAME TO SVC 34 BLOCK.
*
RELEASE  DC    CL8'RELEASE '
MSGID    DC    CL9'ZR3701I  '
MSGCON   DC    CL11' RELEASING '
EIGHT    DC    H'8'
NOUGHT   DC    H'0'
BLANK    EQU   X'40'
CXD      CXD
         END
