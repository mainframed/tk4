SRE01    START
         TITLE 'RAAJ8SRE M01 - SELECTIVE RECORD EDIT - CONTROL MODULE'
         LNKMD LEVELS=4,SUPPORT=NO
         ST    1,ST1
         LA    1,FIMPRMS               LOAD ADDR OF I-M PARAMS.
         MVI   XINDS+1,C'O'            CALL FCP
         DOMD  SRE00                     TO OPEN FILES
         DOMD  SRE05                   PRINT HEADINGS
*
** OUTPUT D/SET IDENTIFIER LINE
*
         MVI   LINE,X'02'
         MVC   LINE+8(9),=C'DATASET -'
         MVC   LINE+19(59),TINFO       MOVE D/SET INFO.
         L     GRA,ST1
         L     GRA,0(GRA)              )
         LH    0,0(GRA)                ) MOVE PARM.
         AH    0,=H'1'                 ) INFORMATION
         STC   0,Z01B+1                ) TO
Z01B     MVC   LINE+80(1),0(GRA)       ) OUTPUT
         MVI   XINDS+1,C'P'            ) PRINT
         DOMD  SRE00                   ) LINE
*
         MVI   XINDS+1,C'S'            ) ACCESS
         DOMD  SRE00                   ) PARAMS.
         CLC   ARTP,=4X'00'            ANY PARAMS.
         BNE   Z01C                    YES
         MVC   ARTP,=A(CEND)           NO - SET UP ADDR. OF END
*
Z01C     DOMD  SRE02
         CLI   XINDS+2,C'C'            PARAMS. COMPLETE ?
         BNE   Z03A                    NO - END OF RUN
         CLI   XINDS+4,C'I'            ISAM INPUT ?
         BNE   Z02A
         MVI   PARAMS,C'I'              YES - RECORD IN PARAMS
         EJECT
*
** MAIN EDIT LOOP
*
Z02A     MVI   XINDS+1,C'G'            READ A
         DOMD  SRE00                      BLOCK
         CLI   XINDS+1,C'E'            E.O.F. ?
         BNE   Z02B                    NO - Z02B
*
Z02AC    MVC   CSTHIGH,=20X'FF'        STORE VERY HIGH CONTROL
         MVC   LINE+11(8),=C'END FILE'       AND OUTPUT FILE-END
         MVI   XINDS+1,C'P'                  COMMENT
         DOMD  SRE00
         B     Z02D
*
Z02B     AP    PBLNO,=P'1'             ENTER SELECT & EDIT MODULES
Z02C     DOMD  SRE03                   )
         CLI   XINDS,C'F'              HIGH CONTROL FOUND ?
         BNE   Z02A                    NO - READ NEXT BLOCK
         MVC   CSTHIGH,CHIGH           YES - STORE HIGH CONTROL
         MVC   LINE+11(8),=C'END EDIT'       AND OUTPUT END OF EDIT
         MVI   XINDS+1,C'P'                  COMMENT
         DOMD  SRE00
Z02D     DOMD  SRE05                       PRINT HEADINGS
         NI    XPIND,X'DF'             SET OFF RECORD RECEIVED IND.
         DOMD  SRE02                   GET & VET NEXT PARAMS.
         CLI   XINDS+2,C'C'            PARAMS COMPLETE ?
         BNE   Z03A                    NO - END OF RUN
         MVI   XINDS,C' '              CLEAR HIGH FOUND IND.
         CLC   CLOW,CSTHIGH            IS NEW LOW BEFORE OLD HIGH
         BH    Z02C                    NO - CONTINUE
         MVI   XINDS+1,C'R'            YES - REOPEN
         DOMD  SRE00                      INPUT FILE  &
         ZAP   PBLNO,=P'0'                START AGAIN
         B     Z02A
         EJECT
*
** END OF RUN
*
Z03A     MVC   LINE+11(8),=C'END RUN'
         MVI   XINDS+1,C'P'
         DOMD  SRE00
         MVI   XINDS+1,C'C'
         DOMD  SRE00
*
         L     13,4(13)
         LM    14,12,12(13)
         SR    15,15                   ZERO RETURN CODE            0673
         BR    14
*
         LTORG
*
         EJECT
*
** INTER-MODULAR PARAMETER TABLE
*
FIMPRMS  DS    0F
APARAM   DC    A(PARAMS)     ADDRESS OF SELECTION PARAMETERS
AINDIC   DC    A(XINDS)                 INDICATORS
AINPUT   DC    A(0)                     INPUT BLOCK
ABLTH    DC    A(FRECSZ)                LTH. OF INPUT BLOCK
ALINE    DC    A(CLINE)                 OUTPUT AREA
ACOUNT   DC    A(HLNCT)                 LINE COUNT  (H-WORD)
ARECORD  DC    A(0)                     RECORD TO BE EDITED
ABLNO    DC    A(PBLNO)                 BLOCK NO.
ARTP     DC    A(0)                     RUN-TIME PARAMETERS
APGNO    DC    A(PPGNO)                 PAGE NO.
ATINFO   DC    A(TINFO)
*
* SELECTION PARAMETER TABLE
*
PARAMS   DS    0H
         DC    C'U'                    FILE TYPE (V,F,U,I)
XPIND    DC    X'00'                    INDS FOR USE BY VET MODULE
         DC    H'0'                     REC.LTH (F ONLY)
         DC    C'C'                     CONTROL FORMAT (X,C,P)
         DC    X'00'                    CONTROL LENGTH (M/C LTH)
         DC    X'FFFF'                  CONTROL DISP. (F'S IF NONE)
         DC    X'00'                    SEARCH  IND(S) OR BLOCK IND(B)
         DC    X'00'                    KEY (IF SPECIFIED)
         DC    X'FFFF'                  KEY DISP. (F'S IF NONE)
CLOW     DC    XL20'0'                  LOW CONTROL
CHIGH    DC    XL20'0'                  HIGH CONTROL
*
XINDS    DC    X'00'      MODULE 03 'F'- HIGH CONTROL FOUND
         DC    X'00'      MODULE 00 'G'- GET,'P'- PUT,'O'- OPEN
*                                   'C'- CLOSE,'R'- REOPEN,
*                                   'S'- SYSIN,'W'- WRITE OUTF
         DC    X'00'      MODULE 02 'C'- COMPLETE,'E'- END OF RUN
         DC    X'00'         MODULE 00 'P'- ERROR ON INPUT
         DC    X'00'         MODULE 00 'I'- ISAM INPUT DATASET
*
FRECSZ   DC    F'0'                    STORE FOR RECSIZE
ST1      DC    F'0'
TINFO    DS    CL59
*
CLINE    DC    X'81'
         DC    160C' '                 ONLY 132 COLS USED
LINE     EQU   CLINE
*
HLNCT    DC    H'0'
*
PBLNO    DC    PL3'0'
*
PPGNO    DC    PL3'0'
*
* STORAGE EXCLUSIVE TO CONTROL MODULE
*
DWORD    DC    D'0'
FWORD    DC    F'0'
HWORD    DC    H'0'
RUNNO    DS    CL5
CEND     DC    C' END  '
CSTHIGH  DC    XL20'0'
GRA      EQU   14
GRB      EQU   15
         END   SRE01
