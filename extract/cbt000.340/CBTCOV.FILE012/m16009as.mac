         MACRO
&B       IFOPEN &DCB,&NOT=,&FLAG=,&OK=,&REPORT=,&MSG=
         COPY  JGLOBAL
         LCLC  &G,&H
         LCLA  &NOPS,&KOP
         LCLB &BOPX,&SETOK
         AIF   (N'&DCB GT 1).MULTI
&B       TM    &DCB+48,X'10' .         IS DCB OPEN
         AIF   ('&CNAME' NE '').GO1
&CNAME   SETC  '$$'
.GO1     AIF   (T'&NOT EQ 'O').GO2
         BZ    &NOT .                  NO
         MEXIT
.GO2     AIF   (T'&OK EQ 'O').GO3
         BO    &OK .                   YES
         MEXIT
.GO3     AIF   (T'&FLAG EQ 'O').GOOUT
         AIF   (N'&FLAG EQ 2).GO4
         MNOTE 4,'FLAG OPTION REQUIRES 2 OPERANDS'
         MEXIT
.GO4     BZ    &CNAME&SYSNDX .         NO
         OI    &FLAG(2),&FLAG(1) .     SET FLAG MARKER.
&CNAME&SYSNDX EQU  *
         MEXIT
         AIF  ((T'&REPORT NE 'O') AND (T'&MSG NE 'O')).E2
.GOOUT   AIF  ((T'&REPORT EQ 'O') AND (T'&MSG    EQ 'O')).ERROR
         BO    &CNAME&SYSNDX .             DCB OPEN
         GENMSG
         L     1,=A(&CNAME.EWTO)
&G       SETC '&MSG'
         USING &CNAME.EWTO,1
         MVC   &CNAME.EDDNM(8),&DCB+40 .     COPY DDNAME
         AIF   (T'&REPORT EQ 'O').WTO
&G       SETC  '&REPORT(1)'
&H       SETC  'E'
         AIF   ((N'&REPORT  NE 2) AND ('&REPORT(2)'NE 'ABORT')).RGEN
&H       SETC 'T'
.RGEN    REPORT &CNAME.MSGX,&CNAME&H.MSG-&CNAME.MSGX-1
         AGO   .DROP1
.WTO     SVC   35
.DROP1   DROP  1
         AIF   ('&G' EQ '*').NOBR
         B     &G .                TO  TERMINATE
.NOBR  ANOP
&CNAME&SYSNDX EQU  *
         MEXIT
.ERROR   MNOTE  4,'MISSING OPTION'
         MEXIT
.E2  MNOTE  4,'MSG AND REPORT MUST NOT BE  SPECIFIED'
.DIDIT   MEXIT
.MULTI   AIF   (T'&NOT EQ 'O').TRYOK
&H       SETC  '&B'                     SET NAME FOR FIRST
&G       SETC  '&NOT'                   A DCB NOT OPEN
&BOPX    SETB  (1)                      FLAG TO CAUSE BRANCH
         AGO   .GLOX
.TRYOK   AIF   (T'&OK EQ 'O').OPPS      OK OR NOT MUST BE GIVEN
&H       SETC  '&B'
&KOP     SETA  N'&DCB*8-4
&BOPX    SETB  (0)
.KOX     ANOP
&G       SETC  '*+&KOP'
.GLOX    AIF   (&NOPS GE N'&DCB).DIDIT
&NOPS    SETA  &NOPS+1
&H       TM    &DCB(&NOPS)+48,X'10'     IS DCB OPEN
&H       SETC  ''
         AIF   (&SETOK).BROK
         BZ    &G                       FILE NOT OPEN
         AIF   (&BOPX).GLOX
&KOP     SETA  &KOP-8                   BRANCH DISP
         AIF   (&KOP GT 4).KOX
&SETOK   SETB  (1)
         AGO   .GLOX
.BROK    BO    &OK
         MEXIT
.OPPS    MNOTE 4,'OK OR NOT MUST BE GIVEN'
         MEND
