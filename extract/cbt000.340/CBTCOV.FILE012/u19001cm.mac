         MACRO
&NAME    BZR   &R
&NAME    BCR   8,&R                BR ON ZERO
         MEND
         MACRO
&NAME    BNZR  &R
&NAME    BCR   7,&R                BR ON NOT ZERO
         MEND
         MACRO
&NAME    BMR   &R
&NAME    BCR   4,&R                BR ON MINUS OR MIXED
         MEND
         MACRO
&NAME    BOR   &R
&NAME    BCR   1,&R                BR ON OVERFLOW OR IF ONES
         MEND
         MACRO
&NAME    BPR   &R
&NAME    BCR   2,&R                BR ON PLUS
         MEND
         MACRO
&NAME    BNER  &R
&NAME    BCR   7,&R                BR ON NOT EQUAL
         MEND
         MACRO
&NAME    BNLR  &R
&NAME    BCR   11,&R               BR ON NOT LOW
         MEND
         MACRO
&NAME    BNHR  &R
&NAME    BCR   13,&R               BR ON NOT HIGH
         MEND
         MACRO
&NAME    BER   &R
&NAME    BCR   8,&R                BR ON EQUAL
         MEND
         MACRO
&NAME    BLR   &R
&NAME    BCR   4,&R                BR ON LOW
         MEND
         MACRO
&NAME    BHR   &R
&NAME    BCR   2,&R                BR ON HIGH
         MEND
         MACRO
&NAME    BNPR  &R
&NAME    BCR   13,&R               BR ON NOT PLUS
         MEND
         MACRO
&NAME    BNMR  &R
&NAME    BCR   11,&R               BR ON NOT MINUS
         MEND
         MACRO
&NAME    BNOR  &R
&NAME    BCR   14,&R               BR IF NOT ONES
         MEND
         MACRO
&NAME    NUMBR
         B     F&SYSNDX+6
A&SYSNDX DC    X'FF00FFFFFFFFFF00FFFFFFFFFF00'
B&SYSNDX DC    X'F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0'
C&SYSNDX DC    XL10'00'
         DC    X'FAFBFCFDFEFF'
X&SYSNDX DC    F'0'                SAVE AREA REGISTER 2
D&SYSNDX NC    B&SYSNDX,0(14)
E&SYSNDX XC    B&SYSNDX,=20X'F0'
F&SYSNDX TRT   0(0,14),C&SYSNDX-240
&NAME    MVI   D&SYSNDX+1,X'00'    SET INSTR. LENGTHS TO ZERO
         MVI   E&SYSNDX+1,X'00'
         MVI   F&SYSNDX+1,X'00'
         BCTR  15,0
         ST    2,X&SYSNDX          SAVE REGISTER 2
         MVC   B&SYSNDX,=20X'F0'   SET MASK TO LOWEST VALID CHARACTER
         EX    15,D&SYSNDX         EXECUTE AND WITH DATA LENGTH IN R15
         EX    15,E&SYSNDX
         BM    G&SYSNDX            ERROR IF ANY BITS LEFT ON
         EX    15,F&SYSNDX         EXECUTE TRANSLATE AND TEST
         BC    8,H&SYSNDX          IF  OK  BYPASS ERROR ROUTINE
G&SYSNDX LR    14,15               ERROR ROUTINE SETS REGS.14 & 15 EQL.
H&SYSNDX L     2,X&SYSNDX          RESTORE REGISTER 2
         MEND
         MACRO
&NAME    PROGOUT  &RC
         LCLA  &A
&NAME    L     R13,4(R13)
         LM    R14,R12,12(R13)
         AIF   (K'&RC GT 0).G01
         MNOTE 0,'RC OMITTED RC=0 ASSUMED'
&A       SETA  0
         AGO   .G03
.G01     ANOP
&A       SETA  &RC
         AIF   (&A GT 15).ERROR
.G03     LA    R15,&A.(R0,R0)
         BR    R14
         MEXIT
.ERROR   MNOTE 8,'INVALID RETURN CODE'
         MEND
         MACRO
&NAME    PROGIN
         ENTRY SAVEAREA
&NAME    SAVE  (14,12)
         BALR  R5,R0
         USING *,R5
         CNOP  6,8
         ST    R13,*+22
         LR    R6,R13
         LA    R13,*+12
         ST    R13,8(R6)
         B     *+76
SAVEAREA DS    9D
         MEND
         MACRO
&NAME    MODOUT &R1,&R2,&R3,&R4,&R5,&R6
         LCLA  &A,&B
&NAME    L     R13,4(R13)
         AIF   (K'&R6 EQ 0).M01
         MNOTE 1,'NO MORE THAN 5 REGS CAN BE SPECIFIED- REST IGNORED'
.M01     AIF   (K'&R1 EQ 0).M09
&A       SETA  &R1
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AIF   (K'&R2 EQ 0).M09
&A       SETA  &R2
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AIF   (K'&R3 EQ 0).M09
&A       SETA  &R3
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AIF   (K'&R4 EQ 0).M09
&A       SETA  &R4
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AIF   (K'&R5 EQ 0).M09
&A       SETA  &R5
         AIF   (&A GT 12).ERROR
&B       SETA  &A*4+20
         ST    &A,&B.(R13)
         AGO   .M09
.ERROR   MNOTE 1,'REGS 13,14 AND 15 CANNOT BE CHANGED- MACRO IGNORED'
         MEXIT
.M09     RETURN (14,12)
         MEND
         MACRO
&NAME    MODIN
         CNOP  4,8
&NAME    SAVE  (14,12)
         BALR  R5,R0
         USING *,R5
         ST    R13,*+26
         LR    R6,R13
         LA    R13,*+16
         ST    R13,8(R6)
         LM    R6,R10,0(R1)
         B     *+76
         DS    9D
         MEND
         MACRO
&NAME    LPYR  &INPUT,&NOLPYR
         AIF   ('&INPUT' NE ' ').NO
         MNOTE 'FIRST PARAMETER OMITTED'
         AGO   .N1
.NO      AIF   ('&NOLPYR' NE ' ').N2
         MNOTE 'SECOND PARAMETER OMITTED'
.N1      MNOTE 'GENERATION TERMINATED'
         MEXIT
.N2      ANOP
&NAME    TM    &INPUT,X'01'
         BO    &NOLPYR
         TM    &INPUT,X'12'
         BM    &NOLPYR
         MEND
B05      TITLE 'SUBRTN.05, CONVERT PACKED DATE TO DDMY OR DD.MM.YY'
*  HBS STANDARD SUBRTN. 05, CATALOGED IN RELOCATABLE LIBRARY.
*  CONVERTS PACKED DATE YYDDD WITH SIGN TO DDMY OR DD.MM.YY.
*  ROUTINE WORKS WITH HBS YEAR, WHERE 1 FEB. IS DAY 001.
*  ALLOWS FOR LEAP YEARS 1968 AND 1972.
*  NEGATIVE PACKED DATE TRANSLATED AS FOLLOWS,E.G. SUP FEBRUARY 1967
*                                                  DDMY IS 3217
*                                              DD.MM.YY IS SUP 1.67
*
*  LINK TO SUBRTN. BY  CALL B0501,(P1,P2,P3,P4)
*  P1 IS NAME OF INPUT FIELD, ASSUMED 3 BYTES PACKED
*  P2 IS NAME OF OUTPUT FIELD, ASSUMED 4 OR 8 BYTES
*  P3 IS LENGTH OF OUTPUT FIELD, USED TO SELECT OPTION DDMY (LENGTH 4)
*     OR DD.MM.YY (LENGTH 8).
*  P4 IS NAME OF ERROR ROUTINE
         ISEQ  73,80
B0501    CSECT
         ENTRY B050100
B050100  EQU   *
B0501A   SAVE  (14,12)
         BALR  5,0
         USING *,5
         LM    6,9,0(1)
*
B05013   CH    8,=H'8'
         BE    B05014
         CH    8,=H'4'
         BNE   B05011
*
B05014   MVC   C0501,0(6)
       MVC   C0502(13),=C'       .  .  '     CLEAR RESULT FIELDS
       MVN   C05SGN,C0501+2        STORE SIGN OF INPUT
       MVN   C0501+2(1),=X'0C'     SET SIGN TO POSITIVE
       CLC   C0501+1(2),=X'366C'   Q. DAY NO. MORE THAN 366
       BH    B0532                 ERROR IF SO
       CLC   C0501+1(2),=X'001C'   Q. DAY NO. LESS THAN 001
       BL    B0532                 ERROR IF SO
       CLI   C0501,X'68'           Q. LEAP YEAR 1968
       BE    B0533
       CLI   C0501,X'72'           Q. LEAP YEAR 1972
       BE    B0533
B0509  MVC   C05TBL+1(2),=X'028C'  SET UP TABLE FOR NON-LEAP YEAR
B0510  LA    3,C05TBL              ADDRESS OF TABLE TO REG.3
B0511  CP    C0501+1(2),1(2,3)     COMPARE DAYS WITH DAYS IN TABLE MTH.
       BNH   B0515                 EQUAL OR LOW - MONTH LOCATED
       SP    C0501,1(2,3)          SUBTRACT DAYS IN MTH.FROM TOTAL DAYS
       LA    3,3(3)                UPDATE REG.3 TO MOVE UP TABLE
       CLI   0(3),C'Z'             Q. END OF TABLE
       BE    B0532                 ERROR IF SO
       B     B0511                 OTHERWISE RECYCLE
B0515  CLI   0(3),C'1'             Q. MONTH 1 (IN LAST HBS YEAR)
       BE    B0535                 IF SO GO TO ADD 1 TO YEAR
B0516  UNPK  C05UNP,C0501          UNPACK YEAR AND DAY
       OI    C05UNP+4,X'F0'        SET ZONE TO F
       MVC   C0502(2),C05UNP+3     MOVE DAY TO DDMY FIELD
       MVC   C0503+2(1),C05UNP+4   MOVE UNITS OF DAY TO DD.MM.YY FIELD
       CLI   C05UNP+3,C'0'         Q. DAY 1-9
       BE    B0522
       MVC   C0503+1(1),C05UNP+3   NO - MOVE TENS OF DAY
B0522  MVC   C0502+2(1),0(3)       MOVE MONTH TO DDMY FIELD
       MVC   C0503+5(1),0(3)       MOVE MONTH TO DD.MM.YY. FIELD
       CLI   0(3),X'60'            Q. MONTH 10
       BE    B0538
       CLI   0(3),X'50'            Q. MONTH 11
       BE    B0539
       CLI   0(3),C'A'             Q. MONTH 12
       BE    B0540
B0527  MVC   C0502+3(1),C05UNP+1   MOVE UNITS OF YEAR TO DDMY
       MVC   C0503+7(2),C05UNP     MOVE DECADE AND UNITS TO DD.MM.YY
       CLI   C05SGN,X'0D'          Q. MINUS SIGN (SUPPLEMENTARY)
       BE    B0541
B0530    CH    8,=H'8'
         BE    B05311
         MVC   0(4,7),C0502
B0531    RETURN (14,12)
*
B05311   MVC   0(8,7),C0503+1
         B     B0531
*
B0532    ST    9,12(13)
         B     B0531
*
*
*
B0533  CP    C0501+1(2),=X'029C'   LEAP YEAR - Q. DAY NO MORE THAN 28
       BL    B0509                 NO - SET UP TABLE FOR NORMAL YEAR
       MVC   C05TBL+1(2),=X'029C'  YES - SET UP TABLE FOR LEAP YEAR
       B     B0510
*
B0535  MVC   C05STR(1),C0501       STORE YEAR
       AP    C05STR,=X'010C'       ADD 1 TO YEAR
       MVC   C0501(1),C05STR       MOVE BACK UPDATED YEAR
       B     B0516
*
B0538  MVC   C0503+4(2),=C'10'     MOVE IN MONTH NO.10
       B     B0527
*
B0539  MVC   C0503+4(2),=C'11'     MOVE IN MONTH NO.11
       B     B0527
*
B0540  MVC   C0503+4(2),=C'12'     MOVE IN MONTH NO.12
       B     B0527
*
B0541  MVC   C0502(2),=C'32'       MOVE 32 TO DDMY FIELD FOR SUP DATE
         MVC   C0503+1(3),=C'SUP'       MOVE SUP TO DD.MM.YY FIELD
       B     B0530
B05011   ABEND 92,DUMP                                               OS
C0501  DC    PL3'0'
C0502  DC    CL4'0'
C0503  DC    CL9'0'
C05SGN DC    X'00'
C05UNP DS    CL5
C05STR DC    PL2'0'
C05TBL DC    C'2'
       DC    P'28'
       DC    C'3'
       DC    P'31'
       DC    C'4'
       DC    P'30'
       DC    C'5'
       DC    P'31'
       DC    C'6'
       DC    P'30'
       DC    C'7'
       DC    P'31'
       DC    C'8'
       DC    P'31'
       DC    C'9'
       DC    P'30'
       DC    X'60'
       DC    P'31'
       DC    X'50'
       DC    P'30'
       DC    C'A'
       DC    P'31'
       DC    C'1'
       DC    P'31'
       DC    C'Z'
*
      END
