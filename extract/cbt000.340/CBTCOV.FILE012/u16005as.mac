         TITLE  'SM19PRM .... EXAMINE PARM FIELD'
BX       DSECT
*
*        PROCESSOR FOR PARMSCAN MACRO
*
         USING *,11
$PARM    DS    C
$PARMENT DS    CL8
$PARMNXT DS    C   .
$PARMX   DS    CL8
SM19PRM  ENTER ENT=PARMSCAN,PUN=NO
         LA    7,ERRORS                  ERROR SPACE
         SR   0,0                     CLEAR RET REG
         L    2,0(,1)                 LOCATE PARAMETER WORD LIST
         L    3,16                    LOCATE CVT
         L    3,0(,3)                 TCB POINTERS
         L    3,4(,3)                 CURRENT TCB
         L    3,112(,3)               1ST SAVE AREA
         L     3,24(,3)                REG 1 POINTER ON ENTRY
         L     3,0(,3)                 PARM FIELD DATA
         L     4,=F'-1'                SET NEG 1 INTO 4
         AH    4,0(,3)                 ADD ON PARM LENGTH
         BM    EXM005                  THERE WAS NOT ONE.
         EX    4,EXMSHIFT              MOVE IT TO WORK AREA
         LA    3,WORKAREA              SET SCAN
         LA    5,WORKAREA+1(4)            CONTROL
         LA    4,1                           REGISTERS
EXM001   LA    6,TESTWORD              TO START
         ST    3,0(0,7)
         SR    10,10                    CLEAR INDEX CONTROL
EXM002   MVI   0(6),C' '               BLANK
         MVC   1(7,6),0(6)               OUT WORK SPACE
         LA    8,1                     FILLER INDEX
         LA    9,8(,6)                 FILLER LIMIT
EXM003   MVC   0(1,6),0(3)             COPY CHAR
         CLI   1(3),C'A'               SPECIAL CHARACTER
         BL    EXM007                  YES
EXM004   BXH   6,8,EXM014              UP OUT LIMIT
         BXLE  3,4,EXM003              UP INPUT
EXM005   LA    1,ERRORS                THE END
         MVC   0(4,7),=F'-1'           SET TERMINATOR OF ERROR LIST
         L     13,4(,13)               RELOAD SAVE AREA POINTER
         L     14,12(,13)              GET RETURN ADDRESS
         LM    2,12,28(13)             RESTORE USED REGS
         SR    15,15                   RETURN CODE 0
         BR    14                      RETURN
*
         B     EXM011A                  KEY OPTION SELECTOR
EXM006   B     EXM011                   SCAN FOR NAME
         B     EXM015                   TERMINATE
         B     EXM021                   EXTRACT A NUMERIC ITEM
*
EXM007   CLI   1(3),C','                TERMINAL COMMA
         BE    EXM006(10)               YES TO APPRORIATE RTN.
         CLI   1(3),C'='                KEYWORD SEP
         EX    0,EXM009(10)             YES TO APPROPRIATE RTN.
EXM008   MVC   0(1,6),0(3)              OTHER CHARACTER TYPE
         B     EXM004                   CONTINUE MOVING
*
         B     EXM008                   KEY OPTION ACCEPTS ALL
EXM009   BE    EXM010                   SET INDEX REG
         B     EXM008                   AND
         B     EXM014                   SPECIAL CHARACTERS NOT ALLOWED
*
EXM010   LA    10,4                     SET INDEX FOR KEYWORD
EXM011   LR    11,2                     LIST START
         B     EXM012                   SKIP
*
EXM011A  LR    8,1                      RETRIEVE CODE
EXM012   CLC   $PARMENT,TESTWORD          FOUND ENTRY
         BE    EXM016                     YES
EXM013   LA    11,$PARMNXT              TO NEXT ENTRY
         CLI   $PARM,255                END
         BE    EXM014                   YEP
         CLI   $PARM,C'='               KEY WORD
         BNE   EXM013A                  NOPE
         LTR   10,10                    TEST IF IN OPTION SELECT
         BNM   EXM013                   NOT
         MVC   $PARMENT,TESTWORD        GET DE DATA
         B     EXM015                   FINISH KEY
*
EXM013A  CLI   $PARMENT,C' '            KEY WORD SPACE
         BE    EXM013                   YES
         CLI   $PARMENT,C'0'            NUMERIC EXTRACT REQ.
         BE    EXM013                   YES
         SLL   8,1                      NO UP NO
         CLI   $PARM,C'+'               BEEN SET
         BE    EXM013                   YEP IGNORE
         CLI   $PARM,X'FF'              END
         BNE   EXM012                   NO
EXM014   CR    3,5                      CHECK LIMIT
         BH    EXM005                   OPPS
         LA    6,1(,3)                  END POINT
         S     6,0(,7)                 CALCULATE
         STC   6,0(,7)                  LENGTH AND STORE IT
         LA    7,4(,7)                 UP ERROR LIST
EXM015   LA    3,2(,3)                 UP INPUT POINT
         B     EXM001                  NEXT PARAM
*
EXM016   MVI   $PARM,C'+'              MARK PRESENCE IN LIST
         OR    0,8                     AND IN MASK REG.
         B     EXM017(10)              CONTINUE PROCESSING.
*
         MVI   $PARM,C'/'               RESTORE
EXM017   B     EXM015                  COMMA ENDING.
         CLI   $PARMX,C'0'             NUMERIC REQ.
         BE    EXM019
         LA    6,$PARMX                OK SET FILL POINTER
         CLI   $PARMX,C' '              PROBABLE KEYWORD
         BE    EXM020                   YES
         CLI   $PARMNXT,C'='            DEFNITE
         BE    EXM020                   YEP
         CLI   $PARMNXT,C'/'            SPECIAL OPTIONS
         BNE   EXM018                   NO
         LA    6,TESTWORD
         LR    1,8                      SAVE 8
         LNR   10,10                    SET PROB OPTION
         B     EXM020A                  CARRY ON SCAN
*
*
EXM018   XR    0,8                     SET OFF MASK VALUE
         MVI   $PARM,C' '              CLEAR MARKER
         B     EXM014                  FINISH ERROR FILLER
*
EXM019   LA    6,TESTWORD               RELOAD FILLPOINT
         LA    10,8                     SET NUMERIC REQ. INDEX
EXM020   MVI   $PARMNXT,C'='           SET EQUAL IN
EXM020A  LA    3,2(,3)                  STEPOVER
         B     EXM002                  GO FILL IT
EXM021   S     6,=A(TESTWORD)           CAL PACK LENGTH
         EX    6,EXMPACK                 GET IT OUT
         UNPK  $PARMX,PACKWORK           JUSTIFY RIGHT IN PARAM LIST
         B     EXM015                   GO FINISH
EXMPACK  PACK  PACKWORK,TESTWORD(1)     USED TO GET NUMERIC ITEM
PACKWORK DC    PL4'0'      WORK FIELD
EXMSHIFT MVC   WORKAREA,2(3)
         LTORG
TESTWORD DC    CL8' '                  COPY FOR TESTING.
WORKAREA DC    100C','                 FILLERS
ERRORS   DC    20F'0'                  ERROR SPACES.
         END
