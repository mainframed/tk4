         MACRO
&NAME    MOVE  &A,&B
         LCLA  &POS
         LCLA  &LEN
         LCLA  &DECR
         LCLC  &MVA
         LCLC  &MVB
         LCLC  &MVC
.*
&POS     SETA  0
&LEN     SETA  L'&A
&DECR    SETA  256
&MVC     SETC  '(256)'
.*
         AIF   (N'&SYSLIST NE 2).ERROR
.*
.LOOP    AIF   (&LEN GT &DECR).BIG
         AIF   (&POS EQ 0).SIMP
&MVA     SETC  '+'.'&POS'
&MVB     SETC  '('.'&LEN'.')'
         MVC   &A&MVA&MVB,&B&MVA
         MEXIT
.*
.SIMP    ANOP
&NAME    MVC   &A,&B
         MEXIT
.*
.BIG     ANOP
         AIF   (&POS EQ 0).FIRST
&MVA     SETC  '+'.'&POS'
         MVC   &A&MVA&MVC,&B&MVA
         AGO   .LP
.*
.FIRST   ANOP
&NAME    MVC   &A&MVC,&B
.LP      ANOP
&LEN     SETA  &LEN-&DECR
&POS     SETA  &POS+&DECR
         AGO   .LOOP
.*
.ERROR   MNOTE 1,'INVALID NO OF OPERANDS. NO STATEMENTS GENERATED'
         MEND
