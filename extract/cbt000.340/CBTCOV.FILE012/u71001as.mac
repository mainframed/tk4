*
         MACRO
&N       EOJ   &C=0
* EOJ - GENERATE STANDARD RETURN LINKAGE
&N       L     13,PRGMSAVE+4
         RETURN (14,12),,RC=&C
         MEND
         MACRO
&N       BEGIN &R
* BEGIN - GENERATE A CSECT STATEMENT, SPECIFY AND LOAD THE FIRST
*         BASE REGISTER, AND GENERATE STANDARD REGISTER SAVE
*         PROCEEDURE IN A MODULE SAVE AREA.
&N       CSECT
         AIF   ('&R' LT '2').E1
         AIF   ('&R' GT '12').E1
         SAVE  (14,12)
         LR    &R,15
         USING &N,&R
         LR    15,13
         LA    13,PRGMSAVE
         ST    13,8(15)
         ST    15,4(13)
         B     PRGMSAVE+72
PRGMSAVE DC    18F'0'
         MEXIT
.E1      MNOTE 9,'IMPROPER REGISTER SPECIFIED,NO STATEMENTS GENERATED'
         MEXIT
         MEND
         MACRO
         REGEQS
*
*        REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FPR0     EQU   0
FPR2     EQU   2
FPR4     EQU   4
FPR6     EQU   6
         MEND
UTPRNT   BEGIN 10
         REGEQS
*                                                                     *
*    PROGRAM NAME : UTPRINT                                           *
*                                                                     *
*    DESCRIPTION : THE PROGRAM INPUTS RECORDS FROM A SPOOLED PRINT    *
*        FILE  ON TAPE  CONTAINING BLOCKED OR UNBLOCKED               *
*        RECORDS, AND OUTPUTS THEM TO A PRINTER. RECORD FORMAT IS     *
*        AS PER NORMAL PRINT LINE SPECIFICATION.                      *
*            INITIALLY, USING INFORMATION PROVIDED BY THE USER        *
*        PARM FIELD, THE PROGRAM PROMPTS THE OPERATOR TO MOUNT        *
*        THE APPROPRIATE STATIONARY. THE PRINT STARTS WHEN THE        *
*        OPERATOR SIGNIFIES THAT THIS HAS BEEN DONE, AND THEREAFTER   *
*        THE OPERATOR HAS THE OPERTUNITY OF INTERRUPTING THE          *
*        PRINTOUT TO BACK SPACE OR FORWARD SPACE A GIVEN NUMBER OF    *
*        BLOCKS, TO GO BACK TO THE BEGINING OF THE FILE, OR STOP      *
*        THE PRINT COMPLETLY.
*            THE PROGRAM SENDS A MESSAGE TO THE CONSOLE EVERY FIVE    *
*        MINUTES INDICATING THE NUMBER OF THE BLOCK FROM THE INPUT    *
*        DATA SET BEING PRINTED, AND AT THE END OF THE PRINT,         *
*        SENDS A MESSAGE INDICATING THE TOTAL NUMBER OF BLOCKS        *
*        PRINTED.                                                     *
*                                                                     *
*    JCL :                                                            *
*        // EXEC PGM=UTPRINT,PARM='0001',TIME=1440                    *
*        //SPOOLDS DD UNIT=TAPE,VOL=............                      *
*        //PRINTER DD UNIT=1403                                       *
*            THE ABOVE JCL WOULD PROMPT THE OPERATOR TO MOUNT         *
*        FORM '0001'.                                                 *
*            IT SHOULD BE NOTICED THAT UTPRINT REQUIRES A DEDICATED   *
*        PRINTER.                                                     *
*                                                                     *
*   UTPRINT MESSAGES :                                                *
*        1. NN UTP001A SPOOL PRT : CHANGE FORM XXXX                   *
*        R NN,Y  (WHEN PAPER READY)                                   *
*                                                                     *
*        2. NN UTP002A SPOOL PRT : WAITING FOR REPLY 'Y' TO UTP001A   *
*        R NN,Y  (AFTER INVALID REPLY TO MESSAGE UTP001A)             *
*                                                                     *
*        3. NN UTP003I SPOOL PRT : START PRINT - XXXX LINES PER BLOCK *
*   (A) TO ALIGN PAPER OR RESTART FROM THE BEGINNING IN THE EVENT OF  *
*        A PRINT WRECK :-                                             *
*            R NN,BSP=DS                                              *
*   (B) TO GO BACK XXX BLOCKS OF LINES :-                             *
*           R NN,BSP=XXX                                              *
*   (C) TO GO FORWARD XXX BLOCKS OF LINES :-                          *
*            R NN,FSP=XXX                                             *
*   (D) TO STOP PRINTING BEFORE END OF FILE :-                        *
*            R NN,STOP                                                *
*        AFTER RESPONSE (A),(B) OR (C) :-                             *
*            NN UTP004I SPOOL PRT : CONTINUE PRINT                    *
*        AFTER RESPONSE (D) :-                                        *
*            NN UTP008I SPOOL PRT : PRINT STOPPED BY OPERATOR         *
*                                                                     *
*        ERROR MESSAGES ***********                                   *
*        GENERAL :-                                                   *
*        NN UTP005A SPOOL PRT : INVALID REQUEST - RETRY               *
*         RE-ENTER (A),(B),(C) OR (D) AS REQUIRED
*        NO OF BLOCKS NOT NUMERIC :-                                  *
*        NN UTP006A SPOOL PRT : NON-NUMERIC 3 FIGURE NO - RETRY       *
*         RE-ENTER (A),(B),(C) OR (D) AS REQUIRED                     *
*        TOO MANY BLOCKED SPECIFIED (OUTSIDE RANGE OF TAPE) :-        *
*         NN UTP007A SPOOL PRT : TOO MANY BLOCKS SPECIFIED - RETRY    *
*        RE-ENTER (A),(B),(C) OR (D) AS REQUIRED                      *
*    INFORMATION MESSAGES **********                                  *
*        EVERY 5 MINUTES :-                                           *
*         NN UTP010I SPOOL PRT : BLOCK NO XXXX BEING PRINTED          *
*        AT END OF THE PRINT :-                                       *
*         NN UTP009I SPOOL PRT - XXXXXX LINES PRINTED                 *
*                                                                     *
*        NOTE - IF THE NUMBER OF PHYSICAL RECORDS DIVIDED BY THE      *
*             NUMBER OF LOGICAL RECORDS EQUALS THE NUMBER OF LINES    *
*            PER PAGE FOR THE SPOOLDS THEN ON FORWARD OR BACKWARD     *
*             SPACEING THE NUMBER OF BLOCKS WILL BE THE NUMBER        *
*             OF PAGE.                                                *
*                                                                     *
         L     R4,0(R1)
         LA    R4,2(R4)      SKIP LENGTH,LOAD ADDR OF PARAM LIST
         MVC   WFORM+50(4),0(R4)
*
         XR    R11,R11             ZEROISE BLOCK COUNT
         OPEN  INDCB
         LH    R3,INDCB+62    BLOCK SIZE
         LH    R4,INDCB+82    RECORD LENGTH
         STH   R4,PRTDCB+62
         STH   R4,PRTDCB+82
         MVC   PRTDCB+36(1),INDCB+36    RECFM
         OPEN  (PRTDCB,(OUTPUT))
*
         SETPRT  PRTDCB,UCS=(QN,F)
         LTR   R15,R15
         BZ    WFORM
         ABEND 099,DUMP
*
WFORM    WTOR  'UTP001A SPOOL PRT: CHANGE TO FORM XXXX    ',REPLYFLD,1,*
               ECBFLD
*
         XR    R2,R2
         LR    R0,R3
         DR    R2,R4
         ST    R3,RECB        NO.OF RECORDS PER BLOCK
         CVD   R3,PACKFLD
         UNPK  RECZ(4),PACKFLD+5(3)
         OI    RECZ+3,X'F0'
         MVC   YES+49(4),RECZ
         GETMAIN R,LV=(0)
         ST    R1,BUFFAD
         L     R2,INDCB+68         POINT R2 TO IOB PREFIX
*
ANSWER   WAIT  ECB=ECBFLD
         NI    ECBFLD,X'00'
         OI    REPLYFLD,X'40'
         CLI   REPLYFLD,C'Y'
         BE    YES
         WTOR  'UTP002A SPOOL PRT: WAITING FOR REPLY ''Y'' TO UTP001A',*
               REPLYFLD,1,ECBFLD
         B     ANSWER
YES      WTOR  'UTP003I SPOOL PRT: START PRINT - XXXX LINES PER BLOCK',*
               REPLYFLD,8,ECBFLD
*
         STIMER REAL,TIMEX,BINTVL=TIME  SET INTERVAL FOR BLOCK NO. MSG
*
*
RDIN     L     R5,BUFFAD
         READ  DECB1,SF,INDCB,(5)
         CHECK DECB1
         NI    PIND,X'00'
         TM    PERR,X'FF'
         BO    PREC
*
         LA    R11,1(R11)          INCREMENT BLOCK COUNT
         ST    R11,BLKC
         TM    FLAGT,X'FF'
         BNO   RESCT
         STIMER REAL,TIMEX,BINTVL=TIME
         NI    FLAGT,X'00'
*
RESCT    LH    R3,22(R2)        PICK UP RESIDUAL COUNT FROM CSW IN IOB
         ST    R3,RESID
         LTR   R3,R3
         BZ    LR8
         XR    R8,R8
         LH    R9,INDCB+62
         SR    R9,R3
         DR    R8,R4
         LR    R8,R9
         B     TRLEN
LR8      L     R8,RECB
*
TRLEN    LR    R3,R4
         S     R3,=F'2'
WRTOUT   EX    R3,TRANS
         WRITE DECB2,SF,PRTDCB,(5)
         CHECK DECB2
*
         TM    ECBFLD,X'40'        TEST FOR OPERATOR INTERRUPT
         BO    TST0
*
         LA    R5,0(R4,R5)         NEXT RECORD
         BCT   R8,WRTOUT
         B     RDIN
TRANS    TR    1(0,R5),TR
*
*
TST0     NI    ECBFLD,X'00'
TST1     OC    REPLYFLD(6),ORFLD   UPPER OR LOWER CASE ACCEPTED
         CLC   REPLYFLD(4),=C'BSP='
         BE    TST2
         CLC   REPLYFLD(4),=C'FSP='
         BE    TST2
         CLC   REPLYFLD(4),=C'STOP'
         BE    STOP
         B     INVAL
TST2     CLC   REPLYFLD+4(2),=C'DS'
         BE    TCLOSE
TST3     MVC   TEMP(4),REPLYFLD+4
         MVC   BLKZ(4),ZEROS
         MVC   REPLYFLD+4(4),ORFLD      BLANK OUT
         LA    R7,TEMP+3
         LA    R8,BLKZ+3
         LA    R9,4
*
CLI40    CLI   0(R7),X'40'         TEST FOR BLANK
         BNE   DIGIT
         BCTR  R7,0
         BCT   R9,CLI40
         B     NONUM
*
DIGIT    TM    0(R7),X'F0'         TEST FOR NUMERIC
         BNO   NONUM
         MVC   0(1,R8),0(R7)
         BCTR  R8,0
         BCTR  R7,0
         BCT   R9,DIGIT
*
PCK      PACK  PACKFLD(8),BLKZ(4)
         CVB   R8,PACKFLD
         ST    R8,BLKB
         CLI   REPLYFLD,C'F'
         BE    FRWD
         CL    R11,BLKB
         BL    TOOBIG
*
BACK     SR    R11,R8              RESET BLOCK COUNT
         ST    R11,BLKC
         B     POINT
*
FRWD     AR    R11,R8
         ST    R11,BLKC
*
POINT    LA    R6,BLKC
         OI    PIND,X'FF'
         POINT INDCB,(6)
         L     R11,INDCB+12
         B     CONT
*
TCLOSE   CLOSE (INDCB,REREAD),TYPE=T
         XR    R11,R11
*
CONT     CVD   R11,PACKFLD
         UNPK  MSG4+49(7),PACKFLD+4(4)
         OI    MSG4+55,X'F0'
MSG4     WTOR  'UTP004I SPOOL PRT: BLOCK COUNT = XXXXXXX',REPLYFLD,8,  *
               ECBFLD
         B     RDIN
*
INVAL    WTOR  'UTP005A SPOOL PRT: INVALID REQUEST - RETRY',REPLYFLD,11*
               ,ECBFLD
         B     WAIT
NONUM    WTOR  'UTP006A SPOOL PRT: NON-NUMERIC NO. - RETRY',           *
               REPLYFLD,8,ECBFLD
         B     WAIT
TOOBIG   WTOR  'UTP007A SPOOL PRT: TOO MANY BLOCKS SPECIFIED - RETRY', *
               REPLYFLD,8,ECBFLD
         B     WAIT
*
SYNEX    TM    PIND,X'FF'
         BZ    IOERR
         OI    PERR,X'FF'
*        REGS NOT SAVED THEREFORE RETURN MACRO NOT USED
         LA    R15,0
         BR    R14
WAIT     WAIT  ECB=ECBFLD
         NI    ECBFLD,X'00'
         B     TST1
*         POINT ERROR RECOVERY  ROUTINE
PREC     EQU   *
         NI    PERR,X'00'
         WTO   'UTP011I - POINT ERROR - END OF TAPE? - TRY BSP'
         CLOSE (INDCB,LEAVE),TYPE=T
         B     WAIT
*
IOERR    SYNADAF ACSMETH=BSAM
         MVC   IOEMSG+8(78),50(R1)
IOEMSG   WTO   '                                                       *
                                          ',ROUTCDE=(11)
         SYNADRLS
         ABEND 0001,DUMP
*
STOP     WTO   'UTP008I SPOOL PRT: PRINT STOPPED BY OPERATOR'
ENDRTN   CVD   R11,PACKFLD
         UNPK MSG9+27(9),PACKFLD+3(5)
         OI    MSG9+35,X'F0'
MSG9     WTO   'UTP009I SPOOL PRT: XXXXXXXXX BLOCKS PRINTED'
         CLOSE (INDCB,,PRTDCB)
         EOJ
*
*
         DROP  R10
         USING TIMEX,R12
TIMEX    SAVE (14,12)
         LR    R12,R15
         ST    R13,SAVET+4
         LR    R11,R13
         LA    R13,SAVET
         ST    R13,8(R11)
         L     R5,BLKC
         CVD   R5,PACKT
         UNPK  BLKNO+36(7),PACKT+4(4)
         OI    BLKNO+42,X'F0'
         TIME
         ST    R0,PACKT
         OI    PACKT+3,X'0F'
         UNPK  TOD(7),PACKT(4)
         MVC   BLKNO+59(2),TOD
         MVC   BLKNO+62(2),TOD+2
         MVC   BLKNO+65(2),TOD+4
BLKNO    WTO   'UTP010I SPOOL PRT: BLOCK NO.XXXXXXX BEING PRINTED  XX.X*
               X.XX'
         OI    FLAGT,X'FF'
         L     R13,4(R13)
         RETURN (14,12)
*
*
SAVET    DS    18F
PACKT    DS    D
TOD      DS    CL8
FLAGT    DC    X'00'
BUFFAD   DS    A(0)
REPLYFLD DC    CL20' '
ECBFLD   DC    F'0'
RECB     DC    F'0'
RECZ     DS    CL4
BLKB     DC    F'0'
BLKZ     DS    CL4
BLKC     DC    F'0'
TIME     DC    F'30000'
TEMP     DS    CL4
PACKFLD  DS    D
ORFLD    DC    X'404040404040'
ZEROS    DC    X'F0F0F0F0'
RCD      DS    CL1
PIND     DS    CL1
PERR     DC    CL1'0'
RESID    DS    F'0'
*
*                                  TRANSLATE TABLE
TR       DC    256C' '        INITIALIZE THE TABLE TO BLANK
         ORG   TR+C'.'
         DC    C'.<(+×&&'
         ORG   TR+C'$'
         DC    C'$*);^-/'
         ORG   TR+C','
         DC    C',%_>?'
         ORG   TR+C':'
         DC    C':#@''="'
         ORG   TR+C'A'
         DC    C'ABCDEFGHI'
         ORG   TR+C'J'
         DC    C'JKLMNOPQR'
         ORG   TR+C'S'
         DC    C'STUVWXYZ'
         ORG   TR+C'0'
         DC    C'0123456789'
         ORG   TR+256
*
INDCB    DCB  DDNAME=SPOOLDS,DSORG=PS,MACRF=RP,EODAD=ENDRTN,SYNAD=SYNEX
PRTDCB   DCB   DDNAME=PRINTER,DSORG=PS,MACRF=W,SYNAD=IOERR
         END
