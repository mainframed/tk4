GRIPECL  CSECT
**********************************************************************
*      GRIPECL IS THE CLEARING PROGRAM FOR GRIPE.                    *
*       GRIPE WRITES SMF TYPE131 RECORD AND GRIPEAN READS THEM       *
*       CHECKS THE DATE AND TIME AND DRAWS A HISTOGRAM OF            *
*       USERIDS AGAINST TIME.                                        *
*      GRIPECL IS THEN RUN.IT READS THE FIRST RECORD GETS THE MONTH, *
*      ALLOCATES THE MONTHLY DATASET AND WRITES ALL THE RECORDS TO   *
*      IT AND CLEARS THE DAILY DUMP DATASET.                         *
*                                                                    *
*    WRITTEN BY JESS HAND           MET.O.12B        27/03/84        *
*                                                                    *
**********************************************************************
         PRINT NOGEN
         EQUREG R
         CHAIN
         L     R2,0(R1)
*
         OPEN  (SMFDCB,(INPUT))       OPEN DATASET
         GET   SMFDCB                 READ FIRST SMF RECORD
         LR    R2,R1                  ADDRESS OF SMF RECORD
         LH    R3,0(R2)               LENGTH OF RECORD.
         MVC   DATE(4),10(R2)         GET DATE.
PARMDATE MVC   DWRD,ZERO              CLEAR DOUBLEWORD WORK AREA
         LH    R6,DATE                EXTRACT YEAR FROM DATE.
         SLL   R6,4
         ST    R6,DWRD+4
         OI    DWRD+7,X'0F'           PUT IN SIGN BITS
         CVB   R6,DWRD
         A     R6,=F'1900'
         ST    R6,YEAR
         UNPK  DSN+9(3),DWRD+6(2)    YEAR TO DATASET NAME.
         MVC   DWRD,ZERO              CLEAR WORK AREA.
         MVC   DWRD+6(2),DATE+2       YEAR DAY.
         CVB   R6,DWRD
         ST    R6,DAYS
         CALL  DATE23,(DAYS,YEAR,DAYS,MONTH,YEAR)  DATE TO DD/MM/YYYY
         L     R6,MONTH
         LA    R5,MONTHS
         SLL   R6,2                   MULTIPLY BY 4
         AR    R5,R6
         MVC   DSN+7(3),0(R5)        MONTH TO DATASET NAME.
         MVC   DSAL2+6(12),DSN    DSN TO TEXT.
*
LOC      DS    0H             LOCATE MONTHLY DATA SET NAME.
*        PUT DATA SET NAME INTO CANLST MACRO
*
         LOCATE IT
*
*        CHECK EXCEPTION CODES FORM LOCATE MACRO
         LTR   15,15
         BZ    ALLOC    DATASET ALREADY EXISTS.
         B     NEWDS        MAKE NEW MONTHLY DATASET.
*
IT       CAMLST NAME,DSN,,AREA
DSN      DC    C'M12.ACGMMMYY',32CL1' '
AREA     DS    0D
         DS    265C
*
*
*    WE NEED TO ALLOCATE THE APPROPRIATE MONTHLY DATASET.
*
*
*    NOW ALLOCATE M12.ACGMMMYY.
*
ALLOC    LA    4,DSALPNTR         ALLOCATION POINTER.
         ST    4,TXTPNTR
         LA    1,RBPNTR
         SVC   99                 DYNAMIC ALLOCATION.
         LTR   15,15
         BZ    OPEN               ALLOCATION OK. OPEN DATASET.
         B     FINI               DATASET NOT ALLOCATED.END
NEWDS    LA    4,DSNEWPTR         NEW DATASET ALLOCATION POINTER.
         ST    4,TXTPNTR
         LA    1,RBPNTR
         SVC   99                 DYNAMIC ALLOCATION.
         LTR   15,15
         BZ    OPEN               ALLOCATION OK. OPEN DATASET.
         B     FINI
OPEN     OPEN  (MONDCB,(OUTPUT))      OPEN MONTHLY DATASET.
         PUT   MONDCB
         LR    R5,R1                  NEXT OUTPUT BUFFER
         EX    R3,MOVE
         B     WRITE
MOVE     MVC   0(0,R5),0(R2)       MOVE RECORD FROM INPUT TO OUTPUT.
*
*   ALL SUBSEQUENT READS OF SMF RECORDS ARE DONE HERE.
*
NEXTREAD GET   SMFDCB
         LR    R2,R1                   ADDRESS OF SMF RECORD.
         LH    R3,0(R2)
         EX    R3,MOVE
WRITE    PUT   MONDCB                  WRITE RECORD TO MONTHLY DATASET
         LR    R5,R1                   NEXT OUTPUT BUFFER.
         B     NEXTREAD                GET NEXT RECORD.
*
*    CLOSE DATASET AND RETURN
*
FINISH   CLOSE SMFDCB
         CLOSE MONDCB
*     NOW WE HAVE FINISHED WITH THE MONTHLY DATASET.
*     CLEAR THE DAILY DUMP DATASET.
*
         OPEN  (SMFDCB,(OUTPUT))
         CLOSE SMFDCB
         RETURN13
*
*
FINI     CLOSE SMFDCB
         CLOSE MONDCB
         RETURN13 RC=(15)
*    DCBS AND STORAGE.
*
SMFDCB   DCB   DDNAME=SMF,MACRF=(GL,PM),BFTEK=A,RECFM=VBS,             *
               SYNAD=FINI,EODAD=FINISH,DSORG=PS
*
MONDCB   DCB   DDNAME=MONDD,MACRF=PL,BFTEK=A,RECFM=VBS,                *
               DSORG=PS
*
MONTHS  DC    C'    '
        DC    C'JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC'
DWRD     DC    D'0'
DATE     DC    F'0'
ZERO     DC    F'0'
YEAR     DC    F'0'
DAYS     DC    F'0'
MONTH    DC    F'0'
RBPNTR   DS    0F
         DC    X'80',AL3(RB)       REQUEST BLOCK POINTER
RB       DC    FL1'20'             LENGTH OF REQUEST BLOCK.
VERB     DC    X'01'               DYNALLOC VERB
         DC    B'00110000'         FLAG1
         DC    B'00000000'         FLAG1
ERR      DS    H                   ERROR CODES.
INFO     DS    H                   INFORMATION CODES.
TXTPNTR  DS    A                   TEXT POINTER.
         DC    F'0'
         DC    F'0'
*
DSALPNTR DC    A(DSAL1)
         DC    A(DSAL2)
         DC    X'80',AL3(DSAL3)
DSAL1    DC    X'0001',X'0001',X'0005',C'MONDD'    DDNAME
DSAL2    DC    X'0002',X'0001',X'000C',C'M12.ACGMMMYY'  DSNAME
DSAL3    DC    X'0004',X'0001',X'0001',X'02'        MOD
*
DSNEWPTR DC    A(DSAL1)
         DC    A(DSAL2)
         DC    A(DSNEW3)
         DC    A(DSNEW4)
         DC    A(DSNEW5)
         DC    A(DSNEW6)
         DC    A(DSNEW7)
         DC    A(DSNEW8)
         DC    A(DSNEW9)
         DC    A(DSNEWA)
         DC    A(DSNEWB)
         DC    A(DSNEWC)
         DC    X'80',AL3(DSNEWD)
DSNEW3   DC    X'0004',X'0001',X'0001',X'04'        NEW
DSNEW4   DC    X'0005',X'0001',X'0001',X'02'        CATLG
DSNEW5   DC    X'0007',X'0000'                      TRACKS
DSNEW6   DC    X'000A',X'0001',X'0003',X'000005'    5 PRIMARY
DSNEW7   DC    X'0010',X'0001',X'0006',C'SYS420'    VOLUME
DSNEW8   DC    X'0015',X'0001',X'0004',C'DISK'      UNIT=DISK
DSNEW9   DC    X'002F',X'0001',X'0001',X'60'        BFTEK=A
DSNEWA   DC    X'0030',X'0001',X'0002',X'4000'      BLKSIZE=4096
DSNEWB   DC    X'003C',X'0001',X'0002',X'4000'      DSORG=PS
DSNEWC   DC    X'0042',X'0001',X'0002',X'7FF8'      LRECL
DSNEWD   DC    X'0049',X'0001',X'0001',X'58'        RECFM=VBS
*
         END
