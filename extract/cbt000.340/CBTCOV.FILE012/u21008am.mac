         MACRO
**    AUTHORS - ICI (MOND DIVISION)
&NAME    PLISAV &CSECT=YES,&SAVE=SAV,&SAVLEN=104
         AIF   ('&CSECT' EQ 'NO').NOCS
         CSECT
         ENTRY &NAME
.NOCS    DC    CL7'&NAME',AL1(7)
&NAME    STM   14,12,12(13)
         LR    10,15
         USING &NAME,10
         AIF   ('&SAVE' NE 'NO').SAV
         MEXIT
.SAV     LA    0,&SAVLEN           LENGTH REQUIRED
         LR    3,1                 SAVE PARAMETER LIST ADDRESS
         L     1,76(13)            NEXT AVAILABLE BYTE
         ALR   0,1
         CL    0,12(12)            ENOUGH SPACE AVAILABLE ?
         BNH   E&SYSNDX            BRANCH IF SO
         L     15,116(12)          OVERFLOW ROUTINE
         BALR  14,15
E&SYSNDX ST    0,76(1)             NEW NAB POINTER
         ST    13,4(1)             BACK CHAIN
         ST    1,8(13)             FORWARD CHAIN
         MVC   72(4,1),72(13)      COPY LWS ADDRESS
         LR    13,1                NEW DSA
         USING &SAVE,13            BASE REGISTER FOR DSECT
         XC    1(3,13),1(13)       CLEAR DSA BYTES 2 - 4
         MVI   0(13),X'80'         PRESERVE
         MVI   86(13),X'91'         PL/I ERROR
         MVI   87(13),X'C0'          HANDLING
         LR    1,3                 RESTORE PARAMETER LIST ADDRESS
         MEND
