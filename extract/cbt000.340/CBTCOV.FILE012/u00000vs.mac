DISMOUNT CSECT
*
*  THIS PROGRAM IS INTENDED FOR USE AT THE END OF A JOB WHICH HAS
*  EARLIER USED THE RTPMOUNT PROGRAM TO FORCIBLY MOUNT DISK VOLUMES.
*  IT WORKS IN THE SAME MANNER AS RTPMOUNT, SETTING ON THE 'UNLOAD'
*  BIT IN THE UCB FOR EVERY DISK VOLUME MENTIONED IN A DD CARD FOR
*  THE CURRENT STEP (UNLESS THE VOLUME IS EITHER PERMANENTLY RESIDENT
*  OR IS INCLUDED IN PRESRES - IN BOTH CASES THE UNLOAD REQUEST IS
*  IGNORED)
*
*  THIS PROGRAM REQUIRES A PRESRES LIST TO BE AVAILABLE IN SYS1.PARMLIB
*  AT RUN TIME, AND PARMLIB MUST BE UNBLOCKED.
*  THERE IS A LIMIT OF 40 VOLUMES IN THE PRESRES 'DEBARRED LIST' SET UP
*  BY THIS PROGRAM.
*  IT IS ASSUMED THAT ALL DISK VOLUMES IN PRESRES HAVE 6 CHARACTER
*  VOLUME SERIALS - IF NOT THE METHOD OF EXTRACTING THEM BY TAKING THE
*  FIRST 6 CHARACTERS OF EACH PRESRES ENTRY MAY REQUIRE CHANGE.
*
*  THE OPERATOR IS INFORMED OF THE VOLUMES FOR WHICH AN UNLOAD HAS BEEN
*  ISSUED, AND OF THE JOBNAME, AND THE PACKS WILL BE UNLOADED WHEN ALL
*  USERS HAVE FINISHED WITH THE VOLUME.
*
*        BEGIN
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
** LOCATE PARMLIB - ABEND USER 10 IF ANY ERROR IN LOCATE
         LOCATE  PARMLIB
         LTR   15,15
         BZ    PFND
         ABEND 10,DUMP
**  PUT PARMLIB VOL SER IN JFCB AND CLEAR LOCAREA FOR USE AS A LIST OF
**  UP TO 40 VOLUMES FOUND IN PRESRES.
PFND     MVC   JFCBVOL,LOCAREA+6
         XC    LOCAREA(256),LOCAREA
         L     1,16
         L     1,40(1)             UCB LOOK-UP TABLE
**  SEARCH ALL UCBS FOR ONE WITH THE PARMLIB VOL SERIAL - ABEND WITH
**  USER 12 IF VOLUME NOT FOUND.
SRCHUCBS LH    2,0(1)
         LTR   2,2
         BZ    NEXTUCB
         BM    NM                  END OF TABLE
         CLI   18(2),32
         BNE   NEXTUCB
         CLC   JFCBVOL,28(2)
         BE    VF
NEXTUCB  LA    1,2(1)
         B     SRCHUCBS
NM       ABEND 12,DUMP
**  REMEMBER ADDRESS OF PARMLIB UCB AND SEARCH TIOT FOR FIRST DD ENTRY
**  (EXCLUDING PGM=*.DD). THIS DD ENTRY WILL BE TEMPORARILY USED FOR
**  OPENING PARMLIB BY SAVING THE UCB ADDRESS FROM IT AND TEMPORARILY
**  INSERTING THE PARMLIB UCB ADDEESS PLUS MOVING THE DDNAME FROM THAT
**  TIOT ENTRY TO PDCB + 40.
VF       ST    2,PUCB
         L     1,16
         L     1,0(1)
         L     1,4(1)
         L     1,12(1)             TCB TIOT
         LA    1,24(1)             FIRST DDNAME
         SR    3,3
FT       CLC   4(3,1),PGM
         BNE   USET
         IC    3,0(1)
         LA    1,0(3,1)
         B     FT
USET     ST    1,TUCB
         MVC   ORUCB,16(1)
         MVC   17(3,1),PUCB+1
         MVC   PDCB+40(8),4(1)
         OI    JFCB+86,1
**  OPEN PARMLIB WITH A JFCB IMAGE THAT CONTAINS - 1) THS DSNAME AND
**  MEMBER NAME,  2)  THE 'DO NOT WRITE BACK' BIT, 3) THE 'MEMBER OF A
**  PDS' BIT, AND 4) THE VOLSERIAL WITH A VOL COUNT OF 1 - THE REST OF
**  THE JFCB IS BINARY ZEROES.
         OPEN  (PDCB),TYPE=J
         LA    3,40
         LA    4,LOCAREA
**  READ EACH PRESRES ENTRY AND MOVE THE FIRST 6 CHARACTERS TO THE
**  'DO NOT UNLOAD' LIST - NOTE THAT IT IS ASSUMED THAT ALL VOLUMES
**  HAVE SIX CHARACTER SERIALS.
**  BSAM IS USED TO READ PRESRES, BUT ONLY BECAUSE WHEN TESTING IT WAS
*  FOUND THAT QSAM GET LOCATE GAVE AN 001 ABEND AT END OF DISK TRACK -
*  HOWEVER BSAM ALSO GAVE THIS AND THE REASON WAS FOUND TO BE THAT THE
*  PARMLIB DSCB HAD RECFM FS - THE PROBLEM WAS CURED BY CODING RECFM F
*  IN THIS PROGRAM.
GETP     READ  PDECB,SF,PDCB,PBLK,80
         CHECK PDECB
         MVC   0(6,4),PBLK
         LA    4,6(4)
         BCT   3,GETP
         ABEND 14,DUMP
PEOF     CLOSE (PDCB)
         L     1,TUCB
         MVC   16(4,1),ORUCB
*        SPMODE  PROB,0
         STM   14,2,12(13)
         CNOP  0,4
         BAL   15,*+8
         DC    V($SPMODE)
         L     15,0(15)
         LA    1,4
         BALR  14,15
         LM    14,2,12(13)
$SPMODE  CSECT
         LA    2,IHB3A-8-$SPMODE(15)
         SVC   54
         BR    14
IHB3B    LPSW  IHB3C-*(12)
         L     2,16(0)
         L     2,0(2)
         L     3,4(2)
         L     2,0(3)
         L     2,28(2)
         MVZ   17(1,2),28(3)
         OI    17(2),1
         EX    0,IHB3D-IHB3B(12,1)
         BR    13
IHB3A    DC    2A(*-40),A(IHB3B)
IHB3D    OI    17(2),1                 PP MODE WITH USER KEY
         NI    17(2),15                P/P MODE WITH ZERO KEY
         NI    17(2),X'FE'             SUP MODE WITH USER KEY
         NI    17(2),14                SUP MODE WITH ZERO KEY
IHB3C    DC    0D'0',X'FF040000',A(IHB3B+4)
DISMOUNT CSECT
**  HERE WE FIND THE TIOT AND CLEAR THE MESSAGE WTO
         L     1,16
         L     1,0(1)
         L     1,4(1)
         L     1,12(1)
         MVC   MSG+8(8),0(1)
         LA    5,MSG+39
         MVC   MSG+39(60),SPACES
         LA    2,24(1)
         SR    3,3
**  SEARCH THE TIOT, AND FOR EACH DISK VOLUME MENTIONED PUT ON THE
**  UNLOAD BIT IN ITS UCB UNLESS THE VOLUME IS EITHER IN THE LIST
**  EXTRACTED FROM PRESRES OR THE UCB IS MARKED PERM. RES.
TIOTLP   IC    3,0(2)
         LTR   3,3
         BZ    ENDTIOT
SUBSVOL  L     4,16(2)
         CLI   18(4),32
         BNE   NEXT
         NC    28(6,4),28(4)
         BZ    NEXT
         LA    8,LOCAREA
UNLOOP   CLI   0(8),0
         BE    UNLOAD
         CLC   28(6,4),0(8)
         BE    NEXT
         LA    8,6(8)
         B     UNLOOP
UNLOAD   TM    3(4),4                  IS IT PERM RES.
         BO    NEXT
         OI    3(4),16                 NO - UNLOAD IT.
         MVC   0(6,5),28(4)
         MVI   6(5),C','
         MVC   7(3,5),13(4)
         LA    5,12(5)
         C     5,ENDMSG
         BNL   WTMSG
MSGWRIT  C     3,TWENTY
         BE    NEXT
         LA    2,4(2)
         S     3,FOUR
         B     SUBSVOL
NEXT     AR    2,3
         B     TIOTLP
WTMSG    LA    1,MSG
         WTO   MF=(E,(1))
         MVC   MSG+39(60),SPACES
         LA    5,MSG+39
         B     MSGWRIT
ENDTIOT  CLI   MSG+39,64
         BE    GOBACK
         LA    1,MSG
         WTO   MF=(E,(1))
GOBACK   DS    0H
*        GOBACK
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
FOUR     DC    F'4'
TWENTY   DC    F'20'
ENDMSG   DC    A(MSG+96)
SPACES   DC    CL60' '
MSG      WTO   'JOB 12345678 HAS ISSUED UNLOAD FOR 123456,123  123456,1*
               23  123456,123  123456,123  123456,123  ',MF=L
         DC    CL4' '
PGM      DC    C'PGM'
PBLK     DC    CL80' '
PDCB     DCB   DDNAME=X,DSORG=PS,MACRF=(R),EODAD=PEOF,EXLST=XLIST,     *
               RECFM=F,LRECL=80,BLKSIZE=80
PARMLIB  CAMLST  NAME,JFCB,,LOCAREA
LOCAREA  DC    34D'0'
PUCB     DC    F'0'
ORUCB    DC    F'0'
TUCB     DC    F'0'
XLIST    DC    X'87',AL3(JFCB)
JFCB     DC    CL44'SYS1.PARMLIB'
         DC    CL8'PRESRES'
         DC    X'08'
         DC    XL64'00'
         DC    X'01'
JFCBVOL  DC    CL6' '
         DC    XL52'00'
         END
