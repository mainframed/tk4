RTPDSCPZ CSECT
*
*  THIS PROGRAM READS  AN INPUT TAPE (DDNAME = TAPE) AND COPIES IT,
*  INCLUDING ANY SINGLE TAPEMARKS, UNTIL IT FINDS A DOUBLE TAPEMARK.
*
*  WHATEVER IS READ (UP TO A MAXIMUM BLKSIZE OF 10000) IS COPIED ONTO
*  A NUMBER OF OUTPUT TAPES WITHOUT ALTERATION.  THE NUMBER OF OUTPUT
*  TAPES (FROM 1 - 5) IS DEFINED IN THE PARM FIELD OF THE EXEC CARD.
*
*  THE TYPE OF OUTPUT TAPE IS DEFINED BY THE USER THROUGH DD CARDS WITH
*  DDNAMES OPTAPE1, OPTAPE2,...OPTAPE5.
*
*  IT IS POSSIBLE TO BEGIN COPYING FROM ANY POINT ON THE INPUT TAPE
*  THAT CAN BE SPECIFIED BY JCL (LABEL=...).
*  SIMILARLY THE OUTPUT TAPE(S) CAN BE OPENED SO THAT THE PREVIOUS
*  CONTENTS ARE ADDED TO (O.S. WILL TREAT THE OUTPUT OF THIS PROGRAM AS
*  A SINGLE FILE BEING ADDED TO THE TAPE).
*
*  JCL CAN ALSO BE USED TO SPECIFY DIFFERENT UNIT TYPES, MODES, AND
*  DENSITIES FOR ANY OF THE 6 TAPES INVOLVED.
*
*  THE JCL REQUIRED IS (EXAMPLE)..
*        //MULTCOPY EXEC PGM=RTPDSCPZ,PARM=2
*        //TAPE DD UNIT=2400,VOL=SER=INPUT,DSN=FRED,DISP=(SHR,KEEP),
*        //    LABEL=(,BLP)
*        //OPTAPE1 DD UNIT=2400,LABEL=(,NL),DCB=DEN=2,DSN=A,
*        //    DISP=(NEW,KEEP)
*        //OPTAPE2 DD UNIT=2400-2,LABEL=(,NL),DCB=(DEN=2,TRTCH=C),
*        //    DSN=B,DISP=(NEW,KEEP)
*
LEN      EQU   10
PRINNO   EQU   9                 NUMBER OF RECORDS TO BE PRINTED
TMCNT    EQU   7                 NUMBER OF TAPEMARKS COUNTED
R11      EQU   11
MSGPTR   EQU   6                 FOR DIAGNOSTIC MESSAGES
R3       EQU   3
FILES    EQU   X'40'             FILE OPTION SPECIFIEC
EVEN     EQU   X'80'             PRINT EVEN ON I/O ERROR
BADT1    EQU   X'10'
BADT2    EQU   X'20'
BADT3    EQU   X'03'
BADT4    EQU   X'40'
BADT5    EQU   X'05'
R1       EQU   1
COUNT    EQU   2
PARM     EQU   4
R5       EQU   5
R8       EQU   8
R9       EQU   9
F1        EQU  X'10'
F2        EQU  X'11'
F3        EQU  X'12'
F4        EQU  X'14'
F5        EQU  X'18'
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         L     R1,0(R1)      PARM POINTER
         IC    PARM,2(R1)    NUMBER OF COPIES REQUIRED
         SLL   PARM,28
         SRL   PARM,28
    GETMAIN  R,LV=10000
  LR  R9,R1
  USING AREAF,R9
  ST  R9,OINCCW
  MVI  OINCCW,X'02'
         LR    R5,PARM
REOPEN   DS    0H
         OPEN  (TAPEOLD,INPUT,PRINT,OUTPUT)
         MVI   ECB,0         CLEAR FOR INPUT
         L     COUNT,F10000  SET MAX COUNT
         EXCP  IOBIN         READ FIRST BLOCK
         WAIT  ECB=ECB
         CLI   ECB,X'7F'     GOOD?
         BNE   GETOUT        NO LEAVE
         SH    COUNT,IOBIN+14          GET REMAINDER
         CH    COUNT,H80     SL?
         BE    *+12          YES MAY BE
         OI    SLFLG,X'40'   NOT SL
         B     SLIN          CONTINUE
         CLC   AREA(4),VOL1  IS THIS STANDARD HEADER
         BE    *+8           SL
         OI    SLFLG,X'40'   NOT SL
SLIN     DS    0H
         MVC   USECCW,AOINCCW          READ OUTPUT TAPE
         XC    ECB1(20),ECB1
         RDJFCB TAPE1
          TM   JFCB+66,X'02'       STANDARD LABEL?
          BO   CHK1
          OI   SLFLG,F1
          B    *+8
CHK1      EQU  *
          OI   SL1FLG,F1
         NI    JFCB+66,X'FD'
         OI    JFCB+66,X'10' SET BLP
         NI    JFCB+52,X'F7'
         OPEN  (TAPE1,OUTPUT),TYPE=J
          TM   SLFLG,F1
          BO   OP2-8
    L     10,F10000
         EXCP  IOB1
          WAIT ECB=ECB1
         CLI   ECB1,X'7F'
  BE  *+12
         OI    SW,BADT1
         B     OUT
  SH  10,IOB1+14
  CH  10,H80
         BNE   NL1
         CLC   AREAF(4),VOL1
         BNE   NL1
         BCT   R5,OP2
         B     TOP
OP2      DS    0H
         RDJFCB TAPE2
   MVC  USE2,USECCW
          TM   JFCB+66,X'02'       STANDARD LABEL?
          BO   CHK2
          OI   SLFLG,F2
          B    *+8
CHK2      EQU  *
          OI   SL1FLG,F2
         NI    JFCB+66,X'FD'
         OI    JFCB+66,X'10' SET BLP
         NI    JFCB+52,X'F7'
         OPEN  (TAPE2,OUTPUT),TYPE=J
          TM   SLFLG,F2
          BO   OP3-8
    L     10,F10000
         EXCP  IOB2
         WAIT  ECB=ECB2
         CLI   ECB2,X'7F'
  BE  *+12
         OI    SW,BADT2
         B     OUT
    SH    10,IOB2+14
    CH    10,H80
         BNE   NL2
         CLC   AREAF(4),VOL1
         BNE   NL2
         BCT   R5,OP3
         B     TOP
OP3      DS    0H
         RDJFCB TAPE3
  MVC  USE3,USECCW
          TM   JFCB+66,X'02'       STANDARD LABEL?
          BO   CHK3
          OI   SLFLG,F3
          B    *+8
CHK3      EQU  *
          OI   SL1FLG,F3
         NI    JFCB+66,X'FD'
         OI    JFCB+66,X'10' SET BLP
         NI    JFCB+52,X'F7'
         OPEN  (TAPE3,OUTPUT),TYPE=J
          TM   SLFLG,F3
          BO   OP4-8
    L     10,F10000
         EXCP  IOB3
         WAIT  ECB=ECB3
         CLI   ECB3,X'7F'
  BE  *+12
         OI    SW,BADT3
         B     OUT
    SH   10,IOB3+14
    CH    10,H80
         BNE   NL3
         CLC   AREAF(4),VOL1
         BNE   NL3
         BCT   R5,OP4
         B     TOP
OP4      DS    0H
         RDJFCB TAPE4
  MVC  USE4,USECCW
          TM   JFCB+66,X'02'       STANDARD LABEL?
          BO   CHK4
          OI   SLFLG,F4
          B    *+8
CHK4      EQU  *
          OI   SL1FLG,F4
         NI    JFCB+66,X'FD'
         OI    JFCB+66,X'10' SET BLP
         NI    JFCB+52,X'F7'
         OPEN  (TAPE4,OUTPUT),TYPE=J
          TM   SLFLG,F4
          BO   OP5-8
    L     10,F10000
         EXCP  IOB4
         WAIT  ECB=ECB4
         CLI   ECB4,X'7F'
  BE  *+12
         OI    SW,BADT4
         B     OUT
    SH   10,IOB4+14
    CH    10,H80
         BNE   NL4
         CLC   AREAF(4),VOL1
         BNE   NL4
         BCT   R5,OP5
         B     TOP
OP5      DS    0H
         RDJFCB TAPE5
  MVC  USE5,USECCW
          TM   JFCB+66,X'02'       STANDARD LABEL?
          BO   CHK5
          OI   SLFLG,F5
          B    *+8
CHK5      EQU  *
          OI   SL1FLG,F5
         NI    JFCB+66,X'FD'
         OI    JFCB+66,X'10' SET BLP
         NI    JFCB+52,X'F7'
         OPEN  (TAPE5,OUTPUT),TYPE=J
          TM   SLFLG,F5
          BO   TOP
    L    10,F10000
         EXCP  IOB5
         WAIT  ECB=ECB5
         CLI   ECB5,X'7F'
  BE  *+12
         OI    SW,BADT5
         B     OUT
    SH    10,IOB5+14
    CH    10,H80
         BNE   NL5
         CLC   AREAF(4),VOL1
         BNE   NL5
TOP      EQU   *
         MVC   ECB1(20),GOOD SET UP GOOD DUMMY COMPLETE
          TM   SLFLG,X'40'         INPUT STANDARD LABEL
   BNO  *+16
          TM   SL1FLG,X'10'        WAS AN SL FOUND
          BO   ERRCOMP              YES INCOMPATIBLE
         B     GOODREAD+4          GO WRITE FIRST RECORD
          TM   SLFLG,X'10'       _AS AN NL FOUND?
         BO    ERRCOMP            YES INCOMPATIBLE
READ     EQU   *
         WAIT  5,ECBLIST=LISTECB
         CLC   ECB1(20),GOOD                GOOD WRITES?
         BNE   BADTAPE
LOOP     NI    ECB,X'3F'
         L     COUNT,F10000
         EXCP  IOBIN
         WAIT  ECB=ECB
         CLI   ECB,X'7F'     GOOD           READ?
         BE    GOODREAD
         TM    IOBIN+12,X'01'               TM?
         BO    LAST
         B     GETOUT
LAST     EQU   *
DOUBLTM  CLI   FLAG,X'FF'        WAS TM READ LAST TIME
         BE    EOT
         MVI   FLAG,X'FF'
WTMNOW   MVC   USECCW,AWTMCCW
         AP    TCNT(2),PONE(1)     TAPE MARK COUNT
         B     WRITE
GOODREAD DS    0H
         SH    COUNT,IOBIN+14               SET UP LENGTH FIELD
         MVI   FLAG,0
         STH   COUNT,OUTCCW+6
         MVC   USECCW,AOUTCCW
WRITE    EQU   *
WRTTAPE  LR    R5,PARM
WRITOUT  LR    R5,PARM
W1       DS    0H
         TM    SW,BADT1
     BO   *+14
S1       MVI   ECB1,X'00'
         EXCP  IOB1
         BCT   R5,W2
         B     READ
W2       DS    0H
         TM    SW,BADT2
     BO   *+20
S2       MVI   ECB2,X'00'
         MVC   USE2,USECCW
         EXCP  IOB2
         BCT   R5,W3
         B     READ
W3       DS    0H
         TM    SW,BADT3
     BO   *+20
S3       MVI   ECB3,X'00'
         MVC   USE3,USECCW
         EXCP  IOB3
         BCT   R5,W4
         B     READ
W4       DS    0H
         TM    SW,BADT4
     BO   *+20
S4       MVI   ECB4,X'00'
         MVC   USE4,USECCW
         EXCP  IOB4
         BCT   R5,W5
         B     READ
W5       DS    0H
         TM    SW,BADT5
     BO   *+20
S5       MVI   ECB5,X'00'
         MVC   USE5,USECCW
         EXCP  IOB5
         B     READ
EOT      CLOSE (TAPEOLD,,TAPE1)
         LR    R5,PARM
         BCT   R5,C2
         B     OUT
C2       CLOSE (TAPE2)
         BCT   R5,C3
         B     OUT
C3       CLOSE (TAPE3)
         BCT   R5,C4
         B     OUT
C4       CLOSE (TAPE4)
         BCT   R5,C5
         B     OUT
C5       CLOSE (TAPE5)
OUT      TM    SW,BADT1
         BNO   TST2
         MVI   WTOTAB,C'1'
         BAL   R8,WMSG
TST2     TM    SW,BADT2
         BNO   TST3
         MVI   WTOTAB,C'2'
         BAL   R8,WMSG
TST3     TM    SW,BADT3
         BNO   TST4
         MVI   WTOTAB,C'3'
         BAL   R8,WMSG
TST4     TM    SW,BADT4
         BNO   TST5
         MVI   WTOTAB,C'4'
         BAL   R8,WMSG
TST5     TM    SW,BADT5
          BNO  GOBACKG
         MVI   WTOTAB,C'5'
         BAL   R8,WMSG
         B     GOBACK
WMSG     PUT   PRINT,OUTBAD
         BR    R8
NL5      DS    0H
NL4      DS    0H
NL3      DS    0H
NL2      DS    0H
NL1      DS    0H
ERRCOMP  PUT  PRINT,INC
          B    GOBACK
GOBACKG  DS    0H
         UNPK  SUCC+13(3),TCNT     TAPE MARK COUNT
         OI    SUCC+15,X'F0'
         PUT   PRINT,SUCC
GOBACK   DS    0H
         CLOSE (PRINT)
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),255
         LA    15,0
         BR    14
BADTAPE  CLI   ECB1,X'7F'
         BE    T2
         OI    SW,BADT1
T2       CLI   ECB2,X'7F'
         BE    T3
         OI    SW,BADT2
T3       CLI   ECB3,X'7F'
         BE    T4
         OI    SW,BADT3
T4       CLI   ECB4,X'7F'
         BE    T5
         OI    SW,BADT4
T5       CLI   ECB5,X'7F'
         BE    READ
         OI    SW,BADT5
         B     READ
GETOUT   DS    0H
         PUT   PRINT,INBAD
         B     GOBACK
         LTORG
DOUBLE   DS    D
INAREA   DS    80C
LASTFILE DS    H
RECPRT   DS    H
RECPRTB  DS    H
SL1FLG    DC   X'00'
OPSW     DC    X'00'             OPTIONS REQUIRED
ECB      DC    F'00'
ECB1     DC    X'7F000000'
ECB2     DC    X'7F000000'
ECB3     DC    X'7F000000'
ECB4     DC    X'7F000000'
ECB5     DC    X'7F000000'
GOOD     DS    0F
         DC    X'7F000000'
         DC    X'7F000000'
         DC    X'7F000000'
         DC    X'7F000000'
         DC    X'7F000000'
IOBIN    DC    X'02000000'
         DC    A(ECB)
         DC    2F'0'
         DC    A(INCCW)
         DC    A(TAPEOLD)
         DC    2F'0'
INCCW    CCW   X'02',AREA,X'20',10000
AOUTCCW  DC    A(OUTCCW)
AWTMCCW  DC    A(WTMCCW)
AOINCCW  DC    A(OINCCW)
OINCCW   CCW   X'02',0,X'20',10000
OUTCCW   CCW   X'01',AREA,X'20',100
WTMCCW   CCW   X'1F',AREA,X'20',100
IOB1     DC    X'02000000'
         DC    A(ECB1)
         DC    2F'0'
USECCW   DS    F
         DC    A(TAPE1)
         DC    2F'0'
IOB2     DC    X'02000000'
         DC    A(ECB2)
         DC    2F'0'
USE2     DS    F
         DC    A(TAPE2)
         DC    2F'0'
IOB3     DC    X'02000000'
         DC    A(ECB3)
         DC    2F'0'
USE3     DS    F
         DC    A(TAPE3)
         DC    2F'0'
IOB4     DC    X'02000000'
         DC    A(ECB4)
         DC    2F'0'
USE4     DS    F
         DC    A(TAPE4)
         DC    2F'0'
IOB5     DC    X'02000000'
         DC    A(ECB5)
         DC    2F'0'
USE5     DS    F
         DC    A(TAPE5)
         DC    2F'0'
LISTECB  DC    A(ECB1)
         DC    A(ECB2)
         DC    A(ECB3)
         DC    A(ECB4)
FINAL    DC    X'80',AL3(ECB5)
TAPEOLD  DCB   DSORG=PS,MACRF=(E),DDNAME=TAPE
INC       DC   CL133'-  INCOMPATIBLE LABEL ATTRIBUTES ON INPUT AND OUTPU
               UT TAPES'
PRINT DCB DDNAME=SYSPRINT,MACRF=PM,RECFM=FA,LRECL=133,DSORG=PS,        *
               BLKSIZE=133
VOL1     DC    C'VOL1'
SLFLG    DC    X'00'
H80      DC    H'80'
INBAD    DC    CL133'- INPUT TAPE ERROR'
SUCC     DC    CL133'- COMPLETED        TAPE MARKS COPIED'
PONE     DC    X'1F'
TCNT     DC    X'001F'
OUTBAD   DC    CL133'- '
         ORG    OUTBAD+2
         DC    C' ERROR ENCOUNTERED ON OUTPUT TAPE '
WTOTAB   DS    C
         ORG
JFCB     DC    44F'0'
EX       DC    X'87',AL3(JFCB)
TAPE5    DCB   MACRF=E,DDNAME=OPTAPE5,EXLST=EX
TAPE4    DCB   MACRF=E,DDNAME=OPTAPE4,EXLST=EX
TAPE3    DCB   MACRF=E,DDNAME=OPTAPE3,EXLST=EX
TAPE2    DCB   MACRF=E,DDNAME=OPTAPE2,EXLST=EX
TAPE1    DCB   MACRF=E,DDNAME=OPTAPE1,EXLST=EX
FLAG     DC    X'00'
F10000   DC    F'10000'
SW       DC    X'00'
         DC    X'0F'
AREA     DS    1250D
AREAF    DS    1250D
         END
