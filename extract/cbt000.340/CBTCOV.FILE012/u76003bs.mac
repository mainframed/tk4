*          DATA SET ZMONPRT    AT LEVEL 002 AS OF 28/12/78
SMON TITLE 'REPORT GENERATOR FOR SUPERMON - INITIALISATION           '
REPORT   CSECT
         ENTRY SYSPRINT
         ENTRY READ
         ENTRY IEBSW
         ENTRY DEVTBL
         ENTRY PAGNO
         ENTRY HEAD
         ENTRY SLSW
         ENTRY D8
         USING REPORT,13,11,12
         STM   14,12,12(13)
         ST    15,8(0,13)
         ST    13,4(0,15)
         LR    13,15
         LM    11,12,BASES
         OPEN  (SYSPRINT,OUTPUT,INPUT,)
         TM    SYSPRINT+48,16
         BZ    ABEND
         TM    INPUT+48,16
         BZ    ABEND
         TM    LDSW,LDM
         BZ    NOPL
         OPEN  (LOAD,OUTPUT)
         TM    LOAD+48,16
         BZ    ABEND
NOPL     EQU   *
         USING REC,2
         GET   INPUT
         LR    2,1
         OPEN  (LOAD,OUTPUT)
     TITLE 'REPORT GENERATOR FOR SUPERMON - EDIT RECORD ZERO         '
         OC    RECTYP,RECTYP
         BNZ   NOTZ
         LA    14,SYSTAB
         LA    15,(ESYSTAB-SYSTAB)/5
NSYS     CLC   SYSTYP,0(14)
         BE    SFND
         LA    14,5(0,14)
         BCT   15,NSYS
SFND     MVC   SYSTP(4),1(14)
         MVI   SYSTYP,0
         UNPK  12(3,13),DATE+1(2)
         MVC   DAT+11(2),12(13)
         MVI   DWWD+3,12
         MVO   DWWD(4),DATE(2)
         DP    DWWD(4),P4
         TM    DWWD+3,240
         BNZ   NOLEAP
         MVI   MONTHS+3,X'9C'
NOLEAP   SR    5,5
NEXTM    LA    4,MONTHS(5)
         SP    DATE+2(2),0(2,4)
         BC    13,STOPM
         LA    5,2(0,5)
         B     NEXTM
BASES    DC    A(REPORT+4096,REPORT+8192)
STOPM    AP    DATE+2(2),0(2,4)
         OI    DATE+3,15
         UNPK  DWWD(3),DATE+2(2)
         MVC   DAT+5(2),DWWD+1
         SRA   5,1
         LA    5,1(0,5)
         CVD   5,DWWD
         OI    DWWD+7,15
         UNPK  DWWD(3),DWWD+6(2)
         MVC   DAT+8(2),DWWD+1
         ED    TIM,TIMZ
         MVC   INTVL+1(3),REPTIN+1
         CLI   DAREQ,241
         BNE   DRQ
         MVC   SKR+5(13),=C'NOT COLLECTED'
         NI    DASW,255-DAM
DRQ      MVC   SMPL(2),CYCLE
         MVC   SMPL+3(2),CYCLE+2
         MVC   RE,NOREPT
         CLI   RE,240
         BNE   *+8
         MVI   RE,64
         CLI   LDREQ,241
         BNE   LRQ
         MVC   LDR+5(13),=C'NOT COLLECTED'
         NI    LDSW,255-LDM-LFM
         NI    SVCSWW,255-SVCM
LRQ      EQU   *
         CLI   SMREQ,241
         BNE   SRQ
         MVC   SKR+5(13),=C'NOT COLLECTED'
         NI    SMSW,255-SMM
SRQ      EQU   *
         L     15,=V(READ)
         BALR  14,15
         BAL   9,PAGPRT
         LA    5,7
         LA    4,D1
PNLA     PUT   SYSPRINT,(4)
         LA    4,133(0,4)
         BCT   5,PNLA
         LA    4,D5
         CLI   3(4),X'40'
         BE    NODISK
         PUT   SYSPRINT,(4)
NODISK   LA    4,D6
         LA    5,3
PNLA1    PUT   SYSPRINT,(4)
         LA    4,133(0,4)
         BCT   5,PNLA1
         MVC   TITL(40),=CL40'SYSTEM DESCRIPTION'
         MVC   CPUMDL(3),CPUMOD+1
         MVC   REL(2),RELNO
         MVC   REL+3(2),RELNO+2
         L     15,NUCSZ
         BAL   14,CVD
         MVC   DW,PATX
         ED    DW,DWWD+4
         MVC   NUCS(6),DW+2
         LH    15,CORSZ
         BAL   14,CVD
         MVC   COR(4),DW+4
         LH    15,RQENO
         BAL   14,CVD
         MVC   RQEN(3),DW+5
         MVC   SVCA(3),SVCUN+1
         MVC   LNKA(3),LNKUN+1
         MVC   JBQA(3),JBQUN+1
         MVC   NSEL(1),NOSEL+1
         OI    NSEL,240
         MVC   LKAD(3),LNKA
         MVC   SVCAD(3),SVCA
         MVC   JQAD(3),JBQA
         CLI   SYSTP,C'V'
         BE    *+10
         MVC   RAMS+14(30),RAMS+13
         MVC   PAGA(16),PGUN+1
         MVC   NOPAGZ,NOPAGS
         BAL   9,PAGPRT
         PUT   SYSPRINT,D17
         PUT   SYSPRINT,D18
REC0     L     10,LPANO
         L     1,=V(RECSV)
         MVC   0(256,1),0(2)
         MVC   256(80,1),256(2)
         LR    15,10
         A     15,LPATOT
         ST    15,LPATOT
         LR    15,10
         MH    15,=H'12'
         LA    15,LPAMOD(15)
         MVI   0(15),64
         SRA   10,3
         LA    10,1(0,10)
         LA    8,LPAMOD
NULPA    LA    9,8
         LA    7,LINE+4
NULPAX   EQU   *
         CLI   0(8),64
         BE    PLINZ
         MVC   0(8,7),0(8)
         L     15,8(0,8)
         BAL   14,CVD
         MVC   9(5,7),DW+3
         LA    7,16(0,7)
         LA    8,12(0,8)
         BCT   9,NULPAX
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         BCT   10,NULPA
         B     EOLP
PLINZ    CH    9,=H'8'
         BE    NOPLIN
PLIN     PUT   SYSPRINT,LINE
NOPLIN   MVC   LINE,LINE-1
EOLP     EQU   *
         BAL   9,LFLINT
     TITLE 'REPORT GENERATOR FOR SUPERMON - READ INPUT FILE          '
RNXT     GET   INPUT
         LR    2,1
BSET     LH    15,RECTYP
         AR    15,15
         AR    15,15
BNOP     NOP   BT(15)
         LTR   15,15
         BZ    REC0
         ST    2,0(0,13)
         L     2,=V(RECSV)
         L     15,LPATOT
         BAL   14,CVD
         MVC   LKNO(4),DW+4
         PUT   SYSPRINT,D19
         L     15,SQASZ
         L     14,BLDSZ
         A     14,RAMSZ
         A     14,SVCSZ
         ST    14,NSQA
         SR    15,14
         MVC   DW,PATX
         ED    DW,DWWD+4
         BAL   14,CVD
         MVC   SQAS(6),DW+2
         L     15,BLDSZ
         BAL   14,CVD
         MVC   BLDS(6),DW+2
         L     15,RAMSZ
         BAL   14,CVD
         MVC   RAMS(6),DW+2
         L     15,SVCSZ
         BAL   14,CVD
         MVC   SVCS(6),DW+2
         L     15,NUCSZ
         A     15,SQASZ
         BAL   14,CVD
         MVC   ARSS(6),DW+2
         BAL   9,PAGPRT
         LA    5,7
         LA    4,D9
PNLB     PUT   SYSPRINT,(4)
         LA    4,133(0,4)
         BCT   5,PNLB
         NI    NSEL,15
         LA    3,DADEV
         LA    4,DADEVT
         LA    7,DEVTBL
         LA    8,3
         L     10,=V(DAPARM)
         LTR   10,10
         BZ    NOPIT
         CLI   0(10),64
         BE    NOPX
NOPIT    MVC   DMC(6),=5X'0700'
         MVC   DMD(4),=5X'0700'
         MVC   DME(4),=5X'0700'
         MVC   DMC+6(4),=5X'0700'
NOPX     EQU   *
NLIN     LA    5,DEVAD
         LA    6,DEVTYP
         MVC   DEVAD(118),DEVAD-1
         MVC   DEVTYP(118),DEVTYP-1
         NI    SW1,255-128
         LA    9,16
NLINX    CLI   1(3),64
         BE    ENTB
         OI    SW1,128
         MVC   0(3,5),1(3)
         LA    14,(EDATP-DATAP)/5
         LA    15,DATAP
NDTP     CLC   0(1,15),0(4)
         BE    DTPF
         LA    15,5(0,15)
         BCT   14,NDTP
DTPF     MVC   0(4,6),1(15)
         MVC   3(4,7),0(6)
         MVC   0(3,7),0(5)
DMC      MVC   0(3,10),0(5)
         MVI   3(10),C','
         LA    10,4(0,10)
         CH    9,=H'9'
         BNE   NDA
         BCTR  10,0
DME      MVI   0(10),64
         LA    10,49(0,10)
NDA      EQU   *
         LA    5,5(0,5)
         LA    6,5(0,6)
         LA    7,7(0,7)
         LA    3,4(0,3)
         LA    4,1(0,4)
         BCT   9,NLINX
         PUT   SYSPRINT,D15
         PUT   SYSPRINT,D16
         BCTR  10,0
DMD      MVI   0(10),64
         LA    10,49(0,10)
         BCT   8,NLIN
         B     EODA
ENTB     TM    SW1,128
         BZ    EODA
         PUT   SYSPRINT,D15
         PUT   SYSPRINT,D16
EODA     EQU   *
         S     7,=A(DEVTBL)
         SR    6,6
         D     6,=F'7'
         STH   7,NODEV
         MVC   MXRQE(3),RQEN
         MVC   SQASS(6),SQAS
         L     2,0(0,13)
         OI    BNOP+1,240
         TM    SMSW,SMM+LDM+LFM
         BZ    EOFI
         TM    SMSW,SMM+LFM
         BNO   NOTSM
         OC    RESTS(1),LDSW
         MVI   REST2+1,240
         NI    SMSW,255-LDM-LFM
NOTSM    EQU   *
         B     BSET
BT       B     RNXT
         B     REC1
         B     REC2
         B     REC3
         B     REC4
REC2     TM    LDSW,LDM+LFM
         BZ    RNXT
         MVC   HEAD+21(4),=CL4'TIME'
     TITLE 'REPORT GENERATOR FOR SUPERMON - EDIT RECORD TWO          '
         MVC   HEAD+25(9),TIMPAT
         ED    HEAD+25(9),TIMZ
         L     8,LCNT
         LA    3,170
         LA    4,LOADR
         L     5,ATBL
         USING LDS,4
         USING TDS,5
NLENT    CLI   LTYP,0
         BE    EOR21
NLENTA   TM    TUSE,128+64
         BO    OPLN
         BZ    FREE
         MVI   TLIB,C'R'
OPLN     SR    15,15
         IC    15,TTCB
         IC    15,PTTCB(15)
         LTR   15,15
         BZ    OTCB
         LA    14,0(15,15)
         LA    14,TCNTX-2(14)
         MVI   1(14),1
         IC    15,PTBL-1(15)
TBQ      CLI   TNAM+7,X'C0'
         BNE   *+8
         MVI   TNAM+7,C'*'
         CLI   TBLD,C'B'
         MVI   TBLD,64
         BE    TBB
         CLI   TLIB,C'R'
         BE    TBB
         CLI   TTYP,C'F'
         BE    TBB
         LTR   15,15           POSN FOR B
         BZ    TB
         MVI   LINE+10,C'B'
         MVC   TBLD,LINE+10
         B     TBB
TB       MVI   LINE+16,C'B'
         MVC   TBLD,LINE+16
TBB      LA    15,LINE(15)
         TM    LFSW,LFM
         BZ    REC2A
CTCB     CLI   TTYP,C'L'
         BH    XCTT
         BE    LNKT
         CLI   TTYP,C'D'
         BL    ATTT
         BE    LODT
         MVC   0(4,15),=C'FTCH'
         B     NAMFIL
OTCB     IC    15,TTCB
         BAL   14,CVD
         MVC   LINE+123(2),DW+6
         LA    15,109
         MVI   TCNTX+19,1
         B     TBQ
ATTT     MVC   0(4,15),=C'ATCH'
         B     NAMFIL
LODT     MVC   0(4,15),=C'LOAD'
         B     NAMFIL
LNKT     MVC   0(4,15),=C'LINK'
         B     NAMFIL
XCTT     MVC   0(4,15),=C'XCTL'
NAMFIL   MVC   5(8,15),TNAM
         CLI   TLIB,C'R'
         BH    SVCXN
         BE    RESID
         CLI   TLIB,C'L'
         BE    LNKXN
         BL    JBQXN
         MVC   LINE+126(7),=C'PRIVATE'
         B     NXLQ
JBQXN    MVC   LINE+126(7),=C'JOBLIB '
         B     NXLQ
LNKXN    MVC   LINE+126(7),=C'LINKLIB'
         B     NXLQ
RESID    MVC   LINE+126(7),=C'RESIDNT'
         B     NXLQ
SVCXN    MVC   LINE+126(7),=C'SVCLIB '
NXLQ     EQU   *
         TM    TUSE,32
         BZ    NOTTA
         MVC   LINE+8(2),=X'4020'
         NI    TUSE,15
         SR    15,15
         IC    15,TUSE
         CVD   15,DW
         ED    LINE+8(2),DW+7
         MVC   LINE+1(7),=C'FETCHER'
NOTTA    BCT   8,PLINX
         MVC   TITL(40),=CL40'MODULE LOADING (CHRONOLOGICAL)'
         BAL   9,PAGPRT
         PUT   SYSPRINT,D7
         LA    8,55
PLINX    PUT   SYSPRINT,LINE
         TM    LDSW,LDM
         BZ    NOLX
REC2A    PUT   LOAD,TTYP
         XC    TCNTX(20),TCNTX
NOLX     MVC   LINE,LINE-1
         MVC   LINE,LINE-1
         MVI   TUSE,0
FREE     NOP   EOR22
         CLI   LTYP,C'F'
         BE    FTCH
FRTN     MVC   TTYP(LLDS),LTYP
         OI    TUSE,128
         CLI   TTYP,C'R'
         BNE   NOTRS
         OI    TUSE,64
         MVI   TTCB,0
         MVI   TTYP,C'F'
         MVI   TLIB,C'R'
NOTRS    L     5,TLNK
NOTU5    LA    4,LLDS(0,4)
         BCT   3,NLENT
         ST    5,ATBL
         ST    8,LCNT
EOR2     B     RNXT
EOR21    LA    3,10
         MVI   FREE+1,240
         MVI   NLENTA+5,X'70'
         CLI   TLIB,C'O'
         BNE   *+8
         MVI   TLIB,C'S'
         B     NLENTA
EOR22    L     5,TLNK
         CLI   TLIB,C'O'
         BNE   *+8
         MVI   TLIB,C'S'
         BCT   3,NLENTA
         MVI   FREE+1,0
         MVI   NLENTA+5,16
         B     RNXT
FTCH     L     6,TBAK
         DROP  5
         USING TDS,6
         LA    14,10
TSTLT    TM    TUSE,128+64
         BNM   NXTF
         CLC   LNAM,TNAM
         BE    ELFN
NXTF     L     6,TBAK
         BCT   14,TSTLT
         DROP  6
         USING TDS,5
         OI    TUSE,128+64
         B     FRTN
         DROP  5
         USING TDS,6
ELFN     MVC   TLIB,LLIB
         CLC   LTCB,TTCB
         BE    *+14
         OC    TUSE,LTCB
         OI    TUSE,32
         OI    TUSE,64
         B     NOTU5
REC3     B     RNXT
REC4     B     RNXT
     TITLE 'REPORT GENERATOR FOR SUPERMON - EDIT RECORD ONE PAGE ONE '
REC1     TM    SMSW,SMM
         BZ    RNXT
         LH    15,RECNO
*        CH    15,STREC                TROUBLE     STEPH SPT
*        BL    RNXT                    TROUBLE     STEPH SPT
*        CH    15,STPREC               TROUBLE     STEPH SPT
*        BH    RNXT                    TROUBLE     STEPH SPT
         MVC   TITL(40),=CL40'SAMPLING REPORT NO. XXXX'
         AP    REPNO,=PL1'1'
         UNPK  DWWD,REPNO
         MVC   TITL+20(4),DWWD+4
         OI    TITL+23,240
         MVC   HEAD+21(4),=CL4'TIME'
         MVC   HEAD+25(9),TIMPAT
         ED    HEAD+25(9),TIMZ
         BAL   9,PAGPRT
         MVC   INTU(5),SMPL
         LH    1,NOSAMP
         ST    1,NS
         CVD   1,DWWD
         MH    1,=H'5'
         ST    1,NS5
         UNPK  DW,DWWD+4(4)
         MVC   CYCL(5),DW+3
         OI    CYCL+4,240
         LH    15,NOSAMPC
         LA    1,CPU
         BAL   9,PERCENT
         LH    15,MAXRQE
         BAL   14,CVD
         MVC   RQE(2),DW+6
         L     15,SQA
         S     15,NSQA
         BAL   14,CVD
         MVC   SQAM(5),DW+3
         LH    15,RECNO
         BAL   14,CVD
         MVC   RECN(4),DW+4
         LH    15,MXDSP
         BAL   14,CVD
         MVC   MXRT(3),DW+5
         LH    15,NDSP
         MH    15,=H'100'
         SR    14,14
         D     14,NS
         BAL   14,CVD
         MVC   ART(3),DW+3
         MVC   ART+4(2),DW+6
         CLI   ART+4,X'40'
         BNE   PRINTOUT
         MVI   ART+4,X'F0'
PRINTOUT LA    5,6
         LA    4,E1
PNLS     PUT   SYSPRINT,(4)
         LA    4,133(0,4)
         BCT   5,PNLS
         MVC   LINE(24),=C'0   PROTECT KEY  =  ZERO'
PL2C     LH    15,PKNCS
         LA    1,LINE+31
         BAL   9,PERCENT
         LH    15,PKNCP
         LA    1,LINE+48
         BAL   9,PERCENT
         LH    15,PKNWS
         LA    1,LINE+66
         BAL   9,PERCENT
         LH    15,PKNWP
         LA    1,LINE+83
         BAL   9,PERCENT
         LH    15,PKNCS
         AH    15,PKNWS
         LA    1,LINE+101
         BAL   9,PERCENT
         LH    15,PKNCP
         AH    15,PKNWP
         LA    1,LINE+118
         BAL   9,PERCENT
         PUT   SYSPRINT,LINE
NL2      NOP   EOL2
NL3      NOP   EOL3
         MVI   NL2+1,240
         SH    2,=H'8'
         MVC   LINE+16(3),=C'NOT'
         B     PL2C
EOL2     L     15,PKNCS
         AL    15,PKNCS+8
         ST    15,PKNCS
         L     15,PKNWS
         AL    15,PKNWS+8
         ST    15,PKNWS
         MVI   NL2+1,0
         MVI   NL3+1,240
         MVC   LINE,LINE-1
         MVC   LINE(9),=C'0   TOTAL'
         B     PL2C
EOL3     MVI   NL3+1,0
         MVC   LINE,LINE-1
         LA    2,8(0,2)
         PUT   SYSPRINT,E3
         XR    4,4
         LA    3,PART
         USING PARTD,3
         LA    5,16
PLUP     LH    15,PSIZ
         LTR   15,15
         BZ    NP
         CVD   15,DWWD
         MVC   LINE+5(L'PPAT),PPAT
         CVD   4,DW
         ED    LINE+5(4),DW+6
         ED    LINE+15(6),DWWD+5
         L     15,PUSE
         CVD   15,DWWD
         ED    LINE+29(8),DWWD+4
         MVC   LINE+46(2),PCLS
         LH    15,PPPR
         LA    1,LINE+55
         BAL   9,PERCENT
         LH    15,PDSP
         LA    1,LINE+66
         BAL   9,PERCENT
         PUT   SYSPRINT,LINE
NP       LA    4,1(0,4)
         LA    3,12(0,3)
         BCT   5,PLUP
         SPACE 3
         PUT   SYSPRINT,E4
         PUT   SYSPRINT,E5
         MVC   LINE,LINE-1
         DROP  3
         LH    03,MAXRQE
         LR    14,3
         LA    3,1(0,3)
         AR    14,14
         LA    4,NORQE(14)
         MVI   LINE+72,C'Q'
         LA    5,9
         LA    6,ENQ
         LA    7,241
NXRQE    LH    15,0(0,4)
         LTR   15,15
         BZ    NRQE
         LA    1,LINE+32
         BAL   9,PERCENT
         BCTR  3,0
         CVD   3,DWWD
         LA    3,1(0,3)
         MVC   LINE+15(4),NPAT
         ED    LINE+15(4),DWWD+6
         BAL   8,QANAL
         NOP   *
         PUT   SYSPRINT,LINE
NRQE     BCTR  4,0
         BCTR  4,0
         BCT   3,NXRQE
         LTR   5,5
         BZ    PG2
         MVC   LINE(66),LINE-1
NQAN     BAL   8,QANAL
         B     PG2
         PUT   SYSPRINT,LINE
         B     NQAN
QANAL    LTR   5,5
         BCR   8,8
CENQ     LH    15,0(0,6)
         AH    15,2(0,6)
         AH    15,4(0,6)
         LTR   15,15
         BZ    NQ
         STC   7,LINE+73
         S     15,NS
         LCR   15,15
         LA    1,LINE+76
         BAL   9,PERCENTB
         LA    1,LINE+85
         LH    15,0(0,6)
         BAL   9,PERCENTB
         LA    1,LINE+94
         LH    15,2(0,6)
         BAL   9,PERCENTB
         LA    1,LINE+103
         LH    15,4(0,6)
         BAL   9,PERCENTB
         LA    6,6(0,6)
         LA    7,1(0,7)
         BCTR  5,0
         B     4(0,8)
NQ       LA    6,6(0,6)
         LA    7,1(0,7)
         BCT   5,CENQ
         MVC   LINE+70(50),LINE+69
         BR    8
PG2      EQU   *
         MVC   LINE,LINE-1
     TITLE 'REPORT GENERATOR FOR SUPERMON - EDIT RECORD ONE PAGE TWO '
         BAL   9,PAGPRT
         PUT   SYSPRINT,E6
         LA    1,ANCH-1
         LH    15,ANYCH
         BAL   9,PERCENT
         LA    1,ANSCH-1
         LH    15,ANYSCH
         BAL   9,PERCENT
         PUT   SYSPRINT,E7
         LA    1,ANCHN-1
         LH    15,ANYCHW
         BAL   9,PERCENT
         LA    1,ANSCHW-1
         LH    15,ANYSCHW
         BAL   9,PERCENT
         PUT   SYSPRINT,E8
         PUT   SYSPRINT,E9
         MVC   LINE+3(7),=C'CHANNEL'
         LA    4,1
         LA    3,SELCH
         USING SELBR,3
         SR    5,5
         IC    5,NSEL
CHLUP    STC   4,LINE+12
         OI    LINE+12,240
         LA    1,LINE+18
         LH    15,SELB
         BAL   9,PERCENTB
         LA    1,LINE+32
         LH    15,SELBW
         BAL   9,PERCENTB
         LA    1,LINE+46
         L     15,NS
         SH    15,SELB
         SH    15,SELNO
         BAL   9,PERCENTB
         LA    1,LINE+60
         LH   15,SELNO
         BAL   9,PERCENTB
         PUT   SYSPRINT,LINE
         LA    4,1(0,4)
         LA    3,6(0,3)
         BCT   5,CHLUP
         MVC   LINE,LINE-1
         DROP  3
         SPACE 3
         PUT   SYSPRINT,E10
         PUT   SYSPRINT,E11
         PUT   SYSPRINT,E12
         LA    3,DEVTBL
         LH    7,NODEV
         LA    5,DABIZ
         USING DAD,5
DALUP    MVC   LINE+4(3),0(3)
         MVC   LINE+15(4),3(3)
         LH    15,DAUS
         CVD   15,DW
         MVC   LINE+9(4),NPAT
         ED    LINE+9(4),DW+6
         BAL   8,DALIN
         L     15,NS
         SH    15,DAB
         SH    15,DAS
         SH    15,DANR
         SH    15,DAOF
         LA    1,LINE+56
         BAL   9,PERCENTB
         LH    15,DANR
         LA    1,LINE+66
         BAL   9,PERCENTB
         LH    15,DAOF
         LA    1,LINE+75
         BAL   9,PERCENTB
         BAL   8,DARQE
         LA    5,DALN(0,5)
         LA    3,7(0,3)
         BCT   7,DALUP
         MVC   LINE,LINE-1
         PUT   SYSPRINT,E13
         PUT   SYSPRINT,E14
         PUT   SYSPRINT,E15
         MVC   LINE+4(6),=C'SVCLIB'
         MVC   LINE+15(3),SVCAD
         LA    5,SVCB
         BAL   7,SYSDS
         MVC  LINE+4(7),=C'LINKLIB'
         MVC  LINE+15(3),LKAD
         BAL   7,SYSDS
         MVC   LINE+4(8),=C'SYSJOBQE'
         MVC   LINE+15(3),JQAD
         BAL   7,SYSDS
         CLI   SYSTP,C'V'
         BNE   NOTVS2
         MVC   LINE+4(8),=CL8'PAGE'
         LA    3,PAGA
         LH    10,NOPAGZ
NPGAL    MVC   LINE+15(3),0(3)
         BAL   7,SYSDS
         LA    3,4(0,3)
         BCT   10,NPGAL
NOTVS2   MVC   LINE,LINE-1
     TITLE 'REPORT GENERATOR FOR SUPERMON - EDIT RECORD ONE PAGE TREY'
         TM    SVCSWW,SVCM
         BZ    NOSVC
         PUT   SYSPRINT,HEAD
         PUT   SYSPRINT,E16
         PUT   SYSPRINT,E17
         LA    3,43
         L     4,SVCTX
         LA    5,SVCNO
         SR    6,6
         L     15,SVCNO+188
         S     15,NS
         ST    15,SVCNO+188
NSVC     LR    15,6
         BAL   14,CVD
         MVC   LINE+7(3),DW+5
         MVC   LINE+16(10),0(4)
         L     15,0(0,5)
         BAL   14,CVD
         MVC   LINE+27(6),DW+2
         LA    15,43(0,6)
         BAL   14,CVD
         MVC   LINE+47(3),DW+5
         MVC   LINE+56(10),430(4)
         L     15,172(0,5)
         BAL   14,CVD
         MVC   LINE+67(6),DW+2
         CH    3,=H'2'
         BL    BLKR
         BH    NORSVC
         MVI   LINE+88,C'+'
NORSVC   LA    15,86(0,6)
         BAL   14,CVD
         MVC   LINE+87(3),DW+5
         MVC   LINE+96(10),860(4)
         L     15,344(0,5)
         BAL   14,CVD
         MVC   LINE+107(6),DW+2
         B     PTLN
BLKR     MVC   LINE+87(30),LINE+86
PTLN     EQU   *
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         LA    4,10(0,4)
         LA    5,4(0,5)
         LA    6,1(0,6)
         BCT   3,NSVC
         SPACE 3
NOSVC    PUT   SYSPRINT,E18
         B     RNXT
     TITLE 'REPORT GENERATOR FOR SUPERMON - END OF FILE              '
EOFI     CLOSE (INPUT,REREAD)
REST2    NOP   PASS2
         FREEPOOL INPUT
         CLOSE (LOAD,REREAD)
         FREEPOOL LOAD
         L     2,=V(SORTIN)
         OPEN  ((2))
         GETMAIN VU,LA=W1,A=W2
         L     0,W2+4
         S     0,=F'6000'
         ST    0,SORTC+30
         ST    0,SORTD+30
         TM    LDSW,LDM
         BZ    NOLDM
         L     0,W2+4
         L     1,W2
         FREEMAIN R,LV=(0),A=(1)
         LA    1,SORTP
         LINK  EP=SORT
         LTR   15,15
*        BNZ   1                       DUNNO WHAT THIS DID-LEN-SPT
         MVC   TITL(40),=CL40'MODULE LOADING (SUMMARY)'
         L     15,=V(IEBSW)
         TM    0(15),X'FF'
         BNO   NOIEB
         OPEN  (SVCLIB,,LINKLIB,,PUN1,OUTPUT,PUN2,OUTPUT,PUN3,OUTPUT)
NOIEB    MVC   HEAD+21(13),HEAD+20
         L     2,=V(SORTIN)
         OPEN  ((2))
         GETMAIN VU,LA=W1,A=W2
         L     0,W2+4
         S     0,=F'6000'
         ST    0,SORTC+30
         ST    0,SORTD+30
         L     0,W2+4
         L     1,W2
         FREEMAIN R,LV=(0),A=(1)
         LA    1,SORTP2
         LINK  EP=SORT
         LTR   15,15
*        BNZ   1                       DUNNO WHAT THIS DID-LEN-SPT
NOLDM    TM    DASW,DAM
         BZ    NODAM
         L     15,DARTN
         BALR  14,15
NODAM    SVC   3
DARTN    DC    V(DARTX)
PASS2    OC    LDSW(1),RESTS
         NI    SMSW,255-SMM
         MVI   REST2+1,X'F0' ***** X'00' IN ORIGINAL CODE *****
         OPEN  INPUT
         B     RNXT
     TITLE 'REPORT GENERATOR FOR SUPERMON - SUBROUTINES              '
PAGPRT   AP    PAGNO,=PL1'1'
         MVC   DW,PATX
         ED    DW,PAGNO
         MVC   PGNO,DW+4
         PUT   SYSPRINT,HEAD
         BR    9
         SPACE 2
ABEND    ABEND 1001,DUMP
NOTZ     WTO   'FIRST RECORD NOT TYPE ZERO'
         B     ABEND
         SPACE 2
PERCENTB LTR   15,15
         BZ    BLK
PERCENT  MH    15,=H'10000'
         A     15,NS5
         SR    14,14
         D     14,NS
         CVD   15,DWWD
         MVC   0(7,1),PATTP
         ED    0(7,1),DWWD+5
         BR    9
BLK      MVC   0(7,1),=CL7' '
         BR    9
SYSDS    BAL   8,DALIN
         L     15,NS
         SH    15,DAB
         SH    15,DAS
         LA    1,LINE+58
         BAL   9,PERCENTB
         SH    5,=H'6'
         BAL   8,DARQE
         LA    5,DALN(0,5)
         BR    7
         SPACE 2
DALIN    LH    15,DAB
         LA    1,LINE+20
         BAL   9,PERCENTB
         LH    15,DABW
         LA    1,LINE+29
         BAL   9,PERCENTB
         LH    15,DAS
         LA    1,LINE+38
         BAL   9,PERCENTB
         LH   15,DASWX
         LA    1,LINE+47
         BAL   9,PERCENTB
         BR    8
         SPACE 2
DARQE    EQU   *
         LA    1,LINE+85
         LA    4,5
         LA    6,DA0
URQEL    LH    15,0(0,6)
         BAL   9,PERCENTB
         LA    6,2(0,6)
         LA    1,9(0,1)
         BCT   4,URQEL
         PUT   SYSPRINT,LINE
         BR    8
         SPACE 2
CVD      CVD   15,DWWD
         MVC   DW,PATX
         ED    DW,DWWD+4
         BR    14
LFLINT   MVC   D7+2(130),D7+1
         MVI   D7,240
         L     7,=V(HDLN)
         LA    7,47(0,7)
         LA    14,D7+4
         LA    15,D8+13
         LA    1,9
LPPP     MVC   0(7,14),0(15)
         MVC   0(7,7),0(15)
         LA    7,9(0,7)
         LA    14,12(0,14)
         LA    15,10(0,15)
         BCT   1,LPPP
         MVC   D8+127(5),=C'OTHER'
         MVC   D7+112(21),=C'OTHER      ID LIBRARY'
         BR    9
     TITLE 'REPORT GENERATOR FOR SUPERMON - TEXT FOR OPTIONS         '
NSQA     DC    A(0)
HEAD     DC    CL133'1SUPERMON'
         ORG   HEAD+41
TITL     DC    C'MONITORING && REPORTING OPTIONS USED'
         ORG   HEAD+91
DAT      DC    C'DATE XX/YY/ZZ'
         ORG   HEAD+124
         DC    C'PAGE '
PGNO     DC    C'1234'
D1       DC    CL133'0   START TIME'
         ORG   D1+20
TIM      DC    X'4021204B20204B2020'
         ORG   D1+42
         DC    C'REPORT INTERVAL    '
INTVL    DC    X'40202120'
         DC    C'MINS'
         ORG   D1+89
         DC    C'D/A SEEK MONITORING    - '
SKR      DC    C'DATA COLLECTED'
         ORG
D2       DC    CL133'0   SAMPLE INTERVAL '
         ORG   D2+20
SMPL     DC    C'NN.NN',C'SECS'
         ORG   D2+42
         DC    C'NO. OF REPORTS REQUIRED'
RE       DC    X'40202120'
         ORG   D2+89
         DC    C'MODULE LOAD MONITORING - '
LDR      DC    C'DATA COLLECTED'
         ORG
D3       DC    CL133'0'
         ORG   D3+89
         DC    C'SAMPLING               - '
SMPR     DC    C'DATA COLLECTED'
         ORG
D3A      DC    CL133'-'
         ORG   D3A+42
         DC    C'D/A SEEK ANALYSIS - '
SKRR     DC    C'REQUIRED'
         ORG   D3A+89
         DC    C'MODULE LOAD FLOW       - '
LDFR     DC    C'NOT REQUIRED'
         ORG
D3B      DC    CL133'0'
         ORG   D3B+42
         DC    C'SAMPLING REPORTS  - '
SMRR     DC    C'REQUIRED'
         ORG   D3B+89
         DC    C'MODULE LOAD TOTALS     - '
LDTR     DC    C'REQUIRED'
         ORG
IEB      DC    CL133'0'
         ORG   IEB+89
         DC    C'IEBCOPY CARDS ?        - '
IEBP     DC    C'NOT REQUIRED'
         ORG
D4       DC    CL133'-   D/A SEEK MONITORING      - '
         ORG   D4+31
DAOP     DC    C'DEFAULT VALUES TAKEN AS FOLLOWS:-'
         ORG
D5       DC    CL133'0   ALL D/A DEVICES MONITORED, NO CROSS REFERENCESX
                PRODUCED'
D6       DC    CL133'-   MODULE LOADING HEADINGS  - '
         ORG   D6+31
LDOP     DC    C'DEFAULT VALUES TAKEN AS FOLLOWS'
         ORG
D7       DC    CL133'0   TCBID        0         1         2         6  *
                      A         C         D         E         F'
D8       DC    CL133'    HEADING   PGSUP     TALT    SYSERR       COMMS*
                    SMF        P1        P2        P3        P4     OTH*
               ER'
     TITLE 'REPORT GENERATOR FOR SUPERMON - TEXT FOR SYS DESC        '
D9       DC    CL133'0   CPU MODEL NUMBER'
         ORG   D9+29
CPUMDL   DC    C'XXX         SIZE IN BYTES OF NUCLEUS  '
NUCS     DC    C'XXXXXX        UNIT ADDR FOR SVCLIB    '
SVCA     DC    C'XXX'
         ORG
D10      DC    CL133'0   OPERATING SYSTEM TYPE   '
         ORG   D10+28
SYSTP    DC    C'XXXX         SIZE IN BYTES OF SQA      '
SQAS     DC    C'XXXXXX        UNIT ADDR FOR LINKLIB   '
LNKA     DC    C'XXX'
         ORG
D11      DC    CL133'0   RELEASE NUMBER'
         ORG   D11+28
REL      DC    C'XX.X         SIZE IN BYTES OF BLDL     '
BLDS     DC    C'XXXXXX        UNIT ADDR FOR SYSJOBQE  '
JBQA     DC    C'XXX'
         ORG
D12      DC    CL133'0   STORAGE SIZE IN K BYTES'
         ORG   D12+28
COR      DC    C'XXXX         SIZE IN BYTES OF RAM      '
RAMS     DC    C'XXXXXX        UNIT ADDR FOR PAGE      '
PAGA     DC    C'XXX'
         ORG
D13      DC    CL133'0   TOTAL RQE IN SYSTEM'
         ORG   D13+29
RQEN     DC    C'XXX         SIZE IN BYTES OF RSVC     '
SVCS     DC    C'XXXXXX'
         ORG
D13A     DC    CL133'0   TOTAL SELECTOR CHANNELS'
         ORG   D13A+31
NSEL     DC    C'X         TOTAL SYSTEM AREA         '
ARSS     DC    C'XXXXXX'
         ORG
D14      DC    CL133'-   DIRECT ACCESS DEVICES'
D15      DC    CL133'0   DEV ADDR'
DEVAD    EQU   D15+15
D16      DC    CL133'    DEV TYPE'
DEVTYP   EQU   D16+15
D17      DC    CL133'-   CONTENTS OF LINKPACK AREA (RSVC,RAM)'
D18      DC    CL133'0   MODULE NAMES && LENGTH IN BYTES (INCLUDING RB)*
               '
D19      DC    CL133'0   NUMBER OF MODULES IN LINKPACK AREA'
         ORG   D19+39
LKNO     DC    C'XXXX'
         ORG
     TITLE 'REPORT GENERATOR FOR SUPERMON - TEXT FOR SAMPLE P1       '
E1       DC    CL133'0   SAMPLE INTERVAL '
         ORG   E1+20
INTU     DC    C'NN.NNSECS            TOTAL NUMBER OF CYCLES '
CYCL     DC    C'XXXXX                 DURATION '
DUR      DC    C'XXMIN YYSEC'
         ORG
E2       DC    CL133'0   CPU UTILISATION'
         ORG   E2+23
CPU      DC    C'XXX.X%            NO. OF RQE USED SINCE LAST IPL '
RQE      DC    C'XX            MAX SQA USED '
SQAM     DC    C'XXXXX BYTES'
         ORG
E2A      DC    CL133'0   RECORD NUMBER'
         ORG   E2A+23
RECN     DC    C'XXXX              MAX RQE IN SYSTEM              '
MXRQE    DC    C'XXX           SIZE OF SQA  '
SQASS    DC    C'XXXXXX BYTES'
         ORG
E2AB     DC    CL133'0   AVE NO READY TASKS'
         ORG   E2AB+23
ART      DC    C'XXX.XX            MAX NO READY TASKS            '
MXRT     DC    C'XXX'
         ORG
E2B      DC    CL133'0                                      CPU  ACTIVEZ
                                      CPU  WAIT                        *
                     TOTAL'
E2C      DC    CL133' '
         ORG   E2C+24
         DC    3C'   SUPERVISOR STATE   PROBLEM STATE'
         ORG
E3       DC    CL133'0   PARTITION   ALLOCATED  MAX USED(BYTES)  CLASSES
               S   PP RUNNING    READY'
E4       DC    CL133'0   I/O REQUEST QUEUE USAGE'
         ORG   E4+70
         DC    C'RESOURCE SYSIEFSD ENQ USAGE'
         ORG
E5       DC    CL133'          NO OF RQES USED    PERCENTAGE'
         ORG   E5+70
         DC    C'QUEUE     0        1        2        3+'
         ORG
     TITLE 'REPORT GENERATOR FOR SUPERMON - TEXT FOR SAMPLE P2       '
E6       DC    CL133'0   CHANNEL LOAD'
E7       DC    CL133'0   ANY CHANNEL BUSY'
         ORG   E7+32
ANCH     DC    C'XXX.X%'
         ORG   E7+51
         DC    C'ANY SELECTOR CHANNEL BUSY'
         ORG   E7+88
ANSCH    DC    C'XXX.X%'
         ORG
E8       DC    CL133'0   ANY CHANNEL BUSY + CPU WAIT'
         ORG   E8+32
ANCHN    DC    C'XXX.X%'
         ORG   E8+51
         DC    C'ANY SELECTOR CHANNEL BUSY + CPU WAIT'
         ORG   E8+88
ANSCHW   DC    C'XXX.X%'
         ORG
E9       DC    CL133'0                   BUSY     BUSY+CPU WAIT      FRE
               EE     NOT OPERATIONAL'
E10      DC    CL133'0   DIRECT ACCESS I/O DEVICE LOAD'
E11      DC    CL133'    ADDR  USE  TYPE   DATA  BUSY AND    SEEK  SEEKZ
                AND    FREE      NOT      OFF       UNIT REQUEST QUEUE S
               SIZE DISTRIBUTION'
E12      DC    CL133'          CNT      TRANSFER CPU WAIT          CPU W
               WAIT             READY     LINE        0        1       X
                2        3        4+'
E13      DC    CL133'0   SYSTEM DATA SET USAGE'
E14      DC    CL133'              ADDR    BUSY  BUSY AND    SEEK  SEEKX
                AND    FREE                         UNIT REQUEST QUEUE S
               SIZE DISTRIBUTION'
E15      DC    CL133'                            CPU WAIT          CPU W
               WAIT                                   0        1       X
                2        3        4+'
     TITLE 'REPORT GENERATOR FOR SUPERMON - TEXT FOR SAMPLE P3       '
E16      DC    CL133'- NO. OF EXECUTIONS OF SVC''S'
E17      DC    CL133'0   SVC NUMBER  DESCRIPTION  COUNT          SVC NUM
               MBER  DESCRIPTION  COUNT          SVC NUMBER  DESCRIPTION
               N  COUNT'
         TITLE 'REPORT GENERATOR - DCBS CONSTANTS ETC'
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=133,  *
               BLKSIZE=133
         EJECT
INPUT    DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT,EODAD=EOFI
         EJECT
LOAD     DCB   DSORG=PS,MACRF=PM,DDNAME=SORTIN,RECFM=FB,LRECL=40,      *
               EXLST=XL
XL       DC    X'85',AL3(XSIN)
XSIN     LH    2,62(0,1)
         LTR   2,2
         BCR   7,14
         LA    2,3520
         STH   2,62(0,1)
         BR    14
         EJECT
SYSTAB   DC    X'10',C' MVT'
         DC    X'14',C'MVTM'
         DC    X'20',C' MFT'
         DC    X'22',C'VS/1'
ESYSTAB  DC    X'00',C'OTHR'
DWWD     DS    D
MONTHS   DC    PL2'31,28,31,30,31,30,31,31,30,31,30,31'
P4       DC    PL2'4'
DASW     EQU   *
LDSW     EQU   *
SMSW     EQU   *
SPSW     EQU   *
LFSW     EQU   *
SVCSWW   EQU   *
SLSW     EQU   *
         DC    X'EF'
         DC    X'8F'
DAM      EQU   128
LDM      EQU   64
SMM      EQU   32
LFM      EQU   16
SVCM     EQU   8
SLLNKM   EQU   8
SLSVCM   EQU   4
RESTS    DS    C
PAGNO    DC    PL4'0'
DW       DS    D
         LTORG
TIMPAT   DC    X'4021204B20204B2020'
PATTP    DC    X'402021204B206C'
NPAT     DC    X'40202120'
PPAT     DC    X'4020212040404040404040202020212040D24040404040404020202
               2020202120'
PATX     DC    X'4020202020202120'
PTBL     DC    AL1(1,13,25,37,49,61,73,85,97)
PTTCB    DC    AL1(1,2,3,0,0,0,4,0,0,0,5,0,6,7,8,9,0,0,0,0,0,0,0,0,0,0)
LPATOT   DC    F'0'
LCNT     DC    F'1'
SORTP2   DC    0F'0',AL1(128),AL3(SORTD)
         DC    H'0'
SORTD    DC    H'40'
         DC    A(SRS)
         DC    A(ESRS)
         DC    A(RECX,EREX)
         DC    V(E15X)
         DC    V(E35A)
         DC    C'SORT',F'66000',C'BALN'
         DC    X'FF00'
         DC    C'AC'
SRS      DC    C' SORT FIELDS=(17,4,FI,D) '
ESRS     EQU   *-1
SORTP    DC    0F'0',AL1(128),AL3(SORTC)
         DC    H'0'
SORTC    DC    H'40'
         DC    A(SRT)
         DC    A(ESRT)
         DC    A(RECX,EREX)
         DC    V(E15X)
         DC    V(E35)
         DC    C'SORT',F'66000',C'BALN'
         DC    X'FF00'
         DC    C'AC'
SRT      DC    C' SORT FIELDS=(2,9,CH,A) '
ESRT     EQU   *-1
RECX     DC    0F'0',C' RECORD TYPE=F,LENGTH=(40,40,40) '
EREX     EQU   *-1
LKAD     DS    3C
SVCAD    DS    3C
JQAD     DS    3C
         PRINT NODATA
DEVTBL   DC    48CL7' '
NODEV    DC    H'0'
DATAP    DC    X'01',C'2311'
         DC    X'02',C'2301'
         DC    X'03',C'2303'
         DC    X'04',C'2302'
         DC    X'05',C'2321'
         DC    X'07',C'2305'
         DC    X'08',C'2314'
         DC    X'09',C'3330'
EDATP    DC    X'00',C'OTHR'
SW1      EQU   *
SW2      EQU   *
SW3      EQU   *
SW4      EQU   *
SW5      EQU   *
SW6      EQU   *
SW7      EQU   *
SW8      DC    X'00'
         DC    C' '
LINE     DC    CL133' '
SVCTX    DC    V(SVCNM)
TREC     DC    CL12' '
TSW      DC    X'00'
ATBL     DC    V(LDTBL)
W1       DC    A(66000,1000000)
W2       DC    2A(0)
         EJECT
LINEN    DC    A(1)
E18      DC    C'0    END OF REPORT '
     TITLE 'REPORT GENERATOR FOR SUPERMON - DESCRIPTION FOR SVC''S   '
SVCNM    CSECT
SVCTXT   DC    CL10'EXCP     1'
         DC    CL10'WAIT     1'
         DC    CL10'POST     1'
         DC    CL10'EXIT     1'
         DC    CL10'GETMAIN  1'
         DC    CL10'FREEMAIN 1'
         DC    CL10'LINK     2'
         DC    CL10'XCTL     2'
         DC    CL10'LOAD     2'
         DC    CL10'DELETE   2'
         DC    CL10'REGMAIN  1'
         DC    CL10'TIME     1'
         DC    CL10'SYNCH    2'
         DC    CL10'ABEND    4'
         DC    CL10'SPIE     2'
         DC    CL10'ERREXCP  1'
         DC    CL10'PURGE    3'
         DC    CL10'RESTORE  3'
         DC    CL10'BLDL     2'
         DC    CL10'OPEN     4'
         DC    CL10'CLOSE    4'
         DC    CL10'STOW     3'
         DC    CL10'OPEN J   4'
         DC    CL10'CLOSE T  4'
         DC    CL10'DEVTYPE  3'
         DC    CL10'TRKBAL   3'
         DC    CL10'CATALOG  4'
         DC    CL10'OBTAIN   3'
         DC    CL10'CVOL     4'
         DC    CL10'SCRATCH  4'
         DC    CL10'RENAME   4'
         DC    CL10'FEOV     4'
         DC    CL10'ALLOC    4'
         DC    CL10'IOHALT   3'
         DC    CL10'MGCR     4'
         DC    CL10'WTO      4'
         DC    CL10'WTL      4'
         DC    CL10'SEGLD    2'
         DC    CL10'TTROUTER 2'
         DC    CL10'LABEL    3'
         DC    CL10'EXTRACT  2'
         DC    CL10'IDENTIFY 2'
         DC    CL10'ATTACH   2'
         DC    CL10'CIRB     3'
         DC    CL10'CHAP     3'
         DC    CL10'OVLYBRCH 2'
         DC    CL10'TTIMER   1'
         DC    CL10'STIMER   2'
         DC    CL10'DEQ      2'
         DC    CL10'TTOPEN   3'
         DC    CL10'RESERVED 0'
         DC    CL10'SNAP     4'
         DC    CL10'RESTART  4'
         DC    CL10'RELEX    3'
         DC    CL10'DISABLE  2'
         DC    CL10'EOV      4'
         DC    CL10'ENQ      1'
         DC    CL10'FREEDBUF 3'
         DC    CL10'RELBUF   2'
         DC    CL10'OLTEP    4'
         DC    CL10'STAE     3'
         DC    CL10'TTSAV    3'
         DC    CL10'DETACH   3'
         DC    CL10'CHKPT    4'
         DC    CL10'RDJFCB   3'
         DC    CL10'QWAIT    2'
         DC    CL10'BTAMTEST 4'
         DC    CL10'ENDREADY 2'
         DC    CL10'SYNADAF  4'
         DC    CL10'BSP      3'
         DC    CL10'GSERV    2'
         DC    CL10'RLSEBFR  4'
         DC    CL10'CHATR    4'
         DC    CL10'SPAR     4'
         DC    CL10'DAR      4'
         DC    CL10'DQUEUE   3'
         DC    CL10'IFBSTAT  4'
         DC    CL10'QTAMTEST 4'
         DC    CL10'DSCAN    3'
         DC    CL10'STATUS   1'
         DC    CL10'IKASVC   3'
         DC    CL10'SETPRT   4'
         DC    CL10'DASDR    4'
         DC    CL10'SMFWTM   3'
         DC    CL10'GRAPHICS 1'
         DC    CL10'DDRSWAP  4'
         DC    CL10'ATLAS    4'
         DC    CL10'DOM      3'
         DC    CL10'MOD88    3'
         DC    CL10'EMSRV    3'
         DC    CL10'XQMNGR   4'
         DC    CL10'VOLSTAT  4'
         DC    CL10'TCB-EXCP  '
         DC    CL10'TGET/TPUT '
         DC    CL10'Q CONTROL '
         DC    CL10'TSIP      '
         DC    CL10'STAX      '
         DC    CL10'TEST      '
         DC    CL10'PROTECT  4'
         DC    CL10'DYNAM DD  '
         DC    CL10'FIB       '
         DC    CL10'QTIP     1'
         DC    CL10'AQCTL    1'
         DC    CL10'CLATE    3'
         DC    CL10'TOPCTL   4'
         DC    CL10'IMAGLIB  3'
         DC    CL10'RESERVED  '
         DC    CL10'MODESET  1'
         DC    CL10'ISOLATE  4'
         DC    CL10'LINK TA  2'
         DC    CL10'RESERVED  '
         DC    CL10'JECS     2'
         DC    CL10'PGRLSE   1'
         DC    CL10'PGFIX    1'
         DC    CL10'EXCPVR   1'
         DC    CL10'SWAP     1'
         DC    CL10'RESERVED  '
         DC    CL10'DEBCHK   2'
         DC    CL10'RESERVED  '
         DC    CL10'TESTAUTH 1'
         DC    CL10'RESERVED  '
         DC    CL10'RESERVED  '
         DC    CL10'RESERVED  '
         DC    CL10'RESERVED  '
         DC    CL10'RESERVED  '
         DC    CL10'RESERVED  '
         DC    CL10'RESERVED  '
         DC    CL10'USER SVCS '
     TITLE 'REPORT GENERATOR FOR SUPERMON - TABLE FOR LOAD FLOW      '
LDTBL    CSECT
EZ1      DC    XL40'0',A(EZ10,EZ2)
EZ2      DC    XL40'0',A(EZ1,EZ3)
EZ3      DC    XL40'0',A(EZ2,EZ4)
EZ4      DC    XL40'0',A(EZ3,EZ5)
EZ5      DC    XL40'0',A(EZ4,EZ6)
EZ6      DC    XL40'0',A(EZ5,EZ7)
EZ7      DC    XL40'0',A(EZ6,EZ8)
EZ8      DC    XL40'0',A(EZ7,EZ9)
EZ9      DC    XL40'0',A(EZ8,EZ10)
EZ10     DC    XL40'0',A(EZ9,EZ1)
         SPACE 2
         EJECT
         TITLE 'SMON   SYSIN VALIDITY CHECKING ROUTINE'
REPORT   CSECT
REPNO    DC    PL3'0'
STIME    DC    F'0'
NS       DS    F
NS5      DS    F
STREC    DC    H'0'
STPREC   DC    H'32000'
NOPAGZ   DC    H'0'
READ     CSECT
         SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         LA    14,RDSAVE
         ST    13,4(14)
         ST    14,8(13)
         LR    13,14
         ENTRY IEBP
         ENTRY DAPARM
         ENTRY D5
         ENTRY D7
         ENTRY STREC
         ENTRY STPREC
         ENTRY SKRR
         ENTRY LDFR
         ENTRY SMRR
         ENTRY LDTR
         ENTRY DASW
         ENTRY SMSW
         ENTRY LDSW
         ENTRY LFSW
         ENTRY DAOP
         ENTRY LDOP
         ENTRY PTTCB                                              TCB
         OPEN  (SYSIN,INPUT)
         TM    SYSIN+48,16
         BZ    NOS
         L     3,=V(DAPARM)
         ST    3,PARMAD
RD       GET   SYSIN
         LR    2,1
         CLI   0(2),X'F1'
         BNE   TYP2
         CLI   2(2),X'D5'
         BNE   SMPLE
         L     3,=V(SKRR)
         MVC   0(12,3),NOTREQ
         L     3,=V(DASW)
         NI    0(3),255-DAM
SMPLE    CLI   3(2),X'D5'
         BNE   LDFLOW
         L     3,=V(SMRR)
         MVC   0(12,3),NOTREQ
         L     3,=V(SMSW)
         NI    0(3),255-SMM
LDFLOW   CLI   4(2),X'E8'
         BNE   LDTOT
         L     3,=V(LDFR)
         MVC   0(12,3),REQ
         L     3,=V(LFSW)
         OI    0(3),LFM
LDTOT    CLI   5(2),X'D5'
         BNE   PAC
         L     3,=V(LDTR)
         MVC   0(12,3),NOTREQ
         L     3,=V(LDSW)
         NI    0(3),255-LDM
PAC      L     7,=V(STREC)
         L     8,=V(STPREC)
         PACK  RDSAVE+12(8),6(5,2)
         CVB   15,RDSAVE+12
         STH   15,0(0,7)
         PACK  RDSAVE+12(8),11(5,2)
         CVB   15,RDSAVE+12
         STH   15,0(0,8)
         CLI   16(2),X'D5'
         BE    RD
         OI    IEBSW,X'FF'
         L     3,=V(IEBP)
         MVC   0(8,3),REQ
         MVC   8(4,3),=CL4' '
         B     RD
TYP2     CLI   0(2),X'F2'
         BNE   TYP3
         TM    SWIT,X'FF'
         BO    XIT
         L     3,=V(DAOP)
         MVI   SWIT,X'FF'
         MVC   0(33,3),=C'VALUES SPECIFIED AS FOLLOWS:-    '
XIT      L     3,PARMAD
         MVC   0(73,3),2(2)
         LA    3,80(3)
         ST    3,PARMAD
         B     RD
TYP3     CLI   0(2),X'F3'
         BNE   RD
         L     3,=V(LDOP)
         MVC   0(31,3),=C'VALUES TAKEN AS FOLLOWS:-      '
         L     3,=V(D7)
         L     4,=V(D8)
         LA    7,1                                                TCB
         LA    5,8
         MVC   16(2,3),2(2)
         MVC   13(7,4),5(2)
X4       L     6,=V(PTTCB)
         LA    8,26(6)                                            TCB
         PACK  RDSAVE+12(8),2(2,2)                                TCB
         CVB   15,RDSAVE+12                                       TCB
         STC   15,RDSAVE+12                                       TCB
X2       CLC   0(1,6),RDSAVE+12
         BE    X1                                                 TCB
         MVI   0(6),X'00'                                         TCB
         LA    6,1(6)                                             TCB
         CR    6,8                                                TCB
         BNH   X2                                                 TCB
         B     X3                                                 TCB
X1       STC   7,0(6)                                             TCB
         LA    7,1(7)                                             TCB
         NOP   HEAD1
X3       LA    3,18(3)                                            TCB
         LA    4,19(4)
         LA    2,12(2)
HEAD1    MVC   0(8,3),=C'        '
         MVC   0(3,4),=C'   '
         MVC   8(2,3),0(2)
         MVC   3(7,4),2(2)
         MVI   X3+3,X'0A'
         MVI   X3+7,X'0A'
         MVI   X3+10,X'09'
         MVC   X1+8(2),=X'47F0'
         MVI   X4+12,X'00'
         BCT   5,X4
         SR    8,6                                                TCB
         BCTR  8,0                                                TCB
         MVI   0(6),X'00'                                         TCB
         EX    8,MOVEZERO                                         TCB
         B     RD
FIN      L     3,=V(DAPARM)
         L     4,=V(D5)
         SR    5,5
TYPE2    CLI   0(3),C' '
         BE    ENDIT
         LA    5,1(5)
         LA    3,1(3)
         B     TYPE2
ENDIT    LTR   5,5
         BZ    FIN1
         MVI   SWIT,X'00'
         L     3,=V(DAPARM)
         LA    4,2(4)
         EX    5,PRTYPE2
         LA    3,131
         SR    3,5
         AR    4,5
         MVI   1(4),C' '
         BCTR  3,0
         EX    3,PRTBLNK
         B     FIN1
PRTYPE2  MVC   0(1,4),0(3)
PRTBLNK  MVC   2(1,4),1(4)
MOVEZERO MVC   1(1,6),0(6)                                        TCB
FIN1     CLOSE (SYSIN)
         FREEPOOL SYSIN
NOS      EQU   *
         L     13,4(13)
         LM    14,12,12(13)
         BR    14
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=FIN
RDSAVE   DS    18F
NOTREQ   DC    C'NOT REQUIRED'
REQ      DC    C'REQUIRED'
PARMAD   DC    V(DAPARM)
SWIT     DC    X'00'
IEBSW    DC    X'00'
         LTORG
     TITLE 'REPORT GENERATOR FOR SUPERMON - SORT  EXIT ROUTINE E15X  '
E15X     CSECT
         ENTRY E35
         ENTRY SORTIN
         USING *,15
         STM   12,14,SAVE3
         LR    12,15
         DROP  15
         USING E15X,12
ISOPEN   GET   SORTIN
         LM    12,14,SAVE3
         LA    15,12
         BR    14
EOSIN    CLOSE (SORTIN,REREAD)
         FREEPOOL SORTIN
         LM    12,14,SAVE3
         LA    15,8
         BR    14
     TITLE 'REPORT GENERATOR FOR SUPERMON - SORT  EXIT ROUTINE E35   '
E35      EQU   *
         USING *,15
         STM   12,1,SAVE3
         LR    12,15
         DROP  15
         USING E35,12
         L     1,0(0,1)
         DROP  6
         USING TDS,1
         CLC   TNAM(9),SNAM
         BE    UPXC
         LTR   1,1
         BZ    EO35
         TM    SORTIN+48,16
         BO    PUTREC
         OPEN  (SORTIN,OUTPUT)
SVREC    L     1,SAVE3+20
         L     1,0(0,1)
         MVC   SREC(TL),TTYP
         MVI   SCNT+3,1
         LM    12,1,SAVE3
         LA    15,4
         BR    14
PUTREC   PUT   SORTIN,SREC
         B     SVREC
UPXC     LR    15,1
         DROP  1
         USING TDS,15
         LA    1,1
         AL    1,SCNT
         ST    1,SCNT
         L     1,TCNTX
         AL    1,SCNT+4
         ST    1,SCNT+4
         L     1,TCNTX+4
         AL    1,SCNT+8
         ST    1,SCNT+8
         L     1,TCNTX+8
         AL    1,SCNT+12
         ST    1,SCNT+12
         L     1,TCNTX+12
         AL    1,SCNT+16
         ST    1,SCNT+16
         L     1,TCNTX+16
         AL    1,SCNT+20
         ST    1,SCNT+20
         LA    15,4
         LM    12,14,SAVE3
         BR    14
EO35     PUT   SORTIN,SREC
         CLOSE (SORTIN,REREAD)
         FREEPOOL SORTIN
         LM    12,1,SAVE3
         LA    15,8
         BR    14
     TITLE 'REPORT GENERATOR FOR SUPERMON - SORT  EXIT ROUTINE E35A  '
E35A     EQU   *
         USING *,15
         STM   10,1,SAVE3
         LR    12,15
         DROP  15
         USING E35A,12
         L     10,=V(SYSPRINT)
         L     15,LCT
         BCT   15,STLCT
         L     11,=V(PAGNO)
         AP    0(4,11),=PL1'1'
         MVC   DWS,PATS
         ED    DWS,0(11)
         L     11,=V(HEAD)
         MVC   129(4,11),DWS+4
         PUT   (10),(11)
         PUT   (10),HDLN
         LA    15,60
STLCT    ST    15,LCT
         L     1,SAVE3+28
         L     1,0(0,1)
         LTR   1,1
         BZ    EO35A
         ENTRY HDLN
         ENTRY E35A
         USING TDS,1
         L     15,TCNT
         CVD   15,DWS
         MVC   DWZ,PATS
         ED    DWZ,DWS+4
         MVC   LINF+1(5),DWZ+3
         MVC   LINF+17(8),TNAM
         CLI   TLIB,C'R'
         BH    RSVCXN
         BE    RRESID
         CLI   TLIB,C'L'
         BE    RLNKXN
         BL    RJBQXN
         MVC   LINF+27(7),=C'PRIVATE'
         B     RNXLQ
RJBQXN   MVC   LINF+27(7),=C'JOBLIB '
         B     RNXLQ
RLNKXN   MVC   LINF+27(7),=C'LINKLIB'
         B     RNXLQ
RRESID   MVC   LINF+27(7),=C'RESIDNT'
         B     RNXLQ
RSVCXN   MVC   LINF+27(7),=C'SVCLIB'
RNXLQ    MVC   LINF+38(1),TBLD
         LA    14,LINF+46
         LA    15,TCNTX
         LA    0,10
RNPOS    LH    1,0(0,15)
         CVD   1,DWS
         MVC   0(6,14),=X'402020202120'
         ED    0(6,14),DWS+5
         LA    14,9(0,14)
         LA    15,2(0,15)
         BCT   0,RNPOS
         LA    14,TAB1
         LA    15,TABEND
COMP     CLC   LINF+17(3),0(14)
         BE    SAME
         LA    14,6(14)
         CR    14,15
         BL    COMP
         MVC   LINF+10(3),=CL3' '
         B     PUTIT
SAME     CLC   0(3,14),=C'IGC'
         BE    SVCNO1
         CLC   0(3,14),=C'IGG'
         BE    NO
         CLC   0(3,14),=C'GO '
         BE    GO
         MVC   LINF+10(3),3(14)
         B     PUTIT
SVCNO1   MVC   LINF+10(3),LINF+22
         OI    LINF+12,X'F0'
         B     PUTIT
NO       MVC   LINF+10(3),LINF+20
         B     PUTIT
GO       CLI   LINF+27,C'L'
         BE    LINKGO
         MVC   LINF+10(3),=C'EXC'
         B     PUTIT
LINKGO   MVC   LINF+10(3),3(14)
PUTIT    EQU   *
         L     1,=V(IEBSW)
         TM    0(1),X'FF'
         BNO   NOSEL1
         CLI   LINF+27,C'P'
         BE    NOSEL1
BLDL     MVC   BLST+4(8),LINF+17
         CLI   BLST+11,C'*'
         BNE   *+8
         MVI   BLST+11,X'C0'
         MVC   SELCD+15(8),BLST+4
         LA    15,BLST+8
         LA    14,4
BLNX     CLI   0(15),64
         BE    BLOK
         CLI   0(15),X'C0'
         BL    NOSEL1A
         LA    15,1(0,15)
         BCT   14,BLNX
BLOK     EQU   *
OPN      CLI   LINF+27,C'L'
         BE    LNK1
SVC      CLC   =C' 10',LINF+3
         BNL   LOW1
         BLDL  SVCLIB,BLST
TST      LTR   15,15
         BNZ   NOSEL1A
         CLI   LINF+27,C'L'
         BE    LNK
         CLC   TTRS(4),BLST+12
         BNH   LOW1
         PUT   PUN1,CCD
LOW1     PUT   PUN1,SELCD
         MVC   TTRS(4),BLST+12
         B     NOSEL1
LNK1     CLC   =C' 10',LINF+3
         BNL   LOW2
         BLDL  LINKLIB,BLST
         B     TST
LNK      CLC   TTRL(4),BLST+12
         BNH   LOW2
         PUT   PUN2,CCD
LOW2     MVC   EXCD+16(8),LINF+17
         PUT   PUN2,SELCD
         PUT   PUN3,EXCD
         MVC   TTRL(4),BLST+12
NOSEL1   PUT   (10),LINF
         MVI   LINF+9,64
         LM    10,1,SAVE3
         LA    15,4
         BR    14
NOSEL1A  MVI   LINF+9,C'#'
         B     NOSEL1
EO35A    CLOSE (SVCLIB,,LINKLIB,,PUN1,,PUN2,,PUN3,)
         L     1,=V(IEBSW)
         TM    0(1),X'FF'
         BZ    NOCOP
         FREEPOOL PUN1
         FREEPOOL PUN2
         FREEPOOL PUN3
NOCOP    LM    10,1,SAVE3
         LA    15,8
         BR    14
     TITLE 'REPORT GENERATOR FOR SUPERMON - DCB && CONSTANTS FOR SORT '
DWS      DS    D
DWZ      DS    D
         LTORG
SORTIN   DCB   DSORG=PS,MACRF=(GL,PM),DDNAME=SORTIN,EODAD=EOSIN
SAVE3    DC    3F'0'
         DC    3F'0'
         DC    2F'0'
LCT      DC    F'1'
         DS    0F
SREC     DC    X'00'
SNAM     DC    XL8'00'
         DC    XL4'00'
SCNT     DC    6F'0'
LINF     DC    CL133' '
PATS     DC    X'4020202020202120'
HDLN     DC    CL133'0LOADS  SVC NO.    NAME    LIBRARY  BLDL'
TTRL     DS    F
TTRS     DS    F
CCD      DC    CL80' C I=I,O=O'
SELCD    DC    CL80' SELECT MEMBER='
EXCD     DC    CL80' EXCLUDE MEMBER='
PUN1     DCB   DSORG=PS,MACRF=PM,RECFM=FB,BLKSIZE=1280,LRECL=80,       *
               DDNAME=PUN1,EODAD=NOSEL1
PUN2     DCB   DSORG=PS,MACRF=PM,RECFM=FB,BLKSIZE=1280,LRECL=80,       *
               DDNAME=PUN2,EODAD=NOSEL1
PUN3     DCB   DSORG=PS,MACRF=PM,RECFM=FB,BLKSIZE=1280,LRECL=80,       *
               DDNAME=PUN3,EODAD=NOSEL1
SVCLIB   DCB   DSORG=PO,MACRF=R,DDNAME=SVCLIB
LINKLIB  DCB   DSORG=PO,MACRF=R,DDNAME=LINKLIB
BLST     DC    0F'0',H'1',H'14',CL8' ',F'0'
         DS    F
TAB1     DC    CL6'IGCSVC'
         DC    CL6'IGGSVR'
         DC    CL6'IEMPL1'
         DC    CL6'IERSRT'
         DC    CL6'IEFSCH'
         DC    CL6'IEESCH'
         DC    CL6'IGEERR'
         DC    CL6'IEUASF'
         DC    CL6'IEYF4G'
         DC    CL6'IEBUTL'
         DC    CL6'IEHUTL'
         DC    CL6'IEWLNK'
         DC    CL6'IHEPL1'
         DC    CL6'IFCMCH'
         DC    CL6'DEVSCH'
         DC    CL6'IEKF4H'
         DC    CL6'GO SCH'
TABEND   EQU       *
RECSV    CSECT
         DC    336C' '
     TITLE 'REPORT GENERATOR FOR SUPERMON - DSECT TO DEFINE INPUT RECS'
REC      DSECT
RECNO    DS    H
RECTYP   DS    H
TIMZ     DS    F
DESC     EQU   *
SYSTYP   DS    0C
DATE     DS    4C
CYCLE    DS    F
REPTIN   DS    F
NOREPT   DS    F
DAREQ    DS    C
LDREQ    DS    C
SMREQ    DS    C
SPREQ    DS    C
CPUMOD   DS    F
RELNO    DS    F
NUCSZ    DS    F
SQASZ    DS    F
BLDSZ    DS    F
RAMSZ    DS    F
SVCSZ    DS    F
CORSZ    DS    H
VIRTSZ   DS    H
RQENO    DS    H
NOSEL    DS    H
NOPAGS   DS    H
SPH      DS    H
SVCUN    DS    F
LNKUN    DS    F
JBQUN    DS    F
PGUN     DS    4F
DADEV    DS    48F
DADEVT   DS    48C
LPANO    DS    F
LPAMOD   DS    100CL12
         ORG   DESC
SAMPLE   EQU   *
SVCNO    DS    128F
NOSAMP   DS    H
NOSAMPC  DS    H
NDSP     DS    H
MXDSP    DS    H
PKZCS    DS    H
PKZCP    DS    H
PKZWS    DS    H
PKZWP    DS    H
PKNCS    DS    H
PKNCP    DS    H
PKNWS    DS    H
PKNWP    DS    H
NORQE    DS    31H
MAXRQE   DS    H
SELCH    DS    18H
SQA      DS    F
PART     DS    16CL12
ANYCH    DS    H
ANYSCH   DS    H
ANYCHW   DS    H
ANYSCHW  DS    H
SVCB     DS    H
SVCSK    DS    H
SVCBW    DS    H
SVCSW    DS    H
SVCQ0    DS    H
SVCQ1    DS    H
SVCQ2    DS    H
SVCQ3    DS    H
SVCQ4    DS    H
LNKB     DS    H
LNKS     DS    H
LNKBW    DS    H
LNKSW    DS    H
LNKQ0    DS    H
LNKQ1    DS    H
LNKQ2    DS    H
LNKQ3    DS    H
LNKQ4    DS    H
JBQB     DS    H
JBQS     DS    H
JBQBW    DS    H
JBQSW    DS    H
JBQQ0    DS    H
JBQQ1    DS    H
JBQQ2    DS    H
JBQQ3    DS    H
JBQQ4    DS    H
PGBZ     DS    36H
DABIZ    DS    48CL24
         ORG   REC+1994
ENQ      DS    27H
ENREC    EQU   *
LOADR    EQU   DESC
SEEKR    EQU   DESC
     TITLE 'REPORT GENERATOR FOR SUPERMON - OTHER DSECTS              '
PARTD    DSECT
PUSE     DS    F
PDSP     DS    H
PCLS     DS    H
PSIZ     DS    H
PPPR     DS    H
         SPACE 2
LDS      DSECT      I/P
LTYP     DS    C
LNAM     DS    CL8
LLIB     DS    C
LTCB     DS    X
LBLD     DS    C
LLDS     EQU   *-LDS
         SPACE 2
TDS      DSECT      O/P
TTYP     DS    C
TNAM     DS    CL8
TLIB     DS    C
TTCB     DS    X
TBLD     DS    C
TUSE     DS    X
TCNT     DS    F
TCNTX    DS    10H
TL       EQU   *-TDS
TLNK     DS    F
TBAK     DS    F
         SPACE 2
SELBR    DSECT
SELB     DS    H
SELBW    DS    H
SELNO    DS    H
         SPACE 2
DAD      DSECT
DAB      DS    H
DAS      DS    H
DABW     DS    H
DASWX    DS   H
DANR     DS    H
DAOF     DS    H
DAUS     DS    H
DA0      DS    H
DA1      DS    H
DA2      DS    H
DA3      DS    H
DA4P     DS    H
DALN     EQU   *-DAB
  TITLE 'REPORT GENERATOR FOR SUPERMON - DISK ANALYSIS'
DARTX    CSECT
         SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         LA    14,SAVE
         ST    13,4(14)
         ST    14,8(13)
         LR    13,14
*
*        ANALYZE PARMFIELD
*
         LM    14,15,PARX
NPASS    STM   14,15,PARX
         MVC   PARM(80),0(14)
         LA    2,UNTABD      OUTPUT TABEL
         USING UNTAB,2
         LA    3,HEAF+21
         LA    4,PARM
         LA    5,8           LIMIT
B0100    CLI   3(4),C'/'     2 ADDRESSES
         BE    B0140
         MVC   5(3,3),0(4)   MOVE TO HEADING
         MVC   TXT(3),0(4)
         B     B0150
B0140    MVC   3(7,3),0(4)   MOVE TO HEADING
         MVC   TXT(7),0(4)
B0150    TR    0(3,4),TAB-193
         L     15,DEVTBLX
         LA    14,48
NDEV     CLC   0(3,15),0(4)
         BE    DEVFN
         LA    15,7(0,15)
         BCT   14,NDEV
DEVFN    CLC   4(4,15),=C'3330'
         BNE   *+4 ***** *+8 IN ORIGINAL CODE *****
         MVI   DEVTP,1
         PACK  HELP(3),0(4,4) PACK FIRST ADDRESS
         MVC   ADR1(2),HELP
         CLI   3(4),C'/'     2 ADDRESSES
         BE    B0170         YES
         LA    4,3(4)
         B     B0190
B0170    TR    4(3,4),TAB-193
         PACK  HELP(3),4(4,4) PACK SECOND ADDRESS
         MVC   ADR2(2),HELP
         LA    4,7(4)
B0190    CLI   0(4),C'X'     XREF REQUESTED
         BNE   B0195
         LA    4,1(4)        CORRECT PARM POINTER
         MVI   SWX,C'X'      SET SWITCH
         OPEN  MF=(E,OPENO)  OPEN SCRATCH FILE OUTPUT
B0195    CLI   0(4),C'D'     DRUM REQUESTED
         BNE    B0200
         MVI   SWD,C'D'      SET SWITCH
         LA    4,1(4)        CORRECT PARM POINTER
B0200    LA    3,14(3)       HEADING POINTER
         LA    2,UNTABL(2)
         CLI   0(4),C' '     END OF PARM
         BE    B0300
         CLI   0(4),C','     NEXT ADDRESS PRESENT
         BNE   ABENX
         LA    4,1(4)        CORRECT PARM POINTER
         BCT   5,B0100       TEST LIMIT
ABENX    ABEND 4,DUMP
*
*        OPEN FILES, ZERO ARRAY
*
B0300    OPEN  TAPE
         GETMAIN EU,LV=12800,A=ARRAY
         L     14,ARRAY
         LH    15,=H'12800'
         SR    0,0
         XR    1,1
         MVCL  14,0
*
*        READ TAPE, FIND MATCHING ADDRESS
*
B0400    L     14,NBLKQ
         BCT   14,B0410
B0401    GET   TAPE
         CLC   2(2,1),=AL2(3)
         BNE   B0401
         LA    3,8(0,1)
         LA    14,509
         B     B0413
B0410    L     3,SVREX
         LA    3,4(0,3)
B0413    CLC   0(4,3),=XL4'0'
         BE    B0401
         ST    3,SVREX
         ST    14,NBLKQ
         LH    4,0(0,3)
         LTR   4,4
         BZ    B0400
         LA    2,UNTABD
         LA    5,8           SEARCH LIMIT
         L     6,ARRAY
B0420    CH    4,ADR1        ADDRESS EQUAL
         BE    B0500
         CH    4,ADR2
         BE    B0510
         LA    2,UNTABL(2)
         A     6,=F'1600'              NEXT ELT OF ARRAY
         BCT   5,B0420       TEST LIMIT
         B     B0400         ADDRESS NOT FOUND
*
*        PROCESS RECORD
*
B0500    SR    4,4
         B     B0520
B0510    LA    4,4
B0520    CLI   DEVTP,1
         BE    B0525
         CLC   2(2,3),=H'199'
         BNH   B0530
         B     B0400
B0525    CLC   2(2,3),=H'399'
         BH    B0400
B0530    EQU   *
         L     5,CHA(4)      ADD COUNT PER CHANNEL
         A     5,=F'1'
         ST    5,CHA(4)
         L     5,SIO         SIO COUNT
         A     5,=F'1'
         ST    5,SIO
         LH    7,2(0,3)
         SLA   7,2           *4
         L     5,0(7,6)
         A     5,=F'1'
         ST    5,0(7,6)
         LH    7,2(0,3)
         CLI   CYLSAV,X'FF'  FIRST TIME
         BE    B0550
         SH    7,CYLSAV
         BZ    B0540
         L     5,TOTSEEKS
         A     5,=F'1'
         ST    5,TOTSEEKS
         LPR   7,7
B0540    L     5,TOTCYL
         AR    5,7
         ST    5,TOTCYL
B0550    MVC   CYLSAV(2),2(3)
         CLI   SWX,C'X'      XREF
         BNE   B0400
         CLI   DEVTP,1
         BNE   B0555
         LH    15,2(0,3)      THEN HALF CYLNO
         SRA   15,1
         STH   15,2(0,3)
B0555    PUT   DCB,(3)        PUT RECORD TO WORK FILE
         B     B0400
*
*        PRINT RESULTS
*
EOF1     LA    2,UNTABD
         BAL   11,HEADR
         MVC   R+1(11),=C'TOTAL SIO''S'
         LA    3,SIO
         BAL   10,EDITPR
         MVC   R+1(15),=C'SIO''S CHANNEL A'
         LA    3,CHA
         BAL   10,EDITPR
         MVC   R+1(15),=C'SIO''S CHANNEL B'
         LA    3,CHB
         BAL   10,EDITPR
         MVC   R+1(15),=C'CYLINDERS MOVED'
         LA    3,TOTCYL
         BAL   10,EDITPR
         LA    3,TOTSEEKS
         MVC   R+1(15),=C'TOTAL SEEKS    '
         BAL   10,EDITPR
         LA    1,8
         LA    3,TOTCYL
B0600    L     15,4(0,3)
         SRA   15,1
         A     15,0(0,3)
         SR    14,14
         L     10,4(0,3)
         LTR   10,10
         BNZ   DIV
         SR    15,15
         B     STOR
DIV      D     14,4(0,3)
STOR     ST    15,8(0,3)
         LA    3,UNTABL(0,3)
         BCT   1,B0600
         MVC   R+1(17),=C'AVERAGE SEEK CYLS'
         LA    3,AVCYL
         BAL   10,EDITPR
*
*        PRINT SIO PER CYLINDER
B0690    LA    2,8                     NO OF PAGES FOR SIO REPORT
         CLI   SWO,1
         BNE   *+8
         LA    2,4
         SR    3,3           CYLNO
         SR    4,4           INDEX IN ARRAY
B0700    LA    5,50          # LINES
         C    2,=F'4'        FIRST PAGE
         BE    *+8           SKIP HEADING
         BAL   11,HEADR
         MVC   R+1(14),=C'SIO''S PER CYL.'
B0710    CVD   3,DWORD                 PACKED CYLNO
         UNPK  R+17(3),DWORD+6(2)       3 DIGIT CYLNO
         MVZ   R+19(1),=X'F0'
         LA    3,1(3)        CYL+1
         LA    6,R+20        INDEX TO LINE
         L     7,ARRAY
         LA    8,8
B0730    MVC   0(14,6),ED1
         L     9,0(7,4)
         CVD   9,D
         ED    3(11,6),D+3
         A     7,=F'1600'          GET NEXT DISK
         LA    6,14(6)
         BCT   8,B0730
         BAL   11,PUT
         LA    4,4(4)
         BCT   5,B0710
         BCT   2,B0700
*
*        CLOSE FILES
*
         FREEMAIN E,LV=12800,A=ARRAY
         CLOSE (TAPE)
         FREEPOOL TAPE
         LA    2,UNTABD
         LA    3,8
B0800    CLI   SWX,C'X'
         BNE    B0820
         CLOSE MF=(E,CLOSE)
         FREEPOOL DCB
B0820    LA    2,UNTABL(2)
         BCT   3,B0800
*
*        PREPARE XREF PROCESSING
*
         LA    2,UNTABD
         LA    3,8
B1000    ST    3,HELP        SAVE LIMIT
         CLI   SWX,C'X'
         BNE   B1990
         OPEN  MF=(E,OPENI)  OPEN INPUT
         GETMAIN  EU,LV=80000,A=ARRAY
         LA    3,399
         L     4,ARRAY
         MVI   0(4),0
         MVC   1(199,4),0(4)
B1020    MVC   200(200,4),0(4)
         LA    4,200(4)
         BCT   3,B1020
         MVI   CYLSAV,X'FF'
         L     6,ARRAY
*
*        READ AND PROCESS SCRATCH FILE
*
B1030    GET   DCB
         CLI   CYLSAV,X'FF'
         BE    B1050
         LH    3,CYLSAV
         LH    5,2(0,1)
         SLA   3,1                 *2   FOR HOR DISP.
         M     4,=F'400'     SECOND INDEX*400
         AR    5,3
         LH    7,0(5,6)
         A     7,=F'1'
         STH   7,0(5,6)
B1050    MVC   CYLSAV(2),2(1)
         B     B1030
*
*        PRINT XREF LISTING
*
EOF2     SR    4,4           J=0
B1400     SR   3,3           I=0
B1410    LR    5,3           I2=I
         LR    6,4           J
         SR    6,3           J-I
         C     6,=F'50'      (J-I)GT 50
         BNL   B1800         SKIP PAGE
*
*        CONSTRUCT HEADING WITH CYLNO
*
         LA    9,R+10                 ADDR OF START CYL ACROSS PAGE-
         LA    7,25          LIMIT
         LR    8,4           J=START VALUE
         CVD   4,DWORD                 PACKED CYLNO
         AP    DWORD,DWORD       DOUBLE ACROSS PG FROM 0 TO 175
B1450    EQU   *
         UNPK  0(3,9),DWORD+6(2)        3 DIGIT CYLNO
         MVZ   2(1,9),=X'F0'
         LA    9,5(9)
         LA    8,1(8)
         AP    DWORD,=PL1'2'           ADD 2 TO CYL NO IN PAIRS
         BCT   7,B1450
         MVC   R+1(7),TXT
         MVI   R,C'1'
         BAL   11,PUT
         MVI   R,C'0'
*
*        CONSTRUCT LINE
*
B1470    LR    6,4           J2=J
         CVD   5,DWORD                 PACKED CYLNO
         AP    DWORD,DWORD             DOUBLR CYL AS IN PAIRS
         UNPK  R+5(3),DWORD+6(2)        3 DIGIT CYLNO
         MVZ   R+7(1),=X'F0'
         LA    7,R+8         INDEX IN LINE
B1500    CR    6,5
         BH    B1600         J2 GT I2 SKIP REST OF LINE
         LR    11,6          J2
         SLA   11,1          J2*2
         LR    9,5           I2
         M     8,=F'400'     I2*400
         AR    11,9
         A     11,ARRAY
         LH    10,0(11)      ARRAY(J2,I2)
         CR    6,5
         BE    B1520         I2 EQ J2 SKIP REST
         LR    11,5          I2
         SLA   11,1          I2*2
         LR    9,6           J2
         M     8,=F'400'     J2*400
         AR    11,9
         A     11,ARRAY
         AH    10,0(11)      ARRAY(J2,I2)+ARRAY(I2,J2)
B1520    CVD   10,D
         MVC   EDH(6),=X'402020202120'
         ED    EDH(6),D+5
         MVC   0(5,7),EDH+1
         LA    7,5(7)
         LA    6,1(6)        J2=J2+1
         LR    9,6           J2
         SR    9,4           J2-J
         C     9,=F'25'      LINE FULL
         BL    B1500
B1600    BAL   11,PUT
         LA    5,1(5)        I2=I2+1
         LR    9,5
         SR    9,3           I2-I
         C     9,=F'50'      PAGE FULL
         BL    B1470
B1800    LA    3,50(3)       I=I+50
         C     3,=F'200'
         BL    B1410
         LA    4,25(4)       J=J+25
         C     4,=F'200'
         BL    B1400
*
*        TERMINATE XREF
*
         FREEMAIN  E,LV=80000,A=ARRAY
         CLOSE MF=(E,CLOSE)
B1990    L     3,HELP
         LA    2,UNTABL(2)
         BCT   3,B1000
         LM    14,15,PARX
         LA    14,80(0,14)
         CLI   1(14),64
         BE    EOPROG
         BCT   15,NPASS
*
*        TERMINATE PROGRAM
*
EOPROG   L     13,4(0,13)
         RETURN (14,12),RC=0
*
*        SUBROUTINES
*
PUT      L     1,=V(SYSPRINT)
         PUT   (1),R
         MVI   R,C' '
         MVC   R+1(132),R
         BR    11
*
HEADR    L     15,=V(PAGNO)
         AP    0(4,15),=PL1'1'
         MVC   D,PATQ
         ED    D,0(15)
         L     15,=V(HEAD)
         MVC   41(40,15),=CL40'DISK ANALYSIS'
         MVC   129(4,15),D+4
         L     1,=V(SYSPRINT)
         LR    0,15
         PUT   (1),(0)
         L     1,=V(SYSPRINT)
         PUT   (1),HEAF
         MVI   R,C'0'
         BR    11
EDITPR   LA    5,8
         LA    4,R+20
EDB1     MVC   1(13,4),ED1+1
         L     6,0(3)
         CVD   6,D
         ED    3(11,4),D+3
         LA    3,UNTABL(3)
         LA    4,14(4)
         BCT   5,EDB1
         BAL   11,PUT
         MVI   R,C'0'
         BR    10
*
*        CONSTANTS
*
DWORD    DS    D              0
D        DC    D'0'
HELP     DC    F'0'
ARRAY    DC    F'0'
CYLNO    DC    F'0'
NBLKQ    DC    F'1'
SVREX    DC    F'0'
SAVE     DS    18F
ED1      DC    X'404040402020202020206B202120'
HEAF     DC    CL21' DISK ADDRESSES',8C'------------- '
PATQ     DC    X'4020202020202120'
PARM     DC    CL101' '
TAB      DC    X'FAFBFCFDFEFF',CL41' ',C'0123456789ABCDEF'
R        DC    CL133' '
EDH      DC    CL6' '
SWO      DC    X'00'
DAPARM   DC    6CL80' '
         DC    C' '
DEVTBLX  DC    V(DEVTBL)
PARX     DC    A(DAPARM)
         DC    A(6)
TAPE     DCB   DDNAME=INPUT,DSORG=PS,MACRF=GL,RECFM=F,BLKSIZE=2048,    *
               LRECL=2048,EODAD=EOF1
*
*        DSECT WITH INFORMATION PER ADDRESS
*
UNTAB    DSECT
ADR1     DS    H
ADR2     DS    H
TXT      DS    CL8
CYLSAV   DS    H
SWX      DS    C
SWD      DS    C
CHA      DS    F
CHB      DS    F
SIO      DS    F
TOTCYL   DS    F
TOTSEEKS DS    F
AVCYL    DS    F
DEVTP    DS    C
         DS    C
OPENO    DS    F
OPENI    DS    F
CLOSE    DS    F
DCB      DS    24F
UNTABE   EQU   *
UNTABL   EQU   UNTABE-UNTAB
DARTX    CSECT
         PRINT NOGEN
         LTORG
UNTABD   DC    F'0',CL8' ',X'FF000000',4F'0'
         DC    3F'0'
         OPEN  (SYSUT1,OUTPUT),MF=L
         OPEN  (SYSUT1,INPUT),MF=L
         CLOSE (SYSUT1),MF=L
SYSUT1   DCB   DDNAME=SORTWK01,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,      *
               RECFM=FBS,LRECL=4,BLKSIZE=612
         DC    F'0',CL8' ',X'FF000000',4F'0'
         DC    3F'0'
         OPEN  (SYSUT2,OUTPUT),MF=L
         OPEN  (SYSUT2,INPUT),MF=L
         CLOSE (SYSUT2),MF=L
SYSUT2   DCB   DDNAME=SORTWK02,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,      *
               RECFM=FBS,LRECL=4,BLKSIZE=612
         DC    F'0',CL8' ',X'FF000000',4F'0'
         DC    3F'0'
         OPEN  (SYSUT3,OUTPUT),MF=L
         OPEN  (SYSUT3,INPUT),MF=L
         CLOSE (SYSUT3),MF=L
SYSUT3   DCB   DDNAME=SORTWK03,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,      *
               RECFM=FBS,LRECL=4,BLKSIZE=612
         DC    F'0',CL8' ',X'FF000000',4F'0'
         DC    3F'0'
         OPEN  (SYSUT4,OUTPUT),MF=L
         OPEN  (SYSUT4,INPUT),MF=L
         CLOSE (SYSUT4),MF=L
SYSUT4   DCB   DDNAME=SORTWK04,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,      *
               RECFM=FBS,LRECL=4,BLKSIZE=612
         DC    F'0',CL8' ',X'FF000000',4F'0'
         DC    3F'0'
         OPEN  (SYSUT5,OUTPUT),MF=L
         OPEN  (SYSUT5,INPUT),MF=L
         CLOSE (SYSUT5),MF=L
SYSUT5   DCB   DDNAME=SYSUT5,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=4,BLKSIZE=612
         DC    F'0',CL8' ',X'FF000000',4F'0'
         DC    3F'0'
         OPEN  (SYSUT6,OUTPUT),MF=L
         OPEN  (SYSUT6,INPUT),MF=L
         CLOSE (SYSUT6),MF=L
SYSUT6   DCB   DDNAME=SYSUT6,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=4,BLKSIZE=612
         DC    F'0',CL8' ',X'FF000000',4F'0'
         DC    3F'0'
         OPEN  (SYSUT7,OUTPUT),MF=L
         OPEN  (SYSUT7,INPUT),MF=L
         CLOSE (SYSUT7),MF=L
SYSUT7   DCB   DDNAME=SYSUT7,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=4,BLKSIZE=612
         DC    F'0',CL8' ',X'FF000000',4F'0'
         DC    3F'0'
         OPEN  (SYSUT8,OUTPUT),MF=L
         OPEN  (SYSUT8,INPUT),MF=L
         CLOSE (SYSUT8),MF=L
SYSUT8   DCB   DDNAME=SYSUT8,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=4,BLKSIZE=612
         LTORG
         END
         LTORG
         END
