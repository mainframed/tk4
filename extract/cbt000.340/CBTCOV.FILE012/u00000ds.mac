LINKUP   CSECT
         SPACE
*  THIS VERSION OF ENQUPDTE IS SET UP FOR USE WITH IEBUPDTE.
*  YOU SHOULD COMPILE AND LINKEDIT IT INTO LINKLIB UNDER THENAME
*  'ENQUPDTE' AND WHEN SATISFIED THAT IT IS CORRECTLY RECORDED YOU
*  SHOULD RENAME 'IEBUPDTE' TO 'TIEBUPDT', THEN RENAME 'ENQUPDTE' TO
*  'IEBUPDTE'.
         SPACE
*
*
*  THIS PROGRAM WILL.........
*         1  FIND THE DSNAME OF THE DATASET TO BE UPDATED BY THE
*            RELEVANT PROGRAM (IEBCOPY,IEBUPDTE,LINKEDIT)
*         2  ISSUE AN ENQ ON 'RTPUPDTE' (MAJOR NAME) AND THE DSNAME
*            FOUND (MINOR NAME) FOR EXCLUSIVE USE.
*         3  LINK TO THE RELEVANT PROGRAM (IEBCOPY,IEBUPDTE,LINKEDIT)
*            PASSING ON THE ORIGINAL PARAMETER LIST
*         4  ON RETURN FROM LINK THE RETURN CODE WILL BE SAVED AND PASS
*            -ED BACK TO THE USER
*
*   IF THIS PROGRAM IS APPLIED TO EACH OF IEBCOPY, IEBUPDTE, AND ALL
*   VERSIONS OF LINK-EDIT IN YOUR SYSTEM THE CHANCE OF MULTIPLE
*   CONCURRENT UPDATES OF THE SAME LIBRARY ARE GREATLY REDUCED.
*   THIS PROGRAM DOES NOT GIVE PROTECTION AGAINST UPDATING BY IEHMOVE
*   FOR WHICH THE LIBRARIES TO BE UPDATED CANNOT BE FOUND BY
*   INSPECTION OF THE INPUT JCL, NOR AGAINST ANY USER PROGRAMS WHICH
*   MODIFY YOUR LIBRARIES.
*
*   TO INSTAL THIS PROGRAM THE USER MUST, FOR EACH OF THE RELEVANT
*   PROGRAMS SEPARATELY,........
*        1  DECIDE ON A NEW NAME FOR THE PROGRAM TO BE MODIFIED (E.G.
*            IN THIS EXAMPLE TIEBCOPY FOR IEBCOPY) AND PUT THIS NAME IN
*            THE EP PARAMETER OF THE LINK STATEMENT FOLLOWING THE LABEL
*            'LINK'.
*         2  COMPILE AND LINK-EDIT THIS PROGRAM TO THE LIBRARY CONTAIN-
*            ING THE PROGRAM TO BE MODIFIED, USING A UNIQUE MEMBER NAME
*            E.G. 'XIEBCOPY'
*         3  RENAME E.G. IEBCOPY TO TIEBCOPY (THE LINKED TO MODULE),
*            AND XIEBCOPY (THE ENQUING AND LINKING MODULE) TO IEBCOPY
*            (THE PROGRAM NAME WHICH USERS WILL EXECUTE).
*            THIS BRINGS THE NEW VERSION INTO OPERATION IN A WAY WHICH
*            MAKES THE CHANGE TRANSPARENT TO USERS.
*
*   FOR LINK-EDIT THE ABOVE PROCEDURE REQUIRES MODIFICATION IN RESPECT
*   OF.....
*         1  ALIAS NAMES IEWL AND LINKEDIT MUST BE CHANGED TO APPLY TO
*            THE NEW MODULE AS WELL AS THE BASIC NAME (IEWLF128, OR
*            IEWLF880, 440, 180)
*         2  IF YOU HAVE MORE THAN ONE LINK-EDITOR IN YOUR SYSTEM YOU
*            MUST HAVE AN ENQ MODULE (THIS PROGRAM) TO LINK TO EACH
*            APPLICABLE NAME.  NOTE THAT IN RESPOND THIS IS ACHIEVED
*            BY MAKING THE NAME IEWL APPLY TO A DIFFERENT PROGRAM WHICH
*         BOTH ISSUES THE ENQ AND SELECTS THE APPROPRIATE LEVEL OF
*            LINK-EDITOR FOR THE CURRENT PARTITION SIZE.
*         3  IF USING THIS MODULE WITH LINK-EDIT ALTER THE INSTRUCTION
*            LABELLED 'IFLINKED' TO BE AN UNCONDITIONAL BRANCH.
*
*
*   THIS PROGRAM USES AS A MINOR NAME FOR ENQ THE DSNAME OF THE DATASET
*   BEING UPDATED, BUT EXCLUDES TERMINAL BLANKS FROM THE 44 CHAR NAME
*   FOUND IN THE APPROPRIATE JFCB IN ORDER TO SAVE SQA SPACE.
*
*   THE JFCB IS SELECTED BY INSPECTION OF THE INPUT PARAMETER LIST (TO
*   DETERMINE IF ALTERNATIVE DDNAMES APPLY) AND THE TIOT.  THE FIRST
*   DDNAME FOUND BY THE FOLLOWING SEQUENCE OF SEARCH IS USED TO SUPPLY
*   THE DSNAME FOR ENQ......
*         1  ALT NAME FOR SYSLMOD IN THE PARM LIST
*         2  ACTUAL SYSLMOD DDNAME IN THE TIOT
*         3  ALT NAME FOR SYSUT2 IN THE PARM LIST
*         4  ACTUAL SYSUT2 DDNAME IN THE TIOT
*         5  ALT NAME FOR SYSUT1 IN THE PARM LIST
*         6  ACTUAL SYSUT1 DDNAME IN THE TIOT
*   NOTE THAT DD DUMMY STATEMENTS ARE IGNORED, BUT THERE ARE NO
*   CHECKS ON INVALID JCL (SUCH AS A LINK-EDIT WITHOUT A SYSLMOD OR
*   ALT FOR SYSLMOD)
*
*   A USER ABEND 002 SIGNIFIES THAT NO VALID NAMES WERE FOUND IN THE
*   TIOT.
*
*   A USER ABEND 004 SIGNIFIES THAT THE DSNAME FOUND IN THE SELECTED
*   JFCB WAS  ALL BLANK
*
*
*         BEGIN
*  EXPANSION OF THE RESPOND TSG BEGIN MACRO FOLLOWS
          STM  14,12,12(13)
          BALR 12,0
          USING     *,12
          CNOP 0,4
          BAL  15,*+76
          DC   18F'0'
          ST   15,8(13)
          ST   13,4(15)
          LR   13,15
*  END OF EXPANSION OF BEGIN
         LR    10,1
IFLINKED BC    0,LE
         XC    SYSL,SYSL
         B     START
LE       XC    SYS1,SYS1
         XC    SYS2,SYS2
         NI    A+1,15
         OI    B+1,240
START    L     2,16          CVT
         L     2,0(2)        TCB DW
         L     2,4(2)        TCB
         L     3,12(2)       TIOT
         LA    3,24(3)
         L     4,0(1)
         LTR   4,4
         BM    SRCHTIOT      IF VL BIT IS ON IN FIRST PARM THERE IS
*                                           NO ALTERNATIVE DDNAME LIST
          L    4,4(1)         ADD OF ALT DDNAME LIST
          LH   5,0(4)                       LENGTH OF ALT LIST
          LA   9,24
          CR   5,9
          BL   SRCHTIOT       ALT DDNAME LIST TOO SHORT FOR SYSLMOD
A        B     B
          MVC  ALTS,18(4)
B        BC    0,SRCHTIOT
          LA   9,64
          CR   5,9
          BL   SRCHTIOT       ALT DDNAME LIST TOO SHORT FOR SYSUT1
          MVC  ALT1,58(4)
          LA   9,72
          CR   5,9
          BL   SRCHTIOT       ALT DDNAME LIST TOO SHORT FOR SYSUT2
          MVC  ALT2,66(4)
SRCHTIOT SR    6,6
LOOPT     LA   7,ALTS
          LA   8,6
         IC    6,0(3)
         LTR   6,6
         BZ    ENDT
CLCT     CLC   4(8,3),0(7)
         BNE   NEXTT
         LH    9,18(3)       TEST DUMMY
         LTR   9,9
         BZ    ART           IF DUMMY
         ST    3,8(7)        TIOT ADDRESS TO TABLE
         B     ART
NEXTT    LA    7,12(7)
         BCT   8,CLCT
ART      AR    3,6
         B     LOOPT         TO NEXT TIOT ENTRY
ENDT     L     6,8(7)        REGS 7,8 SET AT LOOPT
         LTR   6,6
         BNZ   NAMEFND       FIRST NON-ZERO ENTRY IN OUR TABLE
         LA    7,12(7)
         BCT   8,ENDT
         ABEND 2,DUMP        NO ENTRY IN OUR TABLE
NAMEFND  MVC   DCB+40(8),0(7)
         RDJFCB  (DCB)
         LA    7,44
         LA    8,JFCB+43
*  ESTABLISH LENGTH OF DSNAME EXCLUDING TERMINAL SPACES
CLI      CLI   0(8),64
         BNE   ENQSETUP
         BCTR  8,0
         BCT   7,CLI
         ABEND  4,DUMP       DSNAME IS ALL SPACES
ENQSETUP STC   7,ENQ+5       PUT LENGTH IN MACRO
         STC   7,DEQ+5
ENQ      ENQ   (RTPUPDTE,JFCB,E,44,SYSTEM)
LINK     LR    1,10          RESTORE PARM ADDRESS
**  EP IN FOLLOWING LINK STATEMENT MUST BE AMENDED BY USER - SEE ABOVE
         LINK  EP=TIEBUPDT
**
         ST    15,FIFTEEN
DEQ      DEQ   (RTPUPDTE,JFCB,44,SYSTEM)
         L     15,FIFTEEN
*         GOBACK  RC=(15)
*  EXPANSION OF RESPOND TSG GOBACK MACRO FOLLOWS
GOBACK    L    13,4(13)
          L    14,12(13)
          LM   0,12,20(13)
          MVI  12(13),255
          BR   14
*  END OF EXPANSION OF GOBACK
DCB      DCB   MACRF=(E),DDNAME=SYSUT2,EXLST=XLIST
XLIST    DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
RTPUPDTE DC    C'RTPUPDTE'
SIXTY4   DC    H'64'
SEVEN2   DC    H'72'
FIFTEEN  DC    F'0'
*  IF AN ALTERNATIVE DDNAME PARM LIST IS PROVIDED THE APPROPRIATE
*  NAMES FOR SYSLMOD, SYSUT1, AND SYSUT2 ARE PLACED IN THIS TABLE
*  BEFORE TIOT SEARCH BEGINS
*  DURING THE TIOT SEARCH THE TIOT ADDRESS OF EACH NON-DUMMY ENTRY
*  FOR THE DDNAMES IN THIS TABLE IS STORED AFTER ITS DDNAME ENTRY.
*  FROM THIS THE FIRST VALID ENTRY CAN BE FOUND REGARDLESS OF TIOT SEQ
         DS    0F
ALTS      DC   XL8'00'
          DC   XL4'00'
SYSL      DC   CL8'SYSLMOD'
          DC   XL4'00'
ALT2     DC    XL8'00'
         DC    XL4'00'
SYS2     DC    CL8'SYSUT2'
         DC    XL4'00'
ALT1     DC    XL8'00'
         DC    XL4'00'
SYS1     DC    CL8'SYSUT1'
         DC    XL4'00'
         END
