         MACRO
&N       BEGIN &R
* BEGIN - GENERATE A CSECT STATEMENT, SPECIFY AND LOAD THE FIRST
*         BASE REGISTER, AND GENERATE STANDARD REGISTER SAVE
*         PROCEEDURE IN A MODULE SAVE AREA.
&N       CSECT
         AIF   ('&R' LT '2').E1
         AIF   ('&R' GT '12').E1
         SAVE  (14,12)
         LR    &R,15
         USING &N,&R
         LR    15,13
         LA    13,PRGMSAVE
         ST    13,8(15)
         ST    15,4(13)
         B     PRGMSAVE+72
PRGMSAVE DC    18F'0'
         MEXIT
.E1      MNOTE 9,'IMPROPER REGISTER SPECIFIED,NO STATEMENTS GENERATED'
         MEXIT
         MEND
         MACRO
         REGEQS
*
*        REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FPR0     EQU   0
FPR2     EQU   2
FPR4     EQU   4
FPR6     EQU   6
         MEND
         MACRO
&N       EOJ   &C=0
* EOJ - GENERATE STANDARD RETURN LINKAGE
&N       L     13,PRGMSAVE+4
         RETURN (14,12),,RC=&C
         MEND
ZCHATR   BEGIN 10
         REGEQS
*
*                                                                     *
*    PROGRAM NAME : UTCHATR                                           *
*                                                                     *
*    DESCRIPTION : THIS PROGRAM OUTPUTS MESSAGES TO THE OPERATOR      *
*         & OPTIONALLY WAITS FOR A REPLY. MESSAGE TEXT IS ACCEPTED    *
*         FROM EITHER ''PARM'' OR ''SYSIN'' DATA SET. THE FORMER      *
*         SHOULD BE USED WHEN ONLY A SHORT ONE LINE MESSAGE IS        *
*         REQUIRED; THE LATTER WHEN SEVERAL LINES ARE TO BE OUTPUT.   *
*         EACH LINE IS RESTRICTED TO 79 CHARACTERS.                   *
*              THE FIRST CHARACTER OFTHE MESSAGE SHOULD BE EITHER     *
*         AN ''M'' OR AN ''R'' INDICATING A MESSAGE OR A MESSAGE      *
*         THAT REQUIRES A REPLY OF ''U'' TO CONTINUE.                 *
*              IF INPUT IS FROM SYSIN, ANY NUMBER OF CARDS MAY BE     *
*         SUBMITTED, THE FIRST CHARACTER OF EACH BEING AN ''M'',      *
*         EXCEPT FOR THE LAST CARD WHICH MAY CONTAIN EITHER AN        *
*         ''M'' OR AN ''R''.                                          *
*                                                                     *
*    JCL :                                                            *
*         EITHER -                                                    *
*         // EXEC PGM=UTCHATR,PARM='M MESSAGE TEXT '                  *
*                                   R                                 *
*         OR -                                                        *
*         // EXEC PGM=UTCHATR                                         *
*         //SYSIN DD *                                                *
*         M MESSAGE 1                                                 *
*         M MESSAGE N-1                                               *
*         M MESSAGE N                                                 *
*         R                                                           *
*                                                                     *
         MVC   PARMAD(4),0(R1)
*
         EXTRACT TIOTAD,FIELDS=TIOT
         L     R3,TIOTAD
         MVC   MSSG(8),0(R3)       MOVE JOBNAME & STEPNAME
         MVC   MSSG+9(8),8(R3)     INTO MESSAGE AREA
*
         L     R4,PARMAD
         LH    R5,0(R4)
         LTR   R5,R5               IF NO PARM FIELD -
         BZ    CARDS               LOOK FOR CARD INPUT
         LA    R4,2(R4)            SKIP LENGTH FIELD
         S     R5,=F'2'
         EX    R5,MOVE
         B     OPCHAT
MOVE     MVC   MSSG+39(0),1(R4)
*
CARDS    OPEN  SYSIN
         OI    CFLAG,X'FF'
*
CRDIN    GET   SYSIN
         LR    R4,R1
         MVC   MSSG+39(79),1(R4)
*
OPCHAT   CLI   0(R4),C'R'
         BE    REPLY
         CLI   0(R4),C'M'
         BE    NOREP
*
         WTO   'CHR001I **** CONTROL CHARACTER PRECEDING MESSAGE TEXT N*
               OT M OR R',ROUTCDE=(11)
         ABEND 001,DUMP
*
NOREP    MVC   WTO+8(120),MSSG
WTO      WTO   '                                                       *
                                                                       *
                        '
         MVI   MSSG+39,C' '
         MVC   MSSG+40(78),MSSG+39
         CLI   CFLAG,X'FF'
         BE    CRDIN
         B     FINI
*
REPLY    MVC   MSSG+18(19),RU
         MVC   WTOR+16(120),MSSG
WTOR     WTOR  '                                                       *
                                                                       *
                        ',REPLYFLD,1,ECBFLD
         WAIT  ECB=ECBFLD
         NI    ECBFLD,X'00'
         OI    REPLYFLD,X'40'
         CLI   REPLYFLD,C'U'
         BNE   WTOR
         CLI   CFLAG,X'FF'
         BE    CRDIN
         B     FINI
*
CARDEND  CLOSE SYSIN
*
FINI     EOJ
PARMAD   DS    F
TIOTAD   DS    F
MSSG     DC    CL120' '
CFLAG    DC    X'00'
RU       DC    CL19'REPLY U TO CONTINUE'
REPLYFLD DS    CL1
ECBFLD   DC    F'0'
SYSIN    DCB   DDNAME=SYSIN,MACRF=GL,DSORG=PS,EODAD=CARDEND
         END
