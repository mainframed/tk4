         MACRO
&NAME    PROGIN
         ENTRY SAVEAREA
&NAME    SAVE  (14,12)
         BALR  R5,R0
         USING *,R5
         CNOP  6,8
         ST    R13,*+22
         LR    R6,R13
         LA    R13,*+12
         ST    R13,8(R6)
         B     *+76
SAVEAREA DS    9D
         MEND
         MACRO
&NAME    PROGOUT  &RC
         LCLA  &A
&NAME    L     R13,4(R13)
         LM    R14,R12,12(R13)
         AIF   (K'&RC GT 0).G01
         MNOTE 0,'RC OMITTED RC=0 ASSUMED'
&A       SETA  0
         AGO   .G03
.G01     ANOP
&A       SETA  &RC
         AIF   (&A GT 15).ERROR
.G03     LA    R15,&A.(R0,R0)
         BR    R14
         MEXIT
.ERROR   MNOTE 8,'INVALID RETURN CODE'
         MEND
*
         TITLE 'XM15 - LIST PDS MEMBERS IN ALPHAMERIC ORDER           '
XM150112 CSECT
         ENTRY XM150100
XM150100 EQU   *
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
***********************************************************************
*                                                                     *
*    MODULE XM150112  PROGRAMMER PAUL.A.DANIELSON   ASSIGNED 14/05/73 *
*                                                                     *
*                           MODULE FUNCTIONS                          *
*                           ----------------                          *
*  PROGRAM PRINTS ALL THE MEMBERS OF A PDS IN ALPHAMERIC ORDER.       *
*                                                                     *
*  MODULES USED -   GP020112  SETS UP TABLE OF MEMBER NAMES AND       *
*  ------------               ADDRESSES.                              *
*                                                                     *
*                   GP020212  READS PDS MEMBERS.                      *
*                                                                     *
*                   B3401     PRINTS PDS MEMBERS.                     *
*                                                                     *
*                                                                     *
*  A NEW PAGE IS STARTED AT CHANGE OF MEMBER.                         *
*                                                                     *
*                                                                     *
         SPACE 2
         EJECT
         ISEQ  73,80
         PROGIN
*
** MOVE PARM INFORMATION TO HEADINGS
*  ---------------------------------
         L     R2,0(R1)            POINT TO PARM INFORMATION
         CLC   0(2,R2),=2X'00'     Q, IS PARM INFO PRESENT
         BNE   BA02                A. YES - GO MOVE TO HEADINGS
*
         MVC   CCHXXX+50(23),=C'H.B.S. LIST PDS MEMBERS'  MOVE IN      X
                                   DEFAULT HEADINGS
         B     BA05                GO OBTAIN NAMES AND ADDRESSES.
*
BA02     EQU   *
         LH    R1,0(R2)            GET LENGTH OF PARM INFORMATION
         BCTR  R1,R0               DECREMENT BY 1 FOR EXECUTE
         EX    R1,EX01             MOVE PDS NAME TO HEADINGS
*
**  OBTAIN NAMES AND ADDRESSES OF MEMBERS IN ALPHAMERIC ORDER AND PUT
**  ADDRESSES IN A CONTIGUOUS LIST FOR GP020212.
*   --------------------------------------------
BA05     EQU   *
         CALL  GP020112,(CFTXXX1)  GET NAMES AND ADDRESSES
         LA    R1,CFTXXX1          NAME AND ADDRESS TABLE
         L     R2,=A(CFTXXX2)      ADDRESS TABLE
BA07     EQU   *
         MVC   0(4,R2),8(R1)       MOVE ADDRESS TO SECOND TABLE
         CLI   0(R1),X'FF'         Q. END OF LIST
         BE    BA10                A. YES - GO PROCESS
         LA    R1,12(R1)           POINT TO NEXT INPUT ADDRESS
         LA    R2,4(R2)                  - DITTO OUTPUT -
         B     BA07                LOOP TO END OF TABLE
*
**  PUT MEMBER NAME IN HEADINGS
*   ---------------------------
BA10     EQU   *
         LA    R3,CFTXXX1          POINT TO START OF N/A TABLE
         MVC   CCH133+20(8),0(R3)  MOVE MEMBER NAME TO HEADINGS
*
**  READ PDS MEMBERS AND LIST.
*   --------------------------
BA15     EQU   *
         CALL  GP020212,(CFTXXX2,CXI001,SCO133+25,CXI001A)
         TM    CXI001A,X'FF'       Q. END OF PDS
         BO    BA60                A. YES - GO TERMINATE PROCESSING
         TM    CXI001A,X'F0'       Q. END OF MEMBER
         BO    BA25                A. YES - GO CHANGE HEADINGS ETC
         CALL  B3401,(CCHXXX,SCO133,CCI001,CFD004) PRINT A LINE
         B     BA15                LOOP
*
**  END OF MEMBER,PUT NEW MEMBER NAME IN HEADINGS.
*   ----------------------------------------------
BA25     EQU   *
         MVI   CXI001A,X'00'       SET OFF END OF MEMBER INDICATOR
         LA    R3,12(R3)           POINT TO NEXT MEMBER NAME
         MVC   CCH133+20(8),0(R3)  MOVE MEMBER NAME TO HEADINGS
         MVI   CCI001,C'1'         FORCE NEW HEADINGS
         CALL  B3401,(CCHXXX,SCO133,CCI001,CFD004)
         MVI   CCI001,C'0'         RESET PRINT CONTROL INDICATOR
         B     BA15                CONTINUE READING PDS
*
**  END OF PDS - CLOSE PRINT FILE AND TERMINATE
*   -------------------------------------------
BA60     EQU   *
         MVI   SCO133,X'FF'        INDICATE CLOSE TO B3401
         CALL  B3401,(CCHXXX,SCO133,CCI001,CFD004) CLOSE PRINTER
*
         PROGOUT 0                 END OF JOB
*
         EJECT
*
** CONSTANTS,STORAGE AREAS LITERALS AND HEADINGS.
*  ----------------------------------------------
EX01     MVC   CCHXXX+50(0),2(R2)  MOVE IN HEADINGS
         LTORG
         SPACE 3
CFD004   DC    F'50'               B3401 LINECOUNT
CXI001   DC    X'00'               GP020212 INDICATOR
CXI001A  DC    X'00'               END OF MEMBER/FILE INDICATOR
SCO133   DC    CL133' '            PRINT LINE
CCI001   DC    C'0'                B3401 PRINT INDICATOR
CCHXXX   DS    0CL400
         DC    CL133'1'            FIRST LINE OF HEADINGS
CCH133   DC    CL133'        MEMBER NAME'
         DC    CL8' ',11C'-',114C' ',CL133' ',C'Z' LAST 2 HEADING LINES
*
CFTXXX1  DC    3000F'-1'           TABLE OF NAMES AND ADDRESSES
CFTXXX2  DC    1000F'0'            TABLE OF ADDRESSES
         END
