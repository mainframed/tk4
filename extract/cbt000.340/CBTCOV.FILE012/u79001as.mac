 TITLE 'SCANPDS - SCAN FOR STRING OR PRINT IN ALFA ORDER'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*     THIS PROGRAM WILL READ THE DIRECTORY OF A SOURCE PDS, THEN      *
* ACCORDING TO PARM WILL EITHER SCAN SAID PDS FOR A CHARACTER STRING, *
* REPORTING EACH OCCURENCE, OR WILL PRINT THE PDS IN ALPHABETIC       *
* ORDER.                                                              *
* * * * * * * *     U S A G E   * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  //JOBNAME  JOB ACCT-INFO,PGMR,-----ETC                             *
*  //SNN      EXEC   PGM=SCANPDS,PARM=PRINT (1)--------OR-------      *
*  //SNNN     EXEC   PGM=SCANPDS,PARM=SCAN  (2)--------OR--------     *
*  //STEPNAME EXEC   PGM=SCANPDS            (3)                       *
*  //SYSPRINT DD SYSOUT=A                                             *
*  //PDS      DD   DSN=DATASET.NAME,DISP=SHR                          *
*  //DIRECTRY DD   DSN=DATASET.NAME,DISP=SHR                          *
*  //SYSIN  DD  *                           (4)                       *
*    INPUT CARDS CONTAINING CHARACTER STRING                          *
*  /*                                                                 *
*                                                                     *
* * * * * * * * * COMMENTS  * * * * * * * * * * * * * * * * * * * * * *
*  CARD (1) ABOVE USED FOR PRINT                                      *
*  CARDS (2) AND (3) ABOVE ARE EQUIVALENT                             *
*  CARD (4) ET AL ONLY NEEDED FOR SCAN - IGNORED FOR PRINT            *
*    THIS PROGRAM USES THE DEVON COUNTY COUNCIL MACROS, STARTER AND   *
*  STOPPER.......... THESE ALREADY EXIST ON THE GUIDE GOODIES LIB.    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SCANPDS  STARTER NOCALL,LV=12288
         L     R1,0(R1)
         LH    R3,0(R1)
         LTR   R3,R3
         BZ    NOPARM
         CLC   2(5,R1),=C'PRINT'
         BNE   NOPARM
         OI    SWOTCH,X'80'
NOPARM   EQU   *
         OPEN  (DIRDCB)
         LA    R3,256(R2)
         LR    R10,R3
         L     R9,=A(12288)
          AR   R9,R10
         LR    R9,R2
*
LOOP1    READ  DECB1,SF,DIRDCB,(2),'S'
         CHECK  DECB1
         LH    R8,0(R2)
         LA    R7,0(R2,R8)
         LA    R2,2(R2)
LOOP2    MVC   0(12,R3),0(R2)
         MVC   SWITCH,11(R3)
         NI    11(R3),X'80'
         NI    SWITCH,X'1F'
         SR    R4,R4
         IC    R4,SWITCH
         SLL   R4,1            LENGTH IN BYTES
         LA    R2,12(R4,R2)
         LA    R3,12(R3)
         CLC   0(8,R2),=8X'FF'
         BE    ENDIT
         CR    R2,R7
         BL    LOOP2
         LR    R2,R9
         B     LOOP1
*
ENDIT    MVC   0(8,R3),=8X'FF'
         MVC   8(4,R3),=8X'FF'
         CLOSE  (DIRDCB)
*
         OPEN  (PDS,,PRINT,(OUTPUT))
         LH    R6,PDS+(DCBBLKSI-IHADCB)
         GETMAIN R,LV=(6)
         LR    R2,R1
         LA    R1,0(R6,R2)
         ST    R1,ENDBUFF
         LR    R6,R2
         TM    SWOTCH,X'80'
         BO    NOCARDS
         OPEN  (CARDS)
GETCARDS GET   CARDS,CARDAREA
         NI    SCAN+1,X'0F'
NOCARDS  EQU   *
         LR    R3,R10
LOOP4    MVI   11(R3),0
         LA    R4,8(R3)
         FIND  PDS,(4),C
         TM    SWOTCH,X'80'
         BNO   NOHEADS
         BAL   R8,HEAD1
NOHEADS  EQU   *
LOOP3    LR    R2,R6
         MVI   0(R2),0
         MVC   1(79,R2),0(R2)
         LH    R5,PDS+(DCBBLKSI-IHADCB)
         SR    R4,R4
         D     R4,=F'80'
         STH   R5,SAVE
         BCTR  R5,0
         LTR   R5,R5
         BZ    LR26
XXXX     MVC   80(80,R2),0(R2)
         LA    R2,80(R2)
         BCT   R5,XXXX
LR26     LR    R2,R6
         READ  DECB2,SF,PDS,(2),'S'
         CHECK DECB2
         LH    R5,SAVE
LAR280   EQU   *
         TM    SWOTCH,X'80'
         BO    OUTPUT
         BAL   R8,SCAN
RET1     EQU   *
         BCT   R5,NXT1
         B     LOOP3
NXT1     EQU   *
         LA    R2,80(R2)
         CLI   0(R2),0
         BE    ENDPDS
         B     LAR280
*
ENDPDS   LA    R3,12(R3)
         CLC   0(8,R3),=8X'FF'
         BNE   LOOP4
         TM    SWOTCH,X'80'
         BNO   GETCARDS
         B     CLOSPDS
CARDEND  CLOSE (CARDS)
CLOSPDS  CLOSE (PDS,,PRINT)
         STOPPER
*
SCAN     NOP   END1
         OI    *-3,X'F0'
         PUT   PRINT,CARDAREA-1
         LA    R7,CARDAREA+79
CLIR7    CLI   0(R7),C' '
         BNE   ENSCAN
         BCTR  R7,0
         C    R7,=A(CARDAREA)
         BNL   CLIR7
*
ENSCAN   S     R7,=A(CARDAREA)
         BNZ   *+8
         LA    R7,79
         LA    R9,80
         SR    R9,R7
END1     LR    R11,R9
         LR    R4,R2
EXCLC    EX    R7,XCLC1
         BE    PUTPRT
         LA    R4,1(R4)
         BCT   R11,EXCLC
         BR    R8
*
PUTPRT   MVC   PLINE+1(8),0(R3)
         LA    R8,ENDPDS
PUT1     EQU   *
         PUT   PRINT,PLINE
         MVC   PLINE+1(8),=CL8' '
         BR    R8
XCLC1    CLC   0(0,R4),CARDAREA
OUTPUT   MVC   PLINE+1(80),0(R2)
         BAL   R8,PUT1
         SP    LINECNT,=P'1'
         BNM   RET1
         BAL   R8,HEAD1
         B     RET1
         SPACE 3
HEAD1    MVC   MODULE,0(R3)
         SP    LINECNT,=P'3'
         BP    PUTH
         MVI   HEADING,C'1'
         ZAP   LINECNT,=P'75'
PUTH     PUT   PRINT,HEADING
         MVI   HEADING,C'-'
         BR    R8
         EJECT
CARDS    DCB   DDNAME=SYSIN,DSORG=PS,RECFM=F,BLKSIZE=80,EODAD=CARDEND, C
               MACRF=GM
DIRDCB   DCB   DDNAME=DIRECTRY,DSORG=PS,MACRF=(R),EODAD=ENDIT,RECFM=F, C
               BLKSIZE=256
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FA,BLKSIZE=133,MACRF=PM
PDS      DCB   DDNAME=PDS,DSORG=PO,MACRF=(R),EODAD=ENDPDS,RECFM=FB,    C
               SYNAD=SYNAD
         LTORG
ENDBUFF  DS    F
SAVE     DS    H
         DC    C'1'
CARDAREA DS    CL80
PLINE    DC    CL133' '
SWITCH   DS    C
SWOTCH   DC    X'00'
LINECNT  DC    PL2'0'
HEADING  DS    0CL133
         DC    C'-*** MEMBER NAME - '
MODULE   DS    CL8
         DC    CL114' '
         REGEQU
         DS    0H
SYNAD    EQU   *
         USING *,15
         LR    R4,R1
         LR    R2,R15
         USING SYNAD,2
         DROP  15
         SYNADAF ACSMETH=BPAM
         LR    R3,R1
         ABEND 999,DUMP,STEP
         SPACE 2
         DCBD  DSORG=(PS),DEVD=(DA)
         END
