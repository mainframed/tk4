         MACRO
&NAME    BEGIN &SAVE=,&BR=
         LCLA  &A1,&A2
         LCLC  &C1
&A1      SETA  N'&BR
&A2      SETA  N'&SAVE
&C1      SETC  '15'
&NAME    CSECT
         AIF   (&A1 NE 0).B1
         USING *,12
         AGO   .B5
.B1      AIF   (&A1 NE 1).B2
         USING *,&BR(1)
         AGO   .B5
.B2      AIF   (&A1 NE 2).B3
         USING *,&BR(1),&BR(2)
         AGO   .B5
.B3      AIF   (&A1 NE 3).B4
         USING *,&BR(1),&BR(2),&BR(3)
         AGO   .B5
.B4      ANOP
         USING *,&BR(1),&BR(2),&BR(3),&BR(4)
.B5      ANOP
         STM   14,12,12(13)        SAVE REGISTERS
         AIF   (&A1 NE 0).B6
         LR    12,15               LOAD BASE REGISTER
         AGO   .B8
.B6      AIF   ('&BR(1)' EQ '15').B7
         LR    &BR(1),15           LOAD BASE REGISTER
         AGO   .B8
.B7      ANOP
&C1      SETC  '14'
.B8      ANOP
         B     *+12
         DC    CL8'&NAME'
         AIF   (&A2 EQ 0).B9
         CNOP  0,4
         L     &C1,*+8             LOAD SAVE AREA ADDRESS
         B     *+8
         DC    A(&SAVE)            SAVE AREA ADDRESS
         AGO   .B10
.B9      ANOP
         CNOP  0,4
         LA    &C1,*+16            LOAD SAVE AREA ADDRESS
.B10     ANOP
         ST    13,4(&C1)           CHAIN THE
         ST    &C1,8(13)           SAVE AREAS
         AIF   (&A2 NE 0).B11
         B     *+76
         DS    18F
.B11     ANOP
         LR    13,&C1              LOAD R13 WITH SAVE AREA ADDRESS
.B12     AIF   (&A1 LT 2).B13
         LA    &BR(2),4095(&BR(1)) LOAD SECOND
         LA    &BR(2),1(&BR(2))         BASE REGISTER
         AIF   (&A1 LT 3).B13
         LA    &BR(3),4095(&BR(2)) LOAD THIRD
         LA    &BR(3),1(&BR(3))         BASE REGISTER
         AIF   (&A1 LT 4).B13
         LA    &BR(4),4095(&BR(3)) LOAD FOURTH
         LA    &BR(4),1(&BR(4))         BASE REGISTER
.B13     ANOP
         MEND
