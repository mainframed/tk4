YEBDB001 CSECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* YEBDB001 GIVEN A DATE IN ONE OF THREE FORMS WILL GIVE THE DAY OF THE*
* WEEK AND WILL CONVERT THE DATE TO ANOTHER FORM.                     *
* A DATE IN FORM DDMMMYY OR DDMMYY (DATETYPES 1 & 2) WILL BE CONVERTED*
* TO YYDDD. A DATE IN FORM YYDDD (DATETYPE 3) WILL BE CONVERTED TO    *
* DDMMMYY.                                                            *
*                                                                     *
* THE FOLLOWING PARAMETERS ARE REQUIRED TO BE PASSED FROM THE CALLING *
* PROGRAM:
*        A FULLWORD CONTAINING THE ADDRESS OF AN 8-BYTE FIELD. THE    *
*    FIRST BYTE CONTAINS A ZONED DECIMAL NUMBER INDICATING THE DATE
*    TYPE. THE REMAINING 7 BYTES CONTAIN THE DATE. THE CONVERTED DATE *
*    IS RETURNED IN THIS FIELD.
*        A FULLWORD CONTAINING THE ADDRESS OF A 10-BYTE FIELD. THE DAY*
*    IS RETURNED IN THIS FIELD. THE FIRST BYTE CONTAINS A ZONED       *
*    DECIMAL NUMBER FORM 0 TO 6 CORRESPONDING TO THE DAYS SUNDAY TO   *
*    SATURDAY WHICH IS RETURNED IN THE REMAINDER OF THE FIELD.        *
*        A FULLWORD CONTAINING THE ADDRESS OF A 2-BYTE FIELD WHICH    *
*    WILL CONTAIN A BINARY RETURN CODE.                               *
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         ST    R13,SAVEAREA+4
         LR    R11,R13
         LA    R13,SAVEAREA
         ST    R13,8(R11)
*
         LM    R2,R4,0(R1)
         USING DATE,R2
         USING DAYNAME,R3
         USING RETCODE,R4
         MVC   CODE,=H'0'         SET RETURN CODE OF ZERO
*
         CLI   DATETYPE,C'3'      IS DATE IN JULIAN FORM
         BE    CONVTJ1            OR
         CLI   DATETYPE,C'2'      IS DATE IN DDMMYY FORM
         BE    CONVTN1            OR
         CLI   DATETYPE,C'1'      IS DATE IN DDMMMYY FORM
         BE    CONVTA1            OR
*                                 IS IT NONE OF THESE
ERROR16  MVC   CODE,=H'16'
         B     RETURN
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* MONTH IN ALPHA FORM HAS TO BE CONVERTED TO NUMERIC MONTH
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
CONVTA1  LA    R6,12              SET UP COUNTER
         LA    R5,MONTHS-9
COMPMTH  LA    R5,9(R5)           ADVANCE ADDRESS OF 'MONTHS'
         CLC   MONTHA,0(R5)       IS MONTH THE SAME?
         BE    CONVTA2
         BCT   R6,COMPMTH         IF NOT EQUAL TO ANY
         B     ERROR16            GO TO ERROR16
*
CONVTA2  PACK  WORK1,DAYA
         PACK  WORK2,YEARA
         MVC   YEARN,YEARA
         B     LEAPYEAR
*
CONVTN1  PACK  WORK1,DAYN
         PACK  WORK2,MONTHN
         CP    WORK2,=P'1'         IS THE MONTH NUMBER ...
         BL    ERROR16             ... LESS THAN 1
         CP    WORK2,=P'12'        OR
         BH    ERROR16             GREATER THAN 12
         SP    WORK2,=P'1'         FIND '''
         MP    WORK2,=P'9'         '''DISPLACEMENT
         CVB   R6,WORK2
         LA    R5,MONTHS
         AR    R5,R6
         PACK  WORK2,YEARN
LEAPYEAR MVC  WORK3,WORK2
         DP    WORK3,=P'4'
         UNPK  DATETYPE,WORK3+7(1) MOVE REMAINDER TO DATETYPE
         OI    DATETYPE,X'F0'      CLEAR SIGN
         CP    WORK3+7(1),=P'0'    IF THIS IS A LEAP YEAR
         BNE   NOTLEAP
         AP    WORK1,7(2,R5)       ADD LEAP YEAR ADJUSTMENT
         B     SETJ
NOTLEAP  AP    WORK1,5(2,R5)       ADD ORD YEAR ADJUSTMENT
SETJ     MVC   YEARJ,YEARN         MOVE JULIAN DATE''''
         UNPK  DAYJ,WORK1          ''' INTO DATE
         OI    DAYJ+2,X'F0'        CLEAR SIGN
         MVC   YEARA,=X'4040'      FILL OUT WITH SPACES
         B     CONVTJ2
CONVTJ1  PACK  WORK1,DAYJ
CONVTJ2  PACK  WORK2,YEARJ
         MVC   WORK3,WORK2
         MP    WORK2,=P'365'       NO OF DAYS TO END OF LAST YEAR
         AP    WORK1,WORK2
         MVC   WORK2,WORK3         MOVE YEAR BACK TO WORK2
         DP    WORK2,=P'4'
         UNPK  DATETYPE,WORK2+7(1) MOVE REMAINDER TO DATETYPE
         OI    DATETYPE,X'F0'      CLEAR SIGN
         SP    WORK3,=P'1'
         DP    WORK3,=P'4'         AND NO OF LEAP DAYS'''
         AP    WORK1,WORK3(7)      ''' TO END OF LAST YEAR
         DP    WORK1,=P'7'
         ZAP   WORK2,WORK1+7(1)
         MP    WORK2,=P'10'
         CVB   R8,WORK2
         LA    R7,NAMES
         AR    R7,R8               ADD DISPLACEMENT
         MVC   NAMEDAY,0(R7)
*
         CLI   DATETYPE,C'3'
         BNE   RETURN
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* IF DATE WAS IN JULIAN FORM IT IS NOW CONVERTED TO DDMMMYY
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         MVC   YEARA,YEARJ
         PACK  WORK1,YEARJ
         PACK  WORK2,DAYJ
         LA    R5,MONTHS+99        SET ADDRESS FOR COMPARE
         DP    WORK1,=P'4'
         UNPK  DATETYPE,WORK1+7(1) MOVE REMAINDER TO DATETYPE
         OI    DATETYPE,X'F0'      CLEAR SIGN
         CP    WORK1+7(1),=P'0'    IS THIS A LEAP YEAR?
         BNE   NOTLEAP2
         LA    R6,7
         B     LEAP
NOTLEAP2 LA    R6,5
LEAP     AR    R5,R6               R5 NOW CONTAINS THE ADDRESS OF THE '
*                                  RELEVANT JULIAN DAY NO
COMPDAY  CP    WORK2,0(2,R5)
         BH    THISMTH
         S     R5,=F'9'            REDUCE ADDRESS BY 9
         B     COMPDAY
*
THISMTH  SP    WORK2,0(2,R5)       FIND THE DAY OF THE MONTH
         UNPK  DAYA,WORK2          THEN UNPACK
         OI    DAYA+1,X'F0'        CLEAR SIGN
         SR    R5,R6
         MVC   MONTHA,0(R5)        MOVE MONTH NAME TO MONTH
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*DATE SHOULD NOW CONTAIN DDMMMYY
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
RETURN   L     R13,SAVEAREA+4
         RETURN (14,12)
WORK1    DS    D
WORK2    DS    D
WORK3    DS    D
*
SAVEAREA DC    18F'0'
         LTORG
MONTHS   DC    C'JAN01',X'000C',X'000C'
         DC    C'FEB02',X'031C',X'031C'
         DC    C'MAR03',X'059C',X'060C'
         DC    C'APR04',X'090C',X'091C'
         DC    C'MAY05',X'120C',X'121C'
         DC    C'JUN06',X'151C',X'152C'
         DC    C'JUL07',X'181C',X'182C'
         DC    C'AUG08',X'212C',X'213C'
         DC    C'SEP09',X'243C',X'244C'
         DC    C'OCT10',X'273C',X'274C'
         DC    C'NOV11',X'304C',X'305C'
         DC    C'DEC12',X'334C',X'335C'
*
NAMES    DC    CL10'0SUNDAY'
         DC    CL10'1MONDAY'
         DC    CL10'2TUESDAY'
         DC    CL10'3WEDNESDAY'
         DC    CL10'4THURSDAY'
         DC    CL10'5FRIDAY'
         DC    CL10'6SATURDAY'
*
DATE     DSECT
DATETYPE DS    CL1
DATEFLD  DS   0CL7
DAYA     DS    CL2
MONTHA   DS    CL3
YEARA    DS    CL2
*
         ORG   DAYA
DAYN     DS    CL2
MONTHN   DS    CL2
YEARN    DS    CL2
*        DS    CL1
         ORG   DAYN
YEARJ    DS    CL2
DAYJ     DS    CL3
         DS    CL2
*
DAYNAME  DSECT
DAYNUM   DS    CL1
NAMEDAY  DS    CL9
*
RETCODE  DSECT
CODE     DS    H
         END
