VTOC TITLE 'VTOCLEAN: ROUTINE TO RESET FMT 1 POINTER IN FMT 4 DSCB '
VTOCLEAN CSECT
         COMEIN SAVE,12
         L     1,0(0,1)
         LH    3,0(0,1)
         LA    3,1(0,3)
         SR    2,2
         D     2,=F'7'
         LA    4,2(0,1)
*        GET AT FIRST VTOC DATA SET
DDNM     MVC   DCBVTOC+41(6),0(4)
         RDJFCB (DCBVTOC)
         MVI   JFCB,4
         MVC   JFCB+1(43),JFCB
         OPEN  (DCBVTOC,UPDAT),TYPE=J
         MVC   RNAME,JFCB+118
         ENQ   MF=(E,QLST)
*        SEARCH VTOC FOR FIRST FORMAT 0 OR LAST FORMAT 1
READ     READ  DECB1,SF,MF=E
         CHECK DECB1
         CLI   DSCBAREA+44,241
         BE    FMT1
         CLI   DSCBAREA+44,0
         BNE   READ
         CLI   FMT0IND,1
         BE    READ
FMT1     MVC   FILEADDR,DCBVTOC+8
         MVI   FMT0IND,1
         B     READ
ENDVTOC  CLOSE (DCBVTOC,REREAD),TYPE=T
         READ  DECB1,SF,MF=E
         CHECK DECB1
         MVC   DSCBAREA+45(5),FILEADDR
         WRITE DECB1,SF,MF=E
         CHECK DECB1
         DEQ   MF=(E,QLST)
         CLOSE DCBVTOC
         LA    4,7(0,4)
         BCT   3,DDNM
         L     13,4(0,13)
         RETURN (14,12),,RC=0
*
         READ  DECB1,SF,DCBVTOC,DSCBAREA,MF=L
DSCBAREA DC    35F'-1'
JFCB     DC    44F'0'
JFCBL    DC    0F'0',X'87',AL3(JFCB)
DCBVTOC  DCB   DSORG=PS,MACRF=(R,W),DDNAME=A,BLKSIZE=96,KEYLEN=44,     X
               RECFM=FS,EXLST=JFCBL,EODAD=ENDVTOC
QLST     ENQ   (QNAME,RNAME,E,6,SYSTEM),MF=L
QNAME    DC    CL8'SYSVTOC'
RNAME    DC    CL6'Z'
FMT0IND  DC    X'00'
FILEADDR DC    CL5' '
         END
