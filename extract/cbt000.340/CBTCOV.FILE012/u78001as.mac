         TITLE 'DIRECT ACCESS STORAGE DEVICES SEEK ANALYSIS'
         EJECT
***********************************************************************
*  INPUT TO PROGRAM:- GTFIN=SIO TRACE RECORDS FROM GTF.SYSIN=CONTROL  *
*  CARDS,FREE FORM,2 PARAMETERS ONLY:-                                *
*     DEVICE(XXX)       RANGE(HHMM,HHMM) HHMM=START AND STOP TIMES    *
***********************************************************************
SEEKANAL CSECT
**************************************************************
*        MACRO DEFINITIONS                                   *
**************************************************************
         MACRO
&NAME    PTIME &F,&P
&NAME    SR    R2,R2
         LH    R2,&F
         CVD   R2,PL8
         UNPK  &P.(2),PL8
         OI    &P+1,X'F0'
         MEND
**************************************************************
*        END OF MACRO DEFINITIONS                            *
**************************************************************
         EJECT
**************************************************************
*       PROLOGUE ...ADDRESSABILITY AND OPEN DATA SETS        *
**************************************************************
         SAVE  (14,12),,*
         BALR  R12,0
         USING *,12,9
START    EQU   *
         L     R9,R9BASE
         CNOP  0,4
         BAL   15,*+76
         DC    18F'0'    SAVE
         ST    15,8(13,0)
         ST    13,4(15,0)
         LR    13,15
         BC    15,BEGIN
R9BASE   DC    A(START+4096)
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BEGIN    OPEN   (SYSIN)
         TM     SYSIN+48,X'10'      DID IT OPEN?
         BO     SYSOP               YES
EODSYS   WTO   'SEEK ANALYSIS-SYSIN DD CARD MISSING OR NO INPUT'
         ABEND  4,DUMP
SYSOP    DS    0H
         OPEN  (GTFIN)
         TM    GTFIN+48,X'10'
         BO    GTFOP
         WTO   'SEEK ANALYSIS-GTFIN DD CARD MISSING'
         ABEND 8,DUMP
GTFOP    DS    0H
         OPEN  (SYSPRINT,(OUTPUT))
         TM    SYSPRINT+48,X'10'
         BO    PRTOP
         WTO   'SEEK ANAYSIS-SYSPRINT DD CARD MISSING'
         ABEND 12,DUMP
PRTOP    DS    0H
         GET   SYSIN,REQUEST
         LA    R2,REQUEST           START OF CARD FOR SYNTAX ANAYSIS
         LA    R3,80(R2)            END OF CARD FOR SYNTAX ANALYSIS
LOOKSYS  DS    0H
         CLI   0(R2),C' '           IS IT BLANK
         BE    LOOKLOOP             YES
         CLC   0(7,R2),=CL7'DEVICE(' HAVE WE FOUND DEVICE PARAMETER?
         BE    DEVGOT               YES
         CLC   0(6,R2),=CL6'RANGE('  WELL HAVE WE FOUND THE RANGE PARM
         BE    RANGEGOT              YES
LOOKLOOP DS    0H
         LA    R2,1(R2)              BUMP ALONG CARD
         CR    R2,R3                 END OF CARD?
         BL    LOOKSYS               NO-GO ROUND AGAIN
PUTCARD  DS    0H
         MVI   PRINT,C'1'            PRINT
         MVC   PRINT+1(80),REQUEST       CONTROL
         PUT   SYSPRINT,PRINT                  CARD
         B     CONTINUE              AND GO TO MAINLINE CODE
DEVGOT   DS    0H                    ANALYZE DEVICE PARAMETER
         MVC   SPARE4(3),7(R2)       GET ADDRESS INTO SPARE FIELD
         MVC   PDEV(3),7(R2)         SAVE FOR HEADING PRINTS
         MVI   SPARE4+3,X'F0'
         TR    SPARE4(3),TRTAB       CONVERT TO
         PACK  PL8,SPARE4                     HEXADECIMAL
         MVC   ADDR(2),PL8+5          AND SAVE
         B     LOOKLOOP              GO CONTINUE TO LOOK AT CARD
RANGEGOT DS    0H                    ANALYZE RANGE PARAMETER
         PACK  PL8,6(2,R2)           GET START HOUR
         CVB   R4,PL8                CONVERT TO HEX
         STH   R4,SHR                AND SAVE
         PACK  PL8,8(2,R2)           DO THE SAME
         CVB   R4,PL8                     WITH THE OTHER
         STH   R4,SMIN                                FIELDS
         PACK  PL8,11(2,R2)
         CVB   R4,PL8
         STH   R4,EHR
         PACK  PL8,13(2,R2)
         CVB   R4,PL8
         STH   R4,EMIN
         B     LOOKLOOP              CONTINUE SYSIN ANALYSIS
         EJECT
***************************************************************
*        SYSIN ANALYZED AND SAVED-CONTINUE PROCESSING         *
***************************************************************
CONTINUE DS    0H
         GETMAIN R,LV=8000          GET STORAGE FOR JOBNAMES TABLE
         LR    R10,R1               ADDRESS IT VIA REGISTER 10
         LR    R2,R10
         A     R2,=F'8000'          AND INITIALISE
PLINIT   DS    0H                                IT TO
         MVC   0(8,R1),=PL8'0'                       DECIMAL
         LA    R1,8(R1)                                    PACKED
         CR    R1,R2                                          ZEROES
         BL    PLINIT
         MVC   0(8,R10),=CL8'N/A     '   THIS USED IF TABLE FILLS UP
         GETMAIN R,LV=20000         GET AREA FOR INPUT RECORD
         LR    R11,R1               ADDRESS IT VIA REGISTER R11
         USING  GTFDSECT,R11        ENABLE DSECT ADDRESSING
         EJECT
***************************************************************
*        READ AND ANALYZE INPUT RECORDS                       *
***************************************************************
GTFREAD  DS    0H
         GET   GTFIN,(R11)          READ A RECORD
         CLI   GAID,X'FF'           IS IT A GTF TRACE RECORD?
         BNE   GTFREAD              NO
         CLC   GEID(2),=X'5100'     IS IT A SIO RECORD
         BNE   GTFREAD              NO NOT INTERESTED IN IT
         CLC   GADDR(2),ADDR        IS IT THE DEVICE WE WANT?
         BNE   GTFREAD              NO
         BAL   R14,TODCALC          GO CALCULATE TIME OF DAY
         CLI   STARTED,C'S'         HAS CALCULATION BEGUN?
         BE    STARTCAL             YES SKIP FIRST TIME LOGIC
         CLC   HH,SHR               ARE HOURS LOWER THAN START HOURS
         BL    GTFREAD              YES
         BH    SKIPMIN              IF HOURS HIGHER DON'T CHECK MINUTES
         CLC   MM,SMIN              HOURS ARE EQUAL-CHECK MINUTES
         BL    GTFREAD              MINUTES LOWER-NOT REACHED START POS
SKIPMIN  DS    0H
         MVC   LASTSEEK,GCYL        ASSUME NO DISTANCE ON FIRST SEEK
         MVI   STARTED,C'S'         SAY STARTED
         MVC   BHR,HH               SAVE ACTUAL START TIME HOURS
         MVC   BMIN,MM              MINUTES,
         MVC   BSEC,SS              AND SECONDS
         SPACE 5
STARTCAL DS    0H                   FIRST CHECK FOR END OF RANGE
         CLC   HH,EHR               IS HOUR > END RANGE
         BL    NOTEND               NO-GO ADD VALUES IN
         BH    ENDRANGE             IF HIGHER WE HAVE FINISHED
         CLC   MM,EMIN              HOURS EQUAL CHECK MINUTES
         BH    ENDRANGE             FINISHED IF HIGHER
NOTEND   DS    0H
         AP    TOTCOUNT,=PL1'1'     INCREMENT TOTAL RECORD COUNT
         CLC   GCYL(4),=F'0'        IS THIS A SEEK TO CYLINDER 0?
         BNE   NOTZERO              NO
         AP    SEEKZERO,=PL1'1'     YES INCREMENT TO SEEKZERO COUNTER
NOTZERO  DS    0H
         SR    R2,R2
         SR    R3,R3
         LH    R3,GCYL               GET SEEK CYLINDER ADDRESS
         D     R2,=F'10'             DIVIDE TO GIVE ADDRESS RANGE
         SLL   R3,3                  TIME 8 TO INDEX CORRECT COUNTER
         LA    R2,COUNTERS           ADDRESS THE PACKED COUNTER FIELDS
         AR    R2,R3                 ADD INDEX VALUE
         AP    0(8,R2),=PL1'1'       AND INCREMENT COUNTER
         LA    R4,DISTANCE           ADDRESS CUMULATIVE DISTANCE CNTRS
         SLL   R3,1                  DIST. TABLE NOW 16BYTES PER ENTRY
         AR    R4,R3                 ADD INDEX VALUE
         SR    R2,R2
         SR    R3,R3
         LH    R2,GCYL               GET SEEK CYLINDER ADDRESS
         LH    R3,LASTSEEK           GET LAST SEEK ADDRESS
         SR    R3,R2                 SUBTRACT ONE FROM T'OTHER
         LPR   R3,R3                 IGNORE SIGN
         CVD   R3,PL8                PACK RESULT
         AP    0(8,R4),PL8           AND ADD TO CUMUL. DISTANCE COUNTER
         CP    8(4,R4),PL8           IS CURRENT MINIMUM < THIS DIST?
         BL    MIMSKIP               YES
         MVC   8(4,R4),PL8+4         NO-SAVE NEW MINIMUM VALUE
MIMSKIP  DS    0H
         CP    12(4,R4),PL8          IS CURRENT MAX > THIS DIST
         BH    MAXSKIP               YES
         MVC   12(4,R4),PL8+4        NO-SAVE NEW MAXIMUM
MAXSKIP  DS    0H
         SR    R2,R2                 CLEAR R2 AGAIN
         D     R2,=F'10'             GET DISTANCE RANGE
         SLL   R3,3                  TIMES 8 FOR INDEX VALUE
         LA    R2,RNGCNT             ADDRESS THE RANGE COUNTERS
         AR    R2,R3                 ADD INDEX OFFSET
         AP    0(8,R2),=PL1'1'       INCREMENT RANGE COUNTER
         LR    R2,R10                ADDRESS JOBNAME TABLE
         LR    R3,R2
         A     R3,=F'8000'           END OF TABLE
JOBLOOK  DS    0H
         CLC   0(8,R2),GJOB          HAVE WE SEEN THIS JOB BEFORE?
         BE    JOBGOT                YES
         LA    R2,16(R2)             BUMP TO NEXT ENTRY
         CR    R2,R3                 END OF TABLE REACHED?
         BL    JOBLOOK2              NO
         LR    R2,R10                YES-ADD THIS ONE TO UNKNOWN SLOT
         B     JOBGOT
JOBLOOK2 DS    0H
         CLC   0(8,R2),=PL8'0'       IS THIS AN EMPTY ENTRY?
         BNE   JOBLOOK               NO-GO ROUND AGAIN
         MVC   0(8,R2),GJOB          ENTER JOB IN TABLE
JOBGOT   AP    8(8,R2),=PL1'1'       ADD 1 TO JOB COUNTER
         MVC   LASTSEEK,GCYL         SAVE LAST SEEK ADDRESS
         B     GTFREAD               AND GO GET ANOTHER RECORD
         EJECT
**************************************************************
*       END OF RANGE OR END OF FILE...REPORTING PHASE ENTERED*
**************************************************************
ENDRANGE DS    0H
         CLOSE (GTFIN)               FINISHED WITH INPUT FILE
         MVC   FHR,HH                SAVE ACTUAL FINISH HOUR
         MVC   FMIN,MM               DITTO MINUTE
         MVC   FSEC,SS               DITTO SECOND
         SR    R2,R2
         SR    R3,R3
         SR    R4,R4
         LH    R2,FHR               DURATION CALCULATION BEGINS
         LH    R3,FMIN
         LH    R4,FSEC
DURSEC   DS    0H
         SH    R4,BSEC              SUBTRACT END SECONDS FROM START SEC
         BNM   DURMIN               OK IF NOT GONE NEGATIVE
         AH    R4,=H'60'            ADD A MINUTE IF NEGATIVE
         SH    R3,=H'1'             AND TAKE IT FROM MINUTES
DURMIN   DS    0H
         SH    R3,BMIN              SUBTRACT END MIN FROM START MIN
         BNM   DURHR                OK IF NOT GONE NEGATIVE
         AH    R3,=H'60'            ADD AN HOUR
         SH    R2,=H'1'             AND TAKE IT FROM END HOUR FIELD
DURHR    DS    0H
         SH    R2,BHR               TAKE START HOUR FROM END HOUR
         STH   R2,DHR                SAVE DURATION HOURS,
         STH   R3,DMIN               MINUTES,
         STH   R4,DSEC               AND SECONDS.
         LR    R7,R2                CALCULATE DURATION IN
         SR    R6,R6                    SECONDS
         M     R6,=F'3600'
         LR    R2,R7
         LR    R7,R3
         SR    R6,R6
         M     R6,=F'60'
         AR    R2,R7
         AR    R2,R4
         CVD   R2,TOTSECS           SAVE DURATION IN SECONDS
         BAL   R14,PAGE             GO PRINT REPORT 1 HEADINGS
         EJECT
*************************************************************
*        PUT OUT A LINE FOR EACH ADDRESS RANGE              *
*************************************************************
         LA    R2,COUNTERS          GET RANGE COUNTER START
         LA    R3,448(R2)           GET RANGE COUNTER END
         LA    R8,DISTANCE          GET DISTANCE COUNTER
         MVI   STARTED,C'1'         INDICATE REPORT 1 PROCESSING
PROCPAG  DS    0H                   BRANCH ADDRESS FOR REPORT 1 AND 2
         CVB   R4,TOTCOUNT          GET THE TOTAL COUNT
         ST    R4,FTOT              AND SAVE IN BINARY FOR CALCULATIONS
         LA    R4,0                 INITIAL
         LA    R5,9                 ADDRESS RANGE
PROCSTRT DS    0H
         MVC   PRINT(133),=CL133' ' CLEAR THE PRINTLINE
         CVD   R4,PL8                PRINT
         MVC   PRINT(8),=X'4020212040202120'
         ED    PRINT(4),PL8+6
         CVD   R5,PL8                PRINT
         ED    PRINT+4(4),PL8+6           SEEK
         MVI   PRINT+4,C'-'
         MVC   PRINT+10(10),=X'40202020202020202120'
         ED    PRINT+10(10),3(R2)
         SR    R6,R6
         CVB   R7,0(R2)             GET COUNT
         M     R6,=F'10000'         X 10000 FOR PERCENT CALCULATION
         D     R6,FTOT              AND DIVIDE TO GET PERCENTAGE
         CVD   R7,PL8
         MVC   PRINT+23(7),=XL7'402120204B2020' EDIT MASK
         ED    PRINT+23(7),PL8+5
         MVC   HISTR(50),=CL50' '   CLEAR HISTOGRAM STRING
         LA    R7,50(R7)            ADD 50 FOR ROUNDING
         SR    R6,R6
         D     R6,=F'200'  DIVIDE TO GIVE NUMBER OF HISTOGRAM POINTS
* REGISTER R7 NOW CONTAINS REQUIRED NUMBER OF HISTOGRAM POINTS
         LTR   R7,R7                ANY POINTS TO PLOT
         BZ    RHSTMOV              NO.
         BCTR  R7,0                 CHOP FOR MVC
         EX    R7,HISTMOV           AND MOVE HISTOGRAM SCALE
         B     RHSTMOV              BRANCH ROUND EXECUTED MVC
HISTMOV  MVC   HISTR(0),HISTA
RHSTMOV  DS    0H
         MVC   PRINT+76(50),HISTR   MOVE IN HISTOGRAM STRING
         CLI   STARTED,C'1'         ARE WE ON REPORT 1?
         BNE   NOSKDIS              NO-SKIP SEEK DISTANCE CALC
         CVB   R7,0(R2)             GET SEEK COUNT
         ST    R7,FDIS              AND SAVE AS A FULLWORD
         CVB   R7,0(R8)             GET CUMULATIVE DISTANCE
         LTR   R7,R7                WAS THERE ANY DISTANCE?
         BZ    ZERODIS              NO DISTANCE ON THESE SEEKS
         SR    R6,R6
         D     R6,FDIS              DIVIDE DISTANCE BY COUNT
         B     NZERODIS
ZERODIS  SR    R7,R7
NZERODIS DS    0H
         CVD   R7,PL8
         MVC   PRINT+34(4),=X'40202120'
         ED    PRINT+34(4),PL8+6
         MVC   PRINT+39(4),=CL4'CYLS'
         CP    10(2,R8),=PL2'999'      WAS MINIMUM SET
         BNE   MINSET                  YES
         MVC   8(4,R8),=PL4'0'
MINSET   MVC   PRINT+48(4),=X'40202120'
         ED    PRINT+48(4),10(R8)      MINIMUM SEEK DISTANCE
         MVC   PRINT+63(4),=X'40202120'
         ED    PRINT+63(4),14(R8)      MAXIMUM SEEK DISTANCE
         MVC   PRINT+53(4),=C'CYLS'
         MVC   PRINT+68(4),=C'CYLS'
NOSKDIS  DS    0H                   BRANCH AROUND ON REPORT 2
         MVI   PRINT+75,C'×'
         MVI   PRINT+126,C'×'
         PUT   SYSPRINT,PRINT       PRINT THE LINE
         LA    R2,8(R2)             BUMP TO NEXT COUNTER
         CR    R2,R3                ANY MORE RANGES
         BNL   REPEND               NO-THIS REPORT FINISHED
         LA    R8,16(R8)            INCREMENT DISTANCE COUNTER
         LA    R4,1(R5)             INCREMENT
         LA    R5,10(R5)                    RANGE
         B     PROCSTRT             AND GO DO ANOTHER LINE
REPEND   DS    0H                   END OF REPORT
         CLI   STARTED,C'2'         WAS THIS REPORT 2?
         BE    REP3                 YES GO DO REPORT 3
         MVI   STARTED,C'2'         SET UP FOR REPORT 2
         LA    R2,RNGCNT            POINT TO SEEK MAGNITUDE COUNTERS
         LA    R3,448(R2)           END OF COUNTERS
         BAL   R14,PAGE             GO PRINT PAGE HEADINGS
         B     PROCPAG              AND GO ROUND AGAIN
REP3     DS    0H                   REPORT 3 PROCESSOR
         BAL   R14,PAGE             GO PRINT PAGE HEADINGS
         LR    R2,R10               ADDRESS JOBNAME TABLE
         LR    R3,R2
         A     R3,=F'8000'          END OF TABLE
         LA    R6,1                 INITIALISE LINE COUNTER
REPLOOP  DS    0H
         MVC   PRINT(133),=CL133' ' CLEAR PRINTLINE
         MVC   PRINT+1(8),0(R2)     SET UP JOBNAME
         MVC   PRINT+12(6),=XL6'402020202120'
         ED    PRINT+12(6),13(R2)
*        UNPK  PRINT+13(5),8(8,R2)  SET UP
*        OI    PRINT+17,X'F0'            RESPONSIBILITIES COUNTER
         CVB   R5,8(R2)             GET COUNTER FOR % CALCULATION
         SR    R4,R4
         M     R4,=F'10000'
         D     R4,FTOT
         CVD   R5,PL8
         MVC   PRINT+23(7),=XL7'402120204B2020'  EDIT MASK FOR %
         ED    PRINT+23(7),PL8+5
         PUT   SYSPRINT,PRINT
         LA    R6,1(R6)              BUMP LINE COUNTER
         CH    R6,=H'50'             END OF PAGE?
         BL    NOPEND                NOT YET
         BAL   R14,PAGE              PRINT PAGE HEADINGS
         LA    R6,1                  RE-INITIALISE LINE COUNTER
NOPEND   DS    0H
         LA    R2,16(R2)             BUMP TO NEXT JOB
         CLC   0(8,R2),=PL8'0'       IS IT EMPTY?
         BE    ENDREP3               YES
         CR    R2,R3                 NO-BUT IS THIS END OF TABLE?
         BL    REPLOOP               NO-GO ROUND AGAIN
ENDREP3  DS    0H
         PUT SYSPRINT,=CL133' SEEK ANALYSIS-ENDED    '
         L     13,4(13,0)
         LM    14,12,12(13)
         MVI   12(13),X'FF'
         LA    15,0(0,0)
         BR    14
         EJECT
*************************************************************
*        TOD CLOCK TIME CALCULATION                         *
*************************************************************
TODCALC  DS    0H
         STM   R0,R15,SAVEA           SAVE REGISTERS
         MVC   DOUBLE(8),GTOD         ALIGN TOD CLOCK
         LM    R2,R3,DOUBLE           LOAD UP THE REGISTERS
         SRDL  R2,12                  SHIFT TO GET 52 BITS
         D     R2,=F'10000000'        GET TIME IN 10 SECOND MULTIPLES
         LR    R5,R2                  SAVE REMAINDER
         SR    R2,R2                  CLEAR R2
         D     R2,=F'8640'            DIVIDE TO GET DAYS
* R5 NOW CONTAINS AN ADJUSTMENT FACTOR OF < 10 SECONDS IN MICROSECONDS
* R2 CONTAINS TIME OF DAY IN 10'S OF SECONDS
         LR    R3,R2                  SET UP
         SR    R2,R2                       FOR DIVIDE
         D     R2,=F'360'             GET HOURS
         STH   R3,HH
         LR    R3,R2
         SR    R2,R2
         D     R2,=F'6'               GET MINUTES
         STH   R3,MM                  AND SAVE THEM
         LR    R3,R2                  SHIFT REMAINDER
         SR    R2,R2
         M     R2,=F'10'              R3 NOW CONTAINS THE SECONDS
         SR    R4,R4
         D     R4,=F'1000000'         GET 10 SECOND ADJUSTMENT FACTOR
         AR    R3,R5
         STH   R3,SS                  SAVE SECONDS
         LM    R0,R15,SAVEA           RESTORE REGISTERS
         BR    R14                    RETURN TO CALLER
         EJECT
*************************************************************
*        PAGE HEADING PRINT ROUTINE                         *
*************************************************************
PAGE     DS    0H
         STM   R0,R15,SAVEA           SAVE REGISTERS
         UNPK  PGN(3),PAGENO           PAGE
         OI    PGN+2,X'F0'                 NUMBER
         TIME  DEC                    GET DATE
         ST    R1,PL4
         UNPK  DTE(5),PL4             PUT INTO
         OI    DTE+4,X'F0'                   HEADING
         CP    PAGENO,=PL1'1'         IS THIS REPORT 1?
         BH    ZNOTPAG1
         MVC   REPNAME(40),=CL40'     SEEK COUNT REPORT '
         B     ZPAG1
ZNOTPAG1 DS    0H
         CP    PAGENO,=PL1'2'
         BH    ZNOTPAG2
         MVC   REPNAME(40),=CL40'     SEEK MAGNITUDE REPORT '
         B     ZPAG1
ZNOTPAG2 DS    0H
         MVC   REPNAME(40),=CL40'     SEEK RESPONSIBILITY REPORT '
ZPAG1    DS    0H
         PUT   SYSPRINT,HEAD1          PRINT THE HEADING
         AP    PAGENO,=PL1'1'         INCREMENT PAGE NUMBER
         PTIME BHR,PRHR
         PTIME BMIN,PRMIN
         PTIME BSEC,PRSEC
         PTIME FHR,PTHR
         PTIME FMIN,PTMIN
         PTIME FSEC,PTSEC
         PTIME DHR,PDHR
         PTIME DMIN,PDMIN
         PTIME DSEC,PDSEC
         MVC   PRTOT(6),=XL6'402020202120'
         ED    PRTOT(6),TOTSECS+5
         MVC   PCNT(8),=XL8'4020202020202120'
         ED    PCNT(8),TOTCOUNT+4
         MVC   PSZERO(6),=XL6'402020202120'
         ED    PSZERO(6),SEEKZERO+5
         PUT   SYSPRINT,HEAD2
         CP    PAGENO,=PL1'2'              ARE WE ON REPORT 1?
         BH    PAGE2                       NO
         PUT   SYSPRINT,HEAD4
         PUT   SYSPRINT,HEAD5
         PUT   SYSPRINT,HEAD6
         PUT   SYSPRINT,HEAD7
         B     PAGERET
PAGE2    DS    0H
         CP    PAGENO,=PL1'3'       ARE WE ON REPORT 2?
         BH    PAGE3
         PUT   SYSPRINT,HEAD8
         PUT   SYSPRINT,HEAD9
         PUT   SYSPRINT,HEAD10
         PUT   SYSPRINT,HEAD11
         B     PAGERET
PAGE3    DS    0H
         PUT   SYSPRINT,HEAD12
         PUT   SYSPRINT,HEAD13
         PUT   SYSPRINT,HEAD14
         PUT   SYSPRINT,HEAD15
PAGERET  DS    0H
         LM    R0,R15,SAVEA         RESTORE REGISTERS
         BR    R14                  RETURN TO CALLER
         EJECT
**************************************************************
*        DATA CONTROL BLOCKS                                 *
**************************************************************
         PRINT NOGEN
SYSIN    DCB   DDNAME=SYSIN,EODAD=EODSYS,RECFM=F,LRECL=80,BLKSIZE=80,  *
               DSORG=PS,MACRF=(GM)
GTFIN    DCB   DDNAME=GTFIN,DSORG=PS,RECFM=V,MACRF=(GM),EODAD=ENDRANGE
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FA,LRECL=133, *
               BLKSIZE=133
         PRINT GEN
         SPACE 5
*************************************************************
*         ANALYSIS COUNTERS                                 *
*************************************************************
         DS    0D
         DC    CL16'ANALYSISCOUNTERS'
COUNTERS DC    56PL8'0'             SEEK COUNTERS
RNGCNT   DC    56PL8'0'             MAGNITUDE COUNTERS
DISTANCE DC    56XL16'000000000000000C9999999C0000000C' DIST COUNTERS
SEEKZERO DC    PL8'0'               # SEEKS TO CYLINDER ZERO
TOTSECS  DC    PL8'0'               DURATION INTERVAL IN SECONDS
TOTCOUNT DC    PL8'0'               TOTAL # RECORDS READ
         SPACE 5
**************************************************************
*        TIMING FIELDS                                       *
**************************************************************
         DS    0D
         DC    CL16'TIMING    FIELDS'
BHR      DC    H'0'                 ACTUAL BEGIN TIME HOURS,
BMIN     DC    H'0'                 MINUTES,
BSEC     DC    H'0'                 SECONDS.
FHR      DC    H'0'                 ACTUAL FINISH TIME HOURS,
FMIN     DC    H'0'                 MINUTES,
FSEC     DC    H'0'                 SECONDS.
DHR      DC    H'0'                 DURATION TIME HOURS,
DMIN     DC    H'0'                 MINUTES,
DSEC     DC    H'0'                 SECONDS.
SHR      DC    H'0'                 REQUESTED START TIME HOUR
SMIN     DC    H'0'                 MINUTE
EHR      DC    H'24'                REQUESTED END TIME HOURS,
EMIN     DC    H'0'                 MINUTE
HH       DC    H'0'                 TOD CLOCK HOURS,
MM       DC    H'0'                 MINUTES,
SS       DC    H'0'                 SECONDS.
         SPACE 5
*************************************************************
*         GENERAL WORK CONSTANTS                            *
*************************************************************
HISTR    DC    CL50' '             HISTOGRAM STRING
HISTA    DC    CL50'**************************************************'
SAVEA    DS    18F
FTOT     DC    F'0'
FDIS     DC    F'0'
         DS    0D
PL8      DC    PL8'0'
PL4      DC    PL4'0'
SPARE4   DC    CL4' '
DOUBLE   DS    0D
         DC    D'0'
STARTED  DC    C' '                 START RANGE AND REPORTING SWITCH
LASTSEEK DC    H'0'                 LAST SEEK ADDRESS
REQUEST  DC    CL80' '              SYSIN INPUT AREA
ADDR     DC    CL2' '               REQUESTED DEVICE IN HEX
TRTAB    DS    256C                 TRANSLATE TABLE FOR HEX CONVERSION
         ORG   TRTAB+X'C1'
         DC    X'0A0B0C0D0E0F'
         ORG   TRTAB+X'F0'
         DC    X'00010203040506070809'
         ORG   *
         SPACE  5
*************************************************************
*    PRINT HEADINGS AND AREAS                               *
*************************************************************
PRINT    DC    CL133' '
HEAD1    DC    C'1'
         DC    CL5'DATE '
DTE      DC    CL5' '
         DC    CL38' '
         DC    CL36'T.I.COMPUTER UNIT DASD SEEK ANALYSIS'
REPNAME  DC    CL40' '
         DC    C'PAGE '
PGN      DC    CL3' '
PAGENO   DC    PL1'1'
HEAD2    DC    C' '
         DC    CL5'FROM '
PRHR     DC    CL2'HH'
         DC    C'/'
PRMIN    DC    CL2'MM'
         DC    C'/'
PRSEC    DC    CL2'SS'
         DC    CL10'       TO '
PTHR     DC    CL2'HH'
         DC    C'/'
PTMIN    DC    CL2'MM'
         DC    C'/'
PTSEC    DC    CL2'SS'
         DC    CL15'     DURATION '
PDHR     DC    CL2'HH'
         DC    C'/'
PDMIN    DC    CL2'MM'
         DC    C'/'
PDSEC    DC    CL2'SS'
         DC    CL9'      (   '
PRTOT    DC    CL6' '
         DC    CL20' SECONDS)    DEVICE '
PDEV     DC    CL3' '
         DC    CL8'  COUNT   '
PCNT     DC    CL8' '
         DC    CL2' ('
PSZERO   DC    CL6' '
         DC    CL16' SEEKS TO CYL 0)'
HEAD3    DC    CL133' '
HEAD4    DC    C' '
         DC    CL30'CYLINDER              PERCENT '
         DC    CL30'   AVERAGE        MINIMUM     '
         DC    CL30'   MAXIMUM               PERCE'
         DC    CL20'NTAGE HISTOGRAM     '
         DC    CL22' '
HEAD5    DC    C' '
         DC    CL30'ADDRESS               TOTAL   '
         DC    CL30'   SEEK           SEEK        '
         DC    CL30'   SEEK                       '
         DC    CL42' '
HEAD6    DC    C' '
         DC    CL30'RANGE       COUNT     ACCESSES'
         DC    CL30'   DISTANCE       DISTANCE    '
         DC    CL30'   DISTANCE   ×   10   20   30'
         DC    CL35'   40   50   60   70   80   90  100'
         DC    CL07' '
HEAD7    DC    C' '
         DC    CL30'--------   --------   --------'
         DC    CL30'   --------       --------    '
         DC    CL30'   --------   ×---------------'
         DC    CL42'-----------------------------------×'
HEAD11   DC    C' '
         DC    CL30'--------   --------   --------'
         DC    CL30'   --------                   '
         DC    CL30'              ×---------------'
         DC    CL42'-----------------------------------×'
HEAD8    DC    C' '
         DC    CL30'SEEK                  PERCENT '
         DC    CL30' '
         DC    CL30'                         PERCE'
         DC    CL20'NTAGE HISTOGRAM     '
         DC    CL22' '
HEAD9    DC    C' '
         DC    CL30'DISTANCE              TOTAL   '
         DC    CL102' '
HEAD10   DC    C' '
         DC    CL30'RANGE       COUNT     ACCESSES'
         DC    CL30' '
         DC    CL30'              ×   10   20   30'
         DC    CL42'   40   50   60   70   80   90  100 '
HEAD12   DC    C' '
         DC    CL30'JOB                   PERCENT '
         DC    CL102' '
HEAD13   DC    C' '
         DC    CL30'CAUSING               TOTAL   '
         DC    CL102' '
HEAD14   DC    C' '
         DC    CL30'SEEK        COUNT     ACCESSES'
         DC    CL102' '
HEAD15   DC    C' '
         DC    CL30'--------    -----     --------'
         DC    CL102' '
         SPACE 5
*************************************************************
*         GTF RECORD INPUT DSECT                            *
*************************************************************
GTFDSECT DSECT
GLEN     DS    H                    RECORD DESCRIPTOR
G1       DS    H                                     WORD
GAID     DS    C                    GTF IDENT X'FF' FOR A TRACE RECORD
G2       DS    C
GTOD     DS    8C                   TOD CLOCK
GEID     DS    2C                   EVENT INDICATOR X'5100' FOR A SIO
G3       DS    6C
GJOB     DS    8C                   JOB ISSUING SEEK
G4       DS    13C
GADDR    DS    2C                   DEVICE ADDRESS
G5       DS    9C
GCYL     DS    2C                   SEEK CYLINDER
GTRK     DS    2C                   SEEK TRACK
         END
