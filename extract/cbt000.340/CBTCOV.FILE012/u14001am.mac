         TITLE 'J8SRC DOCUMENTATION MEMBER'
*              THIS PROGRAM IS KNOWN AS J8SRC - SELECTIVE RECORD COPY
*              ITS FUNCTION IS TO COPY AND/OR EDIT A DATASET
*                           ACCORDING TO PARAMETER SPECIFICATIONS.
*                           PARAMETERS ALLOW RECORDS TO BE SELECTED
*                           BY THE CONTENT OF A DEFINED FIELD FOR THE
*                           EDIT OR COPY FUNCTION.
*              NOTE - AN EARLIER VERSION WAS NAMED J8SRE
*
*
*              ORIGINATED BY D.E.MILLS - EMEB NOTTINGHAM
*
*              MODULES OF THE PROGRAM - J8SRC00 - I/O CONTROL
*                                       J8SRE01 - CONTROL RTN.
*                                        (PROG E.P. - SRE01)
*                                        J8SRE02 - PARAMETER RTN.
*                                       J8SRE03 - SELECT RECORD
*                                       J8SRC04 - EDIT/COPY RTN.
*                                       J8SRE05 - HEADINGS RTN.
*                                       J8SRE06 - LOWER TO UPPER CASE
*                                       J8SRE07 - ISAM DS HANDLERCASE
*              ASSEMBLY - LOCAL MACROS USED - MACRO DEFINITIONS FOLLOW
*
./ REPL LIST=ALL,NAME=LNKMD
         MACRO
&TAG     LNKMD &LEVELS=1,&SUPPORT=YES
.*                       &SUPPORT=NO - NO CONVERSION SUPPORT INCLUDED
         LCLA  &AL1,&AL2
         AIF   ('&SUPPORT' EQ 'NO').H
         EXTRN SYSFCP
.H       ENTRY RLINK
         AIF   ('&TAG' EQ '').G
         ENTRY &TAG
.G       ANOP
         USING RLINK,15
&TAG     STM   14,12,12(13)            SAVE ALL REGISTERS
         LA    15,26(0,15)
         LA    12,RSAVEAS              ADDRESS 1ST SAVEAREA
         ST    12,8(13)                STORE SAVEAREA IN HSA
         ST    13,4(12)                STORE HSA ADDRESS
         LR    13,12
         B     REND&SYSNDX
*
RLINK    B     ROUT&SYSNDX
RIN&SYSNDX  LA    12,RSAVEAS+72           ADDRESS 1ST POSS. SAVEAREA
RA&SYSNDX   CLI   0(12),C'U'              UNUSED ?
         BE    RB&SYSNDX
         LA    12,72(0,12)
         CLI   0(12),C'*'              END OF SAVEAREAS ?
         BNE   RA&SYSNDX
         ABEND 97,DUMP
*
RB&SYSNDX   MVI   0(12),C'I'
         ST    13,4(0,12)              STORE ADDR OF NEXT HIGHER S.A.
         ST    12,8(0,13)              STORE ADDR OF NEXT LOWER S.A.
         LR    13,12
         B     RC&SYSNDX
*
ROUT&SYSNDX MVI   0(13),C'U'              MARK LOWER LEVEL S.A. UNUSED
         LR    12,13
         L     13,4(13)                LOAD ADDR OF THIS LEVEL S.A.
         XC    4(4,12),4(12)           CLEAR ADDR OF HIGH SAVE AREA
         XC    8(4,13),8(13)           CLEAR ADDR. OF LOWER  SAVE AREA
         STH   14,RH&SYSNDX
         LM    0,12,20(13)
         L     14,12(13)
         AH    14,RH&SYSNDX
         AIF   ('&SUPPORT' EQ 'YES').D
RC&SYSNDX BR   14                  EXIT
         AGO   .E
.D       ANOP
RC&SYSNDX L    15,=A(SYSFCP)
          L    15,4(0,15)
          ST   13,0(0,15)
          BR   14
*
.E       ANOP
         DS    0F
RSAVEAS  DC    X'C9000000'                                )
         DC    F'0'         ADDR OF NEXT HIGHER LEVEL S.A.)
         DC    F'0'         ADDR OF NEXT LOWER LEVEL S.A. ) 1ST LEVEL
         DC    15F'0'       GEN.REGS.14 TO 12             )
&AL1     SETA  &LEVELS
.A       ANOP
         DC    X'E4000000'
         DC    17F'0'
&AL1     SETA  &AL1-1
         AIF   (&AL1 EQ 0).B
         AGO   .A
.B       ANOP
         DC    X'5C5C'
RH&SYSNDX DS    H
*
REND&SYSNDX BALR  12,0
         USING *,12
         DROP  15
         AIF   ('&SUPPORT' NE 'YES').F
         L     15,=A(SYSFCP)           LOAD ADDR OF SYSFCP
         L     15,4(0,15)
         ST    13,0(0,15)
.F       MEND
./ REPL LIST=ALL,NAME=INMD
         MACRO
&TAG     INMD
         AIF   ('&TAG' NE '').A
         MNOTE *,'CSECT NAME USED AS ENTRY NAME'
         USING &SYSECT,15
         STM   14,12,12(13)
         AGO   .B
.A       ANOP
         ENTRY &TAG
         USING &TAG,15
&TAG     STM   14,12,12(13)
.B       L     15,=V(RLINK)
         BAL   14,4(0,15)
         BALR  12,0
         USING *,12
         DROP  15
         MEND
./ REPL LIST=ALL,NAME=OUTMD
         MACRO
&TAG     OUTMD &N
         LCLA  &AL1
         AIF   ('&TAG' EQ '').C
&TAG     L     15,=V(RLINK)
         AGO   .NOTAG
.C       ANOP
         L     15,=V(RLINK)
.NOTAG   ANOP
         AIF   ('&N' NE '').A
&AL1     SETA  0
         AGO   .B
.A       ANOP
&AL1     SETA  &N*4
.B       LA    14,&AL1
         BR    15
         LTORG
         MEND
./ REPL LIST=ALL,NAME=DOMD
         MACRO
&TAG     DOMD  &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&L,&M,&N,&O,&P,&Q,&R,&S
         LCLA  &AL1,&AL2
         AIF   ('&A' NE '').A
         MNOTE 9,'ROUTINE OMITTED - GENERATION TERMINATED'
         MEXIT
.*
.A       CNOP  0,4
         AIF   ('&TAG' EQ '').Z
&TAG     L     15,=V(&A)
         AGO   .NOTAG
.Z       ANOP
         L     15,=V(&A)
.NOTAG   ANOP
&AL1     SETA  19
.B       AIF   ('&SYSLIST(&AL1)' NE '').BA
&AL1     SETA  &AL1-1
         AIF   (&AL1 EQ 1).D
         AGO   .B
.*
.BA      ANOP
&AL2     SETA  &AL1*4
         BAL   1,*+&AL2
&AL2     SETA  1
.C       ANOP
&AL2     SETA  &AL2+1
         AIF   ('&SYSLIST(&AL2)' NE '').CA
         DC    A(0)
         AGO   .CB
.CA      DC    A(&SYSLIST(&AL2))
.CB      AIF   (&AL2 NE &AL1).C
.D       BALR  14,15
         MEND
*
*              LINKAGE EDITOR ATTRIBUTES - NONE
*                             CONTROL STATEMENTS -
*              INCLUDE DDNAME(J8SRC00)
*              INCLUDE DDNAME(J8SRE01)
*              ENTRY SRE01
*              INCLUDE DDNAME(J8SRE02)
*              INCLUDE DDNAME(J8SRE02)
*              INCLUDE DDNAME(J8SRE03)
*              INCLUDE DDNAME(J8SRE03)
*              INCLUDE DDNAME(J8SRC04)
*              INCLUDE DDNAME(J8SRE05)
*              INCLUDE DDNAME(J8SRE06)
*              INCLUDE DDNAME(J8SRE07)
         END
