         TITLE  'SPY54 - ALLOCATION DETAILS - REL 1.0'
**************************************************************
* SPY54 ALLOWS THE OPERATOR TO DISPLAY ON THE SYSTEM CONSOLE *
* VOL SER NOS OF DEVICES ALLOCATED TO A SPECIFIC JOB STEP OR *
* NAMES OF JOBS USING A SPECIFIC DEVICE                      *
**************************************************************
* MACRO
         MACRO
&NAME    CUREG
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUREG MACRO:16/01/75'
         POP   PRINT
         ACTR  50
         LCLA  &X,&Y,&Z
*    GENERAL REGISTERS
.LPR     ANOP
R&X      EQU   &X
&X       SETA  &X+1
         AIF   (&X LT 16).LPR
*    CONTROL REGISTERS
.LPC     ANOP
C&Y      EQU  &Y
&Y       SETA  &Y+1
         AIF   (&Y LT 16).LPC
*    FLOATING POINT REGISTERS
.LPF     ANOP
F&Z      EQU   &Z
&Z       SETA  &Z+2
         AIF   (&Z LT 8).LPF
         MEND
         MACRO
&NAME    CUENTER &BASE=(R12),&RECURSE=,&RENT=,&FIRST=,&CSECT=,&PARM=
         LCLA  &AL0,&AL1,&AL2,&AL9,&AL12
         ACTR  45                                              DEC 75
&AL2     SETA  &SYSNDX
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUENTER MACRO:16/06/77'
         POP   PRINT
         LCLC  &CL0,&CL1,&CL2,&CL3,&CL12,&CL13,&CL50
         LCLC  &CL51,&CL52,&CL53
         GBLB  &BG15
         AIF   ('&NAME' EQ '').B98
         ENTRY &NAME
.B98     ANOP
&CL0     SETC  '12'
         AIF   ('&BASE'(1,1) EQ '(').B10  *VALIDATE &BASE PARAMETER
&CL0     SETC  '&BASE'
         AIF   ('&CL0' EQ 'NONE').B11
         AIF   ('&CL0'(1,1) NE 'R').B4B
&AL9     SETA  K'&CL0-1
&CL0     SETC  '&CL0'(2,&AL9)
.B4B     AIF   ((&CL0 GT 1) AND (&CL0 LT 13)).B11
.BERA    MNOTE 9,'&&BASES WRONG,MACRO ABANDONED'
         MEXIT
.B10     ANOP
&CL0     SETC  '&BASE(1)'
&AL0     SETA  1                        COUNT ALONG TO-BE-SCANNED BASES
.B13     ANOP
&AL1     SETA  1                       *COUNT ALONG PREVIOUSLY SCANNED
&CL12    SETC  '&BASE(&AL0)'
         AIF ('&CL12'(1,1) NE 'R').B12A
&AL9     SETA  K'&CL12-1
&CL12    SETC  '&CL12'(2,&AL9)
.B12A    ANOP
&AL12    SETA  &CL12
.B12     AIF    ((&AL12 LT 2) OR (&AL12 GT 12)).BERA
.B15     AIF   (&AL1 EQ &AL0).B14      *J OUT IF REACHED LATEST
&CL13    SETC  '&BASE(&AL1)'
         AIF   ('&CL13'(1,1) NE 'R').B15A
&AL9     SETA  K'&CL13-1
&CL13    SETC  '&CL13'(2,&AL9)
.B15A    AIF   (&CL12 EQ &CL13).BERA *ERROR IF DUPLICATED
&AL1     SETA  &AL1+1                  *STEP ALREADY SCANNED PTR
         AGO   .B15
.B14     ANOP
&AL0     SETA  &AL0+1                  *STEP SCAN POINTER
         AIF   ('&BASE(&AL0)' NE '').B13 *J IF NOT NULL
&AL9     SETA  N'&BASE
         AIF   (&AL0 LT &AL9).BERA *ERROR IF NULL ELEMENT
.B11    ANOP
.*   START GENERATING PROGRAM
         DS    0H
         BALR  R15,0
         USING *,R15
&CL1     SETC  '&CSECT'
         AIF   ('&CL1' NE '').B30
&CL1     SETC  '*'
.B30     ANOP
&NAME    SAVE  (14,12),,&CL1
.B32     ANOP
.B90A    BAL   &CL0,OCSVA&AL2
         DROP  R15
         USING *,&CL0
OCSVD&AL2 EQU  *         HOLD IMMEDIATELY AFTER USING STATEMENT
         AIF   (&BG15).NODATIM
         CUREG                          REGISTER MNEMNONICS
&CL51    SETC  '&NAME'
         AIF   (K'&CL51 LE 8).DT1
&CL51    SETC  '&CL51'(1,8)
         AGO   .DT2
.DT1     ANOP
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '&SYSECT'
         AIF   ('&CL51' NE '').DT2
&CL51    SETC  '????????'
.DT2     ANOP
&CL52    SETC  '&SYSDATE'(4,2)'.'.'&SYSDATE'(1,2)'.'.'&SYSDATE'(7,2)
&CL53    SETC  '&SYSTIME'(1,2)':'.'&SYSTIME'(4,2)':00'
CUVRSION DC    C'****VERSION/',CL8'&CL51',C'/&CL52./&CL53'
&BG15    SETB  (1)                                             DEC 75
.NODATIM ANOP
OCSAR&AL2 DS    18F
OCSVA&AL2 EQU  *
         AIF   ('&BASE' EQ '').B89
&AL9     SETA  N'&BASE
         AIF   (&AL9 LE 1).B89
&AL1     SETA  &AL9
&AL0     SETA  2
.B88     ANOP
&CL2     SETC  '&BASE(&AL0)'
&CL3     SETC  '&BASE(&AL0-1)'
         LA    &CL2,1
         LA    &CL2,4095(&CL2,&CL3)
         USING OCSVD&AL2+4096*(&AL0-1),&CL2
         AIF   (&AL0 EQ &AL1).B87
&AL0     SETA  &AL0+1
         AGO   .B88
.B89     ANOP
.B87     ANOP
         ST    R13,OCSAR&AL2+4
         LR    R15,R13
         LA    R13,OCSAR&AL2
         ST    R13,8(R15)
         MEXIT
.MEND    MEND
* MACRO
         MACRO
&NAME    CUEXIT   &RC=(15)
         GBLB  &BG15                                 DEC 75
         LCLA  &B,&W
         LCLC  &Z
&NAME    DS 0H
         PUSH  PRINT
         PRINT ON
         MNOTE 'DATE OF THIS VERSION OF THE CUEXIT MACRO:16/06/77'
         POP   PRINT
         L      R13,4(13)
&Z         SETC  '&RC'
.B94     AIF   ('&Z'(1,2) NE '(R').B84
&B       SETA  K'&Z-2
&Z        SETC  '&Z'(1,1).'&Z'(3,&B)
.B84     ANOP
         AIF   ('&Z' EQ '(15)').B60
         AIF   ('&Z'(1,1) EQ '(').B61
         RETURN (14,12),RC=&Z
         AGO   .B62
.B61     ANOP
&W       SETA  K'&Z-2
&Z       SETC  '&Z'(2,&W)
         LR    R15,&Z
.B60     RETURN (14,12),RC=(15)
.B62     ANOP
         MEND
SPY54    CUENTER
         L     1,0(1)               GET PARM ADDRESS
         MVC   NAME,2(1)            MOVE PARM TO NAME
         LA    1,128(1)             GET ECB ADDRESS
         POST  (1)                  LET COMMAND MODULE CONTINUE
         CLC   NAME(2),=C'ZZ'       DEVICE ENTRY
         BNE   NOPARM               BRANCH IF JOB
         NI    DEVSWIT+1,X'00'      SET DEVICE SWITCH
         MVC   DISK(6),NAME+2       MOVE VOL SER NO
         B     DISKRTN              GET OUT
NOPARM   NI    NAMSWIT+1,X'00'      SET JOBNAME SWITCH
DISKRTN  L     5,16                 LOAD CVT
         L     5,244(5)             LOAD P0 TCB
         SR    9,9                  CLEAR OUT 9
         IC    9,248(9)             LOAD NUMBER OF PARTITIONS
         L     5,0(5)               LOAD FIRST TCB
         LA    6,WTOAREA            LOAD WTO AREA ADDRESS
LOOP     L     4,12(5)              LOAD TIOT
         CLC   13(3,5),=X'000000'   IS TIOT ZERO?
         BE    SYSTEM               IF SO NO DICE
         C     4,TIOTSTOR           IS THIS A SUBTASK?
         BE    SYSTEM               IF SO GET OUT
         ST    4,TIOTSTOR           STORE TIOT ADDRESS
         XR    2,2                  CLEAR OUT 2
         MODESET KEY=ZERO           GET INTO KEY ZERO
NAMSWIT  B     NAME1                BYPASS JOBNAME TEST
         CLC   0(8,4),NAME          IS THIS THE JOB WE WANT
         BNE   SYS                  IF NOT CARRY ON
         OI    NAMSWIT1,X'FF'       MARK OFF JOBNAME FOUND
NAME1    MVC   NAME,0(4)            MOVE JOBNAME TO STORE
         LA    4,24(4)              MOVE UP TIOT
         LA    2,24(2)              MOVE UP REG 2
TIOTLOOP CLI   0(4),X'14'           END OF TIOT?
         BNE   SYS                  IF SO TRY NEXT TCB
         CLC   17(3,4),=X'000000'   UCB ADDRESS ZERO?
         BE    NEWTIOT              IF SO TRY NEXT ENTRY
         L     8,16(4)              LOAD UCB ADDRESS
DEVSWIT  B     DEV1                 BYPASS DEVICE COMPARISON
         CLC  28(6,8),DISK          IS THIS THE SPECIFIC DEVICE?
         BNE   NEWTIOT              IF NOT TRY ANOTHER
DEV1     MVC   28(3,6),13(8)        MOVE DEVICE NUMBER TO WTO AREA
         MVC   0(8,6),NAME          MOVE JOBNAME TO WTOAREA
         MVC   10(8,6),4(4)         MOVE DDNAME TO WTOAREA
         MVC   20(6,6),28(8)        MOVE VOL SER NO TO WTO AREA
         CLC   10(4,6),=C'    '     CONCATENATED DATA SET
         BNE   NOWORRY              IF SO
         MVC   10(8,6),DDNAME       MOVE PREVIOUS DDNAME
NOWORRY  MVC   DDNAME,10(6)         UPDATE DDNAME
ALLOVER  L     10,8(5)              LOAD DEB ADDRESS
         CLC   9(3,5),=X'000000'    IS DEB ZERO
         BE    UPWTO                IF SO PUT OUT WTO
DEBLOOP  L     11,24(10)            LOAD DCB ADDRESS
         CH    2,40(11)             IS OFFSET ON DCB = OUR TIOT OFFSET
         BNE   NOTTHIS              IF NOT TRY NEXT DEB
         LR    3,10                 LOAD DEB ADDRESS IN 3
         SH    3,=H'13'             GET BACK 13 BYTES
         MVC   CCHHR(5),0(3)        MOVE CCHHR OF DSCB
         CLC   CCHHR,=X'FFFFFFFFFF' IS IT X'FF'
         BE    NOTTHIS              IF SO MISS OUT OBTAIN
         MVC   VOLSER1,20(6)        MOVE VOL SER FOR OBTAIN
         OBTAIN DATASET             GET DATASET NAME
         MVC    32(28,6),READAR     MOVE DATA SET NAME TO WTO AREA
NOTTHIS  CLC    5(3,10),=X'000000'  NEXT DEB ADDRESS = ZERO
         BE     UPWTO               IF SO PUT OUT WTO
         L      10,4(10)            GET NEXT DEB
         B      DEBLOOP             BRANCH BACK
UPWTO    LA     6,50(6)             UP WTO AREA ADDRESS
         L     1,COUNTER            LOAD NUMBER OF WTOS SO FAR
         LA    1,1(1)               UP COUNT
         ST    1,COUNTER            STORE AGAIN
         CH    1,=H'48'             GREATER THEN MAX NO OF WTOS
         BL    NEWTIOT              FINE
         MODESET KEY=NZERO          OTHERWISE GET OUT OF KEY ZERO
         WTO   '** SPY - WTO BUFFER EXCEEDED. MORE THAN 48 OCCURENCES'
         B      RETURN              GET OUT
NEWTIOT  LA     2,20(2)             UP REG 2 ADDRESS
         LA     4,20(4)             UP TIOT ADDRESS
         B      TIOTLOOP            BRANCH BACK
SYS      MODESET KEY=NZERO          GET OUT OF KEY ZERO
SYSTEM   CLC     116(4,5),=X'00000000'  NEXT TCB = ZERO
         BE      RETURN              IF SO GET OUT
         CLI     NAMSWIT1,X'FF'      JOBNAME ONLY?
         BE      RETURN              IF SO FINISH OFF
         L       5,116(5)
         BCT     9,LOOP              BRANCH BACK ON PARTITION COUNT
RETURN   LA      6,WTOAREA           LOAD WTO AREA ADDRESS
         CLC     0(20,6),BLANKS      NO ENTRY IN WTO AREA?
         BNE     PUTOUT              IF ENTRY PUT OUT
         WTO     '** SPY - DEVICE / JOBNAME NOT FOUND IN QUEUES **'
         B       FIN                 GET OUT OF JOB
PUTOUT   MVC     WTO1+11(50),0(6)    MOVE WTO AREA TO WTO
WTO1     WTO   '**                                                 **'
         LA      6,50(6)             UP WTO AREA ADDRESS
         CLC     0(20,6),BLANKS      END OF WTO AREA
         BNE     PUTOUT              IF NOT TRY AGAIN
FIN      CUEXIT  RC=0
DATASET  CAMLST  SEEK,CCHHR,VOLSER1,READAR
CCHHR    DC      XL5'0000000000'
VOLSER1  DC      CL6' '
         DS      0D
READAR   DS      148C
NAME     DC      CL8' '
TIOTSTOR DC      F'0'
DISK     DC      CL6' '
DDNAME   DC      CL8' '
BLANKS   DC      CL20' '
COUNTER  DC      F'0'
NAMSWIT1 DC      X'00'
WTOAREA  DC      10CL250' '
         END
