QINIT    CSECT
*
*  THE RTPQINIT PROGRAM ALLOWS A SECONDARY JOBQUEUE TO BE FORMATTED AS
*  A NORMAL O.S. JOB  IF IT IS SUBSEQUENTLY NECESSARY TO REFORMAT THE
*  QUEUE AT IPL TIME THE USER CAN SAVE A CONSIDERABLE AMOUNT OF
*  UNPRODUCTIVE TIME BY RE-IPLING AND SPECIFYING THE ALTERNATE QUEUE IN
*  THE SET COMMAND.  THIS ALSO ALLOWS THE USER TO EXTRACT WHAT HE CAN
*  FROM THE CORRUPT QUEUE (NO PRGRAMS ARE PROVIDID FOR THIS) BEFORE
*  RE-FORMATTING IT BY RTPQINIT IN ANTICIPATION OF THE NEXT IPL.
*
*  EXAMPLE OF JCL REQUIREDD....
*        //FORMAT  EXEC  PGM=RTPQINIT,PARM='12,240,60'
*        //JOBQUE  DD  DSN=SYS1.SYSJOBQE,VOL=REF=XYZ,DISP=SHR
*
*  NOTE THAT AN ALTERNATE QUEUE CAN BE CREATED AND FORMATTED IN ONE RUN
*  IF DISP=(,KEEP) IS USED
*
*  THE CONFIRMATION OF THE SUCCESS OR FAILURE OF THIS RUN IS GIVEN BY
*  WTO.
*
*  NOTES
*        THIS PROGRAM WAS WRITTEN BY HAVANT PLANT, NOT BY RESPOND.
*        IT WILL NEED REVISION FOR RELEASE 19, WHICH WILL REQUIRE
*              FOUR JOBQ PARAMETERS INSTEAD OF THREE - IT WOULD BE
*              WORTH CHANGING THE WTO MACROS TO ROUTCODE 11.
*        JOBQUEUE PARAMETERS MUST BE GIVEN ON THE EXEC CARD - IF 'U'
*              IS USED IT GIVES A DEFAULT OF '13,260,260', NOT YOUR
*              SYSGEN DEFAULTS.
*        THE PROGRAM WILL NOT FORMAT A DATA SET, REGARDLESS OF NAME, ON
*              THE SAME DISK DRIVE AS THE LIVE JOBQUEUE.
*
*  RTPQINIT CALLS THE 'IEFORMAT' CSECT WHICH CAN BE FOUND IN LINKLIB
*  IN THE MODULE 'IEFSQINT' (OR SIMILAR IN OTHER SYSTEMS). THE WAY OF
*  MAKING THIS CSECT AVAILABLE USED AT RESPOND IS TO RUN A LINKEDIT AS
*  FOLLOWS....
*        //LKED   EXEC  PGM=IEWL,PARM='XREF,LIST,LET,NCAL'
*        //LINK  DD  DSN=SYS1.LINKLIB,DISP=SHR
*        .....................................
*        //SYSLIN   DD   *
*          (INCLUDE RTPQINIT MODULE)
*          REPLACE  IEFSD055
*          REPLACE  IEFSD300
*          REPLACE  IEFSD304
*          REPLACE  IEFSD311
*          REPLACE  IEFQMSGV
*          INCLUDE  LINK(IEFSQINT)
*          NAME  RTPQINIT(R)
*
*   FORMAT SECONDARY JOB Q DATA SET.
*
         USING   *,R12
         STM     R14,R12,12(R13)        SAVE REGS
         LR      R12,R15                LOAD BASE
         ST      R13,SAVEAREA+4         SAVE R13
         LA      R13,SAVEAREA+12        SET R13
*
         TITLE   '*** QINIT -- FORMAT SECONDARY NUCLEUS.'
*
*   CHECK IF PARAMETERS SUPPLIED.
*
         LR    10,1
         SPACE
*  FIND OUT IF FOURTH JOBQ PARAMETER APPLIES BY CONSULTING RELEASE NO
*  AT CVT-4 (NOTE THAT THIS PROGRAM WILL NOT WORK WITH RELEASES PRIOR
*  TO REL 18).
*  FOR REL 18 IGNORE FOURTH (WTP) PARAMETER.
*  FOR REL 19 AND LATER USE IT.
         SPACE
         L     1,16
         LA    2,4
         SR    1,2
         PACK  WORK1(8),0(2,1)
         CVB   2,WORK1
         LA    1,19
         CR    1,2
         BNH   *+8
         OI    REL18+1,240   SET BRANCH IF REL = 18
         SPACE
*  NOW TEST PARAMETERS SUPPLIED.  IF NONE, OR 'U', EXTRACT PARAMETERS
*  FROM MASTER RESIDENT CORE.  OTHERWISE ASSUME THAT THE FOUR (THREE
*  FOR REL 18) PARAMETERS HAVE BEEN SUPPLIED IN FULL E.G.'15,390,90,10'
*  AND STORE THEM AS BINARY VALUES FOR SUBSEQUENT CALCULATION OF THE
*  PARAMETERS TO BE PASSED TO IEFORMAT.
         SPACE
*  NOTE THAT REL 19 SUPPLIES THE SAME THREE PARAMETERS TO IEFORMAT
*  (WHICH HAS NOT CHANGED SINCE REL 17) BUT ADJUSTS THE SECOND BY AN
*  ALLOWANCE FOR WTP.
         SPACE
         L     1,0(10)
         LH    2,0(1)
         LTR   2,2
         BZ    DEFAULTS      NO PARM SUPPLIED.
         CLI   2(1),C'U'
         BE    DEFAULTS
*
         LA      R3,2(R1)               INIT SCAN PTR
         BAL     R9,QNT002              GET FIRST PARM
         C       R5,=F'10'              Q..LT. 10
         BL      QNT050                 YES-ERROR
         C       R5,=F'255'             Q..GT. 255
         BH      QNT050                 YES-ERROR
         ST    5,LPT         PARM 1 - RECORDS PER LOGICAL TRK
         BAL     R9,QNT002              GET SECOND PARM
         ST    5,TRS         PARM 2 - RECORDS RESERVED PER INIT.
         BAL     R9,QNT002              GET THIRD PARM
         ST    5,HKT         PARM 3 - RECORDS RESERVED FOR CANCEL
         SPACE
REL18    BC    0,COMPUTE     IF REL 18 OMIT PARM 4.
         SPACE
         BAL   9,QNT002
         ST    5,WTP         PARM 4 - WTP LIMIT
         SPACE
*  NOW COMPUTE PARAMETERS TO BE PASSED TO IEFORMAT.
*  THIS INVOLVES ROUNDING AND (IF NOT REL 18) ALLOWANCE FOR WTP.
         SPACE
COMPUTE  LM    2,5,LPT
         STH   2,QNLPT
         SR    0,0
         SR    1,1
         TM    REL18+1,240
         BO    IGNWTP
         LR    1,5
         LA    1,1(1)
         DR    0,2
         LA    1,1(1)
         MR    0,2
IGNWTP   EQU   *
         LR    6,1           (WTP)
         AR    6,3           (TRS)
         BAL   14,CONVERT
         STH   1,QNTRS
         LR    6,4           (HKT)
         BAL   14,CONVERT
         STH   1,QNHKT
         B     QNT010
         SPACE
CONVERT  LTR   6,6
         BZ    SET2
         BCTR  6,0
         SR    0,0
         LR    1,6
         DR    0,2
         LA    1,2(1)
         BR    14
         SPACE
SET2     LA    1,2
         BR    14
         SPACE
DEFAULTS L     8,16
         L     6,12(8)
         LH    1,X'74'(6)
         STH   1,QNHKT
         LH    1,X'80'(6)
         STH   1,QNLPT
         LH    1,X'82'(6)
         STH   1,QNTRS
         B     QNT010
*
         EJECT
*
*   SUBROUTINE TO BREAK OUT PARAMETER INFORMATION.
*
QNT002   EQU     *
         LTR     R2,R2                  Q.END OF LIST
         BNP     QNT050                 YES-ERROR
         SR      R4,R4                  ZERO R4
         LA      R5,4                   SET LOOP COUNT
*
QNT004   EQU     *
         CLI     0(R3),C','             Q.COMMA
         BE      QNT008                 YES
         SLL     R4,8                   SHIFT R4 LEFT FOR NEXT CHAR
         IC      R4,0(R3)               SAVE CHARACTER
         LA      R3,1(R3)               STEP POINTER
         BCT     R2,QNT006              DECREMENT COUNT
         B       QNT008
*
QNT006   EQU     *
         BCT     R5,QNT004              BR IF MORE
         B       QNT050                 ERROR- GT 3 CHARACTER NUMBER
*
QNT008   BCTR  2,0
         LA      R3,1(R3)               STEP PTR
         ST      R4,WORK1+4             SAVE RESULT
         PACK    WORK2(8),WORK1(8)      PACK RESULT
         CVB     R5,WORK2               CONVERT TO BINARY
         BR      R9                     RETURN TO CALLER
*
*   PARAMETER INFORMATION AS FOLLOWS--
*     'AA,BBB,CCC' - JOB Q PARAMETERS AS DEFINED IN SYSGEN SRL.
*     'U' - USE DEFAULT PARAMETERS OF 13,260,260
*
         SPACE   2
*
R0       EQU     0
R1       EQU     1
R2       EQU     2
R3       EQU     3
R4       EQU     4
R5       EQU     5
R6       EQU     6
R7       EQU     7
R8       EQU     8
R9       EQU     9
R10      EQU     10
R11      EQU     11
R12      EQU     12
R13      EQU     13
R14      EQU     14
R15      EQU     15
*
         SPACE   2
*
         EJECT
*
*   OPEN DATA SET AND CALL FORMATTING MODULE.
*
QNT010   EQU     *
         OPEN    (JOBQ,(OUTPUT))        OPEN DCB
         TM      JOBQ+48,X'10'          Q.OPEN SUCCESSFUL
         BZ      QNT060                 NO-ERROR
         L       R2,JOBQ+44             GET DEB ADDRESS
         L       R3,16                  CVT POINTER
         L       R3,12(R3)              SYSJOBQE DCB PTR
         L       R3,44(R3)              SYSJOBQE DEB PTR
         CLC     33(3,R2),33(R3)        Q.ARE WE FORMATTING
*                                        ACTIVE JOB Q
         BE      QNT070                 YES-ERROR
*
         LA      R1,QNPARMS             SET PARAMETER LIST POINTER
         CALL    IEFORMAT               CALL FORMATTING MODULE.
*
         CLI     SYNFLAG,0              Q.ANY ERROR
         BNE     QNT080                 YES
*
         L       R2,JOBQ+44             GET DEB ADDRESS
         LH      R2,34(R2)              UCB PTR
         MVC     QNT012+37(3),13(R2)    MOVE UNIT ADDR INTO MSG
         CNOP    0,4
*
QNT012   EQU     *
         WTO     'IZZQINT - SECONDARY JOB Q ON XXX FORMATTED SUCCESSFULX
               LY.',ROUTCDE=(2,11)
*
         EJECT
*
*   TERMINATION ROUTINES.
*
QNTEXITA EQU     *
         NI      JOBQ+48,X'7F'          TURN OFF WRITE FLAG
         CLOSE   (JOBQ)                 CLOSE DATA SET
*
QNTEXITB EQU     *
         L       R13,SAVEAREA+4         RESTORE R13
         LM      R14,R12,12(R13)        RESTORE REGS
         SR      R15,R15                ZERO RC
         BR      R14                    RETURN
*
         EJECT
*   ERROR ROUTINES.
*
*
QNT050   EQU     *
         WTO     'IZZQINT - PARAMETERS INVALID.',ROUTCDE=(2,11)
         B       QNTEXITB
*
QNT060   EQU     *
         WTO     'IZZQINT - COULD NOT OPEN DATA SET.',ROUTCDE=(2,11)
         B       QNTEXITB
*
QNT070   EQU     *
         WTO     'IZZQINT - JOB Q SPECIFIED IS IN USE.',ROUTCDE=(2,11)
         B       QNTEXITA
*
QNT080   EQU     *
         WTO     'IZZQINT - ERROR FORMATTING SECONDARY JOB Q',         *
               ROUTCDE=(2,11)
         B       QNTEXITA
*
         EJECT
*
*
*   SYNAD ROUTINE.
*
SYNAD    EQU     *
*        SYNAD ROUTINE TO BE INSERTED HERE. ******************
*
         EJECT
*
*   Q MANAGER PARAMETER AREA.
*
QNPARMS  DS      0F
QNDCB    DC      A(JOBQ)                ADDR OF DCB
QNHDA    DC      2F'0'                  DISK ADDR OF M.H.(MBBCCHHR)
         DC      X'00'
QNFLTM   DC      X'0000'                NN OF FIRST LTRK AVAILABLE
QNQBK    DC      X'00'                  QUEUE BREAKING INFORMATION
QNTLNM   DC      H'0'                   NO. OF LTRKS IN Q EXTENT
QNNOTM   DC      H'0'                   NO. OF LTRKS AVAILABLE
QNHKT    DC    H'0'                     THRSHLD OF LTRKS FOR OVERFLOW
QNTBT    DC      H'0'                   TOTAL THRSHLD OF LTRKS + K
QNTIDM   DC      H'0'                   NN OF LAST LTRK AVAILABLE
QNKTT    DC      H'0'                   NN OF FIRST LTRK OF ALL JOBQ
QNHPT    DC      H'0'                   NUMBER OF HANDLES PER PTRK
QNRPT    DC      H'0'                   NO. OF RECORDS PER PHYS. TRK
QNLPT    DC    H'0'                     NO. OF RECORDS PER LTRK
QNTRS    DC    H'0'                     THRSHLD OF LTRKS SAVED PER INIT
QNNHM    DC      H'0'                   NUMBER OF HANDLES ON MIXED TRK
QNFQR    DC      H'0'                   NN OF 1ST JOBQ RCD (QNKTT)
QNECBA   DC      F'0'                   ECB FOR NO SPACE
*
         EJECT
*
*   CONSTANTS, WORK AREAS, DCB, ETC.
*
LPT      DC    F'0'
TRS      DC    F'0'
HKT      DC    F'0'
WTP      DC    F'0'
SAVEAREA DS      9D
WORK1    DC      2F'0'
WORK2    DC      2F'0'
SYNFLAG  DC      X'00'
*
         LTORG
*
         PRINT   ON,NOGEN
JOBQ     DCB     DSORG=PS,DDNAME=JOBQUE,MACRF=WP,DEVD=DA,SYNAD=SYNAD
*
         END
