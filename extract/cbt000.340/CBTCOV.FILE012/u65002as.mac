         TITLE 'SPVL     -  VSAM CATALOG LIST PROGRAM  '
***********************************************************************
************************* L I S T C A T *******************************
***********************************************************************
*         L I S T   V S A M   C A T A L O G   P R O G R A M           *
***********************************************************************
*****                                                             *****
*****    THIS PROGRAM ANALYSES AND SUMMARISES THE INFORMATION     *****
*****    NORMALLY PRODUCED BY THE AMS UTILITY IDCAMS.             *****
*****                                                             *****
*****    THE REPORT IS IN COMPONENT NAME SEQUENCE WITH ALL        *****
*****    RELEVANT INFORMATION ON ONE LINE.                        *****
*****                                                             *****
*****    THE PROGRAM ONLY ACCEPTS CONTROL CARDS OF THE            *****
*****    FORMAT:                                                  *****
*****              LISTC ALL LEVEL( ????? )                       *****
*****    THE PROGRAM ONLY WORKS WHEN RUN THE SELECTED INPUT       *****
*****    COMES FROM AN 'ICF' CATALOG.                             *****
*****                                                             *****
*****                                                             *****
*****    EXECUTION JCL:                                           *****
*****         //   EXEC  PGM=?                                    *****
*****         //STEPLIB  DD  DSN=?,DISP=SHR                       *****
*****         //SYSPRINT  DD  SYSOUT=*                            *****
*****         //SYSIN  DD  *                                      *****
*****             LISTC ALL LEVEL(A)                              *****
*****             LISTC ALL LEVEL(?????)                          *****
*****                                                             *****
*****    THE PROGRAM IS VERY SIMPLE IN CONCEPT AS IT WAS          *****
*****    DEVELOPED VERY QUICKLY. IT DYNAMICALLY INVOKES AMS       *****
*****    AND INTERCEPTS THE AMS SYSPRINT OUTPUT TO PRODUCE        *****
*****    ITS OWN. AS SUCH IT HAS BEEN FAIRLY EASY TO AMEND.       *****
*****                                                             *****
*****                                                             *****
***********************************************************************
         SPACE 4
***********************************************************************
*****  A U T H O R :- B.HOCKEN/D.J.HARRIS  30  09  82             *****
*****                                                             *****
*****                                                             *****
***** AMENDED 08 12 82 DJH   CHANGE CONDCODE 8 TO CONDCODE  4 FOR *****
***** LEVELS WITH NO CATALOG ENTRIES                              *****
*****                                                             *****
***** AMENDED 08 03 83 DJH   ADD CREATION DATE                    *****
*****                        ADD DEVICE TYPE                      *****
*****                        SKIP LINE AFTER GDG GROUP            *****
*****                                                             *****
*****                                                             *****
*****                                                             *****
*****                                                             *****
***********************************************************************
*                                                                     *
*                                                                     *
***********************************************************************
         PRINT NOGEN
SPVL     CSECT
         INIT  SPVL,12
         SPACE 4
*
***      THIS PROGRAM IS PARAMETER DRIVEN.
*
         LR    R3,R1                   PRESERVE REG1.
         LOAD  EP=IDCAMS               LOAD IDCAMS.
         LR    R15,R0                  GET EP ADDRESS.
         ST    R15,IDCAMS              STORE IT FOR LATER USE.
         L     R3,0(R3)                PICK UP PARM PTR.
         CLC   0(2,R3),=X'0000'        Q.PARM SUPPLIED.
*        BE    ABEND20                 NO.
         L     R15,IDCAMS              LOAD ADDRESS OF IDCAMS.
         CALL  (15),(OPTIONS,DNAMES,PAGENUM,IOLIST),VL
         LTR   R15,R15
         BNZ   ABEND8                  GO ABEND WITH CODE 8.
         WTO   'SPVL   -  LISTCAT COMPLETE'
         EXIT  RC=0
ABEND8   EQU   *
         WTO   'SPVL   -  LEVEL WITH NO CATALOG ENTRIES'
         EXIT  RC=4
ABEND20  EQU   *
         WTO   'SPVL   -  NO PARM SUPPLIED'
         EXIT  RC=20
         TITLE 'SPVL   -  IDCAMS INPUT JCL OVERIDE PARAMETERS'
OPTIONS  DC    H'0'
DNAMES   DC    H'48'
         DC    32X'00'
         DC    CL8'SYSIN'
         DC    CL8'SYSPRINT'
PAGENUM  DC    H'0',XL8'00'
IOLIST   DC    F'2'
         DC    A(DDN1)
         DC    A(EXPRTN)
         DC    A(0)
DDN1     DC    CL10'DDSYSPRINT'
         DC    A(DDN2)
         DC    A(CARDRTN)
         DC    A(0)
DDN2     DC    CL10'DDSYSIN'
IDCAMS   DC    F'0'
         ORG
         LTORG
         CNOP  0,4
         TITLE 'SPVL  -  INPUT CARD ROUTINE'
CARDRTN  EQU   *
         INIT  CARDRTN,12
         LR    R2,R1
         L     R3,4(R2)
         CLI   0(R3),0                 OPEN
         BE    CARDOPEN
         CLI   0(R3),4                 CLOSE
         BE    CARDCLOS
         CLI   0(R3),8                 GET
         BNE   ABEND16
         GET   SYSIN,CARD
         LA    R4,PARAM
         ST    R4,8(R2)                STORE BACK.
CARDEXIT EQU   *
         EXIT  RC=0
CARDOPEN EQU   *
         OPEN  SYSIN
         B     CARDEXIT
CARDCLOS EQU   *
         CLOSE SYSIN
         B     CARDEXIT
CARDEND  EQU   *
         EXIT  RC=4
ABEND16  EQU   *
         WTO   'SPVL    -  CARDRTN FAILURE'
         ABEND 16,DUMP
         DS    0F
PARAM    DC    A(CARD)
         DC    A(80)
CARD     DC    CL80' '
SYSIN    DCB   DSORG=PS,MACRF=GM,LRECL=80,BLKSIZE=400,DDNAME=SYSIN,    C
               RECFM=FB,EODAD=CARDEND
         LTORG
         CNOP  0,4
         TITLE 'SPVL    -  PUT PRINT CONTROL ROUTINE'
EXPRTN   EQU   *
         INIT  EXPRTN,12
         LR    R2,R1
         L     R3,4(R2)
         CLI   0(R3),0                 Q. OPEN REQD?
         BE    EXPOPEN                 YES GO OPEN.
         CLI   0(R3),4                 Q. CLOSE REQD?
         BE    EXPCLOSE                GO CLOSE.
         CLI   0(R3),8                 Q. GET REQD.
         BE    ABEND4                  SHOULDN'T HAPPEN.
         L     R3,8(R2)
         L     R6,0(R3)                POINT TO REC.
         L     R5,4(R3)                RECORD LENGTH.
         CLC   1(7,R6),=C'NONVSAM'     Q.IS IT NONVSAM.
         BE    NONVSAM                 YES BRANCH.
         CLC   1(4,R6),DATA            Q.IS IT DATA ENTRY.
         BE    PDATA                   GO PROCESS DATA ENTRY
         CLC   1(4,R6),GDG             Q.IS IT A GDG BASE
         BE    PGDG                    GO PROCESS GDG BASE.
         CLC   1(5,R6),INDEX           Q.IS IT INDEX ENTRY?
         BE    PINDEX                  GO PROCESS INDEX ENTRY.
         CLC   8(7,R6),CLUSTER         Q.IS IT A CLUSTER ASSOCIATION?
         BE    PASSOC                  GO PROCCESS.
         CLC   8(5,R6),=C'LIMIT'       Q.IS IT A LIMIT LINE?
         BE    PLIMIT                  SURE IS.
         CLC   37(6,R6),SPLITS         Q.IS IT A CI/CA SPLIT?
         BE    PSPLITS                 GO PROCESS SPLITS.
         CLC   37(7,R6),=C'FREESPC'    Q.IS IT FREESPACE LINE.
         BE    PFSPC1                  GO PROCESS.
         CLC   37(7,R6),=C'HI-ALLO'    Q.IS IT HIGH ALLOCATED.
         BE    PFSPC2                  GO PROCESS.
         CLC   8(6,R6),=C'VOLSER'      Q.IS IT VOLUME SERIAL?
         BE    VOLSER                  YES.
         CLC   37(8,R6),=C'CREATION'    Q.IS IT A CREATION DATE
         BE    CREATE                  YES.
         CLC   8(7,R6),=C'DEVTYPE'     Q.IS IT DEVTYPE FOR VSAM D/I
         BE    DEVTYP                  YES.
         CLC   6(5,R6),=C'ASSOC'       IS THIS AN ASSOCIATION
         BE    ASSOCI                  YES GO TO ASSOCIATION
         B     EXPRET
NONVSAM  EQU   *
         MVC   SVOL,=6C' '             RESET SVOL.
         MVI   IBYTE,X'80'             INDICATE NONVSAM
         MVC   ETYPE,=C'NVSAM'
COMMON   EQU   *
         SH    R5,=H'18'               SUBTRACT FOR DSNAME MOVE.
         STC   R5,MOVER+1              STORE LENGTH
MOVER    MVC   ENAME(1),17(R6)
         CLI   GDGSWI,X'10'            IS PREVIOUS ENTRY A GDG
         BNE   IBYTECHK                NO THEN CARRY ON
         L     R9,NINESAVE              LOAD LENGTH OF DSNAME
         STC   R9,COMP+1               STORE LENGTH TO BE COMPARED
COMP     CLC   SAVEGDG(1),17(R6)    IS THIS NONVSAM PART OF GDG
         BNE   SETPRT                 NOT-EQUAL SO GO     SET CARR/CON
         LA    R12,ENAME              LOAD ADDESS OF ENAME
         LH    R9,0(R9,R12)           LOAD LAST CHAR OF GDG NAME + 1
         STC   R9,FULLSTOP            STORE BITS 24 TO 31 IN FULLSTOP
         CLI   FULLSTOP,C'.'          IS IT A GDG DATASET
         BE    IBYTECHK               YES SO DO NOT SET CARR/CON
SETPRT   MVI   PRINAREA,C'-'           SKIP 3 LINES BETWEEN GDG,S
IBYTECHK CLI   IBYTE,X'10'              IS THIS A GDG ENTRY
         BNE   EXPRET                   NO THEN EXIT
         ST    R5,NINESAVE              STORE LENGTH OF GDG ENTRY
         MVI   GDGSWI,X'10'            SET GDG IND FOR SKIP PRINTING
         MVC   SAVEGDG(44),17(R6)      MOVE IN DS NAME FOR COMPARE
         B     EXPRET
PDATA    EQU   *
         MVC   VSAMVOL,=6C' '            CLEAR VSAM VOL
         MVC   SVOL,=6C' '             RESET SVOL.
         MVI   IBYTE,X'40'             SET DATA INDICATOR.
         MVC   ETYPE,DATA
         B     COMMON
PINDEX   EQU   *
         MVC   VSAMVOL,=6C' '            CLEAR VSAM VOL
         MVC   SVOL,=6C' '             RESET SVOL.
         MVI   IBYTE,X'20'             SET INDEX INDICATOR.
         MVC   ETYPE,INDEX
         B     COMMON
PASSOC   EQU   *
         TM    IBYTE,X'60'             Q.DATA OR INDEX PROCCESSING
         BZ    EXPRET
         SH    R5,=H'18'               SUBTRACT FOR DSNAME MOVE.
         STC   R5,MOVE1+1
MOVE1    MVC   CNAME(1),17(R6)
         B     EXPRET
PSPLITS  EQU   *
         CLI   45(R6),C'I'             Q.IS IT A CI SPLIT
         BE    PCISPLIT
*        MUST BE A CA SPLIT.
         CLC   58(3,R6),=C'--0'        Q.ARE THERE ANY?
         BE    EXPRET                  JUST RETURN.
         MVC   CAS,58(R6)              MOVE SA SPLIT TO PLINE.
         CLC   CAS(2),=C'--'
         BE    CPCAC2                  GO CLEAR TWO BYTES.
         CLI   CAS,C'-'
         BNE   *+8
         MVI   CAS,C' '
         B     EXPRET
CPCAC2   EQU   *
         MVC   CAS(2),=C'  '
         B     EXPRET
PCISPLIT EQU   *
*        THIS IS A CI SPLIT.
         CLC   58(3,R6),=C'--0'        Q.ARE THERE ANY?
         BE    EXPRET                  JUST RETURN.
         MVC   CIS,58(R6)              MOVE SA SPLIT TO PLINE.
         CLC   CIS(2),=C'--'
         BE    CPCIC2                  GO CLEAR TWO BYTES.
         CLI   CIS,C'-'
         BNE   *+8
         MVI   CIS,C' '
         B     EXPRET
CPCIC2   EQU   *
         MVC   CIS(2),=C'  '
         B     EXPRET
         SPACE 4
*
***      HERE WE HAVE A GDG BASE RECORD.
*
PGDG     EQU   *
         MVI   IBYTE,X'10'             SET GDG BASE INDICATOR.
         MVC   ETYPE,GDG               MOVE CONSTANT TO LINE.
         B     COMMON                  BRANCH TO COMMON
         SPACE 2
*
***      HERE WE HAVE A GDG LIMIT LINE.
*
PLIMIT   EQU   *
         MVC   LIMIT,29(R6)
         CLC   LIMIT(2),=C'--'
         BE    PLIMITC2
         CLI   LIMIT,C'-'
         BNE   PUT
         MVI   LIMIT,C' '
         B     PUT
PLIMITC2 EQU   *
         MVC   LIMIT(2),=C'  '
         B     PUT
         SPACE 4
*
***      CALCULATE THE FREESPACE PERCENTAGE.
*
PFSPC1   EQU   *
         CLI   IBYTE,X'40'             Q.IS IT DATA?
         BNE   EXPRET                  NO RETURN.
         MVC   FREESPC,51(R6)
         LA    R4,FREESPC
         LA    R7,10
         BAL   R14,CLEARCOM
         B     EXPRET
         SPACE 2
PFSPC2   EQU   *
         CLI   IBYTE,X'40'
         BNE   EXPRET
         MVC   HIALLOC,51(R6)
         LA    R4,HIALLOC
         LA    R7,10
         BAL   R14,CLEARCOM
         CLC   FREESPC(2),=C'  '       CHECK FREESPACE IN RANGE.
         BNE   FSPCERR
         PACK  DWORD1,HIALLOC
         PACK  DWORD2,FREESPC
         MP    DWORD2,=P'100'
         DP    DWORD2,DWORD1+2(6)
         UNPK  PERCENT,DWORD2(2)
         OI    PERCENT+2,X'F0'
         B     EXPRET
FSPCERR  EQU   *
         MVC   PERCENT,=C'ERR'
         B     EXPRET
         SPACE 4
CREATE   EQU   *
         MVC   CREDATE(6),55(R6)        MOVE IN CREATION DATE
         B     EXPRET                   EXIT
DEVTYP   EQU   *
         TM    IBYTE,X'60'            IS ENTRY PART OF DATA/INDEX
         BZ    EXPRET                    EXIT
         CLC   VSAMVOL,VOLUME           IS THIS THE SAME AS LAST
         BNE   MOVDATA                   NO
         MVC   DEVT,=8C' '               CLEAR DEVTYPE
         B     EXPRET                    EXIT
MOVDATA  MVC   DEVT(8),23(R6)           MOVE IN DEVICE TYPE
         MVC   VSAMVOL,VOLUME          MOVE VOLUME TO VSAMVOL
         B     PUT1                     GO AND PRINT
ASSOCI   EQU   *
         CLC   27(4,R6),=C'NULL'        IS ENTRY NULL
         BNE   EXPRET                   NO
         MVI   GDGSWI,X'00'            THIS IS A GDG ENTRY CLEAR SWITCH
         B     EXPRET                  EXIT
*
***      GENERAL REMOVAL OF - ROUTINE
*
CLEARCOM EQU   *
         CLI   0(R4),C'-'
         BNER  R14
         MVI   0(R4),C' '
         LA    R4,1(R4)
         BCT   R7,CLEARCOM
         BR    R14
         SPACE 4
VOLSER   EQU   *
         MVC   VOLUME,26(R6)             MOVE IN VOLSER
         CLI   VOLUME,C'-'               IS 1ST CHAR A '-'
         BNE   *+8                       NO
         MVI   VOLUME,C' '               SET '-' TO ' '
         CLI   IBYTE,X'80'               IS IT NONVSAM
         BNE   CARRYON                   NO THEN CARRY ON
         MVC   DEVT(8),52(R6)           MOVE IN DEVICE TYPE
CARRYON  CLC   SVOL,VOLUME             Q.IS THIS SAME AS LAST.
         BE    EXPRET
         MVC   SVOL,VOLUME             SAVE CURRENT VOLUME ID.
         CLI   IBYTE,X'80'
         BNE   PUT
         CLC   88(2,R6),=C'-0'
         BE    PUT
         MVC   FSEQN+1(2),88(R6)       MOVE IN FILE SEQUENCE NUMBER
         CLI   FSEQN+1,C'-'             IS FIRST POSITION = '-'
         BNE   *+8                       NO
         MVI   FSEQN+1,C' '             SET '-' TO BLANK
PUT      TM    IBYTE,X'60'              IS THIS A DATA/INDEX COMPONENT
         BM    EXPRET                   YES SO EXIT
PUT1     L     R8,LINECT               LOAD NUMBER OF LINES
         BAL   R11,OUTPUT              GO AND PRINT LINE
         LA    R9,55                   MAX NUMBER OF LINES
         CR    R9,R8                   HAVE WE PRINTED 57 LINES
         BH    NOHEAD                  NO HEADERS REQUIRED
         BAL   R10,EXPLAIN             GO AND PRINT EXPLANATIONS
         MVI   PRINAREA,C'1'        SKIP TO NEW PAGE
         MVC   PRINLINE,HEAD1               MOVE HEAD1 TO PRINT LINE
         BAL   R11,OUTPUT                   GO TO OUTPUT
         MVI   PRINAREA,C'0'        SKIP TWO LINES
         MVC   PRINLINE,HEAD2               MOVE HEAD2 TO PRINT LINE
         BAL   R11,OUTPUT                   GO TO OUTPUT
         MVI   PRINAREA,C' '        SKIP ONE LINES
         LA    R8,6
         ST    R8,LINECT             STORE LINE COUNT
NOHEAD   MVI   PRINAREA,C' '
         MVC   PRINAREA+1(132),PRINAREA
         B     EXPRET
         SPACE 4
EXPOPEN  EQU   *
         OPEN  (SYSPRINT,(OUTPUT))
         L     R8,LINECT
         MVC   PRINLINE,HEAD1               MOVE HEAD1 TO PRINT LINE
         BAL   R11,OUTPUT                   GO TO OUTPUT
         MVI   PRINAREA,C'0'        SKIP TWO LINES
         MVC   PRINLINE,HEAD2               MOVE HEAD2 TO PRINT LINE
         BAL   R11,OUTPUT                   GO TO OUTPUT
         MVI   PRINAREA,C' '                SKIP ONE LINE
         MVI   PRINLINE,C' '
         MVC   PRINLINE+1(131),PRINLINE
EXPRET   EQU   *
         EXIT  RC=0
EXPCLOSE EQU   *
         BAL   R11,OUTPUT
         BAL   R10,EXPLAIN             GO AND PRINT EXPLANATIONS
         CLOSE SYSPRINT
         B     EXPRET
         SPACE 4
*
***      CONSTRUCT A VBA OUTPUT RECORD.
*
OUTPUT   EQU   *
         LA    R4,PRINLINE+131         PT. TO END OF LINE
         LA    R5,132                  MAX LINE LENGTH.
OUTAG    EQU   *
         CLI   0(R4),C' '
         BNE   OUTNB
         BCTR  R4,R0
         BCT   R5,OUTAG
         LA    R5,5                    SET MIN LENGTH.
OUTNB    EQU   *
         LA    R5,5(R5)
         STH   R5,RDW
         PUT   SYSPRINT,RDW
         CLI   PRINAREA,C'-'            IS IT SPACE THREE LINES
         BNE   ADDONE                   NO JUST ADD ONE
         LA    R8,3(R8)                 ADD THREE TO LINE COUNT
         B     STORCNT                  BRANCH TO STORE LINE COUNT
ADDONE   LA    R8,1(R8)                ADD 1 TO LINE COUNT
STORCNT  ST    R8,LINECT               STORE VALUE FROM REG8
         BR    R11
EXPLAIN  EQU   *
         MVI   PRINAREA,C' '           SINGLE LINE SPACING
         MVC   PRINLINE,HEAD2          MOVE ONE LINE OF BLANKS
         BAL   R11,OUTPUT              AND PRINT
         MVC   PRINLINE,TOPHD          MOVE LINE OF *
         BAL   R11,OUTPUT              AND PRINT
         MVC   PRINLINE,EXPF           MOVE LINE OF EXPF
         BAL   R11,OUTPUT              AND PRINT
         MVC   PRINLINE,EXPL           MOVE LINE OF EXPF
         BAL   R11,OUTPUT              AND PRINT
         MVC   PRINLINE,EXPC           MOVE LINE OF EXPC
         BAL   R11,OUTPUT              AND PRINT
         MVC   PRINLINE,EXPFP          MOVE LINE OF EXPFP
         BAL   R11,OUTPUT              AND PRINT
         MVC   PRINLINE,TOPHD          MOVE LINE OF *
         BAL   R11,OUTPUT              AND PRINT
         BR    R10                     BRANCH BACK
RDW      DC    XL4'00000000'
PRINAREA DC    C'1'
PRINLINE DS    0CL132
ENAME    DC    CL44' '
         DC    CL2' '
VOLUME   DC    CL6' '
         DC    CL2' '
ETYPE    DC    CL5' '
         DC    CL2' '
CIS      DC    CL3' '
         DC    C' '
CAS      DC    CL3' '
         ORG   CIS
         DC    CL2' '
FSEQN    DC    CL3' '
         ORG   FSEQN
LIMIT    DC    CL3' '
         ORG
         DC    CL2' '
DEVT     DC    CL8' '
         DC    CL2' '
CREDATE  DC    CL6' '
         DC    CL7' '
CNAME    DC    CL33' '              ASSUM A CLUSTER NAME OF LT 33
         DC    CL2' '
PERCENT  DC    CL3' '
         DC    CL5' '                 SPARE SPACE
HEAD1    DC    CL132'       E N T R Y  N A M E                     VOLSC
               ER  ETYPE   F/L/C   DEVTYPE  CRE/DATE     C L U S T E R C
                 N A M E              FP'
HEAD2    DC    CL132' '
TOPHD    DC    CL132'                  ********************************C
               *******************************************************'
EXPF     DC    CL132'                  *     F - IS THE FILE SEQUENCE NC
               UMBER FOR NON VSAM TAPE DATASETS                      *'
EXPL     DC    CL132'                  *     L - IS THE LIMIT NUMBER FOC
               R GENERATION DATA GROUPS                              *'
EXPC     DC    CL132'                  *     C - IS THE CONTROL INTERVAC
               L/CONTROL AREA SPLITS FOR THE VSAM DATA COMPONENT     *'
EXPFP    DC    CL132'                  *     FP - IS THE PERCENTAGE OF C
               FREE SPACE IN THE VSAM DATA COMPONENT                 *'
LINECT   DC    F'6'
IBYTE    DC    X'00'
GDGSWI   DC    X'00'
NINESAVE DC    F'0'
SAVEGDG  DC    CL44' '
FULLSTOP DC    CL1' '
DATA     DC    CL5'DATA'
INDEX    DC    CL5'INDEX'
GDG      DC    CL5'GDG'
SPLITS   DC    C'SPLITS'
CLUSTER  DC    C'CLUSTER'
SVOL     DC    CL6' '
VSAMVOL  DC    CL6' '
FREESPC  DC    CL10' '
HIALLOC  DC    CL10' '
DWORD1   DC    D'0'
DWORD2   DC    D'0'
         LTORG
         CNOP  0,4
ABEND4   EQU   *
         WTO   'SPVL     -  GET FOR SYSPRINT?'
         ABEND 4,DUMP
         TITLE 'SPVL     -  DCB  AREA'
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=VBA,LRECL=137,  X
               BLKSIZE=3994
         END
