* THIS PROGRAM PRODUCED BY BRITISH STEEL CORPORATION CORBY
* REFER ANY PROBLEMS TO MALCOLM SISMEY TEL 053-66-2121 EXT 729
         TITLE 'BSC RQE MONITOR PROGRAM '-SYRQECNT- CAMPEN'
         SPACE 3
*       **********************************************************
*       * THIS PROGRAM MONITORS RQE USAGE AT SPECIFIED INTERVALS *
*       * EXEC HAS PARM=(IIIIIIII,NNNNNNNN)                      *
*       * WHERE IIIIIIII IS THE LENGTH OF THE INTERVAL REQUIRED  *
*       * FORMAT  HH MM SS T H ,                                 *
*       * AND   NNNNNNNN IS THE NUMBER OF EXAMINATIONS REQUIRED. *
*       *                                                        *
*       * THE PROGRAM PRINTS ONE LINE PER EXAMINATION WHICH LISTS*
*       * -- THE TOTAL NO. OF RQES -- THE NUMBER OF ACTIVE RQES  *
*       * -- AND A LIST OF THE ADDRESSES OF BUSY UNITS.          *
*       *                                                        *
*       * THE DDNAME OF THE OUTPUT FILE  IS  'PRINT'             *
*       *  NOTE IF MORE THAN 30 BUSY UNITS AT ONE TIME -WILL     *
*       *  OVERFLOW PRINT AREA                                   *
*       *                                                        *
*       *                                                        *
*       *                                                        *
*       *                                                        *
*       *                                                        *
*       *                                                        *
*       *  C.CAMPEN B.S.C. CORBY 19-9-72                         *
*       **********************************************************
         EJECT
*     *** REGISTER USAGE ***
*   REGISTER    USE
*     R0       OS
*     R1       OS
*     R2       NUMBER FROM PARM = NO OF EXAMINATIONS LEFT
*     R3       RQE TABLE POINTER
*     R4       STEPS UP RQE TABLE
*     R5       RQE COUNT
*     R6       ACTIVE COUNT
*     R7       PRINT AREA POINTER
*     R8       TEST NUMBER COUNTER
*     R9
*     R10      WORK REGISTER WITHIN ROUTINES - ALSO CONTAINS UCB ADDR
*     R10 CONT.  WHEN GOING TO PRINTDEV
*     R11      LINKAGE BETWEEN ROUTINES IN PROGRAM
*     R12      BASE
*     R13      OS
*     R14      OS
*     R15      OS
*
*
*
         EJECT
SYRQECNT BEGIN
         BAL   11,GETPARM          PARAMETERS
         BAL   11,GETRQE           SET UP RQE POINTER
         BAL   11,INITIAL          PRINT HEADING ETC
LOOP     LR    4,3                 SET POINTER TO START OF TABLE
         LA    5,0                 RQE COUNT
         LA    6,0                 ACTIVE COUNT
         LA    7,PAREA+35          NEXT UNIT ENTRY
LOOP1    CLC   0(2,4),LASTRQE      IS END OF ENTRIES
         BE    DROPOUT             YES- PRINT LINE ETC
         LA    5,1(5)              ADD 1 TO RQE COUNT
         CLI   4(4),X'FF'          IS RQE USED
         BE    NEXTRQE             NO TRY NEXT
         LA    6,1(6)              ADD 1 TO ACTIVE RQE COUNT
         C     6,F24               MORE THAN 24 ACTIVE RQES
         BH    AB2                 WILL OVERFLOW PAREA - ABEND
         LA    10,0               ZERO R10
         LH    10,2(4)             R10 = UCB ADDR
         BAL   11,PRINTDEV         PRINT DEVICE ROUTINE
NEXTRQE  LA    4,12(4)             STEP UP ONE 3 WORD RQE
         B     LOOP1               GO  BACK
         EJECT
** CONSTANTS AND WORK AREAS **
INTERVAL DS    D                   FROM
NUMBER   DS    D                   PARM ON EXEC CARD
H17      DC    H'17'               LENGTH OF PARM
ZEROS    DC    CL8'00000000'       USED IN CHECKING
NINES    DC    CL8'99999999'       PARMS
DWORK    DS    D                   WORK AREA
         DC    CL1' '              PRINT
PAREA    DS    CL133               AREA
SK1      DC    X'8B'               SKIP TO CH 1 NOW
SP1      DC    X'09'               WRITE THEN SPACE 1
SP3      DC    X'19'               WRITE THEN SPACE 3
BL2      DC    CL2'  '             TWO BLANKS
HEAD     DC    CL27'RQE MONITOR PROGRAM STARTED'
DATE     DC    CL4'DATE'
TIME     DC    CL4'TIME'
HEAD1    DC    CL40'PARMS ARE...INTERVAL =          NUMBER ='
HEAD2    DC    CL47'TEST NO. TOTAL RQES. ACTIVE RQES.    BUSY UNITS'
LASTRQE  DC    XL2'0001'           END OF RQE TABLE
F24      DC    F'24'               MORE THAN ENOUGH RQES
VERK     DS    CL20                WORK AREA
FOOT1    DC    CL27'RQE MONITOR PROGRAM ENDED  '
PRINT    DCB   DSORG=PS,RECFM=FBM,LRECL=133,DEVD=DA,MACRF=PM,          C
               DDNAME=PRINT
         EJECT
**  GET AND QUICKLY VET PARAMETERS  - ADDRESS IN REG1
**
**     PARM='IIIIIIII,NNNNNNNN'  (REG1 = LLIIIIIIII,NNNNNNNN)
GETPARM  EQU   *
         L     1,0(1)
         CLC   0(2,1),H17          PARM IS ALWAYS
         BE    LENOK               LENGTH 17
AB1      ABEND 1,DUMP              IF NOT ABEND
LENOK    CLI   10(1),X'6B'         IS COMMA PRESENT
         BNE   AB1                 NO ABEND
         CLC   2(8,1),ZEROS        ARE
         BL    AB1                 INTERVAL
         CLC   2(8,1),NINES        AND
         BH    AB1                 NUMBER
         CLC   11(8,1),ZEROS       BOTH
         BL    AB1                 NUMERIC
         CLC   11(8,1),NINES       IF YES  CONTINUE
         BH    AB1                 IF NOT  ABEND NO 1
*
         MVC   INTERVAL(8),2(1)    STORE
         MVC   NUMBER(8),11(1)     INTERVAL AND NUMBER
         PACK  DWORK,NUMBER        PACK NUMBER
         CVB   2,DWORK             BINARY IN R2
         BR    11                  RETURN
         EJECT
*        PROGRAM USER ABENDS AND MEANING
*
*     ABEND NO.               MEANING
*
*       1                INVALID PARAMETERS
*       2                MORE THAN 24 ACTIVE RQES-WILL OVFLOW PRINTAREA
*
AB2      EQU   *
         ABEND 2,DUMP
         EJECT
*   GET RQE POINTER
*   LOCN HEX 10 POINTS TO CVT . CVT PLUS HEX 78 POINTS TO REQUEST
*   ELEMENT TABLE
GETRQE   EQU   *
         L     10,16               CVT ADDRESS
         L     3,120(10)           RQE TABLE ADDRESS TO REG 3
         BR    11
         EJECT
**  INITIALIZATION ROUTINE
**  PRINT HEADINGS, ZEROISE FIELDS ETC.
**
INITIAL  EQU   *
         OPEN  (PRINT,(OUTPUT))
         MVC   PAREA(133),PAREA-1  BLANK OUT
         MVC   PAREA(1),SK1        SKIP TO
         PUT   PRINT,PAREA         CHANNEL ONE
         TIME  DEC                 GET DATE AND TIME
*  REG 0 HAS TIME, REG 1 HAS DATE
         ST    0,DWORK             STORE TIME
         MVI   DWORK+4,X'0C'       MOVE IN SIGN
         UNPK  PAREA+49(9),DWORK(5)     UNPACK
         MVC   PAREA+57(2),BL2     BLANK OUT EXTRA ZEROS
         ST    1,DWORK             STORE DATE
         UNPK  PAREA+36(5),DWORK+1(3)   UNPACK
         MVZ   PAREA+40(1),PAREA+39
         MVC   PAREA+1(27),HEAD    HEADING
         MVC   PAREA+31(4),DATE    CONSTANTS
         MVC   PAREA+44(4),TIME    TO PRINTAREA
         MVC   PAREA(1),SP3        SKIP CHAR
         PUT   PRINT,PAREA         PUT OUT
         MVC   PAREA(133),PAREA-1  BLANK OUT
         MVC   PAREA+1(40),HEAD1        SECOND
         MVC   PAREA+24(8),INTERVAL     HEADING
         MVC   PAREA+41(8),NUMBER       LINE
         MVC   PAREA(1),SP3             SPACE 3
         PUT   PRINT,PAREA              WRITE OUT
         MVC   PAREA(133),PAREA-1       BLANK OUT
         MVC   PAREA+1(47),HEAD2        HEADING
         MVC   PAREA(1),SP3             SPACE 3
         PUT   PRINT,PAREA              PUT OUT
*  ALL HEADINGS NOW PRINTED
         MVC   PAREA(133),PAREA-1       BLANK OUT
         LA    8,0                      ZEROISE TEST NUMBER
         BR    11
         EJECT
*  PRINTDEV -MOVE CURRENT DEVICE ADDRESS TO PRINTLINE
*  ON ENTRY   R10 POINTS AT CURRENT UCB
*             R7  POINTS AT AVAILABLE SPACE IN PRINT AREA
PRINTDEV EQU   *
         MVC   0(3,7),13(10)      UNIT NAME
         MVI   3(7),X'6B'          COMMA
         LA    7,4(7)              STEP UP AREA
         BR    11                  RETURN
         EJECT
**DROPOUT - ENTERED WHEN ALL RQES IN THE CHAIN HAVE BEEN EXAMINED
**   MOVE COUNTS TO PRINTLINE
**   PRINT THIS LINE - RESET PRINT POINTERS
**   ZEROISE AND INITIALISE COUNTERS
**   ISSUE STIMER IF NECCESSARY
**
DROPOUT  EQU   *
         LA    8,1(8)              ADD 1 TO TEST NUMBER
         CVD   8,DWORK             CONVERT TO DECIMAL
         UNPK  VERK(9),DWORK+3(5)  UNPACK
         MVZ   VERK+8(1),VERK+7    CLEAR ZONE
         MVC   PAREA+1(8),VERK+1   MOVE TO PRINT
         CVD   5,DWORK             TOTAL RQES
         UNPK  VERK(9),DWORK+3(5)  UNPACK
         MVZ   VERK+8(1),VERK+7    CLEAR
         MVC   PAREA+10(5),VERK+4  MOVE
         CVD   6,DWORK             ACTIVE COUNT
         UNPK  VERK(9),DWORK+3(5)  UNPACK
         MVZ   VERK+8(1),VERK+7    CLEAR
         MVC   PAREA+22(5),VERK+4  MOVE
         MVC   PAREA(1),SP1        CONTROL SPACE 1
         PUT   PRINT,PAREA         PUT OUT
         LA    5,0                 TOTAL COUNT
         LA    6,0                 ACTIVE COUNT
         MVC   PAREA(133),PAREA-1  BLANK OUT
         BCT   2,WAIT              TAKE ONE OFF NUMBER
         B     FINAL              FINISH WHEN ZERO
*   WHEN ZERO PRINT FINAL MESSAGE AND END
*   OTHERWISE ISSUE STIMER AND CONTINUE
WAIT     STIMER WAIT,DINTVL=INTERVAL
*   COME BACK INTO CONTROL AND CONTINUE
         B     LOOP
         EJECT
*  FINAL - PRINT OUT ENDED MESSAGE CLOSE FILES ETC
*
*
FINAL    EQU   *
         TIME  DEC
*  R0 HAS TIME R1 HAS DATE
         ST    0,DWORK             STORE TIME
         MVI   DWORK+4,X'0C'       MOVE IN SIGN
         UNPK  PAREA+49(9),DWORK(5)     UNPACK
         MVC   PAREA+57(2),BL2          BLANK
         ST    1,DWORK             DATE
         UNPK  PAREA+36(5),DWORK+1(3)   UNPACK
         MVZ   PAREA+40(1),PAREA+39
         MVC   PAREA+1(27),FOOT1        FOOTING
         MVC   PAREA+31(4),DATE         CONST-
         MVC   PAREA+44(4),TIME    ANTS
         MVC   PAREA(1),SP1        SPACE 1 LINE
         PUT   PRINT,PAREA         PUT OUT
         SPACE 3
         SPACE 3
*  HAVE FINISHED TASK NOW
         SPACE 3
         SPACE 3
         CLOSE  (PRINT)
         L     13,4(13)            RESET SAVE POINTER
         RETURN (14,12)            GO BACK
         LTORG *
         END   SYRQECNT
