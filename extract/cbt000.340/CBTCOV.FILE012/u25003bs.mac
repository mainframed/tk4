         MACRO
&NAME    BEGIN   &BASE=1,&PL1=NO,&ENTRY=,&REGS=R01
         GBLC  &REG00,&REG01,&REG09     REFERENCED IN 'BACK' MACRO
         GBLB  &FIRST                   FOR REG EQU GEN.
         GBLB  &BGEXTRN,&BGPL1SW
         LCLC  &PENTRY
         LCLA  &AWORK1,&AWORK2,&AWORK3
*BEGIN    V2                                                  19.4.73
*        GENERATE CSECT INITIALISATION CODING INCLUDING PL1
*        COMPATIBILITY IF REQUIRED
&NAME    CSECT
&BGPL1SW SETB  0                        SET  OFF PL1 SW
         AIF   ('&PL1' NE 'YES').NOPL1
&BGPL1SW SETB  1
.NOPL1   ANOP
         AIF   (K'&ENTRY EQ 0).NOENTRY
         ENTRY &ENTRY
&ENTRY   DS    0H
&PENTRY  SETC  '&ENTRY'
         AGO   .GENREGS
.NOENTRY ANOP
         AIF   (&BGPL1SW NE 1).GENREGS
         AIF   (K'&NAME LE 7).GENREGS
.* TRUNCATE 8 CHAR CSECT NAME  TO 7 CHAR PL1 ENTRY POINT
&PENTRY  SETC  '&NAME'(1,4).'&NAME'(6,3)
         ENTRY &PENTRY
&PENTRY  DS    0H   PL1 ENTRY - 1ST 4 AND LAST 3 CHARS OF CSECT NAME
.GENREGS ANOP
         AIF   (&FIRST).NORGEN
&FIRST   SETB  1
         SPACE 1
         AIF   ('&REGS' NE 'R01').GENREG1
R00      EQU   0                        ) I/0 AND
R01      EQU   1                        ) PARAMETERS
R02      EQU   2
R03      EQU   3
R04      EQU   4
R05      EQU   5
R06      EQU   6
R07      EQU   7
R08      EQU   8
R09      EQU   9                        USED BY PL1
&REG00   SETC  'R00'
&REG09   SETC  'R09'
&REG01   SETC  'R01'
         AGO   .GENREG2
.GENREG1 ANOP
R0       EQU   0                        ) I/0 AND
R1       EQU   1                        ) PARAMETERS
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9                        USED BY PL1
&REG00   SETC  'R0'
&REG09   SETC  'R9'
&REG01   SETC  'R1'
.GENREG2 ANOP
R10      EQU   10                       )      ) PL1= BASE REG
R11      EQU   11                       ) BASE ) PL1=A(STATIC STORAGE)
R12      EQU   12                       ) REGS ) PL1= PSUEDO REG VECTOR
R13      EQU   13                       POINTER TO REG SAVE AREA
R14      EQU   14                       RETURN ADDR
R15      EQU   15                       BRANCHING ADDR
.NORGEN  ANOP
         SPACE 1
         CNOP  2,4
         BALR  R15,&REG00
         USING *,R15
         AIF   (&BGPL1SW EQ 0).ASAVE
.* GENERATE PL1 COMPATIBLE SAVE ROUTINE
         B     *+20
         AIF   (K'&NAME GT 7).PSAVE05
         DC    AL1(7)
         DC    CL7'&NAME'
         AGO   .PSAVE10
.PSAVE05 ANOP
         DC    AL1(7)
         DC    CL7'&PENTRY'
.PSAVE10 ANOP
         DC    F'136'                   DSA LENGTH
         DC    A(*+148)                 ADDR OF STATIC STORAGE
         CLC   *+176(4),*+196           IS PLI CNTRL MOD IN CORE
         BE    *+42                     NO THEN USE ASS SAVE ROUTINE
         SPACE 1
* PL1 SAVE ROUTINE
         STM   R14,R11,12(R13)          SAVE REGS
         L     R11,&NAME+16             GET ADDR OF STATIC STORAGE
         L     &REG00,&NAME+12          GET DSA LENGTH
         L     R15,32(0,R11)            GET IHESADA ADDR
         BALR  R14,R15                  LINK TO SET UP PL1 ENVIRONMENT
         LA    &REG09,100(0,R13)        USED BY PL1
         ST    R13,0(R12,0)             STORE DSA ADDR IN PR VECTOR
         MVI   0(R13),X'80'             SET DSA FLAG BYTE
         DC    X'0700'                  PADDING BYTES
         BALR  R15,&REG00
         B     156(R15)                 GO TO SET BASE REGS
         SPACE 1
.ASAVE   ANOP
* ASSEMBLER SAVE ROUTINE
         STM   R14,R12,12(R13)          SAVE REGS
         ST    R13,*+24                 CHAIN LAST S/A TO THIS
         LR    R14,R13
         LA    R13,*+14                 POINT R13 TO SAVEAREA
         ST    R13,8(R14)               CHAIN THIS S/A TO LAST
         AIF   (&BGPL1SW EQ 0).ASAVE05
         B     *+134                    GO TO SET BASE REGS
         AGO   .ASAVE10
.ASAVE05 ANOP
         B     *+78                     GO TO SET BASE REGS
.ASAVE10 ANOP
         DC    X'0700'                  PADDING BYTES
         DC    18F'0'                   SAVEAREA
         AIF   (&BGPL1SW EQ 0).SETBASE
         SPACE 1
         DC    F'4096'                  PL1 STATIC STORAGE
         DC    A(*-4+1024)                FIXED AREAS
         DC    A(*-8+2048)
         DC    A(*-12+3072)
         DC    A(*-16+4096)
         DC    A(*-20+5120)
         DC    A(*-24+6144)
         DC    A(*-28+7168)
         AIF   (&BGEXTRN).NOEXTRN
         EXTRN IHESADA,IHESADB,IHESAFA,IHESARC
.NOEXTRN ANOP
&BGEXTRN SETB  1
         DC    A(IHESADA)
         DC    A(IHESADB)
         DC    F'0'
         DC    F'0'
         DC    A(IHESAFA)
         DC    F'0'
.SETBASE ANOP
         SPACE 1
         DROP  R15
         AIF   ('&BASE'(1,1) NE '(').BASE05
.* GENERATE USING'S FOR EACH BASE SPECIFIED
&AWORK1  SETA  0
&AWORK2  SETA  0
&AWORK3  SETA  1
         BALR  &BASE(&AWORK3),&REG00    SET UP BASE REGISTER
.ALOOP   ANOP
         USING *-&AWORK1+&AWORK2,&BASE(&AWORK3)
&AWORK3  SETA  &AWORK3+1
         AIF   (K'&BASE(&AWORK3) EQ 0).AEND  EXIT IF END OF SUB-LIST
&AWORK1  SETA  &AWORK1+8
&AWORK2  SETA  &AWORK2+4096
         LA    &BASE(&AWORK3),2048(&BASE(&AWORK3-1)) SET UP NEXT
         LA    &BASE(&AWORK3),2048(&BASE(&AWORK3))     BASE REGISTER
         AGO   .ALOOP
.BASE05  ANOP
         AIF   (&BGPL1SW EQ 0).BASE10
         BALR  R10,&REG00               SET UP BASE REGISTER
         USING *,R10
         AIF   ('&BASE' EQ '1').AEND
         LA    R11,2048(R10)            SET UP NEXT
         LA    R11,2048(R11)              BASE REGISTER
         USING *-8+4096,R11
         AGO   .AEND
.BASE10  ANOP
         BALR  R12,&REG00               SET UP BASE REGISTER
         USING *,R12
         AIF   ('&BASE' EQ '1').AEND
         LA    R11,2048(R12)            SET UP NEXT
         LA    R11,2048(R11)              BASE REGISTER
         USING *-8+4096,R11
         AIF   ('&BASE' EQ '2').AEND
         LA    R10,2048(R11)            SET UP NEXT
         LA    R10,2048(R10)              BASE REGISTER
         USING *-16+8192,R10
.AEND    ANOP
         MEND
         MACRO
&NAME    BACK   &RC=0
*BACK     V2                                                  19.4.73
*        USE ONLY IN CONJUNCTION WITH 'BEGIN'
*        PROVIDES PL1 COMPATIBILITY IF REQUIRED
         GBLC  &REG00,&REG01            SET IN 'BEGIN' MACRO
         GBLB  &BGPL1SW
         LCLA  &AWORK1
         LCLC  &CWORK1
&NAME    DS    0H
         AIF   ('&RC'(1,1) EQ '(').REGCODE
         LA    R15,&RC                  SET RETURN CODE
         AGO   .ABACK05
.REGCODE ANOP
         AIF   ('&RC' EQ '(15)').ABACK05
         AIF   ('&RC' EQ '(R15)').ABACK05
&AWORK1  SETA  K'&RC
&AWORK1  SETA  &AWORK1-2
&CWORK1  SETC  '&RC'(2,&AWORK1)
         LR    R15,&CWORK1              SET RETURN CODE
.ABACK05 ANOP
         AIF   (&BGPL1SW EQ 0).ABACK10       DID BEGIN HAVE PL1 COMPAT.
         CNOP  0,4                      ALIGN'A'TYPE
         B     *+12
         DC    A(IHESADA)               BEGIN HAS EXTRN
         DC    F'0'
         CLC   *-8(4),*-4               IS PL1 IN CORE
         BE    *+56                     NO THEN USE ASS RETURN
         LTR   R15,R15                  IS A RETCODE SET
         BZ    *+44                       NO
         ST    R15,*+28                 YES THEN SET CODE
         OI    *+20,X'80'                 AND LINK
         LA    &REG01,*+16                TO IHESARC TO SET
         L     R15,*+20                   RETURN
         BALR  R14,R15                    CODE
         B     *+22                     BRANCH ROUND
         DC    X'0700'                    PADDING BYTES AND
         DC    A(*+4)                     PARM LIST
         DC    F'0'                     RETURN CODE
         DC    A(IHESARC)
         DC    A(IHESAFA)
         L     R15,*-4                  GET ADDR OF IHESAFA
         BALR  R14,R15                  AND RETURN
.ABACK10 ANOP
* ASSEMBLER STANDARD RETURN
         L     R13,4(R13)               GET ADDR OF LAST S/A
         L     R14,12(R13)              GET RETURN ADDR
         LM    &REG00,R12,20(R13)       RESTORE REGS
         BR    R14                      RETURN
         MEND
         TITLE 'EJECT - ENABLE JOB ENTRY BY CONSOLE TYPEWRITER'
*
*                -- E -- J -- E -- C -- T --
*
*
*
*        THE PURPOSE OF EJECT IS TO ENABLE THE OPERATOR TO ENTER JOBS
*              INTO THE SYSTEM EVEN THOUGH THE CARD READER IS DOWN OR
*              OTHERWISE OCCUPIED
*
*        THIS IS ACHIEVED BY ALLOCATING A DISK DATA-SET WHICH IS UP-
*              DATED WITH CARD-IMAGES VIA THE CONSOLE TYPEWRITER
*
*        THERE ARE THREE FUNCTIONS - LIST, RECREATE AND CHANGE
*
*              LIST WILL CAUSE A LISTING ON THE LOG OF THE EXISTING
*              CARD-IMAGE DATA-SET.
*
*              RECREATE WILL ALLOW THE OPERATOR TO ENTER CARD-IMAGES
*              WHICH WILL THEN BE WRITTEN OUT TO THE DATA-SET.
*
*              CHANGE WILL ALLOW THE OPERATOR TO MODIFY INDIVIDUAL
*              CARD-IMAGES ON THE DATA-SET
*
*        A SPECIAL READER PROCEDURE - RDREJECT - MUST BE PROVIDED TO
*              ACCESS THE DATA-SET
*
         PRINT NOGEN
         EJECT
EJECT    BEGIN
*
FUNCTION MVC   REPLY,SPACES
         XC    CNSLECB,CNSLECB
         WTOR  'EJECT --- ENTER FUNCTION - LIST, RECREATE, CHANGE OR ENX
               D',REPLY,8,CNSLECB
         WAIT  ECB=CNSLECB
         OC    REPLY,SPACES
         CLC   REPLY(2),=C'L '     TEST FOR ABBREVIATIONS
         BE    LISTRTNE
         CLC   REPLY(2),=C'R '
         BE    RECREATE
         CLC   REPLY(2),=C'C '
         BE    CHANGER
         CLC   REPLY(2),=C'E '
         BE    ENDING
         CLC   REPLY(5),=C'LIST '  TEST FOR FULL KEYWORDS
         BE    LISTRTNE
         CLC   REPLY(8),=C'RECREATE'
         BE    RECREATE
         CLC   REPLY(7),=C'CHANGE '
         BE    CHANGER
         CLC   REPLY(4),=C'END '
         BE    ENDING
         TM    SWITCHES,BADFUNC    WRONG - TEST IF SECOND ERROR
         BO    NAUGHTY             YES
         OI    SWITCHES,BADFUNC    SET SWITCH & WARN HIM
         WTO   'EJECT --- TUT TUT, FINGERS! - TRY AGAIN'
         B     FUNCTION
*                        LOUSY OPERATOR - PACK UP AND GO HOME
NAUGHTY  WTO   'EJECT --- IF YOU CAN''T TYPE, I''M NOT PLAYING!'
*                        END OF JOB MESSAGE
ENDING   WTO   'EJECT --- CLOSING DOWN'
         BACK
*
REPLY    DC    CL8' '
SPACES   DC    CL8' '
CNSLECB  DC    F'0'
SWITCHES DC    X'00'     BITS 0-1  RESERVED
PUTEX    EQU   32        BIT    2  LIST CARD BEFORE PUTX
CHANGE   EQU   16        BIT    3  RECORD CHANGE IN PROGRESS
*        EQU   8         BIT    4  RESERVED
LISTED   EQU   4         BIT    5  DATA-SET HAS BEEN LISTED
LASTCARD EQU   2         BIT    6  EOF ON DATA-SET I/P
BADFUNC  EQU   1         BIT    7  FUNCTION KEYING ERROR
*
         EJECT
*
*  ROUTINE TO LIST OUT EXISTING DATA-SET ON CONSOLE
*
         SPACE 3
LISTRTNE OPEN  (CARDS,(INPUT))     OPEN DATA-SET AS I/P ONLY
         WTO   'EJECT --- LISTING OF EXISTING DATA-SET'
         ZAP   COUNTER,=P'0'
*
READIN   GET   CARDS
         MVC   INLINE(80),0(R01)
         AP    COUNTER,=P'1'
*
WTOCARD  MVC   WTO+8(72),INLINE    MOVE IMAGE TO MESSAGE AREA
         LA    R02,72
         LA    R03,INLINE+71       TRUNCATE TRAILING
LOOP     CLI   0(R03),X'40'          BLANKS AND
         BNE   NOSPA                   TEST FOR ANY
         BCTR  R03,0                     BLANK CARDS
         BCT   R02,LOOP
         MVC   WTO+8(23),=C'     *** BLANK CARD ***'
         MVC   WTO+4(2),=H'27'
*
WTO      WTO   '72 BYTES MESSAGE AREA,WHICH WILL BE OVERLAID BY THE    X
               CARD IMAGE            '
         TM    SWITCHES,PUTEX
         BO    GETREPL
         B     READIN
*
NOSPA    LA    R02,4(R02)
         STH   R02,WTO+4
         B     WTO
*                                  END OF I/P DATA-SET
EOFCARDS CLOSE (CARDS)
         NI    SWITCHES,255-BADFUNC SET OFF SWITCH
         OI    SWITCHES,LISTED     DATASET HAS BEEN LISTED
         TM    SWITCHES,CHANGE
         BO    OK
         B     FUNCTION            GET NEXT FUNCTION
*
INLINE   DC    CL80' '             CARD-IMAGE WORKAREA
COUNTER  DC    PL2'0'              COUNT OF RECORDS ON DATA-SET
*
         EJECT
*
*  ROUTINE TO RECREATE CARD-IMAGE DATA-SET
*
         SPACE 3
RECREATE OPEN  (CARDS,(OUTPUT))    OPEN DATA-SET AS O/P
*
         PUT   CARDS               DUMMY TO PRIME REG 1
         ST    R01,R1STRE          SAVE IT DURING WTOR & WAIT
READCNSL XC    CNSLECB,CNSLECB     ZEROISE ECB
         MVI   INLINE,X'40'        BLANK FIRST
         MVC   INLINE+1(79),INLINE THEN PROPAGATE
         MVC   OUTLINE,INLINE      BLANK OUTPUT AREA
         WTOR  'EJECT --- ENTER CARD-IMAGE OR END',OUTLINE,80,CNSLECB
         WAIT  ECB=CNSLECB
         L     R01,R1STRE          RESTORE REG 1 FOR PUT-LOCATE
         OC    OUTLINE,INLINE      CONVERT REPLY TO UPPER CASE
         CLC   OUTLINE(4),=C'END ' IS I/P COMPLETE
         BE    LASTONE             YES - PUT OUT NULL
*
PUTOUT   MVC   0(80,R01),OUTLINE
         PUT   CARDS
         ST    R01,R1STRE
         TM    SWITCHES,LASTCARD   IS IT END OF I/P
         BNO   READCNSL            NO - TRY AGAIN
         NI    SWITCHES,255-(LASTCARD+BADFUNC)
         CLOSE (CARDS)             CLOSE DOWN
         B     FUNCTION            GET NEXT FUNCTION
*
LASTONE  MVC   OUTLINE,=C'// '     MAKE NULL
         MVC   OUTLINE+3(77),OUTLINE+2 PROPAGATE BLANK
         OI    SWITCHES,LASTCARD   SET END OF I/P INDICATOR
         B     PUTOUT
*
OUTLINE  DC    CL80' '             OUTPUT AREA
R1STRE   DS    F
*
         EJECT
*
*  ROUTINE TO ALLOW THE CHANGING OF INDIVIDUAL RECORDS
*
         SPACE 3
CHANGER  OI    SWITCHES,CHANGE
         TM    SWITCHES,LISTED
         BO    OK
         MVC   REPLY,SPACES
WTOLST   XC    CNSLECB,CNSLECB
         WTOR  'EJECT --- IS DATA-SET TO BE LISTED? - REPLY YES/NO',   X
               REPLY,3,CNSLECB
         WAIT  ECB=CNSLECB
         OC    REPLY,SPACES
         CLC   REPLY(4),=C'YES '
         BE    LISTRTNE
         CLC   REPLY(3),=C'NO '
         BNE   WTOLST
*
OK       OPEN  (CARDS,(UPDAT))
         MVC   REPLY,SPACES
         TM    SWITCHES,LISTED
         BNO   WTOCH
         MVC   WTOCZ+50(4),=X'40202120'
         ED    WTOCZ+50(4),COUNTER
*
WTOCH    XC    CNSLECB,CNSLECB
WTOCZ    WTOR  'EJECT --- ENTER CHANGE CARD NO.1 - 999 OR END',REPLY,  X
               3,CNSLECB
         WAIT  ECB=CNSLECB
         OC    REPLY,SPACES
         CLC   REPLY(2),=C'E '
         BE    NOMORE
         CLC   REPLY(4),=C'END '
         BE    NOMORE
         MVZ   ZERO1(3),REPLY
         CLC   ZERO1(3),ZERO2
         BNE   WTOCH
         PACK  DWORD(8),REPLY(3)
         CVB   R04,DWORD
*
GETIN    GET   CARDS                                            * 1ST *
         MVC   INLINE(80),0(R01)                                * 2ND *
         ST    R01,R1STRE          SAVE REG 1 DURING LOG I/O    * 3RD *
         BCT   R04,GETIN
         OI    SWITCHES,PUTEX
         B     WTOCARD
*
GETREPL  MVI   INLINE,X'40'
         MVC   INLINE+1(79),INLINE
         XC    CNSLECB,CNSLECB
         WTOR  'EJECT --- ENTER NEW CARD-IMAGE OR RETRY',INLINE,80,    X
               CNSLECB
         WAIT  ECB=CNSLECB                                     * 1ST *
         L     R01,R1STRE          RESTORE REG 1 FOR PUTX       * 2ND *
         MVI   OUTLINE,X'40'
         MVC   OUTLINE+1(79),OUTLINE
         OC    INLINE,OUTLINE
         CLC   INLINE(6),=C'RETRY '
         BE    CLOSEUP
         CLC   INLINE(2),=C'R '
         BE    CLOSEUP
         MVC   0(80,R01),INLINE                                 * 1ST *
         PUTX  CARDS                                            * 2ND *
*
CLOSEUP  CLOSE (CARDS)
         B     OK
*
NOMORE   NI    SWITCHES,255-(CHANGE+LISTED+PUTEX)
         B     FUNCTION
*
ZERO1    DC    C'000'
ZERO2    DC    C'000'
DWORD    DS    D
*
         EJECT
*
*  DCB FOR CARD-IMAGE DATA-SET
*
*        SUPPORTS GET-LOCATE (LIST FUNCTION AND CHANGE FUNCTION)
*                 PUT-LOCATE (RECREATE FUNCTION)
*                 PUTX       (CHANGE FUNCTION)
*
         SPACE 3
CARDS    DCB   BUFNO=1,                                                X
               DSORG=PS,                                               X
               MACRF=(GL,PL),                                          X
               DDNAME=CARDS,                                           X
               EODAD=EOFCARDS
*
         END
