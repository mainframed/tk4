*        GENERAL DESCRIPTION
*        THE UT17 PROGRAM PRODUCES EDITED LISTINGS FROM CARD INPUT.
*        JCL TO RUN UT17
*        // EXEC PGM=UT17
*        //SYSPRINT DD SYSOUT=A    PRINTED OUTPUT
*        //SYSUT1   DD *           INPUT SOURCE CARD FILE
*          INPUT SOURCE CARDS
*        //TITLES   DD *           REQUIRED IF TITLE CARDS SUBMITTED.
*          TITLE CARDS (OPTIONAL)  WHEN SUBMITTED THESE CARDS RESTRICT
*                                  PRINTING TO PAGES WITH THE SPECIFIED
*                                  TITLES.
*        INPUT SOURCE CARDS (SYSUT1 FILE)
*        CHARACTERS TO CONTROL LINE SPACING ARE PUNCHED IN COLUMN 8 OF
*        EACH CARD.
*            T = TITLE CARD.
*                THIS CARD IS PRINTED AT THE TOP OF EVERY PAGE UNTIL
*                ANOTHER TITLE CARD IS ENCOUNTERED.
*            + = SKIP TO TOP OF PAGE.
*                NO DATA SHOULD BE INCLUDED FOR PRINTING ON THIS CARD.
*                THE UTILITY PROGRAM AUTOMATICALLY SKIPS TO A NEW PAGE
*                EVERY 60 LINES.
*            1 = SPACE 1 LINE BEFORE PRINTING DATA
*            2 = SPACE 2 LINES BEFORE PRINTING DATA
*            3 = SPACE 3 LINES BEFORE PRINTING DATA
*                A BLANK IN COLUMN 8 IS EQUIVALENT TO 1.
*        DATA TO BE PRINTED IS PUNCHED IN COLUMNS 10 - 71 INCLUSIVE.
*        IF COLUMN 72 IS NOT BLANK THE UT17 UTILITY AUTOMATICALLY
*        REPOSITIONS THE DATA SO THAT THERE ARE NO SPACES AT THE RIGHT
*        HAND END OF THE LINE, BY INCREASING THE NUMBER OF SPACES
*        BETWEEN INDIVIDUAL WORDS. IF THE LAST CHARACTER OF DATA TO BE
*        PRINTED IS A FULLSTOP, PADDING DOES NOT OCCUR.
*        TITLE CARDS
*        THIS FILE IS ONLY REQUIRED WHEN YOU WISH TO RESTRICT PRINTING
*        OF THE SYSUT1 SOURCE CARD FILE TO PAGES WITH SPECIFIED TITLES.
*        FORMAT:
*        COLS 10-71 - TITLE OF PAGES TO BE PRINTED (UNDERLINED BY
*                    HYPHENS IN UT17 PRINT, AND THE SAME COLUMNS AS
*                    THE 'T' SOURCE CARD)
*        OTHER COLS ARE IGNORED
*        A MAXIMUM OF 50 TITLE CARDS MAY BE SUBMITTED
         TITLE 'UT17     TEXT  PRINT  UTILITY     BY G M DEARLOVE'
UT17     IN
         OPEN  (IN,(INPUT),OUT,(OUTPUT))     OPEN FILES
         OPEN  (PARMDCB,(INPUT))
         TM    PARMDCB+48,X'10'    SUCCESSFUL OPEN?
         BZ    B01                 NO, BUT VALID
         LA    2,PARMLIST          R2 = ADDR OF PARMLIST
A01      EQU   *
         GET   PARMDCB             READ PARM CARD
         CL    2,=A(PARMEND)       SPACE IN TABLE?
         BNL   A05                 NO
         NI    B01BRNCH+1,X'0F'    SET BRANCH TO NOP SO SELECTION MADE
         MVC   0(62,2),9(1)        STORE PARM
         LA    2,62(2)             NEXT POSN IN TABLE
         B     A01                 READ NEXT CONTROL CARD
A05      EQU   *
         WTO   'UT17, ONLY 50 CONTROL CARDS WILL BE ACTIONED'
A10      EQU   *
         MVI   0(2),X'FF'          END OF PARM IND
         CLOSE PARMDCB
B01      EQU   *
         GET   IN                  GET NEXT RECORD
         LR    6,1                 SAVE POINTER
         CLI   7(6),C'T'           TEST TITLE CARD
         BNE   B04                 NO
         MVC   HEADING(62),9(6)    SAVE HEADING
B01BRNCH BC    15,B03              SKIP SELECTION (THIS INSTR IS SET TO
*                                    NOP IF SELECTION CARDS SUPPLIED)
         LA    2,PARMLIST          R2 = ADDR OF PARMLIST
B02      EQU   *
         CLC   0(62,2),9(6)        REQUIRED HEADING?
         BE    B03                 YES
         LA    2,62(2)             NEXT PARM
         CLI   0(2),X'FF'          END OF PARMS?
         BNE   B02                 NO
*  THIS TITLE NOT WANTED
         OI    B04BRNCH+1,X'F0'    SET BRANCH SO CARDS FOR THIS TITLE
*                                    ARE IGNORED
         B     B01                 READ NEXT CARD
B03      EQU   *
         NI    B04BRNCH+1,X'0F'    SET BRANCH TO NOP SO CARDS FOR THIS
*                                    TITLE ACCEPTED
         BAL   7,S01               PRINT HEADING
         B     B01                 READ NEXT CARD
B04      EQU   *
B04BRNCH BC    0,B01               SET TO ALWAYS BRANCH TO READ NEXT
*                                    CARD WHEN TITLE NOT REQUIRED
         CLI   7(6),X'4E'          TEST NEW PAGE REQUEST
         BNE   B05                 NO
         BAL   7,S01               PRINT HEADING
         B     B01                 READ NEXT CARD
B05      EQU   *
         MVI   CONTROL,X'40'       ASSUME SINGLE LINE FEED
         AP    COUNT,=P'1'         ADD 1 TO LINE COUNT
         CLI   7(6),X'F0'          TEST IF NO LINE ADVANCE REQUIRED
         BNE   *+14                NO
         MVI   CONTROL,X'4E'       + NO ADVANCE
         SP    COUNT,=P'1'         REDUCE LINE COUNT
         CLI   7(6),X'F1'          TEST IF SINGLE LINE FEED REQD
         BNE   *+8                 NO
         MVI   CONTROL,X'40'       B SINGLE LINE FEED
         CLI   7(6),X'F2'          TEST IF DOUBLE LINE FEED REQD
         BNE   *+14                NO
         MVI   CONTROL,X'F0'       0 DOUBLE LINE FEED
         AP    COUNT,=P'1'         ADD 1 TO LINE COUNT
         CLI   7(6),X'F3'          TEST IF TREBLE LINE FEED REQD
         BNE   *+14                NO
         MVI   CONTROL,X'60'       - TREBLE LINE FEED
         AP    COUNT,=P'2'         ADD 2 TO LINE COUNT
         CP    COUNT,LIMIT         TEST IF NEW PAGE REQD
         BL    B06                 NO
         BAL   7,S01               PRINT HEADINGS
         B     B05                 RETURN TO ACCUMULATE TO COUNT AGAIN
B06      EQU   *
         CLI   71(6),X'40'         TEST PADDING REQD
         BE    B11                 NO
         LA    RF,70(6)            LOAD 'FROM' ADDRESS
         LA    RT,70(6)            LOAD 'TO'   ADDRESS
         LA    RS,9(6)             START POSITION
B07      EQU   *
         CLI   0(RF),X'40'         TEST BLANK
         BNE   B08                 NO
         CR    RS,RF               TEST IF 'FROM' POSITION IS COL 1
         BE    B11                 NO PADDING (BLANK CARD)
         BCTR  RF,0                YES (REDUCE 'FROM' BY 1)
         B     B07
B08      EQU   *
         CLI   0(RF),C'.'          TEST FULL STOP
         BE    B11                 YES  (DO NOT PADD)
B09      EQU   *
         CR    RF,RT               TEST IF FROM & TO EQUAL
         BE    B11                 NO MORE PADDING
         MVC   0(1,RT),0(RF)       MOVE CHARACTER
         CR    RS,RF               TEST IF COL 1 HAS BEEN COPIED
         BE    B11                 YES (NO MORE PADDING)
         CLI   0(RF),X'40'         TEST IF IT WAS A SPACE
         BNE   B10                 NO
         BCTR  RT,0                REDUCE 'TO' POSITION
         MVI   0(RT),X'40'         MOVE IN AN EXTRA SPACE
B10      EQU   *
         BCTR  RF,0                REDUCE 'FROM' POSITION
         BCTR  RT,0                REDUCE 'TO'   POSITION
         B     B09                 CONTINUE PADDING
B11      EQU   *
         MVC   DETAIL,9(6)         MOVE DETAIL FOR PRINTING
         PUT   OUT,DATALINE        PUT OUT LINE
         B     B01                 READ NEXT CARD
EOD      EQU   *
         CLOSE (IN)                CLOSE INPUT FILE
         CLOSE (OUT)               CLOSE OUTPUT FILE
         OUT
S01      EQU   *                   NEW PAGE ROUTINE
         ZAP   COUNT,=P'0'         RESET LINE COUNT
         PUT   OUT,HEADLINE        PRINT HEADING
         PUT   OUT,HYPHENS
         BR    7                   RETURN
HEADLINE EQU   *                   HEADING LINE
         DC    C'1'                SKIP TO CHANNEL 1
HEADING  DC    62C' '
DATALINE EQU   *                   DATA LINE
CONTROL  DS    CL1
DETAIL   DS    CL62
HYPHENS  DC    C' ',62C'-'         HYPHEN LINE
COUNT    DC    P'100'              LINE COUNTER
LIMIT    DC    P'59'               PAGE COUNT
RF       EQU   7
RS       EQU   8
RT       EQU   9
IN       DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=EOD
OUT      DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=FBA,            X
               BLKSIZE=1575,LRECL=63
PARMDCB  DCB   DDNAME=TITLES,MACRF=GL,DSORG=PS,EODAD=A10
         LTORG
PARMLIST DS    50CL62
PARMEND  DC    X'FF'
         END
