SPIE     TITLE 'ZXTPSPIE - SPIE MODULE FOR ZXTAPRNT - ERIC BALDWIN'
Z0602    CSECT
         EXTRN IDTEST1,IDTEST2,IFTEST,LINES,NOTOUT  REFS. TO ZXTAPRNT
         EXTRN IDSW,IFSW                                     DO.
         EXTRN PRINT         REF TO ZXTPDCB
         SPACE 2
*                                                                     *
*        THIS MODULE IS GIVEN CONTROL IF A DATA PROGRAM INTERRUPTION  *
*   OCCURS DURING THE PRINTING ROUTINE.  IF THE RECORD IS AT FAULT,   *
*   THE RELEVANT SELECTION TEST IS CANCELLED, & CONTROL RETURNED.  IF *
*   NOT, IT IS A PROGRAM ERROR, SO MODULE ABENDS USER 12.             *
*                                                                     *
*********                                                     *********
         SPACE 2
*        REGISTER USAGE -
*
*              2    USED BY ISITPAKT
*              3    USED BY ISITPAKT
RBAL     EQU   4    FOR ALL BAL'S
RPIE     EQU   5    ADDR. OF PIE
RWK1     EQU   6    FOR ALL LOAD ADDRS.
RWK2     EQU   7    WORK REG.
RINST    EQU   8    NEXT INSTR. ADDR. FROM PSW IN PIE
*              12   BASE REG
         EJECT
         BALR  12,0
         USING *,12
         STM   2,1,SAVEAREA
         LR    RPIE,1
         TM    8(RPIE),X'C0'      LOOK AT ILC IN PSW IN PIE, IF ZERO,
         BZ    DUMP               NEXT ADDR. CAN'T BE TRUSTED
         SPACE 1
         L     RINST,8(RPIE)      LOAD NEXT INSTR. ADDR
         SLL   RINST,8            GET RID OF...
         SRL   RINST,8                  ...5TH PSW BYTE
         L     RWK1,=A(IDTEST1+6) SEE
         CR    RINST,RWK1          WHICH
         BE    ID1FAIL              INSTR.
         L     RWK1,=A(IDTEST2+6)    CAUSED
         CR    RINST,RWK1             INTERRUPT
         BE    ID2FAIL
         L     RWK1,=A(IFTEST+6)
         CR    RINST,RWK1
         BE    IFFAIL
         B     DUMP               IF NONE, DUMP
         SPACE 2
ID1FAIL  L     RWK1,=A(IDTEST1)
         BAL   RBAL,CHKADDR
         B     IDFAIL
ID2FAIL  L     RWK1,=A(IDTEST2)
         BAL   RBAL,CHKADDR
         SPACE 1
IDFAIL   MVI   PRINAR,X'11'
         BAL   RBAL,PUT
         MVC   PRINAR+1(39),ZXPT03
         MVC   PRINAR+40(19),=C'ID TESTS ABANDONED.'
*
         BAL   RBAL,PUT
         L     RWK1,=A(IDSW)
         MVI   1(RWK1),X'F0'      RESET ZXTAPRNT TO BRANCH ROUND
         B     IDIF               ID TESTS
         SPACE 1
IFFAIL   L     RWK1,=A(IFTEST)
         BAL   RBAL,CHKADDR
         MVI   PRINAR,X'11'
         BAL   RBAL,PUT
         MVC   PRINAR+1(39),ZXPT03
         MVC   PRINAR+40(24),=C'PRINTIF TESTS ABANDONED.'
*
         BAL   RBAL,PUT
         L     RWK1,=A(IFSW)
         MVI   1(RWK1),X'F0'      RESET ZXTAPRNT TO BRANCH ROUND
*                                 PRINTIF TESTS
IDIF     L     RWK1,=A(LINES)
         SP    0(2,RWK1),=P'4'    CORRECT LINE COUNT
         LA    RWK1,=A(NOTOUT)
         MVC   9(3,RPIE),1(RWK1)  MOVE NEW RETURN ADDR. INTO PSW
         LM    2,1,SAVEAREA
         BR    14
         EJECT
INVPACK  MVI   PRINAR,X'11'
         MVC   PRINAR+1(25),=C'INV. COMPARE FIELD - DUMP'
         BAL   RBAL,PUT
         SPACE 1
DUMP     CLOSE (PRINT)
         LM    14,0,12(RPIE)
         LM    2,13,SAVEAREA      SET REGS. TO VALUES AS AT INTERRUPT
         ABEND 12,DUMP            ** ABEND  USER 12 **
         SPACE 2
CHKADDR  MVC   COMPADDR,4(RWK1)
         MVC   REGAREA(12),24(RPIE)         MOVE IN REGS 0-2
         MVC   REGAREA+12(44),SAVEAREA+4                 3-13
         MVC   REGAREA+56(8),16(RPIE)                    14-15
         MVO   BYTES(2),COMPADDR(1)
         LA    RWK2,0
         IC    RWK2,BYTES         LOAD BASE REG. USED,
         SLL   RWK2,2             MULT. BY 4, THEN
         L     RWK1,REGAREA(RWK2) USE AS INDEX REG.
         NC    COMPADDR,=X'0FFF'  NOW BLANK OUT BASE REG,
         A     RWK1,COMPADDR-2    & ADD DISP. TO BASE REG VALUE
         LR    2,RWK1
         LA    3,4
*        CALL  ISITPAKT                 TEST FOR PACKED
         ABEND 912
         LTR   3,3
         BNP   INVPACK            IF INVALID, DON'T RETURN
         BR    RBAL
         SPACE 2
PUT      L     1,=A(PRINT)        * PRINT ROUTINE *
         LA    0,PRINAR
         L     15,48(1)
         BALR  14,15
         MVI   PRINAR+1,X'40'
         MVC   PRINAR+2(131),PRINAR+1
         BR    RBAL
         EJECT
SAVEAREA DS    16F
         DC    2X'00'  (TO GET COMPADDR-2 ONTO F BOUNDARY)
COMPADDR DC    XL2'00'
BYTES    DC    XL2'00'
PRINAR   DC    CL133' '
ZXPT03   DC    C'*** ZXPT03 - INV. PACKED FIELD FOUND - '
REGAREA  DS    16F
         LTORG
         END
