         TITLE 'GN013 - TAPE ABSTRACT'
***********************************************************************
*
*     PROGRAM   GN013
*     -------
*
*     OBJECT   TO PRINT AN ABSTRACT OF A SINGLE OR MULTI DATA SET
*     ------
*              MAGNETIC TAPE VOLUME. STANDARD TAPE FORMATS SL,AL,NL
*
*              ARE ACCEPTABLE AND ALSO NSL PROVIDED THERE IS ADEQUATE
*
*              USE OF TAPEMARKS. PROCESSING IS TERMINATED EITHER ON A
*
*              TAPEMARK FOLLOWING AN EOV TRAILER LABEL OR TWO
*
*              CONSECUTIVE TAPEMARKS.
*
*     PARAMETER NONE
*     ---------
*
*     MESSAGES  NONE
*     --------
*
*     NOTES    THE VOLUME LABEL(VOL1) IS LISTED TOGETHER WITH THE
*     -----
*               LABEL NUMBER, HEADER LABELS HDR1 AND HDR2, BLOCK
*
*               COUNT AS READ AND TRAILER LABELS EOF1,EOF2,EOV1
*
*               AND EOV2.
*
*     SAMPLE JCL //  EXEC PGM=GN013
*     ----------
*                //SYSPRINT DD SYSOUT=A
*
*                //TAPE     DD UNIT=TAPE9,VOL=SER=XXXXXX,DISP=OLD
*
*                             -WHERE XXXXXX IS REQUIRED SERIAL NO.-
*
***********************************************************************
*
*
*
GN013    BEGIN
*
*        THIS SECTION OPENS THE PRINT FILE AND READS THE JFCB FOR
*        THE TAPE FILE.  THE JFCB IS MODIFIED TO PERMIT BLP
*        PROCESSING.
*
         OPEN  (PRDCB,OUTPUT)          OPEN SYSPRINT
         RDJFCB (MTDCB)                READ JFCB
         MVI   MTJFCB+100,X'C0'        SET RECFM = U
         MVC   MTJFCB+102(2),=X'36B0'  SET BLKSIZE = 14000
         MVI   MTJFCB+66,X'10'         SET LABEL TYPE = BLP
         LA    R2,0                    SET FILE SEQ NO = 0
         MVC   H1VOL,MTJFCB+118        MOVE VOL SER TO HEADING
*
*        THIS SECTION READS UP THE TAPE PRINTING LABELS AND BLOCK
*        COUNTS IN THE PROCESS.  READING IS TERMINATED EITHER ON
*        THE FIRST TAPEMARK AFTER AN EOV TRAILER LABEL OR BY TWO
*        CONSECUTIVE TAPEMARKS.
*
LOOP     AH    R2,=H'1'                ADD 1 TO FILE SEQ NO
         STH   R2,MTJFCB+68            SET FILE SEQ NO IN JFCB
         OI    MTJFCB+76,X'80'         SET BIT TO WRITE BACK JFCB
         LA    R3,0                    SET BLOCK COUNT = 0
         NI    BCPRT+1,X'00'           SET SW TO PRINT BLOCK COUNT
         OPEN  (MTDCB,INPUT),TYPE=J    OPEN TAPE
MTREAD   READ  MTDECB,SF,MTDCB,MTREC,80    READ TAPE
         CHECK MTDECB
         AH    R3,=H'1'                ADD 1 TO BLOCK COUNT
         CLC   RECID,=C'VOL'           VOLUME LABEL?
         BE    MTPRT
         CLC   RECID,=C'HDR'           HEADER LABEL?
         BNE   TESTEOF
         NI    TMSW+1,X'00'            SET SW FOR POSSIBLE EMPTY FILE
         B     MTPRT
TESTEOF  CLC   RECID,=C'EOF'           TRAILER LABEL?
         BNE   TESTEOV
         OI    TMSW+1,X'F0'            UNSET SW FOR EMPTY FILE
         B     MTPRT
TESTEOV  CLC   RECID,=C'EOV'           TRAILER LABEL?
         BNE   MTREAD
         OI    MTEOD+1,X'F0'           SET SW FOR END OF TAPE
         B     MTPRT
MTEOD    NOP   MTEND                   END OF TAPE BY EOV LABEL
         LTR   R3,R3                   TEST FOR 2 CONSECUTIVE TAPEMARKS
         BNE   BCPRT
TMSW     NOP   MTEND                   END OF TAPE BY 2 TAPEMAKRS
BCPRT    NOP   MTCLOS
         CVD   R3,DECFLD               CONVERT
         UNPK  BCMESS+14(5),DECFLD       BLOCK COUNT
         MVZ   BCMESS+18(1),BCMESS+17      TO DECIMAL
         MVC   PRDET,BCMESS
         BAL   R4,PRINT
MTCLOS   CLOSE (MTDCB,LEAVE)           CLOSE WITHOUT REWIND
         B     LOOP
MTPRT    MVC   PRDET,MTREC
         CLC   RECIDN,=C'HDR1'         START OF DATA SET?
         BNE   NOCNT
         AP    DSCNT,=P'1'             ADD 1 TO DATA SET COUNT
         MVC   PRDS,EDMSK
         ED    PRDS,DSCNT
         B     PRBAL
NOCNT    MVC   PRDS,=C'    '
PRBAL    BAL   R4,PRINT
         OI    BCPRT+1,X'F0'           BYPASS BLOCK COUNT PRINT
         B     MTREAD
*
*        THIS SECTION CONTROLS THE PAGE HEADINGS.
*
PRINT    CP    LNCNT,=P'50'            LINE COUNT EXCEEDED?
         BL    NOHEAD                  NO
         AP    PGCNT,=P'1'             ADD 1 TO PAGE COUNT
         MVC   H1PC,EDMSK
         ED    H1PC,PGCNT
         ZAP   LNCNT,=P'00'
         MVC   PRREC,HEAD1
         BAL   R5,PRWRITE              PRINT FIRST HEADING
         MVC   PRREC,HEAD2
         BAL   R5,PRWRITE              PRINT SECOND HEADING
NOHEAD   MVC   PRREC,PRLINE
         BAL   R5,PRWRITE              PRINT DETAIL LINE
         AP    LNCNT,=P'2'             ADD 2 TO LINE COUNT
         BR    R4
*
*        THIS SECTION WRITES THE PRINT RECORDS.
*
PRWRITE  WRITE PRDECB,SF,PRDCB,PRREC,133   WRITE PRINT RECORD
         CHECK PRDECB
         BR    R5
*
*        THIS SECTION CLOSES THE FILES AND RETURNS CONTROL.
*
MTEND    CLOSE (MTDCB,REWIND,PRDCB)    CLOSE FILES REWIND TAPE
         LA    R15,0
         GOBACK RC=(15)
*
*        WORKING STORAGE CONSTANTS ETC
*
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R15      EQU   15
DECFLD   DS    D
PGCNT    DC    PL2'0'
LNCNT    DC    P'60'
DSCNT    DC    PL2'0'
EDMSK    DC    X'40202020'
JFCBEX   DS    0F
         DC    X'87'
         DC    AL3(MTJFCB)
MTJFCB   DS    0F
         DC    176X'00'
MTREC    DS    0CL80
RECIDN   DS    0CL4
RECID    DS    CL3
         DS    CL1
         DS    CL76
PRREC    DC    CL133' '
PRLINE   DS    0CL133
         DC    C'0'
         DC    C' '
PRDS     DS    CL4
         DC    CL5' '
PRDET    DS    CL80
         DC    CL42' '
BCMESS   DC    CL80'BLOCK COUNT = XXXXX'
HEAD1    DS    0CL133
         DC    CL33'1GN013 - ABSTRACT OF TAPE VOLUME '
H1VOL    DS    CL6
         DC    CL61' '
         DC    CL5'PAGE '
H1PC     DC    CL4' '
         DC    CL25'.'
HEAD2    DS    0CL133
         DC    CL30'0DATA SET'
         DC    CL20'LABELS && BLOCK COUNT'
         DC    CL83' '
*
*
MTDCB    DCB   DSORG=PS,MACRF=(R),DDNAME=TAPE,EXLST=JFCBEX,EODAD=MTEOD
*
*
PRDCB    DCB   DSORG=PS,MACRF=(W),DDNAME=SYSPRINT,RECFM=FA,BLKSIZE=133
         END
