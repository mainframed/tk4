VTOCPGM  CSECT
*_____________________________________________________________________*
*
*     THIS PROGRAM WAS ORIGINALLY WRITTEN TO PUNCH CARDS OF SYSTEM DATA
*   SETS WHICH COULD LATER BE USED TO SCRATCH UNWANTED DATA SETS.
*     A MODIFICATION ALLOWED THE PROGRAM ITSELF TO SCRATCH SYSTEM DATA
*   SETS IF THEY WERE MORE THAN TWO DAYS OLD FROM THE CURRENT DATE.
*     NO DISK DD CARDS ARE REQUIRED BUT DISKS MUST BE ON-LINE BEFORE
*   THE REPLY IS ENTERED.   DISK SERIAL NUMBER CANNOT BEGIN 'END'.
*   FURTHER MODIFICATIONS HAVE BEEN MADE TO INCLUDE 3330 DRIVES AND
*     EXCLUDE 2311'S AND OEM'S;  TO LIST DATA SETS REMAINING ON DISK
*     AS WELL AS THOSE SCRATCHED;  TO WTO THE SPACE AVAILABLE ON DISK;
*     AND TO REMOVE SOME MINOR LIMITATIONS.
*_____________________________________________________________________*
         SAVE  (14,12)
         BALR  11,0
         USING *,11
         LA    2,SAVE
         ST    13,4(2)
         ST    2,8(13)
         LR    13,2
         XR    4,4
         OPEN  (DCBA,(OUTPUT))
         OPEN  (DCBB,(OUTPUT))
         TIME  DEC                     GET DATE (00YYDDDC) IN R1
         ST    1,DATE
         UNPK  NICE(5),DATE+1(3)
         OI    NICE+4,X'F0'            TODAYS DATE IN CHARACTER
         SP    DATE+1(3),=PL1'1'
         UNPK  SOON(5),DATE+1(3)
         OI    SOON+4,X'F0'            YESTERDAYS DATE IN CHARACTER
POINTA   WTOR  'VTOCPGM - REPLY 6 CHAR VOL SER, OR ''END''',WTORADDR,6,C
               ECBADDR
         XC    ECBADDR,ECBADDR
         WAIT  ECB=ECBADDR
         OC    WTORADDR(6),=XL6'404040404040'  UPPER CASE
         CLC   WTORADDR(3),=C'END'
         BE    POINTZ          YES, EXIT ;  OTHERWISE -
         MVC   VOLSER,WTORADDR
         MVC   CARDA+18(6),VOLSER
         MVI   SINGWD,X'F0'            SIGNIFY F5 NOT YET INTERPRETED
         B     UCBSRCH
LASTSRCH MVI   SINGWD,X'F2'            SIGNIFY END OF VTOC REACHED
UCBSRCH  L     5,16(0)                 CVT ADDRESS
         L     7,40(5)   LIST SECTION IN UCB LOOKUP TABLE        **
         LA    8,250     COUNT JUST IN CASE
         LR    9,7   LIST SECTION IN UCB LOOKUP TABLE            **
ENDUCB   CLC   0(2,9),=X'FFFF'   END OF UCB TABLE ?              **
         BE    POINTGA    YES - 'VOL NOT MOUNTED'                **
         CLC   0(2,9),=X'0000'    NULL ENTRY ?                   **
         BE    UPDTE9      YES                                   **
         LH    5,0(9)    ADDRESS UCB ENTRY                       **
         CLI   18(5),X'20'             DIRECT ACCESS UCB ?
         BNE   UPDTE9      NO                                    **
         CLC   28(6,5),WTORADDR   IS THIS THE REQ'D DISC ?       **
         BE    GETUCB       YES                                  **
UPDTE9   LA    9,2(9)     ADDRESS NEXT ENTRY                     **
         BCT    8,ENDUCB   LIMIT NO OF ENTRIES IN TABLE          **
         B     POINTGB     LIMIT REACHED                         **
GETUCB   LH    3,36(5)                 TT(R0) FOR VTOC
         ST    5,UCBADDR
         SR    2,2
         CLI   19(5),X'09'             3330 ?
         BE    DIVBY19                 YES
         CLI   19(5),X'08'             2314 ?
         BE    DIVBY20
         B     UPDTE9                  NO
DIVBY20  D     2,=F'20'                20 TRKS FOR 2314 CYL
         MVI   MAXRECNO+3,X'19'
         MVI   MAXTRKNO+3,X'13'
         B     LDDSCB
DIVBY19  D     2,=F'19'                19 TRKS FOR 3330 CYL
         MVI   MAXRECNO+3,X'27'        MAX NO DSCB'S / TRK
         MVI   MAXTRKNO+3,X'12'        TRKS / CYL - 1
LDDSCB   STH   2,DSCBC+2               STORE HH
         STH   3,DSCBC                 STORE CC
         MVC   DSCBC+4(1),38(5)        STORE R
POINTD   MVC   DSCBADDR,DSCBC
         OBTAIN DSCBX
         B     POINTE(15)      CHECK RETURN CODE
POINTE   B     POINTF         0 - CONTINUE
         B     POINTGA      4 - VOL NOT MOUNTED
         B     POINTGB         8 - DSCB NOT FOUND
         B     POINTGC         12 - I/O ERROR
         B     POINTGD          16 - INVALID WK AREA PTR.
         B     POINTGE         20 - CCHH NOT WITHIN VTOC
POINTGA  WTO   'VTOCPGM - VOLUME NOT MOUNTED'
         B    POINTA
POINTGB  WTO   'VTOCPGM - RC=8, PGM ERROR'
         B     POINTA
POINTGC  WTO  'VTOCPGM - I/O ERROR'
         B     POINTFB
POINTGD  WTO  'VTOCPGM - RC=16, PGM ERROR'
         B     POINTA
POINTGE  EQU   *                       END OF VTOC REACHED
         B     LASTSRCH
POINTF   CLI   WORKAREA+44,X'00'       FORMAT 0
         BE    INCREM
         CLI   SINGWD,X'F1'            EMBEDDE D FORMAT 5
         BE    TYPE1
         CLI   WORKAREA+44,X'F5'       FORMAT 5 DSCB
         BE    FORMAT5
TYPE1    CLI   WORKAREA+44,X'F1'       FORMAT 1 DSCB
         BNE   INCREM
         CLC   WORKAREA(3),=C'SYS'
         BNE   POINTFA
         CLC   WORKAREA+8(2),=C'.T'
         BNE   POINTFA
         CLC   WORKAREA+3(5),NICE      TODAY'S ?
         BE    POINTFA
         CLC   WORKAREA+3(5),SOON      YESTERDAY'S ?
         BE    POINTFA
         CLI   WORKAREA+4,C'.'
         BE    POINTFA
         L     5,UCBADDR
         CLI   19(5),X'09'             3330 ?
         BE    DISK3330
         CLI   19(5),X'08'             2314 ?
         BE    DISK2314
         B     POINTFA
DISK2314 MVC   VOLIST+2(4),=X'30C02008'
         MVI   CARDA+16,X'F4'
         MVI   CARDC+16,X'F4'
         B     DELETE
DISK3330 MVC   VOLIST+2(4),=X'30002009'
         MVC   CARDA+13(4),=C'3330'
         MVC   CARDC+13(4),=C'3330'
DELETE   MVC   VOLIST+6(6),VOLSER
         SR    0,0
         SCRATCH DELABC
         B     ERRA(15)
ERRA     B     ERRD                    0 - CONTINUE
         B     ERRB                    4 - VOL NOT MOUNTED
         B     ERRC                    8 - UNUSUAL
ERRB     WTO   'VTOCPGM - VOLUME NOT MOUNTED'
         B     ERRD
ERRC     WTO   'VTOCPGM - UNUSUAL CONDITION ON VOLUME'
ERRD     OI    VOLIST+13,X'F0'
         MVC   CONDIT+19(1),VOLIST+13
         PUT   DCBB,CONDIT
         CLI   WORKAREA+39,C' '
         BNE   TWOCARDS
         MVC   CARDA+32(39),WORKAREA
         PUT   DCBB,CARDA
         B     INCREM
TWOCARDS MVC   CARDC+18(6),CARDA+18
         PUT   DCBB,CARDC
         MVC   CARDB+22(44),WORKAREA
         PUT   DCBB,CARDB
         B     INCREM
POINTFA  MVC   CARDD+80(44),WORKAREA   PRINT D S NOT SCRATCHED
         MVC   CARDD+126(6),VOLSER     AND SER NO.
         PUT   DCBA,CARDD
*   INCREMENT DSCB ADDRESS & REPLACE IN 'DSCBC'
INCREM   SR    4,4
         IC    4,DSCBC+4         RECORD NO.
         LA    4,1(4)       ADD 1
         C     4,MAXRECNO
         BH    POINTFB          OVER
         STC   4,DSCBC+4       LT OR EQ
         B     POINTD
POINTFB  XR    4,4           CLEAR GR 4 TO ZEROS
         LA    4,1(4)          ADD 1
         STC   4,DSCBC+4        ZEROISE RECORD NO
         LH    4,DSCBC+2               TRK NO.
         LA    4,1(4)           ADD 1
         C     4,MAXTRKNO
         BH    POINTFC
         STH   4,DSCBC+2
         B     POINTD
POINTFC  XR    4,4
         LA    4,1(4)                  ADD 1
         STH   4,DSCBC+2               COMMENCE TRK 1, NEW CYL
         LH    4,DSCBC                 CYLINDER NO.
         LA    4,1(4)                  ADD 1
         STH   4,DSCBC
         B     POINTD
RFINAL   CVD   4,CONVERT
         UNPK  INFORM+18(4),CONVERT+5(3)  MOVE AVAILABLE CYLS TO WTO
         OI    INFORM+21,X'F0'
         CVD   5,CONVERT
         UNPK  INFORM+29(4),CONVERT+5(3)   MOVE AVAILABLE TRKS TO WTO
         OI    INFORM+32,X'F0'
         CLI   SINGWD,X'F0'
         BE    FIRSTIME
         MVC   INFORM+40(6),=CL6'AFTER '
         B     INFORM
FIRSTIME MVC   INFORM+40(6),=CL6'BEFORE'
INFORM   WTO   'VTOCPGM - 0125 CYLS, 0014 TRKS  BEFORE CLEANUP'
         CLI   SINGWD,X'F2'            IS THIS 2ND. CALC ON FORMAT 5
         BE    POINTA
         MVI   SINGWD,X'F1'            SIGNIFY 1ST CALC DONE ON F5
         B     INCREM
FORMAT5  SR    4,4
         SR    5,5
FORM     LA    3,WORKAREA+4            TT OF 1ST AVAILABLE EXTENT
RADDCYL  MVC   CONVERT(8),CLEAR
         MVC   CONVERT+6(2),2(3)       CYLS
         A     4,CONVERT+4             ADD UP CYL COUNT
         MVC   CONVERT+7(1),4(3)       TRKS
         A     5,CONVERT+4             ADD UP TRK COUNT
         LA    3,5(3)                  UP DSCB POINTER
         C     3,RDPOS45               CHECK IF AT POS 45
         BE    RADD
RCHKEND  C     3,RDPOS136              CHECK IF AT POS 136
         BE    READDSCB
         B     RADDCYL
RADD     LA    3,1(3)                  SKIP OVER FORMAT 5 ID
         B     RCHKEND
READDSCB CLC   CLEAR(5),0(3)           CHECK FOR CHAIN ADDRESS
         BE    RFINAL                  IF NONE, PRINT ON CONSOLE
         MVC   CCHHR(5),0(3)           ADDR OF CCHHR
         OBTAIN DSCBY
         B     FORM
POINTZ   CLOSE DCBA
         CLOSE DCBB
         L     13,SAVE+4
         RETURN (14,12)
         LTORG                                                   **
DCBA     DCB   DSORG=PS,DEVD=DA,MACRF=PM,DDNAME=VTOCCARD,LRECL=133
DCBB     DCB   DSORG=PS,DEVD=DA,MACRF=PM,DDNAME=VTOCLIST,LRECL=80
DELABC   CAMLST SCRATCH,WORKAREA,,VOLIST,,
DSCBX    CAMLST SEEK,DSCBADDR,VOLSER,WORKAREA
DSCBY    CAMLST SEEK,CCHHR,VOLSER,WORKAREA
DUBWORD  DS    D
CONVERT  DS    8X
         DS    0D
WORKAREA DS    350C
RDPOS45  DC    A(WORKAREA+44)
RDPOS136 DC    A(WORKAREA+135)
DATE     DS    F
ECBADDR  DC    F'0'
MAXRECNO DC    F'16'
MAXTRKNO DC    F'9'
SAVE     DS    18F          GENERAL SAVE AREA
UCBADDR  DS    F
CCHHR    DS    5X
VOLIST   DC    H'1'
         DS    2H
         DS    3H
         DS    H'0'
DSCBC    DC    X'0000000003'   MUST BE HALF WORD ALIGNED         **
DSCBADDR DS    CL5
NICE     DS    CL5
SINGWD   DS    CL4                                               **
SOON     DS    CL5
VOLSER   DS    CL6
WTORADDR DS    CL6
CARDA    DC    C' SCRATCH VOL=2311=XXXXXX,DSNAME=',48C' '
CARDB    DC    15C' ',C'DSNAME=',58C' '                             +22
CARDC    DC    C' SCRATCH VOL=2311=XXXXXX,',46C' ',C'C',8C' '
CARDD    DC    CL133' '
CLEAR    DC    X'0000000000000000'
CONDIT   DC    CL80' SCRATCH CONDITION'
         END   VTOCPGM
