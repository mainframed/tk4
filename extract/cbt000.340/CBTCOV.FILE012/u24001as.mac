*          DATA SET PICKFMID   AT LEVEL 002 AS OF 27/11/80
         TITLE 'HANDLE LISTPTS FOR SPECIFIED FMIDS'
***********************************************************************
*                                                                     *
*                          PICKFMID                                   *
*                                                                     *
* DESCRIPTION                                                         *
*        THIS PROGRAM WORKS ON A LIST PTS PRODUCED BY SMP4. IT ALSO   *
*        REQUIRES (AS A PARM) A LIST OF FMIDS, AND IT MAY HAVE AN     *
*        EXCLUDE LIST (IN PUT PROCESS FORMAT) SUPPLIED. IT PRODUCES   *
*        A LIST OF THE FMIDS SPECIFIED, AND THOSE SYSMODS WHICH ARE   *
*        OWNED BY THAT FMID (AND ARE NOT IN THE EXCLUDE LIST, IF ANY).*
*        IT ALSO PRODUCES A SEQUENTIAL FILE CONTAING SMP CONTROL      *
*        CARDS FOR AN APPLY SELECT CHECK ON THE SAME SYSMODS.         *
*                                                                     *
* JCL REQUIRED.                                                       *
*                                                                     *
*        //STEPA  EXEC  SMP4 (OR WHATEVER)                            *
*              .... OTHER JCL FOR SMP4 ....                           *
*        //SMPLIST  DD DSN=&&LISTPTS,DISP=(,PASS),                    *
*        //       UNIT=SYSDA, SPACE=   ... AS REQUIRED/APPROPRIATE    *
*              .... REST OF JCL ...                                   *
*         LIST PTS SYSMOD NOAPP NOACC .                               *
*        //STEPB  EXEC  PGM=PICKFMID,PARM='LIST OF FMIDS'             *
*        //EXCLUDE  DD DSN=INDEX.PUTPDS(CNTLAPCK),DISP=SHR **OPTIONAL**
*        //LISTPTS  DD DSN=&&LISTPTS,DISP=(OLD,DELETE),               *
*        //           DCB=(RECFM=FB,LRECL=121,BLKSIZE=121)            *
*        //LISTOUT  DD ***** SEQUENTIAL DATA SET CONTAINING GENERATED *
*                            CONTROL CARDS *******                    *
*        //SYSPRINT DD SYSOUT=A        PRINTED LISTING                *
*                                                                     *
* ERROR MESSAGES                                                      *
*                                                                     *
*        PARM IS REQUIRED FOR THIS PROGRAM                            *
*                  NO PARM, NO FMID, NO SELECTION. SUPPLY A PARM      *
*                  AND RERUN.                                         *
*        PARM IS TOO SHORT FOR AN FMID                                *
*                  THERE WAS A PARM, BUT IT WAS NOT 7 (OR MORE)       *
*                  CHARACTERS LONG - IT CANNOT BE AN FMID             *
*        ERROR IN PARMS, EXECUTION CONTINUES                          *
*                  THE FMIDS SHOULD BE SEPARATED BY COMMAS. PARM      *
*                  PROCESSING DID NOT FIND A COMMA WHEN IT EXPECTED   *
*                  ONE (BEFORE END OF PARMS). ANY FMIDS ALREADY FOUND *
*                  WILL BE PROCESSED                                  *
*                                                                     *
*        THE PARMS SHOULD BE ARRANGED AS, E.G.:                       *
*        EXEC PGM=PICKFMID,PARM='EBB1102,FBB1221'                     *
*        EXEC PGM=PICKFMID  -->   PARM IS REQUIRED FOR THIS PROGRAM   *
*        EXEC PGM=PICKFMID,PARM='VTAM' --> PARM IS TOO SHORT FOR AN   *
*                  FMID (VTAM IS NOT AN FMID)                         *
*        EXEC PGM=PICKFMID,PARM='EVT1102,JVT1112 VTAM' --> ERROR IN   *
*                  PARMS, EXECUTION CONTINUES. WILL USE EVT1102 &     *
*                  JVT1112                                            *
*                                                                     *
***********************************************************************
*        MIKE HARRIES, ICI MOND DIVISION, NOVEMBER 1980               *
***********************************************************************
*                                                                     *
*        REGISTER USAGE                                               *
*                                                                     *
*        R1       POINTER IN PARMS                                    *
*        R2       COUNT DOWN LENGTH OF PARMS                          *
*        R3       POINTER TO CURRENT FMID                             *
*        R4       COUNTER FOR NUMBER OF FMIDS, USED IN BCT            *
*        R5      POINTER TO NEXT SLOT IN OUTPUT LINE                  *
*        R13     SAVE AREA, BASE REGISTER                             *
*                                                                     *
*        OTHER REGISTERS ARE NOT USED EXCEPT FOR PUT, GET, ETC.       *
*                                                                     *
***********************************************************************
         TITLE 'PROCESS PARMS TO EXTRACT FMIDS'                       *
         PRINT NOGEN
PICKFMID BEGIN
         LTR   R1,R1              ANY PARMS ?
         BZ    NOPARMS                        NO - FATAL ERROR
         L     R1,0(,R1)          POINT AT START
         LH    R2,0(,R1)          LENGTH
         CH    R2,=H'7'
         BL    NOFMID             INSUFFICIENT FOR AN FMID
         LA    R1,2(,R1)
         SH    R2,=H'2'
         LA    R3,FMIDS
         SR    R4,R4
         LA    R5,SYSP2+5
MOVEFMID MVC   0(7,R3),0(R1)      MOVE AN FMID INTO LIST
         MVC   0(7,R5),0(R1)
         LA    R4,1(,R4)          COUNT FMIDS MOVED
         SH    R2,=H'7'           ACCOUNT FOR LENGTH
         LTR   R2,R2              ANYTHING LEFT ?
         BNP   ENDFMIDS                           NO, FINISHED
         CLI   7(R1),C','
         BNE   PARMERR            FMIDS SEPARATED BY COMMAS
         LA    R1,8(,R1)          SKIP OVER 'FMID,'
         LA    R3,7(R3)           FMID ONLY
         MVI   7(R5),C','
         LA    R5,8(,R5)
         BCT   R2,MOVEFMID        ACCOUNT FOR COMMA
ENDFMIDS ST    R4,NRFMIDS         STORE NUMBER OF FMIDS FOUND
         TITLE 'INITIALIZE FOR MAIN LOOP'                             *
         OPEN  (LISTPTS,,LISTOUT,(OUTPUT),SYSPRINT,(OUTPUT))
         PUT   LISTOUT,FIRSTOUT
         PUT   SYSPRINT,SYSP1
         PUT   SYSPRINT,SYSP2
         RDJFCB (EXCLUDE)
         LTR   R15,R15            DD CARD PRESENT ?
         BNZ   NOEXCL                               NO
         OPEN  (EXCLUDE)
         TM    EXCLUDE+48,X'10'   OPEN SUCCESSFUL ?
         BO    ENDINIT
NOEXCL   OI    SAVESYSM+1,X'F0'   CHANGE NOP TO B
         OI    CLOSE+8,X'80'      CUT OFF EXCLUDE
ENDINIT  EQU   *
         TITLE 'MAIN LOOP'
GET1     GET   LISTPTS,LINE
         CLC   LINE+1(7),BLANKS   ANY SYSMOD IDENTIFIER ?
         BE    GET1                                       NO
         CLC   LINE+11(4),=C'TYPE'
         BNE   GET1
SAVESYSM NOP   STORSYSM           B     IF NO EXCLUDES
         CLC    LINE+1(7),EXCLSYSM+6
         BL    STORSYSM
         BE    GET1
         GET   EXCLUDE,EXCLSYSM
         B     SAVESYSM
         SPACE
EOFEXCL  MVC   EXCLSYSM+6(7),=C'ZZ99999'
         B     SAVESYSM
         SPACE 3
STORSYSM MVC   SYSMOD,LINE+1      SAVE SYSMOD IDENTIFIER
GET2     GET   LISTPTS,LINE
         CLC   LINE+1(7),BLANKS   IS THIS A NEW SYSMOD ?
         BE    TRYFMID                                      NO
         CLC   LINE+11(4),=C'TYPE' IS IT ?
         BE    SAVESYSM                   YES
TRYFMID  CLC   LINE+11(4),=C'FMID'
         BNE   GET2
         L     R4,NRFMIDS
         LA    R3,FMIDS
NEXTFMID CLC   0(7,R3),LINE+29    IS THIS AN FMID WE WANT ?
         BE    WANTIT                                       YES
         LA    R3,7(,R3)          NEXT ENTRY IN LIST
         BCT   R4,NEXTFMID        GO ROUND FOR NUMBER OF FMIDS
         B     GET1               GO LOOK FOR NEXT SYSMOD
         SPACE
WANTIT   L     R5,LISTPTR         POINTER TO NEXT SLOT
         C     R5,=A(OUT+65)      ROOM FOR ANOTHER ?
         BL    MOVEIN
         CLC   OUT,BLANKS
         BE    NEWLINE
         MVI   0(R5),C','         ADD TRAILING COMMA
         PUT   LISTOUT,OUT
         PUT   SYSPRINT,OUT
         MVC   OUT,BLANKS
NEWLINE  LA    R5,OUT+10          NEW LINE
         B     *+8
MOVEIN   MVI   0(R5),C','
         MVC   1(7,R5),SYSMOD     MOVE SYSMOD ID INTO LINE
         LA    R5,8(,R5)          SKIP OVER ',FMID'
         ST    R5,LISTPTR         AND STORE POINTER
         MVC   SYSMOD,BLANKS      BLANK SYSMOD
         B     GET1               GET NEXT SYSMOD
         TITLE 'FINISH OFF LIST'
EOF      CLC   SYSMOD,BLANKS      ANY SYSMOD LEFT ?
         BE    LASTLINE                             NO
         L     R5,LISTPTR
         C     R5,=A(OUT+65)
         BL    MOVEIN2
         MVI   1(R5),C','
         PUT   LISTOUT,OUT
         PUT   SYSPRINT,OUT
         MVI   OUT,C' '
         MVC   OUT+1(79),OUT
         LA    R5,OUT+10
         B     *+8
MOVEIN2  MVI   0(R5),C','
         MVC   1(7,R5),SYSMOD
LASTLINE PUT   LISTOUT,OUT
         PUT   SYSPRINT,OUT
         PUT   LISTOUT,ENDLINE
CLOSE    CLOSE (LISTPTS,,OUT,,SYSPRINT,,EXCLUDE)
         SPACE
         SR    R15,R15
RETURN   L     R15,CODE
         L     R13,4(,R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
         TITLE 'ERROR ROUTINES'
NOPARMS  WTO   'PARM IS REQUIRED FOR THIS PROGRAM'
         MVC   CODE,=F'16'
         B     RETURN
         SPACE
NOFMID   WTO   'PARM IS TOO SHORT FOR AN FMID'
         MVC   CODE,=F'16'
         B     RETURN
         SPACE
PARMERR  WTO   'ERROR IN PARMS, EXECUTION CONTINUES'
         MVC   CODE,=F'4'
         B     ENDFMIDS
         TITLE 'CONSTANTS AND WORK AREAS'
BLANKS   DC    CL80' '
CODE     DC    F'0'               RETURN CODE
ENDLINE  DC    CL80'          ) CHECK DIS(WRITE) .'
EXCLSYSM DC    CL80' '
EXLST    DC    X'87',AL3(JFCB)
FIRSTOUT DC    CL80'  APPLY SELECT ('
FMIDS    DC    10CL7' '
JFCB     DS    44F
LISTPTR  DC    A(OUT+70)          POINTER IN OUTPUT LINE
LINE     DC    CL121' '           INPUT LINE
NRFMIDS  DC    F'0'               NUMBER OF FMIDS
OUT      DC    CL80' '            OUTPUT LINE
         DC    CL53' '
SYSMOD   DC    CL7' '
SYSP1    DC    CL133'   SYSMODS ELIGIBLE FOR APPLY FOR FUNCTIONS'
SYSP2    DC    CL133' '
         SPACE
EXCLUDE  DCB   DDNAME=EXCLUDE,RECFM=FB,EODAD=EOFEXCL,EXLST=EXLST,      *
               MACRF=(GM),DSORG=PS
LISTPTS  DCB   DDNAME=LISTPTS,RECFM=FB,LRECL=121,EODAD=EOF,MACRF=(GM), *
               DSORG=PS,BLKSIZE=121
LISTOUT  DCB   DDNAME=LISTOUT,RECFM=FB,LRECL=80,BLKSIZE=80,MACRF=(PM), *
               DSORG=PS
SYSPRINT DCB   DDNAME=SYSPRINT,RECFM=FBA,LRECL=133,BLKSIZE=133,        *
               MACRF=(PM),DSORG=PS
         SPACE
         LTORG
         DC    CL21'002PICKFMID  27/11/80'
         END
