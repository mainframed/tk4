DIAN     TITLE 'DISK ANALYSIS - TFLOW INPUT'
         SPACE 3
         EJECT
         PRINT NOGEN
DISKANAL CSECT
         SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         LA    14,SAVE
         ST    13,4(14)
         ST    14,8(13)
         LR    13,14
*
*        ANALYZE PARMFIELD
*
         L     2,0(1)
         LH    3,0(2)        COUNT
         LTR   3,3
         BZ    ABEND
         BCTR  3,0           -1
         STC   3,*+5
         MVC   PARM(1),2(2)  MOVE PARM
         LA    2,UNTABD      OUTPUT TABEL
         USING UNTAB,2
         LA    3,HEAD+21
         LA    4,PARM
         LA    5,8           LIMIT
B0100    CLI   3(4),C'/'     2 ADDRESSES
         BE    B0140
         MVC   5(3,3),0(4)   MOVE TO HEADING
         MVC   TXT(3),0(4)
         B     B0150
B0140    MVC   3(7,3),0(4)   MOVE TO HEADING
         MVC   TXT(7),0(4)
B0150    TR    0(3,4),TAB-193
         PACK  HELP(3),0(4,4) PACK FIRST ADDRESS
         MVC   ADR1(2),HELP
         CLI   3(4),C'/'     2 ADDRESSES
         BE    B0170         YES
         LA    4,3(4)
         B     B0190
B0170    TR    4(3,4),TAB-193
         PACK  HELP(3),4(4,4) PACK SECOND ADDRESS
         MVC   ADR2(2),HELP
         LA    4,7(4)
B0190    CLI   0(4),C'X'     XREF REQUESTED
         BNE   B0195
         LA    4,1(4)        CORRECT PARM POINTER
         MVI   SWX,C'X'      SET SWITCH
         OPEN  MF=(E,OPENO)  OPEN SCRATCH FILE OUTPUT
B0195    CLI   0(4),C'D'     DRUM REQUESTED
         BNE    B0200
         MVI   SWD,C'D'      SET SWITCH
         LA    4,1(4)        CORRECT PARM POINTER
B0200    LA    3,14(3)       HEADING POINTER
         LA    2,UNTABL(2)
         CLI   0(4),C' '     END OF PARM
         BE    B0300
         CLI   0(4),C','     NEXT ADDRESS PRESENT
         BNE   ABEND
         LA    4,1(4)        CORRECT PARM POINTER
         BCT   5,B0100       TEST LIMIT
ABEND    ABEND 4
*
*        OPEN FILES, ZERO ARRAY
*
B0300    OPEN  (TAPE,,SYSPRINT,OUTPUT)
         GETMAIN  EU,LV=6400,A=ARRAY
         L     3,ARRAY
         LA    4,31
         MVI   0(3),0
         MVC   1(199,3),0(3)
B0320    MVC   200(200,3),0(3)
         LA    3,200(3)
         BCT   4,B0320
*
*        READ TAPE, FIND MATCHING ADDRESS
*
B0400    GET   TAPE
         LR    3,1
         CLI   0(3),X'AA'    USER RECORD
         BE    B0400
         CLI   1(3),0        SIO RECORD
         BNE   B0400
         LH    4,2(3)        DISK ADDRESS
         LTR   4,4
         BZ    B0400
         LA    2,UNTABD
         LA    5,8           SEARCH LIMIT
         L     6,ARRAY
B0420    CH    4,ADR1        ADDRESS EQUAL
         BE    B0500
         CH    4,ADR2
         BE    B0510
         LA    2,UNTABL(2)
         A     6,=F'800'     NEXT ELT OF ARRAY
         BCT   5,B0420       TEST LIMIT
         B     B0400         ADDRESS NOT FOUND
*
*        PROCESS RECORD
*
B0500    SR    4,4
         B     B0520
B0510    LA    4,4
B0520    CLC   4(2,3),=H'199'
         BH    B0400         SKIP IF CYL GT 400
         CLC   6(2,3),=H'199'
         BH    B0400
         L     5,CHA(4)      ADD COUNT PER CHANNEL
         A     5,=F'1'
         ST    5,CHA(4)
         L     5,SIO         SIO COUNT
         A     5,=F'1'
         ST    5,SIO
         CLI   SWD,C'D'      DRUM
         BNE    *+10
         MVC   4(2,3),6(3)   MOVE TRK TO CYL
         LH    7,4(3)        CYL-NO
         SLA   7,2           *4
         L     5,0(7,6)
         A     5,=F'1'
         ST    5,0(7,6)
         LH    7,4(3)
         CLI   CYLSAV,X'FF'  FIRST TIME
         BE    B0550
         SH    7,CYLSAV
         LPR   7,7
         L     5,TOTCYL
         AR    5,7
         ST    5,TOTCYL
B0550    MVC   CYLSAV(2),4(3)
         CLI   SWX,C'X'      XREF
         BNE   B0400
         PUT   DCB,(3)       PUT RECORD TO SCRATCH
         B     B0400
*
*        PRINT RESULTS
*
EOF1     LA    2,UNTABD
         BAL   11,HEADR
         MVC   R+1(11),=C'TOTAL SIO''S'
         LA    3,SIO
         BAL   10,EDITPR
         MVC   R+1(15),=C'SIO''S CHANNEL A'
         LA    3,CHA
         BAL   10,EDITPR
         MVC   R+1(15),=C'SIO''S CHANNEL B'
         LA    3,CHB
         BAL   10,EDITPR
         MVC   R+1(15),=C'CYLINDERS MOVED'
         LA    3,TOTCYL
         BAL   10,EDITPR
*
*        PRINT SIO PER CYLINDER
*
B0690    LA    2,4           NUMBER OF PAGES
         SR    3,3           CYLNO
         SR    4,4           INDEX IN ARRAY
B0700    LA    5,50          # LINES
         C    2,=F'4'        FIRST PAGE
         BE    *+8           SKIP HEADING
         BAL   11,HEADR
         MVC   R+1(14),=C'SIO''S PER CYL.'
B0710    STC   3,CYLNO
         UNPK  R+17(3),CYLNO(2)
         MVI   R+19,C' '
         TR    R+17(2),TAB-193
         LA    3,1(3)        CYL+1
         LA    6,R+20        INDEX TO LINE
         L     7,ARRAY
         LA    8,8
B0730    MVC   0(14,6),ED1
         L     9,0(7,4)
         CVD   9,D
         ED    3(11,6),D+3
         LA    7,800(7)
         LA    6,14(6)
         BCT   8,B0730
         BAL   11,PUT
         LA    4,4(4)
         BCT   5,B0710
         BCT   2,B0700
*
*        CLOSE FILES
*
         FREEMAIN  E,LV=6400,A=ARRAY
         CLOSE (TAPE)
         LA    2,UNTABD
         LA    3,8
B0800    CLI   SWX,C'X'
         BNE    B0820
         CLOSE MF=(E,CLOSE)
B0820    LA    2,UNTABL(2)
         BCT   3,B0800
*
*        PREPARE XREF PROCESSING
*
         LA    2,UNTABD
         LA    3,8
B1000    ST    3,HELP        SAVE LIMIT
         CLI   SWX,C'X'
         BNE   B1990
         OPEN  MF=(E,OPENI)  OPEN INPUT
         GETMAIN  EU,LV=80000,A=ARRAY
         LA    3,399
         L     4,ARRAY
         MVI   0(4),0
         MVC   1(199,4),0(4)
B1020    MVC   200(200,4),0(4)
         LA    4,200(4)
         BCT   3,B1020
         MVI   CYLSAV,X'FF'
         L     6,ARRAY
*
*        READ AND PROCESS SCRATCH FILE
*
B1030    GET   DCB
         CLI   CYLSAV,X'FF'
         BE    B1050
         LH    3,CYLSAV
         LH    5,4(1)
         SLA   3,1           *2
         M     4,=F'400'     SECOND INDEX*400
         AR    5,3
         LH    7,0(5,6)
         A     7,=F'1'
         STH   7,0(5,6)
B1050    MVC   CYLSAV(2),4(1)
         B     B1030
*
*        PRINT XREF LISTING
*
EOF2     SR    4,4           J=0
B1400     SR   3,3           I=0
B1410    LR    5,3           I2=I
         LR    6,4           J
         SR    6,3           J-I
         C     6,=F'50'      (J-I)GT 50
         BNL   B1800         SKIP PAGE
*
*        CONSTRUCT HEADING WITH CYLNO
*
         LA    9,R+11
         LA    7,25          LIMIT
         LR    8,4           J=START VALUE
B1450    STC   8,CYLNO
         UNPK  0(3,9),CYLNO(2)
         MVI   2(9),C' '
         TR    0(2,9),TAB-193
         LA    9,5(9)
         LA    8,1(8)
         BCT   7,B1450
         MVC   R+1(7),TXT
         MVI   R,C'1'
         BAL   11,PUT
         MVI   R,C'0'
*
*        CONSTRUCT LINE
*
B1470    LR    6,4           J2=J
         STC   5,CYLNO
         UNPK  R+6(3),CYLNO(2)
         MVI   R+8,C' '
         TR    R+6(2),TAB-193
         LA    7,R+8         INDEX IN LINE
B1500    CR    6,5
         BH    B1600         J2 GT I2 SKIP REST OF LINE
         LR    11,6          J2
         SLA   11,1          J2*2
         LR    9,5           I2
         M     8,=F'400'     I2*400
         AR    11,9
         A     11,ARRAY
         LH    10,0(11)      ARRAY(J2,I2)
         CR    6,5
         BE    B1520         I2 EQ J2 SKIP REST
         LR    11,5          I2
         SLA   11,1          I2*2
         LR    9,6           J2
         M     8,=F'400'     J2*400
         AR    11,9
         A     11,ARRAY
         AH    10,0(11)      ARRAY(J2,I2)+ARRAY(I2,J2)
B1520    CVD   10,D
         MVC   EDH(6),=X'402020202120'
         ED    EDH(6),D+5
         MVC   0(5,7),EDH+1
         LA    7,5(7)
         LA    6,1(6)        J2=J2+1
         LR    9,6           J2
         SR    9,4           J2-J
         C     9,=F'25'      LINE FULL
         BL    B1500
B1600    BAL   11,PUT
         LA    5,1(5)        I2=I2+1
         LR    9,5
         SR    9,3           I2-I
         C     9,=F'50'      PAGE FULL
         BL    B1470
B1800    LA    3,50(3)       I=I+50
         C     3,=F'200'
         BL    B1410
         LA    4,25(4)       J=J+25
         C     4,=F'200'
         BL    B1400
*
*        TERMINATE XREF
*
         FREEMAIN  E,LV=80000,A=ARRAY
         CLOSE MF=(E,CLOSE)
B1990    L     3,HELP
         LA    2,UNTABL(2)
         BCT   3,B1000
*
*        TERMINATE PROGRAM
*
         CLOSE (SYSPRINT)
         L     13,4(13)
         RETURN (14,12),RC=0
*
*        SUBROUTINES
*
PUT      PUT   SYSPRINT,R
         MVI   R,C' '
         MVC   R+1(132),R
         BR    11
*
HEADR    PUT   SYSPRINT,HEAD
         MVI   R,C'0'
         BR    11
EDITPR   LA    5,8
         LA    4,R+20
EDB1     MVC   1(13,4),ED1+1
         L     6,0(3)
         CVD   6,D
         ED    3(11,4),D+3
         LA    3,UNTABL(3)
         LA    4,14(4)
         BCT   5,EDB1
         BAL   11,PUT
         MVI   R,C'0'
         BR    10
*
*        CONSTANTS
*
D        DC    D'0'
HELP     DC    F'0'
ARRAY    DC    F'0'
CYLNO    DC    F'0'
SAVE     DS    18F
ED1      DC    X'404040402020202020204B202120'
HEAD     DC    CL21'1DISK ANALYSIS',8C'------------- '
PARM     DC    CL101' '
TAB      DC    X'FAFBFCFDFEFF',CL41' ',C'0123456789ABCDEF'
R        DC    CL133' '
EDH      DC    CL6' '
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=GL,RECFM=FB,LRECL=32,        *
               BLKSIZE=3200,EODAD=EOF1
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,            *
               LRECL=133,BLKSIZE=1197
*
*        DSECT WITH INFORMATION PER ADDRESS
*
UNTAB    DSECT
ADR1     DS    H
ADR2     DS    H
TXT      DS    CL8
CYLSAV   DS    H
SWX      DS    C
SWD      DS    C
CHA      DS    F
CHB      DS    F
SIO      DS    F
TOTCYL   DS    F
OPENO    DS    F
OPENI    DS    F
CLOSE    DS    F
DCB      DS    24F
UNTABE   EQU   *
UNTABL   EQU   UNTABE-UNTAB
DISKANAL CSECT
UNTABD   DC    F'0',CL8' ',X'FF000000',4F'0'
         OPEN  (SYSUT1,OUTPUT),MF=L
         OPEN  (SYSUT1,INPUT),MF=L
         CLOSE (SYSUT1),MF=L
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=6,BLKSIZE=600
         DC    F'0',CL8' ',X'FF000000',4F'0'
         OPEN  (SYSUT2,OUTPUT),MF=L
         OPEN  (SYSUT2,INPUT),MF=L
         CLOSE (SYSUT2),MF=L
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=6,BLKSIZE=600
         DC    F'0',CL8' ',X'FF000000',4F'0'
         OPEN  (SYSUT3,OUTPUT),MF=L
         OPEN  (SYSUT3,INPUT),MF=L
         CLOSE (SYSUT3),MF=L
SYSUT3   DCB   DDNAME=SYSUT3,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=6,BLKSIZE=600
         DC    F'0',CL8' ',X'FF000000',4F'0'
         OPEN  (SYSUT4,OUTPUT),MF=L
         OPEN  (SYSUT4,INPUT),MF=L
         CLOSE (SYSUT4),MF=L
SYSUT4   DCB   DDNAME=SYSUT4,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=6,BLKSIZE=600
         DC    F'0',CL8' ',X'FF000000',4F'0'
         OPEN  (SYSUT5,OUTPUT),MF=L
         OPEN  (SYSUT5,INPUT),MF=L
         CLOSE (SYSUT5),MF=L
SYSUT5   DCB   DDNAME=SYSUT5,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=6,BLKSIZE=600
         DC    F'0',CL8' ',X'FF000000',4F'0'
         OPEN  (SYSUT6,OUTPUT),MF=L
         OPEN  (SYSUT6,INPUT),MF=L
         CLOSE (SYSUT6),MF=L
SYSUT6   DCB   DDNAME=SYSUT6,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=6,BLKSIZE=600
         DC    F'0',CL8' ',X'FF000000',4F'0'
         OPEN  (SYSUT7,OUTPUT),MF=L
         OPEN  (SYSUT7,INPUT),MF=L
         CLOSE (SYSUT7),MF=L
SYSUT7   DCB   DDNAME=SYSUT7,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=6,BLKSIZE=600
         DC    F'0',CL8' ',X'FF000000',4F'0'
         OPEN  (SYSUT8,OUTPUT),MF=L
         OPEN  (SYSUT8,INPUT),MF=L
         CLOSE (SYSUT8),MF=L
SYSUT8   DCB   DDNAME=SYSUT8,DSORG=PS,MACRF=(GL,PM),EODAD=EOF2,        *
               RECFM=FBS,LRECL=6,BLKSIZE=600
         END   DISKANAL
TFLOWE   CSECT
         USING *,15
         B     *+8
         DC    C'EPOK'
         MVC   0(4,1),=A(EXIT)
         BR    14
         USING EXIT,15
EXIT     B     *+8
         DC    C'EPOK'
         TM    2(2),X'F0'
         BZ    0(14,0)
         B     12(14,0)
         END
