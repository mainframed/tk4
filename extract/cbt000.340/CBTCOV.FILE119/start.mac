      LOGICAL FUNCTION START(DUMMY)
C
C  CHECK TO SEE IF THIS IS "PRIME TIME".  IF SO, ONLY WIZARDS MAY PLAY,
C  THOUGH OTHERS MAY BE ALLOWED A SHORT GAME FOR DEMONSTRATION PURPOSES.
C  IF SETUP<0, WE'RE CONTINUING FROM A SAVED GAME, SO CHECK FOR SUITABLE
C  LATENCY.  RETURN TRUE IF THIS IS A DEMO GAME (VALUE IS IGNORED FOR
C  RESTARTS).
C
      IMPLICIT INTEGER(A-Z)
      LOGICAL PTIME,SOON,YESM,WIZARD
      DIMENSION HNAME(20)
      COMMON /WIZCOM/ WKDAY,WKEND,HOLID,HBEGIN,HEND,HNAME,
     1SHORT,MAGIC,MAGNM,LATNCY,SAVED,SAVET,SETUP
      COMMON /IOSCOM/ TTYI,TTYO,BLKLIN,DBFI,DBINIT,DBSAVE
C
C  FIRST FIND OUT WHETHER IT IS PRIME TIME (SAVE IN PTIME) AND, IF
C  RESTARTING, WHETHER IT'S TOO SOON (SAVE IN SOON).  PRIME-TIME SPECS
C  ARE IN WKDAY, WKEND, AND HOLID; SEE MAINT ROUTINE FOR DETAILS.
C  LATNCY IS REQUIRED DELAY BEFORE RESTARTING.  WIZARDS MAY CUT THIS TO
C  A THIRD.
C
      CALL DATIME(D,T)
      PRIMTM=WKDAY
C
C     DAY OF WEEK IS FOUND BY USING METHOD FOUND IN
C     COMPUTERWORLD JANUARY 14,1980
C
C     SATURDAY=0, SUNDAY=1, ETC.
C
      YEAR=D/1000
      DAY=D-YEAR*1000
      YEAR=YEAR+1900
      LYEAR=YEAR-1
      P1=LYEAR/4
      P2=LYEAR/100
      P3=LYEAR/400
      P4=LYEAR/4000
      P5=LYEAR/1000
      P6=LYEAR/2000
      XDAYS=P1-P2+P3-P4+P5-P6
      P7=DAY+YEAR+XDAYS-1
      DOW=MOD(P7,7)
      IF(DOW .EQ. 0 .OR. DOW .EQ. 1)PRIMTM=WKEND
C
      IF(D.GE.HBEGIN.AND.D.LE.HEND)PRIMTM=HOLID
      PTIME=AND(PRIMTM,SHIFT(1,T/60)).NE.0
      SOON=.FALSE.
      IF(SETUP.GE.0)GOTO 20
      DELAY=(D-SAVED)*1440+(T-SAVET)
      IF(DELAY.GE.LATNCY)GOTO 20
      WRITE(TTYO,10)DELAY
10    FORMAT(' THIS ADVENTURE WAS SUSPENDED A MERE',I3,' MINUTES AGO.')
      SOON=.TRUE.
      IF(DELAY.GE.LATNCY/3)GOTO 20
      CALL MSPEAK(2)
      STOP
C
C  IF NEITHER TOO SOON NOR PRIME TIME, NO PROBLEM.  ELSE SPECIFY WHAT'S
C  WRONG.
C
20    START=.FALSE.
      IF(SOON)GOTO 30
      IF(PTIME)GOTO 25
22    SAVED=-1
      RETURN
C
C  COME HERE IF NOT RESTARTING TOO SOON (MAYBE NOT RESTARTING AT ALL),
C  BUT IT'S PRIME TIME.  GIVE OUR HOURS AND SEE IF HE'S A WIZARD.  IF
C  NOT, THEN CAN'T RESTART, BUT IF JUST BEGINNING THEN WE CAN OFFER A
C  SHORT GAME.
C
25    CALL MSPEAK(3)
      CALL HOURS
      CALL MSPEAK(4)
      IF(WIZARD(0))GOTO 22
      IF(SETUP.LT.0)GOTO 33
      START=YESM(5,7,7)
      IF(START)GOTO 22
      STOP
C
C  COME HERE IF RESTARTING TOO SOON.  IF HE'S A WIZARD, LET HIM GO (AND
C  NOTE THAT IT THEN DOESN'T MATTER WHETHER IT'S PRIME TIME).  ELSE,
C  TOUGH BEANS.
C
30    CALL MSPEAK(8)
      IF(WIZARD(0))GOTO 22
33    CALL MSPEAK(9)
      STOP
      END
