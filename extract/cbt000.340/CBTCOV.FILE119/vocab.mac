      INTEGER FUNCTION VOCAB(ID,INIT)
C
C  LOOK UP ID IN THE VOCABULARY (ATAB) AND RETURN ITS "DEFINITION"
C  (KTAB), OR -1 IF NOT FOUND.  IF INIT IS POSITIVE, THIS IS AN
C  INITIALIZATION CALL SETTING UP A KEYWORD VARIABLE, AND NOT FINDING IT
C  CONSTITUTES A BUG.  IT ALSO MEANS THAT ONLY KTAB VALUES WHICH TAKEN
C  OVER 1000 EQUAL INIT MAY BE CONSIDERED.  (THUS "STEPS", WHICH IS A
C  MOTION VERB AS WELL AS AN OBJECT, MAY BE LOCATED AS AN OBJECT.)  AND
C  IT ALSO MEANS THE KTAB VALUE IS TAKEN MOD 1000.
C
      IMPLICIT INTEGER(A-Z)
      COMMON /VOCCOM/ KTAB,ATAB,TABSIZ
      DIMENSION KTAB(300),ATAB(300)
C
      HASH=SCRMBL(ID)
      DO 1 I=1,TABSIZ
      IF(KTAB(I).EQ.-1)GOTO 2
      IF(INIT.GE.0.AND.KTAB(I)/1000.NE.INIT)GOTO 1
      IF(ATAB(I).EQ.HASH)GOTO 3
1     CONTINUE
      CALL BUG(21,ID,INIT)
2     VOCAB=-1
      IF(INIT.LT.0)RETURN
      CALL BUG(5,ID,INIT)
3     VOCAB=KTAB(I)
      IF(INIT.GE.0)VOCAB=MOD(VOCAB,1000)
      RETURN
      END
