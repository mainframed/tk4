         TITLE '* * *  ADVENTURE - GETDTM SUBROUTINE  * * *'
***********************************************************************
* SUBROUTINE GETDTM(DATE,TIME)                                        *
*    RETURNS IN BINARY:                                               *
*     DATE = JULIAN DATE                                              *
*     TIME = TIME IN HUNDRETHS OF SECONDS                             *
***********************************************************************
         SPACE 2
GETDTM   CSECT
         STM   R14,R12,12(R13)     SAVE REGS
         LR    R12,R15             GET BASE
         USING GETDTM,R12
         LR    R10,R1              SAVE @ PARM LIST
         LR    R11,R13             SAVE @ SVA
         LA    R13,SVA             GET MY SVA
         ST    R11,4(,R13)         CHAIN THEM
         ST    R13,8(,R11)
         SPACE
         TIME  BIN                 GET DATE/TIME
*  DATE IN R1 (PACKED), TIME IN R0 (BINARY)
         LM    R2,R3,0(R10)        GET @ OF PARMS
         ST    R0,0(,R3)           SET TIME INTO PARM
         XC    DWD,DWD             CLEAR WORK AREA
         ST    R1,DWD+4            STORE DATE
         CVB   R1,DWD              GET DATE IN R1
         ST    R1,0(,R2)           SET DATE INTO PARM
         SPACE 1
RETURN   DS    0H
         L     R13,4(,R13)         GET CALLERS SVA BACK
         LM    R14,R12,12(R13)     RESTORE REGS
         MVI   12(R13),X'FF'       SHOW CONTROL RETURNED FOR FORTRAN
         SLR   R15,R15             SET RETURN CODE
         BR    R14                 AND RETURN
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
DWD      DC    D'0'                DOUBLE WORD WORK AREA
SVA      DC    18F'0'              SAVE AREA
         SPACE
         LTORG
         END
