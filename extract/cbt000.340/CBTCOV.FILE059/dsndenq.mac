         LCLC  &OPSYSTM
&OPSYSTM SETC  'MVS'                   DEFINE OPERATING SYSTEM
*                                        MVT
*                                        SVS (OS/VS2 RELEASE 1)
*                                        MVS (OS/VS2 RELEASE 3)
         TITLE 'DISCLAIMER - INSTALLATION INSTRUCTIONS'
***********************************************************************
*                                                                     *
*     THIS PROGRAM, PROVIDED BY FPL, IS OF GENERAL INTEREST           *
*     SUBMITTED FOR UNRESTRICTED DISTRIBUTION.  THIS PROGRAM          *
*     HAS MET A BASIC SET OF PROGRAMMING AND DOCUMENTATION            *
*     STANDARDS, BUT MAY NOT HAVE BEEN PROGRAM TESTED IN ANY          *
*     FORMAL FASHION BY FPL.  THE USER IS EXPECTED TO MAKE THE        *
*     FINAL EVALUATION AS TO THE USEFILLNESS IN HIS OWN               *
*     ENVIRONMENT.                                                    *
*                                                                     *
*     FPL MAKES NO WARRANTY, EXPRESSED OR IMPLIED, INCLUDING, BUT     *
*     NOT LIMITED TO, THE IMPLIED WARRANTIES OR MERCHANTABILITY AND   *
*     FITNESS FOR A PARTICULAR PUTPOSE AS TO THE DOCUMENTATION,       *
*     FUNCTION, OR PERFORMANCE OF THESE PROGRAMS.                     *
*                                                                     *
*     ACCEPTANCE AND USE OF THIS PROGRAM CONSTITUTES A RELEASE        *
*     FROM LIABILITY OF FPL FOR ANY PROBLEMS USE OF THE PROGRAM       *
*     MAY CAUSE AT THE USER'S INSTALLATION.                           *
*                                                                     *
*     USERS ARE INVITED TO SUBMIT SUGGESTIONS OR ERROR DOCUMENTATION  *
*     TO FPL. HOWEVER, NO PROMISE CAN BE MADE THAT SUCH SUGGESTIONS   *
*     WILL BE IMPLEMENTED OR ERRORS CORRECTED.  SUBMIT COMMENTS TO:   *
*              COORDINATOR OF TECHNICAL SYSTEMS                       *
*              SYSTEMS & PROGRAMMING DEPARTMENT                       *
*              FLORIDA POWER & LIGHT COMPANY - GENERAL OFFICE         *
*              P. O. BOX  529100                                      *
*              MIAMI, FLORIDA  33152                                  *
*                                                                     *
*     THIS PROGRAM IS MADE AVAILABLE BY FPL WITHOUT CHARGE OR         *
*     CONSIDERATION.  RECIPIENTS ARE FREE TO MAKE THIS PROGRAM        *
*     AVAILABLE TO OTHERS IN LIKE MANNER.  IT MAY NOT BE SOLD.        *
*                                                                     *
* INSTALLATION INSTRUCTIONS                                           *
*                                                                     *
*     THIS MODULE MAY BE ASSEMBLED AND LINKED INTO A LIBRARY IN THE   *
*     LINK LIST OR SPECIFIED AS A STEPLIB IN THE TSO LOGON PROCEDURE. *
*     THE COMMAND MAY BE INVOKED FROM TSO BY ENTERING THE NAME OR AN  *
*     ALIAS OF THE MODULE.                                            *
*                                                                     *
*     NOTE -                                                          *
*        THIS MODULE HAS TWO ENTRY POINTS, DSN AND DENQ.  THE MODULE  *
*        MUST BE LINKED WITH THE NAMES DSN AND DENQ AS ALIASES (OR    *
*        WITH DENQ AS A TRUE NAME AND DSN AS AN ALIAS).               *
*                                                                     *
*     BEFORE ASSEMBLING THE MODULE, INSURE THAT ALL THE REQUIRED      *
*     MACROS ARE AVAILABLE.  SOME MACROS FROM SYS1.AMODGEN MAY BE     *
*     REQUIRED.  THE $ENTER MACRO USES SYSTEM VARIABLE SYMBOLS        *
*     &SYSDATE AND &SYSTIME.  THESE SYMBOLS ARE NOT SUPPORTED BY ALL  *
*     ASSEMBLERS.  IF THE ASSEMBLER TO BE USED DOES NOT SUPPORT THESE *
*     SYMBOLS, THE $ENTER MACRO MUST BE MODIFIED.                     *
*                                                                     *
*     THE SOURCE PROVIDED IS OPERATING SYSTEM DEPENDENT.  SPECIFY THE *
*     DESIRED OPERATING SYSTEM AS THE VALUE FOR &OPSYSTM.             *
*                                                                     *
*     THE MODULE MAY BE LINKED WITH ATTRIBUTES RENT AND REFR AND IS   *
*     ELIGIBLE FOR INCLUSION IN THE LINK PACK AREA.                   *
*                                                                     *
***********************************************************************
SS0804   TITLE 'TSO COMMAND TO DISPLAY ENQUEUES'
***********************************************************************
*                                                                     *
* SS0804       ALIAS DENQ                                             *
*              ALIAS DSN                                              *
*                                                                     *
* FUNCTION     DENQ  DISPLAY THE ENQUEUES ON A SPECIFIED              *
*                    QNAME/RNAME COMBINATION.                         *
*                                                                     *
*              DSN   DISPLAY JOBS OR TSO USERS USING A SPECIFIED      *
*                    DATA SET.  EXCLUSIVE CONTROL REQUESTS ARE        *
*                    IDENTIFIED.                                      *
*                                                                     *
* SYNTAX       DENQ  QNAME(NAME1) RNAME(NAME2)                        *
*                                                                     *
*              DEFAULTS - QNAME(SYSIEFSD), RNAME(Q4)                  *
*              REQUIRED - NONE                                        *
*                                                                     *
*              QNAME  SPECIFIES THE MAJOR QUEUE NAME TO BE            *
*                     DISPLAYED.  ALL MAY BE SPECIFIED TO             *
*                     DISPLAY ALL MAJOR QUEUES.                       *
*                                                                     *
*              RNAME  SPECIFIES THE MINOR QUEUE NAME TO BE            *
*                     DISPLAYED.  ALL MAY BE SPECIFIED TO             *
*                     DISPLAY ALL MINOR QUEUES.                       *
*                                                                     *
*              THE DEFAULT NAMES ARE SYSIEFSD/Q4, ON WHICH EACH       *
*              JOB STEP AND TSO LOGON ARE ENQUEUED IN MVT AND SVS     *
*              SYSTEMS.  THIS ENQUEUE IS A MAJOR BOTTLENECK IN        *
*              THESE SYSTEMS.                                         *
*                                                                     *
*                                                                     *
*              DSN   DATA-SET-NAME                                    *
*                                                                     *
*              DEFAULTS - DATA-SET-NAME DEFAULTS TO THE PREFIX        *
*                         (USERID FOR MVT AND SVS, PREFIX             *
*                         SPECIFIED IN THE PROFILE FOR MVS).          *
*              REQUIRED - NONE                                        *
*                                                                     *
*              DATA-SET-NAME SPECIFIES THE NAME OF THE DATA           *
*                            SET. STANDARD TSO NAMING                 *
*                            CONVENTIONS ARE USED.                    *
*                                                                     *
* RETURN CODES                                                        *
*                                                                     *
*              ALWAYS 4                                               *
*                                                                     *
* FPL MACROS USED                                                     *
*                                                                     *
*              $ENTER         ENTRY LINKAGE                           *
*              $RTRN          EXIT LINKAGE                            *
*              #TSOBLKS       CONSTRUCT TSO CONTROL BLOCKS            *
*              #TSOMSG        CONSTRUCT MESSAGE IN PUTLINE FORMAT     *
*                                                                     *
* IBM MACROS USED                                                     *
*                                                                     *
*              LINK           INVOKE SERVICE ROUTINES                 *
*              PUTLINE        OUTPUT A LINE TO THE TERMINAL           *
*              IKJPARM        BEGIN PARSE CONTROL LIST                *
*              IKJKEYWD       DESCRIBE KEYWORD OPERAND                *
*              IKJNAME        DESCRIBE A POSSIBLE KEYWORD NAME        *
*              IKJSUBF        BEGIN A KEYWORD SUBFIELD DESCRIPTION    *
*              IKJPOSIT       DESCRIBE A POSITIONAL PARAMETER         *
*              IKJIDENT       DESCRIBE A POSITIONAL PARAMETER         *
*              IKJENDP        END PARSE CONTROL LIST                  *
*              IKJUPT         MAP USER PROFILE TABLE                  *
*                                                                     *
* SERVICE ROUTINES USED                                               *
*                                                                     *
*              PARSE          PROCESS COMMAND OPERANDS                *
*              PUTLINE        OUTPUT MESSAGES TO THE TERMINAL         *
*                                                                     *
* ATTRIBUTES   RENT, REUS, REFR, ENABLED, NON-PRIVELEGED              *
*                                                                     *
*                                                 9/76 GORDON P. WEST *
***********************************************************************
         TITLE 'DENQ ENTRY CODING'
DENQ     $ENTER BASE=R12,              BASE REGISTER IS R12            X
               RENT=DSECTLEN,          RE-ENTRANT, WORK AREA LENGTH    X
               SAVE=DENQSAVE           NAME OF SAVE AREA
         MVI   FLAGS,X'00'             SET ENTRY INDICATOR
         B     COMMON                  BRANCH TO COMMON CODE
         TITLE 'DSN ENTRY CODING'
DSN      $ENTER BASE=R12,              BASE REGISTER IS R12            X
               RENT=DSECTLEN,          RE-ENTRANT, WORK AREA LENGTH    X
               SAVE=DENQSAVE,          NAME OF SAVE AREA               X
               CSECT=NO                DO NOT DEFINE CSECT
         S     R12,=A(DSN-DENQ)        ADJUST BASE TO RELATIVE ZERO
         USING DENQ,R12
         MVI   FLAGS,X'01'             SET ENTRY INDICATOR
         TITLE 'CONSTRUCT TSO CONTROL BLOCKS'
***********************************************************************
*                                                                     *
*        CONSTRUCT TSO CONTROL BLOCKS                                 *
*                                                                     *
***********************************************************************
         SPACE 2
COMMON   #TSOBLKS DSECT,                                               X
               ECB=DENQECB,                                            X
               CPPL=(R11,R1),                                          X
               IOPL=(R10,DENQIOPL,DENQIOPB),                           X
               PPL=(R9,DENQPPL,DENQPCL,DENQANS)
         MVC   DENQIOPB(IOPBLEN),MODLIOPB   SET UP IOPB
*--------SET UP OUTPUT LINE HEADER
         MVC   DENQHEDR(HEDRLEN),MODLHEDR   SET UP HEADER
         MVC   MSG,BLANKS              CLEAR MESSAGE AREA
         TM    FLAGS,X'01'             TEST FOR DENQ COMMAND
         BZ    PARSE
         L     R1,=V(DSNPCL)           SET UP DSN COMMAND PCL
         ST    R1,PPLPCL
         TITLE 'PARSE COMMAND OPERANDS'
***********************************************************************
*                                                                     *
*        PARSE COMMAND OPERANDS                                       *
*                                                                     *
***********************************************************************
         SPACE 2
PARSE    LA    R1,PPL                  LOAD PPL ADDRESS
         LINK  EP=IKJPARS              LINK TO PARSE
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   ERRPARS
         L     R9,DENQANS              LOAD ANSWER AREA ADDRESS
         TM    FLAGS,X'01'             TEST FOR DSN COMMAND
         BNZ   DSN1
         USING DENQPDL,R9              ESTABLISH ADDRESSABILITY
         LA    R8,QNAM                 LOAD QNAME PDL ADDRESS
         LA    R9,RNAM                 LOAD RNAME PDL ADDRESS
         LH    R14,4(R8)               LOAD QNAME LENGTH
         BCTR  R14,0                   REDUCE BY 1
         LA    R1,MAJQNAME             LOAD OUTPUT AREA ADDRESS
         MVC   MAJQNAME,=CL8' '        BLANK OUT AREA
         L     R15,0(R8)               LOAD QNAME ADDRESS
         EX    R14,MOVE15T1            COPY QNAME
         B     LOADCVT
         USING DSNPDL,R9               ADDRESSABILITY FOR DSN PDL
DSN1     MVC   MAJQNAME,=CL8'SYSDSN'   SET MAJOR QUEUE NAME
         LA    R9,DSNAME               DSNAME PDE
         TITLE 'CONSTRUCT DATA SET NAME'
***********************************************************************
*                                                                     *
*        CONSTRUCT DATA SET NAME                                      *
*                                                                     *
***********************************************************************
         SPACE 2
         MVC   DSNAM,BLANKS            BLANK OUT DSNAME FIELD
         SR    R0,R0                   ZERO DSNAME LENGTH
         LA    R1,DSNAM                LOAD DSNAME FIELD ADDRESS
         ST    R1,DSNPDE               STORE ADDRESS IN FAKE PDE
         TM    6(R9),X'40'             SEE IF NAME ENCLOSED IN QUOTES
         BNZ   MOVEREST
*--------APPEND PREFIX TO DATA SET NAME
         AIF   ('&OPSYSTM' NE 'MVS').PSCB
         L     R14,CPPLUPT             LOAD UPT ADDRESS
         USING UPT,R14                 UPT ADDRESSABILITY
         LA    R15,UPTPREFX            LOAD PREFIX ADDRESS
         CLI   UPTPREFL,0              TEST FOR NOPREFIX
         BNE   GETLEN
         NC    4(2,R9),4(R9)           SEE IF DSNAME ENTERED
         BNE   MOVEREST
         DROP  R14
.PSCB    L     R15,CPPLPSCB            LOAD USERID ADDRESS
GETLEN   SR    R14,R14
         IC    R14,7(R15)              LOAD LENGTH OF PREFIX
         AR    R0,R14                  ADD TO DSNAME LENGTH
         BCTR  R14,0                   REDUCE LENGTH BY 1
         EX    R14,MOVE15T1            COPY PREFIX
         LA    R1,1(R14,R1)            INCREMENT TO END OF NAME
         MVI   0(R1),C'.'              ADD .
         LA    R1,1(R1)                INCREMENT PAST .
         A     R0,=F'1'                ADD 1 TO LENGTH
         NC    4(2,R9),4(R9)           SEE IF DSNAME ENTERED
         BZ    STORELEN
MOVEREST L     R15,0(R9)               LOAD ADDRESS OF INPUT
         LH    R14,4(R9)               LOAD LENGTH OF INPUT
         LTR   R14,R14                 TEST FOR ZERO
         BZ    STORELEN
         AR    R0,R14                  ADD TO DSNAME LENGTH
         BCTR  R14,0                   REDUCE BY 1
         EX    R14,MOVE15T1            COPY NAME
STORELEN STH   R0,DSNLEN               SAVE DSNAME LENGTH
         LA    R9,DSNPDE               POINT TO FAKE PDE
         DROP  R9
         TITLE 'FIND QUEUE CONTROL BLOCK'
***********************************************************************
*                                                                     *
*        FIND QUEUE CONTROL BLOCK (QCB)                               *
*                                                                     *
***********************************************************************
         SPACE 2
LOADCVT  L     R2,CVTPTR               CVT ADDRESS
         AIF   ('&OPSYSTM' EQ 'MVS').MVSQCB
         L     R2,200(R2)              LOAD SECONDARY CVT ADDRESS
         L     R2,20(R2)               LOAD DUMMY QCB ADDRESS
         L     R2,0(R2)                LOAD FIRST MAJOR QCB ADDRESS
         AGO   .QCB
.MVSQCB  L     R2,CVTFQCB(R2)          ADDRESS OF FIRST MAJOR QCB
.QCB     ANOP
QCBNEXT  LTR   R2,R2                   TEST FOR END OF CHAIN
         BZ    RETURN
         ST    R2,QNAME                SAVE QNAME ADDRESS
         CLC   MAJQNAME,=CL8'ALL'      TEST FOR ALL
         BE    THISQ
         CLC   MAJQNAME,MAJNAME(R2)    COMPARE QNAMES
         BNE   NEXTMAJ2
         TITLE 'PROCESS A MAJOR QUEUE NAME'
***********************************************************************
*                                                                     *
*        THIS MAJOR QUEUE IS TO BE PROCESSED                          *
*                                                                     *
***********************************************************************
         SPACE 2
THISQ    MVC   MAJQUEUE(8),MAJNAME(R2) COPY QNAME
         MVI   MAJQUEUE+8,C'/'         INSERT /
FINDRNAM L     R3,MAJFMIN(R2)          ADDRESS OF FIRST MINOR QCB
NEXTRNAM LTR   R3,R3                   TEST FOR END OF CHAIN
         BZ    NEXTMAJ
         ST    R3,RNAME                SAVE RNAME ADDRESS
         SR    R4,R4                   GET LENGTH OF RNAME
         IC    R4,MINNAMEL(R3)
         L     R1,0(R9)                LOAD R NAME ADDRESS
         LH    R15,4(R9)               LOAD R NAME LENGTH
         CH    R15,=H'3'               TEST FOR LENGTH OF 3
         BNE   CHEKAGN
         CLC   0(3,R1),=C'ALL'         TEST FOR ALL
         BE    THISR
CHEKAGN  CR    R15,R4                  COMPARE RNAME LENGTHS
         BH    NEXTMIN
         BCTR  R15,0                   REDUCE LENGTH BY 1
         EX    R15,COMPR               COMPARE RNAME
         BNE   NEXTMIN
         TITLE 'PROCESS A MINOR QUEUE NAME'
***********************************************************************
*                                                                     *
*        THIS MINOR QUEUE TO BE PROCESSED                             *
*                                                                     *
***********************************************************************
         SPACE 2
THISR    TM    FLAGS,X'01'             TEST FOR DSN COMMAND
         BNZ   DSN2
         MVC   MSG(9),MAJQUEUE         MOVE QNAME INTO MESSAGE
         B     TESTLEN
DSN2     MVC   MSG(9),=C'DSNAME = '    MOVE IN HEADER DATA
TESTLEN  LA    R1,50                   MAXIMUM LENGTH TO BE PROCESSED
         CR    R4,R1                   TEST AGAINST MAXIMUM RNAME LEN
         BL    MOVRNAME
         LR    R4,R1                   SET LENGTH TO MAXIMUM
MOVRNAME BCTR  R4,0                    REDUCE LENGTH BY 1
         EX    R4,MOVERNAM             COPY RNAME
         PUTLINE OUTPUT=(DENQHEDR,,,DATA),MF=(E,IOPL)
         MVC   MSG,BLANKS              BLANK MESSAGE AREA
         L     R3,MINFQEL(R3)          LOAD FIRST QEL ADDRESS
         B     SHR
         SPACE 2
NEXTQEL  L     R3,QELNQEL(R3)          LOAD ADDRESS NEXT QEL
         LTR   R3,R3                   TEST FOR END OF CHAIN
         BZ    NEXTMIN
SHR      BAL   R6,FINDJN               GET JOB NAME
         TM    QELQFLGS(R3),QELSHARE   TEST FOR EXCLUSIVE ENQ
         BO    SHRIT
         MVC   ENQ,=C'EXCLUSIVE'       MOVE IN 'EXCLUSIVE'
SHRIT    PUTLINE OUTPUT=(DENQHEDR,,,DATA),MF=(E,IOPL)
         MVC   MSG,BLANKS              BLANK MESSAGE AREA
         OI    FLAGS,X'06'             INDICATE ENQ DISPLAYED
         B     NEXTQEL
         TITLE 'INCREMENT TO NEXT MINOR QUEUE NAME'
***********************************************************************
*                                                                     *
*        INCREMENT TO NEXT MINOR QUEUE                                *
*                                                                     *
***********************************************************************
         SPACE 2
NEXTMIN  L     R3,RNAME                LOAD PREVIOUS RNAME ADDRESS
         L     R3,MINNMIN(R3)          LOAD NEXT RNAME ADDRESS
         B     NEXTRNAM
         TITLE 'INCREMENT TO NEXT MAJOR QUEUE NAME'
***********************************************************************
*                                                                     *
*        INCREMENT TO NEXT MAJOR QUEUE                                *
*                                                                     *
***********************************************************************
         SPACE 2
NEXTMAJ  L     R2,QNAME                LOAD LAST QNAME ADDRESS
         L     R3,RNAME                LOAD LAST RNAME ADDRESS
         TM    FLAGS,X'02'             SEE IF ENQUEUE DISPLAYED
         BNZ   NEXTMAJ2
         TM    FLAGS,X'01'             TEST FOR DSN
         BZ    DENQ1
         MVC   MSG(44),DSNAM           MOVE DSNAME INTO MESSAGE
         LH    R1,DSNLEN               LOAD LENGTH OF DSNAME
         LA    R1,MSG(R1)              POINT TO END OF DSNAME
         MVC   0(14,R1),=CL14' NOT ALLOCATED'  COMPLETE MESSAGE
         PUTLINE OUTPUT=(DENQHEDR,,,DATA),MF=(E,IOPL)
         MVC   MSG,BLANKS              CLEAR MESSAGE AREA
         B     NEXTMAJ2
DENQ1    LH    R14,4(R9)               LOAD RNAME LENGTH
         CH    R14,=H'3'               TEST LENGTH FOR 3
         BNE   DENQ2
         CLC   0(3,R1),=C'ALL'         TEST FOR ALL
         BE    NEXTMAJ2
DENQ2    BCTR  R14,0                   REDUCE LENGTH BY 1
         MVC   MSG(37),=CL37'NO ENQUEUES FOR QNAME=         RNAME='
         L     R15,0(R9)               LOAD RNAME ADDRESS
         LA    R1,MSG+37               LOAD DESTINATION ADDRESS
         EX    R14,MOVE15T1            MOVE RNAME INTO MESSAGE
         MVC   MSG+22(8),MAJNAME(R2)   MOVE QNAME INTO MESSAGE
         PUTLINE OUTPUT=(DENQHEDR,,,DATA),MF=(E,IOPL)
         MVC   MSG,BLANKS              CLEAR MESSAGE AREA
         OI    FLAGS,X'04'             SUPPRESS NO QNAME MESSAGE
NEXTMAJ2 L     R2,MAJNMAJ(R2)          LOAD NEXT QNAME ADDRESS
         B     QCBNEXT
         TITLE 'EXIT CODING'
***********************************************************************
*                                                                     *
*        RETURN                                                       *
*                                                                     *
***********************************************************************
         SPACE 2
RETURN   L     R2,QNAME                LOAD LAST QNAME ADDRESS
         TM    FLAGS,X'05'             TEST FOR DSN OR ENQ DISPLAYED
         BNZ   OUT
         MVC   MSG(22),=CL22'NO ENQUEUES FOR QNAME='
         CLC   MAJQNAME,=CL8'ALL'      TEST FOR ALL MAJOR QUEUES
         BNE   COPY
         MVC   MSG+22(8),MAJNAME(R2)     COPY QNAME INTO MESSAGE
         B     DISPLAY
COPY     MVC   MSG+22(8),MAJQNAME        COPY QNAME INTO MESSAGE
DISPLAY  PUTLINE OUTPUT=(DENQHEDR,,,DATA),MF=(E,IOPL)
         MVC   MSG,BLANKS              BLANK OUTPUT AREA
OUT      $RTRN RENT=DSECTLEN,RC=4      RETURN
         TITLE 'FIND JOB OR USER NAME'
***********************************************************************
*                                                                     *
*        FINDJN    -    FINDS THE JOB NAME AND MOVES IT               *
*                       TO A SPECIFIED LOCATION                       *
*                                                                     *
*        UPON ENTRY     REG. 3  -  ADDRESS OF QEL                     *
*                       REG. 6  -  RETURN REG.                        *
*                                                                     *
*                       REG. 1  -  WORK REG.                          *
*                       REG. 4  -  WORK REG.                          *
*                       REG. 5  -  WORK REG.                          *
*                       REG. 7  -  WORK REG.                          *
*                                                                     *
***********************************************************************
         SPACE 4
FINDJN   EQU   *
         AIF   ('&OPSYSTM' EQ 'MVS').MVSFIND
         SR    R7,R7
         CLI   12(R3),X'00'
         BE    FJN1
         IC    R7,12(R3)               TJID
         B     GETID
         SPACE 2
FJN1     L     R1,8(R3)                LOAD TCB ADDRESS
         L     R4,132(R1)              CHECK FOR INIT
         CLI   28(R4),0
         BNZ   FJN15
         L     R1,136(R1)              LAST TCB ON QUEUE
         LTR   R1,R1
         BNZ   FJN15
         L     R1,8(R3)
FJN15    L     R4,180(R1)              LOAD JSCB ADDRESS
         CLI   269(R4),X'00'           CHECK FOR TSO USER
         BE    FJN2                    IN CORE
         IC    R7,269(R4)              TJID
         B     GETID
         SPACE 2
FJN2     MVC   TYPE,=C'JOB '
         L     R1,12(R1)               LOAD TIOT ADDRESS
         MVC   JOBNAME,0(R1)           COPY JOBNAME
         MVC   STEPNAME,16(R1)         COPY STEPNAME
         MVC   PROCSTEP,8(R1)          COPY PROCSTEP NAME
GOHOME   BR    R6
         SPACE 2
GETID    L     R1,16                   ADDRESS OF CVT
         L     R1,228(R1)              ADDRESS OF TSCVT
         LH    R5,26(R1)               SIZE OF TJB
         MR    R4,R7
         L     R4,0(R1)
         AR    R4,R5                   ADDRESS OF TJB FOR TJID
         MVC   TYPE,=C'USER'
         MVC   JOBNAME,28(R4)          COPY USERID
         B     GOHOME
         AGO   .ENDFIND
.MVSFIND LH    R7,QELASID(R3)          ASID OF USER MAKING ENQ
         L     R1,CVTPTR               ADDRESS OF CVT
         L     R1,CVTASVT(R1)          ADDRESS SPACE VECTOR TABLE
         BCTR  R7,0                    ASID-1
         SLA   R7,2                    4X(ASID-1)
         AR    R1,R7                   USER'S ASVT ENTRY
         L     R1,ASVTENTY(R1)         ASCB ADDRESS
         L     R5,ASCBCSCB(R1)         CSCB ADDRESS
         LTR   R5,R5                   TEST CSCB ADDRESS
         BZ    SYSTEM
         CLI   CHTRKID(R5),CHTSID      TSO USER
         BNE   FJN1                    NO
         MVC   TYPE,=C'USER'           YES
         MVC   JOBNAME,CHKEY(R5)       MOVE USERID
GOHOME   BR    R6
FJN1     MVC   TYPE,=C'JOB '
FJN2     MVC   JOBNAME,CHKEY(R5)       MOVE JOB NAME OR INIT NAME
         MVC   STEPNAME,CHSTEP(R5)     MOVE STEP NAME
         MVC   PROCSTEP,CHPROCSN(R5)   MOVE NAME OF STEP INVOKING PROC
         CLI   CHTRKID(R5),CHJOBID     CHECK FOR A JOB
         BE    GOHOME
*--------DISPLAY JOB NAME, NOT INITIATOR NAME
FJN3     L     R5,CHPTR(R5)            LOAD ADDRESS OF NEXT CSCB
         LTR   R5,R5                   TEST FOR LAST CSCB
         BZ    GOHOME
         CLC   QELASID(2,R3),CHASID(R5)     CSCB FOR USER MAKING ENQ?
         BE    FJN2
         B     FJN3
*--------CSCB ADDRESS IS ZERO
SYSTEM   MVC   TYPE,=C'SYS '           INDICATE THAT THE OPERATING
         B     GOHOME                       SYSTEM HAS THE DATA SET
.ENDFIND ANOP
         TITLE 'ERRORS'
***********************************************************************
*                                                                     *
*        ERRORS                                                       *
*                                                                     *
***********************************************************************
         SPACE 2
*--------PARSE FAILURE
ERRPARS  PUTLINE OUTPUT=(ERRMSG1,,,DATA),MF=(E,IOPL)
         B     RETURN
         TITLE 'CONSTANT DATA'
***********************************************************************
*                                                                     *
*        CONSTANT DATA                                                *
*                                                                     *
***********************************************************************
         SPACE 2
         LTORG
         SPACE 2
MOVE15T1 MVC   0(0,R1),0(R15)          COPY FROM R15 TO R1
MOVERNAM MVC   MSG+9(0),MINNAME(R3)    COPY RNAME
COMPR    CLC   0(0,R1),MINNAME(R3)     COMPARE RNAMES
ERRMSG1  #TSOMSG 'UNABLE TO INTERPRET COMMAND'
BLANKS   DC    CL90' '
MODLIOPB PUTLINE MF=L
IOPBLEN  EQU   *-MODLIOPB
MODLHEDR DC    H'94,0'
HEDRLEN  EQU   *-MODLHEDR
         TITLE 'EQUATES'
***********************************************************************
*                                                                     *
*        EQUATES                                                      *
*                                                                     *
***********************************************************************
*
*  TAG         VALUE          MACRO    MEANING
*
CVTPTR   EQU   16                      POINTER TO CVT
         AIF   ('&OPSYSTM' EQ 'MVS').MVSEQU
MAJNMAJ  EQU   0                       NEXT MAJOR QCB ADDRESS
MAJFMIN  EQU   8                       FIRST MINOR QCB ADDRESS
MAJNAME  EQU   12                      MAJOR QUEUE NAME
MINFQEL  EQU   0                       FIRST QEL ADDRESS
MINNMIN  EQU   8                       NEXT MINOR QCB ADDRESS
MINNAMEL EQU   12                      MINOR QUEUE NAME LENGTH
MINNAME  EQU   14                      MINOR QUEUE NAME
QELNQEL  EQU   0                       NEXT QEL ADDRESS
QELQFLGS EQU   4                       FLAGS
QELSHARE EQU   X'80'
*                1... ....               SHARE
*                0... ....               EXCLUSIVE
         AGO   .ENDEQU
.MVSEQU  ANOP
CVTASVT  EQU   556            CVT      ADDRESS OF ASVT
CVTFQCB  EQU   640            CVT      ADDRESS OF FIRST QCB
ASVTENTY EQU   528            IHAASVT  ADDRESS OF FIRST ASVT ENTRY
ASCBCSCB EQU   56             IHAASCB  ADDRESS OF CSCB
CHPTR    EQU   0              IEECHAIN POINTER TO NEXT CSCB
CHKEY    EQU   8              IEECHAIN JOBNAME
CHTRKID  EQU   28             IEECHAIN FLAGS
CHTSID   EQU   X'01'                     USER IS A TSO SESSION
CHJOBID  EQU   X'02'                     USER IS A JOB
CHASID   EQU   30             IEECHAIN ASID OF CSCB
CHPROCSN EQU   32             IEECHAIN PROC STEPNAME
CHSTEP   EQU   64             IEECHAIN STEPNAME
MAJNMAJ  EQU   0              IHAQCB   NEXT MAJOR QCB ADDRESS
MAJFMIN  EQU   8              IHAQCB   FIRST MINOR QCB ADDRESS
MAJNAME  EQU   16             IHAQCB   MAJOR QUEUE NAME
MINFQEL  EQU   8              IHAQCB   FIRST QEL ADDRESS
MINNMIN  EQU   0              IHAQCB   NEXT MINOR QCB ADDRESS
MINNAMEL EQU   16             IHAQCB   MINOR QUEUE NAME LENGTH
MINNAME  EQU   20             IHAQCB   MINOR QUEUE NAME
QELNQEL  EQU   0              IHAQEL   NEXT QEL ADDRESS
QELQFLGS EQU   12             IHAQEL   FLAGS
QELSHARE EQU   X'80'          IHAQEL
*                1... ....               SHARE
*                0... ....               EXCLUSIVE
QELASID  EQU   14             IHAQEL   ASID OF USER MAKING ENQ
.ENDEQU  ANOP
         TITLE 'PARSE PARAMETER CONTROL LISTS'
***********************************************************************
*                                                                     *
*        PARSE PARAMETER LISTS                                        *
*                                                                     *
***********************************************************************
         EJECT
DENQPCL  IKJPARM  DSECT=DENQPDL
Q1       IKJKEYWD DEFAULT='QNAME'
         IKJNAME  'QNAME',SUBFLD=Q2
R01      IKJKEYWD DEFAULT='RNAME'
         IKJNAME  'RNAME',SUBFLD=R02
Q2       IKJSUBF
QNAM     IKJIDENT 'QNAME',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,        X
               DEFAULT='SYSIEFSD'
R02      IKJSUBF
RNAM     IKJIDENT 'RNAME',MAXLNTH=50,FIRST=ALPHA,OTHER=ANY,            X
               DEFAULT='Q4'
         IKJENDP
         SPACE 2
DSNPCL   IKJPARM  DSECT=DSNPDL
DSNAME   IKJPOSIT DSNAME
         IKJENDP
         TITLE 'WORK AREA'
***********************************************************************
*                                                                     *
*        WORK AREA                                                    *
*                                                                     *
***********************************************************************
         SPACE 2
DENQD    DSECT
DENQSAVE DS    18F                     SAVE AREA
DENQHEDR DS    H,H                     HEADER
MSG      DS    0CL90                   MESSAGE AREA
         DS    4X
TYPE     DS    CL4                     JOB/USER
         DS    2X
JOBNAME  DS    CL8                     JOBNAME/USERID
         DS    2X
STEPNAME DS    CL8                     STEPNAME
         DS    2X
PROCSTEP DS    CL8                     PROC STEPNAME
         DS    2X
ENQ      DS    CL9
         DS    41X
DENQECB  DS    F
DENQIOPL DS    0F
         ORG   *+IOPLLEN
DENQPPL  DS    0F
         ORG   *+PPLLEN
DENQANS  DS    A
MAJQUEUE DS    CL9                     QNAME/
MAJQNAME DS    CL8                     MAJOR QUEUE NAME SELECTED
QNAME    DS    A                       ADDR OF LAST MAJOR Q PROCESSED
RNAME    DS    A                       ADDR OF LAST MINOR Q PROCESSED
DSNPDE   DS    A(DSNAM)                POINTER TO DSNAME
DSNLEN   DS    H                       LENGTH OF DSNAME
         DS    H                       NOT USED
DSNAM    DS    CL44                    DSNAME
FLAGS    DS    X                       FLAGS
*                 .... ...0              DENQ
*                 .... ...1              DSN
*                 .... ..1.              ENQUEUE DISPLAYED FOR RNAME
*                 .... .1..              ENQUEUE DISPLAYED FOR QNAME
*                 XXXX X...              NOT USED
DENQIOPB PUTLINE MF=L
DSECTLEN EQU   *-DENQD
         EJECT
         IKJUPT
         END
