         TITLE 'DISCLAIMER - INSTALLATION INSTRUCTIONS'
***********************************************************************
*                                                                     *
*     THIS PROGRAM, DEVELOPED AT FPL OR SUPPLIED BY OTHER USERS       *
*     ON A NON-RESTRICTIVE BASIS, IS OF GENERAL INTEREST              *
*     SUBMITTED FOR UNRESTRICTED DISTRIBUTION.  THIS PROGRAM          *
*     HAS MET A BASIC SET OF PROGRAMMING AND DOCUMENTATION            *
*     STANDARDS, BUT MAY NOT HAVE BEEN PROGRAM TESTED IN ANY          *
*     FORMAL FASHION BY FPL.  THE USER IS EXPECTED TO MAKE THE        *
*     FINAL EVALUATION AS TO THE USEFULLNESS IN HIS OWN               *
*     ENVIRONMENT.                                                    *
*                                                                     *
*     FPL MAKES NO WARRANTY, EXPRESSED OR IMPLIED, INCLUDING, BUT     *
*     NOT LIMITED TO, THE IMPLIED WARRANTIES OR MERCHANTABILITY AND   *
*     FITNESS FOR A PARTICULAR PUTPOSE AS TO THE DOCUMENTATION,       *
*     FUNCTION, OR PERFORMANCE OF THESE PROGRAMS.                     *
*                                                                     *
*     ACCEPTANCE AND USE OF THIS PROGRAM CONSTITUTES A RELEASE        *
*     FROM LIABILITY OF FPL FOR ANY PROBLEMS USE OF THE PROGRAM       *
*     MAY CAUSE AT THE USER'S INSTALLATION.                           *
*                                                                     *
*     USERS ARE INVITED TO SUBMIT SUGGESTIONS OR ERROR DOCUMENTATION  *
*     TO FPL, HOWEVER, NO PROMISE CAN BE MADE THAT SUCH SUGGESTIONS   *
*     WILL BE IMPLEMENTED OR ERRORS CORRECTED.  SUBMIT COMMENTS TO:   *
*              COORDINATOR OF TECHNICAL SYSTEMS                       *
*              SYSTEMS & PROGRAMMING DEPARTMENT                       *
*              FLORIDA POWER & LIGHT COMPANY - GENERAL OFFICE         *
*              P. O. BOX  013100                                      *
*              MIAMI, FLORIDA  33101                                  *
*                                                                     *
*     THIS PROGRAM IS MADE AVAILABLE BY FPL WITHOUT CHARGE OR         *
*     CONSIDERATION.  RECIPIENTS ARE FREE TO MAKE THIS PROGRAM        *
*     AVAILABLE TO OTHERS IN LIKE MANNER.  IT MAY NOT BE SOLD.        *
*                                                                     *
* INSTALLATION INSTRUCTIONS                                           *
*                                                                     *
*     THIS MODULE MAY BE ASSEMBLED AND LINKED INTO A LIBRARY IN THE   *
*     LINK LIST OR SPECIFIED AS A STEPLIB IN THE EXECUTION JCL.       *
*                                                                     *
*     BEFORE ASSEMBLING THE MODULE, INSURE THAT ALL THE REQUIRED      *
*     MACROS ARE AVAILABLE.  SOME MACROS FROM SYS1.AMODGEN MAY BE     *
*     REQUIRED.                                                       *
*                                                                     *
*     SYSTEM VARIABLE SYMBOLS &SYSDATE AND &SYSTIME ARE USED IN THE   *
*     $ENTER MACRO AND PUNCH STATEMENT.  THESE SYMBOLS ARE NOT        *
*     SUPPORTED BY ALL ASSEMBLERS.  IF THE ASSEMBLER TO BE USED       *
*     DOES NOT SUPPORT THESE SYMBOLS, THEY MUST BE REMOVED FROM       *
*     $ENTER AND THE PUNCH STATEMENT.                                 *
*                                                                     *
*     AN IDENTIFICATION BLOCK IS PRINTED TO THE MESSAGE OUTPUT        *
*     DATA SET.  THE PROGRAM NAME AND DATE AND TIME OF ASSEMBLY       *
*     ARE OBTAINED FROM THE ENTRY CODE GENERATED BY $ENTER.  IF       *
*     &SYSDATE AND &SYSTIME ARE REMOVED FROM $ENTER, THE              *
*     IDENTIFICATION CODE MUST ALSO BE MODIFIED.                      *
*                                                                     *
***********************************************************************
SS0300   TITLE 'LISTDATA PROGRAM - VERSION 3'
***********************************************************************
*                                                                     *
* SS0300       ALIAS LISTDATA                                         *
*                                                                     *
* FUNCTION     PROVIDE THE CAPABILITY TO LIST OR PUNCH ALL OR         *
*              SELECTED MEMBERS OF A PARTITIONED DATA SET (PDS) IN    *
*              ALPHABETICAL ORDER.                                    *
*                                                                     *
* DESCRIPTION  THE USER MAY SPECIFY THAT THE PROGRAM IS TO:           *
*              1.   PRINT THE MEMBERS WITH EACH MEMBER STARTING ON A  *
*                   NEW PAGE.                                         *
*              2.   PRINT THE MEMBERS WITH 3 BLANK LINES BETWEEN EACH *
*                   MEMBER.                                           *
*              3.   PUNCH (OR SPOOL TO A DATA SET) MEMBERS.           *
*              4.   PUNCH OR SPOOL MEMBERS WITH A ./ ADD IEBUPDTE     *
*                   CONTROL RECORD SEPARATING EACH MEMBER.            *
*              5.   PUNCH OR SPOOL MEMBERS WITH ./ ADD AND ./ NUMBER  *
*                   IEBUPDTE CONTROL RECORDS SEPARATING EACH MEMBER.  *
*              6.   SPOOL MEMBERS INTO A HASP OR JES2 INTERNAL        *
*                   READER.                                           *
*                                                                     *
*              THE USER MAY SELECT OR EXCLUDE MEMBERS TO BE PROCESSED *
*              BY SPECIFYING THE MEMBER NAMES.  GROUPS OF MEMBERS     *
*              HAVING THE SAME STARTING CHARACTERS IN THEIR NAMES MAY *
*              BE SELECTED OR EXCLUDED BY SPECIFYING THE COMMON       *
*              PREFIX.                                                *
*                                                                     *
* ATTRIBUTES   ASSEMBLY LANGUAGE.                                     *
*              NOT AUTHORIZED                                         *
*              NOT REUSABLE                                           *
*              NOT REENTRANT                                          *
*              NOT REFRESHABLE                                        *
*                                                                     *
* ENVIRONMENT  IBM 360/370                                            *
*              MVT/SVS/MVS                                            *
*              HASP/JES2    (FOR INTRDR OPTION)                       *
*                                                                     *
* INPUT        INPUT CONSISTS OF:                                     *
*                                                                     *
*              1.   A PARTITIONED DATA SET.                           *
*                                                                     *
*              2.   A SEQUENTIAL CONTROL DATA SET CONTAINING UTILITY  *
*                   CONTROL STATEMENTS.                               *
*                                                                     *
* OUTPUT       OUTPUT CONSISTS OF:                                    *
*                                                                     *
*              1.   A SEQUENTIAL DATA SET CONTAINING THE LISTINGS OF  *
*                   THE MEMBERS SELECTED, CARD IMAGES OF THE MEMBERS, *
*                   OR INPUT TO A HASP OR JES2 INTERNAL READER.       *
*                                                                     *
*              2.   A MESSAGE DATA SET CONTAINING THE INPUT CONTROL   *
*                   RECORDS, DIAGNOSTIC MESSAGES, AND A LISTING OF    *
*                   THE NAMES OF THE MEMBERS PROCESSED.  IF A LISTING *
*                   OF THE MEMBERS WAS SELECTED, THE MESSAGE DATA SET *
*                   WILL CONTAIN THE PAGE NUMBERS FOR EACH MEMBER.    *
*                                                                     *
*                   COUNTS OF THE MEMBERS AND ALIASES IN THE LIBRARY  *
*                   AND PROCESSED ARE PROVIDED.  IF ENTRIES EXIST     *
*                   WHICH HAVE ALIASES, BUT NO TRUE NAMES, THE FIRST  *
*                   ALIAS FOR EACH ENTRY IS COUNTED AS A MEMBER, AND  *
*                   ADDITIONAL ALIASES ARE COUNTED AS ALIASES.        *
*                                                                     *
* JOB CONTROL LANGUAGE                                                *
*                                                                     *
*              EXEC                                                   *
*                   SPECIFIES THE PROGRAM NAME AND SUPPLIES PARM      *
*                   INFORMATION FOR THE PROGRAM.                      *
*                                                                     *
*              SYSPRINT DD                                            *
*                   DEFINES THE MESSAGE DATA SET.                     *
*                                                                     *
*              SYSUT1   DD                                            *
*                   DEFINES THE INPUT PDS.                            *
*                                                                     *
*              SYSUT2   DD                                            *
*                   DEFINES THE OUTPUT DATA SET.  THIS MAY BE A       *
*                   PRINTER OR CARD PUNCH SYSOUT CLASS, A DIRECT      *
*                   ACCESS DATA SET, A TAPE, OR A JES2 OR HASP        *
*                   INTERNAL READER.                                  *
*                                                                     *
*              SYSIN    DD                                            *
*                   DEFINES THE CONTROL DATA SET.  THIS MUST BE A     *
*                   CARD IMAGE DATA SET.                              *
*                                                                     *
*              THE USER MAY SPECIFY BLKSIZE FOR SYSPRINT AND SYSUT2.  *
*              RECFM AND LRECL ARE FIXED BY THE PROGRAM.  IF BLKSIZE  *
*              IS NOT SPECIFIED, THE FOLLOWING DEFAULTS APPLY:        *
*                                                                     *
*                           --------------------------------------    *
*                          ×   DDNAME   × RECFM × LRECL × BLKSIZE ×   *
*                          ×--------------------------------------×   *
*                          × SYSPRINT   ×  FBA  ×  133  ×   133   ×   *
*                          × SYSUT2     ×       ×       ×         ×   *
*                          ×    PRINT   ×  FBA  ×  133  ×   133   ×   *
*                          ×    PUNCH   ×  FB   ×   80  ×    80   ×   *
*                           --------------------------------------    *
*                                                                     *
* PARM FIELD   THE PARM FIELD OF THE EXEC STATEMENT IS USED TO        *
*              SPECIFY WHAT TYPE OF OUTPUT IS TO BE PRODUCED, AND TO  *
*              INCREASE PROGRAM BUFFERS TO PROCESS LARGE DATA SETS.   *
*              NO PARM SPECIFIED IMPLIES THAT MEMBERS ARE TO BE       *
*              LISTED WITH EACH MEMBER STARTING ON A NEW PAGE.        *
*                                                                     *
*              PARAMETER VALUES MAY BE ABBREVIATED BY SPECIFYING ONLY *
*              AS MANY CHARACTERS AS ARE NEEDED TO DISTINGUISH THEM   *
*              FROM OTHER KEYWORDS.  FOR EXAMPLE, THE FOLLOWING       *
*              ABBREVIATIONS ARE VALID FOR NOPAGE - NOP, NOPA, NOPAG. *
*              N AND NO ARE NOT VALID AS THEY COULD ALSO MEAN         *
*              NOHEADER.                                              *
*                                                                     *
*              VALID PARM VALUES ARE:                                 *
*                                                                     *
*              INDEX=                                                 *
*                   SPECIFIES IF THE INDEX IS TO CONTAIN THE NAME OF  *
*                   EACH MEMBER AND ALIAS IN THE DATA SET             *
*                   (INDEX=FULL), OR ONLY THE NAMES AND ALIASES       *
*                   ACTUALLY PROCESSED (INDEX=SHORT).                 *
*                                                                     *
*                   THE DEFAULT VALUE FOR INDEX IS SHORT.             *
*                                                                     *
*              INTRDR                                                 *
*                   IS USED WHEN THE OUTPUT IS WRITTEN DIRECTLY TO A  *
*                   HASP OR JES2 INTERNAL READER.                     *
*                                                                     *
*                   THE SYSUT2 STATEMENT MUST DEFINE AN INTERNAL      *
*                   READER.  FOR EXAMPLE:                             *
*                   HASP                                              *
*                     //SYSUT2  DD UNIT=INTRDR                        *
*                   JES2                                              *
*                     //SYSUT2  DD SYSOUT=(A,INTRDR)                  *
*                                                                     *
*                   A /*EOF RECORD IS GENERATED FOLLOWING THE LAST    *
*                   MEMBER PROCESSED.                                 *
*                                                                     *
*              LINECNT=NN                                             *
*                   SPECIFIES THE NUMBER OF LINES PER PAGE.  THE      *
*                   DEFAULT IS 60.                                    *
*                                                                     *
*              NOHEADER                                               *
*                   HEADER LINES WILL NOT BE PRINTED ON EACH PAGE.    *
*                   NO MEMBER IDENTIFICATION IS PROVIDED BY THE       *
*                   PROGRAM.                                          *
*                                                                     *
*              NOPAGE                                                 *
*                   MEMBERS ARE LISTED WITH 2 BLANK LINES BETWEEN     *
*                   EACH MEMBER.                                      *
*                                                                     *
*              ORDER=                                                 *
*                   SPECIFIES THE ORDER IN WHICH THE ENTRIES ARE TO   *
*                   BE PROCESSED.  ORDER=ALPHA IS THE DEFAULT AND     *
*                   SPECIFIES THAT ENTRIES ARE TO BE PROCESSED IN     *
*                   ALPHABETICAL ORDER OF THE TRUE NAMES.             *
*                   ORDER=SELECT SPECIFIES THAT THE MEMBERS ARE TO BE *
*                   PROCESSED IN THE ORDER SPECIFIED BY SELECT INPUT  *
*                   CONTROL STATEMENTS.  ORDER=SELECT IS IGNORED IF   *
*                   THE INPUT CONTROL STATEMENTS SPECIFY EXCLUDE.     *
*                                                                     *
*              POWER=N                                                *
*                   N IS AN INTEGER WHICH ACTS AS A MULTIPLIER ON THE *
*                   NAME TABLE SIZE.  THE DEFAULT TABLE IS SUFFICIENT *
*                   TO CONTAIN 409 DIRECTORY ENTRIES.                 *
*                                                                     *
*                   IF THE SPECIFIED TABLE SIZE IS NOT SUFFICIENT,    *
*                   THE PROGRAM WILL OBTAIN A NEW TABLE AND CONTINUE  *
*                   PROCESSING.  PROCESSING TIME AND INPUT OPERATIONS *
*                   CAN BE REDUCED BY USING THE POWER PARAMETER TO    *
*                   OBTAIN A TABLE OF SUFFICIENT SIZE.                *
*                                                                     *
*              PUNCH                                                  *
*                   OUTPUT PRODUCED IS 80 BYTE CARD IMAGES WHICH CAN  *
*                   BE WRITTEN TO A SEQUENTIAL DATA SET OR A SYSOUT   *
*                   CLASS FOR THE PUNCH.  NO SEPARATION IS PROVIDED   *
*                   BETWEEN MEMBERS.                                  *
*                                                                     *
*              SEQ                                                    *
*                   CAUSES ./ ADD AND ./ ALIAS RECORDS TO BE          *
*                   GENERATED AS DOES UNLOAD, AND A                   *
*                        ./ NUMBER NEW1=10,INCR=10                    *
*                   IEBUPDTE CONTROL STATEMENT TO BE GENERATED AFTER  *
*                   EACH ./ ADD STATEMENT.                            *
*                                                                     *
*              SYNAD=NN                                               *
*                   SPECIFIES THE LIMIT ON SYNAD ERRORS PERMITTED.    *
*                   THE RUN WILL BE TERMINATED WHEN THE NUMBER OF     *
*                   SYNAD ERRORS REACHES THIS VALUE.  THE DEFAULT     *
*                   VALUE IS 8.                                       *
*                                                                     *
*                   A SERIES OF SYNAD ERRORS MAY BE CAUSED WHEN THE   *
*                   SYSUT1 STATEMENT DEFINES A SEQUENTIAL DATA SET OR *
*                   A PDS MEMBER.                                     *
*                                                                     *
*              UNLOAD                                                 *
*                   SAME AS PUNCH EXCEPT THAT EACH MEMBER IS          *
*                   PRECEEDED BY A                                    *
*                        ./ ADD NAME=________                         *
*                   IEBUPDTE CONTROL STATEMENT IN THE OUTPUT DATA     *
*                   SET.  THIS DATA SET MAY THEN BE USED AS INPUT TO  *
*                   IEBUPDTE TO CREATE A NEW PDS.                     *
*                                                                     *
*                   IF THE MEMBER HAS ALIASES, A                      *
*                        ./ ALIAS NAME=________                       *
*                   IEBUPDTE CONTROL STATEMENT IS GENERATED FOR EACH  *
*                   ALIAS.                                            *
*                                                                     *
* UTILITY CONTROL STATEMENTS                                          *
*                                                                     *
*              UTILITY CONTROL STATEMENTS MAY BE USED TO SELECT       *
*              MEMBERS FOR PROCESSING, EXCLUDE MEMBERS FROM           *
*              PROCESSING, OR TO ENTER A TITLE.                       *
*                                                                     *
*              IF NO UTILITY CONTROL STATEMENTS ARE PRESENT, ALL      *
*              MEMBERS OF THE PDS ARE PROCESSED.                      *
*                                                                     *
*              ALIASES ARE PROCESSED PROPERLY, AS ALIASES OF A TRUE   *
*              NAME.                                                  *
*                                                                     *
*              CONTROL STATEMENTS MAY BE CODED IN COLUMNS 1-72 AND    *
*              MAY NOT BE CONTINUED.  MULTIPLE STATEMENTS ARE         *
*              PERMITTED.  A BLANK IS USED TO TERMINATE THE OPERAND   *
*              FIELD.                                                 *
*                                                                     *
*              CONTROL STATEMENT KEYWORDS MAY BE ABBREVIATED BY       *
*              SPECIFYING ONLY AS MANY CHARACTES AS ARE NEEDED TO     *
*              DISTINGUISH THEM FROM OTHER KEYWORDS.  FOR EXAMPLE,    *
*              THE FOLLOWING ABBREVIATIONS ARE VALID FOR SELECT - S,  *
*              SE, SEL, SELE, SELEC.                                  *
*                                                                     *
*                               --------------------------------      *
*                              × OPERATOR ×       OPERAND       ×     *
*                              ×--------------------------------×     *
*                              × SELECT   × MEMBER=(NAME-LIST)  ×     *
*                              × EXCLUDE  × PREFIX=(NAME-LIST)  ×     *
*                              ×--------------------------------×     *
*                              × TITLE    × TEXT                ×     *
*                               --------------------------------      *
*                                                                     *
*              SELECT                                                 *
*                   SPECIFIES MEMBERS TO BE PROCESSED.  ENTRIES ARE   *
*                   PROCESSED ONLY ONCE EVEN IF SPECIFIED AS BOTH     *
*                   TRUE NAME AND ALIAS OR SPECIFIED MULTIPLE TIMES.  *
*                                                                     *
*              EXCLUDE                                                *
*                   SPECIFIES MEMBERS TO BE BYPASSED.                 *
*                                                                     *
*              TITLE                                                  *
*                   SPECIFIES A 30 CHARACTER TITLE TO BE PRINTED ON   *
*                   EACH PAGE HEADING.                                *
*                                                                     *
*              MEMBER                                                 *
*                   SPECIFIES A LIST OF MEMBER NAMES.  THE NAMES ARE  *
*                   SEPARATED BY COMMAS AND THE LIST MAY BE ENCLOSED  *
*                   IN PARENTHESES.                                   *
*                                                                     *
*              PREFIX                                                 *
*                   SPECIFIES A LIST OF CHARACTER STRINGS.  ALL       *
*                   MEMBERS WHOSE NAMES BEGIN WITH ANY OF THE         *
*                   SPECIFIED STRINGS ARE SELECTED OR EXCLUDED AS     *
*                   SPECIFIED BY THE OPERATOR.  THE CHARACTER STRINGS *
*                   ARE SEPARATED BY COMMAS AND THE LIST MAY BE       *
*                   ENCLOSED IN PARENTHESES.                          *
*                                                                     *
*              NAME-LIST                                              *
*                   SPECIFIES THE MEMBER NAMES OR PREFIX CHARACTERS   *
*                   USED TO DETERMINE THE ENTRIES TO BE SELECTED OR   *
*                   EXCLUDED.  THE NAME LIST MAY BE SPECIFIED AS A    *
*                   SINGLE NAME, A LIST OF NAMES SEPARATED BY COMMAS  *
*                   (ENCLOSING THE LIST IN PARENTHESES IS OPTIONAL),  *
*                   A RANGE OF NAMES SPECIFIED AS TWO NAMES SEPARATED *
*                   BY A DASH (-), OR A COMBINATION OF NAMES AND NAME *
*                   RANGES.                                           *
*                                                                     *
*              TEXT                                                   *
*                   SPECIFIES THE 30 CHARACTER TEXT MESSAGE TO BE     *
*                   DISPLAYED ON EACH PAGE HEADER.  THE 30 CHARACTER  *
*                   COUNT BEGINS WITH THE FIRST NON BLANK CHARACTER   *
*                   FOLLOWING THE OPERATOR.                           *
*                                                                     *
*              FOR MEMBERS WITH ALIASES, A SELECT FOR THE TRUE NAME   *
*              OR ANY ALIAS WILL CAUSE THE MEMBER TO BE PROCESSED.    *
*              AN EXCLUDE FOR THE TRUE NAME OR ANY ALIAS WILL CAUSE   *
*              THE MEMBER TO BE BYPASSED.                             *
*                                                                     *
*              MEMBERS WITH ALIASES ARE PROCESSED ONCE, UNDER THE     *
*              TRUE NAME.                                             *
*                                                                     *
*              ALIASES WITH NO TRUE NAMES ARE PROCESSED ONCE, UNDER   *
*              THE FIRST ALIAS NAME.  IF UNLOAD IS SPECIFIED, THE     *
*              ./ ADD CARD WILL SPECIFY THE FIRST ALIAS NAME.         *
*                                                                     *
*              MEMBER AND PREFIX NAMES MUST BE FROM 1 TO 8 CHARACTERS *
*              IN LENGTH.                                             *
*                                                                     *
*              INPUT CONTROL STATEMENTS ARE LISTED IN THE MESSAGE     *
*              DATA SET.  IF A SYNTAX ERROR IS DETECTED, A $ IS       *
*              PRINTED BELOW THE CHARACTER WHERE THE ERROR WAS        *
*              DETECTED.  THE REMAINDER OF THE STATEMENT IS NOT       *
*              PROCESSED.  IF AN ERROR IS DETECTED IN A CONTROL       *
*              STATEMENT, THE RUN IS TERMINATED WITH A CONDITION CODE *
*              8 AND NO OUTPUT IS PRODUCED.                           *
*                                                                     *
*              SPECIFICATION OF A NON-EXISTANT MEMBER OR PREFIX NAME  *
*              IS NOT CONSIDERED AND ERROR.                           *
*                                                                     *
* RETURN CODES 00 - SUCCESSFUL COMPLETION                             *
*              04 - SYNAD ERRORS OCCURRED                             *
*              08 - INVALID CONTROL STATEMENT                         *
*              12 - INVALID PARM FIELD                                *
*              16 - UNABLE TO OPEN FILES                              *
*                   SYNAD ERROR LIMIT REACHED                         *
*                                                                     *
* ABEND CODES  U1000 - UNABLE TO OPEN SYSPRINT                        *
*                                                                     *
* EXAMPLE      PRINT ALL MEMBERS OF FPL.PROCLIB THAT BEGIN WITH THE   *
*              LETTERS "SS".                                          *
*                                                                     *
*              //LIST   EXEC  PGM=SS0300                              *
*              //SYSPRINT DD  SYSOUT=A                                *
*              //SYSUT1   DD  DISP=SHR,                               *
*              //             DSNAME=FPL.PROCLIB                      *
*              //SYSUT2   DD  SYSOUT=A                                *
*              //SYSIN    DD  *                                       *
*                S P=SS                                               *
*                T TECHNICAL SYSTEMS PROCEDURES                       *
*              /*                                                     *
*                                                                     *
*              CREATE AN INPUT STREAM WHICH MAY BE USED TO RE-CREATE  *
*              FPL.CMDPROC.                                           *
*                                                                     *
*              //BACKUP EXEC  PGM=SS0300,PARM=SEQ                     *
*              //SYSPRINT DD  SYSOUT=A                                *
*              //SYSUT1   DD  DISP=SHR,                               *
*              //         DD  DSNAME=FPL.CMDPROC                      *
*              //SYSUT2   DD  SYSOUT=B                                *
*              //SYSIN    DD  DUMMY                                   *
*                                                                     *
* MISCELLANEOUS                                                       *
*              THIS PROGRAM IS A REVISED VERSION OF A LISTDATA        *
*              PROGRAM WRITTEN BY PAUL ENGELS OF PRATT & WHITNEY      *
*              AIRCRAFT, FLORIDA RESEARCH & DEVELOPMENT CENTER.       *
*                                                                     *
*              THE REVISIONS INCLUDE -                                *
*                   TITLE MODIFICATIONS                               *
*                   ALIAS PROCESSING                                  *
*                   NON-CARD IMAGE DATA SET SUPPORT                   *
*                   INDEX FORMAT CHANGES                              *
*                   AUTOMATIC POWER CALCULATION                       *
*                                                                     *
* FPL MACROS USED   $ENTER      ENTRY LINKAGE                         *
*                   $RTRN       EXIT LINKAGE                          *
*                                                                     *
* IBM MACROS USED   OPEN        PREPARE FILES FOR PROCESSING          *
*                   CLOSE       TERMINATE PROCESSING ON DATA FILES    *
*                   DCB         DESCRIBE DATA FILES                   *
*                   READ        READ A RECORD FROM AN INPUT FILE      *
*                   CHECK       WAIT FOR COMPLETION OF READ           *
*                   PUT         PUT A RECORD TO AN OUTPUT FILE        *
*                   RDJFCB      READ JOB FILE CONTROL BLOCK           *
*                   ABEND       TERMINATE PROGRAM ABNORMALLY          *
*                   GETMAIN     OBTAIN WORKING STORAGE                *
*                   FREEMAIN    FREE WORKING STORAGE                  *
*                   DCBD        DSECT OF A DCB                        *
*                   IEFJFCBN    DSECT OF A JFCB                       *
*                   EXTRACT     OBTAIN TIOT ADDRESS                   *
*                                                                     *
*                                                GORDON P. WEST 10/76 *
***********************************************************************
         TITLE 'UPDATE LOG'
***********************************************************************
*                                                                     *
* 06/01/77       MOVE RECORD LENGTH OF VARIABLE RECORDS TO HALFWORD   *
*                ALIGNED WORK AREA BEFORE LOADING INTO REGISTER.      *
*                                                                     *
* 07/20/77       NOHEADER OPTION ADDED.                               *
*                LINE COUNTING ERROR WITH NOPAGE OPTION CORRECTED.    *
*                                                                     *
* 09/20/77 V 3.1 PERMIT NAME RANGES IN SELECT AND EXCLUDE STATEMENTS. *
*                ADD IDENTIFICATION BLOCK TO MESSAGE OUTPUT.          *
*                ADD ORDER=SELECT OPTION.  REGION REQUIREMENTS        *
*                     INCREASED BY 1,640 BYTES FOR EACH POWER         *
*                     MULTIPLE WHETHER OR NOT ORDER=SELECT IS         *
*                     SPECIFIED.                                      *
*                                                                     *
* 02/06/78 V 3.2 SYNAD ERROR LIMIT ADDED                              *
*                                                                     *
***********************************************************************
         TITLE 'LINKAGE EDITOR CONTROL RECORDS'
***********************************************************************
*                                                                     *
*        THE FOLLOWING PUNCH STATEMENT(S) CAUSE LINKAGE EDITOR        *
*        CONTROL STATEMENTS TO BE INCLUDED IN THE OBJECT OUTPUT.      *
*                                                                     *
*        IF MESSAGE IEW0731 IS GENERATED (ALIAS NAME MATCHES MEMBER   *
*        NAME) IT MAY BE IGNORED'                                     *
*                                                                     *
***********************************************************************
*
         PUNCH '  ALIAS LISTDATA  &SYSDATE  &SYSTIME    IGNORE IEW0731'
         TITLE 'ENTRY LINKAGE'
SS0300   $ENTER BASE=(R11,R12),                                        +
               SAVE=SAVEAREA
         TITLE 'OPEN OUTPUT MESSAGE FILE - SYSPRINT'
***********************************************************************
*                                                                     *
*        OPEN OUTPUT MESSAGE FILE - SYSPRINT                          *
*                                                                     *
***********************************************************************
         SPACE 2
         LR    R2,R1                   SAVE PARM LIST ADDRESS
         OPEN  (SYSPRINT,OUTPUT)       OPEN OUTPUT MESSAGE FILE
         TM    SYSPRINT+48,X'10'       TEST FOR SUCCESSFUL OPEN
         BNZ   GETDATE
         ABEND 1000                    ABEND USER=1000
         TITLE 'GET DATE AND TIME OF RUN'
***********************************************************************
*                                                                     *
*        GET DATE AND TIME - ADAPTED FROM SUBROUTINE GDATE        GPW *
*                                                                     *
***********************************************************************
         SPACE 2
GETDATE  TIME  DEC                     GET DATE AND TIME
         ST    R1,WORK                 SAVE DATE
         UNPK  JULIAN,DATE             UNPACK DATE
         OI    JULDAY+2,X'F0'          SET ZONE
         MVC   TARGET,=PL3'0'          00 00 0C
         MVO   YEAR2,YEAR              00 0Y YC
         DP    TARGET,=P'4'            0Q QC RC
         CP    REM,=P'0'               TEST REMAINDER
         BE    VALID2
         CP    DAY,=P'59'              TEST FOR BEFORE FEB 29
         BNH   VALID2
         AP    DAY,=P'1'               ADJUST TO LEAP YEAR
VALID2   LA    R1,TABLE                LOAD TABLE ADDRESS
         USING CALENDAR,R1             SET BASE ADDRESS
         ZAP   MONTHP,=P'1'            SET MONTH TO 1
LOOP     CP    DAY,DAYS                SEE IF MORE DAYS THAN THIS MNTH
         BNH   OUT
         SP    DAY,DAYS                SUBTRACT DAYS IN THIS MONTH
         AP    MONTHP,=P'1'            ADD 1 TO MONTH
         LA    R1,LEN(R1)              INCREMENT TO NEXT MONTH
         B     LOOP
OUT      UNPK  MO,MONTHP               UNPACK MONTH
         OI    MO+1,X'F0'              SET ZONE
         UNPK  DA,DAY                  UNPACK DAY
         OI    DA+1,X'F0'              SET ZONE
         MVI   SLASH1,C'/'             SET SLASH
         MVI   SLASH2,C'/'             SET SLASH
         MVC   YEARDATE,MO             COPY TO HEADER LINE
         MVC   JULDATE,JULDAY
         DROP  R1
         SPACE 3
         LR    R1,R0                   NOW R1 HAS   HHMMSSTH
         SRL   R1,8                    SHIFT OUT TH FIELD
         SLL   R1,4
         O     R1,TIMEMASK
         ST    R1,VALUE+4
         ED    TIMEDAY(10),VALUE+4
         MVC   TIMEDAY(2),=C'E='
         L     R15,=A(PRINTID)         LOAD SUBROUTINE ADDRESS
         BALR  R4,R15                  PRINT IDENTIFICATION BLOCK
         TITLE 'PROCESS PARM FIELD'
***********************************************************************
*                                                                     *
*        PROCESS PARM FIELD                                           *
*                                                                     *
***********************************************************************
         SPACE 2
         MVI   CC,X'00'                INITIALIZE COMPLETION CODE
         L     R2,0(R2)                LOAD PARM FIELD ADDRESS
         LH    R3,0(R2)                LOAD PARM FIELD LENGTH
         LA    R2,2(R2)                STEP OVER LENGTH FIELD
         SPACE 2
         LTR   R3,R3                   TEST FOR NO PARAMETER LIST
         BZ    NOPARM
         BCTR  R3,0                    REDUCE LENGTH BY 1
         EX    R3,COPYPARM             COPY PARM FIELD
         PUT   SYSPRINT,PARMFLD        PRINT PARM FIELD
         LA    R3,1(R3)                ADD BACK THE 1
         SPACE 2
NEXTPARM LTR   R3,R3
         BNH   DONEPARM
         LA    R4,PARM                 LOAD WORK AREA ADDRESS
         LR    R15,R4                  SAVE IT FOR LATER
         LA    R8,1                    SET WORK AREA INCREMENT
         LA    R9,16(R4)               SET WORK AREA LIMIT
         MVC   0(17,R4),BLANKS         BLANK WORK AREA
         MVC   REASON,REASON-1         BLANK OUT REASON AREA
SCANPARM CLI   0(R2),C','              TEST FOR COMMA
         BE    ENDPARM
         CLI   0(R2),C' '              TEST FOR A BLANK
         BE    INCRPARM
         MVC   0(1,R4),0(R2)           COPY CHARACTER TO WORK AREA
         BXLE  R4,R8,INCRPARM          INCREMENT WORK AREA POINTER
         MVC   REASON(19),=C' PARAMETER TOO LONG'
         B     BADPARM
INCRPARM LA    R2,1(R2)                INCREMENT PARM FIELD POINTER
         BCT   R3,SCANPARM             DECREMENT COUNTER AND LOOP
         EJECT
***********************************************************************
*                                                                     *
*        PARAMETER OBTAINED, FIND =                                   *
*                                                                     *
***********************************************************************
         SPACE 2
ENDPARM  LA    R2,1(R2)                STEP PAST THE COMMA
         BCTR  R3,0                    DECREMENT THE COUNTER
         LR    R1,R4                   COPY WORK AREA POINTER
         SR    R1,R15                  GET LENGTH OF WORK AREA
         BZ    NEXTPARM                ZERO LENGTH, SKIP IT
         LR    R5,R1                   SAVE LENGTH
         LR    R4,R15                  RESET POINTER
FINDEQ   CLI   0(R4),C'='              SCAN FOR EQUALS
         BE    GOTEQUAL
         LA    R4,1(R4)                INCREMENT POINTER
         BCT   R1,FINDEQ               DECREMENT COUNTER AND LOOP
         EJECT
***********************************************************************
*                                                                     *
*        CHECK THE KEYWORD                                            *
*                                                                     *
***********************************************************************
         SPACE 2
         BCTR  R5,0                    REDUCE LENTGH BY 1
         EX    R5,COMPPNCH             TEST FOR PUNCH
         BE    PUNCH
         EX    R5,COMPUNLD             TEST FOR UNLOAD
         BE    UNLOAD
         EX    R5,COMPSEQ              TEST FOR SEQ
         BE    SEQ
         EX    R5,COMPINTR             TEST FOR INTRDR
         BE    INTRDR
         EX    R5,COMPNO               TEST FOR NO
         BE    AMBIG
         EX    R5,COMPNOPG             TEST FOR NOPAGE
         BE    NOPAGE
         EX    R5,COMPNHDR             TEST FOR NOHEADER
         BE    NOHEADER
         B     BADKEYWD
         SPACE 3
GOTEQUAL LR    R14,R4                  COPY POINTER
         SR    R14,R15                 CALCULATE LENGTH OF KEYWORD
         BCTR  R14,0                   DECREMENT BY 1
         SR    R5,R5                   ZERO REGISTER 5
         EX    R14,COMPLINE            TEST FOR LINECNT
         BE    LINECNT
         EX    R14,COMPOWER            TEST FOR POWER
         BE    POWER
         EX    R14,COMPSYN             TEST FOR SYNAD
         BE    SYNADLMT
         EX    R14,COMPINDX            TEST FOR INDEX
         BE    INDEX
         EX    R14,COMPORDR            TEST FOR ORDER
         BNE   BADKEYWD
         EJECT
***********************************************************************
*                                                                     *
*        ORDER=                                                       *
*                                                                     *
***********************************************************************
         CLC   1(6,R4),=C'ALPHA '      TEST FOR ORDER=ALPHA
         BE    SAVEORDR
         CLC   1(7,R4),=C'SELECT '     TEST FOR ORDER=SELECT
         BNE   BADVALUE
SAVEORDR MVC   ORDER,1(R4)             SAVE FIRST CHAR OF VALUE
         B     NEXTPARM
         EJECT
***********************************************************************
*                                                                     *
*        INDEX=                                                       *
*                                                                     *
***********************************************************************
         SPACE 2
INDEX    CLC   1(6,R4),=C'SHORT '      TEST FOR INDEX=SHORT
         BE    NEXTPARM
         OI    OPTIONS,$FULL           SET INDEX=FULL INDICATOR
         CLC   1(5,R4),=C'FULL '       TEST FOR INDEX=FULL
         BNE   BADVALUE
         B     NEXTPARM
         EJECT
***********************************************************************
*                                                                     *
*        NOPAGE                                                       *
*                                                                     *
***********************************************************************
         SPACE 2
NOPAGE   MVI   HEADING1,C'-'           SET HEADER CONTROL TO 3 SPACE
         XC    PGSPACE(2),PGSPACE      INHIBIT LINE COUNTING
         OI    OPTIONS,$NOPAGE         SET NOPAGE INDICATOR
         B     NEXTPARM
         EJECT
***********************************************************************
*                                                                     *
*        NOHEADER                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
NOHEADER OI    OPTIONS,$NOHDR          SET NOHEADER INDICATOR
         MVC   ACTION,=C' PRT'         SET ACTION TO "PRINTED"
         B     NEXTPARM
         EJECT
***********************************************************************
*                                                                     *
*        PUNCH, UNLOAD, SEQ, AND INTRDR                               *
*                                                                     *
***********************************************************************
         SPACE 2
SEQ      OI    OPTIONS,$SEQ
UNLOAD   OI    OPTIONS,$UNLOAD
PUNCH    OI    OPTIONS,$PUNCH
         LA    R10,SYSUT2              LOAD DCB ADDRESS
         USING IHADCB,R10              ADDRESSABILITY
         MVC   DCBLRECL,=H'80'         SET LRECL=80
         MVC   UT2BLKSI,=H'80'         SET DEFAULT BLKSIZE
         MVI   DCBRECFM,DCBRECF+DCBRECBR   SET RECFM=FB
         B     NEXTPARM
         SPACE 3
INTRDR   OI    OPTIONS,$INTRDR         SET OPTION INDICATOR
         B     PUNCH
         DROP  R10
         EJECT
***********************************************************************
*                                                                     *
*        POWER=   OR   LINECNT=                                       *
*                                                                     *
***********************************************************************
         SPACE 2
SYNADLMT LA    R5,4(R5)                +8
POWER    LA    R5,4(R5)                +4
LINECNT  EQU   *                       +0
         SPACE 2
         LA    R4,1(R4)                STEP OVER =
         BCT   R1,EVAL                 DECREMENT COUNTER AND TEST
         B     NOVALUE
EVAL     LR    R0,R1                   COPY COUNT
         LR    R15,R4                  COPY COUNTER
VALIDATE CLI   0(R15),C'0'             TEST AGAINST ZERO
         BL    BADNUM
         CLI   0(R15),C'9'             TEST AGAINST NINE
         BH    BADNUM
         LA    R15,1(R15)              INCREMENT POINTER
         BCT   R0,VALIDATE             DECREMENT COUNT, TEST, AND LOOP
         BCTR  R1,0                    RECUCE COUNT BY 1
         EX    R1,PACK                 PACK THE NUMBER
         CVB   R15,VALUE               CONVERT TO BINARY
         B     BRANCH(R5)              SPLIT TO PROPER OPTION
         SPACE
BRANCH   B     LINECNT2                +0 - LINECNT
         B     POWER2                  +4 - POWER
         SPACE 2
         ST    R15,SYNADLIM            +8 - SYNADLIM
         B     NEXTPARM
         SPACE 2
POWER2   ST    R15,POWERVAL            STORE VALUE
         B     NEXTPARM
         SPACE 2
LINECNT2 ST    R15,#LINES              STORE VALUE
         B     NEXTPARM
         EJECT
***********************************************************************
*                                                                     *
*        EXECUTED INSTRUCTIONS                                        *
*                                                                     *
***********************************************************************
         SPACE 2
COMPPNCH CLC   0(0,R15),=C'PUNCH '
COMPUNLD CLC   0(0,R15),=C'UNLOAD '
COMPSEQ  CLC   0(0,R15),=C'SEQ '
COMPINTR CLC   0(0,R15),=C'INTRDR '
COMPNO   CLC   0(0,R15),=C'NO '
COMPNOPG CLC   0(0,R15),=C'NOPAGE '
COMPNHDR CLC   0(0,R15),=C'NOHEADER '
COMPLINE CLC   0(0,R15),=C'LINECNT='
COMPOWER CLC   0(0,R15),=C'POWER='
COMPSYN  CLC   0(0,R15),=C'SYNAD='
COMPINDX CLC   0(0,R15),=C'INDEX='
COMPORDR CLC   0(0,R15),=C'ORDER='
COPYPARM MVC   PARMDATA(0),0(R2)       COPY PARM FIELD TO MESSAGE
PACK     PACK  VALUE,0(0,R4)
         EJECT
***********************************************************************
*                                                                     *
*        PARAMETER FIELD INVALID                                      *
*                                                                     *
***********************************************************************
         SPACE 2
BADVALUE MVC   REASON(14),=C' INVALID VALUE'
         B     BADPARM
         SPACE 2
BADKEYWD MVC   REASON(16),=C' INVALID KEYWORD'
         B     BADPARM
         SPACE 2
NOVALUE  MVC   REASON(19),=C' NO VALUE SPECIFIED'
         B     BADPARM
         SPACE 2
BADNUM   MVC   REASON(21),=C' NON-NUMERIC IN VALUE'
         B     BADPARM
         SPACE 2
AMBIG    MVC   REASON(10),=C' AMBIGUOUS'
         SPACE 2
BADPARM  PUT   SYSPRINT,INVALID        PRINT INVALID PARM
         MVI   INVALID,C' '            SET CARRIAGE CONTROL
         MVI   CC,12                   SET CONDITION CODE
         B     NEXTPARM
         SPACE 2
NOPARM   PUT   SYSPRINT,NOPARMS        PRINT MESSAGE
         SPACE 2
DONEPARM CLI   CC,X'00'                TEST CONDITION CODE
         BNE   RETURN
         TITLE 'OPEN REMAINING FILES'
***********************************************************************
*                                                                     *
*        OPEN REMAINING FILES - SYSIN, SYSUT1, SYSUT2                 *
*                                                                     *
***********************************************************************
         SPACE 2
         OPEN  (SYSIN,INPUT,                                           +
               SYSUT1,INPUT,                                           +
               SYSUT2,OUTPUT)
         TM    SYSIN+48,X'10'          TEST FOR SUCCESSFUL OPEN
         BNZ   CKSYSUT1
         MVC   DDNAME,=CL8'SYSIN'      MOVE IN DDNAME
         PUT   SYSPRINT,ERRMSG1        PRINT ERROR MESSAGE
         MVI   CC,16                   SET CONDITION CODE
CKSYSUT1 TM    SYSUT1+48,X'10'         TEST FOR SUCCESSFUL OPEN
         BNZ   CKSYSUT2
         MVC   DDNAME,=CL8'SYSUT1'     MOVE IN DDNAME
         PUT   SYSPRINT,ERRMSG1        PRINT ERROR MESSAGE
         MVI   CC,16                   SET CONDITION CODE
CKSYSUT2 TM    SYSUT2+48,X'10'         TEST FOR SUCCESSFUL OPEN
         BNZ   CKCODE
         MVC   DDNAME,=CL8'SYSUT2'     MOVE IN DDNAME
         PUT   SYSPRINT,ERRMSG1        PRINT ERROR MESSAGE
         MVI   CC,16                   SET CONDITION CODE
CKCODE   CLI   CC,X'00'                TEST FOR OPEN FAILURE
         BNE   CLOSE3                  CLOSE FILES AND TERMINATE
         RDJFCB SYSUT1                 READ JFCB FOR SYSUT1
         MVC   DSNAME,JFCBDSNM         SAVE DSNAME
         MVC   VOLUME,JFCBVOLS         SAVE VOLUME SERIAL
*--------IF PUNCH IS SPECIFIED, MAKE SURE DATA SET IS CARD IMAGE
         TM    OPTIONS,$PUNCH          TEST FOR PUNCH OPTION
         BZ    GETSTRG
         LA    R10,SYSUT1              LOAD DCB ADDRESS
         USING IHADCB,R10              ADDRESSABILITY
         TM    DCBRECFM,DCBRECF        TEST FOR FIXED RECORDS
         BZ    NOTCARDS
         CLC   DCBLRECL,=H'80'         TEST FOR 80 BYTE RECORDS
         BE    INITLINE
         DROP  R10
*--------PUNCH REQUESTED FOR NON CARD IMAGE DATA SET
NOTCARDS PUT   SYSPRINT,ERRMSG5        PRINT ERROR MESSAGE
         MVI   CC,12                   SET CONDITION CODE
         B     RETURN
*--------INITIALIZE LINE COUNTER
INITLINE LA    R10,8                   INITIALIZE LINE COUNTER
         TITLE 'OBTAIN STORAGE FOR MEMBER NAME TABLE'
***********************************************************************
*                                                                     *
*        GET STORAGE FOR MEMBER NAMES  - SIZE DETERMINED BY           *
*                                        POWER VALUE                  *
*                                                                     *
*        FOR EACH ADDITIONAL 8K ALLOCATED FOR TABLE, AN ADDITIONAL    *
*        409 ENTRIES MAY BE PROCESSED.                                *
*                                                                     *
***********************************************************************
         SPACE 2
GETSTRG  L     R3,POWERVAL             LOAD POWER VALUE
         MH    R3,HCORE                MULTIPLY BY CORE INCREMENT
         ST    R3,NAMELEN              SAVE LENGTH FOR FREEMAIN
         GETMAIN R,LV=(R3),SP=1        GET STORAGE
         ST    R1,ANAMES               SAVE THE ADDRESS
         AR    R3,R1                   ADD LENGTH OF AREA
         S     R3,=F'20'               REDUCE BY 1 ENTRY
         ST    R3,ENDNAMES             SAVE IT
*--------GET STORAGE FOR SELECTION TABLE
         L     R3,POWERVAL             LOAD POWER VALUE
         MH    R3,HCORE2               MULTIPLY BY CORE INCREMENT
         ST    R3,SELLEN               SAVE LENGTH FOR FREEMAIN
         GETMAIN R,LV=(R3),SP=1        GET STORAGE
         ST    R1,SELECTAD             SAVE THE ADDRESS
         AR    R3,R1                   ADD LENGTH OF AREA
         S     R3,=F'4'                REDUCE BY ONE ENTRY
         ST    R3,ENDSELCT             STORE END OF TABLE ADDRESS
         TITLE 'READ MEMBER NAMES FROM PDS'
***********************************************************************
*                                                                     *
*        READ MEMBER NAMES FROM PDS                                   *
*                                                                     *
*              R0  - LINKAGE REGISTER                                 *
*              R1  - LINKAGE REGISTER                                 *
*              R2  - SELECTION TABLE POINTER                          *
*              R3  - MEMBER NAME TABLE POINTER                        *
*              R4  - MEMBER NAME TABLE INCREMENT                      *
*              R5  - MEMBER NAME TABLE LIMIT                          *
*              R6  - OUTPUT LINE POINTER                              *
*              R7  - DIRECTORY RECORD POINTER/OUTPUT LINE LIMIT       *
*              R8  - DIRECTORY RECORD LENGTH COUNTER                  *
*              R9  - ENTRY COUNT                                      *
*              R10 - NOT USED                                         *
*              R11 - BASE REGISTER                                    *
*              R12 - BASE REGISTER                                    *
*              R13 - SAVE AREA                                        *
*              R14 - LINKAGE REGISTER                                 *
*              R15 - LINKAGE REGISTER                                 *
*                                                                     *
***********************************************************************
         SPACE 2
         L     R3,ANAMES               LOAD START OF TABLE ADDRESS
         LA    R4,20                   LENGTH OF ONE ENTRY
         L     R5,ENDNAMES             LOAD END OF TABLE ADDRESS
         L     R2,SELECTAD             LOAD SELECTION TABLE POINTER
         SR    R9,R9                   CLEAR ENTRY COUNTER
READPDS  READ  ECB1,                   READ A DIRECTORY BLOCK          +
               SF,                                                     +
               SYSUT1,                                                 +
               LIBDIR,                                                 +
               'S'
         CHECK ECB1                    WAIT FOR COMPLETION
         LA    R7,LIBDIR+8             LOAD COUNT ADDRESS
         LH    R8,LIBDIR+8             LOAD COUNT
         AR    R8,R7                   LOAD END OF ENTRY ADDRESS
         LA    R7,2(R7)                STEP OVER COUNT
         SPACE 2
         SR    R1,R1                   CLEAR WORK REGISTER
NAMELOOP CR    R7,R8                   SEE IF DONE
         BNL   READPDS
         CLC   0(8,R7),=8X'FF'         TEST FOR END
         BE    PDSEOF
         LA    R9,1(R9)                COUNT ENTRY
         CLI   CC,0                    SEE IF TABLE FULL
         BNE   GETLEN
         ST    R3,0(R2)                STORE POINTER IN SELECTION TABLE
         LA    R2,4(R2)                INCREMENT TABLE POINTER
         MVC   0(12,R3),0(R7)          COPY NAME AND TTR INTO TABLE
         NI    11(R3),X'80'            SAVE ALIAS INDICATOR ONLY
         XC    12(8,R3),12(R3)         ZERO OUT POINTER WORDS
         BXLE  R3,R4,GETLEN            INCREMENT AND TEST TABLE PTR
         MVI   CC,8                    INDICATE TABLE FULL
GETLEN   NI    11(R7),X'1F'            SAVE USER DATA LENGTH
         IC    R1,11(R7)               LOAD ENTRY LENGTH (HALFWORDS)
         SLA   R1,1                    DOUBLE TO GET BYTES
         LA    R7,12(R1,R7)            POINT TO NEXT ENTRY
         B     NAMELOOP
         SPACE 2
*--------END OF DIRECTORY
PDSEOF   CLOSE SYSUT1                  CLOSE DIRECTORY
         CLI   CC,0                    SEE IF TABLE EXCEEDED
         BE    RESETLIM
*--------MEMBER NAME TABLE EXCEEDED, DETERMINE REQUIRED SIZE
         MVI   CC,0                    RESET CONDITION CODE
         CVD   R9,VALUE                CONVERT TO DECIMAL
         ED    ENTRIES,VALUE+5         EDIT INTO PRINT LINE
         MH    R9,=H'20'               SEE HOW MUCH STORAGE NEEDED
         SR    R8,R8                   CLEAR FOR DIVIDE
         D     R8,FCORE                DIVIDE BY CORE INCREMENT
         LA    R9,1(R9)                ADD 1 TO QUOTIENT
         ST    R9,POWERVAL             STORE NEW POWER VALUE
         CVD   R9,VALUE                CONVERT TO DECIMAL
         ED    REQPOWER,VALUE+6        EDIT INTO PRINT LINE
         LA    R10,2(R10)              INCREMENT LINE COUNTER
         PUT   SYSPRINT,LOWPOWER
         L     R3,SELLEN               FREE OLD SELECTION TABLE
         L     R1,SELECTAD
         FREEMAIN R,LV=(R3),A=(R1),SP=1
         L     R3,NAMELEN              FREE OLD NAME TABLE
         L     R1,ANAMES
         FREEMAIN R,LV=(R3),A=(R1),SP=1
         OPEN  (SYSUT1,INPUT)          RE-OPEN DIRECTORY
         B     GETSTRG
RESETLIM LR    R5,R3                   SAVE END OF TABLE ADDRESS
         BCTR  R5,0                    REDUCE BY 1
         ST    R5,ENDNAMES             SAVE ADDRESS
         BCTR  R2,0                    REDUCE BY 1
         ST    R2,ENDSELCT             SAVE SELECTION TABLE LIMIT
         ST    R9,#ENTRIES             SAVE ENTRY COUNT
         TITLE 'MODIFY DCB TO OPEN SYSUT1 AS A PDS'
***********************************************************************
*                                                                     *
*        MODIFY DCB TO OPEN SYSUT1 AS A PDS                           *
*                                                                     *
***********************************************************************
         SPACE 2
         LA    R9,SYSUT1               LOAD DCB ADDRESS
         USING IHADCB,R9               ADDRESSABILITY
         MVI   UT1XLIST,X'05'          ACTIVATE DCB EXIT ROUTINE
         XC    DCBBLKSI,DCBBLKSI       ZERO BLOCK SIZE
         XC    DCBLRECL,DCBLRECL       ZERO RECORD LENGTH
         MVI   DCBRECFM,DCBRECKL       PREVENT KEYLENGTH FROM JFCB
         MVI   DCBKEYLE,X'00'          ZERO KEY LENGTH
         MVI   DCBDSORG,DCBDSGPO       SET DSORG=PO
         LA    R1,NEXTNAME             UPDATE EOD ADDRESS
         ST    R1,DCBEODAD
         OPEN  (SYSUT1,INPUT)          OPEN THE FILE
         TM    DCBOFLGS,DCBOFOPN       TEST FOR SUCCESSFUL OPEN
         BNO   FREENAME                FREE NAME TABLE, CLOSE FILES
         CLC   DCBLRECL,=H'132'        TEST RECORD LENGTH
         BNH   GETBUFF
         MVC   MAXLRECL,=F'130'        RESET MAX LRECL
         MVC   CONLRECL,=F'120'        RESET CONTINUATION LRECL
         MVI   HEADING2+132,C'C'       INDICATE CONTINUATION COLUMN
         TITLE 'GET BUFFERS FOR INPUT RECORDS'
***********************************************************************
*                                                                     *
*        GET BUFFERS FOR INPUT RECORDS                                *
*                                                                     *
***********************************************************************
         SPACE 2
GETBUFF  LH    R2,DCBBLKSI             LOAD BLOCK SIZE
         LA    R2,7(R2)                ROUND UP TO 8 BYTE MULTIPLE
         N     R2,=X'FFFFFFF8'
         LR    R3,R2                   SAVE LENGTH
         SLA   R2,1                    DOUBLE FOR 2 BUFFERS
         ST    R2,BUFFLEN              STORE LENGTH
         GETMAIN R,LV=(R2),SP=1        GET MEMORY
         ST    R1,BUFFERS              STORE GETMAINED ADDRESS
         ST    R1,BUFFER1              STORE FIRST BUFFER ADDRESS
         ST    R1,BUFFER3
         AR    R1,R3                   ADD LENGTH
         ST    R1,BUFFER2              STORE SECOND BUFFER LENGTH
         DROP  R9
         TITLE 'READ AND PROCESS INPUT CONTROL STATEMENTS'
***********************************************************************
*                                                                     *
*        READ AND PROCESS INPUT CONTROL STATEMENTS                    *
*                                                                     *
***********************************************************************
         SPACE 2
         LA    R10,3(R10)              INCREMENT LINE COUNTER
         MVC   PRNTLINE,BLANKS         CLEAR PRINT LINE
         MVI   PRNTLINE,C'-'           THREE LINE SKIP FIRST TIME
         L     R9,SELECTAD             LOAD SELECTION TABLE ADDRESS
         SPACE
NEXTCARD GET   SYSIN,CARDIN            GET A CONTROL STATEMENT
         LA    R10,1(R10)              INCREMENT LINE COUNT
         C     R10,#LINES              TEST AGAINST LIMIT
         BNH   PRNTCARD
         MVI   PRNTLINE,C'1'           START NEW PAGE
         LA    R10,1                   RESET LINE COUNT
PRNTCARD PUT   SYSPRINT,PRNTLINE       PRINT INPUT RECORD
         MVI   PRNTLINE,C' '           RESET CARRIAGE CONTROL
         LA    R2,CARDIN               POINT TO START OF RECORD
         LA    R6,1                    INCREMENT
         LA    R7,71(R2)               END OF INPUT STATEMENT
         MVC   CARDIN+72(8),BLANKS     BLANK ID COLUMNS
         SPACE
*--------PROCESS OPERATION FIELD
OPERATN1 CLI   0(R2),C' '              FIND START OF OPERATION
         BNE   OPERATN2
         BXLE  R2,R6,OPERATN1          CONTINUE PROCESSING RECORD
         B     NEXTCARD                BLANK CARD, SKIP IT
OPERATN2 LR    R15,R2                  SAVE STARTING ADDRESS
OPERATN3 CLI   0(R2),C' '              FIND END OF OPERATION
         BE    OPERATN4
         BXLE  R2,R6,OPERATN3          CONTINUE PROCESSING RECORD
         MVC   CARDMSG,=CL20'INCOMPLETE OPERATION'
         B     BADCARD
OPERATN4 LR    R1,R2                   GET LENGTH OF OPERATION
         SR    R1,R15
         BCTR  R1,0                    SUBTRACT 1
         EX    R1,COMPTITL             TEST FOR TITLE
         BE    TITLE1
         EX    R1,COMPSEL              TEST FOR SELECT
         BE    MODE1
         EX    R1,COMPEXCL             TEST FOR EXCLUDE
         BE    MODE1
         MVC   CARDMSG,=CL20'INVALID OPERATION'
         B     BADCARD
         SPACE
*--------TITLE CARD FOUND
TITLE1   LA    R2,1(R2)                SCAN UNTIL A NON-BLANK IS FOUND
         CLI   0(R2),C' '
         BE    TITLE1
         MVC   TITLE,0(R2)             COPY TITLE
         B     NEXTCARD
         SPACE
*--------SELECT OR EXCLUDE FOUND
MODE1    CLI   MODE,C' '               TEST FOR PREVIOUS INPUT
         BNE   MODE2
         MVC   MODE,0(R15)             SAVE MODE CHARACTER
         CLI   MODE,C'S'               IS IT SELECT?
         BE    KEYWORD1
         CLI   ORDER,C'S'              NO, IS ORDER=SELECT SPECIFIED?
         BNE   KEYWORD1
         MVI   ORDER,C'A'              TURN ORDER=SELECT OFF
         B     KEYWORD1
MODE2    CLC   MODE,0(R15)             TEST AGAINST PREVIOUS MODE
         BE    KEYWORD1
         MVC   CARDMSG,=CL20'MODE ERROR'
         B     BADCARD
         SPACE
*--------TEST FOR PREFIX OR MEMBER
KEYWORD1 LR    R15,R2                  SAVE START OF FIELD ADDRESS
KEYWORD2 CLI   0(R2),C' '              FIND START OF KEYWORD
         BNE   KEYWORD3
         BXLE  R2,R6,KEYWORD2          CONTINUE PROCESSING RECORD
         MVC   CARDMSG,=CL20'NO OPERANDS'
         B     BADCARD
KEYWORD3 LR    R15,R2                  SAVE START OF FIELD ADDRESS
KEYWORD4 CLI   0(R2),C'='              FIND =
         BE    KEYWORD5
         BXLE  R2,R6,KEYWORD4          CONTINUE PROCESSING RECORD
         MVC   CARDMSG,=CL20'INCOMPLETE OPERAND'
         B     BADCARD
KEYWORD5 LR    R1,R2                   GET OPERAND LENGTH
         SR    R1,R15
         BCTR  R1,0                    SUBTRACT 1
         EX    R1,COMPMEMB             TEST FOR MEMBER
         BE    KEYWORD6
         EX    R1,COMPPREF             TEST FOR PREFIX
         BE    KEYWORD6
         MVC   CARDMSG,=CL20'INVALID OPERAND'
         B     BADCARD
KEYWORD6 MVC   MEMPRFX,0(R15)          SAVE FIRST LETTER OF OPERAND
         SPACE
*--------PROCESS NAMES
         LA    R2,1(R2)                MOVE PAST =
         CLI   0(R2),C'('              TEST FOR (
         BNE   NAME1
         LA    R2,1(R2)                MOVE PAST (
NAME1    LR    R15,R2                  SAVE STARTING ADDRESS
NAME2    CLI   0(R2),C' '              FIND END OF NAME
         BE    NAME3
         CLI   0(R2),C','
         BE    NAME3
         CLI   0(R2),C')'
         BE    NAME3
         CLI   0(R2),C'-'
         BE    NAME3
         BXLE  R2,R6,NAME2             CONTINUE PROCESSING RECORD
NAME3    LR    R1,R2                   GET LENGTH OF NAME
         SR    R1,R15
         BNZ   NAME4
NAME3ERR MVC   CARDMSG,=CL20'NO NAME'
         B     BADCARD
NAME4    C     R1,=F'8'                TEST NAME LENGTH
         BNH   NAME5
NAME4ERR MVC   CARDMSG,=CL20'NAME TOO LONG'
         B     BADCARD
NAME5    BCTR  R1,0                    REDUCE NAME LENGTH BY 1
         MVC   NAM1,BLANKS             BLANK WORK FIELD
         EX    R1,CPYNAME1             COPY NAME TO WORK FIELD
         CLI   MEMPRFX,C'M'            TEST FOR MEMBER OR PREFIX
         BNE   NAME6
         LA    R1,7                    MEMBER - CHECK 8 CHARACTERS
NAME6    STH   R1,NAMELEN1             STORE NAME1 LENGTH
         CLI   0(R2),C'-'              TEST FOR NAME RANGE
         BE    NAME6A
         MVC   NAM2,NAM1               NOT A RANGE, SET NAME2=NAME1
         MVC   NAMELEN2,NAMELEN1
         B     NAME6E
NAME6A   BXH   R2,R6,NAME3ERR          INCREMENT POINTER
         LR    R15,R2                  SAVE STARTING ADDRESS
NAME6B   CLI   0(R2),C' '              FIND END OF NAME
         BE    NAME6C
         CLI   0(R2),C','
         BE    NAME6C
         CLI   0(R2),C')'
         BE    NAME6C
         BXLE  R2,R6,NAME6B
NAME6C   LR    R1,R2                   GET LENGTH OF NAME
         SR    R1,R15
         BZ    NAME3ERR
         C     R1,=F'8'
         BH    NAME4ERR
         BCTR  R1,0                    REDUCE BY 1
         MVC   NAM2,BLANKS             INITIALIZE WITH BLANKS
         EX    R1,CPYNAME2             COPY ENDING NAME
         CLI   MEMPRFX,C'M'            TEST FOR MEMBER OR PREFIX
         BNE   NAME6D
         LA    R1,7                    MEMBER, SET LENGTH TO 8
NAME6D   STH   R1,NAMELEN2             SAVE LENGTH
         CLC   NAM1,NAM2               COMPARE NAMES
         BNH   NAME6E
         MVC   CARDMSG,=CL20'NAMES OUT OF ORDER'
         B     BADCARD
NAME6E   L     R3,ANAMES               LOAD FIRST TABLE ENTRY ADDRESS
         LA    R4,20                   LOAD INCREMENT
         L     R5,ENDNAMES             LOAD END OF TABLE ADDRESS
NAME7    LH    R14,NAMELEN1
         LH    R15,NAMELEN2
NAME7A   EX    R14,CMPNAME1
         BL    NAME8
         EX    R15,CMPNAME2
         BH    NAME9
         TM    11(R3),X'40'            SEE IF ALREADY SELECTED/EXCLUDED
         BO    NAME8
         OI    11(R3),X'40'            SET SELECT/EXCLUDE INDICATOR
         CLI   ORDER,C'S'              TEST FOR ORDER=SELECT
         BNE   NAME8
         ST    R3,0(R9)                STORE IN SELECTION TABLE
         LA    R9,4(R9)                INCREMENT TABLE POINTER
NAME8    BXLE  R3,R4,NAME7             CONTINUE PROCESSING LIST
NAME9    CLI   0(R2),C','              TEST FOR END OF RECORD
         BNE   NEXTCARD
         BXLE  R2,R6,NAME1             CONTINUE PROCESSING NAMES
         B     NEXTCARD
         SPACE
*--------ERROR DETECTED ON CONTROL STATEMENT
BADCARD  MVC   CARDIN,BLANKS           BLANK OUT INPUT RECORD
         MVI   0(R15),C'$'             INDICATE ERROR POSITION
         LA    R10,1(R10)              INCREMENT LINE COUNTER
         PUT   SYSPRINT,PRNTLINE       PRINT MESSAGE
         MVC   CARDMSG,BLANKS          BLANK OUT ERROR MESSAGE FIELD
         MVI   CC,8                    SET ERROR CODE
         B     NEXTCARD
         SPACE
*--------EXECUTED INSTRUCTIONS
COMPTITL CLC   0(0,R15),=C'TITLE '
COMPSEL  CLC   0(0,R15),=C'SELECT '
COMPEXCL CLC   0(0,R15),=C'EXCLUDE '
COMPMEMB CLC   0(0,R15),=C'MEMBER '
COMPPREF CLC   0(0,R15),=C'PREFIX '
CPYNAME1 MVC   NAM1(0),0(R15)
CPYNAME2 MVC   NAM2(0),0(R15)
CMPNAME1 CLC   0(0,R3),NAM1
CMPNAME2 CLC   0(0,R3),NAM2
         SPACE
*--------END OF INPUT
ENDSYSIN CLI   CC,X'00'                TEST FOR ERRORS
         BNE   FREEMAIN                FREE STORAGE, CLOSE FILES, TERM
         CLI   ORDER,C'S'              TEST FOR ORDER=SELECT
         BNE   TESTMODE
         BCTR  R9,0                    REDUCE BY 1
         ST    R9,ENDSELCT             STORE END OF TABLE ADDRESS
TESTMODE CLI   MODE,C' '               TEST MODE
         BNE   MODEEND
         MVI   MODE,C'E'               SET MODE TO EXCLUDE
MODEEND  MVC   PRNTLINE,BLANKS         BLANK OUT PRINT LINE
         TITLE 'CHAIN ALIAS NAMES TO EACH OTHER AND TRUE NAMES'
***********************************************************************
*                                                                     *
*        CHAIN ALIAS NAMES TO EACH OTHER AND TRUE NAMES               *
*                                                                     *
*        20 BYTE ENTRY                                                *
*           0 -  7   NAME                                             *
*           8 - 10   TTR                                              *
*               11   INDICATORS   X'80' MEANS ALIAS                   *
*                                 X'40' MEMBER SELECTED OR EXCLUDED   *
*                                 X'20' MEMBER HAS BEEN PROCESSED     *
*          12 - 15   POINTER TO TRUE NAME                             *
*          16 - 19   POINTER TO NEXT ALIAS (IF ALIAS)                 *
*                    POINTER TO FIRST ALIAS (IF TRUE NAME)            *
*                                                                     *
*        R1  - PREVIOUS ALIAS ADDRESS                                 *
*        R2  - TRUE NAME ADDRESS                                      *
*        R3  - ADDRESS OF ENTRY BEING PROCESSED                       *
*        R4  - LENGTH OF ONE ENTRY                                    *
*        R5  - LAST ENTRY ADDRESS                                     *
*        R6  - ADDRESS OF ENTRY BEING COMPARED                        *
*        R7  - MEMBER COUNTER                                         *
*        R8  - ALIAS COUNTER                                          *
*                                                                     *
***********************************************************************
         SPACE 2
         L     R3,ANAMES               LOAD TABLE ADDRESS
         LA    R4,20                   LENGTH OF ONE ENTRY
         L     R5,ENDNAMES             TABLE LIMIT
         SR    R7,R7                   INITIALIZE MEMBER COUNT
         SR    R8,R8                   INITIALIZE ALIAS COUNT
TESTA    TM    11(R3),X'80'            TEST FOR ALIAS
         BZ    TESTAA
         LA    R8,1(R8)                INCREMENT ALIAS COUNT
         B     TESTAAA
TESTAA   LA    R7,1(R7)                INCREMENT MEMBER COUNT
TESTAAA  NC    12(4,R3),12(R3)         SEE IF POINTER EXISTS
         BNZ   ENDLOOP
         ST    R3,12(R3)               INITIALIZE POINTER
         TM    11(R3),X'80'            TEST FOR ALIAS
         BZ    ENDLOOP
         MVC   WORK(1),11(R3)          SAVE INDICATOR BYTE
         LR    R1,R3                   SAVE PREVIOUS ALIAS ADDRESS
         LR    R2,R3                   SET TRUE NAME PTR TO FIRST ALIAS
         L     R6,ANAMES               LOAD FIRST ENTRY ADDRESS
TESTTTR  CLC   8(3,R3),8(R6)           COMPARE TTR'S
         BNE   ENDTTR
         CR    R3,R6                   POINTING TO MYSELF?
         BE    ENDTTR
         OC    WORK(1),11(R6)          SAVE INDICATOR
         TM    11(R6),X'80'            TEST FOR ALIAS
         BNZ   ALIAS
         LR    R2,R6                   SAVE TRUE NAME ADDRESS
         ST    R3,16(R6)               STORE FIRST ALIAS ADDRESS
         B     ENDTTR
ALIAS    ST    R6,16(R1)               STORE NEXT ALIAS ADDRESS
         LR    R1,R6                   SAVE THIS ALIAS ADDRESS
ENDTTR   BXLE  R6,R4,TESTTTR           CONTINUE THROUGH TABLE
         LR    R6,R3                   LOAD FIRST ALIAS ADDRESS
STORTRUE ST    R2,12(R6)               STORE TRUE NAME IN ALIAS
         L     R6,16(R6)               GET NEXT ALIAS ADDRESS
         LTR   R6,R6                   TEST FOR END OF CHAIN
         BNZ   STORTRUE
         NI    WORK,X'40'              ISOLATE SELECT/EXCLUDE INDICATOR
         OC    11(1,R2),WORK           SET SELECT/EXCLUDE IN TRUE NAME
         CR    R2,R3                   TEST FOR ALIAS WITHOUT TRUE NAME
         BNE   ENDLOOP
         BCTR  R8,0                    DECREMENT ALIAS COUNTER
         LA    R7,1(R7)                INCREMENT MEMBER COUNTER
ENDLOOP  BXLE  R3,R4,TESTA             CONTINUE THROUGH TABLE
         ST    R7,#MEMBERS             STORE COUNTS
         ST    R8,#ALIASES
         TITLE 'SELECT MEMBERS FOR PROCESSING'
***********************************************************************
*                                                                     *
*        SELECT MEMBERS FOR PROCESSING                                *
*                                                                     *
*        IF TRUE NAME POINTER POINTS TO CURRENT ENTRY, THE ENTRY      *
*        IS A TRUE NAME OR AN ALIAS WITHOUT A TRUE NAME AND IS        *
*        ELIGIBLE FOR PROCESSING.  OTHERWISE, THE ENTRY IS AN         *
*        ALIAS AND WILL BE PROCESSED UNDER THE TRUE NAME.             *
*                                                                     *
*        IF MODULE IS SELECTED, PROCESS IT                            *
*        IF MODULE IS EXCLUDED, DO NOT PROCESS IT                     *
*                                                                     *
*        REPLACE ALIAS POINTER FIELD WITH PAGE NUMBER.                *
*                                                                     *
***********************************************************************
         SPACE 2
         MVI   SYNAD,X'00'             CLEAR SYNAD ERROR INDICATOR
         L     R3,SELECTAD             LOAD SELECTION TABLE ADDRESS
         LA    R4,4                    ENTRY LENGTH
         L     R5,ENDSELCT             LOAD END OF TABLE ADDRESS
         L     R10,#LINES              SET LINE COUNT TO FORCE NEW PAGE
         LA    R9,SYSUT1               LOAD DCB ADDRESS
         USING IHADCB,R9               ADDRESSABILITY
PRNTPNCH L     R2,0(R3)                LOAD ENTRY ADDRESS
         TM    11(R2),X'20'            HAS IT BEEN PROCESSED ALREADY?
         BO    NEXTNAM3
         CLI   ORDER,C'S'              TEST FOR ORDER=SELECT
         BNE   TSTMODE
         L     R2,12(R2)               YES, LOAD TRUE NAME POINTER
         B     PROCESS
TSTMODE  C     R2,12(R2)               CHECK FOR POINTER TO ITSELF
         BNE   NEXTNAM3
         CLI   MODE,C'E'               TEST FOR EXCLUDE MODE
         BE    MODEE
         TM    11(R2),X'40'            SEE IF SELECTED
         BZ    NEXTNAM3
         B     PROCESS
MODEE    TM    11(R2),X'40'            SEE IF EXCLUDED
         BO    NEXTNAM3
         SPACE
*--------MEMBER IS TO BE PROCESSED
PROCESS  AP    MCOUNTER,=P'1'          COUNT MEMBER
         MVC   TTR,8(R2)               COPY TTR
         FIND  SYSUT1,TTR,C            POSITION TO MEMBER
         TM    OPTIONS,$PUNCH          TEST FOR A PUNCH OPTION
         BNZ   NOHEADNG
         MVI   PRNTLINE,C'0'           DOUBLE SPACE FIRST LINE
         AP    PGNMBR,=P'1'            INCREMENT PAGE NUMBER
         MVC   PGCOUNT(6),=X'402020202020'   MOVE IN EDIT MASK
         ED    PGCOUNT(6),PGNMBR+1     EDIT PAGE NUMBER
         TM    11(R2),X'80'            TEST FOR ALIAS
         BZ    COPYMEMB
         SPACE
*--------ALIAS WITHOUT TRUE NAME - SET UP DUMMY ENTRY
         ST    R2,ALIASPTR             STORE POINTER IN DUMMY
         LA    R2,NOTRUENM             LOAD DUMMY ADDRESS
         SP    ACOUNTER,=P'1'          DECREMENT ALIAS COUNTER
         SPACE
*--------COPY MEMBER NAME
COPYMEMB MVC   HEADMBR(8),0(R2)        COPY MEMBER NAME
         TM    OPTIONS,$NOPAGE         TEST FOR NOPAGE
         BZ    NEXTPAGE
         MVI   HEADING1,C'-'           SET LINE ADVANCE TO 3 SPACE
         LA    R10,5(R10)              INCREMENT LINE COUNTER
         C     R10,#LINES              TEST AGAINST LIMIT
         BNH   PRNTHDNG
         MVI   HEADING1,C'1'           NEW PAGE
NEXTPAGE LA    R10,3                   SET LINE COUNT TO 3
PRNTHDNG TM    OPTIONS,$NOHDR          TEST FOR NOHEADER
         BO    FIRSTLIN
         PUT   SYSUT2,HEADING1         PRINT HEADER LINE
         PUT   SYSUT2,HEADING2         PRINT SECOND HEADER LINE
         OI    11(R2),X'20'            INDICATE THIS ENTRY PROCESSED
         L     R6,16(R2)               LOAD ALIAS POINTER
         MVC   16(4,R2),PGCOUNT+2      STORE PAGE NO IN ALIAS POINTER
         LTR   R2,R6                   TEST FOR ALIAS POINTER
         BZ    PROCMEMB
         SPACE
*--------ALIAS NAMES TO BE PRINTED - 7 UP
         MVC   PRNTLINE(7),=C'0ALIAS-' SET UP LINE HEADER
SETUPLIN LA    R1,PRNTLINE+11          INITIALIZE POINTER
         LA    R0,7                    INITIALIZE COUNTER
NEXTALAS AP    ACOUNTER,=P'1'          INCREMENT ALIAS COUNTER
         L     R6,16(R2)               LOAD NEXT ALIAS ADDRESS
         MVC   0(8,R1),0(R2)           COPY NAME TO HEADER
         MVC   16(4,R2),PGCOUNT+2      STORE PAGE NO IN TRUE NAME PTR
         OI    11(R2),X'20'            INDICATE THIS ENTRY PROCESSED
         LA    R1,10(R1)               INCREMENT POINTER
         LTR   R2,R6                   COPY AND TEST NEXT ALIAS ADDRESS
         BZ    ENDALIAS
         BCT   R0,NEXTALAS             DECREMENT COUNTER
         LA    R10,1(R10)              INCREMENT LINE COUNTER
         PUT   SYSUT2,PRNTLINE         PRINT ALIASES
         MVC   PRNTLINE,BLANKS         BLANK OUT LINE
         B     SETUPLIN                GO TO PRINT ANOTHER LINE
ENDALIAS LA    R10,1(R10)              INCREMENT LINE COUNTER
         PUT   SYSUT2,PRNTLINE         PRINT ALIASES
         MVC   PRNTLINE,BLANKS         BLANK OUT LINE
         LTR   R2,R2                   TEST FOR MORE NAMES
         BNZ   SETUPLIN
         MVI   PRNTLINE,C'0'           DOUBLE SPACE FIRST LINE
         LA    R10,1(R10)              INCREMENT LINE COUNTER
         B     PROCMEMB
         SPACE
*--------NOHEADER OPTION
FIRSTLIN MVC   PRNTLINE(1),HEADING1    COPY CARRIAGE CONTROL
         S     R10,=F'3'               SUBTRACT 3 FROM LINE COUNT
         OI    11(R2),X'20'            INDICATE THIS ENTRY PROCESSED
         B     PROCMEMB
         SPACE
*--------PUNCH OPTION SELECTED
NOHEADNG OI    11(R2),X'20'            INDICATE THIS ENTRY PROCESSED
         TM    OPTIONS,$UNLOAD         TEST FOR IEBUPTDTE CONTROL CARDS
         BZ    PROCMEMB
         MVC   ADDNAME,0(R2)           COPY MEMBER NAME
         PUT   SYSUT2,ADDCARD          PUT ./ ADD RECORD
         TM    OPTIONS,$SEQ            TEST FOR SEQUENCE OPTION
         BZ    PROCMEMB
         PUT   SYSUT2,NUMBERCD         PUT ./ NUMBER RECORD
         TITLE 'READ MEMBER AND PRINT OR PUNCH'
***********************************************************************
*                                                                     *
*        READ MEMBER AND PRINT OR PUNCH                               *
*                                                                     *
*        R2  - BUFFER POINTER                                         *
*        R3  - MEMBER POINTER                                         *
*        R4  - MEMBER INCREMENT                                       *
*        R5  - MEMBER LIMIT                                           *
*        R6  - RESIDUAL COUNT/RECORD LENGTH                           *
*        R7  - RECORD POINTER                                         *
*        R8  - BUFFER LIMIT                                           *
*        R9  - DCB ADDRESS                                            *
*        R10 - LINE COUNTER                                           *
*                                                                     *
***********************************************************************
         SPACE 2
*--------READ FIRST BLOCK OF MEMBER
PROCMEMB L     R2,BUFFER1              LOAD BUFFER ADDRESS
         XC    RDECB,RDECB             ZERO DECB
         READ  RDECB,                  DECB ADDRESS                    +
               SF,                     READ FORWARD                    +
               SYSUT1,                 DCB ADDRESS                     +
               (R2),                   BUFFER ADDRESS                  +
               'S',                    LENGTH FROM DCB                 +
               MF=(E,READLIST)         EXECUTE FORM OF MACRO
         SPACE
CHECK    CHECK RDECB                   WAIT FOR COMPLETION OF READ
         CLI   SYNAD,0                 TEST FOR SYNAD ERROR
         BNE   BADREC
         MVC   BUFFER1(8),BUFFER2      SWITCH BUFFER ADDRESSES
         MVC   BUFFER3,BUFFER1
         L     R2,BUFFER1              LOAD BUFFER ADDRESS
         L     R6,RDECB+16             LOAD IOB ADDRESS
         LH    R6,14(R6)               LOAD RESIDUAL COUNT
         XC    RDECB,RDECB             CLEAR DECB
*--------INITIATE READ FOR NEXT BLOCK
         READ  RDECB,                  DECB ADDRESS                    +
               SF,                     READ FORWARD                    +
               SYSUT1,                 DCB ADDRESS                     +
               (R2),                   BUFFER ADDRESS                  +
               'S',                    BLOCK LENGTH FROM DCB           +
               MF=(E,READLIST)         EXECUTE FORM
*--------GET END OF BUFFER ADDRESS
         L     R2,BUFFER2              LOAD BUFFER ADDRESS
         LR    R8,R2
         TM    DCBRECFM,DCBRECF        TEST FOR FIXED RECORDS
         BZ    VAREND
*--------FIXED   END = START+BLKSIZE-RESIDUAL
         AH    R8,DCBBLKSI             ADD BLOCKSIZE
         SR    R8,R6                   SUBTRACT RESIDUAL
         B     GETREC
*--------VAR     END = START+BLOCK LENGTH FROM HEADER
*--------        ALSO BUMP POINTER PAST HEADER
VAREND   AH    R8,0(R2)                ADD LENGTH FROM HEADER
         LA    R2,4(R2)                BUMP PAST HEADER
*--------DEBLOCK AND PUT ONE RECORD
GETREC   CR    R2,R8                   TEST FOR END OF BLOCK
         BNL   CHECK                   IF HIGH, NEED ANOTHER BLOCK
         LH    R6,DCBLRECL             LOAD LRECL
         LR    R7,R2                   COPY RECORD ADDRESS
         TM    DCBRECFM,DCBRECF        TEST FOR FIXED RECORDS
         BO    COPYREC
*--------VARIABLE RECORD, GET LENGTH FROM HEADER
         MVC   WORK(2),0(R2)           MOVE LENGTH TO ALIGNED AREA
         LH    R6,WORK                 LOAD RECORD LENGTH
         S     R6,=F'4'                SUBTRACT HEADER LENGTH
         LA    R2,4(R2)                COMPENSATE FOR HEADER
         LR    R7,R2                   COPY RECORD ADDRESS
*--------COPY THE RECORD
COPYREC  AR    R2,R6                   LOAD ADDRESS OF NEXT RECORD
         LR    R15,R6                  COPY RECORD LENGTH
         S     R6,MAXLRECL             SUBTRACT MAX RECORD LENGTH
         C     R15,MAXLRECL            IS LINE OVER MAXIMUM?
         BNH   COPYREC2
         L     R15,MAXLRECL            LINE TOO LONG, LIMIT LENGTH
         MVI   PRNTLINE+132,C'+'       INDICATE CONTINUATION
COPYREC2 BCTR  R15,0                   RECUCE COUNT BY 1
         EX    R15,MOVEREC             COPY LINE TO OUTPUT AREA
         A     R7,MAXLRECL             MOVE RECORD POINTER
         TM    OPTIONS,$PUNCH          TEST FOR A PUNCH OPTION
         BNZ   PUNCHREC
COPYREC3 LA    R10,1(R10)              INCREMENT LINE COUNT
         C     R10,#LINES              TEST AGAINST LIMIT
         BNH   PRINTREC
*--------PRINT HEADERS, ETC
         TM    OPTIONS,$NOPAGE+$NOHDR  TEST FOR NOPAGE OR NOHEADER
         BNZ   SKIPPAGE
         LA    R10,4                   RESET LINE COUNTER
         AP    PGNMBR,=P'1'            INCREMENT PAGE NUMBER
         MVC   PGCOUNT(6),=X'402020202020'   MOVE IN EDIT MASK
         ED    PGCOUNT(6),PGNMBR+1     EDIT PAGE NUMBER INTO HEADER
         PUT   SYSUT2,HEADING1         PRINT HEADERS
         PUT   SYSUT2,HEADING2
         MVI   PRNTLINE,C'0'           SET NEXT LINE TO DOUBLE SPACE
         B     PRINTREC
*--------NOPAGE OPTION SPECIFIED
SKIPPAGE LA    R10,1                   RESET LINE COUNT
         MVI   PRNTLINE,C'1'           SKIP TO NEW PAGE
*--------PRINT A RECORD
PRINTREC PUT   SYSUT2,PRNTLINE         PRINT THE DATA LINE
         MVC   PRNTLINE,BLANKS         BLANK OUT OUTPUT AREA
*--------SEE IF A CONTINUATION RECORD IS REQUIRED
         LTR   R6,R6                   TEST REMAINING RECORD LENGTH
         BNP   GETREC
         LR    R15,R6                  COPY REMAINING RECORD LENGTH
         S     R6,CONLRECL             GET NEW REMAINING LENGTH
         C     R15,CONLRECL            SEE IF OVER LIMIT
         BNH   PRNTCONT
         L     R15,CONLRECL            LOAD MAXIMUM
         MVI   PRNTLINE+132,C'+'       INDICATE CONTINUATION
PRNTCONT BCTR  R15,0                   REDUCE LENGTH BY 1
         EX    R15,MOVEREC2            COPY CONTINUATION LINE
         A     R7,CONLRECL             ADJUST RECORD POINTER
         B     COPYREC3
*--------PUNCH DATA RECORD
PUNCHREC PUT   SYSUT2,PRNTLINE+1       PUNCH THE DATA LINE
         B     GETREC
         TITLE 'SYNAD ERROR ROUTINE'
***********************************************************************
*                                                                     *
*        SYNAD ERROR ROUTINE                                          *
*                                                                     *
***********************************************************************
         SPACE 2
SYNADERR SYNADAF ACSMETH=BPAM          ANALYZE ERROR
         MVC   PRNTLINE+1(120),8(R1)   COPY ERROR MESSAGE
         SYNADRLS
         MVI   SYNAD,4                 INDICATE SYNAD ERROR
         BR    R14                     RETURN
         TITLE 'BAD RECORD PROCESSED'
***********************************************************************
*                                                                     *
*        BAD RECORD PROCESSED                                         *
*                                                                     *
***********************************************************************
         SPACE 2
BADREC   MVC   PRNTLINE(39),=C'-SYNAD ERROR PROCESSING MEMBER XXXXXXXX'
         MVC   PRNTLINE+31(8),0(R3)    COPY MEMBER NAME INTO MESSAGE
         PUT   SYSPRINT,PRNTLINE       DISPLAY ERROR MESSAGE
         MVC   PRNTLINE,BLANKS         BLANK OUT LINE
         MVI   SYNAD,X'00'             RESET ERROR INDICATOR
         MVI   CC,4                    SET CONDITION CODE
         L     R1,SYNADNUM             LOAD SYNAD COUNTER
         LA    R1,1(R1)                INCREMENT COUNTER
         ST    R1,SYNADNUM             STORE NEW COUNT
         C     R1,SYNADLIM             TEST AGAINST LIMIT
         BL    NEXTNAME
         PUT   SYSPRINT,ERRMSG6        PRINT ERROR MESSAGE
         MVI   CC,16
         B     FREEMAIN
         TITLE 'END OF MEMBER REACHED'
***********************************************************************
*                                                                     *
*        END OF MEMBER REACHED                                        *
*                                                                     *
***********************************************************************
         SPACE 2
NEXTNAME TM    OPTIONS,$PUNCH+$NOHDR   SEE IF A PUNCH OR NOHEADER
         BZ    NEXTNAM3
         L     R2,0(R3)                LOAD ADDRESS OF ENTRY
         L     R2,12(R2)               LOAD TRUE NAME ADDRESS
NEXTNAM1 L     R6,16(R2)               LOAD NEXT ALIAS ADDRESS
         MVC   16(4,R2),ACTION         INDICATE PROCESSED
         OI    11(R2),X'20'            INDICATE THIS ENTRY PROCESSED
         TM    OPTIONS,$NOHDR          TEST FOR NOHEADER
         BO    NEXTNAM2
         TM    OPTIONS,$UNLOAD         TEST FOR UNLOAD OPTION
         BZ    NEXTNAM2
         C     R2,12(R2)               TEST FOR ALIAS WITHOUT TRUE NAME
         BE    NEXTNAM2
         TM    11(R2),X'80'            TEST FOR ALIAS
         BZ    NEXTNAM2
         AP    ACOUNTER,=P'1'          INCREMENT ALIAS COUNTER
         MVC   ALIASNM,0(R2)           COPY ALIAS NAME
         PUT   SYSUT2,ALIASCD          PUNCH ALIAS CARD
NEXTNAM2 LTR   R2,R6                   COPY AND TEST NEXT ALIAS ADDRESS
         BNZ   NEXTNAM1
NEXTNAM3 BXLE  R3,R4,PRNTPNCH          CONTINUE PROCESSING LIST
*--------PUT /*EOF IF INTRDR SPECIFIED
         TM    OPTIONS,$INTRDR         TEST FOR INTERNAL READER
         BZ    PUTTOTLS
         PUT   SYSUT2,EOF              GENERATE /*EOF
         DROP  R9
         TITLE 'DISPLAY MEMBER AND ALIAS TOTALS'
***********************************************************************
*                                                                     *
*        DISPLAY MEMBER AND ALIAS TOTALS                              *
*                                                                     *
***********************************************************************
         SPACE 2
PUTTOTLS L     R1,#ENTRIES             LOAD ENTRY COUNT
         CVD   R1,VALUE                CONVERT TO DECIMAL
         ED    TOTAL,VALUE+5           EDIT INTO PRINT LINE
         L     R1,#MEMBERS             LOAD MEMBER COUNT
         CVD   R1,VALUE                CONVERT TO DECIMAL
         ED    MEMBERS,VALUE+5         EDIT INTO PRINT LINE
         L     R1,#ALIASES             LOAD ALIAS COUNT
         CVD   R1,VALUE                CONVERT TO DECIMAL
         ED    ALIASES,VALUE+5         EDIT INTO PRINT LINE
         PUT   SYSPRINT,TOTALS1        PRINT HEADER
         PUT   SYSPRINT,TOTALS2        PRINT IN LIBRARY LINE
         ED    PMEMBERS,MCOUNTER+1     EDIT MEMBERS PROCESSED
         ED    PALIASES,ACOUNTER+1     EDIT ALIASES PROCESSED
         PUT   SYSPRINT,TOTALS3
         TITLE 'COPY TRUE NAME INTO LIST'
***********************************************************************
*                                                                     *
*        COPY TRUE NAME INTO LIST                                     *
*                                                                     *
*        REPLACE TTR AND INDICATOR FIELD AND THE NAME FIELD WITH -    *
*             TRUE NAME IF ALIAS                                      *
*             BLANKS IF TRUE NAME                                     *
*                                                                     *
*        IF MEMBER NOT PROCESSED, BLANK PAGE NUMBER FIELD             *
*                                                                     *
***********************************************************************
         SPACE 2
         SR    R0,R0                   CLEAR INDICATOR
         L     R3,ANAMES               LOAD TABLE ADDRESS
         LA    R4,20                   LENGTH OF ONE ENTRY
         L     R5,ENDNAMES             LOAD TABLE LIMIT
TESTPROC TM    11(R3),X'20'            SEE IF PROCESSED
         BNZ   CHEKALAS
*--------NOT PROCESSED, BLANK OUT PAGE NUMBER FIELD
         MVC   16(4,R3),BLANKS
         LA    R0,1                    SET INDICATOR
CHEKALAS TM    11(R3),X'80'            SEE IF ALIAS
         BO    GETTRUE
         MVC   8(8,R3),BLANKS          COPY IN BLANKS
         B     NEXTTRUE
GETTRUE  L     R1,12(R3)               LOAD TRUE NAME ADDRESS
         MVC   8(8,R3),0(R1)           COPY TRUE NAME
         CR    R1,R3                   TEST FOR NO TRUE NAME
         BNE   NEXTTRUE
         MVC   8(8,R3),NOTRUENM        INDICATE NO TRUE NAME
NEXTTRUE BXLE  R3,R4,TESTPROC          CONTINUE LOOP
         LTR   R0,R0                   SEE IF ANY TO BE REMOVED
         BZ    GENINDEX
         TM    OPTIONS,$FULL           TEST FOR FULL INDEX
         BO    GENINDEX
         TITLE 'COMPRESS UNPROCESSED ENTRIES OUT OF NAME LIST'
***********************************************************************
*                                                                     *
*        IF INDEX=FULL NOT REQUESTED, AND ANY MEMBERS NOT PROCESSED,  *
*        COMPRESS UNPROCESSED ENTRIES OUT OF TABLE.                   *
*                                                                     *
***********************************************************************
         SPACE 2
         L     R3,ANAMES               LOAD TABLE ADDRESS
         LR    R2,R3                   COPY TABLE ADDRESS
COMPRES2 CLC   16(4,R3),BLANKS         SEE IF MEMBER PROCESSED
         BE    COMPRES3
         MVC   0(20,R2),0(R3)          COPY ENTRY
         LA    R2,20(R2)               INCREMENT POINTER
COMPRES3 BXLE  R3,R4,COMPRES2          CONTINUE TO END OF TABLE
         LR    R5,R2                   RESET END OF LIST ADDRESS
         SR    R5,R4
         TITLE 'GENERATE INDEX'
***********************************************************************
*                                                                     *
*        GENERATE INDEX TWO COLUMNS PER PAGE                          *
*                                                                     *
***********************************************************************
         SPACE 2
GENINDEX L     R3,ANAMES               LOAD FIRST ENTRY ADDRESS
         L     R1,#LINES               LOAD LINECOUNT
         S     R1,=F'2'                SUBTRACT 2
         ST    R1,INDEXCTR             STORE COUNTER
         MH    R1,=H'20'               MULTIPLY BY ENTRY LENGTH
         ST    R1,INCR1                STORE INCREMENT 1
         SLA   R1,1                    DOUBLE
         ST    R1,INCR2                STORE INCREMENT 2
NEWPAGE  LR    R2,R3                   COPY IT
         L     R6,INDEXCTR             INITIALIZE COUNTER
         PUT   SYSPRINT,INDEXHDR       PRINT HEADER LINE
         MVI   PRNTLINE,C'0'           DOUBLE SPACE
NEXTLINE MVC   PRNTLINE+1(132),BLANKS  BLANK OUT LINE
         LA    R7,2                    SET COUNTER
         LA    R1,PRNTLINE+1           INITIALIZE POINTER
         LR    R15,R2                  COPY ENTRY ADDRESS
INDXLOOP CR    R15,R5                  SEE IF PAST END OF TABLE
         BH    PRNTINDX
         MVC   0(8,R1),0(R15)          COPY NAME
         MVC   13(4,R1),16(R15)        COPY PAGE NUMBER
         MVC   9(3,R1),=C'...'
         MVC   20(8,R1),8(R15)         COPY TRUE NAME
         LA    R1,40(R1)               INCREMENT POINTER
         A     R15,INCR1               POINT TO NEXT COLUMN
         BCT   R7,INDXLOOP
PRNTINDX PUT   SYSPRINT,PRNTLINE       PRINT INDEX LINE
         MVI   PRNTLINE,C' '           RESET CARRIAGE CONTROL
         LA    R2,20(R2)               INCREMENT BASE POINTER
         CR    R2,R5                   TEST AGAINST LIMIT
         BH    FREEMAIN
         BCT   R6,NEXTLINE             DECREMENT LINE COUNT
         A     R3,INCR2                SKIP TO NEXT PAGE BASE
         CR    R3,R5                   TEST AGAINST LIMIT
         BNH   NEWPAGE
         TITLE 'CLEAN UP AND EXIT'
***********************************************************************
*                                                                     *
*        FREE STORAGE FOR BUFFERS                                     *
*                                                                     *
*        FREE STORAGE FOR NAME TABLES                                 *
*                                                                     *
*        CLOSE DATA FILES                                             *
*                                                                     *
*        RETURN TO OS                                                 *
*                                                                     *
***********************************************************************
         SPACE 2
*--------FREE BUFFERS
FREEMAIN L     R3,BUFFLEN              LOAD LENGTH OF WORK AREA
         L     R1,BUFFERS              LOAD ADDRESS OF WORK AREA
         FREEMAIN R,LV=(R3),A=(R1),SP=1  FREE BUFFER WORK AREA
         SPACE
*--------FREE NAME TABLE
FREENAME L     R3,SELLEN               LOAD LENGTH OF SELECTION TABLE
         L     R1,SELECTAD             LOAD ADDRESS OF SELECTION TABLE
         FREEMAIN R,LV=(R3),A=(R1),SP=1  FREE SELECTION TABLE AREA
         L     R3,NAMELEN              LOAD LENGTH OF WORK AREA
         L     R1,ANAMES               LOAD ADDRESS OF WORK AREA
         FREEMAIN R,LV=(R3),A=(R1),SP=1  FREE MEMBER NAME WORK AREA
         SPACE
*--------CLOSE FILES
CLOSE3   CLOSE (SYSIN,,                                                +
               SYSUT1,,                                                +
               SYSUT2)
         SPACE
*--------CLOSE MESSAGE FILE AND RETURN TO OS
RETURN   CLOSE SYSPRINT
         SR    R15,R15                 LOAD RETURN CODE
         IC    R15,CC
         $RTRN RC=(R15)
         TITLE 'DATA AREAS'
MOVEREC  MVC   PRNTLINE+1(0),0(R7)     COPY LINE TO PRINT LINE
MOVEREC2 MVC   PRNTLINE+11(0),0(R7)    COPY CONTINUATION LINE
         SPACE 2
SAVEAREA DS    18F                     SAVE AREA
VALUE    DC    D'0'                    DECIMAL CONVERSION WORK AREA
POWERVAL DC    F'1'                    VALUE OF POWER PARAMETER
FCORE    DC    F'8192'                 NUMBER OF BYTES PER POWER VALUE
HCORE    DC    H'8192'                 HALFWORD - SAME AS FCORE
HCORE2   DC    H'1640'                 SELECTION TABLE SIZE
*                                           (FCORE+19)/20*4
#LINES   DC    F'60'                   LINES PER PAGE
INDEXCTR DC    F'0'                    LINES IN INDEX (#LINES-2)
INCR1    DC    F'0'                    DISPLACEMENT TO COL 2 ENTRIES
INCR2    DC    F'0'                    DISPLACEMENT TO NEXT PAGE ENT.
UT2BLKSI DC    H'133'                  SYSUT2 DEFAULT BLOCK SIZE
ANAMES   DS    A                       ADDRESS OF NAME TABLE
ENDNAMES DS    A                       ADDRESS OF END OF NAME TABLE
#MEMBERS DC    F'0'                    MEMBERS IN LIBRARY COUNT
#ALIASES DC    F'0'                    ALIASES IN LIBRARY COUNT
#ENTRIES DC    F'0'                    DIRECTORY ENTRIES COUNT
ACOUNTER DC    PL4'0'                  ALIASES PROCESSED COUNT
MCOUNTER DC    PL4'0'                  MEMBERS PROCESSED COUNT
MAXLRECL DC    F'132'                  MAXIMUM LRECL
CONLRECL DC    F'0'                    CONTINUATION LRECL
NAMELEN  DS    F                       LENGTH OF NAME TABLE WORK AREA
BUFFLEN  DS    F                       LENGTH OF BUFFERS
BUFFERS  DS    A                       ADDRESS OF BUFFER AREA
BUFFER1  DS    A                       NEXT BUFFER ADDRESS
BUFFER2  DS    A                       IN-USE BUFFER ADDRESS
BUFFER3  DS    A                       SAME AS BUFFER1
SELECTAD DS    A                       SELECTION TABLE ADDRESS
ENDSELCT DS    A                       SELECTION TABLE END
SELLEN   DS    F                       SELECTION TABLE LENGTH
SYNADLIM DC    F'8'                    SYNAD ERROR LIMIT
SYNADNUM DC    F'0'                    SYNAD ERROR COUNT
TIMEMASK DC    X'1000000F'
ACTION   DC    C' PCH'
         SPACE
NAM1     DC    CL8' '                  FIRST NAME OF RANGE
NAM2     DC    CL8' '                  LAST NAME OF RANGE
NAMELEN1 DC    H'0'                    LENGTH OF FIRST NAME
NAMELEN2 DC    H'0'                    LENGTH OF LAST NAME
         SPACE
PGNMBR   DC    PL4'0'                  PAGE NUMBER
MODE     DC    C' '                    S FOR SELECT, E FOR EXCLUDE
ORDER    DC    C'A'                    A FOR ALPHA,  S FOR SELECT
MEMPRFX  DC    C' '                    M FOR MEMBER, P FOR PREFIX
*
SYNAD    EQU   MEMPRFX                 04 IF SYNAD ERROR DETECTED
*
CC       DC    X'00'                   CONTITION CODE
*
OPTIONS  DC    X'00'
*        EQU   X'80'  X... ....  NOT USED
$NOHDR   EQU   X'40'  .1.. ....  NOHEADER
$NOPAGE  EQU   X'20'  ..1. ....  NOPAGE
$FULL    EQU   X'10'  ...1 ....  FULL INDEX REQUESTED
$PUNCH   EQU   X'08'  .... 1...  PUNCH OPTION
$UNLOAD  EQU   X'04'  .... .1..  UNLOAD OPTION
$SEQ     EQU   X'02'  .... ..1.  SEQ OPTION
$INTRDR  EQU   X'01'  .... ...1  INTRDR OPTION
         SPACE
READLIST READ  RDECB,SF,,,'S',MF=L     LIST FORM OF READ MACRO
         DS    0F
NOTRUENM DC    CL8'********'           DUMMY ENTRY FOR ALIAS WITHOUT
*                                            A TRUE NAME
TTR      DC    XL3'00'                 TTR OF MEMBER
         DC    X'00'
         DC    F'0'
ALIASPTR DC    F'0'                    POINTER TO ALIAS
         SPACE
BLANKS   DC    CL133' '
         SPACE
INVALID  DC    C'0INVALID PARAMETER FIELD SPECIFIED - '         37   37
PARM     DC    CL17' '                                          17   54
         DC    C' - '                                            3   57
REASON   DC    CL76' '                                          76  133
         SPACE
PARMFLD  DC    C'0PARM FIELD - '                                14   14
PARMDATA DC    CL119' '                                        119  133
NOPARMS  DC    CL133'0NO PARAMETER FIELD SPECIFIED'
         SPACE
ERRMSG1  DC    CL133' U'
         ORG   ERRMSG1
         DC    C' UNABLE TO OPEN '
DDNAME   DC    CL8' '
         ORG
         SPACE
ERRMSG5  DC    CL133' PUNCHED OUTPUT REQUESTED - DATA SET IS NOT CARD I+
               MAGE.  RUN TERMINATED.'
ERRMSG6  DC    CL133'-SYNAD ERROR LIMIT REACHED. RUN TERMINATED.'
         SPACE
LOWPOWER DC    CL133'0'
         ORG   LOWPOWER
         DC    C'0INSUFFICIENT STORAGE FOR'
ENTRIES  DC    X'402020202120'
         DC    C' DIRECTORY ENTRIES.  POWER ='
REQPOWER DC    X'40202120'
         DC    C' REQUIRED.'
         ORG
         SPACE
PRNTLINE DC    CL133' '
         ORG   PRNTLINE
         DC    C' '
CARDMSG  DC    CL20' '
         DC    CL2' '
CARDIN   DC    CL80' '
         ORG
         SPACE
ADDCARD  DC    CL80' '
         ORG   ADDCARD
         DC    C'./ ADD   NAME='
ADDNAME  DC    CL8' '
         ORG
         SPACE
NUMBERCD DC    CL80'./ NUMBER NEW1=10,INCR=10'
         SPACE
ALIASCD  DC    CL80' '
         ORG   ALIASCD
         DC    C'./ ALIAS NAME='
ALIASNM  DC    CL8' '
         ORG
         SPACE
PGSPACE  DC    H'1'
         SPACE
EOF      DC    CL80'/*EOF'
         SPACE
HEADING1 DC    CL133' '
         ORG   HEADING1
         DC    C'1'
         DC    C'PAGE '
PGCOUNT  DC    CL6' '
         DC    CL5' '
TITLE    DC    CL30' '
         DC    C'   DATE='
YEARDATE DC    CL8' '
         DC    CL1' '
JULDATE  DC    CL3' '
         DC    C' TIM'
TIMEDAY  DC    X'402020204B20204B2020'
         ORG
         SPACE
HEADING2 DC    CL133' '
         ORG   HEADING2
         DC    C' '
         DC    C'MEMBER='
HEADMBR  DC    CL8' '
         DC    C' DSNAME='
DSNAME   DC    CL44' '
         DC    C'   VOL='
VOLUME   DC    CL6' '
         ORG
         SPACE
INDEXHDR DC    C'1'
         DC    CL40'-MEMBER-     PAGE  TRUE NAME'
         DC    CL40'-MEMBER-     PAGE  TRUE NAME'
         DC    CL52' '
         SPACE
TOTALS1  DC    CL133'-                       TOTAL MEMBERS ALIASES'
         SPACE
TOTALS2  DC    CL133' '
         ORG   TOTALS2
         DC    CL23' ENTRIES IN DIRECTORY'
TOTAL    DC    X'402020202120'
         DC    C' '
MEMBERS  DC    X'402020202120'
         DC    C'  '
ALIASES  DC    X'402020202120'
         ORG
         SPACE
TOTALS3  DC    CL133' '
         ORG   TOTALS3
         DC    CL23' ENTRIES PROCESSED'
         DC    CL6' '
         DC    C' '
PMEMBERS DC    X'402020202120'
         DC    C'  '
PALIASES DC    X'402020202120'
         ORG
         SPACE
MO       DS    CL2                      MONTH
SLASH1   DS    C                        SLASH
DA       DS    CL2                      DAY
SLASH2   DS    C                        SLASH
JULIAN   DS    0CL5                     JULIAN DATE - EBCDIC
YR       DS    CL2                      YEAR
JULDAY   DS    CL3                      JULIAN DAY
         SPACE
***********************************************************************
*                                                                     *
*        CALENDAR TABLE - NUMBER OF DAYS IN EACH MONTH (LEAP YEAR)    *
*                                                                     *
***********************************************************************
         SPACE
TABLE    DC    PL2'31'                  JANUARY
         DC    PL2'29'                  FEBRUARY
         DC    PL2'31'                  MARCH
         DC    PL2'30'                  APRIL
         DC    PL2'31'                  MAY
         DC    PL2'30'                  JUNE
         DC    PL2'31'                  JULY
         DC    PL2'31'                  AUGUST
         DC    PL2'30'                  SEPTEMBER
         DC    PL2'31'                  OCTOBER
         DC    PL2'30'                  NOVEMBER
         DC    PL2'31'                  DECEMBER
         EJECT
SYSUT2   DCB   DDNAME=SYSUT2,                                          +
               DSORG=PS,                                               +
               MACRF=PM,                                               +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               EXLST=UT2XLIST
         SPACE 2
UT2XLIST DC    X'85',AL3(UT2EXIT)       DCB EXIT ROUTINE ADDRESS
         SPACE 2
*--------DCB EXIT ROUTINE - SET BLKSIZE IF NOT SPECIFIED IN JCL
         USING IHADCB,R1                DCB ADDRESSABILITY
UT2EXIT  NC    DCBBLKSI,DCBBLKSI        SEE IF BLKSIZE SPECIFIED
         BNZR  R14                      YES - RETURN
         MVC   DCBBLKSI,UT2BLKSI        NO  - SET BLKSIZE
         BR    R14                      RETURN
         DROP  R1
         EJECT
SYSUT1   DCB   DDNAME=SYSUT1,                                          +
               EODAD=PDSEOF,                                           +
               DSORG=PS,                                               +
               RECFM=U,                                                +
               BLKSIZE=264,                                            +
               KEYLEN=8,                                               +
               MACRF=R,                                                +
               SYNAD=SYNADERR,                                         +
               EXLST=UT1XLIST
         SPACE 2
         USING IHADCB,R1
UT1XLIST DC    X'00',AL3(UT1EXIT)       DCB EXIT ROUTINE (DEACTIVATED)
         DC    X'87',AL3(JFCB)
JFCB     DS    XL176
         SPACE 2
UT1EXIT  TM    DCBRECFM,DCBRECF+DCBRECSB TEST FOR FS
         BNOR  R14                     RETURN IF NOT
         NI    DCBRECFM,X'FF'-DCBRECSB TURN OFF S INDICATOR
         BR    R14                     RETURN
         DROP  R1
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,                                        +
               DSORG=PS,                                               +
               MACRF=PM,                                               +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               EXLST=PRTXLIST
         SPACE 2
PRTXLIST DC    X'85',AL3(PRTEXIT)       DCB EXIT ROUTINE ADDRESS
         SPACE 2
*--------DCB EXIT ROUTINE - SET BLKSIZE IF NOT SPECIFIED IN JCL
         USING IHADCB,R1                DCB ADDRESSABILITY
PRTEXIT  NC    DCBBLKSI,DCBBLKSI        SEE IF BLKSIZE SPECIFIED
         BNZR  R14                      YES - RETURN
         MVC   DCBBLKSI,=H'133'         NO  - SET BLKSIZE
         BR    R14                      RETURN
         DROP  R1
         EJECT
SYSIN    DCB   DDNAME=SYSIN,                                           +
               DSORG=PS,                                               +
               MACRF=GM,                                               +
               EODAD=ENDSYSIN,                                         +
               RECFM=FB,                                               +
               LRECL=80,                                               +
               EXLST=INXLIST
         SPACE 2
INXLIST  DC    X'85',AL3(INEXIT)        DCB EXIT ROUTINE ADDRESS
         SPACE 2
*--------DCB EXIT ROUTINE - SET BLKSIZE IF NOT SPECIFIED IN JCL
         USING IHADCB,R1                DCB ADDRESSABILITY
INEXIT   NC    DCBBLKSI,DCBBLKSI        SEE IF BLKSIZE SPECIFIED
         BNZR  R14                      YES - RETURN
         MVC   DCBBLKSI,=H'80'          NO  - SET BLKSIZE
         BR    R14                      RETURN
         DROP  R1
         EJECT
         LTORG
         SPACE
         DS    0F
LIBDIR   DS    264C
         ORG   LIBDIR
WORK     DS    0F                       DATE
         DS    C                        FILLER
DATE     DS    0CL3                     JULIAN DATE - PACKED DECIMAL
YEAR     DS    C                        YEAR - NO SIGN
DAY      DS    CL2                      DD DS
MONTHP   DS    PL2                      0M MS
TARGET   DS    0CL3                     00 0Y YS  -  0Q QS RS
         DS    C                        0Q         PART OF QUOTIENT
YEAR2    DS    0PL2                     0Y YS      YEAR AND SIGN
         DS    C                        QS         QUOTIENT AND SIGN
REM      DS    C                        RS         REMAINDER AND SIGN
         ORG
         SPACE 2
         ORG   JFCB
         IEFJFCBN
         ORG
         TITLE 'PRINT IDENTIFICATION BLOCK'
***********************************************************************
*                                                                     *
*        PRINT IDENTIFICATION BLOCK                                   *
*                                                                     *
*        OBTAIN PROGRAM NAME AND DATE AND TIME OF ASSEMBLY FROM       *
*        CODE GENERATED BY $ENTER MACRO.  OBTAIN JOBNAME AND          *
*        STEPNAMES FROM TIOT.  PRINT IDENTIFICATION BLOCK WITH        *
*        MESSAGE OUTPUT DATA SET.                                     *
*                                                                     *
*        R3  - SUBROUTINE BASE REGISTER                               *
*        R4  - RETURN ADDRESS                                         *
*        R11 - BASE ADDRESS FOR PROGRAM AND DATA                      *
*        R12 - BASE ADDRESS FOR PROGRAM AND DATA                      *
*                                                                     *
***********************************************************************
         SPACE 2
PRINTID  LR    R3,R15                  ESTABLISH BASE ADDRESS
         USING PRINTID,R3              ADDRESSABILITY
         MVI   PRNTLINE,C'1'           NEW PAGE
         MVI   CARDIN,C'*'             BLOCK OF *'S
         MVC   CARDIN+1(79),CARDIN
         PUT   SYSPRINT,PRNTLINE       PRINT LINE
         MVI   PRNTLINE,C' '           SINGLE SPACE
         MVC   CARDIN+1(78),BLANKS     BLANK BOX
         PUT   SYSPRINT,PRNTLINE       PRINT LINE
         MVC   CARDIN+04(07),=C'PROGRAM'
         MVC   CARDIN+12(08),SS0300+5
         MVC   CARDIN+21(5),=C'V 3.2'  INSERT VERSION ID
         MVC   CARDIN+28(26),=C'DATE AND TIME OF ASSEMBLY '
         MVC   CARDIN+58(14),SS0300+14
         PUT   SYSPRINT,PRNTLINE       PRINT LINE
         MVC   CARDIN+1(78),BLANKS     BLANK BOX
         MVC   CARDIN+28(26),=C'DATE AND TIME OF EXECUTION'
         MVC   CARDIN+58(8),YEARDATE   COPY IN DATE
         MVC   CARDIN+67(8),TIMEDAY+2  COPY IN TIME
         PUT   SYSPRINT,PRNTLINE       PRINT LINE
         MVC   CARDIN+1(78),BLANKS     BLANK BOX
         PUT   SYSPRINT,PRNTLINE
         EXTRACT WORK,,FIELDS=TIOT     GET TIOT ADDRESS
         L     R1,WORK
         MVC   CARDIN+04(08),=C'JOBNAME-'
         MVC   CARDIN+12(08),0(R1)     COPY JOBNAME FROM TIOT
         MVC   CARDIN+28(09),=C'STEPNAME-'
         MVC   CARDIN+37(08),8(R1)
         MVC   CARDIN+50(09),=C'PROCSTEP-'
         MVC   CARDIN+59(08),16(R1)
         PUT   SYSPRINT,PRNTLINE
         MVC   CARDIN+1(78),BLANKS
         PUT   SYSPRINT,PRNTLINE
         MVC   CARDIN+1(78),CARDIN
         PUT   SYSPRINT,PRNTLINE
         MVC   PRNTLINE,BLANKS
         BR    R4                      RETURN
         DROP  R3
         LTORG
         SPACE 2
         DCBD  DSORG=BS,DEVD=DA
         SPACE 2
***********************************************************************
*                                                                     *
*        CALENDAR TABLE DSECT                                         *
*                                                                     *
***********************************************************************
         SPACE
CALENDAR DSECT
DAYS     DS    PL2                      DAYS IN MONTH
LEN      EQU   *-DAYS                   LENGTH OF ENTRY
         END
