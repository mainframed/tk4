         MACRO
&NAME    #TSOBLKS &DSECTS,&CPPL=,&IOPL=,&PPL=,&DAPL=,&ECB=
         MNOTE *,'       #TSOBLKS  VERSION 002 06/06/75  09/22/75  GPW'
.**********************************************************************
.*                                                                    *
.* #TSOBLKS                                                           *
.*                                                                    *
.* FUNCTION       CONSTRUCT TSO CONTROL BLOCKS REQUIRED BY TSO        *
.*                COMMAND PROCESSORS.                                 *
.*                                                                    *
.* DESCRIPTION    CODE TO INITIALIZE TSO CONTROL BLOCKS IS GENERATED  *
.*                BY THE MACRO.  DSECTS DESCRIBING THE CONTROL BLOCKS *
.*                ARE GENERATED IF REQUESTED.  THE CONTROL BLOCKS ARE *
.*                GENERATED USING USER SUPPLIED INFORMATION AND DATA  *
.*                FROM THE COMMAND PROCESSOR PARAMETER LIST (CPPL).   *
.*                                                                    *
.*                THE CONTROL BLOCKS WHICH MAY BE GENREATED ARE:      *
.*                                                                    *
.*                ECB  - EVENT CONTROL BLOCK.  THE USER MUST SPECIFY  *
.*                       THE SYMBOLIC NAME OF THE ECB.  IT WILL BE    *
.*                       INITIALIZED TO ZERO.                         *
.*                                                                    *
.*                IOPL - INPUT/OUTPUT PARAMETER LIST.  THE USER MUST  *
.*                       SPECIFY THE REGISTER TO BE USED AS THE IOPL  *
.*                       BASE REGISTER AND THE SYMBOLIC LABEL OF THE  *
.*                       IOPL.  HE MAY SPECIFY THE LABEL OF AN IOPB   *
.*                       IF IT IS TO BE INCLUDED IN THE IOPL.         *
.*                                                                    *
.*                PPL  - PARSE PARAMETER LIST.  THE USER MUST SPECIFY *
.*                       THE REGISTER TO BE USED AS THE PPL BASE      *
.*                       REGISTER, THE SYMBOLIC NAME OF THE PPL,      *
.*                       THE SYMBOLIC NAME OF THE PCL CSECT, AND THE  *
.*                       SYMBOLIC NAME OF THE ANSWER LIST LOCATION.   *
.*                       THE SYMBOLIC NAME OF A USER WORK AREA MAY    *
.*                       ALSO BE SPECIFIED.                           *
.*                                                                    *
.*                DAPL - DYNAMIC ALLOCATION PARAMETER LIST.  THE USER *
.*                       MUST SPECIFY THE REGISTER TO BE USED AS THE  *
.*                       DAPL BASE REGISTER AND THE SYMBOLIC NAME     *
.*                       OF THE DAPL.  A DAPB NAME MAY BE SPECIFIED.  *
.*                                                                    *
.*                THE LENGTHS OF THE CONTROL BLOCKS GENERATED WILL BE *
.*                GIVEN BY THE LABELS IOPLLEN, PPLLEN, DAPLLEN.       *
.*                THE USER MAY USE THESE VALUES TO ALLOCATE AREAS FOR *
.*                THE CONTROL BLOCKS.  USE OF THESE LABELS INSURE     *
.*                THAT THE PROPER AMOUNT OF SPACE WILL BE ALLOCATED.  *
.*                IF THE SIZE OF THESE BLOCKS ARE CHANGED BY IBM,     *
.*                ONLY A RE-ASSEMBLY AND LINK WILL BE NECESSARY.      *
.*                                                                    *
.* SYNTAX         NAME     #TSOBLKS  DSECT                            *
.*                                   CPPL=(REG1,REG2)                 *
.*                                   IOPL=(REG3,SYM1,SYM2,SYM3)       *
.*                                   PPL=(REG4,SYM4,SYM5,SYM6,SYM7)   *
.*                                   DAPL=(REG5,SYM8,SYM9,SYM10)      *
.*                                   ECB=SYM11                        *
.*                                                                    *
.*                NAME   - A SYMBOLIC TAG ASSIGNED TO THE FIRST       *
.*                         INSTRUCTION GENERATED.                     *
.*                                                                    *
.*                DSECT  - INDICATES THAT DSECTS ARE TO BE GENERATED  *
.*                         FOR THE CONTROL BLOCKS SPECIFIED.          *
.*                                                                    *
.*                CPPL   - COMMAND PROCESSOR PARAMETER LIST, TWO      *
.*                         REQUIRED SUBPARAMETERS.                    *
.*                         1. - REGISTER TO BE USED AS THE CPPL BASE  *
.*                              REGISTER.                             *
.*                         2. - REGISTER CONTAINING THE CPPL ADDRESS. *
.*                                                                    *
.*                IOPL   - INPUT/OUTPUT PARAMETER LIST, TWO REQUIRED  *
.*                         SUBPARAMETERS AND ONE OPTIONAL             *
.*                         SUBPARAMETER.                              *
.*                         1.  - REGISTER TO BE USED AS IOPL BASE     *
.*                               REGISTER.                            *
.*                         2.  - SYMBOLIC LABEL OF USER ALLOCATED     *
.*                               AREA TO BE USED FOR IOPL.            *
.*                         3.  - SYMBOLIC LABEL OF AN IOPB (PARAMETER *
.*                               BLOCK FOR GETLINE, PUTLINE, PUTGET,  *
.*                               OR STACK).  THE IOPB ADDRESS MAY BE  *
.*                               SPECIFIED IN THE GETLINE, PUTLINE,   *
.*                               PUTGET OR STACK MACRO.               *
.*                                                                    *
.*                PPL    - PARSE PARAMETER LIST, FOUR REQUIRED        *
.*                         SUBPARAMETERS AND ONE OPTIONAL             *
.*                         SUBPARAMETER.                              *
.*                         1.  - REGISTER TO BE USED AS PPL BASE      *
.*                               REGISTER.                            *
.*                         2.  - SYMBOLIC LABEL OF USER ALLOCATED     *
.*                               AREA TO BE USED FOR PPL.             *
.*                         3.  - SYMBOLIC LABEL OF PCL CSECT (NAME    *
.*                               FIELD OF IKJPARM MACRO.              *
.*                         4.  - SYMBOLIC LABEL OF USER ALLOCATED     *
.*                               FULLWORD WHERE ANSWER LIST (PDL)     *
.*                               ADDRESS WILL BE PLACED.              *
.*                         5.  - SYMBOLIC LABEL OF USER WORK AREA.    *
.*                                                                    *
.*                DAPL   - DYNAMIC ALLOCATION PARAMETER LIST, TWO     *
.*                         REQUIRED SUBPARAMETERS AND ONE OPTIONAL    *
.*                         SUBPARAMETER.                              *
.*                         1.  - REGISTER TO BE USED AS DAPL BASE     *
.*                               REGISTER.                            *
.*                         2.  - SYMBOLIC LABEL OF USER ASSIGNED AREA *
.*                               TO BE USED FOR DAPL.                 *
.*                         3.  - SYMBOLIC LABEL OF A DAPB.  THE DAPB  *
.*                               ADDRESS MAY BE SUPPLIED BY THE USER  *
.*                               BEFORE INVOKING IKJDAIR.             *
.*                                                                    *
.*                ECB    - EVENT CONTROL BLOCK, REQUIRED.  SYMBOLIC   *
.*                         LABEL OF A USER ASSIGNED FULLWORD TO BE    *
.*                         USED AS AN ECB.                            *
.*                                                                    *
.*                                                                    *
.* ERRORS         THE NUMBER OF SUBPARAMETERS FOR EACH PARAMETER IS   *
.*                CHECKED.  IF ANY PARAMETER IS SPECIFIED INCORRECTLY,*
.*                A MESSAGE WILL BE DISPLAYED AND NO CODE OR DSECTS   *
.*                WILL BE GENERATED.  EACH PARAMETER WILL BE CHECKED, *
.*                EVEN IF A PRECEEDING PARAMETER WAS FOUND TO BE      *
.*                INCORRECT.  ALL ERRORS ARE SEVERITY CODE 8.         *
.*                                                                    *
.* EXAMPLE        BUILD    #TSOBLKS DSECT,ECB=DSATECB,              X *
.*                               CPPL=(R11,R1),                     X *
.*                               IOPL=(R10,DSATIOPL,DSATPTPB),      X *
.*                               PPL=(R9,DSATPPL,DSATPCL,DSATANS)     *
.*                              .                                     *
.*                              .                                     *
.*                              .                                     *
.*                DSATIOPL DS    0F       ALIGNMENT                   *
.*                         ORG   *+IOPLLEN                            *
.*                DSATPPL  DS    0F                                   *
.*                         ORG   *+PPLLEN                             *
.*                DSATECB  DS    F                                    *
.*                DSATANS  DS    A                                    *
.*                DSATPTPB PUTLINE MF=L                               *
.*                              .                                     *
.*                              .                                     *
.*                              .                                     *
.*                DSATPCL  IKJPARM DSECT=DSATPDL                      *
.*                                                                    *
.* GLOBAL SYMBOLS                                                     *
.*                                                                    *
.*                NONE                                                *
.*                                                                    *
.* MACROS CALLED                                                      *
.*                                                                    *
.*                IKJCPPL   IKJIOPL   IKJPPL    IKJDAPL               *
.*                                                                    *
.**********************************************************************
.*
         LCLA  &IER                     ERROR INDICTOR
.*
&IER     SETA  0                        INITIALIZE ERROR INDICATOR
         AIF   (N'&CPPL EQ 2).CKIOPL
         MNOTE 8,'*** CPPL SUBPARAMETERS INCORRECT'
&IER     SETA  1                        SET ERROR INDICATOR
.CKIOPL  AIF   (N'&IOPL EQ 0 OR N'&IOPL EQ 2 OR N'&IOPL EQ 3).CKPPL
         MNOTE 8,'*** IOPL SUBPARAMETERS INCORRECT'
&IER     SETA  1                        SET ERROR INDICATOR
.CKPPL   AIF   (N'&PPL EQ 0 OR N'&PPL EQ 4 OR N'&PPL EQ 5).CKDAPL
         MNOTE 8,'*** CPPL SUBPARAMETERS INCORRECT'
&IER     SETA  1                        SET ERROR INDICATOR
.CKDAPL  AIF   (N'&DAPL EQ 0 OR N'&DAPL EQ 2 OR N'&DAPL EQ 3).CKECB
         MNOTE 8,'*** DAPL SUBPARAMETERS INCORRECT'
&IER     SETA  1                        SET ERROR INDICATOR
.CKECB   AIF   ('&ECB' NE '').CKERR
         MNOTE 8,'*** ECB PARAMETER MISSING'
&IER     SETA  1                        SET ERROR INDICATOR
.CKERR   AIF   (&IER EQ 0).PARMSOK
         MEXIT
.PARMSOK AIF   ('&DSECTS' NE 'DSECT').USING
         MNOTE *,' IKJCPPL'
         IKJCPPL
* END IKJCPPL - #TSOBLKS
         SPACE 4
         AIF   ('&IOPL' EQ '').IKJPPL
         MNOTE *,' IKJIOPL'
         IKJIOPL
* END IKJIOPL - #TSOBLKS
IOPLLEN  EQU   *-IOPL
         SPACE 4
.IKJPPL  AIF   ('&PPL' EQ '').IKJDAPL
         MNOTE *,' IKJPPL'
         IKJPPL
* END IKJPPL  - #TSOBLKS
PPLLEN   EQU   *-PPL
         SPACE 4
.IKJDAPL AIF   ('&DAPL' EQ '').EJECT
         MNOTE *,' IKJDAPL'
         IKJDAPL
* END IKJDAPL - #TSOBLKS
DAPLLEN  EQU   *-DAPL
.EJECT   EJECT
&SYSECT  CSECT                          CONTINUE USER CSECT
.USING   ANOP
***********************************************************************
*                                                                     *
*                     SET UP TSO CONTROL BLOCKS                       *
*                                                                     *
***********************************************************************
&NAME    XC    &ECB.(4),&ECB            ZERO ECB
         LR    &CPPL(1),&CPPL(2)        LOAD CPPL ADDRESS
         USING CPPL,&CPPL(1)            DEFINE CPPL BASE REGISTER
         AIF   ('&IOPL' EQ '').UPPL     SEE IF IOPL REQUESTED
         LA    &IOPL(1),&IOPL(2)
         USING IOPL,&IOPL(1)
.UPPL    AIF   ('&PPL' EQ '').UDAPL
         LA    &PPL(1),&PPL(2)
         USING PPL,&PPL(1)
.UDAPL   AIF   ('&DAPL' EQ '').SETUP
         LA    &DAPL(1),&DAPL(2)
         USING DAPL,&DAPL(1)
.SETUP   ANOP
         AIF   ('&IOPL' EQ '').BLDPPL
*--------BUILD IOPL
         MVC   IOPLUPT(4),CPPLUPT       COPY UPT ADDRESS
         MVC   IOPLECT(4),CPPLECT       COPY ECT ADDRESS
         LA    R1,&ECB                  GET ECB ADDRESS
         ST    R1,IOPLECB
         AIF   ('&IOPL(3)' EQ '').BLDPPL
         LA    R1,&IOPL(3)              GET IOPB ADDRESS
         ST    R1,IOPLIOPB
.BLDPPL  AIF   ('&PPL' EQ '').BLDDAPL
*--------BUILD PPL
         MVC   PPLUPT(4),CPPLUPT        COPY UPT ADDRESS
         MVC   PPLECT(4),CPPLECT        COPY ECT ADDRESS
         LA    R1,&ECB                  GET ECB ADDRESS
         ST    R1,PPLECB
         L     R1,=V(&PPL(3))           GET PCL ADDRESS
         ST    R1,PPLPCL
         LA    R1,&PPL(4)               GET ANSWER AREA WORD ADDRESS
         ST    R1,PPLANS
         MVC   PPLCBUF(4),CPPLCBUF      COPY COMMAND BUFFER ADDRESS
         AIF   ('&PPL(5)' EQ '').BLDDAPL
         LA    R1,&PPL(5)               GET USER WORK AREA ADDRESS
         ST    R1,PPLUWA
.BLDDAPL AIF   ('&DAPL' EQ '').DONE
*--------BUILD DAPL
         MVC   DAPLUPT(4),CPPLUPT       COPY UPT ADDRESS
         MVC   DAPLECT(4),CPPLECT       COPY ECT ADDRESS
         LA    R1,&ECB                  GET ECB ADDRESS
         ST    R1,DAPLECB
         MVC   DAPLPSCB(4),CPPLPSCB     COPY PSCB ADDRESS
         AIF   ('&DAPL(3)' EQ '').DONE
         LA    R1,&DAPL(3)              GET DAPB ADDRESS
         ST    R1,DAPLDAPB
.DONE    MEND
