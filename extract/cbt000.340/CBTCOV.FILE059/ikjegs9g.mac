         MACRO
&SYMBOL  IKJEGS9G &OPRN,&TCBADDR=,&VALUE=O,&REGNO=P,&MF=D
         LCLB  &B(11),&SW(2)
         LCLA  &FLGS1,&FLGS2,&LEN
         LCLC  &REGNAM
         AIF   ('&MF' NE 'D').CONT1 CHECK IF DSECT REQUESTED.
         AIF   ('&SYMBOL' EQ '').DGEN1 CHECK FOR LABEL.
         MNOTE *,'LABEL IGNORED'
.DGEN1   ANOP
IKJEGS9G DSECT
         SPACE 1
***********************************************************************
*                                                                     *
*        THIS MACRO MAPS THE INPUT PARAMETER LIST TO THE              *
*        BREAKPOINT SVC (SVC 97).                                     *
*                                                                     *
* STATUS -- VERSION NO. 01 - OS/VS2 RELEASE NO. 01                    *
*                                                                     *
* CHANGE LEVEL -- 03 - DATE 04/25/71                                  *
*                                                                     *
***********************************************************************
         SPACE 1
S9GTCBPT DS    A -            TARGET TCB ADDRESS, OR PRB OR IRB
*                             ADDRESS.
S9GVALUE DS    A -            ADDRESS OF VALUE, OR VALUE.
*                             DEPENDING ON FUNCTION REQUESTED.
S9GFLGS1 DS    X -            FLAGS, BYTE 1.
S9GMACRO EQU   B'10000000' -  MACRO FLAG.
S9GTCPON EQU   B'01000000' -  SET TCBTCP BIT TO '1'B.
S9GTCPOF EQU   B'00100000' -  SET TCBTCP BIT TO '0'B.
S9GTRN   EQU   B'00010000' -  ALTER TCBTRN FIELD.
S9GOPSW  EQU   B'00001000' -  ALTER RBOPSW FIELD.
S9GGPR   EQU   B'00000100' -  ALTER SPECIFIED GPR.
S9GAGPR  EQU   B'00000010' -  ALTER ALL GPRS.
S9GFPR   EQU   B'00000001' -  ALTER SPECIFIED FPR.
S9GFLGS2 DS    X -            FLAGS, BYTE 2.
S9GWCF   EQU   B'10000000' -  SET RBWCF TO ZERO.
S9GACHK  EQU   B'01000000' -  VALIDITY CHECK SPECIFIED ADDRESS.
S9GDEQ   EQU   B'00100000' -  ISSUE DEQ FOR IKJEFT04 AFTER ABEND.
*              B'0000XXXX' -  RESERVED BITS.
S9GREGNO DS    H -            REGISTER NUMBER TO BE ALTERED.
         SPACE 1
         AGO   .EXIT
.*
.* FUNCTION VALIDATION SECTION.
.*
.CONT1   ANOP
&B(1)    SETB  1
&B(2)    SETB  ('&OPRN' EQ 'TCPON')
&B(3)    SETB  ('&OPRN' EQ 'TCPOFF')
&B(4)    SETB  ('&OPRN' EQ 'TRN')
&B(5)    SETB  ('&OPRN' EQ 'OPSW')
&B(6)    SETB  ('&OPRN' EQ 'GPR')
&B(7)    SETB  ('&OPRN' EQ 'AGPR')
&B(8)    SETB  ('&OPRN' EQ 'FPR')
&B(9)    SETB  ('&OPRN' EQ 'WCF')
&B(10)   SETB  ('&OPRN' EQ 'ADDRCHK')
&B(11)   SETB  ('&OPRN' EQ 'DEQ')
         AIF   (&B(2)+&B(3)+&B(4)+&B(5)+&B(6)+&B(7)+&B(8)+&B(9)+&B(10)+*
               &B(11) NE 1).ERROR1
&FLGS1   SETA  128*&B(1)+64*&B(2)+32*&B(3)+16*&B(4)+8*&B(5)+4*&B(6)+2*&*
               B(7)+&B(8)
&FLGS2   SETA  128*&B(9)+64*&B(10)+32*&B(11)
         AIF   ('&MF' NE 'L').EXEC1
.*
.* LIST SECTION.
.*
&SYMBOL  DS    0F -           ALIGN ON WORD BOUNDARY.
        AIF ('&TCBADDR' EQ '').NOTCB
         DC    A(&TCBADDR) -  TARGET TCB ADDRESS, OR PRB OR IRB
*                             ADDRESS.
        AGO   .CHKVAL
.NOTCB   ANOP
         DC    A(0)        -  NO  TCB ADDR SUPPLIED
.CHKVAL  ANOP
         AIF   ('&VALUE' EQ 'O').SKIP1 CHECK IF VALUE SPECIFIED.
         DC    A(&VALUE) -    VALUE, OR ADDRESS OF VALUE.
         AGO   .SKIP2
.SKIP1   ANOP
         DC    A(0) -         VALUE NOT SPECIFIED.
.SKIP2   ANOP
         DC    AL1(&FLGS1) -  FLAGS, BYTE 1.
         DC    AL1(&FLGS2) -  FLAGS, BYTE 2.
         AIF   ('&REGNO' EQ 'P').SKIP3 CHECK IF REG SPECIFIED.
         DC    AL2(&REGNO) -  REGISTER NUMBER.
         AGO   .EXIT
.SKIP3   ANOP
         DC    AL2(0) -       REGISTER NOT SPECIFIED.
         AGO   .EXIT
.*
.* EXECUTE FORM
.*
.EXEC1   ANOP
&SW(2)   SETB  0
         AIF   ('&MF(1)' NE 'E').ERROR2
         AIF   ('&MF(2)' EQ '(1)').R1SET
         AIF   ('&MF(2)'(1,1) EQ '(').REG
&SYMBOL  LA    1,&MF(2) -     LOAD LIST ADDRESS.
         AGO   .R1SET
.REG     ANOP
&LEN     SETA  K'&MF(2)
&REGNAM  SETC  '&MF(2)'(2,&LEN-2)
&SYMBOL  LR    1,&REGNAM - LOAD LIST ADDRESS.
.R1SET   ANOP
         MVI   8(1),&FLGS1 -  SET FLAG BYTE 1.
         MVI   9(1),&FLGS2 -  SET FLAG BYTE 2.
         AIF   ('&TCBADDR' EQ '').ERROR3
&SW(1)   SETB  ('&VALUE' EQ 'O')
         AIF   ('&TCBADDR'(1,1) EQ '(').REGT
         LA    14,&TCBADDR -  GET TCB, PRB, OR IRB ADDRESS.
&SW(2)   SETB  1
.REGT    ANOP
         AIF   (&SW(1) EQ 1).STREGS
         AIF   (&SW(2) EQ 1).VALUE
&LEN     SETA  K'&TCBADDR
&REGNAM  SETC  '&TCBADDR'(2,&LEN-2)
         LR    14,&REGNAM - COPY TCB, PRB, OR IRB ADDRESS.
.VALUE   ANOP
         AIF   ('&VALUE'(1,1) EQ '(').REGV
         LA    15,&VALUE -    GET VALUE, OR ADDRESS OF VALUE.
         AGO   .STBOTH
.REGV    ANOP
&LEN     SETA  K'&VALUE
&REGNAM  SETC  '&VALUE'(2,&LEN-2)
         LR    15,&REGNAM - COPY VALUE, OR ADDRESS OF VALUE.
.STBOTH  ANOP
         STM   14,15,0(1) -   STORE INTO PARAMETER LIST.
         AGO   .REGNO
.STREGS  ANOP
         AIF   (&SW(2) EQ 1).ST1
&LEN     SETA  K'&TCBADDR
&REGNAM  SETC  '&TCBADDR'(2,&LEN-2)
         ST    &REGNAM,0(,1) - STORE INTO LIST.
         AGO   .REGNO
.ST1     ANOP
         ST    14,0(,1) -     STORE INTO PARAMETER LIST.
.REGNO   ANOP
         AIF   ('&REGNO' EQ 'P').SVC
         AIF   ('&REGNO'(1,1) EQ '(').REGR
         LA    0,&REGNO -     GET REGISTER NUMBER.
         STH   0,10(,1) -     STORE REGISTER NUMBER IN LIST.
         AGO   .SVC
.REGR    ANOP
&LEN     SETA  K'&REGNO
&REGNAM  SETC  '&REGNO'(2,&LEN-2)
         STH   &REGNAM,10(,1) - STORE REGISTER NUMBER.
.SVC     ANOP
         SVC   97 -           PASS CONTROL TO BREAKPOINT SVC.
.EXIT    ANOP
         MEXIT
.ERROR1  MNOTE 12,'INVALID OPTION OR OPTION NOT SPECIFIED'
         MEXIT
.ERROR2  MNOTE 12,'INVALID MACRO FORM SPECIFIED'
         MEXIT
.ERROR3  MNOTE 12,'TCBADDR NOT SPECIFIED, BUT REQUIRED'
         MEXIT
         MEND
