         TITLE 'DELETE MULTIPLE MEMBERS FROM A PDS'
***********************************************************************
*                                                                     *
* SS0810       ALIAS MMDEL                                            *
*                                                                     *
* FUNCTION     THE MMDEL TSO COMMAND IS USED TO DELETE MEMBERS OF     *
*              A PARTITIONED DATA SET.  NO LIMIT IS SET ON THE NUMBER *
*              OF MEMBERS TO BE DELETED.  MEMBERS ARE ENTERED AS A    *
*              LIST ENCLOSED IN PARENTHESES.                          *
*                                                                     *
* SYNTAX                                                              *
*                                                                     *
*              MMDEL  DSNAME                                          *
*                     (MEMBER1 MEMBER2 ... MEMBERN)                   *
*                     PRINT/NOPRINT                                   *
*                                                                     *
*              DEFAULTS -  PRINT                                      *
*                                                                     *
*                                                                     *
*              DSNAME      THE NAME OF THE PDS FROM WHICH THE MEMBERS *
*                          ARE TO BE DELETED.  NORMAL TSO NAMING      *
*                          CONVENTIONS ARE FOLLOWED.                  *
*                                                                     *
*              MEMBERN     THE NAMES OF MEMBERS TO BE DELETED.        *
*                                                                     *
*              PRINT/NOPRINT                                          *
*                          SPECIFIES WHETHER OR NOT MESSAGES          *
*                          INDICATING SUCCESSFUL OR UNSUCCESSFUL      *
*                          DELETION OF MEMBERS ARE TO BE DISPLAYED.   *
*                                                                     *
*              THE MMDEL COMMAND WILL NOT DELETE MEMBERS FROM THE     *
*              T PRODLIBS.  THE DELMEM COMMAND SHOULD BE USED         *
*              TO DELETE MEMBERS FROM T PRODLIBS.                     *
*                                                                     *
* FPL MACROS USED                                                     *
*                                                                     *
*              $ENTER      ENTRY LINKAGE                              *
*              $RTRN       RETURN LINKAGE                             *
*              #TSOBLKS    CONSTRUCT TSO CONTROL BLOCKS               *
*              #TSOMSG     BUILD A TSO MESSAGE                        *
*                                                                     *
* IBM MACROS USED                                                     *
*                                                                     *
*              LINK        INVOKE A SERVICE ROUTINE                   *
*              OPEN        OPEN A DATA SET FOR PROCESSING             *
*              CLOSE       CLOSE A DATA SET                           *
*              STOW        DELETE A MEMBER                            *
*              PUTLINE     DISPLAY A MESSAGE                          *
*              IKJPARM     DESCRIBE PARAMETER LIST                    *
*              IKJIDENT    DESCRIBE A POSITIONAL PARAMETER            *
*              IKJENDP     END PARAMETER LIST                         *
*              IKJCPPL     MAP A TSO CONTROL BLOCK                    *
*              IKJIOPL     MAP A TSO CONTROL BLOCK                    *
*              IKJPPL      MAP A TSO CONTROL BLOCK                    *
*              IKJDAPL     MAP A TSO CONTROL BLOCK                    *
*              IKJDAP08    MAP A DYNAMIC ALLOCATION CONTROL BLOCK     *
*              IKJDAP18    MAP A DYNAMIC ALLOCATION CONTROL BLOCK     *
*                                                                     *
* SERVICE ROUTINES USED                                               *
*                                                                     *
*              PUTLINE     DISPLAY A MESSAGE                          *
*              PARSE       PROCESS COMMAND PARAMETERS                 *
*              DAIR        INVOKE DYNAMIC ALLOCATION                  *
*                                                                     *
* ATTRIBUTES   ASSEMBLY LANGUAGE                                      *
*              REUS                                                   *
*              RENT                                                   *
*              REFR                                                   *
*              ENABLED                                                *
*              NON-PRIVELEGED                                         *
*              NOT AUTHORIZED                                         *
*                                                                     *
* ENVIRONMENT                                                         *
*              OS/MVT WITH TSO                                        *
*              OS/VS2 REL 1 WITH TSO                                  *
*              OS/VS2 REL 3 WITH TSO                                  *
*                                                                     *
* RETURN CODES                                                        *
*                                                                     *
*               0 - SUCCESSFUL COMPLETION                             *
*               4 - ONE OR MORE MEMBERS NOT DELETED                   *
*              16 - UNABLE TO PARSE OPERANDS                          *
*                                                                     *
*                                                                     *
* REGISTER ASSIGNMENTS                                                *
*                                                                     *
*               0 - STANDARD LINKAGE                                  *
*               1 - STANDARD LINKAGE - WORK                           *
*               2 - WORK REGISTER                                     *
*               3 - RETURN CODE STORAGE                               *
*               4 - MEMBER POINTER                                    *
*               5 - NOT USED                                          *
*               6 - PDE POINTER                                       *
*               7 - DAPB BASE REGISTER                                *
*               8 - PPL BASE REGISTER - PDE BASE REGISTER             *
*               9 - DAPL BASE REGISTER                                *
*              10 - IOPL BASE REGISTER                                *
*              11 - CPPL BASE REGISTER                                *
*              12 - PROGRAM BASE REGISTER                             *
*              13 - SAVE AREA ADDRESS - WORK AREA BASE REGISTER       *
*              14 - STANDARD LINKAGE                                  *
*              15 - STANDARD LINKAGE                                  *
*                                                                     *
*                                                                     *
*              THE MMDEL COMMAND IS DERIVED FROM DELETEM COMMAND      *
*              WRITTEN BY GERALD MUFFOLETTO AT LOUISIANA STATE        *
*              UNIVERSITY SYSTEM NETWORK COMPUTER CENTER.             *
*                                                                     *
*              THE MODIFICATIONS FROM DELETEM WERE MADE TO THE        *
*              RLSE COMMAND AND CHECKS WERE MADE FOR AN ATTEMPT       *
*              TO USE MMDEL AGAINST THE T PRODLIBS.  ANOTHER          *
*              COMMAND, DELMEM, PROVIDES FOR GREATER INTEGRITY        *
*              WHEN DELETING MEMBERS FROM PRODLIBS.                   *
*                                                                     *
*                                                GORDON P. WEST 11/76 *
***********************************************************************
         TITLE 'ENTRY LINKAGE'
MMDEL    $ENTER BASE=R12,                                              +
               RENT=DSECTLEN,                                          +
               SAVE=SAVEAREA
         TITLE 'SET UP TSO CONTROL BLOCKS'
***********************************************************************
*                                                                     *
*              SET UP TSO CONTROL BLOCKS                              *
*                                                                     *
***********************************************************************
         SPACE 2
         #TSOBLKS DSECT,ECB=MMDLECB,                                   +
               CPPL=(R11,R1),                                          +
               IOPL=(R10,MMDLIOPL,MMDLPTPB),                           +
               DAPL=(R9,MMDLDAPL),                                     +
               PPL=(R8,MMDLPPL,MMDLPCL,MMDLANS)
*--------BUILD PTPB
         MVC   MMDLPTPB(PTPBLEN),MODPTPB
*--------SET UP DAPB'S
         LA    R1,MMDLPDSN             LOAD DSNAME FIELD ADDRESS
         ST    R1,MMDLDSN              SAVE IT
         LA    R7,DA18DAPB             LOAD FREE DAPB ADDRESS
         USING DAPB18,R7               DEFINE BASE REGISTER
         XC    DAPB18(DA18LEN),DAPB18  ZERO OUT CONTROL BLOCK
         MVC   DA18CD,=X'0018'         MOVE IN BLOCK CODE
         XC    DA18PDSN,DA18PDSN       ZERO DSNAME ADDRESS POINTER
         MVC   DA18DDN,=CL8'MMDLDDN'   MOVE IN DDNAME
         MVC   DA18MNM,=CL8' '         BLANK OUT MEMBER NAME
         MVC   DA18SCLS,=CL2' '        BLANK OUT SYSOUT CLASS
         MVI   DA18DPS2,DA18KEEP       DISP=KEEP
         MVI   DA18CTL,DA18PERM
         MVC   DA18JBNM,=CL8' '        BLANK OUT MEMBER NAME
         LA    R7,DA08DAPB             LOAD ALLOCATE DAPB ADDRESS
         USING DAPB08,R7               DEFINE BASE REGISTER
         XC    DAPB08(DA08LEN),DAPB08  ZERO OUT CONTROL BLOCK
         MVC   DA08CD,=X'0008'         MOVE IN BLOCK CODE
         MVC   DA08PDSN,MMDLDSN        MOVE IN DSNAME ADDRESS
         MVC   DA08DDN,=CL8'MMDLDDN'   MOVE IN DDNAME
         MVC   DA08UNIT,=CL8' '        BLANK OUT UNIT NAME
         MVC   DA08SER,=CL8' '         BLANK OUT SERIAL
         MVC   DA08MNM,=CL8' '         BLANK OUT MEMBER NAME
         MVC   DA08PSWD,=CL8' '        BLANK OUT PASSWORD
         MVI   DA08DSP1,DA08OLD        DISP=(OLD,KEEP,KEEP)
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         MVC   DA08ALN,=CL8' '         BLANK OUT ATTRIBUTE LIST NAME
         DROP  R7
*--------SET UP OPEN LIST FORM
         MVC   OPENLIST(OPENLEN),MODLOPEN
         EJECT
***********************************************************************
*                                                                     *
*              PARSE COMMAND OPERANDS                                 *
*                                                                     *
***********************************************************************
         SPACE 2
         LA    R1,PPL                  LOAD PPL ADDRESS
         LINK  EP=IKJPARS              PARSE THE OPERANDS
         LTR   R15,R15                 TEST PARSE RETURN CODE
         BNZ   ERRPARS
         L     R8,MMDLANS              LOAD ANSWER AREA ADDRESS
         USING MMDLPDL,R8              DEFINE BASE REGISTER
         TITLE 'CONSTRUCT DATA SET NAME'
***********************************************************************
*                                                                     *
*              CONSTRUCT DATA SET NAME                                *
*                                                                     *
***********************************************************************
         LA    R6,DSNAME               LOAD PDE ADDRESS
         LA    R2,MMDLPDSN+2           LOAD DSNAME FIELD ADDRESS
         MVI   0(R2),C' '              BLANK OUT DSNAME
         MVC   1(43,R2),0(R2)
         LA    R3,DA08DAPB             LOAD ALLOCATE DAPB ADDRESS
         USING DAPB08,R3               DEFINE BASE REGISTER
         TM    6(R6),X'40'             TEST FOR NAME IN QUOTES
         BNZ   CHEKPROD
*--------HAVE DAIR APEND USERID TO DSNAME
         OI    DA08CTL,DA08UID         SET FLAG IN ALLOC  DAPB
         B     MOVEREST
         DROP  R3                      DROP BASE REGISTERS
*--------TEST FOR TN.PRODLIB
CHEKPROD CLC   4(2,R6),=H'10'          TEST LENGTH
         BNE   MOVEREST
         L     R3,0(R6)                LOAD DSNAME ADDRESS
         CLI   0(R3),C'T'
         BNE   MOVEREST
         CLC   2(8,R3),=C'.PRODLIB'
         BNE   MOVEREST
         PUTLINE OUTPUT=(MSG4,,,DATA),MF=(E,IOPL)
         LA    R15,16                  SET CONDITION CODE
         B     EXIT
*--------MOVE REMAINDER OF NAME
MOVEREST L     R3,0(R6)                LOAD INPUT DSNAME ADDRESS
         LH    R1,4(R6)                LOAD INPUT DSNAME LENGTH
         STH   R1,MMDLPDSN             STORE LENGTH
         BCTR  R1,0                    REDUCE LENGTH BY 1
         EX    R1,MOVENAME             COPY INPUT DSNAME
         TITLE 'ALLOCATE THE DATA SET'
***********************************************************************
*                                                                     *
*              ALLOCATE THE DATA SET                                  *
*                                                                     *
***********************************************************************
*--------SET UP ALLOCATE DAPB
SETUPDDN LA    R7,DA08DAPB             LOAD ALLOCATE DAPB ADDRESS
         USING DAPB08,R7               DEFINE BASE REGISTER
         ST    R7,DAPLDAPB             STORE DAPB ADDRESS IN DAPL
         LA    R1,DAPL                 LOAD DAPL ADDRESS
         LINK  EP=IKJDAIR              LINK TO DAIR
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TESTORG
         C     R15,=F'20'              TEST FOR FILE IN USE
         BNE   ERRDAIR
         LA    R1,DA18DAPB             LOAD FREE DAPB ADDRESS
         ST    R1,DAPLDAPB             STORE IN DAPL
         LA    R1,DAPL                 LOAD DAPL ADDRESS
         LINK  EP=IKJDAIR              FREE THE FILE
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   ERRDAIR
         LA    R1,DA08DAPB             LOAD ALLOCATE DAPB ADDRESS
         ST    R1,DAPLDAPB             STORE IN DAPL
         LA    R1,DAPL                 LOAD DAPL ADDRESS
         LINK  EP=IKJDAIR              FREE THE FILE
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   ERRDAIR
         SPACE 2
***********************************************************************
*                                                                     *
*              BE SURE DSORG IS PARTITIONED                           *
*                                                                     *
***********************************************************************
TESTORG  TM    DA08DSO,X'02'           TEST FOR PARTITIONED
         BNZ   COPYDCB
         PUTLINE OUTPUT=(MSG2,,,DATA),MF=(E,IOPL)
         LA    R15,16                  SET CONDITION CODE
         B     EXIT
*--------INITIALIZE DCB
COPYDCB  MVC   MMDLDCB(DCBLEN),DCB     COPY DCB
         TITLE 'OPEN DATA SET'
***********************************************************************
*                                                                     *
*              OPEN THE DATA SET                                      *
*                                                                     *
***********************************************************************
         SPACE
*--------OPEN THE DATA SET
         LA    R1,OPENLIST             LOAD ADDRESS OF OPEN LIST FORM
         LA    R2,MMDLDCB              LOAD DCB ADDRESS
         OPEN  ((R2),OUTPUT),MF=(E,(R1))     OPEN THE DATA SET
         TM    MMDLDCB+48,X'10'        SEE IF IT OPENED
         BNZ   PROCMEM
         PUTLINE OUTPUT=(MSG3,,,DATA),MF=(E,IOPL)
         B     FREE
         TITLE 'DELETE MEMBERS'
***********************************************************************
*                                                                     *
*              DELETE MEMBERS                                         *
*                                                                     *
***********************************************************************
         SPACE
PROCMEM  LA    R6,MEMBER               LOAD ADDR OF FIRST PDE
         LA    R4,DELMEM               PT TO MEMBER TO DELETE
         LA    R3,0                    SET RETURN CODE TO 0
MVCNEXT  L     R2,0(R6)                LOAD ADDR OF MEMBER
         MVC   DELMEM(8),=C'        '  BLANK MEMBER AREA TO DELETE
         LH    R1,4(R6)                GET LNT OF MEMBER
         BCTR  R1,R0                   DECR R1 BY 1 FOR EX
         EX    R1,MOVEMEM              MOVE MEMBER TO BE DELETED
         STOW  MMDLDCB,DELMEM,D        DELETE THE MEMBER
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   MEMNOTS
         CLC   PRINT,=H'2'             TEST FOR NOPRINT
         BE    NEXTMEM
         MVC   OUTLINE(32),OKMSG       MOVE IN MODEL MESSAGE
         MVC   OUTLINE+11(8),DELMEM    MOVE IN DELETED MEMBER NAME
         PUTLINE OUTPUT=(OUTLINE,,,DATA),MF=(E,IOPL)
NEXTMEM  DS    0H
         L     R6,8(R6)                GET NEXT MEMBER ADDR
         C     R6,=X'FF000000'         TEST FOR END OF MEMBER LIST
         BNE   MVCNEXT
         TITLE 'CLOSE THE DATA SET'
***********************************************************************
*                                                                     *
*              CLOSE THE DATA SET                                     *
*                                                                     *
***********************************************************************
         SPACE 2
CLOSE    LA    R1,OPENLIST             LOAD ADDRESS OF OPEN LIST FORM
         LA    R2,MMDLDCB              LOAD DCB ADDRESS
         CLOSE ((R2)),MF=(E,(R1))      CLOSE THE DATA SET
         TITLE 'FREE THE DATA SET'
***********************************************************************
*                                                                     *
*              FREE THE DATA SET                                      *
*                                                                     *
***********************************************************************
         SPACE 2
FREE     LA    R7,DA18DAPB             LOAD FREE DAPB ADDRESS
         ST    R7,DAPLDAPB             STORE DAPB ADDRESS IN DAPL
         LA    R1,DAPL                 LOAD DAPL ADDRESS
         LINK  EP=IKJDAIR              LINK TO DAIR
         LTR   R15,R15                 TEST RETURN CODE
         BZ    NORMAL
         TITLE 'ERROR IN DYNAMIC ALLOCATION'
***********************************************************************
*                                                                     *
*              ERROR IN DYNAMIC ALLOCATION                            *
*                                                                     *
***********************************************************************
         SPACE 2
ERRDAIR  ST    R15,DAIRRC              STORE RETURN CODE
         LA    R1,DAPL                 LOAD DAPL ADDRESS
         ST    R1,DFDAPLP              STORE IN DAIRFAIL PARM LIST
         LA    R1,DAIRRC               LOAD RETURN CODE ADDRES
         ST    R1,DFRCP                STORE IN DAIRFAIL PARM LIST
         LA    R1,MSGWTRAD             LOAD MESSAGE WRITER ADDRESS
         ST    R1,DFJEFF02             STORE IN DAIRFAIL PARM LIST
         XC    MSGWTRAD,MSGWTRAD       ZERO MESSAGE WRITER ADDRESS
         LA    R1,1                    SET UP CALLER ID
         STH   R1,ID
         LA    R1,ID                   LOAD ID ADDRESS
         ST    R1,DFIDP                STORE IN DAIRFAIL PARM LIST
         LA    R1,DAIRFAIL             LOAD DAIRFAIL PARM LIST ADDR
         LINK  EP=IKJEFF18             LINK TO DAIRFAIL ROUTINE
         LA    R15,16                  SET CONDITION CODE
         B     EXIT
***********************************************************************
*                                                                     *
*              ERROR DELETING MEMBER                                  *
*                                                                     *
***********************************************************************
         SPACE
MEMNOTS  LA    R3,4                    SET CONTITION CODE
         CLC   PRINT,=H'2'             TEST FOR NOPRINT
         BE    NEXTMEM
         MVC   OUTLINE(36),DELERROR    SET UP ERROR MESSAGE
         MVC   OUTLINE+22(8),DELMEM    MOVE IN MEMBER NAME
         PUTLINE OUTPUT=(OUTLINE,,,DATA),MF=(E,IOPL)
         B     NEXTMEM
         TITLE 'PARSE ERROR'
***********************************************************************
*                                                                     *
*              PARSE ERROR                                            *
*                                                                     *
***********************************************************************
         SPACE 2
ERRPARS  PUTLINE OUTPUT=(MSG1,,,DATA),MF=(E,IOPL)
         LA    R15,16                  SET RETURN CODE
         B     EXIT
         TITLE 'EXIT CODING'
***********************************************************************
*                                                                     *
*              END OF ROUTINE                                         *
*                                                                     *
***********************************************************************
         SPACE 2
NORMAL   LR    R15,R3                  LOAD RETURN CODE
EXIT     $RTRN RC=(R15),RENT=DSECTLEN
         TITLE 'DATA AREA - CONSTANTS'
***********************************************************************
*                                                                     *
*              CONSTANTS                                              *
*                                                                     *
***********************************************************************
         SPACE
MOVENAME MVC   0(0,R2),0(R3)           COPY DSNAME
MOVEMEM  MVC   DELMEM(0),0(R2)         COPY MEMBER NAME
MSG1     #TSOMSG 'UNABLE TO INTERPRET COMMAND'
MSG2     #TSOMSG 'DATA SET NOT PARTITIONED'
MSG3     #TSOMSG 'ERROR IN OPEN'
MSG4     #TSOMSG 'USE DELMEM TO DELETE MEMBERS FROM PRODLIB'
OKMSG    #TSOMSG 'MEMBER XXXXXXXX DELETED   ',LENGTH=32
DELERROR #TSOMSG 'DELETE FAILED FOR XXXXXXXX',LENGTH=32
DCB      DCB   DDNAME=MMDLDDN,MACRF=W,DSORG=PO
MODPTPB  PUTLINE MF=L
PTPBLEN  EQU   *-MODPTPB
MODLOPEN OPEN  (,),MF=L
OPENLEN  EQU   *-MODLOPEN
         SPACE
***********************************************************************
*                                                                     *
*              LITERAL POOL                                           *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         TITLE 'PARSE CONTROL BLOCKS'
***********************************************************************
*                                                                     *
*              PARSE CONTROL BLOCKS                                   *
*                                                                     *
***********************************************************************
         SPACE 2
MMDLPCL  IKJPARM DSECT=MMDLPDL
DSNAME   IKJPOSIT DSNAME,PROMPT='DSNAME'
MEMBER   IKJIDENT 'MEMBER',                                            +
               LIST,                                                   +
               MAXLNTH=8,                                              +
               FIRST=ALPHA,                                            +
               OTHER=ALPHANUM,                                         +
               PROMPT='MEMBER NAMES'
PRINT    IKJKEYWD DEFAULT='PRINT'
         IKJNAME  'PRINT'
         IKJNAME  'NOPRINT'
         IKJENDP
         TITLE 'TSO CONTROL BLOCK DSECTS'
***********************************************************************
*                                                                     *
*              TSO CONTROL BLOCK DSECTS                               *
*                                                                     *
***********************************************************************
         SPACE 2
         IKJDAP18
DA18LEN  EQU   *-DAPB18                DAPB LENGTH
         SPACE
         IKJDAP08
DA08LEN  EQU   *-DAPB08                DAPB LENGTH
         TITLE 'WORK AREA DSECT'
***********************************************************************
*                                                                     *
*              WORK AREA DSECT                                        *
*                                                                     *
***********************************************************************
         SPACE 2
MMDLDS   DSECT
SAVEAREA DS    18F                     SAVE AREA
MMDLDSN  DS    A
MMDLECB  DS    F                       EVENT CONTROL BLOCK
MMDLANS  DS    A                       PARSE ANSWER AREA ADDRESS
DELMEM   DS    CL8                     NAME OF MEMBER TO BE DELETED
MMDLPDSN DS    H,CL44
OUTLINE  DS    H,H,CL32                OUTPUT MESSAGE AREA
OPENLIST OPEN  (,),MF=L                OPEN CONTROL BLOCK LIST FORM
         SPACE
DAIRRC   DS    F                       DAIRFAIL PARAMETERS
MSGWTRAD DS    A
ID       DS    H
         SPACE
         DS    0F                      FULLWORD ALIGNMENT
DAIRFAIL EQU   *
DFDAPLP  DS    A                       ADDRESS OF DAPL
DFRCP    DS    A                       ADDRESS OF RETURN CODE
DFJEFF02 DS    A                       ADDRESS OF MESSAGE WRITER
DFIDP    DS    A                       ADDRESS OF IDENTIFIER
DFCPPLP  DS    A                       ADDRESS OF CPPL
         SPACE
MMDLDAPL DS    0F                      FULLWORD ALIGNMENT
         ORG   *+DAPLLEN
         SPACE
MMDLPTPB PUTLINE MF=L                  PUTLINE PARAMETER BLOCK
         SPACE
MMDLIOPL DS    0F                      FULLWORD ALIGNMENT
         ORG   *+IOPLLEN
         SPACE
MMDLPPL  DS    0F                      FULLWORD ALIGNMENT
         ORG   *+PPLLEN
         SPACE
DA18DAPB DS    0F                      FULLWORD ALIGNMENT
         ORG   *+DA18LEN
         SPACE
DA08DAPB DS    0F                      FULLWORD ALIGNMENT
         ORG   *+DA08LEN
         SPACE
MMDLDCB  DCB   DDNAME=MMDLDDN,MACRF=W,DSORG=PO
         SPACE
DCBLEN   EQU   *-MMDLDCB               LENGTH OF DCB
DSECTLEN EQU   *-MMDLDS                LENGTH OF WORK AREA
         END
***********************************************************************
*                                                                     *
*              HELP MEMBER                                            *
*                                                                     *
***********************************************************************
)F FUNCTION -
             THE MMDEL TSO COMMAND IS USED TO DELETE MEMBERS OF
             A PARTITIONED DATA SET.  NO LIMIT IS SET ON THE NUMBER
             OF MEMBERS TO BE DELETED.  MEMBERS ARE ENTERED AS A
             LIST ENCLOSED IN PARENTHESES.

             THE MMDEL COMMAND WILL NOT DELETE MEMBERS FROM THE
             T PRODLIBS.  THE DELMEM COMMAND SHOULD BE USED
             TO DELETE MEMBERS FROM T PRODLIBS.
)X SYNTAX -
             MMDEL  DSNAME
                    (MEMBER1 MEMBER2 ... MEMBERN)
                    PRINT/NOPRINT

             REQUIRED -  DSNAME, MEMBER1
             DEFAULTS -  PRINT
)O OPERANDS -
             DSNAME      THE NAME OF THE PDS FROM WHICH THE MEMBERS
                         ARE TO BE DELETED.  NORMAL TSO NAMING
                         CONVENTIONS ARE FOLLOWED.

             MEMBERN     THE NAMES OF MEMBERS TO BE DELETED.

))PRINT      DISPLAY MESSAGES FOR SUCCESSFUL DELETION OR UNSUCCESSFUL
             DELETION.
))NOPRINT    DO NOT DISPLAY MESSAGES FOR SUCCESSFUL DELETION OR
             UNSUCCESSFUL DELETION.
