         TITLE 'DISCLAIMER - INSTALLATION INSTRUCTIONS'
***********************************************************************
*                                                                     *
*     THIS PROGRAM, PROVIDED BY FPL, IS OF GENERAL INTEREST           *
*     SUBMITTED FOR UNRESTRICTED DISTRIBUTION.  THIS PROGRAM          *
*     HAS MET A BASIC SET OF PROGRAMMING AND DOCUMENTATION            *
*     STANDARDS, BUT MAY NOT HAVE BEEN PROGRAM TESTED IN ANY          *
*     FORMAL FASHION BY FPL.  THE USER IS EXPECTED TO MAKE THE        *
*     FINAL EVALUATION AS TO THE USEFULLNESS IN HIS OWN               *
*     ENVIRONMENT.                                                    *
*                                                                     *
*     FPL MAKES NO WARRANTY, EXPRESSED OR IMPLIED, INCLUDING, BUT     *
*     NOT LIMITED TO, THE IMPLIED WARRANTIES OR MERCHANTABILITY AND   *
*     FITNESS FOR A PARTICULAR PUTPOSE AS TO THE DOCUMENTATION,       *
*     FUNCTION, OR PERFORMANCE OF THESE PROGRAMS.                     *
*                                                                     *
*     ACCEPTANCE AND USE OF THIS PROGRAM CONSTITUTES A RELEASE        *
*     FROM LIABILITY OF FPL FOR ANY PROBLEMS USE OF THE PROGRAM       *
*     MAY CAUSE AT THE USER'S INSTALLATION.                           *
*                                                                     *
*     USERS ARE INVITED TO SUBMIT SUGGESTIONS OR ERROR DOCUMENTATION  *
*     TO FPL, HOWEVER, NO PROMISE CAN BE MADE THAT SUCH SUGGESTIONS   *
*     WILL BE IMPLEMENTED OR ERRORS CORRECTED.  SUBMIT COMMENTS TO:   *
*              COORDINATOR OF TECHNICAL SYSTEMS                       *
*              COMPUTER OPERATIONS DEPARTMENT                         *
*              FLORIDA POWER & LIGHT COMPANY - GENERAL OFFICE         *
*              P. O. BOX  529100                                      *
*              MIAMI, FLORIDA  33152                                  *
*                                                                     *
*     THIS PROGRAM IS MADE AVAILABLE BY FPL WITHOUT CHARGE OR         *
*     CONSIDERATION.  RECIPIENTS ARE FREE TO MAKE THIS PROGRAM        *
*     AVAILABLE TO OTHERS IN LIKE MANNER.  IT MAY NOT BE SOLD.        *
*                                                                     *
* INSTALLATION INSTRUCTIONS                                           *
*                                                                     *
*     THIS MODULE MAY BE ASSEMBLED AND LINKED INTO A LIBRARY IN THE   *
*     LINK LIST OR SPECIFIED AS A STEPLIB IN THE EXECUTION JCL.       *
*                                                                     *
*     BEFORE ASSEMBLING THE MODULE, INSURE THAT ALL THE REQUIRED      *
*     MACROS ARE AVAILABLE.  SOME MACROS FROM SYS1.AMODGEN MAY BE     *
*     REQUIRED.  THE $ENTER MACRO USES SYSTEM VARIABLE SYMBOLS        *
*     &SYSDATE AND &SYSTIME.  THESE SYMBOLS ARE NOT SUPPORTED BY ALL  *
*     ASSEMBLERS.  IF THE ASSEMBLER TO BE USED DOES NOT SUPPORT THESE *
*     SYMBOLS, THE $ENTER MACRO MUST BE MODIFIED.                     *
*                                                                     *
***********************************************************************
SS0127   TITLE 'CATALOG/VTOC COMPARISON PROGRAM'
***********************************************************************
*                                                                     *
* NAME         SS0127                                                 *
*                                                                     *
* FUNCTION     THIS PROGRAM WILL READ THE VOLUME TABLE OF CONTENTS    *
*              (VTOC) OF UP TO 99 DIRECT ACCESS VOLUMES TO OBTAIN     *
*              THE DATA SET NAME OF EACH DATA SET ON THE VOLUME.      *
*              THE CATALOG ENTRY FOR EACH DATA SET WILL BE READ TO    *
*              DETERMINE IF THE DATA SET IS CATALOGED, AND IF THE     *
*              CATALOG INFORMATION IS CORRECT.                        *
*                                                                     *
* ENVIRONMENT  OS/VS2                                                 *
*              OS/MVT                                                 *
*              BACKGROUND                                             *
*                                                                     *
* DESCRIPTION  THE VTOC IS OPENED AS A SEQUENTIAL DATA SET AND READ   *
*              USING BSAM.  THE PROGRAM MUST SUPPLY THE VTOC DSNAME   *
*              BY READING AND MODIFYING THE JFCB AND PERFORMING AN    *
*              OPEN TYPE=J.  EVERY ENTRY WHICH IS NOT A FORMAT 1      *
*              DSCB IS IGNORED.  THE DSNAME IS OBTAINED FROM THE      *
*              FORMAT 1 DSCB AND A LOCATE BY NAME IS ISSUED.  IF THE  *
*              RETURN CODE IS NOT ZERO, THE DATA SET IS NOT CATALOGED *
*              AND A MESSAGE IS ISSUED.                               *
*                                                                     *
*              IF THE RETURN CODE IS ZERO, THE VOLUME LIST RETURNED   *
*              BY LOCATE IS SEARCHED TO BE SURE THAT THE LIST         *
*              CONTAINS THE VOLUME SERIAL OF THE VOLUME BEING         *
*              PROCESSED.  IF NOT, A MESSAGE IS ISSUED.               *
*                                                                     *
*              IF BOTH TESTS ARE MET, THE NEXT ENTRY IS PROCESSED.    *
*              THIS CONTINUES UNTIL THE END OF DATA IS REACHED.       *
*                                                                     *
*              VSAM DATA SETS ARE IGNORED.                            *
*                                                                     *
* INPUT        THE INPUT CONSISTS OF A VTOC OF A VOLUME (DEFINED BY   *
*              A DD STATEMENT), AND THE CATALOG (NO DD STATEMENT      *
*              REQUIRED).                                             *
*                                                                     *
* OUTPUT       OUTPUT CONSISTS OF -                                   *
*                   1.  A PRINTED REPORT LISTING THE DATA SETS ON THE *
*                       VOLUME WHICH HAVE NO CATALOG ENTRIES, OR FOR  *
*                       WHICH THE CATALOG ENTRY DOES NOT POINT TO THE *
*                       VOLUME.                                       *
*                                                                     *
*                   2.  A FILE OF IEHPROGM SCRATCH STATEMENTS FOR     *
*                       EACH UNCATALOGED OR MISCATALOGED DATA SET.    *
*                                                                     *
* PARAMETERS   NONE.                                                  *
*                                                                     *
* EXECUTION    EXEC         SPECIFIES THE PROGRAM NAME.  NO PARM      *
*                           INFORMATION IS SPECIFIED.                 *
*                                                                     *
*              SYSPRINT DD  DEFINES THE OUTPUT MESSAGE DATA SET.      *
*                                                                     *
*              SYSUT2   DD  DEFINES THE OUTPUT IEHPROGM STATEMENTS.   *
*                                                                     *
*              VOLUMENN DD  DEFINES THE VOLUMES TO BE PROCESSED.      *
*                           VOLUME01 FOR THE FIRST VOLUME,            *
*                           VOLUME02 FOR THE SECOND VOLUME, ETC.      *
*                                                                     *
* EXAMPLE      LIST THE UNCATALOGED AND IMPROPERLY CATALOGED DATA     *
*              SETS ON WK1101 AND WK1102.                             *
*                                                                     *
*              //LIST   EXEC  PGM=SS0127                              *
*              //SYSPRINT DD  SYSOUT=A                                *
*              //SYSUT2   DD  SYSOUT=B                                *
*              //VOLUME01 DD  DISP=OLD,UNIT=DISK,VOL=SER=WK1101       *
*              //VOLUME02 DD  DISP=OLD,UNIT=DISK,VOL=SER=WK1102       *
*                                                                     *
* RETURN CODES                                                        *
*               0 - SUCCESSFUL COMPLETION                             *
*               4 - NOT USED                                          *
*               8 - NOT USED                                          *
*              12 - NOT USED                                          *
*              16 - UNABLE TO OPEN SYSUT2, NO OUTPUT PRODUCED         *
*                                                                     *
* ABEND CODES  USER 0001 - UNABLE TO OPEN SYSPRINT, NO OUTPUT         *
*                          PRODUCED.                                  *
*                                                                     *
* FPL MACROS USED -                                                   *
*                                                                     *
*              $ENTER    $RTRN                                        *
*              #SORT                                                  *
*              #VTCFMT1  #VTCFMT3  #VTCFMT4                           *
*                                                                     *
* IBM MACROS USED -                                                   *
*                                                                     *
*              OPEN      CLOSE     DCB       DCBD                     *
*              READ      CHECK     PUT                                *
*              RDJFCB    IEFJFCBN                                     *
*              LOCATE    OBTAIN    CAMLST                             *
*              GETMAIN   FREEMAIN                                     *
*              ABEND                                                  *
*              LINK                                                   *
*                                                 9/76 GORDON P. WEST *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
* UPDATE LOG                                                          *
*                                                                     *
* 03/07/78  CORRECTED ERROR WHEN OVER 200 UNCATALOGED OR MISCATALOGED *
*                DATA SETS FOUND                                      *
*           ADDED 3350 TO DEVICE TABLE                                *
*                                                                     *
***********************************************************************
         EJECT
SS0127   $ENTER BASE=(R12,R11)         ENTRY CODING
         OPEN  (SYSPRINT,OUTPUT)       OPEN SYSPRINT
         TM    SYSPRINT+48,X'10'       TEST FOR SUCCESSFUL OPEN
         BNZ   OPEN1OK
         ABEND 0001                    OPEN FAILED, ABEND
OPEN1OK  OPEN  (SYSUT2,OUTPUT)         OPEN SYSUT2
         TM    SYSUT2+48,X'10'         TEST FOR SUCCESSFUL SYSUT2 OPEN
         BNZ   OPEN2OK
         PUT   SYSPRINT,ERROR8
         MVI   CCODE+3,16              SET CONDITION CODE
         B     RETURN
         EJECT
***********************************************************************
*                                                                     *
*        GET STORAGE FOR OUTPUT LINES                                 *
*                                                                     *
***********************************************************************
         SPACE 2
OPEN2OK  GETMAIN R,LV=26600,SP=1       STORAGE FOR 200 133 CHAR LINES
         ST    R1,TABLEADR             SAVE TABLE ADDRESS
         OI    SWITCH,X'02'            INDICATE FREEMAIN REQUIRED
         EJECT
***********************************************************************
*                                                                     *
*        PUT DATE AND TIME IN HEADER LINE                             *
*                                                                     *
***********************************************************************
         SPACE 2
         TIME  DEC                     GET DATE AND TIME
         BAL   R5,DATECON1             CONVERT DATE TO EBCDIC
         MVC   HDRDATE,PDEC            COPY DATE TO HEADER
         LR    R1,R0                   PROCESS TIME
         SRL   R1,8                    GET RID OF LOW ORDER BITS
         SLL   R1,4
         ST    R1,PDEC
         ED    TIME,PDEC               EDIT TIME
         EJECT
***********************************************************************
*                                                                     *
*        OPEN VTOC AND READ FORMAT 4 DSCB                             *
*                                                                     *
***********************************************************************
         SPACE 2
READJFCB AP    COUNTER,=P'1'           INCREMENT COUNTER
         CP    COUNTER,=P'99'          TEST FOR MAXIMUM
         BH    RETURN
         L     R1,TABLEADR             LOAD TABLE ADDRESS
         LA    R2,133                  LOAD LENGTH OF AN ENTRY
         L     R3,=F'26600'            LOAD LENGTH OF TABLE
         SR    R3,R2                   SUBRACT LENGTH OF ONE ENTRY
         AR    R3,R1                   ADD TABLE ADDRESS
         SR    R1,R2                   SUBTRACT LENGTH FROM TABLE ADDR
         STM   R1,R3,SAVE              SAVE
         UNPK  SYSUT1+46(2),COUNTER    PUT NUMBER IN DDNAME
         OI    SYSUT1+47,X'F0'         SET ZONE TO F
         RDJFCB SYSUT1                 READ SYSUT1 JFCB
         MVC   JFCBDSNM,VTOCDSNM       SET VTOC DSNAME
         MVC   VOLSER,JFCBVOLS         SAVE VOLUME SERIAL
         DEVTYPE SYSUT1+40,DEVCODE     GET DEVICE CODE
         OPEN  SYSUT1,TYPE=J           OPEN VTOC
         TM    SYSUT1+48,X'10'         TEST FOR SUCCESSFUL OPEN
         BZ    RETURN
         PUT   SYSPRINT,HEADER         PRINT HEADER LINE
*--------READ FORMAT 4 DSCB
         READ  DECB4,SF,SYSUT1,FMT1DSCB,'S'      READ FORMAT 4 DSCB
         CHECK DECB4                   WAIT UNTIL READ COMPLETE
         MVC   TRKPRCYL,DS4DEVSZ+2     SAVE TRACKS PER CYLINDER
         SR    R9,R9                   CLEAR TOTAL TRACKS ALLOCATED
         NI    SWITCH,X'FF'-X'01'      RESET INDICATOR
         SR    R2,R2                   CLEAR R2
         IC    R2,DEVCODE+3            LOAD DEVICE CODE
         SLA   R2,3                    MULTIPLY BY 8
         LA    R2,DEVTABLE(R2)         LOAD TABLE ADDRESS
         SR    R15,R15
         IC    R15,0(R2)               INSERT DEVICE NAME LENGTH
         LTR   R15,R15                 TEST NAME LENGTH
         BNZ   BLANKOUT
         PUT   SYSPRINT,WARN           PRINT WARNING MESSAGE
         LA    R15,5                   SET LENGTH FOR SYSDA
BLANKOUT MVC   SCRATCHV(14),=CL14' '   BLANK OUT DEVICE CODE & SERIAL
         MVC   SCRATCHV(7),1(R2)       MOVE IN DEVICE NAME
         LA    R1,SCRATCHV(R15)        LOAD ADDRESS FOR SERIAL
         MVC   1(6,R1),VOLSER          COPY SERIAL TO SCRATCH RECORD
FINDBLNK LA    R1,1(R1)
         CLI   0(R1),C' '
         BNE   FINDBLNK
         MVI   0(R1),C','
         EJECT
***********************************************************************
*                                                                     *
*        READ VTOC RECORDS AND COMPARE TO CATALOG                     *
*                                                                     *
***********************************************************************
         SPACE 2
READVTOC READ  RDECB,SF,SYSUT1,FMT1DSCB,'S'      READ A VTOC RECORD
         CHECK RDECB                   WAIT UNTIL READ COMPLETE
         CLI   DS1FMTID,C'1'           TEST FOR FORMAT 1 DSCB
         BNE   READVTOC
         TM    DS1DSORG+1,X'08'        TEST FOR VSAM DATA SET
         BNZ   READVTOC
         MVC   DSNAME,DS1DSNAM         COPY DSNAME FOR LOCATE (LOCATE
*                                           WILL CHANGE ALIAS NAME TO
*                                           TRUE NAME).
         LOCATE LOCLIST                READ CATALOG ENTRY
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   NOTCTLGD
         LA    R8,CATBLOCK             LOAD CATALOG BLOCK ADDRESS
         LH    R1,0(R8)                LOAD VOLUME COUNT
         LA    R8,2(R8)                POINT TO FIRST VOLUME ENTRY
         C     R1,=F'20'               TEST VOL COUNT AGAINST VS MAX
         BNH   GETSER
         LA    R1,20                   SET VOLUME COUNT TO 20
GETSER   CLC   VOLSER,4(R8)            TEST VOL SER IN CAT TO THIS VOL
         BE    READVTOC
         LA    R8,12(R8)               INCREMENT TO NEXT VOLUME ENTRY
         BCT   R1,GETSER               DECREMENT COUNT AND LOOP
         EJECT
***********************************************************************
*                                                                     *
*        CATALOG DOES NOT POINT TO THIS VOLUME                        *
*                                                                     *
***********************************************************************
         SPACE 2
         MVC   DSN2,DS1DSNAM           INCORRECT CAT ENTRY, COPY DSNAME
         MVC   SER2,VOLSER             COPY VOLUME SERIAL TO MESSAGE
         LA    R2,ALLOC2               LOAD OUTPUT AREA ADDRESS
         MVC   0(14,R2),=CL14' '       BLANK OUT OUTPUT AREA
         BAL   R14,$ALLOC              GET ALLOCATION INFORMATION
         AR    R9,R6                   ACCUMULATE TRACKS ALLOCATED
         LA    R2,CRDATE2              LOAD OUTPUT AREA ADDRESS
         MVC   0(14,R2),=CL14' '       BLANK OUT OUTPUT AREA
         BAL   R14,$CRDATE             GET CREATION DATE
         LM    R1,R3,SAVE              LOAD POINTERS
         BXH   R1,R2,OVERFLOW          INCREMENT POINTERS
         MVC   0(133,R1),ERROR2        MOVE LINE INTO STORAGE
         STM   R1,R3,SAVE              SAVE POINTERS
         OI    SWITCH,X'01'            INDICATE OUTPUT PRODUCED
         B     READVTOC                GET ANOTHER ENTRY
         EJECT
***********************************************************************
*                                                                     *
*        DATA SET IS NOT CATALOGED                                    *
*                                                                     *
***********************************************************************
         SPACE 2
NOTCTLGD MVC   DSN3,DS1DSNAM           NOT CATALOGED, COPY DSNAME
         LA    R2,ALLOC3               LOAD OUTPUT AREA ADDRESS
         MVC   0(14,R2),=CL14' '       BLANK OUT OUTPUT AREA
         BAL   R14,$ALLOC              GET ALLOCATION INFORMATION
         AR    R9,R6                   ACCUMULATE TRACKS ALLOCATED
         LA    R2,CRDATE3              LOAD OUTPUT AREA ADDRESS
         MVC   0(14,R2),=CL14' '       BLANK OUT OUTPUT AREA
         BAL   R14,$CRDATE             GET CREATION DATE
         LM    R1,R3,SAVE              LOAD POINTERS
         BXH   R1,R2,OVERFLOW          INCREMENT POINTER
         MVC   0(133,R1),ERROR3        MOVE LINE INTO STORAGE
         STM   R1,R3,SAVE              SAVE POINTERS
         OI    SWITCH,X'01'            INDICATE OUTPUT PRODUCED
         B     READVTOC                GET ANOTHER ENTRY
         EJECT
***********************************************************************
*                                                                     *
*        GET ALLOCATION  (ALLOCATED, USED, AND EXTENTS)               *
*                                                                     *
***********************************************************************
*                                                                     *
*        R2        - OUTPUT AREA ADDRESS                              *
*        R3        - RESERVED (DSNAME PDE POINTER)                    *
*        R4        - NUMBER OF EXTENTS (COUNTER)                      *
*        R5        - EXECUTE TABLE POINTER                            *
*        R6        - TRACKS ALLOCATED                                 *
*        R7        - EXTENT DESCRIPTION POINTER/TRACKS USED           *
*        R8 - R13  - RESERVED (POINTERS AND BASE REGISTERS)           *
*                                                                     *
***********************************************************************
         SPACE 2
$ALLOC   CLC   DS1PTRDS,=XL5'00'       SEE IF FORMAT 3 DSCB IS NEEDED
         BE    PROCESS
         OBTAIN OBTAIN                 READ FORMAT 3 DSCB
         LTR   R15,R15                 TEST RETURN CODE
         BZ    PROCESS
         MVC   DSN7,DS1DSNAM           MOVE DSNAME INTO MESSAGE
         PUT   SYSPRINT,ERROR7         DISPLAY ERROR MESSAGE
         MVI   CCODE+3,16              SET CONDITION CODE
         B     CLOSEUT1
*--------GET TRACK ALLOCATION
PROCESS  SR    R4,R4                   CLEAR REGISTER 4
         SR    R5,R5                   CLEAR REGISTER 5
         SR    R6,R6                   CLEAR REGISTER 6
         SR    R7,R7                   CLEAR REGISTER 7
         IC    R4,DS1NOPEV             LOAD NUMBER OF EXTENTS
         LTR   R4,R4                   TEST NUMBER OF EXTENTS
         BZ    ALLOCEND
DSATTRKS EX    R0,DSATADDR(R5)         PROCESS EXTENT BLOCK
         CLI   0(R7),X'00'
         BE    DSATUTRK
         LA    R7,2(R7)                LOAD ADDRESS OF STARTING TRACK
         MVC   HALF(2),4(R7)           LOAD ENDING CYL NUMBER
         LH    R1,HALF
         MVC   HALF(2),0(R7)           LOAD STARTING CYL NUMBER
         SH    R1,HALF                 SUBTRACT STARTING FROM ENDING
         MH    R1,TRKPRCYL             MULTIPLY BY TRACKS PER CYL
         MVC   HALF(2),6(R7)           LOAD ENDING TRACK
         AH    R1,HALF                 ADD TO TRACK COUNT
         MVC   HALF(2),2(R7)           LOAD STARTING TRACK
         SH    R1,HALF                 SUBTRACT FROM TRACK COUNT
         AR    R6,R1                   ACCUMULATE TOTAL
         LA    R6,R1(R6)               ADD 1 TRACK
         LA    R5,4(R5)
         BCT   R4,DSATTRKS             DECREMENT EXTENT COUNT
DSATUTRK CVD   R6,PDEC                 CONVERT TRACKS TO DECIMAL
         MVC   DEC,=X'4020202020202120'      MOVE MASK
         ED    DEC,PDEC+4              EDIT
         MVC   0(5,R2),DEC+3           MOVE INTO MESSAGE
*--------GET TRACKS USED
         LH    R7,DS1LSTAR             LOAD TRACKS USED
         CLC   DS1LSTAR,=X'000000'
         BE    DSATXNTS
         LA    R7,1(R7)                ADD 1
         CVD   R7,PDEC                 CONVERT TO DECIMAL
         MVC   DEC,=X'4020202020202120'  MOVE MASK
         ED    DEC,PDEC+4              EDIT
         MVC   6(5,R2),DEC+3           MOVE INTO MESSAGE
*--------GET NUMBER OF EXTENTS
DSATXNTS SR    R1,R1
         IC    R1,DS1NOPEV             LOAD EXTENTS
         CVD   R1,PDEC                 CONVERT TO DECIMAL
         MVC   DEC,=X'4020202020202120'  MOVE MASK
         ED    DEC,PDEC+4              EDIT
         MVC   12(2,R2),DEC+6          MOVE INTO MESSAGE
ALLOCEND BR    R14                     RETURN
         EJECT
*--------DSCB'S
DSATADDR LA    R7,DS1EXT1
         LA    R7,DS1EXT2
         LA    R7,DS1EXT3
         LA    R7,DS3EXT01
         LA    R7,DS3EXT02
         LA    R7,DS3EXT03
         LA    R7,DS3EXT04
         LA    R7,DS3EXT05
         LA    R7,DS3EXT06
         LA    R7,DS3EXT07
         LA    R7,DS3EXT08
         LA    R7,DS3EXT09
         LA    R7,DS3EXT10
         LA    R7,DS3EXT11
         LA    R7,DS3EXT12
         LA    R7,DS3EXT13
         EJECT
***********************************************************************
*                                                                     *
*        CREATION DATE                                                *
*                                                                     *
***********************************************************************
         SPACE 2
$CRDATE  L     R1,DS1CREDT-1           LOAD CREATION DATE
         BAL   R5,DATECONV             CONVERT IT
         MVC   0(8,R2),PDEC            MOVE DATE INTO MESSAGE
         BR    R14                     RETURN
         EJECT
***********************************************************************
*                                                                     *
*        DATE CONVERSION ROUTINE                                      *
*                                                                     *
*             THIS SECTION WAS ADAPTED FROM AN INSTALLATION           *
*             DATE CONVERSION ROUTINE TO AVOID HAVING MANY            *
*             DIFFERENT DATE CONVERSION ROUTINES.  IT IS              *
*             NECESSARY TO CONVERT THE DATE TO PACKED DECIMAL         *
*             FORM (AS IS THE DATE RETURNED BY TIME) TO               *
*             CONFORM TO THIS ROUTINE.                                *
*                                                                     *
***********************************************************************
         SPACE 2
DATECONV LA    R1,0(R1)                ZERO HIGH BYTE
         TM    DS1DSORG+1,X'08'        TEST FOR VSAM
         BO    DATECON1
         LR    R0,R1                   SAVE DATE 00YYDDDD - BINARY
         N     R0,=X'0000FFFF'                   0000DDDD - BINARY
         CVD   R0,PDEC                           0000DDDS - DECIMAL
         L     R0,PDEC+4                         0000DDDS - DECIMAL
         N     R1,=X'00FF0000'                   00YY0000 - BINARY
         SRA   R1,16                             000000YY - BINARY
         CVD   R1,PDEC                           00000YYS - DECIMAL
         L     R1,PDEC+4                         00000YYS - DECIMAL
         SLL   R1,12                             00YYS000 - DECIMAL
         N     R1,=X'00FF0000'                   00YY0000 - DECIMAL
         OR    R1,R0                             00YYDDDS - DECIMAL
DATECON1 ST    R1,WORK                 SAVE DATE
         LA    R1,PDEC                 SAVE OUTPUT AREA ADDRESS
         USING OUTPUT,R1               DEFINE OUTPUT AREA BASE REG
         CP    DATE,=P'0'              TEST FOR ZERO DATE
         BE    NODATE
         UNPK  JULIAN,DATE             UNPACK DATE
         OI    JULDAY+2,X'F0'          SET ZONE
         MVI   BLANK,C' '              SET BLANK
         MVC   TARGET,=PL3'0'          00 00 0C
         MVO   YEAR2,YEAR              00 0Y YC
         DP    TARGET,=P'4'            0Q QC RC
         CP    REM,=P'0'               TEST REMAINDER
         BE    LEAP
         CP    DAY,=P'59'              TEST FOR BEFORE FEB 29
         BNH   LEAP
         AP    DAY,=P'1'               ADJUST TO LEAP YEAR
LEAP     CP    DAY,=P'1'               TEST MINIMUM DAY
         BL    BADDAY
         CP    DAY,=P'366'             TEST MAXIMUM DAY
         BH    BADDAY
         LA    R6,CALTABLE             LOAD TABLE ADDRESS
         USING CALENDAR,R6             SET BASE ADDRESS
         ZAP   MONTHP,=P'1'            SET MONTH TO 1
LOOP     CP    DAY,DAYS                SEE IF MORE DAYS THAN THIS MNTH
         BNH   OUT
         SP    DAY,DAYS                SUBTRACT DAYS IN THIS MONTH
         AP    MONTHP,=P'1'            ADD 1 TO MONTH
         LA    R6,LEN(R6)              INCREMENT TO NEXT MONTH
         B     LOOP
OUT      UNPK  MO,MONTHP               UNPACK MONTH
         OI    MO+1,X'F0'              SET ZONE
         UNPK  DA,DAY                  UNPACK DAY
         OI    DA+1,X'F0'              SET ZONE
         MVI   SLASH1,C'/'             SET SLASH
         MVI   SLASH2,C'/'             SET SLASH
         B     DATERTRN
NODATE   MVC   0(12,R1),=CL12' '       BLANK OUTPUT FIELD
         B     DATERTRN
BADDAY   MVC   0(12,R1),=CL12'INVALID DATE'
DATERTRN BR    R5                      RETURN
         SPACE
***********************************************************************
*                                                                     *
*        CALENDAR TABLE - NUMBER OF DAYS IN EACH MONTH (LEAP YEAR)    *
*                                                                     *
***********************************************************************
         SPACE
CALTABLE DC    PL2'31'                 JANUARY
         DC    PL2'29'                 FEBRUARY
         DC    PL2'31'                 MARCH
         DC    PL2'30'                 APRIL
         DC    PL2'31'                 MAY
         DC    PL2'30'                 JUNE
         DC    PL2'31'                 JULY
         DC    PL2'31'                 AUGUST
         DC    PL2'30'                 SEPTEMBER
         DC    PL2'31'                 OCTOBER
         DC    PL2'30'                 NOVEMBER
         DC    PL2'31'                 DECEMBER
         EJECT
***********************************************************************
*                                                                     *
*        FINISHED PROCESSING VTOC AND CATALOG                         *
*                                                                     *
*        SORT RECORDS                                                 *
*                                                                     *
***********************************************************************
         SPACE 2
OVERFLOW SR    R1,R2                   REDUCE POINTER ONE ENTRY
         STM   R1,R3,SAVE              SAVE POINTERS
         PUT   SYSPRINT,ERROR9         DISPLAY ERROR MESSAGE
         PUT   SYSPRINT,HEADER         PRINT HEADER
EOD1     CLOSE SYSUT1                  CLOSE DATA SETS
         TM    SWITCH,X'01'            SEE IF ANY RECORDS
         BZ    NONE
         LM    R1,R3,SAVE              LOAD POINTERS
         LR    R3,R1                   RESET LIMIT TO LAST ENTRY
         L     R1,TABLEADR             LOAD TABLE ADDRESS
         STM   R1,R3,SAVE              SAVE POINTERS
         LR    R2,R1                   COPY FIRST TO R2
         #SORT FIELDS=(2,              KEY IN POS 2                    +
               44,                     KEY IS 44 BYTES LONG            +
               CH,                     DATA IS CHARACTER FORMAT        +
               A),                     SORT IN ASCENDING ORDER         +
               FIRST=(R2),             FIRST ENTRY ADDRESS IN REG 2    +
               LAST=(R3),              LAST ENTRY ADDRESS IN REG 3     +
               LENGTH=133              RECORDS ARE 133 BYTES LONG
         CVD   R9,PDEC                 CONVERT TOTAL TRACKS
         MVC   DEC,=X'4020202020202120'     SET UP EDIT MASK
         ED    DEC,PDEC+4              EDIT
         MVC   TOTAL,DEC+3             COPY INTO MESSAGE
         LA    R2,0                    INITIALIZE LINE COUNTER
         LA    R8,1                    INCREMENT
         LA    R9,50                   LIMIT
         PUT   SYSPRINT,HEADER1
         PUT   SYSPRINT,HEADER2
         LM    R5,R7,SAVE              LOAD POINTERS
         SR    R5,R6                   SUBTRACT LENGTH OF ONE ENTRY
         EJECT
***********************************************************************
*                                                                     *
*        PRINT REPORT AND PUNCH SCRATCH STATEMENTS                    *
*                                                                     *
***********************************************************************
         SPACE 2
READLINE BXH   R5,R6,ENDVOL            POINT TO NEXT RECORD
         BXLE  R2,R8,PRINT             INCREMENT LINE COUNT AND TEST
         PUT   SYSPRINT,HEADER         PRINT HEADERS
         PUT   SYSPRINT,HEADER1
         PUT   SYSPRINT,HEADER2
         LA    R2,1                    RESET LINE COUNTER
PRINT    PUT   SYSPRINT,0(R5)          PRINT LINE
         MVC   SCRATCHD,1(R5)          COPY DSNAME
         PUT   SYSUT2,SCRATCH1         PUT SCRATCH RECORDS
         PUT   SYSUT2,SCRATCH2
         B     READLINE                LOOP
         EJECT
***********************************************************************
*                                                                     *
*        NO UNCATALOGED OR MISCATALOGED DATA SETS ON VOLUME           *
*                                                                     *
***********************************************************************
         SPACE 2
NONE     PUT   SYSPRINT,OK
         B     CLOSEUT1
         EJECT
***********************************************************************
*                                                                     *
*        END OF VOLUME                                                *
*                                                                     *
***********************************************************************
         SPACE 2
ENDVOL   PUT   SYSPRINT,DONE
CLOSEUT1 CLOSE SYSUT1                  CLOSE SYSUT1
         B     READJFCB
         EJECT
***********************************************************************
*                                                                     *
*        OUTPUT COMPLETE - TERMINATE                                  *
*                                                                     *
***********************************************************************
         SPACE 2
RETURN   CLOSE (SYSPRINT,,SYSUT2)      CLOSE FILES
         TM    SWITCH,X'02'            SEE IF FREEMAIN REQUIRED
         BZ    RETURN2
         L     R1,TABLEADR             LOAD STORAGE ADDRESS
         FREEMAIN R,LV=26600,SP=1,A=(1)     FREE STORAGE
RETURN2  L     R15,CCODE               SET RETURN CODE
         $RTRN RC=(R15)                RETURN TO OS
         EJECT
***********************************************************************
*                                                                     *
*        DATA                                                         *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         SPACE
CCODE    DC    F'0'                    CONDITION CODE
TABLEADR DS    A                       ADDRESS OF STORAGE AREA
SAVE     DS    3A                      POINTERS TO ENTRIES IN STORAGE
COUNTER  DC    PL2'0'                  VOLUME COUNTER
HALF     DS    H                       WORK AREA
TRKPRCYL DS    H                       TRACKS PER CYLINDER
SWITCH   DC    X'00'                   INDICATORS
         DS    X                       NOT USED
PDEC     DS    D                       WORK AREA
DEC      DS    D                       WORK AREA
DEVCODE  DS    D                       DEVTYPE INFORMATION
DEVTABLE DC    X'00',CL7'SYSDA='         UNSUPPORTED
         DC    X'04',CL7'2311='
         DC    X'04',CL7'2301='
         DC    X'04',CL7'2303='
         DC    X'04',CL7'2302='
         DC    X'04',CL7'2321='
         DC    X'06',CL7'2305-1='
         DC    X'06',CL7'2305-2='
         DC    X'04',CL7'2314='
         DC    X'04',CL7'3330='
         DC    X'00',CL7'SYSDA='
         DC    X'04',CL7'3350='
         DC    X'00',CL7'SYSDA='
         DC    X'06',CL7'3330-1='
         DC    X'00',CL7'SYSDA='
         DC    X'00',CL7'SYSDA='
         SPACE
WORK     DS    0F                      DATE
         DS    C                       FILLER
DATE     DS    0CL3                    JULIAN DATE - PACKED DECIMAL
YEAR     DS    C                       YEAR - NO SIGN
DAY      DS    CL2                     DD DS
MONTHP   DS    PL2                     0M MS
TARGET   DS    0CL3                    00 0Y YS  -  0Q QS RS
         DS    C                       0Q         PART OF QUOTIENT
YEAR2    DS    0PL2                    0Y YS      YEAR AND SIGN
         DS    C                       QS         QUOTIENT AND SIGN
REM      DS    C                       RS         REMAINDER AND SIGN
         SPACE
DSNAME   DS    CL44
VTOCDSNM DC    44X'04'                 VTOC DSNAME
LOCLIST  CAMLST NAME,DSNAME,,CATBLOCK
OBTAIN   CAMLST SEEK,DS1PTRDS,VOLSER,FMT3DSCB
         EJECT
FMT1DSCB DS    0F                      ALIGNMENT
         #VTCFMT1
         EJECT
         ORG   FMT1DSCB
         #VTCFMT4
         ORG   FMT1DSCB+148
         EJECT
FMT3DSCB DS    0F                      ALIGNMENT
         #VTCFMT3
         ORG   FMT3DSCB+148
         EJECT
ERROR2   DC    CL1' '                  CARRIAGE CONTROL          1    1
DSN2     DC    CL44' '                 DSNAME                   44   45
         DC    CL5' '                                            5   50
ALLOC2   DC    CL14' '                 ALLOCATION               14   64
         DC    CL5' '                                            5   69
CRDATE2  DC    CL8' '                  CREATION DATE             8   77
         DC    CL5' '                                            5   82
         DC    CL21' IS NOT CATALOGED ON '                      21  103
SER2     DC    CL6' '                  VOLUME SERIAL             6  109
         DC    CL24' '                 FILLER                   24  133
         SPACE 2
ERROR3   DC    CL1' '                  CARRIAGE CONTROL          1    1
DSN3     DC    CL44' '                 DSNAME                   44   45
         DC    CL5' '                                            5   50
ALLOC3   DC    CL14' '                 ALLOCATION               14   64
         DC    CL5' '                                            5   69
CRDATE3  DC    CL8' '                  CREATION DATE             8   77
         DC    CL5' '                                            5   82
         DC    CL17' IS NOT CATALOGED'                          17   99
         DC    CL34' '                 FILLER                   34  133
         SPACE 2
ERROR7   DC    CL1'-'                                            1    1
         DC    CL32'ERROR READING FORMAT 3 DSCB FOR '           32   33
DSN7     DC    CL44' '                                          44   77
         DC    CL56' '                                          56  133
         SPACE 2
ERROR8   DC    CL133'-UNABLE TO OPEN SYSUT2 - STEP TERMINATED'
         SPACE 2
ERROR9   DC    CL133'-LIST INCOMPLETE, TABLE EXCEEDED'
         SPACE 2
HEADER   DC    CL1'1'                                                 1
         DC    CL28'CATALOG/VTOC COMPARISON FOR '                    29
VOLSER   DC    CL6' '                                                35
         DC    CL5' '                                                40
HDRDATE  DC    CL8' '                                                48
         DC    CL3' '                                                51
TIME     DC    XL10'402020204B20204B2020'                            61
         DC    CL72' '                                              133
         SPACE 2
HEADER1  DC    CL1'0'                                                 1
         DC    CL44'-------------------DSNAME-------------------'    45
         DC    CL5' '                                                50
         DC    CL14'ALLOC  USED EX'                                  64
         DC    CL5' '                                                69
         DC    CL8'CR. DATE'                                         77
         DC    CL56' '                                              133
         SPACE 2
HEADER2  DC    CL133'0'                                             133
         SPACE 2
OK       DC    CL133'-NO UNCATALOGED OR MISCATALOGED DATA SETS ON VOLUM+
               E'
         SPACE 2
WARN     DC    CL133'-** WARNING **  DEVICE NAME COULD NOT BE DETERMINE+
               D.  SYSDA USED.  SCRATCH RECORDS MAY REQUIRED MODIFICATI+
               ON FOR MVS SYSTEMS.'
         SPACE 2
DONE     DC    CL50'0'                                               50
TOTAL    DC    CL5' '                                                55
         DC    CL78' '                                              133
         SPACE 2
SCRATCH1 DC    CL14'  SCRATCH VOL='                                  14
SCRATCHV DC    CL57' '                                               71
         DC    CL9'X'                                                80
         SPACE 2
SCRATCH2 DC    CL15' '                                               15
         DC    CL7'DSNAME='                                          22
SCRATCHD DC    CL44' '                                               66
         DC    CL14' '                                               80
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,        DDNAME IS SYSPRINT              +
               DSORG=PS,               ORGAINZATION IS SEQUENTIAL      +
               MACRF=PM,               PUT MOVE                        +
               RECFM=FBA,              FIXED RECORDS WITH ASA CONTROL  +
               LRECL=133,              RECORD LENGTH IS 133            +
               BLKSIZE=133             BLOCKSIZE IS 133
         EJECT
SYSUT2   DCB   DDNAME=SYSUT2,          DDNAME IS SYSUT2                +
               DSORG=PS,               ORGAINZATION IS SEQUENTIAL      +
               MACRF=PM,               PUT MOVE                        +
               RECFM=FB,               FIXED, BLOCKED RECORDS          +
               LRECL=80,               RECORD LENGTH IS 80             +
               BLKSIZE=80              BLOCKSIZE IS 80
         EJECT
SYSUT1   DCB   DDNAME=VOLUMENN,        DDNAME IS VOLUMENN              +
               DSORG=PS,               ORGANIZATION IS SEQUENTIAL      +
               MACRF=R,                READ MACRO WILL BE USED         +
               KEYLEN=44,              44 BYTE KEYS (DSNAME)           +
               RECFM=F,                RECORD FORMAT IS FIXED          +
               BLKSIZE=96,             SIZE OF DSCB DATA PORTION       +
               EODAD=EOD1,             ENE OF DATA ROUTINE ADDRESS     +
               EXLST=EXLST1            EXIT LIST ADDRESS
         SPACE 2
EXLST1   DC    X'87',AL3(JFCBAREA)
         SPACE 2
JFCBAREA DS    0F                      AREA FOR JFCB
         IEFJFCBN
         EJECT
CATBLOCK DS    0D                      ALIGNMENT
         DS    265C                    AREA FOR CATALOG BLOCK
         EJECT
***********************************************************************
*                                                                     *
*        CALENDAR TABLE DSECT                                         *
*                                                                     *
***********************************************************************
         SPACE
CALENDAR DSECT
DAYS     DS    PL2                     DAYS IN MONTH
LEN      EQU   *-DAYS                  LENGTH OF ENTRY
         SPACE 2
***********************************************************************
*                                                                     *
*        OUTPUT AREA DSECT                                            *
*                                                                     *
***********************************************************************
         SPACE
OUTPUT   DSECT                         OUTPUT DSECT
MO       DS    CL2                     MONTH
SLASH1   DS    C                       SLASH
DA       DS    CL2                     DAY
SLASH2   DS    C                       SLASH
JULIAN   DS    0CL5                    JULIAN DATE - EBCDIC
YR       DS    CL2                     YEAR
JULDAY   DS    CL3                     JULIAN DAY
BLANK    DS    C                       BLANK
         END
