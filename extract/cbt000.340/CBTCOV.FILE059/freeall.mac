         LCLC  &OPSYSTM                DEFINE OPERATING SYSTEM
&OPSYSTM SETC  'MVS'                   MVT
*                                      SVS
*                                      MVS
         TITLE 'DISCLAIMER - INSTALLATION INSTRUCTIONS'
***********************************************************************
*                                                                     *
*     THIS PROGRAM, PROVIDED BY FPL, IS OF GENERAL INTEREST           *
*     SUBMITTED FOR UNRESTRICTED DISTRIBUTION.  THIS PROGRAM          *
*     HAS MET A BASIC SET OF PROGRAMMING AND DOCUMENTATION            *
*     STANDARDS, BUT MAY NOT HAVE BEEN PROGRAM TESTED IN ANY          *
*     FORMAL FASHION BY FPL.  THE USER IS EXPECTED TO MAKE THE        *
*     FINAL EVALUATION AS TO THE USEFULLNESS IN HIS OWN               *
*     ENVIRONMENT.                                                    *
*                                                                     *
*     FPL MAKES NO WARRANTY, EXPRESSED OR IMPLIED, INCLUDING, BUT     *
*     NOT LIMITED TO, THE IMPLIED WARRANTIES OR MERCHANTABILITY AND   *
*     FITNESS FOR A PARTICULAR PUTPOSE AS TO THE DOCUMENTATION,       *
*     FUNCTION, OR PERFORMANCE OF THESE PROGRAMS.                     *
*                                                                     *
*     ACCEPTANCE AND USE OF THIS PROGRAM CONSTITUTES A RELEASE        *
*     FROM LIABILITY OF FPL FOR ANY PROBLEMS USE OF THE PROGRAM       *
*     MAY CAUSE AT THE USER'S INSTALLATION.                           *
*                                                                     *
*     USERS ARE INVITED TO SUBMIT SUGGESTIONS OR ERROR DOCUMENTATION  *
*     TO FPL, HOWEVER, NO PROMISE CAN BE MADE THAT SUCH SUGGESTIONS   *
*     WILL BE IMPLEMENTED OR ERRORS CORRECTED.  SUBMIT COMMENTS TO:   *
*              COORDINATOR OF TECHNICAL SYSTEMS                       *
*              SYSTEMS & PROGRAMMING DEPARTMENT                       *
*              FLORIDA POWER & LIGHT COMPANY - GENERAL OFFICE         *
*              P. O. BOX  529100                                      *
*              MIAMI, FLORIDA  33152                                  *
*                                                                     *
*     THIS PROGRAM IS MADE AVAILABLE BY FPL WITHOUT CHARGE OR         *
*     CONSIDERATION.  RECIPIENTS ARE FREE TO MAKE THIS PROGRAM        *
*     AVAILABLE TO OTHERS IN LIKE MANNER.  IT MAY NOT BE SOLD.        *
*                                                                     *
* INSTALLATION INSTRUCTIONS                                           *
*                                                                     *
*     THIS MODULE MAY BE ASSEMBLED AND LINKED INTO A LIBRARY IN THE   *
*     LINK LIST OR SPECIFIED AS A STEPLIB IN THE TSO LOGON PROCEDURE. *
*     THE COMMAND MAY BE INVOKED FROM TSO BY ENTERING THE NAME OR AN  *
*     ALIAS OF THE MODULE.                                            *
*                                                                     *
*     BEFORE ASSEMBLING THE MODULE, INSURE THAT ALL THE REQUIRED      *
*     MACROS ARE AVAILABLE.  SOME MACROS FROM SYS1.AMODGEN MAY BE     *
*     REQUIRED.  THE $ENTER MACRO USES SYSTEM VARIABLE SYMBOLS        *
*     &SYSDATE AND &SYSTIME.  THESE SYMBOLS ARE NOT SUPPORTED BY ALL  *
*     ASSEMBLERS.  IF THE ASSEMBLER TO BE USED DOES NOT SUPPORT THESE *
*     SYMBOLS, THE $ENTER MACRO MUST BE MODIFIED.                     *
*                                                                     *
*     THE SOURCE PROVIDED IS OPERATING SYSTEM DEPENDENT.  SPECIFY THE *
*     DESIRED OPERATING SYSTEM AS THE VALUE FOR &OPSYSTM.             *
*                                                                     *
*     THE MODULE MAY BE LINKED WITH ATTRIBUTES RENT AND REFR AND IS   *
*     ELIGIBLE FOR INCLUSION IN THE LINK PACK AREA.                   *
*                                                                     *
***********************************************************************
FREEALL  TITLE 'TSO COMMAND PROCESSOR - FREEALL'
***********************************************************************
*                                                                     *
* SS0806       ALIAS FREEALL                                          *
*                                                                     *
* FUNCTION     THE FREEALL COMMAND MAY BE USED TO FREE ALL            *
*              ATTRIBUTE LISTS AND ALL DATA SETS WHICH WERE           *
*              DYNAMICALLY ALLOCATED (ALLOCATED DURING THE            *
*              SESSION BY THE ALLOCATE COMMAND OR BY OTHER            *
*              COMMANDS).                                             *
*                                                                     *
* ENVIRONMENT  OS/MVT                                                 *
*              OS/VS2 RELEASE 1 (SVS)                                 *
*              OS/VS2 RELEASE 3 (MVS)                                 *
*              MODE - FOREGROUND                                      *
*                                                                     *
*              NOTE - OPERATING SYSTEM MUST BE SPECIFIED IN SETC      *
*                     STATEMENT AT START OF PROGRAM.                  *
*                                                                     *
* SYNTAX       FREEALL                                                *
*                                                                     *
* ATTRIBUTES   RENT, REUS, REFR, ENABLED, NON-PRIVELEGED              *
*                                                                     *
* CONSIDERATIONS                                                      *
*                                                                     *
*              DATA SETS ALLOCATED TO DDNAMES SYSPROC AND SYSHELP     *
*              AND CONCATENATED DATA SETS WILL NOT BE FREED.          *
*                                                                     *
* EXAMPLE      FREE ALL DYNAMICALLY ALLOCATED DATA SETS AND           *
*              ATTRIBUTE LISTS.                                       *
*                                                                     *
*              FREEALL                                                *
*                                                                     *
* FPL MACROS USED -                                                   *
*                                                                     *
*              $ENTER         ENTRY CODING                            *
*              $RTRN          EXIT CODING                             *
*              #TSOBLKS       CONSTRUCT TSO CONTROL BLOCKS            *
*                                                                     *
* IBM MACROS USED -                                                   *
*                                                                     *
*              LINK           INVOKE SERVICE ROUTINES                 *
*              IKJDAP18       MAP DAIR CONTROL BLOCK                  *
*              IKJDAP34       MAP DAIR CONTROL BLOCK         (SVS)    *
*              IKJATRCB       MAP ATTRIBUTE CONTROL BLOCK    (SVS)    *
*              IKJEFFDF       MAP IKJEFF18 PARAMETER LIST    (MVS)    *
*                                                                     *
* TSO SERVICE ROUTINES USED -                                         *
*                                                                     *
*              IKJDAIR        DYNAMIC ALLOCATION INTERFACE ROUTINE    *
*              IKJEFF18       ISSUE DYNAMIC ALLOCATION ERROR MESSAGE  *
*                                                                     *
* REGISTER USAGE -                                                    *
*                                                                     *
*              R0  - LINKAGE REGISTER                                 *
*              R1  - LINKAGE REGISTER                                 *
*              R2  - WORK REGISTER/BASE REGISTER FOR DSE/ATRCB/DSAB   *
*              R3  - ADDRESS OF NEXT DSE/ATRCB/DSAB                   *
*              R4  - ADDRESS OF FIRST ATTRIBUTE CONTROL BLOCK         *
*              R5  - NOT USED                                         *
*              R6  - NOT USED                                         *
*              R7  - NOT USED                                         *
*              R8  - NOT USED                                         *
*              R9  - DAPB BASE REGISTER                               *
*              R10 - DAPL BASE REGISTER                               *
*              R11 - CPPL BASE REGISTER                               *
*              R12 - PROGRAM BASE REGISTER                            *
*              R13 - SAVE AREA/WORK AREA BASE REGISTER                *
*              R14 - LINKAGE REGISTER                                 *
*              R15 - LINKAGE REGISTER                                 *
*                                                                     *
*                                                 9/76 GORDON P. WEST *
***********************************************************************
         TITLE 'ENTRY CODING'
FREEALL  $ENTER BASE=R12,              BASE REGISTER IS 12             +
               RENT=DSECTLEN,          GETMAIN LENGTH                  +
               SAVE=SAVEAREA           SAVE AREA NAME
         TITLE 'CONSTRUCT TSO CONTROL BLOCKS'
***********************************************************************
*                                                                     *
*        CONSTRUCT TSO CONTROL BLOCKS                                 *
*                                                                     *
***********************************************************************
         SPACE 2
         #TSOBLKS DSECT,               GENERATE DSECTS                 +
               ECB=FREEECB,            ECB NAME                        +
               CPPL=(R11,R1),          CPPL REGISTERS                  +
               DAPL=(R10,FREEDAPL,TYPE18)   DAIR INFORMATION
         LA    R9,TYPE18               LOAD DAPB ADDRESS
         USING DAPB18,R9               DEFINE DAPB BASE REGISTER
         XC    DAPB18(LEN18),DAPB18    ZERO DAPB
         MVC   DA18CD,=X'0018'         DAPB IDENTIFIER
         MVC   DA18MNM,=CL8' '         BLANK OUT MEMBER NAME
         MVC   DA18SCLS,=CL2' '        BLANK OUT SYSOUT CLASS
         MVI   DA18CTL,DA18PERM        FREE PERM ALLOC DATA SETS
         MVC   DA18JBNM,=CL8' '        BLANK OUT JOB NAME
         AIF   ('&OPSYSTM' EQ 'MVS').GETDSAB
         TITLE 'GET FIRST DSE'
***********************************************************************
*                                                                     *
*        GET FIRST DSE                                                *
*                                                                     *
***********************************************************************
         SPACE 2
         L     R1,16                   LOAD CVT ADDRESS
         L     R2,228(R1)              LOAD TSCVT ADDRESS
         L     R1,0(R1)                LOAD TCB PTR ADDRESS
         L     R1,4(R1)                LOAD TCB ADDR
         L     R3,180(R1)              LOAD JSCB ADDRESS
         L     R1,0(R2)                LOAD FIRST TJB ADDRESS
         LH    R15,26(R2)              LOAD TJB SIZE
         MH    R15,268(R3)             INDEX INTO TJB TABLE
         L     R2,8(R15,R1)            USER'S TJBX
         L     R4,40(R2)               LOAD ADDRESS OF FIRST ATRCB
         L     R2,8(R2)                LOAD ADDRESS OF FIRST DSE
         USING DSE,R2                  DEFINE DSE ADDRESSABILITY
         TITLE 'PROCESS DSE'
LOOP1    L     R3,DSEFORWD             LOAD NEXT DSE ADDRESS
         TM    DSECNTRL,DSEDYN+DSENUSED  TEST FOR DYNAMIC & NOT USED
         BNO   NEXT
         LA    R1,DSEDDNAM             LOAD DDNAME ADDRESS
         AGO   .FREEDDN
.GETDSAB TITLE 'LOCATE FIRST DSAB'
***********************************************************************
*                                                                     *
*        FIND DSAB AND SEARCH FOR DYNAMICALLY ALLOCATED DATA SETS     *
*                                                                     *
***********************************************************************
         SPACE
         L     R2,CVTPTR               CVT ADDRESS
         L     R2,CVTTCBP(R2)          TCB POINTER
         L     R2,4(R2)                TCB ADDRESS
         L     R2,TCBJSCB(R2)          JSCB ADDRESS
         L     R2,JSCDSABQ(R2)         QDB ADDRESS
         L     R2,QDBFELMP(R2)         FIRST DSAB ADDRESS
         TITLE 'PROCESS DSAB'
***********************************************************************
*                                                                     *
*        PROCESS DSAB                                                 *
*                                                                     *
***********************************************************************
         SPACE 2
LOOP1    L     R3,4(R2)                LOAD NEXT DSAB ADDRESS
         TM    DSABFLG1(R2),X'80'      DYNAMICALLY ALLOCATED
         BZ    NEXT                    NO - DON'T FREE IT
         TM    DSABFLG1(R2),X'09'      CONCATENATED OR IN USE
         BNZ   NEXT                    YES - DON'T FREE IT
         CLC   DSABOPCT(2,R2),=H'0'    DATA SET OPEN
         BNZ   NEXT                    YES - DON'T FREE IT
         TM    DSABFLG3(R2),X'08'      DATA SET A PRIVATE CATALOG
         BNZ   NEXT                    YES - DON'T FREE IT
         L     R1,DSABTIOT(R2)         TIOT DD ENTRY
         LA    R1,4(R1)                ADDRESS OF DDNAME
.FREEDDN CLC   0(8,R1),=CL8'SYSPROC'   TEST FOR SPECIAL DDNAMES
         BE    NEXT
         CLC   0(8,R1),=CL8'SYSHELP'
         BE    NEXT
         TITLE 'FREE FILE'
***********************************************************************
*                                                                     *
*        FREE FILE                                                    *
*                                                                     *
***********************************************************************
         SPACE 2
         MVC   DA18DDN,0(R1)           MOVE DDNAME TO DAPB
         LA    R1,DAPL                 ADDRESS OF DAPL
         LINK  EP=IKJDAIR              FREE SPECIFIED DDNAME
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   DAIRFAIL
         TITLE 'CONTINUE TO NEXT FILE'
***********************************************************************
*                                                                     *
*        CONTINUE TO NEXT FILE                                        *
*                                                                     *
***********************************************************************
         SPACE 2
NEXT     LTR   R2,R3                   CHECK FOR LAST FILE
         BNZ   LOOP1
         AIF   ('&OPSYSTM' EQ 'MVS').RETURN
         TITLE 'FREE ATTRIBUTE CONTROL BLOCKS'
         LA    R9,TYPE34               LOAD DAPB ADDRESS
         USING DAPB34,R9               DEFINE DAPB BASE RESISTER
         ST    R9,DAPLDAPB             STORE IN DAPL
         XC    DAPB34(LEN34),DAPB34    ZERO DAPB
         MVC   DA34CD,=X'0034'         SET TYPE CODE
         MVI   DA34CTRL,DA34UNCH       INDICATE UNCHAIN (DELETE)
         LTR   R2,R4                   POINT TO FIRST ATRCB
         BZ    RETURN
         USING ATRCB,R2
LOOP2    L     R3,ATRFORWD             LOAD ADDRESS OF NEXT ATRCB
         MVC   DA34NAME,ATRNAME        COPY NAME
         LA    R1,DAPL                 LOAD DAPL ADDRESS
         LINK  EP=IKJDAIR              FREE THE ATTRIBUTE
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   DAIRFAIL
         LTR   R2,R3                   LOAD ADDRESS OF NEXT ATRCB
         BNZ   LOOP2
.RETURN  ANOP
         TITLE 'EXIT CODING'
***********************************************************************
*                                                                     *
*        EXIT CODING                                                  *
*                                                                     *
***********************************************************************
         SPACE 2
RETURN   $RTRN RC=(R15),               RETURN CODE                     +
               RENT=DSECTLEN           AREA TO BE FREED
         TITLE 'ISSUE DYNAMIC ALLOCATION ERROR MESSAGE'
***********************************************************************
*                                                                     *
*        ISSUE DYNAMIC ALLOCATION ERROR MESSAGE                       *
*                                                                     *
***********************************************************************
         SPACE 2
DAIRFAIL ST    R15,DAIRER              STORE RETURN CODE
         LA    R1,DAPL                 LOAD DAIR PARAMETER LIST ADDR
         ST    R1,DFDAPLP              STORE IN DAIRFAIL PARM LIST
         LA    R1,DAIRER               LOAD DAIR ERROR CODE ADDRESS
         ST    R1,DFRCP                STORE IN DAIRFAIL PARM LIST
         LA    R1,IKJEFF02             LOAD MESSAGE WRITER ADDRESS ADDR
         ST    R1,DFJEFF02             SAVE IN PARAMETER LIST
         LA    R1,DFID                 LOAD ADDRESS OF IDENTIFIER
         ST    R1,DFIDP
         LA    R1,EF18PARM             LOAD PARM LIST ADDRESS
         LINK  EP=IKJEFF18             DISPLAY ERROR MESSAGE
         LA    R15,12                  SET CONDITION CODE
         B     RETURN
         TITLE 'CONSTANT DATA AREA'
***********************************************************************
*                                                                     *
*        CONSTANT DATA AREA                                           *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         SPACE
IKJEFF02 DC    F'0'                    MESSAGE WRITER ADDRESS UNKNOWN
DFID     DC    AL2(DFDAIR)             DAIR CALLER IDENTIFIER
         TITLE 'DYNAMIC ALLOCATION CONTROL BLOCK - TYPE 18 - FREE'
         IKJDAP18
LEN18    EQU   *-DA18CD
         AIF   ('&OPSYSTM' EQ 'MVS').WORK
         TITLE 'DYNAMIC ALLOCATION CONTROL BLOCK - TYPE 34 - ATTR'
         IKJDAP34
LEN34    EQU   *-DA34CD
         TITLE 'ATTRIBUTE CONTROL BLOCK'
         IKJATRCB
         TITLE 'DATA SET EXTENT BLOCK'
         IKJDSE
.WORK    ANOP
         TITLE 'WORK AREA'
***********************************************************************
*                                                                     *
*        WORK AREA                                                    *
*                                                                     *
***********************************************************************
         SPACE
FREEDS   DSECT
SAVEAREA DS    18A
FREEECB  DS    F
DAIRER   DS    F
FREEDAPL DS    0F
         ORG   *+DAPLLEN
TYPE18   DS    0F
         ORG   *+LEN18
         AIF   ('&OPSYSTM' EQ 'MVS').ERRPARM
TYPE34   DS    0F
         ORG   *+LEN34
EF18PARM DS    0F
DFDAPLP  DS    A                       ADDRESS OF FAILING DAPL
DFRCP    DS    A                       ADDRESS OF RETURN CODE
DFJEFF02 DS    A                       ADDRESS OF MESSAGE WRITER
DFIDP    DS    A                       ADDRESS OF IDENTIFIER
DFDAIR   EQU   1                       DAIR IDENTIFIER
DSECTLEN EQU   *-FREEDS
         AGO   .END
.ERRPARM ANOP
EF18PARM DS    0F
         IKJEFFDF
DSECTLEN EQU   *-FREEDS
         TITLE 'EQUATES'
***********************************************************************
*                                                                     *
*        EQUATES                                                      *
*                                                                     *
***********************************************************************
         SPACE
CVTPTR   EQU   16
CVTTCBP  EQU   0
TCBJSCB  EQU   180
JSCDSABQ EQU   320
QDBFELMP EQU   12
DSABOPCT EQU   14
DSABTIOT EQU   16
DSABFLG1 EQU   34
DSABFLG3 EQU   36
.END     ANOP
         END
