PROC 0  DEBUG
CONTROL NOMSG PROMPT NOLIST NOFLUSH
IF &DEBUG NE &STR() THEN +
  CONTROL LIST CONLIST SYMLIST MSG
/***************  INITIALIZING OPTIONS TO DEFAULT VALUES*************/
SET &OPT EQ &STR()
SET &LISTOPT EQ &STR()
SET &ADSN EQ &STR()
SET &ADSG EQ &STR()
SET &ACOMB EQ &STR()
SET &AVOL EQ &STR()
SET &AVOLG EQ &STR()
/***************  DISPLAY VTOC REPORT PANEL *************************/
RESTART: +
SET &ACMD EQ &STR( PRINT VTOC,SUMPFX=ALL)
SET &ACMD2 EQ &STR( )
SET &COMMA EQ &STR()
SET &CONT EQ &STR(NO)
ISPEXEC DISPLAY PANEL(ABRRPT4) CURSOR(LISTOPT)
IF &LASTCC GE 8 THEN +
  DO
    EXIT
  END
IF &SYSPREF EQ &STR() THEN +
  DO
    SET &DSPRFX EQ &SYSUID
  END
ELSE +
  IF &SYSPREF EQ &SYSUID THEN +
    DO
      SET &DSPRFX EQ &SYSUID
    END
  ELSE +
    DO
      SET &DSPRFX EQ &SYSPREF
    END
SET &DSYS EQ &STR(D&SUBSTR(1:2,&SYSDATE)&SUBSTR(4:5,&SYSDATE)&SUBSTR(7:+
8,&SYSDATE)
SET &TSYS EQ &STR(T&SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)&SUBSTR(7:+
8,&SYSTIME))
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
/***************  ALLOCATE SPACE FOR PRINT UTILITY  *****************/
FREE F(SYSIN,SYSPRINT,SYSOUT,ABRSUM)
FREE A(FB80 FBA121)
ATTR FB80 RECFM(F B) LRECL(80) BLKSIZE(3120)
ATTR FBA121 RECFM(F B A) LRECL(121) BLKSIZE(121)
ALLOC F(SYSIN) NEW DELETE SPACE(1 1) TRACKS USING(FB80) UNIT(VIO)
ALLOC F(SYSOUT) DUMMY
ALLOC F(SYSPRINT) DUMMY
ALLOC F(ABRSUM) DA('&DSPRFX..&DSYS..&TSYS..ABRSUM') NEW CATALOG +
SPACE(10 5) TRACKS USING(FBA121)
IF &ADSN NE &STR() THEN +
  DO
    SET &ACMD EQ &STR(&ACMD&STR(,DSN=(&ADSN))
  END
IF &ADSG NE &STR() THEN +
  DO
    SET &ACMD EQ &STR(&ACMD&STR(,DSG=(&ADSG))
  END
IF &ACOMB NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(COMBINE))
    SET &COMMA EQ &STR(,)
  END
IF &AVOL NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(VOL=(&AVOL)))
    SET &COMMA EQ &STR(,)
  END
IF &AVOLG NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(VOLG=(&AVOLG)))
    SET &COMMA EQ &STR(,)
  END
IF &AVOL EQ &STR() THEN +
  DO
    IF &AVOLG EQ &STR() THEN +
      DO
        SET &CONT EQ &STR(YES)
        SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(ONLINE))
        SET &COMMA EQ &STR(,)
      END
  END
IF &CONT EQ &STR(YES) THEN +
  SET &ACMD EQ &STR(&ACMD&STR(&COMMA))
OPENFILE SYSIN OUTPUT
SET &SYSIN EQ &STR(&ACMD)
PUTFILE SYSIN
IF &CONT EQ &STR(YES) THEN +
  DO
    SET &SYSIN EQ &STR(      &ACMD2)
    PUTFILE SYSIN
  END
CLOSFILE SYSIN
$ FDRABRP
ISPEXEC BROWSE DATASET('&DSPRFX..&DSYS..&TSYS..ABRSUM')
IF &LISTOPT EQ &STR() THEN +
  DO
    GOTO CONTIN
  END
IF &ACLA EQ &STR(X) THEN +
  DO
    SET &HOLD EQ &STR(HOLD KEEP)
  END
ELSE +
  DO
    SET &HOLD EQ &STR()
  END
FREE F(SYSIN,SYSPRINT,SYSUT1,SYSUT2)
ALLOC F(SYSUT1) DA('&DSPRFX..&DSYS..&TSYS..ABRSUM')
ALLOC F(SYSUT2) SYS(&ACLA) &HOLD
ALLOC F(SYSIN) DUMMY
ALLOC F(SYSPRINT) DUMMY
$ IEBGENER
FREE F(SYSIN,SYSPRINT,SYSUT1,SYSUT2)
ALLOC F(SYSIN) DA(*)
ALLOC F(SYSPRINT) DA(*)
ALLOC F(SYSUT1) UNIT(VIO) SPACE(10 2) CYLINDERS
ALLOC F(SYSUT2) UNIT(VIO) SPACE(10 2) CYLINDERS
CONTIN: +
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
DELETE '&DSPRFX..&DSYS..&TSYS..ABRSUM'
FREE F(SYSIN,SYSOUT,ABRSUM,SYSPRINT)
FREE F(FB80,FBA121)
IF &DEBUG EQ &STR() THEN +
  CONTROL MSG
GOTO RESTART
END
