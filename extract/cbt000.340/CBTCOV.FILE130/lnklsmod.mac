IEAVNP03 CSECT
* THIS IS A MODIFICATION TO MODULE IEAVNP03 TO RECOGNIZE A DATASET
* IN THE LINKLIST THAT IS EITHER NOT CATALOGUED OR IS CATALOGUED
* BUT NOT IN THE MVS MASTER CATALOG.
* THIS MOD IS AT THE 8107 LEVEL OF MVS/SP1.1
*
*
* USE:  PLACE THE VOLSER OF THE DATASET IN PARENTHESIS IMMEDIATELY
*       FOLLOWING THE DATA SET NAME
*
*  LINKLIST CARD EXAMPLE:
*
*       SYSTEMS.LINKLIB(RES012)
*
* THIS CODE IS INTENDED TO BE ASSEMBLED AND THEN PASSED THROUGH
* AN ASSMBLER-TO-ZAP TRANSLATOR LIKE THE YALE ASMTOZAP PROGRAM
*
@0       EQU   0
@1       EQU   1
@2       EQU   2
@3       EQU   3
@4       EQU   4
@5       EQU   5
@6       EQU   6
@7       EQU   7
@8       EQU   8
@9       EQU   9
@10      EQU   10
@11      EQU   11
@12      EQU   12
@13      EQU   13
@14      EQU   14
@15      EQU   15
SCANREG  EQU   8
LNKVOLID EQU   X'2D'
         SPACE 2
*ZAP CARD ++USERMOD(MS00100).
*ZAP CARD ++VER(Z038) FMID(EBB1102).
*ZAP CARD ++ZAP(IEAVNP03) DISTLIB(AOSC5).
*ZAP CARD   NAME         IEAVNP03 IEAVNP03
*ZAP CARD   IDRDATA      MS00100
         SPACE 3
         ORG   IEAVNP03+X'26'
@PSTART  EQU   *
         USING @PSTART,@11
         USING @PSTART+4095,@12
         USING @PSTART+8190,@13
         SPACE 1
         ORG   IEAVNP03+X'1D40'
LCNAMPTR EQU   *
         ORG   IEAVNP03+X'CDA'
*ZAP START VER
NP3LCLOC L     @7,LCNAMPTR
         ORG   IEAVNP03+X'CFE'
NP3LCLXT LA    SCANREG,51(,SCANREG)
         ORG   IEAVNP03+X'CDA'
*ZAP START REP
         B     PATCHLOC
         ORG   IEAVNP03+X'2768'
PATCHLOC LA    @15,36
SCANLPAR LA    @7,1(@15,SCANREG)
         CLI   0(@7),C'('
         BE    FOUND
         BCT   @15,SCANLPAR
         B     NOTFOUND
         SPACE 1
FOUND    CLI   7(@7),C')'
         BE    MOVEVOL
NOTFOUND L     @7,LCNAMPTR
         B     NP3LCLOC+4
         SPACE 1
MOVEVOL  MVC   LNKVOLID(6,SCANREG),1(@7)
         MVI   0(@7),C' '
         MVC   1(7,@7),0(@7)
         SLR   @15,@15
         B     NP3LCLXT
*ZAP END
         END
