SADUMP   START
         REG
         XSAVE  R12,SVA,SADUMP
         ST    R1,SAVE1
         L     R15,16
         TM    116(R15),X'01'
         BNO   NOMVS
         MVI   EPLOC,C'A'
         MVC   ONGO,=CL80' O SUMMARY,F,L,Q,CVTMAP,CPUDATA,ASMDATA,P N,C*
               ,SQA,CSA'
         MVI   MVSONGO+1,X'00'    NOP BRANCH
NOMVS    EQU   *
NEXT     EQU   *
         XC    ECB1,ECB1
         MVC   VOLSER,BLANK
         WTOR  'PDUMP01 ENTER TAPE VOL.-SER. OR END FOR SA-DUMP',      *
               VOLSER,6,ECB1,ROUTCDE=2
         WAIT  ECB=ECB1
         OC    VOLSER,BLANK
         CLC   VOLSER(3),=C'END'
         BE    RETURN
         MVC   TITLE+24(6),VOLSER
DEFAULT  EQU   *
         XC    ECB1,ECB1
         MVC   JFCB(3),BLANK
         WTOR  'PDUMP02 ENTER 3 CHAR DUMP-NO., ''PARM'' OR ''RESET''', *
               JFCB,5,ECB1,ROUTCDE=2
         WAIT  ECB=ECB1
         OC    JFCB(5),BLANK
         CLC   JFCB(5),=C'RESET'
         BE    NEXT
         CLC   JFCB(4),=C'PARM'
         BE    PARM
         OI    SWITCH,X'80'
         MVC   TITLE+11(3),JFCB
         XC    ECB1,ECB1
         WTOR  'PDUMP03 ENTER DUMP-TITLE, MAX. 33 CHAR.',TITLE+32,33,  *
               ECB1,ROUTCDE=2
GOON     EQU   *
         RDJFCB TAPE
         MVC   JFCB+118(6),VOLSER
         OI    JFCB+52,X'46'
         NI    JFCB+52,X'F7'
         OI    JFCB+76,X'80'
         OPEN  (TAPE),TYPE=J
         CLOSE (TAPE,REREAD)
         FREEPOOL TAPE
         TM    SWITCH,X'80'
         BNO   ATTACH
         WAIT  ECB=ECB1
         OC    TITLE+32(33),BLANK
         OPEN  (SINDCB,OUTPUT)
         PUT   SINDCB,TITLE
MVSONGO  B     SKIPONGO
         PUT   SINDCB,ONGO2
         PUT   SINDCB,GO
SKIPONGO EQU   *
         PUT   SINDCB,ONGO
         PUT   SINDCB,GO
         PUT   SINDCB,END
ATTACH   EQU   *
         CLOSE (SINDCB)
         FREEPOOL SINDCB
         L     R1,SAVE1
         XC    ECB1,ECB1
         ATTACH EPLOC=EPLOC,ECB=ECB1
         ST    R1,TCB
         WAIT  ECB=ECB1
         DETACH TCB
RETURN   EQU   *
         XRETURN 0
PARM     EQU   *
         OPEN  (SINDCB,OUTPUT)
NXTPRM   EQU   *
         MVC   BUF,BLANK
         XC    ECB1,ECB1
         WTOR  'PDUMP04 ENTER NEXT PARM, ''RESET'', ''DEFAULT'' OR ''EX*
               IT''',BUF,80,ECB1,ROUTCDE=2
         WAIT  ECB=ECB1
         OC    BUF,BLANK
         CLC   BUF(5),=C'RESET'
         BNE   TSTXIT
         CLOSE (SINDCB)
         WTO   'PDUMP05 ALL INPUT UP TO HERE HAS BEEN NULLIFIED',      *
               ROUTCDE=2
         B     PARM
TSTXIT   EQU   *
         CLC   BUF(4),=C'EXIT'
         BE    GOON
         CLC   BUF(7),=C'DEFAULT'
         BNE   PROCPRM
         CLOSE (SINDCB)
         FREEPOOL SINDCB
         MVI   SWITCH,X'00'
         B     DEFAULT
PROCPRM  EQU   *
         PUT   SINDCB,BUF
         B     NXTPRM
TCB      DS    F
SAVE1    DS    F
SINDCB   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(PM)
TAPE     DCB   DSORG=PS,MACRF=(GL),DDNAME=TAPE,RECFM=F,BLKSIZE=1,      *
               LRECL=1,BUFNO=1,BUFL=1,EXLST=DCBEX
BLANK    DC    CL80' '
BUF      DC    CL80' '
JFCB     DC    CL176' '
ECB1     DC    F'0'
EPLOC    DC    CL8'IMDPRDMP'
DCBEX    DC    X'87'
         DC    AL3(JFCB)
VOLSER   DC    CL6' '
TITLE   DC     CL80' T DUMP-NO XXX, TAPE-NO 123456, '
ONGO     DC    CL80' ONGO F,L,Q,P N,C'
ONGO2    DC    CL80' ONGO RSMDATA,TRCDATA,LOGDATA'
GO       DC    CL80' GO'
END      DC    CL80' END'
SWITCH   DC    X'00'
         END
