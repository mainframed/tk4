* STAND 05/20/81
**********************************************
*
*   PROGRAM   TXTPARM
*
*   AUFRUF    CALL TXTPARM,(ARGLIST,CALL)
*
*   A         ARGLIST = ADRESSE DER ARGUMENTENLISTE, DIE
*                       VOM ASTTXT AUFRUEFENDEM PROGRAMM
*                       UEBERGEBEN WIRD
*
*   CL1       CALL    = X'03' CALL ASTTXT
*                       X'04' CALL ASTTXTOP
*
*   ZWECK      MERKEN DER VARIABLEN TEXTE
*              ATXTKZ  = 0  ABSTIMMBLATT MIT TEXTEN DRUCKEN
*                      > 0  NUR TEXTE DRUCKEN
*                      < 0  WIE > 0
*                 BEI UNGLEICH 0 WIRD CONDCODE 16 GESETZT
*
*
******************************************************
TXTPARM  CSECT
         XSAVE 12,SAVE,TXTPARM
         AREG
         L     R10,PATAB      -->ATAB
         USING DATAB,R10
         L     R2,4(R1)       -->CALL
         L     R1,0(R1)
         L     R11,0(R1)      -->TEXTPOINTER
         SR    R15,R15
*
*  TXTKZ MERKEN
*
ANFANG   EQU   *
         TM    0(R11),X'80'
         BO    ENDE
         L     R3,0(R11)      -->TXTKZ IN CALL
         CLC   ATXTP,=F'0'
         BNE   A050           NICHT 1.AUFRUF
         MVC   ATXTKZ,=H'0'   INITIALISIEREN
A050     EQU   *
*  KZ NUR UEBERNEHMEN, WENN CONDCODE GESETZT WERDEN SOLL
         CLC   0(2,R3),=H'0'
         BE    A060
         MVC   ATXTKZ,0(R3)
         B     A100
         SPACE 1
*  FALLS CALL ASTTXT IMMER CONDCODE SETZEN
*
A060     EQU   *
         CLI   0(R2),3
         BNE   A100           CALL ASTTXTOP
         MVC   ATXTKZ,=H'-1'    D.H. CONCODE SETZEN
         SPACE 1
A100     EQU   *
         CLC   ATXTP,=F'0'
         BNE   A200           PLATZ VORHANDEN
         SPACE 2
*  PLATZ FUER TEXTE HOLEN UND INITIALISIEREN
         SPACE 1
         GETMAIN EC,LV=TXTLN,A=WORD
         LTR   R15,R15
         BNZ   FEHLER         GETMAIN NICHT OK
         L     R1,WORD        -->PLATZ FUER TEXTE
         ST    R1,ATXTP       IN ATAB MERKEN
         LA    R3,0           ANZ TEXTE
         LA    R4,0           ANZ TEXTE UNGLEICH NULL
         LA    R5,8(R1)       -->FREIER PLATZ
         B     A300
         SPACE 2
*  PLATZ SCHON ZUGEORDNET
         SPACE 1
A200     EQU   *
         L     R1,ATXTP       -->TEXTE
         LH    R3,0(R1)       ANZ TEXTE
         LH    R4,2(R1)       ANZ TEXTE UNGLEICH NULL
         L     R5,4(R1)       -->FREIER PLATZ
         SPACE 2
A300     EQU   *
         LA    R11,4(R11)
         SPACE 2
*  TEXTE UMSPEICHERN
         SPACE 1
LOOP     EQU   *
*  PRUEFUNG, OB MAX.ANZAHL TEXTE UMGESPEICHERT
         LA    R0,TXTMAX
         CR    R0,R3
         BNH   VOLL           KEIN PLATZ MEHR
         SPACE 1
         L     R6,0(R11)      -->VON
         MVC   0(50,R5),0(R6) NACH
         LA    R3,1(R3)       ANZ.TEXTE
         CLC   0(50,R5),=CL50' '
         BE    LOOP010
         LA    R4,1(R4)       ANZ.TEXTE UNGLEICH NULL
LOOP010  EQU   *
         LA    R5,50(R5)      -->FREIER PLATZ
         TM    0(R11),X'80'   LETZTER TEXT
         BO    LOOPE          JA
         LA    R11,4(R11)
         B     LOOP
         SPACE 2
*  VORSPANN VOR TEXTEN AUF NEUE WERTE SETZEN
         SPACE 1
LOOPE    EQU   *
         STH   R3,0(R1)       ANZ TEXTE
         STH   R4,2(R1)       ANZ TEXTE UNGLEICH NULL
         ST    R5,4(R1)       -->FREIER PLATZ
         SPACE 2
ENDE     EQU   *
         XRETURN (R15)
         SPACE 2
* GETMAIN NICHT OK
FEHLER   EQU   *
         LA    R15,12
         B     ENDE
         SPACE 2
*  KEIN PLATZ MEHR FUER TEXTE
VOLL     EQU   *
         LA    R15,12
         B     LOOPE
*
PATAB    DC    V(ATAB)
WORD     DS    F
TXTMAX   EQU   20
TXTLN    EQU   50*TXTMAX+8
         LTORG
         MATAB
         END
