SORT     TITLE 'SORTIERUNG SPEICHER-INTERN (NACH WDSORT)'
*
*        CALL  'LUSORT'   USING TABELLE, PARAMETER
*
*
*
LUSORT   CSECT
B        EQU   2
RANF     EQU   3
REND     EQU   4
WSR      EQU   5
K        EQU   8
RLNG     EQU   9
AK       EQU   10
CNT      EQU   11
         XSAVE (12),,LUSORT
         LM    2,3,0(1)      R.2 = BLOCK-ANFANG  = B
         LH    RLNG,2(3)     RLNG = FELD-LAENGE
         CH    RLNG,=H'256'
         BH    ZULANG
         LA    4,2     R4=2
         LH    8,0(3)  R8=NO OF FIELDS TO BE SORTED
         CR    4,8     R8=0 OR 1 ?
         BH    WTP     YES IF HIGH
         LR    REND,RLNG
         MH    REND,0(3)     PARMM (1)= ANZAHL DER SAETZE
         AR    REND,B        REND = B + FLD-LNG * FLD-ANZ
         SR    REND,RLNG     REND = ADR. OF LAST RECORD
         ST    REND,A        A(0) = REND
         SR    K,K           K = 0
         LR    CNT,RLNG
         BCTR  CNT,0
NXTKEY   LH    4,4(3)        R.4  = PARAM (3)
         BCTR  4,0           R.4  = DISPLACEMENT OF SORT KEY
         LH    7,6(3)        R.7  = PARAM (4)
         LA    5,X'81'
         LTR   7,7           ASCENDING GIBT  BZ/BO
         BNM   *+8
         LA    5,X'18'       DESCENDING GIBT BO/BZ
         LA    6,X'80'
STBIT    STC   6,DISPL+1(K)
         STC   5,DISPL(K)
         STH   4,DISPL+2(K)
         LA    K,4(,K)       K = K + 1
         SRA   6,1
         BNZ   STBIT
         LA    4,1(4)
         CH    4,8(3)        PARAM (5) =  FELD-ENDE
         BE    FLDENDE
         CH    K,=H'2048'
         BL    STBIT-4
         WTO   'FEHLER IM UNTERPROGRAMM LUSORT. SORTIERBEGRIFFE SIND ZU*
                LANG',ROUTCDE=11
         ABEND 888
ZULANG   EQU   *
         WTO   'FEHLER IM UNTERPROGRAMM LUSORT. FELDLAENGE IST ZU GROSS*
               ',ROUTCDE=11
         ABEND 999
*
FLDENDE  CLC   10(2,3),=H'0'
         BE    LASTKEY
         LA    3,6(3)
         B     NXTKEY
*                  VARIABLE BRANCH-INSTR.
IFZERO   BZ    FINDONE
IFONE    BO    FINDZERO
*                  INITIALIZE NEXT SEARCH PHASE
LASTKEY  ST    K,KEND
         SR    K,K
NEXTBIT  C     K,KEND             AK = POINTER IN DISPL-TABLE
         BNL   LASTBIT
         LA    AK,DISPL(K)        IF END OF A-TABLE
         LR    RANF,B
         L     REND,A(K)          REND = A(K)
         SR    RANF,RLNG          RANF = B - 1
         MVZ   IFZERO+1(1),0(AK)  SET CONDITION MASK
         MVO   IFONE+1(1),0(1,AK)
*                  MODIFY TM-INSTRUCTIONS
         MVC   TEST0+1(3),1(AK)   SET DISPLACEMENT
         MVC   TEST1+1(3),1(AK)     AND MASK-BITS
         OI    TEST0+2,REND*16    SET BASE-REGISTER
         OI    TEST1+2,RANF*16
         B     FINDZERO+2
LASTBIT  L     B,A(K)        B = A(K) + 1
         AR    B,RLNG
         L     WSR,A-4(K)
         ST    WSR,A(K)      A(K) = A(K-1)
         CR    B,WSR         B = A(K)
         BL    LASTBIT
         BE    KSUBTR
         LR    B,WSR         B = A(K)
KSUBTR   SH    K,=H'4'       K = K - 1
         B     IFDONE
*                  0-BIT  SUCHEN   ABWAERTS
FINDZERO SR    REND,RLNG     EIN FELD ZURUECK
         CR    REND,RANF
         BNH   GRUPEND
         EX    0,TEST0
         EX    0,IFONE
*                  1-BIT SUCHEN   AUFWAERTS
FINDONE  AR    RANF,RLNG     EIN FELD VORAN
         CR    RANF,REND
         BNL   GRUPEND
         EX    0,TEST1
         EX    0,IFZERO
*                  FELDER VERTAUSCHEN
XCHANGE  EX    CNT,MOVE1
         EX    CNT,MOVE2
         EX    CNT,MOVE3
         B     FINDZERO
*
TEST0    TM    0(REND),X'00'
TEST1    TM    0(RANF),X'00'
MOVE1    MVC   ZWISP(0),0(REND)
MOVE2    MVC   0(0,REND),0(RANF)
MOVE3    MVC   0(0,RANF),ZWISP
*                  RANF = END OF 1ST SUBGROUP
GRUPEND  ST    RANF,A+4(K)   A(K+1) =  D
         CR    RANF,B
         BL    LOWB          IF  D  =  B
         BE    EQUB
HIGHB    LA    K,4(,K)       K = K + 1
         B     NEXTBIT
*                  NUR 1-BITS GEFUNDEN
LOWB     L     RANF,A(K)
         B     GRUPEND
*                  GENAU EIN 0-BIT GEFUNDEN
EQUB     AR    B,RLNG        B = B + 1
         L     WSR,A(K)
         ST    WSR,A+4(K)    A(K+1) = A(K)
         CR    WSR,B
         BH    HIGHB
         LTR   K,K
IFDONE   BZ    DONE
         L     WSR,A-4(K)
         ST    WSR,A(K)
         CR    B,WSR         IF B = A(K)
         BE    KSUBTR
         AR    B,RLNG        B = B + 1
         CR    B,WSR
         BL    NEXTBIT
         B     KSUBTR
*
WTP      WTO   'LUSORT: ANZAHL DER TABELLENELEMENTE KLEINER ALS 2, AUFR*
               UF IGNORIERT.',ROUTCDE=11
DONE     XRETURN ,R
         LTORG
KEND     DS   F
ZWISP    DS    CL256
DISPL    DS    512F
A        DS    512F
         END
