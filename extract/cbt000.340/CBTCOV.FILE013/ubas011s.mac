         TITLE '*** KEYSCAN ***  GENERAL INFORMATION.'
* THIS PROGRAM ANALYSES A HEADER RECORD AND BUILDS UP
* A TABLE WHICH ADDRESS IS PASSED IN REGISTER 1.
* IT IS USED AS A SUBPROGRAM BY  :
*        CALL  KEYSCAN,(HEADER)
         SPACE 2
* FORM OF HEADER :  (ON A HALFWORD BOUNDARY)
         SPACE 1
* H1     DC    H'XX,0'   XX = LENGTH OF HEADER INCLUDING
*                             2 BYTES LENGTH, 2 BYTES ZEROES
*        DC    C'KEY1=PARM1,KEY2=PARM2,.....'
         SPACE 2
* FORM OF TABLE :   (ON A DOUBLEWORD BOUNDARY)
         SPACE 1
*        LLLL       LENGTH OF TABLE    (HEX)
*        LL         LENGTH OF KEYWORD  (HEX)
*        CC...      KEYWORD            (CHAR)
*        X'40'      ONLY IF KEYLENGTH WAS EVEN, OTHERWISE MISSING
*        YYYY       OFFSET OF KEYWORD IN HEADER  (HEX)
*        ZZZZ       LENGTH OF PARM    IN HEADER  (HEX)
*        ....
*        X'FFFF'    TABLE END SIGN
         SPACE 3
* EXAMPLE OF USE
         SPACE 2
* H1     DC    H'31,0'
*        DC    CL27'Q=ABC,D=''X=''Z'',F,R=(80,10)'
         SPACE 1
* AFTER CALLING KEYSCAN R1 POINTS AT THE FOLLOWING TABLE :
* X'001C01D9 00160007 01C60014 000001C4 000A0007 01D80004 0003FFFF'
         SPACE 3
* WHILE CREATING A HEADER THE USER SHOULD FOLLOW THE RULES OF THE JCL
* THE FOLLOWING RECORDS WOULD BE INCORRECT :
         SPACE 1
* X'00030000' C'X=1'                         WRONG LENGTH
* X'00120000' C'AB=(80,5)),B=C'              WRONG USE OF APOSTROPHEES
* X'00090000' C'R=A=4'                       = NOT INSIDE APOSTROPHEES
* X'00080000' C' X=1'                        1ST CHARACTER IS BLANK
*                   (A BLANK IS ALWAYS INTERPRETED AS END OF RECORD)
         SPACE 3
* AFTER RETURN REGISTER 15 SHOULD CONTAIN ZEROES :  ALL OK
* IF REGISTER 15 CONTAINS 4, AN ERROR WAS DETECTED
         TITLE '*** KEYSCAN ***   PROGRAM CHANGES.....'
         SPACE 15
*   SEVERAL CHANGES HAVE BEEN DONE
         SPACE 2
*   1. EVEN NUMBER OF APOSTROPHES WITHIN KEYWORD ALLOWED
*   2. BLANKS - ENCLOSED IN APOSTROPHES - ARE ALLOWED WITHIN KEYWORD
*      AND PARAMETER
*   3. COMMA AS LAST SIGN BEHIND LAST OPERAND ALLOWED
*                  (ON A VERY SPECIAL REQUEST OF H SOLTAU)
*   4. A KEYWORD AS   XXX=,   IS TREATED AS   XXX,
         TITLE '*** KEYSCAN ***  EQUATE SUYMBOLS, REGISTER USE'
KEYSCAN  CSECT
         SPACE 2
* EQUATE SYMBOLS
*        USE OF REGISTERS
         SPACE 1
* REG 12 USED AS BASE REGISTER FOR HDRSCANV
* REG 13 USED AS BASE REGISTER FOR DSECT
* REG 11 USED AS COUNTER FOR PARENTHESIS
* REG 9  USED AS COUNTER FOR NOAPS
* REG 5  USED AS VARIABLE LENGTH FOR TRT BY EX
         SPACE 1
H        EQU   3         POINTER THROUGH HEADER
T        EQU   4         POINTER THROUGH TABLE
W1       EQU   6         WORK REGISTER 1
W2       EQU   7         WORK REGISTER 2
W3       EQU   8         WORK REGISTER 3
LL       EQU   10        TABLE LENGTH
         SPACE 1
*        EXPLANATION OF BITS SET IN SW
         SPACE 1
PAR      EQU   X'01'     ON WHILE SCANNING A PARAMETER OF THE HEADER
KEY      EQU   X'02'     ON WHILE SCANNING A KEYWORD   OF THE HEADER
LPA      EQU   X'04'     ON WHEN LEFT PARENTHESIS OCCURED
RPA      EQU   X'08'     ON WHEN RIGHT PARENTHESIS OCCURED
FIN      EQU   X'10'     ON WHEN END OF HEADER WAS FOUND
FND      EQU   X'20'     ON WHEN AT LEAST ONE KEYWORD FOUND
EQ       EQU   X'40'     ON WHEN     KEY=,
         TITLE '*** KEYSCAN ***  CHAINING SAVE AREAS, START OF PROGRAM'
         XSAVE 12,,KEYSCAN,DUMEND-DUM
         USING DUM,13
         L     H,0(1)              LOAD HDR LENGTH ADDRESS
         LH    W1,0(H)             LOAD LENGTH
         SH    W1,=H'4'            IT MUST BE > 4, ELSE ERROR
         BNP   ERROR0
         ST    H,HSA               SAVE HDR START ADDRESS
         LA    H,4(H)              SET H ON 1ST KEYWORD
         ST    H,KEYADDR           SAVE H AS ADDR OF 1ST KEYWORD
         CLI   0(H),C' '           1ST CHAR BLANK ?
         BE    ERROR0
         CLI   0(H),C'='
         BE    ERROR0
         LR    5,H
         AR    5,W1                FIND END OF HDR + 1
         BCTR  5,0                 (EX)
         ST    5,HEA               SAVE HEADER END ADDRESS
         MVI   SW,X'00'            CLEAR SWITCH
         OI    SW,KEY
         SPACE 2
         LH    T,=H'800'
         GETMAIN R,LV=(T)  GET MAX OF STORAGE THAT COULD BE REQUESTED
         ST    1,TABSTART          SAVE TABLE START ADDRESS
         AR    T,1                 FIND END OF TABLE
         SH    T,=H'2'             DECREASE T BY 2
         LA    LL,2                LL = 2
         MVC   0(2,T),=X'FFFF'     SET TABLE END SIGN
         XC    NOAP,NOAP           CLEAR NO. OF APOSTROPHEES
         SR    2,2                 CLEAR REG2 (BECAUSE OF TRT)
         SR    9,9                 CLEAP COUNTER FOR NOAP
         SR    11,11               CLEAR COUNTER FOR PARENTHESES
         TITLE '*** KEYSCAN ***  ANALYSING HEADER'
TRTSTART L     5,HEA               FIND LENGTH FOR TRT INSTRUCTION
         SR    5,H                 SUBTRACT TRT- START ADDR
         LTR   5,5
         BM    SETR1               IF MINUS, END OF HEADER FOUND
         EX    5,TRT              FIND NEXT BYTE OF TRTTAB NOT ZERO
         BZ    SETR1               NOTHING LEFT
         CLI   0(1),C'='
         BE    EQSIGN              T E S T
         CLI   0(1),C','               F O U N D
         BE    COMMA                       B Y T E
         CLI   0(1),C' '
         BE    BLANK
         CLI   0(1),C''''
         BE    APOS
         CLI   0(1),C'('
         BE    LEFTPAR
         B     RIGPAR
         SPACE 2
EQSIGN   TM    NOAP+1,X'01'        = INSIDE APOSTROPHEES
         BO    SETH                IF YES SKIP SAVING KEY
         TM    SW,PAR              PROCESSING IN PARM STATE ?
         BO    ERROR               IF YES :  ERROR
         CLI   1(1),C','           PROVIDE   KEY1=,KEY2=,   ...
         BNE   SETPAR
         OI    SW,EQ               LENGTH WILL BE 1 TOO HIGH
         B     SAVE1
SETPAR   EQU   *
         OI    SW,PAR              SET PARM BIT
         NI    SW,255-KEY          CLEAR KEY BIT
         SPACE 1
SAVE1    EQU   *
         ST    1,ADDREQ            SAVE ADDR OF EQ SIGN
         SR    1,H
         STH   1,KEYLEN            SAVE LENGTH OF THIS KEYWORD
         LA    H,1(1,H)            SET H ON FOUND ADDR + 1
         B     TRTSTART
         SPACE 1
APOS     LA    9,1(9)              APOSTROPH  FOUND, COUNTER + 1
         STH   9,NOAP              SAVE NUMBER
         B     SETH
         SPACE 1
LEFTPAR  EQU   *
         TM    NOAP+1,X'01'        IF NOAP UNEVEN LEFT PAREN
         BO    SETH                MAY BE CHAR INSIDE APOSTROPHEES
         LTR   11,11               TEST IF ( HAD OCCURED EARLIER
         BNZ   ADD1                IF YES SKIP NEXT TEST
         LR    W1,1
         BCTR  W1,0
         CLI   0(W1),C'='
         BNE   ERROR
         OI    SW,LPA              SET SW FOR LEFT PAREN
ADD1     LA    11,1(11)            R11 + 1
         B     SETH
         SPACE 1
RIGPAR   EQU   *
         TM    NOAP+1,X'01'        IF NOAP UNEVEN RIGHT PAREN
         BO    SETH                MAY BE INSIDE APOSTROPHEES
         TM    SW,LPA              IF NOT THERE MUST HAVE BEEN
         BZ    ERROR               A LEFT PAREN, ELSE :  ERROR
         OI    SW,RPA              SET RIGHT PAREN BIT
         BCTR  11,0                R11 - 1
         LTR   11,11               PROCESSING STILL INSIDE PARENS ?
         BNZ   SETH
         L     W1,HEA
         SR    W1,1                TEST, IF ) IS FOLLOWED BY ,
         BZ    *+12
         CLI   1(1),C','
         BNE   ERROR
         NI    SW,255-LPA          CLEAR LPA BIT
         SPACE 2
SETH     LA    H,1(1)              SET H
         TM    SW,FIN
         BO    ERROR
         B     TRTSTART
         SPACE 2
BLANK    EQU   *
         TM    NOAP+1,X'01'
         BO    SETH
         B     SETLAST
         SPACE 2
         TITLE '*** KEYSCAN *** END OF 1 PARM, FILLING TABLE '
SETR1    L     1,HEA               MAKE R1 POINT AT HEA + 1
         LA    1,1(1)
SETLAST  OI    SW,FIN              SET END OF HEADER BIT
COMMA    TM    SW,LPA              COMMA FOUND. END OF ONE PARM, IF
         BO    SETH                 IT IS NOT INSIDE OF PARENS
         TM    NOAP+1,X'01' NO OF APOS. IN PARM FIELD MUST BE EVEN
         BO    SETH
         LTR   11,11               TEST IF NO. OF ( WAS EQUAL TO NO. OF
         BNZ   ERROR
         LA    H,1(1)              SET H
         LR    W1,1                TEST IF ) WAS JUST BEFORE
         BCTR  W1,0                  COMMA
         CLI   0(W1),C','          TWO COMMAS ?
         BE    SVKEYAD             IF YES :  IGNORE ONE
         L     W2,HSA              WAS 1ST SIGN A COMMA ?
         LA    W2,4(W2)            IF YES IGNORE IT
         CR    W2,1                SAME ADDR LIKE HSA + 4 ?
         BE    SVKEYAD
         TM    SW,RPA              WAS ) IN PARM ?
         BZ    GETOFF              IF NO SKIP NEXT STATEMENTS
         TM    SW,FIN
         BO    GETOFF
         CLI   0(W1),C')'          IF NOT: ERROR
         BNE   ERROR
GETOFF   EQU   *                   GET OFFSET
         L     W1,KEYADDR          LOAD ADDR OF KEYWORD
         S     W1,HSA              SUBTRACT HEADER START ADDR
         STH   W1,OFF              SAVE DIFFERENCE AS OFFSET
         TM    SW,KEY              PROCESSING STILL IN KEY STATE ?
         BO    NOPAR
         S     1,ADDREQ            R1 = LENGTH OF PARM
         BCTR  1,0                 OFF = OFFSET KEYWORD IN TABLE
         B     FILTAB
NOPAR    EQU   *                   HEADER WAS : ...,KEY,... (NO PARM)
         S      1,KEYADDR
         TM    SW,EQ               WAS   KEY=,    ?
         BZ    *+6                 IF ZERO IT WAS   KEY,
         BCTR  1,0                 KEYLENGTH - 1
         STH   1,KEYLEN
         SR    1,1                 PARM LENGTH IS ZERO
FILTAB   EQU   *
         TM    KEYLEN+1,X'01'      KEYLEN EVEN ?
         BO    *+10                --- IF NO SKIP
         BCTR  T,0                   ×  NEXT     STATEMENT
         LA    LL,1(LL)              ×
         SH    T,KEYLEN            <--  DECREASE T BY KEYLEN
         SH    T,=H'5'             2 OFFSET, 2 LEN PARM, 1 LEN KEY
         AH    LL,KEYLEN           ADD KEYLENGTH TO TABLE LENGTH
         LA    LL,5(LL)
         L     W1,KEYADDR          SET W1 ON KEYWORD
         LH    W3,KEYLEN           LOAD IT'S LENGTH
         STC   W3,0(T)             STORE IT INTO 1ST BYTE
         BCTR  W3,0                W3 = W3 - 1 (BECAUSE OF EX)
         EX    W3,MOVEKEY          MOVE KEYWORD INTO TABLE
         OI    SW,FND              AT LEAST ONE FOUND
         TM    KEYLEN+1,X'01'      WERE NO. OF CHAR EVEN ?
         BO    NOBLANK             ---  IF NO SKIP
         LR    W1,W3                 ×  FILLING ONE BYTE WITH A BLANK
         AR    W1,T                  ×
         MVI   2(W1),X'40'           ×
         LA    W3,1(W3)              ×
NOBLANK  LH    W1,OFF              <--
         STH   W1,2(W3,T)          STORE OFFSET INTO TABLE
         STH   1,4(W3,T)           STORE LENGTH INTO TABLE
SVKEYAD  ST    H,KEYADDR           SAVE ADDR OF  KEYWORD
         SPACE 1
         XC    NOAP,NOAP
         SR    9,9                 CLEAR COUNTER FOR NOAPS
         NI    SW,255-LPA-RPA-PAR-EQ   CLEAR BITS
         OI    SW,KEY              NOW AGAIN A KEYWORD IS TO BE FOUND
         TM    SW,FIN              WAS LAST PARM ?
         BZ    TRTSTART            IF NO, TRY TO FIND NEXT
         TM    SW,FND              ONLY COMMAS FOUND ?
         BZ    ERROR               YES IF ZERO
         SH    T,=H'2'             DECREASE T BY 2
         LA    LL,2(LL)            LL + 2
         STH   LL,0(T)             SAVE IT AT BEGINNING OF TABLE
         SPACE 2
         STH   T,SHIFT             SAVE RIGHT HALF OF TABLE ADDRESS
         MVI   SHIFT,X'00'
         NI    SHIFT+1,X'07'       GET   0000 0XXX
         SH    T,SHIFT             SET T AT A DOUBLEWORD BOUNDARY
         LR    W1,T
         MVC   MOVETAB(6),MVC
         MVC   MOVETAB+5(1),SHIFT+1    STORE DIFFERENCE INTO MOVETAB
SHIFTTAB EX    0,MOVETAB
         LA    W1,1(W1)
         BCT   LL,SHIFTTAB         SHIFT LEFT WHOLE TABLE
         SPACE 1
         SR    15,15               CLEAR REG 15, ALL OK
GETBACK  EQU   *
         L     1,TABSTART          LA OF 1ST GETMAINED BYTE
         LA    T,0(T)              CLEAR HIGH ORDER BYTE
         LR    0,T                 LOAD TABLE ADDR
         SR    0,1                 GET DIFFERENCE
         FREEMAIN R,LV=(0),A=(1)   FREE UNUSED AREA
         L     1,4(0,13)          GET HSA ADDR
         ST    T,24(0,1)     STORE TAB ADDR INTO SA AS REG 1
XRET     XRETURN (15),R
ERROR0   LA    15,4                ERROR, BUT NO FREMAIN
         B     XRET
ERROR    LA    15,4                ERROR FOUND. SET REG 15 ON 4
         B     GETBACK
         TITLE '*** KEYSCAN ***  TRANSLATE AND TEST TABLE, DSECT'
TRTTAB   DC    256X'00'
         ORG   TRTTAB+64
         DC    C' '                BLANK
         ORG   TRTTAB+77
         DC    C'('                LEFT PAREN  (
         ORG   TRTTAB+93
         DC    C')'                RIGHT PAREN )
         ORG   TRTTAB+107
         DC    C','                COMMA       ,
         ORG   TRTTAB+125
         DC    C'''='              APOSTROPHEE '  EQUAL SIGN =
         ORG   TRTTAB+256
         SPACE 2
MVC      MVC   0(1,W1),0(W1)
MOVEKEY  MVC   1(0,T),0(W1)        MOVE KEYWORD INTO TABLE
TRT      TRT   0(0,H),TRTTAB       FIND NEXT BYTE IN TRTTAB NOT ZERO
         LTORG
         SPACE 2
DUM      DSECT
SAVEAREA DS    18F
HSA      DS    F                   HEADER START ADDRESS
HEA      DS    F                   HEADER END ADDRESS
TABSTART DS    F                   ADDR OF 1ST GETMAINED BYTE
ADDREQ   DS    F                   ADDR OF = BEHIND KEYWORD
KEYADDR  DS    F                   ADDR OF KEYWORD
OFF      DS    H                   OFFSET OF PARM IN HEADER
NOAP     DS    H                   NO. OF APOSTROPHEES
KEYLEN   DS    H                   LENGTH OF KEYWORD
SHIFT    DS    H                   USED FOR SHIFTING TABLE
MOVETAB  MVC   0(1,W1),0(W1)
SW       DS    CL1
DUMEND   EQU   *
         END
