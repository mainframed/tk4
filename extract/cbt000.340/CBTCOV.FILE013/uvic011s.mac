********************************************************************
*                                                                  *
*  DAS PROGRAMM  SYHA171  LISTET SYSCTLG-ENTRIES. IM GEGENSATZ     *
*  ZUM IBM-LISTCTLG WERDEN HIER AUCH GIPE-ENTRIES UND INDEX-       *
*  POINTER-STRUKTUREN GELISTET, ZU DENEN KEIN DSPE MEHR GEHOERT.   *
*                                                                  *
*  JCL :                                                           *
*                                                                  *
*  //STEPXX  EXEC PGM=SYHA171,                                     *
*  //             PARM=GIPE                                        *
*  //SYHA171A DD  DSN=SYSCTLG,DISP=SHR,                            *
*  //             UNIT=SYSDA,VOL=SER=......                        *
*  //SYHA171B DD  SYSOUT=U                                         *
*  //SYHA171C DD  *                                                *
*          AL.LA                                                   *
*   GR                                                             *
*  //SYHA171D DD  DSN=  .... DATEI ...                             *
*  /*                                                              *
*                                                                  *
*  WENN PARM=GIPE ANGEGEBEN WIRD, WERDEN NUR GIP-ENTRIES           *
*  GELISTET. BEI PARM=NOGIPE WIRD ALLES AUSSER GIP-ENTRIES         *
*  GELISTET.                                                       *
*  PARM=BV ENTSPRICHT PARM=GIPE ,ES WIRD JEDOCH ZUSAETZLICH        *
*  EINE DATEI GESCHRIEBEN, DIE DEN DSN, DIE ANZAHL DER             *
*  BELEGTEN UND MAXIMAL MOEGLICHEN EINTRAEGE BEINHALTET.           *
*  DAS DD-STATEMENT SYHA171C KANN ENTFALLEN, WENN DER GESAMTE      *
*  KATALOG ODER ALLE GIPE GELISTET WERDEN SOLLEN.                  *
*                                                                  *
*  DAS DD-STATEMENT SYHA171D WIRD NUR BEI PARM=BV BENOETIGT.       *
*                                                                  *
*  DIE PARAMETERKARTEN KOENNEN AUF EINER BELIEBIGEN STELLE         *
*  BEGINNEN, PRO KARTE KANN ABER NUR 1 INDEX ANGEGEBEN WERDEN.     *
*  ALLE EINTRAEGE, DIE MIT DEM ANGEGEBENEN INDEX BEGINNEN,         *
*  WERDEN GEDRUCKT. DAS DRUCKEN DER CVOL- UND ALIAS-ENTRIES KANN   *
*  NICHT UNTERDRUECKT WERDEN.                                      *
*  MAXIMAL 20 PARAMETERKARTEN WERDEN AKZEPTIERT.                   *
*                                                                  *
*        IPE   :  INDEX-POINTER-ENTRY                              *
*        GIPE  :  GENERATION-INDEX-POINTER-ENTRY                   *
*        AE    :  ALIAS-ENTRY                                      *
*        DSPE  :  DATASET-POINTER-ENTRY                            *
*        CVPE  :  CATALOG-VOLUME-POINTER-ENTRY                     *
*                                                                  *
********************************************************************
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2                       BASISREGISTER
R3       EQU   3                       BASISREGISTER
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
SYHA171  CSECT
         B     12(R15)
         DC    CL8'SYHA171'
         SAVE  (14,12)
         LR    R2,R15
         LA    R3,2048(R2)
         LA    R3,2048(R3)
         USING SYHA171,R2,R3
         ST    R13,SAV+4
         LR    R15,R13
         LA    R13,SAV
         ST    R13,8(R15)
         LTR   R1,R1
         BZ    BEGINN
         L     R4,0(R1)
         CLC   2(4,R4),=C'GIPE'
         BNE   NOG
         OI    KZ,X'40'
         B     BEGINN
NOG      EQU   *
         CLC   2(6,R4),=C'NOGIPE'
         BNE   BANDV
         OI    KZ,X'20'
         B     BEGINN
BANDV    EQU   *
         CLC   2(2,R4),=C'BV'
         BNE   BEGINN
         OI    KZ,X'50'               GIPE UND BANDVERW.-LISTE
         OPEN  (BVDAT,(OUTPUT))
         B     BEGINN
         SPACE 3
*      QBV6*STATUSA
         SPACE 3
BEGINN   EQU   *
         TIME  DEC
         ST    R1,FW
         MVC   A1DAT,=X'40202021204B202020'
         ED    A1DAT,FW
         ST    R0,FW
         UNPK  A1ZEIT(3),FW(2)
         UNPK  A1ZEIT+3(3),FW+1(2)
         MVI   A1ZEIT+2,C'.'
         MVI   A1ZEIT+5,C' '
         OPEN  (LISTE,(OUTPUT))
         RDJFCB PARM
         LTR   R15,R15
         BNZ   NOPARM                  DD-STATEMENT FEHLT
         OI    KZ,X'80'
NOPARM   EQU   *
         RDJFCB CAT
         LTR   R15,R15
         BZ    CATVOL
         ABEND 99                      DD-STATEMENT FEHLT
CATVOL   EQU   *
         CLC   JFCB(8),=CL8'SYSCTLG'
         BNE   ABEND
         MVC   A1VOL,JFCB+118
         TM    KZ,X'80'
         BZ    OPCAT                   GESAMTEN CATALOG LISTEN
         OPEN  (PARM,(INPUT))
         LA    R4,INDTAB
         LA    R5,20                   MAX. ANZAHL EINTRAEGE
         LR    R9,R4
         SPACE 3
GETPARM  EQU   *
         GET   PARM,PIN
         LA    R6,PIN
         LA    R7,80
         XR    R10,R10
GETP010  EQU   *                       PARM-ANFANG SUCHEN
         CLI   0(R6),C' '
         BNE   GETP020
         LA    R6,1(R6)
         BAL   R7,GETP010
         B     GETPARM                 LEERKARTE
GETP020  EQU   *                       PARM-ANFANG GEFUNDEN
         LR    R8,R6                   NAMENS-SEGMENTE SUCHEN
GETP030  EQU   *
         CLI   0(R6),C' '
         BE    GETP040
         CLI   0(R6),C'.'
         BE    GETP040
         LA    R6,1(R6)
         BCT   R7,GETP030
GETP040  EQU   *
         LA    R10,8(R10)
         C     R10,=F'40'
         BH    GETP050                 PARM-FEHLER
         ST    R6,FW
         SR    R6,R8
         BCTR  R6,0
         C     R6,=F'7'
         BH    GETP050
         C     R6,=F'0'
         BNL   GETP060
GETP050  EQU   *
         PUT   LISTE,PFEHL
         LR    R4,R9
         MVC   0(2,R4),C' '            TAB-PLATZ IST FREI
         B     GETPARM
GETP060  EQU   *
         EX    R6,MVCSEG               MVC  1(0,R4),0(R8)
         L     R6,FW
         CLI   0(R6),C' '
         BE    GETP070                 PARM-ENDE
         LTR   R7,R7
         BZ    GETP070                 PARM-ENDE
         LA    R4,8(R4)                NAECHSTES SEG. IN TAB
         LA    R6,1(R6)
         LR    R8,R6
         B     GETP030
GETP070  EQU   *
         BCTR  R10,0
         STC   R10,0(R9)               VERGLEICHSLAENGE
         LA    R4,41(R9)               NAECHSTER PLATZ IN TAB
         LR    R9,R4
         BCT   R5,GETPARM
         SPACE 3
PARMEND  EQU   *
         CLOSE PARM
         CLC   INDTAB(8),=CL8' '
         BNE   PARMSORT
         MVI   KZ,X'00'
         B     OPCAT                   OPEN CATALOG
         SPACE 3
PARMSORT EQU   *                       PARAMETER SORTIEREN
         ST    R4,INDEND
         SH    R4,=H'82'
         C     R5,=F'19'
         BE    OPCAT                   NUR 1 EINTRAG
         LA    R5,INDTAB
         LA    R6,41(R5)
PS30     EQU   *
         CLC   1(40,R5),1(R6)
         BL    PS40
         XC    0(41,R5),0(R6)          PLAETZE TAUSCHEN
         XC    0(41,R6),0(R5)
         XC    0(41,R5),0(R6)
PS40     EQU   *
         LA    R6,41(R6)
         C     R6,INDEND
         BL    PS30
         LA    R5,41(R5)
         CR    R5,R4
         BNL   OPCAT                   ENDE DER SORTIERUNG
         LA    R6,41(R5)
         B     PS30
         SPACE 3
OPCAT    EQU   *                       OPEN CATALOG
         OPEN  (CAT,(INPUT))
         LA    R4,CRTAB
         LA    R6,ZREC
         SPACE 3
NCATR    EQU   *
         BAL   R12,LESCAT
         ST    R4,CRADR
NEXT00   EQU   *
         LA    R4,10(R4)               PREFIX UEBERSPRINGEN
         B     NEXT10
         SPACE 3
NEXT     XR    R5,R5
         IC    R5,11(R4)               RECORD-TYPE
         MH    R5,=H'2'
         AR    R4,R5
         LA    R4,12(R4)
NEXT10   EQU   *
         CLC   0(8,R4),=8X'FF'
         BE    SATZENDE                ENDE DES CATALOG-RECORDS
         CLC   0(8,R4),=8X'00'
         BE    SATZENDE                LEERER SATZ
         CLI   11(R4),X'03'            CVOL ODER ICE
         BE    CVOL
         CLI   11(R4),X'04'            ALIAS
         BE    ALIAS
         CLI   11(R4),X'05'            CVOL ODER VICE
         BE    CVOL
         CLI   11(R4),X'00'            IPE
         BNE   TYPPR
         CLC   8(3,R4),=3X'00'
         BNE   IPE
         TM    KZ,X'40'                NUR GIPE ?
         BO    NEXT                    JA
         MVC   A4TYP,=C'IPE '
         LA    R12,NEXT                LEERER INDEX
         B     DRUCK
         SPACE 3
CVOL     EQU   *
         TM    KZ,X'40'                NUR GIPE ?
         BO    NEXT                    JA
         CLC   0(8,R4),=X'0000000000000001'
         BE    NEXT                    IPE
         MVC   A4NAME(8),0(R4)
         MVC   A4TYP,=C'CVPE'
         CLI   11(R4),X'05'
         BE    CVOL10                  NEW CVOL-PTR
         MVC   A4VOL,12(R4)            OLD CVOL-PTR
         B     CVOL20
CVOL10   EQU   *
         MVC   A4VOL,16(R4)
         UNPK  A4DEV(9),12(R4)
         TR    A4DEV(8),TRTAB-240
         MVI   A4DEV+8,X'40'
CVOL20   EQU   *
         LA    R12,NEXT
         B     DRU30
         SPACE 3
IPE      EQU   *
         MVI   TYPKZ,X'00'
         MVC   TTR(3),8(R4)
         MVC   0(8,R6),0(R4)           INDEX-NAME
         BAL   R12,TABSU
IPE10    EQU   *
         LA    R6,8(R6)
         L     R15,CRADR
         ST    R4,264(R15)             ADR. DES NAECHSTEN ENTRIES
         LA    R4,268(R15)             NAECHSTER PLATZ IN CRTAB
         B     NCATR                   NEXT CATALOG RECORD
         SPACE 3
TYPPR    EQU   *
         CLI   11(R4),X'02'            GIPE
         BNE   DSPE
         MVI   TYPKZ,X'02'
         MVC   0(8,R6),0(R4)           GIPE-NAME
         TM    KZ,X'20'                KEINE GIPE DRUCKEN ?
         BO    TYPPR10
         BAL   R12,TABSU
         XR    R15,R15
         IC    R15,13(R4)              MAX. ANZAHL ENTRIES
         CVD   R15,DW
         UNPK  A4MAX,DW+6(2)
         OI    A4MAX+2,X'F0'
         IC    R15,15(R4)              MOMENTANE ANZAHL ENTRIES
         CVD   R15,DW
         UNPK  A4CUR,DW+6(2)
         OI    A4CUR+2,X'F0'
         MVC   A4TYP,=C'GIPE'
         BAL   R12,DRUCK
TYPPR10  EQU   *
         CLC   8(3,R4),=3X'00'
         BE    NEXT
         MVC   TTR(3),8(R4)
         B     IPE10
         SPACE 3
DSPE     EQU   *
         TM    KZ,X'40'                NUR GIPE ?
         BO    NEXT                    JA
         CLI   11(R4),X'01'            VCBPE ?
         BE    DSPE01                  JA
         CLI   11(R4),X'07'
         BL    ABEND                   UNBEKANNTER ENTRY-TYP
         CLI   11(R4),X'1F'
         BH    ABEND
DSPE01   EQU   *
         MVC   0(8,R6),0(R4)
         BAL   R12,TABSU
         MVC   A4TYP,=C'DSPE'
         CLI   TYPKZ,X'02'             GDG ?
         BNE   DSPE10
         XC    1(4,R6),=F'-1'
         B     DSPE20
DSPE10   EQU   *
         MVC   TYPKZ,11(R4)
DSPE20   EQU   *
         CLI   11(R4),X'01'            VCBPE ?
         BNE   DSPE30                  NEIN
         MVI   KZV,C'Y'                VCBPE
         MVC   TTR(3),8(R4)
         ST    R4,REG4
         LA    R4,VCBREC
VCBPE05  EQU   *
         BAL   R12,LESCAT
         LA    R4,8(R4)
         LH    R11,0(R4)
         CH    R11,=H'20'
         BNH   VCBPE10
         LH    R11,=H'20'
VCBPE10  EQU   *
         LA    R10,2(R4)
         B     DSPE05
DSPE30   EQU   *
         LH    R11,12(R4)              VOLUME-COUNT
         LA    R10,14(R4)              ANFANG VOL-ENTRY
DSPE05   EQU   *
         UNPK  A4DEV(9),0(5,R10)       DEVICE-TYPE
         TR    A4DEV(8),TRTAB-240
         MVI   A4DEV+8,X'40'
         MVC   A4VOL,4(R10)            VOL-SER
         LH    R15,10(R10)             SEQ-NO.
         CVD   R15,DW
         UNPK  A4SEQ,DW+5(3)
         OI    A4SEQ+5,X'F0'
         MVC   A4TYP,=C'DSPE'
         BAL   R12,DRUCK
         LA    R10,12(R10)             NAECHSTER VOL-ENTRY
         BCT   R11,DSPE05
         CLI   KZV,C'Y'                 VCBPE-RECORD ?
         BNE   NEXT                     NEIN
         CLC   VCBREC+252(3),=X'000000' WEITERE VCB ?
         BNE   DSPE06                   JA
         L     R4,REG4
         MVI   KZV,C' '
         B     NEXT
DSPE06   EQU   *
         LA    R4,VCBREC
         MVC   TTR(3),VCBREC+252        ADR. NAECHSTER VCB
         B     VCBPE05
         SPACE 3
ALIAS    EQU   *
         TM    KZ,X'40'                NUR GIPE ?
         BO    NEXT                    JA
         MVC   A4NAME,0(R4)
         MVC   A4TYP,=C'AE  '
         MVC   A4ALIAS,12(R4)
         LA    R12,NEXT
         B     DRU30
         SPACE 3
SATZENDE EQU   *                       ENDE DES CATALOG-RECORDS
         CLC   8(3,R4),=3X'00'
         BE    STUF                    1 STUFE ZURUECKGEHEN
         MVC   TTR(3),8(R4)            GLEICHER LEVEL
         L     R4,CRADR
         LA    R12,NEXT00
         B     LESCAT
         SPACE 3
STUF     EQU   *
         TM    KZ,X'40'                NUR GIPE?
         BO    STUF10                  JA
         CLI   TYPKZ,X'00'
         BNE   STUF10
         MVC   A4TYP,=C'IPE '
         BAL   R12,DRUCK
STUF10   EQU   *
         MVI   TYPKZ,X'40'
         MVC   0(8,R6),=8C' '
         SH    R6,=H'8'
         L     R4,CRADR
         SH    R4,=H'268'
         ST    R4,CRADR
         C     R4,=A(CRTAB)
         BL    EINEND
         L     R4,264(R4)              LETZTER VERARBEITETER EINTRAG
         B     NEXT
         SPACE 3
DRUCK    EQU   *
         MVC   A4NAME,=CL44' '
         CLC   ZREC,ZNAME
         BE    DRU30
         MVC   ZNAME,ZREC
         STM   R4,R7,SAV2              NAMEN AUFBEREITEN
         LA    R4,ZREC
         LA    R5,8                    INNERE SCHLEIFE
         LA    R6,5                    AEUSSERE SCHLEIFE
         LA    R7,A4NAME
         B     DRU10
DRU05    EQU   *
         CLC   0(8,R4),=8C' '
         BE    DRU25
         MVI   0(R7),C'.'
         LA    R7,1(R7)                ENDE DER AUFBEREITUNG
DRU10    EQU   *
         CLI   0(R4),C' '
         BE    DRU20                   SEGMENT-ENDE
         MVC   0(1,R7),0(R4)
         LA    R4,1(R4)
         LA    R7,1(R7)
         BCT   R5,DRU10
DRU20    EQU   *
         AR    R4,R5
         LA    R5,8
         BCT   R6,DRU05
DRU25    EQU   *
         LM    R4,R7,SAV2
DRU30    EQU   *
         CP    ZZ,=P'60'
         BL    DRU40
         AP    SZ,=P'1'
         UNPK  A1S,SZ
         OI    A1S+3,X'F0'
         MVI   AREC4,C'0'
         PUT   LISTE,AREC1
         PUT   LISTE,AREC2
         PUT   LISTE,AREC3
         ZAP   ZZ,=P'5'
DRU40    EQU   *
         PUT   LISTE,AREC4
         TM    KZ,X'10'              BANDVERW.-LISTE
         BZ    DRU50
         MVC   BVNAME,A4NAME
         MVC   BVCUR,A4CUR
         MVC   BVMAX,A4MAX
         PUT   BVDAT,BVSATZ
DRU50    EQU   *
         AP    ZZ,=P'1'
         MVI   AREC4,C' '
         MVC   A4NAME(103),A4NAME-1    ZEILE LOESCHEN
         BR    R12
         SPACE 3
TABSU    EQU   *
         TM    KZ,X'80'
         BZ    TABSU70                 KEINE PARM. VORHANDEN
         CLC   INDTAB(8),=CL8' '
         BE    TABSU70
         STM   R4,R7,SAV2
         XR    R5,R5
         LA    R4,INDTAB
TABSU10  EQU   *
         LR    R7,R6
         S     R7,=A(ZREC)
         LA    R7,7(R7)
         IC    R5,0(R4)
         CR    R5,R7                   IN DER KLEINEREN LAENGE
         BNH   TABSU20                 VERGLEICHEN
         LR    R5,R7
TABSU20  EQU   *
         EX    R5,TABVGL               CLC  ZREC(0),1(R4)
         BE    TABSU60
         BL    TABSU50
         LA    R4,41(R4)
         C     R4,INDEND
         BNL   EINEND
         B     TABSU10
TABSU50  EQU   *
         LA    R12,NEXT
TABSU60  EQU   *
         LM    R4,R7,SAV2
TABSU70  EQU   *
         BR    R12
         SPACE 3
EINEND   EQU   *
         CLOSE CAT
         CLOSE LISTE
         TM    KZ,X'10'
         BZ    RETURN
         CLOSE BVDAT
RETURN   EQU   *
         L     R13,SAV+4
         RETURN (14,12),RC=0
         SPACE 3
LESCAT   EQU   *                       CATALOG LESEN
         POINT CAT,TTR
         READ  DECB1,SF,CAT,(4),264
         CHECK DECB1
         MVI   DECB1,X'00'
         MVI   TTR+3,X'00'
         BR    R12
         SPACE 3
ABEND    EQU   *
         ABEND 99,DUMP
         EJECT
******    DEFINITIONEN    ******
         SPACE 3
SAV      DC    9D'0'
SAV2     DC    4F'0'                   SAVE-AREA FUER DRUCKROUTINE
DW       DC    D'0'
FW       DC    F'0'
CRADR    DC    F'0'                    ADRESSE DES LETZTEN BELEGTEN
*                                      PLATZES IN CRTAB
REG4     DC    F'0'                     R4 SICHERN BEI VCB-VERARB.
TTR      DC    F'1'
TRTAB    DC    C'0123456789ABCDEF'
SZ       DC    PL2'0'                  SEITENZAEHLER
ZZ       DC    PL2'70'                 ZEILENZAEHLER
KZV      DC    C' '                    Y =  VCBPE-RECORD
KZ       DC    X'00'
*                    1... ....         SYHA171C VORHANDEN
*                    .1.. ....         NUR GIPE DRUCKEN
*                    ..1. ....         KEINE GIPE DRUCKEN
*                    ...1 ....         GIPE UND BANDVERW.-LISTE
TYPKZ    DC    C' '
CATEX    DS    0F                      CATALOG - EXITLIST
         DC    X'07'
         DC    AL3(JFCB)
JFCB     DC    CL176' '
INDEND   DC    F'0'
         SPACE 3
AREC1    DS    0CL133
         DC    C'1       C A T A L O G - I N F O R M A T I O N E N'
         DC    CL10' '
         DC    C'VOL='
A1VOL    DC    CL6' '
         DC    CL7' '
A1DAT    DC    CL9' '
         DC    CL5' '
A1ZEIT   DC    CL5' '
         DC    CL10' UHR'
         DC    CL10' '
         DC    CL6'SEITE'
A1S      DC    CL4' '
         DC    CL8' '
         SPACE 3
AREC2    DS    0CL133
         DC    CL52'0   NAME'
         DC    CL4' '
         DC    C'ENTRY-   '
         DC    C'VOL.ID.   SEQ.NO.   DEV.TYPE     ENTRIES'
         DC    CL13'    ALIAS OF'
         SPACE 3
AREC3    DS    0CL133
         DC    CL57' '
         DC    CL40'TYPE'
         DC    CL36'MAX   CUR'
         SPACE 3
AREC4    DS    0CL133
         DC    CL4'0'
A4NAME   DC    CL44' '
         DC    CL9' '
A4TYP    DC    CL4' '
         DC    CL4' '
A4VOL    DC    CL6' '
         DC    CL4' '
A4SEQ    DC    CL6' '
         DC    CL4' '
A4DEV    DC    CL8' '
         DC    CL4' '
A4MAX    DC    CL3' '
         DC    CL3' '
A4CUR    DC    CL3' '
         DC    CL3' '
A4ALIAS  DC    CL8' '
         DC    CL16' '
         DC    CL27' '
         SPACE 3
BVSATZ   DS    0CL50
BVNAME   DC    CL44' '
BVCUR    DC    CL3' '
BVMAX    DC    CL3' '
         SPACE 3
PFEHL    DC    C' '
PIN      DC    CL80' '                 PARAMETER-EINGABE
         DC    CL52' PARM.-KARTE IST UNGUELTIG UND WIRD IGNORIERT.'
TABVGL   CLC   ZREC(0),1(R4)
MVCSEG   MVC   1(0,R4),0(R8)           SEGMENT ZUR TABELLE
ZREC     DC    CL40' '
ZNAME    DC    CL40' '
*                         40 BYTES NAMENSEGMENTE ( 5 * 8 BYTES )
         EJECT
CAT      DCB   DDNAME=SYHA171A,                                        *
               MACRF=(RP),                                             *
               DSORG=PS,                                               *
               RECFM=U,                                                *
               LRECL=264,                                              *
               BLKSIZE=264,                                            *
               KEYLEN=8,                                               *
               EODAD=EINEND,                                           *
               EXLST=CATEX
         EJECT
LISTE    DCB   DDNAME=SYHA171B,                                        *
               MACRF=(PM),                                             *
               DSORG=PS,                                               *
               LRECL=133,                                              *
               BLKSIZE=1330,                                           *
               RECFM=FBA
         EJECT
PARM     DCB   DDNAME=SYHA171C,                                        *
               MACRF=(GM),                                             *
               DSORG=PS,                                               *
               LRECL=80,                                               *
               RECFM=FB,                                               *
               EODAD=PARMEND,                                          *
               EXLST=CATEX
         EJECT
BVDAT    DCB   DDNAME=SYHA171D,                                        *
               MACRF=(PM),                                             *
               DSORG=PS,                                               *
               LRECL=50,                                               *
               RECFM=FB
INDTAB   DC    20CL41' '
*                                       1 BYTE ANZAHL SEHMENTE
*                                    JE 8 BYTES PRO SEGMENT
         LTORG
         DS    0H
VCBREC   DC    2CL132' '                VCB-RECORD
CRTAB    DC    10CL134' '              TABELLE DER CATALOG-RECORDS
*                        5 PLAETZE JE 268 BYTES
*                        264 BYTES CATALOG-RECORD
*                          4 BYTES ADR. DES NAECHSTEN ENTRIES IM SATZ
         END
