MEMBER NAME  EXIN
         MACRO
         EXIN  &BREG=12,&SAVE=(14,12),&SAREA=
         GBLA  &ASAVE(3)
         GBLC  &ASAREA
         LCLA  &Z,&A,&B,&C,&D,&E
         LCLC  &REG(11)
.*
.* DIESES MACRO ORDNET BASISREGISTER ZU, LAEDT SIE, DEFINIER0 UND VER-
.* KETTET DIE SAVE-AREA.
.* ERSTELLT: FEBRUAR 1969  - P.MERTENS/E.LANGE -.
.*
&ASAVE(3) SETA 0
&D       SETA  0
         AIF   ('&SYSECT' NE '').A00
&D       SETA  1
.A00     ANOP
&Z       SETA  1
.A01     AIF   ('&BREG(&Z)' LT '2' OR '&BREG(&Z)' GT '13').A02
         AIF   (N'&BREG EQ &Z).A03
&Z       SETA  &Z+1
         AIF   (N'&BREG EQ 3 AND &Z EQ 3 AND '&BREG(&Z)' EQ 'L').A03
         AGO   .A01
.A02     MNOTE 16,'*** FALSCHES REGISTER -BREG- ***'
&ASAVE(3) SETA 1
         MEXIT
.A03     ANOP
&ASAREA  SETC  '&SAREA'
         AIF   ('&SAREA' EQ '0').A38
         AIF   (&D EQ 1).A030
         B     12(15)         BRANCH AROUND ID
         DC    CL8'&SYSECT'
&E       SETA  12
.A030    ANOP
&ASAVE(2) SETA 16
&ASAVE(1) SETA &SAVE(1)
         AIF   (N'&SAVE NE 1).A06
&E       SETA  &E+4
         STM   14,1,12(13)
         AIF   (&ASAVE(1) EQ 14).A14
.A04     ANOP
&C       SETA  &ASAVE(1)*4+20
         AIF   (&C LE 75).A05
&C       SETA  &C-64
.A05     ANOP
         ST    &ASAVE(1),&C.(13)
&E       SETA  &E+4
         AGO   .A14
.A06     ANOP
&ASAVE(2) SETA &SAVE(2)
         AIF   (&ASAVE(2) GT &ASAVE(1)).A11
&C       SETA  &ASAVE(1)*4+20
         AIF   (&C LE 75).A07
&C       SETA  &C-64
.A07     ANOP
         AIF   (&ASAVE(1) GT 12).A070
         AIF   (&ASAVE(1) NE 12).A08
         ST    &ASAVE(1),&C.(13)
&E       SETA  &E+4
.A070    ANOP
         AIF   (&ASAVE(2) GE 12).A071
         STM   14,&ASAVE(2),12(13)
&E       SETA  &E+4
         AIF   (&ASAVE(1) EQ 12).A14
&ASAVE(1)  SETA 14
         AGO   .A14
.A071    ANOP
         STM   14,12,12(13)
&ASAVE(1) SETA 14
&ASAVE(2) SETA 12
&E       SETA  &E+4
         AGO   .A14
.A08     ANOP
         STM   &ASAVE(1),12,&C.(13)
&E       SETA  &E+4
         AIF   (&ASAVE(2) EQ 12).A14
         AIF   (&ASAVE(2) NE 14).A10
         STM   &ASAVE(2),1,12(13)
&E       SETA  &E+4
         AGO   .A14
.A10     ANOP
         STM   14,&ASAVE(2),12(13)
&E       SETA  &E+4
         AGO   .A14
.A11     ANOP
&E       SETA  &E+4
         STM   14,1,12(13)
         AIF   (&ASAVE(2) LE 11).A12
&ASAVE(2) SETA 12
         AIF   (&ASAVE(1) GE 13).A110
         AIF   (&ASAVE(1) NE 12).A12
&E       SETA  &E+4
         ST    12,68(13)
         AGO   .A14
.A110    ANOP
&ASAVE(1) SETA 14
&ASAVE(2) SETA 1
         AGO   .A14
.A12     ANOP
&C       SETA  &ASAVE(1)*4+20
         AIF   (&C LE 75).A13
&C       SETA  &C-64
.A13     ANOP
&E       SETA  &E+4
         STM   &ASAVE(1),&ASAVE(2),&C.(13)
.A14     ANOP
         AIF   ('&BREG(1)' EQ '13').A50
         BALR  &BREG(1),0
         AIF   (N'&BREG NE 1).A15
         USING *,&BREG(1)
         AGO   .A21
.A15     ANOP
&A       SETA  N'&BREG
&Z       SETA  &BREG(1)
         AIF   ('&BREG(&A)' NE 'L').A18
&A       SETA  1
         AIF   ('&BREG(1)' GT '&BREG(2)').A17
.A16     ANOP
&REG(&A) SETC  ','.'&Z'
         AIF   (&BREG(2) EQ &Z).A20
&A       SETA  &A+1
&Z       SETA  &Z+1
         AGO   .A16
.A17     ANOP
&REG(&A) SETC  ','.'&Z'
         AIF   (&BREG(2) EQ &Z).A20
&A       SETA  &A+1
&Z       SETA  &Z-1
         AGO   .A17
.A18     ANOP
&Z       SETA  1
.A19     ANOP
&REG(&Z)  SETC ','.'&BREG(&Z)'
         AIF   (&Z EQ &A).A20
&Z       SETA  &Z+1
         AGO   .A19
.A20     USING *&REG(1).&REG(2).&REG(3).&REG(4).&REG(5).&REG(6).&REG(7).
               .&REG(8).&REG(9).&REG(10).&REG(11)
.A21     ANOP
         AIF   (&BREG(1) EQ 13).A500
         AIF   ('&SAREA' EQ '0').A39
CSBASZU  LR    14,13
         AIF   ('&SAREA' EQ '').A22
         LA    13,&SAREA
         AGO   .A23
.A22     ANOP
         LA    13,CSBASREG
.A23     ANOP
         ST    14,4(13)
         ST    13,8(14)
         AIF   ('&SAREA' NE '' AND N'&BREG EQ 1).A37
         AIF   (N'&BREG EQ 1).A28
&Z       SETA  N'&BREG
         AIF   ('&BREG(&Z)' EQ 'L').A25
&A       SETA  1
&B       SETA  0
.A24     ANOP
&A       SETA  &A+1
         L     &BREG(&A),CSBASADR+&B
         AIF   (&A EQ &Z).A28
&B       SETA  &B+4
         AGO   .A24
.A25     ANOP
         AIF   ('&BREG(1)' EQ '&BREG(2)').A28
         AIF   ('&BREG(1)' LT '&BREG(2)').A27
&A       SETA  &BREG(2)
&B       SETA  &BREG(1)-1
.A26     ANOP
         LM    &A,&B,CSBASADR
         AGO   .A28
.A27     ANOP
&A       SETA  &BREG(1)+1
&B       SETA  &BREG(2)
         AGO   .A26
.A28     ANOP
         B     CSBASEND
         AIF   ('&SAREA' NE '').A29
CSBASREG DC    18F'0'
.A29     ANOP
         AIF   (N'&BREG EQ 1).A36
&Z       SETA  N'&BREG
CSBASADR EQU   *
         AIF   ('&BREG(&Z)' EQ 'L').A31
&A       SETA  1
&B       SETA  0
.A30     ANOP
&A       SETA  &A+1
&B       SETA  &B+4096
         DC    A(CSBASZU+&B)
         AIF   (&A EQ &Z).A36
         AGO   .A30
.A31     ANOP
         AIF   ('&BREG(1)' EQ '&BREG(2)').A36
&Z       SETA  0
         AIF   ('&BREG(1)' LT '&BREG(2)').A34
&A       SETA  &BREG(2)
&B       SETA  &BREG(1)
.A32     ANOP
&Z       SETA  &Z+4096
&A       SETA  &A+1
         AIF   (&A NE &B).A32
.A33     ANOP
         DC    A(CSBASZU+&Z)
         AIF   (&Z EQ 4096).A36
&Z       SETA  &Z-4096
         AGO   .A33
.A34     ANOP
&A       SETA  &BREG(1)
&B       SETA  &BREG(2)
.A35     ANOP
&Z       SETA  &Z+4096
&A       SETA  &A+1
         DC    A(CSBASZU+&Z)
         AIF   (&A NE &B).A35
.A36     ANOP
CSBASEND EQU   *
.A37     ANOP
         MEXIT
.A38     ANOP
         AIF   (&BREG(1) EQ 13).A381
         AIF   (&D EQ 1).A380
         B     12(15)
         DC    CL8'&SYSECT'
         STM   14,13,20(15)
         BAL   13,84(15)
         DC    16F'0'
         AGO   .A14
.A380    ANOP
         STM   14,13,8(15)
         BAL   13,72(15)
         DC    16F'0'
         AGO   .A14
.A381    ANOP
         MNOTE 16,'*** DIESER FALL IST VON HAND ZU STRICKEN ***'
&ASAVE(3) SETA 1
         MEXIT
.A39     ANOP
CSBASBEG ST    13,CSBASR13
         AIF   (N'&BREG EQ 1).A43
&A       SETA  N'&BREG
         AIF   ('&BREG(&A)' NE 'L').A41
         AIF   ('&BREG(1)' EQ '&BREG(2)').A43
         AIF   ('&BREG(1)' LT '&BREG(2)').A40
&A       SETA  &BREG(2)
&A       SETA  &A+1
         LM    &A,&BREG(1),CSBASADR
         AGO   .A43
.A40     ANOP
&A       SETA  &BREG(1)
&A       SETA  &A+1
         LM    &A,&BREG(2),CSBASADR
         AGO   .A43
.A41     ANOP
&Z       SETA  0
&B       SETA  1
.A42     ANOP
&B       SETA  &B+1
         L     &BREG(&B),CSBASADR+&Z
         AIF   (&A EQ &B).A43
&Z       SETA  &Z+4
         AGO   .A42
.A43     ANOP
         CNOP  0,4
         BAL   13,CSBASREN
         DC    18F'0'
CSBASR13 DC    F'0'
         AIF   (N'&BREG EQ 1).A49
&Z       SETA  4096
&A       SETA  N'&BREG
         AIF   ('&BREG(1)' EQ '&BREG(2)').A49
CSBASADR EQU   *
         AIF   ('&BREG(&A)' NE 'L').A47
&A       SETA  &BREG(1)
&B       SETA  &BREG(2)
         AIF   ('&BREG(1)' LT '&BREG(2)').A46
.A44     ANOP
&A       SETA  &A-1
         AIF   (&A EQ &B).A45
&Z       SETA  &Z+4096
         AGO   .A44
.A45     ANOP
         DC    A(CSBASBEG+&Z)
         AIF   (&Z EQ 4096).A49
&Z       SETA  &Z-4096
         AGO   .A45
.A46     ANOP
&A       SETA  &A+1
         DC    A(CSBASBEG+&Z)
         AIF   (&A EQ &B).A49
&Z       SETA  &Z+4096
         AGO   .A46
.A47     ANOP
&B       SETA  1
.A48     ANOP
&B       SETA  &B+1
         DC    A(CSBASBEG+&Z)
         AIF   (&A EQ &B).A49
&Z       SETA  &Z+4096
         AGO   .A48
.A49     ANOP
CSBASREN EQU   *
         MEXIT
.A50     ANOP
         AIF   ('&BREG(3)' EQ 'L').A381
         USING *-&E,15
         LR    14,13
         CNOP  0,4
         BAL   13,CSBASBEG
         DROP  15
         AIF   (N'&BREG NE 1).A15
         USING *,13
.A500    ANOP
CSBASREG DC    18F'0'
CSBASBEG EQU   *
         ST    14,4(13)
         ST    13,8(14)
         AIF   ('&SAREA' NE '').A381
         AIF   (N'&BREG EQ 1).A55
&Z       SETA  N'&BREG
         AIF   ('&BREG(&Z)' EQ 'L').A381
&A       SETA  1
&B       SETA  0
.A51     ANOP
&A       SETA  &A+1
         L     &BREG(&A),CSBASADR+&B
         AIF   (&A EQ &Z).A52
&B       SETA  &B+4
         AGO   .A51
.A52     ANOP
         B     CSBASEND
CSBASADR EQU   *
&A       SETA  1
&B       SETA  0
.A53     ANOP
&A       SETA  &A+1
&B       SETA  &B+4096
         DC    A(CSBASREG+&B)
         AIF   (&A EQ &Z).A54
         AGO   .A53
.A54     ANOP
CSBASEND EQU   *
.A55     ANOP
         MEND
         END
