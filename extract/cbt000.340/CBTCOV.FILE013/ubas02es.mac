ZS905PRM CSECT
         REG
         XSAVE (R12),SVA,ZS905PRM ZIMPELMANN
         L     R3,0(R1)
         LA    R3,0(R3)               VL SWITCH-BIT LOESCHEN
         USING Y1,R3
         L     R4,Y1SVR1
         L     R8,0(R4)               ANFANGSADR. PARMKART
         LH    R4,0(R8)               LAENGE PARMKART
         CALL  TDATE,(ZEIT,DATUM),VL
         AP    Y1SEIZ,=P'+1'
         MVC   SEITNR,MASKE
         ED    SEITNR,Y1SEIZ
         CALL  ZS905WRF,((R3),ZEIL1),VL
         CALL  ZS905WRF,((R3),ZEIL2),VL
         CALL  ZS905WRF,((R3),ZEIL3),VL
         LA    R7,1
PARMVGL  EQU   *
         LTR   R4,R4
         BZ    PARMEND                ALLE PARAMETER VERARBEITET ?
         LTR   R7,R7
         BNZ   PARMANF
         BCTR  R4,0
         CLI   1(R8),C','
         BE    PARMANF
         LA    R9,1          PARAMETER NICHT DURCH KOMMA GETRENNT
         B     FEHLER
PARMANF  EQU   *
         SR    R7,R7
         LA    R8,2(R8)
         L     R5,=A(PRMFELD)
         SR    R6,R6
         SR    R9,R9
LOOP1    EQU   *
         IC    R6,0(R5)
         LTR   R6,R6
         BNZ   LOOP1A
         LA    R9,2                    FALSCHER PARAMETER
         B     FEHLER
LOOP1A   EQU   *
         LA    R5,1(R5)
         BCTR  R6,0
         EX    R6,EXCLC1               PARAMETERVERGLEICH
         BE    GEFPARM
         LA    R5,1(R6,R5)
         LA    R9,1(R9)
         B     LOOP1
GEFPARM  EQU   *
         LA    R6,1(R6)
         SR    R4,R6
         LA    R8,0(R6,R8)
         SLL   R9,2                   MULTIPLY BY 4
         LA    R5,PRMADR(R9)
         L     R15,0(R5)
         BR    R15
*                  LINES-PARAMETER
*
PARMLIN  EQU   *
         SR    R1,R1
         SR    R2,R2
         TRT   0(5,R8),TAB
         BNZ   PARML1
         LA    R9,3                    LINES-PARM > 4 STELLEN
         B     FEHLER
PARML1   EQU   *
         B     *(R2)
         LA    R9,4
         B     FEHLER                  LINES-PARM NICHT NUMERISCH
         SR    R1,R8
         SR    R4,R1
         BCTR  R1,0
         EX    R1,EXPACK1
         LA    R8,1(R1,R8)
         BCTR  R4,0
         B     PARMVGL
*                  TERM-PARAMETER
*
PARMTERM EQU   *
         CLC   0(4,R8),=C'YES)'
         BNE   NOTERM
         MVI   Y1TFLG,Y1TYES
         LA    R8,1(R8)
         BCTR  R4,0
         B     TERMEND
NOTERM   EQU   *
         CLC   0(3,R8),=C'NO)'
         BE    TERMEND
         LA    R9,2
         B     FEHLER
TERMEND  EQU   *
         LA    R8,2(R8)
         LA    R1,3
         SR    R4,R1
         B     PARMVGL
*               DSTYPE-PARAMETER
*
PARMTYP  EQU   *
         CLC   0(3,R8),=C'IC)'
         BNE   TYPDB
         MVI   Y1TYPF,Y1TIC
         B     TYPEND
TYPDB    EQU   *
         CLC   0(3,R8),=C'DB)'
         BE    TYPEND
         LA    R9,2
         B     FEHLER
TYPEND   EQU   *
         LA    R8,2(R8)
         LA    R1,3
         SR    R4,R1
         B     PARMVGL
*                  ACCESS-PARAMETER
*
PARMACC  EQU   *
         CLC   0(4,R8),=C'VSAM'
         BNE   ACCOSAM
         MVI   Y1AFLG,Y1AVS
         CLI   4(R8),X'5D'
         BE    ACCEND
         LA    R8,4(R8)
         LA    R1,L'DATFLD-1
         CLC   0(L'DATFLD,R8),DATFLD
         BNE   INDABFR
         MVI   Y1ARTD,Y1DDAT
         LA    R8,0(R1,R8)
         SR    R4,R1
         B     ACCEND1
INDABFR  EQU   *
         LA    R1,L'INDFLD-1
         CLC   0(L'INDFLD,R8),INDFLD
         BNE   CLUSTABF
         MVI   Y1ARTD,Y1DIND
         LA    R8,0(R1,R8)
         SR    R4,R1
         B     ACCEND1
CLUSTABF EQU   *
         LA    R1,L'CLUSTFLD-1
         CLC   0(L'CLUSTFLD,R8),CLUSTFLD
         BNE   FEHLPARM
         LA    R8,0(R1,R8)
         SR    R4,R1
         B     ACCEND1
FEHLPARM EQU   *
         LA    R9,2
         B     FEHLER
ACCOSAM  EQU   *
         CLC   0(5,R8),=C'OSAM)'
         BE    ACCEND
         LA    R9,2
         B     FEHLER
ACCEND   EQU   *
         LA    R8,4(R8)
ACCEND1  EQU   *
         LA    R1,5
         SR    R4,R1
         B     PARMVGL
*                  SIZE-PARAMETER
*
PARMSIZ  EQU   *
         SR    R1,R1
         SR    R2,R2
         TRT   0(4,R8),TAB1
         BNZ   PARMS1
         LA    R9,5                    EING-SIZE > 3 STELLEN
         B     FEHLER
PARMS1   EQU   *
         B     *(R2)
         LA    R9,6
         B     FEHLER                  EING-SIZE NICHT NUMERISCH
         SR    R1,R8
         SR    R4,R1
         BCTR  R1,0
         EX    R1,EXPACK2
         CVB   R10,PACKF1
         STH   R10,Y1ESIZ
         LA    R8,2(R1,R8)
         BCTR  R4,0
         SR    R1,R1
         SR    R2,R2
         TRT   0(5,R8),TAB
         BNZ   PARMS2
         LA    R9,7                    TAB-SIZE > 4 STELLEN
         B     FEHLER
PARMS2   EQU   *
         B     *(R2)
         LA    R9,8
         B     FEHLER                  TAB-SIZE NICHT NUMERISCH
         SR    R1,R8
         SR    R4,R1
         BCTR  R1,0
         EX    R1,EXPACK2
         CVB   R10,PACKF1
         STH   R10,Y1TSIZ
         LA    R8,1(R1,R8)
         BCTR  R4,0
         B     PARMVGL
*                  WTOR-PARAMETER
*
PARMWTOR EQU   *
         CLC   0(4,R8),=C'YES)'
         BNE   NOWTOR
         MVI   Y1WFLG,Y1WYES
         LA    R8,1(R8)
         BCTR  R4,0
         B     WTREND
NOWTOR   EQU   *
         CLC   0(3,R8),=C'NO)'
         BE    WTREND
         LA    R9,2
         B     FEHLER
WTREND   EQU   *
         LA    R8,2(R8)
         LA    R1,3
         SR    R4,R1
         B     PARMVGL
*                  EJBLK-PARAMETER
*
PARMASA  EQU   *
         CLC   0(3,R8),=C'NO)'
         BNE   YBLKASA
         MVI   Y1VORS,Y1NOV
         B     ASAEND
YBLKASA  EQU   *
         CLC   0(4,R8),=C'YES)'
         LA    R8,1(R8)
         BCTR  R4,0
         BE    ASAEND
         LA    R9,2
         B     FEHLER
ASAEND   EQU   *
         LA    R8,2(R8)
         LA    R1,3
         SR    R4,R1
         B     PARMVGL
*                  ENDE-ROUTINE
*
PARMEND  EQU   *
         CLI   Y1TYPF,Y1TIC
         BNE   ENDE
         CLI   Y1TFLG,Y1TYES
         BNE   ENDE
         LA    R9,69
FEHLER   EQU   *
         STH   R9,Y1MSGC
         CALL  ZS905FHL,((R3)),VL
         MVC   Y1COCO,=H'8'
ENDE     EQU   *
         XRETURN 0
         EJECT
*                  TABELLE DER EINGABEPARAMETER
*    MIT LAENGENANGABE
PRMFELD  DS    0CL1
         DC    AL1(L'LINFLD)
LINFLD   DC    C'LINES('
         DC    AL1(L'TERMFLD)
TERMFLD  DC    C'TERM('
         DC    AL1(L'TYPFLD)
TYPFLD   DC    C'DSTYPE('
         DC    AL1(L'ACCFLD)
ACCFLD   DC    C'ACCESS('
         DC    AL1(L'SIZFLD)
SIZFLD   DC    C'SIZE('
         DC    AL1(L'WTOFLD)
WTOFLD   DC    C'WTOR('
         DC    AL1(L'ASAFLD)
ASAFLD   DC    C'EJBLK('
         DC    AL1(0)
*                  TABELLE : VERZWEIGADRESSEN
*                  DER EINGABEPARAMETER
PRMADR   DS    0F
         DC    A(PARMLIN)
         DC    A(PARMTERM)
         DC    A(PARMTYP)
         DC    A(PARMACC)
         DC    A(PARMSIZ)
         DC    A(PARMWTOR)
         DC    A(PARMASA)
         SPACE 2
CLUSTFLD DC    C',CLUSTER)'
DATFLD   DC    C',DATEN)'
INDFLD   DC    C',INDEX)'
EXCLC1   CLC   0(0,R5),0(R8)
EXPACK1  PACK  Y1LCNT,0(0,R8)
EXPACK2  PACK  PACKF1,0(0,R8)
         CNOP  0,8
PACKF1   DS    PL8
MASKE    DC    XL6'402020202120'
TAB      DS    0CL256
         DC    256XL1'04'
         ORG   TAB+C')'
         DC    XL1'0C'
         ORG   TAB+C'0'
         DC    10XL1'00'
         ORG
TAB1     DS    0CL256
         DC    256XL1'04'
         ORG   TAB1+C','
         DC    XL1'0C'
         ORG   TAB1+C'0'
         DC    10XL1'00'
         ORG
ZEIL1    DS    0CL133
         DC    CL1'1'
         DC    C'ZS905 L01     DB-DUMP-UTILITY     '
DATUM    DC    CL10' '
         DC    CL5' '
ZEIT     DC    CL8' '
         DC    C'     SEITE'
SEITNR   DC    CL6' '
         DC    CL59' '
ZEIL2    DS    0CL133
         DC    CL1'-'
         DC    C'     KONTROLLSTATEMENTS UND FEHLERMELDUNGEN :'
         DC    CL87' '
ZEIL3    DS    0CL133
         DC    CL1' '
         DC    45CL1'-'
         DC    CL87' '
         LTORG
         EJECT
Y1       ZS905COM
         END
