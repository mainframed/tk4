XABEND   CSECT
         REG
         ENTRY LUDUMP
         ENTRY LUABEND
         ENTRY ABEND
         ENTRY DUMP
         USING DSA,R13
LUDUMP   EQU   *
DUMP     EQU   *
         XSAVE R12,,XABEND,DSAL
         MVC   COMPCODE,=X'800003E7'
         B     PROC
ABEND    EQU   *
LUABEND  EQU   *
         XSAVE R12,,,DSAL
         L     R12,=A(DUMP)
         DROP  R12
         USING DUMP,R12
         MVC   COMPCODE,=X'000003E6'
PROC     EQU   *
         LR    R8,R1
         WTO   MF=(E,MESS1)
         MVC   MESS(70),MESS2
         L     R2,16
         L     R2,0(R2)
         L     R2,4(R2)
         L     R2,0(R2)
         L     R2,12(R2)   A(CDE)
         MVC   MESS+20(8),8(R2)
         WTO   MF=(E,MESS)
         LTR   R8,R8
         BZ    NOMSG
         MVC   MESS+2(2),=X'8000'
NXTMSG   EQU   *
         TM    0(R8),X'80'
         BO    NOMSG
         L     R10,0(R8)
         L     R10,0(R10)
         C     R10,=F'121'
         BNH   NOTLONG
         LA    R10,121
NOTLONG  EQU   *
         LA    R9,4(R10)
         STH   R9,MESS
         BCTR  R10,0
         L     R9,4(R8)
         EX    R10,MVCEX
         LA    R11,MESS
         LA    R10,5(R10)
         AR    R10,R11
         MVC   0(4,R10),=X'00000020'
         WTO   MF=(E,MESS)
         TM    4(R8),X'80'
         BO    NOMSG
         LA    R8,8(R8)
         B     NXTMSG
MVCEX    MVC   MESS+4(0),0(R9)
NOMSG    EQU   *
         WTO   MF=(E,MESS1)
         L     R1,COMPCODE
         L     R13,4(R13)
         LM    R14,R15,12(R13)
         LM    R2,R12,28(R13)
         L     R0,20(R13)
         SVC   13
         EJECT
MESS1    WTO   ' ',ROUTCDE=11,MF=L,MCSFLAG=HRDCPY
MESS2    WTO   'DUMP01I PROGRAM 12345678 SCHEDULED FOR ABEND BY LUDUMP/*
               LUABEND',ROUTCDE=11,MF=L,MCSFLAG=HRDCPY
         LTORG
         SPACE 3
DSA      DSECT
         DS    18F
COMPCODE DS    F
MESS     DS    CL132
DSAL     EQU   *-DSA
         END
