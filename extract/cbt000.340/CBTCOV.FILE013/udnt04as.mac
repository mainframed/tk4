SMFPLOT  CSECT
         PRINT NOGEN
         BASER 1
START    EQU   *
         REG
         ST    R1,APARM
         LR    R6,R1
         USING DPARM,R6
         L   R4,PLOTNORM
         CVD   R4,DWORT
         MVC   HD1MAX,SCHAB1
         ED    HD1MAX,DWORT+4
         ZAP   HDCNT,=P'99'
         ZAP   ERRCNT,=P'0'
         NI    NOP1+1,X'0F'
         L     R2,ADCBPLOT
         MVC   33(3,R2),EODATA+1
READPLOT EQU   *
         L     R2,ADCBPLOT
         GET   (R2),PLOTIN
         CLC   PLOTLOW,=F'0'
         BE    NOLIMIT
         CLC   PLOTLOW,PLTIME
         BH    READPLOT
         CLC   PLOTHIGH,PLTIME
         BL    READPLOT
NOLIMIT  EQU   *
         CLI   PLID,X'01'
         BE    IDFOUND
         LA    R1,PLOTDEV
CLCID    EQU   *
         CLC   0(2,R1),=X'FFFF'
         BE    READPLOT
         CLC   0(2,R1),PLID+3
         BE    IDFOUND
         LA    R1,2(R1)
         B     CLCID
IDFOUND  EQU   *
NOP1     NOP   NOTFIRST
         OI    NOP1+1,X'F0'
         MVI   HD0TEXT,C' '
         MVC   HD0TEXT+1(64),HD0TEXT
         MVC   ZPLID,PLID
         MVC   ZPLDATE,PLDATE
NOTFIRST EQU   *
         CLC   ZPLID,PLID
         BNE   NEWPAGE
         CLC   ZPLDATE,PLDATE
         BE    SAME
         L     R1,PLTIME
         C     R1,=F'360000'
         BL    CONTIG
NEWPAGE  EQU   *
         ZAP   HDCNT,=P'99'
         MVC   ZPLID,PLID
CONTIG   EQU   *
         MVC   ZPLDATE,PLDATE
SAME     EQU   *
         BAL   R9,PLOT
         B     READPLOT
         EJECT
PLOT     EQU   *
         CP    HDCNT,MAXLINE
         BNH   NOHD1
         TM    ZPLID,X'01'
         BO    CHAR
         UNPK  HD0ID,PLID+3(3)
         TR    HD0ID,TRANSTAB-X'F0'
         MVI   HD0ID+4,C' '
         B     HEXA
CHAR     EQU   *
         MVC   HD0ID,PLID+1
HEXA     EQU   *
         LH    R1,PLTXTLN
         BCTR  R1,0
         EX    R1,MVCTEXT
         B     *+10
MVCTEXT  MVC   HD0TEXT(0),PLTXT
         MVC   HD1DATE,SCHAB2
         ED    HD1DATE,PLDATE+1
         L     R2,ADCBPRNT
         PUT   (R2),HD0-1
         PUT   (R2),HD0
         PUT   (R2),HD1
         ZAP   HDCNT,=P'0'
         MVI   HD0TEXT,X'40'
         MVC   HD0TEXT+1(64),HD0TEXT
NOHD1    EQU   *
         L     R4,PLVALUE
         SRDA  R4,32
         MH    R5,=H'100'
         D     R4,PLOTNORM
         LTR   R5,R5
         BM    ERROR
         CH    R5,=H'100'
         BH    ERROR
         LA    R1,HD2DATA
         LR    R2,R1
         AR    R1,R5
         SR    R1,R2
         LTR   R1,R1
         BZ    NOBCTR
         BCTR  R1,0
NOBCTR   EQU   *
         EX    R1,ASTERIX
         L     R1,PLTIME
         BAL   R14,UCONV
         CVD   R1,DWORT
         MVC   HD2TIME,SCHAB3
         ED    HD2TIME,DWORT+4
         MVC   HD2TIME+7(3),=3X'40'
         L     R2,ADCBPRNT
         PUT   (R2),HD2
         MVI   HD2DATA,X'40'
         MVC   HD2DATA+1(99),HD2DATA
         AP    HDCNT,=P'1'
         BR    R9
ASTERIX  MVC   HD2DATA(0),STERNE
ERROR    EQU   *
         AP    ERRCNT,=P'1'
         CP    ERRCNT,=P'10'
         BL    ERRWTO
         ABEND 10,DUMP
ERRWTO   EQU   *
         WTO   'PLOT FAILURE',ROUTCDE=(13)
         L     R14,M0001M00+12
         BR    R14
EOD      EQU   *
         B     END99
         EJECT
***********************************************************************
*        U P R O S
***********************************************************************
*        UVONV KONVERTIERT HUNDERTSTEL SEKUNDEN NACH STD/MIN/SEK
*        ZUR DRUCKAUFBEREITUNG
*        EINGABE:R1 ENTHAELT DIE ZEIT BINAER IN HUNDERTSTEL SEKUNDEN
*        AUSGABE: R1 BINAER IN DER FORM  0HHMMSS0
***********************************************************************
UCONV    EQU   *
         STM   2,6,SVUCONV
         LR    2,1
         SRDA  2,32
         D     2,=F'100'                HUNDERTSTEL SEK. -----> SEK.
         XR    2,2                      SEK. NACH R3 , LOESCHEN R2
         LA    4,60                     R4=60
         DR    2,4                      SEK:60 =MIN
         LR    6,2                      SEK NACH R6
         XR    2,2                      R2 LOESCHEN
         DR    2,4                      MIN:60 =STD
         MH    2,=H'100'                MIN*100 (STELLEN-AUSRICHTUNG)
         ALR   6,2                      ADD MINUTEN UND SEKUNDEN
         MH    3,=H'10000'              STD*10000 (STELLEN-AUSRICHTUNG)
         ALR   6,3                      ADD STD,MIN,SEK
         LR    1,6
         LM    2,6,SVUCONV
         BR    14
SVUCONV  DC    5F'0'
***********************************************************************
***********************************************************************
         EJECT
***********************************************************************
*        DEFINITIONEN
***********************************************************************
***********************************************************************
         DC    X'8B'                   SKIP TO CHAN 1 IMMEDIATELY
HD0      DS    0CL133
         DC    X'09'
         DC    CL32'DYNAMIT NOBEL-AG / SYSTEMTECHNIK'
         DC    CL24' '
HD0ID    DS    CL5
         DC    6X'40'
HD0TEXT  DS    CL65
*
HD1      DS    0CL133
HD1VZ    DC    X'09'
HD1DATE  DC    CL7' '
         DC    CL6' '
         DC    CL10'---------1'
         DC    CL10'---------2'
         DC    CL10'---------3'
         DC    CL10'---------4'
         DC    CL10'---------5'
         DC    CL10'---------6'
         DC    CL10'---------7'
         DC    CL10'---------8'
         DC    CL10'---------9'
         DC    CL10'---------0'
HD1MAX   DS    CL10
         DC    CL9'=100%'
*
HD2      DS    0CL133
HD2VZ    DC    X'09'
HD2TIME  DS    CL10
         DC    C'  '
         DC    C'I'
HD2DATA  DC    101C' '
         DC    C'I'
         DC    17C' '
*
***********************************************************************
*        EINGABEBEREICH PLOTSAETZE
***********************************************************************
         CNOP  2,4
PLOTIN   DS    0CL18
PLID     DS    CL6
PLDATE   DS    CL4
PLTIME   DS    CL4
PLVALUE  DS    CL4
*
***********************************************************************
*        ZWISCHENSPEICHER PLOTSATZWERTE
***********************************************************************
ZPLID    DS    CL6
ZPLDATE  DS    CL4
***********************************************************************
*        SCALARE FELDER
***********************************************************************
SCHAB1   DC    X'40204B2020204B202120'
SCHAB2   DC    X'4020204B202120'
SCHAB3   DC    X'402120204B20204B2020'
DWORT    DS    D
HDCNT    DC    PL2'99'
APARM    DS    F
MAXLINE  DC    PL2'64'
TRANSTAB DC    C'0123456789ABCDEF'
STERNE   DC    100CL1'*'
EODATA   DC    A(EOD)
ERRCNT   DC    PL2'0'
***********************************************************************
         LTORG
***********************************************************************
DPARM    DSECT
ADCBPLOT DS    F
ADCBPRNT DS    F
PLOTLOW  DS    F
PLOTHIGH DS    F
PLOTDEV  DS    11D
PLOTNORM DS    F
PLTXTLN  DS    H
PLTXT    DS    0C
         END
