PRT      TITLE 'DRUCKMODUL   UHYP11AS =  MODPRT '
MODPRT   START
         PRINT DATA
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 5
*        START UND STEUERUNG
         SPACE 5
         SAVE  (14,12)
         BALR  R4,0
         USING *,R4
         XR    R1,R2
         XR    R2,R1
         XR    R1,R2
         USING IHADCB,R2
         ST    R13,SAVEAREA+4
         LR    R12,R13
         LA    R13,SAVEAREA
         ST    R13,8(R12)
         L     R11,0(R1)
         LH    R12,4(R1)
         UNPK  VD1,1(4,R1)
         TR    VD1+1(6),K1-240
         MVC   V2+1(6),VD1+1
         SH    R1,KH14
         LH    R0,0(R1)
         CVD   R0,VD1
         UNPK  VD1(5),VD1+6(3)
         MVC   K9+1(2),VD1+1
         MVC   V3+1(4),K9
         TM    DCBOFLGS,X'10'
         BO    B1
         OPEN  ((R2),(OUTPUT))
         TM    DCBOFLGS,X'10'
         BZ    A1
         B     B1
A2       L     R13,SAVEAREA+4
         RETURN (14,12)
A1       WTO   '*** MODPRT 3235 DRUCKER DD-KARTE FEHLT'
         B     A2
         SPACE 5
*        BEREICH DRUCKEN
         SPACE 5
B1       SR    R3,R3
B6       LA    R9,V2+10
         LA    R10,V2+91
B4       LTR   R12,R12
         BZ    B5
         B     BA1
B2       CH    R12,KH4
         BL    B3
         UNPK  VD1(9),0(5,R11)
         TR    VD1,K1-240
         MVC   132(8,R9),VD1
         MVC   132(4,R10),0(R11)
         LA    R9,9(R9)
        LA    R10,4(R10)
         LA    R11,4(R11)
         SH    R12,KH4
         LA    R3,4(R3)
         C     R10,KF1
         BL    B4
B5       MVI   V2+85,C'*'
         TR    V2+223(39),K5
         BAL   R14,UA1
         LTR   R12,R12
         BNE   B6
         B     A2
B3       BCTR  R12,0
         EX    R12,K4
         LA    R0,1(R12)
         LR    R1,R0
         SLL   R0,1
         SLL   R1,28
         SLDL  R0,4
         LR    R1,R0
         EX    R1,K6
         SRL   R1,4
         BCTR  R1,0
         EX    R1,K7
         EX    R1,K8
         SR    R12,R12
         B     B5
         SPACE 5
*        RELATIVE BEREICHSADRESSE
         SPACE 5
BA1      CVD   R3,VD2
         LA    R1,VD1+7
         LR    R8,R1
         MVC   VD1+4(4),K2
         EDMK  VD1+4(4),VD2+6
         SR    R8,R1
         EX    R8,K3
         MVC   0(3,R10),0(R9)
         B     B2
         SPACE 5
*        DRUCKZEILEN AUSGEBEN
         SPACE 5
UA1      STM   R14,R1,UAVF1
         MVI   V2,X'09'
         PUT   (R2),V1
         MVC   V2+1(132),V3
         MVI   V2,X'11'
         PUT   (R2),V1
         MVI   V2+1,C' '
         MVC   V2+2(132),V2+1
         MVC   V3,V2+1
         LM    R14,R1,UAVF1
         BR    R14
UAVF1    DC    4F'0'
         SPACE 5
SAVEAREA DC    18F'0'
         SPACE 5
V1       DC    H'137'
         DC    H'0'
V2       DC    X'09'
         DC    CL132' '
V3       DC    CL132' '
         SPACE 5
VD1      DS    D
         DS    CL1
VD2      DS    D
         SPACE 5
K1       DC    C'0123456789ABCDEF'
K2       DC    X'40202120'
K3       MVC   0(1,R9),0(R1)
K4       MVC   132(1,R10),0(R11)
K5       DC    64C'.'
         DC    C' '
         DC    13C'.'
         DC    C'+'
         DC    13C'.'
         DC    C'*'
         DC    3C'.'
         DC    C'-/'
         DC    9C'.'
         DC    C','
         DC    18C'.'
         DC    C'='
         DC    66C'.'
         DC    C'ABCDEFGHI'
         DC    7C'.'
         DC    C'JKLMNOPQR'
         DC    8C'.'
         DC    C'STUVWXYZ'
         DC    6C'.'
         DC    C'0123456789'
         DC    6C'.'
K6       UNPK  VD1(1),0(1,R11)
K7       TR    VD1(1),K1-240
K8       MVC   132(1,R9),VD1
K9       DC    C'(  )'
         SPACE 5
KH4      DC    H'4'
KH14     DC    H'14'
         SPACE 5
KF1     DC     A(V2+123)
         SPACE 5
         DCBD  DSORG=PS
         END   MODPRT
