DASDI    START
         XSAVE R12,SVA,DASDI
         REG
         L     R1,0(R1)
         LH    R2,0(R1)
         LTR   R2,R2
         BZ    NOPARM
         CLC   2(7,R1),=CL7'PASSES='
         BNE   NOPARM
         MVC   ANALCD+21(7),=CL7'CHK(1)'
         MVC   ANALCD+25(1),9(R1)
NOPARM   EQU   *
WTOR     EQU   *
         XC    ECB,ECB
         MVC   REPLY,=CL20' '
         WTOR  'DASDI00 REPLY FUNCTION, CUU AND VOLSER',ROUTCDE=(2,4), *
               REPLY,20,ECB
         WAIT  ECB=ECB
         OC    REPLY,=CL20' '
         CLI   REPLY+1,C','
         BNE   ERROR
         CLI   REPLY+5,C','
         BNE   ERROR
         CLI   REPLY,C'L'
         BNE   NOLAB
         MVC   CARD,LABELCD
         MVC   CARD+11(3),REPLY+2
         MVC   CARD+26(6),REPLY+6
         B     FUCB
NOLAB    EQU   *
         CLI   REPLY,C'A'
         BNE   ERROR
         CLC   REPLY+12(5),=CL5',MSS '
         BNE   NOMSS
         MVC   CARD,MSSCARD
         MVC   CARD+11(3),REPLY+2
         MVC   CARD+40(6),REPLY+6
         B     FUCB
NOMSS    EQU   *
         MVC   CARD,ANALCD
         MVC   CARD+11(3),REPLY+2
         MVC   CARD+60(6),REPLY+6
FUCB     EQU   *
         L     R4,16
         L     R4,40(R4)
         SR    R5,R5
NEXTA    EQU   *
         BAL   R7,SEARCH
         CLC   REPLY+2(3),13(R5)
         BNE   NEXTA
         BE    UCBFOUND
SEARCH   EQU   *
         ICM   R5,3,0(R4)
         LA    R4,2(R4)
         BZ    SEARCH
         CLC   0(2,R4),=X'FFFF'
         BNER  R7
NOTFOUND EQU   *
         WTO   'DASDI02 SPECIFIED CUU NOT FOUND',ROUTCDE=(2,4)
         B     WTOR
UCBFOUND EQU   *
         CLC   18(2,R5),=XL2'200B'
         BE    DEVOK
         CLC   18(2,R5),=XL2'2009'
         BE    DEVOK
         WTO   'DASDI04 SPECIFIED CUU IS INVALID DEVTYPE',ROUTCDE=(2,4)
         B     WTOR
DEVOK    EQU   *
         CLC   REPLY+12(5),=CL5',MSS '
         BE    PUT
         CLI   REPLY,C'L'
         BE    PUT
         XC    ECB,ECB
         MVC   REPLY,=CL20' '
         WTOR  'DASDI03 REPLY VTOC-START AND EXTENT (CYL,TRK)',        *
               REPLY,20,ECB,ROUTCDE=(2,4)
         WAIT  ECB=ECB
         CLI   REPLY+3,C','
         BE    NOERR
         WTO   'DASDI03 ERROR IN PARAMETER, REPEAT REPLY',ROUTCDE=(2,4)
         B     DEVOK
NOERR    EQU   *
         PACK  ECB(5),REPLY(4)
         XC    DBLWD,DBLWD
         L     R1,ECB
         SLL   R1,4
         ST    R1,DBLWD+4
         OI    DBLWD+7,X'0C'
         CVB   R1,DBLWD
         ST    R1,DBLWD
         UNPK  ECB,DBLWD+2(3)
         TR    ECB(3),TABLE
         MVC   CARD+43(3),ECB
         MVC   CARD+49(3),REPLY+4
         CLC   REPLY(3),=CL3'000'
         BNE   PUT
         MVI   CARD+47,C'1'
PUT      EQU   *
         MVC   CMD+19(3),CARD+11
         LA    R1,CMD
         SVC   249
         OPEN  (SYSIN,(OUTPUT))
         PUT   SYSIN,CARD
         CLOSE SYSIN
         LINK  EP=ICKDSF,PARAM=(OPTLIST),VL=1
         LR    R6,R15
         LR    R1,R5
         LA    R15,SVCROUT
         SVC   250
         LTR   R6,R6
         BZ    RETURN
         WTO   'DASDI01 ICKDSF RETURN-CODE NOT ZERO',ROUTCDE=(2,4)
RETURN   EQU   *
         XRETURN 0
ERROR    EQU   *
         WTO   'DASDI03 ERROR IN PARAMETER, REPEAD REPLY',ROUTCDE=(2,4)
         B     WTOR
SVCROUT  EQU   *
         MVC   28(6,R1),=6XL1'00'
         BR    R14
         CNOP  2,4
OPTLIST  DC    H'0'
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(PM),RECFM=F,BLKSIZE=80,    *
               LRECL=80,BUFNO=1
ANALCD   DC    CL80' INIT UNIT(CUU) NVFY NOCHECK     RCLM VTOC(000:0,00*
               0) VOLID(123456)'
LABELCD  DC    CL80' RFMT UNIT(CUU)     VOLID(123456) NVFY'
MSSCARD  DC    CL80' INIT UNIT(CUU) OWNER(BASFAG67LU) VOLID(123456) MIM*
               IC(MSS) CHK(1) NVFY'
CMD      DS    0F
         DC    H'22,0'
         DC    CL18'F X,IOS,SIMINT,CUU'
ECB      DS    F
         DS    X
DBLWD    DS    D
REPLY    DS    CL20
CARD     DS    CL80
TABLE    DC    XL256'00'
         ORG   TABLE+X'F0'
         DC    C'0123456789ABCDEF'
         ORG
         END
