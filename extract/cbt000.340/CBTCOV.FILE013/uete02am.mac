** ADD NAME=SETXP
         MACRO
&NAME    SETXP
.*  THIS MACRO BUILDS A BLOCK OF TEXT POINTERS FOR DYNAMIC ALLOCATION.
.*  IT HAS A VARIABLE NUMBER OF POSITIONAL PARAMETERS
.*  THE FIRST PARAMETER IS THE LABEL OF A WORK AREA WHICH WILL
.*  CONTAIN THE ADDRESSES OF THE REMAINING PARAMETERS. IT MUST BE
.*  DEFINED AS AT LEAST ENOUGH FULLWORDS TO HOLD THEM.
.*  THE REMAINING PARAMETERS ARE THE LABELS OF TEXT UNITS WHICH YOU
.*  MUST DEFINE.
.*  THIS IS AN EXAMPLE OF DYNAMIC ALLOCATION USING THE MACROS
.*  SETXP AND SETRB:
.*       SETXP   TEXTPTRS,DDNAME,DSNAME,OLD,KEEP1,KEEP2
.*       SETRB   DSNA,RBPTR,TEXTPTRS
.*       DYNALLOC
.*
.*TEXTPTRS DS   5A(0)
.*RBPTR    DS   6A(0)
.*
.*DDNAME   DC   0H'0',X'000300010006',C'DDNAME'
.*DSNAME   DC   0H'0',X'00020001000C',C'DATASET.NAME'
.*OLD      DC   0H'0',X'00040001000101'
.*KEEP1    DC   0H'0',X'00050001000108'
.*KEEP2    DC   0H'0',X'00060001000108'
.*
         LCLA  &LCLA1,&LCLA2
&NAME    LA    15,&SYSLIST(1)      GET ADDR OF POINTER BLOCK
&LCLA1   SETA  2
&LCLA2   SETA  0
.P1      AIF   (T'&SYSLIST(&LCLA1) EQ 'O').SETVL
         LA    1,&SYSLIST(&LCLA1)  GET ADDR OF TEXT UNIT
         ST    1,&LCLA2.(15)       SET UP POINTER
&LCLA2   SETA  &LCLA2+4
&LCLA1   SETA  &LCLA1+1
         AGO   .P1
.SETVL   ANOP
&LCLA2   SETA  &LCLA2-4
         MVI   &LCLA2.(15),X'80'  SET VL BIT
         MEND
** ADD NAME=SETRB
         MACRO
&NAME    SETRB &VERB,&RBPTR,&TXTPTRS
.* THIS MACRO BUILDS THE REQUEST BLOCK AND POINTER FOR DYNAMIC
.* ALLOCATION.
.* THERE ARE THREE POSITIONAL PARAMETERS.
.* PARM 1 IS THE TYPE OF ALLOCATION IE
.*     DSNA  -DATA SET NAME ALLOCATION
.*     UNAL  -DEALLOCATION
.*     CONC  -CONCATENATION
.*     DECO  -DECONCATENATION
.*     REMI  -REMOVE IN USE ATTRIBUTE
.*     DDNA  -DDNAME ALLOCATION
.*     INFO  -REQUEST FOR INFORMATION
.* PARM 2 IS THE LABEL OF A 6 WORD WORK AREA
.* PARM 3 IS THE LABEL OF A WORK AREA OF TEXT POINTERS
.*        (THIS AREA IS FILLED BY MACRO SETXP)
.*
.* WHEN THIS MACRO HAS BEEN PERFORMED
.* REGISTER 1 POINTS TO THE RBPOINTER  AND THE MACRO DYNALLOC
.* CAN BE ISSUED.
&NAME    LA    1,&RBPTR
         XC    0(24,1),0(1)  CLEAR AREA
         LA    15,&RBPTR+4
         ST    15,0(1)
         MVI   0(1),X'80'  SET VL BIT
         MVI   4(1),X'14'  LENGTH
         AIF   ('&VERB' NE 'DSNA').P1
         MVI   5(1),X'01'   DSN ALLOCATION
         AGO   .P99
.P1      AIF   ('&VERB' NE 'UNAL').P2
         MVI   5(1),X'02'   UNALLOCATION
         AGO   .P99
.P2      AIF   ('&VERB' NE 'CONC').P3
         MVI   5(1),X'03'   CONATENATION
         AGO   .P99
.P3      AIF   ('&VERB' NE 'DECO').P4
         MVI   5(1),X'04'   DECONCATENATION
         AGO   .P99
.P4      AIF   ('&VERB' NE 'REMI').P5
         MVI   5(1),X'05'   REMOVE IN USE ATTRIBUTE
         AGO   .P99
.P5      AIF   ('&VERB' NE 'DDNA').P6
         MVI   5(1),X'06'   DDNAME ALLOCATION
         AGO   .P99
.P6      AIF   ('&VERB' NE 'INFO').P99
         MVI   5(1),X'07'   REQUEST FOR INFORMATION
.P99     LA    15,&TXTPTRS  GET ADDR OF TEXT POINTERS
         ST    15,12(1)     PUT INTO REQUEST BLOCK
         MEND
** ADD NAME=SPFEM
         MACRO
&NAME    SPFEM
.* WITH THIS MACRO YOU CAN CALL UP THE SPF MESSAGE HANDLER
.* IT HAS A VARIABLE NUMBER OF POSITIONAL PARAMETERS
.* THE FIRST PARM IS THE LABEL OF A NUMBER OF FULLWORDS
.* WHICH WILL BECOME THE CALLING PARAMETER LIST
.* YOU MUST DEFINE IT LARGE ENOUGH TO TAKE THE ADDRESSES OF
.* THE REST OF THE PARAMETERS
.* THE SECOND PARAMETER IS THE LABEL OF A FIELD CONTAINING THE NAME OF
.* THE ERROR MESSAGE TO BE DISPLAYED.
.* THE THIRD PARAMETER IS THE LABEL OF A WORD IN WHICH A BINARY
.* NUMBER HAS BEEN PLACED. THIS NUMBER INDICATES THE FIELD
.* WHERE THE CURSOR IS TO BE POSITIONED AND IS THE NUMBER OF
.* THE PARAMETER PASSED TO THE MENU DISPLAY MODULE CDISPL.
.* THE REMAINING PARAMETERS ARE THE LABELS OF FIELDS AS DEFINED
.* IN THE MESSAGE TO BE DISPLAYED.
.*       E.G.
.*             SPFEM  WORDS,MSGN,W1,DSN
.*
.*WORDS   DS  10F
.*MSGN    DC  C'E311'   ERROR MESSAGE NAME
.*W1      DC  A(1)      POSITION CURSOR ON FIRST FIELD
.*DSN     DC  CL44'DATASET.NAME'  INSERT THIS INTO MESSAGE
.*
         LCLA  &LCLA1,&LCLA2
&NAME    L     15,4(13)  GET ADDR OF OLD SAVEAREA
         L     1,24(15)  GET OLD REG 1
         L     14,0(1)   GET ADDR OF TLD
         LA    1,&SYSLIST(1)  POINT AT POINTER BLOCK
         ST    14,0(1)   PUT IN TLD ADDR
&LCLA1   SETA  2
&LCLA2   SETA  4
.P1      AIF   (T'&SYSLIST(&LCLA1) EQ 'O').SETVL
         LA    15,&SYSLIST(&LCLA1)  GET ADDR OF TEXT UNIT
         ST    15,&LCLA2.(1)        SET UP POINTER
&LCLA2   SETA  &LCLA2+4
&LCLA1   SETA  &LCLA1+1
         AGO   .P1
.SETVL   ANOP
&LCLA2   SETA  &LCLA2-4
         MVI   &LCLA2.(1),X'80'  SET VL BIT
         L     15,8(14)      GET ADDR OF THE TSI
         L     15,12(15)     GET THE ADDR OF THE TSC
         L     15,24(15)     GET THE EP OF MERR
         BALR  14,15
         MEND
** ADD NAME=SPFMH
         MACRO
&NAME    SPFMH
.* WITH THIS MACRO YOU CAN CALL UP THE SPF MENU HANDLER
.* IT HAS A VARIABLE NUMBER OF PARAMETERS
.* PARM 1 ADDRESS OF A WORKAREA CONTAINING ENOUGH FULLWORDS TO HOLD
.*        THE ADDRESSES OF THE REMAINING PARMS
.* PARM 2 LABEL OF THE 8 BYTE LONG NAME OF A MENU
.* PARM 3 LABEL OF A FULLWORD CONTAINING OPTIONS (1 2 OR 3)
.* PARM 4 - N  LABELS OF FIELDS WHICH CONTAIN DATA TO BE DISPLAYED OR
.*        WHICH ARE TO RECEIVE DATA.
.*
.*    FOR MORE DETAILS ON HOW TO USE THE MODULE CDISPL
.*    SEE THE SPF LOGIC MANUAL
.*
         LCLA  &LCLA1,&LCLA2
&NAME    L     15,4(13)  GET ADDR OF OLD SAVEAREA
         L     1,24(15)  GET OLD REG 1
         L     14,0(1)   GET ADDR OF TLD
         LA    1,&SYSLIST(1)  POINT AT POINTER BLOCK
         ST    14,0(1)   PUT IN TLD ADDR
&LCLA1   SETA  2
&LCLA2   SETA  4
.P1      AIF   (T'&SYSLIST(&LCLA1) EQ 'O').SETVL
         LA    15,&SYSLIST(&LCLA1)  GET ADDR OF TEXT UNIT
         ST    15,&LCLA2.(1)        SET UP POINTER
&LCLA2   SETA  &LCLA2+4
&LCLA1   SETA  &LCLA1+1
         AGO   .P1
.SETVL   ANOP
&LCLA2   SETA  &LCLA2-4
         MVI   &LCLA2.(1),X'80'  SET VL BIT
         L     15,8(14)      GET ADDR OF THE TSI
         L     15,12(15)     GET THE ADDR OF THE TSC
         L     15,20(15)     GET THE EP OF MHA
         BALR  14,15
         MEND
** ADD NAME=REQUATEN
         MACRO
         REQUATEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
BASE     EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
         MEND
** ADD NAME=INIT
         MACRO
&NAME    INIT  &CXD=0
         LCLA  &LA1
         LCLC  &LC1
&LC1     SETC  '&NAME'
         AIF   (N'&SYSLIST(2) EQ 2 AND '&CXD' EQ '0').ONESECT
         AIF   ('&CXD' EQ '0').MOVECSC
         AGO   .MANYSEX
.ONESECT ANOP
         L     0,=A(&SYSLIST(2,2)) GET LENGTH OF AREA
         AGO   .GETMAIN
.MANYSEX ANOP
&LC1     L     0,&CXD
.GETMAIN GETMAIN R,LV=(0)
         LR    &SYSLIST(1),1
&LC1     SETC  '        '
.MOVECSC ANOP
&LA1     SETA  2
.P1      AIF   (T'&SYSLIST(&LA1)   EQ 'O').E
         AIF   (N'&SYSLIST(&LA1) EQ 3).QCON
         LR    14,&SYSLIST(1)      POINT TO TARGET
         AGO   .NOQCON
.QCON    ANOP
&LC1     L     14,&SYSLIST(&LA1,3)    GET Q CONSTANT
&LC1     SETC  '        '
         AR    14,&SYSLIST(1)      POINT TO TARGET
.NOQCON  L     15,=A(&SYSLIST(&LA1,2))  LENGTH
         LR    1,15
         L     0,=A(&SYSLIST(&LA1,1)) FROM ADDRESS
         MVCL  14,0
&LA1     SETA  &LA1+1
         AGO   .P1
.E       ANOP
         MEND
** ADD NAME=LCNVE
         MACRO
&NAME    LCNVE &Y,&M,&N,&O,&P,&Q,&R,&S,&T,&U,&V,&W,&X
*CHANGE LEVEL 009      VOM 14.07.76
         GBLC  &LCNVGC1(140)
         LCLA  &KA1,&KA2,&KA3,&KA4,&KA5
         LCLC  &KC1,&A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&L
         LCLB  &KB1,&KB2
         AIF   (T'&Y NE 'O').R1
&KB2     SETB  1
.R1      ANOP
&KA1     SETA  1
&KA2     SETA  2
.G1      AIF   ('&LCNVGC1(&KA1)' EQ '').G2
         AIF   ('&LCNVGC1(&KA1)' EQ '&SYSECT').G3
&KA1     SETA  &KA1+14
&KA2     SETA  &KA2+14
         AIF   (&KA1  LT  140).G1
         MNOTE 8,'ZU VIELE CSECTS'
         MEXIT
.G2      ANOP
         AIF   (&KB2 NE 1).R2
         MNOTE 4,'PARAMETER FEHLEN'
         MEXIT
.R2      ANOP
ASTX&SYSNDX EQU   &Y
&KA4     SETA  &KA1
&LCNVGC1(&KA1)  SETC  '&SYSECT'
&LCNVGC1(&KA2) SETC 'ASTX&SYSNDX'
&KA3     SETA  &KA1+2
&KA2     SETA  2
.RP1     ANOP
&LCNVGC1(&KA3) SETC  '&SYSLIST(&KA2)'
&KA2     SETA  &KA2+1
&KA3     SETA  &KA3+1
         AIF   (&KA2 LT 14).RP1
         AGO   .G4
.G3      ANOP
&KA4     SETA  &KA1
         AIF   (&KB2).G4
         MNOTE *,'PARAMETERS SUPERFLUOUS'
.G4      ANOP
&KA5     SETA  &KA4
&KA4     SETA  &KA1+1
&KC1     SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
&KA1     SETA  1
&A       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&B       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&C       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&D       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&E       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&F       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&G       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&H       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&I       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&J       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&K       SETC  '&LCNVGC1(&KA4)'
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
&KA4     SETA  &KA4+1
         AIF   ('&LCNVGC1(&KA4)' EQ '').RAUS
&KA1     SETA  &KA1+1
.RAUS    ANOP
         AIF   (&KA1 EQ 1).U1
         AIF   (&KA1 EQ  2).U2
         AIF   (&KA1 EQ  3).U3
         AIF   (&KA1 EQ  4).U4
         AIF   (&KA1 EQ  5).U5
         AIF   (&KA1 EQ  6).U6
         AIF   (&KA1 EQ  7).U7
         AIF   (&KA1 EQ  8).U8
         AIF   (&KA1 EQ  9).U9
         AIF   (&KA1 EQ 10).U10
         AIF   (&KA1 EQ 11).U11
         AIF   (&KA1 EQ 12).U12
         AGO   .U12
.U1      USING &KC1,&A
         AGO   .U13
.U2      USING &KC1,&A,&B
         AGO   .U13
.U3      USING &KC1,&A,&B,&C
         AGO   .U13
.U4      USING &KC1,&A,&B,&C,&D
         AGO   .U13
.U5      USING &KC1,&A,&B,&C,&D,&E
         AGO   .U13
.U6      USING &KC1,&A,&B,&C,&D,&E,&F
         AGO   .U13
.U7      USING &KC1,&A,&B,&C,&D,&E,&F,&G
         AGO   .U13
.U8      USING &KC1,&A,&B,&C,&D,&E,&F,&G,&H
         AGO   .U13
.U9      USING &KC1,&A,&B,&C,&D,&E,&F,&G,&H,&I
         AGO   .U13
.U10     USING &KC1,&A,&B,&C,&D,&E,&F,&G,&H,&I,&J
         AGO   .U13
.U11     USING &KC1,&A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K
         AGO   .U13
.U12     USING &KC1,&A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&L
.U13     ANOP
         PUSH  USING
         USING *,15
&NAME    STM   14,12,12(13)
         LA    0,80
         LA    1,1  SUBPOOL 1
         SLL   1,24
         OR    0,1
         GETMAIN R,LV=(0)
         ST    13,4(1)
         ST    1,8(13)
         LM    13,1,8(13)
         CNOP  0,4
         BAL   15,*+8
         DC    A(&KC1) .   1-   4
         L     15,0(15)
&KA2     SETA  &KA1-1
&KA3     SETA  &KA5+2
&KA1     SETA  &KA5+3
         LR    &LCNVGC1(&KA3),15
         AIF   (&KA2 EQ 0).P1
         LA    14,1
.P2      LA    &LCNVGC1(&KA1),4095(14,&LCNVGC1(&KA3))
&KA1     SETA  &KA1+1
&KA3     SETA  &KA3+1
&KA2     SETA  &KA2-1
         AIF   (&KA2 GT 0).P2
.P1      ANOP
         DROP  15
         POP   USING
         MEND
** ADD NAME=LEXIT
         MACRO
&NAME    LEXIT &RC=0
*CHANGE LEVEL 009      VOM 14.07.76
&NAME    LR    1,13
         L     13,4(13)
.P6      AIF   ('&RC'(1,1) NE '(').P3
         ST    &RC(1),16(13)
         AGO   .P4
.P3      ANOP
         LA    15,&RC
         ST    15,16(13)
.P4      ANOP
         LA    0,80
         LA    2,1  SUBPOOL 1
         SLL   2,24
         OR    0,2
         FREEMAIN R,LV=(0),A=(1)
         LM    14,12,12(13)
         BR    14
         MEND
