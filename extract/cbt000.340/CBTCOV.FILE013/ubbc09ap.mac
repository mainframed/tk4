 JOBLIS: PROCEDURE OPTIONS (MAIN);
 /**/
 /********************************************************************/
 /*                                                                  */
 /*      DAS PROGRAMM BENUTZT DAS PL1-SORT UEBER ENTRY IHESRTC       */
 /*                                                                  */
 /*      EINGABE   DIE IN PROGRAMM JOBGEN ERZEUGTE DATEI MIT         */
 /*                SAETZEN FUER LISTE JOB  -  STATISTIK              */
 /*                (DEFINITION SH. BSATZ)                            */
 /*                                                                  */
 /*                SORTIERUNG NACH  LISNR, SORT, JOB-NAME            */
 /*                                                                  */
 /*      AUSGABE   LISTE JOB  -  STATISTIK                           */
 /*                                                                  */
 /*                                                                  */
 /*      UEBER SYSIN MUSS EINE DATUMSANGABE FUER DIE KOPFZEILE DER   */
 /*      LISTE EINGEGEBEN WERDEN                                     */
 /*                                                                  */
 /*      BEISPIEL FUER DATUMSKARTE (GELOCHT SPALTE 1 BIS 30)         */
 /*                                                                  */
 /*      >>>MONATE JAN. - MAERZ 1972      <<<                        */
 /*      >>>MONAT SEPTEMBER 1972          <<<                        */
 /*                                                                  */
 /********************************************************************/
 /**/
 /**/
         DCL IHESRTC ENTRY (CHAR(42),CHAR(28),FIXED BIN(31,0),
                           FIXED BIN(31,0),ENTRY),
             IHESARC ENTRY(FIXED BIN(31,0)),
             SFELD CHAR(42),
             RFELD CHAR(28),
             RCODE FIXED BIN(31,0);
 /**/
         SFELD = ' SORT FIELDS=(1,2,CH,A,3,5,PD,D,8,8,CH,A) ';
         RFELD = ' RECORD TYPE=F,LENGTH=(115) ';
         CALL IHESRTC (SFELD,RFELD,50000,RCODE,AUSG);
         IF RCODE ^= 0 THEN DO;
          PUT EDIT ('*** FEHLER IM SORT-PROGRAMM ***')(A(31));
          END;
 /**/
 /**/
 /** ERSTELLUNG DER LISTE **/
 /**/
 AUSG:   PROCEDURE (ASATZ);
         DCL ASATZ CHAR(115),
             1 BSATZ BASED(P0),
             2 LISNR   PIC'99',
             2 SORT    FIXED(9),
             2 JOB     CHAR(8),
             2 AUFR    FIXED(9),
             2 STEP    FIXED(9),
             2 REGFO   FIXED(9),
             2 REGNU   FIXED(9),
             2 CPU     FIXED(9,3),
             2 ERRLZ   FIXED(9,3),
             2 SMFLZ   FIXED(9,3),
             2 FAKT    FIXED(9,3),
             2 KOSTEN  FIXED(9,2),
             2 ZUGR    FIXED(9),
             2 PLDAT   FIXED(9),
             2 PLZUG   FIXED(9),
             2 BDDAT   FIXED(9),
             2 BDZUG   FIXED(9),
             2 URDAT   FIXED(9),
             2 URZUG   FIXED(9),
             2 SIDAT   FIXED(9),
             2 SIKAR   FIXED(9),
             2 SODAT   FIXED(9),
             2 SOZEI   FIXED(9),
 /**/
             TAB(21) CHAR(25) INIT
               ('JOB NAME                 ',
                'JOB AUFRUFE              ',
                'ZAHL DER STEPS           ',
                'REGION GEFORDERT         ',
                'REGION GENUTZT           ',
                'CPU ZEIT                 ',
                'ERRECHNETE LAUFZEIT      ',
                'SMF LAUFZEIT             ',
                'FAKTOR                   ',
                'KOSTEN                   ',
                'GESAMT ZUGRIFFE          ',
                'PLATTEN DATEIEN          ',
                'PLATTEN ZUGRIFFE         ',
                'BAND DATEIEN             ',
                'BAND ZUGRIFFE            ',
                'UR DATEIEN               ',
                'UR ZUGRIFFE              ',
                'SYSIN DATEIEN            ',
                'SYSIN KARTEN             ',
                'SYSOUT DATEIEN           ',
                'SYSOUT ZEILEN            ') STATIC,
             1 TABA BASED(P1),
               2 TEXT CHAR(25),
 /**/
             DATUM  CHAR(30) INIT((30)' ') STATIC,
             ZLISNR PIC'99', BZ FIXED(3),
             SCH1 BIT(1) INIT('0'B) STATIC,
             LISTE FILE PRINT;
 /**/
 /**/
         P0 = ADDR(ASATZ);
         IF SCH1 = '1'B THEN GO TO VERGL;
         OPEN FILE(LISTE) PAGESIZE(68);
         GET EDIT (DATUM)(A(30));
         SCH1 = '1'B;
 SPEICH: ZLISNR = LISNR;
         IF LISNR > 21 THEN I = LISNR - 21;
                       ELSE I = LISNR;
         P1 = ADDR(TAB(I));
         BZ = 0;
         GO TO UEBER;
 VERGL:  IF LISNR ^= ZLISNR THEN GO TO SPEICH;
         ON ENDPAGE(LISTE) GO TO UEBER;
 ZEILE:  PUT FILE(LISTE) SKIP(1) EDIT
          (JOB,AUFR,STEP,REGFO,REGNU,CPU,ERRLZ,SMFLZ,FAKT,
           KOSTEN,ZUGR,PLZUG,PLDAT,BDZUG,BDDAT,URZUG,URDAT,SIKAR,
           SIDAT,SOZEI,SODAT)
          (A(8),P'BZZ9',P'BZZZ9',(2)P'BZZZ9',P'BZZ9V.999',
           (2)P'BZZZ9V.99',P'B9V.999',P'BZZZZZ9',P'BZZZZZZ9',
           (5)(P'BZZZZZZ9',P'BZZ9'));
         CALL IHESARC(4);
         RETURN;
 UEBER:  BZ = BZ + 1;
         PUT FILE(LISTE) PAGE EDIT
          ('LISTE NR 9028','J O B  -  STATISTIK ',DATUM,
           'SORTIERUNG: ',TEXT,'BLATT ',BZ)
          (A(13),X(6),A(20),X(6),A(30),X(5),A(12),A(25),X(6),
           A(6),P'999');
         PUT FILE(LISTE) SKIP(2) EDIT
          ('JOB-NAME×JOB×      **********')
          (A(29));
         IF ZLISNR > 21 THEN PUT FILE(LISTE) EDIT
          ('DURCHSCHNITTS - WERTE FUER EINEN JOB - LAUF')
          (A(43));
                        ELSE PUT FILE(LISTE) EDIT
          ('MAXIMAL - WERTE EINES JOBS')
          (A(26));
         PUT FILE(LISTE) EDIT
          ('**********')
          (A(10));
         PUT FILE(LISTE) SKIP(1) EDIT
          ('        ×AUF×STEP×  REGION × C P U ×ERRECHN× S M F',
           ' × FAKT×KOSTEN× GESAMT× PLATTEN   × B A N D   × U ',
           'R       × SYSIN/TGET×SYSOUT/TPUT')
          ((2)A(50),A(32));
         PUT FILE(LISTE) SKIP(1) EDIT
          ('        × RU×ZAHL×GE   GE  ×  ZEIT ×LF-ZEIT×LF-ZEI',
           'T×CPU-Z× VOLLE×     ZU×     ZU  DA×     ZU  DA×   ',
           '  ZU  DA× KARTEN  DA× ZEILEN  DA')
          ((2)A(50),A(32));
         PUT FILE(LISTE) SKIP(1) EDIT
          ('        × FE×    ×FORD NUTZ×(ZEITANGABE IN MINUTEN',
           ')×/ERR-×    DM× GRIFFE× GRIFFE TEI× GRIFFE TEI× GR',
           'IFFE TEI×        TEI×        TEI')
          ((2)A(50),A(32));
         PUT FILE(LISTE) SKIP(1) EDIT
          ('        ×   ×    ×  KB   KB×       ×       ×      ',
           ' ×  L-Z×      ×       ×         EN×         EN×   ',
           '      EN×         EN×         EN')
          ((2)A(50),A(32));
         IF ZLISNR ^> 21 THEN PUT FILE(LISTE) SKIP(1) EDIT
          ('DER IN DER SPALTE KOSTEN AUSGEWIESENE BETRAG IST D',
           'IE SUMME DER KOSTEN ALLER LAEUFE DES JOBS IM AUSWE',
           'RTUNGSZEITRAUM')
          (X(8),(2)A(50),A(14));
         PUT FILE(LISTE) SKIP(1);
         GO TO ZEILE;
         END AUSG;
         END JOBLIS;
