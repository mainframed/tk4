KZ080    TITLE 'UNCATLG + SCRATCH ANGEGEBENER DSNAMEN'
***********************************************************************
*        VERFAHREN: JOB-ABLAUFSTEUERUNG                               *
*        MODUL: KZ080                                                 *
*        AUFRUF: //STEP   EXEC  PGM=KZ080,PARM=&RETCDE,PURGE          *
*                                                      UNCAT          *
*                                                                     *
*        FUNKTION:  DIE MIT DIESEM STEP EINGEGEBENEN DD-STATEMENTS    *
*              WERDEN DARAUFHIN UEBERPRUEFT, OB DIE DSNAMEN           *
*              CATALOGISIERT SIND. WENN JA, WIRD EIN UNCATLG,UND      *
*              BEI PLATTENDATEIEN EIN SCRATCH AUSGEFUEHRT.            *
*                                                                     *
*              WENN 'UNCAT' IM PARMFELD NICHT ANGEGEBEN IST, WIRD     *
*              BEI BANDDATEIEN WIRD FUER DAS                          *
*              BANDVERWALTUNGSSYSTEM EIN KORREKTURSATZ ERSTELLT,      *
*              DAMIT DAS BAND WIEDER FREIGEGEBEN WERDEN KANN.         *
*                                                                     *
*              DER IN DER PARMANGABE ANGEGEBENE WERT WIRD             *
*              ALS RETURN-CODE GEGEBEN. STANDARDWERT = 0.             *
*                                                                     *
*              WENN PURGE ANGEGEBEN IST,WIRD BEI PLATTENDATEIEN       *
*              DAS EXPIRATION-DATE IGNORIERT.                         *
*                                                                     *
*        REGISTERBENUTZUNG:                                           *
*              R2  = TIOT-ADRESSE                                     *
*              R3  = LAENGE TIOT-ENTRY                                *
*              R4  = LAENGE TIOT-ENTRY  + RETURN-CODE REGISTER        *
*              R5  = ADRESSE   DDNAMEN BZW. DSNAMEN TABELLE           *
*              R6  = INCREMENT DDNAMEN BZW. DSNAMEN TABELLE           *
*              R7  = ENDADRESSEDDNAMEN BZW. DSNAMEN TABELLE           *
*              R8  = BAL-REGISTER                                     *
*              R9  = MAXIMAL- BZW RESTZAHL DER VOLUMES                *
*              R10 = FREI                                             *
*              R11 = BASISREGISTER DCB-DSECT                          *
*              R12 = BASIS-MODUL                                      *
*        MACROS: ZSTRT,ZEND,EXTRACT,RDJFCB,ABEND,LOCATE,CAMLST,       *
*              ZPRLC,                                                 *
*              CATALOG,SCRATCH,WTO.                                   *
*        AUTOR:  ASANGER                                              *
*        ERSTELLT:  JULI 1975                                         *
***********************************************************************
         PRINT NOGEN
KZ080    ZSTRT BASIS=R11,REGS=2
         B     KZ080S                                            200278
         DC    CL8'&SYSDATE'                                     200278
KZ080S   DS    0H                                                200278
         USING IHADCB,R9               BASISREGISTER MODELL-DCB
         LA    R9,DCBM
         SPACE 3
         MVI   CDSN,X'40'
         MVC   CDSN+1(L'CDSN-1),CDSN
         SPACE
         L     R1,0(R1)                PARM-ADRESSE
         ST    R1,PARMA
         LH    R2,0(R1)                LAENGE PARMANGABE
         LTR   R2,R2
         BNZ   A05                     PARMANGABE IST VORHANDEN
         B     A09
MOVEP    MVC   UEB1PR(0),0(R3)         PARMANGABE IN UEBERSCHRIFT
         SPACE
A05      EQU   *
         SLR   R5,R5
         LA    R3,2(R1)                PARMADRESSE
         LR    R4,R2                   PARMLAENGE
         CH    R4,=H'20'               PARMLAENGE MAX. 20 STELLEN
         BNH   A051
         LA    R4,20
A051     EQU   *
         BCTR  R4,R0
         EX    R4,MOVEP
         LR    R4,R2
         SPACE
A053     EQU   *
         CLI   0(R3),C','              IST EIN KOMMA VORHANDEN
         BE    A055                    JA
         LA    R3,1(R3)
         LA    R5,1(R5)                ANZAHL DER ZEICHEN VOR KOMMA
         BCT   R4,A053
         B     A060                    KEIN KOMMA VORHANDEN
A055     EQU   *
         CH    R4,=H'1'                IST NUR KOMMA ANGEGEBEN
         BE    A058                    JA, DANN IST OK.
         CH    R4,=H'6'
         BNE   A056                    NICHT ',PURGE'
         CLC   1(5,R3),=C'PURGE'
         BE    A057
         CLC   1(5,R3),=C'UNCAT'        IST NUR UNCATLG F. BAENDER ?
         BNE   A056                     NEIN
         SPACE
         OI    S1,BIT2                  ANZEIGER F. UNCATLG
         B     A058
         SPACE
A056     EQU   *
         WTO   'KZ0802I ZWEITER PARAMETER IST NICHT ''PURGE'' ODER ''UN*
               CAT''',ROUTCDE=(2,11)
         B     A058
A057     EQU   *
         OI    S1,BIT0                 ANZEIGER FUER PURGE
A058     EQU   *
         LTR   R2,R5                   IST ERSTER PARAMETER VORHANDEN
         BZ    A09                     NEIN
A060     EQU   *
         CH    R2,=H'4'                MXIMALLAENGE
         BNH   A07
         SPACE
         CLC   2(5,R1),=C'PURGE'       IST PURGE DER ERSTE PARAMETER
         BNE   A065                    NEIN
         OI    S1,BIT0                 ANZEIGER FUER PURGE
         B     A09
A065     EQU   *
         CLC   2(5,R1),=C'UNCAT'        IST UNCAT DER ERSTE PARAMETER ?
         BNE   A067                     NEIN, ERSTER PARAMETER IST
*                                       UNGUELTIG
         OI    S1,BIT2                  ANZEIGER F. NICHT TPSCRA.
         B     A09
         SPACE
A067     EQU   *
         WTO   'KZ0801I PARMANGABE MEHR ALS 4 STELLEN',                *
               ROUTCDE=(2,11)
         B     A09
         SPACE
A07      EQU   *
         L     R1,PARMA                PARMADRESSE LADEN
         BCTR  R2,R0                   LAENGE-1
         EX    R2,OCP                  AUF NUMERISCH STELLEN
         EX    R2,PACKP                ZIFFERN PACKEN
         CVB   R2,DW
         ST    R2,RETCD
         B     A09
         SPACE
OCP      OC    2(0,R1),=X'F0F0F0F0'
         SPACE
PACKP    PACK  DW,2(0,R1)
         SPACE 3
A09      EQU   *
         EXTRACT TIOT,'S',FIELDS=(TIOT)
         SPACE
         L     R2,TIOT                 ADRESSE TIOT
         MVC   JOBN(24),0(R2)          JOB,STEP,U.PROC-STEP-NAMEN
         LA    R3,24                   TIOT DD-ENTRY
         SPACE
A095     EQU   *
         LA    R2,0(R3,R2)
         ICM   R3,BIT7,0(R2)           LENGTH DD ENTRY
         BZ    A097                    ENDE DER TIOT
         CLC   4(8,R2),=CL8'PRINTDD'
         BNE   A095
         OI    S1,BIT1                 ANZEIGER PRINTDD IST VORHANDEN
A097     EQU   *
         L     R2,TIOT
         LA    R3,24
         SPACE 2
A10      EQU   *
         LA    R2,0(R3,R2)
         ICM   R3,BIT7,0(R2)           LENGTH DD-ENTRY
         BZ    ENDE                    KEIN DDNAME MEHR
         SPACE
         CLC   17(3,R2),=X'000000'     IST UCB-ADRESSE VORHANDEN
         BE    A10                     NEIN, DUMMY-DD
         SPACE
         MVC   DCBDDNAM,4(R2)          DDNAME NACH DCB
         SPACE
         LM    R5,R7,DDNAMW            TABELLENWERTE F. GESCHUETZTE DDN
         MVC   CDSN,=44C' '
         SPACE
A20      EQU   *
         CLC   DCBDDNAM,0(R5)
         BE    PR20                    GESCHUETZTER DDNAME
         BXLE  R5,R6,A20
         SPACE
         RDJFCB (DCBM)
         SPACE
         LTR   R15,R15
         BZ    OK
         SPACE
         LA    R4,100(R15)
         ABEND (R4),DUMP
         SPACE 3
OK       EQU   *
         MVC   CDSN,JFCB               DSN FUER CATALOG LESEN
PR00     EQU   *
         CLC   CDSN(4),=C'SYS1'
         BE    PR20                    GESCHUETZTER DSNAME
         CLC   CDSN(7),=C'SYSCTLG'
         BE    PR20                    GESCHUETZTER DSNAME
         LM    R5,R7,DSNAMW            TABELLENWERTE F. GESCHUETZTE DSN
         SPACE
PR10     EQU   *
         CLC   CDSN,0(R5)
         BE    PR20                    GESCHUETZTER DSNAME
         BXLE  R5,R6,PR10
         SPACE
PR102    EQU   *
         LA    R4,CATRETRY             ANZAHL WIEDERHOLUNGEN
PR105    EQU   *
         LOCATE DSN1
         LTR   R15,R15
         BZ    PR50
         CH    R15,RC12                WURDE INDEX GELESEN
         BE    PR60                    JA
         SPACE
         CH    R15,RC28                WAR SYSCTLG I/O ERROR ?
         BNE   PR1055                  NEIN
         BCT   R4,PR105                CATALOG LESEN NOCHMAL VERSUCHEN
         LA    R4,900(R15)             ABEND-CODE
         ABEND (R4),DUMP
CATRETRY EQU   3                       MAXIMAL DREI VERSUCHE
RC12     DC    H'12'                   RETURN-CODE NACH INDEX LESEN
RC28     DC    H'28'                   RETURN-CODE BEI CATALOG I/O ERRO
         SPACE 3
PR1055   EQU   *
         BAL   R8,PR12                 RETURN-CODE UMWANDELN
         B     A10
PR12S    DC    F'0'
PR12     EQU   *
         ST    R8,PR12S
         CVD   R15,DW                  RETURN-CODEN IN DEZIMAL UMWANDEL
         UNPK  WTOF,DW+5(3)
         OI    WTOF+L'WTOF-1,X'F0'
         MVC   WTOF(5),=C'RETCD.'
         BAL   R8,WTON
         L     R8,PR12S
         BR    R8
         SPACE
OK10     EQU   *
         B     A10
         SPACE 3
         SPACE
PR20     EQU   *
         MVC   WTOF,=C'PROTECT'
         BAL   R8,WTON
         B     OK10
         SPACE 3
PR50     EQU   *
         BAL   R8,UNCATS10
         B     A10
         SPACE 3
*    RETURN CODE=12 NACH CATALOG LESEN
PR60     EQU   *
         CLC   CAT1VLE(8),CATINDX      IST INDEX CONTROL ENTRY ?
         BNE   PR1055                  NEIN
         SPACE
         MVC   CAT3,CAT1               INDEX-BLOCK IN ZWISCHENSP.
         MVC   CAT3F,CAT1+L'CAT3
         SPACE
         LA    R1,CDSN
         LA    R0,35                   L'DSN MINUS (8+1) .G0001V00
PR62     EQU   *
         CLI   0(R1),C' '
         BE    PR65
         LA    R1,1(R1)
         BCT   R0,PR62
         ABEND 699,DUMP
CDSNIA   DC    A(0)                    INDEX-PUNKT ADRESSE
PR65     EQU   *
         ST    R1,CDSNIA
         LA    R10,CAT3+2+18           ADRESSE ERSTER DATA SET POINTER
GDG10    EQU   *
         CLI   0(R10),C'G'             IST GDG ?
         BNE   PR1055
         SPACE
         CLC   5(3,R10),=C'V00'        IST GDG ?
         BNE   PR1055
         SPACE 3
         L     R0,1(R10)               GENERATIONSNR.
         LCR   R0,R0                   KONPLEMENTIEREN
         BCTR  R0,R0                   KOMPLEMENT MINUS 1
         ST    R0,1(R10)
         SPACE
         L     R1,CDSNIA               INDEX-PUNKT ADRESSE
         MVI   0(R1),C'.'              INDEX-PUNKT
         MVC   1(8,R1),0(R10)          GENERATIONS-NR.
         SPACE
         SLR   R1,R1
         IC    R1,11(R10)              HALBWORT-COUNT
         SLL   R1,1                    MAL 2 = ANZAHL BYTES
         BCTR  R1,R0                   MINUS 1
         EX    R1,MOVEVOL              VOLUME ENTRY'S IN CAT1
         CLI   11(R10),X'01'           SIND VOLUME-ENTRIES VORHANDEN ?
         BE    GDG20                   NEIN, MEHR ALS 5 VOLUME'S
         SPACE
         LA    R10,13(R1,R10)          NAECHSTE GENERATION
GDG15    EQU   *
         BAL   R8,UNCATS10             UNCATLG U. SCRATCH AUSFUEHREN
         SPACE
         CLC   0(8,R10),CATLIE         IST INDEX LINK ENTRY ?
         BNE   GDG10                   NEIN
         SPACE 3
         CLC   8(3,R10),CATTTRZ        IST TTR = NULL
         BE    A10                     JA, INDEX IST ABGEARBEITET
         SPACE
         L     R1,CDSNIA               INDEX-PUNKT ADRESSE
         MVC   0(9,R1),=9C' '          INDEX ENTFERNEN
         B     PR102
         SPACE 2
* LESEN NAECHSTEN INDEXBLOCK
         SPACE
         MVC   CAT3TTR,8(R10)
         MVC   CAT3CVOL,CAT3F+3        CONTROL VOLUME SERIAL NUMBER
         LOCATE BLK3                   LESEN CATALOG BLOCK
         LA    R10,CAT3+2              ADRESSE ERSTER ERSTER DATA SET
         LTR   R15,R15
         BZ    GDG10
         SPACE
         LA    R4,600(R15)
         ABEND (R4),DUMP
         SPACE 3
GDG20    EQU   *
         MVC   BLKTTR,8(R10)           TTR OF VOLUME CONTROL BLOCK
         MVC   BLKCVOL,CAT3F+3         CONTROL VOLUME SERIAL NUMBER
         LA    R10,13(R1,R10)          NAECHSTE GENERATION
         LOCATE BLK1
         LTR   R15,R15
         BZ    GDG15
         LA    R4,650(R15)
         ABEND (R4),DUMP
         SPACE 3
BLK3     CAMLST BLOCK,CAT3TTR,CAT3CVOL,CAT3
         SPACE
MOVEVOL  MVC   CAT1(0),12(R10)         VOLUME ENTRY'S IN CAT1
         SPACE 2
DSN1     CAMLST NAME,CDSN,,CAT1
         SPACE 3
UNCAT1   CAMLST UCATDX,CDSN
         SPACE 3
UNCATS   DC    F'0'
UNCATS10 EQU   *
         ST    R8,UNCATS
         CLC   CAT1+2(4),DEVTAPE3
         BE    TPSCRA3
         CLC   CAT1+2(4),DEVTAPE6
         BE    TPSCRA3
         CLC   CAT1+2(4),DEVTAPE4
         BE    TPSCRA4
         CLC   CAT1+2(4),=X'34208003'
         BE    TPSCRA3
         CLC   CAT1+2(4),=X'32008003'
         BE    TPSCRA3
         CLC   CAT1+2(4),=X'34208004'
         BE    TPSCRA4
         CLC   CAT1+2(4),=X'00018000'
         BE    TPSCRA3
         CLC   CAT1+2(4),DEV33301
         BE    SCRA00
         CLC   CAT1+2(4),DEV33500
         BE    SCRA00
         CLC   CAT1+2(4),DEV33300
         BE    SCRA00
         MVC   WTOF,=C'UNDEF. '        UNBEKANNTER DEVICE-TYPE
         MVC   WTOVOL,CAT1+2+4
         BAL   R8,WTON
         L     R8,UNCATS
         BR    R8
         SPACE 2
SCRA00   EQU   *
         SPACE
SCRA03   EQU   *
*   SCRATCH DSNAME
         MVC   WTOF,=C'SCRATCH'
         MVC   WTOVOL,CAT1+2+4         VOLUME-SERIAL-NUMBER
         BAL   R8,WTON
         SLR   R0,R0
         MVI   XNOMOUNT,AUS                                      250178
SCRA05   EQU   *
         SCRATCH SCRA1
SCRA07   EQU   *
         ST    R15,SCRARET
         XC    SCRASTAT,SCRASTAT       LOESCHEN SCRATCH-STATUS
         LTR   R15,R15
         BZ    TPSCRA9                 UNCATLG AUSFUEHREN
         CH    R15,=H'4'
         BE    VOLM1                   VOLUME MOUNT
SCRA10   EQU   *
         SPACE
         SLR   R4,R4
         LA    R5,CAT1+2
         LH    R6,CAT1
SCRA12   EQU   *
         CLI   11(R5),X'05'            IST VOLUME NCIHT MONTIERT ?
         BE    VOLM3                   NEIN, ANFORDERN!
SCRA13   EQU   *
         MVC   WTODDN,=CL8' '
         LA    R1,WTODDN
         ICM   R4,BIT6+BIT7,10(R5)     SCRATCH-STATUS-CODE
         BZ    SCRA14
         OC    SCRASTAT,10(R5)         HOECHSTEN SCRATCH-STATUS MERKEN
         CVD   R4,DW
         UNPK  4(1,R1),DW+7(1)
         OI    4(R1),X'F0'
         MVI   3(R1),C'-'
         MVC   WTOVOL,4(R5)
         L     R15,SCRARET
         CVD   R15,DW
         UNPK  0(3,R1),DW+6(2)
         OI    2(R1),X'F0'
         MVC   WTOF,=C'RETCDE.'
         BAL   R8,WTON00
         CLC   WTODDN(5),=C'008-3'     IST SCHUTZDATUM NICHT ERREICHT
         BNE   SCRA14                  NEIN
         TM    S1,BIT0                 IST PURGE ANGEGEBEN
         BZ    SCRA14                  NEIN
         XC    10(2,R5),10(R5)         SCRATCHM STATUS ANZEIGE AUF NULL
         SLR   R0,R0
         SCRATCH SCRA1P                SCRATCH MIT PURGE
         B     SCRA07
SCRA14   EQU   *
         LA    R5,12(R5)
         BCT   R6,SCRA12
         ICM   R15,X'0F',SCRARET       SCRATCH RETURN-CODE = NULL ?
         BZ    TPSCRA9                 JA, UNCATLG AUSFUEHREN
         CLC   SCRASTAT,=H'1'          SCRATCH-STATUS OK ?
         BNH   TPSCRA9                 JA,UNCATLG AUSFUEHREN
SCRA17   EQU   *                       NEIN, GR 1 IST NICHT OK.
         BAL   R8,PR12
SCRA19   EQU   *
         L     R8,UNCATS
         BR    R8
         SPACE 3
SCRA1    CAMLST SCRATCH,CDSN,,CAT1
SCRA1P   CAMLST  SCRATCH,CDSN,,CAT1,,OVRD
         SPACE 3
VOLM1    EQU   *                       ERFORDERLICHES VOLUME MONTIEREN
         LA    R5,CAT1+2               ADRESSE ERSTES VOLUME
         LH    R6,CAT1                 ANZAHL VOLUMES
         SPACE
VOLM2    EQU   *
         CLI   11(R5),X'05'            SCRATCH STATUS CODE
         BE    VOLM3                   VOLUME NICHT MONTIERT
         LA    R5,12(R5)               NAECHSTES VOLUME
         BCT   R6,VOLM2
         B     SCRA10                  ANDERER FEHLER
         SPACE
VOLM3    EQU   *
*        L     R7,TIOT                 TIOT-ADRESSE
*        LA    R4,24                   TIOT DD-ENTRY-OFFSET
VOLM5    EQU   *
*        LA    R7,0(R4,R7)             DD-ENTRY
*        CLC   PRIVDDN,4(R7)
*        BE    VOLM7
*        ICM   R4,BIT7,0(R7)           LAENGE DD-ENTRY
*        BNZ   VOLM5                   NAECHSTES DD LESEN
*        MVC   WTOF,=C'DDFEHLT'
*        MVC   WTOVOL,4(R5)            VOLSER
*        BAL   R8,WTON
*        B     SCRA13
         SPACE
VOLM7    EQU   *
         MVC   WTOMOUNT+16+5(8),JOBN    PUT JOBNAME IN WTOR-MSG  250178
         MVC   WTOMOUNT+16+22(6),CAT1+2+4   PUT VOL IN WTOR-MSG  250178
         MVC   WTOMDSN+8+8(44),CDSN         PUT DSN IN  WTO-MSG  250178
VOLM72   DS    0H                                                250178
         XC    ECBMOUNT,ECBMOUNT                                 250178
WTOMDSN  WTO   'DSNAME: 00000000001111111111222222222233333333334444'  *
               ROUTCDE=(2,11)                                    200278
WTOMOUNT WTOR  'JOB  XXXXXXXX: MOUNT  YYYYYY  AND REPLY ''YES'', OR    *
                  REPLY ''CANCEL''',REPMOUNT,6,ECBMOUNT          250178
         WAIT  ECB=ECBMOUNT                                      250178
         CLC   REPMOUNT(3),=C'YES'                               250178
         BE    VOLM73          O.K. PLATTE IST MONTIERT          250178
         CLC   REPMOUNT(6),=C'CANCEL'                            250178
         BNE   VOLM72        KEIN GUELTIGES REPLY                250178
         ABEND 222                                               250178
         SPACE 2                                                 250178
VOLM73   DS    0H                                                250178
         SLR   R0,R0                                             250178
         B     VOLM78        BRANCH AROUND UCB-ADR. SETZEN       250178
         SPACE                                                   250178
VOLM75   DS    0H                                                250178
*        MVI   XNOMOUNT,EIN                                      250178
*        ICM   R0,BIT5+BIT6+BIT7,17(R7) UCB-ADRESSE
VOLM78   DS    0H                                                250178
         XC    10(2,R5),10(R5)         SCRATCH-STATUS-ANZEIGE AUF NULL
         B     SCRA05
         SPACE 3
MAXVOLN  EQU   20                      MAXIMALE ANZAHL VOLUMES PRO BLK.
TPSCRA3  EQU   *
TPSCRA4  EQU   *
         SPACE
         TM    S1,BIT2                 IST NUR UNCATLG GEFORDERT ?
         BO    TPSCRA9                 JA
         SPACE
         MVC   WTOF,=C'TPSCRA.'
         LH    R6,CAT1                 NUMBER OF VOLUMES ON LIST
TPSCRA5  EQU   *
         LA    R5,CAT1+2               ADRESSE ERSTER VOLUME-ENTRY
         LA    R7,MAXVOLN              MAXIMAL-ANZAHL VOLUMES PRO BLOCK
TPSCRA8  EQU   *
         MVC   WTOVOL,4(R5)            VOLUME SERIAL NUMBER
         BAL   R8,WTON
         BAL   R8,SMFRCD               SCHREIBEN SMF-RECORD TYPE 140
         LA    R5,12(R5)               NAECHSTE VOLSER
         BCT   R6,TPSCRA85
         B     TPSCRA9                 KEINE VOLUMES MEHR
TPSCRA85 EQU   *
         BCT   R7,TPSCRA8              MAXIMAL 20 VOLUME'S PRO BLOCK
         MVC   BLKTTR,CAT1TTRN
         MVC   BLKCVOL,CAT1CVOL
         LOCATE BLK1
         SPACE
         LTR   R15,R15
         BZ    TPSCRA5
         LA    R4,500(R15)
         ABEND (R4),DUMP
         SPACE
         B     TPSCRA5
TPSCRA9  EQU   *
         MVC   WTOF,=C'UNCATLG'
         MVC   WTOVOL,CAT1+2+4         ERSTE VOLUME SERIAL NUMBER
         BAL   R8,WTON
         LA    R4,CATRETRY             ANZAHL WIEDERHOLUNGEN
TPSCRA95 EQU   *
         CATALOG UNCAT1
         LTR   R15,R15
         BZ    SCRA19
         CH    R15,RC28                WAR SYSCTLG I/O ERROR ?
         BNE   SCRA17                  NEIN
         BCT   R4,TPSCRA95             UNCATLG NOCHMAL VERSUCHEN
         LA    R4,900(R15)             ABEND-CODE
         ABEND (R4),DUMP
         SPACE 3
*
*        WRITE AN SMF RECORD
*
CVTPTR   EQU   16
VOLSER   DC    CL6' '
SAVE3    DC    18F'0'
SMFRCD   EQU   *
         STM   14,12,SAVE3
         MVC   VOLSER,4(R5)
         MVI   DISP,BIT5+BIT7          DELETE U. UNCATLG
         LA    R3,ENDSMF-HDR(0)
         STH   R3,LEN                   STORE IN RDW
         MVI   RTY,141                  STORE RECORD TYPE = 141 030179
         TIME  BIN
         STM   R0,R1,TME+2              MOVE IN DATE
         MVC   TME(8),TME+2             MOVE IN TIME
         MVC   DSNSMF,JFCB              MOVE IN DSN
         L     R3,CVTPTR                A(CVT)
         L     R3,0(R3)                 A(PTR TO TCB)
         L     R3,4(R3)                 A(CURRENT TCB)
         L     R3,164(R3)               A(TCT)
         L     R3,28(R3)                A(JMR)
         MVC   JBN(16),0(R3)            JOBNAME,RDR TIME AND DATE
         MVC   UID(8),20(R3)           USER ID
         MVC   SID(4),16(R3)            CPUID AND MODEL
         MVC   SMFVOL,VOLSER            GET VOLUME SERIAL
         LA    R3,0
         STH   R3,SEG
         MVI   RSV,0
         LA    R7,HDR                   ADDRESS OF SMF RECORD
         L     R1,16                   GET CVT-ADR               170378
         TM    116(R1),X'13'           IS IT MVS ?               170378
         BO    SMFWTM                  YES, BRANCH AROUND SVC    170378
         LA    R1,250                  SVC FUER MUENCHEN
SMFRCDA  EQU   *
         EX    R1,SMFRCDX              SVC AUSFUEHREN
         SPACE
SMFWTM   DS    0H                                                170378
         SMFWTM (R7)                    WRITE TO SMF DATA SET
         LTR   R15,R15
         BZ    SMFRCDE
         LA    R4,1400(R15)
         ABEND (R4),DUMP
SMFRCDE  EQU   *
         LM    14,12,SAVE3
         BR    R8            RETURN TO CALLER
         SPACE
SMFRCDX  SVC   0             SET NEXT INSTRUCTION IN SUPERVISOR-STATUS
*              WIRD MIT EXECUTE AUSGEFUEHRT
         SPACE
BLK1     CAMLST BLOCK,BLKTTR,BLKCVOL,CAT1
         SPACE 3
ENDE     EQU   *
         TM    S1,BIT1                 IST PRINTDD VORHANDEN
         BZ    ENDE10                  NEIN
         OI    PRINTCNT,PRINTCND       CLOSE NACH DRUCKEN
         BAL   R14,PRINTPRT
ENDE10   EQU   *
         SPACE
         L     R4,RETCD                RETURN-CODE LADEN
         ZEND  RC=(R4)
         SPACE 3
WTON     EQU   *
         MVC   WTODDN,DCBDDNAM
WTON00   EQU   *
         MVC   WTODSN,CDSN
         LA    R1,WTOEND
WTON01   EQU   *
         CLI   0(R1),C' '
         BNE   WTON02
         BCT   R1,WTON01
WTON02   EQU   *
         LA    R0,WTONR
         SR    R1,R0
         CH    R1,=H'110'              MAXIMALE WTO-LAENGE
         BNH   WTON03
         LA    R1,110                  MAXLNG ANNEHMEN
WTON03   EQU   *
         TM    S1,BIT1                 IST PRINTDD VORHANDEN
         BO    PRINT1                  JA
         EX    R1,MOVEWTO
         LA    R1,1+4(R1)              WTOLAENGE
         STH   R1,WTON1+4              WTO-LAENHGE
         LA    R1,WTON1+4(R1)
         MVC   0(4,R1),=X'00000020'  DESCRIPTOR + ROUTING-CODES
WTON1 WTO  '                                                           *
                                                                   ',  *
               ROUTCDE=(11)
         B     PRINT2
         SPACE 2
MOVEWTO  MVC   WTON1+8(0),WTONR
         SPACE 3
MOVEPRT  MVC   DEBER+1(0),WTONR
         SPACE 2
PRINT1   EQU   *
         EX    R1,MOVEPRT
         MVC   UEB1J,JOBN              JOBNAME IN UEBERSCHRIFT
         MVC   UEB1S,JOBS              STEPNAME IN UEBERSCHRIFT
         MVC   UEB1P,JOBP              PROCEDUR-STEPNAME IN UEBERSCHR.
         MVI   DEBER,X'40'
         BAL   R14,PRINTPRT
PRINT2   EQU   *
         MVC   WTOVOL,=CL8' '
         MVC   WTODSN,=44C' '
         BR    R8
         SPACE 3
PRINT    ZPRLC DEBER=DEBER,CTLCHR=A,HDRCNT=2,HDRADR=UEB1
         SPACE 3
DEBER    DC    CL133' '
UEB1     DS    0CL108
         DC    C'KZ080  PROTOKOLL UEBER UNCATALOG + SCRATCH IM JOB '
UEB1J    DS    CL8' '
         DC    C','
UEB1S    DS    CL8' '
         DC    C','
UEB1P    DS    CL8' '
         DC    C'PARM='
UEB1PR   DC    CL20' '
UEB1E    EQU   *
         DC    CL(UEB1+L'UEB1-UEB1E)' '
         SPACE 2
UEB2     DS    0CL133
         DC    C'-'
         DC    CL8' '
         DC    C'DDNAME  VOLSER   AKTION DATEI-NAME'
UEB2E    EQU   *
         DC    CL(UEB2+L'UEB2-UEB2E)' '
         SPACE 3
WTONR    DC    C'KZ0800I '
WTODDN   DC    CL8' '
         DC    C' '
WTOVOL   DC    CL6' '                  VOLUME-SERIAL NUMBER
         DC    C' '
WTOF     DS    CL7                     WTO-FUNKTION, SCRATCH OD UNCATLG
         DC    C' DSNAME='
WTODSN   DS    CL44
WTOEND   EQU   *-1
         SPACE 2
DW       DS    D                       ZWISCHENFELD FUER CVD
         SPACE 3
BLKCVOL  DS    CL6                     CATALOG VOLUME SERIAL NUMBER
BLKTTR   DS    CL3                     TTR FUER VOLUME-CONTROL-BLOCK
         DS    C
CAT1     DS    0D
CAT1CNT  DS    H
CAT1VLE  DS    20CL12
CAT1DSCB DS    CL3                     TTR FUER DSCB BEI EINEM VOLUME
         DS    CL7
CAT1TTRN DS    CL3                     TTR NAECHSTER VOLUME CNTRL BLOCK
         DS    C
         DS    CL3
CAT1CVOL DS    CL6                     CATALOG VOLUME SERIAL NUMBER
         SPACE
         DS    0F
JFCB     DS    CL176
         SPACE 2
JOBN     DS    CL8                     JOB-NAME
JOBS     DS    CL8                     JOB-STEPNAME
JOBP     DS    CL8                     JOB-PROCEDURE-STEPNAME
         SPACE
         DS    0D
CAT3     DS    CL256
CAT3F    DS    CL9
CAT3TTR  DS    CL3
CAT3CVOL DS    CL6                     CONTROL VOLUME-SERIAL NUMBER
CATINDX  DC    X'0000000000000001'     INDEX CONTROL ENTRY
CATLIE   DC    8X'FF'                  INDEX LINK ENTRY
CATTTRZ  DC    XL3'00'                 ZERO TTR
         SPACE
PRIVDDN  DC    CL8'PRIVDDN'
RETCD    DC    F'0'                    RETURN-CODE
         SPACE
DDNAMW   DC    A(DDNAMT,L'DDNAMT,DDNAMTE)
DSNAMW   DC    A(DSNAMT,L'DSNAMT,DSNAMTE)
         SPACE
DEV33300 DC    X'30502009'             3330 MOD.1
DEV33301 DC    X'3050200D'             3330 MOD.11
DEV33500 DC    X'3050200B'             3350
         SPACE
DEVTAPE3 DC    X'34008003'
DEVTAPE4 DC    X'34008004'
DEVTAPE6 DC    X'32108003'             3400-6
         SPACE
EXL1     DC    AL1(7+128)
         DC    AL3(JFCB)
         SPACE
TIOT     DS    A(0)
SCRARET  DS    F'0'                    SCRATCH RETURN-CODE
PARMA    DS    F                       PARM-ADRESSE
         SPACE
CDSN     DS    CL44                    DSN AUS JFCB
CELMN    DS    CL8                     ELEMENTNAME AUS JFCB
         SPACE
         DS    0D
HDR      EQU   *
LEN      DS    BL2'0'
SEG      DS    BL2'0'
RSV      DS    BL1'0'
RTY      DS    BL1'0'
TME      DS    BL4'0'
DTE      DS    PL4'0000'
SID      DS    CL2' '
MOD      DS    CL2' '
JBN      DS    CL8' '
RST      DS    BL4'0'
RSD      DS    PL4'0000'
UID      DS    CL8' '
DSNSMF   DS    CL44' '
DISP     DS    CL1
SMFVOL   DS    CL6
ENDSMF   EQU   *
SCRASTAT DS    H'0'                    SCRATCH-STATUS SPEICHER
S1       DC    X'00'                   STEUERBYTE-BITSCHALTER
*              BIT0 = 1 = PURGE IST ANGEGEBEN
*              BIT1 = 1 = PRINTDD IST VORHANDEN
*              BIT2 = 1 = UNCAT IST ANGEGEBEN, KEIN TPSCRA AUSFUEHREN
         SPACE                                                   250178
ECBMOUNT DS    F                                                 250178
REPMOUNT DS    CL6    'YES' OR 'CANCEL'                          250178
XNOMOUNT DC    X'40'  ON, IF DISK NOT MOUNT AND 2.X UCB-ADR.LADEN250178
EIN      EQU   X'F1'                                             250178
AUS      EQU   X'40'                                             250178
         SPACE 3                                                 010278
         COPY  KZ08001      INSERT DSNAME- AND DDNAME-TABELLEN   010278
         LTORG
         SPACE
DCBM     DCB   DSORG=PS,MACRF=PL,DDNAME=SYSUT2,EXLST=EXL1
         SPACE 3
         DCBD  DSORG=PS,DEVD=DA
         SPACE 3
         ZBIT                          , SYMBOLISCHE BITDEFINITIONEN
         END
***********************************************************************
UDRU     TITLE 'MODUL ZUM DRUCKEN MIT ZEILENZAEHLEN U.UEBERSCHRIFTEN'
KZ00002  ZSTRT BASIS=R12
         USING PLCD,R10 BASISREGISTER PRINT-LINE-CONTROL
         USING DEBERD,R11  BASISREGISTER DRUCKBEREI H
         SPACE
         LM    R10,R11,0(R1) PARAMETER-ADRESSEN LADEN
         SPACE 3
         TM    PLCCNT,PLCOVD IST NOCH EIN OPEN ZU MACHEN
         BZ    A100  NEIN
         SPACE
         L     R4,PLCDCA  DCB-ADRESSE
         OPEN  ((4),OUTPUT)
         SPACE
         XI    PLCCNT,PLCOVD OPEN-ANZEIGE LOESCHEN
         CLI   PLCDAT+1,X'40' IST DAS DATUM = BLANK
         BNE   A100  NEIN, ES WURDE VOM RUFENDEN PROGRAMM GELIEFERT
         SPACE 3
         ZCALL KZ00003,ZDATUM
         MVC   PLCDAT,ZDATUM TAGESDATUM EINSPEICHERN
         SPACE 3
A100     EQU   *
         NI    PLCCNT,255-PLCOVL  UEBERLAUFANZEIGER LOESCHEN
         LH    R2,PLCNUM  ANZAHL DER ZU DRUCKENDEN ZEILEN
         LTR   R2,R2
         BZ    A500  NICHTS MEHR ZU DRUCKEN
A1010    BAL   R14,A1100
         LA    R11,L'ZDEBER(R11)
         BCT   R2,A1010
         SPACE 3
A500     EQU   *
         TM    PLCCNT,PLCCND IST CLOSE NACH DEM DRUCKEN
         BZ    A510 NEIN
         SPACE
         LA    R11,ZDEBER
         MVC   DSTEU,PLCCHA KANALSTEUERZEICHEN
         OI    PLCCNT,PLCEXE ANZEIGER LETZTER AUFRUF FUER EXIT-ROUTINE
         BAL   R14,DRUK NOCH EINEN KANALVORSCHUB AUSFUEHREN
         L     R4,PLCDCA  DCB-ADRESSE
         CLOSE ((4))
         SPACE
         XI    PLCCNT,PLCCND+PLCOVD CLOSE-ANZ.LOESCHEN,OPEN-ANZ.SETZEN
         NI    PLCCNT,255-(PLCVVZ+PLCEXE) VORSCHUBVERZOEGERUNG U.
*              EXIT ENDE ANZEIGER LOESCHEN
         SP    PLCBLZ,PLCBLZ BLATTZAEHLER LOESCHEN
         XC    PLCLCL,PLCLCL ZEILENZAEHLER LOESCHEN
         SPACE
A510     EQU   *
         ZEND
         SPACE 3
         DC    F'0' SAVE FUER RUECKSPRUNGREGISTER
A1100    ST    R14,A1100-4 REUCKSPRUNGREGISTER SICHERN
         BAL   R14,A140 STERZEICHEN UEBERSETZEN PRUEFEN U.ZAEHLEN
         BAL   R14,A200  UEBERSCHRIFTEN SCHREIBEN, WIRD NUR AUSGEFUEHRT
*              WENN UEBERLAUF OD. KANALSTEUERZEICHEN AUFGETRETEN IST
*              ANSONSTEN WIRD DIESE INSTRUKTION UEBERSPRUNGEN
A15      EQU   *
         BAL   R14,DRUK
         LA    R0,1
         A     R0,PLCZZL DRUCKZEILEN ZAEHLEN
         ST    R0,PLCZZL
         SPACE
         TM    PLCCNT,PLCDBL IST DER DRUCKBEREICH ZU LOESCHEN
         BZ    A16  NEIN
         SPACE
         MVI   DEBER,X'40'
         MVC   DEBER+1(L'DEBER-1),DEBER
         SPACE
A16      EQU   *
         L     R14,A1100-4 RUECKSPRUNGREGISTER LADEN
         BR    R14
         SPACE 3
A110     EQU   *   ASA-STEUERZEICHEN UEBERSETZEN
         LM    R3,R5,=A(CTLASA,1,CTLASAE)
         SPACE
A113     EQU   *
         LA    R1,CTLIBM  ADRESSE MASCHINENSTEUERZEICHEN
         LR    R8,R3 ADRESSE GEGEBENER STEUERZEICHEN-ART
         SPACE
A112     CLC   DSTEU,0(R3) VORHANDENES STEUERZEICHEN ZU TABELLE
         BE    A111 GEFUNDEN
         SPACE
         LA    R1,1(R1)  MASCHINEN-CODE TABELLE ERHOEHEN
         BXLE  R3,R4,A112
         SPACE
         MVC   DSTEU,0(R3) FEHLERSTEUERZEICHEN WIRD ANGENOMMEN
         B     A140  ANGENOMMENES STEUERZEICHEN MUSS AUCH UEBERSETZT WE
         SPACE 2
A111     EQU   *
         MVC   CTLCHR,0(R1)  STEUERZEICHEN ZWISCHENSPEICHERN
         B     A130
         SPACE 3
A120     EQU   *   SIEMENS-STEUERZEICHEN UEBERSETZEN
         LM    R3,R5,=A(CTLSIM,1,CTLSIME)
         B     A113
         SPACE 3
A125     EQU   *   IBM-MASCHINENCODE PRUEFEN
         LM    R3,R5,=A(CTLIBM,1,CTLIBME)
         B     A113
         SPACE 3
A140     EQU   *
         TM    PLCCNT,PLCCHR STEUERZEICHENART PRUEFEN
         BO    A110  ASA-STEUERZEICHEN
         BM    A120  SIEMENS-CODE
         B     A125  IBM-MASCHINEN-CODE
         SPACE 3
A130     EQU   *
         TM    CTLCHR,BIT0 IST ES EIN KANAL-STEUERZEICHEN
         BCR   1,R14  JA, KANALSTEUERZEICHEN LIEGT VOR
         SPACE
         LH    R1,CTLCHRH STEUERZEICHEN INS REG.1 BRINGEN
         SRL   R1,3       UM 3 STELLEN NACH RECHTS SCHIEBEN
         SPACE
         LH    R0,PLCLCL  LAUFENDEN ZEILENZAEHLER LADEN
         SR    R0,R1 UM ZEILENTRANSPORT VERRINGERN
         STH   R0,PLCLCL LAUFENDEN ZEILENZAEHLER SPEICHERN
         BCR   13,R14   UEBERLAUF IST AUFGETRETEN
         B     4(R14)  UEBERLAUF-ROUTINEN-ANSPRUNG UEBERSPRINGEN
         SPACE 3
         DC    F'0'  SAVE FUER RUECKSPRUNGADRESSE
DRUK     ST    R14,DRUK-4
         SPACE
         L     R15,PLCEXT  EXIT-ADRESSE LADEN
         LTR   R15,R15  IST DIESE ADRESSE = NULL
         BZ    DRUK1  JA, DANN IST KEIN EXIT
         SPACE
         ST    R11,PLCDBR  DRUCKBEREICHSADRESSE EINSPEICHERN
         ZCALL (15),(R10)
         SPACE
DRUK1    EQU   *
         L     R1,PLCDCA  DCB-ADRESSE
         PUT   (1),(11)
         L     R14,DRUK-4
         BR    R14
         SPACE 3
         DC    F'0'
A200     EQU   *   UEBERLAUF OD.KANALSTEUERZEICHEN IST AUFGETRETEN
         ST    R14,A200-4
         TM    PLCCNT,PLCVVZ+PLCCND IST DER VORSCHUB ZU VERZOEGERN
*              ODER CLOSE NACH DEM DRUCKEN
         BNZ   A227  JA, DANN IST MOMENTAN NICHTS ZU UNTERNEHMEN
         ST    R11,DBADR DRUCKBEREICHSADRESSE SICHERSTELLEN
         LA    R11,ZDEBER ADRESSE EIGENER DRUCKBEREICH
         MVC   DSTEU,PLCCHA KANAL FUER VORSCHUB NACH UEBERLAUF
         SPACE 2
         TM    PLCCNT,PLCCHR WELCHE STEUERZEICHENART
         BNZ   A203  KEIN MASCHINEN-STEUERZEICHEN
         SPACE
         BAL   R14,DRUK  DIREKT-VORSCHUB AUSFUEHREN
         MVI   DSTEU,X'11' ZWEI ZEILEN NACH DEM DRUCK
         CLI   PLCHDC+1,X'01' IST MEHR ALS EINE UEBERSCHRIFTSZEILE
         BH    A203 JA
         MVI   DSTEU,X'19' NEIN, DANN DREI ZEILEN NACH DEM DRUCKEN
         SPACE
A203     EQU   *
         OI    PLCCNT,PLCOVL  UEBERLAUFANZEIGER SETZEN
         LA    R0,1
         A     R0,PLCGBL  GESAMTBLATT-ZAEHLER ADDIEREN
         ST    R0,PLCGBL
         SPACE
         L     R7,PLCHDA ADRESSE DER ERSTEN UEBERSCHRIFTSZEILE
         LH    R0,PLCLNC ANZAHL DER ZEILEN PRO SEITE
         STH   R0,PLCLCL LAUFENDER ZEILENZAEHLER
         LH    R9,PLCHDC  ANZAHL DER UEBERSCHRIFTSZEILEN
         LTR   R9,R9 SIND UEBERSCHRIFTEN ZU SCHREIBEN
         BZ    A225  NEIN, DANN IST SCHON VORGESCHOBEN,BEI MASCHINEN-CO
         SPACE 3
         MVC   DEBER(L'DEBER-L'KUE1),0(R7) ERSTE UEZEILE
         MVC   DEBER+(L'DEBER-L'KUE1)(L'KUE1),KUE1
         AP    PLCBLZ,KP1  BLATTZAEHLER ERHOEHEN
         ED    DEBER+(L'DEBER-L'KUE1+17)(6),PLCBLZ+1 BLATTZAEHLER AUFB.
         MVC   DEBER+(L'DEBER-L'KUE1+3)(8),PLCDAT TAGESDATUM
         LA    R7,L'DEBER-L'KUE1(R7) UEBERSCHRIFTSADRESSE ERHOEHEN
         B     A221
         SPACE 3
A220     MVC   DSTEU(L'ZDEBER),0(R7)
         LA    R7,L'ZDEBER(,R7)
         BAL   R14,A140  STEUERZEICHEN PRUEFEN UND UEBERSETZEN
         NOP   A200  WIRD NORMALERWEISE UEBERSPRUNGEN, MUSS ABER
*              SEIN SONST WUERDE EINE ENDLOSE SCHLEIFE
A221     EQU   *
         BAL   R14,DRUK
         MVI   DEBER,X'40'
         MVC   DEBER+1(L'DEBER-1),DEBER
         SPACE
         BCT   R9,A220
         SPACE
A225     EQU   *
         L     R11,DBADR
         TM    PLCCNT,PLCCHR
         BZ    A227   MASCHINEN-CODE = OK
         MVC   DSTEU,3(R8) DREI ZEILENTRANSPORT
         CLI   PLCHDC+1,X'00' WAREN UEBERSCHRIFTSZEILEN ZU SCHREIBEN
         BH    A227  JA, DANN IST ZEILENTRANSPORT RICHTIG
         MVC   DSTEU,PLCCHA NEIN, DANN MUSS AUF ERSTE ZEILE VORGE=
*                           SCHOBEN WERDEN
A227     L     R14,A200-4
         BR    R14
         SPACE 3
*              DEFINITIONEN FUER ZWISCHENFELDER
         SPACE
DBADR    DC    F'0' DRUCKBEREICHSADRESSE-ZWISCHENSPEICHER
CTLCHRH  DS    0H
         DC    X'00'
CTLCHR   DC    X'00'  STEUERZEICHEN ZWISCHENSPEICHER
         SPACE 3
KUE1     DS    0CL24
         DC    CL12'AM'
         DC    C'BLATT'
         DC    X'40212020202060'
         SPACE
ZDATUM   DC    CL12' '
         SPACE 3
ZDEBER   DC    CL133' ' DEBER ZWISCHENBEREICH
         SPACE 3
CTLIBM   DC    X'01091119899199A1A9B1B9C1C9D1D9E1' NACH DEM DRUCKEN
*                0 1 2 3 K1K2K3K4K5K6K7K8K9KAKBKC
         DC    X'030B131B8B939BA3ABB3BBC3CBD3DBE3' VOR DEM DRUCKEN
CTLIBME  EQU   *-1
CTLIBMF  DC    X'09' ANGENOMMENES FEHLERSTEURZEICHEN
         SPACE 3
CTLASA   DC    C'+ 0-123456789ABC'  ASA-STEUERZEICHEN
CTLASAE  EQU   *-1
CTLASAF  DC    C' ' ANGENOMMENES FEHLERSTEUERZEICHEN
         SPACE 3
CTLSIM   EQU   *   SIEMENS-STEUERZEICHEN
         DC    X'40414243C1C2C3C4C5C6C7C8C9CACBCC'
*        DC    X'000102030405060708090A0B0C0D0E0F' ZT.NACH DEM DRUCKEN
*        DC    X'404142434445464748494A4B4C4D4E4F' ZT. VOR DEM DRUCKEN
*        DC    X'C1C2C3C4C5C6C7C8C9CACBCCCDCECF' VORSCH. VOR DRUCKEN
*        DC    X'8182838485868788898A8B8C8D8E8F' VORSCH.NACH DRUCKEN
CTLSIME  EQU   *-1
CTLSIMF  DC    X'41'  ANGENOMMENES FEHLERSTEUERZEICHEN
         SPACE 3
KP1      DC    P'1'
         LTORG
         SPACE 3
PLC      ZPRLC DSECT=YES,CTLCHR=M
DEBERD   DSECT BASISREGISTER = R11
DSTEU    DS    C   STEUREZEICHEN
DEBER    DS    CL132
         SPACE 3
BIT0     EQU   128
BIT1     EQU   64
BIT2     EQU   32
BIT3     EQU   16
BIT4     EQU   8
BIT5     EQU   4
BIT6     EQU   2
BIT7     EQU   1
         SPACE 3
         END   KZ00002
