***********************************************************************
*    NAME=     SCR2                                                   *
*                                                                     *
*    ZWECK=    STATUS-CODE-ROUTINE FUER IMS-DLI-CALLS                 *
*                                                                     *
*    FUNKTIONEN= AUFRUF DES IMS-DLI-INTERFACE                         *
*                AUSGABE EINER MELDUNG BEI FEHLERHAFTEM STATUS-CODE   *
*                ABBRUCH BEI FEHLERHAFTEM STATUS-CODE                 *
*                ERWEITERUNGSMOEGLICHKEITEN FUER STATISTIKEN          *
*                                                                     *
*    ATTR=     REENTRANT                                              *
*                                                                     *
*    REGISTERVERWENDUNG  R2=  BASISREGISTER                           *
*                        R3=  SPEICHER R13                            *
*                        R4=  SPEICHER R1                             *
*                        R5=  ADRESSE SCR-PARM1                       *
*                        R6=  ADRESSE  SCR-PARM2                      *
*                        R7=  ADRESSE FKT                             *
*                        R8=  ADRESSE PCB                             *
*                        R9=  ARBEITSREGISTER                         *
*                        R10= ARBEITSREGISTER, BASISREG. STAE-EXIT-ROU*
*                                                                     *
*********************************************A1= SAVE 1.TEIL          *
SCR2     CSECT
         SAVE  (14,12)
         LR    R3,R13              ZWSP R13
         BALR  R2,R0
         USING *,R2
         LR    R4,R1               ZWSP R1
         LM    R5,R8,0(R1)         ZWSP PARAMETERLISTE
*********************************************A2= AREA ANFORDERN
A2       CLI   4(R5),X'EE'         KZ-AREA ABFRAGEN
         BE    A3
         GETMAIN R,LV=200
         ST    R1,4(,R5)
*********************************************A3= SAVE 2.TEIL
A3       L     R13,4(,R5)
         ST    R13,8(,R3)
         ST    R3,4(,R13)
*********************************************A5= ERSTER CALL
A5       CLI   4(R5),X'EE'         KZ-AREA ABFRAGEN
         BE    A6
         B     ANF                 ERSTER CALL
*********************************************A6= VERZWEIGEN JE FKT
A6       CLC   0(3,R6),=C'DBB'     SCR-PARM2
         BE    NOR
         CLC   0(3,R6),=C'MSG'     SCR-PARM2
         BE    NOR
         CLC   0(3,R6),=C'SPA'     SCR-PARM2
         BE    NOR
         CLC   0(3,R6),=C'SYS'     SCR-PARM2
         BE    NOR
         CLC   0(3,R6),=C'END'     SCR-PARM2
         BE    END
         CLC   0(3,R6),=C'ANF'     SCR-PARM2
         BE    A7
         ABEND 1420,DUMP           CALL-TYP-ANGABE FALSCH
*********************************************A7= RESTORE
A7       LR    R13,R3
         MVI   12(R13),X'FF'
         RETURN (14,12),RC=0
***********************************************************************
* INIT-CALL                                                           *
*********************************************ANF1= KZ-AREA SETZEN     *
ANF      MVI   4(R5),X'EE'
*********************************************ANF2= IMS-INFORM. HOLEN
ANF2     LA    R9,76(,R13)         A(REGION-TYP)
         ST    R9,100(,R13)
         LA    R9,80(,R13)         A(PGM-NAME)
         ST    R9,104(,R13)
         LA    R9,72(,R13)         A(A(PCB-ADR-LISTE))
         ST    R9,108(,R13)
         OI    108(R13),X'80'      VL-BIT SETZEN
         LA    R1,100(,R13)
         CALL  IMSCB               IMS-CONTROL-BLOCKS-ROUTINE
*********************************************ANF4= STAE SETZEN
ANF4     STAE  SCRSTAE,CT,PARAM=(4)
         B     A6
***********************************************************************
* END-CALL                                                            *
*********************************************END2= STAE LOESCHEN      *
END      CLC   76(3,R13),=C'MSG'   ABFRAGE OB MSG-REGION
         BNE   END3
         STAE  0,CT
*********************************************END3= FREEMAIN/DELETE
END3     LR    R1,R13
         FREEMAIN R,LV=200,A=(1)
         MVI   4(R5),X'00'         KZ-AREA LOESCHEN
         MVI   0(R5),X'00'         KZ-SCR  LOESCHEN
         DELETE EP=SCR2
         B     A7
***********************************************************************
* NORMALER CALL                                                       *
*********************************************NOR= PARAMETERLISTE TESTEN
NOR      TM    0(R7),X'C0'         TESTEN PARM-COUNT(VOLLWORT) ODER FKT
         BO    NOR1                PARM-COUNT NICHT VORH
         LM    R7,R8,12(R4)        PARM-COUNT VORHANDEN
*********************************************NOR1= CALL DLI-INTERFACE *
NOR1     LA    R1,8(,R4)
NOR1COB  CLC   10(3,R5),=C'COB'
         BNE   NOR1ASM
         CALL  CBLTDLI
         B     NOR2
NOR1ASM  CLC   10(3,R5),=C'ASM'
         BNE   NOR1PLI
         CALL  ASMTDLI
         B     NOR2
NOR1PLI  CLC   10(3,R5),=C'PLI'
         BNE   NOR1FOR
         CALL  PLITDLI
         B     NOR2
NOR1FOR  CLC   10(3,R5),=C'FOR'
         BNE   NOR1ABBR
         CALL  FORTDLI
         B     NOR2
NOR1ABBR ABEND 1410,DUMP           SPRACH-ANGABE FALSCH
*********************************************NOR2= STATUS-CODE-ANALYSE
NOR2     LA    R9,1                R9=  RETURN-CODE
         LA    R10,3(,R6)          R10= ADRESSE IN STATUS-CODE-LISTE
         CLC   10(2,R8),=C'  '     PRUEFEN OB STATUS-CODE BLANK
         BE    NOR3                STATUS-CODE BLANK
NOR2A    AH    R9,=H'1'            R9  ERHOEHEN
         AH    R10,=H'2'           R10 ERHOEHEN
         CLC   0(2,R10),10(R8)     STATUS-CODE VERGLEICHEN
         BE    NOR3                STATUS-CODE GEFUNDEN
         CLC   0(2,R10),=C'**'
         BE    NOR3A               ENDE DER STATUS-CODE-LISTE
         CLC   0(2,R10),=C'//'
         BE    NOR3A               ENDE DER STATUS-CODE-LISTE
         CH    R9,=H'99'
         BE    NOR3A               ENDE DER STATUS-CODE-LISTE FEHLT
         B     NOR2A
*********************************************NOR3= RETURN-CODE SETZEN
NOR3     STH   R9,8(,R5)           STATUS-CODE RICHTIG (R10 FREI)
         B     NOR7                STATUS-CODE RICHTIG (R10 FREI)
NOR3A    STH   R9,8(,R5)           STATUS-CODE FALSCH (R10 BIS NOR6 BEN
*********************************************NOR4= MELDUNG-1 AUSGEBEN
NOR4     LA    R1,100(,R13)
         MVC   0(LG1,R1),MELD1     WTO-LISTE NACH AREA+100
         LA    R9,DISPL(,R1)
         MVC   5(3,R9),76(R13)     REGION-TYP
         MVC   9(8,R9),80(R13)     PGM-NAME
         MVC   18(3,R9),0(R6)      CALL-TYP
         MVC   22(2,R9),3(R6)      CALL-NR
         MVC   30(8,R9),0(R8)      DATENBANK/LTERM
         MVC   39(2,R9),10(R8)     STATUS-CODE
         CLC   0(3,R6),=C'DBB'
         BNE   NOR4A
         MVC   42(8,R9),20(R8)     SEGMENT-NAME
NOR4A    MVC   52(4,R9),0(R7)      FKT
         CLC   0(2,R10),=C'//'
         BE    NOR4B
         MVC   58(4,R9),=C'ABBR'   ABBR
NOR4B    WTO   MF=(E,(1))
*********************************************NOR6= ABBRUCH
NOR6     CLC   0(2,R10),=C'//'
         BE    NOR7
         ABEND 1400,DUMP
*********************************************NOR7= RETURN-ADRESSE ERHOE
NOR7     CLC   13(2,R5),=C'00'     FAKTOR ABFRAGEN
         BNH   A7
         PACK  104(8,R13),13(2,R5) FAKTOR NACH DOPPELWORT
         CVB   R9,104(,R13)
         LH    R10,8(,R5)          RETURN-CODE
         BCTR  R10,R0              RETURN-CODE MINUS 1
         STH   R10,100(,R13)
         MH    R9,100(,R13)        RETURN-CODE MINUS 1 MAL FAKTOR
         L     R10,4(,R13)         ADR. SAVEAREA DES AUFRUF. PROGRAMMS
         A     R9,12(,R10)         ERHOEHTR RETURN-ADRESSE
         ST    R9,12(,R10)         ERHOEHTR RETURN-ADRESSE ZURUECKSP.
         B     A7
***********************************************************************
*  DEFINITIONEN                                                       *
***********************************************************************
MELD1    WTO   '*IMSSCR  CALL=   ,        ,   ,    PCB=        ,  ,    *
                               ',                                      *
               ROUTCDE=(11),MF=L
LG1      EQU   *-MELD1
MELD2    WTO   '*IMSSCR     ,        ,   ,        ,  ',                *
               ROUTCDE=(13),MF=L
LG2      EQU   *-MELD2
DISPL    EQU   13                  4=DISPL D MELD. 9= VERSATZ IN D MELD
         REG
***********************************************************************
* STAE-EXIT-ROUTINE                                                   *
*********************************************SAVE                     *
SCRSTAE  SAVE  (14,12)
         LR    R3,R13              ZWSP R13
         BALR  R10,R0
         USING *,R10
         LR    R4,R1               ZWSP R1
         GETMAIN R,LV=72
         LR    R13,R1
         ST    R13,8(,R3)
         ST    R3,4(,R13)
*********************************************STAE-WORKAREA
         L     R4,0(,R4)           ADR DER STAE-PARAMETERLISTE
         L     R5,0(,R4)           ADR SCR-PARM1
         CLI   4(R5),X'EE'         KZ-AREA ABFRAGEN OB FREEMAIN NOETIG
         BNE   SCRSTAE2
*********************************************FREEMAIN/DELETE
         L     R1,4(,R5)
         FREEMAIN R,LV=200,A=(1)
         MVI   4(R5),X'00'         KZ-AREA LOESCHEN
         MVI   0(R5),X'00'         KZ-SCR  LOESCHEN
         DELETE EP=SCR2
*********************************************RESTORE
SCRSTAE2 LR    R1,R13
         FREEMAIN R,LV=72,A=(1)
         LR    R13,R3
         MVI   12(R13),X'FF'
         RETURN (14,12),RC=0
         END
