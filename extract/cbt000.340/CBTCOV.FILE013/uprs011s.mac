*/T0518X44 JOB (S,23556),34.HOFFM,CLASS=Z
*/XDATA44 EXEC AFC
*/SYSIN    DD  *
         TITLE 'INLINE MACROS'
         SPACE
         MACRO
&L       DDSEARCH &PRAE,&ANZ,&NOT
&L       LA    RE,&PRAE.STB                 ADR STEUERBLOCK
         LA    RF,&ANZ                      ANZAHL DD'S
         BAL   R9,B71001                    IN TIOT SUCHEN
         LTR   R0,R0                        GEFUNDEN..
         BZ    &NOT                          NEIN
         OI    0(RE),DDSTMT+OPENED          STEUERBLOCK AKTIVIEREN
         MEND
         SPACE
         MACRO
&L       DEFSTB &DDN,&DCB
&L.STB   DS    0F                           DD-STEUERBLOCK
&L.SCH   DC    XL1'0'                       STATUS-SCHALTER
         ORG   *-1
&L.DCB   DS    XL1                          DCB-ADRESSE
         DC    AL3(&DCB)
&L.DDN   DC    CL8'&DDN'                    DDNAME
         DC    XL4'0'                       RESERVE
         MEND
         SPACE
         MACRO
&L       STL   &LAB
&L       BAL   R9,B62200                    STEUERLEISTE AUFBAUEN
         DC    S(&LAB)                      ADRESS-KONSTANTE
         MEND
XDAMAIN  TITLE '-KOPIEREN, -SELEKTIEREN UND DRUCKEN VON OS-DATEIEN'
***********************************************************************
**                                                                   **
**       PROGRAMM : XDAMAIN                                          **
**       AUTOR    : HARTMUT HOFFMANN                                 **
**                  C/O PREUSSAG AG                                  **
**                  ABT. SYSTEMPROGRAMMIERUNG                        **
**                  LEIBNIZUFER 9                                    **
**                  3000 HANNOVER 1                                  **
**       SPRACHE  : ASSEMBLER                                        **
**       DATUM    : 10.10.76                                         **
**       VERSION  : 04 VOM 15.03.78                                  **
**                                                                   **
***********************************************************************
         SPACE 2
***********************************************************************
**                                                                   **
**       P R O G R A M M B E S C H R E I B U N G                     **
**       ---------------------------------------                     **
**                                                                   **
**       DAS PROGRAMM -XDATA- KOPIERT OS-DATEIEN, DIE ENTWEDER       **
**       PHYSISCH SEQUENTIELL ODER INDEX SEQUENTIELL ORGANISIERT     **
**       SIND. MOEGLICHE SATZFORMATE SIND F, V ODER U.               **
**                                                                   **
**       NACH DEM LESEN EINES DATENSATZES ODER SATZSEGMENTES         **
**       KANN EIN USER-EXIT ANGESTEUERT WERDEN, IN DEM SAETZE        **
**       GEAENDERT, GELOESCHT ODER EINGEFUEGT WERDEN KOENNEN.        **
**                                                                   **
**       AUSSERDEM IST ES MOEGLICH, DATENSAETZE IN 5 VERSCHIEDENEN   **
**       FORMATEN ANZUDRUCKEN.                                       **
**                                                                   **
**       AM ENDE DER VERARBEITUNG WIRD EIN AUSFUEHRLICHES PROTOKOLL  **
**       GESCHRIEBEN, DAS DATEI-INFORMATIONEN UND ANZAHL GELESENER   **
**       UND GESCHRIEBENER SAETZE AUSWEIST.                          **
**                                                                   **
**       DIE STEUERUNG DES PROGRAMMS WIRD WEITGEHENST DURCH DIE      **
**       AUFGETRETENEN DD-NAMEN BESTIMMT.                            **
**       WEITERE STEUERMOEGLICHKEITEN SIND UEBER SYSIN-STEUER-       **
**       KARTEN GEGEBEN.                                             **
**                                                                   **
***********************************************************************
         EJECT
***********************************************************************
**                                                                   **
**       FUNKTIONSBESCHREIBUNG DER PROGRAMMBLOECKE                   **
**       -----------------------------------------                   **
**                                                                   **
**  BLOCK:                                                           **
**  B10001    OPEN SYSPRINT, GRUNDWERTE SETZEN                       **
**            DDNAMEN ERMITTELN, OPEN DATEIEN, EXITS LADEN           **
**            PROGRAMM-MODIFIKATIONEN VORNEHMEN                      **
**  B21001    STEUERLEISTE, WIRD IN B10 MODIFIZIERT                  **
**  B21100    LESEN EINGABEN                                         **
**  B21200    CALL USER-EXIT                                         **
**  B21300    QSAM AUSGABEN (SYSUT2,SYSUT3)                          **
**  B21400    ISAM AUSGABE                                           **
**  B21500    CALL DRUCK UNTERPROGRAMM                               **
**  B21600    CALL SYSIN PROCEDURE                                   **
**  B21700    AUSGABE SEPARATE FILES                                 **
**  B49001    SCHLUSSROUTINEN, CLOSE DATEIEN, PROTOKOLL              **
**  B61100    CALL XDATA DRUCKROUTINE FUER SYSPRINT                  **
**  B62001    ZAHL AUFBEREITEN (EDMK)                                **
**  B62100    TEXT FUER PROTOKOLL ERGAENZEN                          **
**  B62200    STEUERLEISTE (B21001) AKTIVIEREN                       **
**  B62300    STEUERLEISTE (B21001) DEAKTIVIEREN                     **
**  B63001    OPEN EXIT ROUTINE, BLKSIZE ANPASSEN                    **
**  B64001    PUT FUER QSAM                                          **
**  B71001    DDNAMEN IN DER TIOT SUCHEN                             **
**  B72001    DATEI-INFORMATIONEN AUS JFCB DRUCKEN                   **
**  B72200    JCL PARM EINTRAGUNGEN PRUEFEN                          **
**  B81001    FEHLERMELDUNGEN AUFBEREITEN UND DRUCKEN                **
**  B81500    SYNAD EXIT-ROUTINE                                     **
**  SPIEEXIT  0C7 IN SYSIN PROC ABFANGEN                             **
**  B91001    GEMEINSAME DRUCKROUTINE (SYSPRINT)                     **
**                                                                   **
***********************************************************************
         EJECT
***********************************************************************
**                                                                   **
**       FLOWCHART                                                   **
**       =========                                                   **
**                                                                   **
**                                                                   **
**       XDAMAIN                                                     **
**       +---------------+                                           **
**       × OPEN          ×                                           **
**       × SYSPRINT      ×                                           **
**       +---------------+                                           **
**               ×                                                   **
**       +---------------+                                           **
**       × PARM-EINTRAG  ×                                           **
**       × PRUEFEN       ×                                           **
**       +---------------+                                           **
**               ×                                                   **
**       +---------------+                                           **
**       × DDNAMEN IN    ×                                           **
**       × TIOT SUCHEN   ×                                           **
**       +---------------+                                           **
**               ×                                                   **
**       +---------------+      +---------------+                    **
**       × IST SYSIN     × CALL × XDACGEN       ×                    **
**       × DA            ×----->× SYSIN PRUEFEN ×                    **
**       +---------------+      +---------------+                    **
**               ×<---------------------×                            **
**       +---------------+      +---------------+                    **
**       × SIND FEHLER   ×      × EXIT          ×                    **
**       × AUFGETRETEN   ×----->×               ×                    **
**       +---------------+      +---------------+                    **
**               ×                                                   **
**       +---------------+                                           **
**       × OPEN DATEIEN  ×                                           **
**       × LADEN U-EXIT  ×                                           **
**       +---------------+                                           **
** GET --------->×                                                   **
**       +---------------+                                           **
**       × LESEN         ×                                           **
**       × EINGABE       ×                                           **
**       +---------------+                                           **
**               ×                                                   **
**       +---------------+                                           **
**       × CALL EXITS    ×                                           **
**       × USER/SYSIN    ×                                           **
**       +---------------+                                           **
**               ×                                                   **
**       +---------------+  ENDE  +---------------+                  **
**       × SCHREIBEN     ×        × HOUSEKEEPING  ×                  **
**       × AUSGABEN      ×        × PROTOKOLL     ×                  **
**       +---------------+        × EXIT          ×                  **
**               ×---> GET        +---------------+                  **
**                                                                   **
***********************************************************************
         EJECT
         PRINT NOGEN
XDAMAIN  CSECT
         PROST (RA,RB,RC),,REG              INITIALISIERUNG
         LR    R5,R1                        PARM-ADR SICHERN
         SPACE 3
B10001   NULL
         OPEN  (PRINT,OUTPUT)               PROTOKOLL-DATEI
         TM    PRINT+48,X'10'               WAR OPEN ERFOLGREICH..
         BO    B10010                        JA
         LA    R2,NOPRINT                   ZUGRIFFS-ROUTINE
         STCM  R2,7,PRINT+49                 SIMULIEREN
         MVC   XDATARC,FW04                 RC, WENN SYSPRINT FEHLT
B10010   NULL
         L     R1,16                        CVT-ADRESSE
         L     R1,0(,R1)                    TCB-WORDS
         L     R1,0(,R1)                    NEW-TCB
         L     R1,12(,R1)                   TIOT-ADRESSE
         LA    R1,0(,R1)                    CLEAR HIGH ORDER BYTE
         ST    R1,TIOTADR                   SICHERN
         MVC   JOBINFO(24),0(R1)            JOBNAME/STEPNAME/PROCNAME
         XTIME ,                            DATUM/UHRZEIT AUFBEREITEN
         MVC   JOBINFO+24(16),3(R1)         TAGESDATUM/UHRZEIT
         LA    R1,JOBINFO                   JOB-INFORMATIONEN
         LA    R0,24                        LAENGE
B10020   NULL
         CLI   0(R1),BLANK                  BLANKS WERDEN
         BNE   B10030                        IN MINUS
         MVI   0(R1),MINUS                    UMGESETZT,
B10030   NULL                                  WEIL DAS BESSER
         LA    R1,1(,R1)                        AUSSIEHT.
         BCT   R0,B10020
         L     R3,0(,R5)                    PARM-ADR
         SR    R2,R2                        LOESCHEN
         AH    R2,0(,R3)                    PARM-LAENGE
         BZ    B12001                       KEINE PARM-EINTRAGUNG
         EJECT
***********************************************************************
**       PARM-EINTRAGUNGEN PRUEFEN                                   **
***********************************************************************
         SPACE
B10200   NULL                               PARM-EINTRAGUNGEN PRUEFEN
         BCTR  R2,0                         PARMLAENGE - 1
         STC   R2,*+5                       LAENGE NACH MVC
         MVC   DB+21(*-*),2(R3)             TEXT NACH DB
         MVT   DB,'-PARM - EINTRAGUNG:'     TEXT
         BAL   R9,B61001                    DRUCKEN
         LA    R1,2(,R3)                    PARM-ANFANG
         LR    RE,R1                        SICHERN
         LH    R2,0(,R3)                    PARMLAENGE
B10210   NULL
         CLI   0(R1),KOMMA                  KOMMA SUCHEN
         BE    B10230                        GEFUNDEN
         CLI   0(R1),EQUAL                  SCHLUESSELWORT..
         BNE   B10220                        NEIN
         OI    PARMCODE,X'01'               KENNZEICHNEN
B10220   NULL
         LA    R1,1(,R1)                    NAECHSTES ZEICHEN
         BCT   R2,B10210                    R2 ENTHAELT ANZ ZEICHEN
B10230   NULL
         TM    PARMCODE,X'01'               SCHLUESSELWORT..
         BO    B10250                       JA
         NI    PARMCODE,255-SWEXIT          SCHALTER AUS
         MVC   UPRONAME,BLANKS               LOESCHEN
         SR    R1,RE                        LAENGE ERMITTELN
         BZ    B81010                       FEHLER
         CL    R1,FW08                      MAXIMAL LAENGE
         BH    B81010                       FEHLER
         BCTR  R1,0                         R1 - 1
         STC   R1,*+5                       LAENGE NACH MVC
         MVC   UPRONAME(*-*),0(RE)          NAMEN SPEICHERN
         OI    PARMCODE,SWEXIT              AKTIVIEREN
B10240   NULL
         LTR   R2,R2                        ENDE..
         BZ    B12001                       JA
         LA    R1,1(R1,RE)                  POINTER AUF NAECHSTES ZEICH
         LA    RE,1(,R1)                     STELLEN
         B     B10220                       WEITER
B10250   NULL
         NI    PARMCODE,255-X'01'           SCHALTER AUS
         CLC   0(6,RE),=C'ABEND='           ABEND-PARAMETER..
         BE    B10255                       JA
         CLC   0(9,RE),=C'MSGLEVEL='        MSGLEVEL-PARAMETER..
         BE    B10270                        JA
         CLC   0(5,RE),=C'DATA='            DATA-PARAMETER..
         BE    B10280                       JA
         B     B81010                       FALSCHE PARM-EINTRAGUNG
         EJECT
B10255   NULL
         LA    RF,6(,RE)                    ANFANG WERT
         SR    R1,RF                        ENDE - ANF
         BNP   B81010                       FEHLER
         LR    R0,R1                        LAENGE
         CL    R0,FW04                      MAX-LAENGE
         BH    B81010                        UEBERSCHRITTEN
B10260   NULL
         CLI   0(RF),C'0'                   PRUEFEN
         BL    B81010                        AUF
         CLI   0(RF),C'9'                     NUMERISCHEN INHALT
         BH    B81010
         LA    RF,1(,RF)                    POINTER + 1
         BCT   R0,B10260
         BCTR  R1,0                         R1 - 1
         STC   R1,*+9                       LAENGE NACH PACK
         OI    *+5,X'70'                    2. LAENGE
         PACK  WORK(*-*),6(*-*,RE)          AB-CODE PACKEN
         CVB   R0,WORK                      BINAER MACHEN
         ST    R0,MAXABEND                  SPEICHERN
         OI    PARMCODE,SWABEND             SCHALTER AN
         LA    R1,6(R1)                     R1 BERICHTIGEN
         B     B10240                       WEITER
B10270   NULL
         NI    PARMCODE,255-SWSYSIN-SWINFO  SCHALTER AUS
         LA    RF,9(,RE)                    AUF ANFANG
         SR    R1,RF                        LAENGE
         BCT   R1,B81010                    FEHLER
         CLI   0(RF),C'0'                   MSGLV 0 - 3 SIND GUELTIG
         BL    B81010                       FEHLER
         CLI   0(RF),C'3'
         BH    B81010                       FEHLER
         PACK  0(1,RF),0(1,RF)              MSG PACKEN
         MVN   0(1,RF),BLANKS                LOESCHEN
         OC    PARMCODE,0(RF)               BITS AN
         LA    R1,9(,R1)                    R1 BERICHTIGEN
         B     B10240                       WEITERMACHEN
B10280   NULL
         LA    RF,5(,RE)                    AUF ANFANG
         MVC   DATALEN,=X'0001'             LAENGE
         MVC   DATAFLD,BLANKS               LOESCHEN
         SR    R1,RF                        LAENGE
         BZ    B81010                       FEHLER
         STH   R1,DATALEN                   LAENGE VON DATA
         BCTR  R1,0                         LAENGE - 1
         STC   R1,*+5                        NACH MVC
         MVC   DATAFLD(*-*),0(RF)
         LA    R1,5(,R1)                    R1 BERICHTIGEN
         B     B10240                       WEITERMACHEN
         EJECT
B12001   NULL
         DDSEARCH SYS,1,B14001              IST SYSIN DA
         TM    PARMCODE,SWSYSIN             SOLL SYSIN GEDRUCKT WERDEN
         BO    B12010                        JA
         L     R2,=A(B91005)                DRUCKROUTINE ABHAENGEN
         MVI   1(R2),240                    BRANCH UM DRUCK
B12010   NULL
         MVT   DB,'-SYSIN STEUERKARTEN'
         BAL   R9,B61001                    DRUCKEN
         CALL  XDACGEN,(B91001,MODAREA,PROCADR),VL CALL ENTSCHLUESSLER
         CL    RF,XDATARC                   RC SCHON GROESSER
         BL    B12090
         ST    RF,XDATARC                   SICHERN
B12090   NULL
         MVT   DB,' SYSIN EINGABE-ENDE'
         BAL   R9,B61100                    DRUCKEN
         NI    SYSSCH,255-BIT1              SYSIN INAKTIV
         L     R2,=A(B91005)                DRUCKROUTINE
         MVI   1(R2),NULL                    WIEDER AKTIVIEREN
         B     B14020
         EJECT
B14001   NULL
         XC    ISAMKEY,ISAMKEY              KEY LOESCHEN
B14020   NULL
         CLC   XDATARC,FW04                 FEHLT SYSPRINT..
         BH    B49900                       FEHLER SYSIN
         LA    R8,B21001                    ANFANG STEUERLEISTE
B14040   NULL
         DDSEARCH QSE,4,B14180              IST EINGABE DA
         MVC   EINSTB(LNSTB),0(RE)          STB UEBERTRAGEN
         MVT   DB+10,'EINGABE :'
         STL   B21110                       EINGABE AKTIVIEREN
         L     R1,16(,R1)                   UCB-ADR
         LA    R3,EINSTB                    STB-ADR
         L     R2,EINDCB                    DCB-ADRESSE
         MVC   40(8,R2),EINDDN              DDN NACH DCB
         TM    18(R1),BIT2                  DASD-DATEI..
         BZ    B14100                        NEIN, OPEN INPUT
         CLI   EINDDN+5,C'X'                UPDATE - FILE ..
         BNE   B14100                        NEIN, INPUT
         CLI   UPRONAME,BLANK               USER-EXIT..
         BNE   B14060                        JA
         OC    PROCADR,PROCADR              PROC GENERIERT..
         BZ    B14100                        NEIN
         NI    B21630+1,NULL                PUTX ROUTINE FREIGEBEN
         B     B14080                       OPEN UPDATE
B14060   NULL
         NI    B21230+1,NULL                PUTX ROUTINE FREIGEBEN
B14080   NULL
         OPEN  ((2),UPDAT)                  OPEN FUER UPDATE
         AP    AUSANZ,P1                    AUSGABEN + 1
         B     B14120                       RDJFCB
B14100   NULL
         OPEN  ((2),INPUT)                  OPEN FUER INPUT
B14120   NULL
         BAL   R9,B72001                    DRUCKEN JFCB INFORMATIONEN
         CLI   EINDDN+3,C'I'                ISAM-DATEI
         BNE   B14140                        NEIN
         OC    ISAMKEY,ISAMKEY              ANFANG MIT KEY
         BZ    B14140                        NEIN
         SETL  ISAMEIN,KH,ISAMKEY           SET LIMIT
B14140   NULL
         ZAP   ZAEDROP,ZAEDROP              IST DROP DA..
         BNP   B14160                        NEIN
         NI    B21140+1,NULL                EXITS
         NI    B21150+1,NULL                 FREIGEBEN
B14160   NULL
         ZAP   INDSKIP,INDSKIP              IST SKIP DA..
         BNP   B14180                        NEIN
         NI    B21140+1,NULL                NOP IN
         NI    B21160+1,NULL                 GET ROUTINE
         EJECT
B14180   NULL
         OC    PROCADR,PROCADR              IST PROC GENERIERT..
         BZ    B14220                        NEIN
         CLI   UPRONAME,BLANK               USER-EXIT DA..
         BE    B14200                        NEIN
         TM    PROCADR,X'40'                PROC SECOND..
         BZ    B14200                        NEIN
         OI    PARMCODE,BIT6                PROC SECOND + USER-EXIT
         B     B14220                       ERST USER-EXIT RUFEN
B14200   NULL
         STL   B21600                       STEUERLEISTE
         ST    R8,PROCSAVE                  RETURN SICHERN
         TM    PARMCODE,BIT6                EXITS VERTAUSCHT
         BO    B14240
B14220   NULL
         CLI   UPRONAME,BLANK               USER-EXIT..
         BE    B14240                        NEIN
         LOAD  EPLOC=UPRONAME,DCB=0         USER-EXIT LADEN
         ST    R0,UPROADR                   ENTRY-POINT SICHERN
         STL   B21200                       STEUERLEISTE
         ST    R8,EXITSAVE                  ANSCHLUSS-ADR
         TM    PARMCODE,BIT6                EXITS VERTAUSCHT
         BO    B14200                        JA
B14240   NULL
         DDSEARCH QSA,1,B14360              IST SYSUT2 DA
         OI    SWAUS,241                    SCHALTER AUSGABE
         ZAP   ZAECOPY,ZAECOPY
         BZ    B14300                       WERT = 0
         BM    B14280                       WERT < 0
         NI    B21310+1,NULL                EXITS
         NI    B21330+1,NULL                 FREIGEBEN
B14280   NULL
         AP    AUSANZ,P1                    ANZAHL AUSGABEN + 1
         STL   B21300                       STEUERLEISTE
         EJECT
B14300   NULL
         OPEN  (QSAMAUS,OUTPUT)
         DDSEARCH UT3,2,B14360              IST SYSUT3/SYSDEL DA..
         MVC   QS2STB(LNSTB),0(RE)          STB UEBERTRAGEN
         L     R1,QS2DCB                    DD-NAMEN
         MVC   40(8,R1),QS2DDN               NACH DCB
         CLI   QS2DDN+5,C'3'                EXIT-ROUTINEN
         BNE   B14320                        IN ABHAENGIGKEIT
         MVI   B21310+1,NULL                  VOM DD-NAMEN
         MVI   B21320+1,NULL                   AKTIVIEREN
         B     B14340
B14320   NULL
         MVI   B21100+1,NULL                DELETE-FILE AKTIVIEREN
B14340   NULL
         OPEN  (QSA2AUS,OUTPUT)             OPEN SYSUT3/SYSDEL
         SPACE 2
B14360   NULL
         DDSEARCH ISA,1,B14420              IST SYSIS2 DA
         OI    SWAUS,241                    SCHALTER AUSGABE
         ZAP   ZAECOPY,ZAECOPY
         BZ    B14400
         BM    B14380                       WERT < 0
         NI    B21410+1,NULL
B14380   NULL
         AP    AUSANZ,P1                    ANZAHL AUSGABEN + 1
         STL   B21400                       STEUERLEISTE
B14400   NULL
         OPEN  (ISAMAUS,OUTPUT)
         SPACE 2
B14420   NULL
         ZAP   ZAELIST,ZAELIST              ANZAHL = 0
         BZ    B15001                        JA
         DDSEARCH CHA,5,B15001              IST DRUCKDATEI DA
         MVC   PRTSTB(LNSTB),0(RE)          STB UEBERTRAGEN
         NI    PRTSCH,255-X'40'             AKTIV AUS
         MVC   PRTDCB(16),0(RE)             AUSGEWAEHLTEN STB
         AP    AUSANZ,P1                    ANZAHL AUSGABEN + 1
         STL   B21500                       STEUERLEISTE
         OI    PRTSCH,DDSTMT+OPENED         SCHALTER AKTIV
         L     R0,=V(XDALIST)
         ST    R0,LISTADR                   ENTRY-POINT SICHERN
         ZAP   ZAELIST,ZAELIST              ZAEHLER TESTEN
         BM    B14440                       KEINE LIST-BEGRENZUNG
         MVI   B21520+1,NULL                EXIT FREIGEBEN
         EJECT
B14440   NULL
         CLC   MODT1,BLANKS                 TITELZEILE LEER..
         BNE   B14460
         CLC   MODT2,BLANKS
         BE    B15001
B14460   NULL
         L     RF,LISTADR                   ENTRY DRUCK-UPRO
         CALL  (15),(MODHDR1),VL             INIT FUER UEBERSCHRIFTEN
B15001   NULL
         TM    PROCADR,X'80'                SEPARATE FILES..
         BZ    B17001                        NEIN
         DDSEARCH U21,1,B15040                SYSUT21
         BAL   R9,B15200                    OPEN
B15040   NULL
         DDSEARCH U22,1,B15060                SYSUT22
         BAL   R9,B15200                    OPEN
B15060   NULL
         DDSEARCH U23,1,B15080                SYSUT23
         BAL   R9,B15200                    OPEN
B15080   NULL
         DDSEARCH U24,1,B15100                SYSUT24
         BAL   R9,B15200                    OPEN
B15100   NULL
         DDSEARCH U25,1,B15120                SYSUT25
         BAL   R9,B15200                    OPEN
B15120   NULL
         DDSEARCH U26,1,B15140                SYSUT26
         BAL   R9,B15200                    OPEN
B15140   NULL
         DDSEARCH U27,1,B15160                SYSUT27
         BAL   R9,B15200                    OPEN
B15160   NULL
         DDSEARCH U28,1,B15180                SYSUT28
         BAL   R9,B15200                    OPEN
B15180   NULL
         STL   B21700                       STEUERLEISTE
         B     B17001
B15200   NULL
         L     R2,0(,RE)                    DCB ADR
         OPEN  ((2),OUTPUT)                 OPEN SEPARATE FILE
         OI    SWAUS,240                    SCHALTER AUSGABE
         AP    AUSANZ,P1                    ANZAHL AUSGABEN + 1
         BR    R9                           RETURN
         EJECT
B17001   NULL
         TM    PARMCODE,BIT6                EXITS TAUSCHEN..
         BO    B17040                        JA
         CLI   UPRONAME,BLANK               USER-EXIT
         BE    B17040                        NEIN
B17020   NULL
         MVC   0(2,R8),NOOP                 NOP, BRANCH BEI INSERT
         ST    R8,INSEXIT                   POINTER SICHERN
         STL   B21250                       STEUERLEISTE
         TM    PARMCODE,BIT6                EXITS TAUSCHEN..
         BO    B17060                        JA
B17040   NULL
         OC    PROCADR,PROCADR              PROC GENERIERT..
         BZ    B17060                        NEIN
         MVC   0(2,R8),NOOP                 NOP, BRANCH BEI INSERT
         ST    R8,INSPROC                   POINTER SICHERN
         STL   B21650                       STEUERLEISTE
         SPIE  SPIEEXIT,(7)                 0C7 ABFANGEN
         TM    PARMCODE,BIT6                EXITS TAUSCHEN..
         BO    B17020                        JA
B17060   NULL
         MVC   0(2,R8),BRANCH               RETURN AN DEN ANFANG
         SPACE 2
B19001   NULL
         TM    EINSCH,BIT1                  EINGABE AKTIV..
         BZ    B49001                        NEIN, EOF
         CLI   SWAUS,BLANK                  AUSGABE VORHANDEN
         BE    B19010                        NEIN
         ZAP   AUSANZ,AUSANZ                ZAEHLER = 0
         BZ    B49001                        JA, ENDE
B19010   NULL
         B     B21001
         EJECT
B21001   NULL                               ENTRY STEUERLEISTE
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         SPACE 2
         BAL   R8,B21001
         EJECT
B21100   B     B21102                       NOP BEI SYSDEL
         LA    R1,QSA2AUS                   DCB-ADR
         BAL   R9,B64001                    SCHREIBEN
B21102   NULL
         AP    ZAEDEL,P1                    DELETE ZAEHLEN
         L     R8,DELADR                    RETURN ADR
         BR    R8                           RETURN
         SPACE 2
B21110   NULL
         L     R1,EINDCB                    DCB-ADRESSE
         GET   (1)                          LESEN EINGABE
B21130   NULL
         ST    R1,SATZADR                   RECORD POINTER
         AP    ZAEEIN,P1                    SATZZAEHLER
B21140   BR    R8                           EXIT, NOP DURCH -MOD KARTE
B21150   B     B21160                       NOP DURCH DROP
         CP    ZAEEIN,ZAEDROP               SATZ UEBERLESEN
         BL    B21100                        JA
         OI    B21150+1,240                 DROP ABHAENGEN
         AP    ZAESKIP,ZAEDROP              SKIP ANFANG EINSTELLEN
         B     B21100                       NEU LESEN
B21160   BR    R8                           EXIT, NOP DURCH SKIP
         CP    ZAEEIN,ZAESKIP               SATZ SKIPPEN
         BL    B21100                        JA
         AP    ZAESKIP,INDSKIP              SKIP ZAEHLER + INDEX
         BR    R8                           EXIT
         SPACE 2
CONCOPER B     B21180                       BEI DATASET WECHSEL
B21180   NULL
         MVC   B21130(4),CONCSAVE           INSTR. ZURUECK
         TM    PARMCODE,SWINFO              DATEI-INFORMATIONEN..
         BZ    B21110                        NEIN
         ZAP   WORK(8),ZAEEIN               EINGABE-ZAEHLER
         SP    WORK(8),ZAEDAT
         MVC   DB+30(6),SAETZE              TEXT
         BAL   R9,B62001
         MVC   DB+42(9),0(R1)               ANZAHL
         BAL   R9,B62110                    DRUCKEN
         ZAP   ZAEDAT,ZAEEIN                ANZAHL VOR DATEI-WECHSEL
         MVI   DB,C'0'                      VORSCHUB
         LA    R3,QSEDCB
         BAL   R9,B72003                    DRUCKEN JFCB INFORMATIONEN
         B     B21110                       NEU LESEN
         EJECT
B21200   NULL                               ENTRY CALL USER EXIT
         L     R3,SATZADR                   RECORD POINTER
         L     R4,EINDCB                    DCB-ADR EINGABE
         L     RF,UPROADR                   ENTRY-POINT
         CALL  (15),((3),(4),B91001),VL     CALL USER-EXIT
         CL    RF,FW12                     RC=EINFUEGEN
         BE    B21210
         LTR   R3,R3                        EOF EINGABE
         BZ    B49005
         CL    RF,FW00                      RC=AUSGEBEN
         BE    B21220                       SATZADR VOM UPRO UEBERNEH.
         CL    RF,FW04                      RC=UEBERLESEN
         BE    B21240                       NEU LESEN
         MVI   UPRONAME,BLANK               UPRO ABHAENGEN
         CL    RF,FW08                      RC=ENDE
         BE    B49001                       END-ROUTINE
         ST    RF,UPRORC                    FALSCHER CODE
         B     B49001
B21210   NULL
         MVC   SAVEADR,SATZADR              SATZADR ALT
         ST    R1,SATZADR                   SATZADR NEU
         ZAP   AUSANZ,AUSANZ                AUSGABE MOEGLICH
         BZ    B21200                        NEIN
         AP    ZAEINS,P1                    ZAEHLER INSERT
         L     R2,INSEXIT                   INSERT-ADR
         MVC   0(2,R2),BRANCH
         BR    R8                           EXIT
B21220   NULL
         ST    R1,SATZADR                   INS RECORD POINTER
B21230   BR    R8                           EXIT, NOP BEI PUTX
         L     R1,EINDCB                    UPDATE-DCB
         PUTX  (1)                          SATZ ZURUECKSCHREIBEN
         AP    ZAEUPD,P1                    UPDATES ZAEHLEN
         BR    R8                           EXIT
B21240   NULL                               EXIT BEI DELETE
         L     R8,INSEXIT                   RETURN ADR
         LA    R8,4(,R8)                     + 4
         ST    R8,DELADR                    SICHERN
         B     B21100
B21250   NULL                               ENTRY NACH INSERT
         L     R2,INSEXIT                   INSERT-ADR
         MVC   0(2,R2),NOOP                 NOP EINSETZEN
         L     R8,EXITSAVE                  RETURN-ADR
         MVC   SATZADR,SAVEADR              SATZADR ZURUECK
         B     B21200                       CALL USER EXIT
         EJECT
B21300   NULL                               ENTRY QSAM AUSGABE
         LA    R1,QSAMAUS                   DCB-ADR
         BAL   R9,B64001                    SCHREIBEN
         AP    ZAEQSA,P1                    SATZZAEHLER
B21310   BR    R8                           NOP BEI SYSUT3
B21320   B     B21330                       NOP BEI SYSUT3
         LA    R1,QSA2AUS                   DCB-ADR
         BAL   R9,B64001                    SCHREIBEN
B21330   BR    R8                           NOP BEI COPY
         CP    ZAEQSA,ZAECOPY               LIMIT ERREICHT
         BLR   R8                            NEIN, EXIT
         BAL   RE,B62300                    FUNKTION AUSSCHALTEN
         SP    AUSANZ,P1                    AUSGABE - 1
         BZ    B49001                       ENDE
         BR    R8                           EXIT
         SPACE 2
B21400   NULL                               ENTRY ISAM-AUSGABE
         LA    R1,ISAMAUS                   DCB-ADR
         BAL   R9,B64001                    SCHREIBEN
         AP    ZAEISA,P1                    SATZZAEHLER
B21410   BR    R8                           EXIT, NOP BEI COPY
         CP    ZAEISA,ZAECOPY               LIMIT ERREICHT
         BLR   R8                           EXIT
         BAL   RE,B62300                    FUNKTION AUSSCHALTEN
         SP    AUSANZ,P1                    AUSGABE - 1
         BZ    B49001                       ENDE
         BR    R8                           EXIT
         EJECT
B21500   NULL                               ENTRY DRUCKEN
         L     R3,SATZADR                   RECORD POINTER
         L     R1,EINDCB                    EINGABE-DCB
         LTR   R1,R1                        KEINE EINGABE
         BZ    B21510                        JA
         LH    R4,82(,R1)                   LRECL
         TM    36(R1),X'80'                 RECFM=F
         BO    B21510
         TM    36(R1),X'40'
         BZ    B21510                       KEIN EINGABE-DCB LRECL=100
         LH    R4,0(,R3)                    LRECL VAR-SAETZE
         LTR   R3,R3                        EODAD EINGABE..
         BZ    B21510                        JA
         LA    R3,4(,R3)                    RECFM=V
         S     R4,FW04                      SATZLAENGE - 4
B21510   NULL
         ST    R4,PRINTLN                   SATZLAENGE
         L     RF,LISTADR                   XDALIST ENTRY POINT
         CALL  (15),((3),PRINTLN),VL        CALL PRINT UPRO
         LTR   R3,R3                        EODAD EINGABE
         BZ    B49010
         AP    ZAEPRT,P1                    ZAEHLER PRINT
B21520   BR    R8                           EXIT
         CP    ZAEPRT,ZAELIST               LIMIT ERREICHT
         BLR   R8                           EXIT
         BAL   RE,B62300                    FUNKTION AUSSCHALTEN
         SP    AUSANZ,P1                    AUSGABE - 1
         BZ    B49001                       ENDE
         BR    R8                           EXIT
         EJECT
B21600   NULL                               ENTRY PROC ROUTINE
         L     R1,SATZADR                   RECORD POINTER
         L     RF,PROCADR                   ENTRY-POINT
         CALL  (15)                         CALL PROC-EXIT
B21605   B     B21610(RF)                   WEITER GEMAESS RC
B21610   B     B21620                       RC = 00
         B     B21640                       RC = 04
         B     B49001                       RC = 08
         LTR   R1,R1                        NEW AREA ANGELEGT
         BZ    B21690                        NEIN, FEIERABEND
         AP    ZAEINS,P1                    INSERT-ZAEHLER
         MVC   EXITADR,SATZADR              RECORD-POINTER
         ST    R1,SATZADR                   NEUER POINTER
         CLR   RF,R0                        SIND DIE INHALTE IDENTISCH
         BNE   B21660                        NEIN, WRITE-BEFEHL
         L     R2,INSPROC                   INSERT-ADR
         MVC   0(2,R2),BRANCH               AKTIVIEREN
         BR    R8                           EXIT
B21620   NULL                               RC = 0
         LTR   R1,R1                        NEW AREA ANGELEGT
         BZ    B21690                        NEIN, FEIERABEND
         ST    R1,SATZADR                   RECORD-POINTER
B21630   BR    R8                           NOP BEI PUTX
         L     R1,EINDCB                    DCB-ADRESSE
         PUTX  (1)                          SATZ UPDATEN
         AP    ZAEUPD,P1                    ZAEHLER
         BR    R8                           RETURN
B21640   NULL                               EXIT BEI DELETE
         L     R8,INSPROC                   RETURN ADR
         LA    R8,4(,R8)                     + 4
         ST    R8,DELADR                    SICHERN
         B     B21100
B21650   NULL
         L     R2,INSPROC                   INSERT-ADR
         MVC   0(2,R2),NOOP                 FUNKTION TOT
         L     R8,PROCSAVE                  RETURN-ADR
         MVC   SATZADR,EXITADR              POINTER ZURUECK
         B     B21600                       ERNEUTER AUFRUF
         SPACE
B21660   NULL                               ENTRY WRITE ROUTINE
         ST    R0,NEWSTART                  ENTRY ADRESSE PROC
         ST    RE,NEWENTRY                  NEUER ENTRY-POINT
         MVC   SAVE2,12(RD)                 SAVEAREA FESTHALTEN
         LH    R2,0(,RE)                    OFFSET
         SLL   R2,3                          * 8
         L     RF,WRITER(R2)                OFFSET IN WRITE TABLE (STL)
         TM    0(RF),DDSTMT+OPENED          DATASET AKTIV
         BZ    B21680                        NEIN
         LA    RF,WRITER(R2)                FILE NO
         MVC   WORK(1),0(RF)                 NACH WORK
         L     RF,WRITER+4(R2)              BRANCH ADR
         ST    R8,SAVER8                    RETURN ADR SICHERN
         BALR  R8,RF                        AUSGABE
         L     R8,SAVER8                    RETURN ADR ZURUECKLADEN
         EJECT
B21670   NULL
         L     R1,EXITADR                   RECORD POINTER ALT
         L     R2,SATZADR                   RECORD POINTER NEU
         ST    R1,SATZADR                   RECORD POINTER ALT
         L     RF,PROCADR                   ENTRY ADR PROC
         L     R5,SVSTART(,RF)              ADR SYSVARS
         MVC   12(60,RD),SAVE2              SAVEAREA ZURUECK
         LA    RE,B21605                    ALTE RETURN ADR
         L     RF,NEWSTART                  ENTRY POINT PROC
         L     R3,NEWENTRY                  RETURN ADR IN EXIT
         B     2(,R3)                       RETURN IN EXIT
         SPACE
B21680   NULL
         TM    0(RF),B'00100000'            FEHLERMELDUNG GEDRUCKT
         BO    B21670                        JA
         OI    0(RF),B'00100000'
         LA    R1,4(,RF)                    DDNAME
         ST    R1,MSGPARM4
         LA    RF,080                       MSG-NUMMER
         BAL   R9,B64100                    MSG EDIT
         AP    ZAEDEL,P1
         BAL   R9,B61001                    DRUCKEN
         B     B21670                       WEITER
         SPACE
B21690   NULL
         LA    RF,090                       MSG-NUMMER
         BAL   R9,B64100                    MSG EDIT
         BAL   R9,B61001                    DRUCKEN
         B     B49001                       ENDE
         EJECT
B21700   NULL                               ENTRY SEPARATE-FILES
         L     RF,PROCADR                   ADR SYSIN-EXIT
         L     RF,SVSTART(,RF)              ADR SYSVARS
         USING DSVARS,RF
         CLI   $FILE,C'1'                   UNTERGRENZE $FILE
         BL    B21100                        UNGUELTIG
         CLI   $FILE,C'8'                   OBERGRENZE
         BH    B21100                        UNGUELTIG
         PACK  WORK(1),$FILE                UMDREHEN
B21710   NULL                               ENTRY SEPARATE-FILES
         SR    R1,R1                        LOESCHEN
         IC    R1,WORK                      NACH R1
         SRL   R1,4                         SHIFTEN
         BCTR  R1,0                         R1 - 1
         SR    R0,R0                        LOESCHEN
         LA    RE,LNSTB                     LAENGE STB
         LR    R2,R1                        SICHERN
         MR    R0,RE                        OFFSET
         LA    R1,U21STB(R1)                 ERMITTELN
         TM    0(R1),DDSTMT+OPENED          AKTIV..
         BZ    B21100                        NEIN
         L     R1,0(,R1)                    DCB-ADR
         BAL   R9,B64001                    SATZ AUSGEBEN
         SLL   R2,3                         R2 * 8
         LA    R2,ZAEU21(R2)                SATZZAEHLER
         AP    0(L'ZAEU21,R2),P1             ERHOEHEN
B21790   BR    R8                           EXIT, NOP BEI COPY
         BR    R8                           COPY WIRKUNGSLOS
         EJECT
B49001   NULL                               EODAD EINGABE
         XC    SATZADR,SATZADR              ANZEIGE EODAD
         CLI   UPRONAME,BLANK               WAR UPRO DA..
         BE    B49005                        NEIN
         MVI   UPRONAME,BLANK               ABHAENGEN
         L     R8,EXITSAVE                  RETURN ADR
         TM    PARMCODE,BIT6                EXITS VERTAUSCHT..
         BZ    B21200                       USER-EXIT RUFEN
         L     R2,PROCSAVE                  ANSCHLUSS-ADR
         S     R2,FW04                       - 4
         MVC   0(2,R2),NOOP                 PROC DEAKTIVIEREN
         B     B21200                       LETZTER AUFRUF
B49005   NULL
         TM    PRTSCH,X'40'                 PRINT AKTIV
         BZ    B49010                        NEIN
         NI    PRTSCH,255-X'40'             AUS
         B     B21500                       AUFRUF FUER CLOSE
B49010   NULL
         TM    EINSCH,X'40'                 EINGABE DA
         BZ    B49020                        NEIN
         NI    EINSCH,255-X'40'             INAKTIV
         L     R2,EINDCB                    DCBADR
         CLOSE ((2))                        DATEI SCHLIESSEN
         ZAP   ZAEDAT,ZAEDAT
         BZ    B49020
         TM    PARMCODE,SWINFO              DATEI-INFORMATIONEN
         BZ    B49020                        NEIN
         ZAP   WORK(8),ZAEEIN               EINGABE-ZAEHLER
         SP    WORK(8),ZAEDAT
         MVC   DB+30(6),SAETZE              TEXT
         BAL   R9,B62001
         MVC   DB+42(9),0(R1)               ANZAHL
         BAL   R9,B62110                    DRUCKEN
B49020   NULL
         LA    R6,QSASTB                    ANFANG STEUERTABELLE
         LA    R7,11                        ANZAHL
B49030   NULL
         TM    0(R6),X'40'                  STEUERBLOCK AKTIV
         BZ    B49040                        NEIN
         MVT   DB+10,'AUSGABE :'            TEXT
         LR    R3,R6                        POINTER STB
         BAL   R9,B72001                    DRUCKEN JFCB INFORMATIONEN
         L     R2,0(,R6)                    DCB ADR
         NI    0(R6),255-X'40'              STB INAKTIV
         CLOSE ((2))                        DATEI SCHLIESSEN
B49040   NULL
         LA    R6,LNSTB(,R6)                NAECHSTER STB
         BCT   R7,B49030                    WEITER
         EJECT
B49100   NULL
         ZAP   ZAEAUS,ZAEQSA                AUSGABE SAETZE
         CP    ZAEAUS,ZAEISA                 AUS ISAM
         BNL   *+10                          ODER QSAM
         ZAP   ZAEAUS,ZAEISA                 ERMITTELN
*                                           LAENGE
         LA    RE,ZAEEIN                     DES GROESSTEN
         LA    RF,ZAEAUS                      WERTES
         LA    R0,5                            ERMITTELN
B49210   NULL
         CP    0(5,RE),0(5,RF)
         BNL   B49220
         LR    RE,RF
B49220   NULL
         LA    RF,5(,RF)
         BCT   R0,B49210
         MVC   WORK(24),BLANKS
         MVC   WORK(12),SBZAE
         LA    R1,WORK+11
         EDMK  WORK(12),0(RE)               RE ENTHAELT ADR DES
*                                            ZAEHLERS MIT DEM GROESSTEN
*                                             WERT
         LR    R3,R1                        ANFANG SICHERN
         MVI   DB,C'-'                      VORSCHUB
         TM    EINSCH,BIT0                  WAR EINGABE DA..
         BZ    B49230                        VORHANDEN
         MVT   DB+28,'EINGELESEN'           TEXT
         MVC   WORK(12),SBZAE               SATZZAEHLER
         ED    WORK(12),ZAEEIN               EINGABE
         MVC   DB+42(12),0(R3)                DRUCKEN
         BAL   R9,B62100                    DRUCKEN
         MVI   DB,C'0'                      VORSCHUB
         ZAP   ZAEUPD,ZAEUPD                UPDATE - FILE..
         BZ    B49230                        NEIN
         MVT   DB+28,'UPGEDATET'            TEXT
         MVC   WORK(12),SBZAE               SCHABLONE
         ED    WORK(12),ZAEUPD              ANZAHL UPDATES
         MVC   DB+42(12),0(R3)               DRUCKEN
         BAL   R9,B62100                    DRUCKEN
         MVI   DB,C'0'                      VORSCHUB
B49230   NULL
         CLI   SWAUS,C'1'                   AUSGABE
         BL    B49240
         MVT   DB+28,'AUSGEGEBEN'           TEXT
         MVC   WORK(12),SBZAE
         ED    WORK(12),ZAEAUS
         MVC   DB+42(12),0(R3)
         BAL   R9,B62100                    DRUCKEN
         MVI   DB,C'0'
         EJECT
B49240   NULL
         ZAP   ZAEDEL,ZAEDEL                DELETES
         BZ    B49250
         MVT   DB+28,'UEBERLESEN'           TEXT
         MVC   WORK(12),SBZAE
         ED    WORK(12),ZAEDEL
         MVC   DB+42(12),0(R3)
         BAL   R9,B62100                    DRUCKEN
         MVI   DB,C'0'
B49250   NULL
         ZAP   ZAEINS,ZAEINS                INSERTES
         BZ    B49260
         MVT   DB+28,'EINGEFUEGT'           TEXT
         MVC   WORK(12),SBZAE
         ED    WORK(12),ZAEINS
         MVC   DB+42(12),0(R3)
         BAL   R9,B62100                    DRUCKEN
         MVI   DB,C'0'                      VORSCHUB
B49260   NULL
         TM    PRTSCH,X'80'                 WURDE GEDRUCKT..
         BZ    B49270                        NEIN
         MVT   DB+28,'GEDRUCKT'             TEXT
         MVC   WORK(12),SBZAE
         ED    WORK(12),ZAEPRT
         MVC   DB+42(12),0(R3)
         BAL   R9,B62100                    DRUCKEN
         MVI   DB,C'0'                      VORSCHUB
B49270   NULL
         TM    PROCADR,X'80'                SEPARATE FILES
         BZ    B49300                        NEIN
         LA    R5,U21STB                    STERERBLOECKE
         LA    R6,ZAEU21                    SATZZAEHLER
         LA    R7,8                         ANZAHL
B49280   NULL
         TM    0(R5),X'80'                  WAR DDNAME IN TIOT
         BZ    B49290                        NEIN
         MVT   DB+28,'AUF'                  TEXT
         MVC   DB+32(08),4(R5)              DDNAME
         MVC   WORK(12),SBZAE               SCHABLONE
         ED    WORK(12),3(R6)               AUFBEREITEN
         MVC   DB+42(12),0(R3)              ZAEHLER NACH DB
         BAL   R9,B62100                    DRUCKEN
         MVI   DB,C'0'                      VORSCHUB
B49290   NULL
         LA    R5,LNSTB(,R5)                NAECHSTER PLATZ
         LA    R6,8(,R6)                    NAECHSTER ZAEHLER
         BCT   R7,B49280
B49300   NULL
         EJECT
B49900   NULL
         L     R3,XDATARC                   XDATA RETURN-CODE
         SR    R2,R2                        R2 LOESCHEN
         L     RF,PROCADR                   ADRESSE SYSIN-PROC LADEN
         LTR   RF,RF                        WAR SYSIN-PROC DA..
         BZ    B49940                        NEIN
         L     RF,SVSTART(,RF)              ADRESSE SYSVARS
         ZAP   WORK(8),$RETCD               PROC RETURN-CODE
         BNP   B49940                        IST NULL
         CVB   R2,WORK                      RETURN-CODE BINAER
         CL    R2,=F'4095'                  MAXIMAL-WERT
         BNH   B49940
         L     R2,=F'4095'
B49940   NULL
         CLR   R3,R2                        RC GROESSER..
         BNL   B49950
         LR    R3,R2
B49950   NULL
         LA    R2,RCMSG                     TEXT ADRESSE
         CL    R3,MAXABEND                  ABEND ODER RETURN-CODE..
         BL    B49960                       RETURN-CODE
         LA    R2,UAMSG                     TEXT ADRESSE
         MVI   B49992+1,NULL                ABEND FREIGEBEN
B49960   NULL
         CVD   R3,WORK+16
         MVI   DB,C'-'                      VORSCHUB
         MVC   DB+10(L'ENDKON),ENDKON       SCHLUSSMELDUNG
         MVC   WORK(10),SBZBL               FUER SCHLUSSMELDUNG
         ED    WORK(10),WORK+19
         LA    R1,XDAVERS                   VERSIONS-NR
         ST    R1,MSGPARM4
         ST    R2,MSGPARM5                  TEXT RCODE ODER ABEND
         LA    R1,WORK
         ST    R1,MSGPARM6                  CODE-NUMMER
         LA    R1,MSGPARM2                  PARAMETERLISTE
         L     RF,MSGEDIT
         BALR  RE,RF
         L     R2,=A(ZZ)                    ZEILENZAEHLER LOESCHEN
         SP    0(L'ZZ,R2),=P'9'
         BAL   R9,B61001                    SCHLUSSMELDUNG DRUCKEN
         LA    R2,NOPRINT                   WAR SYSPRINT DA..
         CLM   R2,7,PRINT+49
         BE    B49990                        JA
         CLOSE PRINT
         SPACE 2
B49990   NULL
B49992   B     B49996                       NOP BEI ABEND
         ABEND (3),DUMP                     ABEND-CODE IN REG 3
B49996   NULL
         LR    RF,R3                        RETURN-CODE NACH R15
         PEND  RC=(15)                      RETURN
         EJECT
B61001   NULL                               ENTRY DRUCKROUTINE
B61100   NULL
         L     RF,=A(B91001)                EP DRUCK-ROUTINE
         CALL  (15),(DB)                    CALL DRUCK
         BR    R9
         SPACE 3
B62001   NULL                               AUFBEREITEN ZAHLEN
         LA    RE,WORK+3
         MVC   WORK+8(24),BLANKS             DER ANFANG DES
         MVC   WORK+8(10),SBZBL              FELDES STEHT NACH
         LA    R1,WORK+17                     RUECKKEHR IN R1
         EDMK  WORK+8(10),0(RE)
         BR    R9                           RETURN
         SPACE 3
B62100   NULL                               TEXT ERGAENZEN UND DRUCKEN
         MVC   DB+21(6),SAETZE              TEXT
B62110   NULL
         MVI   DB+40,EQUAL                  GLEICHHEITS-ZEICHEN
         B     B61001                       ZUM DRUCKEN
         SPACE 2
B62200   NULL                               STEUERLEISTE AUFBAUEN
         MVC   2(2,R8),0(R9)                BRANCH-ADR EINSETZEN
         LA    R8,4(,R8)                    POINTER ERHOEHEN
         B     2(,R9)                       RETURN
         SPACE 2
B62300   NULL                               EXIT TOTLEGEN
         LA    RF,0(,R8)                    HOB LOESCHEN
         S     RF,FW04                      FOLGEADR - 4
         MVC   0(2,RF),NOOP                 NOP EINSETZEN
         BR    RE                           RETURN
         EJECT
B63001   NULL                               DCB-EXIT QSE
         NOP   B63010                       1. OPEN
         OI    *-3,240
         L     R1,EINDCB
         OI    48(R1),X'08'                 UNLIKE ATTRIBUTES
         CLC   82(2,R1),FW00                KEINE LRECL..
         BNER  RE                            DOCH
         LA    R0,100                       DEFAULT
         STH   R0,82(,R1)                    EINSETZEN
         BR    RE
B63010   NULL
         MVC   CONCSAVE,B21130              INSTR. SICHERN
         MVC   B21130(4),CONCOPER           VERKETTUNG
         BR    RE
B63020   NULL                               DCB-EXIT ISE
         BR    RE
B63200   NULL
         LA    R2,0(,R1)                    DCB-ADRESSE
         SR    R1,R1                        LOESCHEN
         A     R1,EINDCB                    KEINE EINGABE
         BZR   RE                            VORHANDEN
         CLI   36(R2),NULL                  RECFM
         BNE   *+10
         MVC   36(1,R2),36(R1)
         CLC   62(2,R2),FW00                BLKSIZE
         BNE   *+10
         MVC   62(2,R2),62(R1)
         CLC   82(2,R2),FW00                LRECL
         BNE   *+10
         MVC   82(2,R2),82(R1)
         TM    36(R2),X'C0'                 RECFM=U..
         BOR   RE                            JA
         TM    36(R2),X'40'                 RECFM=V..
         BOR   RE                            JA
         CLC   62(2,R2),FW08+2             BLKSIZE >= 8
         BNL   B63260                       JA, JUSTIEREN
         EJECT
***********************************************************************
**       OPTIMALE BLKSIZE FUER 3350 PLATTEN ERRECHNEN                **
***********************************************************************
         SPACE
         LH    R6,OVHD3350                BLOCK-OVERHEAD
         MH    R6,62(,R2)                  * BLKSIZE (1,2,3,4)
         L     R7,PTRK3350                PHYSISCHE TRACKLAENGE
         SR    R7,R6                      MAXLAENGE LOGISCH
         SR    R6,R6                      LOESCHEN FUER DIVISION
         STH   R6,FAKT3350                FAKTOR LOESCHEN
         STH   R6,MREC3350                ANZAHL RECORDS PRO SPUR
         LH    R5,62(,R2)                 BLKSIZE
         DR    R6,R5                      R7 ENTHAELT MAXSIZE LOGISCH
         CH    R7,82(,R2)                 MAXSIZE < LRECL
         BH    B63220                      JA, RECHNEN
         MVC   62(2,R2),82(R2)            BLKSIZE = LRECL
         BR    RE                         RETURN INS OPEN
B63220   NULL
         ST    R7,MAXS3350                MAXSIZE
B63240   NULL
         LH    R6,82(,R2)                 LRECL
         LH    R5,FAKT3350                BLOCKUNGSFAKTOR
         LA    R5,1(,R5)                   + 1
         STH   R5,FAKT3350                STOREN
         MH    R6,FAKT3350                LRECL * BLOCKUNGSFAKTOR
         CL    R6,MAXS3350                BLKSIZE > MAXSIZE
         BHR   RE                         RETURN INS OPEN
         STH   R6,BLKS3350                BLKSIZE MERKEN
         LH    R5,OVHD3350                OVERHEAD
         AR    R5,R6                       + BLKSIZE
         SR    R6,R6                      LOESCHEN
         L     R7,PTRK3350                TRACKLAENGE
         DR    R6,R5                      TRACKL / BLOCKL = BLK PRO TRK
         MH    R7,FAKT3350                 * FAKTOR = SATZE PRO TRACK
         CH    R7,MREC3350                IST BLKSIZE GUENSTIGER
         BL    B63240                      NEIN
         STH   R7,MREC3350
         MVC   62(2,R2),BLKS3350
         B     B63240
         SPACE 2
PTRK3350 DC    F'19254'
MAXS3350 DC    F'0'
OVHD3350 DC    H'185'
FAKT3350 DC    H'0'
MREC3350 DC    H'0'
BLKS3350 DC    H'0'
         EJECT
***********************************************************************
**       BLKSIZE BEI FESTER SATZLAENGE JUSTIEREN                     **
***********************************************************************
         SPACE
B63260   NULL
         SR    R6,R6                        LOESCHEN
         LH    R7,62(,R2)                   BLKSIZE
         LH    R5,82(,R2)                   LRECL
         CLR   R5,R7                        BLKSIZE < LRECL
         BER   RE                           BLKSIZE = LRECL
         BH    B63290                       BLKSIZE < LRECL
         DR    R6,R5                        BLOCKUNGSFAKTOR
         LTR   R6,R6                        REST = 0
         BZR   RE                            JA, EXIT
         LR    R5,R7                        FAKTOR NACH R5
         MH    R5,82(,R2)                    * LRECL
B63290   NULL
         STH   R5,62(,R2)                   IN DCB STELLEN
         BR    RE
         SPACE 2
B64001   NULL                               SCHREIBEN AUSGABEN
         TM    36(R1),X'C0'                 RECFM=U..
         BNO   B64010                        NEIN
         L     RE,EINDCB                    DCB-ADR EINGABE
         LH    RE,82(,RE)                   LRECL
         STH   RE,82(,R1)                    NACH AUSGABE DCB
B64010   NULL
         L     R0,SATZADR                   RECORD POINTER
         PUT   (1),(0)                      SCHREIBEN
         BR    R9                           RETURN
         SPACE 2
B64100   NULL                               MSG-EDIT
         ST    RF,MSGPARM1                  MSG-NUMMER
         LA    R1,MSGPARM                   PARAMETER-LISTE
         L     RF,XDAMSGS                   MESSAGE UPRO
         BALR  RE,RF                        EDIT
         CL    RF,XDATARC                   RC SCHON GROESSER
         BNHR  R9                            JA, RETURN
         ST    RF,XDATARC
         BR    R9                           RETURN
         EJECT
B71001   NULL                               SUCHEN DD-NAMEN
         L     R1,TIOTADR                   TIOT-ADRESSE
         LA    R1,24(,R1)                   DD-ENTRY
         SR    R0,R0
         STM   RE,RF,DDNADR                 ANZAHL DD-NAMEN
B71010   NULL
         IC    R0,0(,R1)                    LAENGE DD-ENTRY
         LTR   R0,R0                        ENDE DER TIOT..
         BZR   R9                            JA, NICHT GEFUNDEN
B71020   NULL
         CLC   4(8,R1),4(RE)                DDNAME IN TIOT..
         BER   R9                           JA, RETURN
         LA    RE,16(,RE)                   NAECHSTER TAB PLATZ
         BCT   RF,B71020                    NEUER VERSUCH
         AR    R1,R0                        NAECHSTES DD
         LM    RE,RF,DDNADR                 ANZAHL DD-NAMEN
         B     B71010                       WEITERSUCHEN
         SPACE 2
B72001   NULL                               ENTRY RDJFCB
         MVI   DB,C'-'                      VORSCHUB
B72003   NULL
         ST    R9,SAVER9                    RETURN SICHERN
         TM    PARMCODE,SWINFO              DATEI-INFORMATIONEN..
         BZ    B72099                        NEIN
         L     R1,=A(ZZ)                    ADR ZEILENZAEHLER
         MVC   WORK(4),0(R1)                ZWISCHENSPEICHERN
         AP    WORK(2),=P'9'                PASST NOCH ALLES
         SP    WORK(2),WORK+2(2)             AUF EINE SEITE..
         BM    B72010                       JA, GOTT SEI DANK
         ZAP   0(2,R1),2(2,R1)              ZZ = ZZMAX
         SPACE 2
B72010   NULL
         L     R3,0(,R3)                    DCB-ADRESSE AUS STB
         RDJFCB ((3))                       LESEN JFCB
         L     R1,TIOTADR                   TIOT-ADRESSE
         AH    R1,40(,R3)                   DD-ENTRY IN TIOT
         MVC   DB+21(8),4(R1)               DDNAMEN
         TM    JFCB+52,X'20'                SYSIN/SYSOUT - DATEI..
         BO    B72050                        JA
         L     R1,16(,R1)                   UCB-ADRESSE
         TM    18(R1),B'10100000'           BAND / PLATTE
         BZ    B72050                       KEIN DSNAME
         MVT   DB+30,'DSNAME'               TEXT
         MVC   DB+42(44),JFCB               DSNAME
         BAL   R9,B62110                    DRUCKEN
         TM    JFCB+86,BIT7                 PDS-MEMBER..
         BZ    B72020                        NEIN
         MVT   DB+30,'MEMBER'               TEXT
         MVC   DB+42(08),JFCB+44            MEMBER-NAME
         BAL   R9,B62110                    DRUCKEN
         EJECT
B72020   NULL
         MVT   DB+30,'CRDT'                 TEXT
         SR    R2,R2                        LOESCHEN
         IC    R2,JFCB+80                   JAHR
         CVD   R2,WORK                      DEZIMAL
         LH    R2,WORK+6                    LADEN JAHR GEPACKT
         SRL   R2,4                         VORZEICHEN WEG
         STC   R2,WORK+5                    ZUM ENTPACKEN
         ICM   R2,3,JFCB+81                 TAG
         CVD   R2,WORK+8                    DEZIMAL
         ZAP   WORK+6(2),WORK+14(2)
         UNPK  DB+42(5),WORK+5(3)           NACH DB
         OI    DB+46,240                    VORZEICHEN
         CLC   JFCB+80(3),JFCB+83           CREDATE/EXPDT
         BH    B72023                        NICHT DRUCKEN
         MVT   DB+34,'/EXPDT'               TEXT
         MVI   DB+47,C'/'                   TRENNSTRICH
         SR    R2,R2                        LOESCHEN
         IC    R2,JFCB+83                   JAHR
         CVD   R2,WORK                      DEZIMAL
         LH    R2,WORK+6                    LADEN JAHR GEPACKT
         SRL   R2,4                         VORZEICHEN WEG
         STC   R2,WORK+5                    ZUM ENTPACKEN
         LH    R2,JFCB+84                   TAG
         CVD   R2,WORK+8                    DEZIMAL
         ZAP   WORK+6(2),WORK+14(2)
         UNPK  DB+48(5),WORK+5(3)           NACH DB
         OI    DB+52,240                    VORZEICHEN
B72023   NULL
         BAL   R9,B62110                    DRUCKEN
         SPACE 2
B72030   NULL
         CLC   JFCB+118(6),BLANKS            VOL=SER BLANK
         BE    B72040
         MVT   DB+30,'VOL=SER'              TEXT
         LA    RE,JFCB+118                   AUFBEREITEN
         LA    RF,DB+42                       (DIE ERSTEN FUENF)
         LA    R0,5
B72033   CLC   0(6,RE),BLANKS
         BE    B72036
         MVC   0(6,RF),0(RE)
         LA    RE,6(,RE)
         LA    RF,7(,RF)
         BCT   R0,B72033
B72036   NULL
         BAL   R9,B62110                    DRUCKEN
         SPACE 2
B72040   NULL                               LABEL AUFBEREITEN
         SR    R2,R2
         AH    R2,JFCB+68
         BZ    B72050
         CVD   R2,WORK
         BAL   R9,B62001
         MVT   DB+30,'LABEL'                TEXT
         MVC   DB+42(5),0(R1)
         BAL   R9,B62110                    DRUCKEN
         SPACE 2
B72050   NULL
         MVC   WORK(5),BLANKS
         LA    RE,WORK
         LA    RF,36(,R3)                   RECFM DCB
         TM    0(RF),X'C0'                  RECFM=U
         BNO   *+12
         MVI   0(RE),C'U'
         B     B72053
         TM    0(RF),X'80'                  RECFM=F
         BZ    *+12
         MVI   0(RE),C'F'
         B     B72053
         TM    0(RF),X'40'                  RECFM=V
         BZ    B72056
         MVI   0(RE),C'V'
B72053   LA    RE,1(,RE)
B72056   NULL
         TM    0(RF),X'10'                  BLOCKED
         BZ    *+12
         MVI   0(RE),C'B'
         LA    RE,1(,RE)
         TM    0(RF),X'08'                  SPANNED
         BZ    *+12
         MVI   0(RE),C'S'
         LA    RE,1(,RE)
         TM    0(RF),X'04'                  ASA-VORSCHUB
         BZ    *+12
         MVI   0(RE),C'A'
         LA    RE,1(,RE)
         TM    0(RF),X'02'                  MASCHINENCODE-VORSCHUB
         BZ    *+12
         MVI   0(RE),C'M'
         LA    RE,1(,RE)
         MVT   DB+30,'RECFM'                TEXT
         MVC   DB+42(5),WORK
         BAL   R9,B62110                    DRUCKEN
         SPACE 2
B72060   NULL
         TM    36(R3),X'C0'                 RECFM = U
         BO    B72070                       NICHT DRUCKEN
         LH    R2,82(,R3)                   LRECL DCB
         CVD   R2,WORK
         BAL   R9,B62001
         MVT   DB+30,'LRECL'                TEXT
         MVC   DB+42(5),0(R1)
         BAL   R9,B62110                    DRUCKEN
         EJECT
B72070   NULL
         LH    R2,62(,R3)                   BLKSIZE
         CVD   R2,WORK
         BAL   R9,B62001
         MVT   DB+30,'BLKSIZE'              TEXT
         MVC   DB+42(5),0(R1)
         BAL   R9,B62110                    DRUCKEN
         SPACE 2
B72080   NULL
         TM    JFCB+98,X'80'                DSORG=IS
         BO    B72083                        JA
         TM    JFCB+99,X'08'                VSAM DATEI..
         BZ    B72099                        NEIN
         SPACE 2
B72083   NULL
         LH    R2,60(,R3)                   RKP DCB
         CVD   R2,WORK
         BAL   R9,B62001
         MVT   DB+30,'RKP'                  TEXT
         MVC   DB+42(5),0(R1)
         BAL   R9,B62110                    DRUCKEN
         SPACE 2
B72086   NULL
         SR    R2,R2
         IC    R2,16(,R3)                   KEYLEN
         CVD   R2,WORK
         BAL   R9,B62001
         MVT   DB+30,'KEYLEN'               TEXT
         MVC   DB+42(5),0(R1)
         BAL   R9,B62110                    DRUCKEN
         SPACE 2
B72099   NULL
         MVC   DB,DB-1                      DB LOESCHEN
         L     R9,SAVER9
         BR    R9                           RETURN
         EJECT
B81001   NULL                               ENTRY ERROR ROUTINE
B81010   NULL
         LA    RF,010                       MSG-NUMMER
         BAL   R9,B64100
         BAL   R9,B61001
         B     B12001                       PARM-SCAN BEENDEN
         SPACE 3
B81500   NULL                               SYNAD QSAM
         SYNADAF ACSMETH=QSAM
B81510   ST    RE,SAVERE
         MVI   DB,C'-'
         LA    R1,68(,R1)                   MSG TEXT
         ST    R1,MSGPARM4
         LA    RF,060                       MSG-NUMMER
         BAL   R9,B64100
         BAL   R9,B61001                    DRUCKEN
         B     B81590
B81550   NULL                               SYNAD ISAM
         SYNADAF ACSMETH=QISAM
         B     B81510
B81590   NULL
         SYNADRLS
         L     RE,SAVERE
         B     B49020                       CLOSE AUSGABEN
         EJECT
**********************************************************************
**       SPIE-EXIT ROUTINE 0C7 IN SYSIN PROCEDURE                   **
**********************************************************************
         SPACE
SPIEEXIT NULL
         LR    R3,R1                       PIE ADRESSE SICHERN
         L     RE,12(,R3)                  REGISTER 14 ZURUECKLADEN
         L     RE,8(,R3)                   INTERRUPT ADRESSE
         LA    RE,0(,RE)                   HOB LOESCHEN
         SR    R0,R0                       LOESCHEN
         IC    R0,8(,R3)                   INSTRUCTION LENGTH
         SRL   R0,6                         ERMITTELN
         SLL   R0,1                        MAL 2
         SR    RE,R0                       INTERRUPT
         ST    RE,SAVE2
         L     R2,PROCADR                  ENTRY-ADR SYSIN-EXIT
         L     RF,SVSTART(,R2)             ADR SYSVARS
         LA    R4,$$OFFSET                 ADRESSE OFFSET TABELLE
SPIE0020 NULL
         LA    R1,0(,R2)                   ENTRY POINT
         LR    R0,R1                        SICHERN
         LA    R1,4095(,R1)                ENTRY POINT+LAENGE
         CL    R0,SAVE2
         BH    SPIE0040
         CL    R1,SAVE2
         BH    SPIE0060
SPIE0040 NULL
         L     R2,X'0FC4'(,R2)             ADRESSE NEUE PROC
         B     SPIE0020
SPIE0060 NULL
         SR    RE,R0                       OFFSET
         STH   RE,SAVE2+4                   NACH WORK
         LH    R1,X'0FC8'(,R2)             OFFSET IN STMT-TAB
         LH    R0,X'0FCA'(,R2)              ANZAHL STMTS
         LA    R1,0(R4,R1)                 ADR OFFSET TABELLE
SPIE0100 NULL
         CLC   0(2,R1),SAVE2+4             OFFSET VERGLEICHEN
         BH    SPIE0200                    GEFUNDEN
         LA    R1,2(,R1)                   NAECHSTER PLATZ
         BCT   R0,SPIE0100                 WEITER
SPIE0200 NULL
         SR    R1,R4                       PLATZ
         SRL   R1,1                         / 2
         CVD   R1,WORK                     DEZIMAL
         MVC   WORK+11(10),SBZBL           SCHABLONE
         ED    WORK+11(10),WORK+3          ERROR-STMT
         UNPK  WORK+21(5),SAVE2+4(3)       INTERRUPT RELATIV
         UNPK  WORK+25(7),SAVE2(5)         INTERRUPT DIREKT
         TR    WORK+21(10),TRTAB-240       HEXA UEBERSETZEN
         MVC   MSGPARM4(12),=A(WORK+11,WORK+21,WORK+25)
         LA    RF,070                      MSG-NUMMER
         BAL   R9,B64100                   MSG-EDIT
         MVI   DB,C'0'                     VORSCHUB
         BAL   R9,B61001                   DRUCKEN
         B     B49010                      ENDE
         LTORG
         EJECT
XDAPRNT  CSECT
B91001   NULL                               DRUCK-ROUTINE
         PROST RA                           INITIALISIERUNG
         L     R3,0(,R1)                    ADR DRUCKBEREICH
         L     R4,=A(PRINT)                 DCB-ADRESSE
         L     R5,=A(JOBINFO)
         USING DSINFO,R5
B91005   NOP   B91190                       WIRD BRANCH, WENN SYSIN
*                                            NICHT GEDRUCKT WERDEN SOLL
         CLI   0(R3),C'1'                   NEUE SEITE..
         BE    B91010
         CLI   0(R3),C'+'                   KEIN VORSCHUB..
         BE    B91140
         CP    ZZ,ZZMAX                     SEITE VOLL..
         BL    B91100                        NEIN
B91010   NULL
         MVI   UEB1,C'1'                    VORSCHUB
         MVC   UEB1+1(8),JOBNAME
         MVT   UEB1+31,'X D A T A'
         MVT   UEB1+62,'DATUM'
         MVC   UEB1+70(8),DATUM
         BAL   R9,B91090                    DRUCKEN
         MVC   UEB1+1(8),STEPNAME
         MVT   UEB1+24,'VERARBEITUNGS-PROTOKOLL'
         MVT   UEB1+62,'ZEIT'
         MVC   UEB1+70(8),ZEIT
         BAL   R9,B91090                    DRUCKEN
         MVC   UEB1+1(8),PROCNAME
         MVT   UEB1+62,'SEITE'
         AP    SZ,=P'1'                     SEITENZAEHLER + 1
         MVC   UEB1+74(4),=X'40202120'      MASKE
         ED    UEB1+74(4),SZ
         BAL   R9,B91090                    DRUCKEN
         MVI   UEB1+1,EQUAL
         MVC   UEB1+2(76),UEB1+1
         BAL   R9,B91095                    DRUCKEN
         SP    ZZ,ZZ                        ZZ LOESCHEN
         MVI   0(R3),C'-'                   VORSCHUB
         B     B91110
B91090   NULL                               UEBERSCHRIFTEN DRUCKEN
         MVI   UEB1+68,C':'
B91095   NULL
         PUT   (R4),UEB1
         MVC   UEB1,UEB1-1                  LOESCHEN
         BR    R9                           RETURN
         EJECT
B91100   NULL
         CLI   0(R3),C'-'                   3-ZEILIG
         BE    B91110
         CLI   0(R3),C'0'                   2-ZEILIG
         BE    B91120
         MVI   0(R3),BLANK                  EINZEILIG
         B     B91130
B91110   AP    ZZ,=P'1'                     ZZ + 1
B91120   AP    ZZ,=P'1'                     ZZ + 1
B91130   AP    ZZ,=P'1'                     ZZ + 1
B91140   PUT   (4),(3)                      DRUCKEN EINZELZEILE
B91190   NULL
         MVI   0(R3),BLANK                  LOESCHEN
         MVC   1(120,R3),0(R3)               DBER
         PEND                               RETURN
         SPACE 3
SZ       DC    PL2'0'                       SEITENZAEHLER
ZZ       DC    PL2'999'                     ZEILENZAEHLER
ZZMAX    DC    PL2'058'                     ANZAHL ZEILEN PRO SEITE
         DC    CL1' '                       LOESCHBYTE
UEB1     DC    CL121' '                     UEBERSCHRIFT
         LTORG
         EJECT
DSVARS   DSECT
         COPY  XDAVARS
         EJECT
DSINFO   DSECT
JOBNAME  DS    CL8
STEPNAME DS    CL8
PROCNAME DS    CL8
DATUM    DS    CL8
ZEIT     DS    CL8
         SPACE 2
XDAMAIN  CSECT
NOPRINT  NULL                               DUMMY ZUGRIFFS-ROUTINE
         SR    RF,RF                        LOESCHEN
         BR    RE                            RETURN
         SPACE 2
LISTADR  DC    A(0)                         XDALIST ENTRY-POINT
UPROADR  DC    A(0)                         ADR USER EXIT
MAXABEND DC    F'9999'                      ABEND, WENN RC >=  MAX
ZAESKIP  DC    PL5'+1'                      SKIP
ZAEDAT   DC    PL5'0'                       ZAEHLER FUER DATEI WECHSEL
*
PARMCODE DC    B'00110000'                  ZUSTAND AUS EXEC KARTE
*        BIT0  USER-EXIT UEBER PARM
*        BIT1  ABEND-ROUTINE
*        BIT2  SYSIN NICHT IM PROTOKOLL DRUCKEN
*        BIT3  DATEI-INFORMATIONEN NICHT DRUCKEN
*        BIT4  RESERVE
*        BIT5  RESERVE
*        BIT6  RESERVE
*        BIT7  KEYWORD-INDICATOR
*
SAETZE   DC    C'SAETZE'                    TEXT
AUSANZ   DC    PL2'0'                       ANZAHL AUSGABEN
SWAUS    DC    C' '                         SCHALTER AUSGABE
SBZBL    DC    X'40202020202020202120'
SBZAE    DC    XL12'402020204B2020204B202120'
P1       DC    PL1'1'                       KONSTANTE
JOBINFO  DC    5CL8' '                      JOB-INFORMATIONEN
         DC    C' '                         LOESCH-BYTE
DB       DC    CL121' '                     DRUCKBEREICH
ENDKON   DC    C'*** XDATA VERSION <4> ENDET MIT <11> <10> ***'
XDAVERS  DC    CL4'4.4'                     PROGRAMM-LEVEL
RCMSG    DC    CL11'RETURN-CODE'
UAMSG    DC    CL11'USER-ABEND '
         EJECT
***********************************************************************
***      D A T E I  -  S T E U E R B L O E C K E                    ***
***********************************************************************
         SPACE 2
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      X
               RECFM=FBA,LRECL=121,BLKSIZE=242
         SPACE
QSAMEIN  DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL,PM),BFTEK=A,           X
               EODAD=B49001,SYNAD=B81500,EXLST=QSELIST
         SPACE
ISAMEIN  DCB   DDNAME=SYSIS1,DSORG=IS,MACRF=(GL,SK,PU),                X
               EODAD=B49001,SYNAD=B81550,EXLST=ISELIST
         SPACE
QSAMAUS  DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,                        X
               SYNAD=B81500,EXLST=QSALIST
         SPACE
QSA2AUS  DCB   DDNAME=SYSUT3,DSORG=PS,MACRF=PM,                        X
               SYNAD=B81500,EXLST=QS2LIST
         SPACE
ISAMAUS  DCB   DDNAME=SYSIS2,DSORG=IS,MACRF=PM,                        X
               SYNAD=B81550,EXLST=ISALIST
         SPACE
U21AUS   DCB   DDNAME=SYSUT21,DSORG=PS,MACRF=PM,                       X
               SYNAD=B81500,EXLST=SEPLIST
         SPACE
U22AUS   DCB   DDNAME=SYSUT22,DSORG=PS,MACRF=PM,                       X
               SYNAD=B81500,EXLST=SEPLIST
         SPACE
U23AUS   DCB   DDNAME=SYSUT23,DSORG=PS,MACRF=PM,                       X
               SYNAD=B81500,EXLST=SEPLIST
         SPACE
U24AUS   DCB   DDNAME=SYSUT24,DSORG=PS,MACRF=PM,                       X
               SYNAD=B81500,EXLST=SEPLIST
         SPACE
U25AUS   DCB   DDNAME=SYSUT25,DSORG=PS,MACRF=PM,                       X
               SYNAD=B81500,EXLST=SEPLIST
         SPACE
U26AUS   DCB   DDNAME=SYSUT26,DSORG=PS,MACRF=PM,                       X
               SYNAD=B81500,EXLST=SEPLIST
         SPACE
U27AUS   DCB   DDNAME=SYSUT27,DSORG=PS,MACRF=PM,                       X
               SYNAD=B81500,EXLST=SEPLIST
         SPACE
U28AUS   DCB   DDNAME=SYSUT28,DSORG=PS,MACRF=PM,                       X
               SYNAD=B81500,EXLST=SEPLIST
         EJECT
***********************************************************************
***      STEUERBLOCK BESCHREIBUNGEN                                 ***
***********************************************************************
         SPACE
EIN      DEFSTB DUMMY,0
LNSTB    EQU   *-EINSTB                     LAENGE STEUERBLOCK
PRT      DEFSTB PRINT,0
QSE      DEFSTB SYSUT1,QSAMEIN
QSX      DEFSTB SYSUTX,QSAMEIN
ISE      DEFSTB SYSIS1,ISAMEIN
ISX      DEFSTB SYSISX,ISAMEIN
QSA      DEFSTB SYSUT2,QSAMAUS
QS2      DEFSTB DUMMY,0
ISA      DEFSTB SYSIS2,ISAMAUS
U21      DEFSTB SYSUT21,U21AUS
U22      DEFSTB SYSUT22,U22AUS
U23      DEFSTB SYSUT23,U23AUS
U24      DEFSTB SYSUT24,U24AUS
U25      DEFSTB SYSUT25,U25AUS
U26      DEFSTB SYSUT26,U26AUS
U27      DEFSTB SYSUT27,U27AUS
U28      DEFSTB SYSUT28,U28AUS
UT3      DEFSTB SYSUT3,QSA2AUS
DEL      DEFSTB SYSDEL,QSA2AUS
CHA      DEFSTB CHAR,0
HEX      DEFSTB HEXA,0
DUM      DEFSTB DUMP,0
DAT      DEFSTB DATA,0
DIT      DEFSTB DITTO,0
SYS      DEFSTB SYSIN,0
         SPACE 3
*        EXLST-ADRESSEN
EXITLIST DS    0F
QSELIST  DC    X'05',AL3(B63001)
         DC    X'87',AL3(JFCB)
ISELIST  DC    X'05',AL3(B63020)
         DC    X'87',AL3(JFCB)
QSALIST  NULL
QS2LIST  NULL
ISALIST  NULL
SEPLIST  NULL
         DC    X'05',AL3(B63200)            DCB-EXIT AUSGABE
         DC    X'87',AL3(JFCB)              READ JFCB EXIT
         EJECT
WORK     DC    4D'0'                        ARBEITSBEREICH
WRITER   DS    0F                           WRITE ADRESS BEREICH
         DC    X'10',AL3(U21STB),A(B21710)
         DC    X'20',AL3(U22STB),A(B21710)
         DC    X'30',AL3(U23STB),A(B21710)
         DC    X'40',AL3(U24STB),A(B21710)
         DC    X'50',AL3(U25STB),A(B21710)
         DC    X'60',AL3(U26STB),A(B21710)
         DC    X'70',AL3(U27STB),A(B21710)
         DC    X'80',AL3(U28STB),A(B21710)
         DC    X'00',AL3(QSASTB),A(B21300)
         DC    X'00',AL3(ISASTB),A(B21400)
         DC    X'00',AL3(PRTSTB),A(B21500)
NEWENTRY DC    F'0'                         RETURN ADR PROC EXIT
NEWSTART DC    F'0'                         ENTRY ADR PROC
SAVE2    DC    XL60'0'                      SAVEAREA 2
SATZADR  DC    F'0'                         RECORD POINTER
SAVEADR  DC    F'0'                         SAVE RECORD POINTER
EXITADR  DC    F'0'                         SAVE NACH INSERT PROC
PROCADR  DC    F'0'                         ENTRY POINT PROC
SAVER8   DC    F'0'                         SAVE LINK REGISTER
SAVER9   DC    F'0'                         SAVE LINK REGISTER
SAVERE   DC    F'0'                         SAVE RETURN REGISTER
DELADR   DC    A(B21001)                    RETURN NACH DELETE
TIOTADR  DC    F'0'
PRINTLN  DC    F'0'
XDATARC  DC    F'0'                         RETURNCODE XDATA
UPRORC   DC    F'0'                         RETURNCODE UPRO
DDNADR   DC    F'0'                         ADR STEUERBLOECKE
DDNANZ   DC    F'0'                         ANZAHL DD-NAMEN FUER B71001
JFCB     DC    CL176' '                     JOB-FILE-CONTROL-BLOCK
FW00     DC    F'00'
FW04     DC    F'04'
FW08     DC    F'08'
FW12     DC    F'12'
TRTAB    DC    C'0123456789ABCDEF'
BLANKS   DC    CL120' '                     BLANKS
EXITSAVE DC    F'0'                         ZWISCHENFELDER
INSEXIT  DC    F'0'
PROCSAVE DC    F'0'
INSPROC  DC    F'0'
CONCSAVE DC    F'0'
BRANCH   DC    X'47F0'                      BRANCH-CODE
NOOP     DC    X'4700'                      NOP-CODE
         EJECT
MSGEDIT  DC    V(XDAMEDT)                   MSG-EDITOR
XDAMSGS  DC    V(XDAMSGS)                   XDATA MESSAGES
MSGPARM  DS    0F                           PARAMETERLISTE XDAMSGS
MSGPARM1 DC    A(0)                         MSG-NUMMER
MSGPARM2 DC    A(DB+1)                      DBER
MSGPARM3 DC    A(MSGLEN)                    LAENGE DBER
MSGPARM4 DC    A(BLANKS)                    VARIABLE
MSGPARM5 DC    A(BLANKS)                     PARA-
MSGPARM6 DC    A(BLANKS)                      METER
MSGLEN   DC    A(120)                       LAENGE
*        ZAEHLER
ZAEEIN   DC    PL5'0'                       EINGABE
ZAEAUS   DC    PL5'0'                       AUSGABE
ZAEDEL   DC    PL5'0'                       DELETES
ZAEINS   DC    PL5'0'                       INSERTES
ZAEPRT   DC    PL5'0'                       PRINT
ZAEUPD   DC    PL5'0'                       UPDATES
ZAEQSA   DC    PL5'0'                       QSAM AUS
ZAEISA   DC    PL5'0'                       ISAM AUS
ZAEU21   DC    PL8'0'                       ZAEHLER SEPARATE FILES
ZAEU22   DC    PL8'0'
ZAEU23   DC    PL8'0'
ZAEU24   DC    PL8'0'
ZAEU25   DC    PL8'0'
ZAEU26   DC    PL8'0'
ZAEU27   DC    PL8'0'
ZAEU28   DC    PL8'0'
MODAREA  DS    0C                           PARAMETER-NAME
ZAEDROP  DC    PL5'-1'                      DROP
INDSKIP  DC    PL5'-1'                      SKIP INDEX
ZAECOPY  DC    PL5'-1'                      COPY
ZAELIST  DC    PL5'-1'                      ANZAHL DRUCKSAETZE
UPRONAME DC    CL8' '                       NAME USER EXIT
DATALEN  DC    XL2'0001'                    DATA-FELD LAENGE
DATAFLD  DC    CL120' '                     DATA-FELD AUS PARM
         ORG   DATALEN
ISAMKEY  DS    XL256                        KEY FUER OFFSET ISAM
MODHDR1  DS    0CL133                       TITEL 1
         DC    CL19'1'
MODT1    DC    CL114' '
MODHDR2  DS    0CL133                       TITEL 2
         DC    CL19' '
MODT2    DC    CL114' '
         EJECT
***      EQUAL-WERTE
         SPACE
SVSTART  EQU   X'0FB8'                     OFFSET SYSVAR ADRESSE
SWEXIT   EQU   X'80'
SWABEND  EQU   X'40'
SWSYSIN  EQU   X'20'
SWINFO   EQU   X'10'
KOMMA    EQU   C','
EQUAL    EQU   C'='
BLANK    EQU   C' '
MINUS    EQU   C'-'
NULL     EQU   X'00'
*
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT2     EQU   X'20'
BIT3     EQU   X'10'
BIT4     EQU   X'08'
BIT5     EQU   X'04'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
         SPACE 2
*        STATUS-SCHALTER BITS IM DD-STEUERBLOCK
DDSTMT   EQU   B'10000000'              DD-STATEMENT VORHANDEN
OPENED   EQU   B'01000000'              DATASET IST OPEN
*        EQU   B'00100000'              ** RESERVE
*        EQU   B'00010000'              ** RESERVE
*        EQU   B'00001000'              ** RESERVE
*        EQU   B'00000100'              ** RESERVE
*        EQU   B'00000010'              ** RESERVE
*        EQU   B'00000001'              ** RESERVE
         LTORG
         SPACE 3
         END   XDAMAIN
