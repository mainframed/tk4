 TITLE 'OPERCANC - SAMPLE COMMUNICATIONS TASK USER EXIT FOR MESSAGE    X
               IEF099I,TESTED DOENCH'
***********************************************************************
*                                                                     *
* MODULE NAME         =  OPERCANC                                     *
*                                                                     *
* DESCRIPTIVE NAME    =  SAMPLE COMMUNICATIONS TASK USER EXIT         *
*                        FOR MESSAGE IEF099I.                         *
*                                                                     *
* FUNCTION            =  ISSUES A MESSAGE TO THE OPERATOR REQUESTING  *
*                        THAT APPLICATION JOBS BE CANCELLED           *
*                        RATHER THAN WAIT FOR THE DATASETS THEY       *
*                        NEED.                                        *
*                                                                     *
*   OPERATION         =  JOBNAMES FOR APPLICATION TEST WORK BEGIN     *
*                        WITH THE PREFIX 'PR4'.  IF THE FIRST THREE   *
*                        CHARACTERS OF THE JOBNAME BEGIN WITH         *
*                        THIS PREFIX, THE TEXT OF MESSAGE IEF099I     *
*                         'IEF099I JOB JJJ WAITING FOR DATASETS'      *
*                        WILL BE CHANGED TO SAY:                      *
*                         'IEF099I JOB JJJ WAITING - ISSUE CANCEL'    *
*                        THE MESSAGE WILL BE QUEUED VIA DESCRIPTOR    *
*                        CODE 2. THE DESCRIPTOR CODE WAS PREVIOUSLY   *
*                        6.                                           *
*                                                                     *
* ENTRY POINT         =  OPERCANC                                     *
*                                                                     *
*   PURPOSE           =  TO DETERMINE IF A JOB IS APPLICATION TEST    *
*                        WORK, AND IF IT IS, CHANGE THE TEXT OF       *
*                        MESSAGE IEF099I TO INFORM THE OPERATOR TO    *
*                        CANCEL THE JOB.                              *
*                                                                     *
*   LINKAGE           =  BALR                                         *
*                                                                     *
*   INPUT DATA        =  REG 1 POINTS TO THE ADDRESS OF THE CTXT      *
*                        REG 13 ADDRESS OF A STANDARD SAVE AREA       *
*                        REG15 ENTRY POINT                            *
*                                                                     *
*                                                                     *
*   REGISTERS SAVED   =  REG0 - REG15                                 *
*                                                                     *
*   REGISTER USAGE    =  REG5  - POINTER TO THE ADDRESS OF THE CTXT   *
*                        REG12 - MODULE BASE REGISTER                 *
*                        REG13 - POINTER TO A STANDARD SAVE AREA      *
*                        REG14 - RETURN POINT                         *
*                                                                     *
*   REGISTERS RESTORED = REG0 - REG15                                 *
*                                                                     *
*   CONTROL BLOCKS    =                                               *
*     NAME     MAPPING MACRO   REASON USED                  USAGE     *
*     ----     -------------   -----------                 -------    *
*                                                                     *
*     CTXT      IEZVX100        USER EXIT PARAMETER LIST     R,W      *
*                                                                     *
*   KEY = R-READ, W-WRITE, C-CREATE, D-DELETE                         *
*                                                                     *
*   TABLES            =  NONE                                         *
*                                                                     *
*   MACROS            =  NONE                                         *
*                                                                     *
***********************************************************************
OPERCANC CSECT
OPERCANC AMODE 31                      31-BIT ADDRESSING MODE
OPERCANC RMODE ANY                     31-BIT RESIDENCE
         SPACE 1
***********************************************************************
*                                                                     *
*         REGISTER ASSIGNMENTS                                        *
*                                                                     *
***********************************************************************
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
CTXTPTR  EQU   5
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
MSGLNTH  EQU   L'MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*        STANDARD ENTRY LINKAGE                                       *
*                                                                     *
***********************************************************************
         STM   REG14,REG12,12(REG13)    SAVE CALLER'S REGISTERS
         BALR  REG12,REG0               ESTABLISH MODULE BASE
         USING *,REG12                  REGISTER
         L     CTXTPTR,0(REG1)          ESTABLISH ADDRESSABILITY
         USING CTXT,CTXTPTR             TO THE CTXT
***********************************************************************
*                                                                     *
*        DETERMINE IF THE JOB BEING PROCESSED IS APPLICATION TEST     *
*        WORK.  IF IT IS CHANGE THE TEXT AND THE DESCRIPTOR CODE.     *
*                                                                     *
***********************************************************************
         L     REG2,CTXTTXPJ            ESTABLISH ADDRESSABILITY
         USING CTXTATTR,REG2            TO THE MSG ATTRIBUTES
         LA    REG4,CTXTTMSG            GET ADDRESS OF TEXT AREA
         USING MSGTEXT,REG4             BASE TEXT MAPPING
         CLC   JOBPREFX,JOBCLASS        APPLICATION TEST WORK?
         BNE   FINISHED                 NO, THEN EXIT LINKAGE
         L     REG3,CTXTDCP             ESTABLISH ADDRESSABILITY
         USING CTXTDESC,REG3            TO THE DESCRIPTOR CODES
         OI    CTXTRFB1,CTXTRCMT        REQUEST TO CHANGE THE          X
                                        MESSAGE TEXT
         MVC   NEWTEXT,TEXTCHNG         MOVE IN THE NEW TEXT
         MVI   CTXTTLEN+1,MSGLNTH       INSERT LENGTH OF MESSAGE
         OI    CTXTRFB1,CTXTRCDC        REQUEST TO CHANGE THE          X
                                        DESCRIPTOR CODE
         XC    CTXTDESC,CTXTDESC        CLEAR OUT THE DESCRIPTOR CODES
         OI    CTXTDC1,CTXTDC02         CHANGE TO DESCRIPTOR CODE 2
         DROP  REG2,REG3,REG4           DROP ALL USINGS
***********************************************************************
*                                                                     *
*        STANDARD EXIT LINKAGE                                        *
*                                                                     *
***********************************************************************
FINISHED EQU   *
         LM    REG14,REG12,12(REG13)    RESTORE CALLER'S               X
                                        REGISTERS
         BR    REG14                    RETURN TO CALLER
         EJECT
JOBCLASS DC    CL3'PR4'                 JOB CLASSIFICATION
TEXTCHNG DC    CL14'- ISSUE CANCEL'     TEXT TO BE INSERTED IN MESSAGE
         SPACE 1
***********************************************************************
*                                                                     *
*         MAPPING OF THE MESSAGE TEXT                                 *
*                                                                     *
***********************************************************************
MSGTEXT  DSECT
MESSAGE  DS    0CL43                    LENGTH OF ENTIRE MESSAGE
MSGID    DS    CL8                      MESSAGE ID
         DS    CL4
JOBPREFX DS    CL3                      JOB CLASSIFICATION
         DS    CL14
NEWTEXT  DS    CL14                     TEXT AREA TO BE REPLACED
         EJECT
         IEZVX100
         EJECT
         END   OPERCANC
