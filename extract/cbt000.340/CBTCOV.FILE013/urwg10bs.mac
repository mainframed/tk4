         TITLE 'XVIRT                             X-COMMAND VIRT'
VIRT     CSECT
         SPACE 3
***********************************************************************
*                                                                     *
*      PROGRAM-ID. VIRT.                                              *
*      AUTHOR. PGM K1VIRT :           HR. KNOEFERL, ALLIANZ MUENCHEN. *
*              ERWEITERT ALS X-COMM.: FR. LAMPARTER, RWG.             *
*      INSTALLATION. RWG, STUTTGART.                                  *
*      DATE-WRITTEN. 18. 06. 1982.                                    *
*      REMARKS.                                                       *
*                                                                     *
*          DAS X-COMMAND 'VIRT' GIBT INFORMATIONEN UEBER DEN          *
*          AUFBAU DES VIRTUELLEN SPEICHERS AUS.                       *
*                                                                     *
*          FEHLENDE ADDRESSEN SIND MANUELL AUS AMBLIST UND            *
*          DUMP ZU ERMITTELN.                                         *
*                                                                     *
***********************************************************************
         EJECT
*
         XSAVE R12,,VIRT,WORKL
*
         USING WORK,R13
         USING PARMAREA,R11
         LR    R11,R1
         MVC   WTO01,MESS               NACHRICHT IN ARBEITSBEREICH
         LA    R14,TABANF               TABELLE IN ARBEITSBEREICH
         LA    R0,TABK
         LA    R15,TABEND-TABANF
         LR    R1,R15
         MVCL  R14,R0
*
*
* HEADLINE
*
         L     R3,16                    A(CVT)
         L     R3,196(R3)               A(SMCA)
         MVC   HEAD+35(4),16(R3)        SID
         OSDATUM
         MVC   HEAD+23(8),0(R1)         DATUM
*
* VIRTUAL STORAGE AUS CVT
*
         L     R3,16                    LOAD CVT
* NUCLEUS BOUNDARY
         L     R6,X'80'(R3)
         BAL   R10,HEX
         MVC   CVT80+43(6),VIRT1+1
         BAL   R10,CONV
         MVC   CVT80(6),VIRT1+1
* V=V BOUNDARY
         L     R6,X'11C'(R3)
         BAL   R10,HEX
         MVC   CVT11C+43(6),VIRT1+1
         BAL   R10,CONV
         MVC   CVT11C(6),VIRT1+1
         MVC   CVT11C1+26(6),VIRT1+1
* CSA BOUNDARY
         L     R6,X'1A0'(R3)
         BAL   R10,HEX
         MVC   CVT1A0+43(6),VIRT1+1
         BAL   R10,CONV
         MVC   CVT1A0(6),VIRT1+1
         MVC   CVT1A01+26(6),VIRT1+1
* LPA BOUNDARY
         SR    R6,R6
         ICM   R6,7,X'169'(R3)
         BAL   R10,HEX
         MVC   CVT169+43(6),VIRT1+1
         BAL   R10,CONV
         MVC   CVT169(6),VIRT1+1
* VIRT BOUNDARY
         L     R6,X'A4'(R3)
         BAL   R10,HEX
         MVC   CVTA4+43(6),VIRT1+1
         BAL   R10,CONV
         MVC   CVTA4(6),VIRT1+1
* USER REG (CALCULATE)
         L     R6,X'1A0'(R3)
         L     R7,X'11C'(R3)
         SR    R6,R7
         BAL   R10,CONV
         MVC   UREG+26(6),VIRT1+1
*
* DISPLAY VIRTUAL STORAGE VIA WTO
*
         LA    R6,(TABEND-TABANF)/50
         LA    R7,TABANF
*
AA10     MVC   WTO01+14(50),0(R7)
         BAL   R10,WTO
*
         LA    R7,50(R7)
         BCT   R6,AA10
*
*
         XRETURN ,R
*
WTO      EQU   *
         L     R0,REG4
         WTO   MF=(E,WTO01)
         BR    R10
*
CONV     XC    DW,DW
         LA    R8,1024
         SRDA  R6,32
         DR    R6,R8
         CVD   R7,DW
         MVC   VIRT1(7),=X'4020204B202120'
         ED    VIRT1(7),DW+5
         BR    R10
*
HEX      ST    R6,DW
         UNPK  DW(9),DW(5)
         TR    DW,CCTAB-240
         MVC   VIRT1+1(6),DW+2
         BR    R10
*
         REG
*
CCTAB    DC    CL16'0123456789ABCDEF'
*
TABK     EQU   *
         DC CL50'VIRTUAL STORAGE LAYOUT XX.XX.XX    SSSS           '
         DC CL50'                                                  '
         DC CL50'00.000 K --> ×-----------------------× <-- 000000 '
         DC CL50'             ×SYSTEM QUEUE AREA      ×            '
         DC CL50'             ×                       ×            '
         DC CL50'...... K --> ×-----------------------× <-- ...... '
         DC CL50'             ×PAGEABLE LINK PACK AREA×            '
         DC CL50'             ×                       ×            '
         DC CL50'00.000 K --> ×-----------------------× <-- 000000 '
         DC CL50'             ×MODIFIED LPA           ×            '
         DC CL50'             ×-----------------------×            '
         DC CL50'             ×PAGEABLE BLDL TABLE    ×            '
         DC CL50'             ×-----------------------×            '
         DC CL50'             ×PSA                    ×            '
         DC CL50'             ×-----------------------×            '
         DC CL50'             ×COMMON SERVICE AREA    ×            '
         DC CL50'             ×                       ×            '
         DC CL50'             ×                       ×            '
         DC CL50'00.000 K --> ×=======================× <-- 000000 '
         DC CL50'             ×LSQA U. SWA            ×            '
         DC CL50'             ×-----------------------×            '
         DC CL50'             ×V=V REGION  000000 K   ×            '
         DC CL50'             ×         -  000000 K   ×            '
         DC CL50'             ×                       ×            '
         DC CL50'             ×         =  000000 K   ×            '
         DC CL50'             ×                       ×            '
         DC CL50'00.000 K --> ×-----------------------× <-- 000000 '
         DC CL50'             ×V= R REGION            ×            '
         DC CL50'             ×-----------------------×            '
         DC CL50'             ×SYSTEM REGION          ×            '
         DC CL50'00.000 K --> ×=======================× <-- 000000 '
         DC CL50'             ×RMS NUC. EXTENSION     ×            '
         DC CL50'             ×-----------------------×            '
         DC CL50'             ×FIXED LPA              ×            '
         DC CL50'             ×-----------------------×            '
         DC CL50'             ×FIXED BLDL             ×            '
         DC CL50'...... K --> ×-----------------------× <-- ...... '
         DC CL50'             ×NUCLEUS LOAD MODULE    ×            '
         DC CL50'             ×                       ×            '
         DC CL50'     0 K --> ×-----------------------× <-- 000000 '
         DC CL50'                                                  '
         DC CL50'SIEHE DEBUGGING HANDBOOK VOLUME 1 S. 5.73         '
TABKE    EQU   *
*
MESS     WTO   'XVIR001   012345678901234567890123456789012345678901234*
               56789',MCSFLAG=REG0,MF=L
MESSL    EQU   *-MESS
*
         XPARM
*
WORK     DSECT
SAVE     DS    18F
VIRT1    DS    CL7
WTO01    DS    CL80
DW       DS    D
         DS    C
*
TABANF   EQU   *
HEAD     DS    CL50
         DS    CL50
CVTA4    DS    CL50
         DS    CL250
CVT169   DS    CL50
         DS    CL450
CVT1A0   DS    CL50
         DS    CL100
CVT1A01  DS    CL50
CVT11C1  DS    CL50
         DS    CL50
UREG     DS    CL50
         DS    CL50
CVT11C   DS    CL50
         DS    CL150
CVT80    DS    CL50
         DS    CL550
TABEND   EQU   *
WORKL    EQU   *-WORK
*
         END
