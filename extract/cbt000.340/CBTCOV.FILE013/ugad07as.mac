LISTVTOC TITLE 'LISTVTOC WISSING LIST VTOC INFORMATION PER DATASET'
***********
*
****  AUTHOR: J. WISSING   G A D - MUENSTER TEL: 0251/7061
*
*        MODULE: LISTVTOC
*        ALIAS:  LV
*
*        TSO-COMMAND: LISTVTOC DA('DATASETNAME') OR VOL(VOLSER)
*              AND (OPTIONAL) SYSOUT('CLASS')
*
*        ENTRY:
*              ENTRY PARAMETERLIST IS COVERED BY IKJCPPL DSECT
*
*        EXIT: AS ON ENTRY
*
*        OUTPUT: VTOC-INFORMATION IS DISPLAYED ON TSO-TERMINAL
*
*        FUNCTION: LISTVTOC DISPLAYS VTOC-INFORMATION ON TSO-TERMINAL
*                  SUCH AS ALLOCATION VALUES AND USED SIZE
*
*
**********
         SPACE 5
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
LISTVTOC CSECT
         SAVE  (14,12),,*               SAVE CALLERS REGISTERS
         LR    R12,R15                  LOAD ENTRY-POINT ADDRESS
         USING LISTVTOC,R12,R11         ESTABLISH ADDRESSABILITY
         LA    R11,2048(,R12)           LOAD BASE ADDRES
         LA    R11,2048(,R11)           LOAD BASE ADDRES
         LR    R10,R1                   ADDRESS OF CPPL
         USING CPPL,R10                 ESTABLISH ADDRESSABILITY
         LA    R3,LDYNSTOR              LENGTH OF DYNAMIC STORAGE
         GETMAIN R,LV=(3)               OBTAIN STORAGE
         LR    R2,R1                    RELOAD STORAGE ADDRESS
         XR    R5,R5                    CLEAR CHARACTER
         MVCL  R2,R4                    CLEAR STORAGE
         ST    R1,8(,R13)               CHAIN OWN SAVE-AREA
         ST    R13,4(,R1)               CHAIN LOWER SAVE-AREA
         LR    R13,R1                   LOAD SAVE-AREA ADDRESS
         USING DYNSTOR,R13              ESTABLISH ADDRESSABILITY
         LA    R9,DPPL                  ADDRESS OF PPL-STORAGE
         USING PPL,R9                   ESTABLISH PPL ADDRESSABILITY
         MVC   PPLUPT,CPPLUPT           FILL PPL LIST
         MVC   PPLECT,CPPLECT
         LA    R2,DECB                  ADDRESS OF ECB
         ST    R2,PPLECB                STORE ADDRESS
         L     R2,CPCL                  ADDRESS OF PCL
         ST    R2,PPLPCL                SAVE IN PARM-LIST
         LA    R2,DANS                  ADDRESS OF ANSWER-PLACE
         ST    R2,PPLANS                SAVE IN PARM-LIST
         MVC   PPLCBUF,CPPLCBUF         COMMAND-BUFFER-ADDRESS
         LA    R1,DPPL                  ADDRESS OF PARM-LIST
         LINK  EP=IKJPARS               LINK TO PARS-ROUTINE
         LTR   R15,R15                  TEST RETURN CODE FROM PARS
         BZ    MA0005                   PROCEED
         ST    R15,DRETC                SAVE RETURN-CODE
         MVC   DMSGID,C1100             INDICATE PARS ERROR
         BAL   R14,ER0000               ISSUE ERROR MESSAGE
         B     MA9000                   RETURN
         EJECT
         DROP  R9                       FREE PPL BASE
         SPACE 3
MA0005   DS    0H
         L     R9,DANS                  LOAD PDE ADDRESS
         USING IKJPARMD,R9              ESTABLISH ADDRESSABILITY
         TM    IDENT3+6,X'80'           SYSOUT KEYWORD ENTERED
         BZ    MA0010                   NO
         BAL   R14,AOS000               ALLOCATE PRINT DATASET
MA0010   DS    0H
         TM    POSIT1+6,X'80'           DATASET NAME PRESENT
         BZ    MA0500                   MUST BE VOLUME
         L     R2,POSIT1                LOAD DSNAME ADDRESS
         LH    R3,POSIT1+4              LOAD DSNAME LENGTH
         BCTR  R3,0                     DECREMENT FOR EXECUTE
         MVI   DCAMRET,X'40'            FORCE BLANKS TO DSNAME
         MVC   DCAMRET+1(43),DCAMRET
         MVI   LVDSN,X'40'              FORCE BLANK TO DSNAME
         MVC   LVDSN+1(L'LVDSN-1),LVDSN
         EX    R3,MA0020                EXECUTE MVC
         EX    R3,MA0025                EXECUTE MVC
         B     MA0030                   SKIP EXECUTED INSTRUCTION
         SPACE 3
MA0020   DS    0H
         MVC   DCAMRET(0),0(R2)         ***  EXECUTED ONLY   ***
         SPACE 3
MA0025   DS    0H
         MVC   LVDSN(0),0(R2)           ***   EXECUTED ONLY   ***
         SPACE 3
MA0030   DS    0H
         TM    POSIT2+6,X'80'           VOLUME SERIAL ALSO
         BO    MA0070                   YES
         BAL   R14,LC0000               LOCATE DATASET
         LTR   R15,R15                  TEST RETURN CODE
         BZ    MA0040                   O.K.
         MVC   DMSGID,C1102             DATASET NOT IN CATALOG
         BAL   R14,ER0000               DISPLAY MESSAGE
         B     MA9000                   RETURN
         SPACE 3
MA0040   DS    0H
         MVC   LVVOL,DCAMRET+6          SAVE VOLUME
         B     MA0090                   PROCESS
         SPACE 3
MA0070   DS    0H
         MVI   LVVOL,X'40'              FORCE BLANKS TO VOLUME
         MVC   LVVOL+1(L'LVVOL-1),LVVOL
         L     R2,POSIT2                LOAD VOLSER ADDRESS
         LH    R3,POSIT2+4              LOAD VOLSER-LENGTH
         BCTR  R3,0                     DECREMENT FOR EXECUTE
         EX    R3,MA0080                EXECUTE MVC
         B     MA0090                   SKIP EXECUTED INSTRUCTION
         SPACE 3
MA0080   DS    0H
         MVC   LVVOL(0),0(R2)           ***   EXECUTED ONLY   ***
         SPACE 3
MA0090   DS    0H
         BAL   R14,FU0000               FIND VOLUME
         LTR   R15,R15                  VOLUME FOUND
         BZ    MA0100                   YES
         MVC   DMSGID,C1101             DATASET NOT ON DIRECT ACCESS
         BAL   R14,ER0000               DISPLAY MESSAGE
         B     MA9000                   RETURN
         SPACE 3
MA0100   DS    0H
         BAL   R14,OF1000               OBTAIN FORMAT 1 DSCB
         LTR   R15,R15                  TEST ON ERRORS
         BZ    MA0110                   PROCEED
         ST    R15,DRETC                SAVE RETURN CODE
         MVC   DMSGID,C1104             OBTAIN ERROR
         BAL   R14,ER0000               DISPLAY MESSAGE
         B     MA9000                   RETURN
         SPACE 3
MA0110   DS    0H
         BAL   R14,PD1000               PROCESS FORMAT 1 DSCB
         BAL   R14,DS1000               DISPLAY FORMAT 1 DSCB
         LTR   R15,R15                  TEST RETURN CODE
         BZ    MA9000                   RETURN
         ST    R15,DRETC                SAVE RETURN CODE
         MVC   DMSGID,C1106             PUTLINE ERROR
         BAL   R14,ER0000               DISPLAY MESSAGE
         B     MA9000                   RETURN
         EJECT
MA0500   DS    0H
         TM    POSIT2+6,X'80'           VOLSER PRESENT
         BZ    MA9000                   RETURN
         LA    R2,AEX000                ADDRESS OF ATTENTION EXIT
         LA    R1,WSTAXL                ADDRESS OF STAX PARMLIST
         STAX  (2),USADDR=(13),MF=(E,(1))
         OI    DSW,VOL                  INDICATE VOLUME PROCESSING
         L     R2,POSIT2                LOAD VOLSER ADDRESS
         LH    R3,POSIT2+4              LOAD LENGTH
         BCTR  R3,0                     DECREMENT FOR EXECUTE
         MVI   LVVOL,X'40'              FORCE BLANKS TO VOLSER
         MVC   LVVOL+1(L'LVVOL-1),LVVOL
         EX    R3,MA0510                EXECUTE MVC
         B     MA0520                   SKIP EXECUTED INSTRUCTION
         SPACE 3
MA0510   DS    0H
         MVC   LVVOL(0),0(R2)           ***   EXECUTED ONLY   ***
         SPACE 3
MA0520   DS    0H
         BAL   R14,FU0000               FIND VOLUME
         LTR   R15,R15                  TEST RETURN CODE
         BZ    MA0530                   PROCEED
         MVC   DMSGID,C1101             VOLUME NOT MOUNTED
         BAL   R14,ER0000               DISPLAY MESSAGE
         B     MA9000                   RETURN
         SPACE 3
MA0530   DS    0H
         BAL   R14,AOD000               ALLOC AND OPEN DIRECTORY
         LTR   R15,R15                  TEST RETURN CODE
         BZ    MA0540                   PROCEED
         MVC   DMSGID,C1108             ALLOC ERROR
         BAL   R14,ER0000               DISPLAY MESSAGE
         B     MA9000                   RETURN
         SPACE 3
MA0540   DS    0H
         BAL   R14,DS2000               DISPLAY VTOC HEADER
         SPACE 3
MA0550   DS    0H
         TM    DSW,ATTN                 ATTENTION ISSUED
         BO    MA0620                   TERMINATE
         READ  DDECB,SF,WVTOCIN,DCAMRET,MF=E
         CHECK DDECB
         SPACE 3
         CLI   DCAMRET+44,X'00'         FORMAT 0 DSCB
         BE    MA0550                   YES SKIP
         CLI   DCAMRET+44,C'4'          FORMAT 4 DSCB
         BNE   MA0555                   NO
         BAL   R14,PD4000               PROCESS FORMAT 4 DSCB
         L     R8,LV4DSCBN              LOAD DSCB-COUNT NOT ZERO
         BCT   R8,MA0550                READ NEXT
         B     MA0610                   SKIP REMAINING RECORD IN VTOC
         SPACE 3
MA0555   DS    0H
         CLI   DCAMRET+44,C'1'          FORMAT 1 DSCB
         BE    MA0560                   PROCESS
         CLI   DCAMRET+44,C'5'          FORMAT 1 DSCB
         BE    MA0600                   PROCESS
         BCT   R8,MA0550                READ ALL VTOC
         B     MA0610                   SKIP REMAINING RECORDS OF VTOC
         SPACE 3
MA0560   DS    0H
         BAL   R14,PD1000               PROCESS FORMAT 1 DSCB
         BAL   R14,DS3000               DISPLAY LINE
         BCT   R8,MA0550                READ NEXT RECORD
         B     MA0610                   SKIP REMAINING VTOC RECORDS
         SPACE 3
MA0600   DS    0H
         BAL   R14,PD5000               PROCESS FORMAT 5 DSCB
         BCT   R8,MA0550                READ NEXT VTOC RECORD
         SPACE 3
MA0610   DS    0H
         BAL   R14,DS4000               DISPLAY FREE SPACE INFO
         SPACE 3
MA0620   DS    0H
         BAL   R14,CFD000               CLOSE AND FREE DIRECTORY
         EJECT
MA9000   DS    0H
         TM    DSW,SYSOALC              SYSOUT DS ALLOCATED
         BZ    MA9010                   NO
         BAL   R14,CFS000               CLOSE AND FREE DS
         STAX  ,                        CANCEL ATTENTION EXIT
         SPACE 3
MA9010   DS    0H
         LR    R1,R13                   ADDRESS OF STORAGE
         L     R13,4(,R13)              LOAD HSA ADDRESS
         LA    R3,LDYNSTOR              LENGTH OF STORAGE
         FREEMAIN R,LV=(3),A=(1)
         LM    R14,R12,12(R13)          RELOAD REGISTERS
         XR    R15,R15                  RETURN CODE 0
         BR    R14                      RETURN TO CALLER
         TITLE 'AEX000 ATTENTION HANDLING ROUTINE'
AEX000   DS    0H
         STM   R14,R12,12(R13)          SAVE CALLERS REGISTERS
         LR    R10,R13                  SAVE SAVE-AREA ADDRESS
         L     R13,8(,R1)               LOAD ADDRESS OF STORAGE
         OI    DSW,ATTN                 INDICATE ATTENTION ISSUED
         LR    R13,R10                  RELOAD SAVE AREA ADDRESS
         LM    R14,R12,12(R13)          RESTORE REGISTERS
         BR    R14                      RETURN
         TITLE 'AOD000 ALLOCATE AND OPEN DIRECTORY'
AOD000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         SPACE 3
AOD010   DS    0H
         LA    R1,6                     LENGTH OF VOLSER
         STH   R1,WVOL                  STORE LENGTH
         MVC   WVOL+2(6),LVVOL          VOLUME SERIAL NUMBER
         LA    R1,6                     LENGTH OF UNIT
         STH   R1,WUNIT                 STORE LENGTH
         MVC   WUNIT+2(6),LVUNIT        MOVE UNIT-TYPE
         DALLO ERROR=WERROR,                                           *
               FILE='VTOCIN',                                          *
               DISP='SHR',                                             *
               UNIT=WUNIT,                                             *
               VOLUME=WVOL
         LTR   R15,R15                  TEST RETURN CODE
         BNZ   AOD020                   ERROR DURING ALLOCATION
         CLC   WERROR,=F'0'             ERROR FIELD MUST BE ZERO
         BE    AOD030                   NO ERROR
         SPACE 3
AOD020   DS    0H
         LA    R15,4                    INDICATE ALLOCATION ERROR
         B     AOD900                   RETURN
         SPACE 3
AOD030   DS    0H
         MVI   JFCB,X'04'               PREPARE USER JFCB
         MVC   JFCB+1(43),JFCB
         MVI   JFCB+44,X'40'
         MVI   JFCB+52,X'08'
         MVI   JFCB+87,X'08'
         MVI   JFCB+117,X'01'
         MVC   JFCB+118(6),LVVOL
         MVC   WVTOCIN(LVTOCIN),VTOCIN  BUILD DCB IN ORK AREA
         LA    R1,WVTOCIN               ADDRESS OF DCB
         USING IHADCB,R1                ESTABLISH ADDRESSABILITY
         LA    R2,JFCB                  ADDRESS OF JFCB
         LA    R3,X'87'                 TYPE OF EXLIST INDICATOR
         SLL   R3,24                    SHIFT TO HIGH ORDER BYTE
         OR    R2,R3                    OR WITH ADDRESS
         ST    R2,WEXLIST               STORE ADDRESS
         LA    R2,WEXLIST               LOAD ADDRESS
         L     R3,DCBEXLST              LOAD RECORD FORMAT
         OR    R2,R3                    OR IN WITH ADDRESS
         ST    R2,DCBEXLST              STORE BACK
         LR    R2,R1                    SAVE DCB-ADDRESS
         MVC   WOPEN(LOPEN),COPEN       MOVE OPEN TO WORK
         OPEN  ((2)),TYPE=J,MF=(E,WOPEN)
         OI    DSW,VTOCALC              INDICATE VTOC ALLOCATED
         SPACE 3
AOD900   DS    0H
         L     R14,DSAVE2               RELOAD RETURN ADDRESS
         LM    R0,R12,DSAVE2+8          RELOAD CALLERS REGISTERS
         BR    R14                      RETURN
         DROP  R1
         TITLE 'AOS000 ALLOCATE AND OPEN PRINT DATASET'
AOS000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         L     R9,DANS                  ADDRESS OF PARS ANSWER
         USING IKJPARMD,R9              ESTABLISH ADDRESSABILITY
         SPACE 3
AOS010   DS    0H
         L     R3,IDENT3                SYSOUT CLASS ADDRESS
         CLI   0(R3),C'X'               CLASS X ALLOCATION WITH HOLD
         BNE   AOS020                   NO
         DALLO FILE=(R,WFILE),SYSOUT=0(R3),ERROR=WERROR,HOLD='YES'
         B     AOS030                   CHECK ALLOCATION
         SPACE 3
AOS020   DS    0H
         DALLO FILE=(R,WFILE),SYSOUT=0(R3),ERROR=WERROR
         SPACE 3
AOS030   DS    0H
         LTR   R15,R15                  TEST RETURN CODE
         BNZ   AOS900                   RETURN TO CALLER
         L     R15,WERROR               LOAD ERROR CODE
         LTR   R15,R15                  TEST ERROR CED
         BNZ   AOS900                   RETURN TO CALLER
         MVI   WOPEN,X'80'              END OF PARM
         MVC   WSYSOUT(LSYSOUT),SYSOUT  BUILD DCB IN WORK AREA
         LA    R2,WSYSOUT               ADDRESS OF DCB
         USING IHADCB,R2                ESTABLISH ADDRESSABILITY
         MVC   DCBDDNAM(8),WFILE+2      MOVE DDNAME TO DCB
         OPEN  ((2),OUTPUT),MF=(E,WOPEN)
         TM    DCBOFLGS,DCBOFOPN        OPEN SUCCESSFUL
         BZ    AOS900                   RETURN TO CALLER
         OI    DSW,SYSOALC              INDICATE SYSOUT ALLOCATED
         SPACE 3
AOS900   DS    0H
         LM    R14,R12,DSAVE2           RELOAD CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         SPACE 3
         DROP  R9,R2
         TITLE 'CFD000 CLOSE AND FREE VTOC DATASET'
CFD000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         TM    DSW,VTOCALC              IS VTOC ALLOCATED
         BZ    CFD030                   RETURN WITHOUT ERROR
         MVC   WCLOSE(LCLOSE),CCLOSE    MOVE CLOSE TO WORK
         LA    R2,WVTOCIN               ADDRESS OF DCB
         CLOSE ((2)),MF=(E,WCLOSE)
         SPACE 3
CFD010   DS    0H
         DFREE ERROR=WERROR,FILE='VTOCIN'
         LTR   R15,R15                  TEST RETURN CODE
         BNZ   CFD020                   ERROR OCCURED
         CLC   WERROR,=F'0'             MUST ALSO BE ZERO
         BE    CFD030                   PROCEED
         SPACE 3
CFD020   DS    0H
         LA    R15,4                    SET NONZERO RETURN CODE
         B     CFD900                   RETURN
         SPACE 3
CFD030   DS    0H
         XR    R15,R15                  ZERO RETURN CODE
         SPACE 3
CFD900   DS    0H
         L     R14,DSAVE2               RELOAD RETURN ADDRESS
         LM    R0,R12,DSAVE2+8          RELOAD CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'CFS000 CLOSE AND FREE PRINT DATASET'
**********
*
*        CLOSE AND FREE PRINT DATASET
*
**********
         SPACE 3
CFS000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         MVC   WCLOSE(LCLOSE),CCLOSE    CLOSE TO WORK AREA
         MVI   WCLOSE,X'80'             END OF PARM-LIST
         LA    R2,WSYSOUT               ADDRESS OF DCB
         CLOSE ((2)),MF=(E,WCLOSE)      CLOSE DATASET
         SPACE 3
CFS010   DS    0H
         DFREE FILE=WFILE,ERROR=WERROR
         SPACE 3
CFS900   DS    0H
         LM    R14,R12,DSAVE2           RESTORE CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'DS1000 DISPLAY VTOC INFORMATION FOR ONE DS'
DS1000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         MVC   TLINE1(82),KLINE1        EDIT VTOC DISPLAY
         MVC   TLINE2(82),KLINE2
         MVC   TLINE3(82),KLINE3
         MVC   TLINE4(82),KLINE4
         MVC   TLINE9(82),KLINE9        LINE CONSTANT TO IOAREA
         LA    R1,ALINE2                ADDRESS OF 2ND LINE
         ST    R1,ALINE1                CHAIN TO 1ST
         LA    R1,ALINE3                ADDRESS OF 3RD LINE
         ST    R1,ALINE2                CHAIN TO 2ND
         LA    R1,ALINE4                ADDRESS OF 4TH LINE
         ST    R1,ALINE3                CHAIN TO 3RD
         LA    R1,0                     END OF CHAIN
         ST    R1,ALINE4
         MVC   TLINE1A,LVDSN            EDIT DATASET NAME
         MVC   TLINE2A,LVVOL            EDIT VOLUME SERIAL
         MVC   TLINE2B,LVUNIT           EDIT UNIT TYPE
         MVC   TLINE2C,LVDSORG          EDIT ORGANIZATION
         MVC   TLINE3A,LVALC            EDIT 1ST ALLOCATION
         LH    R1,LVSQUAN               LOAD 2ND QUANTITY
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         MVC   TLINE3B,DMASK1           EDIT-MASK
         ED    TLINE3B,DWORD+6          EDIT COUNT
         LH    R1,LVNOEXT               LOAD NUMBER OF EXTENT
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         MVC   TLINE3C,DMASK1           EDIT-MASK
         ED    TLINE3C,DWORD+6          EDIT COUNT
         L     R1,LVALCCYL              LOAD CYL-COUNT
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         MVC   TLINE4A,DMASK1           EDIT-MASK
         ED    TLINE4A,DWORD+6          EDIT CYLINDER-COUNT
         L     R1,LVALCTRK              LOAD TRK-COUNT
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         MVC   TLINE4B,DMASK1           EDIT-MASK
         ED    TLINE4B,DWORD+6          EDIT CYLINDER-COUNT
         L     R1,LVPERCNT              LOAD TRK-COUNT
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         MVC   TLINE4C,DMASK1           EDIT-MASK
         ED    TLINE4C,DWORD+6          EDIT PERCENTAGE
         CLC   LVDSORG,=CL2'IS'         INDEX-SEQ
         BE    DS1001                   YES
         CLC   LVDSORG,=CL2'DA'         OR DIRECT ACCESS
         BE    DS1001                   YES
         B     DS1010                   DISPLAY LINE
         SPACE 3
DS1001   DS    0H
         LA    R1,ALINE9                ADDRESS OF ADDITIONAL LINE
         ST    R1,ALINE4                CHAIN TO START
         LA    R1,0                     LAST OF CHAIN
         ST    R1,ALINE9                STORE
         MVC   TLINE9A,LVOPTCD          OPTION CODE TO LINE
         LH    R1,LVKEYL                LOAD KEYLENGTH
         CVD   R1,DWORD                 CONVERT TO DCIMAL
         MVC   TLINE9B,DMASK1           EDIT MASK TO IOAREA
         ED    TLINE9B,DWORD+6          EDIT KEYLENGTH
         LH    R1,LVRKP                 LOAD KEYLENGTH
         CVD   R1,DWORD                 CONVERT TO DCIMAL
         MVC   TLINE9C,DMASK1           EDIT MASK TO IOAREA
         ED    TLINE9C,DWORD+6          EDIT KEYLENGTH
         SPACE 3
DS1010   DS    0H
         LA    R2,WPTPB                 ADDRESS OF PUTLINE-BLOCK
         L     R3,CPPLUPT               ADDRESS OF UPT
         L     R4,CPPLECT               ADDRESS OF ECT
         XC    DECB,DECB                CLEAR CP-ECB
         LA    R5,DECB                  ADDRESS OF ECB
         LA    R6,ALINE1                ADDRESS OF OUTPUT LINES
         LA    R1,WIOPL                 ADDRESS OF WIOPL
         PUTLINE PARM=(2),UPT=(3),ECT=(4),                             *
               ECB=(5),OUTPUT=((6),TERM,MULTLIN,DATA),                 *
               MF=(E,(1))
         ST    R15,DRETC                SAVE RETURN CODE
         LTR   R15,R15                  TEST RETURN CODE
         BZ    DS1020                   NO ERRORS
         LA    R15,4                    SET RETURN CODE 4
         SPACE 3
DS1020   DS    0H
         TM    DSW,SYSOALC              SYSOUT DS ALLOCATED
         BZ    DS1900                   NO RETURN
         LA    R1,WIOPL                 PARAMETER-LIST ADDRESS
         BAL   R14,PRT000               PRINT LIST
         SPACE 3
DS1900   DS    0H
         L     R14,DSAVE2               RELOAD RETURN ADDRESS
         LM    R0,R12,DSAVE2+8          RELOAD CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'DS2000 EDIT AND DISPLAY VTOC HEADER LINE'
DS2000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         MVC   TLINE6(82),KLINE6        MOVE KONSTANT TO IOAREA
         MVC   TLINE7(82),KLINE7
         LA    R1,ALINE7                ADDRESS OF 2ND LINE
         ST    R1,ALINE6                CHAIN TO 1ST LINE
         LA    R1,0                     INDICATE LAST
         ST    R1,ALINE7                CHAIN TO 2ND LINE
         MVC   TLINE6A,LVVOL            MOVE VOLUME SERIAL TO LINE
         MVC   TLINE6B,LVUNIT           MOVE UNIT TYPE TO LINE
         LA    R2,WPTPB                 ADDRESS OF PUTLINE BLOCK
         L     R3,CPPLUPT               ADDRESS OF UPT
         L     R4,CPPLECT               ADDRESS OF ECT
         XC    DECB,DECB                CLEAR ECB
         LA    R5,DECB                  ADDRESS OF ECB
         LA    R6,ALINE6                ADDRESS OF OUTPUT LINES
         LA    R1,WIOPL                 ADDRESS OF INP OUTP PARMLIST
         PUTLINE PARM=(2),UPT=(3),ECT=(4),ECB=(5),                     *
               OUTPUT=((6),TERM,MULTLIN,DATA),MF=(E,(1))
         LTR   R15,R15                  TEST RETURN CODE
         BZ    DS2010                   NO ERRORS
         ST    R15,DRETC                SAVE RETURN CODE
         LA    R15,4                    SET NONZERO RETURN CODE
         B     DS2900                   RETURN
         SPACE 3
DS2010   DS    0H
         TM    DSW,SYSOALC              SYSOUT DS ALLOCATED
         BZ    DS2800                   NO RETURN
         LA    R1,WIOPL                 PARAMETER-LIST ADDRESS
         BAL   R14,PRT000               PRINT LIST
         SPACE 3
DS2800   DS    0H
         XR    R15,R15                  ZERO RETURN CODE
         SPACE 3
DS2900   DS    0H
         L     R14,DSAVE2               RELOAD RETURN ADDRESS
         LM    R0,R12,DSAVE2+8          RELOAD CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'DS3000 DISPLAY ONE LINE DSCB 1 INFORMATION'
DS3000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         MVC   TLINE5(82),KLINE5        MOVE CONSTANT TO IOAREA
         MVC   TLINE5A,LVDSN            MOVE DATASET NAME TO LINE
         MVC   TLINE5B,LVDSORG          DATASET ORGANIZATION
         MVC   TLINE5C,LVALC            DATASET ORGANIZATION
         LH    R1,LVSQUAN               LOAD SECONDARY QUANTITY
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE5D,DWORD+6          EDIT SECONDARY QUANTITY
         LH    R1,LVNOEXT               LOAD NUMBER OF EXTENTS
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE5E,DWORD+6          EDIT NUMBER OF EXTENTS
         L     R1,LVALCCYL              LOAD COUNT ALLOCATED CYLS
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE5F,DWORD+6          EDIT COUNT OF CYLS
         L     R1,LVALCTRK              LOAD COUNT ALLOCATED TRKS
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE5G,DWORD+6          EDIT USED TRACKS
         L     R1,LVPERCNT              LOAD PERCENTAGE
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE5H,DWORD+6          EDIT PERCENTAGE
         SPACE 3
DS3005   DS    0H
         LA    R2,WPTPB                 ADDRESS OF PUTLINE BLOCK
         L     R3,CPPLUPT               ADDRESS OF UPT
         L     R4,CPPLECT               ADDRESS OF ECT
         XC    DECB,DECB                CLEAR ECB
         LA    R5,DECB                  ADDRESS OF ECB
         LA    R6,TLINE5                ADDRESS OF OUTPUT-LNE
         LA    R1,WIOPL                 ADDRESS OF INPUT-OUTPUT-BLOCK
         PUTLINE PARM=(2),UPT=(3),ECT=(4),ECB=(5),                     *
               OUTPUT=((6),TERM,SINGLE,DATA),MF=(E,(1))
         LTR   R15,R15                  TEST RETURN CODE
         BZ    DS3010                   NO ERRORS
         ST    R15,DRETC                SAVE RETURN CODE
         LA    R15,4                    SET NONZERO RETURN CODE
         B     DS3900                   RETURN
         SPACE 3
DS3010   DS    0H
         TM    DSW,SYSOALC              SYSOUT DS ALLOCATED
         BZ    DS3800                   NO
         LA    R1,WIOPL                 PARAMETER-LIST ADDRESS
         BAL   R14,PRT000               PRINT LIST
         SPACE 3
DS3800   DS    0H
         XR    R15,R15                  RETURN CODE 0
         SPACE 3
DS3900   DS    0H
         L     R14,DSAVE2               RELOAD RETURN ADDRESS
         LM    R0,R12,DSAVE2+8          RELOAD CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'DS4000 DISPLAY UNUSED SPACE LINE'
DS4000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         MVC   TLINE8(82),KLINE8        MOVE CONSTANTS TO LINE
         MVC   TLINE10(82),KLINE10      MOVE CONSTANTS TO LINE
         LA    R1,ALINE10               CHAIN TOGETHER
         ST    R1,ALINE8
         LA    R1,0
         ST    R1,ALINE10
         L     R0,LV5SPCE               LOAD FREE SPACE COUNT
         SRDA  R0,32                    SHIFT TO ODD REGISTER
         LH    R2,DNOTRK                LOAD NUMBER OF TRACKS
         DR    R0,R2                    CALCULATE FREE CYLS AND TRKS
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE8A,DWORD+6          EDIT CYL COUNT
         CVD   R0,DWORD                 CONVERT TO DECIMAL
         ED    TLINE8B,DWORD+6          EDIT TRK COUNT
         L     R0,LV5EXT3               LOAD GR EXTENTS
         SRDA  R0,32                    SHIFT TO ODD REGISTER
         LH    R2,DNOTRK                LOAD NUMBER OF TRACKS
         DR    R0,R2                    CALCULATE FREE CYLS AND TRKS
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE10A,DWORD+6         EDIT CYL COUNT
         CVD   R0,DWORD                 CONVERT TO DECIMAL
         ED    TLINE10B,DWORD+6         EDIT TRK COUNT
         L     R0,LV5EXT2               SECOND GR EXTENT
         SRDA  R0,32                    SHIFT TO ODD REGISTER
         LH    R2,DNOTRK                LOAD NUMBER OF TRACKS
         DR    R0,R2                    CALCULATE FREE CYLS AND TRKS
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE10C,DWORD+6         EDIT CYL COUNT
         CVD   R0,DWORD                 CONVERT TO DECIMAL
         ED    TLINE10D,DWORD+6         EDIT TRK COUNT
         L     R0,LV5EXT1               LOAD FREE SPACE COUNT
         SRDA  R0,32                    SHIFT TO ODD REGISTER
         LH    R2,DNOTRK                LOAD NUMBER OF TRACKS
         DR    R0,R2                    CALCULATE FREE CYLS AND TRKS
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    TLINE10E,DWORD+6         EDIT CYL COUNT
         CVD   R0,DWORD                 CONVERT TO DECIMAL
         ED    TLINE10F,DWORD+6         EDIT TRK COUNT
         LA    R2,WPTPB                 ADDRESS OF PUTLINE BLOCK
         L     R3,CPPLUPT               ADDRESS OF UPT
         L     R4,CPPLECT               ADDRESS OF ECT
         XC    DECB,DECB                CLEAR ECB
         LA    R5,DECB                  ADDRESS OF ECB
         LA    R6,ALINE8                ADDRESS OF OUTPUT LINE
         LA    R1,WIOPL                 ADDRESS OF INPUT OUTPUT BLOCK
         PUTLINE PARM=(2),UPT=(3),ECT=(4),ECB=(5),                     *
               OUTPUT=((6),TERM,MULTLIN,DATA),MF=(E,(1))
         LTR   R15,R15                  TEST RETURN CODE
         BZ    DS4010                   NO ERRORS
         ST    R15,DRETC                SAVE RETURN CODE
         LA    R15,4                    SET NONZERO RETRUN CODE
         B     DS4900                   RETURN
         SPACE 3
DS4010   DS    0H
         TM    DSW,SYSOALC              SYSOUT DS ALLOCATED
         BZ    DS4800                   NO
         LA    R1,WIOPL                 ADDRESS OF WIOPL
         BAL   R14,PRT000               PRINT LIST
         SPACE 3
DS4800   DS    0H
         XR    R15,R15                  ZERO RETURN CODE
         SPACE 3
DS4900   DS    0H
         L     R14,DSAVE2               LOAD RETURN ADDRESS
         LM    R0,R12,DSAVE2+8          RELOAD REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'PRT000 PRINT VTOC INFORMATION'
**********
*
*        PRINT VTOC INFORMATION
*
**********
         SPACE 3
PRT000   DS    0H
         STM   R14,R12,DSAVE3           SAVE CALLERS REGISTERS
         MVI   WLINE,X'40'              BLANK PRINT LINE
         MVC   WLINE+1(L'WLINE-1),WLINE
         MVI   WLINE,X'09'              SKIP ONE LINE AFTER PRINT
         LR    R9,R1                    RELOAD PARM ADDRESS
         USING IOPL,R9                  ESTABLISH ADDRESSABILITY
         L     R8,IOPLIOPB              PTPB ADDRESS
         USING PTPB,R8                  ESTABLISH ADDRESSABILITY
         L     R3,PTPBOPUT              ADDRESS OF OUTPUT BUFFER
         TM    PTPB,X'10'               SINGLE LINE DATA
         BZ    PRT100                   NO MULTILINE
         LH    R4,0(,R3)                LOAD LINE LENGTH
         SH    R4,=H'5'                 DECREMENT FOR EXECUTE
         EX    R4,PRT010                EXECUTE MVC
         LA    R2,WSYSOUT               DCB ADDRESS
         PUT   (2),WLINE                WRITE RECORD
         B     PRT900                   RETURN TO CALLER
         SPACE 3
PRT010   DS    0H
         MVC   WLINE+1(0),4(R3)         ***   EXECUTED ONLY   ***
         SPACE 3
PRT100   DS    0H
         LH    R4,4(,R3)                LOAD LINE LENGTH
         SH    R4,=H'5'                 DECREMENT FOR EXECUTE
         EX    R4,PRT110                EXECUTE MVC
         LA    R2,WSYSOUT               ADDRESS OF DCB
         PUT   (2),WLINE                WRITE DATA LINE
         L     R3,0(,R3)                NEXT LINE ADDRESS
         LTR   R3,R3                    TEST ON END
         BNZ   PRT100                   NO
         B     PRT900                   RETURN TO CALLER
         SPACE 3
PRT110   DS    0H
         MVC   WLINE+1(0),8(R3)         ***   EXECUTED ONLY   ***
         SPACE 3
PRT900   DS    0H
         LM    R14,R12,DSAVE3           RESTORE CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         SPACE 3
         DROP  R8,R9
         TITLE 'FU0000 FIND UCB FOR VOLUME AND FIND UNIT-TYPE'
FU0000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         L     R2,16                    LOAD CVT-ADDRESS
         L     R2,40(,R2)               ADDRESS OF UCB-LOOKUP-TABLE
         SH    R2,=H'2'                 POINT BEFORE TABLE
         SPACE 3
FU0010   DS    0H
         LA    R2,2(,R2)                ADDRESS OF NEXT UCB
         XR    R8,R8                    CLEAR FOR INSERT
         ICM   R8,3,0(R2)               LOAD UCB ADDRESS
         BZ    FU0010                   NEXT UCB
         CL    R8,=A(X'FFFF')           END OF LOOKUP TABLE
         BNE   FU0020                   NO
         LA    R15,4                    SET NONZERO RETURN CODE
         B     FU9000                   RETURN TO CALLER
         SPACE 3
FU0020   DS    0H
         CLI   18(R8),X'20'             DEVICE-TYPE DIRECT ACCESS
         BNE   FU0010                   NO
         CLC   28(6,R8),LVVOL           VOLUME EQUAL
         BNE   FU0010                   NO
         MVC   LVUNIT,=CL8'3350'        UNIT 3350
         LA    R1,30                    NO OF TRK'S PER CYL
         CLI   19(R8),X'0B'             DEVICE 3350
         BE    FU0030                   YES
         MVC   LVUNIT,=CL8'3330-11'     UNIT 3330-11
         LA    R1,19                    NO OF TRK'S PER CYL
         CLI   19(R8),X'0D'             TEST
         BE    FU0030                   YES
         MVC   LVUNIT,=CL8'********'    UNKNOWN DEVICE
         SPACE 3
FU0030   DS    0H
         STH   R1,DNOTRK                SAVE NO OF TRK'S
         XR    R15,R15                  RETURN CODE 0
         SPACE 3
FU9000   DS    0H
         L     R14,DSAVE2               RELOAD RETURN ADDRESS
         LM    R0,R12,DSAVE2+8          RELOAD REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'PD1000 PROCESS FORMAT 1 DSCB EXTRACT INFORMATION'
PD1000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         SPACE 3
PD1100   DS    0H
         LA    R9,DCAMRET               ADDRESS OF RETURN AREA
         USING DSCB,R9                  ESTABLISH ADDRESSABILITY
         NI    DSW,X'FF'-DSCB1P         SWITCH OFF BIT
         TM    DSW,VOL                  VOLUME PROCESSING REQUEST
         BZ    PD1105                   NO
         MVC   LVDSN,DS1DSNAM           SAVE DATASET NAME
         SPACE 3
PD1105   DS    0H
         MVC   LVDSORG,=CL2'IS'         INDEXED SEQUENTIAL
         TM    DS1DSORG,DSORGIS         TEST
         BO    PD1110                   YES
         MVC   LVDSORG,=CL2'PS'         SEQUENTIAL
         TM    DS1DSORG,DSORGPS         TEST
         BO    PD1110                   YES
         MVC   LVDSORG,=CL2'DA'         DIRECT
         TM    DS1DSORG,DSORGDA         TEST
         BO    PD1110                   YES
         MVC   LVDSORG,=CL2'PO'         PARTITIONED
         TM    DS1DSORG,DSORGPO         TEST
         BO    PD1110                   YES
         MVC   LVDSORG,=CL2'VS'         VSAM
         TM    DS1DSORG+1,DSORGAM       TEST
         BO    PD1110                   YES
         MVC   LVDSORG,=CL2'**'         UNKNOWN
         SPACE 3
PD1110   DS    0H
         MVC   LVALC,=CL3'CYL'          CYLINDER ALLOCATION
         TM    DS1SCALO,ALOCYL          TEST
         BO    PD1120                   PROCEED
         MVC   LVALC,=CL3'REL'          TRACK RELATIVE
         BZ    PD1120                   PRECEED
         MVC   LVALC,=CL3'TRK'          TRACK ABSOLUTE
         TM    DS1SCALO,ALOTRK          TEST
         BO    PD1120                   PROCEED
         MVC   LVALC,=CL3'BLK'          MUST BE BLOCK
         SPACE 3
PD1120   DS    0H
         XR    R2,R2                    CLEAR FOR INSERT
         ICM   R2,7,DS1SCALO+1          INSERT SECONDARY QUANTITY
         STH   R2,LVSQUAN               SAVE QUANTITY
         XR    R2,R2                    CLEAR FOR INSERT
         ICM   R2,1,DS1NOEPV            INSERT NUMBER OF EXTENTS
         STH   R2,LVNOEXT               SAVE NUMBER OF EXTENTS
         XR    R2,R2                    CLEAR FOR INSERT
         ICM   R2,3,DS1LSTAR            USED SIZE
         LA    R2,1(,R2)                INCREMENT TO FULL TRACK
         ST    R2,LVSIZEU               SAVE LAST BLOCK POINTER
         TM    DS1DSORG,DSORGIS         DS-ORGANIZATION INDEX-SEQ
         BO    PD112001                 YES
         TM    DS1DSORG,DSORGDA         DIRECT ORGANIZATION
         BO    PD112001                 YES
         B     PD1130                   SKIP OPTION CODE ETC
         SPACE 3
PD112001 DS    0H
         LA    R1,LVOPTCD               ADDRESS OF OPTCD-OUTPUT
         TM    DS1OPTCD,X'80'           WRITE CHECK OPTION
         BZ    PD112002                 NO
         MVI   0(R1),C'W'               OPTION CODE W
         LA    R1,1(,R1)                NEXT POSITION
         SPACE 3
PD112002 DS    0H
         TM    DS1OPTCD,X'40'           ONLY FOR INDEX-SEQ
         BZ    PD112003                 SKIP
         TM    DS1DSORG,DSORGIS         INDEX-SEQ
         BZ    PD112003                 NO
         MVI   0(R1),C'U'               OPTION CODE U
         LA    R1,1(,R1)                NEXT POSITION
         SPACE 3
PD112003 DS    0H
         TM    DS1OPTCD,X'20'           EXTENDED SEARCH - MASTER INDEX
         BZ    PD112005                 NO
         MVI   0(R1),C'E'               EXTENDED SEARCH OPTION
         TM    DS1DSORG,DSORGIS         INDEXED
         BZ    PD112004                 NO DIRECT
         MVI   0(R1),C'M'               MASTER INDEX OPTION
         SPACE 3
PD112004 DS    0H
         LA    R1,1(,R1)                NEXT POSITION
         SPACE 3
PD112005 DS    0H
         TM    DS1OPTCD,X'10'           FEEDBACK - INDEPENDENT OVERFLOW
         BZ    PD112007                 NO
         MVI   0(R1),C'F'               FEEDBACK OPTION
         TM    DS1DSORG,DSORGIS         INDEX-SEQ
         BZ    PD112006                 NO
         MVI   0(R1),C'I'               INDEPENDENT OVERFLOW OPTION
         SPACE 3
PD112006 DS    0H
         LA    R1,1(,R1)                NEXT POSITION
         SPACE 3
PD112007 DS    0H
         TM    DS1OPTCD,X'08'           CYLINDER OVERFLOW - ACTUAL ADDR
         BZ    PD112009                 NO
         MVI   0(R1),C'A'               ACTUAL ADDRESSING
         TM    DS1DSORG,DSORGIS         INDEX-SEQ
         BZ    PD112008                 NO
         MVI   0(R1),C'Y'               CYLINDER OVERFLOW OPTION
         SPACE 3
PD112008 DS    0H
         LA    R1,1(,R1)                NEXT POSITION
         SPACE 3
PD112009 DS    0H
         TM    DS1OPTCD,X'02'           DELETE OPTION
         BZ    PD112010                 NO
         TM    DS1DSORG,DSORGIS         INDEX SEQ
         BZ    PD112010                 NO
         MVI   0(R1),C'L'               DELETE OPTION
         LA    R1,1(,R1)                NEXT POSITION
         SPACE 3
PD112010 DS    0H
         TM    DS1OPTCD,X'01'           RELATIVE ADDR. - REORGANIZATION
         BZ    PD112011                 NO
         MVI   0(R1),C'R'               OPTION
         SPACE 3
PD112011 DS    0H                       END OF OPTION CODE PROCESSING
         XR    R1,R1                    CLEAR FOR INSERT
         ICM   R1,1,DS1KEYL             INSERT KEYLENGTH
         STH   R1,LVKEYL                SAVE KEYLENGTH
         XR    R1,R1                    CLEAR FOR INSERT
         ICM   R1,3,DS1RKP              RELATIVE KEY POSITION
         STH   R1,LVRKP                 SAVE KEY POSITION
         SPACE 3
         SPACE 3
PD1130   DS    0H
         XC    LVSIZE,LVSIZE            SET SIZE ZERO
         LH    R3,LVNOEXT               LOAD NUMBER OF EXTENTS
         LA    R4,3                     NUMBER OF EXTENTS ON DSCB1
         LA    R2,DS1EXT1               ADDRESS OF FIRST EXTENT
         SPACE 3
PD1140   DS    0H
         CLI   0(R2),X'00'              END OF EXTENTS
         BE    PD1200                   END REACHED
         TM    0(R2),EXT1UL             USER LABEL EXTENT
         BZ    PD1145                   NO CALC SIZE
         LA    R2,10(,R2)               ADDRESS OF NEXT EXTENT
         B     PD1150                   SKIP THIS EXTENT
         SPACE 3
PD1145   DS    0H
         LA    R1,2(,R2)                ADDRESS OF LIMIT-ADDRESSES
         BAL   R14,SZ0000               CALCULATE SIZE
         A     R1,LVSIZE                ADD SIZE
         ST    R1,LVSIZE                SAVE SIZE
         LA    R2,10(,R2)               ADDRESS OF NEXT EXTENT
         BCT   R3,PD1150                GET ALL EXTENTS
         B     PD1200                   END REACHED
         SPACE 3
PD1150   DS    0H
         BCT   R4,PD1140                EXTENT ON DSCB1
         TM    DSW,DSCB1P               DSCB1 PROCESSED
         BZ    PD1160                   NO
         LA    R2,1(,R2)                ADDRESS OF NEXT EXTENT
         LA    R4,10                    NEW BCT COUNT
         B     PD1140                   GET ALL DSCBS
         SPACE 3
PD1160   DS    0H
         CLC   DS1PTRDS,=XL5'00'        END OF CHAIN
         BE    PD1200                   END
         MVC   LVCCHHR,DS1PTRDS         SAVE POINTER
         BAL   R14,OFX000               OBTAIN DSCB2 OR DSCB3
         CLC   LVDSORG,=CL2'IS'         IS ORGANIZATION
         BNE   PD1170                   PROCEED
         LA    R9,DCAMRET               ADDRESS OF RETURN AREA
         USING DSCB,R9                  ESTABLISH ADDRESSABILITY
         CLC   DS2PTRDS,=XL5'00'        END OF CHAIN
         BE    PD1200                   END REACHED
         MVC   LVCCHHR,DS2PTRDS         SAVE POINTER
         BAL   R14,OFX000               OBTAIN DSCB3
         SPACE 3
PD1170   DS    0H
         LA    R10,DCAMRET              ADDRESS OF DSCB
         LA    R4,4                     LOAD NEW BCT COUNT
         LA    R2,DS3EXTNT              ADDRESS OF EXTENTS
         OI    DSW,DSCB1P               DSCB1 PROCESSED
         B     PD1140                   GET ALL EXTENTS
         SPACE 3
PD1200   DS    0H
         CLC   LVDSORG,=CL2'PS'         SEQUENTIAL ORGANIZED
         BE    PD1210                   CALCULATE UNUSED SIZE
         CLC   LVDSORG,=CL2'PO'         PARTITIONED ORGANIZED
         BE    PD1210                   CALCULATE UNUSED SIZE
         L     R1,LVSIZE                LOAD ALLOCATED SIZE
         ST    R1,LVSIZEU               SAVE AS USED SIZE
         SPACE 3
PD1210   DS    0H
         L     R2,LVSIZE                LOAD SIZE
         L     R4,LVSIZEU               LOAD USED SIZE
         MH    R4,=H'100'               CALCULATE USED PERCENTAGE
         SRDL  R4,32                    SHIFT TO ODD REGISTER
         LTR   R2,R2                    TEST DIVIDEND ZERO
         BNZ   PD1220                   PROCEED
         LA    R2,1(,R2)                GET A VALID DIGIT
         SPACE 3
PD1220   DS    0H
         DR    R4,R2
         ST    R5,LVPERCNT              SAVE PERCENTAGE
         L     R2,LVSIZE                LOAD SIZE
         SRDL  R2,32                    SHIFT TO ODD REGISTER
         LH    R4,DNOTRK                LOAD TRACK PER CYL
         DR    R2,R4                    CALCULATE CYL AND TRACKS
         ST    R2,LVALCTRK              SAVE TRACK ALLOACTED
         ST    R3,LVALCCYL              SAVE CYLS ALLOCATED
         SPACE 3
PD1900   DS    0H
         LM    R14,R12,DSAVE2           RESTORE CALLERS REGISTERS
         BR    R14                      RETURN TO CALLERS
         TITLE 'PD4000 PROCESS FORMAT 4 DSCB AND EXTRACT INFORMATION'
PD4000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         LA    R9,DCAMRET               ADRESS OF DSCB-BUFFER
         USING DSCB,R9                  ESTABLISH ADDRESSABILITY
         SPACE 3
PD4010   DS    0H
         LA    R1,DS4VTOCE+2            VTOC EXTENT DESCRIPTION
         BAL   R14,SZ0000               CALCULATE VTOC-SIZE
         XR    R2,R2                    CLEAR FOR INSERT
         IC    R2,DS4DEVDT              INSERT # OF DSCB'S ON TRACK
         STH   R2,LV4DEVDT              SAVE COUNT
         MH    R1,LV4DEVDT              CALC # OF DSCB'S IN VTOC
         SH    R1,DS4DSREC              SUBTRACT # OF FORMAT 0 DSCB'S
         ST    R1,LV4DSCBN              NUMBER OF DSCB'S NOT FORMAT 0
         SPACE 3
PD4900   DS    0H
         LM    R14,R12,DSAVE2           RELOAD CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         DROP  R9
         TITLE 'PD5000 EXTRACT INFORMATION FROM FORMAT 5 DSCB'
PD5000   DS    0H
         STM   R14,R12,DSAVE2           SAVE CALLERS REGISTERS
         LA    R9,DCAMRET               ADDRESS OF DSCB
         USING DSCB,R9                  ESTABLISH ADDRESSABILITY
         LA    R8,DS5AVEXT              ADDRESS OF EXTENT DESCRIPTION
         LA    R2,8                     LOOP COUNTER
         NI    DSW,X'FF'-DSCB5P         SWITCH OFF BIT
         SPACE 3
PD5010   DS    0H
         XR    R3,R3                    CLEAR FOR INSERT
         ICM   R3,3,2(R8)               FREE CYLINDER COUNTT
         MH    R3,DNOTRK                CALCULATE TRACKS
         XR    R4,R4                    CLEAR FOR INSERT
         ICM   R4,1,4(R8)               INSERT TRACK COUNT
         AR    R3,R4                    TOTAL SPACE
         C     R3,LV5EXT1               TEST SMALLEST EXTENT
         BL    PD5020                   THIS EXTENT SMALLER
         ST    R3,LV5EXT1               SAVE AS EXTENT
         C     R3,LV5EXT2               GREATER SECOND EXTENT
         BL    PD5020                   YES
         XC    LV5EXT1,LV5EXT2          EXCHANGE
         XC    LV5EXT2,LV5EXT1
         XC    LV5EXT1,LV5EXT2
         C     R3,LV5EXT3               GREATER THAN GREATEST
         BL    PD5020
         XC    LV5EXT2,LV5EXT3
         XC    LV5EXT3,LV5EXT2
         XC    LV5EXT2,LV5EXT3
         SPACE 3
PD5020   DS    0H
         A     R3,LV5SPCE               CALCULATE TOTAL
         ST    R3,LV5SPCE               STORE SUM BACK
         LA    R8,5(,R8)                NEXT EXTENT DESCRIPTION
         BCT   R2,PD5010
         TM    DSW,DSCB5P               DSCB 5 PROCESSED
         BO    PD5900                   YES END
         LA    R8,1(,R8)                SKIP IDENTIFIER
         LA    R2,18                    NEW BCT COUNT
         OI    DSW,DSCB5P               INDICATE DSCB 5 PROCESSED
         B     PD5010                   LOOP THROUGH WHOLE DSCB
         SPACE 3
PD5900   DS    0H
         LM    R14,R12,DSAVE2           RESTORE REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'LC0000 LOCATE A DATASET IN CATALOG'
LC0000   DS    0H
         STM   R14,R12,DSAVE3           SAVE CALLERS REGISTERS
         XC    DCAMOP0(4),DCAMOP0       CLEAR OPTION BYTES
         MVI   DCAMOP0,X'44'            INDICATE LOCATE
         LA    R1,LVDSN                 ADDRESS OF DSNAME
         ST    R1,DCAMDSN               SAVE IN PARM
         XC    DCAMVOL,DCAMVOL          CLEAR VOLUME POINTER
         LA    R1,DCAMRET               ADDRESS OF RETURN AREA
         ST    R1,DCAMRETA              SAVE IN PARM
         LA    R1,DCAMLST               ADDRESS OF PARM-LIST
         LOCATE (1)                     LOCATE DATASET
         L     R14,DSAVE3               RELOAD RETURN ADDRESS
         LM    R0,R12,DSAVE3+8          RELOAD REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'OF1000 OBTAIN FORMAT 1 DSCB FROM VTOC'
OF1000   DS    0H
         STM   R14,R12,DSAVE3           SAVE CALLERS REGISTERS
         XC    DCAMOP0(4),DCAMOP0       CLEAR OPTION
         MVI   DCAMOP0,X'C1'            INDICATE SEARCH
         LA    R1,LVDSN                 ADDRESS OF DATASET NAME
         ST    R1,DCAMDSN               SAVE IN PARM
         LA    R1,LVVOL                 ADDRESS OF VOLUME
         ST    R1,DCAMVOL               SAVE IN PARM
         LA    R1,DCAMRET+44            ADDRESS OF RETURN
         ST    R1,DCAMRETA              SAVE IN PARM
         LA    R1,DCAMLST               ADDRESS OF PARM-LIST
         OBTAIN (1)                     OBTAIN DSCB
         L     R14,DSAVE3               LOAD RETURN ADDRESS
         LM    R0,R12,DSAVE3+8          LOAD CALLERS REGISTERS
         BR    R14                      RETURN
         TITLE 'OFX000 OBTAIN FORMAT OTHER THAN ONE BY CCHHR'
OFX000   DS    0H
         STM   R14,R12,DSAVE3           SAVE CALLERS REGISTERS
         XC    DCAMOP0(4),DCAMOP0       CLEAR OPTIONS
         MVI   DCAMOP0,X'C0'            SEEK OPTION
         MVI   DCAMOP1,X'80'            SEEK OPTION
         LA    R1,LVCCHHR               ADDRESS OF DSCB
         ST    R1,DCAMDSN               SAVE IN PARM
         LA    R1,LVVOL                 ADDRESS OF VOLUME
         ST    R1,DCAMVOL               SAVE IN PARM
         LA    R1,DCAMRET               ADDRESS OF RETURN-AREA
         ST    R1,DCAMRETA              SAVE IN PARM
         LA    R1,DCAMLST               LOAD PARM ADDRESS
         OBTAIN (1)                     OBTAIN DSCB
         L     R14,DSAVE3               RELOAD RETURN ADDRESS
         LM    R0,R12,DSAVE3+8          RELOAD REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'ER0000 ERROR MESSAGE ISSUER ROUTINE'
ER0000   DS    0H
         STM   R14,R12,DSAVE2           CALLERS REGISTERS
         SPACE 3
ER0010   DS    0H
         LA    R1,MTCSECTP              ADDRESS OF MESSAGE DESCRIPTION
         ST    R1,MTPLPTR               STORE ADDRESS
         ST    R10,MTCPPLP              STORE POINTER
         XC    DECB,DECB                CLEAR ECB
         LA    R1,DECB                  ADDRESS OF ECB
         ST    R1,MTECBP                STORE POINTER
         MVI   MTRESV1,X'80'            HIGH ORDER BYTE ON
         L     R1,=A(MSGCSECT)          ADDRESS OF MESSAGE DEFINITIONS
         ST    R1,MTCSECTP              STORE POINTER
         SPACE 3
ER0020   DS    0H
         CLI   DMSGID,C'1'              MESSAGE CLASS 1
         BNE   ER9000                   RETURN
         LA    R1,4                     LENGTH OF ERROR CODE
         SLL   R1,24                    SHIFT TO HIGH ORDER
         LA    R2,DRETC                 ADDRESS OF RETURN CODE
         OR    R2,R1                    OR IN REGISTER
         LA    R1,X'80'                 INDICATE CONVERSION
         SLL   R1,24                    SHIFT TO HIGH ORDER
         OR    R2,R1                    OR IN REGISTER
         ST    R2,MTINSRT               STORE INSERT POINTER
         MVI   MTSW1,X'40'              INDICATE PUTLINE
         MVC   MTMSGID(4),DMSGID        MOVE MSG-NUMBER
         LA    R1,MTPLPTR               ADDRESS OF PARAMETER-LIST
         LINK  EP=IKJEFF02              LINK TO IKJEFF02
         SPACE 3
ER9000   DS    0H
         LM    R14,R12,DSAVE2           RESTORE CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         TITLE 'SZ0000 CALCULATE USED SIZE'
SZ0000   DS    0H
         STM   R14,R12,DSAVE3           SAVE CALLERS REGISTERS
         XR    R2,R2                    CLEAR FOR INSERT
         ICM   R2,3,4(R1)               INSERT CYL-COUNT
         MH    R2,DNOTRK                MULTIPLY BY TRACK
         XR    R3,R3                    CLEAR FOR INSERT
         ICM   R3,3,6(R1)               INSERT TRACK-COUNT
         LA    R3,1(,R3)                ADD ONE
         AR    R2,R3                    CALCULATE SUM
         XR    R3,R3                    CLEAR FOR INSERT
         ICM   R3,3,0(R1)               INSERT CYL-COUNT
         MH    R3,DNOTRK                MULTIPLY BY TRACK-COUNT
         XR    R4,R4                    CLEAR FOR INSERT
         ICM   R4,3,2(R1)               INSERT TRACKS
         LA    R4,1(,R4)                ADD ONE
         AR    R3,R4                    GET SUMM
         SR    R2,R3                    CALCULATE SIZE
         LA    R1,1(,R2)
         LM    R14,R0,DSAVE3            RESTORE REGISTERS
         LM    R2,R12,DSAVE3+16         DITO
         BR    R14
         EJECT
CPCL     DC    A(PARS)
         SPACE 3
C1100    DC    CL4'1100'                PARS ERROR MESSAGE-ID
C1101    DC    CL4'1101'                NO MEMBER MESSAGE-ID
C1102    DC    CL4'1102'                DATA SET NOT CATALOGED
C1103    DC    CL4'1103'                NOT DIRECT ACCESS DEVICE
C1104    DC    CL4'1104'                OBTAIN ERROR MSG-ID
C1106    DC    CL4'1106'                PUTLINE ERROR MSG-ID
C1107    DC    CL4'1107'                ERROR ID
C1108    DC    CL4'1108'                ERROR ID
         SPACE 5
KLINE1   DC    AL2(LKLINE1),AL2(0)
         DC    CL78'DSNAME:'
LKLINE1  EQU   *-KLINE1
         SPACE
KLINE2   DC    AL2(LKLINE2),AL2(0)
         DC    CL78'VOLUME:                   UNIT:             DSORG:'
LKLINE2  EQU   *-KLINE2
         SPACE
KLINE3   DC    AL2(LKLINE3),AL2(0)
         DC    CL78'ALLOC:                    2ND-ALLOC:        EXTENTS*
               :'
LKLINE3  EQU   *-KLINE3
         SPACE
KLINE4   DC    AL2(LKLINE4),AL2(0)
         DC    CL78'SIZE:        CYL     TRK  USED:          %'
LKLINE4  EQU   *-KLINE4
         SPACE
KLINE5   DC    AL2(LKLINE5),AL2(0)
         DS    0CL78
         DC    CL53' '
         DC    XL24'402021204040202120404020212040402021204040202120'
         DC    CL1'%'
LKLINE5  EQU   *-KLINE5
         SPACE
KLINE6   DC    AL2(LKLINE6),AL2(0)
         DC    CL78'VOLUME:            UNIT:'
LKLINE6  EQU   *-KLINE6
         SPACE
KLINE7   DC    AL2(LKLINE7),AL2(0)
         DC    CL78'DSNAME                                     DSORG AL*
               C  2ND  EXT  CYL  TRK USED'
LKLINE7  EQU   *-KLINE7
         SPACE
KLINE8   DC    AL2(LKLINE8),AL2(0)
         DS    0CL78
         DC    CL14'UNUSED SPACE:'
         DC    XL4'40202120'
         DC    CL5' CYL '
         DC    XL4'40202120'
         DC    CL49' TRK'
LKLINE8  EQU   *-KLINE8
         SPACE
KLINE9   DC    AL2(LKLINE9),AL2(0)
         DC    CL78'OPTCD:                    KEYL:             RKP:'
LKLINE9  EQU   *-KLINE9
         SPACE
KLINE10  DC    AL2(LKLINE10),AL2(0)
         DS    0CL78
         DC    CL17'GREATEST EXTENTS:'
         DC    XL4'40202120'
         DC    CL4' CYL'
         DC    XL4'40202120'
         DC    CL8' TRK ---'
         DC    XL4'40202120'
         DC    CL4' CYL'
         DC    XL4'40202120'
         DC    CL8' TRK ---'
         DC    XL4'40202120'
         DC    CL4' CYL'
         DC    XL4'40202120'
         DC    CL9' TRK'
LKLINE10 EQU   *-KLINE10
         SPACE 5
DMASK1   DC    XL4'40202120'            EDIT-MASK
         SPACE 5
CPTPB    PUTLINE MF=L
LPTPB    EQU   *-CPTPB
         ORG   CPTPB
         SPACE 3
STAXL    STAX  MF=L
LSTAXL   EQU   *-STAXL
         ORG   STAXL                    FREE UNUSED SPACE
         SPACE 3
         PRINT NOGEN
VTOCIN   DCB   DSORG=PS,MACRF=RC,DDNAME=VTOCIN,EODAD=MA0610,           *
               BLKSIZE=96,KEYLEN=44,RECFM=F
LVTOCIN  EQU   *-VTOCIN
         SPACE 3
SYSOUT   DCB   DSORG=PS,MACRF=(PM),BLKSIZE=133,RECFM=FM
LSYSOUT  EQU   *-SYSOUT
         PRINT GEN
         SPACE
COPEN    OPEN  (,),MF=L
LOPEN    EQU   *-COPEN
         SPACE
CCLOSE   CLOSE (,),MF=L
LCLOSE   EQU   *-COPEN
         SPACE
         LTORG
         EJECT
DYNSTOR  DSECT
DSAVE1   DS    18F                      SAVE AREA
DSAVE2   DS    15F                      2ND SAVE AREA
DSAVE3   DS    15F                      2ND SAVE AREA
         SPACE 3
DPPL     DS    7F                       PARS-PARAMETER-LIST
         SPACE 3
DMSGID   DS    CL4                      MESSAGE IDENTIFIER
DRETC    DS    F                        ERRANEOUS RETURN CODE
         SPACE 3
DANS     DS    F                        PARS ANSWER-AREA-POINTER
         SPACE 3
LVDSN    DS    CL44                     DATA-SET NAME
LVVOL    DS    CL6                      VOLUME-SERIAL
LVUNIT   DS    CL8                      UNIT-TYPE
LVALC    DS    CL3                      1ST ALLOCATION
LVDSORG  DS    CL2                      DATA SET ORGANIZATION
LVSQUAN  DS    H                        2ND ALLOCATION QUANTITY
LVNOEXT  DS    H                        NUMBER OF EXTENTS
LVSIZE   DS    F                        SIZE ALLOCATED
LVSIZEU  DS    F                        SIZE USED
LVCCHHR  DS    XL5                      DSCB ADDRESS
LVPERCNT DS    F                        PERCENTAGE USED
LVALCCYL DS    F                        ALLOCATED CYLINDERS
LVALCTRK DS    F                        ALLOCATED TRACKS
LVOPTCD  DS    CL8                      OPTION CODE FOR IS AND DA
LVKEYL   DS    H                        KEY LENGTH FOR IS OR DA
LVRKP    DS    H                        RELATIVE KEY POSITION IS OR DA
         SPACE
LV5SPCE  DS    F                        UNUSED SPACE
LV5EXT1  DS    F                        SMALLEST - GREATEST EXTENT
LV5EXT2  DS    F                                   GREATEST EXTENT
LV5EXT3  DS    F                                   GREATEST EXTENT
         SPACE 3
LV4DSCBN DS    F                        COUNT OF NON-FORMAT 0 DSCB'S
LV4DEVDT DS    F                        # OF VTOC-RECORDS ON 1 TRACK
         EJECT
         IKJEFFMT
         EJECT
DECB     DS    F                        ECB FOR IO-SERVICE
         SPACE
DDECB    DS    10F                      DECB FOR READ
         SPACE
DCAMLST  DS    0F                       PARAMETER-LIST
DCAMOP0  DS    X                        FIRST OPTION
DCAMOP1  DS    X                        2ND OPTION
DCAMOP2  DS    X                        3RD OPTION
DCAMOP3  DS    X                        4TH OPTION
DCAMDSN  DS    A                        ADDRESS OF CCHHR OR DSN
DCAMVOL  DS    A                        ADDRESS OF VOLUME OR ZERO
DCAMRETA DS    A                        ADDRESS OF RETURN AREA
         SPACE 3
DWORD    DS    D                        WORK DOUBLEWORD
         SPACE
DCAMRET  DS    CL265                    LOCATE-OBTAIN WORK AREA
         SPACE
DNOTRK   DS    H                        NUMBER OF TRACKS PER CYL
         SPACE
DSW      DS    X
DSCB1P   EQU   X'80'                    DSCB FORMAT 1 PROCESSED
DSCB5P   EQU   X'40'                    DSCB FORMAT 5 PROCESSED
VOL      EQU   X'20'                    VOLUME PROCESSING REQUESTED
VTOCALC  EQU   X'10'                    VTOC ALLOCATED
ATTN     EQU   X'08'                    ATTENTION ISSUED
SYSOALC  EQU   X'04'                    SYSOUT DS ALLOCATED
         SPACE 2
ALINE1   DS    A                        MULTILINE-POINTER
TLINE1   DS    CL4,CL9
TLINE1A  DS    CL44                     DATA SET NAME
         DS    CL25                     RESERVED
         SPACE 3
ALINE2   DS    A                        MULTILINE-POINTER
TLINE2   DS    CL4,CL9
TLINE2A  DS    CL6                      VOLUME SERIAL NUMBER
         DS    CL19
TLINE2B  DS    CL8                      UNIT-TYPE
         DS    CL12
TLINE2C  DS    CL2                      DSORG
         DS    CL22
         SPACE 3
ALINE3   DS    A                        MULTI-LINE POINTER
TLINE3   DS    CL4,CL9
TLINE3A  DS    CL3                      1ST ALLOCATION TYPE
         DS    CL26
TLINE3B  DS    CL4                      2ND ALLOCATION QUANTITY
         DS    CL10
TLINE3C  DS    CL4                      NUMBER OF EXTENTS
         DS    CL22
         SPACE 3
ALINE4   DS    A                        MULTI-LINE POINTER
TLINE4   DS    CL4,CL8
TLINE4A  DS    CL4                      CYLS USED
         DS    CL4
TLINE4B  DS    CL4                      TRKS USED
         DS    CL16
TLINE4C  DS    CL4                      PERCENTAGE USED
         DS    CL34
         SPACE 3
         ORG   ALINE1
ALINE5   DS    A
TLINE5   DS    CL4
TLINE5A  DS    CL44
         DS    CL1
TLINE5B  DS    CL2
         DS    CL2
TLINE5C  DS    CL3
         DS    CL1
TLINE5D  DS    CL4
         DS    CL1
TLINE5E  DS    CL4
         DS    CL1
TLINE5F  DS    CL4
         DS    CL1
TLINE5G  DS    CL4
         DS    CL1
TLINE5H  DS    CL4
         DS    CL1
         SPACE 3
ALINE6   DS    A
TLINE6   DS    CL4,CL9
TLINE6A  DS    CL6
         DS    CL11
TLINE6B  DS    CL8
         DS    CL44
         SPACE 3
ALINE7   DS    A
TLINE7   DS    CL4,CL78
         SPACE 3
ALINE8   DS    A
TLINE8   DS    CL4,CL14
TLINE8A  DS    CL4
         DS    CL5
TLINE8B  DS    CL4
         DS    CL51
         SPACE 3
ALINE9   DS    A
TLINE9   DS    CL4,CL9
TLINE9A  DS    CL8
         DS    CL21
TLINE9B  DS    CL4
         DS    CL10
TLINE9C  DS    CL4
         DS    CL22
         SPACE 5
         ORG   ALINE9
ALINE10  DS    A
TLINE10  DS    CL4,CL17
TLINE10A DS    CL4
         DS    CL4
TLINE10B DS    CL4
         DS    CL8
TLINE10C DS    CL4
         DS    CL4
TLINE10D DS    CL4
         DS    CL8
TLINE10E DS    CL4
         DS    CL4
TLINE10F DS    CL4
         DS    CL9
         SPACE 5
WLINE    DS    CL133                    PRINT LINE
         SPACE 3
WPTPB    DS    5F                       PUTLINE BLOCK
         SPACE
WUPARM   DS    4F                       STAX USER PARM
         SPACE
WVOL     DS    H,CL8                    VOLUME SERIAL
WUNIT    DS    H,CL8                    UNIT TYPE
WFILE    DS    H,CL8                    FILE NAME
WERROR   DS    F                        ERROR INDICATOR
         SPACE
         DS    0F
WVTOCIN  DS    CL(LVTOCIN)
         DS    0F
WSYSOUT  DS    CL(LSYSOUT)              PRINT WORK DCB
         SPACE
WEXLIST  DS    F
         SPACE
         DS    0F
WOPEN    DS    CL(LOPEN)
         DS    0F
WCLOSE   DS    CL(LCLOSE)
         SPACE
         DS    0F
WSTAXL   DS    CL(LSTAXL)               STAX PARAMETERLIST
         SPACE
         DS    0F
JFCB     DS    CL176
         SPACE 3
WIOPL    DS    4F                       INPUT-OUTPUT BLOCK
         DS    0D                       FORCE LENGTH MULTIPLE TO 8
LDYNSTOR EQU   *-DYNSTOR
         EJECT
         IKJCPPL
         SPACE 5
         IKJPPL
         SPACE 5
         IKJIOPL
         SPACE 3
         IKJPTPB
         SPACE 3
         PRINT NOGEN
         DCBD
         PRINT GEN
         EJECT
**********
*
*        -INC DSCB
*
*        DATA SET CONTROL BLOCKS
*
*        THE DATA SET LABEL FOR A DATA SET RESIDING ON A
*        DIRECT ACCESS VOLUME IS CALLED A DATA SET CONTROL
*        BLOCK (DSCB). ONE OR MORE DSCBS ARE USED TO DESCRIBE
*        THE DATA SET. EACH DSCB IS 140 BYTES, CONSISTING
*        OF A 44 BYTE KEY AND A 96 BYTE DATA PORTION.
*
*        THE DSCBS DESCRIBING ALL DATA SETS ON A VOLUME
*        MAKE UP THE VOLUME TABLE OF CONTENTS (VTOC).
*
*        SEPARATE DIAGRAMS AND DESCRIPTION ARE PRESENTED FOR
*        THE FOLLOWING USES OF DSCBS:
*        FORMAT 1 -- IDENTIFIER DSCB
*        FORMAT 2 -- INDEX DSCB
*        FORMAT 3 -- EXTENSION DSCB
*        FORMAT 4 -- VTOC DSCB
*        FORMAT 5 -- FREE SPACE DSCB
*        FORMAT 6 -- SHARED EXTENT DSCB
*
*        IN ADDITION, THERE IS A FORMAT 0 DSCB, THE FREE VTOC
*        RECORD. IT HAS THE SAME FORMAT AS OTHER DSCBS; HOWEVER
*        IT CONTAINS ALL BINARY ZEROS.
*
**********
         EJECT
DSCB     DSECT
DS1DSNAM DS    CL44     DATA SET NAME IN EBCDIC.
DS1FMTID DS    C'1'     FORMAT IDENTIFIER; HEX F1 IDENTIFIES
*                       THIS AS A FORMAT 1 DSCB.
DS1DSSN  DS    CL6      DATA SET SERIAL NUMBER. THIS FIELD
*                       IS IDENTICAL TO THE CONTENTS OF THE VOLUME
*                       SERIAL NUMBER FIELD IN THE VOLUME
*                       LABEL OF THE FIRST OR ONLY VOLUME ON
*                       WHICH THE DATA SET RESIDES.
DS1VOLSQ DS    XL2      VOLUME SEQUENCE NUMBER IN BINARY.
*                       INDICATES THE ORDER OF THIS VOLUME RELATIVE
*                       TO THE FIRST VOLUME ON WHICH THE DATA SET
*                       RESIDES.
DS1CREDT DS    XL3      CREATION DATA IN BINARY, IN THE FORM
*                       YDD. Y - YEAR: 00-99
*                            DD - DAY: 1-366
DS1EXPDT DS    XL3      EXPIRATION DATE IN BINARY, IN THE FORM
*                       YDD (AS ABOVE). INDICATES THE YEAR AND THE DAY
*                       OF THE YEAR THE DATA SET MAY BE PURGED. IF
*                       NEITHER A RETENTION PERIOD NOR AN
*                       EXPIRATION DATE HAS BEEN SPECIFIED, YDD
*                       IS ZERO.
DS1NOEPV DS    XL1      NUMBER OF SEPARATE EXTENTS IN WHICH
*                       THE DATA SET RESIDES ON THIS VOLUME. THIS COUNT
*                       DOES NOT INCLUDE THE EXTENT DESCRIBING
*                       A USER'S LABEL TRACK
DS1NOBDB DS    XL1      NUMBER OF BYTES USED IN THE LAST PDS
*                       DIRECTORY BLOCK. A VALUE OF ZERO INDICATES
*                       THAT THE LAST AVAILABLE BLOCK IS NOT BEING
*                       USED.
         DS    XL1      RESERVED.
DS1SYSCD DS    CL13     SYSTEM CODE. AN EBCDIC CODE THAT
*                       UNIQUELY IDENTIFIES THE OPERATING SYSTEM.
*                       THE FIRST THREE CHARACTERS ARE 'IBM'.
*                       THE REMAINING CHARACTERS ARE THE SYSTEM CODE
*                       ASSIGNED TO THE CREATING SYSTEM.
         DS    XL7      RESERVED.
DS1DSORG DS    XL2      DATA SET ORGANIZATION.
DSORGIS  EQU   X'80'    INDEXED SEQUENTIAL ORGANIZATION.
DSORGPS  EQU   X'40'    PHYSICAL SEQUENTIAL ORGANIZATION.
DSORGDA  EQU   X'20'    DIRECT ORGANIZATION.
DSORGCX  EQU   X'10'    BTAM OR QTAM LINE GROUP.
DSORGCQ  EQU   X'08'    QTAM DIRECT ACCESS MESSAGE QUEUE.
DSORGMQ  EQU   X'04'    QTAM PROBLEM PROGRAM MESSAGE QUEUE.
DSORGPO  EQU   X'02'    PARTITIONED ORGANIZATION.
DSORGU   EQU   X'01'    UNMOVABLE - THE DATA CONTAINS LOCATION
*                       DEPENDENT INFORMATION.
*                       BYTE 2
DSORGGS  EQU   X'80'    GRAPHICS ORGANIZATION.
DSORGTX  EQU   X'40'    TCAM LINE GROUP.
DSORGTQ  EQU   X'20'    TCAM MESSAGE QUEUE.
DSORGAM  EQU   X'08'    VSAM ORGANIZATION.
DSORGTR  EQU   X'04'    TCAM 3705.
DSORGRS  EQU   X'13'    (RESERVED BITS).
*
DS2RECFM DS    XL1      RECORD FORMAT.
RECFMF   EQU   X'80'    FIXED LENGTH RECORD FORMAT.
RECFMV   EQU   X'40'    VARIABLE LENGTH RECORD FORMAT.
RECFMU   EQU   X'C0'    UNDEFINED LENGTH RECORD FORMAT.
RECFMT   EQU   X'20'    TRACK OVERFLOW.
RECFMB   EQU   X'10'    BLOCKED: MAY NOT OCCUR WITH
*                       UNDEFINED (U).
REFMS    EQU   X'08'    FIXED LENGTH RECORD FORMAT:
*                       STANDARD BLOCKS (NO TRUNCATED BLOCKS OR
*                       UNFILLED TRACKS ARE EMBEDDED IN THE DATA
*                       SET).
*                       VARIABLE LENGTH RECORD FORMAT: SPANNED
*                       RECORDS.
RECFMA   EQU   X'04'    ANSI CONTROL CHARACTER
RECFMM   EQU   X'02'    MACHINE CONTROL CHARACTER.
RECFMR   EQU   X'01'    (RESERVED BIT).
*
DS1OPTCD DS    XL1      OPTION CODE - SAME AS DCBOPTCD FIELD
*                       IN DCB.
DS1BLKL  DS    XL2      BLOCK LENGTH FOR FIXED LENGTH
*                       RECORDS OR MAXIMUM BLOCK SIZE FOR
*                       VARIABLE OR UNDEFINED LENGTH RECORDS.
DS1LRECL DS    XL2      FORMAT F RECORDS: RECORD LENGTH,
*                       FORMAT U RECORDS: ZERO.
*                       FORMAT V RECORDS -
*                          UNSPANNED RECORD FORMAT:
*                             MAXIMUM RECORD LENGTH.
*                          SPANNED RECORD FORMAT -
*                             RECORDS UP TO 32,756 BYTES:
*                                MAXIMUM RECORD LENGTH.
*                             RECORDS EXCEEDING 32,756 BYTES:
*                               X'80000'.
DS1KEYL  DS    XL1      KEY LENGTH. THE LENGTH (1-255 BYTES) OF
*                       THE KEY OF THE DATA RECORDS IN THE DATA SET.
*                       A VALUE OF ZERO INDICATES THAT NO KEY
*                       EXISTS.
DS1RKP   DS    XL2      RELATIVE KEY POSITION IN THE DATA BLOCK.
DS1DSIND DS    XL1      DATA SET INDICATOR.
DSINDLV  EQU   X'80'    THIS IS THE LAST VOLUME ON WHICH
*                       THIS DATA SET NORMALLY RESIDES.
DSINDSB  EQU   X'20'    BLOCK LENGTH MUST ALWAYS BE A
*                       MULTIPLE OF 8 BYTES.
DSINDPRW EQU   X'10'    PASSWORD IS REQUIRED TO READ OR WRITE.
DSINDPW  EQU   X'14'    PASSWORD IS REQUIRED TO WRITE BUT
*                       NOT TO READ.
DSINDRES EQU   X'4B'    (RESERVED BITS).
*
DS1SCALO DS    XL4      ALLOCATION PARAMETERS.
*                       TYPE OF REQUEST ISSUED FOR THE INITIAL
*                       ALLOCATION AND TO BE USED FOR SUBSEQUENT
*                       EXTENSIONS.
*                       BYTE 1: ORIGINAL REQUEST WAS:
ALOREL   EQU   X'00'    IN TRACKS RELATIVE TO A SPECIFIC
*                       LOCATION. NO SECONDARY ALLOCATION WILL BE
*                       ALLOWED.
ALOBLK   EQU   X'40'    IN BLOCKS (PHYSICAL RECORDS).
ALOTRK   EQU   X'80'    IN TRACKS.
ALOCYL   EQU   X'C0'    IN CYLINDERS
ALORES   EQU   X'30'    (RESERVED BITS).
ALOCONT  EQU   X'08'    FOR A CONTIGUOUS EXTENT.
ALOMAX   EQU   X'04'    FOR THE MAXIMUM CONTIGUOUS EXTENT
*                       ON THE VOLUME.
ALOMIN   EQU   X'02'    FOR THE FIVE (OR LESS) LARGEST
*                       EXTENTS THAT ARE GREATER THAN
*                       OR EQUAL TO A SPECIFIED MINIMUM.
ALOREC   EQU   X'01'    IN RECORDS, TO BE ROUNDED UP TO A
*                       CYLINDER BOUNDARY.
*
*                       BYTES 2-4:
*                       SECONDARY ALLOCATION QUANTITY.
*                       NUMBER OF BLOCKS, TRACKS, OR CYLINDERS TO BE
*                       REQUESTED AT END OF DATA SET WHEN PROCESSING
*                       A SEQUENTIAL OR PARTITIONED DATA SET.
DS1LSTAR DS    XL3      THE LAST-BLOCK POINTER IDENTIFIES
*                       THE LAST BLOCK WRITTEN IN A SEQUENTIAL OR
*                       PARTITIONED ORGANIZATION DATA SET. IT IS
*                       IN THE FORMAT TTRLL (LL IS DEFINED
*                       UNDER THE NEXT FIELD NAME):
*                       TT - RELATIVE ADDRESS OF TRACK CONTAING
*                       THE LAST BLOCK.
*                       R - BLOCK NUMBER ON THAT TRACK.
DS1TRBAL DS    XL2      LL PORTION OF THE FORMAT GIVEN IN
*                       DS1LSTAR.
*                       LL - NUMBER OF BYTES REMAINING ON TRACK
*                       FOLLOWING THE BLOCK.
*                       NOTE: IF BOTH FIELDS CONTAIN BINARY
*                       ZEROS, THE LAST BLOCK POINTER DOES
*                       NOT APPLY.
         DS    XL2      RESERVED.
DS1EXT1  DS    XL10     EXTENT DESCRIPTION FOR THE FIRST EXTENT.
*                       THIS EXTENT DESCRIPTION IS ALSO USED IN
*                       FORMAT 3 AND 4 DSCBS.
*              BYTE 1   DATA SET EXTENT TYPE INDICATOR.
EXT1NO   EQU   X'00'    FOLLOWING 9 BYTES DO NOT
*                       INDICATE ANY EXTENT.
EXT1LB   EQU   X'01'    THE EXTENT CONTAINS THE DATA BLOCKS
*                       (USER'S BLOCKS), OR IS A PRIME AREA
*                       (FOR IS DATA SETS).
EXT1OV   EQU   X'02'    THE EXTENT IS AN OVERFLOW AREA
*                       (FOR IS DATA SETS ONLY).
EXT1IX   EQU   X'04'    THE EXTENT IS AN INDEX AREA
*                       (FOR IS DATA SETS ONLY).
EXT1UL   EQU   X'40'    THE FIRST EXTENT DESCRIPTION DESCRIBES
*                       THE USER LABEL EXTENT.
EXT1SHR  EQU   X'80'    THE EXTENT DESCRIBED IS SHARING ONE OR
*                       MORE CYLINDERS WITH ONE OR MORE DATA SETS.
EXT1BD   EQU   X'81'    THE EXTENT DESCRIBED BEGINS AND
*                       ENDS ON CYLINDER BOUNDARIES, I.E., THE EXTENT
*                       IS COMPOSED OF ONE OR MORE CYLINDERS.
*              BYTE 2   EXTENT SEQUENCE NUMBER.
*                       UNIQUELY IDENTIFIES EACH SEPARATE EXTENT
*                       ON A GIVEN VOLUME FOR A DATA SET. FOR ALL
*                       ORGANIZATIONS BUT INDEXED SEQUENTIAL
*                       THE FIRST EXTENT OF THE DATA SET ON
*                       EACH VOLUME IS IDENTIFIED WITH ZERO
*                       IN THIS FIELD.
*                       THE FIRST EXTENT ON EACH VOLUME OF AN INDEXED
*                       SEQUENTIAL DATA SET IS IDENTIFIED WITH
*                       A VALUE OF ONE IN THE FIELD.
*                       ADDITIONAL EXTENTS ON THE VOLUME ARE
*                       IDENTIFIED WITH SEQUENTIALLY INCREASING
*                       BINARY VALUES. THIS FIELD IS ALWAYS
*                       ZERO FOR AN EXTENT FIELD POINTING TO
*                       A USER LABEL TRACK.
*              BYTES 3-6  LOWER LIMIT OF THIS EXTENT (CCHH).
*                       CONTAINS THE CYLINDER AND THE TRACK
*                       ADDRESS SPECIFYING THE STARTING POINT OF THIS
*                       EXTENT.
*              BYTES 7-10  UPPER LIMIT OF THIS EXTENT (CCHH).
*                       CONTAINS THE CYLINDER AND TRACK ADDRESS
*                       SPECIFYING THE ENDING POINT OF THIS
*                       EXTENT.
DS1EXT2  DS    XL10     EXTENT DESCRIPTION FOR THE SECOND
*                       EXTENT. SAME FORMAT AS DS1EXT1 FIELD
DS1EXT3  DS    XL10     EXTENT DESCRIPTION FOR THE THIRD
*                       EXTENT. SAME FORMAT AS DS1EXT1 FIELD.
DS1PTRDS DS    XL5      POINTER TO A FORMAT 2 DSCB,
*                       IF DATA SET HAS IS ORGANIZATION, OR POINTER
*                       TO A FORMAT 3 DSCB IS DATA SET
*                       HAS SEQUENTIAL OR DIRECT ORGANIZATION AND
*                       MORE THAN 3 EXTENTS. THIS POINTER HAS
*                       THE FORMAT CCHHR. CONTAINS BINARY
*                       ZEROS IF NO ADDITIONAL DSCB IS POINTED TO.
         EJECT
         ORG   DSCB
         DS   X'02'     HEX CODE 02 - PROVIDES A UNIQUE KEY FIELD.
DS22MIND DS   XL7       ADDRESS OF THE FIRST TRACK OF THE SECOND LEVEL
*                       MASTER INDEX IN THE FORM MBBCCHH.
DS2L2MEN DS   XL5       CCHHR OF THE LAST ACTIVE INDEX ENTRY IN THE
*                       SECOND LEVEL MASTER INDEX.
DS23MIND DS   XL7       ADDRESS OF THE FIRST TRACK OF THE THIRD LEVEL
*                       MASTER INDEX IN THE FORM MBBCCHH.
DS2L3MIN DS   XL5       CCHHR OF THE LAST ACTIVE INDEX ENTRY IN THE
*                       THIRD LEVEL MASTER INDEX.
         DS   XL11      RESERVED
DS2LPDT  DS   XL8       LAST PRIME TRACK ON THE LAST PRIME CYLINDER.
DS2FMTID DS   C'2'      FORMAT IDENTIFIKATION FOR FORMAT 2 DSCB.
*                       (EBCDIC "2").
DS2NOLEV DS   XL1       NUMBER OF INDEX LEVELS. A BINARY NUMBER
*                       INDICATING HOW MANY LEVELS OF INDEX ARE PRESENT
*                       WITH AN INDEXED SEQUENTIAL DATA SET.
DS2DVIND DS   XL1       NUMBER OF TRACKS DETERMINING DEVELOPMENT
*                       OF THE MASTER INDEX
DS21RCYL DS   XL3       HHR OF THE FIRST DATA RECORD ON EACH
*                       CYLINDER
DS2LTCYL DS   XL2       HH OF THE LAST DATA TRACK ON EACH CYLINDER.
DS2CYLOV DS   XL1       NUMBER OF TRACKS OF CYLINDER OVERFLOW AREA
*                       ON EACH CYLINDER.
DS2HIRIN DS   XL1       HIGHEST POSSIBLE R ON A TRACK CONTAINING
*                       HIGH LEVEL INDEX ENTRIES.
DS2HIRPR DS   XL1       HIGHEST POSSIBLE R ON PRIME DATA TRACKS
*                       FOR FORMAT F RECORDS.
DS2HIROV DS   XL1       HIGHEST POSSIBLE R ON OVERFLOW DATA
*                       TRACKS FOR FORMAT F RECORDS
DS2RSHTR DS   XL1       R OF THE LAST DATA RECORD ON A SHARED TRACK.
DS2HIRTI DS   XL1       HIGHEST POSSIBLE R ON AN UNSHARED
*                       TRACK OF THE TRACK INDEX.
DS2HIIOV DS   XL1       FIXED LENGTH RECORD FORMAT:
*                       HIGHEST POSSIBLE R FOR INDEPENDENT OVERFLOW
*                       DATA TRACKS. VARIABLE
*                       LENGTH RECORD FORMAT: UNUSED.
DS2TAGDT DS   XL2       USER SUPPLIED NUMBER OF RECORDS TAGGED
*                       FOR DELETION. THIS FIELD IS MERGED TO AND FROM
*                       THE DCB FOR BISAM, QUISAM SCAN MODE, AND RESUME
*                       LOAD
DS2RORG3 DS   XL3       A COUNT OF THE NUMBER OF READ AND WRITE
*                       ACCESSES, MADE DURING THE LAST USE OF THE DATA
*                       SET TO AN OVERFLOW RECORD THAT IS NOT FIRST
*                       IN A CHAIN OF SUCH RECORDS
DS2NOBYT DS   XL2       NUMBER OF BYTES NEEDED TO HOLD THE
*                       HIGHEST-LEVEL INDEX IN MAIN STORAGE
DS2NOTRK DS   XL1       NUMBER OF TRACKS OCCUPIED
*                       BY THE HIGHEST LEVEL INDEX.
DS2PRCTR DS   XL4       NUMBER OF RECORDS IN THE PRIME DATA AREA.
DS2STIND DS   XL1       STATUS INDICATORS.
STINDRES EQU  X'9C'     (RESEVED BITS)
STINDSQC EQU  X'40'     KEY SEQUENCE CHECKING IS TO BE PERFORMED.
STINDLC  EQU  X'20'     AN INITIAL LOAD HAS BEEN COMPLETED.
STINDLBF EQU  X'02'     LAST BLOCK FULL.
STINDLTF EQU  X'01'     LAST TRACK FULL.
*
DS2CYLAD DS   XL7       ADDRESS OF THE FIRST TRACK OF THE CYLINDER
*                       INDEX IN THE FORM MBBCCHH.
DS2ADLIN DS   XL7       ADDRESS OF THE FIRST TRACK OF THE LOWEST LEVEL
*                       MASTER INDEX IN THE FORM MBBCCHH.
DS2ADHIN DS   XL7       ADDRESS OF THE FIRST TRACK OF THE HIGHEST
*                       LEVEL INDEX IN THE FORM MBBCCHH.
DS2LPRAD DS   XL8       ADDRESS OF THE LAST RECORD IN THE PRIME
*                       DATA AREA, IN THE FORM MBBCCHHR.
DS2LTRAD DS   XL5       CCHHR OF THE LAST NORMAL ENTRY IN THE TRACK
*                       INDEX ON THE CYLINDER CONTAINING THE LAST
*                       PRIME DATA RECORD OF THE DATA SET.
DS2LCYAD DS   XL5       CCHHR OF THE LAST INDEX ENTRY IN THE
*                       CYLINDER INDEX.
DS2LMSAD DS   XL5       CCHHR OF THE LAST INDEX ENTRY IN THE
*                       MASTER INDEX.
DS2LOVAD DS   XL8       ADDRESS OF THE LAST RECORD WRITTEN IN THE
*                       CURRENT INDEPENDENT OVERFLOW AREA, IN THE
*                       FORM MBBCCHHR.
DS2BYOVL DS   XL2       NUMBER OF BYTES REMAINING ON THE CURRENT
*                       INDEPENDENT OVERFLOW TRACK.
DS2RORG2 DS   XL2       NUMBER OF TRACKS REMAINING IN THE
*                       INDEPENDENT OVERFLOW AREA.
DS2OVRCT DS   XL2       NUMBER OF RECORDS IN THE OVERFLOW AREA.
DS2RORG1 DS   XL2       NUMBER OF CYLINDER OVERFLOW AREAS
*                       THAT ARE FULL.
DS2NIRT  DS   XL3       HHR OF THE DUMMY TRACK INDEX ENTRY.
DS2PTRDS DS   XL5       POINTER TO FORMAT 3 DSCB IF A
*                       CONTINUATION IS NEEDED TO DESCRIBE THIS
*                       DATA SET. THIS POINTER HAS THE FORM:
*                       CCHHR.
         EJECT
         ORG   DSCB
         DS    X'03030303' A HEXADECIMAL 03 IN EACH BYTE.
DS3EXTNT DS    XL40     EXTENT (IN KEY) - FOUR TEN-BYTE FIELDS
*                       IDENTICAL TO THE DS1EXT1 FIELD IN THE FORMAT 1
*                       DSCB.
DS3FMTID DS   C'3'      FORMAT IDENTIFIER - (EBCDIC '3').
DS3ADEXT DS   XL90      ADDITIONAL EXTENT - NINE TEN-BYTE FIELDS
*                       IDENTICAL TO THE DS1EXT1 FIELD IN THE FORMAT 1
*                       DSCB.
         DS   XL5       RESERVED - CONTAINS BINARY ZEROS.
         EJECT
         ORG   DSCB
         DS    XL44'04' HEX 04 IN EACH BYTE
DS4FMTID DS    C'4'     FORMAT IDENTIFIER - HEX F4.
DS4HPCHR DS    XL5      HIGHEST ADDRESS PREVIOUSLY USED FOR
*                       A FORMAT 1 DSCB. THE ADDRESS IS IN THE
*                       FORM CCHHR.
DS4DSREC DS   XL2       NUMBER OF AVAILABLE FORMAT 0 DSCB'S IN
*                       THE VTOC.
DS4HCCHH DS   XL4       CCHH OF NEXT ALTERNATE TRACK AVAILABLE.
DS4NOATK DS   XL2       NUMBER OF ALTERNATE TRACKS REMAINING.
DS4VTOCI DS   XL1       VTOC INDICATORS.
VTOCINO5 EQU  X'80'     EITHER NO FORMAT 5 DSCBS EXIST OR THEY
*                       DO NOT REFLECT THE TRUE STATUS OF THE VOLUME.
VTOCIY56 EQU  X'08'     ACCURATE FORMAT 5 AND 6 DSCBS NOW
*                       EXIST AND BIT 0 HAS BEEN TURNED OFF. THIS
*                       VOLUME MAY CONTAIN DATA SETS PRODUCED BY
*                       IBM/DOS . IBM/OS ACCESS METHODS MAY NOT BE
*                       ABLE TO PROCESS THESE DATA SETS.
VTOCIERR EQU  X'04'     A DADSM FUNCTION HAS BEEN PREMATURELY
*                       TERMINATED. POSSIBLE VTOC ERRORS EXIST.
VTOCIRES EQU  X'73'     (RESERVED BITS).
*
DS4NOEXT DS   XL1'01'   HEXADECIMAL CONSTANT 01 TO INDICATE
*                       THE VTOC IS ONE EXTENT.
         DS   XL2       RESERVED
*                       DEVICE CONSTANTS (DS4DEVXX).
*                       THE FOLLOWING FIELDS DESCRIBE THE DEVICE
*                       ON WHICH THIS VOLUME WAS MOUNTED WHEN THE
*                       VTOC WAS CREATED
DS4DEVSZ DS   XL4       DEVICE SIZE.
*                       BYTES 1-2 NUMBER OF LOGICAL CYLINDERS.
*                       A LOGICAL CYLINDER IS THE SMALLEST
*                       COLLECTION OF TWO OR MORE TRACKS THAT CAN
*                       BE PROCESSED BY A SET FILE MASK CCW (HEX 1F).
*                       BYTES 3-4 NUMBER OF TRACKS PER LOGICAL CYLINDER
DS4DEVTK DS   XL2       DEVICE TRACK LENGTH.
*                       NUMBER OF AVAILABLE BYTES ON A TRACK
*                       EXCLUSIVE OF HOME ADDRESS AND RECORD
*                       ZERO.
DS4DEVOV DS   0XL2      OVERHEAD BYTES FOR ANY KEYED BLOCK
*                       ON THE 2305. IF BIT 4 OF THE DEVICE INDICATORS
*                       FIELD (BIT 71.4 OF DSCB4) IS SET TO ONE,
*                       THIS FIELD IS USED AS A SINGLE TWO BYTE
*                       FIELD CONTAINING A BINARY COUNT
*                       OF THE NUMBER OF BYTES (OVERHEAD BYTES)
*                       OCCUPIED BY THE COUNT FIELD, GAPS, AND CHECK
*                       BYTE OF A KEYED RECORD
*                       IF BIT 71.4 IS ZERO, THE FIELD CONSISTS OF
*                       THE FOLLOWING SUBFIELDS.
DS4DEVI  DS   XL1       CONTAINS A COUNT OF THE NUMBER OF BYTES
*                       (OVERHEAD BYTES) OCCUPIED BY THE COUNT FIELD,
*                       GAPS, AND CHECK BITS OF A KEYED RECORD THAT IS
*                       NOT THE LAST RECORD ON A TRACK.
DS4DEVL  DS   XL1       CONTAINS A COUNT OF THE NUMBER OF BYTES
*                       (OVERHEAD BYTES) OCCUPIED BY THE COUNT FIELD,
*                       GAPS, AND CHECK BITS OF A RECORD THAT IS
*                       THE LAST RECORD ON A TRACK.
DS4DEVK  DS   XL1       THE NUMBER OF OVERHEAD BYTES TO BE
*                       SUBTRACTED FROM DS4DEVI, DS4DEVL, OR DS4DEVOV
*                       IF THE BLOCK HAS NO KEY FIELD.
DS4DEVFG DS   XL1       DEVICE INDICATORS.
DEVFGRES EQU  X'F0'     (RESERVED BITS).
DEVFG2B  EQU  X'08'     THE KEYED RECORD OVERHEAD FIELD
*                       (DS4DEVI) IS USED AS A TWO BYTE FIELD
*                       TO SPECIFY THE OVERHEAD REQUIRED BY
*                       A KEYED RECORD AS IN THE CASE OF THE 2305.
DEVFGTOL EQU  X'01'     A TOLERANCE FACTOR MUST BE APPLIED
*                       TO ALL BUT THE LAST RECORD ON A TRACK.
DS4DEVTL DS   XL2       DEVICE TOLERANCE.
*                       VALUE WHICH WHEN DIVIDED BY 512 IS USED
*                       TO DETERMINE EFFECTIVE LENGTH OF A BLOCK
*                       ON A TRACK.
DS4DEVDT DS   XL1       NUMBER OF FULL DSCBS THAT CAN BE CONTAINED
*                       ON ONE TRACK (44 BYTE KEY PLUS 96 BYTE
*                       DATA LENGTH).
DS4DEVDB DS   XL1       NUMBER OF FULL PDS DIRECTORY BLOCKS
*                       THAT CAN BE CONTAINED ON ONE TRACK
*                       (8 BYTE KEY PLUS 256 BYTE DATA LENGTH).
         DS   XL24      RESERVED
DS4F6PTR DS   XL5       POINTER TO THE FIRST FORMAT 6 DSCB.
*                       THIS POINTER HAS THE FORM CCHHR.
*                       IT CONTAINS BINARY ZEROS WHEN NOT IN USE.
DS4VTOCE DS   XL10      VTOC EXTENT.
*                       CONTENTS AND MEANING ARE THE SAME AS DS1EXT1
*                       IN THE FORMAT 1 DSCB
         DS   XL25      RESERVED
         EJECT
         ORG   DSCB
DS5KEYID DS    XL4'05'  KEY IDENTIFIKATION - HEX 05 IN EACH BYTE
DS5AVEXT DS   XL5       AVAILABLE EXTENT.
*                       DESCRIBES AN EXTENT OF SPACE AVAILABLE FOR
*                       ALLOCATION TO A DATA SET.
*                       BYTES 1-2 RELATIVE TRACK ADDRESS, IN BINARY
*                       OF THE FIRST IN THE EXTENT. THE RELATIVE
*                       TRACK ADDRESS IS RELATIVE TO THE FIRST TRACK ON
*                       THE VOLUME, WHICH HAS A RELATIVE TRACK
*                       ADDRESS OF 0.
*                       BYTES 3-4 THE NUMBER, IN BINARY, OF ENTIRELY
*                       UNUSED CYLINDERS IN THIS EXTENT.
*                       BYTE 5 THE NUMBER, IN BINARY, OF UNUSED
*                       TRACKS IN THE EXTENT
*                       IN THE ADDITION TO THOSE CONTAINED IN THE
*                       UNUSED CYLINDERS
DS5EXTAV DS   XL35      AVAILABLE EXTENTS.
*                       7 FIVE-BYTE FIELDS IDENTICAL IN FORMAT TO
*                       THE DS5AVEXT FIELD. EACH SET, IF IT IS USED,
*                       DESCRIBES A DIFFERENT EXTENT.
*                       THE EXTENTS ARE IN ASCENDING ORDER OF
*                       THEIR FIRST TRACK ADDRESSES.
DS5FMTID DS   C'5'      FORMAT IDENTIFIER - HEX F5
DS5MAVET DS   XL90      AVAILABLE EXTENTS.
*                       18 FIVE-BYTE FIELDS IDENTICAL IN
*                       FORMAT TO THE DS5AVEXT FIELD.
DS5PTRDS DS   XL5       THE CCHHR ADDRESS OF THE NEXT
*                       FORMAT 5 DSCB IF IT EXISTS. IF NONE
*                       EXISTS, THIS FIELD CONTAINS BINARY
*                       ZEROS.
         EJECT
         ORG   DSCB
DS6KEYID DS    XL4'06'  KEY IDENTIFIER - HEX 06 IN EACH BYTE.
DS6AVEXT DS    XL5      EXTENT OF SPACE (ONE OR MORE
*                       CONTIGUOUS CYLINDERS) THAT IS BEING
*                       SHARED BY ONE OR MORE DATA SETS.
*                       BYTE 1-2 RELATIVE TRACK ADDRESS OF THE FIRST
*                       CYLINER.
*                       BYTE 3-4 NUMBER OF FULL CYLINDERS BEING SHARED.
*                       BYTE 5 NUMBER OF DATA SETS SHARING THE
*                       EXTENT.
DS6EXTAV DS    XL35     SHARED EXTENTS.
*                       7 FIVE-BYTE FIELDS IDENTICAL IN FORMAT
*                       TO DS6AVEXT. THE FIELDS ARE IN RELATIVE
*                       TRACK ADDRESS SEQUENCE.
DS6FMTID DS    C'6'     FORMAT IDENTIFIER - HEX F6
DS6MAVET DS    XL90     SHARED EXTENTS.
*                       18 FIVE-BYTE FIELDS IDENTICAL IN FORMAT
*                       TO DS6AVEXT.
DS6PTRDS DS    XL5      POINTER TO NEXT FORMAT 6
*                       DSCB. THIS POINTER HAS THE FORM
*                       CCHHR
         TITLE 'MESSAGE CONSTANT CSECT'
MSGCSECT CSECT
         PRINT NOGEN
         IKJTSMSG ('LV100E PARS SERVICE ERROR RC=',),1100
         SPACE 2
         IKJTSMSG ('LV102E DATASET IS NOT IN CATALOG'),1102
         SPACE 2
         IKJTSMSG ('LV103E DATASET IS NOT ON DASD'),1103
         SPACE 2
         IKJTSMSG ('LV101E VOLUME NOT MOUNTED'),1101
         SPACE 2
         IKJTSMSG ('LV104E OBTAIN ERROR RC=',),1104
         SPACE 2
         IKJTSMSG ('LV106E PUTLINE ERROR RC=',),1106
         SPACE 2
         IKJTSMSG ('LV108E VTOC ALLOCATION OR OPEN ERROR'),1108
         SPACE 2
         IKJTSMSG
         PRINT GEN
         EJECT
LISTVTOC CSECT                          RESUME OLD CSECT
         PRINT NOGEN
PARS     IKJPARM
KEYWD1   IKJKEYWD
         IKJNAME 'DA',SUBFLD=SUBFLD1
KEYWD2   IKJKEYWD
         IKJNAME 'VOL',SUBFLD=SUBFLD2
KEYWD3   IKJKEYWD
         IKJNAME 'SYSOUT',SUBFLD=SUBFLD3
SUBFLD1  IKJSUBF
POSIT1   IKJPOSIT DSNAME,USID,PROMPT='DATA SET NAME',                  *
               HELP='NAME OF DATASET YOU WANT VTOC INFORMATION FROM'
SUBFLD2  IKJSUBF
POSIT2   IKJPOSIT DSNAME,VOLSER,PROMPT='VOLUME SERIAL',                *
               HELP='SERIAL NUMBER FROM WHICH YOU WANT VTOC LISTING'
SUBFLD3  IKJSUBF
IDENT3   IKJIDENT 'CLASS',MAXLNTH=1,FIRST=ALPHA,                       *
               PROMPT='SYSOUT CLASS',                                  *
               HELP='VALID SYSOUT-CLASS WHERE LIST SHOULD BE PRINTED'
         IKJENDP
         PRINT GEN
         END
 PUNCH ' ALIAS LV '
 PUNCH ' NAME LISTVTOC(R) '
         END
