 /*  PS200 GRAPHISCHE DARSTELLUNG DER TSO-ANTWORTZEITEN              */
        /* DATA SET PS200      AT LEVEL 001 AS OF 05/11/81    */
 /*
 /*******************************************************************/
 /*                                                                 */
 /*    PS200         GRAPHISCHE DARSTELLUNG VON TSO-ANTWORTZEITEN   */
 /*                                                                 */
 /*                  JACKOBS       PLANUNG                          */
 /*   11.05.1981     JACKOBS       PROGRAMMIERUNG                   */
 /*                                                                 */
 /*                                                                 */
 /*******************************************************************/
 /*                                                                 */
 /*   1  PROGRAMMNAME      PS200                                    */
 /*                                                                 */
 /*                                                                 */
 /*   2  KURZBESCHREIBUNG DER PROGRAMMTAETIGKEIT                    */
 /*                                                                 */
 /*      PS200 STELLT TSO-ANTWORTZEITEN  IN FORM VON                */
 /*      BALKEN-DIAGRAMMEN GRAPHISCH DAR.                           */
 /*                                                                 */
 /*                                                                 */
 /*   3  DATEIEN           KEINE                                    */
 /*                                                                 */
 /*      3.1  EINGABE-DATEIEN                                       */
 /*                                                                 */
 /*           PS200E1    RECFM:    VB                               */
 /*                      LRECL:    133                              */
 /*                      BLKSIZE:  OHNE DCB-ANGABE                  */
 /*                                                                 */
 /*           PS200A1    RECFM:    FB                               */
 /*                      LRECL:    131                              */
 /*                      BLKSIZE:  6026                             */
 /*                      SYSOUT-DATASET                             */
 /*                                                                 */
 /*           SYSPRINT   SYSOUT-DATASET                             */
 /*                                                                 */
 /*                                                                 */
 /*   4  PARAMETERBESCHREIBUNG, RETURNCODES                         */
 /*                                                                 */
 /*      4.1  UMWANDLUNGS-PARAMETER      UEBLICHE PARAMETER         */
 /*                                                                 */
 /*      4.2  PROGRAMM-PARAMETER         KEINE                      */
 /*                                                                 */
 /*      4.3  RETURN-CODES               KEINE                      */
 /*                                                                 */
 /*                                                                 */
 /*   5  ALLGEMEINES                                                */
 /*                                                                 */
 /*      PROGRAMMIERSPRACHE    PL/1                                 */
 /*      PROGRAMMTYP           HAUPTPROGRAMM                        */
 /*      SONSTIGE HINWEISE     ---                                  */
 /*                                                                 */
 /*                                                                 */
 /*   6  FEHLERMELDUNGEN UND ABEND-CODES      KEINE                 */
 /*                                                                 */
 /*                                                                 */
 /*   7  UNTERPROGRAMME UND MACROS            KEINE                 */
 /*                                                                 */
 /*                                                                 */
 /*   8  BESCHREIBUNG DER ARBEITSWEISE                              */
 /*                                                                 */
 /*      DER OUTPUT EINER RMF-POST-PROCESSOR AUSWERTUNG WIRD        */
 /*      GELESEN, DIE ANTWORTZEITEN WERDEN JE 10-MINUTEN-INTERVALL  */
 /*      IN EINER TABELLE GESPEICHERT.                              */
 /*                                                                 */
 /*      BEI DATUMS-WECHSEL WIRD DIE ERSTELLTE TABELLE AUSGEGEBEN   */
 /*      UND NEU GEFUELLT.                                          */
 /*                                                                 */
 /*                                                                 */
 /*   9  JCL FUER  UMWANDLUNG/LINK                                  */
 /*                                                                 */
 /*  //OCL       EXEC OCL,REGION=2048K,                             */
 /*  //         PARM='ND,SZ(MAX),NIS,MAP,OPT(TIME),NSTG,X(S),M'     */
 /*  //SYSIN DD *  PS200                                            */
 /*  //*----------------------------------------------------------- */
 /*  //LKED.SYSLMOD DD DISP=SHR,DSN=............(PSYS200)           */
 /*  //LKED.SYSLIB  DD DISP=SHR,DSN=SYS1.PLIBASE                    */
 /*  //*----------------------------------------------------------- */
 /*                                                                 */
 /*                                                                 */
 /*  10  JCL FUER  ANWENDUNGS-BEISPIEL                              */
 /*                                                                 */
 /*  /*JOBPARM LINECT=72                                            */
 /*  //*----------------------------------------------------------- */
 /*  //STEP020  EXEC AMS,COND=EVEN                                  */
 /*      DELETE RMF.TSO.TSOTIME                                     */
 /*    SET LASTCC = 0                                               */
 /*    SET MAXCC  = 0                                               */
 /*  //*----------------------------------------------------------- */
 /*  //RMFPOST1 EXEC PGM=ERBRMFPP,REGION=3072K                      */
 /*  //MFPINPUT DD DISP=(OLD,PASS),DSN=...ACCT-DATEN                */
 /*  //PPEXT001 DD DISP=(,CATLG),DSN=RMF.TSO.TSOTIME,               */
 /*  //         UNIT=SYSDA,DCB=(RECFM=VBA,LRECL=137,BLKSIZE=6165),  */
 /*  //         SPACE=(TRK,(15,2),RLSE)                             */
 /*  //SYSIN    DD *                       CONTROL-CARDS            */
 /*      DATE(81001,82365)                                          */
 /*      SYSID(S...)                                                */
 /*      NOSUMMARY                                                  */
 /*      SYSOUT(A)                                                  */
 /*      ETOD(0700,1859)                                            */
 /*      EXCEPT( RTIME1   (RTIME(0021),GE,0.000) )                  */
 /*  //**EXCEPT( RTIME1   (RTIME(0021),GE,0.000) )                  */
 /*  //**        AUSWERTUNG FUER TSO-PGN (002)  PERIODE  1          */
 /*  //**---------------------------------------------------------- */
 /*  //PSYS201  EXEC PGM=PSYS200,                                   */
 /*  //         PARM='NOREPORT',COND=(0,NE),REGION=512K             */
 /*  //STEPLIB  DD DISP=SHR,DSN=............                        */
 /*  //SYSPRINT DD SYSOUT=*                                         */
 /*  //PS200A1  DD SYSOUT=*,DCB=RECFM=FBA   ,COPIES=3               */
 /*  //PS200E1  DD DISP=(OLD,DELETE,KEEP),DSN=RMF.TSO.TSOTIME       */
 /*  //PLIDUMP  DD SYSOUT=*                                         */
 /*  //*----------------------------------------------------------- */
 /*                                                                 */
 /*******************************************************************/
 */                                                               */

 PS200:    PROC OPTIONS(MAIN) REORDER;
 /********************************************************************/
 DCL     1 A          STATIC,        /***  AUSGABESATZ        ***/
           2 ASA      CHAR(1)        INIT(' '),
           2 ITIME    PIC'ZZZ9.99'   INIT(0),
           2 AST      CHAR(02)       INIT('  '),
           2 II       CHAR(01)       INIT('I'),
           2 LINE     CHAR(120)      INIT('');
 /********************************************************************/
 DCL     1 B          STATIC,        /***  AUSGABESATZ        ***/
           2 ASA      CHAR(1)        INIT(' '),
           2 BLK01    CHAR(10)       INIT(''),
           2 LINE     CHAR(120)      INIT((120)'-');
 /********************************************************************/
 DCL     1 C          STATIC,        /***  AUSGABESATZ        ***/
           2 ASA      CHAR(1)        INIT(' '),
           2 BLK01    CHAR(10)       INIT(''),
           2 LINE     CHAR(120)      INIT((11)'I    .    ');
 /********************************************************************/
 DCL     1 D          STATIC,        /***  AUSGABESATZ        ***/
           2 ASA      CHAR(1)        INIT('0'),
           2 TEXT1    CHAR(11)       INIT(' SEKUNDEN 0'),
           2 RT(10)   PIC'(9)Z9'     INIT((10)0),
           2 RT11     CHAR(19)       INIT('  GROESSER 10');
 /********************************************************************/
 DCL     1 F          STATIC,        /***  AUSGABESATZ        ***/
           2 ASA      CHAR(1)        INIT('0'),
           2 BLK01    CHAR(10)       INIT(''),
           2 TEXT01   CHAR(45)       INIT(
             'TSO - ANTWORTZEITEN - STATISTIK  FUER SYSTEM'),
           2 SYSTEM   CHAR(4)        INIT(''),
           2 TEXT02   CHAR(8)        INIT('   VOM  '),
           2 DATE     PIC'99.99.99' INIT(0),
           2 BLK02    CHAR(30)
                      INIT('     ---PS200---          '),
           2 BLK03    CHAR(18)       INIT(''),
           2 BLK04    CHAR(07)       INIT('');
 /********************************************************************/
 /***  EINGABESATZ FUER RMF-OUTPUT                                 ***/
 /********************************************************************/
 DCL     1 G01        BASED(POINTVB),
           2 BLK01    CHAR(38),
           2 TEXT1    CHAR(09),       /* SYSTEM ID                   */
           2 BLK02    CHAR(01),
           2 SYSTEM   CHAR(4),        /* = SYSTEM ID                 */
           2 BLK03    CHAR(13),
           2 TEXT2    CHAR(6),        /* START  BZW. END             */
           2 DATE     PIC'99.99.99',  /* =START BZW. END             */
           2 BLK04    CHAR(54);
 /********************************************************************/
 /***  EINGABESATZ FUER RMF-OUTPUT                                 ***/
 /********************************************************************/
 DCL     1 G02        BASED(POINTVB),
           2 BLK01    CHAR(1),
           2 TEXT1    CHAR(6),        /* RTIME-IDENTIFIER            */
           2 BLK02    CHAR(4),
           2 DATEMM   PIC'Z9',        /* INTERVAL DATE  MM           */
           2 DATEDD   PIC'B99',       /* INTERVAL DATE    /DD        */
           2 BLK03    CHAR(01),
           2 INTV     PIC'99.99',     /* INTERVAL START TIME         */
           2 BLK04    CHAR(04),
           2 INTVL    PIC'99.99',     /* INTERVAL LENGTH             */
           2 BLK05    CHAR(15),
           2 RTIME    PIC'ZZZZ.999',  /* RESPONSE TIME SSSS.TTT      */
           2 BLK06    CHAR(79);
 /********************************************************************/
 DCL     1 H    DEF   DATUM,          /* DATUM DREHEN                */
           2 DATEDD   PIC'99',
           2 DATEMM   PIC'99',
           2 DATEYY   PIC'99';
 /********************************************************************/
 DCL     1 S          STATIC,        /***  AUSGABESATZ DUMMY  ***/
           2 ASA      CHAR(1)        INIT('1'),
           2 BLK01    CHAR(130)      INIT('');
 /********************************************************************/
 /*      ALLGEMEINE DECLARATIONEN                                    */
 /********************************************************************/
 DCL     ADDR         BUILTIN;
 DCL     DATUM        PIC'(6)9'      STATIC   INIT(0);
 DCL     DEC          DEC FIXED(7,3) STATIC   INIT(0);
 DCL     DECC         DEC FIXED(5)   STATIC   INIT(0);
 DCL     G02DATE      DEC FIXED(7)   STATIC   INIT(0);
 DCL     I            BIN FIXED(31,5) STATIC  INIT(0);
 DCL     J            BIN FIXED(31,5) STATIC  INIT(0);
 DCL     K            BIN FIXED(31,5) STATIC  INIT(0);
 DCL     MOD          BUILTIN;
 DCL     POINTVB      STATIC         POINTER;
 DCL     RTBLANK      CHAR(11)       DEF      A.LINE      POS(109);
 DCL     RTLINE       PIC'////YYZ99V.9' DEF   A.LINE      POS(109);
 DCL     SW(5)        CHAR(1)        STATIC   INIT((5)(1)'N');
                                     /* 1  =  SYSTEM ID GELESEN   */
 DCL     TAB(700:1900)
                      DEC FIXED(7,3) STATIC   INIT((1201)0);
 DCL     TAST(700:1900)
                      CHAR(1)        STATIC   INIT((1201)(1)' ');
 DCL     TLINE(1:120) CHAR(1)        DEF      A.LINE;
 DCL     VB           CHAR(133)      STATIC   INIT('');
 DCL     Z(5)         BIN FIXED(15)  STATIC   INIT((5)0);
 /********************************************************************/
 DCL     PS200E1 FILE RECORD INPUT
         ENV(VB  RECSIZE(133)                 );
 DCL     PS200A1 FILE RECORD OUTPUT
         ENV(FB  RECSIZE(131)  BLKSIZE(6026) CTLASA );
 DCL     SYSPRINT  EXT     FILE   PRINT;
 /********************************************************************/
         ON ERROR  BEGIN;
         PUT DATA(I,J,K,Z(1),VB) ;
         CLOSE FILE(SYSPRINT);
         END;
 /********************************************************************/
         POINTVB     = ADDR(VB);
         ON ENDFILE(PS200E1) BEGIN;
         CALL U100;
         CLOSE FILE(PS200E1);
         CLOSE FILE(PS200A1);
         GOTO P999;
         END;
         OPEN  FILE(PS200E1);
         OPEN  FILE(PS200A1);
         TAB(*)      = '';
 P100:
         READ FILE(PS200E1) INTO(VB);
         Z(1)        = Z(1) + 1;
         IF SW(01)   = 'N' THEN DO;
         IF G01.TEXT1 ^= 'SYSTEM ID' THEN GOTO P100;
         ELSE DO;
         F.SYSTEM = G01.SYSTEM;      /* ERMITTLUNG DES SYS-ID S*/
         SW(01)      = 'Y';
         END;
         END;

         IF G02.TEXT1  ^= 'RTIME1' THEN GOTO P100;
         IF DATUM       = 0 THEN
         DO;
         H.DATEDD       = G02.DATEDD;
         H.DATEMM       = G02.DATEMM;
         H.DATEYY       = 81;
         GOTO P010;
         END;
         IF H.DATEDD    = G02.DATEDD
         &  H.DATEMM    = G02.DATEMM
         THEN GOTO P010;
         CALL U100;                         /* ANTWORTZEITEN-TABELLE */
                                            /* AUSGEBEN              */
 P010:
         I             = G02.INTV;
         J             = G02.RTIME;
         TAB(I)        = J / 100 + 0.5;
         DECC          = G02.INTVL;
         IF DECC       <   940  ×
            DECC       > 1020  THEN
         TAST(I)       = 'Y';
         GOTO P100;
 /********************************************************************/
 U100:   PROC REORDER;
         WRITE FILE(PS200A1) FROM(S);       /* SEITENVORSCHUB        */
         F.DATE       = DATUM;
         WRITE FILE(PS200A1) FROM(F);       /* UEBERSCHRIFTSZEILE    */
         DO I = 1 TO 10;
         D.RT(I)      = I;
         END;
         WRITE FILE(PS200A1) FROM(D);
         C.ASA        = '0';
         WRITE FILE(PS200A1) FROM(C);
         B.ASA        = ' ';
         WRITE FILE(PS200A1) FROM(B);
         DO I         = 700 TO 1900  BY 10;
         IF TAB(I)    > 0 THEN DO
         A.LINE       = '';
         A.ITIME      = I;
         IF TAST(I)   = 'Y'  THEN
         A.AST        = '**';
         ELSE A.AST   = '  ';
         J            = TAB(I) ;
         IF J         > 120 THEN DO;
         DEC          = J;
         RTLINE       = DEC;
         J            = 108;
         END;
         ELSE RTBLANK = '           ';
         DO K         = 1 TO J WHILE (J < 120);
         TLINE(K)     = '*';
         END;
         WRITE FILE(PS200A1) FROM(A);
         IF MOD(I,100)= 50 THEN
         I            = I + 40;
         END;
         END;
         WRITE FILE(PS200A1) FROM(B);
         C.ASA        = ' ';
         WRITE FILE(PS200A1) FROM(C);
         WRITE FILE(PS200A1) FROM(D);
         WRITE FILE(PS200A1) FROM(F);
         H.DATEDD     = G02.DATEDD;
         H.DATEMM     = G02.DATEMM;
         H.DATEYY     = 81;
         END  U100;
 /********************************************************************/
 P999:
         END PS200;
