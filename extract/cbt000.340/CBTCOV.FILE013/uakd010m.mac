./ ADD NAME=ZSTRT
         MACRO
&NAME    ZSTRT &BASIS=(12),&REGS=1,&PARM=
.**********************************************************************
.*  ZSTRT - DEFINIEREN UNTERPROGRAMM-ANFANG                           *
.*  ---------------------------------------                           *
.*                                                                    *
.*  MAKROGRUPPE:         ZSTRT, ZEND, ZCALL                           *
.*  ANWENDUNG:                                                        *
.*  DIESER MAKRO IST DIE ERSTE ANWEISUNG EINES                        *
.*  UNTERPROGRAMMS.                                                   *
.*  FUNKTION:                                                         *
.*  SICHERSTELLEN REGISTER                                            *
.*  ZUWEISEN BASISREGISTER                                            *
.*  GENERIEREN SICHERSTELLUNGSBEREICH                                 *
.*  LADEN ADRESSE SICHERSTELLLUNGSBEREICH NACH REG. 13                *
.*  SICHERN PARAMETER-ADRESSE                                         *
.*  GENERIEREN KONSTANTE MIT UP-NAMEN (FUER DUMP)                     *
.*  GLEICHSETZEN REGISTER-NUMMER MIT R0 BIS R15                       *
.*  FORMAT:                                                           *
.*  NAME  ZSTRT  <BASIS=REG><,REGS=NR><,PARM=ADR>                     *
.*                                                                    *
.*       REG   = BASIS-REGISTER-NUMMER                                *
.*               SCHREIBWEISE: ENTWEDER (REG-NR)   Z.B. (3)           *
.*                             ODER     RREG-NR    Z.B. R3            *
.*              DAMIT KANN EIN, ODER EIN ERSTES BASIS-REGISTER        *
.*              ZUGEWIESEN WERDEN.                                    *
.*              WIRD DIESER OPERAND WEGGELASSEN WIRD REGISTER 12      *
.*              ALS BASISREGISTER VERWENDET.                          *
.*      NR    = ANZAHL DER ZU VERWENDETEN BASISREGISTER               *
.*              WIRD DIESER OPERAND WEGGELASSEN, WIRD 1 ANGENOMMEN.   *
.*      ADR   = A) SYMB.ADRESSE  -  ADRESSE EINES FESTWORTES, IN      *
.*                                  DAS REG.1 SICHERGESTELLT WIRD.    *
.*              B) (REG-N) ODER                                       *
.*                 RREG-N           DAS REG.1 WIRD IN DAS ANGEGEBENE  *
.*                                  REGISTER UMGELADEN.               *
.*              DAMIT KANN DIE PARAMETER-ADRESSE SICHERGESTELLT       *
.*              WERDEN. DAS REG.1 WIRD NICHT VERAENDERT.              *
.*  INTERNE MAKROS:      KEINE                                        *
.*  GLOBALPARAMETER:     &BG127 (BINAER/GLOBAL - DIENT DER            *
.*                               ERKENNUNG 'ERSTER MAKROAUFRUF')      *
.*                       &CG0  (NUR BEI SIEMENS)                      *
.*  AUTHOR:              SCHUETZ/FISCHER                              *
.*  GUELTIG:             AB JULI 1972                                 *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.*  COPYRIGHT BY AKDB - ANSTALT F. KOMMUNALE DATENVERARB. IN BAYERN   *
.*                      8 MUENCHEN 83, ALBERT SCHWEITZER STR. 62      *
.*                      MUENCHEN, DEN 1.7.1973
.*                                                                    *
.**********************************************************************
         GBLB  &BG127
         LCLC  &CG0
         LCLA  &AL0,&AL1,&AL2,&AL3,&AL4
         AIF   ('&NAME' NE '').A        MODULMAME VORHANDEN?
         MNOTE 'MODUL-NAME FEHLT'
         MEXIT
.A       ANOP
&NAME    CSECT
         USING *,15
         STM   14,12,12(13)             SICHERN REGS IN RUFPROG
         LA    14,*+8*4                 LADEN A(SAVEAREA)
         ST    14,8(13)                 SPEICHERN A(SAVEAREA) IN RUFPRO
         ST    13,4(14)                 SICHERN REG13 VON RUFPROG
         LR    13,14                    LADEN A(SAVEAREA) IN REG13
         AIF   ('&BASIS'(1,1) EQ 'R').R 1.BASREGPARM RNN?
         AIF   ('&BASIS'(1,1) NE '(').FEH 1.BASREG WEDER RN NOCH (N)?
         AIF   ('&BASIS'(2,1) LT '0').FEH 1.BASREG NICHT NUM?
         AIF   ('&BASIS'(3,1) NE ')').DR  1.BASREG ZWEISTELLIG?
.F       ANOP
&CG0     SETC  '&BASIS'(2,1)              1.BASREG NACH CG0
         AGO   .E
.DR      AIF   ('&BASIS'(3,1) LT '0').FEH 1.BASREG 2.STELLE NICHT NUM?
         AIF   ('&BASIS'(4,1) NE ')').FEH 1.BASREGPARM ENDET NICHT )
.G       ANOP
&CG0     SETC  '&BASIS'(2,2)              1.BASREG NACH CG0
         AGO   .E
.R       AIF   ('&BASIS'(2,1) LT '0').FEH 1.BASREG NUM?    RN
&AL4     SETA  K'&BASIS
         AIF   (&AL4 EQ 2).F
         AIF   (&AL4 EQ 2).F
         AIF   ('&BASIS'(3,1) LT '0').FEH 1.BASREG 2.STELLE NUM?
         AIF   (&AL4 GT 3).FEH            1.BASREG ZU LANG
         AGO   .G
.E       ANOP
&AL1     SETA  &CG0                     1. BASREG UMLADEN NACH AL1
&AL0     SETA  &REGS                    ANZAHL BASREGS NACH AL0
&AL3     SETA  0
         AIF   (&AL1 LT 3).FEH          1.BASREG < 3
         AIF   (&AL0+&AL1 GT 13).FEH    LETZTES BESREG > 12
&AL2     SETA  &AL0+&AL1-1              LETZTES BASREG NACH AL2
         LM    &AL1,&AL2,*+22*4+1*2     LADEN ALLE BASREGS
         B     *+21*4+&AL0*4+1*2        SPRINGEN UEBER SAVEAREA
         USING *,0,1,2,3,4,5,6,7,8,9,10,11,12,13,14
* DIESE USING-ANWEISUNG IST ZUR VERMEIDUNG DER FEHLERMELDUNG 'IEH028'
* NOETIG
         DROP  0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15
         DC    CL10'*&NAME*'            PROGNAME FUER DUMP
         DC    18A(0)                   SAVEAREA
         USING *,&AL1                   1.BASREG
         DC    A(*)                     ADR 1.BASREG
.D       AIF   (&AL1 EQ &AL2).PRM       ALLE USING GENERIERT?
&AL3     SETA  &AL3+1
&AL1     SETA  &AL1+1                   BASREG WEITERSCHALTE
         USING *+&AL3*4092,&AL1         BASREG
         DC    A(*+&AL3*4092)           BASADR
         AGO   .D
.PRM     AIF   ('&PARM' EQ '').SEQU     PARM-PARAMETER VORHANDEN?
         AIF   ('&PARM'(1,1) EQ '(').KLAM PARMADR IN FORM (N)
         AIF   ('&PARM'(1,1) NE 'R').SYMP  PARMADR IN FORM RN
         AIF   ('&PARM'(2,1) LT '0').SYMP PARMADR IN FORM RA???
&AL4     SETA  K'&PARM
         AIF   (&AL4 GT 2).DREI            PARMADR IN FORM RN???
&CG0     SETC  '&PARM'(2,1)               PARMADR (REG) NACH AL1
         AGO   .REG
.DREI    AIF   (&AL4 GT 3).SYMP           PARMADR RNN?
         AIF   ('&PARM'(3,1) LT '0').SYMP PARMADR RNA?
&CG0     SETC  '&PARM'(2,2)
         AIF   ('&CG0'(2,1) GT '5').SYMP
         AGO   .REG
.KLAM    AIF   ('&PARM'(2,1) LT '0').FEH  PARMADR (?)
         AIF   ('&PARM'(3,1) NE ')').B   PARMADR(N)
&CG0     SETC  '&PARM'(2,1)               PARMADR (REG) NACH AL1
         AGO   .REG
.B       AIF   ('&PARM'(3,1) LT '0').FEH  PARMADR(N?)
         AIF   ('&PARM'(4,1) NE ')').FEH  PARNADR NICHT ZWEISTELLIG?
&CG0     SETC  '&PARM'(2,2)               PARMADR (REG) NACH AL1
.REG     ANOP
&AL1     SETA  &CG0
         AIF   (&AL1 EQ 0).FEH          PARMADRREG = 0?
         AIF   (&AL1 GT 12).FEH         PARMADRREG > 12?
         LR    &AL1,1                   SPEICHERN PARMADR
         AGO   .SEQU
.SYMP    ST    1,&PARM                  SPEICHER PARMADR
.SEQU    AIF   (&BG127).ENDE            1.MODUL IN DIESEM COMPILLAUF?
&BG127   SETB  (1)                      DANN REGS EQU RN SETZEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R13      EQU   13
R12      EQU   12
R14      EQU   14
R15      EQU   15
.ENDE    MEXIT
.FEH     MNOTE 'FORMFEHLER BEI PARAMETER'
         MEND
./ ADD NAME=ZEND
         MACRO
&NAME    ZEND  &RC=
         LCLA  &AL0,&AL1,&AL2,&AL3,&AL4
         LCLB  &BL0
         LCLC  &CG0
.**********************************************************************
.*                                                                    *
.*  ZEND - DEFINIEREN UNTERPROGRAMM-ENDE                              *
.*  ====================================                              *
.*                                                                    *
.*  MAKROGRUPPE:        ZSTRT, ZCALL, ZEND                            *
.*  ANWENDUNG:                                                        *
.*  DIESER MAKROAUFRUF WIRD AM LOGISCHEN ENDE EINES UP'S (AUCH ROOT)  *
.*  KODIERT UND STEUERT DEN RUECKSPRUNG INS RUFENDE UP (ODER SUPERV.).*
.*  SUPERVISOR UEBERTRAGEN WERDEN.                                    *
.*  FUNKTIONS:                                                        *
.*  RUECKLADEN REGISTER                                               *
.*  VERZWEIGEN INS RUFENDE UP ODER SUPERVISOR                         *
.*  UEBERTRAGEN RUECKEHRKODE                                          *
.*  FORMAT:                                                           *
.*  <NAME>  ZEND  <RC=OPERAND>                                        *
.*                                                                    *
.*  OPERAND:  RC=ZAHL  ODER   RC=REGISTER                             *
.*                                                                    *
.*  ZAHL      DIE HIER ANGEGEBENE ZAHL IST DER RUECKKEHRKODE, DER     *
.*            DEM RUFENDEN UNTERPROGRAMM BZW. SUPERVISOR UEBER-       *
.*            GEBEN WERDEN SOLL (IN REG 15 DES RUFENDEN PGM-TEILS).   *
.*            DER RUECKKEHRKODE DARF NICHT GROESSER ALS 4096 SEIN.    *
.*                                                                    *
.*  REGISTER  HIER WIRD ANGENOMMEN, DASS DER ZU UEBERGEBENDE RUECK-   *
.*            KEHRCODE IM ANGEGEBENEN REGISTER STEHT.                 *
.*            DER RUECKKEHRCODE WIRD VOM ANGEGEBENEN REGISTER IN      *
.*            DAS REGISTER 15 DES RUFENDEN UNTERPROGRAMMES BZW.       *
.*            SUPERVISOR'S UMGELADEN.                                 *
.*            FOLGENDE SCHREIBWEISEN SIND ZUGELASSEN:                 *
.*            (3) ODER (R3) ODER  R3                                  *
.*                                                                    *
.*  WIRD DER OPERAND 'RC=' WEGGELASSEN, WIRD R15 GELOESCHT.           *
.*                                                                    *
.*  INTERNE MAKROS:     KEINE                                         *
.*  GLOBALPARAMETER:    KEINE                                         *
.*  AUTOR:              SCHUETZ/FISCHER                               *
.*  GUELTIG:            AB AUGUST 1972                                *
.*  SYSTEM:             IBM/OS                                        *
.*  VERSION:            2                                             *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.*  COPYRIGHT BY AKDB - ANSTALT F. KOMMUNALE DATENVERARB. IN BAYERN   *
.*                      8 MUENCHEN 83, ALBERT SCHWEITZER STR. 62      *
.*                      MUENCHEN, DEN 1.7.1973
.*                                                                    *
.**********************************************************************
         CNOP  0,4
         AIF   ('&NAME' EQ '').A1
&NAME    EQU   *
.A1      ANOP
         AIF   ('&RC' EQ '').K
&BL0     SETB  0
.*                                     DEFINIEREN OPERANDEN-LAENGE
&AL1     SETA  K'&RC
.*                                     ? OPERANDEN-ART
.B       ANOP
&AL0     SETA  1
         AIF   ('&RC'(1,1) NE '(').C
&AL0     SETA  &AL0+1
&AL1     SETA  &AL1-2
&BL0     SETB  1
.C       ANOP
         AIF   ('&RC'(&AL0,1) NE 'R').D
&AL0     SETA  &AL0+1
&AL1     SETA  &AL1-1
&BL0     SETB  1
.D       ANOP
         AIF   (&AL1 LT 1).F2
.*                                     ? RC ODER REG = NUMERISCH
&AL2     SETA  &AL0
&AL3     SETA  &AL1
.E       ANOP
         AIF   (&AL3 EQ 0).G
         AIF   ('&RC'(&AL2,1) LT '0').F2
&AL2     SETA  &AL2+1
&AL3     SETA  &AL3-1
         AGO   .E
.*                                     VERWENDEN  RC ODER REG-ZAHL
.G       ANOP
&CG0     SETC  '&RC'(&AL0,&AL1)
&AL4     SETA  &CG0
.*                                     ? RC ODER REGISTER
         AIF   (&AL4 GT 4096).F3
         AIF   (&BL0 EQ 0).H
.*                                     GENERIEREN REGISTERWEG
         AIF   (&AL4 GT 15).F4
         AIF   (&AL4 EQ 13).F5
         ST    &AL4,*+8                SPEICHERN REGISTER
.K       ANOP
&AL4     SETA  0
.*                                     GEN REG + RC WEG
.H       ANOP
         B     *+8                     SPRINGEN UEBER A-KONST.
         DC    A(&AL4)                 FUER RUECKKEHRKODE
         L     13,4(13)                ADRESSIEREN SICHERST.BEREICH
         MVC   16(4,13),*-8            UEB RC
         LM    14,12,12(13)            LADEN REGS VON RUFPROG
         BR    14                      STEUERUNG AN RUFPROG
         MEXIT
.F1      MNOTE 'OPERAND GROESSER 5 STELLEN'
         MEXIT
.F2      MNOTE 'NUMERISCHER OPERANDENTEIL FEHLERHAFT'
         MEXIT
.F3      MNOTE 'RUECKKEHRKODE GROESSER 4096'
         MEXIT
.F4      MNOTE 'REGISTERANGABE GROESSER 15'
         MEXIT
.F5      MNOTE 'REGISTER 13 ANGEWENDET'
         MEND
./ ADD NAME=ZPRLC
         MACRO
&NAME    ZPRLC &LINECNT=60,&CHANN=1,&HDRCNT=0,&HDRADR=0,&LINCL=YES,    *
               &DEBER=,&DSECT=NO,&CTLCHR=A,                            *
               &DCBNAME=,                                              *
               &LINEXT=0,&LINEXP=0,                                    *
               &LINNUMB=1
         GBLA  &PLCC
         LCLA  &A1,&A2,&A3
         LCLC  &C1,&C2,&C3
.*
.*             &A1 = ANZAHL DER UEBERSCHRIFTSZEILEN
.*             &A2 = FUNKTIONSKONTROLLBYTE
.*             &A3 = KANALSTEUERZEICHEN FUER IBM- UND SIEMENS-CODE
.*
.*             &C1 =
.*             &C2 = NAME DES BEREICHES
.*             &C3 = KANALSTEUERZEICHEN FUER ASA-CODE
.*
.*             &PLCC = ANZAHL DER MAKROAUFRUFE
.*
&PLCC    SETA  &PLCC+1
         AIF   ('&NAME' EQ '').P10 NAME FEHLT
         AIF   (K'&NAME LT 6).P11 NAME IST NICHT ZU LANG
         MNOTE 8,'      NAMENSFELD &NAME HAT MEHR ALS FUENF STELLEN, WI*
               RD AUF FUENF STELLEN ABGESCHNITTEN'
&C2      SETC  '&NAME'(1,5)
         AGO   .P12
.P10     ANOP
&C2      SETC  'PLC&PLCC'
         MNOTE *,'      NAME FEHLT &C2 WIRD ANGENOMMEN'
         AGO   .P12
.P11     ANOP
&C2      SETC  '&NAME'
.P12     ANOP
&A1      SETA  &HDRCNT
         AIF   ('&HDRCNT' LT '16').P13
         MNOTE 8,'      ANZAHL DER UEBERSCHRIFTSZEILEN=&HDRCNT, 15 WIRD*
                ANGENOMMEN'
&A1      SETA  15
.P13     AIF   ('&HDRCNT' EQ '0').P14 KEINE ANZAHL ANGEGEBEN
         AIF   ('&HDRADR' NE '0').P15 ADRESSE IST ANGEGEBEN
         MNOTE 8,'       UEBERSCHRIFTSADRESSE FEHLT, ABER &HDRCNT UEBER*
               SCHRIFTSZEILEN ANGEGEBEN'
         AGO   .P15
.P14     AIF   ('&HDRADR' EQ '0').P15  KEINE ADRESSE ANGEGEBEN
         MNOTE 8,'       UEBERSCHRIFTSADRESSE ''&HDRADR'' ANGEGEBEN, AB*
               ER KEINE ANZAHL DER UEBERSCHRIFTSZEILEN'
.P15     AIF   ('&CTLCHR' NE '').P16 STEUERZEICHENART IST ANGEGEBEN
         MNOTE 8,'       KEINE STEUERZEICHENART ANGEGEBEN'
&A2      SETA  0
         AGO   .P17
.P16     AIF   ('&CTLCHR' NE 'M').P18
&A2      SETA  0
         AGO   .P17
.P18     AIF   ('&CTLCHR' NE 'A').P19
&C3      SETC  '&CHANN'
         AIF   ('&CHANN' LT '10').P180
         AIF   ('&CHANN' NE '10').P181
&C3      SETC  'A'
         AGO   .P180
.P181    AIF   ('&CHANN' NE '11').P182
&C3      SETC  'B'
         AGO   .P180
.P182    AIF   ('&CHANN' NE '12').P183
&C3      SETC  'C'
         AGO   .P180
.P183    ANOP
         MNOTE *,'  UNGUELTIGE KANALANGABE, KANAL 1 WIRD ANGENOMMEN'
&C3      SETC  '1'
.P180    ANOP
&A2      SETA  3*16
         AGO   .P17
.P19     AIF   ('&CTLCHR' NE 'S').P20
&A2      SETA  1*16
         AGO   .P17
.P20     ANOP
         MNOTE 8,'       UNGUELTIGE STEUERZEICHENART ''&CTLCHR'' IST AN*
               GEGEBEN'
.P17     AIF   ('&DCBNAME' NE '').P170 DCBNAME IST ANGEGEBEN
&C1      SETC  '&C2.DCB'
         AGO   .P175
.P170    ANOP
&C1      SETC  '&DCBNAME'
.P175    AIF   ('&DSECT' EQ 'NO').P21
&C2.D    DSECT FUER PRINT-LINE-CONTROL
&C1      SETC  '0'
.P21     ANOP
&C2      DS    0F
&C2.HDA  DC    A(&HDRADR) ADRESSE DER ERSTEN UEBERSCHRIFTSZEILE
&C2.DCA  DC    A(&C1)  DCB-ADRESSE
         AIF   ('&LINEXT' EQ '0').P177 IST EIN EXIT-PARAMETER ANGEGEBEN
&C2.EXT  DC    V(&LINEXT) EXIT-ADRESSE
         AGO   .P178
.P177    ANOP
&C2.EXT  DC    A(&LINEXT)  EXIT-ADRESSE NULL
.P178    ANOP
&C2.DBR  DC    A(0) DRUCKBEREICHSADRESSE F. EXIT
&C2.EXP  DC    A(&LINEXP) PARAMETER-ADRESSE F. EXIT
&C2.NUM  DC    H'&LINNUMB' ANZAHL DER AUF EINMAL ZU DRUCKENDEN ZEILEN
&C2.HDC  DC    H'&A1' ANZAHL DER UEBERSCHRIFTSZEILEN
&C2.LNC  DC    H'&LINECNT' ANZAHL DER ZEILEN PRO SEITE
&C2.LCL  DC    H'0' LAUFENDER ZEILENZAEHLER
         DC    H'0' RESERVE FELD
&A3      SETA  &CHANN
         AIF   ('&CTLCHR' NE 'M').P22
&A3      SETA  (&A3*8)+129+2 DIREKT-VORSCHUB
         AGO   .P23
.P22     AIF   ('&CTLCHR' NE 'A').P24
&C2.CHA  DC    CL1'&C3'  KANAL FUER VORSCHUB NACH UEBERLAUF
         AGO   .P25
.P24     ANOP
         AIF   ('&CTLCHR' NE 'S').P23
&A3      SETA  &A3+128+64 VORSCHUB VOR DEM DRUCKEN
.P23     ANOP
&C2.CHA  DC    AL1(&A3) KANAL FUER VORSCHUB NACH UEBERLAUF
.P25     ANOP
         AIF   ('&LINCL' NE 'YES').P26
&A2      SETA  &A2+4
         MNOTE *,'       DRUCKBEREICH WIRD NACH DEM DRUCKEN GELOESCHT'
         AGO   .P27
.P26     ANOP
         MNOTE *,'  DRUCKBEREICH WIRD NACH DEM DRUCKEN NICHT GELOESCHT'
.P27     ANOP
&A2      SETA  &A2+1 OPEN-ANZEIGER
         MNOTE *,'       OPEN VOR DEM ERSTEN DRUCKEN'
&C2.CNT  DC    AL1(&A2) FUNKTIONSKONTROLLBYTE
&C2.OVL  EQU   128  UEBERLAUFANZEIGER
&C2.EXE  EQU   64 LETZTER AUFRUF DER EXIT-ROUTINE
&C2.CHR  EQU   48 STEUERZEICHEN-ART 00=IBM-CODE,01=SIEMENS-CODE,11=ASA
&C2.VVZ  EQU   8 VORSCHUBVERZOEGERUNG
&C2.DBL  EQU   4 DRUCKBEREICH LOESCHEN NACH DEM DRUCKEN
&C2.CND  EQU   2 CLOSE NACH DEM DRUCKEN
&C2.OVD  EQU   1 OPEN VOR DEM DRUCKEN
&C2.ZZL  DC    F'0' DRUCKZEILENZAEHLER
&C2.GBL  DC    F'0'   GESAMT-BLATTZAEHLER
&C2.BLZ  DC    PL4'0' BLATTZAEHLER
&C2.DAT  DC    CL8' ' TAGESDATUM
         SPACE 3
         AIF   ('&DSECT' EQ 'YES').P271
         AIF   ('&DCBNAME' NE '').P270
         MNOTE *,'       DCB-MAKRODEFINITION'
         MNOTE *,'&C2.DCB DCB DSORG=PS,MACRF=PM,DDNAME=&C2.DD,RECFM=FB&*
               CTLCHR,EXLST=&C2.DEX'
&C2.DCB  DCB   DSORG=PS,MACRF=PM,DDNAME=&C2.DD,RECFM=FB&CTLCHR,        *
               EXLST=&C2.DEX
         SPACE 3
         USING *,R15                   BASISREGISTER FUER DCB-EXIT ROUT
         SPACE
&C2.OPX  DS    0H
         ST    R14,&C2.SV
         LH    R14,82(R1)              LRECL
         LTR   R14,R14                 IST LRECL ANGEGEBEN
         BNZ   *+12                    JA
         LA    R14,133                 NEIN, LRECL=133 WIRD ANGENOMMEN
         STH   R14,82(R1)              IN DCB EINSPEICHERN
*      BLKSIZE PRUEFEN
         LH    R14,62(R1)              BLKSIZE LADEN
         LTR   R14,R14                 IST BLKSIZE VORHANDEN
         BNZ   *+12                    JA
         LA    R14,1729                NEIN, BLKSIZE=1729 WIRD ANGENOMM
         STH   R14,62(R1)              IN DCB EINSPEICHERN
         L     R14,&C2.SV
         BR    R14
         SPACE
&C2.SV   DS    F                       SAVE R14 F. OPEN EXIT-ROUTINE
&C2.DEX  DC    AL1(5+128)              OPEN-EXIT
         DC    AL3(&C2.OPX)
         DROP  R15
         SPACE 3
         AGO   .P270
.P271    ANOP
         DCBD  DEVD=DA,DSORG=PS
.P270    ANOP
         AIF   ('&DSECT' NE 'NO').P29
         AIF   ('&DEBER' EQ '').P28 DEBER IST NICHT ANGEGEBEN
         MNOTE *,'UNTERPROGRAMM-AUFRUF WIRD GENERIERT, MUSS MIT  BAL   *
               R14,&C2.PRT         ANGESPRUNGEN WERDEN'
         MNOTE *,'       &C2.PRT   ZCALL  KZ00002,&C2,&DEBER  '
&C2.SAV  DC    F'0' SAVE FUER RUECKSPRUNGREGISTER
&C2.PRT  ST    14,&C2.SAV RUECKSPRUNGREGISTER SICHERN
         ZCALL KZ00002,&C2,&DEBER  AUFRUFEN UNTERPROGRAMM
         SPACE
         L     14,&C2.SAV RUECKSPRUNGADRESSE LADEN
         BR    14   RUECKSPRUNG
.P28     ANOP
         AGO   .P30
.P29     ANOP
         CSECT
.P30     ANOP
         SPACE 3
         MEND
./ ADD NAME=ZCALL
         MACRO
&NAME    ZCALL &UP,&P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,&P11,&P12,&F
               P13,&P14,&P15,&P16,&P17,&P18,&P19,&P20,&P21,&P22,&P23,&PF
               24,&P25,&P26,&P27,&P28,&P29,&P30,&P31,&P32,&P33,&P34,&P3F
               5,&P36,&P37,&P38,&P39,&P40,&P41,&P42,&P43,&P44,&P45,&P46F
               ,&P47,&P48,&P49,&P50,&P51,&P52,&P53,&P54,&P55,&P56,&P57,F
               &P58,&P59,&P60,&P61,&P62,&P63,&P64,&P65,&P66,&P67,&P68,&F
               P69,&P70,&P71,&P72,&P73,&P74,&P75,&P76,&P77,&P78
         LCLA  &AL0,&AL1,&AL2,&AL3,&AL4,&AL5,&AL6,&AL7,&AL8,&AL9
         LCLA  &AL10,&AL11,&AL12,&AL13
         LCLC  &CG0
.**********************************************************************
.*
.*  ZCALL - AUFRUFEN UNTERPROGRAMM                                    *
.*                                                                    *
.*  MAKROGRUPPE:        ZSTRT, ZEND, ZCALL                            *
.*  ANWENDUNG:                                                        *
.*  DIESER MAKROAUFRUF DIENT ZUM AUFRUFEN EINES UNTERPROGRAMMES.      *
.*  FUNKTION:                                                         *
.*  GENERIEREN EINER V-KONSTANTE MIT ANSPRUNGNAMEN                    *
.*  VERZWEIGEN NACH UNTERPROGRAMM                                     *
.*  SPEICHERN RUECKSPRUNGADRESSE                                      *
.*  GENERIEREN PARAMETERLISTE                                         *
.*  FORMAT:                                                           *
.*  <NAME>  ZCALL  ANSPRUNGNAME<,ADR1,ADR2,ADR3,....,ADR77>           *
.*  ANSPRUNGNAME   = ADRESSE DES ANZUSPRINGENDEN UP'S                 *
.*                   A) SYMBOLISCHE ADRESSE                           *
.*                   B) REGISTER (MIT ADRESSE)                        *
.*                      SCHREIBWEISE  (3) ODER (R3) ODER R3           *
.*                      (DIE ZAHL 3 IST NUR BEISPIEL)                 *
.*  ADR1 USW.      = PARAMETERLISTE                                   *
.*                   JEDE ANGEG. ADR. ERGIBT EINEN EINTRAG IN DER     *
.*                   PARAMETERLISTE.                                  *
.*                   DIE ANZAHL DER OPERANDEN DARF 77 NICHT UEBER-    *
.*                   STEIGEN.                                         *
.*                   WIRD HIER KEIN OPERAND ANGEGEN, BLEIBT REG 1     *
.*                   UNVERAENDERT.                                    *
.*  DER OPERAND KANN WIE FOLGT ANGEGEBEN WERDEN:                      *
.*  SYMB. ADRESSE  = DIE ADRESSE DES ANGEGEBENEN FELDES WIRD          *
.*                   EINGETRAGEN.                                     *
.*  /SYMB. ADRESSE = DIE, DURCH VERWENDUNG VON DSECT DEFINIERTE       *
.*                   ADRESSE WIRD EINGETRAGEN.                        *
.*  (R) ODER RR                                                       *
.*  ODER (RR)      = DER INHALT DES ANGEGEBENEN REGISTERS WIRD        *
.*                   EINGETRAGEN                                      *
.*  INTERNE MAKROS:     KEINE                                         *
.*  GLOBALPARAMETER:    &CG0 (NUR BEI SIEMENS + LOKAL VERWENDET)      *
.*  AUTOR:              SCHUETZ/FISCHER                               *
.*  GUELTIG:            AB AUGUST 1972                                *
.*  SYSTEM:             IBM/OS                                        *
.*  VERSION             2                                             *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.*  COPYRIGHT BY AKDB - ANSTALT F. KOMMUNALE DATENVERARB. IN BAYERN   *
.*                      8 MUENCHEN 83, ALBERT SCHWEITZER STR. 62      *
.*                      MUENCHEN, DEN 1.7.1973
.*                                                                    *
.**********************************************************************
.*
.*  PARAMETERLISTE
.*  ==============
.*
.*  &AL0      PARAMETERZAEHLER FUER ST-BEFEHL
.*  &AL1      /-ZAEHLER        FUER ST-BEFEHL
.*  &AL2      REGISTER-ZAEHLER FUER ST-BEFEHL
.*  &AL3      REGISTER-NOTATION FUER ANSPRUNGNAMEN (0 - JA / 4 - NEIN)
.*  &AL4      LA-BEFEHL VOR ST-BEFEHL  (0 - NEIN / 4 - JA)
.*  &AL5      ARBEITSZAHLER FUER PARAMETER &SYSLIST
.*  &AL6      SPEICHER FUER PARAMETER-ZAEHLER
.*  &AL7      REGISTER-NUMMER WENN REG-NOTATION (SONST 0)
.*  &AL8      REGISTER-NUMMER FUER ST-BEFEHL
.*  &AL9      STELLUNGS-POINTER
.*  &AL10     SUBTRAHEND /-ZAEHLER
.*  &AL11     SUBTRAHEND R-ZAEHLER
.*  &AL12     SUMMENATTRIBUT
.*  &AL13     HILFSPATAMETER FUER ZAHLENWERTE
.*  &CG0      ZWISCHENSPEICHER FUER SYMB. NAMEN USW
.*
         CNOP  0,4
         AIF   ('&UP' NE '').A
         MNOTE 'ANSPRUNGNAME FEHLT'
         MEXIT
.A       ANOP
         AIF   ('&NAME' EQ '').AB
&NAME    EQU   *
.AB      ANOP
&AL0     SETA  0
&AL1     SETA  0
&AL2     SETA  0
&AL3     SETA  4
&AL5     SETA  1
&AL7     SETA  0
.B       ANOP
&AL12    SETA  K'&SYSLIST(&AL5)
         AIF   (&AL12 EQ 0).C
         AIF   (&AL12 EQ 1).BD
         AIF   ('&SYSLIST(&AL5)'(1,1) EQ '/').BC
         AIF   ('&SYSLIST(&AL5)'(1,1) EQ '(').BA
         AIF   ('&SYSLIST(&AL5)'(1,1) NE 'R').BD
         AIF   ('&SYSLIST(&AL5)'(2,1) LT '0').BD
         AIF   (&AL12 LE 2).BA
         AIF   ('&SYSLIST(&AL5)'(3,1) LT '0').BD
         AIF   (&AL12 GT 3).BD
.BA      ANOP
         AIF   (&AL5 EQ 1).BB
&AL2     SETA  &AL2+1
         AGO   .BD
.BB      ANOP
&AL3     SETA  0
         AGO   .BD
.BC      ANOP
         AIF   (&AL5 EQ 1).FEHL
&AL1     SETA  &AL1+1
.BD      ANOP
&AL5     SETA  &AL5+1
         AGOB  .B
.C       ANOP
&AL6     SETA  &AL5-1
&AL5     SETA  1
.D       ANOP
&AL4     SETA  0
&AL10    SETA  0
&AL11    SETA  0
&AL12    SETA  K'&SYSLIST(&AL5)
         AIF   ('&SYSLIST(&AL5)'(1,1) NE '/').DA
&AL13    SETA  &AL12-1
&CG0     SETC  '&SYSLIST(&AL5)'(2,&AL13)
         LA    1,&CG0
&AL4     SETA  4
&AL10    SETA  1
&CG0     SETC  '1'
         AGO   .DE
.DA      ANOP
         AIF   (&AL12 EQ 1).DB
         AIF   ('&SYSLIST(&AL5)'(1,1) NE 'R').DB
         AIF   ('&SYSLIST(&AL5)'(2,1) LT '0').DG
&CG0     SETC  '&SYSLIST(&AL5)'(2,1)
         AIF   (&AL12 LE 2).DD
         AIF   (&AL12 GT 3).DG
         AIF   ('&SYSLIST(&AL5)'(3,1) LT '0').DG
&CG0     SETC  '&SYSLIST(&AL5)'(2,2)
         AGO   .DD
.DB      ANOP
         AIF   ('&SYSLIST(&AL5)'(1,1) NE '(').DG
&AL9     SETA  2
         AIF   ('&SYSLIST(&AL5)'(2,1) NE 'R').DC
&AL9     SETA  3
.DC      ANOP
         AIF   ('&SYSLIST(&AL5)'(&AL9,1) LT '0').FEHL
&CG0     SETC  '&SYSLIST(&AL5)'(&AL9,1)
&AL9     SETA  &AL9+1
         AIF   ('&SYSLIST(&AL5)'(&AL9,1) EQ ')').DD
         AIF   ('&SYSLIST(&AL5)'(&AL9,1) LT '0').FEHL
&AL9     SETA  &AL9+1
         AIF   ('&SYSLIST(&AL5)'(&AL9,1) NE ')').FEHL
&AL9     SETA  &AL9-2
&CG0     SETC  '&SYSLIST(&AL5)'(&AL9,2)
.DD      ANOP
         AIF   (&AL5 EQ 1).DE
&AL11    SETA  1
.DE      ANOP
&AL8     SETA  &CG0
         AIF   (&AL5 NE 1).DF
&AL7     SETA  &AL8
         AGO   .DH
.DF      ANOP
         ST    &AL8,*+12+&AL0*4+&AL1*8+&AL2*4+&AL3-&AL4
&AL1     SETA  &AL1-&AL10
&AL2     SETA  &AL2-&AL11
.DG      ANOP
         AIF   (&AL5 EQ 1).DH
&AL0     SETA  &AL0+1
.DH      ANOP
         AIF   (&AL5 EQ &AL6).E
&AL5     SETA  &AL5+1
         AGO   .D
.E       ANOP
         AIF   (&AL3 EQ 0).F
         L     15,*+12                 ANSPRUNG LT. SYMB.ADRESSE
         AGO   .G
.F       ANOP
         LA    15,0(&AL7,0)            ANSPRUNG LT. REGISTER
.G       ANOP
         AIF   (&AL6 EQ 1).GA
         LA    1,*+8+&AL3              LAD PARAMETER-LISTADRESSE
         AGO   .GB
.GA      ANOP
         LA    1,0(1,0)
.GB      ANOP
&AL6     SETA  &AL6-1
         B     *+&AL6*4+4+&AL3         UEBERSPRINGEN KONSTANTEN
         AIF   (&AL3 EQ 0).H
         DC    V(&UP)
.H       ANOP
         AIF   (&AL6 EQ 0).LB
&AL5     SETA  2
&AL6     SETA  &AL6+1
.K       ANOP
&AL12    SETA  K'&SYSLIST(&AL5)
         AIF   ('&SYSLIST(&AL5)'(1,1) EQ '(').KA
         AIF   ('&SYSLIST(&AL5)'(1,1) EQ '/').KA
         AIF   ('&SYSLIST(&AL5)'(1,1) NE 'R').KB
         AIF   ('&SYSLIST(&AL5)'(2,1) LT '0').KB
         AIF   (&AL12 LE 2).KA
         AIF   ('&SYSLIST(&AL5)'(3,1) LT '0').KB
         AIF   (&AL12 GT 3).KB
.KA      ANOP
&CG0     SETC  '0'
         AGO   .KC
.KB      ANOP
&CG0     SETC  '&SYSLIST(&AL5)'
.KC      ANOP
         DC    A(&CG0)
         AIF   (&AL5 GE &AL6).LA
&AL5     SETA  &AL5+1
         AGO   .K
.LA      ANOP
         OI    *-4,X'80'               SET HIGH-ORDER-BIT
.LB      ANOP
         BALR  14,15
         MEXIT
.FEHL    ANOP
         MNOTE 'FORMFEHLER BEI PARAMETER'
         MEND
