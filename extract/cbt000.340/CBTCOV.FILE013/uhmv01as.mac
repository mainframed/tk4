         TITLE 'D R U C K E N   D A T E I'
***********************************************************************
*           PROGRAMMIERER: SCHWARZ          AEND.DATUM: 08.04.71      *
*           PROGRAMMIERER: LUEHMANN         AEND.DATUM: 03.02.72      *
*             PROGRAMMIERER: BARDUHN        AEND.DATUM: 09.01.73      *
*   HMDD      PROGRAMMIERER: LUETTIG        AEND.DATUM: 07.08.73      *
*   HMDD      PROGRAMMIERER: LUETTIG        AEND.DATUM: 08.08.73      *
*   HMDD      PROGRAMMIERER: BARDUHN        AEND.DATUM: 20.02.74      *
*   HMDD      PROGRAMMIERER: STEINER        AEND.DATUM: 18.04.75      *
*   HMDD      PROGRAMMIERER: LUETTIG        AEND.DATUM: 11.06.75      *
*   HMDD      PROGRAMMIERER: LUETTIG        AEND.DATUM: 23.06.75      *
*   HMDD      PROGRAMMIERER: BARDUHN        AEND.DATUM: 21.08.75      *
*   HMDD      PROGRAMMIERER: FEHLING        AEND.DATUM: 20.07.76      *
*   HMDD      PROGRAMMIERER: LUETTIG        AEND.DATUM: 28.07.76      *
*   HMDD      PROGRAMMIERER: BARDUHN        AEND.DATUM: 02.09.76      *
*   HMDD      PROGRAMMIERER: FEHLING        AEND.DATUM: 06.10.76      *
*
*  HMDD DRUCKT EINE DATEI (DDNAME=DATA) SATZWEISE IN CHARACTER- UND
*       HEXADEZIMALER FORM. DCB-ANGABEN WERDEN AUS DER DD-KARTE UND
*       DEN KENNSAETZEN GENOMMEN. FEHLENDE ANGABEN KOENNEN ZU ARBEITS-
*       ABBRUCH FUEHREN.DIE KENNSAETZE WERDEN NICHT AUSGEGEBEN.
*        3 PARAMETER KOENNEN UEBERGEBEN WERDEN:'BZ,SB,C'
*       BZ: MAX. ANZAHL ZU DRUCKENDER BLOECKE (DEFAULT=100)
*       SB: ANZAHL DER ZU UEBERSPRINGENDEN BLOECKE (DEFAULT=0)
*        C: AUSDRUCK ALLER SAETZE DIE NUR DRUCKBARE ZEICHEN ENTHALTEN
*           IM CHARACTERFORMAT  DEFAULT: HEXADEZIMAL UND CHARACTER-FORM
*                                                                     *
*                                                                     *
*   BASISREGISTER:   R3  R10  R11                                     *
*                                                                     *
***********************************************************************
         EJECT
HMDD    ANFANG BR=(R3,R10,R11)
         SPACE 2
*  PARAMETER VERARBEITEN                                            P
         SPACE 1
         L     R1,0(,R1)
         LH    R15,0(,R1)         LAENGE PARAMETERLISTE
         LTR   R15,R15            PARAMETER VORHANDEN
         BZ    DC080              NEIN
         LA    R4,2(0,R1)
         BCTR  R15,0
         STC   R15,*+5            EINFUEGEN LAENGENSCHLUESSEL
         TRT   0(,R4),TRTAB3      SUCHEN ','
         BC    8,DC050            NUR 1. PARAMETER VORHANDEN
         LA    R1,0(,R1)          FUEHRENDES BYTE LOESCHEN
         SR    R1,R4              LAENGE 1. PARAMETERFELD
         BZ    DC020              1. PARAMETER FEHLT
         BCTR  R1,0               L' BLOCKZAEHLER - 1
DC010    EX    R1,INSTR1          PACK BLKCNT,0(,R4)
         LA    R1,1(,R1)
DC020    LA    R1,1(,R1)
         AR    R4,R1
         SR    R15,R1
         STC   R15,*+5            EINFUEGEN LAENGENSCHLUESSEL
         TRT   0(,R4),TRTAB3      SUCHEN ','
         BC    8,DC060            3. PARAMETER FEHLT
         LA    R1,0(,R1)          FUEHRENDE BITS LOESCHEN
         SR    R1,R4              LAENGE 2. PARAMETERFELD
         BZ    DC070              2. PARAMETER FEHLT
         BCTR  R1,0               L' UEBERSPRINGEN - 1
DC030    EX    R1,INSTR2          PACK SKPBLK,0(,R4)
         AP    BLKCNT,SKPBLK
         LA    R1,2(,R1)
         CR    R1,R15
         BH    DC080              3. PARAMETER FEHLT
         AR    R4,R1
DC040    CLI   0(R4),C'C'         3. PARAMETER RICHTIG
         BNE   DC080              NEIN
         MVC   DDROUT,=A(DD300)
         MVC   DUEB,=A(T10)
         MVC   DEND,=A(T11)
         B     DC080
DC050    EX    R15,INSTR1         PACK BLKCNT,0(,R4)
         B     DC080
DC060    LR    R1,R15
         B     DC030
DC070    EQU   *                  2. PARAMETER FEHLT
         LTR   R15,R15            3. PARAMETER VORHANDEN ?
         BZ    DC080              NEIN
         LA    R4,1(,R4)
         B     DC040
DC080    EQU   *                  STEUERKARTEN VORHANDEN ?
         LA    R1,=CL8'SYSIN'
         LINK  EP=SP54            SYSIN DD-STMT. VORHANDEN ?
         LTR   R15,R15
         BNZ   DD010              NEIN, KEINE SATZAUSWAHL
         OPEN  (SYSIN)
         BAL   R14,DD700          STEUERKARTE LESEN UND AUFBEREITEN
         SPACE 2
*  OPEN DATEI, BESTIMMEN RECFM, LRECL UND BLKSIZE                   P
         SPACE 1
DD010    EQU   *
         OPEN  (DATA)
        RDJFCB DATA               JFCB LESEN
         LTR   R15,R15            GEFUNDEN ?
         BNZ   DD180              NEIN
         L     R7,DUEB
         LA    R4,DATA
         USING IHADCB,R4
         OC    DCBBLKSI,DCBBLKSI  DATEI LEER ?
         BZ    DD400              JA
         MVC   LRECL,DCBLRECL
         MVC   SRECFM,DCBRECFM
         NI    DCBRECFM,B'11010000'
         IC    R14,DCBRECFM
         SRL   R14,3
         LA    R14,2(,R14)
         N     R14,=F'28'     X '00 00 00 1C'
         BZ    DD400              DATEI LEER
         STH   R14,RECFM
         B     *-4(R14)
         B     DD020          RECFM = V
         B     DD030          RECFM = VB
         B     DD040          RECFM = F
         B     DD050          RECFM = FB
         MVI   T3+6,C'U'      RECFM = U
         B     DD070
DD020    EQU   *              RECFM=V
         MVI   T3+6,C'V'
         B     DD070
DD030    EQU   *              RECFM=VB
         MVC   T3+6(2),=C'VB'
         B     DD060
DD040    EQU   *              RECFM=F
         MVI   T3+6,C'F'
         MVC   LRECL,DCBBLKSI
         B     DD070
DD050    EQU   *              RECFM=FB
         MVC   T3+6(2),=C'FB'
DD060    LH    R12,DCBLRECL
         CVD   R12,D
         OI    D+7,X'0F'
         UNPK  T3+31(5),D
         LA    R12,T3+36
         B     DD080
DD070    MVC   T3+25(20),=CL20'KEYLEN='
         LA    R12,T3+23
DD080    SR    R14,R14
         IC    R14,DCBKEYLE
         LTR   R14,R14            KEYLEN ANGEGEBEN ?
         BNZ   DD0801             JA
         MVC   0(9,R12),=CL9' '
         B     DD0802
DD0801   CVD   R14,D              KEYLEN AUFBEREITEN
         OI    D+7,X'0F'          VORZEICHEN
         UNPK  9(3,R12),D
DD0802   LH    R12,DCBBLKSI
         CVD   R12,D
         OI    D+7,X'0F'
         UNPK  T3+18(5),D
         BAL   R14,DD900          TITELZEILE AUFBEREITEN
         MSG   132,T12,C              UND DRUCKEN
         MSG   48,T3,C
         SP    ZZ,=P'2'           ZEILENZAEHLER
         TM    SRECFM,X'20'
         BNO   DD081
         MSG   14,'TRACK OVERFLOW',D
         SP    ZZ,=P'2'           ZEILENZAEHLER
DD081    EQU   *
         TM    SRECFM,X'88'       F(B)S ?
         BNO   DD082
         MSG   15,'STANDARD BLOCKS',D
         SP    ZZ,=P'2'           ZEILENZAEHLER
DD082    EQU   *
         TM    SRECFM,X'48'       V(B)S ?
         BNO   DD0825             NEIN
         MSG   15,'SPANNED RECORDS',D
         SP    ZZ,=P'2'           ZEILENZAEHLER
DD0825   EQU   *
         TM    SRECFM,X'04'
         BNO   DD083
         MSG   20,'ASA-VORSCHUB ZEICHEN',D
         SP    ZZ,=P'2'           ZEILENZAEHLER
         B     DD084
DD083    EQU   *
         TM    SRECFM,X'02'
         BNO   DD084
         MSG   14,'MASCHINEN-CODE',D
         SP    ZZ,=P'2'           ZEILENZAEHLER
DD084    EQU   *
         PUT   MSGPRINT
         SP    ZZ,=P'3'           ZEILENZAEHLER
         MVC   0(5,R1),=X'0087000019' WSP3, L=135
         MVC   5(130,R1),0(R7)         KOPFZEILE T7 ODER T10
         SPACE 3
*   CLOSE UND ERNEUTES OEFFNEN DER DATEI MIT RECFM=U                I
         SPACE 1
         CLOSE (DATA,REREAD)
         MVI   DCBRECFM,X'C0' RECFM=U  ERZWINGEN
         MVZ   DEVT,DCBDEVT       DEVICE TYPE
         CLI   DEVT,X'20'         PLATTE ?
         BNE   DD085              NEIN
         TM    JFCBTSDM,X'20'     SYSIN-SYSOUT ?
         BO    DD085              JA
         OBTAIN DSCAMLST          DSCB LESEN
         LTR   R15,R15            GEFUNDEN ?
         BNE   DD180              NEIN
         CLI   DS1FMTID,C'1'      FORMAT 1 DSCB ?
         BNE   DD180              NEIN, VTOC KAPUTT
         OC    DS1LSTAR,DS1LSTAR DATEI LEER ?
         BZ    DD400              JA, EOF
         TM    SRECFM,X'20'       TRACK OVERFLOW ?
         BZ    DD085              NEIN
         OI    DCBRECFM,X'20'     TRACK OVERFLOW ERZWINGEN
DD085    EQU   *
         ZAP   D,=P'0'
         OPEN  (DATA)
         SR    R5,R5
         IC    R5,DCBKEYLE
         LTR   R5,R5              KEYLEN = 0 ?
         BZ    DD091              JA
         MVC   T5+23(9),=C', KEYLEN '
         CVD   R5,D
         OI    D+7,X'0F'
         UNPK  T5+32(3),D
         ZAP   D,=P'0'
         B     DD091
DD090    BAL   R5,DD500           GEWUENSCHTE ANZAHL BLOECKE SKIPPEN
         AP    BLKZ,=P'1'
DD091    SP    SKPBLK,=P'1'
         BNL   DD090
         LH    R12,RECFM
         B     *-4(R12)       RECFM BESTIMMEN                       P
         B     DD110          RECFM=V                               BT
         B     DD110          RECFM=VB                              BT
         B     DD140          RECFM=F                               BT
         B     DD140          RECFM=FB                              BT
         SPACE 2
DD100    EQU   *              RECFM=U                               N
         BAL   R5,DD500           SATZ LESEN
         LH    R8,DCBLRECL
         SR    R6,R6
         IC    R6,DCBKEYLE
         SR    R8,R6              NUR DATENLAENGE
         LR    R6,R1
         L     R15,DDROUT         ADR. DRUCKROUTINE
         BALR  R5,R15             DRUCKEN SATZ
         AP    BLKZ,=P'1'
         CP    BLKZ,BLKCNT    BLOCKZAEHLER UEBERSCHRITTEN?          DY
         BNH   DD100                                                C
         B     DD160          ENDE                                  B
         SPACE 2
*        VARIABLE GEBLOCKTE SAETZE AUSGEBEN                         N
         SPACE 1
DD110    BAL   R5,DD500           SATZ LESEN
         LH    R9,DCBLRECL    BLKSIZE
         CVD   R9,D
         BCTR  R9,0
         SR    R15,R15
         IC    R15,DCBKEYLE
         SR    R9,R15             DATENLAENGE
         LR    R6,R1
         AR    R9,R6          R9=ENDE DES BLOCKS'
         LA    R6,4(,R6)
DD120    MVC   H,0(R6)        SATZLAENGE
         LH    R8,H
         L     R15,DDROUT         ADR. DRUCKROUTINE
         BALR  R5,R15             DRUCKEN SATZ
         AP    SATZZ,=P'1'
         LH    R8,H
         BXLE  R6,R8,DD120
         AP    BLKZ,=P'1'
         CP    BLKZ,BLKCNT    BLOCKZAEHLER UEBERSCHRITTEN?          DY
         BNH   DD110                                                C
         B     DD160          ENDE                                  B
         SPACE 2
*        BLOECKE FESTER SATZLAENGE AUSGEBEN                         N
         SPACE 1
DD140    BAL   R5,DD500           SATZ LESEN
         LR    R6,R1
         LH    R9,DCBLRECL    BLKSIZE
         CVD   R9,D
         BCTR  R9,0
         SR    R15,R15
         IC    R15,DCBKEYLE
         SR    R9,R15             DATENLAENGE
         AR    R9,R6          R9 = BLOCKENDE
         LH    R8,LRECL       SATZLAENGE
         CH    R8,DCBLRECL        SATZLAENGE > BLOCKLAENGE ?
         BNH   DD150              NEIN
         LH    R8,DCBLRECL        NUR BLOCKLAENGE DRUCKEN
DD150    L     R15,DDROUT         ADR. DRUCKROUTINE
         BALR  R5,R15             DRUCKEN SATZ
         LH    R8,LRECL       SATZLAENGE
         AP    SATZZ,=P'1'
         BXLE  R6,R8,DD150
         AP    BLKZ,=P'1'
         CP    BLKZ,BLKCNT
         BNH   DD140          BLOCKZAEHLER UEBERSCHRITTEN?          DY
DD160    MSG   27,BLOCKZAEHLER-UEBERSCHRITTEN,D
         B     DD410
DD170    CLOSE (DATA,,MSGPRINT)
         ENDE  ,RC=0              ENDE                               E
         SPACE 3
DD180    EQU   *
         MSG   20,'DATEI NICHT GEFUNDEN',D
         B     DD170
         DC    A(0)
DD190    ST    R14,*-4
         MSG   80,KARTE+2,C
         MSG   22,'FALSCHE PARAMETERKARTE',D
         CLOSE (MSGPRINT)
         ABEND 1003,DUMP
         EJECT
*  UNTERPROGRAMM
*     EINGABE   R5  RETURN
*               R6  ADRESSE DES AUSZUGEBENDEN BEREICHES
*               R8  LAENGE  DES AUSZUGEBENDEN BEREICHES
*     AUSGABE   UEBER MSGPRINT
*               BLOCKZAEHLER UND -LAENGE (D)
*               SATZZAEHLER UND -LAENGE (R8)
*               BEREICH IN CHARACTER UND HEX.
*                  NUR BEI DEM 1. SATZ EINES BLOCKS IST  D > 0
         SPACE
DD200    STM   R5,R6,SDD200
         BAL   R14,DD800          PRUEFUNG, OB SATZ GEDRUCKT WERDEN SOL
         LTR   R15,R15            SATZ DRUCKEN ?
         BZR   R5                 NEIN
         SP    SZZ,SZZ
         LTR   R8,R8          FEHLT SATZLAENGE?                     DY
         BH    DD202          NEIN                                  C
         MSG   28,T9,C        FEHLERMELDUNG                         I
         B     DD170          ARBEITSABBRUCH    '                   B
DD202    EQU   *
         LH    R14,RECFM
         B     *-4(R14)       WELCHE RECFM?                         P
         B     DD220          RECFM = V                             BT
         B     DD220          RECFM = VB                            BT
         B     DD205          RECFM = F                             BT
         B     DD220          RECFM = FB                            BT
         CVD   R8,D           RECFM = U                             N
DD205    UNPK  T5+4(5),BLKZ   BLOCKZAEHLER
         OI    T5+8,X'F0'
         OI    D+7,X'0F'
         UNPK  T5+18(5),D     BLOCKLAENGE
         PUT   MSGPRINT       ZEILENVORSCHUB
         SP    ZZ,=P'4'           ZEILENZAEHLER
         BH    DD210
         BAL   R5,DD280       NEUE SEITE
DD210    EQU   *
         MVC   0(5,R1),=X'0028000011' WSP2,L=40
         MVC   5(35,R1),T5
         B     DD250          SATZ AUSGEBEN                         B
         SPACE 2
DD220    EQU   *              RECFM=V,VB,FB                         N
         CP    D,=P'0'        ERSTER SATZ IM BLOCK?                 DN
         BE    DD235              NEIN
         UNPK  T5+4(5),BLKZ
         OI    T5+8,X'F0'
         OI    D+7,X'0F'
         UNPK  T5+18(5),D
         ZAP   SATZZ,=P'1'    GRUNDSTELLUNG SATZZAEHLER
         PUT   MSGPRINT       ZEILENVORSCHUB
         SP    ZZ,=P'2'           ZEILENZAEHLER
         BH    DD230
         BAL   R5,DD280       NEUE SEITE
DD230    EQU   *
         MVC   0(5,R1),=X'0028000011' WSP2,L=40
         MVC   5(35,R1),T5
         PUT   MSGPRINT
         SP    ZZ,=P'2'           ZEILENZAEHLER
         BH    DD232
DD231    MVC   0(5,R1),=X'000600000B' SP1
         PUT   MSGPRINT
         BAL   R5,DD280           NEUE SEITE
         B     DD240
DD232    SP    ZZ,=P'2'
         BNH   DD231
         AP    ZZ,=P'2'
         B     DD240
DD235    PUT   MSGPRINT           VORSCHUB
         SP    ZZ,=P'2'           ZEILENZAEHLER ERSCHOEPFT ?
         BH    DD240              NEIN
         BAL   R5,DD280           NEUE SEITE
DD240    UNPK  T6+4(5),SATZZ  SATZZAEHLER
         OI    T6+8,X'F0'
         CVD   R8,D           SATZLAENGE
         OI    D+7,X'0F'
         UNPK  T6+18(5),D
         SP    ZZ,=P'2'       ZEILENZAEHLER
         BH    DD245
         BAL   R5,DD280       NEUE SEITE
         SP    ZZ,=P'2'
DD245    MVC   0(5,R1),=X'001C000011' WSP2,L=28
         MVC   5(23,R1),T6
         TM    SRECFM,X'48'       V(B)S ?
         BNO   DD250              NEIN
         CLI   2(R6),0            SPANNED ?
         BE    DD250              NEIN
         MVC   28(16,R1),=C', SPANNED RECORD'
         MVI   1(R1),16+28        SATZLAENGE KORRIGIEREN
DD250    EQU   *              SATZ AUFBEREITEN UND DRUCKEN          N
         SR    R5,R5
         IC    R5,DCBKEYLE
         ST    R8,SAVER8
         LTR   R5,R5              KEYLEN=0 ?
         BZ    DD255              JA
         L     R6,SAVER6          ADRESSE DES SCHLUESSELS
         LR    R8,R5              LAENGE DES SCHLUESSELS
         BAL   R5,DD600           SCHLUESSEL ANDRUCKEN
         MVC   0(5,R1),=X'0006000003' SP0
DD255    L     R8,SAVER8
         L     R6,SDD200+4
         BAL   R5,DD600           DATEN ANDRUCKEN
         SP    D,D                LOESCHEN
         LM    R5,R6,SDD200
         BR    R5                                                   B
         SPACE 2
*        UNTERPROGRAMM                                              T
*     EINGABE   R5 RETURN                                           C
*     AUSGABE   UEBER MSGPRINT                                      C
*               SCHLUSSZEILE,                                       C
*               VORSCHUB AUF NEUE SEITE,                            C
*               KOPFZEILE                                           C
         SPACE 1
DD280    EQU   *
         MVC   0(5,R1),=X'0087000089' WSK1, L=135
         MVC   5(130,R1),T7
         PUT   MSGPRINT       KOPFZEILE                             I
         MVC   0(5,R1),=X'0089000011' WSP2, L=137
         MVC   5(L'T12,R1),T12    JOB-, STEP-, DS-NAME
         PUT   MSGPRINT
         MVC   0(5,R1),=X'0087000019' WSP3, L=135
         MVC   5(130,R1),T7
         PUT   MSGPRINT
         ZAP   ZZ,=P'60'          ZEILENZAEHLER IN GRUNDST.
         BR    R5
*
*
*   EINGABE    R5   RETURN
*              R6   ADRESSE DES AUSZUGEBENDEN BEREICHES
*              R8   LAENGE  DES AUSZUGEBENDEN BEREICHES
*   AUSGABE    UEBER MSGPRINT
*              BLOCKZAEHLER UND -LAENGE (D)
*              SATZZAEHLER  UND  LAENGE (R8)
*              BEREICH   N U R   IM CHARACTERFORMAT
*              NUR BEI DEM 1. SATZ EINES BLOCKES IST D > 0
*
*
DD300    EQU   *
         BAL   R14,DD800          PRUEFUNG, OB SATZ GEDRUCKT WERDEN SOL
         LTR   R15,R15            SATZ DRUCKEN ?
         BZR   R5                 NEIN
         STM   R5,R6,SDD300
         BCTR  R8,0                    LAENGENSCHLUESSEL - 1
         STC   R8,DD310+1
DD310    TRT   0(0,R6),TRTAB4          ALLES DRUCKBARE ZEICHEN
         BNE   DD380                   NEIN
         MVC   LZ1+124(6),=X'402020202120'       SCHABLONE SATZZAEHL
         ED    LZ1+124(6),SZAEH        SATZNR AUSGEBEN
DD320    EQU   *
         LTR   R8,R8
         BM    DD380
         PUT   MSGPRINT
         MVC   0(130,R1),LZ1           ZEILE LOESCHEN
         MVC   LZ1+124(6),LZ1+100
         SH    R8,=H'100'
         BNM   DD340
         AH    R8,=H'100'
         STC   R8,DD330+1
DD330    EQU   *
         MVC   5(0,R1),0(R6)
         MVI   4(R1),X'11'             VORSCHUB WSP2
         SP    ZLZ,=P'2'
         BH    DD380
         BAL   R5,DD390
         B     DD380
DD340    EQU   *
         MVC   5(100,R1),0(R6)         UEBERTRAGEN
         LA    R6,100(,R6)
         SP    ZLZ,=P'1'
         BH    DD320
         BAL   R5,DD390
         B     DD320
*
*   EINGABE    R5   RETURN
*   AUSGABE    UEBER MSGPRINT
*              SCHLUSSZEILE
*              VORSCHUB AUF NEUE SEITE
*              KOPFZEILE
*
DD380    EQU   *
         AP    SZAEH,=P'1'
         LM    R5,R6,SDD300
         BR    R5
DD390    EQU   *
         PUT   MSGPRINT
         MVC   0(5,R1),=X'000600000B' SP1
         PUT   MSGPRINT
         MVC   0(5,R1),=X'0087000089' WSK1, L=135
         MVC   5(130,R1),T11
         PUT   MSGPRINT           FUSSZEILE, VORSCHUB NEUE SEITE
         MVC   0(5,R1),=X'0089000011' WSP2, L=137
         MVC   5(L'T12,R1),T12    JOB-, STEP-, DS-NAME
         PUT   MSGPRINT               KOPFZEILE
         MVC   0(5,R1),=X'0087000019' WSP3, L=135
         MVC   5(130,R1),T10
         ZAP   ZLZ,=P'55'
         BR    R5
*   E O F   E I N G A B E D A T E I                                 N
         SPACE 1
*
*
DD400    EQU   *
         MSG   1,.,D
         MSG   1,.,D
         MSG   20,T8,C        END_OF_DATA                           I
DD410    EQU   *
         L     R6,DEND
         MSG   130,(R6),C
         LH    R0,DCBBLKSI        EINLESEBEREICH ...
         L     R1,SAVER1          ... WIEDER ...
         LTR   R1,R1              ... FREIGEBEN ?
         BZ    DD170              NEIN, GETMAIN NOCH NICHT DURCHGEF.
      FREEMAIN R,LV=(0),A=(1)
         LA    R4,SYSIN
         TM    DCBOFLGS,DCBOFOPN  OPEN ?
         BZ    DD170              NEIN, ANDERE DATEIEN ABSCHLIESSEN
         CLOSE SYSIN
         CLC   CTLLO,=20X'FF'     WAR EOF AUF SYSIN ?
         BE    DD170              JA, ANDERE DATEIEN ABSCHLIESSEN
         MSG   80,KARTE+2,C       ZULETZT GELESENE AUSWAHLKARTE DRUCKEN
         B     DD170          SCHLIESSEN DATEIEN, ENDE              B
*
*        UNTERPROGRAMM: BLOCK LESEN
*   EINGABE    R5 RETURN
*   AUSGABE    R1 WEIST AUF EINGELESENEN BLOCK HINTER SCHLUESSEL
*
DD500    TS    MM500              1. ANSPRUNG ?
         BNZ   DD510              NEIN
         LH    R0,DCBBLKSI        EINLESE- ...
         GETMAIN R,LV=(0)         ... BEREICH ...
         ST    R1,SAVER1          ... BESCHAFFEN
DD510    L     R6,SAVER1
         LH    R8,DCBBLKSI
         READ  ECBIN,SF,DATA,(6),(8)
         CHECK ECBIN
         ST    R6,SAVER6          A(SCHLUESSEL) RETTEN
         SR    R1,R1
         IC    R1,DCBKEYLE
         AR    R1,R6              A(DATENTEIL)
         BR    R5                 RETURN
*
*        UNTERPROGRAMM: SCHLUESSEL BZW. DATEN ANDRUCKEN
*     EINGABE  R5 RETURN
*               R6 AUSZUGEBENDER BEREICH
*               R8 LAENGE AUSZUGEBENDER BEREICH
*
         DC    F'0'
DD600    ST    R5,DD600-4
DD610    PUT   MSGPRINT
DD615    EQU   *
         MVC   0(5,R1),=X'0087000009' WSP1,LAENGE=135
         UNPK  05(15,R1),00(8,R6) UMWANDELN IN HEX. FORMAT
         UNPK  19(15,R1),07(R8,R6)
         UNPK  33(15,R1),14(8,R6)
         UNPK  47(15,R1),21(8,R6)
         UNPK  61(15,R1),28(8,R6)
         MVC   ZWF(5),35(R6)
         UNPK  75(11,R1),ZWF(6)
         TR    5(80,R1),TRTAB1-240
         MVC   85(10,R1),=CL130' '
         MVC   86(7,R1),SCHAB
         ED    86(7,R1),SZZ
         AP    SZZ,=P'40'
         SH    R8,=H'40'          KURZSATZ ?
         BNH   DD620              JA
         MVC   95(40,R1),0(R6)    CHAR. FORMAT
         TR    95(40,R1),TRTAB2   UNDRUCKBAR NACH *
         LA    R6,40(,R6)
         SP    ZZ,=P'1'           ZEILENZAEHLER
         BP    DD610              KEIN SEITENWECHSEL
         MVC   ZWSP,0(R1)
         MVC   0(5,R1),=X'0006000013' SP2
         PUT MSGPRINT
         BAL   R5,DD280
         MVC   0(135,R1),ZWSP     ALTEN PUFFER LADEN
         PUT   MSGPRINT
         SP    ZZ,=P'1'           ZEILENZAEHLER
         B     DD615
DD620    EQU   *
         LCR   R15,R8
         AR    R15,R15
         LA    R15,49(,R15)
         AH    R8,=H'40'
         LA    R14,5(R8,R1)
         AR    R14,R8
         STC   R15,*+5
         MVC   0(,R14),=CL130' '
         SP    SZZ,=P'40'
         MVC   86(7,R1),SCHAB
         ED    86(7,R1),SZZ
         BCTR  R8,0
         STC   R8,*+5
         MVC   95(,R1),0(R6)      CHAR. FORMAT
         STC   R8,*+5
         TR    95(,R1),TRTAB2     UNDRUCKBAR NACH *
         SP    ZZ,=P'1'
         PUT MSGPRINT
         MVC   0(5,R1),=X'0006000013' SP2
DD690    L     R5,DD600-4
         BR    R5
         SPACE 3
*
*        STEUERKARTE LESEN UND AUFBEREITEN
*
         DC    A(0)
DD700    ST    R14,*-4
         MVC   CTLLO,=XL20'0'
         MVC   CTLHI,=XL20'0'
         MVI   CTLLOC,C' '
         MVC   CTLLOC+1(19),CTLLOC
         MVI   CTLHIC,0           MM *OBERE SCHRANKE FEHLT*
         MVC   CTLLEN,=H'1'
         MVC   CTLPOS,=H'1'
         GET   SYSIN,KARTE+2
         LA    R1,KARTE+81
DD705    EQU   *
         CLI   0(R1),C' '         LAENGE DES CHAR-STRING BERECHNEN
         BNE   *+8
         BCT   R1,DD705
         LA    R0,KARTE+2
         SR    R1,R0              LEERKARTE ?
         BNL   *+8                NEIN
         BAL   R14,DD190          FEHLERHAFTE KARTE, ABBRUCH
         LA    R1,1(,R1)
         STH   R1,KARTE           LAENGENSCHLIESSEL
         LA    R1,=A(KARTE)
         SM02  (CTLLOC,CTLFMT,CTLLEN,CTLPOS,CTLHIC,CTLDUM)
         CLI   CTLFMT,C'C'        FORMAT = CHAR ?
         BNE   DD710              NEIN
         MVC   CTLLO,CTLLOC
         MVC   CTLHI,CTLHIC
         B     DD740
DD710    CLI   CTLFMT,C'F'        FORMAT = BIN FIXED ?
         BNE   DD720              NEIN
         LA    R1,=A(KARTE)
         SM02  (CTLLOF,CTLFMT,CTLLEN,CTLPOS,CTLHIF,CTLDUM)
         LH    R1,CTLLEN
         CH    R1,=H'4'           BEI BIN MAX. 4 STELLEN
         BNH   *+8
         BAL   R14,DD190          FEHLERHAFTE KARTE, ABBRUCH
         LA    R14,4
DD715    SR    R14,R1
         LA    R15,CTLLOF(R14)
         MVC   CTLLO,0(R15)
         LA    R15,CTLHIF(R14)
         MVC   CTLHI,0(R15)
         B     DD740
DD720    EQU   *
         CLI   CTLFMT,C'X'        FORMAT = HEX ?
         BNE   DD730              NEIN
         LH    R1,CTLLEN
         CH    R1,=H'7'           BEI HEX MAX. 7 STELLEN ERLAUBT
         BNH   *+8
         BAL   R14,DD190          FEHLERHAFTE KARTE, ABBRUCH
         AR    R1,R1              LAENGE * 2
         EX    R1,TR              TR    CTLLOC(0),TRTAB5
         LH    R14,CTLLEN
         SLL   R14,4              LEENGENSCHLUESSSEL 1.OPERAND
         LH    R1,CTLLEN
         AR    R1,R1              LAENGENSCHLUESSEL 2.OPERAND
         OR    R14,R1
         EX    R14,PACK1          PACK  CTLLO(0),CTLLOC(0)
         CLI   CTLHIC,0           HI ANGEGEBEN ?
         BE    DD740              NEIN
         EX    R1,TR1             TR    CTLHIC(0),TRTAB5
         EX    R14,PACK2          PACK  CTLHI(0),CTLHIC(0)
         B     DD740
DD730    EQU   *
         CLI   CTLFMT,C'P'        GEPACKT ?
         BE    *+8                JA
         BAL   R14,DD190          FALSCHE STEUERKAREE
         LA    R1,=A(KARTE)
         SM02  (CTLLOP,CTLFMT,CTLLEN,CTLPOS,CTLHIP,CTLDUM)
         LH    R1,CTLLEN
         CH    R1,=H'15'          MAX. 15-STELLIG
         BNH   *+8
         BAL   R14,DD190          FEHLERHAFTE KARTE, ABBRUCH
         LA    R14,15
DD740    EQU   *
         LH    R0,CTLPOS
         SH    R0,=H'1'
         BL    DD190              FEHLERHAFTE KARTE, ABBRUCH
         STH   R0,CTLPOS
         CLI   CTLLEN+1,0         LAENGE PRUEFEN
         BNE   *+8
         BAL   R14,DD190
         CLC   CTLLEN,=H'20'
         BNH   *+8
         BAL   R14,DD190
         CLI   CTLHI,0            HI >= LO PRUEFEN
         BE    DD780              HI NICHT ANGEGEBEN
         CLC   CTLLO,CTLHI
         BNH   *+8
         BAL   R14,DD190
DD780    L     R14,DD700-4
         BR    R14
DD790    EQU   *                  EOF SYSIN
         MVC   CTLLO,=20X'FF'
         XC    CTLHI,CTLHI
         B     DD780              RETURN
         SPACE 3
*
*        PRUEFUNG, OB SATZ GEDRUCKT WERDEN SOLL
*
*        EINGABE.
*              R6   A(SATZ)
*
         DC    A(0)
DD800    ST    R14,*-4
         CLI   CTLLOC,0           AUSWAHLKARTE VORHANDEN ?
         LA    R15,4              MM *SATZ AUSWAEHLEN*
         BER   R14                NEIN, ALLE SAETZE DRUCKEN
DD810    LH    R1,CTLLEN
         BCTR  R1,0
         LH    R15,CTLPOS
         LA    R0,1(R15,R1)
         CR    R0,R8              FELD IM SATZ ?
         BH    DD830              NEIN, NICHT AUSWAEHLEN
         AR    R15,R6
         CLI   CTLFMT,C'P'        GEPACKT ?
         BE    DD812              JA
         EX    R1,CLC1            CLC   0(,R15),CTLLO
         B     DD814
DD812    EQU   *
         LR    R14,R1             FELDLAENGE - 1
         EX    R14,TRT1           TRT   0(,R15),TRTAB6
         BNH   DD830              NICHT NUMERISCH
         TRT   0(1,R1),TRTAB7     LETZTES BYTE MIT VORZEICHEN ?
         BNZ   DD830              NEIN, NICHT AUSWAEHLEN
         LR    R1,R14             RESTORE R1
         SLL   R1,4               LAENGENSCHLUESSEL 1.OPERAND
         EX    R1,CP1             CP    0(,R15),CTLLOP
DD814    EQU   *
         BL    DD830              SATZ NICHT AUSWAEHLEN
         BE    DD840              SATZ AUSWAEHLEN
         CLI   CTLHIC,0           OBERE SCHRANKE BEKANNT ?
         BE    DD830              NEIN
         CLI   CTLFMT,C'P'        GEPACKT ?
         BE    DD816              JA
         EX    R1,CLC2            CLC   0(,R15),CTLHI
         B     DD818
DD816    EQU   *
         EX    R1,CP2             CP    0(,R15),CTLHIP
DD818    EQU   *
         BNH   DD840              SATZ AUSWAEHLEN
DD820    EQU   *
         BAL   R14,DD700          STEUERKARTE LESEN, AUFBEREITEN
         B     DD810
DD830    EQU   *                  SATZ NICHT AUSWAEHLEN
         SR    R15,R15            MM *NICHT AUSWAEHLEN*
         B     DD890
DD840    EQU   *                  SATZ AUSWAEHLEN
         SP    ZZ,=P'2'
         MSG   80,KARTE+2,C
         CLI   CTLHIC,0           OBERE GRENZE VORHANDEN ?
         BNE   DD880              JA
         BAL   R14,DD700          NEIN, NEUE KARTE LESEN
DD880    EQU   *
         LA    R15,4              MM *SATZ AUSWAEHLEM*
DD890    EQU   *
         L     R14,DD800-4
         BR    R14
         SPACE 3
*        U E B E R S C H R I F T   A U F B E R E I T E N              *
         DC    A(0)
DD900    ST    R14,*-4
       EXTRACT ATIOT,FIELDS=(TIOT)
         L     R1,ATIOT
         MVC   T12+4(8),0(R1)     JOBNAME
         MVC   T12+20(8),8(R1)    STEPNAME
         MVC   T12+36(8),16(R1)   PROCSTEPNAME
         MVC   T12+51(44),JFCBDSNM DSNAME
         TAG   T12+099,NN         DATUM
         ST    R0,D               ZEIT (HHMMSSZH)
         OI    D+3,X'0F'          VORZEICHEN
         SRP   D(4),64-1,5
         MVC   T12+110(10),=X'402021207A20207A2020'
         ED    T12+110(10),D      ZEIT AUFBEREITEN
         L     R14,DD900-4
         BR    R14
         EJECT
         LTORG
ATIOT    DC    A(0)
BLKCNT   DC    PL3'100'
BLKZ     DC    PL3'1'
CLC1     CLC   0(,R15),CTLLO
CLC2     CLC   0(,R15),CTLHI
CP1      CP    0(,R15),CTLLOP
CP2      CP    0(,R15),CTLHIP
CTLDUM   DS    CL80               DUMMY
CTLFMT   DC    C'C'               FORMAT X,C,P ODER F
CTLHI    DC    XL20'0'            KENNBEGRIFF HOCH
CTLHIC   DC    XL20'0'            KENNBEGRIFF HOCH IN KARTE
CTLHIF   DC    F'0'
CTLHIP   DC    PL15'0'
CTLLEN   DC    H'1'               L'SUCHBEGRIFF IN SATZ
CTLLO    DC    XL20'0'            SUCHBEGRIFF NIEDRIG
CTLLOC   DC    XL20'0'            SUCHBEGRIFF NIEDRIG IN KARTE
CTLLOF   DC    F'0'
CTLLOP   DC    PL15'0'
CTLPOS   DC    H'1'               POSITION SUCHBEGRIFF IN SATZ
D        DC    D'0'
DDROUT   DC    A(DD200)
DEND     DC    A(T7)
DEVT     DC    X'0'               DEVICE TYPE
DUEB     DC    A(T7)
H        DC    H'0'
INSTR1   PACK  BLKCNT,0(,R4)
INSTR2   PACK  SKPBLK,0(,R4)
KARTE    DC    H'80',CL80' '
LRECL    DC    H'0'               URSPRUENGL. LRECL
LZ1      DC    H'130,0',X'09',CL130' '
MM500    DC    X'0'               MM *1. ANSPRUNG DD500*
PACK1    PACK  CTLLO(0),CTLLOC(0)
PACK2    PACK  CTLHI(0),CTLHIC(0)
RECFM    DC    H'0'               URSPRUENGL. RECFM
SATZZ    DC    PL3'1'
SAVER1   DS    F
SAVER6   DS    F
SAVER8   DS    F
SCHAB    DC    X'4020204B202120'
SDD200   DC    2F'0'
SDD300   DC    2F'0'
SKPBLK   DC    PL3'0'
SRECFM   DC    C' '
SZAEH    DC    PL3'1'
SZZ      DC    PL3'0'
TR       TR    CTLLOC(0),TRTAB5
TRT1     TRT   0(,R15),TRTAB6
TR1      TR    CTLHIC(0),TRTAB5
ZLZ      DC    PL2'55'            ZEILENZAEHLER
ZWF      DC    XL6'00'
ZWSP     DC    CL135' '
ZZ       DC    PL2'55'            ZEILENZAEHLER
TRTAB1   DC    C'0123456789ABCDEF'
TRTAB2   DC    C'****************'  0
         DC    C'****************'  1
         DC    C'****************'  2
         DC    C'****************'  3
         DC    C' **********.<(+×'  4
         DC   C'&&********* $*);^'  5
         DC    C'-/*********,%->?'  6
         DC    C'**********:#@''="' 7
         DC    C'****************'  8
         DC    C'****************'  9
         DC    C'****************'  A
         DC    C'****************'  B
         DC    C'*ABCDEFGHI******'  C
         DC    C'*JKLMNOPQR******'  D
         DC    C'* STUVWXYZ******'  E
         DC    C'0123456789******'  F
TRTAB3   DC    107X'0'
         DC    C','
         DC    148X'0'
TRTAB4   TRTAB ND
TRTAB5   DC    XL193'0'
         DC    X'FAFBFCFDFEFF',XL41'0'
         DC    C'0123456789'
TRTAB6   TRTAB N1
TRTAB7   TRTAB N2
T1       DC    C'RECFM-ANGABE FEHLT, UNDEFINED ANGENOMMEN'
T2       DC    C'BLKSIZE-ANGABE FEHLT, 32767 ANGENOMMEN'
T3       DC    C'RECFM=U , BLKSIZE=     , LRECL=     , KEYLEN=   '
T4       DC    C'LRECL ANGABE FEHLT, LRECL=BLKSIZE ANGENOMMEN'
T5       DC    CL35'BLK      , LENGTH'
T6       DC    CL23'REC      , LENGTH      '
T7       DC    CL130'01......05........10........15........20........25*
               ........30........35........40'
         ORG   T7+90
         DC    C'1...5...10....5...20....5...30....5...40'
         ORG   T7+130
T8       DC    CL20'E N D   O F  D A T A'
T9       DC    CL28'BLKSIZE ODER LRECL UNBEKANNT'
T10      DC    CL130'1...5...10....5...20....5...30....5...40....5...50X
               ....5...60....5...70....5...80....5...90....5..100      X
                            SATZNR     '
T11      DC    C'1...5...10....5...20....5...30....5...40....5...50....X
               5...60....5...70....5...80....5...90....5..100          X
                                   '
         DC    C' '
T12      DC    CL132'JOB=12345678   STEP=12345678   PROC=12345678   DSN=
               =1       10        20        30        40   4 AM 12.01.76
               6 UM 123:59:59'
DATA     DCB   DSORG=PS,MACRF=R,DDNAME=DATA,EODAD=DD400,EXLST=EXLST
EXLST    DS    0F
         DC    X'87',AL3(JFCB)
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=DD790,RECFM=FB
         LTORG
DSCB1    DS    0D
DS1FMTID DS    C
         ORG   DSCB1+54
DS1LSTAR DS    CL3
         ORG   DSCB1+104
JFCB     DS    0F
         IEFJFCBN
DSCAMLST CAMLST SEARCH,JFCBDSNM,JFCBVOLS,DSCB1
         PRINT NOGEN
         DCBD  DSORG=QS
         END
