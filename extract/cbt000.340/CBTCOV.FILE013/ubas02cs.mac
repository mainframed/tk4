ZS905EKL CSECT
         REG
         XSAVE (R12),SVA,ZS905EKL ZIMPELMANN
         L     R3,0(R1)
         LA    R3,0(R3)                VL-SWITCHBIT LOESCHEN
         LTR   R3,R3
         BZ    CLOSDAT
         USING Y1,R3
         LA    R7,SYSIN
         TM    DCBOFLGS-IHADCB(R7),DCBOFOPN    SYSIN OPEN ????
         BO    INITIAL
         ST    R3,SAVR3                        REGISTER 3 SPEICHERN
         OPEN  (SYSIN,(INPUT))
         TM    DCBOFLGS-IHADCB(R7),DCBOFOPN     SYSIN-DATEI DA???
         BO    NOWTR
         TM    Y1WFLG,Y1WYES                    WTOR ERWUENSCHT???
         BO    YESWT
         LA    R9,9                              SYSIN-DATEI FEHLT
         B     FEHLER
YESWT    EQU   *
         MVI   DCBOFLGS-IHADCB(R7),DCBOFOPN      OPEN-BIT ANMACHEN
         LA    R10,WTREING
         ST    R10,DCBGET-IHADCB(R7)             GET-ADRESSE ERSETZEN
NOWTR    EQU   *
         LH    R7,Y1ESIZ            MAX. KOMPRIMIERTE EINGABE-GROESSE
         LH    R8,Y1TSIZ            MAX. GROESSE DER BLOCKTABELLE
         AR    R7,R8                SUMME DER BEIDEN GROESSEN
         GETMAIN R,LV=(R7)    SPEICHERPL.FUER EINGABEKARTE UND BLKTAB
         ST    R1,Y1TADR               SPEICHERN TABELLENADRESSE
INITIAL  EQU   *
         L     R1,Y1TADR
         AH    R1,Y1ESIZ
         ST    R1,TABADR               ADRESSE DER BLOCKTABELLE
         LR    R2,R1
         LA    R2,12(R2)               ADRESSE 1.EINTRAG
         ST    R2,0(R1)        ADRESSE 1.EINTRAG INS 1.TABELLENWORT
         ST    R2,4(R1)        ADRESSE LETZTER EINTR.INS 2. TABWORT
         LR    R2,R1
         AH    R2,Y1TSIZ
         ST    R2,8(R1)        SPEICHERN ADRESSE LETZTER VALUE-EINTRAG
NOOPEN   EQU   *
         L     R7,Y1TADR
         LA    R7,2(R7)                LAENGENFELD UEBERSPRINGEN
         LR    R8,R7
         LA    R10,0                   KLAMMER GESCHLOSSEN
LOOP2    EQU   *
         LA    R11,0                   1. EINGABEKARTE
         GET   SYSIN,INAREA1
         MVC   INAREA,INAREA1
         CALL  ZS905WRF,((R3),INAREA2),VL
         SR    R9,R9                   SCHALTER (FORTSETZUNG) LOESCHEN
         MVC   PRFELD,PRFELD1
         TR    PRFELD,INAREA           EINGABEKARTE UMDREHEN
         SR    R1,R1
         SR    R2,R2
         TRT   PRFELD,TRTTAB1         1.NO-BLANK-ZEICHEN SUCHEN
         BNZ   NOFEHL
         LA    R9,10                  EINGABEKARTE LEER
         B     FEHLER
NOFEHL   EQU   *
         LA    R6,PRFEND
         SR    R6,R1                  LAENGE DER EINGABE
         B     *(R2)
         B     KOMPR                  KEINE FORTSETZUNGSKARTE
         BCTR  R6,0                FORTSETZUNGSZEICHEN UEBERSPRINGEN
         LA    R9,1                    SCHALTER (FORTSETZUNG) SETZEN
         LA    R4,L'INAREA            LAENGE EINGABEBEREICH
         SR    R4,R6           LAENGE EINGABE MIT FORTSETZUNGSZEICHEN
         LA    R4,PRFELD(R4)
         SR    R1,R1
         SR    R2,R2
         BCTR  R6,0
         EX    R6,EXTRT1              SUCHEN NAECHSTES NO-BLANK
         BNZ   NOFEHL1
         LA    R9,10                  LEERE EINGABEKARTE
         B     FEHLER
NOFEHL1  EQU   *
         LA    R6,1(R6)
         SR    R1,R4
         SR    R6,R1            LAENGE EINGABE OHNE FORTSETZUNGSZ.
KOMPR    EQU   *
         LA    R4,INAREA               ADRESSE EINGABE
LOOP1    EQU   *
         LTR   R6,R6
         BZ    KOMPEND
         BCTR  R6,0
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT1              SUCHEN NAECHSTES NO-BLANK
         SR    R1,R4
         LTR   R1,R1                  NAECHSTES ZEICHEN = BLANK
         BZ    NOBLANK                NEIN
         SR    R6,R1                  EINGABELAENGE REDUZIEREN
         LA    R4,0(R1,R4)  EINGABEADRESSE UM ANZAHL BLANKS ERHOEHEN
         LTR   R10,R10                 KLAMMER GEOEFFNET ???????
         BZ    NOBLAMOV                NEIN
         SR    R4,R1        EINGABEADRESSE UM ANZ. BLANKS REDUZIEREN
         BCTR  R1,0
         EX    R1,EXMVC1               BLANKS UEBERTRAGEN
         LA    R4,1(R1,R4)  EINGABEADRESSE UM ANZAHL BLANKS ERHOEHEN
         LA    R7,1(R1,R7)  AUSGABEADRESSE UM ANZAHL BLANKS ERHOEHEN
         B     NOBLANK
NOBLAMOV EQU   *
         LTR   R11,R11                 1.EINGABEKARTE ???????
         BZ    NOBLANK                 JA
         CLI   0(R4),C'('              PARAMETER MIT KLAMMER
         BE    NOBLANK                 JA
         MVI   0(R7),X'40'             1 BLANK IN DIE AUSGABE
         LA    R7,1(R7)                ADRESSE AUSGABE ERHOEHEN
NOBLANK  EQU   *
         MVC   0(1,R7),0(R4)           ZEICHEN IN AUSGABE BRINGEN
         CLI   0(R7),C'('
         BNE   NOKLAUF
         LA    R10,1                   KLAMMER-KZ SETZEN (OEFFNEN)
NOKLAUF  EQU   *
         CLI   0(R7),C')'
         BNE   NOKLZU
         LA    R10,0                 KLAMMER-KZ LOESCHEN (SCHLIESSEN)
NOKLZU   EQU   *
         CLI   0(R7),X'7D'             HOCHKOMMA IN EINGABE?????
         BNE   NOHOKOM                 NEIN
         BCTR  R4,0
         CLI   0(R4),C'('              HOCHKOMMA NACH KLAMMER-AUF
         BE    NOTYPHKO                JA
         CLI   0(R4),C','              HOCHKOMMA NACH KOMMA
         BE    NOTYPHKO                JA
         CLI   0(R4),X'7D'             HOCHKOMMA NACH HOCHKOMMA
         BE    NOTYPHKO                JA
         CLI   0(R4),X'40'             HOCHKOMMA NACH BLANK
         BE    NOTYPHKO                JA
         LA    R7,1(R7)               HOCHKOMMA FUER TYPBEZEICHNUNG
NOTYPHKO EQU   *
         LA    R4,2(R4)
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT2               SUCHEN 2. HOCHKOMMA
         BNZ   HOKOMDA
         LA    R9,11                   2.HOCHKOMMA FEHLT
         B     FEHLER
HOKOMDA  EQU   *
         SR    R1,R4
         SR    R6,R1
         LTR   R1,R1
         BNZ   KEYMOV
         LA    R9,12                   LEERE KEYEINGABE
         B     FEHLER
KEYMOV   EQU   *
         BCTR  R1,0
         EX    R1,EXMVC1             ZEICHEN ZWISCHEN DEN HOCHKOMMATA
         BCTR  R6,0
         LA    R7,0(R1,R7)           AUSGABEADRESSE ERHOEHEN
         LA    R4,1(R1,R4)           EINGABEADRESSE ERHOEHEN
         CLI   1(R4),X'7D'
         BNE   NOHOKOM
         LA    R7,1(R7)
         MVI   0(R7),X'7D'
NOHOKOM  EQU   *
         LA    R11,1
         LA    R4,1(R4)
         LA    R7,1(R7)
         B     LOOP1
KOMPEND  EQU   *
         LTR   R9,R9                 FORTSETZUNGSKARTE ???
         BZ    NOFORTS               NEIN
         MVI   0(R7),X'40'
         LA    R7,1(R7)
         B     LOOP2
NOFORTS  EQU   *
         C     R7,TABADR
         BNH   NOFEHL2
         LA    R9,71                 EINGABE-SIZE ZU KLEIN
         B     FEHLER
NOFEHL2  EQU   *
         SR    R7,R8                 LAENGE DER KOMPR. EINGABE
         L     R8,Y1TADR             ADRESSE KOMPR. EINGABE
         STH   R7,0(R8)              LAENGE RETTEN
         B     VERARB
WTREING  EQU   *
         ST    R14,WTORSVE
         MVI   INAREA1,C' '
         MVC   INAREA1+1(L'INAREA1-1),INAREA1
         NI    ECB1,X'00'
         WTOR  '*ZS905* EINGABEKARTE EINGEBEN,END EINGEBEN(BEI ENDE)', *
               INAREA1,80,ECB1
         WAIT  1,ECB=ECB1
         L     R14,WTORSVE
         BR    R14
VERARB   EQU   *
         CALL  ZS905ABT,((R3)),VL
         CLI   Y1RTCE,Y1RYES
         BE    ENDE
         LH    R9,Y1MSGC
         LTR   R9,R9                 FEHLER IN ZS905ABT ??????
         BZ    RETABFR               NEIN
         CLI   Y1TFLG,Y1TNO          TERM(NO) ???????????
         BE    ENDE                  JA
         SR    R9,R9
         STH   R9,Y1MSGC
         B     NOOPEN
RETABFR  EQU   *
         CLI   Y1POKZ,Y1PYES    POINT-KZ GESETZT ???
         BE    NOOPEN
         CLI   Y1RTCE,Y1RNO
         BNE   ENDE             RT-CODE AUF ENDE ODER RT-TERM GESETZT
         B     NOOPEN
CLOSDAT  EQU   *
         L     R3,SAVR3           LADEN REGISTER 3
         L     R10,Y1TADR         LADEN TABELLENADRESSE
         LH    R7,Y1ESIZ
         LH    R8,Y1TSIZ
         AR    R7,R8              TABELLENLAENGE LADEN
         FREEMAIN R,LV=(R7),A=(R10)
         CLOSE (SYSIN)
         B     ENDE
FEHLER   EQU   *
         STH   R9,Y1MSGC
         CALL  ZS905FHL,((R3)),VL
         LA    R9,9
         CH    R9,Y1MSGC
         BNE   MCODLOE
         MVC   Y1COCO,=H'8'
         B     ENDE
MCODLOE  EQU   *
         LA    R1,8
         CH    R1,Y1COCO
         BE    ENDE
         SR    R9,R9
         STH   R9,Y1MSGC
         B     NOOPEN
ENDE1    EQU   *
         MVI   Y1RTCE,Y1RYES
ENDE     EQU   *
         XRETURN 0
         EJECT
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,                                  *
               RECFM=FB,LRECL=80,                                      *
               MACRF=(GM),EODAD=ENDE1
         EJECT
WTORSVE  DS    F
TABADR   DS    F
ECB1     DS    F
SAVR3    DS    F
INAREA1  DS    CL80
INAREA2  DS    0CL133
         DC    CL1' '
INAREA   DS    CL72
         DC    CL60' '
PRFELD   DS    0CL72
         DC    CL72' '
PRFEND   DS    0CL1
PRFELD1  DC    72AL1(PRFELD1-*+L'INAREA-1)
TRTTAB1  DS    0CL256
         DC    256XL1'04'
         ORG   TRTTAB1+C'+'
         DC    XL1'08'
         ORG   TRTTAB1+C'-'
         DC    XL1'08'
         ORG   TRTTAB1+C' '
         DC    XL1'00'
         ORG
TRTTAB2  DS    0CL256
         DC    256XL1'00'
         ORG   TRTTAB2+X'7D'
         DC    XL1'04'
         ORG
EXTRT1   TRT   0(0,R4),TRTTAB1
EXTRT2   TRT   0(0,R4),TRTTAB2
EXMVC1   MVC   0(0,R7),0(R4)
         LTORG
         EJECT
Y1       ZS905COM
         EJECT
         PRINT NOGEN
         DCBD  DSORG=PS
         END
