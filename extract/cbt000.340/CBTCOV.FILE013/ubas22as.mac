//Z223068E JOB (6695,068,2),CLASS=1,MSGCLASS=O
/*JOBPARM LINES=9999
//ASM     EXEC PGM=IEV90,REGION=1024K,
//             PARM='TERM,NODECK,OBJECT,XREF(SHORT),BATCH'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//  DD  DSN=SYS1.AMODGEN,DISP=SHR
//  DD  DISP=SHR,DSN=SYS1.HASPSRC,UNIT=DISK,VOL=SER=DSR002
//  DD  DISP=SHR,DSN=LU.MACLIB
//SYSUT1   DD  SPACE=(CYL,(5,1)),UNIT=DISK
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
         TITLE 'ETPS - EMERGENCY TELE-PROCESSING SYSTEM'
***********************************************************************
**                                                                   **
**             E  T  P  S                                            **
**                                                                   **
***********************************************************************
**                                                                   **
** FUNCTION-                                                         **
**                                                                   **
** THIS PROGRAM IS INTENDED TO PROVIDE EMERGENCY TELE-PROCESSING     **
** SERVICES WHEN THE NORMAL SUBSYSTEMS SUCH AS JES2, VTAM, TCAM, ETC **
** ARE NOT AVAILABLE. IT IS INTENDED TO BE INVOKED AS A STARTED TASK,**
** WHICH MEANS IT MUST BE DEFINED AS A SECONDARY SUBSYSTEM IN ORDER  **
** TO BE ABLE TO START WHEN JES2 IS NOT AVAILABLE.                   **
**                                                                   **
**                                                                   **
** PARM -                                                            **
**                                                                   **
** THIS PROGRAM MUST BE INVOKED WITH A STANDARD EXECUTE-CARD FORMAT  **
** PARM. THE LENGTH MUST BE 8 BYTES, AS FOLLOWS:                     **
**                                                                   **
**    1. THE FIRST 4 CHARACTERS MUST BE "ETPS".                      **
**                                                                   **
**    2. THE 5TH BYTE IS TERMINAL TYPE. VALID TERMINAL TYPES ARE:    **
**                                                                   **
**            2 = 3278 MODEL 2                                       **
**            3 = 3279 MODEL 3B                                      **
**            4 = 3278 MODEL 4                                       **
**            5 = 3278 MODEL 5 (80-COLUMN MODE)                      **
**                                                                   **
**    3. THE LAST 3 BYTES MUST BE THE UNIT ADDRESS OF THE TERMINAL.  **
**                                                                   **
** AS AN EXAMPLE, //STEP1  EXEC  PGM=ETPS,PARM='ETPS49C4' STARTS     **
** TIME-SHARING ON A LOCAL 3278  MOD 4 AT ADDR 9C4.                  **
**                                                                   **
**                                                                   **
** LOGGING ON AND OFF -                                              **
**                                                                   **
** THE FIRST THING THE PROGRAM DOES IS PROMPT FOR A USER-ID AND      **
** PASSWORD. THE USER-ID IS COMPARED TO A LIST OF VALID USER-ID'S.   **
** THIS SHOULD BE CHANGED TO CHECK AGAINST UADS, MAYBE. SEE THE      **
** "USERTABL" IN THE CODE, BELOW.                                    **
**                                                                   **
** THE PASSWORD IS NOT CURRENTLY VALIDATED IN ANY WAY, BUT IS USED   **
** AS THE DEFAULT PASSWORD FOR DATASET ALLOCATIONS.                  **
**                                                                   **
** TO SHUT DOWN, ENTER "EOJ" IN THE USER-ID FIELD, OR HIT ANY PFK.   **
**                                                                   **
** THE LOGON MENU LOOKS LIKE:                                        **
**                                                                   **
**                                                                   **
**                                                                   **
**  ENTER USERID ==>_            (ONLY "BCOOK" IS VALID)             **
**  AND PASSWORD ==>             (DEFAULT DATASET PASSWORD)          **
**    RACF GROUP ==>             (OPTIONAL, SHOULD BE "SYS1")        **
**                                                                   **
**                                                                   **
** IF "LOGON" IS SUCCESSFUL, THE PRIMARY OPTION MENU (AS IN SPF) IS  **
** DISPLAYED, AND THE USER CAN BEGIN PROCESSING.                     **
**                                                                   **
**                                                                   **
** THE PRIMARY OPTION MENU LOOKS LIKE:                               **
**                                                                   **
** --------------------- ETPS PRIMARY OPTION MENU  ----------------  **
** SELECT OPTION==>_                                                 **
**                                                                   **
**  1  BROWSE       - DISPLAY SOURCE DATA                            **
**  2  EDIT         - CREATE OR CHANGE SOURCE DATA                   **
**  3  UTILITY      - PERFORM ETPS UTILITY FUNCTIONS          (NA)   **
**  4  FOREGROUND   - COMPILE, LINK-EDIT, ETC.                (NA)   **
**  5  BACKGROUND   - COMPILE, UTILITY, ETC.                  (NA)   **
**  6  COMMAND      - ENTER ETPS COMMAND                      (NA)   **
**  X  EXIT         - TERMINATE ETPS AND RETURN TO LOGON MENU        **
**                                                                   **
**  PRESS PFK 3 TO TERMINATE ETPS                                    **
**                                                                   **
**                                                                   **
**  (NA) INDICATES NOT AVAILABLE                                     **
**                                                                   **
**                                                                   **
**                                                                   **
**  IF OPTION 2 (EDIT) IS SELECTED, AN EDIT MENU IS DISPLAYED:       **
**                                                                   **
**                                                                   **
** --------------------- ETPS EDIT MENU  --------------------------  **
**                                                                   **
** ENTER DATASET NAME ==>_                                           **
**             VOLSER ==>          (IF NOT CATALOGED)                **
**           PASSWORD ==>          (IF PASSWORD-PROTECTED)           **
**                                                                   **
**                                                                   **
**                                                                   **
**                                                                   **
**  NOTE THAT DATASET NAMES MUST BE ENTERED AS FULLY-QUALIFIED       **
**       NAMES, NOT IN QUOTES.                                       **
**                                                                   **
**  NOTE THAT THE OPTION 1 (BROWSE) MENU IS ESSENTIALLY THE SAME.    **
**                                                                   **
**                                                                   **
**  ALSO, THERE IS A BUG IN THE CODE SUCH THAT YOU CANNOT ENTER      **
**  THE MEMBER NAME IN PARENS. THIS WILL BE FIXED WHEN I GET AROUND  **
**  TO IT.                                                           **
**                                                                   **
**  ONLY PS AND PO FILES ARE SUPPORTED.                              **
**                                                                   **
**  IF THE DATASET IS A PDS, YOU WILL BE PRESENTED WITH A MEMBER     **
**  LIST. YOU CAN SCROLL UP AND DOWN USING PFK7 AND 8.  A MEMBER     **
**  CAN BE SELECTED FOR EDIT BY ENTERING AN "S" IN THE COLUMN TO     **
**  THE LEFT OF THE MEMBER NAME. THERE IS A  "LOCATE" FUNCTION TO    **
**  POSITION YOURSELF IN THE LIBRARY, SIMILAR TO SPF; JUST TYPE      **
**  L MEM  ON THE COMMAND INPUT LINE.                                **
**                                                                   **
**  THE EDITOR IS A VERY PRIMITIVE TOOL. YOU CAN REPEAT LINES,       **
**  DELETE LINES, AND TYPE OVER LINES. YOU CAN COPY A LINE           **
**  AFTER ANOTHER LINE.                                              **
**                                                                   **
**  THAT'S IT. THIS IS NOT SPF.                                      **
**                                                                   **
**  TO DELETE A LINE, ENTER A "D" IN THE SEQUENCE NUMBER COLUMN AT   **
**  THE LEFT. THERE IS NO "DD" (DELETE RANGE) COMMAND.               **
**                                                                   **
**  TO REPEAT A LINE, ENTER AN "R" IN THE SEQUENCE NUMBER COLUMN AT  **
**  THE LEFT.  YOU CAN ONLY REPEAT ONE LINE AT A TIME.               **
**                                                                   **
**  TO COPY A LINE, ENTER A "C" IN THE SEQUENCE NUMBER COLUMN AT     **
**  THE LEFT, AND AN "A" ON THE LINE YOU WANT TO COPY IT AFTER.      **
**                                                                   **
**  YOU CAN TYPE OVER ANY OR ALL LINES ON THE SCREEN, BUT I HAVE NOT **
**  TESTED MIXING THIS WITH "D" AND "R" LINE COMMANDS, SO WATCH IT.  **
**                                                                   **
**  SCROLLING UP AND DOWN IS ACCOMPLISHED BY PF7 AND 8, AS IS USUAL, **
**  AND THE NUMBER OF LINES CAN BE ENTERED IN THE COMMAND LINE. ALSO **
**  "M" IS MAX, FOR TOP AND BOTTOM.                                  **
**                                                                   **
**                                                                   **
**  TO SEARCH FOR A CHARACTER STRING, ENTER "F " ON THE COMMAND LINE **
**  FOLLOWED BY THE CHARACTER STRING. IMBEDDED BLANKS ARE OK, BUT IF **
**  THE STRING HAS LEADING OR TRAILING BLANKS OR SINGLE QUOTES, YOU  **
**  HAVE TO PUT THE WHOLE SHOT IN SINGLE QUOTES. (PF5 IS REPEAT FIND)**
**                                                                   **
**  NOTE THAT "SAVE" IS THE DEFAULT, WHETHER OR NOT YOU HAVE CHANGED **
**       ANY CARDS IN THE MEMBER, SO ENTER "CANCEL" ON THE COMMAND   **
**       LINE IF YOU DON'T WANT THIS TO HAPPEN. (SEE PF KEYS BELOW)  **
**                                                                   **
**  TO SUBMIT THE JCL STREAM YOU ARE EDITING, JUST TYPE "SUB" ON     **
**  THE COMMAND LINE; THE JES2 JOB NUMBER WILL APPEAR IN THE MESSAGE **
**  AREA IN THE UPPER-RIGHT CORNER OF THE SCREEN. NOTE THAT IF YOU   **
**  ARE RUNNING AS A SUB-SYSTEM, YOU CANNOT SUBMIT JOBS. FOR THAT    **
**  REASON, YOU SHOULD ALWAYS HAVE 2 PROC'S IN SYS1.PROCLIB--ONE     **
**  DEFINED AS A SUB-SYSTEM IN IEFSSN01, AND ONE NOT SO DEFINED.     **
**  YOU SHOULD START THE SUB-SYSTEM IF JES IS NOT UP, AND START THE  **
**  OTHER IF YOU WILL NEED TO SUBMIT BATCH JOBS.                     **
**                                                                   **
**                                                                   **
**  IF YOU HAVE RACF, DON'T FORGET TO ADD BOTH STARTED TASK NAMES    **
**  TO ICHRIN03.                                                     **
**                                                                   **
**                                                                   **
***********************************************************************
**                                                                   **
**         GENERAL NOTES                                             **
**                                                                   **
**   PFK DEFINITIONS CANNOT BE CHANGED:                              **
**                                                                   **
**            PF1  - HELP                                            **
**            PF2  - SPLIT THE SCREEN                                **
**            PF3  - RETURN (IN EDIT, SAVE AND END)                  **
**            PF4  - CANCEL (IN EDIT, END NOSAVE)                    **
**            PF5  - REPEAT FIND IN EDIT OR BROWSE                   **
**            PF7  - SCROLL UP                                       **
**            PF8  - SCROLL DOWN                                     **
**            PF9  - SWITCH SCREENS (FOR SPLIT-SCREEN MODE)          **
**                                                                   **
**                                                                   **
**   ETPS IS IMPLEMENTED AS A STARTED TASK, AND IS DEFINED AS A      **
**   SUB-SYSTEM IN "SYS1.PARMLIB(IEFSSN01)". IT WILL START WITHOUT   **
**   JES BEING ACTIVE. THE JCL IS JUST AN EXEC CARD, SO THE CHANCES  **
**   OF A JCL ERROR ARE MINIMAL. ALSO, IF PUT IN "SYS1.PROCLIB",     **
**   IT SHOULD ALWAYS BE START-ABLE, UNLESS THAT LIBRARY IS DAMAGED. **
**                                                                   **
**   IT IS STARTED FROM THE CONSOLE BY ENTERING:                     **
**                                                                   **
**   S ETPS,TRM1=ETPS34AB    (THIS IS THE DEFAULT TERMINAL)          **
**                                                                   **
**                                                                   **
***********************************************************************
         TITLE 'ETPS - CHANGES TO BE DONE'
***********************************************************************
**                                                                   **
**  SCROLL   WHEN SCROLLING A PDS DIRECTORY MEMBER LIST, THE BOTTOM  **
**           LINE OF THE DISPLAY IS SOMETIMES NOT USED. THIS ALSO    **
**           HAPPENS WHEN IN SPLIT-SCREEN MODE.                      **
**                                                                   **
**  SCROLL   WHEN SCROLLING A PDS DIRECTORY MEMBER LIST DOWN AND UP, **
**           POSITIONING IS SOMEWHAT ERRATIC; THE SCROLL AMOUNT IS   **
**           USUALLY LESS THAN A PAGE.                               **
**                                                                   **
**  LIB MGMT NEEDS PANVALET SUPPORT.                                 **
**                                                                   **
**  ESTAE    NEEDS TO BE ADDED.                                      **
**                                                                   **
**  BROWSE   NEEDS EVERYTHING.                                       **
**                                                                   **
**  EDIT     WHEN RETURNING TO THE DIRECTORY LIST AFTER EDIT, THE    **
**           PDS MEMBER THAT WAS JUST EDITTED IS NOT AT THE TOP OF   **
**           THE DIRECTORY DISPLAY.                                  **
**                                                                   **
**  EDIT     NEEDS THE FOLLOWING:                                    **
**           1. COPY RANGE ("CC").                                   **
**           2. REPEAT RANGE ("RR").                                 **
**           3. DELETE RANGE ("DD").                                 **
**           4. COPY DATASET OR MEMBER.                              **
**           5. CREATE MEMBER.                                       **
**           6. PANVALET SUPPORT.                                    **
**           7. CHANGE AND REPEAT CHANGE.                            **
**                                                                   **
** UTILITY   NEEDS EVERYTHING ON THE MENU.                           **
**                                                                   **
** FOREGROUND NEEDS THE FOLLOWING:                                   **
**            1. ASSEMBLY.                                           **
**            2. LINK-EDIT.                                          **
**            3. IDCAMS.                                             **
**            4. SUPER-ZAP.                                          **
**            5. SMP.                                                **
**                                                                   **
** BACKGROUND NEEDS THE FOLLOWING:                                   **
**            1. ASSEMBLY.                                           **
**            2. LINK-EDIT.                                          **
**            3. IDCAMS.                                             **
**            4. SUPER-ZAP.                                          **
**            5. SMP.                                                **
**            6. DF/DSF.                                             **
**            7. DF/DSS OR FDR.                                      **
**                                                                   **
** COMMANDS   NEEDS THE FOLLOWING:                                   **
**            1. CALL.                                               **
**            2. SVC 34 INTERFACE.                                   **
**            3. DISPLAY UNITS/DASD.                                 **
**            4. SEND/WTO.                                           **
**            5. CONSOLE DISPLAY.                                    **
**            6. ETC.                                                **
**                                                                   **
***********************************************************************
         TITLE 'ETPS - CHANGE LOG  '
***********************************************************************
**                                                                   **
**  4/19/83  ADDED BYPASS OF LOGON PANEL FOR EMERGENCY USE. IF ETPS  **
**           IS INVOKED WITH A PARM BEGINNING WITH THE LETTERS "OP", **
**           A LOGON IS NOT REQUIRED.           BRIAN COOK       MNP **
**                                                                   **
**  6/10/83  CHANGED TUBE IO TO EXCP.           BRIAN COOK      MNP  **
**                                                                   **
**  6/11/83  ADDED UPDATE-IN-PLACE LOGIC FOR    BRIAN COOK      MNP  **
**           PDS MEMBERS THAT ARE EDITED AND                         **
**           SAVED. IF THE NUMBER OF CARDS IS                        **
**           THE SAME BEFORE AND AFTER THE                           **
**           EDIT, AN UPDATE-IN-PLACE IS DONE.                       **
**           THIS SHOULD REDUCE THE POSSIBILITY                      **
**           OF X37 ABENDS.                                          **
**                                                                   **
**  6/23/83  ADDED "COPY-AFTER"   TO EDIT       BRIAN COOK      MNP  **
**                                                                   **
**  8/01/83  ADDED SPLIT-SCREEN LOGIC           BRIAN COOK      MNP  **
**                                                                   **
** 12/05/83  RENAMED TO ETPS, ADDED "ET" LOGO.  BRIAN COOK      MNP  **
**                                                                   **
**  5/16/84  ADDED SPF STATS SUPPORT.           BRIAN COOK      MNP  **
**                                                                   **
***********************************************************************
         TITLE 'ETPS - WTO MESSAGES'
***********************************************************************
**                                                                   **
** USER WTO MESSAGES, ALL PREFIXED BY "ETPS" -                       **
**                                                                   **
** ALL "WTO" CODES START WITH 1000; THEIR MEANINGS ARE:              **
**                                                                   **
**     1001  -  INVALID PARM.                                        **
**                                                                   **
**     1002  -  DYNAMIC ALLOCATION OF TUBE FAILED.                   **
**                                                                   **
**     1003  -  OPEN FOR TUBE FAILED.                                **
**                                                                   **
**     1004  -  DYNAMIC UN-ALLOCATION OF TUBE FAILED.                **
**                                                                   **
**     1021  -  TUBE I/O ERROR ON READ AFTER CLEAR.                  **
**                                                                   **
**     1022  -  TUBE I/O ERROR                                       **
**                                                                   **
**     1023  -  DYNALLOC FAILED FOR LIB                              **
**                                                                   **
**     1025  -  DYNALLOC FAILED FOR LIB(MEM)                         **
**                                                                   **
**     1026  -  PREMATURE EOF (INVALID EODAD)                        **
**                                                                   **
**     1028  -  OPEN FAILED FOR PDS MEM                              **
**                                                                   **
**     1030  -  TUBE I/O ERROR                                       **
**                                                                   **
**     1032  -  DYNALLOC FAILED FOR LIB(MEM)                         **
**                                                                   **
**     1041  -  TUBE I/O ERROR ON WRITE ECB = X'41'.                 **
**                                                                   **
**     1051  -  OPEN  INPUT FAILED IN EDITOR MODULE.                 **
**                                                                   **
**     1061  -  OPEN OUTPUT FAILED IN EDITOR MODULE. THIS IS USUALLY **
**              DUE TO TRYING TO SUBMIT A JOB WHILE RUNNING AS A     **
**              SUB-SYSTEM.                                          **
**                                                                   **
**     1011  -  COMM NON-ZERO RC AFTER TUBE WRITE.                   **
**                                                                   **
**     1012  -  COMM BAD ECB  RC AFTER TUBE WRITE.                   **
**                                                                   **
**     1013  -  COMM NON-ZERO RC AFTER TUBE READ.                    **
**                                                                   **
**     1014  -  COMM BAD ECB  RC AFTER TUBE WRITE.                   **
**                                                                   **
**     1015  -  COMM  SBA CODE NOT FOUND.                            **
**                                                                   **
**     1041  -  COMM  TUBE I/O ERROR.                                **
**                                                                   **
***********************************************************************
         TITLE 'ETPS - EMERGENCY TELE-PROCESSING SYSTEM'
ETPS     CSECT
*
         PRINT NOGEN
*
         USING *,12,11,9
         USING MYSAVE,13,10
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         L     R2,0(,R1)          SAVE PARM POINTER
         B     PROLOG
SYSNAME  DC    C'ETPS     -  BRIAN COOK &SYSDATE &SYSTIME'
PROLOG   DS    0H
         L     R9,GETSIZE        GET SAVE AREA LENGTH
         SRL   R9,12             ROUND UP TO 4K BOUNDARY
         LA    R9,1(,9)          ROUND UP TO 4K BOUNDARY
         SLL   R9,12             ROUND UP TO 4K BOUNDARY
         GETMAIN R,LV=(9)        GET MY SAVE AREA
         LR    R8,R1             SAVE GETMAIN POINTER
         ST    R8,8(R13)         STORE FORWARD POINTER
         ST    R13,4(R8)         STORE BACKWARDS POINTER
         LR    R13,R8            SET DSECT BASE 1
*
         LR    R4,R9             SAVE THE LENGTH FOR FREEMAIN
*
         MVI   0(R8),X'00'       SET BINARY ZEROS
         LA    R14,1(,R8)
         LR    R15,R9            LENGTH FOR MVCL
         BCTR  R15,0             RECEIVING FIELD IS 1 LESS
         MVCL  R14,R8        CLEAR BUFFER
*
         ST    R13,SAVETOP       SAVE THE START ADDRESS
         LA    R3,1
         LA    R11,4095(R3,R12)   SET PROGRAM BASE REGISTER 2
         LA    R9,4095(R3,R11)    SET PROGRAM BASE REGISTER 3
         LA    R10,4095(R3,R13)   SET DSECT BASE 2
         ST    R4,FREESIZE       SAVE THE LENGTH FOR FREEMAIN
*
RE       EQU   14
RF       EQU   15
         TITLE 'ETPS - MAINLINE PROCESSING LOOP'
*
MAIN     DS    0H
         BAL   R14,INIT
ALOGON   DS    0H
         BAL   R14,LOGON
*
         CLC   3(3,R3),=CL3'EOJ'     ALL DONE?
         BE    CLOSETB               GO CLOSE DOWN
*
         BAL   R14,PRIOPT
*
         CLC   TUBEID(2),=C'OP'  IS IT OPERATOR
         BNE   ALOGON            NO, NO AUTOMATIC LOGOFF
*
*
CLOSETB  DS    0H
*                                         BECOME SWAPPABLE
*
         LA    R2,TUBE
         MVI   PARMOP,128
         CLOSE ((2)),MF=(E,PARMOP)
* DE-ALLOCATE TUBE
FREETUBE DS    0H
         MVI   VERBCODE,2       SET TO INDICATE UNALLOCATE
         OI    DYNTEXT1,X'80'   DDNAME ONLY
         LA    R1,DYNPARM
         DYNALLOC
*
         LTR   R15,15
         BNZ   WTOMS4
*
RETRNEND DS    0H
         LR    R11,R13
         L     R9,FREESIZE
         L     R13,4(,R13)  PICK UP CALLING SAVE AREA
         FREEMAIN R,LV=(9),A=(11)
         LM    R14,R12,12(R13)
         SR    R15,15
         BR    R14
         TITLE 'ETPS - INITIALIZATION'
*
INIT     DS    0H
         ST    R14,INIT14
* ALLOCATE TUBE
*
         MVC   DYNRB(DYNLENG),CONRB
*
         LA    R1,DYNDDNAM
         ST    R1,DYNTEXT1
*
         LA    R1,DYNDISP
         ST    R1,DYNTEXT2
*
         LA    R1,DYNUNIT
         ST    R1,DYNTEXT3
         OI    DYNTEXT3,X'80'
*
         LA    R1,DYNTEXT1
         ST    R1,TEXTPTRS
*
         LA    R1,DYNRB
         ST    R1,DYNPARM
         OI    DYNPARM,X'80'
*
* PICK UP PARM = DDNAME
*
         LA    R1,8
         CH    R1,0(,R2) IF PARM LENGTH IS NOT = 8
         BNE   WTOMS1       SHUT IT DOWN
*
         MVC   TUBEDDNM(8),2(R2) MOVE IN DDNAME FOR DYNALLOC
         MVC   TUBEID(8),2(R2)   ALSO SAVE IT
         MVC   UNITADDR(3),7(R2) MOVE IN UNIT ADDRESS
*
         LA    R1,DYNPARM
         DYNALLOC
*
         LTR   R15,15
         BNZ   WTOMS2
*
         CLC   2(2,R2),=C'OP'    IS IT OPERATOR TERMINAL
         BE    *+14              YES, LET IT GO
         CLC   2(4,R2),SYSNAME   SYSTEM NAME
         BNE   WTOMS5       SHUT IT DOWN
*
         MVI   PERMERR,0                FIRST TIME THROUGH
*
         MVI   PFKFLAG,0                NOT A PFK/ATTN
*
         LA    R4,UNITADDR
*
*    SAVE REGISTERS; NOTE R1 POINTS TO EBCDIC UNIT ADDRESS ON
*    ENTRY. R1 IS SET TO THE UCB ADDRESS OR ZEROS ON EXIT
*
         STM   2,7,12(13)
NXTUCB   EQU   *
         UCBSCAN
*
*    GET INFORMATION FROM UCB.
*
         LTR   R15,R15
         BNZ   NOGOTUCB
*
         CLC   0(3,4),13(1)             MATCHING UNIT ADDRESS?
         BE    GOTUCB
         B     NXTUCB
NOGOTUCB EQU   *
         SR    1,1
GOTUCB   EQU   *
         LM    2,7,12(13)
*
         ST    R1,UCBADDR
*
         LTR   R1,1
         BZ    WTOMS6
*
         MVC   SUPRMOD(SMODLEN),SMOD
         MVC   PROBMOD(PMODLEN),PMOD
*
         MVI   USUALWCC,X'C3'           NO ALARM
         MVI   ALARMWCC,X'C7'           ALARM
*
         L     R1,=A(BUFF3278)          3278 SBA CODES
         ST    R1,SBACODES              SET DEFAULT
         MVI   SBACODE,X'11'            DEFAULT SBA
*
         LA    R1,1920           DEFAULT BUFF SIZE
         ST    R1,SCREENSZ       SET IT
         LA    R15,24            DEFAULT NUMBER OF ROWS
         ST    R15,SCROWS        SET IT
*
         CLI   TUBEID+4,C'2'     IS IT A MOD2
         BE    SETSCRSZ             USE DEFAULT
         CLI   TUBEID+4,C'3'     IS IT A MOD3
         BE    ITSMOD3              GO SET IT
         CLI   TUBEID+4,C'4'     IS IT A MOD4
         BE    ITSMOD4              GO SET IT
         CLI   TUBEID+4,C'5'     IS IT A MOD5
         BE    ITSMOD5              GO SET IT
         B     SETSCRSZ             USE DEFAULT
*
SMOD     MODESET KEY=ZERO,MODE=SUP,MF=L
SMODLEN  EQU   *-SMOD
PMOD     MODESET KEY=NZERO,MODE=PROB,MF=L GET BACK IN PROB STATE
PMODLEN  EQU   *-PMOD
ITSMOD3  DS    0H
         LA    R1,2240           MOD 3 BUFF SIZE
         ST    R1,SCREENSZ       SET IT
         LA    R15,28            DEFAULT NUMBER OF ROWS
         ST    R15,SCROWS        SET IT
         B     SETSCRSZ          ALL SET
*
ITSMOD4  DS    0H
         LA    R1,3440           MOD 4 BUFF SIZE
         ST    R1,SCREENSZ       SET IT
         LA    R15,43            DEFAULT NUMBER OF ROWS
         ST    R15,SCROWS        SET IT
         B     SETSCRSZ          ALL SET
*
ITSMOD5  DS    0H
         LA    R1,3564           MOD 5 BUFF SIZE
         ST    R1,SCREENSZ       SET IT
         LA    R15,27            DEFAULT NUMBER OF ROWS
         ST    R15,SCROWS        SET IT
         B     SETSCRSZ          ALL SET
*
SETSCRSZ DS    0H
         MVC   SCROWS1(4),SCROWS
         XC    SCROWS2(4),SCROWS2
*
         MVC   ROWFILL(8),DUMMYROW
*
*
OPENTUBE DS    0H
*
* OPEN TUBE
         MVC   TUBE(56),TINIT
         MVC   TUBE+40(8),TUBEDDNM MOVE IN DDNAME
* SET IOB ADDRESS
         LA    R2,TUBEIOB        SET IOB ADDRESS
         STCM  R2,7,TUBE+29                       IN THE DCB
         LA    R2,TUBE           GET DCB ADDRESS
*
         OPEN  ((2))
*
         TM    48(R2),X'10'      GOOD OPEN
         BZ    WTOMS7            NOPE
*
         L     R3,44(,R2) GET DEB ADDRESS
         LA    R3,0(,3)  CLEAR LEFT BYTE
         MODESET MF=(E,SUPRMOD)
*                                         BECOME NON-SWAPPABLE
         SYSEVENT DONTSWAP
*
         LRA   R1,0(,R3)  GET REAL ADDRESS OF DEB
         ST    R1,DEBADDR  SAVE IT
         MVI   DEBADDR,X'01'  DEVICE INDEX
         L     R6,UCBADDR
         USING UCBBGN,6
         MVC   UCBCTLNK(4),DEBADDR    MOVE IN DEB ADDRESS (REAL)
         MVI   UCBATNCT,0             CLEAR ATTENTION COUNT
         MVI   UCBATNCT+1,X'06'       TURN ON SKIP FLAG, RI PENDING
         MVI   UCBIRBA+2,X'01'        SET IRB ADDRESS TO NON-ZERO
         L     R4,UCBEXTPT            UCB EXTENSION
         USING UCBCMEXT,4
*
* SET ATTENTION EXIT. X'08' IS DESIGNATED FOR PROGRAM PRODUCTS.
*
         USING PSA,0
         L     1,FLCCVT          CVT POINTER
         USING CVT,1
         L     1,CVTIXAVL        IOS COMM ADDRESS
         USING IOCOM,1
         L     1,IOCATTBL        ATTENTION TABLE
         LA    R1,ATBLEN(,1)     BUMP TO X'04'
         LA    R2,ATBLEN(,1)     BUMP TO X'08'
         USING ATB,2
*
         CLC   ATBRTN(4),=X'00000000'  NO ROUTINE
         BE    GOTATB                  YUP, USE THIS ONE
*
NOTATB   DS    0H
         B     GOTATB                  YUP, USE THIS ONE
*
         LA    15,ATXTLEN     PICK UP SIZE OF CSECT
         L     14,=A(ETPSATEN)
         L     R1,ATBRTN
         EX    15,ATXTCLC
         BE    UCBATION
         DC    H'0'                    JUST ABEND, CAN'T FIND ATB
*
GOTATB   DS    0H
*
         LA    R0,ATXTLEN     PICK UP SIZE OF CSECT
         GETMAIN RU,LV=(0),SP=228
         ST    R1,ATBRTN
         LA    15,ATXTLEN     PICK UP SIZE OF CSECT
         L     14,=A(ETPSATEN)
         EX    15,ATXTMVC
         B     UCBATION
*
ATXTMVC  MVC   0(1,R1),0(R14)
ATXTCLC  CLC   0(1,R1),0(R14)
UCBATION DS    0H
         MVI   UCBATI,X'08'          ETPSATEN
         XC    UCBRDYQ(4),UCBRDYQ    CLEAR ECB
         DROP  6
         DROP  4
         MODESET MF=(E,PROBMOD)
*
         XC    RECB(4),RECB   CLEAR ECB
         OI    RECB,X'40'        INIT RECB TO POSTED
* INITIALIZE IOB
         MVC   TUBEIOB(40),IOBLANK
         LA    R2,TUBE           SET DCB ADDRESS
         ST    R2,TUBEIOB+20                      IN THE IOB
* SET SCREEN BUFFER ADDRESSES
         LA    R1,SCREENIO
         ST    R1,BUFF
         LA    R1,4095(,1)
         ST    R1,REPLY
         LA    R1,4095(,1)
         ST    R1,TERMINPT
         LA    R1,4095(,1)
         ST    R1,LINES
*
         LA    R1,4095(,1)
         SRL   R1,2                    FULL-WORD ALIGN
         SLL   R1,2                    FULL-WORD ALIGN
         ST    R1,DYNWORK1
         LA    R1,DYNRCODE
         ST    R1,DYNWORK2
         OI    DYNWORK2,X'80'
*
         MVI   SPLIT,0                  NOT IN SPLIT SCREEN
*
         LA    R14,WCCFLAG              "WCC" IS USED TO SET THE ALARM
         ST    R14,TERMOUT              NOW BUILD INADDRESSES
*
         LA    R14,TERMOUT+4            NOW BUILD INADDRESSES
         LA    R1,43                    LOOP CONTROL
         L     R15,LINES                FIRST LINE ADDRESS
*
         ST    R15,0(,R14)              STORE THE ADDRESS
         LA    R15,93(,15)              BUMP 93 BYTES
         LA    R14,4(,14)               BUMP TO NEXT INADDR
         BCT   R1,*-12                  LOOP 43 TIMES
*
         MVI   WCCFLAG,C'A'             ALARM REQUESTED
*
         LA    R14,TERMINPT+4           NOW BUILD INADDRESSES
         LA    R1,43                    LOOP CONTROL
         L     R15,TERMINPT             FIRST LINE ADDRESS
         LA    R15,93(,15)              BUMP 93 BYTES
         MVI   0(R15),X'00'             SET FIRST BYTE TO ZEROS
*
         ST    R15,0(,R14)              STORE THE ADDRESS
         LA    R15,93(,15)              BUMP 93 BYTES
         MVI   0(R15),X'00'             SET FIRST BYTE TO ZEROS
         LA    R14,4(,14)               BUMP TO NEXT INADDR
         BCT   R1,*-16                  LOOP 43 TIMES
*
         LA    R0,LIBDCB1
         LA    R1,MEMDCB1
         LA    R2,PRTDCB1
         LA    R3,COPDCB1
         LA    R4,LIBDCB2
         LA    R5,MEMDCB2
         LA    R6,PRTDCB2
         LA    R7,COPDCB2
*
         MVC   LIBDCB1(88),DIRDCB
         MVC   LIBDCB2(88),DIRDCB
         MVC   LIBDCB1+40(8),=CL8'LIBDCB1'
         MVC   LIBDCB2+40(8),=CL8'LIBDCB2'
         MVC   MEMDCB1(88),SEQDCB
         MVC   MEMDCB2(88),SEQDCB
         MVC   MEMDCB1+40(8),=CL8'MEMDCB1'
         MVC   MEMDCB2+40(8),=CL8'MEMDCB2'
         MVC   PRTDCB1(88),OUTDCB
         MVC   PRTDCB2(88),OUTDCB
         MVC   PRTDCB1+40(8),=CL8'PRTDCB1'
         MVC   PRTDCB2+40(8),=CL8'PRTDCB2'
         MVC   COPDCB1(88),SEQDCB
         MVC   COPDCB2(88),SEQDCB
         MVC   COPDCB1+40(8),=CL8'COPDCB1'
         MVC   COPDCB2+40(8),=CL8'COPDCB2'
*
         DROP  R10
         LA    R10,SPLITWRK       SET DSECT BASE FOR SPLIT-SCREEN
         USING SPLTAREA,10
*
         ST    R0,LIBDCBA
         ST    R1,MEMDCBA
         ST    R2,PRTDCBA
         ST    R3,COPDCBA
*
         LR    R1,R13                  SAVE THE SAVE AREA ADDRESS
         LA    R13,SPLIT2
         LA    R10,SPLITWRK
         ST    R4,LIBDCBA
         ST    R5,MEMDCBA
         ST    R6,PRTDCBA
         ST    R7,COPDCBA
         LR    R13,R1                 RESET THE SAVE AREA ADDRESS
         LA    R10,SPLITWRK
         MVI   PRIMEFLG,0               NOT IN PRIMARY OPTION MENU
*
         MVC   SUBRB(SUBLENG),SUBCONRB
*
         LA    R1,SUBRB
         ST    R1,SUBSUBP
         OI    SUBSUBP,X'80'
*
INITDONE DS    0H
         L     R14,INIT14
         BR    R14
*
         TITLE 'ETPS - SUBMIT DYNAMIC ALLOCATION PARAMETERS, ETC'
*
GETSIZE  DC    A(GMSIZE)
*
         DS    0F
SUBCONRB DC    X'14'           LENGTH = 20
         DC    X'01'           DSNAME ALLOCATION
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(SUBTEXT1)     POINTER TO SUBTEXT1
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
SUBLENG  EQU   *-SUBCONRB
*
SUBTEXT1 DC    A(SUBDDNAM)     POINTER TO DDNAME
SUBTEXT2 DC    A(SUBCLS)       POINTER TO SYSOUT CLASS
SUBTEXT3 DC    A(SUBFRE)       POINTER TO FREE=CLOSE
SUBTEXT4 DC    X'80',AL3(SUBPGM) POINTER TO INTRDR
*
SUBTEXT5 DC    X'80',AL3(SUBDDNAM)          DDNAME FOR FREE
*
         DS    0H
SUBDDNAM DC    X'0001'         DDNAME=  PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0006'         LENGTH OF PARM
         DC    CL8'SUBJOB'     DDNAME
*
         DS    0H
SUBCLS   DC    X'0018'         SYSOUT= PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0001'         LENGTH OF PARM
         DC    C'A'            SYSOUT CLASS
*
         DS    0H
SUBPGM   DC    X'0019'         INTRDR PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0006'         LENGTH OF PARM
         DC    C'INTRDR'       PGM NAME
*
         DS    0H
SUBFRE   DC    X'001C'         FREE=CLOSE
         DC    X'0000'         NUMBER OF TEXT UNITS
*
IOBLANK  DC    X'0200'
         DC    X'0000'
         DC    X'00000000'              ECB
         DC    X'0000000000000000'
         DC    X'00000000'              CH PGM
         DC    X'00000000'              DCB ADD
         DC    X'00000000'
         DC    X'00000000'
         DC    X'0000000000000000'      UCB INDEX
*
DUMMYROW DC    AL1(6),X'1D60',CL5'     '
*
SEQDCB   DCB   DSORG=PS,MACRF=(GM,PM),DDNAME=DUMMY,EODAD=DUMMYROW
OUTDCB   DCB   DSORG=PS,MACRF=(PM),DDNAME=DUMMY
DIRDCB   DCB      DSORG=PS,MACRF=(RP,WP),DDNAME=PPROCLIB,RECFM=F,      C
               LRECL=256,BLKSIZE=256,KEYLEN=8,EODAD=DIRENDOF
         TITLE 'ETPS - LOGON PROCESSING'
LOGON    DS    0H
         ST    R14,LOG14
*
*              PROMPT FOR LOGON AND PASSWORD
*
RELOGON  DS    0H
*
         MVI   SPLIT,0                  NOT IN SPLIT SCREEN
         MVI   PRIMEFLG,0               NOT IN PRIMARY OPTION MENU
*
         CLC   TUBEID(2),=C'OP'  IS IT OPERATOR
         BE    OPRLOGON          YES, NO LOGON
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         XC    MSGADD(4),MSGADD    NO MESSAGE AT UPPER-RIGHT
*
         MVI   WCCFLAG,C'A'             ALARM REQUESTED
*
         L     R6,=A(LOGSCREE)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)       GO COMMUNICATE
         BALR  R14,R15
*
         MVI   WCCFLAG,C'N'          NO ALARM REQUESTED
         L     R2,TERMINPT           PICK UP RESPONSES
         LM    R4,R6,TERMINPT+84     PICK UP RESPONSES
*
         LTR   R15,15                   IS IT A BAD RETURN CODE
         BNZ   CLOSETB                  YUP, GO EOJ
*
         CLI   PFKFLAG,0                IS IT A PF KEY/ATTN
         BE    CHECKEOJ              NOPE, SEE IF IT IS EOJ
*
         CLI   0(R2),X'6E'           IS IT PA2
         BE    RELOGON               YUP, RESHOW THE SCREEN
         B     CLOSETB              NOPE- GO CLOSE THE TUBE AND EOJ
*
CHECKEOJ DS    0H
         CLC   36(3,R4),=CL3'EOJ'    ALL DONE?
         BE    CLOSETB               YUP- GO CLOSE THE TUBE AND EOJ
         CLC   36(3,R4),=CL4'DUMP'   TAKE A DUMP??
         BNE   CHECKUSR              NO, GO CHECK THE USER-ID
         DC    H'0'                  YUP- TAKE AN S0C1
CHECKUSR DS    0H
         LA    R1,USERTABL           VALID USERS
USERTOP  DS    0H
         CLC   0(8,R1),SPACES        END OF TABLE
         BE    GOODUSER               NO- GO REPROMPT
         CLC   36(8,R4),0(R1)           AUTHORIZED USERID
         BE    GOODUSER               YUP
         LA    R1,8(,R1)             BUMP TO NEXT USER IN TABLE
         B     USERTOP               LOOP
USERTABL DS    0H
         DC    CL8'NOTTSO'
SPACES   DC    CL8' '
MASTPASS DC    CL8'NOTTSO  '
GOODUSER DS    0H
         MVC   USERID(8),MASTPASS     SAVE USERID
         MVC   USERPASS(8),MASTPASS  SAVE PASSWORD
         MVC   RACGROUP(8),35(R6)    SAVE RACF GROUP ID
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*   NOTE ADD PASSWORD CHECKING HERE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ENDLOGON DS    0H
         L     R14,LOG14
         BR    R14
OPRLOGON DS    0H
         MVC   USERID(8),USERTABL     SET USERID=FIRST IN TABLE
         MVC   USERPASS(8),MASTPASS      PASSWORD IS MASTER PASSWORD
         B     ENDLOGON
         TITLE 'ETPS - DISPLAY PRIMARY OPTION MENU'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
PRIOPT   DS    0H
*
         CLI   SPLIT,0                AM I IN SPLIT-SCREEN?
         BNE   REPRIOPT               YUP
         STM   R2,R13,PRISPLIT        SAVE REGISTERS
*
REPRIOPT DS    0H
         ENTRY REPRIOPT
*
REPRIMSG DS    0H
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         MVI   PRIMEFLG,1          ON SCREEN1
*
PRIBLD   DS    0H
*
         L     R6,=A(PRIMOPT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)       GO COMMUNICATE
         BALR  R14,R15
*
         LTR   R15,15              NON-ZERO
         BNZ   PRIEND              YUP  - GET OUT
*
         MVI   PRIMEFLG,0          ON SCREEN1
*
         LM    R2,R5,TERMINPT        PICK UP RESPONSES
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   *+22                     NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         BCTR  R3,0                MAKE RELATIVE 0?????
         SLL   R3,2                MULTIPLY BY 4
         LA    R3,TERMINPT(R3)     PICK UP LOCATION OF RESPONSES
         LM    R3,R5,4(R3)         PICK UP RESPONSES
*
         CLI   PFKFLAG,0                IS IT A PF KEY/ATTN
         BNE   PRIPFK                   YUP
*
         CLI   25(R4),C'X'         END
         BE    PRIEND              YUP  - GO RESHOW THE LOGON MENU
*
         B     PARSEOPT            NOPE - GO SEE WHAT OPTION WAS INPT
*
PRIPFK   DS    0H
*
*
         CLI   0(R2),X'F3'         IS IT PF3
         BE    PRIEND              YUP  - GO RESHOW THE LOGON MENU
         CLI   0(R2),X'C3'         IS IT PF15
         BE    PRIEND              YUP  - GO RESHOW THE LOGON MENU
*
         CLI   0(R2),X'6C'         IS IT PA1
         BE    PRIEND              YUP  - GO RESHOW THE LOGON MENU
*
         CLI   0(R2),X'6E'         IS IT PA2
         BE    REPRIOPT            YUP  - GO RESHOW THE PRIMOPT
*
         CLI   0(R2),X'6D'         IS IT "CLEAR"
         BE    REPRIOPT            YUP  - GO RESHOW THE PRIMOPT
*
PRIEND   DS    0H
         B     RELOGON
*
         TITLE 'ETPS - DETERMINE PRIMARY OPTION PROCESSOR'
PARSEOPT DS    0H
*
         CLI   25(R4),C'1'         BROWSE?
         BE    BROPT               GO DO IT
         CLI   25(R4),C'2'         EDIT?
         BE    EDOPT               GO DO IT
         CLI   25(R4),C'3'         UTILITIES?
         BE    UTLOPT              GO DO IT
         CLI   25(R4),C'4'         FOREGROUND?
         BE    FOOPT               GO DO IT
         CLI   25(R4),C'5'         BACKGROUND?
         BE    BAOPT               GO DO IT
         CLI   25(R4),C'6'         COMMANDS?
         BE    CDOPT               GO DO IT
*
         LA    R1,25(,R4)          POINT TO OPTION
         DC    H'0'                SOC1
         LA    R1,INVOPT
         ST    R1,MSGADD
         B     REPRIMSG   GO RESHOW THE PRIMARY OPTION MENU
*
INVOPT   DC    CL20' INVALID OPTION    '
         TITLE 'ETPS - BROWSE PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
BROPT    DS    0H
*
         MVI   LIBFUNC,C'B'       INDICATE BROWSE
REBROPT  DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(BROWSCRN)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)       GO COMMUNICATE
         BALR  R14,R15
*
         LTR   R15,15              BAD RC
         BNZ   BREND               YUP
*
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BE    BRPARSE             NOPE, GO BROWSE THE DATASET
*
         L     R2,TERMINPT           PICK UP RESPONSES
         CLI   0(R2),X'6E'           IS IT PA2
         BE    REBROPT               YUP, RESHOW THE SCREEN
         B     BREND               ELSE I'M DONE
*
BRPARSE  DS    0H
         B     EDCKINPT
*
BREND    DS    0H
         B     REPRIOPT
*
         TITLE 'ETPS - EDIT PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
EDOPT    DS    0H
*
         MVI   LIBFUNC,C'E'       INDICATE FUNCTION
*
REEDOPT  DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(EDITOPT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15              BAD RC
         BNZ   EDTEND              YUP
*
EDCKINPT DS    0H
         LM    R2,R7,TERMINPT      PICK UP FOUR REPLY FIELDS
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   *+22                     NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         BCTR  R3,0                MAKE RELATIVE 0?????
         SLL   R3,2                MULTIPLY BY 4
         LA    R3,TERMINPT(R3)     PICK UP LOCATION OF RESPONSES
         LM    R3,R7,4(R3)         PICK UP RESPONSES
*
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BE    EDPARSE             NOPE, GO EDIT THE DATASET
*
         CLI   0(R2),X'6E'         IS IT PA2?
         BE    EDTPA2              YUP
         B     EDTEND              ELSE I'M DONE
*
EDTPA2   DS    0H
*
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    REBROPT             YES, RESHOW THE SCREEN
*
         CLI   LIBFUNC,C'U'       INDICATE FUNCTION
         BE    REUTOPT             YES, RESHOW THE SCREEN
*
         B     REEDOPT             ASSUME EDIT, RESHOW THE SCREEN
*
EDPARSE  DS    0H
*
         CLC   30(4,R5),=C'END '  END OF EDIT
         BE    EDTEND
*
         MVC   EDTDSN1(54),30(R5)
*
         MVC   DIRPASS(8),USERPASS SET DSN PASSWORD TO LOGON PASSWORD
         MVC   DIRVOL(6),SPACES
*
         CLI   0(R6),X'00'         IF NO VOLSER ENTERED LOOK FOR
         BE    *+10                PASSWORD
*
         MVC   DIRVOL(6),30(R6)  MOVE IN THE VOLSER
*
         CLI   0(R7),X'00'         IF NO PASSWORD ENTERED,
         BE    *+10                PASSWORD
*
         MVC   DIRPASS(8),30(R7)   MOVE IN THE PASSWORD
*
NOVOLPAS DS    0H
*
*
         MVC   EDTPAS1(8),DIRPASS SET PASSWORD
*
         LA    R14,EDTDSN1        START ADDRESS FOR DSN LENGTH CALC
         SR    R15,15             SET COUNTER
*
EDDSNLOP DS    0H
         CLI   0(R14),C'('        LEFT PAREN INDICATES MEMBER
         BE    EDDSNMEM           GOT ONE
         CLI   0(R14),C' '        BLANK IS END OF DSN
         BE    EDDSNEND           GOT END
*
         LA    R14,1(,14)         BUMP ADDRESS
         LA    R15,1(,15)         BUMP COUNTER
         B     EDDSNLOP           TEST NEXT BYTE
*
EDDSNMEM DS    0H
         STC   R15,EDTDSN1L       SAVE LENGTH
*
         LA    R14,1(,14)         BUMP ADDRESS
         LA    R15,EDTMEM1        SET ADDRESS OF MEMBER FIELD
         MVC   EDTMEM1(8),SPACES  INIT MEM FIELD
EDMEMLOP DS    0H
         CLI   0(R14),C')'       RIGHT PAREN INDICATES MEMBER END
         BE    EDTSTPAN           ALL DONE
         CLI   0(R14),C' '        BLANK IS END OF DSN
         BE    EDTSTPAN           ALL DONE
*
         MVC   0(1,R15),0(R14)    MOVE 1 CHARACTER
         LA    R14,1(,14)         BUMP ADDRESS - INPUT
         LA    R15,1(,15)         BUMP ADDRESS - OUTPUT
         B     EDMEMLOP           TEST NEXT BYTE
*
*
EDDSNEND DS    0H
         STC   R15,EDTDSN1L       SAVE LENGTH
*
EDTSTPAN DS    0H
         L     R15,=A(PANAMES)    FIRST SEE IF IT'S A PAN LIBRARY
*
EDORGLP1 DS    0H
         CLI   0(R15),X'FF'       END OF TABLE
         BE    EDORGNPN           CAN'T BE A PAN LIBRARY
*
         CLC   1(44,R15),EDTDSN1  DSN MATCH
         BE    EDORGPAN           MUST BE A PAN LIBRARY
*
         LA    R15,45(,15)        BUMP TO NEXT PANAMES ENTRY
         B     EDORGLP1          TEST FOR A PAN LIBRARY
*
EDORGPAN DS    0H
         MVC   EDTORG1(3),=C'PAN' SET IT PAN
*
EDORGNPN DS    0H
*
EDSETVOL DS    0H
         CLI   DIRVOL,C' '        WAS VOLSER PROVIDED?
         BNE   EDSETORG           YUP
*
         XC    CAT(16),CAT        CLEAR CAMLST AREA
         MVI   CAT,X'44'          SET FLAGS
         LA    R14,EDTDSN1        SET DSN
         ST    R14,CAT+4
         LA    R14,CATINFO        SET OUTPUT AREA
         ST    R14,CAT+12
*
         LOCATE CAT
*
         LTR   R15,15             TEST RETURN CODE
         BNZ   EDBADLOC           IF NO GOOD, SET MESSAGE
*
         MVC   DIRVOL(6),CATINFO+6   SET THE VOLSER
*
EDSETORG DS    0H
*
         XC    VTOC(16),VTOC      CLEAR CAMLST AREA
         MVI   VTOC,X'C1'         SET FLAGS
         LA    R14,EDTDSN1        SET DSN
         ST    R14,VTOC+4
         LA    R14,DIRVOL         SET VOLSER
         ST    R14,VTOC+8
         LA    R14,VTOCINFO       SET OUTPUT AREA
         ST    R14,VTOC+12
*
         OBTAIN VTOC
*
         LTR   R15,15             CHECK RETURN CODE
         BNZ   BADEDOBT           IF NO GOOD, SET MESSAGE
*
         LA    R14,VTOCINFO       PICK UP VTOCINFO
*
         CLC   EDTORG1(3),=C'PAN' IS PAN ALREADY SET
         BE    EDFMTDIR               YUP
*
         MVC   EDTORG1(3),=C'SEQ' SET DEFAULT DSORG
         CLC   38(2,R14),=X'4000'     IS IT SEQUENTIAL
         BE    EDTSTLRC               YUP
*
         MVC   EDTORG1(3),=C'PDS' SET DEFAULT DSORG
*
         CLC   38(2,R14),=X'0200'     IS IT PO
         BE    EDTSTLRC               YUP
*
         B     EDBADORG           IF NO GOOD, SET MESSAGE
*
EDTSTLRC DS    0H
*
         CLC   44(2,R14),=X'0050'   IS LRECL=80
         BNE   EDBAD80            IF NO GOOD, SET MESSAGE
*
EDFMTDIR DS    0H
         L     R15,=A(ETPSLIB2)   GO DO EDIT
*
         CALL  (15),(EDTDSN1,EDTDSN1L,DIRVOL,EDTORG1,DIRDD,            X
               USERPASS,EDTMEM1),VL,MF=(E,PARM43)
*
         MVI   EDTMEM1,C' '       RESET MEMBER NAME
         MVC   EDTMEM1+1(7),EDTMEM1
*
         B     REEDSCN            RETRY THE SCREEN
*
EDBADLOC DS    0H
         LA    R1,BADEDLOC
         ST    R1,MSGADD
         B     REEDSCN            RETRY THE SCREEN
BADEDLOC DC    CL20'EDIT - LOCATE FAILED'
*
EDBADOBT DS    0H
         LA    R1,BADEDOBT
         ST    R1,MSGADD
         B     REEDSCN            RETRY THE SCREEN
BADEDOBT DC    CL20'EDIT - OBTAIN FAILED'
*
EDBADORG DS    0H
         LA    R1,BADEDORG
         ST    R1,MSGADD
         B     REEDSCN            RETRY THE SCREEN
BADEDORG DC    CL20'EDIT - BAD DSORG    '
*
EDBAD80  DS    0H
         LA    R1,BADED80
         ST    R1,MSGADD
         B     REEDSCN            RETRY THE SCREEN
BADED80  DC    CL20'EDIT - LRECL NOT 80 '
*
REEDSCN  DS    0H
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    REBROPT             YES, RESHOW THE SCREEN
*
         CLI   LIBFUNC,C'U'       INDICATE FUNCTION
         BE    REUTOPT             YES, RESHOW THE SCREEN
*
         B     REEDOPT             ASSUME EDIT, RESHOW THE SCREEN
*
EDTEND   DS    0H
*
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    REBROPT             YES, RESHOW THE SCREEN
*
         CLI   LIBFUNC,C'U'       INDICATE FUNCTION
         BE    REUTOPT             YES, RESHOW THE SCREEN
*
         B     REPRIOPT
*
*
         TITLE 'ETPS - UTILITY       PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
UTLOPT   DS    0H
*
         MVI   LIBFUNC,C'U'       INDICATE FUNCTION
*
         CLI   26(R4),C'.'         SUB-OPTION SELECTED?
         BNE   REUTOPT             NOPE
*
         LA    R4,2(,4)            BUMP 2
         B     CHKUTOPT            GO CHECK SUB-OPTION
*
REUTOPT  DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(UTOPT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    YUP
*
         LM    R2,R5,TERMINPT        PICK UP RESPONSES
*
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BE    UTPARSE             NOPE, GO PARSE INPUT
*
         CLI   0(R2),X'6E'           IS IT PA2
         BE    REUTOPT               YUP, RESHOW THE SCREEN
         B     UTEND              ELSE I'M DONE
*
UTPARSE  DS    0H
*
CHKUTOPT DS    0H
         CLI   25(R4),C'1'         OPTION 1
         BE    UT1SEL              YUP
         CLI   25(R4),C'2'         OPTION 2
         BE    UT2SEL              YUP
         CLI   25(R4),C'3'         OPTION 3
         BE    UT3SEL              YUP
         CLI   25(R4),C'4'         OPTION 4
         BE    UT4SEL              YUP
         CLI   25(R4),C'5'         OPTION 5
         BE    UT5SEL              YUP
         CLI   25(R4),C'6'         OPTION 6
         BE    UT6SEL              YUP
         CLI   25(R4),C'7'         OPTION 7
         BE    UT7SEL              YUP
         CLI   25(R4),C'8'         OPTION 8
         BE    UT8SEL              YUP
*
         B     REUTOPT             RESHOW THE OPTIONS
*
UT1SEL   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(UT1MENU)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         L     R2,TERMINPT         PICK UP AID ADDRESS
         L     R3,TERMINPT+8               COMMAND INPUT LINE
         L     R5,TERMINPT+36              DSNAME FIELD
         L     R6,TERMINPT+40              VOLSER
         L     R7,TERMINPT+44              PASSWORD
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   *+30                     NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         BCTR  R3,0                MAKE RELATIVE 0?????
         SLL   R3,2                MULTIPLY BY 4
         L     R5,TERMINPT+36(R3)  PICK UP RESPONSES
         L     R6,TERMINPT+40(R3)  PICK UP RESPONSES
         L     R7,TERMINPT+44(R3)  PICK UP RESPONSES
         LA    R3,TERMINPT+8(R3)   PICK UP COMMAND INPUT LINE
*
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BNE   REUTOPT             YUP
*
         CLC   25(4,R5),=C'END '  END OF EDIT
         BE    REUTOPT
*
         CLI   0(R3),X'00'        IF NO COMMAND ENTERED
         BE    UT1LIST            GO LIST THE LIBRARY
*
         CLI   25(R3),C'C'        IS IT A COMPRESS
         BE    UT1CPRS            YUP
*
         CLI   25(R3),C'X'        IS IT LIST DIRECTORY
         BE    UT1DLIST           YUP
*
         CLI   25(R3),C'L'        IS IT LIST ENTIRE DATASET
         BE    UT1FLIST           YUP
*
         LA    R1,UT1BADOP
         ST    R1,MSGADD
         B     UT1SEL
*
UT1CPRS  DS    0H
*
UT1CPBAD DS    0H
         LA    R1,UT1BADCP
         ST    R1,MSGADD
         B     UT1SEL
*
UT1DLIST DS    0H
*
UT1DLBAD DS    0H
         LA    R1,UT1BADDL
         ST    R1,MSGADD
         B     UT1SEL
*
UT1FLIST DS    0H
*
UT1FLBAD DS    0H
         LA    R1,UT1BADLS
         ST    R1,MSGADD
         B     UT1SEL
*
UT1LIST  DS    0H
         MVC   EDTDSN1(54),30(R5)
*
         MVC   DIRPASS(8),USERPASS SET DSN PASSWORD TO LOGON PASSWORD
         MVC   DIRVOL(6),SPACES
*
         CLI   0(R6),X'00'         IF NO VOLSER ENTERED LOOK FOR
         BE    *+10                PASSWORD
*
         MVC   DIRVOL(6),30(R6)  MOVE IN THE VOLSER
*
         CLI   0(R7),X'00'         IF NO PASSWORD ENTERED,
         BE    *+10                PASSWORD
*
         MVC   DIRPASS(8),30(R7)   MOVE IN THE PASSWORD
*
         MVC   EDTPAS1(8),DIRPASS SET PASSWORD
*
         LA    R14,EDTDSN1        START ADDRESS FOR DSN LENGTH CALC
         SR    R15,15             SET COUNTER
*
U1DSNLOP DS    0H
         CLI   0(R14),C' '        BLANK IS END OF DSN
         BE    U1DSNEND           GOT END
*
         LA    R14,1(,14)         BUMP ADDRESS
         LA    R15,1(,15)         BUMP COUNTER
         B     U1DSNLOP           TEST NEXT BYTE
*
U1DSNEND DS    0H
         STC   R15,EDTDSN1L       SAVE LENGTH
         MVC   EDTMEM1(8),SPACES  INIT MEM FIELD
         MVC   EDTORG1(3),=C'PDS' SET IT PDS, BY DEFAULT
*
U1TSTPAN DS    0H
         L     R15,=A(PANAMES)    FIRST SEE IF IT'S A PAN LIBRARY
*
U1ORGLP1 DS    0H
         CLI   0(R15),X'FF'       END OF TABLE
         BE    U1ORGNPN           CAN'T BE A PAN LIBRARY
*
         CLC   1(44,R15),EDTDSN1  DSN MATCH
         BE    U1ORGPAN           MUST BE A PAN LIBRARY
*
         LA    R15,45(,15)        BUMP TO NEXT PANAMES ENTRY
         B     U1ORGLP1          TEST FOR A PAN LIBRARY
*
U1ORGPAN DS    0H
         MVC   EDTORG1(3),=C'PAN' SET IT PAN
*
U1ORGNPN DS    0H
*
U1SETVOL DS    0H
         CLI   DIRVOL,C' '        WAS VOLSER PROVIDED?
         BNE   U1SETORG           YUP
*
         XC    CAT(16),CAT        CLEAR CAMLST AREA
         MVI   CAT,X'44'          SET FLAGS
         LA    R14,EDTDSN1        SET DSN
         ST    R14,CAT+4
         LA    R14,CATINFO        SET OUTPUT AREA
         ST    R14,CAT+12
*
         LOCATE CAT
*
         LTR   R15,15             TEST RETURN CODE
         BNZ   UT1LBAD            IF NO GOOD, RETRY THE SCREEN
*
         MVC   DIRVOL(6),CATINFO+6   SET THE VOLSER
         B     U1SETORG
*
UT1LBAD  DS    0H
*
         LA    R1,U1BADLOC
         ST    R1,MSGADD
         B     UT1SEL
*
UT1OBAD  DS    0H
*
         LA    R1,U1BADOBT
         ST    R1,MSGADD
         B     UT1SEL
*
UT1ORBAD DS    0H
*
         LA    R1,U1BADORG
         ST    R1,MSGADD
         B     UT1SEL
*
UT1LRBAD DS    0H
*
         LA    R1,U1BADLRE
         ST    R1,MSGADD
         B     UT1SEL
*
U1SETORG DS    0H
*
         XC    VTOC(16),VTOC      CLEAR CAMLST AREA
         MVI   VTOC,X'C1'         SET FLAGS
         LA    R14,EDTDSN1        SET DSN
         ST    R14,VTOC+4
         LA    R14,DIRVOL         SET VOLSER
         ST    R14,VTOC+8
         LA    R14,VTOCINFO       SET OUTPUT AREA
         ST    R14,VTOC+12
*
         OBTAIN VTOC
*
         LTR   R15,15             CHECK RETURN CODE
         BNZ   UT1OBAD            IF NO GOOD, RETRY THE SCREEN
*
         LA    R14,VTOCINFO       PICK UP VTOCINFO
*
         CLC   EDTORG1(3),=C'PAN' IS PAN ALREADY SET
         BE    U1FMTDIR               YUP
*
         CLC   38(2,R14),=X'0200'     IS IT PO
         BNE   UT1ORBAD               NOPE
*
         CLC   44(2,R14),=X'0050'   IS LRECL=80
         BNE   UT1LRBAD           IF NO GOOD, RETRY THE SCREEN
*
U1FMTDIR DS    0H
         L     R15,=A(ETPSLIB2)   GO DO EDIT
*
         CALL  (15),(EDTDSN1,EDTDSN1L,DIRVOL,EDTORG1,DIRDD,            X
               USERPASS,EDTMEM1),VL,MF=(E,PARM43)
*
         MVI   EDTMEM1,C' '       RESET MEMBER NAME
         MVC   EDTMEM1+1(7),EDTMEM1
*
         B     UT1SEL             IF NO GOOD, RETRY THE SCREEN
*
U1BADLOC DC    CL20'DATASET NOT CATALOGD'
U1BADOBT DC    CL20'VTOC OBTAIN FAILED  '
U1BADORG DC    CL20'INVALID DSORG       '
U1BADLRE DC    CL20'INVALID LRECL-NOT 80'
UT1BADCP DC    CL20'COMPRESS FAILED     '
UT1BADDL DC    CL20'DIRECTORY LIST FAILD'
UT1BADLS DC    CL20'DATASET LIST FAILED '
UT1BADOP DC    CL20'INVALID OPTION      '
*
UT2SEL   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         B     REUTOPT             RESHOW THE OPTIONS
*
UT3SEL   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         B     REUTOPT             RESHOW THE OPTIONS
*
UT4SEL   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         B     REUTOPT             RESHOW THE OPTIONS
*
UT5SEL   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         B     REUTOPT             RESHOW THE OPTIONS
*
UT6SEL   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         B     REUTOPT             RESHOW THE OPTIONS
*
UT7SEL   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         B     REUTOPT             RESHOW THE OPTIONS
*
UT8SEL   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         B     REUTOPT             RESHOW THE OPTIONS
*
UTEND    DS    0H
         B     REPRIOPT
*
         TITLE 'ETPS - FOREGROUND    PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
FOOPT    DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
FOEND    DS    0H
         B     REPRIOPT
*
         TITLE 'ETPS - BACKGROUND    PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
BAOPT    DS    0H
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
BAEND    DS    0H
         B     REPRIOPT
*
         TITLE 'ETPS - COMMAND       PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
CDOPT    DS    0H
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
CDEND    DS    0H
         B     REPRIOPT
*
*
*
         TITLE 'ETPS - USER WTOS'
         PRINT NOGEN
WTOMS1   WTO   'ETPS1001 INVALID PARM LENGTH',ROUTCDE=11
         B     RETRNEND
WTOMS2   WTO   'ETPS1002 COULD NOT ALLOCATE TUBE          ',ROUTCDE=11
         B     RETRNEND
WTOMS4   WTO   'ETPS1004 COULD NOT FREE TUBE              ',ROUTCDE=11
         B     RETRNEND
WTOMS5   WTO   'ETPS1005 INVALID PARM',ROUTCDE=11
         B     RETRNEND
WTOMS6   WTO   'ETPS1006 UCB LOOKUP FAILED',ROUTCDE=11
         B     RETRNEND
WTOMS7   WTO   'ETPS1007 COULD NOT OPEN TUBE              ',ROUTCDE=11
         DC    H'0'
         B     FREETUBE
WTOMS21  WTO   'ETPS1021 TUBE I/O ERROR                   ',ROUTCDE=11
         B     CLOSETB
WTOMS22  WTO   'ETPS1041 TUBE I/O ERROR                   ',ROUTCDE=11
         B     CLOSETB
         TITLE 'ETPS - DATA AREAS AND CONSTANTS'
******** SCREEN DCB
         DS    0F
TINIT    DCB   DSORG=PS,MACRF=E,DDNAME=SCREEN,BUFL=4096,RECFM=U
         DS    0F
UPCASE   DC    CL80' '
*
ASTERS   DC    80C'*'
*
SBA3278  DC    A(BUFF3278)
*
         DS    0F
CONRB    DC    X'14'           LENGTH = 20
         DC    X'01'           DSNAME ALLOCATION
         DC    B'00000000'     FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    F'0'            POINTER TO TEXT POINTERS
         DC    F'0'            RESERVED
         DC    X'08000000'     FLAGS2, CONSIDER OFFLINE DEVICES
*
         DS    0H
CONDDNAM DC    X'0001'         DDNAME=  PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0008'         LENGTH OF PARM
         DC    CL8'TUBE'       DDNAME
*
DISPOLD  DS    0H
CONDISP  DC    X'0004'         DISP=    PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0001'         LENGTH OF PARM
         DC    X'01'           OLD
*
         DS    0H
CONUNIT  DC    X'0015'         UNIT= PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0003'         LENGTH OF PARM
         DC    C'45B'          UNIT ADDRESS
*
DISPSHR  DC    X'0004'         DISP=    PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0001'         LENGTH OF PARM
         DC    X'08'           SHR
         LTORG
         TITLE 'ETPS GENERAL SCREEN BUILD SUB ROUTINE'
SCRNBLD  DS    0H
*
* AT ENTRY, R6 POINTS TO THE ADDRESS OF THE SCREEN
*
         LA    R3,TERMOUT+4             SECOND TERMOUT, FIRST LINE
         L     R4,SCROWS1               SCREEN 1
         BCTR  R4,0                     SUBTRACT 1 FOR LUCK
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   SCRNBLD1                 NOPE, I'M ALL SET
*
         SLL   R4,2                     MULTIPLY OFFSET BY 4
         LA    R3,TERMOUT+4(R4)         POINT TO ROW 1 OF SCREEN 2
         L     R4,SCROWS2               NUMBER OF ROWS IN SCREEN 2
*
SCRNBLD1 DS    0H
         L     R2,0(,R6)                ADDRESS OF FROM FIELD
         L     R7,0(,R3)                ADDRESS OF "TO" FIELD
         IC    R1,0(,R2)                PICK UP LENGTH
         EX    R1,SCRNBLDM              MOVE IT
*
         CLI   PRIMEFLG,1               PRIMARY OPTION MENU
         BNE   SCRNBL11                 NOPE, SKIP THIS
         CLC   UOFF(11,R7),=C'USERID   - '     USERID
         BE    SCRNBL1U                 YES, MOVE USERID
         CLC   TIMEOFF(11,R7),=C'TIME     - '     TIME
         BE    SCRNBL1T                 YES, MOVE TIME
         CLC   TERMOFF(11,R7),=C'TERMINAL - '     TERMINAL
         BNE   SCRNBL11                 NOPE, SKIP THIS
*
         MVC   TERMOFF+11(1,R7),TERMTYPE
         MVC   TERMOFF+13(3,R7),TERMADDR
         B     SCRNBL11                 DO THE REST
*
SCRNBL1U DS    0H
         MVC   UOFF+11(7,R7),USERID     MOVE IT
         B     SCRNBL11                 DO THE REST
*
SCRNBL1T DS    0H
         TIME  DEC
         SRL   R0,12
         ST    R0,PACK8+4
         OI    PACK8+7,X'0F'
         UNPK  TIMEOFF+11(5,R7),PACK8+5(3)
         MVC   TIMEOFF+11(2,R7),TIMEOFF+12(R7)
         MVI   TIMEOFF+13(R7),C':'
         B     SCRNBL11                 DO THE REST
*
SCRNBL11 DS    0H
         TM    0(R6),X'80'              END OF PARMS?
         BO    SCRNBLD2                 YUP
         LA    R6,4(,6)                 BUMP TO NEXT PARM
         LA    R3,4(,3)                 BUMP TO NEXT PARM
         NI    0(R3),X'7F'              CLEAR THE "VL" BIT
         BCT   R4,SCRNBLD1              KEEP LOOPING
*
SCRNBLD2 DS   0H
         LTR   R4,4                     END OF SCREEN ROWS
         BZ    SCRNBLD4                 YES
         BCTR  R4,0                     SUBTRACT 1 FOR LUCK
         LTR   R4,4                     END OF SCREEN ROWS
         BZ    SCRNBLD4                 YES
         LA    R3,4(,3)                 BUMP TO NEXT PARM
         L     R7,0(,R3)                ADDRESS OF "TO" FIELD
*
SCRNBLD3 DS   0H
         MVC   0(8,R7),DUMMYROW         MOVE IN A DUMMY ROW
         NI    0(R3),X'7F'              CLEAR THE "VL" BIT
         LA    R3,4(,3)                 BUMP TO NEXT PARM
         L     R7,0(,R3)                ADDRESS OF "TO" FIELD
         BCT   R4,SCRNBLD3              KEEP LOOPING
         CLI   SPLIT,1                  AM I IN SPLIT-SCREEN, SCR 1
         BE    SCRNBLD4                 YUP, DON'T MOVE THIS IN
         MVC   0(8,R7),DUMMYROW         MOVE IN A DUMMY ROW
*
SCRNBLD4 DS   0H
*
         CLI   SPLIT,0                  AM I IN SPLIT-SCREEN
         BE    SCRNBLDZ                 NOPE, I'M ALL SET
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BE    SCRNBLDZ                 YUP, GO OR THE PARM LIST
         BR    R14
*
SCRNBLDZ DS   0H
         OI    0(R3),X'80'              END OF TERMOUT PARMS
         BR    R14
*
SCRNBLDM MVC   0(1,R7),0(R2)
         LTORG
         TITLE 'ETPS - SCREEN FORMATS'
*
*  THE BUFFER ALWAYS STARTS WITH A WRITE CONTROL CHARACTER (WCC):
*
*    X'C7'  =  SOUND ALARM, RESET KEYBOARD, RESET MODIF DATA TAG BITS
*    X'C3'  =  RESET KEYBOARD, RESET MDT BITS
*
*
*  FOLLOWING THE WCC ARE ORDER CODE SEQUENCES AND DATA:
*
*    X'11'  =  SET BUFFER ADDRESS (SBA), FOLLOWED BY 2-BYTE BUFF ADDR
*    X'13'  =  INSERT CURSOR (IC)
*    X'1D'  =  START FIELD (SF), FOLLOWED BY 1-BYTE ATTRIBUTE CHARACTER
*
         TITLE 'ETPS - LOGON LOGO'
*
LOGSCREE CSECT
         DC    A(LOGSCR01)
         DC    A(LOGSCR02)
         DC    A(LOGSCR03)
         DC    A(LOGSCR04)
         DC    A(LOGSCR05)
         DC    A(LOGSCR06)
         DC    A(LOGSCR07)
         DC    A(LOGSCR08)
         DC    A(LOGSCR09)
         DC    A(LOGSCR10)
         DC    A(LOGSCR11)
         DC    A(LOGSCR12)
         DC    A(LOGSCR13)
         DC    A(LOGSCR14)
         DC    A(LOGSCR15)
         DC    A(LOGSCR16)
         DC    A(LOGSCR17)
         DC    A(LOGSCR18)
         DC    A(LOGSCR19)
         DC    A(LOGSCR20)
         DC    A(LOGSCR21)
         DC    A(LOGSCR22)
         DC    A(LOGSCR23)
         DC    X'80',AL3(LOGSCR24)
LOGSCR01 DS    0H
         DC    AL1(77),X'1D60'       , SBA = 0
         DC    40C'* '
LOGSCR02 DC    AL1(16),X'1D60',CL16'** EEEE        '
LOGSCR03 DC    AL1(77),X'1D60',CL16'** EE          '
 DC C'                    (((-----)))                             '
LOGSCR04 DC    AL1(77),X'1D60',CL16'** EEE         '
 DC C'                  ** (       )**                            '
LOGSCR05 DC    AL1(77),X'1D60',CL16'** EE          '
 DC C'                 *( (        ) )*                           '
LOGSCR06 DC    AL1(77),X'1D60',CL16'** EEEE MERGENCY'
 DC C'               ((                )                          '
LOGSCR07 DC    AL1(77),X'1D60',CL16'*   '
*DC C'              (  < (O) > < (O) >   )                        '
 DC C'              (  <',X'1DE8'
 DC C'(O)',X'1D60'
 DC C'> <',X'1DE8'
 DC C'(O)',X'1D60'
 DC C'>  )                        '
LOGSCR08 DC    AL1(77),X'1D60',CL16'** TTTTT       '
 DC C'               ((       (       )))                         '
LOGSCR09 DC    AL1(77),X'1D60',CL16'**   T         '
 DC C'               ((               )))                         '
LOGSCR10 DC    AL1(77),X'1D60',CL16'**   T         '
 DC C'                ((((    ___   ))))                          '
LOGSCR11 DC    AL1(77),X'1D60',CL16'**   T  ELE -  '
 DC C'                 ((((        ))))                           '
LOGSCR12 DC    AL1(77),X'1D60',CL16'*   '
 DC C'                    ((      ))                              '
LOGSCR13 DC    AL1(77),X'1D60',CL16'** PPPP        '
 DC C'                      )    /                                '
LOGSCR14 DC    AL1(77),X'1D60',CL16'** P   P       '
 DC C'                      ( (                                 '
LOGSCR15 DC    AL1(77),X'1D60',CL16'** PPPP        '
 DC C'                      ) )                                 '
LOGSCR16 DC    AL1(77),X'1D60',CL16'** P           '
 DC C'                      ( (                                 '
LOGSCR17 DC    AL1(77),X'1D60',CL16'** P   ROCESSING'
 DC C'                      ) )                                 '
LOGSCR18 DC    AL1(77),X'1D60',CL16'*   '
 DC C'                      ( (                                 '
LOGSCR19 DC    AL1(77),X'1D60',CL16'**  SS         '
 DC C'                      ) )                                 '
LOGSCR20 DC    AL1(16),X'1D60',CL16'** S           '
LOGSCR21 DC    AL1(77),X'1D60',CL16'**  SS         '
         DC    CL14'ENTER USER ID:'
         DC    X'1D40'               SF = UNPROT
         DC    X'13'                 IC
         DC    CL43'              '
LOGSCR22 DC    AL1(77),X'1D60',CL16'**    S        '
         DC    CL14' AND PASSWORD:'
         DC    X'1D4C'               SF = UNPROT, NO DISPLAY
         DC    CL47'              '
LOGSCR23 DC    AL1(77),X'1D60',CL16'** SSS  YSTEM  '
         DC    CL14'   RACF GROUP:'
         DC    X'1D4C'               SF = UNPROT, NO DISPLAY
         DC    CL47'              '
LOGSCR24 DC    AL1(81),X'1D60',79C'*'
LOGSCRSZ EQU   *-LOGSCREE
         TITLE 'ETPS - PRIMARY OPTION MENU'
PRIMOPT  CSECT
         DC    A(PRIMEL1)
         DC    A(PRIMEL2)
         DC    A(PRIMEL3)
         DC    A(PRIMEL4)
         DC    A(PRIMEL5)
         DC    A(PRIMEL6)
         DC    A(PRIMEL7)
         DC    A(PRIMEL8)
         DC    A(PRIMEL9)
         DC    A(PRIMEL10)
         DC    A(PRIMEL11)
         DC    X'80',AL3(PRIMEL12)
PRIMEL1  DS    0H
         DC    AL1(82)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    19C'-'
         DC    CL40'       ETPS  -  PRIMARY OPTION MENU     '
         DC    21C'-'
PRIMEL2  DC    AL1(84)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL19'SELECT OPTION ===> '
         DC    X'1D40'               SF UNPROTECTED              ATTR
PRIMHOME EQU   *-PRIMOPT
         DC    X'13'                 IC                          ATTR
         DC    CL60' '
PRIMEL3  DC    AL1(81)
         DC    X'1D60'               SF   PROTECTED              ATTR
         DC    CL61' '
UOFF     EQU   *-PRIMEL3
         DC    CL11'USERID   - '
         DC    CL7'  '
PRIMEL4  DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   1  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'BROWSE     - DISPLAY SOURCE DATA OR OUTPUT LISTINGS   '
TIMEOFF  EQU   *-PRIMEL4
         DC    CL11'TIME     - '
         DC    CL8'  '
*
PRIMEL5  DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   2  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'EDIT       - CREATE OR CHANGE SOURCE DATA             '
TERMOFF  EQU   *-PRIMEL5
         DC    CL11'TERMINAL - '
         DC    CL8'  '
*
PRIMEL6  DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   3  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'UTILITIES  - PERFORM ETPS UTILITY FUNCTIONS           '
         DC    CL19' '
*
PRIMEL7  DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   4  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'FOREGROUND - COMPILE, LINK EDIT ETC.                  '
         DC    CL19' '
*
PRIMEL8  DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   5  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'BACKGROUND - COMPILE, UTILITY, ETC.                   '
         DC    CL19' '
*
PRIMEL9  DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   6  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'COMMAND    - ENTER ETPS COMMAND                       '
         DC    CL19' '
*
PRIMEL10 DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   X  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'EXIT       - TERMINATE ETPS AND RETURN TO LOGON MENU  '
         DC    CL19' '
*
PRIMEL11 DC    AL1(81)
         DC    X'1D60'               SF PROTECTED                ATTR
         DC    CL79' '
*
PRIMEL12 DC    AL1(85)
         DC    X'1D60'               SF   PROTECTED              ATTR
         DC    CL6'PRESS '
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL7'END KEY'
         DC    X'1D60'               SF   PROTECTED              ATTR
         DC    CL66' TO TERMINATE ETPS'
*
PRIMOPSZ EQU   *-PRIMOPT
         TITLE 'ETPS - BROWSE MENU'
BROWSCRN CSECT
*
* THIS IS THE BROWSE MENU
*
         DC    A(BROROW1)
         DC    A(BROROW2)
         DC    A(BROROW3)
         DC    A(BROROW4)
         DC    A(BROROW5)
         DC    X'80',AL3(BROROW6)
BROROW1  DC    AL1(81)                  ROW 01, COL 01
         DC    X'1DE8'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21'BROWSE- ENTRY PANEL  '
         DC    34C'-'
BROROW2  DC    AL1(83)                  ROW 02, COL 01
         DC    X'1DE8'
         DC    CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    X'1D40'
         DC    CL49' '
BROROW3  DC    AL1(86)                  ROW 03, COL 01
         DC    X'1D40'
         DC    CL22'FULLY-QUALIFIED DSNAME'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1DC8'
         DC    X'13'                       IC
         DC    CL53' '
BROROW4  DC    AL1(87)                  ROW 04, COL 01
         DC    X'1D60'
         DC    CL22'         VOLUME SERIAL'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1DC8'
         DC    CL6' '
         DC    X'1D60'
         DC    CL47'          (IF NOT CATALOGED)'
BROROW5  DC    AL1(81)                  ROW 05, COL 01
         DC    X'1D60'
         DC    CL79' '
BROROW6  DC    AL1(87)                  ROW 06, COL 01
         DC    X'1D60'
         DC    CL22'      DATASET PASSWORD'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1D4C'            NO DISPLAY
         DC    CL8' '
         DC    X'1D60'
         DC    CL45'        (IF PASSWORD PROTECTED)'
         TITLE 'ETPS - EDIT OPTIONS MENU'
*
* THIS IS THE EDIT OPTIONS MENU
*
EDITOPT  CSECT
         DC    A(EDTROW1)
         DC    A(EDTROW2)
         DC    A(EDTROW3)
         DC    A(EDTROW4)
         DC    A(EDTROW5)
         DC    X'80',AL3(EDTROW6)
EDTROW1  DC    AL1(81)                  ROW 01, COL 01
         DC    X'1DE8'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21' EDIT - ENTRY PANEL  '
         DC    34C'-'
EDTROW2  DC    AL1(83)                  ROW 02, COL 01
         DC    X'1DE8'
         DC    CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    X'1D40'
         DC    CL49' '
EDTROW3  DC    AL1(86)                  ROW 03, COL 01
         DC    X'1D40'
         DC    CL22'FULLY-QUALIFIED DSNAME'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1DC8'
EDTHOME  EQU   *-EDITOPT
         DC    X'13'                       IC
EDTDSNAM EQU   *-EDITOPT
         DC    CL53' '
EDTROW4  DC    AL1(87)                  ROW 04, COL 01
         DC    X'1D60'
         DC    CL22'         VOLUME SERIAL'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1DC8'
EDTVOLSR EQU   *-EDITOPT
         DC    CL6' '
         DC    X'1D60'
         DC    CL47'          (IF NOT CATALOGED)'
EDTROW5  DC    AL1(81)                  ROW 05, COL 01
         DC    X'1D60'
         DC    CL79' '
EDTROW6  DC    AL1(87)                  ROW 06, COL 01
         DC    X'1D60'
         DC    CL22'      DATASET PASSWORD'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1D4C'            NO DISPLAY
         DC    CL8' '
         DC    X'1D60'
         DC    CL45'        (IF PASSWORD PROTECTED)'
EDITOPSZ EQU   *-EDITOPT
*
         TITLE 'UTILITY OPTIONS MENU'
UTOPT    CSECT
         DC    A(UTOPT1)
         DC    A(UTOPT2)
         DC    A(UTOPT3)
         DC    A(UTOPT4)
         DC    A(UTOPT5)
         DC    A(UTOPT6)
         DC    A(UTOPT7)
         DC    A(UTOPT8)
         DC    A(UTOPT10)
         DC    A(UTOPT11)
         DC    A(UTOPT12)
         DC    X'80',AL3(UTOPT13)
UTOPT1   DS    0H
         DC    AL1(82)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    19C'-'
         DC    CL40'       ETPS  -  UTILITY OPTION MENU     '
         DC    21C'-'
UTOPT2   DC    AL1(84)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL19'SELECT OPTION ===> '
         DC    X'1D40'               SF UNPROTECTED              ATTR
         DC    X'13'                 IC                          ATTR
         DC    CL60' '
UTOPT3   DC    AL1(70)
         DC    X'1D60'               SF   PROTECTED              ATTR
         DC    CL61' '
         DC    CL7'  '
UTOPT4   DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   1  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'LIBRARY    - PRINT, RENAME, DELETE, BROWSE MEMBERS'
         DC    CL19' '
*
UTOPT5   DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   2  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'DATASET    - ALLOC, RENAME, DELETE, CATALOG, UNCATALOG'
         DC    CL19' '
*
UTOPT6   DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   3  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'MOVE/COPY  - MOVE OR COPY MEMBERS OR DATASETS         '
         DC    CL19' '
*
UTOPT7   DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   4  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'CATALOG    - DISPLAY CATALOG ENTRIES, DEFINE ALIASES  '
         DC    CL19' '
*
UTOPT8   DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   5  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'RESET      - RESET ETPS DIRECTORY DATA                '
         DC    CL19' '
*
UTOPT9   DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   6  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'HARDCOPY   - INITIATE HARDCOPY OUTPUT                 '
         DC    CL19' '
*
UTOPT10  DC    AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   7  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'VTOC       - DISPLAY OR PRINT VTOC ENTRIES            '
         DC    CL19' '
*
UTOPT11 DC     AL1(83)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   8  '
         DC    X'1D60'               SF   PROTECTED              ATTR
 DC CL54'OUTLIST    - JES2 DISPLAY FACILITY                    '
         DC    CL19' '
*
UTOPT12 DC     AL1(80)
         DC    X'1D60'               SF PROTECTED                ATTR
         DC    CL80' '
*
UTOPT13 DC     AL1(85)
         DC    X'1D60'               SF   PROTECTED              ATTR
         DC    CL6'PRESS '
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL7'END KEY'
         DC    X'1D60'               SF   PROTECTED              ATTR
         DC    CL66' TO RETURN TO PRIMARY OPTION MENU'
*
         TITLE 'ETPS - LIBRARY UTILITY MENU'
*
* THIS IS THE LIBRARY UTILITY MENU
*
UT1MENU  CSECT
         DC    A(UT1ROW1)
         DC    A(UT1ROW2)
         DC    A(UT1ROW3)
         DC    A(UT1ROW4)
         DC    A(UT1ROW5)
         DC    A(UT1ROW6)
         DC    A(UT1ROW7)
         DC    A(UT1ROW9)
         DC    A(UT1ROW10)
         DC    A(UT1ROW11)
         DC    A(UT1ROW12)
         DC    X'80',AL3(UT1ROW13)
UT1ROW1  DC    AL1(81)                  ROW 01, COL 01
         DC    X'1DE8'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21'UTILITY- ENTRY PANEL '
         DC    34C'-'
UT1ROW2  DC    AL1(34)
         DC    X'1DE8'               SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL19'SELECT OPTION ===> '
         DC    X'1D40'               SF UNPROTECTED              ATTR
         DC    X'13'                 IC                          ATTR
         DC    CL10' '
UT1ROW3  DC    AL1(12)                  ROW 02, COL 01
         DC    X'1DE8'
         DC    CL10' '
UT1ROW4  DC    AL1(73)                  ROW 02, COL 01
         DC    X'1D60',C'   '
         DC    X'1DE8',C'    C'
         DC    X'1D60'
         DC    CL25' - COMPRESS DATASET      '
         DC    X'1DE8',C'     '
         DC    X'1D60'
         DC    CL25'                         '
UT1ROW5  DC    AL1(73)                  ROW 02, COL 01
         DC    X'1D60',C'   '
         DC    X'1DE8',C'    X'
         DC    X'1D60'
         DC    CL25' - PRINT DIRECTORY LIST  '
         DC    X'1DE8',C'     '
         DC    X'1D60'
         DC    CL25'                         '
UT1ROW6  DC    AL1(73)                  ROW 02, COL 01
         DC    X'1D60',C'   '
         DC    X'1DE8',C'    L'
         DC    X'1D60'
         DC    CL25' - PRINT ENTIRE DATASET  '
         DC    X'1DE8',C'     '
         DC    X'1D60'
         DC    CL25'                         '
UT1ROW7  DC    AL1(73)                  ROW 02, COL 01
         DC    X'1D60',C'   '
         DC    X'1DE8',C'BLANK'
         DC    X'1D60'
         DC    CL25' - DISPLAY MEMBER LIST   '
         DC    X'1DE8',C'     '
         DC    X'1D60'
         DC    CL25'                         '
UT1ROW8  DC    AL1(12)                  ROW 02, COL 01
         DC    X'1DE8'
         DC    CL10' '
UT1ROW9  DC    AL1(83)                  ROW 02, COL 01
         DC    X'1DE8'
         DC    CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    X'1D60'
         DC    CL49' '
UT1ROW10 DC    AL1(85)                  ROW 03, COL 01
         DC    X'1D60'
         DC    CL22'FULLY-QUALIFIED DSNAME'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1DC8'
         DC    CL53' '
UT1ROW11 DC    AL1(87)                  ROW 04, COL 01
         DC    X'1D60'
         DC    CL22'         VOLUME SERIAL'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1DC8'
         DC    CL6' '
         DC    X'1D60'
         DC    CL47'          (IF NOT CATALOGED)'
UT1ROW12 DC    AL1(81)                  ROW 05, COL 01
         DC    X'1D60'
         DC    CL79' '
UT1ROW13 DC    AL1(87)                  ROW 06, COL 01
         DC    X'1D60'
         DC    CL22'      DATASET PASSWORD'
         DC    X'1DE8'
         DC    CL4'==>'
         DC    X'1D4C'            NO DISPLAY
         DC    CL8' '
         DC    X'1D60'
         DC    CL45'        (IF PASSWORD PROTECTED)'
         TITLE 'ETPS - FUNCTION NOT AVAILABLE DISPLAY'
*
* THIS IS DISPLAYED WHEN AN OPERATOR ATTEMPTS TO INVOKE A ETPS
* FUNCTION THAT HAS NOT YET BEEN IMPLEMENTED.
*
NOTGOT   CSECT
         DC    A(NOTGOT1)
         DC    A(NOTGOT2)
         DC    X'80',AL3(NOTGOT3)
NOTGOT1  DC    AL1(81)                  ROW 01, COL 01
         DC    X'1DE8'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21' NOT AVAILABLE       '
         DC    34C'-'
NOTGOT2  DC    AL1(32)                  ROW 02, COL 01
         DC    X'1DE8'
         DC    CL30'THE OPTION YOU HAVE SELECTED  '
NOTGOT3  DC    AL1(36)                  ROW 03, COL 01
         DC    X'1DE8'
         DC    CL29'HAS NOT YET BEEN IMPLEMENTED.'
         DC    X'1DC8'
         DC    X'13'                       IC
         DC    CL2' '
         TITLE 'ETPS - HELP NOT AVAILABLE DISPLAY'
*
* THIS IS DISPLAYED WHEN AN OPERATOR ATTEMPTS TO INVOKE A ETPS
* HELP SCREEN THAT HAS NOT YET BEEN IMPLEMENTED.
*
NOHELP   CSECT
         DC    A(NOHELP1)
         DC    A(NOHELP2)
         DC    X'80',AL3(NOHELP3)
NOHELP1  DC    AL1(81)                  ROW 01, COL 01
         DC    X'1DE8'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21' NOT AVAILABLE       '
         DC    34C'-'
NOHELP2  DC    AL1(34)                  ROW 02, COL 01
         DC    X'1DE8'
         DC    CL32'NO HELP INFORMATION IS AVAILABLE'
NOHELP3  DC    AL1(36)                  ROW 03, COL 01
         DC    X'1DE8'
         DC    CL29'FOR THIS SCREEN.             '
         DC    X'1DC8'
         DC    X'13'                       IC
         DC    CL2' '
         TITLE 'ETPS - LIBRARY MANAGEMENT'
ETPSLIB2 CSECT
*
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE2,13
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         L     R2,0(,R1)          SAVE PARM POINTER
         LA    R11,72(,R13)       LEVEL 2 MODULE, SO BUMP TO SAVE2
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         LA    R3,1
         LA    R11,4095(R3,R12)   SET PROGRAM BASE REGISTER 2
         LA    R10,SPLITWRK       SET DSECT FOR SPLIT-SCREEN
         USING SPLTAREA,10
*
         LM    R2,R8,0(R1)         PICK UP 7 PARMS
*
* R2=DSNAME
* R3=DSN LENGTH
* R4=VOLSER
* R5=DSORG
* R6=DDNAME
* R7=PASSWORD, IF ANY
* R8=MEMBER NAME, IF ANY
*
*                                ALLOCATE THE FILE
*
*
         USING PARMLIST,6
         L     R6,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R6),C' '
         MVC   001(256,R6),000(R6)
         MVC   255(255,R6),255(R6)
*
         MVC   LIBORG(3),0(R5)    DSORG
*
         SR    R1,1
         IC    R1,0(,R3)          PICK UP LENGTH
         BCTR  R1,0
         EX    R1,EXMV1           DSNAME
*
         L     R9,LIBDCBA
         MVC   DDNAME(8),40(R9) DDNAME
         MVC   DSVOLSER(6),0(R4)  VOLSER
         MVC   PASSWORD(8),0(R7)  PASSWORD
         MVI   DSSTATUS,C'S'         SET DISP=SHR
*
         MVI   EDSELMEM,C' '      CLEAR MEMBER NAME FIELD
         MVC   EDSELMEM+1(7),EDSELMEM
*
         CLI   0(R8),X'41'         WAS A MEMBER NAME PROVIDED
         BL    *+10                NOPE
*
         MVC   DSMEMBER(8),0(R8)  MEMBER NAME
*
         MVC   DSUNIT(5),=C'SYSDA'
*
         CLC   LIBORG,=C'SEQ'      IS THIS A SEQUENTIAL FILE
         BE    LIBDYN4             YUP
*
         CLC   LIBORG,=C'PAN'      IS THIS A PAN LIBRARY
         BE    LIBDYN3             YUP
*
         CLI   DSMEMBER,C' '       MEMBER SPECIFIED
         BNE   LIBDYN2             YUP
         B     LIBDYN1             NOPE, DISPLAY MEMBER SELECTION
*
EXMV1    MVC   DSNAME(1),0(R2)
         EJECT
LIBDYN1  DS    0H
*
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
         LTR   R15,15
         BNZ   ERRMS23
*
         L     R2,LIBDCBA         PICK UP LIBRARY ADDRESS
         XC    DIRPOINT(4),DIRPOINT    INITIALIZE
         XC    DIRNOTE(4),DIRNOTE
         XC    DIRTOP(4),DIRTOP
         MVI   FIRSTMEM,C' '
APDSDCB  DS    0H
*
         TM    48(R2),16      IS IT OPEN ALREADY
         BO    APDSOPND       YUP
*
*    OPEN FILE.
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((2),(UPDAT)),MF=(E,PARMOP)
*
         TM    48(R2),16      TEST FOR GOOD OPEN
         BZ    ERRMS25
*
APDSOPND DS    0H
         MVC   DECB(20),DIRECB
*
         MVI   DIREOFLG,0         INDICATE NOT AT EOF
*
*    PROCESS PDS.
*
READNXT  DS    0H
*
         LA    R4,TERMOUT+4      PICK UP ADDR OF FIRST OUTPUT LINE
         L     R8,SCROWS          PICK UP NUMBER OF ROWS
         CLI   SPLIT,0           AM I DOING SPLIT-SCREEN?
         BE    SETDIRNX          NOPE
*
         CLI   SPLIT,2           AM I ON SCREEN 2?
         BE    SETDIRN2          YUP
*
         L     R8,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         B     SETDIRNX          GO DO IT
*
SETDIRN2 DS    0H
*
         L     R8,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         SLL   R8,2              MULTIPLY BY 4
         LA    R4,TERMOUT(R8)    SET ADDRESS OF FIRST OUTPUT LINE
         L     R8,SCROWS2        PICK UP NUMBER OF ROWS ON SCREEN 2
*
SETDIRNX DS    0H
*
         NI    0(R4),X'7F'        NOT END OF PARM LIST
         L     R14,0(,R4)         PICK UP ADDR OF FIRST OUTPUT LINE
         LA    R15,DIRROW1        PICK UP FIRST LINE OF INPUT
         SR    R1,1
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
         MVC   16(44,R14),EDTDSN1
*
         CLI   LIBFUNC,C'E'       INDICATE FUNCTION
         BE    LIBROW2
*
         MVC   4(7,R14),=C'BROWSE '
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    LIBROW2
*
         MVC   4(8,R14),=C'UTILITY '
*
         LA    R14,DIRDSNAM(,14)   MOVE IN DSNAME
         MVC   DIRDSN1(44),EDTDSN1
*
LIBROW2  DS    0H
         L     R14,4(,R4)         BUMP
         NI    4(R4),X'7F'        NOT END OF PARM LIST
         LA    R15,DIRROW2        PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
         L     R14,8(,R4)         BUMP
         NI    8(R4),X'7F'        NOT END OF PARM LIST
         LA    R15,DIRROW3        PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
*
         LA    R4,12(,R4)         SET R4 TO START OF BUILD AREA
         LA    R1,4               SUBTRACT FIRST 3 ROWS
         SR    R8,R1   MAX DIRECTORY MEMBERS FOR ONE SCREEN
*
READ     EQU        *
         L     R2,LIBDCBA
         LA    R3,PDSKEY
         READ  DECB,SF,(2),(3),'S',MF=E
         CHECK DECB
         NOTE  (R2)
         ST    R1,DIRNOTE
*
         SR    R15,15
         C     R15,DIRTOP      AT TOP OF MEMBER LIST
         BNE   *+8             NO
         ST    R1,DIRTOP       YES, RESET POINTER TO TOP
*
         LA    R9,10(,R3)          LOAD ADDRESS OF FIRST MEMBER
NEXT     EQU        *
         CLI   0(R9),X'FF'         TEST FOR END OF DIRECTORY
         BE    DIRENDOF            BRANCH ON LAST USED BLOCK
*
         CLI   DIREOFLG,1          HAVE I  HIT EOF
         BE    DIRENDOF            YUP
*
         USING DIRMASK,1           BRANCH ON LAST USED BLOCK
         NI    0(R4),X'7F'        NOT END OF PARM LIST
         L     R1,0(,R4)           BRANCH ON LAST USED BLOCK
         MVI   0(R1),77           LENGTH OF DIR ENTRY
         MVC   1(77,R1),DIRLIST4  MOVE IT IN
*
*
         USING PDS2,9
*
*    FORMAT THE PDS MEMBER
*
         MVC   DIRNAME(8),PDS2NAME MOVE MEMBER NAME TO PRINT
*
         NI    PDS2INDC,X'1F'
         CLI   PDS2INDC,X'0F'
         BNE   NOTSPF
*
         LA    R14,PDS2USRD       START  OF USER AREA
         USING SPFMT,14
*
*
         UNPK  DIRCREAT(5),SPFCRDT(3)     CREATION DATE
         UNPK  DIRMOD(5),SPFMODT(3)     CREATION DATE
*
         SR    R15,15
         ICM   R15,3,SPFSIZE
         CVD   R15,PACK8
         OI    PACK8+7,X'0F'
         UNPK  DIRLINES(5),PACK8+5(3)
*
         MVC   DIRUSER(7),SPFUID
*
NOTSPF   DS    0H
*
         CLI   FIRSTMEM,C' '
         BNE   *+10
         MVC   FIRSTMEM(8),0(R9)    SET TOP OF LIST
*
         BCT   R8,DIRTNEXT         KEEP GOING
*
         B     DIRFMT
*
DIRTNEXT DS    0H
         MVC   LASTMEM(8),0(R9)    SET IT
         LA    R4,4(,4)   LOAD ADDRESS OF NEXT MEMBER
         CLC   PDSKEY,0(R9)        TEST FOR LAST MEMBER IN BLOCK
         BNH   READ
DIRBUMP1 DS    0H
         SR    R14,14
         NI    11(R9),X'1F'       CLEAR ALIAS AND TTR FLAGS
         IC    R14,11(,R9)       PICK UP USER DATA LENGTH IN HWORDS
         SLL   R14,1             MULTIPLY BY 2
         LA    R9,12(R14,R9)      LOAD ADDRESS OF NEXT MEMBER
         B     NEXT
DIRENDOF DS    0H
         MVI   DIREOFLG,1         INDICATE I HAVE HIT EOF
*
         NI    0(R4),X'7F'        NOT END OF PARM LIST
         L     R1,0(,R4)           BRANCH ON LAST USED BLOCK
         MVI   0(R1),77           LENGTH OF DIR ENTRY
         MVC   1(77,R1),DIRLIST4  MOVE IT IN
*
         MVC   DIRNAME(9),=C'** EOF **'
*
*
         BCT   R8,DIREOFUP         KEEP GOING
*
         B     DIRFMT
*
DIREOFUP DS    0H
         LA    R4,4(,4)   LOAD ADDRESS OF NEXT MEMBER
         B     DIRENDOF+4
         DROP  1
DIRFMT   DS    0H
*
         CLI   SPLIT,1            AM I ON TOP SCREEN
         BE    *+8                YUP
         OI    0(R4),X'80'        END OF PARM LIST
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R15,=A(COMM3)       GO COMMUNICATE
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   FREELIB                  OUT
*
*                   CHECK TO SEE WHETHER AN EDIT OPTION WAS ENTERED
*
         LM    R2,R5,TERMINPT      PICK UP FIRST REPLY FIELD
*
         CLI   SPLIT,2             AM I ON SECOND SCREEN
         BNE   DIRFMTPF            NO, GO TEST PFKEY
*
         L     R3,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         SLL   R3,2              MULTIPLY BY 4
         LA    R3,TERMINPT(R3)   SET ADDRESS OF FIRST INPUT LINE
         LM    R3,R5,0(R3)       PICK UP FIRST REPLY FIELD
*
DIRFMTPF DS    0H
         CLI   0(R2),X'6E'         PA 2
         BNE   DIRCHKPF            NOPE
*
         MVC   LOCNAME(8),FIRSTMEM RE-LOCATE THE TOP OF DISPLAY
         MVI   FIRSTMEM,C' '
         B     EDDIRLC2            NOPE
*
DIRCHKPF DS    0H
         MVI   FIRSTMEM,C' '
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BNE   EDDIRPFK            YUP
*
         CLI   0(R4),X'00'      COMMAND ENTERED?
         BE    EDDIRSEL         NOPE
*
         CLC   25(2,R4),=C'PF'    END OF EDIT
         BE    EDDIRPFK
         CLC   25(3,R4),=C'END'   END OF EDIT
         BE    EDDSNDUN
*
         CLC   25(3,R4),=C'ABE'   ABEND
         BE    S0C1
*
         CLI   25(R4),C'L'        LOCATE?
         BE    EDDIRLOC           YUP, GO LOCATE
         CLI   25(R4),C'F'        ALLOW "F" FOR FIND AS WELL
         BE    EDDIRLOC           GO LOCATE
*
* PUT OTHER TESTS FOR COMMAND LINE INPUT HERE
*
         WTO   'ETPSLIB2- INVALID COMMAND                 ',ROUTCDE=11
*
S0C1     DC    H'0'
*
MVEDR    MVC   0(1,R14),0(R15)    MOVE INPUT INTO INADDR WORK AREA
EDDIRSEL DS    0H
         LA    R3,TERMINPT+16     POINT TO LINE 4
         L     R14,SCROWS1       PICK UP NUMBER OF ROWS ON SCREEN 1
         CLI   SPLIT,2             AM I ON SECOND SCREEN
         BNE   EDIRSEL1            NO, GO ROLL
*
         L     R3,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         BCTR  R3,0                MAKE RELATIVE 0?????
         SLL   R3,2              MULTIPLY BY 4
         LA    R3,TERMINPT+16(R3) SET ADDRESS OF FIRST INPUT LINE
         L     R14,SCROWS2       PICK UP NUMBER OF ROWS ON SCREEN 2
*
EDIRSEL1 DS    0H
         LA    R1,3               SUBTRACT FIRST 3 ROWS
         SR    R14,R1  MAX DIRECTORY MEMBERS FOR ONE SCREEN
*
         L     R5,0(,R3)          POINT TO EACH LINE IN TURN
         CLI   0(R5),X'00'        ANY INPUT?
         BNE   *+16               YUP
         LA    R3,4(,R3)          POINT TO NEXT LINE
         BCT   R14,*-16
         B     EDMORDIR           NOTHING ENTERED, SO GO SHOW MORE
*
         CLI   2(R5),C'S'        SELECT CODE?
         BE    DIRMEMBR          YES PROCESS IT
*
* PUT TESTS FOR OTHER SELECTION CODES HERE
*
         DC    H'0'               JUST S0C1
*
DIRMEMBR DS    0H
*
         L     R1,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R1),C' '
         MVC   001(256,R1),000(R1)
         MVC   255(255,R1),255(R1)
*
         MVC   EDSELMEM(8),6(R5)    MEMBER SELECTED
         MVC   DSMEMBER(8),EDSELMEM
         L     R4,MEMDCBA
         MVC   DDNAME(8),40(R4)     SET DDNAME TO SHOW MEMBER
         MVC   DSNAME(44),EDTDSN1   SET DSNAME OF LIBRARY
         MVI   DSSTATUS,C'S'            STATUS, DISP=SHR
*
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
         LTR   R15,15
         BNZ   ERRMS32
*
         LA    R1,AMEMEOF1
         STCM  R1,7,33(R4)
         SR    R3,3           CLEAR RECORD COUNTER
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((4),(INPUT)),MF=(E,PARMOP)
*
         TM    48(R4),X'10'   GOOD OPEN
         BZ    ERRMS28        NOPE
*
AMEMCOUN DS    0H
*
         GET   (4),EDAFTSV1
         LA    R3,1(,3)       BUMP COUNTER
         B     AMEMCOUN
*
ERRMS28  WTO   'ETPS1028 OPEN FAILED FOR LIB(MEM)         ',ROUTCDE=11
         LA    R1,ERRMS28+13
         ST    R1,MSGADD
         B     AMEMEOFF           SKIP IT
ERRMS26  WTO   'ETPS1026 PREMATURE EOF                    ',ROUTCDE=11
         LA    R1,ERRMS26+13
         ST    R1,MSGADD
         B     AMEMEOFF           SKIP IT
AMEMEOF1 DS    0H
*
         MVI   PARMOP,128
         CLOSE ((4)),MF=(E,PARMOP)
*
AMEMEOFF DS    0H
         ST    R3,SEQRNUM
         LA    R3,SEQRNUM
         LA    R2,DSNAME
         LA    R8,DSMEMBER
         LA    R5,LIBORG
*
         L     R15,=A(EDIT3)
* PARM LIST FOR EDITOR IS : DCB, DSN, MEMBER NAME, DSORG, NUM RECS
         CALL  (15),((4),(2),(8),(5),(3)),                             X
               VL,MF=(E,PARM43)
*
         L     R1,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R1),C' '
         MVC   001(256,R1),000(R1)
         MVC   255(255,R1),255(R1)
*
         MVI   DSNAME,X'00'       INDICATE FREE REQUEST
         MVC   DSNAME+1(43),DSNAME
         L     R4,MEMDCBA
         MVC   DDNAME(8),40(R4)     SET DDNAME TO SHOW MEMBER
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
         LTR   R15,R15
         BZ    AMEMEOFG
         WTO   'ETPSLIB2 - DYN FREE FOR LIB(MEM) FAILED   ',ROUTCDE=11
*
AMEMEOFG DS    0H
*
         MVC   LOCNAME(8),DSMEMBER MOVE IN "LOCATE" NAME
         MVI   DSMEMBER,C' '      CLEAR MEMBER NAME FIELD
         MVC   DSMEMBER+1(7),DSMEMBER
         MVI   EDSELMEM,C' '      CLEAR MEMBER NAME FIELD
         MVC   EDSELMEM+1(7),EDSELMEM
         B     EDDIRLC2
*
ERRMS32  WTO   'ETPS1032 ALLOC FAILED FOR LIB(MEM)        ',ROUTCDE=11
         LA    R1,ERRMS32+13
         ST    R1,MSGADD
         L     R1,DYNWORK1         PICK UP PARAMETER BASE
         LM    R2,R3,DYNRCODE      PICK UP ERROR CODES
         DC    H'0'
         B     AMEMEOFG           SKIP IT
EDDIRLOC DS    0H
*                   LOCATE MEMBER IN LIBRARY
         MVC   LOCNAME(8),27(R4)   MOVE IN "LOCATE" NAME
EDDIRLC2 DS    0H
         L     R2,LIBDCBA          POINT TO START OF LIBRARY DIR
         POINT (2),POINT0
         LA    R3,PDSKEY
EDLOCRD  DS    0H
         READ  DECB,SF,(2),(3),'S',MF=E
         CHECK DECB
         NOTE  (R2)
         ST    R1,DIRNOTE
*
         CLC   PDSKEY(8),LOCNAME
         BL    EDLOCRD             NOPE
*
         CLI   DIRNOTE+2,1         AM I ON BLOCK 1
         BE    EDLOCTRK             YES, JUST SUBTRACT FROM TRACK
         SR    R2,2
         IC    R2,DIRNOTE+2        PICK UP BLOCK NUMBER
         BCTR  R2,0                 SUBTRACT 1
         STC   R2,DIRNOTE+2
*
EDLOCP2  DS    0H
*                   LOCATE MEMBER IN LIBRARY
         L     R2,LIBDCBA
         POINT (2),DIRNOTE
         B     APDSDCB
EDLOCTRK DS    0H
*                   LOCATE MEMBER IN LIBRARY
         SR    R2,2
         LH    R2,DIRNOTE          PICK UP TRACK NUMBER
         LTR   R2,2                IF I'M ON TRACK 0
         BZ    EDLOCP2             JUST GO READ THE BLOCK
         BCTR  R2,0                 SUBTRACT 1
         STH   R2,DIRNOTE
         MVI   DIRNOTE+2,20        SET TO MIDDLE OF TRACK
         LA    R2,LIBDCBA
         POINT (2),DIRNOTE
         B     APDSDCB
POINT0   DC    F'256'
EDDIRPFK DS    0H
*                   PF KEY WAS ENTERED, SEE WHAT IT WAS
*
         CLI   0(R2),X'7D'         IS IT ENTER
         BE    EDDIRSEL            YUP  - GO SEE WHAT WAS ENTERED
*
         CLI   0(R2),X'F3'         IS IT PF3
         BE    EDDSNDUN            YUP  - ALL DONE
         CLI   0(R2),X'C3'         IS IT PF15
         BE    EDDSNDUN            YUP  - ALL DONE
*
         CLI   0(R2),X'F8'         IS IT PF8
         BE    EDMORDIR            YUP  - SHOW NEXT SCREEN OF DIR
         CLI   0(R2),X'C8'         IS IT PF20
         BE    EDMORDIR            YUP  - SHOW NEXT SCREEN OF DIR
*
         CLI   0(R2),X'F7'         IS IT PF7
         BE    EDPREDIR            YUP  - SHOW PREV SCREEN OF DIR
         CLI   0(R2),X'C7'         IS IT PF19
         BE    EDPREDIR            YUP  - SHOW PREV SCREEN OF DIR
*
         CLI   0(R2),X'6C'         IS IT PA1
         BE    EDDSNDUN            YUP  - ALL DONE
*
         CLI   0(R2),X'6E'         IS IT PA2
         BE    DIRFMT              YUP  - GO RESHOW THE SCREEN
*
         CLI   0(R2),X'6D'         IS IT "CLEAR"
         BE    DIRFMT              YUP  - GO RESHOW THE SCREEN
*
         B     EDDSNDUN            ALL OTHER PFK'S
*
EDMORDIR DS    0H
*                   SHOW MORE DIRECTORY INFO
*
*
         LA    R4,TERMOUT+4      PICK UP ADDR OF FIRST OUTPUT LINE
         L     R8,SCROWS1         PICK UP NUMBER OF ROWS
         CLI   SPLIT,2           AM I DOING SPLIT-SCREEN?
         BNE   SETMORNX          NOPE
*
SETMORN2 DS    0H
*
         SLL   R8,2              MULTIPLY BY 4
         LA    R4,TERMOUT(R8)    SET ADDRESS OF FIRST OUTPUT LINE
         L     R8,SCROWS2        PICK UP NUMBER OF ROWS ON SCREEN 2
*
SETMORNX DS    0H
*
         NI    0(R4),X'7F'        NOT END OF PARM LIST
         L     R14,0(,R4)         PICK UP ADDR OF FIRST OUTPUT LINE
*
         LA    R15,DIRROW1        PICK UP FIRST LINE OF INPUT
         SR    R1,1
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
         MVC   16(44,R14),EDTDSN1
*
         CLI   LIBFUNC,C'E'       INDICATE FUNCTION
         BE    LIBROW22
*
         MVC   4(7,R14),=C'BROWSE '
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    LIBROW22
*
         MVC   4(8,R14),=C'UTILITY '
*
         LA    R14,DIRDSNAM(,14)   MOVE IN DSNAME
         MVC   DIRDSN1(44),EDTDSN1
*
LIBROW22 DS    0H
         L     R14,4(,R4)         BUMP
         NI    4(R4),X'7F'        NOT END OF PARM LIST
         LA    R15,DIRROW2        PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
         L     R14,8(,R4)         BUMP
         NI    8(R4),X'7F'        NOT END OF PARM LIST
         LA    R15,DIRROW3        PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
*
         LA    R4,8(,4)           SET R4 TO START OF BUILD AREA, -1 ROW
         LA    R1,3               SUBTRACT FIRST 3 ROWS
         SR    R8,R1   MAX DIRECTORY MEMBERS FOR ONE SCREEN
*
         L     R2,LIBDCBA
         XC    DIRTOP(4),DIRTOP
         CLI   LOCNAME,C' '        "LOCATE" NAME PRESENT
         BE    DIRTNEXT            NOPE
         LA    R4,4(,4)           SET R4 TO START OF BUILD AREA
         B     READ
*
EDPREDIR DS    0H
*
*                   SHOW PREV DIRECTORY INFO
*
         MVC   DIRPOINT(4),DIRTOP   SET LAST BLOCK READ
         CLI   DIRPOINT+2,1         AM I ON BLOCK 1
         BE    EDPRETRK             YES, JUST SUBTRACT FROM TRACK
*
         SR    R2,2
         IC    R2,DIRPOINT+2        PICK UP TRACK NUMBER
         BCTR  R2,0                 SUBTRACT 1
         STC   R2,DIRPOINT+2
         CLI   DIRPOINT+2,1         AM I ON BLOCK 1
         BE    EDPRETRK             YES, JUST SUBTRACT FROM TRACK
         BCTR  R2,0                 SUBTRACT 2
         STC   R2,DIRPOINT+2
         CLI   DIRPOINT+2,1         AM I ON BLOCK 1
         BE    EDPRETRK             YES, JUST SUBTRACT FROM TRACK
         BCTR  R2,0                 SUBTRACT 3
         STC   R2,DIRPOINT+2
*
         B     EDPREPOI             GO POINT TO IT
*
EDPRETRK DS    0H
         SR    R2,2
         CH    R2,DIRPOINT          AM I ON TRACK 0
         BE    EDPREPOI             GO POINT TO IT
         LH    R2,DIRPOINT          AM I ON TRACK 0
         BCTR  R2,0
         STH   R2,DIRPOINT          AM I ON TRACK 0
         MVI   DIRPOINT+2,X'10'     POINT TO BLOCK 16
EDPREPOI DS    0H
         L     R2,LIBDCBA
         POINT (2),DIRPOINT
         XC    DIRTOP(4),DIRTOP
         MVI   DSMEMBER,C' '      CLEAR MEMBER NAME FIELD
         MVC   DSMEMBER+1(7),DSMEMBER
         MVI   LOCNAME,C' ' NOTLOOKING FOR PARTICULAR MEMBER
         B     APDSDCB
*
         EJECT
LIBDYN2  DS    0H
         B     FREELIB             AND I'M DONE
*
         EJECT
LIBDYN3  DS    0H
*
         B     FREELIB
*
         EJECT
LIBDYN4  DS    0H
*
         B     FREELIB             AND I'M DONE
*
         EJECT
EDDSNDUN DS    0H
*                   DONE EDITING THIS FILE, SO FREE IT
*
FREELIB  DS    0H
         LA    R4,LIBDCBA
         LA    R3,4
BUMPLIB1 DS    0H
         L     R2,0(,R4)
         TM    48(R2),X'10'
         BZ    BUMPLIB2
         MVI   PARMOP,128
         CLOSE ((2)),MF=(E,PARMOP)
BUMPLIB2 DS    0H
*
         L     R1,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R1),C' '
         MVC   001(256,R1),000(R1)
         MVC   255(255,R1),255(R1)
*
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         MVC   DDNAME(8),40(R2)     SET DDNAME FOR FREE
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
         LA    R4,4(,4)
         BCT   R3,BUMPLIB1        SEQ ALLOCATED
EOJ      DS    0H
DIRRETRN DS    0H
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R14,R12,12(R13)          RESTORE CALLERS REGS
         BR    R14                      NORMAL RETURN
ERRMS23  DS    0H
         CLC   DYNRCODE(2),ALLO0210
         BE    ERRINUSE
         WTO   'ETPS1023 ALLOC FAILED FOR LIB             ',ROUTCDE=11
         LA    R1,ERRMS23+17
         ST    R1,MSGADD
         L     R1,DYNWORK1         PICK UP PARAMETER BASE
         B     EDDSNDUN            YUP  - ALL DONE
ERRINUSE DS    0H
         LA    R1,ALLO0210+2
         ST    R1,MSGADD
         B     EDDSNDUN            YUP  - ALL DONE
ALLO0210 DC    X'0210',CL20'0210-DATASET IN USE '
ERRMS25  WTO   'ETPS1025 OPEN FAILED FOR LIB             ',ROUTCDE=11
         LA    R1,ERRMS25+13
         ST    R1,MSGADD
         B     EDDSNDUN            YUP  - ALL DONE
         LTORG
*
PDSDIR   DCB      DSORG=PS,MACRF=(RP,WP),DDNAME=PPROCLIB,RECFM=F,      C
               LRECL=256,BLKSIZE=256,KEYLEN=8,EODAD=DIRENDOF
         READ  DIRECB,SF,PDSDIR,PDSDIR,'S',MF=L
         TITLE 'ETPS - DIRECTORY LIST FORMAT'
*
* THIS IS THE FORMAT FOR A PDS OR PAN LIBRARY DIRECTORY LISTING.
*
DIRTITLE DS    0F
DIRROW1  DC    AL1(81)
         DC    X'1DE8'
         DC    CL8'   EDIT '
         DC    C'- '
DIRDSNAM EQU   *-DIRTITLE
         DC    69C'-'
DIRROW2  DC    AL1(85)
         DC    X'1DE8'
         DC    CL19'COMMAND INPUT ===> '
         DC    X'1D40'
DIRHOME  EQU   *-DIRTITLE
         DC    X'13'                         IC
         DC    CL40' '
         DC    X'1DE8'
         DC    CL13' SCROLL ===> '
         DC    X'1D40'
         DC    CL4'PAGE'
DIRROW3  DC    AL1(71)
         DC    X'1D60'
         DC    CL23'   NAME       NEWNAME '
         DC    CL6'LEVEL '
         DC    CL10' CREATED  '
         DC    CL16' LAST MODIFIED  '
         DC    CL4'    '
         DC    CL6'SIZE  '
         DC    CL4' ID '
DIRTLENG EQU   *-DIRTITLE
*
DIRLIST4 DS    0XL1
       DC X'1D40',C' ',X'1D60',CL12' ',X'1D40',CL10' ',X'1D60',CL46' '
DIRELEN  EQU   *-DIRLIST4
*
         TITLE 'ETPS - EDITOR'
EDIT3    CSECT
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE3,13
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         L     R2,0(,R1)          SAVE PARM POINTER
         LA    R11,72(,R13)       LEVEL 3 MODULE, SO BUMP TO SAVE3
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         LA    R3,1
         LA    R11,4095(R3,R12)   SET PROGRAM BASE REGISTER 2
         LA    R10,SPLITWRK       SET DSECT FOR SPLIT-SCREEN
         USING SPLTAREA,10
*
* PARM LIST FOR EDITOR IS : DCB, DSN, MEMBER NAME, DSORG, NUM RECS
*
         LM    R2,R6,0(R1)              PICK UP THE REGISTERS
*
*  R2=DCB ADDRESS
*  R3=DSN ADDRESS
*  R4=MEMBER NAME ADDRESS
*  R5=DSORG ADDRESS
*  R6=RECORD COUNT ADDRESS
*
         L     R6,0(,R6)                PUT RECORD COUNT IN R6
*
         ST    R2,EDTDCB                SAVE DCB ADDRESS
         MVC   EDTDSN(44),0(R3)         SAVE DSNAME
         MVC   EDTMEM(8),0(R4)          SAVE MEMBER NAME
         MVC   EDTORG(3),0(R5)          SAVE DSORG
         ST    R6,EDTRECS               SAVE REC COUNT
         ST    R6,EDNOWLN1              SAVE REC COUNT
*
         LA    R7,22(,R6)               SET UP FOR MULTIPLY (ADD 22)
         SR    R6,R6                    SET UP FOR MULTIPLY
         LA    R1,80                    MULTIPLIER
         MR    R6,R1                    MULTIPLY
*
         GETMAIN R,LV=(R7)
         ST    R1,EDTMAIN1              SAVE GETMAIN ADDRESS
         ST    R7,EDTLENG1              SAVE GETMAIN LENGTH
         LA    R7,0(R1,R7)              ADDR OF LAST BYTE
         ST    R7,EDTLAST1              SAVE LAST BYTE ADDRESS
*
         LR    R7,R1                    START LOCATION
         LA    R1,20                    20 CARDS
*
         MVI   72(R7),X'00'             INDICATE BLANK CARD
         LA    R7,80(,7)                BUMP TO NEXT CARD
         BCT   R1,*-8                   LOOP
*
EDTQORG  DS    0H
*        CLC   EDTORG(3),=C'PAN'        PAN LIBRARY
*        BE    EDTPAN                   GO DO IT
*
         TITLE 'ETPS - EDITOR    READ IN NON-PAN DS'
EDTSEQ   DS    0H
*
         L     R2,MEMDCBA     SET EODAD
         LM    R0,R1,40(R2)         SAVE DDNAME
         MVC   0(96,R2),GMVDCB      SET MACRF TO GET-MOVE
         STM   R0,R1,40(R2)        RESET DDNAME
         LA    R1,EDTEOF1     SET EODAD
         STCM  R1,7,33(R2)
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((2),(INPUT)),MF=(E,PARMOP)
*
         TM    48(R2),X'10'   TEST FOR GOOD OPEN
         BZ    WTOMS51        NOT OPEN
*
         L     R3,EDTMAIN1    PICK UP STORAGE AREA
         LA    R3,1600(,3)    CURRENT LINE POINTER
         ST    R3,EDTCURR1    CURRENT LINE POINTER
         ST    R3,EDTFIRST    FIRST LINE
         LA    R4,10          FIRST LINE NUMBER
*
EDTGET1  DS    0H
*
         GET   (R2),(R3)
*
         CVD   R4,PACK8
         OI    PACK8+7,X'0F'
         UNPK  72(6,R3),PACK8+5(3)
*
         LA    R3,80(,3)
         LA    R4,10(,4)
         B     EDTGET1
*
         TITLE 'ETPS - EDITOR    DISPLAY MEM'
EDTEOF1  DS    0H
*
         MVI   PARMOP,128
         CLOSE ((2)),MF=(E,PARMOP)
*     FREEPOOL (2)
         MVI   EDTEOF,0     NOT EOF
         XC    EDTRPTA1(4),EDTRPTA1 CLEAR ADDRESS POINTER
         MVI   EDAFTSV1,C' '      CLEAR LINE SAVE AREA
*
EDTSHOW  DS    0H
*
         LM    R6,R9,TERMOUT     LINES TO BUILD IN
         LA    R5,TERMOUT+8      FIRST DATA LINE ADDRESS
         LA    R1,TERMOUT        POINT TO START
*
         CLI   SPLIT,2           AM I ON SCREEN 2?
         BNE   EDTSHOW1          NOPE, JUST GO DO IT
*
         L     R3,SCROWS1        PICK UP NUMBER OF ROWS ON SCRN1
         BCTR  R3,0              SUBTRACT 1 FOR LUCK
         SLL   R3,2              MULTIPLY BY 4
         LA    R1,TERMOUT(R3)    POINT TO START
         LM    R6,R9,0(R1)       LINES TO BUILD IN
         LA    R5,8(,R1)         FIRST DATA LINE ADDRESS
*
EDTSHOW1 DS    0H
         MVC   0(81,R7),DSNROW1  SET FIRST ROW
*
         CLI   LIBFUNC,C'E'       INDICATE FUNCTION
         BE    *+10
         MVC   4(7,R7),=C'BROWSE '
*
         MVC   0(85,R8),DSNROW2  SET NEXT ROW
*
         NI    0(R1),X'7F'       NOT LAST PARM
         NI    4(R1),X'7F'       NOT LAST PARM
         NI    8(R1),X'7F'       NOT LAST PARM
*
         L     R3,EDTCURR1       FIRST LINE
         L     R4,EDTLAST1       LAST LINE
         L     R6,SCROWS1         PICK UP NUMBER OF ROWS
         CLI   SPLIT,2           AM I IN SPLIT SCREEN
         BNE   EDTMOV0           NOPE, JUST GO DO IT
         L     R6,SCROWS2        MUST BE SCREEN 2
*
EDTMOV0  DS    0H
         LA    R1,3               SUBTRACT FIRST 3 ROWS
         SR    R6,R1   MAX LINES ON SCREEN
*
EDTMOV1  DS    0H
*
         CR    R3,R4
         BNL   EDTEND2          EOF BEFORE END OF SCREEN
*
         CLI   72(R3),X'FF'     CHECK FOR DELETED LINE
         BE    EDTBMP1          SKIP THIS LINE
*
         CLI   72(R3),X'00'     CHECK FOR DELETED LINE
         BE    EDTBMP1          SKIP THIS LINE
*
         NI    0(R5),X'7F'  NOT LAST PARM
         LA    R5,4(,R5)
*
         L     R1,0(,R5)    PICK UP TERMOUT POINTER
         MVC   0(83,R1),DSNLINE  MOVE IN MASK
*
         MVC   3(6,R1),72(R3)   MOVE IN LINE NUMBER
*
         MVC   11(72,R1),0(R3)  MOVE IN LINE DATA
*
         LA    R3,80(,3)        BUMP
         BCT   R6,EDTMOV1
*
         CLI   SPLIT,1           AM I ON SCREEN 1
         BE    EDCOMM            YES, DON'T SET PARM FLAG
         OI    0(R5),X'80'  SET LAST PARM
         B     EDCOMM           GO SHOW THE SCREEN
*
EDTBMP1  DS    0H
*
         LA    R3,80(,3)        BUMP
         B     EDTMOV1          LOOP
*
EDTEND2  DS    0H
         MVI   EDTEOF,1         EOF
*
         NI    0(R5),X'7F'  NOT LAST PARM
         LA    R5,4(,R5)
*
         L     R1,0(,R5)    PICK UP TERMOUT POINTER
         MVC   0(83,R1),DSNLINE  MOVE IN MASK
*
         MVC   3(6,R1),=C'**EOF*'
*
         BCT   R6,EDTEND2+4
*
         CLI   SPLIT,1           AM I ON SCREEN 1
         BE    EDCOMM            YES, DON'T SET PARM FLAG
         OI    0(R5),X'80'  SET LAST PARM
*
EDCOMM   DS    0H
*
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R15,=A(COMM4)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   EDTFREE1                 OUT
*
         L     R2,TERMINPT
         CLI   PFKFLAG,0           PFKEY?
         BE    EDLINES             NOPE
*
         CLI   0(R2),X'6C'         IS IT PA1
         BE    EDTFREE1            YUP  - ALL DONE
*
         CLI   0(R2),X'6E'         IS IT PA2
         BE    EDRESCR             YUP  - GO RESHOW THE SCREEN
*
         CLI   0(R2),X'6D'         IS IT "CLEAR"
         BE    EDRESCR             YUP  - GO RESHOW THE SCREEN
*
EDLINES  DS    0H
*
* PUT MODIFIED LINES LOOP HERE
*
         L     R3,EDTCURR1        PICK UP CURRENT LINE POINTER
         LA    R4,TERMINPT+12     POINT TO LINE1 TERMINAL INPUT ADDR
         L     R2,SCROWS1         PICK UP NUMBER OF ROWS
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   *+22                     NOPE
*
         L     R1,SCROWS1          PICK UP SIZE OF SCREEN1
         BCTR  R1,0                MAKE RELATIVE 0?????
         SLL   R1,2                MULTIPLY BY 4
         LA    R4,TERMINPT+12(R1)  PICK UP RESPONSES
         L     R2,SCROWS2          PICK UP NUMBER OF ROWS
*
         LA    R1,2               SUBTRACT FIRST 2 ROWS
         SR    R2,R1   MAX LINES ON SCREEN
*
         CLI   72(R3),X'FF'       THIS LINE DELETED?
         BE    *+12               YES, BUMP TO NEXT
         CLI   72(R3),X'00'       THIS LINE DELETED?
         BNE   *+12               NOPE, NO BUMP
         LA    R3,80(,3)          BUMP TO LINE IN BUFFER
         B     *-20               CHECK THIS LINE
*
EDLINESL DS    0H
         L     R5,0(,R4)          POINT TO LINE1 TERMINAL INPUT
         CLI   0(R5),X'00'      WAS THIS LINE CHANGED BY THE OPERATOR?
         BNE   EDLINEIN         YUP.
*
EDLINEUP DS    0H
         LA    R4,4(,4)           BUMP TO NEXT TERM INPUT ADDRESS
         LA    R3,80(,3)          BUMP TO LINE IN BUFFER
         C     R3,EDTLAST1        IF I'VE LOOKED AT ALL THE LINES,
         BNL   EDLINESN           CHECK FOR COMMANDS, PFK
         CLI   72(R3),X'FF'       THIS LINE DELETED?
         BE    *-16               BUMP TO NEXT
         CLI   72(R3),X'00'       THIS LINE DELETED?
         BE    *-24               BUMP TO NEXT
         BCT   R2,EDLINESL        LOOP
         B     EDLINESN           CHECK FOR COMMANDS, PFK
*
EDLINEIN DS    0H
*
*  MOVE ONE LINE OF DATA
*
         MVC   0(72,R3),11(R5)    MOVE IN LINE DATA
*
*  NEXT TEST FOR A LINE COMMAND
*
         LA    R6,2(,R5)         POINT TO LINE NUMBER COLUMNS
         LA    R7,6              SET LOOP CONTROL
*
EDLINCOL DS    0H
         CLI   0(R6),C'D'         LINE DELETE
         BE    EDLINDEL           DELETE IT
         CLI   0(R6),C'C'         LINE COPY
         BE    EDLINCOP           REPEAT IT
         CLI   0(R6),C'A'         LINE COPY AFTER LOCATION
         BE    EDLINAFT           REPEAT IT
         CLI   0(R6),C'R'         LINE REPEAT
         BE    EDLINRPT           REPEAT IT
*
*  PUT TESTS FOR OTHER LINE COMMANDS HERE
*
EDBUMPCL DS    0H
         LA    R6,1(,6)           BUMP POINTER
         BCT   R7,EDLINCOL        LOOP
         B     EDLINEUP           CHECK FOR NEXT LINE
*
EDLINDEL DS    0H
*
*  DELETE ONE LINE
*
         MVI   72(R3),X'FF'             SET DELETE FLAG
         B     EDLINEUP           CHECK FOR NEXT LINE
*
EDLINCOP DS    0H
*
*  COPY ONE LINE
*
         MVC   EDAFTSV1(80),0(R3) SAVE THIS LINE
         L     R7,EDTRPTA1       DO I HAVE AN "AFTER" LOCATION
         LTR   R7,7
         BNZ   EDGOTAFT          YUP, GO MOVE IT IN
         B     EDLINEUP           CHECK FOR NEXT LINE
*
EDLINAFT DS    0H
*
*  GOT "AFTER" LOCATION
*
         BAL   R14,EDLINRP1        REPEAT THE LINE
*
         CLI   EDAFTSV1,C' '      DO I HAVE A LINE TO MOVE IN
         BE    EDLINEUP           NOPE, JUST CHECK FOR NEXT LINE
*
         L     R7,EDTRPTA1        PICK UP ADDRESS OF REPEATED LINE
EDGOTAFT DS    0H
         XC    EDTRPTA1(4),EDTRPTA1 CLEAR ADDRESS POINTER
         MVC   0(80,R7),EDAFTSV1  MOVE IN THE LINE TO BE COPIED
         MVI   EDAFTSV1,C' '      CLEAR LINE SAVE AREA
         B     EDRENUM             RENUMBER AND RESCREEN
*
EDLINRPT DS    0H
         BAL   R14,EDLINRP1        REPEAT THE LINE
         XC    EDTRPTA1(4),EDTRPTA1 CLEAR ADDRESS POINTER
         B     EDRENUM             RENUMBER AND RESCREEN
EDLINRP1 DS    0H
*
*  REPEAT ONE LINE
*
         L     R7,EDTMAIN1        PICK UP FIRST LINE
         LA    R7,1600(,7)        20 LINES OF GAS
         LA    R1,20              LOOP CONTROL
         LA    R0,80              LINE LENGTH
*
         CLI   72(R7),X'00'       AVAILABLE LINE
         BE    *+14               GOT ONE
         SR    R7,R0              BACK UP ONE
         BCT   R1,*-10            KEEP LOOPING
         B     EDNORPT            CAN'T DO REPEAT
*
         ST    R7,EDTFIRST        NEW START LOCATION
         L     R1,EDNOWLN1        NUMBER OF LINES
         LA    R1,1(,1)           BUMP 1
         ST    R1,EDNOWLN1
*
         MVC   0(80,R7),80(R7)
         LA    R7,80(,7)         BUMP
         CR    R7,R3
         BNE   *-12
*
         MVC   0(80,R7),0(R3)     MOVE IN REPEATED LINE
         ST    R7,EDTRPTA1        SAVE ADDRESS
*
         L     R7,EDTCURR1         PICK UP CURRENT LINE POINTER
         LA    R1,80
         SR    R7,R1
         ST    R7,EDTCURR1         RESET CURRENT LINE POINTER
*
         BR    R14                 RETURN
*
EDNORPT  DS    0H
         B     EDRESCR             CAN'T DO REPEAT
*
EDLINESN DS    0H
         L     R3,TERMINPT+8       COMMAND LINE
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   *+18                     NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         BCTR  R3,0                MAKE RELATIVE 0?????
         SLL   R3,2                MULTIPLY BY 4
         L     R3,TERMINPT+8(R3)   PICK UP RESPONSES
*
         CLI   PFKFLAG,1           PFK FLAG
         BE    EDMEMPFK            YUP
*
         CLI   0(R3),X'00'         COMMAND ENTERED
         BNE   EDCOMMND            YUP
*
         B     EDRESCR             RESHOW THE SCREEN
*
EDCOMMND DS    0H
*
* PUT TESTS FOR COMMAND LINE HERE
*
         CLC   25(6,R3),=C'RENUM ' IS IT A RENUM COMMAND
         BE    EDRENUM             GO DO IT
*
         CLC   25(6,R3),=C'SAVE  ' IS IT A SAVE COMMAND
         BE    EDTSAVE1            GO DO IT
*
         CLC   25(3,R3),=C'SUB'    IS IT A SUBMIT COMMAND
         BE    EDTSUBMT            GO DO IT
*
         CLC   25(4,R3),=C'END '   IS IT AN END COMMAND
         BE    EDTSAVE1            GO DO IT
         CLC   25(4,R3),=C'PF3 '   IS IT AN END COMMAND
         BE    EDTSAVE1            GO DO IT
         CLC   25(6,R3),=C'CANCEL' IS IT "CANCEL"
         BE    EDTFREE1            GO DO IT
*
         CLC   25(6,R3),=C'RESET ' IS IT A RESET COMMAND
         BE    EDRESCR             RESHOW THE SCREEN
*
         CLC   25(2,R3),=C'F '     IS IT "FIND"  COMMAND
         BE    EDFIND1             SET FIND STRING
         CLC   25(2,R3),=C'L '     IS IT "FIND"  COMMAND
         BE    EDFIND1             SET FIND STRING
*
         CLI   PFKFLAG,1           PFK FLAG
         BE    EDMEMPFK            YUP
*
         B     EDRESCR             RESHOW THE SCREEN
*
EDRENUM  DS    0H
*                                  RENUMBER THE DS BY 10
         L     R3,EDTFIRST        NEW START LOCATION
         L     R1,EDNOWLN1        NUMBER OF LINES
         LA    R4,10
*                                  RENUMBER THE DS BY 10
         CLI   72(R3),X'00'       BLANK LINE
         BE    *+30               YUP
         CLI   72(R3),X'FF'       DELETED LINE
         BE    *+22               YUP
*
         CVD   R4,PACK8
         OI    PACK8+7,X'0F'
         UNPK  72(6,R3),PACK8+5(3)
*
         LA    R4,10(,4)
         LA    R3,80(,3)
         BCT   R1,*-38
*                                  RENUMBER THE DS BY 10
         B     EDRESCR             RESHOW THE SCREEN
*
EDINPUT  DS    0H
*                   PROCESS ONE UPDATED LINE
*
EDRESCR  DS    0H
*                   RE-DISPLAY THE SCREEN
         B     EDTSHOW             RE-DISPLAY
*
*
EDFIND1  DS    0H
*
         LA    R14,67(,R3)      SET END OF FIELD
         LA    R15,40           MAXIMUM LENGTH
         SR    R1,1             COUNTER
*
         CLI   0(R14),X'41'     IF NOT NUMERIC
         BH    *+10
         BCTR  R14,0            DECREMENT BASE ADDRESS
         BCT   R15,*-10
*
         CLI   27(R3),X'7D'     QUOTED STRING
         BNE   EDFINDNQ         NO
         BCTR  R15,0
         BCTR  R15,0
         STC   R15,EDFINDL      SET LENGTH MINUS 2
         EX    R15,FNDMOVE2     MOVE IT TO WORK AREA
         L     R1,EDTCURR1      PICK UP LINE POINTER
         LR    R15,R1              COPY START OF LINE
         B     EDRFIND2         FIND IT
*
EDFINDNQ DS    0H
         STC   R15,EDFINDL      SET LENGTH MINUS 1 (FROM "BCT", ABOVE)
         EX    R15,FNDMOVE1     MOVE STRING TO WORK AREA
         L     R1,EDTCURR1      PICK UP LINE POINTER
         LR    R15,R1              COPY START OF LINE
         B     EDRFIND2         FIND IT
*
FNDMOVE1 MVC   EDFINDS(1),27(R3)
FNDMOVE2 MVC   EDFINDS(1),28(R3)
FNDCLC   CLC   EDFINDS(1),0(R1)
*
EDRFIND  DS    0H
*                   FIND THE NEXT OCCURRENCE OF A STRING
         L     R1,EDTCURR1      PICK UP LINE POINTER
         LA    R1,80(,R1)       BUMP TO NEXT LINE
         LR    R15,R1              COPY START OF LINE
*
EDRFIND2 DS    0H
*
         C     R15,EDTLAST1     IF PAST END OF FILE
         BH    EDRFNOTF         ALL DONE
*
         SR    R14,R14             CLEAR THE REGISTER
         IC    R14,EDFINDL         RE-DISPLAY
         LA    R0,72
         SR    R0,R14             SET R15 TO MAXIMUM LINE LOOP
*
         EX    R14,FNDCLC          SEARCH FOR THE STRING
         BE    EDRFINDZ            GOT IT
         LA    R1,1(,R1)         BUMP 1 BYTE
         BCT   R0,*-12            GOT IT
*
         LA    R15,80(,R15)        BUMP TO NEXT LINE
         LR    R1,R15              COPY START OF LINE
         B     EDRFIND2         ALL DONE
*
EDRFINDZ DS    0H
*
         ST    R15,EDTCURR1        SET LINE POINTER
         LA    R1,EDRFOUND         FOUND
         ST    R1,MSGADD
         B     EDTSHOW             RE-DISPLAY
*
EDRFNOTF DS    0H
*
         LA    R1,EDNOTFMS         NOT FOUND
         ST    R1,MSGADD
         B     EDTSHOW             RE-DISPLAY
*
EDNOTFMS DC    CL20'STRING NOT FOUND'
EDRFOUND DC    CL20'STRING FOUND'
*
EDMEMPFK DS    0H
*                   PF KEY WAS ENTERED, SEE WHAT IT WAS
         L     R2,TERMINPT
*
         CLI   0(R2),X'F3'         IS IT PF3
         BE    EDTSAVE1            YUP  - ALL DONE
         CLI   0(R2),X'C3'         IS IT PF15
         BE    EDTSAVE1            YUP  - ALL DONE
*
         CLI   0(R2),X'F4'         IS IT PF4
         BE    EDTFREE1            CANCEL - NO SAVE
         CLI   0(R2),X'C4'         IS IT PF4
         BE    EDTFREE1            CANCEL - NO SAVE
*
         CLI   0(R2),X'F5'         IS IT PF5
         BE    EDRFIND             REPEAT FIND
         CLI   0(R2),X'C5'         IS IT PF5
         BE    EDRFIND             REPEAT FIND
*
         CLI   0(R2),X'F8'         IS IT PF8
         BE    EDMORMEM            YUP  - SHOW NEXT SCREEN OF DIR
         CLI   0(R2),X'C8'         IS IT PF20
         BE    EDMORMEM            YUP  - SHOW NEXT SCREEN OF DIR
*
         CLI   0(R2),X'F7'         IS IT PF7
         BE    EDPREMEM            YUP  - SHOW PREV SCREEN OF DIR
         CLI   0(R2),X'C7'         IS IT PF19
         BE    EDPREMEM            YUP  - SHOW PREV SCREEN OF DIR
*
*
         B     EDRESCR             ALL OTHER PFK'S
*
*
EDMORMEM DS    0H
*
         CLI   25(R3),C' '      COMMAND ENTERED
         BE    EDMORM12         NO, JUST BUMP DEFAULT
*
         CLI   00(R3),X'00'     COMMAND ENTERED
         BE    EDMORM12         NO, JUST BUMP DEFAULT
*
         CLI   25(R3),C'M'      SCROLL "MAX"
         BE    EDMORMAX         GO TO BOTTOM
*
         CLI   25(R3),C'0'      IF NOT NUMERIC
         BL    EDMORM12         JUST BUMP DEFAULT
*
         LA    R14,25(,R3)      SET START OF FIELD
         LA    R15,6            MAXIMUM LENGTH
         SR    R1,1             COUNTER
*
         CLI   0(R14),C'0'      IF NOT NUMERIC
         BL    *+16
         LA    R1,1(,R1)        BUMP COUNTER
         LA    R14,1(,R14)      BUMP POINTER
         BCT   R15,*-16
*
         LTR   R1,1             IF LENGTH IS ZERO
         BZ    EDMORM12         JUST BUMP DEFAULT
*
         BCTR  R1,0             SUBTRACT 1 FOR EXECUTED PACK
         EX    R1,NUMPACK       PACK IT IN EDDWORD
         CVB   R1,EDDWORD       PUT VALUE IN R1
         SR    R0,R0
         LA    R14,80
         MR    R0,R14
         A     R1,EDTCURR1
         ST    R1,EDTCURR1
         C     R1,EDTLAST1
         BL    EDTSHOW             RE-DISPLAY
         B     EDMORMAX           RE-DISPLAY
*
NUMPACK  PACK  EDDWORD(8),25(1,R3)
EDMORM12 DS    0H
*                   BUMP THE SCREEN BY 12 LINES
         LA    R1,960
         A     R1,EDTCURR1
         ST    R1,EDTCURR1
         C     R1,EDTLAST1
         BL    EDTSHOW             RE-DISPLAY
         B     EDMORMAX            RE-DISPLAY
*
EDMORMAX DS    0H
*                   BUMP THE SCREEN TO BOTTOM AND BACK UP 12
         LA    R1,960
         L     R0,EDTLAST1
         SR    R0,R1
         ST    R0,EDTCURR1
         B     EDTSHOW             RE-DISPLAY
*
EDPREMEM DS    0H
         CLI   25(R3),C' '      COMMAND ENTERED
         BE    EDPREM12         NO, JUST BUMP DEFAULT
*
         CLI   00(R3),X'00'     COMMAND ENTERED
         BE    EDPREM12         NO, JUST BUMP DEFAULT
*
         CLI   25(R3),C'M'      SCROLL "MAX"
         BE    EDPREMAX         GO TO TOP
*
         CLI   25(R3),C'0'      IF NOT NUMERIC
         BL    EDPREM12         JUST BUMP DEFAULT
*
         LA    R14,25(,R3)      SET START OF FIELD
         LA    R15,6            MAXIMUM LENGTH
         SR    R1,1             COUNTER
*
         CLI   0(R14),C'0'      IF NOT NUMERIC
         BL    *+16
         LA    R1,1(,R1)        BUMP COUNTER
         LA    R14,1(,R14)      BUMP POINTER
         BCT   R15,*-16
*
         LTR   R1,1             IF LENGTH IS ZERO
         BZ    EDPREM12         JUST BUMP DEFAULT
*
         BCTR  R1,0             SUBTRACT 1 FOR EXECUTED PACK
         EX    R1,NUMPACK       PACK IT IN EDDWORD
         CVB   R1,EDDWORD       PUT VALUE IN R1
         SR    R0,R0
         LA    R14,80
         MR    R0,R14
*
         LR    R0,R1            AMOUNT TO BACK UP
         L     R1,EDTCURR1
         SR    R1,R0
         ST    R1,EDTCURR1
         C     R1,EDTFIRST
         BH    EDTSHOW             RE-DISPLAY
         B     EDPREMAX           RE-DISPLAY
EDPREM12 DS    0H
*                   BACK UP THE SCREEN 12 LINES
         L     R1,EDTCURR1
         LA    R0,960
         SR    R1,R0
         ST    R1,EDTCURR1
         C     R1,EDTFIRST
         BH    EDTSHOW             RE-DISPLAY
EDPREMAX DS    0H
         MVC   EDTCURR1(4),EDTFIRST
         B     EDTSHOW             RE-DISPLAY
*
         TITLE 'ETPS - EDITOR    READ IN PAN DS'
EDTPAN   DS    0H
*
         B     EDTFREE1            ALL DONE
*
         TITLE 'ETPS - EDITOR    SUBMIT COMMAND'
EDTSUBMT DS    0H
*
* NOTE ALL THE DATASET SUBMIT LOGIC GOES HERE
*
         LA    R1,SUBSUBP
         DYNALLOC
*
         GENCB BLK=ACB,AM=VSAM,MACRF=(ADR,SEQ,OUT),                    X
               DDNAME=SUBJOB,MF=(G,ACBWORK,ACBSIZ)
*
         ST    R1,SUBACB
         LR    R2,R1                           * ACB ADDRESS
         USING IFGACB,2
*
         GENCB BLK=RPL,AM=VSAM,ACB=(2),OPTCD=(ADR,SEQ,SYN,NUP),        X
               RECLEN=80,MF=(G,RPLWORK,RPLSIZ)
*
         ST    R1,SUBRPL
         LR    R3,R1                           * RPL ADDRESS
         USING IFGRPL,3
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((2)),MF=(E,PARMOP)
*
         LTR   R15,15                          * GOOD OPEN?
         BNZ   WTOMS61        NOT OPEN
*
         L     R4,EDTFIRST    PICK UP STORAGE AREA
*
EDTSUB1  DS    0H
*
         C     R4,EDTLAST1
         BNL   EDTSUBCL
*
         CLI   72(R4),X'00'  DELETED LINE
         BE    EDTSUBB1
         CLI   72(R4),X'FF'  DELETED LINE
         BE    EDTSUBB1
*
         MODCB RPL=(3),AREA=(4),ACB=(2),MF=(G,MODWORK,MODSIZ)
*
         PUT   RPL=(3)
*
EDTSUBB1 DS    0H
         LA    R4,80(,4)
         B     EDTSUB1
*
EDTSUBCL DS    0H
        ENDREQ RPL=(3)
         MVC   SUBMSG1(20),SUBMSG   SET MESSAGE
         MVC   SUBMSG1(8),RPLRBAR   SET JOB NUMBER IN MESSAGE
         MVI   PARMOP,X'80'   SET VL BIT
         CLOSE ((2)),MF=(E,PARMOP)
         LA    R1,SUBMSG1
         ST    R1,MSGADD
         B     EDRESCR              RESHOW THE SCREEN
*
         DROP  2,3
SUBMSG   DC    CL20'         SUBMITTED'
*
         TITLE 'ETPS - EDITOR    SAVE PROCESSING'
         PRINT GEN
EDTSAVE1 DS    0H
*
* NOTE ALL THE DATASET SAVE LOGIC GOES HERE
*
         CLI   LIBFUNC,C'E'       INDICATE BROWSE
         BNE   EDTFREE1
*
*
* FIRST COUNT THE NUMBER OF RECORDS IN MY EDIT WORK AREA TO DETERMINE
* WHETHER I CAN DO AN UPDATE-IN-PLACE.
*
         SR    R1,1                 RECORD COUNTER
*
         L     R3,EDTFIRST    PICK UP STORAGE AREA
*
EDTALLY  DS    0H
*
         C     R3,EDTLAST1
         BNL   EDTCFREC
*
         CLI   72(R3),X'00'  DELETED LINE
         BE    EDTALLUP
         CLI   72(R3),X'FF'  DELETED LINE
         BE    EDTALLUP
*
         LA    R1,1(,1)      ADD 1 TO MY COUNT
*
EDTALLUP DS    0H
         LA    R3,80(,3)
         B     EDTALLY
EDTCFREC DS    0H
*
         C     R1,EDTRECS   COMPARE CURRENT RECS VERSUS ORIGINAL RECS
         BNE   EDTSUPD2     NO MATCH, CAN'T DO UPDATE-IN-PLACE
*
EDTSUPD1 DS    0H
         L     R2,EDTDCB            PICK UP DCB ADDRESS
         MVC   50(2,R2),=X'4850'    SET MACRF TO GL,PM
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((2),(UPDAT)),MF=(E,PARMOP)
*
         TM    48(R2),X'10'   TEST FOR GOOD OPEN
         BZ    WTOMS61        NOT OPEN
*
         L     R3,EDTFIRST    PICK UP STORAGE AREA
*
EDTPUT1  DS    0H
*
         C     R3,EDTLAST1
         BNL   EDTEOF2
*
         CLI   72(R3),X'00'  DELETED LINE
         BE    EDTPUTB1
         CLI   72(R3),X'FF'  DELETED LINE
         BE    EDTPUTB1
*
         GET   (R2)
         MVC   0(80,R1),0(R3)
         PUTX  (R2)
*
EDTPUTB1 DS    0H
         LA    R3,80(,3)
         B     EDTPUT1
*
*
EDTSUPD2 DS    0H
         ST    R1,EDTRECS       SET CURRENT RECS
         L     R2,EDTDCB            PICK UP DCB ADDRESS
         LM    R0,R1,40(R2)         SAVE DDNAME
         MVC   0(96,R2),PMVDCB      SET MACRF TO PM
         STM   R0,R1,40(R2)        RESET DDNAME
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((2),(OUTPUT)),MF=(E,PARMOP)
*
         TM    48(R2),X'10'   TEST FOR GOOD OPEN
         BZ    WTOMS61        NOT OPEN
*
         L     R3,EDTFIRST    PICK UP STORAGE AREA
*
EDTPUT2  DS    0H
*
         C     R3,EDTLAST1
         BNL   EDTEOF2
*
         CLI   72(R3),X'00'  DELETED LINE
         BE    EDTPUTB2
         CLI   72(R3),X'FF'  DELETED LINE
         BE    EDTPUTB2
*
         PUT   (R2),(R3)
*
EDTPUTB2 DS    0H
         LA    R3,80(,3)
         B     EDTPUT2
*
EDTEOF2  DS    0H
*
         MVI   PARMOP,128
         CLOSE ((2)),MF=(E,PARMOP)
*     FREEPOOL (2)
*
         MVI   MSGBUFF,C' '
         MVC   MSGBUFF+1(19),MSGBUFF
*
         CLI   EDTMEM,C' '         MEMBER PROVIDED
         BE    EDTMSG2             NOPE
*
         L     R2,LIBDCBA
         CALL  SPFSTOW,((2),EDTRECS,USERID,EDTMEM),VL,MF=(E,PARM43)
*
         MVC   MSGBUFF(8),EDTMEM        SET THE "SAVED" MESSAGE
         MVC   MSGBUFF+9(5),EDTSAVMM
         LA    R1,MSGBUFF
         ST    R1,MSGADD
         B     EDTCMDT
*
EDTSAVDS DC    CL8'DATASET '
EDTSAVMM DC    CL12'SAVED'
PMVDCB   DCB   DDNAME=DUMMY,MACRF=PM,DSORG=PS
GMVDCB   DCB   DDNAME=DUMMY,MACRF=GM,DSORG=PS
EDTMSG2  DS    0H
         LA    R1,EDTSAVDS
         ST    R1,MSGADD
*
EDTCMDT  DS    0H
         L     R3,TERMINPT+8       COMMAND LINE
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   *+18                     NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         BCTR  R3,0                MAKE RELATIVE 0?????
         SLL   R3,2                MULTIPLY BY 4
         L     R3,TERMINPT+8(R3)   PICK UP RESPONSES
*
         CLI   0(R3),X'00'         COMMAND ENTERED
         BNE   EDTFREE1            NO, JUST FREE UP AND EXIT
*
         CLC   25(6,R3),=C'SAVE  ' IS IT A SAVE COMMAND
         BE    EDRESCR             RESHOW THE SCREEN
*
         B     EDTFREE1            ALL DONE
*
EDTFREE1 DS    0H
         L     R6,EDTMAIN1              SAVE GETMAIN ADDRESS
         L     R7,EDTLENG1              SAVE GETMAIN LENGTH
         FREEMAIN R,LV=(R7),A=(R6)
EDTRETRN DS    0H
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R14,R12,12(R13)          RESTORE CALLERS REGS
         BR    R14                      NORMAL RETURN
WTOMS51  WTO   'ETPS1051 OPEN INPUT FAILED IN EDITOR  ',ROUTCDE=11
         LA    R1,WTOMS51+13
         ST    R1,MSGADD
         B     EDTFREE1            ALL DONE
WTOMS61  WTO   'ETPS1051 OPEN OUTPUT FAILED IN EDITOR ',ROUTCDE=11
         LA    R1,WTOMS61+13
         ST    R1,MSGADD
         B     EDTFREE1            ALL DONE
         LTORG
         PRINT NOGEN
         TITLE 'ETPS - EDIT FILE SCREEN FORMAT'
*
* THIS IS THE FORMAT FOR A PDS OR PAN LIBRARY MEMBER, OR A SEQ FILE.
*
EDTTITLE DS    0F
DSNROW1  DC    AL1(81)
         DC    X'1DE8'
         DC    CL8'   EDIT '
         DC    C'- '
DSNDSNAM EQU   *-EDTTITLE
         DC    69C'-'
DSNROW2  DC    AL1(85)
         DC    X'1DE8'
         DC    CL19'COMMAND INPUT ===> '
         DC    X'1D40'
DSNHOME  EQU   *-EDTTITLE
         DC    X'13'                         IC
         DC    CL40' '
         DC    X'1DE8'
         DC    CL13' SCROLL ===> '
         DC    X'1D40'
         DC    CL4'PAGE'
*
DSNLINE  DC    AL1(82)
         DC    X'1D40',C'      ',X'1D40',CL72' '
         TITLE 'ETPS - TERMINAL COMMUNICATIONS SUBROUTINE'
*
*     CONSOLE I/O SUBROUTINE.
*
         SPACE 5
******** COMMUNICATIONS SUBROUTINE - EXCP INTERFACE
COMM2    CSECT
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE2,13,10
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         LA    R11,72(,R13)       LEVEL 2 MODULE, SO BUMP TO SAVE2
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         L     R15,=A(COMM3)
         BALR  R14,R15
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R0,R12,20(R13)          RESTORE CALLERS REGS
         L     R14,12(R13)              PRESERVE R15
         BR    R14                      NORMAL RETURN
         LTORG
COMM3    CSECT
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE3,13,10
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         LA    R11,72(,R13)       LEVEL 3 MODULE, SO BUMP TO SAVE3
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         L     R15,=A(COMM4)
         BALR  R14,R15
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R0,R12,20(R13)          RESTORE CALLERS REGS
         L     R14,12(R13)              PRESERVE R15
         BR    R14                      NORMAL RETURN
         LTORG
COMM4    CSECT
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE4,13
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         LA    R11,72(,R13)       LEVEL 4 MODULE, SO BUMP TO SAVE4
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         LA    R3,1
         LA    R11,4095(R3,R12)   SET PROGRAM BASE REGISTER 2
         LA    R10,4095(R3,R13)   SET DSECT BASE 2
         LA    R10,SPLITWRK       SET DSECT FOR SPLIT-SCREEN
         USING SPLTAREA,10
*
RESCREEN DS    0H
*
         MVI   HELPFLAG,0         INDICATE "NOT DOING HELP"
         L     R9,SCROWS          PICK UP TERMINAL SCREEN SIZE
*
*
BLDBUFF  DS    0H
*
         SR    R5,5                     LENGTH COUNTER
*
         L     R7,BUFF                  ACTUAL WRITE BUFFER
         CLI   WCCFLAG,C'A'             ALARM REQUESTED
         MVI   WCCFLAG,C'N'             ALWAYS SET IT OFF
         MVC   0(1,R7),USUALWCC         NO ALARM
         BE    *+10                     ALARM REQUESTED
         MVC   0(1,R7),ALARMWCC         SET IT ON
         LA    R7,1(,7)                 BUMP ADDRESS
         LA    R5,1(,5)                 BUMP COUNTER
*
         L     R8,SBACODES              PICK UP LINE 1
*
         LA    R6,TERMOUT+4             PICK UP LINE 1
*
         CLI   HELPFLAG,0               AM I DOING HELP
         BE    BLDBUFFS                 NOPE
         L     R6,HELPADD               YUP
*
BLDBUFFS DS    0H
*
         MVC   0(1,R7),SBACODE          ADDRESS OF FROM FIELD
         MVC   1(2,R7),0(R8)            ADDRESS OF FROM FIELD
*
         L     R2,0(,R6)                ADDRESS OF FROM FIELD
*
         CLI   0(R2),0                  IS LENGTH ZERO?
         BNE   *+8                      NOPE
         MVI   0(R2),81                 SET IT TO 81 AS A DEFAULT
*
         SR    R1,1                     CLEAR REGISTER
         IC    R1,0(,R2)                PICK UP LENGTH
         BCTR  R1,0                     MINUS ONE FOR EXECUTED MOVE
         EX    R1,MOVINBUF              MOVE IT
*
         CLI   SPLIT,0                  AM I IN SPLIT-SCREEN
         BE    NOCURSR                  NOPE
*
         L     R14,SCROWS1
         SLL   R14,2                    MULTIPLY BY 4
         LA    R14,TERMOUT(R14)         PICK UP START OF SCREEN 2
*
         CLI   SPLIT,2                  IS THE CURSOR ON SCREEN 2
         BE    CURSOR2                  YUP
*
CURSOR1  DS    0H
*
* OK, THE CURSOR SHOULD BE ON SCREEN 1. IF I AM BUILDING THE LINES
* THAT WILL GO ON SCREEN 1, I DON'T NEED TO CHECK FOR THE CURSOR
* CHARACTER. IF I'M BUILDING SCREEN 2, I NEED TO DESTROY THE CURSOR
* CHARACTER.
*
         CR    R14,R6                   AM I BUILDING SCREEN 2?
         BL    KILLCURS                 YES, DON'T ALLOW CURSOR
         B     NOCURSR                  NO, DON'T KILL CURSOR
*
CURSOR2  DS    0H
*
         CR    R14,R6                   AM I BUILDING SCREEN 2?
         BL    NOCURSR                  YES, DON'T KILL CURSOR
         B     KILLCURS                 NO, DON'T ALLOW CURSOR
*
KILLCURS DS    0H
         LR    R14,R1                   SET R14 = LENGTH OF MOVE
         LA    R15,3(,R7)               SET R15 = START ADDRESS
         CLI   0(R15),X'13'             CURSOR CHARACTER?
         BE    *+16                     YES, KILL IT
         LA    R15,1(,15)               BUMP ADDRESS
         BCT   R14,*-12                 LOOP
         B     NOCURSR                  END OF LOOP
*
         MVI   0(R15),X'40'             KILL IT
*
NOCURSR  DS    0H
         SR    R14,14
         C     R14,MSGADD               IS THERE A MESSAGE
         BE    BLDBUMP2                 NO, GO BUMP
*
         CLI   SPLIT,2                  AM I IN SPLIT-SCREEN 2
         BE    CHKMSG2                  YES, SKIP THIS
         LA    R14,TERMOUT+4            SET LINE 1 ADDRESS
         CR    R6,R14                   AM I ON LINE1
         BE    BLDBUMP1                 YES I AM
         B     BLDBUMP2                 NO, GO BUMP
*
CHKMSG2  DS    0H
         L     R14,SCROWS1
         SLL   R14,2
         LA    R14,TERMOUT(14)          SET ADDR OF LINE1, SCRN2
         CR    R6,R14                   AM I ON LINE1
         BNE   BLDBUMP2                 NO, GO BUMP
*
BLDBUMP1 DS    0H
         LA    R14,60
         CR    R1,R14                   IF LINE LENGTH IS SHORT
         BL    BLDBUMP2                 GO BUMP
*
         LR    R15,R7
         LA    R14,20
         SR    R15,R14
         LA    R15,4(R1,15)
         L     R14,MSGADD               MESSAGE ADDRESS
         MVC   0(20,R15),0(R14)
         XC    MSGADD(4),MSGADD         CLEAR THE MESSAGE
         L     R14,BUFF                 BUFFER ADDRESS
         MVC   0(1,R14),ALARMWCC        SOUND ALARM
*
BLDBUMP2 DS    0H
         LA    R5,4(R1,5)               ADD TO LENGTH
         ST    R5,BUFFL1                MAKE SURE THE LENGTH IS SET
         LA    R7,4(R1,7)               ADD TO LOCATION FOR NEXT MOVE
         LA    R8,4(,8)                 BUMP TO NEXT SBACODES
*
         TM    0(R6),X'80'              END OF LINES
         BO    WRTBUFF                  LET 'ER RIP
*
         LA    R6,4(,6)                 BUMP TO NEXT PARM
         BCT   R9,BLDBUFFS              GO DO IT
*
         B     WRTBUFF                  LET 'ER RIP
*
MOVINADD MVC   0(1,R7),0(R2)
MOVINBUF MVC   3(1,R7),1(R2)
         EJECT
WRTBUFF  DS    0H
*
         L     R4,BUFF                  ACTUAL WRITE BUFFER
         L     R5,BUFFL1                PICK UP THE LENGTH
         MVI   RECB,X'80'               RESET ECB
WRITUBE1 DS    0H
*
         MVC   CCW1(8),SELECT0B
         MVC   CCW2(8),WRITECCW
         STCM  R5,3,CCW2+6              SET THE LENGTH
         STCM  R4,7,CCW2+1              SET THE DATA ADDRESS
*
         LA    R1,CCW1
         STCM  R1,7,TUBEIOB+17
         LA    R2,RECB           SET ECB ADDRESS
         ST    R2,TUBEIOB+4                       IN THE IOB
         EXCP  TUBEIOB
*
         WAIT  ECB=RECB
*
         CLI   RECB,X'44'               CHECK FOR INTERCEPT
         BE    RESCREEN                 IF YES,RE-WRITE SCREEN
         CLI   RECB,X'41'               CHECK FOR PERMANENT I/O ERR
         BE    PERMIOER                 TRY TO RECOVER
         CLI   RECB,X'7F'               CHECK FOR OTHERWISE OKAY
         BNE   WTOMS12                  IF NOT,DO ERROR RECOVERY
         MVI   PERMERR,0                FIRST TIME THROUGH
*
         L     R6,UCBADDR
         USING UCBBGN,6
         MODESET MF=(E,SUPRMOD)
         XC    UCBRDYQ(4),UCBRDYQ    CLEAR ECB
         WAIT ECB=UCBRDYQ
         MODESET MF=(E,PROBMOD)
*
TUBEREAD DS    0H
         L     R5,SCREENSZ              PICK UP BUFFER LENGTH
*
         L     R4,REPLY                 PICK UP BUFFER ADDRESS
         MVI   RECB,X'80'               RESET ECB
*
         MVC   CCW1(8),SELECT0B
         MVC   CCW2(8),READCCW
         STCM  R5,3,CCW2+6              SET THE LENGTH
         STCM  R4,7,CCW2+1              SET THE DATA ADDRESS
*
         LA    R1,CCW1
         STCM  R1,7,TUBEIOB+17
         LA    R2,RECB           SET ECB ADDRESS
         ST    R2,TUBEIOB+4                       IN THE IOB
*
         EXCP  TUBEIOB
*
         WAIT  ECB=RECB
*
*
*
         B     TESTREAD                 GO CHECK READ
*
*
SEC10    DC    F'1000' 10 SECOND WAIT
WRITECCW DC    X'0D',AL3(0),X'2000',AL2(0)
NOOP     DC    X'04',AL3(0),X'2000',AL2(1)
SELECT0B DC    X'0B',AL3(0),X'6000',AL2(1)
READCCW  DC    X'06',AL3(0),X'2000',AL2(0)
PERMIOER DS    0H
         CLI   PERMERR,0                FIRST TIME THROUGH?
         BNE   WTOMS41                  NOPE
         MVI   PERMERR,1                NOT FIRST TIME THROUGH.
         B     RESCREEN                 TRY TO RESHOW THE SCREEN
COPYMSG  MVC   0(1,R3),2(R2)            MOVE MSG INTO BUF-VAR INSTR
INFO     EQU   X'01'                    DO NO DO READ TI,NO REPLY
FIRST    EQU   X'80'                    FIRST READ/WRITE
*        EXAMINE READ RESULT AND SET POINTERS TO INPUT FIELDS
TESTREAD DS    0H
*
*
         LA    R14,TERMINPT+4           NOW BUILD INADDRESSES
         L     R1,SCROWS1          PICK UP SIZE OF SCREEN1
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   *+20                     NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         SLL   R3,2                MULTIPLY BY 4
         LA    R14,TERMINPT+4(R3)  PICK UP RESPONSES
         L     R1,SCROWS2          PICK UP SIZE OF SCREEN2
*
         L     R15,0(,R14)              STORE THE ADDRESS
         MVI   0(R15),X'00'             SET FIRST BYTE TO ZEROS
         LA    R14,4(,14)               BUMP TO NEXT INADDR
         BCT   R1,*-12                  LOOP 43 TIMES
*
         LA    R4,TERMINPT              GET A(BUFFER)
         L     R3,REPLY                 GET A(BUFFER)
         CLI   RECB,X'44'               CHECK IF READ INTERCEPTED
         BE    RESCREEN                 IF YES,ASSUME CLEAR WAS HIT
         CLI   RECB,X'41'               PERMANENT IO ERROR
         BE    PERMIOER                 -RECOVER IF NOT
         CLI   RECB,X'7F'               CHECK FOR OTHERWISE OKAY
         BNE   WTOMS14                  -RECOVER IF NOT
         MVI   PERMERR,0                FIRST TIME THROUGH
*
         CLI   HELPFLAG,0               AM I DOING HELP ALREADY
         MVI   HELPFLAG,0               SHUT IT OFF
         BNE   AFTRHELP                 GO REDISPLAY THE SCREEN
*
         MVI   PFKFLAG,0                NOT A PFK/ATTN
         CLI   0(R3),X'7D'              IS IT ENTER
         BE    *+8                      YES
         MVI   PFKFLAG,1                IF NOT ENTER, MUST BE PFK
*
         L     R5,SCREENSZ              START EXAMINING BUFFER FOR DATA
         SH    R5,TUBEIOB+14            (RESIDUAL COUNT)
         LA    R9,1(R5,R3)              LAST BYTE OF INPUT READ
         CLC   =X'016C6102',0(R3)       CHECK FOR TEST-REQ
         BE    RESCREEN                 RESET SCREEN IF YES
SREAD    EQU   *
         L     R14,0(,R4)               PICK UP ADDR FOR AID
         MVC   0(1,R14),0(R3)           MOVE IN AID
         MVC   SBAWORK(2),1(R3)         MOVE IN AID ADDRESS
*
         BAL   R14,SBAXLATE             TRANSLATE TO ROW AND COLUMN
         MVC   AIDADDR(2),SBAWORK       SAVE CURSOR POSITION
         L     R14,0(,R4)               PICK UP ADDR FOR AID
         MVC   1(2,R14),SBAWORK         MOVE CURSOR POSITION
*
TFORHELP DS    0H
*
         CLI   0(R3),X'F1'              IS IT HELP PFKEY
         BE    *+12                     YUP
         CLI   0(R3),X'C1'              IS IT HELP PFKEY
         BNE   TSPLIT                   NO, SEE IF IT'S SPLIT-SCREEN
*
         MVI   HELPFLAG,1               TURN IT ON
         B     BLDBUFF                  DISPLAY HELP
*
TSPLIT   DS    0H
*
         CLI   0(R3),X'F2'              IS IT SPLIT PFKEY
         BE    *+12                     YUP
         CLI   0(R3),X'C2'              IS IT SPLIT PFKEY
         BNE   TSWITCH                  OTHERWISE, TEST FOR SWITCH
*
         CLI   SPLIT,0                  SPLIT-SCREEN ALREADY?
         BE    *+16                     NO, SPLIT IT
         L     R2,TERMINPT              PICK UP RESPONSE ADDRESS
         MVI   0(R2),X'6E'              RESET IT TO PA2
         B     TRETURN                  JUST EXECUTE PA2
*
         L     R1,SCROWS                SET IT
         SRL   R1,1                     DIVIDE BY 2
         ST    R1,SCROWS1               STORE IT
         L     R0,SCROWS                PICK IT UP
         SR    R0,R1                    SUBTRACT
         ST    R0,SCROWS2               STORE THE REMAINDER
         TM    SCROWS+3,1               EVEN NUMBER OF ROWS?
         BZ    *+12                     YES
         A     R0,=F'1'                 BUMP 1
         ST    R0,SCROWS2               STORE IT
*
*
         MVI   SPLIT,2                  INDICATE SCREEN 2
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         LA    R0,SPLIT1                ADDRESS OF " TO " AREA
         MVCL  R0,R14
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         LA    R0,SPLIT2                ADDRESS OF "FROM" AREA
         MVCL  R14,R0
*
         L     R14,R14ADDR              PICK UP RETURN ADDRESS
         LM    R2,R13,PRISPLIT
         BR    R14                      SHOW PRIMARY OPTION MENU
*
R14ADDR  DC    A(REPRIOPT)
SPLTMOVL DC    A(AREALEN)
*                              OF BUFFERS
TSWITCH  DS    0H
*
         CLI   0(R3),X'F9'              IS IT SWITCH SCREEN
         BE    *+12                     YUP
         CLI   0(R3),X'C9'              IS IT SWITCH SCREEN
         BNE   TENDPFK                  OTHERWISE, TEST FOR END PFK
*
         L     R2,TERMINPT              PICK UP RESPONSE ADDRESS
         MVI   0(R2),X'6E'              RESET IT TO PA2
*
         CLI   SPLIT,0                  IN SPLIT-SCREEN MODE?
         BE    TRETURN                  NO, JUST EXECUTE PA2
*
         CLI   SPLIT,1                  AM I ON TOP SCREEN
         BNE   SWBOTTOP                 NOPE, GO FROM BOTTOM TO TOP
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         LA    R0,SPLIT1                ADDRESS OF " TO " AREA
         MVCL  R0,R14
*
         MVI   SPLIT,2                  INDICATE SCREEN 2
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         LA    R0,SPLIT2                ADDRESS OF " TO " AREA
         MVCL  R14,R0
*
         B     SETASWCH                 SET UP FOR SWITCH
*
SWBOTTOP DS    0H
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         LA    R0,SPLIT2                ADDRESS OF " TO " AREA
         MVCL  R0,R14
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         LA    R0,SPLIT1                ADDRESS OF " TO " AREA
         MVCL  R14,R0
*
         MVI   SPLIT,1                  TURN IT ON
*
SETASWCH DS    0H
         B     RESCREEN
*
TENDPFK  DS    0H
*
         CLI   0(R3),X'F3'              IS IT END PFK
         BE    *+12                     YUP
         CLI   0(R3),X'C3'              IS IT END PFK
         BNE   LREAD                    OTHERWISE, GO PROCESS
*
         CLI   SPLIT,0                  SPLIT-SCREEN?
         BE    TRETURN                  NOPE, JUST GO PROCESS
*
         CLI   PRIMEFLG,0          PRIMARY OPTION MENU ON SCREEN 1?
         BE    TRETURN             NO, JUST RETURN
*
         CLI   SPLIT,2                  AM I ON BOTTOM SCREEN
         BE    SWBOTEOF                 YES, GO END THE BOTTOM SCREEN
*
         L     R2,TERMINPT              PICK UP RESPONSE ADDRESS
         MVI   0(R2),X'6E'              RESET IT TO PA2
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         LA    R0,SPLIT2                ADDRESS OF " TO " AREA
         MVCL  R14,R0
*
         MVC   SCROWS1(4),SCROWS        RESET NUMBER OF ROWS
         XC    SCROWS2(4),SCROWS2       CLEAR THIS
*
         MVI   SPLIT,0                  INDICATE NOT DOING SPLIT-SCREEN
         B     TRETURN                  OTHERWISE, GO PROCESS
*
SWBOTEOF DS    0H
*
         L     R2,TERMINPT              PICK UP RESPONSE ADDRESS
         MVI   0(R2),X'6E'              RESET IT TO PA2
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         LA    R0,SPLIT1                ADDRESS OF " TO " AREA
         MVCL  R14,R0
*
         MVC   SCROWS1(4),SCROWS        RESET NUMBER OF ROWS
         XC    SCROWS2(4),SCROWS2       CLEAR THIS
*
         MVI   SPLIT,0                  INDICATE NOT IN SPLIT-SCREEN
         B     TRETURN                  GO PROCESS PA2
*
SETAEOF  DS    0H
         B     RESCREEN
*
AFTRHELP DS    0H
         B     RESCREEN
         EJECT
LREAD    DS    0H
         C     R5,=F'3'                RETURN IF ENTER WITH NO DATA
         BL    TRETURN
*
         LA    R4,4(,R4)                BUMP TO NEXT ADDRESS
         LA    R3,3(,R3)                POINT PAST AID AND CURSOR
         CLI   0(R3),X'11'              LOOK FOR SBA
         BNE   WTOMS15                  IF NOT, ITS AN ERROR
         LR    R8,R3                    START OF DATA
         SR    R6,6                     LENGTH OF DATA
         B     GOTSBA2                  AFTER START OF SBA LOOP
GOTSBA1  DS    0H
         BCTR  R6,0
         EX    R6,REPLYCAP              UPPER-CASE THE INPUT
         EX    R6,REPLYMVC              MOVE IT INTO THE LINE
         SR    R6,6                     LENGTH OF DATA
         LR    R8,R3                    START OF DATA
GOTSBA2  DS    0H
*
* NOW I HAVE TO PICK UP THE SBA AND LOCATE THE CORRESPONDING
* OUTPUT LINE.
*
* FIRST TRANSLATE SBA CODES TO ROW AND COLUMN
*
         CLI   0(R8),X'11'              DO I HAVE AN SBA
         BE    *+6
         DC    H'0'                     NOPE, SOC1
         MVC   SBAWORK(2),1(R8)         SET ADDR OF SBA
         LA    R1,SBAWORK               SET ADDR OF SBA
         BAL   R14,SBAXLATE
*
* NOW MOVE THE IMAGE OF THE ROW THAT WAS TRANSMITTED TO THE TERMINAL
* INTO MY INPUT BUFFER SO THAT I CAN SIMULATE A FULL-LINE READ.
*
         LA    R14,TERMOUT+4            LINE 1 THAT WAS DISPLAYED
         SR    R15,15                   CLEAR REGISTER
         IC    R15,SBAROW               PICK UP LINE NUMBER (REL 0)
         SLL   R15,2                    MULTIPLY BY 4
         L     R2,0(R15,R14)            POINT TO ACTUAL OUTPUT LINE
         LA    R14,TERMINPT+4           POINT TO INPUT LINE SLOT
         L     R7,0(R15,R14)            POINT TO ACTUAL INPUT LINE
         LA    R4,0(R15,R14)            SET ADDRESS POINTER
*
         CLI   0(R7),X'00'              IS THIS A BLANK LINE
         BNE   *+14                     NO, SECOND FIELD ON THIS LINE
*
         SR    R14,14                   CLEAR REGISTER
         IC    R14,0(,R2)               PICK UP LENGTH
         EX    R14,MOVINADD             MOVE LINE IMAGE TO INPT BUFF
*
         SR    R14,14                   CLEAR REG
         IC    R14,SBACOL               PICK UP COLUMN (REL 0)
         LA    R7,1(R7,R14)             POINT TO ACTUAL INPUT LOCATION
*
         CLI   0(R7),X'13'              CURSOR?
         BE    *+20
         CLI   0(R7),X'1D'              ATTRIBUTE BYTES?
         BE    *+8
         B     *+16
         LA    R7,1(,7)                 BUMP 2
         LA    R7,1(,7)                 BUMP 1
         B     *-28                     LOOP
*
*
* NOW I HAVE TO FIND THE LENGTH OF INPUT THAT WAS ENTERED AT THE
* TERMINAL SO I CAN MOVE IT INTO THE LINE IMAGE
*
         LA    R3,1(,3)                 BUMP ADDRESS
SBABUMP1 DS    0H
         CLI   0(R3),X'11'              LOOK FOR NEXT SBA
         BE    GOTSBA1                  GOT IT
         LA    R3,1(,3)                 BUMP ADDRESS
         LA    R6,1(,6)                 BUMP LENGTH
         CR    R3,R9                    COMPARE VS LAST BYTE READ IN
         BL    SBABUMP1                 KEEP LOOPING
GOTSBAS  DS    0H
         BCTR  R6,0
         EX    R6,REPLYCAP              UPPER-CASE THE INPUT
         EX    R6,REPLYMVC              MOVE IT INTO THE LINE
TRETURN  DS    0H
         SR    R15,15                   SET RC=0
TRETURN2 DS    0H
         OI    0(R4),X'80'              FLAG AS LAST ENTRY
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R0,R12,20(R13)          RESTORE CALLERS REGS
         L     R14,12(R13)              PRESERVE R15
         BR    R14                      NORMAL RETURN
         TITLE 'SBA TRANSLATE TO ROW AND COLUMN'
SBAXLATE DS    0H
         STM   R14,R1,12(R13)           SAVE SBAS WORK REGISTERS
         LA    R15,SBATABLE
         SR    R14,14  CLEAR COUNTER
*
SBALOOP1 DS    0H
         CLC   0(1,R15),SBAROW                FIRST SBA BYTE
         BE    SBAROWOK
         LA    R15,1(,15)  BUMP POINTER
         LA    R14,1(,14)  BUMP COUNTER
         B     SBALOOP1    LOOP
*
SBAROWOK DS    0H
         SLL   R14,6       MULTIPLY BY 64
         LR    R0,R14      SAVE IN R0
*
         SR    R14,14  CLEAR COUNTER
*
         LA    R15,SBATABLE
*
SBALOOP2 DS    0H
         CLC   0(1,R15),SBACOL             SECOND SBA BYTE
         BE    SBACOLOK
         LA    R15,1(,15)  BUMP POINTER
         LA    R14,1(,14)  BUMP COUNTER
         B     SBALOOP2    LOOP
*
SBACOLOK DS    0H
         AR    R0,R14      ADD TO PREVIOUS VALUE
         LR    R15,R0      PUT IN R15 FOR DIVIDE
         SR    R14,14      CLEAR FOR DIVIDE
         LA    R0,80       DIVISOR
         DR    R14,R0      DIVIDE
*
         STC   R14,SBACOL  REMAINDER IS COLUMN
         STC   R15,SBAROW  QUOTIENT IS ROW
*
         LM    R14,R1,12(R13)         RESTOR SBAS WORK REGISTERS
         BR    R14
*
SBATABLE DC    X'40'
         DC    X'C1'
         DC    X'C2'
         DC    X'C3'
         DC    X'C4'
         DC    X'C5'
         DC    X'C6'
         DC    X'C7'
         DC    X'C8'
         DC    X'C9'
         DC    X'4A'
         DC    X'4B'
         DC    X'4C'
         DC    X'4D'
         DC    X'4E'
         DC    X'4F'
         DC    X'50'
         DC    X'D1'
         DC    X'D2'
         DC    X'D3'
         DC    X'D4'
         DC    X'D5'
         DC    X'D6'
         DC    X'D7'
         DC    X'D8'
         DC    X'D9'
         DC    X'5A'
         DC    X'5B'
         DC    X'5C'
         DC    X'5D'
         DC    X'5E'
         DC    X'5F'
         DC    X'60'
         DC    X'61'
         DC    X'E2'
         DC    X'E3'
         DC    X'E4'
         DC    X'E5'
         DC    X'E6'
         DC    X'E7'
         DC    X'E8'
         DC    X'E9'
         DC    X'6A'
         DC    X'6B'
         DC    X'6C'
         DC    X'6D'
         DC    X'6E'
         DC    X'6F'
         DC    X'F0'
         DC    X'F1'
         DC    X'F2'
         DC    X'F3'
         DC    X'F4'
         DC    X'F5'
         DC    X'F6'
         DC    X'F7'
         DC    X'F8'
         DC    X'F9'
         DC    X'7A'
         DC    X'7B'
         DC    X'7C'
         DC    X'7D'
         DC    X'7E'
         DC    X'7F'
*
         TITLE 'ETPS - CAPS ONLY TRT TABLE'
CAPSONLY DC    CL16' '
         DC    X'1111'         SBA
         DC    CL46' '
         DC    CL10' '
         DC    C'.<(+&&'
         DC    CL9' '
         DC    C'!$*)'
         DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
         DC    C'^-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
REPLYCAP TR    3(1,R8),CAPSONLY
REPLYMVC MVC   0(1,R7),3(R8)
WTOMS11  WTO   'ETPS1011 COMM   BAD WRITE TO TUBE      ',ROUTCDE=11
         LA    R15,11
         B     TRETURN2
WTOMS12  WTO   'ETPS1012 COMM   BAD ECB CODE ON WRITE  ',ROUTCDE=11
         LA    R15,12
         B     TRETURN2
WTOMS13  WTO   'ETPS1013 COMM   BAD READ FROM TUBE     ',ROUTCDE=11
         LA    R15,13
         B     TRETURN2
WTOMS14  WTO   'ETPS1014 COMM   BAD ECB CODE ON READ   ',ROUTCDE=11
         LA    R15,14
         B     TRETURN2
WTOMS15  WTO   'ETPS1015 COMM   INVALID SBA CODE       ',ROUTCDE=11
         DC    H'0'
         LA    R15,15
         B     TRETURN2
WTOMS41  WTO   'ETPS1041 COMM   PERMANENT I/O ERROR    ',ROUTCDE=11
         DC    H'0'
         B     TRETURN2
         SPACE 2
         LTORG
         TITLE 'ETPS - PAN LIBRARY NAMES'
*
*
PANAMES  CSECT
         DC    CL45'NETPS1.PANLIB'
         DC    CL45'CPROD.JCL'
         DC    CL45'CP.SOURCE'
         DC    CL45'CCICS.SOURCE'
         DC    CL45'NT.SOURCE'
         DC    CL45'NP.SPOOL'
         DC    45X'FF'
         TITLE 'ETPS - BUFFER ADDRESSES'
*
* THE SET BUFFER ADDRESS (SBA) ORDER CODE IS FOLLOWED BY A 2-BYTE
* BUFFER ADDRESS THAT CORRESPONDS (BUT NOT IN ANY RATIONAL WAY) TO
* THE ROWS AND COLUMNS ON THE DISPLAY TUBE. THIS TABLE IS PROVIDED
* TO SUPPORT DYNAMICALLY BUILDING THE BUFFER ADDRESS FIELDS WHEN THE
* ROW AND COLUMN IS KNOWN.
*
* EACH BUFFER ADDRESS ENTRY CORRESPONDS TO A ROW ON THE DISPLAY
* THE FIRST BYTE IS THE SAME FOR ALL ROWS, THE SECOND, THIRD AND
* FOURTH BYTES DEFINE COLUMNS 1, 7, AND 8, RESPECTIVELY.
*
* TO PICK UP THE BUFFER ADDRESS YOU WANT, MULTIPLY THE ROW NUMBER
* BY THE LENGTH OF EACH BUFFER ADDRESS ENTRY AND ADD THE START ADDRESS
* OF THE TABLE.
*
* FOR EXAMPLE, THE ROW3 ENTRY IS =>  X'C1',X'50',X'D6',X'D7'
* THE BUFFER ADDRESS CODES ARE:
*
* ROW 3, COL 1 = X'C150'
* ROW 3, COL 7 = X'C1D6'
* ROW 3, COL 8 = X'C1D7'
*
BUFF3278 CSECT
ROW781   DC    X'40',X'40',X'C6',X'C7'
BALENGTH EQU   *-ROW781
ROW782   DC    X'C1',X'50',X'D6',X'D7'
ROW783   DC    X'C2',X'60',X'E6',X'E7'
ROW784   DC    X'C3',X'F0',X'F6',X'F7'
ROW785   DC    X'C5',X'40',X'C6',X'C7'
ROW786   DC    X'C6',X'50',X'D6',X'D7'
ROW787   DC    X'C7',X'60',X'E6',X'E7'
ROW788   DC    X'C8',X'F0',X'F6',X'F7'
ROW789   DC    X'4A',X'40',X'C6',X'C7'
ROW7810  DC    X'4B',X'50',X'D6',X'D7'
ROW7811  DC    X'4C',X'60',X'E6',X'E7'
ROW7812  DC    X'4D',X'F0',X'F6',X'F7'
ROW7813  DC    X'4F',X'40',X'C6',X'C7'
ROW7814  DC    X'50',X'50',X'D6',X'D7'
ROW7815  DC    X'D1',X'60',X'E6',X'E7'
ROW7816  DC    X'D2',X'F0',X'F6',X'F7'
ROW7817  DC    X'D4',X'40',X'C6',X'C7'
ROW7818  DC    X'D5',X'50',X'D6',X'D7'
ROW7819  DC    X'D6',X'60',X'E6',X'E7'
ROW7820  DC    X'D7',X'F0',X'F6',X'F7'
ROW7821  DC    X'D9',X'40',X'C6',X'C7'
ROW7822  DC    X'5A',X'50',X'D6',X'D7'
ROW7823  DC    X'5B',X'60',X'E6',X'E7'
ROW7824  DC    X'5C',X'F0',X'F6',X'F7'
ROW7825  DC    X'5E',X'40',X'C6',X'C7'
ROW7826  DC    X'5F',X'50',X'D6',X'D7'
ROW7827  DC    X'60',X'60',X'E6',X'E7'
ROW7828  DC    X'61',X'F0',X'F6',X'F7'
ROW7829  DC    X'E3',X'40',X'C6',X'C7'
ROW7830  DC    X'E4',X'50',X'D6',X'D7'
ROW7831  DC    X'E5',X'60',X'E6',X'E7'
ROW7832  DC    X'E6',X'F0',X'F6',X'F7'
ROW7833  DC    X'E8',X'40',X'C6',X'C7'
ROW7834  DC    X'E9',X'50',X'D6',X'D7'
ROW7835  DC    X'6A',X'60',X'E6',X'E7'
ROW7836  DC    X'6B',X'F0',X'F6',X'F7'
ROW7837  DC    X'6D',X'40',X'C6',X'C7'
ROW7838  DC    X'6E',X'50',X'D6',X'D7'
ROW7839  DC    X'6F',X'60',X'E6',X'E7'
ROW7840  DC    X'F0',X'F0',X'F6',X'F7'
ROW7841  DC    X'F2',X'40',X'C6',X'C7'
ROW7842  DC    X'F3',X'50',X'D6',X'D7'
ROW7843  DC    X'F4',X'60',X'E6',X'E7'
BTABLEN4 EQU   *-ROW781
         TITLE 'ETPS - SAVE AREA DSECT'
*
SPLTAREA DSECT
EDMEMADD DS    F
EDSELMEM DS    CL8
LASTMEM  DS    CL8
LOCNAME  DS    CL8
FIRSTMEM DS    CL8
*
DIRPOINT DS    F
DIRNOTE  DS    F
DIRTOP   DS    F
EDTDSN1  DS    CL54   LAST EDIT DSNAME IN SCREEN 1
EDTDSN1L DS    XL1    LAST EDIT DSNAME IN SCREEN 1 LENGTH
PRIMEFLG DS    XL1 1 = DOING PRIMARY OPTION MENU
EDTMEM1  DS    CL8    LAST EDIT MEMBER IN SCREEN1
EDTPAS1  DS    CL8    LAST PASSWORD IN SCREEN1
EDTVOL1  DS    CL6    LAST VOLSER IN SCREEN1
EDTORG1  DS    CL3    LAST DSORG IN SCREEN1
LIBORG   DS    CL3    DSORG FOR ETPSLIB2
EDTEOF   DS    CL1    EOF FLAG FOR EDITOR
EDTDSN   DS    CL44
EDTMEM   DS    CL8
EDTORG   DS    CL3
EDTRECS  DS    F
EDNOWLN1 DS    F
EDTFIRST DS    F
EDTLAST1 DS    F
EDTCURR1 DS    F
EDTMAIN1 DS    F
EDTLENG1 DS    F
EDTRPTA1 DS    F
*
EDDWORD  DS    D
*
EDFINDS  DS    XL40
EDFINDL  DS    X
*
EDTDCB   DS    F      EDITOR WORK AREAS
*
* PARMS FOR ETPSLIB2
DIRDD    DS    CL8
DIRDSN1  DS    CL54
DIRVOL   DS    CL6
DIRPASS  DS    CL8
LIBFUNC  DS    CL1   FUNCTION CODE = EDIT, BROW, UTIL
DIREOFLG DS    XL1 INDICATES I HAVE HIT EOF ON A LIBRARY DIRECTORY
DIRBUFF  DS    F
DIRNUM   DS    F
EDAFTSV1 DS    CL80 COPY LINE SAVE AREA
*
PDSKEY   DS    CL8,XL256
*
LIBDCBA  DS    F
MEMDCBA  DS    F
PRTDCBA  DS    F
COPDCBA  DS    F
*
SPLTLEN  EQU   *-SPLTAREA
*
MYSAVE   DSECT
SAVE1    DS    18F
SAVE2    DS    18F
SAVE3    DS    18F
SAVE4    DS    18F
SPLITWRK DS    XL(SPLTLEN)
AREALEN  EQU   *-MYSAVE
PRISPLIT DS    16F  REGS 0-15 TO RETURN TO PRIM TO BEGIN SPLIT-SCR
PARM43   DS    43F
FREESIZE DS    F    SIZE OF GETMAINED AREA
SAVETOP  DS    F    ADDR OF GETMAINED AREA
TERMOUT  DS    44F
TERMINPT DS    44F
*
DYNWORK1 DS    F
DYNWORK2 DS    F
DYNRCODE DS    2F
*
ROWFILL  DS    XL8
*
         DS    0F
TUBE     DCB   DSORG=CX,MACRF=(R,W),EROPT=E,READYQ=0,DDNAME=TUBE
         DS    0F
TUBEIOB  DS    XL40
RECB     DS    XL4
CCW1     DS    D
CCW2     DS    D
TUBEID   DS    CL4  C'ETPS'
TERMTYPE DS    CL1          2=3278M2, 3=3279M3, 4=3278M4, 5=3278M5
TERMADDR DS    CL3  UNIT ADDRESS
USERID   DS    CL8
USERPASS DS    CL8
RACGROUP DS    CL8         RACF GROUP ID
*
SUBACB   DS    F
*
SUBRPL   DS    F
*
SUBSUBP  DS    F
*
SUBRB    DS    X               LENGTH = 20
         DS    X               DSNAME ALLOCATION
         DS    X               FLAGS1 = ALLOW OFFLINE UNITS
         DS    X               REST OF FLAGS1
         DS    F               RETURN CODES
         DS    F               POINTER TO SUBTEXT1
         DS    F               RESERVED
         DS    F               FLAGS2
*
ACBWORK  DS    XL(ACBSIZ)      WORK AREA FOR GENCB ACB
RPLWORK  DS    XL(RPLSIZ)      WORK AREA FOR GENCB RPL
MODWORK  DS    XL(MODSIZ)      WORK AREA FOR MODCB RPL
*
SUBMSG1  DS    CL20            SUBMIT MESSAGE
*
BUFF     DS    F               ADDRESS OF OUTPUT BUFFER
REPLY    DS    F               ADDRESS OF INPUT BUFFER
LINES    DS    F               ADDRESS OF BLDLINES
*
UCBADDR  DS    F
DEBADDR  DS    F
SUPRMOD  DS    F
PROBMOD  DS    F
PACK8    DS    D
SBACODES DS    F
HELPADD  DS    F  ADDRESS OF HELP SCREEN
INIT14   DS    F  SAVE AREA FOR INITIALIZATION
LOG14    DS    F  SAVE AREA FOR LOGON
*
MSGADD   DS    F
MSGBUFF  DS    CL20
*
SBAWORK  DS    0XL2
SBAROW   DS    XL1
SBACOL   DS    XL1
*
AIDADDR  DS    0XL2      SAVE CURSOR POSITION
AIDROW   DS    XL1
AIDCOL   DS    XL1
*
PERMERR  DS    XL1 PROCESSING PERM IO ERR IF NE 0
PFKFLAG  DS    XL1 RECEIVED PFK/ATTN ON READ IF NE 0
SPLIT    DS    XL1 1 = TOP, 2 = BOTTOM, 0 = NOT IN SPLIT-SCREEN MODE
*
HELPFLAG DS    CL1    HELP FLAG FOR COMM
SBACODE  DS    XL1
ALARMWCC DS    XL1     WCC WHEN ALARM IS DESIRED
USUALWCC DS    XL1     WCC WHEN ALARM IS NOT DESIRED
WCCFLAG  DS    CL1
*                        SPLIT-SCREEN STUFF
BUFFL1   DS    F                        LENGTH OF OUTPUT BUFFER1
SCREENSZ DS    F                        SIZE OF SCREEN
SCROWS   DS    F                        NUMBER OF ROWS ON SCREEN
SCROWS1  DS    F                        NUMBER OF ROWS ON SCREEN 1
SCROWS2  DS    F                        NUMBER OF ROWS ON SCREEN 2
PARMOP   DS    F
SEQRNUM  DS    F
*
DYNPARM  DS    F
*
DYNTEXT1 DS    F               POINTER TO DYNDDNAM
DYNTEXT2 DS    F               POINTER TO DYNDISP
DYNTEXT3 DS    F               POINTER TO DYNUNIT
*
DYNRB    DS    X 14            LENGTH = 20
VERBCODE DS    X 01            DSNAME ALLOCATION
         DS    X 00            FLAGS1 = ALLOW OFFLINE UNITS
         DS    X 00            REST OF FLAGS1
         DS    F 0             RETURN CODES
TEXTPTRS DS    F 0             POINTER TO DYNTEXT1
         DS    F 0             RESERVED
         DS    F 0             FLAGS2
*
DYNDDNAM DS    H 0001          DDNAME=  PARAMETER
         DS    H 0001          NUMBER OF TEXT UNITS
         DS    H 0008          LENGTH OF PARM
TUBEDDNM DS    CL8 TUBE        DDNAME
*
DYNDISP  DS    H 0004          DISP=    PARAMETER
         DS    H 0001          NUMBER OF TEXT UNITS
         DS    H 0001          LENGTH OF PARM
         DS    X 01            OLD
*
DYNUNIT  DS    H 0015          UNIT= PARAMETER
         DS    H 0001          NUMBER OF TEXT UNITS
         DS    H 0003          LENGTH OF PARM
UNITADDR DS    CL3 45B         UNIT ADDRESS
*
DYNLENG  EQU   *-DYNRB         LENGTH FOR MOVE
*
CAT      DS    4F
CATINFO  DS    CL256
VTOC     DS    4F
VTOCINFO DS    CL140
*
         READ       DECB,SF,PDSDIR,PDSKEY,'S',MF=L
*
SPLIT1   DS    XL(AREALEN)    SAVE AREAS 1-4 FOR SCREEN 1
SPLIT2   DS    XL(AREALEN)    SAVE AREAS 1-4 FOR SCREEN 2
*
LIBDCB1  DS    CL96
MEMDCB1  DS    CL96
PRTDCB1  DS    CL96
COPDCB1  DS    CL96
*
LIBDCB2  DS    CL96
MEMDCB2  DS    CL96
PRTDCB2  DS    CL96
COPDCB2  DS    CL96
*
         DS    0D
*
SCREENIO DS    XL4095   THIS IS THE PHYSICAL I/O AREA BUFFER, BUILT
*                       BY COMM FROM THE TERMOUT AREAS, AND USED IN
*                       THE "WRITE" CCW.
*
REPLYARE DS    XL4095   THIS IS THE PHYSICAL INPUT AREA USED BY COMM
*                       IN THE "READ" CCW.
*
INPUTBUF DS    XL4095   THIS IS THE INTERMEDIATE AREA, BUILT BY COMM
*                       TO REFLECT THE RESULTS OF THE READ IN PSEUDO
*                       FULL-SCREEN FORMAT.
*
BLDLINES DS    XL4095   THIS IS THE INTERMEDIATE AREA, CONSTRUCTED
*                       BY COMM FROM CALLING ROUTINE'S PARAMETER LIST.
*
DYNWORKS DS    XL2048   THIS IS "DYNAM" WORK AREA
*
*
         DS    0F
GMSIZE   EQU   *-MYSAVE
         TITLE 'ETPS - DIRECTORY SCREEN FORMAT'
*
DIRMASK  DSECT
         DS    XL1         LENGTH CODE
         DS    XL2         UNPROTECTED
         DS    XL1         SELCODE
         DS    XL2         PROTECTED
DIRNAME  DS    CL12        MEMBER NAME
         DS    XL2         UNPROTECTED
         DS    CL10        NEW NAME FOR COPY OR RENAME
         DS    XL2         PROTECTED
         DS    0CL56       DIRECTORY INFO
DIRLEVEL DS    CL6
DIRCREAT DS    CL10
DIRMOD   DS    CL16
DIRLINES DS    CL6
DIRUSER  DS    CL8
         DS    CL10        GAS
*
*
*    PDS DIRECTORY ENTRY
*
         IHAPDS PDSBLDL=NO
*
*    SPF USER DATA
*
SPFMT  DSECT
SPFVER   DS    XL1
SPFMOD   DS    XL1
         DS    XL2
         DS    XL1
SPFCRDT  DS    PL3
         DS    XL1
SPFMODT  DS    PL3
SPFMODTM DS    XL2
SPFSIZE  DS    XL2
SPFSIZEI DS    XL2
SPFMOD2  DS    XL2
SPFUID   DS    CL7
         DS    CL3
         TITLE 'SUBMIT - ACB AND RPL DSECTS'
         IFGACB
         IFGRPL
         TITLE 'DYNAM - DYNAMIC ALLOCATION INTERFACE'
DYNAM    CSECT
         USING *,R12
         SAVE  (14,12),,*
         LR    R12,R15
         L     R11,0(0,R1)         PICK UP PARAMETER LIST ADDRESS.
         USING PARMLIST,R11
         L     R0,WORKLEN
         LA    R1,SAVEAREA
         LR    R2,R13
         LR    R13,1
         USING SAVEAREA,R13
         LR    R14,R1              SET UP FOR CLEAR OF WORK AREA.
         L     R15,WORKLEN
         SR    R0,R0
         SR    R1,R1
         MVCL  R14,R0              CLEAR WORK AREA TO ALL X'00'
         ST    R2,SAVEAREA+4
         ST    R13,8(0,R2)
         LA    R10,DYNWORK
         USING S99RB,R10
         ST    R10,REQBKPTR        POINT TO REQUEST BLOCK.
         OI    REQBKPTR,X'80'
         LA    R1,S99RBEND-S99RB   LENGTH OF REQUEST BLOCK.
         STC   R1,S99RBLN
         CLC   DSNAME,NULLS        CHECK FOR UNALLOCATION REQUEST.
         BNE   *+12
         MVI   S99VERB,S99VRBUN    REQUEST UNALLOCATION.
         B     *+12
         MVI   S99VERB,S99VRBAL    REQUEST ALLOCATION.
         OI    S99FLG11,S99NOCNV+S99NOMNT
         LA    R8,S99RBEND         PICK UP ADDRESS FOR TEXT POINTERS
         ST    R8,S99TXTPP
         L     R9,TXPTRSPC         PICK UP SPACE RESERVED FOR TEXT PTRS
         ALR   R9,R8               POINT TO FIRST TEXT UNIT.
         USING S99TUNIT,R9
         SPACE 1
* DDNAME **************************
         SPACE 1
         LA    R7,DDNAME-PARMLIST  INIT 'OFFSET' REGISTER.
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BNE   *+12
         LA    R1,DUNDDNAM
         B     *+8
         LA    R1,DALDDNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         LA    R14,DDNAME          FIND OUT ACTUAL LENGTH OF DDNAME.
         LA    R15,L'DDNAME
         BAL   R1,PP000
         LTR   R15,R15
         BNP   QQ000
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DDNAME.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BE    MM000               GO DO REQUEST.
         SPACE 1
* DSNAME **************************
         SPACE 1
         LA    R7,DSNAME-PARMLIST  OFFSET
         CLC   DSNAME,=CL44'NULLFILE ' CHECK FOR DUMMY ALLOCATION.
         BNE   AA100
         LA    R1,DALDUMMY
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     AA190
AA100    EQU   *
         LA    R14,DSNAME
         LA    R15,L'DSNAME
         BAL   R1,PP000            GET DSNAME LENGTH.
         LTR   R15,R15
         BNP   AA110
         LA    R1,DALDSNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSNAME.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSMEMBER ************************
         SPACE 1
AA110    EQU   *
         LA    R7,DSMEMBER-PARMLIST
         LA    R14,DSMEMBER
         LA    R15,L'DSMEMBER
         BAL   R1,PP000            GET DSMEMBER LENGTH.
         LTR   R15,R15
         BNP   AA120
         LA    R1,DALMEMBR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSMEMBER.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* PASSWORD ************************
         SPACE 1
AA120    EQU   *
         LA    R7,PASSWORD-PARMLIST
         LA    R14,PASSWORD
         LA    R15,L'PASSWORD
         BAL   R1,PP000            GET PASSWORD LENGTH
         LTR   R15,R15
         BNP   AA130
         LA    R1,DALPASSW
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE PASSWORD.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSSTATUS ************************
         SPACE 1
AA130    EQU   *
         LA    R7,DSSTATUS-PARMLIST
         LA    R14,DSSTATUS
         LA    R15,L'DSSTATUS
         BAL   R1,PP000            GET DSSTATUS LENGTH
         LTR   R15,R15
         BNP   AA140
         LA    R1,DALSTATS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSSTATUS,C'O'         OLD
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA135
         CLI   DSSTATUS,C'M'         MOD
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA135
         CLI   DSSTATUS,C'N'         NEW
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA135
*        CLC   DSSTATUS,=CL8'SHR '
*        BNE   QQ000
*                                  ASSUME DISP=SHR
         MVI   S99TUPAR,X'08'
AA135    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSNDISP *************************
         SPACE 1
AA140    EQU   *
         LA    R7,DSNDISP-PARMLIST
         LA    R14,DSNDISP
         LA    R15,L'DSNDISP
         BAL   R1,PP000            GET DSNDISP LENGTH
         LTR   R15,R15
         BNP   AA150
         LA    R1,DALNDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSNDISP,C'U'            UNCATLG
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA145
         CLI   DSNDISP,C'C'            CATLG
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA145
         CLI   DSNDISP,C'D'            DELETE
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA145
         MVI   S99TUPAR,X'08'           ASSUME KEEP
AA145    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSADISP *************************
         SPACE 1
AA150    EQU   *
         LA    R7,DSADISP-PARMLIST
         LA    R14,DSADISP
         LA    R15,L'DSADISP
         BAL   R1,PP000            GET DSADISP LENGTH
         LTR   R15,R15
         BNP   AA160
         LA    R1,DALCDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSADISP,C'U'             UNCATLG
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA155
         CLI   DSADISP,C'C'             CATLG
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA155
         CLI   DSADISP,C'D'             DELETE
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA155
         MVI   S99TUPAR,X'08'            ASSUME KEEP
AA155    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSUNIT **************************
         SPACE 1
AA160    EQU   *
         LA    R7,DSUNIT-PARMLIST
         LA    R14,DSUNIT
         LA    R15,L'DSUNIT
         BAL   R1,PP000            GET DSUNIT LENGTH
         LTR   R15,R15
         BNP   AA170
         LA    R1,DALUNIT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSUNIT.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV1 ************************
         SPACE 1
AA170    EQU   *
* DSVOLSER ************************
         SPACE 1
         LA    R7,DSVOLSER-PARMLIST
         CLC   DSVOLSER,NULLS
         BNE   AA172
         LA    R1,DALRTVOL         SET UP TO HAVE VOLSER RETURNED TO US
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,6
         MVC   S99TUPAR(6),BLANKS
         LA    R1,S99TUPAR
         ST    R1,WKRTNVOL         SAVE ADDRESS OF PARAMETER FIELD.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+6       NEXT TEXT UNIT.
         B     AA180
AA172    EQU   *
         LA    R14,DSVOLSER
         LA    R15,L'DSVOLSER
         BAL   R1,PP000            GET DSVOLSER LENGTH.
         LTR   R15,R15
         BNP   AA180
         LA    R1,DALVLSER
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLSER.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV2 ************************
         SPACE 1
AA180    EQU   *
         SPACE 1
* DSVOLREF ************************
         SPACE 1
         LA    R7,DSVOLREF-PARMLIST
         LA    R14,DSVOLREF
         LA    R15,L'DSVOLREF
         BAL   R1,PP000            GET DSVOLREF LENGTH.
         LTR   R15,R15
         BNP   AA190
         CLC   DSVOLSER,BLANKS     CHECK TO SEE THAT DSVOLREF DOES NOT
         BE    AA184                   CONFLICT WITH DSVOLSER.
         CLC   DSVOLSER,NULLS
         BNE   QQ000
AA184    EQU   *
         LA    R1,DALVLRDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLREF.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSFREE **************************
         SPACE 1
AA190    EQU   *
         LA    R7,DSFREE-PARMLIST
         LA    R14,DSFREE
         LA    R15,L'DSFREE
         BAL   R1,PP000            GET DSFREE LENGTH.
         LTR   R15,R15
         BNP   AA198
         CLC   DSFREE,=CL8'END   '
         BE    AA198
         CLC   DSFREE,=CL8'CLOSE '
         BNE   QQ000
         LA    R1,DALCLOSE
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
AA198    EQU   *
         CLC   DSNAME,=CL44'NULLFILE ' CHECK FOR DUMMY ALLOCATION.
         BE    CC100               GO DO DCB PARAMETERS.
         SPACE 1
* DSLABEL *************************
         SPACE 1
AA200    EQU   *
         LA    R7,DSLABEL-PARMLIST
         LA    R14,DSLABEL
         LA    R15,L'DSLABEL
         BAL   R1,PP000            GET DSLABEL LENGTH.
         LTR   R15,R15
         BNP   AA210
         LA    R1,DALLABEL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSLABEL,=CL4'SL  '
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA205
         CLC   DSLABEL,=CL4'SUL '
         BNE   QQ000
         MVI   S99TUPAR,X'08'
AA205    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSINOUT *************************
         SPACE 1
AA210    EQU   *
         LA    R7,DSINOUT-PARMLIST
         LA    R14,DSINOUT
         LA    R15,L'DSINOUT
         BAL   R1,PP000            GET DSINOUT LENGTH
         LTR   R15,R15
         BNP   AA220
         LA    R1,DALINOUT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSINOUT,C'I'
         BNE   *+12
         MVI   S99TUPAR,X'80'
         B     AA215
         CLI   DSINOUT,C'O'
         BNE   QQ000
         MVI   S99TUPAR,X'40'
AA215    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* $RESERV3 ************************
         SPACE 1
AA220    EQU   *
         LA    R7,$RESERV3-PARMLIST
         CLC   $RESERV3,BLANKS
         BNE   QQ000
         SPACE 3
         CLC   DSSTATUS,BLANKS     CHECK FOR NEW ALLOCATION.
         BE    BB100
         CLC   DSSTATUS,=CL8'NEW '
         BNE   CC100               GO DO DCB PARAMETERS.
         EJECT
* DSPWDLBL ************************
         SPACE 1
BB100    EQU   *
         LA    R7,DSPWDLBL-PARMLIST
         LA    R14,DSPWDLBL
         LA    R15,L'DSPWDLBL
         BAL   R1,PP000            GET DSPWDLBL LENGTH.
         LTR   R15,R15
         BNP   BB110
         LA    R1,DALPASPR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSPWDLBL,=CL8'PASSWORD'
         BNE   *+12
         MVI   S99TUPAR,X'10'
         B     BB105
         CLC   DSPWDLBL,=CL8'NOPWREAD'
         BNE   QQ000
         MVI   S99TUPAR,X'30'
BB105    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSDATE **************************
         SPACE 1
BB110    EQU   *
         LA    R7,DSDATE-PARMLIST
         LA    R14,DSDATE
         LA    R15,L'DSDATE
         BAL   R1,PP000            GET DSDATE LENGTH.
         LTR   R15,R15
         BNP   BB120
         CLC   DSDATE(6),=C'EXPDT=' CHECK FOR DATE TYPE.
         BNE   BB114
         SH    R15,=H'6'
         CH    R15,=H'5'           CHECK FOR VALID LENGTH.
         BNE   QQ000
         LA    R14,6(0,R14)        BUMP POINTER FOR MVCPARM
         LA    R1,DALEXPDT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE EXPDT DATE.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         B     BB120
         SPACE 1
BB114    EQU   *
         CLC   DSDATE(6),=C'RETPD='
         BNE   QQ000               ERROR
         SH    R15,=H'6'
         BNP   QQ000
         CH    R15,=H'4'
         BH    QQ000
         LA    R14,6(0,R14)        BUMP POINTER TO START OF NUMBER.
         BAL   R1,PP100            EDIT AND CONVERT RETPD.
         LA    R1,DALRETPD
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR        PLACE RETPD VALUE IN TEXT UNIT.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSALLOC *************************
         SPACE 1
BB120    EQU   *
         LA    R7,DSALLOC-PARMLIST
         LA    R14,DSALLOC
         LA    R15,L'DSALLOC
         BAL   R1,PP000            GET DSALLOC LENGTH.
         LTR   R15,R15
         BNP   QQ000               ERROR
         CLC   DSALLOC,=CL5'TRK '  CHECK FOR TRACK ALLOCATION.
         BNE   BB122
         LA    R1,DALTRK
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     BB130
         SPACE 1
BB122    EQU   *
         CLC   DSALLOC,=CL5'CYL '  CHECK FOR CYLINDER ALLOCATION.
         BNE   BB124
         LA    R1,DALCYL
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     BB130
         SPACE 1
BB124    EQU   *
         BAL   R1,PP100            CHECK FOR AVERAGE BLOCK ALLOCATION.
         LA    R1,DALBLKLN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSPRI ***************************
         SPACE 1
BB130    EQU   *
         LA    R7,DSPRI-PARMLIST
         LA    R14,DSPRI
         LA    R15,L'DSPRI
         BAL   R1,PP000            GET DSPRI LENGTH.
         LTR   R15,R15
         BNP   QQ000
         BAL   R1,PP100            CHECK FOR PRIMARY QUANTITY.
         LA    R1,DALPRIME
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSSEC ***************************
         SPACE 1
BB140    EQU   *
         LA    R7,DSSEC-PARMLIST
         LA    R14,DSSEC
         LA    R15,L'DSSEC
         BAL   R1,PP000            GET DSSEC LENGTH.
         LTR   R15,R15
         BNP   BB150
         BAL   R1,PP100            CHECK FOR SECONDARY QUANTITY.
         LA    R1,DALSECND
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSDIR ***************************
         SPACE 1
BB150    EQU   *
         LA    R7,DSDIR-PARMLIST
         LA    R14,DSDIR
         LA    R15,L'DSDIR
         BAL   R1,PP000            GET DSDIR LENGTH.
         LTR   R15,R15
         BNP   BB160
         BAL   R1,PP100            CHECK FOR DIRECTORY QUANTITY.
         LA    R1,DALDIR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSRLSE **************************
         SPACE 1
BB160    EQU   *
         LA    R7,DSRLSE-PARMLIST
         LA    R14,DSRLSE
         LA    R15,L'DSRLSE
         BAL   R1,PP000            GET DSRLSE LENGTH.
         LTR   R15,R15
         BNP   BB170
         CLC   DSRLSE,=CL8'RLSE '
         BNE   QQ000
         LA    R1,DALRLSE
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         SPACE 1
* DSCONTIG ************************
         SPACE 1
BB170    EQU   *
         LA    R7,DSCONTIG-PARMLIST
         LA    R14,DSCONTIG
         LA    R15,L'DSCONTIG
         BAL   R1,PP000            GET DSCONTIG LENGTH.
         LTR   R15,R15
         BNP   BB180
         LA    R1,DALSPFRM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSCONTIG,=CL8'CONTIG '
         BNE   QQ000
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSROUND *************************
         SPACE 1
BB180    EQU   *
         LA    R7,DSROUND-PARMLIST
         LA    R14,DSROUND
         LA    R15,L'DSROUND
         BAL   R1,PP000            GET DSROUND LENGTH.
         LTR   R15,R15
         BNP   BB190
         CLC   DSROUND,=CL8'ROUND '
         BNE   QQ000
         LA    R1,DALROUND
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         SPACE 1
* $RESERV4 ************************
         SPACE 1
BB190    EQU   *
         LA    R7,$RESERV4-PARMLIST
         CLC   $RESERV4,BLANKS
         BNE   QQ000
         EJECT
* DSBLKSI *************************
*
CC100    EQU   *
*
         CLC   DSSTATUS(3),=C'NEW' IF ALLOCATION IS NOT NEW
         BNE   MM000
*
         LA    R7,DSBLKSI-PARMLIST
         LA    R14,DSBLKSI
         LA    R15,L'DSBLKSI
         BAL   R1,PP000            GET DSBLKSI LENGTH.
         LTR   R15,R15
         BNP   CC110
         BAL   R1,PP100            CHECK FOR BLKSIZE VALUE.
         CH    R15,=H'32767'       CHECK FOR MAXIMUM BLOCKSIZE.
         BH    QQ000               ERROR.
         LA    R1,DALBLKSZ
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSORG ***************************
         SPACE 1
CC110    EQU   *
         LA    R7,DSORG-PARMLIST
         CLC   DSORG,NULLS
         BNE   CC112
         LA    R1,DALRTORG         SET UP TO HAVE DSORG RETURNED TO US
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         MVC   S99TUPAR(2),NULLS
         LA    R1,S99TUPAR
         ST    R1,WKRTDSRG         SAVE ADDRESS OF PARAMETER FIELD
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         B     CC120
CC112    EQU   *
         LA    R14,DSORG
         LA    R15,L'DSORG
         BAL   R1,PP000            GET DSORG LENGTH
         LTR   R15,R15
         BNP   CC120
         LA    R1,DALDSORG
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSORG,=CL8'VSAM '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0008'
         B     CC115
         CLC   DSORG,=CL8'PO   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0200'
         B     CC115
         CLC   DSORG,=CL8'POU  '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0300'
         B     CC115
         CLC   DSORG,=CL8'DA   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'2000'
         B     CC115
         CLC   DSORG,=CL8'DAU  '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'2100'
         B     CC115
         CLC   DSORG,=CL8'PS   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'4000'
         B     CC115
         CLC   DSORG,=CL8'PSU  '
         BNE   QQ000               ERROR
         MVC   S99TUPAR(2),=XL2'4100'
CC115    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSKEYLEN ************************
         SPACE 1
CC120    EQU   *
         LA    R7,DSKEYLEN-PARMLIST
         LA    R14,DSKEYLEN
         LA    R15,L'DSKEYLEN
         BAL   R1,PP000            GET DSKEYLEN LENGTH.
         LTR   R15,R15
         BNP   CC130
         BAL   R1,PP100            CHECK FOR KEYLENGTH QUANTITY.
         CH    R15,=H'255'         CHECK FOR MAX KEYLENGTH.
         BH    QQ000               ERROR
         LA    R1,DALKYLEN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         STC   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSLRECL *************************
         SPACE 1
CC130    EQU   *
         LA    R7,DSLRECL-PARMLIST
         LA    R14,DSLRECL
         LA    R15,L'DSLRECL
         BAL   R1,PP000            GET DSLRECL LENGTH.
         LTR   R15,R15
         BNP   CC140
         LA    R1,DALLRECL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSLRECL,=CL5'X '    CHECK FOR SPANNED RECORDS.
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'8000'
         B     CC134
         BAL   R1,PP100            CHECK FOR LRECL QUANTITY.
         CH    R15,=H'32767'
         BH    QQ000
         STH   R15,S99TUPAR
CC134    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSRECFM1 THRU DSRECFM8 **********
         SPACE 1
CC140    EQU   *
         LA    R7,DSRECFM1-PARMLIST
         CLC   DSRECFM,BLANKS      CHECK FOR NO INFORMATION
         BE    CC180
         LA    R1,DALRECFM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSRECFM1,C' '
         BE    CC144
         CLI   DSRECFM1,C'V'
         BNE   *+12
         OI    S99TUPAR,X'40'
         B     CC144
         CLI   DSRECFM1,C'F'
         BNE   *+12
         OI    S99TUPAR,X'80'
         B     CC144
         CLI   DSRECFM1,C'U'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'C0'
         SPACE 1
CC144    EQU   *
         LA    R7,DSRECFM2-PARMLIST
         CLI   DSRECFM2,C' '
         BE    CC148
         CLI   DSRECFM2,C'B'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'10'
         SPACE 1
CC148    EQU   *
         LA    R7,DSRECFM3-PARMLIST
         CLI   DSRECFM3,C' '
         BE    CC152
         CLI   DSRECFM3,C'S'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'08'
         SPACE 1
CC152    EQU   *
         LA    R7,DSRECFM4-PARMLIST
         CLI   DSRECFM4,C' '
         BE    CC156
         CLI   DSRECFM4,C'T'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'20'
         SPACE 1
CC156    EQU   *
         LA    R7,DSRECFM5-PARMLIST
         CLI   DSRECFM5,C' '
         BE    CC160
         CLI   DSRECFM5,C'M'
         BNE   *+12
         OI    S99TUPAR,X'02'
         B     CC160
         CLI   DSRECFM5,C'A'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'04'
         SPACE 1
CC160    EQU   *
         LA    R7,DSRECFM6-PARMLIST
         CLI   DSRECFM6,C' '
         BNE   QQ000               ERROR
         SPACE 1
CC164    EQU   *
         LA    R7,DSRECFM7-PARMLIST
         CLI   DSRECFM7,C' '
         BNE   QQ000               ERROR
         SPACE 1
CC168    EQU   *
         LA    R7,DSRECFM8-PARMLIST
         CLI   DSRECFM8,C' '
         BNE   QQ000               ERROR
         SPACE 1
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSDCBDS *************************
         SPACE 1
CC180    EQU   *
         LA    R7,DSDCBDS-PARMLIST
         LA    R14,DSDCBDS
         LA    R15,L'DSDCBDS
         BAL   R1,PP000            GET DSDCBDS LENGTH.
         LTR   R15,R15
         BNP   CC190
         LA    R1,DALDCBDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV5 ************************
         SPACE 1
CC190    EQU   *
         LA    R7,$RESERV5-PARMLIST
         CLC   $RESERV5,BLANKS
         BNE   QQ000
         B     MM000               GO DO REQUEST
         SPACE 3
MVCPARM  MVC   S99TUPAR(0),0(R14)
         EJECT
MM000    EQU   *
         SH    R8,=H'4'            BACK UP TO LAST USED TEXT POINTER.
         OI    0(R8),X'80'         INDICATE END OF LIST.
         LA    R1,REQBKPTR         GET THE BEGINNING OF THIS MESS.
         DYNALLOC
         L     R0,S99RSC           PICK UP REASON CODES.
         LTR   R15,R15             CHECK FOR ERRORS.
         BNZ   ZZ000
         L     R1,WKRTNVOL         CHECK FOR VOLSER RETURN REQUEST.
         LTR   R1,R1
         BZ    MM010
         MVC   DSVOLSER,0(R1)      GIVE CALLER INFO.
MM010    EQU   *
         L     R1,WKRTDSRG         CHECK FOR DSORG RETURN REQUEST.
         LTR   R1,R1
         BZ    MM020
         CLC   0(2,R1),=XL2'0008'
         BNE   *+14
         MVC   DSORG,=CL8'VSAM '
         B     MM020
         CLC   0(2,R1),=XL2'0200'
         BNE   *+14
         MVC   DSORG,=CL8'PO   '
         B     MM020
         CLC   0(2,R1),=XL2'0300'
         BNE   *+14
         MVC   DSORG,=CL8'POU  '
         B     MM020
         CLC   0(2,R1),=XL2'2000'
         BNE   *+14
         MVC   DSORG,=CL8'DA   '
         B     MM020
         CLC   0(2,R1),=XL2'2100'
         BNE   *+14
         MVC   DSORG,=CL8'DAU  '
         B     MM020
         CLC   0(2,R1),=XL2'4000'
         BNE   *+14
         MVC   DSORG,=CL8'PS   '
         B     MM020
         CLC   0(2,R1),=XL2'4100'
         BNE   *+14
         MVC   DSORG,=CL8'PSU  '
         B     MM020
         CLC   0(2,R1),=XL2'8000'
         BNE   *+14
         MVC   DSORG,=CL8'IS   '
         B     MM020
         CLC   0(2,R1),=XL2'8100'
         BNE   MM020
         MVC   DSORG,=CL8'ISU  '
MM020    EQU   *
         B     ZZ000
         EJECT
PP000    EQU   *                   ROUTINE TO DETERMINE LENGTH OF
         LTR   R15,R15                 NON-BLANK CHARACTERS IN FIELD.
         BNP   PP030                   AT ENTRY R1 CONTAINS THE RETURN
         ALR   R15,R14                 ADDRESS.  R14 CONTAINS FIELD
PP010    EQU   *                       ADDRESS.  R15 CONTAINS LENGTH
         BCTR  R15,0                   OF FIELD.  AT EXIT R15 WILL BE
         CLI   0(R15),C' '             ALTERED TO CONTAIN LENGTH OF
         BNE   PP020                   NON-BLANK CHARACTERS IN FIELD.
         CLR   R15,R14
         BH    PP010
         SR    R15,R15
         B     PP030
PP020    EQU   *
         SLR   R15,R14
         LA    R15,1(0,R15)
PP030    EQU   *
         BR    R1
         SPACE 3
PP100    EQU   *                   ROUTINE TO EDIT EBCDIC NUMERIC
         LTR   R15,R15                 DATA AND, IF VALID, TO CONVERT
         BNP   PP120                   IT TO BINARY.
         ST    R15,DBLWORD             AT ENTRY R1 CONTAINS THE RETURN
         ALR   R15,R14                 ADDRESS.  R14 CONTAINS DATA
PP110    EQU   *                       ADDRESS.  R15 CONTAINS LENGTH
         BCTR  R15,0                   OF DATA.  AT NORMAL EXIT, R15
         CLI   0(R15),C'0'             WILL BE ALTERED TO CONTAIN THE
         BL    QQ000                   BINARY EQUIVALENT OF THE NUMBER.
         CLI   0(R15),C'9'
         BH    QQ000                   AN ABNORMAL EXIT WILL BE TAKEN
         CLR   R15,R14                 TO THE COMMON ERROR ROUTINE IF
         BH    PP110                   DATA IS NOT NUMERIC.
         L     R15,DBLWORD
         BCTR  R15,0
         EX    R15,PACKNUM
         CVB   R15,DBLWORD
PP120    EQU   *
         BR    R1
         SPACE 1
PACKNUM  PACK  DBLWORD,0(0,R14)
         EJECT
QQ000    EQU   *
         LR    R0,R7               SET UP RETURN CODES
         LA    R15,15
         B     ZZ000
         SPACE 3
ZZ000    EQU   *
         LR    R1,R13              PREPARE FOR FREEMAIN.
         L     R13,SAVEAREA+4
         LTR   R11,R11             CHECK TO SEE IF PARAMETER LIST IS
         BNP   ZZ010                   ONLY ENTRY IN CALLER'S ADDRESS
         L     R2,24(0,R13)            LIST.
         L     R2,4(0,R2)          GET SECOND ADDRESS IN LIST.
         ST    R0,0(0,R2)          PUT REGISTER INFORMATION IN LIST.
         ST    R15,4(0,R2)
ZZ010    EQU   *
         LR    R2,R0               SAVE CONTENTS OF REGISTERS.
         LR    R3,R15
FREE1    DS    0H
         LR    R0,R2
         LR    R15,R3
         L     R14,12(0,R13)
         RETURN (1,12),RC=(15)
WORKLEN  DC    A(DYNWRKSZ)
TXPTRSPC DC    A(4*40)             RESERVE SPACE FOR 40 TEXT POINTERS.
BLANKS   DC    CL44' '
NULLS    DC    XL44'00'
         LTORG
         EJECT
PARMLIST DSECT
DDNAME   DS    CL8
DSNAME   DS    CL44
DSMEMBER DS    CL8
PASSWORD DS    CL8
DSSTATUS DS    CL8
DSNDISP  DS    CL8
DSADISP  DS    CL8
DSUNIT   DS    CL8
$RESERV1 DS    CL8
DSVOLSER DS    CL6
$RESERV2 DS    CL40
DSVOLREF DS    CL44
DSFREE   DS    CL8
DSLABEL  DS    CL4
DSINOUT  DS    CL4
$RESERV3 DS    CL16
DSPWDLBL DS    CL8
DSDATE   DS    CL12
DSALLOC  DS    CL5
DSPRI    DS    CL6
DSSEC    DS    CL6
DSDIR    DS    CL5
DSRLSE   DS    CL8
DSCONTIG DS    CL8
DSROUND  DS    CL8
$RESERV4 DS    CL24
DSBLKSI  DS    CL5
DSORG    DS    CL8
DSKEYLEN DS    CL3
DSLRECL  DS    CL5
DSRECFM  DS    0CL8
DSRECFM1 DS    C
DSRECFM2 DS    C
DSRECFM3 DS    C
DSRECFM4 DS    C
DSRECFM5 DS    C
DSRECFM6 DS    C
DSRECFM7 DS    C
DSRECFM8 DS    C
DSDCBDS  DS    CL44
$RESERV5 DS    CL24
PARMEND  EQU   *
         DS    0F
SAVEAREA DS    18F
REQBKPTR DS    F
WKRTNVOL DS    F
WKRTDSRG DS    F
         DS    F
DBLWORD  DS    D
         DS    4F
DYNWORK  DS    XL1600
DYNWRKSZ EQU   *-SAVEAREA
         DS    0D
         PRINT NOGEN
         IHAPSA
         IHAASCB
         IEFUCBOB
         IEFZB4D0
         IEFZB4D2
         CVT   DSECT=YES
         IECDIOCM
         IECDATB
ATBLEN   EQU   *-ATB
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
*
         CNOP  0,8
ETPSATEN CSECT
ATXTBGN  EQU   *
         STM   R0,R15,0(R13)               SAVE REGS
         BALR  R11,R0                      ADDRESS SET
         USING *,11
         LR    R4,R1
         L     R3,28(,R4)
         L     R5,8(,R3)   ASCB
         USING ASCB,5
         L     R7,16(,R4)  UCB
         USING UCBBGN,7
         L     R2,UCBEXTPT UCB EXTENSION
         USING UCBCMEXT,2
         CLC   UCBASID,574(R11)
         BE    BADEXIT
         CLC   ASCBASID,UCBASID
         BE    TFORECB
         MVC   6(2,R4),UCBASID
         LM    R0,R15,0(R13)               RESTORE REGS
         SR    R15,R15
         LA    R15,4(R15)
         B     892                         PSA REFERENCE
TFORECB  DS    0H   124(,R11)
         TM    UCBRDYQ,X'80'               IS SOMEONE WAITING
         BZ    BADEXIT                     NO, UNSOL INT
         LR    R9,R13                      THIS REGISTER IS PRESERVED
         LA    R11,UCBRDYQ                 ECB ADDRESS
         LR    R13,R5                      SET ASCB
         POST  (11),ASCB=(13),ERRET=AFTPOST,BRANCH=YES
AFTPOST  DS    0H
         LR    R13,R9
BADEXIT  DS    0H
         LM    R0,R15,0(R13)               RESTORE REGS
         SR    R15,R15
         B     890                         PSA REFERENCE
         CNOP  0,8
ATXTLEN  EQU   *-ATXTBGN
         END
SPFSTOW  CSECT
         USING *,12
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET BASE REGISTER
         LR    R10,R1             SAVE PARM ADDRESS
         B     PROLOG
         DC    C'SPFSTOW  05/15/84 BRIAN COOK'
PROLOG   DS    0H
         GETMAIN R,LV=4096        GET MY SAVE AREA ADDRESS
         LR    R11,R1             SAVE GETMAIN POINTER
         ST    R13,4(R11)         STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11
         USING MYSAVE,13
*
GETPARM  DS    0H
         LM    R2,R5,0(10)    R2=ADDRESS OF OPEN DIRECTORY DCB
         L     R3,0(,R3)      R3=NUMBER OF CARDS
*                             R4=ADDRESS OF USERID
*                             R5=ADDRESS OF MEMBER NAME
*
         MVC   BLDLENT(60),BLDLAREA
         MVC   MEMNAME(8),0(R5)  MOVE THE MEMBER NAME
*
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
         EJECT
         BLDL  (2),BLDLENT
*
         LTR   R15,15
         BZ    *+10
         LA    R14,BLDLENT
         DC    X'0000'
         MVC   SPFVER(30),SPFVER+2   SKIP BLDL JUNK
*
         MVC   SPFVER(5),=X'0100000000'
         MVI   STALIAS,X'0F'
*
         MVC   SPFUID(8),SPACES
         MVC   SPFUID(7),0(R4)    USERID
*
         TIME  DEC
         STCM  R1,7,SPFMODT
         CLC   SPFCRDT(3),SPACES
         BNE   *+8
         STCM  R1,7,SPFCRDT
         SRL   R0,16              SHIFT OFF SECONDS
         STCM  R0,3,SPFMODTM
         STCM  R3,3,SPFSIZE
         CLC   SPFSIZEI(2),SPACES
         BNE   *+8
         STCM  R3,3,SPFSIZEI
*
         STOW  (2),MEMNAME,R
*
*
*
JUSTEOJ  DS    0H
*
         L     R13,4(,R13)  PICK UP CALLING SAVE AREA
         FREEMAIN R,LV=4096,A=(11)
         LM    R14,R12,12(R13)
         SR    R15,15
         BR    R14
         TITLE 'WORKING STORAGE AREAS'
SPACES   DC    CL16' '
FZEROS   DC    F'0'
BLDLAREA DS    0H
         DC    H'1'     NUMBER OF ENTRIES IN BLDL LIST
         DC    H'56'    LENGTH OF ENTRY
         DC    56X'40'
         TITLE 'DSECT '
MYSAVE   DSECT
         DS    18F
BLDLENT  DS    F
MEMNAME  DS    CL8
TTRC     DS    XL3
STALIAS  DS    XL1
*
*    SPF USER DATA
*
SPFVER   DS    XL1
SPFMOD   DS    XL1
         DS    XL2
         DS    XL1
SPFCRDT  DS    PL3
         DS    XL1
SPFMODT  DS    PL3
SPFMODTM DS    XL2
SPFSIZE  DS    XL2
SPFSIZEI DS    XL2
SPFMOD2  DS    XL2
SPFUID   DS    CL7
         DS    CL3
BLDLPAD  DS    XL2
         END
/*
//LKED     EXEC PGM=HEWLF064,PARM='XREF,LET,LIST,NCAL,TERM,AC=1',
//             COND=(8,LT,ASM),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS1.LINKLIB,DISP=SHR
//SYSLMOD  DD   DISP=SHR,DSN=LU.LNKLIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=DISK,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
     NAME NOTTSO(R)
//
//*
//* THE PARM FIELD SPECIFIES A 3278-4, UNIT ADDRESS 458
//*                                                
//*                                 ----------  -----
//*                                            
//ETPS      EXEC PGM=ETPS,PERFORM=3,PARM=ETPS4458
//STEPLIB   DD   DISP=SHR,DSN=TECH.SERV.LOAD
//SYSUDUMP  DD   SYSOUT=*
//*
