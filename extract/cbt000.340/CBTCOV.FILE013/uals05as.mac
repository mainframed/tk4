TRACERS  TITLE 'TRACERS - REAL STORAGE TRACE (MVS ONLY)'
*---------------------------------------------------------------------*
*                                                                     *
*                        T R A C E R S                                *
*                       ----------------                              *
*                                                                     *
*              A REAL STORAGE TRACE FACILITY (MVS ONLY)               *
*                                                                     *
*                                                                     *
* JCL:                                                                *
*                                                                     *
* //     JOB ACCNT                                                    *
* //TRACERS  EXEC PGM=TRACERS,PARM='HHMMSSTH,CCC'                     *
* //STEPLIB  DD DISP=SHR,DSN=                                         *
* //SYSPRINT DD SYSOUT=A                                              *
* /*                                                                  *
*                                                                     *
* PARAMETER:                                                          *
*                                                                     *
* HHMMSSTH IS THE INTERVAL TRACERS WILL TRACE REAL STORAGE            *
*          (FOR DESCRIPTION SEE OS/VS2 SUPERVISOR SERVICES AND MACRO  *
*          INSTRUCTIONS GC28-0683, MACRO STIMER, PARAMETER DINTVL)    *
*                                                                     *
* CCC      NO. OF INTERVALS                                           *
*                                                                     *
*                                                                     *
* OUTPUT:                                                             *
*                                                                     *
* TRACERS WRITES AT EACH INTERVAL ONE LINE TO THE SYSPRINT DATASET.   *
* AT LEAST ONE LINE WITH AVERAGE VALUES WILL BE PRINT.                *
*                                                                     *
*                                                                     *
*  AUTOR : MICHAEL SAUTTER                                            *
*          C/O ALLIANZ LEBENSVERSICHERUNGS AG.                        *
*          ABTEILUNG DVZ/SYST/A1 - TEL. (0711)/6666 4214              *
*          REINSBURGSTRASSE 32/34                                     *
*          7000 STUTTGART 1                                           *
*                                                                     *
*---------------------------------------------------------------------*
TRACERS  CSECT
*
* STANDARD LINKAGE FOLLOWS FOR ENTRY:
*
         SAVE  (14,12),,TRACERS-&SYSDATE/&SYSTIME-SAUTTER
         LR    R12,R15            USE R12 AS 0-ORIGIN BASE
         USING TRACERS,R12        *-------->R12 (PROGRAM BASE)
         ST    R13,SAVE+4         SAVE ADDRESS OF CALLERS SAVE AREA
         LR    R15,R13            . . . .
         LA    R13,SAVE           LOAD ADDRESS OF OWN SAVE AREA
         ST    R13,8(R15)         CHAIN SAVE AREAS
         B     SAVE+72            BRANCH AROUND DEFINITIONS
SAVE     DS    18F                SAVE AREA
         L     R1,0(,R1)
         LA    R1,2(,R1)
*
         MVC   INTVL,0(R1)        INTERVAL
         PACK  USECOUNT,09(3,R1)  LOOP COUNT
         OI    USECOUNT+1,X'0F'
         ZAP   #CHECKS,USECOUNT
*
         OPEN  (SYSP,(OUTPUT))
*
         PUT   SYSP,LNE6
         PUT   SYSP,LNE2
         PUT   SYSP,LNE3          HEAD LINE
         PUT   SYSP,LNE5
*
         B     SKIP
*
STIMER   STIMER WAIT,DINTVL=INTVL WAIT FOR TIME INTERVAL
*
SKIP     EQU   *
         XC    AREA(AREAL),AREA   CLEAN UP
         L     R7,X'010'          CVT
         L     R1,X'168'(R7)      CVTLPDIA
         SRL   R1,8               WE NEED BITS 8-23
         STH   R1,CVTLPDIA        SAVE ADDRESS IN FORM OF THE PFT/VBN
         L     R7,X'164'(,R7)     PVT
         L     R6,X'00C'(,R7)     APPARENT ADDRESS OF PFT
         LR    R5,R6              . . .
         AH    R6,X'010'(,R7)     INDEX TO FIRST PTFE ENTRY
         AH    R5,X'012'(,R7)     INDEX TO LAST  PTFE ENTRY
PFTLOOP  EQU   *
         CR    R6,R5
         BE    ENDPFT             END OF PFT
CHECK1   CLI   14(R6),X'04'       SQA RESERVED
         BNE   CHECK2
         L     R1,SQA             ADD 1 TO SQA
         LA    R1,1(,R1)
         ST    R1,SQA
         B     NEXT
CHECK2   CLI   14(R6),X'08'       COMMON FRAME QUEUE
         BNE   CHECK3
         CLC   0(2,R6),=X'FFFF'   CHECK FOR SYSTEM ID
         BNE   CHECK3
         CLC   2(2,R6),CVTLPDIA   IF VBN > LPDIA IT IS A PLPA FRAME
         BNH   CHECK2A
         L     R1,PLPA            ADD 1 TO PLPA
         LA    R1,1(,R1)
         ST    R1,PLPA
         B     NEXT
CHECK2A  L     R1,CSA             ADD 1 TO CSA
         LA    R1,1(,R1)
         ST    R1,CSA
         B     NEXT
CHECK3   CLI   14(R6),X'0C'       SQA FRAME QUEUE
         BNE   CHECK4
         L     R1,SQA             ADD 1 TO SQA
         LA    R1,1(,R1)
         ST    R1,SQA
         B     NEXT
CHECK4   CLI   14(R6),X'80'       LOCAL FRAME QUEUE
         BNE   CHECK5
         L     R1,LFQ             ADD 1 TO LFQ
         LA    R1,1(,R1)
         ST    R1,LFQ
         B     NEXT
CHECK5   CLI   14(R6),X'84'       LSQA FRAME QUEUE
         BNE   CHECK6
         L     R1,LSQA            ADD 1 TO LSQA
         LA    R1,1(,R1)
         ST    R1,LSQA
         B     NEXT
CHECK6   CLI   12(R6),X'80'       AVAILABLE FRAME QUEUE
         BNE   NEXT
         L     R1,AVAIL           ADD 1 TO AFQ
         LA    R1,1(,R1)
         ST    R1,AVAIL
         B     NEXT
NEXT     EQU   *
         LA    R6,16(,R6)         NEXT PFTE ELEMENT
         B     PFTLOOP            LOOP THRU PAGE FRAME TABLE
ENDPFT   EQU   *
*
         TIME  DEC
         SRL   R0,4
         ST    R0,DOPWORT+4
         XC    DOPWORT(4),DOPWORT
         OI    DOPWORT+7,X'0F'
         MVC   LNE4TIME,=X'2021204B20204B2020'
         ED    LNE4TIME,DOPWORT+4
*
         L     R1,16
         L     R1,X'80'(,R1)      CVTNUCB (LOWEST ADDRESS NOT IN NUC)
         SR    R0,R0
         D     R0,=F'1024'
         SRL   R1,2
         ST    R1,NUC             STORE NO. OF NUCLEUS FRAMES
*
         LA    R4,0               CLEAR
         LA    R6,LNE4AREA        ADDR OF FIRST ELEMENT IN LNE
         LA    R3,NOELEM          NO. ELEMENTS
         LA    R1,AREA            ADDR OF FIRST VALUE
MULT     EQU   *
         L     R2,0(,R1)          LOAD VALUE
         SLL   R2,2               MULTIPLY WITH 4 (VALUE IN K)
         CVD   R2,DOPWORT         EDIT
         OI    DOPWORT+7,X'0F'
         MVC   1(6,R6),=X'402020202120'
         ED    1(6,R6),DOPWORT+5
         LA    R6,9(,R6)          NEXT ELEMENT IN LNE
         LA    R1,4(,R1)          NEXT ELEMENT
         BCT   R3,MULT
*
         L     R1,SQA             NUCLEUS+CSA+PLPA+SQA
         AH    R1,CSA+2
         AH    R1,NUC+2
         AH    R1,PLPA+2
         ST    R1,SYS
         SLL   R1,2               MULTIPLY WITH 4 (VALUE IN K)
         CVD   R1,DOPWORT         EDIT
         OI    DOPWORT+7,X'0F'
         MVC   LNE4SYS+1(6),=X'402020202120'
         ED    LNE4SYS+1(6),DOPWORT+5
*
         PUT   SYSP,LNE4
*
* BUILD TOTAL VALUES
*
         LA    R1,AREA
         LA    R2,AVAREA
         LA    R3,NOELEM+1
AVLOOP   EQU   *
         L     R4,0(,R1)
         CVD   R4,DOPWORT
         OI    DOPWORT+7,X'0F'
         AP    0(16,R2),DOPWORT(8)
         LA    R1,4(,R1)
         LA    R2,16(,R2)
         BCT   R3,AVLOOP
*
         CP    USECOUNT,=PL2'1'
         BNH   DIV
         SP    USECOUNT,=P'1'
         B     STIMER
*
DIV      EQU   *                  EDIT AVERAGE VALUES
         LA    R6,LNE4AREA        ADDR OF FIRST ELEMENT IN LNE
         LA    R3,NOELEM          NO. ELEMENTS
         LA    R2,AVAREA          ADDR OF FIRST AVERAGE VALUE FIELD
AVMULT   EQU   *
         MP    0(16,R2),=P'400'   *400
         DP    0(16,R2),#CHECKS(2)
         SRP   0(14,R2),64-2,5    /100
         OI    13(R2),X'0F'
         MVC   1(6,R6),=X'402020202120'
         ED    1(6,R6),11(R2)
AVMULTX  LA    R6,9(,R6)          NEXT ELEMENT IN LNE
         LA    R2,16(,R2)         NEXT ELEMENT
         BCT   R3,AVMULT
*
         MP    AVSYS,=P'400'      *400
         DP    AVSYS,#CHECKS
         SRP   AVSYS(14),64-2,5   /100
         OI    AVSYS+13,X'0F'
         MVC   LNE4SYS+1(6),=X'402020202120'
         ED    LNE4SYS+1(6),AVSYS+11
*
*
         PUT   SYSP,LNE1          SKIP TO CHANNEL 1
         PUT   SYSP,LNE6
         PUT   SYSP,LNE7
         PUT   SYSP,LNE3
         PUT   SYSP,LNE5
         PUT   SYSP,LNE4
*
*
* STANDARD LINKAGE FOLLOWS FOR RETURN TO OS:
*
RETURN   EQU   *
         L     R13,SAVE+4
         LM    R14,R12,12(R13)    RESTORE REGISTERS
         BR    R14                RETURN
         EJECT
*
* DEFINITIONS
*
*
         CNOP  0,8
INTVL    DC    C'00000000'
USECOUNT DC    PL2'0'
#CHECKS  DC    PL2'0'
DOPWORT  DC    D'0'
*
* TOTAL VALUE AREA
*
AVAREA   EQU   *
AVNUC    DC    PL16'0'            NUCLEUS SIZE
AVLFQ    DC    PL16'0'            LOCAL FRAME QUEUE
AVLSQA   DC    PL16'0'            LSQA FRAME QUEUE
AVAVAIL  DC    PL16'0'            AVAILABLE FRAME QUEUE
AVCSA    DC    PL16'0'            COMMON FRAME QUEUE
AVPLPA   DC    PL16'0'            NO. OF NUCLEUS FRAMES
AVSQA    DC    PL16'0'            SQA FRAME QUEUE
AVSYS    DC    PL16'0'            SYS   NO. OF FRAMES
*
* SINGLE VALUE AREA
*
AREA     EQU   *
NUC      DC    A(0)               NUCLEUS SIZE
LFQ      DC    A(0)               LOCAL FRAME QUEUE
LSQA     DC    A(0)               LSQA FRAME QUEUE
AVAIL    DC    A(0)               AVAILABLE FRAME QUEUE
CSA      DC    A(0)               COMMON FRAME QUEUE
PLPA     DC    A(0)               NO. OF NUCLEUS FRAMES
SQA      DC    A(0)               SQA FRAME QUEUE
NOELEM   EQU   (*-AREA)/4         NO. OF ELEMENTS
SYS      DC    A(0)               SYS   NO. OF FRAMES
CVTLPDIA DC    H'0'               VIRTUAL ADDRESS OF LPA DIRECTORY
AREAL    EQU   *-AREA
*
* OUTPUT LINES
*
LNE1     DC    CL95' '
         ORG   LNE1
         DC    X'8B'
         ORG
*
LNE2     DC    CL95' '
         ORG   LNE2
         DC    X'19'
         DC    CL06' '
         DC    C'                             S A M P L E   R E P O R T*
                                  '
         ORG
*
LNE3     DC    CL95' '
         ORG   LNE3
         DC    X'09'
LNE3TIME DC    CL12'     TIME   '
         DC    C' NUCLEUS    LOCAL     LSQA      AFQ      CSA     PLPA *
                    SQA (NUC+CSA+PLPA+SQA)'
         ORG
*
LNE4     DC    CL95' '
         ORG   LNE4
         DC    X'09'
LNE4TIME DC    CL9' '
         DC    CL3' '
LNE4AREA EQU   *
LNE4NUC  DC    CL8'       K'
         DC    CL1' '
LNE4LFQ  DC    CL8'       K'
         DC    CL1' '
LNE4LSQA DC    CL8'       K'
         DC    CL1' '
LNE4AFQ  DC    CL8'       K'
         DC    CL1' '
LNE4CSA  DC    CL8'       K'
         DC    CL1' '
LNE4PLPA DC    CL8'       K'
         DC    CL1' '
LNE4SQA  DC    CL8'       K'
         DC    CL11' '
LNE4SYS  DC    CL8'       K'
         ORG
*
LNE5     DC    CL95' '
         ORG   LNE5
         DC    X'09'
         DC    93CL1'-'
         ORG
LNE6     DC    CL95' '
         ORG   LNE6
         DC    X'19'
         DC    CL06' '
         DC    CL82'                        R E A L   S T O R A G E   T*
                R A C E                   '
         ORG
LNE7     DC    CL95' '
         ORG   LNE7
         DC    X'19'
         DC    CL06' '
         DC    CL82'                            A V E R A G E   R E P O*
                R T                       '
         ORG
*
         PRINT NOGEN
SYSP     DCB   MACRF=(PM),LRECL=95,BLKSIZE=950,DDNAME=SYSPRINT,        *
               DSORG=PS,RECFM=FBM
*
* REGISTER EQUATES:
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
