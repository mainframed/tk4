* DIESES PROGRAMM LIEST DIE DIRECTORY EINES PARTITIONED DATA SET
* SEQUENTIELL DURCH UND STELLT DIE MEMBER-NAMEN ZUR VERFUEGUNG.
         SPACE
* FRANCKE, 02.08.1973
         SPACE
QX01443  CSECT
         SPACE
*
* LINKAGE CONVENTION
*
         STM   14,12,12(13)       RETTEN REG.14-12 DES RUFENDEN PROGR.
         LR    14,13              RETTEN A(SAVE-AREA DES RUF.PROGR.)
         BALR  3,0                BASISREGISTER 3
         USING *,3                GELADEN
         LA    13,SAVE            A(SAVE-AREA DES GERUF.PROGR.)
         ST    14,4(13)           RETTEN A(SAVE-AREA DES RUF.PROGR.)
         ST    13,8(14)           RETTEN A(SAVE-AREA DES GERUF.PROGR.)
         B     SAVE+18*4          VERZWEIGEN ZUR NAECHSTEN INSTRUKTION
         SPACE
SAVE     DS    18F                SAVE-AREA
         SPACE
         L     4,0(1)             A(A(KENNZ)) NACH REG.4
         L     4,0(4)             A(KENNZ) NACH REG.4
         L     5,4(1)             A(A(STORE)) NACH REG.5
         L     5,0(5)             A(STORE) NACH REG.5
         L     6,8(1)             A(A(MEMBER)) NACH REG.6
         L     6,0(6)             A(MEMBER) NACH REG.6
*
* EROEFFNEN DER DIRECTORY
*
         TM    0(4),X'C0'         DIRECTORY SCHON EROEFFNET ?
         BO    M1                 VERZWEIGEN, WENN EROEFFNET
         SPACE
         OPEN  PDSDCB
         SPACE
         OI    0(4),X'80'         EROEFFNUNG KENNZEICHNEN
         LA    12,EIN             A(DIRECTORY-BLOCK) NACH REG.12
         B     READPDS            VERZWEIGEN ZUM LESEN DIRECTORY
         SPACE
M1       EQU   *
         LM    9,12,0(5)          LADEN ALTE REGISTER-STAENDE
         B     M2                 VERZWEIGEN ZUM VERGLEICH AUF ENDE
         EJECT
*
* LESEN DIRECTORY-BLOCK UND BEREITSTELLEN MEMBER-NAME
*
READPDS  EQU   *
         GET   PDSDCB,(12)
         SPACE
         CLC   0(2,12),=H'0'      ENDE DIRECTORY ?
         BE    ENDPDS             VERZWEIGEN BEI ENDE
         LR    11,12              A(DIRECTORY-BLOCK) NACH REG.11
         LH    10,0(12)           L(DIRECTORY-BLOCK) NACH REG.10
         AR    11,10              ENDE DIRECTORY-BLOCK NACH REG.11
         LA    9,2(12)            A(MEMBER) NACH REG.9
M2       EQU   *
         CR    9,11               ENDE DIRECTORY-BLOCK ?
         BNL   READPDS            VERZWEIGEN BEI ENDE
         CLI   0(9),X'FF'         ENDE DIRECTORY ?
         BE    ENDPDS             VERZWEIGEN BEI ENDE
         MVC   0(8,6),0(9)        UEBERTRAGEN MEMBER-NAMEN
         LA    7,11(9)            ADRESSE VON C IN TTRC
         IC    7,0(7)             INHALT VON C IN TTRC
         SLL   7,27               LOESCHEN RECHTE BITS
         SRL   7,26               LAENGE DER USER-DATAS (TTRN'S)
         LA    9,12(7,9)          ADRESSE DES NAECHSTEN MEMBERS
         LA    15,0               LADEN RETURN-CODE 0
         B     RETURN             VERZWEIGEN ZUM RUECKSPRUNG
         EJECT
*
* SCHLIESSEN DER DIRECTORY
*
ENDPDS   EQU   *
         CLOSE PDSDCB
         SPACE
         NI    0(4),X'80'         SCHLIESSEN KENNZEICHNEN
         LA    15,4               LADEN RETURN-CODE 4
*
* RUECKSPRUNG
*
RETURN   EQU   *
         STM   9,12,0(5)          RETTEN REGISTER-STAENDE
         L     13,4(13)           LADEN A(SAVE-AREA DES RUF.PROGR.)
         L     14,12(13)          LADEN RUECKSPRUNGADRESSE
         LM    2,12,28(13)        WIEDERHERSTELLEN DER REGISTER-STAENDE
         L     0,20(13)           LADEN REG.0
         BR    14
         EJECT
*
* LITERALE
*
         LTORG
         SPACE
*
* DEFINITIONEN
*
EIN      DS    CL256              DIRECTORY-BLOCK
         EJECT
PDSDCB   DCB   DSORG=PS,DDNAME=PDSDCB,MACRF=GM,EODAD=ENDPDS,RECFM=U,   C
               BLKSIZE=264
         SPACE
         END   QX01443
