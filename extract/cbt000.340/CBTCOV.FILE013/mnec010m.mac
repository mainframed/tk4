         MACRO
&NAME    BEGIN &R1,&R2,&SPIE=,&IC=((1,15)),&PARM=,                     *
               &UREGS=,&USAVE=,&GENA=,&CHPEXIT=,&CHPWAIT=,&REGFF=YES
* NECKERMANN RECHENZENTRUM - IBM/370-VS - 15.10.69/22.03.76
         GBLB  &IXMMAKN       NEUE AUFL. "BEGIN" USW.
         GBLA  &REGUBAS,&REGURET
         GBLB  &REGSYMB,&REGUSCT,&REGUSAV,&EXTSPIE
         GBLB  &PRBGENA,&GNBGENA,&GNBUEND,&GNBUPGJ
         GBLB  &EQQ                11.09.72
         LCLA  &A,&A0,&A1(11),&A2(11),&A3,&AR
         LCLA  &AX1
         LCLB  &ERR,&PC,&ERRB,&ERRB2,&PRG,&FF
         LCLC  &CS            CSECT-NAME
         LCLC  &X             =&SYSNDX
         AIF   (&IXMMAKN).NEU
&NAME    @BEGIN &R1,&R2,SPIE=&SPIE,IC=&IC,PARM=&PARM,                  *
               UREGS=&UREGS,USAVE=&USAVE,GENA=&GENA,                   *
               CHPEXIT=&CHPEXIT,CHPWAIT=&CHPWAIT,REGFF=&REGFF
.*
         MEXIT
.*
.NEU     ANOP
.*
&EQQ     SETB  1                   11.09.72
&CS      SETC  '&NAME'
         AIF   ('&CS' NE '').XA010
&CS      SETC  '&SYSECT'
.XA010   ANOP
         AIF   (K'&CS EQ 8).XA020
&CS      SETC  '&CS'.(8-K'&CS)' '
.XA020   ANOP
&X       SETC  '&SYSNDX'
&AR      SETA  3
         AIF   (T'&GENA EQ 'O').A0
         MNOTE 08,'BEGIN   --- "GENA" UNGUELTIG MIT "MAKPARM"'
&ERR     SETB  1
.A0      ANOP
         AIF   (T'&R1 NE 'O').A1
&A1(1)   SETA  &AR
&A2(1)   SETA  &AR
         AIF   (T'&R2 EQ 'O').B
         MNOTE 0,'ERSTER OPERAND FEHLT, ZWEITER OPERAND WIRD IGNORIERT'
&ERRB    SETB  1
&ERRB2   SETB  1
         AGO   .B
.A1      ANOP
&A       SETA  1
.A10     AIF   (T'&R1(&A) NE 'N').A11
&A1(&A)  SETA  &R1(&A)
         AIF   (&A1(&A) GE &AR AND &A1(&A) LE 12).A12
.A11     MNOTE 0,'ERSTER OPERAND UNGUELTIG, REGISTER &AR ANGENOMMEN'
&ERRB    SETB  1
         AGO   .A2
.A12     AIF   (&A EQ 1).A14
&A0      SETA  &A-1
.A13     AIF   (&A1(&A) EQ &A1(&A0)).A11
&A0      SETA  &A0-1
         AIF   (&A0 GE 1).A13
.A14     AIF   (&A EQ N'&R1).A2
&A       SETA  &A+1
         AGO   .A10
.A2      ANOP
&A2(1)   SETA  &A1(N'&R1)
         AIF   (T'&R2 EQ 'O').B
&A       SETA  1
.A20     AIF   (T'&R2(&A) NE 'N').A21
&A2(&A)  SETA  &R2(&A)
         AIF   (&A2(&A) GE &AR AND &A2(&A) LE 12).A22
.A21     MNOTE 0,'ZWEITER OPERAND UNGUELTIG, WIRD IGNORIERT'
&ERRB2   SETB  1
         AGO   .B
.A22     AIF   (&A EQ 1).A24
&A0      SETA  &A-1
.A23     AIF   (&A2(&A) EQ &A2(&A0)).A21
&A0      SETA  &A0-1
         AIF   (&A0 GE 1).A23
.A24     AIF   (&A EQ N'&R2).A3
&A       SETA  &A+1
         AGO   .A20
.A3      AIF   (&ERR).B
         AIF   (&A1(N'&R1) LE &A2(1)).B
         MNOTE 0,'ANGEGEBENE BASIS-REGISTER UNGUELTIG'
         MNOTE *,'REGISTER &AR ALS BASIS-REGISTER ANGENOMMEN'
&ERRB    SETB  1
&ERRB2   SETB  1
.B       AIF   (NOT &ERRB).B1
&A1(1)   SETA  &AR
.B1      AIF   (T'&SPIE EQ 'O').C
         AIF   ('&SPIE' NE 'YES').C
&PC      SETB  1
.C       AIF   (T'&UREGS EQ 'O').C4
         AIF   (NOT &REGUSCT).C1
         MNOTE 0,'UREGS BEREITS AUFGESETZT, WIRD IGNORIERT'
         AGO   .C4
.C1      AIF   ('&UREGS'(1,1) NE '(' OR '&UREGS'(K'&UREGS,1) NE ')').C2
         AIF   (N'&UREGS EQ 2).C3
.C2      MNOTE 0,'UREGS=&UREGS UNGUELTIG, WIRD IGNORIERT'
         AGO   .C4
.C3      AIF   (T'&UREGS(1) EQ 'O' OR T'&UREGS(2) EQ 'O').C2
         AIF   (T'&UREGS(1) NE 'N' OR T'&UREGS(2) NE 'N').C2
       AIF  (&UREGS(1) LT &AR OR &UREGS(1) EQ 13 OR &UREGS(1) GT 15).C2
       AIF  (&UREGS(2) LT &AR OR &UREGS(2) EQ 13 OR &UREGS(2) GT 14).C2
         AIF   (&UREGS(1) EQ &UREGS(2)).C2
&REGUSCT SETB  1
&REGUBAS SETA  &UREGS(1)
&REGURET SETA  &UREGS(2)
.C4      AIF   (T'&USAVE EQ 'O').C5
&REGUSAV SETB  1
         AIF   ('&USAVE' EQ 'YES').C5
         MNOTE 0,'USAVE=&USAVE UNGUELTIG, ''YES'' ANGENOMMEN'
.C5      AIF   (T'&PARM EQ 'O').C7
         AIF   ('&PARM' EQ 'YES').C7
         MNOTE  0,'PARM=&PARM UNGUELTIG, ''YES'' ANGENOMMEN'
.C7      ANOP
         AIF   ('&REGFF' EQ 'NO').C8
         AIF   ('&REGFF' EQ 'YES').C7FF1
         MNOTE 0,'REGFF=&REGFF FALSCH, ''YES'' ANGENOMMEN'
.C7FF1   ANOP
&FF      SETB  1
.C8      ANOP
         SPACE
         AIF   (NOT &REGUSCT AND NOT &REGUSAV).D0C
         AIF   (NOT &REGUSCT).D0A
         MNOTE *,'***   UREGS   BASE-REG = &REGUBAS'
         MNOTE *,'***   UREGS   LINK-REG = &REGURET'
.D0A     AIF   (NOT &REGUSAV).D0B
         MNOTE *,'***   USAVE = YES'
.D0B     SPACE
.D0C     ANOP
         AIF   (&ERR).D8
         AIF   (T'&NAME EQ 'O').D1
&NAME    CSECT
.D1      USING *,R15
IXM&X.S  B     IXM&X.B
         DC    AL1(IXM&SYSNDX.A-*-1),CL31'&CS   &SYSDATE   &SYSTIME'
IXM&X.A  EQU   *
IXM&X.C  DC    A(IXMSAVEA)
IXMTSOSW DC    XL4'0'
IXM&X.B  DS    0H
         STM   R14,R12,12(R13)
         L     R14,IXM&SYSNDX.C
         ST    R13,4(0,R14)
         ST    R14,8(0,R13)
         LR    R13,R14
         USING IXMSAVEA,R13
         DROP  R15
         AIF   (NOT &FF).D3FF
         SR    R2,R2
         BCTR  R2,0
         LR    R3,R2
         LR    R4,R2
         LR    R5,R2
         LR    R6,R2
         LR    R7,R2
         LR    R8,R2
         LR    R9,R2
         LR    R10,R2
         LR    R11,R2
         LR    R12,R2
.D3FF    LR    R&A1(1),R15
         USING IXM&SYSNDX.S,R&A1(1)
         AIF   (&ERRB).D5
         AIF   (T'&R1 EQ 'O').D5
         AIF   (N'&R1 EQ 1 AND T'&R2 EQ 'O').D5
         AIF   (N'&R1 EQ 1 AND &ERRB2).D5
         LA    R14,2048
         AIF   (N'&R1 EQ 1).D4
&A0      SETA  1
.D31     LA    R&A1(&A0+1),2048(R14,R&A1(&A0))
&AX1     SETA  &AX1+1
         USING IXM&SYSNDX.S+&AX1*4096,R&A1(&A0+1)
&A0      SETA  &A0+1
         AIF   (&A0 LT N'&R1).D31
.D4      AIF   (T'&R2 EQ 'O').D5
         AIF   (&ERRB2).D5
&A0      SETA  &A1(N'&R1)
.D40     AIF   (&A0 GE &A2(1)).D41
&A3      SETA  &A0+1
         LA    R&A3,2048(R14,R&A0)
&AX1     SETA  &AX1+1
         USING IXM&SYSNDX.S+&AX1*4096,R&A3
&A0      SETA  &A3
         AGO   .D40
.D41     AIF   (N'&R2 EQ 1).D5
&A0      SETA  1
.D42     LA    R&A2(&A0+1),2048(R14,R&A2(&A0))
&AX1     SETA  &AX1+1
         USING IXM&SYSNDX.S+&AX1*4096,R&A2(&A0+1)
&A0      SETA  &A0+1
         AIF   (&A0 LT N'&R2).D42
.D5      ANOP
         NOP   IXM&SYSNDX.E
         OI    *-3,X'F0'
*        EXTRACT IXMTSOSW,FIELDS=TSO
         EXTRACT IXMTSOSW,FIELDS=TSO
*        ENDE EXTRACT
         L     R14,IXMTSOSW
         MVC   IXMTSOSW,0(R14)
IXM&X.E  DS    0H
         AIF   (T'&SPIE EQ 'O').D7
         TM    IXMTSOSW,X'80'
         BNZ   IXM&X.F
         AIF   (&PC).D6
         MNOTE '*        SPIE  &SPIE,&IC'
         SPIE  &SPIE,&IC
*        ENDE SPIE
         AGO   .D7X
.D6      ANOP
         MNOTE '*        SPIE  EXITSPIE,&IC'
         SPIE  EXITSPIE,&IC
*        ENDE SPIE
         AIF   (&EXTSPIE).D7X
&EXTSPIE SETB  1
         EXTRN EXITSPIE
.D7X     ANOP
IXM&X.F  DS    0H
.D7      ANOP                                                19.03.76
         L     R1,4(R13)                                     22.03.76
         LM    R14,R1,12(R1)                                 19.03.76
         AIF   (T'&PARM EQ 'O').D8                           19.03.76
         L     R1,0(0,R1)
         LH    R15,0(0,R1)
         LTR   R14,R15
         BNZ   *+10
         BAL   R1,*+12
         DC    C'  '
         LA    R1,2(0,R1)
         BCTR  R14,0
.D8      AIF   (&REGSYMB).D9
&REGSYMB SETB  1
*
R0       EQU   X'0'     REGISTER  0
R1       EQU   X'1'     REGISTER  1
R2       EQU   X'2'     REGISTER  2
R3       EQU   X'3'     REGISTER  3
R4       EQU   X'4'     REGISTER  4
R5       EQU   X'5'     REGISTER  5
R6       EQU   X'6'     REGISTER  6
R7       EQU   X'7'     REGISTER  7
R8       EQU   X'8'     REGISTER  8
R9       EQU   X'9'     REGISTER  9
R10      EQU   X'A'     REGISTER 10
R11      EQU   X'B'     REGISTER 11
R12      EQU   X'C'     REGISTER 12
R13      EQU   X'D'     REGISTER 13
R14      EQU   X'E'     REGISTER 14
R15      EQU   X'F'     REGISTER 15
*
.D9      ANOP
.E       MEND
