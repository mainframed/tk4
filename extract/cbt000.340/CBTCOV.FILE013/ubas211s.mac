* STAND 05/20/81
***********************************************************************
*                                                                     *
*  PROGRAMM    = ABSTROOT                                             *
*                                                                     *
*  ZWECK       =STEUERT DEN ABLAUF DER VERSCHIEDENEN                  *
*                ABSTIMMFUNKTIONEN                                    *
         AENTRY
***********************************************************************
ABSTROOT CSECT
         PRINT NOGEN
         XSAVE 12,SAVE,ABSTROOT
         AREG
*              V O R S P A N N
*              ---------------
         USING DATAB,R11
         LA    R11,ATAB
*  KZCALL UNTERSUCHEN
         L     R2,0(R1)                --> KZCALL
         LH    R2,0(R2)                R2 = KZCALL
         STC   R2,KZLAUF               RECHTES BYTE STEUERT PGM-ABLAUF
         SRL   R2,8
         STC   R2,ASPRACHE             LINKES BYTE ZEIGT PROG.SPRACHE
         L     R2,4(R1)
         MVC   REG1,0(R2)              --> AUFRUFLISTE SPEICHERN
*
*  VERSION VON RZASTBL PRUEFEN
*
         TM    8(R1),X'80'
         BO    V05
*    3.ARGUMENT FEHLT --> VERSION 0
         OI    V85+1,X'F0'    COMPCODE-DRUCKEN UEBERGEHEN
         B     START
*
V05      EQU   *
         L     R2,8(R1)       --> VERSION
         CLI   0(R2),X'01'
         BNE   ENDE           UNGUELTIGE VERSION
*
*              H A U P T T E I L
*              -----------------
*
START    EQU   *
         CLI   ASPRACHE,3     AUFRUF ASTTXT
         BE    ASTTXT
         CLI   ASPRACHE,4     AUFRUF ASTTXTOP
         BNE   TESTPARM
*
*    AUFRUF A S T T X T
*     NUR VOR AUFRUF AKALESEN ERLAUBT
*    -----------------
ASTTXT   EQU   *
         LA    R15,8          CONDCODE FALLS AUFRUF NACH AKALESEN
         TM    PARMOK,SAOK
         BO    ENDE           AKALESEN SCHON AUFGERUFEN
         LA    R3,ASPRACHE
         CALL  TXTPARM,(REG1,(R3))
         LTR   R15,R15
         BNZ   ENDE           RETURNCODE VON ASTTXT
         SR    R15,R15
         CLC   ATXTKZ,=H'0'
         BE    ENDE           KEIN CONDITION CODE
         MVC   COND,CONDDEF   CONDITION CODE DEFAULT
         B     ENDE
*
TESTPARM EQU   *
         TM    KZLAUF,X'01'
         BZ    TESTCP                  PARAMETER NICHT UMSPEICHERN
*
*              COBAPARM + PL1APARM
*              -------------------
         TM    PARMOK,SAOK
         BNO   CALLP                   PARAMETER NICHT UMGESPEICHERT
         MVC   ERR,=F'12'              WIEDERHOLTER AUFRUF VON PARMM.
         CALL  DRUFEHL,(ERR),VL        FEHLERMELDUNG DRUCKEN
         MVC   RETCODE,=H'88'
         B     RETURN
CALLP    EQU   *
* ATAB INITIALISIEREN
         L     R2,KTABADR              --> KTAB
         LA    R3,BKTAB-4(R2)          --> KTAB LETZTES WORT
         L     R4,STABADR              --> STAB
         LA    R5,STABXX-4             --> STAB LETZTES WORT
         STM   R2,R5,PKTAB
         MVI   LDCBSW,DCBCLOSE         ABSTBLATT NOT OPENED
         MVI   AUPDTE,SUYES            UPDATE GENERELL ERLAUBT
         MVI   ADCBSW,DCBCLOSE         DD-KARTE ASTSUM VORHANDEN
         XC    RETCODE,RETCODE         RETURNCODE = 0
         MVI   AERGI,SANO              GENERELL NICHT INT.ABGESTIMMT
         MVI   AERGE,SANO              GENERELL NICHT EXT.ABGESTIMMT
         MVI   ALCOPY,RNO     NICHT LISTENKOPIE
         MVI   AREPEAT,RLEER           WIEDERHOL.NOCH NICHT FESTGESTELL
         MVC   APGM,=CL33' '           FELDER LOESCHEN
         MVC   AUEB,=CL33' '           FELDER LOESCHEN
         MVC   AASTC,=CL33' '          FELDER LOESCHEN
         MVC   ASBZT,=CL33' '
         LA    R3,ASBZT       ZUSAETZLICHE BERICHTSZEIT AUS 4.
         CALL  ACCTPARM,(H4,H8,(R3))  ACCOUNTINGPARAMETER HOLEN
         MVI   ACCT,0         KEINE ACCOUNTING ANALYSIERT
         XC    ASWITCH,ASWITCH
         CALL  PARMMOVE,(ERR,REG1)
         CLC   ERR,=F'0'
         BE    CALLLS                  KEIN FEHLER
FEHLERP  EQU   *
         MVC   RETCODE,=H'88'          RETURNCODE = 88
         ABEND 888,DUMP                ABSTIMMABBRUCH
CALLLS   EQU   *
         OI    PARMOK,SAOK             PARAMETER RICHTIG UEBERGEBEN
         CALL  AKALESEN                ABSTIMMKARTEN LESEN
         B     *+4(R15)
         B     CALLLSE                 KEIN FEHLER
         B     CALLLSF        FEHLER IN ASTPARM
         MVC   COND,=H'24'    ASTPARM OHNE DATEN
         B     TESTCP
CALLLSF  EQU   *
         LA    R2,2           PARAMETERKARTENFEHLER
         BAL   R10,CONDSET
         MVI   AERGI,SAIMPOS  GENERELLES ABSTIMM=
         MVI   AERGE,SAIMPOS  ERGEBNIS FALSCH
         MVC   RETCODE,=H'22'          RETURNCODE = 22
CALLLSE  EQU   *
         CALL  PRUEFUPD
         TM    KZLAUF,X'FE'  SIND WEITERE FUNKTIONEN DURCHZUFUEHREN
         BZ    RETURN                  NEIN --> RUECKSPRUNG
*  FALLS AUFRUF UEBER CALL COBYPL1AST, MUSS FUER VERGLEICH UND UPDATE
*  NEUE ARGUMENTENLISTE AUFGEBAUT WERDEN MIT ARGTYP=0
         XC    REG1,REG1               --> ARGUMENTENLISTE
TESTCP   EQU   *
         CLC   COND,=H'24'
         BE    RETURN         ASTPARM OHNE DATEN
         TM    PARMOK,SAOK
         BO    TESTCP1                 PARAMETER RICHTIG UEBERGEBEN
         MVC   ERR,=F'12'              PARAMETER NICHT RICHTIG UEBERG.
         CALL  DRUFEHL,(ERR),VL        FEHLERMELDUNG DRUCKEN
         B     FEHLERP                 AUFRUFFEHLER
TESTCP1  EQU   *
         TM    KZLAUF,X'02'
         BZ    TESTDR     VERGLEICH SOLL NICHT DURCHGEFUEHRT WERDEN
*
*              COBACOMP + PL1ACOMP
*              -------------------
         CALL  AVERGL,(ERR,REG1)       ABSTIMMABGLEICH DURCHFUEHREN
         TM    AERGI,SAFALSE
         BNO   CALLCP5                 KEIN INT.FEHLER
         LA    R2,3           INTERNE ABST. FALSCH
         BAL   R10,CONDSET
         MVC   RETCODE,=H'66'          INTERNER ABST.FEHLER
CALLCP5  EQU   *
         TM    AERGE,SAFALSE+SAIMPOS
         BZ    CALLCPE
         LA    R2,4           EXTERNE ABST. FALSCH
         BAL   R10,CONDSET
         CLI   KZLAUF,X'FF'
         BE    CALLCPE                 AUFRUF  COB/PL1AST
         MVC   RETCODE,=H'77'          EXT.ABST.FEHLER ODER
*                                      NICHT ALLE SUMMEN VERGLICHEN
CALLCPE  EQU   *
         TM    KZLAUF,X'FC'  SIND WEITERE FUNKTIONEN DURCHZUFUEHREN
         BZ    RETURN                  NEIN --> RUECKSPRUNG
TESTDR   EQU   *
         TM    KZLAUF,X'04'
         BZ    TESTUD   DRUCKEN DER LISTE NICHT VERLANGT
*
*              COBALIST + PL1ALIST
*              -------------------
         CALL  DRUALIST,(ERR)          ABSTIMMLISTE DRUCKEN
         CLC   ERR,=F'0'
         BE    CALLDRE                 KEIN FEHLER
         LA    R2,5           DDKARTE ABSTBLATT FEHLT
         BAL   R10,CONDSET
         MVC   RETCODE,=H'33'          RETURNCODE = 33
CALLDRE  EQU   *
         TM    AERGI,SAODER   ODER-BEDINGUNG
         BNZ   CALLDRE1       JA
         TM    AERGE,SAODER
         BZ    CALLDRE2       NEIN
CALLDRE1 EQU   *
         SR    R2,R2          ODER-BEDINGUNG
         BAL   R10,CONDSET
CALLDRE2 EQU   *
         TM    KZLAUF,X'F8'  SIND WEITERE FUNKTIONEN DURCHZUFUEHREN
         BZ    RETURN                  NEIN --> RUECKSPRUNG
TESTUD   EQU   *
         TM    KZLAUF,X'08'
         BZ    RETURN   UPDATE DER ABST-SU-DATEI NICHT VERLANGT
*
*              COBAUPDT + PL1UPDT
*              ------------------
         CALL  UPDATE,(ERR,REG1)       UPDATE AUF ABST-SU-DATEI
         CLC   ERR,=F'0'
         BE    TESTKP                  KEIN FEHLER
FEHLERU  EQU   *
         LA    R2,6           UODATE NICHT ERLAUBT
         BAL   R10,CONDSET
         CLI   KZLAUF,X'FF'
         BE    TESTKP                  AUFRUF  COB/PL1AST
         MVC   RETCODE,=H'44'          RETURNCODE = 44
*
TESTKP   EQU   *
*
*  INHALT DER DATEI MIT DDNAME = PARMKART LISTEN
         CALL  DRUPARM,(ERR)  PARMKART LISTEN
*
*  OP023 AUFRUFEN, WENN L-KARTE IN ASTPARM UND ROUTINELAUF
*
         TM    ASWITCH,LKARTE+LCALL
         BNO   V80
         CALL  ASTOP023
         STH   R15,CONDTAB+14
         LA    R2,7           OP023
         BAL   R10,CONDSET
V80      EQU   *
*
*  TAPECLEAR AUFRUFEN FUER BANDFILES
*
         TM    ASWITCH,CCALL
         BNO   A900
         CALL  ASTCLRTP
         SPACE 1
*  CONDITION CODE SETZEN BEI VSAM-FEHLER
         SPACE 1
A900     EQU   *
         CLI   VSAMKZ,0
         BE    V85            KEIN FEHLER
         LA    R2,9
         BAL   R10,CONDSET
*
*  CONDITION CODE AUS ASTBLATT DRUCKEN
*
V85      NOP   V90            BRANCH, WENN VERSION 0 RZASTBL
         LH    R2,COND
         LTR   R2,R2
         BZ    V88            COND = 0
*  URSACHE DES CONDCODE LISTEN
         MVI   CREASON,C'('
         MVI   CREASON+17,C')'
         LH    R3,CONDIX
         SLA   R3,4           *16
         LA    R3,CONDTXT(R3)
         MVC   CREASON+1(16),0(R3)
V88      EQU   *
         CVD   R2,DWORD
         UNPK  CONDDR,DWORD
         OI    CONDDR+3,X'F0'
         MVC   ERR,=F'15'
         CALL  DRUFEHL,(ERR,ZEILE),VL  CONDITION CODE DRUCKEN
         SPACE 1
*  CONDITIONCODE IN DATEI AN NAECHSTEN STEP WEITERGEBEN
         OPEN  (ASTCOND,(OUTPUT))
         TM    ASTCOND+48,X'10'
         BZ    V90            DATEI FEHLT
         L     R1,16          -->CVT
         L     R1,0(R1)
         L     R1,4(,R1)      -->TCB
         L     R1,12(,R1)     -->TIOT
         MVC   CONDJOB,0(R1)
         TIME
         ST    R1,CONDDAT
         SPACE 1
         PUT   ASTCOND,CONDSATZ
         CLOSE ASTCOND
         SPACE 1
V90      EQU   *
         CLC   ALCNR,=CL33' ' LISTENNR FUER KOPIE
         BE    RETURN         NICHT VORHANDEN
*
*  KOPIE DER ABSTIMMSUMMENLISTE MIT LISTENNR ERSTELLEN
*
         MVI   ALCOPY,RYES
         L     R3,=V(JFCBD)
         MVI   0(R3),X'FF'    INITIALISIEREN
         MVI   LDCBSW,DCBCLOSE
         CALL  DRUALIST,(ERR)
*
*              R U E C K S P R U N G
*              ---------------------
RETURN   EQU   *
         L     R2,PRETURN              --> RETURNCODE
         MVC   0(2,R2),RETCODE         RETURNCODE FUER AUFRUFENDES PGM
         LH    R15,COND       CONDITION CODE
ENDE     EQU   *
         XRETURN (R15)
*
*  CONDITIONCODE SETZEN
*
CONDSET  EQU   *   R2 = BEDINGUNG
         LR    R0,R2          RETTEN
         SLA   R2,1
         LA    R2,CONDTAB(R2) -->COND INDIZIERT MIT BEDINGUNG
         LH    R2,0(R2)       CONDITION CODE
         CH    R2,COND
         BNH   CONDSET9
         STH   R0,CONDIX
         STH   R2,COND        COND=MAX(COND,CONDTAB)
CONDSET9 EQU   *
         BR    R10            RUECKSPRUNG
*
         PRINT GEN
         MATAB
         MKTAB
         MSTAB
ABSTROOT CSECT
*              D E F I N I T I O N E N
*              -----------------------
DWORD    DS    D
REG1     DS    A   ZWI-SP.FUER ADR.D.ARGUMENTLIST.D.AUFRUF.PGM
PARGTYP  DC    A(ARGTYP)
ERR      DS    F                       FEHLERWORT
RETCODE  DS    H                       ZWISCHENSPEICHER RETURNCODE
CONDSATZ DS    0F
CONDJOB  DS    CL8
CONDDAT  DS    CL4
COND     DC    H'0'           CONDITION CODE
         DC    CL6' '
CONDIX   DC    H'8'           URSACHE DES CONDITION CODE
CONDDEF  DC    H'16'          CONDITION CODE DEFAULT
CONDTAB  DC    H'08'          0  ODER-BEDINGUNG
         DC    H'20'          1  AUFRUF FALSCH
         DC    H'16'          2  PARAMETERKARTEN FEHLER
         DC    H'16'          3  INT.ABST.FALSCH
         DC    H'16'          4  EXT.ABST.FALSCH
         DC    H'20'          5  DDKARTE ASTBLATT FEHLT
         DC    H'00'          6  EINSPEICHERN NICHT ERLAUBT
         DC    H'0'           RETURNCODE OP023
         DC    H'00'          8
         DC    H'16'          9  VSAM FEHLER
CONDTXT  DS    0CL16
         DC    CL16'ODER-BEDINGUNG'     0
         DC    CL16'AUFRUF FALSCH'      1
         DC    CL16'FEHLER ASTPARM'     2
         DC    CL16'INT.ABST.FALSCH'    3
         DC    CL16'EXT.ABST.FALSCH'    4
         DC    CL16' '                  5
         DC    CL16'EINSP.NICHT ERLAUB' 6
         DC    CL16'RETCODE OP023'      7
         DC    CL16'VON ASTTXT   '      8
         DC    CL16'VSAM FEHLER'     8
         SPACE 1
         ENTRY VSAMKZ
VSAMKZ   DC    XL1'0'         KEIN VSAM FEHLER
KZLAUF   DS    CL1                     KENNUNG FUER DURCHZUFUEHR.AKTIO
PARMOK   DC    X'00'                   = 1 -->PARAMETER RICHTIG UEBERG
ARGTYP   DC    CL1'0'
ASTCOND  DCB   DDNAME=ASTCOND,DSORG=PS,MACRF=(PM),                     *
               RECFM=FB,BLKSIZE=20,LRECL=20
KTABADR  DC    A(KTAB)
         ENTRY STABADR
STABADR  DC    A(STAB)
H4       DC    H'4'
H8       DC    H'8'
ZEILE    DC    CL35'RZASTBL GEENDET MIT CONDITION CODE'
CONDDR   DS    CL4
         DC    CL2' '
CREASON  DC    CL18' '
         DC    CL22' '
         LTORG
         ENTRY ATAB
*              TABELLEN
         DC    CL4'ATAB'
ATAB     DS    0F                      ATAB-
         DS    (LATAB)CL1              BEREICH
         ORG   ATAB
TOFF     EQU   ATXTP-DATAB
         DS    (TOFF)CL1
         DC    A(0)           ATXTP =  KEINE TEXTE UEBERGEBEN
         ORG
         DC    CL4'KTAB'
BKTAB    EQU   (05*LKTABK+05*LKTABF+10*LKTABS)   GESAMTLAENGE KTAB
KTAB     DS    0F                                KTAB-
         DS    CL(BKTAB)                         BEREICH
BSTAB    EQU   (10*LSTAB+10*LSETAB)              GESAMTLAENGE STAB
         DC    CL4'STAB'
STAB     DS    0F                                STAB-
         DS    CL(BSTAB)                         BEREICH
STABXX   DS    0C
*
*
         END
