*/T0518XLI JOB (S,23556),34.HOFFM,CLASS=Z
*/XDALIST EXEC AFC
*/SYSIN    DD  *
XDALIST  TITLE 'ANDRUCKEN VON DATEIEN ANHAND VON DD-NAMEN.'
         PRINT NOGEN
XDALIST  CSECT
         ENTRY YLIST                        S M S  REQUIREMENT
YLIST    NULL
         SPACE 3
*        PROGRAMM : XDALIST
*        AUTHOR   : HARTMUT HOFFMANN
*                   C/O PREUSSAG AG
*                   ABT. SYSTEMPROGRAMMIERUNG
*                   LEIBNIZUFER 9
*                   3000 HANNOVER 1
*        SPRACHE  : ASSEMBLER
*        DATUM    : 15.04.77
*        VERSION  : 01
         SPACE 3
*        P R O G R A M M B E S C H R E I B U N G
*        ---------------------------------------
*
*
*        DAS UNTER PROGRAMM XDALIST WIRD VOM -XDATA- DYNAMISCH
*        GELADEN, WENN EIN DD-NAME FUER EINE DRUCKDATEI GEFUNDEN WIRD.
*
*        ES KOENNEN DATENSAETZE IN 5 VERSCHIEDENEN AUFBEREITUNGS-
*        FORMATEN ANGEDRUCKT WERDEN.
*
*              DIE DD-NAMEN LAUTEN:  CHAR
*                                    HEXA
*                                    DUMP
*                                    DATA
*                                    DITTO
*
*        WIRD KEINES DIESER DD-STATEMENTS GEFUNDEN, HAENGT DAS
*        UNTERPROGRAMM SICH SELBSTAENDIG AB.
*        ALLE AKTIVITAETEN FUER DEN DRUCK WERDEN GESTOPPT.
*
*        AUFRUF:    PARM1 = SATZADRESSE  (BEI EODAD NULL-ADR.)
*                   PARM2 = SATZLAENGE
         EJECT
B10001   NULL                               PROGRAMM-ANFANG
         PROST RA,,REG                      INITIALISIERUNG
         LM    R3,R7,0(R1)                  PARAMETER-LISTE LADEN
         TM    0(R1),X'80'                  NUR EIN PARAMETER..
         BZ    B10010                        NEIN
         MVC   HDRADR,0(R1)                 ADRESSE UEBERSCHRIFTEN
         SP    ZZMAX,=P'4'                  ZZMAX VERMINDERN
         B     B49900                       RETURN
B10010   NULL
         L     R4,0(,R4)                    SATZLAENGE
         NOP   B21001                       1. DURCHLAUF
         OI    *-3,240                       AUSSCHALTEN
         L     R1,16(0,0)                   CVT-ADR
         L     R1,00(,R1)                   TCB-WORDS
         L     R1,00(,R1)                   NEW-TCB
         L     R1,12(,R1)                   TIOT-ADR
         LA    R1,24(,R1)                   DD-ENTRY
         SR    R0,R0                        LOESCHEN
B10050   NULL
         LA    RE,DDNAMTAB                  ADR DD-NAMEN-TABELLE
         LA    RF,DDNAMANZ                  ANZAHL PLAETZE
         AR    R1,R0                        NAECHSTER PLATZ
         L     R0,0(,R1)                    LAENGE
         LTR   R0,R0                        TIOT ENDE..
         BZ    B10070                        JA, NICHT GEFUNDEN
B10060   NULL
         CLC   4(8,R1),0(RE)                GESUCHTER DD-NAME..
         BE    B10100                        JA
         LA    RE,12(,RE)                   NAECHSTER PLATZ
         BCT   RF,B10060                    NEUER VERSUCH
         SRL   R0,24                        LAENGE NACH LOW-ORDER-BYTE
         B     B10050                       NOCHMAL VON VORNE
B10070   NULL                               KEIN DD-NAME GEFUNDEN
         L     RF,04(,RD)                   HSA-ADR
         L     RF,16(,RF)                   ENTRY-POINT
         MVC   0(4,RF),DUMMY                MODUL ABHAENGEN
         B     B49900                       RETURN
*
DUMMY    SR    RF,RF
         BR    RE
         EJECT
B10100   NULL
         MVC   ENDKON+9(5),0(RE)            DDNAME NACH SCHLUSSMELDUNG
         MVC   DDNAMTAB(12),0(RE)           AUF 1. TABELLENPLATZ
         MVC   PRINT+40(8),0(RE)            DDNAME NACH DCB
         OPEN  (PRINT,OUTPUT)               PRINT DATEI EROEFFNEN
         SR    R2,R2                        LOESCHEN
         IC    R2,BRDISP                    BRANCH DISPLACEMENT
         B     *(R2)                        BRANCH NACH INIT
         B     B10200                       INIT CHAR  PRINT
         B     B10220                       INIT HEXA  PRINT
         B     B10240                       INIT DUMP  PRINT
         B     B10260                       INIT DATA  PRINT
         B     B10280                       INIT DITTO PRINT
         SPACE 3
B10200   NULL                               CHAR
         LA    R0,10                        ANZAHL 10ER BLOECKE
         BAL   R9,B62001                    UEBERSCHRIFT AUFBEREITEN
         B     B21001                       VERARBEITUNG
         SPACE 3
B10220   NULL                               HEXA
         LA    R0,5                         ANZAHL 10ER BLOECKE
         BAL   R9,B62100                    UEBERSCHRIFT AUFBEREITEN
         B     B21001                       VERAEBEITUNGSTEIL
         SPACE 3
B10240   NULL                               DUMP
         LA    R0,3                         ANZAHL 10ER BLOECKE
         BAL   R9,B62100                    UEBERSCHRIFT HEXA-TEIL
         LA    RE,UEB+86                    ANFANG CHAR-TEIL
         LA    R0,3                         ANZAHL 10ER BLOECKE
         BAL   R9,B62010                    UEBERSCHRIFT CHAR-TEIL
         B     B21001                       VERARBEITUNGS-TEIL
         SPACE 3
B10260   NULL                               DATA
         MVI   TABTR1+C'.',C' '             BLANK STATT PUNKT
         TR    TABTR1(256),TABTR1           TABELLE AENDERN
         B     B10220                       UEBERSCHRIFT WIE HEXA
         SPACE 3
B10280   NULL                               DITTO
         B     B10200                       UEBERSCHRIFT WIE CHAR
         EJECT
B21001   NULL                               DRUCKSATZ AUFBEREITEN
         LTR   R3,R3                        LETZTER AUFRUF..
         BZ    B49001                        JA, ENDE
         AP    SATZNR,P1                    SATZZAEHLER + 1
         MVC   DB(8),SCHAB                  SATZNR. AUFBEREITEN
         ED    DB(8),SATZNR                 SATZZAEHLER
         ZAP   ZAEVON,P1                    VON STELLE
         ZAP   ZAEBIS,PKDLN                 DRUCKLAENGE GEPACKT
         CL    R4,PRINTLN                   DRUCKLAENGE VON AUSSEN..
         BL    *+8                           NEIN
         L     R4,PRINTLN                   EXTERNE DRUCKLAENGE
         CVD   R4,WKBER                     GEPACKTES FORMAT
         ZAP   SATZLN,WKBER+4(4)            SATZLAENGE SPEICHERN
         BNP   B21900                       SL < 1, KEIN DRUCK
         MVI   DB,C'0'                      VORSCHUB
         AP    ZZ,P1                        ZEILENZAEHLER + 1
         SR    R6,R6                        LOESCHEN
         IC    R6,BINLN                     DRUCKLAENGE BINAER
B21020   NULL
         CLR   R4,R6                        LAENGE > DRUCKLN..
         BH    B21030                        JA
         ZAP   ZAEBIS,SATZLN                RESTLAENGE
         LR    R6,R4                        LAENGE NACH R6
B21030   NULL
         UNPK  DB+09(4),ZAEVON+1(3)         VON NACH DB
         CP    ZAEVON,=P'999'               BEI SATZPOSITION > 999
         BH    B21040                        WIRD DIE 4. STELLE
         MVI   DB+09,C' '                     MITGEDRUCKT
B21040   NULL
         UNPK  DB+14(3),ZAEBIS+2(2)         BIS NACH DB
         OI    DB+12,240                    VORZEICHEN
         OI    DB+16,240                     AUF 'F'
         MVI   DB+13,C'-'                   VERBINDUNG
         SR    R2,R2                        LOESCHEN
         IC    R2,BRDISP                    BRANCH DISPLACEMENT
         B     *(R2)                        NACH AUFBEREITEN
         B     B21100                       CHAR
         B     B21200                       HEXA
         B     B21300                       DUMP
         B     B21400                       DATA
         B     B21500                       DITTO
         EJECT
B21100   NULL                               CHAR
         BAL   R9,B62200                    CHAR AUFBEREITEN
         B     B21900                       DRUCKEN
         SPACE 3
B21200   NULL                               HEXA
         BAL   R9,B62300                    HEXA IN WKBER AUFBER.
         BAL   R9,B62400                    HEXA NACH DB
         B     B21900                       DRUCKEN
         SPACE 3
B21300   NULL                               DUMP
         BAL   R9,B62300                    HEXA AUFBEREITEN
         BAL   R9,B62400                    HEXA NACH DB
         MVI   DB+85,C'*'                   BEGRENZER
         LA    RE,DB+86                     ANFANG CHAR TEIL
         BAL   R9,B62210                    CHAR AUFBEREITEN
         TR    DB+86(30),TABTR1             UNDRUCKBARE ZEICHEN = '.'
         LA    R1,DB+86(R6)                 BEGRENZER
         MVI   0(R1),C'*'                    SETZEN
         B     B21900                       DRUCKEN
         SPACE 3
B21400   NULL                               DATA
         LA    R1,2                         ZEICHEN-INDEX
         LA    R2,1                         ZEICHEN-LAENGE
         LA    RE,DBTXT+1                   ANFANG IN DB
         LR    RF,R3                        SATZPOINTER NACH RF
         BAL   R9,B62510                    CHAR-TEIL AUFBEREITEN
         TR    DBTXT(104),TABTR1            UNDRUCKBARE ZEICHEN = ' '
         BAL   R9,B61001                    DRUCKEN
         B     B21200                       HEXA AUFBEREITEN
         SPACE 3
B21500   NULL                               DITTO
         BAL   R9,B62200                    CHAR-TEIL AUFBEREITEN
         BAL   R9,B61001                    DRUCKEN
         BAL   R9,B62300                    HEXA NACH WKBER
         LA    R1,1                         ZEICHEN-INDEX
         LA    R2,2                         ZEICHEN-LAENGE
         LA    RF,WKBER                     HEXA-ZONE NACH DB
         BAL   R9,B62500                    DB AUFBEREITEN
         BAL   R9,B61001
         LA    R1,1                         ZEICHEN-INDEX
         LA    R2,2                         ZEICHEN-LAENGE
         LA    RF,WKBER+1                   ANFANG
         BAL   R9,B62500                    HEXA-ZIFFER NACH DB
         B     B21900                       DRUCKEN
         SPACE 3
B21900   NULL                               FORTSETZUNG
         BAL   R9,B61001                    DRUCKEN
         SR    R4,R6                        SATZLN - PRINTLN
         BNP   B49900                        NULL = ENDE
         AR    R3,R6                        SATZPOINTER NEU EINSTELLEN
         AP    ZAEVON,PKDLN                 PRINTLN GEPACKT
         AP    ZAEBIS,PKDLN                  ERHOEHEN
         B     B21020                       WEITERMACHEN
         EJECT
B49001   NULL                               SCHLUSSROUTINE
         ZAP   SATZNR,SATZNR                EINGABE VORHANDEN..
         BZ    B49100                        NEIN
         MVC   DB(L'ENDKON),ENDKON          ENDE DRUCK
         MVI   UEB,C'0'                     VORSCHUB
         BAL   R9,B61030                    DRUCKEN
B49100   NULL
         CLOSE PRINT                        DRUCK-DATEI SCHLIESSEN
B49900   NULL
         PEND  RC=0                         RETURN MIT RC = 0
         EJECT
B61001   NULL                               DRUCK-ROUTINE
         CP    ZZ,ZZMAX                     SEITE VOLL..
         BL    B61100                        NEIN
         AP    SZ,P1                        SEITENZAEHLER + 1
         CP    SZ,P1                        1. SEITE..
         BE    B61010                        JA
         MVI   UEB,C'0'                     VORSCHUB
         PUT   PRINT,UEB                    ORIENTIERUNGSLEISTE UNTEN
B61010   NULL
         MVI   UEB,C'1'                     VORSCHUB
         L     R2,HDRADR                    ADRESSE UEBERSCHRIFTEN
         LTR   R2,R2                        SIND UEBERSCHRIFTEN DA..
         BZ    B61020                        NEIN
         PUT   PRINT,(R2)                   1. UEBERSCHRIFT
         LA    R2,133(,R2)
         PUT   PRINT,(R2)                   2. UEBERSCHRIFT
         MVI   UEB,C'0'                     VORSCHUB
B61020   NULL
         MVC   UEB+123(6),=C' SEITE'        TEXT
         MVC   UEB+129(4),SCHAB             SCHABLONE
         ED    UEB+129(4),SZ                SEITENZAHL
         ZAP   ZZ,=P'4'                     ANFANGSSTELLUNG
         MVC   DB(8),SCHAB                  SCHABLONE
         ED    DB(8),SATZNR                 SATZ-NUMMER
B61030   NULL
         PUT   PRINT,UEB                    UEBERSCHRIFT DRUCKEN
         MVC   UEB+124(9),UEB+123           TEXT LOESCHEN
         MVI   DB,C'0'                      VORSCHUB
         SPACE 3
B61100   NULL
         PUT   PRINT,DB                     EINZELZEILE
         AP    ZZ,P1                        ZZ + 1
         MVC   DB,DB-1                      LOESCHEN
         BR    R9                           RETURN
         EJECT
B62001   NULL                               CHAR UEBERSCHRIFT
         LA    RE,UEBTXT                    ANFANG
B62010   NULL
         MVC   0(10,RE),MARK1               MARKIERUNG
         LA    RE,10(,RE)                    ZUWEISEN
         BCT   R0,B62010                    WEITERMACHEN
         BR    R9                           RETURN
         SPACE 3
B62100   NULL                               HEXA-UEBERSCHRIFT
         LA    RE,UEBTXT                    ANFANG
B62110   NULL
         MVC   0(20,RE),MARK2               MARKIERUNG
         LA    RE,21(,RE)                    ZUWEISEN
         BCT   R0,B62110                    WEITERMACHEN
         BR    R9                           RETURN
         SPACE 3
B62200   NULL
         LA    RE,DBTXT                     ANFANG
B62210   NULL
         LR    R0,R6                        PRINTLN
         BCTR  R0,0                          - 1
         STC   R0,*+5                       NACH MVC
         MVC   0(*-*,RE),0(R3)              SATZ NACH DB
         BR    R9                           RETURN
         SPACE 3
B62300   NULL                               HEXA NACH WKBER
         LA    R0,7                         MAXLN
         LR    R1,R6                        SATZLAENGE
         LA    RE,WKBER                     WORK-BEREICH
         LR    RF,R3                        SATZ-POINTER
B62310   NULL
         CLR   R1,R0                        SATZLN > MAXLN..
         BNH   B62320                        NEIN
         UNPK  0(15,RE),0(8,RF)             8 BYTES ENTPACKEN
         LA    RE,14(,RE)                   WK-POINTER +14
         LA    RF,07(,RF)                   SATZ-POINTER + 7
         SR    R1,R0                        LN - MAXLN
         B     B62310                       WEITERMACHEN
B62320   NULL
         LR    R0,R1                        RESTLN
         SLL   R0,05                         * 2 UND SHIFT NACH ZONE
         OR    R0,R1                        LAENGEN VERKNUEPFEN
         BCTR  R1,0                         RESTLN - 1
         STC   R1,*+5                       NACH MVC
         MVC   WKBER+201(*-*),0(RF)         REST NACH WKBER
         STC   R0,*+5                       NACH UNPK
         UNPK  0(*-*,RE),WKBER+201(*-*)     REST ENTPACKEN
         LA    R0,0(R6,R6)                  LAENGE * 2
         BCTR  R0,00                        LN - 1
         STC   R0,*+5                       NACH TR
         TR    WKBER(*-*),TABTR2            UEBERSETZEN
         BR    R9                           RETURN
         SPACE 3
B62400   NULL                               HEXA NACH DB
         LA    R0,10                        BLOCKLAENGE
         LR    R1,R6                        DRUCKLAENGE
         LA    RE,DBTXT                     DB ANFANG
         LA    RF,WKBER                     WORK-BEREICH
B62410   NULL
         CLR   R1,R0                        DRUCKLN > BLOCKLN..
         BNH   B62420                        NEIN
         MVC   0(20,RE),0(RF)               WKBER NACH DB
         LA    RE,21(,RE)                   DB-POINTER
         LA    RF,20(,RF)                   WK-POINTER
         SR    R1,R0                        LAENGE
         B     B62410                       WEITERMACHEN
B62420   NULL
         SLL   R1,1                         DRUCKLN * 2
         BCTR  R1,0                         DRUCKLN - 1
         STC   R1,*+5                       NACH MVC
         MVC   0(*-*,RE),0(RF)              REST ZUWEISEN
         BR    R9                           RETURN
         SPACE 3
B62500   NULL                               BYTE-WEISE UEBERTRAGEN
         LA    RE,DBTXT                     ANFANG
B62510   NULL
         LR    R0,R6                        DRUCKLN
         LA    R7,10                        LAENGE BEREICH
B62520   NULL
         MVC   0(1,RE),0(RF)                BYTE NACH DB
         LA    RE,0(R1,RE)                  RE ERHOEHEN
         LA    RF,0(R2,RF)                  RF ERHOEHEN
         BCT   R7,B62530                    WEITERMACHEN
         LA    R7,10                        MAXLN
         LA    RE,2(,RE)                    RE + 2
         SR    RE,R2                        RE - R2
B62530   NULL
         BCT   R0,B62520                    WEITER ZUWEISEN
         BR    R9                           RETURN
         EJECT
B71001   NULL                               DCB-EXIT
         CLC   PRINT+62(2),BLKSIZE          BLOCKSIZE = 0..
         BE    B71010                        JA, STANDARD
         LH    R0,PRINT+62                  BLOCKSIZE LADEN
         ST    R0,PRINTLN                    UND SPEICHERN
B71010   NULL
         MVC   PRINT+62(2),BLKSIZE+2        BLOCKSIZE = 1330
         BR    RE                           RETURN NACH OPEN
         EJECT
*        DEFINITIONEN
*
WKBER    DC    26D'0'                       WORK-BEREICH 208 BYTES
PRINTLN  DC    F'9999999'                   DRUCKLAENGE EXTERN
EXLIST   DC    X'85',AL3(B71001)            DCB-EXIT
BLKSIZE  DC    F'1330'                      BLOCKSIZE PRINT
HDRADR   DC    F'0'                         ADRESSE KOPFZEILEN
DDNAMTAB DS    0F                           TABELLE DD-NAMEN
         DC    CL8'CHAR    ',PL2'100',AL1(100),AL1(04)
         DC    CL8'HEXA    ',PL2'050',AL1(050),AL1(08)
         DC    CL8'DUMP    ',PL2'030',AL1(030),AL1(12)
         DC    CL8'DATA    ',PL2'050',AL1(050),AL1(16)
         DC    CL8'DITTO   ',PL2'100',AL1(100),AL1(20)
DDNAMLN  EQU   12                           ELEMENT-LN
DDNAMANZ EQU   (*-DDNAMTAB)/DDNAMLN         ANZAHL PLAETZE
         ORG   DDNAMTAB
DDNAME   DS    CL8                          DD-NAME
PKDLN    DS    CL2                          DRUCKLN GEPACKT
BINLN    DS    CL1                          DRUCKLN BINAER
BRDISP   DS    CL1                          BRANCH-DISPLACEMENT
         ORG   DDNAMTAB+(DDNAMANZ*DDNAMLN)  FREIER PLATZ
TABTR1   DC    256CL1'.'                    UEBERSETZUNGS-TABELLE
         ORG   TABTR1+C' '
         DC    CL1' '                       BLANK
         ORG   TABTR1+C'A'
         DC    CL9'ABCDEFGHI'
         ORG   TABTR1+C'J'
         DC    CL9'JKLMNOPQR'
         ORG   TABTR1+C'S'
         DC    CL8'STUVWXYZ'
         ORG   TABTR1+C'0'
         DC    CL10'0123456789'
         ORG   TABTR1+16
TABTR2   DS    CL240
         DC    CL16'0123456789ABCDEF'       TRANSLATE HEXA
         EJECT
MARK1    DC    CL10'1234567890'             CHAR-MARKIERUNG
MARK2    DC    CL20'1 2 3 4 5 6 7 8 9 0'    HEXA-MARKIERUNG
ENDKON   DC    C'0** ENDE XXXXX ANDRUCK **'
SATZNR   DC    PL4'0'                       SATZZAEHLER
SATZLN   DC    PL4'0'                       SATZLAENGE
ZAEVON   DC    PL4'0'                       ZAEHLER VON
ZAEBIS   DC    PL4'0'                       ZAEHLER BIS
*
*
UEB      DS    0CL133                       UEBERSCHRIFT
         DC    CL19' SATZ-NR  VON BIS'
UEBTXT   DC    CL114' '
*
         DC    C' '                         LOESCH-BYTE
DB       DS    0CL133                       DRUCKBEREICH
         DC    CL19' '
DBTXT    DC    CL114' '
SZ       DC    PL2'0'                       SEITENZAEHLER
ZZ       DC    PL2'99'                      ZEILENZAEHLER
ZZMAX    DC    PL2'66'                      MAX-ZEILEN
P1       DC    PL1'1'                       KONSTANTE
SCHAB    DC    X'4020202020202120'          SCHABLONE
*********  PRINT-DCB  ************************************************
*
PRINT    DCB   DDNAME=PRINT,DSORG=PS,MACRF=PM,EXLST=EXLIST,            X
               RECFM=FBA,LRECL=133
**********************************************************************
         LTORG
         END   XDALIST
