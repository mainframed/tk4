 TITLE 'CANCWAIT - SAMPLE COMMUNICATIONS TASK USER EXIT FOR MESSAGES   X
               IEF690I AND IEF238D, TESTED DOENCH'
***********************************************************************
*                                                                     *
* MODULE NAME         =  CANCWAIT                                     *
*                                                                     *
* DESCRIPTIVE NAME    =  SAMPLE COMMUNICATIONS TASK USER EXIT         *
*                        FOR MESSAGES IEF690I AND IEF238D.            *
*                                                                     *
* FUNCTION            =  ROUTES MESSAGE IEF690I VIA ROUTING CODE 11,  *
*                        AND SUPPRESSES AND REPLIES TO MESSAGE        *
*                        IEF238D.                                     *
*                                                                     *
*   OPERATION         =  DETERMINES WHICH MESSAGE IS BEING PROCESSED. *
*                        IF THE MESSAGE IS IEF690I, THIS EXIT ROUTINE *
*                        REQUESTS TO CHANGE THE ROUTING CODES TO BE   *
*                        ROUTING CODE 11 ONLY.                        *
*                                                                     *
*                        IF THE MESSAGE IS IEF238D, THIS EXIT ROUTINE *
*                        REQUESTS TO SUPPRESS THE WTOR AND THEN       *
*                        REPLIES TO IT VIA THE MGCR MACRO, INDICATING *
*                        THAT THE JOB SHOULD BE CANCELLED.            *
*                                                                     *
*                        A MESSAGE IS THEN ISSUED INDICATING THAT THE *
*                        JOB BEING CANCELLED.                         *
*                                                                     *
* ENTRY POINT         =  CANCWAIT                                     *
*                                                                     *
*   PURPOSE           =  TO PROCESS MESSAGES IEF690I AND IEF238D      *
*                                                                     *
*   LINKAGE           =  BALR                                         *
*                                                                     *
*   INPUT DATA        =  REG 1 POINTS TO THE ADDRESS OF THE CTXT      *
*                        REG 13 ADDRESS OF STANDARD SAVE AREA         *
*                        REG15 ENTRY POINT                            *
*                                                                     *
*   REGISTERS SAVED   =  REG0 - REG15                                 *
*                                                                     *
*   REGISTER USAGE    =  REG5  - POINTER TO THE ADDRESS OF THE CTXT   *
*                        REG11 - MODULE DATA REGISTER                 *
*                        REG12 - MODULE BASE REGISTER                 *
*                        REG13 - POINTER TO A STANDARD SAVE AREA      *
*                        REG14 - RETURN POINT                         *
*                                                                     *
*   REGISTERS RESTORED = REG0 - REG15                                 *
*                                                                     *
*   CONTROL BLOCKS    =                                               *
*     NAME     MAPPING MACRO   REASON USED                  USAGE     *
*     ----     -------------   -----------                 -------    *
*     CTXT      IEZVX100        USER EXIT PARAMETER LIST     R,W      *
*     MGCR      IEZMGCR         SVC 34 PARAMETER LIST        C,D      *
*                                                                     *
*   KEY = R-READ, W-WRITE, C-CREATE, D-DELETE                         *
*                                                                     *
*   TABLES            =  NONE                                         *
*                                                                     *
*   MACROS            =  GETMAIN, FREEMAIN, MGCR, WTO                 *
*                                                                     *
***********************************************************************
         EJECT
CANCWAIT CSECT
CANCWAIT AMODE 31                      31-BIT ADDRESSING MODE
CANCWAIT RMODE ANY                     31-BIT RESIDENCE
         SPACE 1
***********************************************************************
*                                                                     *
*         REGISTER ASSIGNMENTS                                        *
*                                                                     *
***********************************************************************
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
CTXTPTR  EQU   5
REG11    EQU   11
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
SPINPRVT EQU   230
         EJECT
***********************************************************************
*                                                                     *
*        STANDARD ENTRY LINKAGE                                       *
*                                                                     *
***********************************************************************
         STM   REG14,REG12,12(REG13)    SAVE CALLER'S REGISTERS
         BALR  REG12,REG0               ESTABLISH MODULE BASE
         USING *,REG12                  REGISTER
         L     CTXTPTR,0(REG1)          ESTABLISH ADDRESSABILITY
         USING CTXT,CTXTPTR             TO THE CTXT
***********************************************************************
*                                                                     *
*        DYNAMIC STORAGE FOR THIS MODULE IS BEING OBTAINED BELOW      *
*        THE 16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR PARAMETER   *
*        LIST TO BE IN 24-BIT ADDRESSABLE STORAGE.                    *
*                                                                     *
***********************************************************************
         GETMAIN  RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW  OBTAIN DYNAMIC  X
                                                       STORAGE
         LR    REG11,REG1               ADDRESS RETURNED IN REG1
         USING DATAAREA,REG11           ADDRESSABILITY TO DYNAMIC      X
                                        STORAGE
         ST    REG13,SAVEAREA+4         SET BACKWARD PTR
         LA    REG15,SAVEAREA           GET ADDRESS OF SAVE AREA
         ST    REG15,8(REG13)           SET FORWARD PTR
         LR    REG13,REG15              SET REG13 TO POINT TO          X
                                        STANDARD SAVE AREA
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE WHICH MESSAGE IS TO BE PROCESSED.  IEF690I OR      *
*        IEF238D?                                                     *
*                                                                     *
***********************************************************************
         SPACE 1
         L     REG2,CTXTTXPJ            ESTABLISH ADDRESSABILITY
         USING CTXTATTR,REG2            TO THE MSG ATTRIBUTES
         LA    REG4,CTXTTMSG            ADDRESS OF TEXT AREA
         USING MSGTEXT,REG4             BASE TEXT MAPPING
         CLC   MSGID,IEF690I            MESSAGE IEF690I?
         BNE   MSG235D                  NO, THEN MESSAGE IEF238D
***********************************************************************
*                                                                     *
*        WORKING WITH MESSAGE IEF690I THEREFORE REQUEST TO CHANGE     *
*        THE ROUTING CODES.                                           *
*                                                                     *
***********************************************************************
         OI    CTXTRFB1,CTXTRCRC        REQUEST TO CHANGE              X
                                        ROUTING CODES
         L     REG3,CTXTRCP             ESTABLISH ADDRESSABILITY
         USING CTXTROUT,REG3            TO THE ROUTING CODES
         XC    CTXTROUT(2),CTXTROUT     ZERO THE ROUTING CODES
         OI    CTXTR002,CTXTR11         QUEUE TO ROUTING CODE 11
         DROP  REG3                     DROP ADDRESSABILITY TO         X
                                        THE ROUTING CODES
         DROP  REG2,REG4                DROP ADDRESSABILITY TO         X
                                        THE MESSAGE ATTRIBUTES
         B     FINISHED                 EXIT MODULE
***********************************************************************
*                                                                     *
*        WORKING WITH MESSAGE IEF238D THEREFORE REPLY TO THE          *
*        MESSAGE AND ISSUE ANOTHER STATING A JOB IS BEING CANCELLED   *
*                                                                     *
***********************************************************************
MSG235D  EQU   *
         OI    CTXTRFB2,CTXTRHCO        SUPPRESS THE MESSAGE
         BAL   REG14,ISSUMGCR           REPLY TO IEF238D 'R XX,NO'
         BAL   REG14,ISSUWTO            ISSUE THE MESSAGE
***********************************************************************
*                                                                     *
*        STANDARD EXIT LINKAGE                                        *
*                                                                     *
***********************************************************************
FINISHED EQU   *
         L     REG13,4(REG13)           RESTORE REG 13
         FREEMAIN  RU,LV=DATAEND,A=(REG11),SP=SPINPRVT  FREE STORAGE
         LM    REG14,REG12,12(REG13)    RESTORE CALLER'S               X
                                        REGISTERS
         BR    REG14                    RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*   PROCEDURE - ISSUMGCR                                              *
*                                                                     *
*   FUNCTION  - REPLIES TO MESSAGE IEF238D WITH A NEGATIVE RESPONSE   *
*                                                                     *
*   INPUT     - NONE                                                  *
*                                                                     *
*   OUTPUT    - A REPLY COMMAND IS ISSUED VIA SVC 34                  *
*                                                                     *
*   NOTES     - THE MGCR MACRO MAY BE USED TO ISSUE A REPLY COMMAND   *
*                                                                     *
***********************************************************************
         SPACE 1
ISSUMGCR EQU   *
         XC    MGCRPL(MGCRLTH),MGCRPL   CLEAR THE PARAMETER LIST
         MVC   MGCRTEXT(L'TXTINSRT),TXTINSRT   MOVE IN THE REPLY BUFFER
         MVC   REPLY,CTXTRPID           INSERT THE REPLY ID
         LA    REG1,(MGCRTEXT-MGCRPL)+L'TXTINSRT      GET MGCRPL LENGTH
         STC   REG1,MGCRLGTH            SAVE LENGTH IN THE MGCRPL
         SR    REG0,REG0                CLEAR REGISTER ZERO
         MGCR  MGCRPL                   ISSUE THE COMMAND
         BR    REG14                    RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*   PROCEDURE - ISSUWTO                                               *
*                                                                     *
*   FUNCTION  - ISSUES A MESSAGE INFORMING OPERATOR THAT A JOB IS
*               BEING CANCELLED                                       *
*                                                                     *
*   INPUT     - NONE                                                  *
*                                                                     *
*   OUTPUT    - MESSAGE STATING JOB CANCELLED                         *
*                                                                     *
***********************************************************************
         SPACE 1
ISSUWTO  EQU   *
         L     REG2,CTXTTXPJ            ESTABLISH ADDRESSABILITY
         USING CTXTATTR,REG2            TO MESSAGE ATTRIBUTES
         LA    REG4,CTXTTMSG            ADDRESS OF TEXT AREA
         USING MSGTEXT,REG4             BASE TEXT MAPPING
         MVC   USERAUTO,USERSTAT        MOVE WTO LIST FORM FROM        X
                                        STATIC TO DYNAMIC
         MVC   USERAUTO+24(8),JOBDATA   OBTAIN THE JOB DATA
         DROP  REG2,REG4                DROP ADDRESSABILITY TO         X
                                        THE MESSAGE ATTRIBUTES
         WTO   MF=(E,USERAUTO)          ISSUE THE MESSAGE
         BR    REG14                    RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        CHARACTER CONSTANTS                                          *
*                                                                     *
***********************************************************************
IEF690I  DC    CL8'IEF690I '            MESSAGE IEF690I
TXTINSRT DC    CL15'REPLY XX,CANCEL'    WTOR REPLY
         SPACE 1
***********************************************************************
*                                                                     *
*        LIST FORM OF WTO MACRO (STATIC)                              *
*                                                                     *
***********************************************************************
USERSTAT WTO 'USER001I CANCELLING ???????? - WAITING FOR VOLUMES',     X
               ROUTCDE=(11),DESC=(6),MF=L
CNCLMSG  EQU   *-USERSTAT
         EJECT
***********************************************************************
*                                                                     *
*        STORAGE DEFINITIONS                                          *
*                                                                     *
***********************************************************************
DATAAREA DSECT
         DS    0F
SAVEAREA DS    18F                      STANDARD SAVE AREA
         DS    0F
USERAUTO DS    CL(CNCLMSG)              DYNAMIC FORM OF WTO
         EJECT
MGCR     IEZMGCR  DSECT=NO
         ORG   MGCRTEXT
COMMAND  DS    CL6                      THE REPLY COMMAND
REPLY    DS    CL2                      REPLY ID
REPLYMSG DS    CL3                      WTOR RESPONSE
         ORG
DATAEND  EQU   *-DATAAREA
         SPACE 2
***********************************************************************
*                                                                     *
*         MAPPING OF THE MESSAGE TEXT                                 *
*                                                                     *
***********************************************************************
MSGTEXT  DSECT
MSGID    DS    CL8                      MESSAGE ID
JOBDATA  DS    CL8                      JOB DATA (ID AND NAME)
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
         EJECT
         IEZVX100
         EJECT
         END   CANCWAIT
