SYS81    CSECT
         MBASIS R3
         MSPSN COUNT=10
         OPEN  (I1KART,(INPUT),O1KART,(OUTPUT))
A00      GET   I1KART,I1BER             LESEN KARTE
         MVC   ZORDNR,I1ORDNR           ORDNER UND
         LA    R10,I1BER+79
         LA    R2,L'I1TEXT
A05      CLI   0(R10),C' '              TEST BLANK
         BNE   A10
         BCTR  R10,0
         BCT   R2,A05
A10      LA    R11,I1TEXT
         LA    R10,1(R10)
A12      EQU   *
         CR    R11,R10                  TEST ENDE
         BH    A00                      JA
         BAL   R14,U05                  TEST UNGUELTIGES STICHWORT
         B     *+4(R15)
         B     A15                      OK
         B     A00                      SATZENDE
A15      LA    R14,ZSTICHW
A20      MVC   0(1,R14),0(R11)
         LA    R14,1(0,R14)
         LA    R11,1(0,R11)
         CLI   0(R11),C' '
         BE    A25
         BCT   R2,A20
         MVC   ZTEXT,I1TEXT             TEXT UEBERTRAGEN
         PUT   O1KART,ZBER
         MVC   ZSTICHW,=CL20' '
         B     A00
A25      MVC   ZTEXT,I1TEXT             TEXT UEBERTRAGEN
         PUT   O1KART,ZBER
         MVC   ZSTICHW,=CL20' '
A27      CLI   0(R11),C' '              TEST NAECHSTES ZEICHEN BLANK
         BNE   A12                      NEIN
         LA    R11,1(0,R11)             ADRESSE + 1
         CR    R11,R10                  TEST SATZENDE
         BH    A00                      JA
         BCT   R2,A27                   LOOP
         B     A00
A30      CLOSE (I1KART,,O1KART)
         MRETURN RC=0
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*        SUCHEN VON UNGUELTIGEN WORTEN                                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
U05      MUPEIN
U05A00   LA    R14,ZTEXTTAB
         LA    R15,17
         SR    R1,R1
U05A05   IC    R1,0(0,R14)
         EX    R1,ZEXVERGL              REST UNGUELTIGES STICHWORT
         BE    U05A10                   OK
         LA    R14,L'ZTEXTTAB(0,R14)    TABELLE DURCHSUCHEN
         BCT   R15,U05A05
         LA    R15,0                    RC=0
         B     U05A95                   NICHT GEFUNDEN
U05A10   LA    R15,0                    RC=0
         LA    R11,1(R1,R11)            ADRESSE AUF NEUS WORT
         SR    R2,R1                    LAENGE VERPRINGEN
         BP    U05A15                   POSTIV = OK
         LA    R15,4                    RC=4 ENDE
         B     U05A95
U05A15   CLI   0(R11),C' '              TEST BLANK
         BNE   U05A00                   NEIN
         LA    R11,1(0,R11)             WORTANFANG SUCHEN
         BCT   R2,U05A15
         LA    R15,4                    RC=4 NICHT GEFUNDEN
U05A95   MUPAUS U05                     RETURN
ZEXVERGL CLC   0(1,R11),1(R14)          VERGLEICH UNGUELT. STICHWORT
         SPACE 1
*        TEXT-TABELLE DER UNGUELTIGEN STICHWORTE
         DS    0F
ZTEXTTAB DS    0CL9
         DC    AL1(2),CL8'OF'
         DC    AL1(2),CL8'TO'
         DC    AL1(3),CL8'FOR'
         DC    AL1(3),CL8'AND'
         DC    AL1(3),CL8'THE'
         DC    AL1(3),CL8'IBM'
         DC    AL1(2),CL8'OS '
         DC    AL1(2),CL8'ON '
         DC    AL1(3),CL8'UND'
         DC    AL1(3),CL8'   '
         DC    AL1(3),CL8'   '
         DC    AL1(3),CL8'   '
         DC    AL1(3),CL8'   '
         DC    AL1(3),CL8'   '
         DC    AL1(3),CL8'   '
         DC    AL1(3),CL8'   '
         DC    AL1(3),CL8'   '
ZTEXTEND EQU   *
         LTORG
I1BER    DS    0CL80
I1ORDNR  DS    CL15
         DS    CL2
I1TEXT   DS    CL63
         DS    CL8
ZBER     DS    0CL110                   AUSGABE-BEREICH
ZORDNR   DS    CL15                     ORDNER-NK
         DS    CL2
ZSTICHW  DS    CL20                     STICHWORT
         DS    CL2
ZTEXT    DS    CL63                     TEXT
         DS    CL16
         PRINT NOGEN
I1KART   DCB   DDNAME=I1KART,DSORG=PS,MACRF=GM,EODAD=A30
O1KART   DCB   DDNAME=O1KART,DSORG=PS,MACRF=PM
         END
