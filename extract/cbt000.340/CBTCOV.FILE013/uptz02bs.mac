         MACRO
&NAME    MPLISTRT &BASIS=11
         LCLA  &A1,&ANZ,&SZ,&BREG(11)
         LCLB  &BZ(11)
         LCLC  &CR1,&CR2,&CR3,&R(10)
&A1      SETA  K'&BASIS
         AIF   (&A1 EQ 0).OHNE
&ANZ     SETA  N'&BASIS
         AIF   (&ANZ EQ 0).OHNE
         AIF   (&ANZ GT 16).DOPPELT
&SZ      SETA  1
.LOOP    ANOP
&A1      SETA  &BASIS(&SZ)
         AIF   (&A1 GT 11).FBASIS
         AIF   (&A1 LT 2).FBASIS
         AIF   (&A1 EQ 4).FBASIS
         AIF   (&BZ(&A1)).DOPPELT
&BZ(&A1) SETB  (1)
&BREG(&SZ) SETA &A1
&R(&SZ)  SETC  ','.'&A1'
         AIF   (&ANZ EQ &SZ).WEITER
&SZ      SETA  &SZ+1
         AGO   .LOOP
.FBASIS  MNOTE 'BASISREGISTER FALSCH'
.FEHLER  MNOTE 'KEINE GENERIERUNG'
         MEXIT
.DOPPELT MNOTE 'BASISREGISTER MEHRFACH'
         AGO   .FEHLER
.OHNE    ANOP
&BREG(1) SETA  11
&ANZ     SETA  1
.WEITER  ANOP
&NAME    START 0
         USING *,15
         B     *+12
         DC    AL1(7)
         DC    CL7'&NAME'
         STM   14,11,12(13)
         DROP  15
&CR1     SETC  '&BREG(1)'
         BALR  &CR1,0
         USING *&R(1)&R(2)&R(3)&R(4)&R(5)&R(6)&R(7)&R(8)&R(9)&R(10)
         ST    13,SAV&SYSNDX+4
         LA    4,SAV&SYSNDX
         LA    13,SAV&SYSNDX
         B     SAV&SYSNDX+4*20
SAV&SYSNDX DC  20F'0'
         AIF   (&ANZ EQ 1).ENDE
&CR3     SETC  '&BREG(&ANZ)'
         LA    &CR3,2048
&SZ      SETA  2
.GEN     AIF   (&BREG(&SZ) EQ 0).ENDE
&CR1     SETC  '&BREG(&SZ)'
&CR2     SETC  '&BREG(&SZ-1)'
         LA    &CR1,2048(&CR2,&CR3)
&SZ      SETA  &SZ+1
         AGO   .GEN
.ENDE    ANOP
         MEND
         MACRO
&NAME    MPLIEND
&NAME    L     13,4(13)
         LM    14,11,12(13)
         BR    14
         MEND
         TITLE 'MPLICS, KONSOLNACHRICHTEN VON PL/I-PROGRAMMEN BEI MCS'
         PRINT NOGEN
MPLICS   MPLISTRT ,                     VERBINDUNG ZU PL/I
*
*        DIESE ASSEMBLER-ROUTINE GIBT FUER PL/I KONSOLNACHRICHTEN BEI
*        MCS AUS. BEI EINGABE EINER ANTWORT WIRD DIESE AUF DEM
*        MSGCLASS-DATA-SET DOKUMENTIERT, WENN U. A. ROUTING CODE 11
*        ANGEGEBEN WAR.
*        DER AUFBAU UND INHALT DER ZU UEBERGEBENDEN STRUKTUR IST DER
*        DUMMY-SECTION PLI AM SCHLUSS DER LISTE ZU ENTNEHMEN.
         SPACE 2
*        REGISTER-BENUTZUNG
         SPACE
R0       EQU   0                  SYSTEM
R1       EQU   1                  SYSTEM, RECHENREGISTER
R2       EQU   2                  TEXTLAENGE - 1 BEI WTO
R3       EQU   3                  TEXTLAENGE + DESC-CODE + ROUTCDE
R4       EQU   4                  ANTWORTLAENGE
R5       EQU   5                  ADR. DER ANTWORT
R6       EQU   6                  FREI
R7       EQU   7                  FREI
R8       EQU   8                  FREI
R9       EQU   9                  FREI
R10      EQU   10                 BASISREGISTER FUER DSECT PLI
R11      EQU   11                 BASISREGISTER
R12      EQU   12                 RESERVIERT FUER PLI
R13      EQU   13                 ADR. DER SAVEAREA
R14      EQU   14                 RUECKSPRUNG-REGISTER
R15      EQU   15                 ADR.EINGANGSPUNKT
         EJECT
*        PROGRAMM
         SPACE
         USING PLI,R10            BASISREGISTER 10 FUER UEBERGABEBER.
         L     R10,0(R1)          LADEN ADRESSE UEBERGABEBEREICH
         ZAP   DWORT,TLANG        LAENGE TEXT IN RECHENFELD
         CVB   R2,DWORT           LAENGE TEXT BINAER IN REG 2
         LA    R3,4(R2)           LAENGE TEXT + LAENGE DC/ROUTCDE R 3
         SH    R2,=H'1'           LAENGE TEXT - 1 FUER MVC-LAENGE
         CP    BEFEHL,=P'2'       BEFEHL = 2 (WTOR) ?
         BE    MWTOR              JA, WTOR VERLANGT
         STH   R3,WTOL            SPEICHERN TEXTLAENGE + CODELAENGE
         EX    R2,MOVEWTO         UEBERTRAGEN TEXT LAENGENGERECHT
         LA    R1,WTOL(R3)        LADEN ADRESSE NACH TEXT
         XC    0(2,R1),0(R1)      DESCRIPTOR CODE = 0
         MVC   2(2,R1),TROUTCDE   UEBERTRAGEN ROUTING-CODE
         WTO   MF=(E,WTOL)        AUSGEBEN KONSOLNACHRICHT
         B     RETURN             ZURUECK INS PL/I-PROGRAMM
         SPACE 3
MWTOR    DS    0H
         LA    R5,TANTW           ADR. DER ANTWORT IN REG. 5
         ST    R5,WTORRL          SPEICHERN ADR. DER ANTWORT
         STH   R3,WTORL           SPEICHERN TEXTLAENGE + CODELAENGE
         ZAP   DWORT,RLANG        LAENGE DER ANTWORT IN RECHENFELD
         CVB   R4,DWORT           ANTWORTLAENGE BINAER IM REG. 4
         STC   R4,WTORRL          SPEICHERN ANTWORTLAENGE
         EX    R2,MOVEWTOR        UEBERTRAGEN TEXT LAENGENGERECHT
         LA    R1,WTORL(R3)       ADR. NACH TEXT IN REG. 1
         XC    0(2,R1),0(R1)      DESCRIPTOR-CODE = 0
         MVC   2(2,R1),TROUTCDE    ROUTING-CODE UEBERTRAGEN
         XC    WECB,WECB          LOESCHEN EVENT CONTROL BLOCK
         WTOR  ,,,,MF=(E,WTORRL)  AUSGEBEN TEXT
         WAIT  ECB=WECB           WARTEN AUF ANTWORT
         LA    R3,4(R4)           ANTWORTLAENGE + CODELAENGE IN R3
         SH    R4,=H'1'           ANTWORTLAENGE - 1 FUER MVC-LAENGE
         EX    R4,BLANKOC          KLEIN-BUCHSTABEN ZU GROSS-BUCHSTABEN
         TM    TROUTCDE+1,X'20'    WAR AUSGABE AUCH FUER MSG-DATASET?
         BZ    RETURN              NEIN, SOFORT ZURUECK
         WTO   '*SYS0003* OPERATOR-ANTWORT:',ROUTCDE=(11) KONST. TEXT
         STH   R3,WTOL            SPEICHERN ANTWORTLAENGE + CODELAENGE
         EX    R4,MOVERPLY        UEBERTRAGEN ANTWORT LAENGENGERECHT
         LA    R1,WTOL(R3)        ADR. NACH TEXT IN REG. 1
         XC    0(2,R1),0(R1)      DESCRIPTOR-CODE = 0
         MVC   2(2,R1),=B'0000000000100000'   ROUTING-CODE STETS (11)
         WTO   MF=(E,WTOL)        DOKUMENTIEREN ANTWORT AUS MSG-DATAS.
RETURN   DS    0H
         MPLIEND                        RUECKKEHR INS PL/I-PROGRAMM
         SPACE 3
MOVEWTO  MVC   WTOTEXT(1),TTEXT
MOVEWTOR MVC   WTORTEXT(1),TTEXT
MOVERPLY MVC   WTOTEXT(1),TANTW
BLANKOC  OC    TANTW(1),=72C' '    KLEIN-BUCHSTABEN ZU GROSS-BUCHSTABEN
         EJECT
*        DEFINITIONEN
         SPACE
         DS    0F                 WTO-LISTFORM
WTOL     DC    AL2(0)             TEXT LENGTH
         DC    X'8000'            MCS FLAGS
WTOTEXT  DS    CL76               MAXIMAL-TEXT + DESC-CODE + ROUTCDE
         SPACE
         DS    0F                 WTOR-LISTFORM
WTORRL   DC    A(0)               REPLY-LENGTH (1), REPLY-ADDRESS(2-4)
         DC    A(WECB)            ECB ADDRESS
WTORL    DC    AL2(0)             TEXT LENGTH
         DC    X'8000'            MCS-FLAGS
WTORTEXT DS    CL76               MAXIMAL-TEXT + DESC-CODE + ROUTCDE
         SPACE
DWORT    DS    D                  RECHENFELD FUER UMW. DEZIM. - BINAER
WECB     DS    F                  EVENT CONTROL BLOCK FUER WTOR
         LTORG
*        PL/I-UEBERGABEBEREICH
         SPACE
PLI      DSECT
BEFEHL   DS    CL1                1 = WTO, 2 = WTOR (GEPACKT)
TLANG    DS    CL2                LAENGE TEXTAUSGABE GEPACKT
TROUTCDE DS    CL2                ROUTING-CODE
TTEXT    DS    CL72               AUSGABETEXT
RLANG    DS    CL2                LAENGE DER ANTWORT GEPACKT
TANTW    DS    CL72               EINLESEBEREICH FUER DIE ANTWORT
         END
