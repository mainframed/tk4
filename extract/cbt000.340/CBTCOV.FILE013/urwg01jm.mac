         MACRO
&NAME    MSYNAD &ACSMETH,&INF1,&VERZWAD,&DCBNAME,&DECBNAM
         LCLC  &X
.* MSYNAD. IM DCB EINER DATEI MUSS EINE ROUTINE ANGEGEBEN WERDEN, IN
.*       DIE VERZWEIGT WIRD, WENN BESTIMMTE FEHLER BEI E/A-OPERATIONEN
.*       AUFTAUCHEN. EINE SOLCHE FEHLER-ROUTINE IST DAS VORLIEGENDE
.*       MACRO MSYNAD.
.* ANWENDUNG. IM DCB EINER DATEI-BESCHREIBUNG WIRD Z.B. ANGEGEBEN
.*
.* DATEI DCB   ...,SYNAD=F,...
.*
.*       AN ENTSPRECHENDER STELLE IM PROGRAMM WIRD DANN DAS MACRO AUF-
.*       GERUFEN, Z.B.
.*
.* F     MSYNAD ZUGRIFF,INFORM,VERZWAD,DCBNAME,DECBNAM
.*
.*       DIE PARAMETER BEDEUTEN IM EINZELNEN:
.*
.*       ZUGRIFF: HIER IST DIE ZUGRIFFS-METHODE ANZUGEBEN. DIESER PARA-
.*          METER MUSS ANGEGEBEN WERDEN. FOLGENDE SIND MOEGLICH:
.*                               BSAM
.*                               QSAM
.*                               BISAM
.*                               QISAM
.*                               BDAM
.*                               BPAM
.*
.*       INFORM: HIER IST ANZUGEBEN DER SYMBOLISCHE NAMEN DES INFOR-
.*          MATIONS-BYTES DES ENTSPRECHENDEN BESTANDS-MACROS. WIRD
.*          NICHT MIT BESTANDS-MACROS GEARBEITET, KANN AUCH EIN ANDERES
.*          BYTE ALS INFORMATIONS-BYTE ANGEGEBEN _ERDEN. IN DIESES
.*          BYTE WIRD VON DER SYNAD-ROUTINE ENTWEDER 'S','N','D' ODER
.*          ' ' GEBRACHT (SIEHE ARBEITSWEISE). DIESER PARAMETER MUSS
.*          ANGEGEBEN WERDEN.
.*
.*       VERZWAD: HIER IST ANZUGEBEN WOHIN AM ENDE DER SYNADROUTINE
.*          VERZWEIGT WERDEN SOLL. DER PARAMETER KANN WEGGELASSEN
.*          WERDEN. ES ERFOLGT DANN EIN RUECKSPRUNG HINTER DAS E/A-
.*          MACRO, DAS DEN FEHLER VERURSACHT HAT. WIRD Z.B. MRETURN
.*          ANGEGEBEN, SO WIRD DIE GEGENWAERTIGE CSECT VERLASSEN UND
.*          IN DIE NAECHSTHOEHERE CSECT ZURUECK-VERZWEIGT.
.*
.*       DCBNAME: DER NAME DES DCB DIESER DATEI IST HIER ANZUGEBEN.
.*          DIESER PARAMETER WIRD NUR BENOETIGT BEI DER ZUGRIFFS-METHO-
.*          DE QISAM.
.*
.*       DECBNAM: DIESER PARAMETER IST NUR ERFORDERLICH BEI DER ZU-
.*          GRIFFS-METHODE BISAM. ES IST DER NAME DES LIST-FORM READ/
.*          WRITE-MACROS
.*
.*
.*
.*
.* FUNKTIONSWEISE DES SYNAD - MACROS
.*
.*       DIESES MACRO UNTERSUCHT JE NACH DER SPEZIFIZIERTEN ZUGRIFFS-
.*       ROUTINE DIE EXCEPTION-CODE BITS IM DCB BEI QISAM BZW. IM DATA-
.*       EVENT-CONTROL-BLOCK BEI BISAM. REG 9 WIRD ZUR ADRESSIERUNG DER
.*       DUMMY-SECTION DCB BENUTZT. WIRD EINE UNGUELTIGE ANFORDERUNG
.*       FESTGESTELLT (Z.B. SCHLUESSEL NICHT VORHANDEN) SO WIRD IN DAS
.*       INFORMATIONSBYTE DER WERT 'N' GEBRACHT UND JE NACH ANGABE IM
.*       VERZWEIGUNGS-PARAMETER ZURUECK-VERZWEIGT (S. VERZWAD). WIRD
.*       BEIM HINZUFUEGEN EIN DOPPELTER SATZ FESTGESTELLT, SO BEKOMMT
.*       DAS INFORMATIONS-BYTE EIN 'D' UND DIE RUECKVERZWEIGUNG ERFOLGT
.*       ENTSPRECHEND DER PARAMETER-EINTRAGUNG. BEI QISAM WIRD NUR DANN
.*       NACH MSYNAD (BEI OVERFLOW-SAETZEN) VERZWEIGT, WENN NOCH ANDERE
.*       EXCEPTION-CODE-BITS AUF EIN SIND.
.*       BISAM VERZWEIGT GRUNDSAETZLICH IN DIE SYNAD-ROUTINE WENN OVER-
.*       FLOW-SAETZE VERARBEITET WERDEN. DER RUECKSPRUNG ERFOLGT JEDOCH
.*       GRUNDSAETZLICH UEBER R14, GLEICHGUELTIG OB EIN VERZWEIGUNGS-
.*       PARAMETER ANGEGEBEN WURDE ODER NICHT. R14 BEINHALTET JEWEILS
.*       DIE ADRESSE NACH DEM E/A-MACRO. DAS INFORMATIONS-BYTE BLEIBT
.*       BLANK.
.*       IN ALLEN ANDEREN FAELLEN WIRD 'S' INS INFORMATIONSBYTE GE-
.*       BRACHT, AUF DER KONSOL-SCHREIBMASCHINE EINE SYNADAF-FEHLER-
.*       MELDUNG GEDRUCKT UND ENTSPRECHEND ZURUECKVERZWEIGT.
.*
.*       BEI DER ZUGRIFFS-METHODE QISAM IST MIT DEM SYSTEM-MACRO DCBD
.*       EINE DUMMY-SECTION ZU BILDEN.
.*
&X       SETC  '&SYSNDX'
         AIF   ('&NAME' NE '').E2
         MNOTE 12,'***** NAME FEHLT. MAKRO IGNORIERT *****'
         MEXIT
.E2      AIF   ('&INF1' NE '').E4
         MNOTE 12,'***** INF-BYTE FEHLT. MAKRO IGNORIERT *****'
         MEXIT
.E4      AIF   ('&ACSMETH' NE 'QISAM').E6
         AIF   ('&DCBNAME' NE '').E6
         MNOTE 12,'***** DCB-NAME FEHLT. MAKRO IGNORIERT *****'
         MEXIT
.E6      AIF   ('&ACSMETH' NE 'BISAM').L2
         AIF   ('&DECBNAM' NE '').L2
         MNOTE 12,'***** DECB-NAME FEHLT. MAKRO IGNORIERT *****'
         MEXIT
.L2      ANOP
         EJECT
&NAME    CSECT
         USING &NAME,R15           REG 15 CONTAINS ENTRY-ADDRESS
         B     SY10&X              BRANCH AROUND IDENTIFIER
         DC    AL1(L'SY05&X)       LENGTH OF IDENTIFIER
SY05&X   DC    C'RWG-CSECT-SYNAD &NAME  &SYSTIME &SYSDATE'
SY10&X   DC    0H'0'
         STM   R10,R14,SY40&X      SAVE USER REGS
         LR    R10,R15             INIT NEW BASE-REGISTER
         DROP  R15                 RELEASE BASE-REGISTER
         USING &NAME,R10           ESTABLISH NEW BASE
         LA    R11,&INF1           LOAD ADDRESS OF INF-BYTE
         MVI   0(R11),C' '         CLEAR INF-BYTE
         AIF   ('&ACSMETH' EQ 'QISAM').QISAM
         AIF   ('&ACSMETH' EQ 'BISAM').BISAM
         AGO   .L4
.QISAM   ANOP
         L     R12,SY50&X          LOAD ADDRESS OF DCB
         USING IHADCB,R12          USE R12 FOR DCB-DSECT
         TM    DCBEXCD1,X'D2'      TEST EXCEPTION
         BM    SY20&X              GO, SET 'N' INTO INF-BYTE
         AGO   .L4
.BISAM   ANOP
         L     R12,SY55&X          LOAD ADDRESS OF DEB
         TM    24(R12),X'FD'       TEXT EXCEPTION
         BZ    SY35&X              NO EXCEPTION
         TM    24(R12),X'84'       INVALID REQUEST
         BNZ   SY20&X              GO, SET 'N' INTO INF-BYTE
         TM    24(R12),X'01'       DUPLICAT RECORD
         BO    SY25&X              GO, SET 'D' INTO INF-BYTE
         TM    24(R12),X'02'       OVERFLOW
         BO    SY30&X              GO, BRANCH AFTER I/O-REQUEST
.L4      ANOP
         SYNADAF ACSMETH=&ACSMETH  SYSTEM-MACRO FOR SYNAD-MSG
         MVC   SY15&X+8(78),50(R1) MOVE SYNADAF-MSG INTO WTO
SY15&X   WTO   '1...5...10...15...20...25...30...35...40...45...50...55*
               ...60...65...70...75...',ROUTCDE=(2,11)
         SYNADRLS ,                RELEASE MSG-BUFFER
         MVI   0(R11),C'S'         SET ERROR-IND TO ERROR OCCURED
         B     SY35&X              GO TO EXIT
         SPACE 1
         AIF   ('&ACSMETH' EQ 'QISAM').L6
         AIF   ('&ACSMETH' EQ 'BISAM').L6
         AGO   .L10
.L6      ANOP
SY20&X   MVI   0(R11),C'N'         SET ERROR-IND INVALID REQUEST
         B     SY35&X              GO TO EXIT
         SPACE 1
         AIF   ('&ACSMETH' NE 'BISAM').L10
SY25&X   MVI   0(R11),C'D'         SET ERROR-INT TO DUPLICAT RECORD
         B     SY35&X              GO TO EXIT
         SPACE 1
         AIF   ('&VERZWAD' EQ '').L8
SY30&X   LM    R10,R14,SY40&X      RESTORE USER REGS
         BR    R14                 BRANCH AFTER I/O-REQUEST
         AGO   .L10
.L8      ANOP
SY30&X   EQU   *                   ENTRY FOR BISAM OVERFLOW
.L10     AIF   ('&VERZWAD' EQ '').L12
SY35&X   L     R14,SY60&X          LOAD USER SPECIFIED RETURN ADDRESS
         LM    R10,R13,SY40&X      RESTORE USER REGS
         BR    R14                 BRANCH TO USER
         AGO   .L14
.L12     ANOP
SY35&X   LM    R10,R14,SY40&X      RESTORE USER REGS
         BR    R14                 BRANCH AFTER I/O-REQUEST
.L14     ANOP
         SPACE 3
SY40&X   DC    5F'0'               SAVE AREA FOR USER REGS
         AIF   ('&ACSMETH' NE 'QISAM').L16
SY50&X   DC    A(&DCBNAME)         DCB FOR QOISAM
.L16     AIF   ('&ACSMETH' NE 'BISAM').L18
SY55&X   DC    A(&DECBNAM)         DECB FOR BISAM
.L18     AIF   ('&VERZWAD' EQ '').L20
SY60&X   DC    A(&VERZWAD)         USER RETURN ADDRESS
.L20     ANOP
&SYSECT  CSECT
         EJECT
         MEND
