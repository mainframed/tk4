**********************************************************************
*                                                                    *
*     THE USE OF THIS PROGRAM IS TO PREVENT AN UPDATING OF AN        *
*                                                                    *
*     PO-DATA-SET BY MORE THAN ONE IEBUPDTE- OR IEBGENER-PROGRAMS    *
*                                                                    *
*     FROM 1 ORE MORE THAN 1 SYSTEM.                                 *
*                                                                    *
*     THIS PROGRAM MUST BE LINKED UNDER THE NAME IEBGENER IN AN      *
*                                                                    *
*     LOAD MODULE LIBRARY (FOR EXAMPLE THE SYS1.LINKLIB) AND YOU     *
*                                                                    *
*     MUST CHANGE THE NAME OF THE ORIGINAL IBM - UTILITY TO XXXGENER,*
*                                                                    *
*     BECAUSE IEBGENER WILL INVOKE THE ORIGINAL IBM - UTILITY        *
*                                                                    *
*     WITH THE NAME XXXGENER.                                        *
*                                                                    *
*     WHAT HAPPENS IF THIS PROGRAM FINDS AN PO-DATA-SET-NAME ON THE  *
*                                                                    *
*     SYSUT2 STATEMENT ?                                             *
*                                                                    *
*     IF THE PO-DATA-SET IS NOT ON A SHARED-DEVICE THIS PROGRAM      *
*                                                                    *
*     GIVES AN EXCLUSIVE ENQUEUE.                                    *
*                                                                    *
*     IF THE DATA-SET IS ON A SHARED-DEVICE THIS PROGRAM TESTS       *
*                                                                    *
*     WETHER THE PO-DATA-SET IS ON ONE OF THE VOLUMES, WHICH ARE     *
*                                                                    *
*     ALLOWED  TO BE SHARED. IN THIS CASE THE PROGRAM ISSUES THE     *
*                                                                    *
*     MACRO RESERVE FOR THE SHARED-DEVICE.                           *
*                                                                    *
**********************************************************************
         EJECT
RESGENER CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
***********************************************************
*    LINKAGE CONVENTION, SAVE ADDRESS OF PARAMETERLIST    *
***********************************************************
         SAVE  (14,12)
         BALR  RC,0               LOAD BASISREGISTER
         USING *,RC
         LR    RA,RD
         LA    RD,SA
         ST    RA,4(RD)           ADDRESS OF SAVEAREA BACKWARDS
         ST    RD,8(RA)           ADDRESS OF SAVEAREA FORWARD
         B     PARM1
SA       DS    18F
PARM1    EQU   *
         L     R1,0(0,R1)          ADDRESS OF ADDRESS OF PARMLIST
         ST    R1,PARMADR
         SR    RB,RB
         EXTRACT TIOTADDR,FIELDS=TIOT ADDRESS OF TIOT
         L     RA,TIOTADDR         A(TIOT) INTO RA
         LA    RA,24(,RA)          A(DDENTRY) IN TIOT
TIOTUT2  EQU   *
         CLI   0(RA),X'00'         LENGTH OF THIS ENTRY = 0
         BE    IEBGENER            YES, NO SYSUT2 ENTRY- NO ENQ,RESERVE
         CLC   4(8,RA),UT2DCB+40   DDNAME=SYSUT2?
         BE    RESUT2              YES
         IC    RB,0(RA)            LENGTH OF THIS ENTRY IN REG11
         LA    RA,0(RB,RA)         ADDRESS OF NEXT DDENTRY
         B     TIOTUT2
RESUT2   EQU   *
         MVC   FUCB+1(3),17(RA)
         L     RA,FUCB             RA=ADDRESS OF UCB
         MVC   UCBFIELD,0(RA)
         RDJFCB UT2DCB
         LTR   RF,RF
         BZ    RESUT20
         DC    F'0'
RESUT20  EQU   *
         TM    JFCB+87,X'80'       DISP = NEW/MOD
         BO    IEBGENER
         CLC   JFCB+44(8),BLANK    IS THERE A MEMBERNAME
         BNE   RESUT22             YES, THERE IS ONE
         OI    JFCB+52,X'01'
         NI    JFCB+75,X'7F'
         OPEN   (UT2DCB),TYPE=J
         CLOSE  (UT2DCB)
*  READ JOB FILE CONTROLBLOCK
         RDJFCB UT2DCB
         LTR   RF,RF
         BZ    RESUT21
         DC    F'0'
RESUT21  EQU   *
         TM    JFCB+98,X'02'     PO-DATEI ?
         BO    RESUT22
         CLC   JFCB+160(3),X0    DIRECTORY SPACE = PO-DATEI
         BNE   RESUT22
         B     IEBGENER
RESUT22  EQU   *
         TM    17(RA),X'20'      SHARED BIT IN UCB ON
         BZ    ENQUEUE           NO, IT IS OFF
TABVOL   EQU   *
         LA    R7,VOLNR          NUMBER OF SHARED VOLUMES
         LA    R9,TABVOLS        NAMES OF SHARED VOLUMES
TABVOL1  EQU   *
         CLC   JFCB+118(6),0(R9) SHARED VOLUME NAME?
         BE    RESERVE
         LA    R9,6(R9)
         BCT   R7,TABVOL1
         B     ENQUEUE
**********************************************************************
*                                                                    *
*  ENQ REQUEST CONTROL OF ONE SERIALLY REUSABLE RESOURCE TO THE TASK
*                                                                    *
**********************************************************************
ENQUEUE  ENQ   (QNAME,JFCB,E,44,SYSTEM),RET=HAVE
         LTR   RF,RF
         BZ    IEBGENRQ      YES
         DC    F'0'          NO, ABEND 0C1
**********************************************************************
*                                                                    *
*  RESERVE A DEVICE FOR USE BY A PARTICULAR SYSTEM
*                                                                    *
**********************************************************************
RESERVE  RESERVE (QNAME,JFCB,E,44,SYSTEMS),RET=HAVE,UCB=FUCB
         LTR   RF,RF         RETURN CODE
         BZ    IEBGENRQ      OK
         DC    F'0'          NO, ABEND 0C1
*  LINK UTILITY IEBGENER
IEBGENRQ OI    FLG,X'80'     ENQ,RESERVE
IEBGENER L     R2,PARMADR    ADR DER PARM-LISTE AUS EXEC KARTE LADEN
         LINK  EP=XXXGENER,PARAM=((2)) DO NOT CODE 'VL=1'
         ST    RF,RCODE      RETURN-CODE VOM IEBGENER RETTEN
         TM    FLG,X'80'     ENQ,RESERVE?
         BZ    RETURN        NO
         DEQ   (QNAME,JFCB,44,SYSTEM),RET=HAVE
RETURN   L     RF,RCODE      IEBGENER-RC AN OS UEBERGEBE
         L     RD,SA+4
         RETURN (14,12),RC=(15)  ZURUECK ZUM OS
F100     EQU   *
         SYNADAF ACSMETH=QSAM
         ABEND 2,DUMP
E100     EQU   *
         ABEND 3,DUMP
         EJECT
UT2DCB   DCB   DDNAME=SYSUT2,EXLST=AJFCB,SYNAD=F100,EODAD=E100,        *
               DSORG=PS,MACRF=GL,RECFM=U,BLKSIZE=32767,BUFNO=1
AJFCB    DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176         JOB-FILE CONTROL BLOCK (RNAME)
QNAME    DC    C'PRESERVE'
PARMADR  DC    F'0'
TIOTADDR DC    F'0'
RCODE    DC    F'12'
X0       DC    XL4'00000000'
FLG      DC    X'00'
         DC    C'UCB'
FUCB     DC    F'0'
         DC    C'UCBINH. '
UCBFIELD DC    CL48' '
BLANK    DC    CL8'        '
VOLNR    EQU   6
TABVOLS  DC    CL36'SYSSHRTESTB1TESTB2TESTC1TESTC2P50008'
         END
