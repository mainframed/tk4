*/T0518XCL JOB (S,23556),34.HOFFM,CLASS=Z,MSGCLASS=A,NOTIFY=HOFFM
*/XDACALL EXEC AFC
*/SYSIN    DD  *
XDACALL  TITLE 'CALL DRIVER FUER XDATA SYSIN CALLS'
XDACALL  CSECT
         PRINT NOGEN
R0       EQU   0                            REGISTER EQUAL SETZEN
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*       REGISTER IN HEXAFORM
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         USING XDAVARS,R5
         USING *,R9
         STM   RE,RC,SAVE2              REGISTER SAVEN
         ST    RD,SAVE+4                SAVE
         LA    RE,SAVE                   AREA
         ST    RE,8(,RD)                  KETTUNG
         LR    RD,RE                       SYSIN EXIT
         LA    R3,4(,R8)                ADR XDATA PARAMETERLISTE
         SR    R0,R0                    LOESCHEN FUER IC
         IC    R0,0(,R8)                ANZAHL PARAMETER
         LTR   R0,R0                    GIBT ES PARAMETER
         BP    B100                      JA
         SR    R1,R1                    R1 LOESCHEN
         B     B800                     CALL UPRO
B100     NULL
         LA    R4,PARMLIST-4            ADR PARAMETERLISTE
B200     NULL
         MVC   *+8(2),0(R3)             S(PARM)
         LA    R6,*-*                   ADR(PARM)
         ST    R6,4(,R4)                STORE IN ORIGINAL PARMLISTE
         LA    R3,2(,R3)                NAECHSTER PLATZ I-LISTE
         LA    R4,4(,R4)                NAECHSTER PLATZ O-LISTE
         BCT   R0,B200                  WEITERMACHEN
         MVI   0(R4),X'80'              SET VL-SWITCH
         LA    R1,PARMLIST              PARMLISTE IN R1
B800     NULL
         ST    R3,SAVE2+40              RETURN-ADR (R8)
         L     RF,0(,R8)                ENTRY ADRESSE
         BALR  RE,RF                    CALL UPRO
         CVD   RF,PARMLIST              RETURNCODE
         ZAP   $LASTCC,PARMLIST(8)
         CP    $LASTCC,$MAXCC
         BNH   B820
         ZAP   $MAXCC,$LASTCC
B820     NULL
         LM    RE,RC,SAVE2              REGISTER ZURUECK
         L     RD,SAVE+4                R13 ZURUECK
         BR    R8                       RETURN IN XDATA-EXIT
         DS    0D
PARMLIST DC    32F'0'
SAVE     DC    18F'0'
SAVE2    DC    15F'0'
         EJECT
XDAVARS  DSECT
         COPY  XDAVARS
         END
