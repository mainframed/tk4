PROC 0
GLOBAL &HEXVALUE
GLOBAL &VALUE
/**********************************************************************/
/*     H     H   EEEEEEE    AAAAA    DDDDDD    EEEEEEE   RRRRRR       */
/*     H     H   E         A     A   D     D   E         R     R      */
/*     HHHHHHH   EEEEEEE   AAAAAAA   D     D   EEEEEEE   RRRRRR       */
/*     H     H   E         A     A   D     D   E         R   R        */
/*     H     H   EEEEEEE   A     A   DDDDDD    EEEEEEE   R     R      */
/**********************************************************************/
/*  CLIST NAME: VTAMDIAG          REL/DATE: 2.1/08.15.79              */
/*                                                                    */
/*  DOC: THIS CLIST EXAMINES THE CURRENT DUMP TO SEE IF THE ATCVT     */
/*       POINTER HAS BEEN INITIALIZED. IF IT HAS, THE SU BIT STRING   */
/*       IS EXAMINED TO SEE IF VTAM 3 ACF IS INSTALLED.  DIFFERENT    */
/*       CLISTS ARE USED FOR VTAM3 DUMPS AND THE CLIST WILL GIVE      */
/*       CONTROL TO THE APPROPRIATE CLIST IN EITHER CASE.             */
/*       AFTER THAT DETERMINATION IS MADE, A MENU IS DISPLAYED FROM   */
/*       WHICH THE USER MAY CHOOSE THE TYPE OF DIAGNOSIS HE WISHES.   */
/*  SYMBOLS EQUATED: ATCVT                                            */
/*  SYMBOLS FOUND:   ATCVT                                            */
/*  RETURN: CONTROL IS RETURNED TO THE CALLING CLIST OR IPCS.         */
/*  DEPENDENCIES: 1) THIS CLIST REQUIRES THAT COMMAND                 */
/*                   CLEAR BE INSTALLED IN YOUR ENVIRONMENT.        */
/*                2) THIS CLIST REQUIRES THAT CLIST 'HEX' BE          */
/*                   INSTALLED IN YOUR ENVIRONMENT.                   */
/*  USE: THIS CLIST PROMPTS THE USER FOR INPUT AND GIVES CONTROL TO   */
/*       ANOTHER CLIST FOR THE ACTUAL DUMP SHOOTING.                  */
/*SJM******************************************************************/
SETD PRINT TERMINAL NODISPLAY
EVAL 408. LE(4)                          /* IS ATCVT INITIALIZED?     */
IF &LASTCC=0 × &LASTCC=12 THEN DO        /* IF NOT TELL USER AND EXIT */
WRITE COULD NOT FIND THE ATCVT POINTER. WILL TERMINATE VTAMDIAG.
WRITE DEPRESS ENTER WHEN READY TO CONTINUE
READ
GOTO FINI
END
EQ ATCVT 408.%                           /* EQUATE ATCVT              */
SET &RCODE=0
SET &VTAM=2
COMPARE ADDR(4C.%+414) LEN(4) WITH(VALUE(X'00000000')) MASK(X'00FFFFFF')
IF &LASTCC=0 THEN DO                     /* LOOK AT SU BIT STRING     */
WRITE *** ERROR: SU BIT STRING NOT AVAILABLE
WRITENR IS VTAM 3 ACF INSTALLED ON YOUR SYSTEM Y/(N) ==>
READ &ANS
IF &ANS=Y THEN SET &VTAM=3               /* IF BIT STRING IS NOT AVAIL*/
SET &RCODE=8                             /* ASK THE USER IF VTAM 3 ACF*/
GOTO ENDIT                               /* IS ON.                    */
END
EQ SUBITADD 4C.%+414% PTR
COMPARE ADDR(SUBITADD+5N) LEN(1) WITH(VALUE(X'80')) MASK(X'80')
IF &LASTCC=0 THEN DO
SET &VTAM=3
WRITE VTAM 3 IS INSTALLED VTAMDIAG WILL USE THE VTAM 3 CLISTS
END
ELSE DO                                  /* DIFFERENT CLISTS ARE USED */
SET &VTAM=2                              /* FOR THE DIFFERENT LEVELS  */
WRITE VTAM 3 IS NOT INSTALLED VTAMDIAG WILL USE THE VTAM 2 CLISTS
END                                      /* OF VTAM.                  */
ENDIT: WRITE
IF &VTAM=2 THEN EQ BPDTY 408.%+80%  LE(4)
IF &VTAM=3 THEN EQ BPDTY ATCVT+134% LE(4) /* SHOW USER THE MENU       */
/*******************************************************************/
MAINMENU: WRITE DEPRESS ENTER KEY WHEN READY TO CONTINUE
          READ
          CLEAR
WRITE
WRITE
WRITE
WRITE ++++++++ VTAMDIAG = VTAM &VTAM DUMP ANALYZER +++++++ PRIMARY MENU
WRITE
WRITE FUNCTION: 1) ANALYZE THE STATUS OF ALL BUFFER TYPES.
WRITE           2) FIND RPH'S AND CRA'S AND LIST 32 BYTES OF THE RPH.
WRITE              DETERMINE WHETHER LOCKS ARE HELD BY ANY RPH'S.
WRITE           3) ANALYZE ALL READY QUEUES FOR PABS TO BE PROCESSED.
WRITE           4) SCAN BUFFER POOLS FOR SELECTED CONTROL BLOCKS.
WRITE           5) FIND (START : END) OF VTAM BUFFS AND EQUATE THE
WRITE              ADDRESSES.
WRITE           6) FIND REQUESTED RDTE BY NAME OR CID
WRITE           X) EXIT VTAMDIAG.
/*******************************************************************/
WRITE
PROMPT0: WRITENR ENTER REQUIRED FUNCTION NUMBER OR  X/EXIT VTAMDIAG ===>
READ &P0
          CLEAR
IF &P0=&STR(1) THEN DO                   /* READ USER RESPONSE AND    */
  %VTAM&VTAM.1                           /* GIVE CONTROL TO THE CLIST */
  GOTO MAINMENU                          /* WHICH WILL DO THE WORK    */
  END
IF &P0=&STR(2) THEN DO
  %VTAM&VTAM.2
  GOTO MAINMENU
  END
IF &P0=&STR(3) THEN DO
  %VTAM&VTAM.3
  GOTO MAINMENU
  END
IF &P0=&STR(4) THEN DO
  %VTAM&VTAM.4
  GOTO MAINMENU
  END
IF &P0=&STR(5) THEN DO
  %VTAM&VTAM.5
  GOTO MAINMENU
  END
IF &P0=&STR(6) THEN DO
  IF &VTAM = 2 THEN DO
  WRITE THIS FUNCTION NOT SUPPORTED IN SNA2
  WRITE DEPRESS ENTER WHEN READY TO CONTINUE
  GOTO MAINMENU
  END
  %VTAM36
  GOTO MAINMENU
  END
IF &P0=&STR(X) THEN GOTO FINI
IF &P0^=&STR(1) && &P0^=&STR(2) && &P0^=&STR(3) && &P0^=&STR(4)-
 && &P0^=&STR(X) && &P0^=&STR(5) && &P0^=&STR(6) THEN
 WRITE INVALID RESPONSE ENTER  1 2 3 ETC. OR  X/EXIT VTAMDIAG
 GOTO PROMPT0
 END
FUNC999: WRITE THIS FUNCTION NOT SUPPORTED YET
FINI: WRITE END OF VTAM ANALYSIS.
      SETD DISPLAY NOPRINT
