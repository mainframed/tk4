IEFUTL   START
         XSAVE R12,,*,WORKL,SP=255
         USING COMEXTBL,R11
         USING WORK,R13
         L     R11,0(R1)
         L     R10,4(R13)
         LR    R2,R0
         B     *+4(R2)
         B     JOBCPUTL
         B     STPCPUTL
         B     JOBWAITL
JOBCPUTL EQU   *
         MVC   WTO,MESS1
         MVC   WTO+16(8),CJOBNM
WTOR1    EQU   *
         XC    ECB,ECB
         MVI   REPLY,X'00'
         WTOR  ,REPLY,1,ECB,MF=(E,WTO)
         WAIT  ECB=ECB
         OI    REPLY,X'40'
         CLI   REPLY,C'C'
         BE    CANCEL1
         CLI   REPLY,C'U'
         BNE   WTOR1
         MVC   16(4,R10),=F'4'
         MVC   24(4,R10),=F'34615380'
         B     RETURN
CANCEL1  EQU  *
         MVC   16(4,R10),=F'0'
         B     RETURN
STPCPUTL EQU   *
         MVC   WTO,MESS2
         MVC   WTO+16(8),CJOBNM
         BAL   R9,SUCHSTPN
         B     *+4(R1)
         MVC   WTO+30(8),8(R6)
WTOR2    EQU   *
         XC    ECB,ECB
         MVI   REPLY,X'00'
         WTOR  ,REPLY,1,ECB,MF=(E,WTO)
         WAIT  ECB=ECB
         OI    REPLY,X'40'
         CLI   REPLY,C'C'
         BE    CANCEL1
         CLI   REPLY,C'U'
         BNE   WTOR2
         MVC   16(4,R10),=F'4'
         MVC   24(4,R10),=F'11538480'
         B     RETURN
JOBWAITL EQU   *
         CLC   COMEXTBL(3),=CL3'ONL'
         BNE   NONL
         MVC   16(4,R10),=F'4'
         MVC   24(4,R10),=F'300000000'
         B     RETURN
NONL     EQU   *
         L     R1,16
         L     R1,0(R1)
         L     R1,4(R1)
         L     R1,164(R1)
         CLC   40(4,R1),=F'0'
         BNE   NONEWSTP
         MVI   COMEXTBL+22,X'00'
NONEWSTP EQU   *
         CLI   COMEXTBL+22,X'40'
         BNE   INCR
         MVI   COMEXTBL+22,X'00'
INCR     EQU   *
         SR    R1,R1
         IC    R1,COMEXTBL+22
         LA    R1,1(R1)
         STC   R1,COMEXTBL+22
         CLI   COMEXTBL+22,X'14'
         BNE   GOON
         MVC   16(4,R10),=F'0'
         MVI   COMEXTBL+22,X'40'
         B     RETURN
GOON     EQU   *
         LA    1,COMMD
         SR    R0,R0
         SVC   34
         MVC   WTO,MESS3
         MVC   WTO+8(8),CJOBNM
         BAL   R9,SUCHSTPN
         B     *+4(R1)
         MVC   WTO+22(8),8(R6)
         WTO   MF=(E,WTO)
         MVC   16(4,R10),=F'4'
         MVC   24(4,R10),=F'6923100'
         CLC   WTO+22(8),=CL8'XXXXXXXX'
         BE    RETURN
         MVC   WTO,MESS4
         MVC   WTO+8(8),CJOBNM
         L     R15,16   CVT
         L     R8,0(R15)
         L     R8,4(R8)
         L     R15,120(R15)   A(RQE-CHAIN)
         LA    R1,10   MAX. NO OF DEVICES
         LA    R3,WTO+30
LPDX     EQU   *
         CLI   4(R15),X'FF'
         BE    UPDL
         L     R9,12(R15)    GET TCB FROM RQE
         LA    R9,0(R9)   CLEAR PK
         CR    R8,R9
         BNE   UPDL
         LH    R9,2(R15)   GET UCB-POINTER
         SRL   R9,2
         SLL   R9,2   MOVE ERROR-INDICATOR
         MVC   0(3,R3),13(R9)
         LA    R3,4(R3)
         BCT   R1,UPDL
         B     WTOX
UPDL     EQU   *
         LA    R15,16(R15)   GET NEXT RQE
         CLC   0(2,R15),=XL2'0001'
         BNE   LPDX
WTOX     EQU   *
         CLC   WTO+30(4),=CL4' '
         BE    RETURN
         WTO   MF=(E,WTO)
RETURN   EQU   *
         XRETURN ,R,SP=255
SUCHSTPN EQU   *
         LA    R1,6
         L     R8,16
         L     R8,160(R8)   HIGH PRTY TCB
SCHLEIFE EQU   *
         L     R6,12(R8)
         CLC   CJOBNM,0(R6)
         BE    STPNGFDN
         L     R8,116(R8)   NEXT TCB
         LA    R8,0(R8)
         LTR   R8,R8
         BCR   8,R9
         B     SCHLEIFE
STPNGFDN EQU   *
         LA    R1,0
         BR    R9
MESS1    WTOR  'JOB XXXXXXXX CPU-TIME EXPIRED - REPLY ''U'' OR ''C''', *
               ,1,,MF=L,ROUTCDE=(2),DESC=3
MESS2    WTOR  'JOB XXXXXXXX STEP XXXXXXXX CPU-TIME EXPIRED - REPLY ''U*
               '' OR ''C''',,1,,MF=L,ROUTCDE=(2),DESC=3
MESS3    WTO   'JOB XXXXXXXX STEP XXXXXXXX PERMANENT IN WAIT-STATE',   *
               ROUTCDE=(2),DESC=2,MF=L
MESS4    WTO   'JOB XXXXXXXX DEV STARTED:                              *
                           ',ROUTCDE=(2),DESC=3,MF=L
COMMD    DS    0F
         DC    X'00090000'
         DC    CL5' D R '
         REG
COMEXTBL DSECT
CJOBNM   DS    CL8
         DS    CL24
COMEXDAT DS    F
WORK     DSECT
SVA      DS    18F
ECB      DS    F
WTO      DS    CL90
REPLY    DS    CL1
WORKL    EQU   *-WORK
         END
