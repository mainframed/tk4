E15      CSECT
         BASER 1
START    LA    3,0           R3 LOESCHEN
         C     3,0(1)        EOD ?
         BE    EXCLOSE
         L     3,0(1)        SATZADRESSE LADEN
         CLI   5(3),X'04'    TYP 4 RECORD ?
         BE    SELEKT
         CLI   5(3),X'06'    TYP 6 RECORD ?
         BE    SELEKT
         CLI   5(3),X'05'    TYP 5 RECORD ?
         BNE   NIX
         MVC   WORT,0(3)     WEGEN ALIGNMENT
         L     4,WORT        SATZLAENGENFELD IN WORT GESPEICHERT
         SRL   4,16          SATZLAENGE RECHTSBUENDIG IN REG 4
         BCTR  4,0           WEGEN EXECUTE !
         LTR   4,4
         BM    MOVEND
         EX    4,MOVE
         SRL   4,8
         LTR   4,4
         BZ    MOVEND
         SR    6,6
         IC    6,WORT+1
         LA    5,BEREICH
         AR    5,6
         AR    3,6
MLOOP    EQU   *
         MVC   0(256,5),0(3)
         LA    5,256(5)
         LA    3,256(3)
         BCT   4,MLOOP
MOVEND   EQU   *
         MVI   BEREICH+4,X'FF'
         LA    1,BEREICH
         L     13,M0001M00+4
         ST    1,24(13)      SATZADRESSE NACH SAVEFELD FUER REG 1
         RETURN (14,12),RC=0 CODE FUER AENDERUNG
SELEKT   L     13,M0001M00+4
         ST    3,24(13)      SATZADRESSE INS SAVEFELD FUER REG 1
         RETURN (14,12),RC=0 CODE FUER 'NO ACTION'
NIX      L     13,M0001M00+4
         ST    3,24(13)
         RETURN (14,12),RC=4 DELETE RECORD
EXCLOSE  L     13,M0001M00+4
         RETURN (14,12),RC=8 CODE FUER EXIT CLOSE
*
*
BEREICH  DS    CL2008
WORT     DC    F'0'
MOVE     MVC   BEREICH(0),0(3)  SATZ IN WORK AREA MOVEN
         END   E15
