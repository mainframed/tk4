         TITLE 'VSAM CATALOG RECOVERY PROGRAM'
VSCATREC START
*
*
*   THIS PROGRAM IS USED TO UPDATE A RESTORED VSAM-CATALOG TO THE
*   STATUS IT HAD BEFORE THE RESTORE.
*   ONLY NONVSAM DATASET ENTRIES WILL BE AFFECTED, USING SMF RECORDS
*   TYPE 63 AND 67.
*
*   INPUT:  SYSIN:      UP TO 5 CATALOG NAMES INCLUDING PASSWORD.
*                       FORMAT:  CATALOG-NAME/PASSWORD     (BEG.COL.1)
*                       EXAMPLE: SYS1.MASTCAT.RV0303/MASTCAT
*                       IF ANY CATALOG NAMES ARE SPECIFIED, ONLY THESE
*                       CATALOGS WILL BE UPDATED.
*                       IF NO CATALOG NAME IS SPECIFIED (NO SYSIN-CARD)
*                       ALL CATALOGS FOUND IN THE SMF-DATA WILL BE
*                       UPDATED.
*           PARM-FLD:   DATE AND TIME OF RECOVERY START (USUALLY THE
*                       TIME THE DUMP WAS TAKEN)
*                       FORMAT:   PARM='YYDDD,HHMM'
*                       EXAMPLE:  PARM='76053,1017'
*                       DEFAULT:  NONE, ALL INPUT RECORDS WILL BE
*                                 PROCESSED.
*           SYSUT1:     SMF RECORDS TYPE 63 AND 67, SORTED ASCENDING
*                       DATE AND TIME
*   OUTPUT: SYSPRINT:   EXECUTION PROTOCOL
*           SYSUT2:     IDCAMS CONTROL STATEMENTS (DEFINE, DELETE)
*
*
         EJECT
         REG
         XSAVE R12,SVA,VSCATREC
         LR    R3,R1          SAVE PARM-REG.
         OPEN  (SYSPRINT,(OUTPUT))
         TM    SYSPRINT+48,X'10'    WAS OPEN OK ?
         BO    SYSPROK        YES, BRANCH
         WTO   'VSCR01A SYSPRINT DD-STMT MISSING',ROUTCDE=11
RETURN4  EQU   *
         XRETURN 4
SYSPROK  EQU   *
         CALL  TDATE,(MESS1+47,MESS1+36),VL
         PUT   SYSPRINT,MESS1   PUT HEADER-LINE
         MVI   MESS1,X'40'
         MVC   MESS1+1(79),MESS1
         MVI   MESS1,C'-'
         PUT   SYSPRINT,MESS1
         MVI   MESS1,X'40'
         L     R3,0(R3)
         CLC   0(2,R3),=H'0'  TEST FOR NULL PARM-FIELD
         BNE   TSTLEN  BR IF NOT
         MVC   MESS2+25(17),=C'NO DATE SELECTION'
         PUT   SYSPRINT,MESS2
         B     PRMOK
TSTLEN   EQU   *
         CLC   0(2,R3),=H'10'   TEST MAX. LENGTH OF PARM-FIELD
         BE    CONVRT
PRMINV   EQU   *
         MVC   MESS2+25(7),=C'INVALID'
         PUT   SYSPRINT,MESS2
ENDEXEC  EQU   *
         PUT   SYSPRINT,MESS3
         CLOSE SYSPRINT
         B     RETURN4
CONVRT   EQU   *
         CLI   7(R3),C','  ASSURE PROPER PARM-FIELD
         BNE   PRMINV
         MVC   MESS2+25(10),2(R3)    MOVE PARM-FIELD INF. INTO MESSAGE
         PUT   SYSPRINT,MESS2
         PACK  FRDATE+1(3),2(5,R3)  PACK DATE INTO COMPARE-FIELD
         PACK  DBLWD,8(2,R3)   PACK HOURS
         CVB   R4,DBLWD     CONVERT TO BINARY
         MH    R4,=H'60'   CONVERT TO MINUTES
         PACK  DBLWD,10(2,R3)   PACK MINUTES
         CVB   R5,DBLWD
         AR    R4,R5   ADD MINUTES
         MH    R4,=H'6000'  CONVERT TO HUNDRETHS OF SECONDS
         ST    R4,FRTIME   STORE INTO COMPARE-FIELD
PRMOK    EQU   *
         OPEN  SYSIN
         TM    SYSIN+48,X'10'
         BNO   NOSYSIN
         PUT   SYSPRINT,MESS4
         MVI   MESS4,X'40'
         LA    R5,5   ESTABL. LOOP CONTROL
         LA    R4,CATNAMES    ADDRESS OF CATALOG-NAMES-AREA
NXTSYSIN EQU   *
         LA    R6,2(R4)    LOAD ADDR. OF DATA-AREA
         GET   SYSIN,(R6)
         MVC   MESS4+1(52),0(R6)
         PUT   SYSPRINT,MESS4
         MVI   MESS4,X'40'
         TRT   0(45,R6),TRTTAB
         BNZ   GETLEN    NO BLANK/ NO SLASH WITHIN 45 CHARS. - ERROR
         PUT   SYSPRINT,MESS5
         CLOSE SYSIN
         B     ENDEXEC
GETLEN   EQU   *
         SR    R1,R6   COMP. NO. CHARS. TO BLANK/SLASH
         BCTR  R1,0   REDUCE FOR EXECUTE
         STH   R1,0(R4)   AND STORE INTO TABLE
         LA    R4,54(R4)   INCR. POINTER TO NEXT ENTRY
         BCT   R5,NXTSYSIN   LOOP
EODSYIN  EQU   *
         CLOSE SYSIN
         B     SYINOK
NOSYSIN  EQU   *
         PUT   SYSPRINT,MESS6
SYINOK   EQU   *
         OPEN  (SYSUT1,,SYSUT2,(OUTPUT))
         TM    SYSUT1+48,X'10'   OPEN FOR SYSUT1 SUCCESSFUL?
         BO    TSTUT2
MISS     EQU   *
         PUT   SYSPRINT,MESS12
         B     ENDEXEC
TSTUT2   EQU   *
         TM    SYSUT2+48,X'10'   OPEN FOR SYSUT2 SUCCESSFUL?
         BNO   MISS   NO - TERMINATE
         SR    R2,R2          NO. OF SMF-RECS. TYPE 63
         SR    R3,R3          NO. OF SMF-RECS. TYPE 67
         SR    R4,R4          NO. IDCAMS DEFINE COMMANDS EDITED
         SR    R5,R5          NO. IDCAMS DELETE COMMANDS EDITED
         EJECT
*
*    NOW BEGIN TO PROCESS EACH INDIVIDUAL SMF-RECORD.
*
NXTUT1   EQU   *
         GET   SYSUT1
         LA    R6,4(R1)    POINT TO BEGIN OF RECORD
         CLC   OLDSEQ,=XL8'00'   FIRST ENTRY?
         BNE   TSTSEQ   NO, GO TEST SEQU.
         MVC   OLDSEQ,2(R6)
TSTSEQ   EQU   *
         CLC   6(4,R6),OLDSEQ+4   TEST SEQU. OF DATE
         BL    SEQERR  NOT IN SEQUENCE
         CLC   2(4,R6),OLDSEQ
         BNL   SEQOK    BR. IF SEQU. OK
SEQERR   EQU   *
         PUT   SYSPRINT,MESS11
         CLOSE (SYSUT1,,SYSUT2)
         B     ENDEXEC
SEQOK    EQU   *
         MVC   OLDSEQ,2(R6)
         CLC   6(4,R6),FRDATE   TEST FROM-DATE
         BL    NXTUT1
         CLC   2(4,R6),FRTIME   TEST FROM-TIME
         BL    NXTUT1
         CLI   1(R6),63   CHECK RECORD-TYPE
         BE    TSTNVSAM
         CLI   1(R6),67
         BNE   NXTUT1  NEITHER TYP 63 NOR TYP 67 - GET NEXT SMF REC
TSTNVSAM EQU   *
         TM    39(R6),X'08'    NVSAM RECORD?
         BNO   NXTUT1   NO, GET NEXT SMF REC
         LA    R7,44(R6)   LOC. OF CATALOG NAME IN TYP 63
         CLI   1(R6),63    TYP = 63?
         BE    CHKCAT   YES, R7 OK
         LA    R7,40(R6)  READJUST R7 FOR TYP 67
CHKCAT   EQU   *
         LA    R9,43    LENGTH FOR MOVE CATALOG-NAME WITH EX.
         CLI   CATNAMES,X'FF'
         BE    CATOK    NO CATALOGES SPECIFIED VIA SYSIN
         LA    R8,CATNAMES
NXTCAT   EQU   *
         LH    R9,0(R8)
         EX    R9,CLCCAT
         BNE   INCRCAT
         LA    R9,51     CATALOG FOUND IN TABLE - RESET REG. 9 (LENGTH)
         LA    R7,2(R8)
         B     CATOK
INCRCAT  EQU   *
         LA    R8,54(R8)   GET NEXT ENTRY WITHIN CATALOG NAME TABLE
         CLI   0(R8),X'FF'   LAST ONE?
         BE    NXTUT1   YES, CATALOG NOT FOUNT IN TABLE -NEXT SMF REC.
         B     NXTCAT
CLCCAT   CLC   2(0,R8),0(R7)
MVCCAT   MVC   OUTR2CAT(0),0(R7)
CATOK    EQU   *
         EX    R9,MVCCAT   MOVE CATALOG-NAME TO OUTPUT-REC.
         CLI   1(R6),63    REC. TYPE 63?
         BE    PROC63   YES, GO PROCESS IT
         LA    R3,1(R3)    INCR. COUNT FOR TYPE 67 RECORDS
         MVC   OUTR1NAM,84(R6)   MOVE ENTRY NAME
         PUT   SYSUT2,OUTR1
         PUT   SYSUT2,OUTR2
         LA    R5,1(R5)   INCR. COUNT FOR NO. OF DELETES
         B     NXTUT1
         EJECT
*
*   NOW BEGIN PROCESSING THE TYPE 63 SMF RECORDS
*
PROC63   EQU   *
         LA    R2,1(R2)   INCR. COUNT FOR TYPE 63 RECORDS
         MVC   OUTR3NAM,88(R6)
         TM    38(R6),X'80'    NEW DEFINITION?
         BO    NEW
         MVC   OUTR1NAM,88(R6)
         PUT   SYSUT2,OUTR1
         PUT   SYSUT2,OUTR2
         LA    R5,1(R5)   # OF DELETES
NEW      EQU   *
         LA    R7,132(R6)  POINT TO NEW CATALOG RECORD
         LA    R4,1(R4)    # OF DEFINES
         PUT   SYSUT2,OUTR3
*
*   NOW PROCESS THE VOLUMES
*
         PUT   SYSUT2,OUTR4
         SR    R8,R8
         IC    R8,112(R7)   SET NO. OF FIELDS FOR BCT
         LR    R9,R8
         MH    R9,=H'5'   MULTPLY WITH LENGTH OF FIELD-POINTER
         LA    R6,113(R7)   BEGIN OF POINTERS
         LA    R10,8    NO OF VOLS. PER CARD
         LA    R1,MESS1-6    CURRENT POSITION IN OUTPUT RECORD
         LA    R9,113(R9,R7)   START OF FIELDS
NXTFLDV  EQU   *
         LA    R1,7(R1)    UPDATE POINTER IN OUTPUT-RECORD
         NI    3(R6),X'3F'   ELIMINATE UNWANTED BITS
         CLI   3(R6),X'03'   IS IT A VOLUME-FIELD?
         BNE   INCRFLDV     NO, PROCESS NEXT FIELD
         LH    R11,1(R6)    OFFSET OF CURRENT FIELD
         LA    R11,0(R11,R9)     ADDR. OF FIELD
         MVC   0(6,R1),6(R11)   MOVE VOLUME-SERIAL NBR.
         BCT   R10,INCRFLDV
*   NOW ONE CARD IS EXHAUSTED - PUT IT OUT AND BEGIN NEXT
         CH    R8,=H'1'    WAS THIS FIELD THE LAST ONE?
         BNE   NORM      NO, NORMAL PROC.
         MVC   7(3,R1),=CL3') -'   TERMINATE THE VOL-PARAMETER
         B     PUTVOL
NORM     EQU   *
         MVI   7(R1),C'-'    ADD CONTINUATION CHAR.
PUTVOL   EQU   *
         PUT   SYSUT2,MESS1
         MVC   MESS1+1(79),MESS1
         LA    R1,MESS1-6     RESET POINTER
         LA    R10,8     RESET COUNT OF VOLS/CARD
INCRFLDV EQU   *
         LA    R6,5(R6)    STEP TO NEXT FIELD POINTER
         BCT   R8,NXTFLDV    AND GO PROC. THIS FIELD
         CH    R10,=H'8'    DO WE HAVE TO TERMINATE THE CUURENT CARD?
         BE    PROCDEV    NO
         MVC   7(3,R1),=CL3') -'   TERMINATE THE VOL-PARM.
         PUT   SYSUT2,MESS1
         MVC   MESS1+1(79),MESS1    BLANK CARD
PROCDEV  EQU   *
         PUT   SYSUT2,OUTR5
         IC    R8,112(R7)    NO. OF FIELDS
         LA    R1,MESS1-6
         LA    R10,8
         LA    R6,113(R7)
NXTFLDU  EQU   *
         LA    R1,7(R1)
         NI    3(R6),X'3F'
         CLI   3(R6),X'03'
         BNE   INCRFLDU
         LH    R11,1(R6)
         LA    R11,0(R11,R9)
         CLC   4(2,R11),=XL2'2009'
         BNE   NO3330V
         TM    3(R11),X'08'
         BNO   NO3330V
         MVC   0(5,R1),=CL5'3330V'
         B     ENDTAB
NO3330V  EQU   *
         CLC   4(2,R11),=XL2'2000'
         BNE   TSTTAPE
         MVC   0(4,R1),=CL4'3330'
         B     ENDTAB
TSTTAPE  EQU   *
         CLC   4(2,R11),=XL2'8000'
         BNE   TSTTAB
         MVC   0(6,R1),=CL6'3400-3'
         B     ENDTAB
TSTTAB   EQU   *
         LA    R14,DEVTYPES
NXTDEVT  EQU   *
         CLI   4(R11),X'20'  DASD-DEV?
         BNE   CLC4   NO, MUST BE TAPE, COMPARE 4 BYTE DEVTYPE
         CLC   2(2,R14),4(R11)
         BE    DEVFND
INCRDEV  EQU   *
         LA    R14,10(R14)
         CLI   0(R14),X'FF'   END OF TABLE?
         BNE   NXTDEVT    NO, PROC. NEXT ENTRY
         ST    R1,DBLWD   SAVE R1
         UNPK  MESS13+22(9),2(5,R11)
         MVI   MESS13+30,X'40'
         TR    MESS13+22(8),TRTAB-240
         PUT   SYSPRINT,MESS13    PUT WARNG. MSG FOR UNKNOWN DEVT.
         MVI   MESS13,X'40'
         L     R1,DBLWD
         B     INCRFLDU    GO PROC. NEXT FIELD
CLC4     EQU   *
         CLC   0(4,R14),2(R11)
         BNE   INCRDEV
DEVFND   EQU   *
         MVC   0(6,R1),4(R14)
ENDTAB   EQU   *
         BCT   R10,INCRFLDU
         CH    R8,=H'1'
         BNE   NORM1
         MVC   7(3,R1),=CL3') -'
         B     PUTDEV
NORM1    EQU   *
         MVI   7(R1),C'-'
PUTDEV   EQU   *
         PUT   SYSUT2,MESS1
         MVC   MESS1+1(79),MESS1
         LA    R1,MESS1-6
         LA    R10,8
INCRFLDU EQU   *
         LA    R6,5(R6)
         BCT   R8,NXTFLDU
         CH    R10,=H'8'
         BE    PROCSEQ
         MVC   7(3,R1),=CL3') -'
         PUT   SYSUT2,MESS1
         MVC   MESS1+1(79),MESS1
PROCSEQ  EQU   *
         PUT   SYSUT2,OUTR6
         IC    R8,112(R7)
         LA    R1,MESS1-6
         LA    R10,8
         LA    R6,113(R7)
NXTFLDS  EQU   *
         LA    R1,7(R1)
         NI    3(R6),X'3F'
         CLI   3(R6),X'03'
         BNE   INCRFLDS
         LH    R11,1(R6)
         LA    R11,0(R11,R9)
         CLC   12(2,R11),=XL2'0000'
         BNE   NTZ
         MVI   13(R11),X'01'
NTZ      EQU   *
         LH    R15,12(R11)
         CVD   R15,DBLWD
         MVC   0(5,R1),MASK
         ED    0(5,R1),DBLWD+6
         BCT   R10,INCRFLDS
         CH    R8,=H'1'
         BNE   NORM2
         MVC   7(4,R1),=CL4')) -'
         B     PUTSEQ
NORM2    EQU   *
         MVI   7(R1),C'-'
PUTSEQ   EQU   *
         PUT   SYSUT2,MESS1
         MVC   MESS1+1(79),MESS1
         LA    R1,MESS1-6
         LA    R10,8
INCRFLDS EQU   *
         LA    R6,5(R6)
         BCT   R8,NXTFLDS
         CH    R10,=H'8'
         BE    ENDPROC
         MVC   7(4,R1),=CL4')) -'
         PUT   SYSUT2,MESS1
         MVC   MESS1+1(79),MESS1
ENDPROC  EQU   *
         PUT   SYSUT2,OUTR2
         B     NXTUT1
EODSYUT1 EQU   *
         CLOSE (SYSUT1,,SYSUT2)
         CVD   R2,DBLWD
         UNPK  MESS7+33(5),DBLWD
         OI    MESS7+37,X'F0'
         PUT   SYSPRINT,MESS7
         CVD   R3,DBLWD
         UNPK  MESS8+33(5),DBLWD
         OI    MESS8+37,X'F0'
         PUT   SYSPRINT,MESS8
         CVD   R4,DBLWD
         UNPK  MESS9+33(5),DBLWD
         OI    MESS9+37,X'F0'
         PUT   SYSPRINT,MESS9
         CVD   R5,DBLWD
         UNPK  MESS10+33(5),DBLWD
         OI    MESS10+37,X'F0'
         PUT   SYSPRINT,MESS10
         PUT   SYSPRINT,MESS3
         CLOSE SYSPRINT
         XRETURN 0
         EJECT
*
*    DEFINITIONS
*
MESS1    DC    CL80'1VSCATREC EXECUTION PROTOCOL        TT.MM.19JJ/HH.M*
               M.SS'
MESS2    DC    CL60'0PARM-FIELD INFORMATION:'
MESS3    DC    CL60'-END OF EXECUTION'
MESS4    DC    CL60'0CATALOG NAMES SPECIFIED VIA SYSIN:'
MESS5    DC    CL60'0INVALID CATALOG NAME'
MESS6    DC    CL60'0NO CATALOG NAMES SPECIFIED'
MESS7    DC    CL60'0NUMBER OF TYPE 63 SMF RECORDS = 12345'
MESS8    DC    CL60' NUMBER OF TYPE 67 SMF RECORDS = 12345'
MESS9    DC    CL60' NUMBER OF AMS DEFINE COMMANDS = 12345'
MESS10   DC    CL60' NUMBER OF AMS DELETE COMMANDS = 12345'
MESS11   DC    CL60'0SYSUT1 SEQUENCE ERROR'
MESS12   DC    CL60'0SYSUT1/SYSUT2 DD-STMT MISSING'
MESS13   DC    CL60'0UNKNOWN DEVICE-TYPE: XXXXXXXX'
         SPACE 3
DEVTYPES DS    0F
         DC    XL4'30008001',CL6'2400  '
         DC    XL4'30808001',CL6'2400-1'
         DC    XL4'30C08001',CL6'2400-2'
         DC    XL4'34008001',CL6'2400-3'
         DC    XL4'34208001',CL6'2400-4'
         DC    XL4'34008003',CL6'3400-3'
         DC    XL4'34208003',CL6'3400-4'
         DC    XL4'32008003',CL6'3400-5'
         DC    XL4'32108003',CL6'3400-6'
         DC    XL4'30C02008',CL6'2314  '
         DC    XL4'30502007',CL6'2305-2'
         DC    XL4'30502006',CL6'2305-1'
         DC    XL4'30502009',CL6'3330  '
         DC    XL4'3050200D',CL6'3330-1'
         DC    X'FF'
         SPACE 3
OUTR1    DC    CL9' DELETE ('
OUTR1NAM DC    CL44' '
         DC    CL27') NVSAM NSCR -'
OUTR2    DC    CL8'    CAT('
OUTR2CAT DC    CL52' '
         DC    CL20')'
OUTR3    DC    CL20' DEFINE NVSAM (NAME('
OUTR3NAM DC    CL44' '
         DC    CL16') -'
OUTR4    DC    CL80'  VOL( -'
OUTR5    DC    CL80'  DEVT( -'
OUTR6    DC    CL80'  FSEQN( -'
         SPACE 3
CATNAMES DC    X'FF00',CL52' '
         DC    X'FF00',CL52' '
         DC    X'FF00',CL52' '
         DC    X'FF00',CL52' '
         DC    X'FF00',CL52' '
         SPACE 3
FRTIME   DC    F'0'
FRDATE   DC    PL4'0'
DBLWD    DC    D'0'
OLDSEQ   DC    XL8'00'
TRTAB    DC    CL16'0123456789ABCDEF'
MASK     DC    XL5'4020202021'
         PRINT ON,NOGEN
         SPACE 3
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBA,LRECL=60, *
               BLKSIZE=60
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),RECFM=F,LRECL=80,      *
               BLKSIZE=80,EODAD=EODSYIN
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),EODAD=EODSYUT1,       *
               BFTEK=A
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),LRECL=80,RECFM=FB,    *
               BLKSIZE=3040
         PRINT ON GEN
         LTORG
TRTTAB   DC    XL256'00'
         ORG   TRTTAB+97
         DC    X'FF'
         ORG   TRTTAB+64
         DC    X'FF'
         ORG
         END
