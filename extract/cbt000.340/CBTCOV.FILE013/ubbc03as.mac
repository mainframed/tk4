         PRINT NOGEN
PGMREC   CSECT
*
***********************************************************************
*                                                                     *
*        DAS PROGRAMM ERSTELLT FUER JEDES WAEHREND EINES MONATS       *
*        GELAUFENE PROGRAMM EINEN DATENSATZ (SH. DEFINITION SSATZ)    *
*        MIT STATISTISCHEN WERTEN                                     *
*        AUSSERDEM WIRD JE 1 DATENSATZ FUER DIE ZUORDNUNGSLISTEN      *
*        JOBS / PROGRAMME UND PROGRAMME / JOBS ERSTELLT               *
*                                                                     *
*        EINGABE    SMF RECORDS TYPE 4/34 (STEP-SAETZE) SORTIERT      *
*                   NACH PROGRAMM-NAME                                *
*                                                                     *
*        AUSGABE    DATENSATZ JE PROGRAMM MIT STATISTISCHEN WERTEN    *
*                   2 DATENSAETZE JE RECORD 4/34 FUER ZUORDN.-LISTEN  *
*                                                                     *
*        DIE ERMITTLUNG DER WERTE                                     *
*                                                                     *
*                   ERRECHNETE LAUFZEIT                               *
*                   GESAMT ZUGRIFFE                                   *
*                   PLATTEN DATEIEN                                   *
*                   PLATTEN ZUGRIFFE                                  *
*                   BAND DATEIEN                                      *
*                   BAND ZUGRIFFE                                     *
*                   UR DATEIEN                                        *
*                   UR ZUGRIFFE                                       *
*                   SYSIN/TGET DATEIEN                                *
*                   SYSIN/TGET KARTEN                                 *
*                   SYSOUT/TPUT DATEIEN                               *
*                   SYSOUT/TPUT ZEILEN                                *
*                   PROGRAMMKOSTEN                                    *
*                                                                     *
*        ERFOLGT IM UNTERPROGRAMM PGMJOBBW                            *
*                                                                     *
*        AUFRUF     CALL  PGMJOBBW,(SMFREC,CBER)                      *
*                                                                     *
*                                 SMFREC = SMF-RECORD                 *
*                                 CBER   = ERGEBNISFELDER             *
*                                          (SH. DEFINITIONEN)         *
*                                                                     *
***********************************************************************
*                                                                     *
*        DAS UNTERPROGAMM  PGMJOBBW (BEWERTUNG) MUSS VOM JEWEILIGEN   *
*        ANWENDER SELBST ERSTELLT WERDEN                              *
*                                                                     *
***********************************************************************
***********************************************************************
         EJECT
*
******** VERWENDUNG DER REGISTER **************************************
*                                                                     *
R0       EQU   0                                                      *
R1       EQU   1                                                      *
R2       EQU   2        ARBEITS REGISTER                              *
R3       EQU   3        ARBEITS REGISTER                              *
R4       EQU   4                                                      *
R5       EQU   5                                                      *
R6       EQU   6                                                      *
R7       EQU   7                                                      *
R8       EQU   8                                                      *
R9       EQU   9                                                      *
R10      EQU   10                                                     *
R11      EQU   11       ADR. SMF - SATZ                               *
R12      EQU   12       BASIS REGISTER                                *
R13      EQU   13       SAVE AREA                                     *
R14      EQU   14                                                     *
R15      EQU   15                                                     *
*                                                                     *
***********************************************************************
         EJECT
*
******** PROGRAMM - ANFANG ********************************************
*
         B     12(R15)
         DC    C'PGMREC  '
         STM   R14,R12,12(R13)
         BALR  R12,0
         USING *,R12
         ST    R13,SVA+4
         LR    R2,R13
         LA    R13,SVA
         ST    R13,8(R2)
         OPEN  (SMF,,PGM,(OUTPUT),ZOL,(OUTPUT))
         LA    R11,SMFREC+4
LESEN    GET   SMF,SMFREC
         CLI   1(R11),X'04'
         BE    CPUT                     STEP-SATZ BATCH
         CLI   1(R11),X'22'
         BE    CPUT                     STEP-SATZ TSO
         B     LESEN
CPUT     LA    R2,102(R11)             ADR.
         AH    R2,102(R11)              CPU-ZEIT
         CLC   1(3,R2),=X'000000'      STEP-SATZ
         BE    LESEN                    MIT CPU-ZEIT = 0
************** DATEN FUER ZUORDNUNGSLISTEN
         MVC   ZO1,=C'JP'              JOB/PROGRAMM
         MVC   ZO2,14(R11)             JOB
         MVC   ZO3,54(R11)             PROGRAMM
         PUT   ZOL,ZBER
         MVC   ZO1,=C'PJ'              PROGRAMM/JOB
         MVC   ZO2,54(R11)             PROGRAMM
         MVC   ZO3,14(R11)             JOB
         PUT   ZOL,ZBER
**************
WEICH1   NOP   VERGL
         OI    WEICH1+1,X'F0'
SPEICH   MVC   PROGR,54(R11)           PROGRAMM-NAME
         ZAP   AUFR,=X'0F'             LOESCHEN
         MVC   AUFR+5(185),AUFR         SSATZ
         ZAP   RCPU,=X'0F'             LOESCHEN
         MVC   RCPU+6(12),RCPU          ADD.-SPEICHER
VERGL    CLC   PROGR,54(R11)
         BNE   AUSGABE                 NEUER PROGRAMM-NAME
************** AUFRUFE
         AP    AUFR,=X'1F'             ADD. AUFRUFE
************** AUFRUFE MIT ABEND
         CLC   51(2,R11),=X'0000'
         BE    KON1                    KEIN COMPL. CODE
         CLI   1(R11),X'22'
         BE    KON1                    TSO SATZ
         TM    51(R11),X'80'
         BO    KON1                    USER COMPL. CODE
         AP    AUFRABE,=X'1F'          ADD. AUFRUFE MIT ABEND
         B     LESEN
************** REGION GEFORDERT
KON1     XC    WORT,WORT
         MVC   WORT+2(2),70(R11)
         L     R2,WORT
         CVD   R2,DWORT
         CP    DWORT+3(5),MREGFO
         BNH   KON2
         ZAP   MREGFO,DWORT+3(5)       REGION GEFORDERT MAXIMAL
KON2     AP    DREGFO,DWORT+3(5)       REGION GEFORDERT FUER DURCHSCHN.
************** REGION GENUTZT
         MVC   WORT+2(2),74(11)
         L     R2,WORT
         CVD   R2,DWORT
         CP    DWORT+3(5),MREGNU
         BNH   KON3
         ZAP   MREGNU,DWORT+3(5)       REGION GENUTZT MAXIMAL
KON3     AP    DREGNU,DWORT+3(5)       REGION GENUTZT FUER DURCHSCHN.
************** CPU ZEIT
         LA    R2,102(R11)
         AH    R2,102(R11)
         MVC   WORT+1(3),1(R2)
         L     R2,WORT
         CVD   R2,DWORT
         CP    DWORT+3(5),MCPU
         BNH   KON4
         ZAP   MCPU,DWORT+3(5)         CPU ZEIT MAXIMAL
KON4     AP    RCPU,DWORT+2(6)         CPU ZEIT FUER DURCHSCHN.
************** SMF LAUFZEIT
         MVC   WORT,2(R11)
         L     R2,WORT
         MVC   WORT,39(R11)
         L     R3,WORT
         CR    R2,R3
         BNL   KON5
         A     R2,=F'8640000'          START V. 24 UHR/STOP N. 24 UHR
KON5     SR    R2,R3
         CVD   R2,DWORT
         CP    DWORT+3(5),MSMFLZ
         BNH   KON6
         ZAP   MSMFLZ,DWORT+3(5)       SMF LAUFZEIT MAXIMAL
KON6     AP    RSMFLZ,DWORT+2(6)       SMF LAUFZEIT FUER DURCHSCH.
************** ERRECHN. LAUFZEIT, ZUGRIFFE, DATEIEN, KOSTEN
         ZAP   ERRLZ,=X'0F'            LOESCHEN SPEICHER
         MVC   ERRLZ+5(60),ERRLZ        FUER UNTERPROGRAMM
         CALL  PGMJOBBW,(SMFREC,CBER)
         CP    ERRLZ,MERRLZ
         BNH   KON7
         ZAP   MERRLZ,ERRLZ            ERRECHN. LAUFZEIT MAXIMAL
KON7     AP    RERRLZ,ERRLZ            ERRECHN. LAUFZEIT F. DURCHSCHN.
         CP    ZUGR,MZUGR
         BNH   KON8
         ZAP   MZUGR,ZUGR              GESAMT ZUGRIFFE MAXIMAL
KON8     AP    DZUGR,ZUGR              GESAMT ZUGRIFFE FUER DURCHSCHN.
         CP    PLDAT,MPLDAT
         BNH   KON9
         ZAP   MPLDAT,PLDAT            PLATTEN DATEIEN MAXIMAL
KON9     AP    DPLDAT,PLDAT            PLATTEN DATEIEN FUER DURCHSCHN.
         CP    PLZUG,MPLZUG
         BNH   KON10
         ZAP   MPLZUG,PLZUG            PLATTEN ZUGRIFFE MAXIMAL
KON10    AP    DPLZUG,PLZUG            PLATTEN ZUGRIFFE FUER DURCHSCHN.
         CP    BDDAT,MBDDAT
         BNH   KON11
         ZAP   MBDDAT,BDDAT            BAND DATEIEN MAXIMAL
KON11    AP    DBDDAT,BDDAT            BAND DATEIEN FUER DURCHSCHN.
         CP    BDZUG,MBDZUG
         BNH   KON12
         ZAP   MBDZUG,BDZUG            BAND ZUGRIFFE MAXIMAL
KON12    AP    DBDZUG,BDZUG            BAND ZUGRIFFE FUER DURCHSCHN.
         CP    URDAT,MURDAT
         BNH   KON13
         ZAP   MURDAT,URDAT            UR DATEIEN MAXIMAL
KON13    AP    DURDAT,URDAT            UR DATEIEN FUER DURCHSCHN.
         CP    URZUG,MURZUG
         BNH   KON14
         ZAP   MURZUG,URZUG            UR ZUGRIFFE MAXIMAL
KON14    AP    DURZUG,URZUG            UR ZUGRIFFE FUER DURCHSCHN.
         CP    SIDAT,MSIDAT
         BNH   KON15
         ZAP   MSIDAT,SIDAT            SYSIN DATEIEN MAXIMAL
KON15    AP    DSIDAT,SIDAT            SYSIN DATEIEN FUER DURCHSCHN.
         CP    SIKAR,MSIKAR
         BNH   KON16
         ZAP   MSIKAR,SIKAR            SYSIN KARTEN MAXIMAL
KON16    AP    DSIKAR,SIKAR            SYSIN KARTEN FUER DURCHSCHN.
         CP    SODAT,MSODAT
         BNH   KON17
         ZAP   MSODAT,SODAT            SYSOUT DATEIEN MAXIMAL
KON17    AP    DSODAT,SODAT            SYSOUT DATEIEN FUER DURCHSCHN.
         CP    SOZEI,MSOZEI
         BNH   KON18
         ZAP   MSOZEI,SOZEI            SYSOUT ZEILEN MAXIMAL
KON18    AP    DSOZEI,SOZEI            SYSOUT ZEILEN FUER DURCHSCHN.
         CP    KOSTEN,MKOSTEN
         BNH   KON19
         ZAP   MKOSTEN,KOSTEN          PROGRAMMKOSTEN MAXIMAL
KON19    AP    DKOSTEN,KOSTEN          PROGRAMMKOSTEN FUER DURCHSCHN.
         B     LESEN
AUSGABE  ZAP   WORT,AUFR
         SP    WORT,AUFRABE
         BZ    SCHREIB                 ALLE PGM-AUFRUFE MIT ABEND
************** RECHNEN MINUTEN AUS HUNDERTSTEL SEKUNDEN
         ZAP   RF,MCPU
         DP    RF,=X'6F'
         ZAP   MCPU,RF+4(5)            CPU MAXIMAL
         ZAP   RF,MERRLZ
         DP    RF,=X'6F'
         ZAP   MERRLZ,RF+4(5)          ERRECHN. LAUFZEIT MAXIMAL
         ZAP   RF,MSMFLZ
         DP    RF,=X'6F'
         ZAP   MSMFLZ,RF+4(5)          SMF LAUFZEIT MAXIMAL
         ZAP   RF,RCPU
         DP    RF,=X'6F'
         ZAP   DCPU,RF+4(5)            CPU FUER DURCHSCHN.
         ZAP   RF,RERRLZ
         DP    RF,=X'6F'
         ZAP   DERRLZ,RF+4(5)          ERRECHN. LAUFZEIT F. DURCHSCHN.
         ZAP   RF,RSMFLZ
         DP    RF,=X'6F'
         ZAP   DSMFLZ,RF+4(5)          SMF LAUFZEIT FUER DURCHSCHN.
************** RECHNEN DURCHSCHNITT
         LA    R2,DREGFO
         LA    R3,DSOZEI
KON20    ZAP   RF,0(5,R2)
         DP    RF,WORT
         ZAP   0(5,R2),RF+1(5)
         LA    R2,5(R2)
         CR    R2,R3
         BNH   KON20
************** WENN BEI DURCHSCHNITT DATEI = 0 UND DAZUGEHOERENDE
************** ZUGRIFFE NICHT = 0, DANN DATEI = 1
         LA    R2,DPLDAT
         LA    R3,DSODAT
KON21    ZAP   5(5,R2),5(5,R2)
         BZ    KON22
         ZAP   0(5,R2),0(5,R2)
         BNZ   KON22
         AP    0(5,R2),=X'1F'
KON22    LA    R2,10(R2)
         CR    R2,R3
         BNH   KON21
************** RECHNEN FAKTOR
         ZAP   MERRLZ,MERRLZ
         BZ    KON23
         ZAP   RF,MCPU
         MP    RF,=X'01000F'
         DP    RF,MERRLZ
         ZAP   MFAKT,RF(5)
KON23    ZAP   DERRLZ,DERRLZ
         BZ    SCHREIB
         ZAP   RF,DCPU
         MP    RF,=X'01000F'
         DP    RF,DERRLZ
         ZAP   DFAKT,RF(5)
SCHREIB  PUT   PGM,SSATZ
WEICH2   NOP   ENDE1
         B     SPEICH
ENDE     OI    WEICH2+1,X'F0'
         B     AUSGABE
ENDE1    MVI   ZBER,C'Z'               ENDEMARKE
         MVC   ZBER+1(17),ZBER          DATEI
         PUT   ZOL,ZBER                 ZOL
         CLOSE (SMF,,PGM,,ZOL)
         L     R13,SVA+4
         LM    R14,R12,12(R13)
         XR    R15,R15                 RETURN-CODE = 0
         BR    R14
         EJECT
*
******** SPEICHER, BEREICHE, LITERALE *********************************
*
         DS    0D
SVA      DS    18F                     SAVE AREA
DWORT    DS    CL8                     DOPPEL WORT
WORT     DS    CL4                     WORT
RF       DS    CL10                    RECHENFELD
RCPU     DS    PL6                     CPU ZEIT    HUNDERTSTEL SEKUNDEN
RERRLZ   DS    PL6                     ERRECHN.LFZ HUNDERTSTEL SEKUNDEN
RSMFLZ   DS    PL6                     SMF LFZ     HUNDERTSTEL SEKUNDEN
         LTORG
         EJECT
*
******** SPEICHER FUER DEN AUFRUF DES UNTERPROGRAMMES PGMJOBBW
*
CBER     DS    0CL65
ERRLZ    DS    PL5      ERRECHN. LAUFZEIT   HUNDERTST. SEKUNDEN
ZUGR     DS    PL5      GESAMT ZUGRIFFE
PLDAT    DS    PL5      PLATTEN DATEIEN
PLZUG    DS    PL5      PLATTEN ZUGRIFFE
BDDAT    DS    PL5      BAND DATEIEN
BDZUG    DS    PL5      BAND ZUGRIFFE
URDAT    DS    PL5      UR DATEIEN
URZUG    DS    PL5      UR ZUGRIFFE
SIDAT    DS    PL5      SYSIN/TGET DATEIEN
SIKAR    DS    PL5      SYSIN/TGET KARTEN
SODAT    DS    PL5      SYSOUT/TPUT DATEIEN
SOZEI    DS    PL5      SYSOUT/TPUT ZEILEN
KOSTEN   DS    PL5      PROGRAMMKOSTEN      (9,2 / DM)
         EJECT
*
******** STATISTIK - SATZ *********************************************
*
SSATZ    DS    0CL198
PROGR    DS    CL8      PROGRAMM-NAME
AUFR     DS    PL5      AUFRUFE GESAMT
AUFRABE  DS    PL5      AUFRUFE MIT ABEND
MREGFO   DS    PL5      REGION GEFORDERT                    MAXIMAL
MREGNU   DS    PL5      REGION GENUTZT                      MAXIMAL
MCPU     DS    PL5      CPU ZEIT             (9,3 / MINUT.) MAXIMAL
MERRLZ   DS    PL5      ERRECHN. LAUFZEIT    (9,3 / MINUT.) MAXIMAL
MSMFLZ   DS    PL5      SMF LAUFZEIT         (9,3 / MINUT.) MAXIMAL
MFAKT    DS    PL5      FAKTOR               (9,3)          MAXIMAL
MKOSTEN  DS    PL5      PROGRAMMKOSTEN       (9,2 / DM)     MAXIMAL
MZUGR    DS    PL5      GESAMT ZUGRIFFE                     MAXIMAL
MPLDAT   DS    PL5      PLATTEN DATEIEN                     MAXIMAL
MPLZUG   DS    PL5      PLATTEN ZUGRIFFE                    MAXIMAL
MBDDAT   DS    PL5      BAND DATEIEN                        MAXIMAL
MBDZUG   DS    PL5      BAND ZUGRIFFE                       MAXIMAL
MURDAT   DS    PL5      UR DATEIEN                          MAXIMAL
MURZUG   DS    PL5      UR ZUGRIFFE                         MAXIMAL
MSIDAT   DS    PL5      SYSIN/TGET DATEIEN                  MAXIMAL
MSIKAR   DS    PL5      SYSIN/TGET KARTEN                   MAXIMAL
MSODAT   DS    PL5      SYSOUT/TPUT DATEIEN                 MAXIMAL
MSOZEI   DS    PL5      SYSOUT/TPUT ZEILEN                  MAXIMAL
DREGFO   DS    PL5      REGION GEFORDERT                    DURCHSCHN.
DREGNU   DS    PL5      REGION GENUTZT                      DURCHSCHN.
DCPU     DS    PL5      CPU ZEIT             (9,3 / MINUT.) DURCHSCHN.
DERRLZ   DS    PL5      ERRECHN. LAUFZEIT    (9,3 / MINUT.) DURCHSCHN.
DSMFLZ   DS    PL5      SMF LAUFZEIT         (9,3 / MINUT.) DURCHSCHN.
DFAKT    DS    PL5      FAKTOR               (9,3)          DURCHSCHN.
DKOSTEN  DS    PL5      PROGRAMMKOSTEN       (9,2 / DM)     DURCHSCHN.
DZUGR    DS    PL5      GESAMT ZUGRIFFE                     DURCHSCHN.
DPLDAT   DS    PL5      PLATTEN DATEIEN                     DURCHSCHN.
DPLZUG   DS    PL5      PLATTEN ZUGRIFFE                    DURCHSCHN.
DBDDAT   DS    PL5      BAND DATEIEN                        DURCHSCHN.
DBDZUG   DS    PL5      BAND ZUGRIFFE                       DURCHSCHN.
DURDAT   DS    PL5      UR DATEIEN                          DURCHSCHN.
DURZUG   DS    PL5      UR ZUGRIFFE                         DURCHSCHN.
DSIDAT   DS    PL5      SYSIN/TGET DATEIEN                  DURCHSCHN.
DSIKAR   DS    PL5      SYSIN/TGET KARTEN                   DURCHSCHN.
DSODAT   DS    PL5      SYSOUT/TPUT DATEIEN                 DURCHSCHN.
DSOZEI   DS    PL5      SYSOUT/TPUT ZEILEN                  DURCHSCHN.
         EJECT
*
******** ZUORDNUNGSLISTEN - SATZ **************************************
*
ZBER     DS    0CL18
ZO1      DS    CL2
ZO2      DS    CL8
ZO3      DS    CL8
         EJECT
*
******** DCB / SMF - SATZ *********************************************
*
SMF      DCB   DDNAME=SMFDAT,DSORG=PS,MACRF=GM,EODAD=ENDE
*
*
PGM      DCB   DDNAME=PGMDAT,DSORG=PS,MACRF=PM
*
*
ZOL      DCB   DDNAME=ZOLDAT,DSORG=PS,MACRF=PM
*
*
SMFREC   DS    CL16384
*
*
         END
