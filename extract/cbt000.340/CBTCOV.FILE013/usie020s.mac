.*********************************************************************
.*                                                                    *
.*    TPUCLEAR  WRITTEN BY SIEMENS AG, MUENCHEN, DEPT DV VGR TPU1     *
.*                                                                    *
.*    THE FIRST 118 RECORDS ARE TWO MACROS 'EIN' AND 'AUS'            *
.*    AND MERELY PERFORM NORMAL PROGRAM START AND END FUNCTIONS.      *
.*                                                                    *
.*    THE PROGRAM TPUCLEAR CONSISTS OF THE OTHER 8 RECORDS AND WILL   *
.*    ERASE A 3277 SCREEN OF DATA AND POSITION THE CURSOR AT THE      *
.*    TOP OF THE SCREEN.                                              *
.*                                                                    *
.*    THIS PROGRAM IS USED BY THE CLIST PDS AND SUBCLISTS.            *
.*                                                                    *
.*********************************************************************
         MACRO
&NAME    EIN   &BASE=R12,&TYPE=BATCH
         GBLC  &ANTYPE
&ANTYPE  SETC  'BATCH'
         AIF   ('&TYPE' EQ 'BATCH').AN1
         AIF   ('&TYPE' EQ 'RENT').AN0
         MNOTE 8,'INVALID TYPE PARAMETER ON BEGIN MACRO'
         MEXIT
.AN0      ANOP
&ANTYPE  SETC  'RENT'
.AN1      ANOP
&NAME    CSECT
.*  MNOTE *,'*********************************************************'
.*  MNOTE *,'       MACRO  EIN                      JULY 9 1979 J.A.S*'
.*  MNOTE *,'       POSITIONAL PARAMETER:- NAME                      *'
.*  MNOTE *,'       KEYWORD PARAMETERS  :- BASE=(REG 1,REG 2,REG 3), *'
.*  MNOTE *,'                              TYPE=RENT/BATCH           *'
.*  MNOTE *,'       DEFAULTS     EIN BASE=R12,TYPE=BATCH             *'
.*  MNOTE *,'       EIN PROVIDES - CSECT NAME                        *'
.*  MNOTE *,'                    - REGISTER EQUATES                  *'
.*  MNOTE *,'                    - ADDRESSABILITY                    *'
.*  MNOTE *,'                    - SAVE AREA CHAINING                *'
.*  MNOTE *,'                    - LOAD MODULE LABELS                *'
.*  MNOTE *,'*********************************************************'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   R14,R12,12(R13)
         BALR  &BASE(1),R0
         USING *,&BASE(1)
         AIF   (N'&BASE EQ 1).AN2
         LA    &BASE(2),4095(&BASE(1))
         LA    &BASE(2),1(&BASE(2))
         USING *+4088,&BASE(2)
         AIF   (N'&BASE EQ 2).AN2
         LA    &BASE(3),4095(&BASE(2))
         LA    &BASE(3),1(&BASE(3))
         USING *+4088,&BASE(3)
         AIF   (N'&BASE EQ 3).AN2
         MNOTE 8,'3 BASE REGISTERS EXCEEDED - MACRO NOT EXPANDABLE'
.AN2     ANOP
         AIF   ('&TYPE' EQ 'RENT').AN3
         ST    R13,SAVEAREA+4
         LA    R11,SAVEAREA
         ST    R11,8(R13)
         LR    R13,R11
.AN3     ANOP
         B     BEGIN01
         DC    CL8'&NAME'
         DC    CL16'&SYSDATE.&SYSTIME'
         AIF   ('&TYPE' EQ 'RENT').AN4
SAVEAREA DC    18F'0'
.AN4     ANOP
PATCH    DC    14F'0'
BEGIN01  DS    0H
         EJECT
         MEND
         MACRO
&NAME    AUS   &RC=0
         GBLC  &ANTYPE           GLOBAL SET UP BY MACRO EIN
&NAME    DS    0H
.*       MNOTE *,'****************************************************'
.*       MNOTE *,'  MACRO AUS                 9 JULY 1979    J.A.S   *'
.*       MNOTE *,'     KEYWORD PARAMETERS :- RC=(REG)/ADDRESS/VALUE*'
.*       MNOTE *,'     DEFAULT  RC=0                                 *'
.*       MNOTE *,'     AUS PROVIDES - REGISTER RELOADS               *'
.*       MNOTE *,'                  - SETS RETURN CODE               *'
.*       MNOTE *,'****************************************************'
         AIF   ('&ANTYPE' EQ 'BATCH').AUS1
         MNOTE 0,'WARNING - RE-ENTERANT PROGRAM R13 MUST BE INTACT'
         AGO   .AUS2
.AUS1    ANOP
         L     R13,SAVEAREA+4
.AUS2    ANOP
         LM    R14,R12,12(R13)
         AIF   ('&RC'(1,1) EQ '(').REG
         AIF   ('&RC'(1,1) LT '0' OR '&RC'(K'&RC,1) EQ ')').ADDR
         LA    R15,&RC
         AGO   .AUSEND
.REG     ANOP
         LR    R15,&RC
         AGO   .AUSEND
.ADDR    ANOP
         L     R15,&RC
.AUSEND  ANOP
         BR    R14
         MEND
CLEAR    TITLE 'CLEAR SCREEN COMMAND'
TPUCLEAR EIN
         TPUT  BUFFER,L'BUFFER,FULLSCR  ERASE SCREEN
         AUS   RC=0
         EJECT
BUFFER   DC    X'27F54013'         ERASE/WRITE,NOP,INSERT CURSOR
         SPACE 3
         END
