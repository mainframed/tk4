         PRINT NOGEN
E15      CSECT
***********************************************************************
*                                                                     *
*        DAS PROGRAMM IST EINE MODIFICATION DES OS-SORT FUER          *
*        EXIT E15 BEI SORTIERUNG VON SMF-SAETZEN                      *
*                                                                     *
*        AN DIE SORT-PHASE WERDEN NUR DIE SAETZE (RECORD TYPE)        *
*                                                                     *
*              0 = IPL-SATZ                                           *
*              1 = WAITSATZ (SYSTEM)                                  *
*              4 = STEPSATZ                                           *
*             12 = EOD (SYSTEM)                                       *
*             34 = TSO-STEPSATZ                                       *
*             40 = TSO-DYNAMIC ALLOCATION                             *
*        WEITERGEGEBEN UND AUFSTEIGEND SORTIERT NACH                  *
*                                                                     *
*              1. START-DATUM                                         *
*              2. START-ZEIT                                          *
*                                                                     *
*        START-DATUM UND -ZEIT WERDEN BEI RECORD (4 34 40)            *
*        MIT DEM END-DATUM BZW. END-ZEIT VERTAUSCHT                   *
*                                                                     *
***********************************************************************
         SAVE  (14,12)
         BALR  2,0
         USING *,2
         ST    13,SBER+4
         LR    12,13
         LA    13,SBER
         ST    13,8(12)
         L     1,0(1)                  ADR.  SMF-SATZ IN REG. 1
         LTR   1,1                     DATEI-ENDE SORTIN
         BZ    ENDE                    JA
         CLI   5(1),X'00'
         BE    SORT                    IPL-SATZ
         CLI   5(1),X'01'              WAIT-SATZ
         BE    SORT
         CLI   5(1),X'0C'              EOD
         BE    SORT
         CLI   5(1),X'04'              STEP-SATZ
         BE    MOVE
         CLI   5(1),X'22'              TSO-STEPSATZ
         BE    MOVE1
         CLI   5(1),X'28'              TSO-DYN-ALLOC
         BE    MOVE1
         L     13,SBER+4
         RETURN (14,12),RC=4           SATZ ENTFERNEN
MOVE     MVC   DW,6(1)                 END-DATUM/ZEIT NCH DW
         MVC   6(8,1),43(1)        START-DATUM/ZEIT NACH END-DATUM/ZEIT
         MVC   43(8,1),DW          END-DATUM/ZEIT NACH START-DATUM/ZEIT
         B     SORT
MOVE1    MVC   DW,6(1)
         MVC   6(8,1),26(1)
         MVC   26(8,1),DW
SORT     L     13,SBER+4
         ST    1,24(13)                ADR. SMF-SATZ AN SORT
         RETURN (14,12),RC=0           SATZ AN SORT UEBERGEBEN
ENDE     L     13,SBER+4
         RETURN (14,12),RC=8           ENDE SORT MODIFIKATION
*
SBER     DS    18F
DW       DS    CL8
         END
