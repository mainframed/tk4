./ ADD NAME=MACROANF
               MACRO
&NAME MACROANF &V,&CD=S,&LC=0,&BR=1,&EP=,&ID=
.*       * CD= STANRDWERT IST *S* WIE *START*
.*       *     ZULAESSIG  AUCH  *C* WIE *CSECT*
.*       * LC= STANDARDWERT IST *0*, SETZT DEN ZUORDNUNGSZAEHLER AUF 0
.*       *     NUR ZULAESSIG IN VERBINDUNG MIT *START*, SIEHE CD=
.*       * BR= STANDARDWERT IST *1* WIE *EIN BASISREGISTER=BASIS1= R12*
.*       *     ZULAESSIG IST 0, 1, 2 ODER 3
.*       * EP= NAME DES EINGANGSPUNKTES WENN ANGEGEBEN, SO WIRD DER
.*       *     MERKMAL DES SAVE-MAKROS IN DER MAKROAUFLOESUNG
.*   *    ID= IDENTIFIKATION, WIE BEIM SAVE-MAKRO ANGEGEBEN WERDEN
.*       *    KANN
.*       * GLOBALE VARIABLE ZUM TEST GEGEN DOPPELDEFINITIONEN DER
.*       *     REGISTER
               GBLB &GLBIT BETROFFEN SIND DIE EQU'S
               GBLA &GLBR
               LCLA &SETBR ZUM TEST FUER BR=
               AIF (T'&NAME EQ 'O').M1
.M2            AIF ('&CD' EQ 'S').M3
               AIF ('&CD' EQ 'C').M4
         MNOTE 4,'CD= IST UNGUELTIG'
.M3      MNOTE *,'START GENERIERT'
&NAME START &LC
               AGO .M5
.M4      MNOTE *,'CSECT GENERIERT'
&NAME CSECT
.M5            ANOP
&SETBR    SETA &BR
               AIF (&GLBIT).M18
         MNOTE *,'SYMBOLISCHE REGISTER GENERIERT'
R15   EQU 15 VERZWEIG-REGISTER
R14   EQU 14 RUECKKEHR-REGISTER
R13   EQU 13 REGISTER FUER DEN LAUFENDEN SICHERUNGSBEREICH
R12   EQU 12
R11   EQU 11
R10   EQU 10
R9    EQU 9
R8    EQU 8
R7    EQU 7
R6    EQU 6
R5    EQU 5
R4    EQU 4
R3    EQU 3
R2    EQU 2 BEI TRT - EINGABEDATENPRUEFUNG VERWENDET
R1    EQU 1 PARAMETER-REGISTER
R0    EQU 0 PARAMETER-REGISTER
R09      EQU   9
R08      EQU   8
R07      EQU   7
R06      EQU   6
R05      EQU   5
R04      EQU   4
R03      EQU   3
R02      EQU   2
R01      EQU   1
R00      EQU   0
.M19           AIF (&SETBR EQ 0).M6
               AIF (&SETBR GE 1).M8
.M9            AIF (&SETBR GE 2).M10
.M12           ANOP
               AIF (&GLBR GT &SETBR).M22
&GLBR SETA &SETBR
.M22           ANOP
               USING *,R15
MACR&SYSNDX EQU *
&EP   SAVE (14,12),,&ID
               AIF (T'&EP EQ 'O').M13
               AIF ('&EP' EQ '&SYSECT').M21
               ENTRY &EP
         MNOTE *,'ENTRY - EINGANGSPUNKT GENERIERT'
.M13           CNOP  2,4
               ST    R13,*+22
               LR    R12,R13
               LA    R13,*+12
               ST    R13,8(R12)
               B     *+19*4
               AIF   (NOT &GLBIT).M16
               DS    18F
               AGO   .M17
.M16           DS    0F
               DC    F'0' WORT 0 NUR VON PL/1 VERWENDET
               DS  F    WORT 1 A(SAVEAREA IM RUFENDEB PROGRAMM)
               DS    F    WORT 2 A(SAVEAREA IM GERUFENEN PROGRAMM)
               DS    F    WORT 3 R14
               DS    F    WORT 4 R15
               DS    F    WORT 5 R0    ** BEI          R14
               DS    F    WORT 6 R1    ** ZUGRIFFS-    R15
               DS    F    WORT 7 R2    ** ROUTINEN     R0
               DS    F    WORT 8 R3    ** WERDEN       R1
               DS    F    WORT 9 R4    ** HIER         R2
               DS    F    WORT10 R5    ** FOLGENDE     R3
               DS    F    WORT11 R6    ** REGISTER     R4
               DS    F    WORT12 R7    ** VON DER      R5
               DS    F    WORT13 R8    ** ZUGRIFFS-    R6
               DS    F    WORT14 R9    ** ROUTINE      R7
               DS    F    WORT15 R10 BASIS3    ** GESPEICHERT R8
               DS    F    WORT16 R11 BASIS2
               DS    F    WORT17 R12 BASIS1
.M17           AIF (&SETBR EQ 0).M14
               AIF ('&V' EQ 'V').M25
               BALR BASIS1,0
               USING *,BASIS1
.M26           ANOP
         MNOTE *,'BASIS1 BEREITGESTELLT, GELADEN'
.M24           ANOP
               AIF (&SETBR EQ 1).M14
               AIF (&SETBR EQ 2).M15
               AIF ('&V' EQ 'V').M27
               USING *+4096,BASIS2,BASIS3
.M28           ANOP
               LA  BASIS2,4095
               LR  BASIS3,BASIS2
               LA  BASIS2,1(BASIS1,BASIS2)
               LA  BASIS3,1(BASIS2,BASIS3)
         MNOTE *,'BASIS2 UND BASIS3 BEREITGESTELLT, GELADEN'
.M14           ANOP
&GLBIT    SETB 1
*        *  ENDE DES VORSPANNES
               MEXIT
.M15           AIF ('&V' EQ 'V').M29
               USING *+4096,BASIS2
.M30           ANOP
               LA  BASIS2,4095
               LA  BASIS2,1(BASIS1,BASIS2)
         MNOTE *,'BASIS2 BEREITGESTELLT, GELADEN'
               AGO .M14
.M18           AIF (&GLBR GE &SETBR).M12
               AIF (&GLBR EQ 0).M19
               AIF (&GLBR EQ 1).M9
               AIF (&GLBR EQ 2).M11
               AGO .M20
.M1      MNOTE 12,'UNBENANNTE CONTROL-SECTION'
               AGO .M2
.M6      MNOTE *,'KEINE BASISREGISTER GENERIERT'
               AGO .M12
.M8      MNOTE *,'BASIS1 GENERIERT'
BASIS1    EQU 12 *** BASISREGISTER 1 ***
               AGO .M9
.M10     MNOTE *,'BASIS2 GENERIERT'
BASIS2    EQU 11 *** BASISREGISTER 2 ***
               AIF (&SETBR GE 3).M11
               AGO .M12
.M11     MNOTE *,'BASIS3 GENERIERT'
BASIS3    EQU 10 *** BASISREGISTER 3 ***
               AIF (&SETBR EQ 3).M12
.M20     MNOTE 4,'BR= ZU HOCH'
               AGO .M12
.M21     MNOTE 4,'EP = CSECT-NAME'
               AGO .M13
.M25           USING MACR&SYSNDX,BASIS1
               LR  BASIS1,R15
         DROP  R15                     DROP BASE 15 ADRESSABILITY
               AGO .M26
.M27           USING MACR&SYSNDX+4096,BASIS2,BASIS3
               AGO .M28
.M29           USING MACR&SYSNDX+4096,BASIS2
               AGO .M30
               MEND
