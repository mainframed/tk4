./  ADD        NAME=UXXX02BS
MEMBIN   DIESES PROGRAMM WIRD VON MEMBLIST ALS SUBTASK AUFGERUFEN UND
         ERSTELLT AUS MEMBERNAMEN, DATASETNAMEN UND BLATTNUMMER EINEN
         INDEX ZU DER VON MEMBLIST ERZEUGTEN LISTE.
         EJECT
MEMBINDX CSECT
         SAVE  (14,12)
         USING MEMBINDX,15
         ST    13,SAVE+4
         LR    2,13
         LA    13,SAVE
         ST    13,8(2)
         B     SKIPSAVE
SAVE     DS    18F
SKIPSAVE EQU   *
         DROP  15
         USING SAVE,13
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         B     SKIPCOPY
         DC    C'COPYRIGHT BY HADSCHI HALEF SIMANDL, BAGDAD'
SKIPCOPY EQU   *
         EJECT
         USING PARMS,R2   DSECT UEBERGEBENE PARAMETER
         USING IHADCB,R3  DUMMYDCB
         USING INDXCOL,R5 INDEXZEILE
         LR    R2,R1
OPENSW   EQU   *
         LA    R3,PRINT2
         OPEN  (PRINT2,OUTPUT)      EROEFFNEN INDEXLIST
         TM    DCBOFLGS,DCBOFOPN
         BO    OPENOK
         L     R12,PSYSOUT          FEHLERROUTINE
         PUT   (R12),MELDNG11
         L     R12,PECB1
         POST  (R12),255            VERSTAENDIGUNG DES HAUPTPROGRAMMS
         B     ENDE2                ABNORMALES ENDE
OPENOK   EQU   *
         L     R12,PTITLE           UEBERTRAGEN VON ZEIT, DATUM,
         MVC   TITLE1,0(R12)        UEBERSCHRIFTSTEILEN AUS HAUPTPROGR.
         L     R12,PZEIT
         MVC   ZEIT,0(R12)
         L     R12,PDATUM
         MVC   DATUM,0(R12)
         L     R12,PECB1
         POST  (R12)                 VERSTAENDIGUNG MIT HAUPTPROGRAMM
         LA    R3,3               DASS ANFANGSARBEITEN ABGESCHLOSSEN
         LA    R4,50
         LA    R5,TAB+1
         LR    R9,R5
         L     R6,PECB2
         L     R12,PECB3
         POST  (R12)              VERSTAENDIGUNG MIT HAUPTPROGRAMM,
         L     R7,PECB4           BEREIT ZUR UEBERNAHME VON DATEN
         L     R8,PSAVER5
         L     R2,PBLATT
SKIPOPEN EQU   *
ANF      EQU   *
         WAIT  ECB=(R6)           WARTEN OB NEUE DATEN VORHANDEN
         NI    0(R6),X'3F'
         CLI   3(R6),X'FF'        VERGLEICH OB PROGRAMMENDE
         BE    ENDE
         L     R10,0(R8)          VERGLEICH OB VERZWEIGUNG ZU
         CLC   CHAR,110(R10)      BUCHSTABENWECHSEL NOTWENDIG
         BNE   NEWCHAR
MVCDS    EQU   *
         MVC   DSN,80(R10)        UEBERTRAGEN VON MEMBERNAMEN,
         MVC   MEMB,110(R10)      DATASETNAMEN UND BLATTNUMMER AUS
         MVC   NUMMER,1(R2)       DEM HAUPTPROGRAMM
         POST  (R7)               VERSTAENDIGUNG AN DAS HAUPTPROGRAMM,
         LA    R5,133(R5)         DASS DATEN UEBERNOMMEN
         BCT   R4,POSTECB3        VERZWEIGUNG NACH BEDARF ZU SPALTEN-
         BCT   R3,BNEWCOL         ODER BLATTWECHSEL
         B     BHDR
BNEWCOL  BAL   R14,NEWCOL
         B     POSTECB3
BHDR     BAL   R14,HDR
         BAL   R14,PUT
         BAL   R14,CLEAR
POSTECB3 POST  (R12)             VERSTAENDIGUNG AN DAS HAUPTPROGRAMM,
         B     ANF               DASS WEITERE DATEN UEBERNOMMEN WERDEN
         SPACE 2
CLEAR    EQU   *                 UEBERSCHREIBEN DER AUSGABETABELLE
         ST    R14,CLEAR14       MIT BLANKS UND SETZEN DES ASAKONTROLL-
         LA    R5,TAB            ZEICHENS FUER ERSTE ZEILE
         LA    R4,50
BLANK    MVC   0(133,R5),=CL133' '
         LA    R5,133(R5)
         BCT   R4,BLANK
         LA    R3,3
         LA    R4,50
         LA    R5,TAB+1
         LR    R9,R5
         MVI   TAB,C'-'
         L     R14,CLEAR14
         BR    R14
         SPACE 2
NEWCOL   EQU   *                SETZEN DER REGISTER FUER DIE ADDRESSIE-
         ST    R14,NEWCOL14     RUNG DER AUSGABETABELLE AUF WERTE FUER
         LA    R9,44(R9)        EINE NEUE SPALTE UND SETZEN DES ZEILEN-
         LR    R5,R9            ZAHLERS
         LA    R4,50
         L     R14,NEWCOL14
         BR    R14
         SPACE 2
NEWCHAR  EQU   *                TEST OB NOCH 4 ZEILEN IN SPALTE FREI
         CH    R4,=H'4'         WENN NICHT, TEST OB NOCH SPALTE(N)
         BNL   MVCCHAR          FREI, WENN NICHT, BLATTWECHSEL
         CH    R3,=H'1'
         BNE   NEWCOLB
         BAL   R14,HDR
         BAL   R14,PUT
         BAL   R14,CLEAR
         B     MVCCHAR
NEWCOLB  BAL   14,NEWCOL
         BCTR  R3,R0
MVCCHAR  LA    R5,133(R5)         UEBERTRAGEN DES NEUEN BUCHSTABEN
         MVC   CHAR,110(R10)      IN DIE AUSGABETABELLE
         MVC   0(44,R5),CHARALL   SETZEN DER REGISTER ADDRESSIERUNG
         LA    R5,266(R5)         UND ZEILENZAEHLER
         SH    R4,=H'3'
         B     MVCDS
         SPACE 3
PUT      EQU   *                  DRUCKEN DER ZEILEN AUS DER
         ST    R14,PUT14          AUSGABETABELLLE
         LA    R5,TAB
         LA    R4,50
PUTD     PUT   PRINT2,(R5)
         LA    R5,133(R5)
         BCT   R4,PUTD
         L     R14,PUT14
         BR    R14
         SPACE 3
HDR      EQU   *
         ST    R14,PUTHDR14            ERHOHEN DES BLATTZAEHLERS
         L     R3,PAGECNT              UND DRUCKEN DER TITELZEILEN
         LA    R3,1(R3)
         ST    R3,PAGECNT
         CVD   R3,D
         MVC   BLATT,=X'402020202020'
         ED    BLATT,D+5
         LA    R3,HDR1
         LA    R11,4
PUTHDR   PUT   PRINT2,(R3)
         LA    R3,133(R3)
         BCT   R11,PUTHDR
         L     R14,PUTHDR14
         BR    R14
         SPACE 3
ENDE     EQU   *             DRUCKEN DER LETZTEN UEBERSCHRIFTSZEILEN
         BAL   R14,HDR       UND DER LETZTEN AUSGABETABELLE
         BAL   R14,PUT
         CLOSE (PRINT2)
ENDE2    EQU   *
         L     R13,SAVE+4
         RETURN (14,12)
         EJECT
*********************** DEFINITIONEN *****************************
* BEREICHE ZUR REGISTERRETTUNG
CLEAR14  DS    F
NEWCOL14 DS    F
PUT14    DS    F
PUTHDR14 DS    F
* ZEILEN FUER BUCHSTABENWECHSEL
CHARALL  DC    CL44' '
         ORG   CHARALL
         DS    CL15
         DC    C'* '
CHAR     DC    X'FF'
         DC    C' *'
         ORG   CHARALL+44
* UEBERSCHRIFTSZEILEN
HDR1     DC    CL133' '
         ORG   HDR1
         DC    C'1'
         DC    C'MD800102'
         DS    CL93
ZEIT     DS    CL8
         DS    CL2
DATUM    DS    CL8
         DS    CL2
         DC    C'BLATT'
BLATT    DS    CL6
         ORG   HDR1+133
HDR2     DC    CL133' '
         ORG   HDR2
TITLE1   DC    C' PARAMETERLISTUNG'
         DS    CL116
         ORG   HDR2+133
HDR3     DC    CL133' '
         ORG   HDR3
         DC    C'-'
         DS    CL55
         DC    C'I N D E X'
         DS    CL68
         ORG   HDR3+133
HDR4     DC    CL133' '
         ORG   HDR4
         DC    C'-'
         DS    CL2
         DC    3C'NAME       LIBRARY                 BLATT    '
         ORG   HDR4+133
* FEHLERROUTINE
MELDNG11 DC    133C' '
         ORG   MELDNG11+1
         DC    C'OPEN FOR PRINT2 DD-CARD FAILED'
         ORG   MELDNG11+133
PAGECNT  DC    F'0'
D        DS    D
* DCB FUER DRUCKAUSGABE
PRINT2   DCB   DDNAME=PRINT2,MACRF=PM,LRECL=133,BLKSIZE=11970,         *
               RECFM=FBA,DSORG=PS
         LTORG
* DUMMYDCB
         DCBD
* DUMMYSECTION FUER INDEXZEILE
INDXCOL  DSECT
MEMB     DS    CL8
         DS    CL3
DSN      DS    CL24
NUMMER   DS    CL5
         DS    CL4
* DUMMYSECTION FUER UEBERGEBENE PARAMETER
PARMS    DSECT
PSAVER5  DS    AL4
PBLATT   DS    AL4
PSYSOUT  DS    AL4
PTITLE   DS    AL4
PZEIT    DS    AL4
PDATUM   DS    AL4
PECB1    DS    AL4
PECB2    DS    AL4
PECB3    DS    AL4
PECB4    DS    AL4
MEMBINDX CSECT
* AUSGABETABELLE
TAB      DC    50CL133' '
         ORG   TAB
         DC    C'-'
         ORG   TAB+6650
         END   MEMBINDX
