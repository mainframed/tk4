* STAND 01/12/78
***********************************************************************
*                                                                     *
*  PROGRAMM    = MODAST                                               *
*                                                                     *
*  AUFRUF      = CALL MODAST,(VON,NACH,TYP,ERR)                       *
*        F     ERR  = FEHLERCODE = 0 KEIN FEHLER                      *
*                                = 1 FEHLER IN VON                    *
*        CL1   TYP  = 1  ABSTIMMBLATTNR UMFORMEN                      *
*              --------                                               *
*              VON(CL8)  ABSTIMMBLATTNR (EINGABE)                     *
*                        BLANKS AN BELIEBIGER STELLE                  *
*              NACH(CL8) UMGEFORMTE ABSTIMMBLATTNR                    *
*                                                                     *
*              TYP  = 2  ABSTIMMHINWEIS UNFORMEN                      *
*              --------                                               *
*              VON(CL10) ABSTIMMHINWEIS (EINGABE)                     *
*                        BLANKS AN BELIEBIGER STELLE                  *
*              NACH(CL10) ABSTIMMHINWEIS UMGEFORMT                    *
*                                                                     *
*                                                                     *
*  ZWECK       = ABSTIMMBLATTNR ODER ABSTIMMHINWEIS IN                *
*                EINHEITLICHE FORM BRINGEN UND AUF RICHTIGKEIT PRUEFEN*
*                                                                     *
*  ABSTIMMBLATT-NR = PROGRAMMNAME                                     *
*                  = PROGRAMM-NR. + EINSATZ-NR.                       *
*                                                                     *
*  ABSTIMMHINWEIS  = 1. ABSTIMMBLATT-NR + POSITIONS-NR                *
*                    2. FREIER AUFBAU, WENN * AN 1.STELLE             *
*                    3. HINWEIS AUF LOCHKARTENBEGLEITSCHEIN           *
*                                                                     *
***********************************************************************
MODAST   CSECT
         XSAVE 12,SAVE,MODAST
         SPACE 2
         AREG
         SPACE 2
         L     R11,0(,R1)               --> VON
         L     R10,4(,R1)               --> NACH
         L     R9,8(,R1)                --> TYP
         L     R8,12(,R1)               --> ERR
         SPACE 2
         XC    0(4,R8),0(R8)            FEHLERCODE = 0
         MVC   0(8,R10),=CL8' '         BLANKS --> NACH
         CLI   0(R9),C'2'
         BE    TYP2                     AUFRUFTYP = 2
         CLI   0(R9),C'4'
         BE    TYP4                     AUFRUF = 4
*  ABSTIMMBLATT-NR UMFORMEN
         MVC   LENG,=F'8'               LAENGE EIN = 8
         BAL   R7,COMPRESS             CALL BLANKEX
         CLC   LENG,=F'7'              LAENGE MUSS 7 SEIN
         BNE   FEHLER
         BAL   R7,TEST                 INHALT PRUEFEN      N
         B     FEHLER                  RUECKSPRUNG 1 AUS TEST
         B     RETURN                  RUECKSPRUNG 2 AUS TEST
*  ABSTIMMHINWEIS UMFORMEN
TYP2     EQU   *
         MVC   8(2,R10),=CL8' '
         MVC   LENG,=F'10'              LAENGE EIN = 10
         BAL   R7,COMPRESS             CALL BLANKEX
         CLI   0(R10),C'*'
         BE    RETURN                  ABSTIMMHINWEIS FREIER AUFBAU
         CLC   LENG,=F'10'
         BE    FEHLER                  ZU LANG
         MVC   DWORD(2),7(R10)
         MVC   8(2,R10),DWORD          POS.NR --> RECHTSBUENDIG
         MVI   7(R10),C' '
         BAL   R7,TEST                 INHALT 1.TEIL PRUEFEN
         B     TYP2K                   RUECKSPRUNG 1 AUS TEST
         B     TYP2PGM                 RUECKSPRUNG 2 AUS TEST
         SPACE 2
TYP2K    EQU   *
         CLC   8(2,R10),=C'  '         POS.NR MUSS FEHLEN
         BNE   FEHLER
         B     RETURN
         SPACE 2
TYP2PGM  EQU   *
         MVC   DWORD(2),8(R10)
         NC    DWORD(2),MASK           ) POS.NR = NUM. ?
         CLC   DWORD(2),MASK           )
         BNE   FEHLER
         B     RETURN
*  JOBNAME PRUEFEN
TYP4     EQU   *
         MVC   DWORD,0(R11)             JOBNAME
         LA    R10,DWORD
         CLI   DWORD+5,C'#'             FOLGEJOB KENNZEICHEN
         BNE   *+8
         MVI   DWORD+5,C'0'
         CLI   DWORD+4,C'#'             FOLGEJOB KENNZEICHEN
         BNE   *+8
         MVI   DWORD+4,C'0'
         BAL   R7,TEST
         B     FEHLER
         B     RETURN
         SPACE 2
*  AUS IST FEHLERHAFT
FEHLER   EQU   *
         MVC   0(4,R8),=F'1'            FEHLERCODE = 1
         B     RETURN
         SPACE 2
LENG     DS    F
         SPACE 2
RETURN   EQU   *
         XRETURN 0
         SPACE   2
*  PROGRAMMNAME PRUEFEN
TEST     EQU   *
         CLI   0(R10),C'A'             )
         BL    FEHLER                  ) 1.ZEICHEN = ALPHA ?
         TM    0(R10),X'30'            )
         BO    FEHLER                  )
*
         CLI   1(R10),C'A'             )
         BL    FEHLER                  ) 2.ZEICHEN = ALPHA ?
         TM    1(R10),X'30'            )
         BO    FEHLER                  )
*
         MVC   DWORD(5),2(R10)
         LA    R3,TAB                   -->1.PGM NAME
         LA    R4,PANZ                  ANZAHL PGM NAMEN
LOOP     EQU   *
         MVC   TYP,0(R3)                TYP = KZ AUS TAB
         NC    TYP,0(R9)                VERGLEICH MIT TYP AUS AUFRUF
         BZ    LOOPE                    UNGLEICH D.H. KEIN VERGLEICH
         SR    R5,R5
         IC    R5,1(,R3)                VERGLEICHSLAENGE - 1
         EX    R5,CLC                   TEST PGM NAME
         BE    FOUND
LOOPE    EQU   *
         LA    R3,3(R5,R3)              -->NAECHSTER PGM NAME
         BCT   R4,LOOP
         B     TNUM
FOUND    EQU   *
         EX    R5,MVC                   NUM.ZEICHEN IN GEPRUEFTES FELD
TNUM     EQU   *
         CLI   DWORD,C'K'
         BE    TESTK
         NC    DWORD(5),MASK           )
         CLC   DWORD(5),MASK           ) 3.-7.ZEICHEN = NUM. ?
         BNE   FEHLER                  )
         B     4(0,R7)
TESTK    EQU   *
         CLI   DWORD+4,C' '            7.ZEICHEN = BLANK ?
         BNE   FEHLER
         NC    DWORD+1(3),MASK         )
         CLC   DWORD+1(3),MASK         ) 4.-6.ZEICHEN = NUM. ?
         BNE   FEHLER                  )
         BR    R7
         SPACE 2
*  EINGABE LINKSBUENDIG ZUSAMMENSCHIEBEN
COMPRESS EQU   *
         CALL  BLANKEX,((R11),(R10),LENG)    BLANKS ENTFERNEN
         BR    R7
DWORD    DS    D
MASK     DC    5XL1'F0'
TYP      DS    CL1
CLC      CLC   DWORD(0),2(R3)
MVC      MVC   DWORD(0),MASK
         SPACE 2
*  PROGRAMMNAMEN TABELLE
PANZ     EQU   20
TAB      DS    0CL1
         DC    X'07',X'2',CL3'AST'
         DC    X'07',X'2',CL3'CTB'
         DC    X'07',X'2',CL3'DUP'
         DC    X'07',X'1',CL2'DU'
         DC    X'07',X'2',CL3'GEN'
         DC    X'07',X'1',CL2'GE'
         DC    X'07',X'2',CL3'KSL'
         DC    X'07',X'2',CL3'MER'
         DC    X'07',X'1',CL2'ME'
         DC    X'07',X'2',CL3'SIC'
         DC    X'07',X'1',CL2'SI'
         DC    X'07',X'2',CL3'SOR'
         DC    X'07',X'1',CL2'SO'
         DC    X'07',X'2',CL3'CTD'
         DC    X'07',X'2',CL3'KSL'
         DC    X'07',X'2',CL3'JAS'
         DC    X'07',X'2',CL3'MLT'
         DC    X'07',X'1',CL2'ML'
         DC    X'07',X'2',CL3'PAP'
         DC    X'07',X'2',CL3'LST'
         END
