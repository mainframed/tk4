         TITLE 'PRINT ALTERNATE TRACK UTILITY FOR 3330'
PALTTR   CSECT
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         ST    13,SAVE+4
         LA    13,SAVE
         B     START
RETURN   EQU   *
         L     13,SAVE+4
         LM    14,12,12(13)
         XR    15,15
         BR    14
SAVE     DC    18F'-1'
          SPACE 2
START    EQU   *
         OPEN  DCB
         L     2,DCB+44      DEB
         MVC   SAVEDEB(12),X'24'(2)
         SVC   252      INTO PK 0 ************************************
         MVC   X'24'(12,2),DISKC
         SVC   252      BACK TO ORIG PK ******************************
         OPEN  (SYSPRINT,(OUTPUT))
         BAL   11,PRINT
         LH    2,34(,2)      UCB  ADDR
         MVC   OUT(6),MSG1
         MVC   OUT+7(6),28(2)     MOVE VOL SER
         BAL   11,PRINT
         SPACE 2
         LH    5,MAXC
         LA    4,1
         SR    3,3
LOOPC    EQU   *
         LH    9,MAXH
         LA    8,1
         SR    7,7
LOOPH    EQU   *
         STH   3,SEEK+3
         STH   7,SEEK+5
         MVI   ECB,X'00'
         EXCP  IOB
         WAIT  ECB=ECB
         CLI   ECB,X'7F'
         BNE   EDIT1
INCORRL  EQU   *
         CLC   SEEK+3(4),DATA
         BNE   EDIT2
NEXT     EQU   *
         BXLE  7,8,LOOPH
         BXLE  3,4,LOOPC
         L     2,DCB+44
         SVC   252      INTO PK 0 ************************************
         MVC   X'24'(12,2),SAVEDEB
         SVC   252      BACK TO ORIG PK ******************************
         MVC   OUT+0(12),MSG6
         BAL   11,PRINT
         CLOSE   DCB
         CLOSE SYSPRINT
         B     RETURN
         EJECT
PRINT    EQU   *
         PUT   SYSPRINT,OUT
         MVI   OUT,X'40'
         MVC   OUT+1(120),OUT
         BR    11
         SPACE 2
EDIT2    STM   3,8,EDITS
         MVC   OUT+0(15),MSG2
         MVC   OUT+25(14),MSG3
         LA    8,OUT+16
         L     7,SEEK+3
         BAL   11,EDIT
         LA    8,OUT+40
         L     7,DATA
         BAL   11,EDIT
         BAL   11,PRINT
         LM    3,8,EDITS
         B     NEXT
          SPACE 2
EDIT1    STM   3,8,EDITS
         CLI   IOB+13,X'40'
         BNE   EDIT12
         CLI   IOB+15,X'08'
         BE    INCORRL
EDIT12   EQU   *
         MVC   OUT+0(18),MSG4
         MVC   OUT+28(14),MSG5
         LA    8,OUT+19
         L     7,SEEK+3
         BAL   11,EDIT
         LA    8,OUT+43
         L     7,ECB
         BAL   11,EDIT
         MVC   OUT+45(6),OUT+60
         BAL   11,PRINT
         LM    3,8,EDITS
         B     NEXT
          SPACE 2
EDIT     EQU   *
         LR    5,8
         LA    3,8
EDITL    SR    6,6
         SLDL  6,4
         STC   6,0(8)
         LA    8,1(,8)
         BCT   3,EDITL
         TR    0(8,5),TTABLE
         BR    11
TTABLE   DC    C'0123456789ABCDEF'
         EJECT
DCB      DCB   DDNAME=DISK,DSORG=PS,MACRF=E,IOBAD=IOB,DEVD=DA
IOB      DC    B'01000010',XL3'0'
         DC    A(ECB)
         DC    XL8'0'
         DC    A(CCW)
         DC    A(DCB)
         DC    XL8'0'
SEEK     DC    X'0000000048000100'
         DC    XL8'0'
ECB      DC    F'0'
CCW      CCW   X'39',SEEK+3,96,4       SEARCH HA EQ
         CCW   X'08',*-8,0,0           TIC
         CCW   X'16',DATA,0,16         READ R0
DATA     DC    2D'0'
DISKC    DC    3H'0' START BB CC  HH
MAXC     DC    H'403'        END CC
MAXH     DC    H'18'         END HH
         DC    H'0'          LENGTH
SAVEDEB  DC    12C' '        SAVE AREA FROM DEB
         EJECT
EDITS    DC    6F'-1'
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=FBA,            *
               BLKSIZE=605,LRECL=121
OUT      DC    CL121'1RZ-ROCHE PRINT ALTERNATE TRACK UTILITY'
MSG1     DC    C'0DISK='
MSG2     DC    C'0DEF.TRK(CCHH)='
MSG3     DC    C'ALT.TRK(CCHH)='
MSG4     DC    C'0HOME ADDR.(CCHH)='
MSG5     DC    C'ECB COMP.CODE='
MSG6     DC    C'0UTILITY END'
         END
