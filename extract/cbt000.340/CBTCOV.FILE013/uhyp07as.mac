REP      TITLE 'REPLACEPROGRAMM   UHYP07AS  =  MODREP '
         MACRO
&NAME    MACDCBDD &DCBADR
.**********************************************************************
.*                                                                    *
.*       DIESES MACRO LIEFERT IM REGISTER 1 DIE ADRESSE DES ZU
.*       EINEM NOCH NICHT EREOFFNETEN DCB GEHOERIGEN DD-ENTRIES.      *
.*       IST KEIN ENTSPRECHENDER DD-ENTRY VORHANDEN, D.H. DIE         *
.*       DD-KARTE FUER DEN DCB FEHLT, DANN IST REGISTER 1 = 0.        *
.*                                                                    *
.**********************************************************************
         LCLC  &C
         AIF   ('&DCBADR' EQ '').ERROR
&C       SETC  '&SYSNDX'
&NAME    STM   15,0,MAC&C.2     REGISTER SICHERN
         AIF   ('&DCBADR'(1,1) EQ '(').MAC2
         LA    15,&DCBADR           DCB-ADRESSE
.MAC1    SR    0,0
         MACTIOT
         LA    1,24(1)                 1.DD-ENTRY
MAC&C.1  IC    0,0(1)                  LAENGE DES DD-ENTRIES
         LTR   0,0                     LETZTER DD-ENTRY
         BZ    MAC&C.3
         CLC   4(8,1),40(15)            DD-NAMEN VERGLEICH TIOT-DCB
         BE    MAC&C.4
         AR    1,0                     NAECHSTER DD-ENTRY
         B     MAC&C.1
MAC&C.2  DC    2F'0'
MAC&C.3  SR    1,1
MAC&C.4  LM    15,0,MAC&C.2            REGISTER WIEDERHERSTELLEN
         MEXIT
.MAC2    LR    15,&DCBADR(1)           DCB-ADRESSE
         AGO   .MAC1
.ERROR   MACMES 2,MACDCBDD
         MEND
         MACRO
&NAME    MACTIOT
&NAME    L     1,16                    CVT-ADRESSE
         L     1,0(1)                  TCB-LISTE
         L     1,4(1)                  TCB-ADRESSE
         L     1,12(1)                 TIOT-ADRESSE
         MEND
         MACRO
         MACMES &CODE,&MAKRO
.***********************************************************
.*                                                         *
.*       MAKRO ZUR GENERIERUNG VON MNOTE-BEMERKUNGEN       *
.*             STANDARDTEXT IST   *** &MAKRO               *
.*                                                         *
.* CODE  TEXT                                              *
.* ****  ****                                              *
.*                                                         *
.*   1   MAKRO BEREITS EINGESETZT                          *
.*   2   PARAMETER FEHLT                                   *
.*   3   FEHLER IN PARAMETERANGABE                         *
.*                                                         *
.***********************************************************
         AIF   ('&CODE' EQ '' OR '&MAKRO' EQ '').NOTE1
         AIF   ('&CODE' EQ '0' OR '&CODE' GT '3').NOTE2
         AIF   ('&CODE' EQ '1').M1
         AIF   ('&CODE' EQ '2').M2
         AIF   ('&CODE' EQ '3').M3
.M1      MNOTE '*** &MAKRO MAKRO BEREITS EINGESETZT'
         MEXIT
.M2      MNOTE '*** &MAKRO PARAMETER FEHLT'
         MEXIT
.M3      MNOTE '*** &MAKRO FEHLER IN PARAMETERANGABE'
         MEXIT
.NOTE1   MACMES 2,MACMES
         MEXIT
.NOTE2   MACMES 3,MACMES
         MEND
         MACRO
&NAME    MCDAT &BER
         GBLB  &MCDAT
         AIF   (&MCDAT).MCDAT4
&MCDAT   SETB  1
*        DATUMSPRUEFUNG
* DAS DATUM IST UNGEPACKT IN DER FORM TT.MM.JJ (8 BYTES)
*------- LEVEL = 05 -------*
UPDAT    EQU   *
         AIF   ('&BER' EQ '').MCDAT2
         AIF   ('&BER'(1,1) EQ '(').MCDAT3
&NAME    LA    1,&BER
.MCDAT1  ANOP
UPDAT0   ZAP   UPDATV2+2(2),UPDATK28
UPDAT1   STM   0,2,UPDATVF1
         MVC   UPDATVD1(2),0(1)
         MVC   UPDATVD1+2(2),3(1)
         MVC   UPDATVD1+4(2),6(1)
         LA    2,UPDATVD1
         LA    0,6
UPDAT2   CLI   0(2),C'0'
         BL    UPDAT4
         CLI   0(2),C'9'
         BH    UPDAT4
         LA    2,1(2)
         BCT   0,UPDAT2
         PACK  UPDATV1,0(2,1)
         CLI   UPDATV2+2,X'03'
         BE    UPDAT3
         PACK  UPDATVD1(3),6(2,1)
         DP    UPDATVD1(3),UPDATK4
         TM    UPDATVD1+2,X'F0'
         BNZ   UPDAT3
         ZAP   UPDATV2+2(2),UPDATK29
UPDAT3   PACK  UPDATVD1,3(2,1)
         CP    UPDATVD1,UPDATK12
         BH    UPDAT4
         CVB   2,UPDATVD1
         SLA   2,1
         BZ    UPDAT4
         LA    2,UPDATV2-2(2)
         CP    UPDATV1,0(2,2)
         BH    UPDAT4
         BE    UPDAT5
UPDAT6   CP    UPDATV1,UPDATV2+7(1)
         BE    UPDAT4
         LM    0,2,UPDATVF1
         B     4(2)
UPDAT5   CLI   UPDATV2+2,X'03'
         BNE   UPDAT6
         MVI   1(1),X'F0'
         B     UPDAT6
UPDAT4   LM    0,2,UPDATVF1
         BR    2
UPDAT30  ZAP   UPDATV2+2(2),UPDATK30
         B     UPDAT1
UPDATK4  DC    P'4'
UPDATK12 DC    P'12'
UPDATK28 DC    P'28'
UPDATK29 DC    P'29'
UPDATK30 DC    P'30'
UPDATV1  DC    PL2'0'
UPDATV2  DC    P'31,28,31,30,31,30,31,31,30,31,30,31'
UPDATVF1 DC    3F'0'
UPDATVD1 DS    D
         MEXIT
.MCDAT2  AIF   ('&NAME' EQ '').MCDAT1
&NAME    EQU   *
         AGO   .MCDAT1
.MCDAT3  ANOP
&NAME    LR    1,&BER(1)
         AGO   .MCDAT1
.MCDAT4  MACMES 1,MCDAT
         MEND
         EJECT
MODREP   START 0
         SPACE 5
**A      MODUL ''REPLACEPROGRAMM''
         SPACE
         USING *,R8,R9
         PRINT DATA
         SAVE  (14,12)
         LR    R8,R15
         ST    R13,SAVEAREA+4
         LR    R3,R13
         LA    R13,SAVEAREA
         ST    R13,8(R3)
         L     R9,BASIS
         B     B1
         SPACE 5
SAVEAREA DC    18F'0'
         SPACE
BASIS    DC    A(MODREP+4096)
         SPACE 5
         USING IHADCB,R2
A1       LA    R2,REPOUT
         MACDCBDD (R2)
         LTR   R1,R1
         BZ    A2
         OPEN  (REPOUT,(OUTPUT))
         TM    DCBOFLGS,X'10'
         BZ    A2
A3       LA    R2,REPIN
         MACDCBDD (R2)
         LTR   R1,R1
         BZ    A4
         OPEN  (REPIN)
         TM    DCBOFLGS,X'10'
         BZ    A4
         B     C0
A5       B     AB1
A4       OI    DB8,X'40'
         TM    VLBIT,X'80' ALTES ODER NEUES MCST?
         BZ    A5 WENN ALT, RETURN
         L     R3,DB16
         LTR   R3,R3
         BNZ   C010
AB400    WTO   '*** PROGNAME 3433 PARMKARTE FEHLT'
         ABEND 3433
         B     A5
A2       OI    DB8,X'20'
         B     A3
         SPACE 5
**AB     EOJ
         SPACE
AB1      CLI   DB16,0
         BNE   AB7
         L     R1,DB20
         SH    R1,KH4
         CLC   0(2,R1),K47F0
         BE    AB70            HYPOTHEKENABTEILUNG
AB5      MACDCBDD K44-40
         LR    R0,R1
         MACTIOT
         CLC   0(2,R1),KTX
         BNE   AB8
         LTR   R0,R0
         BNZ   AB6
         CLC   K44,K45
         BE    AB7
         MVC   K44,K45
         B     AB5
AB8      LTR   R0,R0
         BNZ   AB70                  //SYSUDUMP/SYSABEND IST DA
         B     AB7                     //SYSUDUMP/SYSABEND IST NICHT DA
AB70     MVI   DB16,1               W/TX-JOB'S 28.04.76 BAUER
AB7      L     R1,DB20
         MVC   16(1,R1),DB16
         TM    DB8,X'20'
         BO    AB2
         CLOSE (REPOUT)
         FREEPOOL REPOUT
AB2      TM    DB8,X'40'
         BO    AB3
         CLOSE (REPIN)
         FREEPOOL REPIN
AB3      TM    DB8,X'10'
         BO    AB4
         L     R13,4(R13)
         SR    R15,R15
         RETURN (14,12),RC=(15)
AB4      WTO   '*** PROGNAME 3430 REPKARTEN FALSCH'
         ABEND 3430
AB6      ABEND 3860
         SPACE 5
**B      GRUNDWERTE
         SPACE
B1       L     R1,0(R1)
         LTR   R1,R1 WENN VLBIT GESETZT,
         BP    B11 NEUES MCST
         OI    VLBIT,X'80' NEUES MCST
         LA    R1,0(R1)
B11      SR    R14,R14
         IC    R14,0(R1)
         BCTR  R14,0
         SLL   R14,2
         LA    R15,V2(R14)
         LA    R2,24(R14,R3)
         SR    R14,R14
         IC    R14,4(R1)
         SLL   R14,2
         BCTR  R14,0
         EX    R14,K909
         MVC   V3(20),0(R1)
         MVI   DB16,0
         ST    R1,DB20
         MVI   DB0,X'00'
         MVI   DB4,X'00'
         L     R1,16 CVT-ADDRESS
         L     R1,0(R1) TCB-LIST
         L     R1,4(R1) TCB-ADDRESS
         L     R2,16 CVT-ADDRESS
         CLI   116(R2),X'20' MFT OR MVT
         BNE   BMVT1 IF NOT EQUAL = MVT
         L     R1,0(R1) RB-ADDRESS
         L     R2,28(R1) PREVIOUS RB-ADDRESS
         LA    R2,0(R2)
         MVC   DC0,0(R2) PROGNAME
         L     R1,DB4
         LH    R14,8(R2)
         SLL   R14,3
         AR    R14,R1
         ST    R14,DC12
B12      L     R0,DB0
         SR    R1,R0
         ST    R1,DC8
         ZAP   DC16,K34
         SR    R12,R12
         L     R11,DB8
         SLL   R11,8
         SRL   R11,8
         MVC   K39+8(8),DC0
         MVC   AB4+12(8),DC0
         MVC   AB400+12(8),DC0
         MVC   H3+12(8),DC0
         MVC   UA2+12(8),DC0
         MVC   WK1+8(8),DC0
         MVC   WK2+16(8),DC0
         LA    R0,15
         LA    R1,V2
B2       MVI   0(R1),X'0'
         LA    R1,4(R1)
         BCT   R0,B2
         B     A1
BMVT1    L     R2,152(R1) D-PQE LESS 8
         ST    R2,ADPQE
         L     R1,0(R1) RB-ADDRESS
         L     R2,12(R1) CDE-ADDRESS MODREP
         L     R2,16(R2) ENTRYPOINT MODRES
         LA    R2,0(R2)
         ST    R2,EPMODREP
         L     R1,28(R1) PREVIOUS RB
         L     R1,12(R1) CDE-ADDRESS PROG
         LA R1,0(R1)
         MVC   DC0,8(R1) PROGNAME
         L     R1,16(R1) EP PROG
         LA    R1,0(R1)
         C     R1,EPMODREP
         BNL   BMVT2
         L     R1,EPMODREP
         ST    R1,DC12
         B     BMVT3
BMVT2    L     R1,ADPQE D-PQE ADDRESS LESS 8
         LA    R1,0(R1)
         L     R1,8(R1) PQE-ADDRES
         L     R2,20(R1) REGIONSIZE
         L     R1,24(R1) REGIONBEGIN
         AR    R2,R1
         ST    R2,DC12
BMVT3    L     R1,DB4
         B     B12
ADPQE    DC    F'0'
EPMODREP DC    F'0'
         SPACE 5
**C0     PRUEFUNG DER PARMKARTE
         SPACE
C0       BAL   R4,UB1
         MVI   DD0,X'09'
         TM    VLBIT,X'80' ALTES ODER NEUES MCST?
         BZ    C7 WENN ALT, PRUEFUNG SCHON IM MCST ERFOLGT
         L     R3,DB16
         LTR   R3,R3 WENN REG O NULL,
         BNZ   C010 PARMKARTE ALS REPKARTE
         BAL   R4,UA1
         CLC   0(7,R5),K35
         BNE   C016
         LA    R5,7(R5) A(ATNN)
         TRT   0(52,R5),K10
         BC    7,C015
         CLC   52(3,R5),K36
         BNE   C016
         LR    R3,R5
         B     C011
C010     L     R3,0(R3)
         CLC   0(2,R3),K37 PARMLAENGE
         BNE   C015
         LA    R3,2(R3)
C011     LA    R0,4
         LA    R2,12(R3) A(1.PARMFELD)
C012     CLC   0(4,R2),K38 PRUEFUNG AUF TEST
         BE    C013
         LA    R2,10(R2)
         BCT   R0,C012
         B     C014
C013     CLI   4(R2),C'J'
         BNE   C014
         OI    DB8,X'80'
         L     R1,DB20 A(PARAMETERLISTE PROBLEMPROGRAMM)
         OI    8(R1),X'80' BIT FUER 'TST=J' SETZEN (MCST108)
         SPACE
*        PRUEFUNG DER PARMANGABE
         SPACE
C014     CLC   0(4,R3),DC0
         BNE   C016
         SPACE
*        DATUMSPRUEFUNG
         SPACE
         L     R1,DB20 A(PARAMETERLISTE PROBLEMPROGRAMM)
         L     R1,20(R1) A(PARMBEREICH PROBLEMPROGRAMM)
         MVC   0(52,R1),0(R3) MOVE PARMKARTE
         LA    R1,4(R3)
         BAL   R2,UPDAT1
         B     C017
         TM    DB8,X'40' WENN BIT GESETZT,
         BO    A5 REPIN NICHT VORHANDEN
         B     C6
         SPACE
*        FEHLERMELDUNGEN FUER PARMKARTE
         SPACE
C015     MVC   K39+22(L'K40),K40
         B     C018
C016     MVC   K39+22(L'K41),K41
         B     C018
C017     MVC   K39+22(L'K42),K42
C018     WTO   MF=(E,K39)
         ABEND 3432
         SPACE 5
**C      KARTENVERARBEITUNG
         SPACE
C7       BAL   R4,UA1
C2       TM    0(R5),X'B0'
         BNM   F1
         CLC   0(5,R5),K43
         BE    C8
         CLI   0(R5),C'*'
         BE    W1
         CLC   0(13,R5),K22
         BE    H1
         CLC   0(7,R5),K23
         BE    E1
         TM    DB8,X'80'
         BZ    C5
         CLC   0(14,R5),K24
         BE    G1
         CLC   0(7,R5),K25
         BE    C3
         CLC   0(7,R5),K26
         BE    C4
C5       MVI   DD1,C'A'
C6       BAL   R4,UB1
         B     C7
C3       OI    4(R11),X'01'
         B     C6
C4       OI    5(R11),X'01'
         CLI   9(R5),C' '
         BE    C6
         OI    5(R11),X'11'
         B     C6
C8       MVI   DB16,1
         B     C6
         SPACE 5
**E      TESTWORTBIT
         SPACE
E1       TM    9(R5),X'F0'
         BNO   E3
         TM    10(R5),X'F0'
         BO    E4
         CLI   10(R5),X'40'
         BNE   E2
         PACK  VD1,9(1,R5)
         B     E5
E3       CLI   9(R5),X'40'
         BNE   E2
         TM    10(R5),X'F0'
         BNO   E2
E4       PACK  VD1,9(2,R5)
E5       CVB   R15,VD1
         LA    R1,31
         SR    R1,R15
         BM    E2
         LA    R15,1
         SLL   R15,0(R1)
         O     R15,0(R11)
         ST    R15,0(R11)
         B     C6
E2       MVI   DD1,C'D'
         B     C6
**F      REPLACEKARTENANALYSE
         SPACE
F1       NI    DB8,X'F0'
         BAL   R4,UK1
         TM    DB8,X'01'
         BO    F3
         B     FA1
F2       TM    DB8,X'09'
         BNZ   F3
         CLC   0(5,R6),K18
         BE    FB1
         CLC   0(4,R6),K19
         BE    FC1
         CLC   0(3,R6),K20
         BE    FD1
         CLC   0(2,R6),K21
         BE    FE1
         B     J1
F3       BAL   R4,UB1
         BAL   R4,UA1
         CLC   0(13,R5),K22
         BNE   F1
         B     H1
         SPACE 5
**FA     ADRESSVERARBEITUNG
         SPACE
FA1      CLI   V1,C' '
         BE    FA6
         LA    R3,V1
         BAL   R4,UD1
         TM    DB8,X'01'
         BO    FA2
         LR    R12,R1
FA8      LR    R6,R2
         LA    R6,1(R6)
         CLI   0(R6),C' '
         BE    *-8
         LR    R7,R6
         LA    R7,1(R7)
         CLI   0(R7),C' '
         BNE   *-8
         LR    R3,R7
         SR    R3,R6
         CH    R3,=H'5'
         BH    FA4
         LA    R7,1(R7)
         CLI   0(R7),C' '
         BE    *-8
FA5      B     F2
FA2      MVI   DD1,C'C'
         B     FA3
FA4      MVI   DD1,C'D'
FA3      OI    DB8,X'01'
         B     FA5
FA6      CLI   V1+71,C'*'
         BE    FA7
         LA    R2,V1
         B     FA8
FA10     MVI   DD1,C'B'
         OI    DB8,X'01'
         B     FA5
FA7      CLC   V1+1(70),V1
         BE    FA9
         PACK  VD1,V1+9(4)
         MVN   VD1+7(1),K17
         CLC   VD1+5(3),DC16
         BNH   FA10
         MVC   DC16,VD1+5
FA9      OI    DB8,X'08'
         B     FA5
         SPACE 5
**FB     USING-KARTE
         SPACE
FB1      LR    R3,R7
         CLI   0(R3),C'*'
         BE    FB2
         BAL   R4,UD1
         LR    R3,R2
         TM    DB8,X'01'
         BO    FB8
         LR    R15,R1
FB3      CLI   0(R3),C','
         BE    FB4
         CLI   0(R3),C'+'
         BE    FB5
         CLI   0(R3),C'-'
         BE    FB6
FB8      MVI   DD1,C'E'
FB10     B     F3
FB5      LA    R3,1(R3)
         BAL   R4,UC1
         B     FB7
FB6      LA    R3,1(R3)
         BAL   R4,UC1
         LNR   R1,R1
FB7      TM    DB8,X'01'
         BO    FB8
         AR    R15,R1
         LR    R3,R2
         B     FB3
FB2      LR    R15,R12
         LA    R3,1(R3)
         B     FB3
FB9      MVI   DD1,C'F'
         B     FB10
FB4      LA    R3,1(R3)
         BAL   R4,UE1
         TM    DB8,X'01'
         BO    FB9
         LTR   R1,R1
         BZ    FB9
         BAL   R4,UF1
         LA    R15,0(R15)
         ST    R15,0(R1)
         B     FB10
         SPACE 5
**FC     DROP-KARTE
         SPACE
FC1      LR    R3,R7
         BAL   R4,UE1
         TM    DB8,X'01'
         BO    FC2
         LTR   R1,R1
         BZ    FC2
         BAL   R4,UF1
FC3      XC    0(4,R1),0(R1)
         B     F3
FC2      MVI   DD1,C'E'
         B     FC3
         SPACE 5
**FD     REP-KARTE
         SPACE
FD1      LR    R15,R7
         LA    R0,64
FD2      CLI   0(R15),C' '
         BNE   FD7
         SR    R15,R7
         BCTR  R15,0
         LR    R3,R7
         EX    R15,K907
         BNE   FD3
         EX    R15,K908
         LA    R15,1(R15)
FD5      MVO   0(1,R12),0(1,R7)
         BCT   R15,FD4
         LA    R12,1(R12)
FD6      B     F3
FD7      LA    R15,1(R15)
         BCT   R0,FD2
FD3      MVI   DD1,C'E'
         B     FD6
FD4      MVN   0(1,R12),1(R7)
         LA    R12,1(R12)
         LA    R7,2(R7)
         BCT   R15,FD5
         B     FD6
         SPACE 5
**FE     DC-KARTE
         SPACE
FE1      CLC   0(2,R7),=C'C'''
         BNE   FE2
         LR    R15,R7
         LA    R0,64
FE3      CLC   3(2,R15),=C''' '
         BE    FE4
         LA    R15,1(R15)
         BCT   R0,FE3
FE5      MVI   DD1,C'E'
         B     FE7
FE4      SR    R15,R7
         EX    R15,K905
         LA    R12,1(R12,R15)
         B     FE7
FE2      CLC   0(2,R7),=C'AL'
         BNE   FE5
         CLI   10(R7),C')'
         BNE   FE5
         LA    R3,4(R7)
         BAL   R4,UD1
         TM    DB8,X'01'
         BO    FE5
         LA    R15,3
         CLI   2(R7),C'3'
         BNE   FE6
         SLL   R1,8
         BCTR  R15,0
FE6      ST    R1,VD1
         EX    R15,K906
         LA    R12,1(R15,R12)
FE7      B     F3
         SPACE 5
**G      SUCHTABELLE
         SPACE
G1       LA    R15,6(R11)
         LA    R14,3
G3       CLI   0(R15),X'00'
         BNE   G2
         LA    R3,15(R5)
         BAL   R4,UC1
         TM    DB8,X'01'
         BO    G4
         LTR   R1,R1
         BZ    G4
         CH    R1,=H'255'
         BH    G4
         STC   R1,0(R15)
G5       B     GA1
G6       B     C2
G2       LA    R15,28(R15)
         BCT   R14,G3
G4       MVI   DD1,C'E'
         B     G5
         SPACE 5
**GA     FOLGEKARTEN
         SPACE
GA1      BAL   R4,UB1
         BAL   R4,UA1
         LA    R1,4(R15)
         CLC   9(3,R5),K27
         BE    GAA1
         LA    R1,12(R1)
         CLC   9(3,R5),K28
         BE    GAA1
         CLC   9(3,R5),K29
         BE    GA2
         CLC   9(3,R5),K30
         BE    GA3
         CLC   9(3,R5),K31
         BE    GA4
         TM    1(R15),X'F0'
         BNZ   GA6
         OI    1(R15),X'C0'
GA6      TM    1(R15),X'0F'
         BNZ   G6
         OI    1(R15),X'0C'
         B     G6
GA4      BAL   R4,UL1
GA5      STC   R0,VD1
         OC    1(1,R15),VD1
         B     GA1
GA3      BAL   R4,UL1
         SLL   R0,4
         B     GA5
GA2      LA    R3,15(R5)
         BAL   R4,UC1
         STH   R1,2(R15)
         B     GA1
         SPACE 5
**GAA    SUCHBEGRIFF
         SPACE
GAA1     LA    R3,15(R5)
         ST    R15,GAAVF1
         LR    R15,R3
GAA2     LA    R15,1(R15)
         CLI   0(R15),C' '
         BNE   GAA2
         SR    R15,R3
         BCTR  R15,0
         EX    R15,K907
         BNE   GAA6
         EX    R15,K908
         LA    R15,1(R15)
GAA4     MVO   0(1,R1),0(1,R3)
         BCT   R15,GAA3
GAA5     L     R15,GAAVF1
         B     GA1
GAAVF1   DC    F'0'
GAA6     MVI   DD1,C'E'
         B     GAA5
GAA3     MVN   0(1,R1),1(R3)
         LA    R1,1(R1)
         LA    R3,2(R3)
         BCT   R15,GAA4
         B     GAA5
         SPACE 5
**H      EINGABE ENDE
         SPACE
H1       NI    DB8,X'F0'
         BAL   R4,UK1
         TM    DB8,X'01'
         BO    H2
         MVC   H3+43(3),V1+77
H3       WTO   '*** PROGNAME 3236 ANZAHL REPKARTEN 001'
H2       BAL   R4,UB1
         B     A5
         SPACE 5
**J      SYMBOLISCHE KARTEN
         SPACE
J1       B     JH1
J2       TM    DB8,X'01'
         BO    J3
         LA    R0,6
         STH   R0,DA2
         CLI   DA4,X'D0'
         BH    JB1
         NI    DA2+1,X'FD'
         CLI   DA4,X'91'
         BL    J4
         CLI   DA4,X'98'
         BNE   JC1
J4       CLI   DA4,X'85'
         BH    JD1
         CLI   DA4,X'40'
         BNL   JE1
         LA    R0,2
         STH   R0,DA2
         B     JF1
J5       TM    DB8,X'01'
         BO    J6
         B     JG1
J6       B     F3
J3       MVI   DD1,C'D'
         B     J6
         SPACE 5
**JA     BEFEHLSCODE ERMITTELN
         SPACE
JA1      BCTR  R3,0
JA2      EX    R3,K902
         BE    JA3
         LA    R2,2(R2,R3)
         CR    R2,R15
         BL    JA2
         OI    DB8,X'01'
JA4      B     J2
JA3      MVC   DA4(1),0(R2)
         B     JA4
         SPACE 5
**JB     SS-FORMAT
         SPACE
JB1      OI    DB8,X'02'
         LR    R3,R7
         BAL   R4,UJ1
         CLI   0(R3),C','
         BNE   JB5
         TM    DB8,X'01'
         BO    JB5
         MVC   DA4+1(3),DA10
         CLI   DA4,X'F0'
         BH    JB2
         NI    DB8,X'FD'
JB3      LA    R3,1(R3)
         BAL   R4,UJ1
         TM    DB8,X'01'
         BO    JB6
         MVC   DA4+4(2),DA10+1
         TM    DB8,X'02'
         BZ    JB4
         MVN   DA4+1(1),DA10
JB4      NI    DB8,X'FD'
         B     J5
JB5      MVI   DD1,C'E'
         B     JB4
JB2      MVO   DA4+1(1),DA10(1)
         B     JB3
JB6      MVI   DD1,C'F'
         B     JB4
         SPACE 5
**JC     SI-FORMAT
         SPACE
JC1      LR    R3,R7
         BAL   R4,UJ1
         CLI   0(R3),C','
         BNE   JC2
         TM    DB8,X'01'
         BO    JC2
         MVC   DA4+2(2),DA10+1
         LA    R3,1(R3)
         CLI   0(R3),C'X'
         BE    JC3
         CLI   0(R3),C'C'
         BE    JC5
         BAL   R4,UC1
         TM    DB8,X'01'
         BO    JC7
         CH    R1,=H'255'
         BH    JC7
         STC   R1,DA4+1
JC6      B     J5
JC2      MVI   DD1,C'E'
         B     JC6
JC5      MVC   DA4+1(1),2(R3)
         B     JC6
JC7      MVI   DD1,C'F'
         B     JC6
JC3      MVI   VD1,X'F0'
         MVC   VD1+1(1),2(R3)
         CLI   4(R3),C''''
         BNE   JC4
         MVC   VD1(2),2(R3)
JC4      TRT   VD1(2),K12
         BNE   JC7
         TR    VD1(2),K13-193
         MVO   DA4+1(1),VD1(1)
         MVN   DA4+1(1),VD1+1
         B     JC6
         SPACE 5
**JD     RS-FORMAT
         SPACE
JD1      B     JDA1
JD2      CLI   0(R3),C','
         BNE   JD3
         TM    DB8,X'01'
         BO    JD3
         LA    R3,1(R3)
         CLC   0(2,R6),=C'SR'
         BE    JD5
         CLC   0(2,R6),=C'SL'
         BE    JD5
         B     JDB1
JD4      TM    DB8,X'01'
         BO    JD3
JD5      BAL   R4,UJ1
         TM    DB8,X'01'
         BO    JD6
         MVC   DA4+2(2),DA10+1
JD7      B     J5
JD3      MVI   DD1,C'E'
         B     JD7
JD6      MVI   DD1,C'F'
         B     JD7
**JDA    1.OPERAND
         SPACE
JDA1     LR    R3,R7
         BAL   R4,UE1
         STC   R1,VD1
         MVO   DA4+1(1),VD1(1)
         LR    R3,R2
         B     JD2
         SPACE 5
**JDB    2.REGISTER
         SPACE
JDB1     BAL   R4,UE1
         STC   R1,VD1
         MVN   DA4+1(1),VD1
         CLI   0(R2),C','
         BNE   JDB3
         LA    R3,1(R2)
JDB2     B     JD4
JDB3     OI    DB8,X'01'
         B     JDB2
         SPACE 5
**JE     RX-FORMAT
         SPACE
JE1      LR    R3,R7
         CLI   DA4,X'47'
         BNE   JE2
         CLC   0(3,R6),=C'BC '
         BNE   JE3
JE2      BAL   R4,UE1
         LA    R3,1(R2)
         CLI   0(R2),C','
         BNE   JE5
JE4      TM    DB8,X'01'
         BO    JE5
         STC   R1,VD1
         MVO   DA4+1(1),VD1(1)
         OI    DB8,X'04'
         BAL   R4,UJ1
         TM    DB8,X'01'
         BO    JE6
         MVC   DA4+2(2),DA10+1
         MVN   DA4+1(1),DA10
JE7      B     J5
JE5      MVI   DD1,C'E'
         B     JE7
JE3      SR    R1,R1
         CLC   0(4,R6),=C'NOP '
         BE    JE4
         B     JEA1
JE6      MVI   DD1,C'F'
         B     JE7
         SPACE 5
**JEA    MASKE ERMITTELN
         SPACE
JEA1     CLC   0(2,R6),=C'B '
         BE    JEA2
         L     R2,KF1+16
         LA    R1,1
         LA    R15,1(R6)
         CLI   0(R15),C'N'
         BNE   JEA3
         LA    R15,1(R15)
JEA3     LA    R0,4
JEA5     LA    R4,2
JEA4     CLC   0(1,R15),0(R2)
         BE    JEA6
         LA    R2,1(R2)
         BCT   R4,JEA4
         AR    R1,R1
         BCT   R0,JEA5
         OI    DB8,X'01'
JEA7     B     JE4
JEA6     CLI   1(R6),C'N'
         BNE   JEA7
         SH    R1,=H'15'
         LPR   R1,R1
         B     JEA7
JEA2     LA    R1,15
         B     JEA7
         SPACE 5
**JF     RR-FORMAT
         SPACE
JF1      LR    R3,R7
         CLC   0(3,R6),=C'BR '
         BE    JF2
         CLC   0(5,R6),=C'NOPR '
         BE    JF3
         CLI   DA4,X'0A'
         BE    JF4
         BAL   R4,UE1
         STC   R1,VD1
         MVO   DA4+1(1),VD1(1)
         CLI   0(R2),C','
         BNE   JF6
         TM    DB8,X'01'
         BO    JF6
         LA    R3,1(R2)
JF3      BAL   R4,UE1
         STC   R1,VD1
         MVN   DA4+1(1),VD1
         TM    DB8,X'01'
         BZ    JF5
         MVI   DD1,C'F'
JF5      B     J5
JF2      OI    DA4+1,X'F0'
         B     JF3
JF6      MVI   DD1,C'E'
         B     JF5
JF4      BAL   R4,UC1
         STC   R1,DA4+1
         TM    DB8,X'01'
         BO    JF6
         B     JF5
         SPACE 5
**JG     BEFEHLSUEBERTRAGUNG
         SPACE
JG1      ST    R12,VD1
         UNPK  VD1(9),VD1(5)
         TR    VD1,K16-240
         MVC   DD3,VD1+2
         LH    R1,DA2
         BCTR  R1,0
         EX    R1,K903
         LA    R12,1(R1,R12)
         LA    R1,1(R1)
         SLL   R1,1
         BCTR  R1,0
         UNPK  VD1(13),DA4(7)
         TR    VD1(12),K16-240
         EX    R1,K904
         XC    DA4,DA4
         B     J6
         SPACE 5
**JH     OPERATION
         SPACE
JH1      LA    R1,KF1
         LR    R2,R3
         BCTR  R2,0
         SLL   R2,2
         L     R15,4(R2,R1)
         L     R2,0(R2,R1)
         B     JA1
         SPACE 5
*W       WTO/WTOR-KARTE
         SPACE
W1       CLI   1(R5),C'*'
         BNE   W2
         MVC   WK2+30(69),2(R5)
W3       XC    WVF1,WVF1
         MVI   WV1,C' '
         LA    R1,WK2
         WTOR  MF=(E,(1))
         WAIT  ECB=WVF1
         OI    WV1,X'40'
         CLI   WV1,C'U'
         BNE   W3
W4       B     C6
W2       MVC   WK1+22(70),1(R5)
         LA    R1,WK1
         WTO   MF=(E,(1))
         B     W4
*        WTO/WTOR-LISTE
         DS    0F
WK1      DC    H'92'
         DC    H'0'
         DC    C'*** PROGNAME 3237 '
         DC    CL70' '
         DS    0F
WK2      DC    X'01'
         DC    AL3(WV1)
         DC    A(WVF1)
         DC    H'91'
         DC    H'0'
         DC    C'*** PROGNAME 3033 '
         DC    CL69' '
WV1      DC    C' '
WVF1     DC    F'0'
         SPACE 5
**UA     KARTE LESEN
         SPACE
UA1      STM   R14,R1,VF1
         GET   REPIN,V1
         LA    R5,V1
         MVC   V8,V1
         LM    R14,R1,VF1
         BR    R4
UA2      WTO   '*** PROGNAME 3431 REPKARTEN FEHLEN'
         LM    R14,R1,VF1
         ABEND 3431
         SPACE 5
**UB     DRUCKEN
         SPACE
UB1      STM   R14,R1,VF1
         CLI   DD1,C' '
         BE    UB2
         OI    DB8,X'10'
UB2      TM    DB8,X'20'
         BO    UB3
         MVC   DD24,V8
         PUT   REPOUT,V6
UB3      MVI   V7+1,C' '
         MVC   V7+2(102),V7+1
         LM    R14,R1,VF1
         BR    R4
         SPACE 5
**UC     UMWANDLUNG IN BINAER
         SPACE
UC1      LA    R0,4
         LR    R2,R3
UC3      CLI   1(R2),X'F0'
         BL    UC2
         CLI   1(R2),X'F9'
         BH    UC2
         LA    R2,1(R2)
         BCT   R0,UC3
UC4      OI    DB8,X'01'
         B     UC5
UC2      SR    R2,R3
         EX    R2,K900
         BNE   UC4
         EX    R2,K901
         CVB   R1,VD1
         LA    R2,1(R2,R3)
UC5      BR    R4
         SPACE 5
**UD     ADRESSPRUEFUNG
         SPACE
UD1      SR    R1,R1
         TRT   0(6,R3),K12
         BNE   UD2
         CLI   6(R3),C'+'
         BE    UD4
         CLI   6(R3),C'-'
         BE    UD4
         CLI   6(R3),C'('
         BE    UD4
         CLI   6(R3),C','
         BE    UD4
         CLC   6(2,R3),=C') '
         BE    UD4
         CLI   6(R3),C' '
         BNE   UD2
UD4      TR    0(6,R3),K13-193
         PACK  VD1(5),0(7,R3)
         L     R1,VD1
         A     R1,DC8
         C     R1,DB4
         BL    UD2
         C     R1,DC12
         BNL   UD2
UD3      LA    R2,6(R3)
         BR    R4
UD2      OI    DB8,X'01'
         B     UD3
         SPACE 5
**UE     REGISTERANGABE
         SPACE
UE1      MVI   UGV1,X'F0'
         LR    R2,R3
         CLI   0(R2),C'R'
         BNE   UE2
         LA    R2,1(R2)
UE2      MVC   UGV1+1(1),0(R2)
         CLI   0(R2),X'F1'
         BNE   UE3
         CLI   1(R2),X'F0'
         BL    UE3
         CLI   1(R2),X'F5'
         BH    UE3
         MVC   UGV1,0(R2)
         LA    R2,1(R2)
UE3      LA    R2,1(R2)
         TRT   UGV1,K11
         BNE   UE4
         PACK  VD1,UGV1
         CVB   R1,VD1
UE5      BR    R4
UEV1     DC    CL2' '
UE4      OI    DB8,X'01'
         B     UE5
         SPACE 5
**UF     BASISADRESSE
         SPACE
UF1      LTR   R1,R1
         BZ    UF2
         BCTR  R1,0
         SLL   R1,2
         LA    R1,V2(R1)
UF2      BR    R4
         SPACE 5
**UG     ADRESSZERLEGUNG
         SPACE
UG1      STM   R0,R3,VF1
         MVC   UGV1,K14+1
         OI    DB8,X'01'
         LA    R3,V2
         LA    R0,15
UG3      S     R1,0(R3)
         BM    UG2
         CH    R1,UGV1
         BH    UG2
         NI    DB8,X'FE'
         STH   R1,UGV1
         LR    R2,R3
         S     R2,KF2
         SRL   R2,2
         LA    R2,1(R2)
         STC   R2,UGV2
UG2      A     R1,0(R3)
         LA    R3,4(R3)
         BCT   R0,UG3
         MVO   UGV1(1),UGV2(1)
         MVC   DA10+1(2),UGV1
         LM    R0,R3,VF1
         BR    R4
UGV1     DC    XL2'0'
UGV2     DC    X'0'
         SPACE 5
**UH     HEXAOPERAND
         SPACE
UH1      BAL   R4,UD1
         TM    DB8,X'01'
         BO    UH2
         BAL   R4,UG1
         TM    DB8,X'01'
         BO    UH2
         MVI   DA10,X'00'
         LR    R3,R2
         CLI   0(R3),C' '
         BE    UH2
         CLI   0(R3),C','
         BE    UH2
         CLI   0(R3),C'('
         BNE   UH3
         LA    R3,1(R3)
         TM    DB8,X'04'
         BO    UH4
         TM    DB8,X'02'
         BZ    UH3
         BAL   R4,UC1
         BCTR  R1,0
UH5      STC   R1,DA10
         LR    R3,R2
         CLI   0(R3),C')'
         BNE   UH3
         LA    R3,1(R3)
UH2      BR    R5
UH3      OI    DB8,X'01'
         B     UH2
UH4      BAL   R4,UE1
         B     UH5
         SPACE 5
**UI     EXPLIZITER OPERAND
         SPACE
UI1      B     UIA1
UI2      CLI   0(R3),C' '
         BE    UI3
         CLI   0(R3),C'('
         BNE   UI4
         LA    R3,1(R3)
         B     UIB1
UI5      CLI   0(R3),C')'
         BE    UI7
         CLI   0(R3),C','
         BNE   UI6
         LA    R3,1(R3)
UI6      BAL   R4,UE1
         STC   R1,VD1
         MVO   DA10+1(1),VD1(1)
         LR    R3,R2
         CLI   0(R3),C')'
         BNE   UI4
UI7      LA    R3,1(R3)
UI3      BR    R5
UI4      OI    DB8,X'01'
         B     UI3
         SPACE 5
**UIA    DISPLACEMENT
         SPACE
UIA1     MVC   DA10,K14
         BAL   R4,UC1
         STH   R1,VD1
         STC   R1,DA10+2
         MVN   DA10+1(1),VD1
         LR    R3,R2
         B     UI2
         SPACE 5
**UIB    LAENGE UND INDEX
         SPACE
UIB1     TM    DB8,X'02'
         BO    UIB2
         TM    DB8,X'04'
         BZ    UIB6
         CLI   0(R3),C','
         BE    UIB5
         BAL   R4,UE1
UIB4     STC   R1,DA10
         LR    R3,R2
UIB6     B     UI5
UIB2     BAL   R4,UC1
         BCTR  R1,0
         B     UIB4
UIB5     LA    R3,1(R3)
         B     UIB6
         SPACE 5
**UJ     OPERANDENANALYSE
         SPACE
UJ1      ST    R4,UJVF1
         CLI   0(R3),X'F0'
         BNE   UJ2
         CLI   1(R3),X'F9'
         BH    UJ2
         CLI   1(R3),X'F0'
         BNL   UJ3
UJ2      BAL   R5,UI1
UJ4      L     R4,UJVF1
         BR    R4
UJVF1    DC    F'0'
UJ3      BAL   R5,UH1
         B     UJ4
         SPACE 5
**UK     FOLGEPRUEFUNG
         SPACE
UK1      CLC   V1+72(5),DC0
         BNE   UK2
         AP    DC16,K17
         PACK  VD1,V1+77(3)
         MVN   VD1+7(1),K17
         CLC   VD1+5(3),DC16
         BE    UK3
         MVC   DC16,VD1+5
UK2      OI    DB8,X'01'
         MVI   DD1,C'B'
UK3      BR    R4
         SPACE 5
**UL     SUCHBITS
         SPACE
UL1      LA    R0,1
         CLC   15(5,R5),K32
         BE    UL2
         AR    R0,R0
         CLC   15(4,R5),K33
         BE    UL2
         AR    R0,R0
         CLC   15(7,R5),K26
         BE    UL3
         AR    R0,R0
         CLC   15(7,R5),K25
         BE    UL2
         MVI   DD1,C'E'
         SR    R0,R0
UL2      BR    R4
UL3      CLI   23(R5),C' '
         BE    UL2
         LR    R15,R0
         LA    R0,1(R15)
         B     UL2
         SPACE
         MCDAT
         SPACE
V1       DC    CL80' '        EINGABEBEREICH FUER KARTE
         SPACE
V2       DC    15F'0'         BASISREGISTER DES PROBLEMPROGRAMMS
         SPACE
V3       DS    0CL20          PARAMETERLISTE AUS PROBLEMPROGRAMM
DB0      DC    X'0'
DB1      DC    XL3'0'
DB4      DC    X'0'
DB5      DC    XL3'0'
DB8      DC    X'0'
DB9      DC    XL3'0'
DB12     DC    XL4'0'
DB16     DC    XL4'0'
DB20     DC    XL4'0' A(PARAMETERLISTE)
VLBIT    DC    X'0' MERKBYTE, OB NEUES ODER ALTES MCST
K47F0    DC    X'47F0'               HYPOTHEKENABTEILUNG
KH4      DC    H'4'                    HYPOTHEKENABTEILUNG
KTX      DC    C'TX'           W/TX-JOB'S 28.04.76 BAUER
         SPACE
V4       DS    0CL19          MODREP-SPEICHER
DC0      DC    CL8' '
DC8      DC    F'0'
DC12     DC    F'0'
DC16     DC    PL3'0'
         SPACE
V5       DS    0CL13                 BEFEHLSSPEICHER
DA0      DC    H'0'
DA2      DC    H'0'
DA4      DC    XL6'0'
DA10     DC    XL3'0'
         SPACE
V6       DC    H'108'         IOCS
         DC    H'0'           FELD
V7       DS    0CL104         DRUCKBEREICH
DD0      DC    X'89'
DD1      DC    C' '
         DC    C' '
DD3      DC    CL6' '
         DC    C' '
DD10     DC    CL12' '
         DC    CL2' '
DD24     DC    CL80' '
V8       DC    CL80' '
         SPACE
VF1      DC    16F'0'         REGISTERSICHERUNGSBEREICH
         SPACE
VD1      DS    D
VD2      DS    D
         SPACE 5
K10      DC    XL125'0'   TRT - KONSTANTE APOSTROPH
         DC    X'01'
         DC    XL130'0'
K11      DC    CL240' '
         DC    XL10'0'   TRT KONSTANTE F0 - F9
         DC    CL6' '
K12      DC    193C' '
         DC    XL6'0'
         DC    41C' '    TRT KONSTANTE C1 - C6, F0 - F9
         DC    XL10'0'
         DC    CL6' '
K13      DC    XL6'FAFBFCFDFEFF'
         DC    41C' '
         DC    C'0123456789'
K14      DC    X'000FFF'
K15      DC    X'00'
K16      DC    C'0123456789ABCDEF'
K17      DC    P'1'
K18      DC    C'USING'
K19      DC    C'DROP'
K20      DC    C'REP'
K21      DC    C'DC'
K22      DC    C'EINGABE  ENDE'
K23      DC    C'TESTBIT'
K24      DC    C'SUCHEN   ZUORD'
K25      DC    C'DRUCKEN'
K26      DC    C'TRACING'
K27      DC    C'VON'
K28      DC    C'BIS'
K29      DC    C'ADR'
K30      DC    C'EIN'
K31      DC    C'AUS'
K32      DC    C'ABEND'
K33      DC    C'VERZ'
K34      DC    P'0'
K35      DC    C'PARM=('''
K36      DC    C''') '
K37      DC    H'52'
K38      DC    C'TST='
K39      WTO   '*** PROGNAME 3432                   ',MF=L
K40      DC    C'FALSCHE PARMLAENGE'
K41      DC    C'FALSCHE PARMKARTE'
K42      DC    C'FEHLER IN DATUM'
K43      DC    C'DUMP '
K44      DC    CL8'SYSUDUMP'
K45      DC    CL8'SYSABEND'
         SPACE
K900     TRT   0(1,R3),K11
K901     PACK  VD1,0(1,R3)
K902     CLC   1(1,R2),0(R6)
K903     MVC   0(1,R12),DA4
K904     MVC   DD10(1),VD1
K905     MVC   0(1,R12),2(R7)
K906     MVC   0(1,R12),VD1
K907     TRT   0(1,R3),K12
K908     TR    0(1,R3),K13-193
K909     MVC   0(1,R15),0(R2)
         SPACE
KF1      DC    A(K800)
         DC    A(K801)
         DC    A(K802)
         DC    A(K803)
         DC    A(K804)
KF2      DC    A(V2)
         SPACE
K800     DC    X'47',C'B'
         DC    X'58',C'L'
         DC    X'5A',C'A'
         DC    X'59',C'C'
         DC    X'5B',C'S'
         DC    X'5C',C'M'
         DC    X'5D',C'D'
         DC    X'54',C'N'
         DC    X'56',C'O'
         DC    X'57',C'X'
K801     DC    X'07',C'BR'
         DC    X'47',C'BH'
         DC    X'47',C'BL'
         DC    X'47',C'BE'
         DC    X'47',C'BO'
         DC    X'47',C'BP'
         DC    X'47',C'BM'
         DC    X'47',C'BZ'
         DC    X'47',C'BC'
         DC    X'41',C'LA'
         DC    X'DE',C'ED'
         DC    X'FA',C'AP'
         DC    X'F9',C'CP'
         DC    X'18',C'LR'
         DC    X'1B',C'SR'
         DC    X'98',C'LM'
         DC    X'50',C'ST'
         DC    X'91',C'TM'
         DC    X'48',C'LH'
         DC    X'43',C'IC'
         DC    X'DC',C'TR'
         DC    X'FB',C'SP'
         DC    X'49',C'CH'
         DC    X'FC',C'MP'
         DC    X'FD',C'DP'
         DC    X'96',C'OI'
         DC    X'94',C'NI'
         DC    X'97',C'XI'
         DC    X'19',C'CR'
         DC    X'1A',C'AR'
         DC    X'4A',C'AH'
         DC    X'4B',C'SH'
         DC    X'4C',C'MH'
         DC    X'44',C'EX'
         DC    X'1C',C'MR'
         DC    X'1D',C'DR'
         DC    X'D4',C'NC'
         DC    X'D6',C'OC'
         DC    X'D7',C'XC'
         DC    X'14',C'NR'
         DC    X'16',C'OR'
         DC    X'17',C'XR'
         DC    X'55',C'CL'
         DC    X'5E',C'AL'
         DC    X'5F',C'SL'
K802     DC    X'47',C'NOP'
         DC    X'47',C'BNH'
         DC    X'47',C'BNL'
         DC    X'47',C'BNE'
         DC    X'47',C'BNO'
         DC    X'47',C'BNM'
         DC    X'47',C'BNP'
         DC    X'47',C'BNZ'
         DC    X'D2',C'MVC'
         DC    X'45',C'BAL'
         DC    X'07',C'BCR'
         DC    X'92',C'MVI'
         DC    X'95',C'CLI'
         DC    X'F8',C'ZAP'
         DC    X'D5',C'CLC'
         DC    X'90',C'STM'
         DC    X'12',C'LTR'
         DC    X'40',C'STH'
         DC    X'F1',C'MVO'
         DC    X'42',C'STC'
         DC    X'4E',C'CVD'
         DC    X'4F',C'CVB'
         DC    X'46',C'BCT'
         DC    X'88',C'SRL'
         DC    X'89',C'SLL'
         DC    X'8A',C'SRA'
         DC    X'8B',C'SLA'
         DC    X'D1',C'MVN'
         DC    X'D3',C'MVZ'
         DC    X'0A',C'SVC'
         DC    X'10',C'LPR'
         DC    X'13',C'LCR'
         DC    X'15',C'CLR'
         DC    X'DD',C'TRT'
         DC    X'11',C'LNR'
         DC    X'1E',C'ALR'
         DC    X'1F',C'SLR'
         DC    X'86',C'BXH'
K803     DC    X'05',C'BALR'
         DC    X'F2',C'PACK'
         DC    X'F3',C'UNPK'
         DC    X'8C',C'SRDL'
         DC    X'8D',C'SLDL'
         DC    X'8E',C'SRDA'
         DC    X'8F',C'SLDA'
         DC    X'06',C'BCTR'
         DC    X'DF',C'EDMK'
         DC    X'87',C'BXLE'
         DC    X'07',C'NOPR'
K804     DC    C' OHPLMEZ'
         SPACE 5
REPIN    DCB   DSORG=PS,MACRF=GM,DDNAME=REPIN,LRECL=80,                X
               EODAD=UA2,RECFM=FB
         SPACE 5
REPOUT   DCB   DSORG=PS,MACRF=PM,RECFM=VBM,LRECL=108,               X  X
               BLKSIZE=112,BUFNO=2,EROPT=ACC,DDNAME=REPOUT
         SPACE 5
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 5
         DCBD
         END   MODREP
