         TITLE '  J C L   X R E F   '
         PRINT NOGEN
*
*        PGM-NAME    :  JCLXREF
*        DATE        :  OKT.1979
*
*  LAST MODIFICATION :  ........
*  REASON            :  NONE
*
*  LAST COMPILE      :  07.11.79
*  LAST LINK         :  13.11.79     LIB = SYS2.LINKLIB
*  LAST SAVE         :  14.11.79
*
*  PGM-INFORMATION   :  ERSTELLEN JCL-VERZEICHNIS. (WANN, WIE, WO,
*                       EIN DATA-SET ANGESPROCHEN WIRD)
*
JCLXREF  INIT  BASIS=(2,11)
*
*
         OPEN  (SYSUT2,OUTPUT)
         OPEN  (SYSUT3,OUTPUT)
         OPEN  (DIR)
         OPEN  (MBR)
         TM    MBR+48,X'10'
         BZ    ERROR0
         LA    R4,MBR
         LH    R4,62(R4)
         ST    R4,STORE4
         RDJFCB (MBR)
         MVC   DSDI,JFCBAREA
         MVC   VOLSER,JFCBAREA+118
         LR    R0,R4
         N     R0,LOESCH
         GETMAIN R,LV=(0)
         ST    R1,BLOCK
*
READDIR  READ  DIRDECB,SF,DIR,DIRAREA,'S'
         CHECK DIRDECB
         LA    R7,DIRAREA
         LH    R8,0(R7)
         SH    R8,=H'2'
         LA    R7,2(R7)
         STM   R7,R8,SAREA2
NEXTMEMB CLC   0(8,R7),=8XL1'FF'
         BE    ENDDIR
         CLC   0(8,R7),=8XL1'00'
         BE    LOOKNEXT
*
         AP    MEMBZ,=P'1'
         MVC   MEMBER,0(R7)
         BAL   R10,GOFIND
*
         MVC   XMEMB,MEMBER
*
READMBR  READ  READDECB,SF,MBR,BLOCK,'S'
         CHECK READDECB
         L     R4,STORE4
         LA    R8,READDECB
         L     R8,16(R8)
         LA    R9,BLOCK
         LH    R12,14(R8)
         SR    R4,R12
         EJECT
LOOP     EQU   *
         STM   R3,R10,SAREA1
         MVC   INCARD,0(R9)
*************************
****     X R E F     ****
*************************
         EJECT
         MVC   XLINE3+1(80),INCARD
         PUT   SYSUT3,XLINE3
*
         CLC   INCARD(2),=C'//'
         BNE   ENDLOOP
         CLC   INCARD(2),=C'/*'
         BE    ENDLOOP
         CLC   KARTE,INCARD
         BE    ENDLOOP
         CLC   INCARD(3),=C'//*'
         BE    ENDLOOP
         CLC   INCARD(10),=C'//STEPLIB '
         BE    ENDLOOP
         CLC   INCARD(9),=C'//JOBLIB '
         BE    ENDLOOP
*
*******  CHECK JOB-CARD  *******
*
         LA    R3,INCARD+2
         LA    R4,20
XJOBL01  EQU   *
         CLC   0(5,R3),=C' JOB '
         BE    GOJOB
         LA    R3,1(R3)
         BCT   R4,XJOBL01
         CLI   EXECSW,C'1'
         BE    GOEXEC
         CLI   DDSW,C'1'
         BE    GODD
         CLI   INCARD+2,C' '
         BNE   GOEXEC
         LA    R3,INCARD+2
         LA    R4,20
CONCL01  EQU   *
         CLC   0(4,R3),=C' DD '
         BE    CONCL02
         LA    R3,1(R3)
         BCT   R4,CONCL01
         B     ENDLOOP
CONCL02  EQU   *
         MVI   DDSW,C'1'
         MVC   XDDNM,ZWDDNM
         LA    R3,4(R3)
         B     XDDLAB2
*
GOJOB    EQU   *
         ZAP   STEPCNT,=P'0'
         MVI   EXECSW,C' '
         MVI   DDSW,C' '
         MVI   LISW,C' '
         MVI   MBRSW,C' '
         MVC   XJOBNM,INCARD+2
         B     ENDLOOP
*
*******  CHECK EXEC-CARD  *******
*
GOEXEC   EQU   *
         LA    R3,INCARD+2
         CLI   EXECSW,C'1'
         BNE   XEXECL00
XEXECLA  CLI   0(R3),C' '
         BNE   XEXECLB
         LA    R3,1(R3)
         B     XEXECLA
XEXECLB  EQU   *
         CLI   MBRSW,C'1'
         BE    XEXECL06
XEXECLC  BAL   R10,LASTTWOE
         B     ENDLOOP
         LA    R3,1(R3)
         B     XEXECLC
XEXECL00 EQU   *
         LA    R3,INCARD+2
         LA    R4,20
         LR    R5,R3
XEXECL01 EQU   *
         CLC   0(6,R3),=C' EXEC '
         BE    XEXECL02
         LA    R3,1(R3)
         BCT   R4,XEXECL01
         B     GODD
XEXECL02 EQU   *
         MVI   EXECSW,C'1'
         MVI   DDSW,C' '
         MVI   LISW,C' '
         AP    STEPCNT,=P'1'
         ZAP   XSTEPCNT,STEPCNT
         ST    R3,R3SA
         MVC   XSTEPNM,=8CL1' '
         MVC   XPGMMBR,=8CL1' '
         MVC   XPROC,=8CL1' '
         LA    R4,8
         LA    R5,INCARD+2
         LA    R6,XSTEPNM
         BAL   R10,LENGTH
*
         L     R3,R3SA
         LA    R4,10
         LA    R3,5(R3)
XEXECL03 EQU   *
         CLI   0(R3),C' '
         BNE   XEXECL04
         LA    R3,1(R3)
         BCT   R4,XEXECL03
*
         DATA  MEMBER
         DATA  CLOSE=YES
         WTO   'JCLXREF: INPUT NOT OK.  *****'
         MVC   RETCODE+3(1),=X'01'
         B     CLOSEN
*
XEXECL04 EQU   *
         CLC   0(4,R3),=C'PGM='
         BNE   XEXECL05
         LA    R3,4(R3)
         LR    R5,R3
         LA    R6,XPGMMBR
         LA    R4,8
         BAL   R10,LENGTH
         CLI   LASTBYTE,C' '
         BE    ENDEXEC
         BAL   R10,LASTTWOE
         B     ENDLOOP
         B     XEXECLC
XEXECL05 EQU   *
         LR    R5,R3
         MVC   XPROC,=8CL1' '
         LA    R6,XPROC
         LA    R4,8
         BAL   R10,LENGTH
         CLI   LASTBYTE,C' '
         BE    ENDEXEC
         MVI   MBRSW,C'1'
XEXECL06 EQU   *
         CLC   0(2,R3),=C'  '
         BE    ENDEXEC
         CLC   0(2,R3),=C', '
         BE    ENDLOOP
         CLC   0(4,R3),=C'MBR='
         BE    XEXECL07
         LA    R3,1(R3)
         B     XEXECL06
XEXECL07 EQU   *
         LA    R3,4(R3)
         LR    R5,R3
         LA    R6,XPGMMBR
         LA    R4,8
         BAL   R10,LENGTH
         MVI   MBRSW,C' '
         B     XEXECLC
*
ENDEXEC  EQU   *
         MVI   EXECSW,C' '
         MVI   MBRSW,C' '
         B     ENDLOOP
         EJECT
*
*******  CHECK DD-CARD  *******
*
GODD     EQU   *
*
         LA    R3,INCARD+2
         CLI   DDSW,C'1'
         BNE   XDDLAB
         B     XDDLAB2
*
XDDLAB   LA    R4,20
         LA    R3,2(R3)
         LR    R5,R3
XDDLOOP1 EQU   *
         CLC   0(4,R3),=C' DD '
         BE    XDDLAB1
         LA    R3,1(R3)
         BCT   R4,XDDLOOP1
         LA    R3,INCARD+2
         LA    R4,20
PPLOOP   EQU   *
         CLC   0(6,R3),=C' PROC '
         BE    PROCEND
         CLC   0(6,R3),=C' PEND '
         BE    PENDEND
         LA    R3,1(R3)
         BCT   R4,PPLOOP
         DATA  MEMBER
         MVC   DATALINE,INCARD
         DATA  DATALINE
         DATA  CLOSE=YES
*
         WTO   'JCLXREF: INPUT NOT OK.  *****'
         MVC   RETCODE+3(1),=X'02'
         B     CLOSEN
*
PROCEND  EQU   *
         LA    R3,6(R3)
         LA    R4,10
PROCEND1 CLI   0(R3),C' '
         BNE   PROCEND2
         LA    R3,1(R3)
         BCT   R4,PROCEND1
         B     ENDLOOP
PROCEND2 EQU   *
         B     XEXECL05
PENDEND  EQU   *
         B     ENDLOOP
XDDLAB1  EQU   *
         MVI   ZWUNIT,C'F'
         MVI   DDSW,C'1'
         MVI   LISW,C' '
         MVI   EXECSW,C' '
         LA    R4,8
         MVC   XDDNM,=8CL1' '
         LA    R6,XDDNM
         ST    R3,R3SA
         LA    R5,INCARD+2
         BAL   R10,LENGTH         *** DD-NAME
         MVC   ZWDDNM,XDDNM
         L     R3,R3SA
         LA    R3,3(R3)
XDDLAB2  CLI   0(R3),C' '
         BNE   XDDLAB11
         CLC   0(2,R3),=C' *'
         BNE   DDDATA
         MVC   XDSN,=44CL1' '
         MVI   XDSN,C'*'
         B     XDDLAB3
DDDATA   CLC   0(5,R3),=C' DATA'
         BNE   DDDUMMY
         MVC   XDSN,=44CL1' '
         MVC   XDSN(4),=C'DATA'
         B     XDDLAB3
DDDUMMY  CLC   0(6,R3),=C' DUMMY'
         BNE   DDSYSOUT
         MVC   XDSN,=44CL1' '
         MVC   XDSN(5),=C'DUMMY'
         B     XDDLAB3
DDSYSOUT CLC   0(8,R3),=C' SYSOUT='
         BNE   DDNEXT
         MVC   XDSN,=44CL1' '
         MVC   XDSN(6),=C'SYSOUT'
         B     XDDLAB3
DDNEXT   LA    R3,1(R3)
         B     XDDLAB2
XDDLAB3  EQU   *
         MVC   XGEN(1),=C' '
         ZAP   XGEN+1(3),=P'0'
         MVC   XDISP,=3CL1' '
         MVI   XUNIT,C' '
         MVC   XSER,=6CL1' '
         MVI   LISW,C'1'
XDDLAB10 LA    R3,1(R3)
XDDLAB11 EQU   *
         CLC   0(2,R3),=C'  '
         BE    ENDDD
         CLC   0(2,R3),=C', '
         BE    ENDLOOP
         CLI   LISW,C'1'
         BE    XDDLAB10
*
         CLC   0(4,R3),=C'DSN='
         BNE   DISP
         LA    R3,4(R3)
         LR    R5,R3
         LA    R4,44
         LA    R6,XDSN
         BAL   R10,LENGTH
         CLI   LASTBYTE,C'('
         BNE   XDDLAB12
         LA    R3,1(R3)
         CLI   0(R3),C'+'
         BE    GENERL01
         CLI   0(R3),C'-'
         BE    GENERL01
         CLI   0(R3),C'0'
         BL    XDDLAB12
         CLI   0(R3),C'9'
         BH    XDDLAB12
         B     GENERL01
XDDLAB12 EQU   *
         BAL   R10,LASTTWOD
         B     ENDLOOP
         B     XDDLAB10
*
DISP     CLC   0(5,R3),=C'DISP='
         BNE   UNIT
         BAL   R10,GODISP
         BAL   R10,LASTTWOD
         B     ENDLOOP
         B     XDDLAB10
*
UNIT     CLC   0(5,R3),=C'UNIT='
         BNE   SERIAL
         LA    R6,XUNIT
         BAL   R10,GOUNIT
         BAL   R10,LASTTWOD
         B     ENDLOOP
         B     XDDLAB10
*
SERIAL   CLC   0(4,R3),=C'SER='
         BNE   XDDLAB10
         LA    R6,XSER
         BAL   R10,GOSERIAL
         BAL   R10,LASTTWOD
         B     ENDLOOP
         B     XDDLAB10
*
         B     ENDLOOP
*
ENDDD    EQU   *
         MVI   DDSW,C' '
         EJECT
PUTEN    EQU   *
         MVC   STRING,XLINE
         PUT   SYSUT2,XLINE2
*        PUT   SYSUT2,XLINE
*
LINEINIT MVC   XDDNM,=8CL1' '
         MVC   XDSN,=44CL1' '
         MVC   XGEN(1),=C' '
         ZAP   XGEN+1(3),=P'0'
         MVC   XDISP,=3CL1' '
         MVI   XUNIT,C' '
         MVC   XSER,=6CL1' '
*
*
*
ENDLOOP  LM    R3,R10,SAREA1
         SH    R4,=H'80'
         LTR   R4,R4
         BZ    READMBR
         LA    R9,80(R9)
         B     LOOP
ENDMBR   EQU   *
         EJECT
LOOKNEXT EQU   *
         LM    R7,R8,SAREA2
         IC    R9,11(R7)
         N     R9,FN
         LA    R7,12(R7)
         SH    R8,=H'12'
         BZ    READDIR
         SLA   R9,1
         SR    R8,R9
         BZ    READDIR
         LA    R7,0(R9,R7)
         STM   R7,R8,SAREA2
         B     NEXTMEMB
ENDDIR   B     CLOSEN
*
         EJECT
LENGTH   LR    R12,R5         SUCHEN EINE LAENGE
LSLOOP   EQU   *
         CLI   0(R12),C' '
         BE    ESLOOP
         CLI   0(R12),C','
         BE    ESLOOP
         CLI   0(R12),C'('
         BE    ESLOOP
         LA    R12,1(R12)
         BCT   R4,LSLOOP
ESLOOP   EQU   *
         LR    R3,R12
         MVC   LASTBYTE,0(R12)
         BCTR  R12,R0
         SR    R12,R5
         STC   R12,LSMVC+1
LSMVC    MVC   0(8,R6),0(R5)
         BR    R10
         EJECT
LASTTWOE EQU  *
         CLC   0(2,R3),=C', '
         BE    LAST2RE
         CLC   0(2,R3),=C'  '
         BE    ENDEXEC
         LA    R10,4(R10)
LAST2RE  BR    R10
*****************************
LASTTWOD EQU  *
         CLC   0(2,R3),=C', '
         BE    LAST2RD
         CLC   0(2,R3),=C'  '
         BE    ENDDD
         LA    R10,4(R10)
LAST2RD  BR    R10
*
*
         EJECT
GODISP   EQU   *
         LA    R3,5(R3)
         CLI   0(R3),C'('
         BNE   DISPL01
         NI    DISPL05+1,X'0F'
         LA    R3,1(R3)
DISPL01  EQU   *
         LA    R12,XDISP
         LA    R7,3
DISPL02  LA    R4,10
         LA    R6,DISPTAB
DISPL03  CLC   0(1,R6),0(R3)
         BE    DISPL04
         LA    R6,2(R6)
         BCT   R4,DISPL03
*
         DATA  MEMBER
         DATA  CLOSE=YES
         WTO   'JCLXREF: WRONG DISP-FIELD  *****'
         MVC   RETCODE+3(1),=X'03'
         B     CLOSEN
*
DISPL04  MVC   0(1,R12),1(R6)
         LA    R12,1(R12)
DISPL05  B     DISPL06
         CLI   0(R3),C','
         BE    DISPNEXT
DISPL06  CLI   0(R3),C','
         BE    DISPRET
         CLI   0(R3),C')'
         BE    DISPRETA
         CLI   0(R3),C' '
         BE    DISPRET
         LA    R3,1(R3)
         B     DISPL05
DISPNEXT EQU   *
         LA    R3,1(R3)
         BCT   R7,DISPL02
DISPRETA LA    R3,1(R3)
DISPRET  EQU   *
         OI    DISPL05+1,X'F0'
         BR    R10
*
*
         EJECT
GOUNIT   EQU   *
*                                             *
*    TAPE      =  1                           *
*    TAPE08    =  2                           *
*    TAPE16    =  3                           *
*    3330-1    =  4                           *
*    3350      =  5                           *
*    SYSDA     =  6                           *
*    DISK      =  7                           *
*                                             *
         LA    R3,5(R3)
*
         CLI   0(R3),C'('
         BNE   GUL0
         LA    R3,1(R3)
GUL0     CLC   0(6,R3),=C'TAPE16'
         BNE   GUL1
         MVI   0(R6),C'3'
         LA    R3,4(R3)
         B     GURET
GUL1     EQU   *
         CLC   0(6,R3),=C'TAPE08'
         BNE   GUL2
         MVI   0(R6),C'2'
         LA    R3,6(R3)
         B     GURET
GUL2     EQU   *
         CLC   0(4,R3),=C'TAPE'
         BNE   GUL3
         MVI   0(R6),C'1'
         LA    R3,6(R3)
         B     GURET
GUL3     EQU   *
         CLC   0(6,R3),=C'3330-1'
         BNE   GUL4
         MVI   0(R6),C'4'
         LA    R3,6(R3)
         B     GURET
GUL4     EQU   *
         CLC   0(4,R3),=C'3350'
         BNE   GUL5
         MVI   0(R6),C'5'
         LA    R3,4(R3)
         B     GURET
GUL5     EQU   *
         CLC   0(5,R3),=C'SYSDA'
         BNE   GUL6
         MVI   0(R6),C'6'
         LA    R3,5(R3)
         B     GURET
GUL6     EQU   *
         CLC   0(4,R3),=C'DISK'
         BNE   GULF
         MVI   0(R6),C'7'
         LA    R3,4(R3)
GURET    EQU   *
         MVC   ZWUNIT,0(R6)
         BR    R10
GULF     EQU   *
         CLC   0(3,R3),=C'AFF'
         BNE   GULFF
         MVC   0(1,R6),ZWUNIT
         B     GURET
GULFF    MVI   0(R6),C'F'   UNIT=AFF  ODER UNBEKANNT
         BR    R10
GUL7     LA    R3,1(R3)
         CLI   0(R3),C','
         BE    GURET
         CLI   0(R3),C' '
         BE    GURET
         B     GUL7
         EJECT
*
*
         EJECT
GOSERIAL EQU   *
         LA    R3,4(R3)
         CLI   0(R3),C'('
         BNE   GSL0
         MVC   0(6,R6),=C'* ? * '
         B     GSL1
GSL0     MVC   0(6,R6),0(R3)
GSL1     LA    R3,1(R3)
         CLI   0(R3),C','
         BE    GSRET
         CLI   0(R3),C' '
         BE    GSRET
         B     GSL1
GSRET    BR    R10
         EJECT
*
GENERL01 EQU   *
         CLI   0(R3),C'+'
         BE    GENERL02
         CLI   0(R3),C'-'
         BE    GENERL02
         MVC   XGEN(1),=C'+'
         B     GENERL03
GENERL02 MVC   XGEN(1),0(R3)
         LA    R3,1(R3)
GENERL03 EQU   *
         MVC   ZWGEN2,0(R3)
         LA    R4,ZWGEN1
         LA    R5,ZWGEN2
         LA    R6,2
GLOOP    EQU   *
         CLI   0(R5),C')'
         BE    ENDGLOOP
         LA    R4,1(R4)
         LA    R5,1(R5)
         BCT   R6,GLOOP
ENDGLOOP BCTR  R4,R0
         PACK  XGEN+1(3),0(3,R4)
GENERL04 LA    R3,1(R3)
         CLI   0(R3),C')'
         BNE   GENERL04
         LA    R3,1(R3)
         B     XDDLAB12
*
         EJECT
GOFIND   EQU   *
         MVC   DWNAME,MEMBER
         FIND  MBR,DWNAME,D
         B     *+4(R15)
         B     0(R10)
         B     NOTFOUND
         B     IOERR
*
NOTFOUND ABEND 4,DUMP
IOERR    ABEND 8,DUMP
         EJECT
ERROR0   WTO   'WRONG OPEN'
CLOSEN   CLOSE (DIR)
         CLOSE (SYSUT2)
         CLOSE (SYSUT3)
ENDE2    EQU   *
         CLC   RETCODE+3(1),=X'00'
         BNE   ENDE3
         WTO   'JCLXREF: FEHLERLOSE VERARBEITUNG.  *****'
ENDE3    L     R13,S0001+4
         L     R15,RETCODE
         RETURN (14,12),RC=(15)
         EJECT
DIR      DCB   DDNAME=PDS,DSORG=PO,MACRF=(R),EODAD=ENDDIR,             *
               RECFM=F,LRECL=256,BLKSIZE=256
MBR      DCB   DDNAME=PDS,DSORG=PO,MACRF=(R),EODAD=ENDMBR,             *
               EXLST=LIST1
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,                        *
               RECFM=FBM,LRECL=111,BLKSIZE=111
SYSUT3   DCB   DDNAME=SYSUT3,DSORG=PS,MACRF=PM,                        *
               RECFM=FBM,LRECL=81,BLKSIZE=81
*
DATALINE DS    CL50
SAREA1   DS    8F
SAREA2   DS    2F
KARTE    DS    0CL30
         DC    CL2'//'
         DC    CL28' '
INCARD   DS    CL80
*
DSDI     DS    CL44
VOLSER   DS    CL6
*
*
XLINE2   DS  0CL111
         DC  XL1'09'
STRING   DS  CL110
*
XLINE3   DS  0CL81
         DC  XL1'09'
         DS  CL80
*
XLINE    DS  0CL110
XMEMB    DC  CL8' '
XJOBNM   DC  CL8' '
XSTEPNM  DC  CL8' '
XSTEPCNT DC  PL2'0'
*
XDDNM    DC  CL8' '
XDSN     DC  CL44' '
XGEN     DS  0CL4
         DC  CL1' '
         DC  PL3'0'
XDISP    DC  CL3' '
XUNIT    DC  CL1' '
XSER     DC  CL6' '
*
XPROC    DC  CL8' '
XPGMMBR  DC  CL8' '
XEMPTY   DC  CL2' '
***********************************************
*    X D I S P                                *
*                                             *
*    SHR       =  1                           *
*    NEW (,)   =  2 -----------------         *
*    OLD       =  3                  ×        *
*    KEEP      =  4 -----------------×-       *
*    MOD       =  5                  × ×      *
*    DELETE    =  6 -----------------×-×-     *
*    CATLG     =  7                  × × ×    *
*    UNCATLG   =  8                  × × ×    *
**** DISP=(NEW,KEEP,DELETE)   DISP=  2 4 6    *
*                                             *
*    X U N I T                                *
*                                             *
*    TAPE      =  1                           *
*    TAPE08    =  2                           *
*    TAPE16    =  3                           *
*    3330-1    =  4                           *
*    3350      =  5                           *
*                                             *
*                                             *
***********************************************
DISPTAB  DS    0CL20
         DC    CL2'S1'    SHR
         DC    CL2'N2'    NEW
         DC    CL2',2'    ,
         DC    CL2'O3'    OLD
         DC    CL2'K4'    KEEP
         DC    CL2'M5'    MOD
         DC    CL2'D6'    DELETE
         DC    CL2'C7'    CATLG
         DC    CL2'U8'    UNCATLG
         DC    CL2'P9'    PASS
DIRAREA  DS    CL256
MEMBER   DS    CL8
DWNAME   DS    D
ZWGEN1   DC    CL2'00'
ZWGEN2   DC    CL3' '
MEMBZ    DC    PL4'0'
STEPCNT  DC    PL2'0'
MBRSW    DC    CL1' '
DSNSW    DC    CL1' '
EXECSW   DC    CL1' '
DDSW     DC    CL1' '
LISW     DC    CL1' '
R3SA     DS    F
ZWDDNM   DS    CL8
ZWUNIT   DS    CL1
LASTBYTE DC    CL1' '
FN       DC    F'31'
LOESCH   DC    XL4'0000FFFF'
STORE4   DS    F
RETCODE  DC    F'0'
LIST1    DC    AL1(128+7),AL3(JFCBAREA)
JFCBAREA DS    CL176
         LTORG
BLOCK    DS    F
         DS    CL13030
         END
