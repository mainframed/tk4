BASFUPDT START
         REG
         XSAVE (R11,R12),SV,BASFUPDT
         USING IHADCB,R4
         L     R3,0(R1)
         LA    R3,0(R3)
         LH    R5,0(R3)
         LTR   R5,R5
         BNZ   RDJF
WTO2     WTO   'UPDT002 PARAMETER-ERROR',ROUTCDE=(11)
         B     FEHL1
RDJF     EQU   *
         BCTR  R5,0
         LTR   R5,R5
         BZ    WTO2
         EX    R5,MVCPA
         LA    R5,5(R5)
         STH   R5,PARAM
         CALL  KEYSCAN,(PARAM)
         LTR   R15,R15
         BNZ   WTO
         LR    R5,R1
         LA    R7,PARAM
PAN      EQU   *
         SR    R6,R6
         CLC   2(2,R5),=X'FFFF'
         BE    CRDJF
         IC    R6,2(R5)
         CH    R6,=H'4'
         BNE   WTO2
         CLC   3(4,R5),=C'COMP'
         BNE   C1RD
         LH    R6,8(R5)
         LH    R3,10(R5)
         LTR   R3,R3
         BZ    WTO2
         LA    R6,5(R6,R7)
         CH    R3,=H'3'
         BNE   C111R
         CLC   0(2,R6),=C'YES'
         BNE   WTO2
         B     C222
C111R    EQU   *
         CH    R3,=H'2'
         BNE   WTO2
         CLC   0(2,R6),=C'NO'
         BNE   WTO2
         OI    NPB+1,X'F0'
         OI    BA12+1,X'F0'
         OI    BA112+1,X'F0'
         OI    BA3+1,X'F0'
         B     C222
C1RD     EQU   *
         CLC   3(4,R5),=C'GADD'
         BNE   C2RD
         LH    R6,8(R5)
         LA    R6,5(R6,R7)
         LH    R3,10(R5)
         CH    R3,=H'6'
         BNE   C22RP
         CLC   0(6,R6),=C'ASMBLR'
         BNE   WTO2
         MVC   LA(4),LAR2
         OI    NPL+1,X'F0'
         B     C222
C22RP    EQU   *
         CH    R3,=H'4'
         BNE   C22R
         CLC   0(4,R6),=C'PROC'
         BNE   WTO2
         MVC   KEY1(2),=H'4'
         MVC   KEY1+2(4),=C'PROC'
         OI    FO+1,X'F0'
         OI    NPL+1,X'F0'
         OI    NOTF0+1,X'F0'
         OI    NF0+1,X'F0'
         B     C222
C22R     EQU   *
         CH    R3,=H'2'
         BNE   WTO2
         CLC   0(2,R6),=C'NO'
         BNE   WTO2
C222     EQU   *
         LA    R5,10(R5)
         B     PAN
C2RD     EQU   *
         CLC   3(4,R5),=C'PAGO'
         BNE   C3RD
         LH    R6,8(R5)
         LH    R3,10(R5)
         LTR   R3,R3
         BZ    WTO2
         LA    R6,5(R6,R7)
         STH   R3,PARMA
         BCTR  R3,0
         EX    R3,MVCPA1
         B     C222
C3RD     EQU   *
         CLC   3(4,R5),=C'NMBR'
         BNE   WTO2
         LH    R6,8(R5)
         LA    R6,5(R6,R7)
         LH    R3,10(R5)
         CH    R3,=H'3'
         BNE   C33R
         CLC   0(3,R6),=C'YES'
         BNE   WTO2
         MVC   NPI(4),BALINCR
         B     C222
C33R     EQU   *
         CH    R3,=H'2'
         BNE   WTO2
         CLC   0(2,R6),=C'NO'
         BNE   WTO2
         MVC   H72,=H'80'
         MVC   VSPARM+8(2),=H'80'
         B     C222
CRDJF    EQU   *
         MVI   JFCBUT1,X'FF'
         RDJFCB (SYSUT1,INPUT)
         CLI   JFCBUT1,X'FF'
         BE    NUT
         LA    R4,SYSUT11
         OPEN (SYSUT11,OUTPUT)
         TM    DCBOFLGS,X'10'
         BNO   FEHL1
         OPEN (SYSUT1,INPUT),TYPE=J
         LA    R5,SYSUT1
         LA    R6,SYSUT11
         B     OPSYS
NUT      EQU   *
         LA    R5,SYSIN
         LA    R6,SYSIN1
OPSYS    EQU   *
         LA    R4,SYSIN
         OPEN  (SYSIN,INPUT)
         TM    DCBOFLGS,X'10'
         BNO   FEHL1
         LA    R4,SYSPRINT
         OPEN  (SYSPRINT,OUTPUT)
         TM    DCBOFLGS,X'10'
         BNO   FEHL1
         LA    R4,SYSIN1
         OPEN  (SYSIN1,OUTPUT)
         TM    DCBOFLGS,X'10'
         BO    C1
FEHL1    EQU   *
         XRETURN 4
UPKCNT   EQU   *
         ST    R14,SVR14
         L     R7,INCNTR
         CVD   R7,DBLW
         SR    R7,R7
         ST    R7,INCNTR
         UNPK  KAEIN,DBLW
         OI    KAEIN+7,X'F0'
         L     R7,OUTCNTR
         CVD   R7,DBLW
         SR    R7,R7
         ST    R7,OUTCNTR
         UNPK  KAAUS,DBLW
         OI    KAAUS+7,X'F0'
         MVC   DATA,MESS1
         MVI   VZ,C'-'
         BAL   R14,PRINT
         LA    R7,65
         ST    R7,LINECNT
         L     R14,SVR14
         BR    R14
PUT      EQU   *
BA1      B     PUTC
         OI    BA1+1,X'F0'
         BAL   R14,UPKCNT
         MVC   COUNT,=PL5'0'
BA12     NOP   PUTC
         BAL   R14,NEW
PUTC     EQU   *
         OI    BA2+1,X'F0'
         PUT   SYSIN1,SYSINFLD
         MVC   DATA,SYSINFLD
         BAL   R14,PRINT
         B     C1
LINK     EQU   *
         CALL  SCAN,(SYSINFLD,ANF,END,DPL,KEY1),VL
         B     *+4(R15)
         B     FOUND
         B     NOTF
         B     WTO
FO1      EQU   *
         TRT   SYSINFLD+2(9),TABB
         BZ    CLC
         S     R1,=A(SYSINFLD+3)
         EX    R1,MVCN1
         B     FO2
FOUND    EQU   *
         LA    R2,SYSINFLD
         LH    R3,DPL
         LA    R2,0(R2,R3)
         BCTR  R2,0
         BCTR  R2,0
         CLI   0(R2),C' '
         BNE   NOTF
LA       LA    R2,5(R2)
         CLI   0(R2),C' '
         BNE   NOTF
         MVC   NAME,=CL8' '
FO       NOP   FO1
         TRT   SYSINFLD(9),TABB
         BZ    NOTF
         LA    R4,SYSINFLD
         SR    R1,R4
         LA    R4,0(R4,R1)
         LR    R7,R1
         EX    R3,TRTC
         CLC   KEY1+2(5),0(R1)
         BNE   NOTF
         BCTR  R7,0
         EX    R7,MVCN
         CLC   ZWNAME,NAME
         BE    NOTF
         MVC   ZWNAME,NAME
FO2      EQU   *
BA11     B     PC
         OI    BA11+1,X'F0'
         BAL   R14,UPKCNT
         MVC   COUNT,=PL5'0'
BA112    NOP   PC
         BAL   R14,NEW
PC       EQU   *
         LA    R7,65
         ST    R7,LINECNT
         MVC   DATA,ADDCARD
         BAL   R14,PRINT
         OI    BA2+1,X'F0'
         PUT   SYSIN1,ADDCARD
NF0      NOP   NF2
NF1      NI    NOTF+1,X'0F'
         MVC   KEY1+2(5),=C'START'
NF2      B     CLC
NOTF     NOP   NF1
NOTF0    NOP   NF2
         MVC   KEY1+2(5),=C'CSECT'
         OI    NOTF+1,X'F0'
         B     LINK
BALVA    EQU   *
         MVC   VASATZ(80),SYSINFLD
         BAL   R14,PUTVA
         B     C1
C1       EQU   *
         GET   (5),SYSINFLD
         CLI   SYSINFLD,C'*'
         BE    CLC
NPL      NOP   LINK
CLC      EQU   *
         L     R7,INCNTR
         LA    R7,1(R7)
         ST    R7,INCNTR
         CLC   SYSINFLD(2),=C'./'
         BE    PUT
         MVC   DATA,SYSINFLD
         BAL   R14,PRINT
         NI    BA1+1,X'0F'
         NI    BA11+1,X'0F'
NPB      NOP   BALVA
         NI    BA12+1,X'0F'
         NI    BA112+1,X'0F'
         NI    BA2+1,X'0F'
         NI    BA3+1,X'0F'
         CALL  COMPRS,(ESATZ,ASATZ),VL
         B     *+4(R15)
         B     C2
WTO      WTO   'UPDT001 GET A DUMP AND CALL SYSTEMS PROGRAMMER',       *
               ROUTCDE=(11)
AB1      ABEND 1,DUMP
C2       EQU   *
         LA    R1,VSPARM
         L     R15,=V(VSHIFT)
         BALR  R14,R15
         LR    R2,R0
         B     *+4(R15)
         B     WTO
         B     INPMOV
         B     WTO
         B     IMOF
         B     WTO
         B     OFSP
         B     WTO
         B     WTO
INPMOV   EQU   *
         L     R1,VSPARM
         LH    R7,H72
         STC   R7,3(R1)
         LA    R1,0(R2,R1)
         ST    R1,VSPARM
         LH    R1,VSPARM+8
         SR    R1,R2
         CH    R1,=H'5'
         BL    OUTPF
         STH   R1,VSPARM+8
         B     C1
OUTPF    EQU   *
         OI    BA12+1,X'F0'
         OI    BA112+1,X'F0'
         BAL   R14,NEW
         OI    BA2+1,X'F0'
         OI    BA3+1,X'F0'
         B     C1
NEW      EQU   *
         ST    R14,SVR14A
         LA    R1,VASATZ
         ST    R1,VSPARM
         MVC   VSPARM+8(2),H72
         BAL   R14,PUTVA
         L     R14,SVR14A
         BR    R14
PRINT    EQU   *
         ST    R14,SVR14C
         L     R7,LINECNT
         CH    R7,=H'64'
         BNH   PR1
         SR    R7,R7
         MVI   VZ,C'1'
PR1      EQU   *
         PUT   SYSPRINT,PRTFLD
         LA    R7,1(R7)
         ST    R7,LINECNT
         MVI   VZ,C' '
         L     R14,SVR14C
         BR    R14
IMOF     EQU   *
         L     R1,VSPARM
         LH    R7,H72
         STC   R7,3(R1)
         B     OUTPF
OFSP     EQU   *
         L     R1,VSPARM
         LH    R7,H72
         STC   R7,3(R1)
         LA    R1,VASATZ
         ST    R1,VSPARM
         MVC   VSPARM+8(2),H72
         BAL   R14,PUTVA
         B     C2
PUTVA    EQU   *
         ST    R14,SVR14
NPI      NOP   INCR
         L     R7,OUTCNTR
         LA    R7,1(R7)
         ST    R7,OUTCNTR
         PUT   (6),VASATZ
         MVI   VASATZ,X'00'
         MVC   VASATZ+1(79),VASATZ
         L     R14,SVR14
         BR    R14
INCR     EQU   *
         ST    R14,SVR14B
         AP    COUNT,=P'10'
         UNPK  VASATZ+72(8),COUNT
         OI    VASATZ+79,X'F0'
         L     R14,SVR14B
         BR    R14
EODUT1   EQU   *
         CLOSE SYSUT1
BA3      NOP   EUT1
         BAL   R14,NEW
EUT1     EQU   *
         CLOSE SYSUT11
         OI    BA1+1,X'F0'
         OI    BA11+1,X'F0'
         MVC   COUNT,=PL5'0'
         B     NUT
EODSYSIN EQU   *
         CLOSE SYSIN
BA2      NOP   E
         BAL   R14,PUTVA
E        EQU   *
         BAL   R14,UPKCNT
         CLOSE SYSIN1
         CLOSE SYSPRINT
         LINK  EP=IEBUPDTE,PARAM=(PARMA,DDNAMES),VL=1
         LR    R6,R15
         LA    R4,SYSOUT1
         OPEN (SYSOUT1,INPUT)
         TM    DCBOFLGS,X'10'
         BNO   FEHL1
         LA    R4,SYSOUT
         OPEN  (SYSOUT,OUTPUT)
         TM    DCBOFLGS,X'10'
         BNO   FEHL1
GET      EQU   *
         GET   SYSOUT1,OUTF
         CLC   OUTF+8(2),=C'./'
         BE    PUTOUT
         CLC   OUTF+1(4),=C'IEB8'
         BNE   GET
PUTOUT   EQU   *
         MVI   OUTF,C' '
         L     R7,OLCNT
         CH    R7,=H'64'
         BNH   PO1
         SR    R7,R7
         ST    R7,OLCNT
         MVI   OUTF,C'1'
PO1      EQU   *
         PUT   SYSOUT,OUTF
         L     R7,OLCNT
         LA    R7,1(R7)
         ST    R7,OLCNT
         B     GET
EODOUT   EQU   *
         CLOSE SYSOUT
         CLOSE SYSOUT1
         XRETURN (R6)
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,RECFM=FB,LRECL=80,       *
               EODAD=EODSYSIN
SYSIN1  DCB   DDNAME=SYSIN1,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80
SYSUT11  DCB   DDNAME=SYSUT11,RECFM=FB,MACRF=PM,DSORG=PS,LRECL=80
SYSUT1   DCB   DDNAME=SYSUT1,RECFM=FB,MACRF=GM,DSORG=PS,               *
               EODAD=EODUT1,EXLST=EXL,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,RECFM=FBA,MACRF=PM,DSORG=PS,LRECL=81,   *
               BLKSIZE=810
SYSOUT1  DCB   DDNAME=SYSOUT1,RECFM=FB,MACRF=GM,DSORG=PS,LRECL=121,    *
               BLKSIZE=121,EODAD=EODOUT
SYSOUT   DCB   DDNAME=SYSOUT,RECFM=FBA,MACRF=PM,DSORG=PS,LRECL=121,    *
               BLKSIZE=1210
EXL      DC    X'87'
         DC    AL3(JFCBUT1)
JFCBUT1  DS    CL176
DBLW     DS    D
INCNTR  DC    F'0'
OUTCNTR  DC    F'0'
LINECNT  DC    F'65'
OLCNT    DC    F'65'
OUTF     DS    CL121
PRTFLD   DS    0CL81
VZ       DC    C'1'
DATA     DS    CL80
MESS1    DS    0CL80
         DC    C' '
KAEIN    DS    CL8
         DC    C' '
         DC    C'EINGABEKARTEN,  '
KAAUS    DS    CL8
         DC    C' '
         DC    CL45'AUSGABEKARTEN'
SVR14    DC    F'0'
SVR14A   DC    F'0'
SVR14B   DC    F'0'
SVR14C   DS    F
H72      DC    H'72'
BALINCR  BAL   R14,INCR
LAR2     LA    R2,6(R2)
         CNOP  2,4
PARMA    DC    H'3'
         DC    C'NEW'
         DS    37C
ESATZ    DS    0F
         DC    H'84'
         DC    H'0'
SYSINFLD DS    CL80
ASATZ    DS    0F
         DC    F'0'
         DS    CL80
         DS    F
VSPARM   DS    0F
         DC    A(VASATZ)
         DC    A(ASATZ)
         DC    H'72'
         DC    3H'0'
VASATZ   DS    0F
         DC    80X'00'
COUNT    DC    P'00000000'
         CNOP  2,4
DDNAMES  DC    H'64'
         DC    32X'00'
         DC    CL8'SYSIN1'
SYSPR    DC    CL8'SYSOUT1'
         DC    8X'00'
         DC    CL8'SYSUT11'
MVCPA    MVC   PARAM+4(0),2(R3)
MVCPA1   MVC   PARMA+2(0),0(R6)
MVCN     MVC   NAME(0),SYSINFLD
MVCN1    MVC   NAME(0),SYSINFLD+2
TRTC     TRT   0(0,R4),TRTAC
PARAM    DC    F'0'
         DS    CL50
ANF      DC    H'2'
END      DC    H'72'
DPL      DC    H'0'
KEY1     DC    H'5'
         DC    CL5'START'
ADDCARD  DS    0CL80
         DC    C'./ ADD NAME='
NAME     DS    CL8
         DC    60C' '
ZWNAME   DS    CL8
TABB     DC    256X'00'
         ORG   TABB+X'40'
         DC    X'40'
         ORG
TRTAC    DC    256AL1(*-TRTAC)
         ORG   TRTAC+X'40'
         DC    X'00'
         ORG
         DCBD  DSORG=PS
         END   BASFUPDT
 NAME BASFUPDT(R)
