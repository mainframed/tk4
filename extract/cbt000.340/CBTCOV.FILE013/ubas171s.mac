BASINITT START
         REG
         XSAVE R12,SVA,BASINITT
DE       EQU   X'80'
         LR    R2,R1
         LINK  EP=DESETUP,PARAM=((R2),SYSPRINT,SYSUT1),VL=1
         B     *+4(R15)
         B     *+8
         B     NODE
         OI    SWITCH,DE
         MVC   JFCB(44),=CL44'TAPE.TO.BE.LABELLED'
NODE     EQU   *
         OPEN  (SYSIN)
         OPEN  (SYSPRINT,(OUTPUT)),TYPE=J
         TM    SYSIN+48,X'10'
         BO    SYSINOK
         MVC   SYSIN+49(3),=AL3(WTOROUT)
SYSINOK  EQU   *
         CALL  DATE,(DATUM),VL
         LA    R6,GOON
HEAD     EQU   *
         PUT   SYSPRINT,LINE1
         PUT   SYSPRINT,LINE2
         MVI   ASA,C'-'
         PUT   SYSPRINT,ASA
         LA    R5,50
         BR    R6
GOON     EQU   *
         TM    SWITCH,DE
         BO    NEW
         WTOR  'WTLB000 TO TERMINATE WTLB BEFORE NORMAL COMPLETION, REP*
               LY ANY CHAR.',REPLY1,1,WTORECB,ROUTCDE=2
NEW      EQU   *
         MVI   WEICHE+1,X'F0'
         LA    R3,1
WTOR     EQU   *
         GET   SYSIN,REPLY
         MVI   ASA1,C' '
         PUT   SYSPRINT,ASA1
         BCTR  R5,0
         LTR   R5,R5
         BNZ   NTALL
         BAL   R6,HEAD
NTALL    EQU   *
         CLC   REPLY(3),=C'END'
         BE    RETURN
         CLI   REPLY+5,C','
         BNE   ERROR1
         CLI   REPLY+6,C','
         BNE   NOMULT
         CLI   REPLY+10,C','
         BNE   ERROR1
         TRT   REPLY+2(3),TRTTAB
         BNZ   ERROR1
         TRT   REPLY+7(3),TRTTAB
         BNZ   ERROR1
         PACK  DBLWD,REPLY+2(3)
         CVB   R4,DBLWD
         PACK  DBLWD,REPLY+7(3)
         CVB   R3,DBLWD
         CH    R3,=H'1'
         BNH   ERROR1
NOMULT   EQU   *
         MVC   LABEL1+4(5),REPLY
         MVC   SMFTEXT,REPLY
         L     R15,16
         L     R15,196(R15)
         MVC   OWNER(2),16(R15)
         MVC   MDL,16(R15)
         TIME
         ST    R1,FULLWD
         UNPK  OWNER+2(5),FULLWD
         ST    R0,FULLWD
         UNPK  OWNER+7(3),FULLWD(2)
         OI    OWNER+9,X'F0'
         TIME  BIN
         STM   R0,R1,DBLWD
         MVC   SMFTIME,DBLWD
         LA    R1,SMFRDW
         LA    R15,SVCROUT
         SVC   250
         MVC   JFCB+118(6),LABEL1+4
         CH    R3,=H'1'
         BE    OVER
NXTAPE   EQU   *
         CVD   R4,DBLWD
         UNPK  JFCB+120(3),DBLWD
         OI    JFCB+122,X'F0'
WEICHE   B     OVER
         CLC   JFCB+120(3),=C'000'
         BE    ERROR
OVER     EQU   *
         MVI   WEICHE+1,X'00'
         MVC   LABEL1+4(6),JFCB+118
         OPEN  (SYSUT1,(OUTPUT)),TYPE=J
         PUT   SYSUT1,LABEL1
         PUT   SYSUT1,LABEL2
         CLOSE (SYSUT1)
         FREEPOOL SYSUT1
         MVI   ASA1,X'40'
         MVC   REPLY,=CL80'WTLB004 TAPE 123456 HAS BEEN SUCCESSFULLY LA*
               BELLED'
         MVC   REPLY+13(6),LABEL1+4
         PUT   SYSPRINT,ASA1
         BCTR  R5,0
         LTR   R5,R5
         BP    OK
         BAL   R6,HEAD
OK       EQU   *
         LA    R4,1(R4)
         TM    WTORECB,X'40'
         BO    RETURN
         BCT   R3,NXTAPE
         B     NEW
RETURN   EQU   *
         CLOSE (SYSIN,,SYSPRINT)
         XRETURN 0
ERROR    EQU   *
         MVC   REPLY,=CL80'WTLB002 TAPE-VOL.-SER. CROSSED 999'
         MVI   ASA1,C'0'
         PUT   SYSPRINT,ASA1
         MVI   ASA,X'40'
         PUT   SYSPRINT,ASA
         SH    R5,=H'3'
         LTR   R5,R5
         BP    TSTWTO1
         BAL   R6,HEAD
TSTWTO1  EQU   *
         TM    SYSIN+48,X'10'
         BO    NEW
         WTO   'WTLB002 TAPE-VOL.-SER. CROSSED 999',ROUTCDE=2
         B     NEW
ERROR1   EQU   *
         MVC   REPLY,=CL80'WTLB003 INVALID INPUT IGNORED - REENTER'
         MVI   ASA1,C'0'
         PUT   SYSPRINT,ASA1
         MVI   ASA,X'40'
         PUT   SYSPRINT,ASA
         SH    R5,=H'3'
         LTR   R5,R5
         BP    TSTWTO2
         BAL   R6,HEAD
TSTWTO2  EQU   *
         TM    SYSIN+48,X'10'
         BO    WTOR
         WTO   'WTLB003 ERROR IN REPLY - RESPECIFY',ROUTCDE=2
         B     WTOR
SVCROUT  EQU   *
         SVC   83
         SVC   3
WTOROUT  EQU   *
         ST    R14,SAV14
         XC    ECB,ECB
         MVC   REPLY,BLNK
         WTOR  'WTLB001 ENTER TAPE VOLUME-SERIAL NUMBER OR END',       *
               REPLY,80,ECB,ROUTCDE=2
         WAIT  ECB=ECB
         OC    REPLY,BLNK
         L     R14,SAV14
         BR    R14
SAV14    DS    F
REPLY1   DC    C' '
WTORECB  DC    F'0'
ECB      DS    F
LABEL1   DC    CL41'VOL1      0'
OWNER    DC    CL10' '
         DC    CL29'  BASF-WTLB-UTILITY'
LABEL2   DC    CL4'HDR1'
         DC    76CL1'0'
ASA1     DC    C' '
REPLY    DC    CL80' '
ASA      DC    C'-'
BLNK     DC    CL80' '
FULLWD   DS    F
DBLWD    DS    D
DCBEXLST  DS 0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
EXSOUT   DS    0F
         DC    X'87'
         DC    AL3(JFCB1)
JFCB1    DS    CL176
SWITCH   DC    X'00'
SMFREC   DS    0F
SMFRDW   DC    H'98,0'
SMFTYP   DC    X'0081'
SMFTIME  DS    CL8
MDL      DS    CL4
SMFTEXT  DS    CL80
LINE1    DC    CL56'1BASINITT - CONTROL-CARDS AND ERROR-MESSAGES'
DATUM    DC    CL24' '
LINE2    DC    C' '
         DC    43CL1'-'
         DC    CL36' '
SYSUT1   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,LRECL=80,RECFM=F,       *
               BLKSIZE=80,EXLST=DCBEXLST
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),LRECL=80,RECFM=FB,     *
               EODAD=RETURN
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),LRECL=80,           *
               RECFM=FBSA,BLKSIZE=1600,EXLST=EXSOUT
TRTTAB   DC    256XL1'FF'
         ORG   TRTTAB+X'F0'
         DC    10XL1'00'
         ORG
         END
