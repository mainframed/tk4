./ ADD NAME=UICR05
         TITLE 'EXTENDED OPER COMMAND FOR TSO'
SP3OPER  CSECT
***********************************************************************
*                                                                     *
*              P R O L O G                                            *
*                                                                     *
***********************************************************************
*                                                                     *
*              PROGRAMMER              STOEHLER / DATEV EG. NUERNBERG *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*              DESCRIPTION                                            *
*                                                                     *
*              THIS PROGRAM WILL DO CONSOLE SIMULATION FOR A TSO      *
*              TERMINAL. IT GOES OUT AND FINDS A GRAPHICS (3277)      *
*              ACTIVE CONSOLE AND USES THAT CONSOLE SCREEN IMAGE      *
*              FOR OUTPUT AND THEIR UCM-ID FOR SVC 34.                *
*              THIS PROGRAM REQUIRES AUTHORIZATION TO USE THE SVC 34  *
*              INTERFACE.                                             *
*              TO USE IT SIMPLY SAY   ''XOPER <CUU>''                 *
*              A NULL INPUT CAUSES THE SCREEN TO BE UPDATED. (SO YOU  *
*              CAN USE IT UN-AUTHORIZED AS LONG AS YOU DON'T TYPE     *
*              ANYTHING IN.)                                          *
*              COMMANDS ARE BROADCAST TO THE CONSOLE BEING USED.      *
*                                                                     *
*---------------------------------------------------------------------*
*              MODIFICATIONS:   A.HUST / ICR NEUSTADT  01/84          *
*                                                                     *
*              FULL PF-KEY-SUPPORT:                                   *
*              DYNAMIC ALLOCATION TO "SYS1.XHELP", WHERE              *
*              PF-KEYS ARE STORED IN MEMBERS NAMED '@USERID'.         *
*              (ICR)                                                  *
*---------------------------------------------------------------------*
*                                                                     *
*              PARAMETER               ADDRESS OF A SPECIFIC CONSOLE  *
*                                      CUU (NOT REQUIRED)             *
*                                                                     *
*              I/O                     ONLY TGET/TPUT                 *
*                                                                     *
*              EXIT                    NORMAL RETURN (AFTER YOU TYPE  *
*                                      'END')                         *
*                                                                     *
*              ERROR MESSAGES          NO ACCEPTABLE (3277) CONSOLES  *
*                                      FOUND                          *
*                                                                     *
***********************************************************************
         XSAVE (R12,R2),,XOPER,WORKL
         REG
         USING WORK,R13
         BAL   R14,APFK001            ASSIGN PFKEY VALUES
         STM   R1,R2,SVCSAVE
         LR    R0,R13
         LR    R1,R12
         LA    R15,SVCROUT
         SVC   250
         XRETURN 0,R
SVCROUT  EQU   *
         LR    R13,R0
         LR    R12,R1
         LM    R1,R2,SVCSAVE
         L     R3,16
         L     R3,0(R3)
         L     R3,4(R3)
         L     R3,12(R3)
         MVC   CMDID+11(8),0(R3)   TSO-USER NAME
         L     R3,PSAAOLD-PSA(0)   ASCB
         L     R3,ASCBCSCB-ASCBEGIN(R3)
         LTR   R3,R3
         BZ    END3
         CLI   X'1C'(R3),1         TSO ?
         BNE   END3                NO --> TERM.
         CLC   4(4,R1),=C'OPER'    CALLED FROM XC COMMAND ?
         BNE   END3                NO - INVALID REQUEST
         CLI   8(R1),C','          IS A SPECIFIC UNIT GIVEN ?
         BNE   GOON1               NO
         MVC   UNITXXX(3),9(R1)    SAVE IT
GOON1    EQU   *
         SPACE 2
         STFSMODE ON,INITIAL=YES
         SPACE 2
         MODESET KEY=ZERO,MODE=SUP
   SPACE
         L     R14,=A(ESRB-SRBSECT)  GET LENGTH
         GETMAIN R,LV=(14),SP=245
   SPACE
         LR    R7,R1
         USING SRBSECT,R7
         LA    R0,LSCREEN
         GETMAIN R,LV=(0)
         LR    R9,R1
SCHEDUL  XC    SRBSECT(SRBSIZ),SRBSECT
         MVC   CMDBUF,BLANKS
         LA    R1,44 ***********   NUMBER OF LINES WAS   24 !!!!!
         LA    R14,BUFF
GOTONE2  MVC   0(2,R14),=X'1D60'    NORMAL INTENSITY
         MVI   2(R14),64
         MVC   3(78,R14),2(R14)     CLEAR
         LA    R14,81(R14)           NEXT LINE
         BCT   R1,GOTONE2          REPEAT
         MVC   SRBID,=C'SRB '
         L     R1,16
         L     R1,X'22C'(R1)
         LA    R15,528(R1)
TM15     TM    0(R15),X'80'
         BO    NEXTAS
         ICM   R1,7,1(R15)
         BZ    0
         ICM   R14,7,1+ASCBJBNS-ASCBEGIN(R1)
         BNZ   NEXT1
         ICM   R14,7,1+ASCBJBNI-ASCBEGIN(R1)
NEXT1    CLC   0(8,R14),=CL8'CONSOLE'
         BE    FOUND
NEXTAS   LA    R15,4(R15)
         B     TM15
FOUND    EQU   *
         ST    R1,SRBASCB
         L     R1,PSAAOLD-PSA(0)
         ST    R1,SRBMYAS
         MVC   SRBPASID,X'24'(R1)
         MVC   SRBCPAFF,X'64'(R1)
         L     R1,ASCBTSB-ASCBEGIN(R1)
         MVC   SRBLINE,40(R1)
         L     R0,PSATOLD-PSA(0)
         ST    R0,SRBPTCB
         LA    R0,ABND
         ST    R0,SRBRMTR
         LA    R0,SRBCODE
         ST    R0,SRBEP
         LA    R0,SRBECB
         ST    R0,SRBPARM
         LA    R0,SAVESRB
         ST    R0,SRBSAVE
         OI    SRBPRIOR,SRBPNONQ
         MVC   SRBCONAD(3),UNITXXX
         OC    SRBCONAD(3),BLANKS
          SPACE 2
         LA    R0,SRBCODE
         LA    R14,CODEA
         LA    R1,CODEE-CODEA
         LR    R15,R1
         MVCL  R0,R14
   SPACE 2
         SCHEDULE SRB=(R7)
     SPACE
         WAIT  1,ECB=SRBECB
         MVC   XUCMID,SRBCONID
         CLI   SRBECB,X'41'       ERROR ?
         BE    ERRMSG
         CLI   SRBECB,X'42'       ERROR ?                  ****16.12.81
         BE    ERRMSG2                                     ****16.12.81
         MVI   SRBECB,0
         LA    R1,CLEAR
         MVC   0(11,R9),=X'C71140403C404000114040'
         LA    R0,11(R9)
         LA    R1,LSCREEN-11
         LA    R14,BUFF
         LR    R15,R1
         MVCL  R0,R14
         L     R14,SRBBUFL
         LR    R1,R9
         TPUT  (1),(R14),FULLSCR
         SPACE 2
TGET     EQU   *
         MVC   CMDBUF,BLANKS
         TGET  CMDBUFP,83,ASIS
         ST    R1,TGETLENG         LENGTH OF GOTTEN DATA
         SPACE 2
         BAL   R14,PPFK001         RECOGNICES PFKEY OR ENTER KEY
         B     *+4(R15)            TEST RETURN CODE
         B     ENTER               RC = 0 - EXECUTE COMMAND IN BUFFER
         B     TGET                RC = 4 - LAST EXECUTED COMMAND WAS
*                                  DISPLAYED, NOW TGET AGAIN
         B     SCHEDUL             RC = 8 - PFKEY DEFINITIONS WERE
*                                  DISPLAYED, RETURN TO NORMAL MODE
         SPACE 2
ENTER    EQU   *
         LA    R1,44
         LA    R14,BUFF
MOVEBLK  MVC   2(79,R14),BLANKS
         LA    R14,81(R14)
         BCT   R1,MOVEBLK
    SPACE 2
         OC    CMDBUF(4),BLANKS
         CLC   CMDBUF(4),=C'END '
         BE    END2
         B     OSOK
MVCBLK   EQU   *
         MVC   CMDBUF,BLANKS
         B     SCHEDUL
OSOK     EQU   *
         MVC   CMNDBUF,CMDBUF
         OC    CMNDBUF,BLANKS
         CLC   CMNDBUF(3),=C'K  '    CLEAR NOT ALLOWED
         BE    MVCBLK
         CLC   CMNDBUF(5),BLANKS
         BE    SCHEDUL
         CLC   CMNDBUF(4),=C'END '
         BE    END2
         MVC   WTOBUF,CMNDBUF
         LA    R1,WTOX
         SR    R0,R0
         SVC   35
         LA    R1,CMND
         SR    R0,R0
         IC    R0,XUCMID
         SVC   34
         STIMER WAIT,DINTVL=TIME
         B     SCHEDUL
         SPACE 2
ERRMSG2  EQU   *                                           ****16.12.81
         MVI SRBECB,0                                      ****16.12.81
         STFSMODE OFF                                      ****16.12.81
         TPUT  ERRMSG11,LERRMG11                           ****16.12.81
         B     END4                                        ****16.12.81
ERRMSG   EQU   *
         MVI   SRBECB,0
         STFSMODE OFF
         MVC   ERRMSG1+1(3),SRBCONAD
         TPUT  ERRMSG1,LERRMSG1
         B     END4
   SPACE 2
END2     STFSMODE OFF
END4     EQU   *
         SPACE 2
         LA    R0,LSCREEN
         FREEMAIN R,LV=(0),A=(R9)
         SPACE 2
         L     R14,=A(ESRB-SRBSECT)
         FREEMAIN R,LV=(R14),A=(R7),SP=245
      SPACE 2
END3     EQU   *
         SVC   3
         SPACE 2
ABND     EQU   *
         WTO   'AMTFF01 XMPOST ERROR'
         B     END2
         EJECT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*                                                                     *
*        SUBROUTINE TO PROCESS INPUT KEYS FROM TERMINAL               *
*                                                                     *
*        THE AID-BYTE FROM TGET-INPUT IS TRANSLATED TO A NUMERIC VALUE*
*        WHICH CORRESPONDS TO THE PF-KEY-NUMBER (WHERE THE ENTER-KEY  *
*        HAS A VALUE OF 0).                                           *
*                                                                     *
*        SPECIAL PROCESSING IS DONE FOR FOLLOWING PF-KEYS :           *
*        PF1 OR PF13 : DISPLAY LAST ISSUED COMMAND IN INPUT-LINE, SO  *
*            THAT THE USER CAN CHANGE THIS COMMAND                    *
*        PF2 OR PF14 : DISPLAYS PFK-USAGE AND ENABLES THE USER TO     *
*            CHANGE THE ASSIGNED VALUE(S).                            *
*        PF3 OR PF15 : STOPS PROCESSING (SAME AS 'END' COMMAND')      *
*        PF4 OR PF16 : THE LAST COMMAND, THAT WAS ENTERED, IS ISSUED  *
*            AGAIN                                                    *
*        ANY OTHER PF-KEY : ISSUE COMMAND, THAT WAS PREVIOUSLY ASSIGNED
*            TO THAT PF-KEY                                           *
*                                                                     *
*                                                                     *
*        REGISTER USAGE :                                             *
*                                                                     *
*        <R14>   BAL-REGISTER                                         *
*        <R15>   CONTAINS RETURN CODE AT RETURN                       *
*                0  -  EXCUTE COMMAND IN 'CMDBUF'                     *
*                4  -  ISSUE TGET AGAIN                               *
*                8  -  ISSUE SCHEDULE AGAIN TO REFRESH SCREEN         *
*                                                                     *
*                                                                     *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         SPACE 2
         DS    16F                   REGISTER SAVE AREA
PPFK001  EQU   *
         STM   R0,R15,PPFK001-64     SAVE REGISTER
         IC    R15,CMDBUFP           INSERT AID-BYTE FROM TERMINAL INP
         N     R15,=F'63'            DELETE ALL BITS EXCEPT LAST 6
         IC    R15,TABAID(R15)       LOAD NUMERIC VALUE, WHICH
*                                     REPRESENTS THE PFK-VALUE
         STC   R15,NPFK              SAVE PFK-VALUE
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        PROCESS SPECIAL PF KEYS                                      *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         CLI   NPFK,0                Q. ENTER KEY PRESSED ?
         BE    PPFK005               A. YES
         CLI   NPFK,3                Q. END KEY PRESSED ?
         BE    END2                  A. YES
         CLI   NPFK,15               Q. END KEY PRESSED ?
         BE    END2                  A. YES
         CLI   NPFK,4                Q. REPEAT KEY PRESSED ?
         BE    PPFK010               A. YES
         CLI   NPFK,16               Q. REPEAT KEY PRESSED ?
         BE    PPFK010               A. YES
         CLI   NPFK,1                Q. DISPLAY-COMMAND KEY PRESSED ?
         BE    PPFK020               A. YES
         CLI   NPFK,13               Q. DISPLAY-COMMAND KEY PRESSED ?
         BE    PPFK020               A. YES
         CLI   NPFK,2                Q. DISPLAY PFKEY ASSIGNEMENT ?
         BE    PPFK040               A. YES
         CLI   NPFK,14               Q. DISPLAY PFKEY ASSIGNEMENT ?
         BE    PPFK040               A. YES
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        PROCESS OTHER PF KEYS                                        *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         XR    R15,R15
         IC    R15,NPFK              PFKEY VALUE
         BCTR  R15,R0                CORRECT TABLE ENTRY NUMBER
         M     R14,=F'80'            MULTIPLY BY LENGTH OF 1 TABLEENTRY
         A     R15,PFKDPTR           ADD PTR TO BEGINNING OF TABLE
         TRT   0(72,R15),TRTTAB1     SEARCH FOR CHARACTER C'_' = X'6D'
         BNZ   PPFK002               A. FOUND
         MVC   CMDBUF,0(R15)         MOVES COMMAND INTO BUFFER
         XR    R15,R15               RETURN CODE 0
         LM    R0,R14,PPFK001-64     RESTORE REGISTER
         BR    R14
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        DISPLAY COMMAND ON INPUT LINE FOR MODIFICATIONS              *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
PPFK002  EQU   *
         MVI   TPUTBUFF,X'27'        ESCAPE CHARACTER
         MVI   TPUTBUFF+1,X'F1'      COMMAND CODE WRITE
         MVI   TPUTBUFF+2,X'C5'      NO ALARM, KEYBOARD RESTORE
         MVI   TPUTBUFF+3,X'11'      ORDER SBA
         LA    R3,22*80+2            RELATIVE SCREEN POSITION
         STC   R3,TPUTBUFF+5         COMPUTE CURSOR POSITION
         SRL   R3,6
         STC   R3,TPUTBUFF+4
         NI    TPUTBUFF+5,X'3F'
         TR    TPUTBUFF+4(2),TABSBA  TRANSLATE TO SCREEN-ADDRESS
         MVI   TPUTBUFF+6,X'1D'      ORDER SF
         MVI   TPUTBUFF+7,X'C1'      ATTRIBUTES
         MVC   TPUTBUFF+8(72),0(R15) MOVE COMMAND TEXT
         MVI   TPUTBUFF+80,X'11'     SBA
         SR    R1,R15
         LA    R1,22*80+3(R1)        RELATIVE SCREEN POSITION
         STC   R1,TPUTBUFF+82        COMPUTE CURSOR POSITION
         SRL   R1,6
         STC   R1,TPUTBUFF+81
         NI    TPUTBUFF+82,X'3F'
         TR    TPUTBUFF+81(2),TABSBA TRANSLATE TO SCREEN-ADDRESS
         MVI   TPUTBUFF+83,X'13'     INSERT CURSOR
         TPUT  TPUTBUFF,84,FULLSCR
         LA    R15,4                 RETURN CODE 4
         LM    R0,R14,PPFK001-64     RESTORE REGISTER
         BR    R14
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        PROCESS ENTER KEY                                            *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
PPFK005  EQU   *
         CLC   CMDBUF,BLANKS         Q. COMMAND ENTERED ?
         BE    PPFK006               A. NO
         MVC   BPFKSAVE,CMDBUFP      A. YES - SAVE LAST COMMAND ENTERED
PPFK006  EQU   *
         XR    R15,R15               RETURN CODE 0
         BR    R14                   RETURN
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        PROCESS REPEAT KEY (PF4 OR PF16)                             *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
PPFK010  EQU   *
         MVC   CMDBUF,BPFKSAVE+6     RESTORE LAST COMMAND ENTERED
         XR    R15,R15               RETURN CODE 0
         BR    R14                   RETURN
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        PROCESS DISPLAY-LAST-COMMAND KEY (PF1 OR PF13)               *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
PPFK020  EQU   *
         MVI   TPUTBUFF,X'27'        ESCAPE CHARACTER
         MVI   TPUTBUFF+1,X'F1'      COMMAND CODE : WRITE
         MVI   TPUTBUFF+2,X'C5'      WCC : NO ALARM, KEYBOARD RESTORE
         MVI   TPUTBUFF+3,X'11'      ORDER : SBA
         MVC   TPUTBUFF+4(2),BPFKSAVE+4      TEXT POSITION
         MVI   TPUTBUFF+6,X'1D'      ORDER : SF
         MVI   TPUTBUFF+7,X'C1'      ATTRIBUTE : NON-PROTECT, NON-HIGH-
*                                       LIGHT, MODIFIED
         MVI   TPUTBUFF+8,C' '
         LA    R14,9                 LENGTH FOR TPUT IF NO DATA
         LA    R3,TPUTBUFF+9         PTR TO NEXT SBA-ORDER FOR SET CURS
         L     R15,TGETLENG          LENGTH OF PREVIOUS TGET
         S     R15,=F'7'             Q. LENGTH OF DATA ENTERED - 1= 0?
         BM    PPFK025               A. YES
         EX    R15,MVCDATA           A. NO - MOVE DATA
         LA    R14,0(R14,R15)        NEW LENGTH FOR TPUT
         LA    R3,0(R3,R15)
         SPACE 2
PPFK025  EQU   *
         MVI   0(R3),X'11'           ORDER : SBA
         MVC   1(2,R3),BPFKSAVE+1    CURSOR POSITION
         MVI   3(R3),X'13'           ORDER : IC
         LA    R14,4(R14)            NEW LENGTH OR TPUT
         TPUT  TPUTBUFF,(14),FULLSCR WRITE LAST COMMAND ONTO SCREEN
         LA    R15,4                 RETURN CODE 4
         L     R14,PPFK001-8         RESTORE RETURN REGISTER
         BR    R14                   RETURN
         SPACE 2
MVCDATA  MVC   TPUTBUFF+8(1),BPFKSAVE+6
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        PROCESS DISPLAY PFKEY ASSIGNEMENT (PF2 OR PF14)              *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
PPFK040  EQU   *
         LA    R3,2                  2 * 8 KEYS TO BE PROCESSED
         LA    R5,PFKBUFF1           PTR TO PLACE FIRST PFKEY DEF IN
         L     R6,PFKDPTR            PTR TO PFK VALUES
         SPACE 2
PPFK045  EQU   *
         LA    R6,4*80(R6)           PFKEYS 1-4 AND 13-16 SPECIAL USE
         LA    R4,8                  PROCESS 8 PFKEYS
         SPACE 2
PPFK050  EQU   *
         MVC   0(72,R5),0(R6)        MOVE TEXT INTO OUTPUT-BUFFER
         LA    R6,80(R6)             PTR TO NEXT PFKEY DEFINITION
         LA    R5,82(R5)             PTR TO NEXT FIELD IN OUTPUT-BUFFER
         BCT   R4,PPFK050            PROCESS NEXT PFKEY
         BCT   R3,PPFK045            PROCESS NEXT SERIES OF PFKEYS
         SPACE 2
         TPUT  PFKBUFF,PFKBUFFL,FULLSCR
         TGET  CMDBUFP,83,ASIS
         SPACE 2
         LA    R15,8                 RETURN CODE 8
         LM    R0,R14,PPFK001-64     RESTORE REGISTER
         BR    R14                   RETURN
         EJECT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*                                                                     *
*        SUBROUTINE TO ASSIGN COMMANDS TO PF-KEYS                     *
*                                                                     *
*        THIS ROUTINE DYNALLOCS A MEMBER OF A PARTITIONED DATASET     *
*        (THE DSNAME IS CONTAINED IN 'DYNDSN'). THE MEMBER NAME IS THE*
*        TSO-USERS JOBNAME. 24 LINES ARE READ FORM THIS MEMBER, EACH  *
*        LINE MAY CONTAIN A SPECIFIC COMMAND IN COLUMNS 1 - 72.       *
*        THE COMMAND TEXT IS ASSIGNED TO THE CORRESPONDING PF-KEY     *
*        EXCEPT PFKEYS 1,2,3,4,13,14,15 AND 16, WHICH HAVE SPECIAL    *
*        FUNCTIONS ASSIGNED.                                          *
*        LINE 5 IS ASSIGNED TO PF-KEY 5, LINE 6 IS ASSIGNED TO PF-KEY *
*        6, AND SO ON.                                                *
*        THE ASSIGNED COMMANDS ARE EXECUTED THROUGH PRESSING THE      *
*        CORRESPONDING PF-KEY.                                        *
*        IF AN ERROR IS DETECTED, NO PF-KEYS ARE ASSIGNED, AND NO     *
*        PF-KEYS MAY BE USED.                                         *
*                                                                     *
*                                                                     *
*        REGISTER USAGE :                                             *
*                                                                     *
*        <R14>   BAL-REGISTER                                         *
*                                                                     *
*                                                                     *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         SPACE 2
         DS    16F                   REGISTER SAVE AREA
APFK001  EQU   *
         STM   R0,R15,APFK001-64     SAVE REGISTER
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        GETMAIN FOR INTERNAL PF-KEY-DEFINITION-BUFFER                *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         GETMAIN R,LV=24*80
         ST    R1,PFKDPTR
         LR    R0,R1
         LA    R1,24*80
         XR    R14,R14
         LA    R15,1
         SLL   R15,30                SHIFT TO X'40' IN HIGH-ORDER-BYTE
         MVCL  R0,R14                CLEAR BUFFER
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        DYNALLOC PARTITIONED DATASET                                 *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         L     R3,CVTPTR             PTR TO CVT
         USING CVT,R3
         L     R3,CVTTCBP            PTR TO TCBWORDS
         DROP  R3
         L     R3,4(R3)              PTR TO TCB
         L     R3,12(R3)             PTR TO TIOT
         MVC   DYNMEMB+1(7),0(R3)    TSO USER'S JOBNAME
         LA    R1,PDYNRB             PTR TO PTR TO RB
         DYNALLOC
         LTR   R15,R15               Q. DYNALLOC DONE ?
         BNZ   APFK950               A. NO
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        BUILD BPAM-DCB AND OPEN IT                                   *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         LA    R3,PFKDCBPO
         USING IHADCB,R3
         MVC   DCBDDNAM,DYNDDN       MOVE RETRIEVED DDNAM
         OPEN  ((3),INPUT)
         TM    DCBOFLGS,DCBOFOPN     Q. SUCCESSFULL OPENED ?
         BZ    APFK950               A. NO
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        SEARCH  MEMBERNAME                                           *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         FIND  PFKDCBPO,DYNMEMB,D
         LTR   R15,R15               Q. MEMBER FOUND ?
         BNZ   APFK900               A. NO
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        DYNALLOC MEMBER OF PARTITIONED DATASET                       *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         NI    DYNTXTP3,X'FF'-128    RESET END-OF-LIST INDICATOR
         LA    R1,PDYNRB             PTR TO PTR TO RB
         DYNALLOC
         LTR   R15,R15               Q. DYNALLOC DONE ?
         BNZ   APFK900               A. NO
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        BUILD DCB AND OPEN IT                                        *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         LA    R3,PFKDCBPS
         USING IHADCB,R3
         MVC   DCBDDNAM,DYNDDN        MOVE RETRIEVED DDNAME
         OPEN  ((3),INPUT)
         TM    DCBOFLGS,DCBOFOPN      Q. SUCCESSFULL OPENED ?
         BZ    APFK900                A. NO
         DROP  R3
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        READ 24 RECORDS AND PUT INTO PFKEY-DEFINITION-BUFFER         *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         L     R3,PFKDPTR             PTR TO PFKEY-DEFINITION-BUFFER
         LA    R4,24
APFK010  EQU   *
         GET   PFKDCBPS
         OI    IPFK,X'80'             INDICATE PFKEYS ASSIGNED
         MVC   0(72,R3),0(R1)         MOVE COMMAND TEXT
         LA    R3,80(R3)              PTR TO NEXT ENTRY
         BCT   R4,APFK010             LOOP AGAIN
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        CLOSE DATASET AND FREE IT AFTER 24 RECORDS OR AT EOF         *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
APFK020  EQU   *
         CLOSE PFKDCBPS
         FREEPOOL PFKDCBPS
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        CLOSE BPAM DCB                                               *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
APFK900  EQU   *
         CLOSE (PFKDCBPO,)
         SPACE 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        RETURN                                                       *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
APFK950  EQU   *
         LM    R0,R15,APFK001-64      RESTORE REGISTER
         BR    R14
         EJECT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        DYNALLOC REQUEST BLOCK                                       *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         SPACE
PDYNRB   DC    AL1(128),AL3(DYNRB)
DYNRB    DC    AL1(20,1),AL2(0,0,0)
         DC    A(DYNTXTP0)
         DC    A(0,0)
DYNTXTP0 DC    A(DYNRTDDN)
         DC    A(DYNDSN)
         DC    A(DYNDISP)
DYNTXTP3 DC    AL1(128),AL3(DYNFREE)
         DC    AL1(128),AL3(DYNMEMBR)
DYNRTDDN DC    AL2(DALRTDDN,1,8)
DYNDDN   DC    CL8' '
DYNDSN   DC    AL2(DALDSNAM,1,44)
         DC    CL44'SYS1.XHELP'
DYNMEMBR DC    AL2(DALMEMBR,1,8)
DYNMEMB  DC    CL8'@'
DYNDISP  DC    AL2(DALSTATS,1,1),X'08'
DYNFREE  DC    AL2(DALCLOSE,0)
         EJECT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*        DCB                                                          *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
PFKDCBPS DCB   DSORG=PS,MACRF=GL,EODAD=APFK020
         EJECT
PFKDCBPO DCB   DSORG=PO,MACRF=R
         EJECT
         DS    0D
TGETLENG DS    F
TPUTBUFF DS    CL92
TIME     DC    C'00000200'
XUCMID   DC    X'00'
BLANKS   DC    CL80' '
CMND     DC    H'84',X'4000'
CMNDBUF  DC    CL80' '
CMDBUFP  DC    CL6' '
CMDBUF   DC    CL80' '
         DC    F'0'
WTOX     DS    0F
         DC    H'105',X'4000'
CMDID    DC    CL21'COMMAND BY 12345678: '
WTOBUF   DC    CL80' '
ERRMSG1  DC    C' XXX IS NOT AN ACTIV CONSOLE'
LERRMSG1 EQU   *-ERRMSG1
ERRMSG11 DC    C' INVALID SCREEN SIZE'                     ****16.12.81
LERRMG11 EQU   *-ERRMSG11                                  ****16.12.81
BPFKSAVE DS    CL86              SAVED LAST ISSUED COMMAND
NPFK     DS    AL1               SAVED PFKEY # OF LAST INPUT
IPFK     DS    XL1               X'80' INDICATES PFKEYS ASSIGNED
TABAID   DC    AL1(0,13,14,15,16,17,18,19,20,21,22,23,24,0,0,0)
         DC    16AL1(0)
         DC    14AL1(0),AL1(26,0)
         DC    AL1(0,1,2,3,4,5,6,7,8,9,10,11,12,0,0,0)
PFKDPTR  DS    A                 PTR TO INTERNAL PFKEY DEFINITION TABLE
TRTTAB1  DC    256XL1'00'
         ORG   TRTTAB1+C'_'
         DC    XL1'FF'
         ORG
TABSBA   DC    CL16' ABCDEFGHIÖ.<(+×'
         DC    CL16'&&JKLMNOPQR!$*);^'
         DC    CL16'-/STUVWXYZ|,%_>?'
         DC    CL16'0123456789:#@''="'
PFKBUFF  DC    X'C51140403C40400011404013'
         DC    X'1DE8',19CL1'-',C'  OPERATOR COMMANDS ASSIGNED TO PFKEY*
               S  ',20CL1'-'
         DC    X'1DE8',CL79' '
         DC    X'1DE8',C' PF5  ',X'1D60'
PFKBUFF1 DC    CL72' '
         DC    X'1DE8',C' PF6  ',X'1D60',CL72' '
         DC    X'1DE8',C' PF7  ',X'1D60',CL72' '
         DC    X'1DE8',C' PF8  ',X'1D60',CL72' '
         DC    X'1DE8',C' PF9  ',X'1D60',CL72' '
         DC    X'1DE8',C' PF10 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF11 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF12 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF17 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF18 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF19 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF20 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF21 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF22 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF23 ',X'1D60',CL72' '
         DC    X'1DE8',C' PF24 ',X'1D60',CL72' '
         DC    X'1DE8',CL79' '
         DC    X'1DE8',CL79' SPECIAL FUNCTIONS ARE ASSIGNED TO '
         DC    X'1DE8',CL79'    PF1 / PF13    RESHOW LAST COMMAND ISSUE*
               D'
         DC    X'1DE8',CL79'    PF2 / PF14    DISPLAY PF-KEY USAGE'
         DC    X'1DE8',CL79'    PF3 / PF15    TERMINATE'
         DC    X'1DE8',CL79'    PF4 / PF16    REPEAT EXECUTION OF LAST *
               COMMAND ENTERED'
PFKBUFFL EQU   *-PFKBUFF
         LTORG
         TITLE  'SRB ROUTINE'
         DS    0D
CODEA    EQU   *
         DROP  R12,R2,R7
         USING *,R15
         USING SRBSECT,R1
         LR    R1,R0
         STM   R0,R15,SAVESRB
         DROP  R1,R15
         LR    R2,R1
         USING CODEA,R12
         USING SRBSECT,R2
         LR    R12,R15
         SPACE 2
         L     R1,16
         L     R1,100(R1)
         USING UCM,R1
         LM    R3,R5,UCMVEA
DID0     EQU   *
         USING UCMLIST,R3
         L     R1,UCMUCB           DEBUGGING
         CLI   SRBCONAD,64             IF SPECIFIC REQUEST
         BNE   SPEC
         TM    UCMDISP1,UCMDISPA   MASTER-CONSOLE?
         BNO   DID1    NO,CHECK NEXT
         B     DID00
SPEC     CLC   SRBCONAD,13(R1)   IS IT THE REQUESTED CONSOLE ?
         BNE   DID1                NO - SEARCH FOR NEXT
DID00    EQU   *
         MVC   SRBCONAD,13(R1)   MOVE UNITADDRESS
         MVC   UNITXXX(3),SRBCONAD MOVE UNITADDRESS
         TM    UCMATR,UCMUF        IS IT ACTIVE ?
         BZ    DID1                NO - UNUSEABLE
         L     R10,UCMXB           IS IT GRAPHICS ?
         LTR   R10,R10
         BZ    DID1                NO - USELESS
         L     R6,0(R10)           FIND  THE PAGEABLE DCM (TDCM)
         LTR   R6,R6               DOES IT EXIST ?
         BZ    DID1                NO - UNUSEABLE
         CLI   X'131'(R6),X'10'    IS IS A 3277 ?
         MVC   SRBCONID,UCMID        SAVE THIS
         BE    GOTONE              OKAY GO ON
DID1     EQU   *
         BXLE  R3,R4,DID0          GET THE NEXT ENTRY
         LA    R10,1
         SLL   R10,24
         B     ERRPOST
   SPACE 2
*
* A CONSOLE HAS BEEN FOUND COPY THE SCREEN AND DISPLAY IT FOR THE USER
*        R3 ->  UCME
*        R10 -> RDCM
*        R6 ->  TDCM
*
GOTONE   EQU   *
         LH    R1,X'FE'(R6)        NUMBER  OF LINES IN MSG AREA
         LR    R14,R1
         CLM   R1,1,SRBLINE        SMALLER SCREEN ?
         BNH   OKSCR
         SR    R1,R1
         IC    R1,SRBLINE          GET MAX NO LINES
         LR    R14,R1 MAX NO. OF LINES
         BCTR  R1,0
         BCTR  R1,0
         BCTR  R1,0
         LTR   R1,R1                                       ****16.12.81
         BNP   ERRPOST1                                    ****16.12.81
OKSCR    EQU   *
         L     R7,X'30'(R6)        POINT TO  THE  FIRST INPUT LINE
         LA    R8,BUFF             OUTPUT LINE
GOT1     EQU   *
         MVC   2(79,R8),0(R7)      MOVE A LINE
         TR    2(79,R8),BLANKTAB   GET IT READABLE
         CLI   5(R8),C'*'    ************* MVS-SE2 SUPPORT ************
         BE    NOSTAR1
         CLI   5(R8),C'@'    ************* MVS-SE2 SUPPORT ************
         BNE   NOSTAR
NOSTAR1  MVC   0(2,R8),=X'1DE8'    HIGH INTENSITY
NOSTAR   EQU   *
         LA    R8,81(,R8)          OUR SIZE
         LA    R7,84(,R7)          THEIRS
         BCT   R1,GOT1             GET EM ALL
         L     R1,X'3C'(R6)
         CLC   X'FE'(2,R6),=X'0014'
         BE    NOPFK
         LA    R8,81(,R8)          SKIP PFK LINE
NOPFK    MVC   0(2,R8),=X'1D60'
         MVC   3(L'ENTRMSG,R8),ENTRMSG
         LA    R8,(1*81)(,R8)      SKIP INPUT LINE
         MVC   0(2,R8),=X'1DE8'    HIGH INTENSITY
         MVC   3(MODELEN,R8),MODEMSG
         LA    R8,(1*81)(,R8)      SKIP INPUT LINE
         MVC   0(2,R8),=X'1DE8'    HIGH INTENSITY
         MVC   2(4,R8),=X'401DC113' CURSOR POSITION
         LA    R8,85(R8)
         MVC   0(LLMSG,R8),LMSG
         LA    R14,87(R8)
         LA    R1,BUFF
         SR    R14,R1
         ST    R14,SRBBUFL         TPUT AND TGET
         SPACE 2
         SR    R10,R10
ERRPOST  EQU   *
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,RELATED=(X)
         LA    R11,SRBECB
         LA    R0,128
         SLL   R0,24
         OR    R11,R0
         LR    R8,R2
         L     R13,SRBMYAS
         L     R15,16
         L     R15,X'98'(R15)
         BALR  R14,R15
         LR    R2,R8
         SPACE 2
    SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,RELATED=(X)
         LM    R0,R15,SAVESRB
         BR    R14
ERRPOST1 EQU   *                                           ****16.12.81
         LA    R10,2                                       ****16.12.81
         SLL   R10,24                                      ****16.12.81
         B     ERRPOST                                     ****16.12.81
         LTORG
         DROP  R2,R12
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'Ö'
         DC    C'Ö.<(+×',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);^-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+X'81'      TRANSLATE LOWER CASE TO UPPER
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+X'91'      ...
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+X'A2'      ...
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
MODEMSG  DC    C'IEE163I MODE= TSO ('
UNITXXX  DC    CL5'   ) '
MODELEN  EQU   *-MODEMSG
ENTRMSG  DC    C'IEE152I     ENTER     CANCEL     D C,K         '
LMSG     DC    X'1DE8'
         DC    C'PF1/13 RESHOW, PF2/14 DISPLAY PFK USAGE, PF3/15 END, P*
               F4/16 REPEAT COMMAND'
LLMSG    EQU   *-LMSG
CODEE    EQU   *
         TITLE  'DUMMY SECTIONS'
         IHASRB
SAVESRB  DC    16F'0'
SRBECB   DC    F'0'
SRBMYAS  DC    F'0'
SRBCONID DC    X'00'
SRBCONAD DC    AL3(0)
SRBBUFL  DC    F'0'
SRBLINE  DC    X'00'
SRBSIZ   EQU   *-SRBSECT
         DC    XL23'00'
         DS    0D
SRBCODE  DS    CL(CODEE-CODEA)
CLEAR    DS    CL11
BUFF     DS    44CL81' '
ENTR     EQU   BUFF+(81*42)
LSCREEN  EQU   *-CLEAR
ENTRICR  EQU   BUFF+(81*23+4)                                    ICR1
ESRB     EQU   *
         SPACE 5
WORK     DSECT
SAVE     DS    18F
SVCSAVE  DS    2F
WORKL    EQU   *-WORK
         EJECT
         IHAPSA
         EJECT
         IHAASCB
         EJECT
         CVT   DSECT=YES
         EJECT
         IEECUCM FORMAT=NEW
         EJECT
         IEFZB4D2
         EJECT
         PRINT NOGEN
         DCBD  DSORG=PS
         EJECT
         END
