         TITLE 'GMTSR05 - EXECUTE-ROUTINE FUER DMPRINT'
***********************************************************************
*
*              P R O L O G
*
***********************************************************************
*
*              PROGRAMMIERER       STOEHLER
*              ERSTELLUNGSDATUM    13.08.72
*              PGM-FUNKTIONEN      AUSDRUCK DER REGISTER-INHALTE, SOWIE
*                                  DER DURCH DAS MACRO DMPRINT ANGE-
*                                  FORDERTEN FELDER
*
* I/O          1.SEQ.OUTPUT        DRUCKER (DDNAME=PRINTOUT,DCB=ZPRINT)
*
* AUFLISTUNG ALLER VERWENDETEN UNTERPROGRAMME
*
*              D01       REGISTER-INHALTE IN TREG LADEN
*              D02       PARAMETER AUFLOESEN
*              D03       OPEN DRUCK-DATEI
*              D04       DRUCKEN DER KOPF-ZEILE
*              D05       PUT ZPRINT
*              D06       DRUCKEN DER REGISTER-INHALTE
*              D07       DRUCKEN DER FELDER
*              D08       ADRESSEN UND LAENGEN ERRECHNEN
*              D09       AUFBEREITEN 1. ZEILE
*              D10       AUFBEREITEN ZEILE
*              D11       ADRESSE DRUCKEN
*              D12       AUFBEREITEN 8-BYTE-FELD
*              D13       COUNTER ERHOEHEN
*
* REGISTER-VERWENDUNG
*
*              R0        FREI
*              R1        PARAMETER
*              R2        WORK-REGISTER
*              R3        WORK-REGISTER
*              R4        WORK-REGISTER
*              R5        WORK-REGISTER
*              R6        WORK-REGISTER
*              R7        WORK-REGISTER
*              R8        WORK-REGISTER
*              R9        WORK-REGISTER
*              R10       WORK-REGISTER
*              R11       BASIS-REGISTER
*              R12       BASIS-REGISTER
*              R13       SAVE-AREA
*              R14       BAL-REGISTER
*              R15       FREI
*
* ENTRY        GMTSR05                                                *
* ENTRY        GMTSR05A  FUER TEST-SUPPORT-ROUTINE UND LOG-FUNKTION   *
*
***********************************************************************
         EJECT
***********************************************************************
*
*              ANFANGS-ROUTINE
*
***********************************************************************
         SPACE 3
         DMANF PGM=GMTSR15,BASREG=(12,11)
         B     A00
         ENTRY GMTSR15A
GMTSR15A L     15,8(15)
         B     2(15)
         DC    A(GMTSR15)
A00      EQU   *
         EJECT
***********************************************************************
*
*              HAUPTPROGRAMM (STEUERLEISTE)
*
***********************************************************************
         SPACE 3
*              D01
*              REGISTER-INHALTE IN TREG LADEN
*              A(SAVE-AREA FUER REGISTER 0,1,13,14,15)IN ZASAVE LADEN
         SPACE
         BAL   R14,D01
         SPACE
*              D02
*              PARAMETER (ADRESSE IN R1) AUFLOESEN
*              UND IN TABELLE TPARM UND ZKOPF LADEN
         SPACE
         BAL   R14,D02
         SPACE
         TM    SCHALTER,ZTSR
         BO    B01
         SPACE
*              D03
*              OPEN DRUCK-DATEI
         SPACE
         BAL   R14,D03
         SPACE
*              D04
*              DRUCKEN DER KOPF-ZEILE
         SPACE
         BAL   R14,D04
         SPACE
*              D06
*              DRUCKEN DER REGISTER-INHALTE
         SPACE
         BAL   R14,D06
         SPACE
*              NUR REGISTER ?
         SPACE
         CLI   TPARM,X'FF'
         BE    C01                     JA
         SPACE
*              D07
*              DRUCKEN DER FELDER
         SPACE
B01      EQU   *
         BAL   R14,D07
         SPACE
         B     C01
         EJECT
***********************************************************************
*                                                                     *
*              END-ROUTINE                                            *
*                                                                     *
***********************************************************************
         SPACE 3
C01      EQU   *
         DMEND
         SPACE 3
         EJECT
***********************************************************************
*
*              D01
*              REGISTER-INHALTE IN TREG LADEN
*              A(SAVE-AREA FUER REGISTER 0,1,13,14,15) IN ZASAVE LADEN
*
***********************************************************************
         SPACE 3
         DC    F'0'
D01      ST    R14,*-4
*              A(SAVE-AREA) IN ZASAVE
         L     R3,4(R13)               LADE A(SAVE-AREA) HSA
         L     R14,12(R3)              LADE RETURN-ADRESSE
         LA    R14,0(R14)              FLAG LOESCHEN
         SH    R14,=H'10'
D0102    EQU   *
         MVC   ZASAVE,0(R14)
*              LADEN REG. 0,1,13,14,15 IN TREG
         L     R2,ZASAVE
         MVC   TREG+108(4),0(R2)       R13
         MVC   TREG+116(4),4(R2)       R14
         MVC   TREG+124(4),8(R2)       R15
         MVC   TREG+4(4),12(R2)        R0
         MVC   TREG+12(4),16(R2)       R1
*              LADEN REG. 2 - 12 IN TREG
         LA    R2,28(R3)               REG. 2 IN SAVE-AREA
         LA    R3,11                   BCT-REG.
         LA    R4,TREG+20
D0105    EQU   *
         MVC   0(4,R4),0(R2)
         LA    R4,8(R4)
         LA    R2,4(R2)
         BCT   R3,D0105
*
D0199    L     R14,D01-4
         BR    R14
         EJECT
***********************************************************************
*
*              D02
*              PARAMETER (ADDRESSE IN R1) AUFLOESEN
*              UND IN TABELLE TPARM UND ZKOPF LADEN
*
***********************************************************************
         SPACE 3
         DC    F'0'
D02      ST    R14,*-4
*              LOESCHEN TPARM UND ZKOPF
         MVI   TPARM,X'40'
         MVC   TPARM+1(175),TPARM
         MVC   TPARM+176(176),TPARM
         MVI   ZKOPF,X'40'
         MVC   ZKOPF+1(15),ZKOPF
*
         LR    R2,R1                   R1 = A(PARAMETER)
         LA    R3,TPARM
         CLI   0(R2),X'FC'
         BNE   D0202
         OI    SCHALTER,ZNOREG
         LA    R2,1(R2)
         B     D0205
D0202    EQU   *
         NI    SCHALTER,X'FF'-ZTSR
         CLI   0(R2),X'FD'
         BNE   D0205
         OI    SCHALTER,ZTSR
         ST    R2,ZPADR
         MVC   ZDCB,1(R2)
         MVC   ZZZ1,ZZZ
         MVC   ZBZ1,ZBZ
         MVC   ZZZ,5(R2)
         MVC   ZBZ,7(R2)
         LA    R2,9(R2)
         CLI   0(R2),X'FB'
         BNE   D0205
         L     R1,ZASAVE
         LA    R2,TREG+4
         LA    R3,16
D0203    EQU   *
         MVC   0(4,R2),0(R1)
         LA    R2,8(R2)
         LA    R1,4(R1)
         BCT   R3,D0203
         BAL   R14,D06
         LA    R3,TPARM
         L     R2,ZPADR
         LA    R2,10(R2)
D0205    EQU   *
         CLI   0(R2),TPARMKZE          KOPF-ZEILE ?
         BE    D0230                   JA
         CLI   0(R2),TPARMKZR          REGISTER-ANGABE ?
         BE    D0210                   JA
         MVI   0(R3),TPARMKZF          FELD-KZ
         SR    R5,R5
         IC    R5,0(R2)                LAENGE BEZEICHNUNG
         BCTR  R5,0                    - 1 = INSTRUKTIONS-LAENGE
         STC   R5,1(R3)
         EX    R5,D02EX                BEZEICHNUNG UEBERTRAGEN
         LA    R5,2(R5)
         AR    R2,R5
         MVC   18(4,R3),0(R2)          S-ADRESSE UND LAENGE
         LA    R2,4(R2)
         LA    R3,22(R3)
         B     D0205
*
D02EX    MVC   2(0,R3),1(R2)           EX-INSTRIKTION
*
D0210    EQU   *
         MVI   0(R3),TPARMKZR          REGISTER-ANGABE
         SR    R5,R5
         IC    R5,1(R2)                LAENGE BEZEICHNUNG
         BCTR  R5,0                    -1 = INSTRUKTIONS-LAENGE
         STC   R5,1(R3)
         LA    R2,1(R2)
         EX    R5,D02EX                BEZEICHNUNG UEBERTRAGEN
         LA    R5,2(R5)
         AR    R2,R5
         MVC   18(2,R3),0(R2)          S-ADRESSE
         MVC   ZHH,0(R2)
D0215    DMTSUCH TREG+8,8,16,ZHH,POS=2,REG=R6,ORG=NS
         CLC   4(2,R6),=H'0'           LAENGE (REGISTER-INHALT)
         BNE   D0220
         MVC   20(2,R3),6(R6)          LAENGE
         B     D0225
D0220    EQU   *
         MVC   20(2,R3),=H'4096'       LAENGE
D0225    EQU   *
         LA    R2,2(R2)
         LA    R3,22(R3)
         B     D0205
D0230    EQU   *
         MVI   0(R3),TPARMKZE
         MVC   ZKOPFBEZ,1(R2)
         MVC   ZKOPFCSN,9(R2)
*
D0299    L     R14,D02-4
         BR    R14
         EJECT
***********************************************************************
*
*              D03
*              OPEN DRUCK-DATEI
*
***********************************************************************
         SPACE 3
         DC    F'0'
D03      ST    R14,*-4
         TM    ZPRINT+48,X'10'         BEREITS EROEFFNET ?
         BO    D0399                   JA
         OPEN  (ZPRINT,(OUTPUT))
         TM    ZPRINT+48,X'10'
         BO    D0320
D0310    EQU   *
         OI    GMTSR15+1,254
         B     C01
D0320    DMULJ (ZPRINT,OUTPUT),JFCB=YES
         CLC   D032001(9),=C'NULLFILE '
         BE    D0310
D0399    L     R14,D03-4
         BR    R14
         EJECT
***********************************************************************
*
*              D04
*              DRUCKEN DER KOPF-ZEILE
*
***********************************************************************
         SPACE 3
         DC    F'0'
D04      ST    R14,*-4
         CP    ZZZ,=P'0'               ZEILEN-ZAEHLER
         BE    D0405
         CP    ZZZ,=P'40'              ZEILEN-ZAEHLER
         BH    D0405
         MVI   ZVORS,C'-'
         MVI   ZEILE,C'*'
         MVC   ZEILE+1(131),ZEILE
         BAL   R14,D05                 PUT ZPRINT
         MVI   ZVORS,C'-'
         B     D0410
D0405    EQU   *
         MVI   ZVORS,C'1'
         AP    ZBZ,=P'1'
D0410    EQU   *
         MVC   ZEILE(21),=C'* * * P R I N T * * *'
         MVC   ZEILE+23(11),=C'CSECT-NAME='
         MVC   ZEILE+34(8),ZKOPFCSN
         MVC   ZEILE+44(11),=C'PRINT-NAME='
         MVC   ZEILE+55(8),ZKOPFBEZ
         SPACE
*              D13
*              COUNTER ERHOEHEN
         BAL   R14,D13
         SPACE
         MVC   ZEILE+81(6),=C'COUNT='
         CVD   R7,ZHD                  COUNTER
         UNPK  ZEILE+87(4),ZHD+5(3)
         MVZ   ZEILE+90(1),=X'F0'
         MVC   ZEILE+65(8),=C'ADDRESS='
         LA    R4,ZEILE+73
         LR    R7,R10
         L     R10,4(R13)              HSA
         L     R10,4(R10)              HSA
         L     R10,16(R10)             EP-ADDR.
         LA    R10,0(R10)              FLAG LOESCHEN
         SR    R7,R10
         SPACE
*              D11
*              ADRESSE DRUCKEN
         BAL   R14,D11
         SPACE
         MVC   ZEILE+93(4),=C'DATE'
         PRINT NOGEN
         MDATUM DTFELD=ZHD
         PRINT GEN
         MVC   ZEILE+98(8),ZHD
         ST    R0,ZHF
         MVC   ZEILE+111(10),=X'402120204B20204B2020'
         ED    ZEILE+111(10),ZHF
         MVC   ZEILE+108(4),=C'TIME'
         CLI   ZVORS,C'1'
         BNE   D0415
         MVC   ZEILE+123(4),=C'PAGE'
         MVC   ZEILE+127(4),=X'40202020'
         ED    ZEILE+127(4),ZBZ
D0415    EQU   *
         BAL   R14,D05                 PUT ZPRINT
D0499    L     R14,D04-4
         BR    R14
         EJECT
***********************************************************************
*
*              D05
*              PUT ZPRINT
*
***********************************************************************
         SPACE 3
         DC    F'0'
D05      ST    R14,*-4
         CLC   ZDRUCK1+13(74),ZDRUCK+13
         BNE   D0502
         TM    SCHALTER,ZSAMELIN+ZSLINES
         BZ    D0525
         BM    D0530
         B     D0535
D0502    EQU   *
         TM    SCHALTER,ZSAMELIN
         BO    D0540
D0503    EQU   *
         TM    SCHALTER,ZTSR
         BZ    D0504
         L     R14,ZDCB
         PUT   (14),ZDRUCK
         B     D0504A
D0504    EQU   *
         PUT   ZPRINT,ZDRUCK
D0504A   EQU   *
         CLI   ZVORS,C'1'
         BNE   D0505
         ZAP   ZZZ,=P'0'
         B     D0520
D0505    EQU   *
         TM    ZVORS,X'30'
         BZ    D0510
         BM    D0515
         AP    ZZZ,=P'2'
         B     D0520
D0510    EQU   *
         AP    ZZZ,=P'1'
         B     D0520
D0515    EQU   *
         AP    ZZZ,=P'3'
D0520    EQU   *
         MVI   ZVORS,C' '
         MVC   ZDRUCK1,ZDRUCK
         MVI   ZEILE,X'40'
         MVC   ZEILE+1(131),ZEILE
         B     D0599
D0525    EQU   *
         MVC   ZDRUCK2+9(35),=35C' '
         MVC   ZDRUCK2+9(5),=C'LINE '
         MVC   ZDRUCK2+15(6),ZDRUCK+4
         MVC   ZDRUCK2+22(13),=C'SAME AS ABOVE'
         OI    SCHALTER,ZSAMELIN
         B     D0520
D0530    EQU   *
         MVI   ZDRUCK2+13,C'S'
         MVI   ZDRUCK2+21,C'-'
         MVC   ZDRUCK2+22(7),ZDRUCK+4
         MVC   ZDRUCK2+29(13),=C'SAME AS ABOVE'
         OI    SCHALTER,ZSLINES
         B     D0520
D0535    EQU   *
         MVC   ZDRUCK2+22(6),ZDRUCK+4
         B     D0520
D0540    EQU   *
         TM    SCHALTER,ZTSR
         BZ    D0545
         L     R14,ZDCB
         PUT   (14),ZDRUCK2
         B     D0550
D0545    EQU   *
         PUT   ZPRINT,ZDRUCK2
D0550    EQU   *
         MVI   ZDRUCK2,C' '
         MVC   ZDRUCK2+1(132),ZDRUCK2
         NI    SCHALTER,X'FF'-ZSAMELIN-ZSLINES
         AP    ZZZ,=P'1'
         B     D0503
D0599    L     R14,D05-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D06                                                    *
*              DRUCKEN DER REGISTER-INHALTE                           *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D06      ST    R14,*-4
         TM    SCHALTER,ZNOREG
         BO    D0630
         MVI   ZVORS,C'-'
         MVC   ZEILE(17),=C'R E G I S T E R S'
         BAL   R14,D05                 PUT ZPRINT
         LA    R2,TREGBEZ
         LA    R3,TREG
         LA    R4,ZEILE+3
         LA    R5,8                    BCT-REGISTER
         LA    R6,2                    BCT-REGISTER
D0605    EQU   *
         MVI   0(R4),C'R'
         MVC   1(2,R4),0(R2)
         L     R9,4(R3)
         LA    R10,8                   BCT-REGISTER
         LA    R7,ZREGFELD
D0610    EQU   *
         SR    R8,R8
         SLDL  R8,4
         CH    R8,=H'9'
         BH    D0615
         AH    R8,=H'240'
         B     D0620
D0615    EQU   *
         AH    R8,=H'183'
D0620    EQU   *
         STC   R8,0(R7)
         LA    R7,1(R7)
         BCT   R10,D0610
         SPACE
         MVC   5(8,R4),ZREGFELD
         LA    R4,15(R4)
         LA    R3,8(R3)
         LA    R2,2(R2)
         BCT   R5,D0605
         SPACE
         CLI   ZEILE+4,C'8'
         BE    D0625
         MVI   ZVORS,C'0'
D0625    EQU   *
         BAL   R14,D05                 PUT ZPRINT
         LA    R4,ZEILE+3
         LA    R5,8                    BCT-REGISTER
         BCT   R6,D0605
         SPACE 2
D0630    EQU   *
         NI    SCHALTER,X'FF'-ZNOREG
         CLI   TPARM,X'FF'
         BNE   D0699
         MVI   ZVORS,C'0'
         MVC   ZEILE(20),=C'*** END OF PRINT ***'
         BAL   R14,D05                 PUT ZPRINT
         SPACE
D0699    L     R14,D06-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D07                                                    *
*              DRUCKEN DER FELDER                                     *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D07      ST    R14,*-4
         LA    R2,TPARM
D0705    EQU   *
         CLI   0(R2),X'FF'             ENDE ?
         BE    D0740                   JA
         CP    ZZZ,=P'58'              BLATTWECHSEL ?
         BL    D0710                   NEIN
         AP    ZBZ,=P'1'
         MVI   ZVORS,C'1'
         MVC   ZEILE+123(4),=C'PAGE'
         MVC   ZEILE+127(4),=X'40202020'
         ED    ZEILE+127(4),ZBZ
         BAL   R14,D05                 PUT ZPRINT
D0710    EQU   *
         MVI   ZVORS,C'-'
         MVC   ZEILE(16),2(R2)         BEZEICHNUNG
         CLI   0(R2),X'FE'             REGISTER ?
         BNE   D0715                   NEIN
         CLC   20(2,R2),=H'4096'
         BNE   D0715
         MVC   ZHH,18(R2)
         DMTSUCH TREG,16,8,ZHH,POS=2,REG=R3,ORG=NS
         CLC   4(2,R3),=H'0'
         BE    D0715
         MVC   ZEILE+38(68),=C'***WARNING*** INVALID LENGTH IN REG, ONL*
               Y 4096 BYTES WILL BE PRINTED'
D0715    EQU   *
         MVO   ZHB1(2),18(1,R2)
         DMTSUCH TREG,16,8,ZHB1,ORG=DA,REG=R7
         L     R7,4(R7)
         MVC   ZHH,18(R2)
         NC    ZHH,=X'0FFF'
         AH    R7,ZHH
         MVC   ZEILE+18(8),=C'ADDRESS='
         LA    R4,ZEILE+26
         LR    R8,R7
         SPACE
*              D11
*              ADRESSE DRUCKEN
         BAL   R14,D11
         SPACE
         LR    R7,R8
         CLC   20(2,R2),=H'0'
         BNE   D0720
         MVC   ZEILE+38(34),=C'***WARNING*** LENGTH = 0, NO PRINT'
         OI    SCHALTER,ZNOPRINT
D0720    EQU   *
         BAL   R14,D05                 PUT ZPRINT
         SPACE 2
         TM    SCHALTER,ZNOPRINT
         BO    D0730
         SPACE
*              D08
*              ADRESSEN UND LAENGEN ERRECHNEN
         BAL   R14,D08
         SPACE
*              D09
*              AUFBEREITEN 1. ZEILE
         BAL   R14,D09
         SPACE
D0725    EQU   *
         TM    SCHALTER,ZLASTLIN
         BO    D0730
         SPACE
*              D10
*              AUFBEREITEN ZEILE
         BAL   R14,D10
         SPACE
         B     D0725
         SPACE
D0730    EQU   *
         NI    SCHALTER,X'0'+ZSAMELIN+ZSLINES+ZTSR
         LA    R2,22(R2)
         B     D0705
         SPACE
D0740    EQU   *
         TM    SCHALTER,ZTSR
         BO    D0750
         MVI   ZVORS,C'0'
         MVC   ZEILE(20),=C'*** END OF PRINT ***'
         BAL   R14,D05                 PUT ZPRINT
         B     D0799
D0750    EQU   *
         L     R2,ZPADR
         MVC   5(2,R2),ZZZ
         MVC   7(2,R2),ZBZ
         MVC   ZZZ,ZZZ1
         MVC   ZBZ,ZBZ1
         SPACE
D0799    L     R14,D07-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D08                                                    *
*              ADRESSEN UND LAENGEN ERRECHNEN                         *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D08      ST    R14,*-4
         LR    R3,R7
         LR    R4,R3
         ST    R3,ZANFADR              ANFANGS-ADR.
         AH    R3,20(R2)
         BCTR  R3,0
         ST    R3,ZENDADR              END-ADR.
         LR    R5,R4
         SRA   R4,5
         SLA   R4,5
         ST    R4,ZDWADR               32-BYTE-GRENZE
         SR    R5,R4
         LR    R4,R5
         SRA   R4,2
         SLA   R4,2
         SR    R5,R4
         SLA   R5,1
         STC   R5,ZDRUFADR             DISPL. IN 8-BYTE-DRUCKFELD
         LA    R5,ZEILE+12
         SRA   R4,2
         CH    R4,=H'4'
         BL    D0820
         AH    R5,=H'3'
D0820    EQU   *
         MH    R4,=H'9'
         AR    R5,R4
         ST    R5,ZDRUADR              DRUCKADRESSE
         MVC   ZLAENGE,20(R2)          LAENGE DES FELDES
D0899    L     R14,D08-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D09                                                    *
*              AUFBEREITEN 1. ZEILE                                   *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D09      ST    R14,*-4
         L     R7,ZDWADR               32-BYTE-GRENZE
         LA    R4,ZEILE+3
         SPACE
*              D11
*              ADRESSE DRUCKEN
         BAL   R14,D11
         SPACE
         MVC   ZFOLADR,ZANFADR
         L     R3,ZDRUADR              DRUCK-ADR.
D0902    EQU   *
         SPACE
*              D12
*              AUFBEREITEN 8-BYTE-FELD
         BAL   R14,D12
         SPACE
         NI    ZDRUFADR,X'0'           DISPL. IM 8-BYTE-FELD
         MVC   0(8,R3),Z8BF            UEBERTRAGEN 8-BYTE-FELD
         C     R3,=A(ZEILE+39)         MITTE DES DUMPS ?
         BNE   D0905                   NEIN
         LA    R3,3(R3)
D0905    EQU   *
         LA    R3,9(R3)                ERHOEHEN DRUCK-ADR.
         CLC   ZLAENGE,=H'0'           ENDE ?
         BE    D0910                   JA
         C     R3,=A(ZEILE+87)         ENDE DER ZEILE ?
         BE    D0910                   JA
         B     D0902
         SPACE
D0910    EQU   *
         TR    ZCHARF,TRTAB            TRANSLATE CHARACTER-FELD
         MVC   ZEILE+89(34),ZCHARF-1   UEBERTRAGEN CHARACTER-FELD
         MVC   ZCHARF,=32C' '          LOESCHEN
         CLC   ZLAENGE,=H'0'           ENDE ?
         BNE   D0915                   NEIN
         OI    SCHALTER,ZLASTLIN       LAST LINE
D0915    EQU   *
         MVI   ZVORS,C'0'
         BAL   R14,D05                 PUT ZPRINT
D0999    L     R14,D09-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D10                                                    *
*              AUFBEREITEN ZEILE                                      *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D10      ST    R14,*-4
         LA    R4,ZEILE+3
         L     R7,ZFOLADR              32-BYTE-GRENZE
         SPACE
*              D11
*              ADRESSE DRUCKEN
         BAL   R14,D11
         SPACE
         LA    R3,ZEILE+12             DRUCK-ADRESSE
D1005    EQU   *
         SPACE
*              D12
*              AUFBEREITEN 8-BYTE-FELD
         BAL   R14,D12
         SPACE
         MVC   0(8,R3),Z8BF            UEBERTRAGEN 8-BYTE-FELD
         C     R3,=A(ZEILE+39)         MITTE DES DUMPS ?
         BNE   D1010                   NEIN
         LA    R3,3(R3)
D1010    EQU   *
         LA    R3,9(R3)                ERHOEHEN DRUCK-ADR.
         CLC   ZLAENGE,=H'0'           ENDE ?
         BE    D1015                   JA
         C     R3,=A(ZEILE+87)         ENDE DER ZEILE ?
         BE    D1015                   JA
         B     D1005
         SPACE
D1015    EQU   *
         TR    ZCHARF,TRTAB            TRANSLATE CHARACTER-FELD
         MVC   ZEILE+89(34),ZCHARF-1   UEBERTRAGEN CHARACTER-FELD
         MVC   ZCHARF,=32C' '          LOESCHEN
         CLC   ZLAENGE,=H'0'           ENDE ?
         BNE   D1020                   NEIN
         OI    SCHALTER,ZLASTLIN       LAST LINE
D1020    EQU   *
         CP    ZZZ,=P'60'              ZEILEN-ZAEHLER
         BL    D1025
         CLC   ZLAENGE,=H'100'         RESTLAENGE < 100
         BL    D1025
         OI    SCHALTER,ZNEWPAGE
D1025    EQU   *
         BAL   R14,D05                 PUT ZPRINT
         TM    SCHALTER,ZNEWPAGE
         BZ    D1099
         NI    SCHALTER,X'FF'-ZNEWPAGE
         MVI   ZVORS,C'1'
         AP    ZBZ,=P'1'
         MVC   ZEILE+123(4),=C'PAGE'
         MVC   ZEILE+127(4),=X'40202020'
         ED    ZEILE+127(4),ZBZ
         BAL   R14,D05                 PUT ZPRINT
         MVI   ZVORS,C'0'
D1099    L     R14,D10-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D11                                                    *
*              ADRESSE DRUCKEN                                        *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D11      ST    R14,*-4
         SLL   R7,8
         LA    R3,6
D1105    EQU   *
         SR    R6,R6
         SLDL  R6,4
         CH    R6,=H'9'
         BH    D1110
         AH    R6,=H'240'
         B     D1115
D1110    EQU   *
         AH    R6,=H'183'
D1115    EQU   *
         STC   R6,0(R4)
         LA    R4,1(R4)
         BCT   R3,D1105
         SPACE
D1199    L     R14,D11-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D12                                                    *
*              AUFBEREITEN 8-BYTE-FELD                                *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D12      ST    R14,*-4
         MVC   Z8BF,=8C' '             LOESCHEN
         LA    R5,8                    BCT-REG.
         CLI   ZDRUFADR,X'0'           DISPL. ANGEGEBEN ?
         BE    D1205                   NEIN
         SR    R4,R4
         IC    R4,ZDRUFADR             DISPLACEMENT
         SR    R5,R4                   BCT-REG.
D1205    EQU   *
         LR    R4,R5
         SRA   R4,1                    DIV. DURCH 2
         L     R6,ZLAENGE1             RESTLAENGE LADEN
         CR    R6,R4                   KLEINER BCT-REG. ?
         BH    D1210                   NEIN
         LR    R4,R6
         SLA   R6,1                    MULT. MIT 2
         LR    R5,R6                   BCT-REG.
         SR    R6,R6
         STH   R6,ZLAENGE              LAENGE = 0
D1210    EQU   *
         LA    R7,Z8BF
         CH    R5,=H'8'                BCT-REG.
         BE    D1215
         SR    R8,R8
         IC    R8,ZDRUFADR
         AR    R7,R8                   DISPLACEMENT ADD.
D1215    EQU   *
         LR    R6,R7
         L     R8,ZFOLADR              DATEN-ADR.
         MVC   ZHF,0(R8)
         AR    R8,R4
         ST    R8,ZFOLADR              NEUE FOLGE-ADR.
         L     R9,ZHF
D1220    EQU   *                       PRINT-SCHLEIFE
         SR    R8,R8
         SLDL  R8,4
         CH    R8,=H'9'
         BH    D1225
         AH    R8,=H'240'
         B     D1230
D1225    EQU   *
         AH    R8,=H'183'
D1230    EQU   *
         STC   R8,0(R7)
         LA    R7,1(R7)
         BCT   R5,D1220
         SPACE
         ST    R3,ZHF1                 ADRESSIERUNG IN ZCHARF
         DMTSUCH ZATAB,8,8,ZHF1,REG=R10,POS=4
         L     R10,0(R10)
         C     R6,=A(Z8BF)             DISPLACEMENT ?
         BE    D1240                   NEIN
         S     R6,=A(Z8BF)
         SRA   R6,1
         AR    R10,R6                  DISPL. ADD.
D1240    EQU   *
         BCTR  R4,0
         EX    R4,D1245EX              MVC-INSTRUKTION
         CLC   ZLAENGE,=H'0'           ENDE ?
         BE    D1250                   JA
         LA    R4,1(R4)
         L     R6,ZLAENGE1             RESTLAENGE VERMINDERN
         SR    R6,R4
         STH   R6,ZLAENGE
         B     D1250
         SPACE
D1245EX  MVC   0(0,R10),ZHF            EX-INSTRUKTION
         SPACE
D1250    EQU   *
D1299    L     R14,D12-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D13
*              COUNTER ERHOEHEN
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D13      ST    R14,*-4
         L     R7,4(R13)               HSA
         L     R10,12(R7)              RETURN-ADR.
         LH    R7,2(R10)
         LA    R7,1(R7)                COUNTER + 1
         STH   R7,2(R10)
D1399    L     R14,D13-4
         BR    R14
         TITLE 'GMTSR05 - DEFINITIONEN'
***********************************************************************
*
*             D E F I N I T I O N E N
*
***********************************************************************
         SPACE 3
*
***            TABELLE TPARM
***            PARAMETER-INFORMATIONEN AUS DEM AUFRUFENDEN PROGRAMM
***            JE ANGEFORDERTEM FELD EIN TABELLEN-GLIED
*
         DS    0H
TPARM    DS    16CL22
         ORG   TPARM
TPARMKZ  DS    X                       KENNZEICHEN
TPARMKZF EQU   X'00'                   FELD-ANGABE
TPARMKZR EQU   X'FE'                   REGISTER-ANGABE
TPARMKZE EQU   X'FF'                   ENDE
TPARMLB  DS    X                       LAENGE BEZEICHNUNG
TPARMBEZ DS    CL16                    BEZEICHNUNG
TPARMSA  DS    S                       S-ADRESSE
TPARMLF  DS    H                       LAENGE DES FELDES
         ORG
         DC    X'FF'                   ENDE TPARM
         SPACE 3
*
***            ZWISCHENSPEICHER ZKOPF
***            PARAMETER-INFORMATIONEN,WELCHE DIE KOPFZEILE BETREFFEN
*
ZKOPF    DS    0CL16
ZKOPFBEZ DS    CL8                     BEZEICHNUNG (LABEL)
ZKOPFCSN DS    CL8                     CSECT-NAME
         SPACE 3
*
***            ADRESSE SAVE-AREA (REGISTER 0,1,13,14,15)
*
ZASAVE   DS    A
         EJECT
*
***            TREG
***            TABELLE MIT DEN REGISTER-INHALTEN
***            DIE VOR DEM AUFRUF DES MACROS DMPRINT BESTANDEN
*
         DS    0F
TREG     DS    16CL8
         ORG   TREG
TREGXNR  DS    X                       REGISTER-NR. (HEX.)
         DS    X                       FILLER
TREGSA   DS    S                       S-ADRESSE (VERSCHOBEN)
TREGINH  DS    F                       REGISTER-INHALT
         ORG   TREG
         DC    X'0',X'0',S(0),A(0)
         DC    X'1',X'0',S(0(0)),A(0)
         DC    X'2',X'0',S(0),A(0)
         DC    X'3',X'0',S(0(2)),A(0)
         DC    X'4',X'0',S(0),A(0)
         DC    X'5',X'0',S(0(4)),A(0)
         DC    X'6',X'0',S(0),A(0)
         DC    X'7',X'0',S(0(6)),A(0)
         DC    X'8',X'0',S(0),A(0)
         DC    X'9',X'0',S(0(8)),A(0)
         DC    X'A',X'0',S(0),A(0)
         DC    X'B',X'0',S(0(10)),A(0)
         DC    X'C',X'0',S(0),A(0)
         DC    X'D',X'0',S(0(12)),A(0)
         DC    X'E',X'0',S(0),A(0)
         DC    X'F',X'0',S(0(14)),A(0)
*
TREGBEZ  DC    C'0 1 2 3 4 5 6 7 8 9 101112131415'
*
ZREGFELD DC    CL8' '
*
         EJECT
*              ZWISCHENFELDER
ZHD      DS    0D
         DC    8X'0'
ZHF      DC    F'0'
ZHF1     DC    F'0'
ZHH      DC    H'0'
ZHB1     DC    B'0'
         DC    X'0'                    WG/MVO
SCHALTER DC    X'0'
ZNOPRINT EQU   X'80'
ZLASTLIN EQU   X'40'
ZNEWPAGE EQU   X'20'
ZSAMELIN EQU   X'10'
ZSLINES  EQU   X'08'
ZTSR     EQU   X'04'
ZNOREG   EQU   X'02'
*
ZDCB     DC    A(0)
*
ZPADR    DC    A(0)
*
ZANFADR  DC    F'0'                    ANFANGS-ADR.
ZFOLADR  DC    F'0'                    FOLGEADRESSE
ZENDADR  DC    F'0'                    END-ADR.
ZDWADR   DC    F'0'                    32-BYTE-GRENZE
ZDRUADR  DC    F'0'                    DRUCK-ADR
ZLAENGE1 DC    H'0'
ZLAENGE  DC    H'0'                    LAENGE DES FELDES
ZDRUFADR DC    X'0'                    DISPL. IM 8-BYTE-DRUCKFELD (ANF)
*
Z8BF     DC    CL8' '                  8-BYTE-FELD
*
         DC    C'*'
ZCHARF   DC    CL32' '                 CHARACTER-FELD
         DC    C'*'
*
         EJECT
*
***            SPEICHER FUER DRUCK-DATEI
*
ZDRUCK   DS    0CL133
ZVORS    DC    C' '
ZEILE    DC    CL132' '
*
ZZZ      DC    PL2'0'                  ZEILEN-ZAEHLER
ZBZ      DC    PL2'0'                  SEITEN-ZAEHLER
*
ZZZ1     DC    PL2'0'
ZBZ1     DC    PL2'0'
*
ZDRUCK1  DS    CL133
*
ZDRUCK2  DC    CL133' '
*
***            TRANSLATE-TABELLE
*
TRTAB    DS    0CL256
         DC    64X'4B',X'40',11X'4B'
         DC    X'4C4D4E4F50'
         DC    10X'4B'
         DC    X'5B5C5D5E5F6061'
         DC    9X'4B'
         DC    X'6B6C6D6E6F'
         DC    10X'4B'
         DC    X'7A7B7C7D7E7F'
         DC    65X'4B'
         DC    C'ABCDEFGHI'
         DC    7X'4B'
         DC    C'JKLMNOPQR'
         DC    8X'4B'
         DC    C'STUVWXYZ'
         DC    6X'4B'
         DC    C'0123456789'
         DC    6X'4B'
*
         EJECT
*
***            TABELLE ZUR ADRESSIERUNG IM ZCHARF
*
ZATAB    DC    A(ZCHARF),A(ZEILE+12)
         DC    A(ZCHARF+4),A(ZEILE+21)
         DC    A(ZCHARF+8),A(ZEILE+30)
         DC    A(ZCHARF+12),A(ZEILE+39)
         DC    A(ZCHARF+16),A(ZEILE+51)
         DC    A(ZCHARF+20),A(ZEILE+60)
         DC    A(ZCHARF+24),A(ZEILE+69)
         DC    A(ZCHARF+28),A(ZEILE+78)
         EJECT
         LTORG
         EJECT
***********************************************************************
*
*              DCB DRUCK-DATEI
*
***********************************************************************
         SPACE 3
         PRINT NOGEN
ZPRINT   DCB   DDNAME=PRINTOUT,                                        *
               DSORG=PS,                                               *
               MACRF=(PM),                                             *
               RECFM=FBA,                                              *
               LRECL=133,                                              *
               BLKSIZE=1330
         SPACE 3
         END
