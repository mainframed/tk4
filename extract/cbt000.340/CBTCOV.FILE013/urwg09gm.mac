         MACRO
&BEZ     MTIME &MDATUM,                SYMBOL FUER MASCH.DATUM         X
               &ZEIT,                  SYMBOL FUER UHRZEIT             X
               &DATUM,                 SYMBOL TAGESDATUM               X
               &UPRO=NO                MACRO WIRD NICHT ALS UPRO DEFIN.
.*                                     UPRO=YES MACRO WIRD ALS UNTER-
.*                                     PROGRAMM DEFINIERT.
.*
.*
.*       MIT DEM MACRO - MTIME - IST ES MOEGLICH, DIE UHRZEIT,
.*       MASCHINEN- UND TAGESDATUM IN AUFBEREITETER FORM ZU ERHALTEN.
.*
.*       ES IST MOEGLICH NUR EINEN VON DREI OPERANDEN ANZUGEBEN.
.*
.*       DAS MACRO KANN MEHRMALS AUFGERUFEN WERDEN, ALS UNTERPROGRAMM
.*       JEDOCH NUR EINMAL.
.*
.*
.*       BEISPIEL..
.*
.*       MTIME BER1,BER2,BER3,UPRO=YES
.*             BER1     =     MASCHINENDATUM     XX.XXX
.*             BER2     =     UHRZEIT            XX.XX.XX
.*             BER3     =     TAGESDATUM       TT.MM.JJ
.*             UPRO     =     'YES'    MACRO WIRD ALS UPRO GENERIERT
.*
.*
.*
.*
.*
         GBLA  &A1
         GBLA  &G1
         LCLB  &BL1
         LCLB  &BL2
         LCLB  &BL3
         LCLC  &NAME
&A1      SETA  &A1+1
         AIF   ('&MDATUM' EQ '').MT1
&BL1     SETB  1
.MT1     ANOP
         AIF   ('&ZEIT' EQ '').MT2
&BL2     SETB  1
.MT2     ANOP
         AIF   ('&DATUM' EQ '').MT3
&BL3     SETB  1
.MT3     ANOP
         AIF   (&BL1 OR &BL2 OR &BL3).MT4
         MNOTE 12,'KEIN OPERAND IM MTIME MAKRO'
         MEXIT
.MT4     ANOP
&NAME    SETC  '&BEZ'
         AIF  ('&UPRO' NE 'YES').MT42
         AIF   (&G1 EQ 1).MT41
&G1      SETA  1
&NAME    MUPEIN R14
&NAME    SETC  ' '
&G1      SETA  1
         AGO   .MT42
.MT41    ANOP
         MNOTE 8,'ZWEITER AUFRUF VON MTIME ALS UPRO IST FALSCH'
         AGO   .END2
.MT42    ANOP
&NAME    TIME  DEC
&NAME    SETC  ' '
         AIF   (&BL1).MT5
         AGO   .MT6
.MT5     ANOP
         ST    R1,MMDAT&A1
         MVC   &MDATUM.(7),=X'4021204B202020'
         ED    &MDATUM.(7),MMDAT&A1+1
.MT6     ANOP
         AIF   (&BL2).MT7
         AGO   .MT8
.MT7     ANOP
         ST    R0,MMDAT&A1
         MVO   MMDAT&A1,MMDAT&A1.(L'MMDAT&A1-1)
         OI    MMDAT&A1+L'MMDAT&A1-1,X'0F'
         MVC   &ZEIT.(10),=X'402120204B20204B2020'
         ED    &ZEIT.(10),MMDAT&A1
.MT8     ANOP
         AIF   (&BL3).MT9
         AGO   .MT10
.MT9     ANOP
         ST    R1,MMDAT&A1
         MVO   MDAT&A1.(8),MMDAT&A1.(2)
         CVB   R1,MDAT&A1
         LR    R14,R1
         SR    R0,R0
         D     R0,=F'4'
         AR    R0,R0
         BNE   *+8
         OI    MTAB&A1+1,X'01'
         ZAP   MDAT&A1.(8),MMDAT&A1+2(2)
         SR    R0,R0
         CVB   R2,MDAT&A1
         LA    R1,MTAB&A1-1
         LA    R15,0
         LA    R15,1(0,R15)
         IC    R0,0(R15,R1)
         SR    R2,R0
         BP    *-10
         AR    R2,R0
         MH    R15,=H'100'
         AR    R14,R15
         MH    R2,=H'10000'
         AR    R14,R2
         CVD   R14,MDAT&A1
         MVC   &DATUM.(10),=X'402120204B20204B2020'
         ED    &DATUM.(10),MTIME&A1
.MT10    ANOP
         B     MMDAT&A1+4
         AIF   (&BL3).MT11
         CNOP  0,4
         AGO   .MT12
.MT11    ANOP
MTAB&A1  DC    AL1(31),AL1(28),AL1(31),AL1(30),AL1(31),AL1(30),AL1(31)
         DC    AL1(31),AL1(30),AL1(31),AL1(30),AL1(31)
         DS    0D
MDAT&A1  DC    XL4'C'
MTIME&A1 DC    XL4'C'
.MT12    ANOP
MMDAT&A1 DC    XL4'C'
.END     ANOP
         AIF   ('&UPRO' EQ 'YES').END1
         AGO   .END2
.END1    MUPAUS &BEZ
.END2    ANOP
         MEND
