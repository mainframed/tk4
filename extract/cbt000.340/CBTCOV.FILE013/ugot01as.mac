&PROG    SETC  'UGOT01A'          PROGRAM-NAME
         TITLE 'UGOT01A / ESTAE EXIT FOR ABEND CODES S0C7'
         SPACE 3
***********************************************************************
*                                                                     *
*        LEGEND                                                       *
*                                                                     *
***********************************************************************
         SPACE 3
*              THE PROGRAM UGOT01A ESTABLISHES AN ESTAE EXIT ROUTINE
*              FOR ABEND CODE S0C7 (DATA EXCEPTION).
*              THE EXIT-ROUTINE PRINTS IN CASE OF AN ABEND S0C7 THE
*              FOLLOWING FIELDS:
*                  NAME OF THE MODULE, WHICH PRODUCED THE ERROR
*                  PSW (BASIC AND EXTENDED MODE)
*                  RELATIVE ADDRESS OF THE INSTRUCTION, WHICH PRODUCED
*                      THE ERROR, IF IT CAN BE LOCATED WTHIN A MODULE
*                      OF THE JOB PACK-AREA.
*                  CODE OF THE INSTRUCTION
*                  CONTENTS OF THE PARTICIPATING FIELDS
*                  REGISTER CONTENTS
*              IN CASE OF AN ABEND S0C7 THE EXIT ROUTINE INITIATES A
*              RETRY AT THE ADDRESS OF THE NEXT INSTRUCTION TO BE
*              EXECUTED. IN ALL OTHER CASES THE EXIT ENDS WITH RETURN-
*              CODE 'CONTINUE ABEND PROCESSING'.
         EJECT
*              ENTRIES
*                                  UGOT01A             PROGRAM NAME
         SPACE 3
*              INPUT
*                                  PARAMETERLIST :
*                                      1.: MAXIMAL NUMBER OF S0C7
*                                          INTERRUPTS
*                                          (IF 0 THE ESTAE WILL BE
*                                          CANCELLED)
*                                          REQUIRED
*                                      2.: DDNAME OF PRINT DATASET
*                                          OPTIONAL
*                                      3.: RELATED INFORMATION
*                                          OPTIONAL
         SPACE 3
*              OUTPUT
*                                  PRINT-DATASET :
*                                      DDNAME :
*                                           ACCORDING TO PARAMETER 2 IF
*                                           PRESENT
*                                           ELSE SYSPRINT IF PRESENT
*                                           ELSE SYSUDUMP IF PRESENT
*                                           ELSE NONE (CONTINUE
*                                           ABEND PROCESSING)
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER USAGE                                               *
*                                                                     *
***********************************************************************
         SPACE 3
*        R0    WORK
*        R1    PARAMETER/WORK
*        R2    WORK
*        R3    WORK
*        R4    WORK
*        R5    LINK WRITE-ROUTINE (EASY TO BE CHANGED)
*        R6    INTERRUPT-ADDRESS (EASY TO BE CHANGED)
*        R7    EXTENTLIST
*        R8    MAJOR CDE
*        R9    CDE
*        RA    SDWA (SYSTEM DIAGNOSTIC WORKAREA)
*        RB    FREE
*        RC    RESERVED FOR LATER USE
*        RD    BASE
*        RE    LINK
*        RF    BRANCH/RETURN-CODE
         EJECT
***********************************************************************
*                                                                     *
*        EQUATES                                                      *
*                                                                     *
***********************************************************************
         SPACE 3
*        REGISTER DEFINITIONS
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         SPACE 3
         EJECT
***********************************************************************
*                                                                     *
*        INITIAL LINKAGE CONVENTIONS                                  *
*                                                                     *
***********************************************************************
         SPACE 3
&PROG    CSECT
         USING &PROG,RF            ESTABLISH TEMPORARY ADDRESSIBILITY
         SAVE  (14,12),,&PROG.*&SYSDATE.*&SYSTIME
         ST    RD,SA+4             SAVE ADDRESS OF CALLER'S SAVE-AREA
         LR    R2,RD               ADDRESS OF CALLER'S SAVE-AREA
         LA    RD,SA               OUR SAVE-AREA-ADDRESS
         ST    RD,8(R2)            CHAIN ADDRESS OF OUR S.A.TO CALLER'S
         B     A000                BRANCH AROUND S.A.TO BEGIN OF WORK
SA       DC    18F'0'              SAVE-AREA
         DROP  RF                  DROP TEMPORARY ADDRESSIBILITY
         USING SA,RD               ESTABLISH NEW ADDRESSIBILITY
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM INITIALIZATION                                       *
*                                                                     *
***********************************************************************
         SPACE 3
A000     EQU   *                   START-ROUTINE
         SPACE 3
*                                  PARAMETER-LIST
         SPACE 3
         MVI   DDNAM,X'40'         FOR REUSABILITY
         XC    ARINF,ARINF         FOR REUSABILITY
         L     R2,0(R1)            ADDRESS OF PARAMETER
         MVC   MNI,0(R2)           SAVE PARAMETER
         TM    0(R1),X'80'         Q. END OF PARAMETER-LIST
         BO    A010                A. YES
         L     R2,4(R1)            NEXT PARAMETER
         MVC   DDNAM,0(R2)         SAVE DDNAME
         TM    4(R1),X'80'         Q. END OF PARAMETER-LIST
         BO    A010                A. YES
         L     R2,8(R1)            NEXT PARAMETER
         ST    R2,ARINF            ADDRESS OF RELATED INFORMATION
A010     EQU   *
         TS    FE                  Q. FIRST CALL
         BM    A020                A. NO, ESTAE ALREADY ESTABLISHED
         LOAD  EP=UGOT01A          TO AVOID BEING DELETED
         ESTAE E000,CT,PURGE=NONE,ASYNCH=YES,TERM=NO
A020     EQU   *
         CLC   MNI,=F'0'           Q. ANY INTERRUPTS TO BE INTERCEPTED
         BNE   B000                A. YES
         MVI   FE,X'00'            MAKE PROGRAM REUSABLE
         ESTAE 0                   DELETE *** MOST RECENT ESTAE ××× ***
         DELETE EP=UGOT01A         MAKE PROGRAM DELETABLE
         EJECT
***********************************************************************
*                                                                     *
*        END-ROUTINE                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
B000     EQU   *                   END-ROUTINE
         SPACE 3
         SLR   RF,RF               RETURN-CODE 0
         EJECT
***********************************************************************
*                                                                     *
*        RETURN TO CALLER                                             *
*                                                                     *
***********************************************************************
         SPACE 3
C000     EQU   *                   RETURN-ROUTINE
         SPACE 3
         L     RD,4(RD)            HIGHER SAVE-AREA ADDRESS
         RETURN (14,12),T,RC=(15)
         DROP  RD
         EJECT
***********************************************************************
*                                                                     *
* E000   EXIT-ROUTINE                                                 *
*                                                                     *
*        ASYNCHRONOUS ESTAE-EXIT                                      *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        INITIAL LINKAGE CONVENTIONS                                  *
*                                                                     *
***********************************************************************
         SPACE 3
E000     DS    0D
         USING E000,RF
         C     R0,=F'12'           Q. SAVE-AREA EXISTING
         BNE   E010                A. YES
         SLR   RF,RF               RETURN-CODE CONTINUE ABEND PROCESS.
         BR    RE                  RETURN
E010     EQU   *
*        SAVE  (14,12),,&PROG.*&SYSDATE.*&SYSTIME
         B     E020                BRANCH AROUND ID
         DC    AL1(22)             LENGTH OF IDENTIFIER
         DC    CL8'UGOT01A*'       IDENTIFIER
         DC    CL8'&SYSDATE'       IDENTIFIER
         DC    CL6'*&SYSTIME'      IDENTIFIER
         DS    0H                  HALF-WORD-ALIGNMENT
E020     EQU   *
         STM   RE,RC,12(RD)        SAVE REGISTERS
         ST    RD,ESA+4            SAVE ADDRESS OF CALLER'S SAVE-AREA
         LR    R2,RD               ADDRESS OF CALLER'S SAVE-AREA
         LA    RD,ESA              OUR SAVE-AREA-ADDRESS
         ST    RD,8(R2)            CHAIN ADDRESS OF OUR S.A.TO CALLER'S
         B     F000                BRANCH AROUND S.A.TO BEGIN OF WORK
ESA      DC    18F'0'              SAVE-AREA
         DROP  RF                  DROP TEMPORARY ADDRESSIBILITY
         USING ESA,RD              ESTABLISH NEW ADDRESSIBILITY
         EJECT
***********************************************************************
*                                                                     *
*        EXIT INITIALIZATION                                       *
*                                                                     *
***********************************************************************
         SPACE 3
F000     EQU   *                   START-ROUTINE
         SPACE 3
         LR    RA,R1               ADDRESS OF SDWA
         USING SDWA,RA
         CLC   SDWACMPC,=XL3'0C7000' Q. ABEND S0C7
         BNZ   H000                A. NO
         L     R1,MNI              RESIDUAL NUMBER OF INTERRUPTS
         S     R1,=F'1'            -1
         BM    H000                NO RESIDUAL LEFT
         ST    R1,MNI              SAVE RESIDUAL NUMBER OF INTERRUPTS
         EJECT
***********************************************************************
*                                                                     *
*        PRINT-OUTPUT                                                 *
*                                                                     *
***********************************************************************
         SPACE 3
G000     EQU   *                   DETERMINE DD-CARD
         SPACE 3
         CLI   DDNAM,X'40'         Q. DDNAM FILLED
         BE    G010                A.NO
         MVC   SNAPDCB+40(8),DDNAM
         B     G050                BRANCH AROUND SEARCH
G010     EQU   *
         MVI   SNAPDCB+40,X'40'    INDICATE NO DDNAME
         L     R1,16               LOAD CVT-ADDRESS
         L     R1,0(R1)            LOAD TCB-POINTER
         L     R1,4(R1)            LOAD CURRENT-TCB-ADDRESS
         L     R1,12(R1)           LOAD TIOT-ADDRESS
         LA    R1,24(R1)           FIRST DD-ENTRY
         SLR   R0,R0               CLEAR REGISTER
G020     EQU   *
         CLC   4(8,R1),=CL8'SYSPRINT' Q. DD-CARD SYSPRINT
         BE    G040                A. YES
         CLC   4(8,R1),=CL8'SYSUDUMP' Q. DD-CARD SYSUDUMP
         BNE   G030                A. NO
         MVC   SNAPDCB+40,=CL8'SYSUDUMP'
G030     EQU   *
         IC    R0,0(R1)            LENGTH OF DD-ENTRY
         AR    R1,R0               ADDRESS OF NEXT ENTRY
         CLC   0(4,R1),=F'0'       Q. END OF TIOT
         BNE   G020                A. NO
         CLI   SNAPDCB+40,X'40'    Q. DDNAME FOUND
         BE    H000                A. NO, CONTINUE ABEND
         B     G050                OPEN
G040     EQU   *
         MVC   SNAPDCB+40(8),=CL8'SYSPRINT' CORRECT DDNAME
G050     EQU   *
         OPEN  (SNAPDCB,(OUTPUT))
         WRITE L1ECB,SF,SNAPDCB,LINE1
         CHECK L1ECB
         SPACE 3
G100     EQU   *                   INTERRUPT-OFFSETS
         SPACE 3
         L     R6,SDWAPMKA         LOWER PART OF PSW
         LA    R6,0(R6)            CLEAR HIGH-ORDER-BYTE
         SLR   R0,R0               CLEAR REGISTER
         IC    R0,SDWAPMKA         INSTRUCTION LENGTH CODE
         SRL   R0,6                SHIFT AWAY UNUSABLE REST
         SLL   R0,1                *2 (HALFWORDS)
         SR    R6,R0               ADDRESS OF INTERRUPT
         L     R1,16               LOAD CVT-ADDRESS
         L     R1,0(R1)            LOAD TCB-POINTER
         L     R1,4(R1)            LOAD CURRENT-TCB-ADDRESS
         L     R9,44(R1)           LOAD ADDRESS OF CDE
         USING CDENTRY,R9
G110     EQU   *
         LR    R8,R9               ADDRESS OF MAJOR CDE
         TM    CDATTR,CDMIN        Q. MINOR CDE
         BZ    G120                A. NO
         L     R8,CDXLMJP          ADDRESS OF MAJOR CDE
         DROP  R9
G120     EQU   *
         USING CDENTRY,R8
         L     R7,CDXLMJP          ADDRESS OF EXTENT LIST ENTRY
         USING XTLST,R7
         CLM   R6,7,XTLMSBAD      Q. INTERRUPT-ADDRESS
         BL    G190                WITHIN BOUNDS
         L     R1,XTLMSBAA         BEGIN OF MODULE
         L     R2,XTLMSBLA         LENGTH OF MODULE
         LA    R1,0(R1,R2)         END OF MODULE
         CR    R6,R1               Q. INTERRUPT IN MODULE
         BH    G190                A. NO
         CLC   XTLMSBAD,CDENTPT+1  Q. BEGIN = EP OF MAJOR
         BE    G140                A. YES
         L     R1,XTLMSBAA         LOAD BEGIN OF STORAGE
         LA    R1,0(R1)            CLEAR HIGH-ORDER BYTE
         LR    R0,R6               FOR COMPUTING
         SR    R0,R1               OFFSET FROM 0
         ST    R0,DW               SAVE FOR UNPACK
         UNPK  ZF(9),DW(5)         1 HALFBYTE TO 1 BYTE
         TR    ZF(8),HTAB          TRANSLATE FOR HEXADECIMAL PRINT
         MVC   OFFSET,ZF           PRINTAREA
         MVC   ENTRY,=CL8'       0' NO ENTRYPOINT ASSOCIATED
         MVI   IDENT,X'40'
         MVC   IDENT+1(L'IDENT-1),IDENT CLEAR AREA
         SLR   R2,R2               CLEAR REGISTER
         IC    R2,4(R1)            LENGTH OF IDENTIFIER
         CH    R2,=H'44'           Q. LENGTH OK
         BNH   G130                A. YES
         LA    R2,44               CORRECT LENGTH
G130     EQU   *
         BCTR  R2,0                MACHINE-LENGTH
         EX    R2,MVCI             MOVE IDENTIFIER
         WRITE L2ECBA,SF,SNAPDCB,LINE2
         CHECK L2ECBA
G140     EQU   *
         DROP  R8
         USING CDENTRY,R9          ACTUAL CDE-ENTRY
         L     R1,CDENTPT          LOAD ENTRY-POINT
         LA    R1,0(R1)            CLEAR HIGH-ORDER BYTE
         LR    R0,R6               FOR COMPUTING
         SR    R0,R1               OFFSET FROM 0
         ST    R0,DW               SAVE FOR UNPACK
         UNPK  ZF(9),DW(5)         1 HALFBYTE TO 1 BYTE
         TR    ZF(8),HTAB          TRANSLATE FOR HEXADECIMAL PRINT
         MVC   OFFSET,ZF           PRINTAREA
         MVC   ENTRY,CDNAME        ENTRYPOINT ASSOCIATED
         MVI   IDENT,X'40'
         MVC   IDENT+1(L'IDENT-1),IDENT CLEAR AREA
         SLR   R2,R2               CLEAR REGISTER
         IC    R2,4(R1)            LENGTH OF IDENTIFIER
         CH    R2,=H'44'           Q. LENGTH OK
         BNH   G150                A. YES
         LA    R2,44               CORRECT LENGTH
G150     EQU   *
         BCTR  R2,0                MACHINE-LENGTH
         EX    R2,MVCI             MOVE IDENTIFIER
         WRITE L2ECBB,SF,SNAPDCB,LINE2
         CHECK L2ECBB
G190     EQU   *
         L     R9,CDCHAIN          NEXT CDE-ENTRY
         LTR   R9,R9               Q. EXISTING
         BNZ   G110                A. YES
         DROP  R7
         DROP  R9
         SPACE 3
G200     EQU   *                   PSW
         SPACE 3
         UNPK  ZF(9),SDWACTL1(5)   UNPACK PSW FIRST WORD
         TR    ZF(8),HTAB          TRANSLATE FOR HEXADECIMAL PRINT
         MVC   PSW1,ZF             PRINTAREA
         UNPK  ZF(9),SDWACTL1+4(5) UNPACK PSW SECOND WORD
         TR    ZF(8),HTAB          TRANSLATE FOR HEXADECIMAL PRINT
         MVC   PSW2,ZF             PRINTAREA
         UNPK  ZF(9),SDWAEC1(5)    UNPACK PSW FIRST WORD
         TR    ZF(8),HTAB          TRANSLATE FOR HEXADECIMAL PRINT
         MVC   PSW3,ZF             PRINTAREA
         UNPK  ZF(9),SDWAEC1+4(5)  UNPACK PSW SECOND WORD
         TR    ZF(8),HTAB          TRANSLATE FOR HEXADECIMAL PRINT
         MVC   PSW4,ZF             PRINTAREA
         WRITE L3ECB,SF,SNAPDCB,LINE3
         CHECK L3ECB
         SPACE 3
G300     EQU   *                   INSTRUCTION CODE
         SPACE 3
         SLR   R1,R1               CLEAR REGISTER
         IC    R1,0(R6)            INSTRUCTION CODE
         SLL   R1,2                *4 (LENGTH OF TABLE ELEMENT)
         LA    R1,INSTRT(R1)       ACTUAL ELEMENT
         MVC   INSTR,0(R1)         INSTRUCTION MNEMONIC
         WRITE L4ECB,SF,SNAPDCB,LINE4
         CHECK L4ECB
         SPACE 3
G400     EQU   *                   CONTENTS FIRST OPERAND
         SPACE 3
         SLR   R1,R1               CLEAR REGISTER
         IC    R1,1(R6)            INSTRUCTION LENGTH
         SRL   R1,4                ONLY FIRST OPERAND
         SLR   R2,R2               CLEAR REGISTER
         IC    R2,2(R6)            ADDRESS-REGISTER
         SRL   R2,4                ONLY A HALFBYTE
         SLL   R2,2                * 4 (OFFSET IN REGISTER-TABLE)
         L     R2,SDWAGRSV(R2)     USER-REGISTER-CONTENTS
         LH    R3,2(R6)            OFFSET
         N     R3,=XL4'FFF'        CLEAR UNUSED REST
         LA    R2,0(R2,R3)         OPERAND ADDRESS
         EX    R1,MVCO             FOR SECURITY, TO AVOID POSSIBLE 0C4
         UNPK  ZF(15),PF(8)
         UNPK  ZF+14(15),PF+7(8)
         UNPK  ZF+28(5),PF+14(3)
         SLL   R1,1                * 2, HALFBYTES
         LA    R1,1(R1)            + 1 (START WITH 0)
         TR    ZF,HTAB             TRANSLATE TO PRINTABLE
         XC    OPER1,OPER1         CLEAR FIELD
         EX    R1,MVCO1            MOVE TO PRINT-AREA
         WRITE L5ECB,SF,SNAPDCB,LINE5
         CHECK L5ECB
         SPACE 3
G500     EQU   *                   CONTENTS SECOND OPERAND
         SPACE 3
         IC    R1,1(R6)            INSTRUCTION LENGTH
         N     R1,=XL4'0F'         ONLY SECOND OPERAND
         SLR   R2,R2               CLEAR REGISTER
         IC    R2,4(R6)            ADDRESS-REGISTER
         SRL   R2,4                ONLY A HALFBYTE
         SLL   R2,2                * 4 (OFFSET IN REGISTER-TABLE)
         L     R2,SDWAGRSV(R2)     USER-REGISTER-CONTENTS
         LH    R3,4(R6)            OFFSET
         N     R3,=XL4'FFF'        CLEAR UNUSED REST
         LA    R2,0(R2,R3)         OPERAND ADDRESS
         EX    R1,MVCO             FOR SECURITY, TO AVOID POSSIBLE 0C4
         UNPK  ZF(15),PF(8)
         UNPK  ZF+14(15),PF+7(8)
         UNPK  ZF+28(5),PF+14(3)
         SLL   R1,1                * 2, HALFBYTES
         LA    R1,1(R1)            + 1 (START WITH 0)
         TR    ZF,HTAB             TRANSLATE TO PRINTABLE
         XC    OPER2,OPER2         CLEAR FIELD
         EX    R1,MVCO2            MOVE TO PRINT-AREA
         WRITE L6ECB,SF,SNAPDCB,LINE6
         CHECK L6ECB
         SPACE 3
G600     EQU   *                   REGISTERS AT TIME OF INTERRUPT
         SPACE 3
         WRITE L7ECB,SF,SNAPDCB,LINE7
         CHECK L7ECB
         LA    R4,SDWAGRSV         REGISTER AT TIME OF ERROR
         LA    R2,LINE8+13         START MIT REGISTER 0
         LA    R3,8                LOOP-COUNT
G610     EQU   *
         MVC   DW(4),0(R4)         REGISTER CONTENTS AT TIME OF ERROR
         UNPK  ZF(9),DW(5)         HALFBYTES
         TR    ZF(8),HTAB          TRANSFER TO PRINTABLE
         MVC   0(8,R2),ZF          MOVE TO PRINT-AREA
         LA    R2,15(R2)           NEXT AREA
         LA    R4,4(R4)            NEXT REGISTER
         BCT   R3,G610             8 REGISTER PER LINE
         WRITE L8ECB,SF,SNAPDCB,LINE8
         CHECK L8ECB
         LA    R2,LINE9+13         START MIT REGISTER 8
         LA    R3,8                LOOP-COUNT
G620     EQU   *
         MVC   DW(4),0(R4)         REGISTER CONTENTS AT TIME OF ERROR
         UNPK  ZF(9),DW(5)         HALFBYTES
         TR    ZF(8),HTAB          TRANSFER TO PRINTABLE
         MVC   0(8,R2),ZF          MOVE TO PRINT-AREA
         LA    R2,15(R2)           NEXT AREA
         LA    R4,4(R4)            NEXT REGISTER
         BCT   R3,G620             8 REGISTER PER LINE
         WRITE L9ECB,SF,SNAPDCB,LINE9
         CHECK L9ECB
         SPACE 3
G700     EQU   *                   RELATED INFORMATION
         SPACE 3
         CLC   ARINF,=F'0'         Q. RELATED INFORMATION
         BE    G800                A. NO
         L     R1,ARINF            ADDRESS OF RELATED INFORMATION
         MVC   PF(16),0(R1)        FOR SECURITY, TO AVOID POSSIBLE 0C4
         UNPK  ZF(15),PF(8)
         UNPK  ZF+14(15),PF+7(8)
         UNPK  ZF+28(5),PF+14(3)
         TR    ZF,HTAB             TRANSLATE TO PRINTABLE
         MVC   RINF,ZF             MOVE TO PRINT-AREA
         WRITE L10ECB,SF,SNAPDCB,LINE10
         CHECK L10ECB
         SPACE 3
G800     EQU   *                   SAVE AREA TRACE
         SPACE 3
         SNAP  DCB=SNAPDCB,PDATA=(SA)
         SPACE 3
G900     EQU   *                   END EXIT
         SPACE 3
         CLOSE (SNAPDCB)
         L     R2,SDWAPMKP         RETRY-ADDRESS
         MVC   SDWASRSV,SDWAGRSV   RETRY-REGISTER-CONTENTS
         L     RD,4(RD)            CALLER'S SAVE-AREA
         SETRP WKAREA=(RA),REGS=(14,12),DUMP=NO,RC=4,                  *
               RETADDR=(R2),RETREGS=YES,FRESDWA=YES
         EJECT
***********************************************************************
*                                                                     *
*        NO RETRY PROVIDED                                            *
*                                                                     *
***********************************************************************
         SPACE 3
H000     EQU   *                   RETURN-ROUTINE
         SPACE 3
         L     RD,4(RD)            CALLER'S SAVE-AREA
         SETRP WKAREA=(RA),REGS=(14,12),DUMP=IGNORE,RC=0
         EJECT
***********************************************************************
*                                                                     *
*        INSTRUCTIONS TO BE EXECUTED                                  *
*                                                                     *
***********************************************************************
         SPACE 3
MVCI     MVC   IDENT(0),5(R1)      IDENTIFIER
MVCO     MVC   PF(0),0(R2)         OPERAND TO TEMPORARY FIELD
MVCO1    MVC   OPER1(0),ZF         OPERAND 1
MVCO2    MVC   OPER2(0),ZF         OPERAND 2
         EJECT
***********************************************************************
*                                                                     *
*        DEFINITIONS                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
*        LITERAL POOL
         SPACE 3
         LTORG
         SPACE 3
*        DCB'S
         SPACE 3
         DS    0D                  DOUBLE-WORD-BOUNDARY ALIGNMENT
SNAPDCB  DCB   DDNAME=SYSUDUMP,DSORG=PS,RECFM=VBA,MACRF=(W),           *
               BLKSIZE=1632,LRECL=125
         SPACE 3
*        TABLES
         SPACE 3
         DS    0D
         ORG   *-C'0'
HTAB     DS    XL256               HEXADECIMAL TRANSLATE-TABLE
         ORG   HTAB+C'0'
         DC    C'0123456789ABCDEF'
         ORG
         SPACE 3
INSTRT   DC    CL4'    '           TABLE OF INSTRUCTIONS
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'SPM '
         DC    CL4'BALR'
         DC    CL4'BCTR'
         DC    CL4'BCR '
         DC    CL4'SSK '
         DC    CL4'ISK '
         DC    CL4'SVC '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'MVCL'
         DC    CL4'CLCL'
         DC    CL4'LPR '
         DC    CL4'LNR '
         DC    CL4'LTR '
         DC    CL4'LCR '
         DC    CL4'NR  '
         DC    CL4'CLR '
         DC    CL4'OR  '
         DC    CL4'XR  '
         DC    CL4'LR  '
         DC    CL4'CR  '
         DC    CL4'AR  '
         DC    CL4'SR  '
         DC    CL4'MR  '
         DC    CL4'DR  '
         DC    CL4'ALR '
         DC    CL4'SLR '
         DC    CL4'LPDR'
         DC    CL4'LNDR'
         DC    CL4'LTDR'
         DC    CL4'LCDR'
         DC    CL4'HDR '
         DC    CL4'LRDR'
         DC    CL4'MXR '
         DC    CL4'MXDR'
         DC    CL4'LDR '
         DC    CL4'CDR '
         DC    CL4'ADR '
         DC    CL4'SDR '
         DC    CL4'MDR '
         DC    CL4'DDR '
         DC    CL4'AWR '
         DC    CL4'SWR '
         DC    CL4'LPER'
         DC    CL4'LNER'
         DC    CL4'LTER'
         DC    CL4'LCER'
         DC    CL4'HER '
         DC    CL4'LRER'
         DC    CL4'AXR '
         DC    CL4'SXR '
         DC    CL4'LER '
         DC    CL4'CER '
         DC    CL4'AER '
         DC    CL4'SER '
         DC    CL4'MER '
         DC    CL4'DER '
         DC    CL4'AUR '
         DC    CL4'SUR '
         DC    CL4'STH '
         DC    CL4'LA  '
         DC    CL4'STC '
         DC    CL4'IC  '
         DC    CL4'EX  '
         DC    CL4'BAL '
         DC    CL4'BCT '
         DC    CL4'BC  '
         DC    CL4'LH  '
         DC    CL4'CH  '
         DC    CL4'AH  '
         DC    CL4'SH  '
         DC    CL4'MH  '
         DC    CL4'    '
         DC    CL4'CVD '
         DC    CL4'CVB '
         DC    CL4'ST  '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'N   '
         DC    CL4'CL  '
         DC    CL4'O   '
         DC    CL4'X   '
         DC    CL4'L   '
         DC    CL4'C   '
         DC    CL4'A   '
         DC    CL4'S   '
         DC    CL4'M   '
         DC    CL4'D   '
         DC    CL4'AL  '
         DC    CL4'SL  '
         DC    CL4'STD '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'MXD '
         DC    CL4'LD  '
         DC    CL4'CD  '
         DC    CL4'AD  '
         DC    CL4'SD  '
         DC    CL4'MD  '
         DC    CL4'DD  '
         DC    CL4'AW  '
         DC    CL4'SW  '
         DC    CL4'STE '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'LE  '
         DC    CL4'CE  '
         DC    CL4'AE  '
         DC    CL4'SE  '
         DC    CL4'ME  '
         DC    CL4'DE  '
         DC    CL4'AU  '
         DC    CL4'SU  '
         DC    CL4'SSM '
         DC    CL4'    '
         DC    CL4'LPSW'
         DC    CL4'DIAG'
         DC    CL4'WRD '
         DC    CL4'RDD '
         DC    CL4'BXH '
         DC    CL4'BXLE'
         DC    CL4'SRL '
         DC    CL4'SLL '
         DC    CL4'SRA '
         DC    CL4'SLA '
         DC    CL4'SRDL'
         DC    CL4'SLDL'
         DC    CL4'SRDA'
         DC    CL4'SLDA'
         DC    CL4'STM '
         DC    CL4'TM  '
         DC    CL4'MVI '
         DC    CL4'TS  '
         DC    CL4'NI  '
         DC    CL4'CLI '
         DC    CL4'OI  '
         DC    CL4'XI  '
         DC    CL4'LM  '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'SIO '
         DC    CL4'TIO '
         DC    CL4'HIO '
         DC    CL4'TCH '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'STNS'
         DC    CL4'STOS'
         DC    CL4'SIGP'
         DC    CL4'MC  '
         DC    CL4'    '
         DC    CL4'LRA '
         DC    CL4'SEEB'
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'STCT'
         DC    CL4'LCTL'
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'CS  '
         DC    CL4'CDS '
         DC    CL4'    '
         DC    CL4'CLM '
         DC    CL4'STCM'
         DC    CL4'ICM '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'MVN '
         DC    CL4'MVC '
         DC    CL4'MVZ '
         DC    CL4'NC  '
         DC    CL4'CLC '
         DC    CL4'OC  '
         DC    CL4'XC  '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'TR  '
         DC    CL4'TRT '
         DC    CL4'ED  '
         DC    CL4'EDMK'
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'SRP '
         DC    CL4'MVO '
         DC    CL4'PACK'
         DC    CL4'UNPK'
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'ZAP '
         DC    CL4'CP  '
         DC    CL4'AP  '
         DC    CL4'SP  '
         DC    CL4'MP  '
         DC    CL4'DP  '
         DC    CL4'    '
         DC    CL4'    '
         SPACE 3
*        PRINT-LINES
         SPACE 3
LINE1    DC    H'48',H'0'
         DC    H'48',H'0'
         DC    CL44'1PROGRAM INTERRUPT SCC 0C7 (DATA EXCEPTION)'
LINE2    DC    H'111',H'0'
         DC    H'111',H'0'
         DC    CL19'0        AT OFFSET '
OFFSET   DC    CL8'XXXXXXXX'
         DC    CL12' FROM ENTRY '
ENTRY    DC    CL8' '
         DC    CL16' IDENTIFICATION '
IDENT    DC    CL44' '
LINE3    DC    H'097',H'0'
         DC    H'097',H'0'
         DC    CL28'0PSW AT TIME OF INTERRUPT : '
PSW1     DC    CL8' '
         DC    CL1' '
PSW2     DC    CL8' '
         DC    CL15' (BASIC MODE)  '
PSW3     DC    CL8' '
         DC    CL1' '
PSW4     DC    CL8' '
         DC    CL16' (EXTENDED MODE)'
LINE4    DC    H'028',H'0'
         DC    H'028',H'0'
         DC    CL20'0INSTRUCTION CODE : '
INSTR    DC    CL4' '
LINE5    DC    H'074',H'0'
         DC    H'074',H'0'
         DC    CL18'0FIELD CONTENTS : '
         DC    CL24'FIRST OPERAND (TO) :    '
OPER1    DC    CL32' '
LINE6    DC    H'074',H'0'
         DC    H'074',H'0'
         DC    CL18'0                 '
         DC    CL24'SECOND OPERAND (FROM) : '
OPER2    DC    CL32' '
LINE7    DC    H'037',H'0'
         DC    H'037',H'0'
         DC    CL33'0REGISTERS AT TIME OF INTERRUPT :'
LINE8    DC    H'122',H'0'
         DC    H'122',H'0'
         DC    CL43'0R0  XXXXXXXX   R1  XXXXXXXX   R2  XXXXXXXX'
         DC    CL45'   R3  XXXXXXXX   R4  XXXXXXXX   R5  XXXXXXXX'
         DC    CL30'   R6  XXXXXXXX   R7  XXXXXXXX'
LINE9    DC    H'122',H'0'
         DC    H'122',H'0'
         DC    CL43'0R8  XXXXXXXX   R9  XXXXXXXX   R10 XXXXXXXX'
         DC    CL45'   R11 XXXXXXXX   R12 XXXXXXXX   R13 XXXXXXXX'
         DC    CL30'   R14 XXXXXXXX   R15 XXXXXXXX'
LINE10   DC    H'059',H'0'
         DC    H'059',H'0'
         DC    CL23'0RELATED INFORMATION : '
RINF     DC    CL32' '
         SPACE 3
*        DOUBLE-WORD-FIELDS
         SPACE 3
DW       DC    D'0'                DOUBLE-WORD-WORK-FIELD
         SPACE 3
*        WORD-FIELDS
         SPACE 3
MNI      DC    F'55'               MAXIMUM NUMBER OF INTERRUPTS
ARINF    DC    F'0'                ADDRESS OF RELATED INFORMATION
         SPACE 3
*        CHARACTER-FIELDS
         SPACE 3
DDNAM    DC    CL8' '              DDNAME TO SNAP
ZF       DC    CL33' '             TEMPORARY FOR UNPK
PF       DC    PL16'0'             TEMPORARY FOR PACKED FIELDS
         SPACE 3
*        FLAGS
         SPACE 3
         DC    X'0'
FE       DC    X'0'                FIRST CALL
         EJECT
***********************************************************************
*                                                                     *
*        DSECTS                                                       *
*                                                                     *
***********************************************************************
         SPACE 3
         IHASDWA
         SPACE 3
         IHACDE
         SPACE 3
         IHAXTLST
         END
***********************************************************************
*                                                                     *
*        LINKAGE-EDITOR ASSIGNMENTS                                   *
*                                                                     *
***********************************************************************
         SPACE 3
         PUNCH '  ENTRY UGOT01A'
         PUNCH '  NAME  UGOT01A(R)'
         END
