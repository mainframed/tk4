S9210441 CSECT
         SAVE  (14,12),,STE
         LR    2,15              PROLOG
         USING S9210441,2        BASE REG. 2
         ST    13,SAVE+4
         LR    12,13
         LA    13,SAVE
         ST    13,8(12)
         L     4,0(1)        PARM ADDRESSING
         LA    5,2(4)
         LH    6,0(4)
         BCTR  6,0           R6 -1 FOR FIRST CHAR IN PARM
         BCTR  6,0           R6 -1 FOR MASCHINELENGTH
         EX    6,MVC2        DSN NACH CAMLST 2
         EX    6,MVC2A        DSN NACH CAMLST 2A
         LOCATE DSCB2A        LOCATE CATALOG.VIPOCAT
         MVC   VOL1,WORK2A+6  VOLSER AUS LOCATA VIPOCAT
         MVC   VOL3,WORK2A+6     ... " ...
         LTR   15,15
         BZ    OBTAIN1
         MVI   VOL2+5,C'3'
         LOCATE DSCB2        LOCATE USERCATALOG VS0003
         MVC   VOL1,WORK2+6  VOLSER AUS LOCATE FOR OBTAIN FORMAT 1
         MVC   VOL3,WORK2+6  VOLSER AUS LOCATE FOR OBTAIN FORMAT3
         LTR   15,15
         BZ    OBTAIN1
         MVC   KON3(L'K3),K3 ERRORMESSAGE 'DATASET NOT CATALOGED'
         B     ERROR1        DRUCKEN ERRORMESS.
OBTAIN1  EQU   *
         EX    6,MVC3        DSN FOR FORMAT 1 OBTAIN
         OBTAIN DSCB1        OBTAIN FORMAT 1
         MVC    WORKZ,WORK11 FOR CAMLST SEARCH - 44 BYTES
         LTR    15,15
         BZ    DSORGCHK
         MVC   KON3(L'K6),K6 ERRORMESSAGE 'DATASET NOT FOUND'
         B     ERROR1        DRUCKEN ERRORMESS.
DSORGCHK EQU   *
         CLI   WORK1+82,X'02' CHECK PARTITIONED ORGANISATION
         BE    EMPTYCHK
         MVC   KON3(L'K5),K5  ERRORMESSAGE 'NO PARTITIONED ORG.'
         B     ERROR1         DRUCKEN ERRORMESS.
EMPTYCHK EQU   *
         CLI   WORK2+5,X'08'  CHECK 2314 ?
         BE    EM1
         SP    NRTRK,=P'1'    3330 - 19 TRKS
         MVC   KON4+14(4),=C'3330'
EM1      EQU   *
         LA    11,WORK1+107   EXTENTS IM FORMAT 1
         LA    8,3            LOOP COUNT
EM2      CLC   0(10,11),NULL  LAST EXTENT ?
         BE     GOON
         BAL   10,COMPUTE
         LA    11,10(11)      NEXT EXTENT
         BCT   8,EM2
         CLC   WORK1+135(5),NULL FORMAT 3       AVAILABLE ?
         BE    GOON
         MVC   CCHHR3,WORK1+135 CCHHR ADDRESS FOR OBTAIN FORMAT 3
         OBTAIN DSCB3         OBTAIN FORMAT 3
         LA    8,4            LOOP COUNT FOR NEXT FOUR EXTENTS
         LA    11,WORK3+6     NEXT FOUR EXTENTS IN FORMAT 3
EM3      EQU   *
         CLC   0(10,11),NULL  LAST EXTENT ?
         BE    GOON
         BAL   10,COMPUTE
         LA    11,10(11)      NEXT EXTENT
         BCT   8,EM3
         LA    8,9            LOOP COUNT
         LA    11,WORK3+47       LAST NINE EXTENTS IN FORMAT 3
EM4      CLC   0(10,11),NULL  LAST EXTENT ?
         BE    GOON
         BAL   10,COMPUTE
         LA    11,10(11)      NEXT EXTENT
         BCT   8,EM4
GOON     MVC   KON4(6),SCHAB  EDIT ALLOCATED TRACKS
         ED    KON4(6),TRK
         ZAP   ZTRK,TRK       ZWISCHENSPEICHERN ALLOC. TRACKS FOR %
         LH    8,WORK1+98     LAST BLKPOINTER
         CVD   8,DW3
         AP    DW3,=P'1'      FOR RELAT. TRACK ZERO
         SP    TRK,DW3+5(3)   COMPUTE EMPTY TRACKS
         MVC   KON4+33(6),SCHAB EDIT EMPTY TRACKS
         ED    KON4+33(6),TRK
         ZAP   DW3,TRK   PERCENT COMPUTING
         MP    DW3,=P'100'
         DP    DW3,ZTRK+2(3)   COMPUTE PERCENT
         ZAP   DW2,=P'100'   COMPUTE PERCENT USED
         SP    DW2,DW3+3(2)
         MVC   KON5+2(4),SCHAB EDIT PERCENT
         ED    KON5+2(4),DW2+6
         CP    DW2+6(2),=P'90' LIMIT FOR COMPRESS
         BL    OPEN1
         MVC   KON5+13(L'K8),K8 DRUCKEN 'COMPRESS RECOMMANDED'
OPEN1    EQU   *
         OPEN  (PLA,INPUT)     OPEN PLA FOR SEQ. READ THE DIRECTORY
GET1     GET   PLA,EBER     GET DIRECTORY BLOCK
         CLI   SCHF,C'1'
         BE    A0
         CLC   EBER+2(8),NULL
         BE    A0
         CLC   EBER+2(8),FFFF    CHECK UNUSED DIRECTORY BLOCKS
         BNE   A1
         MVI   SCHF,C'1'
A0       EQU   *
         AP    ZUNUSED,=P'1'
A1       AP    ZUSED,=P'1'
         B     GET1
ENDE     EQU   *
         EX    6,MVC1
         LR    7,6            LOOP COUNT
         LA    7,1(7)
         LA    8,KON1
A2       OI    0(8),X'40'     CHANGE UPPER CASE LETTERS
         LA    8,1(8)
         BCT   7,A2
         MVC   KON2(6),SCHAB     EDIT ALLOCATED DIRECTORY BLOCKS
         ED    KON2(6),ZUSED
         MVC   K2(6),SCHAB     EDIT EMPTY DIRECTORY BLOCKS
         ED    K2(6),ZUNUSED
         CLI    0(5),C'P'
         BE    PRINT
         WTO   MF=(E,W1)        WTO DSN
         WTO   MF=(E,W2)        WTO DIRECTORY USAGE
         WTO   MF=(E,W4)        WTO SPACE USAGE
         WTO   MF=(E,W5)        WTO PERCENT SPACE USED
         B     ENDE1
COMPUTE  EQU   *              ROUTINE FOR COMPUTING NR OF TRKS OF E.
         MVC   HW1,0(11)      CC - COMPUTE
         SR    9,9
         LH    9,HW1
         CVD   9,DW1
         MP    DW1,NRTRK      MP CYL./'20' OR '19'
         MVC   HW1,2(11)      HH - COMPUTE
         LH    9,HW1
         CVD   9,DW2
         AP    DW1,DW2        COMPUTE  TRK - NR. OF START
         MVC   HW1,4(11)      CC - COMPUTE
         LH    9,HW1
         CVD   9,DW2
         MP    DW2,NRTRK      MP CYL./'20' OR '19'
         MVC   HW1,6(11)      HH - COMPUTE
         LH    9,HW1
         CVD   9,DW3
         AP    DW2,DW3        COMPUTE  TRK - NR. OF END
         SP    DW2,DW1        COMPUTE NUMBER OF TRACKS
         AP    DW2,=P'1'      FOR RELAT. TRACK ZERO
         AP    TRK,DW2+5(3)   AKKUM. NUMBER OF TRACKS
         BR    10             RETURN
ERROR1   EQU   *
         CLI   0(5),C'P'      CHECK DCB DRU OPEN OR WTO PRINT
         BE    PRINT0
         WTO   MF=(E,W3)      WTO ERROR MESSAGE
         B     E1
PRINT0   EQU   *
         OPEN  (DRU,OUTPUT)   OPEN DRU DCB
         PUT   DRU,KON3       PRINT ERROR MESS.
         CLOSE (DRU)
         B     E1             BRANCH TO END
PRINT    OPEN  (DRU,OUTPUT)        OPEN DRU DCB
         PUT   DRU,KON1     PRINT DSN
         PUT   DRU,KON2     PRINT DIRECTORY USAGE
         PUT   DRU,KON4     PRINT SPACE USAGE
         PUT   DRU,KON5     PRINT PERCENT SPACE USED
         CLOSE (DRU)
ENDE1    CLOSE (PLA)
E1       L     13,4(13)
         RETURN (14,12),RC=0
* *************** D E F I N I T I O N E N ***********
EBER    DS     CL256          EBER FOR DIRECTORY BLOCKS
SAVE     DC    18F'0'
ZUSED    DC    PL3'0'           RF ALLOCATED DIRECTORY BLOCKS
ZUNUSED  DC    PL3'0'           RF EMPTY DIRECTORY BLOCKS
NULL     DC    XL8'0'           BINARY ZERO
         DC    XL2'0'
FFFF     DC    8X'FF'
SCHF     DC     C'0'
         DS    0F
W1       DC    AL2(50)           WTO LIST FOR DSN
         DC    X'0000'
KON1     DC    CL46' '
         DC    X'0200'
         DC    X'4000'
         DS    0F
W2       DC    AL2(50)           WTO LIST FOR DIRECTORY USAGE
         DC    X'0000'
KON2     DC    C' XXXXX DIRECTORYBLOCKS ALLOCATED,'
K2       DC    C' XXXXX UNUSED'
         DC     X'0200'
         DC    X'4000'
         DS    0F
W3       DC    AL2(50)           WTO LIST FOR ERROR MESSAGE
         DC    X'0000'
KON3     DC    CL46' '
         DC    X'0200'
         DC    X'4000'
         DS    0F
W4       DC    AL2(50)           WTO LIST FOR SPACE USAGE
         DC    X'0000'
KON4     DC    CL46' XXXXX TRACKS 2314     ALLOCATED, XXXXX EMPTY'
         DC    X'0200'
         DC    X'4000'
         DS    0F
W5       DC    AL2(50)           WTO LIST FOR PERCENT SPACE USED
         DC    X'0000'
KON5     DC    CL46'   XXX % USED'
         DC    X'0200'
         DC    X'4000'
MVC1     MVC   KON1(0),1(5)       FOR EXECUTE WTO + PRINT DSN
SCHAB    DC     X'402020202020'
PLA       DCB   DSORG=PS,DDNAME=PLA,MACRF=GM,EODAD=ENDE,LRECL=256,     *
               BLKSIZE=256,RECFM=F
DRU       DCB   DSORG=PS,DDNAME=DRU,MACRF=PM,LRECL=46,RECFM=F,         *
               BLKSIZE=46
DSCB1    CAMLST SEARCH,DSN1,VOL1,WORK11   FOR OBTAIN FORMAT 1
DSN1     DC    CL44' '
VOL1     DC    CL6' '
WORK11   DS    0D
         DS    148C
DSCB2    CAMLST NAME,DSN2,VOL2,WORK2      FOR LOCATE CVOL VS0003
DSN2     DC    CL44' '
VOL2     DC    CL6'VS0003'                      CVOL VS0003
WORK2    DS    0D
         DS    265C
DSCB2A    CAMLST NAME,DSN2A,,WORK2A      FOR LOCATE CATALOG.VIPOCAT
DSN2A     DC    CL44' '
WORK2A    DS    0D
         DS    265C
DSCB3    CAMLST SEEK,CCHHR3,VOL3,WORK3   FOR OBTAIN FORMAT 3
CCHHR3   DC    XL5'00'
VOL3     DC    CL6' '
WORK3    DS    0D
         DS    148C
ZTRK     DC    PL5'0'       FOR COMPUTE PERCENT
TRK      DC    PL3'0'       AKK. ALLOCATED TRACKS
DW1      DC    D'0'         FOR CONVERT
DW2      DC    D'0'         AND
DW3      DC    D'0'         COMPUTE
HW1       DC   H'0'
NRTRK    DC    PL3'20'      DIFFERENCE 2314 -3330
K5       DC    C'NO PARTITIONED ORGANISATION'  ERROR
K8       DC    C'     RECOMMANDED TO COMPRESS' MESSAGE
K3       DC    C'DATASET NOT CATALOGED'        AND
K6       DC    C'DATASET NOT FOUND'           ADDIT. OUTPUT
MVC2     MVC   DSN2(0),1(5)   FOR EXECUTE DSN TO CAMLST 2
MVC2A    MVC   DSN2A(0),1(5)  FOR EXECUTE DSN TO CAMLST 2A
MVC3     MVC   DSN1(0),1(5)   FOR EXECUTE DSN TO CAMLST 1
WORK1    DS    0CL192         ZWISCHENSPEICHER FOR CAMLST SEARCH
         DC    CL44' '
WORKZ    DS    CL148
          END   S9210441
