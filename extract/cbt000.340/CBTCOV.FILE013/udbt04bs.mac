         PRINT NOGEN
GDG      CSECT
         REGS
         STM   R14,R12,12(R13)
         LR    R3,R15              BASISREGISTER LADEN
         USING GDG,R3
         B     *+28
         DC    CL8'GDG     '       IDENTIFIER
         DC    CL8'&SYSTIME'
         DC    CL8'&SYSDATE'
         LR    R12,R13
         LA    R13,SAVEAREA        LINKAGE
         ST    R13,8(R12)          CONVENTIONS
         ST    R12,SAVEAREA+4      (NON-REENTRANT)
         L     R2,0(0,R1)               ADR. DER PARAMETERLISTE IN R2
         LH    R7,0(0,R2)               LAENGE DES PARAMETERS NACH R7
         LA    R2,2(R2)
*
         RDJFCB GDGDCB
         LTR   R15,R15
         BZ    MOVEDSN
         WTO   'GDG DD-STATEMENT MISSING',ROUTCDE=11
         B     RETURN8
MOVEDSN  MVC   GEN(44),JFCBAREA
*
         LR    R4,R2
LOOP0    CLI   0(R4),C','
         BE    KOMMA
         LA    R4,1(R4)
         BCT   R7,LOOP0
         B     PARMERR
KOMMA    LR    R5,R4
         SR    R5,R2
         LA    R5,15(R5)
         EX    R5,PACK                  LAENGE-1 + 16 WEGEN LAENGE DES
         CVB   R5,DW                    ERSTEN OPERANDEN
         ST    R5,DIVISOR
         LA    R2,1(R4)
         LA    R7,14(R7)
         EX    R7,PACK                  LAENGE-1 + 16 WEGEN LAENGE DES
         CVB   R7,DW                    ERSTEN OPERANDEN
         ST    R7,REST
         MVC   ALTGEN,GEN
         LA    R7,1(R7)                 ECHTE NAMENSLAENGE NACH R7
*
*        AUFSUCHEN DES CVOLS, IN DEM DIE GDG KATALOGISIERT IST
*
         LA    R4,GEN
         LA    R5,44
LOOP1    CLI   0(R4),C'.'               ENDE DES HIGH-LEVEL-INDEX
         BE    BLANK                    SUCHEN
         LA    R4,1(R4)
         BCT   R5,LOOP1
BLANK    MVI   0(R4),C' '               UND
         BCTR  R5,0                     LOW-LEVEL-
         BCTR  R5,0                     -INDIZES
         EX    R5,MVCBLANK              AUSBLANKEN
*
         LOCATE INDSN                   STELLT CVOL-NAME NACH GEN
         LTR   R15,R15
         BNZ   FEHLER
         MVC   CVOL,GEN+9               VOLSER UEBERTRAGEN FUER KAT
         MVC   GEN,ALTGEN
*
*        KATALOGEINTRAG D. NEUSTEN GENERATION LESEN, ENTKATALOGISIEREN
*
         LOCATE INDSN                   LESEN DES KATALOG-EINTRAGS
         LTR   R15,R15                  PRUEFEN RUECKKEHR-CODE
         BNZ   FEHLER                   WENN NICHT 0 , BRANCH FEHLER
*
         LA    R4,GEN+43
LOOP2    CLI   0(R4),C'.'               BEGINN DES LETZTEN QUALIFIERS
         BE    PUNKT                    SUCHEN
         BCT   R4,LOOP2
         ABEND 1000
*
PUNKT    MVC   GENR(8),1(R4)            UEBERTRAGEN GENERATIONS-NR
         PACK  DW+4(4),GENR+1(4)        PACKEN GEN.NR
         CVB   R9,DW                    GEN.NR BINAER
         SR    R8,R8
         D     R8,DIVISOR
         C     R8,REST
         BE    RC0
*
RC4      LA    R15,4
         B     RETURN
*
RETURN8  LA    R15,8
         B     RETURN
*
RC0      LA    R15,0
RETURN   L     R13,SAVEAREA+4
         ST    R15,16(R13)
         LM    R14,R12,12(R13)     REGISTER ZURUECKSETZEN
         BR    R14
*
FEHLER   EQU   *
         WTO   'GDG NICHT IM KATALOG GEFUNDEN ODER FEHLER BEIM UMKATALO*
               GISIEREN',ROUTCDE=11
         B     RETURN8
*
PARMERR  EQU   *
         WTO   'PARMFELD NICHT KORREKT',ROUTCDE=11
         B     RETURN8
*
PACK     PACK  DW+6(0),0(0,R2)
MVCBLANK MVC   1(0,R4),0(R4)            BLANKEN DER LOW-LEVEL-INDEXES
*
GDGDCB   DCB   DSORG=PS,MACRF=E,DDNAME=GDG,EXLST=LIST
*
INDSN    CAMLST NAME,GEN,,LOCAREA       FUER LOCATE KATALOG-EINTRAG
GEN      DC    CL44' '                  DS-NAME FUER CAMLST
ALTGEN   DC    CL44' '
LOCAREA  DS    0D
         DS    265C                     BLOCK FUER KATALOG. DS-NAMEN
*
         DS    0F
LIST     DC    X'07',AL3(JFCBAREA)
JFCBAREA DS    CL176
*
         DS    0D
DW       DC    XL8'00'
DIVISOR  DC    F'0'
REST     DC    F'0'
GENR     DC    CL8'0'
CVOL     DC    CL6'XXXXXX'
SAVEAREA DC    18F'0'
         LTORG
         DC    C'GRAMM/E7DR'
         END
