         TITLE 'SPSMP01 - SMP-RAHMEN-PROGRAMM'
SPSMP01  DMANF
         ST    R1,SAVEPARM
         EXTRACT ZFW,FIELDS=TIOT
         OPEN  (SYSIN,,SMPCNTL,(OUTPUT))
         TM    SYSIN+48,16             SYSIN MISSING ? OR ERROR
         BZ    ERR1                    YES
         TM    SMPCNTL+48,16
         BZ    ERR2                    OPEN-ERROR
         GET   SYSIN
         ST    1,CARDADR               SAVE RECORD ADDRESS
         CLC   0(8,1),=C' NOLOG. '
         BE    PTFINOK1
         SR    9,9
         L     8,ZFW                   TIOT
         LA    8,24(8)                 DD-ENTRY
A1       CLC   4(8,8),=C'SMPPTFIN'
         BE    A2
         IC    9,0(8)                  NEXT
         LA    8,0(8,9)                DD-ENTRY
         CLC   0(4,8),=A(0)            LAST ?
         BNE   A1                      NO
         B     PTFINOK
A2       L     9,16(8)                 UCB
         CLI   18(9),128               UCBTYPE MAGNETIC TAPE ?
         BE    PTFINOK                 YES
         EJECT
         OPEN  (SMPPTFIN,,SYSUT1,(OUTPUT))
         TM    SMPPTFIN+48,16
         BZ    ERR3                    OPEN-ERROR
         TM    SYSUT1+48,16
         BZ    ERR4                    OPEN-ERROR
A3       GET   SMPPTFIN
         LR    3,1
         BAL   14,CHECKPTF             R3=RECORD-ADDRESS
         B     *+4(15)
         B     A4
         B     A3
         B     A3
A4       BAL   14,CHANGE               CREATE IEBUPDTE CONTROL CARDS
         OI    SCHALTER,LOGBLANK
         NI    SCHALTER,255-NOLOG-NEXTNOL
A5       EQU   *
         CLC   0(2,3),=C'./'
         BNE   A6
         MVC   0(2,3),=C'$/'
A6       PUT   SYSUT1,(3)
         TM    SCHALTER,LOGBLANK
         BZ    A7
         NI    SCHALTER,255-LOGBLANK
         PUT   SMPCNTL,LOGCARD
         PUT   SMPCNTL,LOGCARD1
A7       EQU   *
         TM    SCHALTER,NOLOG
         BO    A8
         TM    SCHALTER,NEXTNOL
         BZ    *+12
         NI    SCHALTER,255-NEXTNOL
         OI    SCHALTER,NOLOG
         BAL   14,LOG                  IN=REG3, OUT=REG4
         PUT   SMPCNTL,LOGCARD
         PUT   SMPCNTL,(4)
A8       EQU   *
         GET   SMPPTFIN
         BAL   14,CHECKPTF             RC=0 -> NEW PTF
         B     *+4(15)
         B     A4
         B     A5
         OI    SCHALTER,NEXTNOL
         B     A5
EOFPTFIN EQU   *
         PUT   SMPCNTL,LOGCARD
         PUT   SMPCNTL,LOGCARD1
         CLOSE SMPPTFIN
         FREEPOOL SMPPTFIN
         TM    SCHALTER,ENDUPREQ
         BZ    NOENDUP
         PUT   SYSUT1,ENDUP
NOENDUP  EQU   *
         CLOSE SYSUT1
         FREEPOOL SYSUT1
         EJECT
         LINK  EP=IEBUPDTE,                                            C
               PARAM=(OPTION,DDNAMES),VL=1
         LTR   15,15
         BZ    PTFINOK
         WTO   'SMP011W IEBUPDTE ENDED WITH RETURN-CODE GREATER THEN ZEC
               RO',ROUTCDE=11
         EJECT
PTFINOK  EQU   *                       MOVE SYSIN TO SMPCNTL
         L     R2,CARDADR              LOAD RECORD ADDRESS
         PUT   SMPCNTL,(2)
PTFINOK1 GET   SYSIN
         ST    R1,CARDADR              SAVE RECORD ADDRESS
         B     PTFINOK                 LOOP
EOFSYSIN EQU   *
         CLOSE SYSIN
         FREEPOOL SYSIN
         CLOSE SMPCNTL
         FREEPOOL SMPCNTL
         EJECT
         L     R1,SAVEPARM
         L     R1,0(R1)                PARM
         LH    R2,0(R1)                L'PARM
         LTR   R3,R2
         BZ    NOPARM
         BCTR  R3,0                    WG/EXECUTE
         EX    R3,MOVEPARM
         AH    R2,PARMLEN
         LA    R2,1(R2)                WG/KOMMA
         STH   R2,PARMLEN
NOPARM   LA    R1,PARMADR
         LINK  EP=HMASMP
         LR    R3,R15
         CVD   R15,DOWO                RETURN-CODE
         UNPK  DOWO(4),DOWO+4(4)
         OI    DOWO+3,240
         MVC   WTO+51(4),DOWO
         L     R2,ZFW                  A(TIOT)
         MVC   WTO+12(8),0(R2)         JOBNAME
         MVC   WTO+21(8),8(R2)         STEPNAME
         WTO   MF=(E,WTO)
         LR    R15,R3
         DMEND RC=NO
         EJECT
CHECKPTF EQU   *
         LA    15,4
         CLC   0(2,3),=C'++'           SMP CONTROL CARD ?
         BNER  14                      NO
         LA    4,2(3)
         LA    5,71(3)
B1       CLI   0(4),64
         BNE   B2
         LA    R4,1(R4)
         CR    R4,R5
         BL    B1
         BR    14
B2       EQU   *
         CLC   0(3,4),=C'MOD'
         BE    B7
         CLC   0(3,4),=C'PTF'
         BNER  14
B3       CLI   0(4),C'('
         BE    B4
         LA    R4,1(R4)
         CR    R4,R5
         BL    B3
         BR    14
B4       LA    R4,1(R4)
B5       CLI   0(R4),64
         BNE   B6
         LA    R4,1(R4)
         CR    R4,R5
         BL    B5
         BR    14
B6       MVC   PTFNAME,0(R4)
         SR    R15,R15
         BR    14
         SPACE 2
B7       LA    R15,8
         BR    R14
         EJECT
LOG      EQU   *
         LA    R5,71(R3)
C1       EQU   *
         CLI   0(R5),64
         BNE   C2
         BCTR  R5,0
         B     C1
C2       SR    R5,R3
         CH    R5,=H'69'
         BH    C3
         CH    R5,=H'1'
         BNL   C4
         LA    R5,1
         B     C4
C3       LA    R5,68
C4       EQU   *
         MVC   LOGCARD2(71),0(R3)
         MVI   LOGCARD2+71,64
         LA    R4,LOGCARD2
         LR    R6,R4
         LR    R7,R5
C5       CLC   0(2,R6),=C').'
         BE    C6
         LA    R6,1(R6)
         BCT   R7,C5
         B     C7
C6       CH    R7,=H'1'
         BE    C7
         MVC   0(2,R6),=C')-'
C7       EQU   *
         LA    R5,1(R5,R4)
         MVC   0(2,R5),=C').'
         BR    14
         EJECT
CHANGE   EQU   *
         ST    14,SAVER14
         MVC   CHANGEOP,=C'ADD '
         OPEN  (SMPPTF,(INPUT))
         TM    SMPPTF+48,16
         BZ    D1
         FIND  SMPPTF,MEMNAME,D
         LTR   R15,R15
         BNZ   D1
         MVC   CHANGEOP,=C'REPL'
D1       EQU   *
         CLOSE SMPPTF
         OI    SCHALTER,ENDUPREQ
         MVC   CHANGENM,MEMNAME
         PUT   SYSUT1,CHANGECA
         PUT   SYSUT1,NUMBERCA
         L     14,SAVER14
         BR    14
         EJECT
ERR1     MVC   WTO1+53(8),=CL8'SYSIN'
         B     ERR
ERR2     MVC   WTO1+53(8),=CL8'SMPCNTL'
         B     ERR
ERR3     MVC   WTO1+53(8),=CL8'SMPPTFIN'
         B     ERR
ERR4     MVC   WTO1+53(8),=CL8'SYSUT1'
         B     ERR
ERR      EQU   *
         WTO   MF=(E,WTO1)
         ABEND 13
         TITLE 'SMP01 - DEFINITIONEN'
         PRINT NOGEN
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL,EODAD=EOFSYSIN
SMPCNTL  DCB   DDNAME=SMPCNTL,DSORG=PS,MACRF=PM,RECFM=FB,              C
               LRECL=80,BLKSIZE=4240
SMPPTFIN DCB   DDNAME=SMPPTFIN,DSORG=PS,MACRF=GL,EODAD=EOFPTFIN
SYSUT1   DCB   DDNAME=SMPUPDTE,DSORG=PS,MACRF=PM,RECFM=FB,             C
               LRECL=80,BLKSIZE=4240
SMPPTF   DCB   DDNAME=SMPPTF,DSORG=PO,MACRF=R
         DS    0D
MEMNAME  DS    0CL8
PTFNAME  DS    CL7
         DC    C' '
DOWO     DS    D
ZFW      DS    F
SAVER14  DS    F
SAVEPARM DS    F
SCHALTER DC    X'0'
ENDUPREQ EQU   128
LOGBLANK EQU   64
NOLOG    EQU   32
NEXTNOL  EQU   16
CARDADR  DS    A
PARMADR  DC    A(PARMLEN)
         CNOP  2,4
PARMLEN  DC    H'12'
         DC    C'ZAP=AMASPZAP,'
PARMDAT  DC    CL100' '
MOVEPARM MVC   PARMDAT(0),2(R1)
WTO1     WTO   'SMP013E OPEN-ERROR (DD-STATEMENT MISSING) DDNAME=XXXXXXX
               XX',ROUTCDE=11,MF=L
WTO      WTO   'SMP012I JJJJJJJJ.SSSSSSSS HMASMP-RETURN-CODE = XXXX',  C
               ROUTCDE=(1,11),MF=L
LOGCARD  DS    0CL80
         DC    C'LOG',CL60' ',C'(        ',CL8' '
LOGCARD1 DC    CL80'  ).'
LOGCARD2 DC    CL80' '
CHANGECA DS    0CL80
         DC    C'./ '
CHANGEOP DS    CL4
         DC    C' LIST=ALL,NAME='
CHANGENM DS    CL8
         DC    CL50' '
NUMBERCA DC    CL80'./ NUMBER NEW1=10,INCR=10'
ENDUP    DC    CL80'./ ENDUP'
         CNOP  2,4
OPTION   DC    H'3',C'MOD'
         CNOP  6,8
DDNAMES  DC    H'88'
         DC    4D'0'
         DC    CL8'SMPUPDTE'
         DC    2D'0'
         DC    2CL8'SMPPTF'
         DC    2D'0'
         END
