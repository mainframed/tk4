* STAND 06/22/79
***********************************************************************
*                                                                     *
*        A S T O P 0 2 3                                              *
*        ---------------                                              *
*        AUFRUF = CALL ASTOP023                                       *
*                                                                     *
*        ZWECK = DURCHSUCHT DIE TABELLE KTAB, OB L-KARTEN EINGELESEN  *
*                WURDEN.                                              *
*                JE L-KARTE WIRD DAS PROGRAMM OP023AST AUFGERUFEN.    *
*                AN OP023AST WIRD ALS ARGUMENT ARG UEBERGEBEN (BE=    *
*                SCHRIEBEN IN MACRO OP023ARG).                        *
*                OP023AST ERSTELLT FUER DIE IN ARG UEBERGEBENE        *
*                LISTEN-NUMMER DRUCKAUFTRAG UND LISTENBEGLEITSCHEIN.  *
*                                                                     *
***********************************************************************
         SPACE 3
         PRINT NOGEN
ASTOP023 CSECT
         XSAVE R12,SAVE,ASTOP023
         AREG
         USING DATAB,R10
         USING DKTAB,R9
         USING OP023ARG,R8
*
         L     R10,PATAB      -->ATAB
         L     R9,PKTAB       -->KTAB
         LA    R8,ARG         -->ARGUMENT OP023
*
         MVC   ZEILE(23),=C'OP023 AUFGERUFEN FUER :'
         LA    R2,15
         ST    R2,KZ
         CALL  DRUFEHL,MF=(E,DRU)
         XC    RETCODE,RETCODE
         MVC   OPBERZ,ASBZT   BERICHTSZEITAUS JOBKARTE
         MVC   OPERSTTG,DATUM
         MVC   OPERSTTM,UHRZEIT
*
*  KTAB DURCHSUCHEN NACH L-KARTEN UNF OP023 AUFRUFEN
*
LOOP     EQU   *
         CLC   KNEXT,=F'-1'
         BE    ENDE
         CLI   KNEXT,KLISTE
         BE    EXEC10
LOOPE    EQU   *
         L     R9,KNEXT       -->NAECHSTE KTAB-EINTRAGUNG
         B     LOOP
*
*  ARGUMENT FUER OP023 AUFBEREITEN
*
EXEC10   EQU   *
         MVC   OPDDN,TLDDN    DDNAME
         MVI   OPVOLSER,C' '
         MVC   OPVOLSER+1(29),OPVOLSER  LOESCHEN
         LA    R5,OPDSN-2     -->DSNAME
         LA    R3,OPVOLCNT    -->-->VOLSER TABELLE
         LA    R4,TLDDN       -->DDNAME
         LA    R2,32
         STH   R2,LV          LAENGE VOLSER TABELLE
         LA    R2,46
         STH   R2,LDS         LAENGE DSNAME TABELLE
         CALL  GTSERNOS,(LV,(R3),(R4),LDS,(R5)),VL
         CLC   LV,=H'0'
         BNE   FEHLER
*
*  SEITENANZAHL BINAER UNFORMEM, DEZIMALSTELLEN ABSCHNEIDEN
*
EXEC15   EQU   *
         LH    R3,TLSLENG     LAENGE SUMME
         BCTR  R3,0
         L     R2,TLSADR      -->SUMME
         CLI   ASPRACHE,FORTRAN
         BNE   EXEC20
         L     R2,0(R2)
         B     EXEC21
EXEC20   EQU   *
         EX    R3,UPZAP       SUMME-->DWORD
         CVB   R2,DWORD
EXEC21   EQU   *
         SRDA  R2,32
         LH    R4,TLSDEZ      DEZIMALSTELLEN SUMME
         SLA   R4,2           *4=INDEX AUF DIVISOR
         D     R2,DIVISOR(R4) DEZIMALSTELLEN ABSCHNEIDEN
         ST    R3,OPPAGES
*
         MVC   OPPGM,APGM     )LISTEN
         MVC   OPLNR,TLNR     )-NR.
*
EXEC29   NOP   EXEC30
         LOAD  EP=OP023AST
         LINK  EP=OP023AST,PARAM=(ARG),VL=1
EXEC30   EQU   *
*
         BAL   R11,LOESCH
*
*  RETURNCODE OP023 MERKEN
*
         C     R15,RETCODE
         BL    *+8
         ST    R15,RETCODE    RETCODE=MAX(R15,RETCODE)
         CVD   R15,DWORD
         OI    DWORD+7,X'0F'
         UNPK  ZEILE+55(3),DWORD+6(2)
         MVC   ZEILE+44(10),=C'RETURNCODE'
*
*  AUFRUF VON OP023AST AUF ASTBLATT LISTEN
*
         MVC   ZEILE+1(8),TLDDN
         MVC   ZEILE+11(12),OPLIST#
         L     R2,OPPAGES     SEITENANZAHL
         LTR   R2,R2
         BM    EXEC40
         CVD   R2,DWORD
         MVC   ZEILE+25(10),MASKS
         ED    ZEILE+25(10),DWORD+4
EXEC40   EQU   *
         MVC   ZEILE+36(6),=C'SEITEN'
         LA    R2,15
         ST    R2,KZ
         CALL  DRUFEHL,MF=(E,DRU)
         B     LOOPE
*
*  ENDE
*
ENDE     XRETURN              0
*
*  LOESCHEN ZEILE
*
LOESCH   MVI   ZEILE,C' '
         MVC   ZEILE+1(80),ZEILE
         BR    R11
*
*  DDKARTE FEHLT
*
FEHLER   EQU   *
         CLC   TLDDN,=C'F-KARTE '  AUTOMAT. ERZEUGT
         NOP   FALSCH         NEIN
         MVI   OPDSN,C' '
         MVC   OPDSN+1(43),OPDSN  LOESCHEN
         XC    OPVOLCNT,OPVOLCNT  LOESCHEN
         MVI   OPVOLSER,C' '
         MVC   OPVOLSER+1(29),OPVOLSER  LOESCHEN
         B     EXEC15
FALSCH   EQU   *
         BAL   R11,LOESCH
         MVC   ZEILE+1(8),TLDDN
         MVC   ZEILE+11(13),=C'DDKARTE FEHLT'
         LA    R2,15
         ST    R2,KZ
         CALL  DRUFEHL,MF=(E,DRU)
         B     LOOPE
*
*  DEFINITIONEN
*
DWORD    DS    D
SAVE     DS    18F
PATAB    DC    V(ATAB)        -->ATAB
KZ       DC    F'15'
RETCODE  DS    F
LV       DS    H
LDS      DC    H'46'
MASKS    DC    XL10'40204020202040202120'
UPZAP    ZAP   DWORD,0(0,R2)
DIVISOR  DC    F'1,10,100,1000'
DRU      CALL  ,(KZ,ZEILE),VL,MF=L
ZEILE    DS    CL81
         PRINT GEN
         MATAB
         MKTAB
OP023ARG OP023ARG
ASTOP023 CSECT
ARG      DS    0F
         DS    CL(OP023END)
         END
