         PRINT NOGEN
JOBREC   CSECT
*
***********************************************************************
*                                                                     *
*        DAS PROGRAMM ERSTELLT FUER JEDEN WAEHREND EINES MONATS       *
*        GELAUFENE J O B    EINEN DATENSATZ (SH. DEFINITION SSATZ)    *
*        MIT STATISTISCHEN WERTEN                                     *
*                                                                     *
*        EINGABE    SMF RECORDS TYPE 4/34 (STEPSAETZE) SORTIERT       *
*                   NACH JOBLOG                                       *
*                                                                     *
*        AUSGABE    DATENSATZ JE J O B    MIT STATISTISCHEN WERTEN    *
*                                                                     *
*        DIE ERMITTLUNG DER WERTE                                     *
*                                                                     *
*                   ERRECHNETE LAUFZEIT                               *
*                   GESAMT ZUGRIFFE                                   *
*                   PLATTEN DATEIEN                                   *
*                   PLATTEN ZUGRIFFE                                  *
*                   BAND DATEIEN                                      *
*                   BAND ZUGRIFFE                                     *
*                   UR DATEIEN                                        *
*                   UR ZUGRIFFE                                       *
*                   SYSIN/TGET DATEIEN                                *
*                   SYSIN/TGET KARTEN                                 *
*                   SYSOUT/TPUT DATEIEN                               *
*                   SYSOUT/TPUT ZEILEN                                *
*                   JOBKOSTEN                                         *
*                                                                     *
*        ERFOLGT IM UNTERPROGRAMM PGMJOBBW                            *
*                                                                     *
*        AUFRUF    CALL PGMJOBBW,(SMFREC,CBER)                        *
*                                                                     *
*                                 SMFREC = SMF-RECORD                 *
*                                 CBER   = ERGEBNISFELDER             *
*                                          (SH. DEFINITIONEN)         *
*                                                                     *
***********************************************************************
*                                                                     *
*        DAS UNTERPROGAMM  PGMJOBBW (BEWERTUNG) MUSS VOM JEWEILIGEN   *
*        ANWENDER SELBST ERSTELLT WERDEN                              *
*                                                                     *
***********************************************************************
***********************************************************************
         EJECT
*
******** VERWENDUNG DER REGISTER **************************************
*                                                                     *
R0       EQU   0                                                      *
R1       EQU   1                                                      *
R2       EQU   2        ARBEITS REGISTER                              *
R3       EQU   3        ARBEITS REGISTER                              *
R4       EQU   4                                                      *
R5       EQU   5                                                      *
R6       EQU   6                                                      *
R7       EQU   7                                                      *
R8       EQU   8                                                      *
R9       EQU   9                                                      *
R10      EQU   10                                                     *
R11      EQU   11       ADR. SMF - SATZ                               *
R12      EQU   12       BASIS REGISTER                                *
R13      EQU   13       SAVE AREA                                     *
R14      EQU   14                                                     *
R15      EQU   15                                                     *
*                                                                     *
***********************************************************************
         EJECT
*
******** PROGRAMM - ANFANG ********************************************
*
         B     12(R15)
         DC    C'JOBREC  '
         STM   R14,R12,12(R13)
         BALR  R12,0
         USING *,R12
         ST    R13,SVA+4
         LR    R2,R13
         LA    R13,SVA
         ST    R13,8(R2)
         OPEN  (SMF,,JOB,(OUTPUT))
         LA    R11,SMFREC+4
LESEN    GET   SMF,SMFREC
         CLI   1(R11),X'04'
         BE    WEI1                    BATCH STEPSATZ
         CLI   1(R11),X'22'
         BE    WEI1                    TSO STEPSATZ
         B     LESEN                   KEIN STEPSATZ
WEI1     NOP   VER
         OI    WEI1+1,X'F0'
SPEI     MVC   JOBNAJ,14(R11)
         MVC   JOBLOG,14(R11)
         ZAP   STEPJ,=X'0F'
         MVC   STEPJ+5(85),STEPJ
         ZAP   RCPU,=X'0F'
         MVC   RCPU+6(12),RCPU
VER      CLC   JOBLOG,14(R11)
         BNE   WEICH1
         AP    STEPJ,=X'1F'
         XC    WORT,WORT
         MVC   WORT+2(2),70(R11)
         L     R2,WORT
         CVD   R2,DWORT
         CP    DWORT+3(5),REGFOJ
         BNH   KON
         ZAP   REGFOJ,DWORT+3(5)       REGION GEFORDERT
KON      MVC   WORT+2(2),74(11)
         L     R2,WORT
         CVD   R2,DWORT
         CP    DWORT+3(5),REGNUJ
         BNH   KONA
         ZAP   REGNUJ,DWORT+3(5)       REGION GENUTZT
KONA     LA    R2,102(R11)
         AH    R2,102(R11)
         MVC   WORT+1(3),1(R2)
         L     R2,WORT
         CVD   R2,DWORT
         AP    RCPU,DWORT+2(6)         CPU ZEIT
         MVC   WORT,2(R11)
         L     R2,WORT
         MVC   WORT,39(R11)
         L     R3,WORT
         CR    R2,R3
         BNL   KONB
         A     R2,=F'8640000'          START V. 24 UHR/STOP N. 24 UHR
KONB     SR    R2,R3
         CVD   R2,DWORT
         AP    RSMFLZ,DWORT+2(6)       SMF LAUFZEIT
         ZAP   ERRLZ,=X'0F'            LOESCHEN SPEICHER
         MVC   ERRLZ+5(60),ERRLZ        FUER UNTERPROGRAMM
         CALL  PGMJOBBW,(SMFREC,CBER)
         AP    RERRLZ,ERRLZ
         AP    KOSTENJ,KOSTEN
         AP    ZUGRJ,ZUGR
         AP    PLDATJ,PLDAT
         AP    PLZUGJ,PLZUG
         AP    BDDATJ,BDDAT
         AP    BDZUGJ,BDZUG
         AP    URDATJ,URDAT
         AP    URZUGJ,URZUG
         AP    SIDATJ,SIDAT
         AP    SIKARJ,SIKAR
         AP    SODATJ,SODAT
         AP    SOZEIJ,SOZEI
         B     LESEN
WEICH1   NOP   VERGL
         OI    WEICH1+1,X'F0'
SPEICH   MVC   JOBNA,JOBNAJ            JOB-NAME
         ZAP   AUFR,=X'0F'             LOESCHEN
         MVC   AUFR+5(190),AUFR         SSATZ
VERGL    CLC   JOBNA,JOBNAJ
         BNE   AUSGABE                 NEUER JOB-NAME
************** RECHNEN MINUTEN AUS HUNDERTSTEL SEKUNDEN
         ZAP   RF,RCPU
         DP    RF,=X'6F'
         ZAP   CPUJ,RF+4(5)            CPU
         ZAP   RF,RERRLZ
         DP    RF,=X'6F'
         ZAP   ERRLZJ,RF+4(5)          ERRECHN. LAUFZEIT
         ZAP   RF,RSMFLZ
         DP    RF,=X'6F'
         ZAP   SMFLZJ,RF+4(5)          SMF LAUFZEIT
         AP    AUFR,=X'1F'             ADD. AUFRUFE
         CP    STEPJ,MSTEP
         BNH   KON1
         ZAP   MSTEP,STEPJ             ANZAHL STEPS MAXIMAL
KON1     AP    DSTEP,STEPJ             ANZAHL STEPS FUER DURCHSCHN.
         CP    REGFOJ,MREGFO
         BNH   KON2
         ZAP   MREGFO,REGFOJ           REGION GEFORDERT MAXIMAL
KON2     AP    DREGFO,REGFOJ           REGION GEFORDERT FUER DURCHSCHN.
         CP    REGNUJ,MREGNU
         BNH   KON3
         ZAP   MREGNU,REGNUJ           REGION GENUTZT MAXIMAL
KON3     AP    DREGNU,REGNUJ           REGION GENUTZT FUER DURCHSCHN.
         CP    CPUJ,MCPU
         BNH   KON4
         ZAP   MCPU,CPUJ               CPU ZEIT MAXIMAL
KON4     AP    DCPU,CPUJ               CPU ZEIT FUER DURCHSCHN.
         CP    ERRLZJ,MERRLZ
         BNH   KON5
         ZAP   MERRLZ,ERRLZJ           ERRECHN. LAUFZEIT MAXIMAL
KON5     AP    DERRLZ,ERRLZJ           ERRECHN. LAUFZEIT FUER DURCHSCHN
         CP    SMFLZJ,MSMFLZ
         BNH   KON6
         ZAP   MSMFLZ,SMFLZJ           SMF LAUFZEIT MAXIMAL
KON6     AP    DSMFLZ,SMFLZJ           SMF LAUFZEIT FUER DURCHSCHN.
         CP    ZUGRJ,MZUGR
         BNH   KON8
         ZAP   MZUGR,ZUGRJ             GESAMT ZUGRIFFE MAXIMAL
KON8     AP    DZUGR,ZUGRJ             GESAMT ZUGRIFFE FUER DURCHSCHN.
         CP    PLDATJ,MPLDAT
         BNH   KON9
         ZAP   MPLDAT,PLDATJ           PLATTEN DATEIEN MAXIMAL
KON9     AP    DPLDAT,PLDATJ           PLATTEN DATEIEN FUER DURCHSCHN.
         CP    PLZUGJ,MPLZUG
         BNH   KON10
         ZAP   MPLZUG,PLZUGJ           PLATTEN ZUGRIFFE MAXIMAL
KON10    AP    DPLZUG,PLZUGJ           PLATTEN ZUGRIFFE FUER DURCHSCHN.
         CP    BDDATJ,MBDDAT
         BNH   KON11
         ZAP   MBDDAT,BDDATJ           BAND DATEIEN MAXIMAL
KON11    AP    DBDDAT,BDDATJ           BAND DATEIEN FUER DURCHSCHN.
         CP    BDZUGJ,MBDZUG
         BNH   KON12
         ZAP   MBDZUG,BDZUGJ           BAND ZUGRIFFE MAXIMAL
KON12    AP    DBDZUG,BDZUGJ           BAND ZUGRIFFE FUER DURCHSCHN.
         CP    URDATJ,MURDAT
         BNH   KON13
         ZAP   MURDAT,URDATJ           UR DATEIEN MAXIMAL
KON13    AP    DURDAT,URDATJ           UR DATEIEN FUER DURCHSCHN.
         CP    URZUGJ,MURZUG
         BNH   KON14
         ZAP   MURZUG,URZUGJ           UR ZUGRIFFE MAXIMAL
KON14    AP    DURZUG,URZUGJ           UR ZUGRIFFE FUER DURCHSCHN.
         CP    SIDATJ,MSIDAT
         BNH   KON15
         ZAP   MSIDAT,SIDATJ           SYSIN DATEIEN MAXIMAL
KON15    AP    DSIDAT,SIDATJ           SYSIN DATEIEN FUER DURCHSCHN.
         CP    SIKARJ,MSIKAR
         BNH   KON16
         ZAP   MSIKAR,SIKARJ           SYSIN KARTEN MAXIMAL
KON16    AP    DSIKAR,SIKARJ           SYSIN KARTEN FUER DURCHSCHN.
         CP    SODATJ,MSODAT
         BNH   KON17
         ZAP   MSODAT,SODATJ           SYSOUT DATEIEN MAXIMAL
KON17    AP    DSODAT,SODATJ           SYSOUT DATEIEN FUER DURCHSCHN.
         CP    SOZEIJ,MSOZEI
         BNH   KON18
         ZAP   MSOZEI,SOZEIJ           SYSOUT ZEILEN MAXIMAL
KON18    AP    DSOZEI,SOZEIJ           SYSOUT ZEILEN FUER DURCHSCHN.
         AP    MKOSTEN,KOSTENJ         JOBKOSTEN GESAMT
KON19    AP    DKOSTEN,KOSTENJ         JOBKOSTEN FUER DURCHSCHN.
WEI2     NOP   WEI3
         B     SPEI
WEI3     OI    WEICH2+1,X'F0'
AUSGABE  ZAP   WORT,AUFR
         BZ    KON201
************** RECHNEN DURCHSCHNITT
         LA    R2,DSTEP
         LA    R3,DSOZEI
KON20    ZAP   RF,0(5,R2)
         DP    RF,WORT
         ZAP   0(5,R2),RF+1(5)
         LA    R2,5(R2)
         CR    R2,R3
         BNH   KON20
************** WENN BEI DURCHSCHNITT DATEI = 0 UND DAZUGEHOERENDE
************** ZUGRIFFE NICHT = 0, DANN DATEI = 1
KON201   LA    R2,DPLDAT
         LA    R3,DSODAT
KON21    ZAP   5(5,R2),5(5,R2)
         BZ    KON22
         ZAP   0(5,R2),0(5,R2)
         BNZ   KON22
         AP    0(5,R2),=X'1F'
KON22    LA    R2,10(R2)
         CR    R2,R3
         BNH   KON21
************** RECHNEN FAKTOR
         ZAP   MERRLZ,MERRLZ
         BZ    KON23
         ZAP   RF,MCPU
         MP    RF,=X'01000F'
         DP    RF,MERRLZ
         ZAP   MFAKT,RF(5)
KON23    ZAP   DERRLZ,DERRLZ
         BZ    SCHREIB
         ZAP   RF,DCPU
         MP    RF,=X'01000F'
         DP    RF,DERRLZ
         ZAP   DFAKT,RF(5)
SCHREIB  PUT   JOB,SSATZ
WEICH2   NOP   ENDE1
         B     SPEICH
ENDE     OI    WEI2+1,X'F0'
         B     VERGL
ENDE1    CLOSE (SMF,,JOB)
         L     R13,SVA+4
         LM    R14,R12,12(R13)
         XR    R15,R15                 RETURN-CODE = 0
         BR    R14
         EJECT
*
******** SPEICHER, BEREICHE, LITERALE *********************************
*
         DS    0D
SVA      DS    18F                     SAVE AREA
DWORT    DS    CL8                     DOPPEL WORT
WORT     DS    CL4                     WORT
RF       DS    CL10                    RECHENFELD
RCPU     DS    PL6                     CPU ZEIT    HUNDERTSTEL SEKUNDEN
RERRLZ   DS    PL6                     ERRECHN.LFZ HUNDERTSTEL SEKUNDEN
RSMFLZ   DS    PL6                     SMF LFZ     HUNDERTSTEL SEKUNDEN
JOBLOG   DC    CL16' '
         LTORG
         EJECT
*
******** SPEICHER FUER DEN AUFRUF DES UNTERPROGRAMMES PGMJOBBW
*
CBER     DS    0CL65
ERRLZ    DS    PL5      ERRECHN. LAUFZEIT   HUNDERTST. SEKUNDEN
ZUGR     DS    PL5      GESAMT ZUGRIFFE
PLDAT    DS    PL5      PLATTEN DATEIEN
PLZUG    DS    PL5      PLATTEN ZUGRIFFE
BDDAT    DS    PL5      BAND DATEIEN
BDZUG    DS    PL5      BAND ZUGRIFFE
URDAT    DS    PL5      UR DATEIEN
URZUG    DS    PL5      UR ZUGRIFFE
SIDAT    DS    PL5      SYSIN/TGET DATEIEN
SIKAR    DS    PL5      SYSIN/TGET KARTEN
SODAT    DS    PL5      SYSOUT/TPUT DATEIEN
SOZEI    DS    PL5      SYSOUT/TPUT ZEILEN
KOSTEN   DS    PL5      PROGRAMMKOSTEN      (9,2 / DM)
         EJECT
*
******** JOB - SUMMENSPEICHER
*
JOBNAJ   DS    CL8
STEPJ    DS    PL5
REGFOJ   DS    PL5
REGNUJ   DS    PL5
CPUJ     DS    PL5
ERRLZJ   DS    PL5
SMFLZJ   DS    PL5
KOSTENJ  DS    PL5
ZUGRJ    DS    PL5
PLDATJ   DS    PL5
PLZUGJ   DS    PL5
BDDATJ   DS    PL5
BDZUGJ   DS    PL5
URDATJ   DS    PL5
URZUGJ   DS    PL5
SIDATJ   DS    PL5
SIKARJ   DS    PL5
SODATJ   DS    PL5
SOZEIJ   DS    PL5
         EJECT
*
******** STATISTIK - SATZ *********************************************
*
SSATZ    DS    0CL203
JOBNA    DS    CL8      JOB-NAME
AUFR     DS    PL5      AUFRUFE GESAMT
MSTEP    DS    PL5      ANZAHL STEPS GESAMT                 MAXIMAL
MREGFO   DS    PL5      REGION GEFORDERT                    MAXIMAL
MREGNU   DS    PL5      REGION GENUTZT                      MAXIMAL
MCPU     DS    PL5      CPU ZEIT             (9,3 / MINUT.) MAXIMAL
MERRLZ   DS    PL5      ERRECHN. LAUFZEIT    (9,3 / MINUT.) MAXIMAL
MSMFLZ   DS    PL5      SMF LAUFZEIT         (9,3 / MINUT.) MAXIMAL
MFAKT    DS    PL5      FAKTOR               (9,3)          MAXIMAL
MKOSTEN  DS    PL5      PROGRAMMKOSTEN       (9,2 / DM)     MAXIMAL
MZUGR    DS    PL5      GESAMT ZUGRIFFE                     MAXIMAL
MPLDAT   DS    PL5      PLATTEN DATEIEN                     MAXIMAL
MPLZUG   DS    PL5      PLATTEN ZUGRIFFE                    MAXIMAL
MBDDAT   DS    PL5      BAND DATEIEN                        MAXIMAL
MBDZUG   DS    PL5      BAND ZUGRIFFE                       MAXIMAL
MURDAT   DS    PL5      UR DATEIEN                          MAXIMAL
MURZUG   DS    PL5      UR ZUGRIFFE                         MAXIMAL
MSIDAT   DS    PL5      SYSIN/TGET DATEIEN                  MAXIMAL
MSIKAR   DS    PL5      SYSIN/TGET KARTEN                   MAXIMAL
MSODAT   DS    PL5      SYSOUT/TPUT DATEIEN                 MAXIMAL
MSOZEI   DS    PL5      SYSOUT/TPUT ZEILEN                  MAXIMAL
DSTEP    DS    PL5      ANZAHL STEPS GESAMT                 DURCHSCHN.
DREGFO   DS    PL5      REGION GEFORDERT                    DURCHSCHN.
DREGNU   DS    PL5      REGION GENUTZT                      DURCHSCHN.
DCPU     DS    PL5      CPU ZEIT             (9,3 / MINUT.) DURCHSCHN.
DERRLZ   DS    PL5      ERRECHN. LAUFZEIT    (9,3 / MINUT.) DURCHSCHN.
DSMFLZ   DS    PL5      SMF LAUFZEIT         (9,3 / MINUT.) DURCHSCHN.
DFAKT    DS    PL5      FAKTOR               (9,3)          DURCHSCHN.
DKOSTEN  DS    PL5      PROGRAMMKOSTEN       (9,2 / DM)     DURCHSCHN.
DZUGR    DS    PL5      GESAMT ZUGRIFFE                     DURCHSCHN.
DPLDAT   DS    PL5      PLATTEN DATEIEN                     DURCHSCHN.
DPLZUG   DS    PL5      PLATTEN ZUGRIFFE                    DURCHSCHN.
DBDDAT   DS    PL5      BAND DATEIEN                        DURCHSCHN.
DBDZUG   DS    PL5      BAND ZUGRIFFE                       DURCHSCHN.
DURDAT   DS    PL5      UR DATEIEN                          DURCHSCHN.
DURZUG   DS    PL5      UR ZUGRIFFE                         DURCHSCHN.
DSIDAT   DS    PL5      SYSIN/TGET DATEIEN                  DURCHSCHN.
DSIKAR   DS    PL5      SYSIN/TGET KARTEN                   DURCHSCHN.
DSODAT   DS    PL5      SYSOUT/TPUT DATEIEN                 DURCHSCHN.
DSOZEI   DS    PL5      SYSOUT/TPUT ZEILEN                  DURCHSCHN.
         EJECT
*
******** DCB / SMF - SATZ *********************************************
*
SMF      DCB   DDNAME=SMFDAT,DSORG=PS,MACRF=GM,EODAD=ENDE
*
*
JOB      DCB   DDNAME=JOBDAT,DSORG=PS,MACRF=PM
*
*
SMFREC   DS    CL16384
*
*
         END
