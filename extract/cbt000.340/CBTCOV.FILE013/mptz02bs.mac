         MACRO
&NAME    MCHHEX &HFELD,&EFELD
         GBLB  &DEFINE
         LCLC  &HK
&HK      SETC  ''''
         AIF   (N'&SYSLIST EQ 2).OK1
         MNOTE 16,'ZAHL DER OPERANDEN NICHT 2'
.FEHLER  MNOTE 16,'GENERIERUNG ABGEBROCHEN'
         MEXIT
.OK1     AIF   (L'&HFELD LE 256).OK2
         MNOTE 16,'URSPRUNGSFELD LAENGER ALS 256 BYTES'
         AGO   .FEHLER
.OK2     AIF   (L'&HFELD/2 EQ L'&EFELD).OK3
         MNOTE *,'ERGEBNISFELD NICHT HALB SO LANG WIE URSPRUNGSFELD'
.OK3     ANOP
&NAME    DS    0H .                     START MAKRO MCHHEX
         AIF   (&DEFINE).WEITER
&DEFINE  SETB  1
         B     MCXA&SYSNDX .               UEBERSPRINGEN DEFINITIONEN
MCXT     DS    CL256 .                  UEBERSETZUNGSTABELLE
MCXS     DS    5F .                     SAVE REGISTER 14 - 2
MCXC     DS    H .                      RETURNCODE
MCXA&SYSNDX EQU * .                      GENERELLER BEGINN
.WEITER  ANOP
         MVI   MCXT,X'FF' .             AUFFUELLEN DER UEBERSETZUNGS-
         MVC   MCXT+1(255),MCXT .       TABELLE MIT HEXA FF (RUECKWSG)
         XC    MCXT+193(6),MCXT+193 .   LOESCHEN BUCHSTABEN A BIS F
         XC    MCXT+240(10),MCXT+240 .  LOESCHEN ZIFFERN 0 BIS 9
         STM   14,2,MCXS .              RETTEN REGISTER 14 BIS 2
         TRT   &HFELD,MCXT .            PRUEFEN AUF 0 - 9 O. A - F
         BZ    MCXR&SYSNDX .               INHALT IST IN ORDNUNG
         MVC   MCXC,=H'4' .             INHALT FALSCH, RETURNCODE = 4
         B     MCXE&SYSNDX .               AUSGANG AUS DEM MAKRO
MCXR&SYSNDX LA 1,&HFELD .               ADRESSE VOM URSPRUNGSFELD
         LA    14,2 .                   INKREMENT FUER ADRESSRECHNUNG
         LA    15,&HFELD+L&HK&HFELD-2 .    ENDADRESSE
MCXN&SYSNDX NI 1(1),X'3F' .             JEDES 2. BYTE AENDERN
         BXLE  1,14,MCXN&SYSNDX .          ERHOEHEN REG 1 BIS ENDE
         MVC   MCXT+1(6),=X'AABBCCDDEEFF' A - F FUERS RECHTE HALBBYTE
         MVC   MCXT+48(10),=X'00112233445566778899' ZIFFERN DAZU
         MVC   MCXT+193(6),MCXT+1 .     A- F FUERS LINKE HALBBYTE
         MVC   MCXT+240(10),MCXT+48 .   ZIFFERN 0 - 9 DAZU
         TR    &HFELD,MCXT .            UEBERSETZEN URSPRUNGSFELD
         LA    1,&EFELD .               ADRESSE ERGEBNISFELD INS R 1
         LA    14,&HFELD .              ADRESSE URSPRUNGSFELD R 14
         LA    15,L&HK&HFELD/2 .           LAENGE ERGEBNIS R 15
MCXM&SYSNDX MVZ 0(1,1),0(14) .           UEBERTRAGEN LINKES HALBBYTE
         MVN   0(1,1),1(14) .           UEBERTRAGEN RECHTES HALBBYTE
         LA    1,1(1) .                 REG 1 UM 1 ERHOEHEN
         LA    14,2(14) .               REG 14 UM 2 ERHOEHEN
         BCT   15,MCXM&SYSNDX .            VERZWEIGEN BIS R 15 = 0
         XC    MCXC,MCXC .              RETURNCODE AUF 0 SETZEN
MCXE&SYSNDX LM 14,2,MCXS .              ZURUECKSPEICHERN REGISTER
         LH    15,MCXC .                RETURNCODE INS REG 15
         MEND
