* STAND 06/22/79
ASTCLRTP CSECT
*
*******************************************************************
*                                                                 *
* AUFRUF: CALL ASTCLRTP                                           *
* ZWECK : PRUEFEN IN DER TABELLE KTAB, OB CLEARTAP VERLANGT WIRD  *
*         UND AUFRUF                                              *
*                                                                 *
*******************************************************************
         REG
         XSAVE R12,SVA,ASTCLRTP
         USING DATAB,R10
         USING DKTAB,R9
*
         L     R10,PATAB    ==> ATAB
         L     R9,PKTAB     ==> KTAB
         SR    R15,R15
*
         LA    R5,2
         MVC   OUTLINE,HEADLINE
*
* DURCHSUCHEN KTAB NACH F-KARTEN
* ------------------------------
LOOP     EQU   *
         CLC   KNEXT,=F'-1'
         BE    ENDE
         CLI   KNEXT,KFILE
         BE    FCARD
LOOPE    EQU   *
         L     R9,KNEXT             ==> NAECHSTE KTAB-EINTRAGUNG
         B     LOOP
*
* FILE-KARTE GEFUNDEN
* -------------------
FCARD    EQU   *
         CLI   TFCLEAR,RYES         TAPECLEAR FUER DIESEN FILE ?
         BNE   LOOPE                NEIN ==> WEITERSUCHEN
AUFRUF   EQU   *
         MVC   CLDDN,TFDDN          DDNAME
         LOAD  EP=CLEARTAP
         LINK  EP=CLEARTAP,PARAM=(CLDDN,CNR)
*
* MELDUNG SCHREIBEN
* -----------------
        LTR    R15,R15
        BNZ    W1
W0      EQU    *
        MVI    MSGNR,C'0'
        MVC    CNR0,CNR
        MVC    MSG,CLR000                ERFOLGREICH GELOESCHT
        B      WRITE
W1      EQU    *
        CH     R15,=H'2'
        BL     W2                        ==> RETURNCODE = 1
        BH     W3                        ==> RETURNCODE > 2
        MVC    CNR2,CNR                  ==> RETURNCODE = 2
        MVI    MSGNR,C'2'
        MVC    MSG,CLR002                ANGEGEBENE VOLSER KEIN BAND
        B      WRITE
W2      EQU    *
        MVI    MSGNR,C'6'
        MVC    MSG,CLR006                ANZAHL VOLUMES > 5
        B      WRITE
W3      EQU    *
        CH     R15,=H'4'
        BL     W4                        ==> RETURNCODE = 3
        BH     W5                        ==> RETURNCODE = 5
        MVC    CNR4,CNR                  ==> RETURNCODE = 4
        MVI    MSGNR,C'4'
        MVC    MSG,CLR004                FEHLER BEI RDJFCB
        B      WRITE
W4      EQU    *
        MVC    CNR3,CNR
        MVI    MSGNR,C'3'
        MVC    MSG,CLR003                I/O ERROR ON TAPE
        B      WRITE
W5      EQU    *
        MVI    MSGNR,C'5'
        MVC    MSG,CLR005                DDCARD NOT FOUND
*
WRITE   EQU    *
        CH     R5,=H'2'                          HEAD ?
        BE     WRITEL                            WRITE HEAD
WRITEM  LA     R5,1
        MVC    OUTLINE,ZEILE
WRITEL  SR     R15,R15
        CALL   OPNABLT
        CH     R15,=H'8'
        BE     ENDE                              DDKARTE FEHLT
        CALL   ABLTPUT,(OUTLINE)
        L      R11,=V(ASTLIST)
        CLOSE  ((R11))
        MVI    LDCBSW,DCBCLOSE
        BCT    R5,WRITEM
        B      LOOPE
ENDE    EQU    *
        XRETURN (R15)
        EJECT
* D E F I N I T I O N E N
* ----------------------
CNR      DC    CL6' '                VOL-SER
DW       DC    D'0'
PATAB    DC    V(ATAB)
*
OUTLINE  DS    CL81
*
HEADLINE DC    CL81'0C L E A R T A P'
*
ZEILE    DS    0CL81
         DC    C'0CLEAR00'
MSGNR    DC    C'0'
         DC    C': NACH DDNAME '
CLDDN    DC    CL8' '
         DC    C' - '
MSG      DC    CL47' '
*
CLR000   DS    0CL47
         DC    C'BAND '
CNR0     DS    CL6
         DC    CL36' ERFOLGREICH GELOESCHT'
CLR006   DC    CL47'MEHR ALS 5 VOL-NUMMERN, MIT LETZTER WIEDERHOLEN'
CLR002   DS    0CL47
         DC    C'VOLSER '
CNR2     DS    CL6
         DC    CL34' IST KEIN BAND'
CLR003   DS    0CL47
         DC    C'I/O ERROR AUF '
CNR3     DS    CL6
         DC    CL28',1 BLOCK NICHT GELOESCHT'
CLR004   DS    0CL47
         DC    C'FEHLER BEI RDJFCB FUER BAND '
CNR4     DS    CL6
         DC    CL43' '
CLR005   DC    CL47'DDCARD NOT FOUND ==> CLEAR FAILED'
*
         MATAB
         MKTAB
         END
