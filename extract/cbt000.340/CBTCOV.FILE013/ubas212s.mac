* STAND 04/01/76
***********************************************************************
*                                                                     *
*    PROGRAMM  = PARMMOVE                                             *
*                                                                     *
*    AUFRUF    = CALL PARMMOVE,(ERR,ARGLIST)                          *
*        A       ARGLIST = ADRESSE DER ARGUMENTENLISTE, DIE           *
*                          VOM DIE ABSTIMMUNG AUFRUFENEDN             *
*                          PROGRAMM UEBERGEBEN WIRD                   *
*        F       ERR     = RETURNCODE = 00  KEIN FEHLER               *
*                                      = 88  AUFRUFFEHLER             *
*                                             ABST-PARAMETER-ANZ.FALSCH
*                                             ABST-SUMME ZU GROSS     *
*                                             ABST-SUMME NICHT GEPACKT*
*                                                                     *
*    ZWECK     = ARGUMENTENLISTE DES DIE ABSTIMMUNG AUFRUFENDEN       *
*                PROGRAMMS IN ATAB SPEICHERN,                         *
*                LAENGE UND ANZAHL DER ABSTIMMSUMMEN ERMITTELN        *
*                                                                     *
********$*****$********************************************************
         SPACE 2
PARMMOVE CSECT
         XSAVE 12,SAVE,PARMMOVE
         AREG
RA       EQU   R11                      ATAB
         L     RA,PATAB                 --> ATAB
         USING DATAB,RA
         LR    R10,R1                   --> ARGUMENTENLISTE
         SPACE 2
*              P A R A M E T E R L I S T E  -->  A T A B
         L     R3,4(R10)                --> VON
         L     R3,0(R3)
         LA    R4,PRETURN               --> NACH
         LA    R5,1                     PARAMETERANZAHL = 1
LOOP1    EQU   *
         MVC   0(4,R4),0(R3)            VON --> NACH  PARAMETERLISTE
         TM    0(R3),X'80'              LETZTER PARAMETER
         BO    LOOP1E                   JA
         LA    R3,4(R3)                 --> NAECHSTES VON
         LA    R4,4(R4)                 --> NAECHSTES NACH
         LA    R5,1(R5)                 PARAM-ZAEHLER + 1
         B     LOOP1
LOOP1E   EQU   *
         LA    R2,TABANZ+2              MAX.ERLAUBTE PARAMETERANZAHL
         CR    R5,R2
         BH    FEHLER1                  MEHR ALS MAX. PARAMETER
         CH    R5,=H'3'
         BL    FEHLER1                  WENIGER ALS 3 PARAMETER
         MVI   0(R4),X'00'              KENNUNG LETZTER PARAM.LOESCHEN
         SH    R5,=H'2'
         ST    R5,HPSUMN                ANZAHL SUMTAB = PARM-ANZ - 2
         SPACE 1
         CLI   ASPRACHE,PL1
         BNE   NOPL1
*  AUFRUF VON PL/1-HP
         L     R2,4(R10)                --> PARAMETERLISTE
         L     R2,0(R2)
         L     R2,4(R2)                 STRING-DOPE-VEKOR - ADRESSE
         MVC   PBZEIT,0(R2)             --> BERICHTSZEIT
NOPL1    EQU   *
         SPACE 1
*              L A E N G E  D E R  A B S T I M M S U M M E N  S U C H .
         LA    R3,PHPSUM                --> 1.SUMTAB-ADRESSE
         XR    R1,R1
         XR    R2,R2
LOOP21   EQU   *
         L     R4,0(R3)                 --> 1.ABST.SUM IN SUMTAB
         CLI   ASPRACHE,FORTRAN
         BNE   LOOP212
         LA    R1,4                    LAENGE KONSTANT 4 (1WORT)
         B     LOOP215
LOOP212  EQU   *
         TRT   0(8,R4),TRTTAB           (TESTLAENGE 8 BYTES)
         BZ    FEHLER2                  KEIN VORZEICHEN IN 8 STELLEN
         C     R2,=F'4'
         BE    FEHLER3                  UNGEPACKTE DATEN
         LR    R6,R1                    --> NAECHSTE ABST.SUM IN SUMTAB
         SR    R1,R4
         LA    R1,1(R1)                 LAENGE ABST.SUM
LOOP215  EQU   *
         STH   R1,TABANZ*4+2(R3)        LAENGE --> PHSUME(RECHTES HWORT
         SPACE 1
*              A N Z A H L  D E R  A B S T I M M S U M M E N
*              I N  J E D E R  S U M T A B  S U C H E N
         LA    R4,1                     SUMMENZAEHLER = 1
*  ANZAHL DER GEPACKTEN FELDER GLEICHER LAENGE = ANZAHL ABST.SUM
LOOP22   EQU   *
         CLI   ASPRACHE,FORTRAN
         BNE   LOOP222
         LA    R4,99                   ANZ.FELDER UNBEKANNT --> MAX=99
         B     LOOP22E                 ANZ.FEL
LOOP222  EQU   *
         XR    R2,R2
         TRT   1(8,R6),TRTTAB
         BZ    LOOP22E                  BETRACHTETES FELD  KANN NICHT -
         C     R2,=F'4'
         BE    LOOP22E                  - ABST.SUMME SEIN
         LR    R2,R1                    LAENGE DES GEPACKTEN
         SR    R2,R6                    FELDS BERECHNEN
         CH    R2,TABANZ*4+2(R3)
         BNE   LOOP22E                  FELDLAENGE UNGLEICH BISHER.LAEN
         LR    R6,R1                    --> NAECHSTES ZU PRUEF.FELD
         LA    R4,1(R4)                 ANZAHL D.SUM. + 1
         B     LOOP22
LOOP22E  EQU   *
         STH   R4,TABANZ*4(R3)          ANZAHL --> PHSUME (LINKES HWORT
         LA    R3,4(R3)                 --> NAECHSTE SUMTAB-ADRESSE
         BCT   R5,LOOP21                SCHLEIFE UEBER ANZAHL SUMTAB
         B     ENDE
         SPACE 2
*              F E H L E R B E H A N D L U N G
FEHLER1  EQU   *
         LA    R2,9
         B     FEHLDRU
FEHLER2  EQU   *
         LA    R2,10                    KEIN VORZEICHEN GEFUNDEN
         B     FEHLDRU
FEHLER3  EQU   *
         LA    R2,11                    UNGEPACKTE ABSTIMMSUMME
         SPACE 1
FEHLDRU  EQU   *
         ST    R2,ERR
         CALL  DRUFEHL,(ERR),VL         FEHLERMELDUNG DRUCKEN
         LA    R3,88                    RETURNCODE = 88
         B     RETURN
         SPACE 2
*              A B S C H L U S S
ENDE     EQU   *
         XR    R3,R3                    RETURNCODE = 0
         SPACE 1
RETURN   EQU   *
         L     R2,0(R10)                --> RETURNCODE
         ST    R3,0(R2)                 R3 = RETURNCODE
         XRETURN 0
         SPACE 2
*              D E F I N I T I O N E N
*  TEST-TABELLE
TRTTAB   DC    10X'00'        00 - 09   NUMERISCH
         DC    06X'08'        0A - 0F   VORZEICHEN
         DC    10X'00'        10 - 19   NUM
         DC    06X'08'        1A - 1F   VOR
         DC    10X'00'        20 - 29   NUM
         DC    06X'08'        2A - 2F   VOR
         DC    10X'00'        30 - 39   NUM
         DC    06X'08'        3A - 3F   VOR
         DC    10X'00'        40 - 49   NUM
         DC    06X'08'        4A - 4F   VOR
         DC    10X'00'        50 - 59   NUM
         DC    06X'08'        5A - 5F   VOR
         DC    10X'00'        60 - 69   NUM
         DC    06X'08'        6A - 6F   VOR
         DC    10X'00'        70 - 79   NUM
         DC    06X'08'        7A -7F    VOR
         DC    10X'00'        80 - 89   NUM
         DC    06X'08'        8A - 8F   VOR
         DC    10X'00'        90 - 99   NUM
         DC    06X'08'        9A - 9F   VOR
         DC    96X'04'        A0 - FF   FEHLER
PATAB    DC    V(ATAB)                  --> ATAB
ERR      DS    F                        FEHLERCODE FUER DRUFEHL
         LTORG
         SPACE 2
         MATAB
         END
