//TSSP278  JOB (6233,00000,1278,TSOTEST),STOEHLER,MSGCLASS=V,
//             NOTIFY=TSSP278,CLASS=H,MSGLEVEL=(2,0)
/*JOBPARM LINES=10,TIME=5
/*ROUTE   PRINT LOCAL
//XOPER EXEC TASMFCL,FORMAT=
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.AMODGEN,DISP=SHR
//         DD DSN=SYS1.ATSOMAC,DISP=SHR
//SYSPRINT DD SYSOUT=V
XOPER    TITLE 'EXTENDED OPER COMMAND FOR TSO'
***********************************************************************
*                                                                     *
*              P R O L O G                                            *
*                                                                     *
***********************************************************************
*                                                                     *
*              PROGRAMMER              STOEHLER / DATEV EG. NUERNBERG *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*              DESCRIPTION                                            *
*                                                                     *
*              THIS PROGRAM WILL DO CONSOLE SIMULATION FOR A TSO      *
*              TERMINAL. IT GOES OUT AND FINDS A GRAPHICS (3277)      *
*              ACTIVE CONSOLE AND USES THAT CONSOLE SCREEN IMAGE      *
*              FOR OUTPUT AND THEIR UCM-ID FOR SVC 34.                *
*              THIS PROGRAM REQUIRES AUTHORIZATION TO USE THE SVC 34  *
*              INTERFACE.                                             *
*              TO USE IT SIMPLY SAY   ''XOPER <CUU>''                 *
*              A NULL INPUT CAUSES THE SCREEN TO BE UPDATED. (SO YOU  *
*              CAN USE IT UN-AUTHORIZED SO LONG AS YOU DON'T TYPY     *
*              ANYTHING IN.)                                          *
*              COMMANDS ARE BROADCAST TO THE CONSOLE BEING USED.      *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*              PARAMETER               ADDRESS OF A SPECIFIC CONSOLE  *
*                                      CUU (NOT REQUIRED)             *
*                                                                     *
*              I/O                     ONLY TGET/TPUT                 *
*                                                                     *
*              EXIT                    NORMAL RETURN (AFTER YOU TYPE  *
*                                      'END')                         *
*                                                                     *
*              ERROR MESSAGES          NO ACCEPTABLE (3277) CONSOLES  *
*                                      FOUND                          *
*                                                                     *
***********************************************************************
XOPER    CSECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING XOPER,R12
         LA    R2,SAVEAREA
         ST    R2,8(R13)
         ST    R13,4(R2)
         LR    R13,R2
         L     R3,0(R1)            POINT TO CPPL
         LH    R4,0(R3)            LENGTH
         SH    R4,2(R3)            OFFSET
         CH    R4,=H'5'
         BL    GOON1               NO PARM GIVEN
         LH    R4,2(R3)            LOAD OFFSET
         LA    R4,4(R4,R3)         POINT TO CCU
         MVC   UNITXXX(3),0(R4)    SAVE IT
         OC    UNITXXX(3),=CL3' '
         CLC   UNITXXX(3),=CL3' '
         BE    GOON1
         NI    CHKUNIT+1,15        NOP
GOON1    EQU   *
         L     R1,16               CVT
         L     R1,100(R1)          UCM
         USING UCM,R1
         LM    R3,R5,UCMVEA        R3 -> TO THE FIRST UCME
*                                  R4 CONTAINS UCME LENGTH
*                                  R5 -> TO THE LAST UCME
DID0     EQU   *                   SEARCH FOR SOMETHING USEFUL
         USING UCMLIST,R3
         L     R1,UCMUCB           DEBUGGING
CHKUNIT  B     DID00               NOP IF SPECIFIC REQUEST
         CLC   UNITXXX(3),13(R1)   IS IT THE REQUESTED CONSOLE ?
         BNE   DID1                NO - SEARCH FOR NEXT
DID00    EQU   *
         MVC   UNITXXX(3),13(R1)   MOVE UNITADDRESS
         TM    UCMATR,UCMUF        IS IT ACTIVE ?
         BZ    DID1                NO - UNUSEABLE
         L     R10,UCMXB           IS IT GRAPHICS ?
         LTR   R10,R10
         BZ    DID1                NO - USELESS
         L     R6,0(R10)           FIND  THE PAGEABLE DCM (TDCM)
         LTR   R6,R6               DOES IT EXIST ?
         BZ    DID1                NO - UNUSEABLE
         CLI   X'131'(R6),X'10'    IS IS A 3277 ?
         MVC   XUCMID,UCMID        SAVE THIS
         BE    GOTONE0             OKAY GO ON
DID1     EQU   *
         BXLE  R3,R4,DID0          GET THE NEXT ENTRY
         TPUT  ERMSG,L'ERMSG       NO ACCEPTABLE CONSOLES ACTIVE
         B     END2                FINISH
         SPACE 3
*
* A CONSOLE HAS BEEN FOUND COPY THE SCREEN AND DISPLAY IT FOR THE USER
*        R3 ->  UCME
*        R10 -> RDCM
*        R6 ->  TDCM
*
GOTONE0  EQU   *                   CLEAR SCREEN
         STFSMODE ON,INITIAL=YES
GOTONE1  EQU   *                   CLEAR SCREEN
         MVC   CMDBUF,BLANKS
         LA    R7,24               NUMBER OF LINES
         LA    R8,SCREEN
GOTONE2  MVC   0(2,R8),=X'1D60'    NORMAL INTENSITY
         MVC   2(79,R8),BLANKS     CLEAR
         LA    R8,81(R8)           NEXT LINE
         BCT   R7,GOTONE2          REPEAT
         SH    R8,=H'81'           LAST LINE (MODE)
         MVC   0(2,R8),=X'1DE8'    HIGH INTENSITY
         MVC   ENTRLNE(4),=X'401DC113' CURSOR POSITION
GOTONE   EQU   *
         LH    R1,X'FE'(R6)        NUMBER  OF LINES IN MSG AREA
         L     R7,X'30'(R6)        POINT TO  THE  FIRST INPUT LINE
         LA    R8,SCREEN           OUTPUT LINE
GOT1     EQU   *
         MVC   5(74,R8),4(R7)      MOVE A LINE
         TR    5(74,R8),BLANKTAB   GET IT READABLE
         LA    R8,81(,R8)          OUR SIZE
         LA    R7,84(,R7)          THEIRS
         BCT   R1,GOT1             GET EM ALL
         L     R1,X'3C'(R6)
         LA    R8,81(,R8)          SKIP PFK LINE
         MVC   0(2,R8),=X'1D60'
         MVC   3(L'ENTRMSG,R8),ENTRMSG
         LA    R8,(3*81)(,R8)      SKIP INPUT LINE
         MVC   4(MODELEN,R8),MODEMSG
         BAL   R9,WRITE            TPUT AND TGET
CHECKCMD CLC   CMNDBUF,BLANKS      NULL INPUT ?
         BE    GOTONE1
         CLC   CMNDBUF(4),=C'END ' REQUEST TO TERMINATE THE COMMAND
         BE    END1
         LA    R1,CMND             DOCUMENT THE COMMAND
         XR    R0,R0               ON THE SELECTED CONSOLE
         IC    R0,XUCMID
         SVC   35
         LA    R1,CMND             SUBMIT THE COMMAND
         XR    R0,R0
         IC    R0,XUCMID           OUR MAN IN THE COMM TASK
         LA    R15,SVCROUT
         SVC   250                 THIS SVC IS ONLY A BR 15
         B     CHECKRC
SVCROUT  EQU   *
         SVC   34                  FIRE HIM THRU
         BR    R14
CHECKRC  EQU   *
         LR    R2,R15              SAVE RETURN CODE
         LTR   R15,R2              RC = 0 ?
         BNZ   RCN0                YES
         STIMER WAIT,DINTVL==CL8'00000200'
         B     GOTONE1             YES
RCN0     MVC   ENTRLNE-81(LERRORMS),ERRORMSG
         MVC   ENTRLNE+4(77),CMNDBUF
         MVC   CMNDBUF,BLANKS
         BAL   R9,WRITE            TPUT AND TGET
         MVI   ENTRLNE+4,C' '      CLEAR ENTRLINE
         MVC   ENTRLNE+5(76),ENTRLNE
         B     CHECKCMD            TRY THE CORRECTED COMMAND
END1     EQU   *
         LA    0,(15*256)+3         LOAD BUFFER-LENGTH & TPUT/FLAG
         LA    1,=X'C1115D7E1140403C40400011404013' LOAD BUFFER-ADDR
         SLL   1,8                      SHIFT BUFFER-LENGTH, FLAGS
         SRDL  0,8                      AND BUFFER-ADDRESS
         SVC   93                       TPUT
         STFSMODE OFF                   TPUT
END2     L     R13,4(R13)
         RETURN (14,12),RC=0       RETURN TO XC COMMAND
         SPACE 2
WRITE    EQU   *
         TPUT  CLEAR,LSCREEN,FULLSCR
         TGET  CMDBUFP,83,ASIS
         MVC   CMNDBUF,CMDBUF
         OC    CMNDBUF,BLANKS
         BR    R9
         TITLE 'X O P E R  -  DATA AREAS'
         LTORG
SAVEAREA DC    18F'0'
ERMSG    DC    C'NO ACCEPTABLE (3277) CONSOLES FOUND'
MODEMSG  DC    C'IEE163I MODE= TSO ('
UNITXXX  DC    CL5'   ) '
MODELEN  EQU   *-MODEMSG
ENTRMSG  DC    C'IEE152I     ENTER     CANCEL     D C,K         '
ERRORMSG DC    X'1DE840'
         DC    C'INVALID COMMAND (RC FROM SVC 34 ^= 0)          '
LERRORMS EQU   *-ERRORMSG
CMND     DC    H'84',X'4000'
CMNDBUF  DC    CL80' '
CMDBUFP  DC    CL6' '
CMDBUF   DC    CL80' '
         DC    F'0'
XUCMID   DC    X'0'
BLANKS   DC    CL80' '
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'Ö'
         DC    C'Ö.<(+×',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);^-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+X'81'      TRANSLATE LOWER CASE TO UPPER
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+X'91'      ...
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+X'A2'      ...
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
CLEAR    DC    X'C71140403C404000114040'
SCREEN   DC    24CL81' '
ENTRLNE  EQU   SCREEN+(81*21)
LSCREEN  EQU   *-CLEAR
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT NOGEN
*        THIS MACRO IS FROM SYS1.AMODGEN
         IEECUCM FORMAT=NEW
         END
//LKED.SYSLMOD DD DSN=SYS1.TSOLOAD(XOPER),DISP=SHR
