         TITLE 'SPU8301  - SMF-RECORD-SELECTION-EXIT'
IEFU83   CSECT
         USING *,15
         B     20(15)
         DC    C'IEFU83 - FEB. 77'
         STM   14,12,12(13)            SAVE
         XC    16(4,13),16(13)         ZERO REG 15 FOR RETURN
         L     3,0(1)                  A(SMF-RECORD) RDW
         CLI   5(3),0                  IPL-RECORD ?
         BE    CREATE                  YES
         L     1,16                    CVT-PTR
         L     2,X'C4'(1)              SMCA-ADDRESS
         L     4,X'80'(2)              SMCA-USER-FIELD
         LTR   4,4                     NO USER-DATA
         BZ    RETURN                  AVAILABLE
         L     4,0(4)                  A(BIT-TABLE)
         LTR   14,4                    NO BIT-TABLE
         BZ    RETURN                  AVAILABLE
         LM    6,8,32(14)              COUNTER FIELDS (ALL)
         LA    6,1(6)                  RECORD COUNTER + 1
         AH    7,0(3)                  RECORD LENGTH
         STM   6,7,32(14)              UPDATE
         SR    6,6                     ZERO
         SR    7,7                     ZERO
         IC    7,36(14)                BYTE COUNTER > 16.777.216
         STC   6,36(14)                CLEAR HIGH ORDER BYTE
         AR    8,7                     COUNTER PER 16'' BYTES
         ST    8,40(14)                UPDATE
         IC    6,5(3)                  SMF-RECORD TYPE
         SRDL  6,3                     DIVIDE BY 8 AND SAVE REMAINDER
         LA    4,0(6,4)                PICK UP BYTE IN BIT-TABLE
         SR    6,6                     ZERO
         SLDL  6,3                     RESTORE REMAINDER
         LA    6,1(6)                  + 1 BECAUSE BCT-LOOP
         LA    7,256                   = X'100' = B'100000000'
BCTLOOP  EQU   *
         SRL   7,1                     SHIFT RIGHT BY ONE
         BCT   6,BCTLOOP               UNTIL REG 6 = ZERO
         EX    7,TESTREC               TEST BIT-TABLE
         BO    RETURN                  RECORD IS TO BE WRITTEN
         LM    6,8,44(14)              COUNTER FIELDS (NOT WRITTEN)
         LA    6,1(6)                  RECORD COUNTER + 1
         AH    7,0(3)                  RECORD LENGTH
         STM   6,7,44(14)              UPDATE
         SR    6,6                     ZERO
         SR    7,7                     ZERO
         IC    7,48(14)                BYTE COUNTER > 16.777.216
         STC   6,48(14)                CLEAR HIGH ORDER BYTE
         AR    8,7                     COUNTER PER 16'' BYTES
         ST    8,52(14)                UPDATE
         MVI   19(13),4                INDICATES RECORD IS NOT TO BE
*                                      WRITTEN
RETURN   EQU   *
         LM    14,12,12(13)            RESTORE
         BR    14                      RETURN
*
TESTREC  TM    0(4),0                  TEST BIT-TABLE VIA EX-INSTR.
*
         EJECT
CREATE   EQU   *                       ENTRY FOR IPL-HANDLING
         DROP  15
         BALR  12,0                    BASE-REG
         USING *,12
         GETMAIN R,LV=WORKLEN,SP=245   WORKING-STORAGE
         XC    0(256,1),0(1)
         XC    256(WORKLEN-256,1),256(1)
         ST    13,4(1)                 HSA
         ST    1,8(13)                 LSA
         LR    13,1                    NEW SAVE-AREA
         USING WORK,13
         LOAD  EP=SPU8302              DEFAULT-MAP
         LR    5,0                     SAVE MAP/ADDRESS
         BAL   14,TODCHECK             CHECK DATE
         BAL   14,WOCHTAG
         BAL   14,CHECKSID             SMF-SID-PARAMETER
         L     2,16                    CVT-PTR
         L     2,X'C4'(2)              SMCA-ADDRESS
         GETMAIN R,LV=32,SP=245
         ST    1,X'80'(2)              SMCA-USER-FIELD
         LR    4,1                     SAVE A(USER-DATA)
         XC    0(32,4),0(4)            CLEAR
GETBITM  GETMAIN R,LV=64,SP=245
         ST    1,0(4)                  SAVE A(BITMAP)
         LR    4,1                     PICK UP
         XC    0(64,4),0(4)            CLEAR
         TIME  DEC
         STM   0,1,56(4)               TIME STAMP
         SR    3,3                     ZERO
         IC    3,0(5)                  NUMBER OF DEFAULT-RECORDS
         LA    5,1(5)                  FIRST DEFAULT-RECORD
         BAL   14,ONMAP                SET BITMAP ON
         DELETE EP=SPU8302
ASKOPER  EQU   *
         BAL   14,WTOST                STATE OF BITMAP
REPEAT   WTO   '   ',ROUTCDE=2
       WTO '   SMF004A SPECIFY SMF-RECORD-TYPES TO BE WRITTEN OR NOT', C
               ROUTCDE=2
         XC    WREPLY,WREPLY           CLEAR
         LA    3,WREPLY                REPLY-AREA
         LA    5,WECB                  ECB-ADDRESS
         WTOR  'SMF004A BY A=(R1,R2,..RN),D=(R1,R2,..RN) OR REPLY U',  C
               (3),80,(5),ROUTCDE=2
         XC    WECB,WECB
         WAIT  ECB=WECB
         BAL   14,CHCKREPL             CHECK REPLY
         SR    3,3                     ZERO
         IC    3,WONMAP                NUMBER OF ADD-RECORDS
         L     5,WONMAP                FIRST ADD-RECORD
         BAL   14,ONMAP                SET BITMAP ON
         SR    3,3                     ZERO
         IC    3,WOFFMAP               NUMBER OF DEL-RECORDS
         L     5,WOFFMAP               FIRST DEL-RECORD
         BAL   14,OFFMAP               SET BITMAP OFF
         TM    WSWITCH,128             IS REPLY = U ?
         BZ    ASKOPER                 NEIN
         WTO   '   ',ROUTCDE=2
         LR    1,13                    SAVE A(WORK)
         L     13,4(13)                HSA
         TM    0(4),128                IS TYPE 0 RECORD TO BE WRITTEN ?
         BO    RETURN1                 YES
         MVI   19(13),4                NO
RETURN1  FREEMAIN R,LV=WORKLEN,A=(1),SP=245
         LM    14,12,12(13)            RESTORE
         BR    14                      RETURN
         EJECT
TODCHECK EQU   *
         SR    4,4
         IC    4,0(5)
         LA    4,1(4,5)
         CLC   0(4,4),10(3)
         BH    ERRDATE
         CLC   4(4,4),10(3)
         BHR   14
ERRDATE  EQU   *
         WTO   'SMF003E IPL-DATE INVALID - RE-IPL',DESC=2,ROUTCDE=2
         STIMER WAIT,BINTVL=TENSEC
         MODESET MODE=SUP
         LPSW  HARDWAIT
         EJECT
WOCHTAG  EQU   *
         MVC   WDATUM,10(3)            00YYDDDF
         CP    WDATUM+2(2),=P'0'
         BNH   ERRDATE
         STM   2,6,WRSAVE              SICHERN WORK-REGISTER
         MVO   WDWD,WDATUM(2)
         OI    WDWD+7,15
         CVB   2,WDWD
         STC   2,WJAHRHX
         LA    3,TABMONN               KEIN SCHALTJAHR
         TM    WJAHRHX,3
         BNZ   WOCH1
         LA    3,TABMONS               SCHALTJAHR
         OI    WDATUM,1
WOCH1    EQU   *
         LR    4,3                     MONAT ERMITTELN
         LA    5,12                    WG/ BCT
WOCH2    EQU   *
         CP    WDATUM+2(2),0(2,4)
         BNH   WOCH3
         LA    4,2(4)
         BCT   5,WOCH2
         B     ERRDATE                 FORMAL-FEHLER (TAG > 365)
WOCH3    EQU   *
         LR    6,4                     A(INNERHALB TABMONN/S)
         SR    4,3
         SRA   4,1                     / 2
         LR    5,4
         MH    5,=H'44'                DISPL. IN MONATS-TABELLE
         LA    2,TBMONAT
         LA    2,35(2,5)
         ST    2,WADMON                ADR(MONATS-NAME, 9STELLIG)
         LTR   4,4
         BZ    WOCH4                   JANUAR (KEIN ABZUG)
         BCTR  6,0                     A(INNERHALB TABMONN/S)
         BCTR  6,0                     - 2
         SP    WDATUM+2(2),0(2,6)      VORABZUG
WOCH4    EQU   *
         CH    4,=H'1'                 JANUAR ODER FEBRUAR ?
         BH    WOCH5                   NEIN
         TM    WDATUM,1                SCHALTJAHR ?
         BZ    WOCH5                   NEIN
         LA    4,6(4)                  DISPL. ERHOEHEN
WOCH5    EQU   *
         MH    4,=H'44'                DISPL. IN TBMONAT
         LA    3,TBMONAT(4)            MONAT EINSTELLEN
         ZAP   WDWD,WDATUM+2(2)        CONVERT TAG
         CVB   2,WDWD
         BCTR  2,0                     - 1 - DISPL. INNERHALB DES
         LA    3,0(2,3)                       MONATS IN TBMONAT
         ZAP   WDWD,0(1,3)             TAGES-KENNZAHL
         SR    2,2
         IC    2,WJAHRHX
         SH    2,=H'76'                BASISJAHR = 1976
         LA    3,TABJAHR(2)
         AP    WDWD,0(1,3)             JAHRES-KENNZAHL
         CP    WDWD,=P'7'
         BNH   WOCH6
         SP    WDWD,=P'7'
WOCH6    EQU   *
         CVB   2,WDWD                  DISPL. IN TABTAG
         BCTR  2,0                     -1
         MH    2,=H'10'                L'ELEMENT
         LA    3,TABTAG(2)
         ST    3,WADTAG                A(TAGES-BEZEICHNUNG, 10STELLIG)
         SPACE 2
* AUFBEREITEN DER WTO'S
         WTO   '   ',ROUTCDE=2
         L     2,WADTAG
         LA    3,10
WOCH7    CLI   0(2),64
         BE    WOCH8
         LA    2,1(2)
         BCT   3,WOCH7
WOCH8    LA    4,10
         SR    4,3                     L'WOCHENTAG
         L     2,WADMON
         LA    3,9
WOCH9    CLI   0(2),64
         BE    WOCH10
         LA    2,1(2)
         BCT   3,WOCH9
WOCH10   LA    5,9
         SR    5,3                     L'MONAT
         LR    3,4
         AR    3,5
         LA    3,27(3)                 L'WTO 23+4
         MVI   WJAHRHX,0
WOCH11   MVI   WWTO+4,C'*'
         MVC   WWTO+5(50),WWTO+4
         STH   3,WWTO
         MVC   WWTO+2(2),=H'0'
         OI    WWTO+2,128
         LA    2,WWTO(3)
         MVC   0(4,2),=X'40004000'     DESC-CODE
         WTO   MF=(E,WWTO)
         CLI   WJAHRHX,255
         BE    WOCH12                  2. *-WTO
         MVI   WJAHRHX,255
         MVC   WWTO+4(4),=C'***'
         L     2,WADTAG
         MVC   WWTO+8(10),0(2)         WOCHENTAG
         LA    2,WWTO+8(4)
         MVC   0(6,2),=C', DER '
         UNPK  6(2,2),WDATUM+2(2)
         OI    7(2),240
         MVC   8(2,2),=C'. '
         L     6,WADMON
         MVC   10(9,2),0(6)            MONAT
         LA    2,10(5,2)
         MVC   0(3,2),=C' 19'
         MVO   WDWD+2(6),WDATUM+1(1)
         OI    WDWD+7,15
         UNPK  WDWD(2),WDWD+2(6)
         MVC   3(2,2),WDWD
         MVC   5(4,2),=C' ***'
         WTO   MF=(E,WWTO)
         B     WOCH11                  *-WTO WIEDERHOLEN
WOCH12   EQU   *
         LM    2,6,WRSAVE
         BR    14
         EJECT
CHECKSID EQU   *
         SR    4,4
         IC    4,0(5)
         LA    4,1(4,5)
         LA    4,8(4)
         MODESET MODE=SUP
         STIDP WCPUID
         MODESET MODE=PROB
CSID1    CLC   WCPUID+1(3),0(4)
         BE    CSID2
         CLC   0(3,4),=3X'FF'
         BE    CSID3
         LA    4,7(4)
         B     CSID1
CSID2    CLC   3(4,4),14(3)            TYPE 0 RECORD
         BER   14
         MVC   WWTO,WTO1
         MVC   WWTO+35(4),14(3)
         MVC   14(4,3),3(4)
         L     6,16                    CVT
         L     6,196(6)                SMCA
         MVC   16(4,6),3(4)
         MVC   WWTO+43(4),3(4)
         B     CSID4
CSID3    EQU   *
         MVC   WWTO,WTO2
         MVC   WWTO+39(4),14(3)
         UNPK  WWTO+53(7),WCPUID+1(4)
         MVI   WWTO+59,C','
CSID4    EQU   *
         WTO   '   ',ROUTCDE=2
         WTO   MF=(E,WWTO)
         BR    14
         EJECT
ONMAP    EQU   *
*              R3 = NUMBER OF RECORD TYPES
*              R4 = A(BITTABLE)
*              R5 = 1. RECORD TYPE
         LTR   R3,R3                   NO RECORD-TYPES ?
         BZR   R14                     YES -> RETURN
         SR    R6,R6                   ZERO
         SR    R7,R7                   ZERO
         IC    R6,0(R5)                SMF RECORD TYPE
         SRDL  R6,3                    DIVIDE BY 8 AND SAVE REMAINDER
         LA    R8,0(R6,R4)             PICKUP BYTE IN BITTABLE
         SR    R6,R6                   ZERO
         SLDL  R6,3                    RESTORE REMAINDER
         LA    R6,1(R6)                + 1 BECAUSE BCT-LOOP
         LA    R7,256                  = X'100' = B'100000000'
ONMAP1   EQU   *                       PICKUP BIT IN BYTE IN BITTABLE
         SRL   R7,1                    = X'80' UNTIL X'01'
         BCT   R6,ONMAP1               PICKUP
         TM    WSWITCH,64              CALL FROM OFFMAP ?
         BO    ONMAP2                  YES
         EX    R7,OI                   EXECUTE OI
         B     ONMAP3
ONMAP2   EQU   *
         LA    R9,255
         SR    R9,R7
         EX    R9,NI                   EXECUTE NI
ONMAP3   EQU   *
         LA    R5,1(R5)                NEXT RECORD TYPE
         BCT   R3,ONMAP                LOOP
         BR    14
*
OI       OI    0(R8),0                 EX INSTRUCTIONS
NI       NI    0(R8),0
*
         EJECT
WTOST    EQU   *
*              R4 = A(BITTABLE)
         WTO   '   ',ROUTCDE=2
         WTO   '   SMF006I FOLLOWING SMF RECORDS WILL BE WRITTEN:',    C
               ROUTCDE=2
         MVC   WWTO(4),=X'00448000'
         MVC   WWTO+4(11),=C'   SMF006I '
         MVC   WWTO+15(53),WWTO+14
         MVC   WWTO+68(4),=X'00004000'
         LA    R8,WWTO+15
         SR    R9,R9
         SR    R5,R5                   COUNTER
         LR    R7,R4                   A(BITTABLE)
WTOST1   LA    R6,128                  =X'80'
WTOST2   EX    R6,TM                   SMF RECORD TO BE WRITTEN ?
         BO    WTOST3                  YES
WTOST2A  LA    R5,1(R5)                COUNTER + 1
         CH    R5,=H'255'              LAST RECORD TYPE REACHED ?
         BH    WTOST4                  YES
         SRA   R6,1                    NEXT BIT IN BYTE IN BITTABLE
         BNZ   WTOST2                  NOT LAST BIT
         LA    R7,1(R7)                NEXT BYTE IN BITTABLE
         B     WTOST1                  LOOP
WTOST3   EQU   *
         CVD   R5,WDWD                 CURRENT SMF RECORD TYPE
         OI    WDWD+7,15               FOR UNPK
         SR    R1,R1                   ZERO
         CLI   WDWD+6,0                ONE-DIGIT NUMBER ?
         BE    WTOST3A                 YES
         LA    R1,1                    LENGTH FOR UNPK
         TM    WDWD+6,240              THREE-DIGIT NUMBER ?
         BZ    WTOST3A                 NO
         LA    R1,1(R1)                LENGTH FOR UNPK
WTOST3A  SLL   R1,4                    FIRST OPERAND LENGTH
         EX    R1,UNPK
         SRL   R1,4                    RESET R1
         OI    WSWITCH,16              INDICATES WTO BUFFER ISN'T EMPTY
         LA    R8,2(R1,R8)             NEXT LOCATION IN WTO BUFFER
         LA    R9,2(R1,R9)             COUNT FOR WTO BUFFER
         CH    R9,=H'40'               BUFFER OVERFLOW ?
         BL    WTOST2A                 NO
WTOST4   EQU   *
         TM    WSWITCH,16              WTO BUFFER EMPTY ?
         BZR   R14                     YES
         WTO   MF=(E,WWTO)
         MVC   WWTO+15(53),WWTO+14
         LA    R8,WWTO+15
         SR    R9,R9
         NI    WSWITCH,255-16          WTO BUFFER IS EMPTY
         CH    R5,=H'255'              LAST SMF RECORD TYPE ?
         BNLR  R14                     YES
         B     WTOST2A
*
TM       TM    0(R7),0                 EX-INSTRUCTIONS
UNPK     UNPK  0(0,R8),WDWD
*
         EJECT
CHCKREPL EQU   *
         NI    WSWITCH,255-32
         LA    R6,WRECTYP              CLEAR
         ST    R6,WONMAP               POINTERS
         LA    R6,20(R6)
         ST    R6,WOFFMAP
         CLI   0(R3),C'U'              REPLY=U ?
         BNE   CHCK1                   NO
         OI    WSWITCH,128             INDICATES 'U'
         BR    R14
CHCK1    EQU   *
         CLC   1(2,R3),=C'=('          SYNTAX-CHECKING
         BNE   WRONG
         CLI   0(R3),C'A'              ADD SMF RECORD TYPES ?
         BE    CHCKA                   YES
         CLI   0(R3),C'D'              DELETE SMF RECORD TYPES ?
         BE    CHCKD
WRONG    WTO   '   SMF005E INVALID REPLY - REENTER',ROUTCDE=2
         B     REPEAT
CHCKA    OI    WSWITCH,32              INDICATES 'ADD'
CHCKD    LA    R6,3(R3)                A(FIRST NUMBER)
CHCK2    TRT   0(4,R6),TRTAB           SEARCH ',' OR ')' IN STRING
         BE    WRONG                   NOT FOUND
         LR    R7,R1                   SAVE R1
         SR    R1,R6                   = L'NUMBER
         BNP   WRONG                   NO VALID NUMBER GIVEN
         BCTR  R1,0                    MINUS 1 FOR EX
         EX    R1,PACK                 PACK IN DOUBLEWORD
         CH    R1,=H'255'              VALID SMF RECORD TYPE ?
         BH    WRONG                   NO
         CVB   R1,WDWD                 CONVERT
         LA    R8,WONMAP
         TM    WSWITCH,32              'ADD' ?
         BO    *+8                     YES
         LA    R8,WOFFMAP              'DELETE'
         SR    R9,R9                   ZERO
         IC    R9,0(R8)                COUNTER (OFFSET)
         L     R15,0(R8)               START ADDRESS
         LA    R15,0(R15)
         LA    R10,0(R9,R15)           + OFFSET
         STC   R1,0(R10)               SMF RECORD TYPE
         LA    R9,1(R9)                INCREASE COUNTER
         STC   R9,0(R8)                RESTORE
         LA    R6,1(R7)                DELIMITER IN REPLY BUFFER
         CLI   0(R7),C','
         BE    CHCK2                   NEXT NUMBER
         CLI   0(R6),X'00'             LAST CHARACTER ?
         BER   R14                     YES -> RETURN
         CLI   0(R6),C' '
         BER   R14
         CLI   0(R6),C','              = KEYWORD FOLLOWS
         BNE   WRONG
         LA    R3,1(R6)                NEXT KEYWORD (A= OR D=)
         NI    WSWITCH,255-32
         B     CHCK1
*
PACK     PACK  WDWD,0(0,R6)
*
         EJECT
OFFMAP   EQU   *
         ST    R14,WRSAVE
         OI    WSWITCH,64              INDICATES NI IS TO PERFORM
         BAL   R14,ONMAP
         NI    WSWITCH,255-64          RESTORE SWITCH
         L     R14,WRSAVE
         BR    14
         EJECT
WTO1     WTO   'SMF001I SYSTEM-ID CHANGED FROM .... TO ....',          C
               MF=L,ROUTCDE=2
WTO2     WTO   'SMF002W UNABLE TO CHECK SYSTEM-ID (....), CPU-ID=......C
               , NO ALTERATION DONE',                                  C
               MF=L,DESC=2,ROUTCDE=2
HARDWAIT DS    0D
         DC    X'000A00000000004A'
TENSEC   DC    F'1000'
TRTAB    DS    0F
         DC    256X'0'
         ORG   TRTAB+C','
         DC    X'01'
         ORG   TRTAB+C')'
         DC    X'02'
         ORG
TBMONAT  DS    0F
         DC    5P'6,7,1,2,3,4,5',CL9'JANUAR'
         DC    5P'2,3,4,5,6,7,1',CL9'FEBRUAR'
         DC    5P'2,3,4,5,6,7,1',CL9'MAERZ'
         DC    5P'5,6,7,1,2,3,4',CL9'APRIL'
         DC    5P'7,1,2,3,4,5,6',CL9'MAI'
         DC    5P'3,4,5,6,7,1,2',CL9'JUNI'
         DC    5P'5,6,7,1,2,3,4',CL9'JULI'      (JANUAR)
         DC    5P'1,2,3,4,5,6,7',CL9'AUGUST'    (FEBRUAR)
         DC    5P'4,5,6,7,1,2,3',CL9'SEPTEMBER'
         DC    5P'6,7,1,2,3,4,5',CL9'OKTOBER'
         DC    5P'2,3,4,5,6,7,1',CL9'NOVEMBER'
         DC    5P'4,5,6,7,1,2,3',CL9'DEZEMBER'
TABJAHR  DS    0F
         DC    P'7,1,2,3,5,6,7,1,3,4,5,6,1,2,3,4,6,7,1,2,4,5,6,7'
TABTAG   DC    CL10'SONNTAG',CL10'MONTAG',CL10'DIENSTAG',CL10'MITTWOCH'
         DC    CL10'DONNERSTAG',CL10'FREITAG',CL10'SAMSTAG'
TABMONN  DC    P'31,59,90,120,151,181,212,243,273,304,334,365'
TABMONS  DC    P'31,60,91,121,152,182,213,244,274,305,335,366'
         SPACE 3
         LTORG
PATCH    DC    XL30'0'
         EJECT
         EQUATE
WORK     DSECT
WSAVE    DS    18F
WECB     DS    F
WREPLY   DS    CL80
WRECTYP  DS    CL40
WONMAP   DS    F
WOFFMAP  DS    F
WSWITCH  DS    X
WCPUID   DS    D
WDWD     DS    D
WDATUM   DS    F
WADMON   DS    A
WADTAG   DS    A
WJAHRHX  DS    X
WRSAVE   DS    5F
WWTO     DS    CL100
WORKLEN  EQU   *-WORK
         END
