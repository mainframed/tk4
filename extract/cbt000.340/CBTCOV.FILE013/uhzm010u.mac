IS050    START
*        KOPIEREN QUELLENPROGRAMME
*        MIT AUFLOESUNG DER COPY-STATEMENTS
*
*        PARAMETER:
*          P  PL1
*          C  COBOL
*          A  ASSEMBLER (KOPIEREN OHNE AUFLOESEN)
*
*        DATEIEN:
*
*        PGMEIN  TEST-SOURCEBIBLIOTHEK
*        PGMAUS  ANWENDUNGS-SOURCEBIBLIOTHEK
*        PGMCOPY  COPY-BIBLIOTHEK
*        SYSOUT  MELDUNGEN
*
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R10      EQU   10
R11      EQU   11
R12      EQU   12
R15      EQU   15
C000     BROUT
         MVC   APARM,0(R1)
         MVI   APARM,X'00'
         L     R7,APARM
         MVC   PKZ(1),2(R7)   UEBERTRAGEN PARAMETER
         OPEN  (DCBPGME,,DCBPGMC)
         OPEN  (DCBPGMA,(OUTPUT),DCBOUT,(OUTPUT))
         MVC   COPYNAME,FLD8B
         MVI   MESSAGE,C'1'
         BAL   R3,MESSROUT
         MVI   MESSAGE,C' '
         B     C010
MESSROUT EQU   *
         WRITE DECBME,SF,DCBOUT,MESSAGE
         CHECK DECBME
         BR    R3
EODATPR  EQU   *
         MVC   MESSAGE+1(030),=C'PROGRAMM KOPIERT              '
         BAL   R3,MESSROUT
         B     C900
EODATCO  EQU   *
         CLI   PKZ,C'C'
         BNE   E2
         PUT   DCBPGMA,TXTCOEND
         B     E6
E2       CLI   PKZ,C'P'
         BNE   E6
         PUT   DCBPGMA,TXTPLEND
E6       MVC   MESSAGE+1(012),=C'COPY-MEMBER '
         MVC   MESSAGE+13(8),COPYNAME
         MVC   MESSAGE+21(10),=C' KOPIERT  '
         MVC   COPYNAME,FLD8B
         BAL   R3,MESSROUT
*        LESEN PROGRAMMANWEISUNG
C010     GET DCBPGME,PGMEIN
*        PRUEFEN PROGRAMMKENNZEICHEN
         CLI   PKZ,C'C'            COBOL ?
         BE    C020C
         CLI   PKZ,C'P'            PL/I ?
         BE    C020P
         CLI   PKZ,C'A'            ASS.?
         BE    C040
         MVC   MESSAGE+1(030),=C'PARAM.KEIN PGM-KENNZEICHEN    '
         BAL   R3,MESSROUT         ENDE ZWEIG
         MVC   RC,=F'04'
         B     C900
*        COPY-ANWEISUNG COBOL
C020C    EQU   *
         LA    R4,PGMEIN
         LA    R5,30(0)
         LA    R6,COPYNAME
         LA    R7,PGMEINC1
C022C    CLC   10(4,R4),=C'COPY'
         BE    C023C
         LA    R4,1(R4)
         LA    R7,1(R7)
         BCT   R5,C022C
         B     C040                SCHREIBEN PROG.ANWEISUNG
C023C    CLI   PGMEIN+6,C'*'
         BE    C032C
         MVC   PGMEINC1,PGMEIN
         MVC   10(40,R7),PUNKT
         MVI   COBOL1,C'1'
         LA    R4,5(R4)
C024C    CLI   10(R4),C' '
         BNE   C026C
         BCT   R5,C024C
         B     C040
C026C    MVC   0(1,R6),10(R4)      UEBERTRAGEN ZEICHEN
         LA    R4,1(R4)
         LA    R6,1(R6)
         CLI   10(R4),C' '
         BE    C030C
         CLI   10(R4),C'.'
         BE    C030C
         B     C026C
C030C    MVI   PGMEIN+6,C'*'    COPY-ANW.ALS KOMMENTAR
         B     C060
C032C    MVC   MESSAGE+1(30),=C'ALTES COPY-MEMBER GEFUNDEN    '
         BAL   R3,MESSROUT
         MVC   RC,=F'04'
         B     C040
*         INCLUDE-ANWEISUNG PL/I
C020P    EQU   *
         LA    R4,PGMEIN
         LA    R5,40(0)
         LA    R6,COPYNAME
C022P    CLC   1(7,R4),=C'INCLUDE'
         BE    C023P
         LA    R4,1(R4)
         BCT   R5,C022P
         B     C040
C023P    CLC   PGMEIN+1(2),=C'/*'
         BE    C032C
         LA    R4,8(R4)
C024P    CLI   01(R4),C' '
         BNE   C026P
         LA    R4,1(R4)
         BCT   R5,C024P
         B     C040
C026P    MVC   0(1,R6),01(R4)      UEBERTRAGEN ZEICHEN
         LA    R4,1(R4)
         LA    R6,1(R6)
         CLI   01(R4),C' '
         BE    C030P
         CLI   01(R4),C';'
         BE    C030P
         B     C026P
C030P    MVC   PGMEIN+1(2),=C'/*' INCLUDE ALS KOMMENTAR
         MVC   PGMEIN+70(2),=C'*/'
         B     C060
*        SCHREIBEN PROG.ANWEISUNG
C040     PUT DCBPGMA,PGMEIN
         B     C010
*        LESEN UND SCHREIBEN COPY-MEMBER
C060     EQU   *
         PUT   DCBPGMA,PGMEIN
         FIND  DCBPGMC,COPYNAME,D
         C     R15,=F'04'
         BNE   C062
         MVC   MESSAGE+1(007),=C'MEMBER '
         MVC   MESSAGE+8(8),COPYNAME
         MVC   MESSAGE+16(15),=C'NICHT GEFUNDEN '
         BAL   R3,MESSROUT
         MVC   RC,=F'04'
         B     C010
C062     EQU   *
         READ  DECBCO,SF,DCBPGMC,COEINB,'S'
         CHECK DECBCO
         SR    R7,R7
         LH    R7,DCBPGMC+62
         L     R8,DECBCO+16
         SH    R7,14(0,R8)
         LA    R4,COEINB
C064     MVC   COEIN,0(R4)
         LA    R4,80(R4)
         CLI   COBOL1,C'1'
         BNE   C065
         MVC   COEIN,PGMEINC1
         MVI   COBOL1,C' '
C065     PUT   DCBPGMA,COEIN
         S     R7,=F'80'
         BNZ   C064
         B     C062
C900     EQU   *         ENDROUTINE
         CLOSE (DCBPGMA,,DCBPGMC,,DCBPGMA,,DCBOUT)
         L     R15,RC
         EROUT R
*        DEFINITIONEN
         ORG   C000+1120
DCBPGME  DCB   DSORG=PS,DDNAME=PGMEIN,MACRF=(GM),                      C
               EODAD=EODATPR
DCBPGMA  DCB   DSORG=PS,DDNAME=PGMAUS,MACRF=(PM)
DCBPGMC  DCB   DSORG=PO,DDNAME=PGMCOPY,MACRF=(R),                      C
               EODAD=EODATCO
DCBOUT   DCB   DSORG=PS,DDNAME=SYSOUT,MACRF=(W)
PKZ      DS    CL1
COBOL1   DC    CL1' '
PGMEIN   DS    CL80
PGMEINC1 DS    CL80
PUNKT    DC    CL40'.                                       '
COEIN    DS    CL80
COPYNAME DS    D
MESSAGE  DS    0CL133
         DC    133CL1' '
TXTCOEND DS    0CL080
         DC    CL40'      *    COPY ENDE                    '
         DC    40CL1' '
TXTPLEND DS    0CL80
         DC    CL40' /*      INCLUDE ENDE                   '
         DC    CL40'                              */        '
FLD8B    DC    CL8'        '
APARM    DC    F'0'
RC       DC    F'0'
COEINB   DS    CL1600
         END   IS050
