*****************************************************************
*                                                               *
*  DAS PROGRAMM SYHA180 DIENT ZUR AENDERUNG DER MAXIMALEN       *
*  ANZAHL VON BLDG-ENTRIES. ALS EINGABE WERDEN INFORMATIONEN    *
*  AUS SYHA171 AUSGEWERTET. DIE KATALOG-AENDERUNGEN WERDEN MIT  *
*  DEM CAMLST-MAKRO DURCHGEFUEHRT.                              *
*                                                               *
*  JCL :                                                        *
*                                                               *
*  //STEP02  EXEC PGM=SYHA180                                   *
*  //SYHA180A DD  DSN=&TEMP,DISP=(OLD,DELETE)   (AUS SYHA171)   *
*  //SYHA180B DD  DSN=&CARD,DISP=(OLD,DELETE)    DSNAMEN        *
*  //SYHA180C DD  SYSOUT=S                                      *
*                                                               *
*                                                               *
*  DIE PARAMETERKARTEN MUESSEN AB STELLE 1 DEN BLDG-INDEX       *
*  UND AB STELLE 50 DEN NEUEN 3-STELLIGEN MAX-WERT ENTHALTEN.   *
*                                                               *
*                                                               *
*                                                               *
*****************************************************************
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2                       BASISREGISTER
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7                       GETMAIN-AREA
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
SYHA180  CSECT
         B     12(R15)
         DC    CL8'SYHA180'
         SAVE  (14,12)
         LR    R2,R15
         USING SYHA180,R2
         ST    R13,SAV+4
         LR    R15,R13
         LA    R13,SAV
         ST    R13,8(R15)
         B     BEGINN
         SPACE 3
*      QBV6*STATUSA
         SPACE 3
BEGINN   EQU   *
         OPEN  (PARAM,(INPUT),AUS,(OUTPUT))
         LA    R3,PTAB-80
LESPARM  EQU   *
         LA    R3,80(R3)
LESP10   EQU   *
         C     R3,=A(PTAB+1600)
         BNL   PARMEND                 NUR 20 ENTRIES MOEGLICH
         GET   PARAM,(R3)
         CLI   0(R3),C' '
         BE    PARMFEHL
         B     LESPARM
PARMEND  EQU   *
         CLOSE PARAM
         CLI   PTAB,C' '
         BE    PFEHL                   KEIN EINTRAG
         SH    R3,=H'80'
         ST    R3,APEND                ADR. LETZTER EINTRAG
         C     R3,=A(PTAB)
         BL    PFEHL                   KEIN EINTRAG
         BE    PSEND                   NUR EIN EINTRAG
PARMSORT EQU   *
         LA    R4,PTAB
         LA    R5,PTAB+80
PS05     EQU   *
         CLC   0(80,R4),0(R5)
         BL    PS10
         XC    0(80,R4),0(R5)          PLAETZE TAUSCHEN
         XC    0(80,R5),0(R4)
         XC    0(80,R4),0(R5)
PS10     EQU   *
         LA    R5,80(R5)
         CR    R5,R3
         BL    PS05                    INNERE SCHLEIFE
         LA    R4,80(R4)
         CR    R4,R3
         BNL   PSEND                   ENDE DER SORTIERUNG
         LA    R5,80(R4)
         B     PS05
PSEND    EQU   *
         SPACE 3
         GETMAIN R,LV=13300            PLATZ FUER 100 DSNAMES
         SPACE 3
         LR    R7,R1
         OPEN  (EIN,(INPUT))
         LA    R4,PTAB
         ST    R7,GETMADR              ANFANG GETMAIN-AREA RETTEN
LES      EQU   *                       INDEX SUCHEN
         GET   EIN,EREC
LES05    EQU   *
         CLC   ETYP,=C'GIPE'
         BNE   LES
LES10    EQU   *
         CLC   ENAME,0(R4)
         BE    VERA                    INDEX GEFUNDEN
         BL    LES
         MVC   FS1+1(44),0(R4)         INDEX NICHT IM KATALOG
         PUT   AUS,FS1
         LA    R4,80(R4)
         CR    R4,R3
         BH    ENDE
         B     LES10
VERA     EQU   *
         MVI   KZ,C'D'
         CLC   ECUR,=C'000'
         BE    DELETX                  KEINE DATEI KATALOGISIERT
         CLC   ECUR,=C'100'
         BH    FEHL2
         L     R7,GETMADR
VERA10   EQU   *
         GET   EIN,(R7)
         CLC   57(4,R7),=C'  VO'
         BE    VERA10                  ZEILE UEBERLESEN
         CLC   57(4,R7),=C'NTRY'
         BE    VERA10                  ZEILE UEBERLESEN
         CLC   57(4,R7),=C'TYPE'
         BE    VERA10                  ZEILE UEBERLESEN
         CLC   57(4,R7),=C'IPE '
         BE    VERA10                  ZEILE UEBERLESEN
         CLC   57(4,R7),=C'DSPE'
         BNE   VER15                   ENDE DES LAUFENDEN INDEX
         LA    R7,133(R7)
         B     VERA10
VER15    EQU   *                       ENDE DER EINTRAEGE
         MVI   KZ,C'G'
VER20    EQU   *
         ST    R7,GETMEND              ADR. NAECHSTER GIPE
         L     R6,GETMADR              ADR. ERSTER SATZ
UNC      EQU   *
         MVC   UNCDSN,4(R6)
         SPACE 3
         CATALOG UNCATLG               DATEI ENTKATALOGISIEREN
         SPACE 3
         LTR   R15,R15
         BNZ   UNCFEHL
         MVC   A1DSN,UNCDSN
         PUT   AUS,A1REC
UNC10    EQU   *
         LA    R6,133(R6)              NAECHSTER DSN
         CLI   4(R6),C' '
         BE    UNC10                   N-TES VOLUME ZUM GLEICHEN DSN
         CR    R6,R7
         BL    UNC
DELETX   EQU   *
         MVC   DELDSN,ENAME
         SPACE 3
         INDEX DELX                    DELETE INDEX
         SPACE 3
         LTR   R15,R15
         BNZ   DELFEHL
         MVC   A2DSN,ENAME             INDEX GELOESCHT
         PUT   AUS,A2REC
         PACK  DW,49(3,R4)
         CVB   R15,DW
         STC   R15,BUILD+3
         SPACE 3
         INDEX BUILD                   BLDG MIT NEUEN WERTEN
         SPACE 3
         LTR   R15,R15
         BNZ   BLDGFEHL
         MVC   A3DSN,DELDSN
         MVC   A3ANZ,49(R4)
         PUT   AUS,A3REC
         CLC   ECUR,=C'000'
         BE    CAT60                   NAECHSTER GIPE
         L     R6,GETMADR
         PACK  RF1,49(3,R4)            NEUE ANZAHL MAX. BLDG-ENTRIES
         CLC   ECUR,49(R4)
         BNL   CAT
         PACK  RF1,ECUR
CAT      EQU   *
         MVC   UNCDSN,4(R6)
         LA    R8,1                    ANZAHL VOLUMES
         LA    R9,VOLS+2               ADR. 1. VOLUME IN LISTE
CAT10    EQU   *
         TR    85(8,R6),TRTAB-193
         PACK  0(5,R9),85(9,R6)        4 BYTES DEVICE CODE
         MVC   4(6,R9),65(R6)          VOL-SER-NR.
         PACK  DW,75(6,R6)
         CVB   R15,DW
         STH   R15,10(R9)              SEQUENCE NUMBER
         LA    R6,133(R6)
         CR    R6,R7
         BNL   CAT40
CAT20    EQU   *
         CLI   4(R6),C' '
         BNE   CAT40
         LA    R9,12(R9)               NAECHSTER PLATZ IN VOLS
         LA    R8,1(R8)                ANZAHL VOLUMES + 1
         B     CAT10
CAT40    EQU   *
         STH   R8,VOLS
         SPACE 3
         CATALOG CATAL
         SPACE 3
         LTR   R15,R15
         BNZ   CATFEHL
         MVC   A4DSN,UNCDSN
         PUT   AUS,A4REC
         SP    RF1,=P'1'
         CP    RF1,=P'0'
         BH    CAT                     NAECHSTER EINTRAG
         CLI   KZ,C'E'
         BE    ENDE
CAT60    EQU   *
         LA    R4,80(R4)
         C     R4,APEND
         BH    ENDE                    ALLE PARM-SAETZE VERARBEITET
         MVC   EREC,0(R7)              NAECHSTER GIPE-RECORD
         B     LES05
EINEND   EQU   *
         CLI   KZ,C'G'                 GIPE-VERARBEITUNG ?
         BE    ENDE                    JA
         MVI   KZ,C'E'
         B     VER20
ENDE     EQU   *
         CLOSE (EIN,,AUS)
         L     R13,SAV+4
         RETURN (14,12),RC=0
         SPACE 3
CATFEHL  EQU   *
         MVC   A5DSN,UNCDSN
CATF10   EQU   *
         PUT   AUS,A5REC
         ABEND 999,DUMP
         SPACE 3
DELFEHL  EQU   *
         MVC   A5REC+14(24),=C'DELETE INDEX FUER       '
         MVC   A5DSN,DELDSN
         B     CATF10
         SPACE 3
PARMFEHL EQU   *
         MVC   A6PARM,0(R4)
         PUT   AUS,A6REC               UNGUELTIGER PARAMETER
         B     LESP10                  NAECHSTER PARAMETER
         SPACE 3
PFEHL    EQU   *
         PUT   AUS,A7REC
         B     ENDE
         SPACE 3
BLDGFEHL EQU   *
         MVC   A5REC+14(18),=C'   BLDG   FUER DIE'
         MVC   A5DSN,DELDSN
         B     CATF10
         SPACE 3
UNCFEHL  EQU   *
         MVC   A5REC+14(14),=CL14'ENTKATAL.'
         MVC   A5DSN,UNCDSN
         B     CATF10
         SPACE 3
FEHL2    EQU   *
         MVC   A8DSN,ENAME
         B     CATF10
         EJECT
SAV      DC    9D'0'
DW       DC    D'0'
APEND    DC    F'0'                    ADR. LETZTER PARM-EINTRAG
GETMADR  DC    F'0'                    ADR. GETMAIN-AREA
GETMEND  DC    F'0'                    ADR. LETZTER SATZ IN GETM.-AREA
RF1      DC    PL3'0'
KZ       DC    C' '
         SPACE 3
TRTAB    DC    X'FAFBFCFDFEFF'         UEBERSETZUNGSTABELLE FUER
         DC    XL41'00'                DEVICE-CODE
         DC    C'0123456789'
         SPACE 3
EREC     DS    0CL133                  EINGABE-SATZ
         DS    CL4
ENAME    DS    CL44
         DS    CL9
ETYP     DS    CL4
         DS    CL4
EVOL     DS    CL6
         DS    CL4
ESEQ     DS    CL6
         DS    CL4
EDEV     DS    CL8
         DS    CL4
EMAX     DS    CL3
         DS    CL3
ECUR     DS    CL3
         DS    CL54
         SPACE 3
UNCATLG  CAMLST UNCAT,UNCDSN
UNCDSN   DC    CL44' '                 DSN FUER UNCATALOG
         SPACE 3
DELX     CAMLST DLTX,DELDSN
DELDSN   DC    CL44' '                 DSN FUER DLTX UND BLDG
         SPACE 3
BUILD    CAMLST BLDG,DELDSN,,,,,100    DIE ANZAHL WIRD UEBERSCHRIEBEN
         SPACE 3
CATAL    CAMLST CAT,UNCDSN,,VOLS       DATEI KATALOGISIEREN
VOLS     DC    H'0'                    ANZAHL VOLUMES
         DC    40CL12' '               PLATZ FUER 40 VOLUMES
*                                      4 BYTES DEVICE CODE
*                                      6 BYTES VOL-SER-NR.
*                                      2 BYTES SEQUENCE NUMBER
FS1      DS    0CL133
         DC    CL60' '
         DC    CL73'IST KEIN GUELTIGER INDEX.'
A1REC    DS    0CL133
         DC    C'0 DATEI  '
A1DSN    DC    CL44' '
         DC    CL80'  ENTKATALOGISIERT.'
         SPACE 3
A2REC    DS    0CL133
         DC    C'0 INDEX  '
A2DSN    DC    CL44' '
         DC    CL80'  GELOESCHT.'
         SPACE 3
A3REC    DS    0CL133
         DC    C'0 INDEX  '
A3DSN    DC    CL44' '
         DC    C'  MIT '
A3ANZ    DC    CL3' '
         DC    C' ENTRIES AUFGEBAUT.'
         DC    CL52' '
A4REC    DS    0CL133
         DC    C'0 DATEI  '
A4DSN    DC    CL44' '
         DC    CL80'  KATALOGISIERT.'
         SPACE 3
A5REC    DS    0CL133
         DC    C'0 FEHLER BEIM KATALOGISIEREN DER DATEI  '
A5DSN    DC    CL44' '
         DC    CL49' '
         SPACE 3
A6REC    DS    0CL133
         DC    C'0 UNGUELTIGE PARAMETERKARTE : '
A6PARM   DC    CL80' '
         DC    CL23' WIRD UEBERGANGEN.'
         SPACE 3
A7REC    DC    CL133'0 PARAMETERKARTEN FEHLEN. ABBRUCH.'
         SPACE 3
A8REC    DS    0CL133
         DC    C'0 MEHR ALS 100 EINTRAEGE VORHANDEN FUER '
A8DSN    DC    CL44' '
         DC    CL49' '
         EJECT
EIN      DCB   DDNAME=SYHA180A,                                        *
               MACRF=(GM),                                             *
               DSORG=PS,                                               *
               RECFM=FB,                                               *
               LRECL=133,                                              *
               EODAD=EINEND
         EJECT
AUS      DCB   DDNAME=SYHA180C,                                        *
               MACRF=(PM),                                             *
               DSORG=PS,                                               *
               RECFM=FBA,                                              *
               LRECL=133
         EJECT
PARAM    DCB   DDNAME=SYHA180B,                                        *
               MACRF=(GM),                                             *
               DSORG=PS,                                               *
               RECFM=FB,                                               *
               LRECL=80,                                               *
               EODAD=PARMEND
         EJECT
         LTORG
         SPACE 3
PTAB     DC    20CL80' '
         END
