./ ADD NAME=UICR03
XDA1     TITLE 'DISPLAY ACTIVITIES'
         SPACE 5
***********************************************************************
*                                                                     *
*        AUTHOR. KEHR.                                                *
*        INSTALLATION. BAYER AG, LEVERKUSEN, RECHENZENTRUM.           *
*        DATE-WRITTEN. 29.10.1972.                                    *
*        REMARKS.                                                     *
*              DURCH DEN MODUL 'DA' WERDEN DIE AKTIVEN TASKS DES      *
*              SYSTEMS ANGEZEIGT. ALS INFORMATION WIRD AUSGEGEBEN:    *
*              NAME DES INITIATORS, JOB-NAME, STEP-NAME, PROCSTEP-NAME*
*              PROTEKTION KEY, REGION GROESSE, DISPATCHING PRIORITAET *
*              UND DER WAIT-COUNT DES LETZTEN ANGEHANGENEN REQUEST    *
*              BLOCKS.                                                *
*                                                                     *
*              DURCH ANGABE DES PARAMETERS 'T' WIRD AN STELLE DIESER  *
*              INFORMATIONEN FOLGENDES AUSGEGEBEN:                    *
*              NAME DES INITIATORS, HEXADEZIMALER ABZUG DER FELDER    *
*              TCBPKF, TCBFLGS, TCBCMP UND TCBDAR, TCBNDSP.           *
*              DIE ANGABE DES PARAMETERS 'A' BEWIRKT EINE AUSGABE     *
*              BEIDER INFORMATIONEN.                                  *
*                                                                     *
*        DER MODUL HAT DIE ATTRIBUTE: REUSABLE, REENTERABLE.          *
*                                                                     *
*        BENUTZTE MAKROS:                                             *
*              REG, RETURN, FREEMAIN, XPARM, XSAVE                    *
*                                                                     *
***********************************************************************
         SPACE 4
*---------------------------------------------------------------------*
*                                                                     *
*        MODIFICATIONS. G.FUCHS.                                      *
*        DATE-WRITTEN. 9.7.1974.                                      *
*        REMARKS.                                                     *
*            'DA' IST ERWEITERT WORDEN, UND VERKRAFTET NUN AUCH       *
*            EINEN AUFRUF UNTER EINEM MVS SYSTEM.                     *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        MODIFICATIONS. D.JACKOBS - ICR.                              *
*        DATE-WRITTEN. 09.07.83 / 23.01.84                            *
*        REMARKS.                                                     *
*                                                                     *
*        -  IN DER UEBERSCHRIFT WIRD ANSTELLE VON 'PROCNAME'          *
*           DIE AKTUELLE CPU-BELASTUNG AUSGEWIESEN                    *
*           (CCVUTILP, WIRD ALLE 3 SRM-SEKUNDEN NEU BERECHNET)        *
*           (ICR 23.01.84).                                           *
*                                                                     *
*        -  DIE AUSGABE DES COMMANDS IST UMGESTALTET.                 *
*           CPU-ZEITEN UND EXCP'S WERDEN IMMER GEMEINSAM              *
*           AUSGEWIESEN; DIE UEBERSCHRIFTEN WURDEN ZU DIESEM          *
*           ZWECK LEICHT MODIFIZIERT (Z.B. 'DP' STATT 'DPRTY')        *
*           (ICR 09.07.83).                                           *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
DA       CSECT
*        B     16(0,15)                                 ICR 09.07.83
         B     22(0,15)                                 ICR 09.07.83
***      DC    AL1(11)                                  ICR 09.07.83
         DC    AL1(16)
         DC    C'DA '
         DC    C'&SYSDATE'       MM/DD/YY               ICR 09.07.83
         DC    C'&SYSTIME'       HH.MM                  ICR 09.07.83
         DC    C' '              ALIGNMENT              ICR 09.07.83
***      DC    C'12/10/79'                              ICR 09.07.83
         STM   14,12,12(13)
         LR    R12,R15
***      USING *-22,R12                                 ICR 09.07.83
         USING *-28,R12                                 ICR 09.07.83
         L     R0,=A(WORKL)
         BAL   1,*+4
         SVC   10
         XC    0(3*4,1),0(1)
         L     R0,=A(WORKL)
         STH   0,2(1)
         ST    1,8(13)
         ST    13,4(1)
         LR    13,1
         L     1,4(13)
         LM    0,1,20(1)
         USING WORK,R13
         USING PARMAREA,R11
         SPACE
         LR    R11,R1                  ESTABLISH ADDRESS OF XPARM
         SPACE 2
*      TEST FOR MVS SYSTEM
         L     R3,CVTPTR               LOAD CVT ADDRESS
         TM    CVTDCB(R3),CVTMVS2      MVS SYSTEM?
         BO    MVSDATA                 BR IF YES
*
         SPACE 2
         L     R0,REG4                 GET MCS CONSOLE ID
XDA1     WTO   'XDA1 INITIATOR PK JOB-NAME STEP-NAME PROC-NAME REGION D*
               PRTY WCT',                                              *
               MCSFLAG=REG0
         SPACE
         MVC   XDA2(XDA2L),XDA2DS      INITIALIZE XDA2 MESSAGE
         MVC   XDA3(XDA3L),XDA3DS      INITIALIZE XDA3 MESSAGE
         SPACE
         LA    R3,CVTPTR               GET ADDRESS OF CVT-POINTER
         L     R3,0(R3)                GET CVT-ADDRESS
         L     TCBR,CVTHEAD(R3)        POINT TO FIRST TCB IN SYSTEM
         L     TCBR,TCBOTC(TCBR)       ORIGINATING TCB IS MAST. SCHED.
         L     OTCBR,TCBLTC(TCBR)      GET LAST CHAINED MOTHER-TCB
         LA    R10,128                 ESTABLISH LOOP CONTROL
DA1      EQU   *
         BAL   R9,DATCB                CALL PROCESSOR OF TCB
         L     OTCBR,TCBNTC(OTCBR)     GET PREVIOUS MOTHER-TCB
         LTR   OTCBR,OTCBR             IS IT FIRST
         BZ    DA2                     YES, JOB DONE, RETURN
         BCT   R10,DA1                 PROCESS NEXT MOTHER-TCB
         EX    1,*                     LOOP LIMIT EXCEEDED *ABEND*
DA2      EQU   *
         XRETURN 0,R
         SPACE 4
*      'DISPLAY ACTIVES' FOR MVS SYSTEMS
MVSDATA  EQU   *
         CLC   TEXT+3(2),=CL2'CB'
         BE    DSPCBS
         MVI   IOSW,0                  CLEAR IO-SWITCH
         MVI   FUNCIND,0               CLEAR FUNCTION INDICATOR
         LA    R1,TEXT+2               START ADDRESS OF DA OPTIONS
OPTLP    CLI   0(R1),C','              END OF OPTIONS?
         BNE   OPTLPE                  BR IF YES
         LA    R1,1(,R1)               ADDRESS OF NEXT BYTE
         LA    R0,ANZDOPT              LOAD # OF VALID OPTIONS
         LA    R15,DISPOPT             LOAD ADDRESS OF FIRST OPTION
         SR    R14,R14                 CLEAR R14
OPT1LP   IC    R14,0(,R15)             INSERT LENGTH OF CURRENT OPT
         BCTR  R14,0                   DECREMENT BY 1 FOR EX CLC
         EX    R14,CLCOPT              COMPARE
         BE    OPT1LPE                 BR IF EQUAL
         LA    R15,L'DISPOPT(,R15)     ADDRESS OF NEXT OPTION
         BCT   R0,OPT1LP               BCT
         B     OPTLPE
*                                                       ICR 09.07.83
OPT1LPE  OC    FUNCIND,9(R15)          OR
***      CLC   1(2,R15),=CL2'IO'                        ICR 09.07.83
***      BNE   NOIO1                                    ICR 09.07.83
***      MVI   IOSW,X'FF'              NO IO'S TO BE PRESENTED.07.83
NOIO1    EQU   *
         LA    R1,1(R14,R1)            ADDRESS OF NEXT BYTE
         B     OPTLP                   PROCESS NEXT OPTION
CLCOPT   CLC   0(0,R1),1(R15)          COMPARE OPTIONS
OPTLPE   EQU   *
***      CLI   IOSW,X'FF'              NO IO'S ?
***      BNE   NOIO                    NO, BRANCH AROUND HEADER
*                                                       ICR 09.07.83
***      MVC   XDA1T+53(6),=CL6'EXCP''S'                ICR 09.07.83
         MVC   XDA1T+(XDA4EXCP-XDA4)(6),=CL6'EXCP''S'    ICR 09.07.83
NOIO     EQU   *
         TM    FUNCIND,X'F0'           ANY OPTION GIVEN?
         BNZ   DAINIT                  BR IF YES
         TM    FUNCIND,DISPPAGE        PAGING STATISTICS TO BE DISPL.?
         BO    DAINIT                  BR IF YES
         OI    FUNCIND,DISPDFLT        IF NO - ASSUME DEFAULT
*
DAINIT   MVI   HEADSW,C'N'             INDICATE HEADLINE NOT WRITTEN
         L     R4,CVTASVT(R3)          LOAD ADDRESS OF ASVT
         LA    R5,ASVTENTY(R4)         LOAD ADDRESS OF FIRST ENTRY
         TM    FUNCIND,DISPPAGE        FUNCTION INDICATOR = PAGE?
         BO    PAGEPROC                BR IF YES
         TM    FUNCIND,DISPTIME        FUNCTION INDICATOR = TIME?
         BZ    SLOTPROC                BR IF NO
         LA    R7,TIMEBLK-8            LOAD ADDRESS OF TIME WKAREA
         BAL   R9,TIMELP1              SAVE TIMER VALUES
         STCK  TOD1                    SAVE TOD-CLOCK
*
         NI    FUNCIND,255-DISPSUPP    SET BIT TO ZERO
         STIMER WAIT,BINTVL=ONESECND   WAIT FOR 1 SECOND
*
         LA    R5,ASVTENTY(R4)         LOAD ADDRESS OF FIRST ASVT ENTRY
         LA    R7,TIMEBLK              LOAD ADDRESS OF TIMER WKAREA
         SR    R10,R10                 ZERO TIMER SUM
         BAL   R9,TIMELP1              SAVE TIMER VALUES
         STCK  TOD2                    SAVE TOD-CLOCK
         BAL   R2,BTODDIFF             BUILD TOD DIFFERENCE
         LTR   R10,R10                 SUM OF ALL TIMERS = 0?
         BNZ   *+8                     BR IF NO
         LA    R10,1                   SET SUM TO NONZERO
         ST    R10,TIMESUM             STORE IT
*
SLOTPROC LA    R5,ASVTENTY(R4)         LOAD ADDRESS OF FIRST ASVT ENTRY
         L     R7,ASVTMAXU(R4)
         MH    R7,=H'4'
         AR    R7,R5
         ST    R7,MAXADR
         LA    R10,TIMEBLK             LOAD ADDRESS OF TIMER WKAREA
         SR    R2,R2                   CLEAR R2 (INIT COUNT)
         SR    R3,R3                   CLEAR R3 (TS USER COUNT)
ASVTLOOP TM    0(R5),ASVTAVAL          ASID ASSIGNED?
         BO    NXTENTY                 BR IF NO
         L     R6,0(,R5)               LOAD ADDRESS OF ASCB
         BAL   R9,DAASCB               CALL PROCESSOR OF ASCB
NXTENTY  LA    R5,4(,R5)               ADDRESS OF NEXT ASCB PTR
         LA    R10,L'TIMEBLK(R10)      ADDRESS OF NEXT TIME ELEMENT
         C     R5,MAXADR        LAST ENTRY?
         BL    ASVTLOOP                BR IF NO
         B     DA2                     RETURN
         EJECT
PAGEPROC EQU   *
         LA    R7,PAGEBLK-12           LOAD ADDRESS OF PAGE WKAREA
         BAL   R9,PAGELP1              SAVE PAGING VALUES
         STCK  TOD1                    SAVE TOD CLOCK VALUE
*
         NI    FUNCIND,255-DISPSUPP    SET BIT TO ZERO
         STIMER WAIT,BINTVL=THREESEC   WAIT FOR 3 SECONDS
*
         LA    R5,ASVTENTY(R4)         LOAD ADDRESS OF FIRST ASVT ENTRY
         LA    R7,PAGEBLK              LOAD ADDRESS OF PAGE WKAREA
         BAL   R9,PAGELP1              SAVE PAGER VALUES
         STCK  TOD2                    SAVE TOD CLOCK VALUE
         TM    FUNCIND,X'F0'           ANY OPTIONS GIVEN?
         BNZ   PAGEOPTD                BR IF YES
         BAL   R2,BTODDIFF             BUILD TOD DIFFERENCE
         L     R0,REG4                 LOAD CONSOLE-ID
         LA    R1,XDA5DS
         BAL   R14,WTOROUT
         L     R15,TODDIFF             LOAD TOD DIFFERENCE
         MVC   XDA6,XDA6DS             MOVE MSG SKELETON
         MVC   XDA6TYPE,=C'DEMAND '    MOVE TYPE
         L     R1,PAGEVAL2+0           PAGE-IN, 2. VALUE
         L     R14,PAGEVAL1+0          PAGE-IN, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-IN/SEC
         MVC   XDA6IN,MASKAREA+1       MOVE TO MSG
         ZAP   SPAGEIN,DOPWORT         SAVE VALUE
         ZAP   SPAGEIO,DOPWORT         SAVE VALUE
         L     R1,PAGEVAL2+4           PAGE-OUT, 2. VALUE
         L     R14,PAGEVAL1+4          PAGE-OUT, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-OUT/SEC
         MVC   XDA6OUT,MASKAREA+1      MOVE TO MSG
         ZAP   SPAGEOUT,DOPWORT        SAVE VALUE
         AP    SPAGEIO,DOPWORT         SAVE VALUE
         L     R1,PAGEVAL2+28          PAGE-REC, 2. VALUE
         L     R14,PAGEVAL1+28         PAGE-REC, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-REC/SEC
         MVC   XDA6REC,MASKAREA+1      MOVE TO MSG
         ZAP   SPAGEREC,DOPWORT        SAVE VALUE
         ZAP   SPAGETOT,DOPWORT        SAVE VALUE
         ZAP   DOPWORT,SPAGEIO         PAGE-IO/SEC VALUE
         BAL   R8,BPAGEVL1             EDIT VALUE
         MVC   XDA6PGIO,MASKAREA+1     MOVE TO MSG
         AP    DOPWORT,SPAGETOT        TOTAL PAGE/SEC VALUE
         BAL   R8,BPAGEVL1             EDIT VALUE
         MVC   XDA6PAGE,MASKAREA+1     MOVE TO MSG
         L     R0,REG4                 LOAD CONSOLE-ID
         LA    R1,XDA6
         BAL   R14,WTOROUT
*
         L     R15,TODDIFF             LOAD TOD DIFFERENCE
         MVC   XDA6TYPE,=C'VIO    '    MOVE TYPE
         L     R1,PAGEVAL2+8           PAGE-IN, 2. VALUE
         L     R14,PAGEVAL1+8          PAGE-IN, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-IN/SEC
         MVC   XDA6IN,MASKAREA+1       MOVE TO MSG
         AP    SPAGEIN,DOPWORT         SAVE VALUE
         ZAP   SPAGEIO,DOPWORT         SAVE VALUE
         L     R1,PAGEVAL2+12          PAGE-OUT, 2. VALUE
         L     R14,PAGEVAL1+12         PAGE-OUT, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-OUT/SEC
         MVC   XDA6OUT,MASKAREA+1      MOVE TO MSG
         AP    SPAGEOUT,DOPWORT        SAVE VALUE
         AP    SPAGEIO,DOPWORT         SAVE VALUE
         L     R1,PAGEVAL2+16          PAGE-REC, 2. VALUE
         L     R14,PAGEVAL1+16         PAGE-REC, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-REC/SEC
         MVC   XDA6REC,MASKAREA+1      MOVE TO MSG
         AP    SPAGEREC,DOPWORT        SAVE VALUE
         ZAP   SPAGETOT,DOPWORT        SAVE VALUE
         ZAP   DOPWORT,SPAGEIO         PAGE-IO/SEC VALUE
         BAL   R8,BPAGEVL1             EDIT VALUE
         MVC   XDA6PGIO,MASKAREA+1     MOVE TO MSG
         AP    DOPWORT,SPAGETOT        TOTAL PAGE/SEC VALUE
         BAL   R8,BPAGEVL1             EDIT VALUE
         MVC   XDA6PAGE,MASKAREA+1     MOVE TO MSG
         L     R0,REG4                 LOAD CONSOLE-ID
         LA    R1,XDA6
         BAL   R14,WTOROUT
*
         L     R15,TODDIFF             LOAD TOD DIFFERENCE
         MVC   XDA6TYPE,=C'SWAP   '    MOVE TYPE
         L     R1,PAGEVAL2+20          PAGE-IN, 2. VALUE
         L     R14,PAGEVAL1+20         PAGE-IN, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-IN/SEC
         MVC   XDA6IN,MASKAREA+1       MOVE TO MSG
         AP    SPAGEIN,DOPWORT         SAVE VALUE
         ZAP   SPAGEIO,DOPWORT         SAVE VALUE
         L     R1,PAGEVAL2+24          PAGE-OUT, 2. VALUE
         L     R14,PAGEVAL1+24         PAGE-OUT, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-OUT/SEC
         MVC   XDA6OUT,MASKAREA+1      MOVE TO MSG
         AP    SPAGEOUT,DOPWORT        SAVE VALUE
         AP    SPAGEIO,DOPWORT         SAVE VALUE
         SR    R1,R1                   PAGE-REC, 2. VALUE
         SR    R14,R14                 PAGE-REC, 1. VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE-REC/SEC
         MVC   XDA6REC,MASKAREA+1      MOVE TO MSG
         AP    SPAGEREC,DOPWORT        SAVE VALUE
         ZAP   SPAGETOT,DOPWORT        SAVE VALUE
         ZAP   DOPWORT,SPAGEIO         PAGE-IO/SEC VALUE
         BAL   R8,BPAGEVL1             EDIT VALUE
         MVC   XDA6PGIO,MASKAREA+1     MOVE TO MSG
         AP    DOPWORT,SPAGETOT        TOTAL PAGE/SEC VALUE
         BAL   R8,BPAGEVL1             EDIT VALUE
         MVC   XDA6PAGE,MASKAREA+1     MOVE TO MSG
         L     R0,REG4                 LOAD CONSOLE-ID
         LA    R1,XDA6
         BAL   R14,WTOROUT
         MVC   XDA6TYPE,=C'*TOTAL*'    MOVE TYPE
         ZAP   DOPWORT,SPAGEIN         MOVE VALUE
         BAL   R8,BPAGEVL1             BUILD PAGE-IN/SEC
         MVC   XDA6IN,MASKAREA+1       MOVE TO MSG
         ZAP   DOPWORT,SPAGEOUT        MOVE VALUE
         BAL   R8,BPAGEVL1             BUILD PAGE-OUT/SEC
         MVC   XDA6OUT,MASKAREA+1      MOVE TO MSG
         ZAP   DOPWORT,SPAGEREC        MOVE VALUE
         BAL   R8,BPAGEVL1             BUILD PAGE-REC/SEC
         MVC   XDA6REC,MASKAREA+1      MOVE TO MSG
         ZAP   DOPWORT,SPAGEIN         COMPUTE
         AP    DOPWORT,SPAGEOUT          PAGE-IO/SEC VALUE
         BAL   R8,BPAGEVL1             EDIT VALUE
         MVC   XDA6PGIO,MASKAREA+1     MOVE TO MSG
         AP    DOPWORT,SPAGEREC        TOTAL PAGE/SEC VALUE
         BAL   R8,BPAGEVL1             EDIT VALUE
         MVC   XDA6PAGE,MASKAREA+1     MOVE TO MSG
         L     R0,REG4                 LOAD CONSOLE-ID
         LA    R1,XDA6
         BAL   R14,WTOROUT
         B     DA2
*
PAGEOPTD LA    R5,ASVTENTY(R4)         LOAD ADDRESS OF FIRST ASVT ENTRY
         L     R7,ASVTMAXU(R4)
         MH    R7,=H'4'
         AR    R7,R5
         ST    R7,MAXADR
         LA    R10,PAGEBLK             LOAD ADDRESS OF PAGER WKAREA
         SR    R2,R2                   CLEAR R2 (INIT COUNT)
         SR    R3,R3                   CLEAR R3 (TS USER COUNT)
ASVTPGLP TM    0(R5),ASVTAVAL          ASID ASSIGNED?
         BO    NXTPGENT                BR IF NO
         L     R6,0(,R5)               LOAD ADDRESS OF ASCB
         BAL   R9,DAASCB               CALL PROCESSOR OF ASCB
NXTPGENT LA    R5,4(,R5)               ADDRESS OF NEXT ASCB PTR
         LA    R10,L'PAGEBLK(R10)      ADDRESS OF NEXT PAGE ELEMENT
         C     R5,MAXADR        LAST ENTRY?
         BL    ASVTPGLP                BR IF NO
         B     DA2                     RETURN
         EJECT
PAGELP1  EQU   *                       PROCESS PAGER VALUES
         TM    FUNCIND,X'F0'           ANY OPTION GIVEN?
         BNZ   PAGELP1S                BR IF YES
         L     R6,CVTPVTP(,R3)         LOAD PVT ADDRESS
         TM    FUNCIND,DISPSUPP        FIRST ENTRY?
         BO    PAGELP11                BR IF YES
***      MVC   PAGEVAL2(32),PVTNPIN(R6) SAVE PAGING COUNTER  ICR 9.7.83
         MVC   PAGEVAL2(32),PVTNPIN(R6) SAVE PAGING COUNTER  ICR 9.7.83
         BR    R9                      RETURN
***ELP11 MVC   PAGEVAL1(32),PVTNPIN(R6) SAVE PAGING COUNTER  ICR 9.7.83
PAGELP11 EQU   *                                             ICR 9.7.83
         MVC   PAGEVAL1(32),PVTNPIN(R6) SAVE PAGING COUNTER  ICR 9.7.83
         BR    R9                      RETURN
PAGELP1S LA    R8,ANZTBLK-1            LOAD NUMBER OF PAGE ELEMENTS
         C     R8,ASVTMAXU(R4)         > MAX. NUMBER OF ADDRESS SP.?
         BNH   *+8                     BR IF NO
         L     R8,ASVTMAXU(R4)         SET IT TO THIS NUMBER
PAGELP1A TM    0(R5),ASVTAVAL          ASID ASSIGNED?
         BO    PAGELP1E                BR IF NO
         L     R6,0(,R5)               LOAD ASCB ADDRESS
         L     R6,ASCBOUXB(,R6)        LOAD OUXB ADDRESS
         STCK  TODVALUE                STORE TOD CLOCK
***                                                          ICR 9.7.83
         MVC   PAGEVAL1(32),OUXBPIN(R6) SAVE PAGING COUNTERS ICR 9.7.83
***      MVC   PAGEVAL1(24),OUXBPIN(R6) SAVE PAGING COUNTERS ICR 9.7.83
         MVC   PAGEVAL1+25(8),OUXBCAPI(R6)   SAVE PAG. CTRS. ICR 9.7.83
         L     R14,PAGEVAL1+00         COMPUTE
         A     R14,PAGEVAL1+12           TOTAL
         A     R14,PAGEVAL1+24             PAGE-IN
         ST    R14,12(,R7)                   OP'S
         L     R15,PAGEVAL1+04         COMPUTE
         A     R15,PAGEVAL1+16           PAGE-OUT
         ST    R15,16(,R7)                 OP'S
         L     R0,PAGEVAL1+08          COMPUTE
         A     R0,PAGEVAL1+20            TOTAL
         A     R0,PAGEVAL1+28              RECLAIM
         ST    R0,20(,R7)                    OP'S
         TM    FUNCIND,DISPSUPP        FIRST ENTRY TO THIS LOOP?
         BZ    PAGELP1T                BR IF NO
         MVC   36(8,R7),TODVALUE       MOVE TOD-VALUE
         B     PAGELP1E
PAGELP1T LM    R0,R1,24(R7)            LOAD FIRST TOD VALUE
         LM    R14,R15,TODVALUE        LOAD SECOND TOD VALUE
         BAL   R2,BTODDIF2             BUILD TOD DIFFERENCE
         ST    R15,24(,R7)             STORE TOD DIFFERENCE
PAGELP1E LA    R7,L'PAGEBLK(,R7)       PROCEED TO NEXT PAGE ELEMENT
         LA    R5,4(,R5)               PROCEED TO NEXT ASCB
         BCT   R8,PAGELP1A             PROCESS NEXT ASVT ENTRY
         BR    R9                      RETURN
         EJECT
TIMELP1  EQU   *                       PROCESS TIMER VALUES
         LA    R8,ANZTBLK-1            LOAD NUMBER OF TIMER AREAS
         C     R8,ASVTMAXU(R4)         > MAX. NUMBER OF ADDRESS SP.?
         BNH   *+8                     BR IF NO
         L     R8,ASVTMAXU(R4)         SET IT TO THIS NUMBER
TIMELP1A TM    0(R5),ASVTAVAL          ASID ASSIGNED?
         BO    TIMELP1Z                BR IF NO
         L     R6,0(,R5)               LOAD ASCB ADDRESS
         LM    R14,R15,ASCBEJST(R6)    LOAD JOB STEP TIME
         LM    R0,R1,ASCBSRBT(R6)      LOAD SRB TIME
         SRDA  R14,22                  R15, BIT 31 = 1024 MICROSEC
         SLDA  R0,10                   R0, BIT 31 = 1024 MICROSEC
         TM    FUNCIND,DISPSUPP        FIRST ENTRY TO THIS LOOP?
         BO    TIMELP1S                BR IF YES
         L     R1,0(,R7)               LOAD PREVIOUS CPU TIMER
         CR    R15,R1                  IS CURRENT < PREVIOUS?
         BL    TIMELP1D                BR IF YES
         SR    R1,R15                  COMPUTE DIFFERENCE (NEG)
         S     R0,4(,R7)               COMPUTE DIFF. OF SRB TIMER
         SR    R0,R1                   COMPUTE TOTAL DIFFERENCE
         AR    R10,R0                  ADD IT TO TIMER SUM
         B     TIMELP1R
TIMELP1Z SR    R15,R15                 SET CPU TIMER TO ZERO
TIMELP1D SR    R0,R0                   SET SRB TIMER TO ZERO
TIMELP1R TM    FUNCIND,DISPSRBT        ARE SRB TIMER TO BE DISPL.?
         BZ    TIMELP1S                BR IF NO
         LM    R14,R15,ASCBSRBT(R6)    LOAD SRB TIMER VALUE
         SRDA  R14,22                  R15, BIT 31 = 1024 MICROSEC
TIMELP1S STM   R15,R0,8(R7)            SAVE CPU & SRB TIMER VALUES
         LA    R7,L'TIMEBLK(,R7)       PROCEED TO NEXT TIME ELEMENT
         LA    R5,4(,R5)               PROCEED TO NEXT ASCB
         BCT   R8,TIMELP1A             PROCESS NEXT ASVT ENTRY
         BR    R9                      RETURN
*
         EJECT
DATCB    EQU   *
         SPACE
*        SUBROUTINE TO PROCESS TCB'S
*        R9 - RETURN ADDRESS
         SPACE 2
         STM   R2,R10,DASAVE           SAVE REGISTERS
         L     TCBR,TCBLTC(OTCBR)      GET FIRST SUBTASK TCB
         LTR   TCBR,TCBR               IS THERE ANY
         BZ    DATCBE                  NO, GO BACK
         SR    R8,R8                   CLEAR WORK-REGISTER
         IC    R8,TCBPKF(TCBR)         GET PROTECTION KEY
         SRL   R8,4                    ALIGN IT TO RIGHT HALFWORD
         IC    R8,HEXTAB(R8)           TRANSLATE TO EBCDIC
         STC   R8,XDA2PKF              STORE INTO MSG
         L     R7,TCBTIO(TCBR)         GET TASK I/O TABLE ADDRESS
         ST    R7,SAVETIO              SAVE TIOT ADDR FOR LATER COMP.
         MVC   XDA3INIT(8),0(R7)       PUT IN INITIATOR NAME
         MVC   XDA2INIT(8),0(R7)       PUT IN INITIATOR NAME
         MVC   XDA2JOB(8),8(R7)        PUT IN JOB-NAME
         MVC   XDA2STEP(8),16(R7)      PUT IN STEP-NAME
         MVC   XDA2PROC(8),=CL8' '     BLANK PROC-NAME
         MVC   XDA2RSI(17),MASK        INITIALIZE MASK FIELDS
         LR    R7,TCBR                 SAVE TCB-REGISTER
DATCB1   EQU   *
         LR    TCBR,R7                 SET LOWER TCB ADDRESS
         L     R7,TCBLTC(TCBR)         STEP DOWN TO LOWER TCB
         LTR   R7,R7                   IS THERE ANY LOWER TCB
         BNZ   DATCB1                  YES, SEARCH FOR NEXT
         L     R7,TCBTIO(TCBR)         GET TASK I/O TABLE ADDRESS
         C     R7,SAVETIO              SAME TCB AS BEFORE
         BE    DATCB11                 YES DO NOT PUT IN STEP-NAME
         MVC   XDA2STEP(8),8(R7)       PUT IN STEP-NAME
         MVC   XDA2PROC(8),16(R7)      PUT IN PROC-NAME
DATCB11  EQU   *
         IC    R8,TCBDSP(TCBR)         GET DISPATCHING PRTY
         CVD   R8,DOPWORT              CONVERT IT AND
         ED    XDA2DSP,DOPWORT+6       EDIT IT INTO MSG
         L     R7,TCBRBP(TCBR)         GET LAST CHAINED RB
         IC    R8,RBWCF(R7)            GET WAIT COUNT FIELD
         CVD   R8,DOPWORT              CONVERT IT AND
         ED    XDA2WCT,DOPWORT+6       EDIT IT INTO MSG
         SR    R8,R8                   CLEAR WORK-REGISTER
         L     R6,TCBPQE(TCBR)         POINT TO DUMMY PQE - 8
         LTR   R6,R6                   IS THERE ANY
         BZ    DATCB3                  NO, GO ON
         L     R7,8(R6)                ADDRESS OF FIRST PQE
DATCB2   EQU   *
         A     R8,PQESIZE(R7)          ADD REGION-SIZE OF PQE
         L     R7,PQEFPQE(R7)          ADDRESS OF NEXT PQE
         LTR   R7,R7                   IS THERE ANY
         BNZ   DATCB2                  YES, PROCESS IT
         SRL   R8,10                   CONVERT SIZE TO K-BOUNDARY
DATCB3   EQU   *
         CVD   R8,DOPWORT              CONVERT SIZE AND
         ED    XDA2RSI,DOPWORT+5       EDIT IT INTO MSG
         SPACE
         CLI   TEXT+3,C'T'             TEST OPTION
         BE    DATST                   YES
         SPACE
         L     R0,REG4                 GET MCS CONSOLE ID
         LA    R1,XDA2
         BAL   R14,WTOROUT
         SPACE
         CLI   TEXT+3,C'A'             TEST AND NORMAL OPTION
         BE    DATST                   YES DO BOTH
         SPACE
DATCBE   EQU   *
         LM    R2,R10,DASAVE           RESTORE REGISTERS
         BR    R9                      RETURN
         EJECT
DATST    EQU   *
*        TEST FLAGS
         SPACE
         LA    R1,TCBPKF(TCBR)         SHOW TCBFLAGS I
         BAL   R14,HEXBYTE
         MVC   XDA3FLG1,0(R15)
         BAL   R14,HEXBYTE             SHOW TCBFLAGS II
         MVC   XDA3FLG2,0(R15)
         LA    R1,TCBDAR(TCBR)         SHOW TCBDAR FLAGS
         BAL   R14,HEXBYTE
         MVC   XDA3DAR,0(R15)
         LA    R1,TCBCMP(TCBR)         SHOW COMPLETION CODES
         BAL   R14,HEXBYTE
         MVC   XDA3CMP,0(R15)
         SPACE
         L     R0,REG4                 GET MCS CONSOLE ID
         LA    R1,XDA3
         BAL   R14,WTOROUT
         SPACE
         B     DATCBE
         SPACE 3
HEXBYTE  EQU   *
         SPACE
*        CONVERT  X'0123ABCD'  --> C'0123ABCD'
         SPACE
         MVO   BYTES,0(4,R1)
         UNPK  DOPWORT,BYTES
         MVZ   DOPWORT,=8X'00'
         TR    DOPWORT,HEXTAB
         LA    R1,4(R1)
         LA    R15,DOPWORT
         BR    R14
         EJECT
DAASCB   EQU   *
         SPACE
*      SUBROUTIINE TO PROCESS ASCB'S
*      R2 - INIT COUNT
*      R6 - ASCB ADDRESS
*      R9 - RETURN ADDRESS
         SPACE
         MVC   XDA4,XDA4DS             MOVE HEADER TO E-FIELD
         LM    R7,R8,ASCBJBNI(R6)      LOAD ADDRESSES OF JOBNAMES
         CLC   0(8,R8),=CL8'INIT'      SECOND JOBNAME = 'INIT'?
         BNE   NOTINIT                 BR IF NO
         LA    R2,1(,R2)               INCREMENT INIT COUNT
         LTR   R7,R7                   ADDR OF FIRST = 0?
         BNZ   JOBDISP                 BR IF NO
         TM    FUNCIND,DISPSYS         ARE SYS ENTRIES TO BE DISPL.?
         BO    NOTINIT                 BR IF YES
         BR    R9
JOBDISP  TM    FUNCIND,DISPJOB         ARE JOBS TO BE DISPLAYED?
         BCR   8,R9                    BR IF NO
***      MVC   XDA4TYP,=CL3'JOB'       MOVE TYPE          ICR 09.07.83
         MVC   XDA4TYP,=CL1'J'         MOVE TYPE          ICR 09.07.83
         LA    R0,8                    R0 = 8
         SLR   R7,R0                   R7 = ADDR (CSCB)
         MVC   XDA4JOBN,CHKEY(R7)      MOVE JOBNAME
         MVC   XDA4STPN,CHSTEP(R7)     MOVE STEPNAME
         MVC   XDA4PRCN,CHPROCSN(R7)   MOVE PROCSTEPNAME
         B     DPRTYEDT
NOTINIT  L     R7,ASCBCSCB(R6)         LOAD CSCB ADDR
         CLI   0(R8),C'*'              '*MASTER*' OR '*AUXSTM*'?
         BNE   NOTSYS                  BR IF NO
         TM    FUNCIND,DISPSYS         ARE 'SYS'-ENTRIES TO BE DISPL.?
         BCR   8,R9                    BR IF NO
         MVC   XDA4JOBN,0(R8)          MOVE JOBNAME
         B     DPRTYEDT
NOTSYS   CLI   CHTRKID(R7),CHTSID      TSU ID?
         BE    TSUSAVE                 BR IF YES
         CLI   CHTRKID(R7),CHJOBID     JOB ID?
         BE    STCDISP                 BR IF NO
         CLI   CHTRKID(R7),CHINITID    INIT ID?
         BCR   7,R9                    BR IF NO
         TM    FUNCIND,DISPSYS         ARE 'SYS'-ENTRIES TO BE DISPL.?
         BO    SYSDISP                 BR IF YES
         BR    R9
STCDISP  TM    FUNCIND,DISPSTC         ARE STC'S TO BE DISPLAYED?
         BCR   8,R9                    BR IF NO
***      MVC   XDA4TYP,=CL3'STC'       MOVE TYPE          ICR 09.07.83
         MVC   XDA4TYP,=CL1'S'         MOVE TYPE          ICR 09.07.83
SYSDISP  MVC   XDA4JOBN,CHCLS(R7)      MOVE JOBNAME
         MVC   XDA4STPN,CHKEY(R7)      MOVE STEPNAME
         MVC   XDA4PRCN,CHPROCSN(R7)   MOVE PROCSTEPNAME
DPRTYEDT EQU   *
         SR    R0,R0                   CLEAR R0BG
         IC    R0,ASCBDP(R6)           INSERT DISP PRIORITY
         CVD   R0,DOPWORT              CONVERT TO DEC
         OI    DOPWORT+7,X'0F'         SIGN = F
         UNPK  XDA4DP,DOPWORT          UNPK DISP PRTY
         LH    R0,ASCBFMCT(R6)         LOAD ALLOC. PAGE FRAME COUNT
         SLA   R0,2
         CVD   R0,DOPWORT              CONVERT TO DEC
         MVC   MASKAREA,=X'402020202120'  MOVE MASK
         ED    MASKAREA,DOPWORT+5      ED FRAME COUNT
         MVC   XDA4REAL(4),MASKAREA+2  MOVE FRAME COUNT
         MVI   XDA4REAL+4,C'K'         MOVE 'K'
         TM    ASCBRCTF(R6),ASCBOUT    ADDRESS SPACE CONSIDERED OUT?
         BZ    *+10                    BR IF NO
         MVC   XDA4REAL,=CL5' SWAP'    MOVE TEXT
*
         TM    FUNCIND,DISPPAGE        DISPLAY PAGE?
         BO    PGDISP                  BR IF YES
         TM    FUNCIND,DISPTIME        DISPLAY TIME?
         BZ    SCDISP                  BR IF NO
         L     R1,12(,R10)             LOAD TIMER DIFF.
         M     R0,=F'400'              MULTIPLY WITH 400
         D     R0,TIMESUM              DIVIDE BY TIMER SUM
         LA    R1,2(,R1)               ROUND
         SRA   R1,2                    DIVIDE BY 4
         CVD   R1,DOPWORT              CONVERT TO DECIMAL
         MVC   XDA4CPU-1(4),=X'40202120'  MOVE ED MASK
         ED    XDA4CPU-1(4),DOPWORT+6  ED
         MVI   XDA4P100,C'%'           MOVE '%'
*
***      CLI   IOSW,X'FF'              NO IO'S ?
***      BNE   NOIO3                   NO, BRANCH AROUND IO-PRESENTING
         L     R1,144(R6)    A(OUCB)
         L     R1,108(R1)    EXCP-COUNT
         CVD   R1,DOPWORT
***      MVC   MASKAREA-3(9),=X'402020202020202120'     ICR 09.07.83
***      ED    MASKAREA-3(9),DOPWORT+4                  ICR 09.07.83
***      MVC   XDA4CPUT+2(6),MASKAREA                   ICR 09.07.83
*
         MVC   MASKAREA-2(8),=X'4020202020202120'       ICR 09.07.83
         ED    MASKAREA-2(8),DOPWORT+4                  ICR 09.07.83
***      MVC   XDA4CPUT+1(6),MASKAREA                   ICR 09.07.83
         MVC   XDA4EXCP+1(6),MASKAREA                   ICR 09.07.83
*        B     OUTPUT                                   ICR 04.08.83
NOIO3    EQU   *
         L     R0,8(,R10)              LOAD TCB CPU TIMER VALUE
         SRDA  R0,22                   VALUE IN MICROSEC
         D     R0,=F'1000000'          VALUE IN SECONDS
***      MVI   XDA4SM,C'"'             MOVE SEC SIGN    ICR 09.07.83
         MVI   XDA4SM,C' '         NO MORE  SEC SIGN    ICR 09.07.83
         SR    R0,R0                   CLEAR R0 FOR DIV
         D     R0,=F'60'               VALUE IN CENTIMIN
         MH    R1,=H'100'              SHIFT MINUTES
         AR    R1,R0                   R1 = MIN * 100 + SEC
         CVD   R1,DOPWORT              CONVERT TO DECIMAL
***      MVC   MASKAREA-1(7),=X'402020207D2120'  MOVE EDIMASK9.07.83
         MVC   MASKAREA-1(7),=X'402020207A2120'  MOVE EDIMASK9.07.83
         ED    MASKAREA-1(7),DOPWORT+5 ED TIMER VALUE
         MVC   XDA4CPUT,MASKAREA       MOVE TO MSG
         B     OUTPUT
*
PGDISP   L     R1,12(,R10)             LOAD 2. PAGE-IN VALUE
         L     R14,0(,R10)             LOAD 1. PAGE-IN VALUE
         L     R15,24(,R10)            LOAD TOD DIFFERENCE
         BAL   R8,BPAGEVAL             BUILD PAGE/SEC VALUE
         MVC   XDA4PIN,MASKAREA+1      MOVE TO MSG
         L     R1,16(,R10)             LOAD 2. PAGE-OUT VALUE
         L     R14,4(,R10)             LOAD 1. PAGE-OUT VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE/SEC VALUE
         MVC   XDA4POUT,MASKAREA+1     MOVE TO MSG
         L     R1,20(,R10)             LOAD 2. PAGE-REC VALUE
         L     R14,8(,R10)             LOAD 1. PAGE-REC VALUE
         BAL   R8,BPAGEVAL             BUILD PAGE/SEC VALUE
         MVC   XDA4PREC,MASKAREA+1     MOVE TO MSG
         B     OUTPUT
*
SCDISP   LH    R0,ASCBNVSC(R6)         LOAD ALLOC. AUX. SLOT COUNT
*                                          (NON-VAM)
         CVD   R0,DOPWORT              CONVERT TO DEC
         OI    DOPWORT+7,X'0F'         SIGN = F
         UNPK  XDA4NVSC,DOPWORT        UNPK SLOT COUNT
         LH    R0,ASCBVSC(R6)          LOAD ALLOC. AUX. SLOT COUNT
*                                          (VAM)
         CVD   R0,DOPWORT              CONVERT TO DEC
         OI    DOPWORT+7,X'0F'         SIGN = F
         UNPK  XDA4VSC,DOPWORT         UNPK SLOT COUNT
         MVI   XDA4SM,C' '
*
OUTPUT   EQU   *
         CLI   HEADSW,C'Y'             HAS HEADLINE BEEN WRITTEN?
         BE    OUTMESS4                BR IF YES
         MVI   HEADSW,C'Y'             SET SWITCH
         L     R0,REG4                 LOAD CONSOLE ID
         TM    FUNCIND,DISPTIME        TIME DISPLAY
         BO    OUTMESS1                BR IF YES
         TM    FUNCIND,DISPPAGE        PAGE DISPLAY
         BO    OUTMESS2                BR IF YES
         LA    R1,XDA1S
         BAL   R14,WTOROUT
         B     OUTMESS4
OUTMESS1 EQU   *
         LA    R1,CVTPTR               GET ADDRESS OF CVT-POINTER.01.84
         L     R1,0(R1)                GET CVT-ADDRESS     ICR 23.01.84
         L     R1,CVTOPCTP(R1)         GET RMCT            ICR 23.01.84
         L     R1,RMCTCCT(R1)          GET CCT             ICR 23.01.84
         MVC   CPUTIL(2),CCVUTILP(R1)  GET CPU UTILIZATION ICR 23.01.84
         SR    R1,R1                   CLEAR R1            ICR 23.01.84
         ICM   R1,B'0011',CPUTIL       INSERT CPU UTILIZ.  ICR 23.01.84
         CVD   R1,DOPWORT              CONVERT TO DEC      ICR 23.01.84
         OI    DOPWORT+7,X'0F'         SIGN = F            ICR 23.01.84
         MVC   XDA1CPU(3),=C' ->'      LEFT  EYE-CATCHER   ICR 23.01.84
         MVC   MASKAREA+2(4),=X'40202120'                  ICR 23.01.84
         ED    MASKAREA+2(4),DOPWORT+6                     ICR 23.01.84
         MVC   XDA1CPU+3(3),MASKAREA+3      CPU UTILIZAT.  ICR 23.01.84
         MVC   XDA1CPU+6(3),=C' <-'    RIGHT EYE-CATCHER   ICR 23.01.84
*                                                          ICR 23.01.84
         LA    R1,XDA1T
         BAL   R14,WTOROUT
         B     OUTMESS4
OUTMESS2 EQU   *
         LA    R1,XDA1P
         BAL   R14,WTOROUT
OUTMESS4 EQU   *
         L     R0,REG4                 LOAD CONSOLE ID
         LA    R1,XDA4
         BAL   R14,WTOROUT
         BR    R9
*
TSUSAVE  EQU   *                       SAVE TS USER ID
         LA    R3,1(,R3)               INCREMENT TS USER COUNT
         TM    FUNCIND,DISPTSU         FUNCTION = TSU?
         BCR   14,R9                   BR IF NO
***      MVC   XDA4TYP,=CL3'TSU'       MOVE TYPE          ICR 09.07.83
         MVC   XDA4TYP,=CL1'T'         MOVE TYPE          ICR 09.07.83
         MVC   XDA4JOBN,CHKEY(R7)      MOVE JOBNAME
         MVC   XDA4STPN,CHSTEP(R7)     MOVE STEPNAME
         MVC   XDA4PRCN,CHCLS(R7)      MOVE PROCNAME
*                                                       ICR 09.07.83
         L     R1,144(R6)    A(OUCB)                    ICR 09.07.83
         L     R1,140(R1)    LAST RESPONSE-TIME         ICR 09.07.83
         CVD   R1,DOPWORT                               ICR 09.07.83
         OI    DOPWORT+7,X'0F'                          ICR 09.07.83
         MVC   MASKAREA-2(8),=X'402020204B212020'     ICR 09.07.83
         ED    MASKAREA-2(8),DOPWORT+4
         MVC   XDA4STPN+0(8),MASKAREA-2  MOVE RESP.TIME ICR 09.07.83
         B     DPRTYEDT
DSPCBS   EQU   *
         L     R0,REG4
         LA    R1,XDA1CB
         BAL   R14,WTOROUT
         L     R4,CVTASVT(R3)
         LA    R5,ASVTENTY(R4)
         L     R7,ASVTMAXU(R4)
         MH    R7,=H'4'
         AR    R7,R5
         ST    R7,MAXADR
CBASVTL  EQU   *
         TM    0(R5),ASVTAVAL
         BO    CBNXTE
         L     R6,0(R5)
         CLC   ASCBASID(2,R6),=H'0'
         BE    CBNXTE
         MVC   XDA4,XDA4DS
         LM    R7,R8,ASCBJBNI(R6)
         CLC   0(8,R8),=CL8'INIT'
         BNE   CBNOINIT
         LTR   R7,R7
         BZ    CBNXTE
***      MVC   XDA4TYP,=CL3'JOB'                          ICR 09.07.83
         MVC   XDA4TYP,=CL1'J'                            ICR 09.07.83
         LA    R0,8
         SLR   R7,R0
CBNAME2  EQU   *
         MVC   XDA4PRCN,CHPROCSN(R7)
CBNAME1  EQU   *
         MVC   XDA4STPN,CHSTEP(R7)
         MVC   XDA4JOBN,CHKEY(R7)
         B     CBASID
CBNOINIT EQU   *
         L     R7,ASCBCSCB(R6)
         CLI   CHTRKID(R7),CHTSID
         BNE   CB1
***      MVC   XDA4TYP,=CL3'TSU'                          ICR 09.07.83
         MVC   XDA4TYP,=CL1'T'                            ICR 09.07.83
         MVC   XDA4PRCN,CHCLS(R7)
         B     CBNAME1
CB1      EQU   *
         CLI   CHTRKID(R7),CHJOBID
         BNE   CB2
***      MVC   XDA4TYP,=CL3'STC'                          ICR 09.07.83
         MVC   XDA4TYP,=CL1'S'                            ICR 09.07.83
CB2      EQU   *
         MVC   XDA4JOBN,CHCLS(R7)
         MVC   XDA4STPN,CHKEY(R7)
         MVC   XDA4PRCN,CHPROCSN(R7)
CBASID   EQU   *
         LH    R0,ASCBASID(R6)
         CVD   R0,DOPWORT
         OI    DOPWORT+7,X'0F'
         UNPK  XDA4DP,DOPWORT
         CLC   XDA4DP(3),=CL3'001'
         BNE   NOTMSTR
         MVC   XDA4JOBN,=CL8'*MASTER*'
***      MVC   XDA4TYP,=CL3'SYS'                          ICR 09.07.83
         MVC   XDA4TYP,=CL3'Y'                            ICR 09.07.83
         MVC   XDA4STPN,=CL8'SCHEDULR'
         MVC   XDA4PRCN,=CL8' '
NOTMSTR  EQU   *
         ST    R6,DOPWORT
         UNPK  XDA4DP+4(9),DOPWORT(5)
         TR    XDA4DP+4(8),HEXTAB-240
         MVI   XDA4DP+12,C' '
         L     R0,REG4
         LA    R1,XDA4
         BAL   R14,WTOROUT
CBNXTE   EQU   *
         LA    R5,4(R5)
         C     R5,MAXADR
         BL    CBASVTL
         B     DA2
         EJECT
BTODDIFF LM    R0,R1,TOD1              LOAD TOD-CLOCK VAL 1
BTODDIF1 LM    R14,R15,TOD2            LOAD TOD-CLOCK VAL 2
BTODDIF2 SLR   R15,R1                  SUBTRACT 2. WORDS
         LA    R1,1                    PREPARE FOR NO CARRY
         BC    3,*+6                   BR IF CARRY
         SLR   R14,R1                  SUB ONE - NO CARRY
         SLR   R14,R0                  SUBTRACT 1. WORDS
         SRDA  R14,22                  R15, BIT 31 = 1024 MICROSEC
         ST    R15,TODDIFF             STORE TOD-CLOCK DIFFERENCE
         BR    R2                      RETURN
*
BPAGEVAL SR    R1,R14                  DIFFERENCE OF PAGE VALUES
         BNM   *+6                     BR IF >= 0
         SR    R1,R1                   SET TO ZERO IF NEGATIVE
         M     R0,=F'10240'            MULTIPLY BY SEC/10 * 1024
         DR    R0,R15                  DIVIDE BY TOD DIFFERENCE
         CVD   R1,DOPWORT              CONVERT TO DECIMAL
BPAGEVL1 MVC   MASKAREA-1(7),=X'40202021204B20' MOVE ED MASK
         ED    MASKAREA-1(7),DOPWORT+5 ED PG/SEC VALUE
         BR    R8
WTOROUT  EQU   *
         ST    R14,SAVE14
         L     R0,REG4
         C     R0,=CL4'ROSC'
         BNE   NOROS
         STM   R14,R12,12(R13)                   SAVE ALL REGS IN SA
*
*              NOW WE CAN ADDRESS DUMMYSECTION IN XMON
*
         LH    R0,0(R1)      LOAD LENGTH OF WTO
         SH    R0,=H'4'      DECREMENT BY 4
         STC   R0,3(R1)      INSERT LENGTH TO MESSAGE FOR USE BY ROSCOE
         LA    R0,3(R1)      LOAD MESSAGE ADDRESS
         L     R13,4(R13)    R13 --> SA IN XMON
         ST    R0,76(R13)    STORE IT IN DYN AREA OF XMON = INAREA
         OI    76(R13),X'80' SET HIGHORDER BIT ON
         L     R15,100(R13)  LOAD ROSCOE-CODE <<OUTCODE>>
         LA    R1,76(R13)    LOAD PARAMETER REGISTER
         L     R13,4(R13)    R13 --> SA IN ROSCOE
         L     R14,12(R13)   LOAD RETURNADDRESS
         LM    R2,R12,28(R13)                    LOAD REMAINDER
         BR    R14           RETURN DIRECT TO ROSCOE
NOROS    EQU   *
         C     R0,=CL4'TSO'
         BNE   NOTSO
         LH    R0,0(R1)
         SH    R0,=H'4'
         LA    R1,4(R1)
         SVC   93             TPUT
         L     R14,SAVE14
         BR    R14
NOTSO    EQU   *
         WTO   MF=(E,(1))
         L     R14,SAVE14
         BR    R14
ONESECND DC    F'100'                  100 CENTISECONDS
THREESEC DC    F'300'                  300 CENTISECONDS
         PRINT DATA                                        ICR 04.08.83
DISPOPT  DS    0CL10
         DC    AL1(3),CL8'JOB',AL1(DISPJOB)
         DC    AL1(3),CL8'STC',AL1(DISPSTC)
         DC    AL1(3),CL8'TSU',AL1(DISPTSU)
         DC    AL1(3),CL8'ALL',AL1(DISPALL)
         DC    AL1(3),CL8'SYS',AL1(DISPSYS)
         DC    AL1(4),CL8'TIME',AL1(DISPTME1)
         DC    AL1(7),CL8'SRBTIME',AL1(DISPSRB1)
         DC    AL1(2),CL8'IO',AL1(DISPTME1)
         DC    AL1(4),CL8'PAGE',AL1(DISPPAG1)
ANZDOPT  EQU   (*-DISPOPT)/L'DISPOPT
         PRINT NODATA                                      ICR 04.08.83
CPUTIL   DC    XL2'0'                                      ICR 23.01.84
HEXTAB   DC    C'0123456789ABCDEF'     HEXADECIMAL TRANSLATE TABLE
MASK     DC    X'402020202120D240404020212040202120'
         SPACE
         SPACE
XDA1CB   WTO   'XDA1 T JOB-NAME STEPNAME PROCNAME ASID A(ASCB)         *
                ',MCSFLAG=REG0,MF=L
XDA1S    WTO   'XDA1 T JOB-NAME STEPNAME PROCNAME  DP  REAL NVSC VIOSC *
                        ',MCSFLAG=REG0,MF=L
         SPACE
XDA1T    WTO   'XDA1 T JOB-NAME STEPNAME PROCNAME  DP  REAL CPU% CPU-M *
                        ',MCSFLAG=REG0,MF=L
XDA1CPU  EQU   XDA1T+29                                    ICR 23.01.84
         SPACE
XDA1P    WTO   'XDA1 T JOB-NAME STEPNAME PROCNAME  DP  REAL  IN OUT REC*
                        ',MCSFLAG=REG0,MF=L
         SPACE
XDA2DS   WTO   'XDA2                                                   *
                        ',                                             *
               MCSFLAG=REG0,MF=L
XDA2L    EQU   *-XDA2DS
         SPACE
XDA3DS   WTO   'XDA3              PK-FLG                   CMP         *
                DAR      ',                                            *
               MCSFLAG=REG0,MF=L
XDA3L    EQU   *-XDA3DS
         SPACE
XDA4DS   WTO   'XDA4                                                   *
                        ',MCSFLAG=REG0,MF=L
XDA4L    EQU   *-XDA4DS
         SPACE
XDA5DS   WTO   'XDA5 -TYPE-   -IN- -OUT- -REC-    -I/O- TOTAL',        *
               MCSFLAG=REG0,MF=L
XDA5L    EQU   *-XDA5DS
         SPACE
XDA6DS   WTO   'XDA6 XXXXXXX ZZ9.9 ZZ9.9 ZZ9.9    ZZ9.9 ZZ9.9',        *
               MCSFLAG=REG0,MF=L
XDA6L    EQU   *-XDA6DS
         SPACE
         LTORG
         EJECT
*        EQUATES
         SPACE
*CVT
CVTPTR   EQU   X'10'                   CVT - POINTER
CVTDCB   EQU   X'74'                   ADDRESS OF OPERATING SYSTEM
CVTMVS2  EQU     B'00000001'           MULTIPLE MEMORY OPTION OF
*                                          OS/VS2 IS PRESENT
CVTHEAD  EQU   X'A0'                   ADDRESS OF FIRST TCB IN SYSTEM
CVTPVTP  EQU   X'164'                  ADDRESS OF PVT
*                                        (PAGE VECTOR TABLE)
CVTASVT  EQU   X'22C'                  ADDRESS OF ASVT
*                                        (ADDRESS SPACE VECTOR TABLE)
CVTOPCTP EQU   X'25C'                  ADDRESS OF RMCT     ICR 23.01.83
*                                        (SRM CONTROL TABLE) ICR 23.013
         SPACE
*TCB
TCBR     EQU   4                       TCB IN PROCESS REGISTER
OTCBR    EQU   5                       ORIGINATING TCB REGISTER
TCBRBP   EQU   X'00'                   ADDRESS OF REQUEST BLOCK
TCBTIO   EQU   X'0C'                   ADDRESS OF TASK I/O TABLE
TCBCMP   EQU   X'10'                   TASK COMLPETION CODE
TCBPKF   EQU   X'1C'                   PROTECTION KEY
TCBFLGS  EQU   X'1D'                   TASK END, ... FLAGS
TCBDSP   EQU   X'23'                   DISPATCHING PRIORITY
TCBNTC   EQU   X'80'                   ADDR OF PREVIOUS TCB ON SUBTASK
TCBOTC   EQU   X'84'                   ADDR OF ORIGINATING TCB
TCBLTC   EQU   X'88'                   ADDR OF LAST TCB ON SUBT. QUEUE
TCBPQE   EQU   X'98'                   ADDR OF DUMMY PQE MINUS 8
TCBDAR   EQU   X'AC'                   DAR FLAGS ×× SEC. NONDISP. BUTS
         SPACE
*RB
RBWCF    EQU   X'1C'                   WAIT COUNT FIELD
         SPACE
*PQE
PQEFPQE  EQU   X'08'                   ADDR OF NEXT PQE
PQESIZE  EQU   X'14'                   SIZE OF REG IN 2K MULTIPLES
         SPACE
*PVT
PVTNPIN  EQU   X'F8'                   # PAGE-IN, EXCL. SWAP AND VIO
PVTNPOUT EQU   X'FC'                   # PAGE-OUT, EXCL. SWAP AND VIO
PVTVAMI  EQU   X'100'                  # VIO PAGE-IN, EXCL. SWAP
PVTVAMO  EQU   X'104'                  # VIO PAGE-OUT, EXCL. SWAP
PVTVAMR  EQU   X'108'                  # VIO RECLAIMS
PVTSPIN  EQU   X'10C'                  # PAGES SWAPPED IN
PVTSPOUT EQU   X'110'                  # PAGES SWAPPED OUT
PVTNPREC EQU   X'114'                  # PAGES RECLAIMED
PVTNSWPS EQU   X'118'                  # SWAP-IN
PVTCAIN  EQU   X'11C'                  # CA NON-VIO PAGE-IN
PVTCAOUT EQU   X'120'                  # CA NON-VIO PAGE-OUT
PVTCAREC EQU   X'124'                  # CA NON-VIO RECLAIM
*PVTCAVI  EQU   X'128'                  # CA VIO PAGE-IN
*PVTCAVO  EQU   X'12C'                  # CA VIO PAGE-OUT
*PVTCAVR  EQU   X'130'                  # CA VIO RECLAIM
         SPACE
*OUXB
OUXBPIN  EQU   X'10'                   INTV PAGE-IN ACCU
OUXBPOUT EQU   X'14'                   INTV PAGE-OUT ACCU
OUXBPREC EQU   X'18'                   INTV RECLAIM ACCU
OUXBVAMI EQU   X'1C'                   INTV VIO PAGE-IN ACCU
OUXBVAMO EQU   X'20'                   INTV VIO PAGE-OUT ACCU
OUXBVAMR EQU   X'24'                   INTV VIO RECLAIM ACCU
OUXBCAPI EQU   X'28'                   INTV CA PAGE-IN ACCU
OUXBCAPR EQU   X'2C'                   INTV CA RECLAIM ACCU
         EJECT
*ASVT
ASVTMAXU EQU   X'204'                  MAX. NUMBER OF ADDRESS SPACES
ASVTENTY EQU   X'210'                  ENTRY FOR EACH POSSIBLE ASID
*                                          IF ASSIGNED: A(ASCB)
*                                          IF ^: A(NEXT AV. ASID)
ASVTAVAL EQU   B'10000000'             ASID AVAILABLE BIT
*                                          IF ASSIGNED: 0
*                                          IF ^: 1
         SPACE 2
*ASCB
ASCBASID EQU   X'24'                   ASID (ADDRESS SPACE IDENTIFIER)
ASCBSEQN EQU   X'26'                   SEQUENCE NUMBER
*                                          ASCB'S POSITION ON THE
*                                          DISPATCHING QUEUE
ASCBDP   EQU   X'2B'                   DISPATCHING PRIORITY
ASCBCSCB EQU   X'38'                   ADDRESS OF CSCB
ASCBEJST EQU   X'40'                   ELAPSED JOB STEP TIMING
ASCBRCTF EQU   X'66'                   FLAGS FOR RCT
ASCBOUT  EQU     B'00000100'               ADDRESS SPACE SWAPPED OUT
ASCBTMLW EQU     B'00000010'               MEMORY IS IN A LONG WAIT
ASCBFLG1 EQU   X'67'                   FLAG FIELD
ASCBSTND EQU     B'00000100'               TCB'S NON-DISPATCHABLE
ASCBVSC  EQU   X'78'                   ALLOC. AUX. SLOT COUNT (VAM)
ASCBNVSC EQU   X'7A'                   ALLOC. AUX. SLOT COUNT (NON-VAM)
ASCBOUXB EQU   X'94'                   SRM USER EXT BLOCK PTR
ASCBFMCT EQU   X'98'                   ALLOCATED PAGE FRAME COUNT
ASCBJBNI EQU   X'AC'                   POINTER TO JOBNAME FIELD FOR
*                                          INITIATED PROGRAMS OR ZERO
ASCBJBNS EQU   X'B0'                   POINTER TO JOBNAME FIELD FOR
*                                          START/MOUNT/LOGON OR ZERO
ASCBSRBT EQU   X'C8'                   ACCUMULATED SRB TIME
         SPACE 2
         SPACE
*RMCT
RMCTCCT  EQU   4                       ADDRESS OF CCT      ICR 23.01.84
*                                        (SRM CPU  MGMT    ICR 23.01.84
*                                          CONTROL TABLE)  ICR 23.01.84
         SPACE
*CCT
CCVUTILP EQU   X'66'                   SYSTEM CPU          ICR 23.01.84
*                                        UTILIZATION       ICR 23.01.84
         SPACE
*CSCB
CHKEY    EQU   X'08'                   1. ID OF A STARTED TASK
*                                  (THIS ID IS THE TASK'S STEPNAME)
*                                      2. JOBNAME OF AN EXECUTED JOB
*
CHCLS    EQU   X'10'                   1. PROCNAME OF A STARTED TASK
*                                  (THE PROCNAME IS THE TASK'S JOBNAME)
*                                      2. JOBNAME OF AN EXECUTED JOB
*                                          (SAME AS CHKEY)
CHUNIT   EQU   X'18'                   UNITNAME (SET FOR STARTED TASKS)
CHTRKID  EQU   X'1C'                   DISPLAY/TRACK IDENTIFIER
CHTSID   EQU     X'01'                     TSU IDENTIFIER
CHJOBID  EQU     X'02'                     JOB IDENTIFIER
CHINITID EQU     X'03'                     INITIATOR IDENTIFIER
CHASID   EQU   X'1E'                   ASID
CHPROCSN EQU   X'20'                   PROCEDURE STEP NAME
CHSTEP   EQU   X'40'                   STEP NAME
         EJECT
         REG
         SPACE 2
         XPARM
         SPACE 2
WORK     DSECT
SAVEAREA DS    18F                     SAVEAREA I
SAVE14   DS    F
DASAVE   DS    9F                      SAVEAREA II
SAVETIO  DS    F                       SAVEAREA FOR TIOT ADDRESS
DOPWORT  DS    D                       D-WORD FOR CVD
MAXADR   DS    F
BYTES    DS    CL5                     WORK-BYTES FOR HEXBYTE
MASKAREA DS    CL6                     WORKAREA FOR ED-MASK
IOSW     DS    X
*
FUNCIND  DS    CL1                     FUNCTION INDICATOR
DISPJOB  EQU     X'80'                     DISPLAY JOBS
DISPSTC  EQU     X'40'                     DISPLAY STC
DISPTSU  EQU     X'20'                     DISPLAY TSU
DISPSYS  EQU     X'10'                     DISPLAY SYS
***PDFLT EQU     X'C0'                     DEFAULT (JOB, STC) ICR 17.7
DISPDFLT EQU     X'80'                     DEFAULT (JOB )  ICR 01.11.83
DISPALL  EQU     X'E0'                     DISPLAY ALL
DISPTIME EQU     X'08'                     DISPLAY TIME
DISPSUPP EQU     X'01'                     FIRST ENTRY INDICATOR
DISPSRBT EQU     X'02'                     DISPLAY SRBTIME
DISPPAGE EQU     X'04'                     DISPLAY PAGING ACTIVITY
DISPPAG1 EQU     X'05'                     DISPLAY PAGING, FIRST ENTRY
DISPSRB1 EQU     X'0B'                     DISPLAY SRBTIME, FIRST ENTRY
DISPTME1 EQU     X'09'                     DISPLAY TIME, FIRST ENTRY
*
SPAGEIN  DS    PL4                     PAGE-IN/SEC COUNTER
SPAGEOUT DS    PL4                     PAGE-OUT/SEC COUNTER
SPAGEREC DS    PL4                     PAGE-REC/SEC COUNTER
SPAGEIO  DS    PL4                     PAGE-IO/SEC COUNTER
SPAGETOT DS    PL4                     TOTAL PAGING/SEC COUNTER
*
         DS    0F
XDA2     DS    0C                      START OF XDA2 MESSAGE
         DS    CL9                     MSG-LENGTH, MCSFLAGS MSG-ID
XDA2INIT DS    CL8                     INITIATOR NAME
         DS    CL3
XDA2PKF  DS    C                       PROTECTION KEY
         DS    C
XDA2JOB  DS    CL8                     JOB-NAME
         DS    C
XDA2STEP DS    CL8                     STEP-NAME
         DS    CL2
XDA2PROC DS    CL8                     PROC-NAME
         DS    C
XDA2RSI  DS    CL6                     REGION SIZE
         DS    CL3
XDA2DSP  DS    CL4                     DISPATCHING PRTY
XDA2WCT  DS    CL4                     WAIT COUNT
         DS    C
XDA2STAT DS    CL10                    STATUS
         DS    CL6                     DESCRIPTOR CODE, ...
         DS    0F
XDA3     DS    0C                      START OF XDA3 MESSAGE
         DS    CL9
XDA3INIT DS    CL8                     INITIATOR NAME
         DS    CL9
XDA3FLG1 DS    CL8                     PK-FLG I
         DS    C
XDA3FLG2 DS    CL8                     FLG II
         DS    CL5
XDA3CMP  DS    CL8                     TCB COMPL. CODES
         DS    CL5
XDA3DAR  DS    CL8                     DAR FLAGS
         DS    CL6                     DESCRIPTOR CODE, ...
         ORG   XDA2
*
XDA4     DS    0CL(XDA4L)
         DS    CL9                     L OF MSG, FLAGS, 'XDA4 '
****                                                       ICR 09.07.83
**A4TYP  DS    CL3                     TYPE (JOB, STC, TSU)ICR 09.07.83
XDA4TYP  DS    CL1                     TYPE (JOB, STC, TSU)ICR 09.07.83
         DS    CL1
XDA4JOBN DS    CL8                     JOBNAME
         DS    CL1
XDA4STPN DS    CL8                     STEPNAME
         DS    CL1
XDA4PRCN DS    CL8                     PROC STEP NAME
         DS    CL1
XDA4DP   DS    CL3                     DISP PRIORITY
         DS    CL1
XDA4REAL DS    CL5                     ALLOCATED PAGE FRAME COUNT
*                                          (9999K)
         DS    CL1
XDA4NVSC DS    CL4                     ALLOCATED AUX. SLOT COUNT
*                                          (NON-VAM)
         DS    CL1
XDA4VSC  DS    CL5                     ALLOCATED AUX. SLOT COUNT (VAM)
         ORG   XDA4NVSC
XDA4CPU  DS    CL3                     CPU USAGE IN %
XDA4P100 DS    CL1                     '%'
XDA4CPUT DS    CL6                     TCB CPU TIME (ZZZ.99)
XDA4SM   DS    CL1                     " × ' × ' '
         DS    CL1
XDA4EXCP DS    CL8                     NO. OF EXCPS          ICR 9.7.83
         ORG   XDA4NVSC
XDA4PIN  DS    CL3                     PAGE-IN/SEC (ZZ9)
         DS    CL1
XDA4POUT DS    CL3                     PAGE-OUT/SEC (ZZ9)
         DS    CL1
XDA4PREC DS    CL3                     PAGE-REC/SEC (ZZ9)
*
         DS    CL3
         ORG   XDA2
*
XDA6     DS    0CL(XDA6L)
         DS    CL9                     L OF MSG, FLAGS, 'XDA6 '
XDA6TYPE DS    CL7                     TYPE (DEMAND×VIO×SWAP×*TOTAL*)
         DS    CL1
XDA6IN   DS    CL5                     PAGE-IN/SEC (ZZ9.9)
         DS    CL1
XDA6OUT  DS    CL5                     PAGE-OUT/SEC (ZZ9.9)
         DS    CL1
XDA6REC  DS    CL5                     RECLAIM/SEC (ZZ9.9)
         DS    CL4
XDA6PGIO DS    CL5                     PAGE-IO/SEC (ZZ9.9)
         DS    CL1
XDA6PAGE DS    CL5                     TOTAL PAGING RATE (ZZ9.9)
         ORG
*
PAGEVAL1 DS    8F                      PAGING STATISTICS (1. ENTRY)
PAGEVAL2 DS    8F                      PAGING STATISTICS (2. ENTRY)
*
ANZTBLK  EQU   100
         DS    0D
TIMEBLK  DS    0CL16
TIMECPU1 DS    F                       TCB CPU TIMER VALUE 1
TIMESRB1 DS    F                       SRB CPU TIMER VALUE 1
TIMECPU2 DS    F                       TCB CPU TIMER VALUE 2
TIMEDIFF DS    F                       VALUES 2 - VALUES 1
*
         DS    (ANZTBLK-1)CL(L'TIMEBLK) SPACE FOR THE REM. TIMEBLK'S
*
TIMESUM  DS    F                       SUM OF ALL TIME DIFFERENCES
PAGESUM  DS    F                       SUM OF ALL PAGE-IO DIFFERENCES
TOD1     DS    D                       TOD-CLOCK VALUE 1
TOD2     DS    D                       TOD-CLOCK VALUE 2
TODVALUE DS    D                       TOD-CLOCK VALUE
TODDIFF  DS    F                       DIFF OF TOD-CLOCK VALUES
HEADSW   DS    CL1                     "Y" INDICATES "HEADLINE WRITTEN"
         DS    CL3                     RESERVED
*
PAGEBLK  DS    0CL32
PAGEIN1  DS    F                       COUNT OF PAGE-IN 1
PAGEOUT1 DS    F                       COUNT OF PAGE-OUT 1
PAGEREC1 DS    F                       COUNT OF PAGE-REC 1
PAGEIN2  DS    F                       COUNT OF PAGE-IN 2
PAGEOUT2 DS    F                       COUNT OF PAGE-OUT 2
PAGEREC2 DS    F                       COUNT OF PAGE-REC 2
PAGETODD DS    0F                      TOD DIFFERENCE
PAGETOD1 DS    D                       TOD-VALUE 1
*
         DS    (ANZTBLK-1)CL(L'PAGEBLK) SPACE FOR THE REM. PAGEBLK'S
*
         DS    0D
WORKL    EQU   *-WORK                  WORK-AREA LENGTH
         SPACE 2
         END   DA
