./  ADD NAME=ULEO030S
//SVTAMASM JOB (1407,O230),'RECHTIEN',CLASS=4,NOTIFY=LTSOHI,
//             MSGCLASS=6,REGION=1024K
//ASM    EXEC  ASMFCL,MAC1='SYS1.AMODGEN',
//             PARM.ASM='OBJ,XREF(SHORT)',
//             PARM.LKED='NCAL,AC=1'
//ASM.SYSIN DD *
*          DATA SET PP18VTAM   AT LEVEL 001 AS OF 11/01/82
*          DATA SET IASXWR00   AT LEVEL 007 AS OF 01/03/82
*          DATA SET IASXWR00   AT LEVEL 005 AS OF 20/01/81
***********************************************************************
*        THIS PROGRAM WAS DESIGNED AS PART OF A DSPRINT REPLACEMENT.
*        IT IS USED AS A STARTED TASK, AUTHORIZED,
*          AND HAS XWTR PROPERTIES.
*        MY IMPLEMENTATION WAS TO LKED IT TO AN AUTHORIZED LIBRARY,
*          AS IASXWR00(TO PICK UP XWTR PROPERTIES).
*        ATTRIBUTES: RENT,REUS,REFR.     SEE NOTE.........
*        RESTRICTION: ONLY LOCAL 328XS SUPPORTED.
*                     4 CHAR FORM NUMBERS
*                     ONLY SKIP TO CHAN. 1 SUPPORTED.
*                     SOME CHRYSLER DEPENDANT CODE (F /CHRYSLER/)
*                                       TSO FIND UNDER EDIT.
*        JCL:
*          //IEFPROC  EXEC  PGM=IASXWR00,REGION=64K,DPRTY=14
*          //STEPLIB  DD    DSN=AUTHORIZED.LIBRARY.............
*          //* VOICE-OF-EXPERIENCE REAL XWTR WILL OPEN 1ST DD AS OUTPUT
*          //*   SO IF ABOVE STEPLIB DOESN'T HAVE IASXWR00 IN IT,
*          //*   AND STEPLIB IS FIRST,
*          //*   THE REAL XWTR WILL OPEN DIRECTORY AS OUTPUT.
*          //*   DOES AN OUTSTANDING JOB OF DESTROYING PDSS.
*          //*   DON'T APAR IT, DOC SAYS IEFRDER MUST BE FIRST.
*          //*   THATS WHAT YOU GET WITH A REL 17 WTR IN MVS!
*
*        STARTING:
*          S VTAMWTRN.P1
*
*          PROC MUST HAVE PARM='A=X------X,P=X------X' TO DEFINE
*          APPLID AND NIB NAME TO VTAM
*
*          FOLLOWING PARM-PARAMETERS ARE USED:
*            P  =  PRINTER-NAME
*            A  =  APPLICATION      DEFAULT = APPLE
*            C  =  CLASSES          DEFAULT = 8
*            D  =  DESTINATION      DEFAULT = LOCAL
*            F  =  FORM-NUMBER      DEFAULT = TSO1
*            S  =  SEPARATOR        DEFAULT = YES
*            L  =  PAGE-LENGTH      DEFAULT = 72
*            W  =  PAGE-WIDTH       DEFAULT = 132
*            T  =  TRACE            DEFAULT = NO
*        IN ALL PARM-PARAMETERS ARE MAX 8 CHAR ALLOWED
*
*        MODIFYING:
*          F P1,T=Y    (OR N)      TRACE ON OR OFF
*          F P1,F=9990             CHANGE FORM ONLY
*          F P1,C=1A               CHANGE CLASS(ES) ONLY
*          F P1,D=LOCAL            CHANGE DESTINATION ONLY
*          F P1,C=1A,F=9990        CHANGE BOTH
*          F P1,FLUSH              PURGE CURRENT JOB AND CONTINUE
*          F P1,DUMP               PROG EXCPT AND DUMP
*          F P1,FLUSH,PGMR00       SAME EXCEPT REQUEST FROM USER(PGMR00
*                                  MODIFY IS ISSUED FROM TSO CP.
*          F P1,STOP               TERM. AFTER FINISHING CURR. DATASET.
*        STOPPING:
*          P P1                    RE-QUEUE CURRENT JOB AND TERM. IMM.
*
*        WHY NOT DSPRINT?
*          NO ACCOUNTING (SMF CURRENTLY BEING ADD TO THIS PGM).
*          MOST TCAM FAILURES DUE TO DSPRINT CODE.
*          TSO USERS CAN BRING TCAM DOWN WITH TOO MUCH DATA.
*          DATA SENSITIVE CODE IN TCAM MSG HANDLER.
*          TCAM DISK REORG GETS OUTPUT OUT OF STEP.
*          TCAM WARM START LOOSES ALL OUTPUT.
*          SEPERATE QUEUE IN TCAM FOR OUTPUT VICE USING SPOOL.
*          WANT TO BE ABLE TO GET A LITTLE JCL ERROR PRINTED ON 3284.
*          I DONT LIKE TCAM - ONLY ONE IN SYSTEM, DIFFICULT TO TEST.
*          DSPRINT NOT SUPPORTED VERY WELL.
*          RINKY-DINKY METHOD OF PURGING PRINTER OUTPUT(OFF/ON)
*          IF A PRINTER GOES DOWN, NO ALTERNATE DEST, CANT USE 3211.
*          CAN'T GET A TSO SYSOUT D.S. PRINTED ON 3284.
*          IF YOU WANT MORE REASONS CALL ME:
*
*        CLARK HUNTER     PHONE (313-497-1342/0524/0533)
*        CHRYSLER SALES AND PARTS SERVICE DIVISION.
*        26311 LAWRENCE AVE.
*        CENTERLINE MICHIGAN 48015
*         CIMS 423-10-17
***********************************************************************
*
* AMENDED FOR VTAM BY P.V.GREENSTOCK - ELI LILLY & CO LTD
*                                      BASINGSTOKE
*                                      HANTS
*                                      ENGLAND(LAND OF THE ANGLES)
* I RUN OUT OF PATIENCE ON REENTRANT STUFF
* SO IT ISNT ANYMORE !!!!
*
***********************************************************************
*   CHANGE ACTIVITY LEONBERGER BAUSPARKASSE
*
*   SIMLOGON ADDED, TO FORCE FREE SLU FROM OTHER PLU
*   STARTLOGON ADDED, TO ALLOW LOGON-EXIT TO WORK
*   FIX SOME MISC BUGS
*   LOGON   EXIT ADDED
*   RELREQ  EXIT ADDED
*   TPEND   EXIT ADDED
*   SKIP TO NEXT PAGE DONE BY FORM-FEED
*
*   F VTAMWTR,P=XXXX (CHANGE PRINTER DESTINATION) NOT SUPPORTED
*
*   REGS USED
*        BASE   = R9,R10,R12
*        WORK   = R2,R3,R4,R5,R7,R8
*
*   REGISTER 6 IS ALWAYS USED AS BASE-REGISTER FOR EXIT-ROUTINES
*
*
*   YOU MUST LINK VTAMWTR TO AN AUTHORIZED LIBRARY AS  IASXWR00  TO
*   PICK UP EXTERNAL WRITER PROPERTIES OR MAKE AN ENTRY TO IEFSDPPT
*   WITH PROPERTIES LIKE IASXWR00.
*   VTAM-WRITER MUST RUN IN KEY 1 TO PREVENT S201 (INVALID POSTING)
*   MY PROPERTIES ARE: E5E3,C1D4,E6E3,D940,5810,FFFF,2000,0000
*
*   IF YOU MISS VTAM LOSTERM-EXIT: I HAD NO TIME, TO DO THIS.
*
*   IF YOU FIX SOME BUG, PLEASE CALL ME.
*   TEL: 07152/201-2698 RECHTIEN, LEONBERGER-BAUSPARKASSE,EDV-SY
*
***********************************************************************
         EJECT
         MACRO
         REGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
         MACRO
&NAME    TERME &OP=,&SAVE=,&LV=0,&MASK=NO
.* TERME WILL RESTORE REGISTERS, UNCHAIN SAVE AREAS, RELEASE DYNAMIC
.* STORAGE OBTAINED BY THE PRIME MACRO, RESET THE PGM MASK AND IN
.* GENERAL PERFORM THE NECESSARY EXIT LINKAGE.
.* TERME WILL FREE THE USERS SAVE AREA PLUS THE NUMBER OF BYTES
.* SPECIFIED BY THE LV OPERAND UNLESS THE USER IMPLIES THAT THE SAVE
.* AREA WAS NOT OBTAINED VIA A GETMAIN.  THE USER IMPLIES THIS BY
.* SPECIFYING SAVE= SOME VALUE OR SYMBOL.
.* IF THE USER SPECIFIED SAVE=NONE IN THE PRIME MACRO, HE SHOULD DO
.*  THE SAME FOR TERME.
.* THE USER MAY CHOOSE TO FOLLOW THE TERME MACRO WITH THE XCTL MACRO
.* RATHER THAN BRANCH ON 14.  THE USER ACCOMPLISHES THIS BY CODING
.* OP=XCTL.
         AIF   ('&NAME' EQ '').F
&NAME    DS    0H
.F       ANOP
         AIF ('&SAVE' EQ 'NONE').C
         L     13,4(0,13)              GET PTR TO USERS AREA
         STM   0,1,20(13)               TEMPORARY SAVE OF R0 AND R1
         AIF   ('&SAVE' NE '').C
         AIF   ('&LV'(1,1) EQ '(').E
         AIF   ('&LV' EQ '0').CONT
         AIF   ('&LV' GT '4023').ERR
         LA    0,&LV.+72(0,0)           PARAMETER FOR FREEMAIN
         AGO   .CONT
.E       ANOP
         AIF   ('&LV' EQ '(0)').CONT
         LA    0,72(&LV(1).,0)          PARAMETER FOR FREEMAIN
.CONT    ANOP
         L     1,8(0,13)                GET SAVE AREA ADDRESS
         LA    1,0(0,1)                 INDICATE FREEMAIN
         AIF   ('&LV' NE '0').CONTC
         L     0,0(0,1)                 LENGTH IS IN 1ST WORD OF AREA
.CONTC   ANOP
         STCM  15,7,17(13)              SAVE RETURN CODE 11-25-78
         SVC   10                       ISSUE SVC 10
         ICM   15,7,17(13)              RESTORE RETURN CODE 11-25-78
.C       ANOP
         AIF   ('&MASK' EQ 'NO').NOMASK
         L     14,16(0,13)              RESET
         SPM   14                       PGM MASK
.NOMASK  ANOP
         L     14,12(0,13)              RESTORE
         LM    0,12,20(13)              REGISTERS
         XC    8(4,13),8(13)            DEQUEUE SAVE AREA
         AIF   ('&OP' EQ 'XCTL').D
         BR    14                       RETURN TO CALLER
         MEXIT
.D       ANOP
         BALR  15,0                     ESTABLISH ADDRESSABILITY
         USING *,15
         MEXIT
.ERR     MNOTE 'LV GT 4023, LV REQUEST NOT HONORED'
         LA    0,72(0,0)                PARAMETER FOR FREEMAIN
         AGO   .CONT
         MEND
         MACRO
&NAME    PRIME &REG=12,&SAVE=,&ID=,&LV=0,&MASK=NO
.* PRIME WILL SAVE REGISTERS, CREATE A NEW SAVE AREA, CHAIN THE NEW
.* SAVE AREA TO THE CALLERS SAVE AREA, ESTABLISH ADDRESSABILITY,
.* OPTIONALLY OBTAIN ADDITIONAL STORAGE AND IN GENERAL PERFORM THE
.* NECESSARY ENTRY LINKAGE.
.* IF THE USER DOES NOT SPECIFY WHAT REGISTER HE REQUIRES FOR A BASE,
.* TWELVE IS USED.  NOTE..IF REG= IS SPECIFIED TO BE 0, 1, 13, 14, OR
.* 15  A WARNING MESSAGE IS ISSUED.
.* IF THE USER SPECIFIES A SAVE AREA VIA THE SAVE= OPERAND THEN PRIME
.* WILL CHAIN THE USERS SAVE AREA TO THE CALLERS SAVE AREA. IF THE
.* USER DOES NOT SPECIFY SAVE,THEN PRIME WILL OBTAIN A NEW SAVE AREA
.* VIA THE GETMAIN SVC.  AN ADDITIONAL AMOUNT OF STORAGE MAY BE
.* OBTAINED AT THIS TIME BY SPECIFIEING THE LV=, OPERAND.  IF LV IS NOT
.* SPECIFIED IN REGISTER NOTATION, 4023 IS THE MAXIMUM VALUE.
.* IF THE USER SPECIFIES SAVE=NONE, PRIME WILL NOT OBTAIN A NEW AREA.
.* REG 13 WILL POINT TO THE NEW 18 WORD SAVE AREA FOLLOWED BY THE EXTRA
.* STORAGE AREA.
         LCLA  &A,&B
         LCLC  &E,&F,&G,&H
         MNOTE *,'&REG WILL BE USED AS A BASE REGISTER'
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&ID' EQ '').CONT4
         AIF   ('&ID' EQ '*').SPEC
&A       SETA  ((K'&ID+2)/2)*2+4
         USING *,&REG
         B     &A.(0,15)                BRANCH AROUND
&A       SETA  K'&ID
         DC    AL1(&A)
.CONTB   AIF   (&A GT 32).SPLIT
.CONTAA  AIF   (&A GT 8).BRAK
&E       SETC  '&ID'(&B+1,&A)
         DC    CL&A'&E'
         AGO   .CONT1
.BRAK    ANOP
&E       SETC  '&ID'(&B+1,8)
         DC    CL8'&E'
&B       SETA  &B+8
&A       SETA  &A-8
         AGO   .CONTAA
.SPLIT   ANOP
&E       SETC  '&ID'(&B+1,8)
&F       SETC  '&ID'(&B+9,8)
&G       SETC  '&ID'(&B+17,8)
&H       SETC  '&ID'(&B+25,8)
         DC    CL32'&E.&F.&G.&H'
&B       SETA  &B+32
&A       SETA  &A-32
         AGO   .CONTB
.SPEC    AIF   ('&NAME' EQ '').CSECTN
&E       SETC  '&NAME'
&A       SETA  1
.CONTQ   AIF   ('&E'(1,&A) EQ '&E').LVE
&A       SETA  &A+1
         AGO   .CONTQ
.LVE     ANOP
&B       SETA  ((&A+2)/2)*2+4
         USING *,&REG
         B     &B.(0,15)                BRANCH AROUND
         DC    AL1(&A)
         DC    CL&A'&E'
         AGO   .CONT1
.CSECTN  AIF   ('&SYSECT' EQ '').E4
&E       SETC  '&SYSECT'
&A       SETA  1
         AGO   .CONTQ
.E4      IHBERMAC 78,360
.CONT4   ANOP
         USING *,&REG
.CONT1   ANOP
         AIF   ('&REG' EQ '0' OR '&REG' EQ '1' OR '&REG' EQ '13').ERR1
         AIF   ('&REG' EQ '14' OR '&REG' EQ '15').ERR1
.CONT3   ANOP
         DS    0H
         STM   14,12,12(13)             SAVE REGS IN CALLERS AREA
         AIF   ('&MASK' EQ 'NO').NOMASK  DONT PRESERVE SYSTEM MASK
         BALR  15,0                     GET MASK 1-26-79
         STCM  15,8,16(13)             SAVE MASK BYTE
         L     15,16(,13)              RESTORE 15 LIKE ENTRY
.NOMASK  ANOP
         LR    &REG,15                  SET TRUE BASE EQUAL EP
         AIF ('&SAVE' EQ 'NONE').A
         AIF   ('&SAVE' EQ '').C
         AIF   ('&SAVE'(1,1) EQ '(').F
         LA    1,&SAVE                  SET R1 EQ USERS SAVE ADDRESS
         AGO   .D
.F       ANOP
         AIF   ('&SAVE' EQ '(1)').D
         LR    1,&SAVE(1)               PICK UP USERS SAVE AREA
         AGO   .D
.C       ANOP
         AIF   ('&LV'(1,1) EQ '(').E
.*         AIF   ('&LV' GT '4023').ERR2
         LA    0,(&LV.+72+15)/16        PARAMETER FOR GETMAIN
         SLL   0,4                      PARAMETER FOR GETMAIN /*8039*/
         AGO   .CONT2
.E       ANOP
         AIF   ('&LV' EQ '(0)').CONT2
         LA    0,72(&LV(1).,0)          PARAMETER FOR GETMAIN
.CONT2   ANOP
         BAL   1,*+4                    INDICATE GETMAIN
         SVC   10                       ISSUE SVC 10
         ST    0,0(0,1)                 SAVE LENGTH IN 1ST WORD
.D       ANOP
         XC    4(68,1),4(1)             CLEAR AREA
         ST    1,8(0,13)                CHAIN FORWARD
         ST    13,4(0,1)                CHAIN BACKWARD
         LM    0,1,20(13)               RESET R0 AND R1
         L     13,8(0,13)               SET SAVE AREA REGISTER
.A       ANOP
         MEXIT
.ERR1    MNOTE 'ILLEGAL BASE REGISTER SPECIFIED'
         AGO   .CONT3
.ERR2    MNOTE 'LV GT 4023, LV REQUEST NOT HONORED'
         LA    0,72(0,0)                PARAMETER FOR GETMAIN
         AGO   .CONT2
         MEND
         EJECT
         TITLE 'VTAM-WRITER START-UP'
***********************************************************************
VTAMWTR  CSECT
         PRINT NOGEN
         SPACE 2
         PRIME REG=12,ID=*,LV=4096
         LA    R10,4095
         LA    R10,1(R10,R12)      ESTABLISH ADDRESSABILITY
         USING VTAMWTR+4096,R10    ESTABLISH ADDRESSABILITY TO THE
*                                  REST OF THE PROGRAM CSECT
         LA    R9,4095
         LA    R9,1(R9,R10)        ESTABLISH ADDRESSABILITY
         USING VTAMWTR+8192,R9     ESTABLISH ADDRESSABILITY TO THE
*                                  REST OF THE PROGRAM CSECT
         L     R2,0(R1)            GET PARM
         SPACE 2
***********************************************************************
**       GET INTO SUPERVISOR STATE
***********************************************************************
         TESTAUTH FCTN=1           AM I AUTHORIZED
         LTR   R15,R15
         BNZ   TSWINIT             NO-ASSUME TEST MODE
         MODESET MODE=SUP
         SPACE 2
***********************************************************************
**       MOVE MODEL DATA TO GOTTEN AREA, ZERO REST.
***********************************************************************
TSWINIT  DS    0H
         LA    R0,TSDWORK          START OF  AREA
         LA    R1,(TSDCLRE-TSDWORK) LENGTH OF AREA
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14              MOVE ZEROES
         LA    R1,TSDBUFR+3        GET START OF BUFFER
         ST    R1,TSDBSTA          SAVE START OF BUFFER
         ST    R1,TSDBFPTR         SAVE IT
         MVC   TSDBUFR(3),CC3270   SET ERASE/WRITE
*                                  WCC = 132-PRINT POSITION,
*                                        START PRINTER, FORM FEED
         SPACE 2
***********************************************************************
**       PROCESS PARM IF ANY
**       PARM FOR PRINTER NAME AT START-UP TIME IS REQUIRED
**       APPLICATION DEFAULTS TO 'APPLE' IF NOPARM
***********************************************************************
         CLC   =H'0',0(R2)         ANY PARM
         BZ    TSNOPARM            NO PARM
         LH    R15,0(R2)           MOVE PARM TO WORK AREA
         LA    R15,2(R15)          +2 TO INCLUDE LENGTH BYTES
         LA    R14,0(R2)           FROM ADDR
         LA    R0,TSDTXT           TO ADDR TEXT WORK AREA
         LA    R1,L'TSDTXT         MAX L'
         ICM   R15,8,=C' '         FILL CHAR IS SPACES
         MVCL  R0,R14              PUT TEXT IN WORK
         BAL   R14,TSCMFMSU        PARSE START PARMS
         B     TSNOPARM            B    0(,R14)  NO ERRORS
         WTO   'VTAMWTR-BAD PARM',ROUTCDE=(8)
         ABEND 111
         SPACE 3
***********************************************************************
**       PUT TASK ID IN MESSAGES
***********************************************************************
TSNOPARM DS    0H
         L     R1,PSATNEW-PSA      NEW TCB-POINTER
         L     R1,TCBTIO-TCB(R1)   ADDRESS OF TIOT
         MVC   TSDBFCL,8(R1)       JOB STEP-NAME
         SPACE 3
***********************************************************************
***      LOCATE STOP/MODIFY ECB
***********************************************************************
         EXTRACT TSDCOMMP,MF=(E,TSDXTRC)
         L     R1,TSDCOMMP         GET PTR TO COMM/CIB ORG WORDS
         MVC   TSDCOMM,0(R1)       COPY ADDR OF ECB
         OI    TSDCOMM,X'80'       INDICATE COMM ECB IS LAST IN LIST
         LA    R2,4(R1)            POINT TO CIB ORIGIN
         QEDIT ORIGIN=(R2),CIBCTR=2 ALLOW 2 MODIFIES
         EJECT
*********************************************************************
***      OPEN OUT ACB - VTAM 328X
*********************************************************************
         ZAP   ZAE,=P'0'
         SPACE
         LA    R2,TSDACB1
         OPEN  ((2))
         SPACE 2
         LTR   15,15
         BZ    ACBOPNOK
         SPACE 2
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR ----ACB OPEN FAILED------ ***'
         DC    X'0000'
         SPACE 2
ACBOPNOK EQU   *
         MVI   TSDBYTE,X'00'           SYNC MAIN-PGM/LOGON-EXIT
         XC    TSDECB,TSDECB           CLEAR WAIT ECB
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         SETLOGON RPL=(R3),ACB=(R2),OPTCD=(SYN,START)
         SPACE 2
         LTR   R15,R15
         BZ    STALOGON
         SPACE
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR -- PRIM SETLOGON START FAILED -- ***'
         DC    X'0000'
         SPACE 2
STALOGON DS    0H
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         LA    R4,TSDNIBL
         SIMLOGON RPL=(R3),NIB=(R4),ACB=(R2),                          X
               OPTCD=(SYN,CONANY,Q,RELRQ)
         SPACE 2
         LTR   15,15
         BZ    DSTOPN
         SPACE 2
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR -  SIMLOGON FAILED  ***'
         DC    X'0000'
         SPACE 3
DSTOPN   DS    0H
         CLI   TSDBYTE,X'FF'      POST ALREADY BE DONE ?
         BE    DSTOPN10           YES, ---> DON'T WAIT
         SPACE 2
         WAIT  1,ECB=TSDECB
         SPACE 2
DSTOPN10 DS    0H
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         LA    R4,TSDNIBL
         OPNDST RPL=(R3),NIB=(R4),ACB=(R2),                            X
               OPTCD=(ACCEPT,SYN,CONALL,Q)
         SPACE
         LTR   R15,R15
         BNZ   OPN0C1
         SPACE
         LA    R3,TSDRPL1
         USING IFGRPL,R3
         CLC   RPLFDBK,=X'000000'
         BE    OPNDSTOK
         SPACE
OPN0C1   DS    0H
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR -  OPNDST   FAILED  ***'
         DC    X'0000'
         SPACE 3
OPNDSTOK EQU   *
         MVI   TSDSESS,X'FF'       VTAMWTR IN SESSION
         MVI   TSDKZ,X'FF'         OPNDST SUCCESSFULL
         SPACE 2
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         SETLOGON RPL=(R3),ACB=(R2),OPTCD=(SYN,STOP)
         LTR   R15,R15
         BZ    OPNW010
         SPACE
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR -- SETLOGON STOP FAILED -- ***'
         DC    X'0000'
         EJECT
***********************************************************************
**       INITIALIZE DYNAMIC ALLOCATION PARAMETER AREA                 *
***********************************************************************
OPNW010  EQU   *
         LA    R8,TSDS99           SVC 99 BUILD-AREA
         LR    R1,R8               ADDRESS OF ALLOC PARAMETER-LIST
         LA    R8,4(R8)            INCREMENT TO DAL REQUEST BLOCK
         ST    R8,0(R1)            SET UP RB PTR ADDR.
         OI    0(R1),S99RBPND
         SPACE 2
         USING S99RB,R8
         XC    S99RB(DYNRBSIZ),S99RB
         MVI   S99RBLN,DYNRBSIZ    SIZE OF RB
         LA    R3,DYNRBSIZ(R8)     INCREMENT TO FIRST TEXT POINTER
         USING DYNTXTPT,R3
         LA    R4,TXTPTSIZ(R3)     INCREMENT TO TEXT UNITS
         SPACE 2
*        DSNAME TEXT UNIT
         USING S99TUNIT,R4
         ST    R4,DSNTXTPT         SET UP ADDR OF FIRST TEXT UNIT
         XC    S99TUKEY(DYNEND-DYNTXTND),S99TUKEY ZERO TEXT UNITS
         MVI   S99TUKEY+1,DALDSNAM DSNAME IS SUPPLIED TO SVC99
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,44       SIZE OF DSNAME
         LA    R4,L'DSNTXTUT(R4)   INCREMENT TO SSREQ TEXT UNIT
         SPACE 2
*        SSREQ TEXT UNIT
         ST    R4,SSRTXTPT         ADDRESS OF SSREQ TEXT UNIT
         MVI   S99TUKEY+1,DALSSREQ THIS IS A REQUEST FOR SUBSYSTEM
         MVI   S99TULNG+1,4        LENGTH OF PARM FIELD
         SPACE 2
         L     R2,PSATNEW-PSA      TCB NEW
         L     R2,TCBJSCB-TCB(R2)  JSCB
         L     R2,JSCBSSIB-IEZJSCB(R2) SSIB
         USING SSIB,R2
         MVC   S99TUPAR(4),SSIBSSNM PRIMARY SUBSYSTEM NAME
         DROP  R2                  UNUSE SSIB
         MVI   S99TUNUM+1,X'01'    NUMBER OF REQUESTS
         LA    R4,L'SSRTXTUT(R4)   INCREMENT TO DDN TEXT UNIT
         SPACE 2
*        DDNAME RETURN TEXT UNIT
         ST    R4,DDNTXTPT         ADDR OF DDNAME RETURNED TEXT UNIT
         MVI   DDNTXTPT,S99TUPLN   LAST TEXT UNIT PTR. IN LIST
         MVI   S99TUKEY+1,DALRTDDN RETURN DDNAME TO ME
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,8        LENGTH OF PARM FIELD
         MVC   S99TUPAR(8),BLANKS  INITIALIZE TO BLANKS
         LA    R4,L'DDNTXTUT(R4)   INCREMENT TO DDNAME UNALLOC TEXT
         SPACE 2
*        DDNAME UNALLOCATE TEXT UNIT
         ST    R4,UDNTXTPT         SAVE UNALLOC TEXT ADDRESS
         MVI   S99TUKEY+1,DUNDDNAM USE DDNAME TO UNALLOCATE
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,8        LENGTH OF PARM FIELD
         LA    R4,L'UDNTXTUT(R4)   INCREMENT TO OVERRIDE TEXT
         SPACE 2
*        DISPOSITION OVERRIDE TEXT UNIT
         ST    R4,UORTXTPT         SAVE ADDRESS OF OVERRIDE TEXT
         MVI   UORTXTPT,S99TUPLN   LAST TEXT UNIT PTR. IN LIST
         MVI   S99TUKEY+1,DUNOVDSP OVERRIDE DISPOSITION KEY
         MVI   S99TULNG+1,1        LENGTH OF PARM FIELD
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         DROP  R4
         SPACE 3
***********************************************************************
*   SET UP SSOB TO ISSUE IEFSSREQ TO ACCESS A DATA SET                *
***********************************************************************
SD82SSOB EQU   *
         LA    R1,TSDSSOB
         LA    R7,4(R1)            INCREMENT TO SSOB ITSELF
         USING SSOB,R7
         ST    R7,0(R1)            SET UP ADDR OF ADDR OF SSOB IN R1
         OI    0(R1),X'80'
         XC    SSOBEGIN(SSOBHSIZ),SSOBEGIN   CLEAR SSOB HEADER
         LA    R2,SSOBGN           INITIALIZE SSOB
         XC    0(SSSOSIZE,R2),0(R2)     CLEAR SYSOUT EXTENSION
         MVC   SSOB(WTRSOSIZ),WTRSSOB   INITIALIZE HEADER
         ST    R2,SSOBINDV         ADDRESS OF SYSOUT EXTENSION
         LA    R4,SSSOSIZE         SYSOUT EXTENSION SIZE
         STH   R4,0(R2)            SET UP SIZE OF SYSOUT EXTENSION
         MVC   SSSOPGMN,BLANKS     INITIALIZE WRITER NAME TO BLANKS
         OI    SSSOFLG1,SSSODST    DESTINATION IS SELECT CRITERION
         OI    SSSOFLG1,SSSOSFRM   " FORMS
         OI    SSSOFLG1,SSSOSCLS   " CLASS LIST
         SPACE 3
***********************************************************************
**             SEND OUT SALUTATION  MESSAGE TO PRINTER (SEE IF UP)
***********************************************************************
         CALL  DATUM,(HELLO1)
         LA    R0,L'HELLO
         LA    R1,HELLO
         MVI   TSDINIT,C'Y'
         MVI   TSDCONCH,X'89'        FORM FEED AFTER WRITE
         MVI   TSDTRUNC,C'Y'         SEND SHORT BLOCK
         BAL   R14,TSWLOCS
         B     TSGEOD1
TSINITRT DS    0H                  RETURN FROM ERROR CHECK AT EODAD
         MVI   TSDINIT,C'N'        TURN OFF START-UP MSG
         TESTAUTH FCTN=1           AM I AUTHORIZED
         LTR   R15,R15
         BNZ   TERME               NO-GET OUT
         TITLE 'ASK JES FOR WORK'
***********************************************************************
*        ASK JES FOR WORK '
***********************************************************************
SD82HASP EQU *
         MVI   TSDFLUSH,0          RESET FLUSH OPTION
         CLI   TSDSTOPA,C'Y'       CLOSE DOWN
         BE    SD82HALT            YES
         MVC   SSSOCLSL,TSDHCLAS   MOVE CLASS LIST
         MVC   SSSOFORM,TSDHFORM   MOVE FORM #
         MVC   SSSODEST(8),TSDHDEST MOVE DESTINATION
         SPACE 2
         LA    R1,TSDSSOB
         IEFSSREQ                  ASK HASP FOR WORK
         LTR   R15,R15             TEST RETURN CODE
         BZ    SD82RTRN            NORMAL RETURN
         MVC   TSDWERR1,=CL20'BAD  JES RETURN CODE'
         B     TSFATALF            GIVE R15
         SPACE 2
SD82RTRN EQU   *
         C     R15,SSOBRETN
         BE    SD82DAL             ZERO = WORK DELIVERED
         LA    R2,4                COMPARE RETURN TO 4
         C     R2,SSOBRETN         ARE THERE ANY MORE DATA SETS.
         BE    SD82WAIT            NO, WAIT FOR WORK
         MVC   TSDWERR1,=CL20'BAD  SSOB RETURN CODE'
         B     TSFATALF            GIVE R15
         SPACE 3
***********************************************************************
*    SET UP TO DYNAMICALLY ALLOCATE DATA SET                          *
***********************************************************************
SD82DAL  EQU   *                   DYNAMICALLY ALLOCATE DATA SET
         MVC   TSDJOBN,SSSOJOBN    SAVE JOBNAME
         MVC   TSDJOBI,SSSOJOBI    JOBNUMBER
         MVC   TSDHFORM,SSSOFORM   FORM #
         MVC   TSDCLAS,SSSOCLAS    CLASS
         MVC   TSDDSN,SSSODSN      DSN
         CLC   =C'LTSO',TSDJOBN    TSO ID PREFEX   *LEBAU DEPENDANT*
         BNE   TS82NNEW            NO - NO NOTIFY
         SPACE 2
         MVC   USERID(6),TSDJOBN   USERID FOR TPUT
         CLI   TSDSEP,C'Y'         PAGE SEPERATORS?
         BNE   TS82NNEW            NO-MUST BE SPECIAL WRITER-NO NOTIFY
         MVC   TSDBFCL(L'TSDBFCL),TSDNIBL+12   PRINTER NAME
         LA    R0,L'TSDBFR         LENGTH
         LA    R1,TSDBFR           ADDR OF MSG
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,           XXXXXX
               USERIDL=USERID
         SPACE 2
TS82NNEW DS    0H
         MVI   S99VERB,S99VRBAL    ALLOCATION VERB
         LA    R3,DISPDSN(R8)      ADDR. DSNAME TEXT UNIT
         ST    R3,S99TXTPP         STORE ADDR OF TEXT UNIT PTRS
         L     R4,0(,R3)           ADDR OF FIRST TEXT UNIT
         MVC   6(44,R4),SSSODSN    GET DATA SET NAME FROM SSOB
         BAL   R11,DYNAL           DYNAMICALLY ALLOCATE DATA SET
         SPACE 3
***********************************************************************
*   INITIALIZE INPUT DCB                                              *
***********************************************************************
*        REFRESH INPUT DCB
         MVC   TSDDCBI(TSSDCBIE-TSSDCBI),TSSDCBI
         L     R3,S99TXTPP         ADDRESS OF TEXT UNITS
         L     R4,DDNTXTPT-DYNTXTPT(R3) ADDRESS OF DDNAME TEXT UNIT
         MVC   DCBDDNAM-IHADCB+TSDDCBIS(8),6(R4) PUT DDNAME IN DCB
         LA    R15,TSDDCBI         GET INPUT DCB ADDR
         MVI   TSDOPENX,X'80'      SIGNIFY END OF PARAM LIST
         OPEN  ((R15),INPUT),MF=(E,TSDOPENX)
         SPACE 2
         MVI   TSDEOF,C'N'         RESET END-OF-FILE FLAG
         CLI   TSDSEP,C'Y'         PAGE SEPERATORS DESIRED?
         BNE   TSGNSEP             NO-SKIP IT
         SPACE 2
         MVC   TSDBEGJ,TSDDSN      PRINT DSN ON TOP
         MVC   TSDBEGJN,TSDJOBN    PRINT JOBNAME ALSO
         CALL  DATUM,(TSDBEGJT)
         LA    R1,TSDBEG
         LA    R0,L'TSDBEG
         MVI   TSDCONCH,X'19'      SKIP 3 LINES
         BAL   R14,TSWLOCS         PRINT HEADER
         SPACE 2
TSGNSEP  DS    0H                  SKIP DATASET HEADER
         MVI   TSDNEWDS,C'Y'       SET FLAG TO SUPPRESS 1ST SKIP
         B     TSGCKSM             CHECK FOR STOP/MODIFY
         SPACE 3
***********************************************************************
**       GET CARRAGE, LINE LEN., AND LOCATION FOR WRITER.
***********************************************************************
TSGLOOP  DS    0H
         GET   TSDDCBI
         SPACE 2
         LH    R0,DCBLRECL-IHADCB+TSDDCBI GET DEF. LRECL
         TM    DCBRECFM-IHADCB+TSDDCBI,DCBRECU UNDEFINED
         BO    TSGNOTV             YES
         TM    DCBRECFM-IHADCB+TSDDCBI,DCBRECV VARIABLE
         BNO   TSGNOTV             NO
         LH    R0,0(R1)            GET REC LEN
         LA    R1,4(R1)            SKIP RDW
         SH    R0,=H'4'            SKIP RDW LEN.
         SPACE 2
TSGNOTV  DS    0H
         TM    DCBRECFM-IHADCB+TSDDCBI,X'02' MACH. CARRIAGE
         BNO   TSFRC1SP            NO-TRY ASCII
         MVC   TSDCONCH,0(R1)
TSADJREG DS    0H
         LA    R1,1(R1)            SKIP CC
         BCTR  R0,0                SKIP CC LEN.
         B     TSPUTIT             SETUP FOR WRITER
         SPACE 2
TSFRC1SP DS    0H
         MVI   TSDCONCH,X'09'      1SP AFTER
         TM    DCBRECFM-IHADCB+TSDDCBI,X'04' ASCII
         BNO   TSPUTIT             NO CARRIAGE
         SPACE 2
***********************************************************************
**       BREAK ASCII COMMAND DOWN INTO IMM. AND PRINT
***********************************************************************
         STM   R0,R1,TSDSAV01      SAVE STRT, LEN
         LA    R14,=X'F00B6013F18B'  ASCII XLATE TBL
         LA    R15,3          L' OF TABLE(3 PAIRS)
         SPACE 2
***********************************************************************
*        IDEA IS TO PRINT ALL W/ 1SP AFTER.
*        NOTE NO SUPPORT FOR '+' - OVERPRINT.
*        SO '0' = (SP 1 IMM)+(PR W/ 1 SP AFTER)
*           '-' = (SP 2 IMM)+(PR W/ 1 SP AFTER)
*           '1' = (SK CH 1 IMM)+(PR W/ 1 SP AFTER)
*           ' ' OR '+' = (PR W/ 1 SP AFTER)
***********************************************************************
TSFRLOC  DS    0H
         CLC   0(1,R14),0(R1)
         BE    TSFRFND             FOUND ASCII ARG. CHAR
         LA    R14,2(R14)          NEXT PAIR
         BCT   R15,TSFRLOC
         B     TSADJREG            UNKNOWN - FORCE 1 SP
         SPACE 2
TSFRFND  DS    0H
         MVC   TSDCONCH,1(R14)     MOVE IMMEDIATE PART OF ASCII
         XR    R0,R0               ZERO LENGTH
         BAL   R14,TSWLOCS         PUT IMMEDIATE PART
         LM    R0,R1,TSDSAV01      RESTORE REGS
         MVI   TSDCONCH,X'09'      1SP AFTER
         B     TSADJREG            ADJ REGS AROUND CARRIAGE
         SPACE 2
***********************************************************************
**       NORMAL PUT INTERFACE
***********************************************************************
TSPUTIT  DS    0H
         BAL   R14,TSWLOCS
         B     TSGCKSM             CK STOP/MODIFY
         SPACE 2
TSGCKSM  DS    0H
         BAL   R11,TSCMCK          CHECK FOR STOP/MODIFY
         B     TSGSTOP             STOP ISSUED
         B     TSGET               NORMAL RETURN
         SPACE 2
TSGSTOP  DS    0H
         MVI   TSDKEEP,C'Y'        KEEP CURRENT D.S.
         MVI   TSDSTOP,C'Y'        STOP AFTER UNALLOC/KEEP
         B     TSGEOD              SIM. EODAD
         SPACE 2
TSGET    DS    0H                  GET NEXT RECORD
         CLI   TSDSTOP,C'Y'        WAS STOP ISSUED DURING BTAM WRITE
         BE    TSGEOD              YES-SIM. EODAD
         CLI   TSDFLUSH,C'Y'       FLUSH AND PURGE CURR. D.S.
         BNE   TSGRSET             NO FLUSH - TEST EOF
         SPACE 2
TSSYNFL  DS    0H                                               9041
         MVI   TSDFLUSH,0          RESET
         LA    R1,TSDFLSMS         GET FLUSH MSG
         LA    R0,L'TSDFLSMS
         MVI   TSDCONCH,X'89'      CH1 AFTER
         MVI   TSDTRUNC,C'Y'       FORCE PHYSICAL WRITE(TRUNC)
         BAL   R14,TSWLOCS
         B     TSGECLS             CLOSE OUT
         SPACE 3
***********************************************************************
**       SYNAD  ROUTINE ON JES INPUT D.S.
***********************************************************************
TSSYN    DS    0H                                               9041
         MVC   TSDBYWHO,=CL8'IO ERROR'                          9041
         WTO   'VTAMWTR - IO ERROR',ROUTCDE=(8)                 9041
         B     TSSYNFL             FLUSH DATASET                9041
         SPACE 2
***********************************************************************
TSGRSET  DS    0H
         CLI   TSDEOF,C'Y'         AT EOF?
         BE    TSGEOD1             YES-RETURN TO CLOSE CODE
         B     TSGLOOP
         TITLE  'END OF DATA SET'
***********************************************************************
*        END OF DATA SET
***********************************************************************
TSGEOD   DS    0H
         MVI   TSDEOF,C'Y'         SET END OF FILE
         MVI   TSDCONCH,X'8B'      SKIP TO CHAN 1 IMM
         LA    R1,TSDCONCH         START ADDR  DUMMY
         XR    R0,R0               DUMMY LENGTH
         BAL   R14,TSWLOCS         SKIP
         SPACE 2
         MVI   TSDTRUNC,C'Y'       FORCE PHYSICAL WRITE
         MVI   TSDCONCH,X'89'      FORM FEED AFTER WRITE
         CALL  DATUM,(TXTDATUM)
         LA    R1,TXTENDDS         START ADDR
         LA    R0,LTXTEND          LENGTH
         BAL   R14,TSWLOCS         SKIP
         SPACE 2
TSGEOD1  DS    0H                  SECONDARY ENTRY POINT
         MVI   TSDEOF,C'Y'         SET END OF FILE
         CLI   TSDSTOP,C'Y'        STOPPING?
         BE    TSGEOD2             YES-DONT CHECK FOR I/O ERROR
TSGEOD2  DS    0H                  SECONDARY ENTRY POINT
         CLI   TSDINIT,C'Y'        IN STARTUP PROCESSING
         BNE   TSGECLS             NO-CONTINUE
         CLI   TSDSTOP,C'Y'        WAS STOP ISSUED DURING START UP
         BNE   TSINITRT            NO-INIT RETURN
         B     TERME               YES-EXIT
         SPACE 3
***********************************************************************
**       CLOSE INPUT DCB AND FREE BUFFER POOL
***********************************************************************
TSGECLS  DS    0H
         LA    R15,TSDDCBI
         CLOSE ((R15),DISP),MF=(E,TSDOPENX)
         FREEPOOL TSDDCBI          FREE BUFFER POOL
         SPACE 3
***********************************************************************
*        UNALLOCATE INPUT D.S.
***********************************************************************
         MVI   S99VERB,S99VRBUN    UNALLOCATION VERB
         DROP  R3
         LA    R3,DISPUAL(R8)      INCREMENT TO UNAL
         ST    R3,S99TXTPP         STORE ADDR. OF TEXT UNIT PTRS.
         L     R3,0(R3)            ADDRESS OF DDNAME FOR UNALLOCATE
         L     R4,DISPDDN(R8)      ADDRESS OF DDNAME UNIT
         MVC   6(8,R3),6(R4)       PUT DDNAME INTO TEXT FOR UNALLOC
         L     R4,DISPOVR(R8)      ADDRESS OF OVERIDE UNIT
         CLI   TSDKEEP,C'Y'        IS INPUT TO BE KEPT
         BE    KEEPDS              YES
         MVI   6(R4),X'04'         DELETE DATA SET
         B     DYNUN               UNALLOCATE WITH DELETE DISP
         SPACE 2
KEEPDS   MVI   6(R4),X'08'         KEEP DATA SET
DYNUN    BAL   R11,DYNAL           DYNAMICALLY UNALLOCATE DATA SET
         MVI   TSDKEEP,0           RESET KEEP OPTION
         CLI   TSDSTOP,C'Y'        STOPPING
         BE    SD82HALT            YES
         B     SD82HASP            GO GET MORE WORK
         TITLE  'W A I T  F O R   W O R K  O R  S T O P  W R I T E R '
***********************************************************************
*        W A I T  F O R   W O R K  O R  S T O P  W R I T E R
***********************************************************************
SD82WAIT EQU   *
         MVI   TSDSESS,X'00'       INDICATE WAITING SESSION
         MVC   TSDWECB,SSSOWTRC    SAVE ADDR. OF HASP ECB
         MVI   TSDWECB,X'00'       CLEAR ECBLIST HIGH ORDER BYTE
         XC    TPENDECB,TPENDECB   CLEAR TPEND ECB
         LA    R1,TSDWECB          ADDR. OF WAIT LIST
         WAIT  1,ECBLIST=(1),LONG=YES   WAIT FOR WORK OR COMMAND ECB
         SPACE 2
         TM    TPENDECB,X'40'      IS POSTED ECB TPEND ECB
         BO    CLOSEACB            YES? ---> CLOSE ACB, END PROGRAM
         SPACE 2
         MVI   SSSODSN,X'00'       INDICATE THIS IS AN INITIAL REQUEST
         MVI   TSDSESS,X'FF'       INDICATE CURRENT SESSION
         CLI   TSDKZ,X'00'         SLU RELEASED
         BNE   WATW010                  NO ? ---> GOON
         SPACE 2
         ZAP   ZAE,=P'0'
         MVI   TSDBYTE,X'00'            SYNC MAIN-PGM/EXITS
         XC    TSDECB,TSDECB            CLEAR ECB
         SPACE
         MVC   TSDNIBL(LTSDNIBL),NIB    CLEAR NIB
         MVC   TSDNIBL+12(8),TSDPRINT   MOVE LU-NAME
         MVC   TSDRPL1(LTSDRPL1),RPL1   CLEAR RPL
         SPACE
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         SETLOGON RPL=(R3),ACB=(R2),OPTCD=(SYN,START)
         SPACE 2
         LTR   R15,R15
         BZ    WATLOGON
         SPACE 2
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR -- SEC  SETLOGON START FAILED -- ***'
         DC    X'0000'
         SPACE 2
WATLOGON DS    0H
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         LA    R4,TSDNIBL
         SIMLOGON RPL=(R3),NIB=(R4),ACB=(R2),                          X
               OPTCD=(SYN,CONANY,Q,RELRQ)
         SPACE 2
         LTR   15,15
         BZ    WAITOPEN
         SPACE 2
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR  SECOND  SIMLOGON FAILED ***'
         DC    X'0000'
         SPACE 2
WAITOPEN DS    0H
         CLI   TSDBYTE,X'FF'      POST ALREADY BE DONE ?
         BE    WATOPN10           YES, ---> DON'T WAIT
         SPACE 2
         WAIT  1,ECB=TSDECB
         SPACE 2
WATOPN10 DS    0H
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         LA    R4,TSDNIBL
         OPNDST RPL=(R3),NIB=(R4),ACB=(R2),                            X
               OPTCD=(ACCEPT,SYN,CONALL,Q)
         SPACE
         LTR   R15,R15
         BNZ   WAT0C1
         SPACE
         LA    R3,TSDRPL1
         USING IFGRPL,R3
         CLC   RPLFDBK,=X'000000'
         BE    WAITEND
         SPACE
WAT0C1   DS    0H
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR -  SEC OPNDST   FAILED  ***'
         DC    X'0000'
         SPACE 2
WAITEND  DS    0H
         MVI   TSDKZ,X'FF'               OPNDST SUCCESSFULL
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         SETLOGON RPL=(R3),ACB=(R2),OPTCD=(SYN,STOP)
         LTR   R15,R15
         BZ    WATW010
         SPACE
         STM   R0,R15,SAVE0C1
         WTO   '*** VTAMWTR -- SETLOGON STOP FAILED -- ***'
         DC    X'0000'
         SPACE 3
WATW010  EQU   *
         BAL   R11,TSCMCK          SEE IF STOP/MODIFY ISSUED
         B     SD82HALT            END OF WORK
         B     SD82HASP            GO TRY TO GET MORE WORK FROM HASP
         TITLE 'END OF WORK'
***********************************************************************
*        STOP THE WRITER, END OF WORK                                 *
***********************************************************************
SD82HALT OI    SSSOFLG2,SSSOCTRL   INDICATING STOPPING
         LA    R1,TSDSSOB          SSOB ADDR
         IEFSSREQ                  TELL HASP WTR IS QUITTING
         XR    R15,R15
         SPACE 3
***********************************************************************
*        RETURN TO CALLER
***********************************************************************
         MODESET MODE=PROB
         SPACE 2
TERME    DS    0H
         CALL  DATUM,(GOODBYE1)
         LA    R0,L'GOODBYE
         LA    R1,GOODBYE
         MVI   TSDCONCH,X'89'        CHANNEL 1 AFTER SEND
         MVI   TSDTRUNC,C'Y'         SEND SHORT BLOCK
         BAL   R14,TSWLOCS
         SPACE 2
         LA    R2,TSDACB1
         LA    R3,TSDRPL1
         LA    R4,TSDNIBL
         CLSDST RPL=(R3),NIB=(R4),ACB=(R2),OPTCD=RELEASE
         SPACE 3
CLOSEACB EQU   *
         MVI   TSDSESS,X'11'      SIGNAL CLOSE IN PROGESS TO EXITS
         SPACE
         LA    R2,TSDACB1
         CLOSE ((2))
         SPACE 3
THEEND   EQU   *
         TERME
         TITLE  'V T A M  W R I T E R'
***********************************************************************
*        V T A M  W R I T E R
***********************************************************************
TSWLOCS  DS   0H
         ST    R0,TSDISIZ          LENGTH OF PRINT RECORD
         ST    R1,TSDILOC          ADDRESS OF PRINT RECORD
TSWLOC   DS    0H
         ST    R14,TSDWRET         SAVE RETURN
         SPACE 2
***********************************************************************
**       SUPRESS SKIP IF FIRST FOR D.S.
***********************************************************************
         CLI   TSDNEWDS,C'Y'
         BNE   TSWNNEW             NOT A NEW D.S.
         MVI   TSDNEWDS,C'N'       RESET NEW FLAG
         CLI   TSDCONCH,X'8B'      IS 1ST CMD A SKIP CH1?
         BE    TSWRET              YES-SUPRESS IT-HEADER ALREADY AT CH1
         CLI   TSDCONCH,X'89'      CH 1 AFTER
         BNE   TSWNNEW             NO-NORMAL
         MVI   TSDCONCH,X'09'      MAKE 1 SP AFTER
         SPACE 2
TSWNNEW  DS    0H                  NO SUPRESSION
**       XLATE 328X CTRL/NON-PRINTS OUT
         LM    R0,R1,TSDILOC       GET ADDR,LENGTH OF LINE
         LM    R14,R15,TSDILOC     "
         SH    R15,=H'1'           -1 FOR EXECUTE INSTR
         BM    TSWNOTR             LEN = ZERO - NO XLATE
         EX    R15,TSSTR           XLATE 3270 CTRL CHARS OUT
         SPACE 2
TSWNOTR  DS    0H
*        MOVE PRINTLINE TO BUFFER
         C     R1,TSDWID           MAX WIDTH (LENGTH OF LINE)
         BL    *+8                 OK
         L     R1,TSDWID           FORCE DOWN TO MAX LENGTH
         L     R14,TSDBFPTR        GET OUTPUT BUFFER NXT AVAL.*
         TM    TSDCONCH,X'02'      IMMEDIATE COMMAND (SPACE ONLY)
         BO    TSWIMM              YES - DONT MOVE DATA
         LR    R15,R1              NOT IMM. MOVE DATA TO BUFFER
         MVCL  R14,R0              R14 WILL POINT AFTER LAST BYTE
         SPACE 2
TSBKSCAN DS    0H
         BCTR  R14,0               BACK UP ONE BYTE
         C     R14,TSDBSTA         GOT TO 3270 CC'S
         BL    TSEOSCAN            YES - STOP BACK SPACING
         CLI   0(R14),C' '         IS IT BLANK
         BE    TSBKSCAN            YES-BACK UP ANOTHER
         SPACE 2
TSEOSCAN EQU   *
         LA    R14,1(R14)          R14 POINTS TO NEXT AVALIABLE BYTE
TSWIMM   DS    0H                  IMMEDIATE DATA
         TM    TSDCONCH,X'C0'      SKIP TO CHANNEL ?
         BNZ   TSSKIP              YES-CALC # LINE FEEDS TO HEAD
         SPACE 2
TSWSPAC  DS    0H                  CHANGE SPACING TO NL(X'15')S
         IC    R1,TSDCONCH         GET CARRIAGE
         N     R1,=X'00000018'     MASK OFF JUNK
         BNZ   TSADDSRL            PUT OUT NLS
         LA    R1,1*8              FORCE AT LEAST 1 SP
TSADDSRL DS    0H
         SRL   R1,3                SHIFT TO GET SPACING
         SPACE 2
         L     R15,TSDCURPO        CURRENT LINE #
         AR    R15,R1              ADD # SPACING LINES
         C     R15,TSDLEN          PAGE OVERFLOW ?
         BL    TSSPCLI             NO
         CLI   TSDCONCH,X'01'      WRITE NO SPACE ?
         BE    TSADDBSP            YES ? ---> ALLOW
         LA    R15,1
         ST    R15,TSDCURPO        RESET LINE #
         MVC   0(2,R14),=X'150C'   NL + FORM FEED
         LA    R14,2(R14)          NEXT AVAILABLE BYTE
         B     TSADDEM
         SPACE 2
***********************************************************************
* THIS IS THE PLACE FOR CARRIAGE RETURN / BACK SPACE STUFF
* WE INSERT AS MANY BACKSPACE CHARS AS THERE IS DATA , THEN FOLLOW
* WITH THE DATA - NO NL'S - EFFECT=UNDERSCORE OR OVERPRINTING
***********************************************************************
         SPACE
TSSPCLI  EQU   *
         CLI   TSDCONCH,X'01'     WRITE NO SPACE ??
         BNE   TSADDLN
TSADDBSP EQU   *
         MVI   0(R14),X'0D'      INSERT CARRIAGE RETURN
         LA    R14,1(R14)
         B     TSADDEM
         SPACE 2
TSADDLN  DS    0H                  LOOP ADDING NLS TO BUFFER
         L     R0,TSDCURPO         GET CURRENT LINE #
         AR    R0,R1               ADD NEW SPACING
         ST    R0,TSDCURPO         STORE IT
TSADDNL  DS    0H
         MVI   0(R14),X'15'        NEW LINE CHAR
         LA    R14,1(R14)          NEW OUTPUT ADDR
         BCT   R1,TSADDNL          LOOP
         SPACE 3
***********************************************************************
**       SEE IF BUFFER FULL YET
***********************************************************************
TSADDEM  DS    0H
         ST    R14,TSDBFPTR        SAVE CURR POS.
         CLI   TSDTRUNC,C'Y'       FORCE WRITE
         BE    TSWFRC              YES
         LA    R15,TSDBUFR+1535    SNA-BUFF-SIZE NOT GT 1536
         S     R15,TSDWID          MAX RECORD-LENGTH
         S     R15,TSDISIZ         CURRENT RECORD-LENGTH
         CR    R14,R15             ENOUGH SPACE FOR NEXT RECORD ?
         BNH   TSWRET              YES, TAKE NEXT RECORD
         SPACE 3
***********************************************************************
*     THIS NEXT TEST TRIES TO AVOID BACKSPACES BEING THE LAST STUFF
*     IN BUFFER - PRINTER WILL LINE FEED AT END OF PRINT REGARDLESS
*     THUS PUTTING UNDERSCORE/OVERPRINT A LINE DOWN.
***********************************************************************
         LR    R1,R14    GET NEXT BUFFER POSITION
         BCTR  R1,R0     POINT TO LAST VALID CHAR IN BUFFER
         CLI   0(R1),X'16'      WAS IT BACKSPACE
         BE    TSWRET    YES - SQUEEZE UNDERSCORE/OVERPRINT STUFF IN
         SPACE 2
TSWFRC   DS    0H
TSWRETRY DS    0H
         MVI   0(R14),X'19'        PUT EOM IN
         LA    R14,1(R14)          NEXT BYTE
         LA    R4,TSDBUFR          GET START
         SR    R14,R4              LENGTH
         LR    R2,R14          PUT BUFF LEN IN REG
         LA    R3,TSDRPL1
         MVC   STLINENO,TSDCURPO   SAVE OVERALL LINE NUMBER
         SPACE 3
COMNSEND EQU   *
         SEND  AREA=TSDBUFR,                                          XX
               RECLEN=(R2),                                           XX
               RPL=(R3),                                              XX
               OPTCD=(SYN,CS),                                        XX
               POST=RESP,                                             XX
               CONTROL=DATA,                                          XX
               RESPOND=(NEX,FME,NRRN),                                XX
               BRACKET=(BB,EB),                                       XX
               STYPE=REQ
         SPACE 2
         LTR   R15,R15
         BZ    TSWWAIT
         SPACE 2
         STM   R0,R15,SAVE0C1
         WTO   'VTAMWTR - SEND FAILED'
         DC    X'0000'
         TITLE 'SYNAD ROUTINES'
SYNADA   EQU   *
         USING IFGRPL,R3
         LA    R3,TSDRPL1
         CLC   RPLRTNCD(2),=X'0C0C'  ) 12 AND 12 INDICATE
         BE    COMNSEND              ) CLEAR SENT - OK
         SPACE 2
SYNADAB  EQU   *
         CLC   RPLRTNCD(2),=X'0404' ) 4 AND 4 INDICATE
         BNE   SYNADB               ) INCOMING EXCEPTION COND
         CLI   RPLSSEI,8         8 MEANS GOOD MESSAGE WAS REJECTED
         BE    LINEUP            RETRY
         CLC   RPLUSNSI,=X'0000' USER SENSE FROM PRINTER OK
         BE    SYNADB            SOME OTHER BAD ERROR
         SPACE 2
TSINTRVA EQU   *
         WTO   'VTAMWTR - PRINTER - INTERVENTION REQUIRED'
         SPACE 2
         LA    R0,L'TSDBFR         LENGTH
         LA    R1,TSDBFR           ADDR OF MSG
         MVI   0(R1),X'00'
         MVC   1(L'TSDBFR-1,R1),0(R1)  CLEAR MSG-BUFFER
         MVC   0(50,R1),=CL50'VTAMWTR - PRINTER INTERVENTION REQUIRED'
         MVC   USERID,=CL8'LTSOHI'
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,           XXXXXX
               USERIDL=USERID
         SPACE 2
TSINTRVB EQU   *
         STIMER WAIT,BINTVL==F'3000'   WAIT A WHILE (SEC * 100)
         SPACE 2
         CLI   RPLSSEI,8         8 MEANS GOOD MESSAGE WAS REJECTED
         BE    COMNSEND          RETRY - WITHOUT LINEUPS
         SPACE 3
**********************************************************************
* THIS CRUD ATTEMPTS TO LINE FEED TO RIGHT POSN. BEFORE RESENDING
* LAST BLOCK
*
* BAD RETURN FROM SESSIONC IS HANDLED BY LERAD
**********************************************************************
         SPACE
LINEUP   EQU   *
         SESSIONC RPL=TSDRPL1,CONTROL=CLEAR
         SPACE
         SESSIONC RPL=TSDRPL1,CONTROL=SDT
         SPACE
         MVI   NLS,X'15'           FILL WITH NLS
         MVC   NLS+1(119),NLS
         L     R1,STLINENO  GET LINE COUNT AT START OF FAILING BLOCK
         SR    R0,R0
         D     R0,TSDLEN    DIVIDE BY LINES PER PAGE
         LR    R1,R0        LOAD REMAINDER - LINES DOWN PAGE
         LTR   R1,R1        LINECOUNT = 0 = DONT LINEUP
         BZ    COMNSEND
         SPACE
         SR    R5,R5
         LA    R5,X'19'     PUT EOM IN REG
         LA    R15,NLS
         STC   R5,0(R1,R15) PUT EOM IN BUFFER FULL OF NL'S
         LR    R5,R1
         LA    R5,6(R5)     GET RECLEN - ADD 1 FOR EOM/5 FOR CC'S
         SPACE 2
         SEND  AREA=NLBLOCK,                                          XX
               RECLEN=(R5),                                           XX
               RPL=(R3),                                              XX
               OPTCD=(SYN,CS),                                        XX
               POST=RESP,                                             XX
               CONTROL=DATA,                                          XX
               RESPOND=(NEX,FME,NRRN),                                XX
               BRACKET=(BB,EB),                                       XX
               STYPE=REQ
         SPACE
         LTR   R15,R15
         BZ    COMNSEND          RETRY SEND
         SPACE
         STM   R0,R15,SAVE0C1
         WTO   'VTAMWTR - SEND LINEUP FAILED'
         SPACE 2
         LA    R0,L'TSDBFR         LENGTH
         LA    R1,TSDBFR           ADDR OF MSG
         MVI   0(R1),X'00'
         MVC   1(L'TSDBFR-1,R1),0(R1)  CLEAR MSG-BUFFER
         MVC   0(L'TSDBFR,R1),=C'VTAMWTR - SEND LINEUP FAILED  PGM CHECX
               K ''0C1'' WILL BE FORCED'
         MVC   USERID,=CL8'LTSOHI'
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,           XXXXXX
               USERIDL=USERID
         DC    X'0000'
         SPACE 3
TSWWAIT  DS    0H                  WAIT FOR  MODIFY
         CLI   TSDTRACE,C'Y'
         BNE   TSTRACE
         SPACE
         SNAP  DCB=SNAPPER,LIST=TRACELST
         SPACE 2
TSTRACE  EQU   *
         DROP  R3
         BAL   R11,TSCMCK          PROCESS MODIFY
         B     TSWHALT             STOP ISSUED
         B     TSWCKCMP ARRIVE HERE BY A 'BR 4(R11)' FROM TSCMCK
         SPACE 2
TSWHALT  DS    0H
         MVI   TSDKEEP,C'Y'        KEEP CURR. D.S.
         MVI   TSDSTOP,C'Y'        SHUT DOWN AFTER UNALLOCATE
TSWCKCMP DS    0H
         MVI   TSDTRUNC,C'N'       RESET FORCED WRITE
         LA    R14,TSDBUFR+2
         ST    R14,TSDBSTA         SAVE BEGIN OF BUFFER
         ST    R14,TSDBFPTR        RESET PTR
         CLI   TSDEOF,C'Y'         END OF FILE ?
         BE    TSDRESET
         MVC   TSDBUFR(2),CC3289
         B     TSWRET
         SPACE 2
TSDRESET EQU   *
         LA    R14,1
         ST    R14,TSDCURPO        RESET LINE #
         MVC   TSDBUFR(3),CC3270
         LA    R14,TSDBUFR+3
         ST    R14,TSDBSTA         SAVE BEGIN OF BUFFER
         SPACE 2
**********************************************************************
*        RETURN FROM VTAM-WRITER
**********************************************************************
TSWRET   DS    0H                  RETURN
         L     R14,TSDWRET         "
         BR    R14                 "
         SPACE 3
**********************************************************************
*        HANDLE SKIP COMMANDS
**********************************************************************
TSSKIP   DS    0H                  HANDLE SKIP COMMAND
         CLI   TSDCONCH,X'89'      SKIP CH 1 AFTER
         BE    TSSKIP1             YES
         CLI   TSDCONCH,X'8B'      SKIP CH 1 IMM.
         BE    TSSKIP1             YES
         B     TSWSPAC             FORCE TO SPACING COMMAND
         SPACE
TSSKIP1  DS    0H
         MVC   0(2,R14),=X'150C'   NL +  FORM FEED
         LA    R14,2(R14)
         LA    R1,1
         ST    R1,TSDCURPO         RESET LINE #
         B     TSADDLN             INSERT NLS
         TITLE 'CHECK FOR STOP/MODIFY'
***********************************************************************
*        CHECK FOR STOP/MODIFY
***********************************************************************
TSCMCK   DS    0H
         L     R1,TSDCOMM          GET COMM PTRS
         TM    0(R1),X'40'         POSTED?
         BNO   4(R11)              NO - RETURN +4
         SPACE
TSCMLOOK DS    0H                  LOOK FOR CIB S
         L     R2,TSDCOMMP         GET COMM PTRS
         L     R2,4(R2)            CIB ORG
         LTR   R2,R2               END OF CHAIN
         BZ    TSCMFINI            END
         CLI   4(R2),X'40'         STOP
         BE    TSCMSTOP
         CLI   4(R2),X'44'         MODIFY
         BE    TSCMMOD
         CLI   4(R2),X'04'         START
         BE    TSCMMOD             HANDLE LIKE MOD
         SPACE 2
TSCMDEL  DS    0H                  DELETE A CIB
         L     R3,TSDCOMMP         ORG.
         LA    R3,4(R3)            POINT TO POINTER (TRUST ME)
         QEDIT ORIGIN=(R3),BLOCK=(2)  DELETE BLOCK
         B     TSCMLOOK            LOOK FOR MORE
         TITLE 'PROCESS MODIFY COMMANDS'
***********************************************************************
*        PROCESS MODIFY
***********************************************************************
TSCMMOD  DS    0H
         LA    R0,TSDTXT           HOLD AREA
         LA    R1,L'TSDTXT         L'
         LH    R15,14(R2)          L' OF MODIFY
         LA    R15,2(R15)          +2BYTE LEN
         ICM   R15,8,=C' '         PAD W/ BLANKS
         LA    R14,14(R2)          GET FROM ADDR
         MVCL  R0,R14              MOVE MODIFY TEXT
         MVC   TSDCONID,12(R2)     SAVE MCS ID. FOR RESPONSE
         SPACE 2
********************************************************************
**             NULL INPUT
********************************************************************
         CLI   TSDTXT+2,C' '       NULL INPUT
         BE    TSCMDEL             JUST DELETE IT
         SPACE 2
********************************************************************
**             CANCEL (CANCEL IMMEDIATELY)
********************************************************************
         CLC   =C'DUMP',TSDTXT+2
         BNE   TSCMFLU             NO - TRY FLUSH
         STM   R0,R15,SAVE0C1
         DC    X'0000'             BLOW IT
         SPACE 2
********************************************************************
**             FLUSH (CANCEL CURRENT PRINT )
********************************************************************
TSCMFLU  EQU   *
         CLC   =C'FLUSH',TSDTXT+2  FLUSH CURRENT D.S.
         BNE   TSCMCAN             NO - TRY CANCEL
         MVI   TSDFLUSH,C'Y'       SET FLAG
         LA    R3,TSDTXT+2+5       POINT TO USERID (IF ANY)
         CLC   =C',LTSO',0(R3)
         BE    TSCMTSO
         B     TSCMOK
         SPACE 2
********************************************************************
**             STOP (AFTER CURRENT  OUTPUT QUIT)
********************************************************************
TSCMCAN  DS    0H
         CLC   =C'STOP',TSDTXT+2   STOP AFTER CURR. D.S.
         BNE   TSCMPAUS            NO - TRY PAUSE
         MVI   TSDSTOPA,C'Y'       SET IT
         LA    R3,TSDTXT+2+4       POINT TO USERID (IF ANY)
         B     TSCMOK
         SPACE 2
********************************************************************
**             PAUSE STOP FOR A WHILE
********************************************************************
TSCMPAUS DS    0H
         CLC   =C'PAUSE',TSDTXT+2  STOP
         BNE   TSCMSTRT            NO-TRY START
         MVI   TSDPAUSE,C'Y'       PAUSE FOR A START CMD
         B     TSCMOK
         SPACE 2
********************************************************************
**             START UP AFTER PAUSE
********************************************************************
TSCMSTRT DS    0H
         CLC   =C'START',TSDTXT+2  START
         BNE   TSCMFORM            NO-LOOK FOR KEYWORDS
         MVI   TSDPAUSE,C'N'       TURN OFF PAUSE SWITCH
         B     TSCMOK
         SPACE 2
**********************************************************************
**             SCAN FOR KEYWORDS
**********************************************************************
TSCMFORM DS    0H
         LA    R14,TSCMFMXT        SET UP RETURN
         TITLE 'PARSE DOWN STARTUP PARMS OR MODIFY KEYWORDS'
**********************************************************************
*                                                                    *
**             PARSE DOWN STARTUP PARMS OR MODIFY KEYWORDS           *
*                                                                    *
**********************************************************************
TSCMFMSU DS    0H
         ST    R14,TSDFMRET        SAVE RETURN ADDR
         LA    R3,TSDTXT+2         GET 1ST BYTE OF PARM DATA AFTER L'
         SPACE
TSCMTYP  DS    0H
         CLI   0(R3),C','          LOOK FOR KEYWORDS
         BNE   *+8                 "
         LA    R3,1(R3)            BUMP PAST COMMA
         CLC   0(2,R3),=C'P='      PRINTER
         BE    TSCMPRNT            HANDLE
         CLC   0(2,R3),=C'A='      APPLICATION
         BE    TSCMAPPL            HANDLE
         CLC   0(2,R3),=C'C='      CLASS
         BE    TSCMCLAS            HANDLE
         CLC   0(2,R3),=C'D='      DESTINATION
         BE    TSCMDEST            HANDLE
         CLC   0(2,R3),=C'F='      FORM
         BE    TSCMFRM             HANDLE
         CLC   0(2,R3),=C'S='      SEPERATOR
         BE    TSCMSEP             HANDLE
         CLC   0(2,R3),=C'L='      PAGE LENGTH
         BE    TSCMLEN             HANDLE
         CLC   0(2,R3),=C'W='      PAGE WIDTH
         BE    TSCMWID             HANDLE
         CLC   0(2,R3),=C'T='      TRACE
         BE    TSCMTRC             HANDLE
         B     TSCMFMER            NOT FORM - ERROR
         SPACE 2
**********************************************************************
**       PROCESS FORMS CHANGE
**********************************************************************
TSCMFRM  DS    0H                  FORM
         LA    R0,4                ALL OUT FORMS ARE 4 CHAR ***CHRYSLER
         LA    R3,2(R3)            BUMP PAST F=
         LA    R15,TSDHFORM        GET HOLD AREA
         MVC   TSDHFORM,TSSFORM    INITIALIZE TO DEFAULT FORM
         BAL   R14,TSCMSCN         EDIT DATA
         LTR   R0,R0               4 CHARS
         BZ    TSCMCKMR            YES-GOOD
         MVC   TSDHFORM,TSSFORM    NO-RETURN TO DEFAULT
         B     TSCMFMER            GIVE INVALID MSG
         SPACE 2
**********************************************************************
**       PROCESS APPLID CHANGE
**********************************************************************
TSCMAPPL DS    0H                  PRINTER DEFINITION
         LA    R3,2(R3)            BUMP PAST T=
         LA    R0,8                8 CHAR MAX
         LA    R15,TSDAPPL         GET HOLD AREA
         BAL   R14,TSCMSCN         EDIT DATA
         MVC   APPLE+1(8),TSDAPPL       MOVE NAME TO ACB-DEF
         B     TSCMCKMR            ASSUME DATA GOOD
         SPACE 2
**********************************************************************
**       PROCESS PRINTER CHANGE
**********************************************************************
TSCMPRNT DS    0H                  PRINTER DEFINITION
         LA    R3,2(R3)            BUMP PAST T=
         LA    R0,8                8 CHAR MAX
         LA    R15,TSDPRINT        GET HOLD AREA
         BAL   R14,TSCMSCN         EDIT DATA
         MVC   TSDNIBL+12(8),TSDPRINT      MOVE NAME TO NIB
         B     TSCMCKMR            ASSUME DATA GOOD
         SPACE 2
**********************************************************************
**       PROCESS TRACE
**********************************************************************
TSCMTRC  DS    0H                  TRACE CHANGE
         MVI   TSDTRACE,C'N'       SET TRACE OFF
         LA    R3,2(R3)            BUMP PAST T=
         LA    R0,8                8 CHAR MAX
         LA    R15,TSDTRACE        GET HOLD AREA
         BAL   R14,TSCMSCN         EDIT DATA
         CLI   TSDTRACE,C'Y'
         BNE   TSCMCKMR
         OPEN  (SNAPPER,OUTPUT)
         B     TSCMCKMR            ASSUME DATA GOOD
         SPACE 2
**********************************************************************
**       PROCESS CLASS CHANGE
**********************************************************************
TSCMCLAS DS    0H                  CLASS CHANGE
         LA    R0,8                8 CHAR MAX
         LA    R3,2(R3)            BUMP PAST C=
         LA    R15,TSDHCLAS        GET HOLD AREA
         MVC   TSDHCLAS,TSSCLAS    MOVE DEFAULT
         BAL   R14,TSCMSCN         EDIT DATA
         B     TSCMCKMR            ASSUME DATA GOOD
         SPACE 2
**********************************************************************
**       PROCESS DESTINATION
**********************************************************************
TSCMDEST DS    0H                  DEST CHANGE
         LA    R0,8                8 CHAR MAX
         LA    R3,2(R3)            BUMP PAST C=
         LA    R15,TSDHDEST        GET HOLD AREA
         MVC   TSDHDEST,=CL8' '    CLEAR AREA
         BAL   R14,TSCMSCN         EDIT DATA
         B     TSCMCKMR            ASSUME DATA GOOD
         SPACE 2
**********************************************************************
**       PROCESS PAGE SEPERATOR OPTION
**********************************************************************
TSCMSEP  DS    0H                  SEP  CHANGE
         LA    R0,1                1 CHAR MAX
         LA    R3,2(R3)            BUMP PAST S=
         LA    R15,TSDSEP          GET SEP SWITCH
         MVI   TSDSEP,C'Y'         SET IT ON AS A DEFAULT
         BAL   R14,TSCMSCN         EDIT DATA
         B     TSCMCKMR            ASSUME DATA GOOD
         SPACE 2
**********************************************************************
**       PROCESS PAGE LENGTH CHANGE
**********************************************************************
TSCMLEN  DS    0H                  LEN  CHANGE
         LA    R1,TSDLEN           GET ANSWER LOC.
         SPACE 2
**********************************************************************
**       COMMON CODE FOR L=NNN,W=NNN
**********************************************************************
TSCMLENT DS    0H                  SCAN FOR NUMERIC KEYWORD
         LA    R0,3                3 DIG. MAX
         LA    R3,2(R3)            BUMP PAST L=
         LA    R15,TSDWORK         GET WORK AREA
         XC    TSDWORK,TSDWORK     CLEAR IT
         BAL   R14,TSCMSCN         EDIT DATA
         SH    R0,=H'3'            (RESIDUAL-MAX)=NEG. DIGITS
         BZ    TSCMCKMR            NONE-USE CURRENT
         LPR   R0,R0               DIGITS OF DATA
         XR    R15,R15             CLEAR WORK
TSCMLNXT DS    0H                  CONVERT VALUE TO BINARY
         IC    R14,TSDWORK         PICK A DIGIT
         N     R14,=F'15'          GET NUMERIC PART
         MH    R15,=H'10'          SHIFT CURRENT VALUE A DIGIT
         AR    R15,R14             ADD NEW DIGIT
         MVC   TSDWORK(7),TSDWORK+1 SHIFT DIGITS DOWN
         BCT   R0,TSCMLNXT         LOOP
         ST    R15,0(R1)           SAVE NEW VALUE IN ANSWER AREA
         B     TSCMCKMR            ASSUME DATA GOOD
         SPACE 2
**********************************************************************
**       PROCESS PAGE WIDTH CHANGE
**********************************************************************
TSCMWID  DS    0H                  WID  CHANGE
         LA    R1,TSDWID           GET ANSWER LOC.
         B     TSCMLENT            GO TO COMMON CODE
         SPACE 2
**********************************************************************
**       LOOK FOR ANOTHER KEYWORD
**********************************************************************
TSCMCKMR DS    0H                  SEE IF ANOTHER KEYWORD
         CLI   0(R3),C' '          END OF STRING
         BE    TSCMFMOK            YES-GIVE OK MSG
         B     TSCMTYP             NO-CLASSIFY
         SPACE 2
**********************************************************************
**       MOVE KEYWORD DATA
**********************************************************************
TSCMSCN  DS    0H                  EDIT
         CLI   0(R3),C' '          END OF STRING
         BE    TSCMSCND            YES-RETURN
         CLI   0(R3),C','          END OF STRING
         BE    TSCMSCND            YES-RETURN
         MVC   0(1,R15),0(R3)      MOVE DATA INTO WORK
         LA    R3,1(R3)            BUMP
         LA    R15,1(R15)          "
         BCT   R0,TSCMSCN          LOOP
TSCMSCND DS    0H                  RETURN
         BR    R14                 "
         SPACE 2
**********************************************************************
*
**             PARSE RETURN
*
**********************************************************************
TSCMFMOK DS    0H                  PARSE OK
         L     R14,TSDFMRET        GET RETURN ADDR
         B     0(R14)              +0 = OK
TSCMFMER DS    0H                  BAD PARSE
         L     R14,TSDFMRET        GET RETURN
         B     4(R14)              +4 = BAD
         TITLE 'RETURN FROM PARSE  OF MODIFY TEXT'
**********************************************************************
*
**              RETURN FROM PARSE  OF MODIFY TEXT
*
**********************************************************************
TSCMFMXT DS    0H                  BRANCH TABLE
         B     TSCMOK              +0=OK
         B     TSCMINV             +4=ERR
         SPACE 2
**********************************************************************
*
**             GIVE OPER STATUS TO MODIFY COMMAND
*
**********************************************************************
TSCMOK   DS    0H                  GIVE OPER OK MSG
         MVC   TSDCMW1,=CL7'OK'
         B     TSCMWTO             ISSUE WTO
         SPACE 2
TSCMINV  DS    0H                  GIVE INVALID MSG
         MVC   TSDCMW1,=CL7'INVALID'
TSCMWTO  DS    0H
         XR    R0,R0
         IC    R0,TSDCONID         GET CALLERS ID
         LA    R1,TSDCMWT1         WTO
         SVC   35                  WTO
         SPACE 2
*********************************************************************
**             GIVE OPERATOR COMPLETE STATUS
*********************************************************************
         L     R0,TSDLEN           GET BINARY PAGE LENGTH
         CVD   R0,TSDWORK          MAKE PRINTABLE
         UNPK  TSDLEN2,TSDWORK+6(2)
         OI    TSDLEN2+L'TSDLEN2-1,C'0'
         L     R0,TSDWID           GET BINARY PAGE WIDTH
         CVD   R0,TSDWORK          MAKE PRINTABLE
         UNPK  TSDWID2,TSDWORK+6(2)
         OI    TSDWID2+L'TSDWID2-1,C'0'
         XR    R0,R0
         IC    R0,TSDCONID         GET CALLERS ID
         LA    R1,TSDCMT2          WTO
         SVC   35                  WTO
         SPACE 2
*********************************************************************
**             LOOK FOR SPEC. INTRDR MODIFY
**              F PX,FLUSH,PGMXXX,PGMYYY (XXX=CALLER)
**                  (YYY=ID OF JOBNAME ON PRINTER(VERIFY))
**             GIVE TSO USER ACK. MSG.
*********************************************************************
TSCMTSO  DS    0H
         CLC   =C',LTSO',0(R3)     TSO CALLER  ***LEOBAU***
         BE    TSCMPGM             YES - FILL USERID IN
         MVC   TSDBYWHO,=CL8'OPER'
         B     TSCMEND             EXIT
         SPACE 2
*********************************************************************
**             VALIDITY CHECK TSOUSERS INFO
**             TSO CP: JESCNCL PGMYYY N (PGMYYY=JOBNAME, N=PRINTER#)
*********************************************************************
TSCMPGM  DS    0H
         MVC   TSDBYWHO(6),1(R3)   TELL WHO CANCELLED OUTPUT
         MVC   TSDBYWHO+6(2),=C'  '
         CLI   7(R3),C','          USER PROVIDES JOBNAME
         BNE   TSCMPINV            NO-PROBABLY OPER
         CLC   8(6,R3),TSDJOBN     MATCH?
         BE    TSCMPOK             YES-OK TO FLUSH
         SPACE 2
TSCMPINV DS    0H
         MVC   TSDCMW1,=CL7'INVALID'
         MVI   TSDFLUSH,C'N'       RESET FLUSH
TSCMPOK  DS    0H
         LA    R0,L'TSDCMW0
         LA    R1,TSDCMW0
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,       XXXXXXXXXX
               USERIDL=TSDBYWHO
TSCMEND  DS    0H
         B     TSCMDEL
TSCMSTOP DS    0H
         BR    R11                 RETURN +0 STOP ISSUED
         SPACE 2
*********************************************************************
**             SEE IF PAUSE ISSUED
*********************************************************************
TSCMFINI DS    0H
         CLI   TSDPAUSE,C'Y'       PAUSE SET
         BNE   4(R11)              NO-RETURN +4 NO STOP ISSUED
         L     R1,TSDCOMM          GET ADDR OF STOP/MOD ECB
         WAIT  1,ECB=(1)           ONLY WAIT FOR STOP/MODIFY
         B     TSCMCK              PROCESS NEW MODIFYS
         B     4(R11)              RETURN +4 NO STOP ISSUED
         TITLE 'CALL DYNAMIC ALLOCATE/UNALLOCATE SUBROUTINE'
***********************************************************************
*        CALL DYNAMIC ALLOCATE/UNALLOCATE SUBROUTINE
***********************************************************************
DYNAL    LA    R1,TSDS99           LOAD ADDR. OF ALLOCATE PARM LIST
         DYNALLOC
         LTR   R15,R15             RETURN NORMAL?
         BZR   R11                 RETURN IF NORMAL RETURN
         MVC   TSDWERR1,=CL20'BAD DYNALLOC RETURN CODE'
         B     TSFATALF            ISSUE FATAL ERROR W/ R15
         TITLE 'F A T A L  E R R O R  M E S S A G E S'
***********************************************************************
*        F A T A L  E R R O R  M E S S A G E S
***********************************************************************
TSFATALF DS    0H
         CVD   R15,TSDWORK         CONVERT CALLERS RETURN CODE IN R15
         STM   R14,R12,12(R13)     SAVE MY REGS
         UNPK  TSDWERR2,TSDWORK+6(2) 999 MAX RC SUPPORTED
         OI    TSDWERR2+L'TSDWERR2-1,C'0' MAKE PRINTABLE
         LA    R1,TSDWERR1         GET MF=L FORM OF WTO
         SVC   35                  ISSUE WTO
         ABEND  5,DUMP
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL
**********************************************************************
         LTORG
         TITLE 'VTAM PHYSICAL ERROR ROUTINE '
***********************************************************************
*        PHYSICAL ERROR ROUTINE
**********************************************************************
         CNOP  0,8
SYNADB   EQU   *
         STM   R0,R15,SAVE0C1
         WTO   'VTAMWTR - SYNAD EXIT ENTERED - NOT INTERV REQ'
         DC    X'0000'
         TITLE 'VTAM LOGICAL ERROR ROUTINE '
***********************************************************************
*        LOGICAL ERROR ROUTINE
**********************************************************************
         CNOP  0,8
LERADA   EQU   *
         USING LERADA,R15
         STM   R0,R15,LSAVE0C1
         USING IFGRPL,R3
         L     R3,=A(TSDRPL1)
         CLC   RPLRTNCD(2),=X'1448'     UNWANTED CLEAR REC'D
         BER   R14                      CARRY ON
         WTO   'VTAMWTR - LERAD ERROR EXIT ENTERED'
         DC    X'0000'
         SPACE 3
LSAVE0C1 DC    18F'0'
         LTORG
         DROP  R3
         DROP  R15
         TITLE 'RELREQ ASYN EXIT-ROUTINE'
***********************************************************************
* RELEASE ON REQUEST EXIT
*
* FUNCTION = EXIT IS ENTERED, WHEN ONE APPLICATION PROGRAM REQUESTS
*            A SESSION WITH A LOGICAL UNIT, THAT IS IN SESSION WITH
*            THIS APPLICATION PROGRAM.
*
* ENTRY-POINT = RELREQA
*
* INPUT
*  REGISTERS
*     0      =  UNPREDICTABLE
*     1      =  POINTER TO A 2 WORD PARAMETER LIST
*     2 - 13 =  UNPREDICTABLE
*     14     =  ADDRESS TO RETURN CONTROL TO
*     15     =  ENTRY ADDRESS OF THIS ROUTINE
*
* PARAMETER LIST - 6 WORDS
*     1      =  ACB ADDRESS
*     2      =  POINTER TO SYMBOLIC NAME OF LOGICAL UNIT
*
* OUTPUT
*  A REQUEST TO VTAM TO TERMINATE TH SESSION (CLSDST) OR TO IGNORE
*  THE RELREQ.
*
* EXTERNAL REFERENCES =   CLSDST
*
* EXIT,  NORMAL = BR 14
*
* EXIT,  ABNORMAL = S0C1
*
* REGS USED
*     6  = BASE
*
**********************************************************************
         CNOP  0,8
RELREQA  EQU   *
         LR    R6,R15             ESTABLISH ADDESSABILITY
         USING RELREQA,R6         REG 6 = BASE-REGISTER
         ST    R14,$SAVE14        SAVE RETURN ADDRESS
         LA    R13,$SAVEA         SAVE AREA FOR THIS EXIT
         SPACE
         L     R5,=A(TSDSESS)     IS SESSION CURRENT OR WAITING
         CLI   0(R5),X'00'        WAITING ?
         BNE   $END               DENY REQUEST
         SPACE
         L     R5,4(R1)           ADDRESS OF NAME OF REQUESTED LU
         MVC   WTO1+8(8),0(R5)    NAME OF REQUESTED RU
         SPACE
WTO1     WTO   'XXXXXXXX = NAME OF REQUESTED RU'
         L     R2,=A(TSDACB1)
         L     R3,=A(TSDRPL1)
         L     R4,=A(TSDNIBL)
         CLC   0(8,R5),12(R4)
         BNE   $0C1
         SPACE
         CLSDST RPL=(R3),NIB=(R4),ACB=(R2),OPTCD=RELEASE
         SPACE
         LTR   15,15
         BNZ   $0C1
         L     R5,=A(TSDKZ)
         MVI   0(R5),X'00'        CLSDST SUCCESFULL
         B     $END
         SPACE 3
$0C1     DS    0H
         STM   R0,R15,$SAVE0C1
         WTO   'VTAMWTR - CLSDST IN RELREQ EXIT FAILED'
         DC    X'0000'
         SPACE 3
$END     DS    0H
         L     R14,$SAVE14        RESTORE RETURN REGISTER
         BR    R14
         SPACE 3
$SAVEA   DS    18F
$SAVE0C1 DS    18F
$SAVE14  DS    F
         LTORG
         DROP  R6
         TITLE 'LOGON  ASYN EXIT-ROUTINE'
***********************************************************************
* LOGON EXIT ROUTINE
*
* FUNCTION = ESTABLISH A SESSION WITH A PRINTER, IF SLU-NAME
*            EQUAL TO REQUESTED PRINTER; OTHERWISE, REJECT
*            THE REQUEST FOR A SESIION
*
* ENTRY-POINT = LOGONA
*
* INPUT
*  REGISTERS
*     0      =  UNPREDICTABLE
*     1      =  POINTER TO A 4-WORD PARAMETER LIST
*     2 - 13 =  UNPREDICTABLE
*     14     =  ADDRESS TO RETURN CONTROL TO
*     15     =  ENTRY ADDRESS OF THIS ROUTINE
*
* PARAMETER LIST - 6 WORDS
*     1      =  ACB ADDRESS
*     2      =  POINTER TO SYMBOLIC NAME OF LOGICAL UNIT
*     3      =  ZEROES
*     4      =  LENGTH OF LOGON MESSAGE
*     5      =  ADDRESS OF READ-ONLY RPL
*     6      =  CID OF PENDING ACTIVE SESSION
*
* OUTPUT
*  A REQUEST TO VTAM TO ACCEPT OR REJECT THE SESSION; OR PROGRAM
*  TERMINATION AND A DUMP IF UNABLE TO CONTINUE
*
* EXTERNAL REFERENCES =  OPNDST, CLSDST
*
* EXIT,  NORMAL = BR 14
*
* EXIT,  ABNORMAL = S0C1
*
* REGS USED
*     6  = BASE
*
*
*
*
**********************************************************************
         CNOP  0,8
LOGONA   EQU   *
         LR    R6,R15             ESTABLISH ADDESSABILITY
         USING LOGONA,R6          REG 6 = BASE-REGISTER
         ST    R14,@SAVE14        SAVE RETURN ADDRESS TO VTAM
         LA    R13,@SAVEA         SAVE AREA FOR THIS EXIT
         SPACE
         L     R5,=A(TSDSESS)     POINT TO TSDSESS
         CLI   0(R5),X'11'        IS CLOSE ACB IN PROGRESS
         BE    @RETURN            YES, ALLOW CLOSE
         SPACE
         L     R5,=A(TSDKZ)       POINT TO TSDKZ
         CLI   0(R5),X'FF'        OPNDST ALREADY SUCCESSFULL
         BE    @CLSDST            YES, REJECT INIT
         SPACE
         LR    R11,R1             SAVE PARAMETER LIST ADDRESS
         L     R5,4(R11)          POINT TO SYMBOLIC NAME OF LU
         L     R4,=A(TSDNIBL)
         CLC   0(8,R5),12(R4)     LU-NAME OF REQUESTED PRINTER
         BNE   @CLSDST
         SPACE
@WAIT    DS    0H
         L     R1,=A(TSDECB)          LOAD ECB
         POST  (1),0                  POST ECB
         SPACE
         L     R5,=A(TSDBYTE)
         MVI   0(R5),X'FF'            INDICATE MAIN PGM POST DONE
         B     @RETURN
         SPACE 3
@CLSDST  DS    0H
         CLSDST RPL=@RPL,OPTCD=SYN    REJECT REQUEST
         SPACE 3
@RETURN  DS    0H
         L     R14,@SAVE14        RESTORE RETURN REGISTER
         BR    R14
         SPACE 3
@SAVEA   DS    18F
@SAVE0C1 DS    18F
@SAVE14  DS    F
@RPL     RPL   AM=VTAM
         LTORG
         DROP  R6
         TITLE 'TPEND  ASYN EXIT-ROUTINE'
***********************************************************************
* TPEND EXIT ROUTINE
*
* FUNCTION = SET AN INDICATION FOR THE MAINLINE PROGRAM
*            TO CLOSE THE ACB AND TERMINATE
*
* ENTRY-POINT = TPENDA
*
* INPUT
*  REGISTERS
*     0      =  UNPREDICTABLE
*     1      =  POINTER TO A 2-WORD PARAMETER LIST
*     2 - 13 =  UNPREDICTABLE
*     14     =  ADDRESS TO RETURN CONTROL TO
*     15     =  ENTRY ADDRESS OF THIS ROUTINE
*
* PARAMETER LIST - 2 WORDS
*     1      =  ACB ADDRESS
*     2      =  A VALUE, INDICATING WHY TPEND WAS ENTERED
*
* OUTPUT
*  INDICATION TO CLOSE ACB SET FOR MAIN PROGRAM
*
* EXTERNAL REFERENCES =  POST
*
* EXIT,  NORMAL = BR 14
*
* EXIT,  ABNORMAL = NONE
*
* REGS USED
*     6  = BASE
*
*
*
*
**********************************************************************
         CNOP  0,8
TPENDA   EQU   *
         LR    R6,R15             ESTABLISH ADDESSABILITY
         USING TPENDA,R6          REG 6 = BASE-REGISTER
         ST    R14,TSAVE14        SAVE RETURN ADDRESS TO VTAM
         LA    R13,TSAVEA         SAVE AREA FOR THIS EXIT
         SPACE
         WTO   '***  VTAMWTR - TPEND EXIT ENTERED ***'
         SPACE
         L     R5,=A(TPENDECB)    POINT TO TPENDECB
         POST  (R5)               INDICATE TPEND REQUIRED
         SPACE 3
         L     R14,TSAVE14        RESTORE RETURN REGISTER
         BR    R14
         SPACE 3
TSAVEA   DS    18F
TSAVE0C1 DS    18F
TSAVE14  DS    F
         LTORG
         DROP  R6
         TITLE 'STATIC STORAGE'
**********************************************************************
*        STATIC STORAGE
**********************************************************************
         PRINT GEN
SAVE     DC    18F'0'
SAVE0C1  DC    18F'0'
TSDECB   DC    F'0'
TPENDECB DC    F'0'
         SPACE
TSDWECB  DC    F'0'                HASP COMM ECB
         DC    A(TPENDECB)         TPEND-ECB
TSDCOMM  DS    A                   O/S COMM ECB
         SPACE
TSDKZ    DC    X'00'
TSDSESS  DC    X'00'
TSDBYTE  DC    X'00'
HELLO    DC    CL120'VTAMWTR - VTAM 328X EXTERNAL WRITER - HELLO'
HELLO1   EQU   HELLO+60,50
GOODBYE  DC    CL120'VTAMWTR - VTAM 328X EXTERNAL WRITER - GOODBYE'
GOODBYE1 EQU   GOODBYE+60,50
TXTENDDS DS    0CL01
         DC    C'          ***   END OF DATASET  ***           '
TXTDATUM DC    CL50' '
LTXTEND  EQU   *-TXTENDDS
ZAE      DC    P'0'
USERID   DC    CL8'LTSOHI'
         SPACE
NLBLOCK  DS    0CL125
CC3270   DC    X'F5C90C'
CC3289   DC    X'F5C900'
NLS      DC    120X'15'     BUFFER OF NL'S FOR LINEUP
         SPACE
APPLE    DC    X'08',CL8' '
STLINENO DC    F'0'
         SPACE 2
EXLSTA   EXLST AM=VTAM,SYNAD=SYNADA,LERAD=LERADA,RELREQ=RELREQA,       X
               LOGON=LOGONA,TPEND=TPENDA
         SPACE 2
TSSTR    TR    0(*-*,R14),TSSTBL   EXECUTED
         SPACE
BLANKS   DC    CL16' '
WTRSSOB  DS    0F
WTSSOBID DC    CL4'SSOB'
WTSSOBSZ DC    AL2(SSOBHSIZ)
WTSSOBFN DC    AL2(SSOBSOUT)
WTRSOSIZ EQU   *-WTRSSOB
         SPACE 2
TSSTBL   DC    256C'?'
UPCTBL   EQU   TSSTBL
         ORG   UPCTBL+C' '
         DC    C' '
         ORG   UPCTBL+C''
         DC    C'.<(+&&'
         ORG   UPCTBL+C'!'
         DC    C'!$*);^-/'
         ORG   UPCTBL+C','
         DC    C',%_>?'
         ORG   UPCTBL+C':'
         DC    C':#@''="'
         ORG   UPCTBL+X'81'
         DC    X'81,82,83,84,85,86,87,88,89'
         ORG   UPCTBL+X'91'
         DC    X'91,92,93,94,95,96,97,98,99'
         ORG   UPCTBL+X'A2'
         DC    X'A2,A3,A4,A5,A6,A7,A8,A9'
         ORG   UPCTBL+C'A'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+C'J'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+C'S'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'0'
         DC    C'0123456789'
         ORG
         SPACE
         DS    0D
         DC    C'00000NIB'
TSDNIBL  NIB   NAME=,BNDAREA=0,LOGMODE=0,MODE=RECORD,              XXXXX
               LISTEND=YES
LTSDNIBL EQU   *-TSDNIBL
         DS    0D
         DC    C'+++++NIB'
NIB      NIB   NAME=,BNDAREA=0,LOGMODE=0,MODE=RECORD,              XXXXX
               LISTEND=YES
LNIB     EQU   *-NIB
         SPACE 2
         DS    0D
         DC    C'00000RPL'
TSDRPL1  RPL   AM=VTAM,ACB=TSDACB1
LTSDRPL1 EQU   *-TSDRPL1
         DS    0D
         DC    C'00000RPL'
RPL1     RPL   AM=VTAM,ACB=TSDACB1
LRPL1    EQU   *-RPL1
         SPACE 2
         DS    0D
         DC    C'00000ACB'
TSDACB1  ACB   AM=VTAM,MACRF=LOGON,EXLST=EXLSTA,APPLID=APPLE
TSDDCBIS EQU   *
         SPACE
TSDDCBI  DCB   DSORG=PS,MACRF=GL,BUFNO=2,SYNAD=TSSYN,EODAD=TSGEOD, XXXXX
               DDNAME=SCRUMBO
TSSDCBIS EQU   *
         SPACE
TSSDCBI  DCB   DSORG=PS,MACRF=GL,BUFNO=2,SYNAD=TSSYN,EODAD=TSGEOD, XXXXX
               DDNAME=SCRUMBO
TSSDCBIE EQU   *
         SPACE
SNAPPER  DCB   DDNAME=SNAPPER,DSORG=PS,MACRF=(W),RECFM=VBA,LRECL=125, XX
               BLKSIZE=1632
         SPACE
TSDBFR   DC    CL50'BEGINNING OUTPUT ON PRINTER PXXXXXXX'
TSDBFCL  EQU   TSDBFR+28,8
         SPACE
TSDXTRC  EXTRACT ,FIELDS=COMM,MF=L
         SPACE
TSDCMWT1 WTO   'VTAMWTR - XXXXXXX',MCSFLAG=(REG0),MF=L
TSDCMW0  EQU   TSDCMWT1+7,14
TSDCMW1  EQU   TSDCMWT1+14,7
         SPACE
TSDFLSMS DC    C'OUTPUT DELETED BY XXXXXXXX'
TSDBYWHO EQU   TSDFLSMS+L'TSDFLSMS-8,8
TSDBEG   DC    CL121' '
TSDBEGJ  EQU   TSDBEG+60,44
TSDBEGJN EQU   TSDBEG+50,8
TSDBEGJT EQU   TSDBEG+1,48
         SPACE
TSDCMT2  DS    0F                  CURRENT OPTIONS MESSAGE
         DC    AL2(TSDCMT2E-*)     L'TXT+L'ROUT+L'DESC
         DC    X'4000'             MCSFLAG=(REG0)
         DC    C'VTAMWTR-F='
         SPACE
TSDHFORM DC    C'TSO1'             CURRENT FORM
         DC    C',C='
TSDHCLAS DC    CL8'Z'              CURRENT CLASS(ES)
         DC    C',S='
TSDSEP   DC    CL1'Y'              DEFAULT PAGE SEPERATORS
         DC    C',L='
TSDLEN2  DC    C'072'              DEFAULT PAGE SIZE
         DC    C',W='
TSDWID2  DC    C'132'              DEFAULT PAGE WIDTH
         DC    C',PAUSE='
TSDPAUSE DC    C'N'                DEFAULT PAUSE IMMEDIATE OPTION
         DC    C',D='
TSDHDEST DC    CL8'LOCAL'           CURRENT DESTINATION
         DC    C',TRACE='
TSDTRACE DC    C'N'
         DC    C',A='
TSDAPPL  DC    CL8' '
         DC    C',P='
TSDPRINT DC    CL8' '
TSDCMT2E EQU   *                   NO ROUT/DESC W/ REG0
TSDLEN   DC    F'72'               PAGE SIZE
TSDWID   DC    F'132'              PAGE WIDTH
TSDWERR  WTO   'VTAMWTR-X....X....X ...X....,R15=999',ROUTCDE=(8)
TSDWERR1 EQU   TSDWERR+4+7,20      DYNAMIC MESSAGE PORTION
TSDWERR2 EQU   TSDWERR+4+32,3      DECIMAL R15 RETURN CODE
TRACELST DC    A(TSDBUFR),X'80',AL3(TSDBUFR+2059)
TSSFORM  DC    C'TSO1'             CURRENT FORM
TSSCLAS  DC    CL8'Z'              CURRENT CLASS(ES)
         SPACE
***********************************************************************
*  EVERYTHING FROM HERE ON IS SET TO LOW VALUES AT INIT TIME          *
***********************************************************************
TSDWORK  DS    D                   WORK AREA
TSDCURPO DS    F                   CURRENT LINE NUMBER
TSDCOMMP DS    A                   PTR TO COMM PTRS
TSDILOC  DS    F                   OUTPUT RECORD TEXT LOC.
TSDISIZ  DS    F                   OUTPUT RECORD LENGTH  .
TSDIOERR DS    C                   C'Y' IF IO ERR TO PRT .
TSDCONCH DS    C                   CARRIAGE CONTROL
TSDSTOP  DS    C                   C'Y' TO STOP
TSDNEWDS DS    C                   C'Y' IF START OF DATA SET
TSDEOF   DS    C                   C'Y' IF POST EOF PROCESSING
TSDINIT  DS    C                   C'Y' IF IN START-UP PROCESSING
TSDCLAS  DS    C                   CURRENT JOB'S CLASS
TSDJOBN  DS    CL8                 CURR. JOBNAME
TSDJOBI  DS    CL8                 CURR. JOB ID.
TSDDSN   DS    CL44                CURR. DSN
TSDKEEP  DS    C                   KEEP SYSOUT
TSDRETLM DS    H                   RETRY COUNTER(IO ERR ON PRINTER)
TSDOPENX DS    F                   LIST FORM OF OPEN
TSDBSTA  DS    F                   START OF BUFFER AFTER CONTROL CHAR
TSDBFPTR DS    F                   CURR. AVAL. 328X BUFFER POS.
TSDTRUNC DS    C                   FORCE WRITE EVEN IF BFR NOT FULL
TSDCONID DS    X                   MCS ID OF MODIFY ISSUER
TSDFLUSH DS    C                   FLUSH CURRENT D.S. FLAG
TSDSTOPA DS    C                   SHUT DOWN AFTER CURR. WORK
TSDWRET  DS    F                   RETURN SAVE AREA
TSDFMRET DS    F                   RETURN SAVE AREA
TSDSAV01 DS    2F                  SAVE LINE/LEN DURING ASCII CONV.
@TSCVDAT DS    F                   ENTRY ADDR
@TSCVDPR DS    F                   PARM ADDR
         DS    0F                  SVC 99 BUILD AREA
TSDS99   DS    XL200               MUST BE LARGER THAN 'DYN' EQUATE
         DS    0F                  ALIGN
TSDTXT   DS    XL110               OPER MODIFY SAVEAREA
         DS    0F                  ALIGN
TSDSSOB  DS    (160)X              SSOB BUILD AREA
         DS    0F                  ALIGN
TSDBUFR  DS    XL2059              3284 BUILD AREA(BLOCK OUTPUT SOMEDAY
TSDCLRE  EQU   *
         SPACE
         PRINT NOGEN
*        TITLE 'DYNAMIC ALLOCATION PARAMETER LIST DSECT'
         IEFZB4D0
         SPACE
*        TITLE 'DYNAMIC ALLOCATION TEXT UNIT KEYS'
         IEFZB4D2
         SPACE
*        TITLE 'SUBSYSTEM OPTION BLOCK - ACCESS SYSOUT DATA SETS'
         IEFJSSOB (SO)
         SPACE
*        TITLE 'JOB ENTRY SUBSYSTEM COMMUNICATIONS TABLE'
         IEFJESCT
         SPACE
*        TITLE 'SUBSYSTEM IDENTIFICATION BLOCK'
         IEFJSSIB ,                SSIB MAPPING MACRO
         SPACE
*        TITLE 'WRITER DYNAMIC ALLOCATION WORK AREA'
DYNTXTPT DSECT
DSNTXTPT DS    F                   POINTER TO DSNAME TEXT UNIT
SSRTXTPT DS    F                   PTR. TO SUBSYSTEM REQUEST TEXT UNIT
DDNTXTPT DS    F                   PTR TO DDNAME RETURNED TEXT UNIT
UDNTXTPT DS    F                   DDNAME TEXT PTR. FOR UNALLOCATE
UORTXTPT DS    F                   OVERRIDE TEXT PTR. FOR UNALLOCATE
DYNTXTND EQU   *                   END OF TEXT POINTERS
DSNTXTUT DS    CL50                DSNAME TEXT UNIT
SSRTXTUT DS    CL10                SUBSYSTEM REQUEST TEXT UNIT
DDNTXTUT DS    CL14                DDNAME RETURNED  TEXT UNIT
UDNTXTUT DS    CL14                UNALLOCATE DDNAME TEXT UNIT
UORTXTUT DS    CL7                 OVERRIDE DISPOSITION FOR UNALLOCATE
DYNEND   EQU   *
TXTPTSIZ EQU   DYNTXTND-DYNTXTPT   SIZE OF TEXT UNIT POINTERS
DYNRBSIZ EQU   S99RBEND-S99RB      SIZE OF DYN ALLOC REQUEST BLOCK
DYN      EQU   TXTPTSIZ+DYNRBSIZ+(DYNEND-DSNTXTUT)+L'S99RBPTR
DISPDDN  EQU   DYNRBSIZ+DDNTXTPT-DYNTXTPT    ADDR. OF DDNAME IN DYN LST
DISPOVR  EQU   DYNRBSIZ+UORTXTPT-DYNTXTPT    ADDR. OVRIDE IN DYN LST
DISPUAL  EQU   DYNRBSIZ+UDNTXTPT-DYNTXTPT    ADDR. UNAL IN DYN LST
DISPDSN  EQU   DYNRBSIZ+DSNTXTPT-DYNTXTPT    ADDR. DSNAME IN DYN LIST
         SPACE
*        TITLE 'MISC EQUATES AND DSECTS'
         REGS
         PUSH  PRINT
         PRINT NOGEN               SAVE A LITTLE PAPER
         IFGRPL AM=VTAM
         DCBD  DSORG=PS
         IEZDEB
UCB      DSECT
         IEFUCBOB
         POP   PRINT
         CVT   DSECT=YES           CVT
         IHAPSA DSECT=YES          PSA
         IKJTCB DSECT=YES          TCB
         IEZJSCB                   JSCB
         END
//LKED.SYSLMOD DD DSN=LEO1.SYSTEM.LINKLIB,DISP=SHR
//LKED.SYSIN DD *
  INCLUDE SYSLMOD(DATUM)
  NAME VTAMWTR(R)
/*
