MOD      TITLE 'KARTENAUFBEREITUNG   UHYP04AS = MODKARTE'
         MACRO
&NAME    MACDCBDD &DCBADR
.**********************************************************************
.*                                                                    *
.*       DIESES MACRO LIEFERT IM REGISTER 1 DIE ADRESSE DES ZU
.*       EINEM NOCH NICHT EREOFFNETEN DCB GEHOERIGEN DD-ENTRIES.      *
.*       IST KEIN ENTSPRECHENDER DD-ENTRY VORHANDEN, D.H. DIE         *
.*       DD-KARTE FUER DEN DCB FEHLT, DANN IST REGISTER 1 = 0.        *
.*                                                                    *
.**********************************************************************
         LCLC  &C
         AIF   ('&DCBADR' EQ '').ERROR
&C       SETC  '&SYSNDX'
&NAME    STM   15,0,MAC&C.2     REGISTER SICHERN
         AIF   ('&DCBADR'(1,1) EQ '(').MAC2
         LA    15,&DCBADR           DCB-ADRESSE
.MAC1    SR    0,0
         MACTIOT
         LA    1,24(1)                 1.DD-ENTRY
MAC&C.1  IC    0,0(1)                  LAENGE DES DD-ENTRIES
         LTR   0,0                     LETZTER DD-ENTRY
         BZ    MAC&C.3
         CLC   4(8,1),40(15)            DD-NAMEN VERGLEICH TIOT-DCB
         BE    MAC&C.4
         AR    1,0                     NAECHSTER DD-ENTRY
         B     MAC&C.1
MAC&C.2  DC    2F'0'
MAC&C.3  SR    1,1
MAC&C.4  LM    15,0,MAC&C.2            REGISTER WIEDERHERSTELLEN
         MEXIT
.MAC2    LR    15,&DCBADR(1)           DCB-ADRESSE
         AGO   .MAC1
.ERROR   MACMES 2,MACDCBDD
         MEND
         MACRO
&NAME    MACTIOT
&NAME    L     1,16                    CVT-ADRESSE
         L     1,0(1)                  TCB-LISTE
         L     1,4(1)                  TCB-ADRESSE
         L     1,12(1)                 TIOT-ADRESSE
         MEND
         MACRO
         MACMES &CODE,&MAKRO
.***********************************************************
.*                                                         *
.*       MAKRO ZUR GENERIERUNG VON MNOTE-BEMERKUNGEN       *
.*             STANDARDTEXT IST   *** &MAKRO               *
.*                                                         *
.* CODE  TEXT                                              *
.* ****  ****                                              *
.*                                                         *
.*   1   MAKRO BEREITS EINGESETZT                          *
.*   2   PARAMETER FEHLT                                   *
.*   3   FEHLER IN PARAMETERANGABE                         *
.*                                                         *
.***********************************************************
         AIF   ('&CODE' EQ '' OR '&MAKRO' EQ '').NOTE1
         AIF   ('&CODE' EQ '0' OR '&CODE' GT '3').NOTE2
         AIF   ('&CODE' EQ '1').M1
         AIF   ('&CODE' EQ '2').M2
         AIF   ('&CODE' EQ '3').M3
.M1      MNOTE '*** &MAKRO MAKRO BEREITS EINGESETZT'
         MEXIT
.M2      MNOTE '*** &MAKRO PARAMETER FEHLT'
         MEXIT
.M3      MNOTE '*** &MAKRO FEHLER IN PARAMETERANGABE'
         MEXIT
.NOTE1   MACMES 2,MACMES
         MEXIT
.NOTE2   MACMES 3,MACMES
         MEND
         MACRO
         MCEQU
         GBLB  &MCEQU
         AIF   (&MCEQU).NOTE1
&MCEQU   SETB  1
*------- LEVEL = 03 -------$
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEXIT
.NOTE1   MACMES 1,MCEQU
         MEND
         SPACE 5
***********************************************************************
*                                                                     *
*        REGISTERUEBERSICHT                                           *
*        R2                                                           *
*        R3    FOLGEKARTENZAEHLER                                     *
*        R4    KARTENANFANG                                           *
*        R5    KARTENENDE (=LSP72)                                    *
*        R6    DATENANFANG                                            *
*        R7    DATENENDE                                              *
*        R8    ADRESSE IM KARTENSPEICHER                              *
*        R9    DATENLAENGE                                            *
*        R10   DCB-ADRESSE                                            *
*        R11                                                          *
*        R12   BASISREGISTER                                          *
*                                                                     *
***********************************************************************
         SPACE 5
***********************************************************************
*                                                                     *
*        RUECKKEHRCODE IN R15                                         *
*              0    OK                                                *
*              4    EODAD                                             *
*              8    OPENFEHLER                                        *
*              12   FOLGEKARTENFEHLER                                 *
*              16   FEHLER IN KARTE                                   *
*                                                                     *
***********************************************************************
         SPACE 3
MODKARTE START 0
         SAVE  (14,12),,EINGANGMODKARTE
         BALR  R12,0
         USING *,R12
         ST    R13,SAVEAREA+4
         LR    R14,R13
         LA    R13,SAVEAREA
         ST    R13,8(R14)
         LA    R15,4
         CLI   V2,0
         BNE   EOMOD
         SPACE 5
***********************************************************************
*                                                                     *
*        GRUNDSTELLUNG                                                *
*                                                                     *
***********************************************************************
         SPACE 3
B1       MVI   V1,0           ERSTKARTE
         LA    R10,HYPIN
         LA    R3,5           MAXIMAL 5 KARTEN
         LA    R8,V99         KARTENSPEICHER
         MVI   V3,C' '
         MVC   V3+1(249),V3
         MVC   V3+250(105),V3
         SPACE 5
***********************************************************************
*                                                                     *
*        EROEFFNUNG
*              IST DER DCB HYPIN NOCH NICHT EROEFFNET (ERSTANSPRUNG)  *
*              SO WIRD HIER DAS OPEN DURCHGEFUEHRT.                   *
*              RUECKKEHRCODE = 8 WENN OPENFEHLER                      *
*                                                                     *
***********************************************************************
         SPACE 3
C1       TM    48(R10),X'10'
         BO    C2
         MACDCBDD (R10)
         LTR   R1,R1
         BZ    C3
         OPEN  ((R10))
         LA    R15,8
         TM    48(R10),X'10'
         BZ    EOMOD
C2       SR    R15,R15
         B     D1
C3       LA    R15,4
         B     EOMOD
         SPACE 5
***********************************************************************
*                                                                     *
*        EINGABEKARTE LESEN                                           *
*              IN DIESEM ABSCHNITT WIRD EINE KARTE GELESEN.           *
*              DAS FOLGEFELD (LSP73-80) WIRD GESICHERT, DIE           *
*              REGISTER WERDEN KORRIGIERT.                            *
*                                                                     *
***********************************************************************
         SPACE 3
D1       GET   (R10)
         BCTR  R3,0
         LR    R4,R1
         LA    R5,71(R1)
         LR    R6,R1
         MVC   V3,72(R1)
         SPACE 5
***********************************************************************
*                                                                     *
*        DATENBEGRENZUNG                                              *
*              HIER WIRD DATENANFANG UND -ENDE ERMITTELT.             *
*              DER ANFANG IST BEI DER ERSTKARTE IMMER LSP1,           *
*                             BEI JEDER FOLGEKARTE DIE ERSTE LSP      *
*                             UNGLEICH BLANK (JEDOCH NICHT LSP1/2).   *
*              DAS ENDE IST DIE LETZTE LSP UNGLEICH BLANK VOR LSP72.  *
*              RUECKKEHRCODE = 16 WENN FEHLER IN KARTE                *
*                                                                     *
***********************************************************************
         SPACE 3
E1       LA    R15,16
         CLI   V1,0
         BE    E2
         LA    R6,2(R6)
E2       LR    R1,R5
         TRT   0(71,R6),KTAB1
         CR    R1,R5
         BNL   EOMOD
         LR    R6,R1
         LR    R7,R5
E3       BCTR  R7,0
         CLI   0(R7),C' '
         BE    E3
         SR    R15,R15
         SPACE 5
***********************************************************************
*                                                                     *
*        DATENUEBERTRAGUNG                                            *
*              AUS DATENANFANG UND -ENDE WIRD HIER DIE LAENGE         *
*              ERRECHNET UND DIE DATEN WERDEN UEBERTRAGEN.            *
*              RUECKKEHRCODE = 16 WENN FEHLER IN KARTE                *
*                                                                     *
***********************************************************************
         SPACE 3
F1       LA    R15,16
         LR    R9,R7
         SR    R9,R6
         BZ    EOMOD
         EX    R9,KMVC
         LA    R8,1(R9,R8)
         SR    R15,R15
         SPACE 5
***********************************************************************
*                                                                     *
*        FOLGEPRUEFUNG                                                *
*              EINE FOLGEKARTE WIRD ERWARTET WENN DAS LETZTE ZEICHEN  *
*              EIN KOMMA IST ODER LSP 72 UNGLEICH BLANK IST.          *
*              RUECKKEHRCODE = 12 WENN KEINE FOLGEKARTE MEHR ERLAUBT. *
*                                                                     *
***********************************************************************
         SPACE 3
G1       SR    R15,R15
         CLI   0(R7),C','
         BE    G2
         CLI   0(R5),C' '
         BE    EOMOD
G2       MVI   V1,1
         LTR   R3,R3
         BNZ   D1
         LA    R15,12
         SPACE 5
***********************************************************************
*                                                                     *
*        PROGRAMMENDE
*                                                                     *
***********************************************************************
         SPACE 3
EOMOD    ST    R15,RETCODE
         L     R13,SAVEAREA+4
         LA    R10,V3
         ST    R10,24(R13)
         RETURN (14,12),RC=(15)
         SPACE 5
***********************************************************************
*                                                                     *
*        ENDE DER KARTENEINGABE                                       *
*                                                                     *
***********************************************************************
         SPACE 3
EOHYPIN  MVI   V2,1
         CLOSE HYPIN
         FREEPOOL HYPIN
         LA    R15,4
         CLI   V1,0
         BE    EOMOD
         LA    R15,12
         B     EOMOD
         SPACE 5
***********************************************************************
*                                                                     *
*        ALLGEMEINE DEFINITIONEN                                      *
*                                                                     *
***********************************************************************
         SPACE 3
RETCODE  DC    F'0'          FUER DUMP
V1       DC    X'0'      0=ERSTKARTE, 1=FOLGEKARTE
V2       DC    X'0'                   1=EODAD IST ERFOLGT
V3       DC    CL8' '    FOLGEFELD
V99      DC    CL71' '
         DC    4CL69' '
KTAB1    DC    64X'FF'   TRT-TABELLE FUER UNGLEICH BLANK
         DC    X'0'
         DC    191X'FF'
KMVC     MVC   0(1,R8),0(R6)
SAVEAREA DC    18F'0'
         SPACE 5
***********************************************************************
*                                                                     *
*        SYNAD-ROUTINE                                                *
*                                                                     *
***********************************************************************
         SPACE 3
SYNQSAM  SYNADAF ACSMETH=QSAM
         LA    R1,4(R1)
         WTO   MF=(E,(1))
         SYNADRLS
         B     EOMOD
         SPACE 5
***********************************************************************
*                                                                     *
*        MACRO-DEFINITIONEN                                           *
*                                                                     *
***********************************************************************
         SPACE 3
         MCEQU
         SPACE 5
***********************************************************************
*                                                                     *
*        DCB-DEFINITION                                               *
*                                                                     *
***********************************************************************
         SPACE 3
HYPIN    DCB   DSORG=PS,DDNAME=HYPIN,LRECL=80,MACRF=GL,OPTCD=C,        X
               SYNAD=SYNQSAM,                                          X
               BUFNO=2,EODAD=EOHYPIN
         END   MODKARTE
