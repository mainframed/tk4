         MACRO
&NAME    TRX   &REG,&TABNAME=
         GBLB  &TABXOK
         GBLC  &TRXTABN
         LCLC  &TN
&NAME    CNOP  0,4
         AIF   (NOT &TABXOK).NOTAB
         ST    14,&TRXTABN
         ST    15,&TRXTABN+4
         ST    1,&TRXTABN+8
         AGO   .TESTR1
.NOTAB   ST    14,*+76
         ST    15,*+76
         ST    1,*+76
.TESTR1  AIF   ('&REG(1)'(1,1) EQ '(').LR1
         LA    14,&REG(1)
         AGO   .TESTR2
.LR1     LR    14,&REG(1)
         NOPR  0
.TESTR2  AIF   ('&REG(2)'(1,1) EQ '(').LR2
         LA    15,&REG(2)
         AGO   .TESTR3
.LR2     LR    15,&REG(2)
         NOPR  0
.TESTR3  AIF   ('&REG(3)'(1,1) EQ '(').LR3
         LA    1,&REG(3)
         AGO   .TESTTAB
.LR3     LR    1,&REG(3)
         NOPR  0
.TESTTAB AIF   (NOT &TABXOK).NOTAB1
         TR    0(2,15),&TRXTABN        TRANSLATE TWO BYTES
         AGO   .PACK
.NOTAB1  TR    0(2,15),*+52            TRANSLATE TWO BYTES
.PACK    PACK  0(2,15),0(2,15) PACK THEM
         LH    0,0(15)
         SRL   0,4                     SHIFT SIGN-BITS OUT
         STC   0,0(14)                 STORE RESULT-BYTE
         LA    14,1(0,14)              BUMP UP REGISTER
         LA    15,2(0,15)              BUMP UP REGISTER
         BCT   1,*-32                  CONVERT AGAIN IF REG. NZERO
         AIF   (NOT &TABXOK).NOTAB2
         L     14,&TRXTABN
         L     15,&TRXTABN+4
         L     1,&TRXTABN+8
         MEXIT
.NOTAB2  L     14,*+16
         L     15,*+16
         L     1,*+16
         BC    15,*+260                BRANCH TO EXECUTABLE INSTR.
&TN      SETC  '&TABNAME'
         AIF   ('&TN' NE '').NAME
&TN      SETC  'TRX&SYSNDX'
.NAME    ANOP
&TN      DS    256X
         ORG   *-256+X'40'             TABLE
         DC    X'F0'                   FOR
         ORG   *+X'80'                 TRANSLATE
         DC    X'FAFBFCFDFEFF'         INSTRUCTION
         ORG   *+X'29'
         DC    C'0123456789'
         ORG
&TABXOK  SETB  1
&TRXTABN SETC  '&TN'
         MEND
