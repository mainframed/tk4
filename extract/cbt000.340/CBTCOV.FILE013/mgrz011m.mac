         MACRO
&NAME    ZZFCB2 &ID=,&DIND=N,&L=,&LNMOD=6,&POS=,&SH=
.*       W.SCHWARZ, 26/09/75
.*                  06/09/77
.*       GENOSSENSCHAFTS-RECHENZENTRALE NORDDEUTSCHLAND GMBH.
.*       SANDKRUGERSTRASSE
.*       2900 OLDENBURG
.*       TEL. 0441/400200
.*       W.SCHWARZ, 26/09/75
.*                  06/09/77
         COPY  ZZGBL
         LCLA  &A1,&A2,&A3,&A4,&A5
         LCLA  &CHAN,&LINE,&DISPL
         LCLA  &LENGTH,&LM,&L2
         LCLC  &CHAN2
         AIF   ('&LNMOD' EQ '6').A1
&LM      SETA  1
         AGO   .A2
.A1      ANOP
&LM      SETA  0
.A2      ANOP
         AIF   ('&ID' NE '').A3
         MNOTE 12,'NO ID FOUND, GENERATION TERMINATED'
         AGO   .MACEND
.A3      ANOP
         AIF   ('&L' EQ '').LERR
         AIF   ('&POS' EQ '').POSERR
         TITLE '                               FORMS CONTROL BUFFER IMA*
               GE : &ID'
FCB2&ID  CSECT
         AIF   ('&DIND' EQ 'N').NODEFLT
         DC    X'80'                   THIS IS A DEFAULT IMAGE.
         AGO   .LDEF
.NODEFLT ANOP
         DC    X'00'                   NOT TO BE USED AS A DEFAULT.
.LDEF    ANOP
         AIF   ('&SH' EQ '').NOSHIFT
&I       SETC  '&SH'(1,1)
         AIF   ('&SH'(1,1) NE 'R' AND '&SH'(1,1) NE 'L').INDERR1
&P2      SETC  '&SH'(2,K'&SH-1)
&A6      SETA  &P2
         AIF   (&A6 LT 1 OR &A6 GT 31).INDERR2
&B(1)    SETB  ('&SH'(1,1) EQ 'R'),('&SH'(1,1) EQ 'L')
&A7      SETA  128*&B(1)+64*&B(2)+&A6
&L2      SETA  &L+1
         DC    AL1(&L2)                FCB-IMAGE LENGTH
         DC    AL1(&A7)                OFFSET PRINT LINE FEATURE USED
         AGO   .INIT1A
.NOSHIFT ANOP
         DC    AL1(&L)                 FCB-IMAGE LENGTH
.INIT1A  ANOP
&A1      SETA  N'&POS
&A2      SETA  &A2+1
&A3      SETA  &A2+1
&A4      SETA  &A4+1
         AGO   .INIT1B
.INIT2A  ANOP
&A2      SETA  &A2+2
&A3      SETA  &A2+1
.INIT1B ANOP
         AIF   (&A2 GT &A1).EXIT
&CHAN    SETA  &POS(&A2)
&LINE    SETA  &POS(&A3)
         AIF   (&CHAN LT 1 OR &CHAN GT 12).CHANERR
&DISPL   SETA  &LINE-&A4
&CHAN2   SETC  '&CHAN'
         AIF   (&CHAN LT 10).B16
         AIF   (&CHAN EQ 11).B17
         AIF   (&CHAN EQ 12).B18
&CHAN2   SETC  'A'
.B16     ANOP
         AIF   (&LINE GT &A4).B19
         DC    X'&LM&CHAN2'            START LINE, CHANNEL SPECIFIED
&A5      SETA  255
         AGO   .INIT2A
.B17     ANOP
&CHAN2   SETC  'B'
         AGO   .B16
.B18     ANOP
&CHAN2   SETC  'C'
         AGO   .B16
.B19     ANOP
         AIF   (&A5 EQ 255).B20
         AIF   (&A4 GT 1).B20
         DC    X'&LM.0'                START LINE,NO CHANNEL SPECIFIED
.B20     ANOP
         AIF   (&DISPL EQ 1).B23
&A5      SETA  &DISPL-1
         DC    XL&A5'0'
.B23     ANOP
         AIF   (&LINE EQ &L).B24G
         DC    X'0&CHAN2'              CHANNEL SPECIFIED
         AGO   .B28
.B24G    ANOP
         DC    X'1&CHAN2'              LAST LINE, CHANNEL SPECIFIED
         AGO   .B29
.B28     ANOP
&A4      SETA  &LINE
         AGO   .INIT2A
.EXIT    ANOP
&LENGTH  SETA  &L
&DISPL   SETA  &LENGTH-&A4
&A5      SETA  &DISPL-1
         AIF   (&A5 EQ 0).B28A
         DC    XL&A5'0'
.B28A    ANOP
         DC    X'10'                   LAST LINE,NO CHANNEL SPECIFIED
.B29     ANOP
         SPACE
         MNOTE *,'FCB - LENGTH IS &L LINES'
         MNOTE *,'LINEMODE IS &LNMOD LPI'
         MNOTE *,'GENERATION OF FCB-IMAGE FCB2&ID SUCCESSFUL'
         AGO   .MACENDN
.LERR    ANOP
         MNOTE 12,'INVALID LENGTH DEF., ASS''BLY TERMINATED'
         AGO   .MACEND
.POSERR  ANOP
         MNOTE 12,'INVALID POSITION-PARAMETER, ASS''BLY TERMINATED'
         AGO   .MACEND
.INDERR1 ANOP
         MNOTE 12,'INVALID SHIFT VALUE &I -- MUST BE R OR L'
         AGO   .MACEND
.INDERR2 ANOP
         MNOTE 12,'INVALID SHIFT VALUE &A6 -- MUST BE 1 TO 31'
         AGO   .MACEND
.CHANERR ANOP
         MNOTE 12,'INVALID CHANNEL SPECIFICATION, -- POS=(&CHAN,&LINE)'
.MACEND  ANOP
         MNOTE 12,'GENERATION OF FCB-IMAGE FCB2&ID FAILED'
.MACENDN ANOP
         END   FCB2&ID
.MACENDL MEND
