         MACRO
&NAME    MUPEIN &REG,&I,&ID=
         GBLB  &MTRB000            MUPTRACE ACTIVE
         GBLB  &MTRB001(2)         MUPTRACE MUPEIN
         GBLB  &MTRB005            MUPTRACE ID=NO
         GBLB  &MBSB003            MBASIS TYPE=R
         GBLA  &MBSA002            MBASIS MAXMUP
         GBLA  &MUEA001            MUPEIN COUNT
         GBLC  &MUEC001(500)       MUPEIN &NAME
         LCLA  &W,&V,&D,&G,&M
         LCLB  &B,&P
         LCLC  &AL1
         AIF   ('&NAME' NE '').A3
         MNOTE 12,'*** NAME DES EINGANGSPUNKTES FEHLT. GENERIERUNG ABGE*
               BROCHEN ***'
         MEXIT
.A3      ANOP
&M       SETA  &MBSA002
&MUEA001 SETA  &MUEA001+1
&G       SETA  &MUEA001
         AIF   (&G LE 500).A4
         MNOTE 12,'*** MEHR ALS 500 MUPEIN. GENERIERUNG ABGEBROCHEN ****
               '
         MEXIT
.A4      ANOP
&MUEC001(&G) SETC '&NAME'
         AIF   (NOT &MBSB003).RE1
         AIF   (&G LE &M).RE1
         MNOTE 12,'*** MEHR ALS &M MUPEIN. GENERIERUNG ABGEBROCHEN ***'
&MUEA001 SETA  &M
         MEXIT
.RE1     ANOP
         AIF   ('&REG' EQ '').A1
&AL1     SETC  '&REG'
         AGO   .A2
.A1      ANOP
&AL1     SETC  'R14'
.A2      ANOP
         AIF   (&MTRB005).I30
         AIF   (&MBSB003).RE2
         DC    0F'0'
.RE2     ANOP
         AIF   (T'&ID EQ 'O').I0
&B       SETB  ('&ID'(1,1) EQ '''')
&W       SETA  K'&ID
         AGO   .I1
.I0      AIF   ('&I' EQ '').I20
&P       SETB  1
&B       SETB  ('&I'(1,1) EQ '''')
&W       SETA  K'&I
.I1      AIF   (NOT &B).I2
&W       SETA  &W-2
.I2      ANOP
         AIF   (&W LE 116).I3
         MNOTE 4,'*** IDENTIFIER HAT MEHR ALS 116 ZEICHEN. IDENTIFIER I*
               GNORIERT ***'
         AGO   .I20
.I3      ANOP
&V       SETA  ((&W+1+3)/4)*4
&W       SETA  &V-1
         AIF   (&P).I5
         AIF   (&B).I4
         DC    CL&W'&ID'           IDENTIFIER
         AGO   .I7
.I4      ANOP
         DC    CL&W&ID             IDENTIFIER
         AGO   .I7
.I5      AIF   (&B).I6
         DC    CL&W'&I'            IDENTIFIER
         AGO   .I7
.I6      ANOP
         DC    CL&W&I              IDENTIFIER
.I7      ANOP
         DC    AL1(&W)             LENGTH OF IDENTIFIER
         AGO   .I30
.I20     ANOP
&W       SETA  K'&SYSECT+K'&NAME+1
&V       SETA  ((&W+1+3)/4)*4
&W       SETA  &V-1
         DC    CL&W'&SYSECT..&NAME' IDENTIFIER
         DC    AL1(&W)             LENGTH OF IDENTIFIER
.I30     ANOP
         AIF   (&MBSB003).RE3
&W       SETA  5
         DC    F'0'                SAVEAREA RETURN-ADDRESS
         SPACE 1
&NAME    ST    &AL1,&NAME-4        SAVE RETURN-ADDRESS
         AGO   .RE4
.RE3     ANOP
&W       SETA  0
         AIF   (NOT &MTRB000).TR0
&W       SETA  24
.TR0     ANOP
&D       SETA  72+&W+4*(&G-1)
&W       SETA  1
         SPACE 1
&NAME    ST    &AL1,&D.(0,R13)     SAVE RETURN-ADDRESS
.RE4     ANOP
         AIF   (NOT &MTRB001(1)).TR1
         STM   R14,R0,84(R13)      SAVE USER REGISTER
         L     R15,72(0,R13)       LOAD TRACE-ROUTINE ADDRESS
         LA    R0,&NAME-&W         LOAD ID-ADDRESS
         ICM   R0,8,76(R13)        SET MACRO-TYPE
         BALR  R14,R15             BRANCH TO TRACE-ROUTINE
         LM    R14,R0,84(R13)      RESTORE USER REGISTER
.TR1     MEXIT
         MEND
