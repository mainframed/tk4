         TITLE 'FCB2GEN - RECREATE FCB2-IMAGE PARAMETERS'
***      GENOSSENSCHAFTS-RECHENZENTRALE NORDDEUTSCHLAND GMBH.
***      SANDKRUGERSTRASSE
***      2900 OLDENBURG
***      TEL. 0441/400200(W.SCHWARZ)
FCB2GEN  CSECT
         ZZ000 RENT=JA
         LM    R09,R11,0(R01)
*        R9    A(FCB2IMAG)
*        R10   A(WORKAREA) 20*80 BYTES
*        R11   A(FCB2NAME)
         L     R00,=A(WORKEND-WORKSET)
         GETMAIN R,LV=(0)
         LR    R07,R01
         USING WORKSET,R07
         ST    R00,GMLENGTH
         MVC   CONAREA,ZZFCB2
         XC    DWORD,DWORD
         LA    R08,432       72*6
         ST    R08,POSTABL
         MVC   FCON(4),=X'FFFFFFFF'
         MVI   DEFAULTC,C'N'
         MVI   SHIFTC1,C' '
         MVI   LNMODC,C'6'
         SR    R12,R12
         CLI   0(R09),X'80'            DEFAULT IMAGE
         BNE   NDEFLT
         MVI   DEFAULTC,C'D'
NDEFLT   SR    R08,R08
         ICM   R08,1,1(R09)            IMAGE LENGTM
         CVD   R08,DWORD
         OI    DWORD+7,X'0F'
         UNPK  LENGTHC(3),DWORD+6(2)
         TM    2(R09),X'80'
         BO    OFFSETR
         TM    2(R09),X'40'
         BO    OFFSETL
LNMODCH  L     R00,POSTABL
         GETMAIN R,LV=(0)
         ST    R01,POSTABLE
         LR    R03,R01
         SR    R05,R05                    LINE    COUNT
         TM    2(R09),X'10'
         BNO   LNMOD6
         MVI   LNMODC,C'8'
LNMOD6   LA    R05,1(R05)              INCR LINE-COUNT
         SR    R08,R08
         ICM   R08,1,2(R09)
         SLL   R08,28
         SRL   R08,28
         CLM   R08,1,=X'00'            1ST LINE,NO CHAN SPECIFIED
         BE    NEXTLINE
         LR    R04,R08                  GET CHANNEL NO.
         BAL   R14,EDITPOS
         LA    R03,6(R03)
NEXTLINE LA    R09,2(R09)
LINELOOP LA    R09,1(R09)
         LA    R05,1(R05)              INCR LINE-COUNT
         SR    R08,R08
         ICM   R08,1,0(R09)
         SLL   R08,28
         SRL   R08,28
         CLM   R08,1,=X'00'
         BE    LASTLINE
         LR    R04,R08                  GET CHANNEL NO.
         BAL   R14,EDITPOS
         L     R04,POSTABLE
         L     R08,POSTABL
         AR    R04,R08
         CR    R03,R04
         BE    NOPOS
         LA    R03,6(R03)
LASTLINE TM    0(R09),X'10'              LAST LINE INDICATED
         BNO   LINELOOP                  NO
         EJECT
FCBEND   MVC   0(4,R03),FCON
         LA    R12,1(R12)
         BAL   R14,CLEAR
         MVC   NAMEC(4),4(R11)
         MVC   0(23,R10),CON1
         MVI   23(R10),C','
         MVI   71(R10),C'*'
         LA    R12,1(R12)
         LA    R10,80(R10)
         BAL   R14,CLEAR
         MVC   0(20,R10),CON2
         MVI   20(R10),C','
         MVI   71(R10),C'*'
         LA    R12,1(R12)
         LA    R10,80(R10)
         BAL   R14,CLEAR
         MVC   0(21,R10),CON3
         MVI   21(R10),C','
         MVI   71(R10),C'*'
         LA    R12,1(R12)
         LA    R10,80(R10)
         BAL   R14,CLEAR
         MVC   0(22,R10),CON4
         MVI   22(R10),C','
         MVI   71(R10),C'*'
         LA    R12,1(R12)
         LA    R10,80(R10)
         MVC   0(4,R10),=XL4'0'
         CLI   SHIFTC1,C' '
         BE    NOSH
         BAL   R14,CLEAR
         MVC   0(22,R10),CON5
         MVI   22(R10),C','
         MVI   71(R10),C'*'
NOSH     LA    R12,1(R12)
         LA    R10,80(R10)
         BAL   R14,CLEAR
         MVC   0(20,R10),CON6
         MVI   71(R10),C'*'
         LA    R06,20(R10)
         L     R03,POSTABLE
POSLOOP  CLC   0(4,R03),FCON
         BE    ENDPOS                  END OF TABLE
         MVC   0(3,R06),0(R03)
         LA    R06,2(R06)
         CLI   1(R06),C'*'
         BE    NEWLINE
         MVI   1(R06),C','
         LA    R06,2(R06)
NEXTR3   LA    R03,3(R03)
         B     POSLOOP
         EJECT
NEWLINE  LA    R12,1(R12)
         C     R12,=F'20'
         BH    NOWORK
         LA    R10,80(R10)
         BAL   R14,CLEAR
         MVI   15(R10),C','
         MVI   71(R10),C'*'
         LA    R06,16(R10)
         B     NEXTR3
ENDPOS   BCTR  R06,0
         MVI   0(R06),C')'
         MVI   71(R10),C' '
         LA    R08,20
         SR    R08,R12
         LTR   R08,R08
         BZ    PEXIT
DLOOP    LA    R10,80(R10)
         MVC   0(4,R10),=XL4'0'
         BCT   R08,DLOOP
         EJECT
PEXIT    BAL   R14,FREEMAIN
         ZZFFF RENT=JA
         SPACE 3
NOPOS    BAL   R14,FREEMAIN
         ZZFFF RC=4,RENT=JA       POS-TABLE SHORTAGE
         SPACE 3
NOWORK   BAL   R14,FREEMAIN
         ZZFFF RC=8,RENT=JA       WORKAREA SHORTAGE
         SPACE 3
         DC    F'0'
FREEMAIN ST    R14,*-4
         L     R00,POSTABL
         L     R01,POSTABLE
         FREEMAIN R,LV=(0),A=(1)
         L     R00,GMLENGTH
         LR    R01,R07
         FREEMAIN R,LV=(0),A=(1)
         L     R14,FREEMAIN-4
         BR    R14
         EJECT
OFFSETR  MVI   SHIFTC1,C'R'
         B     GOFFSET
OFFSETL  MVI   SHIFTC1,C'L'
GOFFSET  LA    R4,0
         LA    R6,1
         TM    2(R09),X'01'
         BNO   TM02
         AR    R4,R6
TM02     LA    R6,2
         TM    2(R09),X'02'
         BNO   TM04
         AR    R4,R6
TM04     LA    R6,4
         TM    2(R09),X'04'
         BNO   TM08
         AR    R04,R06
TM08     LA    R06,8
         TM    2(R09),X'08'
         BNO   TM16
         AR    R04,R06
TM16     LA    R06,16
         TM    2(R09),X'10'
         BNO   EDITSH
         AR    R04,R06
EDITSH   CVD   R04,DWORD
         OI    DWORD+7,X'0F'
         UNPK  SHIFTC2(3),DWORD+6(2)
         B     LNMODCH
         TITLE 'FCB2GEN - SUBROUTINES'
CLEAR    LR    R15,R14
         MVI   0(R10),C' '
         MVC   1(79,R10),0(R10)
         LR    R14,R15
         BR    R14
         SPACE 3
EDITPOS  LR    R15,R14
         CVD   R04,DWORD
         OI    DWORD+7,X'0F'
         UNPK  0(3,R03),DWORD+6(2)   CURRENT CHANNEL NO.
         CVD   R05,DWORD
         OI    DWORD+7,X'0F'
         UNPK  3(3,R03),DWORD+6(2)   CURRENT LINE  NO.
         LR    R14,R15
         BR    R14
         TITLE 'FCB2GEN - CONSTANTS + AREAS'
ZZFCB2   DS    0CL128
         DC    CL9' '
         DC    C'ZZFCB2 ID='
         DC    CL4' '
         DC    CL15' '
         DC    C'L='
         DC    CL3' '
         DC    CL15' '
         DC    C'DIND='
         DC    C'N'
         DC    CL15' '
         DC    C'LNMOD='
         DC    C'6'
         DC    CL15' '
         DC    C'SH='
         DC    C' '
         DC    CL3' '
         DC    CL15' '
         DC    C'POS=('
         TITLE 'FCB2GEN - DSECT'
WORKSET  DSECT
DWORD    DS    D
GMLENGTH DS    F
POSTABLE DS    F
POSTABL  DS    F
FCON     DS    F
CONAREA  DS    0CL128
CON1     DS    CL19
NAMEC    DS    CL4
CON2     DS    CL17
LENGTHC  DS    CL3
CON3     DS    CL20
DEFAULTC DS    C
CON4     DS    CL21
LNMODC   DS    C
CON5     DS    CL18
SHIFTC1  DS    C
SHIFTC2  DS    CL3
CON6     DS    CL20
WORKEND  EQU   *
         END   FCB2GEN
