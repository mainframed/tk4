DATUM    CSECT
         REG
*        BEIM AUFRUF VON  'D A T U M' MUSS REGISTER 1 AUF EIN
*        8 BYTE GROSSES FELD ZEIGEN. DABEI HAT DER FELDINHALD FOLGENDE
*        BEDEUTUNG:
*           FELDINHALT                  FUNKTION
*           ----------                  --------
*        1) TTMMJJ (IN CHAR)  ----------> UMWANDELN IN YYDDD (GEPACKED)
*        2) 00000000 (HEX)    ----------> MASCH-DATUM IN TT.MM.JJ (CHA)
*        3) YYDDD (PACKED)    ----------> UMWAND. IN TT.MM.JJ (CHAR)
*        DIE PARAMETER UND DIE ERGEBNISSE STEHEN LINKSBUENDIG IM FELD
*                                                -----------
*        DAS RESULTAT WIRD IN DEN UEBERGEBENEN BEREICH GESTELLT !!
*
*        AUFRUF AUS ASSEMBLER:
*        FELD  DS  CL8
*        CALL   DATUM,(FELD)
         BASER 1
START    L     9,0(1)              ADRESSE DES PARMFELDES NACH REG9
         TM    0(R9),X'F0'              DATUM IN DER FORM TTMMJJ ???
         BO    KALENDER                 JA!!!!!!
         BZ    MASCHDTE                 TAGES.-DATUM GEWUENSCHT?
*                                       NEIN!!!!!!
         MVC   DWORT(4),0(R9)           YYDDD  -----> TTMMJJ UMWANDELN
         L     R1,DWORT
         B     STOREDW                  BRANCH AROUND MASCH.-DATUM
MASCHDTE EQU   *
         TIME
STOREDW  EQU   *
         ST    1,DWORT+4           DATUM NACH DWORT+4 RETTEN
         ZAP   DWORT,DWORT+6(2)    ANZAHL TAGE GEPACKT NACH DWORT
         CVB   10,DWORT            TAGE BINAER INS REG 10
         SRL   1,12                ANZAHL TAGE AUS DEM REG SCHIFTEN
         ST    1,DWORT+4           JAHR NACH DWORT BRINGEN
         OI    DWORT+7,X'0F'       VORZEICHEN REINSCHIESSEN
         UNPK  JAHR,DWORT+6(2)     JAHR ENTPACKEN
         CVB   1,DWORT               KONV. JAHR NACH BINAER
         SLL   1,30                EINMAL HIN ...........
         SRL   1,30                EINMAL HER ...............
         LTR   1,1                 RINGSHERUM DAS IST NICHT SCHWER!
         BZ    SCHALTJ             WENN NULL, DANN SCHALTJAHR
         LA    1,28                FEBRUAR NORMAL 28 TAGE
         STH   1,TAB+2             HINEIN, ONKEL OTTO!!!
NORMAL   LA    2,TAB               ADRESSE DER MONATSTABELLE LADEN
         LA    3,1                 REG 3 = 1     ZAEHLT DIE MONATE!
LOOP     LH    4,0(2)              INHALT DER TABELLENPLAETZE LADEN
         CR    10,4                TAGE  GEGEN TAGE DES JEW MON VERGL.
         BNH   GEF                 BEI < OD. =  MONAT GEFUNDEN
         SR    10,4                MONATSTAGE VON DEN JAHRESTAGEN SUBTR
         LA    2,2(2)              REG 2 UM EINEN TABELLENPLATZ WEITER
         LA    3,1(3)              MONATSZAEHLER ERHOEHEN
         B     LOOP                SCHLEIFE
GEF      CVD   3,DWORT             GEF MONAT NACH DEC CONVERTIEREN
         UNPK  MONAT,DWORT+6(2)    MONAT ENTPACKEN
         OI    MONAT+1,X'F0'       VORZEICHEN REINSCHIESSEN
         CVD   10,DWORT            TAGE PRO GEF MON NACH DEC CONVERT.
         UNPK  TAG,DWORT+6(2)      TAGE ENTPACKEN
         OI    TAG+1,X'F0'         VORZEICHEN REINKNALLEN
         MVC   0(8,9),ZACK         DEN GANZEN KRAM INS PARMFELD ROTZEN
         B     END99               ENDE DER VORSTELLUNG
SCHALTJ  LA    6,29                29 TAGE FUER SCHALT-FEBRUAR IN REG 6
         STH   6,TAB+2             29 TAGE IN DIE TABELLE PFEIFEN
         B     NORMAL              WEITER GEHT'S MIT NORMALER VERARBEI.
KALENDER EQU   *
***********************************************************************
* TTMMJJ (CHAR) --------------> YYDDD (PACKED) UMWANDELN
***********************************************************************
         LR    R1,R9                    R1 AUF TTMMJJ
         LA    R6,0                     R6=0
         PACK  DWORT,2(2,R1)            MONAT PACKEN
         CVB   R3,DWORT                 MONAT BINAER
         BCTR  R3,0                     IMMER 1 WENIGER ALS MAN DENKT!
         LTR   R3,R3                    NULL ????
         BZ    TAGE                     JA.
         LA    R4,TAB                   R4=ANFANG MONATSTABELLE
ADD      EQU   *
         AH    R6,0(R4)                 TABELLEN ENTRIES SUMMIEREN
         LA    R4,2(R4)
         BCT   R3,ADD                   LOOP
TAGE     EQU   *
         PACK  DWORT,0(2,R1)            TAGE PACKEN
         CVB   R2,DWORT                 TAGE NACH BINAER
         AR    R6,R2                    ADD ZU GESAMT TAGEN
         PACK  DWORT,4(2,R1)            JAHR PACKEN
         CVB   R2,DWORT                 JAHR NACH BINAER
         SLL   R2,30
         LTR   R2,R2                    REST=0 ?
         BNZ   NOSCHALT                 NICHT NULL ---> KEIN SCHALTJAHR
         CLC   2(2,R1),=C'03'           MAERZ UND SPAETER ???
         BL    NOSCHALT
         LA    R6,1(R6)                 SCHALTJAHR: EINS MEHR
NOSCHALT EQU   *
         CVD   R6,DWORT                 MASCHDAT NACH DEZIMAL
         PACK  DWORT(4),4(2,R1)
         MVO   DWORT(2),DWORT+2(2)
         MVC   DWORT+5(1),DWORT
         MVC   0(4,R1),DWORT+4
         B     END99
         LTORG
***** DEFINITIONEN *****
*
TAB      DC    H'31'               TABELLE FUER ANZAHL TAGE PRO MONAT
         DC    H'28'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
*
ZACK     DS    0CL8                AUFBEREITUNGSBEREICH FUER DAS DATUM
TAG      DS    CL2
         DC    C'.'
MONAT    DS    CL2
         DC    C'.'
JAHR     DS    CL2
*
DWORT    DC    D'0'                ARBEITSDOPPELWORT
         END
