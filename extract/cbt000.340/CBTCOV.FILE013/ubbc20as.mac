EDVBWRT  CSECT
*********    BBC PROGRAMM   I 1 5 7   *********************************
*
***********************************************************************
*                                                                     *
*        BEWERTUNGSPROGRAMM FUER EDV - LEISTUNGEN                     *
*                                                                     *
*        BEI AUFRUF MUSS DEM PROGRAMM IN REGISTER 1 DIE ADRESSE       *
*        EINER ADR-LISTE UEBERGEBEN WERDEN                            *
*                                                                     *
*              1. ADR = WORT      WERT BINAER  0 = STEP BEWERTUNG     *
*                                              4 = JOB BEWERTUNG      *
*                                              8 = SYSOUT BEWERTUNG   *
*                                                                     *
*              2. ADR = WERTBEREICHE / ARBEITSBEREICH                 *
*                                                                     *
*        BESCHREIBUNG WERTBEREICHE SH. DSECT WERT1                    *
*                                            WERT2                    *
*                                            WERT3                    *
*        BESCHREIBUNG ARBEITSBEREICH SH. DSECT ARBEIT                 *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        VERWENDUNG DER REGISTER                                      *
*                                                                     *
R0       EQU   0                                                      *
R1       EQU   1   ADR.-LISTE, KENNWERT BEWERTUNG                     *
R2       EQU   2   ADR. ARBEITS-/WERTBEREICH, DSECT WERTBER. WERT1    *
R3       EQU   3   DSECT WERTBER. WERT2                               *
R4       EQU   4   DSECT ARBEITSBEREICH                               *
R5       EQU   5   ARBEITS REGISTER                                   *
R6       EQU   6   DSECT WERTBER. WERT3                               *
R7       EQU   7   ARBEITS REGISTER                                   *
R8       EQU   8                                                      *
R9       EQU   9                                                      *
R10      EQU   10                                                     *
R11      EQU   11                                                     *
R12      EQU   12  BASIS REGISTER                                     *
R13      EQU   13                                                     *
R14      EQU   14                                                     *
R15      EQU   15                                                     *
*                                                                     *
***********************************************************************
         EJECT
*
******** PROGRAMM - ANFANG
*
         B     12(R15)
         DC    CL8'EDVBWRT '
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING EDVBWRT,R12             BASIS REGISTER
         LR    R2,R1                   ADR.-POINTER IN R2
         GETMAIN R,LV=72
         ST    R13,4(R1)               VERKETTEN
         ST    R1,8(R13)                SAVEAREAS
         LR    R13,R1
         LM    R1,R2,0(R2)             LADEN ADR.-LISTE
         USING WERT1,R2                DSECT WERT1
         LR    R3,R2
         USING WERT2,R3                DSECT WERT2
         LR    R6,R2
         USING WERT3,R6                DSECT WERT3
         LA    R4,111(R2)
         USING ARBEIT,R4               DSECT ARBEIT
         L     R1,0(R1)                KENNWERT BEWERTUNG
         B     SPRUNG(R1)
SPRUNG   B     STEP                    STEP BEWERTUNG
         B     JOB                     JOB BEWERTUNG
         B     SYSOUT
         EJECT
*
******** BATCH/TSO STEP BEWERTUNG
*
STEP     ZAP   LAUS,=X'0F'             LOESCHEN
         MVC   CPUDM(64),LAUS           ERGEBNISFELDER
         CLI   KEN1,C'0'               STEP NOT
         BE    ENDE                     EXECUTED
******** RECHNEN LAUFZEIT SEKUNDEN
         ZAP   RF+4(8),EAPL            E/A PLATTE
         AP    RF+4(8),EABD             + E/A BAND
         AP    RF+4(8),EAUR             + E/A UR
         MP    RF+4(8),ZUGR             * SEK PRO E/A
         MVN   RF+9(1),RF+11
         AP    RF+6(4),CPUS             + CPU SEKUNDEN
         ZAP   LAUS,RF+6(4)            = LAUFZEIT SEKUNDEN
******** RECHNEN CPU DM
         ZAP   RF+4(8),CPUS            CPU SEKUNDEN
         MP    RF+4(8),CPU              * CPU PREIS
         MVN   RF+8(1),RF+11
         ZAP   CPUDM,RF+5(4)           = CPU DM
******** RECHNEN SYSTEM DM
         ZAP   RF+4(8),LAUS            LAUFZEIT SEKUNDEN
         MP    RF+4(8),SYST             * SYSTEM PREIS
         MVN   RF+8(1),RF+11
         ZAP   SYSDM,RF+5(4)           = SYSTEM DM
******** RECHNEN REGION DM
         ZAP   RF,REGG                 REGION GEFORDERT
         MP    RF+4(8),LAUS             * LAUFZEIT SEKUNDEN
         MP    RF,REGI                  * REGION PREIS
         MVN   RF+8(1),RF+11
         ZAP   REGDM,RF+5(4)           = REGION DM
******** RECHNEN LIZENZ DM
         AP    CPUS,=X'0F'             BEI CPU = 0
         BZ    LIZZ                     KEINE LIZENZ
         LA    R5,LIZTAB
LIZA     CLC   0(8,R5),=X'4040404040404040'
         BE    LIZZ                    KEIN LIZENZPROGRAMM
         CLC   0(8,R5),PROG
         BE    LIZB                    LIZENZPROGRAMM
         LA    R5,12(R5)
         B     LIZA
LIZB     ZAP   LIZDM,8(4,R5)           LIZENZ DM
LIZZ     EQU   *
******** RECHNEN STEPFIXKOSTEN
         ZAP   STXDM,FIX               STEPFIXKOSTEN DM
******** RECHNEN PLATTEN E/A DM
         ZAP   RF+4(8),EAPL            PLATTEN E/A
         MP    RF+4(8),ZPLA             * E/A PREIS
         MVN   RF+9(1),RF+11
         ZAP   PLEADM,RF+6(4)          = PLATTEN E/A DM
******** RECHNEN PLATTEN PRIVAT DM
         ZAP   RF,PLPR                 PLATTEN PRIVAT
         MP    RF+4(8),LAUS             * LAUFZEIT SEKUNDEN
         MP    RF,PLAT                  * PRIVAT PREIS
         MVN   RF+8(1),RF+11
         ZAP   PLPRDM,RF+5(4)          = PLATTEN PRIVAT DM
******** RECHNEN PLATTEN RUESTKOSTEN DM
         ZAP   RF+4(8),PLPR            PLATTEN GERUESTET
         MP    RF+4(8),RUPL             * RUEST PREIS
         ZAP   PLRUDM,RF+8(4)          = PLATTEN RUESTKOSTEN DM
******** RECHNEN BAND E/A DM
         ZAP   RF+4(8),EABD            BAND E/A
         MP    RF+4(8),ZBAN             * E/A PREIS
         MVN   RF+9(1),RF+11
         ZAP   BDEADM,RF+6(4)          = BAND E/A DM
******** RECHNEN BAND PRIVAT DM
         ZAP   RF,BDPI                 BAND PRIVAT
         MP    RF+4(8),LAUS             * LAUFZEIT SEKUNDEN
         MP    RF,BAND                  * PRIVAT PREIS
         MVN   RF+8(1),RF+11
         ZAP   BDPRDM,RF+5(4)          = BAND PRIVAT DM
******** RECHNEN BAND RUESTKOSTEN DM
         ZAP   RF+4(8),BDRU            BAND GERUESTET
         MP    RF+4(8),RUBD             * RUEST PREIS
         ZAP   BDRUDM,RF+8(4)          = BAND RUESTKOSTEN DM
******** RECHNEN UR PRIVAT DM
         ZAP   RF,URPR                 UR PRIVAT
         MP    RF+4(8),LAUS             * LAUFZEIT SEKUNDEN
         MP    RF,UNIT                  * PRIVAT PREIS
         MVN   RF+8(1),RF+11
         ZAP   URPRDM,RF+5(4)          = UR PRIVAT DM
******** RECHNEN UR RUESTKOSTEN DM
         ZAP   RF+4(8),URPR            UR GERUESTET
         MP    RF+4(8),RUUR             * RUEST PREIS
         ZAP   URRUDM,RF+8(4)          = UR RUESTKOSTEN DM
******** RECHNEN SYSIN+SYSOUT/TGET+TPUT DM
         ZAP   RF+4(8),SYSI            SYSIN+SYSOUT/TGET+TPUT
         CLI   KEN1,C'1'
         BH    SYSA                    RJE/TSO
         MP    RF+4(8),LOCA             * LOCAL PREIS
         B     SYSB
SYSA     MP    RF+4(8),REMO             * REMOTE PREIS
SYSB     MVN   RF+9(1),RF+11
         ZAP   SYSIDM,RF+6(4)          = SYSIN+SYSOUT/TGET+TPUT DM
******** RECHNEN STEPKOSTEN DM
         ZAP   STEPDM,CPUDM
         AP    STEPDM,SYSDM
         AP    STEPDM,REGDM
         AP    STEPDM,LIZDM
         AP    STEPDM,STXDM
         AP    STEPDM,PLEADM
         AP    STEPDM,PLPRDM
         AP    STEPDM,PLRUDM
         AP    STEPDM,BDEADM
         AP    STEPDM,BDPRDM
         AP    STEPDM,BDRUDM
         AP    STEPDM,URPRDM
         AP    STEPDM,URRUDM
         AP    STEPDM,SYSIDM
******** RECHNEN TSO ZUSCHLAG
         CLI   KEN1,C'3'
         BNE   ENDE                    NICHT TSO
         CP    REGG,TSOREG
         BNH   TSO1                    REGION <= TSO-NORMALREGION
         ZAP   RF,REGG                 REGION
         MP    RF+7(5),=X'100F'         * 100
         DP    RF+7(5),TSOREG           / TSO-NORMALREGION
         SP    RF+7(2),=X'100F'         - 1,00
         MP    RF+3(6),REGDM            * REGION DM
         MVN   RF+7(1),RF+8
         AP    REGDM,RF+4(4)           = REGION ZUSCHLAG DM
         AP    STEPDM,RF+4(4)
TSO1     XR    R5,R5
         ZAP   RF,SYSI                 TGET+TPUT
         MP    RF+4(8),TSOZGR           * SEKUNDEN PRO TGET+TPUT
         MVN   RF+9(1),RF+11
         AP    RF+6(4),LAUS             + ERR. LAUFZEIT SEKUNDEN
         ZAP   RF(4),RF+6(4)           = SUMME LAUFZEIT  (ZWISCHENSP.)
         BZ    TSO3                    SUMME = 0, TSOFAK(0)
         ZAP   RF+4(8),LAUS            ERR. LAUFZEIT SEKUNDEN
         MP    RF+6(6),=X'010F'         * 10
         DP    RF+6(6),RF(4)            / SUMME LAUFZEIT
*                                      = INDEX TAB. TSOFAK
         CP    RF+6(2),=X'010F'
         BL    TSO2                    TSOFAK(0) BIS TSOFAK(9)
         LA    R5,10                   TSOFAK(10)
         B     TSO3
TSO2     IC    R5,RF+6                 INDEX FUER
         SLL   R5,8                     TSOFAK
         IC    R5,RF+7                  UMRECHNEN
         SRL   R5,4                     IN BINAER
TSO3     MH    R5,=H'3'                INDEX * 3 (3 BYTES JE TSOFAK)
         LA    R7,TSOFAK               ADR. TABELLE TSOFAK
         AR    R7,R5                    + INDEX = ADR. ERRECHN. TSOFAK
         ZAP   RF,STEPDM               STEPKOSTEN DM
         MP    RF+5(7),0(3,R7)          * TSOFAK
         MVN   RF+9(1),RF+11
         ZAP   TSOZDM,RF+6(4)          = TSO ZUSCHLAG DM
         AP    STEPDM,TSOZDM           STEPKOSTEN + TSO ZUSCHLAG
         B     ENDE
         EJECT
*
******** BATCH JOB BEWERTUNG
*
JOB      MVC   PYER,=C'   '            LOESCHEN
         ZAP   PRTYDM,=X'0F'            ERGEBNIS-
         MVC   AVSEDM(8),PRTYDM         FELDER
******** RECHEN PRTY-ZUSCHLAG DM
         CP    JSTART,RENDE
         BNL   PR1
         AP    JSTART,=X'8640000F'
PR1      SP    JSTART,RENDE            RECHNEN TQ
         CP    JENDE,RENDE
         BNL   PR2
         AP    JENDE,=X'8640000F'
PR2      SP    JENDE,RENDE             RECHNEN TA
         BNZ   PR3
         ZAP   RF(2),=X'0F'            WENN TA=0, DANN FS=0
         B     PR4
PR3      ZAP   RF,TQ(5)                TQ
         MP    RF+5(7),=X'010F'         * 10
         DP    RF+5(7),TA(5)            / TA
         SP    RF+5(2),=X'010F'         - 1,0
         OI    RF+6,X'0F'
         ZAP   RF(2),RF+5(2)           = FS  (ZWISCHENSPEICHER)
PR4      PACK  RF+2(10),PYGE           WUNSCH-PRIORITAET
         MP    RF+8(4),=X'010F'         * 10
         MP    RF+6(6),RF(2)            * FS
         MVN   RF+10(1),RF+11          = ERFUELLUNGS-PRIORITAET
         UNPK  PYER,RF+9(2)
         OI    PYER+2,X'F0'
         XR    R5,R5
         CP    RF+9(2),=X'010F'
         BL    PR5                     PTYFAK(0) BIS PTYFAK(9)
         LA    R5,10                   PTYFAK(10)
         B     PR6
PR5      IC    R5,RF+9                 INDEX FUER
         SLL   R5,8                     PTYFAK
         IC    R5,RF+10                 UMRECHNEN
         SRL   R5,4                     IN BINAER
PR6      MH    R5,=H'3'                INDEX * 3 ( BYTES JE PTYFAK)
         LA    R7,PTYFAK               ADR. TABELLE PTYFAK
         AR    R7,R5                    + INDEX = ADR. ERRECHN. PTYFAK
         ZAP   RF,STEGDM               STEPKOSTEN DM
         MP    RF+5(7),0(3,R7)          * PTYFAK
         MVN   RF+9(1),RF+11
         ZAP   PRTYDM,RF+6(4)          = PRTY-ZUSCHLAG DM
*******  RECHNEN AV-SERVICE DM
         CLI   ARBSCHL+1,C'A'
         BNE   JADD                    KEIN AV-SERVICE
         ZAP   RF,STEGDM               STEPKOSTEN DM
         MP    RF+6(6),AVPROZ           * PROZENT AV-SERVICE
         MVN   RF+10(1),RF+11
         ZAP   AVSEDM,RF+7(4)          = AV-SERVICE DM
         CP    AVSEDM,AVMAX
         BNH   JADD                    AV-SERVICE DM <= MAXIMAL DM
         ZAP   AVSEDM,AVMAX            AV-SERVICE DM > MAXIMAL DM
******** RECHNEN JOBKOSTEN DM
JADD     AP    JOBDM,STEGDM
         AP    JOBDM,PRTYDM
         AP    JOBDM,AVSEDM
         B     ENDE
         EJECT
*
******** SYSOUT BEWERTUNG
*
SYSOUT   ZAP   SYSODM,=X'0F'           LOESCHEN ERGEBNISFELD
******** RECHNEN SYSOUT DM
         ZAP   RF+4(8),SYSO            SYSOUT
         CLI   KEN2,C'2'
         BE    SYSC                    RJE
         MP    RF+4(8),LOCA             * LOCAL PREIS
         B     SYSD
SYSC     MP    RF+4(8),REMO             * REMOTE PREIS
SYSD     MVN   RF+9(1),RF+11
         ZAP   SYSODM,RF+6(4)          = SYSOUT DM
         B     ENDE
         EJECT
*
******** PROGRAMM - ENDE
*
ENDE     EQU   *
         LR    R1,R13
         L     R13,4(R13)
         FREEMAIN R,LV=72,A=(1)
         LM    R14,R12,12(R13)
         BR    R14
         EJECT
*
******** PREISE FUER BEWERTUNG
*
ZUGR     DC    PL4'0000000'  0,000    SEKUNDEN PRO ZUGRIFF
CPU      DC    PL4'0000000'  0,000    DM*SEKUNDEN CPU
SYST     DC    PL4'0000000'  0,000    DM*SEK. LAUFZEIT SYSTEM
REGI     DC    PL4'0000000'  0,000    DM*KB*SEK. LAUFZEIT
PLAT     DC    PL4'0000000'  0,000    DM*PLATTE*SEK. LAUFZEIT
BAND     DC    PL4'0000000'  0,000    DM*BAND*SEK. LAUFZEIT
UNIT     DC    PL4'0000000'  0,000    DM*UR*SEK. LAUFZEIT
ZPLA     DC    PL4'0000000'  0,000    DM*ZUGRIFF PLATTE
ZBAN     DC    PL4'0000000'  0,000    DM*ZUGRIFF BAND
LOCA     DC    PL4'0000000'  0,000    DM*SYSIN+SYSOUT LOCAL
REMO     DC    PL4'0000000'  0,000    DM*SYSIN+SYSOUT REMOTE
*                                     DM*TGET+TPUT TSO
FIX      DC    PL4'0000000'  0,000    DM*STEP FIXKOSTEN
RUPL     DC    PL4'0000000'  0,000    DM*RUESTKOSTEN PLATTE
RUBD     DC    PL4'00002000  0,000    DM*RUESTKOSTEN BAND
RUUR     DC    PL4'0000000'  0,000    DM*RUESTKOSTEN UR
*
*
AVPROZ   DC    PL2'000'               PROZENT AV-SERVICE
AVMAX    DC    PL4'0000000'  0,000    DM MAXIMAL AV-SERVICE
*
*
TSOREG   DC    PL3'00000'  000        KB NORMALREGION FUER TSO
TSOZGR   DC    PL4'0000000'  0,000    SEKUNDEN PRO TGET/TPUT
TSOFAK   DC    PL3'03500'    0,35     FAKTOR FUER TSO-ZUSCHLAG
         DC    PL3'04000'    0,40
         DC    PL3'04300'    0,43
         DC    PL3'04800'    0,48
         DC    PL3'05500'    0,55
         DC    PL3'06500'    0,65
         DC    PL3'07500'    0,75
         DC    PL3'09000'    0,90
         DC    PL3'12500'    1,25
         DC    PL3'19000'    1,90
         DC    PL3'23000'    2,30
*
*
PTYFAK   DC    PL3'00000'    0,00     FAKTOR FUER PRTY-ZUSCHLAG
         DC    PL3'01000'    0,10
         DC    PL3'02000'    0,20
         DC    PL3'03000'    0,30
         DC    PL3'04000'    0,40
         DC    PL3'05000'    0,50
         DC    PL3'06000'    0,60
         DC    PL3'07000'    0,70
         DC    PL3'08000'    0,80
         DC    PL3'09000'    0,90
         DC    PL3'10000'    1,00
*
*
LIZTAB   DC    C'PERTGEN '
         DC    PL4'0000000'  0,00     DM LIZENZGEBUEHR
         DC    C'PMSINC4N'
         DC    PL4'0000000'  0,00     DM LIZENZGEBUEHR
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
         DC    X'40404040404040400000000C'
*
*
         LTORG
         EJECT
*
******** WERTBEREICHE / ARBEITSBEREICH
*
WERT1    DSECT          BATCH/TSO STEP                        W E R T
PROG     DS    CL8  PROGRAMM                     8   STELLEN  VORGEGEB.
KEN1     DS    CL1  KENNZ. 0=NOT EXECUTED        1   STELLE   VORGEGEB.
*                          1=BATCH  2=RJE
*                          3=TSO
REGG     DS    PL3  REGION GEFORDERT             5   STELLEN  VORGEGEB.
REGN     DS    PL3  REGION GENUTZT               5   STELLEN  VORGEGEB.
PLPR     DS    PL2  PLATTEN PRIVAT+GERUESTET     3   STELLEN  VORGEGEB.
BDPI     DS    PL2  BAENDER PRIVAT               3   STELLEN  VORGEGEB.
BDRU     DS    PL2  BAENDER GERUESTET            3   STELLEN  VORGEGEB.
URPR     DS    PL2  UR PRIVAT+GERUESTET          3   STELLEN  VORGEGEB.
EAPL     DS    PL4  E/A OP PLATTE                7   STELLEN  VORGEGEB.
EABD     DS    PL4  E/A OP BAND                  7   STELLEN  VORGEGEB.
EAUR     DS    PL4  E/A OP UR                    7   STELLEN  VORGEGEB.
SYSI     DS    PL4  SYSIN+SYSOUT/TGET+TPUT       7   STELLEN  VORGEGEB.
CPUS     DS    PL4  CPU SEKUNDEN                 5,2 STELLEN  VORGEGEB.
LAUS     DS    PL4  ERR. LAUFZEIT SEKUNDEN       5,2 STELLEN  ERRECHN.
CPUDM    DS    PL4  CPU DM                       5,2 STELLEN  ERRECHN.
SYSDM    DS    PL4  SYSTEM DM                    5,2 STELLEN  ERRECHN.
REGDM    DS    PL4  REGION DM                    5,2 STELLEN  ERRECHN.
LIZDM    DS    PL4  LIZENZ DM                    5,2 STELLEN  ERRECHN.
STXDM    DS    PL4  STEPFIXKOSTEN DM             5,2 STELLEN  ERRECHN.
PLEADM   DS    PL4  PLATTEN E/A OP DM            5,2 STELLEN  ERRECHN.
PLPRDM   DS    PL4  PLATTEN PRIVAT DM            5,2 STELLEN  ERRECHN.
PLRUDM   DS    PL4  PLATTEN RUESTKOSTEN DM       5,2 STELLEN  ERRECHN.
BDEADM   DS    PL4  BAND E/A OP DM               5,2 STELLEN  ERRECHN.
BDPRDM   DS    PL4  BAND PRIVAT DM               5,2 STELLEN  ERRECHN.
BDRUDM   DS    PL4  BAND RUESTKOSTEN DM          5,2 STELLEN  ERRECHN.
URPRDM   DS    PL4  UR PRIVAT DM                 5,2 STELLEN  ERRECHN.
URRUDM   DS    PL4  UR RUESTKOSTEN DM            5,2 STELLEN  ERRECHN.
SYSIDM   DS    PL4  SYSIN+SYSOUT/TGET+TPUT DM    5,2 STELLEN  ERRECHN.
TSOZDM   DS    PL4  TSO ZUSCHLAG DM              5,2 STELLEN  ERRECHN.
STEPDM   DS    PL4  STEPKOSTEN DM                5,2 STELLEN  ERRECHN.
*
*
WERT2    DSECT          BATCH JOB                             W E R T
PYGE     DS    CL3  PRTY GEFORDERT               3   STELLEN  VORGEGEB.
ARBSCHL  DS    CL2  ARBEITSSCHLUESSEL            2   STELLEN  VORGEGEB.
STEGDM   DS    PL4  STEPKOSTEN(JOB) DM           5,2 STELLEN  VORGEGEB.
RENDE    DS    PL5  ZEIT READER ENDE (100/SEK.)  9   STELLEN  VORGEGEB.
JSTART   DS    PL5  ZEIT JOB START   (100/SEK.)  9   STELLEN  VORGEGEB.
TQ       EQU   JSTART
JENDE    DS    PL5  ZEIT JOB ENDE    (100/SEK.)  9   STELLEN  VORGEGEB.
TA       EQU   JENDE
PYER     DS    CL3  PRTY ERHALTEN                3   STELLEN  ERRECHN.
PRTYDM   DS    PL4  PRTY DM                      5,2 STELLEN  ERRECHN.
AVSEDM   DS    PL4  AV-SERVICE DM                5,2 STELLEN  ERRECHN.
JOBDM    DS    PL4  JOBKOSTEN DM                 5,2 STELLEN  ERRECHN.
*
*
ARBEIT   DSECT
RF       DS    CL12                    RECHENFELD
*
*
WERT3    DSECT          BATCH SYSOUT                          W E R T
KEN2     DS    CL1  KENNZ. 1=BATCH  2=RJE        1   STELLE   VORGEGEB.
SYSO     DS    PL4  SYSOUT                       7   STELLEN  VORGEGEB.
SYSODM   DS    PL4  SYSOUT DM                    5,2 STELLEN  ERRECHN.
*
*
         END
