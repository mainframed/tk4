PROC 0
GLOBAL &HEXVALUE
GLOBAL &VALUE
/**********************************************************************/
/*     H     H   EEEEEEE    AAAAA    DDDDDD    EEEEEEE   RRRRRR       */
/*     H     H   E         A     A   D     D   E         R     R      */
/*     HHHHHHH   EEEEEEE   AAAAAAA   D     D   EEEEEEE   RRRRRR       */
/*     H     H   E         A     A   D     D   E         R   R        */
/*     H     H   EEEEEEE   A     A   DDDDDD    EEEEEEE   R     R      */
/**********************************************************************/
/*  CLIST NAME: VTAM21            REL/DATE: 2.1/08.15.79              */
/*                                                                    */
/*  DOC: THIS CLIST WILL ANALYZE THE STATUS OF ALL BUFFER TYPES IN A  */
/*       VTAM DUMP.                                                   */
/*  SYMBOLS EQUATED: LPBUF, FIXFLAG.                                  */
/*  SYMBOLS FOUND: LBBUF, FIXFLAG.                                    */
/*  RETURN: CONTROL IS PASSED TO THE CALLING CLIST OR IPCS.           */
/*  DEPENDENCIES: 1) THIS CLIST REQUIRES THAT COMMAND                 */
/*                   CLEAR BE INSTALLED IN YOUR ENVIRONMENT.        */
/*                2) THIS CLIST REQUIRES THAT CLIST 'HEX' BE          */
/*                   INSTALLED IN YOUR ENVIRONMENT.                   */
/*  USE: THIS CLIST PROMPTS THE USER FOR THE BUFFER TYPES HE IS       */
/*       LOOKING FOR AND DISPLAYS INFORMATION ABOUT VTAM BUFFERS.     */
/*SJM******************************************************************/
SETD PRINT TERMINAL                      /* DO SOME HOUSKEEPING       */
WRITENR DO YOU WISH THE PRINT OPTION? (Y)/N ===>
READ &ANS
IF &ANS = N THEN DO
SETD NOPRINT TERMINAL
END
NOTE 'START OF BUFFER ANALYSIS' NOTERMINAL PRINT PAGE
FUNC1: WRITE ENTER   A/ALL  S/SPECIFIC BUFF ANALYSIS  E/END
READ &ANS                                /* ASK USER WHAT TO LOOK FOR */
IF &ANS=&STR(S) THEN DO
P1: WRITE ENTER NUMBER CORRESPONDING TO BUFFER TYPE EG. 1  2  ETC
 WRITE WHERE 1=ACE,2=SP,3=SF,4=UECB,5=CRPL,6=LF,7=NP,8=FMCB,9=LP,10=DA-
TA,11=IO,12=SMS
 READ &ANS1                              /* WHAT BUFFER TYPE SHOULD WE*/
 IF &DATATYPE(&ANS1)^=NUM × &ANS1>12 × &ANS1<1 THEN DO /* LOOK AT     */
   WRITE INVALID REPLY RENTER
   GOTO P1
   END                                   /* GO TO THE ROUTINE TO DO   */
 SET &TYPE=&ANS1                         /* THE JOB.                  */
 GOTO SETUP
 END
IF &ANS=&STR(E) THEN DO
  GOTO FUNC1END
  END
IF &ANS^=&STR(A) && &ANS^=&STR(S) && &ANS^=&STR(E) THEN DO
  WRITE INVALID REPLY  REENTER
  GOTO FUNC1
  END
SET &TYPE=12
SETUP: IF &TYPE=12 THEN DO               /* THE FOLLOWING CODE PICKS  */
  EQ LPE BPDTY+98%                       /* UP THE POINTER TO THE     */
  EQ FIXFLAG BPDTY+94  LE(2)             /* BUFFER POOL CONTROL BLOCK */
  SET &BTYPE=&STR(S M S)                 /* FOR THE VARIOUS BUFFER    */
  GOTO CALC                              /* TYPES AND BRANCHES TO THE */
  END                                    /* FORMATTING ROUTINE.       */
  IF &TYPE=11 THEN DO
  EQ LPE BPDTY+A8%
  EQ FIXFLAG BPDTY+A4  LE(2)
  SET &BTYPE=&STR(I O)
  GOTO CALC
  END
  IF &TYPE=10 THEN DO
  EQ LPE BPDTY+B8%
  EQ FIXFLAG BPDTY+B4   LE(2)
  SET &BTYPE=&STR(D A T A)
  GOTO CALC
  END
  IF &TYPE=09 THEN DO
  EQ LPE BPDTY+C8%
  EQ FIXFLAG BPDTY+C4  LE(2)
  SET &BTYPE=&STR(L P)
  GOTO CALC
  END
  IF &TYPE=08 THEN DO
  EQ LPE BPDTY+D8%
  EQ FIXFLAG BPDTY+D4   LE(2)
  SET &BTYPE=&STR(F M C B)
  GOTO CALC
  END
  IF &TYPE=07 THEN DO
  EQ LPE BPDTY+E8%
  EQ FIXFLAG BPDTY+E4   LE(2)
  SET &BTYPE=&STR(N P)
  GOTO CALC
  END
  IF &TYPE=06 THEN DO
  EQ LPE BPDTY+F8%
  EQ FIXFLAG BPDTY+F4   LE(2)
  SET &BTYPE=&STR(L F)
  GOTO CALC
  END
  IF &TYPE=05 THEN DO
  EQ LPE BPDTY+108%
  EQ FIXFLAG BPDTY+104   LE(2)
  SET &BTYPE=&STR(C R P L)
  GOTO CALC
  END
  IF &TYPE=04 THEN DO
  EQ LPE BPDTY+118%
  EQ FIXFLAG BPDTY+114   LE(2)
  SET &BTYPE=&STR(U E C B)
  GOTO CALC
  END
  IF &TYPE=03 THEN DO
  EQ LPE BPDTY+128%
  EQ FIXFLAG BPDTY+124  LE(2)
  SET &BTYPE=&STR(S F)
  GOTO CALC
  END
  IF &TYPE=02 THEN DO
  EQ LPE BPDTY+138%
  EQ FIXFLAG BPDTY+134   LE(2)
  SET &BTYPE=&STR(S P)
  GOTO CALC
  END
  IF &TYPE=01 THEN DO
  EQ LPE BPDTY+148%
  EQ FIXFLAG BPDTY+144    LE(2)
  SET &BTYPE=&STR(A C E)
  GOTO CALC
  END
ELSE DO
  GOTO FUNC1
  END
CALC:EVAL LPE+20 LE(2)               /* GET SIZE LPBUF */
SET &LPSIZE=&LASTCC             /* SIZE LPBUF  */
IF &LASTCC=12 THEN GOTO FUNC1END
EVAL LPE+22 LE(2)               /* GET TOTAL NUM LPBUF ALLOCATED */
SET &LPTNUM=&LASTCC             /* TOTAL LPBUF ALLOCATED */
EVAL LPE+24 LE(2)               /* GET THRESHOLD LPBUF   */
SET &LPTHRES=&LASTCC            /* THRESHOLD LPBUF    */
EVAL LPE+3E  LE(2)              /* GET NUM AVAIL LPBUF */
SET &LPAVAIL=&LASTCC            /* AVAILABLE LPBUF */
EVAL LPE+38 LE(1)               /* GET BUF SLOWDOWN FLAGS */
SET &LPSLOW=&LASTCC             /* SLOWDOWN BYTE */
NOTE '********* &BTYPE BUFFER ANALYSIS  *************** ' SPACE
NOTE 'BUFSIZE = &LPSIZE   ALLOCATED = &LPTNUM   THRESHOLD = &LPTHRES -
 AVAILABLE = &LPAVAIL' NOSPACE
IF &LPSLOW>0 THEN  NOTE '&BTYPE BUFFERS ARE IN  SSLLOOWWDDOOWWNN    -
 SLOWDOWN STATUS  ?????? ' NOSPACE
IF &LPTHRES>&LPAVAIL × &LPAVAIL<=0 THEN DO/* PAST THE THRESHOLD ?? */
  NOTE 'NUMBER OF &BTYPE BUFFS AVAILABLE ARE BELOW THRESHOLD OR ZERO -
   ?????? ' NOSPACE
  WRITE
  END
  ELSE DO
  SET &PCENT=(&LPAVAIL*100)/&LPTNUM
SET &PCENT=100-&PCENT
NOTE 'NUMBER OF &BTYPE BUFFERS AVAILABLE ARE O.K. % USED = &PCENT '+
SPACE
  WRITE
  END
SET &TYPE=&TYPE-1
IF &ANS=&STR(S) THEN DO
 GOTO FUNC1
 END
DROPSYM (FIXFLAG)
GOTO SETUP
FUNC1END: WRITE VTAM21 COMPLETED.
