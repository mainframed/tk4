***********************************************************************
*
*  S0STRES ENTSCHEIDET ANHAND UEBERGEBENER PARAMETER OB EINE DRIVE
*  RESERVIERT ODER FREIGEGEBEN WERDEN SOLL. ES WIRD NUR EIN RESERVE
*  AUFGESETZT UND EBENFALLS NUR EIN DEQ , WENN EIN RESERVE VORANGEGAN-
*  GEN IST.
*  RETURN CODES DIE IN DIESER ROUTINE AUFTRETEN KOENNEN:
*              1 - FALSCHER AKTIONSPARAMETER (NICHT RES ODER DEQ)
*              2 - DDNAME FUER DEQ STIMMT NICHT MIT DDNAMEN VON RESERVE
*                  UEBEREIN
*              3 - DDNAME IST IN TIOT NICHT VORHANDEN
*              4 - LESEN DES JFCB WAR NICHT ERFOLGREICH
*              5 - RESERVE IST NOCH AKTIV. KEIN NEUER RESERVE
*
*  ERWARTETE PARAMETER:
*              1. DDNAME
*              2. AKTION   ('RES' ODER 'DEQ')
*              3. RETURNCODE FELD
*
*              DIE PARAMETER WERDEN IN PL/1 SYNTAX ERWARTET
*
*  REGISTERBELEGUNG:
*              R2 - ADRESSE PARAMETERLISTE
*              R3 - ADRESSE DDNAME
*              R4 - ADRESSE AKTION
*              R5 - ADRESSE RETURN - CODES
*              R6 - ARBEITSREGISTER FUER RETURN - CODES
*              R7 - ARBEITSREGISTER FUER TIOT, DD-ENTRY, UCB
*              R8 - ARBEITSREGISTER ALLGEMEIN
*              R12 - BASISREGISTER
*
*
*
S0STRES  CSECT
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         LA    11,A0001
         ST    11,8(13)
         ST    13,4(11)
         LR    13,11
         B     X0001
A0001    DC    18F'0'
X0001    EQU   *
 SPACE
*
*  UEBERGABE PARAMETER UND ENTSCHEIDUNG WELCHE AKTION AUSGEFUEHRT WIRD
*
 SPACE
         LR    2,1           ADR. PARAMETERLISTE UMLADEN
         L     3,0(2)        ADR. DOPEVECTOR DDNAME
         L     3,0(3)        ADR. DDNAME
         L     4,4(2)        ADR. DOPEVECTOR AKTION
         L     4,0(4)        ADR. AKTION
         L     5,8(2)        ADR. RETURN-CODES
*
         CLC   0(3,4),=C'RES' WELCHE AKTION WIRD VERLANGT ?
         BE    ACTRES        RESEVE
         CLC   0(3,4),=C'DEQ'          ODER
         BE    ACTDEQ        DEQ
*
         LA    6,1           FALSCHER PARAMETER - RES, DEQ
RCODE    ST    6,0(5)        RETURNCODE ABSPEICHERN
         B     RETURN
*
 SPACE
*
*  AJTION RESERVE WURDE VERLANGT
*
 SPACE
ACTRES   CLC   ZDDNAME,BLANK ZWISCHENSPEICHER LEER ?
         BNE   FRES
         MVC   ZDDNAME,0(3)  UEBERGEBENER DDNAME N. ZW.SPEICHER
         EXTRACT ADRTIOT,FIELDS=TIOT
         L     7,ADRTIOT     ADR. TIOT NACH R7
         LA    7,24(7)       LADEN ADR. U.DD-ENTRY IN TIOT
NEXTDD   CLC   ZDDNAME,4(7)  SUCHEN UEBERGEBEN. DD NAME IN TIOT
         BE    FUCB          DDNAME GEFUNDEN
         SR    8,8           LOESCHEN R8
         IC    8,0(7)        LAENGE DD-ENTRY NACH R8
         LA    7,0(8,7)      ADR. NEXT DD-ENTRY
         CLC   0(4,7),=F'0'  ENDE TIOT ?
         BNE   NEXTDD
*
         LA    6,3           DDNAME NICHT IN TIOT
         B     RCODE
 SPACE
*
*  LADEN ADRESSE DES UCB
*
 SPACE
FUCB     LA    7,16(7)       LADEN ADR. DER ADR. DES UCB
         L     7,0(7)        LADEN ADR. UCB NACH R7
 SPACE
*
*  LESEN JFCB DES UEBERGEBENEN DDNAMEN
*
 SPACE
         MVC   DDCB+40(8),ZDDNAME  DDNAME IN DCB EINTRAGEN
         RDJFCB MF=(E,EX1)
EDSN     MVC   RNAME,JFCBDSNM DSNAME F. RESEVERVE U. DEQ EINSETZEN
         RESERVE (QNAME,RNAME,E,,SYSTEMS),UCB=(7)
         SR    6,6
         B     RCODE
*
FRES     LA    6,5           NOCH EIN RESERVE OFFEN
         B     RCODE
 SPACE
*
*  AKTION DEQ WURDE VERLANGT
*
 SPACE
ACTDEQ   CLC   ZDDNAME,0(3)  UEBERGEBENER DDNAME = ZWISCHENSPEICHER ?
         BNE   FDDNAME
         DEQ   (QNAME,RNAME,,SYSTEMS)  FREIGABE DER EINHEIT
         MVC   ZDDNAME,BLANK LOESCHEN ZWISCHENSPEICHER
         SR    6,6           ERFOLGREICH
         B     RCODE
*
FDDNAME  LA    6,2           FALSCHER DDNAME F. FREIGABE
         B     RCODE
*
 SPACE
*
RETURN   LR    1,2
         L     13,A0001+4
         LM    14,12,12(13)
         BR    14
*
 SPACE
ZDDNAME  DC    CL8' '        DDNAME - ZWISCHENSPEICHER
QNAME    DC    C'RESERVED'   Y SPEZIFIKATION FUER
RNAME    DC   CL44' '        Y RESERVE UND DEQ MACRO
BLANK    DC    CL8' '
ADRTIOT  DC    F'0'          ADRESSFELD F. TIOT
BJFCB    DS    CL176         BEREICH FUER JFCB
*
*
*
EX1      RDJFCB (DDCB),MF=L
LJFCB    DS    0F
         DC    X'07'
         DC    AL3(BJFCB)
DDCB     DCB   DSORG=PO,MACRF=(W),EXLST=LJFCB
BJFCB    IEFJFCBN
         END
