**********************************************************************
*                                                                    *
*     THE USE OF THIS PROGRAM IS TO PREVENT AN UPDATING OF AN        *
*                                                                    *
*     PO-DATA-SET BY MORE THAN ONE IEBUPDTE- OR IEBGENER-PROGRAMS    *
*                                                                    *
*     FROM 1 ORE MORE THAN 1 SYSTEM.                                 *
*                                                                    *
*     THIS PROGRAM MUST BE LINKED UNDER THE NAME IEBUPDTE IN AN      *
*                                                                    *
*     LOAD MODULE LIBRARY (FOR EXAMPLE THE SYS1.LINKLIB) AND YOU     *
*                                                                    *
*     MUST CHANGE THE NAME OF THE ORIGINAL IBM - UTILITY TO XXXUPDTE,*
*                                                                    *
*     BECAUSE IEBUPDTE WILL INVOKE THE ORIGINAL IBM - UTILITY        *
*                                                                    *
*     WITH THE NAME XXXUPDTE.                                        *
*                                                                    *
*     WHAT HAPPENS IF THIS PROGRAM FINDS AN PO-DATA-SET-NAME ON THE  *
*                                                                    *
*     SYSUT2 STATEMENT ?                                             *
*                                                                    *
*     IF THE PO-DATA-SET IS NOT ON A SHARED-DEVICE THIS PROGRAM      *
*                                                                    *
*     GIVES AN EXCLUSIVE ENQUEUE.                                    *
*                                                                    *
*     IF THE DATA-SET IS ON A SHARED-DEVICE THIS PROGRAM TESTS       *
*                                                                    *
*     WETHER THE PO-DATA-SET IS ON ONE OF THE VOLUMES, WHICH ARE     *
*                                                                    *
*     ALLOWED  TO BE SHARED. IN THIS CASE THE PROGRAM ISSUES THE     *
*                                                                    *
*     MACRO RESERVE FOR THE SHARED-DEVICE.                           *
*                                                                    *
**********************************************************************
         EJECT
RESUPDTE CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
***********************************************************
*    LINKAGE CONVENTION, SAVE ADDRESS OF PARAMETERLIST    *
***********************************************************
         SAVE  (14,12)
         BALR  RC,0               LOAD BASISREGISTER
         USING *,RC
         LR    RA,RD
         LA    RD,SA
         ST    RA,4(RD)           ADDRESS OF SAVEAREA BACKWARDS
         ST    RD,8(RA)           ADDRESS OF SAVEAREA FORWARD
         B     PARM1
SA       DS    18F
PARM1    EQU   *
         L     R1,0(0,R1)          ADDRESS OF ADDRESS OF PARMLIST
         ST    R1,PARMADR
         SR    RB,RB
         EXTRACT TIOTADDR,FIELDS=TIOT ADDRESS OF TIOT
         L     RA,TIOTADDR         A(TIOT) INTO RA
         LA    RA,24(,RA)          A(DDENTRY) IN TIOT
TIOTUT2  EQU   *
         CLI   0(RA),X'00'         LENGTH OF THIS ENTRY = 0
         BE    IEBUPDTE            YES, NO SYSUT2 - NO ENQUEUE/RESERVE
         CLC   4(8,RA),UT2DCB+40   DDNAME=SYSUT2?
         BE    RESUT2              YES
         IC    RB,0(RA)            LENGTH OF THIS ENTRY IN REG11
         LA    RA,0(RB,RA)         ADDRESS OF NEXT DDENTRY
         B     TIOTUT2
RESUT2   EQU   *
*  READ JOB FILE CONTROLBLOCK
         RDJFCB (UT2DCB,OUTPUT)
         MVC   FUCB+1(3),17(RA)
         L     RA,FUCB             RA=ADDRESS OF UCB
         TM    17(RA),X'20'        SHARED BIT IN UCB ON
         BZ    ENQUEUE             NO, IT IS OFF
TABVOL   EQU   *
         LA    R7,VOLNR             NUMBER OF SHARED VOLUMES
         LA    R9,TABVOLS           NAMES OF SHARED VOLUMES
TABVOL1  EQU   *
         CLC   JFCB+118(6),0(R9)    SHARED VOLUME NAME?
         BE    RESERVE
         LA    R9,6(R9)
         BCT   R7,TABVOL1
         B     ENQUEUE
**********************************************************************
*                                                                    *
*  ENQ REQUEST CONTROL OF ONE SERIALLY REUSABLE RESOURCE TO THE TASK
*                                                                    *
**********************************************************************
ENQUEUE  ENQ   (QNAME,JFCB,E,44,SYSTEM),RET=HAVE
         LTR   RF,RF
         BZ    IEBUPDTQ      YES
         DC    F'0'          NO, ABEND 0C1
**********************************************************************
*                                                                    *
*  RESERVE A DEVICE FOR USE BY A PARTICULAR SYSTEM
*                                                                    *
**********************************************************************
RESERVE  RESERVE (QNAME,JFCB,E,44,SYSTEMS),RET=HAVE,UCB=FUCB
         LTR   RF,RF         RETURN CODE
         BZ    IEBUPDTQ      OK
         DC    F'0'          NO, ABEND 0C1
*  LINK UTILITY IEBUPDTE
IEBUPDTQ OI    FLG,X'80'     ENQ,RESERVE
IEBUPDTE L     R2,PARMADR    ADR DER PARM-LISTE AUS EXEC KARTE LADEN
         LINK  EP=XXXUPDTE,PARAM=((2)) DO NOT CODE 'VL=1'
         ST    RF,RCODE      RETURN-CODE VOM IEBUPDTE RETTEN
         TM    FLG,X'80'     ENQ,RESERVE?
         BZ    RETURN        NO
         DEQ   (QNAME,JFCB,44,SYSTEM),RET=HAVE
RETURN   L     RF,RCODE      IEBUPDTE-RC AN OS UEBERGEBE
         RETURN (14,12),RC=(15)  ZURUECK ZUM OS
         EJECT
UT2DCB   DCB   DDNAME=SYSUT2,EXLST=AJFCB,MACRF=(W),DSORG=PO
AJFCB    DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176         JOB-FILE CONTROL BLOCK (RNAME)
QNAME    DC    C'PRESERVE'
PARMADR  DC    F'0'
TIOTADDR DC    F'0'
RCODE    DC    F'12'
FLG      DC    X'00'
FUCB     DC    F'0'
VOLNR    EQU   6
TABVOLS  DC    CL36'SYSSHRTESTB1TESTB2TESTC1TESTC2P50008'
         END
