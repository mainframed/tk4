IEFACTRT CSECT
*********    BBC PROGRAMM   I 1 5 5    ********************************
*
***********************************************************************
*                                                                     *
*        DAS PROGRAMM IST EINE EXIT-ROUTINE FUER                      *
*                                                                     *
*                   SMF - EXIT  IEFACTRT                              *
*                                                                     *
*        UND GIBT BEI JOB- UND STEP-BEENDIGUNG ZUSAETZLICHE           *
*        INFORMATIONEN UEBER SYSTEM-SYSPRINT AUS.                     *
*                                                                     *
*        DIESES PROGRAMM IST NACH DEN VORGABEN FUER                   *
*        SMF-EXIT IEFACTRT GEMAESS                                    *
*                                                                     *
*             OS/VS SYSTEM MANAGEMENT FACILITIES GC35-0004            *
*                                                                     *
*        ERSTELLT.                                                    *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        VERWENDUNG DER REGISTER                                      *
*                                                                     *
R0       EQU   0    MAKRO                                             *
R1       EQU   1    MAKRO, ADR. POINTER, ARBEITS-REGISTER             *
R2       EQU   2    ADR.-LISTE IEFACTRT AUS REG. 1                    *
R3       EQU   3    ADR. JOB/STEP-SATZ + 4                            *
R4       EQU   4    ADR. BEREICH FUER ZEILENAUFBAU                    *
R5       EQU   5    ADR. DSECT ARBEITSBEREICHE / SPEICHER             *
R6       EQU   6    ARBEITS-REGISTER                                  *
R7       EQU   7    ARBEITS-REGISTER                                  *
R8       EQU   8    ARBEITS-REGISTER                                  *
R9       EQU   9    BAL REGISTER                                      *
R10      EQU   10   BASIS-REGISTER                                    *
R11      EQU   11   BASIS-REGISTER                                    *
R12      EQU   12   VERBINDUNGS-REGISTER ZU IEFYS, ADR. POINTER ZEILE *
R13      EQU   13   SAVE AEREA                                        *
R14      EQU   14   BALR REGISTER                                     *
R15      EQU   15   BALR REGISTER                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*        BENUTZTE MAKROS               GETMAIN                        *
*                                      TPUT                           *
*                                      FREEMAIN                       *
*                                                                     *
***********************************************************************
*                                                                     *
*        BENUTZTE EXTERNE ROUTINEN     IEFYS                          *
*                                                                     *
***********************************************************************
*                                                                     *
*        DIE SMF-COMMON EXIT PARAMETER AREA WIRD WIE FOLGT VERWENDET  *
*                                                                     *
*              BYTE 20 - 23 NICHT BENUTZT                             *
*              BYTE 23      SCHALTER WENN BEI WARM-START HASP EINEN   *
*                           JOB NICHT BEI DEN 1. STEP RE-STARTET      *
*                           BLANK = NICHT GESETZT / KEIN HINWEIS      *
*                           1     = GESETZT / HINWEIS BEI JOB-ENDE    *
*              BYTE 24 - 27 SUMMENSPEICHER FUER KOSTEN DER STEPS      *
*                           EINES JOBS                                *
*                           DIESER SPEICHER WIRD BEI JEDEM 1. STEP    *
*                           EINES JOB ODER WENN SCHALTER GESETZT IST  *
*                           AUF GEPACKT NULL GELOESCHT                *
*                                                                     *
***********************************************************************
         EJECT
*
******** PROGRAMM - ANFANG ********************************************
*
         B     12(R15)
         DC    CL8'IEFACTRT'
         STM   R14,R12,12(R13)
         LR    R10,R15
         USING IEFACTRT,R10,R11        BASIS-REGISTER R10, R11
         L     R11,BASIS
         LR    R2,R1                   ADR-POINTER IN R2
         GETMAIN R,LV=586
         ST    R13,4(R1)               VERKETTEN
         ST    R1,8(R13)                SAVEAREAS
         LR    R13,R1                  72 BYTES
         LA    R1,72(R1)                SAVEAREA IEFACTRT
         LR    R4,R1                   120 BYTES
         LA    R1,120(R1)               FUER ZEILENAUFBAU
         LR    R6,R1                   180 BYTES (ADR.ZWISCHENSP.)
         LA    R1,180(R1)               SAVEAREA IEFYS
         LR    R5,R1                   214 BYTES
         USING ABER,R5                  ARBEITSBEREICHE
         ST    R6,SVIEFYS              ADR. AUS ZWISCHENSPEICHER
         L     R3,36(R2)               ADR JOB/STEP-SATZ IN R3
         LA    R3,4(R3)                RDW BERUECKSICHTIGEN
         CLI   14(R3),C'R'
         BNE   NRJE
         CLI   15(R3),X'F0'
         BL    NRJE
         CLI   15(R3),X'F9'
         BH    NRJE
         MVI   SCH2,X'F2'              RJE JOB
         B     XRJE
NRJE     MVI   SCH2,X'F1'              BATCH JOB
XRJE     EQU   *
         CLI   1(R3),X'04'
         BE    DSTEP                   STEP-DATEN FUER KASSENZETTEL
         CLI   1(R3),X'05'
         BE    DJOB                    JOB-DATEN FUER KASSENZETTEL
         CLI   1(R3),X'22'
         BNE   ENDE                    KEIN KASSENZETTEL
         MVI   SCH2,X'F3'              TSO STEP
         B     DSTEP                   STEP-DATEN FUER KASSENZETTEL
BASIS    DC    A(IEFACTRT+4096)
         EJECT
*
******** AUSGANGSDATEN FUER STEP-BEWERTUNG ERMITTELN ******************
*
DSTEP    EQU   *
         MVC   PROG,54(R3)             PROGRAMM NAME
         MVC   KENN,SCH2               KENNZEICHEN BATCH/RJE/TSO
         TM    83(R3),X'01'
         BZ    EX1                     STEP WAR EXECUTED
         MVI   KENN,X'F0'              STEP WAR NICHT EXECUTED
EX1      EQU   *
         XC    FW1,FW1
         MVC   FW1+2(2),70(R3)
         L     R6,FW1
         CVD   R6,DW1
         ZAP   REGG,DW1+5(3)           REGION GEFORDERT
         MVC   FW1+2(2),74(R3)
         L     R6,FW1
         CVD   R6,DW1
         ZAP   REGN,DW1+5(3)           REGION GENUTZT
         ZAP   PLPR,=X'0F'             LOESCHEN
         MVC   BDPI(6),PLPR             WERTFELDER
         ZAP   EAPL,=X'0F'
         MVC   EABD(12),EAPL
         LA    R6,102(R3)
         AH    R6,102(R3)
         CLI   4(R6),X'00'
         BE    ACC1                    KEINE STEP-ACC.
         CLC   6(3,R6),=C'PM='
         BNE   ACC1                    KEIN PLATTEN-MOUNT
         CLI   9(R6),X'F0'
         BL    ACC1                    FEHLERHAFTE ANGABE
         CLI   9(R6),X'F9'
         BH    ACC1                    FEHLERHAFTE ANGABE
         PACK  PLPR,9(1,R6)            PLATTEN PRIVAT+GERUESTET
ACC1     EQU   *
         CLI   SCH2,X'F3'
         BNE   OS1
         MVC   FW1,43(R3)
         L     R6,FW1
         CVD   R6,DW1
         AP    SYSI,DW1+4(4)           TSO TPUT
OS1      MVC   FW1,47(R3)
         L     R6,FW1
         CVD   R6,DW1
         AP    SYSI,DW1+4(4)           OS SYSIN / TSO TGET
         LA    R6,102(R3)              DD-EINTRAEGE
         AH    R6,102(R3)               ABARBEITEN
         LA    R7,104(R3)
         CR    R6,R7
         BNH   DD3                     KEINE DD-EINTRAEGE
         MVC   BTXX,=X'9999'            DER PRIVAT-BAENDER LOESCHEN
         XC    BTAB,BTAB               TABELLE FUER ERMITTLUNG
DD1      CLC   0(2,R7),=X'0000'
         BE    DD2                     DUMMY DD-EINTRAG
         CLC   0(2,R7),=X'0805'        INTERNER READER
         BE    DD2                      NICHT BERECHNEN
         MVC   FW1,4(R7)
         L     R8,FW1
         CVD   R8,DW1
         CLC   2(2,R7),=X'0599'
         BH    HASP                    HASP DD-EINTRAG
         CLC   0(2,R7),=X'2009'
         BE    DISK                    DISK DD-EINTRAG
         CLC   0(2,R7),=X'200D'
         BE    DISK                    DISK DD-EINTRAG
         CLC   0(2,R7),=X'8003'
         BE    TAPE                    TAPE DD-EINTRAG
         CLC   0(2,R7),=X'0801'
         BE    UREC                    UR DD-EINTRAG
         CLC   0(2,R7),=X'0802'
         BE    UREC                    UR DD-EINTRAG
         CLC   0(2,R7),=X'0808'
         BE    UREC                    UR DD-EINTRAG
         CLC   0(2,R7),=X'0810'
         BE    UREC                    UR DD-EINTRAG
         B     UREC1
HASP     AP    SYSI,DW1+4(4)           HASP SYSIN/SYSOUT
         B     DD2
DISK     AP    EAPL,DW1+4(4)           DISK E/A
         B     DD2
TAPE     AP    EABD,DW1+4(4)           BAND E/A
         AP    DW1+4(4),=X'0F'         BEI E/A = NULL
         BZ    TP1                      KEINE RUESTKOSTEN BAND
         AP    BDRU,=X'1F'             BAND GERUESTET
TP1      LA    R8,BTAB
TP2      CLC   0(2,R8),BTXX            ROUTINE ZUR ERMITTLUNG
         BE    DD2                     DER PRIVATEN BANDEINHEITEN
         CLC   0(2,R8),2(R7)
         BE    DD2
         CLC   0(2,R8),=X'0000'
         BNE   TP3
         MVC   0(2,R8),2(R7)
         AP    BDPI,=X'1F'             BAND PRIVAT
         B     DD2
TP3      LA    R8,2(R8)
         B     TP2
UREC     AP    URPR,=X'1F'             UR PRIVAT+GERUESTET
UREC1    AP    EAUR,DW1+4(4)           UR E/A
DD2      LA    R7,8(R7)
         CR    R6,R7
         BH    DD1                     NAECHSTER DD-EINTRAG
DD3      EQU   *                       DD-EINTRAEGE ABGEARBEITET
         LA    R6,102(R3)
         AH    R6,102(R3)
         XC    FW1,FW1
         MVC   FW1+1(3),1(R6)
         L     R7,FW1
         CVD   R7,DW1
         ZAP   CPUS,DW1+4(4)           CPU SEKUNDEN
         XC    CODE,CODE               CODE STEP-BEWERTUNG
         B     BEWERT
         EJECT
*
******** AUSGANGSDATEN FUER JOB-BEWERTUNG ERMITTELN *******************
*
DJOB     EQU   *
         MVC   PYGE,=C'000'
         CLI   130(R3),C'P'
         BNE   PR4                     PRTY NICHT ANGEGEBEN
         CLI   131(R3),C'0'
         BE    PR1
         CLI   131(R3),C'1'
         BE    PR2
         B     PR4                     PRTY FEHLERHAFT
PR1      CLI   132(R3),C'0'
         BL    PR4                     PRTY FEHLERHAFT
         CLI   132(R3),C'9'
         BH    PR4                     PRTY FEHLERHAFT
         B     PR3
PR2      CLI   132(R3),C'0'
         BNE   PR4                     PRTY FEHLERHAFT
PR3      MVC   PYGE+1(2),131(R3)       PRTY GEM. ANGABE
PR4      EQU   *                       PRTY = 0
         MVC   ARBSCHL,127(R3)         ARBEITSSCHLUESSEL
         L     R6,0(R2)
         ZAP   STEGDM,24(4,R6)         KOSTEN DER STEPS
         MVC   FW1,54(R3)
         L     R8,FW1
         CVD   R8,DW1
         ZAP   RENDE,DW1+3(5)          ZEIT READER ENDE
         MVC   FW1,39(R3)
         L     R8,FW1
         CVD   R8,DW1
         ZAP   JSTART,DW1+3(5)         ZEIT JOB START
         MVC   FW1,2(R3)
         L     R8,FW1
         CVD   R8,DW1
         ZAP   JENDE,DW1+3(5)          ZEIT JOB ENDE
         LA    R6,4
         ST    R6,CODE                 CODE JOB-BEWERTUNG
         B     BEWERT
         EJECT
*
******** BEWERTUNG, AUFRUF VON EDVBWRT MIT BALR ***********************
*
BEWERT   EQU   *
         LA    R6,CODE
         LA    R7,PROG
         STM   R6,R7,ADRP              ADR. POINTER EDVBWRT
         LA    R1,ADRP                  IN REG. 1
         L     R15,VEDVBWRT            ENTRY EDVBWRT
         BALR  R14,R15
         L     R6,CODE
         LTR   R6,R6
         BZ    KSTEP                   STEP KASSENZETTEL (BATCH/TSO)
         B     KJOB                    JOB KASSENZETTEL
*
VEDVBWRT DC    V(EDVBWRT)
         EJECT
*
******** KASSENZETTEL STEP AUSGEBEN ***********************************
*
KSTEP    EQU   *
         CLI   1(R3),X'22'
         BE    KTSO                    TSO KASSENZETTEL
         L     R6,0(R2)
         CLI   38(R3),X'01'
         BE    ST1                     1. STEP EINES JOB
         CLC   24(4,R6),=X'40404040'   WARM-START / RESTART DURCH
         BNE   ST2                      HASP NICHT BEI STEP 1
         MVI   23(R6),X'F1'             JA / HINWEIS BEI JOB-KASSENZ.
ST1      ZAP   24(4,R6),=X'0F'         LOESCHEN SPEICHER JOB-KOSTEN
ST2      AP    24(4,R6),STEPDM         STEP-KOSTEN NACH JOB-KOSTEN
         BAL   R9,LOES
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(3,R4),=C'JOB'
         MVC   13(8,R4),14(R3)         JOB NAME
         MVC   25(5,R4),=C'DATUM'
         MVC   FW1,6(R3)
         LA    R12,38(R4)
         BAL   R9,DATUM1               DATUM TT/MM/JJ
         MVC   50(10,R4),=C'ERRECHNETE'
         MVC   78(8,R4),=C'CPU-ZEIT'
         ZAP   RF1,CPUS
         LA    R12,89(R4)
         MVI   SCH1,X'F0'
         BAL   R9,ZEIT1                CPU ZEIT HH.MM.SS,SS
         MVC   103(14,R4),MA1
         ED    107(10,R4),CPUDM        CPU DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(4,R4),=C'STEP'
         MVC   13(8,R4),62(R3)         STEP NAME
         MVC   FW1,6(R3)
         LA    R12,38(R4)
         BAL   R9,DATUM2               DATUM (JJ/TTT)
         MVC   52(8,R4),=C'LAUFZEIT'
         ZAP   RF1,LAUS
         LA    R12,63(R4)
         MVI   SCH1,X'F0'
         BAL   R9,ZEIT1                ERRECHNETE LAUFZEIT HH.MM.SS,SS
         MVC   78(6,R4),=C'SYSTEM'
         MVC   103(14,R4),MA1
         ED    107(10,R4),SYSDM        SYSTEM DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(7,R4),=C'STEP-NR'
         XR    R6,R6
         IC    R6,38(R3)
         CVD   R6,DW1
         MVC   17(4,R4),MA2
         ED    17(4,R4),DW1+6          STEP NR.
         MVC   25(10,R4),=C'STEP-START'
         MVC   FW1,39(R3)
         LA    R12,38(R4)
         MVI   SCH1,X'F1'
         BAL   R9,ZEIT                 STEP START HH.MM.SS
         MVC   50(4,R4),=C'STEP'
         MVC   91(6,R4),MA3
         ED    91(6,R4),REGG           REGION GEFORD. XXXX KB
         MVC   78(14,R4),=C'REGION GEFORD.'
         MVC   98(2,R4),=C'KB'
         MVC   103(14,R4),MA1
         ED    107(10,R4),REGDM        REGION DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(8,R4),=C'PROGRAMM'
         MVC   13(8,R4),54(R3)         PROGRAMM NAME
         MVC   25(9,R4),=C'STEP-ENDE'
         MVC   FW1,2(R3)
         LA    R12,38(R4)
         MVI   SCH1,X'F1'
         BAL   R9,ZEIT                 STEP ENDE HH.MM.SS
         MVC   52(8,R4),=C'LAUFZEIT'
         MVC   FW1,2(R3)
         L     R6,FW1
         MVC   FW1,39(R3)
         L     R7,FW1
         CR    R6,R7
         BNL   SL1
         A     R6,=F'8640000'
SL1      SR    R6,R7
         ST    R6,FW1
         LA    R12,63(R4)
         MVI   SCH1,X'F0'
         BAL   R9,ZEIT                 STEP LAUFZEIT HH.MM.SS,SS
         MVC   91(6,R4),MA3
         ED    91(6,R4),REGN           REGION GENUTZT XXXX KB
         MVC   78(14,R4),=C'REGION GENUTZT'
         MVC   98(2,R4),=C'KB'
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(7,R4),=C'VERR-NR'
         L     R6,16(R2)
         MVC   13(8,R4),1(R6)          VERR-NR XXXXXXXX
         MVC   25(10,R4),=C'COMPL-CODE'
         XC    FW1,FW1
         MVC   FW1(2),51(R3)
         L     R7,FW1
         TM    51(R3),X'80'
         BO    CC1                     USER CODE
         TM    83(R3),X'02'
         BO    CC3                     ABEND
         B     CC2
CC1      MVC   36(4,R4),=C'USER'
         SLL   R7,4
         SRL   R7,4
CC2      SRL   R7,16
         CVD   R7,DW1
         MVC   40(6,R4),MA3
         ED    40(6,R4),DW1+5          NORMAL + USER COMPL-CODE
         B     CC4
CC3      MVC   36(5,R4),=C'ABEND'
         SLL   R7,4
         XR    R6,R6
         SLDL  R6,4
         IC    R8,HEXA(R6)
         STC   R8,43(R4)               1. STELLE ABEND
         XR    R6,R6
         SLDL  R6,4
         IC    R8,HEXA(R6)
         STC   R8,44(R4)               2. STELLE ABEND
         XR    R6,R6
         SLDL  R6,4
         IC    R8,HEXA(R6)
         STC   R8,45(R4)               3. STELLE ABEND
CC4      EQU   *
         MVC   50(15,R4),=C'DISP-PRIORITAET'
         XR    R6,R6
         IC    R6,53(R3)
         CVD   R6,DW1
         MVC   70(4,R4),MA2
         ED    70(4,R4),DW1+6          DISP-PRIORITAET
         MVC   78(14,R4),=C'LIZENZPROGRAMM'
         MVC   103(14,R4),MA1
         ED    107(10,R4),LIZDM        LIZENZ DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   78(15,R4),=C'STEP FIX-KOSTEN'
         MVC   103(14,R4),MA1
         ED    107(10,R4),STXDM        STEP FIX DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         XR    R6,R6
         LH    R6,102(R3)              LAENGE DD-EINTRAEGE + 2
         BCTR  R6,0
         BCTR  R6,0                    - 2
         SRL   R6,3                    / 8 = ZAHL DER DD-EINTRAEGE
         MVI   SCH3,X'F0'
         CLC   47(4,R3),=X'00000000'
         BE    EA1
         MVI   SCH3,X'F1'              OS-SYSIN VORHANDEN
         B     EA1A
EA1      LTR   R6,R6
         BZ    EA4                     KEINE DD-EINTRAEGE
EA1A     LA    R7,104(R3)
         MVI   3(R4),C'-'
         MVC   4(59,R4),3(R4)
         MVC   24(18,R4),=C'  E/A  EINHEITEN  '
         MVC   69(8,R4),MA5
         ED    69(8,R4),EAPL           E/A DISK
         MVC   78(20,R4),=C'DISK E/A OPERATIONEN'
         MVC   103(14,R4),MA1
         ED    107(10,R4),PLEADM       E/A DISK DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(21,R4),=C'TYPE  ADR  E/A-OP.   '
         MVC   24(42,R4),3(R4)
         MVC   73(4,R4),MA2
         ED    73(4,R4),PLPR           DISK PRIVAT
         MVC   78(11,R4),=C'DISK PRIVAT'
         MVC   103(14,R4),MA1
         ED    107(10,R4),PLPRDM       DISK PRIVAT DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,EADRU
         MVC   73(4,R4),MA2
         ED    73(4,R4),PLPR           DISK GERUESTET
         MVC   78(16,R4),=C'DISK RUESTKOSTEN'
         MVC   103(14,R4),MA1
         ED    107(10,R4),PLRUDM       DISK GERUESTET DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,EADRU
         MVC   69(8,R4),MA5
         ED    69(8,R4),EABD           E/A TAPE
         MVC   78(20,R4),=C'TAPE E/A OPERATIONEN'
         MVC   103(14,R4),MA1
         ED    107(10,R4),BDEADM       E/A TAPE DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,EADRU
         MVC   73(4,R4),MA2
         ED    73(4,R4),BDPI           TAPE PRIVAT
         MVC   78(11,R4),=C'TAPE PRIVAT'
         MVC   103(14,R4),MA1
         ED    107(10,R4),BDPRDM       TAPE PRIVAT DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,EADRU
         MVC   73(4,R4),MA2
         ED    73(4,R4),BDRU           TAPE GERUESTET
         MVC   78(16,R4),=C'TAPE RUESTKOSTEN'
         MVC   103(14,R4),MA1
         ED    107(10,R4),BDRUDM       TAPE GERUESTET DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,EADRU
         MVC   69(8,R4),MA5
         ED    69(8,R4),EAUR           E/A UR
         MVC   78(18,R4),=C'UR E/A OPERATIONEN'
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,EADRU
         MVC   73(4,R4),MA2
         ED    73(4,R4),URPR           UR PRIVAT
         MVC   78(9,R4),=C'UR PRIVAT'
         MVC   103(14,R4),MA1
         ED    107(10,R4),URPRDM       UR PRIVAT DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,EADRU
         MVC   73(4,R4),MA2
         ED    73(4,R4),URPR           UR GERUESTET
         MVC   78(14,R4),=C'UR RUESTKOSTEN'
         MVC   103(14,R4),MA1
         ED    107(10,R4),URRUDM       UR GERUESTET DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,EADRU
         MVC   69(8,R4),MA5
         ED    69(8,R4),SYSI           SYSIN/SYSOUT
         MVC   78(12,R4),=C'SYSIN/SYSOUT'
         MVC   103(14,R4),MA1
         ED    107(10,R4),SYSIDM       SYSIN/SYSOUT DM
EA2      BAL   R9,ZEILE
         BAL   R9,LOEB
         LTR   R6,R6
         BNZ   EA3
         CLI   SCH3,X'F1'
         BNE   EA4
EA3      BAL   R9,EADRU                DD-EINTRAEGE SIND NOCH
         B     EA2                      ZU DRUCKEN
EA4      EQU   *
         BAL   R9,ZEILE
         MVC   3(34,R4),=C'MEHRMALS GEDRUCKTE SYSOUT-DATEIEN '
         MVC   37(31,R4),=C'WERDEN AM MONATSENDE VERRECHNET'
         MVC   78(15,R4),=C'KOSTEN DES STEP'
         MVC   103(14,R4),MA1
         ED    107(10,R4),STEPDM       KOSTEN DES STEP
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,ZEILE
         BAL   R9,LOES
         BAL   R9,ZEILE
         B     ENDE
         EJECT
*
******** KASSENZETTEL JOB AUSGEBEN ************************************
*
KJOB     EQU   *
         BAL   R9,LOES
         BAL   R9,ZEILE
         BAL   R9,LOEB
         BAL   R9,ZEILE
         MVC   3(3,R4),=C'JOB'
         MVC   13(8,R4),14(R3)         JOB-NAME
         MVC   25(5,R4),=C'DATUM'
         MVC   FW1,6(R3)
         LA    R12,38(R4)
         BAL   R9,DATUM1               DATUM TT/MM/JJ
         MVC   50(8,R4),=C'CPU-ZEIT'
         XC    FW1,FW1
         MVC   FW1+1(3),113(R3)
         LA    R12,63(R4)
         MVI   SCH1,X'F0'
         BAL   R9,ZEIT                 CPU-ZEIT HH.MM.SS,SS
         MVC   78(16,R4),=C'KOSTEN DER STEPS'
         MVC   103(14,R4),MA1
         ED    107(10,R4),STEGDM       KOSTEN DER STEPS
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(9,R4),=C'STEP-ZAHL'
         XR    R6,R6
         IC    R6,38(R3)
         CVD   R6,DW1
         MVC   17(4,R4),MA2
         ED    17(4,R4),DW1+6          STEP-ZAHL XXX
         MVC   FW1,6(R3)
         LA    R12,38(R4)
         BAL   R9,DATUM2               DATUM (JJ/TTT)
         MVC   50(10,R4),=C'QUEUE-ZEIT'
         MVC   FW1,39(R3)
         L     R6,FW1
         MVC   FW1,54(R3)
         L     R7,FW1
         CR    R6,R7
         BNL   QU1
         A     R6,=F'8640000'
QU1      SR    R6,R7
         ST    R6,FW1
         LA    R12,63(R4)
         MVI   SCH1,X'F0'
         BAL   R9,ZEIT                 QUEUE-ZEIT HH.MM.SS,SS
         MVC   80(12,R4),=C'PRTY GEFORD.'
         MVC   94(2,R4),PYGE+1         PRTY GEFORDERT XXX
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(7,R4),=C'VERR-NR'
         MVC   13(8,R4),118(R3)        VERR-NR XXXXXXXX
         MVC   25(9,R4),=C'JOB-START'
         MVC   FW1,39(R3)
         LA    R12,38(R4)
         MVI   SCH1,X'F1'
         BAL   R9,ZEIT                 JOB-START HH.MM.SS
         MVC   50(3,R4),=C'JOB'
         MVC   80(12,R4),=C'PRTY ERHALT.'
         MVC   94(2,R4),PYER+1         PRTY ERHALTEN XXX
         MVC   103(14,R4),MA1
         ED    107(10,R4),PRTYDM       PRTY DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   25(8,R4),=C'JOB-ENDE'
         MVC   FW1,2(R3)
         LA    R12,38(R4)
         MVI   SCH1,X'F1'
         BAL   R9,ZEIT                 JOB-ENDE HH.MM.SS
         MVC   52(8,R4),=C'LAUFZEIT'
         MVC   FW1,2(R3)
         L     R6,FW1
         MVC   FW1,39(R3)
         L     R7,FW1
         CR    R6,R7
         BNL   JL1
         A     R6,=F'8640000'
JL1      SR    R6,R7
         ST    R6,FW1
         LA    R12,63(R4)
         MVI   SCH1,X'F0'
         BAL   R9,ZEIT                 JOB LAUFZEIT HH.MM.SS,SS
         MVC   80(10,R4),=C'AV SERVICE'
         MVC   103(14,R4),MA1
         ED    107(10,R4),AVSEDM       AV SERVICE DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(12,R4),=C'AUFTRAGGEBER'
         MVC   17(20,R4),93(R3)        AUFTRAGGEBER
         BAL   R9,ZEILE
         BAL   R9,LOEB
         MVC   3(22,R4),=C'BBC - ZOD/R  MANNHEIM,'
         MVC   27(13,R4),=C'IBM /370-158,'
         MVC   42(25,R4),=C'OS-VS2     REL.  1.7/2   '
         MVC   78(14,R4),=C'KOSTEN DES JOB'
         MVC   103(14,R4),MA1
         ED    107(10,R4),JOBDM        KOSTEN DES JOB DM
         BAL   R9,ZEILE
         BAL   R9,LOEB
         L     R6,0(R2)
         CLI   23(R6),X'F1'
         BNE   NT1
         MVC   37(4,R4),=C'****'
         MVC   42(26,R4),=C'DURCH SYSTEM-RESTART EVTL.'
         MVC   69(18,R4),=C'FALSCHE JOB-KOSTEN'
         MVC   88(4,R4),37(R4)
         BAL   R9,ZEILE
         BAL   R9,LOEB
NT1      BAL   R9,ZEILE
         BAL   R9,LOES
         BAL   R9,ZEILE
         B     ENDE
         EJECT
*
******** KASSENZETTEL TSO AUSGEBEN ************************************
*
KTSO     EQU   *
*              FESTSTELLEN AUF WELCHES TERMINAL DER
*              KASSENZETTEL AUSGEGEBEN WERDEN SOLL
         L     R8,16                   ADR CVT NACH R8
         L     R8,228(R8)              ADR TSOCVT NACH R8
         XR    R6,R6
         XR    R7,R7
         LH    R6,30(R8)               MAXIMALE BENUTZER-ZAHL
         LH    R7,26(R8)               GROESSE DES TBJ
         L     R8,0(R8)                ADR TBJ-TABELLE NACH R8
KTSO1    AR    R8,R7
         CLC   28(8,R8),14(R3)         USER-ID TBJ MIT USER-ID
*                                       SMF-SATZ VERGLEICHEN
         BE    KTSO2                   GEFUNDEN
         BCT   R6,KTSO1
         B     KTSO4                   NICHT GEFUNDEN
*                                       KEIN KASSENZETTEL
KTSO2    LA    R8,42(R8)               TERMINAL-ID NACH R8
*
*
         MVI   0(R4),C' '
         MVC   1(69,R4),0(R4)
         MVC   0(6,R4),=C'S M F:'
         MVC   11(8,R4),=C'CPU-ZEIT'
         MVI   SCH1,X'F1'
         ZAP   RF1,CPUS
         LA    R12,20(R4)
         BAL   R9,ZEIT1                CPU-ZEIT HH.MM.SS
         MVC   30(8,R4),=C'LAUFZEIT'
         MVC   FW1,39(R3)
         LA    R12,39(R4)
         BAL   R9,ZEIT                 LAUFZEIT HH.MM.SS
         MVC   49(12,R4),=C'ANSCHALTZEIT'
         MVC   FW1,2(R3)
         L     R6,FW1
         MVC   FW1,22(R3)
         L     R7,FW1
         CR    R6,R7
         BNL   KTSO3
         A     R6,=F'8640000'
KTSO3    SR    R6,R7
         ST    R6,FW1
         LA    R12,62(R4)
         BAL   R9,ZEIT                 ANSCHALTZEIT HH.MM.SS
         TPUT  (R4),70,TJIDLOC=(R8)
         MVI   0(R4),C' '
         MVC   1(69,R4),0(R4)
         MVC   0(7,R4),=C'VERR-NR'
         L     R6,16(R2)
         MVC   8(8,R4),1(R6)           VERR-NR XXXXXXXX
         MVC   18(36,R4),=C'KOSTEN (OHNE E/A AUF FREIG. DATEIEN)'
         MVC   56(14,R4),MA1
         ED    60(10,R4),STEPDM        KOSTEN DM
         TPUT  (R4),70,TJIDLOC=(R8)
KTSO4    B     ENDE
         EJECT
*
******** ENDROUTINE ***************************************************
*
ENDE     EQU   *
         LR    R1,R13
         L     R13,4(R13)
         FREEMAIN R,LV=586,A=(1)
         LM    R14,R12,12(R13)
         XR    R1,R1
         XR    R15,R15
         BR    R14
         EJECT
*
******** LOESCHEN ZEILE MIT STERN *************************************
*
LOES     MVI   0(R4),C'*'
         MVC   1(119,R4),0(R4)
         BR    R9
*
******** LOESCHEN ZEILE MIT BLANK *************************************
*
LOEB     MVI   0(R4),C' '
         MVC   1(119,R4),0(R4)
         MVI   0(R4),C'*'
         MVI   119(R4),C'*'
         BR    R9
*
******** UEBERGEBEN ZEILE AN SYSPRINT *********************************
*
ZEILE    EQU   *
         MVC   LANG,=X'0078'           LAENGE ZEILE = 120 BYTES
         STM   R12,R13,SVR12           SAVE REG. 12/13
         L     R12,68(R13)             REG. 12 AUF URSPRUNGSWERT
         L     R13,SVIEFYS             REG. 13 SAVE AREA IEFYS
         ST    R4,FW1                  ADR. ZEILE
         MVC   36(4,R12),FW1                NACH SYSPRINT
         MVC   42(2,R12),LANG          LAENGE ZEILE NACH SYSPRINT
         L     R15,VIEFYS              SPRUNG
         BALR  R14,R15                      NACH IEFYS
         LM    R12,R13,SVR12
         BR    R9
*
VIEFYS   DC    V(IEFYS)
*
******** DATUM JJTTT UMWANDELN IN TT/MM/JJ ****************************
*
DATUM1   L     R6,FW1                  00JJTTTV
         SRL   R6,12(0)                00000JJT
         LA    R1,15
         OR    R6,R1                   00000JJV
         ST    R6,FW2
         UNPK  6(2,R12),FW2+2(2)       JAHR
         OI    7(R12),X'F0'                 AUFBEREITEN
         MVC   HW1,=X'365F'            TAGE KEIN SCHALTJAHR
         DP    FW2,=X'4F'              PRUEFUNG AUF
         CLI   FW2+3,X'0C'                  SCHALTJAHR
         BNE   DAT11                        NEIN
         MVI   MTAB+3,X'9F'                 JA
         MVI   HW1+1,X'6F'             TAGE SCHALTJAHR
DAT11    ZAP   HW2,=X'1F'
         LA    R6,MTAB
         CP    FW1+2(2),HW1            TAG IM JAHR GR. 365 BZW. 366
         BNH   DAT12                        NEIN
         MVC   FW1+2(2),HW1                 JA, 365 BZW. 366 T. NEHMEN
DAT12    CP    FW1+2(2),0(2,R6)        UMRECHNEN
         BNH   DAT13                        LFD. TAG
         AP    HW2,=X'1F'                   IM JAHR
         SP    FW1+2(2),0(2,R6)             IN
         LA    R6,2(R6)                     TAG
         B     DAT12                        MONAT
DAT13    UNPK  3(2,R12),HW2            MONAT
         OI    4(R12),X'F0'                 AUFBEREITEN
         UNPK  0(2,R12),FW1+2(2)       TAG
         OI    1(R12),X'F0'                 AUFBERETEN
         MVI   2(R12),C'/'
         MVI   5(R12),C'/'
         BR    R9
*
MTAB     DC    X'031F'                 TAGE JANUAR
         DC    X'028F'                      FEBRUAR
         DC    X'031F'                      MAERZ
         DC    X'030F'                      APRIL
         DC    X'031F'                      MAI
         DC    X'030F'                      JUNI
         DC    X'031F'                      JULI
         DC    X'031F'                      AUGUST
         DC    X'030F'                      SEPTEMBER
         DC    X'031F'                      OKTOBER
         DC    X'030F'                      NOVEMBER
         DC    X'031F'                      DEZEMBER
*
******** ZEIT SMF UMWANDELN IN STD/MIN/SEK ****************************
*
ZEIT     L     R6,FW1
         CVD   R6,DW1
         ZAP   RF1,DW1
ZEIT1    DP    RF1,=X'0360000F'        RECHNEN
         UNPK  0(2,R12),RF1+6(2)            STUNDEN
         OI    1(R12),X'F0'
         XC    RF1(8),RF1
         DP    RF1,=X'0006000F'        RECHNEN
         UNPK  3(2,R12),RF1+6(2)            MINUTEN
         OI    4(R12),X'F0'
         XC    RF1(8),RF1
         DP    RF1,=X'0000100F'        RECHNEN
         UNPK  6(2,R12),RF1+6(2)            SEKUNDEN
         OI    7(R12),X'F0'
         MVI   2(R12),C'.'
         MVI   5(R12),C'.'
         CLI   SCH1,X'F1'
         BE    0(R9)                   KEINE H.-SEKUNDEN DRUCKEN
         UNPK  9(2,R12),RF1+10(2)      HUNDERTSTEL
         OI    10(R12),X'F0'                SEKUNDEN
         MVI   8(R12),C','
         BR    R9
*
******** DATUM JJTTT UMWANDELN IN (JJ/TTT) ****************************
*
DATUM2   UNPK  2(5,R12),FW1+1(3)
         OI    6(R12),X'F0'
         MVC   1(2,R12),2(R12)
         MVI   0(R12),C'('
         MVI   3(R12),C'/'
         MVI   7(R12),C')'
         BR    R9
*
******** DRUCKEN E/A EINHEITEN ****************************************
*
EADRU    EQU   *
         LA    R12,3(R4)
         ZAP   HW1,=X'3F'
         LTR   R6,R6
         BNZ   ED1
         CLI   SCH3,X'F1'
         BE    ED5
         B     0(R9)
ED1      CLC   0(2,R7),=X'0805'        INTERNER READER
         BE    ED1A                     NICHT BERECHNEN
         CLC   0(2,R7),=X'0000'
         BNE   ED3
ED1A     BCT   R6,ED2                  DUMMY DD-EINTRAG, INT. READER
         B     ED4
ED2      LA    R7,8(R7)
         B     ED1
ED3      MVC   FW2,4(R7)
         L     R8,FW2
         CVD   R8,DW1
         CLC   2(2,R7),=X'0599'
         BH    EDHASP
         CLC   0(2,R7),=X'2009'
         BE    EDDISK
         CLC   0(2,R7),=X'200D'
         BE    EDDISK
         CLC   0(2,R7),=X'8003'
         BE    EDTAPE
         CLC   0(2,R7),=X'0801'
         BE    EDREAD
         CLC   0(2,R7),=X'0802'
         BE    EDPNCH
         CLC   0(2,R7),=X'0808'
         BE    EDPRNT
         CLC   0(2,R7),=X'0810'
         BE    EDPAPT
         B     EDEND
EDHASP   CLC   0(2,R7),=X'0801'
         BE    EDSYSI
         B     EDSYSO
EDDISK   MVC   0(4,R12),=C'DISK'
         B     EDEND
EDTAPE   MVC   0(4,R12),=C'TAPE'
         B     EDEND
EDREAD   MVC   0(4,R12),=C'READ'
         B     EDEND
EDPNCH   MVC   0(4,R12),=C'PNCH'
         B     EDEND
EDPRNT   MVC   0(4,R12),=C'PRNT'
         B     EDEND
EDPAPT   MVC   0(4,R12),=C'PAPT'
         B     EDEND
EDSYSI   MVC   0(4,R12),=C'SYSI'
         B     EDEND
EDSYSO   MVC   0(4,R12),=C'SYSO'
EDEND    IC    R1,2(R7)
         SLL   R1,28
         SRL   R1,28
         IC    R8,HEXA(R1)
         STC   R8,6(R12)               1. STELLE E/A ADR
         IC    R1,3(R7)
         SRL   R1,4
         IC    R8,HEXA(R1)
         STC   R8,7(R12)               2. STELLE E/A ADR
         IC    R1,3(R7)
         SLL   R1,28
         SRL   R1,28
         IC    R8,HEXA(R1)
         STC   R8,8(R12)               3. STELLE E/A ADR
         MVC   10(8,R12),MA4
         ED    10(8,R12),DW1+4         E/A OPERATIONEN
         MVI   10(R12),X'40'
         BCTR  R6,0
         LA    R7,8(R7)
         SP    HW1,=X'1F'
         BZ    0(R9)
         LA    R12,21(R12)
         LTR   R6,R6
         BZ    ED4
         B     ED1
ED4      CLI   SCH3,X'F1'
         BNE   0(R9)
ED5      MVI   SCH3,X'F0'
         MVC   0(4,R12),=C'SYSI'
         MVC   6(2,R12),=C'OS'
         MVC   FW2,47(R3)
         L     R8,FW2
         CVD   R8,DW1
         MVC   10(8,R12),MA4
         ED    10(8,R12),DW1+4         OS SYSIN
         MVI   10(R12),X'40'
         B     0(R9)
         EJECT
*
******** MASKEN *******************************************************
*
MA1      DC    X'7E40C4D4402020402021206B2020'
MA2      DC    X'40202120'
MA3      DC    X'402020202120'
MA4      DC    X'4B20202020202120'
MA5      DC    X'4020202020202120'
HEXA     DC    C'0123456789ABCDEF'     HALBBYTE DRUCKEN
         EJECT
*
******** LITERALE *****************************************************
*
         LTORG
         EJECT
*
******** DSECT FUER ARBEITSBEREICHE / SPEICHER (206 BYTES) ************
*
ABER     DSECT
SVIEFYS  DS    F             4 BYTES   ADR. SAVE-AREA IEFYS
SVR12    DS    F             4 BYTES   SAVE R12 BEI ANSPRUNG IEFYS
SVR13    DS    F             4 BYTES   SAVE R13 BEI ANSPRUNG IEFYS
HW1      DS    CL2           2 BYTES   ARBEITSSPEICHER
HW2      DS    CL2           2 BYTES   ARBEITSSPEICHER
FW1      DS    CL4           4 BYTES   ARBEITSSPEICHER
FW2      DS    CL4           4 BYTES   ARBEITSSPEICHER
DW1      DS    CL8           8 BYTES   ARBEITSSPEICHER
ADRP     DS    2F            8 BYTES   ADR.-POINTER EDVBWRT
CODE     DS    F             4 BYTES   CODE FUER EDVBWRT
BTAB     DS    CL40         40 BYTES   ELIMINIEREN DOPPELTE
BTXX     DS    CL2           2 BYTES        BANDEINHEITEN PRO STEP
SCH1     DS    CL1           1 BYTE    SCHALTER 0 / 1, WEICHE UNTERROUT
SCH2     DS    CL1           1 BYTE    SCHALTER 1=BATCH, 2=RJE, 3=TSO
SCH3     DS    CL1           1 BYTE    SCHALTER 0 = OS-SYSIN N. DRUCK.
*                                               1 = OS-SYSIN DRUCKEN
LANG     DS    CL2           2 BYTES   LAENGE FUER IEFYS
*
******** WERTBEREICHE
*                       BATCH/TSO STEP                        W E R T
PROG     DS    CL8  PROGRAMM                     8   STELLEN  VORGEGEB.
KENN     DS    CL1  KENNZ. 0=NOT EXECUTED        1   STELLE   VORGEGEB.
*                          1=BATCH 2=RJE
*                          3=TSO
REGG     DS    PL3  REGION GEFORDERT             5   STELLEN  VORGEGEB.
REGN     DS    PL3  REGION GENUTZT               5   STELLEN  VORGEGEB.
PLPR     DS    PL2  PLATTEN PRIVAT+GERUESTET     3   STELLEN  VORGEGEB.
BDPI     DS    PL2  BAENDER PRIVAT               3   STELLEN  VORGEGEB.
BDRU     DS    PL2  BAENDER GERUESTET            3   STELLEN  VORGEGEB.
URPR     DS    PL2  UR PRIVAT+GERUESTET          3   STELLEN  VORGEGEB.
EAPL     DS    PL4  E/A OP PLATTE                7   STELLEN  VORGEGEB.
EABD     DS    PL4  E/A OP BAND                  7   STELLEN  VORGEGEB.
EAUR     DS    PL4  E/A OP UR                    7   STELLEN  VORGEGEB.
SYSI     DS    PL4  SYSIN+SYSOUT/TGET+TPUT       7   STELLEN  VORGEGEB.
CPUS     DS    PL4  CPU SEKUNDEN                 5,2 STELLEN  VORGEGEB.
LAUS     DS    PL4  ERR. LAUFZEIT SEKUNDEN       5,2 STELLEN  ERRECHN.
CPUDM    DS    PL4  CPU DM                       5,2 STELLEN  ERRECHN.
SYSDM    DS    PL4  SYSTEM DM                    5,2 STELLEN  ERRECHN.
REGDM    DS    PL4  REGION DM                    5,2 STELLEN  ERRECHN.
LIZDM    DS    PL4  LIZENZ DM                    5,2 STELLEN  ERRECHN.
STXDM    DS    PL4  STEPFIXKOSTEN DM             5,2 STELLEN  ERRECHN.
PLEADM   DS    PL4  PLATTEN E/A OP DM            5,2 STELLEN  ERRECHN.
PLPRDM   DS    PL4  PLATTEN PRIVAT DM            5,2 STELLEN  ERRECHN.
PLRUDM   DS    PL4  PLATTEN RUESTKOSTEN DM       5,2 STELLEN  ERRECHN.
BDEADM   DS    PL4  BAND E/A OP DM               5,2 STELLEN  ERRECHN.
BDPRDM   DS    PL4  BAND PRIVAT DM               5,2 STELLEN  ERRECHN.
BDRUDM   DS    PL4  BAND RUESTKOSTEN DM          5,2 STELLEN  ERRECHN.
URPRDM   DS    PL4  UR PRIVAT DM                 5,2 STELLEN  ERRECHN.
URRUDM   DS    PL4  UR RUESTKOSTEN DM            5,2 STELLEN  ERRECHN.
SYSIDM   DS    PL4  SYSIN+SYSOUT/TGET+TPUT DM    5,2 STELLEN  ERRECHN.
TSOZDM   DS    PL4  TSO ZUSCHLAG DM              5,2 STELLEN  ERRECHN.
STEPDM   DS    PL4  STEPKOSTEN DM                5,2 STELLEN  ERRECHN.
*
RF1      DS    CL12         12 BYTES   RECHENFELD
*
*                       BATCH JOB                             W E R T
         ORG   PROG
PYGE     DS    CL3  PRTY GEFORDERT               3   STELLEN  VORGEGEB.
ARBSCHL  DS    CL2  ARBEITSSCHLUESSEL            2   STELLEN  VORGEGEB.
STEGDM   DS    PL4  STEPKOSTEN(JOB) DM           5,2 STELLEN  VORGEGEB.
RENDE    DS    PL5  ZEIT READER ENDE (100/SEK.)  9   STELLEN  VORGEGEB.
JSTART   DS    PL5  ZEIT JOB START   (100/SEK.)  9   STELLEN  VORGEGEB.
JENDE    DS    PL5  ZEIT JOB ENDE    (100/SEK.)  9   STELLEN  VORGEGEB.
PYER     DS    CL3  PRTY ERHALTEN                3   STELLEN  ERRECHN.
PRTYDM   DS    PL4  PRTY DM                      5,2 STELLEN  ERRECHN.
AVSEDM   DS    PL4  AV-SERVICE DM                5,2 STELLEN  ERRECHN.
JOBDM    DS    PL4  JOBKOSTEN DM                 5,2 STELLEN  ERRECHN.
*
*
         END
