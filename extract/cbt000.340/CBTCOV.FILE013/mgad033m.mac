         MACRO
&LAB     DECON &ERROR=,&FILE=
.**********************************************************************
.*                                                                    *
.*       DECON - MACRO                                                *
.*                                                                    *
.*       LOEBER, W.      03.04.1976     GAD - MUENSTER                *
.*                                                                    *
.*             DISCRIPTION OF PARAMETERS                              *
.*                                                                    *
.* R E S T  F O L L O W S                                             *
.**********************************************************************
         LCLC  &C2
         LCLA  &LGM,&IND1
.*
&C2      SETC  'GAD'.'&SYSNDX'
.*
         AIF   (T'&ERROR NE 'O').A1001
.A1000   ANOP
         MNOTE 04,'INCORRECT OPERANDS'
         MEXIT
.*
.A1001   ANOP
         AIF   (T'&FILE EQ 'O').A1000
         AIF   (N'&FILE GT 1 OR N'&ERROR GT 1).A1000
         AIF   (K'&FILE GT 10).A1000
.*
&LGM     SETA  24+4+4+8+10
.*
.************** NOW GENERATION *********************
.B1000   ANOP
.*  GENERATE MACRO-LABEL
         AIF   ('&LAB' EQ '').B1005
&LAB     DS    0H
.*
.B1005   ANOP
.*  G E N E R A T E  INSTRUCTIONS
         GETMAIN R,LV=&LGM
         LR    14,1
         LR    0,1
         LA    14,0(0,14)
         LA    1,&LGM.(,0)
         XR    15,15
         MVCL  0,14
.*
         LA    1,4(,14)
         ST    1,0(,14)
         MVC   4(2,14),=X'1404'
         LA    1,24(,14)
         ST    1,12(,14)
         MVI   0(14),X'80'
*
         LA    1,28(,14)
         ST    1,24(,14)
         MVI   24(14),X'80'
.*
         MVC   28(4,14),=X'00010001'
         AIF   ('&FILE'(1,1) NE '''').B1010
&IND1    SETA  K'&FILE-2
         MVC   32(2,14),=H'&IND1.'
         MVC   32+2(&IND1.,14),=C&FILE
         AGO   .B1015
.*
.B1010   ANOP
         MVC   32(10,14),&FILE
.*
.B1015   ANOP
* SVC 99
         LR    1,14
         SVC   99
* ERROR-CODES
         XC    &ERROR.(4),&ERROR
         LH    1,8(,14)
         STCM  1,3,&ERROR
         LH    1,10(,14)
         STCM  1,3,&ERROR.+2
* FREEMAIN
         LA    1,&LGM.(,0)
         LTR   15,15
         BNZ   &C2.E
         FREEMAIN R,LV=&LGM.,A=(14)
&C2.E    DS    0H
         MEXIT
         MEND
