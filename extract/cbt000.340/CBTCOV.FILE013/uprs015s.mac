*/T0518XMS JOB (S,23556),34.HOFFM,CLASS=Z,MSGCLASS=A,NOTIFY=HOFFM
*/XDAMSGS EXEC AFC
*/SYSIN    DD  *
         TITLE 'INLINE MACROS'
         MACRO
&L       ERRM  &CODE,&TEXT,&MAXL=114
         GBLA  &ERRMGA1
         LCLA  &A1,&A2,&A3
         LCLC  &C1
&A3      SETA  12
         AIF   ('&CODE' EQ 'S').M100
&A3      SETA  8
         AIF   ('&CODE' EQ 'E').M100
&A3      SETA  4
         AIF   ('&CODE' EQ 'W').M100
&A3      SETA  0
         AIF   ('&CODE' EQ 'I').M100
         MNOTE 8,'FALSCHER CODE'
         MEXIT
.M100    ANOP
         AIF   ('&TEXT' NE '').M200
         MNOTE 8,'TEXT FEHLT'
         MEXIT
.M200    ANOP
&A1      SETA  K'&TEXT-2
         AIF   (&A1 LE &MAXL).M300
         MNOTE 8,'TEXT ZU LANG'
         MEXIT
.M300    ANOP
&C1      SETC  '&L'(4,3)
&A2      SETA  &C1
&C1      SETC  '&CODE'.'  '.'&TEXT'(2,&A1)
&A1      SETA  (&A1+4)/2*2
&ERRMGA1 SETA  &ERRMGA1+1
         DC    H'&A1'                       LAENGE VON TEXT
         DC    H'&A2'                       TEXT NUMMER
         DC    H'&A3'                       ERROR-CODE
         DC    CL&A1'&C1'
         MEND
XDAMSGS  TITLE 'XDATA MESSAGES'
XDAMSGS  CSECT
         GBLA  &ERRMGA1
         PRINT NOGEN
         PROST RA,,REG
         SR    R7,R7                      RETURN-CODE
         L     R3,0(,R1)                  FEHLER NUMMER
         LTR   R3,R3
         BM    EXIT
         LA    R4,ERRSTART                START DER TEXTE
         L     R5,ERRANZ                  ANZAHL TEXTE
SEARCH   NULL
         CH    R3,2(,R4)                  MSG NUMMER VERGLEICHEN
         BE    FOUND
         AH    R4,0(,R4)                  LAENGE MSG
         LA    R4,6(,R4)                   + LAENGE FELDER
         BCT   R5,SEARCH
         B     EXIT
FOUND    NULL
         L     R6,4(,R1)                 ADR VON BEREICH
         LH    R2,2(,R4)                 MSG-NUMMER
         CVD   R2,WORK
         MVC   0(3,R6),PREFIX            MSG-PREFIX XDA
         UNPK  3(3,R6),WORK              MSGNUMMER
         OI    5(R6),X'F0'               VORZEICHEN
         LH    R2,0(,R4)
         BCTR  R2,0
         MVC   6(*-*,R6),6(R4)           MSG NACH BEREICH
         EX    R2,*-6
         LH    R7,4(,R4)                 RETURN-CODE
         LTR   R6,R6
         BM    EXIT
         LA    R1,4(,R1)
         L     RF,MSGEDIT
         BALR  RE,RF
EXIT     NULL
         PEND  RC=(R7)
         EJECT
***********************************************************************
**       DEFINITIONEN                                                **
***********************************************************************
         SPACE
WORK     DC    D'0'
MSGEDIT  DC    V(XDAMEDT)
PREFIX   DC    CL3'XDA'
***********************************************************************
**       FEHLER MELDUNGEN                                            **
***********************************************************************
         SPACE
         PRINT GEN
ERRSTART DS    0F
ERR000   ERRM  I,'CODELAENGE = <6> BYTES.   NEW-AREA = <6> BYTES.'
         PRINT NOGEN
ERR010   ERRM  E,'PARM-EINTRAGUNG FALSCH'
ERR020   ERRM  E,'UNGUELTIGE STEUERKARTE'
ERR030   ERRM  E,'AN DIESER STELLE IST <4> NICHT GUELTIG'
ERR040   ERRM  E,'LETZTES <4> STATEMENT NICHT ABGESCHLOSSEN'
ERR050   ERRM  E,'INTERNER SPEICHERPLATZ VOLL. FORTSETZUNG MIT NEUEM -P+
               ROC STATEMENT'
ERR060   ERRM  S,'<40>'
ERR070   ERRM  E,'DATENFEHLER IN STATEMENT <10>. INTERRUPT-ADRESSE=<4> +
               (<6>)'
ERR080   ERRM  W,'SATZ KANN NICHT EINGEFUEGT WERDEN. <8> DD-STATEMENT F+
               EHLT'
ERR090   ERRM  E,'NEW-BEREICH NICHT ANGELEGT. INSERT ODER REPLACE NICHT+
                MOEGLICH'
ERR100   ERRM  E,'<8> IST EIN UNGUELTIGES SCHLUESSELWORT'
ERR120   ERRM  E,'SATZPOSITION ODER -LAENGE NICHT NUMERISCH'
ERR130   ERRM  E,'SATZPOSITION ODER -LAENGE ZU GROSS ODER NULL'
ERR140   ERRM  E,'<8> IST EIN UNGUELTIGER OPERATOR'
ERR150   ERRM  E,'DATENKONSTANTE ZU LANG'
ERR160   ERRM  E,'ZEICHENKETTE NICHT ABGESCHLOSSEN'
ERR170   ERRM  E,'UNGUELTIGE ZEICHEN IN EINER HEXA-KETTE'
ERR180   ERRM  E,'ANWEISUNGEN NACH EINER VERZWEIGUNG KOENNEN NICHT AUSG+
               EFUEHRT WERDEN'
ERR190   ERRM  E,'DATENTYP ''<1>'' UND ''<1>'' NICHT VERTRAEGLICH'
ERR200   ERRM  E,'UNAUSGEWOGENE KLAMMERNPAARE'
ERR210   ERRM  E,'''<8>'' IST EIN UNGUELTIGER VERKNUEPFUNGSTYP'
ERR220   ERRM  E,'WERT NACH SCHLUESSELWORT NICHT NUMERISCH'
ERR230   ERRM  E,'WERT NACH SCHLUESSELWORT ZU LANG'
ERR240   ERRM  E,'TEXT ''<20>'' ENTHAELT UNGUELTIGE SYNTAX'
ERR250   ERRM  E,'DATENKONSTANTE NICHT NUMERISCH'
ERR260   ERRM  E,'FELDNAME ODER LABEL <20> ZU LANG. MAXIMAL 12 ZEICHEN'
ERR270   ERRM  E,'FELDNAME ODER LABEL <20> SYNTAKTISCH FALSCH'
ERR280   ERRM  E,'FELDNAME ODER LABEL <12> NICHT DEFINIERT'
ERR290   ERRM  E,'FELDNAME ODER LABEL <12> IST SCHON DEFINIERT'
ERR300   ERRM  E,'COPY IN EINEM MEMBER IST NICHT ERLAUBT'
ERR310   ERRM  E,'SYNTAXFEHLER IM COPY STATEMENT'
ERR320   ERRM  E,'SYSLIB DATASET KANN NICHT EROEFFNET WERDEN'
ERR330   ERRM  E,'SYSLIB DATASET ATTRIBUTE SIND FALSCH'
ERR340   ERRM  E,'MEMBER <8> NICHT GEFUNDEN'
ERR350   ERRM  E,'STACK UEBERLAUF. RECHENOPERATIONEN ZU TIEF GESCHACHTE+
               LT'
ERR400   ERRM  W,'DIESE PROCEDURE KANN NICHT AUSGEFUEHRT WERDEN'
ERR410   ERRM  E,'NAME DES UNTERPROGRAMMS ZU LANG'
ERR420   ERRM  E,'MODUL ''<8>'' KANN NICHT GELADEN WERDEN'
ERR430   ERRM  E,'MEHR ALS 32 PARAMETER IM CALL STATEMENT'
ERR440   ERRM  E,'MEHR ALS 8 VERSCHIEDENE UNTERPROGRAMME'
ERR450   ERRM  E,'ZU VIELE LABEL.  LABEL TABELLE VOLL'
ERRANZ   DC    F'&ERRMGA1'
         DS    0F
         END
