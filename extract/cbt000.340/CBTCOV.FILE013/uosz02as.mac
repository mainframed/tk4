./  ADD        NAME=UXXX02AS
MEMBLI   DIESES PROGRAMM DIENT ZUR ALPHABETISCHEN LISTUNG VON BIBLIO=
         THEKEN MIT SATZLAENGE 80. DER INHALT DER EINZELNEN MEMBERS
         WIRD MITTELS DES UTILITIES IEBPTPCH AUF EINEN TEMPORAEREN
         BESTAND ABGESTANZT, ANSCHLIESSEND WERDEN DIE EINZELNEN MEMBERS
         SORTIERT UND ALPHABETISCH AUSGEDRUCKT. AUSSERDEM WIRD DURCH
         DEN SUBTASK MEMBINDX EIN VERZEICHNIS DER MEMBERS, DER UR=
         SPRUNGSBIBLIOTHEK SOWIE DER BLATTNUMMER AUF DER DAS MEMBER
         ZU FINDEN IST, ERSTELLT.
         BENOETIGTE UNTERPROGRAMME:
         IEBPTPCH (UTILITY), AUFGERUFEN DURCH LINK
         ICEMAN (SORT/MERGE), AUFGERUFEN DURCH LINK
         MEMBINDX, AUFGERUFEN DURCH ATTACH
         PARAMETERANGABEN:
         ES KANN ALS PARAMETER ENTWEDER 'PROC' ODER 'PARM' ANGEGEBEN
         WERDEN. DADURCH WIRD BEI DER UEBERSCHRIFT ENTWEDER POCEDUREN=
         LISTUNG UND LISTUNG DER PROCEDUREN ODER PARAMETERLISTUNG UND
         LISTUNG DER PARAMETER GEDRUCKT. WIRD KEIN PARAMETER ANGEGEBEN,
         LAUTET DIE UEBERSCHRIFT LIBRARYLISTUNG UND LISTUNG DER LIBRA=
         RIES.
         BENOETIGTE DD-KARTEN:
         //SYSOUT   DD  SYSOUT=A  FUER DIAGNOSTIKMELDUNGEN
         //PRINT1   DD  SYSOUT=A  FUER DEN INDEX
         //PRINT2   DD  SYSOUT=A  FUER DIE MEMBERLISTUNG
         //DUMMY    DD  DUMMY     FUER SYSPRINT-AUSGABE VON IEBPTPCH
                                 UND ICEMAN (KANN DURCH DIE ANGABE
                                 SYSOUT=A AUCH GEDRUCKT WERDEN)
         //DISKWK01 DD  UNIT=SYSDA,SPACE=(CYL,(5),,CONTIG) FUER SORT=
         //DISKWK02 DD  UNIT=SYSDA,SPACE=(CYL,(5),,CONTIG) WORKBEREICHE
         //DISKWK03 DD  UNIT=SYSDA,SPACE=(CYL,(5),,CONTIG)
         //DISKWK04 DD  UNIT=SYSDA,SPACE=(CYL,(5),,CONTIG)
         //DISKWK05 DD  UNIT=SYSDA,SPACE=(CYL,(5),,CONTIG)
         //SORTLIB  DD  DSN=SYS1.SORTLIB,DISP=SHR  FUER SORTLOADLIBR.
         //SYSUDUMP DD  SYSOUT=A  FUER EVENT.DUMP BEI PROG.ABSTURZ
         //SYS002   DD  DSN=FILE,DISP=(MOD,DELETE),UNIT=SYSDA,
                        SPACE=(CYLM(5,5)
                        FUER IEBPTPCH OUTPUT UND SORT-INPUT
         //CONTROL  DD  DSN=CONTR,DISP=(NEW,DELETE),UNIT=SYSDA,
                        SPACE=(TRK,(1,1))
                        FUER IEBPTPCH-STEUERKARTEN
         //IN1      DD  BIS
         //IN25     DD  FUER ZU LISTENDE BIBLIOTHEKEN

         EJECT
MEMBLIST CSECT
         SAVE  (14,12)
         USING MEMBLIST,15
         ST    13,SAVE+4
         LR    2,13
         LA    13,SAVE
         LA    12,4095(13)
         LA    12,1(12)
         ST    13,8(2)
         B     SKIPSAVE
SAVE     DS    18F
SKIPSAVE EQU   *
         DROP  15
         USING SAVE,13,12
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         B     SKIPCOPY
         DC    C'COPYRIGHT BY HADSCHI HALEF SIMANDL, BAGDAD'
SKIPCOPY EQU   *
         EJECT
         STM   R12,R13,BASREG1   UEBERTRAGEN DER BASISREGISTER IN
         STM   R12,R13,BASREG2   DIE SORT-EXITROUTINEN
         USING IHADCB,R2
         LR    R3,R1
         LA    R2,SYSOUT
         OPEN  (SYSOUT,OUTPUT)   OPEN FUER FEHLERMELDUNGEN USW.
         TM    DCBOFLGS,DCBOFOPN FEHLERROUTINE ZU OPEN SYSOUT
         BO    OPEN1OK
         WTO   'OPEN FOR SYSOUT DD-CARD FAILED',ROUTCDE=11
         MVI   ENDECC16+1,X'00'
         B     ENDECC16
OPEN1OK  EQU   *                ANALYSIEREN DER PARM-INFORMATION
         GETTD ZEIT,DATUM       UND UEBERTRAGEN DER ENTSPRECHENDEN
         L     R3,0(R3)         ZEICHENKETTEN IN DIE TITELZEILEN
         CLC   0(2,R3),=H'4'
         BNE   UNDPARM
PARMOK   EQU   *
         CLC   2(4,R3),=C'PROC'
         BNE   PARMCHEK
         MVC   TITLE1+1(17),=C'PROCEDURENLISTUNG'
         MVC   TITLE2,=C'PROCEDUREN'
         B     CHECKOK
PARMCHEK EQU   *
         CLC   2(4,R3),=C'PARM'
         BE    CHECKOK
UNDPARM  EQU   *
         MVC   TITLE1+1(17),=CL17'LIBRARYLISTUNG'
         MVC   TITLE2,=CL10'LIBRARIES'
CHECKOK  EQU   *
         ATTACH EP=MEMBINDX,PARAM=(SAVER5,BLATT,SYSOUT,TITLE1,ZEIT,    *
               DATUM,ECB1,ECB2,ECB3,ECB4),ECB=ENDECB
         LTR   R15,R15     AUFRUF DES SUBTASKS 'MEMBINDX' MIT UEBERGABE
         BZ    STORETCB    DER PARAMETER UND RETTEN DER ADDRESSE DES
         CVD   R15,D       TCB
         MVC   CCATTACH,=X'40202020'  FEHLERROUTINE ZU ATTACH
         ED    CCATTACH,D+6
         PUT   SYSOUT,MELDNG10
         CLOSE (SYSOUT)
         MVI   ENDECC16+1,X'00'
         B     ENDECC16
STORETCB EQU   *
         ST    R1,TCBADDR
         LA    R3,IN2-8
         LA    R4,25
OPENCTRL LA    R2,CONTROL        OPEN FUER AUSGABE DER IEBPTPCH-STEUER-
         OPEN  (CONTROL,OUTPUT)  KARTEN
         TM    DCBOFLGS,DCBOFOPN FEHLERROUTINE FUER OPEN CONTROL
         BO    OPEN2OK
         PUT   SYSOUT,MELDUNG5
         MVI   ENDECC16+1,X'00'
         B     SORTOK
OPEN2OK  EQU   *
SKIPSW   NOP   SKIPPUT
         PUT   SYSOUT,MELDUNG2    SCHREIBEN EINER UEBERSCHRIFTSZEILE
         MVI   SKIPSW+1,X'F0'
         WAIT  ECB=ECB1           WARTEN AUF ABSCHLUSS DER ANFANGS-
         NI    ECB1,X'3F'         ARBEITEN IM SUBTASK MEMBINDX
         CLI   ECB1+3,X'FF'       FEHLERROUTINE FUER UNKORREKTE
         BNE   SKIPPUT            ANFANGSARBEITEN DES SUBTASK
         MVI   ENDECC16,X'00'
         CLOSE (SYSOUT)
         B     ENDECC16
SKIPPUT  EQU   *
RDJFCB   RDJFCB (IN1)             LESEN DER JCL-INFORMATIONEN FUER
         LTR   R15,R15            DIE INPUT-BIBLIOTHEK
         BZ    RDJFCBOK
         CH    R15,=H'4'          RC4=KEINE DDKARTE VORHANDEN, DAHER
         BE    ENDINP             ENDE
         CVD   R15,D
         MVC   CCRDJFCB,=X'40202020' FEHLERROUTINE FUER RDJFCB
         ED    CCRDJFCB,D+6
         PUT   SYSOUT,MELDUNG3
         B     PTPCHOK
RDJFCBOK EQU   *                        UEBERTRAGEN DES NAMENS DER
         MVI   OKSW+1,X'00'             BIBLIOTHEK ZUR SPAETEREN VER-
         MVC   MELDUNG4+9(44),WORKAREA  WENDUNG ZUM DRUCK UND ALS
         MVC   DSN(30),WORKAREA         SORTIERBEGRIFF
         PUT   SYSOUT,MELDUNG4          AUSGABE DES NAMENS AUF SYSOUT
         PUT   CONTROL,PUNCH            AUSGABE DER IEBPTPCH STEUER-
         PUT   CONTROL,TITLE            KARTEN
         CLOSE (CONTROL)
         LINK  EP=IEBPTPCH,PARAM=(OPTION,DDNAME),VL=1
         LTR   R15,R15     AUFRUF DES UTILITIES IEBPTPCH MIT PARAMETER-
         BZ    PTPCHOK     UEBERGABE
         CVD   R15,D       FEHLERROUTINE FUER IEBPTPCH
         MVC   CCPTPCH,=X'40202020'
         ED    CCPTPCH,D+6
         PUT   SYSOUT,MELDUNG6
PTPCHOK  EQU   *           UEBRTRAGEN DES DDNAMENS DER NAECHSTEN
         LA    R3,8(R3)    BIBLIOTHEK IN DEN DCB
         LA    R2,IN1
         MVC   DCBDDNAM,0(R3)
         MVC   DDN,0(R3)
         BCT   R4,OPENCTRL
ENDINP   CLOSE (CONTROL)
OKSW     B     ENDE
         LA    R1,SORTAD              AUFRUF DES SORTPROGRAMMS
         LINK  EP=ICEMAN,MF=(E,(1))
         LTR   R15,R15
         BZ    SORTOK                 FEHLERROUTINE FUER SORTAUFRUF
         CVD   R15,D
         MVC   CCSORT,=X'40202020'
         ED    CCSORT,D+6
         PUT   SYSOUT,MELDUNG7
         MVI   ENDECC16+1,X'00'
SORTOK   EQU   *
         CLOSE (SYSOUT)
ENDE     EQU   *
         POST  ECB2,255      VERSTAENDIGUNG DES SUBTASKS DASS ARBEIT
         WAIT  ECB=ENDECB    BEENDET UND WARTEN AUF ABSCHLUSSARBEITEN
         DETACH TCBADDR      DES SUBTASKS.
         XR    R15,R15
ENDECC16 B     RETURN
         LA    R15,16
RETURN   L     R13,SAVE+4
         RETURN (14,12),RC=(15)
         EJECT
*************** SORT EXIT E15 **************************************
* DIESE ROUTINE LIEST DIE VON IEBPTPCH ERZEUGTEN SAETZE, VERSIEHT
* SIE MIT DEN NOTWENDIGEN SORTIERBEGRIFFEN UND GIBT SIE AN DEN
* SORT WEITER.
*
********************************************************************
         CNOP  0,4               HERSTELLEN DER BASISREGISTER
MOD1     EQU   *                 UND RETTEN DER REGISTER
         SAVE  (14,12)
         LR    R2,R13
         CNOP  0,4
         LM    R12,R13,16(R15)
         B     SKIPBASR
BASREG1  DS    2F
SKIPBASR EQU   *
         ST    R2,SAVE13
         ST    R13,8(R2)
         MVC   SAVE1(72),SAVE
         ST    R2,SAVE+4
OPENSW   NOP   READ             OPEN FUER INPUT (OUTPUT VON IEBPTPCH)
         OPEN  (SYS002,INPUT)
         LA    R2,SYS002        FEHLERROUTINE
         TM    DCBOFLGS,DCBOFOPN
         BO    OPEN3OK
         PUT   SYSOUT,MELDUNG8
         B     ENDSRTN2
OPEN3OK  OI    OPENSW+1,X'F0'
READ     GET   SYS002,IN         UNTERSUCHEN OB NEUES MEMBER ODER
         CLC   IN+1(4),=C'*#@$'  NEUE BIBLIOTHEK UND SPEICHERN DER
         BNE   NODSN             NAMEN SOWIE SETZEN VON SCHALTERN
         MVC   LIBDSN,IN+5       IM AUSGABESATZ
         MVI   DSNFLGSW+1,X'00'
         MVI   FLGCLSW+1,X'F0'
         B     READ
NODSN    CLC   IN+1(13),=C'MEMBER NAME  '
         BNE   NOMEMB
         XC    NUMBER,NUMBER
         MVC   MEMBNAME,IN+14
         MVI   MEMFLGSW+1,X'00'
         MVI   FLGCLSW+1,X'F0'
         B     READ
NOMEMB   EQU   *
DSNFLGSW B     NODSNFLG
         OI    DSNFLAG,X'F0'
         OI    DSNFLGSW+1,X'F0'
NODSNFLG EQU   *
MEMFLGSW B     NOMEMFLG
         OI    DSNFLAG,X'0F'
         OI    MEMFLGSW+1,X'F0'
NOMEMFLG EQU   *
FLGCLSW  NOP   NOCLEAR
         XC    DSNFLAG,DSNFLAG
NOCLEAR  EQU   *
         MVI   FLGCLSW+1,X'00'
         LH    R2,NUMBER
         LA    R2,1(R2)
         STH   R2,NUMBER
         LA    R15,12          UBERGEBEN DES SATZES AN DEN SORT
         LA    R1,SORTIN
ENDMOD1  EQU   *
         LA    R2,SAVE1
         MVC   SAVE(72),SAVE1
         L     R13,SAVE13
         L     R14,12(,R13)
         L     R0,20(,R13)
         LM    R2,R12,28(R13)
         BR    R14
ENDSRTIN EQU   *
         CLOSE (SYS002)
ENDSRTN2 LA    R15,8
ENDSRTN1 B     ENDMOD1
         EJECT
******************* SORT EXIT E35 ***********************************
*
* DIESE ROUTINE UEBERNIMMT SORTIERTE SAETZE VON ICEMAN UND DRUCKT
* DEREN INHALT, SOWIE UEBERGIBT DIE ZUR ERSTELLUNG EINES INDEX NOT-
* WENDIGEN INFORMATIONEN AN DEN SUBTASK MEMBINDX.
*
********************************************************************
         CNOP  0,4
MOD2     EQU   *            HERSTELLEN DER BASIS- SOWIE RETTEN DER
         SAVE  (14,12)      UEBRIGEN REGISTER
         LR    R2,R13
         CNOP  0,4
         LM    R12,R13,16(R15)
         B     SKIPBAS2
BASREG2  DS    2F
SKIPBAS2 EQU   *
         ST    R2,SAVE13
         ST    R13,8(R2)
         MVC   SAVE1(72),SAVE
         ST    R2,SAVE+4
         CLC   1(3,R1),=X'000000'   VERGLEICH OB NOCH EIN SATZ ZUR
         BE    ENDOUT               VERFUEGUNG GESTELLT WIRD
         LM    R2,R11,SAVEREG
         L     R5,0(R1)
OPENSW2  NOP   NORMAL             EROEFFNEN DES PRINTOUTPUTS
         LA    R3,1
         OPEN  (PRINT1,OUTPUT)
         LA    R2,PRINT1
         TM    DCBOFLGS,DCBOFOPN   FEHLERROUTINE
         BO    OPEN4OK
         PUT   SYSOUT,MELDUNG9
         B     ENDOUT2
OPEN4OK  EQU   *
         OI    OPENSW2+1,X'F0'
NORMAL   EQU   *
         BCT   R3,NOPAGE         VERZWEIGEN ZU BLATTWECHSEL, WENN
         BAL   R14,NEWPAGE       ZEILENZAEHLER ZU HOCH
NOPAGE   EQU   *
         TM    120(R5),X'0F'     TEST OB ERSTER SATZ EINES NEUEN
         BNO   NOMEMBER          MEMBERS UND EVT. VERZWEIGUNG IN
         BAL   14,NEWMEMB        ENTSPRECHENDE ROUTINE
NOMEMBER EQU   *
         MVC   INSRT,0(R5)       AUSGABE EINES SATZES
         PUT   PRINT1,OUT
         MVC   ASA(47),=CL47' '
         STM   R2,R11,SAVEREG
         WAIT  ECB=ECB4         WARTEN OB SUBTASK INFORMATIONEN
         LA    R15,4            BEREITS UEBERNOMMEN
ENDMOD2  EQU   *
         MVC   SAVE(72),SAVE1   RUECKKEHR ZU ICEMAN MIT ANFORDERUNG
         L     R13,SAVE13       DES NAECHSTEN SATZES
         RETURN (14,12),RC=(15)
ENDOUT   EQU   *
         POST  ECB2,255         VERSTAENDIGUNG DES SUBTASKS DASS
         WAIT  ECB=ENDECB       ARBEIT BEENDET UND WARTEN AUF AB-
         CLOSE (PRINT1)         SCHLUSS DES SUBTASKS
ENDOUT2  LA    R15,8            RUECKKER ZU ICEMAN MIT ANZEIGE
         B     ENDMOD2          DES ARBEITSENDES
         SPACE 2
NEWPAGE  EQU   *
         ST    R14,NEWP14        DRUCKEN DIVERSER TITELZEILEN
         LH    R4,PAGECNT        ERHOEHEN DES SEITENZAEHLERS,
         LA    R4,1(R4)          SETZEN DES ZEILENZAEHLERS,
         STH   R4,PAGECNT        TEST OB FORTSETZUNG EINES MEMBERS
         CVD   R4,D
         MVC   BLATT,=X'402020202020'
         ED    BLATT,D+5
         LA    R7,HDR1
         LA    R6,5
PUT      PUT   PRINT1,(7)
         LA    R7,133(R7)
         BCT   R6,PUT
         TM    120(R5),X'0F'
         BO    SKIPFORT
         MVC   MEMB,110(R5)
         MVC   LIBRARY,=CL30'(FORTSETZUNG)'
SKIPFORT EQU   *
         MVI   ASA,C'-'
         LA    R3,50
         L     R14,NEWP14
         BR    R14
         SPACE 2
NEWMEMB  EQU   *
         ST    R14,NEWM14         UEBERTRAGEN VON BIBLIOTHEKS- UND
         MVC   MEMB,110(R5)       MEMBERNAMEN IN DEN AUSGABESATZ
         MVC   LIBRARY,80(R5)
         MVI   ASA,C'-'
         WAIT  ECB=ECB3           WARTEN OB SUBTASK BEREIT ZUR
         NI    ECB3,X'3F'         UEBERNAHME DER INFORMATIONEN
         NI    ECB4,X'3F'
         ST    R5,SAVER5
         CH    R3,=H'4'           TEST OB BLATTWECHSEL NOTWENDIG
         BNL   SKIPNEWP
         BAL   R14,NEWPAGE
SKIPNEWP EQU   *
         POST  ECB2               VERSTAENDIGUNG DES SUBTASK DASS
         S     R3,=F'2'           NEUE INFORMATIONEN ABZUHOLEN SIND
         L     R14,NEWM14         KORREKTUR DES ZEILENZAHLERS
         BR    R14
***************   DEFINITIONEN *************************************
* PARAMETERLISTE FUER IEBPTPCH
OPTION   DC    H'0'
         DS    H
         CNOP  2,4
DDNAME   DC    X'48'
         DC    8F'0'
         DC    CL8'CONTROL'
         DC    CL8'DUMMY'
         DC    2F'0'
DDN      DC    CL8'IN1'
         DC    CL8'SYS002'
* DEFINITIONEN FUER FEHLERROUTINEN
MELDUNG2 DC    CL133' SIM002I LIBRARIES LISTED:'
MELDUNG3 DC    CL133' '
         ORG   MELDUNG3+1
         DC    C'SIM003I  ****** ERROR      RC FROM RDJFCB: '
CCRDJFCB DC    CL4' '
         ORG   MELDUNG3+133
MELDUNG4 DC    CL133' '
         ORG   MELDUNG4+1
         DC    C'SIM004I '
         ORG   MELDUNG4+133
MELDUNG5 DC    CL133' SIM005I  OPEN FOR CONTROL DD-CARD FAILED'
MELDUNG6 DC    CL133' '
         ORG   MELDUNG6+1
         DC    C'SIM006I  ****** ERROR      RC FROM IEBPTPCH: '
CCPTPCH  DC    CL4' '
         ORG   MELDUNG6+133
MELDUNG7 DC    CL133' '
         ORG   MELDUNG7+1
         DC    C'SIM007I  ****** ERROR      RC FROM ICEMAN: '
CCSORT   DC    CL4' '
         ORG   MELDUNG7+133
MELDUNG8 DC    CL133' '
         ORG   MELDUNG8+1
         DC    C'SIM008I  OPEN FOR SYS002 DD-CARD FAILED'
         ORG   MELDUNG8+133
MELDUNG9 DC    CL133' '
         ORG   MELDUNG9+1
         DC    C'OPEN FOR PRINT1 DD-CARD FAILED'
         ORG   MELDUNG9+133
MELDNG10 DC    CL133' '
         ORG   MELDNG10+1
         DC    C'SIM010I  ****** ERROR      RC FROM ATTACH MEMBINDX: '
CCATTACH DC    CL4' '
         ORG   MELDNG10+133
PARM     DC    CL4' '
* DEFINITIONEN DER PTPCH-STEUERKARTEN
PUNCH    DC    CL80'   PUNCH   TYPORG=PO,MAXNAME=1,MAXFLDS=1'
TITLE    DC    CL80' TITLE  ITEM=(''*#@$'
         ORG   TITLE+19
DSN      DC    CL30' '
         DC    C''',1)'
         ORG   TITLE+80
         DS    0F
* PARAMETERLISTEN FUER ICEMAN
SORTAD   DC    X'80'
         DC    AL3(PARMLIST)
         DC    X'0000'
PARMLIST DC    X'0020'
         DC    A(STARTSOR)
         DC    A(ENDSORT)
         DC    A(STARTREC)
         DC    A(ENDREC)
         DC    A(MOD1)
         DC    A(MOD2)
         DC    X'03'
         DC    AL3(SYSOUTWK)
         DC    C'DISK'
STARTSOR DC    C' SORT FIELDS=(111,8,CH,A,81,30,CH,A,119,2,BI,A) '
ENDSORT  EQU   *-1
STARTREC DC    C' RECORD TYPE=F,LENGTH=(121,121,133) '
ENDREC   EQU   *-1
SYSOUTWK DC    CL8'DUMMY'
D        DS    D
* DEFINITIONEN DER DDNAMEN FUER RDJFCB UND DCB
IN2      DC    CL8'IN2'
         DC    CL8'IN3'
         DC    CL8'IN4'
         DC    CL8'IN5'
         DC    CL8'IN6'
         DC    CL8'IN7'
         DC    CL8'IN8'
         DC    CL8'IN9'
         DC    CL8'IN10'
         DC    CL8'IN11'
         DC    CL8'IN12'
         DC    CL8'IN13'
         DC    CL8'IN14'
         DC    CL8'IN15'
         DC    CL8'IN16'
         DC    CL8'IN17'
         DC    CL8'IN18'
         DC    CL8'IN19'
         DC    CL8'IN20'
         DC    CL8'IN21'
         DC    CL8'IN22'
         DC    CL8'IN23'
         DC    CL8'IN24'
         DC    CL8'IN25'
* DCB'S
SYSOUT   DCB   DSORG=PS,MACRF=PM,RECFM=FA,LRECL=133,BLKSIZE=133,       *
               DDNAME=SYSOUT
CONTROL  DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,BLKSIZE=160,        *
               DDNAME=CONTROL
IN1      DCB   DSORG=PS,MACRF=PM,DDNAME=IN1,EXLST=LSTA
LSTA     DS    0F
         DC    X'07'
         DC    AL3(WORKAREA)
TCBADDR  DS    F
WORKAREA DC    176C' '
         LTORG
SAVE1    DS    18F
SAVE13   DS    F
         DS    CL1
IN       DC    CL81' '
         ORG   IN+1
SORTIN   DS    CL121
         ORG   SORTIN+80
LIBDSN   DS    CL30
MEMBNAME DS    CL8
NUMBER   DS    H
DSNFLAG  DS    CL1
SYS002   DCB   DDNAME=SYS002,MACRF=GM,EODAD=ENDSRTIN,DSORG=PS
******************* DEFINITIONEN FUER MOD2 SORT EXIT E35 **************
NEWP14   DS    F
NEWM14   DS    F
SAVEREG  DS    13F
PAGECNT  DC    H'0'
HDR1     DC    CL133' '
         ORG   HDR1
         DC    C'1'
         DC    C'MD800102'
         DS    CL93
ZEIT     DS    CL8
         DS    CL2
DATUM    DS    CL8
         DS    CL2
         DC    C'BLATT'
BLATT    DS    CL6
         ORG   HDR1+133
HDR2     DC    CL133' '
         ORG   HDR2
TITLE1   DC    C' PARAMETERLISTUNG'
         DS    CL116
         ORG   HDR2+133
HDR3     DC    CL133' '
         ORG   HDR3
         DC    C'-'
         DS    CL51
         DC    C'LISTUNG DER '
TITLE2   DC    C'PARAMETER '
         DS    69C' '
         ORG   HDR3+133
HDR4     DC    CL133' '
         ORG   HDR4
         DC    C'-'
         DC    C'NAME          LIBRARY'
         DS    CL26
         DC    C'         1'
         DC    C'         2'
         DC    C'         3'
         DC    C'         4'
         DC    C'         5'
         DC    C'         6'
         DC    C'         7'
         DC    C'         8'
         DC    CL10' '
         ORG   HDR4+133
HDR5     DC    CL133' '
         ORG   HDR5
         DS    CL48
         DC    8C'1234567890'
         DC    CL10' '
OUT      DC    CL133' '
         ORG   OUT
ASA      DS    CL1
MEMB     DS    CL8
         ORG   OUT+15
LIBRARY  DS    CL30
         ORG   OUT+48
INSRT    DS    CL80
         ORG   OUT+133
PRINT1   DCB   DDNAME=PRINT1,MACRF=PM,LRECL=133,BLKSIZE=11970,         *
               RECFM=FBA,DSORG=PS
SAVER5   DC    F'0'
ECB1     DC    F'0'
ECB2     DC    F'0'
ECB3     DC    F'0'
ECB4     DC    F'0'
ENDECB   DC    F'0'
         DCBD
         END   MEMBLIST
