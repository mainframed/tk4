PARM SCANCOL=72 SUPPRESS=ALL
*
*  RMFEASY2:   R M F   D A Y L Y   R E P O R T   PASS 2
*  31/07/79
*              CALCULATIONS, LISTINGS AND ACCUMULATION
*
*              FILE DEFINITION
FILE FILEA
*
*
*              SERVICE RATE TABLE
*
TSOSRATE 1 2 B
TPSRATE 3 2 B
TESTSRATE 5 2 B
BATCHSRATE 7 2 B
*
*              CHANNEL BUSY TABLE
*
CH1BUS 9 4 B
CH1 9 1 B
CH2BUS 13 4 B
CH2 13 1 B
CH3BUS 17 4 B
CH3 17 1 B
CH4BUS 21 4 B
CH4 21 1 B
CH5BUS 25 4 B
CH5 25 1 B
CH6BUS 29 4 B
CH6 29 1 B
*              CHANNEL BUSY / CPU WAIT
CHBCPUW 33 4 B
*              " OF SAMPLES
SMFSAM 37 4 B
*              SYSTEM ID
SMFSID 41 4 A
*              DATE
SMFDAT 45 4 P
YY 46 1 U
DDD 47 2 P
*              INTERVAL START TIME
SMFIST 49 4 P
*              CPU WAIT TIME IN SECONDS
CPUWAIT 53 4 B
*              INTERVAL DURATION IN SECONDS
CPUINT 57 4 P
*              " OF ASID'S IN MEMORY (MPL)
MPL 61 1 B
*              " OF BATCH ASID'S
BATCH 62 1 B
*              " OF TIME SHARING USERS
TSO 63 1 B
*              " OF PAGES / SECOND
PAGE 64 1 B
*              TSO ACTIVE TIME (SECONDS)
TSOACT 65 4 B
*              MEASUREMENT DURATION IN SECONDS
CYCLENGTH 69 2 B
*
*              OUTPUT AREA
OUTPUTW W-1 80 A
*
*              SERVICE RATE TABLE
*
TSOSRS W-1 4 B
TSOSRP W-3 2 B
TPSRS W-5 4 B
TPSRP W-7 2 B
TESTSRS W-9 4 B
TESTSRP W-11 2 B
BATCHSRS W-13 4 B
BATCHSRP W-15 2 B
*
*              CHANNEL BUSY TABLE
*
CH1BUSS W-17 4 B
CH1S W-17 1 B
CH2BUSS W-21 4 B
CH2S W-21 1 B
CH3BUSS W-25 4 B
CH3S W-25 1 B
CH4BUSS W-29 4 B
CH4S W-29 1 B
CH5BUSS W-33 4 B
CH5S W-33 1 B
CH6BUSS W-37 4 B
CH6S W-37 1 B
*              CHANNEL BUSY / CPU WAIT
CHBCPUWS W-41 4 B
*              " OF SAMPLES
SAMPLES W-45 4 B
*              SYSTEM ID
SYSID W-49 4 A
*              DATE
DATE W-53 6 ND
DAY W-53 2 N
MONTH W-55 2 N
YEAR W-57 2 N
*              SHIFT KEY  1 = 0 - 8.00, 2 = 8 - 16.00, 3 = 16 - 24.00 H
SHIFTCHP W-59 1 B
*              CPU WAIT TIME
CPUWAITS W-60 4 B
*              INTERVAL DURATION IN SECONDS
CPUINTS W-64 4 P
*              " OF ASID'S IN MEMORY (MPL)
MPLS W-68 2 B
MPLP W-69 1 B
*              " OF BATCH ASID'S
BATCHS W-70 2 B
BATCHP W-71 1 B
*              " OF TIME SHARING USERS
TSOS W-72 2 B
TSOP W-73 1 B
*              " OF PAGES / SECOND
PAGES W-74 2 B
PAGEP W-75 1 B
*              TSO ACTIVE TIME (SECONDS)
TSOACTS W-76 4 B
*              RECORD ID
RECID W-80 1 A
*              TOTAL MEASUREMENT DURATION
CYCLNGS W 4 B
*              SUBSCRIPTED CHANNEL BUSY VALUE
CHXBUSY-(SUB1) 1 4 B
CHX-(SUB1) 1 1 B
*
SUB72 W 4 B
*              DATE COMPARE FIELD
SMFDATP W 4 P
*              SHIFT INDICATOR. 1 = 00.00 - 08.00 H
*                               2 = 08.00 - 16.00 H
*                               3 = 16.00 - 24.00 H
SHIFTCHNG W 1 B
*              SHIFT INFO
SHIFT W 5 A
*              FIRST TIME SWITCH
SMFFTSW W 1 B
*
*              HEADLINE SWITCH
HEADLINE W 1 B
*
*              PAGE LINE COUNTER
ZZ W 1 B
*
*              LINE COUNTER
NUMBER W 2 B
*
*              TSO ACTIVE TIME (% OF INTERVAL TIME)
TSOACT% W 1 B
*
*              TSO SERVICE
TSOSERV% W 1 B
*
*              TEST SERVICE
TESTSERV% W 1 B
*
*              TP SERVICE
TPSERV% W 1 B
*
*              BATCH SERVICE
BATCHSERV% W 1 B
*
*              CHANNEL BUSY / CPU WAIT PERCENTAGE
CHBCPUW% W 1 B
*
*              CPU WAIT TIME PERCENTAGE
CPU W 1 B
*
*
TOTSRATE W 2 B
*
SMFISTP W 6 N
*
9999
*
*
*             SET SHIFT INDICATOR DEPENDING ON INTERVAL START TIME
IF SMFIST GQ 0 AND SMFIST LT 80000 THEN
SHIFTCHNG = 1
IF SMFIST GQ 80000 AND SMFIST LT 160000 THEN
SHIFTCHNG = 2
IF SMFIST GQ 160000 THEN
SHIFTCHNG = 3
*
*
*              TEST FOR END OF SHIFT
IF EOF EQ YES THEN GO TO TOTCALC
IF SMFDAT EQ SMFDATP AND SMFSID EQ SYSID AND SHIFTCHNG EQ SHIFTCHP THEN
GO TO FIRSTTIME
*
TOTCALC:
*
IF SMFFTSW EQ 0 THEN GO TO FIRSTTIME
*
IF NUMBER LT 4 THEN GO TO CLEAR
ELSE
*
*            TOTAL CALCULATIONS
*
CPU = CPUWAITS X 100.0 / CYCLNGS + 0.5
MPLS = MPLS X 1.0 / NUMBER + 0.5
TSOS = TSOS X 1.0 / NUMBER + 0.5
BATCHS = BATCHS X 1.0 / NUMBER + 0.5
PAGES = PAGES X 1.0 / NUMBER + 0.5
TSOSRS = TSOSRS / NUMBER
BATCHSRS = BATCHSRS / NUMBER
TPSRS = TPSRS / NUMBER
TESTSRS = TESTSRS / NUMBER
TOTSRATE = TSOSRS + BATCHSRS + TPSRS + TESTSRS
TSOSERV% = TSOSRS X 100.0 / TOTSRATE + 0.5
BATCHSERV% = BATCHSRS X 100.0 / TOTSRATE + 0.5
TPSERV% = TPSRS X 100.0 / TOTSRATE + 0.5
TESTSERV% = TESTSRS X 100.0 / TOTSRATE + 0.5
*
IF TSOACTS GT CPUINTS THEN
TSOACTS = CPUINTS
IF FILE EQ FILE THEN
TSOACT% = TSOACTS X 100.0 / CPUINTS + 0.5
*
*
*              CALCULATE PERCENTAGES OF CH BUSY AND CH BUSY / CPU WAIT
*
CHBCPUW% = CHBCPUWS X 100.0 / SAMPLES + 0.5
SUB72 = 6
SUB1 = SUBW + 16
*
BUSYLOOPS:
IF SUB72 GT 0 THEN
CHX = CHXBUSY X 100.0 / SAMPLES + 0.5
SUB1 = SUB1 + 4
SUB72 = SUB72 - 1
GO TO BUSYLOOPS
*
IF SHIFTCHP = 1 THEN
 SHIFT = NACHT
IF SHIFTCHP = 2 THEN
 SHIFT = FRUEH
IF SHIFTCHP = 3 THEN
 SHIFT = SPAET
IF FILE EQ FILE THEN
*
*             PRINT TOTAL LINE
*
PRINT SP2 SHIFT-1 CPU-8 MPLP-12 TSOP-16 BATCHP-20 PAGEP-24 MORE
      TSOSERV%-28 TSOSRP-33 TSOACT%-40 MORE
      BATCHSERV%-44 BATCHSRP-49 TPSERV%-55 TPSRP-60 MORE
      TESTSERV%-66 TESTSRP-71 TOTSRATE-78 CHBCPUW%-87 CH1S-93 MORE
      CH2S-97 CH3S-101 CH4S-105 CH5S-109 CH6S-113
*
*             PUNCH SUMMARIZATION RECORDS
RECID = 1
PUNCH CYCLNGS SMFDATP DATE-53 SYSID-49 SHIFTCHP-59 RECID-80
*
RECID = 2
PUNCH OUTPUTW
*
*
*             CLEAR ACCUMULATION FIELDS
*
CLEAR:
IF FILE EQ FILE THEN
MPLS = 0
BATCHS = 0
TSOS = 0
PAGES = 0
TSOSRS = 0
BATCHSRS = 0
TPSRS = 0
TESTSRS = 0
TSOACTS = 0
CPUINTS = 0
SAMPLES = 0
CHBCPUWS = 0
CH1BUSS = 0
CH2BUSS = 0
CH3BUSS = 0
CH4BUSS = 0
CH5BUSS = 0
CH6BUSS = 0
CPUWAITS = 0
CYCLNGS = 0
*
NUMBER = 0
*
HEADLINE = 0
*
*
FIRSTTIME:
IF EOF EQ YES THEN
STOP
*
ELSE
*             SAVE SYSTEM ID
SYSID = SMFSID
*             SAVE DATE
SMFDATP = SMFDAT
*             SHIFT KEY
SHIFTCHP = SHIFTCHNG
*
SMFFTSW = 1
*
*
*
*
*              CALCULATE SERVICE PERCENTAGES
TOTSRATE = TSOSRATE + TESTSRATE + TPSRATE + BATCHSRATE
TSOSERV% = TSOSRATE X 100.0 / TOTSRATE + 0.5
TESTSERV% = TESTSRATE X 100.0 / TOTSRATE + 0.5
TPSERV% = TPSRATE X 100.0 / TOTSRATE + 0.5
BATCHSERV% = BATCHSRATE X 100.0 / TOTSRATE + 0.5
*
*              CALCULATE CPU WAIT TIME PERCENTAGE
CPU = CPUWAIT X 100.0 / CYCLENGTH + 0.5
*
*              CALCULATE PERCENTAGE OF TRANSACT ACTIVE TIME
*
IF TSOACT GT CPUINT THEN
TSOACT = CPUINT
IF FILE EQ FILE
TSOACT% = TSOACT X 100.0 / CPUINT + 0.5
CPUINTS = CPUINTS + CPUINT
*
*
*              CALCULATE PERCENTAGES OF CH BUSY AND CH BUSY CPU WAIT
*
CHBCPUWS = CHBCPUWS + CHBCPUW
CH1BUSS = CH1BUSS + CH1BUS
CH2BUSS = CH2BUSS + CH2BUS
CH3BUSS = CH3BUSS + CH3BUS
CH4BUSS = CH4BUSS + CH4BUS
CH5BUSS = CH5BUSS + CH5BUS
CH6BUSS = CH6BUSS + CH6BUS
CHBCPUW% = CHBCPUW X 100.0 / SMFSAM + 0.5
SAMPLES = SAMPLES + SMFSAM
SUB72 = 6
SUB1 = SUBFILEA + 8
*
BUSYLOOP:
IF SUB72 GT 0 THEN
CHX = CHXBUSY X 100.0 / SMFSAM + 0.5
SUB1 = SUB1 + 4
SUB72 = SUB72 - 1
GO TO BUSYLOOP
*
*             CONVERT DATE FROM YYDDD TO DDMMYY
*
IF FILE EQ FILE THEN
YEAR = YY
MONTH = 01
IF DDD GT 335 THEN
DDD = DDD - 335
MONTH = 12
IF DDD GT 305 THEN
DDD = DDD - 305
MONTH = 11
IF DDD GT 274 THEN
DDD = DDD - 274
MONTH = 10
IF DDD GT 244 THEN
DDD = DDD - 244
MONTH = 09
IF DDD GT 213 THEN
DDD = DDD - 213
MONTH = 08
IF DDD GT 182 THEN
DDD = DDD - 182
MONTH = 07
IF DDD GT 152 THEN
DDD = DDD - 152
MONTH = 06
IF DDD GT 121 THEN
DDD = DDD - 121
MONTH = 05
IF DDD GT 91 THEN
DDD = DDD - 91
MONTH = 04
IF DDD GT 60 THEN
DDD = DDD - 60
MONTH = 03
IF DDD GT 31 THEN
DAY = DDD - 31
MONTH = 02
ELSE
DAY = DDD
*
*             PRODUCE HEADLINES
*
IF HEADLINE EQ 0 OR ZZ GT 56 THEN
*
PRINT CC1 SMFSID-1 DATE-7
*
PRINT SP2 (INTVL  CPU)-1 (TSO BAT)-16 MORE
          (------TSO------ --BATCH---)-28  MORE
          (----TP---- ---TEST---)-55  MORE
          TOTAL-77 (CPU WAIT)-84 MORE
          (----CHANNEL BUSY  %----)-93
*
PRINT (ST-TME WT  MPL MPL MPL PAG)-1  MORE
      (S-% S-RATE ACT% S-% S-RATE S-% S-RATE)-28 MORE
      (S-% S-RATE S-RATE)-66  MORE
      (CH BUSY)-84 (CH1 CH2 CH3 CH4 CH5 CH6)-93
*
PRINT ( )
*
*             INIT PAGE LINE COUNT
ZZ = 5
*             SET HEADLINE SWITCH TO  1
HEADLINE = 1
*
IF FILE EQ FILE THEN
SMFISTP = SMFIST
*
*             PRINT INTERVAL LINE
*
PRINT SMFISTP-1 CPU-8 MPL-12 TSO-16 BATCH-20 PAGE-24 MORE
      TSOSERV%-28 TSOSRATE-33 TSOACT%-40 MORE
      BATCHSERV%-44 BATCHSRATE-49 TPSERV%-55 TPSRATE-60 MORE
      TESTSERV%-66 TESTSRATE-71 TOTSRATE-78 CHBCPUW%-87 CH1-93 MORE
      CH2-97 CH3-101 CH4-105 CH5-109 CH6-113
*
*             UPDATE LINE COUNTERS
NUMBER = NUMBER + 1
ZZ = ZZ + 1
*
*             ACCUMULATE VALUES
*
CPUWAITS = CPUWAITS + CPUWAIT
MPLS = MPLS + MPL
TSOS = TSOS + TSO
BATCHS = BATCHS + BATCH
PAGES = PAGES + PAGE
TSOSRS = TSOSRS + TSOSRATE
BATCHSRS = BATCHSRS + BATCHSRATE
TPSRS = TPSRS + TPSRATE
TESTSRS = TESTSRS + TESTSRATE
TSOACTS = TSOACTS + TSOACT
CYCLNGS = CYCLNGS + CYCLENGTH
*
