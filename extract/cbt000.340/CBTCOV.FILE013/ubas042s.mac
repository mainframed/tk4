DCMPRS   CSECT
         REG
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING *-6,R12
         LA    R15,0                   RETURNCODE 0 WIRD GESETZT
         L     R7,0(R1)                R7 WIRD MIT EINGABEADRESSE GE-
*                                      LADEN
         LA    R8,5(R7)                R8 WIRD MIT DAT.-SATZ-ANF.
*                                      GELADEN
         SR    R10,R10                 R10 WIRD AUF 0 GESETZT
         IC    R10,2(R7)               VERARBEITETE LAENGE WIRD IN R10
*                                      GESPEICHERT
         SLL   R10,8
         IC    R10,3(R7)
         LR    R6,R7
         AR    R6,R10
         SR    R9,R9                   R9 WIRD AUF 0 GESTZT
         IC    R9,4(R6)                  L.ANG. WIRD GESP.
         L     R4,4(R1)                R4 WIRD MIT AUSGABEADR. GELADEN
         MVC   0(4,R4),=X'00000000'
         LA    R3,4(R4)                R3 WIRD MIT DATENAUSGABEADR. GEL
*                                      ADEN
         AR    R8,R10                  R8 WIRD UM VERARB. LAEN. ERHOEHT
         SR    R1,R1
         SR    R5,R5
         SR    R6,R6
         SR    R7,R7
DCM1     EQU   *
         SR    R11,R11
         TM    0(R8),X'7F'             KONTOLLE AUF UEBERLAENGE
         BNZ   DCM2
         LA    R1,1(R8)                R1 ENTH. UEBERTRAG. ADR.
         LA    R5,1(R5)                ZAEHL. REG. 5 WIRD ERHOEHT
         IC    R11,0(R1)               IN R11 WIRD KONTR.-BIT 8 GESICH
         LA    R1,1(R1)
         LA    R7,2(R7)                 FORMAT KONTROLLE
         B     DCM3
DCM2     EQU   *
         LA    R7,1(R7)                 FORMAT KONTROLLE
         LA    R1,0(R8)                R1 ENTH. UEBERTR.ADR
         IC    R0,0(R8)                R0 RETTET KONTR.-BIT 8
         NI    0(R1),X'7F'             ECHTE LAENGE WIRD BERECHNET
         IC    R11,0(R1)               R11 ENTH. L DER UEBERTR.
         LA    R1,1(R1)
         STC   R0,0(R8)
DCM3     EQU   *
         TM    0(R8),X'80'             KONTROLLE AUF ZEICHEN OR BLANKS
         BO    DCM4
         MVI   0(R3),X'40'             R3(AUSGABEADR) ERHAELT BLANK
         LR    R2,R3
         LA    R3,1(R3)
         LA    R6,1(R6)                ZAEHL REG 6 WIRD ERH.
         LA    R5,1(R5)                ZAEHL REG 5 WIRD ERH.
         BCT   R11,EXMOVE
         B     DCM6
DCM4     EQU   *
         AR    R7,R11                   FORMAT KONTROLLE
         LA    R5,1(R5)                ZAEHL REG 5 WIRD ERH.
         AR    R5,R11                  ZAEHL REG 5 WIRD UM UEBERTR.-L
*                                      ERHOEHT
         LA    R2,0(R1)                R2 WIRD AUF UEBERTRG.ADR. GES.
         AR    R1,R11                  R1 ENTH. ENDE DER UEBERTRG.ADR
EXMOVE   EQU   *
         CR    R9,R7                    FORMAT KONTROLLE
         BL    DCM7
         AR    R6,R11
         BCTR  R11,0
         EX    R11,MOVED
         LA    R3,1(R11,R3)
DCM6     EQU   *
         LR    R8,R1                   R8 WIRD MIT NEUER ANFANGSADR.
*                                      GELADEN
         CR    R9,R5                   ENDE DES RECORDS ?
         BH    DCM1
         L     R7,24(R13)               ANFANGSADR WIRD ERNEUT GEL.
         L     R7,0(R7)
         AR    R9,R10
         LA    R9,5(R9)                R9 WIRD UM 5 ERHOEHT
         CH    R9,0(R7)                UEBERTRAGUNG VOLLSTAENDIG ?
         BE    DCM5
         LA    R15,4                   RETURNCODE 4 WIRD GESTZT
         LA    R5,1(R5)
         AR    R5,R10
         STC   R5,3(R7)                BISHER VERARB. LAEN. WIRD IN DER
*                                      EINGABE GESPEICHERT
         SRL   R5,8
         STC   R5,2(R7)
DCM5     EQU   *
         LA    R6,4(R6)
         STC   R6,1(R4)                DIE L DES RECORDS WIRD UEBERGEB.
         SRL   R6,8
         STC   R6,0(R4)
BLK50    EQU   *
         LM    R0,R12,20(R13)
         BR    R14
DCM7     EQU   *
         LA    R15,8                    FORMAT FEHLER GEFUNDEN
         B     BLK50
MOVED    MVC   0(0,R3),0(R2)
         LTORG
         END
