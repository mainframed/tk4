         PRINT NOGEN
*      ****************************************************************
*      *                                                              *
*      * NAME : UZED04A        MUSS AUTHORISIERT WERDEN.              *
*      * DATUM: MAI 1976                                              *
*      * AUTOR: GALL F.                                               *
*      *                                                              *
*      * DATA SET AKTIVIT%TEN MESSEN                                  *
*      *                                                              *
*      ****************************************************************
*
PDV056   INIT  BASIS=(2,9)
*      ****************************************************************
*      *                                                              *
*      * PARAMETER HANDLING                                           *
*      *                                                              *
*      * PARM='VOLUME,VONDAT,BISDAT,SUCHKRITERIUM'                    *
*      *       VONDAT= TT.MM JJ                                       *
*      *       BISDAT= TT.MM.JJ  (ANSTELLE BISDAT EIN KOMMA = KEINE   *
*      *                          ABSOLUTE OBERE GRENZE)              *
*      *       SUCHKRITERIUM= TEST,DATA,LMOD,SOURCE,SYS1,SYS2         *
*      *                      ODER ALL  (BEINHALTET: TEST,DATA,LMOD,  *
*      *                                             SOURCE,SYS1 + 2) *
*      *                      ODER COMP (BRINGT ALLES, AUSSER TEMP.   *
*      *                                 SYSTEM-FILES SYS......       *
*      *                                                              *
*      ****************************************************************
         L     R1,0(R1)
         LH    R3,0(R1)
         BCTR  R3,R0
         STC   R3,MVC1+1
MVC1     MVC   PARM,2(R1)          PARM-STRING NACH PARM
*    *** VOLUME ***
         MVC   PARMVOL(6),PARM
         CLC   PARM+6(1),=X'6B'    KOMMA?
         BNE   WRONGP              NEIN  (PARM IST FALSCH)
*    *** VONDAT ***
         MVC   VDATE(2),PARM+13    JJ
         MVC   VDATE+2(2),PARM+10  MM
         MVC   VDATE+4(2),PARM+7   TT
         CALL  UZED13A,(VONDAT,R)  DATUM PR<FEN
         CLI   R,C'R'
         BNE   WRONGP              DATUM IST FALSCH
         MVC   VD11(8),PARM+7
         MVC   VD21,VD11
         CLC   PARM+15(1),=X'6B'   KOMMA?
         BNE   WRONGP              NEIN  (PARM IST FALSCH)
         CLC   PARM+16(1),=X'6B'   KOMMA ANSTELLE BISDAT?
         BE    OSDATUM
*    *** BISDAT ***
         MVC   BDATE(2),PARM+22    JJ
         MVC   BDATE+2(2),PARM+19  MM
         MVC   BDATE+4(2),PARM+16  TT
         CALL  UZED13A,(BISDAT,R)  DATUM PR<FEN
         CLI   R,C'R'
         BNE   WRONGP              DATUM IST FALSCH
         MVC   VD12(8),PARM+16
         MVC   VD22,VD12
         LA    R3,PARM+24
         B     SKRIT2
*
OSDATUM  EQU   *
         CALL  UZED10A,(OSDAT)     TT.MM.JJ
         MVC   ZWFJJ,OSDAT+6
         MVC   ZWFMM,OSDAT+3
         MVC   ZWFTT,OSDAT
         CALL  UZED12A,(ZWF,ODA)   YYDDD
         SP    ODA,=P'1'           - 1 TAG
         ZAP   ODA2+1(3),ODA(3)
         CALL  UZED11A,(ODA2,AVD12) TT.MM.JJ
         MVC   VD22,VD12
         MVC   BDATE(2),VD12+6
         MVC   BDATE+2(2),VD12+3
         MVC   BDATE+4(2),VD12
*    *** SUCHKRITERIUM ***
SKRIT    EQU   *
         LA    R3,PARM+16
SKRIT2   EQU   *
         CLC   0(1,R3),=X'6B'
         BNE   WRONGP              PARM IST FALSCH
         CLC   1(3,R3),=C'ALL'
         BNE   IFCOMP
         MVC   TEST(4),=C'TEST'
         MVC   DATA(4),=C'DATA'
         MVC   LMOD(4),=C'LMOD'
         MVC   SOURCE(6),=C'SOURCE'
         MVC   SYS1(4),=C'SYS1'
         MVC   SYS2(4),=C'SYS2'
         B     ENDPARM
IFCOMP   EQU   *
         CLC   1(4,R3),=C'COMP'
         BNE   NOTALL
         MVI   SWC,C'1'            COMP-SWITCH AUF '1'
         B     ENDPARM
NOTALL   EQU   *
         BAL   R14,SKPRUEF
         MVC   TEST(4),=C'TEST'    TEST
         LA    R3,5(R3)
         CLC   0(1,R3),=X'6B'
         BE    NOTALL
         B     ENDPARM
*
         MVC   DATA(4),=C'DATA'    DATA
         LA    R3,5(R3)
         CLC   0(1,R3),=X'6B'
         BE    NOTALL
         B     ENDPARM
*
         MVC   LMOD(4),=C'LMOD'    LMOD
         LA    R3,5(R3)
         CLC   0(1,R3),=X'6B'
         BE    NOTALL
         B     ENDPARM
*
         MVC   SOURCE,=C'SOURCE'   SOURCE
         LA    R3,7(R3)
         CLC   0(1,R3),=X'6B'
         BE    NOTALL
         B     ENDPARM
*
         MVC   SYS1(4),=C'SYS1'    SYS1
         LA    R3,5(R3)
         CLC   0(1,R3),=X'6B'
         BE    NOTALL
         B     ENDPARM
*
         MVC   SYS2(4),=C'SYS2'    SYS2
         LA    R3,5(R3)
         CLC   0(1,R3),=X'6B'
         BE    NOTALL
         B     ENDPARM
*
WRONGP   WTO   'WRONG PARAMETER',ROUTCDE=11
         MVI   RETURNCD,X'04'
         B     EOJ
*
ENDPARM  EQU   *
*      ****************************************************************
*      *                                                              *
*      * OPEN  (SYSUT1 = INPUT / SYSPRINT + SYSOUT = OUTPUT           *
*      * VORBEREITUNG ZUM TABELLEN LADEN                              *
*      *                                                              *
*      ****************************************************************
OPEN     OPEN  (SYSUT1,,SYSPRINT,OUTPUT,SYSOUT,OUTPUT)
         CALL  UZED12A,(VONDAT,OSDATV)
         CALL  UZED12A,(BISDAT,OSDATB)
         LA    R5,TABELLE
         LA    R6,57
         L     R7,=A(TABELLE+400*57)
*      ****************************************************************
*      *                                                              *
*      * TABELLEN LADEN                                               *
*      *                                                              *
*      ****************************************************************
         EXTRACT TIOTAREA,'S',FIELDS=TIOT
         L     R10,TIOTAREA
         LA    R10,24(,R10)
SUCH     EQU   *
         XR    R12,R12
         IC    R12,0(R10)
         LTR   R12,R12
         BZ    ENDE
         MVC   DDNAME,4(R10)
         AR    R10,R12
         CLC   DDNAME,PARMVOL
         BNE   SUCH
         MVC   VTOC+40(8),DDNAME   DDNAME IM VTOC-DCB ERSETZEN
         MVC   VOLSER(6),PARMVOL
         RDJFCB (VTOC,(INPUT))
         MVI   JFCBAREA,X'04'
         MVC   JFCBAREA+1(43),JFCBAREA
         MVI   JFCBAREA+76,X'C0'
         ENQ   (NAME1,VOLSER,E,,SYSTEM)
         OPEN  (VTOC,(INPUT)),TYPE=J
READ     READ  READDECB,SF,VTOC,DSCB,'S'
         CHECK  READDECB
         CLI   DATA1,X'F1'
         BNE   READ
         CLI   SWC,C'1'            IF COMP?
         BE    IFSYS
         CLC   DSCB(4),TEST
         BE    MOVE
         CLC   DSCB(4),DATA
         BE    MOVE
         CLC   DSCB(4),LMOD
         BE    MOVE
         CLC   DSCB(6),SOURCE
         BE    MOVE
         CLC   DSCB(4),SYS1
         BE    MOVE
         CLC   DSCB(4),SYS2
         BNE   READ
         B     MOVE
IFSYS    EQU   *
         CLC   DSCB(3),=C'SYS'     HANDELT ES SICH UM EINE TEMPOR%RE
         BNE   MOVE                SYSTEM DATEI?
         CLC   DSCB+8(1),=C'.'
         BE    READ                YES
MOVE     EQU   *
         MVC   0(44,R5),DSCB
         ZAP   44(4,R5),=P'0'
         ZAP   48(3,R5),=P'0'
         ZAP   51(3,R5),=P'0'      F<R SMF DATUM
         MVC   DW+3(1),DSCB+53
         L     R3,DW
         CVD   R3,DW
         SRP   DW,3,0
         ZAP   54(3,R5),DW+5(3)
         MVC   DW+2(2),DSCB+54
         L     R3,DW
         CVD   R3,DW
         AP    54(3,R5),DW+6(2)
         BXLE  R5,R6,READ
         CLOSE  (VTOC)
         DEQ    (NAME1,VOLSER,,SYSTEM)
*      ****************************************************************
*      * MEHR ALS 400 FILES                                           *
*      ****************************************************************
         WTO   'MEHR ALS 400 FILES',ROUTCDE=11
         MVI   RETURNCD,X'08'
         B     EOJ
*      ****************************************************************
*      * DD-STATEMENT IS MISSING                                      *
*      ****************************************************************
ENDE     EQU   *
         WTO   'DD-STATEMENT IS MISSING',ROUTCDE=11
         MVI   RETURNCD,X'0C'
         B     EOJ
*      ****************************************************************
*      *                                                              *
*      * LADEN DER TABELLE BEENDET                                    *
*      *                                                              *
*      ****************************************************************
ENDSYSIN EQU   *
         CLOSE (VTOC)
         DEQ   (NAME1,VOLSER,,SYSTEM)
*      ****************************************************************
*      * VORBEREITUNG ZUM SEQUENTIELLEN                               *
*      ****************************************************************
         SR    R5,R6               ADR DES LETZTEN GELADENEN TAB PLATZ
         LR    R7,R5
*      ****************************************************************
*      *                                                              *
*      * LESEN SMF-RECDS TYP 14/15                                    *
*      *                                                              *
*      ****************************************************************
LESEN    EQU   *
         GET   SYSUT1
         LA    R3,4(R1)
         CLI   1(R3),X'0E'
         BE    VERARB
         CLI   1(R3),X'0F'
         BNE   LESEN
VERARB   EQU   *
         CP    7(3,R3),OSDATV
         BL    LESEN
         CP    7(3,R3),OSDATB
         BH    LESEN
         CLI   SWC,C'1'
         BE    VERGL1
         CLC   64(4,R3),TEST
         BE    VERGL1
         CLC   64(4,R3),DATA
         BE    VERGL1
         CLC   64(4,R3),LMOD
         BE    VERGL1
         CLC   64(6,R3),SOURCE
         BE    VERGL1
         CLC   64(4,R3),SYS1
         BE    VERGL1
         CLC   64(4,R3),SYS2
         BNE   LESEN
*
VERGL1   EQU   *
         LA    R5,TABELLE
BXLELOOP CLC   0(44,R5),64(R3)
         BE    FOUND
        BXLE  R5,R6,BXLELOOP
         B     LESEN               NICHT GEFUNDEN
*
FOUND    EQU   *
         AP    48(3,R5),=P'1'
         L     R8,280(R3)
         CVD   R8,DW
       AP      44(4,R5),DW+4(4)
         ZAP   51(3,R5),7(3,R3)    DATUM REC TO SMF-BUFFER
         B     LESEN
*
ENDLESEN EQU   *
         LA    R5,TABELLE
VERGL2   EQU   *
         BAL   R10,DRUCK1
         BXLE  R5,R6,VERGL2
         CLOSE (SYSUT1,,SYSPRINT,,SYSOUT)
         B     EOJ
*      ****************************************************************
*      *                                                              *
*      * DRUCK - ROUTINE                                              *
*      *                                                              *
*      ****************************************************************
DRUCK1   EQU   *
         CP    ZZ1,=P'0'
         BE    TZPUT1
         CP    ZZ1,=P'65'
         BNE   PUTZ1
TZPUT1   EQU   *
         AP    SZ1,=P'1'
         MVI   CCT,X'8B'
         PUT   SYSPRINT,TITELZ
         CALL  UZED10A,(TDATE)
         MVC   TSEITE,MASKE
         ED    TSEITE,SZ1
         MVI   CCT,X'09'
         PUT   SYSPRINT,TITELZ
         ZAP   ZZ1,=P'10'
         MVI   CCU1,X'13'
         PUT   SYSPRINT,UTITEL1
         MVI   CCU1,X'09'
         PUT   SYSPRINT,UTITEL1
         PUT   SYSPRINT,ZEILE5
         PUT   SYSPRINT,ZEILE6
         MVI   CCZ1,X'0B'
         PUT   SYSPRINT,ZEILE1
         MVI   CCZ1,X'09'
PUTZ1    EQU   *
         MVC   STRING(44),0(R5)
         CP    51(3,R5),=P'0'
         BE    NODAT
         ZAP   SMFDAT+1(3),51(3,R5)
         CALL  UZED11A,(SMFDAT,ACHARDAT)   YYDDD NACH TT.MM.JJ
NODAT    EQU   *
         ZAP   CRDAT+1(3),54(3,R5)
         CALL  UZED11A,(CRDAT,ACREDT)
         MVC   COUNT,MASK
         ED    COUNT,48(R5)
         MVC   EXCP,MASKE2
         ED    EXCP,44(R5)
         CP    44(4,R5),=P'0'
         BNE   ZAP
         ZAP   DIV+3(4),=P'0'
         B     DEXCPS
ZAP      EQU   *
         ZAP   DIV+6(4),44(4,R5)
         ZAP   DIVISOR,48(3,R5)
         DP    DIV,DIVISOR
DEXCPS   EQU   *
         MVC   DEXCP,MASKE2
         ED    DEXCP,DIV+3
         ZAP   DIV,=P'0'
ENDDIV   EQU   *
         PUT   SYSPRINT,ZEILE1
         MVC   CHARDAT,=C'        '
         AP    ZZ1,=P'1'
         CP    48(3,R5),=P'0'
         BE    DRUCK2
         BR    R10
DRUCK2   EQU   *
         CP    ZZ2,=P'0'
         BE    TZPUT2
         CP    ZZ2,=P'65'
         BNE   PUTZ2
TZPUT2   EQU   *
         AP    SZ2,=P'1'
         MVI   CCT,X'8B'
         PUT   SYSOUT,TITELZ
         MVC   TSEITE,MASKE
         ED    TSEITE,SZ2
         MVI   CCT,X'09'
         PUT   SYSOUT,TITELZ
         ZAP   ZZ2,=P'10'
         MVI   CCU2,X'13'
         PUT   SYSOUT,UTITEL2
         MVI   CCU2,X'09'
         PUT   SYSOUT,UTITEL2
PUTLEER2 EQU   *
         MVI   CCZ2,X'1B'
         PUT   SYSOUT,ZEILE2
         MVI   CCZ2,X'09'
PUTZ2    EQU   *
         MVC   ZEILE2+1(44),0(R5)
         PUT   SYSOUT,ZEILE2
         AP    ZZ2,=P'1'
         BR    R10
SKPRUEF  EQU   *
         LA    R12,SKTAB
         LA    R10,6
LOOP     EQU   *
         CLC   1(4,R3),0(R12)
         BE    0(R14)
         LA    R12,4(R12)
         LA    R14,24(R14)
         BCT   R10,LOOP
         B     WRONGP              PARM IST FALSCH
EOJ      EQU   *
         L     R13,S0001+4
         L     R15,RETURNCD
         RETURN (14,12),RC=(15)
*      ****************************************************************
*      *                                                              *
*      * D E F I N I T I O N E N                                      *
*      *                                                              *
*      ****************************************************************
*
*
*      ****************************************************************
*      * D C B' S                                                     *
*      ****************************************************************
VTOC     DCB   DDNAME=VTOC,EODAD=ENDSYSIN,DSORG=PS,MACRF=(R),          *
               KEYLEN=44,BLKSIZE=96,EXLST=LIST
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=ENDLESEN,BFTEK=A
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),                    *
               RECFM=FBM,LRECL=133,BLKSIZE=798
SYSOUT   DCB   DDNAME=SYSOUT,DSORG=PS,MACRF=(PM),                      *
               RECFM=FBM,LRECL=133,BLKSIZE=798
*
*      ****************************************************************
*      * EXLST  /  JFCB  /  TIOT                                      *
*      ****************************************************************
         DS    0F
LIST     DC    AL1(128+7),AL3(JFCBAREA)
DSCB     DS    XL44
DATA1    DS    XL96
         DS    0F
JFCBAREA DS    CL176
VOLSER   DC    CL6' '
NAME1    DC    CL8'SYSVTOC '
*
TIOTAREA DS    F
DDNAME   DS    CL8
*
*      ****************************************************************
*      * DATUM-FELDER  /  SKTAB                                       *
*      ****************************************************************
OSDAT    DC    CL8' '
ZWF      DS    0CL8
ZWFOSDAT DC    CL2'19'
ZWFJJ    DC    CL2' '
ZWFMM    DC    CL2' '
ZWFTT    DC    CL2' '
ODA      DC    PL3'0'
ODA2     DC    PL4'0'
AVD12    DC    A(VD12)
VONDAT   DS    0CL8
VD       DC    CL2'19'
VDATE    DS    CL6
BISDAT   DS    0CL8
BD       DC    CL2'19'
BDATE    DS    CL6
*
OSDATV   DC    PL3'0'
OSDATB   DC    PL3'0'
SMFDAT   DC    PL4'0'
ACHARDAT DC    A(CHARDAT)
*
SKTAB    DS    0CL24
         DC    CL4'TEST'
         DC    CL4'DATA'
         DC    CL4'LMOD'
         DC    CL4'SOUR'
         DC    CL4'SYS1'
         DC    CL4'SYS2'
*
TEST     DC    CL4' '
DATA     DC    CL4' '
LMOD     DC    CL4' '
SOURCE   DC    CL6' '
SYS1     DC    CL4' '
SYS2     DC    CL4' '
*
*
*      ****************************************************************
*      * ZEILEN-Z%HLER  /  MASKEN                                     *
*      ****************************************************************
ZZ1      DC    PL2'0'
ZZ2      DC    PL2'0'
SZ1      DC    PL2'0'
SZ2      DC    PL2'0'
MASKE    DC    XL4'40202120'
MASK     DC    XL6'402020202120'
MASKE2   DC    XL8'4020202020202120'
*
*      ****************************************************************
*      * OUTPUT-ZEILEN                                                *
*      ****************************************************************
TITELZ   DS    0CL133
CCT      DS    CL1
         DC    CL55'D A T A   S E T   A U S W E R T U N G E N          *
               VOM '
TDATE    DC    CL8' '
         DC    CL14'         ***  '
PARMVOL  DC    CL8' '
         DC    CL3'***'
         DC    CL16' '
TPAGE    DC    CL6'  PAGE'
TSEITE   DC    CL4' '
         DC    CL18' '
*
UTITEL1  DS    0CL133
CCU1     DS    CL1
         DC    CL23'DATA SET AKTIVIT%T VOM '
VD11     DC    CL8' '
         DC    CL7'  BIS  '
VD12     DC    CL8' '
         DC    CL86' '
*
UTITEL2  DS    0CL133
CCU2     DS    CL1
         DC    CL29'FOLGENDE DATA SET SIND SEIT '
VD21     DC    CL8' '
         DC    CL7'  BIS  '
VD22     DC    CL8' '
         DC    CL32' NICHT MEHR ANGESPROCHEN WORDEN.'
         DC    CL48' '
ZEILE1   DS    0CL133
CCZ1     DC    XL1'09'
STRING   DS    CL44
CREDT    DS    CL8
         DC    CL7' '
COUNT    DC    CL6' '
         DC    CL4' MAL'
         DC    CL6' '
CHARDAT  DC    CL8' '
         DC    CL10' '
EXCP     DC    CL8' '
         DC    CL5' '
DEXCP    DC    CL8' '
         DC    CL18' '
*
ZEILE2   DS    0CL133
CCZ2     DC    XL1'09'
         DC    CL132' '
ZEILE6   DS    0CL133
CC5      DC    XL1'09'
         DC    CL47' '
         DC    CL5'CREDT'
         DC    CL4' '
         DC    CL14'ANGESPROCHEN:'
         DC    CL3' '
         DC    CL11'ZULETZT AM:'
         DC    CL12' '
         DC    CL5'TOTAL'
         DC    CL13'   DURCHSCHN.'
         DC    CL18' '
ZEILE5   DS    0CL133
CC6      DC    XL1'09'
         DC    CL102' '
         DC    CL6'EXCP''S'
         DC    CL24' '
*
*      ****************************************************************
*      * WORK-FELDER                                                  *
*      ****************************************************************
R        DS    CL1
SWC      DC    CL1' '
CRDAT    DC    PL4'0'
ACREDT   DC    A(CREDT)
RETURNCD DC    F'0'
PARM     DS    CL80
DW       DC    D'0'
DIV      DC    PL10'0'
DIVISOR  DC    PL3'0'
         LTORG
TABELLE  DS    400CL57 (44 DSN / 4 EXCP / 3 Z%HLER / 3 DATUM / 3 CREDT)
         END
