ZS905WRF CSECT
         REG
         XSAVE (R12),SVA,ZS905WRF ZIMPELMANN
         L     R3,0(R1)
         LA    R3,0(R3)                VL-SWITCHBIT LOESCHEN
         LTR   R3,R3
         BZ    CLOSDAT
         USING Y1,R3
         L     R8,4(R1)
         LA    R8,0(R8)                VL-SWITCHBIT LOESCHEN
         LA    R7,SYSOUT
         TM    DCBOFLGS-IHADCB(R7),DCBOFOPN
         BO    NOOPEN
         OPEN  (SYSOUT,(OUTPUT))
         ST    R3,SAVR3
         CLI   Y1TFLG,Y1TYES           TERM=YES ??????
         BE    NOOPEN
         LA    R1,L'AUSGABE
         STH   R1,DCBLRECL-IHADCB(R7)
         LA    R1,2660
         STH   R1,DCBBLKSI-IHADCB(R7)
         LA    R1,L'SOUTBER-1
         STH   R1,LAEAUSG
NOOPEN   EQU   *
         LH    R2,LAEAUSG
         EX    R2,EXMVC1
         PUT   SYSOUT,AUSGABE
         LH    R9,Y1MSGC
         LTR   R9,R9
         BZ    NOWTR
         CLI   Y1WFLG,Y1WYES
         BNE   NOWTR
         MVC   MESSAGE(80),0(R8)
         WTO   '                                                       *
                                       '
MESSAGE  EQU   *-82
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(79),MESSAGE
NOWTR    EQU   *
         B     ENDE
         SPACE 2
CLOSDAT  EQU   *
         L     R3,SAVR3
         CLI   Y1FUEB,Y1FYES              UEBERSCHRIFT SCHON GEDRUCKT
         BE    NOUEBER
         CALL  TDATE,(ZEIT,DATUM)
         AP    Y1SEIZ,=P'+1'
         MVC   SEITNR,MASKE
         ED    SEITNR,Y1SEIZ
         LH    R2,LAEAUSG
         EX    R2,EXMVC3
         PUT   SYSOUT,AUSGABE
         EX    R2,EXMVC4
         PUT   SYSOUT,AUSGABE
         EX    R2,EXMVC5
         PUT   SYSOUT,AUSGABE
         MVI   Y1FUEB,Y1FYES
NOUEBER  EQU   *
         MVC   CONDCOD,MASKE
         LH    R1,Y1COCO
         CVD   R1,DOUBLE
         ED    CONDCOD,DOUBLE+5
         LH    R2,LAEAUSG
         EX    R2,EXMVC2
         PUT   SYSOUT,AUSGABE
         CLOSE (SYSOUT)
ENDE     EQU   *
         XRETURN 0
         EJECT
SYSOUT   DCB   DDNAME=SYSOUT,MACRF=(PM),                               *
               DSORG=PS,LRECL=80,                                      *
               RECFM=FBA,BLKSIZE=2400
         EJECT
AUSGABE  DS    0CL133
SOUTBER  DS    CL133
EXMVC1   MVC   SOUTBER(0),0(R8)
EXMVC2   MVC   SOUTBER(0),ZEILE
EXMVC3   MVC   SOUTBER(0),ZEILE1
EXMVC4   MVC   SOUTBER(0),ZEILE2
EXMVC5   MVC   SOUTBER(0),ZEILE3
LAEAUSG  DC    H'79'
MASKE    DC    XL6'402020202120'
DOUBLE   DS    D
SAVR3    DS    F
         SPACE 2
ZEILE    DS    0CL133
         DC    CL1'-'
         DC    C' DER HOECHSTE CONDITION-CODE IST '
CONDCOD  DC    CL6' '
         DC    CL93' '
         SPACE 2
ZEILE1   DS    0CL133
         DC    CL1'1'
         DC    C'ZS905 L01     DB-DUMP-UTILITY     '
DATUM    DC    CL10' '
         DC    CL5' '
ZEIT     DC    CL8' '
         DC    C'     SEITE'
SEITNR   DC    CL6' '
         DC    CL59' '
         SPACE 2
ZEILE2   DS    0CL133
         DC    CL1'-'
         DC    C' FEHLERMELDUNG,COND.-CODEAUSGABE : '
         DC    CL98' '
         SPACE 2
ZEILE3   DS    0CL133
         DC    CL1' '
         DC    C' --------------------------------- '
         DC    CL98' '
         EJECT
Y1       ZS905COM
         EJECT
         PRINT NOGEN
         DCBD  DSORG=PS
         END
