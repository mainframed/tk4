LPTF     START
         XSAVE R12,SVA,LPTF
         REG
         USING ENTRY,R11
         L     R1,0(R1)
         LH    R2,0(R1)
         LTR   R2,R2
         BNZ   PARMOK
         OPEN  (SYSIN,(INPUT))
         MVI   SW1+1,X'F0'
NXTCARD  EQU   *
         MVC   HEAD1+98(3),=C'001'
         GET   SYSIN,CARD
         LA    R1,CARD-2
PARMOK   EQU   *
         MVC   LOW,2(R1)
         MVC   HIGH,8(R1)
         MVC   FMID,14(R1)
         MVC   SORT,22(R1)
         MVC   NBRFLDS+4(12),=H'8,1,12,1,1,7'
         CLI   SORT,C'D'
         BE    SORTOK
         MVC   NBRFLDS+4(12),=H'1,1,7,8,1,12'
SORTOK   NOP   OVER2
         MVI   SORTOK+1,X'F0'
         LA    R1,PARM
         LINK  EP=HMASMP
         GETMAIN R,LV=60000
         ST    R1,AREAADR
         B     OVER3
OVER2    L     R1,AREAADR
OVER3    EQU   *
         LR    R9,R1
         LR    R11,R1
         SH    R11,=H'12'
         OPEN  (SMPLIST,(INPUT))
         L     R10,=F'5000'
         SR    R8,R8
NXTREC   EQU   *
         GET   SMPLIST,LISTREC
         CLC   LISTREC+11(18),=CL18'TYPE            = '
         BNE   NXTREC
         CLC   LISTREC+29(10),=CL10'SUPERSEDED'
         BE    NXTREC
         MVC   PTFZWI,LISTREC+1
NXTREC1  GET   SMPLIST,LISTREC
         CLC   LISTREC+11(6),=CL6'STATUS'
         BNE   NXTREC1
         CLC   LISTREC+29(15),=CL15'REC '
         BE    NXTREC
NXTREC2  GET   SMPLIST,LISTREC
         CLC   LISTREC+11(10),=CL10'FMID'
         BNE   NXTREC2
         CLC   FMID,=C'NSELECT'
         BE    NXTREC21
         CLC   LISTREC+29(7),FMID
FMSEL    BNE   NXTREC
NXTREC21 GET   SMPLIST,LISTREC
         CLC   LISTREC+11(13),=CL13'DATE/TIME REC'
         BNE   NXTREC21
NXTREC3  GET   SMPLIST,LISTREC
         CLC   LISTREC+21(4),=CL4'APP'
         BE    DTEOK
         CLC   LISTREC+21(4),=CL4'ACC'
         BNE   NXTREC3
DTEOK    EQU   *
         MVC   DATZWI(2),LISTREC+29
         MVC   DATZWI+2(3),LISTREC+32
         CLC   DATZWI,LOW
         BL    NXTREC
         CLC   DATZWI,HIGH
         BH    NXTREC
         LA    R11,12(R11)
         LA    R8,1(R8)
         MVC   PTFNUM,PTFZWI
         MVC   DATE,DATZWI
         BCT   R10,NXTREC
         WTO   'LPTF02A TABLE OVERFLOW',ROUTCDE=11
FREE     EQU   *
         OPEN  (PTFTAB,(OUTPUT))
         TM    PTFTAB+48,X'10'
         BNO   NOTAB
         MVC   NBRFLDS+4(8),=H'1,1,7,0'
         MVC   FMID,=C'EJE1102'
         MVC   LOW,=C'00000'
         MVC   HIGH,=C'99999'
         MVI   FMSEL+1,X'80'
         MVI   SW3+1,X'F0'
         B     SORTOK
FREE1    EQU   *
         MVC   CARD,BLANK
         MVC   CARD(6),JFCB+118
         MVC   CARD+6(44),JFCB
         PUT   PTFTAB,CARD
         MVC   CARD,BLANK
         LR    R3,R9
         BCTR  R8,0
NXTAB    EQU   *
         MVC   CARD(7),0(R3)
         PUT   PTFTAB,CARD
         LA    R3,12(R3)
         BCT   R8,NXTAB
         CLOSE (PTFTAB)
NOTAB    EQU   *
         FREEMAIN R,LV=60000,A=(R9)
         CLOSE (SMPLIST,,LPTFOUT)
RETURN   XRETURN 0
LUSORT   EQU   *
         CLOSE (SMPLIST)
         STH   R8,NBRFLDS
         LTR   R8,R8
         BNZ   CALL
         MVI   SWITCH+1,X'F0'
         B     OPEN
CALL     EQU   *
         LA    R8,1(R8)
         LR    R3,R8
         CALL  LUSORT,((R9),SORTPRM),VL
SW3      NOP   FREE1
OPEN     OPEN  (LPTFOUT,(OUTPUT))
         RDJFCB SMPCDS
         MVC   HEAD1+48(13),JFCB
         MVC   HEAD1+37(6),JFCB+118
         CALL  DATE,(HEAD1+71),VL
         LR    R11,R9
         LA    R4,1
NXTPGE   EQU   *
         PUT   LPTFOUT,HEAD1
         PUT   LPTFOUT,SELECT
         PUT   LPTFOUT,HEAD2
         PUT   LPTFOUT,HEAD3
         PUT   LPTFOUT,BLANK
SWITCH   NOP   NOPUT
         MVC   OLDDTE,BLANK
         LA    R5,57
NXTLNE   EQU   *
         LA    R7,8
         LA    R6,OUTREC+1
NXTPTF   EQU   *
         BCT   R8,NOEND
         B     END
NOEND    EQU   *
         CLC   OLDDTE,DATE
         BE    NODATE
         MVC   0(5,R6),DATE
         MVC   OLDDTE,DATE
NODATE   EQU   *
         MVC   6(7,R6),PTFNUM
         LA    R11,12(R11)
         LA    R6,15(R6)
         BCT   R7,NXTPTF
         PUT   LPTFOUT,OUTREC
         MVC   OUTREC,BLANK
         BCT   R5,NXTLNE
         LA    R4,1(R4)
         CVD   R4,DW
         UNPK  HEAD1+98(3),DW
         OI    HEAD1+100,X'F0'
         B     NXTPGE
END      EQU   *
         CH    R7,=H'8'
         BE    NOPUT
         PUT   LPTFOUT,OUTREC
NOPUT    EQU   *
         CVD   R3,DW
         UNPK  FOOT1+11(4),DW
         OI    FOOT1+14,X'F0'
         PUT   LPTFOUT,FOOT1
SW1      NOP   NXTCARD
         B     FREE
EOD      EQU   *
         CLOSE SYSIN
         B     FREE
SYSIN    DCB   DDNAME=LPTFIN,MACRF=GM,DSORG=PS,RECFM=FB,LRECL=80,      *
               EODAD=EOD
SMPLIST  DCB   DDNAME=SMPLIST,DSORG=PS,MACRF=GM,EODAD=LUSORT
SMPCDS   DCB   DDNAME=SMPCDS,DSORG=PS,MACRF=PM,EXLST=EXLST
PTFTAB   DCB   DDNAME=LPTFTAB,DSORG=PS,MACRF=PM,                       *
               RECFM=FB,LRECL=80,BLKSIZE=8000
LPTFOUT  DCB   DDNAME=LPTFOUT,DSORG=PS,MACRF=PM,                       *
               RECFM=FBA,LRECL=133,BLKSIZE=133
EXLST    DS    0F
         DC    X'87',AL3(JFCB)
JFCB     DS    CL176
PARM     DC    A(STR)
         DS    0H
STR      DC    H'6',CL6'DATE=U'
LISTREC  DS    CL121
SORTPRM  DS    0F
NBRFLDS  DS    H
         DC    H'12,8,1,12,1,1,7,0'
OUTREC   DC    CL133' '
HEAD1    DC    CL133'1CHRONOLOGICAL PTF LISTING - CDS-VOL=123456,DSN=SY*
               S1.CDS      - DATE = 0123456789            PAGE 001'
BLANK    DC    CL133' '
DW       DS    D
HEAD2    DC    CL133'-DATE  PTF-NO.  DATE  PTF-NO.  DATE  PTF-NO.  DATE*
                 PTF-NO.  DATE  PTF-NO.  DATE  PTF-NO.  DATE  PTF-NO.  *
               DATE  PTF-NO.'
HEAD3    DC    C' ',118CL1'-',CL14' '
FOOT1    DC    C'-',CL132'THERE ARE 0000 ACTIVE SYSMODS'
SELECT   DC    C' SELECT: DATE='
LOW      DC    CL5'00000'
         DC    C'-'
HIGH     DC    CL5'99999'
         DC    C',FMID='
FMID     DC    CL7'NSELECT'
         DC    C',SORT='
SORT     DC    C'D'
         DC    CL112' '
OLDDTE   DC    CL5' '
PTFZWI   DC    CL7' '
DATZWI   DC    CL5' '
CARD     DC    CL80' '
AREAADR  DC    A(0)
ENTRY    DSECT
PTFNUM   DS    CL7
DATE     DS    CL5
         END
