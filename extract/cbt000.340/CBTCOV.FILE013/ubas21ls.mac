* STAND 06/22/79
***********************************************************************
*                                                                     *
*    PROGRAMM   = OPNABLT                                             *
*                                                                     *
*    ENTRYPOINT = OPNABLT                                             *
*                 -------                                             *
*    AUFRUF    = CALL OPNABLT                                         *
*                                                                     *
*    ZWECK      = DDNAME ASTBLATT OPEN                                *
*                 BEI LISTENKOPIE WIRD AUF ASTLIST AUSGEGEBEN
*                 FEHLT ASTLIST, WIRD KOPIE AUF ASTBLATT
*                 GEDRUCKT
*                                                                     *
*    RETURNCODE REG15 =                                               *
*               = 0    DCB OPEN                                       *
*               = 4    DCB OPEN 1.AUFRUF                              *
*               = 8    DDKARTE FEHLT                                  *
*                                                                     *
***********************************************************************
         PRINT NOGEN
         SPACE 2
OPNABLT  CSECT
         XSAVE R12,SAVE,OPNABLT
         SPACE 1
         AREG
RA       EQU   R7                       ATAB
         L     RA,PATAB                 --> ATAB
         USING DATAB,RA
         SPACE 2
*  DCB DER ABSTIMMLISTE PRUEFEN
         MVC   RETCODE,=F'0'
         TM    LDCBSW,DCBTEST
         BO    RETERR                   DDKARTE FEHLT
         BM    DCBOK                    DCB OPENED
*  OPEN DCB DER ABSTIMMLISTE
*  IM JFCB VON ASTBLATT  DISP=MOD SETZEN
         TM    JFCBD,X'FF'
         BNO   OPNBLATT       NICHT 1.DURCHLAUF
*  BEI LISTENKOPIE DDNAME AUF ASTLIST ABAENDERN
         TM    ALCOPY,RYES    LISTENKOPIE
         BNO   OPN60          NEIN
         MVC   ASTLIST+40(8),=CL8'ASTLIST'
OPN60    EQU   *
         RDJFCB (ASTLIST,(OUTPUT))
         NI    JFCBD+87,X'0F'
         OI    JFCBD+87,X'80' MOD-BIT
         OI    JFCBD+52,X'08'
         MVC   RETCODE,=F'4'
OPNBLATT OPEN  (ASTLIST,(OUTPUT)),TYPE=J
         TM    ASTLIST+48,X'10'
         BNZ   DCBOK                    DCB OPEN
*  FEHLT BEI LISTENKOPIE ASTLIST, WIRD LISTE AUF ASTBLATT AUSGEGEBEN
         CLC   ASTLIST+40(8),=CL8'ASTLIST'
         BNE   OPN80
         MVC   ASTLIST+40(8),=CL8'ASTBLATT'
         B     OPN60          VERSUCH MIT ASTBLATT
OPN80    EQU   *
         MVI   LDCBSW,DDFEHLT           DDKARTE FEHLT
         B     RETERR
*  DCB OPENED
DCBOK    EQU   *
         MVI   LDCBSW,DCBOPEN           DCB OPENED
ENDE     EQU   *
         XRETURN              RETCODE
*
RETERR   EQU   *
         MVC   RETCODE,=F'8'
         B     ENDE
*
         EJECT
         ENTRY ABLTPUT
ABLTPUT  XSAVE R12,SAVE,ABLTPUT
         L     RA,PATAB       -->ATAB
*
         L     R4,0(R1)       -->ZU DRUCKENDE ZEILE
*
         LR    R3,R4
         BAL   R11,UPASA
         CP    ZEIZAE,=P'60'
         BL    DRARG
*              U E B E R S C H R I F T  D R U C K E N
*              --------------------------------------
*
WRUEB    EQU   *
         LA    R3,UEB
         BAL   R11,LOESCH
         MVI   UEB,C'1'               VORSCHUB NEUE SEITE
         MVC   UEB+1(9),APGM          PROGRAMMNAME
         MVC   UEB+29(23),=C'A B S T I M M B L A T T'
         AP    SEIZAE,=PL1'1'          SEITENZAEHLER + 1
         MVC   UEB+58(5),=C'SEITE'
         MVC   UEB+63(2),=X'4020'    MASKE
         ED    UEB+63(2),SEIZAE
         MVC   UEB+69(8),AAST         ABSTIMMLISTEN-NR
         TM    ALCOPY,RYES
         BNO   WRUEB50        NICHT LISTENKOPIE
         MVC   UEB+77(3),ALCNR  LISTENNR KOPIE
WRUEB50  EQU   *
         BAL   R11,PRINT                UEB DRUCKEN
*        MVC   UEB+72(8),AASTOLD     ALTE ALIST-NR
         BAL   R11,PRINT
         MVC   UEB+1(33),AUEB         VON KARTEN EINGELESENER TEXT
         L     R2,PBZEIT                -->BERICHTSZEIT
         MVC   UEB+36(15),0(R2)       BERICHTSZEIT
         MVC   UEB+52(8),ASBZT  ZUSAETZLICHE BERICHTSZEIT
         MVC   UEB+72(8),UHRZEIT
         MVC   UEB+61(10),DATUM
         BAL   R11,PRINT                UEB DRUCKEN
         MVC   UEB+1(15),=C'NR BEZEICHNUNG '
         MVC   UEB+16(49),UEB+15    UEB LOESCHEN
         MVC   UEB+49(05),=C'SUMME'
         MVC   UEB+65(15),=C'ABZUSTIMMEN MIT'
         BAL   R11,PRINT                UEB DRUCKEN
         MVI   UEB,C' '               VORSCHUB 1 UEB
         MVI   UEB+1,C'-'
         MVC   UEB+2(78),UEB+1      UNTERSTREICHUNG
         BAL   R11,PRINT                ZEILE DRUCKEN
         ZAP   ZEIZAE,=P'6'             ZEILENZAEHLER = 6
         BAL   R11,PRINT                ZEILE DRUCKEN
DRARG    EQU   *
         LR    R3,R4
         BAL   R11,PRINT      ZU DRUCKENDE ZEILE
         XRETURN
         SPACE 2
*              TESTEN ASA-VORSCHUBZEICHEN
*                   ZEILENVORSCHUB (ASA) IN ZEILE
*
UPASA    EQU   *
         CLI   0(R3),C'-'               3 0(R3)NVORSCHUEBE
         BE    UPASA1
         CLI   0(R3),C'0'               2 0(R3)NVORSCHUEBE
         BE    UPASA2
         MVI   0(R3),C' '               VORSCHUB 1 0(R3)
         B     UPASA3                   1 ZEILENVORSCHUB
UPASA1   AP    ZEIZAE,=P'1'             ZEILENZAEHLER ERHOEHEN
UPASA2   AP    ZEIZAE,=P'1'
UPASA3   AP    ZEIZAE,=P'1'
         BR    R11                      RUECKSPRUNG
         SPACE 2
*    AUFBEREITETE DRUCKZEILE AUSGEBEN
PRINT    EQU   *
         PUT   ASTLIST,(R3)
LOESCH   EQU   *
         MVI   0(R3),C' '
         MVC   1(80,R3),0(R3)  DRUCKBEREICH LOESCHEN
         BR    R11
         EJECT
*              D E F I N I T I O N E N
*              -----------------------
*
PATAB    DC    V(ATAB)                  --> ATAB
UEB    DC    CL81' '        DRUCKBEREICH
         PRINT GEN
RETCODE  DS    F              RETURNCODE
         SPACE 2
*              DCB
         ENTRY ASTLIST
ASTLIST  DCB   DDNAME=ASTBLATT,DSORG=PS,MACRF=(PM),                    *
               EXLST=EXLSTD,                                           *
               LRECL=81,BLKSIZE=810,RECFM=FBA
         ENTRY JFCBD
JFCBD    DC    176XL1'FF'
EXLSTD   DS    0F
         DC    X'87'
         DC    AL3(JFCBD)
SAVE     DS    18F
         SPACE 1
         PRINT GEN
         MATAB
         END
