         MACRO
&N       SM10  &LAENGE=,&DEFAULT=NO,&SPACE=6,&PRTNDX=
* AUFRUF   NAME SM10 (K1,L1),(K2,L2),...,LAENGE=L,DEFAULT=YES,SPACE=8,*
*              PRTNDX=R08                                             *
*        LAENGE   ANZAHL ZEILEN JE FORMULAR                           *
*        DEFAULT  YES ODER NO                                         *
*        SPACE    ANZAHL ZEILEN JE ZOLL                               *
*        (K1,L1)  KANAL K1 IN ZEILE L1, ANALOG  FUER ALLE             *
*                 KANALSTEUERZEICHEN. DIE ANGABEN L1,L2,... AUFSTEIGEND
*        PRTNDX   PRINT POSITION INDEXING, R=RECHTS, L=LINKS          *
         LCLA    &I,&NP,&J,&K,&L,&M
         LCLC    &C
&L       SETA  &LAENGE
&NP      SETA  N'&SYSLIST
FCB2&N   CSECT
         AIF   ('&DEFAULT' EQ 'NO').M010
         AIF   ('&DEFAULT' EQ 'YES').M020
         MNOTE 12,'FALSCHER DEFAULT PARAMETER'
         MEXIT
.M010    DC    X'00' .            NO DEFAULT IMAGE
         AGO   .M030
.M020    DC    X'80' .            DEFAULT IMAGE
.M030    ANOP
         AIF   (&NP GE 1).M040
         MNOTE 12,'KANALANGABEN FEHLEN'
         MEXIT
.M040    ANOP
         AIF   (T'&PRTNDX  EQ  'O').M045  KEINE DRUCKSTEELLENVERSCHIEB.
         AIF   ('&PRTNDX'(1,1)  EQ  'R').M041  RECHTSVERSCH.
         AIF   ('&PRTNDX'(1,1)  EQ  'L').M042  LINKSVERSCH.
&C       SETC  '&PRTNDX'(1,1)
         IHBERMAC 241,PRTNDX,&C
         MEXIT
.M041    ANOP
&I       SETA  64
.M042    ANOP
&C       SETC  '&PRTNDX'(2,2)
         DC    YL1(&LAENGE+1) .   LAENGE DES FCB
         DC     YL1(&I+64+&C) .   DRUCKSTELLENVERSCHIEBUNG
         AGO   .M046
.M045    ANOP
         DC    YL1(&LAENGE) .     LAENGE DES FCB
.M046    ANOP  ,
&C       SETC  '0'
         AIF   ('&SPACE' NE '8').M050
&C       SETC  '1'
.M050    ANOP
&I       SETA   &SYSLIST(1,2)
         AIF   (&I EQ 1).M060
         DC    X'&C.0' .          ZEILE 1
&K       SETA  1                  KLAMMER ZAEHLER
.*       J = ZEILEN ZAEHLER       ZEILENZAEHLER
         AGO   .M070
.M060    DC    YL1(16*&C+&SYSLIST(1,1)) KANAL IN ZEILE 1
&K       SETA  2                  KLAMMERZAEHLER
.M070    ANOP
&J       SETA  2
.*       LOOP
.M080    AIF  (&K LE &NP).M090    KLAMMERN NOCH NICHT ABGEARBEITET
.*                                KEINE STEUERZEICHEN MEHR VORH.
&M       SETA  &L-&J
         DC    &M.X'00' .         ZEILEN OHNE KANALSTEUERZEICHEN
         DC    X'10' .            LETZTE ZEILE OHNE KANALSTEUERZEICHEN
         END
         MEXIT ,                  ENDE
.M090    ANOP
&M       SETA &J
&I       SETA &SYSLIST(&K,2)      POS. NAECHSTES KANALSTZ.
.M100    AIF   (&J NE &L).M130    NICHT LETZTE ZEILE
         AIF   (&J EQ &I).M110    LETZTE ZEILE MUSS KANALSTZ. ENTHALTEN
         MNOTE 12,'FALSCHE ZEILENNR'
         MEXIT
.M110    ANOP
&M       SETA  &J-&M-1
         AIF   (&M  EQ  0).M115
         DC    &M.X'00' .         ZEILEN OHNE KANALSTZ.
.M115    DC    YL1(16+&SYSLIST(&K,1)) LETZTE ZEILE
         MEXIT
.M130    AIF   (&J EQ &I).M140    ZEILE MIT KANALSTZ. ERREICHT
&J       SETA  &J+1               NAECHSTE ZEILE
         AGO   .M100
.M140    AIF   (&J EQ &M).M150
&M       SETA  &J-&M
         DC    &M.X'00' .         ZEILEN OHNE KANALSTEUERZEICHEN
.M150    MNOTE *,'KANAL &SYSLIST(&K,1) IN ZEILE &J'
&M       SETA  &SYSLIST(&K,1)     KANALSTEUERZ.
         DC    YL1(&M) .          ZEILE MIT KANALSTEUERZEICHEN
&J       SETA  &J+1
&K       SETA  &K+1
         AGO   .M080
         MEND
