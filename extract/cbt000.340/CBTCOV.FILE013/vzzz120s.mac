PDS      TITLE 'P D S    -   TSO PDS PROCESSOR'
*
*   DATA SET PDS AT LEVEL 4 AS OF  9/30/80  (ABL -- MVS ONLY VERSION)
*   DATA SET PDS AT LEVEL 3 AS OF  1/28/80  (ABL -- FACSO MVS VERSION)
*   DATA SET PDS AT LEVEL 2 AS OF 11/14/77  (ABL -- FACSO MVT VERSION)
*   DATA SET PDS AT LEVEL 1 AS OF  6/12/75  (ORIGINAL UCLA VERSION)
*   DATA SET PDS AT LEVEL 0 AS OF  3/20/72  (FIREMAN'S FUND VERSION)
*
*** ***
*** ***  BEFORE ASSEMBLING, READ THE FOLLOWING NOTE EXPLAINING THE
*** ***  VALUES WHICH MAY BE CODED FOR THE SYSPARM VALUE (XXXX BELOW).
*** ***
*** ***  TO ASSEMBLE:  ASM  PDS RENT LINECOUNT(53) LIB('SYS1.AMODGEN')
*** ***                         SYSPARM('XXXX')
*** ***
*** ***  TO LINK:      LINK PDS RENT REUS REFR LOAD(LIBNAME(PDS)) PR(*)
*
*   NOTE: THIS PROGRAM ALLOCATES THE PARTITIONED DATA SETS IT OPERATES
*         ON WITH DISP=SHR.  SINCE THIS PROGRAM CAN ALTER THE DIRECTORY
*         WITH SEVERAL OF ITS SUBCOMMANDS (ALIAS, ATTR, DELETE, RENAME
*         AND RESTORE), THIS CAN CAUSE INTEGRITY PROBLEMS IN A MODIFIED
*         DATA SET.  CONSEQUENTLY, A MODIFICATION TO REALLOCATE THIS
*         PROGRAM'S DATA SETS WITH DISP=OLD AFTER ONE OF THE ABOVE
*         SUBCOMMANDS HAS BEEN ADDED IF THE USER DOES NOT HAVE THE
*         "OPER" CAPABILITY.
*
*         IT HAS BEEN NOTED THAT THIS MODIFICATION DOES NOT PLEASE
*         EVERYONE.  THEREFORE, A SYSPARM ASSEMBLY OPTION HAS BEEN
*         ADDED TO CONTROL THIS FEATURE:
*           A.  FOR SYSPARM('SHR'), THE DATA SET WILL NOT BE ALLOCATED
*               AS OLD FOR ANY USER.
*           B.  FOR SYSPARM('OPER') OR IF NO SYSPARM OPERAND IS CODED,
*               THE DATA SET WILL BE ALLOCATED AS OLD ONLY FOR USERS
*               WHO DO NOT HAVE "OPER" CAPABILITY AFTER ONE OF THE
*               ABOVE-MENTIONED SUBCOMMANDS IS ISSUED.
*           C.  FOR SYSPARM('OLD'), THE DATA SET WILL BE REALLOCATED
*               AS OLD FOR ANY USER AFTER ONE OF THE ABOVE-MENTIONED
*               SUBCOMMANDS IS ISSUED.
*
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
*
*
*
*        PLEASE REPORT ANY PROBLEMS, ENHANCEMENTS, SUGGESTIONS OR
*        COMMENTS CONCERNING THE PDS COMMAND TO:
*            BRUCE LELAND     (805) 497-5323  (MONDAYS - FRIDAYS)
*            P.O. BOX 235
*            PORT HUENEME, CALIF. 93041
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* TITLE -      P D S    ----   TSO PDS PROCESSOR                      *
*                                                                     *
* FUNCTION -   PROVIDE A TSO USER WITH THE CAPABILITY                 *
*              TO MANIPULATE A PARITIONED DATA SET                    *
*                                                                     *
* OPERATION -  ACCEPT FROM THE TSO USER THE NAME OF A                 *
*              PARTITIONED DATA SET NAME. AFTER ALLOCATING            *
*              THE DATA SET, PERFORM ANY OF THE SUBCOMMANDS:          *
*                                                                     *
*        ALIAS    - ASSIGN AN ALIAS TO A REAL MEMBER                  *
*        ATTRIB   - DISPLAY (OR MODIFY) LOAD MODULE ATTRIBUTES        *
*        CHANGE   - CHANGE TO A DIFFERENT PARTITIONED DATA SET        *
*        DISPLAY  - DISPLAY ALL OR PART OF THE DIRECTORY              *
*        DELETE   - SCRATCH A MEMBER                                  *
*        EDIT     - EDIT A MEMBER (ALLOWS NONUM, ASIS AND EDIT TYPE)  *
*        END      - TERMINATE THE PDS COMMAND                         *
*        EXEC     - EXECUTE PDS COMMANDS FROM A CLIST SOURCE          *
*        FIND     - LIST LINES OF A MEMBER CONTAINING A SEARCH STRING *
*        HELP     - PROVIDE SUBCOMMAND HELP FOR PDS                   *
*        HISTORY  - LIST HISTORY OF A LOAD MODULE                     *
*        LIST     - LIST CONTENTS OF A MEMBER                         *
*        MAP      - MAP LOAD MODULE STRUCTURE                         *
*        OPTIONS  - DISPLAY THE SUBCOMMAND MENU                       *
*        PATTERN  - DISPLAY DIRECTORY BASED ON MEMBER NAME SEGMENTS   *
*        PDS      - AN ALIAS FOR THE CHANGE SUBCOMMAND                *
*        RENAME   - RENAME A MEMBER                                   *
*        RESTORE  - RESURRECT A MEMBER PREVIOUSLY DELETED             *
*        SMAP     - SHORT MAP OF LOAD MODULE STRUCTURE                *
*        SCRATCH  - DELETE A MEMBER                                   *
*        SUBMIT   - SUBMIT A MEMBER (JCL) TO BACKGROUND               *
*        USAGE    - LIST DATA SET STATISTICS                          *
*                                                                     *
*                                                                     *
* INPUT -      STANDARD COMMAND PROCESSOR PARAMETER LIST              *
*              POINTED TO BY REGISTER 1                               *
*                                                                     *
* OUTPUT -     NOT APPLICABLE.                                        *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSEABLE, REFRESHABLE.                     *
*                                                                     *
* WRITTEN - IN 1972 BY FIREMAN'S FUND AMERICAN INSURANCE.             *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  CHANGE SECTION:                                                    *
*                                                                     *
*    1.  VER 2: ADDED FORMATTING OPTIONS FOR LIST; ADDED SUBCOMMANDS  *
*        EDIT, FIND AND SUBMIT; AND REORGANIZED THE PDS PROGRAM.      *
*                                                                     *
*    2.  VER 2.2: CORRECTED AN ERROR IN LIST AND FIND FOR DATA SETS   *
*        WITH ASA OR MACHINE CARRIAGE CONTROLS AND ALLOWED THE USE    *
*        OF ALL SUBCOMMANDS BY ALL USERS.                             *
*                                                                     *
*    3.  VER 2.3: MADE SEVERAL IMPROVEMENTS AS SUGGESTED BY DINESH    *
*        DATTANI FROM PRUDENTIAL INSURANCE CO. OF AMERICA, TORONTO    *
*        A.  OUTPUT THE PROGRAM OPTION LIST IN SORTED ORDER.          *
*        B.  ADDED SEVERAL EDIT DESCRIPTIVE QUALIFIERS (BASIC, IPLI   *
*            AND VSBASIC) AS WELL AS INSTRUCTIONS FOR ADDING OTHERS.  *
*        C.  ADDED A LOCKING MECHANISM FOR ATTR (WITH CHANGED ATT-    *
*            RIBUTES), ALIAS, DELETE, RENAME AND RESTORE SUBCOMMANDS. *
*            WHEN ANY OF THE ABOVE SUBCOMMANDS ARE USED FOR THE FIRST *
*            TIME BY A USER WITHOUT "OPER" CAPABILITY, THE PDS IN USE *
*            IS CLOSED AND REALLOCATED WITH DISP=OLD.  NOTE THAT THE  *
*            REALLOCATION WILL FAIL IF ANOTHER USER HAS THE DATA SET  *
*            ALLOCATED.  THIS ALSO PROVIDES A SECURITY ENHANCEMENT    *
*            FOR INSTALLATIONS WITH PCF IN THAT USERS WHO HAVE ONLY   *
*            SHARED ACCESS TO DATA SETS CANNOT MODIFY THEM WITH THE   *
*            PDS COMMAND.                                             *
*                                                                     *
*    4.  VER 3: UPDATED THE PDS COMMAND FOR MVS USE -- GENERALLY TO   *
*        THE LEVEL OF THE MVS CBT VERSION (FILE 182 DATED 9/76)       *
*        A.  ADDED EXEC (EXPLICIT AND IMPLIED) AND HELP SUBCOMMANDS.  *
*        B.  A SINGLE SUBCOMMAND CHARACTER NAME CAN NO LONGER BE      *
*            CONSIDERED AMBIGUOUS (THE FIRST COMMAND TABLE MATCH      *
*            IS USED RATHER THAN OUTPUTTING A ERROR MESSAGE).         *
*        C.  SUPPORT WAS ADDED IN THE FOLLOWING AREAS:                *
*            -- SSI INFORMATION DISPLAY                               *
*            -- APF DATA CAN BE LISTED AND MODIFIED                   *
*            -- HEXADECIMAL MEMBER NAMES ALLOWED AS INPUT             *
*            -- HEXADECIMAL OUTPUT ADDED FOR DISPLAY (AND PATTERN)    *
*            -- $PUTGET, $PUTLINE AND MESSAGE MACROES WERE ADDED      *
*            -- ATTRIB LISTS ALL ALIASES FOR A GIVEN MODULE           *
*        D.  SEVERAL SOURCES OF 0C7'S WERE CORRECTED IN THE HANDLING  *
*            OF IDR DATA.                                             *
*        E.  THE CSECT STRUCTURE OF THIS PDS COMMAND WAS RETAINED     *
*            (NOT SEPARATE CSECTS AS IN THE CBT VERSION).             *
*        F.  THE MEMBER NAME VALIDITY CHECK ROUTINE HAD TWO BUGS:     *
*            --  THE MEMBER NAME LENGTH USED WAS THE INPUT NAME       *
*                LENGTH (INCORRECT FOR HEXADECIMAL NAMES).            *
*            --  IF A MEMBER NAME WAS CONSIDERED INVALID, THE NEXT    *
*                MEMBER NAME PASSED TO THE ROUTINE WAS NOT CONSIDERED *
*                A REPLACEMENT OF THE PREVIOUS INCORRECT MEMBER NAME. *
*        G.  THE ATTRIB SUBCOMMAND SOMETIMES LISTED SSI INFORMATION   *
*            FOR NON-LOAD MODULES WHICH DID NOT HAVE ANY SSI DATA.    *
*                                                                     *
*    5.  VER 3: SEVERAL GENERAL CHANGES WERE MADE (MVS VERSION)       *
*        A.  PDS HELP DATA SET REWORKED TO CURRENT PROGRAM LEVEL.     *
*        B.  SCREEN CLEAR MACRO WAS REMOVED SINCE IT WAS NOT USED.    *
*        C.  SMAP AND PATTERN SUBCOMMANDS WERE ADDED.                 *
*        D.  MAP AND SMAP PROVIDE THE NAME OF THE ENTRY POINT.        *
*        E.  DEFAULT MEMBER NAMES ARE ALLOWED FOR ATTR, EDIT,         *
*            FIND, HISTORY, LIST, MAP, SMAP AND SUBMIT SUBCOMMANDS.   *
*        F.  PDS WAS ADDED AS AN ALIAS FOR THE CHANGE SUBCOMMAND.     *
*        G.  LOAD AND DELETE LOGIC FOR TSO MODULES WAS CHANGED        *
*            (LOADS AND DELETES NOW REMOVED -- ABL)                   *
*        H.  ALIAS SUBCOMMAND IS NOT PREVENTED FROM WORKING IF THE    *
*            APF DATA LENGTH IS INVALID (APF=0 IS USED).              *
*                                                                     *
*    6.  DATA SET REALLOCATION CAN OCCUR FOR ANY OF THE FOLLOWING     *
*        REASONS:                                                     *
*        1.  CHANGE SUBCOMMAND ENTERED BY THE USER.                   *
*        2.  UPDATE LOCK FOR ALIAS, ATTR, DELETE, RENAME OR RESTORE   *
*            AS IN ITEM 3.C, ABOVE).                                  *
*        3.  DISP=OLD ALLOCATION FAILURE -- REALLOCATION IS MADE      *
*            WITH DISP=SHR AGAIN AND THE SUBCOMMAND IS IGNORED.       *
*                                                                     *
*    7.  EDIT SUBCOMMAND WITH DEFAULT MEMBER NAME USED THE PREVIOUS   *
*        PDL.  NOW, THE PDL IS CLEARED BEFORE COMMAND SCAN.           *
*                                                                     *
*    8.  UNPRINTABLE MEMBER NAMES (FOR SUBCOMMANDS DISPLAY AND        *
*        PATTERN) NOW HAVE THEIR UNPRINTABLE CHARACTERS TRANSLATED    *
*        TO CENT SIGNS (Ö) SO THAT THE REMAINDER CAN BE DISPLAYED.    *
*        NOTE THAT A HEXADECIMAL/CHARACTER DISPLAY OF A MEMBER NAME   *
*        (FOR THE DISPLAY OR PATTERN SUBCOMMANDS) IS FORCED IF ANY    *
*        UNPRINTABLE (OR LOWER-CASE) CHARACTERS APPEAR IN THE MEMBER  *
*        NAME; ALSO, A 3270-TYPE DISPLAY TERMINAL IS ASSUMED.         *
*                                                                     *
*    9.  DISP=OLD REALLOCATION OF DATA SETS (AS IN 3.C ABOVE) HAS     *
*        BEEN MADE A PROGRAM GENERATION OPTION AS SUGGESTED BY        *
*        ARNOLD CASINGHINO FROM CONNECTICUT BANK AND TRUST (CBT).     *
*                                                                     *
*   10.  VER 4: MADE SEVERAL IMPROVEMENTS AS SUGGESTED BY SEYMOUR     *
*        METZ FROM COMNET IN WASHINGTON, D.C.:                        *
*        1.  REPLACED IBM/360 INSTRUCTIONS WITH THEIR IBM/370         *
*            EQUIVALENTS (ICM, STCM, MVCL, ...)                       *
*        2.  REPLACED BCR INSTRUCTIONS WITH EQUIVALENT MNEMONICS.     *
*        3.  TSO SERVICE ROUTINE ADDRESSES ARE NOW OBTAINED FROM      *
*            THE CVT INSTEAD OF VIA LOAD/DELETE CODE.                 *
*                                                                     *
*   11.  VER 4.1: SEVERAL ADDITIONAL CHANGES WERE MADE TO TAKE        *
*        ADVANTAGE OF MVS PROGRAM LOGIC AND SERVICE ROUTINES:         *
*        1.  USED THE DAIRFAIL MESSAGE ROUTINE INSTEAD OF INTERNALLY  *
*            GENERATING MESSAGES (FIRST SUGGESTED BY S. METZ)         *
*        2.  USED THE GENERAL FAIL MESSAGE ROUTINE FOR PARSE ERRORS.  *
*        3.  USED THE TSO DEFAULT ROUTINE TO FULLY QUALIFY DATA       *
*            SET NAMES.                                               *
*        4.  CHANGED THE ALLOCATION STRATEGY TO "PERMANENTLY"         *
*            ALLOCATE DATA SETS.  THEN, WHEN AN EDIT IS PERFORMED     *
*            FOR A MEMBER BY THE PDS COMMAND, THE ALLOCATED DATA      *
*            SET WILL BE USED FOR EDIT.  THUS, AN UNCATALOGED DATA    *
*            SET MAY BE USED FOR EDIT (ONCE IT IS ALLOCATED BY THE    *
*            PDS COMMAND).  THIS CHANGE WAS FIRST SUGGESTED BY JOE    *
*            RAREY FROM MARTIN-MARRIETA IN FLORIDA.                   *
*                                                                     *
*   12.  VER 4.2:  AFTER THE DAIRFAIL CHANGES INDICATED ABOVE, THE    *
*            VOLUME PARAMETER FOR UNCATALOGED DATA SETS DID NOT WORK. *
*            ALSO, MORE OF THE PROGRAM'S DSECTS ARE EXPANDED.         *
*                                                                     *
*   13.  VER 4.3:  CORRECTED ONE SOURCE OF 0C4'S IN LISTING HISTORY   *
*            DATA AS REPORTED BY ARNIE CASINGHINO FROM CBT.           *
*                                                                     *
*   14.  VER 4.4:  ADDED THE RESTORE SUBCOMMAND AND CLEANED UP SOME   *
*            PDS CODE AS SUGGESTED BY S. METZ OF COMNET IN WASH D.C.  *
*            IN ADDITION, A SYSPARM ASSEMBLY PARAMETER WAS ADDED TO   *
*            PROVIDE MORE EASE IN GENERATING PDS.                     *
*                                                                     *
*   15.  FUTURE PLANS:                                                *
*            REPLACE THE SCRATCH SUBCOMMAND BY THE DELETE SUBCOMMAND. *
*                                                                     *
*            PRINT: A SUBCOMMAND TO LINK TO THE PRINTOFF COMMAND FOR  *
*            A MEMBER (SUGGESTED BY DAVID FILSINGER OF FACSO).        *
*                                                                     *
*            SCROLL: A SUBCOMMAND TO LINK TO THE O.S. VERSION OF THE  *
*            LIST COMMAND FOR THOSE INSTALLATIONS WHICH HAVE A FULL-  *
*            SCREEN LISTER (SUGGESTED BY S. METZ).                    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         MACRO
&NAME    CLEAR &FIELD
         LCLC  &L
&L       SETC  'L'''
&NAME    MVI   &FIELD,C' '
         MVC   &FIELD+1(&L&FIELD-1),&FIELD
         MEND
         SPACE 1
         MACRO
&NAME    ENTER &TYPE
         LCLC  &LABEL
&LABEL   SETC  'IHB'.'&SYSNDX'
         AIF   ('&TYPE' NE '').A1
&NAME    CSECT
         SAVE  (14,12),,*
         LR    BASEREG1,R15
         USING &NAME,BASEREG1,BASEREG2,BASEREG3,BASEREG4
         LA    BASEREG4,2048
         LA    BASEREG2,2048(BASEREG1,BASEREG4)
         LA    BASEREG3,2048(BASEREG2,BASEREG4)
         LA    BASEREG4,2048(BASEREG3,BASEREG4)
         MEXIT
.A1      AIF   ('&TYPE' NE 'VALCHECK').A2
&NAME    SAVE  (14,12)
         L     WORKREG,4(R1)      WORK AREA ADDRESS
         LM    BASEREG1,BASEREG4,BASES RESTORE BASE REGISTERS
         L     R7,0(R1)       PDE ADDRESS
         LA    R15,VALSAVE
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         SPACE 3
         MEXIT
.A2      ANOP
         AIF   ('&TYPE' NE 'ATTNEXIT').A3
&NAME    SAVE  (14,12)
         L     WORKREG,8(R1)
         LM    BASEREG1,BASEREG4,BASES
         LA    R15,ATTNSAVE
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         SPACE 2
         MEXIT
.A3      ANOP
         MNOTE 8,'INVALID TYPE ''&TYPE'''
         MEND
         SPACE 6
         MACRO
&NAME    EXIT  &LV=
&NAME    DS    0H
         AIF   ('&LV' EQ '').A1
         LR    R2,R13         ADDR OF THIS SAVE AREA
.A1      L     R13,4(R13)
         STM   R15,R1,16(R13) RETURN REGS 15, 0, 1
         AIF   ('&LV' EQ '').A2 NOT DYNAMIC CORE
         FREEMAIN R,LV=&LV,A=(R2)
.A2      SPACE
         RETURN (14,12),T
         MEND
         SPACE 2
         MACRO
&NAME    MSG   &TEXT
         LCLA  &A
&A       SETA  K'&TEXT-2+4
&NAME    DC    H'&A',H'0',C&TEXT
         MEND
         SPACE 3
         MACRO $PUTGET -  OUTPUT A DATA LINE AND GET RESPONSE
&NAME   $PUTGET  &LINE,&ATTN=
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&LINE' EQ '(1)' OR '&LINE' EQ '(R1)').CALL
         AIF   ('&LINE'(1,1) EQ '(').R
         LA    R1,&LINE              ADDRESS OF DATA LINE
         AGO   .CALL
.R       ANOP
         LR    R1,&LINE(1)                  ADDRESS OF DATA LINE
.CALL    ANOP
         BAL   R14,$PUTGET              INVOKE PUTGET INTERFACE
         AIF   ('&ATTN' EQ '').NOATTN
         B     &ATTN                 EXIT IF ATTENTION OCCURRED
         AGO   .END
.NOATTN  NOP   0                        IGNORE ATTENTIONS
.END     MEND
         SPACE 15
         MACRO $PUTLINE -  OUTPUT A DATA LINE
&NAME   $PUTLINE &LINE,&ATTN=
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&LINE' EQ '(1)' OR '&LINE' EQ '(R1)').CALL
         AIF   ('&LINE'(1,1) EQ '(').R
         LA    R1,&LINE              ADDRESS OF DATA LINE
         AGO   .CALL
.R       ANOP
         LR    R1,&LINE(1)                  ADDRESS OF DATA LINE
.CALL    ANOP
         BAL   R14,$PUTLINE             INVOKE PUTLINE INTERFACE
         AIF   ('&ATTN' EQ '').NOATTN
         B     &ATTN                 EXIT IF ATTENTION OCCURRED
         AGO   .END
.NOATTN  NOP   0                        IGNORE ATTENTIONS
.END     MEND
         SPACE 2
         MACRO MESSAGE - OUTPUT AN ERROR/STATUS MESSAGE
&NAME    MESSAGE &MSG1,&MSG2
         AIF   ('&MSG1' NE '').MSG1OK
         MNOTE 12,'PRIMARY MESSAGE ADDRESS NOT SPECIFIED'
         AGO   .END
.MSG1OK  ANOP
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&MSG1' EQ '(1)' OR '&MSG1' EQ '(R1)').MSG2
         AIF   ('&MSG1'(1,1) EQ '(').RMSG1
         LA    R1,&MSG1                 ADDRESS OF PRIMARY MESSAGE
         AGO   .MSG2
.RMSG1   ANOP
         LR    R1,&MSG1(1)              ADDRESS OF PRIMARY MESSAGE
.MSG2    ANOP
         AIF   ('&MSG2' EQ '').NOMSG2 NO SECONDARY MESSAGE
         AIF   ('&MSG2' EQ '(0)' OR '&MSG2' EQ '(R0)').CALL
         AIF   ('&MSG2'(1,1) EQ '(').RMSG2
         LA    R0,&MSG2                 ADDRESS OF SECONDARY MESSAGE
         AGO   .CALL
.RMSG2   ANOP
         LR    R0,&MSG2(1)              ADDRESS OF SECONDARY MESSAGE
.CALL    ANOP
         BAL   R14,MESSAGE              SEND MESSAGES
         AGO   .END
.NOMSG2  ANOP
         BAL   R14,MESSAGE0             SEND PRIMARY MESSAGE
.END     MEND
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
WORKREG  EQU   8
BASEREG1 EQU   9
BASEREG2 EQU  10
BASEREG3 EQU  11
BASEREG4 EQU  12
R13      EQU  13
R14      EQU  14
R15      EQU  15
         SPACE 5
PDS      ENTER
         LR    R2,R1          SAVE ADDR OF CPPL
         SPACE
         L     R5,WORKSIZE
         GETMAIN R,LV=(R5)
         SPACE 2
         LR    WORKREG,R1    ADDR OF WORK AREA
         USING WORKAREA,WORKREG
         EJECT
*
*        CLEAR WORK AREA
*
         SPACE 2
         LR    R4,R1          AREA TO CLEAR
         XR    R1,R1          NULL SOURCE
         MVCL  R4,R0          FILL WITH PAD 0 FROM R1(0:7)
         SPACE 2
         STM   BASEREG1,BASEREG4,BASES SAVE BASE REGISTERS
         SPACE 3
*
*        INITIALIZE WORK AREA
*
         SPACE 2
         MVC   BLDLLIST,BLDLPARM
         SPACE
         GTSIZE
         STH   R1,LINESIZE
         LA    R0,IOECB                 ECB ADDRESS
         ST    R0,IOBECB                SET INTO IOB
         LA    R0,INDCB                 DCB ADDRESSS
         ST    R0,IOBDCB                SET INTO DCB
         LA    R0,CCWS                  CCW CHAIN ADDRESS
         ST    R0,IOBCCW                SET INTO IOB
         MVI   IOBFLAG,X'42'            SET IOB FLAGS
         EJECT
*
*        ESTABLISH STAE EXIT AND INITIALIZE CP PARAMETERS
*
         SPACE 3
         MVC   STAEPARM(LSTAE),STAE MOVE PATTERN STAE TO WORK AREA
         STAE  STAEEXIT,CT,PARAM=WORKAREA,MF=(E,STAEPARM) ISSUE STAE
         SPACE
         LA    R15,MAINSAVE
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         ST    R13,ADDRSAVE        ADDRESS OF FIRST SAVE AREA
         SPACE 3
         USING CPPL,R2             BASE FOR COMMAND PARM LIST
         MVC   ADDRUPT(12),CPPLUPT GET UPT, PSCB AND ECT ADDRESSES
         MVC   ADDRCBUF,CPPLCBUF   COMMAND BUFFER
         ST    R2,CPPLADDR         SAVE THE CPPL ADDRESS
         SPACE 1
         LA    R14,PARMLIST        POINTER TO DAPL
         LA    R15,DAIRRC          POINTER TO RETURN CODE
         LA    R0,ADREFF02         POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFDAIR)     POINTER TO INVOCATION TYPE
*        L     R2,CPPLADDR         POINTER TO CPPL
         STM   R14,R2,DFDAPLP      SAVE IN DAIRFAIL PARAMETER LIST
         SPACE 1
         LA    R2,PARSELST
         USING PPL,R2
         L     R15,ADDRUPT
         L     R0,ADDRECT
         LA    R1,ATTNECB
         STM   R15,R1,PPLUPT      INITIALIZE PPLUPT, PPLECT AND PPLECB
         ST    WORKREG,PPLUWA     PARSE PARAMETER LIST
         LA    R2,DFPBLIST        INITIALIZE DFPLUPT, DFPLECT,
         STM   R15,R2,DFPLLIST               DFPLECB AND DFPLDFPB
         DROP  R2
         EJECT
*
*    LOCATE TSO SERVICE ROUTINES AND POINT CONVERT ROUTINE
*
         L     R14,CVTPTR
         USING CVTMAP,R14
         L     R15,CVTPUTL    PUTLINE SERVICE ROUTINE
         L     R0,CVTPARS     PARSE   SERVICE ROUTINE
         L     R1,CVTPTGT     PUTGET  SERVICE ROUTINE
         L     R2,CVTSCAN     SCAN    SERVICE ROUTINE
         L     R3,CVTDAIR     DAIR    SERVICE ROUTINE
         L     R4,CVTSTCK     STACK   SERVICE ROUTINE
         L     R5,CVTEHDEF    DEFAULT SERVICE ROUTINE
         L     R6,CVTEFF02    MESSAGE SERVICE ROUTINE
         L     R7,CVTPCNVT    TTR CONVERT     ROUTINE
         STM   R15,R7,ADDRPUTL      SAVE FOR PROGRAM USE
         DROP  R14
*
*        GET THE PARTITIONED DATA SET NAME
*
         SPACE 3
         STAX  , REPLACE=YES
         L     R1,MAINPCL     ADDRESS OF PCL FOR DATA SET NAME
         BAL   R14,GOPARSE    GET THE DATA SET NAME
         B     EXIT12         ERROR EXIT
         XC    ADDRCBUF,ADDRCBUF
         EJECT
RESTART  DS    0H       ***ENTRY TO ALLOCATE A DIFFERENT DATA SET******
         SPACE 1
         MVI   DSPALLOC,DA08SHR    SET DISP=SHR FOR INITIAL ALLOCATION
         SPACE 2
RESTART2 DS    0H       ***ENTRY TO REALLOCATE A DATA SET**************
         SPACE 1
         STAX  ,                   CANCEL ANY OUTSTANDING STAX MACROS
         BAL   R14,ALLOCATE        ALLOCATE THE DATA SET
         B     EXIT12              ALLOCATION UNSUCCESSFUL
         SPACE 2
         MVC   INDCB(LEXCPDCB),EXCPDCB MOVE PATTERN DCB
         MVC   STOWDCB(LSAMDCB),SAMDCB ALSO FOR STOW OPERATIONS
         MVC   DCBDDNAM-IHADCB+INDCB,DDNAME
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         SPACE
         XC    OPENLIST(8),OPENLIST
         MVI   OPENLIST,X'80'
         LA    R1,EXLSTX
         STCM  R1,7,DCBEXLST+1-IHADCB+INDCB
         LA    R1,JFCB
         ST    R1,EXLSTX
         MVI   EXLSTX,X'87'
         RDJFCB (INDCB,INPUT),MF=(E,OPENLIST)
         SPACE
         CLI   VOLUME,X'40'     ANY VOLUME REQUESTED?
         BE    VOLUMEOK         NO, BRANCH
         CLC   VOLUME,JFCBVSER  THIS VOLUME?
         BNE   BADSER           NO, WRITE AN ERROR MESSAGE
         SPACE 2
VOLUMEOK XC    OPENLIST(8),OPENLIST
         MVI   OPENLIST+4,X'80'
         OPEN  (INDCB,INPUT,STOWDCB,INPUT),MF=(E,OPENLIST)
         MVI   OPENLIST,X'80'
         CLOSE (STOWDCB),MF=(E,OPENLIST)
         SPACE
         TM    DCBOFLGS-IHADCB+INDCB,DCBOFOPN  DCB OPEN?
         BNO   NOOPEN                          NO, BRANCH
         SPACE
         CLI   DSPALLOC,DA08OLD  DISP=OLD OPEN?
         BNE   NEWCMD            NO, GET THE NEXT COMMAND
         L     R15,ADDRCMD       ENTRY POINT OF THE INTERRUPTED COMMAND
         BR    R15               CONTINUE WITH REQUESTED COMMAND
         SPACE 2
NOOPEN   MESSAGE MSGNOPEN        DID NOT OPEN CORRECTLY
         B     EXIT12
         SPACE 1
BADSER   MESSAGE MSGWRNGV        WRONG VOLUME SERIAL ALLOCATED
         B     EXIT12
         EJECT
***********************************************************************
***      OPTIONS SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 2
OPTIONS  LA    R2,OPTNTBL           LIST OF AVAILABLE OPTIONS
         SPACE 1
NEXTOPTN DS    0H
        $PUTLINE (R2)
         SPACE
         MVC   0(2,R13),0(R2)
         LH    R1,0(R13)      LENGTH OF OPTION
         LA    R1,1(R1)       ROUND LENGTH TO HALFWORD
         SRL   R1,1           MULTIPLE
         SLL   R1,1
         AR    R2,R1          NEXT OPTION
         CLI   0(R2),X'FF'    DONE?
         BNE   NEXTOPTN       NO, BRANCH
         SPACE 2
NEWCMD   DS    0H
         MVI   ATTNECB,0
         NI    FLAGS,X'FF'-FMEMBER1-FMEMBER2-FATTR
         NI    FLAGSRD,X'FF'-FATTN
         MVI   FLAGSCN,0
         XC    ATTRYES,ATTRYES
         XC    ATTRNO,ATTRNO
         SPACE 2
*        STAX  REPLACE=YES,MF=(E,PARMLIST) NULLIFY ATTN EXITS
         STAX  , REPLACE=YES,MF=(E,PARMLIST) NULLIFY ATTN EXITS
         SPACE 2
         TM    FLAGS,FCMD     COMMAND BUFFER AVAILABLE?
         BO    HAVECMD        YES
         SPACE 2
        $PUTGET MSGENTER,ATTN=EXIT8
         EJECT
*
*
*        SET UP FOR SUBCOMMAND SCAN
*
*
         SPACE 2
HAVECMD  DS    0H
         NI    FLAGS,X'FF'-FCMD
         SPACE 2
         LA    R1,PARMLIST
         USING CSPL,R1
         SPACE
         MVC   CSPLUPT,ADDRUPT
         MVC   CSPLECT,ADDRECT
         LA    R0,ATTNECB
         ST    R0,CSPLECB
         MVI   ATTNECB,0
         LA    R0,SCANANSR
         ST    R0,CSPLOA
         XC    SCANANSR,SCANANSR
         XC    ANSWERS(24),ANSWERS
         LA    R0,R14SAVE
         ST    R0,CSPLFLG
         XC    R14SAVE,R14SAVE
         SPACE
         MVC   CSPLCBUF,ADDRCBUF
         DROP  R1
         SPACE
         L     R15,ADDRSCAN
         BALR  R14,R15        INVOKE COMMAND SCAN
         SPACE
         LA    R15,SCANANSR    ADDR OF ANSWER AREA
         USING CSOA,R15
         SPACE
         TM    CSOAFLG,CSOANOC    EMPTY BUFFER?
         BO    NEWCMD             YES, STILL NEED COMMAND
         SPACE
         TM    CSOAFLG,CSOAEXEC   IMPLICIT EXEC?
         BZ    HAVECMD1           NO, BRANCH
         EJECT
***********************************************************************
***      EXEC SUBCOMMAND       ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 2
         L     R2,ADDRCBUF        ** ENTRY FOR IMPLICIT FORM OF EXEC **
         XC    2(2,R2),2(R2)      CLEAR OFFSET
         SPACE
EXEC     L     R2,ADDRCBUF        ** ENTRY FOR EXPLICIT FORM OF EXEC **
         L     R1,CPPLADDR
         ST    R2,0(,R1)
         SPACE
         LINK  EPLOC=EXECLIT
         B     NEWCMD
         EJECT
HAVECMD1 TM    CSOAFLG,CSOAVWP+CSOAVNP VALID COMMAND?
         LA    R1,MSGINVLD             ASSUME NOT
         BZ    MSGNEW                  NO, BRANCH
         SPACE 2
         OI    FLAGS,FOPTIONS
         TM    CSOAFLG,CSOAVWP OPERAND PRESENT?
         BO    *+8            YES
         XI    FLAGS,FOPTIONS NO
         SPACE 2
         L     R2,CSOACNM     ADDR OF COMMAND NAME
         LH    R3,CSOALNM     LENGTH OF NAME
         BCTR  R3,0
         DROP  R15
         SPACE 2
         LA    R1,CMDTABLE
         SR    R15,R15        SET UP FOR COMMAND SCAN
*
*   COMMAND SCAN - IN THE CASE OF A POTENTIALLY AMBIGUOUS ENTRY
*                  (E.G. 'A ') THE FIRST MATCH IN THE COMMAND TABLE
*                  (ATTR IN THIS CASE) IS USED.
*
         SPACE 2
CMDSCAN  IC    R15,8(R1)      GET CMD LENGTH
         CR    R15,R3         CHECK CMD LENGTH
         BNH   NEXTCMD
         SPACE
         CLC   0(0,R1),0(R2)
         EX    R3,*-6         CHECK COMMAND NAME
         BE    CMDVALID       MATCH, MUST BE COMMAND
         SPACE
NEXTCMD  LA    R1,16(R1)      NEXT ENTRY IN TABLE
         CLI   0(R1),X'FF'    END OF TABLE?
         BNE   CMDSCAN        NO, CONTINUE
         SPACE
         MESSAGE MSGUNKN          NOT A VALID COMMAND
         MESSAGE MSGCMDS1         AVAILABLE COMMANDS (LINE ONE)
         MESSAGE MSGCMDS2         AVAILABLE COMMANDS (LINE TWO)
         MESSAGE MSGCMDS3         AVAILABLE COMMANDS (LINE THREE)
         B     NEWCMD
         SPACE 3
CMDVALID DS    0H
         MVC   SUBNAME(8),0(R1)  FULL SUBCOMMAND NAME
         MVC   ADDRPCL,12(R1)    SAVE PCL ADDR
         L     R1,8(,R1)         GET COMMAND PROCESSOR ADDRESS
         ST    R1,ADDRCMD          AND SAVE IT
         SPACE 2
         TM    ADDRPCL,X'40'  IGNORE OPTIONS?
         BO    CALLCMD        YES
         SPACE
         TM    ADDRPCL,X'20'    MEMBER NAME DEFAULT?
         BNO   CHKREQD          NO, CHECK IF OPERANDS REQUIRED
         TM    FLAGS,FOPTIONS   ANY OPERAND SPECIFIED
         BO    CMDPARSE         YES, USE THE SPECIFIED MEMBER NAME
         CLI   DEFMEMB,X'00'    ANY MEMBER NAME YET?
         BE    CMDPARSE         NO, CONTINUE THE PARSE
         MVC   MEMBER1,DEFMEMB  USE THE DEFAULT MEMBER NAME
         MVC   MSGTEXT1,MSGDEFM MESSAGE IF DEFAULT MEMBER NAME IN USE
         MVC   MSGTEXT1+22(8),DEFMEMB
         MESSAGE MSGTEXT1       TELL THE USER WHO IS BEING PROCESSED
         B     CALLCMD         CALL THE COMMAND
         SPACE 2
CHKREQD  TM    ADDRPCL,X'80'  OPERANDS REQUIRED?
         BZ    CMDPARSE       YES, BRANCH
         TM    FLAGS,FOPTIONS NO, WERE ANY SPECIFIED?
         BZ    CALLCMD            NO, BRANCH
         SPACE 2
CMDPARSE DS    0H
         L     R1,ADDRPCL
         BAL   R14,GOPARSE    GET THE COMMAND OPERANDS
         B     NEWCMD         INVALID PARSE, IGNORE
         EJECT
CALLCMD  DS    0H
         XC    PARMLIST(20),PARMLIST
         SPACE 2
         STAX  ATTNEXIT,USADDR=(WORKREG),REPLACE=YES,IBUF=0,OBUF=0,    X
               MF=(E,PARMLIST)
         SPACE 2
         L     R15,ADDRCMD
         BR    R15            LINK TO COMMAND PROCESSOR
         EJECT
ATTNEXIT DS    0H
         ENTER ATTNEXIT
         TM    FLAGSRD,FATTN  ALLOW ATTENTIONS?
         BZ    EXITATTN       NO
         SPACE 2
        $PUTGET MSGENTER     IGNORE ANY MORE ATTENTIONS
         SPACE
         TM    FLAGS,FNULL    NULL LINE ENTERED?
         BO    EXITATTN       YES, JUST LEAVE
         SPACE
         OI    FLAGS,FCMD     INDICATE COMMAND AVAILABLE
         SPACE
         POST  ATTNECB
         SPACE
EXITATTN DS    0H
         SR    R15,R15
         EXIT
         EJECT
***********************************************************************
***      EDIT SUBCOMMAND       ADDED BY BRUCE LELAND -- NOV, 1977   ***
***********************************************************************
*
         SPACE
EDIT     MVC   DIRNAME,MEMBER1      GET MEMBER NAME FOR BLDL
         BLDL  INDCB,BLDLLIST       MEMBER THERE?
         B     *+4(R15)             BRANCH TABLE
         B     EDIT3          MEMBER IS THERE
         B     NOMEMBER       MEMBER NOT FOUND
         B     IOERROR        DIRECTORY I/O ERROR
         SPACE 2
EDIT3    MVC   MSGTEXT2+4(8),EDITLIT    MOVE IN "EDIT    "
         MVI   MSGTEXT2+12,C''''        ADD A QUOTE
         MVC   MSGTEXT2+13(44),DSNAME   ADD THE DATA SET NAME
         LH    R15,DSNLEN               DSNAME ACTUAL LENGTH
         LA    R1,13(,R15)              LENGTH OF "EDIT    'DSNAME"
         LA    R2,MSGTEXT2+13(R15)      POINTER TO CURRENT BYTE
         MVI   0(R2),C'('               MEMBER NAME PAREN
         SR    R15,R15                  LENGTH OF LAST QUALIFIER
         LR    R14,R2                   BACKWARD SCAN POINTER
         BCTR  R14,0                    BACK UP FROM (
EDIT7    BCTR  R14,0                    SCAN
         LA    R15,1(,R15)                  TO
         CLI   0(R14),C'.'                    PREVIOUS
         BNE   EDIT7                                  PERIOD
         MVC   MSGTEXT1+4(9),1(R14)
         SLL   R15,16
         ST    R15,MSGTEXT1             SAVE LENGTH OF STRING
         MVC   1(8,R2),MEMBER1          MEMBER NAME
         MVI   9(R2),X'40'              INSURE SCAN STOPS
         LA    R1,1(,R1)                SCAN
         LA    R2,1(,R2)                    FOR
         CLI   0(R2),X'40'                     FIRST
         BNE   *-12                                 BLANK
         MVC   0(4,R2),=C')''  '        ADD )' AND TWO BLANKS
         LA    R1,4(,R1)                UPDATE LENGTH
         LA    R2,4(,R2)
         USING PDLEDIT,R14
         LA    R14,ANSWERS
         CLI   PCLNON+1,1               NONUM CODED?
         BNE   EDIT9                    NO, BRANCH
         MVC   0(6,R2),=C'NONUM '       YES
         LA    R1,6(,R1)
         LA    R2,6(,R2)
EDIT9    CLI   PCLASIS+1,1              ASIS CODED?
         BNE   EDIT11                   NO, BRANCH
         MVC   0(5,R2),=C'ASIS '        YES
         LA    R1,5(,R1)
         LA    R2,5(,R2)
EDIT11   CLI   PCLTYPE+1,0              ANY DATA TYPE CODED?
         BE    EDIT13                   NO, BRANCH
         LH    R15,PCLTYPE              GET THE OPTION CODED
         SLL   R15,3                    INDEX  =  OPTIONUM * 8
         LA    R15,EDITTYPE-8(R15)      POINT TO THE PROPER OPTION
         B     EDIT19                   DONE WITH FORMATTING
EDIT13   LH    R14,MSGTEXT1             QUALIFIER LENGTH
         BCTR  R14,0                    MACHINE LENGTH
         LA    R15,EDITTYPE-8
EDIT15   LA    R15,8(,R15)              TABLE WIDTH IS 8
         CLC   PLIF,0(R15)              END OF DESCRIPTIVE QUALIFIERS?
         BE    EDIT17                   YES, USE "DATA" OR "GOFORT"
         EX    R14,EDITCLC              THIS QUALIFIER?
         BNE   EDIT15                   NO, TRY THE NEXT ONE
         B     EDIT19                   YES, USE THIS QUALIFIER
EDITCLC  CLC   0(*-*,R15),MSGTEXT1+4    <<EXECUTED>>
         SPACE 2
EDIT17   LA    R15,EDITTYPE             NOT FOUND, ASSUME "DATA"
         CLC   =C'FORT(',MSGTEXT1+4     FORT QUALIFIER?
         BNE   EDIT19
         LA    R15,GOFORT               YES, USE "GOFORT" (EDIT RULES)
         SPACE 1
EDIT19   MVC   0(8,R2),0(R15)           MOVE IN DATA TYPE
         LA    R1,8(,R1)
         SLL   R1,16
         ST    R1,MSGTEXT2              SAVE LENGTH OF STRING
***     $PUTLINE MSGTEXT2               TO OUTPUT THE COMMAND BUFFER
         MVI   MSGTEXT2+3,8             OPERAND OFFSET
         LA    R1,MSGTEXT2
         ST    R1,ADDRTEXT              COMMAND ADDRESS
         L     R14,ADDRECT
         USING ECT,R14
         MVC   DOUBLE(8),ECTPCMD        SAVE THE COMMAND NAME FOR PDS
         MVC   ECTPCMD(8),EDITLIT       CHANGE COMMAND NAME TO EDIT
         BAL   R2,CLOSEIT               NEED TO CLOSE BEFORE THE EDIT
         SPACE 1
         LA    R1,ADDRTEXT              ADDRESS OF THE CPPL FOR EDIT
         LINK  EPLOC=EDITLIT            GO DO THE EDIT COMMAND
         L     R14,ADDRECT
         MVC   ECTPCMD(8),DOUBLE        RESET IT TO THE ORIGINAL NAME
         BAL   R14,DEALLDCB             MAKE SURE IT IS FREE'D
         B     RESTART2                 ALLOCATE AND OPEN INDCB AGAIN
         DROP  R14
         EJECT
***********************************************************************
***      SUBMIT SUBCOMMAND     ADDED BY BRUCE LELAND -- NOV, 1977   ***
***********************************************************************
*
         SPACE 1
SUBMIT   LA    R1,MSGNF80                  *** ASSUME ERROR MESSAGE
         TM    DCBRECFM-IHADCB+INDCB,DCBRECV  RECFM=V OR RECFM=U?
         BO    MSGNEW                         YES, INVALID
         CLC   =H'80',LRECL                   LRECL=80?
         BNE   MSGNEW                         NO, INVALID
         MVC   DIRNAME,MEMBER1
         BLDL  INDCB,BLDLLIST
         B     *+4(R15)
         B     SUBMIT7        MEMBER IS THERE
         B     NOMEMBER       MEMBER NOT FOUND
         B     IOERROR        DIRECTORY I/O ERROR
         SPACE 2
SUBMIT7  MVC   MSGTEXT2+4(8),SUBLIT     MOVE IN "SUBMIT  "
         MVI   MSGTEXT2+12,C''''        ADD A QUOTE
         MVC   MSGTEXT2+13(44),DSNAME   ADD THE DATA SET NAME
         LH    R15,DSNLEN               DSNAME ACTUAL LENGTH
         LA    R1,13(,R15)              LENGTH OF "SUBMIT  'DSNAME"
         LA    R2,MSGTEXT2+13(R15)      POINT TO CURRENT BYTE
         MVI   0(R2),C'('               MEMBER NAME PARENTHESIS
         MVC   1(8,R2),MEMBER1          ADD MEMBER NAME
         MVI   9(R2),X'40'              INSURE SCAN STOPS
         LA    R1,1(,R1)                SCAN
         LA    R2,1(,R2)                    FOR
         CLI   0(R2),X'40'                     FIRST
         BNE   *-12                                 BLANK
         MVC   0(4,R2),=C')''  '        ADD TERMINATOR BYTES
         LA    R1,4(,R1)                ACCOUNT FOR ")'  "
         SLL   R1,16                    CLEAR BOTTOM TWO BYTES
         ST    R1,MSGTEXT2              SAVE STRING TOTAL LENGTH
*       $PUTLINE MSGTEXT2               DELETE * TO OUTPUT COMMAND
         MVI   MSGTEXT2+3,8             OPERAND OFFSET
         LA    R1,MSGTEXT2
         ST    R1,ADDRTEXT              COMMAND ADDRESS
         LA    R1,ADDRTEXT              ADDRESS OF THE CPPL FOR SUBMIT
         LINK  EPLOC=SUBLIT             GO DO THE SUBMIT COMMAND
         B     NEWCMD                   DO THE NEXT COMMAND
         EJECT
***********************************************************************
***      RESTORE SUBCOMMAND    ADDED BY BRUCE LELAND -- FEB, 1981   ***
***********************************************************************
*
         SPACE 1
RESTORE  BAL   R14,CHKUSER         OPEN WITH DISP=OLD IF REQUIRED
         LA    R1,MSGNLIST         DOES NOT WORK FOR LOAD LIBRARIES
         TM    FLAGS,FLOADMOD      LOAD LIBRARY?
         BO    MSGNEW              YES, ERROR
         MVC   DIRNAME,MEMBER1     MEMBER NAME TO BE ADDED
         ICM   R1,15,MEMBER2       TTR TO BE USED
         CLI   LMEMBER2+1,1        COMPARE LENGTH:1
         BH    RESTORE2              HIGH (DO NOT ADJUST)
         SRL   R1,8                      SHIFT IN EIGHT ZEROS
         CLI   LMEMBER2+1,1        COMPARE LENGTH:1
         BE    RESTORE2              EQUAL (ADJUSTED ONE TIME)
         SRL   R1,8                  LOW (ADJUSTED TWICE)
RESTORE2 STCM  R1,15,DIRTTR        SAVE THE UPDATED ADDRESS
         SPACE 2
         MVI   DIRFLAG,X'80'       ADD AS AN ALIAS
*
         BAL   R14,OPENSTOW        OPEN THE STOW DCB IF REQUIRED
         BZ    NEWCMD              DID NOT OPEN -- QUIT
         STOW  STOWDCB,DIRNAME,A   ADD AN ALIAS WITH THE GIVEN TTR
         LA    R1,MSGRESET         ASSUME ADDED CORRECTLY
         B     *+4(R15)
         SPACE
         B     MSGNEW              ADDED CORRECTLY
         B     MEMEXIST            NAME IS ALREADY IN THE DIRECTORY
         EX    0,*                 SHOULD NOT HAPPEN WITH ADD
         B     FULLDIR             DIRECTORY IS ALREADY FULL
         B     IOERROR             I/O ERROR IN THE DIRECTORY
         EJECT
***********************************************************************
***      FIND SUBCOMMAND       ADDED BY BRUCE LELAND -- NOV, 1977   ***
***********************************************************************
*
         SPACE 1
FIND     OI    FLAGSOP,FINDOP      FIND SUBCOMMAND FLAG
         LH    R1,FINDLTH          STRING LENGTH
         LTR   R1,R1               STRING EVER CODED?
         BP    LIST+4              YES, ENTER LIST COMMAND CODE
         LA    R1,MSGFINDN         NO, ERROR
         B     MSGNEW
         SPACE 3
***********************************************************************
***      LIST SUBCOMMAND    MODIFIED BY BRUCE LELAND -- NOV, 1977   ***
***********************************************************************
*
         SPACE 1
LIST     NI    FLAGSOP,X'FF'-FINDOP     TURN OFF FIND FLAG
         TM    FLAGS,FLOADMOD           THIS A LOAD MODULE LIBRARY?
         LA    R1,MSGNLIST              MSG IF YES
         BO    MSGNEW                   YES, ERROR
         SPACE 2
         MVC   DIRNAME,MEMBER1          LOCATE MEMBER TO BE LISTED
         BLDL  INDCB,BLDLLIST
         B     *+4(R15)
         SPACE 2
         B     LIST1                    00 - SUCCESSFUL
         B     NOMEMBER                 04 - MEMBER NOT FOUND
         B     IOERROR                  08 - I/O ERROR IN DIRECTORY
         SPACE 2
LIST1    DS    0H
         SPACE 2
         OI    FLAGSRD,FATTN            INDICATE ATTENTIONS ACCEPTED
         EJECT
*
*
*        INITIALIZE CHANNEL PROGRAM TO READ SELECTED MEMBER
*
*
*        CCW CHAIN USED IS -      SEARCH ID    -----+
*                                 TIC           ----+
*                                 READ COUNT
*                                 READ DATA
*
         SPACE 2
         XC    CCWS(LCCWS),CCWS         CLEAR CCW AREA
         SPACE 2
         LA    R0,IOBSEEK               ADDRESS OF IOB SEEK
         LA    R1,CCWS                  FIRST CCW - SEARCH CCW
         L     R2,BUFFADDR              ADDRESS OF BUFFER
         ST    R0,CCWS                  SEEK ADDRESS IN CCW1
         ST    R1,CCWS+8                TIC ADDRESS IN CCW2
         ST    R0,CCWS+16               READ COUNT ADDR IN CCW3
         ST    R2,CCWS+24               BUFFER ADDR IN CCW4
         LH    R3,BUFFSIZE              LENGTH OF BUFFER
         STH   R3,CCWS+30               SET IN CCW4
         SPACE 2
         OC    CCWS(LLISTCCW),LISTCCWS  COPY PATTERN CCW CHAIN
         SPACE 2
*
*        CONVERT TTR OF START OF MEMBER TO CCHHR FORMAT
*
         SPACE 2
         STM   14,12,12(13)             SAVE REGISTERS FOR RETURN
         LA    R2,IOBSEEK-3             RETURN CCHHR IN IOBSEEK
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         MVI   DIRTTR+3,0               CLEAR TTR CONCATENTION FIELD
         L     R0,DIRTTR                GET TTR0 OF MEMBER
         LA    R15,X'100'               GET TTR0 OF PREVIOUS RECORD
         SLR   R0,R15
         L     R15,ADRPCNVT             ADDRESS OF TTR-CCHHR CONVERT
         LR    R3,R13                   SAVE ADDRESS OF SAVE AREA
         BALR  R14,R15
         LR    R13,R3                   RESTORE SAVE AREA ADDRESS
         LM    14,12,12(13)             RESTORE REGISTERS
         EJECT
LIST2    DS    0H
         MVI   IOECB,0
         EXCP  IOB                      READ NEXT RECORD
         WAIT  ECB=IOECB
         MVI   DCBIFLGS-IHADCB+INDCB,0  CLEAR I/O STATUS FLAGS
         SPACE 2
         TM    IOBCSW+3,X'01'           END-OF-FILE?
         BO    NEWCMD                   YES
         SPACE 2
         CLI   IOECB,X'42'              OUT OF EXTENT?
         BNE   LIST3                    NO, CHECK STATUS
         BAL   R14,NEWEXTNT             YES, ADJUST FOR NEW EXTENT
         MVI   IOBSEEK+4,0              CORRECT TO RECORD 0
         B     LIST2                    THEN RE-READ RECORD
         SPACE
LIST3    DS    0H
         CLI   IOECB,X'7F'              SUCCESSFUL?
         LA    R1,MSGLISTI              ERROR MESSAGE FOR I/O ERROR
         BNE   MSGNEW                   NO, I/O ERROR
         SPACE 2
         LH    R5,BUFFSIZE              MAXIMUM RECORD LENGTH
         SH    R5,IOBCSW+5              SUBTRACT RESIDUAL COUNT
         L     R6,BUFFADDR              START OF BUFFER
         AR    R5,R6                    ADDRESS OF END OF BUFFER
         SPACE 2
         TM    DCBRECFM-IHADCB+INDCB,DCBRECV  FILE VARIABLE FORMAT?
         LH    R4,LRECL                       GET RECORD LENGTH IF NOT
         LA    R3,LIST8                 ASSUME FIXED-LENGTH RECORDS
         BZ    LIST6                    NO, FIXED FORMAT
         SPACE 2
         MVC   0(2,R13),0(R6)           GET BLOCK LENGTH
         LH    R5,0(,R13)
         AR    R5,R6                    END OF VARIABLE-LENGTH BLOCK
         LA    R6,4(,R6)                START OF RECORD ENTRIES
         LA    R3,LISTVAR               VARIABLE LEN RECORD PROCESSOR
         SPACE 3
LIST6    DS    0H
         BCTR  R5,0                     SET END OF BUFFER ADDRESS
         BR    R3                       PROCESS FIRST RECORD
         EJECT
LISTVAR  DS    0H
         MVC   0(2,R13),0(R6)           GET LENGTH OF VARIABLE RECORD
         LA    R6,4(,R6)                SKIP OVER HEADER
         LH    R4,0(,R13)               GET LOGICAL RECORD LENGTH
         LA    R0,4
         SR    R4,R0                    SUBTRACT HEADER LENGTH
         BP    LIST8                    BRANCH IF RECORD EXISTS
         SR    R4,R4                    NO, RESET R2
         SPACE 1
LIST7    DS    0H
         BXLE  R6,R4,0(R3)              SKIP TO NEXT RECORD
         B     LIST2                    IF END OF BLOCK
         SPACE 3
LIST8    DS    0H
         LR    R1,R4                    LENGTH OF DATA AREA
         LA    R0,8               WIDTH OF LINE NUMBER FIELD
         LR    R15,R6             MOVE START POSITION
         TM    DCBRECFM-IHADCB+INDCB,DCBRECCC  CONTROL CHARACTERS?
         BZ    LIST9                           NO, BRANCH
         LA    R15,1(,R15)                YES, SKIP CARRIAGE CONTROL
         BCTR  R1,0
LIST9    TM    FLAGSOP,NONUM      NONUM?
         BO    LIST10             YES, BRANCH
         TM    FLAGSOP,SNUM       SNUM?
         BNO   LIST11             NO, BRANCH
         TM    DCBRECFM-IHADCB+INDCB,DCBRECV  RECFM=V?
         BNO   *+6                            NO, BRANCH
         AR    R15,R0             VARIABLE, ADJUST START COLUMN
         SR    R1,R0              VARIABLE AND FIXED, ADJUST LENGTH
         BNP   LIST7
LIST10   LA    R14,256        MAXIMUM LRECL TO DISPLAY
         CR    R1,R14
         BL    *+6
         LR    R1,R14         LONGER THAN 256, USE 256
         BCTR  R1,0                     DECREMENT RECORD LEN FOR MOVE
         MVC   MSGTEXT1+4(0),0(R15)
         EX    R1,*-6                   MOVE RECORD TO LINE BUFFER
         LR    R15,R1                   SAVE DATA LENGTH
         LA    R14,MSGTEXT1+4           SAVE DATA START ADDRESS
         LA    R1,5(,R1)                LINE LENGTH PLUS HEADER FIELD
LIST10B  SLL   R1,16
         ST    R1,MSGTEXT1              CREATE OUTPUT LINE HEADER
*
*    CHECK IF FIND OR LIST
*
         TM    FLAGSOP,FINDOP           FIND OPERATION?
         BNO   LISTOUT                  NO, LIST -- BRANCH
         LR    R2,R15                   DATA LENGTH
         LR    R1,R14                   START OF DATA
         AR    R14,R2                   END OF DATA
         LH    R15,FINDLTH              STRING LENGTH
         BCTR  R15,0                    MACHINE LENGTH
         SPACE 1
FIND5    EX    R2,FINDTRT               FIRST CHARACTER FOUND?
         BZ    LIST7                    NO, NOT IN STRING, QUIT
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         CR    R15,R2                   POSSIBLY IN STRING?
         BH    LIST7                    NOT IN STRING, QUIT
         EX    R15,FINDCLC              ENTIRE STRING FOUND?
         BE    LISTOUT                  YES, OUTPUT THE LINE
         LA    R1,1(,R1)                NO, TRY NEXT CHARACTER
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BM    LIST7                    YES, NOT IN STRING, QUIT
         B     FIND5                    CONTINUE SEARCHING
         SPACE 1
FINDTRT  TRT   0(*-*,R1),FINDTBL        <<EXECUTED>> -- FIRST BYTE
FINDCLC  CLC   0(*-*,R1),FINDSTR        <<EXECUTED>> -- COMPLETE STRING
         SPACE 2
LISTOUT  DS    0H
        $PUTLINE MSGTEXT1,ATTN=NEWCMD  OUTPUT THE NEXT LINE
         SPACE 1
         B     LIST7                    GET NEXT RECORD
         SPACE 2
LIST11   LR    R14,R15        NUM FORMAT
         TM    DCBRECFM-IHADCB+INDCB,DCBRECV RECFM=V?
         BNO   LIST12                        NO, BRANCH
         AR    R15,R0         VARIABLE, ADJUST DATA START
         B     LIST13
LIST12   AR    R14,R1         FIXED, ADJUST TO END OF LINE NUMBER
         SR    R14,R0         ADJUST TO BEGINNING OF LINE NUMBER
LIST13   SR    R1,R0          ADJUST DATA LENGTH
         MVC   MSGTEXT1+4(5),3(R14)     LINE NUMBER FIELD
         MVI   MSGTEXT1+9,X'40'         BLANK
         LA    R14,MSGTEXT1+3
LIST14   LA    R14,1(R14)
         CLI   0(R14),C'0'    BLANK ALL LEADING ZEROES IN THE
         BNE   *+12                 LINE NUMBER FIELD
         MVI   0(R14),X'40'
         B     LIST14
         LA    R14,250        MAXIMUM DATA LENGTH FOR NUM FORMAT
         CR    R1,R14
         BL    *+6
         LR    R1,R14
         BCTR  R1,0
         LTR   R1,R1                    ANY DATA?
         BP    *+6                      YES, BRANCH
         SR    R1,R1                    NO, DISPLAY LINE NUMBER ONLY
         MVC   MSGTEXT1+10(0),0(R15)    DATA
         EX    R1,*-6
         LR    R15,R1                   SAVE DATA LENGTH
         LA    R14,MSGTEXT1+10          SAVE DATA START ADDRESS
         LA    R1,11(R1)                LINE LENGTH AND HEADERS
         B     LIST10B
         EJECT
***********************************************************************
***      HELP  SUBCOMMAND      ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 2
HELP     L     R2,ADDRCBUF           INPUT BUFFER ADDRESS
         L     R1,CPPLADDR
         USING CPPL,R1               ACTUAL ADDRESS
         ST    R2,CPPLCBUF           SET COMMAND ADDRESS
         DROP  R1
         LINK  EPLOC=HELPLIT         GET THE HELP PROCESSOR
         MVC   DDNAMEH,DDNAME        SAVE ACTUAL DDNAME
         MVC   DDNAME,=C'SYSHELP '   DDNAME TO FREE
         BAL   R14,DEALLOCZ          FORCE A FREE OF SYSHELP
         MVC   DDNAME,DDNAMEH        RESTORE ACTUAL DDNAME
         B     NEWCMD                NEXT COMMAND
         EJECT
***********************************************************************
***      USAGE SUBCOMMAND                                           ***
***********************************************************************
*
*                      *** NO PDL IS USED FOR THIS SUBCOMMAND ***
         SPACE 2
USAGE    OI    FLAGSRD,F1STREAD         INDICATE FIRST ENTRY
         OI    FLAGSCN,FDIRSCAN         ALSO DIRECTORY SCAN REQUEST
         MVC   MSGTEXT2,MSGDSNDD        ADD "//DDNAME    DD  DSN='"
         LH    R1,MSGTEXT2              LENGTH OF MESSAGE
         LA    R2,MSGTEXT2-1(R1)        LOCATION OF QUOTE
         MVC   MSGTEXT2+4+2(8),DDNAME   ADD THE CURRENT DDNAME
         LH    R15,DSNLEN               DSNAME STRING LENGTH
         MVC   0(44,R2),DSNAME          MOVE IN MAXIMUM LENGTH DSNAME
         LA    R1,1(R1,R15)             ALLOW FOR QUOTE AND COMMA
         AR    R2,R15                   ADDRESS OF QUOTE
         MVI   0(R2),C''''              ADD FINAL QUOTE
         MVI   1(R2),C','               FOLLOWING COMMA
         STH   R1,MSGTEXT2              UPDATE MESSAGE LENGTH
        $PUTLINE MSGTEXT2
         MVC   MSGTEXT2(24),MSGDCB
         LA    R1,MSGTEXT2+19
         TM    DCBRECFM-IHADCB+INDCB,DCBRECU  RECFM=U?
         BNO   *+12                           NO, BRANCH
         MVI   0(R1),C'U'
         B     UVORF
         TM    DCBRECFM-IHADCB+INDCB,DCBRECF  RECFM=F?
         BNO   *+12                           NO, BRANCH
         MVI   0(R1),C'F'
         B     UVORF
         TM    DCBRECFM-IHADCB+INDCB,DCBRECV  RECFM=V?
         BNO   UVORF                          NO, BRANCH
         MVI   0(R1),C'V'
UVORF    LA    R1,1(R1)
         TM    DCBRECFM-IHADCB+INDCB,DCBRECBR RECFM=.B?
         BNO   *+12                           NO, BRANCH
         MVI   0(R1),C'B'
         LA    R1,1(R1)
         TM    DCBRECFM-IHADCB+INDCB,DCBRECSB RECFM=.S?
         BNO   *+12                           NO, BRANCH
         MVI   0(R1),C'S'
         LA    R1,1(R1)
         TM    DCBRECFM-IHADCB+INDCB,DCBRECTO RECFM=.T?
         BNO   *+12                           NO, BRANCH
         MVI   0(R1),C'T'
         LA    R1,1(R1)
         TM    DCBRECFM-IHADCB+INDCB,DCBRECCA RECFM=.A?
         BNO   *+12                           NO, BRANCH
         MVI   0(R1),C'A'
         LA    R1,1(R1)
         TM    DCBRECFM-IHADCB+INDCB,DCBRECCM RECFM=.M?
         BNO   *+12                           NO, BRANCH
         MVI   0(R1),C'M'
         LA    R1,1(R1)
         TM    DCBRECFM-IHADCB+INDCB,DCBRECU  RECFM=U?
         BO    USBLK                          YES, NO LRECL
         MVC   0(7,R1),LRECLTXT        MOVE IN LRECL TEXT
         LA    R1,7(R1)
         LH    R4,LRECL
         CVD   R4,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R4,DOUBLE-1
         LA    R4,1(R4)
         CLI   0(R4),C'0'
         BE    *-8
         OI    DOUBLE+4,X'F0'
USMOVE1  MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         TM    1(R4),X'F0'
         LA    R4,1(R4)
         BO    USMOVE1
USBLK    MVC   0(9,R1),BLKSITXT
         LA    R1,9(R1)
         LH    R4,BLKSI
         CVD   R4,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R4,DOUBLE-1
         LA    R4,1(R4)
         CLI   0(R4),C'0'
         BE    *-8
         OI    DOUBLE+4,X'F0'
USMOVE2  MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         TM    1(R4),X'F0'
         LA    R4,1(R4)
         BO    USMOVE2
         SPACE 2
*    ADD THE VOLUME SERIAL NUMBER:  DCB=(RECFM=...),VOL=SER=...
*
         MVC   0(10,R1),=C'),VOL=SER='  ADD PAREN, COMMA & VOL KEYWORD
         MVC   10(6,R1),JFCBVSER        ADD THE SERIAL NUMBER
         LA    R4,16(,R1)               INCREMENT THE MESSAGE LENGTH
         SPACE 2
         LA    R1,MSGTEXT2
         SR    R4,R1
         SLL   R4,16                  SAVE MESSAGE LENGTH
         ST    R4,0(R1)
         MVI   ATTNECB,0              ALLOW NO INTERRUPTS
        $PUTLINE (R1)                 OUTPUT THE DCB LINE
         SPACE 2
         BAL   R14,READDIR              SCAN THROUGH THE DIRECTORY
         B     USE1                     IF END OF DIRECTORY
         B     IOERROR                  IF I/O ERROR
         B     IOERROR                  IF PREMATURE EXIT
         SPACE 2
USE1     DS    0H
         LA    R2,MSGUSEB               MSG - TOTAL BLOCKS ALLOCATED
         LH    R1,TOTBLOCK              GET COUNTER
         BAL   R4,USEFMT                FORMAT MESSAGE
         SPACE
         LA    R2,MSGUSED               MSG - TOTAL BLOCKS USED
         LH    R1,TOTUSED               GET COUNTER
         BAL   R4,USEFMT
         SPACE
         LA    R2,MSGUSER               MSG - TOTAL MEMBERS
         LH    R1,TOTMEMR               GET COUNTER
         AH    R1,TOTMEMA
         BAL   R4,USEFMT
         SPACE
         LA    R2,MSGUSEA               MSG - TOTAL ALIASES
         LH    R1,TOTMEMA
         LTR   R1,R1                    ANY ALIASES?
         BZ    USETOTAL                 NO
         BAL   R4,USEFMT
         SPACE 2
USETOTAL DS    0H
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         SR    R14,R14
         SR    R15,R15
         SPACE 2
*
*        CALCULATE TOTAL SIZE OF DATA SET AND UNUSED SPACE
*
         SPACE 2
         IC    R14,DEBXTNT#(,R1)        GET NUMBER OF EXTENTS IN D.S
         STH   R14,DSNEXTNT             SAVE VALUE
         LA    R15,DEBAMLNG             GET SIZE OF EACH EXTENT
         SR    R0,R0
USEXTNT  DS    0H
         AH    R0,DEBEXTNT+14(,R1)      ADD NUMBER OF TRACKS IN EXTENT
         AR    R1,R15                   TO TOTAL AND JUMP TO NEW EXTENT
         BCT   R14,USEXTNT              IF MORE EXTENTS
         SPACE
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         SPACE 2
         L     R14,OBTAIN               GET OBTAIN FLAGS
         LA    R15,JFCBDSNM             ADDRESS OF FULL DATA SET NAME
         LA    R0,JFCBVSER              ADDRESS OF VOLUME SERIAL
         LA    R1,DSCB                  ADDRESS OF DSCB/WORKAREA
         STM   R14,R1,CAMLST            SAVE FOR OBTAIN
         SPACE
         SR    R0,R0
         OBTAIN CAMLST                  GET DSCB
         LTR   R15,R15
         LA    R1,MSGNDSCB              DSCB NOT FOUND MESSAGE
         BNZ   MSGNEW
         SPACE 2
         LH    R1,DS1LSTAR+DSCB-44      GET TT OF LAST TRACK
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         LH    R0,DSNTOTAL              COMPUTE AMOUNT OF FREE SPACE
         SR    R0,R1
         STH   R0,DSNEMPTY
         SPACE 3
         LH    R1,DSNTOTAL              TOTAL SPACE USED
         LA    R2,MSGUSESI              MSG FOR TOTAL SPACE
         BAL   R4,USEFMT
         SPACE
         LH    R1,DSNEMPTY              TOTAL FREE SPACE
         LA    R2,MSGUSEMP
         BAL   R4,USEFMT
         SPACE
         LH    R1,DSNEXTNT              TOTAL EXTENTS
         LA    R2,MSGUSEXT
         BAL   R4,USEFMT
         B     NEWCMD
         SPACE 2
USEFMT   DS    0H
         CVD   R1,DOUBLE                CONVERT COUNTER TO DECIMAL
         LA    R14,10
         SR    R15,R15
USEFMT1  SR    R0,R0
         DR    R0,R14                   COMPUTE NUMBER OF SIGIF. DIGITS
         LTR   R1,R1                    RESIDUAL FACTOR ZERO?
         LA    R15,1(,R15)
         BNZ   USEFMT1                  NO, KEEP GOING
         BCTR  R15,0                    YES, REDUCE FOR EXECUTE
         LR    R1,R15
         SLL   R1,4                     SHIFT FOR EXECUTED UNPACK
         OI    DOUBLE+7,X'0F'           CORRECT SIGN FOR EXECUTE
         SPACE
         UNPK  MSGTEXT1+4(0),DOUBLE
         EX    R1,*-6
         LA    R1,MSGTEXT1+5(R15)
         SPACE
         LH    R15,0(,R2)
         LA    R0,5
         SR    R15,R0
         MVC   0(,R1),4(R2)
         EX    R15,*-6
         LA    R15,1(R1,R15)
         LA    R0,MSGTEXT1
         SR    R15,R0
         SLL   R15,16
         ST    R15,MSGTEXT1
         SPACE 1
        $PUTLINE MSGTEXT1
         BR    R4                       RETURN
         EJECT
***********************************************************************
***      RENAME SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 3
RENAME   DS    0H
         BAL   R14,CHKUSER     VERIFY DISP=OLD FOR NON SYSTEMS USERS
         SPACE
         BAL   R14,OPENSTOW   OPEN STOW DCB
         BZ    NEWCMD         EXIT IF DATA SET NOT OPENED
         STOW  STOWDCB,MEMBERS,C ISSUE STOW W/ CHANGE OPTION
         SPACE
         LA    R1,MSGRENAM     ASSUME RENAMED
         B     *+4(R15)       PROCESS RETURN CODE
         B     MSGNEW          0- SUCESSFUL
         B     MEMEXIST        4- MEMBER EXISTS WITH NEW NAME
         B     NOMEMBER        8- CURRENT NAME DOES NOT EXIST
         EX    0,*            12- INVALID RETURN FROM RENAME
         B     IOERROR        16- IO ERROR IN DIRECTORY
         EJECT
*
*
*         ENTRY SUBCOMMAND
*
*
         SPACE 2
ENTRY    DS    0H
         BAL   R14,CHKUSER
         MVC   DIRNAME,MEMBER1
         BLDL  INDCB,BLDLLIST
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         B     *+4(R15)
         B     ENTRY1
         B     ENTRYNON
         B     ENTRY6
ENTRY1   EQU   *
         TM    FLAGS,FLOADMOD
         BO    ENTRY2
ENTRYER  EQU   *
         MESSAGE ENTRYM1
         B     ENTRYEND
ENTRYM1  MSG   ' MEMBER OR ALIAS IS NOT A LOAD-MODULE'
ENTRY2   DS    0F
         MVC   DIRENTRY,ENTRYAD
         CLC   DIRENTRY,=XL3'00'
         BE    EPZERO
         NI    DIRATTR+1,255-ATTREP0
         B     ENTRY3
EPZERO   EQU   *
         OI    DIRATTR+1,ATTREP0
ENTRY3   EQU   *
         BAL   R14,OPENSTOW
         BZ    ENTRYEND
         MVC   DCBRELAD-IHADCB+STOWDCB,DIRTTR
         MVI   DCBRELAD-IHADCB+STOWDCB+3,0
         STOW  STOWDCB,DIRNAME,R
         B     *+4(R15)
         B     ENTRY4
         B     ENTRYNO   SHOULD NOT OCCUR
         B     ENTRY5
         B     ENTRYNO
         B     ENTRY6
         B     ENTRYNO
         B     ENTRYNO
ENTRYNO  EQU   *
         MESSAGE MSGSTOWC
         B     ENTRYEND
ENTRY4   EQU   *
         MESSAGE MSGNTRY
         B     ENTRYEND
ENTRYNON EQU   *
         MESSAGE MSGNONE
         B     ENTRYEND
ENTRY5   EQU   *
         MESSAGE MSGSTOW8
         B     ENTRYEND
         LTORG
ENTRY6   DS    0H
         MESSAGE MSGIOERR
ENTRYEND EQU   *
         B     NEWCMD
         EJECT
***********************************************************************
***      ALIAS SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE
ALIAS    DS    0H
         BAL   R14,CHKUSER     VERIFY DISP=OLD FOR NON SYSTEMS USERS
         SPACE 2
         MVC   DIRNAME,MEMBER1 ORIGINAL MEMBER NAME
         SPACE
         BLDL  INDCB,BLDLLIST LOCATE DIRECTORY ENTRY
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE
         B     *+4(R15)       PROCESS RETURN CODE
         B     ALIAS1         00-SUCESSFUL
         B     NOMEMBER       04-MEMBER NOT FOUND
         B     IOERROR        08-I/O ERROR
         SPACE 3
ALIAS1   DS    0H
         TM    DIRFLAG,X'80'  IS MODULE A CURRENT ALIAS?
         BZ    ALIAS2         NO
         SPACE
         MESSAGE MSGNREAL     YES, ERROR
         B     MAPAREAL       BRANCH TO OUTPUT REAL ENTRY NAME
         SPACE 2
ALIAS2   DS    0H
         MVC   DIRNAME,MEMBER2 SET ALIAS NAME
         OI    DIRFLAG,X'80'  SET ALIAS FLAG
         SPACE
         TM    FLAGS,FLOADMOD IS THIS A LOAD MODULE?
         BZ    ALIAS4         NO
         SPACE
         TM    DIRATTR,ATTRSCAT          SCATTER LOADED?
         LA    R1,MSGASCAT               SCATTER LOADED MESSAGE
         BO    MSGNEW                    YES, COMPLAIN
         SPACE
         XC    DIRAPF2,DIRAPF2           ZERO ALIAS APF DATA
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG VS LKED & APF DATA PRESENT?
         BZ    ALIAS2Z                    NO, BRANCH
         SPACE
         LA    R2,DIRAPF                 POINT TO APF INFORMATION
         TM    DIRATTR2,DIR2SSI          SSI PRESENT?
         BZ    ALIAS2B                   NO, BRANCH
         SPACE
         LA    R2,1(,R2)                 ROUND TO HALFWORD
         N     R2,=F'-2'                 FFFFFFFE MASK
         LA    R2,4(,R2)                 POINT TO APF DATA
         MESSAGE MSGSSIDR                TELL SSI DROPPED
         SPACE
ALIAS2B  NI    DIRATTR2,255-DIR2SSI      TURN OFF SSI INDICATOR
         CLI   0(R2),1                   IS APF LENGTH OK?
         BE    ALIAS2D                   YES, BRANCH
         MESSAGE MSGAABAD                BAD APF INFORMATION FORMAT MSG
         LA    R2,=F'0'                  SET APF=0
         SPACE
ALIAS2D  MVC   DIRAPF2+1(1),1(R2)       MOVE IN DATA
         MVI   DIRAPF2,1                  AND SET PROPER LENGTH
         SPACE
ALIAS2Z  MVC   DIRREAL,MEMBER1 REAL MODULE NAME
         MVC   DIREP,DIRENTRY REAL MODULE ENTRY ADDRESS
         NI    DIRFLAG,X'E0'  SET DIRECTORY ENTRY LENGTH
         OI    DIRFLAG,(DIREND2-DIRUSER+1)/2  ADD NUMBER OF HALFWORDS
         SPACE
         BAL   R14,READCESD   LOCATE POSSIBLE EXTERNAL NAME
         B     ALIAS4         NOT FOUND, MUST BE PSUEDO ENTRY
         SPACE
         LTR   R1,R1          ENTRY ADDRESS ZERO?
         BZ    ALIAS3A        YES
         NI    DIRATTR+1,X'FF'-ATTREP0 NO, INSURE ATTR FLAG OFF
ALIAS3A  DS    0H
         ST    R1,0(R13)      MOVE ENTRY ADDRESS FOR
         MVC   DIRENTRY,1(R13) ALIAS TO DIRECTORY ENTRY
         SPACE 2
ALIAS4   DS    0H
         SPACE
         BAL   R14,OPENSTOW
         BZ    NEWCMD         EXIT IF DATA SET NOT OPENED
         STOW  STOWDCB,DIRNAME,A UPDATE DIRECTORY WITH ALIAS ENTRY
         SPACE
         B     *+4(R15)       PROCESS RETURN CODE
         SPACE 2
         B     ALIAS5         00-SUCESSFUL
         B     MEMEXIST       04-MEMBER ALREADY EXISTS
         EX    0,*            08-SHOULD NOT OCCUR WITH ADD OPTION
         B     FULLDIR        12-DIRECTORY IS FULL
         B     IOERROR        16-I/O ERROR IN DIRECTORY
         SPACE 2
ALIAS5   DS    0H
        $PUTLINE MSGALIAS     MSG - ALIAS ASSIGNED
         SPACE
         TM    FLAGS,FLOADMOD MUST WE GIVE ENTRY POINT?
         BZ    NEWCMD         NO
         SPACE
         MVC   MSGTEXT1,MSGENTRY
         LH    R15,MSGTEXT1   LENGTH OF MSG
         LA    R14,MSGTEXT1(R15)
         LA    R15,6(R15)
         STH   R15,MSGTEXT1
         SPACE
         MVC   DOUBLE(3),DIRENTRY
         MVI   DOUBLE+3,X'04'
         UNPK  0(7,R14),DOUBLE(4)
         TR    0(6,R14),TRTABLE
         SPACE
        $PUTLINE MSGTEXT1
         B     NEWCMD
         EJECT
***********************************************************************
***      DELETE SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 2
DELETE   DS    0H
         BAL   R14,CHKUSER     VERIFY DISP=OLD FOR NON SYSTEMS USERS
         SPACE
         BAL   R14,OPENSTOW   OPEN STOW DCB
         BZ    NEWCMD         EXIT IF DATA SET NOT OPENED
         STOW  STOWDCB,MEMBER1,D ISSUE STOW W/ DELETE OPTION
         SPACE 2
         LA    R1,MSGGONE     ASSUME DELETED
         B     *+4(R15)       PROCESS RETURN CODE
         SPACE 2
         B     MSGNEW         00-SUCESSFUL
         EX    0,*            04-SHOULD NOT OCCUR
         B     NOMEMBER       08-MEMBER DOES NOT EXIST
         EX    0,*            12-SHOULD NOT OCCUR
         B     IOERROR        16-IO ERROR IN DIRECTORY
         EJECT
***********************************************************************
***      PATTERN SUBCOMMAND    ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 2
PATTERN  OI    FLAGSOP,PATTOP          PATTERN MEMBER SEARCH DESIRED
         B     DISPLAY0                ENTER DISPLAY CODE
         SPACE 4
***********************************************************************
***      DISPLAY SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 2
DISPLAY  NI    FLAGSOP,X'FF'-PATTOP    DIRECTORY DISPLAY DESIRED
         TM    FLAGS,FMEMBER1+FMEMBER2 DISPLAY RANGE?
         BNO   DISPLAY0       NO
         LH    R14,LMEMBER1
         LH    R15,LMEMBER2
         CR    R14,R15        DETERMINE MIN NAME LENGTH
         BNH   DSPCHECK
         LR    R14,R15
DSPCHECK DS    0H
         SPACE
         CLC   MEMBER1(0),MEMBER2
         EX    R14,*-6        VALID DISPLAY RANGE?
         LA    R1,MSGRANGE    INVALID RANGE MESSAGE
         BH    MSGNEW         NO, BRANCH
         SPACE
DISPLAY0 DS    0H
         OI    FLAGSRD,F1STREAD+FATTN
         NI    FLAGSRD,X'FF'-FEXIST-FLINESET-FRANGE
         SPACE 2
         BALR  R3,0           RETURN ADDRESS
         LH    R4,LINESIZE    GET TERMINAL LINE SIZE
         LA    R0,11
         SR    R4,R0
         LA    R4,MSGTEXT1+3(R4) END OF LINE ADDRESS
         LA    R5,MSGTEXT1+4  START OF LINE
         SPACE
         CLEAR MSGTEXT1
         SPACE 2
DISPLAY2 DS    0H
         BAL   R14,READDIR    GET NEXT DIRECTORY ENTRY
         B     DISPLAY6       LAST DIRECTORY BLOCK PROCESSED
         B     DISPLAY6       LAST MEMBER IN DIRECTORY
         SPACE 2
         OI    FLAGSRD,FEXIST MEMBER EXISTS FLAG
         TM    FLAGSOP,PATTOP MEMBER NAME PATTERN MATCH DESIRED?
         BO    PATTERN1       YES, BRANCH
         TM    FLAGS,FMEMBER1 START ENTRY SPECIFIED?
         BZ    DISPLAY3       NO
         SPACE
         LH    R15,LMEMBER1   LENGTH-1 OF MEMBER NAME
         CLC   MEMBER1(0),MEMNAME
         EX    R15,*-6
         BH    DISPLAY2       NOT WANTED, GET NEXT
         XI    FLAGS,FMEMBER1
         SPACE 2
DISPLAY3 DS    0H
         TM    FLAGS,FMEMBER2 LAST ENTRY SPECIFIED?
         BZ    DISPLAY4       NO
         SPACE
         LH    R15,LMEMBER2
         CLC   MEMBER2(0),MEMNAME THIS PAST END?
         EX    R15,*-6
         BL    DISPLAY6       YES, END OF DISPLAY
         B     DISPLAY4       NO, DISPLAY THIS MEMBER NAME
         SPACE 1
PATTERN1 LH    R15,LMEMBER1   LENGTH-1 OF THE PATTERN
         LA    R1,8           MAXIMUM PATTERN LENGTH
         SR    R1,R15         NUMBER OF PATTERN COMPARE LOOPS
         LA    R14,MEMNAME    START SCAN POSITION
         CLC   0(*-*,R14),MEMBER1  <<EXECUTED>>
PATTERN2 EX    R15,*-6        PATTERN IN THIS MEMBER NAME?
         BE    PATTERN3         YES, CHECK FOR ANOTHER PATTERN
         LA    R14,1(,R14)      MAYBE, CHECK AT THE NEXT BYTE
         BCT   R1,PATTERN2             CHECK ALL POSSIBLE POSITIONS
         B     DISPLAY2         NO, IGNORE THIS DIRECTORY ENTRY
         SPACE 1
PATTERN3 TM    FLAGS,FMEMBER2 A SECOND PATTERN SPECIFIED?
         BZ    DISPLAY4       NO, DISPLAY THE MEMBER NAME
         LH    R15,LMEMBER2   LENGTH-1 OF THE PATTERN
         LA    R1,8           MAXIMUM PATTERN LENGTH
         SR    R1,R15         NUMBER OF PATTERN COMPARE LOOPS
         LA    R14,MEMNAME    START SCAN POSITION
         CLC   0(*-*,R14),MEMBER2  <<EXECUTED>>
PATTERN4 EX    R15,*-6        PATTERN IN THIS MEMBER NAME?
         BE    DISPLAY4         YES, DISPLAY THE MEMBER NAME
         LA    R14,1(,R14)      MAYBE, CHECK AT THE NEXT BYTE
         BCT   R1,PATTERN4             CHECK ALL POSSIBLE POSITIONS
         B     DISPLAY2         NO, IGNORE THIS DIRECTORY ENTRY
         SPACE 3
DISPLAY4 TRT   DIRNAME(8),TRTMN        CHECK FOR UNPRINTABLE
         BZ    DISPLAY8                SKIP IF ALL PRINTABLE
         TM    FLAGSRD,FLINESET        LINE IN PROGRESS?
         BZ    DISPLAY9                NO, SKIP THE PUTLINE
         LA    R1,MSGTEXT1
         SR    R5,R1
         SLL   R5,16
         ST    R5,MSGTEXT1
        $PUTLINE (R1),ATTN=NEWCMD
         NI    FLAGSRD,255-FLINESET
         CLEAR MSGTEXT1
         LA    R5,MSGTEXT1+4
         SPACE
DISPLAY9 UNPK  0(9,R5),DIRNAME(5)      FIRST HALF OF NAME
         UNPK  8(9,R5),DIRNAME+4(5)    SECOND HALF OF NAME
         TR    0(16,R5),TRTABLE        FINISH TRANSLATION
         MVI   16(R5),X'40'            CLEAN OUT GARBAGE
         TM    DIRFLAG,X'80'           ALIAS?
         BZ    *+10                    NO, SKIP THE '-A'
         MVC   16(L'IDALIAS,R5),IDALIAS SET ALIAS
         MVI   19(R5),C'*'             CHARACTER FORM OF MEMBER NAME
         MVC   20(8,R5),DIRNAME        MEMBER NAME FROM DIRECTORY
         LA    R0,8                    CHARACTERS IN MEMBER NAME
         SR    R1,R1                   IC REGISTER
         LA    R14,20(,R5)             CURRENT MEMBER CHARACTER
DPRINT   IC    R1,0(R14)               GET NEXT MEMBER CHARACTER
         LA    R15,TRTMN(R1)           CORRESPONDING TRANSLATE BYTE
         CLI   0(R15),X'FF'            UNPRINTABLE BYTE?
         BNE   *+8                     NO, BRANCH
         MVI   0(R14),C'Ö'             YES, DENOTE AS UNPRINTABLE
         LA    R14,1(,R14)             POINT AT NEXT MEMBER BYTE
         BCT   R0,DPRINT               LOOP FOR ALL EIGHT BYTES
*
         MVI   28(R5),C'*'
         OI    FLAGSRD,FLINESET+FRANGE OUTPUT AND MEMBERS EXIST NOW
         LA    R5,31(,R5)
         B     DISPLAY5
         SPACE
DISPLAY8 MVC   0(8,R5),DIRNAME
         TM    DIRFLAG,X'80'  ALIAS?
         BZ    DISPLAY7       NO
         MVC   8(L'IDALIAS,R5),IDALIAS
DISPLAY7 OI    FLAGSRD,FLINESET+FRANGE
         LA    R5,10+L'IDALIAS(R5)
         CR    R5,R4          LINE FULL?
         BL    DISPLAY2       NO, CONTINUE
         SPACE
DISPLAY5 LA    R1,MSGTEXT1
         SR    R5,R1
         SLL   R5,16
         ST    R5,MSGTEXT1
        $PUTLINE (R1),ATTN=NEWCMD
         SPACE
         NI    FLAGSRD,X'FF'-FLINESET
         BR    R3             RETURN
         SPACE 3
DISPLAY6 LA    R3,NEWCMD      EXIT ADDRESS
         TM    FLAGSRD,FLINESET BUFFER IN PROGRESS?
         BO    DISPLAY5       YES, OUTPUT IT
         SPACE 2
         TM    FLAGSRD,FRANGE MEMBER IN RANGE?
         BO    NEWCMD         YES
         TM    FLAGSRD,FEXIST NO, BUT ANY IN DIRECTORY?
         LA    R1,MSGEMPTY
         BZ    MSGNEW         NO, ** EMPTY DIRECTORY **
         LA    R1,MSGNODSP    YES, ASSUME NONE IN RANGE
         TM    FLAGSOP,PATTOP PATTERN CHECKING
         BNO   MSGNEW         NO, BRANCH
         LA    R1,MSGNOPAT    NO MATCHING MEMBER NAMES
         B     MSGNEW
         EJECT
***********************************************************************
***      ATTR SUBCOMMAND                                            ***
***********************************************************************
*
         SPACE 2
ATTR     DS    0H
         MVC   DIRNAME,MEMBER1 MEMBER NAME FOR BLDL
         SPACE
         XC    DIRFLAG(8),DIRFLAG      ENSURE ZEROES TO START
         BLDL  INDCB,BLDLLIST GET DIRECTORY ENTRY
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE
         B     *+4(R15)       PROCESS RETURN CODE
         SPACE 2
         B     ATTR1          00-SUCESSFUL
         B     NOMEMBER       04-MEMBER NOT FOUND
         B     IOERROR        08-I/O ERROR
         SPACE 3
ATTR1    DS    0H
         LA    R2,DIRUSER        LOAD START OF USER AREA (FOR SSI)
         TM    FLAGS,FLOADMOD    LOAD MODULE LIBRARY?
         BO    ATTR1A            YES, BRANCH
         MESSAGE MSGNLOAD        NOT A LOAD LIBRARY
         B     ATCK4SSI          PROCESS SSI INFORMATION
         SPACE
ATTR1A   TM    FLAGSCN,FAPFON+FAPFOFF  CHANGE APF PROCESSING?
         BZ    ATTR1B                  NO, BRANCH
         SPACE
         BAL   R14,CHKUSER       VERIFY DISP=OLD FOR NON-SYSTEMS USERS
         SPACE 1
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE APF INFORMATION?
         BNO   A1NOGO                      NO, BRANCH
         LA    R2,DIRAPF                   YES, POINT TO APF DATA
         SPACE 1
         TM    DIRATTR,ATTRSCAT  SCATTER LOAD?
         BNO   *+8               NO, BRANCH
         LA    R2,8(,R2)         SCATTER SIZE
         SPACE
         TM    DIRFLAG,X'80'     ALIAS?
         BNO   *+8               NO, BRANCH
         LA    R2,11(,R2)        ADD ALIAS LENGTH
         SPACE
         TM   DIRATTR2,DIR2SSI   SSI?
         BNO  *+12               NO, BRANCH
         LA   R2,5(,R2)          ADD 4 AND ROUND TO HALFWORD
         N    R2,=F'-2'
         SPACE
         CLI  0(R2),1            LENGTH RIGHT?
         BNE  A1NOGO             NO, BRANCH
         SPACE
         TM   FLAGSCN,FAPFON     TURN ON?
         MVI  1(R2),0            ASSUME TURN OFF
         BNO  *+8                NO, BRANCH
         MVI  1(R2),1            YES, USE ONE
         B    ATTR1D
         SPACE
A1NOGO   MESSAGE MSGADNC         INVALID APF DATA LENGTH
         B    ATTR3
         SPACE 2
ATTR1B   TM    FLAGS,FATTR       CHANGE ATTRIBUTES?
         BZ    ATTR3             NO, BRANCH
         SPACE
ATTR1D   BAL   R14,CHKUSER       VERIFY DISP=OLD FOR NON-SYSTEMS USERS
         XC    ATTRNO,FF
         OC    DIRATTR,ATTRYES
         NC    DIRATTR,ATTRNO
         SPACE 2
*
*   RESTRICTION: BOTH RENT AND REUS MUST BE SPECIFIED FOR REENTRANT
         TM    DIRATTR,ATTRRENT   WAS REENTRABLE SPECIFIED?
         BNO   *+8                NO, BRANCH
         OI    DIRATTR,ATTRREUS   YES, ALSO FORCE REUSABLE
         SPACE
         BAL   R14,OPENSTOW   OPEN STOW DCB
         BZ    NEWCMD         EXIT IF DATA SET NOT OPENED
         MVC   DCBRELAD-IHADCB+STOWDCB,DIRTTR
         MVI   DCBRELAD-IHADCB+STOWDCB+3,0
         STOW  STOWDCB,DIRNAME,R  REPLACE OPTION
         SPACE 2
         B     *+4(R15)       PROCESS RETURN CODE
         SPACE
         B     ATTR3          00-SUCESSFUL
         EX    0,*            04-SHOULD NOT OCCUR
         EX    0,*            08-SHOULD NOT OCCUR
         B     FULLDIR        12-DIRECTORY FULL
         B     IOERROR        16-I/O ERROR
         SPACE 2
ATTR3    DS    0H
         MVI   ATTNECB,0      CAN NOT INTERRUPT THIS
        $PUTLINE MSGATTRS     ATTRIBUTES MSG
         SPACE 2
         CLEAR MSGTEXT1
         SPACE 2
         LA    R1,MSGTEXT1+3  INDENT A SPACE
         SR    R0,R0          NO ATTRIBUTES FLAG
         SPACE
         LA    R14,TBLATTR-12 ATTRIBUTES TABLE
         LH    R2,DIRATTR     GET MODULE ATTRIBUTES
         SPACE 2
NEXTATTR DS    0H
         LA    R14,12(R14)
         CLI   0(R14),X'FF'   END OF TABLE?
         BE    LASTATTR       YES
         SPACE
         LH    R15,0(R14)     GET ATTRIBUTE FLAGS
         SPACE
         TM    2(R14),X'80'   POSITIVE OR NEGATIVE ATTRIBUTE?
         LA    R3,X'80'       MASK FOR POS (FOR BZ INST)
         BZ    *+8
         LA    R3,X'70'       MASK FOR NEG (FOR BNZ) INST
         SPACE
         NR    R15,R2         CHECK FOR ATTRIBUTE PRESENT
         NOP   NEXTATTR
         EX    R3,*-4
         SPACE
         BALR  R0,0           INDICATE ATTRIBUTE EXISTS
         LA    R3,X'7F'
         MVI   0(R1),C','     DELIMITER AFTER LAST ATTR
         IC    R15,2(R14)     LENGTH-1 OF ATTRIBUTE
         NR    R15,R3
         MVC   2(0,R1),3(R14)
         EX    R15,*-6
         LA    R1,3(R15,R1)   JUMP POINTER
         B     NEXTATTR
         SPACE 3
LASTATTR DS    0H
         LTR   R0,R0          ANY ATTRIBUTES PRESENT?
         LA    R1,NONEMSG
         BZ    NOATTRS        NO
         SPACE
         LA    R1,L'MSGTEXT1
         SLL   R1,16
         ST    R1,MSGTEXT1
         LA    R1,MSGTEXT1
         SPACE
NOATTRS  DS    0H
        $PUTLINE (R1)
         SPACE
         CLEAR MSGTEXT1
         LA    R1,L'MSGTEXT1
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE 2
*  FORMAT NEW VS INFORMATION
         SPACE
         LA    R2,DIRAPF      POINT TO APF INFORMATION
         SPACE
         TM    DIRATTR,ATTRSCAT  SCATTER FORMAT?
         BNO   *+8               NO, BRANCH
         LA    R2,8(,R2)         YES, ADD SCATTER SIZE BYTES
         SPACE
         TM    DIRFLAG,X'80'  ALIAS?
         BNO   *+8            NO, BRANCH
         LA    R2,11(,R2)     ADD ALIAS LENGTH
         SPACE
         TM    DIRATTR2,DIRAOSLE VS LINKAGE EDITOR?
         BO    ATCKSSI           YES, BRANCH
         MESSAGE MSGNOVSL        NON-VS LINKAGE EDITOR MESSAGE
         SPACE
         TM    DIRFLAG,X'80'  ALIAS?
         BZ    ATGOTSSI       NO, BRANCH
         B     ATNOSSI
         SPACE
ATCKSSI  TM    DIRATTR2,DIR2SSI  SSI PRESENT?
         BNO   ATNOSSI           NO, SKIP SSI PROCESSING
         SPACE
ATGOTSSI LA    R2,1(,R2)      ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE
ATCK4SSI CLC   =F'0',0(R2)    ZERO?
         BE    ATNOSSI        YES, NO SSI
         CLC   =F'-1',0(R2)   FFFFFFFF?
         BE    ATNOSSI        YES, NO SSI
         SPACE
         CLEAR MSGTEXT1
         LA    R1,L'MSGTEXT1
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE
         MVC   MSGTEXT1+5(16),=C'SSI INFORMATION:'
         UNPK  MSGTEXT1+22(9),0(5,R2)
         TR    MSGTEXT1+22(8),TRTABLE
         MVI   MSGTEXT1+30,X'40'  CLEAR GARBAGE BYTE
        $PUTLINE MSGTEXT1
         CLEAR MSGTEXT1
         LA    R1,L'MSGTEXT1
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE
         TM    FLAGS,FLOADMOD LOAD MODULE LIB?
         BZ    ATTR2          NO, BRANCH
         LA    R2,4(,R2)      ADD SSI SIZE
         SPACE
ATNOSSI  TM    DIRATTR2,DIRAOSLE  VS LINKAGE EDITOR?
         BZ    ATTR2              NO, CANNOT BE AUTHORIZED
         TM    DIRATTR2,DIRAPFLG  APF DATA PRESENT AND VALID?
         BO    ATAPF
         MESSAGE MSGNOAPF     NO APF BYTES
         B     ATTR2
         SPACE
ATAPF    CLI   0(R2),1        APF DATA LENGTH OK?
         BE    ATAPFLOK       YES, BRANCH
         MESSAGE MSGAPFLB     NO, WRONG LENGTH APF DATA
         B     ATTR2
         SPACE
ATAPFLOK CLI   1(R2),1        AUTHORIZED?
         BE    ATAPFOK        YES, BRANCH
         BH    ATAPF2B        NO, TOO LARGE
         MESSAGE MSGNAUTH     NO, APF=0
         B     ATTR2
         SPACE
ATAPFOK  MESSAGE MSGYAUTH
         B     ATTR2
         SPACE
ATAPF2B  MESSAGE MSGAPF2B
         SPACE 2
ATTR2    OI    FLAGSRD,FATTN
         NI    FLAGSRD,255-FLINESET
         TM    DIRFLAG,X'80'    IS MODULE AN ALIAS?
         BO    MAPAREAL         YES, FIND THE REAL ENTRY
         SPACE
         MVC   SAVETTR,DIRTTR   TTR TO SEARCH FOR
         OI    FLAGSRD,F1STREAD
         SPACE 2
ATTR4    BAL   R14,READDIR    GET NEXT DIRECTORY MEMBER
         B     ATTRLAST       END OF DIRECTORY
         B     ATTRLAST       LAST MEMBER PROCESSED
         SPACE 2
         TM    DIRFLAG,X'80'  AN ALIAS?
         BZ    ATTR4          NO, IGNORE
         CLC   SAVETTR,DIRTTR WANTED MODULE?
         BNE   ATTR4          NO, BRANCH
         SPACE
         TM    FLAGSRD,FLINESET LINE IN PROGRESS?
         BO    ATTR5A           YES, BRANCH
         MESSAGE MSGOTHER
         SPACE
         CLEAR MSGTEXT1
         LA    R5,MSGTEXT1+3
         OI    FLAGSRD,FLINESET
         SPACE
ATTR5A   LA    R2,DIRNAME+7
ATTR5B   CLI   0(R2),X'40'
         BNE   ATTR5C
         BCT   R2,ATTR5B
         SPACE
ATTR5C   LA    R0,DIRNAME
         SR    R2,R0
         BNM   ATTR5D
         SR    R2,R2
ATTR5D   LA    R15,0(R5,R2)
         LA    R1,MSGTEXT1
         SR    R15,R0
         CH    R15,LINESIZE
         BNH   ATTR5E
         SLL   R15,16
         ST    R15,MSGTEXT1
        $PUTLINE MSGTEXT1
         CLEAR MSGTEXT1
         LA    R5,MSGTEXT1+3
         SPACE
ATTR5E   MVI   0(R5),C','
         MVC   2(*-*,R5),DIRNAME  <<EXECUTED>>
         EX    R2,*-6
         LA    R5,3(R2,R5)
         B     ATTR4
         SPACE 3
ATTRLAST TM    FLAGSRD,FLINESET   LINE IN PROGRESS?
         BZ    NEWCMD             NO, EXIT
         LA    R1,MSGTEXT1
         SR    R5,R1
         SLL   R5,16
         ST    R5,MSGTEXT1
         B     MSGNEW
         EJECT
***********************************************************************
***      SMAP SUBCOMMAND       ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 1
*        SMAP DISCARDS RECORDS FOR ENTRY NAMES WITHIN CSECTS
SMAP     OI    FLAGSOP,FSMAP  SHORT MAP FLAG
         B     MAP+4          ENTER MAP CODING
         SPACE 4
***********************************************************************
***      MAP SUBCOMMAND                                             ***
***********************************************************************
*
         SPACE 1
MAP      NI    FLAGSOP,255-FSMAP  TURN OFF SHORT MAP FLAG
         TM    FLAGS,FLOADMOD PROCESSING LOAD LIBRARY?
         LA    R1,MSGNLOAD    NOT A LOAD LIBRARY MESSAGE
         BNO   MSGNEW         NO, BRANCH
         SPACE
         MVI   ENTRYPT,C'?'    NO ENTRY POINT YET
         OI    FLAGSRD,FATTN   ALLOW INTERRUPTIONS
         MVC   DIRNAME,MEMBER1 MEMBER NAME TO BLDL LIST
         BLDL  INDCB,BLDLLIST  LOCATE DIRECTORY ENTRY
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE
         B     *+4(R15)       PROCESS RETURN CODE
         SPACE
         B     MAP2           00-SUCESSFUL
         B     NOMEMBER       04-MEMBER NOT FOUND
         B     IOERROR        08-I/O ERROR
         SPACE 2
MAP2     DS    0H
         OI    FLAGSRD,FALLESD INDICATE LIST REQUEST
         SPACE 2
         BAL   R14,READCESD   FORMAT THE CESD DATA
         B     MAPNOESD
         SPACE 2
         LA    R4,ESDPTR      ESD TABLE CHAIN POINTER
         SR    R5,R5          CLEAR SEGTAB/ENTAB SWITCH
         USING ESDENTRY,R4    BASE FOR TABLE
         SPACE 2
MAPESD   ICM   R4,15,ESDLINK  END OF CHAIN?
         BZ    MAPLAST        YES, BRANCH
         CLEAR MSGTEXT1
         SPACE
         CLI   ESDTYPE,CODESD THIS EXTERNAL DEFINATION?
         BE    MAPSD          YES
         SPACE
         CLI   ESDTYPE,CODELR THIS EXTERNAL REFERENCE?
         BE    MAPLR          YES
         SPACE
         CLI   ESDTYPE,CODEPC PRIVATE CODE?
         BE    MAPPC          YES
         SPACE
*        MUST BE SEGTAB OR ENTAB ENTRY
         SPACE
         LTR   R5,R5          SEGTAB PROCESSED?
         MVC   MSGTEXT1+4(8),$SEGTAB
         BZ    MAPSEGTB
         MVC   MSGTEXT1+4(8),$ENTAB
MAPSEGTB DS    0H
         BALR  R5,0           SEGTAB PROCESSED CODE
         B     MAPSD1
         SPACE 2
MAPPC    DS    0H
MAPSD    DS    0H
         MVC   MSGTEXT1+4(8),ESDNAME
         SPACE
MAPSD1   DS    0H
         TM    DIRATTR,ATTROVLY OVERLAY PROGRAM?
         BZ    MAPNOVLY       NO
         SPACE
         SR    R0,R0
         IC    R0,ESDSEG#
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+30(2),DOUBLE+6(2)
         SPACE 3
MAPNOVLY DS    0H
         CLC   ESDADDR(3),DIRENTRY   ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,MSGTEXT1+4    YES, SAVE THE ENTRY POINT NAME
         SPACE
         MVC   DOUBLE(3),ESDADDR
         MVI   DOUBLE+3,X'04'
         UNPK  MSGTEXT1+14(7),DOUBLE(4)
         TR    MSGTEXT1+14(6),TRTABLE
         MVC   DOUBLE(3),ESDLEN
         UNPK  MSGTEXT1+22(7),DOUBLE(4)
         TR    MSGTEXT1+22(6),TRTABLE
         LA    R1,32
         B     MAPPRINT
         SPACE 3
MAPLR    CLC   ESDADDR(3),DIRENTRY   ENTRY POINT?
         BNE   MAPSM                 NO, BRANCH
         MVC   ENTRYPT,ESDNAME       YES, SAVE THE ENTRY POINT NAME
         B     MAPNOSM               ALWAYS MAP THIS SYMBOL (ENTRY PT)
MAPSM    TM    FLAGSOP,FSMAP         SHORT MAP?
         BO    MAPESD                YES, BRANCH
MAPNOSM  MVC   MSGTEXT1+30(8),ESDNAME
         MVC   DOUBLE(3),ESDADDR
         MVI   DOUBLE+3,X'04'
         UNPK  MSGTEXT1+40(7),DOUBLE(4)
         TR    MSGTEXT1+40(6),TRTABLE
         LA    R1,46
         SPACE
MAPPRINT DS    0H
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE
        $PUTLINE MSGTEXT1,ATTN=NEWCMD
         SPACE 3
         B     MAPESD
         SPACE 2
MAPLAST  DS    0H
         BAL   R14,FREEESD
         SPACE 3
         CLEAR MSGTEXT1
         LA    R0,6
         SLL   R0,16
         ST    R0,MSGTEXT1
        $PUTLINE MSGTEXT1,ATTN=NEWCMD
         SPACE 3
MAPNOESD MVC   MSGTEXT1,MSGENTRY
         LH    R15,MSGTEXT1
         LA    R14,MSGTEXT1(R15)
         LA    R15,20(,R15)
         STH   R15,MSGTEXT1
         SPACE
         MVC   DOUBLE(3),DIRENTRY
         MVI   DOUBLE+3,X'04'
         UNPK  0(7,R14),DOUBLE(4)
         TR    0(6,R14),TRTABLE
         MVC   06(8,R14),BLANKS
         MVC   08(2,R14),=C'--'
         MVC   12(8,R14),ENTRYPT      NAME OF THE ENTRY POINT
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BNE   *+10                   YES, BRANCH
         MVC   8(12,R14),=CL12'*NONE FOUND*'
         SPACE 2
         NI    FLAGSRD,X'FF'-FATTN
         MVI   ATTNECB,0
        $PUTLINE MSGTEXT1
         SPACE 3
         MVC   MSGTEXT1,MSGLEN
         LH    R15,MSGTEXT1
         LA    R14,MSGTEXT1(R15)
         LA    R15,6(R15)
         STH   R15,MSGTEXT1
         SPACE
         MVC   DOUBLE(3),DIRCORE
         UNPK  0(7,R14),DOUBLE(4)
         TR    0(6,R14),TRTABLE
         SPACE
        $PUTLINE MSGTEXT1
         TM    DIRFLAG,X'80'  IS MODULE AN ALIAS?
         BZ    NEWCMD         NO, NO ALIAS INFORMATION
         SPACE
MAPAREAL MVC   SAVETTR,DIRTTR     FIND REAL MODULE (MULTIPLE ENTRIES)
         MVC   MSGTEXT1,MSGNOMOD                    ----------------
         OI    FLAGSRD,F1STREAD
         SPACE 2
MAPALIAS DS    0H
         BAL   R14,READDIR    GET NEXT DIRECTORY MEMBER
         B     MAPREAL        END OF DIRECTORY
         B     MAPREAL        LAST MEMBER PROCESSED
         SPACE 2
         TM    DIRFLAG,X'80'  THIS MODULE AN ALIAS?
         BO    MAPALIAS       YES, IGNORE
         SPACE
         CLC   SAVETTR,DIRTTR WANTED  MODULE?
         BNE   MAPALIAS       NO
         SPACE
         MVC   MSGTEXT1,MSGREAL
         LH    R1,MSGTEXT1
         LA    R0,8(R1)
         STH   R0,MSGTEXT1
         LA    R1,MSGTEXT1(R1)
         MVC   0(8,R1),DIRNAME
         SPACE
MAPREAL  DS    0H
         LA    R1,MSGTEXT1
         B     MSGNEW
         SPACE
         DROP  R4
         EJECT
***********************************************************************
***      HISTORY SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 2
HISTORY  DS    0H
         TM    FLAGS,FLOADMOD PROCESSING LOAD LIBRARY?
         LA    R1,MSGNLOAD    NOT A LOAD LIBRARY MESSAGE
         BNO   MSGNEW         NO, BRANCH
         SPACE
         OI    FLAGSRD,FATTN  ALLOW INTERRUPTIONS
         MVC   DIRNAME,MEMBER1 MEMBER NAME TO BLDL LIST
         BLDL  INDCB,BLDLLIST FIND THE MEMBER
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE
         B     *+4(R15)       PROCESS RETURN CODE
         SPACE
         B     HISTORY2       00-SUCESSFUL
         B     NOMEMBER       04-MEMBER NOT FOUND
         B     IOERROR        08-I/O ERROR
         SPACE 2
NOHIST   MESSAGE MSGNHIST
         B     EXITHIST
         SPACE 2
HISTORY2 DS    0H
         OI    FLAGSRD,FALLESD INDICATE BUILD ESD LIST
         SPACE 2
         BAL   R14,READCESD   FORMAT THE CESD DATA
         NOP   0              NO ESD ENTRIES AVAILABLE, CONTINUE
         SPACE 2
         BAL   R14,READIDR    FORMAT THE HISTORY DATA
         B     NOHIST         HISTORY NOT AVAILABLE
         SPACE 3
         MVC   MSGTEXT1,MSGHISTD LAST LINKEDIT DATE MSG
         LH    R1,MSGTEXT1
         LA    R0,L'DATEMASK(R1)
         STH   R0,MSGTEXT1
         LA    R1,MSGTEXT1(R1)
         SPACE 2
         MVC   0(L'DATEMASK,R1),DATEMASK
         ED    0(L'DATEMASK,R1),LKEDDATE
         SPACE
        $PUTLINE MSGTEXT1,ATTN=EXITHIST
         SPACE
         ICM   R0,15,ESDPTR   ANY ESD ENTRIES?
         BZ    EXITHIST       NO, TERMINATE
         EJECT
*
*        FORMAT IMASPZAP IDR DATA ENTRIES
*
         LA    R2,ESDPTR      START OF ESD CHAIN
         USING ESDENTRY,R2
         OI    FLAGSRD,F1IDR
         SPACE
HISTZAP  ICM   R2,15,ESDLINK  END OF ESD CHAIN?
         BZ    HISTZAP0       YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODESD CSECT ENTRY?
         BNE   HISTZAP        NO, SKIP THIS
         SPACE
         LA    R3,IDRPTR      SCAN IDR CHAIN
         USING IDRENTRY,R3
         SPACE
HISTZAP1 ICM   R3,15,IDRLINK  END OF ESD CHAIN?
         BZ    HISTZAP        YES, BRANCH
         SPACE
         CLC   ESDID,IDRESDID REQUESTED IDR RECORD?
         BNE   HISTZAP1       NO
         CLI   IDRTYPE,IDRZAP IMASPZAP ENTRY?
         BNE   HISTZAP1       NO
         SPACE
         TM    FLAGSRD,F1IDR  FIRST IDR RECORD?
         BZ    HISTZAP2       NO
         XI    FLAGSRD,F1IDR
        $PUTLINE MSGHISTZ,ATTN=EXITHIST  IMASPZAP HISTORY TITLE
         SPACE
HISTZAP2 DS    0H
         CLEAR MSGTEXT1
         SPACE
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(L'IDRZDATA),IDRZDATA
         SPACE
         LA    R0,L'ESDNAME+L'DATEMASK+L'IDRZDATA+12
         SLL   R0,16
         ST    R0,MSGTEXT1
        $PUTLINE MSGTEXT1,ATTN=EXITHIST
         B     HISTZAP1       NEXT IDR DATA RECORD THIS ESD ENTRY
         EJECT
HISTZAP0 DS    0H
         SPACE 3
*
*        FORMAT THE USER-SUPPLIED IDR DATA RECORDS
*
         SPACE 2
         LA    R2,ESDPTR      ADDRESS OF ESD CHAIN
         OI    FLAGSRD,F1IDR
         SPACE
HISTUSER ICM   R2,15,ESDLINK  END OF ESD CHAIN?
         BZ    HISTUSR0       YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODESD CSECT ENTRY?
         BNE   HISTUSER       NO, NEXT ESD ENTRY
         SPACE 2
         LA    R3,IDRPTR
         SPACE
HISTUSR1 ICM   R3,15,IDRLINK  END OF IDR CHAIN?
         BZ    HISTUSER       YES, BRANCH
         SPACE
         CLI   IDRTYPE,IDRUSER USER IDR DATA RECORD?
         BNE   HISTUSR1       NO
         CLC   ESDID,IDRESDID WANTED ESD RECORD?
         BNE   HISTUSR1       NO
         SPACE 2
         TM    FLAGSRD,F1IDR  TITLE LINE REQUIRED?
         BZ    HISTUSR2       NO
         XI    FLAGSRD,F1IDR
         SPACE
        $PUTLINE MSGHISTU,ATTN=EXITHIST  USER-SUPPLIED IDR TITLE
         SPACE 2
HISTUSR2 DS    0H
         CLEAR MSGTEXT1
         SPACE 2
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         SR    R1,R1
         IC    R1,IDRLDATA
         CLI   IDRLDATA,X'FF'     ZERO BYTES?
         BE    HISTUSRS           YES, SKIP THE MOVE (LENGTH ZERO)
         SPACE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(0),IDRDATA
         EX    R1,*-6
         B     HISTUSRS+2         LENGTH IN R1 IS VALID
         SPACE 2
HISTUSRS SR    R1,R1              R1 LENGTH IS NOW ZERO
         SPACE
         LA    R1,13+L'ESDNAME+L'DATEMASK(R1)
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE 2
        $PUTLINE MSGTEXT1,ATTN=EXITHIST
         B     HISTUSR1
HISTUSR0 DS    0H
         SPACE 3
EXITHIST DS    0H
         BAL   R14,FREEESD
         BAL   R14,FREEIDR
         SPACE 2
         NI    FLAGSRD,X'FF'-F1IDR
         B     NEWCMD
         SPACE 2
         DROP  R2,R3
         EJECT
*
*
*        IDR SCAN SUBROUTINE
*
*
         SPACE 3
READIDR  DS    0H
         STM   14,12,12(13) SAVE REGISTERS
         SPACE 2
         XC    IOB(LIOB),IOB
         SPACE
         MVC   0(3,R13),DIRTTR
         MVI   3(R13),0
         L     R0,0(R13)      GET STARTING TTR OF MODULE
         LA    R2,MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         SPACE 3
         LR    R3,R13
         L     R15,ADRPCNVT      ADDRESS OF TTR CONVERT ROUTINE
         BALR  R14,R15
         LR    R13,R3
         LM    14,12,12(13)
         SPACE 2
*
*        BUILD CHANNEL PGM TO READ IDR RECORDS
*
         SPACE 2
         LA    R0,IOBSEEK     ADDRESS OF SEEK FIELD
         LA    R1,CCWS
         LA    R2,IDRBUFF
         LA    R3,IOBSEEK
         XC    CCWS(LIDRCCWS),CCWS
         ST    R0,CCWS
         ST    R1,CCWS+8
         ST    R2,CCWS+16
         ST    R3,CCWS+24
         OC    CCWS(LIDRCCWS),IDRCCWS
         SPACE 2
         ST    R1,IOBCCW
         LA    R1,INDCB
         ST    R1,IOBDCB
         LA    R1,IOECB
         ST    R1,IOBECB
         MVI   IOBFLAG,X'42'
         SPACE
         LA    R2,IDRPTR
         USING IDRENTRY,R2
         NI    FLAGSRD,X'FF'-FIDR-F1IDR
         SPACE
IDREXCP  DS    0H
         MVI   IOECB,0
         EXCP  IOB            START I/O
         SPACE
         WAIT  ECB=IOECB
         MVI   DCBIFLGS-IHADCB+INDCB,0
         SPACE
         CLI   IOECB,X'42'    EXTENT VIOLATION?
         BNE   IDREXCP1       NO
         BAL   R14,NEWEXTNT   YES, ADJUST FOR NEW EXTENT
IDREXCP1 DS    0H
         SPACE
         CLI   IOECB,X'7F'    SUCESSFUL?
         BNE   LASTIDR        NO, MUST BE LAST IDR DATA
         SPACE
         CLI   IDRBUFF,X'40'  TEST SYM RECORD?
         BE    IDREXCP        YES, SKIP RECORD
         SPACE
         CLI   IDRBUFF,X'20'  CESD RECORD?
         BE    IDREXCP        NO
         SPACE
         CLI   IDRBUFF,X'80'  IDR RECORD?
         BNE   LASTIDR        NO
         SPACE
         LA    R3,IDRBUFF+3   START OF IDR DATA
         TM    FLAGSCN,FIDRHCON     CONTINUED HEADER?
         BZ    IDREXCP3             NO, BRANCH
         SPACE
         L     R15,IDROFSAV         PICK UP AMOUNT WE GOT
         LA    R14,IDRUPREF+1(R15)  TARGET OF MOVE
         LA    R1,IDRBUFF+3+5-1     FIND LENGTH BYTE
         SR    R1,R15               BACK UP BY WHAT WE GOT
         SR    R4,R4                ZERO FOR IC
         IC    R4,0(,R1)            PICK UP LENGTH
         LR    R1,R4                SAVE FOR LATER
         LA    R4,4(,R1)            TOTAL LENGTH OF RECORD
         MVI   IDRTYPE,IDRUSER      RECORD TYPE
         BCTR  R1,0                 MACHINE LENGTH
         STC   R1,IDRLDATA          SAVE LENGTH
         SR    R4,R15               LENGTH TO MOVE
         MVC   0(*-*,R14),IDRBUFF+3 <<EXECUTED>>
         EX    R4,*-6               MOVE INTO THE RECORD
         MVC   IDRESDID,IDRUPREF    SET ESDID
         NI    FLAGSCN,255-FIDRHCON OFF FLAG
         LA    R3,1(R3,R4)          ADJUST TO NEXT RECORD
         SPACE
IDREXCP3 TM    FLAGSCN,FIDRCONT     CONTINUED IDR RECORD?
         BZ    IDREXCP2             NO, BRANCH
         SPACE
         SR    R5,R5                WHERE TO START MOVE
         IC    R5,IDRLDATA          DATA LENGTH
         LA    R5,IDRDATA+1(R5)     END OF DATA
         L     R15,IDROFSAV         LENGTH IN THIS RECORD
         SR    R5,R15               PLACE TO START MOVE
         LA    R3,0(R15,R3)         MOVE PAST CONTINUED PART
         BCTR  R15,0                MACHINE LENGTH
         MVC   0(*-*,R5),IDRBUFF+3  <<EXECUTED>>
         EX    R15,*-6              ADD THE REMAINDER
         NI    FLAGSCN,255-FIDRCONT TURN OFF CONTINUED FLAG
         SPACE 2
IDREXCP2 SR    R5,R5
         IC    R5,IDRBUFF+1   BYTE COUNT THIS RECORD
         LA    R5,IDRBUFF(R5) END OF BUFFER ADDRESS
         SPACE 2
         TM    IDRBUFF+2,X'80' LAST RECORD OF LOAD MODULE?
         BZ    *+8            NO
         OI    FLAGSRD,F1IDR  YES, SET STOP FLAG
         SPACE
         CR    R5,R3          END OF BUFFER LESS THAN CONTINUED START?
         BNH   NEXTIDR        YES, CONTINUATION (ALREADY DONE ABOVE)
         SPACE
         NI    IDRBUFF+2,X'0F' CLEAR MISC FLAGS
         CLI   IDRBUFF+2,IDRZAP IMASPZAP IDR RECORD?
         BE    ZAPIDR         YES
         CLI   IDRBUFF+2,IDRLKED LINKAGE EDITOR IDR RECORD?
         BE    LKEDIDR        YES
         CLI   IDRBUFF+2,IDRUSER USER-SUPPLIED IDR RECORD?
         BNE   NEXTIDR        NO, NEXT RECORD
         SPACE 3
*        PROCESS USER-SUPPLIED DATA RECORDS
         SPACE
USERIDR  DS    0H
         BAL   R14,GETIDR     GET A NEW IDR RECORD
         ST    R1,IDRLINK
         LR    R2,R1          ADDRESS OF NEW RECORD
         SPACE 2
         LR    R15,R5            CHECK FOR SPANNED RECORD HEADER
         SR    R15,R3            FIND BYTES LEFT IN HEADER
         LR    R14,R15           SAVE VALUE
         SH    R15,=H'5'         ROOM FOR HEADER?
         BNM   USERIDR0          YES, BRANCH
         OI    FLAGSCN,FIDRHCON  TURN ON HEADER CONTINUED FLAG
         MVC   IDRUPREF(0),0(R3) <<EXECUTED>>
         EX    R14,*-6           MOVE IN DATA
         ST    R14,IDROFSAV      SAVE LENGTH FOR LATER
         B     NEXTIDR           GET NEXT IDR RECORD
         SPACE
USERIDR0 MVI   IDRTYPE,IDRUSER
         MVC   IDRESDID,0(R3) MOVE ESDID TO IDR AREA
         LA    R1,2(R3)       YYDDD IDR RECORD CREATED
         BAL   R14,SETIDRDT
         SR    R15,R15
         IC    R15,5(R3)      LENGTH OF USER DATA AREA
         LA    R4,6(R15)      LENGTH OF THIS RECORD ENTRY
         MVI   IDRTYPE,IDRUSER INDICATE USER-SUPPLIED ENTRY
         BCTR  R15,0
         BCTR  R4,0              MACHINE LENGTH FOR MOVE
         STC   R15,IDRLDATA
         SPACE
         MVC   IDRUPREF(0),0(R3) MOVE USER DATA TO RECORD
         EX    R4,*-6
         LA    R4,1(,R4)         RESTORE ACTUAL LENGTH
         SPACE 2
         BXLE  R3,R4,USERIDR
         SR    R3,R5             CHECK FOR SPANNED RECORDS
         BCTR  R3,0
         LTR   R3,R3             ENTRY SPAN AN IDR BLOCK?
         BNP   NEXTIDR           NO, BRANCH
         OI    FLAGSCN,FIDRCONT  MARK FOR CONTINUED PROCESSING
         ST    R3,IDROFSAV       SAVE AMOUNT IN NEXT IDR BLOCK
         B     NEXTIDR
         SPACE 3
*        PROCESS IMASPZAP IDR RECORDS
         SPACE
ZAPIDR   DS    0H
         SR    R6,R6
         NI    0(R3),X'3F'
         IC    R6,0(R3)       GET COUNT OF IMASPZAP ENTRIES
         LA    R3,1(R3)
         LA    R6,1(R6)       JUMP COUNT FOR LOOP
         B     ZAPIDR1
         SPACE 2
ZAPIDR2  DS    0H
         BAL   R14,GETIDR
         ST    R1,IDRLINK
         LR    R2,R1
         SPACE
         MVC   IDRESDID,0(R3) MOVE ESDID TO IDR RECORD
         SPACE
         LA    R1,2(R3)       ADDRESS OF DATE OF RECORD
         BAL   R14,SETIDRDT
         SPACE
         MVC   IDRDATA,5(R3)  MOVE DATA TO IDR RECORD
         MVI   IDRLDATA,8     SET LENGTH FOR COMPATIBILITY
         MVI   IDRTYPE,IDRZAP INDICATE IMASPZAP ENTRY
         SPACE
         LA    R3,13(R3)      JUMP DATA ADDRESS
         SPACE
ZAPIDR1  DS    0H
         BCT   R6,ZAPIDR2     IF ANOTHER ENTRY THIS RECORD
         B     NEXTIDR        READ NEXT IDR RECORD
         SPACE 3
*        LINKAGE EDITOR IDR RECORD PROCESSOR
         SPACE
LKEDIDR  OI    FLAGSRD,FIDR
         LA    R1,12(R3)      ADDRESS OF DATE
         LA    R15,LKEDDATE
         BAL   R14,SETLKDTE
         SPACE 2
NEXTIDR  TM    FLAGSRD,F1IDR  LAST IDR RECORD?
         BZ    IDREXCP        NO
         SPACE 3
LASTIDR  NI    FLAGSCN,255-FIDRCONT-FIDRHCON  OFF CONTINUATION FLAGS
         LA    R2,IDRPTR
SORTIDR  ICM   R2,15,IDRLINK
         BZ    SORTIDR1
         SPACE
         LR    R1,R2
         SPACE
SORTIDR2 DS    0H
         ICM   R1,15,IDRLINK-IDRENTRY(R1)  GET THE NEXT ENTRY
         BZ    SORTIDR
         SPACE
         CLC   IDRDATE,IDRDATE-IDRENTRY(R1)
         BNL   SORTIDR2
         XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         XC    IDRSTART-IDRENTRY(LENIDR1,R1),IDRSTART
         XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         B     SORTIDR2
         SPACE 2
SORTIDR1 DS    0H
         LM    R14,12,12(R13)
         TM    FLAGSRD,FIDR   ANY IDR RECORDS?
         BZR   R14            NO
         B     4(,R14)        YES
         SPACE 2
SETIDRDT DS    0H
         LA    R15,IDRDATE
         DROP  R2
         SPACE
SETLKDTE DS    0H
         ST    R14,R14SAVE
         MVC   DAYTABLE,DAYMONTH
         XC    DOUBLE,DOUBLE
         SR    R14,R14        SETUP FOR LEAP YEAR TEST
         IC    R14,0(,R1)
         SLL   R14,4
         ST    R14,DOUBLE+4
         OI    DOUBLE+7,X'0F'
         LR    R14,R1
         CVB   R0,DOUBLE      GET YEAR IN BINARY
         SR    R1,R1
         SRDL  R0,2           SHIFT OFF REMAINDER OF YEAR/4
         LTR   R1,R1          THIS A LEAP YEAR?
         BNZ   *+8            NO
         MVI   DAYTABLE+1,29  YES, FEB HAS 29 DAYS
         SPACE
         MVC   2(1,R15),0(R14) YEAR RECORD CREATED
         MVC   DOUBLE+6(2),1(R14)
         CLI   DOUBLE+7,0     BAD DATE?
         BE    DAYSKIP        YES, DON'T 0C7
         CVB   R1,DOUBLE      GET RELATIVE DAYS IN BINARY
         SPACE
         LA    R14,DAYTABLE
         SR    R0,R0
DAYLOOP  DS    0H
         IC    R0,0(R14)      DAYS THIS MONTH
         SR    R1,R0          WITHIN THIS MONTH?
         BNP   DAYNOW
         LA    R14,1(R14)
         B     DAYLOOP
DAYNOW   DS    0H
         AR    R1,R0          GET DAY OF THE MONTH
         LA    R0,10
         MR    R0,R0
         CVD   R1,DOUBLE
         MVC   1(1,R15),DOUBLE+6
         LA    R1,DAYTABLE-1
         SR    R14,R1
         LA    R1,10
         MR    R0,R14
         CVD   R1,DOUBLE
         MVC   0(1,R15),DOUBLE+6
         SPACE
DAYSKIP  L     R14,R14SAVE
         BR    R14
         EJECT
*
*
*        CESD SCAN SUBROUTINE
*
*
         SPACE 3
READCESD DS    0H
         ST    R14,R14LINK
         SPACE
         TM    DIRATTR+1,ATTRNE  DOES MODULE CONTAIN ESD ENTRIES?
         MVC   MSGTEXT1,MSGNOESD
         BOR   R14               NO, EXIT W/ ERROR
         STM   14,12,12(13) SAVE REGISTERS
         SPACE 2
         XC    IOB(LIOB),IOB
         SPACE
         MVC   0(3,R13),DIRTTR
         MVI   3(R13),0
         L     R0,0(R13)      GET STARTING TTR OF MODULE
         LA    R2,MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         SPACE 3
         LR    R3,R13
         L     R15,ADRPCNVT      TTR CONVERT
         BALR  R14,R15
         LR    R13,R3
         LM    14,12,12(13)
         SPACE 2
*
*        BUILD CHANNEL PGM TO READ CESD RECORDS
*
         SPACE 2
         LA    R0,IOBSEEK     ADDRESS OF SEEK FIELD
         LA    R1,CCWS
         LA    R2,ESDBUFF
         LA    R3,IOBSEEK
         XC    CCWS(LESDCCWS),CCWS
         ST    R0,CCWS
         ST    R1,CCWS+8
         ST    R2,CCWS+16
         ST    R3,CCWS+24
         OC    CCWS(LESDCCWS),ESDCCWS
         SPACE 2
         ST    R1,IOBCCW
         LA    R1,INDCB
         ST    R1,IOBDCB
         LA    R1,IOECB
         ST    R1,IOBECB
         MVI   IOBFLAG,X'42'
         SPACE
         LA    R2,ESDPTR
         SPACE
ESDEXCP  DS    0H
         MVI   IOECB,0
         EXCP  IOB            START I/O
         SPACE
         WAIT  ECB=IOECB
         MVI   DCBIFLGS-IHADCB+INDCB,0
         SPACE
         CLI   IOECB,X'42'    EXTENT VIOLATION?
         BNE   ESDEXCP1       NO
         BAL   R14,NEWEXTNT   YES, ADJUST FOR NEW EXTENT
ESDEXCP1 DS    0H
         SPACE
         CLI   IOECB,X'7F'    SUCESSFUL?
         BNE   ESDLAST        NO, MUST BE LAST ESD DATA
         SPACE
         CLI   ESDBUFF,X'40'  TEST SYM RECORD?
         BE    ESDEXCP        YES, SKIP RECORD
         SPACE
         CLI   ESDBUFF,X'20'  CESD RECORD?
         BNE   ESDLAST        NO
         SPACE
         LH    R6,ESDBUFF+4   RELATIVE # OF 1ST ESD ID
         LA    R3,ESDBUFF+8   START OF ESD DATA
         LH    R5,ESDBUFF+6   LENGTH OF DATA IN BUFFER
         AR    R5,R3
         LA    R4,16          LENGTH OF ONE ENTRY
         SR    R5,R4
         SPACE 2
ESDSCAN  DS    0H
         USING ESDNAME,R3
         IC    R0,ESDTYPE
         LA    R1,CODESEG     CHECK FOR SEGTAB/ENTAB
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         TM    FLAGSRD,FALLESD SEARCH REQUEST?
         BZ    ESDMATCH       YES, SCAN ESD ENTRIES
         CR    R0,R1          THIS SEGTAB/ENTAB ENTRY?
         BE    ESDUSE1        YES
         SPACE
         CLI   ESDTYPE,CODESD SD ENTRY?
         BE    ESDUSE         YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODELR LR ENTRY?
         BE    ESDUSE         YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODEPC PC ENTRY?
         BE    ESDUSEPC       YES, BRANCH
         SPACE 1
NEXTESD  DS    0H
         LA    R6,1(R6)
         BXLE  R3,R4,ESDSCAN
         SPACE
         B     ESDEXCP        GET NEXT ESD RECORD
         SPACE 2
ESDUSEPC MVC   ESDNAME,$PRIVATE  PRIVATE CODE
         B     ESDUSE
         SPACE 2
ESDUSE1  STC   R0,ESDTYPE     RESTORE SEGTAB/ENTAB TYPE
ESDUSE   BAL   R14,GETESD
         DROP  R3
         USING ESDENTRY,R2
         ST    R1,ESDLINK
         LR    R2,R1
         STH   R6,ESDID       SAVE ESD ID
         MVC   ESDNAME(LENESD1),0(R3) SAVE ESD ENTRY
         B     NEXTESD
         SPACE 1
ESDLAST  DS    0H
         OC    ESDPTR,ESDPTR               ANY ESD DATA AVAILABLE?
         MVC   MSGTEXT1,MSGNOESD
         L     R14,R14LINK
         BZ    ESDEXIT                     NO, BRANCH
         SPACE 2
         LA    R2,ESDPTR
         SPACE
ESDSORT  DS    0H
         L     R2,ESDLINK
         ICM   R1,15,ESDLINK  END OF ESD ENTRIES?
         BZ    ESDSORT1       YES, BRANCH
         SPACE
ESDSORT2 DS    0H
         CLC   ESDSEG#,ESDSEG#-ESDENTRY(R1) CHECK ENTRY SEGMENT #S
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE
         CLC   ESDADDR,ESDADDR-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE
         CLC   ESDTYPE,ESDTYPE-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE
         CLC   ESDNAME,ESDNAME-ESDENTRY(R1)
         BNH   ESDSORT3
         SPACE
ESDSWAP  DS    0H
         XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         XC    ESDNAME-ESDENTRY(LENESD2,R1),ESDNAME
         XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         SPACE
ESDSORT3 ICM   R1,15,ESDLINK-ESDENTRY(R1)
         BNZ   ESDSORT2
         B     ESDSORT
         SPACE 3
ESDSORT1 DS    0H
ESDOUT   DS    0H
         L     R14,R14LINK
         LA    R14,4(R14)     ADJUST EXIT ADDRESS
         NI    FLAGSRD,X'FF'-FALLESD
         SPACE 2
ESDEXIT  DS    0H
         LM    15,12,16(13) RESTORE REGISTERS
         BR    R14            EXIT
         DROP  R2
         SPACE 1
ESDMATCH DS    0H
         USING ESDNAME,R3
         CLI   ESDTYPE,CODESD VALID EXTERNAL SYMBOL?
         BE    ESDCHECK       YES
         CLI   ESDTYPE,CODELR ANOTHER VALID ENTRY
         BNE   NEXTESD        NO VALID, CONTINUE
         SPACE
ESDCHECK DS    0H
         CLC   ESDNAME,DIRNAME  REQUESTED NAME?
         BNE   NEXTESD        NO
         CLI   ESDSEG#,1      IS SYMBOL IN ROOT SEGMENT?
         BNE   NEXTESD        NO, CONTINUE
         SPACE
         L     R1,ESDADDR-1   GET SYMBOL OFFSET
         LA    R1,0(R1)
         ST    R1,24(13)      RETURN OFFSET IN REG 1
         B     ESDOUT
         DROP  R3
         EJECT
*
*
*        ESD TABLE SUBROUTINES
*
*
         SPACE 2
GETESD   LA    R0,LENESD
         GETMAIN R,LV=(0)
         XC    0(LENESD,R1),0(R1)
         BR    R14
         SPACE 5
FREEESD  DS    0H
         ST    R14,R14SAVE
         SPACE
         L     R14,ESDPTR
         SPACE
FREEESD1 DS    0H
         LTR   R1,R14
         BZ    FREEESD2
         L     R14,ESDLINK-ESDENTRY(R14)
         SPACE
         LA    R0,LENESD
         FREEMAIN R,LV=(0),A=(1)
         B     FREEESD1
         SPACE 2
FREEESD2 ST    R1,ESDPTR
         L     R14,R14SAVE
         BR    R14
         EJECT
*
*
*        IDR TABLE SUBROUTINES
*
*
         SPACE 2
GETIDR   DS    0H
         OI    FLAGSRD,FIDR   IDR DATA USED
         LA    R0,LENIDR
         GETMAIN R,LV=(0)
         XC    0(LENIDR,R1),0(R1)
         BR    R14
         SPACE 5
FREEIDR  DS    0H
         ST    R14,R14SAVE
         SPACE
         L     R14,IDRPTR
         SPACE
FREEIDR1 DS    0H
         LTR   R1,R14
         BZ    FREEIDR2
         L     R14,IDRLINK-IDRENTRY(R14)
         SPACE
         LA    R0,LENIDR
         FREEMAIN R,LV=(0),A=(1)
         B     FREEIDR1
         SPACE 2
FREEIDR2 ST    R1,IDRPTR
         L     R14,R14SAVE
         BR    R14
         EJECT
***********************************************************************
***      END SUBCOMMAND                                             ***
***********************************************************************
*
END      DS    0H             END SUBCOMMAND
         BAL   R2,CLOSEIT       CLOSE THE FILE
         BAL   R14,DEALLDCB     DEALLOCATE THE FILE, TOO
         B     EXIT0            QUIT, RC=0
         SPACE 2
CLOSEIT  DS    0H             ENTRY POINT FOR RESTART AFTER CLOSE
*                               (USED BY THE CHANGE SUBCOMMAND,
*                                  BEFORE EACH EDIT IS LINKED TO
         ICM   R1,15,BUFFADDR       AND THE CHKUSER FUNCTION)
         BZ    FREENONE
         XC    BUFFADDR,BUFFADDR
         LH    R0,BUFFSIZE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 2
FREENONE MVI   PARMLIST,0
         MVI   PARMLIST+4,X'80'
         CLOSE (INDCB,,STOWDCB),MF=(E,PARMLIST)
         SPACE 1
         BR    R2
         SPACE 3
***********************************************************************
***      CHANGE SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE
CHANGE   DS    0H             SWITCH TO A DIFFERENT DATA SET
         BAL   R2,CLOSEIT     CLOSE THE DATA SET
         BAL   R14,DEALLDCB   FREE THE DATA SET
         B     RESTART        TREAT AS PSUEDO END OPTION
         EJECT
*
*   IF NO OPERATOR AUTHORITY, SET DISP=OLD AFTER AN
*     ALIAS, ATTR, DELETE, RENAME OR RESTORE SUBCOMMAND IS REQUESTED
*
CHKUSER  CLI   DSPALLOC,DA08OLD  DISP=OLD ALREADY?
         AIF   ('&SYSPARM' NE 'SHR').NOTSHR
         BR    R14               ALWAYS USE SHR
.NOTSHR  ANOP
         BER   R14               YES, RETURN
         AIF   ('&SYSPARM' EQ '').DEFAULT
         AIF   ('&SYSPARM' NE 'OPER').NOTOPER
.DEFAULT ANOP
         L     R2,ADDRPSCB
         USING PSCB,R2
         TM    PSCBATR1,PSCBCTRL   OPERATOR AUTHORITY?
         BOR   R14                 YES, ALLOW SHARED ACCESS
         DROP  R2
.NOTOPER ANOP
         SPACE
         BAL   R2,CLOSEIT        CLOSE THE DATA SET
         BAL   R14,DEALLDCB      DEALLOCATE THE DATA SET
         MVI   DSPALLOC,DA08OLD  SPECIFY DISP=OLD
         B     RESTART2          REALLOCATE AND TRY AGAIN
         EJECT
*
*
*        SUBCOMMAND MESSAGE SUBROUTINES
*
*
         SPACE 2
MEMEXIST DS    0H
         LA    R1,MSGEXIST     MSG - MEMBER EXISTS
         B     MSGNEW
         SPACE 3
NOMEMBER DS    0H
         LA    R1,MSGNONE      MSG - MEMBER NOT FOUND
         B     MSGNEW
         SPACE 3
IOERROR  DS    0H
         LA    R1,MSGIOERR     MSG - I/O ERROR IN DIRECTORY
         B     MSGNEW
         SPACE 3
FULLDIR  DS    0H
         LA    R1,MSGNODIR     MSG - DIRECTORY IS FULL
         SPACE 3
MSGNEW   DS    0H
         MESSAGE (R1)          OUTPUT THE MESSAGE POINTED TO
         B     NEWCMD            AND GET A NEW SUBCOMMAND
         EJECT
*
*
*        INPUT BUFFER RELEASE SUBROUTINE
*
*
         SPACE 2
FREEBUFF DS    0H
         TM    FLAGS,FCMD     BUFFER IN USE?
         BOR   R14            YES, LEAVE IT
         STM   R14,R1,12(R13)
         ICM   R1,15,ADDRCBUF ANY BUFFER?
         BZ    NOBUFF         NO, BRANCH (DO NOT FREEMAIN)
         SPACE
         LH    R0,0(R1)
         LA    R15,1          SUBPOOL IS 1
         SLL   R15,24
         OR    R0,R15
         SPACE
         FREEMAIN R,LV=(0),A=(1)
         SPACE
NOBUFF   DS    0H
         LM    R14,R1,12(R13)
         XC    ADDRCBUF,ADDRCBUF
         BR    R14
         SPACE 3
*
*        DATA SET END-OF-EXTENT PSUEDO APPENDAGE
*
*
NEWEXTNT DS    0H
         SR    R0,R0
         SR    R15,R15
         L     R1,DCBDEBAD-IHADCB+INDCB
         IC    R15,MBBCCHHR   CURRENT EXTENT #
         IC    R0,DEBXTNT#(R1) # EXTENTS IN DATA SET
         LA    R15,1(R15)     ONE MORE DONE
         CR    R15,R0         END OF DATA SET?
         BHR   R14            YES, EXIT
         STC   R15,MBBCCHHR   NO, SAVE NEW EXTENT COUNT
         MVI   IOBSEEK+4,X'01'
         SLL   R15,4          COMPUTE EXTENT OFFSET
         AR    R1,R15         IN DEB
         MVC   IOBSEEK(4),DEBEXTNT+6(R1)
         MVI   IOECB,X'7F'
         BR    R14
         EJECT
*
*
*        DIRECTORY READ SUBROUTINE
*
*
         SPACE 2
READDIR  DS    0H
         ST    R14,R14SAVE
         SPACE
         TM    FLAGSRD,F1STREAD IS THIS FIRST TIME?
         BZ    DEBLOCK        NO
         SPACE
         XI    FLAGSRD,F1STREAD
         NI    FLAGSCN,255-FDIREND           END OF DIRECTORY TO COME
         SR    R0,R0
         STH   R0,TOTUSED
         STH   R0,TOTBLOCK
         STH   R0,TOTMEMR              TOTAL REALS
         STH   R0,TOTMEMA              TOTAL ALIASES
         SPACE
*
*        INITIALIZE DIRECTORY READ SUBROUTINE
*
         SPACE
         XC    IOB(LIOB),IOB
         LA    R0,X'100'      TTR OF FIRST BLOCK
         L     R1,DCBDEBAD-IHADCB+INDCB
         LA    R2,MBBCCHHR
         SPACE
         L     R15,ADRPCNVT       TTR TO CCHHR CONVERT ROUTINE
         STM   14,12,12(R13) SAVE REGISTERS
         LR    R3,R13
         BALR  R14,R15        CONVERT TTR
         LR    R13,R3
         LM    14,12,12(R13) RESTORE REGISTERS
         SPACE 2
         LA    R0,INDCB      DCB ADDRESS
         ST    R0,IOBDCB      TO DCB
         MVI   IOBFLAG,X'42'
         LA    R0,IOECB
         ST    R0,IOBECB
         XC    CCWS(LDIRCCWS),CCWS
         SPACE
         LA    R14,IOBSEEK    CREATE CHANNEL PGM TO READ DIR
         LA    R15,CCWS
         LA    R0,IOBSEEK
         LA    R1,DIRBLOCK
         SPACE
         ST    R14,CCWS       ADDR FOR SEARCH CCW
         ST    R15,CCWS+8     ADDR FOR TIC CCW
         ST    R15,IOBCCW     POINT IOB TO CCW CHAIN
         ST    R1,CCWS+16     ADDR FOR READ DATA
         ST    R0,CCWS+24     ADDR FOR READ COUNT CCW
         SPACE
         OC    CCWS(LDIRCCWS),DIRCCWS MERGE IN REAL CCWS
         SPACE 3
READDIR1 DS    0H
         MVI   IOECB,0
         EXCP  IOB
         SPACE
         WAIT  ECB=IOECB
         MVI   DCBIFLGS-IHADCB+INDCB,0 CLEAR STATUS FLAGS
         SPACE
         CLI   IOECB,X'42'    EXTENT VIOLATION?
         BNE   *+8            NO
         BAL   R14,NEWEXTNT   YES, ADJUST FOR NEW EXTENT
         SPACE
         CLI   IOECB,X'7F'    SUCESSFUL COMPLETION?
         BE    READDIR2       YES, DEBLOCK DIRECTORY
         SR    R15,R15        END OF DIRECTORY EXIT OFFSET
         TM    IOBCSW+3,X'01' END OF FILE?
         BO    READDIR4       YES, EXIT AT END OF DIRECTORY
         B     IOERROR        NO, SIGNAL I/O ERROR IN DIRECTORY
         SPACE 3
READDIR2 DS    0H
         LH    R14,TOTBLOCK
         LA    R14,1(R14)
         STH   R14,TOTBLOCK
         TM    FLAGSCN,FDIRSCAN+FDIREND  END OF DIRECTORY?
         BO    READDIR1
         SPACE
         LA    R15,DIRBLOCK
         LA    R0,2
         LH    R1,DIRBLOCK
         LA    R1,DIRBLOCK-1(R1)
         STM   R15,R1,DIRPTRS
         SPACE
         LH    R14,TOTUSED
         LA    R14,1(R14)
         STH   R14,TOTUSED
         SPACE 2
DEBLOCK  DS    0H
         LM    R15,R1,DIRPTRS
         BXH   R15,R0,READDIR1
         STM   R15,R1,DIRPTRS
         SPACE
         CLC   FF(8),0(R15)   LAST MEMBER?
         BE    READDIR3       YES
         SPACE
         MVC   MEMNAME(74),0(R15)
         SPACE
         NI    11(R15),X'1F'
         IC    R0,11(R15)
         AR    R0,R0
         LA    R14,12
         AR    R0,R14
         STM   R15,R1,DIRPTRS
         SPACE 2
         TM    DIRFLAG,X'80'     ALIAS?
         LA    R14,TOTMEMR       ASSUME NOT
         BZ    *+8
         LA    R14,TOTMEMA       IS AN ALIAS
         SPACE
         LH    R15,0(R14)
         LA    R15,1(R15)
         STH   R15,0(R14)
         SPACE
         TM    FLAGSCN,FDIRSCAN  SCAN ENTIRE DIRECTORY?
         BO    DEBLOCK           YES, CONTINUE THIS BLOCK
         SPACE 2
         LA    R15,8          EXIT OFFSET
         B     READDIR4
         SPACE 2
READDIR3 DS    0H
         OI    FLAGSCN,FDIREND   END OF DIRECTORY
         TM    FLAGSCN,FDIRSCAN  STATISTICS SCAN?
         BO    READDIR1          YES, CONTINUE IT
         SPACE
         LA    R15,4          LAST MEMBER EXIT OFFSET
         SPACE 2
READDIR4 DS    0H
         L     R14,R14SAVE
         NI    FLAGSCN,255-FDIRSCAN-FDIREND
         B     0(R14,R15)
         EJECT
*
*
*        PARSE INTERFACE SUBROUTINE
*
*
         SPACE 3
GOPARSE  DS    0H
         ST    R1,ADDRPCL
         ST    R14,R14PARSE
         SPACE 3
*
*        BUILD PARSE PARAMETER LIST AND INVOKE
*        IKJPARS TO ANALYZE COMMAND OPERANDS
*
         SPACE 3
         LA    R1,PARSELST    AREA FOR PARSE PARAMETERS
         USING PPL,R1         BASE FOR PARSE PARAMETER LIST
         SPACE 2
         MVC   PPLUPT,ADDRUPT PASS UPT ADDRESS
         MVC   PPLECT,ADDRECT AND ECT ADDRESS
         MVC   PPLCBUF,ADDRCBUF AND COMMAND BUFFER ADDR
         SPACE
         ST    WORKREG,PPLUWA ALSO WORK AREA ADDR FOR VALIDITY EXITS
         SPACE
         LA    R0,ATTNECB     ECB FOR ATTN INTERRUPTS
         MVI   ATTNECB,0      CLEAR ECB
         ST    R0,PPLECB      PASSE TO PARSE
         SPACE
         LA    R0,ADDRANSR    PASS ADDR OF WORD WHERE PARSE
         ST    R0,PPLANS      RETURNS PDL ADDRESS
         SPACE
         MVC   PPLPCL,ADDRPCL ADDRESS OF PARSE CONTROL LIST
         SPACE 3
         L     R15,ADDRPARS   INVOKE
         BALR  R14,R15              PARSE
         STC   R15,R14PARSE
         L     R14,ADDRANSR
         DROP  R1
         MVC   ANSWERS(24),0(R14) SAVE FIRST 24 BYTES OF PARSE PDL
         SPACE 2
         LTR   R15,R15            GOOD PARSE?
         BNZ   FREESPDL           NO, BRANCH
         SPACE 3
         USING PDLLIST,R14
         LA    R2,LISTFMT+1       POINT TO THE LIST FORMAT
         DROP  R14
         CLC   SUBNAME,LISTLIT    LIST SUBCOMMAND?
         BE    FINDPDLR           YES, INTERPRET THE FORMAT FIELD
         SPACE
         USING PDLFIND,R14
         LA    R2,FINDFMT+1       POINT TO THE FIND FORMAT
         CLC   SUBNAME,FINDLIT    FIND COMMAND?
         BNE   MAINPDLR           NO, BRANCH
         TM    FINDSTRG+6,X'80'   STRING CODED?
         BNO   FREESPDL           NO, ASSUME OK
         LH    R0,FINDSTRG+4      LENGTH OF STRING
         LTR   R0,R0              NULL STRING CODED?
         BZ    FINDPDLR           YES, ASSUME FORMAT OPTION ONLY
         STH   R0,FINDLTH         SAVE ACTUAL LENGTH
         SR    R15,R15
         IC    R15,FINDSTR        SAVE FIRST CHARACTER OF OLD STRING
         L     R1,FINDSTRG        NEW STRING ADDRESS
         MVC   FINDSTR(60),0(R1)  SAVE THE NEW STRING
         LA    R1,FINDTBL(R15)    LAST USED TRT ADDRESS
         MVI   0(R1),X'00'        RESET TO ZERO
         IC    R15,FINDSTR        FIRST CHARACTER OF NEW STRING
         LA    R1,FINDTBL(R15)    ADDRESS OF NEW TRT BYTE
         MVI   0(R1),X'77'        SET TRT BYTE TO NON-ZERO
         LA    R1,60
         CR    R0,R1              LENGTH(STRING)>=60?
         BL    FINDPDLR           NO, BRANCH
         STH   R1,FINDLTH         YES, USE 60 AS A MAXIMUM LENGTH
         DROP  R14
FINDPDLR CLI   0(R2),0                  WAS A FORMAT SPECIFIED?
         BE    FREESPDL                 NO, QUIT
         NI    FLAGSOP,255-NONUM-SNUM   TURN OFF NONUM AND SNUM
         CLI   0(R2),1                  NUM CODED?
         BE    FREESPDL                 YES, SET CORRECTLY
         OI    FLAGSOP,NONUM            ASSUME NONUM CODED
         CLI   0(R2),2                  RIGHT?
         BE    FREESPDL                 YES, BRANCH
         XI    FLAGSOP,NONUM+SNUM       NO, TURN OFF NONUM, ON SNUM
         B     FREESPDL
         SPACE 2
MAINPDLR CLC   ADDRPCL+1(3),MAINPCL+1   PARSE FOR DSNAME AND VOLUME?
         BNE   FREESPDL                 NO, BRANCH
         TM    FLAGSRD,FQUOTE           QUOTED DATASET NAME?
         BO    FREESPDL                 YES, BRANCH
         CLI   VOLUME,X'40'             ANY VOLUME NAME SPECIFIED?
         BNE   FREESPDL                 YES, ASSUME NO CATALOG USE
*  NEED TO FULLY QUALIFY THE DATA SET NAME
         LA    R2,DFPBLIST              POINT THE DEFAULT DATA BLOCK
         USING DFPB,R2
         MVI   DFPBCNTL,0               DEFAULT ACTION ASSUMED
         OI    DFPBCNTL,DFPBRET         NO, ADD LOW-LEVEL QUALIFIER
         LA    R1,DSNLEN
         ST    R1,DFPBDSN               POINT TO THE DSNAME
         LA    R1,PASSWORD
         ST    R1,DFPBPSWD              POINT TO THE PASSWORD
         MVC   DOUBLE(8),BLANKS
         LA    R1,DOUBLE                NO DEFAULT QUALIFIER
         ST    R1,DFPBQUAL              NO DEFAULT QUALIFIER
         MVI   DFPBCODE,DFPB04          ENTRY CODE X'04'
         MVC   DFPBPSCB+1(3),ADDRPSCB+1 ADDRESS OF THE PSCB
         LA    R1,DFPLLIST    ADDRESS OF PARM LIST
         L     R15,ADDRDEFT   DEFAULT ROUTINE ADDRESS
         BALR  R14,R15        CALL IT
         SPACE 3
FREESPDL BAL   R14,FREEPDL
         SR    R15,R15
         IC    R15,R14PARSE
         SPACE 2
         LA    R14,MAXPARSE   RETURN CODE LIMIT
         SPACE
         CR    R15,R14        VERIFY RETURN CODE WITHIN LIMITS
         BH    PARSEBAD       NO, ERROR
         SPACE
         B     *+4(R15)       PROCESS RETURN CODE
PARSERET B     PARSEOK         0- SUCESSFUL
         B     EXIT8           4- PARSE UNABLE TO PROMPT
         B     PARSE1          8- USER ENTERED ATTENTION
         B     PARSEBAD       12- INVALID PARAMETERS
         B     PARSEBAD       16- PARSE INTERNAL FAILURE
         B     EXIT8          20 - VALIDITY CHECK ERROR
MAXPARSE EQU   *-PARSERET
         SPACE 3
PARSEBAD LA    R1,GFPARMS
         ST    R1,GFPARMP         SAVE ADDRESS OF PARMLIST
         ST    R15,GFRCODE        SAVE THE RETURN CODE
         LA    R1,GFPARSE
         STH   R1,GFCALLID        SAVE CALLER-ID FOR PARSE
         L     R1,CPPLADDR
         ST    R1,GFCPPLP         STORE POINTER TO CPPL
         LA    R1,IOECB
         ST    R1,GFECBP          STORE POINTER TO ECB
         XC    IOECB(4),IOECB     ZERO THE ECB
         LINK  EP=IKJEFF19,MF=(E,GFPARMP)
         CLI   R14PARSE,X'4'      USER ENTER ATTENTION?
         BE    PARSE1             YES, BRANCH (IGNORE THE COMMAND)
         LTR   R15,R15            ERROR MESSAGE OUTPUT?
         BZ    EXIT12             YES, BRANCH
         LH    R14,MSGGENF        GENERAL FAILURE ROUTINE FAILURE
         MVC   MSGTEXT1(0),MSGGENF
         EX    R14,*-6
         CVD   R15,DOUBLE
         LA    R14,MSGTEXT1(R14)
         MVC   0(4,R14),=X'40202120'
         ED    0(4,R14),DOUBLE+6
         MVI   0(R14),C'='
         LA    R0,MSGTEXT1-4
         SR    R14,R0
         SLL   R14,16
         ST    R14,MSGTEXT1
         LA    R1,MSGTEXT1
         BAL   R14,MESSAGE0
         B     EXIT12
         SPACE 2
PARSE1   SR    R15,R15        RETURN AT OFFSET 0
         B     PARSEOUT
         SPACE 2
PARSEOK  LA    R15,4
         SPACE 2
PARSEOUT L     R14,R14PARSE
         B     0(R15,R14)     EXIT FROM PARSE
         EJECT
PUTERROR DS    0H
GETERROR DS    0H
EXIT12   LA    R15,12         ERROR CODE 12
         B     RETURN
         SPACE
EXIT8    LA    R15,8          ERROR CODE 8
         B     RETURN
         SPACE
EXIT4    LA    R15,4
         B     RETURN
         SPACE
EXIT0    SR    R15,R15
         SPACE 2
RETURN   LTR   R2,R15         NORMAL EXIT?
         BZ    RETURN1        YES
         SPACE 2
         LA    R1,PARMLIST    AREA FOR STACK PARM LIST
         USING IOPL,R1
         SPACE
         MVC   IOPLUPT,ADDRUPT
         MVC   IOPLECT,ADDRECT
         LA    R0,ATTNECB
         MVI   ATTNECB,0
         ST    R0,IOPLECB
         DROP  R1
         SPACE 2
         L     R15,ADDRSTCK       STACK ROUTINE
         STACK PARM=PARMLIST+16,DELETE=ALL,MF=(E,(1)),ENTRY=(15)
         SPACE 3
         TCLEARQ INPUT        CLEAR INPUT BUFFERS
         SPACE 3
RETURN1  DS    0H
         BAL   R14,FREEPDL    FREE THE PARSE STROAGE
         STAE  0,OV           CANCEL STAE EXIT
         SPACE 3
         L     R13,ADDRSAVE   RESTORE FIRST SAVE AREA
         LR    R15,R2         RESTORE RETURN CODE
         SPACE
         L     R0,WORKSIZE
         SPACE
         EXIT  LV=(0)
         EJECT
*
*        STAE EXIT
*
         SPACE 3
STAEEXIT DS    0H
         USING *,R15          TEMPORARY BASE
         SPACE
         CH    R0,STAECODE    IS WORK AREA PROVIDED?
         BE    STAE1          NO
         SPACE
         L     WORKREG,0(R1)  YES, GET OUR WORK AREA ADDR
         B     STAE2
         SPACE 2
STAE1    LR    WORKREG,R2     OUR WORK AREA ADDR
         SPACE 2
STAE2    DS    0H
         STM   0,15,STAEREGS
         SPACE 2
         LM    BASEREG1,BASEREG4,BASES
         DROP  R15
         SPACE
         LA    R13,STAESAVE
         SPACE 2
         LM    0,15,STAEREGS
         SR    R15,R15        NO RETRY EXIT
         BR    R14
         SPACE
STAECODE DC    H'20'
         EJECT
*
*
*        STOW DCB OPEN SUBROUTINE
*
*
         SPACE 3
OPENSTOW DS    0H
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BOR   R14                               YES, EXIT
         MVI   OPENLIST,X'80'
         ST    R14,0(,R13)    SAVE RETURN ADDRESS
         SPACE 2
         OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)
         L     R14,0(,R13)
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BO    4(,R14)                           YES, BRANCH
         MESSAGE MSGNOPEN                        STOW DID NOT OPEN
         L     R14,0(,R13)
         BR    R14            RETURN
         EJECT
*
*
*        ENTRY-POINT ADDRESS VALIDITY CHECK SUBROUTINE
*
*
         SPACE 2
CHKNTRY  ENTER VALCHECK
         L     R15,0(R7)
         LH    R14,4(R7)
         CH    R14,=H'6'
         BNE   NTRYER
         MVC   DBLWD(6),0(R15)
         LA    R7,6
         LA    R6,DBLWD
CONTNI   EQU   *
         NI    0(R6),X'3F'
         LA    R6,1(R6)
         BCT   R7,CONTNI
         TR    DBLWD(6),TRTAB
         TRT   DBLWD(6),ZEROES
         BZ    NTRYOK
NTRYER   EQU   *
         MESSAGE NTRYINV
         LA    R15,8
         B     NTRYEX
TRTAB    DS    0CL64
         DC    X'FF0A0B0C0D0E0F'
         DC    41XL1'FF'
         DC    X'00010203040506070809'
         DC    6XL1'FF'
         DC    0F'0'
ZEROES   DC    16XL1'00'
         DC    240XL1'FF'
         LTORG
NTRYINV  MSG   ' INVALID ENTRY-POINT ADDRESS'
NTRYOK   EQU   *
         PACK  ENTRYAD(4),DBLWD(7)
         SR    R15,R15
NTRYEX   EQU   *
         EXIT
         EJECT
*
*
*        MEMBER NAME VALIDITY CHECK SUBROUTINE
*
*
         SPACE 3
VERMEMBR ENTER VALCHECK
         TM    6(R7),X'80'        WAS MEMBER NAME SPECIFIED?
         BZ    SKIPTHIS           NO
         SPACE 1
         L     R15,0(R7)          ADDR OF MEMBER NAME
         LH    R14,4(R7)          LENGTH OF NAME
         BCTR  R14,0              EXECUTE LENGTH
         SPACE 1
         OI    FLAGS,FMEMBER2
         LM    R6,R7,FF           ASSUME DISPLAY (HIGH-VALUES PADDING)
         CLC   SUBNAME,DISPLIT    CORRECT?
         BE    *+8                YES, BRANCH
         LM    R6,R7,BLANKS       NO, USE BLANK PADDING
         TM    FLAGS,FMEMBER1     FIRST MEMBER PROCESSED?
         LA    R1,MEMBER2
         LA    R2,LMEMBER2
         BO    MEM2ND             YES, DO THE SECOND MEMBER NAME
         XI    FLAGS,FMEMBER1+FMEMBER2
         LM    R6,R7,BLANKS       USE BLANK PADDING FOR THE FIRST NAME
         LA    R1,MEMBER1
         LA    R2,LMEMBER1
MEM2ND   DS    0H
         SPACE
         MVI   0(R1),C' '
         MVC   1(7,R1),0(R1)
         STH   R14,0(,R2)         STASH LENGTH
         SPACE
         CLC   =C'X''',0(R15)     BEGINS WITH X'?
         BE    VERHEX             YES, MUST BE A HEX MEMBER NAME
         SPACE
         TM    FLAGS,FMEMBER2     SECOND MEMBER?
         BNO   MEMLEN             NO, BRANCH
         CLC   SUBNAME,RESETLIT   RESTORE SUBCOMMAND?
         BNE   MEMLEN             NO, BRANCH
         LA    R5,0(R14,R15)      YES, POINT TO LAST CHARACTER
         BCTR  R15,0              BACK UP R15
         B     VERNULL            CHECK FOR A NULL
         SPACE
MEMLEN   CLI   1(R2),7            COMPARE LENGTH > 7?
         BNH   VERCHAR            NO, BRANCH
         B     VERBAD             YES, INVALID MEMBER NAME
         SPACE 2
VERHEX   LA    R5,0(R14,R15)      LAST CHARACTER
         CLI   0(R5),C''''        A FINAL QUOTE?
         BNE   VERBAD             NO, INVALID HEXADECIMAL
         BCTR  R5,0               POINT TO PRECEDING CHARACTER
         LA    R15,1(,R15)        POINT TO FIRST QUOTE
         SPACE
VERNULL  CR    R5,R15             NULL HEX STRING?
         BNH   VERBAD             YES, INVALID SYNTAX
         SR    R4,R4              COUNT OF NIBBLES
         L     R14,=F'-1'         BXH INCREMENT
         SPACE
VERHLOOP IC    R3,0(,R5)          NEXT CHARACTER (REVERSE SCAN)
         CLI   0(R5),C'0'
         BL    *+12               0-9?
         CLI   0(R5),C'9'
         BNH   VER0TOF            YES, BRANCH
         LA    R3,9(,R3)          NO, ADJUST LOW NIBBLE 1-5 TO A-F
         CLI   0(R5),C'A'
         BL    VERBAD             A-F?
         CLI   0(R5),C'F'
         BH    VERBAD             NO, BRANCH
         SPACE
VER0TOF  SLL   R3,28              GET NIBBLE IN TOP NIBBLE
         SRDL  R6,4               CREATE ROOM IN R6,R7
         OR    R6,R3              ADD TO R6,R7
         LA    R4,1(,R4)          ANOTHER NIBBLE
         BXH   R5,R14,VERHLOOP    DO ALL NIBBLES
         SPACE
         LA    R14,1(,R4)         NIBBLES+1
         SRL   R14,1              (NIBBLES+1)/2   -- TRUNCATED
         BCTR  R14,0              MACHINE COMPARE LENGTH
         STH   R14,0(,R2)         MACHINE LENGTH AFTER CONVERSION
         LA    R14,2(R14,R14)     (COMPARE LENGTH+1)*2
         CR    R14,R4             EVEN NUMBER OF NIBBLES?
         BE    *+8                YES, BRANCH
         SRDL  R6,4               NO, ADJUST BY ONE MORE NIBBLE
         SPACE
         STM   R6,R7,DOUBLE       SAVE CREATED MEMBER NAME
         LA    R14,7              FULL LENGTH MEMBER NAME
         LA    R15,DOUBLE         START OF MOVE ADDRESS
         SPACE 2
VERCHAR  MVC   0(0,R1),0(R15)
         EX    R14,*-6
         TM    ADDRPCL,X'20'      DEFAULT MEMBER NAME PROCESSING?
         BNO   SKIPTHIS           NO, BRANCH
         MVC   DEFMEMB,MEMBER1    SAVE THE NEW DEFAULT MEMBER NAME
         B     SKIPTHIS
         SPACE
VERBAD   LA    R15,4              INVALID MEMBER NAME -- TRY AGAIN
         TM    FLAGS,FMEMBER2     PROCESSING MEMBER2?
         BO    *+8                YES, BRANCH
         NI    FLAGS,255-FMEMBER1 NO, SAY NO MEMBER1 YET
         NI    FLAGS,255-FMEMBER2 ALWAYS SAY NOT MEMBER2 NOW
         B     VERMEMX            TERMINATE
         SPACE
SKIPTHIS DS    0H
         SR    R15,R15
VERMEMX  EXIT
         EJECT
*
*
*        NEW ATTRIBUTES VALIDITY CHECK SUBROUTINE
*
*
         SPACE 2
VERATTR  ENTER VALCHECK
         L     R15,0(R7)
         LH    R14,4(R7)
         SPACE 2
         LA    R3,ATTRNO
         LA    R1,ATTRYES
         SPACE
         LA    R2,L'NO
         CR    R14,R2         CAN 'NO' OPTION BE PREFIXED?
         BL    VERATTR1       NO
         SPACE
         CLC   NO,0(R15)      'NO' OPTION PREFIXED?
         BNE   VERATTR1       NO
         SPACE
         AR    R15,R2         ADJUST ATTRIBUTE ADDRESS
         SR    R14,R2         DECREMENT LENGTH
         BZ    VERATTR0       LENGTH ERROR
         LA    R1,ATTRNO
         LA    R3,ATTRYES
         SPACE 2
VERATTR1 DS    0H
         BCTR  R14,0          DECREMENT LENGTH FOR EXECUTE
         LA    R0,LATTR       MAXIMUM ATTRIBUTE LENGTH
         CR    R14,R0
         BNL   VERATTR0       INVALID LENGTH
         SPACE 2
         SR    R0,R0          NO FIRST ATTRIBUTE
         SPACE
         LA    R2,ATTRTBL     TABLE OF CHANGEABLE ATTRIBUTES
         SPACE
LOOPATTR DS    0H
         CLC   0(0,R15),0(R2)
         EX    R14,*-6
         BE    VERATTR2
VERATTR5 DS    0H
         LA    R2,LATTRTBL(R2)
         CLI   0(R2),X'FF'
         BNE   LOOPATTR
         LTR   R2,R0          PREVIOUS MATCH?
         BNZ   VERATTR3       YES
         B     VERATTR0       INVALID ATTRIBUTE
         SPACE
VERATTR2 DS    0H
         LTR   R0,R0          FIRST MATCH
         BNZ   VERATTRX       NO
         SPACE
         LR    R0,R2          SAVE ADDRESS
         B     VERATTR5       CONTINUE SCAN
         SPACE 2
VERATTR3 TM    LATTR(R2),X'40' AUTH REQUEST?
         BZ    VERATTRY        NO, BRANCH
         SPACE
         LA    R0,ATTRNO       TURN ON OR OFF?
         CR    R0,R1
         BE    VERATOFF
         SPACE
         OI    FLAGSCN,FAPFON  TURN ON
         B     EXITATT0
         SPACE
VERATOFF OI    FLAGSCN,FAPFOFF TURN OFF
         B     EXITATT0
         SPACE 2
VERATTRY TM    LATTR(R2),X'80'  REVERSE MATCH?
         BZ    VERATTR6       NO
         LR    R1,R3          ALTERNATE BIT MASK
VERATTR6 DS    0H
         OC    0(L'ATTRYES,R1),LATTR+1(R2) SET ATTRIBUTE STATUS FLAGS
         SPACE 2
         OI    FLAGS,FATTR    INDICATE CHANGE ATTRIBUTES
         SPACE
EXITATT0 SR    R15,R15
         SPACE
EXITATTR DS    0H
         EXIT
         SPACE 3
VERATTR0 DS    0H
         MVC   MSGTEXT1,MSGBADAT
BADATTR  DS    0H
         LH    R1,MSGTEXT1
         LA    R0,1(R1,R14)
         STH   R0,MSGTEXT1
         LA    R1,MSGTEXT1(R1)
         MVC   0(0,R1),0(R15)
         EX    R14,*-6
         MESSAGE MSGTEXT1
         SPACE 2
         LA    R15,8
         B     EXITATTR
         SPACE 3
VERATTRX DS    0H
         MVC   MSGTEXT1,MSGDUPAT
         B     BADATTR
         EJECT
*
*
*        INPUT DATA SET VALIDITY CHECK EXIT
*
*
         SPACE 3
VERDSN   ENTER VALCHECK
         CLEAR DSNAME
         SPACE 2
         CLEAR PASSWORD
         SPACE 2
         CLEAR VOLUME
         SPACE 2
         L     R15,0(R7)      ADDR OF DSNAME
         LH    R14,4(R7)      LENGTH OF DSNAME
         STH   R14,DSNLEN
         BCTR  R14,0
         MVC   DSNAME(0),0(R15)
         EX    R14,*-6            MOVE DSNAME TO AREA
         L     R1,ADDRPSCB        ADDRESS OF THE PSCB
         SR    R14,R14
         IC    R14,PSCBUSRL-PSCB(R1)   LENGTH OF USERID
         MVC   DSNAMEH(4),DSNAME  SAVE FIRST 4 BYTES OF DSNAME
         BCTR  R14,0                   MACHINE LENGTH
         CLC   DSNAME(*-*),PSCBUSER-PSCB(R1) START WITH USERID?
         EX    R14,*-6                       VARIABLE LENGTH
         LA    R1,2(R14,R15)                 POINT TO DATA AFTER PERIOD
         BNE   *+10                          NO, BRANCH
         MVC   DSNAMEH(4),0(R1)   SAVE FIRST 4 BYTES OF DSNAME
         SPACE 2
         TM    22(R7),X'80'   PASSWORD SUPPLIED?
         BZ    NOPASS         NO
         SPACE
         L     R15,16(R7)
         LH    R14,20(R7)
         BCTR  R14,0
         MVC   PASSWORD(0),0(R15)
         EX    R14,*-6        MOVE PASSWORD TO AREA
NOPASS   DS    0H
         SPACE 3
         OI    FLAGSRD,FQUOTE ASSUME DSNAME QUOTED
         TM    6(R7),X'40'    IS DSNAME IN QUOTES?
         BO    NOUSERID       YES
         XI    FLAGSRD,FQUOTE NO, MUST QUALIFY NAME
NOUSERID DS    0H
         SPACE 3
         SR    R15,R15
         SPACE
         EXIT
         EJECT
*
*        VOLUME SERIAL VALIDITY CHECK
*
         SPACE 2
VERVOL   ENTER VALCHECK
         LH    R15,4(R7)
         L     R1,0(R7)
         BCTR  R15,0
         SPACE
         MVC   VOLUME(0),0(R1)
         EX    R15,*-6
         SPACE 2
         SR    R15,R15
         EXIT
         EJECT
*
*        FREE DATA SET NO LONGER REQUIRED
*
         SPACE 2
DEALLDCB DS    0H              ***CONDITIONAL FILE DEALLOCATION
         TM    FLAGSOP,FPERMDS    DATA SET PERMANENTLY ALLOCATED?
         BOR   R14                YES, SKIP EXPLICIT DEALLOCATION
         SPACE 2
DEALLOCZ ST    R14,R14LINK     ***ALWAYS DEALLOCATE FILE (EXCEPT HELP)
         LA    R1,DAIRBLK
         XC    DAIRBLK(LDAIRBLK),DAIRBLK
         USING DAPB18,R1
         MVC   DA18DDN,DDNAME          DEALLOCATE THE DATA SET
         CLC   DDNAME(8),=C'SYSHELP '  SYSHELP DDNAME?
         BE    *+8                     YES, BRANCH
         OI    DA18CTL,DA18PERM        DEALLOCATE "PERM" ALLOCATED TOO
         MVI   DA18CD+1,X'18'
         SPACE
         BAL   R14,CALLDAIR
         DROP  R1
         SPACE 2
         L     R14,R14LINK
         BR    R14
         EJECT
*
*        PARSE CLEANUP ROUTINE
*
         SPACE 3
FREEPDL  DS    0H
         SPACE
         ST    R14,R14SAVE
         SPACE
         IKJRLSA ADDRANSR     RELEASE THE STORAGE
         SPACE
         L     R14,R14SAVE
         XC    ADDRANSR,ADDRANSR
         BR    R14
         EJECT
*
*        DATA SET ALLOCATION SUBROUTINE
*
         SPACE 2
ALLOCATE DS    0H
         ST    R14,R14ALLOC   SAVE LINKAGE REGISTER
         SPACE 2
RETRY    NI    FLAGSOP,255-FPERMDS  CLEAR DATA SET PERMANENT STATUS
         SPACE 2
         LA    R1,DAIRBLK           POINT TO THE DAIR BLOCK
         XC    DAIRBLK(LDAIRBLK),DAIRBLK
         USING DAPB08,R1
         SPACE
         MVC   DA08PSWD,PASSWORD
         SPACE
         MVI   DA08CD+1,X'08' DATA SET ALLOCATION CODE
         SPACE
         LA    R0,DSNLEN      ADDR OF DSNAME FIELD
         ST    R0,DA08PDSN
         SPACE
         CLEAR DA08DDN
         SPACE
         CLEAR DA08UNIT       NO UNIT REQUESTED
         SPACE
         MVC   DA08SER,VOLUME VOLUME SPECIFIED
         SPACE
         CLEAR DA08MNM        NO MEMBER NAME
         SPACE
*                                 DISP=(SHR,KEEP,KEEP)
         MVC   DA08DSP1,DSPALLOC    OR DISP=(OLD,KEEP,KEEP)
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         OI    DA08CTL,DA08PERM   DATA SET MUST BE SPECIFICALLY UNALLOC
         SPACE
         BAL   R14,CALLDAIR
         LA    R1,DAIRBLK
         LTR   R15,R15        SUCCESSFUL?
         BNZ   DAIRFAIL       NO, ERROR
         LA    R15,4          YES, SET GOOD RETURN DISPLACEMENT
         SPACE 2
         MVC   DDNAME,DA08DDN SAVE DDNAME
         SPACE
         CLC   DDNAME(3),=C'SYS00000'      TEMPORARY TYPE DDNAME?
         BNE   NOTTEMP                     NO, BRANCH
         NC    DA08DDN(8),=C'SYS00000'     TURN OFF NUMERIC DIGITS
         CLC   DA08DDN(8),=C'SYS00000'     TEMPORARY TYPE DDNAME?
         BE    TEMPDSN                     YES, BRANCH
NOTTEMP  OI    FLAGSOP,FPERMDS             MARK AS "PERMANENT"
         SPACE
TEMPDSN  TM    DA08DSO,X'02'    DSORG=PO?
         BO    EXITALOC         YES, GOOD RETURN
         DROP  R1
         SPACE 2
         BAL   R14,DEALLDCB     FREE THE DATA SET
         SPACE 1
         MESSAGE MSGNLIB
         SPACE 1
RECOVERY DS    0H
         MVI   DSPALLOC,DA08SHR  USE DISP=SHR FIRST
ASKAGAIN DS    0H
        $PUTGET MSGRETRY,ATTN=NOTALLOC
         SPACE 2
         TM    FLAGS,FNULL    USER WANT ANOTHER DATA SET?
         BO    ASKAGAIN       NULL REPLY, ASK AGAIN
         SPACE
         L     R1,MAINPCL     YES, INVOKE PARSE
         BAL   R14,GOPARSE
         B     NOTALLOC       EXIT - PARSE FAILURE
         B     RETRY          SUCESSFUL - RETRY ALLOCATION
         SPACE 2
NOTALLOC SR    R15,R15        ERROR EXIT - NOT ALLOCATED
         SPACE 1
EXITALOC L     R14,R14ALLOC
         B     0(R14,R15)     EXIT FROM ALLOCATION
         EJECT
*
*        DYNAMIC ALLOCATION INVOCATION SUBROUTINE
*
         SPACE 3
CALLDAIR DS    0H
         ST    R14,R14SAVE    SAVE LINKAGE REGISTER
         SPACE 2
         LA    R1,PARMLIST    BUILD DYNAMIC ALLOCATION PARAMETER LIST
         USING DAPL,R1
         SPACE
         MVC   DAPLUPT,ADDRUPT
         MVC   DAPLECT,ADDRECT
         MVC   DAPLPSCB,ADDRPSCB
         SPACE
         LA    R0,ATTNECB
         MVI   ATTNECB,0
         ST    R0,DAPLECB
         SPACE
         LA    R0,DAIRBLK
         ST    R0,DAPLDAPB
         SPACE 2
         L     R15,ADDRDAIR
         BALR  R14,R15
         DROP  R1
         SPACE 2
         L     R14,R14SAVE    RESTORE LINKAGE REGISTER
         BR    R14            EXIT
         EJECT
DAIRFAIL DS    0H
         CLC   ENDLIT(3),DSNAMEH   WAS "END" ENTERED BY THE USER?
         BNE   DAIRSVRC            NO, BRANCH
         CLI   DSNAMEH+3,0         WITH A FOLLOWING NULL?
         BE    NOTALLOC            YES, ALLOW ERROR-FREE TERMINATION
         SPACE 1
DAIRSVRC ST    R15,DAIRRC         SAVE RETURN CODE FROM DAIR
         SPACE 2
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15            ERROR MESSAGE OUTPUT?
         BNZ   FAILBAD            NO, BRANCH
         CLI   DSPALLOC,DA08OLD   OLD ALLOCATION ATTEMPT?
         BE    RESTART            YES, REALLOCATE
         B     RECOVERY           NO, ATTEMPT RETRY
         SPACE 2
FAILBAD  LH    R14,MSGDAIRX   DAIRFAIL FAILURE
         MVC   MSGTEXT1(0),MSGDAIRX
         EX    R14,*-6
         CVD   R15,DOUBLE
         LA    R14,MSGTEXT1(R14)
         MVC   0(4,R14),=X'40202120'
         ED    0(4,R14),DOUBLE+6
         MVI   0(R14),C'='
         LA    R0,MSGTEXT1-4
         SR    R14,R0
         SLL   R14,16
         ST    R14,MSGTEXT1
         MESSAGE MSGTEXT1
         CLI   DSPALLOC,DA08OLD   OLD ALLOCATION ATTEMPT?
         BE    RESTART            YES, REALLOCATE WITH DISP=SHR AGAIN
         B     NOTALLOC           NO, TERMINATE
         EJECT
MESSAGE0 DS    0H             OUTPUT A SINGLE MESSAGE
         SR    R0,R0          NO SECONDARY MESSAGE
         SPACE
MESSAGE  DS    0H             OUTPUT ONE OR TWO MESSAGES
         ST    R14,R14SAVE    SAVE LINKAGE REGISTER
         LTR   R0,R0          SECOND LEVEL MSG?
         BZ    ERRORM1        NO
         SPACE
         MVC   MSGTEXT1,0(R1) INSURE MSG IN WORK AREA
         LA    R1,MSGTEXT1
         LH    R14,0(R1)      LENGTH OF FIRST LEVEL MSG
         LA    R15,0(R14,R1)  ADDR OF END OF MSG
         LA    R14,1(R14)     JUMP MSG LENGTH
         STH   R14,0(R1)
         MVI   0(R15),C'+'    INDICATE SECOND LEVEL MSG EXISTS
         SPACE
         SR    R14,R14        CLEAR CHAIN FIELD
         LA    R15,1          ONE SEGMENT IN 2ND MSG
         STM   R14,R0,PUTOLD2 CREATE SECOND-LEVEL
*                             OUTPUT LINE DESCRIPTOR ('OLD')
         LA    R0,PUTOLD2
         SPACE 2
ERRORM1  LR    R14,R0         NEXT 'OLD' ADDR OR ZERO
         LA    R15,1          ONE SEGMENT
         LR    R0,R1          MSG ADDR
         STM   R14,R0,PUTOLD1 FIRST LEVEL 'OLD'
         SPACE
         LA    R1,PARMLIST
         USING IOPL,R1
         SPACE
         MVC   IOPLECT,ADDRECT
         MVC   IOPLUPT,ADDRUPT
         LA    R0,ATTNECB
         ST    R0,IOPLECB
         DROP  R1
         MVI   ATTNECB,0
         SPACE
         L     R15,ADDRPUTL
         XC    PARMLIST+16(4),PARMLIST+16
         PUTLINE PARM=PARMLIST+16,ENTRY=(15),MF=(E,(1)),               X
               OUTPUT=(PUTOLD1,TERM,MULTLVL,INFOR)
         SPACE
         L     R14,R14SAVE
         BR    R14
         EJECT
*
*
*        PUTLINE PROCESSING
*
*
         SPACE 1
$PUTLINE DS    0H
         TM    ATTNECB,X'40'  ATTN OCCUR?
         BOR   R14            YES, IMMED RETURN
         ST    R14,R14PUTL
         BAL   R14,FREEBUFF
         SPACE
         LR    R14,R1         SAVE DATA LINE POINTER
         LA    R1,PUTLPARM
         USING IOPL,R1
         MVC   IOPLECT,ADDRECT
         MVC   IOPLUPT,ADDRUPT
         SPACE
         LA    R0,ATTNECB
         ST    R0,IOPLECB
         SPACE
         L     R15,ADDRPUTL
         XC    IOPLEND(16),IOPLEND
         LR    R0,R14         ADDR OF DATA LINE
         SPACE
         PUTLINE PARM=IOPLEND,ENTRY=(15),MF=(E,(1)),                   X
               OUTPUT=((0),TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK)
         SPACE
         DROP  R1
         TM    ATTNECB,X'40'  ATTN OCCUR?
         BO    PUTLINE2       YES
         SPACE
         LA    R14,4
         CR    R15,R14        CHECK COMPLETION
         BH    PUTERROR       PUTLINE ERROR
         LR    R15,R14
         BL    PUTLINE1
         SPACE
PUTLINE2 DS    0H
         SR    R15,R15
         SPACE
PUTLINE1 DS    0H
         L     R14,R14PUTL
         B     0(R14,R15)
         EJECT
*
*
*        PUTGET PROCESSING
*
*
         SPACE 3
$PUTGET  DS    0H
         TM    ATTNECB,X'40'  ATTN OCCUR?
         BOR   R14            YES, IMMED RETURN
         ST    R14,R14PTGT
         BAL   R14,FREEBUFF
         NI    FLAGS,X'FF'-FNULL
         SPACE
         LA    R0,1           ONLY ONE SEGMENT
         STM   R0,R1,PUTOLD1
         SPACE
         LA    R1,PTGTPARM
         USING IOPL,R1
         MVC   IOPLECT,ADDRECT
         MVC   IOPLUPT,ADDRUPT
         SPACE
         LA    R0,ATTNECB
         ST    R0,IOPLECB
         SPACE
         L     R15,ADDRPTGT
         XC    IOPLEND(16),IOPLEND
         SPACE 2
         PUTGET PARM=IOPLEND,MF=(E,(1)),ENTRY=(15),                    X
               OUTPUT=(PUTOLD1,SINGLE,MODE),                           X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     X
               TERMGET=(EDIT,WAIT)
         SPACE
         LA    R14,8
         CR    R15,R14
         BH    GETERROR       PUTGET ERROR
         SR    R15,R15
         TM    ATTNECB,X'40'  ATTN OCCUR?
         BO    PUTGET1        YES
         SPACE
         LA    R1,PTGTPARM
         LA    R1,IOPLEND
         DROP  R1
         USING PGPB,R1
         L     R1,PGPBIBUF
         DROP  R1
         LA    R0,4
         CH    R0,0(R1)       IS THIS A NULL LINE
         ST    R1,ADDRCBUF
         BL    NOTNULL        NO
         OI    FLAGS,FNULL    INDICATE NULL LINE
NOTNULL  DS    0H
         LA    R15,4
         SPACE
PUTGET1  DS    0H
         L     R14,R14PTGT
         B     0(R14,R15)
         EJECT
*
*        DCB OPEN EXIT
*
*
         SPACE 2
OPENEXIT DS    0H
         USING IHADCB,R1
         NI    FLAGS,X'FF'-FLOADMOD
         TM    DCBRECFM,DCBRECU  LOAD MODULE FORMAT?
         BNO   OPEN1             NO, NOT RECFM=U
         OI    FLAGS,FLOADMOD INDICATE LOAD MODULE
OPEN1    DS    0H
         ICM   R0,15,BUFFADDR    BUFFER PRESENT?
         BNZR  R14               NO, BRANCH
         SPACE
         LH    R2,DCBBLKSI    GET SIZE OF BUFFER
         MVC   LRECL,DCBLRECL SAVE LRECLTOP
         STH   R2,BLKSI       SAVE BLOCKSIZE
         LR    R3,R14
         GETMAIN EC,LV=(R2),SP=0,A=BUFFADDR,MF=(E,PARMLIST)
         LR    R14,R3
         LTR   R15,R15        SUCESSFUL?
         BZ    OPEN2          YES
         XC    BUFFADDR,BUFFADDR
OPEN2    DS    0H
         STH   R2,BUFFSIZE
         BR    R14
         DROP  R1
         EJECT
*
*
*        P A R S E   C O N T R O L   L I S T
*
*
         PRINT NOGEN
PCLMAIN  IKJPARM DSECT=MAINPDL
INPUTDSN IKJPOSIT DSNAME,USID,VALIDCK=VERDSN,                          $
               PROMPT='NAME OF DATA SET',                              $
               HELP=('NAME OF THE PARTITIONED DATA SET TO BE PROCESSED'$
               )
OPTVOL   IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=$VOLUME
$VOLUME  IKJSUBF
VOLNAME  IKJIDENT 'VOLUME NAME',MAXLNTH=6,FIRST=ALPHANUM,              X
               OTHER=ALPHANUM,VALIDCK=VERVOL,                          X
               PROMPT='VOLUME NAME',                                   X
               HELP=('VOLUME WHERE DATA SET RESIDES')
         IKJENDP
         SPACE
PCLSUB   IKJPARM DSECT=PDLSUB
PCLSUB1  IKJIDENT 'MEMBER NAME',MAXLNTH=19,FIRST=ALPHA,OTHER=ANY,      $
               PROMPT='MEMBER NAME',VALIDCK=VERMEMBR,                  $
               HELP=('MEMBER NAME TO BE SUBMITTED')
         IKJENDP
         SPACE
PCLFIND  IKJPARM DSECT=PDLFIND
PCLFIND1 IKJIDENT 'MEMBER NAME',MAXLNTH=19,FIRST=ALPHA,OTHER=ANY,      $
               PROMPT='MEMBER NAME',VALIDCK=VERMEMBR                   $
               HELP=('MEMBER NAME TO BE SEARCHED')
FINDDLM1 IKJPOSIT DELIMITER
FINDSTRG IKJPOSIT STRING
FINDFMT  IKJKEYWD
         IKJNAME  'NUM'
         IKJNAME  'NONUM'
         IKJNAME  'SNUM'
         IKJENDP
         SPACE
PCLLIST  IKJPARM DSECT=PDLLIST
PCLL1    IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='MEMBER TO BE LISTED',                           $
               HELP=('NAME OF THE MEMBER TO BE DISPLAYED'),            $
               VALIDCK=VERMEMBR
LISTFMT  IKJKEYWD
         IKJNAME  'NUM'
         IKJNAME  'NONUM'
         IKJNAME  'SNUM'
         IKJENDP
         SPACE
PCLPATTR IKJPARM DSECT=PDL2MEM
PCLPAT1  IKJIDENT 'PATTERN NAME',MAXLNTH=19,VALIDCK=VERMEMBR,          $
               HELP=('PATTERN CHARACTERS FOR A DIRECTORY SEARCH'),     $
               FIRST=ALPHANUM,OTHER=ANY,PROMPT='PATTERN NAME'
PCLPAT2  IKJIDENT 'PATTERN NAME',MAXLNTH=19,VALIDCK=VERMEMBR,          $
               FIRST=ALPHANUM,OTHER=ANY
         IKJENDP
         SPACE
PCLDSPLY IKJPARM DSECT=PDL1MEM
PCLD1    IKJIDENT 'MEMBER NAME',MAXLNTH=19,                            $
               FIRST=ALPHA,OTHER=ANY,VALIDCK=VERMEMBR
PCLD2    IKJIDENT 'MEMBER NAME',MAXLNTH=19,                            $
               FIRST=ALPHA,OTHER=ANY,VALIDCK=VERMEMBR
         IKJENDP
         SPACE
PCLENTRY IKJPARM DSECT=PDLENTRY
PCLNTRY1 IKJIDENT 'MEMBER OR ALIAS NAME',FIRST=ALPHA,OTHER=ANY,        *
               PROMPT='CURRENT MEMBER OR ALIAS NAME',                  *
               HELP='NAME OF MEMBER OR ALIAS THE ENTRY-POINT OF WHICH I*
               S TO BE CHANGED',VALIDCK=VERMEMBR,MAXLNTH=19
PCLNTRY2 IKJIDENT 'ENTRY-POINT ADDRESS',FIRST=ANY,OTHER=ANY,           *
               PROMPT='NEW ENTRY-POINT ADDRESS IN HEX',                *
               HELP='ENTRY-POINT ADDRESS TO BE ASSIGNED TO THE MEMBER O*
               R ALIAS',VALIDCK=CHKNTRY,MAXLNTH=6
         IKJENDP
PCLALIAS IKJPARM DSECT=PDLALIAS
PCLAL1   IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='CURRENT MEMBER NAME',                           $
               HELP=('NAME OF THE MEMBER TO BE ASSIGNED AN ALIAS'),    $
               VALIDCK=VERMEMBR
PCLAL2   IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='ALIAS NAME',                                    $
               HELP=('NAME TO BE ASSIGNED AS AN ALIAS'),               $
               VALIDCK=VERMEMBR
         IKJENDP
         SPACE
PCLRESET IKJPARM DSECT=PDLRESET
PCLRS1   IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='MEMBER NAME TO RESTORE',                        $
               HELP=('NAME OF THE MEMBER TO BE RESURRECTED'),          $
               VALIDCK=VERMEMBR
PCLRS2   IKJIDENT 'TTR ADDRESS',FIRST=ALPHANUM,OTHER=ALPHANUM,         $
               PROMPT='HEXADECIMAL TTR',MAXLNTH=6,                     $
               HELP=('THE START ADDRESS OF THE MEMBER TO BE RESTORED'),$
               VALIDCK=VERMEMBR
         IKJENDP
         SPACE
PCLDELET IKJPARM DSECT=PDLDELET
PCLS1    IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='MEMBER TO BE DELETED',                          $
               HELP=('NAME OF THE MEMBER TO BE DELETED'),              $
               VALIDCK=VERMEMBR
         IKJENDP
         SPACE
PCLRENAM IKJPARM DSECT=PDLRENAM
PCLREN1  IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='CURRENT MEMBER NAME',                           $
               HELP=('NAME OF THE MEMBER TO BE RENAMED'),              $
               VALIDCK=VERMEMBR
PCLREN2  IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='NEW MEMBER NAME',                               $
               HELP=('NEW NAME OF THE MEMBER'),                        $
               VALIDCK=VERMEMBR
         IKJENDP
         SPACE
PCLMAP   IKJPARM DSECT=PDLMAP
PCLM1    IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='MEMBER TO BE MAPPED',                           $
               HELP=('NAME OF THE MEMBER FOR WHICH A LOAD MODULE MAP IS$
                TO BE PRODUCED'),                                      $
               VALIDCK=VERMEMBR
         IKJENDP
         SPACE
PCLATTR  IKJPARM DSECT=PDLATTR
PCLAT1   IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='MEMBER NAME',                                   $
               HELP=('NAME OF THE MEMBER FOR WHICH THE ATTRIBUTES ARE T$
               O BE LISTED'),                                          $
               VALIDCK=VERMEMBR
PCLAT2   IKJIDENT 'NEW ATTRIBUTES',LIST,FIRST=ALPHA,OTHER=ALPHA,       $
               VALIDCK=VERATTR,MAXLNTH=8,                              $
               HELP=('THE NEW ATRIBUTES TO BE ASSIGNED TO THIS LOAD MOD$
               ULE','NULLIFY ATTRIBUTES BY PREFIXING ''NO'' TO THE ATTR$
               IBUTE NAME')
         IKJENDP
         SPACE
PCLHIST  IKJPARM DSECT=PDLHIST
PCLH1    IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,MAXLNTH=19,      $
               PROMPT='MEMBER NAME',                                   $
               HELP=('NAME OF THE LOAD MODULE FOR WHICH THE HISTORY IS $
               TO BE LISTED'),                                         $
               VALIDCK=VERMEMBR
         IKJENDP
         SPACE
PCLEDIT  IKJPARM DSECT=PDLEDIT
PCLEDIT1 IKJIDENT 'MEMBER NAME',MAXLNTH=19,FIRST=ALPHA,OTHER=ANY,      $
               PROMPT='MEMBER NAME',VALIDCK=VERMEMBR,                  $
               HELP=('MEMBER NAME TO BE EDITED')
PCLNON   IKJKEYWD
         IKJNAME  'NONUM'
PCLASIS  IKJKEYWD
         IKJNAME  'ASIS'
**
**    THE FOLLOWING ORDERED TABLE MUST CORRESPOND WITH EDITTYPE BELOW
**
**     DATA SET TYPE VS. EDIT DATA TYPE RULES ARE FROM "HELP EDIT S"
**
PCLTYPE  IKJKEYWD
         IKJNAME  'DATA'
** INSERT INSTALLATION-ADDED DATA SET TYPES HERE AND IN EDITTYPE BELOW
**
         IKJNAME  'LIST'        ADDED LOCALLY AS A EDIT DATA SET TYPE
         IKJNAME  'BASIC'       FROM ITF
         IKJNAME  'IPLI'        FROM ITF
         IKJNAME  'VSBASIC'     NEW VS BASIC
         IKJNAME  'TEXT'
         IKJNAME  'COBOL'
         IKJNAME  'ASM'
         IKJNAME  'CLIST'
         IKJNAME  'CNTL'
         IKJNAME  'PLI'          $  -- DATA TYPE .PLI
* END OF VALID DATA SET TYPES
         IKJNAME  'PLIF'         $        (DEFAULT EDIT TYPE IS PLI)
         IKJNAME  'FORTE'    *
         IKJNAME  'FORTG'    *
         IKJNAME  'FORTGI'   *  -- DATA TYPE .FORT
         IKJNAME  'FORTH'    *        (DEFAULT EDIT TYPE IS GOFORT)
         IKJNAME  'GOFORT'   *
* END OF VALID DESCRIPTIVE AND DATA SET QUALIFIERS
         IKJENDP
         PRINT GEN
         SPACE 3
**
**  THE FOLLOWING ORDERED TABLE MUST CORRESPOND WITH PCLTYPE ABOVE
**
**     DATA SET TYPE VS. EDIT DATA TYPE RULES ARE FROM "HELP EDIT S"
**
EDITTYPE DS    0H
         DC    CL8'DATA'
** INSERT INSTALLATION-ADDED EDIT DATA TYPES HERE AND IN PCLTYPE ABOVE
**
         DC    CL8'LIST'      ADDED LOCALLY AS A VALID DATA SET TYPE
         DC    CL8'BASIC'     FROM ITF
         DC    CL8'IPLI'      FROM ITF
         DC    CL8'VSBASIC'   NEW VS BASIC
         DC    CL8'TEXT'
         DC    CL8'COBOL'
         DC    CL8'ASM'
         DC    CL8'CLIST'
         DC    CL8'CNTL'
         DC    CL8'PLI'
* END OF VALID DATA SET TYPES
PLIF     DC    CL8'PLIF'
         DC    CL8'FORTE'    *
         DC    CL8'FORTG'    *
         DC    CL8'FORTGI'   *  -- DATA TYPE .FORT
         DC    CL8'FORTH'    *        (DEFAULT EDIT TYPE IS GOFORT)
GOFORT   DC    CL8'GOFORT'   *
* END OF VALID DESCRIPTIVE AND DATA SET QUALIFIERS
         EJECT
         SPACE
DEBXTNT# EQU   16             OFFSET IN DEB FOR # OF EXTENTS
DEBEXTNT EQU   32             OFFSET IN DEB FOR D.A. EXTENT ENTRY
DEBAMLNG EQU   16             SIZE OF DIRECT ACCESS EXTENT IN DEB
DS1LSTAR EQU   98             OFFSET IN DSCB FOR LAST TTR USED
         SPACE 2
WORKSIZE DC    0F'0',AL1(1),AL3(LENWORK)  SUBPOOL, DYNAMIC STORAGE SIZE
         SPACE 2
STAE     STAE  0,CT,MF=L      PATTERN STAE EXIT
LSTAE    EQU   *-STAE
         SPACE 2
EXCPDCB  DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=E
LEXCPDCB EQU   *-EXCPDCB
SAMDCB   DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=(R,W),EXLST=EXLST
LSAMDCB  EQU   *-SAMDCB
EXLST    DC    X'85',AL3(OPENEXIT)
         SPACE 2
OBTAIN   CAMLST SEARCH,0,0,0
         ORG   OBTAIN+4
         SPACE 2
BLDLPARM DC    AL2(1,74)
DATEMASK DC    X'402120612020612020'
         SPACE 2
NO       DC    C'NO'
IDALIAS  DC    C'-A'
FF       DC    0F'0',8X'FF'
BLANKS   DC    0F'0',8X'40'
TRTABLE  EQU   *-X'F0'
         DC    C'0123456789ABCDEF'
         SPACE 2
MAINPCL  DC    A(PCLMAIN)     PARSE LIST FOR DATA SET NAME
         SPACE
DAYMONTH DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)
         EJECT
*
*        NOTE - THE SAME SET OF LOGICAL CCWS CAN BE USED
*        TO READ A PDS DIRECTORY, OR THE CESD ENTRIES, OR
*        IDR RECORDS WITHIN A LOAD MODULE.
*
         SPACE
IDRCCWS  DS    0D
ESDCCWS  DS    0D
DIRCCWS  DS    0D
         CCW   X'31',0,X'60',5
         CCW   X'08',0,X'60',1
         CCW   X'86',0,X'60',256
         CCW   X'92',0,X'20',5
LDIRCCWS EQU   *-DIRCCWS
LESDCCWS EQU   *-ESDCCWS
LIDRCCWS EQU   *-IDRCCWS
         SPACE 2
LISTCCWS DS    0D
         CCW   X'31',0,X'60',5
         CCW   X'08',0,X'60',1
         CCW   X'92',0,X'60',5
         CCW   X'86',0,X'20',0
LLISTCCW EQU   *-LISTCCWS
         EJECT
TBLATTR  DS    0H
         DC    BL2'1000000000000000',AL1(3),CL9'RENT'
         DC    BL2'0100000000000000',AL1(3),CL9'REUS'
         DC    BL2'0000000000000001',AL1(3),CL9'REFR'
         DC    BL2'0010000000000000',AL1(6),CL9'OVERLAY'
         DC    BL2'0001000000000000',AL1(3),CL9'TEST'
         DC    BL2'0000100000000000',AL1(8),CL9'LOAD ONLY'
         DC    BL2'0000010000000000',AL1(3),CL9'SCTR'
         DC    BL2'0000000000001000',AL1(7),CL9'NOT EDIT'
         DC    BL2'0000001000000000',AL1(128+7),CL9'NOT EXEC'
         DC    BL2'0000000010000000',AL1(128+1),CL9'DC'
         DC    X'FF'
         SPACE 3
ATTRRENT EQU   X'80'
ATTRREUS EQU   X'40'
ATTROVLY EQU   X'20'
ATTRNE   EQU   X'08'          NOT-EDITABLE ATTRIBUTE
ATTRSCAT EQU   X'04'          SCATTER LOADED
ATTREP0  EQU   X'20'          ASSIGNED ENTRY PT AT OFFSET 0
ATTRREFR EQU   X'01'
ATTRLOAD EQU   X'08'
ATTREXEC EQU   X'02'
         SPACE 3
ATTRTBL  DS    0X
         DC    CL8'RENT',X'00',AL1(ATTRRENT,0)  * * *  ATTRIBUTES
LATTRTBL EQU   *-ATTRTBL
LATTR    EQU   8
         DC    CL8'REUS',X'00',AL1(ATTRREUS,0)   * * *  WHICH
         DC    CL8'REFR',X'00',AL1(0,ATTRREFR)    * * *  CAN BE
         DC    CL8'EXEC',X'00',AL1(ATTREXEC,0)     * * *  CHANGED
         DC    CL8'LOAD',X'00',AL1(ATTRLOAD,0)      * * *  BY
         DC    CL8'AUTH',X'40',AL1(0,0)              * * *  PDS
         DC    X'FF'
         SPACE
$SEGTAB  DC    CL8'$SEGTAB'
$ENTAB   DC    CL8'$ENTAB'
$PRIVATE DC    CL8'$PRIVATE'
         PRINT NOGEN
         EJECT
OPTNTBL  MSG   'THE FOLLOWING OPTIONS ARE AVAILABLE:'
         MSG   'ALIAS   -   ASSIGN AN ALIAS TO A MEMBER'
         MSG   'ATTR    -   LIST LOAD MODULE ATTRIBUTES'
         MSG   'CHANGE  -   SELECT A NEW DATA SET'
         MSG   'DISPLAY -   DISPLAY DIRECTORY (ALL OR A RANGE)'
         MSG   'DELETE  -   SCRATCH A MEMBER'
         MSG   'EDIT    -   EDIT A MEMBER'
         MSG   'END     -   STOP THE PROGRAM'
         MSG   'ENTRY   -   ASSIGN A NEW ENTRY-POINT TO A LOADMODULE'
         MSG   'EXEC    -   EXECUTE PDS COMMANDS FROM A CLIST SOURCE'
         MSG   'FIND    -   LIST LINES OF A MEMBER CONTAINING A STRING'
         MSG   'HELP    -   DISPLAY PDS COMMAND HELP'
         MSG   'HISTORY -   LIST HISTORY OF LOAD MODULE'
         MSG   'LIST    -   LIST CONTENTS OF A MEMBER'
         MSG   'MAP     -   MAP LOAD MODULE STRUCTURE'
         MSG   'OPTIONS -   DISPLAY THIS MENU'
         MSG   'PATTERN -   DISPLAY DIRECTORY MATCHING PATTERN'
         MSG   'RENAME  -   RENAME A MEMBER'
         MSG   'RESTORE -   RESURRECT A PREVIOUSLY DELETED MEMBER'
         MSG   'SMAP    -   SHORT MAP OF LOAD MODULE STRUCTURE'
         MSG   'SUBMIT  -   SUBMIT A MEMBER (JCL) TO BACKGROUND'
         MSG   'USAGE   -   LIST DIRECTORY STATISTICS'
         DC    X'FFFF',0F'0'  ALIGN FOR NEXT TABLE
         SPACE 2
*  CMDTABLE OPERAND BITS:
*    X'80' - OPERAND OPTIONAL -- PARSE IS CALLED IF OPERAND IS PRESENT
*    X'40' - OPERAND NOT ALLOWED -- ANY OPERAND IS IGNORED IF CODED
*    X'20' - OPERAND DEFAULTS TO CURRENT MEMBER NAME IN USE
*    X'00' - OPERAND REQUIRED -- PARSE IS ALWAYS CALLED
         SPACE 1
CMDTABLE DC    CL8'ATTR    ',AL1(4),AL3(ATTR),X'20',AL3(PCLATTR)
         DC    CL8'ALIAS   ',AL1(5),AL3(ALIAS),X'00',AL3(PCLALIAS)
         DC    CL8'CHANGE  ',AL1(6),AL3(CHANGE),X'00',AL3(PCLMAIN)
DISPLIT  DC    CL8'DISPLAY ',AL1(7),AL3(DISPLAY),X'80',AL3(PCLDSPLY)
         DC    CL8'DELETE  ',AL1(6),AL3(DELETE),X'00',AL3(PCLDELET)
EDITLIT  DC    CL8'EDIT    ',AL1(4),AL3(EDIT),X'20',AL3(PCLEDIT)
ENDLIT   DC    CL8'END     ',AL1(3),AL3(END),X'40',AL3(0)
         DC    CL8'ENTRY   ',AL1(5),AL3(ENTRY),X'00',AL3(PCLENTRY)
EXECLIT  DC    CL8'EXEC    ',AL1(4),AL3(EXEC),X'40',AL3(0)
FINDLIT  DC    CL8'FIND    ',AL1(4),AL3(FIND),X'20',AL3(PCLFIND)
HELPLIT  DC    CL8'HELP    ',AL1(4),AL3(HELP),X'40',AL3(0)
         DC    CL8'HISTORY ',AL1(7),AL3(HISTORY),X'20',AL3(PCLHIST)
LISTLIT  DC    CL8'LIST    ',AL1(4),AL3(LIST),X'20',AL3(PCLLIST)
         DC    CL8'MAP     ',AL1(3),AL3(MAP),X'20',AL3(PCLMAP)
         DC    CL8'OPTIONS ',AL1(7),AL3(OPTIONS),X'40',AL3(0)
         DC    CL8'PATTERN ',AL1(7),AL3(PATTERN),X'00',AL3(PCLPATTR)
         DC    CL8'RENAME  ',AL1(6),AL3(RENAME),X'00',AL3(PCLRENAM)
RESETLIT DC    CL8'RESTORE ',AL1(7),AL3(RESTORE),X'00',AL3(PCLRESET)
         DC    CL8'SMAP    ',AL1(4),AL3(SMAP),X'20',AL3(PCLMAP)
         DC    CL8'SCRATCH ',AL1(7),AL3(DELETE),X'00',AL3(PCLDELET)
SUBLIT   DC    CL8'SUBMIT  ',AL1(6),AL3(SUBMIT),X'20',AL3(PCLSUB)
         DC    CL8'USAGE   ',AL1(5),AL3(USAGE),X'40',AL3(0)
         DC    CL8'PDS     ',AL1(3),AL3(CHANGE),X'00',AL3(PCLMAIN)
         DC    X'FF'
         EJECT
*
*        PROGRAM MESSAGES
*
         SPACE 2
*        PRINT NOGEN
         SPACE
MSGUSEB  MSG   ' DIRECTORY BLOCKS ALLOCATED'
MSGDSNDD MSG   '//12345678  DD  DSN='''
MSGDCB   MSG   '//  DCB=(RECFM=*,LRECL=,BLKSIZE='
LRECLTXT EQU   MSGDCB+20,7
BLKSITXT EQU   MSGDCB+20+7,9
MSGUSED  MSG   ' DIRECTORY BLOCKS IN USE'
MSGUSER  MSG   ' MEMBERS EXIST'
MSGUSEA  MSG   ' OF THEM ARE ALIASES'
MSGUSESI MSG   ' TRACKS ALLOCATED'
MSGUSEMP MSG   ' TRACKS UNUSED'
MSGUSEXT MSG   ' EXTENTS USED'
MSGNDSCB MSG   ' OBTAIN ERROR'
MSGSTOWC MSG   ' STOW ERROR - CODE = 12'
MSGSTOW8 MSG   ' STOW ERROR - CODE = 8'
MSGNLIST MSG   ' UNABLE TO LIST LOAD MODULES'
MSGLISTI MSG   ' I/O ERROR READING MEMBER'
MSGNLIB  MSG   ' DATA SET IS NOT PARTITIONED'
MSGNLOAD MSG   ' DATA SET NOT LOAD LIBRARY'
MSGNOPEN MSG   ' UNABLE TO OPEN DATA SET'
MSGWRNGV MSG   ' WRONG VOLUME ALLOCATED'
MSGBADAT MSG   ' INVALID ATTRIBUTE, '
MSGDUPAT MSG   ' CONFLICTING ATTRIBUTES, THIS ATTRIBUTE IGNORED, '
MSGIOERR MSG   ' I/O ERROR IN DIRECTORY'
MSGNODIR MSG   ' DIRECTORY IS FULL'
MSGEMPTY MSG   ' NO MEMBERS EXIST'
MSGRANGE MSG   ' INVALID DISPLAY RANGE'
MSGNODSP MSG   ' NO MEMBERS IN DISPLAY RANGE'
MSGNOPAT MSG   ' NO MEMBERS MATCH THE SPECIFIED PATTERN'
MSGEXIST MSG   ' MEMBER ALREADY EXISTS'
MSGNONE  MSG   ' MEMBER NOT FOUND'
MSGGONE  MSG   ' MEMBER DELETED'
MSGRENAM MSG   ' MEMBER RENAMED'
MSGRESET MSG   ' MEMBER RESTORED'
MSGATTRS MSG   'ATTRIBUTES ARE:'
MSGNREAL MSG   ' MODULE IS AN ALIAS, NO ALIAS ASSIGNED'
MSGREAL  MSG   ' MODULE IS ALIAS FOR '
MSGOTHER MSG   ' ALIASES FOR THIS MODULE ARE:'
MSGNOMOD MSG   ' MODULE IS ALIAS BUT NO REAL MODULE EXISTS'
NONEMSG  MSG   ' NONE'
MSGYAUTH MSG   ' APF AUTHORIZED'
MSGNAUTH MSG   ' NOT APF AUTHORIZED'
MSGNOVSL MSG   ' NOT APF AUTHORIZED, NON VS LKED'
MSGNOAPF MSG   ' NOT APF AUTHORIZED, APF DATA MISSING'
MSGAPFLB MSG   ' NOT APF AUTHORIZED, APF DATA WRONG LENGTH'
MSGAPF2B MSG   ' APF AUTHORIZED, APF DATA VALUE > 1'
MSGADNC  MSG   ' APF DATA COULD NOT BE CHANGED'
MSGASCAT MSG   ' ALIAS NOT SUPPORTED FOR SCATTER LOADED MODULES'
MSGSSIDR MSG   ' SSI INFORMATION PRESENT - IN PRIMARY ONLY'
MSGAABAD MSG   ' BAD APF INFORMATION FORMAT, ZERO USED'
MSGNOESD MSG   ' MODULE HAS NO EXTERNAL SYMBOLS'
MSGALIAS MSG   'ALIAS ASSIGNED TO MEMBER'
MSGNTRY  MSG   ' ENTRY-POINT ASSIGNED'
MSGENTRY MSG   'ENTRY POINT AT '
MSGLEN   MSG   'MODULE LENGTH  '
MSGHISTD MSG   'LAST LINK-EDITED ON'
MSGHISTZ MSG   'IMASPZAP UPDATE HISTORY BY CSECT -'
MSGHISTU MSG   'USER-SUPPLIED UPDATE HISTORY BY CSECT -'
MSGNHIST MSG   ' NO HISTORY AVAILABLE'
MSGUNKN  MSG   ' INVALID OPTION, ENTER HELP OR OPTION FOR HELP'
MSGCMDS1 MSG 'SUBCOMMANDS: ALIAS, ATTR, CHANGE, DISPLAY, DELETE, EDIT,'
MSGCMDS2 MSG '   END, EXEC, FIND, HELP, HISTORY, LIST, MAP, OPTIONS,'
MSGCMDS3 MSG '   PATTERN, RENAME, RESTORE, SMAP, SUBMIT, USAGE, ENTRY'
MSGENTER MSG   ' ENTER OPTION'
MSGDEFM  MSG   ' DEFAULT MEMBER - 12345678'
MSGRETRY MSG   ' RE-ENTER DATA SET NAME'
MSGINVLD MSG   ' INVALID SUBCOMMAND'
MSGDAIRX MSG   ' ERROR IN DAIRFAIL SERVICE ROUTINE;  RETURN CODE'
MSGGENF  MSG   ' ERROR IN GENERAL FAIL SERVICE ROUTINE;  RETURN CODE'
MSGFINDN MSG   ' THE FIRST FIND SUBCOMMAND MUST CONTAIN A STRING'
MSGNF80  MSG   ' DATASET IS NOT FIXED FORMAT WITH EIGHTY BYTE RECORDS'
         PRINT GEN
         EJECT
         LTORG
PATCH    DS    0H
         B     PATCH
         DC    18H'0'
         SPACE 3
TRTMN    DC    256X'FF'           ASSUME ALL BAD FIRST
         ORG   TRTMN+C' '
         DC    1X'00'             BLANK IS OK
         ORG   TRTMN+C'Ö'
         DC    7X'00'             Ö.<(+×&  ARE OK
         ORG   TRTMN+C'!'
         DC    8X'00'             !$*);^-/  ARE OK
         ORG   TRTMN+C','
         DC    5X'00'             ,%_>?  ARE OK
         ORG   TRTMN+C':'
         DC    6X'00'             :#@'="  ARE OK
         ORG   TRTMN+X'81'        LOWER-CASE
         DC    9X'AA'             ABCDEFGHI  FORCE A HEX DISPLAY
         ORG   TRTMN+X'91'        LOWER-CASE
         DC    9X'AA'             JKLMNOPQR  FORCE A HEX DISPLAY
         ORG   TRTMN+X'A2'        LOWER-CASE
         DC    8X'AA'             STUVWXYZ  FORCE A HEX DISPLAY
         ORG   TRTMN+C'A'
         DC    9X'00'             ABCDEFGHI  ARE OK
         ORG   TRTMN+C'J'
         DC    9X'00'             JKLMNOPQR  ARE OK
         ORG   TRTMN+C'S'
         DC    8X'00'             STUVWXYZ  ARE OK
         ORG   TRTMN+C'0'
         DC    10X'00'            0123456789  ARE OK
         ORG   ,
         EJECT
*
*        DYNAMIC WORK AREA
*
         SPACE 3
WORKAREA DSECT
MAINSAVE DS    18A
         SPACE 2
GFPARMP  DS    F              POINTER TO START OF GENERAL FAIL PARMS
         IKJEFFGF
         EJECT
         IKJEFFDF
         ORG   DFBUFS         NO CALLER-SUPPLIED BUFFERS NEEDED
         SPACE 2
DFPLLIST DS    4F             SPACE FOR DEFAULT PARAMETER LIST
DFPBLIST DS    5F             SPACE FOR DEFAULT PARAMETER BLOCK
VALSAVE  DS    18A
ATTNSAVE DS    18A
PARSELST DS    6A             AREA FOR PARSE PARAMETER LIST
ADDRSAVE DS    A              ADDRESS OF FIRST SAVE AREA
         SPACE
STAEREGS DS    16A
STAESAVE DS    18A
R14SAVE  DS    A
R14PARSE DS    A
R14ALLOC DS    A
R14LINK  DS    A
R14PUTL  DS    A
R14PTGT  DS    A
         SPACE
BASES    DS    4A             PROGRAM BASE ADDRESSES
         SPACE
***
***      NOTE: KEEP THE FOLLOWING STATEMENTS TOGETHER AND IN THE
***            SAME PHYSICAL ORDER (FOR STM INSTRUCTIONS).
***
ADDRTEXT DS    A        * *    ADDRESS OF TEXT PASSED
ADDRUPT  DS    A        * *    ADDRESS OF THE UPT
ADDRPSCB DS    A        * *    ADDRESS OF THE PSCB
ADDRECT  DS    A        * *    ADDRESS OF THE ECT
ADDRCBUF DS    A        * *    ADDRESS OF THE COMMAND BUFFER
CPPLADDR DS    A        * *    ADDRESS OF THE CPPL
ADDRPCL  DS    A        * *    ADDRESS OF PCL FOR PARSE
ADDRPUTL DS    A        * *    ADDRESS OF IKJPUTL
ADDRPARS DS    A        * *    ADDRESS OF IKJPARS
ADDRPTGT DS    A        * *    ADDRESS OF IKJPTGT
ADDRSCAN DS    A        * *    ADDRESS OF IKJSCAN
ADDRDAIR DS    A        * *    ADDRESS OF IKJDAIR
ADDRSTCK DS    A        * *    ADDRESS OF IKJSTCK
ADDRDEFT DS    A        * *    ADDRESS OF IKJEHDEF
ADREFF02 DS    A        * *    ADDRESS OF IKJEFF02
ADRPCNVT DS    A        * *    TTR --> MBBCCHHR CONVERT ROUTINE
***      END OF CRITICAL AREA
         SPACE 2
ADDRANSR DS    A
ADDRCMD  DS    A
OPENLIST DS    2A
         SPACE 2
ATTNECB  DS    F
DAIRRC   DS    F              DAIR RETURN CODE FROM REGISTER 15
         SPACE
ESDPTR   DS    A              ADDRESS OF ESD DATA CHAIN
IDRPTR   DS    A
         SPACE
PARMLIST DS    10A
PUTLPARM DS    0A                 -- SAVE AREA
PTGTPARM DS    10A
SCANANSR DS    XL8
SUBNAME  DS    CL8            CURRENT FULL SUBCOMMAND NAME
RESETTTR DS    F              SAVEAREA FOR RESTORE TTR (NOT USED NOW)
BUFFADDR DS    A
BUFFSIZE DS    H
LRECL    DS    H
BLKSI    DS    H
ATTRYES  DS    XL2
ATTRNO   DS    XL2
         SPACE 2
LINESIZE DS    H
LMEMBER1 DS    H
LMEMBER2 DS    H
TOTBLOCK DS    H
TOTUSED  DS    H
TOTMEMR  DS    H
TOTMEMA  DS    H
DSNTOTAL DS    H
DSNEMPTY DS    H
DSNEXTNT DS    H
         SPACE
DEFMEMB  DS    CL8            DEFAULT MEMBER NAME FOR 8 SUBCOMMANDS
ENTRYPT  DS    CL8            ENTRY POINT NAME FOR MAP AND SMAP
         SPACE
MEMBERS  DS    0X
MEMBER1  DS    CL8            FIRST MEMBER NAME
MEMBER2  DS    CL8            SECOND MEMBER NAME
         SPACE
SAVETTR  DS    XL3,X
LKEDDATE DS    XL3
ENTRYAD  DS    CL3
         DS    CL1
DBLWD    DS    D
         SPACE 2
VOLUME   DS    CL6
DSPALLOC DS    X        DISPOSITION DESIRED (SHR INITIALLY OR OLD)
         SPACE
DOUBLE   DS    D
         SPACE
STAEPARM STAE  0,CT,MF=L
         SPACE
FLAGS    DS    XL1            GENERAL STATUS FLAGS
*        FLAG BYTE 1
FOPTIONS EQU   X'80'          OPTIONS PRESENT WITH COMMAND
FCMD     EQU   X'40'          SUBCOMMAND BUFFER AVAILABLE
FNULL    EQU   X'20'          NULL LINE ENTERED
FMEMBER1 EQU   X'10'          FIRST MEMBER FLAG
FMEMBER2 EQU   X'08'          SECOND MEMBER FLAG
FLOADMOD EQU   X'04'          DATA SET IN LOAD MODULE FORMAT
*RESTART EQU   X'02'          RESTART W/ NEW DATA SET
FATTR    EQU   X'01'          CHANGE ATTRIBUTES REQUEST
         SPACE
*        FLAGS AT BYTE 1
FLAGSRD  DS    XL1
F1STREAD EQU   X'80'          INITIAL ENTRY TO READ SUBROUTINE
FLINESET EQU   X'40'          OUTPUT LINE IN PROGRESS
FEXIST   EQU   X'20'          MEMBERS EXIST IN DATA SET
FRANGE   EQU   X'10'          MEMBERS EXIST IN DISPLAY RANGE
FIDR     EQU   X'40'          IDR RECORDS EXIST
F1IDR    EQU   X'20'          IDR TITLE PROCESSED
FALLESD  EQU   X'08'          REQUEST ALL ESD ENTRIES
*NORETRY EQU   X'04'          NO ALLOCATION RETRY ALLOWED
FQUOTE   EQU   X'02'          DATA SET NAME IS QUOTED
FATTN    EQU   X'01'          INTERRUPTIONS ALLOWED
         SPACE 1
*        FLAGS AT BYTE 2
FLAGSCN  DS    XL1
*REALMOD EQU   X'80'          TRUE MODULE NAME SPECIFIED
FDIRSCAN EQU   X'20'          SCAN DIRECTORY STATUS
FDIREND  EQU   X'10'          LAST USED BLOCK ENCOUNTERED
FIDRCONT EQU   X'08'          PROCESSING CONTINUED IDR RECORD
FIDRHCON EQU   X'04'
FAPFON   EQU   X'02'          TURN ON APF REQUESTED
FAPFOFF  EQU   X'01'          TURN OFF APF REQUESTED
         SPACE 1
*        FLAGS AT BYTE 3
FLAGSOP  DS    XL1
FSMAP    EQU   X'80'          SMAP (OTHERWISE MAP)
FINDOP   EQU   X'40'          FIND  (OTHERWISE LIST)
PATTOP   EQU   X'20'          PATTERN (OTHERWISE DISPLAY)
FPERMDS  EQU   X'10'          "PERM" DATA SET
NONUM    EQU   X'08'          NONUM LIST OUTPUT DESIRED
SNUM     EQU   X'04'          SNUM LIST OUTPUT DESIRED
*                             NUM (DEFAULT) IF NOT NONUM OR SNUM
         SPACE 2
DDNAME   DS    CL8
DDNAMEH  DS    CL8                      DDNAME HOLD (WRONG VOL, HELP)
PASSWORD DS    CL8
DSNLEN   DS    H
DSNAME   DS    CL44
DSNAMEH  DS    CL4                      FIRST FOUR DSNAME CHARACTERS
         SPACE 2
JFCB     DS    XL176                    AREA FOR JFCB
JFCBDSNM EQU   JFCB+0                   FULLY QUALIFIED DATA SET NAME
JFCBVSER EQU   JFCB+118                 FIRST VOLUME SERIAL POSITION
         SPACE 2
         DS    0D
DSCB     DS    XL148                    AREA FOR OBTAIN WORKAREA/DSCB
         ORG   DSCB
DAIRBLK  DS    25D            SPACE FOR DAIR PARAMETER LIST
LDAIRBLK EQU   *-DAIRBLK
         ORG
         SPACE 2
CAMLST   CAMLST SEARCH,0,0,0
         SPACE
         SPACE 2
         DS    0F
MSGTEXT1 DS    XL124,XL136    NEED 4 BYTE COUNT AND 256 BYTE DISPLAY
MSGTEXT2 DS    XL124
         SPACE
         ORG   MSGTEXT1
DAYTABLE DS    XL12
         ORG
         SPACE 2
PUTOLD1  DS    3F
PUTOLD2  DS    3F
         SPACE 2
INDCB    DCB   DSORG=PO,MACRF=(R,W),DDNAME=X
STOWDCB  DCB   DSORG=PO,MACRF=(R,W),DDNAME=X
EXLSTX   DS    A
         SPACE 2
         DS    0F
BLDLLIST DS    XL4
MEMNAME  DS    0CL8
DIRNAME  DS    CL8
DIRTTR   DS    XL3
DIRFLAG  DS    X
DIRUSER  DS    XL62
         ORG   DIRUSER
*        THE FOLLOWING FIELDS APPLY TO LOAD MODULES
DIRSTART DS    XL4            TTR OF FIRST TEXT BLOCK
DIRNOTE  DS    XL3            TTR OF NOTE LIST
DIRNOTE# DS    X
DIRATTR  DS    XL2
DIRCORE  DS    XL3            SIZE OF LOAD MODULE
DIRTEXTL DS    XL2            LENGTH OF 1ST TEXT RECORD
DIRENTRY DS    XL3            ENTRY POINT ADDRESS
DIRATTR2 DS    XL1        ADDITIONAL ATTRIBUTE BYTES:
DIRAOSLE EQU   X'80'          VS LINKAGE EDITOR CREATED THIS MODULE
DIRRESRV EQU   X'40'          RESERVED FOR FUTURE EXPANSION
DIR2PAGA EQU   X'20'          PAGE ALIGNMENT REQUIRED FOR THIS MODULE
DIR2SSI  EQU   X'10'          SSI INFORMATION IS PRESENT FOR MODULE
DIRAPFLG EQU   X'08'          APF INFORMATION FOR THIS MODULE IS VALID
         SPACE 1
         DS    XL2            RESERVED
DIRAPF   DS    0XL2           APF (IF NOT SCAT, SSI OR ALIAS)
DIREP    DS    XL3            ENTRY POINT (REAL MEMBER)
DIRREAL  DS    CL8            REAL NAME OF MEMBER
DIRAPF2  DS    XL2            APF INFORMATION IF WITH ALIAS
DIREND2  EQU   *              END OF ALIAS SECTION
         ORG
DIREND   EQU   *              END OF DIRECTORY INFORMATION
         SPACE 2
         DS    0D
         SPACE 2
CCWS     DS    6D
LCCWS    EQU   *-CCWS
         SPACE
IOECB    DS    F
IOB      DS    0F
IOBFLAG  DS    X'42000000'
IOBECB   DS    A(IOECB)
         DS    X
IOBCSW   DS    XL7
IOBCCW   DS    A(CCWS)
IOBDCB   DS    A(INDCB)
         DS    2F
MBBCCHHR DS    XL3
IOBSEEK  DS    XL5
         DS    4F
LIOB     EQU   *-IOB
         SPACE 2
IDROFSAV DS    F               CONTINUED IDR BLOCK OFFSET SAVE AREA
DIRPTRS  DS    3F
*        NOTE - THE SAME BUFFER CAN BE USED TO
*        CONTAIN A PDS DIRECTORY BLOCK OR AN IDR OR
*        CESD RECORD FROM A LOAD MODULE
IDRBUFF  DS    0XL256
ESDBUFF  DS    0XL256
DIRBLOCK DS    XL256
FINDTBL  DS    XL256           TRT TABLE (FIND FIRST BYTE OF STRING)
ANSWERS  DS    24C             FIRST 24 BYTES OF PDL (PARSE RESULTS)
FINDLTH  DS    H               ACTUAL STRING LENGTH
FINDSTR  DS    60C             ACTUAL FIND STRING
         SPACE 3
LENWORK  EQU   *-WORKAREA
*        PRINT GEN
         EJECT
*
*
*        ESD TABLE FORMAT
*
*
         SPACE 2
ESDENTRY DSECT
ESDLINK  DS    F
ESDNAME  DS    CL8            NAME OF EXTERNAL SYMBOL
ESDTYPE  DS    X              ESD TYPE
CODESD   EQU   X'00'          ENTERNAL DEFINATION
CODELR   EQU   X'03'          EXTERNAL REFERENCE
CODESEG  EQU   X'14'          OVERLAY SEGMENT TABLE
CODEPC   EQU   X'04'          PRIVATE CODE DEFINITION
CODEENTB EQU   CODESEG        OVERLAY ENTRY TABLE
ESDADDR  DS    XL3            RELATIVE OFFSET
ESDSEG#  DS    X              SEGMENT NUMBER
ESDLEN   DS    XL3            LENGTH OF SD ENTRY
LENESD1  EQU   *-ESDNAME      LENGTH OF ESD DATA
ESDID    DS    XL2            RELATIVE ESD ID #
LENESD2  EQU   *-ESDNAME      LENGTH OF ESD DATA
LENESD   EQU   *-ESDENTRY     LENGTH OF ENTRY
         EJECT
IDRENTRY DSECT
IDRLINK  DS    A
IDRSTART EQU   *
IDRESDID DS    XL2
IDRTYPE  DS    X
IDRZAP   EQU   X'01'
IDRLKED  EQU   X'02'
IDRUSER  EQU   X'08'
IDRDATE  DS    XL3
IDRLDATA DS    X
IDRUPREF DS    XL6            USER RECORD PREFIX
IDRZDATA DS    0CL8
IDRDATA  DS    CL40
LENIDR1  EQU   *-IDRSTART
LENIDR   EQU   *-IDRENTRY
         EJECT
         IKJPPL
         EJECT
         IKJIOPL
IOPLEND  DS    0F
         EJECT
         IKJPSCB
         EJECT
         IKJECT
         EJECT
         IKJCPPL
         EJECT
         IKJCSPL
         EJECT
         IKJCSOA
         EJECT
         IKJPGPB
**       EJECT
**       IKJGTPB
         EJECT
         IKJDAPL
**       EJECT
**       IKJDAP00
**       EJECT
**       IKJDAP04
         EJECT
         IKJDAP08
         EJECT
         IKJDAP18
         SPACE 2
**       IKJDAP0C
**0CDDN  DS    XL8 START OF DDNAME LIST
**       EJECT
**       IKJDAP10
**       EJECT
**       IKJDAP14
**       EJECT
**       IKJDAP1C
**       EJECT
**       IKJDAP24
**       EJECT
**       IKJDAP28
**       EJECT
**       IKJDAP2C
**       EJECT
**       IKJDAP30
         EJECT
         IKJDFPL
         SPACE 3
         IKJDFPB
         EJECT
         PRINT NOGEN
         DCBD  DSORG=PO
         SPACE 5
         CVT   DSECT=YES
         SPACE 5
         END  PDS
