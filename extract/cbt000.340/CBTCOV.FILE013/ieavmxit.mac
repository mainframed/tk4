 TITLE 'SAMPLE COMMUNICATIONS TASK USER EXIT, TESTED DOENCH'
***********************************************************************
*                                                                     *
* MODULE NAME         =  IEAVMXIT                                     *
*                                                                     *
* DESCRIPTIVE NAME    =  SAMPLE COMMUNICATIONS TASK USER EXIT FOR     *
*                        MESSAGES IFB010D, ILR022A, $HASP426, ERB306D *
*                                                                     *
* FUNCTIONS           =  ROUTES MESSAGES VIA ROUTING CODE 11,         *
*                        SUPPRESSES AND REPLIES TO MESSAGES           *
*                                                                     *
*                        ISSUES $S AFTER JES2 INITIALIZATION          *
*                        ISSUES S TSO AFTER VTAM INITIALIZATION       *
*                                                                     *
*OPERATION            =  DETERMINES WHICH MESSAGE IS BEING PROCESSED. *
*                        REQUESTS TO CHANGE THE ROUTING CODES TO BE   *
*                        ROUTING CODE 11 ONLY.                        *
*                                                                     *
*                        THIS EXIT ROUTINE                            *
*                        REQUESTS TO SUPPRESS THE WTOR AND THEN       *
*                        REPLIES TO IT VIA THE MGCR MACRO.            *
*                                                                     *
*                                                                     *
* ENTRY POINT         =  IEAVXMIT                                     *
*                                                                     *
*   LINKAGE           =  BALR                                         *
*                                                                     *
*   INPUT DATA        =  REG 1 POINTS TO THE ADDRESS OF THE CTXT      *
*                        REG 13 ADDRESS OF STANDARD SAVE AREA         *
*                        REG15 ENTRY POINT                            *
*                                                                     *
*   REGISTERS SAVED   =  REG0 - REG15                                 *
*                                                                     *
*   REGISTER USAGE    =  REG5  - POINTER TO THE ADDRESS OF THE CTXT   *
*                        REG11 - MODULE DATA REGISTER                 *
*                        REG12 - MODULE BASE REGISTER                 *
*                        REG13 - POINTER TO A STANDARD SAVE AREA      *
*                        REG14 - RETURN POINT                         *
*                                                                     *
*   REGISTERS RESTORED = REG0 - REG15                                 *
*                                                                     *
*   CONTROL BLOCKS    =                                               *
*     NAME     MAPPING MACRO   REASON USED                  USAGE     *
*     ----     -------------   -----------                 -------    *
*     CTXT      IEZVX100        USER EXIT PARAMETER LIST     R,W      *
*     MGCR      IEZMGCR         SVC 34 PARAMETER LIST        C,D      *
*                                                                     *
*   KEY = R-READ, W-WRITE, C-CREATE, D-DELETE                         *
*                                                                     *
*   TABLES            =  NONE                                         *
*                                                                     *
*   MACROS            =  GETMAIN, FREEMAIN, MGCR, WTO                 *
*                                                                     *
***********************************************************************
         EJECT
IEAVMXIT CSECT
IEAVMXIT AMODE 31                      31-BIT ADDRESSING MODE
IEAVMXIT RMODE ANY                     31-BIT RESIDENCE
         SPACE 1
***********************************************************************
*                                                                     *
*         REGISTER ASSIGNMENTS                                        *
*                                                                     *
***********************************************************************
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
CTXTPTR  EQU   5
REG11    EQU   11
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
SPINPRVT EQU   230
         EJECT
***********************************************************************
*                                                                     *
*        STANDARD ENTRY LINKAGE                                       *
*                                                                     *
***********************************************************************
         STM   REG14,REG12,12(REG13)    SAVE CALLER'S REGISTERS
         BALR  REG12,REG0               ESTABLISH MODULE BASE
         USING *,REG12                  REGISTER
         L     CTXTPTR,0(REG1)          ESTABLISH ADDRESSABILITY
         USING CTXT,CTXTPTR             TO THE CTXT
***********************************************************************
*                                                                     *
*        DYNAMIC STORAGE FOR THIS MODULE IS BEING OBTAINED BELOW      *
*        THE 16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR PARAMETER   *
*        LIST TO BE IN 24-BIT ADDRESSABLE STORAGE.                    *
*                                                                     *
***********************************************************************
         GETMAIN  RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW  OBTAIN DYNAMIC  X
                                                       STORAGE
         LR    REG11,REG1               ADDRESS RETURNED IN REG1
         USING DATAAREA,REG11           ADDRESSABILITY TO DYNAMIC      X
                                        STORAGE
         ST    REG13,SAVEAREA+4         SET BACKWARD PTR
         LA    REG15,SAVEAREA           GET ADDRESS OF SAVE AREA
         ST    REG15,8(REG13)           SET FORWARD PTR
         LR    REG13,REG15              SET REG13 TO POINT TO          X
                                        STANDARD SAVE AREA
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE WHICH MESSAGE IS TO BE PROCESSED.                  *
*                                                                     *
***********************************************************************
         SPACE 1
         L     REG2,CTXTTXPJ            ESTABLISH ADDRESSABILITY
         USING CTXTATTR,REG2            TO THE MSG ATTRIBUTES
         LA    REG4,CTXTTMSG            ADDRESS OF TEXT AREA
         USING MSGTEXT,REG4             BASE TEXT MAPPING
         CLC   MSGID,IFB010D            MESSAGE IFB010D?
         BE    MIFB010D
         CLC   MSGID,ILR022A            MESSAGE ILR022A?
         BE    MILR022A
         CLC   MSGID,$HASP426           MESSAGE $HASP426?
         BE    MHASP426
         CLC   MSGID,ERB306D            MESSAGE ERB306D?
         BE    MERB306D
         CLC   MSGID,$HASP400           MESSAGE $HASP400?
         BE    MHASP400
         CLC   MSGID,IST020I            MESSAGE IST020I?
         BE    MIST020I
         B     FINISHED
***********************************************************************
*                                                                     *
*        REPLYING GO TO A MESSAGE                                     *
*                                                                     *
***********************************************************************
MERB306D EQU   *
         OI    CTXTRFB2,CTXTRHCO        SUPPRESS THE MESSAGE
         BAL   REG14,ISSUGO             REPLY 'R XX,GO'
         BAL   REG14,ISSUWGO            ISSUE THE MESSAGE
         B     FINISHED
***********************************************************************
*                                                                     *
*        REPLYING U TO A MESSAGE                                      *
*                                                                     *
***********************************************************************
MIFB010D EQU   *
MILR022A EQU   *
MHASP426 EQU   *
         OI    CTXTRFB2,CTXTRHCO        SUPPRESS THE MESSAGE
         BAL   REG14,ISSUU              REPLY 'R XX,U'
         BAL   REG14,ISSUWU             ISSUE THE MESSAGE
         B     FINISHED
***********************************************************************
*                                                                     *
*        REPLYING $S FOR ENTER REQUEST ($HASP400) *
*                                                                     *
***********************************************************************
MHASP400 EQU   *
         OI    CTXTRFB2,CTXTRHCO        SUPPRESS THE MESSAGE
         BAL   REG14,ISSU400            REPLY '$ S'
         BAL   REG14,ISSUW400           ISSUE THE MESSAGE
         B     FINISHED
***********************************************************************
*                                                                     *
*        REPLYING S TSO AFTER NCP INIT (IST020I)  *
*                                                                     *
***********************************************************************
MIST020I EQU   *
         OI    CTXTRFB2,CTXTRHCO        SUPPRESS THE MESSAGE
         BAL   REG14,ISSU020            REPLY 'S TSO'
         BAL   REG14,ISSUW020           ISSUE THE MESSAGE
         B     FINISHED
***********************************************************************
*                                                                     *
*        STANDARD EXIT LINKAGE                                        *
*                                                                     *
***********************************************************************
FINISHED EQU   *
         L     REG13,4(REG13)           RESTORE REG 13
         FREEMAIN  RU,LV=DATAEND,A=(REG11),SP=SPINPRVT   FREE STORAGE
         LM    REG14,REG12,12(REG13)    RESTORE CALLER'S               X
                                        REGISTERS
         BR    REG14                    RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*   PROCEDURE - ISSUMGCR                                              *
*                                                                     *
*   FUNCTION  - REPLIES TO MESSAGES                                   *
*                                                                     *
*   INPUT     - NONE                                                  *
*                                                                     *
*   OUTPUT    - A REPLY COMMAND IS ISSUED VIA SVC 34                  *
*                                                                     *
*   NOTES     - THE MGCR MACRO MAY BE USED TO ISSUE A REPLY COMMAND   *
*                                                                     *
***********************************************************************
         SPACE 1
ISSUGO EQU     *
         XC    MGCRPL(MGCRLTH),MGCRPL   CLEAR THE PARAMETER LIST
         MVC   MGCRTEXT(L'TXTINGO),TXTINGO     MOVE IN THE REPLY BUFFER
         MVC   REPLY,CTXTRPID           INSERT THE REPLY ID
         LA    REG1,(MGCRTEXT-MGCRPL)+L'TXTINGO       GET MGCRPL LENGTH
         STC   REG1,MGCRLGTH            SAVE LENGTH IN THE MGCRPL
         SR    REG0,REG0                CLEAR REGISTER ZERO
         MGCR  MGCRPL                   ISSUE THE COMMAND
         BR    REG14                    RETURN TO CALLER
         SPACE 1
ISSUU EQU      *
         XC    MGCRPL(MGCRLTH),MGCRPL   CLEAR THE PARAMETER LIST
         MVC   MGCRTEXT(L'TXTINU),TXTINU       MOVE IN THE REPLY BUFFER
         MVC   REPLY,CTXTRPID           INSERT THE REPLY ID
         LA    REG1,(MGCRTEXT-MGCRPL)+L'TXTINU        GET MGCRPL LENGTH
         STC   REG1,MGCRLGTH            SAVE LENGTH IN THE MGCRPL
         SR    REG0,REG0                CLEAR REGISTER ZERO
         MGCR  MGCRPL                   ISSUE THE COMMAND
         BR    REG14                    RETURN TO CALLER
ISSU400  EQU   *
         XC    MGCRPL(MGCRLTH),MGCRPL   CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'           SET FLAG
         MVC   MGCRTEXT(L'TXTIN400),TXTIN400   MOVE IN THE REPLY BUFFER
         LA    REG1,(MGCRTEXT-MGCRPL)+L'TXTIN400      GET MGCRPL LENGTH
         STC   REG1,MGCRLGTH            SAVE LENGTH IN THE MGCRPL
         SR    REG0,REG0                CLEAR REGISTER ZERO
         MGCR  MGCRPL                   ISSUE THE COMMAND
         BR    REG14                    RETURN TO CALLER
ISSU020  EQU   *
         XC    MGCRPL(MGCRLTH),MGCRPL   CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'           SET FLAG
         MVC   MGCRTEXT(L'TXTIN020),TXTIN020   MOVE IN THE REPLY BUFFER
         LA    REG1,(MGCRTEXT-MGCRPL)+L'TXTIN020      GET MGCRPL LENGTH
         STC   REG1,MGCRLGTH            SAVE LENGTH IN THE MGCRPL
         SR    REG0,REG0                CLEAR REGISTER ZERO
         MGCR  MGCRPL                   ISSUE THE COMMAND
         BR    REG14                    RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*   PROCEDURE - ISSUWTO                                               *
*                                                                     *
*   FUNCTION  - ISSUES A MESSAGE                                      *
*                                                                     *
*   INPUT     - NONE                                                  *
*                                                                     *
***********************************************************************
         SPACE 1
ISSUWU  EQU    *
         L     REG2,CTXTTXPJ            ESTABLISH ADDRESSABILITY
         USING CTXTATTR,REG2            TO MESSAGE ATTRIBUTES
         LA    REG4,CTXTTMSG            ADDRESS OF TEXT AREA
         USING MSGTEXT,REG4             BASE TEXT MAPPING
         MVC   USERAUTO,USERU           MOVE WTO LIST FORM FROM        X
                                        STATIC TO DYNAMIC
         DROP  REG2,REG4                DROP ADDRESSABILITY TO         X
                                        THE MESSAGE ATTRIBUTES
         WTO   MF=(E,USERAUTO)          ISSUE THE MESSAGE
         BR    REG14                    RETURN TO CALLER
ISSUWGO  EQU   *
         L     REG2,CTXTTXPJ            ESTABLISH ADDRESSABILITY
         USING CTXTATTR,REG2            TO MESSAGE ATTRIBUTES
         LA    REG4,CTXTTMSG            ADDRESS OF TEXT AREA
         USING MSGTEXT,REG4             BASE TEXT MAPPING
         MVC   USERAUTO,USERGO          MOVE WTO LIST FORM FROM        X
                                        STATIC TO DYNAMIC
         DROP  REG2,REG4                DROP ADDRESSABILITY TO         X
                                        THE MESSAGE ATTRIBUTES
         WTO   MF=(E,USERAUTO)          ISSUE THE MESSAGE
         BR    REG14                    RETURN TO CALLER
ISSUW400 EQU   *
         L     REG2,CTXTTXPJ            ESTABLISH ADDRESSABILITY
         USING CTXTATTR,REG2            TO MESSAGE ATTRIBUTES
         LA    REG4,CTXTTMSG            ADDRESS OF TEXT AREA
         USING MSGTEXT,REG4             BASE TEXT MAPPING
         MVC   USERAUTO,USER400         MOVE WTO LIST FORM FROM        X
                                        STATIC TO DYNAMIC
         DROP  REG2,REG4                DROP ADDRESSABILITY TO         X
                                        THE MESSAGE ATTRIBUTES
         WTO   MF=(E,USERAUTO)          ISSUE THE MESSAGE
         BR    REG14                    RETURN TO CALLER
ISSUW020 EQU   *
         L     REG2,CTXTTXPJ            ESTABLISH ADDRESSABILITY
         USING CTXTATTR,REG2            TO MESSAGE ATTRIBUTES
         LA    REG4,CTXTTMSG            ADDRESS OF TEXT AREA
         USING MSGTEXT,REG4             BASE TEXT MAPPING
         MVC   USERAUTO,USER020         MOVE WTO LIST FORM FROM        X
                                        STATIC TO DYNAMIC
         DROP  REG2,REG4                DROP ADDRESSABILITY TO         X
                                        THE MESSAGE ATTRIBUTES
         WTO   MF=(E,USERAUTO)          ISSUE THE MESSAGE
         BR    REG14                    RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        CHARACTER CONSTANTS                                          *
*                                                                     *
***********************************************************************
$HASP426 DC    CL8'$HASP426 '           MESSAGE $HASP426
$HASP400 DC    CL8'$HASP400 '           MESSAGE $HASP400
IST020I  DC    CL8'IST020I '            MESSAGE IST020I
ERB306D  DC    CL8'ERB306D '            MESSAGE ERB306D
IFB010D  DC    CL8'IFB010D '            MESSAGE IFB010D
ILR022A  DC    CL8'ILR022A '            MESSAGE ILR022A
TXTINGO  DC    CL11'REPLY XX,GO'        REPLY GO
TXTINU   DC    CL10'REPLY XX,U'         REPLY U
TXTIN400 DC    CL02'$S'                 REPLY $S
TXTIN020 DC    CL05'S TSO'              REPLY S TSO
         SPACE 1
***********************************************************************
*                                                                     *
*        LIST FORM OF WTO MACRO (STATIC)                              *
*  NOTE: ALL LIST FORMS MUST HAVE THE SAME LENGTH !!!                 *
*        ---                                                          *
***********************************************************************
USERGO  WTO 'USER01I ANSWERED WITH GO                         ',       X
               ROUTCDE=(02),DESC=(4),MF=L
LWTOLIST EQU   *-USERGO
USERU   WTO 'USER02I ANSWERED WITH U                          ',       X
               ROUTCDE=(02),DESC=(4),MF=L
USER400 WTO 'USER03I ANSWERED WITH $ S                        ',       X
               ROUTCDE=(02),DESC=(4),MF=L
USER020 WTO 'USER04I ANSWERED WITH S TSO                      ',       X
               ROUTCDE=(02),DESC=(4),MF=L
         EJECT
***********************************************************************
*                                                                     *
*        STORAGE DEFINITIONS                                          *
*                                                                     *
***********************************************************************
DATAAREA DSECT
         DS    0F
SAVEAREA DS    18F                      STANDARD SAVE AREA
         DS    0F
USERAUTO DS    CL(LWTOLIST)             DYNAMIC FORM OF WTO
         EJECT
MGCR     IEZMGCR  DSECT=NO
         ORG   MGCRTEXT
COMMAND  DS    CL6                      THE REPLY COMMAND
REPLY    DS    CL2                      REPLY ID
REPLYMSG DS    CL3                      WTOR RESPONSE
         ORG
DATAEND  EQU   *-DATAAREA
         SPACE 2
***********************************************************************
*                                                                     *
*         MAPPING OF THE MESSAGE TEXT                                 *
*                                                                     *
***********************************************************************
MSGTEXT  DSECT
MSGID    DS    CL8                      MESSAGE ID
JOBDATA  DS    CL8                      JOB DATA (ID AND NAME)
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
         EJECT
         IEZVX100
         EJECT
         END   IEAVMXIT
