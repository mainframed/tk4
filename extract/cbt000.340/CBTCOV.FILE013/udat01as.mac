         TITLE 'SPTSO01 - TSOMCP-EXIT'
         GBLA  &KEYLEN,&BUFSIZE
         GBLC  &QUALIFY
         SPACE 2
&KEYLEN  SETA  XXX            ANGABEN SIND DEM MCP
&BUFSIZE SETA  XXXX           ZU ENTNEHMEN !
&QUALIFY SETC  'TSL'          FREI WAEHLBAR 1-8 STELLEN
         SPACE 5
         DMANF PGM=SPTSO01,                                            X
               BASREG=(12,11)
         SPACE 3
         XC    RETCODE(12),RETCODE
         NI    S,255-16-8
         CLI   1(R14),1
         BE    A01A
         LH    2,18(1)
         LA    5,45(1)
         SH    2,=H'33'
         BNP   PFKFUNKT
A00      EQU   *
         SH    R2,=H'4'
         BM    A01
         EX    R2,SCANTRT
         BE    A01
         EJECT
         NOP   A00B
         OI    *-3,240
         L     R15,16                  CVT
         SH    R15,=H'4'
         CLC   0(2,R15),=C'03'
         BNE   DISABLE
         MVI   0(R13),C'T'
TSOSTART DMPRINT
         L     R1,=V(GMTSR15A)
         ST    R1,GMTSR15
A00B     EQU   *
         TM    0(R1),X'A2'
         BO    A00X
         TM    0(R1),X'93'
         BO    A00A
         B     A01
A00X     EQU   *
         MVC   ZD,0(R1)
         OC    ZD,BLANKS
         CLC   ZD(3),=C'SPF'
         BNE   A01
         OI    S,8
         LA    R3,=C'AUS '
         B     LOGR1
A00A     EQU   *
         MVC   ZD,0(R1)
         OC    ZD,BLANKS
         CLC   ZD(2),=C'L '
         BE    A00Z
         CLC   ZD(4),=C'LIST'
         BE    A00Z
         CLC   ZD(3),=C'LOG'
         BNE   A01
         ST    R1,SCANSAVE
         LA    R2,3(R5)
         SR    R1,R2
         STH   R1,SCANLEN
         CLC   ZD(4),=C'LOG '
         BE    LOGR
         CLC   ZD(6),=C'LOGOFF'
         BE    LOGOFF
         CLC   ZD(5),=C'LOGON'
         BE    LOGON
A00Z     EQU   *
         OI    S,16                    LIST-COMMAND
         EJECT
A01      EQU   *
         L     R1,4(R13)
         L     R1,24(R1)
A01A     MVC   LCBADR,13(1)
         LA    R14,RETURNX
         BAL   R10,SUCHTAB
A02      EQU   *
         TM    3(4),64
         BO    LOG1
         TM    3(R4),2
         BO    SPFEND
RETURN   EQU   *
         LA    R0,240                  ID
EVERDUMP NOP   DUMP
S1       NOP   LOGEND1
S2       NOP   SESSCANC
S3       NOP   STPRINT
RETURNX  DMEND RC=RETCODE
         SPACE 5
ABEND    EQU   *
TSOABEND DMPRINT REGS,LCBTAB(10*20)
         LR    R2,R12
         L     R3,=A(SHIFTBER)
         LA    R3,&BUFSIZE.(R3)
TSOABND2 DMPRINT (2)
         XC    RETCODE(12),RETCODE
DISABLE  MVC   SPTSO01(4),IEFBR14
         B     RETURNX
IEFBR14  SR    15,15
         BR    14
         EJECT
LOGR     EQU   *
         BAL   R14,EDITPARM
LOGR1    EQU   *
         L     R1,4(R13)
         L     R1,24(R1)
         MVC   MHPUT(6),=X'0000000F0000'
         MVC   MHPUTC,42(R1)
         MVC   MHPUTD+2(4),LOG
         MVI   MHPUTD,64
         L     R15,16
         SH    R15,=H'4'
         CLC   0(2,R15),=C'03'
         BNE   *+8
         MVI   MHPUTD,C'%'
         CLC   0(4,R3),=C'EIN '
         BE    LOGEIN
         CLC   0(3,R3),=C'ON '
         BE    LOGEIN
         CLC   0(2,R3),=C'S '
         BE    STATUS
         MVC   LCBADR,13(R1)
         LA    R14,A01
         BAL   R10,SUCHTAB
         TM    3(R4),64
         BZ    LOGR2
         TM    3(R4),128
         BNO   A01
         CLC   0(4,R3),=C'AUS '
         BE    LOGAUS
         CLC   0(4,R3),=C'OFF '
         BE    LOGAUS
LOGR2    EQU   *
         CLC   0(6,R3),=C'PRINT '
         BE    LOGPRINT
         CLC   0(4,R3),=C'DEL '
         BE    LOGDEL
         CLC   0(5,R3),=C'EDIT '
         BE    LOGEDIT
         CLC   0(5,R3),=C'DUMP '
         BE    LOGDUMP
         CLC   0(5,R3),=C'TIME '
         BE    LOGTIME
         B     A01
         SPACE 3
LOGEIN   EQU   *
         MVC   LCBADR,13(R1)
         LA    R14,LOGE1
         BAL   R10,SUCHTAB
         TM    3(R4),64
         BO    A01
         TM    3(R4),32
         BZ    LOGEIN0
         LA    R0,*
         B     ABEND
         SPACE 3
LOGE1    EQU   *
         LA    R5,LCBTAB
         LH    R6,LCBANZ
         LA    R4,ZD
         CH    R6,LCBANZ1
         BNL   TOOMUCH
         LR    R3,R1
         GETMAIN EC,LV=6520,A=ZF
         LTR   R15,R15
         BNZ   TOOMUCH
         FREEMAIN E,LV=6520,A=ZF
         LR    R1,R3
         LR    R4,R6
         LA    R6,1(R6)
         STH   R6,LCBANZ
         MH    R4,=H'20'
         LA    R4,0(R5,R4)
         XC    0(20,R4),0(R4)
         MVC   0(3,R4),13(R1)
         BAL   R9,CREATDCB
         BAL   R10,SORTTAB
         LA    R0,*
         LA    R14,ABEND
         BAL   R10,SUCHTAB
         OI    3(R4),32
         SPACE 2
LOGEIN0  EQU   *
         OI    3(R4),64
         MVC   AUSG(2),=H'44'
         MVC   AUSG+4(40),=CL40'1***LOG*** BEGIN '
         BAL   R10,DATUM
         MVC   AUSG+22(22),DATETIME
         BAL   R10,PUTR
         MVI   AUSG+4,C'-'
         MVI   MHPUTD+1,C'V'
         TM    S,64
         BZ    LOGEIN01
         NI    S,255-64
         MVI   MHPUTD+1,C'X'
LOGEIN01 EQU   *
         MVC   MHPUTD+6(3),8(R4)
         SPACE 2
LOGEXIT  LA    R4,MHPUT
         ST    R4,RC2
         LA    R4,RC1
         ST    R4,RETCODE
         B     A01
         SPACE 3
LOGAUS   EQU   *
         TM    3(R4),64
         BZ    A01
         BAL   R8,LOGEND
         NI    3(R4),255-64
         BAL   R9,LOGENDMS
         MVI   MHPUTD+1,C'Y'
         MVI   MHPUT+3,11
         TM    S,8
         BZ    LOGEXIT
         OI    3(R4),2                 SPF-BIT
         B     A01
         SPACE 2
LOGEDIT  EQU   *
         OI    S,32
         B     LOGDEL
LOGPRINT EQU   *
         OI    S,128
LOGDEL   EQU   *
         TM    3(R4),32
         BO    A01
         BAL   R8,LOGEND
         L     R5,4(R4)
         BAL   R9,LOGENDMS
         CLOSE ((R5))
         FREEPOOL ((R5))
         MVC   MHPUTD+6(3),8(R4)
         TM    S,128
         BZ    LOGEXT1
         MVI   MHPUTD+1,C'Z'
         NI    S,255-128
         B     LOGEXT2
LOGEXT1  EQU   *
         TM    S,32
         BZ    LOGEXT1A
         MVI   MHPUTD+1,C'S'
         NI    S,255-32
         B     LOGEXT2
LOGEXT1A EQU   *
         MVI   MHPUTD+1,C'W'
LOGEXT2  EQU   *
         BAL   R9,CLEARDCB
         B     LOGEXIT
         SPACE 2
LOGEND   EQU   *
         OI    S1+1,240
         ST    R8,SAVER8
         B     A02
LOGEND1  EQU   *
         NI    S1+1,15
         L     R8,SAVER8
         BR    R8
         SPACE 2
LOGDUMP  EQU   *
         MVI   MHPUTD+1,C'T'
         MVI   MHPUT+3,11
         TM    3(R4),1
         BO    LOGDUMP1
         OI    3(R4),1
         B     LOGEXIT
LOGDUMP1 EQU   *
         NI    3(R4),255-1
         B     LOGEXIT
         SPACE 2
LOGTIME  EQU   *
         MVI   MHPUTD+1,C'T'
         MVI   MHPUT+3,11
         TM    17(R4),128
         BO    LOGTIME1
         OI    17(R4),128
         B     LOGEXIT
LOGTIME1 EQU   *
         NI    17(R4),255-128
         B     LOGEXIT
         EJECT
LOGON    EQU   *
LOGOFF   EQU   *
         L     R1,4(R13)
         L     R1,24(R1)
         MVC   LCBADR,13(R1)
         LA    R14,RETURN
         BAL   R10,SUCHTAB
         TM    3(R4),32
         BO    LOGONOFF
         MVC   MHPUT(6),=X'0000000C0000'
         MVC   MHPUTC,42(R1)
         MVC   MHPUTD(6),=C'%ULOG '
         B     LOGEXIT
LOGONOFF EQU   *
         BAL   R9,CLEARDCB
         B     RETURN
         EJECT
CREATDCB EQU   *
         LH    R3,DDTABANZ
         LTR   R3,R3
         BZ    CLEARX
         BCTR  R3,0
         LA    R5,DDTAB(R3)
         L     R15,=A(LOGDCB)
         MVC   47(1,R15),0(R5)
         STH   R3,DDTABANZ
         GETMAIN R,LV=280
         ST    R1,4(R4)
         L     R15,=A(LOGDCB)
         MVC   4(1,R4),47(R15)
         MVC   0(96,R1),0(R15)
NEWQUAL  EQU   *
         L     R15,=A(LOGDCB)
         MVC   ZF(1),47(R15)
         NI    ZF,15
         TIME
         ST    R0,ZD
         SR    R1,R1
         IC    R1,ZD
         NI    ZD,0
         MH    R1,ZD
         MVC   ZD+1(1),ZD+2
         MH    R1,ZD
         LR    R2,R1
         SRDA  R2,32
         D     R2,=F'26'
         STC   R2,ZF+1
         NI    ZD+2,0
         MH    R1,ZD+2
         LR    R2,R1
         SRDA  R2,32
         D     R2,=F'26'
         STC   R2,ZF+2
         TR    ZF(3),TRANSL
         MVC   8(3,R4),ZF
         SPACE 5
         L     R3,4(R4)
         RDJFCB ((R3),(OUTPUT))
         SR    R6,R6
         EXTRACT ZF,FIELDS=TIOT
         L     R5,ZF
         LA    R5,24(R5)
NEXTDD   EQU   *
         CLC   40(8,R3),4(R5)
         BE    FINDUCB
         IC    R6,0(R5)
         LA    R5,0(R5,R6)
         CLC   0(4,R5),=A(0)
         BNE   NEXTDD
         LA    R0,*
         B     ABEND
FINDUCB  EQU   *
         L     R5,16(R5)
         MVC   UCBTYPE,16(R5)
         L     R15,=A(JFCB)
         MVC   VOLSER,118(R15)
         MVC   QUALIF(3),8(R4)
         XC    RETCOD,RETCOD
         CATALOG CATAL
         LTR   R15,R15
         BNZ   NEWQUAL
         CATALOG UNCAT
         LTR   R15,R15
         BNZ   NEWQUAL
         L     R15,=A(JFCB)
         MVC   104(176,R3),0(R15)
         LA    R5,104(R3)
         ST    R5,100(R3)
         MVI   100(R3),X'87'
         MVI   96(R3),X'11'
         MVC   97(3,R3),=AL3(DCBABEND)
         LA    R5,96(R3)
         ST    R5,ZF
         MVC   37(3,R3),ZF+1
         MVI   MHPUT+3,25
         MVC   VOLPARM+3(6),VOLSER
         MVC   MHPUTD+9(10),VOLPARM
         OI    S,64
         BR    R9
         EJECT
TOOMUCH  EQU   *
         BAL   R10,TMUCH
         B     A01
TMUCH    EQU   *
         LA    R2,FEHL1
         ST    R2,RC1
         MVI   MHPUT+3,11
         MVI   MHPUTD+1,C'R'
         LA    R2,MHPUT
         ST    R2,RC2
         LA    R2,RC1
         ST    R2,RETCODE
         BR    R10
         EJECT
SPFEND   EQU   *
         L     R15,4(R13)
         L     R15,12(R15)
         TM    1(R15),1
         BZ    RETURN
         CLC   48(5,R1),=C'READY'
         BNE   RETURN
         OI    3(R4),64
         MVC   AUSG(2),=H'43'
         MVC   AUSG+4(39),=CL39'-***LOG*** CONT '
         BAL   R10,DATUM
         MVC   AUSG+21(22),DATETIME
         BAL   R10,PUTR
         MVI   AUSG+4,C'-'
         B     A01
         EJECT
CLEARDCB EQU   *
         LH    R5,DDTABANZ
         LA    R6,DDTAB(R5)
         MVC   0(1,R6),4(R4)
         LA    R5,1(R5)
         STH   R5,DDTABANZ
         L     R1,4(R4)
         FREEMAIN R,LV=104,A=(1)
CLEAR1   EQU   *
         MVC   0(3,R4),=3X'FF'
         BAL   R10,SORTTAB
         LH    R4,LCBANZ
         BCTR  R4,0
         STH   R4,LCBANZ
         BR    R9
         SPACE 5
CLEARX   EQU   *
         BAL   R10,TMUCH
         BAL   R9,CLEAR1
         B     A01
         EJECT
LOGENDMS EQU   *
         MVC   AUSG(2),=H'42'
         MVC   AUSG+4(38),=CL38'-***LOG*** END '
         BAL   R10,DATUM
         MVC   AUSG+20(22),DATETIME
         TM    S,8
         BZ    LOGENDM1
         MVC   AUSG+15(3),=C'INT'
         BAL   R10,PUTR
         MVC   AUSG(2),=H'42'
         MVC   AUSG+4(38),=CL38'-LOG018I SPF IN PROCESS - LOG STOPPED'
LOGENDM1 EQU   *
         BAL   R10,PUTR
         BR    R9
         EJECT
SORTTAB  EQU   *
         CLC   LCBANZ,=H'1'
         BNH   SORTTABE
         DMTSORT LCBTAB,LCBANZ,20,FIELDS=(0,3,,A)
SORTTABE BR    R10
         EJECT
SUCHTAB  EQU   *
         LH    R4,LCBANZ
         LTR   R4,R4
         BCR   8,R14
         DMTSUCH LCBTAB,                                               X
               LCBANZ,                                                 X
               20,                                                     X
               LCBADR,                                                 X
               REG=4,                                                  X
               NFOUND=(R14)
         BR    R10
         EJECT
DATUM    EQU   *
         DMDAT DATUM=DATETIME,ZEIT=DATETIME+10
         BR    R10
         EJECT
EDITPARM EQU   *
         MVC   AUSG+4(256),BLANKS
         L     R1,4(R13)
         L     R1,24(R1)
         LH    R2,18(R1)
         SH    R2,=H'37'
         SH    R2,SCANLEN
         L     R3,SCANSAVE
         EX    R2,MOVEPARM
         EX    R2,OCINSTR
         OI    AUSG+4,64
         STH   R2,AUSG
         LR    R3,R2
         EX    R3,TRTPARM
         BE    A01
         LR    R4,R1
         LA    R5,AUSG+4
         AR    R3,R5
         SR    R3,R4
         LR    R1,R4
         EX    R3,TRTPARM1
         LR    R3,R1
         BR    R14
         EJECT
LOG1     EQU   *
         TM    3(R4),1
         BZ    *+8
         OI    EVERDUMP+1,240
         L     R14,4(R13)
         L     R14,12(R14)
         XC    SHIFTLEN,SHIFTLEN
         TM    1(14),1
         BO    OUTPUT
         LA    R5,45(R1)
         LH    3,18(1)
         SH    3,=H'33'
         BNP   STEUER
         NI    3(R4),255-128-16-4
         TM    S,16
         BZ    LOG1B
         OI    3(R4),4
         NI    S,255-16
LOG1B    EQU   *
         TM    0(5),X'11'
         BO    LOG1A
         CLI   0(5),2
         BE    STEUER
         B     DUMP
LOG1A    EQU   *
         LA    R3,2(R3)                 WG/RDW + ASA
         STH   R3,AUSG
         SH    R3,=H'6'
         EX    3,MOVE
         EX    3,OCINSTR
         BAL   10,PUTR
         CLC   AUSG+5(2),=C'H '
         BE    HELPBIT
         CLC   AUSG+5(5),=C'HELP '
         BNE   RETURN
HELPBIT  OI    3(R4),16
         MVC   AUSG(HELPEND-HELPMSG),HELPMSG
         BAL   R10,PUTR
         B     RETURN
         EJECT
OUTPUT   EQU   *
         NI    3(R4),255-128
         TM    3(R4),16
         BO    RETURN
         CLC   18(R1),=H'0'
         BE    RETURN
         LH    R8,18(R1)
         LA    R3,42(R1)
         LA    R2,&KEYLEN
         CH    R2,18(R1)
         BNL   BUFF1
         BAL   R14,SHIFT
         LH    R8,SHIFTLEN
         B     BUFF2
BUFF1    EQU   *
         SH    R8,=H'30'
BUFF2    EQU   *
         MVC   AUSG+5(3),BLANKS
         CLI   0(R3),X'C1'
         BNE   SESSCAN
         LA    R3,1(R3)
         BCTR  R8,0
         CLI   3(R3),X'1D'
         BE    SBA
         LA    R3,1(R3)
         BCTR  R8,0
         CLC   11(17,R3),=C'SESSION CANCELLED'
         BNE   FIELD
         BCTR  R3,0
         LA    R8,1(R8)
         OI    S2+1,240
         B     SF
SBA      EQU   *
         SR    R0,R0
         CLI   0(R3),X'11'
         BNE   DUMP
         LA    R3,3(R3)
         SH    R8,=H'3'
         CLI   0(R3),X'13'
         BE    READYBIT
SF       EQU   *
         CLI   0(R3),X'1D'
         BNE   DUMP
         CLI   1(R3),X'40'
         BNE   FIELD
         LA    R3,2(R3)
         BCTR  R8,0
         BCTR  R8,0
         B     SBA
READYBIT EQU   *
         NI    FIELDEND+1,15
         CLC   AUSG+8(6),=C'READY '
         BNE   RETURN
         OI    3(R4),128
         B     RETURN
SESSCAN  EQU   *
         CLC   18(R1),=H'30'
         BNH   RETURN
         CLI   0(R3),X'C3'
         BNE   DUMP
* SESSION CANCELLED
         OI    S2+1,240
         LA    R3,1(R3)
         BCTR  R8,0
         B     SBA
SESSCANC EQU   *
         L     R5,4(R4)
         BAL   R9,LOGENDMS
         CLOSE ((R5))
         FREEPOOL ((R5))
         MVC   FEHL6+42(3),8(R4)
         LA    R15,FEHL6
         ST    R15,RC1
         LA    R15,RC1
         ST    R15,RETCODE
         BAL   R9,CLEARDCB
         NI    S2+1,15
         B     RETURN
         SPACE 2
FIELD    EQU   *
         SR    R2,R2
         LA    R0,255
         CR    R8,R0
         BL    FIELDTRT
         LR    R15,R8
         LR    R8,R0
FIELDTRT EQU   *
         EX    R8,TRTEXEC
         BE    DUMP
         B     *+4(R2)
         B     DUMP
         B     FIELD11
FIELDX   EQU   *
         LA    R1,0(R1)
         LR    R2,R1
         SR    R1,R3
         CR    R8,R0
         BNE   FIELDX1
         LR    R8,R15
FIELDX1  EQU   *
         SR    R8,R1
         BP    FIELDX2
         AR    R1,R8
         OI    FIELDEND+1,240
FIELDX2  EQU   *
         SH    R1,=H'3'
         BM    LEER
         EX    R1,MOVEOUT
         EX    R1,OCINOUT
         LA    R1,9(R1)
         STH   R1,AUSG
FIELDEX  NOP   FIELD11E
         LR    R3,R2
         BAL   R10,PUTR
FIELDEND NOP   READYBIT
         B     SF
LEER     EQU   *
         MVC   AUSG(2),=H'6'
         B     FIELDEX
FIELD11  EQU   *
         CLI   3(R1),X'13'
         BNE   DUMP
         OI    FIELDEX+1,240
         B     FIELDX
FIELD11E EQU   *
         NI    FIELDEX+1,15
         LR    R3,R2
         B     SBA
         EJECT
SHIFT    EQU   *
         ST    R14,SAVE14
         LA    R14,SHIFTTAB
         ST    R1,0(R14)
         LA    R14,4(R14)
         LH    R3,18(R1)
         LA    R15,&BUFSIZE
         CR    R3,R15
         BH    DUMP
         L     R15,=A(SHIFTBER)
         MVC   0(&KEYLEN-30,R15),42(R1)
         LA    R15,&KEYLEN-30(R15)
         L     R1,8(R1)
         ST    R1,0(R14)
         LA    R14,4(R14)
         LA    R2,12(R1)
         SH    R3,=H'&KEYLEN'
SHIFT1   EQU   *
         CH    R3,=H'&KEYLEN'
         BNH   SHIFT2
         MVC   0(&KEYLEN,R15),0(R2)
         LA    R15,&KEYLEN.(R15)
         L     R1,8(R1)
         CLC   4(4,R14),=F'-1'
         BE    SHIFT1A
         ST    R1,0(R14)
         LA    R14,4(R14)
SHIFT1A  EQU   *
         LA    R2,12(R1)
         SH    R3,=H'&KEYLEN'
         B     SHIFT1
SHIFT2   EQU   *
         BCTR  R3,0
         EX    R3,SHIFTMVC
         LA    R15,1(R3,R15)
         L     R3,=A(SHIFTBER)
         SR    R15,R3
         STH   R15,SHIFTLEN
         SR    R15,R15
         ST    R15,0(R14)
         L     R14,SAVE14
         BR    R14
         EJECT
DUMP     EQU   *
         NI    EVERDUMP+1,15
         STM   R0,R15,SAVEAREA
         CH    R0,=H'255'
         BE    ENDBUFF
DUMP0    EQU   *
         MVC   AUSG(2),=H'32'
         MVC   AUSG+4(28),=C'-LOG015I DUMP FORMAT PRINTED'
         BAL   10,PUTR
         CLC   SHIFTLEN,=H'0'
         BNE   DUMP1
         L     R6,4(R13)
         L     R6,24(R6)
         ST    R6,SHIFTTAB
         SR    R7,R7
         ST    R7,SHIFTTAB+4
DUMP1    EQU   *
         ZAP   ZAE,=P'0'
         LA    R6,SHIFTTAB-4
         MVC   TLAENG,=H'64'
         MVC   ZNAME(16),=C'REGISTERS 0 - 15'
         B     DUMP2
DUMP1A   EQU   *
         OI    ZAE+1,15
         UNPK  ZAEHL,ZAE
DUMP2    EQU   *
         L     R7,0(R6)
         LTR   R7,R7
         BZ    DUMP4
         ST    R6,DUMP2A
         L     R14,=A(SAVETSR)
         ST    4,0(R14)
         MVC   AUSG(2),=H'137'
         ZAP   ZZZ,=P'0'
         ZAP   ZBZ,=P'0'
         L     R15,GMTSR15
         B     DUMP3
DUMP2A   DS    A
DUMP3    LA    R1,TSR15PRM
         BALR  R14,R15
         MVC   TLAENG,=AL2(12+&KEYLEN.)
         MVC   ZNAME,=C'TCAM-BUFFER '
         LA    R6,4(R6)
         AP    ZAE,=P'1'
         B     DUMP1A
DUMP4    EQU   *
         MVI   AUSG+4,C'-'
         CLC   SHIFTLEN,=H'0'
         BE    RETURN
         LA    R6,SHIFTTAB
         L     R7,=A(SHIFTBER)
         ST    R7,0(R6)
         SR    R7,R7
         ST    R7,4(R6)
         MVC   TLAENG,SHIFTLEN
         XC    SHIFTLEN,SHIFTLEN
         MVC   ZNAME(16),=C'SHIFT - AREA    '
         B     DUMP2
         EJECT
ENDBUFF  EQU   *
         L     R0,=A(SHIFTBER)
         CR    R3,R0
         BL    TCAMBUFF
         LR    R1,R0
         LA    R0,&BUFSIZE.(R1)
         CR    R3,R0
         BH    TCAMBUFF
         AH    R1,SHIFTLEN
         SR    R1,R3
         B     MOVEBUFF
TCAMBUFF EQU   *
         L     R1,4(R13)
         L     R1,24(R1)
         LH    R0,18(R1)
         LA    R1,12(R1)
         AR    R1,R0
         SR    R1,R3
MOVEBUFF EQU   *
         SH    R1,=H'3'
         BM    DUMP0
         EX    R1,MOVEOUT
         EX    R1,OCINOUT
         LA    R1,9(R1)
         STH   R1,AUSG
         BAL   R10,PUTR
         B     RETURN
         EJECT
STEUER   EQU   *
         LA    6,STTAB
         SH    5,=H'3'
STKZ1    EQU   *
         CLC   0(1,5),0(6)
         BE    STKZ2
         CLI   0(6),255
         BE    HEXSTKZ
         LA    6,10(6)
         B     STKZ1
STKZ2    EQU   *
         CLI   0(R6),X'7D'
         BE    STKZ2A
         CLI   0(R6),X'6D'
         BE    STKZ2A
         NI    3(R4),255-128-16-4
         B     STKZ2B
STKZ2A   EQU   *
         TM    3(R4),16+4
         BNZ   RETURN
STKZ2B   EQU   *
         MVC   AUSG+5(9),1(6)
STKZ3    EQU   *
         MVC   AUSG(2),=H'14'
         BAL   10,PUTR
         B     RETURN
HEXSTKZ  MVC   AUSG+5(9),1(6)
         UNPK  ZF(3),0(2,5)
         TR    ZF(2),TRANS-240
         MVC   AUSG+9(2),ZF
         B     STKZ3
         EJECT
PUTR     EQU   *
         L     1,4(4)
         LA    0,AUSG
         LH    2,AUSG
         TM    17(R4),128
         BO    PUTTIME
PUTR0    EQU   *
         SH    2,=H'138'
         BNM   PUTR2
PUTR1    L     15,48(1)
         TM    3(R4),32
         BO    PUTR3
         BALR  14,15
PUTR1A   EQU   *
         LH    R14,18(R4)
         LA    R14,1(R14)
         CH    R14,=H'1024'
         BNL   PUTR1B
         STH   R14,18(R4)
         B     PUTR1C
PUTR1B   EQU   *
         SH    R14,=H'1024'
         STH   R14,18(R4)
         L     R1,4(R4)
         BAL   R14,DCBEOVX
PUTR1C   EQU   *
         MVI   AUSG+4,64
         XC    AUSG(4),AUSG
         BR    10
PUTR2    MVC   AUSG(2),=H'137'
         B     PUTR1
PUTR3    EQU   *
         CLI   11(R4),5
         BNL   PUTR5
         TR    11(1,R4),TRANS00
         CLI   11(R4),1
         BH    PUTR4
         GETMAIN R,LV=137*5
         ST    R1,12(R4)
PUTR4    EQU   *
         SR    R2,R2
         IC    R2,11(R4)
         BCTR  R2,0
         MH    R2,=H'137'
         L     R1,12(R4)
         LA    R2,0(R1,R2)
         MVC   0(137,R2),AUSG
         B     PUTR1A
PUTR5    EQU   *
         L     R2,4(R4)
         MVI   104(R2),64
         MVC   105(43,R2),104(R2)
         MVC   104(L'DSNAME,R2),DSNAME
         MVC   104+L'DSNAME(3,R2),8(R4)
         OPEN ((R2),(OUTPUT)),TYPE=J
         TM    48(R2),16
         BZ    OPENFEHL
         NI    3(R4),255-32
         LA    R1,104(R2)
         FREEMAIN R,LV=176,A=(1)
         LR    R1,R2
         MVI   100(R1),X'06'
         MVC   101(3,R1),=AL3(DCBEOVX)
         L     R2,12(R4)
         ST    R3,SAVE
         LR    R3,R1
         PUT   (R3),(R2)
         AH    R2,=H'137'
         PUT   (R3),(R2)
         AH    R2,=H'137'
         PUT   (R3),(R2)
         AH    R2,=H'137'
         PUT   (R3),(R2)
         AH    R2,=H'137'
         PUT   (R3),(R2)
         LA    R2,AUSG
         PUT   (R3),(R2)
         L     R1,12(R4)
         FREEMAIN R,LV=137*5,A=(1)
         XC    11(5,R4),11(R4)
         LA    R14,6
         STH   R14,18(R4)
         L     R3,SAVE
         B     PUTR1A
         SPACE 2
OPENFEHL EQU   *
         LA    R15,FEHL5
         ST    R15,RC1
         LA    R15,RC1
         ST    R15,RETCODE
         B     RETURN
         SPACE 2
PUTTIME  EQU   *
         MVC   TAUSG1,AUSG
         LH    R1,AUSG
         LA    R1,12(R1)
         STH   R1,TAUSG1
         MVC   AUSG+1(4),BLANKS
         TIME
         STCM  R0,7,ZF
         UNPK  ZTIME,ZF
         MVC   TAUSGT,ZTSCHAB
         TR    TAUSGT,ZTIME
         LA    R0,TAUSG
         LH    R2,TAUSG
         L     R1,4(R4)
         B     PUTR0
         EJECT
DCBABEND EQU   *
         CLI   0(R13),C'T'
         BE    DCBAB1
         L     R13,4(R13)
         LM    R2,R12,28(R13)
         BR    R15
DCBAB1   EQU   *
         MVC   SAVEDCBA(16),0(R1)
         TM    3(R1),4
         BZ    DCBAE
         MVI   3(R1),0
         UNPK  ZD(7),0(4,R1)
         TR    ZD(6),TRANS-240
         MVI   ZD+3,C'-'
         LA    R2,FEHL2
         MVC   27(6,R2),ZD
         ST    R2,RC1
         MVC   LCBADR,5(R1)
         DMTSUCH LCBTAB,LCBANZ,20,LCBADR,REG=4,POS=5,NFOUND=DCBAE
         LA    R2,RC1
         ST    R2,RETCODE
         MVC   0(3,R4),=3X'EE'
         BAL   R10,SORTTAB
         MVI   3(R1),4
DCBAE    EQU   *
         BR    R14
         EJECT
DCBSYNAD EQU   *
         LA    R14,RETURN
         MVC   ZD1(4),=X'00100404'
         ST    R1,ZD1+4
         LA    R1,ZD1
         B     DCBABEND
         EJECT
DCBEOVX  EQU   *
         ST    R1,ZF
         MVC   LCBADR,ZF+1
         DMTSUCH LCBTAB,LCBANZ,20,LCBADR,POS=5,REG=4,NFOUND=(R14)
         TR    16(1,R4),TRANS00
         CLI   16(R4),4
         BL    DCBEOVE
         CLI   16(R4),7
         BL    DCBEOV1
         LA    R15,FEHL4
         MVC   40(3,R15),8(R4)
         ST    R15,RC1
         MVC   0(3,R4),=3X'EE'
         BAL   R10,SORTTAB
         BR    R14
DCBEOV1  EQU   *
         LA    R15,FEHL3
         ST    R15,RC1
         LA    R15,RC1
         ST    R15,RETCODE
DCBEOVE  EQU   *
         BR    R14
         EJECT
STATUS   EQU   *
* AUFBEREITEN DER STATUS-MELDUNG (MSGGEN)
         MVC   MHPUT(6),=X'000000070000'
         MVC   MHPUTC,42(R1)
         MVI   MHPUTD,64
         LA    R14,MHPUT
         ST    R14,RC2
         MVC   LCBADR,13(R1)
         LA    R14,STAUS
         BAL   R10,SUCHTAB
         MVC   STATUS2,8(R4)
         TM    3(R4),64
         BO    STATU1
         MVC   STATUS1,=C'AUS'
         B     STATU2
STATU1   EQU   *
         MVC   STATUS1,=C'EIN'
STATU2   EQU   *
         SR    R14,R14
         IC    R14,16(R4)
         SLL   R14,10
         AH    R14,18(R4)
         CVD   R14,ZD1
         OI    ZD1+7,15
         UNPK  STATUS3,ZD1
         UNPK  STATUS4(3),3(2,R4)
         UNPK  STATUS4+2(3),17(2,R4)
         UNPK  STATUS4+4(3),16(2,R4)
         UNPK  STATUS4+6(5),18(3,R4)
         TR    STATUS4,TRANS-240
         MVI   STATUSE,C''''
         MVC   STATUSMS+1(1),STATUSMS
         TM    3(R4),64
         BZ    STATEX
         OI    S3+1,240
         B     STATEX
STAUS    EQU   *
         MVC   STATUS1,=C'AUS'
         MVI   STATUSMS+1,12
STATEX   EQU   *
         LA    R14,STATUSMS+1
         ST    R14,RC1
*
         LA    R14,RC1
         ST    R14,RETCODE
         B     A01
         SPACE 2
STPRINT  EQU   *
         NI    S3+1,15
         MVC   AUSG(2),=H'48'
         MVC   AUSG+4(44),STATUSMS+3
         MVI   AUSG+8,C'*'
         BAL   R10,PUTR
         B     RETURN
         EJECT
PFKFUNKT EQU   *
* PFK-TABELLE UEBER LOAD HEREINHOLEN
         B     A00
         TITLE 'SPTSO01 - DEFINITIONEN'
         PRINT NOGEN
SHIFTMVC MVC   0(0,R15),0(R2)
MOVE     MVC   AUSG+5(0),3(5)
OCINSTR  OC    AUSG+5(0),BLANKS
MOVEOUT  MVC   AUSG+8(0),2(R3)
OCINOUT  OC    AUSG+8(0),BLANKS
MOVEPARM MVC   AUSG+4(0),0(R3)
TRTPARM  TRT   AUSG+4(0),TRTBL
TRTPARM1 TRT   0(0,R4),TRTEXT
TRTEXEC  TRT   2(0,R3),TRT1D
SCANTRT  TRT   3(0,R5),TRTEXT
LOG      DC    C'LOG '
STTAB    DS    0F
         DC    X'7D',C'**ENTER**'
         DC    X'6D',C'**CLEAR**'
         DC    X'6C',C'***PA1***'
         DC    X'6E',C'**CNCL **'
         DC    X'01',C'**TEST **'
         DC    X'F1',C'**PFK 1**'
         DC    X'F2',C'**PFK 2**'
         DC    X'F3',C'**PFK 3**'
         DC    X'F4',C'**PFK 4**'
         DC    X'F5',C'**PFK 5**'
         DC    X'F6',C'**PFK 6**'
         DC    X'F7',C'**PFK 7**'
         DC    X'F8',C'**PFK 8**'
         DC    X'F9',C'**PFK 9**'
         DC    X'7A',C'**PFK10**'
         DC    X'7B',C'**PFK11**'
         DC    X'7C',C'**PFK12**'
         DC    X'FF',C'**X''  ''**'    LAST ENTRY
TRANS    DC    C'0123456789ABCDEF'
BLANKS   DC    CL256' '
TRTBL    DS    0F
         DC    64X'0',X'1',191X'0'
TRTEXT   DS    0F
         DC    64X'1',X'0',191X'1'
TRT1D    DS    0F
         DC    17X'0',X'4',11X'0',X'8',226X'0'
TRANSL   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
TRANS00  DC    X'010203040506070809'
HELPMSG  DS    0H
         DC    AL2(HELPEND-HELPMSG)
         DC    H'0'
         DC    C'    LOG005I HELP-OUTPUT NOT PRINTED '
HELPEND  DS    0C
STATUSMS DC    AL1(STATUSE-STATUSMS-1)
         DS    AL1
         DC    C'       S='
STATUS1  DS    CL3
         DC    C' D='
STATUS2  DS    CL3
         DC    C' R='
STATUS3  DS    CL8
         DC    C' / X'''
STATUS4  DS    CL10
STATUSE  DS    C
FEHL1    DC    AL1(FEHL2-FEHL1-1)
         DC    C'LOG001E LOG-DATASET IS NOT AVAILABLE'
FEHL2    DC    AL1(FEHL3-FEHL2-1)
         DC    C'LOG002E LOG-DATASET ABEND ...... '
FEHL3    DC    AL1(FEHL4-FEHL3-1)
         DC    C'LOG003W LOG-DATASET NEAR FULL * * * * *'
FEHL4    DC    AL1(FEHL5-FEHL4-1)
         DC    C'LOG004E LOG-DATASET FULL - ENTER ''ZLOG ...'''
FEHL5    EQU   *
         DC    AL1(FEHL6-FEHL5-1)
         DC    C'LOG006E LOG-DATASET OPEN-ERROR'
FEHL6    EQU   *
         DC    AL1(FEHL7-FEHL6-1)
         DC    C'LOG019I TO PRINT LOG-DATASET '
         DC    C'ENTER ''ZLOG ...'''
FEHL7    EQU   *
         EJECT
         LTORG
         EJECT
RETCODE  DS    F
RC1      DS    F
RC2      DS    F
ZF       DS    F
ZD       DS    D
ZD1      DS    D
ZTIME    DS    CL7
         DC    C'.,'
ZTSCHAB  DC    X'0001070203080405'
SAVE     DS    F
SAVE14   DS    F
SAVER8   DS    F
SAVEAREA DC    16F'0'
SCANSAVE DS    A
SCANLEN  DS    H
SHIFTLEN DS    H
         DC    A(SAVEAREA)             HINTER DIESER DEFINITION NICHTS
*                                      EINFUEGEN !!!
SHIFTTAB DS    (&BUFSIZE./&KEYLEN.)F
         DC    F'-1'
LCBADR   DS    AL3
LCBTAB   DS    0F
         DC    10XL20'0'
LCBANZ1  DC    AL2((LCBANZ1-LCBTAB)/20)
LCBANZ   DS    0H
         DC    AL2(0)
TAUSG    DS    0F
TAUSG1   DS    CL5
TAUSGT   DS    0CL8
         DS    CL7
AUSG     DS    0F
         DS    H
         DC    H'0'
         DS    CL256
MHPUT    DS    0F
         DC    XL6'0'
MHPUTC   DS    XL6
MHPUTD   DS    CL80
DDTAB    DC    C'9876543210'
DDTABANZ DC    H'10'
S        DC    X'0'
DATETIME DC    CL22' '
SAVEDCBA DC    4F'0'
DUMPEX   DC    A(DUMPEXIT)
GMTSR15  DS    A
ZAE      DC    PL2'0'
TSR15PRM DS    0F
         DC    X'FD',AL4(DUMPEX-48)
ZZZ      DC    PL2'0'
ZBZ      DC    PL2'0'
         DC    AL1(16)
ZNAME    DS    CL12
ZAEHL    DS    CL4
         DC    X'D000'
TLAENG   DS    AL2
         DC    X'FF'
CATAL    CAMLST CAT,DSNAME,,VOLUME
UNCAT    CAMLST UNCAT,DSNAME
DSNAME   DC    C'&QUALIFY..LOG#'
QUALIF   DC    CL4' '
VOLUME   DC    H'1'
UCBTYPE  DS    XL4
VOLSER   DS    CL6
RETCOD   DS    H'0'
VOLPARM  DC    C' V(VOLSER)'
LOGDCB   DCB   DDNAME=LOGDATAX,                                        X
               DSORG=PS,                                               X
               MACRF=PM,                                               X
               RECFM=VBA,                                              X
               LRECL=137,                                              X
               BLKSIZE=3120,                                           X
               SYNAD=DCBSYNAD,                                         X
               EXLST=EXLIST
EXLIST   DS    0F
         DC    X'87',AL3(JFCB)
JFCB     DS    CL176
SHIFTBER DS    CL&BUFSIZE
         EJECT
DUMPEXIT DS    0F
         USING *,15
         STM   4,7,20(13)
         L     4,SAVETSR
         LR    R6,R0
         L     7,=A(AUSG)
         MVC   4(133,7),0(R6)
         LH    1,18(4)
         LA    1,1(1)
         STH   1,18(4)
         L     1,4(4)
         LR    0,7
         LM    4,7,20(13)
         L     15,48(1)
         BR    15
SAVETSR  DS    F
         END
