         MACRO
         REGISTER
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
*
*        ANWENDUNG: ORIGINAL-IEWL RENAMEN UND VORPROGRAMM UNTER IEWL EIN
*
LKED     CSECT
         REGISTER
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING LKED,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         LR    R7,R1                    SAVE PARMADRESSE
**********************************************************************
*        ERMITTLUNG VORHANDENER FREIER SPEICHER
**********************************************************************
         GETMAIN VC,LA=MAXMIN,A=ADRLEN
         L     R5,LEN       FREIER KERNSPEICHER
         SRA   R5,10        UMRECHNUNG IN K-BYTES
         FREEMAIN V,A=ADRLEN
**********************************************************************
*        SIZE1 UND SIZE2 ERMITTELN
**********************************************************************
         SH    R5,=H'6'                SIZE1=REGION-10K
         LR    R6,R5                   R6=SIZE1
         SH    R5,=H'82'               SIZE1=82K=MINWERT FUER SIZE2=6K
         BNM   LKED10
         SR    R5,R5                   WENN R5<0 THEN R5=0
LKED10   EQU   *
         CH    R6,=H'106'              BIS SIZE1=106 ZUWACHS IN SIZE2
         BL    SIZE
         CH    R6,=H'130'              BIS SIZE1=130 SIZE2=30K
         BH    LKED20
         LA    R5,24
         B     SIZE
LKED20   EQU   *
         SRA   R5,1                    ZUWACHS HALBIEREN
         CH    R6,=H'270'              MAX SIZE2=100K
         BL    SIZE
         LA    R5,94
SIZE     EQU   *
         LA    R5,6(R5)                MIN SIZE2=6K
**********************************************************************
*        PRUEFEN OB SIZE IN PARM ANGEGEBEN WURDE
**********************************************************************
         L     R3,0(R7)
         LH    R4,0(R3)
         CH    R4,=H'6'
         BL    NSIZE     PARM < 6 STELLEN
         SH    R4,=H'6'
         LA    R1,2(R3)    ADR. PARM-FELD
LOOP     EQU   *
         CLC   0(5,R1),=C'SIZE='
         BE    XCTL
         LA    R1,1(R1)
         BCT   R4,LOOP
**********************************************************************
* PARM INFORMATIONEN IN NEUES FELD BRINGEN UND SIZE ANHAENGEN
**********************************************************************
NSIZE    EQU   *
         CVD   R6,DW                    SIZE1
         OI    DW+7,X'0F'
         UNPK  SIZE1,DW
         CVD   R5,DW                    SIZE2
         OI    DW+7,X'0F'
         UNPK  SIZE2,DW
         LH    R4,0(R3)       LAENGENFELD AUS PARM
         LA    R0,32(R4)      +18 BYTES FUER SIZE + 2 BYTES FUER PARM-
         GETMAIN R,LV=(0)   LAENGENFELD + 12 BYTES FUER PARAMETERLISTE
         LR    R2,R1          AREA-ADR. INS R2
         LA    R1,1(R4)       LAENGE FUER MVC
         STC   R1,MVC+1
MVC      MVC   12(10,R2),0(R3) PARM UEBERTRAGEN
         LA    R1,14(R2,R4)     ADR FUER SIZE ZUSATZ BERECHNEN
         MVC   0(18,R1),SIZEANG
         LA    R4,18(R4)      LAENGENFELD BERICHTIGEN
         STH   R4,12(R2)
**********************************************************************
*         PARAMETERLISTE NEU AUFBAUEN UND XCTL LINKAGE-EDITOR
**********************************************************************
         LA    R1,12(R2)       PARAMETERLISTE AUFBAUEN
         MVC   0(12,R2),0(R7)          MOVE ALTE PARAMETERLISTE
         STCM  R1,7,1(R2)               PARMADRESSE AENDERN
         LR    R7,R2
XCTL     EQU   *
         LR    R1,R7
         L     R13,SAVE+4
         L     R14,12(R13)
         XCTL  (2,12),EP=HEWLF064,MF=(E,(1))
**********************************************************************
*         KONSTANTEN UND AREAS
**********************************************************************
DW       DS    D
SAVE     DS    18F
MAXMIN   DC    A(0),A(1000000)
ADRLEN   DS    F
LEN      DS    F
SIZEANG  DS    0CL18
         DC    C',SIZE=('
SIZE1    DC    C'0000'
         DC    C'K,'
SIZE2    DC    C'000'
         DC    C'K)'
          LTORG
          END
