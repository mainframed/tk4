000010* STAND 05/20/81
000020 IDENTIFICATION DIVISION.
000030 PROGRAM-ID.    OP001
000040 AUTHOR.        LINCK FOD/O
000050 DATE-WRITTEN.  OKTOBER 1974.
000060 DATE-COMPILED. OKTOBER 1974.
000070 REMARKS.       LOGDATEI IN ABSTIMMSUMMEN-DATEI EINSPIELEN.
000080     SKIP3
000090 ENVIRONMENT DIVISION.
000100 INPUT-OUTPUT SECTION.
000110 FILE-CONTROL.
000120     SELECT D-ASTLOG   ASSIGN UT-S-ASTLOG.
000130     SELECT D-ASTSUM   ASSIGN DA-ASTSUM
000140                       ACCESS      IS RANDOM
000150                       ORGANIZATION IS INDEXED
000160                       FILE STATUS IS STAT
000170                       RECORD  KEY IS ASUMNR IN S-ASTSUM.
000180     SELECT D-ASTPRINT ASSIGN UT-S-ASTPRINT.
000190     SKIP3
000200 DATA DIVISION.
000210 FILE SECTION.
000220 FD  D-ASTLOG    BLOCK  CONTAINS   0 RECORDS
000230                 RECORD CONTAINS  48 CHARACTERS
000240                 LABEL RECORD STANDARD
000250                 RECORDING MODE F
000260                 DATA RECORD S-ASTLOG.
000270              COPY ACLOGDF.
000280     SKIP3
000290 FD  D-ASTSUM    BLOCK  CONTAINS   1 RECORDS
000300                 LABEL RECORD STANDARD
000310                 DATA RECORD S-ASTSUM.
000320              COPY ACSUMDFV.
000330     SKIP3
000340 FD  D-ASTPRINT  BLOCK  CONTAINS   0 RECORDS
000350                 RECORD CONTAINS  77 CHARACTERS
000360                 LABEL RECORD STANDARD
000370                 RECORDING MODE F
000380                 DATA RECORD S-ASTPRINT.
000390 01  S-ASTPRINT              PIC X(77).
000400     SKIP3
000410 WORKING-STORAGE SECTION.
000420             COPY ACDATDF.
000430 01  FELD-D.
000440     05  WERTN               PIC X(25).
000450     05  SORTZWI.
000460         10  LOGDAT          PIC 9(7).
000470         10  LOGUHR          PIC 9(7).
000480     05  SORTALT             PIC X(14).
000490 01  TAB.
000500     05  TAB1.
000510         10  DAT1.
000520             15  JAHR1       PIC S99             VALUE +0.
000530             15  TAG1        PIC S999            VALUE +0.
000540         10  UHR1            PIC S9(7)   COMP-3  VALUE +0.
000550         10  DAT22.
000560             15  JAHR2       PIC S99             VALUE +0.
000570             15  TAG2        PIC S999            VALUE +0.
000580         10  DAT2        REDEFINES DAT22
000590                             PIC S9(5).
000600         10  UHR2            PIC S9(7)   COMP-3  VALUE +0.
000610         10  ZAE             PIC S9(5)   COMP-3  VALUE +0.
000620     05  TAB2.
000630         10  DAT1.
000640             15  JAHR1       PIC S99             VALUE +0.
000650             15  TAG1        PIC S999            VALUE +0.
000660         10  UHR1            PIC S9(7)   COMP-3  VALUE +0.
000670         10  DAT22.
000680             15  JAHR2       PIC S99             VALUE +0.
000690             15  TAG2        PIC S999            VALUE +0.
000700         10  DAT2        REDEFINES DAT22
000710                             PIC S9(5).
000720         10  UHR2            PIC S9(7)   COMP-3  VALUE +0.
000730         10  ZAE             PIC S9(5)   COMP-3  VALUE +0.
000740 01  UEB.
000750     05  FILLER              PIC X(20)   VALUE '1OP001 01 L 01'
000760     05  FILLER              PIC X(34)   VALUE
000770                 'PROTOKOLL ASTLOG-BEARBEITUNG'.
000780 01  UEB1.
000790     05  FILLER              PIC X(54)   VALUE SPACES.
000800     05  DATUM               PIC X(10).
000810     05  FILLER              PIC X       VALUE ' '.
000820     05  ZEIT                PIC X(8).
000830 01  ZEILE1.
000840     05  FILLER              PIC X       VALUE '0'.
000850     05  TVAR                PIC X(17).
000860     05  DAT1.
000870         10  TAG1            PIC ZZ9.
000880         10  FILLER          PIC XXX     VALUE '.19'.
000890         10  JAHR1           PIC 99.
000900     05  UHR1                PIC ZZ9,99,99,9.
000910     05  FILLER              PIC X(7)    VALUE '  BIS  '.
000920     05  DAT22.
000930         10  TAG2            PIC ZZ9.
000940         10  FILLER          PIC XXX     VALUE '.19'.
000950         10  JAHR2           PIC 99.
000960     05  UHR2                PIC Z99,99,99,9.
000970     05  ZAE                 PIC Z(6)9.
000980     05  FILLER              PIC X(7)    VALUE ' SAETZE'.
000990 01  ZEILE2.
001000     05  FILLER              PIC X(044)  VALUE
001010                 '0PROGRAMMABBRUCH : LOGDATEI FALSCHER SATZTYP'.
001020 01  ZEILE3.
001030     05  FILLER              PIC X(042)  VALUE
001040                 '0PROGRAMMABBRUCH : SORTIERFOLGE FALSCH'.
001050 01  ZEILE4.
001060     05  FILLER     PIC X(17) VALUE '0PROGRAMMABBRUCH '.
001070     05  PR-INDEX   PIC X(10).
001080     05  FILLER     PIC X(7)  VALUE ' ERR = '.
001090     05  PR-ERR     PIC 9.
001100     05  FILLER     PIC X(8)  VALUE ' STAT = '.
001110     05  PR-STAT    PIC XX.
001120     EJECT
001130 PROCEDURE DIVISION.
001140 VORUEB.
001150     COPY ACVOR.
001160 VORSPANN.
001170     OPEN INPUT  D-ASTLOG.
001180     OPEN I-O    D-ASTSUM.
001190     MOVE 0 TO RETURN-CODE.
001200     WRITE S-ASTPRINT FROM UEB.
001210     WRITE S-ASTPRINT FROM UEB1.
001220     MOVE ZERO TO SORTALT.
001230* H A U P T T E I L
001240* -----------------
001250 MAIN.
001260     PERFORM LESLOG.
001270     IF  KTYP IN S-ASTLOG = NEU      PERFORM SCHIEBEN    ELSE
001280     IF  KTYP IN S-ASTLOG = WIEDER   PERFORM KORR        ELSE
001290     IF  KTYP IN S-ASTLOG = INIT     PERFORM HINZUF      ELSE
001300     IF  KTYP IN S-ASTLOG = REP      PERFORM KORR        ELSE
001310     IF  KTYP IN S-ASTLOG = DEL      PERFORM DELE        ELSE
001320     IF  KTYP IN S-ASTLOG =  DELPER  PERFORM DEL-PERIODE ELSE
001330     IF  KTYP IN S-ASTLOG =  INSPER  PERFORM EINSCHIEBEN ELSE
001340     IF  KTYP IN S-ASTLOG = KORPER   PERFORM CHANGE-PERIODE ELSE
001350     MOVE 8 TO RETURN-CODE
001360     WRITE S-ASTPRINT FROM ZEILE2
001370     GO TO ENDE.
001380     ADD 1 TO ZAE IN TAB1.
001390     MOVE LOGUHR IN S-ASTLOG TO UHR2 IN TAB1.
001400     MOVE LOGDAT IN S-ASTLOG TO DAT2 IN TAB1.
001410     IF  ZAE IN TAB1 = 1
001420         MOVE UHR2 IN TAB1 TO UHR1 IN TAB1
001430         MOVE DAT2 IN TAB1 TO DAT1 IN TAB1.
001440     IF ERR = ZERO
001450         ADD 1 TO ZAE IN TAB2
001460         MOVE LOGUHR IN S-ASTLOG TO UHR2 IN TAB2
001470         MOVE LOGDAT IN S-ASTLOG TO DAT2 IN TAB2
001480         IF  ZAE IN TAB2 = 1
001490             MOVE UHR2 IN TAB2 TO UHR1 IN TAB2
001500             MOVE DAT2 IN TAB2 TO DAT1 IN TAB2.
001510     GO TO MAIN.
001520*  READ/WRITE ERROR
001530     MOVE ASUMNR IN S-ASTLOG TO PR-INDEX.
001540     MOVE ERR                TO PR-ERR.
001550     MOVE STAT               TO PR-STAT.
001560     WRITE S-ASTPRINT FROM ZEILE4.
001570     SKIP3
001580* A B S C H L U S S
001590* -----------------
001600 ENDE.
001610     CLOSE D-ASTSUM.
001620     CLOSE D-ASTLOG.
001630     MOVE 'LOGDATEI VOM' TO TVAR IN ZEILE1.
001640     MOVE CORRESPONDING TAB1 TO ZEILE1.
001650     WRITE S-ASTPRINT FROM ZEILE1.
001660     MOVE 'VERARBEITET VOM' TO TVAR IN ZEILE1.
001670     MOVE CORRESPONDING TAB2 TO ZEILE1.
001680     WRITE S-ASTPRINT FROM ZEILE1.
001690     CLOSE D-ASTPRINT.
001700     STOP RUN.
001710     EJECT
001720* A U F G E R U F E N E  S E C T I O N S
001730* --------------------------------------
001740     SKIP3
001750 LESLOG SECTION.
001760* LOGDATEI-SATZ LESEN
001770     READ D-ASTLOG AT END GO TO ENDE.
001780     MOVE CORRESPONDING SORTBEGRIFF IN S-ASTLOG  TO  SORTZWI
001790     IF  SORTZWI < SORTALT
001800         WRITE S-ASTPRINT FROM ZEILE3
001810         MOVE 8 TO RETURN-CODE
001820         GO TO ENDE.
001830     MOVE SORTZWI TO SORTALT.
001840     MOVE ZERO TO ERR.
001850     MOVE ASUMNR IN S-ASTLOG TO ASUMNRZ ASUMNR IN S-ASTSUM.
001860     SKIP3
001870 HINZUF SECTION.
001880     COPY ACHINZ REPLACING S-ASTKOR BY S-ASTLOG.
001890 DEL-PERIODE SECTION.
001900     COPY ACDLP  REPLACING S-ASTKOR BY S-ASTLOG.
001910     SKIP3
001920     SKIP3
001930 KORR SECTION.
001940* ABSTIMMSUMMENSATZ KORRIGIEREN
001950     PERFORM READSATZ.
001960     IF  ERR NOT = 0
001970         GO TO KORR-E.
001980     COMPUTE NPER-I =
001990                   NPER IN S-ASTSUM + NPER IN S-ASTLOG.
002000     IF  NPER-I < 1
002010         OR NPER-I > NPER IN S-ASTSUM
002020         MOVE 9 TO ERR
002030         GO TO KORR-E.
002040     SET PIX TO NPER-I
002050     MOVE KTYP IN S-ASTLOG  TO FLAG  IN S-ASTSUM (PIX).
002060     MOVE AWERT IN S-ASTLOG TO AWERT IN S-ASTSUM (PIX).
002070     MOVE BERZT IN S-ASTLOG TO BERZT IN S-ASTSUM (PIX).
002080     PERFORM REWRITESATZ.
002090 KORR-E.
002100     EXIT.
002110     SKIP3
002120 DELE SECTION.
002130     COPY ACDELEV REPLACING S-ASTKOR BY S-ASTLOG.
002140     SKIP3
002150 SCHIEBEN SECTION.
002160*  AELTESTE PERIODE AUS ASTSUM-SATZ ENTFERNEN UND
002170*  ALS NEUESTE PERIODE DATEN AUS ASTLOG-SATZ EINFUEGEN
002180     PERFORM READSATZ.
002190     IF  ERR > ZERO GO TO SCHIEBEN-E.
002200     MOVE NPER IN S-ASTSUM TO NPER-I.
002210     PERFORM SHIFTDISK
002220         VARYING I FROM 2 BY 1 UNTIL I > NPER-I.
002230     PERFORM CARDTODISK.
002240     PERFORM REWRITESATZ.
002250 SCHIEBEN-E.
002260     EXIT.
002270 SHIFTDISK SECTION.
002280     COMPUTE J = I - 1.
002290     MOVE PERDEF IN S-ASTSUM (I) TO PERDEF IN S-ASTSUM(J).
002300     SKIP3
002310 CARDTODISK SECTION.
002320     COPY ACCTD   REPLACING S-ASTKOR BY S-ASTLOG.
002330     SKIP3
002340 ZEROTODISK SECTION.
002350     COPY ACZTD.
002360     SKIP3
002370 RSHIFT SECTION.
002380     COPY ACRSHIFT.
002390     SKIP3
002400 READSATZ SECTION.
002410     COPY ACSUMRD.
002420     SKIP3
002430 SCHRSUM SECTION.
002440     COPY ACSUMWT.
002450     SKIP3
002460 REWRITESATZ SECTION.
002470     COPY ACSUMRW.
002480     SKIP3
002490 MOVELOG SECTION.
002500 MOVELOG-P.
002510     EXIT.
002520 WRITELOG SECTION.
002530 WRITELOG-P.
002540     EXIT.
002550     SKIP3
002560 CHANGE-PERIODE SECTION.
002570     COPY ACCHGPER REPLACING S-ASTKOR BY S-ASTLOG.
002580     SPACE3
002590 EINSCHIEBEN SECTION.
002600 COPY ACEIN REPLACING S-ASTKOR BY S-ASTLOG.
