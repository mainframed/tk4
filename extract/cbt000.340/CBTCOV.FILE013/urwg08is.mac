UPDS     TITLE 'UPODIRG - READ PDS-DIRECTORY FOR USER MAINLINE'
UPODIRG  CSECT
PGMENTRY MBASIS ,                      SET UP
         USING DENTRY,R9               UNDEFINED DEBLOCKING
         USING UDIRAREA,R11            AREA IN USER-MAINLINE
OPENSWEX NOP   GETREC                  BRANCH AFTER FIRST TIME
OPENSW   EQU   OPENSWEX+1              FIRST-TIME-SWITCH
         USING IHADCB,R1               DCB'S ADDRESSED BY R1
         LA    R1,PDSDCB               LOAD DESIRED DCB-ADDRESS
         MVC   DCBDDNAM,UMEMBER        MOVE DDNAME FROM MAINLINE TO DCB
         OPEN  (PDSDCB,(INPUT))        OPEN PDS-DIRECTORY AS QSAM
         LA    R1,PDSDCB               LOAD DESIRED DCB-ADDRESS
         TM    DCBOFLGS,DCBOFOPN       WAS OPEN SUCCESSFULL
         BZ    NOTOPEN                 NO, GO INDICATE THIS
         OI    OPENSW,BRANCH           RESET FIRST-TIME SWITCH
         B     GETREC                  GO GET PHYSICAL BLOCK
         SPACE 3
GETREC   EQU   *                       DEBLOCKING ROUTINE
         LM    R9,R10,SAVE0910         RESTORE POINTER
         CR    R9,R10                  IS THIS END OF BLOCK
         BL    GETRECL                 NO, USE NEXT LOGICAL RECORD
         SPACE
GETRECP  EQU   *                       READ PHYSICAL BLOCK
         GET   PDSDCB,DDIRAREA         GET NEW BLOCK
         LA    R10,DDIRAREA            BLK START ADR
         LA    R9,2(R10)               ENTRY START ADR
         AH    R10,DUSED               CALC ENTRY END ADR
         SPACE
GETRECL  EQU   *                       GET LOGICAL RECORD
         XC    USERAREA,USERAREA       CLEAR USERAREA
         CLC   DMEMBER,HIGHVAL         IS IT LAST ENTRY
         BE    EOF                     YES, GO TO INDICATE THIS
         SPACE
MOVENTRY EQU   *                       MOVE ENTRY TO USER-MAINLINE
         MVC   USERAREA(L'UMEMBER+L'UTTR),DISKAREA MOVE MEMBERNM + TTR
         SR    R6,R6                   CLEAR REG6
         SR    R7,R7                   CLEAR REG7
         IC    R6,DC                   GET C-FIELD
         SRDL  R6,5                    SHIFT NO. USER HW TO REG7
         SRL   R7,26                   CALC NO. USER BYTES
         STH   R7,UNODATA              STORE IN USER-MAINLINE
         LTR   R8,R7                   CALC USERLENGTH
         BCTR  R8,0                    FOR MOVE
         BZ    NOTMOVE                 NO USER-DATE, BRANCH AROUND
         EX    R8,MOVEDATA             MOVE USER-DATA TO USER-MAINLINE
NOTMOVE  EQU   *
         SRDL  R6,2                    SHIFT NO. USER TTR TO REG7
         SRL   R7,30                   CALC NO. USER TTRN
         STH   R7,UNOTTRN              STORE IN USER-MAINLINE
         L     R7,HIGHVAL              CONVERT ALIAS INDICATOR
         SRDL  R6,28                   TO PACKED DECIMAL
         STC   R7,UALIAS               STORE IN USER MAINLINE
         SPACE
SETRETCD EQU   *                       SET RETURN CODE AND SAVE POINTER
         LA    R9,12+1(R8,R9)          CALC NEW ENTRY ADDRESS
         STM   R9,R10,SAVE0910         SAVE POINTER FOR DEBLOCKING
         MRETURN RC1=0                 SET RC=0 FORENTRY MOVEDD, GOBACK
         SPACE
MOVEDATA MVC   UUSERDAT(L'ZERO),DUSERDAT WILL BE EX'ED WITH R8
         SPACE 2
EOF      EQU   *
         CLOSE (PDSDCB)                CLOSE PDS-DIRECTORY
         NI    OPENSW,NOP              SET FIRST-TIME SWITCH
         MRETURN RC1=4                 SET RC=4 FOR EOF REACHED, GOBACK
         SPACE 3
NOTOPEN  EQU   *                       ENTRY FOR UNSUCCESSFULLY OPEN
         MRETURN RC1=8                 SET RC=8 FOR DCB NOT OPEN
         EJECT
         DS    0H
DDIRAREA DS    0CL256
DUSED    DC    H'0'
DENTRIES DC    XL254'0'
         SPACE
DENTRY   DSECT
DISKAREA DS    0CL74
DMEMBER  DC    CL8'MEMBERNM'
DTTR     DC    CL3'TTR'
DC       DC    B'10011111'             ALIAS - NO.TTRN - NO.USERDATA.HW
DUSERDAT DC    31H'0'
UPODIRG  CSECT
         SPACE 3
UDIRAREA DSECT
USERAREA DS    0CL78
UMEMBER  DC    CL8'MEMBERNM'
UTTR     DC    CL3'TTR'
UC       DS    0CL5
UALIAS   DC    P'1'
UNOTTRN  DC    H'0'
UNODATA  DC    H'0'
UUSERDAT DC    XL62'0'
UPODIRG  CSECT
         SPACE 3
ZERO     EQU   0
NOP      EQU   15
BRANCH   EQU   240
         SPACE 3
SAVE0910 DC    2F'0'
HIGHVAL  DC    8X'FF'
         SPACE 3
         LTORG ,                       LOCATE LITERAL POOL HERE
         EJECT
PDSDCB   DCB                                                           *
               BLKSIZE=256,                                            *
               DDNAME=PDSMAIN,         MAINLINE PASSES CORRECT NAME    *
               DEVD=DA,                                                *
               DSORG=PS,                                               *
               EODAD=EOF,                                              *
               LRECL=256,                                              *
               MACRF=(GM),                                             *
               RECFM=F
         EJECT
         DCBD                                                          *
               DSORG=QS,                                               *
               DEVD=DA
         SPACE 3
         END   PGMENTRY
