         MACRO
&LAB     DCONC &ERROR=,&FILE=
.**********************************************************************
.*                                                                    *
.*       DCONC - MACRO                                                *
.*                                                                    *
.*       LOEBER, W.      03.05.1976     GAD - MUENSTER                *
.*                                                                    *
.*             DISCRIPTION OF PARAMETERS                              *
.* R E S T  F O L L O W S                                             *
.*                                                                    *
.**********************************************************************
         LCLA  &IND1,&IND2,&I,&LGM
         LCLC  &C2
.*
&C2      SETC  'GAD'.'&SYSNDX'
.*
         AGO   .A1001
.A1000   ANOP
         MNOTE 04,'INCORRECT OPERANDS'
         MEXIT
.*
.A1001   ANOP
         AIF   (T'&ERROR EQ 'O').A1000
         AIF   (T'&FILE EQ 'O').A1000
         AIF   (N'&FILE LT 2).A1000
.*
&LGM     SETA  24+4+4+(10*N'&FILE)+8
.*
.************** NOW GENERATION *********************
.B1000   ANOP
.*  GENERATE MACRO-LABEL
         AIF   ('&LAB' EQ '').B1005
&LAB     DS    0H
.*
.B1005   ANOP
.*  G E N E R A T E  INSTRUCTIONS
         GETMAIN R,LV=&LGM
         LR    14,1
         LR    0,1
         LA    14,0(0,14)
         LA    1,&LGM.(,0)
         XR    15,15
         MVCL  0,14
.*
         LA    1,4(,14)
         ST    1,0(,14)
         MVC   4(2,14),=X'1403'
         LA    1,24(,14)
         ST    1,12(,14)
         MVI   0(14),X'80'
* FILE
         LA    1,32(,14)
         ST    1,24(,14)
         MVI   24(14),X'80'
*
&IND1    SETA  N'&FILE
         MVC   32(2,14),=X'0001'
         MVC   34(2,14),=H'&IND1.'
         LA    15,36(,14)
.*
&I       SETA  1
.B1010   ANOP
         AIF   (T'&FILE(&I) EQ 'O').A1000
         AIF   ('&FILE(&I)'(1,1) NE '''').B1015
         AIF   (K'&FILE(&I) GT 10).A1000
&IND2    SETA  K'&FILE(&I)-2
         MVC   0(2,15),=H'&IND2.'
         MVC   2(&IND2.,15),=C&FILE(&I)
         LA    15,&IND2.+2(,15)
         AGO   .B1020
.*
.B1015   ANOP
         XR    1,1
         ICM   1,3,&FILE(&I)
         STCM  1,3,0(15)
         BCTR  1,0
         N     1,=F'7'
         EX    1,*+8
         B     *+10
         MVC   2(0,15),&FILE(&I).+2
         LA    15,2+1(1,15)
.*
.B1020   ANOP
         AIF   (&I EQ &IND1).B1025
&I       SETA  &I+1
         AGO   .B1010
.*
.B1025   ANOP
* SVC 99
         LR    1,14
         SVC   99
* ERROR-CODES
         XC    &ERROR.(4),&ERROR
         LH    1,8(,14)
         STCM  1,3,&ERROR
         LH    1,10(,14)
         STCM  1,3,&ERROR.+2
* FREEMAIN
         LA    1,&LGM.(,0)
         LTR   15,15
         BNZ   &C2.E
         FREEMAIN R,LV=&LGM.,A=(14)
&C2.E    DS    0H
         MEXIT
         MEND
