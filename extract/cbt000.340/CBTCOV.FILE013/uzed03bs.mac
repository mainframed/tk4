         TITLE 'EDIT ISAM-FMT1/2 DSCBS OF SPECIFIC VOLUME'
PDV032A  CSECT
*              PROGRAMM-AUFRUF                                        *
*              ---------------                                        *
*                                                                     *
*              //S010     EXEC  PGM=PDV032A,REGION=60K                *
*              //SYSPRINT DD    SYSOUT=C                              *
*              //DD1      DD    UNIT=XXX,VOL=SER=XXX,DISP=OLD         *
*              //DDN      DD    UNIT=XXX,VOL=SER=YYY,DISP=OLD         *
*                                                                     *
*                                                                     *
*              KURZBESCHREIBUNG                                       *
*              ----------------                                       *
*              MIT -EXTRACT- WIRD DAS TIOT NACH ALLEN DDNAMEN DURCH-  *
*              SUCHT. NICHT VERARBEITET WERDEN FOLGENDE DDNAMEN:      *
*                   - JOBLIB                                          *
*                   - STEPLIB                                         *
*                   - SYSABEND                                        *
*                   - SYSUDUMP                                        *
*                   - SYSPRINT.                                       *
*              DER WAHLFREIE DDNAME WIRD IM DCB EINGESETZT UND NACH   *
*              -RDJFCB- DIE VOLUME SERIALNUMMER SICHERGESTELLT.       *
*              DER VTOC DES BETR.VOLUMES WIRD SEQUENTIELL GELESEN,    *
*              WOBEI NUR DIE FMT1 DSCBS MIT DSORG=IS BER<CKSICHTIGT   *
*              WERDEN.                                                *
*              AUS DEM FMT1 DSCB WERDEN FOLGENDE FELDER BEN@TIGT:     *
*                   - DSNAME                                          *
*                   - RECORD FORMAT                                   *
*                   - OPTION CODES                                    *
*                   - BLOCK LENGTH                                    *
*                   - RECORD LENGTH                                   *
*                   - KEY LENGTH                                      *
*                   - RELATIVE KEY POSITION                           *
*                   - CCHHR VOM FMT2 DSCB.                            *
*                                                                     *
*              WENN CCHHR NICHT 0 IST, BRINGT -OBTAIN- MIT SEEK DEN   *
*              FMT2 DSCB NACH WORKA. VOM FMT2 DSCB WERDEN FOLGENDE    *
*              FELDER BEN@TIGT:                                       *
*                   - NO.OF RECORDS IN PRIME DATA AREA                *
*                   - NO.OF RECORDS IN OVERFLOW AREA                  *
*                   - NO.OF TRACKS IN OVERFLOW                        *
*                   - NO.OF TRACKS LEFT ON INDEP.OVERFLOW AREA        *
*                   - STATUS INDICATORS                               *
*              AUS DEM TOTAL VON RECORDS IN PRIME UND OVERFLOW WIRD   *
*              ERRECHNET, WIEVIELE PROZENT IM OVERFLOW SIND.          *
*              BEI EOF VOM VTOC WIRD NACH DEM CLOSE WIEDER INS        *
*              TIOT-ABSUCHEN VERZWEIGT, D.H. ES WERDEN BELIEBIG VIELE *
*              DD-KARTEN MIT FREIEM DD-NAMEN VERARBEITET.             *
*                                                                     *
***********************************************************************
         EJECT
         PRINT NOGEN
*              REGISTER EQUATES
*              ----------------
*
R0       EQU   0
R1       EQU   1
R2       EQU   2         BASIS
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         SAVE  (14,12),,*
         BALR  R2,0
         USING *,R2
         MVC   8(4,R13),=A(SAVE)
         ST    R13,SAVE+4
         CNOP  0,4
         BAL   R13,START
SAVE     DC    18F'0'
         LTORG
         EJECT
***********************************************************************
*                                                                     *
* SEARCH TIOT DD-ENTRY F<R G<LTIGE DDNAMEN UND SAVE F<R RDJFCB        *
*                                                                     *
***********************************************************************
         SPACE 2
START    EQU   *
         OPEN  (SYSPRINT,(OUTPUT))
         CALL  UZED10A,(DATUM)     DATUM F<R TITEL TT.MM.JJ
         EXTRACT TIOTAREA,'S',FIELDS=TIOT
         L     R3,TIOTAREA
         LA    R3,24(R3)           FIRST DD-ENTRY
         SPACE 2
SEARCHDD EQU   *
         XR    R4,R4
         IC    R4,0(R3)            LENGTH OF ENTRY
         LTR   R4,R4
         BZ    RETURN              ZERO = LAST ENTRY REACHED
         MVC   DDNAME,4(R3)        SAVE DDNAME
         AR    R3,R4               NEXT ENTRY
         CLC   DDNAME,=C'JOBLIB  '
         BE    SEARCHDD
         CLC   DDNAME,=C'STEPLIB '
         BE    SEARCHDD
         CLC   DDNAME,=C'SYSABEND'
         BE    SEARCHDD
         CLC   DDNAME,=C'SYSUDUMP'
         BE    SEARCHDD
         CLC   DDNAME,=C'SYSPRINT'
         BE    SEARCHDD
         CLC   DDNAME,=C'JOBCAT  '
         BE    SEARCHDD
         CLC   DDNAME,=C'STEPCAT '
         BE    SEARCHDD
         CLC   DDNAME(4),=C'SYS0'
         BE    SEARCHDD
*
         MVC   VTOC+40(8),DDNAME   INSERT DDNAME IN DCB
         RDJFCB (VTOC)
         MVI   JFCBAREA,X'04'      DSNAME 040404 = DSN OF FMT4 DSCB
         MVC   JFCBAREA+1(43),JFCBAREA
         MVI   JFCBAREA+76,X'C0'
         MVC   VOLSER,JFCBAREA+118 SAVE VOLSER FOR OBTAIN AND TITLE
         OPEN  (VTOC,(INPUT)),TYPE=J
         SPACE 4
***********************************************************************
*                                                                     *
* READ VTOC UND FMT1 DSCB VERARBEITEN, WENN DSORG = IS.               *
*                                                                     *
***********************************************************************
         SPACE 2
RVTOC    EQU   *
         READ  VTOCDECB,SF,VTOC,DSCB,'S'
         CHECK VTOCDECB
         CLI   DATA,X'F1'          IS IT A FMT1 DSCB?
         BNE   RVTOC
         TM    DSCB+82,X'80'       IS DSORG INDEX-SEQUENTIAL?
         BNO   RVTOC               IS DSORG INDEX-SEQ?
         MVI   ZEILE,C' '          CLEAR LINE
         MVC   ZEILE+1(132),ZEILE
         MVC   ZDSN,DSCB           DSNAME.
         MVC   ZRECFM,=3C' '
         SPACE 1
*
*              TEST RECFM-BITS: 10.. ....  F  FIXED LENGTH
*                               01.. ....  V  VARIABLE LENGTH
*                               11.. ....  U  UNDEFINED LENGTH
*                               ..1. ....  T  TRACK OVERFLOW
*                               ...1 ....  B  BLOCKED
*                               .... 1...  S  SPANNED RECORDS
*                               .... .10.  A  ASA CONTROL
*                               .... .01.  M  MACHINE CONTROL
*                               .... .00.     NO CONTROL CHAR
*                               .... ...0     ALWAYS ZERO
*
         LA    R5,ZRECFM
         TM    DSCB+84,X'80'       BIT 0 ON?
         BNO   TSTREC3
         MVC   0(1,R5),=C'F'       -F-
TSTREC0  LA    R5,1(R5)
         TM    DSCB+84,X'10'       BIT 3 ON?
         BNO   TSTREC1
         MVC   0(1,R5),=C'B'       -B-
         LA    R5,1(R5)
TSTREC1  EQU   *
         TM    DSCB+84,X'08'       BIT 4 ON?
         BNO   TSTREC5
         MVC   0(1,R5),=C'S'       -S-
         B     TSTOPTCD
*
TSTREC3  EQU   *
         TM    DSCB+84,X'40'       BIT 1 ON?
         BNO   TSTREC4
         MVC   0(1,R5),=C'V'       -V-
         B     TSTREC0
TSTREC4  EQU   *
         MVC   0(1,R5),=C'U'       -U-
         B     TSTOPTCD
*
TSTREC5  EQU   *
         TM    DSCB+84,X'06'       BIT 5 ODER 6 ON?
         BZ    TSTOPTCD
         TM    DSCB+84,X'04'       BIT 5 ON?
         BNO   TSTREC6
         MVC   0(1,R5),=C'A'       -A-
         B     TSTOPTCD
*
TSTREC6  EQU   *
         MVC   0(1,R5),=C'M'       -M-
         SPACE 1
*
*              TEST OPTCD-BITS: 1... ....  W  WRITE VALIDITY CHECK
*                               .1.. ....  U  FULL TRACK INDEX WRITE
*                               ..1. ....  M  MASTER INDEXES
*                               ...1 ....  I  INDEPENDENT OVERFLOW
*                               .... 1...  Y  CYLINDER OVERFLOW
*                               .... .1..     RESERVED BIT
*                               .... ..1.  L  DELETE OPTION
*                               .... ...1  R  REORGANIZATION CRITERIA
*
TSTOPTCD EQU   *
         MVC   ZOPTCD,=5C' '
         LA    R5,ZOPTCD
         TM    DSCB+85,X'10'       BIT 3 ON?
         BNO   TSTOPT1
         MVC   0(1,R5),=C'I'
         LA    R5,1(R5)
TSTOPT1  TM    DSCB+85,X'08'       BIT 4 ON?
         BNO   TSTOPT2
         MVC   0(1,R5),=C'Y'
         LA    R5,1(R5)
TSTOPT2  TM    DSCB+85,X'02'       BIT 6 ON?
         BNO   TSTOPT3
         MVC   0(1,R5),=C'L'
         LA    R5,1(R5)
TSTOPT3  TM    DSCB+85,X'80'       BIT 0 ON?
         BNO   TSTOPT4
         MVC   0(1,R5),=C'W'
         LA    R5,1(R5)
TSTOPT4  TM    DSCB+85,X'01'       BIT 7 ON?
         BNO   TSTBLK
         MVC   0(1,R5),=C'R'
         SPACE 1
*
*              CONVERT BLOCKLENGTH            )
*                      RECORD LENGTH          )
*                      KEY LENGTH             )
*                      RELATIVE KEY POSITION  )  FROM HEX TO DEC
*
TSTBLK   EQU   *
         MVC   FULLW,=4X'00'
         MVC   FULLW+2(2),DSCB+86  BLOCK LENGTH
         L     R5,FULLW
         CVD   R5,DW
         MVC   ZBLOCK,SCHAB1
         ED    ZBLOCK,DW+5
         SPACE 2
         MVC   FULLW,=4X'00'
         MVC   FULLW+2(2),DSCB+88  RECORD LENGTH
         L     R5,FULLW
         CVD   R5,DW
         MVC   ZLRECL,SCHAB1
         ED    ZLRECL,DW+5
         SPACE 2
         MVC   FULLW,=4X'00'
         MVC   FULLW+3(1),DSCB+90  KEY LENGTH
         L     R5,FULLW
         CVD   R5,DW
         MVC   ZKEYLEN,SCHAB2
         ED    ZKEYLEN,DW+6
         SPACE 2
         MVC   FULLW,=4X'00'
         MVC   FULLW+2(2),DSCB+91  RELATIVE KEY POSITION
         L     R5,FULLW
         CVD   R5,DW
         MVC   ZRKP,SCHAB2
         ED    ZRKP,DW+6
         SPACE 2
         CLC   DSCB+135(5),=5X'00' IS THERE NO FMT2/3 DSCB?
         BE    NOFMT2
         MVC   CCHHR,DSCB+135      CCHHR OF FMT2 DSCB
         SPACE 4
***********************************************************************
*                                                                     *
* READ FMT2 DSCB: OBTAIN BRINGT MIT -SEEK- DEN N%CHSTEN DSCB          *
*                 (FMT2 ODER FMT3) NACH WORKA.                        *
*                 BYTE  1-140  GANZER DSCB AB DISPL.0                 *
*                        (45)  IDENTIFIER, SOLLTE F2 SEIN             *
*                     141-148  F<R MACRO SELBST.                      *
*                                                                     *
***********************************************************************
         SPACE 2
         OBTAIN LIST3
         LR    R5,R15
         B     BRTAB(R5)           GEM.RETURN-CODE
BRTAB    B     TSTFMT2              0
         B     ERR1                 4
         B     ERR2                 8
         B     ERR3                12
         B     ERR4                16
         B     ERR5                20
*
TSTFMT2  EQU   *
         CLI   WORKA+44,X'F2'      IS IT A FMT2 DSCB?
         BNE   NOFMT2
*
         MVC   ZPRIM,SCHAB3
         MVC   FULLW,WORKA+67      PRIME AREA RECORD COUNT
         L     R5,FULLW
         CVD   R5,DW
         ED    ZPRIM,DW+4
         ZAP   ZFELD,DW+4(4)       SAVE ANZ.RECS IN PRIME AREA
         MVC   ZOVREC,SCHAB1
         MVC   FULLW,=4X'00'       CLEAR FULLW
         MVC   FULLW+2(2),WORKA+128  RECORDS IN OVERFLOW AREA
         L     R5,FULLW
         CVD   R5,DW
         ED    ZOVREC,DW+5
*
*
         CP    ZFELD,=P'0'         KEINE RECS IN PRIME?
         BE    HUNDRT
         CP    DW+5(3),=P'0'       KEINE RECS IM OVERFLOW?
         BE    NULL
*
         AP    ZFELD,DW+5(3)       TOTAL PRIME + OFLOW RECORDS
         ZAP   RFELD,DW+5(3)       OFLOW RECORDS
         MP    RFELD,=P'100000'    OFLOW RECORDS X 100'000
         DP    RFELD,ZFELD         PRIME / OFLOW
         AP    RFELD(7),=P'5'      3.KOMMASTELLE RUNDEN
         MVO   RFELD(3),RFELD+4(2)
         OI    RFELD+2,X'0F'       VORZEICHEN EINSETZEN
         B     TSTPROZ
*
*
HUNDRT   EQU   *
         CP    DW+5(3),=P'0'       OVERFLOW RECORDS VORHANDEN?
         BE    NULL
         ZAP   RFELD(3),=P'10000'  100 PROZ.IM OVERFLOW
         B     TSTPROZ
*
NULL     ZAP   RFELD(3),=P'0'      NICHTS IM OVERFLOW
*
*
TSTPROZ  EQU   *
         MVC   ZPROZ,SCHAB4
         ED    ZPROZ,RFELD
         MVI   ZSIGN,C';'
*
*
         MVC   FULLW,=4X'00'
         MVC   FULLW+3(1),WORKA+52 ANZAHL TRK PRO CYL F<R CYLOFL
         L     R5,FULLW
         CVD   R5,DW
         MVC   ZCYLOTR,SCHAB2
         ED    ZCYLOTR,DW+6
*
*
         MVC   FULLW,=4X'00'
         MVC   FULLW+2(2),WORKA+126  TRKS LEFT ON INDEP.OFLOW
         L     R5,FULLW
         CVD   R5,DW
         MVC   ZFRTRK,SCHAB5
         ED    ZFRTRK,DW+5
*
*
         MVC   FULLW,=4X'00'
         MVC   FULLW+2(2),WORKA+130  FULL CYLOFL AREAS
         L     R5,FULLW
         CVD   R5,DW
         MVC   ZFULCYL,SCHAB5
         ED    ZFULCYL,DW+5
*
*
         MVO   ZIND(2),WORKA+71(1)   STATUS INDICATORS LEFT HALFBYTE
         MVC   ZIND+1(1),WORKA+71    STATUS INDICATORS RIGTH HALFBYTE
         MVZ   ZIND,=X'F0F0'         INSERT VORZEICHEN
*
         SPACE 4
***********************************************************************
*                                                                     *
* PRINT-ROUTINE                                                       *
*                                                                     *
***********************************************************************
         SPACE 2
PRINT    EQU   *
         CP    ZZEL,=P'60'
         BNH   PRINT1
         BAL   R11,TITEL
PRINT1   EQU   *
         MVI   CTL,X'09'
         PUT   SYSPRINT,ZEILE
         AP    ZZEL,=P'1'
         B     RVTOC
*
*
*
NOFMT2   EQU   *
         MVC   ZPRIM(30),=C'NO FMT2 DSCB FOR THIS DATA SET'
         B     PRINT
*
*
*
TITEL    EQU   *
         MVI   OUT,X'8B'
         PUT   SYSPRINT,OUT
         MVI   OUT,C' '
         MVC   OUT+1(132),OUT
         MVI   OUT,X'19'
         MVC   OUT+1(65),=C'PDV032A:  EDIT FMT1 AND FMT2 DSCB OF ALL IS*
               AM DATA SETS ON VOLUME'
         MVC   OUT+67(6),VOLSER
         MVC   OUT+106(24),=C'DATE               PAGE'
         MVC   OUT+111(8),DATUM
         MVC   OUT+129(4),SCHAB2
         AP    SZEL,=P'1'
         ED    OUT+129(4),SZEL
         PUT   SYSPRINT,OUT
         MVI   OUT+1,C' '
         MVC   OUT+2(131),OUT+1
         MVC   OUT+11(35),=C'STATUS INDICATOR BIT-KOMBINATIONEN:'
         MVC   OUT+52(52),=C'.1.. ....   KEY SEQUENCE CHECKING IS TO BE*
                PERFORMED'
         MVI   OUT,X'09'
         PUT   SYSPRINT,OUT
         MVI   OUT+1,C' '
         MVC   OUT+2(131),OUT+1
         MVC   OUT+52(43),=C'..1. ....   INITIAL LOAD HAS BEEN COMPLETE*
               D'
         PUT   SYSPRINT,OUT
         MVI   OUT+1,C' '
         MVC   OUT+2(131),OUT+1
         MVC   OUT+52(27),=C'.... ..1.   LAST BLOCK FULL'
         PUT   SYSPRINT,OUT
         MVC   OUT+52(27),=C'.... ...1   LAST TRACK FULL'
         MVI   OUT,X'19'
         PUT   SYSPRINT,OUT
         MVI   OUT,C' '
         MVC   OUT+1(132),OUT
         MVI   OUT,X'09'
         MVC   OUT+1(132),=C'DATA SET NAME                       LRECL *
               BLOCK FMT KEY RKP     PRIME   OFLOW    ; IM  ANZ.TRKS  F*
               REE TRKS   FULL CYL-    STAT OPTCD'
         ZAP   ZZEL,=P'7'
         PUT   SYSPRINT,OUT
         MVI   OUT+1,C' '
         MVC   OUT+2(131),OUT+1
         MVI   OUT,X'11'
         MVC   OUT+53(74),=C'LEN       RECORDS RECORDS   OFLOW  P.CYLOF*
               L  IND.OFLOW   OFL-AREAS    IND.'
         PUT   SYSPRINT,OUT
         AP    ZZEL,=P'2'
         MVI   OUT,C' '
         MVC   OUT+1(132),OUT
         BR    R11
*
*
*
*
*
ENDE     EQU   *
         ZAP   SZEL,=P'0'
         ZAP   ZZEL,=P'70'
         CLOSE (VTOC)
         B     SEARCHDD
*
*
*
*
*
RETURN   EQU   *
         CLOSE (SYSPRINT)
         L     R15,STORE15
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
*
*
*
*
*
ERR1     EQU   *
         MVC   ERR1A+8(6),VOLSER
ERR1A    WTO   '      : REQUIRED VOLUME WAS NOT MOUNTED',ROUTCDE=11
         MVC   STORE15,=F'4'
         B     SEARCHDD
*
ERR2     EQU   *
         MVC   ERR2A+8(6),VOLSER
ERR2A    WTO   '      : NO VALID FMT4 DSCB ON VOLUME',ROUTCDE=11
         MVC   STORE15,=F'4'
         B     SEARCHDD
*
ERR3     EQU   *
         MVC   ERR3A+8(6),VOLSER
ERR3A    WTO   '      : PERMANENT I/O ERROR',ROUTCDE=11
         MVC   STORE15,=F'4'
         B     SEARCHDD
*
ERR4     EQU   *
         MVC   ERR4A+8(6),VOLSER
ERR4A    WTO   '      : INVALID WORKA POINTER',ROUTCDE=11
         MVC   STORE15,=F'4'
         B     SEARCHDD
*
ERR5     EQU   *
         WTO   'CCHHR NOT WITHIN BOUNDARIES OF VTOC EXTENT',ROUTCDE=11
         MVC   STORE15,=F'4'
         B     SEARCHDD
         EJECT
***********************************************************************
*                                                                     *
* DEFINITIONEN                                                        *
*                                                                     *
***********************************************************************
         SPACE 2
VTOC     DCB   DDNAME=VTOC,DSORG=PS,MACRF=(R),EODAD=ENDE,              *
               KEYLEN=44,BLKSIZE=96,EXLST=LIST1
*
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),                    *
               LRECL=133,BLKSIZE=2394,RECFM=FBM
*
LIST1    DC    AL1(128+7),AL3(JFCBAREA)
LIST3    CAMLST SEEK,CCHHR,VOLSER,WORKA
*
DATUM    DS    CL8
DDNAME   DS    CL8
VOLSER   DS    CL6
DSCB     DS    XL44
DATA     DS    XL96
CCHHR    DS    CL5
STORE15  DC    F'0'
SZEL     DC    PL2'0'
ZZEL     DC    PL2'70'
FULLW    DS    F
DW       DS    D
ZEILE    DS    0CL133
CTL      DS    CL1
ZDSN     DS    CL35
ZLRECL   DS    CL6
ZBLOCK   DS    CL6
ZBL1     DS    CL1
ZRECFM   DS    CL3
ZKEYLEN  DS    CL4
ZRKP     DS    CL4
ZPRIM    DS    CL10
ZBL2     DS    CL2
ZOVREC   DS    CL6
ZBL3     DS    CL1
ZPROZ    DS    CL7
ZSIGN    DS    CL1
ZBL4     DS    CL3
ZCYLOTR  DS    CL4
ZBL5     DS    CL5
ZFRTRK   DS    CL7
ZBL6     DS    CL4
ZFULCYL  DS    CL7
ZBL7     DS    CL7
ZIND     DS    CL2
ZBL8     DS    CL2
ZOPTCD   DS    CL5
*
OUT      DS    CL133
ZFELD    DS    CL4
RFELD    DS    CL11
SCHAB1   DC    XL6'402020202120'
SCHAB2   DC    XL4'40202120'
SCHAB3   DC    XL10'40206B2020206B202120'
SCHAB4   DC    XL7'402021206B2020'
SCHAB5   DC    XL7'4020206B202120'
*
TIOTAREA DC    F'0'
         DS    0F
JFCBAREA DS    CL176
         DS    0D
WORKA    DS    CL148
         LTORG
         END   PDV032A
