./ ADD NAME=CALLWTL
         MACRO
         CALLWTL   &PREFIX=
         LCLA  &NBR,&INDX,&LIMIT
         LCLB  &SWITCH
         LCLC  &D1,&D2,&D3,&T1,&T2,&PRF,&PARM,&DEF
         GBLC  &CSNAME
.*
.*       DIESES MACRO WIRD VOM PROGRAMMIERER AN GEEIGNETER STELLE
.*       IN EIN ASSEMBLER-PROGRAMM EINGEFUEGT.
.*       ES ERZEUGT AUS DATUM UND ZEIT EINEN EINDEUTIGEN CSECT-NAMEN,
.*       DER DEM MACRO CALLWTL2 UEBERGEBEN WIRD.
.*       DIESER CSECT-NAME BESTEHT AUS EINER 1-7-STELLIGEN ZAHL UND
.*       UND EINEM ALPHABETISCHEN PRAEFIX, DESSEN DEFAULTWERT
.*       SOWOHL UEBER KEYWORD-PARAMETER ALS AUCH UEBER SYSPARM
.*       UEBERSCHRIEBEN WERDEN KANN.
.*       DANACH WIRD EIN PROGRAMMTEIL GENERIERT, DER BEIM JEWEILS
.*       ERSTEN DURCHLAUF EIN CALL AUF DIESE CSECT BEWIRKT.
.*
.*
.*       DEFAULT-PRAEFIX SETZEN:
.*
&DEF     SETC  'P'
.*
.*
.*       DATUM UND TAGESZEIT AUSWERTEN:
.*
&D1      SETC  '&SYSDATE'(1,2)         MONAT
&D2      SETC  '&SYSDATE'(4,2)         TAG
&D3      SETC  '&SYSDATE'(8,1)         ENDZIFFER JAHR
&T1      SETC  '&SYSTIME'(1,2)         STUNDE
&T2      SETC  '&SYSTIME'(4,2)         MINUTE
.*
.*
.*       CSECT-NUMMER ERRECHNEN:
.*
&NBR     SETA  (((&D3*12+&D1-1)*31+&D2-1)*24+&T1)*60+&T2
.*
.*
.*       PREFIX UEBER SYSPARM DEFINIERT?
.*
         AIF   (K'&SYSPARM LT 8).KEYOP
&LIMIT   SETA  K'&SYSPARM-8
         AIF   ('&SYSPARM'(1,7) EQ 'PREFIX=').FOUND
.*
.*       SUCHEN ',PREFIX=' :
.*
.SEARCH  ANOP
&INDX    SETA  &INDX+1
         AIF   (&INDX GT &LIMIT).KEYOP
         AIF   ('&SYSPARM'(&INDX,8) NE ',PREFIX=').SEARCH
.*
.*       'PREFIX=' GEFUNDEN :
.*
.FOUND   ANOP
&PARM    SETC  '(SYSPARM)'
&PRF     SETC  '&SYSPARM'(&INDX+8,1)
         AIF   (&INDX GE &LIMIT).CTEST
&SWITCH  SETB  ('&SYSPARM'(&INDX+9,1) NE ',')
         AGO   .CTEST
.*
.*
.*       PREFIX UEBER KEYWORD-PARAMETER DEFINIERT?
.*
.KEYOP   ANOP
         AIF   (K'&PREFIX EQ 0).SETD
&PRF     SETC  '&PREFIX'(1,1)
&SWITCH  SETB  (K'&PREFIX GT 1)
.*
.*
.*       PREFIX AUF GUELTIGKEIT PRUEFEN:
.*
.CTEST   ANOP
         AIF   ('&PRF' EQ '@').SWTEST
         AIF   ('&PRF' EQ '#').SWTEST
         AIF   ('&PRF' EQ '$').SWTEST
         AIF   (('&PRF' GE 'A') AND ('&PRF' LE 'I')).SWTEST
         AIF   (('&PRF' GE 'J') AND ('&PRF' LE 'R')).SWTEST
         AIF   (('&PRF' GE 'S') AND ('&PRF' LE 'Z')).SWTEST
.*
         MNOTE *,'*** UNGUELTIGER PREFIX &PARM, DEFAULT &DEF BENUTZT ***
               *'
         AGO   .SETD
.*
.SWTEST  ANOP
         AIF   (NOT &SWITCH).GENER
         MNOTE *,'*** ANGEGEBENER PREFIX &PARM ZU LANG, ERSTES BYTE BEN*
               UTZT ***'
         AGO   .GENER
.*
.*
.*       PREFIX-DEFAULT EINSETZEN:
.*
.SETD    ANOP
&PRF     SETC  '&DEF'
.*
.*
.*       ASSEMBLER-ANWEISUNGEN GENERIEREN:
.*
.GENER   ANOP
&CSNAME  SETC  '&PRF'.'&NBR'
.*
         SPACE 2
         CNOP  0,4                     AUF VOLLWORTGRENZE
         NOP   *+28                    WEICHE ERSTER DURCHLAUF
         OI    *-3,X'F0'               WEICHE AUF BRANCH SETZEN
         B     *+8                     BRANCH UM KONSTANTE
         DC    V(&CSNAME)              ENTRY POINT ADRESSE
         L     15,*-4                  LADEN EP-ADRESSE
         LTR   15,15                   REFERENZ AUFGELOEST?
         BZ    *+6                     NEIN, KEIN CALL
         BALR  14,15                   BRANCH ZUM WTL-AUFRUF
         SPACE 2
         MEND
