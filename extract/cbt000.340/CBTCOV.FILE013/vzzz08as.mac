CSAMAP   CSECT
*******************************************************************
*
*        THIS MODULE IS INTENDED TO BE USED AS AN EXIT TO AMDPRDMP,
*        AND WILL OPERATE AS A VERB EXIT.  THE VERB 'CSAMAP' WILL
*        CAUSE THIS EXIT TO GET CONTROL, ONCE AN ENTRY IS ADDED
*        TO AMDPRECT.  THIS ROUTINE USES STANDARD PRINTDUMP LINKAGE
*        TO FORMAT AND PRINT CONTROL BLOCKS, PRINT MESSAGES, AND
*        TO ACCESS DUMPED DATA.  IT IS INTENDED TO BE USED TO
*        AID THE PSR IN DETERMINING A PROBLEM, WHEN EITHER CSA OR
*        SQA IS DEPLETED.  THE PROGRAM DOES NOT USE A GETMAINED
*        AREA FOR ITS' WORKAREA, SO IT IS NOT RE-ENTRANT, BUT THIS
*        SHOULD NOT PRESENT A PROBLEM, IF IT IS NOT PLACED IN LPA.
*        THE EXIT CHANGES SOME DATA IN THE WORKAREA, AFTER MOVING
*        THE DATA FROM PRINTDUMPS' BUFFER, FOR EASE OF FORMATTING.
*        WORKAREA LABELS MAY HAVE TO BE CHANGED, IF THE FOLLOWING MSS
*        CONTROL BLOCKS ARE CHANGED:
*               CVT, GDA, PQE, FBQE, SPQE, DQE, OR FQE.
*        THIS ROUTINE IS NOT FORMALLY SUPPORTED IN ANY WAY, BUT
*        I WOULD BE HAPPY TO ATTEMPT ANY CORRECTIONS, ON AN 'AS
*        AVAILABLE' BASIS.
*
*        FRANK H. CONNERY, JR., REGION 9, T.A.G.
*        (312) 245-7850   (IBM TIE LINE 8-261-7850)
*        REGION 9, TECHNICAL SUPPORT GROUP  (F.E.)
*        1 IBM PLAZA  (INTERNAL ZIP  38-528)
*        CHICAGO, ILLINOIS   60611
*
*******************************************************************
         EJECT
         ENTRY CSAMAP1
CSAMAP1  SAVE (14,12),,CSAMAP             SAVE REGISTERS
         BALR  12,0                       ESTABLISH BASE REGISTER
         USING *,12
         LA    7,CMAPSAV                  POINT TO MY SAVE AREA
         ST    13,CMAPSAV+4               ESTABLISH
         ST    7,8(,13)                    SAVE AREA LINKAGE
         ST    13,SAVPTR                  SAVE SAVE POINTER
         LR    13,7
         USING ABDPL,1                    ESTABLISH PARMLIST DSECT
         L     7,ADPLBUF                  LOCATE PRINT BUFFER
         MVC   52(15,7),TITLE             MOVE '***CSA' TITLE
         BAL   11,MSGSET                  AND PRINT IT
         BAL   11,BLNKPRNT                SPACE
         SR    8,8                        CLEAR REGISTER FOR INSERT
         ICM   8,7,ADPLCVT+1              GET ADDRESS OF DUMPED CVT
         BNZ   CVTOK                      BRANCH IF GIVEN
         LA    8,76(,0)                   ELSE SET FOR CVT POINTER
         BAL   11,GETBLOK                 AND GO GET POINTER
         LTR   15,15                      RETREIEVE OK ?
         BNZ   CVTBAD                     NO, ISSUE MSG AND EXIT
         L     8,0(,8)                    GET ADDRESS OF DUMPED CVT
         B     CVTOK                      CVT POINTER IN REG 8
CVTBAD   MVC   CBNAME,CVTCB               MOVE 'CVT' INTO MSG
CBMSGEND L     7,ADPLBUF                  LOCATE BUFFER
         MVC   0(47,7),CBUNAV             MOVE ENTIRE MESSAGE
         BAL   11,MSGSET                  LINK TO PRINT IT
EXIT     L     13,SAVPTR                  PICK UP PRIOR SAVE AREA
         RETURN (14,12),,RC=0             AND EXIT
CVTOK    LA    8,560(,8)                  PICK UP GDA
         BAL   11,GETBLOK                 GO GET POINTER
         LTR   15,15                      RETRIEVE OK ?
         BNZ   GDAUNAV                    NO, BRANCH
         L     8,0(,8)                    ELSE PICK UP POINTER
         ST    8,GDAPTR                   SET POINTER
         BAL   11,GETBLOK                 AND REQUEST GDA
         LTR   15,15                      RETRIEVE OK ?
         BZ    GDAOK                      YES, BRANCH
GDAUNAV  MVC   CBNAME,GDACB               ELSE MOVE 'GDA' INTO MSG
         B     CBMSGEND                   BR TO PRINT, THEN QUIT
GDAOK    MVC   MYGDA(56),0(8)             MOVE GDA TO WORKAREA
         LA    2,MYGDA                    POINT TO MOVED GDA
         USING GDA,2
         LA    0,GDAPATN                  SET PATTERN FOR GDA
         BAL   11,FRMTPRNT                GO F/P
         B     PQEPROC                    GO PROCESS PQE
         DS    0F
GDAPATN  DC    X'1F220005'                GDA  XXXXXX       (0)
         DC    AL4(GDALBL)
         DC    AL4(GDAPTR+1)
         DC    X'1D300C12'                FLAG  XX          (12)
         DC    AL4(MYGDA)
         DC    X'1D62161F'                CSA PQE  XXXXXX   (22)
         DC    AL4(MYGDA+9)
         DC    X'1D42272E'                PSTRT  XXXXXX     (39)
         DC    AL4(MYGDA+17)
         DC    X'1D32363C'                PSZE  XXXXXX      (54)
         DC    AL4(MYGDA+21)
         DC    X'1D72444E'                SQA SPQE XXXXXX   (68)
         DC    AL4(MYGDA+25)
         DC    X'1D725660'                FREE SQA  XXXXXX  (86)
         DC    AL4(MYGDA+29)
         DC    X'1D726872'                CSA SPQE  XXXXXX  (104)
         DC    AL4(MYGDA+53)
         DC    F'0'
GDALBL   DC    C'GDAFLAGCSA PQEPSTRTPSZESQA SPQEFREE SQACSA SPQE'
         DS    0F
PQEPROC  BAL   11,BLNKPRNT                SPACE OUTPUT
         L     8,CSAPQEP                  GET CSA PQE POINTER
         ST    8,PQEPTR                   SAVE POINTER FOR OUTPUT
         BAL   11,GETBLOK                 GO GET PQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    CPQEOK                     YES, BRANCH
         MVC   CBNAME,CPQECB              ELSE MOVE 'CSA PQE' TO MSG
         BAL   11,CBMSG                   AND PRINT IT
         B     SPQEPROC                   THEN GO PROCESS SPQE'S
CPQEOK   MVC   MYPQE(32),0(8)             MOVE PQE TO WORKAREA
         LA    0,PQEPATN                  PICK UP PATTERN FOR PQE
         BAL   11,FRMTPRNT                GO TO F/P
         B     FBQEPROC                   THEN GO PROCESS FBQE'S
PQEPATN  DS    0F
         DC    X'1F220005'                PQE  XXXXXX       (0)
         DC    AL4(PQELBL)
         DC    AL4(PQEPTR+1)
         DC    X'1D420D14'                CSTRT  XXXXXX     (13)
         DC    AL4(MYPQE+25)
         DC    X'1D421C23'                CSIZE  XXXXXX     (28)
         DC    AL4(MYPQE+21)
         DC    F'0'
PQELBL   DC    C'PQECSTRTCSIZE'
         DS    0F
FBQEPROC LA    7,50(,0)                   SET LOOP COUNT
FBQELOOP SR    8,8                        CLEAR ACCESS REGISTER
         ICM   8,7,MYPQE+1                GET FBQE, IF ANY
         BZ    SPQEPROC                   BRANCH IF NONE
         CLC   CSAPQEP+1(3),MYPQE+1       DOES IT POINT TO PQE ?
         BZ    SPQEPROC                   YES, GET OUT
         BAL   11,GETBLOK                 ELSE GO GET FBQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    FBQECNT                    YES, BR TO CHECK COUNT
         MVC   CBNAME,CFBQECB             ELSE MOVE 'FBQE' TO MSG
         BAL   11,CBMSG                   AND PRINT MSG
         B     SPQEPROC                   THEN GO PROCESS SPQE'S
FBQECNT  BCT   7,FBQEOK                   BR, IF MAX NOT REACHED
         MVC   CBLNAME,CFBQECB            ELSE MOVE NAME TO LP MSG
         BAL   11,LOOPMSG                 AND LINK TO PRINT IT
         B     SPQEPROC                   THEN GO PROCESS SPQE'S
FBQEOK   MVC   MYFBQE(16),0(8)            MOVE FBQE TO WORKAREA
         LA    0,FBQEPATN                 PICK UP PATTERN
         BAL   11,FRMTPRNT                LINK TO F/P ROUTINE
         MVC   MYPQE+1(3),MYFBQE+1        SET UP NEXT FBQE
         B     FBQELOOP                   AND GO PROCESS NEXT
         DS    0F
FBQEPATN DC    X'1F322B31'                FBQE  XXXXXX      (43)
         DC    AL4(FBQELBL)
         DC    AL4(MYPQE+1)
         DC    X'1D32393F'                AREA  XXXXXX      (57)
         DC    AL4(MYFBQE+13)
         DC    X'1D22474C'                SZE XXXXXX        (71)
         DC    AL4(MYFBQE+9)
         DC    F'0'
FBQELBL  DC    C'FBQEAREASZE'
         DS    0F
SPQEPROC MVC   SPQEPTR,CSASPQEP           SET FIRST CSA SPQE
         MVI   SPQEPTR,X'00'              ENSURE FIRST BYTE ZERO
         L     7,ADPLBUF                  LOCATE PRINT BUFFER
         MVC   0(22,7),CMSSBLK            MOVE 'CSA MSS BLOCKS' MSG
         BAL   11,MSGSET                  LINK TO PRINT IT
         BAL   11,BLNKPRNT                SPACE OUTPUT
         BAL   7,MSSROUTN                 LINK TO FORMAT BLOCKS
         L     7,ADPLBUF                  LOCATE PRINT BUFFER
         MVC   0(22,7),SMSSBLK            MOVE 'SQA MSS BLOCKS' MSG
         BAL   11,MSGSET                  LINK TO PRINT IT
         BAL   11,BLNKPRNT                SPACE OUTPUT
         MVC   MAXSPQE(12),SQACT          CHANGE MAX CTS FOR SQA
         MVC   SPQEPTR,SQASPQEP           SET FIRST SQA SPQE
         MVI   SPQEPTR,X'00'              ENSURE HI BYTE ZERO
         BAL   7,MSSROUTN                 LINK TO FORMAT MSS BLOCKS
         BAL   11,BLNKPRNT                SPACE OUTPUT
         L     7,ADPLBUF                  LOCATE PRINT BUFFER
         MVC   0(24,7),COMPMSG            MOVE COMPLETION MSG
         BAL   11,MSGSET                  LINK TO PRINT IT
         B     EXIT                       ALL DONE, RETURN TO PRTDMP
COMPMSG  DC    C'*** CSA MAP COMPLETE ***'
         DS    0F
         EJECT
********************************************************************
*
*        SUBROUTINE TO FORMAT MSS CONTROL BLOCKS.  THIS ROUTINE WILL
*        FORMAT SPQE'S, DQE'S, AND FQE'S, AND IS CALLED FOR BOTH
*        CSA AND FOR SQA.  A PATTERN IS SET UP, DEPENDING ONTHE
*        BLOCKS ACTUALLY FOUND, AND THE FORMAT/PRINT ROUTINE (FPROUT)
*        IS CALLED.  FPROUT WILL FORMAT THE MOVED MSS BLOCKS,DE-
*        PENDING ON THE PATTERN.  PATTERN IS A BYTE, '000SDF00', TO
*        ALLOW INDEXING TO PRTDMP PATTERN. (S = SPQE, D = DQE, AND
*        F = FQE).  THIS ALLOWS A COMMAN FORMAT/PRINT ROUTINE TO PRINT
*        THE BLOCKS, REGARDLESS OF WHICH, IF ANY, ARE PRESENT
*        (IF NO BLOCKS WERE GOTTEN, INDEX WILL BE ZERO, AND FPROUT
*        WILL RETURN TO CALLER.
*
******************************************************************
         SPACE 2
MSSROUTN LM    8,10,MAXSPQE               PICK UP MAX COUNTS
         STM   8,10,SPQECT                AND INITIALIZE LOOP COUNTS
SPQELP   SR    8,8                        CLEAR REG FOR INSERT
         ICM   8,7,SPQEPTR+1              GET SPQE POINTER
         BCR   8,7                        RETURN IF NONE
         MVC   DQECT(8),MAXDQE            SET NEW MAX CTS, DQE & FQE
         MVI   PATTERN,SDFPAT             SET PATTERN TO ALL
         BAL   11,GETBLOK                 GO GET SPQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    TESTSCNT                   YES, GO TEST COUNT
         MVC   CBNAME,SPQECB              ELSE MOVE 'SPQE' TO MSG
         BAL   11,CBMSG                   LINK TO PRINT IT
         BR    7                          AND RETURN
TESTSCNT L     6,SPQECT                   PICK UP REMAINING COUNT
         BCT   6,SCNTOK                   BR IF NOT EXCEEDED
         MVC   CBLNAME,SPQECB             ELSE MOVE 'SPQE' TO LP MSG
         BAL   11,LOOPMSG                 LINK TO PRINT IT
         BR    7                          AND RETURN
SCNTOK   ST    6,SPQECT                   STORE DECREMENTED COUNT
         MVC   SPQE(12),0(8)              MOVE SPQE TO WORKAREA
         TM    FLGSPQE,X'80'              SHARED ?
         BZ    DQEONE                     NO, GO TO DQE PROCESS
         MVI   PATTERN,SPAT               SET TO F/P SPQE ONLY
         BAL   11,FPROUT                  LINK TO PRINT IT
         MVC   SPQEPTR+1(3),NSPQE+1       SET UP NEXT SPQE
         B     SPQELP                     GO TO SPQE PROCESS
DQELP    MVC   FQECT,MAXFQE               NEW DQE, RESET FQE COUNT
         MVI   PATTERN,DFPAT              SAME SPQE, SO D & F ONLY
DQEONE   SR    8,8                        CLEAR FOR INSERT
         ICM   8,7,DQEPTR+1               GET DQE ADDRESS
         BNZ   DQEXIST                    BR IF ONE EXISTS
DQEBAD   NI    PATTERN,SPAT               T/F ALL BUT SPQE
         BAL   11,FPROUT                  AND LINK TO F/PRINT
         MVC   SPQEPTR+1(3),NSPQE+1       SET UP NEXT SPQE
         B     SPQELP                     AND GO PROCESS IT
DQEXIST  BAL   11,GETBLOK                 GO GET DQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    TESTDCNT                   YES, BR TO CHECK COUNT
         MVC   CBNAME,DQECB               ELSE MOVE 'DQE' TO MSG
         BAL   11,CBMSG                   & LINK TO PRINT IT
         B     DQEBAD                     BR OUTPUT & GET NEXT SPQE
TESTDCNT L     6,DQECT                    PICK UP COUNT OF DQE'S
         BCT   6,DCNTOK                   BR IF COUNT NOT EXCEEDED
         MVC   CBLNAME,DQECB              ELSE PUT 'DQE' LOOP MSG
         BAL   11,LOOPMSG                 & LINK TO PRINT IT
         B     DQEBAD                     BR TO F/P & GET NXT SPQE
DCNTOK   ST    6,DQECT                    STORE DECREMENTED COUNT
         MVC   DQE(16),0(8)               MOVE DQE TO WORKAREA
         B     FQEONE                     GO TO PROCESS FQE
FQELP    MVI   PATTERN,FPAT               F/P FQE ONLY FOR ADDNL FQE
FQEONE   SR    8,8                        CLEAR REG FOR INSE
         ICM   8,7,FQEPTR+1               FQE ?
         BNZ   FQEXIST                    YES, BRANCH
FQEBAD   NI    PATTERN,SDPAT              T/F FQE IN PATTERN
         BAL   11,FPROUT                  LINK TO F/P ANY OTHERS
         MVC   DQEPTR+1(3),NDQE+1         SET NEXT DQE, IF ANY
         B     DQELP                      AND BRANCH TO PROCESS
FQEXIST  BAL   11,GETBLOK                 GO GET FQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    TESTFCNT                   YES, BR TO CHECK COUNT
         MVC   CBNAME,FQECB               ELSE PUT 'FQE' IN MSG
         BAL   11,CBMSG                   AND LINK TO PRINT IT
         B     FQEBAD                     BR F/P & GET NXT DQE
TESTFCNT L     6,FQECT                    PICK UP FQE COUNT
         BCT   6,FCNTOK                   BR IF NOT EXCEEDED
         MVC   CBLNAME,FQECB              ELSE MOVE 'FQE' TO LOOP MSG
         BAL   11,LOOPMSG                 LINK TO PRINT IT,
         B     FQEBAD                     BR TO F/P & GET NXT DQE
FCNTOK   ST    6,FQECT                    STORE DECREMENTED COUNT
         MVC   FQE(12),0(8)               MOVE FQE TO WORKAREA
SP245    CLI   SUBPOOL,X'F5'              SQA 'SHORT' FQE ?
         BNE   FQEVALCK                   NO, CK AND SET VALID
         L     8,FQEPTR                   ELSE GET FQE ADDRESS
         LA    8,8(,8)                    ADD 8 TO GET TOP
         STCM  8,7,HIADDR+1               AND SET TOP OF FREE SPACE
         B     FQEVALID                   BYPASS VALIDITY CK
FQEVALCK SR    8,8                        CLEAR REG FOR INSERT
         ICM   8,7,BLKADDR                GET BLK ADR FROM DQE
         C     8,HIADDR                   FREE SPACE BELOW BLOCK ?
         BH    FQEFINCK                   YES, CK FOR 'SHORT' FQE
         A     8,BLKLNGTH                 ELSE ADD FOR TOP OF BLK
         C     8,HIADDR                   FREE SPACE ABOVE BLK ?
         BNL   FQEVALID                   BR, NOT 'SHORT' FQE
FQEFINCK CLC   FQEPTR+1(3),BLKADDR        FQE WITHIN BLOCK ?
         BL    FQEBAD                     BR NO, INVALID FQE
         SR    8,8                        CLEAR REG FOR INSERT
         ICM   8,7,BLKADDR                GET ADDRESS OF BLOCK
         A     8,BLKLNGTH                 POINT TO TOP OF AREA
         CLM   8,7,FQEPTR+1               FQE WITHIN BLOCK ?
         BL    FQEBAD                     NO, DON'T F/P
         L     8,FQEPTR                   ELSE GET FQE ADDRESS
         LA    8,8(,8)                    BUMP FOR TOP OF FQE
         STCM  8,7,HIADDR+1               AND DUMMY UP FOR SHORT FQE
FQEVALID BAL   11,FPROUT                  LINK TO FORMAT/PRINT
         MVC   FQEPTR+1(3),NFQE           SET NXT FQE, IF ANY
         B     FQELP                      AND GO PROCESS
         EJECT
******************************************************************
*
*        FORMAT/PRINT ROUTINE. SETS UP CORRECT PATTERN FOR AMDPRDMP
*        FORMAT ROUTINE, THEN CALLS THE FORMAT ROUTINE, AND, ON
*        RETURN, CALLS THE PRINT ROUTINE OF AMDPRDMP.  THE MAIN
*        INPUT IS THE PATTERN MASK, WHICH HAS BEEN SET BY THE MSS
*        ROUTINE.  IF YOU WISH TO CHANGE THE PRINTED OUTPUT IN
*        ANY WAY, YOU MAY MODIFY THE PRINT PATTERNS.  NOTE THAT
*        THERE ARE ACTUALLY SIX PATTERNS, AS FOLLOWS:
*
*        SDFPATN--PATTERN USED TO OUTPUT SPQE, DQE, & FQE
*        DFPATN---USED ON SUBSEQUENT DQE'S (SPQE PRINTED ON FIRST)
*        FPATN----USED FOR SUBSEQUENT FQE'S
*        SDPATN---USED WHEN NO FQE'S ON FIRST DQE
*        SPATN----USED WHEN NO DQE'S ON SPQE, OR SHARED SPQE
*        DPATN----USED ON SUBSEQUENT DQE WITH NO FQE'S
*
*        TEST IS MADE FOR ZERO PATTERN, AND FPROUT WILL RETURN, IF
*        SO. (NO MSS CONTROL BLOCKS TO BE FORMATTED.)
*
******************************************************************
         SPACE  2
*        PATTERN MASK
SDFPAT   EQU   X'1C'                      SPQE, DQE, AND FQE
DFPAT    EQU   X'0C'                      DQE AND FQE
FPAT     EQU   X'04'                      FQE ONLY
SDPAT    EQU   X'18'                      SPQE AND DQE
DPAT     EQU   X'08'                      DQE ONLY
SPAT     EQU   X'10'                      SPQE ONLY
FULLPAT  EQU   X'1C'                      = SDFPAT, AT THIS TIME
         SPACE  1
*        NOTE THAT X'14' PATTERN NOT PRESENT.  SPQE AND FQE WITHOUT
*        A DQE WOULD NOT OCCUR.
         SPACE  1
FPROUT   SR    5,5                        CLEAR INDEX REG
         ICM   5,1,PATTERN                GET AND CHECK PATTERN MASK
         BCR   8,11                       RETURN IF ZERO
         LA    6,PATTAB                   POINT TO TABLE OF ADCON'S
         L     0,0(5,6)                   USE INDEX TO GET ENTRY
FRMTPRNT L     15,ADPLFRMT                GET EP PRTDMP FORMAT
         BALR   14,15                     LINK TO IT, R0 = PATTERN
BLNKPRNT L     15,ADPLPRNT                GET PRINT ROUTINE E.P.
         BALR  14,15                      LINK TO IT
         BR    11                         RETURN TO CALLER
         SPACE  2
         DS    0F
*        ADCONS TO DIFFERENT FORMAT PATTERNS
         SPACE  1
PATTAB   DC     AL4(SDFPATN)              SHOULD NEVER BE USED
         DC    AL4(FPATN)
         DC    AL4(DPATN)
         DC    AL4(DFPATN)
         DC    AL4(SPATN)
         DC    AL4(SDFPATN)               SHOULD NEVER BE USED
         DC    AL4(SDPATN)
         DC    AL4(SDFPATN)
         SPACE  3
*        THESE ARE THE ACTUAL PATTERNS USED TO FORMAT THE MSS DATA.
*        THE DATA TO BE FORMATTED WILL BE IN FIELDS IN 'MSSDATA'.
*        MSSDATA LABELS MAY HAVE TO BE CHANGED, AND/OR THE AREA
*        RECODED, IF THE CONTROL BLOCKS CHANGE.  DSECT LAYOUT WAS
*        NOT USED, AS USING STATEMENTS WOULD HAVE 'TIED UP' THE
*        REGISTERS.  EACH COMPLETE PATTERN ENDS WITH A FULL WORD OF
*        ZEROES, AND ALTERNATE LABLES ARE USED TO FORMAT PARTIAL
*        LINES.  (EXAMPLE: DFPATN)
*
*
         SPACE  2
SDFPATN  DS    0F
         DC    X'1F320006'
         DC    AL4(SPQELBL)
         DC    AL4(SPQEPTR+1)
         DC    X'1D300E14'
         DC    AL4(FLGSPQE)
         DC    X'1D10181C'
         DC    AL4(SUBPOOL)
         DC    X'15202025'
DFPATN   DC    X'1F22292E'
         DC    AL4(DQELBL)
         DC    AL4(DQEPTR+1)
         DC    X'1D22363B'
         DC    AL4(BLKADDR)
         DC    X'1D224348'
         DC    AL4(BLKLNGTH+1)
FPATN    DC    X'1F225055'
         DC    AL4(FQELBL)
         DC    AL4(FQEPTR+1)
         DC    X'1D525D65'
         DC    AL4(HIADDR+1)
         DC    X'1D126D71'
         DC    AL4(FREEBYTE+1)
         DC    F'0'
SDPATN   DC    X'1F320006'
         DC    AL4(SPQELBL)
         DC    AL4(SPQEPTR+1)
         DC    X'1D300E14'
         DC    AL4(FLGSPQE)
         DC    X'1D10181C'
         DC    AL4(SUBPOOL)
         DC    X'15202025'
DPATN    DC    X'1F22292E'
         DC    AL4(DQELBL)
         DC    AL4(DQEPTR+1)
         DC    X'1D22363B'
         DC    AL4(BLKADDR)
         DC    X'1D224348'
         DC    AL4(BLKLNGTH+1)
         DC    F'0'
SPATN    DC    X'1F320006'
         DC    AL4(SPQELBL)
         DC    AL4(SPQEPTR+1)
         DC    X'1D300E14'
         DC    AL4(FLGSPQE)
         DC    X'1D10181C'
         DC    AL4(SUBPOOL)
         DC    X'15202025'
         DC    F'0'
         EJECT
******************************************************************
*
*        SUBROUTINE TO GET A DUMPED PAGE.  USES REGISTERS 0,8,9,
*        14, AND 15.  INPUT IN REG 8 IS THE ADDRESS OF BLOCK TO BE
*        GOTTEN.  THIS ROUTINE SETS UP FOR PAGE ACCESS, AND, ON
*        RETURN, SETS REG 8 TO CORE LOCATION OF RETRIEVED CONTROL
*        BLOCK.  RETURN CODE IS PASSED IN REG 15 TO CALLER.
*        RC = 0, IF BLOCK SUCCESSFULLY RETRIEVED.
*
******************************************************************
         SPACE  2
GETBLOK  SRDL  8,12(0)                    SET PAGE ADDRESS
         SLL   8,12(0)                     IN REGISTER 8
         SRL   9,20(0)                     DISPLACEMENT IN REG 9
         LR    0,8                        SET 0 FOR ACCESS
         L     15,ADPLMEMA                GET ACCESS ROUTINE E.P.
         BALR  14,15                      AND GO GET THE PAGE
         LR    8,0                        TRANSFER CORE LOCATION
         AR    8,9                        ADD DISPLACEMENT BACK
         BR    11                         AND RETURN, REG 8 SET
         SPACE  2
*        MESSAGE ROUTINES
         SPACE  2
LOOPMSG  L     6,ADPLBUF                   GET PRINT BUFFER
         MVC   0(25,6),CBLOOPMS           MOVE 'POSSIBLE LOOP' MSG
         B     MSGSET                     BR TO OUTPUT MSG
CBMSG    L     6,ADPLBUF                  GET PRINT BUFFER
         MVC   0(25,6),CBUNAV             MOVE 'UNAVAILABLE' MSG
MSGSET   L     15,ADPLPRNT                GET PRINT ROUTINE E.P.
         BALR  14,15                      LINK TO IT
         BR    11                         AND THEN RETURN
         SPACE  2
*        MESSAGES
         SPACE  2
TITLE    DC    C'*** CSA MAP ***'
CBUNAV   DC    C'UNABLE TO ACCESS '
CBNAME   DC    C'BLOCK   '
         DC    C'CSAMAP TERMINATING    '
CBLOOPMS DC    C'POSSIBLE LOOP IN '
CBLNAME  DC    C'BLOCK   '
CMSSBLK  DC    C'*** CSA MSS BLOCKS ***'
SMSSBLK  DC    C'*** SQA MSS BLOCKS ***'
         EJECT
MSSDATA  DS    0F
SPQEPTR  DC    F'0'
SPQE     EQU   *
NSPQE    DC    F'0'
DQEPTR   DC    F'0'
FLGSPQE  DC    XL1'00'
RSVDSPQE DC    XL1'00'
SUBPOOL  DC    XL1'00'
KEY      DC    XL1'00'
DQE      EQU   *
FQEPTR   DC    F'0'
NDQE     DC    F'0'
HIARCH   DC    XL1'00'
BLKADDR  DC    XL3'00'
BLKLNGTH DC    F'0'
FQE      EQU   *
FLGFQE   DC    XL1'00'
NFQE     DC    XL3'00'
FREEBYTE DC    F'0'
HIADDR   DC    F'0'
         SPACE  2
*        FOLLOWING IS PATTERN MASK, SET BY CALLERS OF FPROUT.
         SPACE  2
PATTERN  DC    XL1'1C'                    INITIALIZED TO SDF
         SPACE  2
*        FOLLOWING ARE LABELS FOR AMDPRDMP FORMAT ROUTINE.
SPQELBL  DC    C'SPQEFLAGSPKEY'
DQELBL   DC    C'DQEBLKLNG'
FQELBL   DC    C'FQEHI/ADRLN'
         EJECT
*        FOLLOWING IS THE WORKAREA.
         SPACE  3
CSAWORK  DS    0F                         ALLIGN TO WORD
CMAPSAV  DS    18F
SAVPTR   DS    1F
GDAPTR   DC    F'0'
MYGDA    DS    14F                        MOVED GDA
PQEPTR   DC    F'0'
MYPQE    DS    8F                         MOVED PQE (CSA)
MYFBQE   DS    4F'0'                      MOVED FBQE
MAXSPQE  DC    F'100'                     CSA SPQE MAX COUNT
MAXDQE   DC    F'100'                     CSA MAX DQE COUNT
MAXFQE   DC    F'100'                     CSA MAX FQE COUNT
SPQECT   DS    1F                         OPERATIONAL SPQE COUNT
DQECT    DS    1F                         OPERATIONAL DQE COUNT
FQECT    DS    1F                         OPERATIONAL FQE COUNT
SQACT    DC    F'5'                       SQA MAX COUNTS - SPQE
         DC    F'50'                                     - DQE
         DC    F'700'                                    - FQE
         EJECT
CVTCB    DC    CL8'CVT'                   LABELS FOR MESSAGES
GDACB    DC    CL8'GDA'
SPQECB   DC    CL8'SPQE'
DQECB    DC    CL8'DQE'
FQECB    DC    CL8'FQE'
CPQECB   DC    CL8'CSA PQE'
CFBQECB  DC    CL8'CSA FBQE'
*        DSECTS FOR LABEL REFERENCES FO ADPL AND GDA
         IHAABDPL
         IHAGDA
         END   CSAMAP1
//**********************************************************************
//**********************************************************************
//**********************************************************************
//**********************************************************************
//**********************************************************************
//**********************************************************************
//**********************************************************************
//**********************************************************************
//**********************************************************************
//SYSLIB DD DSN=SYS1.NUCLEUS
* ----------------------------------------------------------------------
*   THIS TRAP WILL ENABLE YOU TO GET A DUMP CLOSER TO THE TIME OF      *
*   A SQA FQE BEING OVERLAID.  WRITTEN WITH MP/AP CONSIDERATIONS.      *
*                                                                      *
*   REGISTERS AT TIME OF ERROR:                                        *
*     REG0 = WORK REGISTER            REG8 = START OF FREE AREA        *
*     REG1 = BALR REGISTER            REG9 = PREVIOUS FQE              *
*     REG2 = SPQE ADDR FOR SQA        REGA = PREVIOUS DQE              *
*     REG3 = CURRENT DQE ADDR.        REGB = WORK REGISTER             *
*     REG4 = START OF PREVIOUS FREE   REGC = WORK REGISTER             *
*            AREA (OR END OF DQE)                                      *
*     REG5 = CURRENT FQE ADDR.        REGD = WORK REGISTER             *
*     REG6 = START OF DQE AREA        REGE = BALR REGISTER             *
*     REG7 = END OF FREE AREA         REGF = ADDR OF THE LDA           *
*                                                                      *
*     NOTE:   IEAVFX00+X'880' THRU X'A22' MUST BE ZEROES PRIOR TO      *
*             APPLYING THIS TRAP                                       *
* ----------------------------------------------------------------------
NAME IEANUC01 IEAVGM00    (PRE UZ34680)
VER 088E 4160,0080
REP 088E 45E0,091C        * BRANCH TO CHECK THE SQAFQE'S
NAME IEANUC01 IEAVGM01                (PRE UZ34680)
VER 00AE 47F0,80A8
REP 00AE 47F0,0A18        * BRANCH TO PATCH TO CLEAR DUMMYDQE
NAME IEANUC01 IEAVFX00
VER 0880 0000,0000          START OF S/ZAP TRAP CODE
VER 0AB0 0000,0000          END OF S/ZAP TRAP CODE
* PATCH1
* CODE TO VALIDITY CHECK THE SQA FQES
REP 0900 C7D4,FFFF          TRACE TABLE ENTRY ID
REP 0904 0000,0001          SQA/FQE CHAIN LAST CHECKED
REP 0908 0000,0008          LENGTH OF FQE
REP 091C 5860,004C          GET ADDRESS OF CVT
REP 0920 9110,60B6          IS NIP ACTIVE?
REP 0924 4710,09AA        * YES, RETURN
REP 0928 900F,444C          SAVE REGISTERS IN THE LDA
REP 092C 4590,DB10          BALR TO GET THE SALLOC LOCK
REP 0930 18F4               SAVE THE POINTER TO THE LDA IN REG 15
REP 0932 5820,6230          GET ADDRESS OF THE GDA
REP 0936 5820,2018          GET SQA SPQE
REP 093A 5830,2004          GET DQE ADDRESS
REP 093E 1233               IS IT ZERO?
REP 0940 4780,0996        * YES, BRANCH TO FREE SALLOC LOCK AND RETURN
REP 0944 5840,3008          GET DQE BLK ADDRESS
REP 0948 5E40,300C          ADD DQE LENGTH
REP 094C 5850,3000          GET FIRST FQE
REP 0950 1255               IS IT ZERO?
REP 0952 4780,098A        * YES, RETURN
REP 0956 5860,3008          GET DQE BLK START ADDRESS
REP 095A 1956               IS FQE BELOW DQE START ADDRESS
REP 095C 4740,09DC        * YES, ERROR ---------------------------------
REP 0960 1875               PUT FQE ADDRESS IN REG6
REP 0962 5E70,0908        * GET END OF FREE AREA
REP 0966 1974               IS END OF FREE AREA HIGHER REG 4?
REP 0968 4720,09DC        * YES, ERROR ---------------------------------
REP 096C 1887               PUT END OF FQE IN REG 7
REP 096E 5F80,5004          SUBTRACT LENGTH OF FQE
REP 0972 1986               IS START OF FQE BELOW START OF DQE?
REP 0974 4740,09DC        * YES, ERROR ---------------------------------
REP 0978 1987               IS FQE START BELOW END OF FQE?
REP 097A 47B0,09DC        * NO, ERROR ----------------------------------
REP 097E 1848               SET REG4 EQUAL TO START OF FREE AREA
REP 0980 1895               SET REG 9 EQUAL TO PREVIOUS DQE
REP 0982 5850,5000          GET NEXT FQE TO CHECK
REP 0986 47F0,0950        * CHECK NEXT FQE
REP 098A 18A3               SET REG A TO PREVIOUS DQE
REP 098C 1B99               SET PREVIOUS FQE TO ZERO
REP 098E 5830,3004          GET NEXT DQE
REP 0992 47F0,093E          CHECK NEXT DQE
REP 0996 5810,0224          GET THE ADDR OF THE CURRENT ASCB
* REP 099A E50D,1024,0900   * MAKE TRACE ENTRY (MICRO-CODE TRACE INSTR)
REP 099A 4590,0A40,0700   * MAKE TRACE ENTRY (NON-SE SYSTEMS)
REP 09A0 184F               RESTORE THE LDA POINTER
REP 09A2 4590,DB5E          RELEASE THE SALLOC LOCK
REP 09A6 980F,444C          RESTORE THE REGISTERS
REP 09AA 4160,0080          REPLACE OVERLAID INSTRUCTION
REP 09AE 07FE               RETURN TO IEAVGM00
* SYSTEM STOP CODE
REP 09DC 0700,0700          UNUSED INSTRUCTION
REP 09E0 900F,0880        * SAVE REGISTERS
REP 09E4 58F0,0010          LOAD ADDRESS OF CVT
REP 09E8 58F0,F294          LOAD ADDRESS OF CSD
REP 09EC 91C0,F008          IS IT AN AP/MP?
REP 09F0 47E0,0A04        * NO, BRANCH TO UP STOP CODE
REP 09F4 48F0,0204          GET PHYSICAL CPU ID FOR THIS CPU
REP 09F8 57F0,0A14        * FLIP ID TO OTHER CPU
REP 09FC AE0F,0009          SIGP OTHER CPU TO STOP
REP 0A00 4760,09FC        * LOOP IF OTHER CPU IS BUSY
REP 0A04 8200,0A08        * LOAD WAIT IN THIS CPU
REP 0A08 0002,0000          DEAD WAIT PSW
REP 0A0C 0000,DEAD          DEAD WAIT PSW
REP 0A14 0000,0001          X/OR MASK TO FLIP CPU IDS
* CODE TO CLEAR OUT THE DUMMY DQE
REP 0A18 D70F,1000,1000     SET THE DUMMY DQE TO ZEROES
REP 0A1E 47F0,80A8       ** RETURN TO IEAVGM00
*
* NON-SE TRACE ROUTINE.
*   BUILDS A TRACE ENTRY AS FOLLOWS:
*   + 0           + 04            + 08             + 0C           + 10
*   +----------------+---------------+----------------+--------------+
*   ×  ID1  :  ID2   ×   LDARQSTA    ×    R3 FROM     ×   R0 FROM    ×
*   × C'GM' : X'FFFF'×   LDA +X'10'  ×   LDA +X'218'  ×  LDA +X'20C' ×
*   ×       :        ×               ×                ×              ×
*   +----------------+---------------+----------------+--------------+
*
*   + 10          + 14            + 18             + 1C           + 20
*   +----------------+---------------+----------------+--------------+
*   ×    R1 FROM     ×**1:CPU:       ×    TCB FROM    ×   R14 FROM   ×
*   ×   LDA +X'210'  ×   :ID : ASID  ×   PSA +X'218'  ×  LDA +X'244' ×
*   ×                ×   :   :       ×                ×              ×
*   +----------------+---------------+----------------+--------------+
*
*   WHERE: **1 - IS FREESW FROM THE LDA +X'1D'
*
REP 0A40 5810,0010        GET CVT PTR
REP 0A44 95FA,1191        TRACE ACTIVE?
REP 0A48 0779,0700        NO - RETURN
REP 0A4C 5820,0054        GET TRACE TBL PTR
REP 0A50 5810,2000        GET CURRENT PTR
REP 0A54 1831,0700        SAVE IN REG 3
REP 0A58 4110,1020        BUMP TO NEXT ENTRY
REP 0A5C 5510,2008        TRACE TBL WRAP?
REP 0A60 47B0,0AAE      * YES, UPDATE POINTERS
REP 0A64 BA31,2000        DID OTHER SIDE GET CURRENT PTR?
REP 0A68 4770,0A50      * YES, TRY AGAIN
REP 0A6C D71F,1000,1000   ZERO TRACE ENTRY
REP 0A72 D203,1000,0900 * MOVE TRACE ID TO ENTRY
REP 0A78 D203,1004,F010   MOVE LDARQSTA TO ENTRY
REP 0A7E D203,1008,F218   MOVE R3 TO ENTRY
REP 0A84 D207,100C,F20C   MOVE R0 AND R1 TO ENTRY
REP 0A8A D200,1014,F01D   MOVE FREESW TO ENTRY
REP 0A90 D200,1015,0205   MOVE PHYSICAL CPU ID TO ENTRY
REP 0A96 5820,0224        GET ASCB ADD         81/05/18 TRF
REP 0A9A D201,1016,2024   MOVE ASID TO ENTRY
REP 0AA0 D203,1018,0218   MOVE CURRENT TCB ADD TO ENTRY
REP 0AA6 D203,101C,F244   MOVE R14 TO ENTRY
REP 0AAC 07F9             RETURN TO CALLER
REP 0AAE 5810,2004        GET TRACE START POINTER
REP 0AB2 47F0,0A64      * SET TO CURRENT BY CS INST.
*
* **  X'880' TO X'8BC'  -  REGISTER SAVE AREA AT TIME OF ERROR
* ***THE ASTERISK PRECEEDING A COMMENT INDICATES AN INSTRUCTION TO
*    BE MODIFIED IF TRAP IS RELOCATED IN PSA
*    THE DOUBLE ASTERISKS PRECEEDING A COMMENT INDICATES AN INSTRUCTION
*    TO BE MODIFIED FOR A DIFFERENT PTF LEVEL OF IEAVGM00
*
* 81/03/09    REWRITTEN AND TESTED
* 81/05/13    REWRITTEN AND TESTED
* 81/05/15    MODIFIED NON-SE TRACE
* 81/05/18    MODIFIED REP AT 0A96
++PTF (PK00001).
++VER (Z037) PRE (UZ28870).
++VER (Z037) PRE (UZ30572).
++VER (Z038) FMID (EBB1102) PRE(UZ27118).
++VER (Z038) FMID (EBB1102) PRE(UZ28870).
++VER (Z038) FMID (EBB1102) PRE(UZ30572).
++VER (Z038) FMID (EBB1102) PRE UZ31872).
++VER (Z038) FMID (EBB1102) PRE(UZ34680).
++VER (Z038) FMID (EBB1102) PRE (UZ35461) /*
************************************************************************
* NOTE:  ALTHOUGH THIS ZAP IS WRITTEN WITH SMP CONTROL                 *
*        STATEMENTS, IT IS RECOMMENDED THAT THE CONTROL                *
*        STATEMENTS BE REMOVED AND THE ZAP BE APPLIED                  *
*        TO AN ALTERNATE NUCLEUS VIA SUPERZAP.  NOTE ALSO              *
*        THAT THE NAME STATEMENTS SPECIFYING IEANUC01 WILL             *
*        HAVE TO BE CHANGED TO THE NAME OF THE ALTERNATE               *
*        NUCLEUS YOU HAVE BUILT                                        *
************************************************************************
*                                                                      *
*                                                                      *
* THIS ZAP WILL BUILD A FOUR WORD TRAILER BLOCK AT THE END OF EACH     *
* GETMAIN AREA IN SUBPOOLS 245, 227, 228, AND 239.                     *
*                                                                      *
*                                                                      *
*  TWO EXCEPTIONS:                                                     *
*  - IF AN AREA GETMAINED IN ONE BLOCK IS LATER FREEMAINED IN PARTS,   *
*    THE TRAILER WILL DISAPPEAR WHEN THE LAST PART OF THE BLOCK IS     *
*    IS FREEMAINED.  THEREFORE, THE TRAILER CAN DISAPPEAR PREMATURELY  *
*                                                                      *
*  - IF THE REQUEST IS FOR A MULTIPLE OF A PAGE, (I.E. 1000, 2000,     *
*    3000 BYTES, ETC), THE TRACE WILL NOT ADD ON X'10' BYTES BUT       *
*    BUILDS THE TRAILER IN THE LAST X'10' BYTES OF THE REQUESTED       *
*    GETMAINED AREA.  SO IF THE REQUESTOR DOES NOT USE THE LAST        *
*    X'10' BYTES OF THE GETMAINED AREA YOU WILL FIND THE TRAILER.      *
*    BUT IF HE ZEROES OR USE THE LAST X'10' BYTES OF THE GETMAINED     *
*    AREA, THE TRAILER WILL BE MISSING.                                *
*                                                                      *
* THE TRAILER CONSISTS OF THE FOLLOWING INFORMATION:                   *
*  +0  X'FE' AS AN EYECATCHER (TESTED BY FREEMAIN HOOK)                *
*  +1  RETURN ADDRESS IF BRANCH REQUEST AND SVC ADDR. IF SVC REQUEST   *
*  +4  FIRST FOUR BYTES OF JOBNAME/INITNAME OF REQUESTOR               *
*  +8  LENGTH OF REQUEST (INCLUDING THE TRAILER)                       *
*  +C  EYE CATCHER "EYE"                                               *
************************************************************************
*                                                                      *
* CHANGE ACTIVITY                                                      *
* 80/03/20 TESTED                                                      *
* 80/04/10 MODIFIED VERS AND REPS TO IEAVGM00 AND MODIFIED THE VERS    *
*        AND REPS FROM 08C4 THRU 08F8.  ALSO MODIFIED REPS AT 08B0     *
*        AND 0822.  THE CHANGES WERE MADE SINCE A REQUEST FOR EXACTLY  *
*        X'1000' BYTES IN SQA WILL GET THE STORAGE ON A PAGE BOUNDARY  *
*        SINCE THIS TRACE ADDED X'10' BYTES, THE STORAGE WAS NOT BEING *
*        RETURNED ON A PAGE BOUNDARY.                                  *
* 80/07/17 MODIFIED REP AT 08C4, 08D4, 08D8, 0886, 088A AND ADDED      *
*        NEW CODE AT X'8FC' THRU X'92C' SO THAT THIS TRACE COULD BE    *
*        USED TO BUILD TRAILERS FOR SUBPOOLS 245, 227, 228, AND 239    *
*        ALSO.                                                         *
* 80/11/18 MODFIED REPS AT 0822, 0886,08B0,08C4, AND 08CC THRU 0928    *
*        DUE TO A PROBLEM IN THAT AN AQE FOR AN EXACT PAGE WAS NOT     *
*        BEING ADDED TO THE GETMAIN REQUEST.                           *
* 81/01/23 ADDED VERS AND REPS FOR PTF LEVEL UZ34680.                  *
* 81/06/23 ADDED SUPPORT FOR UZ35461.
************************************************************************
*                                                                */.
++ZAP (IEAVGM00)      /* PRE UZ27118      */.
NAME IEANUC01 IEAVGM00
VER 0960 9140,4011             AQE REQUESTED ?
VER 0964 0789                  NO RETURN
VER 096A 4770,847E             BRANCHIF NOT 4K REQUEST
VER 0C60 91C0,41FA             LIST REQUESTED ?
VER 0EE4 4590,8462             GO TO ROUND ROUTINE
REP 0960 9141,4011             AQE AND SQA REQUESTED?
REP 096A 47F0,08C4           * SPECIAL RK CASE GO TO PATCH3
REP 0C60 47F0,0800         * END OF GETMAIN GO TO PATCH1
REP 0EE4 4590,0870         * GO TO PATCH2 FOR FREEMAIN
*
++ZAP(IEAVGM00)      /* PRE UZ28870 OR UZ30572 OR UZ31872    */.
NAME IEANUC01 IEAVGM00
VER 0960 9140,4011            AQE REQUESTED ?
VER 0964 0789                 NO RETURN
VER 096A 4770,847E            BRANCH IF NOT 4K REQUEST
VER 0C68 91C0,41FA            LIST REQUESTED ?
VER 0EEC 4590,8462            GO TO ROUND ROUTINE
REP 0960 9141,4011            AQE AND SQA REQUESTED?
REP 096A 47F0,08C4          * SPECIAL RK CASE GO TO PATCH3
REP 0C68 47F0,0800          * END OF GETMAIN GO TO PATCH1
REP 0EEC 4590,0870          * GO TO PATCH2 FOR FREEMAIN
*
++ZAP(IEAVGM00)             /* PRE UZ34680, UZ35461    */ .
NAME IEANUC01 IEAVGM00
VER 0B08 9140,4011            AQE REQUESTED?
VER 0B0C 0789                 NO RETURN
VER 0B12 4770,847E            BRANCH IF NOT 4K REQUEST
VER 0E10 91C0,41FA            LIST REQUESTED?
VER 1094 4590,8462            GO TO ROUND ROUTINE
REP 0B08 9141,4011            AQE AND SQA REQUESTED?
REP 0B12 47F0,08C4          * SPECIAL 4K CASE GO TO PATCH3
REP 0E10 47F0,0800          * END OF GETMAIN GO TO PATCH1
REP 1094 4590,0870          * GO TO PATCH2 FOR FREEMAIN
*
++ZAP(IEAASU00).
NAME IEANUC01 IEAVFX00
* PATCH1
REP 0800 9101,4011            SQA REQUEST ?
REP 0804 4770,0810          * YES BYPASS RETURN
REP 0808 91C0,41FA            RESTORE OVERLAYED INSTRUCTION
REP 080C 47F0,8778            RETURN TO IEAVGM00
REP 0810 12E2                 ANY AREA ?
REP 0812 4780,0808          * IF 0 RETURN
REP 0816 1EEA                 ADD LENGTH
REP 0818 41F0,0010            SET X'10' IN REG F
REP 081C 1FEF                 SUBTRACE X'10' TO BUILD TRAILER
REP 081E 92FE,E000            MOVE IN EYE CATCHER
REP 0822 D203,E00C,0938     * MOVE IN "EYE" CATCHER
REP 0828 58F0,0224            GET PSAAOLD
REP 082C 9500,F0AD            IS THERE AN ADDRESS POINTING TO JOBNAME?
REP 0830 41F0,F0AC            INITIALIZE REG F TO POINT TO ASCB + AC
REP 0834 4770,083C          * YES, THEN BRANCH AROUND
REP 0838 41F0,F004            NO, GET ASCB + B0 FOR STC NAME
REP 083C 58F0,F000            LOAD ADDRESS
REP 0840 D203,E004,F000       MOVE 4 CHARS JOBNAME
REP 0846 50A0,E008            SAVE LENGTH
REP 084A 9101,4010            IS IT BRANCH ENTRY ?
REP 084E 4710,0864          * YES BRANCH
REP 0852 58F0,021C            GET TCB OLD ADDR.
REP 0856 58F0,F000            GET RB ADDRESS
REP 085A D202,E001,F015       COPY PSW ADDRESS
REP 0860 47F0,0808          * RETURN
REP 0864 D202,E001,4245       GET RETURN ADD
REP 086A 47F0,0808          * RETURN
* PATCH2
REP 0870 4160,0007            GET 7
REP 0874 1AA6                 LENGTH + 7
REP 0876 16A6                 OR TO FORCE '7'
REP 0878 17A6                 EXCLUSIVE OR TO ROUND
REP 087A 9101,4011            SQA REQUEST ?
REP 087E 4780,08A6          * NO BYPASS FAKING
REP 0882 186B                 GET START OF AREA
REP 0884 1E6A                 ADD LENGTH
REP 0886 47F0,0910            CHECK TO MAKE SURE PAGE IS VALID
REP 088A 4770,08A6          * NO, BYPASS
REP 088E 41A0,A010            YES, ADD X'10' TO FREE TRAILER
REP 0892 59A0,6008            ARE LENGTHS EQUAL?
REP 0896 4770,08A0          * NO, BRANCH
REP 089A D70F,6000,6000       CLEAR OUT TRAILER
REP 08A0 D703,600C,600C       CLEAR ONLY "EYE" CATCHER
REP 08A6 4160,0007            GET 7 FOR RETURN
REP 08AA 9140,4011            AQE REQUEST ?
REP 08AE 0789                 NO RETURN
REP 08B0 59A0,0930          * COMPARE WITH 4K VALUE
REP 08B4 4770,08BC          * NOT SAME BRANCH
REP 08B8 9680,4011            SET 4K REQUEST
REP 08BC 41AA,0008            +1 FOR AQE
REP 08C0 07F9                 RETURN
* PATCH3
REP 08C4 4770,08D2          * IF REQUEST NOT ONE PAGE, BRANCH
REP 08C8 9140,4011            AQE REQUEST?
REP 08CC 4710,847A            YES, BRANCH TO ADD AQE LENGTH
REP 08D0 07F9                 NO, RETURN
REP 08D2 5860,0934          * GET "ANDING" VALUE
REP 08D6 146A                 TURN OFF LOWER BYTES AND HALF
REP 08D8 156A                 WAS THE REQUEST A PAGE MULTIPLE?
REP 08DA 4770,08EA          * NO, CONTINUE ON
REP 08DE 9140,4011            WAS AN AQE REQUESTED?
REP 08E2 0789                 NO, RETURN WITHOUT ADDING X'10' BYTES
REP 08E4 41AA,0008            YES, ADD X'8' BYTES FOR AQE
REP 08E8 07F9                 RETURN TO CALLER
REP 08EA 9140,4011            IS AN AQE NEEDED?
REP 08EE 4710,08F8          * YES, BRANCH TO ADD X'18' BYTES
REP 08F2 41A0,A010            NO, ADD X'10' BYTES TO REQUESTED SIZE
REP 08F6 07F9                 RETURN TO CALLER
REP 08F8 9101,4011            IS THIS A SQA REQUEST?
REP 08FC 4780,847E            NO RETURN TO IEAVGM00
REP 0900 41A0,A018            YES, ADD X'18' BYTES TO REQUESTED SIZE
REP 0904 07F9                 RETURN TO CALLER
* PATCH 4
REP 0910 B100,6000            IS THE SEGMENT/PAGE VALID?
REP 0914 4770,08A6          * NO, DO NOT ADD X'10' BYTES
REP 0918 95FE,6000            IS THIS MY TRAILER ID?
REP 091C 4770,08A6          * NO, DO NOT ADD X'10' BYTES
REP 0920 47F0,088E            RETURN TO MAILINE CODE
* CONTANTS
REP 0930 0000,1000            4K CONSTANT
REP 0934 FFFF,F000            "ANDING" MASK
REP 0938 C5E8,C5D0            "EYE" CATCHER CONSTANT
*
* *** THE ASTERISKS PRECEEDING A COMMENT INDICATES AN INSTRUCTION TO
* **********************************************************************
* IN THIS FILE YOU WILL FIND S/ZAP APPLICABLE TO THE FOLLOWING PTFS.   *
*      UZ30572    UZ34680    UZ36944                                   *
*      UZ27118    UZ35390                                              *
*      UZ28870    UZ35460                                              *
*      UZ31872    UZ35461                                              *
*      UZ34679    UZ36943                                              *
*                                                                      *
* UPDATE HISTORY:                                                      *
*      DATE          DESCRIPTION                                       *
*      01/28/81      ADDED VER AND REPS FOR PTF LEVEL UZ34680          *
*      08/05/81      ADDED VER AND REPS FOR PTF LEVEL UZ34679, UZ35390 *
*                    UZ35460, UZ35461, UZ36943, UZ36944                *
*                                                                      *
* REGISTER SAVEAREAS:                                                  *
*      X'AB8' THRU X'AF7' - REGS 0 THRU F AT TIME OF ERROR             *
*                                                                      *
* **********************************************************************
NAME IEANUC01 IEAVGM00        (PRE UZ36944)                   08/05/81
VER 0FE2 47F0,8562                                            08/05/81
VER 1ECC 47F0,8562                                            08/05/81
REP 0FE2 47F0,0A74          * GO TO PATCH                     08/05/81
REP 1ECC 47F0,0A74          * GO TO PATCH                     08/05/81
*
NAME IEANUC01 IEAVGM00        (PRE UZ36943)                   08/05/81
VER 0F20 47F0,8562                                            08/05/81
VER 1DE4 47F0,8562                                            08/05/81
REP 0F20 47F0,0A74          * GO TO PATCH                     08/05/81
REP 1DE4 47F0,0A74          * GO TO PATCH                     08/05/81
*
NAME IEANUC01 IEAVGM00        (PRE UZ35461)                   08/05/81
VER 0EE0 47F0,8562                                            08/05/81
VER 1DA4 47F0,8562                                            08/05/81
REP 0EE0 47F0,0A74          * GO TO PATCH                     08/05/81
REP 1DA4 47F0,0A74          * GO TO PATCH                     08/05/81
*
NAME IEANUC01 IEAVGM00        (PRE UZ35460)                   08/05/81
VER 0FA6 47F0,8562                                            08/05/81
VER 1E94 47F0,8562                                            08/05/81
REP 0FA6 47F0,0A74          * GO TO PATCH                     08/05/81
REP 1E94 47F0,0A74          * GO TO PATCH                     08/05/81
*
NAME IEANUC01 IEAVGM00        (PRE UZ34680)
VER 0EE0 47F0,8562
VER 1DAC 47F0,8562
REP 0EE0 47F0,0A74          * GO TO PATCH
REP 1DAC 47F0,0A74          * GO TO PATCH
*
NAME IEANUC01 IEAVGM00    (PRE UZ34679 OR UZ35390)            08/05/81
VER 0FA6 47F0,8562                                            08/05/81
VER 1E9C 47F0,8562                                            08/05/81
REP 0FA6 47F0,0A74          * GO TO PATCH                     08/05/81
REP 1E9C 47F0,0A74          * GO TO PATCH                     08/05/81
*
NAME IEANUC01 IEAVGM00    (PRE UZ31872 OR UZ30572 OR UZ28870)
VER 0D38 47F0,8562
VER 1C04 47F0,8562
REP 0D38 47F0,0A74          * GO TO PATCH
REP 1C04 47F0,0A74          * GO TO PATCH
*
NAME IEANUC01 IEAVGM00    (PRE UZ27118)
VER 0D30 47F0,8562
VER 1BFC 47F0,8562
REP 0D30 47F0,0A74          * GO TO PATCH
REP 1BFC 47F0,0A74          * GO TO PATCH
*
NAME IEANUC01 IEAVFX00
VER 0A70 0000,0000            START OF TRAP
VER 0AFC 0000,0000            END OF TRAP
REP 0A70 C2C7,D540            BGN - BEGINNING OF TRAP
REP 0A74 55A0,0A80          * IS REQUEST SPECIAL IMS ONE?
REP 0A78 4770,0A84          * NO, STOP SYSTEM(S)
REP 0A7C 47F0,8562            YES, REUTRN TO GM00 AND ABEND
REP 0A80 00FF,FFF8            CONSTANT FOR IMS
REP 0A84 900F,0AB8          * SAVE REGISTERS
REP 0A88 58F0,0010            LOAD ADDR OF CVT
REP 0A8C 58F0,F294            LOAD ADDR OF CSD
REP 0A90 91C0,F008            IS SYSTEM AN AP/MP?
REP 0A94 47E0,0AA8          * NO, BRANCH TO UP STOP CODE
REP 0A98 48F0,0204            GET PHYSICAL CPU ID FOR THIS CPU
REP 0A9C 57F0,0AAC          * FLIP CPU ID TO OTHER CPU
REP 0AA0 AE0F,0009            SIGP OTHER CPU TO STOP
REP 0AA4 4760,0AA0          * LOOP IF OTHER CPU IS BUSY
REP 0AA8 8200,0AB0          * LOAD DEAD WIAT PSW IN THIS CPU
REP 0AAC 0000,0001            X/OR MASK TO FLIP CPU IDS
REP 0AB0 0002,0000            DEAD WAIT PSW
REP 0AB4 0000,DEAD            DEAD WAIT PSW
REP 0AB8 0000,0000            16 WORDS - REGISTER SAVEAREA
REP 0AFC C5D5,C440            END - END OF TRAP
************************ END OF S/ZAP **********************************
ALLOCCSA CSECT
*******************************************************************
*
*        THIS MODULE IS INTENDED TO BE USED AS AN EXIT TO AMDPRDMP,
*        AND WILL OPERATE AS A VERB EXIT.  THE SELECTED VERB WILL
*        CAUSE THIS EXIT TO GET CONTROL, ONCE AN ENTRY IS ADDED
*        TO AMDPRECT.  THIS ROUTINE USES STANDARD PRINTDUMP LINKAGE
*        TO FORMAT AND PRINT CONTROL BLOCKS, PRINT MESSAGES, AND
*        TO ACCESS DUMPED DATA.  IT IS INTENDED TO BE USED TO
*        AID THE PSR IN DETERMINING A PROBLEM, WHEN EITHER CSA OR
*        SQA IS DEPLETED.  THE PROGRAM DOES NOT USE A GETMAINED
*        AREA FOR ITS' WORKAREA, SO IT IS NOT RE-ENTRANT, BUT THIS
*        SHOULD NOT PRESENT A PROBLEM, IF IT IS NOT PLACED IN LPA.
*        THE EXIT CHANGES SOME DATA IN THE WORKAREA, AFTER MOVING
*        THE DATA FROM PRINTDUMPS' BUFFER, FOR EASE OF FORMATTING.
*      (RATHER THAN FORMATTING FQE'S, THE ROUTINE COLLECTS DATA FROM
*      THE FQE, AND PRINTS INFORMATION CONCERNING ALLOCATED CORE.
*      THIS IS THE MAIN DIFFERENCE BETWEEN THIS VERSION OF THE PRO-
*      GRAM, AND THE EARLIER VERSION. )
*        WORKAREA LABELS MAY HAVE TO BE CHANGED, IF THE FOLLOWING MSS
*        CONTROL BLOCKS ARE CHANGED:
*               CVT, GDA, PQE, FBQE, SPQE, DQE, OR FQE.
*        THIS ROUTINE IS NOT FORMALLY SUPPORTED IN ANY WAY, BUT
*        I WOULD BE HAPPY TO ATTEMPT ANY CORRECTIONS, ON AN 'AS
*        AVAILABLE' BASIS.
*
*        FRANK H. CONNERY, JR., REGION 9, T.A.G.
*        (312) 245-7850   (IBM TIE LINE 8-261-7850)
*        REGION 9, TECHNICAL SUPPORT GROUP  (F.E.)
*        1 IBM PLAZA  (INTERNAL ZIP  38-528)
*        CHICAGO, ILLINOIS   60611
*
*   UPDATE 1-- LOGIC HAS BEEN ADDED TO CAPTURE 'RUNNING COUNTS OF
*              CORE ALLOCATED, INDEXED BY KEY AND SUBPOOL ID.
*              CODE USES "CORETAB", WHICH HAS FOUR ENTRIES PER KEY.
*              HOPEFULLY, THIS WILL ALLOW THE USER TO MORE QUICKLY
*              DETERMINE WHO THE LARGE USERS OF CSA/SQA ARE.
*   THERE IS ALSO A MINOR LOGIC UPDATE, WHICH WILL "FLAG" THE DQE
*   WITH A DASH (-DQE), IF NO PART OF THE BLOCK DESCRIBED BY THE DQE
*   APPEARS IN THE DUMP.  THIS OCCURS AS A RESULT OF A PARTIAL, OR
*   INCOMPLETE, DUMP: THE MODIFICATION MERELY SAVES THE PSR TIME,
*   IN THAT HE WILL BE NOTIFIED, VIA ALLOCCSA OUTPUT, THAT TCORE REP-
*   RESENTED BY THE DQE WAS NOT DUMPED.
*              -81/03/12- F.H. CONNERY, JR.
*
*******************************************************************
         EJECT
         ENTRY CSAMAP1
CSAMAP1  SAVE (14,12),,ALLOCCSA           SAVE REGISTERS
         BALR  12,0                       ESTABLISH BASE REGISTER
         USING *,12
         LA    7,CMAPSAV                  POINT TO MY SAVE AREA
         ST    13,CMAPSAV+4               ESTABLISH
         ST    7,8(,13)                    SAVE AREA LINKAGE
         ST    13,SAVPTR                  SAVE SAVE POINTER
         LR    13,7
         USING ABDPL,1                    ESTABLISH PARMLIST DSECT
         L     7,ADPLBUF                  LOCATE PRINT BUFFER
         MVC   52(15,7),TITLE             MOVE '***CSA' TITLE
         BAL   11,MSGSET                  AND PRINT IT
         BAL   11,BLNKPRNT                SPACE
         SR    8,8                        CLEAR REGISTER FOR INSERT
         ICM   8,7,ADPLCVT+1              GET ADDRESS OF DUMPED CVT
         BNZ   CVTOK                      BRANCH IF GIVEN
         LA    8,76(,0)                   ELSE SET FOR CVT POINTER
         BAL   11,GETBLOK                 AND GO GET POINTER
         LTR   15,15                      RETREIEVE OK ?
         BNZ   CVTBAD                     NO, ISSUE MSG AND EXIT
         L     8,0(,8)                    GET ADDRESS OF DUMPED CVT
         B     CVTOK                      CVT POINTER IN REG 8
CVTBAD   MVC   CBNAME,CVTCB               MOVE 'CVT' INTO MSG
CBMSGEND L     7,ADPLBUF                  LOCATE BUFFER
         MVC   0(47,7),CBUNAV             MOVE ENTIRE MESSAGE
         BAL   11,MSGSET                  LINK TO PRINT IT
EXIT     L     13,SAVPTR                  PICK UP PRIOR SAVE AREA
         RETURN (14,12),,RC=0             AND EXIT
CVTOK    LA    8,560(,8)                  PICK UP GDA
         BAL   11,GETBLOK                 GO GET POINTER
         LTR   15,15                      RETRIEVE OK ?
         BNZ   GDAUNAV                    NO, BRANCH
         L     8,0(,8)                    ELSE PICK UP POINTER
         ST    8,GDAPTR                   SET POINTER
         BAL   11,GETBLOK                 AND REQUEST GDA
         LTR   15,15                      RETRIEVE OK ?
         BZ    GDAOK                      YES, BRANCH
GDAUNAV  MVC   CBNAME,GDACB               ELSE MOVE 'GDA' INTO MSG
         B     CBMSGEND                   BR TO PRINT, THEN QUIT
GDAOK    MVC   MYGDA(56),0(8)             MOVE GDA TO WORKAREA
         LA    2,MYGDA                    POINT TO MOVED GDA
         USING GDA,2
         LA    0,GDAPATN                  SET PATTERN FOR GDA
         BAL   11,FRMTPRNT                GO F/P
         B     PQEPROC                    GO PROCESS PQE
         DS    0F
GDAPATN  DC    X'1F220005'                GDA  XXXXXX       (0)
         DC    AL4(GDALBL)
         DC    AL4(GDAPTR+1)
         DC    X'1D300C12'                FLAG  XX          (12)
         DC    AL4(MYGDA)
         DC    X'1D62161F'                CSA PQE  XXXXXX   (22)
         DC    AL4(MYGDA+9)
         DC    X'1D42272E'                PSTRT  XXXXXX     (39)
         DC    AL4(MYGDA+17)
         DC    X'1D32363C'                PSZE  XXXXXX      (54)
         DC    AL4(MYGDA+21)
         DC    X'1D72444E'                SQA SPQE XXXXXX   (68)
         DC    AL4(MYGDA+25)
         DC    X'1D725660'                FREE SQA  XXXXXX  (86)
         DC    AL4(MYGDA+29)
         DC    X'1D726872'                CSA SPQE  XXXXXX  (104)
         DC    AL4(MYGDA+53)
         DC    F'0'
GDALBL   DC    C'GDAFLAGCSA PQEPSTRTPSZESQA SPQEFREE SQACSA SPQE'
         DS    0F
PQEPROC  BAL   11,BLNKPRNT                SPACE OUTPUT
         L     8,CSAPQEP                  GET CSA PQE POINTER
         ST    8,PQEPTR                   SAVE POINTER FOR OUTPUT
         BAL   11,GETBLOK                 GO GET PQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    CPQEOK                     YES, BRANCH
         MVC   CBNAME,CPQECB              ELSE MOVE 'CSA PQE' TO MSG
         BAL   11,CBMSG                   AND PRINT IT
         B     SPQEPROC                   THEN GO PROCESS SPQE'S
CPQEOK   MVC   MYPQE(32),0(8)             MOVE PQE TO WORKAREA
         LA    0,PQEPATN                  PICK UP PATTERN FOR PQE
         BAL   11,FRMTPRNT                GO TO F/P
         B     FBQEPROC                   THEN GO PROCESS FBQE'S
PQEPATN  DS    0F
         DC    X'1F220005'                PQE  XXXXXX       (0)
         DC    AL4(PQELBL)
         DC    AL4(PQEPTR+1)
         DC    X'1D420D14'                CSTRT  XXXXXX     (13)
         DC    AL4(MYPQE+25)
         DC    X'1D421C23'                CSIZE  XXXXXX     (28)
         DC    AL4(MYPQE+21)
         DC    F'0'
PQELBL   DC    C'PQECSTRTCSIZE'
         DS    0F
FBQEPROC LA    7,50(,0)                   SET LOOP COUNT
FBQELOOP SR    8,8                        CLEAR ACCESS REGISTER
         ICM   8,7,MYPQE+1                GET FBQE, IF ANY
         BZ    SPQEPROC                   BRANCH IF NONE
         CLC   CSAPQEP+1(3),MYPQE+1       DOES IT POINT TO PQE ?
         BZ    SPQEPROC                   YES, GET OUT
         BAL   11,GETBLOK                 ELSE GO GET FBQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    FBQECNT                    YES, BR TO CHECK COUNT
         MVC   CBNAME,CFBQECB             ELSE MOVE 'FBQE' TO MSG
         BAL   11,CBMSG                   AND PRINT MSG
         B     SPQEPROC                   THEN GO PROCESS SPQE'S
FBQECNT  BCT   7,FBQEOK                   BR, IF MAX NOT REACHED
         MVC   CBLNAME,CFBQECB            ELSE MOVE NAME TO LP MSG
         BAL   11,LOOPMSG                 AND LINK TO PRINT IT
         B     SPQEPROC                   THEN GO PROCESS SPQE'S
FBQEOK   MVC   MYFBQE(16),0(8)            MOVE FBQE TO WORKAREA
         LA    0,FBQEPATN                 PICK UP PATTERN
         BAL   11,FRMTPRNT                LINK TO F/P ROUTINE
         MVC   MYPQE+1(3),MYFBQE+1        SET UP NEXT FBQE
         B     FBQELOOP                   AND GO PROCESS NEXT
         DS    0F
FBQEPATN DC    X'1F322B31'                FBQE  XXXXXX      (43)
         DC    AL4(FBQELBL)
         DC    AL4(MYPQE+1)
         DC    X'1D32393F'                AREA  XXXXXX      (57)
         DC    AL4(MYFBQE+13)
         DC    X'1D22474C'                SZE XXXXXX        (71)
         DC    AL4(MYFBQE+9)
         DC    F'0'
FBQELBL  DC    C'FBQEAREASZE'
         DS    0F
SPQEPROC MVC   SPQEPTR,CSASPQEP           SET FIRST CSA SPQE
         MVI   SPQEPTR,X'00'              ENSURE FIRST BYTE ZERO
         L     7,ADPLBUF                  LOCATE PRINT BUFFER
         MVC   0(22,7),CMSSBLK            MOVE 'CSA MSS BLOCKS' MSG
         BAL   11,MSGSET                  LINK TO PRINT IT
         BAL   11,BLNKPRNT                SPACE OUTPUT
         BAL   7,MSSROUTN                 LINK TO FORMAT BLOCKS
         L     7,ADPLBUF                  LOCATE PRINT BUFFER
         MVC   0(22,7),SMSSBLK            MOVE 'SQA MSS BLOCKS' MSG
         BAL   11,MSGSET                  LINK TO PRINT IT
         BAL   11,BLNKPRNT                SPACE OUTPUT
         MVC   MAXSPQE(12),SQACT          CHANGE MAX CTS FOR SQA
         MVC   SPQEPTR,SQASPQEP           SET FIRST SQA SPQE
         MVI   SPQEPTR,X'00'              ENSURE HI BYTE ZERO
         BAL   7,MSSROUTN                 LINK TO FORMAT MSS BLOCKS
         BAL   11,BLNKPRNT                SPACE OUTPUT
         L     7,ADPLBUF                  GET BUFFER
         MVC   0(21,7),USEMSG             MOVE MESSAGE
         BAL   11,MSGSET                  AND PRINT IT
         LA    3,3(,0)                    SET TO SKIP 3
USEBLNK  BAL   11,BLNKPRNT                PRINT BLANK BUFFER
         BCT   3,USEBLNK
         L     7,ADPLBUF                  GET BUFFER
         MVC   0(38,7),USEHDR             MOVE HEADER
         BAL   11,MSGSET                  GO PRINT HEADER
         SR    3,3                        CLEAR 3 FOR KEY 0
KEYLOOP  STC   3,HDRKEY                   SET KEY FOR FORMAT
         LR    4,3                        DUP. IN R4
         SLL   4,4(0)                     TIMES '10'
         LA    5,CORETAB                  POINT TO STAT AREA
         AR    5,4                        THEN PROPER KEY AREA
         MVC   HDRSPS(16),0(5)            MOVE STATS TO OUTPUT AREA
         L     4,HDRSPS                   GET 241 LENGTH
         A     4,HDRSPS+4                 PLUS 227 LENGTH
         A     4,HDRSPS+8                 PLUS 228 LENGTH
         A     4,HDRSPS+12                PLUS 231 LENGTH
         LA    4,0(,4)                    CLEAR HIGH
         ST    4,HDRTOTAL                 SET TOTAL
         LTR   4,4                        ZERO ?
         BC    8,UPKEY                    BRANCH IF YES
         LA    0,HDRPAT                   ELSE POINT TO PATTERN
         BAL   11,FRMTPRNT                AND GO OUTPUT
UPKEY    LA    3,1(,3)                    KEY PLUS 1
         C     3,=X'00000010'             0-15 DONE ?
         BE    STATDONE                   YES, BRANCH OUT
         B     KEYLOOP                    ELSE PROCESS NEXT
         DS    0F
HDRPAT   DC    X'1C000001'                KEY = -01
         DC    AL4(HDRKEY)
         DC    X'1C020304'
         DC    AL4(HDRSPS+1)
         DC    X'1C020A0B'
         DC    AL4(HDRSPS+5)
         DC    X'1C021112'
         DC    AL4(HDRSPS+9)
         DC    X'1C021819'
         DC    AL4(HDRSPS+13)
         DC    X'1C021F20'
         DC    AL4(HDRTOTAL+1)
         DC    F'0'
SP239LBL DC    C'KEY 0,SUBPOOL 239--          '
         DC    CL30'(SEE ALSO S.P. 227, KEY 0)'
SP245LBL DC    C'KEY 0,SUBPOOL 245--'
         DS    0F
INFO239  DC    X'1F021314'
         DC    AL4(HDRLABEL)
         DC    AL4(K0SP239+1)
         DC    F'0'
INFO245  DC    X'1F021314'
         DC    AL4(HDRLABEL)
         DC    AL4(K0SP245+1)
         DC    F'0'
STATDONE L     7,ADPLBUF                  GET BUFFER
         MVC   0(55,7),SP239LBL           MOVE LABEL
         LA    0,INFO239                  POINT TO 239 PATN
         BAL   11,FRMTPRNT                FORMAT & PRINT
         L     7,ADPLBUF                  GET BUFFER
         MVC   0(19,7),SP245LBL           MOVE LABEL
         LA    0,INFO245                  POINT TO 245 PATN
         BAL   11,FRMTPRNT                GO FORM. & PRINT
         BAL   11,BLNKPRNT
         BAL   11,BLNKPRNT
         L     7,ADPLBUF                  LOCATE PRINT BUFFER
         MVC   0(24,7),COMPMSG            MOVE COMPLETION MSG
         BAL   11,MSGSET                  LINK TO PRINT IT
         B     EXIT                       ALL DONE, RETURN TO PRTDMP
COMPMSG  DC    C'*** CSA MAP COMPLETE ***'
         DS    0F
         EJECT
********************************************************************
*
*        SUBROUTINE TO FORMAT MSS CONTROL BLOCKS.  THIS ROUTINE WILL
*        FORMAT SPQE'S, DQE'S, AND FQE'S, AND IS CALLED FOR BOTH
*        CSA AND FOR SQA.  A PATTERN IS SET UP, DEPENDING ONTHE
*        BLOCKS ACTUALLY FOUND, AND THE FORMAT/PRINT ROUTINE (FPROUT)
*        IS CALLED.  FPROUT WILL FORMAT THE MOVED MSS BLOCKS,DE-
*        PENDING ON THE PATTERN.  PATTERN IS A BYTE, '000SDF00', TO
*        ALLOW INDEXING TO PRTDMP PATTERN. (S = SPQE, D = DQE, AND
*        F = FQE).  THIS ALLOWS A COMMAN FORMAT/PRINT ROUTINE TO PRINT
*        THE BLOCKS, REGARDLESS OF WHICH, IF ANY, ARE PRESENT
*        (IF NO BLOCKS WERE GOTTEN, INDEX WILL BE ZERO, AND FPROUT
*        WILL RETURN TO CALLER.
*
******************************************************************
         SPACE 2
MSSROUTN LM    8,10,MAXSPQE               PICK UP MAX COUNTS
         STM   8,10,SPQECT                AND INITIALIZE LOOP COUNTS
SPQELP   SR    8,8                        CLEAR REG FOR INSERT
         ICM   8,7,SPQEPTR+1              GET SPQE POINTER
         BCR   8,7                        RETURN IF NONE
         MVC   DQECT(8),MAXDQE            SET NEW MAX CTS, DQE & FQE
         MVI   PATTERN,SDFPAT             SET PATTERN TO ALL
         BAL   11,GETBLOK                 GO GET SPQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    TESTSCNT                   YES, GO TEST COUNT
         MVC   CBNAME,SPQECB              ELSE MOVE 'SPQE' TO MSG
         BAL   11,CBMSG                   LINK TO PRINT IT
         BR    7                          AND RETURN
TESTSCNT L     6,SPQECT                   PICK UP REMAINING COUNT
         BCT   6,SCNTOK                   BR IF NOT EXCEEDED
         MVC   CBLNAME,SPQECB             ELSE MOVE 'SPQE' TO LP MSG
         BAL   11,LOOPMSG                 LINK TO PRINT IT
         BR    7                          AND RETURN
SCNTOK   ST    6,SPQECT                   STORE DECREMENTED COUNT
         MVC   SPQE(12),0(8)              MOVE SPQE TO WORKAREA
         TM    FLGSPQE,X'80'              SHARED ?
         BZ    DQEONE                     NO, GO TO DQE PROCESS
         MVI   PATTERN,SPAT               SET TO F/P SPQE ONLY
         BAL   11,FPROUT                  LINK TO PRINT IT
         MVC   SPQEPTR+1(3),NSPQE+1       SET UP NEXT SPQE
         B     SPQELP                     GO TO SPQE PROCESS
DQELP    MVC   FQECT,MAXFQE               NEW DQE, RESET FQE COUNT
         MVI   PATTERN,DFPAT              SAME SPQE, SO D & F ONLY
DQEONE   SR    8,8                        CLEAR FOR INSERT
         MVI   DQELBL,X'60'               INITIALIZE LABEL TO '-DQE'
         ICM   8,7,DQEPTR+1               GET DQE ADDRESS
         BNZ   DQEXIST                    BR IF ONE EXISTS
DQEBAD   NI    PATTERN,SPAT               T/F ALL BUT SPQE
         BAL   11,FPROUT                  AND LINK TO F/PRINT
         MVC   SPQEPTR+1(3),NSPQE+1       SET UP NEXT SPQE
         B     SPQELP                     AND GO PROCESS IT
DQEXIST  BAL   11,GETBLOK                 GO GET DQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    TESTDCNT                   YES, BR TO CHECK COUNT
         MVC   CBNAME,DQECB               ELSE MOVE 'DQE' TO MSG
         BAL   11,CBMSG                   & LINK TO PRINT IT
         B     DQEBAD                     BR OUTPUT & GET NEXT SPQE
TESTDCNT L     6,DQECT                    PICK UP COUNT OF DQE'S
         BCT   6,DCNTOK                   BR IF COUNT NOT EXCEEDED
         MVC   CBLNAME,DQECB              ELSE PUT 'DQE' LOOP MSG
         BAL   11,LOOPMSG                 & LINK TO PRINT IT
         B     DQEBAD                     BR TO F/P & GET NXT SPQE
DCNTOK   ST    6,DQECT                    STORE DECREMENTED COUNT
         MVC   DQE(16),0(8)               MOVE DQE TO WORKAREA
         LM    8,9,HIARCH                 GET LENGTH AND ADDRESS
         AR    9,8                        GET TOP OF BLOCK
         STM   8,9,BLOCK                  SET BOUNDARIES, IF NO FQE
         MVC    BLOCK+9(3),BLKLNGTH+1     SET LENGTH
         LM    8,9,HIARCH                 GET BLOCK AND LENGTH AGAIN
         CLI   SUBPOOL,X'F5'              SUBPOOL 245 ?
         BNE   CK239                      BR. IF NOT
         LR    3,9                        GET LENGTH IN R3
         A     3,K0SP245                  AND UPDATE
         ST    3,K0SP245                    STAT. VALUE
         B     ENDCORCT                   BR. AROUND REST
CK239    CLI   SUBPOOL,X'EF'              SUBPOOL 239 ?
         BNE   NOTKEY0                    BR IF NOT
         LR    3,9                        ELSE GET LEN. IN R3
         A     3,K0SP239                  ADD TOTAL THUS FAR
         ST    3,K0SP239                  AND STORE BACK
         B     ENDCORCT                   THEN BR. AROUND
NOTKEY0  IC    4,SUBPOOL                  S.P.ID INTO R4
         N     4,=X'00000006'             SET INDICES
         SLL   4,1(0)                     TIMES 2
         IC    5,KEY                      KEY INTO R5
         N     5,=X'000000F0'             KEY TIMES '10'
         LA    10,CORETAB                 POINT TO CORETAB ORIGIN
         LR    3,9                        LN. FROM DQE INTO R3
         AR    10,5                       POINT TO ENTRIES FOR KEY
         A     3,0(10,4)                  ADD TOTAL THUS FAR
         ST    3,0(10,4)                  AND STORE BACK
         B     ENDCORCT                   BR AROUND TABLE(S)
         DS    0F
K0SP245  DC    F'0'
K0SP239  DC    F'0'
         DS    0D
CORETAB  EQU   *
K0SP241  DC    F'0'
K0SP227  DC    F'0'
K0SP228  DC    F'0'
K0SP231  DC    F'0'
K1SPALL  DC    2D'0'
K2SPALL  DC    2D'0'
K3F      DC    26D'0'
************************************************************
*
*   CORETAB IS A TABLE USED TO KEEP RUNNING TOTALS OF SPACE ALLOCATED,
*   WITH FOUR ENTRIES DEFINED FOR EACH OF 16 POSSIBLE KEYS. THE KEYS
*   SELECT THE FOUR WORD ENTRY (KEY TIMES X'10' GIVES OFFSET FROM
*   CORETAB START, AND THE ENTRIES ARE FOR SUBPOOLS 241, 227, 228,
*   AND 231, IN THAT ORDER.  THE HEX VALUE OF THE S.P. ID IS ANDED
*   AGAINST A MASK OF '06', GIVING A VALUE OF 0,2,4, OR 6 FOR SUBPOOLS
*   'F1' (241)='00', 'E3' (227)='02', 'E4' (228)='04, AND 'E7' (231)=
*   '06'.  THE RESULTS ARE THEN SHIFTED LEFT ONE TO INDEX A PARTICULAR
*   WORD IN THE ENTRY.  THIS WILL ALLOW AN ENTRY FOR EACH CURRENTLY
*   USED SUBPOOL, BUT LOGIC WILL HAVE TO BE 'REWORKED' IF NEW SUBPOOLS
*   ARE DEFINED. (S.P.'S 239 AND 245 DATA ARE CAPTURED BY THEMSELVES.)
*
************************************************************
          SPACE 2
USEMSG    DC   C'CORE USAGE STATISTICS'
USEHDR    DC   C'KEY SP-241 SP-227 SP-228 SP-231  TOTAL '
HDRKEY    DC   X'00'
HDRSPS    DC   4F'0'
HDRTOTAL  DC   F'0'
HDRLABEL  DC   CL6' '
ENDCORCT  EQU  *
         SRL   9,12(0)                    SHIFT FOR NUMBER OF PAGES
CKCOR    LR    3,8                          SAVE R8 - USED IN GETBLOK
         BAL   11,GETBLOK                 IS IT IN DUMP ?
         LTR   15,15                            "
         BNZ   NOTINDMP                   BR IF NOT
         MVI   DQELBL,X'40'               ELSE BLANK OUT DASH
         B     INCOR                      AND BRANCH
NOTINDMP LR    8,3                          RESTORE R8
         A     8,=X'00001000'             ELSE BUMP TO NEXT PAGE
         BCT   9,CKCOR                    AND BR TO CK IT
INCOR    EQU   *
FQELP    SR    8,8                        CLEAR REG FOR INSE
         ICM   8,7,FQEPTR+1               FQE ?
         BNZ   FQEXIST                    YES, BRANCH
FQEBAD   BAL   11,FPROUT                  LINK TO F/P ANY OTHERS
         MVC   DQEPTR+1(3),NDQE+1         SET NEXT DQE, IF ANY
         B     DQELP                      AND BRANCH TO PROCESS
FQEXIST  BAL   11,GETBLOK                 GO GET FQE
         LTR   15,15                      RETRIEVE OK ?
         BZ    TESTFCNT                   YES, BR TO CHECK COUNT
         MVC   CBNAME,FQECB               ELSE PUT 'FQE' IN MSG
         BAL   11,CBMSG                   AND LINK TO PRINT IT
         B     FQEBAD                     BR F/P & GET NXT DQE
TESTFCNT L     6,FQECT                    PICK UP FQE COUNT
         BCT   6,FCNTOK                   BR IF NOT EXCEEDED
         MVC   CBLNAME,FQECB              ELSE MOVE 'FQE' TO LOOP MSG
         BAL   11,LOOPMSG                 LINK TO PRINT IT,
         B     FQEBAD                     BR TO F/P & GET NXT DQE
FCNTOK   ST    6,FQECT                    STORE DECREMENTED COUNT
         MVC   FQE(12),0(8)               MOVE FQE TO WORKAREA
SP245    CLI   SUBPOOL,X'F5'              SQA 'SHORT' FQE ?
         BNE   FQEVALCK                   NO, CK AND SET VALID
         L     8,FQEPTR                   ELSE GET FQE ADDRESS
         LA    8,8(,8)                    ADD 8 TO GET TOP
         STCM  8,7,HIADDR+1               AND SET TOP OF FREE SPACE
         B     FQEVALID                   BYPASS VALIDITY CK
FQEVALCK SR    8,8                        CLEAR REG FOR INSERT
         ICM   8,7,BLKADDR                GET BLK ADR FROM DQE
         C     8,HIADDR                   FREE SPACE BELOW BLOCK ?
         BH    FQEFINCK                   YES, CK FOR 'SHORT' FQE
         A     8,BLKLNGTH                 ELSE ADD FOR TOP OF BLK
         C     8,HIADDR                   FREE SPACE ABOVE BLK ?
         BNL   FQEVALID                   BR, NOT 'SHORT' FQE
FQEFINCK CLC   FQEPTR+1(3),BLKADDR        FQE WITHIN BLOCK ?
         BL    FQEBAD                     BR NO, INVALID FQE
         SR    8,8                        CLEAR REG FOR INSERT
         ICM   8,7,BLKADDR                GET ADDRESS OF BLOCK
         A     8,BLKLNGTH                 POINT TO TOP OF AREA
         CLM   8,7,FQEPTR+1               FQE WITHIN BLOCK ?
         BL    FQEBAD                     NO, DON'T F/P
         L     8,FQEPTR                   ELSE GET FQE ADDRESS
         LA    8,8(,8)                    BUMP FOR TOP OF FQE
         STCM  8,7,HIADDR+1               AND DUMMY UP FOR SHORT FQE
FQEVALID  CLC  BLOCK+5(3),HIADDR+1        AT TOP OF BLOCK ?
          BNE  SETALLOC                   NO, BRANCH
SETTOP    LM   8,9,FREEBYTE               GET FREE AND HIGH ADDRESS
          SR   9,8                        CALCULATE BOTTOM OF FREE
          STCM 9,7,BLOCK+5                SET TOP OF NEXT ALLOCATED
          CLM  9,7,BLKADDR                ARE WE AT BOTTOM OF BLOCK?
          BE   FQEBAD+4                   YES- DON'T PRINT -QUIT
          MVC  BLOCK+1(3),BLKADDR         SET LO IN CASE NO FQE
          LM   8,9,BLOCK                  SET LENGTH
          SR   9,8                         IN CASE
          ST   9,BLOCK+8                    NO 'NEXT' FQE
          MVC  FQEPTR+1(3),NFQE           SET UP NEXT FQE
          B    FQELP                      AND GO PROCESS
SETALLOC  MVC  BLOCK+1(3),HIADDR+1        SET LO, HI SET FROM PREV
          LM   8,9,BLOCK                  GET EXTENTS OF ALLOCATED
          SR   9,8                        CALCULATE LENGTH
          ST   9,BLOCK+8                  AND STORE IT
          BAL  11,FPROUT                  LINK TO PRINT ALLOCATED
          MVI  PATTERN,FPAT               SET PATTERN FOR SUBSEQUENT
          B    SETTOP                     GO TO FINISH UP
         EJECT
******************************************************************
*
*        FORMAT/PRINT ROUTINE. SETS UP CORRECT PATTERN FOR AMDPRDMP
*        FORMAT ROUTINE, THEN CALLS THE FORMAT ROUTINE, AND, ON
*        RETURN, CALLS THE PRINT ROUTINE OF AMDPRDMP.  THE MAIN
*        INPUT IS THE PATTERN MASK, WHICH HAS BEEN SET BY THE MSS
*        ROUTINE.  IF YOU WISH TO CHANGE THE PRINTED OUTPUT IN
*        ANY WAY, YOU MAY MODIFY THE PRINT PATTERNS.  NOTE THAT
*        THERE ARE ACTUALLY SIX PATTERNS, AS FOLLOWS:
*
*        SDFPATN--PATTERN USED TO OUTPUT SPQE, DQE, & FQE
*        DFPATN---USED ON SUBSEQUENT DQE'S (SPQE PRINTED ON FIRST)
*        FPATN----USED FOR SUBSEQUENT FQE'S
*        SDPATN---USED WHEN NO FQE'S ON FIRST DQE
*        SPATN----USED WHEN NO DQE'S ON SPQE, OR SHARED SPQE
*        DPATN----USED ON SUBSEQUENT DQE WITH NO FQE'S
*
*        TEST IS MADE FOR ZERO PATTERN, AND FPROUT WILL RETURN, IF
*        SO. (NO MSS CONTROL BLOCKS TO BE FORMATTED.)
*
******************************************************************
         SPACE  2
*        PATTERN MASK
SDFPAT   EQU   X'1C'                      SPQE, DQE, AND FQE
DFPAT    EQU   X'0C'                      DQE AND FQE
FPAT     EQU   X'04'                      FQE ONLY
SDPAT    EQU   X'18'                      SPQE AND DQE
DPAT     EQU   X'08'                      DQE ONLY
SPAT     EQU   X'10'                      SPQE ONLY
FULLPAT  EQU   X'1C'                      = SDFPAT, AT THIS TIME
         SPACE  1
*        NOTE THAT X'14' PATTERN NOT PRESENT.  SPQE AND FQE WITHOUT
*        A DQE WOULD NOT OCCUR.
         SPACE  1
FPROUT   SR    5,5                        CLEAR INDEX REG
         ICM   5,1,PATTERN                GET AND CHECK PATTERN MASK
         BCR   8,11                       RETURN IF ZERO
         LA    6,PATTAB                   POINT TO TABLE OF ADCON'S
         L     0,0(5,6)                   USE INDEX TO GET ENTRY
FRMTPRNT L     15,ADPLFRMT                GET EP PRTDMP FORMAT
         BALR   14,15                     LINK TO IT, R0 = PATTERN
BLNKPRNT L     15,ADPLPRNT                GET PRINT ROUTINE E.P.
         BALR  14,15                      LINK TO IT
         BR    11                         RETURN TO CALLER
         SPACE  2
         DS    0F
*        ADCONS TO DIFFERENT FORMAT PATTERNS
         SPACE  1
PATTAB   DC     AL4(SDFPATN)              SHOULD NEVER BE USED
         DC    AL4(FPATN)
         DC    AL4(DPATN)
         DC    AL4(DFPATN)
         DC    AL4(SPATN)
         DC    AL4(SDFPATN)               SHOULD NEVER BE USED
         DC    AL4(SDPATN)
         DC    AL4(SDFPATN)
         SPACE  3
*        THESE ARE THE ACTUAL PATTERNS USED TO FORMAT THE MSS DATA.
*        THE DATA TO BE FORMATTED WILL BE IN FIELDS IN 'MSSDATA'.
*        MSSDATA LABELS MAY HAVE TO BE CHANGED, AND/OR THE AREA
*        RECODED, IF THE CONTROL BLOCKS CHANGE.  DSECT LAYOUT WAS
*        NOT USED, AS USING STATEMENTS WOULD HAVE 'TIED UP' THE
*        REGISTERS.  EACH COMPLETE PATTERN ENDS WITH A FULL WORD OF
*        ZEROES, AND ALTERNATE LABLES ARE USED TO FORMAT PARTIAL
*        LINES.  (EXAMPLE: DFPATN)
*
*
         SPACE  2
SDFPATN  DS    0F
         DC    X'1F320006'
         DC    AL4(SPQELBL)
         DC    AL4(SPQEPTR+1)
         DC    X'1D300E14'
         DC    AL4(FLGSPQE)
         DC    X'1D10181C'
         DC    AL4(SUBPOOL)
         DC    X'15202025'
DFPATN   DC    X'1F32282E'
         DC    AL4(DQELBL)
         DC    AL4(DQEPTR+1)
         DC    X'1D22363B'
         DC    AL4(BLKADDR)
         DC    X'1D224348'
         DC    AL4(BLKLNGTH+1)
FPATN    DC    X'1F82505B'
         DC    AL4(ALLOCLBL)
         DC    AL4(BLOCK+1)
         DC    X'1D026162'
         DC    AL4(BLOCK+5)
         DC    X'1D126A6E'
         DC    AL4(BLOCK+9)
         DC    F'0'
SDPATN   DC    X'1F320006'
         DC    AL4(SPQELBL)
         DC    AL4(SPQEPTR+1)
         DC    X'1D300E14'
         DC    AL4(FLGSPQE)
         DC    X'1D10181C'
         DC    AL4(SUBPOOL)
         DC    X'15202025'
DPATN    DC    X'1F32282E'
         DC    AL4(DQELBL)
         DC    AL4(DQEPTR+1)
         DC    X'1D22363B'
         DC    AL4(BLKADDR)
         DC    X'1D224348'
         DC    AL4(BLKLNGTH+1)
         DC    F'0'
SPATN    DC    X'1F320006'
         DC    AL4(SPQELBL)
         DC    AL4(SPQEPTR+1)
         DC    X'1D300E14'
         DC    AL4(FLGSPQE)
         DC    X'1D10181C'
         DC    AL4(SUBPOOL)
         DC    X'15202025'
         DC    F'0'
         EJECT
******************************************************************
*
*        SUBROUTINE TO GET A DUMPED PAGE.  USES REGISTERS 0,8,9,
*        14, AND 15.  INPUT IN REG 8 IS THE ADDRESS OF BLOCK TO BE
*        GOTTEN.  THIS ROUTINE SETS UP FOR PAGE ACCESS, AND, ON
*        RETURN, SETS REG 8 TO CORE LOCATION OF RETRIEVED CONTROL
*        BLOCK.  RETURN CODE IS PASSED IN REG 15 TO CALLER.
*        RC = 0, IF BLOCK SUCCESSFULLY RETRIEVED.
*
******************************************************************
         SPACE  2
GETBLOK  SRDL  8,12(0)                    SET PAGE ADDRESS
         SLL   8,12(0)                     IN REGISTER 8
         SRL   9,20(0)                     DISPLACEMENT IN REG 9
         LR    0,8                        SET 0 FOR ACCESS
         L     15,ADPLMEMA                GET ACCESS ROUTINE E.P.
         BALR  14,15                      AND GO GET THE PAGE
         LR    8,0                        TRANSFER CORE LOCATION
         AR    8,9                        ADD DISPLACEMENT BACK
         BR    11                         AND RETURN, REG 8 SET
         SPACE  2
*        MESSAGE ROUTINES
         SPACE  2
LOOPMSG  L     6,ADPLBUF                   GET PRINT BUFFER
         MVC   0(25,6),CBLOOPMS           MOVE 'POSSIBLE LOOP' MSG
         B     MSGSET                     BR TO OUTPUT MSG
CBMSG    L     6,ADPLBUF                  GET PRINT BUFFER
         MVC   0(25,6),CBUNAV             MOVE 'UNAVAILABLE' MSG
MSGSET   L     15,ADPLPRNT                GET PRINT ROUTINE E.P.
         BALR  14,15                      LINK TO IT
         BR    11                         AND THEN RETURN
         SPACE  2
*        MESSAGES
         SPACE  2
TITLE    DC    C'*** CSA MAP ***'
CBUNAV   DC    C'UNABLE TO ACCESS '
CBNAME   DC    C'BLOCK   '
         DC    C'CSAMAP TERMINATING    '
CBLOOPMS DC    C'POSSIBLE LOOP IN '
CBLNAME  DC    C'BLOCK   '
CMSSBLK  DC    C'*** CSA MSS BLOCKS ***'
SMSSBLK  DC    C'*** SQA MSS BLOCKS ***'
         EJECT
MSSDATA  DS    0F
SPQEPTR  DC    F'0'
SPQE     EQU   *
NSPQE    DC    F'0'
DQEPTR   DC    F'0'
FLGSPQE  DC    XL1'00'
RSVDSPQE DC    XL1'00'
SUBPOOL  DC    XL1'00'
KEY      DC    XL1'00'
DQE      EQU   *
FQEPTR   DC    F'0'
NDQE     DC    F'0'
HIARCH   DC    XL1'00'
BLKADDR  DC    XL3'00'
BLKLNGTH DC    F'0'
FQE      EQU   *
FLGFQE   DC    XL1'00'
NFQE     DC    XL3'00'
FREEBYTE DC    F'0'
HIADDR   DC    F'0'
         SPACE  2
*        FOLLOWING IS PATTERN MASK, SET BY CALLERS OF FPROUT.
         SPACE  2
PATTERN  DC    XL1'1C'                    INITIALIZED TO SDF
         SPACE  2
*        FOLLOWING ARE LABELS FOR AMDPRDMP FORMAT ROUTINE.
SPQELBL  DC    C'SPQEFLAGSPKEY'
DQELBL   DC    C'-DQEBLKLNG'
ALLOCLBL DC    C'ALLOCATED-LN'
         EJECT
*        FOLLOWING IS THE WORKAREA.
         SPACE  3
CSAWORK  DS    0F                         ALLIGN TO WORD
CMAPSAV  DS    18F
SAVPTR   DS    1F
BLOCK    DC    3F'0'
GDAPTR   DC    F'0'
MYGDA    DS    14F                        MOVED GDA
PQEPTR   DC    F'0'
MYPQE    DS    8F                         MOVED PQE (CSA)
MYFBQE   DS    4F'0'                      MOVED FBQE
MAXSPQE  DC    F'400'                     CSA SPQE MAX COUNT
MAXDQE   DC    F'600'                     CSA MAX DQE COUNT
MAXFQE   DC    F'800'                     CSA MAX FQE COUNT
SPQECT   DS    1F                         OPERATIONAL SPQE COUNT
DQECT    DS    1F                         OPERATIONAL DQE COUNT
FQECT    DS    1F                         OPERATIONAL FQE COUNT
SQACT    DC    F'9'                       SQA MAX COUNTS - SPQE
         DC    F'200'                                    - DQE
         DC    F'900'                                    - FQE
         EJECT
CVTCB    DC    CL8'CVT'                   LABELS FOR MESSAGES
GDACB    DC    CL8'GDA'
SPQECB   DC    CL8'SPQE'
DQECB    DC    CL8'DQE'
FQECB    DC    CL8'FQE'
CPQECB   DC    CL8'CSA PQE'
CFBQECB  DC    CL8'CSA FBQE'
*        DSECTS FOR LABEL REFERENCES FO ADPL AND GDA
         IHAABDPL
         IHAGDA
         END   CSAMAP1
++PTF (PK00002).
++VER (Z037) PRE (UZ28870).
++VER (Z037) PRE (UZ30572).
++VER (Z038) FMID (EBB1102) PRE(UZ28870).
++VER (Z038) FMID (EBB1102) PRE(UZ30572).
++VER (Z038) FMID (EBB1102) PRE UZ31872).
++VER (Z038) FMID (EBB1102) PRE(UZ34680).
++VER (Z038) FMID (EBB1102) PRE(UZ35461).
++VER (Z038) FMID (EBB1102) PRE(UZ36943)                  /*
************************************************************************
* NOTE:  ALTHOUGH THIS ZAP IS WRITTEN WITH SMP CONTROL                 *
*        STATEMENTS, IT IS RECOMMENDED THAT THE CONTROL                *
*        STATEMENTS BE REMOVED AND THE ZAP BE APPLIED                  *
*        TO AN ALTERNATE NUCLEUS VIA SUPERZAP.  NOTE ALSO              *
*        THAT THE NAME STATEMENTS SPECIFYING IEANUC01 WILL             *
*        HAVE TO BE CHANGED TO THE NAME OF THE ALTERNATE               *
*        NUCLEUS YOU HAVE BUILT                                        *
************************************************************************
*        SUPPORTED BY THE 5752SC1CH C/T.                               *
*        ANY QUESTIONS SHOULD BE DIRECTED TO THE C/T VIA THE           *
*        CALL QUEUE                                                    *
************************************************************************
* DESCRIPTION:
*      THE FOLLOWING IS A GENERAL TRACE FOR THE FOLLOWING:
*           1. IEAVGM00 BRANCH ENTRIES (GM,FM,RM,QC,CR,GL)
*           2. IEAVGM00 RETURN (BOTH BRANCH AND SVC GETMAINS)
*
*      FORMAT OF IEAVGM00 TRACE ENTRY
*         OFFSET
*         IN HEX   DISCRIPTION
*            0     TYPE OF ENTRY. HALFWORD
*                      C'GM  ' - GMBRANCH ENTRY POINT
*                      C'FM  ' - FMBRANCH ENTRY POINT
*                      C'RMQC' - RMBRANCH OR QCBRANCH ENTRY POINT
*                      C'CR  ' - CRBRANCH ENTRY POINT
*                      C'GL  ' - GLBRANCH ENTRY POINT
*                      C'RET ' - RETURN FROM IEAVGM00 (GETMAINS ONLY)
*            4     REGISTERS 0 TO 15. 16 WORDS
*           44     TIME OF DAY CLOCK VALUE (8 BYTES).
*
*      END OF IEAVGM00 TRACE ENTRY
*
*      REGISTER CONTENTS FOR IEAVGM00 TRACE ENTRIES
*         FOR ALL BRANCH ENTRIES SEE THE PROLOG OF IEAVGM00 FOR THE
*         CONTENTS OF THE REGISTERS SAVED IN THE TRACE RECORDS.
*
*         FOR THE GETMAIN RETURN ENTRIES THE REGISTERS ARE AS
*         FOLLOWS:
*             REG 0 - WORK REG.           8 - FIRST BASE
*                 1 - WORK REG. (FQE)     9 - IEAVGM00 INTERNAL LINK
*                 2 - ADDRESS ASSIGNED    A - LENGTH OF ASSIGNED AREA
*                 3 - WORK REG.           B - POINTER TO LIST IF LIST
*                 4 - LDA ADDRESS         C - SUBPOOL ID
*                 5 - WORK REG.           D - SECOND BASE
*                 6 - WORK REG.           E - USED FOR TRACE LINK
*                 7 - WORK REG.           F - WORK REG.
*
*
* INSTALLATION:
*
*   1. CHOOSE THE HOOKS THAT FIT YOUR SYSTEM LEVEL.
*   2. APPLY THE TRACE ZAP AND THE HOOK ZAP.
*   3. BRING UP GTF AND USE AT LEAST-
*
*           TRACE=USR,SVC=(004,005,010,120)
*
*      THE ABOVE IS THE MINIMUM. IF AT ALL POSSIBLE TRACE
*      ALL (INCLUDING USR) SO ANY NEEDED INFORMATION CAN
*      BE EDITED OFF.
*      ALSO USE TIME STAMPS TO HELP REDUCE THE EDITED OUTPUT.
*   4. RECREATE THE PROBLEM AFTER GTF IS ACTIVE.
*
* LIBRARIES AND MODULES HIT:
*      LIBRARY        MODULE     CSECT
*      SYS1.NUCLUES   IEANUC01   IEAVGM00
*                                IEAVFX00
*
* RESTRICTIONS:
*   1. IF THE ZAPS TO IEAVFX00 ARE RELOCATED THE LINES WITH THE
*      ASTERISKS MUST BE ADJUSTED.
*
* UPDATE HISTORY:
*      DATE          DESCRIPTION
*      06/19/80      ADD COMMENTS FOR REGISTER CONTENTS.
*                    ADD SUPPORT FOR NEW LEVELS OF MODULES.
*      11/14/79      ADD SUPPORT FOR NEW LEVELS OF MODULES.
*      11/13/79      CHANGED REP 092C FROM 44C0,0930 TO 44C0,0938.
*      08/23/79      ADD PROLOG AND COMMENTS.
*      07/11/79      CORRECT ERRORS IN ZAP.
*      01/20/81      REWRITTEN TO SPLIT THE PAGEFIX/PAGEFREE TRACE
*                    FROM THE GETMAIN/FREEMAIN TRACE. ADDED A TRACE
*                    ENTRY TO GLBRANCH ENTRY POINT.  ALSO ADDED
*                    VERS AND REPS FOR PTF LEVEL UZ34680.
*      05/18/81      CHANGED REP IN IEAVGM00 PTF LEVEL UZ34680 FROM
*                    45E0,0718 TO 45E0,0710
*      05/18/81      RETROFITTED S/ZAP FOR PTF LEVEL UZ35390 (JBB1226)
*                    SEE DLL FILE NAMED GM00V2 FOR A COPY.
*      08/11/81      RETROFITTED S/ZAP FOR PTF LEVEL UZ35461 AND
*                    UZ36943.
* ====================================================================
*                                                                  */.
++ZAP(IEAVGM00)         /* PRE UZ25020 OR UZ27118)      */.
NAME IEANUC01 IEAVGM00
VER 0188 5040,700C          GMBRANCH HOOK
VER 01CC 5040,700C          FMBRANCH HOOK
VER 0210 5040,700C          RMBRANCH AND QCBBRANCH HOOK
VER 028C 5040,4228          GLBRANCH HOOK                    01/20/81
VER 02EE 5040,700C          CRBRANCH HOOK
VER 0C60 91C0,41FA          RETURN HOOK
REP 0188 45E0,0700        * GO TO GMBR
REP 01CC 45E0,0708        * GO TO FMBR
REP 0210 45E0,0710        * GO TO RMQCB
REP 028C 45E0,079C        * GO TO GLBR                       01/20/81
REP 02EE 45E0,0718        * GO TO CRBR
REP 0C60 45E0,0748        * GO TO RET
*
++ZAP(IEAVGM00)     /* PRE UZ28870 OR UZ30572 OR UZ31872     */.
NAME IEANUC01 IEAVGM00
VER 0188 5040,700C          GMBRANCH HOOK                    11/14/79
VER 01CC 5040,700C          FMBRANCH HOOK                    11/14/79
VER 0210 5040,700C          RMBRANCH AND QCBBRANCH HOOK      11/14/79
VER 028C 5040,4228          GLBRANCH HOOK                    01/20/81
VER 02EE 5040,700C          CRBRANCH HOOK                    11/14/79
VER 0C68 91C0,41FA          RETURN HOOK                      11/14/79
REP 0188 45E0,0700        * GO TO GMBR                       11/14/79
REP 01CC 45E0,0708        * GO TO FMBR                       11/14/79
REP 0210 45E0,0710        * GO TO RMQCB                      11/14/79
REP 028C 45E0,079C        * GO TO GLBR                       01/20/81
REP 02EE 45E0,0718        * GO TO CRBR                       11/14/79
REP 0C68 45E0,0748        * GO TO RET                        11/14/79
*
++ZAP(IEAVGM00)        /* PRE UZ34680 OR UZ35461    */.
NAME IEANUC01 IEAVGM00                                       01/20/81
VER 01F4 5040,700C          GMBRANCH HOOK                    01/20/81
VER 02A2 5040,700C          FMBRANCH HOOK                    01/20/81
VER 0350 5040,700C          RMBRANCH AND QCBBRANCH HOOK      01/20/81
VER 0498 5040,700C          CRBRANCH HOOK                    01/20/81
VER 03CA 5040,4228          GLBRANCH HOOK                    01/20/81
VER 0E10 91C0,41FA          RETURN HOOK                      01/20/81
REP 01F4 45E0,0700        * GO TO GMBR                       01/20/81
REP 02A2 45E0,0708        * GO TO FMBR                       01/20/81
REP 0350 45E0,0710        * GO TO RMQCB                      05/18/81
REP 0498 45E0,0718        * GO TO CRBR                       01/20/81
REP 03CA 45E0,079C        * GO TO GLBR                       01/20/81
REP 0E10 45E0,0748        * GO TO RET                        01/20/81
*
++ZAP(IEAVGM00)        /* PRE UZ36943    */.
NAME IEANUC01 IEAVGM00                                       08/11/81
VER 0234 5040,700C          GMBRANCH HOOK                    08/11/81
VER 02E2 5040,700C          FMBRANCH HOOK                    08/11/81
VER 0390 5040,700C          RMBRANCH AND QCBBRANCH HOOK      08/11/81
VER 04D8 5040,700C          CRBRANCH HOOK                    08/11/81
VER 040A 5040,4228          GLBRANCH HOOK                    08/11/81
VER 0E50 91C0,41FA          RETURN HOOK                      08/11/81
REP 0234 45E0,0700        * GO TO GMBR                       08/11/81
REP 02E2 45E0,0708        * GO TO FMBR                       08/11/81
REP 0390 45E0,0710        * GO TO RMQCB                      08/11/81
REP 04D8 45E0,0718        * GO TO CRBR                       08/11/81
REP 040A 45E0,079C        * GO TO GLBR                       08/11/81
REP 0E50 45E0,0748        * GO TO RET                        08/11/81
*
++ZAP(IEAASU00).
NAME IEANUC01 IEAVFX00
* -------- VERIFY THAT STORAGE IS AVAILABLE -----
VER 05F0 0000,0000          START OF GTF TRACE BUFFER
VER 0660 0000,0000          END OF GTF TRACE BUFFER
VER 0700 0000,0000          START OF ZAP AREA
VER 07CE 0000,0000          END OF ZAP AREA
* --------- TRACE TABLE VALUES -------------------
REP 05F0 0060               LENGTH OF TRACE TABLE AND SIZE OF GTF REC
REP 05F2 004E               FID FOR GTF TRACE FORMATTER
REP 05F4 0000,0600        * START OF TRACE TABLE AND GTF RECORD
REP 05F8 0000,0660        * END OF TRACE TABLE
REP 05FC 0000,0600        * CURRENT TRACE ENTRY
* ------------- GMBRANCH ENTRY (GMBR) -----------
REP 0700 4130,0784        * POINT AT TRACE ID
REP 0704 47F0,071C        * GO TO COMMON CODE
* ------------- FMBRANCH ENTRY (FMBR) -----------
REP 0708 4130,0788        * POINT AT TRACE ID
REP 070C 47F0,071C        * GO TO COMMON CODE
* ----- RMBRANCH AND QCBRANCH ENTRY (RMQCB) -----
REP 0710 4130,078C        * POINT AT TRACE ID
REP 0714 47F0,071C        * GO TO COMMON CODE
* ------------- CRBRANCH ENTRY (CRBR)------------
REP 0718 4130,0790        * POINT AT TRACE ID
* ------------- COMMON CODE ---------------------
REP 071C 182E               SAVE LINK REGISTER
REP 071E 58E0,05F4        * GET TRACE ENTRY
REP 0722 D203,E000,3000     PUT ID IN TRACE ENTRY
REP 0728 D23F,E004,720C     TRACE REGISTERS AT ENTRY TO GM00
REP 072E B205,E044          PUT TIME OF DAY INTO TRACE RECORD
REP 0732 0700,0700,0700     UNUSED INSTRUCTION               01/28/81
REP 0738 45A0,0774        * BALR TO DO GTF TRACE             01/20/81
REP 073C 12E2               PUT RETURN ADDR. IN REG 14       01/20/81
REP 073E 980D,720C          RESTORE REGS 0 TO 13             01/20/81
REP 0742 5040,700C          REPLACE OVERLAID INSTRUCTION
REP 0746 07FE               RETURN TO IEAVGM00               01/20/81
* ------------ RETURN --------------------------
REP 0748 900F,438C          SAVE REGISTERS
REP 074C 58E0,05F4        * GET TRACE ENTRY
REP 0750 D203,E000,0794   * PUT ID IN TRACE ENTRY
REP 0756 D23F,E004,438C     TRACE REGISTERS AT EXIT FROM GM00
REP 075C B205,E044          PUT TIME OF DAY INTO TRACE RECORD 01/28/81
REP 0760 0700,0700,0700     UNUSED INSTRUCTION
REP 0766 45A0,0774        * BALR TO DO GTF TRACE
REP 076A 980F,438C          RESTORE REGISTERS
REP 076E 91C0,41FA          REPLACE OVERLAID INSTRUCTION
REP 0772 07FE               RETURN TO IEAVGM00
* -------- COMMON ROUTINES TO TRACE THE GTF BUFFER ----------------- *
REP 0774 41B0,05F0        * POINT AT TRACE TABLE POINTERS
REP 0778 18D1               SAVE REG 1 OVER GTF HOOK
REP 077A 181B               SET UP PLIST PTR
REP 077C AF0E               GTF HOOK  (X'AF0E02FF')
REP 077E 02FF               EID
REP 0780 181D               RESTORE REG 1
REP 0782 07FA               RETURN TO CALLER
* -------------- DATA AREAS ---------------------------------------- *
REP 0784 C7D4,4040          GMBRANCH ID
REP 0788 C6D4,4040          FMBRANCH ID
REP 078C D9D4,D8C3          RMBRANCH OR QCBRANCH ID
REP 0790 C3D9,4040          CRBRANCH ID
REP 0794 D9C5,E340          RETURN ID
REP 0798 C7D3,4040          GLBRANCH ID                      01/20/81
* -------------- GLBRANCH ENTRY ----------------------------------------
REP 079C 4130,0798        * POINT AT TRACE ID                01/20/81
REP 07A0 182E               SAVE LINK ADDRESS                01/20/81
REP 07A2 58E0,05F4        * GET TRACE ENTRY                  01/20/81
REP 07A6 D203,E000,3000     PUT ID IN TRACE ENTRY            01/20/81
REP 07AC D23F,E004,420C     TRACE REGISTERS AT ENTRY TO GM00 01/20/81
REP 07B2 B205,E044          PUT TIME OF DAY IN TRACE RECORD  01/20/81
REP 07B6 0700,0700,0700     UNUSED INSTRUCTION               01/28/81
REP 07BC 45A0,0774        * BALR TO DO GTF TRACE             01/20/81
REP 07C0 12F2               PUT RETURN ADDR. IN REG 15       01/20/81
REP 07C2 980E,420C          RESTORE REGS 0 THRU 14           01/20/81
REP 07C6 5040,4228          REPLACE OVERLAID INSTRUCTION     01/20/81
REP 07CA 07FF               RETURN TO IEAVGM00               01/20/81
* * * END OF ZAPS * * *
