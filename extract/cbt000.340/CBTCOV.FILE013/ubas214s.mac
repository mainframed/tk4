* STAND 06/22/79
***********************************************************************
*                                                                     *
*        ADRESSE DER ABSTIMMSUMME IM AUFRUFENDEN PROGRAMM             *
*        UND LAENGE DER SUMME SUCHEN                                  *
*        AUFRUF = CALL SUCHADR,(INDEX,ADR,LENG,ERR,DEZ),VL            *
*        CL4   INDEX = TTSS , TT = TABELLENINDEX                      *
*                             SS = INDEX DER SUMME INNERHALB D.TABELLE*
*                             (TTSS NUM. OHNE VORZEICHEN)             *
*        A     ADR   = GESUCHTE ADRESSE                               *
*        H     LENG  = GESUCHTE LAENGE D.SUMME                        *
*        F     ERR   = FEHLERCODE,  0 = OK                            *
*                                   1 = FEHLER                        *
*        H     DEZ  = EINGABE = DEZIMALSTELLE UNGEPACKT
*                               (1 STELLE LINKS IN DEZ)
*                     AUSGABE = DEZIMALSTELLE BINAER
*                               0 <= DEZ <= 3
*                     DEZ KANN IN ARGUMENTENLISTE FEHLEN
***********************************************************************
         SPACE 3
SUCHADR  CSECT
         XSAVE 12,SAVE,SUCHADR
         SPACE 2
         AREG
         SPACE 2
         ST    R1,REG1                  REG.1 RETTEN
         L     R11,0(,R1)               R11 --> INDEX
         USING DATAB,R10
         L     R10,PATAB                --> ATAB
         SPACE 2
*  INDEX AUF NUM. PRUEFEN
         MVC   WORD,0(R11)              INDEX ZWISCHENSPEICHERN
         NC    WORD,=X'F0F0F0F0'
         CLC   WORD,=X'F0F0F0F0'
         BNE   FEHLER                   NICHT NUMERISCH
         SPACE 2
*  ANFANGSADRESSE DER GEWUENSCHTEN SUMMENTABELLE BERECHNEN
         PACK  DWORD,0(2,R11)           TAB-INDEX GEPACKT
         CVB   R3,DWORD                 TAB-INDEX BINAER
         C     R3,HPSUMN
         BH    FEHLER                   TAB-INDEX> ANZAHL TABELLEN
         BCTR  R3,0                     R3 - 1
         SLA   R3,2                     R3 * 4 = TAB-DISPL.ZU TAB-ANF.
         LA    R4,PHPSUM                --> 1.SUMTAB-ADRESSE
         AR    R4,R3                    --> GESUCHTE SUMTAB-ADRESSE
         SPACE 1
*  DISPLACEMENT DER SUMME INNERHALB DER SUMMENTABELLE BERECHNEN
         PACK  DWORD,2(2,R11)           SUM-INDEX GEPACKT
         CVB   R3,DWORD                 SUM-INDEX BINAER
         CH    R3,TABANZ*4(,R4)         VERGLEICH MIT ZUGEHOER. NHPSUM
         BH    FEHLER                   SUM-INDEX > ANZAHL D.SUMMEN
         BCTR  R3,0                     R3-1
         SR    R6,R6
         LR    R7,R3               UMSPEICHERN FUER MULTIPL.
         LH    R5,TABANZ*4+2(,R4)         ZUGEHOER. SUMMENLAENGE
         MR    R6,R5                    DISPLACEMENT SUMME
         SPACE 1
*  SUMMENADRESSE U. SUMMENLAENGE BERECHNEN U. ABSPEICHERN
         L     R11,REG1                 --> UP-PARAMETERLISTE
         L     R2,8(,R11)               --> LENG
         CLI   ASPRACHE,FORTRAN
         BNE   NOFORT
         LA    R5,8                    WENN GEPACKT  LAENGE = 8
NOFORT   EQU   *
         STH   R5,0(,R2)                SUMMENLAENGE SPEICHERN
         L     R2,4(,R11)               --> ADR
         L     R4,0(R4)                 --> GESUCHTE SUMMENTABELLE
         AR    R4,R7                    -->SUMTAB + DISPLACEMENT
         ST    R4,0(,R2)                = ADR
         SPACE 1
         L     R2,12(,R11)              --> FEHLERCODE
         XC    0(4,R2),0(R2)            FEHLERCODE = 0
         TM    12(R11),X'80'
         BNZ   RETURN         ARGUMENT DEZ FEHLT
*
*  DEZIMALSTELLE DER SUMME UNGEPACKT --> BINAER
*
         L     R3,16(,R11)    -->DEZ
         CLI   0(R3),C'0'
         BNL   *+8
         MVI   0(R3),C'0'
         CLI   0(R3),C'3'
         BNH   *+8
         MVI   0(R3),C'3'
         PACK  DWORD,0(1,R3)
         CVB   R2,DWORD
         STH   R2,0(R3)       DEZIMALSTELLE BINAER
RETURN   EQU   *
         XRETURN
         SPACE 3
*  FEHLERHAFTE INDEXANGABE
FEHLER   EQU   *
         L     R11,REG1
         L     R2,12(,R11)              --> FEHLERCODE
         MVC   0(4,R2),=F'1'            FEHLERCODE = 1
         B     RETURN
         SPACE 3
*  DEFINITIONEN
DWORD    DS    D                        DOPPELWORT
WORD     DS    F                        WORT
REG1     DS    F                        SPEICHER REG.1
PATAB    DC    V(ATAB)                  --> ATAB
         LTORG
         EJECT
         MATAB
         END
