 ./       NUMBER SEQ1=100,INCR=100
MFP2     TITLE 'MFP2 MULTI-FUNKTION-PROGRAM      ALLOACTION-ROUTINE'
         PRINT ON,NOGEN
IGC01240 CSECT
*              REG 2 FUER BASIS-REGISTER
*              REG 3 ADRESSE COMMUNICATION VECTOR TABLE
*              REG 4 ADRESSE TASK-CONTROL-BLOCK
*              REG 5 BEREICHE IM DYN. HAUPTSPEICHER
*              REG 6 TIOTADR
*              REG 7 UCB-ADRESSE
*              REG  8 ARBEITSREGISTER
*              REG  9 ARBEITSREGISTER
*              REG 10 ARBEITSREGISTER
*              REG 13 SAVEAREA
*              REG 14 LINKAGE
*              REG 15 LINKAGE
         USING CVT,3
         USING TCB,4
         USING DUMMYS,5
         USING TIOT,6
         USING UCB,7
WORK2    EQU   7
CVTR     EQU   8
WORK1    EQU   9
INDEXR   EQU   10
         SAVE  (14,12)
         BALR  2,0
         USING *,2
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         L     5,0(1)
         MVC   OFFLINEM+17(7),INPUT
         ENQ   (QNAME,RNAME,E,2,SYSTEM)
ANFANG   L     3,16
         L     4,CVTTCBP
         L     4,4(4)
         CLI   VOLSER,C' '             TEST AUF EINGABE EINER VOLSERNO
         BNE   *+10                    JA, BRANCH
         MVC   VOLSER(3),=C'BLP'       NO, MOVE BLP-KONSTANTE
CANF     CLC   FUNKTION(6),=C'HLESEN'
         BE    CONA1A
         CLC   FUNKTION(6),=C'HDRUCK'
         BE    COND1A
         CLC   FUNKTION(6),=C'HSTANZ'
         BE    CONS1A
SUBCON0  LA    15,16                   SETZE FEHLER-INDIKATOR
SUBCON2  ST    15,SAVE
         DEQ   (QNAME,RNAME,2,SYSTEM),RET=HAVE
         L     15,SAVE
         L     13,SAVEAREA+4
         RETURN (14,12),RC=(15)
CONA1A   TM    SCHALTER,B'00000001'
         BO    SUBCON2
         TIME  TU
         ST    0,HLESEN+4
         MVC   HLESEN+16(8),=CL8' '
         MVC   HLESEN+28(3),INPUT
         MVC   HLESEN+31(3),OUTPUT
         MVC   UNITNAME,INPUT
         MVC   DDNAME,=C'LESER   '
         BAL   11,ALLOCATE             BRANCH ZUR ALLOCATION-ROUTINE
         MVC   UNITNAME,OUTPUT
         MVC   DDNAME,=C'HREAD   '
         BAL   11,ALLOCATE             BRANCH ZUR ALLOCATION-ROUTINE
         XC    UCBLIST+2(2),UCBLIST+2
         CLI   UCBTBYT3,X'20'
         BNE   *+8
         STH   7,UCBLIST+2
         OI    SCHALTER,B'00000001'
         LA    15,0
         B     OFFLINE                 BRANCH TO DISPLAY MESSAGE
CONS1A   TM    SCHALTER,B'00000100'
         BO    SUBCON0
         TIME  TU
         ST    0,HSTANZ+4
         MVC   HSTANZ+16(8),=CL8' '
         MVC   HSTANZ+28(3),INPUT
         MVC   HSTANZ+31(3),OUTPUT
         MVC   UNITNAME,OUTPUT
         MVC   DDNAME,=C'STANZER '
         BAL   11,ALLOCATE             BRANCH ZUR ALLOCATION-ROUTINE
         MVC   UNITNAME,INPUT
         MVC   DDNAME,=C'HSTANZ  '
         BAL   11,ALLOCATE             BRANCH ZUR ALLOCATION-ROUTINE
         OI    SCHALTER,B'00000100'
         LA    15,8                    SETZE BRANCH-INDIKATOR 'HSTANZ'
         B     OFFLINE                 BRANCH TO DISPLAY MESSAGE
COND1A   TM    SCHALTER,B'00010000'
         BO    COND2A
         TIME  TU
         ST    0,HDRUCK+4
         MVC   HDRUCK+16(8),=CL8' '
         MVC   HDRUCK+28(3),INPUT
         MVC   HDRUCK+31(3),OUTPUT
         MVC   UNITNAME,OUTPUT
         MVC   DDNAME,=C'PRINTER '
         BAL   11,ALLOCATE             BRANCH ZUR ALLOCATION-ROUTINE
         MVC   UNITNAME,INPUT
         MVC   DDNAME,=C'LISTE   '
         BAL   11,ALLOCATE             BRANCH ZUR ALLOCATION-ROUTINE
         MVC   DDNAME,=CL8'LISTEX'
         BAL   11,ALLOCATE
         OI    SCHALTER,B'00010000'
         LA    15,4                    SETZE BRANCH-INDIKATOR 'HINITT'
         B     OFFLINE                 BRANCH TO DISPLAY MESSAGE
COND2A   TM    SCHALTER,B'00100000'
         BO    SUBCON0
         TIME  TU
         ST    0,HDRUCK1+4
         MVC   HDRUCK1+16(8),=CL8' '
         MVC   HDRUCK1+28(3),INPUT
         MVC   HDRUCK1+31(3),OUTPUT
         MVC   UNITNAME,OUTPUT
         MVC   DDNAME,=C'PRINTER1'
         BAL   11,ALLOCATE             BRANCH ZUR ALLOCATION-ROUTINE
         MVC   UNITNAME,INPUT
         MVC   DDNAME,=C'LISTE1  '
         BAL   11,ALLOCATE             BRANCH ZUR ALLOCATION-ROUTINE
         MVC   DDNAME,=CL8'LISTEX'
         BAL   11,ALLOCATE
         LA    15,12                   SETZE BRANCH-INDIKATOR 'HDRUCK1'
         OI    SCHALTER,B'00100000'
         B     OFFLINE                 BRANCH TO DISPLAY MESSAGE
ALLOCATE CLI   INPUT+3,C'-'
         BE    LOOKUCB
         WTO   'MFP ALLOCATION FEHLT',ROUTCDE=13
         B     SUBCON0
LOOKUCB  EQU   *
         L     6,TCBTIO
         LA    6,24(0,6)
         L     8,CVTILK2
         LR    10,1
P01      EQU   *
         CLC   UNITNAME,=C'   '
         BE    SUBCON0
P1       CLC   0(2,8),=X'0000'
         BNE   P3
P2       LA    8,2(0,8)
         B     P1
P3       CLC   0(2,8),=X'FFFF'
         BE    P4
         LH    7,0(0,8)
         CLC   UCBNAME,UNITNAME
         BE    P5
         B     P2
P4       EQU   *
         WTO   'MFP UNIT NICHT VERFUEGBAR',ROUTCDE=13
         B     SUBCON0
P5       CLC   DDNAME,=CL8'LISTEX'
         BE    P6
         TM    SRTESTAT,SRTEONLI       DEVICE ONLINE?
         BZ    P4                      NEIN, BRANCH
         TM    SRTESTAT,SRTECHGS       CHANGE ONLINE TO OFFLINE?
         BO    P4                      JA,VERZWEIGE
         CLI   UCBTBYT3,X'20'          DEVICE = DIRECT ACCESS?
         BE    P6                      JA, BRANCH
         TM    SRTESTAT,SRTECHGS+SRTERESV+SRTEUNLD
         BO    P4
         TM    SRTESTAT,SRTEALOC       DEVICE ALLOCATED?
         BZ    P6                      NEIN, BRANCH
         TM    UCBWGT,B'11000000'
         BZ    P4
         WTO   'MFP UNIT=SYSIN/SYSOUT',ROUTCDE=13
P6       CLC   4(8,6),DDNAME
         BE    P7
         SR    10,10
         IC    10,0(6)
         AR    6,10
         B     P6
P7       STH   7,18(0,6)
         OI    1(6),B'00000010'
         CLI   UCBTBYT3,X'20'          DEVICE = DIRECT ACCESS ?
         BNE   *+24
         MVC   VOL1(44),SEARCHDS
         MVI   VOL1+45,X'01'
         MVC   VOL1+46(6),VOLSER
         B     RAUS
         NI    SRTESTAT,B'01111111'    SET DEVICE OFFLINE
         CLC   DDNAME,=CL8'LISTEX'
         BNE   RAUS
         CLI   UCBTBYT3,X'80'
         BNE   RAUS
         LA    10,JFCBDSNM
         ST    10,DCBEXIT
         MVI   DCBEXIT,X'87'
         RDJFCB (INDCB)
         MVC   JFCBVOLS(6),VOLSER
         LA    8,1(0,0)
         LA    9,6(0,0)
         LA    10,SEARCHKZ
P70      CLI   0(10),C','
         BE    P71
         CLI   0(10),C' '
         BE    P71
         LA    8,1(0,8)
         LA    9,6(0,9)
         LA    10,6(0,10)
         STC   9,*+5
         MVC   JFCBVOLS(30),VOLSER
         B     P70
P71      STC   8,JFCBNVOL
         MVI   JFCBDSNM,X'40'
         MVC   JFCBDSNM+1(43),JFCBDSNM
         MVC   JFCBDSNM(8),=C'HDRUCKBD'
         CLI   SEARCHKZ,C','
         BNE   P72
         MVC   JFCBDSNM(44),SEARCHKZ
         EJECT
*SUPPOSE WE TAKE A LOOK AT THE FUNDAMENTALS OF THE USE OF THE
*EXECUTE-CHANNEL-PROGRAM-MAKRO.
*A NICE SIMPLE OPERATION LIKE A TAPE-TO-TAPE COPY WILL BE OUR GOAL
*ON THIS ROUND
*
*JUST LIKE WHEN USING THE ACCESS METHODS, WE'RE STILL RESPONSIBLE
*FOR THE OPEN. LET'S DO THAT AND GET IT OUT OF THE WAY
*
P72      OPEN  (INDCB,(INPUT)),TYPE=J
         B     READ
*
*SINCE WE ARE GOING TO BE TALKING DIRECTLY TO THE INPUT/OUTPUT
*SUPERVISOR. THERE ARE A FEW MINOR ITEMS THAT WE ARE GOING TO HAVE
*TO TAKE CARE OF. LETS LIST THEM....
*
*  1. CONSTRUCTION OF AN EVENT CONTROL BLOCK.
*
*     OH YEAH..THEY HAD THOSE THINGS IN THE BSAM DATA-EVENT-CONTROL
*     BLOCKS. WELL WE DON'T GET ONE FOR FREE ANY MORE SO LET'S BUILD
*     US A COUPLE.......
*
*
INECB    DC    F'0'
*
*     THAT WASN'T HARD WAS IT
*
*  2. A CHANNEL PROGRAM.
*
*     DON'T GET EXITED. NO SIO OR TIO OR CAW OR ANY OF THAT STUFF.
*     JUST THE ELEMENTAL CHANNEL COMMAND TO DO THE WORK... HERE...
*
INCCW    CCW   2,VOL,X'40',80
         CCW   2,HDR1,X'40',80
         CCW   2,HDR2,X'00',80
*
*     SEE...JUST A LITTLE READ AND WRITE OPERATION.
*
*  3. CONSTRUCTION OF THE INPUT/OUTPUT-BLOCK. (IOB)
*
*     THIS IS SOME THING NEW.... LET'S REFER TO OUR SYSTEM-PROGRAMMERS-
*     GUIDE FOR A DETAILED DESCRIPTION OF THESE FIELDS. MEANWHILE THE
*     MAGIC ASSEMBLER WILL CONSTRUCT A COUPLE FOR US. GO GO BABE...
*
*     ONE FOR INPUT
INIOB    DS    0F
INFLG    DC    XL2'4000'          BIT 0,1,AND 6 SET BY USER.
INSENS   DC    H'0'               SENSE BYTES
INECBA   DC    A(INECB)           ECB ADDRESS
INSTAT   DC    2F'0'              CHANNEL STATUS
INCPA    DC    A(INCCW)           CHANNEL PROGRAM ADDRESS
IDCB     DC    A(INDCB)           DCB ADDRESS
IRES     DC    F'0'               RESTART ADDRESS
INCR     DC    H'1'               BLOCK COUNT INCREMENT
ICNTS    DC    H'0'               ERROR COUNTS FOR RETRY
*
*
*     AND THEY AREA BEAUTIFUL TO BEHOLD.
*
*  4. THE DATA CONTROL BLOCKS.
*
*     THIS IS ANOTHER FAMILIAR ITEM. HOWEVER THERE ARE A FEW NEW ITEMS
*     TO TAKE INTO CONSIDERATIONS. THE IMSK, UMSK AND THE APPANDAGE
*     FIELDS. BACK TO THE SYSTEM PROGRAMMERS GUIDE WHILE THE ASSEMBLER
*     CONJURES UP A FEW DCB'S......
*
INDCB    DCB   DSORG=PS,MACRF=(E),DDNAME=LISTEX,DEVD=TA,               =
               EXLST=DCBEXIT
*
*     NOTE THE IFLGS AND MACRF FIELDS WHEN USING EXCP. THEY ARE QUITE
*     IMPORTANT IN INFORMING THE OPEN ROUTINE AND IOS AS TO ERROR
*     CORRECTION PROCEDURES AND THE LOADING OF APPANDAGES.
*
*     THAT'S IT AS FAR AS THE CONTROL BLOCKS AND STUFF GO. NOW WE
*     CAN GO AHEAD AND PUT THE EXCP TO WORK FOR US. ONWARD......
*
READ     EXCP  INIOB
         WAIT  ECB=INECB
*
*  YOU GOTTA ADMIT THAT IT WASN'T TOO HARD TO GET THAT FIRST RECORD
*  READ IN, BUT NOW WE HAVE TO DO A LITTLE CHECKING TO SEE WHAT
*  TYPE COMPLETION CODES WE CAN RUN INTO THE EVENT CONTROL BLOCK.
*
*  LET'S SEE IF IT WAS A BOODY TYPE READ
*
         CLI   INECB,X'7F'        TEST GOOD COMPLETION
         BE    FINISH
*
*  AT THIS POINT WE EITHER HAD A GOOD COMPLETION OR SOMETHING OUT OF
*  THE ORDINARY HAS OCCURRED. NORMALY THE PERMANENT ERRORS AS WELL
*  AS END OF FILE INDICATION COME IN WITH CODE X'41'. WITH THE
*  OPERATION THAT WE ARE PERFORMING THIS IS THE CASE. LET'S TEST TO
*  SEE IF UNIT EXCEPTION IS PRESENT.......
*
         TM    INSTAT+4,X'01'     IS IT END OF FILE
         BO    FINISH             YES
*
*  IF NOT THEN THE IBM ERROR RECOVERY ROUTINES HAVE ALREADY DETERMINED
*  THAT IT'S A PERMANENT ERROR, SO YOU CAN EITHER ACCEPT IT OR....
*
SOS      WTO   'MFP FEHLER BEI READ-LABEL',ROUTCDE=13
         ABEND 4095,DUMP
*
*  IF YOU WISH TO CONTINUE AFTER DOING MORE EXTENSIVE CHECKING OF THE
*  STATUS AND SENSE INFORMATION, YOU NEED ONLY TURN OFF THE PERMANENT
*  ERROR INDICATORS IN THE DCBIFLGS FIELD.......
*
         NI    INDCB+44,X'3F'     BEGONE YOU MISERABLE ERROR
         B     READ
*
*  BEFORE WE CLOSE OUR DATA SETS WE HAVE TO MAKE SOME INDICATIONS IN
*  THE DCBOFLGS FIELDS SO THAT THE CLOSE ROUTINES KNOW WHAT TO DO IN
*  RELATION TO END OF DATA MARKERS AND LABELS, ETC. BITS 0,1,4 AND 5
*  ARE PERTINENT. REFER AGAIN TO THE PROGRAMMERS GUIDE FOR DETAILS.
*
*  IN THE MEANTIME WE WILL GO AHEAD AND ADD FINISHING TOUCHES.
*
FINISH   OI    INDCB+48,X'04'     TAPE MARK HAS BEEN READ
         CLOSE (INDCB)
*
* AND THAT ALL SHE WROTE.......
         MVI   VOL1,X'40'
         MVC   VOL1+1(79),VOL1
         MVC   VOL1(17),HDR1+4
         MVC   VOL1+45(1),JFCBNVOL
         MVC   VOL1+46(30),JFCBVOLS
         MVC   VOL1+46(6),VOL+4
RAUS     BR    11
VOL      DS    CL80
HDR1     DS    CL80' '
HDR2     DS    CL80' '
         DS    0F
DCBEXIT  DC    X'87'
         DC    AL3(DCBEXIT)
         EJECT
OFFLINE  ST    15,SAVE
OFFLINEM WTO   'MFP UNIT XXX-YYY NOW OFFLINE',ROUTCDE=13
         L     15,SAVE
         B     SUBCON2
SAVEAREA DS    18F
QNAME    DC    CL8'SYSIEFSD'
RNAME    DC    CL2'Q4'
DDNAME   DC    C'XXXXXXXX'
UNITNAME DC    C'XXX'
         IEFJFCBN
         LTORG
         PRINT ON,GEN
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       COMMUNICATION-AREA'
DUMMYS   DSECT
PARM240  EQU   *             PARAMETER-LISTE AN IGC00240
SCHALTER DC    B'00000000'   BIT 0 = TIMER-EXIT
*                            BIT 1 = SUCHDATEI
*                            BIT 2 = HINITT
*                            BIT 3 = HDRUCK
*                            BIT 4 = WTOR PENDING
*                            BIT 5 = HSTANZ
*                            BIT 6 = DISPLAY ERROR-MESSAGE
*                            BIT 7 = HLESEN
CONBER   DS    0CL30         CONSOL-EINGABE-BEREICH
         DC    CL2' '
AKTION   DC    C' '
         DC    C' '
FUNKTION DC    CL8' '
OPTION1  EQU   FUNKTION+6
OPTION2  EQU   FUNKTION+7
         DC    C' '
INPUT    DC    CL3' '
         DC    C'-'
OUTPUT   DC    CL3' '
         DC    C','
VOLSER   DC    CL6' '
SEARCHKZ DC    C','
SEARCHDS DC    CL44' '
HLESEN   DS    CL82          FUNKTIONS-SATZ HLESEN
HDRUCK   DS    CL82          FUNKTIONS-SATZ HDRUCK
HDRUCK1  DS    CL82          FUNKTIONS-SATZ HDRUCK1
HSTANZ   DS    CL82          FUNKTIONS-SATZ HSTANZ
A1ZAE    EQU   HLESEN+12
D1ZAE    EQU   HDRUCK+12
D2ZAE    EQU   HDRUCK1+12
S1ZAE    EQU   HSTANZ+12
TIMERECB DC    X'00000000'
DCBEOF   DC    A(DCBEOF)     ADRRESSE DES DCB'S FUER FUNKTIONS-ENDE
SAVE     DS    1F
         DC    H'0'
UCBLIST  DS    0F
         DC    X'8000'
         DC    H'0'
VOL1     DS    0CL80         VOLUME-LABEL IMAGE
         DC    C'VOL1'
VOL1SER  DC    C'OSXXXX'
         DC    C'0'
         DC    69CL1'0'
VOLIST   DC    H'1'          NUMBER OF VOLUMES
         TITLE 'MFP2 MULTI-FUNKTION-PROGRAM      C V T '
CVT      DSECT
         CVT   SYS=INT
         TITLE 'MFP2 MULTI-FUNKTION-PROGRAM      T C B '
TCB      TCB
         TITLE 'MFP2 MULTI-FUNKTION-PROGRAM      U C B '
UCB      DSECT
         IEFUCBOB
         TITLE 'MFP2 MULTI-FUNKTION-PROGRAM      T I O T '
TIOT     DSECT
         IEFTIOT1
         END
