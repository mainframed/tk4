ECBSCAN  START
*
*              SCAN ECB-LIST SUBROUTINE
*
*        THIS ROUTINE LOOPS UNTIL EITHER A POSTED ECB FOUND
*          OR A SEARCH THRU THE LIST HAS BEEN COMPLETED
*          (INDICATED BY AN ECB HAVING BEEN TESTED TWICE)
*
* ENTRY  R1    ADDR OF ECB ADDRESS LIST DESCRIPTION
* EXIT   R15   RETURNCODE
*              0   POSTED ECB FOUND
*              4   NO POSTED ECB FOUND
*        R1    ADDR OF POSTED ECB
*        CECBAPTR  LOC IN ADDR LIST
*
         SPACE 2
R1       EQU   1
R2       EQU   2
R3       EQU   3
         SPACE 2
PARAM    DSECT
*
*        ECB-ADDR-LIST DESCRIPTION
*
FECBAPTR DS    A                       FIRST ECB ADDR PTR
LECBAPTR DS    A                       LAST ECB ADDR PTR
CECBAPTR DS    A                       CURRENT ECB ADDR PTR
         EJECT
ECBSCAN  CSECT
         B     12(0,15)
         DC    X'07'
         DC    CL7'ECBSCAN'
         STM   R1,R3,24(13)            SAVE REGS USED
         LR    R3,15                   LOAD BASE
         USING ECBSCAN,R3
         LR    15,R1                   LOAD DSECT BASE
         USING PARAM,15
         L     R2,CECBAPTR             LOAD CURRENT ECB ADDR POINTER
         LTR   R2,R2
         BNZ   TESTLIST
         L     R2,FECBAPTR             START AT LIST ORIGIN
         MVC   CECBAPTR,LECBAPTR       SETUP
         B     TESTECB
TESTLIST EQU   *
         C     R2,LECBAPTR             END-OF-LIST
         BNE   *+12
         L     R2,FECBAPTR             LOAD START OF LIST
         B     TESTECB
         LA    R2,4(0,R2)              GET NEXT LIST ENTRY
TESTECB  EQU   *
         L     R1,0(0,R2)              LOAD ECB ADDR
         LTR   R1,R1
         BZ    TESTSCAN
         TM    0(R1),X'40'             ECB POSTED
         BZ    TESTSCAN
         NI    0(R1),X'3F'             RESET POST FLAG
         ST    R2,CECBAPTR             SAVE POINTER
         LA    R1,0(0,R1)              ZERO HIGH ORDER BYTE AND
         ST    R1,24(0,13)             STORE ECB ADDR
         LA    15,0                    INDICATE COMPLETE - ECB FOUND
         B     EXIT
TESTSCAN EQU   *
         C     R2,CECBAPTR             SCAN CYCLE COMPLETED
         BNE   TESTLIST
         LA    15,4                    INDICATE COMPLETE - NO ECB
EXIT     EQU   *
         LM    R1,R3,24(13)            RELOAD REGS
         BR    14
         END
