* STAND 05/19/81
ASTALLOC CSECT
         BR    14             DYNAMIC ALLOCATION NICHT NOTWENDIG
         AREG
         XSAVE R12,SAVE,ASTALLOC
         SPACE 1
*  UNALLOCATION DDNAME=ASTSUM
         SPACE 1
         LA    R1,UNLOCPTR
         SVC   99
         SPACE 2
*  DYNAMIC ALLOCATION ASTSUM
         SPACE 1
START    EQU   *
         LA    R1,ALOCPTR
         SVC   99
         LTR   R15,R15
         BNZ   FEHLER
         SR    R3,R3          RETURNCODE = OK
         SPACE 1
*  RETURN
         SPACE 1
RETURN   EQU   *
         XRETURN              (R3)
         TITLE 'FEHLER BEI ALLOCATION'
FEHLER   EQU   *
         LA    R3,ALOCP
         USING S99RB,R3
         SPACE 1
         CLC   S99ERROR,=X'0210'
         BE    WARTEN         ANDEREM JOB ZUGEORDNET
         SPACE 2
ABBRUCH  EQU   *
         ABEND 1,DUMP
         TITLE 'WARTEN, BIS ASTSUM VERFUEGBAR'
WARTEN   EQU   *
         STIMER WAIT,BINTVL=ZEIT
         B     START
         SPACE 1
ZEIT     DC    F'6000'        1 MINUTE
         SPACE 2
         LTORG
         TITLE 'DEFINITIONEN'
*  REQUEST BLOCK POINTER
         SPACE 1
ALOCPTR  DS    0F
         DC    AL1(128),AL3(ALOCP)
         SPACE 1
UNLOCPTR DS    0F
         DC    AL1(128),AL3(UNLOCP)
         SPACE 1
*  REQUEST BLOCK
         SPACE 1
ALOCP    DC    AL1(20),AL1(S99VRBAL),AL2(0)
         DC    1F'0'
         DC    A(ALOCTXT)
         DC    2F'0'
         SPACE 1
UNLOCP   DC    AL1(20),AL1(S99VRBUN),AL2(0)
         DC    1F'0'
         DC    A(UNLOCTXT)
         DC    2F'0'
         SPACE 1
*  TEXT UNIT POINTER
         SPACE 1
ALOCTXT  DC    A(ALOCDDN)
         DC    A(ALOCDSN)
         DC    A(ALOCSHR)
         DC    AL1(128),AL3(ALOCPERM)
         SPACE 1
UNLOCTXT DC    A(ULOCDDN)
         DC    AL1(128),AL3(ULOCPERM)
         SPACE 1
*  TEXT UNITS
         SPACE 1
         DS    0F
ALOCDDN  DC    AL2(DALDDNAM)  DDNAME
         DC    AL2(1),AL2(6)
         DC    CL6'ASTSUM'
         SPACE 1
         DS    0F
ALOCDSN  DC    AL2(DALDSNAM)  DSNAME
         DC    AL2(1),AL2(44)
         DC    CL44'OPV.ASTSUM'
         SPACE 1
         DS    0F
ALOCSHR  DC    AL2(DALSTATS)  DISP=SHR
         DC    AL2(1),AL2(1),AL1(8)
         SPACE 1
         DS    0F
ALOCPERM DC    AL2(DALPERMA)
         DC    AL2(0)
         SPACE 2
         DS    0F
ULOCDDN  DC    AL2(DUNDDNAM)  DDNAME
         DC    AL2(1)
         DC    AL2(6)
         DC    CL6'ASTSUM'
         SPACE 1
         DS    0F
ULOCPERM DC    AL2(DUNUNALC)
         DC    AL2(0)
         IEFZB4D0
         SPACE 3
         IEFZB4D2
         SPACE 3
         END
