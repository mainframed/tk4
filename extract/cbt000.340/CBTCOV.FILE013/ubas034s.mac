CONVXE   START
***  THIS  PROGRAM  CONVERTS A HEXADECIMAL-STRING
***  TO AN EBCDIC-STRING
***  EXPECTED VALUES :
***  REG0 : LENGTH OF STRING
***  REG1 : ADDRESS OF STRING
***  LENGTH OF EBCDIC-STRING = 2*REGO + 1
***  REENTRANT   WRITTEN
         USING *,15
         B     12(0,15)
         DC    X'07'
         DC    CL7'CONVXE'
         STM   14,12,12(13)
***  REGISTER USE :
ADDSAV   EQU   1                  ADDRESS
LENSAV   EQU   3                  LENGTH
WORKL1   EQU   4         USED FOR COMPUTATION
WORKL2   EQU   6                  OF LENGTHS
POINT1   EQU   7                  POINTER
POINT2   EQU   8                  POINTER
CON7     EQU   9                  CONSTANTS
CON14    EQU   10                 CONSTANTS
CON256   EQU   11                 CONSTANTS
*** LOADING OF CONSTANTS IN THE REGISTERS
         LA    CON7,7
         LA    CON14,14
         LA    CON256,256
*** INITIAL SETTING OF THE POINTER-REGS
         LR    LENSAV,0
         LA    POINT2,0(ADDSAV,LENSAV)
         LA    POINT1,0(POINT2,LENSAV)
*** IS LENGTH  A MULTIPLE OF SEVEN ?
         LR    WORKL1,LENSAV
         SRDA  WORKL1,32
         DR    WORKL1,CON7
         LTR   WORKL1+0,WORKL1      REMAINDER = 0  ?
         BZ    LOOP-4                 YES,SKIT EXECUTE-BRANCH
*** EXECUTE FOR  REMAINDER OF STRING
         LR    WORKL2,WORKL1
         SLL   WORKL2,1
         SR    POINT1,WORKL2
         SLL   WORKL2,4
         OR    WORKL2,WORKL1
         SR    POINT2,WORKL1
         EX    WORKL2,UNPK
         LR    WORKL2,LENSAV
         SR    WORKL2,WORKL1
*** IN THIS LOOP THE STRING IS CONVERTED IN
*** 7-BYTE BLOCKS FROM  HEXA TO  CHAR-DIGITS
LOOP     EQU   *
         LTR   WORKL2,WORKL2
         BNP   ENDUNPK
         SR    POINT2,CON7
         IC    WORKL1,0(POINT1)
         SR    POINT1,CON14
         UNPK  0(15,POINT1),0(8,POINT2)
         STC   WORKL1,14(POINT1)
         SR    WORKL2,CON7
         B     LOOP
ENDUNPK  EQU   *
***   THE  CHAR-DIGITS  ARE NOW TRANSLATED
         LR    POINT1,ADDSAV
         LR    WORKL1,LENSAV
         SLL   WORKL1,1
         LA    WORKL2,0(POINT1,WORKL1)
         MVI   0(WORKL2),C' '
         CR    WORKL1,CON256
         BL    EXTR
         SR    15,CON256
         TR    0(256,POINT1),CCTAB+16
         AR    15,CON256
         AR    POINT1,CON256
         SR    WORKL1,CON256
         BNP   ENDPROG
EXTR     BCTR  WORKL1,0
         SR    15,CON256
         EX    WORKL1,TRINS+256
         AR    15,CON256
ENDPROG  LM    14,12,12(13)
         BR    14
TRINS    TR      0(0,POINT1),CCTAB+16
UNPK     UNPK  0(0,POINT1),0(0,POINT2)
CCTAB    DC    C'0123456789ABCDEF'
         END
