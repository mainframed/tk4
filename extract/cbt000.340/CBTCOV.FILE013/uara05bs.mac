UR235200 TITLE 'OPTIMALE BLOCKGROESSEN - EINGABE-ROUTINE'
         MACRO
&NAME    QINIT &BASEREG=13,&CSECT=CSECT,&EQU=(,R),&SAVE=(,SAVEAREA),   *
               &RETURN=(,RETURN)
.*
.*
.*       MACRO  QINIT                   INITIATE AN ASSEMBLER-PROGRAM
.*---------------------------------------------------------------------
.*
.*       THE PURPOSE OF THIS MACRO IS TO GENERATE THE NECESSARY
.*       INSTRUCTIONS FOR A CSECT, REGISTER EQUATES, SAVE-MACRO,
.*       DEFINING AND ESTABLISHING A BASE REGISTER, DEFINING A
.*       SAVEAREA AND CHAINING IT TO THE NEXT HIGHER ONE AND FINALLY
.*       TO GENERATE THE RETURN MACRO INCLUDING RESTORING OF SAVE-
.*       AREA ADDRESSES
.*
.*       ONLY KEYWORD OPERANDS ARE USED BY QINIT
.*
.*       THE SUBLIST-OPERANDS CAN BE CODED AS FOLLOWING:
.*          1. IF BOTH SUBLIST-OPERANDS ARE NEEDED, WRITE THE OPERANDS
.*             ENCLOSED IN PARANTHESES, SEPARATED BY A COMMA
.*          2. IF ONLY THE SECOND SUBLIST-OPERAND IS NEEDED, WRITE
.*             ENCLOSED IN PARANTHESES A COMMA FOLLOWED BY THIS OPERAND
.*             THE FIRST SUBLIST-OPERAND WILL BE OMITTED
.*          3. IF ONLY THE FIRST SUBLIST-OPERAND IS NEEDED, WRITE
.*            A) ONLY THIS OPERAND WITHOUT PARANTHESES, IF THE SECOND
.*               SUBLIST-OPERAND SHOULD BE THE DEFAULT VALUE     OR
.*            B) ENCLOSED IN PARANTHESES THIS OPERAND, FOLLOWED BY A
.*               COMMA, IF THE SECOND SUBLIST-OPERAND SHOULD BE OMITTED
.*
.*       ALL OPERANDS ARE OPTIONAL
.*
.*       IF NO OPERANDS ARE SPEZIFIED, THE DEFAULT VALUES WILL BE
.*       ASSUMED
.*
.*       POSSIBLE OPERANDS:
.*
.*       BASEREG=N       WHERE N IS ANY NUMBER FROM 1 TO 15
.*                       THIS REGISTER IS ESTABLISHED AS A BASE
.*                       REGISTER
.*
.*                       REGISTER 0 IS RECOCNIZED AS AN INVALID VALUE.
.*                       FOR 1,14 AND 15 WARNINGS ARE PRINTED.
.*                       EXCEPT FOR 13 AND 15 THE VALUE IS
.*                       LOADED INTO THE REGISTER BY MEANS OF A
.*                       BALR INSTRUCTION
.*
.*                       FOR 13 THE SAVEAREA ADDRESS IS LOADED
.*                       (IF THE SAVE IS SUPPRESSED (SEE KEYWORD SAVE)
.*                       AND BASEREG=13, THE VALUE IS LOADED BY MEANS
.*                       OF A BALR INSTRUCTION)
.*
.*                       FOR 15 IT IS ASSUMED THAT IT STILL CONTAINS
.*                       THE VALUE OF * (E.G. THE EP-ADDRESS),
.*                       ONLY A USING WILL BE GENERATED
.*
.*                       THE BASE REGISTER IS ALSO EQUATED TO THE
.*                       SYMBOL 'BASEREG'
.*
.*                       IF THIS OPERAND IS OMITTED 13 IS ASSUMED
.*
.*       BASEREG=        IF THE OPERAND IS NULLIFIED NO BASEREGISTER
.*                       WILL BE DEFINED
.*
.*       CSECT=START     A START INSTRUCTION IS USED INSTEAD OF CSECT
.*
.*                       IF OMITTED, A CSECT-INSTRUCTION WILL BE
.*                       GENERATED
.*
.*       CSECT=          IF NULLIFIED NO CSECT INSTRUCTION IS GENERATED
.*
.*       EQU=(ONLY,REG)  IF 'ONLY' IS CODED, ONLY EQUATE STATEMENTS
.*                       WILL BE GENERATED. ALL OTHER OPERANDS ARE BY-
.*                       PASSED
.*
.*                       INSTEAD OF 'REG' ANY VALID NAME FOR REGISTER-
.*                       EQUATES CAN BE CODED
.*
.*                       IF THE EQU-OPERAND IS OMITTED, THE DEFAULT
.*                       VALUE WILL BE ASSUMED (EQU=(,R))
.*
.*       EQU=            IF NULLIFIED, NO EQUATES WILL BE GENERATED
.*
.*       SAVE=(ID,LABEL) INSTEAD OF 'ID' ANY VALID CHARACTERS ALLOWED
.*                       IN THE ID-FIELD OF A SAVE MACRO CAN BE CODED
.*
.*                       IF THE ID-SUBOPERAND IS OMITTED AND A CSECT-
.*                       NAME IS GENERATED, THIS NAME WILL BE SUBSTITU-
.*                       TED
.*
.*                       INSTEAD OF 'LABEL' ANY SYMBOL VALID IN A NAME-
.*                       FIELD CAN BE CODED. THIS SYMBOL WILL APPEAR IN
.*                       THE NAME FIELD OF THE SAVEAREA
.*
.*                       IF 'LABEL' IS OMITTED, NO NAME WILL BE
.*                       GENERATED
.*
.*                       IF INSTEAD OF 'LABEL'  'NO' IS CODED NO SAVE-
.*                       AREA IS GENERATED AND SAVE-AREA-CHAINING IS
.*                       SUPPRESSED
.*
.*       SAVE=           IF NULLIFIED, THE GENERATION OF A SAVE-ROUTINE
.*                       WILL BE SUPPRESSED
.*
.*       RETURN=(RC,LABEL) INSTEAD OF 'RC' ANY VALID OPERAND FOR THE
.*                       RC-PARAMETER OF THE RETURN MACRO INSTRUCTION
.*                       CAN BE CODED. IF 'RC' IS OMITTED, ZERO WILL
.*                       BE ASSUMED
.*
.*                       INSTEAD OF 'LABEL' ANY SYMBOL VALID IN A NAME-
.*                       FIELD CAN BE CODED. THIS SYMBOL WILL APPEAR IN
.*                       THE NAME FIELD OF THE RETURN INSTRUCTION. IF
.*                       'LABEL' IS OMITTED THE NAME 'RETURN' APPEARS
.*                       IN THE NAME FIELD
.*
.*                       IF INSTEAD OF 'RC'  (15) IS CODED REGISTER
.*                       15 IS SUPPOSED TO CONTAIN A RETURN-CODE AND
.*                       WILL NOT BE RESTORED UPON RETURN
.*
.*                       IF THE RETURN-OPERAND IS OMITTED, RC = 0
.*                       WILL BE GENERATED AND THE NAME 'RETURN'
.*                       IS ASSUMED
.*
.*       RETURN=         IF NULLIFIED, NO RETURN STATEMENTS WILL BE
.*                       GENERATED
.*
.*
.*--------------------------------------------------------------------*
.*                                                                    *
.*                                                                    *
         GBLC  &DFTR                    = '' OR 'R' DEPENDINGON EQUATE*
.*                                      MAY BE USED INSIDE ALL MACROS *
.*                                      CONCATENATED WITH REGISTER
.*                                      OPERANDS SO THAT REGISTER     *
.*                                      TRACING IS PROVIDED ALSO INSIDE
.*                                      MACROS.                       *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         LCLC  &SANAME                  SAVEAREA NAME
         LCLC  &RETURNC                 RETURN-ROUTINE NAME
         LCLC  &BASE                    = 'BASEREG' OR VALUE OF BASE
         LCLC  &R                       = '' OR 'R' DEPENDING ON EQUATE
         LCLC  &BRANCH                  LABEL FOR BRANCH AROUND RETURN
         LCLA  &SAR                     SAVE REGISTER FOR R13
         LCLA  &VAL                     VALUE OF BASE
         LCLB  &BASEB                   BASEREG OPERAND PRESENT
         LCLB  &BASEBSP                 BASEREG IS 13 OR 15
         LCLB  &CSECTB                  CSECT OPERAND PRESENT
         LCLB  &EQUB                    EQU OPERAND PRESENT
         LCLB  &IDB                     ID OPERAND PRESENT
         LCLB  &RETURNB                 RETURN OPERAND PRESENT
         LCLB  &RCB                     RC OPERAND PRESENT
         LCLB  &SAVEB                   SAVE OPERAND PRESENT
         LCLB  &SAVEARB                 SAVEAR OPERAND PRESENT
         LCLB  &NS                      NO SAVE IF ON
.*
.*       VALIDATE OPERANDS AND SET INDICATORS
.*
         AIF   ('&SAVE(2)' NE 'NO').AREA1
         AIF   ('&BASEREG(1)' EQ '13').AREA0
         AGO   .AREA1
.AREA0   ANOP
&BASE    SETC  '15'
&VAL     SETA  15
&NS      SETB  1
.AREA1   ANOP
         AIF   ('&BASEREG' EQ '').VAL10
         AIF   (N'&BASEREG NE 1).VAL5
         AIF   (T'&BASEREG(1) NE 'N').VAL5
         AIF   (&NS).VAL4
&VAL     SETA  &BASEREG(1)
.VAL4    ANOP
         AIF   (&VAL LT 1 OR &VAL GT 15).VAL5
&BASEB   SETB  1
         AIF   (&VAL NE 13 AND &VAL NE 15).VAL10
&BASEBSP SETB  1
         AGO   .VAL10
.VAL5    ANOP
         MNOTE 8,'INVALID BASEREG OPERAND &BASEREG'
.VAL10   ANOP
         AIF   ('&CSECT' EQ '').VAL20
         AIF   (N'&CSECT NE 1).VAL15
         AIF   ('&CSECT' NE 'START' AND '&CSECT' NE 'CSECT').VAL15
&CSECTB  SETB  1                        INDICATE CSECT
         AGO   .VAL20
.VAL15   ANOP
         MNOTE 8,'INVALID CSECT OPERAND &CSECT'
.VAL20   ANOP
         AIF   ('&EQU' EQ '').VAL30
         AIF   (N'&EQU GT 2).VAL25
         AIF   ('&EQU(1)' NE '' AND '&EQU(1)' NE 'ONLY').VAL25
         AIF   (N'&EQU EQ 1 AND '&EQU' EQ 'ONLY').VAL22
         AIF   (N'&EQU EQ 2 AND '&EQU(2)' EQ '').VAL22
&R       SETC  '&EQU(2)'
         AGO   .VAL23
.VAL22   ANOP
&R       SETC  'R'
.VAL23   ANOP
&EQUB    SETB  1                        INDICATE EQUATES
         AIF   ('&EQU(1)' EQ 'ONLY').CSECT15
         AGO   .VAL30
.VAL25   ANOP
         MNOTE 8,'INVALID EQU OPERAND &EQU'
.VAL30   ANOP
         AIF   ('&SAVE' EQ '').VAL80
         AIF   (N'&SAVE GT 2).VAL70
&SAVEB   SETB  1
         AIF   (N'&SAVE NE 2).VAL40
         AIF   (T'&SAVE(2) EQ 'O').VAL60
&SANAME  SETC  '&SAVE(2)'
         AGO   .VAL50
.VAL40   ANOP
&SANAME  SETC  'SAVEAREA'
.VAL50   ANOP
&SAVEARB SETB  1
.VAL60   ANOP
         AIF   (T'&SAVE(1) EQ 'O').VAL80
&IDB     SETB  1
         AGO   .VAL80
.VAL70   ANOP
         MNOTE 8,'INVALID SAVE OPERAND &SAVE'
.VAL80   ANOP
         AIF   ('&RETURN' EQ '').VAL120
         AIF   (N'&RETURN GT 2).VAL110
&RETURNB SETB  1
         AIF   (N'&RETURN NE 2).VAL90
         AIF   (T'&RETURN(2) EQ 'O').VAL90
&RETURNC SETC  '&RETURN(2)'
         AGO   .VAL100
.VAL90   ANOP
&RETURNC SETC  'RETURN'
.VAL100  ANOP
         AIF   (T'&RETURN(1) EQ 'O').VAL120
&RCB     SETB  1
         AGO   .VAL120
.VAL110  ANOP
         MNOTE 8,'INVALID RETURN OPERAND &RETURN'
.VAL120  ANOP
         AIF   (&BASEB+&CSECTB+&EQUB+&RETURNB+&SAVEB NE 0).VAL130
         MNOTE 12,'NO VALID OPERANDS SPECIFIED'
         MEXIT
.VAL130  ANOP
&BASE    SETC  '&VAL'
.*
.* GENERATE CSECT INSTRUCTION
.*
         SPACE 5
         AIF   (NOT &CSECTB).CSECT10   NO CSECT OR START
         AIF   ('&CSECT' EQ 'START').CSECT5
&NAME    CSECT
         AGO   .CSECT15
.CSECT5  ANOP
&NAME    START
         AGO   .CSECT15
.CSECT10 AIF   ('&NAME' EQ '').CSECT90
&NAME    DS    0H
.CSECT15 ANOP
         SPACE 5
.CSECT90 ANOP
.*
.* GENERATE EQUATES
.*
         AIF   (NOT &EQUB).EQU90
&DFTR    SETC  '&EQU(2)'                EQU-PREFIX
*---------------------------------------------------------------------*
*                                                                     *
*        E Q U A T E S                                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
&R.0     EQU   0
&R.1     EQU   1
&R.2     EQU   2
&R.3     EQU   3
&R.4     EQU   4
&R.5     EQU   5
&R.6     EQU   6
&R.7     EQU   7
&R.8     EQU   8
&R.9     EQU   9
&R.10    EQU   10
&R.11    EQU   11
&R.12    EQU   12
&R.13    EQU   13
&R.14    EQU   14
&R.15    EQU   15
         AIF   ('&EQU(1)' NE 'ONLY').EQU10
         EJECT
         MEXIT
.EQU10   AIF   (NOT &BASEB).EQU80
         SPACE 3
BASEREG  EQU   &R&BASE
&BASE    SETC  'BASEREG'
         AIF   (&VAL NE 1 AND &VAL LT 14).EQU80
***                      CAUTION:       BASE REGISTER SPECIFICATION
***                                     IS NOT CONFORM TO OS-/360
***                                     LINKAGE CONVENTIONS
.EQU80   ANOP
         EJECT
.EQU90   ANOP
.*
.* ESTABLISH BASE AND GENERATE SAVE ROUTINE
.*
         AIF   (NOT (&BASEB OR &SAVEB)).SAVE90
*---------------------------------------------------------------------*
*                                                                     *
         AIF (NOT &BASEB).SAVE05
*        E S T A B L I S H   B A S E                                  *
.SAVE05  AIF   (NOT &SAVEB).SAVE10
         AIF   ('&SAVE(2)' EQ 'NO').NOTXT
*        S A V E   A N D                                              *
*        C H A I N   S A V E   A R E A S                              *
.SAVE10  ANOP
.NOTXT   ANOP
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         AIF   (&SAVEB).SAVE20          SAVE SPECIFIED
         AIF   (&VAL EQ 15).SAVE15
         BALR  &BASE,0                  SET BASE TO VALUE OF *+2
         SPACE 3
.SAVE15  USING *,&BASE                  DEFINE BASE REGISTER
         AGO   .SAVE85
.SAVE20  ANOP
         AIF   (&VAL NE 15).SAVE25
         USING *,&BASE                  INDICATE BASE
         SPACE 3
         AGO   .SAVE30
.SAVE25  ANOP
         AIF   (&VAL NE 13).SAVE30
         USING *,&R.15                  INTERMEDIATE BASE
         SPACE 3
.SAVE30  ANOP
         AIF   (NOT &IDB).SAVE35        NO ID SPECIFIED
         SAVE  (14,12),,&SAVE(1)
         AGO   .SAVE37
.SAVE35  ANOP
         SAVE  (14,12),,&NAME
.SAVE37  ANOP
         AIF   ('&SAVE(2)' NE 'NO').SAVE39
         AIF   (&VAL EQ 15).SAVE85
         BALR  &BASE,0                      BASE *
         USING *,&BASE     .
         AGO   .SAVE85
.SAVE39  ANOP
         AIF   (&BASEBSP OR NOT &BASEB).SAVE40
         SPACE 3
         BALR  &BASE,0                  SET BASE TO VALUE OF *+2
         USING *,&BASE                  INDICATE BASE
         SPACE 3
.SAVE40  ANOP
&SAR     SETA  2
         AIF   (&VAL NE 2).SAVE45       PREVENT DESTROYMENT OF BASE
&SAR     SETA  3
.SAVE45  ANOP
         LR    &R.&SAR,&R.13            SAVE HIGHER SA ADDRESS
         CNOP  0,4
         BAL   &R.13,*+76               BRANCH AROUND SAVEAREA
         SPACE 3
         AIF   (&VAL NE 13).SAVE50
         DROP  &R.15                    DROP INTERMEDIATE BASE
         USING *,&BASE                  CONTAINS SA-ADDR. AND IS BASE
         SPACE 3
.SAVE50  ANOP
&SANAME  DC    18F'0'                   SAVE AREA
         SPACE 3
         ST    &R.13,8(,&R&SAR)         LOGICAL LOWER ADDR IN HIGHER
         AIF   ('&SANAME' NE '').SAVE55
         ST    &R&SAR,4(,&R.13)         AND HIGHER ADDR IN LOWER PGM
         AGO   .SAVE85
.SAVE55  ANOP
         ST    &R&SAR,&SANAME+4         AND HIGHER ADDR IN LOWER PGM
.SAVE85  ANOP
         AIF   (NOT &RETURNB).SAVE90
         SPACE 3
.SAVE90  ANOP
.*
.* GENERATE RETURN ROUTINE
.*
         AIF   (NOT &RETURNB).RETUR90
         AIF   (NOT (&BASEB OR &SAVEB)).RETUR10
&BRANCH  SETC  '#X'.'&SYSNDX'
         B     &BRANCH                  BRANCH AROUND RETURN ROUTINE
         SPACE 5
.RETUR10 ANOP
*---------------------------------------------------------------------*
*                                                                     *
*        R E T U R N   R O U T I N E                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         AIF   ('&SAVE(2)' EQ 'NO').RETUR17
         AIF   (NOT &SAVEB).RETUR17     IF NO SAVE DO NOT RESTORE R13
         AIF   (&SAVEARB).RETUR15
&RETURNC L     &R.13,4(,&R.13)          RESTORE HIGHER SA ADDRESS
         AGO   .RETUR20
.RETUR15 ANOP
&RETURNC L     &R.13,&SANAME+4          RESTORE HIGHER SA ADDRESS
         AGO   .RETUR20
.RETUR17 ANOP
&RETURNC DS    0H
.RETUR20 ANOP
         AIF   (N'&RETURN GT 1).RETUR21
         AIF   ('&RETURN' EQ '(15)').RETUR22
         AGO   .RETUR2A
.RETUR21 ANOP
         AIF   ('&RETURN(1)' EQ '(15)').RETUR22
.RETUR2A ANOP
         AIF   (NOT &RCB).RETUR25
         RETURN (14,12),T,RC=&RETURN(1)
         AGO   .RETUR30
.RETUR22 ANOP
         RETURN (14,12),T,RC=(15)
         AGO   .RETUR30
.RETUR25 ANOP
         RETURN (14,12),T,RC=0
.RETUR30 ANOP
         SPACE 5
         AIF   (NOT (&SAVEB OR &BASEB)).EX
&BRANCH  DS    0H
         AGO   .EX
.RETUR90 AIF   (NOT (&BASEB OR &SAVEB)).EX
         SPACE 5
.EX      ANOP
         MEND
         EJECT
UR235200 QINIT RETURN=(15)
         SPACE 8
***********************************************************************
*                                                                     *
*              USING REGISTER FUER DUMMY-SECTIONS                     *
*                                                                     *
***********************************************************************
         SPACE 3
         USING IHADCB,R12
         USING PARMLIST,R11
         USING TCODES,R8
         EJECT
***********************************************************************
*                                                                     *
*        S05   STEUERLEISTE                                           *
*                                                                     *
***********************************************************************
         SPACE 3
         BAL   R14,A05                 VORROUTINE
         SPACE 2
S05C     EQU   *
         BAL   R14,B05                 EINGABEDATEI LESEN U. AUSGEBEN
         LTR   R15,R15                 END OF FILE?
         BNZ   S05X                    JA, SCHLUSS-ROUTINE
         SPACE 2
         BAL   R14,D05                 EINGABEKARTE PRUEFEN, PARMLIST
         SPACE 1
         CH    R15,=H'4'               HELP ANGEFORDERT?
         BE    S05F
         SPACE 1
         LTR   R15,R15                 KARTE IN ORDNUNG?
         BNZ   S05C                    NAECHSTE KARTE LESEN
         SPACE 2
         LA    R15,0                   RETURNCODE 0
         B     RETURN
         SPACE 2
S05F     EQU   *
         BAL   R14,F05                 HELP-TEXT AUSGEBEN
         SPACE 1
         B     S05C                    NAECHSTE KARTE LESEN
         SPACE 2
S05X     EQU   *
         BAL   R14,Z05                 SCHLUSSROUTINE
         SPACE 1
         LA    R15,4                   RETURNCODE: EOF
         B     RETURN
         EJECT
***********************************************************************
*                                                                     *
*        A05   VORROUTINE                                             *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
A05      EQU   *
         ST    R14,A05-4
         SPACE 2
         BAL   R14,A0505               PARAMETER SICHERN
         SPACE 2
         CLI   SFIRST,X'00'            ERSTER DURCHLAUF?
         BNE   A05Z                    NEIN, OPEN AUSLASSEN
         SPACE 1
         BAL   R14,A0510               SYSIN EROEFFNEN
         SPACE 3
A05Z     EQU   *
         L     R14,A05-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        A0505 PARAMETER SICHERN                                      *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
A0505    EQU   *
         ST    R14,A0505-4
         SPACE 2
         L     R11,0(R1)               PARMLIST-ADRESSE LADEN
         L     R10,4(R1)               ADRESSE CODE-TABELLE LADEN
         L     R9,8(R1)                ADRESSE DER ADDR.KONST. SYSPR.
         L     R9,0(R9)                ADRESSE SYSPRT-DCB IN R9
         SPACE 3
A0505Z   EQU   *
         L     R14,A0505-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        A0510 EROEFFNEN SYSIN                                        *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
A0510    EQU   *
         ST    R14,A0510-4
         SPACE 2
         MVI   SFIRST,X'01'            SCHALTER SETZEN
         SPACE 1
         OPEN  (SYSIN)                 EROEFFNEN
         SPACE 1
         LA    R12,SYSIN               DCB-ADRESSE IN REG 12
         TM    DCBOFLGS,X'10'          ERFOLGREICH EROEFFNET?
         BO    A0510Z                  JA, RETURN
         SPACE 2
         LA    R0,KFEHL1               FEHLERTEXT 1
         BAL   R14,U05                 FEHLERTEXT AUSGEBEN
         SPACE 1
         ABEND 2000
         SPACE 3
A0510Z   EQU   *
         L     R14,A0510-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        A051005 OPEN-EXIT-ROUTINE SYSIN                              *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
A051005  EQU   *
         ST    R14,A051005-4
         SPACE 2
         LR    R12,R1                  DCB-ADRESSE IN REG 12
         LH    R3,DCBBLKSI             BLOCKSIZE IN REG 3
         LTR   R3,R3                   GLEICH NULL ?
         BNZ   A051005C                NEIN, VERZWEIGEN
         SPACE 1
         MVC   DCBBLKSI,DCBLRECL       BLOCKSIZE=LRECL
         SPACE 2
A051005C EQU   *
         CLI   SEXIT1,X'00'            ERSTER DURCHLAUF?
         BNE   A051005F                NEIN, VERZWEIGEN
         SPACE 1
         MVI   SEXIT1,X'01'            SCHALTER SETZEN
         OI    DCBOFLGS,X'08'          UNLIKE-ATTRIBUTES-BIT AN
         B     A051005Z                RETURN TO OPEN
         SPACE 1
A051005F EQU   *
         MVI   SREREAD,X'01'           REREAD-SCHALTER SETZEN
         SPACE 3
A051005Z EQU   *
         L     R14,A051005-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        B05   EINGABEKARTE LESEN UND AUSGEBEN                        *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
B05      EQU   *
         ST    R14,B05-4
         SPACE 2
B05C     EQU   *
         GET   SYSIN,ZKARTE            EINGABEKARTE LESEN
         SPACE 1
         CLI   SREREAD,X'01'           REREAD-SCHALTER EIN?
         BNE   B05F                    NEIN,AUSGEBEN
         SPACE 1
         MVI   SREREAD,X'00'           SCHALTER AUS
         B     B05C                    NOCHMAL LESEN
         SPACE 2
B05F     EQU   *
         OC    ZKARTE,=CL80' '         UMSETZEN AUF GROSSBUCHSTABEN
         SPACE 1
         CLC   =C'END ',ZKARTE         END OF FILE?
         BNE   B05I
         SPACE 1
         LA    R15,4                   END OF FILE
         B     B05Z
         SPACE 2
B05I     EQU   *
         LA    R0,ZEILE                ADRESSE ZEILE IN R0
         BAL   R14,U05                 EINGABEKARTE AUSGEBEN
         SPACE 1
         LA    R15,0                   RETURNCODE 0
         SPACE 3
B05Z     EQU   *
         L     R14,B05-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        D05   EINGABEKARTE PRUEFEN                                   *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D05      EQU   *
         ST    R14,D05-4
         SPACE 2
         BAL   R14,D0505               TEST AUF HELP-FUNKTION
         LTR   R15,R15                 HELP ANGEFORDERT?
         BNZ   D05Z                    JA, RETURN MIT RC 4
         SPACE 2
         BAL   R14,D0510               TEST AUF GUELTIGEN CODE
         LTR   R15,R15                 IN ORDNUNG?
         BNZ   D05Z                    NEIN, RETURN MIT RC 8
         SPACE 2
         BAL   R14,D0515               STELLE 5 PRUEFEN
         LTR   R15,R15                 IN ORDNUNG?
         BNZ   D05Z                    NEIN, RETURN MIT RC 8
         SPACE 2
         BAL   R14,D0520               SATZLAENGE PRUEFEN
         LTR   R15,R15                 IN ORDNUNG ?
         BNZ   D05Z                    NEIN, RETURN MIT RC 8
         SPACE 2
         BAL   R14,D0525               SCHLUESSEL-LAENGE PRUEFEN
         LTR   R15,R15                 IN ORDNUNG ?
         BNZ   D05Z                    NEIN, RETURN MIT RC 8
         SPACE 2
         BAL   R14,D0530               SATZ-ANZAHL PRUEFEN
         SPACE 3
D05Z     EQU   *
         L     R14,D05-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        D0505 TEST AUF HELP-FUNKTION                                 *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D0505    EQU   *
         ST    R14,D0505-4
         SPACE 2
         CLC   =C'HELP ',ZKARTE         HELP COMMAND?
         BE    D0505Y
         SPACE 2
         CLC   =C'H ',ZKARTE            HELP COMMAND?
         BE    D0505Y
         SPACE 2
         LA    R15,0                    RETURN 0 = KEIN HELP
         B     D0505Z
         SPACE 2
D0505Y   EQU   *
         LA    R15,4                    RETURN 4 = HELP
         SPACE 3
D0505Z   EQU   *
         L     R14,D0505-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        D0510 TEST AUF GUELTIGEN FUNKTIONSCODE                       *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D0510    EQU   *
         ST    R14,D0510-4
         SPACE 2
         LR    R8,R10                  ADRESSE CODE-TABELLE IN R8
         SPACE 1
D0510B   EQU   *
         CLC   TCDE,ZKARTE             CODE-VERGLEICH
         BE    D0510X                  GLEICH, OK
         SPACE 1
         CLC   TCDE,=X'FFFFFFFF'       ENDE DER TABELLE?
         BE    D0510D                  JA, FEHLER
         SPACE 1
         LA    R8,L'TCODES(R8)         TABELLEN-ADRESSE ERHOEHEN
         B     D0510B                  WEITERSUCHEN
         SPACE 2
D0510D   EQU   *
         LA    R0,KFEHL2               UNGUELTIGER FUNKTIONSCODE
         BAL   R14,U05                 FEHLERCODE AUSGEBEN
         SPACE 1
         LA    R15,8                   FEHLER
         B     D0510Z
         SPACE 2
D0510X   EQU   *
         MVC   ZCODE,ZKARTE            CODE UEBERTRAGEN
         LA    R15,0                   OK, RETURN
         SPACE 3
D0510Z   EQU   *
         L     R14,D0510-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        D0515 PRUEFEN STELLE 5                                       *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D0515    EQU   *
         ST    R14,D0515-4
         SPACE 2
         CLI   ZKARTE+4,C','           KOMMA AUF STELLE 5?
         BE    D0515Y                  JA, RETURN
         SPACE 1
         CLI   ZKARTE+4,C' '           BLANK AUF STELLE 5?
         BE    D0515C
         SPACE 1
         LA    R0,KFEHL3               KEIN KOMMA AUF STELLE 5
         BAL   R14,U05                 FEHLERTEXT AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0515Z
         SPACE 1
D0515C   EQU   *
         LA    R0,KFEHL4               KEINE SATZLAENGE
         BAL   R14,U05                 FEHLERTEXT AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0515Z
         SPACE 2
D0515Y   EQU   *
         LA    R15,0                   RETURNCODE 0
         SPACE 3
D0515Z   EQU   *
         L     R14,D0515-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        D0520 SATZLAENGE PRUEFEN                                     *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D0520    EQU   *
         ST    R14,D0520-4
         SPACE 2
         LA    R3,ZKARTE+5             ANFANGSADRESSE IN REG 3
         ST    R3,ASANF                STOREN NACH ASANF
         XC    ASKOBL,ASKOBL           ENDADRESSE LOESCHEN
         LA    R1,ASANF                PARMADRESSE IN REG 1
         SPACE 1
         BAL   R14,U10                 SUCHEN ERSTES BLANK ODER KOMMA
         SPACE 1
         L     R3,ASANF                ANFANGSADRESSE INS REG3
         L     R4,ASKOBL               ENDADRESSE INS REG4
         LA    R4,0(R4)                HIGH-ORDER-BIT LOESCHEN
         SR    R4,R3                   LAENGE IN REG 4
         LTR   R4,R4                   LAENGE = 0 ?
         BNZ   D0520C                  NEIN, WEITER
         SPACE 1
         LA    R0,KFEHL4               SATZLAENGE FEHLT
         BAL   R14,U05                 FEHLERTEXT AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0520Z
D0520C   EQU   *
         CH    R4,=H'5'                PRUEFEN LAENGE
         BNH   D0520F                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL5               SATZLAENGE UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0520Z
         SPACE 2
D0520F   EQU   *
         BCTR  R4,R0                   LAENGE-1
         EX    R4,KTRT                 TEST AUF NUMERISCH
         BZ    D0520I                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL5               SATZLAENGE UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0520Z
         EJECT
D0520I   EQU   *
         EX    R4,KPACK                WERT PACKEN
         CVB   R5,ZDOWO                UND NACH BINAER KONVERTIEREN
         SPACE 1
         C     R5,=F'32760'            GROESSE DER SATZLAENGE
         BNH   D0520L                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL5               SATZLAENGE UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0520Z
         SPACE 2
D0520L   EQU   *
         LTR   R5,R5                   SATZLAENGE NULL?
         BNZ   D0520O                  NEIN, WEITER
         SPACE 1
         LA    R0,KFEHL5               SATZLAENGE UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0520Z
         SPACE 2
D0520O   EQU   *
         STH   R5,ZLRECL               SATZLAENGE SPEICHERN
         LA    R15,0                   RETURNCODE 0
         SPACE 3
D0520Z   EQU   *
         L     R14,D0520-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        D0525 PRUEFEN SCHLUESSEL-LAENGE                              *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D0525    EQU   *
         ST    R14,D0525-4
         SPACE 2
         TM    ASKOBL,X'80'            ENDE DER LISTE?
         BZ    D0525A                  NEIN,WEITER
         SPACE 1
         XC    ZKEYL,ZKEYL             SCHLUESSEL-LAENGE 0
         LA    R15,0
         B     D0525Z
         SPACE 2
D0525A   EQU   *
         L     R3,ASKOBL               VORHERIGE END-ADRESSE LADEN
         LA    R3,1(R3)                UM 1 ERHOEHEN
         ST    R3,ASANF                STOREN NACH ASANF
         XC    ASKOBL,ASKOBL           ENDADRESSE LOESCHEN
         LA    R1,ASANF                PARMADRESSE IN REG 1
         SPACE 1
         BAL   R14,U10                 SUCHEN ERSTES BLANK ODER KOMMA
         SPACE 1
         L     R3,ASANF                ANFANGSADRESSE INS REG3
         L     R4,ASKOBL               ENDADRESSE INS REG4
         LA    R4,0(R4)                HIGH-ORDER-BIT LOESCHEN
         SR    R4,R3                   LAENGE IN REG 4
         LTR   R4,R4                   LAENGE = 0 ?
         BNZ   D0525C                  NEIN, WEITER
         SPACE 1
         XC    ZKEYL,ZKEYL             SCHLUESSEL-LAENGE 0
         LA    R15,0
         B     D0525Z
         SPACE 2
D0525C   EQU   *
         CH    R4,=H'3'                PRUEFEN LAENGE
         BNH   D0525F                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL7               SCHLUESSEL-LAENGE UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0525Z
         EJECT
D0525F   EQU   *
         BCTR  R4,R0                   LAENGE-1
         EX    R4,KTRT                 TEST AUF NUMERISCH
         BZ    D0525I                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL7               SCHLUESSEL-LAENGE UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0525Z
         SPACE 2
D0525I   EQU   *
         EX    R4,KPACK                WERT PACKEN
         CVB   R5,ZDOWO                UND NACH BINAER KONVERTIEREN
         SPACE 1
         CH    R5,=H'255'              SCHLUESSEL-LAENGE
         BNH   D0525L                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL7               SCHLUESSEL-LAENGE UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0525Z
         SPACE 2
D0525L   EQU   *
         STH   R5,ZKEYL                SCHLUESSEL-LAENGE SPEICHERN
         LA    R15,0                   RETURNCODE 0
         SPACE 3
D0525Z   EQU   *
         L     R14,D0525-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        D0530 PRUEFEN SATZANZAHL                                     *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
D0530    EQU   *
         ST    R14,D0530-4
         SPACE 2
         TM    ASKOBL,X'80'            ENDE DER LISTE?
         BZ    D0530A                  NEIN,WEITER
         SPACE 1
         XC    ZSATZ,ZSATZ             SATZ-ANZAHL 0
         LA    R15,0
         B     D0530Z
         SPACE 2
D0530A   EQU   *
         L     R3,ASKOBL               VORHERIGE END-ADRESSE LADEN
         LA    R3,1(R3)                UM 1 ERHOEHEN
         ST    R3,ASANF                STOREN NACH ASANF
         XC    ASKOBL,ASKOBL           ENDADRESSE LOESCHEN
         LA    R1,ASANF                PARMADRESSE IN REG 1
         SPACE 1
         BAL   R14,U10                 SUCHEN ERSTES BLANK ODER KOMMA
         SPACE 1
         L     R3,ASANF                ANFANGSADRESSE INS REG3
         L     R4,ASKOBL               ENDADRESSE INS REG4
         LA    R4,0(R4)                HIGH-ORDER-BIT LOESCHEN
         SR    R4,R3                   LAENGE IN REG 4
         LTR   R4,R4                   LAENGE = 0 ?
         BNZ   D0530C                  NEIN, WEITER
         SPACE 1
         XC    ZSATZ,ZSATZ             SATZ-ANZAHL 0
         LA    R15,0
         B     D0530Z
         SPACE 2
D0530C   EQU   *
         CH    R4,=H'10'               PRUEFEN LAENGE
         BNH   D0530F                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL8               SATZ-ANZAHL UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0530Z
         EJECT
D0530F   EQU   *
         BCTR  R4,R0                   LAENGE-1
         EX    R4,KTRT                 TEST AUF NUMERISCH
         BZ    D0530I                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL8               SATZANZAHL UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0530Z
         SPACE 2
D0530I   EQU   *
         EX    R4,KPACK                WERT PACKEN
         CP    ZDOWO,=PL8'2147483647'  SATZANZAHL
         BNH   D0530L                  OK, WEITER
         SPACE 1
         LA    R0,KFEHL8               SATZANZAHL UNGUELTIG
         BAL   R14,U05                 MELDUNG AUSGEBEN
         LA    R15,8                   FEHLER
         B     D0530Z
         SPACE 2
D0530L   EQU   *
         CVB   R5,ZDOWO                UND NACH BINAER KONVERTIEREN
         SPACE 1
         ST    R5,ZSATZ                SATZ-ANZAHL SPEICHERN
         LA    R15,0                   RETURNCODE 0
         SPACE 3
D0530Z   EQU   *
         L     R14,D0530-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        F05   AKTIVIEREN HELP-ROUTINE                                *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
F05      EQU   *
         ST    R14,F05-4
         SPACE 2
         L     R15,AHELPRT              ADRESSE HELP-ROUTINE LADEN
         LTR   R15,R15                  ROUTINE SCHON GELADEN?
         BNZ   F05F                     JA, CALL
         SPACE 2
         LOAD  EPLOC=KNHELP             HELPROUTINE LADEN
         SPACE 1
         ST    R0,AHELPRT               ADRESSE SPEICHERN
         LR    R15,R0                   UND UMLADEN IN R15
         SPACE 2
F05F     EQU   *
         CALL  (15),((R9)),VL           ROUTINE AUFRUFEN
         SPACE 3
F05Z     EQU   *
         L     R14,F05-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        Z05   SCHLUSSROUTINE                                         *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
Z05      EQU   *
         ST    R14,Z05-4
         SPACE 2
         LA    R0,KEOF                 EOF-MELDUNG
         BAL   R14,U05                 EOF-MELDUNG SCHREIBEN
         SPACE 2
         CLOSE (SYSIN)                 DATEI SCHLIESSEN
         SPACE 3
Z05Z     EQU   *
         L     R14,Z05-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        U05   SCHREIBEN AUF SYSPRINT                                 *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
U05      EQU   *
         ST    R14,U05-4
         SPACE 2
         PUT   (9),(0)                 AUSGEBEN AUF SYSPRINT
         SPACE 3
U05Z     EQU   *
         L     R14,U05-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*        U10   SUCHEN ERSTES KOMMA ODER BLANK                         *
*                                                                     *
***********************************************************************
         SPACE 3
         DC    F'0'
U10      EQU   *
         ST    R14,U10-4
         SPACE 2
         L     R3,0(R1)                ANFANGSADRESSE IN R3
         SPACE 1
U10C     EQU   *
         CLI   0(R3),C','              KOMMA GEFUNDEN?
         BE    U10O                    JA, OK
         SPACE 1
         CLI   0(R3),C' '              BLANK GEFUNDEN?
         BE    U10L
         SPACE 1
         LA    R3,1(R3)                ADRESSE ERHOEHEN
         C     R3,=A(ZKARTE+80)        ENDE DER KARTE?
         BNH   U10C                    WEITERSUCHEN
         SPACE 2
U10L     EQU   *
         ST    R3,4(R1)                ENDADRESSE SPEICHERN
         OI    4(R1),X'80'             ENDE DER EINGABE
         B     U10Z
         SPACE 1
U10O     EQU   *
         ST    R3,4(R1)                ENDADRESSE SPEICHERN
         SPACE 3
U10Z     EQU   *
         L     R14,U10-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              DEFINITIONEN                                           *
*                                                                     *
***********************************************************************
         SPACE 3
SFIRST   DC    X'00'                   SCHALTER ERSTER DURCHLAUF
SEXIT1   DC    X'00'                   SCHALTER FUER OPEN-EXIT
SREREAD  DC    X'00'                   REREAD-SCHALTER UNLIKE ATTR.
         SPACE 2
KEOF     DC    CL80'0UR235200 END OF FILE  BEI SYSIN, PROGRAMM-ENDE'
KFEHL1   DC    CL80'0UR235200 OPEN AUF SYSIN NICHT ERFOLGREICH, ABEND'
KFEHL2   DC    CL80' UR235200 UNGUELTIGER FUNKTIONSCODE, KARTE UEBERLES*
               EN'
KFEHL3   DC    CL80' UR235200 KEIN KOMMA AUF STELLE 5, KARTE UEBERLESEN*
               '
KFEHL4   DC    CL80' UR235200 SATZLAENGE FEHLT, KARTE UEBERLESEN'
KFEHL5   DC    CL80' UR235200 UNGUELTIGE SATZLAENGE, KARTE UEBERLESEN'
KFEHL7   DC    CL80' UR235200 UNGUELTIGE SCHLUESSEL-LAENGE, KARTE UEBER*
               LESEN'
KFEHL8   DC    CL80' UR235200 UNGUELTIGE SATZANZAHL, KARTE UEBERLESEN'
         SPACE 2
KTRT     TRT   0(0,R3),TRTTAB          TEST AUF NUMERISCH
KPACK    PACK  ZDOWO,0(0,R3)           PACKEN
         SPACE 2
ZEILE    DS    0CL80                   AUSGABEZEILE
         DC    C'0'                    STEUERZEICHEN
ZTEXT    DS    0CL79                   AUSGABETEXT
ZKARTE   DC    CL80' '                 EINGABEKARTE
         SPACE 2
ZDOWO    DC    D'0'                    DOPPELWORT FUER PACK
         SPACE 2
ASANF    DC    A(0)                    ZWISCHENSPEICHER SUCHADR.
ASKOBL   DC    A(0)                    ZWISCHENSPEICHER SUCH-ENDE
         SPACE 2
AHELPRT  DC    A(0)                    ADRESSE HELP-ROUTINE
KNHELP   DC    CL8'UR235900'           NAME DER HELP-ROUTINE
         SPACE 2
TRTTAB   DC    240X'FF',10X'00',6X'FF' TABELLE FUER TRT AUF NUMERISCH
         SPACE 2
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        DCB   SYSIN                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
         PRINT NOGEN
         SPACE 2
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GM),DSORG=PS,EODAD=S05X,            *
               EXLST=AEXIT1,LRECL=80,RECFM=FB
         SPACE 2
AEXIT1   DS    0F
         DC    X'85'
         DC    AL3(A051005)
         EJECT
***********************************************************************
*                                                                     *
*              DUMMY-SECTION FUER DCB                                 *
*                                                                     *
***********************************************************************
         SPACE 3
         DCBD  DSORG=PS
         EJECT
***********************************************************************
*                                                                     *
*              DUMMY-SECTION FUER PARMLIST                            *
*                                                                     *
***********************************************************************
         SPACE 3
PARMLIST DSECT
ZCODE    DS    F                       FUNKTIONS-CODE
ZLRECL   DS    H                       SATZLAENGE
ZKEYL    DS    H                       KEY-LAENGE
ZSATZ    DS    F                       ANZAHL SAETZE
         SPACE 8
***********************************************************************
*                                                                     *
*              DUMMY SECTION FUER CODE-TABELLE                        *
*                                                                     *
***********************************************************************
         SPACE 3
         DSECT
TCODES   DS    0D
TCDE     DS    F
TADDR    DS    F
         END
