*          DATA SET HD122650T  AT LEVEL 002 AS OF 05/04/76
         PRINT OFF
         MACRO
&LABEL   PROG  &BYTES
         GBLA  &SCHALT
         LCLA  &REGANZ,&REGEND
         LCLC  &NAME
         PRINT NOGEN
*                                                                     *
*        BASISADRESSIERUNG - OS                                       *
*                                                                     *
         AIF   (N'&LABEL EQ 1).P01
         MNOTE 4,'CSECT-NAME FEHLT, ''CSECT1'' WIRD ANGENOMMEN'
&NAME    SETC  'CESECT1'
CSECT1   CSECT
         AGO   .P02
.P01     ANOP
&NAME    SETC  '&LABEL'
&NAME    CSECT
.P02     AIF   (N'&BYTES NE 0).P03
         MNOTE *,'PROGRAMMGROESSE FEHLT, 4096 BYTES WIRD ANGENOMMEN'
&REGANZ  SETA  1
         AGO   .P04
.P03     ANOP
&REGANZ  SETA  (&BYTES/4096)+1
.P04     ANOP
         AIF   (&REGANZ LE 10).P05
         MNOTE *,PROGRAMMGROESSE WIRD AUF 4096 BYTES GEKUERZT'
&REGANZ  SETA  10
.P05     ANOP
&REGEND  SETA  &REGANZ+2
.*
&NAME    SETC  '$@#'.'&SYSNDX'
         SAVE  (14,12)
         BALR  R3,0
         USING *,R3
         ST    R13,&NAME.3+4
         LR    R0,R2
         LR    R2,R13
         LA    R13,&NAME.3
         ST    R13,8(R2)
         LR    R2,R0
         AIF   (&REGANZ EQ 1).P10
         LM    R3,R&REGEND,&NAME.2    LADEN BAEISADRESSKONSTANTEN
         USING &NAME.1,R3             USING-ANWEISUNGEN
         USING &NAME.1+4096,R4
         AIF   (&REGANZ EQ 2).P10
         USING &NAME.1+8192,R5
         AIF   (&REGANZ EQ 3).P10
         USING &NAME.1+12288,R6
         AIF   (&REGANZ EQ 4).P10
         USING &NAME.1+16384,R7
         AIF   (&REGANZ EQ 5).P10
         USING &NAME.1+20480,R8
         AIF   (&REGANZ EQ 6).P10
         USING &NAME.1+24576,R9
         AIF   (&REGANZ EQ 7).P10
         USING &NAME.1+28672,R10
         AIF   (&REGANZ EQ 8).P10
         USING &NAME.1+32768,R11
         AIF   (&REGANZ EQ 9).P10
         USING &NAME.1+36864,R12
.P10     ANOP
         B     &NAME.1
&NAME.3  DC    18F'0'
         AIF   (&SCHALT EQ 1).P10A
&SCHALT  SETA  1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.P10A    AIF   (&REGANZ EQ 1).P11
&NAME.2  DC    A(&NAME.1)
         DC    A(&NAME.1+4096)
         AIF   (&REGANZ EQ 2).P11
         DC    A(&NAME.1+8192)
         AIF   (&REGANZ EQ 3).P11
         DC    A(&NAME.1+12288)
         AIF   (&REGANZ EQ 4).P11
         DC    A(&NAME.1+16384)
         AIF   (&REGANZ EQ 5).P11
         DC    A(&NAME.1+20480)
         AIF   (&REGANZ EQ 6).P11
         DC    A(&NAME.1+24576)
         AIF   (&REGANZ EQ 7).P11
         DC    A(&NAME.1+28672)
         AIF   (&REGANZ EQ 8).P11
         DC    A(&NAME.1+32768)
         AIF   (&REGANZ EQ 9).P11
         DC    A(&NAME.1+36864)
.P11     ANOP
&NAME.1  EQU   *
         MEND
         PRINT ON
*          DATA SET HD122630T  AT LEVEL 004 AS OF 05/04/76
         TITLE 'UHDV030S (HDVDATE) AUFBEREITEN DES DATUMS ZUM VERGLEICH*
               -UND DRUCK'
***********************************************************************
***********************************************************************
****           C O P Y R I G H T                                   ****
****           HAMBURGER DATENVERARBEITUNGS G.M.B.H                ****
****           Z.HD. HERRN HUETTEL                                 ****
****           ALTER WALL 12                                       ****
****                                                               ****
****           2000 HAMBURG 11                                     ****
****           TELEFON  040 - 37 00 5-223                          ****
****                                                               ****
****           DIE GEWERBLICHE NUTZUNG DIESES PROGRAMMS DURCH      ****
****           VERKAUF ODER VERMIETUNG IST VERBOTEN                ****
****                                                               ****
****           EINE HAFTUNG FUER DIESES PROGRAMM SEITENS DER       ****
****           HDV BESTEHT NICHT                                   ***
****                                                               ***
****                                                               ***
***********************************************************************
***********************************************************************
         EJECT
UHDV030S PROG  4000
         B     START
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*               AUFBEREITEN  DES  DATUMS  FUER VERGLEICH  JJMMTT      *
*                                                                     *
*               AUFBEREITEN  DES  DATUMS  FUER  DRUCK    TT.MM.HH     *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
****                                                               ****
*                                                                     *
*                            HILFSFELDER                              *
*                                                                     *
****                                                               ****
DSEINDAT DSECT
EINGDAT  DS    F                       GEPACKTE DATUM
DSVGLDAT DSECT
VGLDAT   DS    CL3                     VERGLEICHDATUM OHNE VORZEICHEN
DSDRUDAT DSECT
DRUDAT   DS    2F                      AUFBEREITEN  DRUCKDATUM
UHDV030S CSECT
HFELD1   DS    CL6
HFELD2   DS    CL10
SCHAB    DC    X'402120204B20204B2020' SCHABLONE DRUCKDAT
****                                                               ****
****                         HAUPTPROGRAMM                         ****
         USING DSEINDAT,R7
         USING DSVGLDAT,R8
         USING DSDRUDAT,R9
         DS    0H
START    EQU   *
         LM    R7,R9,0(R1)             PARAMETER LADEN
         CLI   3(R7),C' '              LETZTE STELLE DES DATUMS BLANK
         BNE   A3
         CLI   2(R7),C' '              LETZTEN 2 STELLEN BLANK
         BE    A2
         MVI   3(R7),X'FF'             FF NACH LETZTE STELLE
A1       MVO   HFELD1(5),0(4,R7)       VORZEICHEN VERSETZEN
         NI    HFELD1,X'0F'            0F AUF 1.STELLE
         MVC   0(4,R7),HFELD1          HILFSFELD NACH REG7
         B     A3
A2       MVC   2(2,R7),=X'00FF'        00FF NACH LETZTEN 2 STELLEN
         B     A1
A3       MVO   HFELD1(5),0(4,R7)
         MVC   0(1,R8),HFELD1+3        JAHR
         MVC   1(1,R8),HFELD1+2        MONAT
         MVC   HFELD2,SCHAB
         MVC   2(1,R8),HFELD1+1        TAG
         ED    HFELD2(10),0(R7)        DATUM AUFBEREITEN
         MVC   0(8,R9),HFELD2+2         DRUCKDATUM
         L     13,4(13)
         RETURN (14,12)
         LTORG
         PUSH  PRINT
         PRINT OFF
         DC    C'COPYRIGHT BY HAMBURGER DATENVERARBEITUNGS GMBH '
         DC    C'ALTER WALL 12  2000 HAMBURG 11 '
         DC    C'*',C'&SYSDATE',C'*'
         PRINT ON
         POP   PRINT
         CSECT
         DC    CL21'004HD122630T 05/04/76'
         END
