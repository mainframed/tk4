         MACRO
&NAME    MPLISTRT &BASIS=11
         LCLA  &A1,&ANZ,&SZ,&BREG(11)
         LCLB  &BZ(11)
         LCLC  &CR1,&CR2,&CR3,&R(10)
&A1      SETA  K'&BASIS
         AIF   (&A1 EQ 0).OHNE
&ANZ     SETA  N'&BASIS
         AIF   (&ANZ EQ 0).OHNE
         AIF   (&ANZ GT 16).DOPPELT
&SZ      SETA  1
.LOOP    ANOP
&A1      SETA  &BASIS(&SZ)
         AIF   (&A1 GT 11).FBASIS
         AIF   (&A1 LT 2).FBASIS
         AIF   (&A1 EQ 4).FBASIS
         AIF   (&BZ(&A1)).DOPPELT
&BZ(&A1) SETB  (1)
&BREG(&SZ) SETA &A1
&R(&SZ)  SETC  ','.'&A1'
         AIF   (&ANZ EQ &SZ).WEITER
&SZ      SETA  &SZ+1
         AGO   .LOOP
.FBASIS  MNOTE 'BASISREGISTER FALSCH'
.FEHLER  MNOTE 'KEINE GENERIERUNG'
         MEXIT
.DOPPELT MNOTE 'BASISREGISTER MEHRFACH'
         AGO   .FEHLER
.OHNE    ANOP
&BREG(1) SETA  11
&ANZ     SETA  1
.WEITER  ANOP
&NAME    START 0
         USING *,15
         B     *+12
         DC    AL1(7)
         DC    CL7'&NAME'
         STM   14,11,12(13)
         DROP  15
&CR1     SETC  '&BREG(1)'
         BALR  &CR1,0
         USING *&R(1)&R(2)&R(3)&R(4)&R(5)&R(6)&R(7)&R(8)&R(9)&R(10)
         ST    13,SAV&SYSNDX+4
         LA    4,SAV&SYSNDX
         LA    13,SAV&SYSNDX
         B     SAV&SYSNDX+4*20
SAV&SYSNDX DC  20F'0'
         AIF   (&ANZ EQ 1).ENDE
&CR3     SETC  '&BREG(&ANZ)'
         LA    &CR3,2048
&SZ      SETA  2
.GEN     AIF   (&BREG(&SZ) EQ 0).ENDE
&CR1     SETC  '&BREG(&SZ)'
&CR2     SETC  '&BREG(&SZ-1)'
         LA    &CR1,2048(&CR2,&CR3)
&SZ      SETA  &SZ+1
         AGO   .GEN
.ENDE    ANOP
         MEND
         MACRO
&NAME    MPLIEND
&NAME    L     13,4(13)
         LM    14,11,12(13)
         BR    14
         MEND
         TITLE 'MPLITP, INTERFACE TCAM - PL/I-PROGRAMM'
         ISEQ  73,80
         PRINT NOGEN
         SPACE 3
*        HINWEISE FUER DEN AUFRUF DES UNTERPROGRAMMS
         SPACE
*        MPLITP WIRD VON PL/I-PROGRAMMEN AUFGERUFEN, UM VON EINER
*        TCAM-QUEUE ZU LESEN ODER AUF EINE TCAM-QUEUE AUSZUGEBEN.
*        DER AUFRUF ERFOLGT DURCH CALL MIT PARAMETER-UEBERGABE.
*        UEBERGEBEN WIRD EINE STRUKTUR MIT FOLGENDEM AUFBAU:
*
*        BEFEHLSSCHLUESSEL              FIXED(1) MIT DEN MOEGLICHKEITEN
*                                       1 = GET, 2 = PUT, 3 = CLOSE
*                                       EIN- UND AUSGABE, 4 = CLOSE
*                                       EINGABE, 5 = CLOSE AUSGABE
*
*        RETURNCODE                     FIXED(1) MIT DEN MOEGLICHKEITEN
*                                       0 = IN ORDNUNG, 1 = ENDFILE
*                                       EINGABE, 2 = PUFFERUEBERLAUF
*                                       BEIM EINLESEN, 3 = FALSCHE
*                                       BESTIMMUNGSDATENSTATION BEI DER
*                                       AUSGABE, 4 = FALSCHER SEGMENT-
*                                       CODE BEI DER AUSGABE.
*                                       RETURNCODE 2 IST NUR MOEGLICH,
*                                       WENN IM OPTCD-PARAMETER DER
*                                       DD-KARTE NICHT C KODIERT WURDE,
*                                       RETURNCODE 4 IST DAGEGEN NUR
*                                       MOEGLICH, WENN IM OPTCD-PARAM.
*                                       DER DD-KARTE C KODIERT WURDE,
*                                       2 UND 4 SCHLIESSEN SICH ALSO
*                                       GEGENSEITIG AUS.
*
*        LAENGE                         FIXED (5), ENTHAELT NACH EINEM
*                                       GET DIE DATENLAENGE UND IST VOR
*                                       EINEM PUT MIT DER SATZLAENGE ZU
*                                       FUELLEN. DIE LAENGE ERRECHNET
*                                       SICH AUS DER LAENGE DES EIN-/
*                                       AUSGABESATZES ZUZUEGLICH
*                                       8 BYTES (DATENSTATIONSNAME) UND
*                                       ZUZUEGLICH 1 BYTE (SEGM.CODE),
*                                       FALLS OPTCD=WUC KODIERT WURDE.
*
*        SEGMENTCODE                    PIC 'Z', GIBT AN, WELCHER TEIL
*                                       EINER MESSAGE VORLIEGT, DAS
*                                       FELD IST NUR VORHANDEN, WENN IM
*                                       OPTCD-PARAMETER DER DD-KARTE
*                                       EIN C KODIERT WURDE.
*                                       BEDEUTUNG:
*                                       1 = 1. SEGMENT VON MEHREREN
*                                       0 (BLANK) = IRGENDEIN MITTLERES
*                                       SEGMENT VON MEHREREN
*                                       2 = LETZTES SEGMENT VON
*                                       MEHREREN
*                                       3 = EINZIGES SEGMENT
*
*        DATENSTATION                   CHAR(8) ENTHAELT NACH EINEM GET
*                                       DIE ABSENDENDE DATENSTATION UND
*                                       MUSS VOR EINEM PUT DIE BESTIM-
*                                       MUNGSDATENSTATION ENTHALTEN
*
*        EIN-/AUSGABESATZ               CHARACTERFELD BELIEBIGER LAENGE
*                                       ENTHAELT NACH EINEM GET DIE
*                                       GELESENEN DATEN, VOR EINEM PUT
*                                       DEN AUSGABESATZ
*
*        IN DEN DD-KARTEN FUER DEN EIN- UND AUSGABE-DCB MUESSEN
*        ANGEGEBEN WERDEN:
*        1. ALS DD-NAMEN  TCAMIN   FUER DEN EINGABE-DCB (GET) UND
*                         TCAMOUT  FUER DEN AUSGABE-DCB (PUT),
*        2. IM BLKSIZE-PARAMETER (UNTERPARAMETER DES DCB-PARAMETERS)
*           DIE GROESSTE MOEGLICHE SATZ- ODER SEGMENTLAENGE EIN-
*           SCHLIESSLICH DER 8 BYTES FUER DEN DATENSTATIONSNAMEN UND,
*           FALLS SO KODIERT, AUCH NOCH DES EINEN BYTES FUER DEN
*           SEGMENTCODE, GROESSTE MOEGLICHE BLOCKLAENGE IST 32760.
*        3. IM OPTCD-PARAMETER (UNTERPARAMETER DES DCB-PARAMETERS)
*           WU, WENN MESSAGE = SATZ SEIN SOLL, WUC, WENN SEGMENTE
*           GEBILDET WERDEN SOLLEN.
*
*        WIRD EINE DD-KARTE MIT DEM DD-NAMEN TCAMLOG VORGELEGT,
*        WERDEN ALLE EIN- UND ABGEHENDEN NACHRICHTEN AUF DIESEM
*        DATASET PROTOKOLLIERT. IN DER DD-KARTE IST IM DCB DIE
*        BLKSIZE ANZUGEBEN. ALS RECFM WIRD U ANGENOMMEN, DIE DATEI
*        KANN SPAETER MIT IEBPTPCH ODER EINEM ANDEREN HILFSPROGRAMM
*        AUSGEDRUCKT WERDEN. AUSGEGEBEN WERDEN DIE UEBERGABESTRUKTUR
*        UND DER DATENTEIL IN DER AKTUELLEN LAENGE.
         EJECT
*        STARTROUTINE
         SPACE
MPLITP   MPLISTRT ,                     VERBINDUNG ZU PL/I
         SPACE 2
*        REGISTERBENUTZUNG
         SPACE
R0       EQU   0                        SYSTEM
R1       EQU   1                        SYSTEM
R2       EQU   2                        RECHENREGISTER
R3       EQU   3                        BASIS FUER PL/I-UEBERGABEFELD
R4       EQU   4                        RESERVIERT FUER PL/I
R5       EQU   5                        ADRESSE DER WORKAREA GET/PUT
R6       EQU   6                        BASIS FUER EMCP UND AMCP-DSECT
R7       EQU   7                        FREI
R8       EQU   8                        FREI
R9       EQU   9                        FREI
R10      EQU   10                       FREI
R11      EQU   11                       BASISREGISTER
R12      EQU   12                       RESERVIERT FUER PL/I
R13      EQU   13                       RESERVIERT FUER PL/I
R14      EQU   14                       RESERVIERT FUER PL/I
R15      EQU   15                       RETURNCODE
         SPACE
         L     R3,0(R1)                 LADEN AUS PARAMETERLISTE
         USING PLI,R3                   BASISREG FUER PL/I-INTERFACE
         USING IHADCB,R6                BASISREGISTER FUER DIE DCBS
ANFANG1  NOP   ANFANG2                  AUSFUEHREN NUR BEIM 1. MAL
         OI    ANFANG1+1,X'F0'          NOP ZU BRANCH
         OPEN  (LOG,(OUTPUT))           LOG-DATEI EROEFFNEN
         TM    LOG+48,X'10'             DD-KARTE VORHANDEN?
         BNO   ANFANG2                  NEIN
         NI    RETURN+1,X'0F'           LOG AKTIVIEREN
ANFANG2  DS    0H
         ZAP   PLIRCODE,=P'0'           RETURNCODE AUF NULL SETZEN
         ZAP   DWORT,PLIORDER           BINAERFORM DES BEFEHLS
         CVB   R2,DWORT                 INS REGISTER 2 BRINGEN
         BCTR  R2,R0                    REG 2 UM 1 VERMINDERN
         SLA   R2,2                     REG 2 MIT 4 MULTIPLIZIEREN
         B     ORDER(R2)                INDIZIERTES VERZWEIGEN
         SPACE
ORDER    B     GETMCP                   VERZWEIGUNGSTABELLE INDIZIERT
         B     PUTMCP                   DURCH DEN IN REG 2 AUFBEREI-
         B     CLOSEMCP                 TETEN BEFEHLSSCHLUESSEL
         B     CLOSEGET
         B     CLOSEPUT
         EJECT
*        LESEN VOM MCP-PROGRAMM
         SPACE
GETMCP   LA    R6,EMCP                  BASISREG FUER DCB-ADRESSIERUNG
GETMCP1  NOP   GETMCP2                  ZUERST OPEN, SPAETER BRANCH
         OI    GETMCP1+1,X'F0'          OPEN-WEICHE AUS
         OPEN  (EMCP)                   EROEFFNEN EINGABE-DCB
GETMCP2  LA    R5,PLIUNIT               LADEN ADRESSE WORKAREA
         GET   EMCP,(R5)                LESEN IN PL/I-WORKAREA
         LH    R2,DCBLRECL              UMRECHNEN DER AKTUELLEN
         CVD   R2,DWORT                 SATZLAENGE UND UEBERTRAGEN
         MVC   PLILANG,DWORT+5          IN DEN PL/I-UEBERGABEBEREICH
         CH    R15,=H'8'                RETURNCODE FUER PUFFERUEBERLAUF
         BE    RECORD                   FEHLERROUTINE AUFSUCHEN
         CLI   0(R5),C'C'               STANDLEITUNG?
         BNE   RETURN                   NEIN
         BCTR  R2,R0                    LAENGE MINUS 1
         AR    R5,R2                    ADRESSE LETZTES ZEICHEN
         CLI   0(R5),X'37'              EOT?
         BNE   RETURN                   NEIN
         SP    PLILANG,=P'1'            LAENGE MINUS 1 = EOT ENTFERNEN
         XC    0(1,R5),0(R5)            EOT LOESCHEN
         B     RETURN                   RUECKSPRUNG INS PL/I-PROGRAMM
         SPACE 3
*        AUSGEBEN AN DAS MCP-PROGRAMM
         SPACE
PUTMCP   LA    R6,AMCP                  BASISREG FUER DCB-ADRESSIERUNG
PUTMCP1  NOP   PUTMCP2                  ZUERST OPEN, SPAETER BRANCH
         OI    PUTMCP1+1,X'F0'          OPEN-WEICHE AUS
         OPEN  (AMCP,(OUTPUT))          EROEFFNEN AUSGABE-DCB
PUTMCP2  LA    R5,PLIUNIT               LADEN ADRESSE DER WORKAREA
         ZAP   DWORT,PLILANG            BINAERFORM DER LAENGE UEBER
         CVB   R2,DWORT                 REG 2 IN DEN
         STH   R2,DCBLRECL              AUSGABE-DCB UEBERTRAGEN
         PUT   AMCP,(R5)                AUSGEBEN ANS MCP
         STC   R15,ERRBYTE              SPEICHERN RETURNCODE
         CLI   ERRBYTE,X'08'            RETURNCODE FUER FALSCHEN
         BE    SCODEFEH                 SEGMENTCODE, FEHLERROUTINE
         CLI   ERRBYTE,X'0C'            RETURNCODE FUER WRONG DESTIN
         BE    DESTFEHL                 FEHLERROUTINE AUFSUCHEN
         CLI   ERRBYTE,X'10'            QUEUE NICHT AUFNAHMEBEREIT?
         BE    LOOPPUT                  JA
         ZAP   LOOPZAHL,=P'10000'       LOOPZAEHLER IN GRUNDSTELLUNG
         B     RETURN                   RUECKKEHR INS PL/I-PROGRAMM
LOOPPUT  DS    0H
         SP    LOOPZAHL,=P'1'           LOOPZAEHLER UM 1 VERMINDERN
         BNZ   PUTMCP2                  WENN NICHT NULL, WIEDER AUSGABE
         STIMER WAIT,BINTVL=WARTZEIT    5 SEKUNDEN WARTEN
         ZAP   LOOPZAHL,=P'10000'       LOOPZAEHLER IN GRUNDSTELLUNG
         B     PUTMCP2                  WIEDER AUSGABE VERSUCHEN
         EJECT
*        FEHLER- UND ENDE-ROUTINEN
         SPACE
RECORD   ZAP   PLIRCODE,=P'2'           RETURNCODE FUER PUFFERUEBERLAUF
         BAL   R2,EMCPENDE              ABSCHLIESSEN EINGABE-DCB
         B     RETURN                   RUECKKEHR INS PL/I-PROGRAMM
         SPACE 2
DESTFEHL ZAP   PLIRCODE,=P'3'           RETURNCODE FUER WRONG DESTIN.
         B     RETURN                   RUECKKEHR INS PL/I-PROGRAMM
         SPACE 2
SCODEFEH ZAP   PLIRCODE,=P'4'           RETURNCODE FUER F. SEGMENTCODE
         B     RETURN                   RUECKKEHR INS PL/I-PROGRAMM
         SPACE 2
ENDE     ZAP   PLIRCODE,=P'1'           RETURNCODE FUER ENDFILE EMCP
         ZAP   PLILANG,=P'0'            DATENLAENGE NULL
RETURN   B     RETURN1                  NORMAL KEIN PROTOKOLL
         LA    R6,LOG                   ADRESSE LOG-DCB
         ZAP   DWORT,PLILANG            LAENGE DATENTEIL
         AP    DWORT,=P'5'              DAZU UEBERGABEFELDER
         CVB   R2,DWORT                 BINAERFORMAT
         STH   R2,DCBLRECL              IM DCB ALS SATZLAENGE SPEICHERN
         PUT   LOG,(R3)                 PROTOKOLLIEREN
RETURN1  MPLIEND                        RUECKKEHR INS PL/I-PROGRAMM
         EJECT
*        ABSCHLIESSEN DER DCBS
         SPACE
CLOSEMCP BAL   R2,EMCPENDE              CLOSE EINGABE-DCB
         BAL   R2,AMCPENDE              CLOSE AUSGABE-DCB
         B     RETURN                   RUECKKEHR INS PL/I-PROGRAMM
         SPACE
CLOSEGET BAL   R2,EMCPENDE              CLOSE EINGABE-DCB
         B     RETURN                   RUECKKEHR INS PL/I-PROGRAMM
         SPACE
CLOSEPUT BAL   R2,AMCPENDE              CLOSE AUSGABE-DCB
         B     RETURN                   RUECKKEHR INS PL/I-PROGRAMM
         SPACE 2
EMCPENDE CLOSE (EMCP)
         NI    GETMCP1+1,X'0F'          WEICHE WIEDER AUF OPEN
         ZAP   PLILANG,=P'0'            DATENLAENGE NULL
         BR    R2
         SPACE
AMCPENDE CLOSE (AMCP)
         NI    PUTMCP1+1,X'0F'          WEICHE WIEDER AUF OPEN
         ZAP   PLILANG,=P'0'            DATENLAENGE NULL
         BR    R2
         SPACE 3
*        DEFINITIONEN
         SPACE 3
*        EINGABE-DCB
         SPACE
EMCP     DCB   DSORG=PS,                                               *
               DDNAME=TCAMIN,                                          *
               MACRF=GM,                                               *
               RECFM=U,                                                *
               STOP=BOTH,                                              *
               EODAD=ENDE
         SPACE 3
*        AUSGABE-DCB
         SPACE
AMCP     DCB   DSORG=PS,                                               *
               DDNAME=TCAMOUT,                                         *
               MACRF=PM,                                               *
               RECFM=U
         SPACE 3
*        LOG-DCB
         SPACE
LOG      DCB   BUFNO=1,DDNAME=TCAMLOG,DSORG=PS,MACRF=PM,RECFM=U
         SPACE 3
DWORT    DS    D                        RECHENFELD
ERRBYTE  DS    CL1
LOOPZAHL DC    P'10000'                 LOOPZAEHLER
WARTZEIT DC    F'500'                   WARTEZEIT FUER AUSGABELOOP
         LTORG
         SPACE 3
*        DUMMY-SECTION FUER DIE ADRESSIERUNG DER DCBS
         SPACE
         DCBD  DSORG=QS
         SPACE 3
*        DUMMY-SECTION FUER DIE ADRESSIERUNG DES PL/I-BEREICHS
         SPACE
PLI      DSECT
PLIORDER DS    CL1                      BEFEHL
PLIRCODE DS    CL1                      RETURNCODE
PLILANG  DS    CL3                      SATZLAENGE
PLIUNIT  DS    CL1                      WORKAREA EINSCHLIESSLICH SEG-
*                                       MENTCODE UND DATENSTATIONSNAME
         END
