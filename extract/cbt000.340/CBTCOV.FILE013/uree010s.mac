         TITLE 'UMRECHNUNGS-ROUTINE FUER DATUMSANGABEN'
*$
*$
*$.      +1
*$       AENDERUNGSSTAND
*$D      AENDERUNGSSTAND
*$D      AENDERUNGSSTAND
*$
*$       ERSTELLUNGSDATUM:   11.03.1980
*$
*$       LETZTE AENDERUNG:   15.07.1980     VERSION 2.0
*$                           01.02.1982     VERSION 2.1
*$                           07.02.1984     VERSION 2.2
*$
*$       VERANTWORTLICH:     R. MEYER / SYSTEMPROGRAMMIERUNG
*$
*$
*$.      +1
*$       AUFGABE DES PROGRAMMS
*$D      AUFGABE DES PROGRAMMS
*$D      AUFGABE DES PROGRAMMS
*$
*$       MIT HILFE DIESER ROUTINE KOENNEN VERSCHIEDENDE BERECHNUNGEN
*$       IM ZUSAMMENHANG MIT DATUMSANGABEN DURCHGEFUEHRT WERDEN. DIE
*$       ROUTINE KANN VON ANDEREN PROGRAMMEN MIT LINK ODER LOAD/CALL
*$       AUFGERUFEN WERDEN. DABEI SIND FOLGENDE INFORMATIONEN
*$       IN DEN REGISTERN 0 UND 1 ZUR VERFUEGUNG ZU STELLEN:
*$
*$          REG 0 (H/H)           REG 1            FUNKTION
*$              0 / 4           DATUM(TTMMJJ)  TTMMJJ ==> JJTTT
*$              0 / 8           DATUM(JJTTT)   JJTTT ==> TTMMJJ
*$              0 / 12          N/A            AKTUELLES DATUM (JJTTT)
*$              0 / 16          N/A            AKTUELLES DATUM (TTMMJJ)
*$          DIFF  / 20          DATUM(JJTTT)   DATUM (JJTTT) VERRINGERN
*$          DIFF  / 24          DATUM(JJTTT)   DATUM (JJTTT) ERHOEHEN
*$          DIFF  / 28          DATUM(JJTTT)   DATUM(TTMMJJ) VERRINGERN
*$          DIFF  / 32          DATUM(JJTTT)   DATUM(TTMMJJ) ERHOEHEN
*$              0 / 36          ZEIT (BIN)     ZEIT ==> HHMMSS
*$              0 / 40          ZEIT (HHMMSS)  ZEIT ==> BIN (1/100 SEC)
*$              0 / 44          N/A            AKT. WOCHENTAG (2 BYTES)
*$              0 / 48          DATUM(JJTTT)   WOCHENTAG (2 BYTES)
*$              0 / 52          DATUM(TTMMJJ)  WOCHENTAG (2 BYTES)
*$
*$       DIE ROUTINE GIBT DEN UMGEWANDELTEN WERT IM REGISTER 1 IN
*$       GEPACKTER FORM ZURUECK. BEI WOCHENTAG FUNKTIONEN WIRD IN
*$       DEN LOW ORDER BITS DER WOCHENTAG IN 2-CHARACTER DARSTELLUNG
*$       ZURUECKGEGEBEN. REGISTER 15 ENTHAELT EINEN RETURN CODE 0
*$       WENN DIE ROUTINE ERFOLGREICH WAR, BZW. 12 BEI
*$       FEHLERHAFTER BEARBEITUNG (FALSCHE EINGABE-PARAMETER).
*$
*$       DIE CODIERUNG DER ROUTINE ERLAUBT ES, SIE IN EINEM PROGRAMM
*$       MEHRFACH MIT CALL AUFZURUFEN.
         EJECT
***********************************************************************
*                                                                     *
*   ANFANG    EROEFFNUNG VON ASSEMBLER-PROGRAMMEN                     *
*             LETZTE AENDERUNG :  06.07.84                            *
*                                                                     *
***********************************************************************
         MACRO
&NAME    ANFANG &V,&CD=C,&LC=0,&BR=1,&EP=,&L=0,&EQU=YES,&RENT=NO
         MNOTE  *,'        ***   VERSION VOM 06.07.84   ***'
.*
.* V   WIRD *V* ANGEGEBEN UND IST *BR>=1*, ERFOLGT DIE ADRESSIERUNG DER
.*     ERSTEN INSTRUKTION SCHON UEBER DAS BASISREG. 3, SONST UEBER R15.
.*     WIRD *R* ANGEGEBEN, WERDEN BIS ZUR DEFINITION DER SAVEAREA R15,
.*     VON DORT AB R13 UND NACH DESSEN ERSCHOEPFUNG MIT DEM PARAMETER
.*     *BR= * EVTL. AUSSERDEM VERLANGTE REGISTER ALS BASISREGISTER
.*     VERWENDET.
.*     WIRD *O* ANGEGEBEN ODER WIRD DER PARAMETER AUSGELASSEN UND IST
.*     *BR>=1*, WERDEN BIS ZUR BEENDIGUNG DES SAVEAREA HANDLINGS R15,
.*     VON DORT AB R3 UND SPAETER MIT DEM PARAMETER *BR= * EVENTUELL
.*     AUSSERDEM VERLANGTE REGISTER ALS BASISREGISTER VERWENDET. IST
.*     *BR=0*, WIRD DURCHGEHEND R15 ALS BASISREGISTER VERWENDET.
.*     DER PARAMETER *O* BEWIRKT AUSSERDEM, DASS FEHLER IN EINER ASS.-
.*     UNTERROUTINE EINES PL/I-OPT. HAUPTPROGRAMMS VOM PL/I-ERRORHAND-
.*     LER BEHANDELT UND FEHLERMELDUNGEN, DIE DIE FEHLERQUELLE IDENTI-
.*     FIZIEREN, AUSGEGEBEN WERDEN.
.* CD= STANDARDWERT IST *C* WIE *CSECT*
.*     ZULAESSIG IST AUCH *S* WIE *START*
.* LC= STANDARDWERT IST *0*, SETZT DEN ZUORDNUNGZAEHLER (AUF 0).
.*     SOWIE *NO*, WENN WEDER CSECT NOCH START ERSCHEINEN SOLL
.*     NUR ZULAESSIG IN VERBINDUNG MIT *START*, SIEHE CD=
.* BR= STANDARDWERT IST *1*, GIBT DIE ANZAHL DER BASISREGISTER AN.
.*     ZULAESSIG SIND *0*,*1*,*2* ODER *3*. ES WERDEN DANN ENTWEDER
.*     KEINE ODER IN DER REIHENFOLGE R3,R4,R5 DIE ANGEGEBENE ANZAHL AN
.*     BASISREGISTERN GENERIERT.ZULAESSIG SIND AUCH DIE FORMEN
.*     BR=(X)  BR=(X,Y)  BR=(X,Y,Z)
.*     WOBEI X,Y,Z JEWEILS FUER DIE REGISTER STEHEN, DIE ALS BASIS-
.*     REGISTER DIENEN SOLLEN, Z.B.  BR=(R5,R6)
.* EP= NAME DES EINGANGSPUNKTES. ANGABE EMPFEHLENSWERT, WENN OBERPRO-
.*     GRAMM IN PL/1 GESCHRIEBEN
.* L=  LETZTES ZEICHEN DES LABELS, DER DEN BEGINN DER BASISREGISTER-
.*     ZUORDNUNG KENNZEICHNET (=BASIS&L, WENN &L ^= 0).
.*
.* EQU= EQUATES FUER DIE REGISTER (RX) WERDEN GENERIERT (DEFAULT) ODER
.*     NICHT (EQU=YES×NO) .
.*
.* RENT= DIE SAVEAREA WIRD STATISCH (DEFAULT) ODER DYNAMISCH ZUGEORDNET
.*      (RENT=YES×NO) .
.*
.* GLOBALE VARIABLE ZUM TEST GEGEN DOPPELDEFINITIONEN DER REGISTER
         GBLB  &GLBIT  BETROFFEN SIND DIE EQU'S
         GBLA  &GLBR
         LCLA  &SETBR  ZUM TESTEN FUER BR=
         LCLA  &SL,&OFS
         LCLC  &MM,&NN,&BR1,&BR2,&BR3
.*
         AIF   (T'&NAME NE 'O').M005
         MNOTE 12,'UNBENANNTE CONTROL-SECTION'
         AGO   .M130
.M005    ANOP
&NN      SETC  '&NAME'
         AIF   ('&EP' NE '&NAME').M010
         AIF   ('&V' NE 'O').M010
         AIF   (K'&NAME EQ 8).M007
&MM      SETC  '1'
         AGO   .M010
.M007    ANOP
&NN      SETC  '&NAME'(1,7)
&MM      SETC  '$'
.M010    ANOP
         AIF   ('&CD' EQ 'C').M020
         AIF   ('&CD' EQ 'S').M030
         AIF   ('&CD' EQ 'NO').M040
         MNOTE 4,'CD= IST UNGUELTIG'
.M020    ANOP
&NN&MM   CSECT
         AGO   .M042
.M030    ANOP
&NN&MM   START &LC
         AGO   .M042
.M040    ANOP
&NN&MM   DS    0F
.M042    ANOP
         AIF   ('&BR'(1,1) EQ '(').M043
&SETBR   SETA  &BR
&BR1     SETC  'R3'
&BR2     SETC  'R4'
&BR3     SETC  'R5'
         AGO   .M044
.M043    ANOP
&SETBR   SETA N'&BR
&BR1     SETC  '&BR(1)'
         AIF   (&SETBR LT 2).M044
&BR2     SETC  '&BR(2)'
         AIF   (&SETBR LT 3).M044
&BR3     SETC  '&BR(3)'
.M044    ANOP
         AIF   (&GLBIT).M045
         AIF   ('&EQU' EQ 'NO').M045
*
*
R0       EQU   0 PARAMETER-REGISTER
R1       EQU   1 PARAMETER-REGISTER
R2       EQU   2 ARBEITSREGISTER
R3       EQU   3 ARBEITSREGISTER
R4       EQU   4 ARBEITSREGISTER
R5       EQU   5 ARBEITSREGISTER
R6       EQU   6 ARBEITSREGISTER
R7       EQU   7 ARBEITSREGISTER
R8       EQU   8 ARBEITSREGISTER
R9       EQU   9 ARBEITSREGISTER
R10      EQU   10 LINK-REGISTER
R11      EQU   11 3. BASISREGISTER
R12      EQU   12 2. BASISREGISTER
R13      EQU   13 1. BASISREGISTER
R14      EQU   14 RUECKKEHR-REGISTER
R15      EQU   15 VERZWEIG-REGISTER
*
*
         MNOTE *,'SYMBOLISCHE REGISTER GENERIERT'
*
*
.M045    AIF   ('&V' EQ 'V').M200
         AIF   ('&V' EQ 'R').M050
         AIF   ('&V' EQ 'O').M055
         AIF   (T'&V EQ 'O').M050
         MNOTE 4,'UNGUELTIGER POSITIONSPARAMETER'
.M050    ANOP
         USING *,R15
.M055    ANOP
&SL      SETA  K'&EP
         AIF   (T'&EP EQ 'O').M100
         AIF   ('&V' NE 'O').M080
         ENTRY &EP
         AIF   (&SL/2 NE (&SL+1)/2).M077
         DC    C' &EP'
         AGO   .M078
.M075    ANOP
         B     *+7+8+5+2*((&SL-1)/2)
         DC    AL1(&SL)
.M077    ANOP
         DC    C'&EP'
.M078    ANOP
         AIF   ('&V' NE 'O').M090
         DC    AL1(&SL)
         CNOP  0,4
         USING *,R15
&EP      EQU   *
         AGO   .M095
.M080    ANOP
         AIF   ('&EP' EQ '&NAME').M075
         ENTRY &EP
&EP      DS    0H
         AGO   .M075
.M090    ANOP
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
.M095    ANOP
*
*
         MNOTE *,'ENTRY - EINGANGSPUNKT GENERIERT'
*
*
.M100    ANOP
         AIF   ('&V' NE 'O').M104
         STM   R14,R12,12(R13) .  SAVE REGISTERS
         B     *+12               BRANCH AROUND CONSTANT
         DC    A(88)              LENGTH OF DSA
         DC    A(0)
         L     R0,*-8
         LR    R4,R1 .            SAVE ANY PARAMETERLIST-ADDR.
         L     R1,76(0,R13) .     ADDR. OF NEXT AVAILABLE BYTE IN STOR.
         ALR   R0,R1
         CL    R0,12(0,R12) .     COMP. WITH LAST AVAIL. BYTE IN STOR.
         BNH   *+10
         L     R15,116(0,R12) .   PL/I STORAGE OVERFLOW ROUTINE
         BALR  R14,R15 .          ATTEMPT TO OBTAIN MORE STORAGE
         ST    R0,76(0,R1) .      ADDR. OF NEXT AVAIL. BYTE IN STOR.
         ST    R13,4(0,R1)
         ST    R1,8(0,R13)
         MVC   72(4,R1),72(R13) . COPY ADDR. OF LIBRARY WORKSPACE
         LR    R13,R1
         MVI   0(R13),X'80'
         MVI   86(R13),X'91'
         MVI   87(R13),X'C0'
         LR    R1,R4
*
*
*        ENDE DES SAVEAREA-HANDLINGS
*
*
         AGO   .M200
.M104    ANOP
         CNOP  2,4
         STM   R14,R12,12(R13) .  SAVE REGISTERS
         LR    R11,R13
         AIF   ('&RENT' NE 'YES').M104A
         LR    &BR1,R15           ENTRY POINT INTO BASE
         USING &NAME,&BR1
         LR    R4,R1              SAVE PARM REGISTER VALUE
         LR    R5,R0              SAVE PARM REGISTER VALUE
         GETMAIN R,LV=72
         LR    R13,R1             POINT TO GOTTEN SAVE AREA
         LR    R1,R4              RESTORE PARM REGISTER VALUE
         LR    R0,R5              RESTORE PARM REGISTER VALUE
         ST    R13,8(0,R11)       NEW SAVE AREA PTR INTO CALLER'S
         ST    R11,4(0,R13)       OLD SAVE AREA PTR INTO OWN
         DROP  R15
*
*
*        ENDE DES SAVEAREA-HANDLINGS
*
*
         AGO   .M999
.M104A   ANOP
         ST    R13,*+12
         BAL   R13,*+19*4
         AIF   ('&V' NE 'R').M105
         USING *,R13
.M105    ANOP
ANFA&SYSNDX DS 0F   WORT 0 NUR VON PL1 VERWENDET
         DC    X'0080'
         DC    AL2(72)
         DC    F'0' WORT 1 A(SAVEAREA IM RUFENDEN PROGRAMM)
         DC    F'0' WORT 2 A(SAVEAREA IM GERUFENEN PROGRAMM)
         DC    F'0' WORT 3 R14
         DC    F'0' WORT 4 R15
         DC    F'0' WORT 5 R0        ** BEI         R14
         DC    F'0' WORT 6 R1        ** ZUGRIFFS-   R15
         DC    F'0' WORT 7 R2        ** ROUTINEN    R0
         DC    F'0' WORT 8 R3 BASIS1 ** WERDEN      R1
         DC    F'0' WORT 9 R4 BASIS2 ** HIER        R2
         DC    F'0' WORT10 R5 BASIS3 ** FOLGENDE    R3
         DC    F'0' WORT11 R6        ** REGISTER    R4
         DC    F'0' WORT12 R7        ** VON DER     R5
         DC    F'0' WORT13 R8        ** ZUGRIFFS-   R6
         DC    F'0' WORT14 R9        ** ROUTINE     R7
         DC    F'0' WORT15 R10       ** GESPEICHERT R8
         DC    F'0' WORT16 R11
         DC    F'0' WORT17 R12
*
         ST    R13,8(0,R11)
*
*
*        ENDE DES SAVEAREA-HANDLINGS
*
*
         AIF   ('&V' NE 'V').M200
         AGO   .M120
.M110    ANOP
.M120    AIF   (&GLBR GT &SETBR).M130
&GLBR    SETA  &SETBR
         AIF   ('&V' EQ 'V').M130
         DROP  R15
.M130    ANOP
&GLBIT   SETB  1
*
*
*        ENDE DES VORSPANNS
*
*
         MEXIT
.M200    AIF   (&SETBR NE 0).M220
         MNOTE *,'KEINE BASISREGISTER GENERIERT'
         AIF   ('&V' EQ 'V').M050
         AGO   .M110
.M220    AIF   ('&V' EQ 'R').M250
         BALR  &BR1,0
         AIF   ('&L' EQ '0').M222
BASIS&L  EQU   *
.M222    USING *+&OFS,&BR1
         AIF   (&GLBR GT 0).M225
.M225    AIF   (&SETBR GE 2).M240
.M230    AIF   ('&V' EQ 'V').M055
         AGO   .M110
.M240    LA    &BR2,4095(,&BR1)
         USING *+&OFS+4091,&BR2
         AIF   (&GLBR GT 1).M245
.M245    AIF   (&SETBR EQ 2).M230
         LA    &BR3,4095(,&BR2)
         USING *+&OFS+2*4091,&BR3
         AIF   (&GLBR GT 2).M230
         AGO   .M230
.M250    LA    &BR1,4095(,R13)
&OFS     SETA  4095-80
         AGO   .M222
.M999    ANOP
         MEND
         EJECT
***********************************************************************
*                                                                     *
*   ENDE      ABSCHLUSS VON ASSEMBLER-PROGRAMMEN                      *
*             LETZTE AENDERUNG :  23.07.84                            *
*                                                                     *
***********************************************************************
         MACRO
&NAME    ENDE &L,&R1,&RC=,&RENT=NO
         MNOTE  *,'      ***   VERSION VOM 23.07.84   *** '
*
         LCLC  &ENDEC1,&ENDEC2
         AIF   (T'&RC EQ 'O').S040
         AIF   ('&RC'(1,1) EQ '(').S010 REG.NOTATION
         AIF   ('&RC'(1,1) LT '0').S020
&NAME    LA    R15,&RC            RETURNCODE LADEN
         AGO   .S030
.S010    ANOP
&NAME    LA    R15,0(,&RC(1))     RETURNCODE LADEN
         AGO   .S030
.S020    ANOP
&NAME    L     R15,&RC            RETURNCODE LADEN
.S030    ANOP
         LR    R11,R13            SAVE SAVE AREA POINTER
         L     R13,4(,R13)        A(SAVEAREA IM RUFENDEN PROGRAMM)
&ENDEC1  SETC  'R0'
&ENDEC2  SETC  '20'
         AGO   .S050
.S040    ANOP
         LR    R11,R13            SAVE SAVE AREA POINTER
&NAME    L     R13,4(,R13)        A(SAVEAREA IM RUFENDEN PROGRAMM)
.S050    ANOP
         AIF   ('&RENT' NE 'YES').S055
         STM   R0,R1,20(R13)       TEMP SAVE
         LR    R3,R15              SAVE RETURN CODE
         FREEMAIN R,LV=72,A=(11)
         LR    R15,R3              RESTORE RETURN CODE
         LM    R0,R1,20(R13)       RESTORE R0 / R1
.S055    ANOP
         AIF   (T'&R1 EQ 'O').S060
         AIF   ('&R1' NE 'R1').S900
&ENDEC1  SETC  'R2'
&ENDEC2  SETC  '28'
.S060    AIF   (T'&R1 EQ 'O' AND T'&RC EQ 'O').S070
         L     R14,12(,R13)       ALTE REG.STAENDE WIEDERHERSTELLEN
         LM    &ENDEC1,R12,&ENDEC2.(R13)
         AGO   .S080
.S070    LM    R14,R12,12(R13)    ALTE REG.STAENDE WIEDERHERSTELLEN
.S080    BR    R14                RETURN
         AIF   (T'&L EQ 'O').S090
         AIF   ('&L' NE 'L').S910
         AGO   .S920
.S090    MEXIT
.S900    IHBERMAC 37,,&R1         INVALID OPERAND
         MEXIT
.S910    AIF   (K'&L GT 1).S090
         IHBERMAC 183,&L,L
.S920    LTORG
         MEND
         EJECT
DATECHNG ANFANG R,BR=(R12),RENT=YES
         EJECT
*        **************************************
*        *                                    *
*        *     PARAMETER-SICHERUNG UND        *
*        *     SPEICHERPLATZ-RESERVIERUNG     *
*        *                                    *
*        **************************************
         LR    R2,R0                          PARAMETER SICHERN
         LR    R3,R1                          PARAMETER SICHERN
         SPACE
         GETMAIN R,LV=WORKLEN,SP=125
         SPACE
         LR    R11,R1                         ADRESSE WORKAREA LADEN
         USING WORKAREA,R11                   ADRESSIERBAR MACHEN
         SPACE
         MVC   MONTAB,DAYS
         SPACE
         LA    R4,ABSCHLUS
         EJECT
*        **************************************
*        *                                    *
*        *     PARAMETER-PRUEFUNG UND         *
*        *     ENTSPRECHENDE VERZWEIGUNG      *
*        *                                    *
*        **************************************
         SPACE
         ST    R2,PARAM1                      REG.-INHALT SPEICHERN
         ST    R3,PARAM2                      REG.-INHALT SPEICHERN
         MVC   PARAM1C+2(2),PARAM1+2
         MVC   PARAM1D+2(2),PARAM1
         XR    R5,R5                   CLEAR WORK REGISTER
         LH    R5,PARAM1
         CVD   R5,DOWO
         ZAP   DIFFDATE,DOWO+6(2)
         L     R2,PARAM1C
         C     R2,=F'4'                       PARAMETER = 4 ?
         BE    DATE1                          JA ===> DATE1
         C     R2,=F'8'                       PARAMETER = 8 ?
         BE    DATE2                          JA ===> DATE2
         C     R2,=F'12'                      PARAMETER = 12 ?
         BE    DATE3                          JA ===> DATE3
         C     R2,=F'16'                      PARAMETER = 16 ?
         BE    DATE4                          JA ===> DATE4
         C     R2,=F'20'                      PARAMETER = 20 ?
         BE    DATE5                          JA ===> DATE5
         C     R2,=F'24'                      PARAMETER = 24 ?
         BE    DATE6                          JA ===> DATE6
         C     R2,=F'28'                      PARAMETER = 28 ?
         BE    DATE7                          JA ===> DATE7
         C     R2,=F'32'                      PARAMETER = 32 ?
         BE    DATE8                          JA ===> DATE8
         C     R2,=F'36'                      PARAMETER = 36 ?
         BE    TIME1                          JA ===> TIME1
         C     R2,=F'40'                      PARAMETER = 40 ?
         BE    TIME2                          JA ===> TIME2
         C     R2,=F'44'                      PARAMETER = 44 ?
         BE    WOTAG1                         JA ===> WOTAG1
         C     R2,=F'48'                      PARAMETER = 48 ?
         BE    WOTAG2                         JA ===> WOTAG2
         C     R2,=F'52'                      PARAMETER = 52 ?
         BE    WOTAG3                         JA ===> WOTAG3
         B     PARAMERR                       SONST PARAMETER-FEHLER
         EJECT
*        **************************************
*        *                                    *
*        *         ==> D A T E  1 <==         *
*        *                                    *
*        *     DATUMS-KONVERTIERUNG           *
*        *     TTMMJJ ===> JJTTT              *
*        *                                    *
*        **************************************
         SPACE
DATE1    EQU   *
         UNPK  CHECKFLD,PARAM2
         OI    CHECKFLD+6,X'F0'
         PACK  PDATE,CHECKFLD+1(2)
         CP    PDATE,=P'31'
         BH    PARAMERR
         PACK  PMONTH,CHECKFLD+3(2)
         CP    PMONTH,=P'12'
         BH    PARAMERR
         CLC   CHECKFLD+5(2),=C'00'
         BE    NSCHALT1
         PACK  RFELD,CHECKFLD+5(2)
         DP    RFELD,=P'4'
         CP    RFELD+2(1),=P'0'
         BNE   NSCHALT3
         MVI   MONTAB+3,X'9C'
         B     NSCHALT1
         SPACE
NSCHALT3 EQU   *
         MVI   MONTAB+3,X'8C'
         SPACE
NSCHALT1 EQU   *
         PACK  PMONAT,=C'0'
         PACK  PTAG,=C'0'
         LA    R6,MONTAB
         SPACE
NEWMONTH EQU   *
         AP    PMONAT,=P'1'
         CP    PMONAT,PMONTH
         BE    MONTHFND
         AP    PTAG,0(2,R6)
         LA    R6,2(R6)
         B     NEWMONTH
         SPACE
MONTHFND EQU   *
         AP    PTAG,PDATE
         MVC   OUTFIELD(2),CHECKFLD+5
         UNPK  OUTFIELD+2(3),PTAG
         OI    OUTFIELD+4,X'F0'
         PACK  PARAM2,OUTFIELD
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *         ==> D A T E  2 <==         *
*        *                                    *
*        *     DATUMS-KONVERTIERUNG           *
*        *     JJTTT ===> TTMMJJ              *
*        *                                    *
*        **************************************
         SPACE
DATE2    EQU   *
         UNPK  CHECKFLD,PARAM2
         OI    CHECKFLD+6,X'F0'
         CLC   CHECKFLD(2),=C'00'
         BNE   PARAMERR
         PACK  PJAHR,CHECKFLD+2(2)
         PACK  PTAG,CHECKFLD+4(3)
         CP    PJAHR,=P'00'
         BE    NSCHALT2
         ZAP   RFELD,PJAHR
         DP    RFELD,=P'4'
         CP    RFELD+2(1),=P'0'
         BNE   NSCHALT2
         MVI   MONTAB+3,X'9C'
         CP    PTAG,=P'366'
         BH    PARAMERR
         B     NSCHALT
         SPACE
NSCHALT2 EQU   *
         CP    PTAG,=P'365'
         BH    PARAMERR
         MVI   MONTAB+3,X'8C'
         SPACE
NSCHALT  EQU   *
         PACK  PMONAT,=C'0'
         PACK  PDATE,=C'0'
         LA    R6,MONTAB
         ZAP   PDATE,PTAG
COMP     EQU   *
         CP    PDATE,0(2,R6)
         BNH   COMPEND
         SP    PDATE,0(2,R6)
         AP    PMONAT,=P'1'
         LA    R6,2(R6)
         B     COMP
         SPACE
COMPEND  EQU   *
         AP    PMONAT,=P'1'
         UNPK  OUTFLD(2),PDATE
         OI    OUTFLD+1,X'F0'
         UNPK  OUTFLD+2(2),PMONAT
         OI    OUTFLD+3,X'F0'
         MVC   OUTFLD+4(2),CHECKFLD+2
         PACK  PARAM2,OUTFLD
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *         ==> D A T E  3 <==         *
*        *                                    *
*        *     ERMITTLUNG AKTUELLES DATUM     *
*        *     IN DER FORM JJTTT              *
*        *                                    *
*        **************************************
         SPACE
DATE3    EQU   *
         SPACE
         TIME
         SPACE
         ST    R1,PARAM2
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *         ==> D A T E  4 <==         *
*        *                                    *
*        *     ERMITTLUNG AKTUELLES DATUM     *
*        *     IN DER FORM TTMMJJ             *
*        *                                    *
*        **************************************
         SPACE
DATE4    EQU   *
         SPACE
         TIME
         SPACE
         ST    R1,PARAM2
         B     DATE2
         EJECT
*        **************************************
*        *                                    *
*        *         ==> D A T E  5 <==         *
*        *                                    *
*        *     DATUM IN DER FORM JJTTT UM     *
*        *     VORG. WERT VERRINGERN          *
*        *                                    *
*        **************************************
         SPACE
DATE5    EQU   *
         UNPK  CHECKFLD,PARAM2                PARAMETER ENTPACKEN
         OI    CHECKFLD+6,X'F0'               VORZEICHEN RICHTEN
         CLC   CHECKFLD(2),=C'00'             PARAMETERLAENGE OK ?
         BNE   PARAMERR                       NEIN ===> FEHLER
         SPACE
         PACK  PJAHR,CHECKFLD+2(2)            JAHR PACKEN
         PACK  PTAG,CHECKFLD+4(3)             TAG PACKEN
DATECMP5 EQU   *
         CP    PTAG,DIFFDATE                  DIFF > TAG ?
         BH    CALC5                          NEIN ===> RECHNEN
         ZAP   RFELD,PJAHR                    JA ==> SCHALTJAHR ?
         DP    RFELD,=P'4'
         CP    RFELD+2(1),=P'0'
         BNE   NSCHALT5                       NEIN ===> 365 ADDIEREN
         SPACE
         AP    PTAG,=P'366'                   JA ===> 366 ADDIEREN
         SP    PJAHR,=P'1'
         B     DATECMP5                       NEUER VERGLEICH
NSCHALT5 EQU   *
         AP    PTAG,=P'365'
         SP    PJAHR,=P'1'
         B     DATECMP5
CALC5    EQU   *
         SP    PTAG,DIFFDATE                  DIFFERENZ ABZIEHEN
DATE5END EQU   *
         ZAP   DOWO,PJAHR                     DATUM WIEDER
         MP    DOWO,=P'1000'                    ZUSAMMEN-
         AP    DOWO,PTAG                            SETZEN UND IN
         ZAP   PARAM2,DOWO                            PARAMETERFELD
         SPACE
         BR    R4                             ZURUECK / ENDE
         EJECT
*        **************************************
*        *                                    *
*        *         ==> D A T E  6 <==         *
*        *                                    *
*        *     DATUM IN DER FORM JJTTT UM     *
*        *     VORG. WERT VERGROESSERN        *
*        *                                    *
*        **************************************
         SPACE
DATE6    EQU   *
         UNPK  CHECKFLD,PARAM2
         OI    CHECKFLD+6,X'F0'
         SPACE
         PACK  PJAHR,CHECKFLD+2(2)
         PACK  PTAG,CHECKFLD+4(3)
         AP    PTAG,DIFFDATE
DATECMP6 EQU   *
         CP    PTAG,=P'365'
         BNH   DATE6END
         ZAP   RFELD,PJAHR
         DP    RFELD,=P'4'
         CP    RFELD+2(1),=P'0'
         BNE   NSCHALT5
         SPACE
         SP    PTAG,=P'366'
         AP    PJAHR,=P'1'
         B     DATECMP6
NSCHALT6 EQU   *
         SP    PTAG,=P'365'
         AP    PJAHR,=P'1'
         B     DATECMP6
         SPACE
DATE6END EQU   *
         ZAP   DOWO,PJAHR
         MP    DOWO,=P'1000'
         AP    DOWO,PTAG
         ZAP   PARAM2,DOWO
         SPACE
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *         ==> D A T E  7 <==         *
*        *                                    *
*        *     DATUM IN DER FORM TTMMJJ UM    *
*        *     VORG. WERT VERRINGERN          *
*        *                                    *
*        **************************************
         SPACE
DATE7    EQU   *
         LA    R4,*+8
         B     DATE1                          DATUM UMRECHNEN
         SPACE
         LA    R4,*+8
         B     DATE5                          VERRINGERN
         SPACE
         LA    R4,ABSCHLUS
         B     DATE2                          DATUM ZURUECKVERWANDELN
         EJECT
*        **************************************
*        *                                    *
*        *         ==> D A T E  8 <==         *
*        *                                    *
*        *     DATUM IN DER FORM TTMMJJ UM    *
*        *     VORG. WERT VERGROESSERN        *
*        *                                    *
*        **************************************
         SPACE
DATE8    EQU   *
         LA    R4,*+8
         B     DATE1                          DATUM UMRECHNEN
         SPACE
         LA    R4,*+8
         B     DATE6                          VERRINGERN
         SPACE
         LA    R4,ABSCHLUS
         B     DATE2                          DATUM ZURUECKVERWANDELN
         EJECT
*        **************************************
*        *                                    *
*        *         ==> T I M E  1 <==         *
*        *                                    *
*        *     ZEIT IN BIN/HUNDERTSTEL IN     *
*        *     FORM HH.MM.SS UMWANDELN        *
*        *                                    *
*        **************************************
         SPACE
TIME1    EQU   *
         LR    R6,R3                   MOVE PARM TO WORK REGISTER
         SRDL  R6,32                   SHIFT FOR DIVIDE
         D     R6,=F'100'              SKIP 1/100 OF SEC
         XR    R6,R6                   CLEAR WORK REGISTER
         D     R6,=F'60'               DIVIDE
         CVD   R6,DOWO                 CONVERT NUMBER OF SEC
         ZAP   PSEK,DOWO               SAVE
         XR    R6,R6                   CLEAR WORK REGISTER
         D     R6,=F'60'               DIVIDE
         CVD   R6,DOWO                 CONVERT NUMBER OF MINUTES
         ZAP   PMIN,DOWO               SAVE
         CVD   R7,DOWO                 CONVERT HOURS
         ZAP   PSTD,DOWO               SAVE
         ZAP   DOWO,PSTD               HOURS INTO DOWO
         MP    DOWO,=P'10000'          ADJUST
         ZAP   DOWO2,PMIN              MINUTES INTO DOWO2
         MP    DOWO2,=P'100'           ADJUST
         AP    DOWO,DOWO2              AND ADD TO HOURS
         AP    DOWO,PSEK               ADD SECONDS
         ZAP   PARAM2,DOWO             LOAD RESULT
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *         ==> T I M E  2 <==         *
*        *                                    *
*        *     ZEIT VON HH.MM.SS IN BINAER    *
*        *     BIS 1/100 SEKUNDE UMWANDELN    *
*        *                                    *
*        **************************************
         SPACE
TIME2    EQU   *
         ST    R3,DOWO+4               MOVE PARM INTO DOWO
         XR    R5,R5                   CLEAR WORK REGISTER
         XR    R6,R6                   CLEAR WORK REGISTER
         ICM   R6,B'1100',DOWO+4       GET HH
         SLL   R6,4
         SRL   R6,4
         STCM  R6,B'1100',DOWO2+6      STORE HOURS
         OI    DOWO2+7,X'0F'           INSERT SIGN
         ZAP   PSTD,DOWO2              SAVE HOURS
         ICM   R6,B'1100',DOWO+5       GET MM
         SLL   R6,4                    SHIFT FOR MULTIPLY
         SRL   R6,4                    SHIFT FOR MULTIPLY
         STCM  R6,B'1100',DOWO2+6      STORE MINUTES
         OI    DOWO2+7,X'0F'           INSERT SIGN
         ZAP   PMIN,DOWO2              SAVE MINUTES
         ICM   R6,B'1100',DOWO+6       GET SECONDS
         SLL   R6,4                    SHIFT FOR MULTIPLY
         SRL   R6,4                    SHIFT FOR MULTIPLY
         STCM  R6,B'1100',DOWO2+6      STORE SECONDS
         OI    DOWO2+7,X'0F'           INSERT SIGN
         ZAP   PSEK,DOWO2              SAVE SECONDS
         SPACE
         XR    R6,R6                   CLEAR WORK REGISTER
         ZAP   DOWO,PSTD
         CVB   R7,DOWO
         M     R6,=F'360000'           GET 1/100 SECONDS
         LR    R8,R7
         ZAP   DOWO,PMIN
         CVB   R7,DOWO
         XR    R6,R6                   SHIFT FOR MULTIPLY
         M     R6,=F'6000'             GET 1/100 SECONDS
         AR    R8,R7
         ZAP   DOWO,PSEK
         CVB   R7,DOWO
         XR    R6,R6
         M     R6,=F'100'              GET 1/100 SECONDS
         AR    R8,R7
         ST    R8,DOWO+4
         MVC   PARAM2,DOWO+4
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *         ==> W O T A G 1 <==        *
*        *                                    *
*        *     AKTUELLEN WOCHENTAG ERMITTELN  *
*        *                                    *
*        **************************************
         SPACE
WOTAG1   EQU   *
         LA    R4,*+8
         B     DATE3                          AKTUELLES DATUM ERMITTELN
         SPACE
         LA    R4,*+8
         B     WOTAG                          WOCHENTAG ERMITTELN
         SPACE
         LA    R4,ABSCHLUS
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *         ==> W O T A G 2 <==        *
*        *                                    *
*        *     WOCHENTAG AUS DATUM ERMITTELN  *
*        *                                    *
*        **************************************
         SPACE
WOTAG2   EQU   *
         LA    R4,*+8
         B     WOTAG                          WOCHENTAG ERMITTELN
         SPACE
         LA    R4,ABSCHLUS
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *         ==> W O T A G 3 <==        *
*        *                                    *
*        *     WOCHENTAG AUS DATUM ERMITTELN  *
*        *                                    *
*        **************************************
         SPACE
WOTAG3   EQU   *
         LA    R4,*+8
         B     DATE1                          DATUM UMRECHNEN
         SPACE
         LA    R4,*+8
         B     WOTAG                          WOCHENTAG ERMITTELN
         SPACE
         LA    R4,ABSCHLUS
         BR    R4
         EJECT
*        **************************************
*        *                                    *
*        *     ALLGEMEINE WOCHENTAGS ROUTINE  *
*        *                                    *
*        **************************************
         SPACE
WOTAG    DS    0H
         ZAP   DOWO2,=P'0'        CLEAR WORK FIELD
         SPACE
         UNPK  CHECKFLD,PARAM2    UNPK
         OI    CHECKFLD+6,X'F0'   CORRECT SIGN
         SPACE
WOTAG01  DS    0H
         PACK  DOWO,CHECKFLD+2(2) PACK YEAR
         CVB   R5,DOWO            CONVERT TO BINARY
         SH    R5,=H'75'          MINUS START YEAR (1975)
         BM    WOTAG02            IF LOWER 1 ==> SKIP YEAR TABLE
         MH    R5,=H'3'           DISPLACEMENT IN YEAR TABLE
         LA    R6,TABJAHR+0(R5)   POINT TO TAB VALUE
         AP    DOWO2,0(3,R6)      ADD DAYS SINCE START YEAR
         SPACE
WOTAG02  DS    0H
         PACK  PTAG,CHECKFLD+4(3) PACK DAYS
         AP    DOWO2,PTAG         ADD TO DAYS SINCE START YEAR
         DP    DOWO2,=P'7'        DIVEDE BY NO OF DAYS PER WEEK
         ZAP   DOWO,DOWO2+7(1)    GET REMAINDER
         CVB   R5,DOWO            INTO WORK REGISTER
         SLL   R5,1               AND BUILD TABLE DISPLACEMENT
         SPACE
WOTAG03  DS    0H
         LA    R6,TABTAG+0(R5)   POINT TO TABLE ELEMENT
         XC    PARAM2,PARAM2      CLEAR OUT FIELD
         MVC   PARAM2+2(2),0(R6)
         SPACE
         BR    R4                RETURN
         EJECT
*        **************************************
*        *                                    *
*        *     PROGRAMM-ABSCHLUSS UND         *
*        *     FEHLER-ROUTINE                 *
*        *                                    *
*        **************************************
         SPACE
ABSCHLUS EQU   *
         L     R3,PARAM2
         SPACE
         FREEMAIN R,SP=125
         SPACE
         LR    R1,R3
         SPACE
END      EQU   *
         ENDE  ,R1,RC=(R15),RENT=YES
         SPACE
PARAMERR EQU   *
         LA    R15,12
         B     END
         EJECT
*        **************************************
*        *                                    *
*        *     SPEICHER-BEREICHE UND DSECT    *
*        *                                    *
*        **************************************
         SPACE
         LTORG
         SPACE
DAYS     DC    X'031C028C031C030C031C030C031C031C030C031C030C031C'
         SPACE 2
TABJAHR  DC    PL3'365'     1975
         DC    PL3'730'     1976
         DC    PL3'1096'    1977
         DC    PL3'1461'    1978
         DC    PL3'1826'    1979
         DC    PL3'2191'    1980
         DC    PL3'2557'    1981
         DC    PL3'2922'    1982
         DC    PL3'3287'    1983
         DC    PL3'3652'    1984
         DC    PL3'4018'    1985
         DC    PL3'4383'    1986
         DC    PL3'4748'    1987
         DC    PL3'5113'    1988
         DC    PL3'5479'    1989
         DC    PL3'5844'    1990
         DC    PL3'6209'    1991
         DC    PL3'6574'    1992
         DC    PL3'6940'    1993
         DC    PL3'7305'    1994
         DC    PL3'7670'    1995
         DC    PL3'8035'    1996
         DC    PL3'8401'    1997
         DC    PL3'8766'    1998
         DC    PL3'9131'    1999
         SPACE 2
TABTAG   DS    0H
         DC    C'MO'        MONTAG
         DC    C'DI'        DIENSTAG
         DC    C'MI'        MITTWOCH
         DC    C'DO'        DONNERSTAG
         DC    C'FR'        FREITAG
         DC    C'SA'        SAMSTAG
         DC    C'SO'        SONNTAG
         EJECT
WORKAREA DSECT
WORKANF  EQU   *
         SPACE
MONTAB   DS    0CL24
JAN      DS    PL2
FEB      DS    PL2
MAE      DS    PL2
APR      DS    PL2
MAI      DS    PL2
JUN      DS    PL2
JUL      DS    PL2
AUG      DS    PL2
SEP      DS    PL2
OKT      DS    PL2
NOV      DS    PL2
DEZ      DS    PL2
         SPACE
PARAM1   DS    F
PARAM2   DS    F
PARAM1D  DS    F
PARAM1C  DS    F
DOWO     DS    D
DOWO2    DS    D
PSTD     DS    PL2
PMIN     DS    PL2
PSEK     DS    PL2
PMONAT   DS    PL2
PMONTH   DS    PL2
PTAG     DS    PL2
PDATE    DS    PL2
PJAHR    DS    PL2
DIFFDATE DS    PL2
RFELD    DS    PL3
CHECKFLD DS    CL7
OUTFIELD DS    CL5
OUTFLD   DS    CL6
WORKLEN  EQU   *-WORKANF
         SPACE
         END   DATECHNG
