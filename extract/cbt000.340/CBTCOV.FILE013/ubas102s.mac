TIOTSCAN CSECT
*
*        AUTHOR: H. FRANZ
*
*              THIS PROGRAM EXPECTS IN REG 1 THE ADDRESS OF
*              AN 8-BYTE DDNAME AND RETURNS IN REG 15
*              THE ADDRESS OF THE ASSOCIATED TIOT-ENTRY OR
*              ZERO IF NO TIOT-ENTRY WAS FOUND.
*
*              ROUTINE IS REENTRABLE
*
         USING *,15
         STM   14,2,12(13)   SAVE REGS
         L     2,16          A(CVT)
         L     2,0(2)        A(CVTTCBP)
         L     2,4(2)        A(CURRTCB)
         L     2,12(2)       A(TIOT)
         LA    2,24(2)       FIRST TIOT ENTRY
         SR    0,0           CLEAR REG
CLC      CLC   4(8,2),0(1)   DDNAME EQUAL ?
         BE    FOUND         YES, RETURN
         IC    0,0(2)        GET ENTRY LENGTH
         LTR   0,0           LAST ONE ?
         BZ    NOTFOUND      YES, RETURN WITH CODE 0
         AR    2,0           NO, EXAMINE NEXT ONE
         B     CLC
NOTFOUND SR    2,2
FOUND    ST    2,16(13)      STORE CODE FOR REG 15
         LM    14,2,12(13)   RELOAD REGS, CHANGED IS REG 15 ONLY
         SPM   14            SET PROGRAM MASK
         BR    14            RETURN TO CALLER
         END
