         TITLE 'DRUCKEN INHALTSVERZEICHNIS FUER UNTERGLIEDERTE DATEIEN'
***********************************************************************
*                                                                     *
*  LISTPDS     DIESES PROGRAMM DRUCKT INHALTSVERZEICHNISSE            *
*              FUER UNTERGLIEDERTE DATEIEN                            *
*                                                                     *
*  AUTOR:      GUELLAND                                               *
*                                                                     *
*              VERSION 00 VOM 07. 09. 1970, AUFTRAG 0001.             *
*                                                                     *
*              FOLGENDE DD-KARTEN SIND ERFORDERLICH:                  *
*                                                                     *
*              //SYSPRINT DD SYSOUT=...                               *
*              //SYSLIB   DD DSN=NAME DER DATEI,DISP=SHR              *
*              //SYSABEND DD SYSOUT=...                               *
*                                                                     *
*              DIE REGISTER WERDEN FUER FOLGENDES BENUTZT:            *
*                                                                     *
*              R0  = ARBEITSREGISTER                                  *
*              R1  = ARBEITSREGISTER                                  *
*              R2  = ARBEITSREGISTER                                  *
*              R3  = ANZAHL ALIASNAMEN IN DER JEWEILIGEN DATEI        *
*              R4  = ARBEITSREGISTER                                  *
*              R5  = ANZAHL ABGEARBEITETE BYTES DES DIRECTORY BLOCKS  *
*              R6  = ADRESSE DIRECTORY ENTRY                          *
*              R7  = ARBEITSREGISTER                                  *
*              R8  = ARBEITSREGISTER                                  *
*              R9 = RUECKSPRUNGADRESSE BEI BRANCHES IN UNTERPROGR.    *
*              R10 = BASISREGISTER                                    *
*              R11 = BASISREGISTER                                    *
*              R12 = BASISREGISTER                                    *
*              R13 = ADRESSE SAVEAREA                                 *
*              R14 = FREI                                             *
*              R15 = FREI                                             *
*                                                                     *
***********************************************************************
         EJECT
         SECTION LISTPDS,(R10,R11,R12),HP
HP       CALL  DAYTIME,(DATUM)
HP000    BAL   R9,UPTIOT           EINSETZEN DD-NAMEN
         TM    SCHENDE,X'02'       TEST AUF ENDE
         BO    HP205
         OI    SCHVORL,X'01'
HP005    RDJFCB MF=(E,RDJFCB1) LESEN JOBFILE CONTROLBLOCK SYSLIB
         OI    JFCBTSDM,X'08'  DSCB NICHT ZURUECKSCHREIBEN
         OPEN  MF=(E,RDJFCB1),TYPE=J OPEN SYSLIB
         OPEN  (SYSPRINT,(OUTPUT))   OPEN SYSPRINT
         USING INHVERZ,R6
         SR    R0,R0
         LA    R2,TALIAS
         SR    R3,R3
HP010    BAL   R9,LESEN
         TM    SCHENDE,X'01'
         BO    HP020
         TM    IINDIC,X'80'        TEST AUF ALIAS
         BNO   HP010
         LA    R3,1(R3)
         CH    R3,TABANZ           TEST TABELLE VOLL
         BH    HP015
         MVC   0(8,R2),INAME       ALIASNAME
         MVC   8(3,R2),ITTRP       SATZ- UND SPURADRESSE
         MVI   11(R2),C'*'
         MVC   12(7,R2),11(R2)
         LA    R2,19(R2)
         B     HP010
HP015    MVI   ZEILE,C' '          LOESCHEN ZEILE
         MVC   ZEILE+1(L'ZEILE-1),ZEILE
         MVI   ZVORSCH,C'1'
         MVC   ZEILE+1(31),=C'TABELLE DER ALIASNAMEN ZU KLEIN'
         MVC   DBER,ZEILE
         BAL   R9,DRUCKEN
         MVC   COND,=H'16'
         B     HP205
HP020    CLOSE (SYSLIB)
         OPEN  MF=(E,RDJFCB1),TYPE=J
         OI    SCH1MAL,X'01'
         SR    R0,R0
HP025    SR    R4,R4
         BAL   R9,LESEN
         TM    SCHENDE,X'01'
         BO    HP040
         TM    IINDIC,X'80'        TEST AUF ALIAS
         BO    HP025
         LA    R2,TALIAS+8
HP030    CLC   0(3,R2),ITTRP
         BE    HP035
HP032    LA    R4,1(R4)
         CR    R4,R3               TEST TABELLENENDE
         BH    HP025
         LA    R2,19(R2)
         B     HP030
HP035    MVC   3(8,R2),INAME       UEBERTRAGEN MEMBERNAMEN
         B     HP032
HP040    CLOSE (SYSLIB)
         OPEN  MF=(E,RDJFCB1),TYPE=J
         OI    SCH1MAL,X'01'
         MVC   KVERSION,VERSION    AUFBEREITEN KOPF
         MVC   KDATUM,XJAHR
         MVC   KZEIT,XSTUNDE
         MVC   KTAG,XTAG
         MVC   KMONAT,XMONAT
         MVC   KJAHR,XJAHR
         MVC   KVOL,JFCBVOLS
         MVC   KDSN,JFCBDSNM
         NI    SCHVORL,X'00'
         SR    R0,R0
HP045    BAL   R8,KOPF             DRUCKEN KOPF
HP050    BAL   R9,LESEN            LESEN INHALTSVERZEICHNIS
         TM    SCHENDE,X'01'       TEST AUF ENDE
         BO    HP190
         MVI   ZEILE,C' '          LOESCHEN ZEILE
         MVC   ZEILE+1(L'ZEILE-1),ZEILE
         NI    SCH,X'00'
         MVC   ZMEMBER,INAME
         TM    IINDIC,X'80'        TEST AUF ALIAS
         BNO   HP065
         OI    SCH,X'01'
         LA    R2,TALIAS           SUCHEN MEMBERNAMEN AUS TABELLE
         SR    R4,R4
HP055    CLC   0(8,R2),INAME
         BNE   HP060
         MVC   ZALIAS,11(R2)
         B     HP065
HP060    LA    R4,1(R4)
         CR    R4,R3               TEST AUF TABELLENENDE
         BH    HP065
         LA    R2,19(R2)
         B     HP055
HP065    AP    ANZMEM,=P'1'
         SR    R7,R7
         MVC   ANZBLI,ITTRP        UMWANDELN SPURNR IN HEX
         LH    R9,ANZBLI
         SLL   R9,16
HP070    SR    R8,R8
         SLDL  R8,4
         IC    R8,HEX(R8)
         STC   R8,ZSPUR(R7)
         LA    R7,1(R7)
         CH    R7,=H'4'
         BNE   HP070
         SR    R7,R7
         LH    R9,ANZBLI+2         UMWANDELN SATZNR. IN HEX
         SLL   R9,16
HP075    SR    R8,R8
         SLDL  R8,4
         IC    R8,HEX(R8)
         STC   R8,ZSATZ(R7)
         LA    R7,1(R7)
         CH    R7,=H'2'
         BNE   HP075
         TM    IINDIC,X'60'        TEST OB SOURCELIB
         BZ    HP162
         MVI   ANZBLBI,X'00'
         MVC   ANZBLBI+1(3),ISPEICH AUSRECHNEN SPEICHERBEDARF
         L     R9,ANZBLBI
         AH    R9,=H'1023'
         SRL   R9,10
         CVD   R9,ZWI
         UNPK  ZREGION,ZWI+5(3)
         OI    ZREGION+3,X'F0'
         MVI   ZK,C'K'
         MVC   ANZBLI(3),IEPADR    UMWANDELN ENTRY POINT IN HEX
         L     R9,ANZBLI
         SR    R7,R7
HP080    SR    R8,R8
         SLDL  R8,4
         IC    R8,HEX(R8)
         STC   R8,ZENTRY(R7)
         LA    R7,1(R7)
         CH    R7,=H'6'
         BNE   HP080
         TM    IATTRIB1,X'80'      TEST REENTERABLE
         BZ    HP085
         MVI   ZRENT,C'X'
         OI    SCH,X'02'
HP085    TM    IATTRIB1,X'40'      TEST REUSABLE
         BZ    HP090
         MVI   ZREUS,C'X'
         OI    SCH,X'02'
HP090    TM    IATTRIB1,X'20'      TEST OVERLAY
         BZ    HP095
         MVI   ZOVLY,C'X'
HP095    TM    IATTRIB1,X'10'      TEST TESTRAN
         BZ    HP100
         MVI   ZTEST,C'X'
HP100    TM    IATTRIB1,X'08'      TEST ONLY LOADABLE
         BZ    HP105
         MVI   ZLOAD,C'X'
HP105    TM    IATTRIB1,X'04'      TEST SCATTER FORMAT
         BZ    HP110
         MVI   ZSCAT,C'X'
         OI    SCH,X'04'
HP110    TM    IATTRIB1,X'02'      TEST EXECUTABLE
         BZ    HP115
         MVI   ZEXEC+1,C'X'
         B     HP120
HP115    MVC   ZEXEC,=C'*NO*'
HP120    TM    IATTRIB2,X'80'      TEST LINKAGE EDITOR
         BZ    HP125
         MVC   ZLINK,=C'F-ED'
         B     HP130
HP125    MVC   ZLINK,=C'ALL '
HP130    TM    IATTRIB2,X'08'      TEST NO LINKAGE EDITOR
         BZ    HP135
         MVC   ZLINK,=C' NO '
HP135    TM    IATTRIB2,X'02'      TEST F-LEVEL LINKAGE EDITOR
         BZ    HP140
         MVI   ZCRF,C'X'
HP140    TM    IATTRIB2,X'01'      TEST REFRESHABLE
         BZ    HP145
         MVI   ZREFR,C'X'
HP145    TM    SCH,X'07'           TEST AUF SCATTER, ALIAS U. RENT REUS
         BNO   HP150
         LH    R7,=H'52'
         B     HP165
HP150    TM    SCH,X'03'           TEST AUF ALIAS UND RENT. ODER REUS
         BNO   HP155
         LH    R7,=H'44'
         B     HP165
HP155    TM    SCH,X'04'           TEST AUF SCATTER
         BNO   HP160
         LH    R7,=H'42'
         B     HP165
HP160    LH    R7,=H'34'
         B     HP165
HP162    LH    R7,=H'12'
HP165    LH    R0,ZWLAE
         CR    R0,R7               TEST OB SSI VORHANDEN
         BNH   HP185
HP167    AR    R7,R6
         SR    R4,R4               UMWANDELN SSI IN HEX
         SR    R2,R2
         MVC   ANZBLI,0(R7)
         L     R9,ANZBLI
HP170    SR    R8,R8
         SLDL  R8,4
         IC    R8,HEX(R8)
         STC   R8,ZLEVEL(R4)
         LA    R2,1(R2)
         CH    R2,=H'2'
         BE    HP175
         CH    R2,=H'4'
         BE    HP175
         CH    R2,=H'8'
         BE    HP185
         B     HP180
HP175    LA    R4,1(R4)
HP180    LA    R4,1(R4)
         B     HP170
HP1801   CLI   ZMEMBER,C'C'
         BE    HP1803
         CLI   ZMEMBER,C'E'
         BE    HP1803
         CLC   ZMEMBER(3),=C'KPZ'
         BE    HP1803
         CLI   ZMEMBER,C'F'
         BE    HP1804
         CLI   ZMEMBER,C'P'
         BE    HP1805
         AP    XXZAE,=P'1'
         B     HP186
HP1802   CLI   ZMEMBER,C'C'
         BE    HP1806
         CLI   ZMEMBER,C'E'
         BE    HP1806
         CLC   ZMEMBER(3),=C'KPZ'
         BE    HP1806
         CLI   ZMEMBER,C'F'
         BE    HP1807
         CLI   ZMEMBER,C'P'
         BE    HP1808
         AP    XXZAE,=P'1'
         B     HP186
HP1803   AP    CAZAE,=P'1'
         B     HP186
HP1804   AP    FLZAE,=P'1'
         B     HP186
HP1805   AP    POZAE,=P'1'
         B     HP186
HP1806   AP    CFZAE,=P'1'
         B     HP186
HP1807   AP    FGZAE,=P'1'
         B     HP186
HP1808   AP    PFZAE,=P'1'
         B     HP186
HP185    MVC   DBER,ZEILE
         CLC   ZLEVEL,=C'FA'
         BE    HP1801
         CLC   ZLEVEL,=C'FF'
         BE    HP1802
         AP    XXZAE,=P'1'
HP186    BAL   R9,DRUCKEN
         AP    ZEIZAE,=P'1'
         CP    ZEIZAHL,ZEIZAE      TEST OB SEITE VOLL
         BH    HP050               LESEN NAECHSTEN SATZ
         SP    ZEIZAE,ZEIZAE       LOESCHEN ZEILENZAEHLER
         B     HP045               DRUCKEN KOPF
HP190    MVI   DBER,C' '           LOESCHEN DBER
HP195    MVC   DBER+1(L'DBER-1),DBER
         CP    ZEIZAE,=P'33'
         BL    HP197
         BAL   R8,KOPF
HP197    BAL   R9,DRUCKEN          DRUCKEN LEERZEILE
         BAL   R9,DRUCKEN
         MVC   DBER+4(L'KONST1),KONST1  DRUCKEN ANZAHL DER MEMBER
         UNPK  DBER+30(4),ANZMEM+2(3)
         OI    DBER+33,X'F0'
         MVC   DBER+38(L'KONST101),KONST101
         UNPK  DBER+43(4),CFZAE+2(3)
         OI    DBER+46,X'F0'
         MVC   DBER+48(L'KONST102),KONST102
         UNPK  DBER+53(4),CAZAE+2(3)
         OI    DBER+56,X'F0'
         MVC   DBER+58(L'KONST103),KONST103
         UNPK  DBER+63(4),PFZAE+2(3)
         OI    DBER+66,X'F0'
         MVC   DBER+68(L'KONST104),KONST104
         UNPK  DBER+73(4),POZAE+2(3)
         OI    DBER+76,X'F0'
         MVC   DBER+78(L'KONST105),KONST105
         UNPK  DBER+83(4),FGZAE+2(3)
         OI    DBER+86,X'F0'
         MVC   DBER+88(L'KONST106),KONST106
         UNPK  DBER+93(4),FLZAE+2(3)
         OI    DBER+96,X'F0'
         MVC   DBER+98(L'KONST107),KONST107
         UNPK  DBER+106(4),XXZAE+2(3)
         OI    DBER+109,X'F0'
         BAL   R9,DRUCKEN
         MVI   DBER,C' '
         MVC   DBER+1(L'DBER-1),DBER
         BAL   R8,UPDSCB
HP200    EX    0,HP190             LOESCHEN DBER
         EX    0,HP195
         CLOSE (SYSLIB)
         B     HP000               NAECHSTER DD-NAME
HP205    EX    0,HP190
         EX    0,HP195
         MVI   DBER,C'1'
         BAL   R9,DRUCKEN
         MVC   DBER+4(L'KONST2),KONST2  DRUCKEN ENDENACHRICHT
         MVI   DBER,C'-'
         LH    R15,COND
         CVD   R15,ANZMEMB
         UNPK  DBER+39(4),ANZMEMB+5(3)
         OI    DBER+42,X'F0'
         BAL   R9,DRUCKEN
         EX    0,HP190             LOESCHEN DBER
         EX    0,HP195
         MVI   DBER,C'1'           VORSCHUB AUF NEUE SEITE
         BAL   R9,DRUCKEN
         CLOSE (SYSPRINT)
         LH    R15,COND
         L     R13,4(13)           SAVEAREA ADDR
         RETURN (14,12),RC=(15)
         EJECT
UPDSCB   MVC   DBER+4(L'KONST3),KONST3 AUFBER. ANZAHL DIRECTORY BLOCKS
         UNPK  DBER+40(4),ANZDIR
         OI    DBER+43,X'F0'
         SP    ANZDIR,ANZBEN       ERRECHNEN FREI DIRECTORY BLOCKS
         MVC   DBER+44(L'KONST4),KONST4
         UNPK  DBER+64(4),ANZDIR
         OI    DBER+67,X'F0'
         MVI   DBER,C'0'
         BAL   R9,DRUCKEN          DRUCKEN ANZAHL DIRECTORY BLOCKS
         EX    0,HP190             LOESCHEN DBER
         EX    0,HP195
         MVI   DBER,C'0'
         MVC   CDSN,JFCBDSNM       AUFBEREITEN CAMLIST FUER DSCB 1
         MVC   CVOL,JFCBVOLS
         OBTAIN CAMLST1            LESEN FORMAT 1 DSCB
         CH    R15,=H'0'           TEST OB RICHTIG GELESEN
         BNE   UPDSCB66
         MVC   ZWITTR+2(2),DS1LSTAR SPEICHERN LETZTES TTR
         MVC   ZWITTR(2),=X'0000'
         TM    DS1SCALO,X'C0'      TEST OB SECONDARY ALLOC. IN CYL.
         BO    UPDSCB10
         TM    DS1SCALO,X'80'      TEST OB SECONDARY ALLOC. IN TRACKS
         BO    UPDSCB05
         MVC   ZWISECND,=F'0'      LOESCHEN SECONDARY ALLOC.
         B     UPDSCB15
UPDSCB05 MVC   ZWISECND+1(3),DS1SCALO+1
         MVI   ZWISECND,X'00'
         B     UPDSCB15
UPDSCB10 MVC   ZWISECND+1(3),DS1SCALO+1  UMRECHNEN ZYLINDER IN TRACKS
         MVI   ZWISECND,X'00'
         L     R0,ZWISECND
         MH    R0,ANZTRACK
         ST    R0,ZWISECND
UPDSCB15 SR    R2,R2
         IC    R2,DS1NOEPV         ANZAHL EXTENTS
         SR    R7,R7
         LA    R4,DS1EXT1
UPDSCB20 MVC   ZWILAE,2(R4)        UMRECHNEN CCHH IN TRACKS (BEGINN)
         LH    R1,ZWILAE
         MH    R1,ANZTRACK
         MVC   ZWILAE,4(R4)
         AH    R1,ZWILAE
         MVC   ZWILAE,6(R4)        UMRECHNEN CCHH IN TRACKS (ENDE)
         LH    R0,ZWILAE
         MH    R0,ANZTRACK
         MVC   ZWILAE,8(R4)
         AH    R0,ZWILAE
         AH    R0,=H'1'
         SR    R0,R1
         MVC   DBER+4(L'KONST5),KONST5
         AP    ANZEX,=P'1'
         UNPK  DBER+10(2),ANZEX
         OI    DBER+11,X'F0'
         MVC   DBER+12(L'KONST6),KONST6
         CVD   R0,CONVERT
         UNPK  DBER+24(4),CONVERT+5(3)
         OI    DBER+27,X'F0'
         MVC   DBER+28(L'KONST7),KONST7
         A     R0,ZTRACKS
         ST    R0,ZTRACKS
         SH    R2,=H'1'            TEST AUF LETZTEN EXTENT
         CH    R2,=H'0'
         BE    UPDSCB33
         BAL   R9,DRUCKEN
         EX    0,HP190
         EX    0,HP195
         CP    ANZEX,=P'3'         TEST OB LETZTER EXTENT IM DSCB 1
         BE    UPDSCB23
         LA    R4,10(R4)
         B     UPDSCB20            NAECHSTER EXTENT
UPDSCB23 MVC   CDSN(L'DS1PTRDS),DS1PTRDS AUFBER. CAMLIST FUER DSCB 3
         OBTAIN CAMLST2            LESEN FORMAT 3 DSCB
         CH    R15,=H'0'           TEST OB RICHTIG GELESEN
         BNE   UPDSCB66
         LA    R4,DS3EXTNT
UPDSCB26 MVC   ZWILAE,2(R4)        UMRECHNEN CCHH IN TRACKS (BEGINN)
         LH    R1,ZWILAE
         MH    R1,ANZTRACK
         MVC   ZWILAE,4(R4)
         AH    R1,ZWILAE
         MVC   ZWILAE,6(R4)        UMRECHNEN CCHH IN TRACKS (ENDE)
         LH    R0,ZWILAE
         MH    R0,ANZTRACK
         MVC   ZWILAE,8(R4)
         AH    R0,ZWILAE
         AH    R0,=H'1'
         SR    R0,R1
         MVC   DBER+4(L'KONST5),KONST5
         AP    ANZEX,=P'1'
         UNPK  DBER+10(2),ANZEX
         OI    DBER+11,X'F0'
         MVC   DBER+12(L'KONST6),KONST6
         CVD   R0,CONVERT
         UNPK  DBER+24(4),CONVERT+5(3)
         OI    DBER+27,X'F0'
         MVC   DBER+28(L'KONST7),KONST7
         A     R0,ZTRACKS
         ST    R0,ZTRACKS
         SH    R2,=H'1'
         CH    R2,=H'0'            TEST AUF LETZTEN EXTENT
         BE    UPDSCB33
         BAL   R9,DRUCKEN
         EX    0,HP190
         EX    0,HP195
         CP    ANZEX,=P'7'
         BE    UPDSCB30
         LA    R4,10(R4)
         B     UPDSCB26
UPDSCB30 LA    R4,11(R4)
         B     UPDSCB26
UPDSCB33 L     R0,ZTRACKS          ERRECHNEN FREIE TRACKS IM
         S     R0,ZWITTR           LETZTEN EXTENT
         SH    R0,=H'1'
         MVC   DBER+35(L'KONST8),KONST8
         CVD   R0,CONVERT
         UNPK  DBER+55(4),CONVERT+5(3)
         OI    DBER+58,X'F0'
         MVC   DBER+59(L'KONST7),KONST7
         BAL   R9,DRUCKEN
         EX    0,HP190
         EX    0,HP195
         MVI   DBER,C'0'
         CP    ANZEX,=P'16'        TEST OB ALLE EXTENTS VORHANDEN
         BE    UPDSCB61
         CLC   ZWISECND,=X'00000000'
         BE    UPDSCB58
         SR    R2,R2
         MVI   CDSN,X'04'          AUFBEREITEN CAMLIST FUER DSCB 4
         MVC   CDSN+1(L'CDSN-1),CDSN
         OBTAIN CAMLST1            LESEN FORMAT 4 DSCB
         MVC   CDSN(4),DS4VTOCE+2    AUFBEREITEN CAMLIST FUER DSCB 5
         MVI   CDSN+4,X'02'
UPDSCB36 OBTAIN CAMLST2            LESEN FORMAT 5 DSCB
         LA    R4,DS5AVEXT
UPDSCB40 MVC   ZWI(2),2(R4)        ANZAHL FREIE ZYLINDER
         LH    R1,ZWI
         MH    R1,ANZTRACK
         MVI   ZWI,X'00'           ANZAHL FREIE SPUREN
         MVC   ZWI+1(1),4(R4)
         AH    R1,ZWI
UPDSCB43 C     R1,ZWISECND
         BL    UPDSCB46
         AP    ANZFREI,=P'1'
         A     R2,ZWISECND
         AP    ANZEX,=P'1'
         S     R1,ZWISECND
         CP    ANZEX,=P'16'        TEST OB 16 EXTENTS VOLL
         BNE   UPDSCB43
         B     UPDSCB56
UPDSCB46 CP    ANZDS5,=P'26'       TEST OB DSCB 5 ABGEARBEITET
         BE    UPDSCB53
         CP    ANZDS5,=P'8'
         BE    UPDSCB50
         AP    ANZDS5,=P'1'
         LA    R4,5(R4)
         B     UPDSCB40
UPDSCB50 LA    R4,6(R4)
         AP    ANZDS5,=P'1'
         B     UPDSCB40
UPDSCB53 CLC   DS5PTRDS,=X'0000000000' TEST OB WEITERER DSCB 5 VORH.
         BE    UPDSCB60
         MVC   CDSN(5),DS5PTRDS
         SP    ANZDS5,ANZDS5
         B     UPDSCB36            LESEN NAECHSTEN FORMAT 5 DSCB
UPDSCB56 MVC   DBER+4(L'KONST5),KONST5  DRUCKEN FREIE EXTENTS
         UNPK  DBER+10(2),ANZFREI
         OI    DBER+11,X'F0'
         MVC   DBER+12(L'KONST9),KONST9
         CVD   R2,CONVERT
         UNPK  DBER+24(4),CONVERT+5(3)
         OI    DBER+27,X'F0'
         MVC   DBER+28(L'KONST7),KONST7
         MVC   DBER+35(L'KONST10),KONST10
         MVI   DBER,C'0'
         BAL   R9,DRUCKEN
         EX    0,HP190
         EX    0,HP195
         B     UPDSCB63
UPDSCB58 MVC   DBER+4(L'KONST14),KONST14
         MVI   DBER,C'0'
         BAL   R9,DRUCKEN
         EX    0,HP190
         EX    0,HP195
         B     UPDSCB63
UPDSCB60 CP    ANZFREI,=P'0'       TEST OB NOCH FREIE EXTENTS VORHANDEN
         BNE   UPDSCB56
UPDSCB61 MVC   DBER+4(L'KONST11),KONST11
         MVI   DBER,C'0'
         BAL   R9,DRUCKEN
         EX    0,HP190
         EX    0,HP195
UPDSCB63 SP    ANZEX,ANZEX         LOESCHEN ZAEHLER
         SP    ANZFREI,ANZFREI
         SP    ANZDS5,ANZDS5
         SP    ANZDIR,ANZDIR
         SP    ANZBEN,ANZBEN
         MVC   ZWITTR,=F'0'
         MVC   ZWISECND,=F'0'
         MVC   ZTRACKS,=F'0'
         MVC   ZWILAE,=H'0'
         B     UPDSCBEX
UPDSCB66 MVC   DBER+4(L'KONST12),KONST12
         MVC   DBER+54(L'KONST13),KONST13
         CVD   R15,CONVERT
         UNPK  DBER+69(16),CONVERT
         OI    DBER+84,X'F0'
         BAL   R9,DRUCKEN
         CLOSE (SYSPRINT)
         ABEND 1000,DUMP
UPDSCBEX BR    R8
         SPACE 3
KOPF     MVC   DBER,KOPF1          KOPFZEILE 1
         BAL   R9,DRUCKEN
         MVC   DBER,KOPF1A
         BAL   R9,DRUCKEN
         MVC   DBER,KOPF2          KOPFZEILE 2
         BAL   R9,DRUCKEN
         MVC   DBER,KOPF3          KOPFZEILE 3
         BAL   R9,DRUCKEN
         MVI   DBER,C' '           LOESCHEN DBER
         MVC   DBER+1(L'DBER-1),DBER
         BAL   R9,DRUCKEN          DRUCKEN LEERZEILE
         SP    ZEIZAE,ZEIZAE
         BR    R8
         SPACE 3
DRUCKEN  PUT   SYSPRINT,DBER
         BR    R9
         SPACE 3
LESEN    TM    SCH1MAL,X'01'       TEST AUF ERSTES MAL
         BZ    LESEN005
         NI    SCH1MAL,X'00'
         NI    SCHENDE,X'00'
         SP    ANZDIR,ANZDIR
         B     LESEN010
LESEN005 AH    R5,=H'2'            TEST DIRECTORY-BLOCK ENDE
         CH    R5,PE
         BE    LESEN010
         SH    R5,=H'2'
         LA    R6,PE+2
         AR    R6,R5
         TM    0(R6),X'FF'
         BO    LESEN015            TEST ENDE DIRECTORY
         SR    R8,R8               ERRECHNEN LAENGE ENTRY
         IC    R8,IINDIC
         N     R8,=F'31'
         LA    R8,12(R8,R8)
         STH   R8,ZWLAE
         AR    R5,R8               ERRECHNEN GESAMTLAENGE
         B     LESENEND
LESEN010 SR    R5,R5               LOESCHEN GESAMTLAENGE
         READ  DECB1,SF,MF=E       LESEN SYSLIB
         CHECK DECB1               CHECK SYSLIB
         AP    ANZDIR,=P'1'
         TM    SCHENDE,X'01'
         BO    LESEN010
         B     LESEN005
LESEN015 OI    SCHENDE,X'01'
         TM    SCHVORL,X'01'
         BO    LESENEND
         MVC   ANZBEN,ANZDIR
         B     LESEN010
LESENEND BR    R9
         SPACE 3
UPTIOT   TM    SCHANF,X'01'        TEST 1. MAL
         BZ    UPTIOT05
         L     R1,16               LADEN ADR. CVT-POINTER
         L     R1,0(R1)            LADEN ADR. TCB-DOPPELWORT
         L     R1,4(R1)            LADEN TCB-ADRESSE
         L     R1,12(R1)           LADEN TIOT-ADRESSE
         LA    R1,24(R1)           LADEN ADR. 1. DD-ENTRY
         ST    R1,ZWITIOT          SPEICHERN ADR. 1. DD-ENTRY
         NI    SCHANF,X'00'        LOESCHEN SCHALTER
UPTIOT05 L     R1,ZWITIOT          LADEN ADR. NAECHSTER DD-ENTRY
UPTIOT07 TM    0(R1),X'FF'         TEST AUF ENDE DER TIOT
         BZ    UPTIOT30
         ZAP   ANZMEM,=P'0'        LOESCHEN FELDER FUER NAECHSTE LISTE
         ZAP   CFZAE,=P'0'
         ZAP   CAZAE,=P'0'
         ZAP   PFZAE,=P'0'
         ZAP   POZAE,=P'0'
         ZAP   FGZAE,=P'0'
         ZAP   FLZAE,=P'0'
         ZAP   XXZAE,=P'0'
         ZAP   ZEIZAE,=P'0'
         ZAP   ANZDIR,=P'0'
         ZAP   ANZBEN,=P'0'
         OI    SCH1MAL,X'01'       SETZEN SCHALTER 1. MAL
         LA    R7,TDDNAMEN
UPTIOT10 CLC   0(8,R7),4(R1)       TEST AUF DD-NAMEN DIE NICHT
         BE    UPTIOT25            VERARBEITET WERDEN SOLLEN
         LA    R7,8(R7)
         CL    R7,=A(TDDNEND)      TEST AUF TABELLENENDE
         BNE   UPTIOT10
         MVC   SYSLIB+40(8),4(R1)  UEBERTRAGEN DD-NAMEN IN DEN DCB
         L     R2,16(R1)           GET UCB-ADDRESS
         CLI   19(R2),X'01'        TEST 2311
         BE    UPTIOT11
         CLI   19(R2),X'08'        TEST2314
         BE    UPTIOT12
         CLI   19(R2),X'09'        TEST3330
         BE    UPTIOT13
         MVC   ANZTRACK,=H'1'
         B     UPTIOT16
UPTIOT11 MVC   ANZTRACK,=H'10'
         B     UPTIOT16
UPTIOT12 MVC   ANZTRACK,=H'20'
         B     UPTIOT16
UPTIOT13 MVC   ANZTRACK,=H'19'
UPTIOT16 MVC   ZWILAE+1(1),0(R1)   ADDIEREN LAENGE DD-ENTRY
UPTIOT17 AH    R1,ZWILAE
         ST    R1,ZWITIOT          SPEICHERN ADR. NAECHSTER DD-ENTRY
         B     UPTIOTEX
UPTIOT25 EX    0,UPTIOT16
         EX    0,UPTIOT17
         B     UPTIOT07
UPTIOT30 OI    SCHENDE,X'02'
UPTIOTEX BR    R9
         EJECT
SYSLIB   DCB   DSORG=PO,DEVD=DA,MACRF=(R,W),DDNAME=SYSLIB,             *
               EXLST=EXSYSLIB,RECFM=U,EODAD=LESENEND
EXSYSLIB DS    0F
         DC    X'87',AL3(JFCB)
RDJFCB1  RDJFCB (SYSLIB,(INPUT)),MF=L
READ1    READ  DECB1,SF,SYSLIB,PE,256,MF=L
         DS    0F
PE       DS    CL256
JFCB     DS    CL176
         ORG   JFCB
         COPY  JFCB
         SPACE 3
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,RECFM=FBA,LRECL=133,DSORG=PS
DBER     DS    CL133
         SPACE  3
CAMLST1  CAMLST SEARCH,CDSN,CVOL,WORKAREA
CAMLST2  CAMLST SEEK,CDSN,CVOL,WORKAREA
CDSN     DS    CL44
CVOL     DS    CL6
         DS    0D
DS1FMTID DS    C         FORMATNUMMER DES DSCB
DS1DSSN  DS    CL6       DATA SET SERIAL NUMMER
DS1VOLSQ DS    CL2
DS1CREDT DS    CL3       ERSTELLUNGSDATUM
DS1EXPDT DS    CL3       FREIGABEDATUM
DS1NOEPV DS    C         ANZAHL DER EXTENTS AUF DIESEM VOLUME
DS1NOBDB DS    C         ANZAHL BENUTZTE BYTES IM LETZTEN DIRECTORYBLOC
         DS    C         RESERVIERT
DS1SYSCD DS    CL13      SYSTEM-CODE
         DS    CL7       RESERVIERT
DS1DSORG DS    CL2       ORGANISATIONSFORM DES DATENBESTANDES
DS1RECFM DS    C         SATZART
DS1OPTCD DS    C         OPTION CODE
DS1BLKL  DS    CL2       BLOCKLAENGE
DS1LRECL DS    CL2       SATZLAENGE
DS1KEYL  DS    C         SCHLUESSELLAENGE
DS1RKP   DS    CL2       RELATIVE SCHLUESSEL POSITION
DS1DSIND DS    C         DATENBESTANDS INDIKATOREN
DS1SCALO DS    CL4       SEKUNDAERER PLATZBEDARF
DS1LSTAR DS    CL3       ADRESSE DES LETZTEN SATZES IM DATENBESTAND
DS1TRBAL DS    CL2
         DS    CL2       RESERVIERT
DS1EXT1  DS    CL10      BESCHREIBUNG DES ERSTEN  EXTENTS
DS1EXT2  DS    CL10      BESCHREIBUNG DES ZWEITEN EXTENTS
DS1EXT3  DS    CL10      BESCHREIBUNG DES DRITTEN EXTENTS
DS1PTRDS DS    CL5       POINTER ZUM FORMAT 2 BZW. FORMAT 3 DSCB
         ORG   DS1FMTID
         DS    CL4       SCHLUESSEL
DS3EXTNT DS    CL40      BESCHREIBUNG VON 4 EXTENTS
DS3FMTID DS    C         FORMATNUMMER DES DSCB
DS3ADEXT DS    CL90      BESCHREIBUNG VON 9 WEITEREN EXTENTS
DS3PTRDS DS    CL5       RESERVIERT
         ORG   DS1FMTID
DS4IDFMT DS    C         FORMATNUMMER DES DSCB
DS4HPCHR DS    CL5       HOECHSTE ADRESSE EINES FORMAT 1 DSCB'S
DS4DSREC DS    CL2       ANZAHL NOCH VERFUEGBARER DSCB'S
DS4HCCHH DS    CL4       ADRESSE FUER DIE NAECHSTE AUSZULAGERNDE SPUR
DS4NOATK DS    CL2       ANZAHL FREIER AUSGELAGERTER SPUREN
DS4VTOCI DS    C         VTOC INDICATOR
DS4NOEXT DS    C
         DS    CL2       RESERVIERT
DS4DEVSZ DS    CL4       ANZAHL ZYLINDER UND SPUREN PRO ZYLINDER
DS4DEVTK DS    CL2       LAENGE DER SPUR
DS4DEVI  DS    C         ANZAHL OVERHEAD BYTES
DS4DEVL  DS    C         ANZAHL OVERHEAD BYTES F. LETZTEN BLOCK D. SPUR
DS4DEVK  DS    C         NICHT BENUTZTE OVERH.BYTES BEI NOT KEYED
DS4DEVFG DS    C         FLAG BYTE
DS4DEVTL DS    CL2       TOLERANZGRENZE
DS4DEVDT DS    C         ANZAHL DSCB'S PRO SPUR
DS4DEVDB DS    C         ANZAHL DIRECTORY BLOCKS PRO SPUR
         DS    CL24      RESERVIERT
DS4F6PTR DS    CL5       POINTER ZUM ERSTEN FORMAT 6 DSCB
DS4VTOCE DS    CL10      BESCHREIBUNG DES EXTENTS DER VTOC
         DS    CL25      RESERVIERT
         ORG   DS1FMTID
DS5KEYID DS    CL4       SCHLUESSEL
DS5AVEXT DS    CL5       BESCHREIBUNG DES ERSTEN FREIEN BEREICHS
DS5EXTAV DS    CL35      BESCHREIBUNG VON SIEBEN FREIEN BEREICHEN
DS5FMTID DS    C         FORMATNUMMER DES DSCB
DS5MAVET DS    CL90      BESCHREIBUNG VON ACHTZEHN FREIEN BEREICHEN
DS5PTRDS DS    CL5
         ORG   DS1FMTID
WORKAREA DS    CL350
         EJECT
K1       DC    CL09'1LISTPDS/'
KVERSION DS    CL02
         DC    CL01'/'
KDATUM   DS    CL06
KZEIT    DS    CL04
         DC    CL42'        INHALTSVERZEICHNIS DES BESTANDES: '
KDSN     DC    CL30' '
         DC    CL06' '
         DC    CL07'STAND: '
KTAG     DS    CL02
         DC    CL02'.'
KMONAT   DS    CL02
         DC    CL02'.'
KJAHR    DS    CL02
         SPACE  2
         DC    CL16' '
         ORG   K1
KOPF1    DS    CL133
K1A      DC    CL49' '
         DC    CL15'VOLUME = SER = '
KVOL     DS    CL6
         DC    CL63' '
         ORG   K1A
KOPF1A   DS    CL133
         SPACE 2
K2       DC    CL45'0 MEMBER    ALIAS     ADRESSE  SYSTEMSTATUS  '
         DC    CL45'SPEICHER  ENTRY   P R O G R A M M   -   A T T'
         DC    CL43' R I B U T E'
         ORG   K2
KOPF2    DS    CL133
         SPACE 2
K3       DC    CL45'   NAME      VON     SPUR SATZ'
         DC    CL45' BEDARF   POINT   RENT REUS OVLY TEST LOAD SC'
         DC    CL43'AT EXEC LINK CR-F REFR'
         ORG   K3
KOPF3    DS    CL133
         SPACE 2
ZVORSCH  DS    C
ZMEMBER  DS    CL8
         DS    CL2
ZALIAS   DS    CL8
         DS    CL2
ZSPUR    DS    CL4
         DS    CL2
ZSATZ    DS    CL2
         DS    CL2
ZLEVEL   DS    CL2
         DS    C
ZFLAG    DS    CL2
         DS    C
ZNR      DS    CL4
         DS    CL5
ZREGION  DS    CL4
         DS    C
ZK       DS    C
         DS    CL2
ZENTRY   DS    CL6
         DS    CL4
ZRENT    DS    C
         DS    CL4
ZREUS    DS    C
         DS    CL4
ZOVLY    DS    C
         DS    CL4
ZTEST    DS    C
         DS    CL4
ZLOAD    DS    C
         DS    CL4
ZSCAT    DS    C
         DS    CL3
ZEXEC    DS    CL4
         DS    C
ZLINK    DS    CL4
         DS    CL2
ZCRF     DS    C
         DS    CL4
ZREFR    DS    C
         DS    CL23
         ORG   ZVORSCH
ZEILE    DS    CL133
         EJECT
         DS    0F
ANZBLBI  DC    XL4'00000000'
ANZBLI   EQU   ANZBLBI
ZWLAE    DS    H
VERSION  DC    CL2'00'
DATUM    TAGZEIT
HEX      DC    CL16'0123456789ABCDEF'
ANZMEM   DC    PL5'0'
ANZDIR   DC    PL3'0'
ANZBEN   DC    PL3'0'
CFZAE    DC    PL5'0'
CAZAE    DC    PL5'0'
PFZAE    DC    PL5'0'
POZAE    DC    PL5'0'
FGZAE    DC    PL5'0'
FLZAE    DC    PL5'0'
XXZAE    DC    PL5'0'
ZWITTR   DS    F
ZWISECND DS    F
         DS    0F
ZTRACKS   DC   F'0'
ANZEX    DC    PL2'0'
         DS    0D
ZWI      DS    D
ZWITIOT  DS    F
ZWILAE   DC    H'0'
SCH1MAL  DC    X'01'
SCH      DC    X'00'
SCHENDE  DC    X'00'
SCHVORL  DC    X'00'
SCHANF   DC    X'01'
ZEIZAE   DC    PL3'000'
ZEIZAHL  DC    PL3'060'
         DS    0D
ANZMEMB  DS    D
         DS    0H
COND     DC    H'0'
         DS    0D
CONVERT  DS    D
ANZFREI  DC    PL2'0'
ANZDS5   DC    PL2'0'
ANZSPUR  DC    PL3'0'
ANZTRACK DC    H'0'
KONST1   DC    CL26'***** ANZAHL DER MEMBER = '
KONST101 DC    CL5'CF = '
KONST102 DC    CL5'CA = '
KONST103 DC    CL5'PF = '
KONST104 DC    CL5'PO = '
KONST105 DC    CL5'FG = '
KONST106 DC    CL5'FL = '
KONST107 DC    CL8'SONST = '
KONST2   DC    CL35'***** LISTPDS PROGRAMMENDE, COND = '
KONST3   DC    CL36'***** ANZAHL DER DIRECTORY BLOCKS = '
KONST4   DC    CL20', DAVON UNBENUTZT = '
KONST5   DC    CL06'*****'
KONST6   DC    CL12'. EXTENT  = '
KONST7   DC    CL07' TRACKS'
KONST8   DC    CL20', DAVON UNBENUTZT = '
KONST9   DC    CL12'  EXTENTS = '
KONST10  DC    CL17', NOCH VERFUEGBAR'
KONST11  DC    CL41'***** KEINE FREIEN EXTENTS MEHR VORHANDEN'
KONST12  DC    CL50'***** DATA SET CONTROL BLOCK NICHT RICHTIG GELESEN'
KONST13  DC    CL15' REGISTER 15 = '
KONST14  DC    CL48'***** KEINE SECONDARY SPACE ALLOCATION VORHANDEN'
         COPY  REGISTER
         COPY  THILFE
TDDNAMEN DC    CL8'SYSPRINT'
         DC    CL8'SYSUDUMP'
         DC    CL8'SYSABEND'
         DC    CL8'SYSIN'
         DC    CL8'SYSOUT'
         DC    CL8'JOBLIB'
         DC    CL8'STEPLIB'
         DC    CL8'PGM=*.DD'
TDDNEND  EQU   *
         EJECT
INHVERZ  DSECT
INAME    DS    CL8       MEMBERNAME BZW. ALIASNAME
ITTRP    DS    CL3       SPUR- UND SATZADRESSE DES ERSTEN SATZES
IINDIC   DS    C         KZ. ALIAS UND LAENGE
ITTRT    DS    CL3       SPUR- UND SATZADRESSE DES ERSTEN TEXTBLOCKS
         DS    C         LEER
ITTRNS   DS    CL3       SPUR- UND SATZADRESSE SCATTER TABELLE
INL      DS    C         ANZAHL DER ENTRIES
IATTRIB1 DS    C         ATTRIBUTE
IATTRIB2 DS    C         ATTRIBUTE
ISPEICH  DS    CL3       KERNSPEICHERBEDARF
ITXTLAEN DS    CL2       LAENGE DES ERSTEN TEXTBLOCKS
IEPADR   DS    CL3       ENTRY POINT ADRESSE
ITXTORIG DS    CL3
ISCLAENG DS    CL2       GROESSE DER SCATTER LISTE
ITTLAENG DS    CL2       LAENGE DER SCATTER UEBERSETZUNGS TABELLE
IESDID   DS    CL2
IESCSECT DS    CL2
ISENTRY  DS    CL3
ISMEMBER DS    CL8
         ORG   ISCLAENG
IRENTRY  DS    CL3
IRMEMBER DS    CL8
LISTPDS  CSECT
         LTORG
TABANZ   DC    H'1001'        ANZAHL DER TABELLENFELDER
TALIAS   DS    CL8            TABELLE ALIASNAMEN
TTTR     DS    CL3
TMEMBER  DS    CL8
         DS    CL19000
         END
