./ ADD NAME=CALLWTL2
         MACRO
         CALLWTL2   &PANVAL
         GBLC       &CSNAME
.*
.*       DIESES MACRO WIRD VOM PROGRAMMIERER DER END-ANWEISUNG
.*       EINES ASSEMBLER-PROGRAMMS VORANGESTELLT.
.*       DIE PANVALET EXIT-ROUTINE UT206600 STELLT DIE PANVALET-
.*       DIRECTORY-INFORMATIONEN LEVEL, NAME UND DATUM DER LETZTEN
.*       AENDERUNG ALS POSITIONSPARAMETER IN DAS MACRO-STATEMENT EIN.
.*       DAS MACRO ERSTELLT EINE CSECT, DIE AUS DEM PROGRAMM DURCH
.*       DAS MACRO CALLWTL ANGESPRUNGEN WIRD, UND IHRERSEITS DIE
.*       RESIDENTE ROUTINE UR211300 AUFRUFT.
.*       DIESE ROUTINE SCHREIBT DIE PANVALET-DIRECTORY-INFORMATIONEN
.*       AUF DAS SYSTEM-LOG.
.*       DER CSECT-NAME WIRD DURCH DAS MACRO CALLWTL GENERIERT UND
.*       UEBERGEBEN. IST DIESES NICHT VORHANDEN, SO WIRD EIN MNOTE
.*       AUSGEGEBEN UND LEDIGLICH EINE UNBENANNTE CSECT ERZEUGT,
.*       DIE DIE OBEN ERWAEHNTEN PANVALET-INFORMATIONEN ALS
.*       DC-KONSTANTE ENTHAELT.
.*
.*       PRUEFEN PARAMETER UND GBLC-VARIABLE,
.*       ENTSPRECHEND VERZWEIGEN
.*
         AIF   ('&CSNAME' NE '').GENER
         AIF   ('&PANVAL' NE '').NOCALL
         AGO   .END
.*
.*
.*       GENERIEREN CALL-ROUTINE:
.*
.GENER   ANOP
         EJECT
***********************************************************************
*                                                                     *
*        DIESE CSECT DIENT DEM AUFRUF DER UNTERROUTINE UR211300,      *
*        DIE PROGRAMMNAME, PAN-LEVEL UND DATUM DER LETZTEN AENDERUNG  *
*        AUF DAS SYSTEM-LOG SCHREIBT.                                 *
*                                                                     *
***********************************************************************
         SPACE 2
&CSNAME  CSECT
         AIF   ('&PANVAL' EQ '').BR14
         USING *,15                    VORLAEUFIGES BASISREGISTER
         SPACE 2
         STM   13,1,*+80               SICHERN REGISTER 13,14,15,0,1
         BAL   13,*+96                 BRANCH UM SAVE AREA
         DROP  15
         USING *,13                    ENTHAELT SA-ADR. UND IST BASIS
         DC    18F'0'                  SAVE AREA
         DC    5F'0'                   SA. FUER REG. DER RUFENDEN PROGR
         SPACE 2
         LOAD  EP=UR211300
         SPACE 2
         CNOP  0,4
         BAL   1,*+32                  PARM-ADRESSE IN REG. 1
         DC    X'80',AL3(*+3)          ADRESSE DES PARAMETERS
         DC    CL24&PANVAL                  PAN LEVEL, NAME, UND DATUM
         LR    15,0                    LADE EP-ADRESS IN REG. 15
         BALR  14,15                   BRANCH ZUR WTL-ROUTINE
         SPACE 2
         LM    13,1,72(13)             RESTORE REGISTER 13,14,15,0,1
         SPACE 1
         BR    14                      ZURUECK INS PROGRAMM
         SPACE 4
         MEXIT
.*
.*
.*       CALLWTL VORHANDEN, ABER KEINE PANVALET-DIRECTORY-INFORMATION
.*
.BR14    ANOP
         BR    14
         SPACE 1
         MNOTE 4,'*** PANVALET DIRECTORY INFORMATIONEN FEHLEN ***'
         SPACE 2
         MEXIT
.*
.*
.*       PANVALET-DIRECTORY-INFORMATION VORHANDEN, ABER KEIN CALLWTL
.*
.NOCALL  ANOP
         SPACE 2
***********************************************************************
*                                                                     *
*        DIESE CSECT ENTHAELT PROGRAMM-NAME, PAN-LEVEL                *
*        UND DAS DATUM DER LETZTEN AENDERUNG.                         *
*                                                                     *
***********************************************************************
         SPACE 2
         CSECT
         DC    CL21&PANVAL                  PAN LEVEL, NAME, UND DATUM
         SPACE 1
         MNOTE 4,'*** CALLWTL MACRO FEHLT ***'
         SPACE 4
         MEXIT
.*
.*
.*       WEDER PANVALET-DIRECTORY-INFORMATION NOCH CALLWTL VORHANDEN
.*
.END     ANOP
         SPACE 1
         MNOTE 4,'*** CALLWTL MACRO UND PANVALET DIRECTORY INFORMATIONE*
               N FEHLEN ***'
         SPACE 2
         MEND
