**                                                              **
**       P R O L O G                                            **
**                                                              **
**       DIESE ROUTINE ERMOEGLICHT ES, BEI  I P L  DEN NAMEN    **
**       DES SUBSYTEMS WELCHES GESTARTET WERDEN SOLL, ANZUGEBEN **
**       ES WIRD DER ENTRY-POINT TRACE IM IEFVH1 BENUTZT.       **
**       T R A C E  WIRD VON IEFVHM ANGESPRUNGEN UND AENDERT    **
**       DEN NAMEN DES PRIMAEREN SUBSYSTEMS IN  J E S C T UND   **
**       S S C V T .                                            **
**                                                              **
**       REGISTER BEI AUFRUF TRACE                              **
**       R9    ADRESSE START-COMMAND                            **
**       R12   ADRESSE COMMON-AREA                              **
**       R14   RUECKSPRUNG                                      **
**                                                              **
**       INSTALLATION-PROCEDURE                                 **
**                                                              **
**       ++PTF(UZ00001) .                                       **
**       ++VER(Z030) .                                          **
**       ++MOD(IEFVH1) DISTLIB(AOSB3) .                         **
**        - - - OBJECT DECK - - -                               **
**       /*                                                     **
**                                                              **
TRACE    CSECT
         USING *,R15
         CLC   4(4,R14),K001           CALLER IEFVHM ?
         BNE   RETURN
         CLC   0(9,R9),K002            START COMMAND ?
         BNE   RETURN
         USING COMWA,R12               C/I COMMON WORKAREA
         STM   R0,R14,IWAIOSA          C/I COMWA + X'58'
         LA    R0,136                  LAENGE DSECT
         GETMAIN R,LV=(0)
         LR    R11,R1                  A(JESDS)
         USING JESDS,R11
         DROP  R15
         BALR  R8,0
         USING *,R8                    BASE - REG
         L     R2,16                   A(CVT)
         L     R2,296(R2)              A(JESCT)IN CVT
         CLC   28(4,R2),9(R9)          PRIM-JES START ?
         BNE   END
         L     R3,24(R2)               A(SSCT) IN JESCT
         LA    R7,28(R2)               A(JESNAME) IN JESCT
         CLC   16(4,R3),K003           SSVT EXIST ?(IN SSCVT)
         BNE   END
         MVC   MSG(57),MSG1
         LA    R2,ANSWER
         ST    R2,MSG                  A(ANSWER)
         MVI   MSG,68                  L'ANSWER'
         LA    R2,ECB
         ST    R2,MSG+4                A(ECB)
JESSTART EQU   *
         XC    ECB,ECB
         MVI   ANSWER,X'40'
         MVC   ANSWER+1(67),ANSWER     CLEAR
         LA    R1,MSG
         WTOR  MF=(E,(1))              ENTER START COMMAND
         WAIT  ECB=ECB
         LA    R2,ANSWER
A050     EQU   *
         CLI   0(R2),C'U'              TAKE PRIME JES
         BE    END
         LA    R4,JESTAB               JESNAME-TABLE
         LA    R5,2
A100     EQU   *
         CLC   0(4,R2),0(R4)           VALID JES - NAME ?
         BE    A150
         LA    R4,4(R4)
         BCT   R5,A100
         WTO   'JES0004E *** INVALID JESNAME ***'
         B     JESSTART
A150     EQU   *
         CLC   0(4,R7),0(R2)           START CMD = JESNAME IN JESCT ?
         BNE   A200
         MVC   MSG2+21(4),0(R2)
MSG2     WTO   'JES0002I *** .... IS PRIMARY SUBSYSTEM ***'
         B     A250
A200     MVC   0(4,R7),0(R2)           MOVE JES-NAME IN JESCT
         MVC   8(4,R3),0(R2)           MOVE JES-NAME IN SSCVT
         MVC   MSG3+21(4),0(R2)
MSG3     WTO   'JES0003I *** .... IS NOW PRIMARY SUBSYSTEM ***'
A250     MVC   9(62,R9),ANSWER         MOVE START-CMD IN
END      EQU   *
         LR    R1,R11                  A(JESDS)
         LA    R0,136
         FREEMAIN R,LV=(R0),A=(R1)
         LM    R0,R14,IWAIOSA
RETURN   EQU   *
         LA    R14,8(R14)              RETURN TO IEFVHM
         BR    R14
**                                                              **
*        DEFINITIONEN                                            *
**                                                               *
MSG1     WTOR  'JES0001R *** ENTER SUB-SYSTEM-NAME  OR  U ***',0,68,0, *
               MF=L
K001     DC    C'VHM '
K002     DC    C'// START '
K003     DC    F'0'
JESTAB   DC    CL4'JES2'
         DC    CL4'JES3'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
**                                                              **
*        CONVERTER/INTERPRETER COMMON WORKAREA DSECT             *
*        DISPLACEMENT X'58' SAVE-AREA                            *
**                                                              **
COMWA    DSECT                         C/I WORK-AREA
         ORG   COMWA+X'58'
IWAIOSA  DS    18F                     SAVE-AREA
**                                                              **
*        DYNAMISCHER BEREICH FUER WTOR                           *
**                                                              **
JESDS    DSECT
ECB      DS    F
ANSWER   DS    CL68
MSG      DS    CL60
         END
