**********************************************************************
*              REGISTER EQUATEN                                      *
**********************************************************************
         SPACE
         REQUATEN
         EJECT
**********************************************************************
*              START PROGRAM                                         *
**********************************************************************
         SPACE
         PRINT NOGEN
SPFETGDG CSECT
         LCNVE *,BASE
         SPACE
         LR    R11,R1                  ADDRESS OF THE PARM PARAMETER
*                                      FOR THE PMD
         EJECT
**********************************************************************
*              GET THE WORK AREA                                     *
**********************************************************************
         SPACE
         INIT  R10,(GDGWORK,GDGWKLE)
         SPACE
         USING GDGWORK,R10
         L     R1,4(R11)               GET ADDRESS OF FORMAT NAME
         MVC   MHAFMTN,0(R1)           SAVE THE FORMAT NAME
         MVC   DSNAME,BLANK            INIT WITH BLANK
         MVC   UNIT,BLANK               "    "    "
         MVI   VOLUME,C' '              "    "    "
         MVC   VOLUME+1(L'VOLUME-1),VOLUME
         MVC   MERRP1,BLANK             "    "    "
         SPACE 2
**********************************************************************
*              CALL THE MENU HANDLER                                 *
**********************************************************************
         SPACE
DISPLAY  SPFMH MHAPARM,MHAFMTN,OPT1,                                   *
               DSN,DSNAME,UNIT,VOLP4,VOLP5,VOLP6,MERRMSG
         SPACE
PICRET   L     R1,0(R11)               ADDRESS OF TLD
         TM    29(R1),B'00000100'      DID THE USER PRESS PFK3
         BO    END
         MVC   P1,DSN
         OC    P1,BLANK                MAKE ALL CAPITALS
         EJECT
**********************************************************************
*              LOCATE THE DATASET NAME                               *
**********************************************************************
         SPACE
         CLC   P1,BLANK                INPUT SPECIFIED ?
         BNE   LOCATE                  READ CATALOG
         LA    R15,0                   NO INPUT WAS SPECIFIED
         B     ERROR                   PUT THE ERROR MESSAGE
LOCATE   MVC   INDGX(16),CAMLST        INIT THE PARAMETER LIST FOR
*                                      THE LOCATE MACRO
         LA    R1,P1                   ADDRESS OF THE REQUESTED DSN
         ST    R1,INDGX+4              STORE THE ADDRESS IN CAMLST
         LA    R1,LOCAREA              ADDRESS OF THE LOCATE-AREA
         ST    R1,INDGX+12             STORE THE ADDRESS IN CAMLST
         LOCATE INDGX
         LTR   15,15                   ANY RETURN CODES ?
         BNZ   ERROR                   PUT THE ERROR MESSAGE
         MVC   DSNAME,P1               DSNAME WITH THE NUMBER OF GDG
         MVI   VOLUME,C' '             DELETE VOLUME FIELD
         MVC   VOLUME+1(L'VOLUME-1),VOLUME
         LA    R6,LOCAREA+4            POINT TO THE UNIT NAME
         LM    R7,R9,=A(UTAB,8,UTABEND)
LOOP1    CLC   0(2,R6),0(R7)           COMPARE POINTER TO UNIT/UTAB
         BE    MVC                     MOVE THE UNIT NAME
         BXLE  R7,R8,LOOP1             MAKE LOOP FOR THE NEXT REQUEST
         MVC   UNIT,BLANK              DELETE UNIT FIELD
         B     NOUNIT                  NO UNIT NAME LOCATED
MVC      MVC   UNIT,2(R7)              MOVE THE UNIT QAULIFIER
NOUNIT   LH    R7,LOCAREA              NUMBER OF VOLS
         LA    R6,LOCAREA+2            POINT TO THE FIRST VOLSER
         LA    R8,VOLUME               POINT TO THE OUTPUT BUFFER
VOLMVC   MVC   0(6,R8),4(R6)           MOVE THE VOLUME NUMBER
         LA    R6,12(R6)               INCREASE TO THE NEXT VOLSER
         MVI   6(R8),C','              SET DECIMAL
         EJECT
**********************************************************************
*              LOCATE THE DATASET NAME                               *
**********************************************************************
         SPACE
         BCT   R7,LOOP2                MAKE LOOP FOR THE NEXT VOLSER
         MVI   6(R8),C' '              DELETE LAST DECIMAL
         B     DISPLAY                 PUT THE GDG MENU
LOOP2    LA    R8,7(R8)                INCREASE TO THE NEXT OUTPUT
         B     VOLMVC                  MOVE THE NEXT VOLSER
         SPACE 2
**********************************************************************
*              END OF SPFETGDG                                       *
**********************************************************************
         SPACE
END      L     R1,0(R11)               ADDRESS OF TLD
         NI    29(R1),B'11111011'      TURN OFF THE PFK3 SWITCH
         L     R0,GDGWKLE              SIZE OF THE WORK AREA
         FREEMAIN R,LV=(0),A=(10)
         LEXIT
         SPACE 2
**********************************************************************
*              PUT THE ERROR MESSAGE                                 *
**********************************************************************
         SPACE
ERROR    L     R1,ATAB(R15)            ADDRESS OF THE ERROR REPORT
*                                      QUALIFIER
         MVC   MERRMSG+3(1),0(R1)      MOVE THE ERROR MESSAGE ID
         MVC   MERRP1+1(44),DSN        SAVE THE INPUT BUFFER
         SPACE
         SPFEM MERRPARM,MERRMSG,OPT1,MERRP1
         SPACE
         B     PICRET                  MAKE A LOOP FOR THE NEXT REQUEST
         EJECT
**********************************************************************
*              DEFINITIONEN                                          *
**********************************************************************
         SPACE
BLANK    DC    50C' '
CAMLST   DS    0F                      ALIGN ON FULL WORD
         DC    AL1(68)                 THREE BYTES OF FLAGS INDICATING
         DC    AL1(0)                  THE FUNCTION TO BE PERFORMED
         DC    AL1(0)
         DC    AL1(0)                  NO OPTION THREE
         DC    A(0)                    PARAMETER TWO
         DC    A(0)                    PARAMETER THREE OMMITTED
         DC    A(0)                    PARAMETER FOUR
OPT1     DC    A(1)                    CONTROL OPTION PARAMETER FOR MHA
ATAB     DC    A(F0)                   ERROR CODE - 00
         DC    A(F1)                   ERROR CODE - 04
         DC    A(F2)                   ERROR CODE - 08
         DC    A(F3)                   ERROR CODE - 12
         DC    A(F4)                   ERROR CODE - 16
         DC    A(F5)                   ERROR CODE - 20
         DC    A(F6)                   ERROR CODE - 24
         DC    A(F7)                   ERROR CODE - 28
         DC    A(F8)                   ERROR CODE - 32
ATABEND  DC    A(F9)                   ERROR CODE - 36
F0       DC    C'0'                    ERROR REPORT QUALIFIER
F1       DC    C'1'                     "     "      "
F2       DC    C'2'                     "     "      "
F3       DC    C'2'                     "     "      "
F4       DC    C'3'                     "     "      "
F5       DC    C'4'                     "     "      "
F6       DC    C'5'                     "     "      "
F7       DC    C'6'                     "     "      "
F8       DC    C'7'                     "     "      "
F9       DC    C'7'                     "     "      "
         EJECT
**********************************************************************
*              DEFINITIONEN                                          *
**********************************************************************
         SPACE
UTAB     DC    X'8001',C'2400  '       TAPE UNIT
         DC    X'8003',C'3400  '        "    "
         DC    X'2008',C'2314  '       DISK UNIT
         DC    X'2009',C'3330  '        "    "
         DC    X'200B',C'3350  '        "    "
UTABEND  DC    X'200D',C'3330-1'        "    "
         SPACE 2
**********************************************************************
*              DEFINITIONEN / LITERALE                               *
**********************************************************************
         SPACE
         LTORG
         EJECT
**********************************************************************
*              DEFINITIONEN / DSECT OF THE WORKAREA                  *
**********************************************************************
         SPACE
GDGWORK  CSECT
INDGX    DS    0D                      PARAMETER LIST FOR THE LOCATE
         DS    A                       MACRO
         DS    A
         DS    A
         DS    A
MHAPARM  DS    A                       ADDRESS OF TLD
         DS    A                       ADDRESS OF FORMAT NAME
         DS    A                       ADDRESS OF CONTROL OPTION
         DS    A                       ADDRESS OF PARM1 FOR MHA
         DS    A                       ADDRESS OF PARM2 FOR MHA
         DS    A                       ADDRESS OF PARM3 FOR MHA
         DS    A                       ADDRESS OF PARM4 FOR MHA
         DS    A                       ADDRESS OF PARM4 FOR MHA
         DS    A                       ADDRESS OF PARM4 FOR MHA
         DS    A                       ADDRESS OF PARM4 FOR MHA
MERRPARM DS    A                       ADDRESS OF TLD
         DS    A                       ADDRESS OF MESSAGE ID.
         DS    A                       ADDRESS OF CURSOR ID.
         DS    A                       ADDRESS OF PARM1 FOR MERR
         DS    A                       ADDRESS OF PARM1 FOR MERR
         DS    H                       RESERVE
LOCAREA  DS    0D                      WORK AREA FOR LOCATE
         DS    CL265
MERRMSG  DC    CL4' '                  ERROR MESSAGE ID.
MHAFMTN  DC    CL8' '                  FORMAT NAME
P1       DC    CL44' '                 INPUT FIELD  / PARM1 - MHA
DSN      DC    CL44' '
DSNAME   DC    CL44' '                 OUTPUT FIELD / PARM2 - MHA
UNIT     DC    CL6' '                  OUTPUT FILED / PARM3 - MHA
         EJECT
**********************************************************************
*              DEFINITIONEN                                          *
**********************************************************************
         SPACE
VOLUME   DS    0CL168                  OUTPUT FILED / PARM4-6
VOLP4    DC    CL56' '                 PARM4 - MHA
VOLP5    DC    CL56' '                 PARM5 - MHA
VOLP6    DC    CL56' '                 PARM6 - MHA
         DC    CL50' '                 RESERVE
MERRP1   DC    CL46' '                 PARM FOR MERR
GDGWKLE  EQU   *-GDGWORK               LENGTH OF WORK AREA
SPFETGDG CSECT
         END   SPFETGDG
