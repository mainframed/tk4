./ ADD NAME=XUCBGET
         MACRO
&NAME    XUCBGET &WKREG,&ENDUCB,&ERROR
.*
.* DIE ZUEINANDER GEHOERENDEN MACROS
.*       XUCBSCAN  XUCBGET  XUCBOFF
.*    UNTERSTUETZEN DIE ANWENDUNG DER UCBSCAN ROUTINE 'IOSVSUCB'.
.*    UCBSCAN IST AB MVS/SP 1.3 UND MIT MVS/SP 2 (XA) VERFUEGBAR.
.*
.* XUCBSCAN WKREG<,DEVC>
.*          GETMAIN UND INITIALISIERN VON PARM LIST UND WORKBEREICH.
.*          LADEN WKREG UND LOESCHEN DES WORKBEREICH.
.*          XUCBSCAN MUSS VOR XUCBGET ODER XUCBOFF  E I N M A L  AUF-
.*          GERUFEN WORDEN SEIN.
.* XUCBGET WKREG,ENDUCB,ERROR
.*          STELLT PRO AUFRUF DIE ADRESSE EINES UCB IN REG 1 ZUR VER-
.*          FUEGUNG. BEIM ERSTEN AUFRUF NACH XUCBSCAN DEN ERSTEN UCB,
.*          DANACH DEN ZWEITEN, DRITTEN USW.
.* XUCBOFF WKREG
.*          FREEMAIN VON PARM LIST UND WORKBEREICH.
.*          NACH XUCBOFF DARF XUCBGET NICHT MEHR GEGEBEN WERDEN.
.*
.*    WKREG   EIN GP REGISTER. ENTHAELT NACH XUCBSCAN DIE PARM LIST
.*            ADDRESSE. WKREG MUSS FUER DIE SCANDAUER BEI AUFRUF
.*            VON XUCBGET ODER XUCBOFF DIESE ADDRESSE ENTHALTEN.
.*    DEVC    OPTIONAL, BESCHRAENKT UCBSCAN AUF ANGEGEBENEN
.*            UCB DEVICE CODE (X'20'=DASD, X'80'=TAPE  ETC.)
.*    ENDUCB  ANLAUFADRESSE NACH LETZTEM UCB:   IOSVSUCB RC=4
.*    ERROR   ANLAUFADRESSE NACH SCAN FEHLER:   IOSVSUCB RC>4
.*
.* ZUM AUFFINDEN VON IOSVSUCB MUSS CVT ALS DSECT DEFINIERT SEIN:
.*           CVT DSECT=YES
.*
&NAME    LR    1,&WKREG                  ADDR PARM LIST
         USING CVT,15
         L     15,CVTPTR                 ADDR CVT
         L     15,CVTUCBSC               ADDR IOSVSUCB
         DROP  15
         BALR  14,15                     GO TO ROUTINE
         LTR   15,15                     TEST RETURN CODE
         BZ    *+18                       0  - UCB FOUND
         LA    1,4
         CR    15,1
         BNH   &ENDUCB                    4  - END UCB CHAIN
         B     &ERROR                    >4  - ERROR
         L     1,12(&WKREG)              RECEIVED UCB ADDRESS
         MEND
./ ADD NAME=XUCBOFF
         MACRO
&NAME    XUCBOFF &WKREG
.*
.* DIE ZUEINANDER GEHOERENDEN MACROS
.*       XUCBSCAN  XUCBGET  XUCBOFF
.*    UNTERSTUETZEN DIE ANWENDUNG DER UCBSCAN ROUTINE 'IOSVSUCB'.
.*    UCBSCAN IST AB MVS/SP 1.3 UND MIT MVS/SP 2 (XA) VERFUEGBAR.
.*
.* XUCBSCAN WKREG<,DEVC>
.*          GETMAIN UND INITIALISIERN VON PARM LIST UND WORKBEREICH.
.*          LADEN WKREG UND LOESCHEN DES WORKBEREICH.
.*          XUCBSCAN MUSS VOR XUCBGET ODER XUCBOFF  E I N M A L  AUF-
.*          GERUFEN WORDEN SEIN.
.* XUCBGET WKREG,ENDUCB,ERROR
.*          STELLT PRO AUFRUF DIE ADRESSE EINES UCB IN REG 1 ZUR VER-
.*          FUEGUNG. BEIM ERSTEN AUFRUF NACH XUCBSCAN DEN ERSTEN UCB,
.*          DANACH DEN ZWEITEN, DRITTEN USW.
.* XUCBOFF WKREG
.*          FREEMAIN VON PARM LIST UND WORKBEREICH.
.*          NACH XUCBOFF DARF XUCBGET NICHT MEHR GEGEBEN WERDEN.
.*
.*    WKREG   EIN GP REGISTER. ENTHAELT NACH XUCBSCAN DIE PARM LIST
.*            ADDRESSE. WKREG MUSS FUER DIE SCANDAUER BEI AUFRUF
.*            VON XUCBGET ODER XUCBOFF DIESE ADDRESSE ENTHALTEN.
.*    DEVC    OPTIONAL, BESCHRAENKT UCBSCAN AUF ANGEGEBENEN
.*            UCB DEVICE CODE (X'20'=DASD, X'80'=TAPE  ETC.)
.*    ENDUCB  ANLAUFADRESSE NACH LETZTEM UCB:   IOSVSUCB RC=4
.*    ERROR   ANLAUFADRESSE NACH SCAN FEHLER:   IOSVSUCB RC>4
.*
.* ZUM AUFFINDEN VON IOSVSUCB MUSS CVT ALS DSECT DEFINIERT SEIN:
.*           CVT DSECT=YES
.*
&NAME    LR    1,&WKREG                  ADDR PARM LIST AND WORK AREA
         LA    0,124                     LG PARM LIST AND WORK AREA
         FREEMAIN  R,LV=(0),A=(1)
         MEND
./ ADD NAME=XUCBSCAN
         MACRO
&NAME    XUCBSCAN &WKREG,&DEVC
.*
.* DIE ZUEINANDER GEHOERENDEN MACROS
.*       XUCBSCAN  XUCBGET  XUCBOFF
.*    UNTERSTUETZEN DIE ANWENDUNG DER UCBSCAN ROUTINE 'IOSVSUCB'.
.*    UCBSCAN IST AB MVS/SP 1.3 UND MIT MVS/SP 2 (XA) VERFUEGBAR.
.*
.* XUCBSCAN WKREG<,DEVC>
.*          GETMAIN UND INITIALISIERN VON PARM LIST UND WORKBEREICH.
.*          LADEN WKREG UND LOESCHEN DES WORKBEREICH.
.*          XUCBSCAN MUSS VOR XUCBGET ODER XUCBOFF  E I N M A L  AUF-
.*          GERUFEN WORDEN SEIN.
.* XUCBGET WKREG,ENDUCB,ERROR
.*          STELLT PRO AUFRUF DIE ADRESSE EINES UCB IN REG 1 ZUR VER-
.*          FUEGUNG. BEIM ERSTEN AUFRUF NACH XUCBSCAN DEN ERSTEN UCB,
.*          DANACH DEN ZWEITEN, DRITTEN USW.
.* XUCBOFF WKREG
.*          FREEMAIN VON PARM LIST UND WORKBEREICH.
.*          NACH XUCBOFF DARF XUCBGET NICHT MEHR GEGEBEN WERDEN.
.*
.*    WKREG   EIN GP REGISTER. ENTHAELT NACH XUCBSCAN DIE PARM LIST
.*            ADDRESSE. WKREG MUSS FUER DIE SCANDAUER BEI AUFRUF
.*            VON XUCBGET ODER XUCBOFF DIESE ADDRESSE ENTHALTEN.
.*    DEVC    OPTIONAL, BESCHRAENKT UCBSCAN AUF ANGEGEBENEN
.*            UCB DEVICE CODE (X'20'=DASD, X'80'=TAPE  ETC.)
.*    ENDUCB  ANLAUFADRESSE NACH LETZTEM UCB:   IOSVSUCB RC=4
.*    ERROR   ANLAUFADRESSE NACH SCAN FEHLER:   IOSVSUCB RC>4
.*
.* ZUM AUFFINDEN VON IOSVSUCB MUSS CVT ALS DSECT DEFINIERT SEIN:
.*           CVT DSECT=YES
.*
&NAME    LA    0,124                     LG PARM LIST AND WORK AREA
         GETMAIN   R,LV=(0)
         LR    &WKREG,1                  ADDR PARM LIST AND WORK AREA
         XC    0(124,&WKREG),0(&WKREG)   CLEAR PARM LIST AND WORK AREA
         LA    1,24(&WKREG)              ADDR WORK AREA -
         ST    1,0(&WKREG)               INTO PARM LIST
         LA    1,19(&WKREG)              ADDR OF DEVICE CODE -
         ST    1,4(&WKREG)               INTO PARM LIST
         LA    1,12(&WKREG)              ADDR OF UCB ADDR TO BE RE-
         ST    1,8(&WKREG)               CEIVED INTO PARM LIST
         MVI   8(&WKREG),X'80'           INDICATE END OF PARM LIST
         AIF   (T'&DEVC NE 'O').A1
         MEXIT
.A1      ANOP
         MVI   19(&WKREG),&DEVC          DEFINED DEVICE CODE
         MEND
