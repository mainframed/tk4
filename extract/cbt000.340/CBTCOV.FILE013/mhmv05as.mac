         MACRO
&NAME    MSG   &LAENGE,&MELDUNG,&TYP,&BLKSIZE=,&DDNAME=MSGPRINT,&SP=
.*
.*  LAENGE = LAENGE DES AUSZUGEBENDEN FELDES IN BYTES
.*
.*  MELDUNG = ADRESSE DES CHARACTER-STRINGS,         FALLS TYP=C
.*          = AUSZUGEBENDER CHAR.-STRING,            FALLS TYP=D
.*          = ADR. DES HEXADEZIMAL AUSZUGEB. FELDES, FALLS TYP=X
.*
.*  TYP      ZULAESSIG SIND  C (=CHARACTER)
.*                           D (=DIREKTWERT)
.*                           X (=HEXADEZIMAL)
.*
.*  LAENGENBESCHRAENKUNG:  FUER TYP=C ODER D  LAENGE <133
.*                         FUER TYP=X         LAENGE <64
.*
.*  SP:  OHNE ANGABE:  ZEILENTRANSPORT VON 2
.*       SP=1:         ZEILENTRANSPORT VON 1
.*       SP=3:         ZEILENTRANSPORT VON 3
.*
         GBLB  &MSGB1
         GBLA  &MSGA5
         LCLA  &MSGA1,&MSGA2,&MSGA3,&MSGA4,&MSGA6
         AIF   (T'&LAENGE EQ 'O').FOP
         AIF   (T'&MELDUNG EQ 'O').FOP
         AIF   (T'&TYP EQ 'O').FOP
         AIF   ('&TYP' EQ 'C').A
         AIF   ('&TYP' EQ 'D').A
         AIF   ('&TYP' EQ 'X').A
.FOP     MNOTE 4,'FEHLERHAFTER OPERAND'
         MEXIT
.A       AIF   (&MSGB1).MELD
         CNOP  0,4
&NAME    B     MSG&SYSNDX UEBER DCB UND OPEN ROUTINE SPRINGEN
         AIF   ('&BLKSIZE' EQ '').A010
&MSGA4   SETA  &BLKSIZE
&MSGA5   SETA  108
         AGO   .A020
.A010    ANOP
&MSGA5   SETA  124
.* EXIT - LISTE
         DC    X'85' DCB-EXIT-ROUTINE
         DC    AL3(MSGPRINT+106) A(EXIT-ROUTINE)
.A020    ANOP
         DC    F'0' SAVE AREA FUER R14
         SPACE 1
*MSGPRINT DCB  DSORG=PS,DDNAME=&DDNAME,MACRF=(PL),RECFM=VBM,LRECL=137,
         AIF   ('&BLKSIZE' NE '').A030
*              BLKSIZE=&BLKSIZE,EXLST=MSGPRINT-8
         AGO   .A040
.A030    ANOP
*              BLKSIZE=&BLKSIZE
.A040    ANOP
MSGPRINT DS    0H
         DC    5F'0' FDAD.DVTBL,KEYLE,DEVT,TRBAL
         DC    A(1) BUFNO,BUFCB
         DC    H'0',X'4000' BUFL,DSORG
         DC    2A(1) IOBAD,BUFTEK,EODAD
         AIF   ('&BLKSIZE' NE '').A045
         DC    X'52',AL3(MSGPRINT-8) RECFM,EXLST
         AGO   .A046
.A045    DC    X'52',AL3(0) RECFM,EXLST
.A046    ANOP
         DC    CL8'&DDNAME' DDNAME
         DC    XL4'02000048' OFLGS,IFLG,MACRF
         DC    2A(1) RER1,CHECK,SYNAD
         DC    H'0,&MSGA4,0,0' CIND,BLKSIZE
         DC    3A(1) IOBA,EOB RECAD
         DC    H'0,137,0,1,0,0,0,1' QSWS,LRECL,EROPT,CNTRL,PRECL,EOB
         SPACE 1
         DC    2H'0',X'00' WSP
         DC    X'000600008B' SK1
         AIF   ('&BLKSIZE' NE '').A047
*  EXIT - ROUTINE
         LH    2,MSGPRINT+62 BLKSIZE IN REGISTER
         LTR   2,2 BLKSIZE VORHANDEN ?
         BCR   2,14 JA, RUECKSPRUNG
         LA    2,141  DEFAULT-VALUE
         STH   2,MSGPRINT+62
         BR    14 RUECKSPRUNG
*  ENDE EXIT - ROUTINE
         AGO   .A050
.A047    DS    H
.A050    ST    14,MSGPRINT-4
         TM    MSGPRINT+48,X'10' DCB SCHON EROEFFNET?
         BO    *+30              JA
*        OPEN  (MSGPRINT,(OUTPUT))
         BAL   1,*+8             OPEN MSGPRINT
         DC    AL1(143)          OPTION BYTE
         DC    AL3(MSGPRINT)     DCB ADDRESS
         SVC   19                OPEN SVC
*        PUT   MSGPRINT          VORSCHUB AUF NEUE SEITE
         LA    1,MSGPRINT        DCB
         L     15,48(,1)         PUT ROUTINE ADDR.
         BALR  14,15             AUFRUF PUT ROUTINE
         MVC   0(5,1),MSGPRINT+101 SATZLAENGENFELD UND STEUERZEICHEN
*        PUT   MSGPRINT          AUSGABEPUFFER BEREITSTELLEN
         LA    1,MSGPRINT        DCB ADRESSE
         L     15,48(,1)         ADR. DER PUT ROUTINE
         BALR  14,15             AUFRUF PUT ROUTINE
         L     14,MSGPRINT-4     RUECKSPRUNGADRESSE
         BR    14
&MSGB1   SETB  1
MSG&SYSNDX EQU *
         AGO   .B
.MELD    ANOP
&NAME    DS    0H
.B       ANOP
         AIF   ('&SP' EQ '').B010
&MSGA6   SETA  &SP
         AGO   .B020
.B010    ANOP
&MSGA6   SETA  2
.B020    MVI   MSGPRINT+100,1+8*&MSGA6
         BAL   14,MSGPRINT+&MSGA5 TESTEN, OB DCB SCHON EROEFFNET
         AIF   ('&LAENGE'(1,1) EQ '(').K REGISTER NOTATION
         AIF   ('&TYP' EQ 'X').E
&MSGA1   SETA  &LAENGE+5
         LA    14,&MSGA1          LAENGE VARIABLER SATZ
         AIF   ('&TYP' EQ 'D').C
         AIF   ('&MELDUNG'(1,1) EQ '(').H REGISTER NOTATION
         MVC   5(&LAENGE,1),&MELDUNG
         AGO   .D
.C       ANOP
         AIF   ('&MELDUNG'(1,1) EQ '''').I LITERAL
         MVC   5(&LAENGE,1),=C'&MELDUNG'
.D       STH   14,MSGPRINT+96
         MVC   0(5,1),MSGPRINT+96  SATZLAENGENFELD UND STEUERZEICHEN
         MEXIT
.E       ANOP
&MSGA1   SETA  &LAENGE
         AIF   (&MSGA1 GT 63).FOP
&MSGA2   SETA  5
         AIF   ('&MELDUNG'(1,1) EQ '(').J REGISTER NOTATION
.F       UNPK  &MSGA2.(15,1),&MELDUNG+&MSGA3.(8) UMWANDELN IN HEX.
&MSGA2   SETA  &MSGA2+14
&MSGA3   SETA  &MSGA3+7
         AIF   (&MSGA3 LT &MSGA1).F
.G       ANOP
         B     *+20
         DC    C'0123456789ABCDEF' UMWANDLUNGSTABELLE
         TR    5(&LAENGE*2,1),*-256
         LA    14,&LAENGE*2+5     SATZLAENGE
         AGO   .D
.H       MVC   5(&LAENGE,1),0&MELDUNG
         AGO   .D
.I       MVC   5(&LAENGE,1),=C&MELDUNG
         AGO   .D
.J       ANOP
         UNPK  &MSGA2.(15,1),&MSGA3.(8,&MELDUNG(1)) UMWANDELN IN HEX.
&MSGA2   SETA  &MSGA2+14
&MSGA3   SETA  &MSGA3+7
         AIF   (&MSGA3 LT &MSGA1).J
         AGO .G
.K       BCTR  &LAENGE(1),0 LAENGE-1
         AIF   ('&TYP' EQ 'X').K040
         STC   &LAENGE(1),*+5 MODIFIZIEREN LAENGENSCHLUESSEL
         AIF   ('&TYP' EQ 'D').K020
         AIF   ('&MELDUNG'(1,1) EQ '(').K010 REGISTER NOTATION
         MVC   5(0,1),&MELDUNG
         AGO   .K070
.K010    MVC   5(0,1),0&MELDUNG
         AGO   .K070
.* DIREKTMELDUNG
.K020    AIF   ('&MELDUNG'(1,1) EQ '''').K030 LITERAL
         MVC   5(0,1),=C'&MELDUNG'
         AGO   .K070
.K030    MVC   5(0,1),=C&MELDUNG
         AGO   .K070
.* HEXADEZIMALE DARSTELLUNG
.K040    LR    0,1 RETTEN R1
         SR    14,14
         AIF   ('&MELDUNG'(1,1) EQ '(').K050 REGISTER NOTATION
         LA    15,&MELDUNG
         AGO   .K060
.K050    LR    15,&MELDUNG(1)
.K060    UNPK  5(15,1),0(8,15) UMWANDLUNG IN HEX.
         LA    1,14(,1)
         LA    15,7(,15)
         LA    14,7(,14)
         CR    14,&LAENGE(1)
         BC    4,*-20
         LR    1,0
         LA    14,1(&LAENGE(1),&LAENGE(1))
         B     *+20
         DC    C'0123456789ABCDEF' UMWANDLUNGSTABELLE
         STC   14,*+5
         TR    5(0,1),*-260
.K070    LA    &LAENGE(1),1(,&LAENGE(1)) REGISTERINH. WIEDERHERSTELLEN
         AIF   ('&TYP' NE 'X').K080
         LA    14,5(&LAENGE(1),&LAENGE(1)) SATZLAENGE
         AGO   .D
.K080    LA    14,5(,&LAENGE(1)) SATZLAENGE
         AGO   .D
         MEND
