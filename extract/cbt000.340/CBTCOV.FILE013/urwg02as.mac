ALX02    CSECT
         MBASIS R3
         MSPSN COUNT=5
         USING IHADCB,R2
         USING INFMJFCB,R8
         MMACODE
         OPEN  (SALBEST,(OUTPUT))  EROEFFNEN AUSGABE BESTAND
         OPEN  (DALBEST,(INPUT))   EROEFFNEN EINGABE BESTAND
         OPEN  (AUSG,(OUTPUT))     EROEFFNEN FEHLERLISTE
         RDJFCB (SALBEST)              LESEN JOB-FILE-CONTROL-BLOCK
         RDJFCB (DALBEST)
         LA    R2,DALBEST          LADE DCB-ADRESSE
         LA    R8,ZLIST2           LADE JFCB-ADRESSE
         MVI   WIOPLA,C' '         I/O FEHLERSCHALTER PLATTE LOESCHEN
         MVI   WIOBAND,C' '        I/O FEHLERSCHALTER BAND LOESCHEN
         LA    R7,ZWBER
***********************************************************************
*        LESEN DALBEST (INDEX) UND FOLGEPRUEFUNG                      *
***********************************************************************
         SPACE 1
A1       GET   DALBEST             LOCATE-MODUS
         ST    R1,RETT1            ADR.R1 RETTEN
         LR    R9,R1               REG UMLADEN
         CLI   WIOPLA,C' '         I/O-ERROR
         BNE   B1
A11      NOP   VERG
         LH    R6,DCBRKP           RKP UEBERTRAGEN NACH
         LTR   R6,R6               TEST RKP = 0
         BZ    A12                 JA
         STC   R6,VERG+3           SPEICHERN RKP - 1 IN CLC BEFEHL
         STC   R6,VERG+5
A12      SR    R6,R6               REG.6 LOESCHEN
         IC    R6,DCBKEYLE         LADEN DCBKEYLE
         BCTR  R6,0                KEY-LENGTH - 1
         STC   R6,VERG+1           SPEICHERN DCBKEYLE - 1 IN CLC OP
         OI    A11+1,X'F0'         NOP AUF BRANCH
         B     WRITE               1. SATZ AUSGEBEN
VERG     CLC   0(0,R9),0(R7)       VERGLEICH
         BH    WRITE               SATZ SCHREIBEN
         BL    SEQ                 SORTIERFOLGE FALSCH
         BE    DOP                 DOPPELTER SATZ NACH DYNDUMP
***********************************************************************
*        AUSDRUCK DER FEHLERHAFTEN SAETZE.                            *
***********************************************************************
         SPACE 1
MOVE     ZAP   ZREFE,ZAEHL
         BAL   R14,U40             AUSDRUCKEN LETZTEN RICHTIGEN SATZ
         ZAP   ZREFE,ZAEHL
         AP    ZREFE,=P'1'
         LR    R7,R9
         BAL   R14,U40             AUSDRUCKEN FALSCHEN SATZ
         L     R9,RETT1
         LA    R8,ZLIST2
         LA    R7,ZWBER            ADRESSE ZWISCHENBREICH NEU LADEN
         B     WRITE               SCHREIBEN
***********************************************************************
*        DOPPELTE SAETZE IM BESTAND.                                  *
***********************************************************************
         SPACE 1
DOP      BAL   R14,WTO
         MVI   DBER,ZVV1           VORSCH.KANAL1
         BAL   R14,PUT
         MVI   DBER,ZTN3           ZEILENVORSCHUB
         MVC   DBER+1(57),WARN1    WARNUNG
         BAL   R14,PUT
         B     MOVE
***********************************************************************
*        AUSGEBEN SATZ NACH SALBEST (QSAM).                           *
***********************************************************************
         SPACE 1
WRITE    PUT   SALBEST,(R9)        PUT-MOVE-MODUS
         AP    ZAEHL,=P'1'
         CLI   WIOBAND,C' '        I/O-ERROR-BAND
         BNE   C1
         LH    R15,DCBLRECL        LADEB DCBLRECL
         LR    R1,R15              REG. UMLADEN
         LR    R14,R9              EINGABE SATZADRESSE UMLADEN
         LA    R0,ZWBER            ADRESSE ZWISCHENBEREICH LADEN
         MVCL  R0,R14              UEBERTRAGEN SATZ
         B     A1
***********************************************************************
*        FOLGEFEHLER DALBEST (INDEX).                                 *
***********************************************************************
         SPACE 1
SEQ      BAL   R14,WTO
         MVI   DBER,ZVV1           VORSCH.KANAL1
         BAL   R14,PUT
         MVI   DBER,ZTN3           ZEILENVORSCHUB
         MVC   DBER+1(56),WARN2    WARNUNG
         BAL   R14,PUT
         B     MOVE
***********************************************************************
*        I/O ERROR DALBEST.                                           *
***********************************************************************
         SPACE 1
B1       BAL   R14,WTO
         MVI   DBER,ZVV1           VORSCH.KANAL1
         BAL   R14,PUT
         MVI   DBER,ZTN3           ZEILENVORSCHUB
         MVC   DBER+1(57),WARN3    WARNUNG
         BAL   R14,PUT
         MVI   SCHF,X'01'          UNERFOLGREICHES ENDE
         B     ENDE
***********************************************************************
*        I/O ERROR SALBEST.                                           *
***********************************************************************
         SPACE 1
C1       BAL   R14,WTO
         MVI   DBER,ZVV1           VORSCH.KANAL1
         BAL   R14,PUT
         MVI   DBER,ZTN3           ZEILENVORSCHUB
         MVC   DBER+1(55),WARN4    WARNUNG
         BAL   R14,PUT
         MVI   SCHF,X'01'          UNERFOLGREICHES ENDE
         B     ENDE
***********************************************************************
*        P O G R A M M E N D E                                        *
***********************************************************************
         SPACE 1
ENDE     WTO   '**KGALX02**UNERFOLGREICHES ENTLADEN*JOBENDE**'
         B     ENDE2
E        CLI   SCHF,X'01'
         BE    ENDE
         WTO   '**KGALX02**ERFOLGREICHES ENTLADEN*JOBENDE**'
         MVI   DBER,ZVV1           VORSCH.KANAL1
         BAL   R14,PUT
         MVI   DBER,ZTN3           ZEILENVORSCH.
         MVC   DBER+1(47),WARN5
         BAL   R14,PUT
ENDE2    MVI   DBER+1,C'*'
         MVI   DBER,ZTN1
         MVC   DBER+2(93),DBER+1
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVI   DBER+94,C'*'
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVI   DBER+94,C'*'
         MVC   DBER+72(10),=C'ERST.DATUM'
         MVC   DBER+86(4),=C'ZEIT'
         MVC   DBER+62(6),=C'SAETZE'
         MVC   DBER+24(3),=C'VON'
         MVC   DBER+35(4),=C'NACH'
         MVC   DBER+49(5),=C'LABEL'
         MVC   DBER+4(6),=C'DSNAME'
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVI   DBER+94,C'*'
         BAL   R14,PUT
         MVC   DBER+56(12),=X'402020204B2020204B202120'
         ED    DBER+56(12),ZAEHL
         MVC   DBER+34(13),JFCBVOLS
         LA    R10,0
         LH    R10,JFCBFLSQ
         CVD   R10,LABELNR
         MVC   DBER+49(4),=X'40202120'
         ED    DBER+49(4),LABELNR+6
         MVC   DBER+74(7),=X'4020204B202020'
         TIME  DEC
         ST    R1,LABELNR+4
         ED    DBER+74(7),LABELNR+5
         ST    R0,LABELNR+4
         MVO   LABELNR,LABELNR(7)
         OI    LABELNR+7,X'0F'
         MVC   DBER+82(10),=X'402020204B20204B2020'
         ED    DBER+82(10),LABELNR+4
         LA    R8,ZLIST1                LADEN ADRESSE JFCB DALBEST
         MVC   DBER+3(24),JFCBDSNM
         MVC   DBER+23(6),JFCBVOLS
         MVI   DBER+94,C'*'
         MVI   DBER+1,C'*'
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVI   DBER+94,C'*'
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVC   DBER+2(93),DBER+1
         MVI   DBER,ZTN3
         BAL   R14,PUT
         CLOSE (SALBEST)
         CLOSE (DALBEST)
         CLOSE (AUSG)
         MRETURN RC=ZRC            PROGRAMMENDE
***********************************************************************
*        W T O BEI PROGRAMM- ODER DATENFEHLER.                        *
***********************************************************************
WTO      MUPEIN
         WTO   '**KGALX02**ACHTUNG FEHLER*NAEHERES SIEHE SYSOUT**'
         MUPAUS
***********************************************************************
*        AUSGABE FEHLERLISTE                                          *
*                                                                     *
***********************************************************************
         SPACE 1
U25      MUPEIN
PUT      EQU   U25
         PUT   AUSG,ZDBER               SATZ AUSGEBEN
         MVI   ZVSZ,ZTN1                ZTN1 EINSTELLEN
         MVI   ZDBER,C' '               DRUCKBEREICH
         MVC   ZDBER+1(L'ZDBER-1),ZDBER LOESCHEN
         MUPAUS
         TITLE 'DATEN- ODER CONTROL-BLOCKS HEX AUFBEREITEN UND AUSDRUCK*
               EN'
***********************************************************************
*                                                                     *
*        DATENSATZ HEXADIZIMAL AUFBEREITEN UND AUSGEBEN               *
*                                                                     *
*              ES WERDEN FOLGENDE REGISTERWERTE ERWARTET              *
*                                                                     *
*                   R8 = DATENDRESSE                                  *
*                   R10= DATENLAENGE                                  *
*                                                                     *
***********************************************************************
         SPACE 1
U30      MUPEIN
U30A00   LA    R5,ZDB+1
         LA    R6,4                     COUNT = 4
U30A05   C     R5,=A(ZDB+120)           TEST DRUCKZEILE VOLL
         BH    U30A10                   JA
         UNPK  0(3,R5),0(2,R8)          ENTPACKEN UND
         TR    0(2,R5),ZTRTAB-240       UEBERSETZEN
         MVI   2(R5),C' '
         BCT   R6,*+12                  BR BEI COUNT DT 0
         LA    R6,4                     COUNT = 4
         LA    R5,2(0,R5)               ADREESE DRUCKBER. ERHOEHEN
         LA    R5,2(0,R5)               ADRESSEN
         LA    R8,1(0,R8)               ERHOEHEN
         BCT   R10,U30A05               LOOP = DATENLAENGE
         C     R5,=A(ZDB+1)             TEST LEERZEILEN
         BE    U30A15                   JA
         BAL   R14,U25                  LETZTE ZEILE AUSGEBEN
         B     U30A15
U30A10   BAL   R14,U25                  ZEILE AUSGEBEN
         LA    R6,4                     COUNT = 4
         B     U30A00
U30A15   BAL   R14,U25                  LEERZEILE
         MUPAUS
         TITLE 'SNAP DER FALSCHEN SAETZE BEI FOLGEFEHLER'
***********************************************************************
*                                                                     *
*        AUFBEREITEN DER SAETZE BEI FOLGEFEHLER                       *
*                                                                     *
*              ES WERDEN FOLGENDE INFORMATION AUSGEGEBEN              *
*                                                                     *
*                   DSNAME                                            *
*                   RKP              * NUR BEI                        *
*                   KEYLEN           * ISAM-DATEIEN                   *
*                   LRECL                                             *
*                   SATZ-NR                                           *
*                   DATENSAETZ HEXADEZIMAL AUFBEREITET                *
*                                                                     *
***********************************************************************
         SPACE 1
U40      MUPEIN
         LA    R8,ZLIST2                LADE JFCB EINGABE DATEI
         MVC   ZDB+1(44),JFCBDSNM
         MVC   ZDB+46(11),=CL11'FOLGEFEHLER'
         MVC   ZDB+58(7),=CL7'SATZ-NR'
         MVC   ZDB+65(12),=X'402020204B2020204B202120'
         ED    ZDB+65(12),ZREFE+3
         TM    DCBDSORG,DCBDSGIS        TEST DCB = IS
         BNO   U40A05                   NEIN
         LH    R14,DCBRKP               RKP
         CVD   R14,ZREFE                CONVERTIEREN
         MVC   ZDB+80(5),=CL5'RKP ='    UND
         MVC   ZDB+85(4),=XL4'40202120' AUFBEREITEN
         ED    ZDB+85(4),ZREFE+6
         MVC   ZDB+92(8),=CL8'KEYLEN =' KEYLEN
         MVC   ZDB+101(4),=XL4'40202120'
         IC    R14,DCBKEYLE
         CVD   R14,ZREFE
         ED    ZDB+101(4),ZREFE+6
U40A05   LH    R14,DCBLRECL
         CVD   R14,ZREFE
         MVC   ZDB+109(7),=CL7'LRECL =' LRECL
         MVC   ZDB+116(4),=XL4'40202120'
         ED    ZDB+116(4),ZREFE+6
         MVI   ZVSZ,ZTN2
         BAL   R14,U25                  ZEILE AUSGABEN
         LH    R10,DCBLRECL
         LR    R8,R7
         BAL   R14,U30                  AUSGEBEN 1. SATZ
         MVI   ZRC+3,20                 RC = 20 EINSTELLEN
U40A95   MUPAUS
         TITLE 'Q S A M - S Y N A D - R O U T I N E'
FBAND    MSYNAD QSAM,WIOBAND
         TITLE 'Q I S A M - S Y N A D - R O U T I N E'
FISAM    MSYNAD QISAM,WIOPLA,,DALBEST
         TITLE 'D E F I N I T I O N E N'
RETT1    DS    F
DBER     DS    0CL133
ZDBER    DS    0CL133
ZVSZ     DC    AL1(ZTN1)
ZDB      DC    CL132' '
ZREFE    DC    D'12'
ZRC      DC    F'0'                     RETURN-CODE
ZTRTAB   DC    C'0123456789ABCDEF'
ZAEHL    DC    PL5'0'
WIOBAND  DS    X'00'
WIOPLA   DC    X'00'
WARN1    DC    CL57'A C H T U N G...DOPPELTE SAETZE IM BESTAND...KEIN JC
               OBENDE'
WARN2    DC    CL56'A C H T U N G...SORTIERFEHLER IM BESTAND...KEIN JOBC
               ENDE'
SCHF     DC    X'00'
WARN3    DC    CL57'A C H T U N G...INPUT / OUTPUT - ERROR - PLATTE...JC
               OBENDE'
WARN4    DC    CL55'A C H T U N G...INPUT / OUTPUT - ERROR - BAND...JOBC
               ENDE'
WARN5    DC    CL47'KGALX02 ... ERFOLGREICHES ENDE ... PROGRAMMENDE'
         DS    0D
LABELNR  DC    XL8'C'
EXLIST1  DC    AL1(135),AL3(ZLIST1)
ZLIST1   DC    CL176' '            JFCB DALBEST
EXLIST2  DC    AL1(135),AL3(ZLIST2)
ZLIST2   DC    CL176' '            JFCB SALBEST
         LTORG
DALBEST  DCB   DDNAME=DALBEST,DSORG=IS,MACRF=(GL),SYNAD=FISAM,EODAD=E, X
               EXLST=EXLIST1
SALBEST  DCB   DDNAME=SALBEST,DSORG=PS,MACRF=(PM),SYNAD=FBAND,         X
               EXLST=EXLIST2
AUSG     DCB   DDNAME=AUSG,DSORG=PS,MACRF=(PM),LRECL=121,              *
               BLKSIZE=121,RECFM=FM,BUFNO=2
ZWBER    DS    CL2000              ZW. BEREICH FUER UNGEBLOCKTE DATEN
         DCBD  DSORG=(IS,PS),DEVD=(DA,TA)
JFCB     DSECT
         IEFJFCBN LIST=YES
         END
