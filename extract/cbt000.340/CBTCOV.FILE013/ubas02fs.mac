ZS905RWD CSECT
         REG
         XSAVE (R11,R12),SVA,ZS905RWD ZIMPELMANN
         L     R3,0(R1)
         LA    R3,0(R3)                LOESCHEN VL-BIT
         USING Y1,R3
         CLI   Y1TYPF,Y1TIC            IMAGECOPY ??????
         BE    IMAGCOP
         CLI   Y1AFLG,Y1AVS             VSAM ?????????
         BE    VSAMOPEN
         LA    R7,SYSUT1DB
         USING IHADCB,R7
         TM    DCBOFLGS,DCBOFOPN
         BO    NOOPEN
         OPEN  (SYSUT1DB,(UPDAT))
         TM    DCBOFLGS,DCBOFOPN
         BO    GEOPEN
         LA    R9,66                  DD-KARTE FUER DATENBANK FEHLT
         B     FEHLER
GEOPEN   EQU   *
         RDJFCB (SYSUT1DB)
         LA    R9,JFCBAR
         ST    R9,Y1ADAT              SPEICHERN ADRESSE DATASET
         MVC   Y1BLKS,DCBBLKSI
         LH    R6,Y1BLKS            SPEICHERN BLOCKSIZE
         LTR   R6,R6                BLOCKSIZE = 0 ?
         BNZ   GETMAIN1             NEIN
         LA    R9,78                BLOCKSIZE = 0
         B     FEHLER
GETMAIN1 EQU   *
         GETMAIN R,LV=(R6)
         ST    R1,Y1ADDB            SPEICHERN ADRESSE DATENBANKBLK
         B     ENDE
         SPACE 2
VSAMOPEN EQU   *
         LA    R7,ACB1
         TM    ACBOFLGS-IFGACB(R7),ACBOPEN
         BO    VSAMREAD
         OPEN  (ACB1)
         TM    ACBOFLGS-IFGACB(R7),ACBOPEN
         BO    OPENED
         LA    R9,66                  DD-KARTE FUER DATENBANK FEHLT
         B     FEHLER
OPENED   EQU   *
         RDJFCB SYSUT1DB
         LA    R9,JFCBAR
         ST    R9,Y1ADAT
         MVC   DSN,JFCBDSNM-INFMJFCB(R9)
         LA    R2,AREA
         USING SHWOUT,R2
         MVC   SHWLEN1,HHAREAL
SHOWCAT  SHOWCAT AREA=AREA,NAME=DSN
         LTR   R15,R15
         BZ    NOFEHL5
         ST    R15,Y1SR15
         LA    R9,73
         B     FEHLER
NOFEHL5  EQU   *
         CLI   SHWTYPE,C'C'
         BNE   GETBLKS
         LA    R10,SHWASS0
         USING SHWASSOC,R10
         MVC   CIADR,SHWACI
         MVI   SHWTYPE,C'D'
         SHOWCAT AREA=AREA,CI=CIADR
         LTR   R15,R15
         BZ    GETBLKS
         ST    R15,Y1SR15
         LA    R9,74
         B     FEHLER
GETBLKS  EQU   *
         LH    R6,SHWCISZ+2
         STH   R6,Y1BLKS
         LTR   R6,R6                BLOCKSIZE = 0 ?
         BNZ   GETMAIN2             NEIN
         LA    R9,78                BLOCKSIZE = 0
         B     FEHLER
GETMAIN2 EQU   *
         GETMAIN R,LV=(R6)
         ST    R1,Y1ADDB
GET      GENCB BLK=RPL,AM=VSAM,COPIES=1,ACB=ACB1,ARG=RBA,              *
               OPTCD=(CNV,UPD,DIR,MVE),AREA=(R1),AREALEN=(R6),         *
               MSGAREA=MESSAGE,MSGLEN=128
         ST    R1,GEPUADR
         LTR   R15,R15
         BZ    ENDE
         ST    R15,Y1SR15
         LA    R9,76                   FEHLER IM VSAM-GENCB
         B     FEHLER
         EJECT
NOOPEN   EQU   *
         CLI   Y1DATA,Y1READ
         BNE   WRTROUT
         L     R4,Y1RBLK
         L     R6,Y1ADDB
         BCTR  R4,0
         ST    R4,RELBLKNO
         READ  DECB1,DI,,(R6),,,,,MF=E
         CHECK DECB1
         LA    R8,DECB1
         USING DECB,R8
         CLI   DECCC2,X'00'            READ VOLLSTAENDIG ???????
         BE    ENDE
         TM    DECCC3,DECCCNBK         BLOCK OUTSIDE DATASET?
         BNO   BLKIN
         LA    R9,77                  BLOCK OUTSIDE DATASET
         B     FEHLER
BLKIN    EQU   *
         TM    DECCC2,DECCCEOD         END OF DATA ??
         BNO   DATEND
         LA    R9,61
         B     FEHLER
DATEND   EQU   *
         LA    R9,62
         B     FEHLER
         SPACE 2
VSAMREAD EQU   *
         CLI   Y1DATA,Y1READ
         BNE   VSAMWRT
         L     R4,Y1RBLK
         MH    R4,Y1BLKS
         ST    R4,RBA
         L     R9,GEPUADR
         GET   RPL=(R9)
         LTR   R15,R15
         BZ    ENDE
         ST    R15,Y1SR15
         LA    R9,63                  FEHLER BEIM VSAM-GET
         B     FEHLER
         EJECT
WRTROUT  EQU   *
         CLI   Y1DATA,Y1WRIT
         BNE   CLOSDAT
         L     R4,Y1WBLK
         L     R5,Y1ADDB
         BCTR  R4,0
         ST    R4,RELBLKNO
         WRITE DECB2,DI,SYSUT1DB,(R5),'S',0,RELBLKNO+1
         CHECK DECB2
         LA    R8,DECB2
         CLI   DECCC2-DECB(R8),X'00'
         BE    ENDE
         LA    R9,64                 WRITE UNVOLLSTAENDIG
         B     FEHLER
         SPACE 2
VSAMWRT  EQU   *
         CLI   Y1DATA,Y1WRIT
         BNE   VSAMEND
         L     R4,Y1WBLK
         L     R9,GEPUADR
         PUT   RPL=(R9)
         LTR   R15,R15
         BZ    ENDE
         ST    R15,Y1SR15
         LA    R9,65                 FEHLER BEIM VSAM-PUT
         B     FEHLER
         EJECT
CLOSDAT  EQU   *
         L     R9,Y1ADDB              ADRESSE GETMAIN LADEN
         LH    R6,Y1BLKS              BLKSIZE LADEN
         FREEMAIN R,LV=(R6),A=(R9)
         CLOSE (SYSUT1DB)
         B     ENDE
VSAMEND  EQU   *
         L     R9,Y1ADDB              ADRESSE GETMAIN LADEN
         LH    R6,Y1BLKS              BLKSIZE LADEN
         FREEMAIN R,LV=(R6),A=(R9)
         CLOSE (ACB1)
         B     ENDE
         SPACE 1
ERRANAL  EQU   *
         SYNADAF ACSMETH=BDAM
         ST    R14,SAVE14
         LA    R5,AREA2
         MVC   1(78,5),50(1)
         CALL  TDATE,(ZEIT,DATUM)
         AP    Y1SEIZ,=P'+1'
         MVC   SEITNR,MASKE
         ED    SEITNR,Y1SEIZ
         CALL  ZS905WRF,((R3),ZEILE1),VL
         CALL  ZS905WRF,((R3),ZEILE2),VL
         CALL  ZS905WRF,((R3),ZEILE3),VL
         MVI   Y1FUEB,Y1FYES
         CALL  ZS905WRF,((R3),AREA1),VL
         SYNADRLS
         L     R14,SAVE14
         RETURN
         SPACE 2
         EJECT
VSAMERR  EQU   *
         USING *,R15
         STM   R14,R12,SVA1+12          REGISTER SAVE
         LA    R14,SVA1                 ADRESSE 1.SAVE-AREA
         ST    R14,8(R13)               VERKETTEN LOW-SA
         ST    R13,4(R14)               VERKETTEN HIGH-SA
         LA    R13,SVA2                 ADRESSE 2.SAVE-AREA
         ST    R13,8(R14)               VERKETTEN LOW-SA
         ST    R14,4(R13)               VERKETTEN HIGH-SA
         LR    R10,R15
         DROP  R15
         USING VSAMERR,R10
         LA    R5,AREA2
         LR    R7,R1                    ADRESSE RPL
         L     R6,RPLERMSA-IFGRPL(R7)   ADRESSE MESSAGE
         LH    R2,RPLEMLEN-IFGRPL(R7)   LAENGE  MESSAGE
         BCTR  R2,0
         EX    R2,EXMVC1                MVC 1(0,R5),0(R6)
         CALL  TDATE,(ZEIT,DATUM)
         AP    Y1SEIZ,=P'+1'
         MVC   SEITNR,MASKE
         ED    SEITNR,Y1SEIZ
         CALL  ZS905WRF,((R3),ZEILE1),VL
         CALL  ZS905WRF,((R3),ZEILE2),VL
         CALL  ZS905WRF,((R3),ZEILE3),VL
         MVI   Y1FUEB,Y1FYES
         CALL  ZS905WRF,((R3),AREA1),VL
         DROP  R10
         L     R13,4(R13)               ADRESSE 1.SAVE-AREA
         LM    R14,R12,12(R13)          RESTORE REGISTER
         L     R13,4(R13)               ADRESSE HIGH SA
         LA    R15,0
         BR    R14
SVA1     DS    18F
SVA2     DS    18F
         EJECT
IMAGCOP  EQU   *
         LA    R7,SYSUT1IC
         TM    DCBOFLGS-IHADCB(R7),DCBOFOPN
         BO    IMAGREAD
         OPEN  (SYSUT1IC,(INPUT))
         TM    DCBOFLGS-IHADCB(R7),DCBOFOPN
         BO    ICDOPEN
         LA    R9,70
         B     FEHLER
ICDOPEN  EQU   *
         ST    R7,BLKADR
         OPEN  (ICDOUT,(OUTPUT))
         LA    R6,ICDOUT
         TM    DCBOFLGS-IHADCB(R6),DCBOFOPN  DD-KARTE FUER ICDOUT DA?
         BO    GEOEFF
         LA    R9,NOAUSG               ADRESSE FUER KEINE AUSGABE
         ST    R9,DCBPUT-IHADCB(R6)    PUTADRESSE UEBERSPEICHERN
GEOEFF   EQU   *
         RDJFCB SYSUT1IC
         LA    R9,JFCBAR1
         ST    R9,Y1ADAT
         GET   SYSUT1IC
         LR    R7,R1
         LH    R1,DORECOUT-DUMPHDR(R7)
         CLI   Y1AFLG,Y1AOS
         BE    ICGBLKS
         LA    R1,7(R1)
ICGBLKS  EQU   *
         STH   R1,Y1BLKS
         PUT   ICDOUT,(R7)
         CLI   Y1AFLG,Y1AOS
         BE    ENDE
         GET   SYSUT1IC
         LR    R7,R1
         PUT   ICDOUT,(R7)         VSAM----BLKNR=0
         B     ENDE
IMAGREAD EQU   *
         CLI   Y1DATA,Y1READ
         BNE   ICDWRT
         L     R4,Y1RBLK
         L     R8,ICDRBA
         CLI   Y1AFLG,Y1AOS
         BNE   ICDRVSAM
BLKLESEN EQU   *
         LA    R8,1(R8)
         GET   SYSUT1IC
         LR    R7,R1
         C     R8,OUTBFRBN-DUMPHDR(R7)
         BE    NOFEHL1
         LA    R9,72                BLOCK FEHLT IM IC-DUMP
         B     FEHLER
NOFEHL1  EQU   *
         CR    R4,R8
         BE    BLKDRK
         CLI   Y1AEND,Y1AEYE
         BE    BLKLESEN
         PUT   ICDOUT,(R7)
         B     BLKLESEN
         SPACE 2
ICDRVSAM EQU   *
         LH    R9,Y1BLKS
         MH    R4,Y1BLKS
BLKREAD  EQU   *
         AR    R8,R9
         GET   SYSUT1IC
         LR    R7,R1
         C     R8,OUTBFRBN-DUMPHDR(R7)
         BE    NOFEHL2
         LA    R9,72                BLOCK FEHLT IM IC-DUMP
         B     FEHLER
NOFEHL2  EQU   *
         CR    R4,R8
         BE    BLKDRK
         CLI   Y1AEND,Y1AEYE
         BE    BLKREAD
         PUT   ICDOUT,(R7)
         B     BLKREAD
         SPACE 2
BLKDRK   EQU   *
         ST    R8,ICDRBA
         CLI   Y1CHNG,Y1CHNO
         BE    NOUPDAT
         ST    R7,BLKADR
         B     LESEND
NOUPDAT  EQU   *
         PUT   ICDOUT,(R7)
LESEND   EQU   *
         LA    R7,8(R7)
         ST    R7,Y1ADDB
         B     ENDE
         SPACE 2
ICDWRT   EQU   *
         CLI   Y1DATA,Y1WRIT
         BNE   IMAGEND
         L     R4,Y1WBLK
         MVI   Y1CHNG,Y1CHNO
         CLI   Y1AEND,Y1AEYE
         BE    ENDE
         L     R8,BLKADR
         PUT   ICDOUT,(R8)
         B     ENDE
         SPACE 2
IMAGEND  EQU   *
         MVI   ICENDABF,C'1'
         CLC   Y1COCO,=H'8'
         BE    DATCLOS
         L     R8,ICDRBA
         CLI   Y1AFLG,Y1AOS
         BNE   IMVSEND
LESENDE  EQU   *
         LA    R8,1(R8)
         GET   SYSUT1IC
         LR    R7,R1
         C     R8,OUTBFRBN-DUMPHDR(R1)
         BE    NOFEHL3
         LA    R9,72
         B     FEHLER
NOFEHL3  EQU   *
         CLI   Y1AEND,Y1AEYE
         BE    LESENDE
         PUT   ICDOUT,(R7)
         B     LESENDE
         SPACE 2
IMVSEND  EQU   *
         LH    R9,Y1BLKS
READEND  EQU   *
         AR    R8,R9
         GET   SYSUT1IC
         LR    R7,R1
         C     R8,OUTBFRBN-DUMPHDR(R1)
         BE    NOFEHL4
         LA    R9,72
         B     FEHLER
NOFEHL4  EQU   *
         CLI   Y1AEND,Y1AEYE
         BE    READEND
         PUT   ICDOUT,(R7)
         B     READEND
         SPACE 2
ICDEND   EQU   *
         CLI   ICENDABF,C'1'
         BE    DATCLOS
         LA    R9,75               BLOECKE AUSSERHALB DATASET
         B     FEHLER
DATCLOS  EQU   *
         CLOSE (SYSUT1IC,,ICDOUT)
         B     ENDE
         EJECT
FEHLER   EQU   *
         STH   R9,Y1MSGC
         CALL  ZS905FHL,((R3)),VL
ENDE     EQU   *
         XRETURN 0
         SPACE 2
OPENEXIT DS    0H
         USING *,R15
         CLC   DCBBLKSI-IHADCB(2,R1),=H'0'      BLOCKSIZE=0
         BNE   OPENRET
         L     R7,BLKADR                ADRESSE EINGABE-ICD
         MVC   DCBBLKSI-IHADCB(L'DCBBLKSI,R1),DCBBLKSI-IHADCB(R7)
         MVC   DCBLRECL-IHADCB(L'DCBLRECL,R1),DCBLRECL-IHADCB(R7)
OPENRET  EQU   *
         DROP  R15
         BR    R14
         SPACE 2
*     BEI FEHLENDER DD-KARTE FUER ICDOUT WIRD DIESE ROUTINE
*                      ANGESPRUNGEN
NOAUSG   EQU   *
         BR    R14
         EJECT
SYSUT1DB DCB   DDNAME=SYSUT1,DSORG=DA,MACRF=(RIC,WIC),                 *
               OPTCD=RW,SYNAD=ERRANAL,EXLST=EXLST
EXLST    DS    0F
         DC    XL1'87'
         DC    AL3(JFCBAR)
JFCBAR   DS    CL176
         EJECT
SYSUT1IC DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),                      *
               RECFM=FB,EODAD=ICDEND,EXLST=EXLST1
EXLST1   DS    0F
         DC    XL1'87'
         DC    AL3(JFCBAR1)
JFCBAR1  DS    CL176
         EJECT
ICDOUT   DCB   DDNAME=ICDOUT,DSORG=PS,MACRF=(PM),                      *
               RECFM=FB,EXLST=EXLIST
EXLIST   DS    0F
         DC    XL1'85'
         DC    AL3(OPENEXIT)
         EJECT
ACB1     ACB   AM=VSAM,BUFND=2,BUFNI=1,DDNAME=SYSUT1,                  *
               MACRF=(CNV,OUT,DIR),EXLST=EXIT
EXIT     EXLST SYNAD=VSAMERR,                                          *
               LERAD=VSAMERR
         EJECT
         READ  DECB1,DI,SYSUT1DB,,'S',0,RELBLKNO+1,,MF=L
GEPUADR  DS    F
RBA      DS    F
ICENDABF DC    CL1'0'
         DS    0D
CIADR    DS    CL3
MESSAGE  DS    0CL128
MESS     DC    C'BLOECKE LIEGEN AUSSERHALB DATASET  '
         DC    CL93' '
         DS    0D
AREA     DC    512XL1'00'
HHAREAL  DC    AL2(*-AREA)
DSN      DC    CL44' '
RELBLKNO DS    F
SAVE14   DS    F
BLKADR   DS    F
ICDRBA   DC    F'0'
AREA1    DS    0CL133
         DC    CL1'-'
AREA2    DC    CL132' '
MASKE    DC    XL6'402020202120'
         SPACE 2
ZEILE1   DS    0CL133
         DC    CL1'1'
         DC    C'ZS905 L01     DB-DUMP-UTILITY     '
DATUM    DC    CL10' '
         DC    CL5' '
ZEIT     DC    CL8' '
         DC    C'     SEITE'
SEITNR   DC    CL6' '
         DC    CL59' '
         SPACE 2
ZEILE2   DS    0CL133
         DC    CL1'-'
         DC    C' FEHLERMELDUNG,COND.-CODEAUSGABE : '
         DC    CL98' '
         SPACE 2
ZEILE3   DS    0CL133
         DC    CL1' '
         DC    C'---------------------------------- '
         DC    CL98' '
EXMVC1   MVC   1(0,R5),0(R6)
         LTORG
         EJECT
Y1       ZS905COM
         SPACE 2
DECB     DSECT
         DS    CL1
DECCC2   DS    CL1
DECCCEOD EQU   X'04'
DECCC3   DS    CL1
DECCCNBK EQU   X'10'
         DS    CL25
         EJECT
         PRINT NOGEN
         IGGSHWPL
         EJECT
         IEFJFCBN
         EJECT
         IFGRPL
         EJECT
         DCBD DSORG=PS
         EJECT
         DFSUDHDR
         IFGACB
         END
