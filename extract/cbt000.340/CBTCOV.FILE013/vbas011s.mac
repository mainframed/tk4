         TITLE 'SPCCTRL - TSO DASD SPACE CONTROL'
SPCCTRL  START
*  THIS TSO-COMMAND IS USED TO CONTROL THE DASD-SPACE USAGE
*  OF EACH INDVIDUAL TSO USER.
* THE MAXIMUM ALLOWABLE AMOUNT OF DASD-STORAGE IS RECORDED IN A
* SPECIAL DATASET, WHICH MUST BE DEFINED IN THE LOGON-PROC.
* ORGANISATION OF THE DATASET IS PARTITIONED, WITH EACH USERID
* BEING A MEMBER-NAME OF THIS DATASET.
* THE FORMAT OF EACH MEMBER IS DESCRIBED IN THE DSECT NAMED
* TSOPRM.
* THE DDNAME OF THE DATASET MUST BE TSOPARM.
*
* THE COMMAND SPCCTRL SHOULD BE EXECUTED AS THE FIRST COMMAND OF
* EACH SESSION VIA THE PARM-FIELD OF THE TMP.
*
* AUTHOR: J. SOLTAU
* INSTALLATION: BASF AG, LUDWIGSHAFEN
* DATE WRITTEN: DEC., 1975
*
*
* PROGRAM INITIALISATION
*
         REG
         XSAVE R12,,SPCCTRL,DSAL
         USING DSA,R13
         USING TSOPRM,R10
         USING CTGPLD,R9
         USING ENTRY,R11
         LA    R2,CLEAR       INIT R2 FOR MVCL
         LA    R3,CLEARL      INIT R3 FOR MVCL
         SR    R4,R4          INIT R4 FOR MVCL
         SR    R5,R5          INIT R5 FOR MVCL
         MVCL  R2,R4          CLEAR WORKING STORAGE TO ALL X'00'
         L     R1,0(R1)
         CLC   2(4,R1),=CL4'LIST'
         BNE   NOLIST
         OI    SWITCH,LIST
         B     PRODLIST
NOLIST   EQU   *
         ENQ   (MAJNM,MINNM,E,,SYSTEMS),RET=TEST
         LTR   R15,R15
         BZ    PRODLIST
         TPUT  MSG11,27
         B     SET16
PRODLIST EQU   *
         L     R3,16
         L     R3,0(R3)
         L     R3,4(R3)
         L     R3,12(R3)
         MVC   USERID,0(R3)
         CLC   16(8,R3),=CL8'IKJACCNT'
         BNE   TSTAV
RETCD2   MVC   RETCD,=F'2'
         B     OK
TSTAV    EQU   *
         CLC   16(8,R3),=CL8'IKJACCVM'
         BE    RETCD2
         CLC   16(8,R3),=CL8'KXJACCNT'
         BE    RETCD2
         CLC   16(8,R3),=CL8'IKJACCAV'
         BNE   SET16
         MVC   RETCD,=F'1'
         B     OK
SET16    EQU   *
         MVC   RETCD,=F'16'
         B     XRET
OK       EQU   *
         LA    R5,USERID
         LA    R4,8
LEND     EQU   *
         CLI   0(R5),C' '
         BE    USEND
         LA    R5,1(R5)
         BCT   R4,LEND
         B     SET16
USEND    EQU   *
         LA    R3,USERID
         SR    R5,R3
         ST    R5,USIDL   SAVE LENGTH OF USERID
         CALL  DYNALC,(P1,P2,P3,P4,0,P6),VL
         MVC   DCB,TSOPARM    MOVE DCB TO WORKAREA
         MVI   OPENL,X'80'
         OPEN (DCB,(INPUT)),MF=(E,OPENL)   OPEN TSOPARM-DATASET
         TM    DCB+48,X'10'   WAS OPEN SUCESSFUL?
         BNO   NOTOPEN        NO - TERMINATE SESSION
         MVI   FF+1,X'01'  PREPARE LIST FOR BLDL
         MVI   LL+1,X'3C'
         BLDL  DCB,BLDLL  ISSUE BLDL
         LTR   R15,R15        MEMBER FOUND?
         BZ    BLDLOK
         XC    OPENL(4),OPENL
         MVI   OPENL,X'80'
         CLOSE (DCB),MF=(E,OPENL)
         B     NOTOPEN
BLDLOK   EQU   *
         LOAD  DE=USERID,DCB=DCB
         LR    R10,R0  SAVE ADDRESS OF MEMBER
         LOAD  EP=TSONEWS,DCB=DCB
         ST    R0,TSONEWS
         XC    OPENL(4),OPENL
         MVI   OPENL,X'80'
         CLOSE (DCB),MF=(E,OPENL)
         LA    R9,CTGPLL
         XC    CTGPL,CTGPL   CLEAR CATALOG PARAMETER LIST
         GETMAIN R,LV=10904
         ST    R1,CTGWKA
         MVC   0(2,R1),=H'4500'
         AH    R1,=H'4504'
         ST    R1,TABLE  SAVE ADDRESS OF OUR TABLE
         OI    CTGOPTN1,CTGNAME+CTGGENLD   PREPARE CATALOG PARM-LIST
         OI    CTGOPTN3,CTGAM0+CTGSUPLT
         LA    R1,USERID
         A     R1,USIDL
         L     R15,USIDL
         LA    R15,1(R15)
         STC   R15,LL+1
         MVI   0(R1),C'.'   INSERT '.' FOR GENERIC LOCATE
         MVC   PREFIX,USERID
         ST    R1,USIDL
         LA    R1,LL+1
         ST    R1,CTGENT  COMPLETE CATALOG PARM-LIST
         CATALOG CTGPL   ISSUE GENERIC LOCATE-REQ.
         L     R1,USIDL
         MVI   0(R1),X'40'   REMOVE '.' FROM USERID
         L     R2,CTGWKA      POINT TO CATALOG WORK AREA
         LH    R3,2(R2)       LOAD ACTUAL LENGTH
         CH    R3,=H'49'
         BL    NEWS
         SH    R3,=H'4'       SUBTRACT 4 BYTES FOR LENGTH-FIELD
         L     R11,TABLE   ESTABLISH ADDRESSABILITY FOR OWN TABLE
         LA    R2,4(R2)   SKIP OVER LENGTH-FIELD
MOVE     EQU   *     CREATE OWN TABLE
         MVC   TYPE(45),0(R2)
         LA    R11,64(R11)  POINT TO NEXT ENTRY
         LA    R2,45(R2)    POINT TO NEXT ENTRY
         SH    R3,=H'45'    REDUCE REMAINING LENGTH
         LTR   R3,R3       CATALOG ANSWER AREA EXHAUSTED?
         BNZ   MOVE        NO, CONTINUE MOVE
         MVI   TYPE,X'FF'  INDICATE END OF TABLE
*
* NOW PREPARE FOR SPECIFIC LOCATE
*
         MVI   CAMLST,X'44'
         MVC   CAMLST+12(4),CTGWKA
         L     R11,TABLE
         L     R3,CTGWKA
LOCLOOP  EQU   *
         LA    R1,1(R11)
         ST    R1,CAMLST+4   INSERT ADDRESS OF DSNAME
         LOCATE CAMLST
         LTR   R15,R15        LOCATE SUCCESFULL?
         BNZ   CATERR         NO - TERMINATE SESSION
         MVC   DEVTYPE(10),2(R3)  MOVE DEVICE-TYPE AND VOLSER
         LA    R11,64(R11)  POINT TO NEXT TABLE ENTRY
         CLI   TYPE,X'FF'   IS IT LAST ENTRY?
         BNE   LOCLOOP      NO, CONTINUE LOCATE LOOP
*
*  NOW OBTAIN DSCBS
*
         MVI   CAMLST,X'C1'
         MVC   CAMUNCAT(2),=X'0C04'
         MVI   CAMSEEK,X'C0'
         MVI   CAMSEEK+1,X'80'
         LA    R2,512(R3)
         ST    R2,CAMSEEK+12 INSERT WORK-AREA ADDRESS
         L     R11,TABLE
OBTLOOP  EQU   *
         LA    R1,DSNAME
         ST    R1,CAMLST+4
         LA    R1,VOLSER
         ST    R1,CAMLST+8
         OBTAIN CAMLST  READ DSCB
         LTR   R15,R15
         BZ    NOUNCAT
         CH    R15,=H'8'
         BE    UNCAT
         MVI   TYPE,X'00'   SET DATASET UNAVAILABLE
         B     NEXTDS
UNCAT    EQU   *
*  ALL DATASETS FOUND IN THE CATALOG, BUT NOT ON THEIR VOLUME
*  WILL BE UNCATALOGED.
*
         MVI   TYPE,X'00' SET DATASET UNAVAILABLE
         LA    R1,DSNAME
         ST    R1,CAMUNCAT+4
         CATALOG CAMUNCAT
         LTR   R15,R15        UNCAT OK?
         BNZ   CATERR         NO - TERMINATE SESSION
NOUNCAT  EQU   *
*
*  NOW OBTAIN THE SPACE ALLOCATED FOR THE DATASET
*
         BAL   R14,GETSPACE
NEXTDS   EQU   *
         LA    R11,64(R11)
         CLI   TYPE,X'FF'
         BNE   OBTLOOP
*
*  NOW CHECK THE SUM OF ALLOCATED TRACKS AGAINST THE ALLOWABLE MAXIMUM
*
         L     R15,MAXSPACE
         MH    R15,=H'19'   CONVERT TO TRACKS
         TM    SWITCH,LIST
         BO    NEWLIST
         C     R15,SUMTRKS   COMPARE AGAINST MAX.
         BL    PRTTBL         TOO MUCH ALLOCATED - PRINT TABLE
*
*  NOW PUT OUT TSO-NEWS
*
NEWS     EQU   *
         TM    SWITCH,LIST
         BO    RETURN
         L     R2,TSONEWS
         L     R3,0(R2)
         LTR   R3,R3
         BZ    NONEWS
         LA    R2,4(R2)   SKIP OVER LENGTH FIELD
NEXTNEWS EQU   *
         TPUT  (R2),70
         LA    R2,70(R2)   INCR. TO NEXT LINE
         BCT   R3,NEXTNEWS
         B     RETURN
NONEWS   EQU   *
         TPUT  NOMSG,70
RETURN   EQU   *
         DELETE EP=TSONEWS
         DELETE EPLOC=USERID
         FREEMAIN R,LV=10904,A=CTGWKA
XRET     EQU   *
         XRETURN RETCD,R
*
*
NOTOPEN  EQU   *
         TPUT  NOTOP,70
         B     SET16
*
CATERR   EQU   *
         TPUT  CATEMSG,70
TERMFLG1 EQU   *
         MVC   RETCD,=F'16'
         B     RETURN
*
OBTERR   EQU   *
         TPUT  OBTEMSG,70
         B     TERMFLG1
*
*  TOO MUCH SPACE IS ALLOCATED - PRINT OUT TABLE OF ALL
*  DATASETS AND REQUEST FREEING.
*
PRTTBL   EQU   *
         L     R1,USIDL
         LA    R2,USERID
         SR    R1,R2
         LA    R1,1(R1)
         ST    R1,USIDL
         MVI   DCB,X'40'
         MVC   DCB+1(95),DCB
         TPUT  MSG1,70
NEWLIST  EQU   *
         MVC   MSGSAV,MSG2
         L     R1,MAXSPACE
         CVD   R1,DBLWD
         UNPK  MSGSAV+16(3),DBLWD
         OI    MSGSAV+18,X'F0'
         MH    R1,=H'19'
         ST    R1,MAXTRKS
         CVD   R1,DBLWD
         UNPK  MSGSAV+27(4),DBLWD
         OI    MSGSAV+30,X'F0'
         L     R2,SUMTRKS
         CVD   R2,DBLWD
         UNPK  MSGSAV+67(4),DBLWD
         OI    MSGSAV+70,X'F0'
         LA    R1,MSGSAV
         TPUT  (1),80
         TM    SWITCH,LIST
         BO    PRINT
         L     R1,MAXTRKS
         SR    R2,R1
         MVC   MSGSAV,MSG4
         CVD   R2,DBLWD
         UNPK  MSGSAV+56(4),DBLWD
         OI    MSGSAV+59,X'F0'
         LA    R1,MSGSAV
         TPUT  (1),70
         TPUT  MSG3,80
         TPUT  MSG3A,70
PRINT    L     R11,TABLE
         MVC   MSGSAV,MSG6
NXTTBLL  EQU   *
         CLI   TYPE,X'FF'
         BE    ENDTBL
         CLI   TYPE,X'00'
         BE    NXTELMT
         L     R1,SPCALLOC
         CVD   R1,DBLWD
         UNPK  MSGSAV(4),DBLWD
         OI    MSGSAV+3,X'F0'
         MVC   MSGSAV+26(44),DSNAME
         LA    R1,MSGSAV
         TPUT  (1),70
NXTELMT  EQU   *
         LA    R11,64(R11)
         B     NXTTBLL
ENDTBL   EQU   *
         TM    SWITCH,LIST
         BO    RETURN
         NI    SWITCH,255-RLSE
         LA    R1,ANSWER
         TGET  (1),44
         LTR   R15,R15
         BZ    TGETOK
         TPUT  MSG9,50
         B     ENDTBL
TGETOK   EQU   *
         OC    ANSWER,BLANKS
         CLC   ANSWER(4),=CL4'END'
         BE    TERMFLG1
         CLC   ANSWER(8),=CL8'NEWLIST'
         BE    NEWLIST
         MVC   DCB(8),PREFIX
         LA    R2,DCB
         A     R2,USIDL
         CLC   ANSWER(2),=C'R,'
         BNE   NORLS
         OI    SWITCH,RLSE
         MVC   0(44,R2),ANSWER+2
         B     LTAB
NORLS    EQU   *
         MVC   0(44,R2),ANSWER
LTAB     L     R11,TABLE
SRCHTBL  EQU   *
         CLI   TYPE,X'FF'
         BE    ENDSRCH
         CLI   TYPE,X'00'
         BE    INCSRCH
         CLC   DSNAME,DCB
         BE    SRCHFND
INCSRCH  EQU   *
         LA    R11,64(R11)
         B     SRCHTBL
ENDSRCH  EQU   *
         TPUT  MSG10,70
         B     ENDTBL
SRCHFND  EQU   *
         TM    SWITCH,RLSE
         BO    RELEASE
         LA    R1,DSNAME
         ST    R1,CAMUNCAT+4
         CATALOG CAMUNCAT
         LTR   R15,R15
         BZ    UNCATOK
PRTMSG8  EQU   *
         TPUT  MSG8,70
         B     ENDTBL
UNCATOK  EQU   *
         MVC   CAMSCR(4),=XL4'41005000'
         LA    R1,DSNAME
         ST    R1,CAMSCR+4
         LA    R1,VOLLIST
         ST    R1,CAMSCR+12
         MVI   VOLLIST+1,X'01'
         MVC   VOLLIST+2(4),DEVTYPE
         MVC   VOLLIST+6(6),VOLSER
         SR    R0,R0
         SCRATCH CAMSCR
         LTR   R15,R15
         BNZ   PRTMSG8
         L     R1,SUMTRKS
         S     R1,SPCALLOC
         ST    R1,SUMTRKS
         MVI   TYPE,X'00'
         MVC   MSGSAV,MSG5
         MVC   MSGSAV+8(44),DSNAME
         LA    R1,MSGSAV
         TPUT  (1),70
XXX      CLC   SUMTRKS,MAXTRKS
         BNH   NEWS
         TPUT  MSG7,70
         B     ENDTBL
         SPACE 3
RELEASE  EQU   *
         MVC   TXDSNAM+6(44),DSNAME
         LA    R1,ALLOC
         SVC   99
         LTR   R15,R15
         BZ    RLSEOK
         TPUT  MSG8,70
         B     ENDTBL
RLSEOK   EQU   *
         MVC   DCB(96),DCB1
         OPEN  (DCB,(OUTPUT)),MF=(E,OPENL)
         CLOSE (DCB),MF=(E,OPENL)
         L     R15,SUMTRKS
         S     R15,SPCALLOC
         ST    R15,SUMTRKS
         XC    SPCALLOC(4),SPCALLOC
         LA    R1,DSNAME
         ST    R1,CAMLST+4
         LA    R1,VOLSER
         ST    R1,CAMLST+8
         OBTAIN CAMLST
         BAL   R14,GETSPACE
         B     XXX
         EJECT
GETSPACE EQU   *
*
* SUBROUTINE FOR CALCULATION OF THE NUMBER OF TRACKS ALLOCATED
* FOR A SPECIAL DATASET.
*
*    R11 = TABLE ELEMENT
*    DSCB1 = CAMLST+12
*
         ST    R14,R14DSCB
         L     R3,CAMLST+12
         SH    R3,=H'44'
         LA    R4,105(R3)   POINT TO 1. EXT. DESCR.
         LA    R7,3
LOOPDSCB EQU   *
         CLI   0(R4),X'00'  EXT. VORHANDEN?
         BE    RETDSCB  NO, RETURN
         MVC   DBLWD,2(R4)
         BAL   R14,EXTTRKS
         LA    R4,10(R4)   POINT TO NEXT EXTENT.
         BCT   R7,LOOPDSCB
         CLC   135(5,R3),=XL5'00'   DSCB3 VORHANDEN?
         BE    RETDSCB    NO, RETURN
         LA    R1,VOLSER
         ST    R1,CAMSEEK+8
         LA    R1,135(R3)
         ST    R1,CAMSEEK+4
         OBTAIN CAMSEEK
         LTR   R15,R15
         BNZ   OBTERR
         L     R3,CAMSEEK+12
         CLI   44(R3),C'3'
         BNE   OBTERR
         LA    R4,4(R3)
         MVC   44(90,R3),45(R3)
         LA    R7,13
LPDSCB3  EQU   *
         CLI   0(R4),X'00'
         BE    RETDSCB
         MVC   DBLWD,2(R4)
         BAL   R14,EXTTRKS
         LA    R4,10(R4)
         BCT   R7,LPDSCB3
RETDSCB  EQU   *
         L     R14,R14DSCB
         BR    R14
EXTTRKS  EQU   *
         LH    R15,DBLWD
         MH    R15,=H'19'
         AH    R15,DBLWD+2
         STH   R15,FULLWD
         LH    R15,DBLWD+4
         MH    R15,=H'19'
         AH    R15,DBLWD+6
         SH    R15,FULLWD
         LA    R15,1(R15)
         L     R1,SUMTRKS
         AR    R1,R15
         ST    R1,SUMTRKS
         L     R1,SPCALLOC
         AR    R1,R15
         ST    R1,SPCALLOC
         BR    R14
         EJECT
*
*    KONSTANTEN
*
TSOPARM  DCB   DDNAME=TSOPARM,DSORG=PO,MACRF=(R)
NOMSG    DC    CL70'NO TSO-NEWS AVAILABLE'
NOTOP    DC    CL70'SESSION TERMINATED - TSOPARM OPEN/BLDL FAILED'
CATEMSG  DC    CL70'SESSION TERMINATED - CATALOG ERROR'
OBTEMSG  DC    CL70'SESSION TERMINATED - OBTAIN ERROR'
MSG1     DC    CL70'YOU HAVE ALLOCATED TOO MUCH DIRECT ACCESS SPACE'
MSG2     DC    CL80'YOUR MAXIMUM IS XXX CYLS = XXXX TRACKS. YOUR CURRENT
               T ALLOCATION IS XXXX TRACKS'
MSG3     DC    CL80'ENTER ANY DSNAME FROM THE FOLLOWING LIST WHICH YOU W
               WANT TO DELETE - OR END'
MSG3A    DC    CL70'TO RELEASE ANY UNUSED SPACE IN A DATASET, ENTER ''R,
               ,DSNAME'''
MSG4     DC    CL70'YOUR SESSION CAN ONLY BE CONTINUED AFTER YOU HAVE FR
               REED XXXX TRACKS'
MSG5     DC    CL70'DATASET                                            *
                 DELETED'
MSG6     DC    CL70'XXXX TRACKS ALLOCATED FOR'
MSG7     DC    CL70'SPACE FREED WAS NOT ENOUGH - ENTER NEXT DSNAME, END*
                OR NEWLIST'
MSG8     DC    CL70'SCRATCH/UNCATLG/RLSE FAILED - SPECIFY OTHER DSNAME,*
                END OR NEWLIST'
MSG9     DC    CL50'INVALID SYNTAX - RESPECIFY'
BLANKS   DC    CL44' '
MSG10    DC    CL70'SPECIFIED DATASET NOT FOUND - SPECIFY OTHER DSNAME,*
                END OR NEWLIST'
MSG11    DC    CL27'LOGON INHIBITED - TRY LATER'
P1       DC    3F'0'
P2       DC    CL8'DSALLOC'
P3       DC    CL8'TSOPARM'
P4       DC    CL44'OX.TSOPARM'
P6       DC    CL3'SHR'
MAJNM    DC    CL8'INHIBIT'
MINNM    DC    CL5'LOGON'
ALLOC    DC    X'80'
         DC    AL3(ALCRQBLK)
ALCRQBLK DC    XL8'1401000000000000'
         DC    A(ALCPTR)
         DC    XL8'00'
ALCPTR   DC    A(TXDDNAM)
         DC    A(TXDSNAM)
         DC    A(TXMOD)
         DC    A(TXFREE)
         DC    X'80'
         DC    AL3(TXRLSE)
TXDDNAM  DC    XL6'000100010008'
         DC    CL8'RLSDDNAM'
TXDSNAM  DC    XL6'00020001002C'
         DC    CL44' '
TXMOD    DC    XL8'0004000100010200'
TXRLSE   DC    XL4'000D0000'
TXFREE   DC    XL4'001C0000'
DCB1     DCB   MACRF=(GL,PM),DDNAME=RLSDDNAM,BUFNO=1,DSORG=PS
         EJECT
*
*    DUMMY SECTIONS
*
DSA      DSECT
         DS    18F
CLEAR    DS    0F
OPENL    DS    F
DCB      DS    CL96
TABLE    DS    F
CTGPLL   DS    CL32
USIDL    DS    F
         CNOP  2,4
BLDLL    DS    0CL62
FF       DS    CL2
LL       DS    CL2
USERID   DS    CL8
REST     DS    CL52
         DS    0F
CAMLST   DS    CL16
CAMSEEK  DS    CL16
TSONEWS  DS    F
CAMUNCAT DS    CL16
DBLWD    DS    D
SUMTRKS  DS    F
R14DSCB  DS    F
FULLWD   DS    F
RETCD    DS    F
MSGSAV   DS    CL80
ANSWER   DS    CL44
CAMSCR   DS    CL16
VOLLIST  DS    CL16
PREFIX   DS    CL8
MAXTRKS  DS    F
SWITCH   DS    X
LIST     EQU   X'80'
RLSE     EQU   X'40'
DSAL     EQU   *-DSA
CLEARL   EQU   *-CLEAR
*
         SPACE 3
TSOPRM   DSECT
MAXSPACE DS    F
         DS    CL76
*
*
ENTRY    DSECT
TYPE     DS    X
DSNAME   DS    CL44
DEVTYPE  DS    XL4
VOLSER   DS    CL6
         DS    XL1   RESERVED
SPCALLOC DS    F
         DS    F              RESERVED
*
*
CTGPLD   DSECT
         CTGPL
         END
