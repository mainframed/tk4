*
*        PARAMETER  :        DATUM-FELD     CHARACTER     8
*
UZED10A  CSECT
         SAVE  (14,12),,*          *
         BALR  5,0                 *
         USING *,5                 *
         ST    13,SAVE+4           *    P R O L O G
         LR    6,13                *
         LA    13,SAVE             *
         ST    13,8(6)             *
*
*
*
         LR    6,1                 PARAM.-LIST.-ADR.  SICHERN
         TIME  DEC                 DATUM  (00YYDDDF)  HOLEN
         ST    1,FULLW             DATUM  SICHERN
         MVO   ZW1,FULLW+1(1)      JAHR  IN  ZWSP 1
         CP    ZW1,=PL2'0'         AUF JAHR NULL VERGLEICHEN
         BE    JAHR0
         ZAP   ZW2,ZW1             JAHR  IN  RECHEN-ZWSP
         DP    ZW2,=PL1'4'         JAHR  DURCH  VIER  DIVIDIEREN
         CP    ZW2+2(1),=PL1'0'    REST  AUF  NULL  VERGLEICHENN
         BNE   NORMAL
JAHR0    MVI   TAB+3,X'9C'         FEBRUAR  AUF  29  SETZEN
         MVI   CONST+1,X'6C'       JAHR  AUF  366  SETZEN
NORMAL   UNPK  DATA+6(2),ZW1       JAHR  UMPACKEN
         MVZ   DATA+7(1),DATA+6    ZONENLOCHUNG  UMSETZEN
         ZAP   ZW1,FULLW+2(2)      TAGE  IN  ZWSP 1
         BZ    FEHLER
         CP    ZW1,CONST           VERGLEICH  AUF  GROESSER  365 / 366
         BH    FEHLER
         ZAP   ZW2,=PL3'0'         ZWSP 2  AUF  NULL  SETZEN
         LA    7,TAB               TABELLEN-ADRESSE  LADEN
LOOP     AP    ZW2,=PL1'1'         MONATS-ZAEHLER
         SP    ZW1,0(2,7)          TAGE  PRO  MONAT  SUBTRAHIEREN
         BNP   WEITER
         LA    7,2(7)              7  UM  2  ERHOEHEN
         B     LOOP
*
*
*
WEITER   AP    ZW1,0(2,7)          MONAT  WIEDER  ADDIEREN
         UNPK  DATA(2),ZW1         TAGE  UMPACKEN
         MVZ   DATA+1(1),DATA      ZONENLOCHUNG  UMSETZEN
         UNPK  DATA+3(2),ZW2       MONATE  UMPACKEN
         MVZ   DATA+4(1),DATA      ZONENLOCHUNG  UMSETZEN
*
*    DATEN-FELD  BEREIT  FUER  MOVE  INS  HAUPT-PROGRAMM
*
RETURN   L     4,0(6)              ADRESSE DES FELDES IM HAPRO
         MVC   0(8,4),DATA         FELD  UEBERTRAGEN
         MVI   TAB+3,X'8C'    WERT ZURUECKSETZEN
         MVI   CONST+1,X'5C'  WERT ZURUECKSETZEN
*
*    RETURN
*
         L     13,SAVE+4           *    E P I L O G
         RETURN (14,12),RC=0       *
*
*    FEHLER-ROUTINE
*
FEHLER   MVC   DATA,=8CL1'*'       STERNE  EINSETZEN
         B     RETURN
*
*    D E F I N I T I O N E N
*
SAVE     DC    9D'0'               SAVEAREA
TAB      DC    PL2'31'             JANUAR
         DC    PL2'28'             FEBRUAR
         DC    PL2'31'             MAERZ
         DC    PL2'30'             APRIL
         DC    PL2'31'             MAI
         DC    PL2'30'             JUNI
         DC    PL2'31'             JULI
         DC    PL2'31'             AUGUST
         DC    PL2'30'             SEPTEMBER
         DC    PL2'31'             OKTOBER
         DC    PL2'30'             NOVEMBER
         DC    PL2'31'             DEZEMBER
FULLW    DC    F'0'                DATUM-SPEICHER
ZW1      DC    PL2'0'              ZWSP  FUER  JAHR
ZW2      DC    PL3'0'              ZWSP  FUER  RECHNEN
CONST    DC    PL2'365'            ANZAHL  TAGE  PRO  NORMALES  JAHR
DATA     DC    CL8'  .  .  '       DATUM
*
         END   UZED10A
