./       ADD MEMBER=OALS061S
***********************************************************************
*                                                                     *
*                          V S B O U N D                              *
*                                                                     *
*   THIS PROGRAM SHOWS THE FOLLOWING VIRTUAL STORAGE BOUNDARIES.      *
*                                                                     *
*   HIGHEST ADDRESS IN SYSTEM                                         *
*   ADDRESS OF LPA DIRECTORY                                          *
*   PLPA AND SQA SIZE                                                 *
*   LOWEST ADDRESS OF SHARED VS (CSA BEGINNING)                       *
*   LOWEST ADDRESS NOT IN NUCLEUS                                     *
*   PRIVATE AREA SIZE (INCLUSIVE 16K RCT)                             *
*                                                                     *
*  AUTOR : RUDOLF PALMER                                              *
*          C/O ALLIANZ LEBENSVERSICHERUNGS AG.                        *
*          ABTEILUNG DVZ/SYST/A1 - TEL. (0711)/66664214               *
*          REINSBURGSTRASSE 32/34                                     *
*          7000 STUTTGART 1                                           *
*                                                                     *
***********************************************************************
         EJECT
VSBOUND  CSECT
         USING *,15                    FIRST ADDRESSABILITY
         B     START                   SURROUND CSECT IDENTIFICATION
*
IDR      DC    C' CSECT  VSBOUND  ***  12/05/1982 * PALMER'
         DS    0H
*
START    EQU   *
         STM   14,12,12(13)            SAVE REGISTERS
         LR    12,13                   SAVE ADDR OF CALLING SAVEAREA
         LA    13,SAVEA                LOAD ADDRESS OF OWN SAVEAREA
         ST    12,4(13)                SAVE CALLING SAVEAREA ADDRESS
         ST    13,8(12)                OWN SAVEA ADDR TO CALLER
         BALR  3,0                     ESTABLISH ADDRESSABILITY
         USING *,3
*
* END OF LINKAGE CONVENTIONS
*
*
*        P R O G R A M   I N I T I A T I O N
*
*
         SR    4,4                CLEAR R4
         L     4,16               LOAD CVT ADDRESS
*
*        BUILD HEADER LINE
*
         L     5,196(,4)          SYSTEM MANAGEMENT CONTROL AREA
         MVC   ADDRESS+2(4),16(5) SYSTEM IDENTIFICATION
*
         AVDATUM (1)              CURRENT DATE (TT.MM.JJ)
         MVC   SIZE(8),0(1)
         BAL   9,PRINT            PRINT HEADER LINE
*
         LA    5,X'00A4'(,4)
         L     11,0(5)
         LR    8,11               STORE HIGHEST ADDRESS
         MVC   TEXT(25),=C'HIGHEST ADDRESS IN SYSTEM'
         XR    10,10
         D     10,=F'1024'        CHANGE BYTES TO KBYTES
         XC    DW,DW              CLEAR DOUBLE WORD
         CVD   11,DW              CONVERT IT TO DECIMAL
         AP    DW,=P'1'
         MVC   ADDRESS,=X'402020202120'
         ED    ADDRESS,DW+5       MOVE TO PRINTER LINE
         MVI   ADDRESS+7,C'K'
         BAL   9,PRINT
*
         LA    5,X'0168'(,4)
         SR    11,11
         ICM   11,B'0111',1(5)    LOAD ADDRESS OF LPA DIRECTORY
         LR    7,11               STORE LPA DIRECTORY ADDRESS
         LA    11,1(,11)
         MVC   TEXT(24),=C'ADDRESS OF LPA DIRECTORY'
         BAL   9,MOVEADDR
*
         LR    11,8               LOAD HIGHEST ADDRESS
         SR    11,7               CALCULATE PLPA AND SQA SIZE
         LA    11,1(,11)
         MVC   TEXT(17),=C'PLPA AND SQA SIZE'
         BAL   9,MOVESIZE
*
         LA    5,X'01A0'(,4)
         L     11,0(5)            LOAD ADDRESS OF CSA
         LA    11,1(,11)
         LR    8,11               SAVE CSA ADDRESS
         MVC   TEXT(43),=C'LOWEST ADDRESS OF SHARED VS (CSA BEGINNING)'
         BAL   9,MOVEADDR
*
         LA    5,X'0080'(,4)
         L     11,0(5)            LOWEST ADDRESS NOT IN NUCLEUS
         LA    11,1(,11)
         LR    7,11
         MVC   TEXT(29),=C'LOWEST ADDRESS NOT IN NUCLEUS'
         BAL   9,MOVEADDR
*
         LR    11,7
         SR    8,11               CALCULATE PRIVATE AREA SIZE
         LR    11,8
         MVC   TEXT(37),=C'PRIVATE AREA SIZE (INCLUSIVE 16K RCT)'
         LA    9,CLOSE
         B     MOVESIZE
*
MOVEADDR EQU   *
         XR    10,10
         D     10,=F'1024'        CHANGE BYTES TO KBYTES
         XC    DW,DW              CLEAR DOUBLE WORD
         CVD   11,DW              CONVERT IT TO DECIMAL
         MVC   ADDRESS,=X'402020202120'
         ED    ADDRESS,DW+5       MOVE TO PRINTER LINE
         MVI   ADDRESS+7,C'K'
         B     PRINT
*
MOVESIZE EQU   *
         XR    10,10
         D     10,=F'1024'        CHANGE BYTES TO KBYTES
         XC    DW,DW              CLEAR DOUBLE WORD
         CVD   11,DW              CONVERT IT TO DECIMAL
         MVC   SIZE,=X'402020202120'
         ED    SIZE,DW+5          MOVE TO PRINTER LINE
         MVI   SIZE+7,C'K'
         B     PRINT
*
PRINT    EQU   *
*
         WTO   MF=(E,WTO)
*
         XC    LINE,LINE
         BR    9
*
CLOSE    EQU   *
         XR    15,15
*
RETURN   EQU   *
         LA    13,SAVEA                LOAD ADDRESS OF OWN SAVEAREA
         L     13,4(13)                LOAD ADDRESS OF CALLING SAVEAREA
         LM    2,12,28(13)             RESTORE
         L     14,12(13)               THE
         L     0,20(13)                REGISTERS
         BR    14                      RETURN
*
*
* DEFINED SPACE AND WORKAREAS
*
SAVEA    DC    18F'0'                  SAVEAREA
DW       DC    D'0'  WORKAREA
*
WTO      WTO   ('                                                      *
                               '),ROUTCDE=2,MF=L
         ORG   WTO+4
LINE     DS    0CL70
TEXT     DC   CL51'V I R T U A L   S T O R A G E   B O U N D A R I E S'
         DC    CL01' '
ADDRESS  DS    XL6
         DC    CL4' '
SIZE     DS    XL6
         DC    CL2' '
*
* END OF CONTIGUOUS DEFINITIONS
*
         EJECT
*
* LITERALS
*
         LTORG
*
         END
