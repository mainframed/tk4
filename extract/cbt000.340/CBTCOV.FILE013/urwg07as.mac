         MBASIS R3,R4,RC=(R15)
         MSPSN
         OPEN  (I1KART,(INPUT))
         OPEN  (O1AEND,(OUTPUT))
         OPEN  (O3LIST,(OUTPUT))
         OPEN  (O3AUSG,(OUTPUT))
         RDJFCB (O2MOD)
         MVI   JFCBMOD+87,X'80'
         OPEN  (O2MOD,(OUTPUT)),TYPE=J
LESEN    GET   I1KART,EINGABE
         MVI   SCHA,C'1'
KART1    NOP   WRITE
         LA    R5,EINGABE
         CLC   0(2,R5),=C'./'
         MVI   RETT15+3,4
         BNE   FEHLER
ERH      LA    R5,1(R5)
         CLC   0(5,R5),=CL5' ADD '
         BE    ADD
         CLC   0(5,R5),=CL5'NAME='
         BE    NAMEMV1
         CLC   0(4,R5),=CL4'SSI='
         BNE   FRAGE
         MVC   SSI,4(R5)
         B     FRAGE
NAMEMV1  LA    R6,6(R5)
         SR    R7,R7
SUCHLN   CLC   0(1,R6),=CL1','
         BE    STELL
         CLC   0(1,R6),=CL1' '
         BE    STELL
         AH    R7,=H'1'
         LA    R6,1(R6)
         B     SUCHLN
STELL    STC   R7,NAMEMV+1
         STC   R7,VERGNAM+1
NAMEMV   MVC   NAME,5(R5)
FRAGE    C     R5,=A(EINGABE+72)
         BL    ERH
         RDJFCB  (I2BIBL)
         RDJFCB  (SYSUT1)
VERGNAM  CLC   NAME,JFCBBIB+44
         BE    NAMEOK
         WTO   '*** RZSYS41 *** NAME NICHT IDENTISCH MIT DSN IN EXEC-KA*
               RTE',ROUTCDE=11
         MRETURN RC=4
NAMEOK   EQU   *
         B     WRITE
ADD      MVC   OPTLIST+2(3),=CL3'NEW'
         B     ERH
FEHLER   WTO   '*** RZSYS41 *** CONTROLKARTE FALSCH',ROUTCDE=11
         B     CLOSE
WRITE    PUT   O1AEND,EINGABE
         MVI   KART1+1,C'0'
UMENQ    NOP   PUTMOD
         ENQ   (NAMEMOD,JFCBMOD,E,44,SYSTEM)
         MVI   UMENQ+1,C'0'
PUTMOD   EQU   *
         PUT   O2MOD,EINGABE
         B     LESEN
ENDEIN   EQU   *
         CLI   SCHA,C'0'
         BE    KEINUPD
         MVI   JFCBMOD+87,X'08'
         CLOSE O2MOD
         DEQ   (NAMEMOD,JFCBMOD,44,SYSTEM)
         CLOSE I1KART
         CLOSE O1AEND
         ENQ   (NAMEUPD,JFCBUPD,E,44,SYSTEM)
         LINK  EP=IEBUPDTE,PARAM=(OPTLIST,DDNMELST),VL=1
         ST    R15,RETT15
         OPEN  (SYSPRINT,(INPUT))
         ZAP   ZZEIL,=X'001C'
         ZAP   ZSEIT,=X'000C'
GETMESS  GET   SYSPRINT,MESSUPD
ERSTUEB  NOP   TESTZEIL
         MVI   ERSTUEB+1,X'F0'
         B     DRUEBER
TESTZEIL EQU   *
         CP    ZZEIL,=X'060C'
         BNE   TESTMESS
DRUEBER  EQU   *
         ZAP   ZZEIL,=X'001C'
         AP    ZSEIT,=X'001C'
         MVI   UEBER,X'8B'
         PUT   O3LIST,UEBER
         MVC   UEBER+19(18),JFCBBIB
         MVC   UEBER+45(8),JFCBBIB+44
         MVC   UEBER+63(6),JFCBUPD+118
         MVC   UEBER+109(5),=X'4040202020'
         ED    UEBER+109(5),ZSEIT
         MVI   UEBER,X'11'
         PUT   O3LIST,UEBER
         TIME  DEC
         ST    R1,FDATE
         ST    R0,FZEIT
         MVC   UEBER3+12(7),=X'4020204B202020'
         ED    UEBER3+12(7),FDATE+1
         MVC   UEBER3+32(10),=X'402020204B20204B2020'
         OI    FZEIT+3,X'0F'
         MVO   FZEIT,FZEIT(3)
         ED    UEBER3+32(10),FZEIT
         PUT   O3LIST,UEBER3
TESTMESS CLC   MESSUPD+110(8),=CL8'RENUMBER'
         BE    GETMESS
         CLC   MESSUPD+102(8),=CL8'RENUMBER'
         BE    GETMESS
         CLC   MESSUPD+7(7),=CL7' SYSIN '
         BE    GETMESS
*  IN  MSG ASA- VORSCHUB ERSETZEN DURCH MASCH-CODE -09-
WRMESS   MVI  MESSUPD,X'09'
         PUT   O3LIST,MESSUPD
         AP    ZZEIL,=X'001C'
         B GETMESS
ENDMESS  EQU   *
         L     R15,RETT15
         CL    R15,=F'12'
         BH    ABEND4
         B     VERZW(R15)
VERZW    B     NORMAL
         B     FEHLER       RETURN-CODE 4
         B     NOMEMB       RETURN-CODE 8
         B     UNCORR       RETURN-CODE 12
ABEND4   ABEND 444
NOMEMB   WTO   '*** RZSYS41 *** MEMBER NOT FOUND ***',ROUTCDE=11
         B     CLOSE
UNCORR   WTO   '*** RZSYS41 *** UNDEFINED ERROR ***',ROUTCDE=11
         B     CLOSE
NORMAL   EQU   *
         DEQ   (NAMEUPD,JFCBUPD,44,SYSTEM)
         B     OPBIBL
KEINUPD  EQU   *
         WTO   '*** RZSYS41 *** KEINE EINGABE FUER UPDATE',ROUTCDE=11
OPBIBL   EQU   *
         OPEN  (I2BIBL,(INPUT))
LESMEM   GET   I2BIBL,AUSGABE
         PUT   O3AUSG,AUSGABE
         B     LESMEM
ENDMEM   EQU   *
         CLOSE I2BIBL
CLOSE    CLOSE O3AUSG
         MRETURN RC=RETT15
FELD     DS  CL1
SCHA     DC    CL1'0'
FECB     DC  F'0'
SSI      DC    CL8' '
NAME     DC    CL8' '
RETT15   DC    F'0'
         DS    0F
JFCBUPD  DS    CL176
JFCBBIB  DS    CL176
JFCBMOD  DS    CL176
LISTUPD  DC    X'87'
         DC    AL3(JFCBUPD)
LISTBIB  DC    X'87'
         DC    AL3(JFCBBIB)
LISTMOD  DC    X'87'
         DC    AL3(JFCBMOD)
NAMEUPD  DC    CL8'NAMEUPD '
NAMEMOD  DC    CL8'NAMEMOD '
DRUCK    DS    0CL81
VOR      DC    X'09'
EINGABE  DS    CL80
AUSGABE  DS    CL80
         DS    0F
         DS    H
FDATE    DS    F
FZEIT    DS    F
UEBER    DS    0CL81
         DC    X'13'
         DC    CL80'** AUF DIE BIBL.                    ,MEMBER=       *
                 ,PLATTE=        WURDE UPDAT'
         DC    CL40'E GEMACHT         SEITE-NR.             '
UEBER3   DS    0CL81
         DC    X'11'
         DC    CL80'** AM TAG           ,ZUR ZEIT                  MIT *
               FOLGENDEN KARTEN ......      '
         DC    CL40' '
OPTLIST  DC    X'0003'
         DC    CL3'MOD'
         DS    0F
         DS    H
DDNMELST DC    X'0000'
ZZEIL    DC    X'000C'
ZSEIT    DC    X'000C'
MESSUPD  DS    CL121
SYSPRINT  DCB  DDNAME=SYSPRINT,MACRF=(GM),DSORG=PS,EODAD=ENDMESS
I1KART   DCB   DDNAME=I1KART,MACRF=(GM),DSORG=PS,EODAD=ENDEIN
O1AEND   DCB    DDNAME=SYSIN,MACRF=(PM),DSORG=PS
O2MOD    DCB   DDNAME=O2MOD,MACRF=(PM),DSORG=PS,EXLST=LISTMOD
O3LIST   DCB   DDNAME=O3LIST,MACRF=(PM),DSORG=PS
I2BIBL   DCB   DDNAME=I2BIBL,MACRF=(GM),DSORG=PS,EODAD=ENDMEM,         *
               EXLST=LISTBIB
O3AUSG   DCB   DDNAME=O3AUSG,MACRF=(PM),DSORG=PS
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=(R),DSORG=PO,EXLST=LISTUPD
         END
