         TITLE 'XCOMPARE VERGLEICHSPROGRAMM UHYP02AS = XCOMPARE'
         MACRO
&NAME    MCST  &TYP=NORMAL,&PCBIO=
         GBLB  &MCST
         GBLC  &MCSTN
         LCLA  &A1,&A2,&A3
         LCLC  &C1
         EJECT
*********************************************
*        GENERIERUNG DES STARTMACROS        *
         AIF   ('&TYP' EQ 'IMS').MACST2
         AIF   ('&TYP' EQ 'MINI').MACST3
*        FUER EIN NORMALES PROGRAMM         *
.MACST1  ANOP
*********************************************
*------- LEVEL = 27 -------*
         AIF   (&MCST).ERROR1
&MCST    SETB  1
&NAME    CSECT
&MCSTN   SETC  '&NAME'
MCST1    SAVE  (14,12),,STARTPUNKT&NAME
         SPACE
*        BASISREGISTER LADEN
         SPACE
         BALR  R9,0
         USING *,R9,R10,R11,R12
MCST2    LM    R10,R12,MCST10
         SPACE
*        SAVEAREA VERKETTUNG
         SPACE
         ST    R13,SAVEAREA+4
         LR    R14,R13
         LA    R13,SAVEAREA
         ST    R13,8(R14)
         AIF   ('&TYP' EQ 'MINI').MACST50
         LR    R3,R1
         SPACE
*        PROGRAMMNAME
         SPACE
         MACPROGN
         ST    R1,MCST112
         SPACE
*        TSOFLAG SICHERN (SVS + MVS)
         SPACE
         L     R1,16 CVT-ADDRESS
         S     R1,MCSTFW4
         CLC   0(2,R1),MCSTREL SVS OR MVS
         BE    MCSTTSO1 IF EQUAL (017), THEN SVS
         A     R1,MCSTFW4                          MVS
         L     R1,0(R1) WORD-LIST                  MVS
         L     R1,12(R1) CURRENT ASCB              MVS
         CLC   172(4,R1),MCSTFW0                   MVS
         BNZ   MCST20 IF NOT ZERO, THEN BATCH-USER MVS
         B     MCSTTSO2 TSO-USER                   MVS
MCSTTSO1 A     R1,MCSTFW4                       SVS
         L     R1,0(R1) WORD-LIST               SVS
         L     R1,4(R1) CURRENT TCB             SVS
         MVC   MCSTTSO,148(R1) TCBTSFLAG        SVS
*        UNTERBRECHUNGSADRESSE
         SPACE
         TM    MCSTTSO,X'80' TSO OR BATCH-USER
         BZ    MCST20 IF ZERO, THEN BATCH-USER
MCSTTSO2 L     R1,MCSTPMSK SET PROGRAM-
         SPM   R1          MASK FOR
         B     MCST20A     TSO-USER
MCST20   SPIE  MCSTERR,((1,15))
         ST    R1,MCSTPICA
         AIF   (&A1 NE 1).MACST9
         SPACE
*        PCB ADRESSEN
         SPACE
MCST20A  LA    R2,MCSTPCB1-72
MCST21   LA    R2,72(R2)
         L     R0,0(R3)
         ST    R0,4(R2)
         LA    R3,4(R3)
         LTR   R0,R0
         BNM   MCST21
         MVI   4(R2),0
         MVC   MCSTPCB0+4(4),4(R2)
         SR    R3,R3
.MACST9  ANOP
         SPACE
*        REPLACUNG
         SPACE
         AIF   (&A1 NE 1).MACST9A
         ST    R3,MCST116
         AGO   .MACST9B
.MACST9A ANOP
MCST20A  ST    R3,MCST116
.MACST9B ANOP
         LINK  EP=MODREP,PARAM=(MCST100),VL=1
         DELETE EP=MODREP
         SPACE
*        TESTHILFSPROGRAMME
         SPACE
         TM    MCST108,X'80'
         BZ    MCST99
         LOAD  EP=MODPRT
         ST    R0,MCSTPRT
         LOAD  EP=MODSEK
         ST    R0,MCSTSEK
         LOAD  EP=MODTRC
         ST    R0,MCSTTRC
MCST9    TM    MCST205,X'01'
         BZ    MCST99
         BAL   R2,MODTRC1
         B     MCST99
         SPACE
*        DEFINITIONEN
         SPACE
         DS    0F
MCSTRC   DC    XL3'0'                       RETURNCODE-FIELD
MCSTRCF  DC    X'0'
MCSTPAKA DC    CL52' '
MCST10   DC    A(MCST2+4096,MCST2+8192,MCST2+12288)
MCSTDCB  DCB   DSORG=PS,BLKSIZE=141,MACRF=PM,RECFM=VBM,                X
               LRECL=137,BUFNO=2,EROPT=ACC,DDNAME=DRUCKER
         SPACE
*        ERROR-ROUTINE
         SPACE
MCSTERR  BALR  R2,0
         USING *,R2
         ST    R1,MCSTERR1
         ST    R13,MCSTERR3+4
         LR    R14,R13
         LA    R13,MCSTERR3
         ST    R13,8(R14)
         LINK  EP=MODERR,PARAM=(MCSTERR1)
         CLOSE (MCSTDCB)
         CLI   MCST116,0
         BE    MCSTERR0
         ABEND 4000,DUMP
MCSTERR0 ABEND 4000
MCSTERR1 DC    F'0'
MCSTERR2 DC    A(MCSTNAME)
         DC    A(MCST116)
         DC    A(MCST1)
MCSTERR3 DC    18F'0'
         DROP  R2
         SPACE
*        EOJ ROUTINE
         SPACE
MCSTEOJ  MVI   MCST144,X'00'
         AIF   (&A1 NE 1).MACST40
         TM    MCST108,X'80'
         BZ    MCST41
         LA    R1,MCSTPCB0
         BAL   R2,MICALLUP
         L     R4,MCSTIMS1
         L     R4,276(R4)
         MCTF  31,(R4),108
.MACST40 ANOP
MCST41   TM    MCSTDCB+48,16
         BZ    MCST33
         CLOSE (MCSTDCB)
MCST33   TM    MCSTTSO,X'80'
         BO    MCST34
         L     R3,MCSTPICA
         SPIE  MF=(E,(3))
MCST34   L     R13,4(R13)
         L     R15,MCSTRC                   LOAD RETURNCODE
         RETURN (14,12),,RC=(15)
         SPACE
*        ANSPRUNG MODPRT
         SPACE
MODPRT1  CLI   MCST204,X'00'
         BE    6(R2)
         STM   R14,R2,MCST30
         MVI   MCST144,X'00'
         L     R2,MCST30+16
         LA    R1,MCSTDCB
         L     R15,MCSTPRT
         BALR  R14,R15
         SPACE
*        RUECKSPRUNG MODPRT
         SPACE
         LM    R14,R2,MCST30
         BAL   R2,MODTRC1
         LM    R14,R2,MCST30
         B     6(R2)
         SPACE
*        ANSPRUNG MODSEK
         SPACE
MODSEK1  TM    MCST108,X'80'
         BZ    2(R2)
         STM   R14,R2,MCST30
         MVI   MCST144,X'00'
         L     R2,MCST30+16
         LA    R0,MCST200
         L     R15,MCSTSEK
         BALR  R14,R15
         SPACE
*        RUECKSPRUNG MODSEK
         SPACE
         LTR   R15,R15
         BNZ   MCSTEOJ
         BAL   R2,MODTRC1
         LM    R14,R2,MCST30
         B     2(R2)
         SPACE
*        ANSPRUNG MODTRC
         SPACE
MODTRC1  CLI   MCST205,X'00'
         BE    0(R2)
         STM   R13,R1,MCST124
         MVC   MCST144(1),MCST205
         L     R15,MCSTTRC
         LR    R0,R2
         LA    R1,MCST124
         BALR  R14,R15
         SPACE
*        RUECKSPRUNG MODTRC
         SPACE
         LM    R13,R1,MCST124
         BR    R2
         SPACE
*        PARAMETER FUER TESTMODULE
         SPACE
         DS    0F
MCST100  DC    FL1'9'
MCST101  DC    XL3'0'
MCST104  DC    FL1'4'
MCST105  DC    AL3(MCST1)
MCST108  DC    X'0'      TESTBYTE
MCST109  DC    AL3(MCST200)
MCST112  DC    F'0' MCSTNAME
MCST116  DC    F'0' REG 3 (A(PARM) ODER 0)
MCST120  DC    A(MCSTPAKA) PARMKARTENBEREICH
MCST124  DC    A(SAVEAREA)
MCST128  DC    F'0'
MCST132  DC    F'0'
MCST136  DC    F'0'
MCST140  DC    F'0'
MCST144  DC    A(MCSTDCB)
         SPACE
MCST30   DC    5F'0'
MCST31   DC    X'0'
MCST32   DC    X'0'
         SPACE
*        TESTBEREICH
         SPACE
         DS    0F
MCST200  DC    F'0'      TESTWORT
MCST204  DC    X'0'
MCST205  DC    X'0'
MCST206  DC    X'0'
         DC    X'0'
         DC    XL14'0'
         DC    12X'FF'
MCST234  DC    X'0'
         DC    X'0'
         DC    XL14'0'
         DC    12X'FF'
MCST262  DC    X'0'
         DC    X'0'
         DC    XL14'0'
         DC    12X'FF'
         SPACE
*        ALLGEMEINE DEFINITIONEN
         SPACE
SAVEAREA DC    18F'0'
MCSTNAME EQU   MCST112
MCSTPICA DC    F'0'
MCSTPMSK DC    X'0F0000'
MCSTTSO  DC    X'0'
MCSTPRT  DC    F'0'
MCSTSEK  DC    F'0'
MCSTTRC  DC    F'0'
MCSTPARM DC    A(MCSTPAKA+4)
MCSTFW0  DC    F'0'
MCSTFW4  DC    F'4'
MCSTREL  DC    C'01' RELEASE 01.7
         AIF   (&A1 NE 1).MACST7
         SPACE
*        IMS - BEREICHE
         SPACE
MCSTPCB0 DC    A(MCSTGSCD)
         DC    A(0)
         DC    X'80'
         DC    AL3(MCSTIMS1)
&A2      SETA  N'&PCBIO
.MACST10 ANOP
&A3      SETA  &A3+1
&C1      SETC  'MCSTPCB&A3'
&C1      DC    A(MCSTIMSF)
         DC    A(0)
         DC    A(&PCBIO(&A3))
         DC    15F'0'
&A2      SETA  &A2-1
         AIF   (&A2 NE 0).MACST10
         DS    0F
MCSTIMS1 DC    XL8'0'
MCSTIMSF DC    CL4' '
MCSTGU   DC    CL4'GU'
MCSTGN   DC    CL4'GN'
MCSTGNP  DC    CL4'GNP'
MCSTGHU  DC    CL4'GHU'
MCSTGHN  DC    CL4'GHN'
MCSTGHNP DC    CL4'GHNP'
MCSTISRT DC    CL4'ISRT'
MCSTDLET DC    CL4'DLET'
MCSTREPL DC    CL4'REPL'
MCSTGSCD DC    CL4'GSCD'
         DC    A(MCSTERR)
         SPACE 3
         MIPARAM
         EJECT
&A2      SETA  N'&PCBIO
.MACST8  ANOP
         MIPCB
&A2      SETA  &A2-1
         AIF   (&A2 NE 0).MACST8
&NAME    CSECT
.MACST7  ANOP
         SPACE
*        EQUATE DEFINITIONEN
         SPACE
         MCEQU
         SPACE
         MCDAT
         SPACE
*        REPLACEBEREICH
         SPACE
MCSTREP  DS    CL200
         SPACE
MCST99   EQU   *
         EJECT
         MEXIT
.MACST2  ANOP
         AIF   ('&PCBIO' EQ '').ERROR2
&A1      SETA  1
         AIF   (N'&PCBIO GT 9).ERROR3
*        FUER EIN IMS PROGRAMM              *
         AGO   .MACST1
.MACST3  ANOP
*        KURZVERSION                        *
         AGO   .MACST1
.MACST50 ANOP
         B     MCST99
         SPACE
*        EOJ - ROUTINE
         SPACE
MCSTEOJ  L     R13,4(R13)
         L     R15,MCSTRC                   LOAD RETURNCODE
         RETURN (14,12),,RC=(15)
MCSTEOJ1 L     R13,4(R13)
         L     R15,MCSTRC                   LOAD RETURNCODE
         L     R14,12(R13)
         L     R0,20(R13)
         LM    R2,R12,28(R13)
         BR    R14                      RETURN
         SPACE
*        ALLGEMEINE DEFINITIONEN
         SPACE
         DS    0F
MCSTRC   DC    XL3'0'                       RETURNCODE-FIELD
MCSTRCF  DC    X'0'
MCST10   DC    A(MCST2+4096,MCST2+8192,MCST2+12288)
SAVEAREA DC    18F'0'
         MCEQU
MCST99   EQU   *
         EJECT
         MEXIT
.ERROR1  MACMES 1,MCST
         MEXIT
.ERROR2  MACMES 2,MCST
         MEXIT
.ERROR3  MACMES 3,MCST
         MEND
         MACRO
&NAME    MACPROGN
&NAME    L     R1,16 CVT-ADDRESS
         L     R1,0(R1) TCB-LISTE
         L     R1,4(R1) TCB-ADDRESS
         L     R1,0(R1) RB-ADDRESS
         L     R1,12(R1) CDE-ADDRESS
         LA    R1,8(R1) PROGRAMNAME
         MEND
         MACRO
&NAME    MIPARAM
*********************************************
*        PARAMETERLISTE FUER IMS-CALL       *
*********************************************
         SPACE
         AIF   ('&NAME' EQ '').MAC1
&NAME    EQU   *
.MAC1    ANOP
MICALLUP TM    8(R1),X'80'
         BO    MICALLU2
         LA    R14,12(R1)
MICALLU1 L     R15,0(R14)
         CLI   13(R15),C'('
         BNE   MICALLU4
         LA    R0,4
         SLR   R15,R0
         LR    R0,R14
         BALR  R14,R15
         LR    R14,R0
         L     R15,0(R14)
MICALLU4 LTR   R15,R15
         BM    MICALLU2
         LA    R14,4(R14)
         B     MICALLU1
MICALLU2 L     R15,MCSTIMS
         ST    R1,MCSTSAVE
         BALR  R14,R15
         L     R1,MCSTSAVE
         LA    R0,7
         L     R14,4(R1)
         LA    R15,MCSTATUS
MICALLU3 CLC   10(2,R14),0(R15)
         BE    MICALLOK
         LA    R15,2(R15)
         BCT   R0,MICALLU3
         L     R4,MCST112
         MVC   MCST13+8(8),0(R4)
         MVC   MCST13+22(L'MCSTSTME),MCSTSTME
         MVC   MCST13+38(2),10(R14)
         WTO   MF=(E,MCST13)
         MCABEND 3600,DUMP
MICALLOK BR    R2
MCST13   WTO   '*** PROGNAME 3600                   ',MF=L
MCSTATUS DC    C'  GAGBGEGKIILB'
MCSTIMS  DC    V(ASMTDLI)
MCSTSAVE DC    F'0'
MCSTSTME DC    C'IMS-STATUSCODE: XX'
         MEND
         MACRO
         MCEQU
         GBLB  &MCEQU
         AIF   (&MCEQU).NOTE1
&MCEQU   SETB  1
*------- LEVEL = 03 -------$
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEXIT
.NOTE1   MACMES 1,MCEQU
         MEND
         MACRO
&NAME    MCDAT &BER
         GBLB  &MCDAT
         AIF   (&MCDAT).MCDAT4
&MCDAT   SETB  1
*        DATUMSPRUEFUNG
* DAS DATUM IST UNGEPACKT IN DER FORM TT.MM.JJ (8 BYTES)
*------- LEVEL = 05 -------*
UPDAT    EQU   *
         AIF   ('&BER' EQ '').MCDAT2
         AIF   ('&BER'(1,1) EQ '(').MCDAT3
&NAME    LA    1,&BER
.MCDAT1  ANOP
UPDAT0   ZAP   UPDATV2+2(2),UPDATK28
UPDAT1   STM   0,2,UPDATVF1
         MVC   UPDATVD1(2),0(1)
         MVC   UPDATVD1+2(2),3(1)
         MVC   UPDATVD1+4(2),6(1)
         LA    2,UPDATVD1
         LA    0,6
UPDAT2   CLI   0(2),C'0'
         BL    UPDAT4
         CLI   0(2),C'9'
         BH    UPDAT4
         LA    2,1(2)
         BCT   0,UPDAT2
         PACK  UPDATV1,0(2,1)
         CLI   UPDATV2+2,X'03'
         BE    UPDAT3
         PACK  UPDATVD1(3),6(2,1)
         DP    UPDATVD1(3),UPDATK4
         TM    UPDATVD1+2,X'F0'
         BNZ   UPDAT3
         ZAP   UPDATV2+2(2),UPDATK29
UPDAT3   PACK  UPDATVD1,3(2,1)
         CP    UPDATVD1,UPDATK12
         BH    UPDAT4
         CVB   2,UPDATVD1
         SLA   2,1
         BZ    UPDAT4
         LA    2,UPDATV2-2(2)
         CP    UPDATV1,0(2,2)
         BH    UPDAT4
         BE    UPDAT5
UPDAT6   CP    UPDATV1,UPDATV2+7(1)
         BE    UPDAT4
         LM    0,2,UPDATVF1
         B     4(2)
UPDAT5   CLI   UPDATV2+2,X'03'
         BNE   UPDAT6
         MVI   1(1),X'F0'
         B     UPDAT6
UPDAT4   LM    0,2,UPDATVF1
         BR    2
UPDAT30  ZAP   UPDATV2+2(2),UPDATK30
         B     UPDAT1
UPDATK4  DC    P'4'
UPDATK12 DC    P'12'
UPDATK28 DC    P'28'
UPDATK29 DC    P'29'
UPDATK30 DC    P'30'
UPDATV1  DC    PL2'0'
UPDATV2  DC    P'31,28,31,30,31,30,31,31,30,31,30,31'
UPDATVF1 DC    3F'0'
UPDATVD1 DS    D
         MEXIT
.MCDAT2  AIF   ('&NAME' EQ '').MCDAT1
&NAME    EQU   *
         AGO   .MCDAT1
.MCDAT3  ANOP
&NAME    LR    1,&BER(1)
         AGO   .MCDAT1
.MCDAT4  MACMES 1,MCDAT
         MEND
         MACRO
         MACMES &CODE,&MAKRO
.***********************************************************
.*                                                         *
.*       MAKRO ZUR GENERIERUNG VON MNOTE-BEMERKUNGEN       *
.*             STANDARDTEXT IST   *** &MAKRO               *
.*                                                         *
.* CODE  TEXT                                              *
.* ****  ****                                              *
.*                                                         *
.*   1   MAKRO BEREITS EINGESETZT                          *
.*   2   PARAMETER FEHLT                                   *
.*   3   FEHLER IN PARAMETERANGABE                         *
.*                                                         *
.***********************************************************
         AIF   ('&CODE' EQ '' OR '&MAKRO' EQ '').NOTE1
         AIF   ('&CODE' EQ '0' OR '&CODE' GT '3').NOTE2
         AIF   ('&CODE' EQ '1').M1
         AIF   ('&CODE' EQ '2').M2
         AIF   ('&CODE' EQ '3').M3
.M1      MNOTE '*** &MAKRO MAKRO BEREITS EINGESETZT'
         MEXIT
.M2      MNOTE '*** &MAKRO PARAMETER FEHLT'
         MEXIT
.M3      MNOTE '*** &MAKRO FEHLER IN PARAMETERANGABE'
         MEXIT
.NOTE1   MACMES 2,MACMES
         MEXIT
.NOTE2   MACMES 3,MACMES
         MEND
         MACRO
&NAME    MCABEND &CODE,&DUMP
*-------LEVEL = 01-------*
         GBLA  &MACTMR
         GBLB &MCST
         LCLC &C
&C       SETC '&SYSNDX'
         AIF   (NOT &MCST).ERROR0
         MACTMR &CODE
         AIF   (&MACTMR EQ 3).ERROR2
         AIF   ('&DUMP' EQ '').MAC1
         AIF   ('&DUMP' NE 'DUMP').ERROR3
&NAME    STM   14,2,MC&C+12
         AIF   (&MACTMR EQ 0).MAC2
         CVD   &CODE(1),MC&C+4
         MVC   MC&C+4(3),MC&C+9
.MAC2    BALR 14,0
         ST    14,MC&C+8
         LA    1,MC&C
         B     MCSTERR
         CNOP 4,8
MC&C     DC    F'0'
         AIF   (&MACTMR EQ 0).MAC3
         DC    PL3'0'
.MAC4    DC    X'0'
         DC    F'0'
         DC    5F'0'
         MEXIT
.ERROR0 MNOTE 'KEIN MCST'
         MEXIT
.ERROR2 MACMES 2,MCABEND
         MEXIT
.ERROR3 MACMES 3,MCABEND
         MEXIT
.MAC1    ANOP
&NAME    ABEND &CODE
         MEXIT
.MAC3    DC    PL3'&CODE'
         AGO   .MAC4
         MEND
         MACRO
         MACTMR &A
         GBLA  &MACTMR
         LCLA  &B
         LCLC  &C
.********************************************************
.*                                                      *
.*       TEST DES OPERANDEN AUF REGISTER 2 BIS 12       *
.*       ANZEIGECODE IST DAS GLOBALE SETA-SYMBOL        *
.*                                                      *
.*       MACTMR = 0  WENN KEIN REGISTER (CHAR.AUSDRUCK) *
.*              = 1  WENN NICHT REG 2 - REG 12          *
.*              = 2  WENN REG 2 - REG 12                *
.*              = 3  WENN OPERAND NICHT VORHANDEN       *
.*                                                      *
.********************************************************
&MACTMR  SETA  0
         AIF   ('&A' NE '').MACTMR0
&MACTMR  SETA  3
         MEXIT
.MACTMR0 AIF   ('&A'(1,1) NE '(').MACTMR3
&MACTMR  SETA  1
         AIF   ('&A'(2,1) EQ 'R').MACTMR2
&C       SETC '&A'(2,K'&A-2)
.MACTMR1 ANOP
&B       SETA  &C
         AIF   (&B LT 2).MACTMR3
         AIF   (&B GT 12).MACTMR3
&MACTMR  SETA  2
         MEXIT
.MACTMR2 ANOP
&C       SETC '&A'(3,K'&A-3)
         AGO   .MACTMR1
.MACTMR3 MEND
         MACRO
&NAME    MCTF  &NR,&ADR,&L
         LCLA  &A,&B,&C
         LCLC  &D,&E,&F
         AIF   ('&NR' EQ '').MCTF5
         AIF   ('&ADR' EQ '' OR '&L' EQ '').MCTF5
         AIF   (&NR EQ 0).MCTF5
         AIF   (&NR GT 31).MCTF5
&F       SETC  'MCTF'.'&SYSNDX'
*------- LEVEL = 04 -------*
&NAME    STM   1,2,&F+20           REGISTER SICHERN
         TM    MCST108,X'80'       TESTLAUFPRUEFUNG
         BZ    &F+10
&A       SETA  &NR/8
&B       SETA  7-(&NR-&A*8)
&C       SETA  1
.MCTF1   AIF   (&B EQ 0).MCTF2
&B       SETA  &B-1
&C       SETA  &C*2
         AGO   .MCTF1
.MCTF2   AIF   ('&ADR'(1,1) NE '(').MCTF7
         LR    1,&ADR(1)           SATZADRESSE
         AGO   .MCTF8
.MCTF7   LA    1,&ADR              SATZADRESSE
.MCTF8   ANOP
&D       SETC  '&ADR'
&E       SETC  '&L'
         AIF   ('&ADR'(1,1) NE '(').MCTF3
         ST    &ADR(1),&F+4        ADRESSE SPEICHERN
&D       SETC  '0'
.MCTF3   AIF   ('&L'(1,1) NE '(').MCTF4
         STH   &L(1),&F+8          LAENGE SPEICHERN
&E       SETC  '0'
.MCTF4   ANOP
         CNOP  2,4
         BAL   2,MODSEK1           ANSPRUNG MODSEK
         DC    H'&NR'              ZUORDNUNGSNUMMER
         TM    MCST200+&A,&C
         BZ    &F+10
&F       BAL   2,MODPRT1           ANSPRUNG MODPRT
         DC    A(&D)               ADRESSE
         DC    H'&E'               LAENGE
         LM    1,2,&F+20
         B    *+14                 RUECKSPRUNG
         DC    2F'0'
         AGO   .MCTF6
.MCTF5   MACMES 3,MCTF
         MEXIT
.MCTF6   MEND
         MACRO
&NAME    MLOESCH
&NAME    MVI   DZ1+1,C' '
         MVC   DZ1+2(131),DZ1+1
         MEND
         MACRO
&NAME    MDRUCK &STZ
&NAME    MVI   DZ1,&STZ
         BAL   R2,UD1
         MEND
         PRINT NOGEN
**       STEUERUNG
XCOMPARE MCST
         B     H1
A0       B     B1
A1       CLI   VFEHLER,0
         BNE   A5
         B     E1
A2       CLI   VFEHLER,0
         BNE   A5
         B     C1
A3       B     G1
A4       B     MCSTEOJ
A5       B     F1
** VERARBEITUNG HYPIN
B1       B     BA1
B2       L     R15,MODKARTE
         BALR  R14,R15
         B     B3(R15)
B3       B     B6                       OK
         B     B5                       EODAD
         B     B5                       KEIN OPEN
         B     B4                       FOLGEKARTENFEHLER
B4       OI    VFEHLER,4                FEHLER IN KARTE
         B     B10
B6       LA    R3,8(R1)
         LA    R1,351(R1)
         ST    R1,MODKEY0+4
         MCTF  1,(3),80
         TRT   0(80,R3),TABNOBL
         BE    B2
         CLC   0(7,R1),KFIELDS
         BE    B7
         CLC   0(7,R1),KLIMITS
         BNE   B2
B7       ST    R1,VD1
         MVC   DZ1+6(120),0(R3) EINGABEKARTE FUER DRUCK
         MDRUCK X'19'
         MLOESCH
         L     R1,VD1
         B     BD1
B8       B     BE1
B9       B     B2
B5       B     BB1
B10      L     R4,MODKEYDS
         LTR   R4,R4
         BZ    B11
         CLC   0(2,R4),KIS
         BNE   B11
         MVC   V1HYPUT1(4),AKHYPUT3
         MVC   V1HYPUT2(4),AKHYPUT4
B11      EQU   *
         MCTF  1,MODKEY0,72
         MCTF  1,UA4,194
         DELETE EP=MODKARTE
         DELETE EP=MODKEY
         DELETE EP=MODSCAN
         B     A1
** MODULE LADEN
BA1      LOAD EP=MODKARTE
         ST    R0,MODKARTE
         LOAD EP=MODKEY
         ST    R0,MODKEY
         LOAD EP=MODSCAN
         ST    R0,MODSCAN
         B     B2
** PARAMETERPRUEFUNG
BB1      STM   R0,R7,BBVF1
         LA    R3,UA4
         LA    R4,PARMS
         LA    R1,MUSTERC
         BAL   R2,UH1
         CLI   VFEHLER,0
         BNE   BB2
         LA    R4,PARMNC
         LA    R1,MUSTERX
         BAL   R2,UH1
BB2      MVC   0(4,R3),MUSTERB
         LM    R0,R7,BBVF1
         B     B10
BBVF1    DC    8F'0'
** SCHLUESSELPARAMETER
BD1      LA    R1,7(R1)
         LR    R3,R1
         TRT   0(80,R3),TABNOBL
         BE    BD2
         ST    R1,MODKEY0
         LA    R1,MODKEY0
         L     R15,MODKEY
         BALR  R14,R15
BD3      B     B8
BD2      OI    VFEHLER,X'05'
         B     BD3
** AUSWERTUNG DER PARAMETER
BE1      L     R15,MODKEYS
         LTR   R15,R15
         BZ    BE10
         SH    R15,KH3
         MVI   0(R15),C' '
BE10     L     R15,MODKEYNC
         LTR   R15,R15
         BZ    BE11
         SH    R15,KH4
         MVI   0(R15),C' '
BE11     L     R4,MODKEYS
         LA    R5,MODKEYS
         LA    R6,PARMS
         LTR   R4,R4
         BZ    BE3
BE2      B     BEA1
BE6      XC    0(4,R5),0(R5)
BE3      L     R4,MODKEYNC
         LA    R5,MODKEYNC
         LA    R6,PARMNC
         LTR   R4,R4
         BNZ   BE2
         MCTF  2,MODKEY0,72
         L     R4,MODKEYPR
         LA    R5,MODKEYPR
         LA    R6,PARMPRO
         LTR   R4,R4
         BZ    BE5
BE4      B     BEB1
BE7      XC    0(4,R5),0(R5)
BE5      L     R4,MODKEYMA
         LA    R5,MODKEYMA
         LA    R6,PARMMAX
         LTR   R4,R4
         BNZ   BE4
         MCTF  2,PARMPRO,4
         B     B9
** FELDBEARBEITUNG
BEA1     ST    R4,MODSCAN0+4
         LA    R1,MODSCAN0
         L     R15,MODSCAN
         BALR  R14,R15
BEA6     CLI   0(R1),X'FF'
         BE    BEA7
         CLI   0(R1),X'AA'
         BNE   BEA5
         L     R8,4(R1)
         LA    R3,2
BEA4     BAL   R2,UF1
         CLI   UFV1,0
         BNE   BEA5
         STH   R0,0(R6)
         LA    R6,2(R6)
         BCT   R3,BEA2
         CLI   0(R8),C')'
         BNE   BEA5
         LA    R1,8(R1)
         B     BEA6
BEA7     B     BE6
BEA2     CLI   0(R8),C','
         BE    BEA3
BEA5     OI    VFEHLER,6
         B     BEA7
BEA3     LA    R8,1(R8)
         B     BEA4
** LIMITBEARBEITUNG
BEB1     LR    R8,R4
         BAL   R2,UF1
         CLI   UFV1,0
         BNE   BEB2
         STH   R0,0(R6)
BEB3     B     BE7
BEB2     OI    VFEHLER,6
         B     BEB3
** VERARBEITUNG DER EINGABESAETZE
C1       LA    R3,V1HYPUT1
         BAL   R2,UA1
         LR    R7,R1
C2       LA    R3,V1HYPUT2
         BAL   R2,UA1
         LR    R8,R1
C4       CLI   RECFM,0
         BE    C3
         CLC   0(2,R7),0(R8)
         BNE   C6
C3       MCTF  1,V2HYPUT1,8
         MCTF  1,V2HYPUT2,8
         CLC   V2HYPUT1,V2HYPUT2
         BE    C7
C6       BNL   C5
         CLI   SW,C'1'
         BE    C20
         CLI   SW,C'2'
         BNE   C21
         LH    R3,V1HYPUT2+12
         BAL   R2,UE1
C21      BAL   R2,UC1
         MVI   SW,C'1'
C20      LA    R3,V1HYPUT1
         BAL   R2,UB1
         BAL   R2,UA1
         LR    R7,R1
         B     C10
C8       B     CA1
C9       B     C1
C7       LH    R3,V1HYPUT1+12
         CLI   SW,C'1'
         BL    C11
         BE    C40
         LH    R3,V1HYPUT2+12
C40      BAL   R2,UE1
         BAL   R2,UC1
         MVI   SW,C'0'
C11      CLC   V2HYPUT1,KFF
         BNE   C8
         B     A3
C5       CLI   SW,C'2'
         BE    C30
         CLI   SW,C'1'
         BNE   C31
         LH    R3,V1HYPUT1+12
         BAL   R2,UE1
C31      BAL   R2,UC1
         MVI   SW,C'2'
C30      LA    R3,V1HYPUT2
         BAL   R2,UB1
         BAL   R2,UA1
         LR    R8,R1
C10      LH    R2,24(R3)
         LA    R2,1(R2)
         STH   R2,24(R3)
         B     C4
** INHALTSVERGLEICH
CA1      MVC   V1HYPUT1+14(2),PARMPRO
         MVC   V1HYPUT2+14(2),PARMPRO
         CLI   RECFM,0
         BE    CA3
         MVC   VLRECL,0(R7)
CA3      LH    R1,VLRECL
         LR    R4,R7
         LR    R5,R8
CA7      CH    R1,KH256
         BH    CA4
         BCTR  R1,0
         EX    R1,KCLC
         BE    CA5
CA6      LA    R3,V1HYPUT1
         BAL   R2,UB1
         LH    R2,26(R3)
         LA    R2,1(R2)
         STH   R2,26(R3)
         LA    R3,V1HYPUT2
         BAL   R2,UB1
         LH    R2,26(R3)
         LA    R2,1(R2)
         STH   R2,26(R3)
         BAL   R2,UC1
CA5      B     C9
CA4      CLC   0(256,R4),0(R5)
         BNE   CA6
         LA    R4,256(R4)
         LA    R5,256(R5)
         SH    R1,KH256
         B     CA7
** EROEFFNUNG
E1       L     R2,V1HYPUT1
         L     R3,V1HYPUT2
         OPEN  ((R2))
         TM    48(R2),X'10'
         BZ    E2
         OPEN  ((R3))
         TM    48(R3),X'10'
         BZ    E3
         MVC   LRECL,82(R2)
         MVC   VLRECL,82(R2)
         TM    36(R2),X'80'
         BZ    E4
         TM    36(R3),X'80'
         BZ    E5
         MVI   RECFM,0
E6       B     A2
E4       TM    36(R3),X'40'
         BZ    E5
         MVI   RECFM,4
         B     E6
E3       CLOSE ((R2))
         MVI   VFEHLER,2
         B     E6
E5       CLOSE ((R2))
         CLOSE ((R3))
         MVI   VFEHLER,3
         B     E6
E2       MVI   VFEHLER,1
         B     E6
** FEHLERMELDUNG
F1       SR    R2,R2
         IC    R2,VFEHLER
         BCTR  R2,0
         MH    R2,FLAENGE
         LA    R2,FM1(R2)
         MLOESCH
         MVC   DZ1+10(L'FM1),0(R2)
         MDRUCK X'11'
         ABEND 1234
**       ABSCHLUSS
G1       L     R2,V1HYPUT1
         L     R3,V1HYPUT2
         CLOSE ((R2),,(R3))
         MLOESCH
         MDRUCK X'19'
         MVC   DZ1+7(64),DK3
         MDRUCK X'09'
         MVI   DZ1+7,C'-'
         MVC   DZ1+8(63),DZ1+7
         MDRUCK X'11'
         MLOESCH
         MVC   DZ1+43(16),DK5
         MDRUCK X'11'
         LA    R3,V1HYPUT1
         BAL   R2,UG1
         LA    R3,V1HYPUT2
         BAL   R2,UG1
         MLOESCH
         MVC   DZ1+7(16),DK6
         MVC   DZ1+40(9),V1HYPUT1+28
         MVC   DZ1+50(9),V1HYPUT2+28
         MDRUCK X'11'
         MVC   DZ1+7(32),DK7
         MVC   DZ1+40(9),V1HYPUT1+37
         MVC   DZ1+50(9),V1HYPUT2+37
         MDRUCK X'11'
         MLOESCH
         MVC   DZ1+22(9),DK8
         MVC   DZ1+40(9),V1HYPUT1+46
         MVC   DZ1+50(9),V1HYPUT2+46
         MDRUCK X'11'
         CLOSE (HYPPRINT)
         B     A4
** DRUCKKOPF
H1       OPEN  (HYPPRINT,OUTPUT)
         MDRUCK X'89'
         LOAD EP=MODDATUM
         LR    R15,R0
         SR    R1,R1
         BALR  R14,R15
         MVC   DK4+5(8),0(R1)
         DELETE EP=MODDATUM
         MVC   DZ1+16(55),DK3+9
         MDRUCK X'09'
         MVI   DZ1+16,C'-'
         MVC   DZ1+17(54),DZ1+16
         MDRUCK X'19'
         MVI   DZ1,X'09'
         MLOESCH
         B     A0
**       SATZ LESEN
UA1      LM    R4,R6,0(R3)
         GET   ((R4))
         LA    R5,1(R5)
         ST    R5,4(R3)
         MVI   0(R6),C' '
         MVC   1(255,R6),0(R6)
UA4      DC    386X'0700'
UA2      ST    R1,20(R3)
         BR    R2
UA3      MVC   0(256,R6),KFF            EODAD
         LR    R1,R6
         B     UA2
** DRUCKSTEUERUNG
UB1      ST    R2,UBVF1
         L     R6,16(R3)
         MVC   0(2,R6),PARMPRO
         CLC   14(2,R3),KF0
         BE    UB5
         CLC   PARMMAX,KF0
         BE    UB6
         MLOESCH
         LA    R6,DZ1+1
         AH    R6,12(R3)
         MVC   0(7,R6),DK1
         L     R0,4(R3)
         CVD   R0,VD1
         UNPK  8(7,R6),VD1+4(4)
         OI    14(R6),X'F0'
         MDRUCK X'09'
         MVC   0(51,R6),DK2
         MDRUCK X'09'
         L     R5,20(R3)
         CLI   RECFM,0
         BE    UB2
         MVC   VLRECL,0(R5)
UB2      B     UBA1
UB3      LH    R0,PARMMAX
         BCTR  R0,0
         STH   R0,PARMMAX
         LH    R0,14(R3)
         BCTR  R0,0
         STH   R0,14(R3)
         MVC   VLRECL,HYPUT1+82
UB4      L     R2,UBVF1
         BR    R2
UB5      AP    VPRO,K1
         B     UB4
UB6      CLI   VMAX,0
         BNE   UB4
         MVI   VMAX,1
         MVC   DZ1+10(47),MELD2
         MDRUCK X'11'
         MLOESCH
         B     UB4
UBVF1    DC    F'0'
** SATZAUFBEREITUNG
UBA1     ZAP   DISPL,K0
UBA4     LA    R0,5
         LH    R15,VLRECL
         MVI   0(R6),C' '
         MVC   1(65,R6),0(R6)
         MVC   5(6,R6),KSCHAB1
         ED    5(6,R6),DISPL
         LA    R1,13(R6)
UBA6     CH    R15,KH4
         BNL   UBA2
         BCTR  R15,0
         EX    R15,KMVC1
         UNPK  0(9,R1),VAUF
         LA    R15,1(R15)
         AR    R15,R15
         LA    R1,0(R1,R15)
         MVC   0(7,R1),KBL
UBA3     TR    0(66,R6),KTR
         MDRUCK X'11'
         MLOESCH
         B     UB3
UBA2     MVC   VAUF(4),0(R5)
         UNPK  0(9,R1),VAUF
         MVI   8(R1),C' '
         LA    R1,9(R1)
         LA    R5,4(R5)
         SH    R15,KH4
         BZ    UBA3
         BCT   R0,UBA6
         STH   R15,VLRECL
         TR    0(66,R6),KTR
         MDRUCK X'09'
         MLOESCH
         AP    DISPL,K20
         B     UBA4
**       DRUCKABSCHLUSS
UC1      ST    R2,UCVF1
         CLC   PARMMAX,KF0
         BE    UC2
         MLOESCH
         MVI   DZ1+50,C'-'
         MVC   DZ1+51(31),DZ1+50
         MDRUCK X'11'
UC2      L     R2,UCVF1
         BR    R2
UCVF1    DC    F'0'
** DRUCKAUSGABE
UD1      PUT   HYPPRINT,DZ1
         BR    R2
**       MELDUNG 1
UE1      STM   R14,R3,UEVF1
         CP    VPRO,K0
         BE    UE2
         LA    R3,DZ1+(R3)
         MVC   0(61,R3),MELD1
         UNPK  8(7,R3),VPRO
         OI    14(R3),X'F0'
         ZAP   VPRO,K0
         MDRUCK X'09'
         MLOESCH
UE2      LM    R14,R3,UEVF1
         BR    R2
UEVF1    DC    7F'0'
** CHARACTER IN BINAER UMWANDELN
UF1      STM   R1,R3,UFVF1
         MVI   UFV1,0
         LR    R1,R8
         LA    R0,4
UF2      CLI   0(R1),C'0'
         BL    UF3
         CLI   0(R1),C'9'
         BH    UF3
         LA    R1,1(R1)
         BCT   R0,UF2
UF3      LA    R2,4
         SR    R2,R0
         BZ    UF5
         BCTR  R2,0
         LA    R3,7
         SLL   R3,4
         AR    R3,R2
         EX    R3,UFK1
         CVB   R0,UFVD1
         LR    R8,R1
UF4      LM    R1,R3,UFVF1
         BR    R2
UF5      OI    UFV1,X'80'
         B     UF4
UFK1     PACK  UFVD1,0(1,R8)
UFV1     DC    X'0'
UFVF1    DC    3F'0'
UFVD1    DS    D
** SUMMENBEREITSTELLUNG
UG1      L     R0,4(R3)
         CVD   R0,VD1
         MVC   UGV1,KSCHAB2
         ED    UGV1,VD1+3
         MVC   28(9,R3),UGV1+1
         LH    R0,24(R3)
         CVD   R0,VD1
         MVC   UGV1,KSCHAB2
         ED    UGV1,VD1+3
         MVC   37(9,R3),UGV1+1
         LH    R0,26(R3)
         CVD   R0,VD1
         MVC   UGV1,KSCHAB2
         ED    UGV1,VD1+3
         MVC   46(9,R3),UGV1+1
         BR    R2
UGV1     DC    CL10' '
** MUSTER MVCS
UH1      LA    R0,64
         SR    R5,R5
UH4      LH    R6,0(R4)
         LTR   R6,R6
         BZ    UH3
         C     R6,KF4096
         BH    UH2
         BCTR  R6,0
         LH    R7,2(R4)
         LTR   R7,R7
         BZ    UH2
         BCTR  R7,0
         STC   R7,1(R1)
         C     R1,AK1
         BE    UH5
         O     R5,KDI1
         STH   R5,2(R1)
         LA    R5,1(R5,R7)
         O     R6,KDI2
         STH   R6,4(R1)
UH6      MVC   0(6,R3),0(R1)
         LA    R3,6(R3)
         LA    R4,4(R4)
         BCT   R0,UH4
UH3      BR    R2
UH5      O     R6,KDI2
         STH   R6,2(R1)
         B     UH6
UH2      OI    VFEHLER,6
         B     UH3
** EINGABEBESCHREIBUNG
*        FIELDS S=(A,B),(A,B),.....,NC=(A,B),.....
*              A=DISPLACEMENT,B=LAENGE
*        LIMITS PRO=C,MAX=D,DSORG=IS
* BIS ZU 64 SORT FIELDS
* MAXIMALLAENGE EINES SORT FIELDS = 256 BYTES
* INNERHALB DER ERSTEN 4096 BYTES DES SATZES BEGINNEND
** WERTE AUS DEN PARAMETERKARTEN
PARMPRO  DC    H'3'
PARMMAX  DC    H'100'
PARMS    DC    64F'0'
PARMNC   DC    64F'0'
** PARAMETERLISTE FUER MODSCAN
MODSCAN  DC    F'0'
MODSCAN0 DC    H'64'
         DC    H'640'
         DC    F'0'
** PARAMETER FUER MODKARTE
MODKARTE DC    F'0'
** PARAMETERLISTE FUER MODKEY
MODKEY   DC    F'0'
MODKEY0  DC    F'0'
         DC    F'0'
         DC    CL8'S'
         DC    XL4'0'
MODKEYS  DC    F'0'
         DC    CL8'NC'
         DC    XL4'0'
MODKEYNC DC    F'0'
         DC    CL8'PRO'
         DC    XL4'0'
MODKEYPR DC    F'0'
         DC    CL8'MAX'
         DC    XL4'0'
MODKEYMA DC    F'0'
         DC    CL8'DSORG'
         DC    XL4'0'
MODKEYDS DC    F'0'
         DC    C' '
** DEFINITIONEN
KFIELDS  DC    CL7'FIELDS'
KLIMITS  DC    CL7'LIMITS'
KIS      DC    C'IS'
         DS    0F
KDI1     DC    X'00006000'
KDI2     DC    X'00001000'
KFF      DC    256X'FF'
K0       DC    P'0'
K1       DC    P'1'
K20      DC    P'20'
KH3      DC    H'3'
KH4      DC    H'4'
KH256    DC    H'256'
KF0      DC    F'0'
KF256    DC    F'256'
KF4096   DC    F'4096'
AK1      DC    A(MUSTERX)
AKHYPUT3 DC    A(HYPUT3)
AKHYPUT4 DC    A(HYPUT4)
KBL      DC    CL10' '
KSCHAB1  DC    X'402020202120'
KSCHAB2  DC    X'40202020202020202120'
KMVC1    MVC   VAUF(1),0(R5)
KCLC     CLC   0(1,R4),0(R5)
MUSTERC  MVC   0(0,R6),0(R1)
MUSTERX  MVC   0(0,R6),HEX0L256
MUSTERB  B     UA2
DK1      DC    C'SATZNR.'
DK2      DC    C'DISPLACEMENT '
         DC    C'0        4        8        12       16'
DK3      DC    C'ABSCHLUSSPROTOKOLL DES VERGLEICHSLAUFS MIT XCOMPARE'
DK4      DC    C' VOM XX.XX.XX'
DK5      DC    C'HYPUT1    HYPUT2'
DK6      DC    C'SAETZE INSGESAMT'
DK7      DC    C'DAVON UNGLEICH IM SORTIERBEGRIFF'
DK8      DC    C'IM INHALT'
MELD1    DC    C'WEITERE XXXXXXX SAETZE WEGEN PRO-BESCHRAENKUNG'
         DC    C' NICHT GEDRUCKT'
MELD2    DC    C'WEITERE SAETZE WEGEN GESAMTLIMIT NICHT GEDRUCKT'
HEX0L256 DC    XL256'0'
**       ALLGEMEINE BEREICHE
RECFM    DC    X'0'          4=V
LRECL    DC    H'0'
VLRECL   DC    H'0'
VD1      DS    D
DISPL    DC    PL3'0'
VAUF     DC    CL5' '
VFEHLER  DC    X'0'
VPRO     DC    PL4'0'
VMAX     DC    X'0'
SW       DC    C'0'
** DRUCKZEILEN
DZ1      DC    X'11'
         DC    CL132' '
         DC    CL10' '
** FEHLERUEBERSICHT
*        1     HYPUT1 FEHLER
*        2     HYPU2 FEHLER
*        3     RECFM NICHT GLEICH
*        4     FEHLER BEIM LESEN HYPIN
*        5     FEHLER IN HYPIN KARTE
*        6     FEHLER IN PARAMETER EINER HYPIN KARTE
FM1      DC    C'HYPUT1 FALSCH          '
FM2      DC    C'HYPUT2 FALSCH          '
FM3      DC    C'RECFM VERSCHIEDEN      '
FM4      DC    C'FEHLER BEIM LESEN HYPIN'
FM5      DC    C'FEHLER IN KARTE HYPIN  '
FM6      DC    C'FEHLER IN PARAMETER    '
FLAENGE  DC    H'23'
** TRT TABELLEN
TABNOBL  DC    CL64' '
         DC    X'0'
         DC    CL191' '
         ORG   *-240
KTR      DS    CL240
         DC    C'0123456789ABCDEF'
**       BEREICHE FUER HYPUT1
V1HYPUT1 DC    A(HYPUT1)
         DC    F'0'
         DC    A(V2HYPUT1)
         DC    H'0'
         DC    H'3'
         DC    A(V1HYPUT2+14)
         DC    F'0'
         DC    2H'0'
         DC    3CL9' '
V2HYPUT1 DC    CL256' '
**       BEREICHE FUER HYPUT2
V1HYPUT2 DC    A(HYPUT2)
         DC    F'0'
         DC    A(V2HYPUT2)
         DC    H'66'
         DC    H'3'
         DC    A(V1HYPUT1+14)
         DC    F'0'
         DC    2H'0'
         DC    3CL9' '
V2HYPUT2 DC    CL256' '
**       DCBS
HYPUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=HYPUT1,EODAD=UA3
HYPUT2   DCB   DSORG=PS,MACRF=GL,DDNAME=HYPUT2,EODAD=UA3
HYPUT3   DCB   DSORG=IS,MACRF=GL,DDNAME=HYPUT1,EODAD=UA3
HYPUT4   DCB   DSORG=IS,MACRF=GL,DDNAME=HYPUT2,EODAD=UA3
HYPPRINT DCB DSORG=PS,MACRF=PM,DDNAME=HYPPRINT,LRECL=133,RECFM=FBM,    X
               BLKSIZE=1330
         END   XCOMPARE
