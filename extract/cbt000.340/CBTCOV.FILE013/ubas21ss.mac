* STAND 09/22/80
***********************************************************************
*                                                                     *
*    PROGRAMM  = GETASUM                                              *
*                                                                     *
*    AUFRUF    = CALL GETASUM,(ERR,ANR,NACH,PER,BZT),VL               *
*        F       ERR = FEHLERCODE = 0 KEIN FEHLER                     *
*                                 = 1 ABST.SUM.SATZ NICHT AUF DATEI   *
*                                 = 2 ABST.PERIODE NICHT GESPEICHERT  *
*                                 = 3 DD-KARTE FEHLT                  *
*                                 = 4 SONST.FEHLER BEIM LESEN         *
*        CL10    ANR = ABSTIMMSUMMEN-NR,FUER DIE SATZ ZU LESEN IST    *
*        CL26    NACH= EMPFANGSFELD FUER GEWUENSCHTE PERIODE          *
*        H       PER = NR.DER GEWUENSCHTEN PERIODE                    *
*                        = 0 LAUT BZT ZU BESTIMMEN                    *
*                        = 1 LETZTE    PERIODE                        *
*                        = 2 VORLETZTE PERIODE                        *
*                        USW.                                         *
*        CL15    BZT = BERICHTSZEIT (NUR BEI PER=0)                   *
*                                                                     *
*    ZWECK     = EINLESEN DES ABSTIMMSUMMENSATZES (KEY = ANR)         *
*                UND UEBERGABE DER GEWUENSCHTEN PERIODENSUMME         *
*                IN NACH                                              *
*                                                                     *
***********************************************************************
GETASUM  CSECT
         XSAVE 12,SAVE,GETASUM
         AREG
         LR    R11,R1                   --> ARGLISTE RETTEN
         USING LFLAG,R9
         L     R9,PSATZ       VSAM I/O BEREICH
         XC    ERR,ERR
         B     DCBOK
         CALL  CENQAST,(KS)
         CALL  VSAMIO,(OPEN)
         LTR   R15,R15
         BZ    DCBOK
         ST    R15,ERR
         BAL   R10,DEQ                 DEQUEUE
         B     RETURN
DCBOK    EQU   *
* LESEN  ABSTIMMSUMME
         L     R3,4(R11)                --> ASUMNR
         CALL  VSAMIO,(GET,(R3))
         ST    R15,ERR
         LTR   R15,R15
         BNZ   CLOSE010       KEIN SATZ GELESEN
* ABSTIMMSUMME GELESEN
READOK   EQU   *
         L     R10,12(R11)              --> GEWUENSCHTE PERIODE
         XR    R3,R3
         XR    R4,R4
         LH    R4,NPER                  ANZ.PER.IM SATZ
         LH    R3,0(R10)                GEWUENSCHTE PERIODE
         LTR   R3,R3
         BZ    PERUN                    =0 --> BERICHTSZEIT VORGEGEB.
         CR    R3,R4
         BNH   PEROK
PERNO    EQU   *
         MVC   ERR,=F'2'                GEWUENSCHTE PERIODE NICHT AUF
         B     CLOSE010       DATEI
PEROK    EQU   *
         SR    R4,R3
         MH    R4,=H'26'
         LA    R9,LFLAG(R4)             -->GEWUENSCH.BERZT.+SUMME
         B     PERMVC
* GEWUENSCHTE PERIODE LAUT BERICHTSZEIT SUCHEN
PERUN    EQU   *
         L     R5,16(R11)               --> GEWUENSCHTE BERZEIT
PERLOOP  EQU   *
         CLC   BERZT,0(R5)              BERZEIT-VERGLEICH
         BE    PERFND
         BCTR  R4,0                     ZAEHLER UEBER PERIODEN
         BZ    PERNO
         LA    R9,PERLN(R9)             -->NAECHSTE PERIODE
         B     PERLOOP
PERFND   EQU   *
         STH   R4,0(R10)                PERIODE LAUT BERZEIT
PERMVC   EQU   *
         L     R4,8(R11)                --> EMPFANGSFELD
         MVC   0(PERLN,R4),PERDEF       BERZEIT+SUMME -->AUFRUFENDE PGM
         SPACE 2
CLOSE010 EQU   *
         B     RETURN
         BAL   R10,CLSAST
RETURN   EQU   *
         L     R10,0(R11)               -->RETURNCODE
         MVC   0(4,R10),ERR             RETURNCODE SETZEN
         XRETURN 0
CLSAST   EQU   *
         CALL  VSAMIO,(CLOSE)
         LTR   R15,R15
         BZ    DEQ
         ST    R15,ERR        RETURNCODE
DEQ      EQU   *
         CALL  CDEQAST
         BR    R10
         SPACE 2
* DEFINITIONEN
*
ERR      DS    F
PSATZ    DC    V(ASTSATZ)
OPEN     DC    H'0'
GET      DC    H'4'
CLOSE    DC    H'12'
KS       DC    CL1'S'
         MASATZ
         END
CLOSE010 EQU   *
