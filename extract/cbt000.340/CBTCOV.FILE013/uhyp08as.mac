ERR      TITLE 'FEHLERBEHANDLUNG   UHYP08AS = MODERR'
MODERR   START 0
         SPACE 5
R0       EQU   0
R1       EQU   1                       PARAMETERLISTE
R2       EQU   2                       BASISREGISTER
R3       EQU   3                       PIE ADRESSE
R4       EQU   4                       ADRESSKONSTANTE DES PROGRAMMN.
R5       EQU   5                       AUSGABE DCB
R6       EQU   6                       STARTADRESSE
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11                      WTO-LISTE
R12      EQU   12                      UP-SPRUNG
R13      EQU   13                      SAVEAREA MCSTERR1
R14      EQU   14
R15      EQU   15
         SPACE 5
**       BASISREGISTER LADEN
         SPACE 3
         LR    R0,R2
         BALR  R2,0
         USING *,R2
         SPACE 5
**       RUECKSPRUNG SICHERN
         SPACE 3
         ST    R14,VF2
         ST    R0,VF3
         SPACE 5
**       REGISTERINHALTE
         SPACE 3
         STM   R0,R15,VF1
         L     R1,0(R1)
         LM    R3,R8,0(R1)
         ST    R8,VF1+52
         MVC   VF1(12),20(R3)
         MVC   VF1+56(8),12(R3)
         SPACE 5
**       PROGRAMMNAME
         SPACE 3
         L     R7,0(R4)
         MVC   WTO1+8(8),0(R7)
         MVC   WTO2+8(8),0(R7)
         SPACE 5
**       PSW FUER AUSGABE
         SPACE 3
         UNPK  V1(9),4(5,R3)
         TR    V1(8),K1-240
         MVC   WTO1+27(7),V1
         MVC   WTO1+35(1),V1+7
         L     R7,8(R3)
         BAL   R12,UB1
         MVC   WTO1+37(8),UBV1
         MVC   WTO1+88(8),UBV2
         LA    R11,WTO1
         BAL   R12,UA1
         SPACE 5
**       REGISTERINHALTE BEI PU AUFBEREITEN
         SPACE 3
         LA    R8,VF1
         LA    R9,K2-5
         LA    R10,4
         LA    R11,WTO2
A1       LA    R1,WTO2+28
         LA    R0,4
A2       L     R7,0(R8)
         BAL   R12,UB1
         MVC   0(8,R1),UBV1
         MVC   42(8,R1),UBV2
         LA    R8,4(R8)
         LA    R1,9(R1)
         BCT   R0,A2
         BAL   R12,UA1
         LA    R9,5(R9)
         MVC   WTO2+22(5),0(R9)
         MVC   WTO2+64(5),0(R9)
         BCT   R10,A1
         SPACE 5
**       RUECKSPRUNG
         SPACE 3
         LM    R4,R15,VF1+16
         L     R14,VF2
         L     R2,VF3
         BR    R14
         SPACE 5
**       AUSGABE UP
         SPACE 3
*        R5    DCB
*        R11   WTO-LISTE
         SPACE 3
UA1      TM    48(R5),X'10'
         BZ    UA3
         CLI   20(R11),C'2'
         BNE   UA2
         PUT   (R5),V2
UA2      MVI   4(R11),X'09'
         PUT   (R5),(R11)
         BR    R12
UA3      LR    R1,R11
         WTO   MF=(E,(1))
         BR    R12
         SPACE 5
**       FESTWORT AUFBEREITEN
         SPACE 3
*        R6    STARTADRESSE
*        R7    FESTWORT
         SPACE 3
UB1      STM   R7,R9,UBVF2
         MVC   UBV2(8),UBK1
         LA    R9,2
         LA    R8,UBV1
UB2      ST    R7,UBVF1
         UNPK  0(9,R8),UBVF1(5)
         TR    0(8,R8),K1-240
         LA    R8,9(R8)
         LA    R7,0(R7)
         CR    R7,R6
         BL    UB3
         SR    R7,R6
         BM    UB3
         BCT   R9,UB2
UB3      LM    R7,R9,UBVF2
         BR    R12
UBV1     DC    CL9' '
UBV2     DC    CL9' '
UBVF1    DC    F'0'
UBVF2    DC    3F'0'
UBK1     DC    C'-SINNLOS'
         SPACE 5
**       DEFINITIONEN
         SPACE 3
VF1      DC    16F'0'                  REGISTERINHALTE BEI PU
VF2      DC    F'0'                    RUECKSPRUNGADRESSE
VF3      DC    F'0'
V1       DC    XL9'0'                  AUFBEREITUNGSFELD
         DS    0F
V2       DC    H'6'
         DC    H'0'
         DC    X'8940'
K1       DC    C'0123456789ABCDEF'
K2       DC    C' 4- 7'
         DC    C' 8-11'
         DC    C'12-15'
         SPACE 5
WTO1     WTO   '*** PROGNAME 3233  PSW XXXXXXX X XXXXXXXX              X
                   ABZUEGLICH STARTADRESSE  XXXXXXXX         ',MF=L
         DC    CL37' '
         SPACE 5
WTO2     WTO   '*** PROGNAME 3234  0- 3 XXXXXXXX XXXXXXXX XXXXXXXX XXXX/
               XXXX  0- 3 XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX',MF=L
         DC    CL37' '
         END   MODERR
