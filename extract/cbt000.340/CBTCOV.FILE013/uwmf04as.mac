*              1.FUNKTION VON REPLX
*              DA IM VORSATZ (HDR1) VON SL-BAND-DATEIEN NUR DIE LETZTEN
*              17 STELLEN DES DS-NAMENS ABGESPEICHERT WERDEN (FELD FL1ID
*              KOENNEN BANDDATEIEN DURCH MEHRERE DS-NAMEN IDENTIFIZIERT
*              WERDEN.
*              ERFORDERNIS DIESER DS-NAMEN IST ABER,
*                          A) DASS SIE IN DEN LETZTEN 17 STELLEN
*              MIT DEM INHALT DES VORSATZES UEBEREINSTIMMEN.
*              B) DASS ALLE DS-NAMEN MIT DEN RICHTIGEN
*              VOLUME-INFORMATIONEN KATALOGISIERT SIND.
*              BEISPIEL:
*              EINE BAND-DATEI WURDE AUFGEBAUT MIT DSN = R.FR251000.G001
*              INFORMATION IM VORSATZ          FR251000.G0012V00
*              DIE DATEI KANN NUN AUFGERUFEN WERDEN MIT
*                                  DSN=R.FR251000.G0012V00
*              ODER                DSN=A.FR251000.G0012V00
*              ODER                DSN=BETA.FR251000.G0012V00
*              ODER                DSN=FR251000.G0012V00
*              DIE DAZU NOTWENDIGEN KATALOGUMSETZARBEIT FUEHRT NUN REPLX
*              INDEM ES
*              BESTEHENDE DATEI-VOLUME-INFORMATIONEN MIT NEUEN DS-NAMEN
*              KNUEPFT UND DIESE NEUEN DATEI-VOLUME-INFORMATIONEN KATALO
*              BEISPIEL:
*              EINE DATEI IST KATALOGISIERT MIT FOLGENDEN ANGABEN:
*                                  DSN=R.FR251000.G0012V00
*                                  UNIT=TAPE,VOL=SER=(004711,004712)
*              DURCH REPLX KOENNTE EIN WEITERER KATALOGEINTRAG MIT FOLGE
*              INHALT GEMACHT WERDEN:
*                                  DSN=A.FR251000.G0012V00,
*                                  UNIT=TAPE,VOL=SER=(004711,004712)
*              BEISPIEL:
*              TAEGLICH WIRD EIN NEUER BESTAND DER GENERATIONSGRUPPE R.F
*              ERSTELLT.
*              ALLE BAENDER WERDEN SOWOHL ZUR MONATL.ALS AUCH ZUR JAEHRL
*              AUSWERTUNG BENOETIGT.
*              SIE BRAUCHEN ALSO NUN 2 GDG'S
*              EIN MONATLICHES FUER DIE BAENDER EINES MONATS,
*                          DAS AM ENDE DES MONATS GELOESCHT WIRD.
*              EIN JAEHRLICHES FUER ALLE BAENDER,DAS ENDE DES
*                          JAHRES GELOESCHT WIRD.
*              LOESUNG MIT REPLX:
*                          A) SIE LEGEN 2 GDG'S AN,ALSO
*                          R.FR251000      ALS JAHRES-GDG
*                          S.FR251000      ALS MONATS-GDG
*                          B) SIE KATALOGISIEREN DIE TAEGLICHEN BAENDER
*                          IM JAHRES-GDG,ALSO
*                          DSN=R.FR251000(+1),DISP=(NEW,CATLG,DELETE)
*                          ALS NAECHSTEN STEP RUEFEN SIE REPLX AUF UND
*                          KATALOGISIEREN DAS NEUERSTELLTE BAND AUCH IM
*                          MONATS-GDG.
*                          C) NACH JEDER MONATS-AUSWERTUNG LOESCHEN SIE
*                          DAS MONATS-GDG
*                          DSN=S.FR251000,DISP=(OLD,UNCATLG,KEEP)
*              2.AUFRUF VON REPLX
*              A) EXEC - KARTE:
*              //SA990640 EXEC PGM=SA990640
*              B) SYSPRINT - KARTE:
*              //SYSPRINT DD SYSOUT=A
*              C) SYSIN - KARTE (WAHLWEISE):
*              //SYSIN     DD  *
*                  DAHINTER REPLX-CONTROL-KARTEN.
*              D) REPLX-CONTROL-KARTEN (DATA-SETS)
*              DIE FORMALEN VORAUSSETZUNGEN SIND DIE DER
*              UTILITY-CONTROL-KARTEN
*              (NAME)      REPLX   DSNAME=     DSNAME1
*                                  DSN=        (DSNAME1,UNCATLG),
*                                  NEWNAME=    DSNAME2
*                                              (DSNAME2,UNCATLG)
*              BEISPIEL:
*                  REPLX   DSN=R.FR251000.G0012V00,                   *
*                          NEWNAME=A.FR251000.G0012V00
*                  REPLX   DSN=R.FR251000.G0012V00,                   *
*                          NEWNAME=FR251000.G0012V00
*                  REPLX   DSN=R.FR251000(0),                         *
*                          NEWNAME=S.FR251000
*                  WIE AUS DEM BEISPIEL ERSICHTLICH,WIRD - WENN BEI
*                  DSN EINE RELATIVE GDG-NR. ANGEGEBEN
*                  WIRD - BEI NEWNAME NUR DAS GDG GENANNT.
*                  REPLX   DSN=(R.FR251000(0),UNCATLG),               *
*                          NEWNAME=(S.FR251000,UNCATLG)
*                  WIRD BEI NEWNAME UNCATLG ANGEGEBEN,DANN WIRD VOR DER
*                  UMKATALOGISIERUNG DER NEUE NAME-FALLS VORHANDEN-
*              ENTKATALOGISIERT.
*                  WIRD BEI DSN UNCATLG ANGEGEBEN,DANN WIRD NACH GE-
*              GLUECKTER UMKATALOGISIERUNG DER ALTE DSNAME ENTKATALOGISI
*              E) REPLX-CONTROL-KARTEN (GDG'S)
*              (NAME)      REPLX   GDG=  GDG1
*                                        (GDG1,UNCATLG)  ,            *
*                                        NEWGDG=  GDG2
*                                                 (GDG2,UNCATLG)
*              WIRD BEI NEWGDG UNCATLG ANGEGEBEN,DANN WERDEN VOR DER
*              UMKATALOGISIERUNG ALLE DATASETS DES NEUEN GDG'S ENT-
*              KATALOGISIERT.
*              WIRD BEI GDG UNCATLG ANGEGEBEN,DANN WIRD NACH JEDER
*              UMKATALOGISIERUNG EINES DSNAMENS DER ALTE DSNAME GELOESCH
*              BEISPIEL: GDG MIT +1 NEW UND WIEDERANZIEHUNG IM
*              GLEICHEN JOB MIT 'OLD'.
*              PROBLEM: WIRD MIT +1 EIN GDG IM JOB ERSTELLT,
*              DASS NOCHEINMAL MIT OLD ANGEZOGEN WIRD,
*              MUSS LT.JCL DIESER FILE MIT (+1) ANGESPROCHEN WERDEN.
*              ENTSTEHT ZWISCHEN ERSTELLUNG UND WIEDERANZIEHUNG EIN REST
*              SO IST DER FILE VON '+1' AUF '0' UMZULOCHEN.
*              LOESUNG MIT REPLX:
*                                  JOB
*              STEP 1      DSN=R.FB......(+1),
*                          DISP=(NEW,CATLG,DELETE),
*              STEP 2  REPLX  DSN=R.FB......(0),NEWNAME=S.FB......
*              STEP 8      DSN=S.FB......(0),
*                          DISP=(OLD,UNCATLG,KEEP)
*              4.INTERNE LOGIK
*              SA990640  BESTEHT AUS 2 ROUTINEN,
*              SA990320 (CTLREPLX),DAS EROEFFNUNG UND ABSCHLUSS
*              DURCHFUEHRT SOWIE PARAMETER-KARTEN INTERPRETIERT,
*              SA990330 (REPLX),DAS DIE VERLANGTEN FUNKTIONEN AUSFUEHRT.
*              DIE FOLGE DER FUNKTIONEN IST:
*              1.UNCATLG - NEWGDG
*              2.CATLG - NEWNAME
*              3.UNCATLG - DSNAME
*              BEI NORMALER AUSFUEHRUNG ENDET REPLX MIT RC = O.
*              BEI FEHLERN ENDET REPLX MIT ABEND:
*                              U0004  :    FORMALE - FEHLER
*                              U0008  :    KATALOG UNVOLLSTAENDIG
*                              U0012  :    KATALOG - FEHLER
*                              U0100  :    UNERKLAERLICHER PROGRAMMFEHLE
         TITLE 'CTLREPLX    WMF-UTILITY   G.HELLRIEGEL'
*--------------FUNKTION VON CTLREPLX----------------------------------*
*                                                                     *
*                                                                     *
*              CTLREPLX STEUERT DIE FUNKTION VON REPLX (SIEHE DORT).  *
*                                                                     *
*              WESENTLICHE TEILE CON CTLREPLX SIND                    *
*                                                                     *
*                   1    START-MELDUNG AUF SYSPRINT                   *
*                                                                     *
*                   2.   EINLESEN UND ANALYSIEREN SYSIN.              *
*                                                                     *
*                   3.   FUER GDG - SUCHEN ANZAHL GENERATIONEN (OLD)  *
*                                   EVTLL. ENTKATALOGISIEREN (NEW)    *
*                                                                     *
*                   4.   AUFBEREITEN PARMLIST UND CALL REPLX          *
*                                                                     *
*                   5.   RETURN MIT RETURN-CODE.                      *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*--------------AUFBAU PARAMETERKARTEN---------------------------------*
*                                                                     *
*              1. REPLACE DATASET-NAME                                *
*                                                                     *
*              NAME     REPLX DSNAME=(DSET1,UNCATLG),                 *
*                             NEWNAME=(DSET2,UNCATLG)                 *
*                                                                     *
*              WIRD ALS DSET1-NAME EIN GDG-NAME MIT RELATIVER NUMMER  *
*              ANGEGEBEN, SO DARF DSET2 NUR DEN GDG-NAMEN BEZEICHNEN. *
*                                                                     *
*              WIRD DSET1 MIT UNCATLG ANGEGEBEN, SO WIRD DER DSET1-   *
*              NAME NACH DER NEUKATALOGISIERUNG GELOESCHT.            *
*              WIRD DSET2 MIT UNCATLG ANGEGEBEN, SO WIRD - FALLS      *
*              ERFORDERLICH - ZUERST DER DSET2-NAME GELOESCHT.        *
*                                                                     *
*                                                                     *
*              2. REPLACE GENERATION-GROUP                            *
*                                                                     *
*              NAME     REPLX GDG=(GDG1,UNCATLG),                     *
*                       NEWGDG=(GDG,UNCATLG)                          *
*                                                                     *
*              WIRD GDG1 MIT UNCATLG ANGEGEBEN, SO WERDEN ALLE DATA-  *
*              SETS VON GDG1 NACH DER NEUKATALOGISIERUNG GELOESCHT.   *
*              WIRD GDG2 MIT UNCATLG ANGEGEBEN, SO WERDEN ALLE DATA-  *
*              SETS VON GDG2 - FALLS ERFORDERLICH - ZUERST GELOESCHT. *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
SA990320 CSECT
         SPACE 3
*--------------PROGRAMMEROEFFNUNG-------------------------------------*
         SPACE 2
A00      EQU   *                     ---BASISREG.-SAVEAREA---
         SAVE  (14,12),,*
         LR    R12,R15
         USING A00,R12                  BASISREGISTER INSTRUKTIONEN
         LR    R14,R13
         LA    R13,SAVE
         USING SAVE,R13                 BASISREGISTER DEFINITIONEN
         ST    R13,8(,R14)
         ST    R14,4(,R13)
         EJECT
*--------------EROEFFNUNGSMELDUNG-------------------------------------*
         SPACE 2
B00      EQU   *                     ---SYSPRINT-MELDUNG---
         OPEN  (SYSPRINT,OUTPUT)
         LA    R10,4
         TM    SYSPRINT+48,X'10'
         BNO   K30
         MVC   ABER,ABER-1
         MVI   ABER,C'1'
         MVC   ABER+1(30),=C'*** REPLX WURDE AUFGERUFEN ***'
         TIME  DEC
         STM   R0,R1,TIME
         MVC   ABER+40(26),=C'DATUM          ZEIT       '
         MVC   ABER+46(7),=X'4021204B202020'
         MVC   ABER+60(6),=X'4021204B2020'
         ED    ABER+46(7),DATE+1
         ED    ABER+60(6),TIME
         PUT   SYSPRINT,ABER
         MVC   ABER,ABER-1
         PUT   SYSPRINT,ABER
         SPACE 2
         OPEN  (SYSIN)
         LA    R10,4
         TM    SYSIN+48,X'10'
         BNO   K30
         EJECT
*--------------GRUNDSTELLUNG VERARBEITUNG-----------------------------*
         SPACE 2
C00      EQU   *                     ---LOESCHEN UEBERG.-BEREICHE---
         MVI   STEUER,X'00'
         MVI   OLDDSN,C' '
         MVC   OLDDSN+1(47),OLDDSN
         MVI   NEWDSN,C' '
         MVC   NEWDSN+1(47),NEWDSN
         MVC   OLDINDX,=H'0'
         MVC   NEWINDX,=H'0'
         EJECT
*--------------INTERPRETATION SYSIN-----------------------------------*
         SPACE 2
D00      EQU   *                     ---LESEN - TRENNUNG---
         GET   SYSIN
         LR    R10,R1
         MVC   ABER,ABER-1
         MVC   ABER(80),0(R10)
         PUT   SYSPRINT,ABER
         MVI   71(R10),X'00'            X'00' LOGICAL END
         SPACE
         CLI   STEUER,X'00'
         BNE   D60                      FOLGEKARTE
         SPACE
D10      EQU   *                     ---TRT NAMENSFELD VORHANDEN---
         XR    R2,R2
         TRT   0(1,R10),TRTBLANK
         B     *+4(R2)
         B     D40                      BLANK
         B     D20                      BUCHSTABE
         B     L00                      ZIFFER
         B     L00                      INVALID SIGN
         B     L00                      X'00' END SIGN
         SPACE 2
D20      EQU   *                     ---TRT NAMENSFELD---
         XR    R2,R2
         TRT   0(9,R10),TRTSIGN
         B     *+4(R2)
         B     L00                      BUCHSTABE/ZIFFER
         B     D30                      BLANK
         B     L00                      PUNKT
         B     L00                      KLAMMER
         B     L00                      KOMMA
         B     L00                      INVALID
         B     L00                      X'00' END SIGN
         SPACE
D30      EQU   *                     ---BEGINN SPACE SETZEN---
         LR    R10,R1
         SPACE
D40      EQU   *                     ---TRT SPACEFELD---
         XR    R2,R2
         TRT   0(80,R10),TRTBLANK
         B     *+4(R2)
         B     L04                      BLANK
         B     D50                      BUCHSTABE
         B     L04                      ZIFFER
         B     L04                      INVALID SIGN
         B     L04                      X'00' END SIGN
         SPACE 2
D50      EQU   *                     ---TEST OPERATION---
         LR    R10,R1
         CLC   0(5,R10),=C'REPLX'
         BNE   L04
         LA    R10,5(R10)
         SPACE
D60      EQU   *                     ---TRT SPACEFELD---
         XR    R2,R2
         TRT   0(80,R10),TRTBLANK
         B     *+4(R2)
         B     L08                      BLANK
         B     D70                      BUCHSTABE
         B     L08                      ZIFFER
         B     L08                      INVALID
         B     D140                     ENDE
         SPACE 2
D70      EQU   *                     ---TEST KEYWORD---
         LR    R10,R1
         CLC   0(7,R10),=C'DSNAME='
         BE    D80
         CLC   0(4,R10),=C'DSN='
         BE    D90
         CLC   0(8,R10),=C'NEWNAME='
         BE    D110
         CLC   0(4,R10),=C'GDG='
         BE    D120
         CLC   0(7,R10),=C'NEWGDG='
         BE    D130
         B     L12
         SPACE
D80      EQU   *                     ---KEY = DSNAME---
         LA    R10,7(,R10)
         B     D100
         SPACE
D90      EQU   *                     ---KEY = DSN---
         LA    R10,4(,R10)
         SPACE
D100     EQU   *                     ---TEST AND SET OLDDSN---
         CLI   OLDTYPE,C' '
         BNE   L16
         MVI   OLDTYPE,C'0'
         LA    R7,OLDDSN
         B     E00
         SPACE
D110     EQU   *
         LA    R10,8(,R10)           ---KEY = NEWNAME---
         CLI   NEWTYPE,C' '
         BNE   L16
         MVI   NEWTYPE,C'0'
         LA    R7,NEWDSN
         B     E00
         SPACE
D120     EQU   *                     ---KEY = GDG---
         LA    R10,4(,R10)
         CLI   OLDTYPE,C' '
         BNE   L16
         MVI   OLDTYPE,C'3'
         LA    R7,OLDDSN
         B     E00
         SPACE
D130     EQU   *                     ---KEY = NEWGDG---
         LA    R10,7(,R10)
         CLI   NEWTYPE,C' '
         BNE   L16
         MVI   NEWTYPE,C'3'
         LA    R7,NEWDSN
         B     E00
         SPACE 2
D140     EQU   *                     ---KEIN OPERAND---
         CLI   STEUER,X'00'
         BNE   L20
         MVI   STEUER,X'FF'
         B     D00
         EJECT
*--------------UEBERTRAGEN OPERANDENFELDER----------------------------*
         SPACE 2
E00      EQU   *                     ---TEST BEGINN NAME---
         MVI   0(R7),C'0'
         CLI   0(R10),C'('
         BNE   E05
         LA    R10,1(,R10)
         MVI   0(R7),C'1'
E05      EQU   *                     ---SICHERN ANFANG NAME---
         LA    R8,0(,R10)
         SPACE
E10      EQU   *                     ---TRT INDEX---
         XR    R2,R2
         TRT   0(80,R10),TRTSIGN
         B     *+4(R2)
         B     L28                      BUCHSTABE/ZIFFER
         B     E70                      BLANK
         B     E30                      PUNKT
         B     E40                      KLAMMER
         B     E70                      KOMMA
         B     L08                      INVALID
         B     E70                      END
         SPACE 2
E30      EQU   *                     ---PUNKT GEFUNDEN---
         LH    R6,2(,R7)
         LA    R6,1(,R6)
         STH   R6,2(,R7)
         LA    R10,1(,R1)
         B     E10
         SPACE 2
E40      EQU   *                     ---KLAMMER GEFUNDEN---
         CLI   0(R1),C')'
         BE    E70
         CLI   1(R7),C'0'
         BNE   L08
         MVI   1(R7),C'2'
         LA    R10,1(,R1)
         CLC   0(2,R10),=C'0)'
         BE    E60
         CLI   0(R10),C'-'
         BNE   L24
         SPACE
E50      EQU   *                     ---REL. NUMBER - TEST ENDE---
         LA    R10,1(,R10)
         TM    0(R10),X'F0'
         BO    E50
         CLI   0(R10),C')'
         BNE   L24
         LA    R1,1(,R10)
         B     E70
         SPACE
E60      EQU   *                     ---REL. NUMBER GENERATION 0---
         LA    R1,2(,R10)
         B     E70
         SPACE
E70      EQU   *                     ---NAMENSENDE - UEBERTRAGUNG---
         LR    R10,R1
         LA    R1,0(,R1)
         SR    R1,R8
         BZ    L28
         CH    R1,=H'44'
         BL    *+8
         B     L28
         BCTR  R1,0
         EX    R1,MVC
         LH    R6,2(,R7)
         LA    R6,1(,R6)
         STH   R6,2(,R7)
         CLI   0(R7),C'1'
         BNE   F00
         CLI   0(R10),C')'
         BE    E80
         CLC   0(9,R10),=C',UNCATLG)'
         BNE   L32
         LA    R10,9(,R10)
         B     F00
         SPACE
E80      EQU   *                     ---KLAMMERN OHNE UNCATLG---
         MVI   0(R7),C'0'
         LA    R10,1(,R10)
         B     F00
         EJECT
*--------------ENDE OPERAND-------------------------------------------*
         SPACE 2
F00      EQU   *                     ---KOMMA---
         CLI   0(R10),C','
         BNE   F20
         CLI   1(R10),C' '
         BE    F10
         CLI   1(R10),X'00'
         BE    F10
         LA    R1,1(,R10)
         B     D70
         SPACE
F10      EQU   *                     ---FOLGEKARTE ERFORDERLICH---
         MVI   STEUER,X'FF'
         B     D00
         SPACE 2
F20      EQU   *                     ---DELIMITER KEIN KOMMA---
         CLI   0(R10),C' '
         BE    F30
         CLI   0(R10),X'00'
         BNE   L36
         SPACE
F30      EQU   *                     ---ENDE PARM-INFORMATIONEN---
         MVC   ABER,ABER-1
         PUT   SYSPRINT,ABER
         SPACE
         CLI   OLDDISP,C' '
         BE    L40
         CLI   NEWDISP,C' '
         BE    L40
         CLI   OLDTYPE,C'2'
         BNE   F40
         CLI   NEWTYPE,C'0'
         BNE   L44
         MVI   NEWTYPE,C'2'
         SPACE 3
F40      EQU   *                     ---TRENNUNG DSN-GDG---
         CLC   OLDTYPE,NEWTYPE
         BNE   L44
         SPACE 2
         CLI   OLDTYPE,C'3'
         BE    H00
         B     G00
         EJECT
*--------------REPLACE DATASET-NAME-----------------------------------*
         SPACE 2
G00      EQU   *
         CALL  REPLX,(SYSPRINT,OLDDSN,NEWDSN)
         LTR   R15,R15
         BZ    K00
         B     L140
         EJECT
*--------------REPLACE GENERATION-GROUP-------------------------------*
         SPACE 2
H00      EQU   *                     ---TEST ANZAHL DATASETS---
         LA    R10,OLDNAME
         BAL   R6,U00
         MVC   OLDSETS,GDGSETS
         CLC   OLDSETS,=H'0'
         BE    L48
         SPACE
         LA    R10,NEWNAME
         BAL   R6,U00
         MVC   NEWSETS,GDGSETS
         CLC   NEWSETS,=H'0'
         BE    H50
         CLI   NEWDISP,C'1'
         BNE   H50
         SPACE
H10      EQU   *                     ---UNCATLG NEWGDG---
         LH    R10,NEWSETS
         SPACE
H20      EQU   *
         MVC   LOCATNM,NEWNAME
         LA    R9,LOCATNM
         SPACE
H30      EQU   *
         CLI   0(R9),C' '
         BE    H40
         LA    R9,1(,R9)
         B     H30
         SPACE
H40      EQU   *                     ---SETZEN RELATIVE NR. (0)---
         MVC   0(3,R9),=C'(0)'
         LOCATE CAMLOC1
         SPACE
         LTR   R15,R15
         BNZ   L100
         CATALOG GDGUNCAT
         LTR   R15,R15
         BNZ   K40
         BAL   R6,V00
         BCT   R10,H20
         SPACE 2
H50      EQU   *                     ---AUFBEREITEN PARM-FIELDS---
         MVI   NEWDISP,C'0'
         MVI   NEWTYPE,C'2'
         MVI   OLDTYPE,C'2'
         LH    R10,OLDSETS
         MVC   OLDSAVE,OLDNAME
         MVC   NEWSAVE,NEWNAME
         SPACE
         LA    R9,OLDSAVE
         SPACE
H60      EQU   *                     ---SETZEN REL.NR (0)---
         CLI   0(R9),C' '
         BE    H70
         LA    R9,1(,R9)
         B     H60
         SPACE
H70      EQU   *
         MVC   0(6,R9),=C'(-000)'
         SPACE
H80      EQU   *                     ---UMKATALOGISIEREN DATASET---
         MVC   OLDNAME,OLDSAVE
         MVC   NEWNAME,NEWSAVE
         CALL  REPLX,(SYSPRINT,OLDDSN,NEWDSN)
         LTR   R15,R15
         BNZ   L140
         BCT   R10,H90
         B     K00
         SPACE 2
H90      EQU   *                     ---SETZEN REL.NR---
         CLI   OLDDISP,C'1'
         BE    H80
         PACK  DW,2(3,R9)
         AP    DW,=P'+1'
         UNPK  2(3,R9),DW+6(2)
         OI    4(R9),X'F0'
         B     H80
         EJECT
*--------------ENDE-VERARBEITUNG--------------------------------------*
         SPACE 2
K00      EQU   *                     ---NORMALES ENDE---
         MVC   ABER,ABER-1
         MVC   ABER(40),=C'0NORMAL END OF FUNKTION. RETURN-CODE 00.'
         PUT   SYSPRINT,ABER
         MVC   ABER,ABER-1
         PUT   SYSPRINT,ABER
         B     C00
         SPACE
K10      EQU   *                     ---EODAD-SYSIN ANSPRUNG---
         CLI   STEUER,X'00'
         BNE   L20
         XR    R15,R15
         SPACE
K20      EQU   *                     ---RETURN---
         LR    R10,R15
         CLOSE (SYSIN)
         CLOSE (SYSPRINT)
         LTR   R15,R10
         BZ    K30
         ST    R15,ABEND+4
         MVI   ABEND+4,X'80'
         B     K40
K30      EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
K40      EQU   *
         LR    R2,R15
ABEND    ABEND 100,DUMP
         EJECT
*--------------FEHLERBEHANDLUNG---------------------------------------*
         SPACE 2
L00      EQU   *
         XR    R15,R15
         B     L130
L04      EQU   *
         LA    R15,4
         B     L130
L08      EQU   *
         LA    R15,8
         B     L130
L12      EQU   *
         LA    R15,12
         B     L130
L16      EQU   *
         LA    R15,16
         B     L130
L20      EQU   *
         LA    R15,20
         B     L130
L24      EQU   *
         LA    R15,24
         B     L130
L28      EQU   *
         LA    R15,28
         B     L130
L32      EQU   *
         LA    R15,32
         B     L130
L36      EQU   *
         LA    R15,36
         B     L130
L40      EQU   *
         LA    R15,40
         B     L130
L44      EQU   *
         LA    R15,44
         B     L130
L48      EQU   *
         LA    R15,48
         B     L130
L52      EQU   *
         LA    R15,52
         B     L130
L100     EQU   *                     ---ERROR LOCATE NEWGDG---
         LR    R10,R15
         MVC   ABER,ABER-1
         MVC   ABER+1(22),=C'ERROR ON LOCATE NEWGDG'
         B     L120
         SPACE
L110     EQU   *                     ---ERROR LOCATE TTR---
         LR    R10,R15
         MVC   ABER,ABER-1
         MVC   ABER+1(19),=C'ERROR ON LOCATE TTR'
         B     L120
         SPACE
L120     EQU   *                     ---AUSGABE LOCATE-ERROR---
         PUT   SYSPRINT,ABER
         MVC   ABER,ABER-1
         LA    R9,LOCERR
         MH    R10,=H'10'
         AR    R9,R10
         MVC   ABER+1(39),1(R9)
         PUT   SYSPRINT,ABER
         XR    R15,R15
         IC    R15,0(R9)
         B     L140
         SPACE
L130     EQU   *                     ---AUSGABE FORMAL-ERROR---
         LA    R9,ERRTAB
         MH    R15,=H'10'
         AR    R9,R15
         MVC   ABER,ABER-1
         MVC   ABER+1(39),1(R9)
         PUT   SYSPRINT,ABER
         XR    R15,R15
         IC    R15,0(R9)
         B     L140
         SPACE
L140     EQU   *                     ---ERROR - ENDE MELDUNG---
         LR    R10,R15
         MVC   ABER,ABER-1
         MVC   ABER(37),=C'0ABNORMAL END OF FUNKTION. RET.CODE ='
         CVD   R10,DW
         MVC   ABER+37(4),=X'40212020'
         ED    ABER+37(4),DW+6
         PUT   SYSPRINT,ABER
         LR    R15,R10
         B     K20
         EJECT
*--------------UNTERROUTINE SUCHEN ANZAHL DATASETS VON GDG------------*
         SPACE 2
U00      EQU   *                     ---LOCATE 1. INDEXBLOCK---
         MVC   LOCATNM,0(R10)
         LOCATE CAMLOC1
         CH    R15,=H'12'
         BNE   L110
         XR    R9,R9
U05      EQU   *
         LA    R8,WORKAREA+2
         SPACE
U10      EQU   *                     ---ZAEHLEN DATA-SETS---
         CLI   0(R8),X'00'
         BE    U20
         CLI   0(R8),X'FF'
         BE    U30
         CLI   11(R8),X'07'
         BL    K40
         CLI   0(R8),C'G'
         BNE   U50
         CLI   5(R8),C'V'
         BNE   U50
         LA    R9,1(,R9)
         SPACE
U20      EQU   *                     ---NAECHSTER DATASET---
         XR    R7,R7
         IC    R7,11(,R8)
         MH    R7,=H'2'
         LA    R8,12(,R8)
         AR    R8,R7
         B     U10
         SPACE
U30      EQU   *                     ---LOCATE TTR---
         CLC   8(3,R8),=XL3'00'
         BE    U40
         MVC   LOCATNM(3),8(R8)
         LOCATE CAMLOC2
         LTR   R15,R15
         BNZ   L110
         LA    R8,WORKAREA+2
         B     U10
         SPACE
U40      EQU   *                     ---RETURN---
         STH   R9,GDGSETS
         BR    R6
         SPACE
U50      EQU   *                     ---KEIN GDG---
         XR    R9,R9
         B     U40
         EJECT
*--------------DISPOSITIONS-MELDUNG UNCATLG NEWGDG--------------------*
         SPACE 2
V00      EQU   *
         MVC   WTOLIST1+14(44),LOCATNM
         MVC   WTOLIST1+59(11),=C'UNCATALOGED'
         WTO   MF=(E,WTOLIST1)
         LH    R9,WORKAREA
         LA    R5,WORKAREA+2
         SPACE
V10      EQU   *
         MVC   WTOLIST2+27(40),WTOLIST2+26
         LA    R8,5
         LA    R7,WTOLIST2+27
         SPACE
V20      EQU   *
         MVC   0(6,R7),4(R5)
         MVI   6(R7),C','
         LA    R7,7(,R7)
         LA    R5,12(,R5)
         BCT   R9,V30
         BCTR  R7,0
         MVI   0(R7),C'.'
         WTO   MF=(E,WTOLIST2)
         BR    R6
         SPACE
V30      EQU   *
         BCT   R8,V20
         WTO   MF=(E,WTOLIST2)
         B     V10
         EJECT
***********************************************************************
**                                                                   **
**             DEFINITIONEN                                          **
**             ============                                          **
**                                                                   **
***********************************************************************
         SPACE 3
*--------------REGISTERVERWENDUNG-------------------------------------*
         SPACE 2
R0       EQU   0
R1       EQU   1                        A(PUFFER) SYSIN
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9                        ARB.-REG.
R10      EQU   10                       ARB.-REG.
R11      EQU   11
R12      EQU   12                       BASISREGISTER INSTRUKTIONEN
R13      EQU   13                       A(SAVEAREA)
R14      EQU   14                       LINK-REG.
R15      EQU   15                       VERZW.REG., RETURN-CODE
         SPACE 3
*--------------ARBEITSBEREICHE----------------------------------------*
         SPACE 2
SAVE     DC    18F'0'                   SAVEAREA
         SPACE
         DC    C' '
ABER     DS    CL133                    AUSGABE-BEREICH
         SPACE
TIME     DS    F
DATE     DS    F
         SPACE
STEUER   DS    X                        CTL  00 = ANF.KARTE
*                                            FF = FOLGEKARTE
         DS    0H
MVC      MVC   4(0,R7),0(R8)            MVC  DSNAME
         SPACE
OLDSETS  DS    H
NEWSETS  DS    H
GDGSETS  DS    H
         SPACE
DW       DS    D
         SPACE
GDGUNCAT CAMLST UNCAT,LOCATNM
CAMLOC1  CAMLST NAME,LOCATNM,,WORKAREA
CAMLOC2  CAMLST BLOCK,LOCATNM,WORKAREA+259,WORKAREA
         SPACE
LOCATNM  DC    CL44' '
         DS    0D
WORKAREA DC    265C' '
OLDSAVE  DC    CL44' '
NEWSAVE  DC    CL44' '
         SPACE
WTOLIST1 WTO   'IEF285I                                                *
                                    ',ROUTCDE=11,MF=L
WTOLIST2 WTO   'IEF285I                                                *
                                    ',ROUTCDE=11,MF=L
         SPACE 3
TRTBLANK DS    0D
         DC    X'10'
         DC    63X'0C'
         DC    X'00'
         DC    128X'0C'
         DC    9X'04'
         DC    7X'0C'
         DC    9X'04'
         DC    8X'0C'
         DC    8X'04'
         DC    6X'0C'
         DC    10X'08'
         DC    6X'0C'
         SPACE 3
TRTSIGN  DS    0D
         DC    X'18'
         DC    63X'14'
         DC    X'04'
         DC    10X'14'
         DC    X'08'
         DC    X'14'
         DC    X'0C'
         DC    15X'14'
         DC    X'0C'
         DC    13X'14'
         DC    X'10'
         DC    85X'14'
         DC    9X'00'
         DC    7X'14'
         DC    9X'00'
         DC    8X'14'
         DC    8X'00'
         DC    6X'14'
         DC    10X'00'
         DC    6X'14'
         SPACE 3
LOCERR   DS    0D
         DC    HL1'8'
         DC    CL39'SYSCTLG-VOLUME OR -DSET NOT FOUND'
         DC    HL1'8'
         DC    CL39'   INDEX-NAME NOT FOUND'
         DC    HL1'8'
         DC    CL39'DATASET-NAME REQUIRED, INDEX-NAME FOUND'
         DC    HL1'8'
         DC    CL39'   INDEX-NAME CONTAINS DATASET-NAME'
         DC    HL1'4'
         DC    CL39'SYNTAX-ERROR OR LENTH-ERROR ( 9 CHAR)'
         DC    HL1'12'
         DC    CL39'PERMANENT I/O-ERROR IN CATALOG'
         DC    HL1'12'
         DC    CL39'INVALID TTR-POINTER - LOCATE BLOCK'
         DC    HL1'12'
         DC    CL39'INVALID WORK-AREA POINTER'
         SPACE 3
ERRTAB   DS    0D
         DC    HL1'4'
         DC    CL39'INVALID NAME-FIELD'
         DC    HL1'4'
         DC    CL39'INVALID OR MISSING OPERATION-FIELD'
         DC    HL1'4'
         DC    CL39'INVALID OPERATION-OPERAND FIELD'
         DC    HL1'4'
         DC    CL39'INVALID KEYWORD IN OPERAND-FIELD'
         DC    HL1'4'
         DC    CL39'DUPLICATE KEYWORD IN OPERAND-FIELD'
         DC    HL1'4'
         DC    CL39'OPERAND-FIELD MISSING'
         DC    HL1'4'
         DC    CL39'INVALID CHARACTER IN REL. GDG-NUMBER'
         DC    HL1'4'
         DC    CL39'DSET- OR GDG-NAME LENGTH-ERROR'
         DC    HL1'4'
         DC    CL39'DSET- OR GDG-NAME INVALID DELIMITER'
         DC    HL1'4'
         DC    CL39'INVALID END-DELIMITTER OF OPERAND-FIELD'
         DC    HL1'4'
         DC    CL39'REQUIRED OPERAND IS MISSING'
         DC    HL1'4'
         DC    CL39'OLDNAME AND NEWNAME INCONSISTENT'
         DC    HL1'4'
         DC    CL39'GDG HAS NO DATASET-POINTERS'
         DC    HL1'0'
         DC    CL39'UNUSED'
         SPACE 3
*--------------UEBERGABEBEREICHE UND DCB'S----------------------------*
         SPACE 2
OLDDSN   DS    0F
OLDDISP  DS    C
OLDTYPE  DS    C
OLDINDX  DS    H
OLDNAME  DS    CL44
         SPACE 2
NEWDSN   DS    0F
NEWDISP  DS    C
NEWTYPE  DS    C
NEWINDX  DS    H
NEWNAME  DS    CL44
         SPACE 2
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,  *
               BLKSIZE=133
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL,EODAD=K10
         SPACE 3
         END
         TITLE 'REPLX    WMF-UTILITY    G.HELLRIEGEL'
*--------------FUNKTION VON REPLX-------------------------------------*
*                                                                     *
*                                                                     *
**             DA IM HDR1 VON SL-BAND-DATEIEN NUR DIE LETZTEN        **
**             17 STELLEN DES DS-NAMENS ABGESPEICHERT WERDEN         **
**             (FELD FL1ID), KOENNEN DIESE DATEIEN DURCH MEHRERE     **
**             DS-NAMEN IDENTIFIZIERT WERDEN.                        **
**             EINZIGE ERFORDERNIS DIESER DS-NAMEN IST, DASS SIE     **
**             IN DEX LETZTEN 17 STELLEN MIT DEM INHALT DES HDR1     **
**             UEBEREINSTIMMEN.                                      **
*                                                                     *
*              BEISPIEL.                                              *
*                                                                     *
*              EINE BAND-DATEI WURDE AUFGEBAUT MIT                    *
*                        DSN=R.FR251000.G0012V00       .              *
*                                                                     *
*              INFORMATION IM FELD FL1ID IM HDR1                      *
*                              FR251000.G0012V00       .              *
*                                                                     *
*              DIE DATEI KANN NUN AUFGERUFEN WERDEN MIT               *
*                        DSN=R.FR251000.G0012V00                      *
*                ODER    DSN=A.FR251000.G0012V00                      *
*                ODER    DSN=BETA.FR251000.G0012V00                   *
*                ODER    DSN=FR251000.G0012V00         .              *
*                                                                     *
*                                                                     *
**             DIESE MOEGLICHKEIT UNTERSTUETZT NUN REPLX,            **
**             INDEM ES IM SYSTEM-KATALOG BESTEHENDE                 **
**             DATEI-VOLUME-INFORMATIONEN MIT NEUEN DS-NAMEN         **
**             VERKNUEPFT UND DIESE NEUEN                            **
**             DATEI-VOLUME-INFORMATIONEN KATALOGISIERT.             **
*                                                                     *
*              BEISPIEL.                                              *
*                                                                     *
*              EINE DATEI IST KATALOGISIERT MIT FOLGENDEN ANGABEN     *
*                        DSN=R.FR251000.G0012V00,                     *
*                        UNIT=TAPE=(B00012,B00014)                    *
*                                                                     *
*              DURCH REPLX KOENNTE EIN WEITERER KATALOGEINTRAG        *
*              MIT FOLGENDEM INHALT GEMACHT WERDEN                    *
*                        DSN=A.FR251000.G0012V00,                     *
*                        UNIT=TAPE=(B00012,B00014)                    *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*--------------AUFBAU VON REPLX---------------------------------------*
*                                                                     *
*              REPLX KATALOGISIERT IN EINEM DURCHLAUF                 *
*                    EINEN DSNAMEN UM.                                *
*                                                                     *
*              1.   LOCATE  OLD-DSN    -     DATEI-INFORMATIONEN      *
*              2.   LOCATE  NEW-DSN    -     INDEX-INFORMATIONEN      *
*              3.   UNCATLG NEW-DSN    -     FALLS VERLANGT UND ERFORD*
*              4.   CATLG   NEW-DSN    -     UMKATALOGISIERUNG        *
*              5.   UNCATLG OLD-DSN    -     FALLS VERLANGT           *
*                                                                     *
*                                                                     *
*              NACH ALLEN KATALOG-AENDERUNGEN SCHREIBT REPLX          *
*              EINE DISPOSITIONS-MELDUNG (PUNKTE 3-5, WTO ROUTCDE=11) *
*                                                                     *
*              BEI FEHLERN  SCHREIBT REPLX EINE FEHLERMELDUNG         *
*              AUF SYSPRINT UND BEENDET DIE VERARBEITUNG, DER         *
*              KATALOG WURDE NICHT VERAENDERT.                        *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*--------------CALL- UND RETURN-KONVENTIONEN--------------------------*
*                                                                     *
*        CALL  REPLX,(PRINT,OLDDSN,NEWDSN)                            *
*                                                                     *
*              PRINT     ADR. DCB FUER SYSPRINT (LRECL=133,ASA)       *
*                                                                     *
*              OLDDSN    ADR. BEREICH (0F,CL50) MIT OLD-DSN-INFORM.   *
*                                                                     *
*                        OLDDISP   BYTE 0    DISPOSITION VON OLDDSN   *
*                                            C'0' = KEEP              *
*                                            C'1' = UNCATLG           *
*                        OLDTYPE   BYTE 1    ART DES OLDNAME          *
*                                            C'0' NORMAL DS           *
*                                            C'1' GEN.-DS (ABSOLUT)   *
*                                            C'2' GEN.-DS (RELATIV)
*                        OLDINDX   BYTE 2-3  ANZAHL INDEX-STUFEN      *
*                                            H    =(PUNKTE + 1)       *
*                        OLDNAME   BYTE 4-47 OLD DATASET-NAME         *
*                                                                     *
*---------------------------------------------------------------------*
*              NEWDSN    ADR. BEREICH (0F,CL50) MIT NEW-DSN-INFORM.   *
*                                                                     *
*                        NEWDISP   BYTE 0    DISPOSITION VON NEWDSN   *
*                                            C'0' NOT CATALOGED       *
*                                            C'1' UNCATLG (VOR CATLG) *
*                        NEWTYPE   BYTE 1    ART DES NEWNAME          *
*                                            C'0' NORMAL DS           *
*                                            C'1' GEN.-DS (ABSOLUT)   *
*                                            C'2'  GEN.-GROUP-NAME    *
*                        NEWINDX   BYTE 2-3  ANZAHL INDEX-STUFEN      *
*                                            H    =(PUNKTE+1)
*                        NEWNAME   BYTE 4-47 NEW DATASET-NAME ODER    *
*                                            NEW GEN.-GROUP-NAME      *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        RETURN (14,12),RC=(15)                                       *
*                                                                     *
*              RETURN-CODES                                           *
*                                                                     *
*              RC=0      FUNKTION OKAY, OLDNAME UND NEWNAME ENTHALTEN *
*                                       DEN TATSAECHLICHEN DSNAMEN    *
*                                                                     *
*              RC=4      FORMALFEHLER, UNERLAUBTE ANGABEN IN DEN      *
*                                       GLIEDERN OLDDSN UND NEWDSN    *
*                                                                     *
*              RC=8      LOCATE-FEHLER, KATALOG ENTHIELT NICHT DIE    *
*                                       ERWARTETEN INFORMATIONEN      *
*                                                                     *
*              RC=12     I/O-ERROR KATALOG                            *
*                                                                     *
*                                                                     *
*              BEI UNERWARTETEN ODER GEFAEHRLICHEN FEHLERN ERZEUGT    *
*              REPLX EINEN    ABEND    USER=100  MIT DUMP.            *
*              DER RETURN-CODE DER CATALOG-FUNKTION IST DABEI         *
*                   IN REGISTER R2 ZU FINDEN.                         *
*---------------------------------------------------------------------*
         EJECT
*--------------HOUSEKEEPING-------------------------------------------*
         SPACE 2
REPLX    CSECT
         SPACE
A00      EQU   *                     ---INITIIERUNG---
         SAVE  (14,12),,*
         LR    R12,R15
         USING A00,R12,R11              BASISREGISTER INSTRUKTIONEN
         LA    R11,4000(,R12)
         LA    R11,96(,R11)
         ST    R13,SAVE+4
         LR    R14,R13
         LA    R13,SAVE
         USING SAVE,R13                 BASISREGISTER DEFINITIONEN
         ST    R13,8(,R14)
         SPACE
A10      EQU   *                     ---PARM-LISTE---
         LM    R3,R5,0(R1)              LADEN PARM-LISTE
         USING OLDDSN,R4                DSECT OLDDSN
         USING NEWDSN,R5                DSECT NEWDSN
         EJECT
*--------------LOCATE OLD DSNAME--------------------------------------*
         SPACE 2
B00      EQU   *                     ---LOCATE OLDNAME---
         LA    R10,OLDNAME
         ST    R10,CAMNAME+4            SETZEN NAME-POINTER
         MVC   CAMNAME+12(4),=A(WAOLD)  SETZEN WORKAREA-POINTER
         SPACE
         LOCATE CAMNAME
         SPACE
B10      EQU   *                     ---VERZWEIGUNG MIT RC VON LOCATE--
         B     *+4(R15)
         B     C00                      RC= 0     LOCATE OKAY
         B     K00                      RC= 4     CVOL,SYSCTLG
         B     K10                      RC= 8     NAME NOT FOUND
         B     L00                      RC=12     INDEX
         B     K20                      RC=16     DATA-SET HIGH LEVEL
         B     L00                      RC=20     SYNTAX-ERROR
         B     L00                      RC=24     PERMANENT I/O-ERROR
         B     K120                     RC=28     -----TTR-----------
         B     K120                     RC=32     INVALID WAOLD
         EJECT
*--------------LOCATE NEW DSNAME--------------------------------------*
         SPACE 2
C00      EQU   *                     ---TRENNUNG DSN - GDG---
         CLI   NEWTYPE,C'2'             NEWNAME GEN.-GROUP
         BNE   C40                      NEWNAME DS-NAME
         SPACE
C10      EQU   *                     ---POSITIONIERUNG NEWNAME---
         XR    R1,R1                    R1 FUER TRT
         MVC   TRTTAB,TRTTAB-1
         MVI   TRTTAB+X'40',X'04'       BLANK INTERRUPT
         TRT   NEWNAME,TRTTAB
         BZ    K60                      NEWNAME ZU LANG
         LA    R10,NEWNAME
         SR    R1,R10                   R1 - R10  = L'NEWNAME
         BNP   K60                      NEWNAME BLANK
         CH    R1,=H'35'
         BH    K60                      NEWNAME ZU LANG
         AR    R10,R1                   R10 + R1  = 1.BLANKSTELLE
         SPACE
C20      EQU   *                     ---POSITIONIERUNG OLDNAME---
         LA    R1,OLDNAME+L'OLDNAME     OLDNAME = 44 STELLEN
         TRT   OLDNAME,TRTTAB
         SH    R1,=H'8'                 SETZEN LETZTER INDEX
         LR    R9,R1
         CLI   0(R9),C'G'               TEST GEN.-DATASET
         BNE   K30
         CLI   5(R9),C'V'
         BNE   K30
         SPACE
C30      EQU   *                     ---MVC DATA-SET-NAME---
         MVI   0(R10),C'.'
         MVC   1(8,R10),0(R9)
         SPACE 2
C40      EQU   *                     ---LOCATE NEWNAME---
         LA    R10,NEWNAME
         ST    R10,CAMNAME+4            SETZEN NAME-POINTER
         MVC   CAMNAME+12(4),=A(WANEW)  SETZEN WORKAREA-POINTER
         SPACE
         LOCATE CAMNAME
         SPACE
C50      EQU   *                     ---VERZWEIGUNG MIT RC VON LOCATE--
         B     *+4(R15)
         B     D00                      RC= 0     LOCATE OKAY
         B     K70                      RC= 4     CVOL,SYSCTLG
         B     D10                      RC= 8     NAME NOT FOUND
         B     L10                      RC=12     INDEX
         B     K90                      RC=16     DATA-SET HIGH LEVEL
         B     L10                      RC=20     SYNTAX-ERROR
         B     L10                      RC=24     PARMENENT I/O-ERROR
         B     K120                     RC=28     -----TTR-----------
         B     K120                     RC=32     INVALID WANEW
         EJECT
*--------------TEST OLD AND NEW---------------------------------------*
         SPACE 2
D00      EQU   *                     ---NEW DATASET FOUND---
         CLI   NEWDISP,C'1'             UNCATALG NEW DATASET
         BNE   L10
         MVI   DELNEW,C'1'              DELETE NEW DATASET-NAME
         B     D20
         SPACE
D10      EQU   *                     ---NEW DATASET NOT FOUND---
         LH    R10,NEWINDEX             ALLE UEBRIGEN INDICES GEFUNDEN
         CLI   NEWTYPE,C'2'             GDG - KEINE VERMINDERUNG
         BE    *+6
         BCTR  R10,0                    INDICES-1 = ERFORD. INDICES
         LR    R9,R0                    R0 = GEFUNDENE INDICES
         CR    R9,R10                   ALLE GEFUNDEN
         BL    K100
         MVI   DELNEW,C'0'              KEIN DELETE
         B     D20
         SPACE
D20      EQU   *                     ---VERGLEICH 17 STELLEN OLD-NEW---
         MVC   TRTTAB,TRTTAB-1
         MVI   TRTTAB+X'40',X'04'
         LA    R1,OLDNAME+L'OLDNAME
         TRT   OLDNAME,TRTTAB           SUCHEN BLANK-STELLE OLDNAME
         SH    R1,=H'17'
         LR    R10,R1
         LA    R1,NEWNAME+L'NEWNAME
         TRT   NEWNAME,TRTTAB           SUCHEN BLANK-STELLE NEWNAME
         SH    R1,=H'17'
         LR    R9,R1
         LA    R1,OLDNAME               OLDNAME GE 17 STELLEN
         CR    R1,R10
         LA    R15,20
         BH    L00
         LA    R1,NEWNAME               NEWNAME GE 17 STELLEN
         CR    R1,R9
         BH    K60
         SPACE
D30      EQU   *                     ---VERGLEICH LETZTE 17 STELLEN---
         CLC   0(17,R10),0(R9)
         BNE   K110
         SPACE
D40      EQU   *                     ---TEST VOLUME-LIST-BLOCK---
         LH    R10,WAOLD
         CH    R10,=H'20'               MAX. 20 ENTRIES ERLAUBT
         BH    K40
         LA    R9,WAOLD+2
         SPACE
D50      EQU   *                     ---TEST UNIT=TAPE---
         TM    2(R9),X'80'              TAPE-CODE = X'80'
         BNO   K50
         LA    R9,12(,R9)
         BCT   R10,D50                  R10 = ANZAHL ENTRIES
         B     E00
         EJECT
*--------------UNCATLG NEWNAME----------------------------------------*
         SPACE 2
E00      EQU   *                     ---UNCATLG NEWNAME---
         CLI   DELNEW,C'1'              UNCATLG ERFORDERLICH
         BNE   F00
         LA    R10,NEWNAME              SETZEN NEWNAME
         ST    R10,CAMUNCAT+4
         SPACE
         CATALOG CAMUNCAT
         SPACE
         LTR   R15,R15                  FEHLER BEI UNCATLG
         BNZ   K120
         SPACE
E10      EQU   *                     ---DISPOSITION-MESSAGE---
         MVC   WTODISP1+14(44),NEWNAME
         MVC   WTODISP1+59(11),=C'UNCATALOGED'
         WTO   MF=(E,WTODISP1)
         SPACE
E20      EQU   *                     ---VOLUME-MESSAGE---
         LA    R1,WANEW
         BAL   R6,U00
         B     F00
         EJECT
*--------------CATALOG NEWNAME----------------------------------------*
         SPACE 2
F00      EQU   *                     ---CATLG NEWNAME---
         LA    R10,NEWNAME              SETZEN NEWNAME
         ST    R10,CAMCAT+4
         MVC   CAMCAT+12(4),=A(WAOLD)        SETZEN WORKAREA (OLD)
         SPACE
         CATALOG CAMCAT
         SPACE
         LTR   R15,R15                  FEHLER BEI CATLG
         BNZ   K120
         SPACE
F10      EQU   *                     ---DISPOSITION-MESSAGE---
         MVC   WTODISP1+14(44),NEWNAME
         MVC   WTODISP1+59(11),=C'CATALOGED  '
         WTO   MF=(E,WTODISP1)
         SPACE
F20      EQU   *                     ---VOLUME-MESSAGE---
         LA    R1,WAOLD
         BAL   R6,U00
         B     G00
         EJECT
*--------------UNCATLG OLDNAME----------------------------------------*
         SPACE 2
G00      EQU   *                     ---UNCATLG OLDNAME---
         CLI   OLDDISP,C'1'             UNCATLG ERFORDERLICH
         BNE   G20
         LA    R10,OLDNAME              SETZEN OLDNAME
         ST    R10,CAMUNCAT+4
         SPACE
         CATALOG CAMUNCAT
         SPACE
         LTR   R15,R15                  FEHLER BEI UNCATLG
         BNZ   K120
         SPACE
G10      EQU   *                     ---DISPOSITION UNCATLG---
         MVC   WTODISP1+59(11),=C'UNCATALOGED'
         B     G30
         SPACE
G20      EQU   *                     ---DISPOSITION KEEP---
         MVC   WTODISP1+59(11),=CL11'KEPT'
         SPACE
G30      EQU   *                     ---DISPOSITION-MESSAGE---
         MVC   WTODISP1+14(44),OLDNAME
         WTO   MF=(E,WTODISP1)
         SPACE
G40      EQU   *                     ---VOLUME-MESSAGE---
         LA    R1,WAOLD
         BAL   R6,U00
         B     H00
         EJECT
*--------------RETURN TO CALLER---------------------------------------*
         SPACE 2
H00      EQU   *                     ---RC = 0---
         XR    R15,R15
         SPACE
H10      EQU   *                     ---NORMAL RETURN---
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
         SPACE 3
H20      EQU   *                     ---ABEND 100---
         ABEND 100,DUMP
         EJECT
*--------------ERROR SONDERFAELLE-------------------------------------*
         SPACE 2
K00      EQU   *                     ---LOCATE OLD - RC = 4---
         MVC   RC04+1(6),WAOLD+259
         B     L00
         SPACE
K10      EQU   *                     ---LOCATE OLD - RC = 8---
         LR    R10,R0
         LA    R10,1(,R10)
         STC   R10,RC08+1
         OI    RC08+1,X'F0'
         B     L00
         SPACE
K20      EQU   *                     ---LOCATE OLD - RC = 16---
         LR    R10,R0
         STC   R10,RC16+1
         OI    RC16+1,X'F0'
         B     L00
         SPACE
K30      EQU   *                     ---ERROR OLDNAME - NO GDG---
         LA    R15,36
         B     L00
         SPACE
K40      EQU   *                     ---ERROR OLDNAME - MAX. 20ENTRIES
         LA    R15,40
         B     L00
         SPACE
K50      EQU   *                     ---ERROR OLDNAME - NO TAPE-VOL.---
         LA    R15,44
         B     L00
         SPACE 3
K60      EQU   *                     ---SYNTAX-ERROR NEWNAME---
         LA    R15,20
         B     L10
         SPACE
K70      EQU   *                     ---LOCATE NEW - RC = 4---
         MVC   RC04(6),WANEW+259
         B     L10
         SPACE
K80      EQU   *                     ---LOCATE NEW - RC = 8---
         LR    R10,R0
         LA    R10,1(,R10)
         STC   R10,RC08+1
         OI    RC08+1,X'F0'
         B     L10
         SPACE
K90      EQU   *                     ---LOCATE NEW - RC = 16---
         LR    R10,R0
         STC   R10,RC16+1
         OI    RC16+1,X'F0'
         B     L10
K100     EQU   *                     ---LOCATE NEW - INDEX NOT FOUND---
         LA    R15,52
         B     L10
         SPACE
K110     EQU   *                     ---NEWNAME UNGLEICH OLDNAME---
         LA    R15,56
         B     L10
         SPACE 3
K120     EQU   *                     ---CATLG-,UNCAT-ERROR---
         LR    R2,R15
         B     H20
         EJECT
*--------------ERROR HANDLING-----------------------------------------*
         SPACE 2
L00      EQU   *                     ---ERROR OLDNAME---
         MVC   ABER,ABER-1
         MVC   ABER+1(25),=CL25'ERROR ON LOCATE OLDNAME'
         B     L20
         SPACE
L10      EQU   *                     ---ERROR NEWNAME---
         MVC   ABER,ABER-1
         MVC   ABER+1(25),=CL25'ERROR ON LOCATE NEWNAME'
         B     L20
         SPACE
L20      EQU   *                     ---AUSGABE FEHLERTEXTE---
         MH    R15,=H'10'
         LA    R10,ERRTAB(R15)
         MVC   ABER+30(39),1(R10)
         PUT   0(,R3),ABER
         MVC   ABER,ABER-1
         MVC   ABER+1(10),=CL10'OLDNAME'
         MVC   ABER+15(44),OLDNAME
         PUT   0(,R3),ABER
         MVC   ABER+1(10),=CL10'NEWNAME'
         MVC   ABER+15(44),NEWNAME
         PUT   0(,R3),ABER
         XR    R15,R15
         IC    R15,0(,R10)
         B     H10
         EJECT
*--------------UNTERROUTINE ZUM DRUCKEN VOL-LIST-BLOCK----------------*
         SPACE 2
U00      EQU   *                     ---GRUNDSTELLUNG WTO---
         LA    R10,2(,R1)
         LH    R8,0(,R1)
         SPACE
U10      EQU   *                     ---LOESCHEN MESSAGE-BLOCK---
         LA    R7,5
         MVC   WTODISP2+27(40),WTODISP2+26
         LA    R9,WTODISP2+27
         SPACE
U20      EQU   *                     ---WTO-LAST---
         MVC   0(6,R9),4(R10)
         LA    R10,12(,R10)
         BCT   R8,U30
         MVI   6(R9),C'.'
         WTO   MF=(E,WTODISP2)
         BR    R6
         SPACE
U30      EQU   *                     ---WTO MESSAGE-BLOCK---
         MVI   6(R9),C','
         LA    R9,7(,R9)
         BCT   R7,U20
         WTO   MF=(E,WTODISP2)
         B     U10
         EJECT
***********************************************************************
**                                                                   **
**             DEFINITIONEN                                          **
**             ============                                          **
**                                                                   **
***********************************************************************
         SPACE 3
*--------------REGISTERVERWENDUNG-------------------------------------*
         SPACE 2
R0       EQU   0
R1       EQU   1                        PARM-REGISTER,ARBEITS-REG.
R2       EQU   2                        TRT-REG.
R3       EQU   3                        A(SYSPRINT)
R4       EQU   4                        A(OLDDSN) -DSECT
R5       EQU   5                        A(NEWDSN) -DSECT
R6       EQU   6                        LINK-REGISTER U00
R7       EQU   7                        ARBEITS-REG.
R8       EQU   8                        ARBEITS-REG.
R9       EQU   9                        ARBEITS-REG.
R10      EQU   10                       ARBEITS-REG.
R11      EQU   11                       BASISREGISTER2 INSTRUKTIONEN
R12      EQU   12                       BASISREGISTER1 INSTRUKTIONEN
R13      EQU   13                       A(SAVE BASISREG. DEFINITIONEN
R14      EQU   14                       LINK-REGISTER
R15      EQU   15                       RETURN-CODE
         SPACE 3
*--------------ARBEITSBEREICHE----------------------------------------*
         SPACE 2
SAVE     DC    18F'0'                   SAVEAREA
         SPACE 3
CAMNAME  CAMLST NAME,0,,0               CAMLST FUER LOCATE VY NAME
         DS    0D
WAOLD    DS    0CL265                   WORK-AREA OLD DSNAME
         DC    265C' '
         DS    0D
WANEW    DS    0CL265                   WORK-AREA NEW DSNAME
         DC    265C' '
         SPACE
         DC    X'00'                    LOESCHBYTE
TRTTAB   DC    256X'00'
         SPACE
DELNEW   DC    C' '                     STATUS NEW DATASET
         SPACE 3
CAMUNCAT CAMLST UNCAT,0                 CAMLST FUER UNCAT
CAMCAT   CAMLST CAT,0,,0                CAMLST FUER CAT
         SPACE 3
WTODISP1 WTO   'IEF285I                                                *
                                     ',ROUTCDE=11,MF=L
WTODISP2 WTO   'IEF285I                                                *
                                     ',ROUTCDE=11,MF=L
         SPACE 3
         DS    0D
ERRTAB   DS    0C
RC00     DC    HL1'8'
         DC    CL39'NEWNAME JUST CATALOGED, SPECIFY UNCATLG'
RC04     DC    HL1'8'
         DC    CL39'       VOLUME OR-SYSCTLG NOT FOUND'
RC08     DC    HL1'8'
         DC    CL39' . NAME REQUIRED - BUT NOT FOUND'
RC12     DC    HL1'8'
         DC    CL39'DATASET-NAME REQUIRED, INDEX-NAME-FOUND'
RC16     DC    HL1'8'
         DC    CL39' . INDEX-NAME CONTAINS DATASET-NAME'
RC20     DC    HL1'4'
         DC    CL39'SYNTAX-ERROR OR LENGTH-ERROR (17 CHAR)'
RC24     DC    HL1'12'
         DC    CL39'PERMANENT I/O-ERROR IN CATALOG'
RC28     DC    HL1'0'
         DC    CL39' '             NOT USED
RC32     DC    HL1'0'
         DC    CL39' '             NOT USED
RC36     DC    HL1'8'
         DC    CL39'GDG REQUIRED, BUT NOT FOUND'
RC40     DC    HL1'8'
         DC    CL39'MORE THAN 20 ENTRIES IN THIS NAME'
RC44     DC    HL1'8'
         DC    CL39'AN ENTRY POINTS TO A NO-TAPE-VOLUME'
RC48     DC    HL1'0'
         DC    CL39' '                  NOT USED
RC52     DC    HL1'8'
         DC    CL39'INDEX-NAME NOT FOUND'
RC56     DC    HL1'8'
         DC    CL39'LAST 17 CHAR. OF BOTH NAMES INLIKE'
         SPACE 3
         DC    C' '
ABER     DC    CL133' '
         LTORG
         EJECT
*--------------DSECTS-------------------------------------------------*
         SPACE 2
OLDDSN   DSECT                  ---DSECT FUER CTL-FIELD OLDDSN---
         SPACE
OLDDISP  DS    C
OLDTYPE  DS    C
OLDINDEX DS    H
OLDNAME  DS    CL44
         SPACE 3
NEWDSN   DSECT                  ---DSECT FUER CTL-FIELD NEWDSN---
         SPACE
NEWDISP  DS    C
NEWTYPE  DS    C
NEWINDEX DS    H
NEWNAME  DS    CL44
         SPACE 3
         END
* SA991480  REPLACE CATALOG INDEX
*        LINKAGE EDITOR SYSIN    : INCLUDE SYSLIB(SA990320)
*                                  INCLUDE SYSLIB(SA990330)
*                                  INCLUDE SYSLIB(SA990640)
*                                  CHANGE REPLX(SA990320)
*                                  NAME SA990640(R)
