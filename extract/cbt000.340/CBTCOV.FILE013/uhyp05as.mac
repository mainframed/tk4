KEY      TITLE 'SCHLUESSELWORTPARAMETER  UHYP05AS = MODKEY'
         MACRO
         MCEQU
         GBLB  &MCEQU
         AIF   (&MCEQU).NOTE1
&MCEQU   SETB  1
*------- LEVEL = 03 -------$
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEXIT
.NOTE1   MACMES 1,MCEQU
         MEND
         MACRO
         MACMES &CODE,&MAKRO
.***********************************************************
.*                                                         *
.*       MAKRO ZUR GENERIERUNG VON MNOTE-BEMERKUNGEN       *
.*             STANDARDTEXT IST   *** &MAKRO               *
.*                                                         *
.* CODE  TEXT                                              *
.* ****  ****                                              *
.*                                                         *
.*   1   MAKRO BEREITS EINGESETZT                          *
.*   2   PARAMETER FEHLT                                   *
.*   3   FEHLER IN PARAMETERANGABE                         *
.*                                                         *
.***********************************************************
         AIF   ('&CODE' EQ '' OR '&MAKRO' EQ '').NOTE1
         AIF   ('&CODE' EQ '0' OR '&CODE' GT '3').NOTE2
         AIF   ('&CODE' EQ '1').M1
         AIF   ('&CODE' EQ '2').M2
         AIF   ('&CODE' EQ '3').M3
.M1      MNOTE '*** &MAKRO MAKRO BEREITS EINGESETZT'
         MEXIT
.M2      MNOTE '*** &MAKRO PARAMETER FEHLT'
         MEXIT
.M3      MNOTE '*** &MAKRO FEHLER IN PARAMETERANGABE'
         MEXIT
.NOTE1   MACMES 2,MACMES
         MEXIT
.NOTE2   MACMES 3,MACMES
         MEND
         EJECT
         SPACE 5
         PRINT NOGEN
*------- VERSION B VOM 05.05.76 -------* (HUBER)
***********************************************************************
*                                                                     *
*        DIESES MODUL UNTERSUCHT EINGABEDATEN (ANFANGS- UND           *
*        ENDEADRESSE IN PARAMETERLISTE) AUF BESTIMMTE SCHLUESSEL.     *
*        DIE ADRESSE DES WERTANFANGS GEFUNDENER SCHLUESSEL WIRD       *
*        IN DER PARAMETERLISTE GESPEICHERT.                           *
*        REGISTER 1 ENTHAELT DIE ADRESSE DER PARAMETERLISTE.          *
*                                                                     *
*                   *-------------------*                             *
*                  0*  ANFANGSADRESSE   *                             *
*                   *-------------------*                             *
*                  4*    ENDEADRESSE    *                             *
*                   *-------------------*                             *
*                  8*  SCHLUESSELWORT   *                             *
*                   *-------------------*                             *
*                 16*RES.×LNG.× ADRESSE *                             *
*                   *-------------------*                             *
*                 24*  SCHLUESSELWORT   *                             *
*                   *-------------------*                             *
*                   *         ×         *                             *
*                   *                   *                             *
*                   BLANK = ENDE                                      *
*                                                                     *
***********************************************************************
         SPACE 3
MODKEY   START 0
         SAVE  (14,12),,EINGANGMODKEY
A1       LR    R5,R1
         BALR  R9,0
         USING *,R9
         ST    R13,SAVEAREA+4
         LR    R11,R13
         LA    R13,SAVEAREA
         ST    R13,8(R11)
         LM    R3,R4,0(R5)
         LR    R8,R3
         LA    R5,8(R5)
         B     B1
A2       B     C1
A3       L     R13,SAVEAREA+4
         RETURN (14,12)
         SPACE 5
***********************************************************************
*                                                                     *
*        GRUNDSTELLUNG DER SCHLUESSELFELDER                           *
*              HIER WERDEN ALLE SCHLUESSELFELDER BEARBEITET.          *
*              DAS = ZEICHEN WIRD GESETZT UND DIE LAENGE DES          *
*              SCHLUESSELS ERMITTELT.                                 *
*                                                                     *
***********************************************************************
         SPACE 3
B1       LR    R6,R5
B2       MVI   8(R6),C' '
         MVI   9(R6),0
         LA    R1,8(R6)
         TRT   0(8,R6),KTAB2
         MVI   0(R1),C'='
         SR    R1,R6
         BCTR  R1,0
         STH   R1,10(R6)
         LA    R6,16(R6)
         CLI   0(R6),C' '
         BNE   B2
         B     A2
         SPACE 5
***********************************************************************
*                                                                     *
*        BEARBEITUNG ALLER SCHLUESSEL                                 *
*              HIER WIRD FUER JEDEN SCHLUESSEL DER EINGABEBEREICH     *
*              ABGEKLAPPERT. IST DAS ZUGEHOERIGE FELD GEFUNDEN ODER   *
*              ENTHAELT DIE GESAMTE EINGABE NICHT DEN PASSENDEN KEY   *
*              SO WIRD DER NAECHSTE SCHLUESSEL BEARBEITET.            *
*              EINGANGSBEDINGUNG                                      *
*                   R3   LAUFENDE EINGABEADRESSE                      *
*                   R4   ENDEADRESSE                                  *
*                   R5   ANFANG SCHLUESSELFELDER                      *
*                   R8   ANFANG DER EINGABE                           *
*                                                                     *
***********************************************************************
         SPACE 3
C1       B     CA1
C2       LTR   R15,R15        WENN SCHLUESSEL
         BZ    C3             GEFUNDEN DANN
C4       LR    R3,R8          VON VORNE
         LA    R5,16(R5)      MIT NAECHSTEM SCHLUESSEL
         CLI   0(R5),C' '
         BNE   C1
         B     A3
C3       LR    R1,R4
         TRT   0(256,R3),KTAB1
         CR    R1,R4
         BNL   C4
         CLI   1(R1),C' '
         BE    C4
         LA    R3,1(R1)
         B     C1
         SPACE 5
***********************************************************************
*                                                                     *
*        SCHLUESSELVERGLEICH                                          *
*              HIER WIRD EIN SCHLUESSEL BEARBEITET WENN ER NOCH       *
*              NICHT ERFOLGREICH BEARBEITET WORDEN IST. DER VERGLEICH *
*              WIRD MIT EXECUTE DURCHGEFUEHRT WENN ER INNERHALB DER   *
*              ENDEADRESSE MOEGLICH IST.                              *
*              IST DER RICHTIGE SCHLUESSEL GEFUNDEN WIRD DIE LAENGE   *
*              DES PARAMETERS ERMITTELT (BIS ZUM BLANK ODER KOMMA,    *
*              BZW. WENN DAS 1.ZEICHEN KLAMMER AUF IST, BIS ZUR       *
*              KLAMMER ZU.                                            *
*              IST DER RICHTIGE SCHLUESSEL GEFUNDEN WIRD DIE ADRESSE  *
*              DES WERTS GESPEICHERT UND DER RUECKKEHRCODE 4 INS      *
*              REGISTER 15 GESETZT.                                   *
*              EINGANGSBEDINGUNG                                      *
*                   R3   ADRESSE DES ZU VERGLEICHENDEN WERTES         *
*                   R4   ENDEADRESSE                                  *
*                   R5   SCHLUESSELFELD                               *
*                        *********************                        *
*                       0*  SCHLUESSELWORT   *                        *
*                        *********************                        *
*                       8* RESERVE * ADRESSE *                        *
*                        *********************                        *
*                        DAS RESERVEFELD WIRD INTERN SO VERWENDET     *
*                             BYTE 1   REST DES SCHLUESSELWORTES (=)  *
*                             BYTE 2   0=NOCH ZU VERGLEICHEN          *
*                                      1=BEREITS GEFUNDEN             *
*                             BYTE 3/4 LAENGE FUER EXECUTE            *
*                                                                     *
***********************************************************************
         SPACE 3
CA1      SR    R15,R15
         CLI   9(R5),1
         BE    CA2
         LH    R6,10(R5)
         LA    R7,2(R3,R6)
         CR    R7,R4
         BNL   CA2
         EX    R6,KCLC
         BNE   CA2
* KEY GEFUNDEN
         LR    R1,R4          *****
         CLI   0(R7),C'('     *
         BE    CA3
         TRT   0(256,R7),KTAB1 BLANK ODER KOMMA SUCHEN
CA4      CR    R1,R4          *
         BNL   CA2            *
         SR    R1,R7          * LAENGE ERMITTELN
         BZ    CA2
         STH   R1,10(R5)      *****
         LA    R15,4
         ST    R7,12(R5)
         MVI   9(R5),1
CA2      B     C2
CA3      TRT   0(256,R7),KTAB3 KLAMMER ZU SUCHEN
         LA    R1,1(R1)
         B     CA4
         SPACE 5
***********************************************************************
*                                                                     *
*        MACRO-DEFINITIONEN
*                                                                     *
***********************************************************************
         SPACE 3
         MCEQU
         SPACE 5
***********************************************************************
*                                                                     *
*        KONSTANTEN                                                   *
*                                                                     *
***********************************************************************
         SPACE 3
SAVEAREA DC    18F'0'
KCLC     CLC   0(1,R3),0(R5)
KTAB1    DC    XL64'0'
         DC    C' '
         DC    XL42'0'
         DC    C','
         DC    XL148'0'
KTAB2    DC    XL64'0'
         DC    C' '
         DC    XL61'0'
         DC    C'='
         DC    XL129'0'
KTAB3    DC    XL93'0'
         DC    C')'
         DC    XL162'0'
         END   MODKEY
