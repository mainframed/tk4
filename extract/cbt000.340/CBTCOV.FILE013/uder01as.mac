*YANK *CPU     00000010
FD010400 CSECT
* SNAP ID=1 WAIT ZEIT  >  LAUFZEIT BEI TYP 1 RECORD
* SNAP ID=2   CPU-ZEIT > LAUFZEIT BEI TYP-4-RECORD
* SNAP ID=3   WAIT-ZEIT > LAUF-ZEIT BEI TYP 12 RECORD
* SNAP ID=4   DATUM/ZEIT < DATUM/ZEIT ALT BEI TYP1 ODER TYP12 RECS
*             END OF STEP NICHT GROESSER START OF STEP BEI TYP 4 RECS
* SNAP ID=5   DIFFERENZ END OF INTERVAL ALT ZU END OF INTERVAL
*             NEU < 100 BEI TYP 1 RECORD.
* WIRD TYP1 REC GESCHRIEBEN NUR IN VERB. MIT TYP4-REC?
* SIND ZEITABSTAENDE 10 MIN ODER GROESSER?
* IS T1. REC NACH IPL-REC START DER 10-MIN INTERVALLE
* IST ZEOD-REC EVTL START EINES NEUEN INTERVALLES
* IST DIE ACCUMULIERTE WAITTIME RICHTIG?
         SUFF
         DROP  2
         USING SAVEAREA,13,2
         L     2,BASE
         B     START
BASE     DC    A(SAVEAREA+4096)
EINGABE  DCB   DDNAME=FD010401,DSORG=PS,MACRF=GL,EODAD=ENDE
AUSGABE1 DCB   DDNAME=FD010402,DSORG=PS,MACRF=PM
AUSGABE2 DCB   DDNAME=FD010403,DSORG=PS,MACRF=PM
TESTBAND DCB   DDNAME=TESTBAND,DSORG=PS,MACRF=PM
SNAP1 DCB DSORG=PS,RECFM=VBA,MACRF=(W),BLKSIZE=882,LRECL=125,          C
               DDNAME=SNAPDUMP
HEAD1    EQU   *
         DC    C'1'
         DC    40C' '
         DC    C'RELATION CPU-TIME / WAIT-TIME / SYSTEM-OVERHEAD'
         DC    50C' '
HEAD2    DC    C'- IPL/  DATE   TIME   ANZ REC-  WAIT-TIME   CPU+SYS  '
         DC    C'CPU-T4   WAIT-TIME   CPU+SYS   CPU'
         DC    C'   PRECL    ENDPER  PIN10  POU10  PGES   PRATE'
         DC    50C' '
HEAD3    EQU   *
         DC    C'  EOD               PER  TYP    IN SEC      IN %     '
         DC    C' IN SEC  1H IN SEC     IN %      %'
         DC    50C' '
DETAIL   EQU   *
HVORS    DS    CL1
         DS    CL2
HI       DS    CL1
         DS    CL4
HDATE    DS    CL5
         DS    CL2
HTIME    DS    CL6
         DS    CL2
ANZPER   DS    CL2
         DS    CL3
HRECTYP  DS    CL1
         DS    CL6
HWAIT10  DS    CL4
         DS    CL7
HCSPRO10 DS    CL3
         DS    CL7
HCPU60   DS    CL4
         DS    CL6
HWAIT60  DS    CL4
         DS    CL6
HCSPRO60 DS    CL4
         DS    CL5
HCPRO60  DS    CL4
         DS    CL4
PRECL    DS    CL4
         DS    CL3
ENDPER   DS    CL6
         DS    CL2
HPIN10   DS    CL5
         DS    CL2
HPOUT10  DS    CL5
         DS    CL2
HPGES    DS    CL5
         DS    CL2
HPRATE   DS    CL3
         DS    CL10
DETAIL2  DS    0CL133
         DC    C'      '
         DC    C'PAGEINS   '
HPIN60   DS    CL9
         DC    C'   PAGEOUTS   '
HPOUT60  DS    CL9
         DC    C'   TOTAL   '
HPGES60  DS    CL9
         DC    C'   PAGING RATE PER SECOND   '
HPRATE60 DS    CL3
         DC    C'    PAGE-RECLAIMS   '
RECLAIMS DS    CL7
         DC    C' '
RPROMIN  DS    CL3
         DC    3C' '
ENDSUM   EQU   *
         DC    C'-'
         DC    10C' '
         DC    C'WAIT '
EWAIT    DS    CL7
         DC    C' '
EPW      DS    CL3
         DC    C'  CPU '
ECPU     DS    CL7
         DC    C' '
EPC      DS    CL3
         DC    C'  SYS '
ESYS     DS    CL7
         DC    C' '
EPS      DS    CL3
         DC    C'  RUN '
ERUN     DS    CL7
         DC    70C' '
KURVE1   EQU   *
         DC    C'1'
         DC    38C' '
         DC    C'CPU-ZEIT UND SYSTEM-OVERHEAD IN % VON LAUFZEIT'
         DC    50C' '
KURVE2   EQU   *
         DC    CL6' '
KI       DS    CL3
         DC    2C' '
KDATE1   DS    CL6
         DC    C' '
KTIME1   DS    CL8
KTIME1   DC    C'           1       10        20        30        40 '
         DC    C'       50        60        70        80        90   '
         DC    C'    100     '
KURVE3   EQU   *
KVORS    DS    CL1
         DC    17C' '
KTIME2   DC    CL8' '
         DC    C' '
KNR      DS    CL1
         DC    C' '
KSTART   DC    100C' '
         DC    10C' '
IPLDATE  DC    F'0'
IPLTIME  DC    F'0'
WKTIME   DC    CL8'  .  .  '
WKDATE1  DC    CL6'  .   '
STERNE   DC    100C'*'
WKDATE2  DC    CL5' '
WAIT10   DS    F
RUN10    DC    F'60000'           10 MINUTEN
PIN10    DC   F'0'
POUT10   DC   F'0'
PRECL10  DC    F'0'
WAIT60   DC    F'0'
RUN60    DC    F'0'
CPU60    DC    F'0'
PIN60    DC   F'0'
POUT60   DC   F'0'
PRECL60  DC    F'0'
WAITTOT  DC    F'0'
RUNTOT   DC    F'0'
RUNTOTS  DC    F'0'
CPUTOT   DC    F'0'
PINTOT   DC   F'0'
POUTTOT  DC   F'0'
PRECLTOT DC    F'0'
WTOTAL   DC    F'0'
CTOTAL   DC    F'0'
RTOTAL   DC    F'0'
PINTOTAL DC   F'0'
POUTOTAL DC   F'0'
RECTOTAL DC    F'0'
INTEND1  DC   F'0'
RESTRUN  DC    F'0'
RESTWAIT DC    F'0'
TIME10   DC    F'0'
DATE10   DC    F'0'
RUN60SEC DC    F'0'
R4SAV    DC    F'0'
CCC      DC    F'56'
TIMESTAR DS    CL4
TIMEEND  DS    CL4
RUN10SEC DC    F'0'
RUNIPLT  DC    F'0'
RUN4COUN DC    F'0'
RUN4     DC    F'0'
T4TIME   DC    F'0'
T4DATE   DC    X'0000000F'
ZEODTIME DC    F'0'
DWORT1   DC    D'0'
DWORT2   DC    D'0'
DWORT3   DC    D'0'
DUMPLIST EQU   *
         DC    F'0'
         DC    F'0'
         DC    X'00'
         DC    AL3(FD010400)
         DC    X'80'
         DC    AL3(ENDREC)
ERRCOUN  DC    PL4'0'
IPLCOUN  DC    PL4'0'
CHKCOUN  DC    PL4'0'
T4COUN   DC    PL5'0'
T5COUN   DC    PL5'0'
CPUERRCT DC    PL3'0'
SEQNR    DC    PL2'1'
SW1      DC    C'0'
SW2      DC    C'0'
SW3      DC    C'0'
SW4      DC    C'0'
         DS    CL1
TIME     DS    CL8
DATE     DS    CL5
DATESTAR DS    CL4
DATEEND  DS    CL4
ENDREC   EQU   *
         DC    C'FALSCHE TYP1-SAETZE '
FALSEREC DC    4C' '
         DC    C' LAUFZ VON IPL BIS ZEOD ODER TYP4 '
LAUFIPL  DC    7C' '
         DC    C' ANZ IPL-RECS '
IPLREC   DC    4C' '
         DC    C'ANZ FEHLER-ENDE '
MASCHCK  DC    3C' '
         DC    C' GESAMT LAUFZ TYP4 '
TYP4RUN  DC    7C' '
         DC    10C' '
ENDREC1  EQU   *
         DC    C'TYP4-REC '
T4RECS   DS    CL8
         DC    C' TYP5-RECS '
T5RECS   DS    CL8
         DC    C'   CPU-TIME-ERRORS   '
CPUERR   DS    CL3
         DC    C'  '
ERROR    DC    30C' '
         DC    C'ERROR ERROR ERROR ERROR'
         DC    80C' '
ECT      DC    P'0'
START    DS    0H
         L     1,0(1)                  GET PARM-ADDR
         LH    3,0(1)                  GET LENGTH-FIELD
* PARM INFO YYDDD,YYDDD     ANFANGSDATUM,ENDEDATUM
         PACK  DATESTAR,2(5,1)
         PACK  DATEEND,8(5,1)
*        PACK DWORT1,14(1,1)
*        CVB   3,DWORT1
*        STC   3,A035+3
         OPEN  (EINGABE,,AUSGABE1,(OUTPUT),AUSGABE2,(OUTPUT))
         OPEN  (TESTBAND,(OUTPUT),SNAP1,(OUTPUT))
A030     EQU   *
         GET   EINGABE
         USING REC0112,3
A035     EQU   *
         LA    3,4(1)              FLIMINATE RDW
         ST    3,DUMPLIST
         LA    4,500(3)
         ST    4,DUMPLIST+4
         CLC   RECDATE,DATESTAR    START-DATE ?
         BL    A030               NO GET NEXT REC
         BE    A050
         CLC   RECDATE,DATEEND     END-DATE ?
         BH    ENDE
A050     EQU   *
         CLI   RECTYP,X'00'
         BE    B000                PROCESS  IPL-RECORD
         CLI   RECTYP,X'01'
         BE    C000                PROCESS WAIT-TIME-RECORD
         CLI   RECTYP,X'04'
         BE    D000               PROCESS  STEP-END-RECORD
         CLI   RECTYP,X'05'
         BE    D000
         CLI   RECTYP,X'0C'
         BNE   A030
         BAL   4,L000
         B     E000
B000     EQU   *                  IPL-RECORD
         BAL   4,L000
B002     B     B010
         CLI   SW2,C'0'
         BNE   B005
         AP    CHKCOUN,=P'1'
         L     5,T4TIME
         ZAP   DWORT1,T4DATE
         B     B007
B005     EQU   *
         L     5,TIME10
         ZAP   DWORT1,DATE10
B007     EQU   *
         L     6,IPLTIME
         CR    5,6
         BNL   B007A
         CP    DWORT1,IPLDATE
         BH    B007B
         MVC   ERROR+60(11),=C'IPL IPL IPL'
         PUT   AUSGABE2,ERROR
         MVC   ERROR+60(11),=C'           '
         MVI   SW1,C'0'
         SR    5,5
         ST    5,WAIT60
         ST    5,RUN60
         ST    5,CPU60
         ST    5,PIN60
         ST    5,POUT60
         ST    5,PRECL60
         ST    5,PRECLTOT
         ST    5,PINTOT
         ST    5,POUTTOT
         ST    5,CPUTOT
         ST    5,RUNTOT
         ST    5,WAITTOT
         B     B010
B007B    EQU   *
         SP    DWORT1,=P'1'
         A    5,=F'8640000'
B007A    EQU   *
         SP    DWORT1,IPLDATE
         CVB   8,DWORT1
         SR    9,9
         LTR   8,8
         BZ    *+12
B008     EQU   *
         A     9,=F'8640000'
         BCT   8,B008
         SR    5,6
         AR    5,9
         L     6,RUNIPLT
         AR    5,6
         ST    5,RUNIPLT
         L     4,RUN4
         L     5,RUN4COUN
         AR    4,5
         ST    4,RUN4COUN
         SR    5,5
         ST    5,RUN4
         CLI   SW1,C'1'           1 = LETZTE VERARB IST ABZUSCHLIESSEN
         BE    B050
B010     EQU   *
         MVC   CCC,=F'56'
         CLI   SW3,C'1'
         BNE   B015
         SP    CHKCOUN,=P'1'
         MVC   IPLTIME,=X'00000000'
         BAL   4,F000
         NI    C005+1,X'0F'
         MVI   SW3,C'0'
         B     A030
B015     EQU   *
         AP    IPLCOUN,=P'1'
         MVC   IPLDATE,RECDATE
         MVC   IPLTIME,RECTIME
         MVC   TIME10,RECTIME
         MVC   T4TIME,RECTIME
         MVC   DATE10,RECDATE
         MVC   T4DATE,RECDATE
         NI    C005+1,X'0F'
         BAL   4,F000             WRITE HEADS
         MVC   KI,=C'IPL'
         L     15,IPLTIME         ERGEBNIS IM FELD TIME IN DER FORM
         BAL  4,CONVTIME         H
         MVC   KDATE1,WKDATE1
         MVC   KTIME1,WKTIME
         PUT   AUSGABE1,KURVE2
         MVI   HVORS,C'-'
         MVI   HVORS+1,C' '
         MVC   HVORS+2(131),HVORS+1
         MVI   HI,C'I'
         MVC   HDATE,WKDATE2
         MVC   HTIME,TIME
         MVI   HRECTYP,C'0'
         PUT   AUSGABE2,DETAIL
         B     A030
B050     EQU   *
         MVI   SW1,C'0'
         L     5,RUN60
         LTR   5,5
         BZ    B060
         MVI   KURVE3,C' '
         MVC   KURVE3+1(132),KURVE3
         MVI   KSTART-1,C'I'
         MVI   KSTART+100,C'I'
         MVI   DETAIL,C' '
         MVC   DETAIL+1(132),DETAIL
         MVI   HVORS,C' '
         MVI   HI,C'I'
         ZAP   SEQNR,=P'1'
         MVC   DWORT3,RECTIME
         L     15,DWORT3
         BAL   4,CONVTIME
         MVC   HTIME,TIME
         UNPK  HDATE,RECDATE
         MVI   HRECTYP,C'0'
         BAL   4,K030
         PUT   AUSGABE2,DETAIL
         PUT   AUSGABE2,DETAIL2
B060     EQU   *
         BAL   4,K040
         B     B010
C000     EQU   *
         BAL   4,L000
C005     NOP   C010
         MVC   INTEND1,INTEND
         L     5,IPLTIME
         LTR   5,5
         BNZ   *+28
         MVC   DATE10,RECDATE
         MVC   TIME10,RECTIME
         MVC   IPLTIME,RECTIME
         MVC   IPLDATE,RECDATE
         OI    C005+1,X'F0'
         B     A030
C010     EQU   *
         MVI   DETAIL,C' '
         MVC   DETAIL+1(132),DETAIL
         MVI   HVORS,C' '
         MVI   HRECTYP,C'1'
         UNPK  KNR,SEQNR
         L     4,TIME10
         MVC   DWORT3,RECTIME
         L     6,DWORT3
         CR    6,4
         BNL   C015
         A     6,=F'8640000'
         CP    RECDATE,DATE10
         BNH   DATEERR
C015     EQU   *
         CP    RECDATE,DATE10
         BL    DATEERR
         MVC   DWORT3,INTEND
         L     6,DWORT3
         L     4,INTEND1
         CR    6,4
         BNL   *+8
         A     6,=F'8640000'
         SR    6,4
         LA    4,100
         CR    6,4
         BH    C016
         LA    6,100
         SNAP  DCB=SNAP1,ID=5,PDATA=(PSW,REGS,SA),LIST=DUMPLIST
C016     EQU   *
         L     5,=F'60000'
         LR    8,6
         SRDA  6,32
         DR    6,5
         C    6,=F'54000'
         BL    *+8
         LA    7,1(7)
         CVD   7,DWORT3
         AP    SEQNR,DWORT3+6(2)
         UNPK  ANZPER,DWORT3+6(2)
         OI    ANZPER+1,X'F0'
         ST    8,RUN10
         MVC   DATE10,RECDATE
         MVC   INTEND1,INTEND
         MVC     TIME10,RECTIME
         L     8,RUN10
         MVC   WAIT10,RECSYSWT
         L     7,WAIT10
         CR    7,8
         BNH   C019
         MVC   WAIT10,RUN10
         L     15,WAIT10
         SRL   15,1
         ST    15,WAIT10
         SNAP  DCB=SNAP1,ID=1,PDATA=(PSW,REGS,SA),LIST=DUMPLIST
         AP    ERRCOUN,=P'1'
C019     EQU   *
         L     6,RUN10
         BAL   4,K060
         ST    6,RUN10SEC
         MVC   PIN10,PAGEIN
         MVC   POUT10,PAGEOUT
         MVC   PRECL10,PAGERECL
         BAL    4,K010
         BAL   4,K020
         CP    SEQNR,=P'7'
         BNL   C050
C020     EQU   *
         L     5,CCC
         BCT   5,C030
         L     5,=F'56'
         BAL   4,F000
C030     EQU   *
         MVC   DWORT3,INTEND
         L    15,DWORT3
         BAL   4,CONVTIME
         MVC   ENDPER,TIME
         ST    5,CCC
         PUT   AUSGABE2,DETAIL
         MVI   SW1,C'1'
         NI    B002+1,X'0F'
         CLI   SW4,C'1'
         BNE   A030
         MVI   SW4,C'0'
         L     5,CCC
         BCT   5,C035
         L     5,=F'56'
         BAL   4,F000
C035     EQU   *
         ST    5,CCC
         PUT   AUSGABE2,DETAIL2
         B     A030
C050     EQU   *
         MVI   SW4,C'1'
         MVI   KURVE3,C' '
         MVC   KURVE3+1(132),KURVE3
         MVI   KSTART-1,C'I'
         MVI   KSTART+100,C'I'
         ZAP   SEQNR,=P'1'
         MVC   DWORT3,RECTIME
         L     15,DWORT3
        BAL   4,CONVTIME
         MVC   KTIME2,WKTIME
         MVC   HTIME,TIME
         BAL   4,K030
         B     C020
D000     EQU   *
         CLI   RECTYP,X'05'
         BNE   *+14
         AP    T5COUN,=P'1'
         B     A030
         MVC   DWORT1(4),6(3)
         MVC   DWORT2(4),43(3)   SOS
         L     4,DWORT1
         L     5,DWORT2
         CR    4,5
         BNL   D020
         L     6,=F'8640000'
         CP    10(4,3),47(4,3)           DEOS,DSOS
         BNH   DATEERR
         AR    4,6
D020     EQU   *
         SR    4,5
         L     5,RUN4
         AR    5,4
         ST    5,RUN4
         MVC   T4TIME,6(3)
         MVC   T4DATE,10(3)
         L     5,IPLTIME
         LTR   5,5
         BNZ   *+28
         MVC   DATE10,47(3)
         MVC   TIME10,43(3)
         MVC   IPLTIME,43(3)
         MVC   IPLDATE,47(3)
         MVI   SW2,C'0'
         AP    T4COUN,=P'1'
         MVC   DWORT1(2),106(3)   LENGTH-FACTOR IN TYP-4-REC
         LA    5,106(3)
         LH    6,DWORT1
         AR    5,6
         MVC   DWORT1+1(3),1(5)
         MVI   DWORT1,X'00'
         L     5,CPU60
         L     6,DWORT1
         CR    4,6
         BNH   D030
         AR    5,6
         ST    5,CPU60
         B     A030
D030     EQU   *
         AP    CPUERRCT,=P'1'
         SNAP  DCB=SNAP1,ID=2,PDATA=(PSW,REGS,SA),LIST=DUMPLIST
         B     A030
E000     EQU   *
         MVI   SW1,C'1'
         MVI   SW2,C'1'
         MVI   DETAIL,C' '
         MVC   DETAIL+1(132),DETAIL
         MVI   HVORS,C' '
         MVI   HRECTYP,C'Z'
         L     4,TIME10
         MVC   DWORT3,RECTIME
         L     6,DWORT3
         CR    6,4
         BNL   E010
         A     6,=F'8640000'
         CP    RECDATE,DATE10
         BNH   DATEERR
E010     EQU   *
         L     4,INTEND1
         MVC   DWORT1,INTEND
         L     6,DWORT1
         CR    6,4
         BNL   *+8
         A     6,=F'8640000'
         SR    6,4
         ST    6,RUN10
         L    15,DWORT1
         BAL   4,CONVTIME
         MVC   ENDPER,TIME
         L     15,DWORT3
         BAL    4,CONVTIME
         MVC   KTIME2,WKTIME
         MVC   HTIME,TIME
         BAL   4,K060
         ST    6,RUN10SEC
         MVC   WAIT10,RECSYSWT
         L     6,RUN10
         L     7,WAIT10
         CR    7,6
         BNH   E015
         MVC   WAIT10,RUN10
         L     15,WAIT10
         SRL   15,1
         ST    15,WAIT10
         SNAP  DCB=SNAP1,ID=3,PDATA=(PSW,REGS,SA),LIST=DUMPLIST
         AP    ERRCOUN,=P'1'
E015     EQU   *
         MVC   TIME10,RECTIME
         MVC   DATE10,RECDATE
         MVC   PIN10,PAGEIN
         MVC   POUT10,PAGEOUT
         MVC   INTEND1,INTEND
         MVC   PRECL10,PAGERECL
         BAL   4,K010
         BAL   4,K020
         MVI   KURVE3,C' '
         MVC   KURVE3+1(132),KURVE3
         MVI   KSTART-1,C'I'
         MVI   KSTART+100,C'I'
         ZAP   SEQNR,=P'1'
         BAL   4,K030
         PUT   AUSGABE2,DETAIL
         PUT   AUSGABE2,DETAIL2
         B     A030
K010     EQU   *
         ST    4,R4SAV
         L     6,PIN10
         CVD   6,DWORT1
         UNPK  HPIN10,DWORT1
         OI    HPIN10+4,X'F0'
         L     7,POUT10
         CVD   7,DWORT1
         UNPK  HPOUT10,DWORT1
         OI    HPOUT10+4,X'F0'
         AR    6,7
         CVD   6,DWORT1
         UNPK  HPGES,DWORT1
         OI    HPGES+4,X'F0'
         L     8,RUN10SEC
         SRDA  6,32
         DR    6,8
         SRL   8,1
         CR    6,8
         BL    *+8
         LA    7,1(7)
         CVD    7,DWORT1
         UNPK  HPRATE,DWORT1
         OI    HPRATE+2,X'F0'
         L     6,PRECL10
         CVD   6,DWORT1
         UNPK  PRECL,DWORT1
         OI    PRECL+3,X'F0'
         L     6,WAIT10
         BAL   4,K060
         CVD   6,DWORT1
         UNPK  HWAIT10,DWORT1
         OI    HWAIT10+3,X'F0'
         L     5,WAIT10
         L     8,RUN10SEC
         L     6,RUN10
         SR    6,5                REG 6 CONTAINS CPU+SYS-TIME IN 1/100
         SRDA  6,32
         DR    6,8
         SRL   8,1
         CR    6,8
         BL    *+8
         LA    7,1(7)
         CVD   7,DWORT2
         UNPK  HCSPRO10,DWORT2
         OI    HCSPRO10+2,X'F0'
         L     4,R4SAV
         BR    4
K020     EQU   *
         L     5,PIN10
         A     5,PIN60
         ST    5,PIN60
         L     5,POUT10
         A     5,POUT60
         ST    5,POUT60
         L     5,PRECL10
         A     5,PRECL60
         ST    5,PRECL60
         L     5,RUN60
         L     6,RUN10
         AR    5,6
         ST    5,RUN60
         OI    KNR,X'F0'
         L     5,WAIT60
         L     6,WAIT10
         AR    5,6
         ST    5,WAIT60
         BR    4
K030     EQU   *
         ST    4,R4SAV
         L     6,RUN60
         BAL   4,K060
         ST    6,RUN60SEC
         L     6,PIN60
         L     7,POUT60
         L     8,RUN60SEC
         L    15,PRECL60
         BAL   4,PAGESTAT
         L     6,CPU60
         BAL   4,K060
         CVD   6,DWORT1
         UNPK  HCPU60-1(5),DWORT1
         OI    HCPU60+3,X'F0'
         L     6,CPU60
         L     11,RUN60SEC
         BAL   4,K050
         UNPK  HCPRO60,DWORT1
         OI    HCPRO60+3,X'F0'
         L     6,WAIT60
         BAL   4,K060
         CVD   6,DWORT1
         UNPK  HWAIT60,DWORT1
         OI    HWAIT60+3,X'F0'
         L     6,RUN60
         L     5,WAIT60
         SR    6,5                REG 6 CONTAINS CPU+SYS-TIME
         L     11,RUN60SEC
         BAL   4,K050
         UNPK  HCSPRO60,DWORT1
         OI    HCSPRO60+3,X'F0'
         MVI   KURVE3,C' '
         CVB   6,DWORT1
         LA    11,100
         BAL   4,K050
         CVB   5,DWORT1
         LA    6,102
         CR    5,6
         BL    *+8
         LA    5,1
         LTR   5,5
         BNZ   *+8
         LA    5,1
         BCTR  5,0
         EX    5,MVCSTERN
         MVI   KSTART-1,C'I'
         MVI   KSTART+100,C'I'
         PUT   AUSGABE1,KURVE3
         SR    5,5
         L     5,WAIT60
         L     6,WAITTOT
         AR    6,5
         ST    6,WAITTOT
         L     5,RUN60
         L     6,RUNTOT
         AR    6,5
         ST    6,RUNTOT
         L     5,CPU60
         L     6,CPUTOT
         AR    6,5
         ST    6,CPUTOT
         L     5,PIN60
         A     5,PINTOT
         ST    5,PINTOT
         L     5,POUT60
         A     5,POUTTOT
         ST    5,POUTTOT
         L     5,PRECL60
         A     5,PRECLTOT
         ST    5,PRECLTOT
         SR    5,5
         ST    5,PRECL60
         ST    5,WAIT60
         ST    5,RUN60
         ST    5,CPU60
         ST    5,PIN60
         ST    5,POUT60
         L     4,R4SAV
         BR    4
MVCSTERN MVC   KSTART(1),STERNE
K040     EQU   *
         ST    4,R4SAV
         L     4,PRECLTOT
         A     4,RECTOTAL
         ST    4,RECTOTAL
         L    4,PINTOT
         A     4,PINTOTAL
         ST    4,PINTOTAL
         L     4,POUTTOT
         A     4,POUTOTAL
         ST    4,POUTOTAL
         L     15,WAITTOT
         L     4,WTOTAL
         AR    4,15
         ST    4,WTOTAL
         BAL   4,CONVTIME
         MVC   EWAIT,TIME-1
         L     15,CPUTOT
         L     4,CTOTAL
         AR    4,15
         ST    4,CTOTAL
         BAL   4,CONVTIME
         MVC   ECPU,TIME-1
         L     15,RUNTOT
         L     4,RTOTAL
         AR    4,15
         ST    4,RTOTAL
         BAL   4,CONVTIME
         MVC   ERUN,TIME-1
         L     5,RUNTOT
         L     6,WAITTOT
         SR    5,6
         L     6,CPUTOT
         SR    5,6
         LR    15,5
         LR    11,15
         BAL    4,CONVTIME
         MVC   ESYS,TIME-1
         L     6,RUNTOT
         BAL   4,K060
         ST    6,RUNTOTS
         LR    7,6
         LR    6,11
         LR    11,7
         BAL   4,K050
         UNPK  EPS,DWORT1
         OI    EPS+2,X'F0'
         L     6,PINTOT
         L     7,POUTTOT
         L     8,RUNTOTS
         L    15,PRECLTOT
         BAL    4,PAGESTAT
         L     6,CPUTOT
         L     11,RUNTOTS
         BAL   4,K050
         UNPK  EPC,DWORT1
         OI    EPC+2,X'F0'
         L     6,WAITTOT
         L     11,RUNTOTS
         BAL   4,K050
         UNPK  EPW,DWORT1
         OI    EPW+2,X'F0'
         PUT  AUSGABE2,ENDSUM
         PUT   AUSGABE2,DETAIL2
         L     4,R4SAV
         SR    5,5
         ST    5,PINTOT
         ST  5,POUTTOT
         ST    5,CPUTOT
         ST    5,RUNTOT
         ST    5,WAITTOT
         ST     5,PRECLTOT
         BR    4
K050     EQU   *
         SRDA   6,32
         M     6,=F'10'
         DR    6,11
         SRL   11,1
         CR    6,11
         BL    *+8
         LA    7,1(7)
         CVD   7,DWORT1
         BR    4
K060     EQU   *
         SRDA  6,32
         LA    8,100
         DR    6,8
         LA    8,50
         CR    6,8
         BL    *+8
         LA    7,1(7)
         LR    6,7
         BR    4
L000     EQU   *
         LR    5,1
         PUT   TESTBAND,(5)
         BR    4
DATEERR  EQU   *
         MVI   SW3,C'1'
         SNAP  DCB=SNAP1,ID=4,PDATA=(PSW,REGS,SA),LIST=DUMPLIST
         MVI   DETAIL,C'-'
         MVI   DETAIL+1,C' '
         MVC   DETAIL+2(131),DETAIL+1
         MVC   DETAIL+10(12),=C'DATUMSFEHLER'
         PUT   AUSGABE2,DETAIL
         B     B002
PAGESTAT EQU   *
         LR    14,8
         CVD   6,DWORT1
         UNPK  HPIN60,DWORT1
         OI    HPIN60+8,X'F0'
         CVD   7,DWORT1
         UNPK  HPOUT60,DWORT1
         OI    HPOUT60+8,X'F0'
         AR    6,7
         CVD   6,DWORT1
         UNPK  HPGES60,DWORT1
         OI    HPGES60+8,X'F0'
         SRDA  6,32
         DR    6,8
         SRL   8,1
         CR    6,8
         BL    *+8
         LA    7,1(7)
         CVD    7,DWORT1
         UNPK  HPRATE60,DWORT1
         OI    HPRATE60+2,X'F0'
         LR    6,14
         SRDA  6,32
         LA    8,60
         DR    6,8
         SRL  8,1
         CR    6,8
         BL    *+8
         LA    7,1(7)
         LTR   7,7
         BNZ   *+8
         LA    7,1
         LR    6,15
         LR    8,7
         SRDA  6,32
         DR    6,8
         SRL  8,1
         CR    6,8
         BL    *+8
         LA    7,1(7)
         CVD   7,DWORT1
         UNPK  RPROMIN,DWORT1
         OI    RPROMIN+2,X'F0'
         CVD   15,DWORT1
         UNPK  RECLAIMS,DWORT1
         OI    RECLAIMS+6,X'F0'
         BR    4
CONVTIME EQU   *
         LR    14,15
         SRDA  14,32
         L     5,=F'360000'
         DR    14,5
         CVD   15,DWORT1
         UNPK  TIME-1(3),DWORT1
         OI    TIME+1,X'F0'
         SRDA  14,32
         L     5,=F'6000'
         DR    14,5
         CVD   15,DWORT1
         UNPK  TIME+2(2),DWORT1
         OI    TIME+3,X'F0'
         SRDA  14,32
         LA    5,100
         DR    14,5
         CVD   15,DWORT1
         UNPK  TIME+4(2),DWORT1
         OI    TIME+5,X'F0'
         CVD   14,DWORT1
         UNPK  TIME+6(2),DWORT1
         OI     TIME+7,X'F0'
         MVC   WKTIME(2),TIME
         MVC   WKTIME+3(2),TIME+2
         MVC   WKTIME+6(2),TIME+4
         UNPK  WKDATE2,IPLDATE
         MVC   WKDATE1(2),WKDATE2
         MVC   WKDATE1+3(3),WKDATE2+2
         BR    4
F000     EQU   *
         NOP   F010
         PUT   AUSGABE1,KURVE1
         OI    F000+1,X'F0'
F010     EQU   *
         PUT   AUSGABE2,HEAD1
         PUT   AUSGABE2,HEAD2
         PUT   AUSGABE2,HEAD3
         BR    4
Z010     EQU   *
         L     5,RUNTOT
         LTR   5,5
         BZ    Z025
         B     Z020
ENDE     EQU   *
         CLI   SW1,C'1'
         BNE   Z010
         L     5,RUN60
         LTR   5,5
         BZ    Z020
         MVI   KURVE3,C' '
         MVC   KURVE3+1(132),KURVE3
         MVI   DETAIL,C' '
         MVC   DETAIL+1(132),DETAIL
         MVI   HVORS,C' '
         MVI   HI,C'E'
         BAL   4,K030
         PUT   AUSGABE2,DETAIL
         PUT   AUSGABE2,DETAIL2
Z020     EQU   *
         L     15,RUNIPLT
         L     14,RUNTOT
         AR    15,14
         ST    15,RUNIPLT
         BAL   4,K040
Z025     EQU   *
         L     5,WTOTAL
         LTR   5,5
         BZ    ERREND
         MVC   WAITTOT(4),WTOTAL
         MVC   CPUTOT(4),CTOTAL
         MVC   RUNTOT(4),RTOTAL
         MVC   PINTOT,PINTOTAL
         MVC   POUTTOT,POUTOTAL
         MVC   PRECLTOT,RECTOTAL
         BAL   4,K040
Z050     EQU   *
         L     15,RUNIPLT
         BAL   4,CONVTIME
         MVC   LAUFIPL(7),TIME-1
         L     15,RUN4COUN
         BAL   4,CONVTIME
         MVC   TYP4RUN(7),TIME-1
         UNPK  FALSEREC(4),ERRCOUN
         OI    FALSEREC+3,X'F0'
         UNPK  IPLREC(4),IPLCOUN
         OI    IPLREC+3,X'F0'
         UNPK  MASCHCK(3),CHKCOUN
         OI    MASCHCK+2,X'F0'
         MVC   DETAIL+2(131),ENDREC
         MVI   DETAIL,C'-'
         PUT   AUSGABE2,DETAIL
         MVI   DETAIL+1,C' '
         MVC   DETAIL+2(131),DETAIL+1
         UNPK  T4RECS,T4COUN
         UNPK  T5RECS,T5COUN
         OI    T4RECS+7,X'F0'
         OI    T5RECS+7,X'F0'
         UNPK  CPUERR(3),CPUERRCT
         OI    CPUERR+2,X'F0'
         MVC   DETAIL+2(62),ENDREC1
         PUT   AUSGABE2,DETAIL
ERREND   EQU   *
         CLOSE TESTBAND
         CLOSE (EINGABE,,AUSGABE1,,AUSGABE2)
         L     13,4(13)
         RETURN (14,12)
REC0112  DSECT                    STANDARD SMF-REC-HEADER
RECNR    DS    CL1
RECTYP   DS    CL1
RECTIME  DS    CL4                TIME IN 1/100 SEC
RECDATE  DS    CL4                DATE IN 00YYDDDF
         DS    CL4
REC0     EQU   *
WLIM     DS    CL4
BUFSIZE  DS    CL4
VSSTOR   DS    CL4
SMFOPT   DS    CL1
REALSTOR DS    CL4
         ORG   REC0
REC1     EQU   *
RECSYSWT DS    CL4
INTEND   DS    CL4
PAGEIN   DS    CL4
PAGEOUT  DS    CL4
PAGERECL DS    CL4
         ORG   REC0
REC12    EQU   *
* DEFINITION SAME AS REC1
* USAGE OF SWITCHES
* SW1 AUF NULL BEI IPL-RECORD (2 MAL).
* SW1 WENN 1 BEI IPL-RECORD DANN IST LETZTE VERARB ABZUSCHLIESSEN
* SW1 AUF 1 BEI MIND. EINEM TYP1 REC     AUF NULL BEI DATE-ERROR
* SW2 AUF ' BEI Z EOD. AUF NULL BEI IPL UND JEDEM TYP4 RECORD
* SW2 WENN NULL BEI IPL DANN KEIN ZEOD, DAS BEDEUTET FEHLER-ENDE
* SW3   GESETZT UND ABGEFRAGT BEI DATE-ERROR
* SW4 AUF 1 BEI  AUFBEREITEN 'STUNDENZEILE'
* SW4  WENN 1 WRITE PAGING STATISTIC PRO STUNDE UND MOVE NULL TO SW4
         END
