VDSCB1   START 0
***********************************************************************
*  THIS ROUTINE IS CALLED BY A PL/1 PROGRAM TO HANDLE ACCESSING
*    OF THE VTOC.
*
*              ENTRY POINTS--
*                  VDSCB1- SCANS THE TIOT FOR A DIRECT ACCESS
*                          VOLUME. SCANS VTOC FOR FORMAT 1,4 OR 5
*                          DSCB'S AND RETURNS IT TO PL/1.
*                  PARAMETERS-
*                          140 BYTE CHARACTER STRING
*                          BINARY FIELD FOR RETURN CODE
*
*                  VDSCB2 - OBTAINS DSCB AT THE ADDRESS SPECIFIED
*                          BY THE CALLING PROGRAM.
*                  PARAMETERS-
*                          140 BYTE CHARACTER STRING
*                          BINARY FIELD FOR RETURN CODE
*                          5 BYTE CHARACTER STRING FOR DSCB ADDRESS
*
*                  VDSCB3 - LINKS TO IEHLIST
*
*              RETURN CODES -
*                  0 - NORMAL RETURN
*                  1 - END OF JOB NO LINK TO IEHLIST
*                  2 - END OF VOLUME NO DSCB RETURNED
*              -2 - END OF VOLUME GOOD DSCB RETURNED
*              3 - END OF JOB LINK TO IEHLIST DESIRED
*              4 - VOLUME NOT MOUNTED
*              8 - DSCB NOT FOUND
*              12 - I/O ERROR
*
         ENTRY VDSCB2
         ENTRY VDSCB3
         SAVE  (14,12),,*
         LA    8,HERE-VDSCB1(15) SET UP ROUTINE ADDR
         B     BEGIN-VDSCB1(15)    GO TO INITIALIZATION
VDSCB3   SAVE  (14,12)
         LA    8,V3-VDSCB3(15)     SET UP ROUTINE ADDR
         B     BEGIN-VDSCB3(15)  GO TO INITIALIZATION
VDSCB2   SAVE  (14,12)
         LA    8,V2-VDSCB2(15)     SET UP ROUTINE ADDR
BEGIN    BALR  3,0              BEGIN INITIALIZATION
         USING *,3
         ST    13,SAVE+4
         LR    12,13
         LA    13,SAVE
         ST    13,8(,12)
         LR    4,1
         BR    8             GO TO PROCESSING ROUTINE
HERE     L     2,0(,4)     LOAD ADR OF STRING DV INTO REG 2
         L     2,0(,2)       LOAD POINTER TO STRING
         L     10,4(,4)   LOAD ADDR OF CODE
         CLI   SW1,C'1'      IS THIS FIRST TIME ON THIS VOLUME?
         BE    TRYAGAIN      NO, GO TO PROCESS
         MVI   SW1,C'1'      INDACATE WE HAVE BEEN HERE
         CLI   SW3,C'1'      IS THIS FORST TIME THRU HERE FOR THIS RUN
         BE    AGAIN          NO , GO TO PROCESS
         MVI   SW3,C'1'   INDICATE THIS IS NOT FIRST TIME THRU
         EXTRACT IDEX28,FIELDS=(TIOT)  GET TIOT ADDR
AGAIN    L     15,IDEX28     PICK UP OFFSET FROM TIOT
         TM    24(15),X'FF'  IS THIS LAST ENTRY IN TIOT
         BZ    TIOEND        YES,END PROCESSING
         SR    7,7
         IC    7,24(,15)     PICK UP LENGTH OF TIOT ENTRY
         AR    7,15          ADD THIS TO CURRENT OFFSET
         ST    7,IDEX28      SAVE FOR NEXT TIME
         TM    25(15),X'10'  IS THIS JOBLIB ENTRY?
         BO    AGAIN         YES , IGNORE
         TM    27(15),X'02'        IS THIS JES2 DATA SET
         BO    AGAIN               YES,IGNORE
         CLC   28(8,15),=CL8'STEPLIB'
         BE    AGAIN
         CLC   28(8,15),=CL8'JOBCAT'
         BE    AGAIN
         CLC   28(8,15),=CL8'STEPCAT'
         BE    AGAIN
         CLC   28(4,15),=CL4'PGM='  IS THIS ENTRY FOR COMPILE,LINK,GO
         BE    AGAIN          YES, IGNORE
         CLC   28(8,15),=CL8'OUT'      IS THIS ERROR ENTRY
         BE    AGAIN              YES,IGNORE
         CLC   28(8,15),=CL8'SOUT'      IS THIS SORT EXIT ?
         BE    AGAIN         YES,IGNORE
         CLC   28(8,15),=CL8'PRINT'     IS THIS ENTRY FOR PRINT
         BE    AGAIN         YES, IGNORE
         CLC   28(8,15),=CL8'SYSPRINT' IS THIS ENTRY FOR SYSPRINT
         BE    AGAIN         YES, IGNORE
         CLC   28(8,15),=CL8'SYSIN   ' IS THIS ENTRY OOR SYSIN
         BNE   NOLINK        N , LINK TO IEHLIST NOT REQUIRED
         MVI   SW2,C'1'      YES, INDICATE LINK TO IEHLIST NEEDED
         B     AGAIN         IGNORE SYSIN DNTRY
NOLINK   L     5,40(,15)     GET UCB ADDR
         TM    18(5),X'20'   IS IT DASD
         BZ    AGAIN               NO,IGNORE
         MVC   VOLNAME,28(5)       GET VOL SERIAL FROM UCB
         LH    7,24(,5)            GET REL TRK-ADR. OF VTOC
         MVC   VTOCADR+4(1),26(5)  GET RECORD-NO ON TRK
         SR    15,15
         IC    15,19(,5)     LOAD DEVICE TYPE INDICATOR
         LA    6,TABLE-2           SET UP TABLE ADDRESS - 2
         SLA   15,1                MULTIPLY BY 2 (TABLE LENGTH)
         LH    14,0(15,6)    GET NO. TRKS PER CYL FROM TABLE
         SR    6,6
         DR    6,14          DIVIDE TO  GET ACTUAL CYL ADDR
         STH   7,VTOCADR     QUOTIONT IS CC OF CCHHR
         STH   6,VTOCADR+2   REMAINDER IS HH OF CCHHR
         STH   R14,R14SAV
         OBTAIN DSCBC        GET FORMAT 4 DSCB
         LH    R14,R14SAV
         ST    15,0(10)      RETURN CODE TO CALLER
         LTR   15,15         IS CODE = 0
         BNZ   RET1          NO, RETURN
         STH   R14,TRK+2          SET NR OF TRACKS PER CYLINDER
         MVC   VTCHOLD,VTOCADR       SAVE VTOCADR
         MVC    LSTDSCB,WKAREA+45     SAVE ADR FF LAST DSCB ON VOL CCHH
         MVC   REC+3(1),WKAREA+74  GET NO. VTOC REDDS PER TRK
         MVC   WKAREA+76(6),VOLNAME    PUT VO. SER IN UNUSED PORTION
RET3     CLC   VTCHOLD,LSTDSCB      LAST DSCB ON VOL
         BNE   RET           NO, OORMAL RETURN
LAST     MVC   0(4,10),=F'2' SET LAST DSCB CODE
RET1     MVI   SW1,C'0'      SET END OF VOLUME
RET      MVC   0(140,2),WKAREA         PASS DSCB TO CALLER
RET2     EQU   *
         L     13,SAVE+4
         RETURN (14,12)
TIOEND   MVC   0(4,10),=F'1' SET END OF JOB FOR CALLER
         CLI   SW2,C'1'      IS LINK TO IEHLIST REQUIRED?
         BNE   RET2          NO, RETURN
         MVI   3(10),X'03'   YES, INDICATE TO CALLER
         B     RET2
V3       LINK  EP=IEHLIST,PARAM=(LIST),VL=1
         B     RET2
TRYAGAIN SR    11,11
         IC    11,VHLDREC    GET LAST PROCESSED RECORD ADDR
         LA    11,1(,11)      ADD 1 TO IT
         CL    11,REC        HAVE WE EXCEEDED TRK
         BNH   NOBUMP1       NO, OK
         LH    12,VHLDTRK    PICK UP LAST TRK ADDR
         LA    12,1(,12)     ADD 1 TO IT
         LA    11,1          SET RECORD ADDR TO 1
         CL    12,TRK        HAVE WE EXCEEDED CYL
         BL    NOBUMP2              NO, OK
         SR    12,12         YES, SET TRK TO 0
         LH    14,VHOLCYL    GET CYL ADDR
         LA    14,1(,14)     ADD 1 TO IT
         STH   14,VHOLCYL     SOORE MODDFIED CYL ADDR
NOBUMP2  STH   12,VHLDTRK    STORE MODIFIED TRK ADDR
NOBUMP1  STC   11,VHLDREC    STORE MODIFIED RECORD ADDR
NOBUMP   MVC   VTOCADR,VTCHOLD    MOVE NEW CCHHR TO PARM LIST
         OBTAIN DSCBC        GET NEXT DSCB
         ST    15,0(10)      PASS CODE TO CALLER
         CLI   WKAREA+44,C'5'  IS IT FORMAT 5?
         BE    RET           YES RETURN
         CLC   VTCHOLD,LSTDSCB     IS IT LAST ONE
         BNL   GOODLAST      YES GO SEE IF IT IS GOOD
         C     15,=F'8'       IS RETURN CODE 0
         BE    TRYAGAIN   YES , WE GOT FORMAT 0. GET NEXT DSCB
         LTR   15,15         IS RETURN CODE 0
         BNZ   RET1          NO, RETURN
         CLI   WKAREA+44,C'1'     WAS IT FORMAT 1
         BNE   TRYAGAIN      NO, GET ANOTHER
         BC    15,RET3       RETURN
GOODLAST LTR   15,15  WAS RETURN CODE 0
         BNZ   LAST     NO, NOT GOOD READ
         CLI   WKAREA+44,C'1'   IS IT FORMAT 1
         BNE   LAST NO, DON2T PROCESS IT
         MVC   0(4,10),=F'-2' YES, TELL CALLER TO PROCESS IT
         B     RET1        RETURN
V2       L     6,8(4)        LOAD ADDR OF DSCB PTR DOPE VECTOR
         L     2,0(,4)     LOAD ADR OF STRING DV INTO REG 2
         L     2,0(,2)       LOAD POINTER TO STRING
         L     10,4(,4)   LOAD ADDR OF CODE
         L     6,0(6)         GET ADDR OF DESIRED DSCB ADDR
         MVC   VTOCADR,0(6)       MOVE DSCB ADDR TO PARM LIST
         OBTAIN DSCBC        G5T THE DSCB
         ST    15,0(10)      PASS CODE TO  CALLER
         LTR   15,15         WAS IT GOOD READ
         BNZ   RET1   NO , END PROCESSING ON THOS VOLUME
         B     RET       RETURN
         DS    0D
WKAREA   DS    CL350
DSCBC    CAMLST  SEEK,VTOCADR,VOLNAME,WKAREA
         CNOP  2,4
LIST     DC    XL2'0000'
         CNOP  2,4
TABLE    DS    0H                  UCBTYP
         DC    H'00'               X'01'
         DC    H'00'               X'02'
         DC    H'00'               X'03'
         DC    H'00'               X'04'
         DC    H'00'               X'05'
T2305M1  DC    H'00'               X'06'
T2305M2  DC    H'00'               X'07'
T2314    DC    H'20'               X'08'
T3330M1  DC    H'19'               X'09'
T3340    DC    H'00'               X'0A'
         DC    H'00'               X'0B'
         DC    H'00'               X'0C'
T3330M11 DC    H'19'               X'0D'
         DC    H'00'               X'0E'
         DC    H'00'               X'0F'
SW1      DC    C'0'
SW2      DC    C'0'
SAVE     DS    18F
REC      DC    F'0'
TRK      DC    F'0'
IDEX28   DC    F'0'
VTCHOLD  DS    CL5
         ORG   VTCHOLD
VHOLCYL  DC    XL2'0000'
VHLDTRK  DC    XL2'0000'
VHLDREC  DC    XL1'00'
SW3      DC    C'0'
         DS    0H
VTOCADR  DS    CL5
LSTDSCB  DS    CL5
VOLNAME  DS    CL6
R14SAV   DS    H
R14      EQU   14
         LTORG
         END
