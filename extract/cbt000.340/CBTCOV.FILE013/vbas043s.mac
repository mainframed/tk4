//Z223253D JOB (6600,253,1),GRAB,MSGCLASS=A,CLASS=B,TIME=1
//LUDATE EXEC ASMFCL,PARM.LKED='MAP,LIST,LET,RENT'
//ASM.SYSIN DD *
TIMEDATE CSECT
*
         ENTRY TIME,DATE,TDATE
*
TIME     B     10(0,15)                ENTRY FOR TIME ONLY
         DC    X'04'
         DC    C'TIME'
         DC    C' '
         STM   14,12,12(13)
         XR    2,2                     CLEAR AND
         LA    3,X'F0'                 LOAD SWITCH
         L     4,0(0,1)                ARG ADDR
         B     ENTPNT-TIME(15)
*
DATE     B     10(0,15)                ENTRY FOR DATE ONLY
         DC    X'04'
         DC    C'DATE'
         DC    C' '
         STM   14,12,12(13)
         LA    2,X'F0'                 LOAD SWITCH
         L     5,0(0,1)                ARG ADDR
         B     ENTPNT-DATE(15)
*
TDATE    B     10(0,15)                ENTRY FOR BOTH, TIME AND DATE
         DC    X'05'
         DC    C'TDATE'
         STM   14,12,12(13)
         XR    2,2                     CLEAR
         XR    3,3                     SWITCHES
         LM    4,5,0(1)                ARG ADDR'S
*
ENTPNT   BALR  6,0                     COMMON ROUTINE, LOAD BASE REG
         USING ENTPNT+2,6
         LA    0,DSECTEND-DSECTANF     GET LENGTH SVA
         BAL   1,*+4                   INDICATE GETMAIN
         SVC   10                      ISSUE GETMAIN SVC
         XC    0(3*4,1),0(1)           ZERO CHAIN ADDRESSES
         LA    0,DSECTEND-DSECTANF     GET LENGTH SVA
         STH   0,2(1)                  STORE LENGTH
         ST    1,8(0,13)               DOWN AND
         ST    13,4(0,1)               UP CHAIN
         LR    13,1                    LOAD SA REG
         L     1,4(0,13)               GET HIGH SA
         LM    0,1,20(1)               RESTORE REGS 0 AND 1
         USING SAVEAREA,13
         XC    REG-2(2),REG-2
         NI    SW,X'0'                 SET BRANCH-SWITCH ZERO
         MVC   DAYTB,=PL2'31,28,31,30,31,30,31,31,30,31,30,31'
         TIME  DEC                     (0)='HHMMSSTH', (1)='00YYDDDZ'
         EX    2,*+4
         NOP   SKIP                    BRANCH ON 'NO TIME'
*     TIME ROUTINE
         ST    0,REG
         MVI   3(4),X'0F'              SUPPLY ZONE
         MVO   0(4,4),REG(3)           IGNORE 10TH AND 100TH
         UNPK  1(7,4),0(4,4)           UNPACK
         MVC   0(2,4),2(4)             SHIFT
         MVC   3(2,4),4(4)             AND
         MVI   2(4),C','               SET
         MVI   5(4),C','               SEPARATORS
         EX    3,*+4
         NOP   EXIT                    BRANCH ON 'NO DATE'
*     DATE ROUTINE
SKIP     ST    1,REG
         MVC   2(8,5),MASK             SUPPLY SEPERATORS, COUNT, ZONE
         MVO   8(2,5),REG+1(1)         GET YEAR
         TM    SW,X'01'
         BO    YEAR
         OI    SW,X'01'
         MVC   REG(2),8(5)             YEAR
         DP    REG-1(3),PFOUR          DIVIDE BY FOUR
         TM    REG+1,X'30'             REMAINDER ZERO
         BNZ   YEAR
         OI    DAYTB+3,X'10'           YES, FEBRUARY 29 DAYS
YEAR     UNPK  8(2,5),8(2,5)           UNPACK YEAR
         LA    2,DAYTB                 TABLE OF DAYS
SEARCH   AP    3(2,5),PONE             COUNT OF MONTHS
         SP    REG+2(2),0(2,2)         SUBTRACT
         BNP   MONTH                   ZERO OR NEGATIVE
         LA    2,2(0,2)                NO, GET NEXT MONTH
         B     SEARCH                  AND LOOP
MONTH    AP    REG+2(2),0(2,2)         RESTORE
         OI    4(5),X'0F'              SET
         OI    REG+3,X'0F'             ZONES
         UNPK  3(2,5),3(2,5)           UNPACK MONTH
         UNPK  0(2,5),REG+2(2)         AND DAY
*
EXIT     L     13,4(0,13)              RELOAD SA REG
         L     1,8(0,13)               GET LOW SA ADDR
         LH    0,2(1)                  GET LENGTH
         LA    1,0(1)                  CLEAR THE HIGH ORDER BYTE
         SVC   10                      ISSUE FREEMAIN SVC
         LM    14,12,12(13)            RESTORE REGS
         SPM   14                      AND PGM MASK
         MVI   12(13),X'FF'            RETURN INDICATOR
         SR    15,15
         BR    14                      RETURN
*
*
MASK     DS    0CL8
         DC    C'.'                    SEPERATOR
         DC    X'000C'                 PACKED ZERO
         DC    C'.19'
         DC    X'000F'                 ZONE
PONE     DC    PL1'+1'
PFOUR    DC    PL1'+4'
         LTORG
*
DSECTANF DSECT
SAVEAREA DS    18F
         DS    F
REG      DS    F
DAYTB    DS    CL24
SW       DS    CL1
DSECTEND EQU   *
         END
//LKED.SYSLMOD DD DSN=LU.SBRLIB,DISP=SHR
//LKED.SYSIN DD *
  ALIAS TIME,DATE,TDATE
  NAME LUDATE(R)
