ZS905VAB CSECT
         REG
         XSAVE (R12),SVA,ZS905VAB ZIMPELMANN
         L     R3,0(R1)
         LA    R3,0(R3)               LOESCHEN VL-BIT
         USING Y1,R3
         L     R5,Y1TADR
         AH    R5,Y1ESIZ
         ST    R5,TABADR               ADRESSE DER BLOCKTABELLE
         L     R4,0(R5)
         L     R9,4(R5)
         CR    R4,R9
         BNE   NOFEHL
         LA    R9,54                  KEINE BLKNR IN TABELLE
         B     FEHLER
NOFEHL   EQU   *
         LA    R7,LTAB
         STH   R7,FLAE
         SR    R9,R4
         SR    R8,R8
         DR    R8,R7
         STH   R9,FANZ
         MVC   ASPA1,=H'1'        1.SORTIERBEGRIFF: BLKVON,BLKBIS
         MVC   ESPA1,=H'8'
         MVC   ASPA2,=H'13'       2.SORTIERBEGRIFF: VALUE-ADRESSE
         MVC   ESPA2,=H'16'
         MVC   ASPA3,=H'9'        3.SORTIERBEGRIFF: OFFVON,OFFBIS
         MVC   ESPA3,=H'12'
         L     R5,0(R5)                LADEN ADRESSE 1.EINTRAG
         LINK  EP=LUSORT,PARAM=((R5),PARM),VL
         LR    R4,R5
         USING BLSOTAB,R4
         MVC   TAB1IND,=H'1'
         MVC   BLKNR,BLKVON
NEXTBLK  EQU   *
         ST    R4,TABADR
         MVC   OFFZVON,OFFVON
         MVC   OFFZBIS,OFFBIS
         LA    R7,0
         LH    R2,TAB1IND          LAUFINDEX AUF ANFANGSWERT SETZEN
BLKVTEST EQU   *
         L     R1,VALADR
         LTR   R1,R1               CHANGEBEFEHL ???????????
         BZ    LASTELEM
         MVI   Y1CHNG,Y1CHYE
         LA    R7,1(R7)
LASTELEM EQU   *
         CR    R2,R9                 TAB2IND = FELDANZAHL ?
         BE    BLKPRT                JA
         LA    R4,LTAB(R4)
         CLC   BLKVON,BLKNR
         BH    BLKPRT
         LA    R2,1(R2)
         SR    R1,R1
         STH   R1,OFFZVON
         MVC   OFFZBIS,Y1BLKS
         CLC   BLKVON,BLKNR           BLKVON < BLKNR
         BE    BLKVTEST
         XC    VALADR,VALADR   VALUE-EINTR LOESCHEN BEI OLDVAL-FEHLER
         B     LASTELEM
         SPACE 2
BLKPRT   EQU   *
         STH   R7,VALIND               SPEICHERN CHANGEANZAHL/BLOCK
         STH   R2,TAB2IND
         MVC   Y1RBLK,BLKNR
         MVI   Y1DATA,Y1READ           KZ FUER READ
         CALL  ZS905RWD,((R3)),VL
         LH    R1,Y1MSGC
         LTR   R1,R1
         BNZ   FCODLOE
         MVC   UPDATE,=11C' '
         BAL   R15,PRTDMP
         XC    Y1RBLK,Y1RBLK
         LH    R1,VALIND
         LTR   R1,R1
         BZ    TABPRUEF
         BAL   R15,CHNGROUT
TABPRUEF EQU   *
         LH    R6,TAB1IND
         LH    R7,TAB2IND
         L     R5,BLKNR
         L     R4,TABADR
         SR    R7,R6
         LA    R7,1(R7)
BLKLOE   EQU   *
         CLC   BLKBIS,BLKNR
         BH    NEXTELEM
         LA    R6,1(R6)
         LA    R4,LTAB(R4)
         BCT   R7,BLKLOE
         CH    R9,TAB2IND
         BE    ENDE
         MVC   BLKNR,BLKVON
         B     VABENDE
NEXTELEM EQU   *
         LA    R5,1(R5)
         ST    R5,BLKNR
VABENDE  EQU   *
         STH   R6,TAB1IND
         B     NEXTBLK
         EJECT
CHNGROUT EQU   *
         ST    R15,CHNGADR
         STH   R9,TABELEM
         LH    R9,VALIND
         L     R4,TABADR
GETVALAD EQU   *
         L     R7,VALADR
         XC    VALADR,VALADR
         LA    R4,LTAB(R4)
         LTR   R7,R7
         BZ    GETVALAD
         LH    R6,0(R7)                GET OFFSET
         LH    R5,2(R7)                GET LAENGE
         LA    R7,4(R7)
         L     R8,Y1ADDB
         LA    R8,0(R6,R8)
         BCTR  R5,0
         EX    R5,EXCLC1
         BE    OLDVALOK
         MVI   Y1AEND,Y1AEYE    KEINE ICD-AUSGABE MEHR
         LA    R9,60           OLD-VALUE-EINTRAG NICHT IM OFFSET
         B     FEHLER
OLDVALOK EQU   *
         LA    R7,1(R5,R7)
         EX    R5,EXMVC4
         BCT   R9,GETVALAD
         MVC   Y1WBLK,BLKNR
         MVI   Y1DATA,Y1WRIT         KZ FUER WRITE
         CALL  ZS905RWD,((R3)),VL
         LH    R1,Y1MSGC
         LTR   R1,R1
         BNZ   FCODLOE
         MVC   Y1RBLK,Y1WBLK
         SR    R1,R1
         ST    R1,Y1WBLK
         XC    VALIND,VALIND
         MVC   UPDATE,=C'NACH UPDATE'
         BAL   R15,PRTDMP
OLDVALFE EQU   *
         MVI   Y1FUEB,Y1FNO
         XC    Y1RBLK,Y1RBLK                READ-BLKNR LOESCHEN
         LH    R9,TABELEM
         L     R15,CHNGADR
         BR    R15
         EJECT
PRTDMP   EQU   *
         ST    R15,PRTADR
         STH   R9,SVR9
         LH    R1,VALIND
         LTR   R1,R1
         BZ    NOUPDAT
         MVC   UPDATE,=C'VOR UPDATE '
NOUPDAT  EQU   *
         BAL   R15,KOPF
         L     R6,Y1RBLK
         MH    R6,Y1BLKS         BERECHNEN RBA
         ST    R6,RBAZWI         RBA ZWISCHENSPEICHERN
         LH    R7,OFFZVON
         LH    R8,OFFZBIS
         SR    R8,R7             LAENGE DES ZU DRUCKENDEN BEREICH
         L     R9,Y1ADDB
         LA    R9,0(R7,R9)
         LH    R5,=H'32'
         CLI   Y1TFLG,Y1TYES
         BNE   NAEZEIL
         LH    R5,=H'16'         LAENGE BEI TERM=YES
NAEZEIL  EQU   *
         LTR   R8,R8
         BZ    PRTENDE
         STH   R7,ZWIOFF
         UNPK  OFFFLD(5),ZWIOFF(3)
         NC    OFFFLD,=4XL1'0F'       ZONENTEIL LOESCHEN
         TR    OFFFLD,TRTAB1
         MVC   OFFSET,OFFFLD
         L     R6,RBAZWI
         AR    R6,R7
         ST    R6,ZWIRBA
         UNPK  HEXAFLD(9),ZWIRBA(5)
         NC    HEXAFLD,=8XL1'0F'       ZONENTEIL LOESCHEN
         TR    HEXAFLD,TRTAB1
         MVC   RBAFELD,HEXAFLD
         LR    R10,R5
         CR    R8,R5
         BNL   ZEILFUEL
         LR    R10,R8
ZEILFUEL EQU   *
         LA    R1,DATAX+36             MITTE ODER ENDE(TERM) MERKEN
         LA    R6,DATAX
         LR    R2,R10
         BAL   R15,DRCKAUFB
         SR    R8,R10
         CP    ZEIZAE,Y1LCNT           MAX. ZEILENANZAHL ERREICHT
         BL    NOKOPF
         BAL   R15,KOPF
NOKOPF   EQU   *
         CALL  ZS905WRF,((R3),EZEIL),VL
         AP    ZEIZAE,=P'+1'
         MVI   EZEIL,C' '
         MVC   EZEIL+1(L'EZEIL-1),EZEIL
         AR    R7,R5
         B     NAEZEIL
PRTENDE  EQU   *
         LH    R9,SVR9
         L     R15,PRTADR
         BR    R15
         EJECT
KOPF     EQU   *
         ST    R15,KOPFADR
         CLI   Y1VORS,Y1NOV     BLOCK/PRO SEITE
         BNE   KOPFDRU
         MVC   PKFLD,PKFLD1     PACKFLD=10
         AP    PKFLD,ZEIZAE     PKFLD=ZEILENZAEHLER+10
         CP    PKFLD,Y1LCNT
         BL    NOKOPF1
KOPFDRU  EQU   *
         CALL  TDATE,(ZEIT,DATUM),VL
         AP    Y1SEIZ,=P'+1'
         MVC   SEITNR,MASKE2
         ED    SEITNR,Y1SEIZ
         L     R15,Y1ADAT
         MVC   DATASET,0(R15)
         ZAP   ZEIZAE,=P'+2'
         CALL  ZS905WRF,((R3),ZEIL1)
         CALL  ZS905WRF,((R3),ZEIL2)
*                              BLOCKNR DEZIMAL
NOKOPF1  EQU   *
         MVC   RECDEZ,MASKE1
         L     R11,Y1RBLK
         CVD   R11,DOUBLE
         ED    RECDEZ,DOUBLE+3
*                              BLOCKNR HEXA
         ST    R11,DOUBLE+4
         UNPK  HEXAFLD(9),DOUBLE+4(5)
         NC    HEXAFLD,=8XL1'0F'
         TR    HEXAFLD,TRTAB1
         MVC   RECHEX,HEXAFLD
         CALL  ZS905WRF,((R3),ZEIL3)
         CALL  ZS905WRF,((R3),ZEIL4)
         CALL  ZS905WRF,((R3),ZEIL5)
         AP    ZEIZAE,=P'+7'
         L     R15,KOPFADR
         BR    R15
         EJECT
DRCKAUFB EQU   *
         BCTR  R2,0
         LH    R11,=H'4'
         CLI   Y1TFLG,Y1TYES
         BNE   NOTERM
         MVI   STERNT1,C'*'
         MVI   STERNT2,C'*'
         EX    R2,EXMVC3
         EX    R2,EXTR3
         LA    R2,1(R2)
         B     HEXALOOP
NOTERM   EQU   *
         MVI   STERN1,C'*'
         MVI   STERN2,C'*'
         EX    R2,EXMVC1
         EX    R2,EXTR1
         LA    R2,1(R2)
HEXALOOP EQU   *
         LTR   R2,R2
         BZ    AUFBEND
         CR    R2,R11
         BL    HEXEND
         MVC   OFFFLD,0(R9)       NAECHSTEN 4 STELLEN ABSPEICHERN
         UNPK  DOUBLE(9),OFFFLD(5)
         NC    DOUBLE,=8XL1'0F'
         TR    DOUBLE,TRTAB1
         MVC   0(8,R6),DOUBLE
         LA    R6,9(R6)            9 STELLEN (1BLANK) WEITER
         LA    R9,0(R11,R9)
         SR    R2,R11
         CR    R6,R1               DRUCKMITTE ERREICHT  ?????????
         BNE   HEXALOOP
         CLI   Y1TFLG,Y1TYES
         BE    AUFBEND
         LA    R6,3(R6)
         B     HEXALOOP
         SPACE 2
HEXEND   EQU   *
         BCTR  R2,0
         EX    R2,EXMVC5           REST IN OFFFLD SPEICHERN
         LA    R2,1(R2)            UM 1 ERHOEHEN
         LR    R11,R2
         SLL   R2,1                MAL 2
         SLL   R2,4                1.LAENGENSCHLUESSEL FUER EXUNPK1
         OR    R2,R11              2.LAENGENSCHLUESSEL ANGEHAENGT
         EX    R2,EXUNPK1
         SRL   R2,4                GET 1.LAENGENSCHLUESSEL
         BCTR  R2,0
         EX    R2,EXNC1
         EX    R2,EXTR2
         EX    R2,EXMVC2
AUFBEND  EQU   *
         BR    R15
         EJECT
FEHLER   EQU   *
         STH   R9,Y1MSGC
         CALL  ZS905FHL,((R3)),VL
FCODLOE  EQU   *
         XC    Y1MSGC,Y1MSGC
         LA    R1,60
         CR    R1,R9              BEI OLDVALUE-FEHLER WEITERMACHEN
         BE    OLDVALFE
ENDE     EQU   *
         XRETURN 0
         EJECT
PARM     DS    0CL12
FANZ     DC    H'0'
FLAE     DC    H'0'
ASPA1    DC    H'0'
SORTA1   DC    H'+1'
ESPA1    DC    H'0'
ASPA2    DC    H'0'
SORTA2   DC    H'+1'
ESPA2    DC    H'0'
ASPA3    DC    H'0'
SORTA3   DC    H'+1'
ESPA3    DC    H'0'
PARMEND  DC    H'0'
         SPACE 2
TABADR   DS    F
BLKNR    DS    F
PRTADR   DS    F
CHNGADR  DS    F
KOPFADR  DS    F
ZWIRBA   DS    F
RBAZWI   DS    F
         DS    CL1
VALIND   DS    H
TAB1IND  DS    H
TAB2IND  DS    H
OFFZVON  DS    H
OFFZBIS  DS    H
TABELEM  DS    H
SVR9     DS    H
ZWIOFF   DS    H
         DS    CL1
OFFFLD   DC    CL4' '
         DS    CL1
PKFLD1   DC    PL3'10'
PKFLD    DC    PL3'0'
ZEIZAE   DC    PL3'999'
MASKE1   DC    XL10'40212020202020202020'
MASKE2   DC    XL6'402020202120'
DOUBLE   DS    D
         DC    CL1' '
HEXAFLD  DS    CL8
         DC    CL1' '
TRTAB1   DC    CL16'0123456789ABCDEF'
EXMVC1   MVC   DATACH(0),0(R9)
EXMVC2   MVC   0(0,R6),DOUBLE
EXMVC3   MVC   DATACHT(0),0(R9)
EXMVC4   MVC   0(0,R8),0(R7)
EXMVC5   MVC   OFFFLD(0),0(R9)
EXTR1    TR    DATACH(0),TAB1
EXTR2    TR    DOUBLE(0),TRTAB1
EXTR3    TR    DATACHT(0),TAB1
EXCLC1   CLC   0(0,R7),0(R8)
EXUNPK1  UNPK  DOUBLE(0),OFFFLD(0)
EXNC1    NC    DOUBLE(0),=8XL1'0F'
TAB1     DC    256CL1'.'
         ORG   TAB1+C' '
         DC    CL1' '
         ORG   TAB1+C'A'
         DC    CL9'ABCDEFGHI'
         ORG   TAB1+C'J'
         DC    CL9'JKLMNOPQR'
         ORG   TAB1+C'S'
         DC    CL8'STUVWXYZ'
         ORG   TAB1+C'0'
         DC    CL10'0123456789'
         ORG
         EJECT
*     ZEILEN-DEFINITIONEN
ZEIL1    DS    0CL133
         DC    CL1'1'
         DC    C'ZS905 L01     DB-DUMP-UTILITY     '
DATUM    DC    CL10' '
         DC    CL5' '
ZEIT     DC    CL8' '
         DC    C'     SEITE'
SEITNR   DC    CL6' '
         DC    CL59' '
         SPACE 2
ZEIL2    DS    0CL133
         DC    CL1' '
         DC    CL20' '
         DC    C'DATASET: '
DATASET  DC    CL44' '
         DC    CL59' '
         SPACE 2
ZEIL3    DS    0CL133
         DC    CL1'-'
         DC    CL20' '
         DC    C'DEC.'
         DC    CL13' '
         DC    C'HEX.'
         DC    CL90' '
         SPACE 2
ZEIL4    DS    0CL133
         DC    CL1' '
         DC    C'BLOCK/CI-NR.: '
RECDEZ   DC    CL10' '
         DC    CL10' '
RECHEX   DC    CL8' '
         DC    CL10' '
UPDATE   DC    CL11' '
         DC    CL69' '
         SPACE 2
ZEIL5    DS    0CL133
         DC    CL1'-'
         DC    C'RBA      OFFSET/BLOCK'
         DC    CL111' '
         SPACE 2
EZEIL    DS    0CL133
         DC    CL1' '
RBAFELD  DC    CL8' '
         DC    CL2' '
OFFSET   DC    CL4' '
         DC    CL2' '
DATAX    DC    CL74' '
         DC    CL5' '
STERN1   DC    CL1' '
DATACH   DC    CL32' '
STERN2   DC    CL1' '
         ORG   DATAX
DATAXT   DC    CL35' '
         DC    CL5' '
STERNT1  DC    CL1' '
DATACHT  DC    CL16' '
STERNT2  DC    CL1' '
         DC    CL55' '
         DC    CL4' '
         LTORG
         SPACE 1
BLSOTAB  DSECT
BLKVON   DS    F
BLKBIS   DS    F
OFFVON   DS    H
OFFBIS   DS    H
VALADR   DS    F
BTABEND  DS    0CL1
LTAB     EQU   *-BLSOTAB
         EJECT
Y1       ZS905COM
         END
