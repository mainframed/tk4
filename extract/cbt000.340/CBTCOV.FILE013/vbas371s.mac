IEECVXIT START
         XSAVE R12,,IEECVXIT,WORKL
         USING WORK,R13
         USING MSGS,R11
         L     R1,0(R1)
         LA    R1,0(R1)
         LR    R4,R1
         BCTR  R1,0
*
*
*    THIS SECTION IS DESIGNED FOR SUPPORT OF A SPECIAL, SEPERATED
*    DEVICE-GROUP. ALL DEVICES ASSOCIATED WITH THIS POOL WILL BE
*    ADDRESSED WITH A ROUTING-CODE OF 15.
*
*
         LA    R11,DOSPLMSG
         SR    R2,R2
NXTDOS   EQU   *
         IC    R2,0(R11)
         SR    R3,R3
         IC    R3,1(R11)
         LA    R3,0(R3,R1)
         EX    R2,CLCEX2
         BE    FNDDOS
         BL    DOSEND
         LA    R11,4(R2,R11)
         CLI   0(R11),X'FF'
         BE    DOSEND
         B     NXTDOS
FNDDOS   EQU   *
         LA    R10,DOSPOOL
         SR    R3,R3
         IC    R3,2(R11)
         SLL   R3,25
         SRL   R3,25
         LA    R3,0(R3,R1)
NXTADR   EQU   *
         CLC   0(3,R3),0(R10)
         BE    DOSCHNG
         BL    TSTNXT
         LA    R10,3(R10)
         CLI   0(R10),X'FF'
         BNE   NXTADR
TSTNXT   EQU   *
         TM    2(R11),X'80'
         BNO   DOSEND
         LA    R11,4(R2,R11)
         B     FNDDOS
DOSCHNG  EQU   *
         OI    129(R4),X'02'  OR ROUTCDE=15
         B     RETURN
DOSEND   EQU   *
*
*
*
         CLC   1(3,R1),=CL3'OCR'
         BE    DOSCHNG
         CLC   1(7,R1),=CL7'IEA911I'
         BE    SPDUMP
         CLC   1(7,R1),=CL7'IEE362A'
         BE    SOPZEIT
         CLC   1(7,R1),=CL7'IEE043I'
         BE    SWTRLOG
         CLC   1(7,R1),=CL7'IEF176I'
         BE    PWTRLOG
         LA    R11,MSGTAB
         SR    R2,R2
NEXTMSG  EQU   *
         IC    R2,MSGIDLEN
         EX    R2,CLCEX1
         BE    FOUND
         BL    RETURN
         LA    R11,11(R2,R11)
         CLI   MSGIDLEN,X'FF'
         BNE   NEXTMSG
RETURN   EQU   *
         XRETURN 0,R
FOUND    EQU   *
         LA    R1,1(R1)
         LA    R11,1(R2,R11)
         TM    MSGSWT,VGLROUT
         BNO   TSVGLDSC
         MVC   VERGL,128(R1)
         NC    VERGL,MSGVROUT
         BZ    RETURN
TSVGLDSC EQU   *
         TM    MSGSWT,VGLDESC
         BNO   OK
         MVC   VERGL,132(R1)
         NC    VERGL,MSGVDESC
         BZ    RETURN
OK       EQU   *
         TM    MSGSWT,ALTROUT
         BNO   TSTDESC
         MVC   128(2,R1),MSGROUT
TSTDESC  EQU   *
         TM    MSGSWT,ALTDESC
         BNO   TSTSWT
         MVC   132(2,R1),MSGDESC
TSTSWT   EQU   *
         TM    MSGSWT,SWT1314
         BNO   RETURN
         LR    R0,R12
         LA    R15,SVROUT
         SVC   246
         B     RETURN
SVROUT   EQU   *
         LR    R12,R0
         XC    ROUT1,ROUT2
         XC    ROUT2,ROUT1
         XC    ROUT1,ROUT2
         MVC   128(2,R1),ROUT1
         BR    R14
SPDUMP   EQU   *
         LA    R1,PDUMP
CMD      EQU   *
         SR    R0,R0
         SVC   34
         B     RETURN
PWTRLOG  EQU   *
         CLI   41(R1),C'L'
         BNE   RETURN
         MVC   COMMD,PCMD
         MVC   COMMD+7(3),13(R1)
         SR    R0,R0
         LA    R1,COMMD
         SVC   34
         MVC   COMMD,SHCPY
         L     R1,16
         L     R1,196(R1)
         MVC   COMMD+20(2),16(R1)
         LA    R1,COMMD
         B     CMD
SOPZEIT  EQU   *
         L     R1,16
         L     R1,196(R1)
         MVC   COMMD,OPZEIT
         MVC   COMMD+18(2),16(R1)
         LA    R1,COMMD
         B     CMD
SWTRLOG  EQU   *
         SR    R0,R0
         LA    R1,DTCMD
         SVC   34
         L     R1,16
         L     R1,196(R1)
         MVC   COMMD,WTRLOG
         MVC   COMMD+18(2),16(R1)
         LA    R1,COMMD
         B     CMD
DTCMD    DS    0F
         DC    H'9,0'
         DC    C' D T '
WTRLOG   DS    0F
         DC    H'22,0'
         DC    CL18'S  WTRLOG,SID=XX'
PDUMP    DS    0F
         DC    H'13',H'0'
         DC    C' S PDUMP '
OPZEIT   DS    0F
         DC    H'22,0'
         DC    CL18'S  OPZEIT,SID=XX'
PCMD     DS    0F
         DC    H'11,0'
         DC    CL7' P CUU '
SHCPY    DS    0F
         DC    H'23,0'
         DC    CL19' S HARDCOPY,SID=XX '
CLCEX1   CLC   1(0,R1),MSGIDENT
CLCEX2   CLC   0(0,R3),3(R11)
DOSPOOL  DC    C'00100204F055059150151152153',X'FF'
DOSPLMSG DS    0F
         DC    X'06010A',C'IEA000A'
         DC    X'060109',C'IEA000I'
         DC    X'06010B',C'IEC001A'
         DC    X'06010B',C'IEC003E'
         DC    X'06010B',C'IEC101A'
         DC    X'06010B',C'IEC120A'
         DC    X'06010B',C'IEC121D'              FRANZ 26. 8. 76
         DC    X'060109',C'IEC122D'              FRANZ 26. 8. 76
         DC    X'060109',C'IEC123D'
         DC    X'060109',C'IEC124I'              FRANZ 26. 8. 76
         DC    X'060109',C'IEC126I'              FRANZ 26. 8. 76
         DC    X'060109',C'IEC127D'              FRANZ 26. 8. 76
         DC    X'06010B',C'IEC128D'              FRANZ 26. 8. 76
         DC    X'060109',C'IEC129D'              FRANZ 26. 8. 76
         DC    X'06010B',C'IEC501A'
         DC    X'06010B',C'IEC501E'
         DC    X'06010B',C'IEF233A'
         DC    X'060112',C'IEF247I'
         DC    X'06018E',C'IGF500I'
         DC    X'060115',C'IGF500I'
         DC    X'06019B',C'IGF502E'
         DC    X'060122',C'IGF502E'
         DC    X'060193',C'IGF505I'
         DC    X'06011A',C'IGF505I'
         DC    X'06010E',C'IGF509I'
         DC    X'060189',C'MULT ON'
         DC    X'06010D',C'MULT ON'
         DC    X'FF'
MSGTAB   DS    0F
         DC    X'07',C'$HASP373',X'80',XL8'00'
         DC    X'07',C'$HASP374',X'80',XL8'00'
         DC    X'07',C'$HASP375',X'80',XL8'00'
         DC    X'07',C'$HASP395',X'80',XL8'00'
         DC    X'07',C'$HASP396',X'80',XL8'00'
         DC    X'06',C'IEC001A',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC101A',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC400A',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC401A',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC402D',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC501A',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC501E',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC502E',X'80',XL8'00'
         DC    X'06',C'IEC509A',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC510D',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC701D',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC703I',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC704A',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEC705I',X'80',XL8'00'
         DC    X'06',C'IEF233A',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEF233D',X'30',XL4'00',X'20',XL3'00'
         DC    X'06',C'IEF234E',X'80',XL8'00'
         DC    X'06',C'IEF471E',X'80',XL8'00'
         DC    X'FF'
ROUT1    DC    XL2'0008'
ROUT2    DC    XL2'0004'
         REG
ALTROUT  EQU   X'80'
ALTDESC  EQU   X'40'
SWT1314  EQU   X'20'
VGLROUT  EQU   X'10'
VGLDESC  EQU   X'08'
WORK     DSECT
SVA      DS    18F
COMMD    DS    CL50
VERGL    DS    XL2
WORKL    EQU   *-WORK
MSGS     DSECT
MSGIDLEN DS    X
MSGIDENT DS    0CL10
MSGSWT   DS    X
MSGROUT  DS    XL2
MSGDESC  DS    XL2
MSGVROUT DS    XL2
MSGVDESC DS    XL2
         END
