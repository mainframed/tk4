**********************************************************************
*              REGISTER EQUATES                                      *
**********************************************************************
         SPACE
         REQUATEN
         EJECT
**********************************************************************
*              PROGRAM START                                         *
**********************************************************************
         SPACE
SPFETSMD CSECT
         LCNVE *,12
         SPACE
         LR    R11,R1                  PARM ADDRESS FROM PMD/SPFETMD
         EJECT
**********************************************************************
*              GET WORK AREA                                         *
**********************************************************************
         SPACE
         INIT  R9,(WORKC,WORKLEN)
         SPACE
         USING WORKC,R9
         L     R10,0(R11)              GET THE TLD ADDRESS FROM PMD/
*                                      SPFETMD
         ST    R10,CPARMS              SAVE THIS ADDRESS FOR THE
*                                      APPLICATION PROGRAM
         L     R1,4(R11)               GET THE OF RETURNED FORMAT
         MVC   MHAFMTN,0(R1)           SAVE THE FORMAT NAME
         EJECT
**********************************************************************
*              CHECK THE PSCB                                        *
**********************************************************************
         SPACE
         L     R2,16                   POINT TO THE CVT
         L     R3,0(R2)                TCB WORDS
         ST    R3,TCBWRD               SAVE THE ADDRESS FOR GET USER ID
         L     R2,4(R3)
         L     R3,X'B4'(R2)
         L     R2,X'108'(R3)           R2 NOW POINTS TO THE PSCB
         TM    16(R2),X'40'            SEE IF ACCOUNT IS ALLOWED FOR
*                                      THIS USER
         BNO   NOAUTH                  NOT AUTHORISED
         EJECT
**********************************************************************
*              PUT THE SELECTION MENU                                *
**********************************************************************
         SPACE
DISPLAY  SPFMH MHAPARMS,MHAFMTN,MHAOPT0,MHAP1,MHAP2
         EJECT
**********************************************************************
*              TEST ON "END" ORDER AND VALID RETURN                  *
**********************************************************************
         SPACE
ORDTEST  TM    29(R10),B'00000100'     END KEY PFK3 HAS BEEN PRESSED ?
         BO    END                     END OF ET-MAIN DRIVER
         CLI   MHAP1,C'^'              INVALID OPTION HAS BEEN ENTER ?
         BE    MERROR                  PUT THE ERROR MESSAGE
         SPACE 2
**********************************************************************
*              LINK THE APPLICATION PROGRAM                          *
**********************************************************************
         SPACE
         LA    R1,CPARMS               POINT TO PARMS FOR THE
*                                      APPLICATION PROGRAM
         ST    R10,0(R1)               SAVE THE TLD ADDRESS FOR THE
*                                      APPLICATION PROGRAM
         LA    R2,MHAP2                SAVE THE ADDRESS OF SUBMENU FOR
         ST    R2,4(R1)                THE APPLICATION PROGRAM
         MVI   4(R1),X'80'             SET THE VL BIT
         SPACE
         LINK  EPLOC=MHAP1,SF=(E,LINK)  LINK THE SELECTED PROGRAM
         SPACE
         NI    29(R10),B'11111011'
         B     DISPLAY
         EJECT
**********************************************************************
*              END OF SYT MAIN DRIVER                                *
**********************************************************************
         SPACE
END      NI    29(R10),B'11111011'     TURN OFF THE PFK3 REFERENCE BIT
         LA    0,WORKLEN               SIZE OF THE WORK AREA
         SPACE
         FREEMAIN R,A=(9),LV=(0)       DROP THE WORK AREA
         SPACE
         LEXIT                         RETURN TO THE SPF-PMD
         EJECT
**********************************************************************
*              PUT THE ERROR FORMAT / USER NOT AUTHORIZED            *
**********************************************************************
         SPACE
NOAUTH   L     R3,TCBWRD               POINT TO THE TCB WORDS
         L     R2,0(R3)                POINT TO THE CURRENT TCB
         L     R3,180(R2)              POINT TO THE TCBJSCB
         L     R2,264(R3)              POINT TO THE PCB
         MVC   USERID,0(R2)            SAVE THE USER-ID.
         SPACE
         SPFMH MHAPARMS,ERRORFMT,MHAOPT0,USERID
         SPACE
         B     END
         EJECT
**********************************************************************
*              PUT THE ERROR MESSAGE / INVALID OPTION SELECTED       *
**********************************************************************
         SPACE
MERROR   MVC   MEHMSG1,MHAP1+1         SAVE THE THE ERROR MESSAGE ID.
*                                      RETURNED FROM THE MHA
         SPACE
         SPFEM MEHPARMS,MEHMSG1,MEHOPT1
         SPACE
         B     ORDTEST                 TEST THE RETURNED ORDER
         EJECT
**********************************************************************
*              DEFINITIONS / LITERALS                                *
**********************************************************************
         SPACE
         LTORG
         EJECT
**********************************************************************
*              DEFINITIONS                                           *
**********************************************************************
         SPACE
WORKC    CSECT
*        PARM AREA FOR THE APPLICATION PROGRAM
CPARMS   DC    A(0)                    POINT TO THE TLD
         DC    A(0)                    ADDRESS OF THE FORMAT NAME
*        PARM AREA FOR THE COMMON SUBROUTINE- MERR
MEHPARMS DC    A(0)                    POINT TO THE TLD
         DC    A(0)                    POINT TO THE ERROR MESSAGE   ID.
         DC    A(0)                    POINT TO THE CURSOR ORDER
         DC    A(0)                    POINT TO THE PARM FIELD
*        PARM AREA FOR THE COMMON SUBROUTINE- MHA
MHAPARMS DC    A(0)                    POINT TO THE TLD
         DC    A(0)                    POINT TO THE FORMAT NAME
         DC    A(0)                    POINT TO THE CONTROL OPTION
         DC    A(0)                    POINT TO RETURNED PGM NAME
         DC    A(0)                    POINT TO RETURNED FMT NAME
         DC    A(0)                    POINT TO RETURNED ERROR MSG-ID.
*
TCBWRD   DC    A(0)                    ADDRESS OF TCB
SW       DC    X'00'
NOTSYT   EQU   1
MHAP1    DC    CL8' '                  RETURNED PROGRAM NAME
MHAP2    DC    CL8' '                  RETURNED FORMAT NAME
MHAFMTN  DC    CL8' '                  SELECTION MENU FORMAT NAME
MEHMSG1  DC    CL4' '                  RETURNED ERROR MESSAGE ID.
USERID   DC    CL4' '                  USER ID HAS BEEN LOGGED ON
ERRORFMT DC    CL8'ETAMSNG'            ERROR FORMAT NAME
LINK     LINK  SF=L
WORKLEN  EQU   *-WORKC                 LENGTH OF THE WORK AREA
         EJECT
**********************************************************************
*              DEFINITIONS                                           *
**********************************************************************
         SPACE
SPFETSMD CSECT
MHAOPT0  DC    A(0)                    CONTROL OPTION FOR MHA
MEHOPT1  DC    A(1)                    CURSOR ORDER FOR MERR
         END
