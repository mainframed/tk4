./ ADD  NAME=UBOE01CS
         TITLE 'SY021A'
*
*------------------------------------------------------------*
*  PROGRAMM SY021A ZUSATZPROGRAMM ZU SY021 .                 *
*  CICS - ANTWORTZEITVERHALTEN , MONATLICHE AUSWERTUNG .     *
*                                                            *
*------------------------------------------------------------*
*
SY021A   CSTART (R7,R12),*
ANFANG   EQU   *
         L     R3,0(1)             LADEN PARMFELD
         LH    R4,0(3)             LADEN PARMLAENGE
         LTR   R4,R4               PARMWERT VORHANDEN
         BZ    FEHL1                NEIN
         BCTR  R4,0
         EX    R4,MVC1
         B     P100
MVC1     MVC   PARMFELD(0),2(R3)   PARMFELD IN PROGR.BEREICH
P100     EQU   *
         CLI   PARMFELD,X'40'      MONAT VORHANDEN
         BE    FEHL1
         MVC   MONAT(2),PARMFELD
         CLI   PARMFELD+2,C','     3.STELLE KOMMA FOLGT PROZ.WERT
         BNE   P120
         CLI   PARMFELD+3,X'40'
         BE    P120
         CLI   PARMFELD+3,C','
         BE    P115
         MVC   PROZW,PARMFELD+3
         CLI   PARMFELD+5,C','
         BE    P116
         B     P120
P115     CLI   PARMFELD+4,X'40'
         BE    P120
         PACK  SCHW,PARMFELD+4(4)
         PACK  SCHW1,PARMFELD+9(4)
         MVC   SCHALT1,C'1'
         B     P120
P116     PACK  SCHW,PARMFELD+6(4)
         PACK  SCHW1,PARMFELD+11(4)
         MVC   SCHALT1,C'1'
P120     EQU   *
         OPEN  (SY021AI,,SY021AO,(OUTPUT))
         OPEN  (SY021AO1,(OUTPUT))
         BAL   R14,UPRO1           BRANCH DRUCKEN UEBERSCHRIFT
         BAL   R14,UPRO3
P130     EQU   *
         GET   SY021AI,EBER        LESEN SATZ
         CLC   MONAT(2),DATUM+2    RICHTIGER MONAT
         BNE   P130                NEIN
         BAL   R14,UPRO2
         BAL   R14,UPRO5
         BAL   R14,UPRO4
         BAL   R14,UPRO6
         B     P130
*-----------------------------------------------------------------
*                  UPRO1  UEBERSCHRIFT DURCHSCHNITTSBLATT
*-----------------------------------------------------------------
UPRO1    ST    R14,SICH141         DRUCKEN UEBERSCHRIFT
         MVI   V,X'F1'
         PUT   SY021AO,KOPF1
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,KOPF2
         PUT   SY021AO,KOPF3
         CLC   SCHALT1,C'1'
         BNE   P144
         MVC   ASCHW,MASK
         ED    ASCHW,SCHW
         PUT   SY021AO,KOPF3B
P144     PUT   SY021AO,LEERZEIL
         PUT   SY021AO,KOPF4
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,KOPF5
         PUT   SY021AO,LEERZEIL
         L     R14,SICH141
         BR    R14
*-----------------------------------------------------------------
*                  UPRO3  UEBERSCHRIFT PROZENTWERTEBLATT
*-----------------------------------------------------------------
UPRO3    ST    R14,SICH141         DRUCKEN UEBERSCHRIFT
         MVI   V,X'F1'
         PUT   SY021AO1,KOPF1
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,KOPF2
         MVC   APROZ,PROZW
         MVC   INTPROZ,PROZW
         PUT   SY021AO1,KOPF3A
         CLC   SCHALT1,C'1'
         BNE   P145
         MVC   ASCHW,MASK
         ED    ASCHW,SCHW1
         PUT   SY021AO1,KOPF3B
P145     PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,KOPF4
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,KOPF5
         PUT   SY021AO1,LEERZEIL
         L     R14,SICH141
         BR    R14
*------------------------------------------------------------
*                 UPRO2    BERECHNUNGEN DURCHSCHNITTSBLATT
*------------------------------------------------------------
UPRO2    ST    R14,SICH142
         MVC   SCHALT,C'1'
         MVC   ADAT(2),DATUM+4     AUFBEREITEN DATUM
         MVI   ADAT+2,C'.'
         MVC   ADAT+3(2),DATUM+2
         MVI   ADAT+5,C'.'
         MVC   ADAT+6(2),DATUM
         AP    SZAEHLER,=P'1'     ANZAHL TAGE
         ZAP   DWT,=P'0'         DURCHSCHNITT PRO TAG
         ZAP   SZAEHL2,=P'0'
         LA    4,11               ZAEHLER FUER 10 EINTRAEGE
         LA    R5,ASTD            ADR. AUSGABEBEREICH
         LA    R6,DSEK            ADR.EINGABEBEREICH
         LA    R3,DWST            DURCHSCHNITT PRO STD
         LA    R8,MAXST           MAXIMUM PRO STD
         LA    R9,SZAEHL4
P240     EQU   *
         CP    0(4,6),=P'0'        SIND WERTE = 0
         BNE   P241
         MVC   3(4,5),=C'  - '     BEI JA  - IN AUSGABEBER.
         B     P250
P241     CLC   SCHALT1,C'1'        SOLLEN SCHWELLWERTE VERARBEITET W.
         BNE   P242
         CP    SCHW,1(3,6)
         BNH   P242
         MVC   3(4,5),=C'  - '
         B     P243
P242     MVC   0(7,R5),MASK
         ED    0(7,5),1(6)        ENTPACKEN
         AP    SCHWZ1,=P'1'
P243     AP    SZAEHL2,=P'1'
         AP    0(2,9),=P'1'
         AP    DWT,0(4,6)         BILDEN DURCHSCHNITT PRO TAG
         AP    0(4,3),0(4,6)     DURCHSCHNITT PRO STD
         CP    0(4,8),0(4,6)     VERGLEICH MAXIMUM
         BNH   P260
P250     LA    R8,4(R8)
         LA    R5,7(R5)
         LA    R6,6(R6)
         LA    R3,4(R3)
         LA    R9,2(R9)
         BCT   4,P240
         CP    SZAEHL2,=P'0'
         BE    P255
         DP    DWT,SZAEHL2         BILDEN DURCHSCHNITT PRO TAG
P255     MVC   ADWT,MASK
         ED    ADWT,DWT+1
         MVC   KVZWI,ABER
         PUT   SY021AO,ABER       AUSGEBEN SATZ
         MVI   ABER,X'40'       AUSGABE-BEREICH LOESCHEN
         MVC   ABER+1(132),ABER
         L     R14,SICH142
         BR    R14
P260     ZAP   0(4,8),0(4,6)
         B     P250
*------------------------------------------------------------
*                 UPRO4    BERECHNUNGEN PROZENTWERTBLATT
*------------------------------------------------------------
UPRO4    ST    R14,SICH144
         MVC   ADAT(2),DATUM+4     AUFBEREITEN DATUM
         MVI   ADAT+2,C'.'
         MVC   ADAT+3(2),DATUM+2
         MVI   ADAT+5,C'.'
         MVC   ADAT+6(2),DATUM
         ZAP   DWT,=P'0'          LOESCHEN FELDER
         ZAP   SZAEHL3,=P'0'
         LA    4,11               SETZEN ZAEHLER
         LA    R10,DWSTP          DURCHSCHNITT PRO STD PRO PROZENT
         LA    R11,MAXSTP         MAXIMUM PRO STD PRO PROZENT
         LA    R8,ASTD
         LA    R5,EPROZ
         L     R2,SICH2
         LA    R2,SZAEHL5
P400     EQU   *
         CLC   PROZW,0(R5)        GESUCHTER PROZENTWERT
         BNE   P430               NEIN
P410     LA    R6,2(R5)
P415     PACK  REFE2,0(6,R6)
P416     EQU   *
         CLC   SCHALT1,C'1'
         BNE   P418
         CP    SCHW1,REFE2+1(3)
         BNH   P418
         MVC   3(4,R8),=C'  - '
         CP    REFE2,=P'0'
         BE    P419
         B     P417
P418     CP    REFE2,=P'0'
         BE    P480
         MVC   0(7,R8),MASK
         ED    0(7,R8),REFE2+1
         AP    SCHWZ2,=P'1'
P417     AP    SZAEHL3,=P'1'
         AP    0(2,2),=P'1'
P419     AP    DWT,REFE2           DURCHSCHNITT PRO TAG PRO PROZENTW.
         AP    0(4,10),REFE2       DURCHSCHNITT PRO STD PRO PROZENT
         CP    0(4,11),REFE2
         BNH   P470
P420     LA    R11,4(R11)
         LA    R10,4(R10)
         LA    R5,58(R5)
         LA    R8,7(R8)
         LA    R2,2(R2)
         BCT   4,P400
         B     P460
P430     EQU   *
         LA    3,6
         LA    R9,8(R5)
P440     CLC   PROZW,0(R9)
         BNE   P450
         LA    R6,2(R9)
         B     P415
P450     LA    R9,8(R9)
         BCT   3,P440
         ZAP   REFE2,=P'0'       PROZW. NICHT GEFUNDEN
         B     P416
P460     EQU   *
         CP    SZAEHL3,=P'0'
         BE    P465
         DP    DWT,SZAEHL3
P465     MVC   ADWT,MASK
         ED    ADWT,DWT+1
         MVC   KVZWI,ABER
         PUT   SY021AO1,ABER
         MVI   ABER,X'40'           LOESCHEN AUSGABEBEREICH
         MVC   ABER+1(132),ABER
         ST    R2,SICH2
         L     R14,SICH144
         BR    R14
P470     ZAP   0(4,11),REFE2        MAXIMUM IN MAXWERTFELD
         B     P420
P480     MVC   3(4,R8),=C'  - '
         B     P419
*----------------------------------------------------------
*                UPRO5  (INTERPRETATION DURCHSCHNITTSWERTE)
*----------------------------------------------------------
UPRO5    ST    R14,SICH141
         CLI   MARK1,C'5'
         BE    P1005
         LA    R5,KVASTD
         LA    R4,KOPF5+14
         ZAP   KVSZ,=P'0'
P1001    AP    KVSZ,=P'1'
         CP    KVSZ,=P'12'
         BE    P1005
         CLC   4(3,R5),=C' - '
         BNE   P1003
         LA    R4,7(R4)
         LA    R5,7(R5)
         B     P1001
P1003    MVI   MARK1,C'5'
         MVC   INTDAT1,KVADAT
         MVC   INTZEIT1,0(R4)
         MVC   INTANTW1,1(R5)
         MVC   KVAUS1,KVZWI
P1005    L     R14,SICH141
         BR    R14
*----------------------------------------------------------
*                UPRO6  (INTERPRETATION PROZENTWERTE)
*----------------------------------------------------------
UPRO6    ST    R14,SICH141
         CLI   MARK2,C'5'
         BE    P2005
         LA    R5,KVASTD
         LA    R4,KOPF5+14
         ZAP   KVSZ,=P'0'
P2001    AP    KVSZ,=P'1'
         CP    KVSZ,=P'12'
         BE    P2005
         CLC   4(3,R5),=C' - '
         BNE   P2003
         LA    R4,7(R4)
         LA    R5,7(R5)
         B     P2001
P2003    MVI   MARK2,C'5'
         MVC   INTDAT2,KVADAT
         MVC   INTZEIT2,0(R4)
         MVC   INTANTW2,1(R5)
         MVC   KVAUS2,KVZWI
P2005    L     R14,SICH141
         BR    R14
*--------------------------------------------------
*               FEHLER-MELDUNGEN
*----------------------------------------------------
FEHL1    EQU   *
         OPEN  (SY021AO1,(OUTPUT))
         PUT   SY021AO1,FEHLER1   KEIN PARMWERT
         CLOSE SY021AO1
         B     ENDE
FEHL2    EQU   *
         PUT   SY021AO1,FEHLER2   KEINE DATEN FUER DIESEN MONAT
         CLOSE SY021AO1
         CLOSE SY021AI
         CLOSE SY021AO
         B     ENDE
*---------------------------------------------------------
*          ENDE-BERECHNUNGEN
*----------------------------------------------------------
END2     EQU   *
         CLC   SCHALT,C'1'      WAREN SAETZE VORHANDEN
         BNE   FEHL2            NEIN
         PUT   SY021AO,LEERZEIL
         LA    4,11             UEBERTRAGEN DURCHSCHNITT/STD
         LA    R5,DWST
         LA    R6,ATAD
         LA    R3,SZAEHL4
P900     EQU   *
         ZAP   REFE1,0(4,5)
         CP    0(2,R3),=P'0'        ANZ.TRANS = 0
         BNE   P901                 NEIN
         MVC   0(7,R6),=C'     - '
         B     P902
P901     DP    REFE1,0(2,3)        DURCHSCHNITT PRO STD
         MVC   0(7,R6),MASK
         ED    0(7,6),REFE1+1
P902     LA    R5,4(R5)
         LA    R6,7(R6)
         LA    R3,2(R3)
         BCT   4,P900
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,ADZEIL
         PUT   SY021AO,LEERZEIL
         LA    4,11
         LA    R9,AMST
         LA    R8,MAXST
P910     EQU   *
         MVC   0(7,R9),MASK
         ED    0(7,R9),1(8)
         LA    R9,7(R9)
         LA    R8,4(R8)
         BCT   4,P910
         PUT   SY021AO,AMZEIL
         CLC   SCHALT1,C'1'
         BNE   P915
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,LEERZEIL
         MVC   ASCHW1,MASK
         ED    ASCHW1,SCHW
         MVC   ASCHWZ,MASK2
         ED    ASCHWZ,SCHWZ1
         MVI   ASCHWZ+4,X'40'
         MVI   V7,X'40'
         PUT   SY021AO,KOPF7
P915     PUT   SY021AO,LEERZEIL
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,KOPF6
         MVI   ATAD,X'40'            LOESCHEN AUSGABEZEILE D-WERT
         MVC   ATAD+1(76),ATAD
         MVI   AMST,X'40'            LOESCHEN AUSGABEZEILE MAX-WERT
         MVC   AMST+1(76),AMST
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,KOPF8
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,KOPF5
         PUT   SY021AO,LEERZEIL
         PUT   SY021AO,KVAUS1
         PUT   SY021AO,INTAUS1
         PUT   SY021AO1,LEERZEIL
*        ENDE-BERECHNUNGEN FUER PROZENTWERTBLATT
         LA    4,11
         LA    R5,DWSTP
         LA    R6,ATAD
         LA    R3,SZAEHL5
P920     EQU   *
         ZAP   REFE1,0(4,5)
         CP    0(2,R3),=P'0'        ANZ.TRANS = 0
         BNE   P921                 NEIN
         MVC   0(7,R6),=C'     - '
         B     P922
P921     DP    REFE1,0(2,3)      DURCHSCHNITT PRO STD PROZW.
         MVC   0(7,R6),MASK
         ED    0(7,R6),REFE1+1
P922     LA    R5,4(R5)
         LA    R6,7(R6)
         LA    R3,2(R3)
         BCT   4,P920
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,ADZEIL
         PUT   SY021AO1,LEERZEIL
         LA    4,11
         LA    R9,AMST
         LA    R8,MAXSTP
P930     EQU   *
         MVC   0(7,R9),MASK
         ED    0(7,R9),1(8)
         LA    R9,7(R9)
         LA    R8,4(R8)
         BCT   4,P930
         PUT   SY021AO1,AMZEIL
         CLC   SCHALT1,C'1'
         BNE   P940
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,LEERZEIL
         MVC   ASCHW1,MASK
         ED    ASCHW1,SCHW1
         MVC   ASCHWZ,MASK2
         ED    ASCHWZ,SCHWZ2
         MVI   ASCHWZ+4,X'40'
         MVI   V7,X'F0'
         PUT   SY021AO1,KOPF7
P940     PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,KOPF6
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,KOPF8
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,KOPF5
         PUT   SY021AO1,LEERZEIL
         PUT   SY021AO1,KVAUS2
         PUT   SY021AO1,INTAUS2
         CLOSE SY021AI
         CLOSE SY021AO
         CLOSE SY021AO1
         B     ENDE
*---------------------------------------------------------------
*                      ENDE
*---------------------------------------------------------------
ENDE     EQU   *
         L     R13,SAVEA+4
         RETURN (14,12)
*----------------------------------------------------------------
*                      DEFINITIONEN
*----------------------------------------------------------------
PARMFELD DC    CL20' '        PROGR.BEREICH F. PARMFELD
PROZW    DC    CL2'95'        HILFSFELD PROZWERT
MONAT    DC    CL2' '         HILFSFELD MONAT
SICH141  DS    F
SICH142  DS    F
SICH144  DS    F
SICH145  DS    F
SICH2    DS    F
SZAEHLER DC    PL2'0'
SZAEHL2  DC    PL2'0'
SZAEHL3  DC    PL2'0'
SZAEHL4  DC    11PL2'0'
SZAEHL5  DC    11PL2'0'
SCHALT   DC    CL1'0'
SCHALT1  DC    CL1'0'
MASK     DC    X'402021206B2020'  MASKE FUER ENTPACKEN
MASK2    DC    X'4020202120'
DWT      DC    PL6'0'         DURCHSCHNITT PRO TAG
DWST     DC    11PL4'0'       DURCHSCHNITT PRO STD
MAXST    DC    11PL4'0'       MAXIMUM PRO STD
DWSTP    DC    11PL4'0'
MAXSTP   DC    11PL4'0'       MAXIMUM PRO STD PRO PROZENT
SCHW     DC    PL3'0'
SCHW1    DC    PL3'0'
SCHWZ1   DC    PL2'0'
SCHWZ2   DC    PL2'0'
REFE1    DC    PL6'0'
REFE2    DC    PL4'0'
FEHLER1  DS    0CL133
         DC    CL20'1** KEIN PARMWERT **'
         DC    CL113' '
FEHLER2  DS    0CL133
         DC    CL33' ** KEINE WERTE FUER DIESEN MONAT'
         DC    CL100' '
KOPF1    DS    0CL133
V        DC    C' '
         DC    C'LISTE 1847'
         DC    CL21' '
         DC    C'C I C S - A N T W O R T Z E I T V E R H A L T E N'
         DC    CL52' '
KOPF2    DS    0CL133
         DC    C'0'
         DC    CL40' '
         DC    C'MONATLICHE  UEBERSICHT'
         DC    CL70' '
KOPF3    DS    0CL133
         DC    C'0'
         DC    CL41' '
         DC    C'(DURCHSCHNITTSWERTE)'
         DC    CL71' '
KOPF3A   DS    0CL133
         DC    C'0'
         DC    CL43' '
APROZ    DC    C'95'
         DC    C' % - WERTE'
         DC    CL77' '
KOPF3B   DS    0CL133
         DC    C'0'
         DC    CL22' '
         DC    C'ANTWORTZEITEN UNTER'
ASCHW    DS    CL7
         DC    C' SEKUNDEN WERDEN NICHT GEDRUCKT'
         DC    CL53' '
KOPF4    DS    0CL133
         DC    C' '
         DC    CL2' '
         DC    C'DATUM'
         DC    CL15' '
         DC    C'UHRZEIT'
         DC    CL103' '
KOPF5    DS    0CL133
         DC    C' '
         DC    CL13' '
         DC    C'07-08  08-09  09-10  10-11  11-12  12-13  '
         DC    C'13-14  14-15  15-16  16-17  17-18    DURCHSCHNITT'
         DC    CL26' '
KOPF6    DS    0CL133
         DC    C' '
         DC    CL15' '
         DC    C'BEI REMOTE-ANWENDUNGEN IST DIE UEBERTRAGUNGSZEIT '
         DC    C'AUF DER LEITUNG NICHT ENTHALTEN .  '
         DC    CL33' '
KOPF7    DS    0CL133
V7       DC    C' '
         DC    CL15' '
         DC    C'ANZAHL DER WERTE > '
ASCHW1   DS    CL7
         DC    C' = '
ASCHWZ   DS    CL5
         DC    CL83' '
INTAUS2  DS    0CL133
         DC    C'-'
         DC    C'AM '
INTDAT2  DC    CL9' '
         DC    C'HATTEN IN DER ZEIT VON '
INTZEIT2 DC    CL6' '
         DC    C'UHR '
INTPROZ  DC    C'95'
         DC    C' % ALLER TRANSAKTIONEN EINE ANTWORTZEIT KLEINER'
INTANTW2 DC    CL6' '
         DC    C' SEKUNDEN'
         DC    CL11' '
LEERZEIL DS    0CL133
         DC    C' '
         DC    CL132' '
INTAUS1  DS    0CL133
         DC    C'-'
         DC    C'AM '
INTDAT1  DC    CL9' '
         DC    C'BETRUG IN DER ZEIT VON '
INTZEIT1 DC    CL6' '
         DC    C'UHR DIE DURCHSCHNITTLICHE ANTWORTZEIT'
INTANTW1 DC    CL6' '
         DC    C' SEKUNDEN'
         DC    CL39' '
EBER     DS    0CL800
SA       DS    CL4   SY21
DATUM    DS    CL6   JJMMTT
ETAB1    DS    0CL638
EHH      DS    CL2
ETAB2    DS    0CL56
EPROZ    DS    CL2
ESEK     DS    CL6
         DS    CL48
         DS    CL580
DTAB     DS    0CL66
DHH      DS    CL2
DSEK     DS    CL4
         DS    CL60
         DS    CL86
ABER     DS    0CL133
         DC    C' '
ADAT     DC    CL10' '
         DC    C' '
ASTD     DS    11CL7
         DC    CL3' '
ADWT     DC    CL7' '
         DC    CL34' '
ADZEIL   DS    0CL133
         DC    C' '
         DC    C'DURCHSCHNITT'
ATAD     DS    11CL7
         DC    CL43' '
AMZEIL   DS    0CL133
         DC    C' '
         DC    C'MAXIMUM     '
AMST     DS    11CL7' '
         DC    CL43' '
KVZWI    DS    0CL133
         DC    C'-'
KVADAT   DC    CL10' '
         DC    C' '
KVASTD   DC    11CL7' '
         DC    CL3' '
KVADWT   DC    CL7' '
         DC    CL34' '
KOPF8    DS    0CL133
         DC    C'-'
         DC    C'INTERPRETATIONSBEISPIEL:'
         DC    CL108' '
KVSZ     DS    H
MARK1    DS    C'0'
MARK2    DS    C'0'
KVAUS1   DS    CL133' '
KVAUS2   DS    CL133' '
SY021AI  DCB   DDNAME=SY021AI,EODAD=END2,LRECL=800,DSORG=PS,RECFM=F,   *
               MACRF=(GM)
SY021AO  DCB   DDNAME=SY021AO,LRECL=133,DSORG=PS,                      *
               RECFM=FBA,MACRF=(PM)
SY021AO1 DCB   DDNAME=SY021AO1,RECFM=FBA,LRECL=133,                    *
               DSORG=PS,MACRF=(PM)
         END   SY021A
