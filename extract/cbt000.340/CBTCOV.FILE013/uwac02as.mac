*   31/10/80 505021302  MEMBER NAME  UWAC02AS (GAPVERS)     ASSEMBLER
USISCAN  TITLE 'ASSEMBLER USING SCANNER V2.0'
**********************************************************************
*                                                                    *
*             ASSEMBLER OUTPUT SCANNER PROGRAM V 2.0                 *
*                                                                    *
*   THIS PROGRAM SCANS ASSEMBLER OUTPUT FOR REGISTER USAGE. A RANGE  *
*   FROM 'USING' TO 'DROP' IS MARKED AT THE RIGHT MARGIN OF THE      *
*   OUTPUT. ANY EXECUTABLE INSTRUCTION IS TESTED FOR ITS REGISTER    *
*   USAGE. 'USING' AND 'DROP' WITH MULTIPLE REGISTERS ARE RECOG-     *
*   NIZED. DUE TO MISSING LISTING SPACE COLUMNS 73-80 ARE OVERLAID   *
*   BY THE REGISTER USAGE INFORMATION.                               *
*                                                                    *
*   THE PROGRAM MAY BE USED IN THREE WAYS:                           *
*       1. WITH 'PARM=U' (OR NO PARM) THE PROGRAM FLAGS ALL RE-      *
*          GISTERS WHICH ARE USED BY THE INSTRUCTIONS.               *
*       2. WITH 'PARM=C' THE PROGRAM ONLY FLAGS THE REGISTERS        *
*          WHICH ARE CHANGED BY THE INSTRUCTIONS.                    *
*       3. WITH 'PARM=N' THE PROGRAM ONLY FLAGS RANGES OF 'USING'    *
*          AND 'DROP'.                                               *
*                                                                    *
*   USAGE: ALLOCATE DDNAME 'INPUT' TO ASSEMBLER OUTPUT, ALLOCATE     *
*   DDNAME 'OUTPUT' TO 'SYSOUT=*' OR APPROPRIATE DATA SET. OUTPUT    *
*   DCB IS '132/132/FA'. OPEN FAILURE RESULTS IN ABEND U001.         *
*                                                                    *
*   PERMANENT RESTRICTION: RRE-INSTRUCTIONS (E.G. IPTE) ARE          *
*                          NOT PROCESSED. THEY ARE MARKED BY '?..?'  *
*                          CHECK 'BY EYE' IF APPLICABLE.             *
*                                                                    *
*   THE PROGRAM IS APPLICABLE TO BOTH OUTPUT OF ASSEMBLER XF AND H   *
*                                                                    *
*   LINKAGE ATTRIBUTES: NORENT,NOREUS,NOREFR                         *
*                                                                    *
*   AUTHOR: BLECKWENN, DESY HAMBURG/WACKER MUENCHEN    NOV. 1.,1980  *
*                                                                    *
**********************************************************************
USISCAN  CSECT
         USING *,R12
         ANF   *
         L     R1,0(R1)            PARAMETER LIST
         LH    R2,0(R1)            ITS LENGTH
         LTR   R2,R2               ZERO LENGTH ?
         BZ    OPENDS              YES, NO PARM GIVEN
         CLI   2(R1),C'C'          IS IT FOR 'CHANGED REGS' ?
         BNE   OTHER               NO
         MVI   FLAG,CHG            MOVE IN FLAG
         B     OPENDS              BRANCH TO OPEN
OTHER    CLI   2(R1),C'N'          IS IT FOR 'RANGE ONLY' ?
         BNE   OPENDS              NO, DEFAULT TO 'USED REGS'
         MVI   FLAG,RNG            MOVE IN FLAG
OPENDS   OPEN  (INPUT,(INPUT))     OPEN INPUT DATA SET
         TM    INPUT+48,X'10'      TEST FOR ERROR
         BZ    OPENERR             ABEND USER 001
         OPEN  (OUTPUT,(OUTPUT))   OPEN OUTPUT DATASET
         TM    OUTPUT+48,X'10'     TEST FOR ERROR
         BZ    OPENERR             ABEND USER 001
         USING INLINE,R4
READ     GET   INPUT               GET A RECORD
         LR    R4,R1               SAVE ITS ADDRESS
         CLC   112(4,R4),=C'PAGE'  HEADER LINE ?
         BNE   LOC                 NO, BRANCH
         MVC   OUTLINE(121),0(R4)  REPRODUCE INPUT
         MVC   OUTLINE+80(L'VERSION),VERSION   SET VERSION ID
         MVC   OUTLINE+121(12),BLANK   ADD BLANKS
         PUT   OUTPUT,OUTLINE      WRITE OUT
         B     READ                GET NEXT RECORD FROM INPUT
LOC      CLC   3(4,R4),=C'LOC '    SECOND HEADER ?
         BNE   NORM                NO, BRANCH
         MVC   OUTLINE(121),0(R4)  REPRODUCE INPUT
         MVC   OUTLINE+121(12),BLANK   ADD BLANKS
         PUT   OUTPUT,OUTLINE      WRITE OUT
         TM    FLAG,ENDBIT         HAS 'END' STMT PASSED ?
         BO    READ                YES, REPRO ALL FOLLOWING
         TM    FLAG,CHG            'CHANGE' SPECIFIED ?
         BO    PUT                 YES, LET 'CHANGED'
         MVC   USTYP(17),=CL17'   USED REGISTERS' USAGE TYPE
         TM    FLAG,RNG            'USING/DROP ONLY' SPECIFIED ?
         BNO   PUT                  NO, LET 'CHANGED'
         MVC   USTYP(17),=CL17' USING/DROP RANGE' USAGE TYPE
PUT      PUT   OUTPUT,INSLINE      INSERT EXTRA HEADER LINE
         B     READ                GET NEXT RECORD
NORM     MVC   REGSAV,REGX         RESTORE OLD REG FIELD
         TM    FLAG,ENDBIT         HAS 'END' STMT PASSED ?
         BO    PRINT               YES, REPRO ALL FOLLOWING
         CLI   TXTFIELD,C'*'       COMMENT ?
         BE    PRINT               YES, PRINT
         CLI   NUMFIELD+4,C' '     BLANK LINE
         BE    PRINT               YES, PRINT
         CLI   ADDR1+3,C' '        USING/DROP/DS/DC/EQU/END/RR ?
         BNE   XINSTR              NO, MUST BE INSTR EXCEPT RR
         LA    R1,TXTFIELD         LOAD FOR SCAN
         LA    R2,1                INCREMENT FOR BXLE
         LA    R3,TXTFIELD+70      COMPARAND FOR BXLE
LOOP1    CLI   0(R1),C' '          NAME FIELD BLANK ?
         BE    LOOP2               LOOK FOR FIRST ...
         BXLE  R1,R2,LOOP1         NON-BLANK
LOOP2    CLI   0(R1),C' '          FOUND, NOW LOOK ...
         BNE   TESTDC              FOR BEGIN OF ...
         BXLE  R1,R2,LOOP2         INSTRUCTION
         B     PRINT               NOT FOUND, WAS BLANK
TESTDC   CLC   0(3,R1),=C'DC '     'DC' ?
         BE    PRINT               YES, PRINT
         CLC   0(3,R1),=C'DS '     'DS' ?
         BE    PRINT               YES, PRINT
         CLC   0(4,R1),=C'CCW '    'CCW' ?
         BE    PRINT               YES, PRINT
         CLC   0(4,R1),=C'CXD '    'CXD' ?
         BE    PRINT               YES, PRINT
         CLC   0(6,R1),=C'USING '  'USING' ?
         BNE   TESTDROP            NO, CHECK 'DROP'
         MVC   USTEST(66),7(R1)    YES, MOVE TO BUFFER
         B     XUSING              PROCESS 'USING'
TESTDROP CLC   0(5,R1),=C'DROP '   DROP ?
         BNE   TESTEND             NO, CHECK 'END'
         MVC   USTEST(66),5(R1)    YES, MOVE TO BUFFER
         B     XDROP               PROCESS 'DROP'
TESTEND  CLC   0(4,R1),=C'END '    'END' ?
         BNE   XINSTR              NO, MUST BE INSTRUCTION
*
         OI    FLAG,ENDBIT         SET 'END STMT PASSED'
         MVC   REGX,MINUS          DASHES TO REG FIELD
         MVC   REGSAV,BLANK        BLANK TO OLD REGFIELD
         B     PRINT               WRITE OUT
*
XUSING   LA    R1,USTEST           BUFFER ADDRESS
         MVC   REGX,PLUS           PLUSES TO REG FIELD
         LA    R3,USTEST+8         CHECK MAX 8 CHAR
LOOP3    CLI   0(R1),C','          COMMA IN OPERAND FOUND ?
         BE    COMMA               YES
         BXLE  R1,R2,LOOP3         CHECK NEXT CHAR
COMMA    LA    R1,1(R1)            SKIP OVER COMMA
NEXTUSI  BAL   R14,CHECK           GET POSITION OF REG
         LA    R3,REGSAV+1(R2)     SET POSITION IN SAVE REG FIELD
         CLI   0(R3),C'.'          WAS PUENKTCHEN ?
         BNE   NOPUNKT             NO, NO USING ON THIS REG BEFORE
         MVC   REGX,STERNE         THIS REG HAS ALREADY 'USING'
NOPUNKT  MVI   0(R3),C'.'          PUENKTCHEN REIN
         LA    R2,REGX+1(R2)         SET POSITION IN REG FIELD
         MVI   0(R2),C'X'          KREUZCHEN REIN
         CLI   1(R1),C','          ENDLICH ZU ENDE ?
         BNE   PRINT               YES, WRITE OUT
         LA    R1,2(R1)            LOOK IF MORE REGS IN 'USING'
         B     NEXTUSI             TRY AGAIN
*
XDROP    LA    R1,USTEST           BUFFER ADDRESS
         MVC   REGX,MINUS          DASHES TO REG FIELD
         LA    R3,USTEST+65        COMPARAND FOR BXLE
LOOP4    CLI   0(R1),C' '          SQUEEZE OUT ...
         BNE   NEXTDROP            POSSIBLE ...
         BXLE  R1,R2,LOOP4         BLANKS
NEXTDROP BAL   R14,CHECK           GET POSITION OF REG
         LA    R3,REGSAV+1(R2)     SET POSITION IN SAVE REG FIELD
         MVI   0(R3),C' '          SET OFF DOTS
         LA    R2,REGX+1(R2)         SET POSITION IN REG FIELD
         MVI   0(R2),C'V'          MOVE 'V' IN ACTUAL LINE
         CLI   1(R1),C','          ENDLICH ZU ENDE ?
         BNE   PRINT               YES, PRINT
         LA    R1,2(R1)            INCR FOR NEXT REG
         B     NEXTDROP            LOOK AGAIN
*
XINSTR   TM    FLAG,RNG            FOR RANGE ONLY ?
         BO    PRINT               YES, PRINT
         MVC   REGSAV,REGX         NEITHER 'USING' NOR 'DROP'
         MVC   INSTEMP(2),INSTR    ZERO FOR TRT INSTR
         TR    INSTEMP(2),TRTAB    MAKE CHAR TO BINARY
         MVO   INSTEMP+1(1),INSTEMP(1)  COMBINE THE HALFBYTES
         MVC   INSTSAV(1),INSTEMP+1  SAVE BIN INSTR CODE
         TR    INSTEMP+1(1),INSTAB GET ACCORDING INSTRUCTION MASK
         TM    INSTEMP+1,X'FF'     VALID INSTRUCTION ?
         BZ    PRINT               NO, PRINT
         MVI   SAVCODE,0
         TM    INSTEMP+1,X'0F'     IS IT SPECIAL INSTR ?
         BNO   INSERT              NO, GO NORMAL
         LA    R1,EXTAB            GET TABLE OF SPECIAL INSTR
LOOP5    CLI   0(R1),X'FF'         NOT FOUND? (SHOULD NEVER OCCUR)
         BE    ABEND2              KRACH....!
         CLC   INSTSAV(1),0(R1)    COMPARE SPECIAL INSTR.
         BE    SPECINST            YES
         LA    R1,3(R1)            ELSE INCR TABLE
         B     LOOP5               AND RETRY
SPECINST MVC   SAVCODE(1),2(R1)    IS SPECIAL, SAVE CODE
         MVC   INSTEMP+1(1),1(R1)  INSERT MASK
INSERT   TM    FLAG,CHG            IS IT FOR 'CHANGE' ?
         BO    CHREG               YES
         IC    R1,INSTEMP+1        ELSE ...
         SRL   R1,4                SHIFT...
         STC   R1,INSTEMP+1        MASK BYTE
CHREG    NI    INSTEMP+1,X'0F'     OFF FIRST HALFBYTE
         MVC   INSTEMP(1),INSTEMP+1 SAVE THE FIELD
         ICM   R1,B'0001',INSTEMP+1  INSERT INSTRUCTION MASK
         ICM   R2,B'1100',RR1      SET 'R1' AND 'R2/X2' FIELDS
         ICM   R2,B'0010',REG1     SET 'B1' FIELD
         ICM   R2,B'0001',REG2     SET 'B2' FIELD
         XC    MASKE,MASKE         ZERO FOR STCM (EXECUTED)
         EX    R1,EXSTCM           EXECUTE STCM INSTRUCTION
         LA    R5,MASKE            ADDRESS OF MASK
         OC    SAVFST(4),=F'-1'    INIT SAVFST AND SAVSEC NEGATVE
         LA    R6,4                THERE ARE FOUR POSSIBLE FIELDS
LOOP     CLI   0(R5),0             IS THIS FIELD PRESENT ?
         BE    SPCHECK             NO, CHECK SPECIAL INSTR
         SR    R2,R2               ZERO FOR TRT
         TRT   0(1,R5),TRTAB       TRANSLATE TO CHAR
         CH    R6,=H'3'            WHICH FIELD IS JUST PROCESSED ?
         BH    FIRST               FIRST
         BL    NOR0                THIRD OR FOURTH
SECOND   STH   R2,SAVSEC           SAVE SECOND FIELD
         CLI   INSTSAV,X'40'       'RR' INSTRUCTION ?
         BL    TESTRR              YES, ACCEPT R0 AS SECOND FIELD
         CLI   INSTSAV,X'7F'       'RX' INSTRUCTION ?
         BNH   NOR0                YES, NEGATE R0 AS SECOND FIELD
         CLI   INSTSAV,X'B1'       SPECIAL CASE 'LRA' INSTR ?
         BE    NOR0                YES, NEGATE R0 AS SECOND FIELD
         B     PROCESS             ELSE ACCEPT R0 FOR RS INSTR
TESTRR   CLI   INSTSAV,X'06'       SPECIAL CASE 'BCTR' INSTR ?
         BE    NOR0                YES, NEGATE R0 AS SECOND FIELD
         B     PROCESS             ELSE ACCEPT R0
FIRST    STH   R2,SAVFST           SAVE FIRST FIELD
         TM    INSTEMP,X'0C'       'S' OR 'SI' INSTR ?
         BZ    NOR0                YES, NEGATE R0
         CLI   INSTEMP,X'06'       'BC' OR FLOATING POINT INSTR ?
         BE    NOR0                YES, NEGATE R0
         CLI   INSTSAV,X'07'       SPECIAL CASE 'BCR' INSTR ?
         BNE   PROCESS             NO, ACCEPT R0 AS SECOND FIELD
NOR0     CLI   0(R5),C'0'          IS IT FOR 'R0' ?
         BE    ENDLOOP             YES, DO NOT PROCESS IT
PROCESS  LA    R2,REGX+1(R2)       GET POSITION IN PRINT FIELD
         MVC   0(1,R2),0(R5)       MOVE IN CHAR
ENDLOOP  LA    R5,1(R5)            NEXT FIELD
         BCT   R6,LOOP             LOOP AGAIN
SPCHECK  CLI   SAVCODE,0           CODE EXIST ?
         BE    PRINT               NO, GO PRINT
         SR    R15,R15             ZERO FOR INSERT
         IC    R15,SAVCODE         INSERT CODE
         B     *(R15)              BRANCH TO PROCESS ...
         B     XTRT                TRT (INSERT 1 + 2)
         B     XEDMK               EDMK (INSERT 2)
         B     XMDSHD              SHIFT DBLE,MR,M,DR,D (INSERT R1+1)
         B     XBXLEBXH            BXLE,BXH (INSERT R3+1)
         B     XLMSTM              LM,STM (INSERT ALL IN RANGE)
         B     XB2                 'B2..' INSTRUCTIONS
         ABEND 3                   HIERHIN DARFST DU NIE KOMMEN!
*
PRINT    MVC   OUTLINE(113),0(R4)  ORIGINAL TEXT
         MVC   OUTLINE+113(18),REGX  PLUS REG FIELD
         LA    R0,OUTLINE          OUTPUT ADDRESS
         PUT   OUTPUT,(0)          WRITE OUT
         MVC   REGX,REGSAV         SAVE REG FIELD
         B     READ                GET NEXT RECORD FROM INPUT
*
CHECK    CLI   0(R1),C'R'          REGISTER FORM ?
         BNE   NUMFORM             NO, NUMERIC
         LA    R1,1(R1)            INCREMENT ONE
NUMFORM  SR    R5,R5               ZERO FOR EVENTAL MULTIPLY
TWODIGIT SR    R2,R2               ZERO FOR INSERT
         NI    0(R1),X'0F'         EBCDIC TO BINARY
         ICM   R2,B'0001',0(R1)    R2 HAS OFFSET COUNT
         AR    R2,R5               ADD IF TWO-DIGIT REG
         TM    1(R1),X'F0'         NEXT A NUMBER ?
         BNOR  R14                 YES, TWO-DIGIT REG
         MH    R2,=H'10'           MAKE FIRST CHAR TO TENS DIGIT
         LA    R1,1(R1)            SHIFT TO ONES DIGIT
         LR    R5,R2               SAVE TENS DIGIT
         B     TWODIGIT            GET ONES DIGIT
*
XTRT     MVI   REGX+2,C'1'         TRT (REG 1 AND ...
XEDMK    MVI   REGX+3,C'2'         REG 2 CHANGED)
         B     PRINT
*
XB2      CLC   RR1(2),=C'0B'       IPK
         BNE   CHECKB2A
         MVC   REGX,REGSAV
         MVI   REGX+3,C'2'         ONLY REG 2 CHANGED
         B     PRINT
CHECKB2A CLC   RR1(2),=C'0D'       PTLB
         BNE   CHECKB2B
         MVC   REGX,REGSAV         NO REGISTER USAGE
         B     PRINT
CHECKB2B CLC   RR1(2),=C'21'       IPTE (NOT SUPPORTED)
         BNE   PRINT
         MVC   REGX,QUEST          SET TO '??...??'
         B     PRINT
*
XMDSHD   LH    R3,SAVFST           SHIFT USW.: GET R1
         LTR   R3,R3               WAS IT COMPUTED AT ALL ?
         BM    PRINT               NO
         TM    SAVFST+1,X'01'      EVEN REGISTER ?
         BNZ   PRINT               NO
         LA    R3,1(R3)            R1+1
         LA    R1,TABX             LOAD TABLE
         AR    R1,R3               GET DISPLACEMENT
         LA    R3,REGX+1(R3)       GET POSITION
         MVC   0(1,R3),0(R1)       PUT IN
         B     PRINT
*
XBXLEBXH LH    R3,SAVSEC           BXLE,BXH: GET R3
         LTR   R3,R3               WAS IT COMPUTED AT ALL ?
         BM    PRINT               NO
         TM    SAVSEC+1,X'01'      EVEN REGISTER ?
         BNZ   PRINT               NO
         LA    R3,1(R3)            R3+1
         LA    R1,TABX             LOAD TABLE
         AR    R1,R3               GET DISPLACEMENT
         LA    R3,REGX+1(R3)       GET POSITION
         MVC   0(1,R3),0(R1)       PUT IN
         B     PRINT
*
XLMSTM   CLI   INSTEMP,0           LM,STM: MASK BYTE ZERO ?
         BE    PRINT               YES, NOTHING MORE TO DO
         SR    R3,R3               ZERO FOR INSERT
         IC    R3,SAVFST+1         GET R1
         LA    R3,1(R3)            DO R1+1
INLOOP   LA    R1,TABX             SCHON WIEDER 'NE TRANSLATE TABLE
         AR    R1,R3               NEXT REG FOR LM/STM
         LA    R7,REGX+1(R3)         GET POSITION
         MVC   0(1,R7),0(R1)       PUT IT IN
         CH    R3,SAVSEC           END OF RANGE REACHED ?
         BE    PRINT               YES, DONE
         CH    R3,=H'15'           WAS CURRENT 'R15' ?
         BE    WRAP                PROCESS WRAPAROUND
         LA    R3,1(R3)            ELSE INCR TO NEXT REG
         B     INLOOP              LOOP AGAIN
WRAP     SR    R3,R3               WRAPAROUND, START WITH R0
         B     INLOOP              LOOP AGAIN
ENDE     SCHL  RET=0               IN DER HEIMAT, DA GIBT'S EIN...
OPENERR  ABEND 1                   WAR NIX..
ABEND2   ABEND 2                   WAR NIX..
EXSTCM   STCM  R2,0,MASKE
SAVFST   DC    H'0'
SAVSEC   DC    H'0'
SAVCODE  DC    C' '
INSTSAV  DC    C' '
TABX     DC    C'0123456789ABCDEF'  TRANSLATE BIN TO CHAR
QUEST    DC    CL18'×????????????????×'
BLANK    DC    CL18' '
REGSAV   DC    CL18'×                ×'
REGX     DC    CL18'×                ×'
STERNE   DC    CL18'×****************×'
MINUS    DC    CL18'×----------------×'
PLUS     DC    CL18'×++++++++++++++++×'
FLAG     DC    X'00'
ENDBIT   EQU   X'80'                'END' HAS BEEN REACHED
CHG      EQU   X'40'                'CHANGE' IN PARM FIELD
RNG      EQU   X'20'                'NONE'   IN PARM FIELD
USTEST   DC    CL66' '
INSLINE  DC    C'0',CL93' '
USTYP    DC    C'CHANGED REGISTERS: ×0123456789ABCDEF×  '
VERSION  DC    C'DESY ASSEMBLER SCANNER V2.0'
INSTEMP  DC    CL2' '
MASKE    DC    F'0'
         LTORG ,
OUTLINE  DC    CL133' '            OUTPUT BUFFER
INSTAB   DC    X'00000000',X'80C8C840',X'C0C800C0',X'0000CCCC' CLCL
         DC    X'C8C8C8C8',X'C8C0C8C8',X'C8C0C8C8',X'FFFFC8C8' SLR
         DC    X'00000000',X'00000000',X'00000000',X'00000000' SWR
         DC    X'00000000',X'00000000',X'00000000',X'00000000' SUR
ZWEIT    DC    X'E0E8E0E8',X'E0E8E860',X'E8E0E8E8',X'E800E0E8' CVB
         DC    X'E0000000',X'E8E0E8E8',X'E8E0E8E8',X'FFFFE8E8' SL
         DC    X'60000000',X'00000060',X'60606060',X'60606060' SW
         DC    X'60000000',X'00000000',X'60606060',X'60606060' SU
DRITT    DC    X'20002000',X'2020FFFF',X'88888888',X'FFFFFFFF' SLDA
         DC    X'FF202020',X'20202020',X'FF000000',X'20202020' TCH
         DC    X'00000000',X'00000000',X'00000000',X'20208820' MC
         DC    X'00E8FF00',X'0000ECEC',X'0000E0E0',X'00A0A0A8' ICM
VIERT    DC    X'00000000',X'00000000',X'00000000',X'00000000' NICHTS
         DC    X'00303030',X'30303030',X'00000000',X'30FF30FF' EDMK
         DC    X'00000000',X'00300000',X'00000000',X'00000000' TPROT
         DC    X'30303030',X'00000000',X'30303030',X'30300000' DP
         ORG   *-193               (NICHTS VERSCHENKEN,TEURER SPEICHER)
TRTAB    DS    193X                CHAR TO BINARY
         DC    X'0A0B0C0D0E0F'     A TO F
         DC    41X'00'
         DC    X'00010203040506070809'   0 TO 9
EXTAB    DS    0H
         DC    X'B2',X'20',AL1(24)       IPK
         DC    X'DD',X'30',AL1(4)        TRT
         DC    X'DF',X'30',AL1(8)        EDMK
         DC    X'1C',X'C8',AL1(12)       MR
         DC    X'1D',X'C8',AL1(12)       DR
         DC    X'5C',X'E8',AL1(12)       M
         DC    X'5D',X'E8',AL1(12)       D
         DC    X'8C',X'88',AL1(12)       SRDL
         DC    X'8D',X'88',AL1(12)       SLDL
         DC    X'8E',X'88',AL1(12)       SRDA
         DC    X'8F',X'88',AL1(12)       SLDA
         DC    X'86',X'E8',AL1(16)       BXH
         DC    X'87',X'E8',AL1(16)       BXLE
         DC    X'90',X'E0',AL1(20)       STM
         DC    X'98',X'EC',AL1(20)       LM
         DC    XL4'FFFFFFFF'
         PRINT NOGEN
INPUT    DCB   DDNAME=INPUT,MACRF=GL,DSORG=PS,EODAD=ENDE
OUTPUT   DCB   DDNAME=OUTPUT,MACRF=PM,DSORG=PS,BLKSIZE=133,LRECL=133,  *
               RECFM=FA
         SPACE 2
INLINE   DSECT                     INPUT LINE
CHAR     DS    CL1
LOCCT    DS    CL6
         DS    CL1
INSTR    DS    CL2
RR1      DS    CL1
RR2      DS    CL1
         DS    CL1
REG1     DS    CL1
         DS    CL4
REG2     DS    CL1
         DS    CL4
ADDR1    DS    CL5
         DS    CL1
ADDR2    DS    CL5
         DS    CL1
NUMFIELD DS    CL5
         DS    CL1
TXTFIELD DS    CL72
USFIELD  DS    CL18
         DS    CL2
         REGISTER
         END
