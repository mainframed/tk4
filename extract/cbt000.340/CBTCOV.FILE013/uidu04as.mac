READVTOC EANF
         B     START                   SURROUND CSECT IDENTIFICATION
         DC    CL44'READVTOC V02L00 SEP 6TH, 1976 SCHMIDT IDUNA'
START    DS    0H
         LR    R2,R1                   SAVE PARMADDR
         LOAD  EP=UCBSCAN
         LR    R12,R0                  SAVE ENTRY POINT ADDRESS
         L     R5,0(R2)                FIRST LOCATOR ADDRESS
         L     R5,0(R5)                FIRST PARAMETER ADDRESS
         MVC   VOLSER,0(R5)            VOLUME SERIAL NUMBER
         L     R5,4(R2)                SECOND LOCATOR ADDRESS
         L     R5,0(R5)                SECOND PARAMETER ADDRESS
         L     R6,12(R2)               FOURTH LOCATOR ADDRESS
         L     R6,0(R6)                FOURTH PARAMETER ADDRESS
         CLC   VTOC,0(R5)              VTOC START REQUESTED
         BE    OBTAIN1                 YES, READ FORMAT4 DSCB
         MVC   CCHHR,0(R5)             GET OLD DSCB ADDRESS
         CLC   ABSTR,0(R6)             SEEK WITH UNMODIFIED TRACK ADDR
         BE    OBTAIN2                 YES, READ WITHOUT CCHHR MODIFIC
         B     NEXTDSCB                SEARCH NEXT DSCB
*
OBTAIN1  EQU   *
         XC    ALA,ALA                 RESET CURRENT UCB ADDRESS
*
UCBCALL  EQU   *
         LR    R15,R12                 UCBSCAN ENTRY POINT ADDRESS
         CALL  (15),(UCBALA,UCBA),VL
         B     *+4(R15)
         B     OK                      UCB FOUND
         B     VTOCEND                 END OF UCB TABLE
*
OK       EQU   *
         USING UCBOB,RUCB              UCB DSECT ADDRESSABILITY
         LA    RUCB,UCBAREA            ADDRESS OF UCB
         CLC   UCBTYP+2(2),=X'2006'    2305-1
         BE    TESTVOL
         CLC   UCBTYP+2(2),=X'2007'    2305-2
         BE    TESTVOL
         CLC   UCBTYP+2(2),=X'2008'    2314 / 2319
         BE    TESTVOL
         CLC   UCBTYP+2(2),=X'2009'    3330
         BE    TESTVOL
         CLC   UCBTYP+2(2),=X'200A'    3340
         BE    TESTVOL
         CLC   UCBTYP+2(2),=X'200B'    3350
         BE    TESTVOL
         CLC   UCBTYP+2(2),=X'200D'    3330-1
         BNE   UCBCALL                 DEVICE TYPE NOT ACCEPTABLE
*
TESTVOL  EQU   *
         CLC   VOLSER,UCBVOLI          THIS VOLUME REQUESTED
         BNE   UCBCALL                 NO, GET NEXT UCB
         DROP  RUCB                    END OF UCB ADDRESSABILITY
         OBTAIN CAMLST1
         B     *+4(R15)                CHECK RETURN CODE
         B     FOUND1                  NORMAL END
         B     MOUNTERR                VOLUME WAS NOT MOUNTED
         B     FOUND1                  F1 DSCB MISSING MEANS F4 DSCB
         B     IOERR                   PERMANENT I/O ERROR
         B     INVAL                   INVALID WORKAREA POINTER
*
FOUND1   EQU   *
         LA    RDSCB,IECSDSL4          ADDRESS OF DSCB
         USING IECSDSL4,RDSCB          DSCB ADDRESSABILITY
         CLI   DS4IDFMT,X'F4'          VERIFY FORMAT4 DSCB
         BNE   F4MISS                  ERROR IF NOT FORMAT4
         MVC   DSCBNO,DS4DEVDT         NUMBER OF DSCB'S ON TRACK
         MVC   CCHHR,DS4HPCHR          HIGHEST FORMAT1 DSCB ADDRESS
         MVC   TRKNUM,DS4DEVSZ+2       NUMBER OF TRACKS ON CYLINDER
         CLC   F4,0(R6)                FORMAT4 DSCB REQUESTED
         BE    PASSF4                  YES, PASS DSCB TO CALLER
         DROP  RDSCB
*
OBTAIN2  EQU   *
         OBTAIN CAMLST2
         B     *+4(R15)                CHECK RETURN CODE
         B     FOUND2                  DSCB FOUND
         B     MOUNTERR                VOLUME WAS NOT MOUNTED
         B     F1MISS                  FORMAT1 DSCB NOT FOUND
         B     IOERR                   PERMANENT I/O ERROR
         B     INVAL                   INVALID WORKAREA POINTER
         B     OUTEX                   SEEK OUT OF VTOC
*
FOUND2   EQU   *
         LA    RDSCB,IECSDSL1          DSCB ADDRESS
         USING IECSDSL1,RDSCB          FORMAT1 DSCB ADDRESSABILITY
         CLI   DS1FMTID,X'F1'          FORMAT1 DSCB
         BE    PASSDATA                PASS DSCB TO CALLER
         CLI   DS1FMTID,X'F4'          END (START) OF VTOC
         BE    VTOCEND                 TERMINATE SCAN
         CLC   ABSTR,0(R6)             SEEK WITH UNMODIFIED TRACK ADDR
         BE    PASSDATA                YES, PASS DSCB TO CALLER
*
NEXTDSCB EQU   *
         XR    R4,R4                   CLEAR WORKREG
         CLI   R,X'01'                 FIRST DSCB ON TRACK
         BE    SUBTRK                  YES, CHANGE TRACK
         IC    R4,R                    CURRENT DSCB RECORD NUMBER
         BCTR  R4,0                    NEXT LOWER RECORD
         STC   R4,R                    STORE RECORD NUMBER
         B     OBTAIN2                 NEXT DSCB
*
SUBTRK   EQU   *
         ICM   R4,15,CCHH              CCHH PART OF ADDRESS
         BCTR  R4,0                    NEXT LOWER TRACK
         STCM  R4,15,CCHH              AND/OR CYLINDER
         MVC   R,DSCBNO                HIGHEST POSSIBLE RECORD NO.
         CLC   HH,FCON                 HAS CYL CHANGED
         BNE   OBTAIN2                 NO, READ NEXT DSCB
         MVC   HH,TRKNUM               HIGHEST TRACK ADDRESS
         XR    R4,R4                   CLEAR WORKREG
         ICM   R4,3,HH                 TRACK ADDRESS
         BCTR  R4,0                    DECREASE BY ONE
         STCM  R4,3,HH                 AND SAVE IT
         B     OBTAIN2                 NEXT DSCB
*
PASSDATA EQU   *
         LA    R15,0                   RETURN CODE ZERO
         MVC   0(5,R5),CCHHR           DSCB ADDRESS TO CALLER
*
DSCBMOVE EQU   *
         L     R5,8(R2)                3RD LOCATOR
         L     R5,0(R5)                3RD PARAMETER ADDRESS
         MVC   0(140,R5),IECSDSL1      DSCB TO CALLER
         B     END                     RETURN TO CALLER
*
VTOCEND  EQU   *
         LA    R15,4                   RETURN CODE 4
         MVC   0(5,R5),=CL5'END'       INDICATE VTOC END
         B     DSCBMOVE                RETURN WITH F4DSCB TO CALLER
*
PASSF4   EQU   *
         LA    R15,0                   RETURN CODE ZERO
         MVC   0(5,R5),=CL5'VTOC'      TO PERMIT NEXT ENTRY
         L     R5,8(R2)                3RD LOCATOR
         L     R5,0(R5)                3RD PARAMETER ADDRESS
         XC    0(140,R5),0(R5)         RESET OLD DATA
         MVC   0(96,R5),IECSDSL4       DSCB4 TO CALLER
         B     END                     RETURN TO CALLER
*
MOUNTERR EQU   *
         LA    R15,8                   RC=8 VOLUME WAS NOT MOUNTED
         MVC   0(5,R5),=CL5'MOUNT'     INDICATE MOUNT ERROR
         B     END                     RETURN TO CALLER
*
F1MISS   EQU   *
*
F4MISS   EQU   *
         LA    R15,12                  RC=12 INCORR FORMAT IDENTIFIER
         MVC   0(5,R5),=CL5'FORM'      INDICATE INCORR FORMAT ID
         B     END                     RETURN TO CALLER
*
IOERR    EQU   *
         LA    R15,16                  RC=16 PERMANENT I/O-ERROR
         MVC   0(5,R5),=CL5'IOERR'     INDICATE I/O-ERROR
         B     END                     RETURN TO CALLER
*
OUTEX    EQU   *
         LA    R15,20                  RC=20 OUT OF VTOC
         MVC   0(5,R5),=CL5'OUTEX'     INDICATE OUT OF VTOC
         B     END                     RETURN TO CALLER
*
INVAL    EQU   *
         LA    R15,24                  RC=24 INVALID WORKAREA POINTER
         MVC   0(5,R5),=CL5'POINT'     INDICATE INVAL WORKA POINTER
*
END      EQU   *
         ERET  RC=(15)
         EJECT
CAMLST1  CAMLST SEARCH,F4DSN,VOLSER,IECSDSL4
CAMLST2  CAMLST SEEK,CCHHR,VOLSER,IECSDSL1
F4DSN    DC    44X'04'                 DSNAME OF F4 DSCB
CCHHR    DS    0CL5                    ABSOLUTE DISK ADDRESS
CCHH     DS    0CL4                    CYLINDER AND HEAD
         DS    CL2
HH       DS    CL2                     HEAD ADDRESS
R        DS    CL1                     RECORD NUMBER
VOLSER   DS    CL6                     VOLUME SERIAL NUMBER
DSCBNO   DS    CL1                     NUMBER OF DSCB'S ON TRACK
TRKNUM   DS    CL2                     NUMBER OF TRACKS ON CYLINDER
VTOC     DC    CL4'VTOC'               TO INDICATE VTOC REQUESTED
ABSTR    DC    CL5'ABSTR'              TO INDICATE SEEK REQUEST
F4       DC    CL2'F4'                 TO INDICATE F4DSCB REQUESTED
FCON     DC    XL2'FFFF'               OWERFLOW CONSTANT
UCBALA   DC    A(ALA)                  UCBSCAN PARAMETER POINTER
UCBA     DC    A(UCBAREA)              UCBSCAN PARAMETER POINTER
ALA      DC    A(0)                    UCBSCAN PARAMETER
UCBAREA  DC    256X'00'                UCB AREA
RUCB     EQU   7                       UCB BASE REGISTER
RDSCB    EQU   7                       DSCB BASE REGISTER
         LTORG
         IECSDSL1 (4)
         DS    CL44                    PADD WORKAREA TO 140 CHAR
         IECSDSL1 (1)
         DSECT
         IEFUCBOB
         END   READVTOC
*/* AN FOLGENDEN TAGEN WURDE DAS PROGRAMM GETESTET */
*/* NEUERSTELLUNG   12. OKT 1976            */
