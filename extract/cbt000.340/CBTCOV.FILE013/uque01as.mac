*          DATA SET GENLIDR1   AT LEVEL 001 AS OF 06/12/75
         TITLE 'AUSGABE DER GENERIERUNGSLISTE '
         SPACE 5
***********************************************************************
*                                                                     *
*  DAS PROGRAMM GIBT DIE IN EINER SEQUENTIELLEN DATEI GESPEICHERTEN   *
*  GENERIERUNGSLISTE AUS, WOBEI EIN BELIEBIGER STEUERCODE IN MASCHI-  *
*  NENCODE UMGEWANDELT WIRD. EINE SEITENNUMERIERUNG WIRD DURCHGEFUEHRT*
*  UND EIN ALPHABETISCH SORTIERTES INHALTSVERZEICHNIS AUFGEBAUT.      *
*                                                                     *
*  UEBER EINE PARM-ENGABE KANN VORGEGEBEN WERDEN, IN WIEVIELFACHER    *
*  AUSGABE DAS INHALTSVERZEICHNIS GEDRUCKT WERDEN SOLL UND WELCHE     *
*  SEITEN AUSGEGEBEN WERDEN SOLLEN.                                   *
*              BEISPIEL:   PARM=(01,0001,1999)                        *
*                                                                     *
*                                          G. NIEMANN                 *
*                                                                     *
***********************************************************************
         SPACE 5
ANFANG   START
         EXIN  BREG=(10,8)
         L     1,0(1)
         CLC   0(2,1),=H'0'             PARM-ANW VORHANDEN ?
         BE    A1
         PACK  DRZAE,2(2,1)       PARAMETER AUFBEREITEN
         PACK  ZSPDW,5(4,1)       B:  PARM=(01,0001,9999)
         CVB   3,ZSPDW                      ×   ×    ×
         SH    3,=H'1'                      ×   ×    -> LETZTE SEITE
         STH   3,ANFWERT                    ×   ------> ERSTE SEITE
         PACK  ZSPDW,10(4,1)                ----------> ANZ LISTE DRU
         CVB   3,ZSPDW
         STH   3,ENDWERT
A1       OPEN  (SYSUT1,,SYSUT2,(OUTPUT))
         GETMAIN VC,LA=LAENGEN,A=IVADR
         L     5,IVADR                  ANF-ADR INHALTSVERZEICHNIS
         L     0,IVADR                  ANF-ADR INHALTSVERZEICHNIS
         A     0,IVLAENGE               ANF-ADR INHALTSVERZEICHNIS
         SH    0,=H'26'
         ST    0,IVENDADR               END-ADR INHALTSVERZEICHNIS
GET1     LA    11,EOF2
         GET   SYSUT1,EBER1
         BAL   12,UPASAMA
         B     ASA
MASCH    LA    4,EBER1
         BAL   12,UPPUT
         B     GET1
ASA      MVC   EBER2(1),EBER1
         TR    EBER2(1),TAB             STEUERZEICHEN UMFORMEN
         LA    4,EBER2
         BAL   12,UPPUT
         MVC   EBER2,EBER1
         LA    11,EOF1
         GET   SYSUT1,EBER1
         BAL   12,UPASAMA
         B     ASA
         MVI   EBER2,X'01'
         LA    4,EBER2
         BAL   12,UPPUT
         MVC   EBER2,EBER3
         B     MASCH
EOF      BR    11
EOF1     MVI   EBER2,X'01'
         LA    4,EBER2
         BAL   12,UPPUT
EOF2     EQU   *
         CLOSE (SYSUT1)
         L     0,IVADR                  ANF-ADR INHALTSVERZEICHNIS
         LA    2,13                     SCHRITTWEITE
         LR    3,5                      END-ADR INHALTSVERZ ASM
         SH    3,=H'26'
         BAL   12,UPSORT
         PUT   SYSUT2,EBER3
IVDRU    L     1,IVADR                  ANF-ADR INHALTSVERZEICHNIS
         LA    2,13                     SCHRITTWEITE
         LR    3,5                      END-ADR INHALTSVERZ ASM
         SH    3,=H'13'
         MVI   EBER4,X'8B'
         MVI   EBER4+1,C' '
         MVC   EBER4+2(119),EBER4+1
         MVI   EBER3+1,C' '
         MVC   EBER3+2(119),EBER3+1
         MVI   EBER3,X'11'
         MVC   EBER3+48(25),=CL25'ASSEMBLER-CROSS-REFERENCE'
         MVI   WIV,X'01'
         BAL   12,UPIVDRU
         L     1,IVADR                  ANF-ADR INHALTSVERZEICHNIS
         LA    2,13                     SCHRITTWEITE
         LR    3,5                      END-ADR INHALTSVERZEICHNIS
         SH    3,=H'13'
         MVI   WIV,X'02'
         MVI   EBER3+1,C' '
         MVC   EBER3+2(119),EBER3+1
         MVC   EBER3+45(30),=CL30'LINKAGE-EDITOR-CROSS-REFERENCE'
         BAL   12,UPIVDRU
         L     1,IVADR                  ANF-ADR INHALTSVERZEICHNIS
         LA    2,13                     SCHRITTWEITE
         LR    3,5                      END-ADR INHALTSVERZEICHNIS
         SH    3,=H'13'
         MVI   WIV,X'03'
         MVI   EBER3+1,C' '
         MVC   EBER3+2(119),EBER3+1
         MVC   EBER3+49(21),=CL21'COPY-CROSS-REFERENCE'
         BAL   12,UPIVDRU
         SP    DRZAE,=P'1'
         BP    IVDRU
         CLI   WABEND,C'1'
         BNE   CLOSE
         ABEND 01,DUMP
CLOSE    CLOSE (SYSUT2)
         EXOUT
         SPACE 3
UPASAMA  TRT   EBER1(1),TAB             TESTEN AUF ASA-ZEICHEN
         BCR   7,12
         LA    12,4(12)
         BR    12
         SPACE 3
UPPUT    MVI   13(5),X'FF'
         MVC   14(13,5),13(5)
         CLC   48(26,4),CKO2
         BNE   B1
         MVI   WASM,C'1'
B1       CLC   3(11,4),CKO3
         BNE   B2
         MVI   WASM,C'0'
B2       CLI   WASM,C'1'
         BNE   C1
         CLC   11(2,4),=C'SD'
         BE    B3
         CLC   11(2,4),=C'LD'
         BE    B3
         CLC   11(2,4),=C'ER'
         BNE   C1
B3       MVI   0(5),X'01'               KZ ASM-WERT
         MVC   1(8,5),1(4)
         MVC   9(2,5),11(4)
         MVC   11(2,5),SEITENZ
         LA    5,13(5)                  INH-INDEX + 13
         C     5,IVENDADR               ABFRAGE AUF INH-VEZ-ENDE
         BNL   ABEND
         B     E1
C1       CLI   FSWITCH,C'1'  FORTSETZUNGSKARTE?
         BE    C100          CHANGE ODER REPLACE
         CLI   FSWITCH,C'2'  FORTSETZUNGSKARTE?
         BNE   C10           NORMALER EINGANG
*              FORTSETZUNGSKARTE FUER INCLUDE
         MVI   FSWITCH,C'0'
         LA    1,15(4)       SPALTE 16 BEGINNT TEXT
         B     C501
C100     MVI   FSWITCH,C'0'
         LA    1,16(4)        SPALTE 16 FOLGEKARTE EINSTELLEN
         B     C601
C10      CLC   1(7,4),=C'IEW0000'
         BNE   D1
         LA    1,8(4)
         TRT   0(20,1),TESTTAB1         TESTEN AUF 1.ALPHA-ZEICHEN
         BE    E1
         CLC   0(4,1),=C'NAME'
         BE    C2
         CLC   0(5,1),=C'ALIAS'
         BE    C2
         CLC   0(5,1),=C'ENTRY'
         BE    C2
         CLC   0(7,1),=C'INCLUDE'
         BE    C5
         CLC   0(7,1),=C'REPLACE'
         BE    C6
         CLC   0(6,1),=C'CHANGE'
         BE    C6
         BNE   D1
C2       MVI   0(5),X'02'               KZ LINK-WERT
         MVC   ZSPLN,0(1)
         MVC   9(2,5),0(1)
         MVC   11(2,5),SEITENZ
         TRT   0(20,1),TESTTAB2         TESTEN AUF 1.BLANK-ZEICHEN
         BE    E1
         TRT   0(20,1),TESTTAB1         TESTEN AUF 1.ALPHA-ZEICHEN
         BE    E1
         LR    2,1
C21      LA    3,8                      ZAEHLER = 8
         MVC   1(8,5),=CL8' '
C3       CLI   0(2),C' '
         BE    C4
         CLI   0(2),C'('
         BE    C4
         CLI   0(2),C','
         BNE   C31
         LA    2,1(2)
         MVI   WLINK,C'1'
         B     C4
C31      MVC   1(1,5),0(2)
         LA    5,1(5)
         LA    2,1(2)
         BCT   3,C3
C4       AR    5,3                      INH-INDEX + ZAEHLER-REST
         LA    5,5(5)                   INH-INDEX + 5
         C     5,IVENDADR               ABFRAGE AUF INH-VEZ-ENDE
         BNL   ABEND
         CLI   WLINK,C'1'
         BNE   E1
         MVI   WLINK,C'0'
         MVI   0(5),X'02'               5Z LINK-WERT
         MVC   9(2,5),ZSPLN
         MVC   11(2,5),SEITENZ
         B     C21
*              INCLUDE       NAME(MEMBER,...,MEMBER)
C5       TRT   0(40,1),TESTTAB4        TESTEN 1. ( ,
         BE    D1
         N     2,=F'255'
         CH    2,=H'107'     KOMMA ?
         BE    C53
C501     EQU   *
         LA    1,1(1)        R1 -> 1. ZEICHEN NACH (
         MVI   0(5),X'02'    CODE FUER LINKWERT
         MVC   1(8,5),=CL8' '          NAME LOESCHEN
         MVC   9(2,5),=C'IN'           CODE FUER INCLUDE
         MVC   11(2,5),SEITENZ
         LR    2,1           R2=R1
         LA    3,8           LAENGE
C50      CLI   0(2),C','     KOMMA ?
         BE    C51
         CLI   0(2),C')'     KLAMMER ZU ?
         BE    C51
         MVC   1(1,5),0(2)   MOVE EIN ZEICHEN DES NAMENS
         LA    5,1(5)
         LA    2,1(2)
         BCT   3,C50
C51      AR    5,3           INH-INDEX+ZAEHLER-REST
         LA    5,5(5)        INH-INDEX+5
         C     5,IVENDADR
         BL    C5
         ABEND 01,DUMP
*              REPLACE       OLDNAME ×(NEW)×,...,OLDNAME×(NEW)×
*              CHANGE
C6       MVC   ZSPLN,0(1)
         TRT   0(40,1),TESTTAB2        TESTEN 1. BLANK
         BE    D1
C601     EQU   *
         TRT   0(40,1),TESTTAB1        TESTEN 1. ALPHAZEICHEN
         BE    D1
C600     MVI   0(5),X'02'    CODE FUER LINKWERT
         MVC   1(8,5),=CL8' '          NAME LOESCHEN
         MVC   9(2,5),ZSPLN  RE ODER CH ALS CODE MOVEN
         MVC   11(2,5),SEITENZ
         LR    2,1
         LA    3,8
C60      CLI   0(2),C','
         BE    C61
         CLI   0(2),C'('
         BE    C61
         CLI   0(2),C')'
         BE    C61
         MVC   1(1,5),0(2)   MOVE EIN ZEICHEN DES NAMENS
         LA    5,1(5)
         LA    2,1(2)
         BCT   3,C60
C61      AR    5,3
         LA    5,5(5)
         C     5,IVENDADR
         BL    C62
         ABEND 01,DUMP
C62      TRT   0(20,1),TESTTAB4        TESTEN ,   (
         BE    D1
         N     2,=F'255'
         CH    2,=H'107'     KOMMA ?
         BE    C63
C620     EQU   *
         LA    1,1(1)        R1 -> 1. ZEICHEN NACH ) ,  (
         B     C600
C53      CLI   1(1),C' '     FORTSETZUNGSKARTE ?
         BNE   C501
         MVI   FSWITCH,C'2'  KENNUNG FUER INCLUDE
         B     E1
C63      CLI   1(1),C' '     FORTSETZUNGSKARTE ?
         BNE   C620
         MVI   FSWITCH,C'1'  KENNUNG FUER REPLACE ODER CHANGE
         B     E1
D1       CLC   1(7,4),=C'IEB154I'
         BE    D11
         CLC   1(7,4),=C'IEB155I'
         BNE   D2
D11      MVI   0(5),X'03'
         MVC   1(8,5),10(4)
         MVC   9(4,5),=C'CN'
         MVC   11(2,5),SEITENZ
         LA    5,13(5)
         C     5,IVENDADR               ABFRAGE AUF INH-VEZ-ENDE
         BL    E1
         B     ABEND
D2       LA    1,1(4)
         TRT   0(40,1),TESTTAB1         TESTEN 1.ALPHA-ZEICHEN
         BE    E1
         CLC   0(6,1),=C'SELECT'
         BNE   E1
         TRT   0(40,1),TESTTAB3         TESTEN 1. (
         BE    E1
         LA    1,1(1)
D21      TRT   0(40,1),TESTTAB3         TESTEN 2.( ODER )
         BE    E1
         N     2,=F'255'
         CH    2,=H'93'      X'00005D'
         BE    E1
         MVI   0(5),X'03'
         MVC   1(8,5),=CL8' '
         MVC   9(2,5),=C'CA'
         MVC   11(2,5),SEITENZ
         LA    1,1(1)
         LR    2,1
         LA    3,8
D3       CLI   0(2),C','
         BE    D4
         CLI   0(2),C')'
         BE    D4
         MVC   1(1,5),0(2)
         LA    5,1(5)
         LA    2,1(2)
         BCT   3,D3
D4       AR    5,3                      INH-INDEX + ZAEHLER-REST
         LA    5,5(5)                   INH-INDEX + 5
         C     5,IVENDADR               ABFRAGE AUF INH-VEZ-ENDE
         BL    D5
         ABEND 01,DUMP
D5       TRT   0(20,1),TESTTAB3         TESTEN )
         BE    E1
         LA    1,1(1)
         B     D21
E1       CLI   0(4),X'89'               VORSCHUB NACH K1 ?
         BE    E2
         CLI   0(4),X'8B'               VORSCHUB NACH K1 ?
         BE    E2
         LR    0,4
         BAL   9,PUT
         BR    12
E2       NI    0(4),X'03'
         OI    0(4),X'E0'               VORSCHUBZEI KANAL12
         LR    0,4
         BAL   9,PUT
         LH    0,SEITENZ
         CVD   0,ZSPDW
         MVC   EBER4+114(6),CKO1
         ED    EBER4+114(6),ZSPDW+5     SEITENNR AUFBEREITEN
         AH    0,=H'1'
         STH   0,SEITENZ
         LA    0,EBER4
         BAL   9,PUT                    SEITENNR DRUCKEN
         BR    12
         SPACE 3
PUT      CLC   SEITENZ,ANFWERT
         BCR   4,9
         CLC   SEITENZ,ENDWERT
         BCR   2,9
         PUT   SYSUT2,(0)
         BR    9
         SPACE 3
UPSORT   LR    1,0
         MVI   WSORT,C'1'
S2       CLC   0(13,1),13(1)
         BNH   S3
         XC    0(13,1),13(1)
         XC    13(13,1),0(1)
         XC    0(13,1),13(1)
         MVI   WSORT,C'2'
S3       BXLE  1,2,S2
         CLI   WSORT,C'2'
         BE    UPSORT
         BR    12
         SPACE 3
UPIVDRU  CLC   0(1,1),WIV
         BE    IVD1
         BXLE  1,2,UPIVDRU
         BR    12
IVD1     ZAP   ZZAE,=P'60'
         MVC   EBER1,EBER3
         STM   1,2,ZSPDW
         PUT   SYSUT2,EBER4
         PUT   SYSUT2,EBER1
         LM    1,2,ZSPDW
         MVI   EBER1,X'09'
IVD2     LA    7,EBER1+1
         LA    8,5
         MVI   EBER1+1,C' '
         MVC   EBER1+2(119),EBER1+1
         LR    6,1
IVD3     CLC   0(1,6),WIV
         BNE   IVD4
         MVC   0(8,7),1(6)
         MVC   8(2,7),=C'  '
         MVC   10(2,7),9(6)
         MVC   ZSPHW,11(6)
         LH    0,ZSPHW
         CVD   0,ZSPDW
         MVC   12(6,7),CKO1
         ED    12(6,7),ZSPDW+5
         LA    7,25(7)
         LA    6,780(6)                 60 * 13
         BCT   8,IVD3
IVD4     STM   1,2,ZSPDW
         PUT   SYSUT2,EBER1
         LM    1,2,ZSPDW
         LA    1,13(1)
         CLC   0(1,1),WIV
         BNE   UPIVDRU
         SP    ZZAE,=P'1'
         BP    IVD2
         LA    1,3120(1)                60 * 13 * 4
         B     IVD1
         SPACE 3
ABEND    MVI   WABEND,C'1'
         B     EOF2
         SPACE 3
ZSPDW    DC    D'0'
LAENGEN  DC    A(10*13,10000*13)        MIN-LAENGE/MAX-LAENGE
IVADR    DC    A(0)                     ADRESSE
IVLAENGE DC    A(0)                     LAENGE
IVENDADR DC    A(0)
ZSPHW    DC    H'0'
SEITENZ  DC    H'0'
ANFWERT  DC    H'0'
ENDWERT  DC    H'32767'
ZSPLN    DC    C'  '
ZZAE     DC    P'50'
DRZAE    DC    P'0001'
CKO1     DC    X'402020202020'
CKO2     DC    C'EXTERNAL SYMBOL DICTIONARY'
CKO3     DC    C'LOC  OBJECT'
WIV      DC    X'00'
FSWITCH  DC    C'0'
WASM     DC    C'0'
WLINK    DC    C'0'
WSORT    DC    C' '
WABEND   DC    C' '
         SPACE 3
EBER1    DS    0CL133
         DC    X'89'
         DC    CL132' '
EBER2    DS    0CL133
         DC    X'89'
         DC    CL132' '
EBER3    DS    0CL133
         DC    X'89'
         DC    CL132' '
EBER4    DS    0CL133
         DC    X'89'
         DC    CL132' '
         SPACE 3
TAB      DC    64X'00'
         DC    X'09'
         DC    13X'00'
         DC    X'01'
         DC    17X'00'
         DC    X'19'
         DC    143X'00'
         DC    X'11899199A1A9B1B9C1C9'
         DC    6X'00'
TESTTAB1 DC    64X'FF'
         DC    X'00'
         DC    191X'FF'
TESTTAB2 DC    64X'00'
         DC    X'FF'
         DC    191X'00'
TESTTAB3 DC    77X'00'
         DC    X'4D'
         DC    15X'00'
         DC    X'5D'
         DC    162X'00'
         SPACE 3
TESTTAB4 DC    77X'00'
         DC    X'4D'
         DC    29X'00'
         DC    X'6B'
         DC    148X'00'
         SPACE 3
         LTORG
SYSUT1   DCB   DDNAME=SYSUT1,                                          *
               DSORG=PS,                                               *
               MACRF=(GM),                                             *
               EODAD=EOF
SYSUT2   DCB   DDNAME=SYSUT2,                                          *
               DSORG=PS,                                               *
               MACRF=(PM)
         END
