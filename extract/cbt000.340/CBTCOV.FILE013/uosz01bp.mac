./  ADD        NAME=UXXX01BP
MD3002   THE TSO USAGE PROGRAM IS DESIGNED TO PRODUCE SUMMARY AND
         REPORTS ON TSO UTILISATION OVER A GIVEN PERIOD TO ENABLE
         AN ASSESSMENT TO BE MADE OF THE IMPACT OF THE WORK RUN
         UNDER TSO WITHIN AN INSTALLATION.
         THE PROGRAM INITIALLY READS AN OPTIONAL CONTROL CARD
         SPECIFYING THE RANGE OF DATES TO BE USED. IF THIS CARD
         IS NOT PROVIDED ALL TSO DATA PROVIDED WILL BE ANALYSED.
         THE INPUT FILE (SMFDATA) IS THEN READ AND TSO STEP END
         RECORDS (TYPE 34) ARE SELECTED BY TYPE AND DATE AND
         REFORMED INTO FIXED LENGTH RECORDS. THE FIXED LENGTH
         RECORDS ARE WRITTEN TO A WORK FILE WHICH IS USED AS A
         INPUT TO THE PL/I INTERNAL SORT (PLISRTA). THE RECORDS
         ARE SORTED INTO ASCENDING SEQUENCE BY LOG-ON TIME
         WITHIN LOG-ON CODE WITHIN LOGON DATE.
         THE SORTED FILE OF TSO RECORDS IS THEN READ AND SELECTED
         DETAILS BY DAY ARE LISTED OUT AN THE DAILY TSO USAGE
         DATAIL REPORT. AT THE END OF EACH DAY'S DATA, A HISTOGRAM
         OF CPU UTILISATION (UNDER TCBS) IS PRODUCED SHOWING THE
         DISTRIBUTION OF USAGE BETWEEN 0700 AND 1900 O'CLOCK. AS
         EACH RECORD IS READ, DETAILS ARE ACCUMULATED FOR REPORTS
         WHICH SUMMARIES TSO UTILISATION OVER THE COMPLETE PERIOD
         OF DATA.
         WHEN ALL THE TSO DATA HAS BEEN PROCESSED, THE PROGRAM     ***/
         PRODUCES THREE SUMMARY REPORTS. THE FIRST REPORT IS A     ***/
         SUMMARY OF TSO USAGE BY USER OVER THE PERIOD OF DATA.     ***/
         THIS REPORT SHOWS TOTAL AND AVERAGE UTILISATION BY CPU,   ***/
         SESSION AND TRANSACT. TIMES.  THE SECOND REPORT SHOWS     ***/
         A DISTRIBUTION OF CPU UTILISATION BY LENGTH OF SESSION    ***/
         FOR EACH USER.   THE THIRD REPORT PROVIDES A HISTOGRAM    ***/
         OF THE AVERAGE CPU UTILISATIONS BETWEEN 0700 AND 1900     ***/
         HOURS OVER THE COMPLETE PERIOD OF DATA.                   ***/
         THIS PROGRAM WAS ORIGINALLY WRITTEN IN SEPTEMBER 1974     ***/
         IN THE KITWE BRANCH OF IBM ZAMBIA.                        ***/
MD3002   TSO USAGE REPORT PROGRAM
 TSOPRNT: PROC OPTIONS(MAIN);
 /***     FILE DECLARATIONS FOR TSOPRNT INPUT/OUTPUT FILES          ***/
 /***                                                               ***/
         DCL SYSIN   FILE RECORD INPUT;
         DCL SYSIN1  FILE RECORD INPUT;
         DCL SYSIN2  FILE RECORD INPUT;
          DCL SMFDATA FILE RECORD INPUT;
          DCL PRINT   FILE RECORD OUTPUT;
          DCL PRINTA  FILE RECORD OUTPUT;
          DCL WORKOUT FILE RECORD OUTPUT;
          DCL WORKIN  FILE RECORD INPUT;
 /***                                                               ***/
 /***     INTERMEDIATE TSO STEP END WORK RECORD                     ***/
 /***                                                               ***/
          DCL 1 TSREC STATIC,
               2 XTYP    CHAR(1),
               2 XDAT    FIXED(5),
               2 XJBN    CHAR(8),
               2 XLOGA   FIXED(11),
               2 XSERV   FIXED(11),
               2 FILXV   CHAR(2),
               2 XSTM    FIXED(11),
               2 XFTM    FIXED(11),
               2 XPGM    CHAR(8),
               2 XSPN    CHAR(8),
               2 XSNO    FIXED(3),
               2 XPRI    FIXED(3),
               2 THOA    FIXED(5),
               2 THOU    FIXED(5),
               2 TPUT    FIXED(7),
               2 XCPU    FIXED(9),
               2 TGET    FIXED(11),
               2 XGRUP   CHAR(1);
 /***                                                               ***/
 /***     SMF RECORD TYPE 34 - TS STEP TERMINATION - VS2 REL 1      ***/
 /***                                                               ***/
          DCL SMFREC CHAR(6500) VARYING STATIC;
          DCL 1 SMF34    BASED(SMFPTR),
               2 S34LGTH   FIXED BIN(15) UNAL,  /* RECORD PREFIX      */
               2 S34SIND   CHAR(1),             /* SYSTEM IND - X'02' */
               2 S34RTYPE  CHAR(1),             /* RECORD TYPE - 34   */
               2 S34TIME   FIXED BIN(31) UNAL,  /* STEP END TIME      */
               2 S34DATE   FIXED(7),            /* STEP END DATE      */
               2 S34SYSID  CHAR(4),             /* SYSTEM IDENTIFIER  */
               2 S34USRID  CHAR(8),             /* USER-ID            */
               2 S34LOGT   FIXED BIN(31) UNAL,  /* LOG ON TIME        */
               2 S34LOGD   FIXED(7),            /* LOG ON DATE        */
               2 S34UFLD   CHAR(8) ,            /* RESERVED FOR USER  */
               2 S34STNO   CHAR(1),             /* STEP NUMBER        */
               2 S34SOCCT  FIXED BIN(31) UNAL,  /* CORE TRANSACT. TIME*/
               2 S34TPUTS  FIXED BIN(31) UNAL,  /* TPUTS ISSUED       */
               2 S34TGETS  FIXED BIN(31) UNAL,  /* TGETS ISSUED       */
               2 S34SCOMP  CHAR(2),             /* STEP COMP CODE     */
               2 S34SPRTY  CHAR(1),             /* STEP PRIORITY      */
               2 S34TMP    CHAR(8),             /* TERM MONITOR NAME  */
               2 S34SNAME  CHAR(8),             /* STEP NAME (PROC)   */
               2 S34REGN   FIXED BIN(15) UNAL,  /* PRIV AREA SIZE     */
               2 S34RESV1  CHAR(2),             /* RESERVED           */
               2 S34CORE   FIXED BIN(15) UNAL,  /* STORAGE USED       */
               2 S34RESV2  CHAR(6),             /* RESERVED           */
               2 S34SPKEY  CHAR(1),             /* STORAGE PROT KEY   */
               2 S34STIND  CHAR(1),             /* STEP TERM INDS     */
               2 S34RESV3  CHAR(2),             /* RESERVED           */
               2 S34DALLT  FIXED BIN(31) UNAL,  /* DEVICE ALLOC TIME  */
               2 S34PPLDT  FIXED BIN(31) UNAL,  /* PROB PGM LOAD TIME */
               2 S34RESV4  CHAR(1),             /* RESERVED           */
               2 S34CPUS   CHAR(3),             /* CPU TIME (SRB)     */
               2 S34RECIND CHAR(2),             /* RECORD INDICATOR   */
               2 S34ROFF   FIXED BIN(15) UNAL,  /* RELOC SECT OFFSET  */
               2 S34DLGTH  FIXED BIN(15) UNAL,  /* DEVICE SECT LENGTH */
               2 S34DEVS(100),                  /* DEVICE ENTRIES     */
                3 S34DTYPE CHAR(2),             /* UCB CLASS/TYPE     */
                3 S34DADDR CHAR(2),             /* CHANNEL/UNIT ADDR  */
                3 S34EXCPS FIXED BIN(31) UNAL;  /* EXCP COUNT         */
 /***                                                               ***/
 /***     SMF RECORD TYPE 34 - ACCOUNTING SECTION                   ***/
 /***                                                               ***/
          DCL 1 SMF34A   BASED(SMFPTRA),
               2 S34LGTHA  CHAR(1),             /* LGTH OF FOLLOWING  */
               2 S34CPUT   CHAR(3),             /* CPU TIME (TCB)     */
               2 S34ALGTH  CHAR(1),             /* NO OF ACCT FIELDS  */
               2 S34ACCT   CHAR(256);           /* ACCOUNTING FIELDS  */
 /***                                                               ***/
 /***     SMF RECORD TYPE 34 - RELOCATE SECTION                     ***/
 /***                                                               ***/
          DCL 1 SMF34B   BASED(SMFPTRB),
               2 S34PGINS  FIXED BIN(31) UNAL,  /* PAGE-INS           */
               2 S34PGOTS  FIXED BIN(31) UNAL,  /* PAGE-OUTS          */
               2 S34SWAPS  FIXED BIN(31) UNAL,  /* SWAPS              */
               2 S34TSPIN  FIXED BIN(31) UNAL,  /* TSO SWAP PAGE-INS  */
               2 S34TSPOT  FIXED BIN(31) UNAL,  /* TSO SWAP PAGE-OUTS */
               2 S34VPI    FIXED BIN(31) UNAL,  /* VIO PAGE IN        */
               2 S34VPO    FIXED BIN(31) UNAL,  /* VIO PAGE OUT       */
               2 S34SST    FIXED BIN(31) UNAL,  /* STEP SERVICE UNITS */
               2 S34ACT    FIXED BIN(31) UNAL,  /* STEP TRANSACCTION  */
               2 S34PGNO   FIXED BIN(15) UNAL,  /* STEP PERFORMANCE   */
               2 S34TRANT  FIXED BIN(31) UNAL,  /* STP TRANSA RESIDENC*/
               2 S34RECLM  FIXED BIN(31) UNAL,  /* NR RECLAIMS        */
               2 S34RCLAM  FIXED BIN(31) UNAL,  /* NR VIO RECLAIMS    */
               2 S34CPGIN  FIXED BIN(31) UNAL,  /* NR COMMON AREA SPAC*/
               2 S34CRECL  FIXED BIN(31) UNAL,  /* NR COMMON AREA RECL*/
               2 S34PGSTL  FIXED BIN(31) UNAL,  /* NR PAGES STOLEN    */
               2 S34GESC   CHAR(8); /* NOT VALID   NR PAGE SECONDS    */
 /***                                                               ***/
 /***     HEADINGS AND PRINT LINES FOR TSO USAGE REPORT             ***/
 /***                                                               ***/
          DCL 1 HEAD1 STATIC,
               2 H11 CHAR(7)  INIT('1DATE: '),
               2 H12 PIC'99.999',
               2 H13 CHAR(39) INIT(' '),
               2 H14 CHAR(81) INIT('T S O   U S A G E   R E P O R T');
          DCL 1 HEAD2 STATIC,
               2 H21 CHAR(1)  INIT('0'),
               2 H22 CHAR(33) INIT('USERID    STEPNAME   START TIME  '),
               2 H23 CHAR(33) INIT(' END TIME   SESSION  TRANSACTION '),
               2 H24 CHAR(33) INIT(' SERVICE      CPU       % CPU    '),
               2 H25 CHAR(33) INIT('  % CPU     PRIVATE   CORE  TPUTS');
          DCL 1 HEAD3 STATIC,
               2 H31 CHAR(1)  INIT(' '),
               2 H32 CHAR(33) INIT('                      HH.MM.SS   '),
               2 H33 CHAR(33) INIT(' HH.MM.SS   HH.MM.SS   HH.MM.SS  '),
               2 H34 CHAR(33) INIT('  UNITS    MM.SS.TT   (SESSION) ('),
               2 H35 CHAR(33) INIT('TRANSACTION)  AREA    USED       ');
          DCL 1 LINE1 STATIC,
               2 L11 CHAR(1)  INIT('0'),
               2 L12 CHAR(8),
               2 L13 CHAR(2)  INIT(' '),
               2 L16 CHAR(8),
               2 L17 CHAR(4)  INIT(' '),
               2 L18 CHAR(8)  INIT('HH.MM.SS'),
               2 L19 CHAR(4)  INIT(' '),
               2 L1A CHAR(8)  INIT('HH.MM.SS'),
               2 L1B CHAR(3)  INIT(' '),
               2 L1C CHAR(8)  INIT('HH.MM.SS'),
               2 L1D CHAR(3)  INIT(' '),
               2 L1E CHAR(8)  INIT('HH.MM.SS'),
               2 L1R PIC'ZZZZZZZZ9',
               2 L1S CHAR(2)  INIT(' '),
               2 L1G CHAR(10) INIT('MMMM.SS.TT'),
               2 L1H CHAR(4)  INIT(' '),
               2 L1I PIC'ZZ9.V99',
               2 L1J PIC'(8)Z9.V99',
               2 L1K CHAR(5)  INIT(' '),
               2 L1L PIC'ZZZZ9',
               2 L1M CHAR(2)  INIT('K'),
               2 L1N PIC'ZZZZ9',
               2 L1O CHAR(1)  INIT('K'),
               2 L1P PIC'ZZZZZ9',
               2 L1Q CHAR(1)  INIT(' ');
 /***                                                               ***/
 /***     HEADINGS AND PRINT LINES FOR DB2 EXTERN USUAGE REPORT     ***/
 /***                                                               ***/
          DCL 1 HEAD1A STATIC,
               2 HB1 CHAR(7)  INIT('1      '),
               2 HB2 CHAR(6) INIT(' '),
               2 HB3 CHAR(30) INIT(' '),
               2 HB4 CHAR(31) INIT('E X T E R N   U S U A G E   O '),
               2 HB5 CHAR(59) INIT('F   P R O C   D B 2');
          DCL 1 HEAD2A STATIC,
               2 HC1 CHAR(1)  INIT('0'),
               2 HC2 CHAR(33) INIT('USERID     DATE      START TIME  '),
               2 HC3 CHAR(33) INIT(' END TIME   SESSION  TRANSACTION '),
               2 HC4 CHAR(33) INIT(' SERVICE      CPU       % CPU    '),
               2 HC5 CHAR(33) INIT('  % CPU     PRIVATE   CORE  TPUTS');
          DCL 1 HEAD3A STATIC,
               2 HD1 CHAR(1)  INIT(' '),
               2 HD2 CHAR(33) INIT('                      HH.MM.SS   '),
               2 HD3 CHAR(33) INIT(' HH.MM.SS   HH.MM.SS   HH.MM.SS  '),
               2 HD4 CHAR(33) INIT('  UNITS    MM.SS.TT   (SESSION) ('),
               2 HD5 CHAR(33) INIT('TRANSACTION)  AREA    USED       ');
 /***                                                               ***/
 /***     HEADINGS AND PRINT LINES FOR SUMMARY OF USAGE BY USER     ***/
 /***                                                               ***/
          DCL 1 HEADA STATIC,
               2 HA1 CHAR(39) INIT('1'),
               2 HA2 CHAR(30) INIT('S U M M A R Y   O F   T S O   '),
               2 HA3 CHAR(64) INIT('U S A G E   B Y   U S E R');
          DCL 1 HEADB STATIC,
               2 HB1 CHAR(1)  INIT('0'),
               2 HB2 CHAR(33) INIT('USERID    NO OF  TOTAL SESSION  T'),
               2 HB3 CHAR(33) INIT('OTAL TRANSACT.  TOTAL SERVICE  TO'),
               2 HB4 CHAR(33) INIT('TAL   CPU    AVERAGE SESSION  AVE'),
               2 HB5 CHAR(33) INIT('RAGE TRANSACT.    % CPU    % CPU ');
          DCL 1 HEADC STATIC,
               2 HC1 CHAR(1)  INIT(' '),
               2 HC2 CHAR(33) INIT('          STEPS    HH.MM.SS      '),
               2 HC3 CHAR(33) INIT('  HH.MM.SS          UNITS        '),
               2 HC4 CHAR(33) INIT(' MM.SS.TT        HH.MM.SS        '),
               2 HC5 CHAR(33) INIT(' HH.MM.SS      (SESSION)  (TRANS)');
          DCL 1 LINEA STATIC,
               2 LA1 CHAR(2)  INIT('0'),
               2 LA2 CHAR(8),
               2 LA3 PIC'(4)Z9',
               2 LA4 CHAR(3)  INIT(' '),
               2 LA5 CHAR(10) INIT('HHHH.MM.SS'),
               2 LA6 CHAR(6)  INIT(' '),
               2 LA7 CHAR(10) INIT('HHHH.MM.SS'),
               2 LA8 CHAR(5)  INIT(' '),
               2 LAJ PIC'ZZZZZZZZZ9',
               2 LAK CHAR(7)  INIT(' '),
               2 LA9 CHAR(10) INIT('MMMM.SS.TT'),
               2 LAA CHAR(6)  INIT(' '),
               2 LAB CHAR(10) INIT('HHHH.MM.SS'),
               2 LAC CHAR(7)  INIT(' '),
               2 LAD CHAR(10) INIT('HHHH.MM.SS'),
               2 LAE CHAR(9) INIT(' '),
               2 LAF PIC'ZZ9.V99',
               2 LAG CHAR(3)  INIT(' '),
               2 LAH PIC'ZZ9.V99';
 /***                                                               ***/
 /***     HEADINGS AND PRINT LINES FOR SUMMARY OF USAGE BY REFERAT  ***/
 /***                                                               ***/
          DCL 1 HEADR STATIC,
               2 HR1 CHAR(39) INIT('1'),
               2 HR2 CHAR(30) INIT('S U M M A R Y   O F   T S O   '),
               2 HR3 CHAR(30) INIT('U S A G E   B Y   D E P A R T '),
               2 HR4 CHAR(34) INIT('M E N T S                     ');
          DCL 1 HEADS STATIC,
               2 HS1 CHAR(1)  INIT('0'),
               2 HS2 CHAR(33) INIT('USERID    NO OF  TOTAL SESSION  T'),
               2 HS3 CHAR(33) INIT('OTAL TRANSACT.  TOTAL SERVICE  TO'),
               2 HS4 CHAR(33) INIT('TAL   CPU    AVERAGE SESSION  AVE'),
               2 HS5 CHAR(33) INIT('RAGE TRANSACT.    % CPU    % CPU ');
          DCL 1 HEADT STATIC,
               2 HT1 CHAR(1)  INIT(' '),
               2 HT2 CHAR(33) INIT('         SESSION   HH.MM.SS      '),
               2 HT3 CHAR(33) INIT('  HH.MM.SS          UNITS        '),
               2 HT4 CHAR(33) INIT(' MM.SS.TT        HH.MM.SS        '),
               2 HT5 CHAR(33) INIT(' HH.MM.SS      (SESSION)  (TRANS)');
          DCL 1 LINER STATIC,
               2 LR1 CHAR(2)  INIT('0'),
               2 LR2 CHAR(8),
               2 LR3 PIC'(4)Z9',
               2 LR4 CHAR(3)  INIT(' '),
               2 LR5 CHAR(10) INIT('HHHH.MM.SS'),
               2 LR6 CHAR(6)  INIT(' '),
               2 LR7 CHAR(10) INIT('HHHH.MM.SS'),
               2 LR8 CHAR(5)  INIT(' '),
               2 LRJ PIC'ZZZZZZZZZ9',
               2 LRK CHAR(7)  INIT(' '),
               2 LR9 CHAR(10) INIT('MMMM.SS.TT'),
               2 LRA CHAR(6)  INIT(' '),
               2 LRB CHAR(10) INIT('HHHH.MM.SS'),
               2 LRC CHAR(7)  INIT(' '),
               2 LRD CHAR(10) INIT('HHHH.MM.SS'),
               2 LRE CHAR(9) INIT(' '),
               2 LRF PIC'ZZ9.V99',
               2 LRG CHAR(3)  INIT(' '),
               2 LRH PIC'ZZ9.V99';
          DCL 1 LINEU STATIC,
               2 LS1 CHAR(2)  INIT('0'),
               2 LS2 CHAR(8),
               2 LS3 PIC'(4)Z9',
               2 LS4 CHAR(8) INIT(' %'),
               2 LS5 PIC'(4)Z9',
               2 LS6 CHAR(11) INIT(' %'),
               2 LS7 PIC'(4)Z9',
               2 LS8 CHAR(10) INIT(' %'),
               2 LSJ PIC'(4)Z9',
               2 LSK CHAR(12) INIT(' %'),
               2 LS9 PIC'(4)Z9',
               2 LSA CHAR(6)  INIT(' %'),
               2 LSB CHAR(51) INIT(' ');
 /***                                                               ***/
 /***     HEADINGS AND PRINT LINES FOR TIME OF DAY DISTRIBUTION     ***/
 /***                                                               ***/
          DCL 1 HEADM STATIC,
               2 HM1 CHAR(7)  INIT('1DATE: '),
               2 HM2 PIC'99.999',
               2 HM3 CHAR(29) INIT(' '),
               2 HM4 CHAR(23) INIT('DISTRIBUTION OF TSO CPU'),
               2 HM5 CHAR(68) INIT(' UTILISATION BY TIME OF DAY');
          DCL 1 HEADN BASED(HEADM_PTR),
               2 HN1 CHAR(1),
               2 HN2 PIC'Z9',
               2 HN3 CHAR(12);
          DCL 1 HEADP STATIC,
               2 HP1 CHAR(1)  INIT('-'),
               2 HP2 CHAR(4)  INIT(' '),
               2 HP3 CHAR(11),
               2 HP4 CHAR(117) INIT(' ');
          DCL 1 LINEM STATIC,
               2 LM1 CHAR(9)  INIT('-'),
               2 LM2 CHAR(1)  INIT('×'),
               2 LM3(120) CHAR(1),
               2 LM4 CHAR(3)  INIT(' ');
          DCL 1 LINEN BASED(LINEM_PTR),
               2 LN1 CHAR(2),
               2 LN2 PIC'ZZ9.V99',
               2 LN3 CHAR(1),
               2 LN4 CHAR(1),
               2 LN5 CHAR(120);
         DCL 1 FOOTM STATIC,
               2 FM1 CHAR(1)  INIT(' '),
               2 FM2 CHAR(33) INIT('         +---------+---------+---'),
               2 FM3 CHAR(33) INIT('------+---------+---------+------'),
               2 FM4 CHAR(33) INIT('---+---------+---------+---------'),
               2 FM5 CHAR(33) INIT('+---------+---------+---------+  ');
          DCL 1 FOOTN STATIC,
               2 FN1 CHAR(1)  INIT(' '),
               2 FN2 CHAR(33) INIT('        0700      0800      0900 '),
               2 FN3 CHAR(33) INIT('     1000      1100      1200    '),
               2 FN4 CHAR(33) INIT('  1300      1400      1500      1'),
               2 FN5 CHAR(33) INIT('600      1700      1800      1900');
 /***                                                               ***/
 /***     HEADINGS AND PRINT LINES FOR USAGE BY LENGTH OF SESSION   ***/
 /***                                                               ***/
          DCL 1 HEADX STATIC,
               2 HX1 CHAR(47) INIT('1'),
               2 HX2 CHAR(21) INIT('TSO CPU UTILISATION '),
               2 HX3 CHAR(65) INIT('BY LENGTH OF SESSION');
          DCL 1 HEADY STATIC,
               2 HY1 CHAR(1)  INIT('0'),
               2 HY2 CHAR(33) INIT('  USERID    (--------------------'),
               2 HY3 CHAR(33) INIT('---------------------------SESSIO'),
               2 HY4 CHAR(33) INIT('N TIME IN MINUTES----------------'),
               2 HY5 CHAR(33) INIT('--------------------------------)');
          DCL 1 HEADZ STATIC,
               2 HZ1 CHAR(1)  INIT(' '),
               2 HZ2 CHAR(33) INIT('              0-5     5-10   10-1'),
               2 HZ3 CHAR(33) INIT('5   15-20   20-30   30-40   40-50'),
               2 HZ4 CHAR(33) INIT('   50-60   60-80  80-100  100-120'),
               2 HZ5 CHAR(33) INIT(' 120-150 150-180 180-240  > 240  ');
          DCL 1 LINEY STATIC,
               2 LY1 CHAR(3)  INIT('0'),
               2 LY2 CHAR(8),
               2 LY3(15) PIC'(4)Z9.V99',
               2 LY4 CHAR(2)  INIT(' ');
          DCL 1 LINEZ BASED(LINEY_PTR),
               2 LZ1 CHAR(11),
               2 LZ2(15) CHAR(8);
 /***                                                               ***/
 /***     MISCELLANEOUS WORK AREAS AND CONSTANTS USED BY TSOPRNT    ***/
 /***                                                               ***/
          DCL (ADDR,SUBSTR) BUILTIN;
          DCL (PLIDUMP,PLISRTA) BUILTIN;
          DCL LINES FIXED BIN(15) STATIC INIT(70);
          DCL LINET FIXED BIN(15) STATIC INIT(70);
         DCL F1 FIXED(15,4) STATIC;
          DCL (F2,F3) FIXED(7) STATIC;
          DCL (F4,F5) FIXED(9) STATIC;
          DCL CHAR10 CHAR(10) STATIC INIT('MMMM.SS.TT');
         DCL 1 CHAR10A DEF CHAR10,
               2 CX1     PIC'ZZZ9',
               2 CX2     CHAR(1),
               2 CX3     PIC'99',
               2 CX4     CHAR(1),
               2 CX5     PIC'99';
          DCL 1 USER_STATS(100) STATIC,
               2 UID     CHAR(8),
               2 UNO     FIXED(7),
               2 UEL     FIXED(15),
               2 UOC     FIXED(15),
               2 USERV   FIXED(15),
               2 UCPU    FIXED(15);
          DCL (I,J,K,L,M,N) FIXED BIN(31) STATIC INIT(0);
          DCL NU FIXED BIN(31) STATIC INIT(0);
          DCL USE(100) CHAR(8) STATIC;
          DCL 1 CARD1 STATIC,                /* PARM FOR USERS*/
               2 CARD1U CHAR(8),
               2 CARD1R CHAR(72);
          DCL CHAR9 CHAR(10) STATIC INIT('HHHH.MM.SS');
          DCL 1 CHAR9A DEF CHAR9,
               2 C91 PIC'ZZZ9',
               2 C92 CHAR(1),
               2 C93 PIC'99',
               2 C94 CHAR(1),
               2 C95 PIC'99';
          DCL (FA,FB) FIXED(11) STATIC;
         DCL (FC,FD) FIXED(15) STATIC;
          DCL SFIELD CHAR(45) STATIC;
          DCL RFIELD  CHAR(28) STATIC;
          DCL RETCDE  FIXED BIN(31) STATIC INIT(0);
          DCL OLD_DATE FIXED(5) STATIC INIT(0);
         DCL FTIME FIXED(15) STATIC;
          DCL LINEY_PTR PTR STATIC;
          DCL SLIMS(15) FIXED(11) STATIC INIT
           (5,10,15,20,30,40,50,60,80,100,120,150,180,240,99999999999);
          DCL 1 ELCPU(100) STATIC,
               2 ECEL(15)     FIXED(15),
               2 ECCPU(15)    FIXED(15);
          DCL REFTAB(256) CHAR(8);  /* FUER MAX 256 USERS */
          DCL HRSTATS(240)  FIXED(7,2) STATIC INIT((240)0);
          DCL THRSTATS(240) FIXED(7,2) STATIC INIT((240)0);
          DCL HRMAX FIXED(7,2) STATIC INIT(0);
          DCL RETLAB LABEL;
         DCL LINEM_PTR PTR STATIC;
          DCL HEADM_PTR PTR STATIC;
         DCL HS(240) FIXED(7,2) BASED(HS_PTR);
          DCL HS_PTR PTR STATIC;
          DCL PRT_SW CHAR(1) STATIC INIT('0');
          DCL HRLIMS(5) FIXED(3) STATIC INIT(5,10,25,50,100);
         DCL HRLIMIT FIXED(3) STATIC;
          DCL HRINCR FIXED(5,3) STATIC;
          DCL HRX(120) FIXED(3) STATIC INIT((120)0);
          DCL FDAYS FIXED(2) STATIC INIT(0);
          DCL SMFPTR  PTR STATIC;
          DCL SMFPTRA PTR STATIC;
          DCL SMFPTRB PTR STATIC;
          DCL IWORK_PTR PTR STATIC;
          DCL 1 CARD STATIC,                 /* INPUT DATE CARD       */
               2 SDATE   PIC'(5)9',          /* START DATE - YYDDD    */
               2 DELIM   CHAR(1),            /* DELIMITER             */
               2 EDATE   PIC'(5)9',          /* END DATE - YYDDD      */
               2 DELIM2  CHAR(1),            /******* OESTZ *******/
               2 USER    CHAR(8),            /******* OESTZ *******/
               2 CREST   CHAR(60);           /* NOT USED              */
         DCL STDATE FIXED(7) STATIC INIT(0);
          DCL ENDATE FIXED(7) STATIC INIT(99999);
          DCL RSDATE FIXED(7) STATIC;
          DCL REDATE FIXED(7) STATIC;
          DCL IWORK FIXED BIN(31) STATIC INIT(0);
          DCL RECHFELD BIN FLOAT(53);
          DCL 1 IWORK1 BASED(IWORK_PTR),
               2 IWORK11 CHAR(1),
               2 IWORK12 CHAR(3);
          DCL 1 IWORK2 BASED(IWORK_PTR),
               2 IWORK21 CHAR(2),
               2 IWORK22 CHAR(2);
          DCL 1 IWORK3 BASED(IWORK_PTR),
               2 IWORK31 CHAR(3),
               2 IWORK32 CHAR(1);
          DCL ACCTFLD CHAR(260) STATIC;
          DCL SERVFLD CHAR(70) STATIC;
          DCL ZUSER CHAR(8) INIT(' ');       /******* OESTZ *******/
          DCL RECHFELD1 FIXED(15,2);
          DCL RECHFELD2 FIXED(15);
          DCL 1 REFERAT(10) STATIC,
                2 REFID CHAR(8),
                2 REFNO FIXED(15),
                2 REFSESS FIXED(15),
                2 REFTRANS FIXED(15),
                2 REFSERV FIXED(15),
                2 REFCPU FIXED(15);
          DCL (FLOAT1,FLOAT2) BIN FLOAT(53);
          ON ENDFILE(SYSIN2) GOTO CLOSE2;    /*     OESTZ      */
          OPEN FILE(SYSIN2);                 /* READ REFERATSTABELLE */
          IREFZ = 0;
 LESE2:   READ FILE(SYSIN2) INTO(CARD1);
          IREFZ = IREFZ + 1;
          REFTAB(IREFZ) = CARD1U;
          GOTO LESE2;
 CLOSE2:  CLOSE FILE(SYSIN2);
          ON ENDFILE(SYSIN1) GOTO CLOSER;
          OPEN FILE(SYSIN1);
          IT = 0;
 LESE:    READ FILE(SYSIN1) INTO(CARD1);
          IT = IT + 1;
          USE(IT) = CARD1U;
          IF CARD1U = '****' THEN GOTO CLOSER;
          GOTO LESE;
 CLOSER:  CLOSE FILE(SYSIN1);
 /***                                                               ***/
 /***     ATTEMPT TO READ INPUT DATE SELECTION CARD                 ***/
 /***                                                               ***/
          ON UNDEFINEDFILE(SYSIN)            /* USE DEFAULT DATES IF  */
             GO TO INITIALISATION;           /* SYSIN IS NOT PRESENT  */
          ON ENDFILE(SYSIN)                  /* OR IF AN IMMEDIATE    */
             GO TO CLOSE_SYSIN;              /* EOF IS DETECTED       */
          OPEN FILE(SYSIN);
          READ FILE(SYSIN) INTO(CARD);
          STDATE = SDATE;
          ENDATE = EDATE;
          IF USER='        ' THEN;           /******* OESTZ *******/
                             ELSE DO;        /******* OESTZ *******/
                                 ZUSER=USER; /******* OESTZ *******/
                           GOTO CLOSE_SYSIN; /******* OESTZ *******/
                                  END;       /******* OESTZ *******/
 CLOSE_SYSIN:
          CLOSE FILE(SYSIN);
 /***                                                               ***/
 /***     INITIALISE ALL PROGRAM VARIABLES AND POINTERS             ***/
 /***                                                               ***/
 INITIALISATION:
          IWORK_PTR = ADDR(IWORK);
          SMFPTR = ADDR(SMFREC);
          SMFPTRA = ADDR(ACCTFLD);
          SMFPTRB = ADDR(SERVFLD);
          REFERAT = '';
          REFID(1) = 'REF T1';
          REFID(2) = 'REF T2.1';
          REFID(3) = 'REF T2.2';
          REFID(4) = 'REF T2.3';
          REFID(5) = 'REF T2.4';
          REFID(6) = 'REF T2.5';
          REFID(7) = 'FA';
          REFID(8) = 'EXT';
          REFID(9) = 'SONST';
          REFID(10) = 'TOTAL';
          DO I = 1 TO 100;
             UID(I) = ' ';
             UNO(I) = 0;
             UEL(I) = 0;
             UOC(I) = 0;
             USERV(I) = 0;
             UCPU(I) = 0;
             DO J = 1 TO 15;
                ECEL(I,J) = 0;
                ECCPU(I,J) = 0;
             END;
          END;
          DO I = 1 TO 14;
             SLIMS(I) = SLIMS(I) * 60;
          END;
          LINEY_PTR = ADDR(LINEY);
          LINEM_PTR = ADDR(LINEM);
          HEADM_PTR = ADDR(HEADM);
 /***                                                               ***/
 /***     SELECT TSO RECORDS AND SORT INTO DATE SEQUENCE            ***/
 /***                                                               ***/
          ON ERROR GOTO ERROR1;
          ON ENDFILE(SMFDATA) GO TO SORT_TSO_RECORDS;
          OPEN FILE(SMFDATA),
               FILE(WORKOUT) TITLE('SORTIN'),
               FILE(PRINTA),
               FILE(PRINT);
 READS:   READ FILE(SMFDATA) INTO(SMFREC);
          IWORK = 0;
          IWORK32 = S34RTYPE;
         IF IWORK ^= 34 THEN GOTO READS;
          IF SUBSTR(S34TMP,1,6) ^= 'IKJEFT' THEN GOTO READS;
          IF S34DATE < STDATE THEN GO TO READS;
          IF S34DATE > ENDATE THEN GO TO READS;
     /*   IF S34USRID = ZUSER THEN;  */      /******* OESTZ *******/
     /*                     ELSE GOTO READS;  ******* OESTZ *******/
          XTYP = 'T';
          XDAT = S34DATE;
          XFTM = S34TIME / 100;              /* CONVERT TO SECONDS    */
          XPGM = S34TMP;
          XJBN = S34USRID;
          XSPN = S34SNAME;
          IWORK = 0;
          IWORK32 = S34STNO;
          XSNO = IWORK;
          IWORK32 = S34SPRTY;
          XPRI = IWORK;
          THOA = S34REGN;
          THOU = S34CORE;
          TPUT = S34TPUTS;
          TGET = S34TGETS;
          XLOGA = S34LOGT / 100;             /* CONVERT TO SECONDS    */
          I = 103 + S34DLGTH;
          IWORK32 = SUBSTR(SMFREC,I,1);
          ACCTFLD = SUBSTR(SMFREC,I,IWORK);  /* ACCOUNTING FIELDS     */
          IWORK12 = S34CPUT;
          XCPU = IWORK;
    /*    IWORK12 = S34CPUS;     */          /* CPU UNDER SRB */
    /*    XCPU = XCPU + IWORK;   */
          I = S34ROFF+1;
          SERVFLD = SUBSTR(SMFREC,I,70);  /* RELOCATE SECTION      */
          RECHFELD = S34ACT;              /* STEP TRANSACTION TIME */
          RECHFELD = RECHFELD * 1024 / 1000000; /*CONV TO SECONDS  */
          XSTM=RECHFELD;
          XSERV= S34SST;                     /* STEP SERVICE UNITS */
          WRITE FILE(WORKOUT) FROM(TSREC);
          GO TO READS;
 SORT_TSO_RECORDS:
         CLOSE FILE(SMFDATA),
                FILE(WORKOUT);
          SFIELD = ' SORT FIELDS=(1,18,CH,A) ';
          RFIELD = ' RECORD TYPE=F,LENGTH=(80) ';
          CALL PLISRTA(SFIELD,RFIELD,90000,RETCDE);
          IF RETCDE ^= 0
             THEN CALL PLIDUMP('TFSHB','ERROR IN TSOPRNT SORT');
          ON ENDFILE(WORKIN) GO TO END_OF_JOB;
          OPEN FILE(WORKIN) TITLE('SORTOUT');
 /***                                                               ***/
 /***     READ TSO RECORDS AND LIST SELECTIVE DETAILS               ***/
 /***                                                               ***/
 READW:   READ FILE(WORKIN) INTO(TSREC);
          IF OLD_DATE ^= XDAT
             THEN DO;
                     FDAYS = FDAYS + 1;
                     LINES = 70;
                     IF OLD_DATE ^= 0
                        THEN DO;
                                RETLAB = SETDATE;
                                GO TO PRINT_CPU_DISTRIB;
                             END;
                  END;
 SETDATE: OLD_DATE = XDAT;
          H12 = XDAT;
          L12 = XJBN;
          L1R = XSERV;
          L16 = XSPN;
          L1L = THOA;
          L1N = THOU;
          L1P = TPUT;
          IF XSTM = 0
             THEN F1 = 0;
             ELSE F1 = XCPU / XSTM;
          L1J = F1;
          FA = XFTM - XLOGA;
          IF FA < 0 THEN FA = FA + 86400;
          IF FA = 0
             THEN F1 = 0;
             ELSE F1 = XCPU / FA;
          L1I = F1;
          I = XLOGA / 360;
          IF I = 0 THEN I = 1;
          J = XFTM / 360;
          IF J = 0 THEN J = 1;
          IF I > J THEN J = 240;
          DO K = I TO J;
             HRSTATS(K) = HRSTATS(K) + F1;
          END;
          FB = FA;
          FTIME = XSTM;                 /* STEP TRANSACTION TIME      */
          CALL CONSECS;
          L1E = SUBSTR(CHAR9,3,8);
          FTIME = XFTM;                 /* SESSION END TIME           */
          CALL CONSECS;
          L1A = SUBSTR(CHAR9,3,8);
          FTIME = XCPU;                 /* SESSION CPU TIME           */
          CALL CONCPUT;
          L1G = CHAR10;
          FTIME = XLOGA;                /* LOG ON TIME                */
          CALL CONSECS;
          L18 = SUBSTR(CHAR9,3,8);
          FA = FB;
          FTIME = FA;                   /* SESSION ELAPSE TIME        */
          CALL CONSECS;
          L1C = SUBSTR(CHAR9,3,8);
          IREFT = 0;
          CALL FINDREF;        /* OESTZ ADDING FOR REFERATSTABELLE */
          REFNO(IREFT)=REFNO(IREFT) + 1;
          REFSESS(IREFT) = REFSESS(IREFT) + FB;
          REFTRANS(IREFT) =REFTRANS(IREFT) + XSTM;
          REFSERV(IREFT) = REFSERV(IREFT) + XSERV;
          REFCPU(IREFT) = REFCPU(IREFT) + XCPU;
         IF LINES > 65
             THEN DO;
                     WRITE FILE(PRINT) FROM(HEAD1);
                     WRITE FILE(PRINT) FROM(HEAD2);
                     WRITE FILE(PRINT) FROM(HEAD3);
                     L11 = '0';
                     LINES = 6;
                  END;
          WRITE FILE(PRINT) FROM(LINE1);
          IF XSPN = 'DB2     ' THEN DO;
                                               DO ID = 1 TO IT;
                                 IF USE(ID) ='********' THEN GOTO UJ;
                                         IF XJBN = USE(ID) THEN GOTO UI;
                                               END;
                                               GOTO UJ;
  UI:                 IF LINET > 65 THEN DO;
                     WRITE FILE(PRINTA) FROM(HEAD1A);
                     WRITE FILE(PRINTA) FROM(HEAD2A);
                     WRITE FILE(PRINTA) FROM(HEAD3A);
                     L11 = '0';
                     LINET = 6;
                     GOTO UK;
                                         END;
          L11 = ' ';
 UK:      L16 = H12 ×× '  ';
          WRITE FILE(PRINTA) FROM(LINE1);
          LINET = LINET + 1;
                                    END;
 UJ:      L11 = ' ';
          LINES = LINES + 1;
          IF NU = 0 THEN GO TO ADD_NEW_CODE;
          DO I = 1 TO NU;
             IF UID(I) = XJBN THEN GO TO ADD_TIMES;
          END;
 ADD_NEW_CODE:
          NU = NU + 1;
          I = NU;
          UID(I) = XJBN;
 ADD_TIMES:
          FA = FB;
          UEL(I) = UEL(I) + FA;
          UOC(I) = UOC(I) + XSTM;
          USERV(I) = USERV(I) + XSERV;
          UCPU(I) = UCPU(I) + XCPU;
          UNO(I) = UNO(I) + 1;
          DO J = 1 TO 15;
             IF FB < SLIMS(J)
                THEN DO;
                        ECEL(I,J) = ECEL(I,J) + FB;
                        ECCPU(I,J) = ECCPU(I,J) + XCPU;
                        GO TO READW;
                     END;
          END;
          GO TO READW;
 /***                                                               ***/
 /***     PRINT SUMMARY REPORT OF TSO USAGE BY LOG-ON CODE          ***/
 /***                                                               ***/
 END_OF_JOB:
          RETLAB = PRINT_TOTAL_USAGE;
          GO TO PRINT_CPU_DISTRIB;
 PRINT_TOTAL_USAGE:
          WRITE FILE(PRINT) FROM(HEADA);
          WRITE FILE(PRINT) FROM(HEADB);
          WRITE FILE(PRINT) FROM(HEADC);
          NU = NU + 1;
          UID(NU) = 'TOTAL';
          DO I = 1 TO NU;
             IF I = NU THEN LA1 = '0';
             LA2 = UID(I);
             LA3 = UNO(I);
             LAJ = USERV(I);
             FC = UEL(I);
             FD = UCPU(I);
             IF FC = 0
                THEN F1 = 0;
                ELSE DO;
                        F1 = FD;
                        F1 = F1 / FC;
                     END;
             LAF = F1;
          FTIME = FD;                   /* TOTAL CPU TIME             */
          CALL CONCPUT;
          LA9 = CHAR10;
          FTIME = FC;                   /* TOTAL SESSION ELAPSE TIME  */
          CALL CONSECS;
          LA5 = CHAR9;
             FC = UOC(I);
          FTIME = FC;                   /* TOTAL STEP TRANSACTION TIME*/
          CALL CONSECS;
          LA7 = CHAR9;
             IF FC = 0
                THEN F1 = 0;
                ELSE DO;
                        F1 = FD;
                        F1 = F1 / FC;
                     END;
             LAH = F1;
             FD = UEL(I);
             F2 = UNO(I);
             FD = FD / F2;
             FC = FC / F2;
          FTIME = FD;                   /* AVERAGE SESSION ELAPSE     */
          CALL CONSECS;
          LAB = CHAR9;
          FTIME = FC;                   /* AVERAGE STEP TRANSACTION TI*/
          CALL CONSECS;
          LAD = CHAR9;
             WRITE FILE(PRINT) FROM(LINEA);
             UNO(NU) = UNO(NU) + UNO(I);
             UEL(NU) = UEL(NU) + UEL(I);
             UOC(NU) = UOC(NU) + UOC(I);
             USERV(NU) = USERV(NU) + USERV(I);
             UCPU(NU) = UCPU(NU) + UCPU(I);
             LA1 = ' ';
          END;
 PRINT_TOTAL_REPORT:             /* FIRST BLOCK */
          DO I = 1 TO 9;
            REFNO(10) = REFNO(10) + REFNO(I);
            REFSESS(10) = REFSESS(10) + REFSESS(I);
            REFTRANS(10) = REFTRANS(10) + REFTRANS(I);
            REFSERV(10) = REFSERV(10) + REFSERV(I);
            REFCPU(10) = REFCPU(10) + REFCPU(I);
          END;
          WRITE FILE(PRINT) FROM(HEADR);
          WRITE FILE(PRINT) FROM(HEADS);
          WRITE FILE(PRINT) FROM(HEADT);
          DO I = 1 TO 10;
             LR2 = REFID(I);
             LR3 = REFNO(I);
             LRJ = REFSERV(I);
             FC = REFSESS(I);
             FD = REFCPU(I);
             IF FC = 0
                THEN F1 = 0;
                ELSE DO;
                        F1 = FD;
                        F1 = F1 / FC;
                     END;
             LRF = F1;
          FTIME = FD;                   /* TOTAL CPU TIME             */
          CALL CONCPUT;
          LR9 = CHAR10;
          FTIME = FC;                   /* TOTAL SESSION ELAPSE TIME  */
          CALL CONSECS;
          LR5 = CHAR9;
             FC = REFTRANS(I);
          FTIME = FC;                   /* TOTAL STEP TRANSACTION TIME*/
          CALL CONSECS;
          LR7 = CHAR9;
             IF FC = 0
                THEN F1 = 0;
                ELSE DO;
                        F1 = FD;
                        F1 = F1 / FC;
                     END;
             LRH = F1;
             FD = REFSESS(I);
             F2 = REFNO(I);
             IF F2 = 0 THEN DO;
                            C95,C91,C93 = 0;
                            LRB = CHAR9;
                            LRD = CHAR9;
                            GOTO LRD9;
                            END;
             FD = FD / F2;
             FC = FC / F2;
          FTIME = FD;                   /* AVERAGE SESSION ELAPSE     */
          CALL CONSECS;
          LRB = CHAR9;
          FTIME = FC;                   /* AVERAGE STEP TRANSACTION TI*/
          CALL CONSECS;
          LRD = CHAR9;
 LRD9:       WRITE FILE(PRINT) FROM(LINER);
             LR1 = '0';
          END;
             LS1 = '-';             /*  SECOND BLOCK  */
             RECHFELD1 = 1;
             RECHFELD1 = RECHFELD1/2;
          DO I = 1 TO 9;        /* PERCENTAGES  */
           LS2 = REFID(I);
           FLOAT1 = REFNO(I);
           FLOAT2 = REFNO(10);
           RECHFELD2 = FLOAT1 / FLOAT2 * 100 + RECHFELD1;
           LS3 = RECHFELD2;
           FLOAT1 = REFSESS(I);
           FLOAT2 = REFSESS(10);
           RECHFELD2 = FLOAT1 / FLOAT2 * 100 + RECHFELD1;
           LS5 = RECHFELD2;
           FLOAT1 = REFTRANS(I);
           FLOAT2 = REFTRANS(10);
           RECHFELD2 = FLOAT1 / FLOAT2 * 100 + RECHFELD1;
           LS7 = RECHFELD2;
           FLOAT1 = REFSERV(I);
           FLOAT2 = REFSERV(10);
           RECHFELD2 = FLOAT1 / FLOAT2 * 100 + RECHFELD1;
           LSJ = RECHFELD2;
           FLOAT1 = REFCPU(I);
           FLOAT2 = REFCPU(10);
           RECHFELD2 = FLOAT1 / FLOAT2 * 100 + RECHFELD1;
           LS9 = RECHFELD2;
             WRITE FILE(PRINT) FROM(LINEU);
             LS1 = '0';
           END;
 /***                                                               ***/
 /***     PRINT SUMMARY OF TSO CPU UTILISATION BY SESSION TIME      ***/
 /***                                                               ***/
          WRITE FILE(PRINT) FROM(HEADX);
          WRITE FILE(PRINT) FROM(HEADY);
          WRITE FILE(PRINT) FROM(HEADZ);
          DO I = 1 TO NU;
             IF I = NU THEN LY1 = '0  ';
             LY2 = UID(I);
             DO J = 1 TO 15;
                IF ECEL(I,J) = 0
                 × ECCPU(I,J) = 0
                   THEN LZ2(J) = ' ';
                   ELSE DO;
                           F1 = ECCPU(I,J);
                           F1 = F1 / ECEL(I,J);
                           LY3(J) = F1;
                        END;
             END;
             WRITE FILE(PRINT) FROM(LINEY);
             LY1 = ' ';
             DO J = 1 TO 15;
                ECEL(NU,J) = ECEL(NU,J) + ECEL(I,J);
                ECCPU(NU,J) = ECCPU(NU,J) + ECCPU(I,J);
             END;
          END;
          PRT_SW = '1';
          RETLAB = CLOSE_FILES;
          DO I = 1 TO 240;
             THRSTATS(I) = THRSTATS(I) / FDAYS;
          END;
          GO TO PRINT_CPU_DISTRIB;
 CLOSE_FILES:
          CLOSE FILE(WORKIN),
                FILE(PRINT),FILE(PRINTA);
          GO TO TERMINATE_PROGRAM;
 /***                                                               ***/
 /***     PRINT DISTRIBUTION OF CPU UTILISATION BY TIME OF DAY      ***/
 /***                                                               ***/
 PRINT_CPU_DISTRIB:
          IF PRT_SW = '0'
             THEN HS_PTR = ADDR(HRSTATS);
             ELSE HS_PTR = ADDR(THRSTATS);
          HRMAX = 0;
          DO I = 71 TO 190;
             IF HRMAX < HS(I)
                THEN HRMAX = HS(I);
          END;
          DO I = 1 TO 5;
             IF HRMAX < HRLIMS(I)
                THEN DO;
                        HRLIMIT = HRLIMS(I);
                        HRINCR = HRLIMIT / 50;
                        GO TO CONVERT_PERCENTAGES;
                     END;
          END;
 CONVERT_PERCENTAGES:
          DO I = 1 TO 120;
             J = I + 70;
             HRX(I) = HS(J) / HRINCR;
             HRMAX = HS(J) - HRX(I) * HRINCR;
             IF HRMAX ^= 0
                THEN HRX(I) = HRX(I) + 1;
          END;
          IF PRT_SW = '0'
             THEN HM2 = OLD_DATE;
             ELSE DO;
                     HN2 = FDAYS;
                     HN3 = ' DAY AVERAGE';
                  END;
          WRITE FILE(PRINT) FROM(HEADM);
          HP1 = '-';
          HP3 = '    CPU    ';
          WRITE FILE(PRINT) FROM(HEADP);
          HP1 = ' ';
          HP3 = 'UTILISATION';
          WRITE FILE(PRINT) FROM(HEADP);
          DO I = 1 TO 10;
             DO J = 1 TO 5;
                K = 5 * (I-1) + J;
                K = 51 - K;
                HRMAX = K * HRINCR;
                IF J = 1
                   THEN LN2 = HRMAX;
                   ELSE LM1 = ' ';
                DO L = 1 TO 120;
                   IF HRX(L) >= K
                      THEN LM3(L) = '*';
                      ELSE LM3(L) = ' ';
                END;
                WRITE FILE(PRINT) FROM(LINEM);
             END;
         END;
          DO I = 1 TO 240;
             THRSTATS(I) = THRSTATS(I) + HRSTATS(I);
             HRSTATS(I) = 0;
          END;
          WRITE FILE(PRINT) FROM(FOOTM);
          WRITE FILE(PRINT) FROM(FOOTN);
          GO TO RETLAB;
 /***                                                               ***/
 /***     LOCATE THE REFERAT BY USER                                ***/
 /***                                                               ***/
 FINDREF: PROC;
          DO IREF = 1 TO IREFZ;
           IF REFTAB(IREF) = '&&&&&&&&' THEN DO;
                                          IREFT = IREFT + 1;
                                          GOTO ENDREF;
                                       END;
           IF REFTAB(IREF) = '********' THEN DO;
                                          IREFT = IREFT + 1;
                                          GOTO FINDU1;
                                        END;
           IF REFTAB(IREF) = XJBN  THEN GOTO ENDREF;
 FINDU1:  END;
 ENDREF:  END FINDREF;
 /***                                                               ***/
 /***     CONVERT TIME IN SECONDS TO THE FORM HHH.MM.SS             ***/
 /***                                                               ***/
 CONSECS: PROC;
             FA = FTIME / 60;
             F4 = FTIME - FA * 60;
             C95 = F4;
             F4 = FA / 60;
             C91 = F4;
             F4 = FA - F4 * 60;
             C93 = F4;
          END CONSECS;
 /***                                                               ***/
 /***     CONVERT TIME IN 1/100TH SECONDS TO THE FORM MMMM.SS.TT    ***/
 /***                                                               ***/
 CONCPUT: PROC;
             FA = FTIME / 100;
             F4 = FTIME - FA * 100;
             CX5 = F4;
             F4 = FA / 60;
             CX1 = F4;
             F4 = FA - F4 * 60;
             CX3 = F4;
          END CONCPUT;
 ERROR1:  PUT DATA(IREFZ,IREFT,FB,XSERV,XCPU,XSTM);
 TERMINATE_PROGRAM:
          END TSOPRNT;
