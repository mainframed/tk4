**********************************************************************
*              REGISTER EQUATES                                      *
**********************************************************************
         SPACE
         REQUATEN
         EJECT
**********************************************************************
*              START PROGRAM                                         *
**********************************************************************
         SPACE
SPFETPMD CSECT
         LCNVE *,12
         SPACE
         LR    R11,R1                  PARM ADDRESS FROM PMD
         EJECT
**********************************************************************
*              GET THE WORK AREA                                     *
**********************************************************************
         SPACE
         INIT  R9,(WORKC,WORKLEN)
         SPACE
         USING WORKC,R9
         L     R10,0(R11)              GET THE TLD ADDRESS FROM PMD
         ST    R10,CPARMS              SAVE THE ADDRESS FOR THE
*                                      APPLICATION PROGRAM
         L     R1,4(R11)               GET THE ADDRESS OF FORMAT
         MVC   MHAFMTN,0(R1)           SAVE THE FORMAT NAME
         EJECT
**********************************************************************
*              PUT THE SELECTION MENU                                *
**********************************************************************
         SPACE
DISPLAY  SPFMH MHAPARMS,MHAFMTN,MHAOPT,MHAP1,MHAP2
         EJECT
**********************************************************************
*              TEST ON "END" ORDER AND VALID RETURN                  *
**********************************************************************
         SPACE
ORDTEST  TM    29(R10),B'00000100'     END KEY PFK3 HAS BEEN PRESSED ?
         BO    END                     END OF ET-MAIN DRIVER
         CLI   MHAP1,C'^'              INVALID OPTION HAS BEEN ENTER ?
         BE    MERROR                  PUT THE ERROR MESSAGE
         SPACE 2
**********************************************************************
*              LINK THE APPLICATION PROGRAM                          *
**********************************************************************
         SPACE
         LA    R1,CPARMS               POINT TO PARMS FOR THE
*                                      APPLICATION PROGRAM
         ST    R10,0(R1)               SAVE THE TLD ADDRESS FOR THE
*                                      APPLICATION PROGRAM
         LA    R2,MHAP2                SAVE THE ADDRESS OF SUBMENU FOR
         ST    R2,4(R1)                THE APPLICATION PROGRAM
         MVI   4(R1),X'80'             SET THE VL BIT
         SPACE
         LINK  EPLOC=MHAP1,SF=(E,LINK)  LINK THE SELECTED PROGRAM
         SPACE
         NI    29(R10),B'11111011'     TURN OFF THE PFK3 REFERENCE BIT
         B     DISPLAY                 DISPLAY THE SELECTION MENU
         EJECT
**********************************************************************
*              END OF ET MAIN DRIVER                                 *
**********************************************************************
         SPACE
END      NI    29(R10),B'11111011'     TURN OFF THE PFK3 REFERENCE BIT
         LA    0,WORKLEN               SIZE OF THE WORK AREA
         SPACE
         FREEMAIN R,A=(9),LV=(0)       DROP THE WORK AREA
         SPACE
         LEXIT                         RETURN TO THE SPF-PMD
         EJECT
**********************************************************************
*              PUT THE ERROR MESSAGE / INVALID OPTION SELECTED       *
**********************************************************************
         SPACE
MERROR   MVC   MEHMSG,MHAP1+1          SAVE THE THE ERROR MESSAGE ID.
*                                      RETURNED FROM THE MHA
         SPACE
         SPFEM MEHPARMS,MEHMSG,MEHOPT
         SPACE
         B     ORDTEST                 TEST THE RETURNED ORDER
         EJECT
**********************************************************************
*              DEFINITIONS / LITERALS                                *
**********************************************************************
         SPACE
         LTORG
         EJECT
**********************************************************************
*              DEFINITIONS                                           *
**********************************************************************
         SPACE
WORKC    CSECT
CPARMS   DC    2A(0)                   PARMS FOR THE APPLICATION PROG.
MEHPARMS DC    A(0)                    POINTER TO THE TLD
         DC    A(0)                    POINTER TO THE ERROR MESSAGE ID.
         DC    A(0)                    POINTER TO THE CURSOR ORDER
MHAPARMS DC    A(0)                    POINTER TO THE TLD
         DC    A(0)                    POINTER TO THE FORMAT NAME
         DC    A(0)                    POINTER TO THE CONTROL OPTION
         DC    A(0)                    POINTER TO RETURNED PGM NAME
         DC    A(0)                    POINTER TO RETURNED FMT NAME
MHAP1    DC    CL8' '                  RETURNED PROGRAM NAME
MHAP2    DC    CL8' '                  RETURNED FORMAT NAME
MHAFMTN  DC    CL8' '                  SELECTION MENU FORMAT NAME
MEHMSG   DC    CL4' '                  RETURNED ERROR MESSAGE ID.
LINK     LINK  SF=L
WORKLEN  EQU   *-WORKC                 LENGTH OF THE WORK AREA
SPFETPMD CSECT
MHAOPT   DC    A(0)                    CONTROL OPTION FOR MHA
MEHOPT   DC    A(1)                    CURSOR ORDER FOR MERR
         END
