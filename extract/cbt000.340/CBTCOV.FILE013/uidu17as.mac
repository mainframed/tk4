PARM SCANCOL=72 SUPPRESS=ALL
*
*  RMFEASY1:   R M F   D A Y L Y   R E P O R T    PASS 1
*  22/10/79
*              DATA COLLECTION FROM RMF RECORDS 70 - 73
*
*              FILE DEFINITION
FILE FILEA
*
OUTPUTW W-1 70 A
*
*              SERVICE RATE TABLE
*
TSOSRATE W-1 2 B
TPSRATE W-3 2 B
TESTSRATE W-5 2 B
BATCHSRATE W-7 2 B
*
*              CHANNEL BUSY TABLE
*
CH1BUS W-9 4 B
CH2BUS W-13 4 B
CH3BUS W-17 4 B
CH4BUS W-21 4 B
CH5BUS W-25 4 B
CH6BUS W-29 4 B
*              CHANNEL BUSY / CPU WAIT
CHBCPUW W-33 4 B
*              " OF SAMPLES
SAMPLES W-37 4 B
*              SYSTEM ID
SYSID W-41 4 A
*              DATE
DATE W-45 4 P
*              INTERVAL START TIME
SMFISTP W-49 4 P
*              CPU WAIT TIME IN SECONDS
CPUWAIT W-53 4 B
*              INTERVAL DURATION IN SECONDS
CPUINT W-57 4 P
*              " OF ASID'S IN MEMORY (MPL)
MPL W-61 1 B
*              " OF BATCH ASID'S
BATCH W-62 1 B
*              " OF TIME SHARING USERS
TSO W-63 1 B
*              " OF PAGES / SECOND
PAGE W-64 1 B
*              TSO ACTIVE TIME (SECONDS)
TSOACT W-65 4 B
*              RMF ACTIVE TIME IN SECONDS
CYCLENGTH W-69 2 B
*              SUBSCRIPTED SERVICE RATE
SERVRATE-(SUB1) 1 2 B
*
*              SERVICE RATE SUBSCRIPTION
SUB72 W 4 B
*              SUBSCRIPTION SAVE FIELD
SUBSAVE W 4 B
*
*              SUBSCRIPTED CHANNEL BUSY VALUE
CHXBUSY-(SUB1) 1 4 B
*
*              FIRST TIME SWITCH
SMFFTSW W 1 B
*
*
*
*              COMMON CONTROL DATA SECTION DEFINITIONS
*
*              RECORD TYPE
SMFTYP 2 1 B
*
*              SYSTEM IDENTIFIER
SMFSID 11 4 A
*
*              INTERVAL START TIME
SMFIST 17 4 P
*
*              INTERVAL START DATE
SMFDAT 21 4 P
*
*              INTERVAL DURATION
SMFINT 25 4 P
MINUTES 25 1 U
SECONDS 26 1 U
*
*              # OF SAMPLES
SMFSAM 33 4 B
*
*              CYCLE LENGTH
SMFCYC 45 4 P
*
*              END OF COMMON CONTROL DATA SECTION DEFINITIONS
*
*
*              RECORD TYPE 70 - CPU ACTIVITY
*
*              # OF CPU DATA SECTIONS
SMF70CPU 51 2 B
*
*              SIZE OF CPU DATA SECTION
SMF70SCD 53 2 B
*
*              CPU WAIT TIME, BITS 0 - 31 (SECONDS)
SMF70WAT-(SUB1) 1 4 B
*              CPU WAIT TIME, BITS 32 - 39
CPUWBYTE4-(SUB1) 5 1 B
*
*
*              % OF INTERVAL RMF WAS ACTIVE
RMFAKT W 4 P1
*
*
*              # OF ASID'S IN MEMORY (MPL)
SMF70ITT-(SUB1) 77 4 B
*
*              # OF BATCH ASID'S
SMF70BTT-(SUB1) 245 4 B
*
*              # OF STARTED TASKS
SMF70STT-(SUB1) 301 4 B
STC W 1 B
*
*              # OF TIME SHARING USERS
SMF70TTT-(SUB1) 357 4 B
*
*
*              RECORD TYPE 71 - PAGING ACTIVITY
*
*              # OF PAGE IN'S
SMF71PIN 53 4 B
*
*              # OF PAGE OUT'S
SMF71POT 57 4 B
*
*              # OF PAGES FOR SWAP IN
SMF71SIN 69 4 B
*
*              # OF PAGES FOR SWAP OUT
SMF71SOT 73 4 B
*
*              TOTAL # OF PAGES
TOTPAGE W 4 B
*
*
*
*
*              RECORD TYPE 72 - WORKLOAD ACTIVITY
*
*              PERFORMANCE GROUP NUMBER
SMF72SUB 31 2 B
*              NUMBER PF PERFORMANCE GROUP PERIODS
SMF72PGP 51 2 B
*              SIZE OF EACH PERFORMANCE GROUP PERIOD SECTION
SMF72SPD 53 2 B
*              TRANSACTION ACTIVE TIME
SMF72ACT-(SUB1) 5 4 B
*              AMOUNT OF SERVICE USED
SMF72SER-(SUB1) 9 4 B
*
*
*
*              RECORD TYPE 73 - CHANNEL ACTIVITY
*
*              SIZE OF PHYSICAL AND LOGICAL CONTROL SECTION
SMF73SHC 49 2 B
*              # OF PHYSICAL CHANNEL DATA SECTIONS
SMF73CHA 51 2  B
*              SIZE OF EACH PHYSICAL CHANNEL DATA SECTION
SMF73SHD 53 2  B
*              # OF LOGICAL CHANNEL DATA SECTIONS
SMF73LCA 55 2  B
*              SIZE OF EACH LOGICAL CHANNEL DATA SECTION
SMF73SLD 57 2  B
*              FREQUENCY COUNTER OF ANY CHAN BUSY WHILE CPU WAITS
SMF73WB0 69 4 B
*              CHANNEL IDENTIFICATION
SMF73PCH-(SUB1) 5 2 B
*              # OF PHYSICAL CHANNEL BUSY SAMPLES
SMF73ABY-(SUB1) 9 4 B
*
*
TOTSRATE W 4 B
*
9999
*
*
IF SMFIST NOT NUMERIC
FLUNK
*
IF SMFTYP NQ 70,71,72,73
FLUNK
*
*              TEST FOR END OF INTERVAL
IF EOF EQ YES THEN GO TO INTCHANGE
IF SMFIST EQ SMFISTP AND SMFSID EQ SYSID AND SMFDAT EQ DATE THEN
GO TO FIRSTTIME
INTCHANGE:
IF SMFFTSW EQ 0 THEN GO TO FIRSTTIME
ELSE
*
*             WRITE INTERIM RECORD
*
PUNCH OUTPUTW
*
*              CLEAR ACCUMULATION FIELDS
TSOSRATE = 0
TPSRATE = 0
TESTSRATE = 0
BATCHSRATE = 0
TSOACT = 0
CHBCPUW = 0
CH1BUS = 0
CH2BUS = 0
CH3BUS = 0
CH4BUS = 0
CH5BUS = 0
CH6BUS = 0
SAMPLES = 0
CPUWAIT = 0
*
*
FIRSTTIME:
IF FILE EQ FILE THEN
*             SAVE INTERVAL START TIME
SMFISTP = SMFIST
*             SAVE SYSTEM ID
SYSID = SMFSID
*             SAVE DATE
DATE = SMFDAT
*
SMFFTSW = 1
*
*
IF EOF EQ YES THEN
STOP
*
*
*
*              PROCESS RECORD TYPE 70 ( X'46' ) - CPU ACTIVITY
*
IF SMFTYP NQ 70 THEN GO TO TYP71
 ELSE
*
*              POINT TO CPU DATA SECTION
SUB1 = SUB1 + 72
*
*              CALCULATE # OF SECONDS WITHIN INTERVAL
CPUINT = MINUTES X 60 + SECONDS
*
*              CALCULATE MEASUREMENT DURATION
SMFCYC = SMFCYC X SMFSAM / 1000
CYCLENGTH = SMFCYC
*
*              CALCULATE % OF TIME RMF WAS ACTIVE DURING INTERVAL
RMFAKT = SMFCYC X 100.00 / CPUINT + 0.05
IF RMFAKT GT 100 THEN
RMFAKT = 100
IF FILE EQ FILE THEN
*
*              CALCULATE REAL CPU WAIT TIME
SMF70WAT = SMF70WAT X 1.048576 X RMFAKT / 100
*
*              ACCUMULATE CPU WAIT TIME
CPUWAIT = CPUWAIT + SMF70WAT
*
*              POINT TO ASID DATA AREA SECTION
SUB1 = SUB1 + SMF70SCD
*
*              FETCH MULTIPROGRAMMING LEVEL
MPL = SMF70ITT X 1.0 / SMFSAM + 0.5
*
*              FETCH # OF BATCH JOBS
BATCH = SMF70BTT X 1.0 / SMFSAM + 0.5
*
*              FETCH # OF STARTED TASKS
STC = SMF70STT / SMFSAM
*
*              FETCH # OF TSO USERS
TSO = SMF70TTT X 1.0 / SMFSAM + 0.5
*
*
TYP71:
*
*              PROCESS RECORD 71 (PAGING ACTIVITY)
*
IF SMFTYP EQ X47 THEN
*
*              ADD ALL PAGE I/O'S
TOTPAGE = SMF71PIN + SMF71POT + SMF71SIN + SMF71SOT
*
*              CALCULATE # OF SECONDS WITHIN INTERVAL
CPUINT = MINUTES X 60 + SECONDS
*
*              CALCULATE PAGES / SECOND
PAGE = TOTPAGE X 1.0 / CPUINT + 0.5
*
*
*
*              PROCESS RECORD 72 (WORKLOAD ACTIVITY)
*
IF SMFTYP NQ 72 THEN GO TO TYP73
ELSE
*              POINT TO FIRST PERFORMANCE GROUP PERIOD SECTION
SUB1 = SUB1 +  88
*              CALCULATE # OF SECONDS WITHIN INTERVAL
CPUINT = MINUTES X 60 + SECONDS
*
*              ASK FOR PERFORMANCE GROUP AND SET SUBSCRIPTION
*
IF SMF72SUB EQ 2 THEN
SUB72 = 0
GO TO SERVLOOP
IF SMF72SUB EQ 5,6 THEN
SUB72 = 2
GO TO SERVLOOP
IF SMF72SUB EQ 10 THEN
SUB72 = 4
GO TO SERVLOOP
ELSE
SUB72 = 6
SERVLOOP:
IF SMF72PGP EQ 0 THEN GO TO TYP73
ELSE
*              CALCULATE AND SUMARIZE SERVICE RATE
TOTSRATE = SMF72SER X 1.0 / CPUINT + 0.5
SUBSAVE = SUB1
SUB1 = SUBW + SUB72
SERVRATE = SERVRATE + TOTSRATE
SUB1 = SUBSAVE
*              TRANSACTION ACTIVE TIME (SEC) - TSO ONLY -
IF SMF72SUB EQ 2 THEN
TSOACT = SMF72ACT X 1024 / 1000000 + TSOACT
IF FILE EQ FILE THEN
*              DECREASE NUMBER OF PERIODS
SMF72PGP = SMF72PGP - 1
*              POINT TO NEXT PERFORMANCE GROUP PERIOD SECTION
SUB1 = SUB1 + SMF72SPD
*              GO BACK TO LOOP
GO TO SERVLOOP
*
*
TYP73:
*
*
*              PROCESS RECORD 73 (CHANNEL ACTIVITY)
*
IF SMFTYP NQ 73 THEN FLUNK
ELSE
SAMPLES = SAMPLES + SMFSAM
*              ACCUMULATE CHAN BUSY / CPU WAIT FREQUENCY
CHBCPUW = CHBCPUW + SMF73WB0
*              POINT TO LOGICAL CHANNEL DATA SECTION
SUB1 = SMF73CHA X SMF73SHD + SUB1 + 48 + SMF73SHC
*
CHBLOOP:
*
IF SMF73LCA EQ 0 THEN FLUNK
*
*              ACCUMULATE CHANNEL BUSY SAMPLES
IF SMF73PCH EQ X4000 THEN
 CH1BUS = CH1BUS + SMF73ABY
IF SMF73PCH EQ X2000 THEN
 CH2BUS = CH2BUS + SMF73ABY
IF SMF73PCH EQ X1000 THEN
 CH3BUS = CH3BUS + SMF73ABY
IF SMF73PCH EQ X0800 THEN
 CH4BUS = CH4BUS + SMF73ABY
IF SMF73PCH EQ X0400 THEN
 CH5BUS = CH5BUS + SMF73ABY
IF SMF73PCH EQ X0200 THEN
 CH6BUS = CH6BUS + SMF73ABY
*
IF FILE EQ FILE THEN
SUB1 = SUB1 + SMF73SLD
SMF73LCA = SMF73LCA - 1
GO TO CHBLOOP
*
*
*
