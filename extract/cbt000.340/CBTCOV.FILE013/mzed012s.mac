         MACRO
         DATAGEN &P1,&P2,&P3,&P4,&CLOSE=
         GBLC  &DAUREGS,&DAENTRY
         LCLA  &NUMCH
         LCLC  &REGCH
         CNOP  0,4
         AIF   (T'&CLOSE NE 'O').CLOSE            IF CLOSE WANTED
         AIF   ('&P1' EQ 'REG').REGIST  IF REGISTERS
         AIF   ('&P1'(1,1) NE '(').NREGNOT  IF NOT REGISTER NOTATION
&NUMCH   SETA  K'&P1-2                  CLAC. NUMBER OF CHARACTERS
&REGCH   SETC  '&P1'(2,&NUMCH)          TAKE CHAR. OUT OF PARENTH.
         ST    &REGCH,*+8 .             STORE REG. TO FIELD-ADDR.
         BAL   1,&DAENTRY .             PARM-LIST-ADDR TO 1 AND BRANCH
         DC    A(0) .                   FIELD-ADDRESS
         AGO   .GOON1
.NREGNOT ANOP
         BAL   1,&DAENTRY .             PARM-LIST-ADDR. TO 1 AND BRANCH
         DC    A(&P1&P2) .              FIELD-ADDRESS
.GOON1   ANOP
         DC    S(R&SYSNDX) .            RETURN-ADDRESS
         DC    X'0&P4' .                FORM OF EDITING
         DC    AL1(&P3-1) .             LENGTH OF THE FIELD - 1
         DC    AL1(A&SYSNDX.E-A&SYSNDX) LENGTH OF THE FIELDNAME - 1
A&SYSNDX DC    C'&P1&P2' .              FIELDNAME
A&SYSNDX.E EQU *-1 .                    LAST CHAR. OF FIELDNAME
         AGO   .GENOUT
.REGIST  AIF   (&P2 GT &P3).WAROUND     IF WRAPAROUND NECESSARY
         AIF   (&P2 EQ &P3).ONEREG      IF ONLY ONE REGISTER
         STM   &P2,&P3,&DAUREGS+&P2*4 . SAVE WANTED REGISTERS
         AGO   .ALLREG
.ONEREG  ST    &P2,&DAUREGS+&P2*4 .     SAVE THE ONLYONE REGISTER
         AGO   .ALLREG
.WAROUND AIF   (&P2 EQ 15).REG15F       IF R15 IS THE FIRST REGISTER
         STM   &P2,15,&DAUREGS+&P2*4 .  STORE UPPER PART
         AGO   .LOWPART
.REG15F  ST    15,&DAUREGS+15*4 .       STORE R15
.LOWPART AIF   (&P3 EQ 0).REG0L         IF R0 IS THE LAST REGISTER
         STM   0,&P3,&DAUREGS .         STORE LOWER PART
         AGO   .ALLREG
.REG0L   ST    0,&DAUREGS .             STORE R0
.ALLREG  BAL   1,&DAENTRY .             PARM-LIST-ADDR. TO 1 AND BRANCH
         DC    H'&P2' .                 START-REGISTER
         DC    H'&P3' .                 END-REGISTER
         DC    S(R&SYSNDX) .            RETURN-ADDRESS
         DC    X'04' .                  FORM OF EDITING
         DC    AL1(0) .                 NOT USED
         DC    AL1(7) .                 LENGTH OF FIELDNAME - 1
         DC    C'REGISTER' .            FIELDNAME
         AGO   .GENOUT
.CLOSE   ANOP
         BAL   1,&DAENTRY .             PARM-LIST-ADDR TO 1 AND BRANCH
         DC    A(0) .                   DUMMY PARM-LIST FOR CLOSE
         DC    S(R&SYSNDX) .            RETURN-ADDRESS
         DC    X'80' .                  INDICATE CLOSE
.GENOUT  DS    0H .                     ALIGN
R&SYSNDX EQU   * .                      RETURN-POINT
         MEND
