SMFTYP01 CSECT
         BASER 2
         REG
         PRINT NOGEN
         EJECT
START    EQU   *
*
         LM    R2,R7,0(R1)
         STM   R2,R7,APARM
         LR    R4,R2         R4 = BASIS FUER ALLGEMEINEN
         USING COMMON,R4     KOMMUNIKATIONSBEREICH .
         L     R3,0(R3)
         USING IFASMF01,R3
         TM    COMEODID,X'FF'
         BO    PLOTTEN
         SPACE 3
EINMAL   NOP   IMMER
         OI    EINMAL+1,X'F0'
         MVC   FW1,SMF1TME
         L     R1,COMINTV     INTERVALL-ZEIT LADEN
         A     R1,FW1
         ST    R1,NEWTIME
         MVC   DATUM,SMF1DTE
         EJECT
IMMER    EQU   *
*
         CLC   DATUM,SMF1DTE
         BE    ZEITVERG
         CLC   NEWTIME,F24UHR TAGESWECHSEL ?
         BL    PLOTTEN       NULL UHR
*
         L     R1,NEWTIME    INTERVALL-ENDE-ZEIT LADEN
         S     R1,F24UHR     MINUS 24 UHR
         ST    R1,NEWTIME    LETZTER INTV ALTES DATUM
         MVC   DATUM,SMF1DTE DATUM UEBERTRAGEN
         B     ZEITVERG
         SPACE 2
ZEITVERG EQU   *
*
         CLC   NEWTIME,SMF1TME
         BNH   PLOTTEN       INTERVALL WECHSEL
         SPACE 2
CUM      EQU   *
*
         MVC   FW1,SMF1SWT
         L     R1,C01WAIT    SUMME WAITZEIT LADEN
         A     R1,FW1        WAITZEIT DES SATZES ADDIEREN
         ST    R1,C01WAIT    IN SUMMENFELD WAIT UEBERTRAGEN
         L     R1,CUMWAIT
         A     R1,FW1
         ST    R1,CUMWAIT
         MVC   FW1,SMF1PGIN
         MVC   FW2,SMF1PGOT
         L     R1,C01PAGEI   SUMMENFELD PAGE-IN LADEN
         A     R1,FW1        PAGEIN DES SATZES ADDIEREN
         ST    R1,C01PAGEI   IN SUMMENFELD PAGEIN SPEICHERN
         L     R1,C01PAGEO   SUMMENFELD PAGE-OUT LADEN
         A     R1,FW2        PAGE-OUT DES SATZES ADDIEREN
         ST    R1,C01PAGEO   INS SUMMENFELD PAGE-OUT SPEICHERN
         L     R1,CUMPAGE
         A     R1,FW1
         A     R1,FW2
         ST    R1,CUMPAGE
         B     END99
         EJECT
PLOTTEN  EQU   *
*
         MVC   WAITDTE,DATUM
         MVC   WAITTME,NEWTIME
         MVC   WAITZEIT,CUMWAIT
         CLC   CUMWAIT,C01WAITX
         BL    PAGEPLOT
         MVC   C01WAITX,CUMWAIT
         SPACE 3
PAGEPLOT EQU   *
*
         MVC   PAGEDTE,DATUM
         MVC   PAGETME,NEWTIME
         MVC   PAGING,CUMPAGE
         CLC   CUMPAGE,C01PAGEX
         BL    WRITE
         MVC   C01PAGEX,CUMPAGE
         SPACE 3
WRITE    EQU   *
*
         L     R2,APLOT01
         PUT   (R2),PLTWAIT
         L     R2,APLOT02
         PUT   (R2),PLTPAGE
         XC    CUMPAGE,CUMPAGE
         XC    CUMWAIT,CUMWAIT
         TM    COMEODID,X'FF' EODATA TYP 01 ?
         BO    END99
         L     R1,COMINTV     INTERVALL-ZEIT INS REG 1
         MVC   FW1,SMF1TME   UHRZEIT DES SATZES NACH VOLLWORT
         A     R1,FW1        UHRZEIT PLUS INTV-ZEIT
         ST    R1,NEWTIME    INS FELD ENDE INTERVALL SPEICHERN
         MVC   DATUM,SMF1DTE   DATUM UEBERTRAGEN
         B     CUM
         EJECT
         LTORG
         EJECT
**********         DEFINITIONEN        ***********
APARM    DC    F'0'
ASATZ    DC    F'0'
ALISTE   DC    F'0'
APLOT01  DC    F'0'
APLOT02  DC    F'0'
APLOT03  DC    F'0'
*
**********                   ************
*            HILFSFELDER
FW1      DC    F'0'
FW2      DC    F'0'
DATUM    DC    F'0'
NEWTIME  DC    F'0'
CUMWAIT  DC    F'0'
CUMPAGE  DC    F'0'
F24UHR   DC    X'0083D600'   24.00 UHR
*******
         EJECT
**********         PLOTSAETZE       *********
*
PLTWAIT  DS    0CL18
WAITFOR  DC    XL1'01'
WAIT     DC    CL5'WAIT '
WAITDTE  DS    CL4
WAITTME  DS    CL4
WAITZEIT DS    CL4
****
*
PLTPAGE  DS    0CL18
PAGEFOR  DC    XL1'01'
PAGE     DC    CL5'PAGE '
PAGEDTE  DS    CL4
PAGETME  DS    CL4
PAGING   DS    CL4
*******
COMMON   DSECT               DUMMY - SECTION
COMINTV  DS    F
         ORG   *-4
COMEODID DS    CL1
         ORG
         DS    3F
C01WAITX DS    F
C01PAGEX DS    F
C01WAIT  DS    F
C01PAGEI DS    F
C01PAGEO DS    F
         EJECT
*******
******
         PRINT GEN
IFASMF01 DSECT
         IFASMFR 01
         END   SMFTYP01
