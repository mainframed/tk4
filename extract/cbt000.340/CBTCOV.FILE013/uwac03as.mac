*   13/02/85 505021303  MEMBER NAME  UWAC03AS (GAPVERS)     ASSEMBLER
*********************************************************************
*  DIESES PROGRAMM ERMITTELT AUS DER 'ELIGIBLE DEVICE TABLE (EDT)'  *
*  DIE ZUGEHOERIGKEIT VON E/A-EINHEITEN ZU GENERISCHEN UND/ODER     *
*  ESOTERISCHEN GRUPPEN.                                            *
*  ES SUCHT WAHLWEISE IN DER LPA (DEFAULT) ODER IN EINER BIBLIOTHEK *
*  (DABEI KANN EIN VOLUME ANGEGEBEN WERDEN (DEFAULT: AUS KATALOG)). *
*  DIE EINGABE KANN SEIN:                                           *
*  -  UNIT-ADRESSE (GENAU 3 CHAR.,HEX-NUMERISCH, Z.B.'3A0')         *
*  -  VOLUME-SERIAL (GENAU 6 CHAR., 1.ALPHABET., Z.B. 'SYSRES')     *
*  -  GENERISCHER NAME (4-6 CHAR, 1.NUMERISCH, Z.B. '3380','3203-5')*
*  -  ESOTERISCHER NAME (ALLES AUSSER 6 CHAR.+1.ALPHA, Z.B.'SYSDA') *
*                                                                   *
*  GGF. IST DIESER EINGABEMODUS INSTALLATIONSSPEZIFISCH ABZUWANDELN)*
*                                                                   *
*  DAS PROGRAMM LAEUFT NUR UNTER MVS/XA, FUER MVS/370 ERSCHEINT     *
*  EINE MELDUNG, ES WIRD DANN NICHTS AUSGEFUEHRT. ES LAEUFT UNTER   *
*  TSO ALS COMMAND-PROCESSOR, DER AUFRUF IST AUS DEM MITGELIEFERTEN *
*  'HELP'-MEMBER ERSICHTLICH.                                       *
*                                                                   *
*  ATTRIBUTE: NON-RENT, NON-REUS, PAGEABLE, NON-PRVILEGED,          *
*             COMMAND PROCESSOR, 31-BIT-ADDRESSING MODE             *
*                                                                   *
*  AUTOR: BLECKWENN, WACKER-CHEMIE MUENCHEN     FEB. 1985           *
*********************************************************************
EDTSCAN  AMODE 31
EDTSCAN  RMODE 24
EDTSCAN  BEG   *
*
         SPACE 1
         LR    R2,R1               SAVE PARAMETER ADDRESS
         USING CPPL,R2
         L     R1,CPPLPSCB         GET PSCB ADDR
         SPACE 1
*--------------------------------------------------------------------
*        BUILD PARSE PARAMATER LIST
*--------------------------------------------------------------------
         SPACE 1
         LA    R3,WPARSPL          ADDRESS OF PARSE PARAMETER LIST
         USING PPL,R3
         MVC   PPLUPT,CPPLUPT      MOVE UPT ...
         MVC   PPLECT,CPPLECT           ... AND ECT POINTER
         LA    R1,WCPECB           (USW.,  KANN
         ST    R1,PPLECB              JEDER
         L     R1,AINPARS                IN DER
         ST    R1,PPLPCL                   LITERATUR
         MVC   PPLCBUF,CPPLCBUF               NACHLESEN)
         LA    R1,WDUMWK
         ST    R1,PPLUWA
         LA    R1,WPANSW
         ST    R1,PPLANS
         DROP  R2,R3
         SPACE 1
*----------------------------------------------------------------------
*        ANALYZE INPUT COMMAND
*---------------------------------------------------------------------
         SPACE 1
         SETAMODE 24
         CALLTSSR EP=IKJPARS,MF=(E,WPARSPL)  MUST RUN IN AMODE 24
         SETAMODE 31
         LTR   R15,R15             RETURN CODE ZERO ?
         BNZ   PARSERR             NO, PARSE ERROR
         L     R5,WPANSW           ADDRESS OF PDE
         USING INPDE,R5
         L     R4,INUNIT           ADDRESS OF UNITNAME
         LH    R3,INUNIT+4         LENGTH OF UNITNAME
         STH   R3,UNIL             STORE IN WORK AREA
         MVC   UNAME,BLANK         PRESET UNIT TO BLANKS
         BCTR  R3,0                LENGTH MINUS ONE FOR EXECUTE
         LA    R1,UNAME            MOVE ...
MOVUNIT  MVC   0(0,R1),0(R4)       THE ...
         EX    R3,MOVUNIT          UNITNAME
         CLC   UNIL(2),=H'3'       LENGTH EXACTLY 3 ?
         BE    ISUNIT              YES, INTERPRET AS UNIT NUMBER
         TM    UNAME,X'F0'         1ST CHAR. NUMERIC ?
         BO    ISGEN               YES, INTERPRET AS GENERIC NAME
         CLC   UNIL(2),=H'6'       LENGTH EXACTLY 6?
         BNE   ISESOT              NO, INTERPRET AS ESOTERIC NAME
ISVOL    OI    FLAG,VOL            ELSE INTERPRET AS VOLSER
         B     MOVEMODE
ISGEN    OI    FLAG,GEN            SET GENERIC NAME
         MVC   GTYPE,=CL15'GENERIC  NAME '
         B     MOVEMODE
ISESOT   OI    FLAG,ESOT           SET ESOTERIC NAME
         MVC   GTYPE,=CL15'ESOTERIC NAME '
         B     MOVEMODE
ISUNIT   OI    FLAG,UNI            SET UNIT NUMBER
MOVEMODE MVC   MODE,INMODE         MOVE MODE (ONLINE OR OFFLINE)
         MVC   DSNAME,BLANK        PRESET DSN TO BLANKS
         OC    INDSN(2),INDSN      DSN PRESENT ?
         BZ    USELPA              NO, USE LINK PACK AREA
         L     R4,INDSN2           ADDRESS OF DSNAME
         LH    R3,INDSN2+4         LENGTH OF DSNAME
         BCTR  R3,0                LENGTH MINUS ONE FOR EXECUTE
         LA    R1,DSNAME           MOVE THE ...
MOVDSN   MVC   0(0,R1),0(R4)       DSNAME ...
         EX    R3,MOVDSN           TO LOCATE AREA
         LA    R1,DATASET          MOVE THE ...
         EX    R3,MOVDSN           DSNAME ...
         LA    R6,DATASET+2(R3)    TO MESSAGE AREA
         MVC   0(10,R6),ONVOL      SQUEEZE BLANKS OUT
         MVC   VOLUME(6),BLANK     BLANK VOLUME FIELD
         OC    INVOL(2),INVOL      WAS VOLUME SPECIFIED ?
         BZ    LOCATE              NO, BRANCH TO LOCATE
         L     R4,INVOL2           GET VOLUME FROM PARSE
         MVC   VOLUME(6),0(R4)     MOVE VOLUME
         B     LOS                 SKIP LOCATE
         DROP  R5
         SPACE 1
*----------------------------------------------------------------------
*                 LOCATE DATA SET
*----------------------------------------------------------------------
         SPACE 1
LOCATE   LOCATE WCAMLST            LOCATE DSNAME FROM CATALOG
         LTR   R15,R15             DSNAME FOUND ?
         BZ    DSNFND              YES, USE VOLUME INFORMATION
         PUTMSG 'SPECIFIED DSNAME NOT FOUND IN CATALOG.',TPUT=TPUT
         B     LOS
DSNFND   MVC   VOLUME(6),LOCAREA+6 MOVE VOLUME TO MESSAGE AREA
*
LOS      LA    R3,34(R3)           SQUEEZE OUT BLANKS ...
         MVC   11(6,R6),VOLUME     FROM MESSAGE
*
         SETAMODE 24               ALLOCXR MUST ...
         CALL  ALLOCXR,(DDNAME,DSNAME,,VOLUME),VL
         SETAMODE 31               ... RUN IN AMODE 24
         LTR   R2,R15              SAVE RC IN CASE OF CRASH
         BZ    OPEN                BR TO OPEN IF SUCCESSFULL
         PUTMSG 'ALLOCATION FOR PRIVATE LIBRARY FAILED.'
         B     RETURN              EXIT IN CASE OF FAILURE
*
OPEN     OPEN  (LOADDCB,(INPUT))   OPEN THE DATASET
         TESTOPEN  LOADDCB,USELPA  WAS OPEN SUCCESSFUL
*
         BLDL  LOADDCB,BLDLIST     CHECK IF EDT IN LIBRARY
         LTR   R2,R15              SAVE RC FROM BLDL
         BNZ   NOMODUL             TELL USER 'BLDL FAILED'
*
         LA    R1,MESSAGE2         TELL USER NAME OF LIBRARY ...
         LR    R0,R3               AND VOLSER
         BAL   R14,TPUT            PUT THE MESSAGE
*
         LOAD  EP=IEFEDTTB,DCB=LOADDCB  LOAD EDT FROM LIBRARY
         LR    R11,R0              SAVE ITS ADDRESS
*
         B     START               BR TO EDT SCANNER
*
USELPA   PUTMSG 'NO PRIVATE LIBRARY ALLOCATED, LPA USED.'
         BLDL  0,BLDLIST           TEST IF EDTTAB IN LPA
         LTR   R2,R15              (WAT MUSSN DET FUER'N SYSTEM
         BNZ   NOLPA               SEIN, WAT KEENE EDT HAT ???)
*
         LOAD  EP=IEFEDTTB,DCB=0   LOAD EDT
         LR    R11,R0              SAVE ITS ADDRESS
*
START    DS    0H                  HIER GEHT ES RICHTIG LOS
*
         L     R9,12(R11)          PTR TO DLIST
         L     R4,0(R11)           PTR TO NMTAB
         LA    R3,8(R4)            PTR TO NMTBSTRT
         L     R4,0(R4)            # OF ENTRIES
         LA    R1,DLILEN           SAVE LENGTH OF ...
         STH   R1,DLILENX          ONE ENTRY
         USING NMENTRY,R3
LOOP1    DS    0H                  LOOP AROUND NM  ENTRIES
         MVC   XNAME(8),NMUNIT     SAVE GEN/ESOT NAME
         TM    FLAG,UNI+VOL        REQUEST FOR UNIT OR VOLSER ?
         BZ    LOOP1A              NO, LOCATE ONLY REQUESTED NAME
         STM   R3,R4,SAVE34        ELSE SAVE LOOP REGS AND CONSIDER
         B     FND                 ALL ENTRIES
LOOP1A   CLC   NMUNIT,UNAME        IS IT OUR ENTRY ?
         BE    FND                 YES
         LA    R3,NMLEN(R3)        ELSE INCR TO NEXT ENTRY
         BCT   R4,LOOP1            AND CYCLE
         B     NOTFND              NOT FOUND
FND      L     R5,NMGENTP          PTR TO GENTAB ENTRY
         L     R2,NMGENNO          # OF GENERICS FOR THIS TYPE
         USING GENENTRY,R5
***GEN****
LOOP0    DS    0H                  LOOP AROUND GEN ENTRIES
         L     R4,GENGRPNO         # OF ENTRIES THIS GENERIC
         L     R3,GENGRPP          PTR TO GRPTAB ENTRY
         USING GRPENTRY,R3
***GRP****
LOOP2    DS    0H                  LOOP AROUND GRP ENTRIES
         L     R6,GRPGDTP          PTR TO GDTTAB ENTRY
         USING GDTENTRY,R6
         LH    R7,GDTDLINO         # OF DEVICE ENTRIES
         LH    R8,GDTDLIIN         INDEX TO DEVICE
         BCTR  R8,0                DLITAB NUMB. STARTS WITH 1, NOT 0
         MH    R8,DLILENX          COMPUTE  DISPLACEMENT
         LA    R8,8(R8,R9)         IN DLITABLE
         USING DLIENTRY,R8
***DLI*
LOOP3    DS    0H                  LOOP AROUND DLI ENTRIES
         MVC   UNIT(3),DLIDEV      MOVE TO MESSAGE
***
         XUCBSCAN R10,X'00'        INITIALIZE UCBSCAN FOR ALL DEV
UCBLOOP  XUCBGET  R10,UNFND,UERROR PICK UP UCB POINTER
         CLC   UNIT(3),13(R1)      IS THIS THE REQUESTED UNIT ?
         BNE   UCBLOOP             NO,GET NEXT UCB POINTER
*
         TM    3(R1),X'80'         UNIT FOUND, IS IT ONLINE ?
         BZ    OFFLINE             NO, IS OFFLINE
         MVI   OFLAG,ON            SET FLAG TO 'ONLINE'
         MVC   MODX,=C'ONLINE '    PUT INTO MESSAGE
         MVC   VOLSER(6),28(R1)    PUT VOLSER INTO MESSAGE
         B     COMMON              BR TO MAINSTREAM
OFFLINE  MVI   OFLAG,OFF           SET FLAG TO 'OFFLINE'
         MVC   MODX,=C'OFFLINE'    PUT INTO MESSAGE
         MVC   VOLSER(6),BLANK     SET VOLSER TO BLANKS
COMMON   DS    0H
         XUCBOFF R10               FREE UCBSCAN WORK AREA
         NC    OFLAG(1),MODE+1     MATCH BETWEEN REQUEST AND STATUS ?
         BZ    BYPASS              NO, DO NOT DISPLAY
***
         TM    FLAG,VOL            REQUEST FOR VOLSER ?
         BZ    CHECKUNI            NO, CHECK FOR UNIT
         CLC   VOLSER(6),UNAME     IS IT REQUESTED VOLSER ?
         BNE   BYPASS              NO, DO NOT DISPLAY
         B     DISPLAY             ELSE GO TO DISPLAY
CHECKUNI TM    FLAG,UNI            REQUEST FOR UNIT ?
         BZ    DISPLAY             NO, MUST GENERIC OR ESOTERIC NAME
         CLC   UNIT(3),UNAME       IS IT REQUESTED UNIT ?
         BNE   BYPASS              NO, DO NOT DISPLAY
DISPLAY  OI    FLAG,FNDONE         INDICATE 'AT LEAST 1 ENTRY FOUND'
         LA    R1,MESSAGE1         LOAD MESSAGE ADDRESS
         LA    R0,MSGLEN1          LOAD MESSAGE LENGTH
         BAL   R14,TPUT            PUT THE MESSAGE
BYPASS   LA    R8,DLILEN(R8)       ELSE INCR TO NEXT ENTRY
         BCT   R7,LOOP3            END LOOP OVER DLITABLE
***DLI* END LOOP3
         LA    R3,GRPLEN(R3)       ELSE INCR TO NEXT ENTRY
         BCT   R4,LOOP2            END LOOP OVER GRPTABLE
***GRP**** END LOOP2
         LA    R5,GENLEN(R5)       ELSE INCR TO NEXT ENTRY
         BCT   R2,LOOP0            END LOOP OVER GENTABLE
***GEN**** END LOOP0
         TM    FLAG,UNI+VOL        WAS REQUEST FO UNIT OR VOLSER ?
         BZ    TESTFND             NO, WAS FOR GENERIC OR ESOTERIC
         LM    R3,R4,SAVE34        POP UP LOOP REGISTERS
         LA    R3,NMLEN(R3)        INCREMENT TO NEXT ENTRY
         BCT   R4,LOOP1            END LOOP NMTABLE
***NM***** END LOOP0
TESTFND  TM    FLAG,FNDONE         AT LEAST ONE LINE DISPLAYED
         BNZ   CLOSE               YES, CLOSE DCB
         PUTMSG 'NO DEVICE FOUND'  TELL USER NOTHING FOUND
CLOSE    TM    FLAG,OPENED         WAS DSNAME OPEN ?
         BZ    NOTOPEN             NO, SKIP CLOSE
         CLOSE (LOADDCB,)          CLOSE DCB
NOTOPEN  TM    FLAG,ALLOCD         WAS DSNAME ALLOCATED ?
         BZ    RETURN              NO, SKIP FREE
         SETAMODE 24               FREEXR MUST RUN ...
         CALL FREEXR,(DDNAME),VL   FREE DATASET
         SETAMODE 31               ...IN AMODE 24
RETURN   SCHL  RET=(R2)            RETURN TO CALLER
*
NOMODUL  PUTMSG 'EDT NOT FOUND IN THIS LIBRARY.'
         B     CLOSE
*
UNFND    PUTMSG 'UNIT NOT FOUND IN UCB OR EDT IS NOT XA.'
         B     CLOSE
*
UERROR   PUTMSG 'UCB ERROR OR EDT IS NOT XA.'
         B     CLOSE
*
NOTFND   PUTMSG 'REQUESTED GENERIC/ESOT. NAME NOT FOUND IN EDT.'
         LA    R2,4                SET RETUN CODE
         B     RETURN
*
PARSERR  PUTMSG 'PARSE ERROR.'
         LA    R2,8                SET RETURN CODE
         B     RETURN
*
NOLPA    PUTMSG 'EDT NOT FOUND IN LPA (SHOULD NEVER OCCUR).'
         LA    R2,12               SET RETURN CODE
         B     RETURN
*
BLANK    DC    CL78' '        MULTI PURPOSE BLANK FIELD
*
AINPARS  DC    A(INPARS)      ADDRESS OF PARSE PDE SECTION
WCPECB   DS    F              ATTENTION ECB
WPARSPL  DS    7F             PARSE PARAMETER LIST
WDUMWK   DS    F              DUMMY WORK AREA FOR PARSE
WPANSW   DS    2F             ANSWER AREA FOR PARSE
WCAMLST  CAMLST NAME,DSNAME,,LOCAREA
LOADDCB  DCB   DDNAME=EDTLIB,DSORG=PO,MACRF=R,RECFM=U
*
DDNAME   DC    CL8'EDTLIB'    INTERNAL DDNAME FOR ALLOCATE
*
BLDLIST  DS    0H             BLDL ENTRY
BLDLFF   DC    H'1'           NUMBER OF ENTRIES
BLDLLL   DC    H'76'          MAXIMUM LENGTH
BLDLNAME DC    CL8'IEFEDTTB'  PROGRAM NAME OF EDT
BLDLTTR  DS    XL3            TTR OF PROGRAM
BLDLK    DC    X'FF'          'K'-FIELD   (SEE...
BLDLZ    DC    X'FF'          'Z'-FIELD     SUPERVISOR...
BLDLC    DC    X'FF'          'C'-FIELD        MACROS (BLDL))
BLDLUSER DC    CL62' '        USERDATA
*
DLILENX  DC    H'0'           LENGTH OF DLI ENTRY
OFLAG    DC    X'00'          ONLINE/OFFLINE FLAG
ON       EQU   X'01'          ONLINE
OFF      EQU   X'02'          OFFLINE
*
FLAG     DC    X'00'          GENERAL FLAG
ESOT     EQU   X'80'          ESOTERIC NAME REQUESTED
GEN      EQU   X'40'          GENERIC  NAME REQUESTED
UNI      EQU   X'20'          UNIT NUMBER REQUESTED
VOL      EQU   X'10'          VOLSER REQUESTED
*
ALLOCD   EQU   X'04'          DATASET IS ALLOCATED
OPENED   EQU   X'02'          DATASET IS OPEN
FNDONE   EQU   X'01'          AT LEAST ONE DEVICE FOUND
*
MESSAGE1 DS    0C             MESSAGE AREA #1
GTYPE    DC    CL9'GEN/ESOT.' UNIT TYPE
         DC    C'NAME '       TEXT
XNAME    DC    CL8'X     '    UNIT NAME
         DC    C' CONTAINS  UNIT '
UNIT     DC    CL3'   '       UNIT NUMBER
         DC    C'   '
VOLSER   DC    CL6'   '       VOLUME SERIAL
         DC    C'   '
MODX     DC    CL7'X      '   ONLINE/OFFLINE
MSGLEN1  EQU   *-MESSAGE1     LENGTH OF MESSAGE
*
MESSAGE2 DC    C'EDT SEARCH FOR '
DATASET  DC    CL44' '        DATASET CONTAINING EDT
ONVOL    DC    C' ON VOLUME '
VOLUME   DC    CL6'      '    VOLSER OF DATASET
MSGLEN2  EQU   *-MESSAGE2     LENGTH OF MESSAGE
*
SAVE34   DS    2F             SAVE AREA FOR REG 3 AND 4
DSNAME   DC    CL44' '        DSNAME FIELD FOR LOCATE
UNAME    DC    CL8'X '        UNIT NAME
UNIL     DC    H'0'           UNIT NAME LENGTH
MODE     DC    H'0'           ONLINE/OFFLINE MODE FIELD
LOCAREA  DS    0D             WORK AREA ...
         DS    265C           FOR LOCATE
*
INPARS   IKJPARM DSECT=INPDE
INUNIT   IKJIDENT 'UNITNAME',PROMPT='UNITNAME',MAXLNTH=8,              *
               FIRST=ALPHANUM,OTHER=ANY,                               *
               HELP=('ESOTERIC OR GENERIC NAME TO BE CHECKED IN EDT.')
INDSN    IKJKEYWD
         IKJNAME 'DSNAME',SUBFLD=INDSN1
INVOL    IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=INVOL1
INMODE   IKJKEYWD DEFAULT='ONLINE'
         IKJNAME 'ONLINE'
         IKJNAME 'OFFLINE'
         IKJNAME 'ALL'
INDSN1   IKJSUBF
INDSN2   IKJPOSIT DSNAME,PROMPT='EDT-DSNAME',                          *
               HELP=('NAME OF DATA SET TO CHECKED FOR EDT.')
INVOL1   IKJSUBF
INVOL2   IKJIDENT 'VOLUME',FIRST=ALPHANUM,OTHER=ALPHANUM,              *
               MAXLNTH=6,HELP=('VOLUME ON WHICH DATASET IS EXPECTED'), *
               PROMPT='ENTER VOLUME (6 CHAR.)'
         IKJENDP
*
*  MAPPING DSECTS FOR EDT SUBTABLES
*
NMENTRY  DSECT                     NAME LOOKUP SECTION (NMTAB)
NMUNIT   DS    CL8                 UNIT NUMBER IN EBCDIC
NMINTCD  DS    1F                  INTERNAL DEVICE CODE
NMMMASK  DS    1A                  MAIN MASK
NMGENNO  DS    1A                  # OF GENERICS FOR THIS NAME
NMGENTP  DS    1A                  PTR TO FIRST GENTAB ENTRY
NMFLAGS  DS    BL.6                FLAG SETTINGS
NMPAD    DS    1A                  PAD TO FULLWORD BOUNDARY
NMLEN    EQU   *-NMENTRY           LENGTH
*
GENENTRY DSECT                     GENERIC SECTION (GENTAB)
GENINTCD DS    1F                  INTERNAL DEVICE CODE
GENGRPNO DS    1A                  # OF GROUPS FOR THIS GENERIC
GENGRPP  DS    1A                  PTR TO FIRST GROUP POINTER
GENLEN   EQU   *-GENENTRY          LENGTH
*
GRPENTRY DSECT                     GROUP POINTER SECTION (GRPTAB)
GRPGDTP  DS    1A                  PTR TO GDT ENTRY
GRPLEN  EQU   *-GRPENTRY           LENGTH
*
GDTENTRY DSECT                     GROUP DESCRIPTOR SECTION (GDTTAB)
GDTGRPID DS    1H                  GROUP ID
GDTDLINO DS    1H                  # OF DEVICES IN THE GROUP
GDTDLIIN DS    1H                  INDEX TO FIRST GROUP
GDTLEN   EQU   *-GDTENTRY          LENGTH
*
DLIENTRY DSECT                     UCB DEVICE ADDRESS SECTION (DLITAB)
DLIDEV   DS    CL3                 DEVICE ADDRESS IN EBCDIC
DLILEN   EQU   *-DLIENTRY          LENGTH
*
         REGISTER
         PRINT NOGEN
         IKJCPPL
         IKJPPL
         CVT   DSECT=YES
         END
