* STAND 01/12/78
***********************************************************************
*                                                                     *
*    PROGRAMM  = AVERGL                                               *
*                                                                     *
*    AUFRUF    = CALL AVERGL,(ERR,ARGLIST)                            *
*        F       ERR     = RETURNCODE = X'00000000'  KEIN FEHLER      *
*                                       X'00000001'  INT.ABST.FALSCH  *
*                                       X'00000002'  EXT.ABST.FALSCH  *
*                                       X'00000004'  NICHT ALLE SUMMEN*
*                                                    VERGLICHEN       *
*                          DIE FEHLERCODES KOENNEN MIT OR VERKNUEPFT  *
*                          AUFTRETEN                                  *
*        A       ARGLIST = ADRESSE DER ARGUMENTENLISTE, DIE           *
*                          VOM DIE ABSTIMMUNG AUFRUFENDEN             *
*                          PROGRAMM UEBERGEBEN WIRD                   *
*                          AUSNAHME = BEI AUFRUF VON COBAST BZW. PL1AST
*                                     WIRD IN ABSTROOT EINE ARGUMENTEN=
*                                     LISTE MIT ARGTYP=0 AUFGEBAUT    *
*                                                                     *
*    ZWECK     = UEBERTRAGEN DER SUMMEN, MIT DENEN ABGESTIMMT WERDEN  *
*                SOLL NACH SETAB UND DURCHFUEHRUNG DES ABSTIMM=       *
*                VERGLEICHS FUER DIE IN DER ARGUMENTENLISTE AUF=      *
*                GEFUEHRTEN ABSTIMMSUMMEN                             *
*                                                                     *
***********************************************************************
         PRINT NOGEN
AVERGL   CSECT
         XSAVE 12,SAVE,AVERGL
         SPACE 1
         AREG
RA       EQU   9                        --> ATAB
RS       EQU   10                       --> STAB
RSE      EQU   11                       --> SETAB
RN       EQU   8                        ANZAHL SETAB
         SPACE 1
         USING DATAB,RA
         USING DSTAB,RS
         USING DSETAB,RSE
         L     RA,PATAB                 --> ATAB
         L     RS,PSTAB                 --> 1.STAB
         SPACE 1
*  ARGUMENTENADRESSEN BZW. ARGUMENTE ZWISCHENSPEICHERN
         L     R3,0(R1)
         ST    R3,RETADR                --> RETURNCODE
         XC    0(4,R3),0(R3)            RETURNCODE = 0
         L     R3,4(R1)                 --> AUFRUFARGUMENTENLISTE-ADR.
         L     R3,0(R3)                 --> AUFRUFARGUMENTENLISTE
         MVI   ARGTYP,C'0'
         LTR   R3,R3                    -->ARGLISTE = 0 -->
         BZ    ARGOK                    ALLE SUMMEN VERGLEICHEN
         L     R4,0(R3)                 --> ARGTYP (COBOL)
         CLI   ASPRACHE,PL1
         BNE   NOPL12
         L     R4,0(R4)                 --> ARGTYP (PL/1)
NOPL12   EQU   *
         MVC   ARGTYP,0(R4)             ARGTYP ZWISCHENSPEICHERN
         OI    ARGTYP,X'F0'             EINHEITLICHES VORZEICHEN
ARGOK    EQU   *
         ZAP   ARGZAE,=P'0'
         CLI   ARGTYP,ARGALL
         BE    ALLSUM                   ALLE ABST.SUM VERGLEICHEN
         LA    R4,4(R3)                 --> ADR.1.ABST.SUMME
         ST    R4,REG1P4
         ZAP   ARGANZ,=P'0'             ANZAHL SUMMEN = 0
LOOPV    EQU   *    ANZAHL DER ZU VERGLEICHENDEN SUMMEN FESTSTELLEN
         AP    ARGANZ,=P'1'             ANZAHL + 1
         TM    0(R4),X'80'
         BO    LOOP1                    LETZTE ADRESSE
         LA    R4,4(R4)                 --> NAECHSTE ADRESSE
         B     LOOPV
ALLSUM   EQU   *
         ZAP   ARGANZ,=P'999'           ANZAHL = HOHERWERT
         SPACE 3
*    S C H L E I F E  UEBER ALLE  A B S T I M M S U M M E N
*    ------------------------------------------------------
LOOP1    EQU   *
         CLC   SNEXT,=F'-1'
         BE    ENDE                     STAB DURCHSUCHT
         SPACE 1
         CLI   ARGTYP,ARGALL
         BE    ARGFOUND                 ALLE ABST.SUM. VERGLEICHEN
         L     R3,REG1P4                REG1+4 = --> SUM.ADR IM AUFRUF
LOOPARG  EQU   *    PRUEFEN, OB DIESE ABST.SUM. VERGLICHEN WERDEN SOLL
         L     R4,0(R3)                 --> SUMME FALLS COBOL-AUFRUF
         CLI   ASPRACHE,PL1
         BNE   NOPL1
         L     R4,0(R4)                 --> SUMME FALLS PL/1-AUFRUF
NOPL1    EQU   *
         CLI   ARGTYP,ARGTAB
         BE    VERTAB
         CLC   SPOS,0(R4)               POSITIONS-NR MIT AUFRUF-NR VERG
         BE    ARGFOUND                 --> DIESE ABST.SUM SOLL VERGL.W
         BH    LOOPARGE
         BL    LOOP1E1
VERTAB   EQU   *
         CLC   SIX,0(R4)                TAB-INDEX MIT AUFRUF-NR VERGL.
         BE    ARGFOUND                 --> DIESE ABST.SUM SOLL VERGL.W
         BL    LOOP1E1
LOOPARGE EQU   *
         TM    0(R3),X'80'
         BO    ENDE                     LETZTE AUFRUF-EINTRAGUNG
*                                       D.H. ABST.SUM SOLL NICHT
*                                       VERGLICHEN WERDEN
         LA    R3,4(R3)                 -->NAECHSTE SUM.ADR IM AUFRUF
         ST    R3,REG1P4               --> NAECHSTES ARGUMENT
         B     LOOPARG
         SPACE 2
*  ABSTIMMSUMME SOLL VERGLICHEN WERDEN
ARGFOUND EQU   *
         AP    ARGZAE,=PL1'1'           ZAEHLER D.BEARBEITETEN ARG.
         LA    R3,SSUM
         BAL   R2,MVCABST               ABST.SUM --> SSUM UEBERTRAGEN
         MVI   SSUMDA,SAOK
         SPACE 2
         XR    R6,R6                   NR D.LETZTEN VERGL.BEI ODER
         MVI   KENN,0
         LA    RSE,LSTAB(RS)            -->1.SETAB
         LH    RN,NSETAB
         LTR   RN,RN
         BNZ   LOOP20                   ABSTIMMHINWEIS VORHANDEN
         MVI   SAERG,SAOK               KEIN VERGLEICH VERLANGT D.H.
*                                       ABSTIMMUNG OK
         B     LOOP1E01
         SPACE 1
*  SCHLEIFE UEBER ABSTIMMHINWEISE JE ABSTIMMSUMME (SETAB)
LOOP2    EQU   *
         CLI   SEOP,ODER               ODER ?
         BNE   LOOP20
         MVI   KENN,SAODER    KENNUNG FUER ODER
         B     LOOP13                               VERGLEICH
LOOP20   EQU   *
         CLI   SATYP,SAEXT
         BE    LOOP2EX                  EXTERNE ABSTIMMUNG
         SPACE 2
*  ABSTIMMHINWEIS INTERN
         LR    R0,RS                    RS ZWISCHENSPEICHERN
         L     RS,SEMITTX1              -->STAB, MIT D.ABGEST.WERDEN S.
         CLI   SSUMDA,SAOK
         BNE   LOOP21                   SUMME STEHT NICHT IN SSUM
         MVC   SESUM,SSUM
         B     LOOP22
LOOP21   EQU   *         SUMME AUS AUFRUFENDEN PGM NACH SESUM UEBERTR.
         LA    R3,SESUM
         BAL   R2,MVCABST
         MVI   SESUMDA,SAOK
LOOP22   EQU   *
         LR    RS,R0                    RS ZURUECKSPEICHERN
         B     LOOP25
         SPACE 2
*  ABSTIMMHINWEIS EXTERN
LOOP2EX  EQU   *
         CLI   ADCBSW,DDFEHLT
         BE    LOOP238                 DD-KARTE ASTSUM FEHLT
         LA    R7,WORK-VORLN
         USING DASATZ,R7
         LA    R5,1                    1.DURCHLAUF
GETEX    EQU   *
         LA    R3,SEMITTXT             ABST.SUM.NR
         LA    R4,SEPER                PERIODE
         CALL  GETASUM,(ERR,(3),WORK,(R4)),VL
         CLC   ERR,=F'0'
         BNE   LOOP235                 FEHLER
         CLC   SEMITTX1,AASTC
         BNE   LOOP23                  WIEDERHOLUNG UNINTERESSANT
         LTR   R5,R5
         BZ    LOOP23                  AREPEAT-TEST NUR BEI 1.DURCHL.
         CLI   AREPEAT,RNO             KEINE WIEDERHOLUNG
         BE    LOOP23
         CLI   AREPEAT,RYES            WIEDERHOLUNG
         BE    LOOP228
         L     R3,PBZEIT               VERGLEICH BERICHTSZEIT PGM
         CLC   BERZT,0(R3)             MIT BERICHTSZEIT ASUM-DATEI
         BNE   LOOP23
LOOP228  EQU   *
         LH    R3,SEPER                BEI GLEICER BERICHTSZEIT HAN=
         LA    R3,1(R3)                DELT ES SICH UM EINEN WIEDER=
         STH   R3,SEPER                HOLUNGSLAUF U.VORANGEGANGENE
         XR    R5,R5                   2.DURCHLAUF
         B     GETEX                   PERIODE WIRD BENOETIGT
LOOP23   EQU   *
         CLI   FLAG,NONE
         BNE   LOOP24                  SUMME VORHANDEN
LOOP235  EQU   *
         CLC   ERR,=F'3'
         BNE   LOOP238
         MVI   ACCT,12
         MVI   AUPDTE,SUNO             DD-KARTE ASTSUM FEHLT
         MVI   ADCBSW,DDFEHLT          DD-KARTE ASTSUM FEHLT
LOOP238  EQU   *
         MVI   SAERG,SAIMPOS            SUMME IN DATEI NICHT GEFUNDEN
         B     LOOP2E
LOOP24   EQU   *
         MVC   SESUM,AWERT             ABST.SUMME --> SETAB
         DROP  R7
         SPACE 2
*  VERGLEICHSABSTIMMSUMME BILDEN
LOOP25   EQU   *
         MVI   SESUMDA,SAOK             SESUM GEFUELLT
         LA    R3,SESUM                 OPERAND
         LA    R4,SEOP                  OPERATOR
         LA    R5,SVERSUM               ERGEBNIS
         CALL  BERERG,((R3),(R4),(5))   RECHNEN
LOOP2E   EQU   *
         MVC   ZWIATYP,SATYP           ABSTIMMTYP ZWISCHENSPEICHERN
         LA    RSE,LSETAB(RSE)          -->NAECHSTER ABSTIMMHINWEIS
         BCT   RN,LOOP2
         SPACE 2
LOOP13   EQU   *
         LA    R6,1(R6)                NR DES VERGLEICHS + 1
         STC   R6,SAVERNR              NR DES VERGLEICHS BEI ODER
LOOP14   EQU   *
*  VERGLEICHSSUMME IST GEBILDET
         CLI   SAERG,SAIMPOS
         BE    LOOP16                   ABST.ERGEBNIS SCHON FESTGEST.
         CLC   SSUM,SVERSUM             V E R G L E I C H
         BNE   LOOP15
         MVI   SAERG,SAOK
         B     LOOP1E00
LOOP15   EQU   *
         MVI   SAERG,SAFALSE
LOOP16   EQU   *              RETURNCODE SETZEN
         L     R2,RETADR                --> RETURNCODE
         CLI   ZWIATYP,SAEXT
         BE    LOOP17
         OI    3(R2),1                  INTERNE ABSTIMMUNG FALSCH
         B     LOOP1E
LOOP17   EQU   *
         OI    3(R2),2                  EXTERNE ABSTIMMUNG FALSCH
         SPACE 2
* DIESE STAB-EINTRAGUNG BEARBEITET
LOOP1E   EQU   *
         LTR   RN,RN
         BZ    LOOP1E00                SETAB DURCHSUCHT
         MVI   SAERG,SANO
         ZAP   SVERSUM,=P'0'           WEITERER VERGLEICH, DA ODER
         B     LOOP20
LOOP1E00 EQU   *
         CLI   ZWIATYP,SAEXT      GENERELLES ABST.ERGEBNIS SETZEN
         BE    LOOP1E0
         OC    AERGI,SAERG             INT.ABST.
         OC    AERGI,KENN     FALLS ODER
         B     LOOP1E01
LOOP1E0  EQU   *
         OC    AERGE,SAERG             EXT.ABST.
         OC    AERGE,KENN     FALLS ODER
LOOP1E01 EQU   *
         CLI   KENN,SAODER
         BE    LOOP1E02
         MVI   SAVERNR,0
LOOP1E02 EQU   *
         CLI   ARGTYP,ARGALL
         BE    LOOP1E1
         L     R3,REG1P4               --> ARGUMENT (LFD.)
         TM    0(R3),X'80'
         BO    ENDE                    LETZTES ARGUMENT BEARBEITET
         LA    R3,4(R3)
         ST    R3,REG1P4               --> NAECHSTES ARGUMENT
LOOP1E1  EQU   *
         L     RS,SNEXT                 --> NAECHSTE STAB
         B     LOOP1                   SPRUNG SCHLEIFENANFANG
         SPACE 2
*              A B S C H L U S S
*              -----------------
ENDE     EQU   *
         CLI   ARGTYP,ARGALL
         BE    ENDE1
         CP    ARGZAE,ARGANZ
         BE    ENDE1               ALLE ANGEFORD.ABST.SUM VERGLICHEN
         SPACE 1
         LA    R2,14               NICHT ALLE ANGEFORDERTEN
         ST    R2,ERR              ABST.SUMMEN GEFUNDEN
         CALL  DRUFEHL,(ERR),VL         FEHLERMELDUNG DRUCKEN
         L     R2,RETADR                --> RETURNCODE
         LA    R3,4
         O     R3,0(R2)            NICHT ALLE VERGLEICHE AUSGEFUEHRT
         SPACE 1
ENDE1    EQU   *
         XRETURN 0
         PRINT GEN
         MVCABST
         SPACE 3
ODER     EQU   1
*              D E F I N I T I O N E N
*              -----------------------
REG1P4   DS    A                   --> SUMMENADRESSEN
RETADR   DS    A                   --> RETURNCODE
ERR      DS    F
PATAB    DC    V(ATAB)             --> ATAB
ARGZAE   DS    PL3                 ANZ.D.BEARBEITETEN SUMMEN
ARGANZ   DS    PL3                 ANZ.SUMMEN IN AUFRUFLISTE
ARGTYP   DS    CL1
ARGALL   EQU   C'0'                ALLE SUMMEN VERGLEICHEN
ARGTAB   EQU   C'1'                SUMMEN MIT TABELLEN-INDEX BESCHRIEB.
ARGPOS   EQU   C'2'                SUMMEN MIT POSITIONS-NR BESCHRIEBEN
ZWIATYP  DS    CL1
KENN     DS    CL1                      ODER MERKEN
         PRINT GEN
         MATAB
         MSTAB
         MASATZ
AVERGL   CSECT
WORK     DS    CL(PERLN)
         END
