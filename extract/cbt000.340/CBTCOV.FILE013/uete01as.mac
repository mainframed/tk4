         TITLE 'SYSTPIG'
         EJECT
SYSTPIG  CSECT
* REGISTER USSAGE.
R15      EQU   15
RET      EQU   14
R14      EQU   14
SAVE     EQU   13
R13      EQU   13
BASE     EQU   12
LK1      EQU   11
LK2      EQU   10
SR1      EQU   9
R8       EQU   8
R7       EQU   7
R6       EQU   6
R5       EQU   5
R4       EQU   4
R3       EQU   3
W        EQU   2
R1       EQU   1
R0       EQU   0
         USING *,R15
         B     PIGPROG            BRANCH AROUND ID
         DC    AL1(7),CL7'SYSTPIG'    ID LENGTH, ID
PIGPROG  DS    0H
         STM   R14,BASE,12(SAVE)  SAVE REGS
         LR    BASE,R15           SET BASE
         DROP  R15
         USING SYSTPIG,BASE
         LA    W,SAVEAREA
         ST    W,8(SAVE)
         ST    SAVE,4(W)
         LR    SAVE,W
         LR    SR1,R1             SAVE REG 1
         L     R0,ENQLEN  GET AREA FOR ENQ TABLE
         GETMAIN R,LV=(R0)
         ST    R1,ENQTADDR   AND SAVE ITS ADDRESS
         LA    R1,0(R1)      REMOVE HIGH ORDER BYTE
         A     R1,ENQLEN   POINT TO END OF TABLE
         ST    R1,ENQTEND     AND SAVE THE ADDR
         MVC   MAJNAME,=CL8'SYSDSN'  SET QUEUE NAME TO DEFAULT
WTOR   MVC   ENQPTR,ENQTADDR SET FLOATING POINTER TO BEGINNING
         XC    WTOECB,WTOECB
         MVI   ANS,C' '   SET ANSWER FIELD TO BLANK+,
         MVC   ANS+1(99),ANS
         PRINT NOGEN
         WTOR  'ENTER SYSTPIG COMMAND OR HELP',ANS,100,WTOECB
         WAIT  ECB=WTOECB
         CLC   ANS(5),=C'HELP '   A PLEA FOR HELP/Q
         BNE   COMMAND     NO CHECK FOR COMMAND
         WTO   'DER SYSTEM PIG SUCHT DENJENIGEN JOB ODER TSO ANWENDER'
         WTO   'DER EINEN RESOURCE BLOCKIERT.'
         WTO   'EINGABE WIE FOLGT:'
         WTO   '    RESOURCE-NAME                   ODER'
         WTO   '    R=RESOURCE-NAME'
         WTO   '    Q=QUEUE-NAME  (WAHLWEISE)'
         WTO   ' DER DEFAULT FUER Q=QUEUE-NAME IST Q=SYSDSN'
         WTO   ' IN DIESEM FALLE MUSS DER DATASET NAME, DER GESUCHT'
         WTO   ' WIRD, ALS RESOURCE-NAME ANGEGEBEN WERDEN'
         WTO   '*******'
         WTO   '    UM DAS PROGRAMM ZU BEENDEN KOENNEN SIE '
         WTO   '    EINEN VON DEN FOLGENDEN BEFEHLEN ANGEBEN:'
         WTO   ' ''C'',''CANCEL'',''END'',''ENDE'' ODER ''E'''
         PRINT GEN
         B     WTOR
COMMAND  CLC   ANS(2),=C'Q='       QUEUE NAME/Q
         BNE   ISITR               NO CHECK FOR R=
         MVC   MAJNAME,ANS+2       SET MAJOR NAME
         B     WTOR                GET MINOR NAME
ISITR    CLC   ANS(2),=C'R='       IS IT RESOURCE/Q
         BNE   ISITEND             NO CHECK FOR END
         MVC   MINAME(98),ANS+2
         B     SETMINLN
ISITEND  CLC   ANS(7),=C'CANCEL '
         BE    RETURN
         CLC   ANS(2),=C'C '
         BE    RETURN
         CLC   ANS(2),=C'E '
         BE    RETURN
         CLC   ANS(5),=C'ENDE '
         BE    RETURN
         CLC   ANS(4),=C'END '
         BE    RETURN
         MVC   MINAME,ANS   MOVE ANSWER TO RESOURCE NAME
SETMINLN EQU   *
         LA    W,0
         LA    R1,MINAME  FIND THE NUMBER OF CHARS IN MINOR NAME
         LA    R0,100  SET LIMIT
SEEKLEN  CLI   0(R1),C' '
         BE    LENFND  GOT THE END
         LA    W,1(W)   COUNT THE CHARACTERS
         LA    R1,1(R1)
         BCT   R0,SEEKLEN
LENFND   STC   W,MNLEN   SAVE THE LENGTH
         NOP   NOMODE   BRANCH ROUND THE MODESET
         OI    *-3,X'F0'   ONLY THE FIRST TIME
         SPACE 3
**********************************************************************
*
*              SET KEY TO ZERO
*
**********************************************************************
         SPACE
         MODESET KEY=ZERO,MODE=SUP
*
**********************************************************************
         SPACE
         ESTAE STAEROUT       SET UP THE STAE EXIT
NOMODE   EQU   *
         STATUS SET,MC,STEP
         OI   STATUSET,X'80'    INDICATE SYSTEM NONDISPATCHABLE
         L    R1,16   CVT ADDR
         L    W,640(R1)     MAJOR QCB IN WORK REG
GNMAJOR  LTR  R1,W          ADDR OF QCB IN R1
         BZ   NOMAJOR       NO ADDR = NO MAJOR LEFT = ****ERROR***
         L    W,0(R1)        SAVE ADDR  OF NEXT MAJOR QCB
         CLC  16(8,R1),MAJNAME  HAVE WE GOT THE RIGHT QCB/Q
         BNE  GNMAJOR       NO TRY THE NEXT ONE
         L     W,8(R1)    GET ADDR OF MINOR QCB IN WORK REG
GNMINOR  LTR   R1,W       GET ADDR OF MINOR QCB IN R1
         BZ    NOMINOR    ZERO = NO ADDR = ***ERROR***
         L     W,0(R1)    SAVE ADDR OF NEXT MINOR QCB
         CLC   16(1,R1),MNLEN  COMPARE THE LENGTHS
         BNE   GNMINOR  NOT THE SAME SO WE ARE WRONG
         XR    R3,R3      ZERO REG3
         IC    R3,MNLEN   GET LENGTH OF MINOR NAME
         BCTR  R3,0       SUBTRACT ONE
         EX    R3,CLCMINAM  COMPARE IT
         BNE   GNMINOR    NO TRY THE NEXT MINOR QCB
         L     R3,8(R1)    GET ADDR OF QEL
GNQEL    LTR   R4,R3    POINT R4 AT NEXT QEL OF LIST
         BZ    CHAINEND  END OF QEL CHAIN
         L     R3,0(R4) SAVE ADDR OF NEXT QEL IN LIST
         L     R5,ENQPTR  GET NEXT ENTRY ADDR IN ENQ TABLE
         MVC   0(4,R5),12(R4)   SAVE THE QEL IN THE ENQ TABLE
*      THE ENQPTR WILL BE INCREASED WHEN THE JOBNAME HAS BEEN FOUND
         L     R5,16        GET THE CVT
          L     R6,X'22C'(R5)    POINTER TO THE ASVT
          LA    R5,X'20C'(R6)   START OF ASCB POINTERS
          LH    R6,14(R4)       INDEX THE RIGHT ASCB POINTERR
          SLL   R6,2       BY MULTIPLYING THE ASID BY 4
          L     R5,0(R6,R5)  GET ASCB ADDR IN R5
          L     R6,X'B0'(R5)  GET JOB/LOGON NAME
          LTR   R6,R6        TEST FOR ZERO
          BNZ   GOTJN1        FOUND
         LA    R6,=8C'*'   SET JOB NAME TO *
GOTJN1   L     R8,X'AC'(R5)   GET SECOND JOB NAME
         LTR   R8,R8
         BNZ   GOTJN2
         LA    R8,=8C'*'    SET JOB NAME 2 TO *
GOTJN2    L     R7,ENQPTR     GET NEXT TABLE
         MVC   4(8,R7),0(R6)  MOVE IN THE JOB NAME
         MVC   12(8,R7),0(R8)   SECOND JOB NAME
         LA    R7,20(R7)   INCREASE R7 TO NEXT ENTRY
         C     R7,ENQTEND  TOO MANY/Q
         BE    TABERR TABLE IS FULL. MORE THAN 400 USERS
         ST    R7,ENQPTR   SAVE NEW ADDR
         B     GNQEL     GET THE NEXT QEL IN THE LIST
CLCMINAM CLC   20(1,R1),MINAME  COMPARE THE NAMES
TABERR   WTO   'ENQ TABLE FULL. MORE THAN 400 USERS FOR THIS RESOURCE'
         B     CHAINEND    PRINT THE CONTENTS EVEN IF IRREGULAR
NOMAJOR  WTO   'NO SUCH MAJOR'
         STATUS RESET,MC,STEP
         NI    STATUSET,X'7F'  TURN OFF INDICATOR
         B     WTOR
NOMINOR  WTO   'NO SUCH MINOR'
         STATUS RESET,MC,STEP
         NI    STATUSET,X'7F'  TURN OFF INDICATOR
         B     WTOR
         EJECT
CHAINEND DS    0H
         STATUS RESET,MC,STEP
         NI    STATUSET,X'7F'  TURN OFF INDICATOR FOR NON DISP
         L     W,ENQTADDR    GET ADDR OF ENQ TABLE
PRNTTAB  MVC   OUTWTO+8(8),4(W)  JOBNAME
         MVC   OUTWTO+17(8),12(W)  MOVE IN SECOND JOB NAME
         TM    0(W),QELSHARE IS  THE SHARE BIT ON
         BO    SHARED         YES BRANCH
         MVC   OUTWTO+26(9),=C'EXCLUSIVE'
         B     *+10
SHARED   MVC   OUTWTO+26(9),=CL9'SHARED'
OUTWTO   WTO   'XXXXXXXX XXXXXXXX LLLLLLLLL           '
         LA   W,20(W) INCREASE WORK REG TO NEXT ENTRY
         C    W,ENQPTR   END OF THE TABLE/Q
         BNE  PRNTTAB    NO PRINT SOME MORE
         B     WTOR   GO AND GET MORE REQUESTS
         SPACE 3
**********************************************************************
*
*        SYSTPIG  EXIT
*
**********************************************************************
         SPACE
RETURN   MODESET KEY=NZERO
         L     R0,ENQLEN   LENGTH AND SUBPOOL
         L     R1,ENQTADDR   AREA ADDR
         FREEMAIN R,LV=(R0),A=(R1)
         L     SAVE,4(SAVE)
         SR    R15,R15
         IC    R15,RCODE
         RETURN (14,12),RC=(15)
         EJECT
         PUSH  USING
         USING *,15
STAEROUT STM   14,13,STAESV
         C     0,=F'12'      IS THERE AN SDWA
         BE    NOSDWA        NO
         STM   14,12,12(13)  SAVE REGS IN OWN SA
         B     STAECONT      AND BRANCH ROUND IT
STAESV   DS    16F
STAECONT LR    3,15       GET BASE REG 3
         DROP  15
         LR    4,1      SAVE ADDR OF SDWA
         USING STAEROUT,3
         TM    STATUSET,X'80'  HAVE WE SET THE SYSTEM/Q
         BNO   DUMP1          NO, BRANCH
         STATUS RESET,MC,STEP  SET SYSTEM DISPATCHABLE
DUMP1    SETRP REGS=(14,12),DUMP=IGNORE,WKAREA=(4)
NOSDWA   LR    3,15
         TM    STATUSET,X'80'    HAVE WE SET THE SYSTEM NON DISP/Q
         BNO   DUMP2             NO  BRANCH
         STATUS RESET,MC,STEP
DUMP2    LM    14,13,STAESV      RESTORE REGS
         LA    15,0              CONTINUE WITH ABEND
         BR    14                AND RETURN
STATUSET DC    X'00'
         POP   USING
         EJECT
*
RCODE    DS    X
*
**********************************************************************
         LTORG
         EJECT
WTOECB   DC    F'0'
MAJNAME  DC    CL8' '   MAJOR NAME
MINAME   DC    CL100' '
ANS      DC    CL100' '
ENQPTR   DC    A(0)
ENQTADDR DC    A(0)
ENQTEND  DC    A(0)
MNLEN    DC    AL1(44)
SAVEAREA DS    18F
QELSHARE EQU   X'80'
ENQLEN   DC    F'8000'
         EJECT
         IHASDWA
         END
