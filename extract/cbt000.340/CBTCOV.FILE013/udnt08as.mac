         TITLE 'STATISTIK UEBER FEHLERHAFTE MAGNETBAENDER (SMF TYP 21)'
SMFTYP21 CSECT
         BASER 1
         REG
         PRINT NOGEN
         EJECT
START    EQU   *
         LM    R2,R4,0(R1)
         STM   R2,R4,APARM
         LR    R5,R2
         USING COMMON,R5
         L     R3,0(R3)
         USING IFASMF21,R3
ERSTER   NOP   WEITERE
         CALL  DATUM,(FDAT)
         MVC   MDAT,FDAT     DATUM UEBERTRAGEN
         OI    ERSTER+1,X'F0'
WEITERE  EQU   *
         TM    COMEODID,X'FF'
         BO    EODTYP21
         SPACE 3
         AP    ZBDT,=P'1'         BAENDER TOTAL ADDIEREN
         CLI   SMF21PR,X'00'      KEINE PERM. LESE-FEHLER ??
         BNE   VERARB             LESE-FEHLER VERARBEITEN
         CLI   SMF21PW,X'00'      KEINE PERM. SCHREIB-FEHLER ??
         BNE   VERARB             SCHREIB-FEHLER VERARBEITEN
         CLI   SMF21TR,X'00'      KEINE TEMP. LESE-FEHLER ??
         BNE   PROZ               LESE-FEHLER VERARBEITEN
         CLI   SMF21TW,X'00'      KEINE TEMP. SCHREIB-FEHLER ??
         BNE   PROZ               SCHREIB-FEHLER VERARBEITEN
         CLI   SMF21NB,X'00'      KEINE NOISE-BLOCKS ??
         BNE   PROZ               NOISE-BLOCKS VERARBEITEN
         CLC   SMF21ERG,=2X'00'   KEINE ERASE-GAPS  ??
         BNE   PROZ               ERASE-GAPS VERARBEITEN
         SPACE 2
         B     END99              ZUM HAUPTPROGRAMM ZURUECK
PROZ     EQU   *
         XR    R1,R1              LOESCHEN
         IC    R1,SMF21TR         TEMP. LESEFEHLER
         XC    HW1,HW1            LOESCHEN
         MVC   HW1+1(1),SMF21TW     TEMP. SCHREIBFEHLER
         AH    R1,HW1             ADDIEREN
         XC    HW1,HW1            LOESCHEN
         MVC   HW1,SMF21ERG
         AH    R1,HW1
         XC    DW1,DW1
         CVD   R1,DW1
         MP    DW1,=P'10000'
         MVC   HW1,SMF21SIO       START I/O'S
         XR    R1,R1
         LH    R1,HW1             NACH REG 1
         XC    DW2,DW2
         CVD   R1,DW2             KONVERTIEREN
         MVC   FW1,DW2+4          UEBERTRAGEN
         XC    DW2,DW2
         MVC   DW2+4(4),DW1+4
         DP    DW2,FW1
         CP    DW2(4),C21PROZ
         BNL   VERARB
         B     END99
         EJECT
VERARB   EQU   *
         SPACE
         XC    DW1,DW1            LOESCHEN
         XR    R1,R1              LOESCHEN
         IC    R1,SMF21TR         1 BYTE UEBERTRAGEN
         CVD   R1,DW1             KONVERTIEREN DER LESE-FEHLER
         MVC   TR,MASK1           MASKE UEBERTRAGEN
         ED    TR,DW1+4           AUSGABEFELD AUFBEREITEN
         A     R1,C21TR           KUMMULIEREN DER LESE-FEHLER
         ST    R1,C21TR           SPEICHERN DER LESE-FEHLER
         SPACE 2
         XC    DW1,DW1            LOESCHEN
         XR    R1,R1              LOESCHEN
         IC    R1,SMF21TW         1 BYTE UEBERTRAGEN
         CVD   R1,DW1             KONVERTIEREN DER SCHREIB-FEHLER
         MVC   TW,MASK1           MASKE UEBERTRAGEN
         ED    TW,DW1+4           AUSAGABEFELD SCHREIB-FEHLER AUFBER.
         A     R1,C21TW           KUMMULIEREN DER SCHREIB-FEHLER
         ST    R1,C21TW           SPEICHERN DER SCHREIB-FEHLER
         SPACE 2
         XC    DW1,DW1            LOESCHEN
         XR    R1,R1              LOESCHEN
         IC    R1,SMF21PR         1 BYTE UEBERTRAGEN
         CVD   R1,DW1             LESE-FEHLER KONVERTIEREN
         MVC   PR,MASK1           MASKE UEBERTRAGEN
         ED    PR,DW1+4           AUSGABEFELD LESE-FEHLER AUFBER.
         A     R1,C21PR           KUMMULIEREN DER LESE-FEHLER
         ST    R1,C21PR           SPEICHERN DER LESE-FEHLER
         SPACE 2
         XC    DW1,DW1            LOESCHEN
         XR    R1,R1              LOESCHEN
         IC    R1,SMF21PW         1 BYTE UEBERTRAGEN
         CVD   R1,DW1             SCHREIB-FEHLER KONVERTIEREN
         MVC   PW,MASK1           MASKE UEBERTRAGEN
         ED    PW,DW1+4           SCHREIB-FEHLER AUFBEREITEN
         A     R1,C21PW           KUMMULIEREN DER SCHREIB-FEHLER
         ST    R1,C21PW           SPEICHERN DER SCHREIB-FEHLER
         SPACE 2
         XC    DW1,DW1
         XR    R1,R1              LOECHEN
         IC    R1,SMF21NB         1 BYTE UEBERTRAGEN
         CVD   R1,DW1             NOISE-BLOCKS KONVERTIEREN
         MVC   NB,MASK1           MASKE UEBERTRAGEN
         ED    NB,DW1+4           AUSGABEFELD NOISE-BLOCKS AUFBER.
         A     R1,C21NB           KUMMULIEREN DER NOISE-BLOCKS
         ST    R1,C21NB           SPEICHERN DER NOISE-BLOCKS
         SPACE 2
         XC    DW1,DW1            LOESCHEN
         XR    R1,R1              LOESCHEN
         XC    HW1,HW1            LOESCHEN
         MVC   HW1,SMF21SIO       START-I/O'S UEBERTRAGEN
         L     R1,C21SIO          LADEN DER KUMM. SIO'S
         AH    R1,HW1             SIO'S ADDIEREN
         ST    R1,C21SIO          SPEICHERN DER SIO'S
         XR    R1,R1              LOESCHEN
         LH    R1,HW1             SIO'S NACH REG. 1
         CVD   R1,DW1             SIO'S KONVERTIEREN
         MVC   SIO,MASK2          MASKE UEBERTRAGEN
         ED    SIO,DW1+3          SIO'S AUFBEREITEN
         SPACE 2
         XC    HW1,HW1            LOESCHEN
         XR    R1,R1              LOESCHEN
         XC    DW1,DW1            LOESCHEN
         MVC   HW1,SMF21ERG       ERASE-GAPS UEBERTRAGEN
         L     R1,C21ERG          KUMM. ERASE-GAPS LADEN
         AH    R1,HW1             ERASE-GAPS ADDIEREN
         ST    R1,C21ERG          ERASE-GAPS SPEICHERN
         XR    R1,R1              LOESCHEN
         LH    R1,HW1             ERASE-GAPS INS REG. 1
         CVD   R1,DW1             ERASE-GAPS KONVERTIEREN
         MVC   ERG,MASK2          MASKE UEBERTRAGEN
         ED    ERG,DW1+3          ERASE-GAPS AUFBEREITEN
         MVC   BDNR,SMF21VOL      BAND-NR. UEBERTRAGEN
         CLC   BDNR,=6C' '        SCRATCH NL BAENDER ?
         BNE   W1                 NORMALE VERARBEITUNG
         MVC   BDNR+1(9),=C'SCRTCH NL'  NOLABEL-BAND
W1       EQU   *
         SPACE
         UNPK  BDST,SMF21CA(3)    BANDSTATION INS AUSGABEFELD
         MVI   BDST+4,X'40'       1 STELLE LOESCHEN
         TR    BDST(4),TRTAB-X'F0'   TRANSLATE
         MVI   BDST,X'40'         DRUCKSTELLE LOESCHEN
         AP    ZBDF,=P'1'         FEHLERHAFTE BAENDER ADDIEREN
         CLI   SMF21PR,X'00'      KEIN PERM. LESE-FEHLER
         BE    V1
         MVC   DC,=C'XX'          DATA CHECK MARKIEREN
         AP    DCZ,=P'1'          ANZAHL DATA CHECK ADDIEREN
V1       EQU   *
         SPACE
         CLI   SMF21PW,X'00'      KEIN PERM. SCHREIB-FEHLER ?
         BE    V2
         CLC   DC,=C'XX'          WURDE SCHON DATA CHECK MARKIERT ?
         BE    V2
         MVC   DC,=C'XX'          DATA CHECK MARKIEREN
         AP    DCZ,=P'1'          DATA CHECK ADDIEREN
V2       EQU   *
         SPACE
         CP    ZZ,=P'49'          SEITENVORSCHUB ??
         BNH   DRU                EINZELZEILE DRUCKEN
         AP    SZ,=P'1'           SEITENZAEHLER PLUS 1
         MVC   UE1SZ,MSZ
         ED    UE1SZ,SZ
         SP    ZZ,ZZ              ZEILENZAEHLER LOESCHEN
         L     R2,ALISTE
         PUT   (R2),UE1-1         SEITENVORSCHUB
         PUT   (R2),UE1           DRUCKEN
         PUT   (R2),UE2-1         DREI-ZEILIGER VORSCHUB
         PUT   (R2),UE2           DRUCKEN
         PUT   (R2),UE3           DRUCKEN
         PUT   (R2),STRICH
         PUT   (R2),EZ-1          ZWEI-ZEILIGER VORSCHUB
         SPACE 2
DRU      EQU   *
         SPACE
         L     R2,ALISTE
         PUT   (R2),EZ
         MVI   EZVZ,C' '           BLANK IN ERSTE DRUCKSTELLE
         MVC   EZ+1(132),EZ       DRUCKZEILE LOESCHEN
         MVI   EZVZ,X'09'         EIN-ZEILIGER VORSCHUB
         AP    ZZ,=P'1'           ZEILENZAEHLER PLUS 1
         SPACE
         B     END99              ZURUECK ZUM HAUPTPROGRAMM
         EJECT
EODTYP21 EQU   *
         SPACE
         L     R2,ALISTE
         PUT   (R2),STRICH        UNTERSTREICHEN
         MVI   EZVZ,C' '           BLANK IN ERSTE DRUCKSTELLE
         MVC   EZ+1(132),EZ       DRUCKZEILE LOESCHEN
         L     R2,ALISTE
         PUT   (R2),EZ-1        ZWEI-ZEILIGER VORSCHUB
         XC    DW1,DW1            LOESCHEN
         L     R1,C21TR           KUMMUL. LESE-FEHLER INS REG 1
         CVD   R1,DW1             KONVERTIEREN
         MVC   TR,MASK1           MASKE UEBERTRAGEN
         ED    TR,DW1+4           AUFBEREITEN
         XC    DW1,DW1            LOESCHEN
         L     R1,C21TW           KUMMUL. SCHREIB-FEHLER INS REG 1
         CVD   R1,DW1             KONVERTIEREN
         MVC   TW,MASK1           MASKE UEBERTRAGEN
         ED    TW,DW1+4           AUFBEREITEN
         XC    DW1,DW1            LOESCHEN
         L     R1,C21SIO          KUMMUL. SIO'S INS REG 1
         CVD   R1,DW1             KONVERTIEREN
         MVC   SIO,MASK2          MASKE UEBERTRAGEN
         ED    SIO,DW1+3          AUFBEREITEN
         XC    DW1,DW1            LOESCHEN
         L     R1,C21PR           KUMMUL. PERM. LESE-FEHLER INS REG 1
         CVD   R1,DW1             KONVERTIEREN
         MVC   PR,MASK1           MASKE UEBERTRAGEN
         ED    PR,DW1+4           AUFBEREITEN
         XC    DW1,DW1            LOESCHEN
         L     R1,C21PW           KUMM. PERM. SCHREIB-FEHLER INS REG 1
         CVD   R1,DW1
         MVC   PW,MASK1           MASKE UEBERTRAGEN
         ED    PW,DW1+4           AUFBEREITEN
         XC    DW1,DW1
         L     R1,C21NB           KUMM. NOISE-BLOCKS INS REG 1
         CVD   R1,DW1             KONVERTIEREN
         MVC   NB,MASK1           MASKE UEBERTRAGEN
         ED    NB,DW1+4           AUFBEREITEN
         XC    DW1,DW1            LOESCHEN
         L     R1,C21ERG          KUMM. ERASE-GAPS INS REG 1
         CVD   R1,DW1             KONVERTIEREN
         MVC   ERG,MASK2          MASKE UEBERTRAGEN
         ED    ERG,DW1+3          AUFBEREITEN
         CP    DCZ,=P'0'          KEIN DATA CHECK ?
         BE    KEIN               JA
         MVC   DC-2(4),MSZ        MASKE UEBERTRAGEN
         ED    DC-2(4),DCZ        ANZAHL DATA CHECK AUFBER.
         MVC   DC+3(3),=C'MAL'    TEXT
         B     GES
KEIN     EQU   *
         MVC   DC-2(4),=C'KEIN'
GES      EQU   *
         MVC   BDNR,=C'GESAMT'
         MVI   EZVZ,X'09'         EIN-ZEILIGER VORSCHUB
         L     R2,ALISTE
         PUT   (R2),EZ            DRUCKEN
         MVC   BDTOT,MBD          MASKE UEBERTRAGEN
         ED    BDTOT,ZBDT+1       WERT UEBERTRAGEN (AUFBEREITEN)
         MVC   BDFEHL,MBD         MASKE UEBERTRAGEN
         ED    BDFEHL,ZBDF+1      WERT AUFBEREITEN
         ZAP   DW1,ZBDF
         MP    DW1,=P'10000'
         DP    DW1,ZBDT
         MVC   PROZW,MPROZ
         ED    PROZW,DW1+1
         MVC   GZ+103(7),MPROZ    MASKE UEBERTRAGEN
         MVI   GZ+111,C'%'
         ED    GZ+103(7),C21PROZ+1 WERT AUS PARM UEBERTRAGEN
         MVC   GZ+74(30),=C'. PARAMETER: FEHLER ZU SIO''S ='
         L     R2,ALISTE
         PUT   (R2),GZ-1          ZWEI-ZEILIGER VORSCHUB
         PUT   (R2),GZ            DRUCKEN
         SPACE 2
         B     END99              ENDE DES PROGRAMMS
         EJECT
         LTORG
         EJECT
**************************************
*        D E F I N I T I O N E N     *
**************************************
         SPACE
*     UEBERSCHRIFTEN    *
         SPACE
         DC    XL1'8B'
UE1      DS    0CL133
UE1VZ    DC    XL1'09'
         DC    CL30'    AUS PROGRAMM  RZSP914A  '
         DC    CL31'STATISTIK  UEBER   F E H L E R '
         DC    CL37'H A F T E   M A G N E T B A E N D E R '
         DC    CL2' '
         DC    CL5'VOM: '
MDAT     DC    CL8' '
         DC    CL8' '
         DC    CL7'SEITE: '
UE1SZ    DC    CL4' '
         SPACE 2
         DC    XL1'13'
UE2      DS    0CL133
UE2VZ    DC    XL1'09'
         DC    CL7' BAND -'
         DC    CL30'  T E M P O R A R Y  E R R O R'
         DC    CL14'  S T A R T   '
         DC    CL30'P E R M A N E N T  E R R O R  '
         DC    CL35' N O I S E   E R A S E    B A N D -'
         DC    CL10'   D A T A'
         DC    CL6' '
         SPACE 2
UE3      DS    0CL133
UE3VZ    DC    XL1'09'
         DC    CL7' NUMMER'
         DC    CL29'     R E A D     W R I T E   '
         DC    CL15'     I/O''S     '
         DC    CL30'   R E A D     W R I T E      '
         DC    CL33'  BLOCKS       GAPS       STATION'
         DC    CL11'      CHECK'
         DC    CL7' '
         SPACE 2
STRICH   DS    0CL133
         DC    XL1'09'
         DC    132CL1'-'
         SPACE
*      MA S K E N     *
MASK1    DC    X'40204B2020204B202020'
MASK2    DC    X'402020204B2020204B202020'
MBD      DC    X'4020204B202020'
MSZ      DC    X'40202020'
MPROZ    DC    X'402021206B2020'
         EJECT
*     EINZELZEILE    *
         SPACE
         DC    XL1'0B'
EZ       DS    0CL133
EZVZ     DC    XL1'09'
         DC    CL1' '
BDNR     DC    CL6' '
         DC    CL2' '
TR       DC    CL10' '
         DC    4CL1' '
TW       DC    CL10' '
         DC    3CL1' '
SIO      DC    CL12' '
         DC    3CL1' '
PR       DC    CL10' '
         DC    4CL1' '
PW       DC    CL10' '
         DC    5CL1' '
NB       DC    CL10' '
         DC    CL1' '
ERG      DC    CL12' '
         DC    6CL1' '
BDST     DC    CL5' '
         DC    CL7' '
DC       DC    CL2' '
         DC    10CL1' '
         SPACE 2
*       HILFSFELDER    *
DW1      DC    D'0'
DW2      DC    D'0'
FW1      DC    F'0'
HW1      DC    H'0'
SZ       DC    PL2'0'
ZZ       DC    PL2'50'
ZBDT     DC    PL4'0'             ZAEHLER FUER BAENDER TOTAL
ZBDF     DC    PL4'0'             ZAEHLER FUER BAENDER MIT FEHLERN
DCZ      DC    PL2'0'             ZAEHLER FUER DATA CHECKS
FDAT     DC    XL8'0000000000000000'
         SPACE 2
*   VOLLWORTE FUER DIE PARAMETERUEBERNAHME AUS HAUPTPROGRAMM
         SPACE
APARM    DC    F'0'
ASATZ    DC    F'0'
ALISTE   DC    F'0'
         SPACE 2
TRTAB    DC    C'0123456789ABCDEF'
         DC    XL1'13'
GZ       DS    0CL133
         DC    XL1'09'
         DC    CL28' VERARBEITETE BAENDER TOTAL:'
BDTOT    DC    CL7' '
         DC    CL19'; DAVON FEHLERHAFT:'
BDFEHL   DC    CL7' '
         DC    CL3'  ='
PROZW    DC    CL7' '
         DC    CL2' %'
         DC    CL59' '
         EJECT
*      ALLGEMEINER KOMUNIKATIONSBEREICH
         SPACE
COMMON   DSECT
         DS    F
         ORG   *-4
COMEODID DS    CL1
         ORG
         DS    12F
C21TR    DC    F'0'
C21TW    DC    F'0'
C21SIO   DC    F'0'
C21PR    DC    F'0'
C21PW    DC    F'0'
C21NB    DC    F'0'
C21ERG   DC    F'0'
C21PROZ  DC    F'0'
         EJECT
IFASMF21 DSECT
         PRINT GEN
         IFASMFR 21
         END   SMFTYP21
