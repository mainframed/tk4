SYSVLOG  START
         REG
         XSAVE R12,SVA,SYSVLOG
         RDJFCB TAPE
         LOCATE INDAB
         LTR   R15,R15
         BZ    OPEN
         CH    R15,=H'8'
         BE    SETNEW
         WTO   'HRDCPY1 HARDCOPY - LOCATE ERROR',ROUTCDE=2,DESC=2
         B     OPEN
SETNEW   EQU   *
         MVI   JFCB+87,X'C0'
         NI    JFCB+52,X'F7'
         OI    JFCB+76,X'80'
OPEN     EQU   *
         OPEN  (TAPE,(OUTPUT)),TYPE=J
         OPEN  (SYSUT1,,IEFRDER,(OUTPUT))
         L     R3,16
         L     R3,196(R3)
         MVC   SID,16(R3)
         LA    R5,INPUT+13
         L     R15,16
         TM    116(R15),X'01'
         BNO   NOMVS1
         LA    R5,INPUT+24
NOMVS1   EQU   *
POS      EQU   *
         GET   SYSUT1,INPUT
         CLC   0(7,R5),=CL7'IEE136I'
         BNE   POS
         B     DATOK
GET      EQU   *
         GET   SYSUT1,INPUT
         L     R15,16
         TM    116(R15),X'01'
         BO    MVS1
         CLI   INPUT,X'40'
         BNE   GET
         B     MVT1
MVS1     EQU   *
         CLI   INPUT+4,X'40'
         BNE   GET
         CLI   INPUT,X'40'
         BE    GET
MVT1     EQU   *
         CLC   INPUT+1(15),=CL15' '
         BE    GET
         SR    R4,R4
         LA    R3,NOHARD
NXTMSG   EQU   *
         IC    R4,0(R3)
         EX    R4,CLCEX1
         BE    GET
         LA    R3,2(R4,R3)
         CLI   0(R3),X'FF'
         BNE   NXTMSG
         CLC   0(7,R5),=CL7'IEE136I'
         BNE   NODAT
DATOK    EQU   *
         L     R15,16
         TM    116(R15),X'01'
         BO    MVS2
         MVC   DATE(2),INPUT+45
         MVC   DATE+2(3),INPUT+48
         B     GET
MVS2     EQU   *
         MVC   DATE(2),INPUT+52
         MVC   DATE+2(3),INPUT+55
         B     GET
NODAT    EQU   *
         MVC   OUTPUT+4(4),SID
         MVI   OUTPUT+8,X'40'
         MVC   OUTPUT+9(5),DATE
         L     R15,16
         TM    116(R15),X'01'
         BNO   NOMVS2
         MVI   OUTPUT+14,X'40'
         MVC   OUTPUT+15(122),INPUT
         B     MVS3
NOMVS2   EQU   *
         MVC   OUTPUT+14(123),INPUT
MVS3     EQU   *
         LA    R6,40
         LA    R8,OUTPUT+40
CHKBLNK  EQU   *
         CLC   0(15,R8),=CL15' '
         BE    WRITE
         CH    R6,=H'127'
         BH    WRITE
         LA    R6,10(R6)
         LA    R8,10(R8)
         B     CHKBLNK
WRITE    EQU   *
         STH   R6,OUTPUT
         XC    OUTPUT+2(2),OUTPUT+2
         PUT   IEFRDER,OUTPUT+4
         PUT   TAPE,OUTPUT
         B     GET
EODAD    EQU   *
         CLOSE (SYSUT1,,TAPE,,IEFRDER)
RETURN   EQU   *
         XRETURN 0
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GM),EODAD=EODAD
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=(PM),LRECL=133,RECFM=VBS,    *
               BLKSIZE=3059,EXLST=EXLST
IEFRDER  DCB   DDNAME=SYSOUT,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=133,   *
               BLKSIZE=3059
EXLST    DS    0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
INDAB    CAMLST NAME,JFCB,,LOCAREA
LOCAREA  DS    0D
         DS    CL265
CLCEX1   CLC   0(0,R5),1(R3)
DBLWD    DS    D
INPUT    DC    CL133' '
SID      DC    CL4' '
DATE     DC    CL5'99999'
NOHARD   DS    0F
         DC    X'04',C'XFREE'
         DC    X'04',C'XLIST'
         DC    X'06',C'IEC202E'
         DC    X'06',C'IEF429I'
         DC    X'06',C'IEF280E'
         DC    X'06',C'IEC002E'
         DC    X'06',C'IEF236I'
         DC    X'06',C'IEF161I'
         DC    X'04',C'XWTOR'
         DC    X'06',C'IEF165I'
         DC    X'06',C'IEE600I'
         DC    X'03',C'D T '
         DC    X'04',C' D T '
         DC    X'03',C'D R '
         DC    X'04',C' D R '
         DC    X'03',C'D N '
         DC    X'03',C'D A '
         DC    X'03',C'S A '
         DC    X'04',C' S A '
         DC    X'07',C'S DUMMY '
         DC    X'08',C' S DUMMY '
         DC    X'03',C'D Q '
         DC    X'07',C'$HASP000'
         DC    X'01',C'$D'
         DC    X'FF'
         DS    0H
OUTPUT   DC    CL137' '
         END
