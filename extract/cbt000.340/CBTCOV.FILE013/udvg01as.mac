         TITLE 'DOKUMENTATION UND UEBERPRUEFUNG VON FCB''S'
         ISEQ  73,80
         PRINT NOGEN
DOKUFCB  START
         SAVE  (14,12),,DOKUFCB
         BALR  4,0
         USING *,4
         LR    14,13
         LA    13,SAVEAREA
         ST    13,8(14)
         ST    14,4(13)
         B     BEGINN
SAVEAREA DS    18F
LMFCB    DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=(R)
KARTE    DCB   DSORG=PS,MACRF=GM,LRECL=80,DDNAME=VLK,EODAD=ENDK
STANZ    DCB   DDNAME=STANZ,DSORG=PS,MACRF=PM,LRECL=80,RECFM=F,        *
               BLKSIZE=80
LISTE    DCB   DDNAME=LISTE,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FM,      *
               BLKSIZE=133
         DS    0H
BEGINN   EQU   *
         OPEN  (LMFCB,,KARTE)
         OPEN  (STANZ,(OUTPUT),LISTE,(OUTPUT))
         B     P10
*DEFINITIONEN
VLK      DS    0CL80
FCBNR    DS    CL8
         DC    CL72' '
ABER     DS    0CL80
ASYM     DS    CL9
AOT      DS    CL6
AOB      DS    CL57
ABL      DS    CL8
HF1      DC    PL2'0'
HF2      DC    CL2'00'
LZ       DC    PL2'0'
ZZ       DC    PL2'0'
KZ       DC    PL3'0'
HWL      DC    H'0'
DW       DC    D'0'
DWL      DC    D'0'
HF3      DC    X'0'
UPKF     DC    CL2'0'
SCHAB    DC    XL4'40202020'
FSCH     DC    F'0'                     RETURN-CODE
DR       DS    CL133
HW       DC    H'0'
*DEFINITIONEN  FCB-LOAD-MODUL
FCB      DS    CL4                      1. 4 ZEICHEN VOM FCB
FCBREG   EQU   10
R0       EQU   0
         EJECT
*PROGRAMMABLAUF
P1       XC    DR,DR
         MVI   DR,X'89'
         PUT   LISTE,DR
P10      GET   KARTE,VLK
         FIND  LMFCB,FCBNR,D
         LTR   15,15
         BZ    P11
         MVI   DR,X'09'
         MVC   DR+1(8),FCBNR
         MVC   DR+10(18),=C'FCB NICHT GEFUNDEN'
         PUT LISTE,DR
         B     P1
P11      EQU   *
         ZAP   KZ,=P'0'
         LOAD  EPLOC=FCBNR,DCB=LMFCB
         LR    3,R0                     REG. 0 NACH REG. 3
         SR    8,8                      REG. 8 LOESCHEN
        MVC    FCB,0(3)                 1. 4 STELLEN UEBERTR.
         ZAP   KZ,=P'0'                 KARTENZAEHLER AUF 0
         MVC   ABER,ABER-1              AUSGABEBEREICH LOESCHEN
         MVC   ASYM(12),=C'./ ADD NAME='
         MVC   ASYM+12(8),FCBNR
         BAL   2,UP4                    ADD - SATZ SCHREIBEN
         MVC   ASYM(8),FCBNR
         MVC   AOT,=C'CSECT'
         BAL   2,UP4                    CSECT-SATZ SCHREIBEN
         MVI   AOB,C'X'
         MVI   AOB+1,X'7D'
         MVI   AOB+4,X'7D'
         MVC   AOT(2),=C'DC'
         CLI   FCB,X'00'                DEFAULT IMAGE ?
         BE    P2                       NEIN
         MVC   AOB+2(2),=C'80'
         MVC   AOB+10(13),=C'DEFAULT IMAGE'
         B     P3
P2       MVC   AOB+2(2),=C'00'
         MVC   AOB+10(18),=C'KEIN DEFAULT IMAGE'
P3       BAL   2,UP4                    TYP-SATZ SCHREIBEN
         SR    8,8                      REG. 8 LOESCHENEIBEN
         IC    8,FCB+1                  LAENGE NACH R. 8
         CVD   8,DWL                    UMRECHNEN IN DEZ
P7       UNPK  HF2,DWL+6(2)             ENTPACKEN
         MVZ   HF2+1(1),HF2             VORZEICHEN KORR.
         MVC   AOT(2),=C'DC'
         MVC   AOB(7),=C'AL1(  )'
         MVC   AOB+4(2),HF2             ADRESSSKONSTANTE
         MVC   AOB+10(18),=C'FCB FUER    ZEILEN'
         TM    FCB+2,X'C0'              INDEXANGABE ?
         BZ    P8                       NEIN
         SP    DWL+6(2),=P'1'           LAENGE - 1
         UNPK  HF2,DWL+6(2)             ENTPACKEN
         MVZ   HF2+1(1),HF2             VORZEICHEN
P8       MVC   AOB+19(2),HF2
         BAL   2,UP4                    LAENGEN-SATZ SCHREIBEN
         TM    FCB+2,X'C0'              INDEX - ANGABE?
         BZ    P4                       NEIN
         MVC   HF3,FCB+2                INDEX NACH HF3
         NI    HF3,X'1F'                LINKE 3 BITS AUF 0
         IC    8,HF3                    REST NACH R. 8
         CVD   8,DW                     UMRECHNEN IN DEZ.
         UNPK  HF2,DW+6(2)              ENTPACKEN
         MVC   AOB+10(38),=C'VERSCHIEBUNG UM    STELLEN NACH RECHTS'
         TM    FCB+2,X'40'              VERSCHIEB. N. LINKS?
         BZ    P6                       NEIN
         MVC   AOB+42(6),=C'LINKS '     DOCH
P6       MVZ   HF2+1(1),HF2             VORZEICHEN
         MVC   AOB+26(2),HF2            LAENGE IN TEXT EINSETZEN
         MVC   AOT(2),=C'DC'
         MVC   AOB(1),=C'X'
         MVO   HF1,FCB+2(1)             ORIGINALLAENGE
         UNPK  HF2,HF1                  ENTPACKEN
         MVI   AOB+1,X'7D'              ' EINS.
         MVI   AOB+4,X'7D'              ' EINS.
         MVZ   HF2+1(1),HF2             VORZEICHEN
         MVC   AOB+2(2),HF2             VERSCHIEBUNG EINSETZEN
         BAL   2,UP4                    INDEX - SATZ SCHREIBEN
        LA     3,3(3)                   REG. 3 + 3
         B     P5
P4      LA     3,2(3)                   REG. 3 + 2
P5       BAL   8,UP1                    EINZELZEILEN VERARBEITEN
         DELETE EPLOC=FCBNR
         B     P1                       NAECHSTE KARTE
*
* PROGRAMMENDE
ENDK     CLOSE (LMFCB,,KARTE,,STANZ,,LISTE)
         L     15,FSCH
         L     13,SAVEAREA+4
         RETURN (14,12),RC=(15)
         EJECT
*
*UNTERROUTINEN
*
*
* EINZELBYTES VERARBEITEN
*
UP1      ZAP   ZZ,=P'1'                 ZEILENZAEHLER AUF 1
         MVO   HF1,0(1,3)               U, BYTE NACH HF1 M. VORZ.
         BAL   9,UP2                    HF1 ENDPACKEN
         MVI   ASYM,C'*'
         MVC   AOB+10(17),=C'  ZEILEN PRO ZOLL'
         CLI   HF2,C'1'
         BE    U101
         MVI   AOB+10,C'6'
         B     U102
U101     MVI   AOB+10,C'8'
U102     BAL   2,UP4                    KOMM. "ZEILEN PRO Z." SCHR.
         CLI   HF2+1,C'0'
         BE    U103
         BAL   2,UP5                    KANAL NACH AUSG.-BER.
U103     MVC   AOT(2),=C'DC'
         MVI   AOB,C'X'
         MVI   AOB+1,X'7D'
         MVI   AOB+4,X'7D'
         MVC   AOB+2(2),HF2
         MVC   AOB+14(4),SCHAB
         ED    AOB+14(4),ZZ
         MVC   AOB+10(5),=C'ZEILE'
         BAL   2,UP4                    SATZ F. 1. ZEILE SCHR.
         ZAP   LZ,=P'0'                 LAENGENZAEHLER AUF 0
U107     AP    ZZ,=P'1'                 ZEILENZAEHLER + 1
         AP    LZ,=P'1'                 LAENGE + 1
         LA    3,1(3)                   REG. 3 + 1
         MVO   HF1,0(1,3)               NAECHSTES BYTE NACH HF1
         BAL   9,UP2                    HF1 NACH HF2 ENTP.
         CLC   HF2,=C'00'               ERGEBNIS = 00 ?
         BE    U107                     NOCHMAL
U108     MVC   AOT(2),=C'DC'
         MVC   AOB+2(2),=C'00'
         UNPK  AOB+2(2),LZ
         MVZ   AOB+3(1),AOB+2           VORZEICHEN
         MVC   AOB(2),=C'XL'
         MVI   AOB+4,X'7D'              '
         MVI   AOB+7,X'7D'              '
         MVC   AOB+14(4),SCHAB
         ED    AOB+14(4),ZZ
         MVC   AOB+10(5),=C'ZEILE'
         BAL   2,UP5                    KANAL NACH AUSGABEBE.
         MVC   AOB+5(2),HF2
         BAL   2,UP4                    ZEILE MIT KANAL ASCHREIBEN
         ZAP   LZ,=P'0'                 LAENGENZ. AUF 0
         CLI   HF2,C'1'                 LETZTES BYTE?
         BNE U107                       NEIN
U109    CP     DWL,ZZ                   FCB - LAENGE OK?
         BE    0(8)
         MVC   ASYM(42),=C'*LAENGENANGABE UND ANZAHL ZEILEN UNGLEICH '
         MVC   FSCH,=F'16'
         BAL   2,UP4                    FEHLERZEILE SCHREIBEN
EX1      BR    8                        RUECKSPRUNG
         EJECT
*
* HF1 ENTPACKEN , BUCHSTABEN BEACHTEN
*
UP2      UNPK  HF2,HF1                  ENTP.
         CLI   HF2+1,X'C9'              NUMERISCH?
         BH    EX2                      NEIN, LINKS HEX. "C" LASSEN
         MVZ   HF2+1(1),HF2             HEX. "F" EINSETZEN
EX2      BR    9                        /
*
* BEREICH AUSGEBEN
*
UP4      AP    KZ,=P'10'                KARTENZAEHLER + 10
         UNPK  ABL,KZ                   KARTENZAEHLER
         OI    ABL+7,X'F0'
         PUT   STANZ,ABER
         MVC   DR+1(L'ABER),ABER
         MVI   DR,X'09'
         PUT   LISTE,DR
         XC    DR,DR
         MVC   ABER,ABER-1              BERICH LOESCHEN
         BR    2
*
*
* KANAL - NUMMER IN AUSGABEBEREICH
*
UP5     CLI    HF2+1,C'0'               KANAL VORHANDEN ?
        BE     EX5                      NEIN
        MVC    AOB+20(5),=C'KANAL'
         BL    U504                     JA
         MVC   AOB+26(1),HF2+1          NEIN, EINS.
         B     EX5
U504     CLI   HF2+1,X'CA'
         BNE   U505
         MVC   AOB+26(2),=C'10'
         MVI   HF2+1,C'A'
         B     EX5
U505     CLI   HF2+1,X'CB'
         BNE   U506
         MVC   AOB+26(2),=C'11'
         MVI   HF2+1,C'B'
         B     EX5
U506     CLI   HF2+1,X'CC'
         BNE   EX5
         MVC   AOB+26(2),=C'12'
         MVI   HF2+1,C'C'
EX5      BR    2
         END
