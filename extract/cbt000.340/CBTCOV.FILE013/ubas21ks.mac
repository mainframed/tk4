* STAND 06/22/79
GTSERNOS START
******************************************************
*
*        CALL  GTSERNOS,(LV,V,DD),VL
*        CALL  GTSERNOS,(LV,V,DD,LDS,DS),VL
*
*        ZWECK : LIEFERT ZU VORGEGEBENEM DDNAMEN 'DD'
*              IN TABELLE 'V' ALLE BISHER BEKANNTEN
*              VOLUME SERIAL NUMMERN.
*              ZUSAETZLICH KOENNEN IN TABELLE 'DS'
*              DIE ZUGEHOERIGEN DSNAMEN GELIEFERT WERDEN.
*
*        DD    CL8  DDNAME
*
*        LV    H    EINGABE : LAENGE V-TABELLE
*                  AUSGABE : RETURNCODE
*                            0 = OK
*                            4 = BETRIEBSSYSTEM NICHT MVS
*                                DATEN NICHT VERFUEGBAR
*                            8 = DDNAME FEHLT
*
*        V     TABELLE VOLSER-NR :
*                BYTE 1+2 : ANZAHL VOLSER-NR (HALBWORT)
*                BYTE 3-N : VOLSER-NR JEWEILS 6 BYTES
*
*        LDS   H    EINGABE : LAENGE DS-TABELLE
*                   AUSGABE : ANZAHL DSNAMEN IN 'DS'
*
*        DS    TABELLE DSNAMEN
*              JEDER EINTRAG ENTHAELT : ANZAHL DER ZUGEHOERIGEN
*                                       VOLSER (HALBWORT)
*                                       DSNAME  (CL44)
*
*******************************************************
         SPACE 2
         REG
         XSAVE R12,SVA,TEST
         MVI   SWITCH,X'00'
         L     R15,16
         TM    116(R15),X'10'
         BO    MVSOK
         L     R1,0(R1)
         MVC   0(2,R1),=H'12'
         XRETURN 12
MVSOK    EQU   *
         CLI   0(R1),X'80'
         BNE   PRMOK
PRMERR   EQU   *
         WTO   'GTSERNOS RECEIVED INVALID PARAMETER-LIST',ROUTCDE=11
         ABEND 111
PRMOK    EQU   *
         CLI   4(R1),X'80'
         BE    PRMERR
         CLI   8(R1),X'80'
         BE    NODSN
         CLI   12(R1),X'80'
         BE    PRMERR
         CLI   16(R1),X'80'
         BNE   PRMERR
         OI    SWITCH,DSNREQ
NODSN    EQU   *
         LR    R11,R1
         L     R2,8(R11)
         TIOTSCAN (R2)
         LTR   R15,R15
         BNZ   FOUND
RET8     EQU   *
         L     R1,0(R11)
         MVC   0(2,R1),=H'8'
         XRETURN 8
FOUND    EQU   *
         L     R3,0(R11)
         LH    R3,0(R3)
         BCTR  R3,0
         BCTR  R3,0
         SR    R2,R2
         D     R2,=F'6'    R3 = NO OF VOLSER-ENTRIES IN AREA
         CH    R3,=H'1'
         BL    RET8
         TM    SWITCH,DSNREQ
         BNO   NODSN1
         L     R10,16(R11)   A(DSN-AREA)
         L     R5,12(R11)
         LH    R5,0(R5)
         SR    R4,R4
         D     R4,=F'46'
         LR    R2,R5
         L     R5,12(R11)
         XC    0(2,R5),0(R5)
         CH    R2,=H'1'
         BL    NODSN1
         OI    SWITCH,DSN
NODSN1   EQU   *
         L     R4,4(R11)
         LR    R5,R4
         XC    0(2,R5),0(R5)
         LA    R4,2(R4)
         LR    R5,R15
NEWTIOT  EQU   *
         L     R6,12(R5)    GET JFCB-ADDR.
         SRL   R6,8
         LA    R6,16(R6)
         SR    R7,R7
         IC    R7,117(R6)   NO. OF VOLUMES
         TM    SWITCH,DSN
         BNO   NODSN2
         L     R15,12(R11)
         LH    R14,0(R15)
         LA    R14,1(R14)
         STH   R14,0(R15)
         STH   R7,0(R10)
         MVC   2(44,R10),0(R6)
         LA    R10,46(R10)
         BCT   R2,NODSN2
         NI    SWITCH,255-DSN
NODSN2   EQU   *
         L     R15,4(R11)
         LH    R14,0(R15)
         AR    R14,R7      INCR. NO. OF VOLUMES
         STH   R14,0(R15)
         LA    R8,5
         LA    R9,118(R6)
MOVE     EQU   *
         TM    SWITCH,BERVOLL
         BO    NXTVOL
         MVC   0(6,R4),0(R9)
         LA    R4,6(R4)
         BCT   R3,NXTVOL
         OI    SWITCH,BERVOLL
         B     NXTIOT
NXTVOL   EQU   *
         LA    R9,6(R9)
         BCT   R8,TSTR7
         B     EXT
TSTR7    EQU   *
         BCT   R7,MOVE
         B     NXTIOT
EXT      EQU   *
         BCT   R7,*+8
         B     NXTIOT
         TM    SWITCH,JFCBEXT
         BO    NXTEXT
         L     R6,148(R6)     GET ADDR. OF JFCB-EXTENSION
         LA    R6,0(R6)
         LTR   R6,R6
         BZ    NXTIOT
         LA    R8,15
         LA    R6,16(R6)
         LA    R9,4(R6)
         OI    SWITCH,JFCBEXT
         B     MOVE
NXTEXT   EQU   *
         L     R6,0(R6)
         SRL   R6,8
         LA    R6,0(R6)
         LTR   R6,R6
         BZ    NXTIOT
         LA    R8,15
         LA    R6,16(R6)
         LA    R9,4(R6)
         B     MOVE
NXTIOT   EQU   *
         NI    SWITCH,255-JFCBEXT
         SR    R15,R15
         IC    R15,0(R5)
         LA    R5,0(R15,R5)
         CLC   4(8,R5),=CL8' '
         BE    NEWTIOT
         L     R1,0(R11)
         MVC   0(2,R1),=H'0'
         XRETURN 0
SWITCH   DC    X'00'
BERVOLL  EQU   X'80'
JFCBEXT  EQU   X'40'
DSN      EQU   X'20'
DSNREQ   EQU   X'10'
         END
