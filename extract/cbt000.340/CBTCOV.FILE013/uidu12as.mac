         TITLE ' LIST SYSCTLG LOG DATASET '
LISTLOG  EANF
         MVC   ABER,ABER-1    CLEAR OUTPUT AREA
         MVI   ABER,C'1'      INIT ASA
         MVI   CBER,C' '
         OPEN  (IN,,OUT,OUTPUT,CTL)  OPEN DATA SETS
NA1      EQU   *
         CLI   CBER,C'*'
         BE    NA11
         GET   CTL,CBER
         CLI   CBER,C'/'
         BE    ENDE
         CLI   CBER,C' '
         BE    NA11
         CLI   CBER,C'*'
         BE    NA11
         CLI   CBER,C'L'
         BNE   NA1M
         LA    R7,EBER+14
         AH    R7,0(R7)
         SH    R7,=H'4'
         CLC   0(3,R7),=3X'00'
         BE    NA12
         MVC   CBER+3(3),0(R7)
         B     NA11
NA1M     EQU   *
         TR    CBER(6),TTRANSX
         PACK  CBER(7),CBER(7)
NA11     EQU   *
         GET   IN,EBER        READ LOG RECORD
         CLI   CBER,C'*'
         BE    NA12
         CLI   CBER,C' '
         BE    NA12
         CLC   EBER(3),CBER+3
         BE    NA12
         BL    NA11
         CLI   CBER,C'L'
         BNE   NA11
         MVI   CBER,X'00'
         CLOSE IN
         OPEN  IN
         B     NA11
NA12     EQU   *
         CLC   EBER+3(3),=3X'00'
         BE    NA11
*
*  CONVERT TTR
*
         LA    R8,EBER        INPUT LOCATION
         LA    R9,ABER+1      OUTPUT LOCATION
         LA    R7,3(0)        LENGTH
         STH   R7,LNG
         BAL   R10,UPCONV     LINK TO CONVERSION ROUTINE
         MVC   ABER+9(3),EBER+3  STATUS
*
*  CONVERT KEY
*
         LA    R8,EBER+6     INPUT LOCATION
         LA    R9,ABER+14     OUTPUT LOCATION
         LA    R7,8(0)        LENGTH
         STH   R7,LNG
         BAL   R10,UPCONV     LINK TO CONVERSION ROUTINE
         MVI   ABER+33,C'*'   KEY CHARACTER FIELD
         MVC   ABER+34(8),EBER+6
         TR    ABER+34(8),TTRANSC  TRANSLATION
         MVI   ABER+42,C'*'
*
*    D A T E
*
         UNPK  ABER+45(5),EBER+270(4)
         OI    ABER+49,X'F0'
*
*    T I M E
*
         UNPK  ABER+52(9),EBER+274(5)
         TR    ABER+52(8),TTRANS-240
         MVI   ABER+60,C' '
         PUT   OUT,ABER       PRINT LINE
         MVC   ABER,ABER-1    CLEAR OUTPUT AREA
*
*  C O M M E N T
*
         MVC   ABER+14(22),EBER+278
         PUT   OUT,ABER       PRINT LINE
         MVC   ABER,ABER-1    CLEAR OUTPUT AREA
*
*  CONVERT CATALOG BLOCK
*
         LA    R6,16(0)       LOOP COUNT
         LA    R8,EBER+14     INPUT LOCATION
NA2      EQU   *
         LA    R9,ABER+14     OUTPUT LOCATION
         LA    R7,16(0)       LENGTH OF ONE SEGMENT
         STH   R7,LNG
         MVI   ABER+52,C'*'   CHAR FIELD
         MVC   ABER+53(16),0(R8)
         TR    ABER+53(16),TTRANSC
         MVI   ABER+69,C'*'
         BAL   R10,UPCONV     CONVERT SEGMENT
         PUT   OUT,ABER       PRINT LINE
         MVC   ABER,ABER-1    CLEAR OUTPUT AREA
         BCT   R6,NA2         DECREASE LOOP COUNT AND TEST
         MVI   ABER,C'-'
         B     NA1            GET NEXT RECORD
*
*   C O N V E R S I O N   R O U T I N E
*
UPCONV   EQU   *
         MVC   LNG2,LNG
         LH    R7,LNG2        CALCULATE MAX CONV LENGTH
         CH    R7,=H'4'       4 IS MAXIMUM
         BNH   NA3
         LA    R7,4(0)
         STH   R7,LNG2        STORE CURRENT LENGTH
NA3      EQU   *
         SLL   R7,5(0)        CALCULATE LENGTH CODE FOR UNPK
         AH    R7,LNG2
         EX    R7,UNPACK
         SRL   R7,4(0)        CALCULATE LENGTH COD FOR TRANSLATE
         BCTR  R7,0
         EX    R7,TRANS
         LA    R9,1(R9,R7)
         MVI   0(R9),C' '
         LA    R9,1(R9)       INCREASE OUTPUT POINTER
         LA    R8,4(R8)       INCREASE INPUT POINTER
         LH    R7,LNG         UPDATE LENGTH
         SH    R7,LNG2
         STH   R7,LNG
         BZ    0(R10)         IF ZERO RETURN
         B     UPCONV
*
UNPACK   UNPK  0(0,R9),0(0,R8)
TRANS    TR    0(0,R9),TTRANS-240
*
*  E N D E
*
ENDE     EQU   *
         CLOSE IN
         CLI   CBER,C'*'
         BE    ENDEC
    OPEN  IN
         B     NA1
ENDEC    EQU   *
         CLOSE (CTL,,OUT)
         ERET
*
*  D E F I N I T I O N S
*
LNG      DS    H
LNG2     DS    H
EBER     DS    300C
         DC    C' '
ABER     DS    CL80
CBER     DS    CL80
TTRANSX  DC    193X'00'
         DC    X'FAFBFCFDFEFF'
         DC    41X'00'
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         DC    6X'00'
TTRANS   DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
TTRANSC  DC    64C'.'
         DC    X'40'
         DC    128C'.'
         DC    C'ABCDEFGHI'
         DC    7C'.'
         DC    C'JKLMNOPQR'
         DC    8C'.'
         DC    C'STUVWXYZ'
         DC    6C'.'
         DC    C'0123456789'
         DC    6C'.'
*
*   D C B ' S
*
IN       DCB   DDNAME=LOG,DSORG=PS,MACRF=GM,EODAD=ENDE
OUT DCB DDNAME=OUT,DSORG=PS,MACRF=PM,RECFM=FA,BLKSIZE=80,    ***********
               LRECL=80
CTL DCB DDNAME=IN,DSORG=PS,MACRF=GM,RECFM=F,BLKSIZE=80,EODAD=ENDEC
         END   LISTLOG
*/* AN FOLGENDEN TAGEN WURDE DAS PROGRAMM GETESTET */
*/* NEUERSTELLUNG   22. DEZ 1975   LEVEL G  */
*/* NEUERSTELLUNG    4. FEB 1976   LEVEL G  */
