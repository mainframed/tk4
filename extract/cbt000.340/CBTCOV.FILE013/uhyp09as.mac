TRC      TITLE 'OS-TRACING   UHYP09AS  =  MODTRC '
         MACRO
&NA      DBM
&NA      DS    0CL1      BITMUSTERDEFINITION
&NA.BIT0 DS    0CL1
&NA.BIT1 DS    0CL1
&NA.BIT2 DS    0CL1
&NA.BIT3 DS    0CL1
&NA.BIT4 DS    0CL1
&NA.BIT5 DS    0CL1
&NA.BIT6 DS    0CL1
&NA.BIT7 DC    X'00'
         MEND
         SPACE 5
MODTRC   START 0
         PRINT DATA
         USING DUMMYR10,R10
         USING IHADCB,R2
R0       EQU   0         ARBEITSREGISTER
R1       EQU   1         ARBEITSREGISTER
R2       EQU   2         ARBEITSREGISTER
R3       EQU   3         ARBEITSREGISTER
R4       EQU   4         BASIS
R5       EQU   5         BASIS
R6       EQU   6         UP-SPRUNG
R7       EQU   7         BEFEHL IM PROBLEMPROGRAMM
R8       EQU   8         NAECHSTER BEFEHL
R9       EQU   9         DRUCKKOLONNE
R10      EQU   10        OPERANDENFELD
R11      EQU   11        BEFEHLSLAENGE
R12      EQU   12        UP-SPRUNG
R13      EQU   13        SAVEAREA
R14      EQU   14        RETURN ADDRESS
R15      EQU   15        ENTRY POINT ADDRESS
         USING *,R15
         USING *,R4
         SPACE 5
**AA     START TRACING
         SPACE 5
         ST    R14,START1
         L     R14,START2
         STM   R2,R13,8(R14)
         MVC   0(8,R14),12(R1)
         MVC   56(8,R14),4(R1)
         L     R14,START1
         LA    R13,START1
         MVC   4(4,R13),0(R1)
         LM    R4,R5,START3
         BR    R5
START1   DC    18F'0'          SAVEAREA MODTRC
START2   DC    A(VF1)
START3   DC    A(MODTRC)
         DC    A(AA1)
         DROP  R15
AA1      LR    R7,R0
         OI    20(R1),X'01'
         ST    R1,DC0
         ST    R14,DC4
         SPIE  EXIT,((1,15))
         ST    R1,DC8
         XC    V9,V9
         L     R2,DC0
         L     R2,20(R2)
         TM    DCBOFLGS,X'10'
         BO    A1
         OPEN  ((R2),(OUTPUT))
         SPACE 5
**A      MODUL ''TRACING''
         SPACE 5
A1       TM    DCBOFLGS,X'10'
         BZ    A4
         BAL   R6,UF1
         MVI   V6,X'09'
A5       B     B1
A2       CLI   0(R7),X'44'
         BE    J1
         B     G1
A10      MVC   PERFORM+2(4),K8
         B     H1
A3       L     R2,DC0
         CLI   20(R2),X'00'
         BNE   A5
A6       B     L1
A4       WTO   '*** MODTRC 3235 DRUCKER DD-KARTE FEHLT'
         B     A6
         SPACE 5
**B      GRUNDSTELLUNG
         SPACE 5
B1       LA    R0,2
         LA    R10,V7
B2       MVI   DA0,X'FF'
         XC    DA1(5),DA1
         MVI   DA6,C'='
         MVI   DA6+1,C' '
         MVC   DA6+2(5),DA6+1
         MVI   DA6+7,C'='
         LA    R10,V8
         BCT   R0,B2
         LA    R10,V7
         MVC   DB32,V7+10
         XC    V9(2),V9
         SPACE 5
**C      BEFEHLSADRESSE UND -SCHLUESSEL
         SPACE 5
C1       LA    R9,V6+1
         LR    R3,R7
         BAL   R6,UA1
         LA    R1,V1
         LA    R2,KF1
         LA    R3,4
C2       CLC   0(1,R7),0(R1)
         BE    C3
         LA    R1,2(R1)
         AR    R1,R3
         C     R1,0(R2)
         BL    C2
         LA    R2,4(R2)
         BCT   R3,C2
         B     E1
C3       MVC   DB1,1(R1)
         BCTR  R3,0
         EX    R3,K100
         B     E1
         SPACE 5
**E      ALLGEMEINE BEFEHLSBEARBEITUNG
         SPACE 5
E1       CLI   0(R7),X'D0'
         BH    EA1
         LA    R11,4
         TM    DB1,X'CF'
         BZ    E2
         LA    R1,1(R7)
         BAL   R6,UD1
         TM    DB1,X'08'
         BZ    E3
         MVC   DA4,KH7
E3       LA    R10,V8
         CLI   0(R7),X'20'
         BL    E4
         CLI   0(R7),X'80'
         BH    EC1
         B     ED1
E6       LA    R1,1(R7)
         BAL   R12,UC2
         B     F1
E4       BAL   R6,UD2
E5       LA    R11,2
         B     F1
E2       UNPK  VD1(3),1(2,R7)
         TR    VD1(2),K1-240
         MVC   V6+19(2),VD1
         CLI   0(R7),X'0A'
         BE    E5
         B     E6
         SPACE 5
**EA     LAENGE BEI SS-FORMAT
         SPACE 5
EA1      SR    R2,R2
         IC    R2,1(R7)
         CLI   0(R7),X'F0'
         BL    EA2
         SRDL  R2,4
         STH   R2,DA4
         SRL   R3,28
         STH   R3,DA4+L'V7
         LA    R3,1(R3)
         CVD   R3,VD1
         UNPK  VD1(5),VD1+6(3)
         MVC   V6+26(2),VD1+1
         MVI   V6+25,C'L'
EA3      LA    R2,1(R2)
         CVD   R2,VD1
         UNPK  VD1(5),VD1+6(3)
         MVC   V6+14(3),VD1
         MVI   V6+13,C'L'
         B     EB1
EA2      STH   R2,DA4
         STH   R2,DA4+L'V7
         TM    DB1,X'08'
         BZ    EA3
         MVI   DA4+L'V7+1,X'FF'
         B     EA3
         SPACE 5
**EB     OPERANDEN BEI SS-FORMAT
         SPACE 5
EB1      LA    R11,6
         LA    R1,1(R7)
         BAL   R12,UC1
         LA    R1,3(R7)
         BAL   R12,UC2
         B     F1
         SPACE 5
**EC     RS-FORMAT
         SPACE 5
EC1      TM    DB1,X'03'
         BZ    EC4
         LA    R10,V7
         IC    R3,1(R7)
         SLL   R3,28
         SRL   R3,28
         CVD   R3,VD1
         UNPK  VD1(5),VD1+6(3)
         MVC   V6+23(2),VD1+1
         MVI   V6+22,C'R'
         TM    DB1,X'02'
         BZ    EC2
         SR    R1,R1
         IC    R1,DA0
         CR    R3,R1
         BNL   EC3
         LA    R3,16(R3)
EC3      SR    R3,R1
         LA    R3,1(R3)
         SLL   R3,2
         BCTR  R3,0
         STH   R3,DA4
         STH   R3,DA4+L'V7
EC4      B     E6
EC2      SLL   R3,2
         LA    R3,VF1(R3)
         MVC   DB32,V6+22
         MVC   DB4,0(R3)
         B     EC4
         SPACE 5
**ED     RX-FORMAT
         SPACE 5
ED1      OI    DB0BIT0,X'80'
         IC    R0,DB1
         SRDL  R0,2
         SRL   R1,30
         LA    R2,1
         SLL   R2,0(R1)
         BCTR  R2,0
         STH   R2,DA4
         B     E6
         SPACE 5
**F      BEARBEITUNG VOR AUSFUEHRUNG
         SPACE 5
F1       LA    R9,V6+40
         TM    DB1,X'04'
         BZ    F3
         MVI   V6+18,C' '
F3       B     FA1
F2       LR    R1,R11
         BCTR  R1,0
         EX    R1,K103
         B     FB1
F4       LR    R8,R7
         AR    R8,R11
         B     FC1
F5       B     A2
         SPACE 5
**FA     OPERANDENAUSDRUCK VOR AUSFUEHRUNG
         SPACE 5
FA1      TM    DB1,X'80'
         BZ    FA2
         BAL   R12,UG1
FA2      TM    DB1,X'20'
         BZ    FA3
         BAL   R12,UG2
FA3      TM    DB1,X'10'
         BZ    FA4
         L     R1,V8
         LH    R2,V8+4
         EX    R2,K102
FA4      B     F2
         SPACE 5
**FB     SPRUNGBEARBEITUNG
         SPACE 5
FB1      CLI   V6+8,C'B'
         BNE   FB3
         CLC   V6+8(3),K9
         BNE   FB2
         OI    DB0BIT1,X'40'
         CLC   PERFORM(2),K10
         BNE   FB2
         OI    DB0BIT2,X'20'
FB2      B     FBA1
FB3      B     F4
         SPACE 5
**FBA    SPRUNGADRESSE
         SPACE 5
FBA1     TM    V8,X'FF'
         BO    FBA2
         BZ    FBA3
         L     R1,V8
         MVC   DB12,0(R1)
FBA5     ST    R1,DB24
         MVC   DB28,0(R1)
         MVC   0(4,R1),KF5
         OI    DB0BIT3,X'10'
FBA3     B     FB3
FBA2     MVC   DB12,V8
FBA4     SR    R1,R1
         IC    R1,PERFORM+2
         SRL   R1,4
         TM    DB0BIT0,X'80'
         BZ    FBA8
         LTR   R1,R1
         BNZ   FBA9
         TM    DB0BIT4,X'08'
         BZ    FBA6
         B     FBA3
FBA8     LTR   R1,R1
         BZ    FBA3
FBA7     SLL   R1,2
         LA    R1,VF1(R1)
         NI    PERFORM+2,X'F0'
         MVI   PERFORM+3,X'00'
         B     FBA5
FBA6     MVO   PERFORM+2(1),PERFORM+1(1)
         OI    DB0BIT4,X'08'
         B     FBA4
FBA9     NI    PERFORM+1,X'F0'
         B     FBA7
         SPACE 5
**FC     EXECUTEBEARBEITUNG
         SPACE 5
FC1      L     R1,DB16
         CLI   0(R7),X'44'
         BNE   FC2
         LTR   R1,R1
         BNE   FC3
FC5      ST    R8,DB16
         L     R8,V8
         MVC   DB2,1(R8)
         MVC   VD1+2(2),0(R8)
         SR    R2,R2
         IC    R2,V7
         LTR   R2,R2
         BZ    FC4
         SLL   R2,2
         L     R3,VF1(R2)
         O     R3,VD1
         STC   R3,1(R8)
FC4      MVC   2(6,R9),K13
         LA    R9,9(R9)
         UNPK  0(5,R9),0(3,R8)
         TR    0(4,R9),K1-240
         MVI   4(R9),C' '
         LA    R9,9(R9)
         B     F5
FC3      BAL   R12,UH2
         LR    R8,R1
         MVC   1(1,R7),DB2
         SR    R1,R1
         ST    R1,DB16
         MVC   PERFORM+2(4),K8
         LR    R7,R8
         B     B1
FC2      LTR   R1,R1
         BZ    F5
         LR    R8,R1
         MVC   1(1,R7),DB2
         SR    R1,R1
         ST    R1,DB16
         B     F5
         SPACE 5
**H      BEARBEITUNG NACH AUSFUEHRUNG
         SPACE 5
H1       TM    DB1,X'40'
         BZ    H2
         BAL   R12,UI1
H2       TM    DB1,X'10'
         BZ    H3
         BAL   R12,UI2
H3       CLI   PERFORM,X'86'
         BE    HA1
         CLI   PERFORM,X'87'
         BE    HA1
         CLI   PERFORM,X'D0'
         BL    J1
         TM    DB1,X'03'
         BZ    J1
         B     HB1
         SPACE 5
**HA     DOPPELREGISTER R3
         SPACE 5
HA1      LA    R10,V7
         MVC   4(4,R9),DB32
         UNPK  VD1(9),DB4(5)
         TR    VD1,K1-240
         MVC   9(8,R9),VD1
         UNPK  VD1(9),DB4+4(5)
         TR    VD1,K1-240
         MVC   18(8,R9),VD1
         B     J1
         SPACE 5
**HB     R1 BZW.R2 DRUCKEN
         SPACE 5
HB1      LA    R1,K6
         LA    R2,VF1+4
HB2      C     R9,KF4
         BL    HB3
         BAL   R6,UF1
         LA    R9,V6+40
HB3      TM    DB1,X'02'
         BO    HB4
         LA    R1,4(R1)
         LA    R2,4(R2)
HB4      MVC   4(4,R9),0(R1)
         MVC   10(7,R9),K3
         UNPK  VD1(9),0(5,R2)
         TR    VD1,K1-240
         MVC   18(8,R9),VD1
         LA    R9,27(R9)
         TM    DB1,X'03'
         BO    HB5
         B     J1
HB5      NI    DB1,X'FD'
         B     HB2
         SPACE 5
**J      BEFEHLSABSCHLUSS
         SPACE 5
J1       MVO   V6+130(2),DB20(1)
         NI    V6+130,X'F3'
         OI    V6+130,X'F0'
         MVI   V6+131,C' '
         BAL   R6,UF1
         LR    R7,R8
         TM    DB0BIT2,X'20'
         BZ    J3
         L     R2,DC0
         TM    20(R2),X'10'
         BO    J3
         MVC   V6+1(12),A4+8
         MVC   V6+14(18),K11
         BAL   R6,UF1
         AP    JV1,JK1
         CP    JV1,JV2
         BL    J4
         ABEND 4030,DUMP
J4       L     R8,VF1+56
         MVC   VF1+56(4),KF8
         LM    R13,R15,VF1+52
         LM    R0,R3,VF1
         BR    R15
J2       STM   R0,R3,VF1
         STM   R13,R15,VF1+52
         ST    R8,VF1+56
         LR    R7,R8
J3       B     A3
JV1      DC    PL2'0'
JV2      DC    P'200'
JK1      DC    P'1'
         SPACE 5
**L      ENDE TRACING
         SPACE 5
L1       MVC   V6+1(12),A4+8
         MVC   V6+14(16),K12
         BAL   R6,UF1
         L     R1,DC8
         SPIE  MF=(E,(1))
         MVN   DB20(1),0(R1)
         L     R1,DB20
         SPM   R1
         L     R2,DC0
         MVC   4(4,R2),VF1+56
         MVC   12(8,R2),VF1
         L     R14,DC4
         LR    R2,R7
         L     R15,VF1+60
         LM    R0,R1,VF1
         LM    R3,R13,VF1+12
         BR    R14
         SPACE 5
**UA     ADRESSE IN DRUCKBEREICH
         SPACE 5
UA1      ST    R3,VD1
         UNPK  VD1(9),VD1+1(4)
         TR    VD1+2(6),K1-240
         MVC   0(6,R9),VD1+2
         BR    R6
         SPACE 5
**UB     ADRESSE AUS X,B U. DIS
         SPACE 5
UB1      SR    R2,R2
         IC    R2,1(R1)
         SR    R3,R3
         SRL   R2,4
         SLA   R2,2
         BZ    UB2
         L     R3,VF1(R2)
UB2      MVC   VD1(2),1(R1)
         NI    VD1,X'0F'
         AH    R3,VD1
         TM    DB0BIT0,X'80'
         BZ    UB3
         IC    R2,0(R1)
         SLL   R2,28
         SRL   R2,26
         LTR   R2,R2
         BZ    UB3
         A     R3,VF1(R2)
UB3      ST    R3,0(R10)
         OI    0(R10),X'FF'
         BR    R6
         SPACE 5
**UC     OPERAND
         SPACE 5
UC1      LA    R9,V6+18
         LA    R10,V7
UC3      BAL   R6,UB1
         BAL   R6,UA1
         MVC   DA6+1(6),VD1+2
         BR    R12
UC2      LA    R9,V6+29
         LA    R10,V8
         B     UC3
         SPACE 5
**UD     REGISTEROPERAND
         SPACE 5
UD1      SR    R3,R3
         IC    R3,0(R1)
         SRL   R3,4
         LA    R2,V6+18
         LA    R10,V7
         B     UD3
UD2      IC    R3,0(R1)
         SLL   R3,28
         SRL   R3,28
         LA    R2,V6+22
         LA    R10,V8
UD3      CVD   R3,VD1
         UNPK  VD1(5),VD1+6(3)
         MVC   1(2,R2),VD1+1
         MVI   0(R2),C'R'
         STC   R3,DA0
         MVC   DA6+4(3),0(R2)
         MVI   DA6,C' '
         MVC   DA6+1(3),DA6
         SLL   R3,2
         LA    R3,VF1(R3)
         ST    R3,VD1
         MVC   DA1,VD1+1
         MVC   DA4,KH3
         BR    R6
         SPACE 5
**UE     HEXAAUSDRUCK
         SPACE 5
UE1      L     R1,DA0
         LH    R2,DA4
UE5      C     R9,KF2
         BL    UE2
         ST    R6,VD1
         BAL   R6,UF1
         L     R6,VD1
         LA    R9,V6+40
UE2      UNPK  VD1(9),0(5,R1)
         TR    VD1,K1-240
         CH    R2,KH3
         BH    UE3
         SLL   R2,1
         LA    R2,1(R2)
         EX    R2,K101
         LA    R9,9(R9)
         BR    R6
UE3      MVC   0(8,R9),VD1
         LA    R9,9(R9)
         LA    R1,4(R1)
         C     R1,KF3
         BNE   UE4
         LA    R1,VF1
UE4      SH    R2,KH4
         B     UE5
         SPACE 5
**UF     DRUCKZEILE AUSGEBEN
         SPACE 5
UF1      STM   R14,R2,UFVF1
         L     R14,DC0
         L     R2,20(R14)
         PUT   (R2),V5
         MVI   V6+1,C' '
         MVC   V6+2(129),V6+1
         LM    R14,R2,UFVF1
         BR    R6
UFVF1    DC    5F'0'
         SPACE 5
**UG     OPERAND VORHER
         SPACE 5
UG1      LA    R10,V7
         MVC   0(8,R9),DA6
         TM    DB1,X'40'
         BZ    UG4
         B     UG5
UG2      LA    R10,V8
         C     R9,KF4
         BL    UG3
         BAL   R6,UF1
         LA    R9,V6+40
UG3      MVC   0(8,R9),DA6
         TM    DB1,X'10'
         BZ    UG4
UG5      MVC   11(6,R9),K2
         LA    R9,9(R9)
UG4      LA    R9,9(R9)
         BAL   R6,UE1
         BR    R12
         SPACE 5
**UH     PROGRAMMFEHLER
         SPACE 5
UH1      MVI   V6+1,C'*'
         MVC   V6+2(129),V6+1
         MVC   V6+30(5),K5
         UNPK  VD1(9),4(5,R1)
         TR    VD1,K1-240
         MVC   V6+35(8),VD1
         MVI   V6+43,C' '
         UNPK  VD1(9),8(5,R1)
         TR    VD1,K1-240
         MVC   V6+44(8),VD1
UH3      MVC   V6+1(13),A4+8
         MVC   V6+14(L'K4),K4
         BAL   R6,UF1
         BAL   R6,UF1
         AP    UHV1,UHV3
         CP    UHV1,UHV2
         BL    UH4
         ABEND 4031,DUMP
UH4      BR    R12
UHV1     DC    PL2'0'
UHV2     DC    P'20'
UHV3     DC    P'1'
UH2      MVI   V6+1,C'*'
         MVC   V6+2(129),V6+1
         B     UH3
         SPACE 5
**UI     OPERAND NACHHER
         SPACE 5
UI1      LA    R10,V7
         B     UI3
UI2      LA    R10,V8
         TM    DB1,X'20'
         BO    UI3
         MVC   DA1,KF6+1
UI3      C     R9,KF4
         BL    UI4
         BAL   R6,UF1
         LA    R9,V6+40
UI4      C     R10,KF7
         BE    UI5
         TM    DB1,X'20'
         BZ    UI6
UI7      MVC   1(7,R9),K3
UI8      LA    R9,9(R9)
         BAL   R6,UE1
         BR    R12
UI5      TM    DB1,X'80'
         BO    UI7
         B     UI6
UI6      MVC   0(8,R9),DA6
         B     UI8
         SPACE 5
**G      BEFEHLSAUSFUEHRUNG
         SPACE 5
G1       L     R1,DB20
         SPM   R1
         STM   R7,R13,VF2
         LM    R0,R15,VF1
PERFORM  BCR   R0,0
         BCR   R0,0
         BCR   R0,0
G2       BCR   R0,0
         DC    H'0'
         DC    H'0'
BRANCH   DC    H'0'
         SPACE 5
**EXIT   UNTERBRECHUNGSANALYSE
         SPACE 5
         USING *,R15
EXIT     L     R2,EXIT1
         MVC   0(12,R2),20(R1)
         STM   R3,R13,12(R2)
         MVC   56(8,R2),12(R1)
         L     R13,EXIT9
         L     R3,EXIT2
         L     R0,8(R1)
         ST    R0,20(R3)
         LM    R7,R13,64(R2)
         L     R4,EXIT5
         SLL   R0,8
         SRL   R0,8
         C     R0,EXIT3
         BNH   EXIT10
EXIT14   TM    0(R3),X'10'
         BZ    EXIT13
         L     R6,24(R3)
         MVC   0(4,R6),28(R3)
EXIT13   TM    0(R3),X'40'
         BZ    EXIT12
         L     R6,EXIT8
         L     R12,0(R6)
         ST    R8,0(R12)
EXIT12   C     R0,EXIT4
         BL    EXIT11
         L     R8,12(R3)
EXIT11   MVC   9(3,R1),EXIT7+1
         BR    R14
EXIT10   L     R6,EXIT6
         BALR  R12,R6
         B     EXIT14
EXIT1    DC    A(VF1)
EXIT2    DC    A(V9)
EXIT3    DC    A(G2)
EXIT4    DC    A(BRANCH)
EXIT5    DC    A(MODTRC)
EXIT6    DC    A(UH1)
EXIT7    DC    A(A10)
EXIT8    DC    A(V7)
EXIT9    DC    A(START1)
         DROP  R15
         SPACE 5
K1       DC    C'0123456789ABCDEF'      TR-KONSTANTE
K2       DC    C'VORHER'
K3       DC    C'NACHHER'
K4       DC    C'PROGRAMMFEHLER  '
K5       DC    C'PSW= '
K6       DC    C'R01='
K7       DC    C'R02='
K8       DC    X'07000700'
K9       DC    C'BAL'
K10      DC    X'05EF'
K11      DC    C'SUPERVISORANSPRUNG'
K12      DC    C'ENDE DES TRACING'
K13      DC    C'MODIF.'
K100     MVC   V6+8(1),2(R1)            BEFEHLSSCHLUESSEL
K101     MVC   0(1,R9),VD1
K102     MVC   V10(1),0(R1)
K103     MVC   PERFORM(1),0(R7)
         SPACE 5
V1       DC    X'05D0',C'BALR'
         DC    X'F2D0',C'PACK'
         DC    X'F3D0',C'UNPK'
         DC    X'8CC8',C'SRDL'
         DC    X'8DC8',C'SLDL'
         DC    X'8EC8',C'SRDA'
         DC    X'8FC8',C'SLDA'
         DC    X'06D0',C'BCTR'
         DC    X'DFD2',C'EDMK'
         DC    X'87C1',C'BXLE'
V2       DC    X'D2D0',C'MVC'
         DC    X'45C2',C'BAL'
         DC    X'0724',C'BCR'
         DC    X'9230',C'MVI'
         DC    X'9520',C'CLI'
         DC    X'F8D0',C'ZAP'
         DC    X'D5A0',C'CLC'
         DC    X'90B2',C'STM'
         DC    X'12D0',C'LTR'
         DC    X'40B1',C'STH'
         DC    X'F1D0',C'MVO'
         DC    X'42B0',C'STC'
         DC    X'4EB3',C'CVD'
         DC    X'4FD3',C'CVB'
         DC    X'46C2',C'BCT'
         DC    X'88C0',C'SRL'
         DC    X'89C0',C'SLL'
         DC    X'8AC0',C'SRA'
         DC    X'8BC0',C'SLA'
         DC    X'D1D0',C'MVN'
         DC    X'D3D0',C'MVZ'
         DC    X'0A00',C'SVC'
         DC    X'10D0',C'LPR'
         DC    X'13D0',C'LCR'
         DC    X'15A0',C'CLR'
         DC    X'DDDB',C'TRT'
         DC    X'11D0',C'LNR'
         DC    X'1ED0',C'ALR'
         DC    X'1FD0',C'SLR'
         DC    X'86C1',C'BXH'
V3       DC    X'4706',C'BC'
         DC    X'41C2',C'LA'
         DC    X'DED0',C'ED'
         DC    X'FAD0',C'AP'
         DC    X'F9A0',C'CP'
         DC    X'18D0',C'LR'
         DC    X'1BD0',C'SR'
         DC    X'98D2',C'LM'
         DC    X'50B2',C'ST'
         DC    X'9120',C'TM'
         DC    X'48D1',C'LH'
         DC    X'43D0',C'IC'
         DC    X'DCD8',C'TR'
         DC    X'FBD0',C'SP'
         DC    X'49A1',C'CH'
         DC    X'FCD0',C'MP'
         DC    X'FDD0',C'DP'
         DC    X'9630',C'OI'
         DC    X'9430',C'NI'
         DC    X'9730',C'XI'
         DC    X'19A0',C'CR'
         DC    X'1AD0',C'AR'
         DC    X'4AD1',C'AH'
         DC    X'4BD1',C'SH'
         DC    X'4CD1',C'MH'
         DC    X'44B1',C'EX'
         DC    X'1CD8',C'MR'
         DC    X'1DD8',C'DR'
         DC    X'D4D0',C'NC'
         DC    X'D6D0',C'OC'
         DC    X'D7D0',C'XC'
         DC    X'14D0',C'NR'
         DC    X'16D0',C'OR'
         DC    X'17D0',C'XR'
         DC    X'55A2',C'CL'
         DC    X'5ED2',C'AL'
         DC    X'5FD2',C'SL'
V4       DC    X'58D2',C'L'
         DC    X'5AD2',C'A'
         DC    X'59A2',C'C'
         DC    X'5BD2',C'S'
         DC    X'5CDA',C'M'
         DC    X'5DDA',C'D'
         DC    X'54D2',C'N'
         DC    X'56D2',C'O'
         DC    X'57D2',C'X'
         CNOP  0,4
V5       DC    H'137'
         DC    H'0'
V6       DC    X'89'                   STEUERZEICHEN
         DC    CL132' '  DRUCKZEILE
         CNOP  0,4
V7       DS    CL16
         CNOP  0,4
V8       DS    CL14
         CNOP  0,4
V9       DS    0CL36
DB0      DBM
DB1      DC    X'00'
DB2      DC    X'0'
DB3      DC    X'00'
DB4      DC    XL8'0'
DB12     DC    XL4'0'
DB16     DC    XL4'0'
DB20     DC    XL4'0'
DB24     DC    XL4'0'
DB28     DC    XL4'0'
DB32     DC    CL4' '
V10      DS    CL256     SICHERUNGSBEREICH FUER 2.OPERANDEN
         CNOP  0,4
V11      DS    0CL12
DC0      DC    XL4'0'
DC4      DC    XL4'0'
DC8      DC    XL4'0'
         SPACE 5
VF1      DC    16F'0'    REGISTER DES PROBLEMPROGRAMMS
VF2      DC    7F'0'
         SPACE 5
VD1      DS    D         HILFSRECHENFELD
         DS    CL1
         SPACE 5
KH3      DC    H'3'
KH4      DC    H'4'
KH7      DC    H'7'
         SPACE 5
KF1      DC    A(V2,V3,V4,V5)
KF2      DC    A(V6+130)
KF3      DC    A(VF1+64)
KF4      DC    A(V6+112)
KF5      DC    A(BRANCH)
KF6      DC    A(V10)
KF7      DC    A(V7)
KF8      DC    A(J2)
         SPACE 5
DUMMYR10 DSECT
DA0      DS    CL1
DA1      DS    CL3
DA4      DS    CL2
DA6      DS    CL8
         DCBD  DSORG=PS
         SPACE 5
         END   MODTRC
