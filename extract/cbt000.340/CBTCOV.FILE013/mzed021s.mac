         MACRO
&NAME    INIT  &SAVEAR=,&BASIS=2,&IDENT=
         GBLC  &SAVE
         LCLA  &CNT,&ALT,&NEU,&BASREG
&NAME    CSECT
         REGEQ
         AIF   (T'&SAVEAR EQ 'O').DEFSA
&SAVE    SETC  '&SAVEAR'
         AGO   .IFIDENT
.DEFSA   ANOP
&SAVE    SETC  'S&SYSNDX'
.IFIDENT AIF   (T'&IDENT EQ 'O').NOIDENT
*        SAVE  (14,12),,&IDENT
         SAVE  (14,12),,&IDENT
         AGO   .BASR
.NOIDENT ANOP
*        SAVE  (14,12),,*
         SAVE  (14,12),,*
.BASR    AIF   (T'&SAVEAR EQ 'O').FORT0
&SAVE    SETC  '&SAVEAR'
         AGO   .FORT1
.FORT0   ANOP
&SAVE    SETC  'S&SYSNDX'
.FORT1   AIF   (N'&BASIS NE 1).BAS1
         AIF   (T'&BASIS EQ 'N').BAS0
         MNOTE 4,'&BASIS NICHT NUMERISCH, 2 EINGESETZT'
&BASREG  SETA  2
.BAS0    ANOP
&BASREG  SETA  &BASIS
         AGO   .BAS2
.BAS1    AIF   (T'&BASIS(1) EQ 'N').BAS11
         MNOTE 4,'&BASIS NICHT NUMERISCH, 2 EINGESETZT'
&BASREG  SETA  2
.BAS11   ANOP
&BASREG  SETA  &BASIS(1)
.BAS2    AIF   (&BASREG NE 0 AND &BASREG NE 13).OBGUT0
         MNOTE 8,'&BASREG ALS BASIS UNGEEIGNET, ABER GELASSEN'
.OBGUT0  AIF   (&BASREG NE 1 AND &BASREG NE 14                         *
               AND &BASREG NE 15).BALR
         MNOTE 8,'&BASREG ALS BASIS UNGEEIGNET, ABER GELASSEN'
         AGO   .BALR
         AIF   (T'&BASIS(1) EQ 'N').BALR
         MNOTE 4,'&BASIS NICHT NUMERISCH, 1 EINGESETZT'
&BASREG  SETA  1
         AGO   .OBGUT1
.BALR    BALR  R&BASREG,R0 .            LOAD BASE REGISTER
INITBAS  EQU   *
         AIF   (N'&BASIS NE 1).OBUS2
         USING *,R&BASREG
         AGO   .LSAVER
.OBUS2   AIF   (N'&BASIS NE 2).OBUS3
         USING *,R&BASIS(1),R&BASIS(2)
         AGO   .LREMAIN
.OBUS3   AIF   (N'&BASIS NE 3).OBUS4
         USING *,R&BASIS(1),R&BASIS(2),R&BASIS(3)
         AGO   .LREMAIN
.OBUS4   AIF   (N'&BASIS NE 4).OBUS5
         USING *,R&BASIS(1),R&BASIS(2),R&BASIS(3),R&BASIS(4)
         AGO   .LREMAIN
.OBUS5   AIF   (N'&BASIS NE 5).OBUS6
         USING *,R&BASIS(1),R&BASIS(2),R&BASIS(3),R&BASIS(4),R&BASIS(5)
.DUMMY   ANOP
         AGO   .LREMAIN
.OBUS6   AIF   (N'&BASIS NE 6).OBUS7
         USING *,R&BASIS(1),R&BASIS(2),R&BASIS(3),R&BASIS(4),R&BASIS(5)*
               ,R&BASIS(6)
         AGO   .LREMAIN
.OBUS7   AIF   (N'&BASIS NE 7).OBUS8
         USING *,R&BASIS(1),R&BASIS(2),R&BASIS(3),R&BASIS(4),R&BASIS(5)*
               ,R&BASIS(6),R&BASIS(7)
         AGO   .LREMAIN
.OBUS8   USING *,R&BASIS(1),R&BASIS(2),R&BASIS(3),R&BASIS(4),R&BASIS(5)*
               ,R&BASIS(6),R&BASIS(7),R&BASIS(8)
.LREMAIN ANOP
&CNT     SETA  N'&BASIS-1
&ALT     SETA  0
&NEU     SETA  1
.INCR    ANOP
&ALT     SETA  &ALT+1
&NEU     SETA  &NEU+1
         AIF   (T'&BASIS(&NEU) EQ 'N').OBGUT1
         MNOTE 4,'&BASIS(&NEU) NICHT NUMERISCH, WEGGELASSEN'
         AGO   .DECR
.OBGUT1  AIF   (&BASIS(&NEU) NE 0 AND &BASIS(&NEU) NE 13).OBGUT2
         MNOTE 8,'&BASIS(&NEU) ALS BASIS UNGEEIGNET, ABER GELASSEN'
         AGO   .LOAD
.OBGUT2  AIF   (&BASIS(&NEU) NE 1 AND &BASIS(&NEU) NE 14 AND           *
               &BASIS(&NEU) NE 15).LOAD
         MNOTE 8,'&BASIS(&NEU) ALS BASIS UNGEEIGNET, ABER GELASSEN'
.LOAD    LA    R&BASIS(&NEU),2048
         LA    R&BASIS(&NEU),2048(R&BASIS(&ALT),R&BASIS(&NEU))
.DECR    ANOP
&CNT     SETA  &CNT-1
         AIF   (&CNT NE 0).INCR
.LSAVER  AIF   (T'&SAVEAR NE 'O').SETNOT
&SAVE    SETC  'S&SYSNDX'
.SETNOT  ST    R13,&SAVE+4 .            STORE ADDR. OF HIGH SAVE-AREA
         LA    R0,&SAVE .              LOAD ADDR OF OWN SA
         ST    R0,8(R13) .             ADD. OF OWN INTO HIGH SA
         LR    R13,R0 .                ADDR. OF OWN INTO R13
         AIF   (T'&SAVEAR NE 'O').MACOUT
         CNOP  0,4
         B     72(R13) .               BRANCH AROUND SAVEAREA
S&SYSNDX DC    18F'0' .                 SAVEAREA
.MACOUT  ANOP
         MEND
