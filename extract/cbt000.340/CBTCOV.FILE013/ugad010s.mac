         TITLE 'DUMPSHOW,KUENNEMANN,SHOW DUMP DATASET'
DUMPSHOW CSECT
***********************************************************************
***                                                                 ***
***    DUMPSHOW          ***    VERSION NOVEMBER 1975               ***
***    DISPLAY DUMP STATUS  * FUER SYS1.DUMPXX DATEIEN UNTER MVS    ***
***  DD STATEMENT ONLY FOR THE FIRST ONE NEEDED                     ***
***                                                                 ***
***    AUTHOR : A.CHASSELOUP IBM GERMANY    SE-DVD K+V              ***
***                                                                 ***
***********************************************************************
         SPACE 3
         STM   14,12,12(13)
         LR    2,15
         USING DUMPSHOW,2
         ST    13,SAVE+4
         LA    13,SAVE
         RDJFCB DUMP
         LA    5,16      PIC UP CVT ADDR.
         L     5,0(5)   PIC UP CVT
         S     5,=A(4)      &IC UP RELAESE NUMBER
         MVC   WTO+31(2),0(5)
         MVC   WTO+34(2),2(5)
         LA    5,576(5)    PIC UP RTCT ADDR
         L     5,0(5)   PIC UP RTCT
         LA    5,36(5)    FIRST DUMP D.S. ENTRY
         LA    3,TABDC
         LA    4,1      NUMBER WTO BUFFERS
IFEND    CLI   2(5),X'00' IF END FROM RTCT
         BE    ENDE OF TABLE RTCT
         MVC   11(2,3),0(5)
         TM    3(5),X'20' IF DUMP TO DASDR ?
         BNO   S99999     NO, TO TAPE
         TM    3(5),X'80'  IF DUMP FULL
         BNO   DSEMP    NO , EMPTY
         MVC   14(5,3),=C'FULL '
         MVC   20(7,3),=C'TITLE= '
         MVC   JFDSN+9(2),0(5)
         MVC   JFCB(176),RJFCB
         OPEN  DUMP,TYPE=J
NEWGET   GET   DUMP
         CLC   0(2,1),=X'FFFF'    IF TITLE RECORD ?
         BNE   NEWGET     NO
         MVC   27(42,3),20(1)
         CLOSE DUMP
         MVI   ENDE+1,X'F0'
         B     DSEMP+12
DSEMP    MVC   14(5,3),=C'EMPTY'
         MVC   20(50,3),19(3)
         LA    5,12(5)
         LA    3,74(3)
         LA    4,1(4)
         B     IFEND
         SPACE 3
ENDE     BC    0,S01050
         WTO   '*** ALL DUMP DATA SET EMPTY ***'
         B     S99999
S01050   STC   4,WTO+45
         LA    1,WTO
         WTO   MF=(E,(1))
S99999   L     13,SAVE+4
         LM    14,12,12(13)
         XR    15,15
         BR    14
         SPACE 3
* ENDE OF DUMP
EOF      CLOSE DUMP
         B     DSEMP
         EJECT
*
* DEFINITIONEN
*
SAVE     DS    18F
         LTORG
         SPACE 3
** DCB FOR ALL SYS1.DUMP DATA SET
*
DUMP     DCB   DSORG=PS,DDNAME=DUMP,RECFM=F,BLKSIZE=4104,LRECL=4104,   *
               EODAD=EOF,MACRF=(GL),BUFNO=1,EXLST=LDUMP
** JOB FILE DEFINITION CONTROL BLOCK
*
* EXIT LISTE FOR THE RDJFCB MACRO
LDUMP    DS    0F
         DC    X'87',AL3(RJFCB)
*
JFCB     DS    CL176
*
RJFCB    DS    0F,0CL176
JFDSN    DC    CL176'SYS1.DUMPXX'
         ORG   RJFCB+176
* ENDE FROM JFCB DATA
*
*
WTO      WTO   ('** DISPLAY DUMP STATUS VS2-  .  **'),                 *
               ('*                                                 '), *
               DESC=4,MF=L
         ORG   WTO+46
         DC    AL2(74),XL2'2000'
TABDC    DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL',AL2(74),XL2'2000'
         DC    CL70'* SYS1.DUMPXX FULL'  10. AND LAST WTO
         END
