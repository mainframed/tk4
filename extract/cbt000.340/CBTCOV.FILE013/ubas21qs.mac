* STAND 06/22/79
***********************************************************************
*                                                                     *
*    PROGRAMM   = ABLTFILE                                            *
*                                                                     *
*    ENTRYPOINT = ABLTFILE                                            *
*                 --------                                            *
*    AUFRUF    = CALL ABLTFILE,(DDNAME)                               *
*        CL8      DDNAME                                              *
*                                                                     *
*    ZWECK      = AUSDRUCK DER DATASETNAMEN UND VOLUME SERIAL NUMBER  *
*                 DES MIT 'DDNAME' ANGESPROCHENEN DATASET             *
*                                                                     *
***********************************************************************
         PRINT NOGEN
ABLTFILE CSECT
         XSAVE R12,SAVE,ABLTFILE
         AREG
         L     R10,0(R1)
         MVC   DDNAME,0(R10)
*
*  ZU VORGEGEBENEM DDNAME DSNAMEN UND VOLSER-NR SUCHEN
*
         MVI   ZKZ,1
         L     R4,VOLTAB
         LTR   R4,R4
         BZ    WRFILE03       KEIN PLATZ FUER MVS-TABELLEN
         BP    START          PLATZ SCHON GEHOLT
*
*  GETMAIN FUER VOLSER UND DSNAME
*
         GETMAIN  EC,LV=2700,A=VOLTAB
         LTR   R15,R15
         BNZ   GM             KEIN PLATZ
         L     R3,VOLTAB
         LA    R3,LNVOL(R3)
         ST    R3,DSNTAB      -->DSNTAB
         B     START
*
GM       EQU   *  KEIN PLATZ FUER VOLSER TABELLE
         SR    R3,R3
         ST    R3,VOLTAB      -->VOLTAB=0
         B     WRFILE03
START    EQU   *
         L     R5,DSNTAB
         LA    R3,LNVOL       LAENGE VOLSER TABELLE
         STH   R3,BLNVOL
         LA    R3,LNDS        LAENGE DSNAME TABELLE
         STH   R3,BLNDS
         LA    R3,DDNAME      -->DDNAME
         L     R4,VOLTAB      -->VOLNR
         CALL  GTSERNOS,(BLNVOL,(R4),(R3),BLNDS,(R5)),VL
         CLC   BLNVOL,=H'8'   DDNAME FEHLT
         BE    WRFILE05       JA
         CLC   BLNVOL,=H'0'   MVS --> ALLE VOLSER GELIEFERT
         BE    WRFILE5        JA
*
*  NICHT MVS --> 1.JFCB LESEN
WRFILE03 EQU   *
*
         MVC   DUMDCB+40(8),DDNAME      ERMITTELN DSNAME,
         LA    R2,DUMDCB                          VOLUME-NR + -ANZAHL
         MVI   JFCB,X'FF'
         RDJFCB ((R2),(OUTPUT))         LESEN JOBFILE-CONTROLBLOCK
         TM    JFCB,X'FF'
         BNO   WRFILE1                  DSNAME GEFUNDEN
WRFILE05 EQU   *
         MVC   ZEILE+4(8),DDNAME        FALSCHER DDNAME
         MVC   ZEILE+18(13),=CL13'DDNAME FALSCH'  DSNAME NICHT GEFUNDEN
         B     WRFILE4
WRFILE1  EQU   *         DSNAME --> DRUCKBEREICH
         MVC   ZEILE+04(22),JFCB        DSNAME
         CLC   JFCB(3),=CL3'SYS'
         BNE   WRFILE3
         LA    R3,JFCB+23
WRFILE2  EQU   *         TEMPORAERER DSNAME
         CLI   0(R3),C'.'     PUNKT AB 24.STELLE DES DSNAME SUCHEN
         LA    R3,1(R3)
         BNE   WRFILE2
         MVI   ZEILE,C' '
         MVC   ZEILE+1(80),ZEILE  LOESCHEN
         MVC   ZEILE+04(11),0(R3)       TEMPORAERER DSNAME
WRFILE3  EQU   *         VOL-NR -- > DRUCKBEREICH
         MVC   ZEILE+27(08),=C'VOL-NR ='
         MVC   ZEILE+36(30),JFCB+118    5 VOLUME-NUMMERN
         XR    R3,R3
         IC    R3,JFCB+117              VOLUME-ANZAHL
         LA    R2,5
         CR    R3,R2
         BNH   WRFILE4                  VOL-ANZ  <=5
         CVD   R3,DWORD                 VOL-ANZ > 5
         MVC   ZEILE+68(06),MASKV       --> ANORUCKEN
         ED    ZEILE+69(04),DWORD+6
         SPACE 2
WRFILE4  EQU   *         DRUCKEN FILEZEILE
         MVI   ZEILE,C'0'               VORSCHUBSZEICHEN
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
WRFILE44 EQU   *
         CLI   ZKZ,0          UNTERSTREICHUNG NOTWENDIG(ZKZ=1)
         BE    WRFILE45
         MVC   ZEILE+4(8),=8CL1'-'    DSNAME UNTERSTREICHEN
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
WRFILE45 EQU   *
*
*  ENDE
*
         XRETURN
*
*  MVS --> ALLE DSNAMEN UND VOLSER GELIEFERT
*
WRFILE5  EQU   *
         MVI   ZEILE,C'0'     ZEILENVORSCHUB
         MVC   ZEILE+4(8),DDNAME
         LH    R2,BLNDS       ANZAHL DSNAME
         L     R4,DSNTAB      -->DSNAME TABELLE
         L     R6,VOLTAB
         LA    R6,2(R6)       -->1.VOLSER NR.
FLOOP1   EQU   *
         LTR   R2,R2          ANZAHL NOCH ZU LISTENDER DSNAME
         BZ    WRFILE44       = 0 --> ALLE GELISTET
         LH    R5,0(R4)       ANZAHL DER ZU DSN GEHOERIGE VOLSER
         MVC   ZEILE+4+9(44),2(R4)  DSNAME --> ZEILE
         LA    R3,ZEILE+22+9
FLOOP15  EQU   *   SUCHE, AB WO VOL-NR GEDRUCKT WEREN KANN
         CLI   0(R3),C' '
         BE    FLOOP16        ENDE DES DSNAME GEFUNDEN
         LA    R3,14(R3)
         B     FLOOP15
FLOOP16  EQU   *
         ST    R3,ZDISP       ADRESSE MERKEN, WO VOL-NR GEDRUCKT WURDE
         B     FLOOP25
FLOOP2   EQU   *
         CLI   ZKZ,0
         BE    FLOOP25        UNTERSTREICHUNG NICHT NOTWENDIG
         MVC   ZEILE+4(8),=8CL1'-'  UNTERSTREICHUNG
         MVI   ZKZ,0
FLOOP25  EQU   *
         LTR   R5,R5
         BZ    FLOOP5         RESTANZAHL VOLSER=0
         L     R3,ZDISP
         MVC   1(8,R3),=C'VOL-NR ='
         LA    R3,10(R3)      -->DRUCKBEREICH 1.VOLSER
FLOOP3   EQU   *
         LTR   R5,R5          ANZ.D.NOCH ZU VERARB.VOLSER JE DSN
         BZ    FLOOP5         = 0 --> NAECHSTER DSNAME LISTEN
         C     R3,=A(ZEILE+75)  ZEILE VOLL
         BNL   FLOOP4         JA
         MVC   0(6,R3),0(R6)  VOLSER --> ZEILE
         LA    R3,7(R3)       -->DRUCKBEREICH
         LA    R6,6(R6)       -->VOLSER TABELLE
         BCTR  R5,0           ANZ VOLSER - 1
         B     FLOOP3         NAECHSTE VOLSER
FLOOP4   EQU   *              ZEILE DRUCKFERTIG
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         B     FLOOP2         NAECHSTE ZEILE
FLOOP5   EQU   *
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         LA    R4,46(R4)      -->NAECHSTER DSNAME
         LH    R2,BLNDS
         BCTR  R2,0           ANZAHL DSNAME - 1
         STH   R2,BLNDS
         B     FLOOP1         NAECHSTER DSNAME
         SPACE 2
         EJECT
*              D E F I N I T I O N E N
*              -----------------------
*
PATAB    DC    V(ATAB)                  --> ATAB
DWORD    DS    D
SAVE     DS    18F
DUMDCB   DCB   DSORG=PS,MACRF=(R,W),DDNAME=DD,EXLST=EXLST
EXLST    DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
MASKV    DC    CL6'( 습)'                                   '( ABA)'
MASKD    DC    CL22' 슬슬슬슬슬슬슬음.슬-'  ' AAAAAAAAAAAAAABA.AAA-'
*                                       A=11/0/1/8/9 -LOCHUNG
*                                       B=0/1/9      -LOCHUNG
ZKZ      DS    XL1            = 1 UNTERSTREICHUNG IN NAECHSTE ZEILE
ZDISP    DS    A              ADRESSE IN ZEILE
LNDS     EQU   50*46
LNVOL    EQU   50*6+2
BLNDS    DS    H
BLNVOL   DS    H
VOLTAB   DC    F'-1'          -->VOLSER TABELLE
DSNTAB   DS    A              -->DSNAME TABELLE
DDNAME   DS    CL8
         SPACE 2
ZEILE    DC    CL81' '        DRUCKBEREICH
ABLTPUTL CALL  ,(ZEILE),VL,MF=L
         PRINT GEN
         END
