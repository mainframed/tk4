*   TAG       UMSETZUNG EINES DATUMS IN EINE ANDERE FORM              *
         MACRO
&NAME    TAG   &FELD,&TYPE,&ZEIT,&MF=
.*            PROGRAMMIERER: FEHLING             DATUM: 10.05.73      *
.*            PROGRAMMIERER: SIEFERT        AEND.DATUM: 27.08.73      *
.*            PROGRAMMIERER: FEHLING        AEND.DATUM: 12.03.76      *
.**********************************************************************
.* PARAMETER: &FELD = BEREITGESTELLTES FELD
.*            &TYPE = K DATUM AUS R1 UMWANDELN IN CHAR TT.MM.JJ       *
.*            &TYPE = L MASCH.DATUM UMWANDELN IN BINAER TTMMJJ
.*            &TYPE = M MASCH.DATUM UMWANDELN IN BINAER JJMMTT
.*            &TYPE = N MASCH.DATUM UMWANDELN IN CHAR   TTMMJJ
.*            &TYPE = O MASCH.DATUM UMWANDELN IN CHAR   JJMMTT
.*            &TYPE = P MASCH.DATUM AUS R1 UMWANDELN IN BINAER TTMMJJ
.*            &TYPE = Q MASCH.DATUM AUS R1 UMWANDELN IN BINAER JJMMTT
.*            &TYPE = R MASCH.DATUM AUS R1 UMWANDELN IN CHAR   TTMMJJ
.*            &TYPE = S MASCH.DATUM AUS R1 UMWANDELN IN CHAR   JJMMTT
.*            &TYPE = T BINAER TTMMJJ UMWANDELN IN MASCH.DATUM NACH R1
.*            &TYPE = U BINAER JJMMTT UMWANDELN IN MASCH.DATUM NACH R1
.*            &TYPE = V CHAR   TTMMJJ UMWANDELN IN MASCH.DATUM NACH R1
.*            &TYPE = W CHAR   JJMMTT UMWANDELN IN MASCH.DATUM NACH R1
.*            &TYPE = X BINAER JJMMTT IN R1 KOMPRIMIEREN AUF 2 BYTE
.*            &TYPE = Y BINAER JJMMTT (2 BYTE) IN R1 SPREIZEN
.*            &TYPE = NN MACH.DATUM UMWANDELN IN CHAR TT.MM.JJ
.*            &ZEIT = K  KEINE UHRZEIT IN R0 LIEFERN
.*            &MF=  L LISTFORM - NUR MONATSLISTE GENERIEREN
.*            &MF=  (E,ADRESSE) - AUSFUEHRFORM OHNE MONATSLISTE
.**********************************************************************
         LCLA  &TAGA1,&TAGA2
         LCLB  &TAGREG
         LCLC  &TYP,&SYMBOL
&TAGA1   SETA  2
&TAGA2   SETA  2
         AIF   ('&MF' EQ 'L').S130          LISTFORM ?
         AIF  ('&TYPE' EQ 'X').S350
         AIF  ('&TYPE' EQ 'Y').S360
         AIF   (T'&FELD NE 'O').S010
         AGO   .FEHL01            1. PARAMETER FEHLT
.S010    AIF   ('&FELD'(1,1) NE '(').S030   REGISTER-NOTATION ?
&TAGREG  SETB  1                            JA
         AIF   ('&FELD'(2,1) EQ '0' OR '&FELD'(2,2) EQ 'R0').S020
         AIF   ('&FELD'(2,1) EQ '1' OR '&FELD'(2,2) EQ 'R1').S020
         AIF   ('&FELD'(2,2) EQ '13' OR '&FELD'(2,3) EQ 'R13').S020
         AIF   ('&FELD'(2,2) EQ '14' OR '&FELD'(2,3) EQ 'R14').S020
         AIF   ('&FELD'(2,2) EQ '15' OR '&FELD'(2,3) EQ 'R15').S020
         AGO   .S030
.S020    AGO   .FEHL02            FALSCHE REGISTER-NOTATION
.S030    AIF   ('&TYPE' EQ '').S034
         AIF   ('&TYPE' EQ 'L' OR '&TYPE' EQ 'P' OR '&TYPE' EQ 'U' OR '&
               &TYPE' EQ 'W').S040
         AIF   ('&TYPE' EQ 'M' OR '&TYPE' EQ 'Q' OR '&TYPE' EQ 'T' OR '&
               &TYPE' EQ 'V').S050
         AIF   ('&TYPE' EQ 'N' OR '&TYPE' EQ 'O' OR '&TYPE' EQ 'R' OR '&
               &TYPE' EQ 'S' OR '&TYPE' EQ 'NN').S060
         AIF  ('&TYPE' EQ 'K').S060
         IHBERMAC 183,TYPE,N
         AGO   .S035
.S034    IHBERMAC 180,TYPE,N      TYPE = N (DEFAULT-ANNAHME)
.S035    ANOP
&TYP     SETC  'N'
         AGO   .S070
.S040    ANOP
&TAGA1   SETA  &TAGA1+4
         AGO   .S060
.S050    ANOP
&TAGA2   SETA  &TAGA2+4
.S060    ANOP
&TYP     SETC  '&TYPE'
.S070    AIF   ('&NAME' EQ '').S075
&NAME    DS    0H
.S075    AIF   ('&TYP' EQ 'L' OR '&TYP' EQ 'M' OR '&TYP' EQ 'N' OR '&TYP
               P' EQ 'O' OR '&TYP' EQ 'NN').S080
         AGO   .S100
.S080    AIF   ('&ZEIT'  EQ  'K').S090
         LA    1,2(0,0)
         SVC   11
         AGO   .S100
.S090    L     1,16
         L     1,56(,1)
.S100    AIF   (T'&MF EQ 'O').S120
         AIF   ('&MF'(1,3) NE '(E,').S110
         LA    14,&MF(2)          LOAD LIST ADDRESS
         AGO   .S150
.S110    IHBERMAC 157,MF,&MF
.S120    CNOP  4,8
         BAL   14,*+38
         AGO   .S140
.S130    AIF   ('&NAME' EQ '').FEHL03
&SYMBOL  SETC  '&NAME'
         CNOP  0,8
.S140    ANOP
&SYMBOL  DC    D'0' .               0(14)
         DC    P'31' . JANUAR       8(14)
         DC    P'28' . FEBRUAR     10(14)
         DC    P'31' . MAERZ       12(14)
         DC    P'30' . APRIL       14(14)
         DC    P'31' . MAI         16(14)
         DC    P'30' . JUNI        18(14)
         DC    P'31' . JULI        20(14)
         DC    P'31' . AUGUST      22(14)
         DC    P'30' . SEPTEMBER   24(14)
         DC    P'31' . OKTOBER     26(14)
         DC    P'30' . NOVEMBER    28(14)
         DC    P'31' . DEZEMBER    30(14)
         DC    PL2'0' .MONATS.ZAE. 32(14)
         AIF   ('&MF' NE 'L').S150
         MEXIT
.S150    ZAP   32(2,14),29(1,14) LOESCHEN MONATSZAEHLER
         NI    11(14),X'EF' FEBRUAR HAT 28 TAGE
         AIF   ('&TYP' EQ 'T' OR '&TYP' EQ 'U' OR '&TYP' EQ 'V' OR '&TYP
               P' EQ 'W').S270
         ST    1,0(,14)
         TM    1(14),X'01' UNGERADES JAHR ?
         BO    *+16
         TM    1(14),X'12' SCHALTJAHR ?
         BM    *+8
         OI    11(14),X'10' FEBRUAR = 29 TAGE
         LR    15,14
         LA    15,2(,15)
         AP    32(2,14),31(1,14)
         SP    2(2,14),6(2,15)
         BH    *-16
         AP    2(2,14),6(2,15)
         BH    *+10 .             FALLS TAG =0, DANN
         SP    32(2,14),31(1,14) .SOLL AUCH MONAT = 0 WERDEN
         AIF   ('&TYP' EQ 'L' OR '&TYP' EQ 'M' OR '&TYP' EQ 'P' OR '&TYP
               P' EQ 'Q').S240
         AIF   ('&TYP' EQ 'NN' OR '&TYP' EQ 'K').S220
         AIF   (&TAGREG).S160
         UNPK  &FELD.(6),1(3,14)
         AGO   .S170
.S160    UNPK  0(6,&FELD(1)),1(3,14)
.S170    AIF   ('&TYP' EQ 'O' OR '&TYP' EQ 'S').S190
         AIF   (&TAGREG).S180
         MVC   &FELD+4(2),&FELD+1
         UNPK  &FELD.(2),2(2,14)
         OI    &FELD+1,X'F0'
         AGO   .S210
.S180    MVC   4(2,&FELD(1)),1(&FELD(1))
         UNPK  0(2,&FELD(1)),2(2,14)
         OI    1(&FELD(1)),X'F0'
         AGO   .S205
.S190    AIF   (&TAGREG).S200
         MVC   &FELD.(2),&FELD+1
         OI    &FELD+5,X'F0'
         AGO   .S210
.S200    MVC   0(2,&FELD(1)),1(&FELD(1))
         OI    5(&FELD(1)),X'F0'
.S205    UNPK  2(2,&FELD(1)),32(2,14)
         OI    3(&FELD(1)),X'F0'
         MEXIT
.S210    UNPK  &FELD+2(2),32(2,14)
         OI    &FELD+3,X'F0'
         AIF   ('&TYP' EQ 'NN' OR '&TYP' EQ 'K').S220
         MEXIT
.S220    AIF   (&TAGREG).S230     TYP = NN ODER K
         UNPK  &FELD.(5),1(3,14)  JJ0TT000
         MVC   &FELD+6(2),&FELD   JJ0TT0JJ
         MVC   &FELD.(2),&FELD+3  TT0TT0JJ
         OI    &FELD+1,X'F0'
         UNPK  &FELD+3(2),32(2,14) TT0MM0JJ
         OI    &FELD+4,X'F0'
         MVI   &FELD+2,C'.'
         MVI   &FELD+5,C'.'
         MEXIT
.S230    UNPK  0(5,&FELD(1)),1(3,14)     JJ0TT000
         MVC   6(2,&FELD(1)),0(&FELD(1)) JJ0TT0JJ
         MVC   0(2,&FELD(1)),3(&FELD(1)) TT0TT0JJ
         OI    1(&FELD(1)),X'F0'
         UNPK  3(2,&FELD(1)),32(2,14)    TT0MM0JJ
         OI    4(&FELD(1)),X'F0'
         MVI   2(&FELD(1)),C'.'
         MVI   5(&FELD(1)),C'.'
         MEXIT
.S240    UNPK  &TAGA2.(2,14),2(2,14)
         UNPK  &TAGA1.(3,14),1(2,14)
         UNPK  4(2,14),32(2,14)
         AIF   ('&TYP' EQ 'M' OR '&TYP' EQ 'Q').S250
         MVC   8(2,14),12(14)
.S250    PACK  0(8,14),2(6,14)
         CVB   15,0(14)
         AIF   (&TAGREG).S260
         ST    15,&FELD
         MEXIT
.S260    ST    15,0(&FELD(1))
         MEXIT
.S270    XC    0(8,14),0(14)
         AIF   ('&TYP' EQ 'V' OR '&TYP' EQ 'W').S300
         AIF   (&TAGREG).S280
         L     15,&FELD
         AGO   .S290
.S280    L     15,0(&FELD(1))
.S290    CVD   15,0(14)
         MVC   0(4,14),4(14)
         UNPK  0(8,14),0(4,14)
         AGO   .S320
.S300    AIF   (&TAGREG).S310
         MVC   2(6,14),&FELD
         AGO   .S320
.S310    MVC   2(6,14),0(&FELD(1))
.S320    PACK  4(2,14),4(2,14)
         LR    15,14
         LA    15,2(,15)
         AP    32(2,14),6(2,15)
         SP    4(2,14),31(1,14)
         BNZ   *-16
         SP    32(2,14),6(2,15)
         PACK  &TAGA1.(2,14),&TAGA1.(2,14)
         AP    32(2,14),&TAGA1.(2,14)
         AIF   ('&TYP' EQ 'T' OR '&TYP' EQ 'V').S330
         PACK  2(3,14),&TAGA2.(3,14)
         MVC   0(3,14),2(14)
         AGO   .S340
.S330    PACK  0(3,14),&TAGA2.(3,14)
.S340    MVC   2(2,14),32(14)
         TM    1(14),X'01' UNGERADES JAHR ?
         BO    *+18
         TM    1(14),X'12' SCHALTJAHR ?
         BM    *+10
         AP    2(2,14),31(1,14)
         L     1,0(,14)
         MEXIT
.*       BINAER DATUM KOMPRIMIEREN
.S350    ANOP
&NAME    SR    R0,R0 
         LA    R14,100 
         DR    R0,R14 
         LR    R14,R0 
         SRDL  R14,5 
         SR    R0,R0 
         LA    R14,100 
         DR    R0,R14 
         LR    R14,R0 
         SLDL  R14,5 
         SLL   R1,9 
         ALR   R1,R14 
         MEXIT
.*       BINAER DATUM SPREIZEN
.S360    ANOP
&NAME    SR    R0,R0 
         SLDL  R0,23 
         LR    R15,R0 
         LA    R0,100 
         MR    R14,R0 
         LR    R0,R14 
         SLDL  R0,4 
         ALR   R15,R0 
         LA    R0,100 
         MR    R14,R0 
         SRL   R1,27 
         ALR   R1,R15 
         MEXIT
.FEHL01  IHBERMAC 2               1. PARAMETER FEHLT
         MEXIT
.FEHL02  IHBERMAC 36,,&FELD       FALSCHE REGISTER-NOTATION
         MEXIT
.FEHL03  MNOTE 12,'MACRO NAME FIELD BLANK NOT ALLOWD W/MF=L'
         MEND
