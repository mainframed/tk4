./ ADD NAME=ALTR
ALTR     CSECT
         XSAVE R12,,ALTR,WORKL
         USING WORK,R13
         LR    R14,R1
         USING PARMAREA,R14
         ST    R14,SAVE14
         LA    R2,TEXT
         XC    ECB,ECB
         LA    R11,ZWI
AGAIN    EQU   *
         LR    R5,R2
         LA    R5,4(R5)
         LA    R4,0
NEXTCHAR EQU   *
         LA    R5,1(R5)
         CLI   0(R5),C','
         BE    PAREND
         LA    R4,1(R4)
         B     NEXTCHAR
PAREND   EQU   *
         LR    R6,R4
         SRL   R6,1
         SLL   R6,1
         CR    R6,R4
         BE    NOER
         MVC   ZWI,5(R2)
         MVI   5(R2),X'F0'
         MVC   6(80,R2),ZWI
         B     AGAIN
         EJECT
ERROR    EQU   *
         MVC   WTO(LENOFER),ER
         L     R0,REG4
         WTO   MF=(E,WTO)
RETURN   EQU   *
         XRETURN ,R
         EJECT
NOER     EQU   *
         LA    R6,4(R2)
         CH    R4,=H'8'
         BH    ERROR
         CH    R4,=H'0'
         BNH   ERROR
         LR    R7,R4
CONTNI   EQU   *
         LA    R6,1(R6)
         NI    0(R6),X'3F'
         BCT   R7,CONTNI
         LA    R6,5(R2)
         BCTR  R4,0
         EX    R4,TREX
         EX    R4,TRTEX
         BNZ   ERROR
         LA    R4,1(R4)
         LA    R7,4
         SLL   R7,4
         OR    R7,R4
         EX    R7,PCKEX
         LA    R5,17
         LA    R7,0
         LA    R6,5(R4,R2)
ZAPLOOP  EQU   *
         LA    R6,1(R6)
         CLI   0(R6),X'40'
         BE    WEITER
         CLI   0(R6),C','
         BE    WEITER
         NI    0(R6),X'3F'
         LA    R7,1(R7)
         BCT   R5,ZAPLOOP
WEITER   EQU   *
         LTR   R7,R7
         BZ    ERROR
         CLI   0(R6),C','
         BNE   ERROR
         CLI   1(R6),C','
         BE    NOCPUID
         LA    R6,1(R6)
         CLI   0(R6),C'A'
         BE    SETRPLY
         CLI   0(R6),C'B'
         BNE   NOCPUID
SETRPLY  EQU   *
         MVC   REPLY,0(R6)
         MVI   ECB,X'FF'
NOCPUID  EQU   *
         CLI   1(R6),C','
         BNE   ERROR
         CLI   2(R6),X'40'
         BE    ERROR
         CLI   2(R6),C','
         BE    ERROR
         BCTR  R7,0
         LA    R6,6(R4,R2)
         TR    0(16,R6),TRTAB
         EX    R7,TRTEX
         BNZ   ERROR
         PACK  5(5,R11),0(9,R6)
         PACK  9(5,R11),8(9,R6)
         L     R4,0(R11)
         SRL   R7,1
         MVC   WTO(LENOFOLD),OLD
         MODESET KEY=ZERO
         CL    R4,=XL4'00FFFFFF'
         BNH   NO31MODE
         XMODE31
NO31MODE UNPK  WTO+29(9),0(5,R4)
         UNPK  WTO+37(9),4(5,R4)
         XMODE24
         MODESET KEY=NZERO
         MVI   WTO+45,X'40'
         TR    WTO+29(16),CCTAB-240
         L     R0,REG4
         WTO   MF=(E,WTO)
         MODESET KEY=ZERO,MODE=SUP
         CL    R4,=F'512'
         BH    LAB1
         PROTPSA DISABLE
         XMODE31
         EX    R7,MVCEX
         XMODE24
         PROTPSA ENABLE
         B     LAB2
LAB1     DS    0H
         XMODE31
         EX    R7,MVCEX
         XMODE24
LAB2     DS    0H
         MODESET KEY=NZERO,MODE=PROB
         B     RETURN
         EJECT
TREX     TR    0(1,R6),TRTAB
TRTEX    TRT   0(0,R6),ZEROES
PCKEX    PACK  0(0,R11),0(0,R6)
MVCEX    MVC   0(0,R4),5(R11)
ER       WTO   'XALTR01 ERROR IN PARAMETER',MCSFLAG=(REG0),MF=L
LENOFER  EQU   *-ER
OLD      WTO   'XALTR03 OLD CONTENTS WAS                  ',MF=L,      *
               MCSFLAG=REG0
LENOFOLD EQU   *-OLD
TRTAB    DS    0CL64
         DC    X'FF0A0B0C0D0E0F'
         DC    41XL1'FF'
         DC    X'00010203040506070809'
         DC    6XL1'FF'
         DC    0F'0'
ZEROES   DC    16XL1'0'
CCTAB    DC    C'0123456789ABCDEF'
         LTORG
         EQUATE
         EJECT
WORK     DSECT
SVA      DS    18F
WTO      DS    CL80
ZWI      DS    CL80
ECB      DS    F
REPLY    DS    X
SAVE14   DS    F
WORKL    EQU   *-WORK
         XPARM
         IHAPSA
         END
./ ADD NAME=CLIP
         TITLE 'XCLIP - ICKDSF - 370/XA'
***********************************************************************
*                                                                     *
*      PROGRAM-ID. CLIP                                               *
*      AUTHOR. BOEHM.                                                 *
*      INSTALLATION. BMW AG, MUENCHEN.                                *
*      DATE-WRITTEN. 14.02.1980 // 28.02.1983 // 15.06.83 // 27.02.84 *
*      REMARKS.                                                       *
*                                                                     *
*          DER X-COMMAND 'CLIP' DIENT ALS INTERFACE ZWISCHEN DER      *
*          SYSTEM-TASK 'XCNTRLVS' UND DEM UTILITY 'ICKDSF'.           *
*          ICKDSF FUEHRT DEN 'REFORMAT'-COMMAND AUS.                  *
*                                                                     *
*          PARAMETERLISTE:                                            *
*                     CLIP,CUU,OOOOOO,NNNNNN,,X                       *
*                          CUU = TODD-ADDR.                           *
*                          OOOOOO = OLDVOLID                          *
*                          NNNNNN = NEWVOLID                          *
*                          X = BELIEBIG (NON-BLANK)                   *
*                                                                     *
*          DER REFMT-COMMAND WIRD NUR AUSGEFUEHRT, WENN DIE SPEZI-    *
*          FIZIERTE EINHEIT OFF-LINE GESETZT IST.                     *
*                                                                     *
*          NON-IBM MACROS: XUCBSCAN, XUCBGET, XUCBOFF, XSAVE, XRETURN *
*          EXTERN. REFER.: ICKDSF, ICK-MSG-NUMBERS                    *
*                                                                     *
*          OPER.-SYST. : /SP1.3.X & /SP2.1.X
*                                                                     *
***********************************************************************
*
         EJECT
         SPACE 1
CLIP     CSECT
***********************************************************************
*                                                                     *
*      BEGINN DER VERARBEITUNG.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      SICHERN DER REGISTER,
*      SPEICHERPLATZANFORDERUNG FUER ARBEITSBEREICHE,
*      ZUORDNUNG DER BASISREGISTER.
*
         SPACE 1
         XSAVE R12,,CLIP,ABERL
         SPACE 1
         LR    R11,R01       BASISREG. FUER DSECT PARMAREA INITIALIS.
         USING EINGABE,R09   BASISREG. FUER DSECT EINGABE ZUORDNEN
         USING AUSGABE,R10   BASISREG. FUER DSECT AUSGABE ZUORDNEN
         USING PARMAREA,R11  BASISREG. FUER DSECT PARMAREA ZUORDNEN
         USING ABER,R13      BASISREG. FUER DSECT ABER ZUORDNEN
         SPACE 2
*
*      ABFRAGE DES SYSTEMS.
*
         SPACE 1
         L     R02,16                  LOAD ADDRESS OF CVT
         TM    116(R02),X'01'          SYSTEM = OS/VS2 (MVS) ?
         BZ    FEHLSYS       NEIN, VERZW. ZUR FEHLERROUTINE
         EJECT
*
*     PRUEFUNG AUF VORHANDENE AKTIVITAET
*     DER COMMANDS 'AMS' ODER 'BAY',
*     WARTEN, BIS DIESE INAKTIV SIND.
*
         SPACE 1
         MVC   ENQTLIST(12),ENQTLCON PARAMETERLISTE UEBERTRAGEN
         ENQ   ,MF=(E,ENQTLIST) ENQ-SVC (TEST)
         LTR   R15,R15       RETURN CODE = 0 ?
         BZ    ENQQ                    BRANCH IF YES
         L     R00,REG4      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG05)  NACHRICHTENZEILE AUSGEBEN
ENQQ     ENQ   ,MF=(E,ENQULIST) ENQ-SVC (UNCONDITIONAL)
         SPACE 2
*
*      INITIALISIERUNG DER SPEICHER.
*
         SPACE 1
INIT     EQU   *
         MVC   EIN(EDCBLEN),EINDCB DCB FUER SYSPRINT UEBERTRAGEN
         MVC   AUS(ADCBLEN),AUSDCB DCB FUER SYSIN UEBERTRAGEN
         LA    R02,EIN       ADRESSEN DER
         LA    R03,AUS           DCB'S LADEN
         STM   R02,R03,EDCBADDR  UND SPEICHERN
         MVI   EDCBADDR,X'80' OPTION BYTE SETZEN
         MVI   ADCBADDR,X'8F' OPTION BYTE SETZEN
         SPACE 2
*
*      PRUEFUNG DER PARAMETERKETTE.
*
         SPACE 1
         CLC   TEXT,BLANK    TEXT = BLANK ?
         BE    PARMERR       JA, VERZW. ZUR FEHLERROUTINE
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DER PARAMETERKETTE.                               *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      SYSIN EROEFFNEN.
*
         SPACE 1
         LA    R01,ADCBADDR            LOAD ADDRESS OF DCB ADDR.
         SVC   19            VERZW. ZUM EROEFFNEN DER DATEI
         MVI   FLAGBYTE,X'00'
         SPACE 2
*
*      PARAMETERKETTE PRUEFEN UND UEBERTRAGEN
*
         SPACE 1
         PUT   AUS           AUSGABEBEREICH ZUR VERFUEGUNG STELLEN
         LR    R10,R01       LAUFENDE ADRESSE LADEN
         LA    R02,TEXT-1    ADRESSE DES PARAMETERTEXTES LADEN
         CLI   0(R02),C','
         BNE   PARMERR
         CLI   4(R02),C','
         BNE   PARMERR
         CLI   11(R02),C','
         BNE   PARMERR
         CLC   18(2,R02),=C',,'
         BNE   PARMERR
         CLI   20(R02),X'40'
         BE    PARMERR
         MVC   AZEILE(80),CARD   MOVE SYSIN CARD
         LA    R07,AZEILE
         MVC   11(3,R07),1(R02)  MOVE DEV.-ADDR.
         MVC   20(6,R07),05(R02)  MOVE OLD-VOLID
         MVC   34(6,R07),12(R02)  MOVE NEW-VOLID
*
*    CHECK FOR SPECIFIED UCB OFFLINE
*
         USING UCBOB,R06
*
         XUCBSCAN R08,DASD
*
SCLOOP   XUCBGET  R08,UCBEND,ERROR
*
         LR    R06,R01             UCB-@
         CLC   UCBNAME(3),1(R02)   DEVICE #
         BNE   SCLOOP
         XUCBOFF R08
         TM    3(R06),X'80'        TEST FOR ONLINE
         BO    ONLINE                  BRANCH IF YES
*
*      SYSIN SCHLIESSEN.
*
         SPACE 1
         DROP  R06
CLOSYSIN EQU   *
         MVI   ADCBADDR,X'80' OPTION BYTE MODIFIZIEREN
         LA    R01,ADCBADDR            LOAD ADDRESS OF DCB ADDR.
         SVC   20            VERZW. ZUM SCHLIESSEN DER DATEI
         L     R01,ADCBADDR     @-DCB (SYSIN)
         FREEPOOL (1)           FREE BUFFERS
         CLI   FLAGBYTE,X'FF'
         BE    ABSCHLUS
         EJECT
***********************************************************************
*                                                                     *
*      AUFRUF DES UTILITIES 'ICKDSF'                                  *
*                                                                     *
***********************************************************************
         SPACE 3
         LA    R01,PARMADDR  ADRESSE DER PARAMETERLISTE LADEN
         LINK  EP=ICKDSF     VERZW. ZUM UTILITY 'ICKDSF'
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DER ICKDSF - NACHRICHTEN                          *
*                                                                     *
***********************************************************************
        SPACE 3
*
*      SYSPRINT EROEFFNEN.
*
         SPACE 1
         LA    R01,EDCBADDR            LOAD ADDRESS OF DCB ADDR.
         SVC   19            VERZW. ZUM EROEFFNEN DER DATEI
WTOMSG06 EQU   *
         L     R00,REG4                LOAD UCM ID.
         WTO   MF=(E,MSG06)  NACHRICHTENZEILE AUSGEBEN
         SPACE 2
*
*      AUSGABE DER ICKDSF-NACHRICHTEN.  ===
*
         SPACE 1
LOOP2OK  EQU   *
         MVC   WTO(LENMSG01),MSG01 NACHRICHTENZEILE UEBERTRAGEN
LOOP2    EQU   *
         MVC   WTOTEXT(120),BLANK
         GET   EIN           EINGABEBEREICH ZUR VERFUEGUNG STELLEN
         LR    R09,R01       LAUFENDE ADRESSE LADEN
         XR    R08,R08                  CLEAR R08
         LH    R08,EINGABE        RDW = L'(MSG)+4
         AH    R08,=H'7'     L'(MSG)+4 + 8(XCLIP01 ) - 1(CC) = L'(WTO)
         STH   R08,WTO       FOR WTO
         SH    R08,=H'13'    L'(WTO)-8(XCLIP01 )-4(RDW)-1 = L'(TEXT-1)
         EX    R08,MOVMSG    TEXT TO WTO
         CLC   WTOTEXT+00(12),=C'ICKDSF - VS2'    TITLE
         BE    LOOP2          YES, BYPASS
         CLC   WTOTEXT+00(20),BLANK   BLANK-LINE
         BE    LOOP2          YES, BYPASS
         CLC   WTOTEXT+00(09),=C'ICK00700I'
         BE    LOOP2          YES, BYPASS
         CLC   WTOTEXT+00(09),=C'ICK00002I'
         BE    LOOP2          YES, BYPASS
         L     R00,REG4                LOAD UCM ID.
         WTO   MF=(E,WTO)    NACHRICHTENZEILE AUSGEBEN
         B     LOOP2          NEXT READ
*
MOVMSG   MVC   WTOTEXT(00),ETEXT        UEBERTR. DES TEXTES
         EJECT
*
*      SYSPRINT SCHLIESSEN.
*
         SPACE 1
ENDFILE  EQU   *
         L     R00,REG4                LOAD UCM ID.
         WTO   MF=(E,MSG07)  NACHRICHTENZEILE AUSGEBEN
CLOSEPRT EQU   *
         LA    R01,EDCBADDR            LOAD ADDRESS OF DCB ADDR.
         SVC   20            VERZW. ZUM SCHLIESSEN DER DATEI
         L     R01,EDCBADDR      @-DCB (SYSPRINT)
         FREEPOOL (1)            FREE BUFFERS
         EJECT
***********************************************************************
*                                                                     *
*      ABSCHLUSSROUTINEN.                                             *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      DEQ-SVC UND LAENGE DER
*      ARBEITSBEREICHE FESTSTELLEN.
*
         SPACE 1
ABSCHLUS EQU   *
         MVC   DEQLIST(12),DEQCLIST PARAMETERLISTE UEBERTRAGEN
         DEQ   ,MF=(E,DEQLIST) DEQ-SVC
         LA    R01,ABERL     LAENGE DES ARBEITSBEREICHS LADEN UND
         ST    R01,SAVEAREA      IM ERSTEN WORT DER SAVEAREA SPEICHERN
         SPACE 2
*
*      SPEICHERPLATZFREIGABE,
*      REINITIALISIERUNG DER REGISTER UND
*      RUECKSPRUNG ZUM AUFRUFENDEN PROGRAMM.
*
         SPACE 1
ENDE     EQU   *
         XRETURN 0,R
         EJECT
***********************************************************************
*                                                                     *
*      FEHLERROUTINEN.                                                *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      ROUTINE BEI UNGUELTIGEM SYSTEM.
*
         SPACE 1
FEHLSYS  EQU   *
         L     R00,REG4                LOAD UCM ID.
         WTO   MF=(E,MSG02)  NACHRICHTENZEILE AUSGEBEN
         B     ENDE                    BRANCH
         SPACE 2
*
*      ROUTINE BEI FEHLER IN DER PARAMETERKETTE.
*
         SPACE 1
PARMERR  EQU   *
         L     R00,REG4                LOAD UCM ID.
         WTO   MF=(E,MSG03)  NACHRICHTENZEILE AUSGEBEN
         MVI   FLAGBYTE,X'FF'   SET RETURN INDICATOR
         B     CLOSYSIN                BRANCH
*
*      ROUTINE BEI UNIT ONLINE.
*
         SPACE 1
ONLINE   EQU   *
         L     R00,REG4                LOAD UCM ID.
         WTO   MF=(E,MSG08)  NACHRICHTENZEILE AUSGEBEN
         MVI   FLAGBYTE,X'FF'   SET RETURN INDICATOR
         B     CLOSYSIN                BRANCH
*
*      ROUTINE BEI DASD NOT FOUND
*
         SPACE 1
UCBEND   EQU   *
         L     R00,REG4                LOAD UCM ID.
         WTO   MF=(E,MSG09)  NACHRICHTENZEILE AUSGEBEN
         MVI   FLAGBYTE,X'FF'   SET RETURN INDICATOR
         B     CLOSYSIN                BRANCH
*
*      ROUTINE BEI VOLSER NOT FOUND
*
         SPACE 1
ERROR    EQU   *
         L     R00,REG4                LOAD UCM ID.
         WTO   MF=(E,MSG10)  NACHRICHTENZEILE AUSGEBEN
         MVI   FLAGBYTE,X'FF'   SET RETURN INDICATOR
         B     CLOSYSIN                BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      REGISTER EQUATES.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
R00      EQU   0             MACRO- UND-PARAMETER REGISTER
R01      EQU   1             MACRO- UND-PARAMETER REGISTER
R02      EQU   2             ARBEITSREGISTER
R03      EQU   3             ARBEITSREGISTER
R04      EQU   4             ARBEITSREGISTER
R05      EQU   5             ARBEITSREGISTER
R06      EQU   6             ARBEITSREGISTER
R07      EQU   7             ARBEITSREGISTER
R08      EQU   8             ARBEITSREGISTER
R09      EQU   9             BASISREG. FUER DSECT EINGABE
R10      EQU   10            BASISREG. FUER DSECT AUSGABE
R11      EQU   11            BASISREG. FUER DSECT PARMAREA
R12      EQU   12            BASISREG. FUER CSECT CLIP
R13      EQU   13            BASISREG. FUER DSECT ABER
R14      EQU   14            RUECKSPRUNGADRESSEN
R15      EQU   15            CSECT-ADRESSEN UND RETURN CODES
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER KONSTANTEN-SPEICHER.                            *
*                                                                     *
***********************************************************************
         SPACE 3
         DS    0F
PARMADDR DC    AL1(128)
         DC    AL3(PARMINFO)
PARMINFO DC    H'0'
BLANK    DC    120C' '
QNAME    DC    C'XCNTRLVS'
RNAME    DC    C'SYSPRINT'
TRTAB    DS    0CL250
         DC    193X'FF'
         DC    X'0A0B0C0D0E0F'
         DC    41X'FF'
         DC    X'00010203040506070809'
CARD     DS    0CL80
         DC    C' RFMT UNIT(???) VFY(??????) VOLID(??????)'
         DC    CL39' '
DASD     EQU   X'20'
         SPACE 2
*
*      PARAMETERLISTE FUER ENQ-SVC (UNCONDITIONAL).
*
         SPACE 1
ENQULIST ENQ   (QNAME,RNAME,E,8,SYSTEMS),RET=NONE,MF=L
         SPACE 2
*
*      PARAMETERLISTE FUER ENQ-SVC (TEST).
*
         SPACE 1
ENQTLCON ENQ   (QNAME,RNAME,E,8,SYSTEMS),RET=TEST,MF=L
         EJECT
*
*      PARAMETERLISTE FUER DEQ-SVC.
*
         SPACE 1
DEQCLIST DEQ   (QNAME,RNAME,8,SYSTEMS),MF=L
         SPACE 2
*
*      DCB FUER SYSPRINT.
*
         SPACE 1
EINDCB   DCB   DDNAME=SYSPRINT,                                        *
               DSORG=PS,                                               *
               MACRF=(GL),                                             *
               EODAD=ENDFILE
EDCBLEN  EQU   *-EINDCB      LAENGE DES DCB
         SPACE 2
*
*      DCB FUER SYSIN.
*
         SPACE 1
AUSDCB   DCB   DDNAME=SYSIN,                                           *
               DSORG=PS,                                               *
               MACRF=(PL),                                             *
               RECFM=F,                                                *
               LRECL=80,                                               *
               BLKSIZE=80
ADCBLEN  EQU   *-AUSDCB      LAENGE DES DCB
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER AUSGABENACHRICHTEN.                             *
*                                                                     *
***********************************************************************
         SPACE 3
MSG01    WTO   'XCLIP01                                                *
                                                ',                     *
               MF=L,MCSFLAG=(REG0)
LENMSG01 EQU   *-MSG01
         SPACE 1
MSG02    WTO   'XCLIP02 FUNCTION SUPPORTED FOR OS/VS2 (MVS) ONLY',     *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG03    WTO   'XCLIP03 INVALID PARAMETER LIST',                       *
               MF=L,MCSFLAG=(REG0)
         EJECT
MSG05    WTO   'XCLIP05 COMMAND ''AMS'' OR ''BAY'' OR ''CLIP'' ACTIVE W*
               AIT FOR COMPLETION',                                    *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG06    WTO   'XCLIP00 TOP OF DATA SET',                              *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG07    WTO   'XCLIP00 END OF DATA SET',                              *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG08    WTO   'XCLIP06 SPECIFIED UNIT ONLINE; VARY OFFLINE',          *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG10    WTO   'XCLIP10 ERROR IN UCB-SCAN ROUTINE RC > 4   ',          *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG09    WTO   'XCLIP09 VOLSER NOT FOUND IN UCB - CHAIN    ',          *
               MF=L,MCSFLAG=(REG0)
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER ARBEITSBEREICHE.                                *
*                                                                     *
***********************************************************************
         SPACE 3
ABER     DSECT
SAVEAREA DS    18F           SAVEAREA FUER ALLGEMEINE REGISTER
UADDR    DS    CL3           WORK F. CUU
FLAGBYTE DS    C             FLAG BYTE
DEQLIST  DS    3F            PARAMETERLISTE FUER DEQ-SVC
EDCBADDR DS    F             DCB-ADRESSE FUER SYSPRINT
ADCBADDR DS    F             DCB-ADRESSE FUER SYSIN
EIN      DS    0F            DCB FUER SYPRINT
         ORG   *+EDCBLEN
AUS      DS    0F            DCB FUER SYSIN
         ORG   *+ADCBLEN
WTO      DS    0F            BEREICH FUER AUSGABENACHRICHTEN
         DS    CL12
WTOTEXT  DS    CL120         BEREICH FUER AUSGABETEXTE
         SPACE 2
*
*      PARAMETERLISTE FUER ENQ-SVC (TEST).
*
         SPACE 1
ENQTLIST DS    0F
         DS    CL3
ENQTRC   DS    C             RETURN CODE
         DS    CL8
         SPACE 1
ABERL    EQU   *-ABER        LAENGE DES ARBEITSBEREICHES
         SPACE 2
*
*      EINGABEBEREICH.
*
         SPACE 1
EINGABE  DSECT
RDW      DS    CL4           RDW
CC       DS    C             STEUERZEICHEN
ETEXT    DS    CL120         EINGABETEXT
         EJECT
*
*      AUSGABEBEREICH.
*
         SPACE 1
AUSGABE  DSECT
AZEILE   DS    CL80          AUSGABEZEILE
         SPACE 2
*
*      PARAMETERBEREICH.
*
         SPACE 1
PARMAREA DSECT
REG4     DS    F             MCS-ID DER CONSOLE
         DS    CL5           = 'CLIP,'
TEXT     DS    CL120
PARMENDE EQU   *
         SPACE 1
         PRINT NOGEN
UCBOB    IEFUCBOB
*
CVTMAP   CVT   DSECT=YES
*
         END   CLIP
./ ADD NAME=DEL
* MVS/XA UCBSCAN                               6.7.83 BMW         /XUCB
***********************************************************************
*                                                                     *
*        INITIAL PROCESSING.                                          *
*                                                                     *
***********************************************************************
         SPACE 2
XDEL     CSECT
         XSAVE (R11,R12),,XDEL,WORKLEN
         USING WORK,R13                 ALLOC. BASE REG. OF WORKAREA
         USING PARMAREA,R07             ALLOC. BASE REG. OF PARMAREA
         LR    R07,R01                  INIT. BASE REG. OF PARMAREA
INIT     EQU   *
         XC    TRTTAB(256),TRTTAB       CLEAR TRANSLATE TABLE
         MVI   MEMBER,C' '              INITIALIZE
         MVC   MEMBER+1(7),MEMBER       MEMBER NAME
         MVI   DSNAME,C' '              INITIALIZE
         MVC   DSNAME+1(43),DSNAME      DSNAME
         MVI   VOLSER,C' '              INITIALIZE
         MVC   VOLSER+1(5),VOLSER       VOLSER
         MVI   PO,X'00'                 INIT. FLAG BYTE FOR PO DATAS.
         MVI   VOL,X'00'                INIT. FLAG BYTE FOR VOL. SPEC.
         MVI   PURGE,X'00'              INIT. FLAG BYTE FOR PUR. SPEC.
         MVI   VOLPURGE,X'00'           INIT. FLAG BYTE FOR VOL×PUR SP.
*
*   SCAN PARAMETERS
*
         CLI   TEXT+3,C','              LOOK FOR FIRST SEPERATOR
         BNE   ERROR                    BRANCH IF NOT FOUND
         MVI   KOMMA,C','               MOVE SEPARATOR TO TRANSL. TAB.
         TRT   TEXT+4(119),TRTTAB       LOOK FOR NEXT SEPERATOR
         BZ    DSNPO                    IF NONE, ONLY DSN. SPECIFIED
         LR    R5,R1                    LOAD ADRESS FOR SECOND SEPERAT.
         OI    VOLPURGE,X'80'           SET FLAG FOR VOL × PURGE SPEC.
DSNPO    EQU   *
         MVI   KOMMA,X'00'              CLEAR TRANSLATE TABLE AGAIN
         MVI   LPAN,C'('                MOVE LEFT PARENTHESIS TO TABLE
         TRT   TEXT(124),TRTTAB         LOOK FOR LEFT PARENTHESIS
         BZ    NOTPO                    IF NONE,SEQUENTIAL DATASET
         LR    R3,R1                    SAVE ADDRESS OF LEFT PAREN.
         MVI   LPAN,X'00'               CLEAR TRANSLATE TABLE AGAIN
         MVI   RPAN,C')'                MOVE RIGHT PARENTHESIS TO TABLE
         TRT   TEXT(124),TRTTAB         LOOK FOR RIGHT PARENTHESIS
         BZ    ERROR                    IF NONE, SYNTAXERROR
         MVI   RPAN,X'00'               CLEAR TRANSLATE TABLE
         LR    R4,R1                    SAVE ADDRESS OF RIGHT PAREN.
         SR    R1,R3                    MEMBERLENGTH+2
         BM    ERROR                    IF NEGATIVE, BRANCH ERROR
         OI    PO,X'80'                 SET FLAG BYTE FOR PO DATASET
         BCTR  R1,0                     DECREASE BY
         BCTR  R1,0                     TWO
         EX    R1,MOVEMEM               BRANCH TO MOVE MEMBER NAME
         LA    R1,TEXT+4                LOAD ADDRESS OF DSN
         LR    R2,R3                    SAVE ADDRESS OF LEFT PAREN.
         SR    R2,R1                    COMPUT LENGTH OF DSN
         BCTR  R2,0                     DECREASE BY ONE
         EX    R2,MOVEDSN               BRANCH TO MOVE DSN
         TM    VOLPURGE,X'80'           TEST FOR VOL × PURGE SPEC.
         BO    VOLUME                   IF SECIFIED, BRANCH
         B     SCRATCH                  ELSE BRANCH TO ASK FOR SCRATCH
NOTPO    EQU   *
         TM    VOLPURGE,X'80'           TEST FOR VOL × PURGE SPEC.
         BO    NAME                     IF SPECIFIED, BRANCH
         MVC   DSNAME(44),TEXT+4        ELSE MOVE DSN
         B     SCRATCH                  BRANCH TO ASK FOR SCRATCH
NAME     EQU   *
         LA    R1,TEXT+4                LOAD ADDRESS OF DSN
         LR    R2,R5                    SAVE ADDRESS OF SECOND SEPERAT.
         SR    R2,R1                    COMPUTE LENGTH OF DSN
         BCTR  R2,0                     DECREASE BY ONE
         EX    R2,MOVEDSN               BRANCH TO MOVE DSN
VOLUME   EQU   *
         CLC   1(5,R5),=C'PURGE'        PURGE SPECIFIED ?
         BNE   VOLFIRST                 IF NOT, VOL FIRST SPECIFIED
         OI    PURGE,X'80'              SET FLAG FOR PURGE
         CLI   6(R5),C','               ANOTHER SEPARETOR ?
         BNE   SCRATCH                  IF NOT, BRANCH TO ASK FOR SCR.
         MVC   VOLSER(6),7(R5)          ELSE MOVE VOLSER
         OI    VOL,X'80'                SET FLAG FOR VOLSER
         B     SCRATCH                  BRANCH TO ASK FOR SCRATCH
VOLFIRST EQU   *
         LA    R2,TEXT+123              LOAD ADDRESS OF TEXTEND
         SR    R2,R5                    COMPUTE LENGTH OF REMAINDER
         BCTR  R2,0                     DECRAESE BY ONE
         MVI   KOMMA,C','               MOVE KOMMA TO TRANSLATE TABLE
         EX    R2,TRT                   BRANCH TO LOOK FOR NEXT SEP.
         BZ    VOLONLY                  IF NONE, BRANCH
         LR    R6,R1                    ELSE SAVE ADDRESS OF THIRD SEP.
         MVI   KOMMA,C' '               CLEAR TRANSLATE TABLE
         SR    R1,R5                    COMPUTE LENGTH OF VOLSER
         BCTR  R1,0                     DECREASE BY
         BCTR  R1,0                     TWO
         EX    R1,MOVEVOL               BRANCH TO MOVE VOLSER
         OI    VOL,X'80'                SET FLAG FOR VOLSER
         CLC   1(5,R6),=C'PURGE'        COMPARE REMAINDER WITH PURGE
         BNE   ERROR                    IF NOT, BRANCH ERROR
         OI    PURGE,X'80'              ELSE SET FLAG FOR PURGE
         B     SCRATCH                  BRANCH TO ASK FOR SCRATCH
VOLONLY  EQU   *
         MVC   VOLSER(6),1(R5)          MOVE VOLSER
         OI    VOL,X'80'                SET FLAG FOR VOLUME SPECIF.
         EJECT
**********************************************************************
*                                                                    *
*        ASK FOR SCRATCH.                                            *
*                                                                    *
**********************************************************************
         SPACE 2
SCRATCH  EQU   *
         MVC   WTO(LMSG05),MSG05        MOVE MESSAGE SKELETON
         LA    R2,ECB                   LOAD ADDRESS OF ECB
         LA    R3,REPLY                 LOAD ADDRESS OF REPLY
         L     R0,REG4                  LOAD UCM-ID.
         TM    PO,X'80'                 TEST FOR PO DATASET
         BO    MOVMEMB                  IF YES, BRANCH TO MEMEBERNAME
         MVC   WTO+71(44),DSNAME        ELSE MOVE DSN TO MESSAGE
         B     M1                       BRANCH AROUND MEMBERNAME
MOVMEMB  EQU   *
         MVC   WTO+71,=C' '             CLEAR MESSAGE
         MVC   WTO+72(43),WTO+70        AREA
         MVC   WTO+63(7),=C'MEMBER '    MOVE CONSTANT TO MSG
         MVC   WTO+70(8),MEMBER         MOVE MEMBERNAME TO MSG
M1       EQU   *
         XC    ECB,ECB                  CLEAR ECB
         WTOR  ,(R3),3,(R2),MCSFLAG=(REG4),MF=(E,WTO)
         WAIT  ECB=ECB                   WAIT FOR REPLY
         CLI   REPLY,C'N'                REPLY=NO?
         BE    UNCATLG                   IF NO,BRANCH TO ASK FOR UNCAT
         CLI   REPLY,C'Y'                REPLY=YES?
         BNE   M1                        IF NOT, BRANCH TO ASK AGAIN
         TM    VOL,X'80'                 ELSE TEST FOR VOLUME SPEC.
         BO    LOOKDEVT                  IF YES, BRANCH TO UCB PROC.
         EJECT
**********************************************************************
*                                                                    *
*        RETRIEVE CATALOG INFORMATION.                               *
*                                                                    *
**********************************************************************
         SPACE 2
         MVC   LOCLIST(16),LOCLISTX      MOVE LOCATELIST SKELETON
         LA    R2,DSNAME                 LOAD ADDRESS OF DSN
         LA    R3,LOCAREA                LOAD ADDRESS OF LOCATE AREA
         ST    R2,LOCLIST+4              STORE ADDRESS OF DSN TO LOCL.
         ST    R3,LOCLIST+12             STORE ADDR. OF LOCAREA TO LOC.
         LOCATE LOCLIST                  RETRIEVE CATALOG INF.
         LTR   R15,R15                   RETURNCODE=0 ?
         BZ    SCRATCHD                  IF YES, BRANCH TO SCRATCH
         CVD   R15,DOPPELW               CONVERT RC TO DECIMAL
         UNPK  RC,DOPPELW                UNPACK RC
         OI    RC+3,X'F0'                MODIFY SIGN
         MVC   WTO(LMSG06),MSG06         MOVE MESSAGE SKELETON
         MVC   WTO+40(2),RC+2            MOVE RC TO MSG
         MVC   WTO+47(44),DSNAME         MOVE DSN TO MSG
         L     R0,REG4                   MOVE UCM-ID.
         WTO   MF=(E,WTO)                EXECUTE MSG
         B     RETURN                    BRANCH RETURN
         EJECT
***********************************************************************
*                                                                     *
*        PROCESSING OF UCB LOOKUP-TABLE.                              *
*                                                                     *
***********************************************************************
         SPACE 2
LOOKDEVT EQU   *
*        L     R1,16                     LOAD CVT ADDRESS         /XUCB
*        L     R2,40(R1)                 LOAD UCB LOOKUP-TABLE ADD/XUCB
*        SR    R4,R4                     CLEAR R4                 /XUCB
         XUCBSCAN R2                     CREATE UCBSCAN WORK AREA /XUCB
LOOP     EQU   *
*        CLC   0(2,R2),=X'FFFF'          END OF UCB LOOKUP-TABLE ?/XUCB
*        BE    NODEV                     IF YES, NO DEVICE FOUND  /XUCB
*        ICM   R4,3,0(R2)                SAVE UCB ADDRESS         /XUCB
         XUCBGET  R2,NODEV,NODEV                                  /XUCB
         LR    R4,R1                     SAVE UCB ADDRESS         /XUCB
         CLC   28(6,R4),VOLSER           COMPARE VOLSER
         BE    MOVEDEV                   IF EQUAL, DEVICE FOUND
*        LA    R2,2(,R2)                 LOAD ADDRESS OF NEXT UCB-ADDR.
         B     LOOP                      BRANCH FOR NEXT UCB
NODEV    EQU   *
         XUCBOFF  R2                     FREE UCBSCAN WORK AREA   /XUCB
         MVC   WTO(LMSG07),MSG07          MOVE MESSAGE SKELETON
         MVC   WTO+19(6),VOLSER           MOVE VOLSER TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     UNCATLG                    BRANCH TO ASK FOR UNCATLG
MOVEDEV  EQU   *
         XUCBOFF  R2                     FREE UCBSCAN WORK AREA   /XUCB
         XC    LOCAREA(256),LOCAREA      CLEAR LOCATE AREA
         XC    LOCAREA+256(9),LOCAREA+256
         MVC   LOCAREA+6(6),VOLSER        MOVE VOLSER TO LOCATE AREA
         MVC   LOCAREA+2(4),16(R4)        MOVE DEVICETYPE TO LOC. AREA
         LA    R1,1(,0)                   LOAD VOLUME COUNT
         STH   R1,LOCAREA                 STORE VOLUME COUNT TO LOC. A.
         EJECT
**********************************************************************
*                                                                    *
*        SCRATCH PROCESSING FOR SEQUENTIAL DATASETS.                 *
*                                                                    *
**********************************************************************
         SPACE 2
SCRATCHD EQU   *
         TM    PO,X'80'                   DATASET PARTITIONED ?
         BO    SCRATCHM                   IF YES, BRANCH TO SCR. MEM.
         SR    R0,R0                      CLEAR R0
         TM    PURGE,X'80'                PURGE SPECIFIED ?
         BZ    NOPURGE                    IF NOT, SCR. WITHOUT EXPIRAT.
         MVC   DELISTP(16),DELISTXP       MOVE SCRATCHLIST SKELETON
         LA    R6,DSNAME                  LOAD ADDRESS OF DSN
         ST    R6,DELISTP+4               STORE ADDR.OF DSN TO LIST
         LA    R6,LOCAREA                 LOAD ADDRESS OF LOCATE AREA
         ST    R6,DELISTP+12              STORE ADDR. OF LOCAREA
         SCRATCH DELISTP                  SCRATCH DATASET
         LTR   R15,R15                    RETURNCODE=0 ?
         BNZ   SCRERROR                   IF NOT,BRANCH ERROR HANDLING
         B     PUTMSG8                    ELSE PUT MSG
NOPURGE  EQU   *
         MVC   DELIST(16),DELISTX         MOVE SCRATCHLIST SKELETON
         LA    R6,DSNAME                  LOAD ADDRESS OF DSN
         ST    R6,DELIST+4                STORE ADDRESS OF DSN TO LIST
         LA    R6,LOCAREA                 LOAD ADDR. OF LOCATE AREA
         ST    R6,DELIST+12               STORE ADDR. OF LOCATE AREA
         SCRATCH DELIST                   SCRATCH DATASET WITHOUT EXPI.
         LTR   R15,R15                    RETURNCODE=0 ?
         BNZ   SCRERROR                   IF NOT, BRANCH ERROR HANDLING
PUTMSG8  EQU   *
         MVC   WTO(LMSG08),MSG08          MOVE MESSAGE SKELETON
         MVC   WTO+35(44),DSNAME          MOVE DSN TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     UNCATLG                    BRANCH TO ASK FOR UNCATLG
SCRERROR EQU   *
         MVC   WTO(LMSG09),MSG09          MOVE MESSAGE SKELETON
         CVD   R15,DOPPELW                CONVERT TO DECIMAL
         UNPK  RC,DOPPELW                 UNPACK RETURNCODE
         OI    RC+3,X'F0'                 MODIFY SIGN
         MVC   WTO+23(2),RC+2             MOVE RETURNCODE TO MSG
         MVC   WTO+30(44),DSNAME          MOVE DSN TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         SR    R4,R4                      CLEAR R4
         LH    R4,LOCAREA                 LOAD VOLUME COUNT
         LA    R2,LOCAREA+2               LOAD ADDRESS DEVICETYPE
VOLLOOP  EQU   *
         CLC   11(1,R2),=X'00'            SCRATCHCODE=0 ?
         BNE   PUTMSG10                   IF NOT, PUT MESSAGE
         MVC   WTO(LMSG15),MSG15          MOVE  MESSAGE SKELETON
         MVC   WTO+35(6),4(R2)            MOVE VOLSER TO MSG
         MVC   WTO+41(2),=C'  '
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     CONTINUE                   BRANCH NEXT VOLUME
PUTMSG10 EQU   *
         MVC   WTO(LMSG10),MSG10          MOVE MESSAGE SKELETON
         SR    R6,R6                      CLEAR R6
         IC    R6,11(R2)                  SAVE SCRATCHCODE
         CVD   R6,DOPPELW                 CONVERT SCRATCHCODE TO DEC.
         UNPK  RC+3(1),DOPPELW            UNPACK SCRATCHCODE
         OI    RC+3,X'F0'                 MODIFY SIGN
         MVC   WTO+25(1),RC+3             MOVE SCRATCHCODE TO MSG
         MVC   WTO+31(6),4(R2)            MOVE VOLSER TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
CONTINUE EQU   *
         LA    R2,12(R2)                  LOAD ADDR. NEXT LOCAREA ENTRY
         BCT   R4,VOLLOOP                 DECREASE R4, BRANCH IF R4^=0
         B     UNCATLG                    BRANCH TO ASK FOR UNCATLG
         EJECT
**********************************************************************
*                                                                    *
*        SCRATCH PROCESSING FOR MEMBERS.                             *
*                                                                    *
**********************************************************************
         SPACE 2
*        DYNALLOC
SCRATCHM EQU   *
         LA    R1,DYNAREA                 LOAD ADDRESS OF DYNAREA
         LA    R2,S99RB                   LOAD ADDRESS OF REQUEST BLOCK
         ST    R2,S99RBPTR                STORE RB-ADDR. TO RBPTR
         OI    S99RBPTR,X'80'             MODIFY HIGH ORDER BYTE
         XC    S99RB(RBLEN),S99RB         CLEAR REQUEST BLOCK
         MVI   S99RBLN,RBLEN              MOVE LENGTH TO RB
         MVI   S99VERB,X'01'              SET FLAG BYTE
         MVI   S99FLG11,X'00'             SET FLAG BYTE
         MVI   S99FLG12,X'00'             SET FLAG BYTE
         LA    R2,S99TUPT1                LOAD ADDRESS FIRST TXTUNITPTR
         ST    R2,S99TXTPP                STORE ADDRESS TO RB
         MVI   S99FLG21,X'D9'             SET FLAG BYTE
         MVI   S99FLG22,X'00'             SET FLAG BYTE
         MVI   S99FLG23,X'00'             SET FLAG BYTE
         MVI   S99FLG24,X'00'             SET FLAG BYTE
         LA    R3,S99TUKE1                LOAD ADDRESS FIRST TEXTUNIT
         ST    R3,S99TUPT1                STORE ADDRESS TO TXTUNITPTR
         LA    R4,4(,0)                   GET THE STATUS KEY
         STH   R4,S99TUKE1                STORE KEY TO TEXTUNIT
         LA    R4,1(,0)                   GET NUMBER AND LENGTH OF
         STH   R4,S99TUNU1                PARAMETER AND STORE IT TO
         STH   R4,S99TULN1                THE TEXTUNIT
         MVI   S99TUPA1,X'01'             MOVE PARAMETER
         LA    R3,S99TUKE2                LOAD ADDRESS OF NEXT TEXTUNIT
         ST    R3,S99TUPT2                STORE ADDR. TO NEXT TXTUNITPT
         LA    R4,5(,0)                   GET THE KEY FOR NORMAL DISP.
         STH   R4,S99TUKE2                STORE KEY TO TEXTUNIT
         LA    R4,1(,0)                   GET NUMBER AND LENGTH OF
         STH   R4,S99TUNU2                PARAMETER AND STORE IT TO
         STH   R4,S99TULN2                THE TEXTUNIT
         MVI   S99TUPA2,X'08'             MOVE PARAMETER TO TEXTUNIT
         LA    R3,S99TUKE3                LOAD ADDRESS OF NEXT TEXTUNIT
         ST    R3,S99TUPT3                STORE ADDR. TO NEXT TXTUNITPT
         LA    R4,6(,0)                   GET THE KEY FOR COND. DISP.
         STH   R4,S99TUKE3                STORE THE KEY TO TEXTUNIT
         LA    R4,1(,0)                   GET NUMBER AND LENGTH OF
         STH   R4,S99TUNU3                PARAMETER AND STORE IT TO
         STH   R4,S99TULN3                THE TEXTUNIT
         MVI   S99TUPA3,X'08'             MOVE PARAMETER TO TEXTUNIT
         LA    R3,S99TUKE4                LOAD ADDRESS OF NEXT TEXTUNIT
         ST    R3,S99TUPT4                STORE ADDR. TO NEXT TXTUNITPT
         MVC   S99TUKE4,=X'001C'          STORE THE KEY TO TEXTUNIT
         LA    R4,0(,0)                   GET NUMBER OF PARAMETER
         STH   R4,S99TUNU4                AND STORE IT TO TEXTUNIT
         LA    R3,S99TUKE5                LOAD ADDRESS OF NEXT TEXTUNIT
         ST    R3,S99TUPT5                STORE ADDR. TO NEXT TXTUNITPT
         MVC   S99TUKE5,=X'0055'          STORE KEY TO TEXTUNIT
         LA    R4,1(0)                    GET NUMBER OF PARAMETER
         STH   R4,S99TUNU5                AND STORE IT TO TEXTUNIT
         LA    R4,8(0)                    GET LENGTH OF PARAMETER
         STH   R4,S99TULN5                AND STORE IT TO TEXTUNIT
         MVC   S99TUPA5(8),=CL8' '        MOVE PARAMETER TO TEXTUNIT
         LA    R3,S99TUKE6                LOAD ADDRESS OF NEXT TEXTUNIT
         ST    R3,S99TUPT6                STORE ADDR. TO NEXT TXTUNITPT
         LA    R4,2(,0)                   GET KEY FOR DSNAME ALLOCATION
         STH   R4,S99TUKE6                STORE KEY TO TEXTUNIT
         LA    R4,1(,0)                   GET NUMBER OF PARAMETER
         STH   R4,S99TUNU6                AND STORE IT TO TEXTUNIT
         LA    R4,44(,0)                  GET LENGTH OF PARAMETER
         STH   R4,S99TULN6                AND STORE IT TO TEXTUNIT
         MVC   S99TUPA6(44),DSNAME        MOVE PARAMETER TO TEXTUNIT
         LA    R3,S99TUKE7                LOAD ADDRESS OF NEXT TEXTUNIT
         ST    R3,S99TUPT7                STORE ADDR. TO NEXT TXTUNITPT
         MVC   S99TUKE7,=X'0010'          STORE KEY TO TEXTUNIT
         LA    R4,1(,0)                   GET NUMBER OF PARAMETER
         STH   R4,S99TUNU7                AND STORE IT TEXTUNIT
         LA    R4,6(,0)                   GET LENGTH OF PARAMETER
         STH   R4,S99TULN7                STORE IT TO TEXTUNIT
         MVC   S99TUPA7(6),LOCAREA+6      MOVE PARAMETER TO TEXTUNIT
         LA    R3,S99TUKE8                LOAD ADDRESS OF NEXT TEXTUNIT
         ST    R3,S99TUPT8                STORE ADDR. TO NEXT TXTUNITPT
         MVC   S99TUKE8,=X'0015'          STORE KEY TO TEXTUNIT
         LA    R4,1(,0)                   GET NUMBER OF PARAMETER
         STH   R4,S99TUNU8                AND STORE IT TO TEXTUNIT
         LA    R4,8(,0)                   GET LENGTH OF PARAMETER
         STH   R4,S99TULN8                AND STORE IT TO TEXTUNIT
         LA    R5,DEVTAB                  LOAD ADDR. DEV.TABLE(EBCDIC)
DEVLOOP  EQU   *
         CLC   0(2,R5),=X'FFFF'           END OF DEV.TABLE(EBCDIC) ?
         BE    PUTMSG11                   IF YES, BRANCH TO PUT MSG
         CLC   0(2,R5),LOCAREA+4          IF DEV.TYPE IN DEV.TABLE
         BE    MOVUNIDS                   (EBCDIC), BRANCH TO MOVE TYPE
         LA    R5,12(,R5)                 LOAD ADDR. NEXT TABLE ENTRY
         B     DEVLOOP                    BRANCH TO LOOK AGAIN
PUTMSG11 EQU   *
         MVC   WTO(LMSG11),MSG11          MOVE MESSAGE SKELETON
         MVC   WTO+47(44),DSNAME          MOVE DSN TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     RETURN                     BRANCH TO RETURN
MOVUNIDS EQU   *
         TM    LOCAREA+3,X'08'            VIRTUEL DEVICE ?
         BNO   REAL                       IF NOT, BRANCH TO MOVE REAL D
         MVC   S99TUPA8(10),=C'3330V     '   MOVE VIRTUEL DEVICE TYPE
         OI    S99TUPT8,X'80'             SET IND. FOR LAST TEXTUNITPTR
         B     ALLOC                      BRANCH ALLOCATION
REAL     EQU   *
         MVC   S99TUPA8(10),2(R5)         MOVE REAL DEV.TYPE TO TEXTUN.
         OI    S99TUPT8,X'80'             SET IND. FOR LAST TEXTUNITPTR
ALLOC    EQU   *
         DYNALLOC                         DYNAMIC ALLOCATION
         LTR   R15,R15                    RETURNCODE = 0 ?
         BZ    OBTAIN                     IF YES, LOOK FOR DSCB1
PUTMSG12 EQU   *
         CVD   R15,DOPPELW                CONVERT RC TO DECIMAL
         UNPK  RC,DOPPELW                 UNPACK RC
         OI    RC+3,X'F0'                 MODIFY SIGN
         MVC   WTO(LMSG12),MSG12          MOVE MESSAGE SKELETON
         MVC   WTO+24(2),RC+2             MOVE RC TO MSG
         LA    R4,S99RB                   LOAD ADDR. OF REQUEST BLOCK
         UNPK  DOPPELW,4(3,R4)            UNPACK ERROR REASON CODE
         TR    DOPPELW+3(4),TRTAB         TRANSLATE  RIGHT PORTION
         MVC   WTO+40(4),DOPPELW+3        MOVE ERROR REASON CODE TO MSG
         UNPK  DOPPELW,6(3,R4)            UNPACK INFORMATIONAL CODE
         TR    DOPPELW+3(4),TRTAB         TRANSLATE RIGHT PORTION
         MVC   WTO+31(4),DOPPELW+3        MOVE INF. CODE TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     RETURN                     BRANCH TO RETURN
*
*    LOOK FOR DSCB1
*
OBTAIN   EQU   *
         MVC   DSCB(16),DSCBX             MOVE CAMLIST SKELETON
         LA    R2,DSNAME                  LOAD ADDR. OF DSNAME
         ST    R2,DSCB+4                  STORE ADDR. TO DSCB
         LA    R2,LOCAREA+6               LOAD ADDR. OF VOLSER
         ST    R2,DSCB+8                  STORE ADDR. TO DSCB
         LA    R2,DSCBAREA                LOAD ADDR. OF AREA
         ST    R2,DSCB+12                 STORE ADDR. TO DSCB
         OBTAIN DSCB                      LOOK FOR DSCB1
         LTR   R15,R15                    RETURNCODE = 0 ?
         BZ    OPEN                       IF YES, BRANCH TO OPEN DATAS.
PUTMSG16 EQU   *
         MVC   WTO(LMSG16),MSG16          MOVE MESSAGE SKELETON
         MVC   WTO+29(44),DSNAME          MOVE DSN TO MSG
         CVD   R15,DOPPELW                CONVERT RC TO DECIMAL
         UNPK  RC,DOPPELW                 UNPACK RC
         OI    RC+3,X'F0'                 MODIFY SIGN
         MVC   WTO+22(2),RC+2             MOVE RC TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     UNCATLG                    BRANCH TO ASK FOR UNCATLG
*
*   OPEN DATASET
*
OPEN     EQU   *
         MVC   DCB(DCBLEN),DCBX           MOVE DCB SKELETON
         MVC   DCB+40(8),S99TUPA5         MOVE DDNAME TO DCB
         LA    R4,DCB                     LOAD ADDRESS OF DCB
         ST    R4,DCBADDR                 STORE ADDRESS
         LA    R1,DCBADDR                 LOAD ADDR. OF DCBADDR.
         MVI   0(R1),X'8F'                SET INDICATOR
         SVC   19                         OPEN
         TM    DCB+48,X'10'               OPEN OK ?
         BO    STOW                       IF YES, BRANCH TO DEL. MEMBER
PUTMSG13 EQU   *
         MVC   WTO(LMSG13),MSG13          MOVE MESSAGE SKELETON
         MVC   WTO+28(44),DSNAME          MOVE DSN TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     RETURN                     BRANCH TO RETURN
*
*   DELETE THE MEMBER
*
STOW     EQU   *
         LA    R4,DCB                     LOAD ADDRESS OF DCB
         LA    R6,MEMBER                  LOAD ADDRESS OF  MEMBER
         STOW  (R4),(R6),D                STOW
         LTR   R15,R15                    RETURNCODE = 0 ?
         BNZ   PUTMSG14                   IF NOT, BRANCH TO PUT MSG
         MVC   WTO(LMSG15),MSG15          MOVE MESSAGE SKELETON
         MVC   WTO+35(8),MEMBER           MOVE MEMBER TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         LA    R1,DCBADDR                 LOAD ADDRESS  OF DCBADDR
         MVI   0(R1),X'80'                SET INDICATOR
         SVC   20                         CLOSE
         B     RETURN                     BRANCH TO RETURN
PUTMSG14 EQU   *
         MVC   WTO(LMSG14),MSG14          MOVE MESSEAGE SKELETON
         ST    R15,RC                     STORE R15 TO RETURNCODE
         UNPK  DOPPELW,RC+3(2)            UNPACK RC
         TR    DOPPELW+5(2),TRTAB         TRANSLATE RIGHT PORTION
         MVC   WTO+27(2),DOPPELW+5        MOVE RC TO MSG
         ST    R0,RC                      ST R0 TO RETURNCODE
         UNPK  DOPPELW,RC+3(2)            UNPACK RC
         TR    DOPPELW+5(2),TRTAB         TRANSLATE RIGHT PORTION
         MVC   WTO+38(2),DOPPELW+5        MOVE RC TO MSG
         MVC   WTO+45(8),MEMBER           MOVE MEMBERNAME TO MSG
         L     R0,REG4                    MOVE UCM-ID. TO MSG
         WTO   MF=(E,WTO)                 EXECUTE MSG
         LA    R1,DCBADDR                 LOAD ADDR. OF DCBADDR.
         MVI   0(R1),X'80'                SET INDICATOR
         SVC   20                         CLOSE
         B     RETURN                     BRANCH TO RETURN
         EJECT
***********************************************************************
*                                                                     *
*        ASK FOR UNCATLG.                                             *
*                                                                     *
***********************************************************************
         SPACE 2
UNCATLG  EQU   *
         TM    PO,X'80'                   PO DATASET ?
         BO    RETURN                     IF YES, BRANCH TO RETURN
         MVC   WTO(LMSG02),MSG02          MOVE MESSAGE SKELETON
         MVC   WTO+71(44),DSNAME          MOVE DSN TO MSG
WTOUNCAT EQU   *
         XC    ECB,ECB                    CLEAR ECB
         XC    REPLY,REPLY                CLEAR REPLY
         LA    R2,ECB                     LOAD ADDRESS OF ECB
         LA    R3,REPLY                   LOAD ADDRESS OF REPLY
         L     R0,REG4                    LOAD UCM-ID.
         WTOR  ,(R3),3,(R2),MF=(E,WTO)    EXECUTE MSG
         WAIT  ECB=ECB                    WAIT FOR REPLY
         CLI   REPLY,C'N'                 REPLY = NO ?
         BE    RETURN                     BRANCH TO RETURN
         CLI   REPLY,C'Y'                 REPLY = YES ?
         BNE   WTOUNCAT                   IF NOT, ASK AGAIN
         EJECT
***********************************************************************
*                                                                     *
*        UNCATLG PROCESSING.                                          *
*                                                                     *
***********************************************************************
         SPACE 2
         MVC   CATLIST(12),UNCATX         MOVE UNCAT LIST
         LA    R2,DSNAME                  LOAD ADDRESS OF DSN
         ST    R2,CATLIST+4               STORE IT TO UNCAT LIST
         CATALOG CATLIST                  UNCATLG
         LTR   R15,R15                    RETURNCODE = 0 ?
         BZ    UNCATOK                    IF YES, BRANCH TO UNCATLG
         MVC   WTO(LMSG03),MSG03          MOVE MESSAGE SKELETON
         MVC   WTO+30(44),DSNAME          MOVE DSN TO MSG
         CVD   R15,DOPPELW                CONVERT RC TO DECIMAL
         UNPK  RC,DOPPELW                 UNPACK RC
         OI    RC+3,X'F0'                 MODIFY SIGN
         MVC   WTO+23(2),RC+2             MOVE RC TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     RETURN                     BRANCH TO RETURN
UNCATOK  EQU   *
         MVC   WTO(LMSG04),MSG04          MOVE MESSAGE SKELETON
         MVC   WTO+35(44),DSNAME          MOVE DSN TO MSG
         L     R0,REG4                    MOVE UCM-ID.
         WTO   MF=(E,WTO)                 EXECUTE MSG
         B     RETURN                     BRANCH TO RETURN
         EJECT
*
*   ERROR PROCESSING.
*
ERROR    EQU   *
         L     R0,REG4
         WTO   'XDEL001 SYNTAX ERROR IN PARMLIST',MCSFLAG=(REG0)
*
*   FREEMAIN WORKAREA AND RETURN
*
RETURN   EQU   *
         XRETURN
         EJECT
***********************************************************************
*                                                                     *
*        MESSAGE SKELETONS.                                           *
*                                                                     *
***********************************************************************
         SPACE 2
MSG03    WTO   'XDEL003 UNCATLG RC=99 FOR XXXXXXXXXXXXXXXXXXXXXXXXXXXXX*
               XXXXXXXXXXXXXXX',MCSFLAG=(REG0),MF=L
LMSG03   EQU   *-MSG03
MSG04    WTO   'XDEL004 UNCATLG SUCCESSFUL FOR XXXXXXXXXXXXXXXXXXXXXXXX*
               XXXXXXXXXXXXXXXXXXXX',MCSFLAG=(REG0),MF=L
LMSG04   EQU   *-MSG04
MSG02    WTOR  'XDEL002 REPLY ''Y'' (YES) OR ''N'' (NO) TO UNCATLG THE *
               DATASET XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX',  *
               MCSFLAG=(REG0),MF=L
LMSG02   EQU   *-MSG02
MSG05    WTOR  'XDEL005 REPLY ''Y'' (YES) OR ''N'' (NO) TO SCRATCH THE *
               DATASET XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX',  *
               MCSFLAG=(REG0),MF=L
LMSG05   EQU   *-MSG05
MSG06    WTO   'XDEL006 CATALOG FAILURE (LOCATE) RC=99 FOR XXXXXXXXXXXX*
               XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX',MCSFLAG=(REG0),MF=L
LMSG06   EQU   *-MSG06
MSG07    WTO   'XDEL007 VOLUME XXXXXX NOT AVAILABLE',MCSFLAG=(REG0),   *
               MF=L
LMSG07   EQU   *-MSG07
MSG08    WTO   'XDEL008 SCRATCH SUCCESSFUL FOR XXXXXXXXXXXXXXXXXXXXXXXX*
               XXXXXXXXXXXXXXXXXXXX',MCSFLAG=(REG0),MF=L
LMSG08   EQU   *-MSG08
MSG09    WTO   'XDEL009 SCRATCH RC=99 FOR XXXXXXXXXXXXXXXXXXXXXXXXXXXXX*
               XXXXXXXXXXXXXXX',MCSFLAG=(REG0),MF=L
LMSG09   EQU   *-MSG09
MSG10    WTO   'XDEL010 SCRATCH CODE=X FOR VOLSER',                    *
               MCSFLAG=(REG0),MF=L
LMSG10   EQU   *-MSG10
MSG11    WTO   'XDEL011 DEVICETYPE NOT IN PROGRAMTABLE FOR XXXXXXXXXXXX*
               XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX',MCSFLAG=(REG0),MF=L
LMSG11   EQU   *-MSG11
MSG12    WTO   'XDEL012 DYNALLOC RC=99, IC=9999, EC=9999',             *
               MCSFLAG=(REG0),MF=L
LMSG12   EQU   *-MSG12
MSG13    WTO   'XDEL013 OPEN FAILED FOR XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX*
               XXXXXXXXXXXXX',MCSFLAG=(REG0),MF=L
LMSG13   EQU   *-MSG13
MSG14    WTO   'XDEL014 STOW FAILED RC=99, REASON=99 FOR XXXXXXXX',    *
               MCSFLAG=(REG0),MF=L
LMSG14   EQU   *-MSG14
MSG15    WTO   'XDEL015 SCRATCH SUCCESSFUL FOR XXXXXXXX',              *
               MCSFLAG=(REG0),MF=L
LMSG15   EQU   *-MSG15
MSG16    WTO   'XDEL016 OBTAIN RC=99 FOR XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX*
               XXXXXXXXXXXXX',MCSFLAG=(REG0),MF=L
LMSG16   EQU   *-MSG16
         EJECT
***********************************************************************
*                                                                     *
*        DECLARATIONS.                                                *
*                                                                     *
***********************************************************************
         SPACE 2
DEVTAB   DC    X'2005'
         DC    C'2305-1    '
         DC    X'2007'
         DC    C'2305-2    '
         DC    X'200E'
         DC    C'3380      '
         DC    X'2009'
         DC    C'3330      '
         DC    X'200A'
         DC    C'3340      '
         DC    X'200B'
         DC    C'3350      '
         DC    X'200D'
         DC    C'3330-11   '
         DC    X'FFFF'
         DC    C'XXXXXXXXXX'
TRTAB    DC    240C'?'
         DC    C'0123456789ABCDEF'
DCBX     DCB   DSORG=PO,MACRF=(W),DDNAME=SYSXXXXX
DCBLEN   EQU   *-DCBX
LOCLISTX CAMLST NAME,0,,0
DELISTXP CAMLST SCRATCH,0,,0,,OVRD
DELISTX  CAMLST SCRATCH,0,,0
UNCATX   CAMLST UNCAT,0
DSCBX    CAMLST SEARCH,0,0,0
         EJECT
***********************************************************************
*                                                                     *
*        COMMANDS FOR EXECUTE STATEMENTS.                             *
*                                                                     *
***********************************************************************
         SPACE 2
MOVEMEM  MVC   MEMBER(0),1(R3)
MOVEDSN  MVC   DSNAME(0),0(R1)
TRT      TRT   1(0,R5),TRTTAB
MOVEVOL  MVC   VOLSER(0),1(R5)
         EJECT
***********************************************************************
*                                                                     *
*        EQUATES.                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
R0       EQU   0
R00      EQU   0
R1       EQU   1
R01      EQU   1
R2       EQU   2
R02      EQU   2
R3       EQU   3
R03      EQU   3
R4       EQU   4
R04      EQU   4
R5       EQU   5
R05      EQU   5
R6       EQU   6
R06      EQU   6
R7       EQU   7
R07      EQU   7
R8       EQU   8
R08      EQU   8
R9       EQU   9
R09      EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RBLEN    EQU   20
         EJECT
***********************************************************************
*                                                                     *
*        PARMAREA.                                                    *
*                                                                     *
***********************************************************************
         SPACE 2
         XPARM
         EJECT
***********************************************************************
*                                                                     *
*        WORKAREA.                                                    *
*                                                                     *
***********************************************************************
         SPACE 2
WORK     DSECT
SAVEAREA DS    18F'0'
TRTTAB   DS    77C
LPAN     DS    C
         DS    15C
RPAN     DS    C
         DS    13C
KOMMA    DS    C
         DS    148C
PO       DS    C
         DS    0F
MEMLENG  DS    F
MEMBER   DS    CL8
CATLIST  DS    CL12
DSNAME   DS    CL44
VOLSER   DS    CL6
VOL      DS    C
PURGE    DS    C
VOLPURGE DS    C
WTO      DS    34F
ECB      DS    F
REPLY    DS    CL3
DOPPELW  DS    0D
         DS    2F
RC       DS    F
LOCAREA  DS    0D
         DS    265C
LOCLIST  DS    0F
         DS    CL16
DELISTP  DS    0F
         DS    CL16
DELIST   DS    0F
         DS    CL16
DCB      DS    CL96
DYNAREA  DS    0F
S99RBPTR DS    F
S99RB    DS    0F
S99RBLN  DS    CL1
S99VERB  DS    CL1
S99FLG11 DS    CL1
S99FLG12 DS    CL1
S99ERROR DS    H
S99INFO  DS    H
S99TXTPP DS    F
S99RSV01 DS    F
S99FLG21 DS    CL1
S99FLG22 DS    CL1
S99FLG23 DS    CL1
S99FLG24 DS    CL1
S99TUPT1 DS    F
S99TUPT2 DS    F
S99TUPT3 DS    F
S99TUPT4 DS    F
S99TUPT5 DS    F
S99TUPT6 DS    F
S99TUPT7 DS    F
S99TUPT8 DS    F
S99TUKE1 DS    H
S99TUNU1 DS    H
S99TULN1 DS    H
S99TUPA1 DS    CL1
S99TUKE2 DS    H
S99TUNU2 DS    H
S99TULN2 DS    H
S99TUPA2 DS    CL1
S99TUKE3 DS    H
S99TUNU3 DS    H
S99TULN3 DS    H
S99TUPA3 DS    CL1
S99TUKE4 DS    H
S99TUNU4 DS    H
S99TUKE5 DS    H
S99TUNU5 DS    H
S99TULN5 DS    H
S99TUPA5 DS    CL8
S99TUKE6 DS    H
S99TUNU6 DS    H
S99TULN6 DS    H
S99TUPA6 DS    CL44
S99TUKE7 DS    H
S99TUNU7 DS    H
S99TULN7 DS    H
S99TUPA7 DS    CL6
S99TUKE8 DS    H
S99TUNU8 DS    H
S99TULN8 DS    H
S99TUPA8 DS    CL8
DCBADDR  DS    F
DSCB     DS    CL16
DSCBAREA DS    CL140
WORKLEN  EQU   *-WORK
         CVT   DSECT=YES                                          /XUCB
         SPACE 3
         END   XDEL
./ ADD NAME=DEVTAB
DEVTAB   DC    X'32108003'
         DC    C'3420-66'
         DC    X'32108003'
         DC    C'3420  4'
         DC    X'32108003'
         DC    C'3400-66'
         DC    X'32108003'
         DC    C'3400  4'
         DC    X'3010200E'
         DC    C'3380  4'
         DC    X'30702009'
         DC    C'3330  4'
         DC    X'3050200D'
         DC    C'3330-16'
         DC    X'3070200D'
         DC    C'3330-16'
         DC    X'3050200A'
         DC    C'3340  4'
         DC    X'3070200A'
         DC    C'3340  4'
         DC    X'3050200B'
         DC    C'3350  4'
         DC    X'3070200B'
         DC    C'3350  4'
DEVEND   EQU   *
./ ADD NAME=DUMP
DUMP     CSECT
         XSAVE R12,,DUMP,WORKL
         USING WORKAREA,R13
         LR    R11,R1
         USING PARMAREA,R11
         SPACE 3
* SCAN THE ENTERED COMMAND
         MVI   SCANINF,C'S'        SET 'START OF INPUT SCAN'
         MVI   ADDRTYPE,C'V'       SET ADDRESS TYPE TO 'VIRTUAL'
*
         LA    R2,TEXT+5           SET UP ADDRESS OF FIRST BYTE OF
*                                     EXPECTED ADDRESS
ADDRLPS  LA    R0,8                MAX. NUMBER OF CHARACTERS
         SR    R10,R10             R10 IS TO CONTAIN THE GIVEN ADDRESS
*
ADDRLP   CLI   0(R2),C'A'          EXAMINE EACH CHARACTER
         BL    ADDRLPE             BR IF < 'A'
         SLL   R10,4               SHIFT 4 BITS TO LEFT
         IC    R15,0(,R2)          INSERT THE CURRENT CHARACTER
         N     R15,=F'15'          CLEAR ALL BUT THE LAST FOUR BITS
         CLI   0(R2),C'F'          HEXADEC. CHARACTER?
         BH    ADDRLP1             BR IF > 'F'
         LA    R15,9(,R15)         C'A' -> X'A'
         B     ADDRLP2             GO ON
ADDRLP1  CLI   0(R2),C'0'          HEXADEC. CHARACTER?
         BL    PARMERR             BR IF NO
         CLI   0(R2),C'9'          HEXADEC. CHARACTER?
         BH    PARMERR             BR IF NO
ADDRLP2  OR    R10,R15             INSERT NEXT 4 BITS OF ADDRESS
         LA    R2,1(,R2)           ADDRESS OF NEXT CHARACTER
         BCT   R0,ADDRLP           BCT
*
ADDRLPE  CLI   SCANINF,C'S'        START OF INPUT SCAN?
         BE    DIRADDR             BR IF YES
         CLI   SCANINF,C'+'        ADDRESS TO BE MODIFIED?
         BE    ADDADDR             BR IF YES
         CLI   SCANINF,C'-'        ADDRESS TO BE MODIFIED?
         BE    SUBADDR             BR IF YES
         B     PARMERR             BR
DIRADDR  CLI   0(R2),C','          DELIMITER?
         BE    SAVEADDR            BR IF YES
         CLI   0(R2),C' '          END OF INPUT?
         BNE   TESTINDA            BR IF NO
         MVI   SCANINF,C'E'        SET 'ALL INPUT SCANNED'
         B     SAVEADDR            BR
* TEST FOR INDIRECT ADDRESS
TESTINDA DS    0H
         CLI   0(R2),C'%'          INDIRECT ADDRESS?
         BE    TESTINDB            BR IF NO
         CLI   0(R2),C'?'          INDIRECT ADDRESS?
         BNE   TESTMODA            BR IF NO
TESTINDB LA    R0,3                TEST FOR
         NR    R0,R10                FULLWORD
         BNZ   INDAERR                 BOUNDARY
*
         MODESET KEY=ZERO,MODE=SUP KEY ZERO, SUPERVISOR STATE
         LR    R4,R10              LOAD ADDRESS TO BE TESTED
         BAL   R6,ADDRTEST         GO TEST ADDRESS AVAILABILITY
         CLI   0(R2),C'?'          31-BIT-MODE
         BE    XA1
         CL    R10,=XL4'00FFFFFF'
         BNH   NO31XA
XA1      XMODE31
NO31XA   LA    R10,0(R10)          CLEAR BIT OR BYTE
         L     R10,0(,R10)         LOAD ADDRESS OF ADDRESS
         LA    R10,0(R10)
         XMODE24
         MODESET KEY=NZERO,MODE=PROB  OLD KEY, PROBLEM STATE
*
         LA    R2,1(,R2)           ADDRESS OF NEXT BYTE
         B     ADDRLPE             BR
* TEST FOR ADDRESS TO BE MODIFIED
TESTMODA ST    R10,PARMADDR        STORE ADDRESS
         NI    PARMADDR,127        CLEAR MINUS BIT
         CLI   0(R2),C'+'          ADDRESS TO BE MODIFIED?
         BE    ADDRADD             BR IF YES
         CLI   0(R2),C'-'          ADDRESS TO BE MODIFIED?
         BNE   PARMERR             BR IF NO
         MVI   SCANINF,C'-'        SET INDICATOR
         LA    R2,1(,R2)           ADDRESS OF NEXT BYTE
         B     ADDRLPS             BR
ADDRADD  MVI   SCANINF,C'+'        SET INDICATOR
         LA    R2,1(,R2)           ADDRESS OF NEXT BYTE
         B     ADDRLPS             BR
*
ADDADDR  A     R10,PARMADDR        ADD TO GIVEN ADDRESS
         MVI   SCANINF,C'S'        SET INDICATOR
         B     DIRADDR             BR
SUBADDR  L     R15,PARMADDR        SUBTRACT
         SR    R15,R10               FROM
         LR    R10,R15                 GIVEN ADDRESS
         MVI   SCANINF,C'S'        SET INDICATOR
         B     DIRADDR             BR
*
SAVEADDR DS    0H
         CL    R10,=XL4'00FFFFFF'  31-BIT-ADDR
         BNH   NON31A1
         XMODE31
NON31A1  DS    0H
         LA    R10,0(R10)          CLEAR BIT OR BYTE
         XMODE24
         ST    R10,PARMADDR        SAVE GIVEN ADDRESS,
         SRL   R10,4                 GET THE NEXT LOWER MULTIPLE
         SLL   R10,4                   OF 16
         ST    R10,DUMPADDR        AND SAVE IT
*
         EJECT
* LOOK FOR NUMBER OF FULLWORDS TO BE DUMPED
         LA    R0,4                DEFAULT NUMBER OF BYTES
         ST    R0,PARMNMBR                                *
         CLI   SCANINF,C'E'        ALL INPUT SCANNED?
         BE    TESTNMBR            BR IF YES
SKIPZERO LA    R2,1(,R2)           ADDRESS OF NEXT CHARACTER
         CLI   0(R2),C'0'          SKIP LEADING ZEROES
         BE    SKIPZERO              -
         BL    TESTNMBR            BR IF CHARACTER < '0'
         CLI   1(R2),C','          1 BYTE OF LENGTH INFORMATION?
         BE    PACK1               BR IF YES
         CLI   1(R2),C' '          1 BYTE OF LENGTH INFORMATION?
         BNE   TEST2               BR IF NO
         MVI   SCANINF,C'E'        SET 'ALL INPUT SCANNED'
         B     PACK1
TEST2    CLI   1(R2),C'0'          NUMERIC CHARACTER?
         BL    PARMERR             BR IF NO
         CLI   2(R2),C','          2 BYTES OF LENGTH INFORMATION?
         BE    PACK2               BR IF YES
         CLI   2(R2),C' '          2 BYTES OF LENGTH INFORMATION?
         BNE   PARMERR             BR IF NO
         MVI   SCANINF,C'E'        SET 'ALL INPUT SCANNED'
         B     PACK2
PACK1    PACK  DBLWD,0(1,R2)       PACK ONE DIGIT
         LA    R2,2(,R2)           ADDRESS OF NEXT BYTE
         B     CVBNMBR             GO ON
PACK2    PACK  DBLWD,0(2,R2)       PACK TWO DIGITS
         LA    R2,3(,R2)           ADDRESS OF NEXT BYTE
         B     CVBNMBR             GO ON
*
CVBNMBR  CVB   R0,DBLWD            CONVERT TO BINARY
         LA    R15,40              MAX. VALUE
         CR    R0,R15              COMPARE TO MAX
         BNH   *+6                 IF > MAX :
         LR    R0,R15                SET TO MAX
         SLA   R0,2                MULTIPLY BY 4 -> NUMBER OF BYTES
         ST    R0,PARMNMBR         SAVE THIS NUMBER
*
TESTNMBR TM    PARMADDR+3,X'03'    TEST FOR ALIGNMENT
         BZ    NEXTPARM            FULLWORD - OK
         L     R0,PARMNMBR         LOAD NUMBER OF BYTES TO BE DUMPED
         BO    SUB3NMBR            RIGHTMOST BYTE - SUBTRACT 3
         TM    PARMADDR+3,X'02'    HALFWORD ALIGNMENT?
         BO    SUB2NMBR            YES - SUBTRACT 2
         CLI   PARMNMBR+3,4        IS ONE WORD TO BE DUMPED?
         BNE   SUB1NMBR            BR IF NO
SUB3NMBR BCTR  R0,0                SUBTRACT 1
SUB2NMBR BCTR  R0,0                SUBTRACT 1
SUB1NMBR BCTR  R0,0                SUBTRACT 1
         ST    R0,PARMNMBR         STORE NUMBER OF BYTES TO BE DUMPED
*
         EJECT
* LOOK FOR MORE PARAMETER
NEXTPARM CLI   SCANINF,C'E'        ALL INPUT SCANNED?
         BE    GODUMP              BR IF YES
*
         CLC   0(4,R2),=C'REAL'    ARE REAL ADDRESSES TO BE GIVEN?
         BNE   *+8                 BR IF NO
         MVI   ADDRTYPE,C'R'       SET INDICATOR
*
*
         EJECT
GODUMP   EQU   *
* DUMP THE SPECIFIED ADDRESS RANGE
         L     R4,DUMPADDR         ADDRESS IN MSG
         L     R5,PARMADDR         ADDRESS OF FIRST BYTE TO BE DUMPED
DUMPLP1  MVC   WTODUMP,WTODUMPL    MOVE MSG SKELETON
* PROCESSING FOR AVOIDING A 0C4
         MODESET MODE=SUP,KEY=ZERO SET SUPERVISOR STATE
         LA    R2,=C' '            FOR INDICATION IN ADDRTEST
         BAL   R6,ADDRTEST         GO TEST ADDRESS AVAILABILITY
*
         LR    R7,R5
         SR    R7,R4
         LA    R8,16
         SR    R8,R7               NUMBER OF CHARACTERS
         L     R0,PARMNMBR         LOAD NUMBER OF BYTES
         CR    R8,R0               COMPARE TO NUMBER OF BYTES
         BNH   *+6                 BR IF ^>
         LR    R8,R0               LOAD NUMBER OF BYTES
         SR    R0,R8               SUBTRACT
         ST    R0,PARMNMBR           AND STORE NUMBER OF BYTES LEFT
         LA    R7,WTOCHAR(R7)      ADDRESS OF FIRST CHARACTER
         LR    R9,R5
         SR    R9,R4
         SLA   R9,1
         ST    R9,HELPWORD
         SRA   R9,3
         A     R9,HELPWORD
         LA    R9,WTOHEX(R9)       ADDRESSE OF FIRST HEXADEC. DIGIT
*
DUMPLP2  DS    0H
         CL    R5,=XL4'00FFFFFF'   31-BIT-ADDR
         BNH   NON31A
         XMODE31
NON31A   DS    0H
         LA    R5,0(R5)            CLEAR BIT OR BYTE
         MVC   0(1,R7),0(R5)       MOVE CHARACTER
         UNPK  HELPWORD(3),0(2,R7) UNPK CHARACTER
         MVC   0(2,R9),HELPWORD    MOVE 2 HEX DIGITS
         TR    0(2,R9),CCTAB-240   TRANSLATE
         TR    0(1,R7),TABLE       TRANSLATE
         LA    R5,1(,R5)           ADDR OF NEXT BYTE
         LA    R7,1(,R7)            "
         LA    R9,2(,R9)            "
         ST    R5,DUMPADDR         STORE ADDR OF CURR. BYTE
         TM    DUMPADDR+3,X'03'    WORD BOUNDARY?
         BNZ   *+8                 BR IF NO
         LA    R9,1(,R9)           SKIP ONE BYTE IN MSG
         BCT   R8,DUMPLP2          BCT
*
         LR    R9,R4               LOAD VIRTUAL ADDRESS
         CLI   ADDRTYPE,C'R'       IS REAL ADDRESS TO BE WRITTEN?
         BNE   *+8                 BR IF NO
         LRA   R9,0(,R4)           LOAD REAL ADDRESS
         ST    R9,HELPWORD         STORE THIS ADDRESS
         UNPK  WTOADDR(9),HELPWORD(5)  UNPK THIS ADDRESS
         TR    WTOADDR,CCTAB-240   TRANSLATE TO 0-F
         MVI   WTOSPC1,C' '        MOVE BLANK
         XMODE24
         MODESET MODE=PROB,KEY=NZERO  RETURN TO PROB STATE
*
         L     R0,PARMNMBR         LOAD NUMBER OF BYTES LEFT
         LTR   R0,R0               IS NUMBER = 0?
         BNP   RETURN              BR IF YES
*
         L     R0,REG4             LOAD CONSOLE ID
         WTO   MF=(E,WTODUMP)      WRITE TO OPERATOR
*
         XMODE31
         LA    R4,16(,R4)          INCREMENT DUMP ADDRESS
         XMODE24
         B     DUMPLP1               AND PREPARE NEXT LINE
*
ERROR    EQU   *
         MVC   WTOBER,ER
RETURN   EQU   *
         L     R0,REG4
         WTO   MF=(E,WTOBER)
         XRETURN ,R
         EJECT
* TEST AN ADDRESS FOR DAT-ERROR
ADDRTEST EQU   *
         SR    R1,R1               CLEAR R1 (IS TO CONTAIN THE CC)
         CLI   0(R2),C'?'          31-BIT-MODE ?
         BE    XA
         CL    R4,=XL4'00FFFFFF'
         BNH   NO31
XA       XMODE31
NO31     DS    0H
         LA    R4,0(R4)            CLEAR BIT OR BYTE
         LRA   R9,0(,R4)           GET REAL ADDRESS
         BC    4,DATCC1            CC = 1: SEGMENT-TABLE ENTRY INVALID
         BC    2,DATCC2            CC = 2: PAGE-TABLE ENTRY INVALID
         BC    1,DATCC3            CC = 3: SEGMENT- OR PAGE-TABLE
*                                            LENGTH VIOLATION
         XMODE24                   CC = 0: OKAY
         BR    R6
*
DATCC3   LA    R1,1(,R1)           ADD 1 TO R1
DATCC2   LA    R1,1(,R1)           ADD 1 TO R1
DATCC1   LA    R1,1(,R1)           ADD 1 TO R1
         XMODE24
         STH   R1,DATCC            STORE THE CC
         CLI   DATCC+1,2           CC = 2?
         BNE   DATERROR
* EXAMINE GETMAIN-BIT IN PTE
         L     R1,=A(X'00FFF000')  LOAD BIT MASK
         NR    R1,R9               COMPUTE
         SRL   R1,8                  OFFSET IN PFT
         L     R3,16               CVT POINTER
         TM    X'74'(R3),X'80'     XA ?
         BO    DATERROR
         L     R15,X'164'(,R3)     PVTP
         L     R15,X'0C'(,R15)     PFT ORIGIN
         LA    R15,0(R1,R15)       ADDRESS OF PFTE
         SR    R14,R14             CLEAR R14 FOR ICM
         ICM   R14,6,2(R15)        INSERT VBN
         LA    R0,X'00000FFF'      LOAD BIT MASK
         NR    R9,R0               CLEAR CORR BITS
         OR    R9,R14              GET VIRT ADDRESS OF PTE
         TM    1(R9),X'01'         GETMAIN-BIT ON?
         BZ    DATERROR            BR IF NO
         BR    R6                  OKAY
*
PARMERR  B     ERROR
*
DATERROR EQU   *                   DYNAMIC ADDRESS TRANSLATION ERROR
         MODESET MODE=PROB,KEY=NZERO  RESET TO PROBLEM STATE
         MVC   WTOCCMSG,DATCCMSG  MOVE MSG SKELETON
         MVC   WTOCC,DATCC+1      MOVE CC
         OI    WTOCC,X'F0'        SET ZONE
         ST    R4,HELPWORD        STORE VIRTUAL ADDRESS
         UNPK  WTOCCADR(9),HELPWORD(5)  UNPK ADDRESS
         TR    WTOCCADR,CCTAB-240 CONVERT TO 0-F
         MVI   WTOCCADR+8,C' '
         B     RETURN
*
INDAERR  EQU   *                  ADDRESS ERROR ROUTINE
         MODESET KEY=NZERO        RETURN TO OLD KEY
         MVC   INDAMSG,INDAMSGL   MOVE MSG SKELETON
         ST    R10,HELPWORD       STORE ADDRESS
         UNPK  INDAADDR(9),HELPWORD(5)  UNPK ADDRESS
         TR    INDAADDR,CCTAB-240 TRANSLATE TO 0-F
         MVI   INDAADDR+8,C' '    MOVE BLANK
         B     RETURN
         EJECT
ER       WTO   'XDUMP01 ERROR IN PARAMETER',MCSFLAG=REG0,MF=L
WTODUMPL WTO   'ABCDEFGH  ........ ........ ........ ........ *        C
                       *',MCSFLAG=REG0,MF=L
DATCCMSG WTO   'XDUMP04 LRA CC=0,VIRTUAL ADDRESS=         ',           *
               MCSFLAG=REG0,MF=L
INDAMSGL WTO   'XDUMP05 ADDRESS ABCDEFGH DOES NOT CONTAIN AN ADDRESS', *
               MCSFLAG=REG0,MF=L
TRTAB    DS    0CL64
         DC    X'FF0A0B0C0D0E0F'
         DC    41XL1'FF'
         DC    X'00010203040506070809'
         DC    6XL1'FF'
TABLE    DC    0F'0'
         DC    C'................'
         DC    C'................'
         DC    C'................'
         DC    C'................'
         DC    C' .........Ö.<(+×'
         DC    C'&&.........!$*);^'
         DC    C'-/.........,%_>?'
         DC    C'..........:#@''="'
         DC    C'................'
         DC    C'................'
         DC    C'................'
         DC    C'................'
         DC    C'.ABCDEFGHI......'
         DC    C'.JKLMNOPQR......'
         DC    C'..STUVWXYZ......'
         DC    C'0123456789......'
CCTAB    DC    C'0123456789ABCDEF'
         LTORG
         EQUATE
         EJECT
WORKAREA DSECT
SVA      DS    18F
DBLWD    DS    D
PARMADDR DS    F                   DUMP ADDRESS
DUMPADDR DS    F                   NEXT LOWER MULTIPLE OF 16
PARMNMBR DS    F                   NUMBER OF BYTES TO BE DUMPED
HELPWORD DS    F                   RESERVED
DATCC    DS    H                   CONDITION CODE AFTER LRA
ADDRTYPE DS    CL1                 ADDRESS TYPE INFORMATION
SCANINF  DS    CL1                 'INPUT SCAN' INFORMATION
WTOBER   DS    CL126
         ORG   WTOBER
WTODUMP  DS    0CL68               DUMP AREA
         DS    F                   RDW
WTOADDR  DS    CL8
WTOSPC1  DS    CL2
WTOHEX   DS    4CL9
         DS    CL1
WTOCHAR  DS    CL16
         DS    CL1
         ORG   WTOBER
WTOCCMSG DS    0CL46               DAT ERROR MSG AREA
         DS    F
         DS    CL15                'XDUMP04 LRA CC='
WTOCC    DS    CL1                 LRA CC
         DS    CL17                ',VIRTUAL ADDRESS='
WTOCCADR DS    CL8                 VIRTUAL ADDRESS
         DS    CL1
         ORG   WTOBER
INDAMSG  DS    0CL56               IND ADDR ERROR MSG AREA
         DS    F
         DS    CL16                'XDUMP05 ADDRESS '
INDAADDR DS    CL8                 ADDRESS
         DS    CL28                ' DOES NOT CONTAIN AN ADDRESS'
         ORG
WORKL    EQU   *-WORKAREA
         XPARM
         END   DUMP
./ ADD NAME=DVOL
         TITLE 'XDVOL                             X-COMMAND DVOL'
DVOL     CSECT
         SPACE 3
* MVS/XA UCBSCAN                              29.6.83 BMW         /XUCB
***********************************************************************
*                                                                     *
*      PROGRAM-ID. DVOL.                                              *
*      AUTHOR. EHNERT.                                                *
*      INSTALLATION. BAYER AG, LEVERKUSEN, RECHENZENTRUM.             *
*      DATE-WRITTEN. 7. 10. 1975.                                     *
*      REMARKS.                                                       *
*                                                                     *
*          DAS X-COMMAND 'DVOL' GIBT INFORMATIONEN UEBER DEN          *
*          STATUS DER ALS PARAMETER SPEZIFIZIERTEN VOLUMES AUS.       *
*                                                                     *
*          ALS PARAMETER KOENNEN BIS ZU ZEHN VOLSER- ODER ADRESS-     *
*          MASKEN ANGEGEBEN WERDEN, MIT DEREN HILFE EINE AUSWAHL      *
*          GETROFFEN WIRD. DABEI STEHT '*' FUER JEDES ZEICHEN AN      *
*          DIESER STELLE.                                             *
*          ALS WEITERE AUSWAHL-PARAMETER KOENNEN DEVICE-TYPEN (DASD,  *
*          MSS, TAPE, UR, TP, GRAPHIC) UND DEVICE-STATUS (ONLINE,     *
*          OFFLINE, READY) ANGEGEBEN WERDEN.                          *
*                                                                     *
*          WIRD DAS PROGRAMM UNTER DEM NAMEN 'DVOL' AUFGERUFEN,       *
*          SO GELTEN DIE SPEZIFIZIERTEN PARAMETER ALS VOLSER-         *
*          MASKEN, WIRD ES UNTER DEM NAMEN 'UNIT' AUFGERUFEN,         *
*          SO GELTEN DIE SPEZIFIZIERTEN PARAMETER ALS ADRESS-         *
*          MASKEN.                                                    *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      INITIAL PROCESSING.                                            *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      SAVE REGISTER CONTENTS,
*      GETMAIN FOR WORK AREAS,
*      ALLOCATION OF BASE REGISTERS.
*
         SPACE 1
         XSAVE R12,,DVOL,WKAREALN
         SPACE 1
         LR    R11,R01                 INITIALIZE BASE REG. OF PARMAREA
         USING UCB,R03                 ALLOC. BASE REG. OF UCB
         USING UCBCMEXT,R09            ALLOC. BASE REG. OF UCB EXTENT
         USING MESSAGE,R10             ALLOC. BASE REG. OF MESSAGE
         USING PARMAREA,R11            ALLOC. BASE REG. OF PARMAREA
         USING WKAREA,R13              ALLOC. BASE REG. OF WKAREA
         EJECT
*
*      INITIALIZE WORK AREAS.
*
         SPACE 1
         MVI   VOLBYTE,X'00'           CLEAR FLAG BYTE
         MVI   VOLBYTE,X'00'           CLEAR FLAG BYTE
         MVI   WTORBYTE,X'FF'          SET FLAG
         LA    R02,ENDTAB1             LOAD ADDRESSES OF
         LA    R03,ENDTPVOL                TABLE ENDS AND
         STM   R02,R03,ADDRETB1            STORE THEM
         MVC   UCMID,REG4              MOVE UCM ID. OF CALLER
         MVC   PNAME,TEXT              MOVE NAME OF PROGRAM
         MVC   HEADER(LENMSG01),MSG01  MOVE HEAD LINE
         LA    R10,HEADER              INITIALIZE BASE REG. OF MESSAGE
         MVC   MSGNAME,PNAME           MOVE NAME OF PROGRAM
         MVC   WTOR(LENMSG05),MSG05    MOVE WTOR MESSAGE
         LA    R10,WTOR                INITIALIZE BASE REG. OF MESSAGE
         MVC   WTORNAME,PNAME          MOVE PROGRAM NAME
         MVI   CPUBYTE,X'00'           CLEAR FLAG BYTE
         MVI   PRMFLAGS,X'00'
         MVC   PRMFLAGS+1(PFLAGLEN-1),PRMFLAGS
         MVI   WTOBYTE,X'00'           CLEAR FLAG BYTE
         LA    R02,PJOBNAME            LOAD ADDRESS OF JOB NAME
         LA    R03,PSTEPNME            LOAD ADDRESS OF STEP NAME
         STM   R02,R03,PJNMEADR        STORE ADDRESSES
         OI    PSNMEADR,X'80'          INDICATE LAST ENTRY
         SPACE 2
*
*      SET FLAG BYTES IN MESSAGE AREA.
*
         SPACE 1
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         ST    R10,SAVE1R10                AND STORE IT
         L     R03,ADDRETB1            LOAD ADDRESS OF TABLE END
CLEARMSS EQU   *
         MVC   MSGFLAG,=X'FFFF'        SET FLAGS
         LA    R10,68(R10)             LOAD ADDRESS OF NEXT ENTRY
         CR    R10,R03                 TABLE END ?
         BL    CLEARMSS                BRANCH IF NOT
         SPACE 1
         L     R02,16                  LOAD ADRESSE OF CVT
         TM    116(R02),X'10'          SYSTEM = MVS ?
         BZ    GETPARM                 BRANCH IF NOT
         L     R02,764(R02)            LOAD ADRESSE OF PCCAVT
         LTR   R02,R02                 PCCAVT AVAILABLE ?
         BZ    GETPARM                 BRANCH IF NOT
         CLC   0(4,R02),=XL6'0'        CPU 0 AVAILABLE ?
         BE    *+8                     BRANCH IF NOT
         OI    CPUBYTE,X'F0'           SET FLAGS
         CLC   4(4,R02),=XL6'0'        CPU 1 AVAILABLE ?
         BE    GETPARM                 BRANCH IF NOT
         OI    CPUBYTE,X'0F'           SET FLAGS
         EJECT
***********************************************************************
*                                                                     *
*      INPUT PARAMETER PROCESSING.                                    *
*                                                                     *
***********************************************************************
         SPACE 3
GETPARM  EQU   *
         CLC   TEXT(4),=C'DVOL'        VOLUMES SPECIFIED ?
         BNE   *+8                     BRANCH IF NOT
         MVI   VOLBYTE,X'FF'           SET FLAG
         LA    R04,TEXT+4              LOAD ADDRESS OF PARAMETER CHAIN
         LA    R08,PARMEND             LOAD ADDRESS OF PARAMETER END
         LA    R06,TABPVOL1            LOAD ADDRESS OF VOLUME TABLE
         CLI   0(R04),C','             PARAMETERS SPECIFIED ?
         BNE   NOPARM                  BRANCH IF NOT
NEXTPARM EQU   *
         SR    R05,R05                 ZERO LENGTH COUNT
         LR    R07,R04                 LOAD ADDRESS OF SEPERATOR
NXTBYTE  EQU   *
         LA    R04,1(R04)              LOAD ADDRESS OF NEXT CHARACTER
         CR    R04,R08                 END OF PARAMETER CHAIN ?
         BNL   PRMERROR                IF YES, BRANCH TO ERROR ROUTINE
         CLI   0(R04),C','             END OF PARAMETER ?
         BE    NEXTPVOL                BRANCH IF YES
         CLI   0(R04),X'00'            END OF PARAMETER CHAIN ?
         BE    NEXTPVOL                BRANCH IF YES
         CLI   0(R04),C' '             END OF PARAMETER CHAIN ?
         BE    NEXTPVOL                BRANCH IF YES
         LA    R05,1(R05)              INCREASE LENGTH COUNT
         CLI   0(R04),C'*'             DON'T CARE CHARACTER ?
         BNE   NXTBYTE                 BRANCH IF NOT
         MVI   0(R04),X'FF'            TRANSLATE CHARACTER
         B     NXTBYTE                 BRANCH
         EJECT
NEXTPVOL EQU   *
         BAL   R14,SETFLAGS            BRANCH TO SET FLAGS
         C     R06,ADDRETPV            END OF TABLE ?
         BNL   PRMERROR                IF YES, BRANCH TO ERROR ROUTINE
         C     R05,=F'6'               VALID LENGTH ?
         BH    PRMERROR                IF NOT, BRANCH TO ERROR ROUTINE
         LTR   R05,R05                 VALID LENGTH ?
         BZ    PRMERROR                IF NOT, BRANCH TO ERROR ROUTINE
         BCTR  R05,0                   MODIFY FOR EX COMMAND
         EX    R05,MOVEPVOL            BRANCH TO MOVE VOLSER NUMBER
         STC   R05,0(R06)              STORE LENGTH
         LA    R06,7(R06)              LOAD ADDRESS OF NEXT ENTRY
         CLI   0(R04),C','             MORE PARAMETERS IN CHAIN ?
         BE    NEXTPARM                BRANCH IF YES
         SPACE 2
*
*      END OF PARAMETER PROCESSING.
*
         SPACE 1
ENDPARM  EQU   *
         MVI   0(R06),X'80'            INDICATE END OF TABLE
         MVC   TABPVOL2,TABPVOL1       COPY TABLE
         LA    R06,TABPVOL2-1          LOAD ADDRESS OF TABLE
NEXTFF   EQU   *
         LA    R06,1(R06)              LOAD ADDRESS OF NEXT CHARACTER
         CLI   0(R06),X'80'            END OF TABLE ?
         BE    GETFASID                BRANCH IF YES
         CLI   0(R06),X'FF'            CHARACTER = X'FF' ?
         BNE   NEXTFF                  BRANCH IF NOT
         MVI   0(R06),X'00'            TRANSLATE CHARACTER
         B     NEXTFF                  BRANCH
         SPACE 1
NOPARM   EQU   *
         MVI   NOPMFLAG,X'FF'          SET FLAG
         B     ENDPARM                 BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      SET PARAMETER FLAGS.                                           *
*                                                                     *
***********************************************************************
         SPACE 3
SETFLAGS EQU   *
         LA    R01,PRMTABLE-10         LOAD ADDRESS OF TABLE
         LA    R02,PRMFLAGS            LOAD ADDRESS OF FLAG BYTES
         SR    R03,R03                 ZERO FOR IC COMMAND
SETFLGLP EQU   *
         LA    R01,10(R01)             LOAD ADDRESS OF NEXT ENTRY
         LA    R02,1(R02)              LOAD ADRESS OF NEXT FLAG BYTE
         CLI   0(R01),X'FF'            END OF TABLE ?
         BER   R14                     RETURN IF YES
         IC    R03,0(R01)              INSERT LENGTH
         EX    R03,CLCPARM             BRANCH TO COMPARE PARAMETERS
         BNE   SETFLGLP                BRANCH IF NOT EQUAL
         MVI   PARMFLAG,X'FF'          SET FLAGS
         MVI   0(R02),X'FF'            SET FLAGS
         CLI   0(R04),C','             MORE PARAMETERS IN CHAIN ?
         BE    NEXTPARM                BRANCH IF YES
         B     ENDPARM                 BRANCH
         SPACE 2
GETFASID EQU   *
         LOAD  EP=FASID                LOAD ENTRY POINT ADDRESS
         ST    R00,FASIDADR                AND STORE IT
         EJECT
***********************************************************************
*                                                                     *
*      PROCESSING OF UCB INFORMATION.                                 *
*                                                                     *
***********************************************************************
         SPACE 3
GETUCB   EQU   *
         DROP  R11                     FREE BASE REG. OF PARMAREA
*        GETMAIN R,LV=UCBTBLEN,SP=125                             /XUCB
*        LR    R11,R01                 INITIALIZE BASE REG. OF UCB/XUCB
*        USING UCBADTAB,R11            ALLOC. BASE REG. OF UCBADTA/XUCB
*        ST    R11,UCBADDR             STORE ADDRESS OF TABLE     /XUCB
*        MVI   UCBADDR,X'80'           SET OPTION BYTE            /XUCB
*        LA    R01,UCBADDR             LOAD ADDRESS OF PARAMETER  /XUCB
*        LINK  EP=GETUCBS              LINK TO SUBROUTINE         /XUCB
*        LA    R02,UCBADTAB            LOAD ADDRESS OF TABLE      /XUCB
*        SR    R03,R03                 ZERO FOR ICM COMMAND       /XUCB
*        MVC   UCBTBEND,=X'FFFF'       SPECIFY END OF TABLE       /XUCB
*        S     R02,=F'2'               DECREASE ADDRESS           /XUCB
         XUCBSCAN R02                  CREATE WORK AREA           /XUCB
NEXTUCB  EQU   *
*        LA    R02,2(R02)              LOAD ADDRESS OF NEXT ENTRY /XUCB
*        CLC   0(2,R02),=X'FFFF'       END OF TABLE ?             /XUCB
*        BE    ENDPROC                 BRANCH IF YES              /XUCB
*        ICM   R03,3,0(R02)            LOAD ADDRESS OF UCB        /XUCB
         XUCBGET R02,ENDPROC,ENDPROC                              /XUCB
         LR    R03,R01                 LOAD ADDRESS OF UCB        /XUCB
         SPACE 1
         MVI   FNDBYTE,X'FF'           SET FLAGS
         TM    PARMFLAG,X'FF'          SELECT PARAMETERS ?
         BZ    *+8                     BRANCH IF NOT
         BAL   R14,TSTFLAGS            BRANCH TO TEST FLAGS
         TM    FNDBYTE,X'FF'           TEST OK ?
         BZ    NEXTUCB                 BRANCH IF NOT
         EJECT
*
*      COMPARISON OF UCB ADDRESSES OR VOLSER NUMBERS.
*
         SPACE 1
         SR    R05,R05                 ZERO FOR IC COMMAND
         LA    R06,TABPVOL1-7          LOAD ADDRESS OF TABLE
         LA    R07,TABPVOL2-7          LOAD ADDRESS OF TABLE
NXTVOLID EQU   *
         LA    R06,7(R06)              LOAD ADDRESS OF NEXT ENTRY
         LA    R07,7(R07)              LOAD ADDRESS OF NEXT ENTRY
         TM    0(R06),X'80'            END OF TABLE ?
         BO    TESTADDR                BRANCH IF YES
         IC    R05,0(R06)              INSERT LENGTH
         CLI   VOLBYTE,X'FF'           VOLSER NUMBERS SPECIFIED ?
         BNE   NEXTADR                 BRANCH IF NOT
         TM    UCBDVCLS,X'A0'          TAPE OR DIRECT ACCESS ?
         BZ    NEXTUCB                 BRANCH IF NOT
         CLC   UCBVOLI,=XL6'0'         VOLSER NUMBER ?
         BE    NEXTUCB                 BRANCH IF NOT
         MVC   ZWVOLID1,UCBVOLI        MOVE VOLSER NUMBER
         MVC   ZWVOLID2,UCBVOLI        MOVE VOLSER NUMBER
EXMVC    EQU   *
         EX    R05,NCVOLID1            BRANCH TO TRANSLATE VOLID
         EX    R05,OCVOLID2            BRANCH TO TRANSLATE VOLID
         CLC   ZWVOLID1,ZWVOLID2       VOLID VALID ?
         BNE   NXTVOLID                BRANCH IF NOT
         MVC   VOLSERNO,UCBVOLI        MOVE VOLSER NUMBER
         B     MVCMSG                  BRANCH
         SPACE 1
NEXTADR  EQU   *
         MVC   ZWVOLID1,UCBNAME        MOVE UNIT NAME
         MVC   ZWVOLID2,UCBNAME        MOVE UNIT NAME
         B     EXMVC                   BRANCH
         SPACE 1
TESTADDR EQU   *
         TM    TABPVOL1,X'80'          ADDRESSES OR VOLUMES SPEC. ?
         BZ    NEXTUCB                 BRANCH IF YES
         TM    VOLBYTE,X'FF'           VOLSER NUMBERS SPECIFIED ?
         BZ    MVCMSG                  BRANCH IF NOT
         TM    UCBDVCLS,X'A0'          TAPE OR DIRECT ACCESS ?
         BZ    NEXTUCB                 BRANCH IF NOT
         EJECT
***********************************************************************
*                                                                     *
*      EDIT MESSAGE LINES.                                            *
*                                                                     *
***********************************************************************
         SPACE 3
MVCMSG   EQU   *
         L     R10,SAVE1R10            INITIALIZE BASE REG. OF MESSAGE
         MVC   MESSAGE(LENMSG02),MSG02 MOVE MESSAGE SKELETON
         MVC   MSGNAME,PNAME           MOVE NAME OF PROGRAM
         MVC   MSGADR,UCBNAME          MOVE NAME OF UNIT
*        STCM  R03,3,MSGUCBAD          STORE ADDRESS OF UNIT      /XUCB
         STCM  R03,7,MSGUCBAD          STORE ADDRESS OF UNIT      /XUCB
*        UNPK  MSGUCBAD(5),MSGUCBAD(3) UNPACK ADDRESS             /XUCB
         UNPK  MSGUCBAD(7),MSGUCBAD(4) UNPACK ADDRESS             /XUCB
*        MVI   MSGUCBAD+4,C' '                                    /XUCB
         MVI   MSGUCBAD+6,C' '                                    /XUCB
         TR    MSGUCBAD,TABHEXA-240    TRANSLATE ADDRESS
         SPACE 1
         TM    UCBSTAT,UCBONLI         DEVICE ONLINE ?
         BO    TESTSYS                 BRANCH IF YES
         MVC   MSGLINE,=C'OFF'         SPECIFY OFFLINE
         B     TESTALOC                BRANCH
TESTSYS  EQU   *
         TM    CPUBYTE,X'FF'           SYSTEM = MVS ?
         BZ    TESTALOC                BRANCH IF NOT
         TM    UCBFL5,UCBALTPH         ALTERNATE PATH ?
         BNZ   TESTCPU0                BRANCH IF YES
         MVC   MSGPATH0(5),=C' N/A '   NOT APPLICATABLE
         B     TESTALOC                BRANCH
TESTCPU0 EQU   *
         TM    CPUBYTE,X'F0'           CPU 0 AVAILABLE ?
         BZ    TESTCPU1                BRANCH IF NOT
         MVC   MSGPATH0,=C'**'
         TM    UCBCHM,UCBPPA           PRIMARY PATH FROM CPU 0 ?
         BNZ   *+8                     BRANCH IF NOT
         MVI   MSGPATH0,C'1'
         TM    UCBCHM,UCBSPA           SECONDARY PATH FROM CPU 0 ?
         BNZ   TESTCPU1                BRANCH IF NOT
         MVI   MSGPATH0+1,C'1'
TESTCPU1 EQU   *
         TM    CPUBYTE,X'0F'           CPU 1 AVAILABLE ?
         BZ    TESTALOC                BRANCH IF NOT
         MVC   MSGPATH1,=C'**'
         TM    UCBCHM,UCBPPB           PRIMARY PATH FROM CPU 1 ?
         BNZ   *+8                     BRANCH IF NOT
         MVI   MSGPATH1,C'1'
         TM    UCBCHM,UCBSPB           SECONDARY PATH FROM CPU 1 ?
         BNZ   TESTALOC                BRANCH IF NOT
         MVI   MSGPATH1+1,C'1'
         EJECT
TESTALOC EQU   *
         TM    UCBSTAT,UCBALOC         DEVICE ALLOCATED ?
         BZ    TESTNRDY                BRANCH IF NOT
         MVC   MSGALLOC,=8C'.'         INITIALIZE MESSAGE AREA
         MVC   PJOBNAME,=CL8' '        CLEAR JOB NAME
         L     R09,UCBEXTPT            LOAD ADDRESS OF COMMON UCB EXT.
         LA    R09,UCBASID             LOAD ADDRESS OF ASID
         ST    R09,PASIDADR                AND STORE IT
         LA    R01,PARMLIST            LOAD ADDRESS OF PARAMETER LIST
         L     R15,FASIDADR            LOAD ADDRESS OF SUBROUTINE
         BALR  R14,R15                 BRANCH TO SUBROUTINE
         LTR   R15,R15                 VALID JOB NAME ?
         BNZ   TESTNRDY                BRANCH IF NOT
         MVC   MSGALLOC,PJOBNAME       MOVE JOB NAME
TESTNRDY EQU   *
         TM    UCBFLA,UCBNRY           DEVICE READY ?
         BZ    TSTCUBSY                BRANCH IF YES
         MVC   MSGREADY,=C'NR'         INDICATE NOT READY
TSTCUBSY EQU   *
         TM    UCBFLA,UCBCUB           CONTROL UNIT BUSY ?
         BZ    TSTDVBSY                BRANCH IF NOT
         MVC   MSGSTAT(4),=C'CUBS'     MOVE TEXT
         B     TSTDVRES                BRANCH
TSTDVBSY EQU   *
         TM    UCBFLA,UCBBSY           DEVICE BUSY ?
         BZ    TSTMOUNT                BRANCH IF NOT
         MVC   MSGSTAT(4),=C'BUSY'     MOVE TEXT
         B     TSTDVRES                BRANCH
TSTMOUNT EQU   *
         TM    UCBDMCT,UCBMOUNT        MOUNT PENDING ?
         BZ    TSTIOREC                BRANCH IF NOT
         MVC   MSGSTAT(5),=C'MOUNT'    MOVE TEXT
         EJECT
TSTIOREC EQU   *
         TM    UCBFLA,UCBQISCE         IO RECOVERY ACTIVE ?
         BZ    TSTDVRES                BRANCH IF NOT
         MVC   MSGSTAT(5),=C'IOREC'    MOVE TEXT
         B     TESTVOLI                BRANCH
TSTDVRES EQU   *
         TM    UCBFLB,UCBRESVH         DEVICE RESERVED ?
         BZ    TESTVOLI                BRANCH IF NOT
*        MVC   MSGSTAT+4(2),=C'-R'     MOVE TEXT                  /XUCB
         MVC   MSGSTAT+3(2),=C'-R'     MOVE TEXT                  /XUCB
TESTVOLI EQU   *
         TM    UCBDVCLS,X'A0'          TAPE OR DIRECT ACCESS ?
         BZ    TESTDFLT                BRANCH IF NOT
         CLC   UCBVOLI,=XL6'0'         VOLID ?
         BE    TESTSTOR                BRANCH IF NOT
         MVC   MSGVOLID,UCBVOLI        MOVE VOLSER NUMBER
TESTSTOR EQU   *
         TM    UCBSTAB,UCBBSTR         STORAGE VOLUME ?
         BZ    TESTPUB                 BRANCH IF NOT
         MVC   MSGATTR1,=C'STG/RMV'    SPECIFY STORAGE VOLUME
TESTPUB  EQU   *
         TM    UCBSTAB,UCBBPUB         PUBLIC VOLUME ?
         BZ    TESTPRV                 BRANCH IF NOT
         MVC   MSGATTR1,=C'PUB/RMV'    SPECIFY PUBLIC VOLUME
TESTPRV  EQU   *
         TM    UCBSTAB,UCBBPRV         PRIVATE VOLUME ?
         BZ    TESTRES                 BRANCH IF NOT
         MVC   MSGATTR1,=C'PRV/RMV'    SPECIFY PRIVATE VOLUME
TESTRES  EQU   *
         TM    UCBSTAT,UCBPRES         VOLUME RESIDENT ?
         BZ    TESTRSV                 BRANCH IF NOT
         MVC   MSGATTR2,=C'RES'        SPECIFY RESIDENT VOLUME
TESTRSV  EQU   *
         TM    UCBSTAT,UCBRESV         VOLUME RESERVED ?
         BZ    TESTDFLT                BRANCH IF NOT
         MVC   MSGATTR2,=C'RSV'        SPECIFY RESERVED VOLUME
TESTDFLT EQU   *
         CLI   NOPMFLAG,X'FF'          DEFAULT OUTPUT ?
         BNE   MSGOK                   BRANCH IF NOT
         CLC   MSGSTAT,=CL8' '         STATUS INFORMATION ?
         BE    NEXTUCB                 BRANCH IF NOT
         CLC   MSGSTAT(5),=C'MOUNT'    MOUNT PENDING ?
         BE    NEXTUCB                 BRANCH IF NOT
MSGOK    EQU   *
         MVI   WTOBYTE,X'FF'           SET FLAGS
         LA    R14,NEXTUCB             LOAD RETURN ADDRESS
         MVC   MSGFLAG,=XL6'0'         CLEAR FLAG BYTES
         LA    R10,68(R10)             LOAD ADDRESS OF NEXT ENTRY
         C     R10,ADDRETB1            END OF TABLE
         BNL   PUTMSG                  BRANCH IF YES
         ST    R10,SAVE1R10            SAVE CURRENT ADDRESS
         BR    R14                     RETURN
         EJECT
***********************************************************************
*                                                                     *
*      FINAL PROCESSING.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
ENDPROC  EQU   *
         XUCBOFF R02                   FREE WORK AREA             /XUCB
*        FREEMAIN R,SP=125             FREE TABLE AREA            /XUCB
         MVI   WTORBYTE,X'00'          CLEAR FLAG BYTE
         TM    WTOBYTE,X'FF'           MESSAGE OUTPUT ?
         BNZ   PUTOPMSG                BRANCH IF YES
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         MVC   MESSAGE(LENMSG03),MSG03 MOVE MESSAGE
         CLI   VOLBYTE,X'FF'           PROGRAM = X-DVOL ?
         BE    *+10                    BRANCH IF YES
         MVC   MSGNAME,PNAME           MOVE NAME OF PROGRAM
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         WTO   MF=(E,MESSAGE)          DISPLAY MESSAGE
         B     RETURN                  BRANCH
         SPACE 2
*
*      DISPLAY OF OPEN MESSAGES.
*
         SPACE 1
PUTOPMSG EQU   *
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         CLC   MSGFLAG,=XL6'0'         ALL MESSAGES DISPLAYED ?
         BNE   RETURN                  BRANCH IF NOT
         BAL   R14,PUTMSG              IF YES, BR. TO MESSAGE OUTPUT
         EJECT
*
*      FREE WORK AREAS,
*      RELOAD REGISTER CONTENTS,
*      RETURN.
*
         SPACE 1
RETURN   EQU   *
         XRETURN ,R
         SPACE 3
***********************************************************************
*                                                                     *
*      PARAMETER ERROR ROUTINE.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
PRMERROR EQU   *
         LA    R10,MSGTAB              LOAD ADDRESS OF MESSAGE TABLE
         MVC   MESSAGE(LENMSG04),MSG04 MOVE MESSAGE
         MVC   MSGNAME,PNAME           MOVE NAME OF PROGRAM
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         WTO   MF=(E,MESSAGE)          DISPLAY MESSAGE
         B     RETURN                  BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      TEST DEVICES FOR SPECIFIED STATUS.                             *
*                                                                     *
***********************************************************************
         SPACE 3
TSTFLAGS EQU   *
         CLC   TAPEFLAG(5),=XL6'0'     DEVICES TYPE(S) SPECIFIED ?
         BE    TSTMSS                  BRANCH IF NOT
         LA    R04,PRMTABLE-10         LOAD ADDRESS OF TABLE
         LA    R05,PRMFLAGS            LOAD ADDRESS OF FLAG BYTES
TSTDEVLP EQU   *
         LA    R04,10(R04)             LOAD ADDRESS OF NEXT ENTRY
         CLI   1(R04),X'FF'            END OF TABLE ?
         BE    CLEARFLG                BRANCH IF YES
         LA    R05,1(R05)              LOAD ADDRESS OF NEXT FLAG BYTE
         TM    0(R05),X'FF'            FLAG ON ?
         BZ    TSTDEVLP                BRANCH IF NOT
         CLC   UCBDVCLS,1(R04)         SPECIFIED DEVICE TYPE ?
         BNE   TSTDEVLP                BRANCH IF NOT
         SPACE 1
TSTMSS   EQU *
         TM    MSSFLAG,X'FF'           VIRTUAL DEVICES SPECIFIED ?
         BZ    TSTNOMSS                BRANCH IF NOT
         TM    UCBDVCLS,UCB3DACC       DIRECT ACCESS ?
         BZ    TSTMSC                  BRANCH IF NOT
         TM    UCBTBYT2,UCBRVDEV       VIRTUAL DEVICE ?
         BZ    CLEARFLG                BRANCH IF NOT
         B     TSTPLINE                BRANCH
TSTNOMSS EQU   *
         TM    UCBDVCLS,UCB3DACC       DIRECT ACCESS ?
         BZ    TSTNOMSC                BRANCH IF NOT
         TM    UCBTBYT2,UCBRVDEV       VIRTUAL DEVICE ?
         BO    CLEARFLG                BRANCH IF YES
         B     TSTPLINE                BRANCH
TSTNOMSC EQU   *
         TM    UCBUNTYP,UCBDSM         MSC ?
         BO    CLEARFLG                BRANCH IF YES
         EJECT
TSTPLINE EQU   *
         CLC   ONLFLAG(2),=XL6'0'      ON/OFFLINE DEVICES SPECIFIED ?
         BE    TSTREADY                BRANCH IF NOT
         TM    UCBSTAT,UCBONLI         DEVICE ONLINE ?
         BZ    TSTPOFFL                BRANCH IF NOT
         TM    ONLFLAG,X'FF'           ONLINE DEVICES SPECIFIED ?
         BZ    CLEARFLG                BRANCH IF NOT
         B     TSTREADY                BRANCH
TSTPOFFL EQU   *
         TM    OFFLFLAG,X'FF'          OFFLINE DEVICES SPECIFIED ?
         BZ    CLEARFLG                BRANCH IF NOT
         SPACE 1
TSTREADY EQU   *
         TM    RDYFLAG,X'FF'           READY DEVICES SPECIFIED ?
         BZ    TSTFLGOK                BRANCH IF NOT
         TM    UCBSFLS,UCBNRY          DEVICE READY ?
         BO    CLEARFLG                BRANCH IF NOT
         SPACE 1
TSTFLGOK EQU   *
         BR    R14                     RETURN
         SPACE 2
CLEARFLG EQU   *
         MVI   FNDBYTE,X'00'           CLEAR FLAG BYTE
         BR    R14                     RETURN
TSTMSC   EQU   *
         TM    UCBUNTYP,UCBDSM         MSC ?
         BNO   CLEARFLG                BRANCH IF NOT
         B     TSTPLINE                BRANCH IF YES
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE DISPLAY.                                               *
*                                                                     *
***********************************************************************
         SPACE 3
PUTMSG   EQU   *
         ST    R14,SAVE1R14            SAVE RETURN ADDRESS
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         WTO   MF=(E,HEADER)           DISPLAY HEAD LINE
         LA    R10,MSGTAB-68           LOAD ADDRESS OF TABLE
         L     R09,ADDRETB1            LOAD ADDRESS OF TABLE END
         BAL   R14,DISPLAY             BRANCH TO DISPLAY
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         ST    R10,SAVE1R10                AND STORE IT
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         CLI   WTORBYTE,X'FF'          FLAG ON ?
         BNER  R14                     RETURN IF NOT
NXTREPLY EQU   *
         XC    ECB,ECB                 CLEAR EVENT CONTROL BLOCK
         MVI   REPLY,C' '              CLEAR REPLY AREA
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         LA    R04,ECB                 LOAD ADDRESS OF ECB
         LA    R05,REPLY               LOAD ADDRESS OF REPLY
         WTOR  ,(R05),1,(R04),MF=(E,WTOR)
         SPACE 1
         WAIT  ECB=ECB                 WAIT FOR COMPLETION
         CLI   REPLY,C'N'              REPLY = NO ?
         BE    RETURN                  BRANCH IF YES
         CLI   REPLY,C'Y'              REPLY = YES ?
         BNE   NXTREPLY                BRANCH IF NOT
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         BR    R14                     RETURN
         EJECT
*
*      DISPLAY SINGLE MESSAGE LINES.
*
         SPACE 1
DISPLAY  EQU   *
         ST    R14,SAVE2R14            SAVE RETURN ADDRESS
NEXTMSG  EQU   *
         LA    R10,68(R10)             LOAD ADDRESS OF NEXT ENTRY
         CR    R10,R09                 END OF TABLE ?
         BNL   ENDMSG                  BRANCH IF YES
         CLC   MSGFLAG,=X'FFFF'        FLAGS ON ?
         BE    ENDMSG                  BRANCH IF YES
         MVC   MSGFLAG,=X'FFFF'        SET FLAGS
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         WTO   MF=(E,MESSAGE)          DISPLAY ONE MESSAGE
         B     NEXTMSG                 BRANCH
ENDMSG   EQU   *
         L     R14,SAVE2R14            LOAD RETURN ADDRESS
         BR    R14                     RETURN
         SPACE 3
***********************************************************************
*                                                                     *
*      COMMANDS VIA EXECUTE COMMAND.                                  *
*                                                                     *
***********************************************************************
         SPACE 3
MOVEPVOL EQU   *
         MVC   1(0,R06),1(R07)         MOVE VOLID TO TABLE
NCVOLID1 EQU   *
         NC    ZWVOLID1(0),1(R06)      TRANSLATE VOLID
OCVOLID2 EQU   *
         OC    ZWVOLID2(0),1(R07)      TRANSLATE VOLID
CLCPARM  EQU   *
         CLC   2(0,R01),1(R07)         PARAMETER = TABLE ENTRY ?
         EJECT
***********************************************************************
*                                                                     *
*      REGISTER EQUATES.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
R00      EQU   0                       MACRO AND PARAMETER REGISTER
R01      EQU   1                       MACRO AND PARAMETER REGISTER
R02      EQU   2                       PTR. IN CVT / UCB LOOKUP TABLE
R03      EQU   3                       POINTER TO UCB
R04      EQU   4                       WORK REGISTER
R05      EQU   5                       WORK REGISTER
R06      EQU   6                       WORK REGISTER
R07      EQU   7                       WORK REGISTER
R08      EQU   8                       WORK REGISTER
R09      EQU   9                       BASE OF COMMON UCB EXTENSION
R10      EQU   10                      POINTER IN MESSAGE TABLE
R11      EQU   11                      BASE OF PARMAREA / UCBADTAB
R12      EQU   12                      BASE OF CSECT DVOL
R13      EQU   13                      BASE OF WKAREA
R14      EQU   14                      RETURN ADDRESSES
R15      EQU   15                      CSECT ADRESSES AND RETURN CODES
         EJECT
***********************************************************************
*                                                                     *
*      TRANSLATE TABLE.                                               *
*                                                                     *
***********************************************************************
         SPACE 2
TABHEXA  DC    C'0123456789ABCDEF'
         SPACE 3
***********************************************************************
*                                                                     *
*      PARAMETER TABLE.                                               *
*                                                                     *
***********************************************************************
         SPACE 2
PRMTABLE EQU   *
         DC    X'03',X'80',CL8'TAPE'
         DC    X'03',X'20',CL8'DASD'
         DC    X'01',X'08',CL8'UR'
         DC    X'01',X'40',CL8'TP'
         DC    X'06',X'10',CL8'GRAPHIC'
         DC    X'02',X'FF',CL8'MSS'
         DC    X'05',X'FF',CL8'ONLINE'
         DC    X'06',X'FF',CL8'OFFLINE'
         DC    X'04',X'FF',CL8'READY'
         DC    X'FF',X'FF',CL8' '      TABLE END
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE CONSTANTS.                                             *
*                                                                     *
***********************************************************************
         SPACE 3
* MSG FORMAT CHANGE FUER 3 BYTE UCB ADRESSE                       /XUCB
*SG01    WTO   'XDVOL01 VOLSER ADR LNE -ALLOC-- -STATUS-- PATHS -ATTR--
*               UCB ',
MSG01    WTO   'XDVOL01 VOLSER ADR LNE -ALLOC-- -STATUS- PATHS -ATTR-- *
               -UCB--',                                                *
               MF=L,MCSFLAG=(REG0)
LENMSG01 EQU   *-MSG01
         SPACE 1
* MSG FORMAT CHANGE FUER 3 BYTE UCB ADRESSE                       /XUCB
*SG02    WTO   'XDVOL02        ADR ON      NO   RD        ../..   N/A
*               9999',
MSG02    WTO   'XDVOL02        ADR ON      NO   RD       ../..   N/A   *
               999999',                                                *
               MF=L,MCSFLAG=(REG0)
LENMSG02 EQU   *-MSG02
         SPACE 1
MSG03    WTO   'XDVOL03 NO OUTPUT PRODUCED',                           *
               MF=L,MCSFLAG=(REG0)
LENMSG03 EQU   *-MSG03
         SPACE 1
MSG04    WTO   'XDVOL04 INVALID PARAMETER LIST',                       *
               MF=L,MCSFLAG=(REG0)
LENMSG04 EQU   *-MSG04
         EJECT
MSG05    WTOR  'XDVOL05 REPLY ''Y'' (YES) OR ''N'' (NO) TO CONTINUE',  *
               MF=L,MCSFLAG=(REG0)
LENMSG05 EQU   *-MSG05
         SPACE 3
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*      WORK AREAS.                                                    *
*                                                                     *
***********************************************************************
         SPACE 3
WKAREA   DSECT
SAVEAREA DS    18F                     SAVEAREA OF GENERAL REGISTERS
         SPACE 2
*
*      ADRESSEN-SPEICHER.
*
         SPACE 1
ADDRETB1 DS    F                       END ADDRESS OF MESSAGE TABLE
ADDRETPV DS    F                       END ADDRESS OF VOLUME TABLE
SAVE1R10 DS    F                       SAVEAREA1 OF R10
SAVE1R14 DS    F                       SAVEAREA1 OF R14
SAVE2R14 DS    F                       SAVEAREA2 OF R14
UCBADDR  DS    F                       ADDRESS OF UCB TABLE
FASIDADR DS    F                       ADDRESS OF SUBROUTINE 'FASID'
UCMID    DS    F                       UCM ID. OF CALLER
         SPACE 2
*
*      WORK AREAS.
*
         SPACE 1
ECB      DS    F                       EVENT CONTROL BLOCK
FNDBYTE  DS    C                       FLAG
VOLBYTE  DS    C                       FLAG OF SPECIFIED VOLUMES
CPUBYTE  DS    C                       FLAG BYTE OF CPU'S
WTOBYTE  DS    C                       FLAG OF WTO
WTORBYTE DS    C                       FLAG OF WTOR
REPLY    DS    C                       REPLY AREA
VOLSERNO DS    CL6                     VOLSER NUMBER
ZWVOLID1 DS    CL6                     VOLSER NUMBER
ZWVOLID2 DS    CL6                     VOLSER NUMBER
PNAME    DS    CL4                     NAME OF PROGRAM
         SPACE 2
*
*      PARAMETER LIST OF 'FASID'.
*
         SPACE 1
PARMLIST DS    0F
PASIDADR DS    F                       ADDRESS OF ASID
PJNMEADR DS    F                       ADDRESS OF JOB NAME
PSNMEADR DS    F                       ADDRESS OF STEP NAME
PJOBNAME DS    CL8                     JOB NAME
PSTEPNME DS    CL8                     STEP NAME
         EJECT
*
*      PARAMETER FLAGS.
*
         SPACE 1
PRMFLAGS DS    0CL8
PARMFLAG DS    C                       FLAG BYTE OF PARAMETER
TAPEFLAG DS    C                       FLAG BYTE OF TAPE SELECT.
DASDFLAG DS    C                       FLAG BYTE OF DASD SELECT.
URFLAG   DS    C                       FLAG BYTE OF UR SELECT.
TPFLAG   DS    C                       FLAG BYTE OF TP SELECT.
GRAPFLAG DS    C                       FLAG BYTE OF GRAPHIC SELECT.
MSSFLAG  DS    C                       FLAG BYTE OF MSS SELECT.
ONLFLAG  DS    C                       FLAG BYTE OF ONLINE SELECT.
OFFLFLAG DS    C                       FLAG BYTE OF OFFLINE SELECT.
RDYFLAG  DS    C                       FLAG BYTE OF READY SELECT.
NOPMFLAG DS    C                       FLAG BYTE OF DEFAULT DISPLAY
PFLAGLEN EQU   *-PRMFLAGS              LENGTH OF FLAG BYTES
         SPACE 2
*
*      TABLE WITH PARAMETER ADDRESSES OR VOLSER NUMBERS.
*
*      BYTE1 - LENGTH OF PARAMETER OR X'80'
*      BYTES2-7 - PARAMETER VALUE(S)
*
         SPACE 1
TABPVOL1 DS    10CL7                   TABLE 1
ENDTPVOL EQU   *
         DS    CL7
TABPVOL2 DS    CL77                    TABLE 2
         SPACE 2
*
*      MESSAGE AREA.
*
*      BYTE1-66: WTO MESSAGE LINE
*      BYTE67-68: X'0000', IF MESSAGE LINE AVAILABLE
*
         SPACE 1
         DS    0F
MSGTAB   DS    10CL68
ENDTAB1  EQU   *
         DS    0F
HEADER   DS    CL(LENMSG01)            HEAD LINE
         DS    0F
WTOR     DS    CL(LENMSG05)            WTOR LINE
         DS    0F
         SPACE 1
WKAREALN EQU   *-WKAREA                LENGTH OF WORK AREAS
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE AREA.                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
MESSAGE  DSECT
         ORG   MESSAGE+5
MSGNAME  DS    CL4                     PROGRAM NAME
         DS    CL3
MSGVOLID DS    CL6                     VOLUME SERIAL NUMBER
         DS    CL1
MSGADR   DS    CL3                     UNIT ADDRESS
         DS    CL1
MSGLINE  DS    CL3                     ON/OFF LINE
         DS    CL1
MSGALLOC DS    CL8                     UNIT ALLOC
         DS    CL1
MSGREADY DS    CL2                     UNIT READY
         DS    CL1
*SGSTAT  DS    CL6                     UNIT STATUS                /XUCB
MSGSTAT  DS    CL5                     UNIT STATUS                /XUCB
         DS    CL1
MSGPATH0 DS    CL2                     PATHS FROM CPU 0
         DS    CL1
MSGPATH1 DS    CL2                     PATHS FROM CPU 1
         DS    CL1
MSGATTR1 DS    CL7                     ATTRIBUTES
         ORG   *-3
MSGATTR2 DS    CL3                     ATTRIBUTES
         DS    CL1
*SGUCBAD DS    CL4                     UCB ADDRESS                /XUCB
MSGUCBAD DS    CL6                     UCB ADDRESS                /XUCB
         ORG   MESSAGE+13
WTORNAME DS    CL4                     PROGRAM NAME
         ORG   MESSAGE+66
MSGFLAG  DS    CL2                     FLAG BYTES
         EJECT
***********************************************************************
*                                                                     *
*      INPUT PARAMETER AREA.                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         XPARM
PARMEND  EQU   *
         SPACE 3
***********************************************************************
*                                                                     *
*      UCB ADDRESS TABLE.                                             *
*                                                                     *
***********************************************************************
         SPACE 3
UCBADTAB DSECT
         DS    1024H
UCBTBEND DS    H             =X'FFFF'
UCBTBLEN EQU   *-UCBADTAB
         EJECT
***********************************************************************
*                                                                     *
*      UCB-DEFINITIONEN.                                              *
*                                                                     *
***********************************************************************
         SPACE 2
UCB      DSECT
         IEFUCBOB LIST=YES
         CVT DSECT=YES                 CVT MAPPING                /XUCB
         END   DVOL
./ ADD NAME=FIND
         TITLE 'XFIND                            X-COMMAND FIND'
FIND     CSECT
         SPACE 3
* MVS/XA UCBSCAN                               6.7.83 BMW         /XUCB
***********************************************************************
*                                                                     *
*      IDENTIFICATION DIVISION.                                       *
*                                                                     *
*      PROGRAM-ID. FIND.                                              *
*      AUTHOR. EHNERT.                                                *
*      INSTALLATION. BAYER AG, LEVERKUSEN, RECHENZENTRUM.             *
*      DATE-WRITTEN. 11. 12. 1974.                                    *
*      REMARKS.                                                       *
*                                                                     *
*          DAS X-COMMAND 'FIND' GIBT INFORMATIONEN UEBER DAS ALS      *
*          PARAMETER SPEZIFIZIERTE MITGLIED PO-ORGANISIERTER DATEIEN  *
*          AUS.                                                       *
*                                                                     *
*          ZUSAETZLICH KOENNEN BIS ZU ZEHN VOLUME-MASKEN ANGE-        *
*          GEBEN WERDEN, MIT DEREN HILFE EINE AUSWAHL DER VOLUMES GE- *
*          TROFFEN WIRD, DIE DAS X-COMMAND NACH DEM SPEZIFIZIERTEN    *
*          MEMBER DURCHSUCHT. DABEI STEHT '*' FUER JEDES ZEICHEN AN   *
*          DIESER STELLE. SIND KEINE MASKEN ANGEGEBEN, SO WERDEN      *
*          STANDARDMAESSIG ALLE ONLINE LIBRARYS DURCHSUCHT.           *
*                                                                     *
*          WIRD ZUDEM DER PARAMETER 'TEST' ANGEGEBEN, SO WERDEN ALLE  *
*          FEHLERNACHRICHTEN AUSGEGEBEN, DIE BEIM FEHLEN DES PARAME-  *
*          TERS STANDARDMAESSIG UNTERDRUECKT WERDEN.                  *
*                                                                     *
*          DURCH DIE ZUM AUFSUCHEN DES MEMBERS NOTWENDIGE DYNAMIC     *
*          ALLOCATION, DIE IM EXTERNEN UNTERPROGRAMM 'DYNALC' DURCH-  *
*          GEFUEHRT WIRD, KANN DAS X-COMMAND 'FIND' NUR IM SYSTEM     *
*          OS/VS2 (MVS) VERWNDET WERDEN.                              *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      BEGINN DER VERARBEITUNG.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      SICHERN DER REGISTER,
*      SPEICHERPLATZANFORDERUNG FUER ARBEITSBEREICHE,
*      ZUORDNUNG DER BASISREGISTER.
*
         SPACE 1
         XSAVE R12,,FIND,ABERL
         SPACE 1
         LR    R11,R1        BASISREG. FUER DSECT PARMAREA INITIALIS.
         USING UCB,R3        BASISREG. FUER DSECT UCB ZUORDNEN
         USING MESSAGE,R10   BASISREG. FUER DSECT MESSAGE ZUORDNEN
         USING PARMAREA,R11  BASISREG. FUER DSECT PARMAREA ZUORDNEN
         USING ABER,R13      BASISREG. FUER DSECT ABER ZUORDNEN
         EJECT
*
*      ABFRAGE DES SYSTEMS.
*
         SPACE 1
         L     R2,16         ADRESSE DER CVT LADEN
         TM    116(R2),X'01' SYSTEM = OS/VS2 (MVS) ?
         BZ    FEHLSYS       NEIN, VERZWEIGEN ZUR FEHLERROUTINE
*
*      INITIALISIERUNG DER SPEICHER.
*
         SPACE 1
         MVC   BLDLFF,=H'1'  ANZ. DER MEMBER ENTRIES FUER BLDL EINTR.
         MVC   BLDLLL,=H'14' ENTRY-LAENGE FUER BLDL EINTRAGEN
         MVI   FOUNDBIT,X'00' SCHALTER FUER GEFUNDENE MEMBER LOESCHEN
         MVI   TESTBYTE,X'00' SCHALTER FUER TEST LOESCHEN
         MVC   CAMOBT(CAMLEN),CAMCON CAMLIST-MACRO UEBERTRAGEN
         LA    R4,CCHHR      ADRESSE VON CCHHR LADEN
         ST    R4,CADRCCHH       UND SPEICHERN
         LA    R4,CAMWKFLD   ADRESSE VON CAMWKFLD LADEN
         ST    R4,CADRCAMW       UND SPEICHERN
         LA    R4,EIN        ADRESSE DES DCB LADEN
         ST    R4,DCBADDR        UND SPEICHERN
         MVI   DCBADDR,X'80' ENDE DER PARAMETERLISTE SPEZIFIZIEREN
         SPACE 2
*
*      PARAMETERLISTE FUER 'DYNALC' UEBERTRAGEN.
*
         SPACE 1
         LA    R4,DALPCON    ADRESSEN DER
         LA    R6,DALPWKS        PARAMETERLISTEN LADEN
         LA    R5,DALPLEN    LAENGE DER
         LR    R7,R5             PARAMETERLISTEN LADEN
         MVCL  R6,R4         PARAMETERLISTE UEBERTRAGEN
         LA    R4,RETCODE    ADRESSE DER RETURNCODE-BEREICHE LADEN
         LA    R5,DALTYPE    ADRESSE DES DYNALLOC-TYPS LADEN
         STM   R4,R5,ADDRRC  ADRESSEN IN DER PARAMETERLISTE SPEICHERN
         LA    R4,DALDDNAM   ADRESSE DES DDNAME LADEN
         ST    R4,ADDRDDN        UND SPEICHERN
         LA    R4,DALUNIT    ADRESSE DES EINHEITENTYPS LADEN
         STCM  R4,7,ADDRUNIT+1   UND SPEICHERN
         MVC   DALDDNAM,DCBDDNAM
         MVC   DALTYPE,=CL8'DSALLOC' DYNALLOC-TYP SPEZIFIZIEREN
         MVC   DALWAIT,=CL8' ' ERASE WAIT REQUEST INDICATOR
         EJECT
*
*      ADRESSEN-SPEICHER INITIALISIEREN.
*
         SPACE 1
         LA    R3,ENDTAB1    ADRESSE DES TABELLENENDES LADEN
         LA    R4,ENDTPVOL   ADRESSE DES TABELLENENDES LADEN
         LA    R5,PARMENDE   ADRESSE DES ENDES DES PARAMETERBER. LADEN
         STM   R3,R5,ADDRETB1 ADRESSEN SPEICHERN
         MVC   UCMID,REG4    UCM-ID. DES AUFRUFERS UEBERTRAGEN
         SPACE 2
*
*      FLAG BYTS IN NACHRICHTENBEREICHEN SETZEN.
*
         SPACE 1
         LA    R10,TABNACHR  ADRESSE DER INFORMATIONSNACHRICHTEN-
         ST    R10,SAVE1R10      TABELLE LADEN UND SPEICHERN
         LA    R10,TABNACHR-72 ADRESSE DER NACHRICHTEN-TABELLEN LADEN
CLEARMSS EQU   *
         LA    R10,72(R10)   R10 ERHOEHEN
         C     R10,ADDRETB1  TABELLENENDE ERREICHT ?
         BNL   GETSADDR      JA, VERZW. ZUR ABFRAGE DES SYSTEMS
         MVC   MESSFLAG,=X'FFFF' FLAG BYTES SETZEN
         B     CLEARMSS      VERZWEIGEN
         SPACE 2
GETSADDR EQU   *
         LOAD  EP=DYNALC               LOAD ENTRY POINT OF SUBROUTINE
         ST    R0,DYNADDR                  AND STORE IT
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DER EINGABEPARAMETER.                             *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      VERARBEITUNG DES MEMBER NAME.
*
         SPACE 1
GETPARM  EQU   *
         LA    R4,TEXT+4     R4 INITIALISIEREN
         SR    R5,R5         R5 LOESCHEN
         MVC   MEMBNAME,=CL8' ' ZWISCHENSPEICHER LOESCHEN
         CLI   0(R4),C','    PARAMETER ANGEGEBEN ?
         BNE   FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
NXTPNAM  EQU   *
         LA    R4,1(R4)      R4 ERHOEHEN
         C     R4,ADDREPRM   PARAMETERBEREICH UEBERSCHRITTEN ?
         BNL   FEHLPARM      JA, VERZW. ZUR FEHLERROUTINE
         CLI   0(R4),C'*'    1. PARAMETER = MEMBER NAME ?
         BE    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         CLI   0(R4),C','    ENDE DES MEMBER NAME ERREICHT ?
         BE    ENDPNAM       JA, VERZWEIGEN
         CLI   0(R4),X'00'   ENDE DER PARAMETERKETTE ERREICHT ?
         BE    ENDPNAM       JA, VERZWEIGEN
         CLI   0(R4),C' '    ENDE DER PARAMETERKETTE ERREICHT ?
         BE    ENDPNAM       JA, VERZWEIGEN
         LA    R5,1(R5)      R5 ERHOEHEN
         B     NXTPNAM       VERZWEIGEN
ENDPNAM  EQU   *
         C     R5,=F'8'      LAENGE DES MEMBER NAME GUELTIG ?
         BH    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         LTR   R5,R5         MEMBER NAME ANGEGEBEN ?
         BZ    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         BCTR  R5,0          R5 UM EINS VERMINDERN
         EX    R5,MOVEPNAM   VERZW. ZUR UEBERTR. DES MEMBER NAME
         CLI   MEMBNAME,C' ' MEMBER NAME GUELTIG ?
         BE    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         EJECT
*
*      VERARBEITUNG DER VOLUME SERIAL NUMBERS.
*
         SPACE 1
         LA    R6,TABPVOL1   ADRESSE DER VOLSER-TABELLE LADEN
NXTPVOLI EQU   *
         CLI   0(R4),X'00'   ENDE DER PARAMETERKETTE ERREICHT ?
         BE    ENDPARM       JA, VERZWEIGEN
         CLI   0(R4),C' '    ENDE DER PARAMETERKETTE ERREICHT ?
         BE    ENDPARM       JA, VERZWEIGEN
         SR    R5,R5         R5 LOESCHEN
         LR    R7,R4         ADRESSE UMLADEN
NXTPVOL  EQU   *
         LA    R4,1(R4)      R4 ERHOEHEN
         C     R4,ADDREPRM   PARAMETERBEREICH UEBERSCHRITTEN ?
         BNL   FEHLPARM      JA, VERZW. ZUR FEHLERROUTINE
         CLI   0(R4),C','    ENDE DER VOLID ERREICHT ?
         BE    ENDPVOL       JA, VERZWEIGEN
         CLI   0(R4),X'00'   ENDE DER PARAMETERKETTE ERREICHT ?
         BE    ENDPVOL       JA, VERZWEIGEN
         CLI   0(R4),C' '    ENDE DER PARAMETERKETTE ERREICHT ?
         BE    ENDPVOL       JA, VERZWEIGEN
         CLI   0(R4),C'*'    BELIEBIGES ZEICHEN AN DIESER STELLE ?
         BNE   *+8           NEIN, VERZWEIGEN
         MVI   0(R4),X'FF'   ZEICHEN UMSETZEN
         LA    R5,1(R5)      R5 ERHOEHEN
         B     NXTPVOL       VERZWEIGEN
ENDPVOL  EQU   *
         C     R5,=F'4'      LAENGE DES PARAMETERS = 4 ?
         BNE   *+22          NEIN, VERZWEIGEN
         CLC   1(4,R7),=C'TEST' PARAMETER = TEST ?
         BNE   *+12          NEIN, VERZWEIGEN
         MVI   TESTBYTE,X'FF' FLAG BYTE SETZEN
         B     NXTPVOLI      VERZWEIGEN
         C     R5,=F'6'      LAENGE DER VOLID GUELTIG ?
         BH    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         LTR   R5,R5         VOLID ANGEGEBEN ?
         BZ    ALLVOL        NEIN, VERZWEIGEN
         BCTR  R5,0          R5 UM EINS VERMINDERN
         EX    R5,MOVEPVOL   VERZW. ZUR UEBERTR. DER VOLSER NUMBER
         STC   R5,0(R6)      LAENGENANGABE IN TABELLE EINTRAGEN
         LA    R6,7(R6)      R6 ERHOEHEN
         C     R6,ADDRETPV   ENDE DER VOLSER-TABELLE ERREICHT ?
         BNL   ENDPARM       JA, VERZWEIGEN
         B     NXTPVOLI      VERZWEIGEN
ALLVOL   EQU   *
         LA    R6,TABPVOL1   ADRESSE DER VOLSER-TABELLE LADEN
         EJECT
ENDPARM  EQU   *
         MVI   0(R6),X'80'   TABELLENENDE SPEZIFIZIEREN
         MVC   TABPVOL2,TABPVOL1 VOLSER-TABELLE KOPIEREN
         LA    R6,TABPVOL2-1 ADRESSE DER VOLSER-TABELLE LADEN
NXTBYTE  EQU   *
         LA    R6,1(R6)      R6 ERHOEHEN
         CLI   0(R6),X'80'   ENDE DER TABELLE ERREICHT ?
         BE    GETUCB        JA, VERZWEIGEN
         CLI   0(R6),X'FF'   ZEICHEN IN DER TABELLE = X'FF' ?
         BNE   NXTBYTE       NEIN, VERZWEIGEN
         MVI   0(R6),X'00'   ZEICHEN UMSETZEN
         B     NXTBYTE       VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DES UCB-VEKTORS.                                  *
*                                                                     *
***********************************************************************
         SPACE 3
GETUCB   EQU   *
         DROP  R11           R11 FREIGEBEN
*        GETMAIN R,LV=VEKLEN,SP=125                               /XUCB
*        LR    R11,R1        BASISREG. FUER DSECT VEKTOR INIT.    /XUCB
*        USING VEKTOR,R11    BASISREG. FUER DSECT VEKTOR ZUORDNEN /XUCB
*        ST    R11,UCBADDR   VEKTOR-ADRESSE SPEICHERN             /XUCB
*        MVI   UCBADDR,X'80' OPTION BYTE SETZEN                   /XUCB
*        LA    R1,UCBADDR    ADRESSE DER VEKTOR-ADRESSE LADEN     /XUCB
*        LINK  EP=GETUCBS              LINK TO SUBROUTINE         /XUCB
*        LA    R2,VEKTOR     ADRESSE DES VEKTORS LADEN            /XUCB
*        BCTR  R2,0          R2 UM EINS VERMINDERN                /XUCB
*        BCTR  R2,0          R2 UM EINS VERMINDERN                /XUCB
*        SR    R3,R3         R3 LOESCHEN                          /XUCB
*        MVC   VEKEND,=X'FFFF' VEKTOR-ENDE SPEZIFIZIEREN          /XUCB
         XUCBSCAN R2,X'20'   UCBSCAN NUR FUER DASD                /XUCB
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DER UCB-INFORMATIONEN.                            *
*                                                                     *
***********************************************************************
         SPACE 3
NEXTUCB  EQU   *
*        LA    R2,2(R2)      R2 ERHOEHEN                          /XUCB
*        CLC   0(2,R2),=X'FFFF' ENDE DES VEKTORS ERREICHT ?       /XUCB
*        BE    ABSCHLUS      JA, VERZW. ZUM ABSCHLUSS DER VERARBEITUNGB
*        ICM   R3,3,0(R2)    ADRESSE DES UCB LADEN                /XUCB
*        CLI   UCBDVCLS,X'20' DEVICE = DASD ?                     /XUCB
*        BNE   NEXTUCB       NEIN, VERZWEIGEN                     /XUCB
         XUCBGET R2,ABSCHLUS,ABSCHLUS                             /XUCB
         LR    R3,R1         ADRESSE DES UCB LADEN                /XUCB
         TM    DEVSTAT,X'80' EINHEIT ONLINE ?
         BZ    NEXTUCB       NEIN, VERZWEIGEN
         TM    DEVSTAT,X'10' EINHEIT IM UNLOAD ?
         BO    NEXTUCB       JA, VERZWEIGEN
         CLC   UCBVOLI,=XL6'0' VOLID VORHANDEN ?
         BE    NEXTUCB       NEIN, VERZWEIGEN
         TM    UCB+17,X'08'           VIRTUAL DEVICE ?
         BO    TSTVVOL                BRANCH IF YES
         SR    R5,R5         R5 LOESCHEN
         TM    TABPVOL1,X'80'          TABLES FILLED ?
         BO    MVCVOLID                BRANCH IF NOT
         LA    R6,TABPVOL1-7 ADRESSE DER VOLSER-TABELLE LADEN
         LA    R7,TABPVOL2-7 ADRESSE DER VOLSER-TABELLE LADEN
NXTVOLID EQU   *
         LA    R6,7(R6)      R6 ERHOEHEN
         LA    R7,7(R7)      R7 ERHOEHEN
         TM    0(R6),X'80'   TABELLENENDE ERREICHT ?
         BO    NEXTUCB       JA, VERZWEIGEN
         IC    R5,0(R6)      LAENGENANGABE LADEN
         MVC   ZWVOLID1,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         MVC   ZWVOLID2,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         EX    R5,NCVOLID1   VERZW. ZUR UEBERLAGERUNG DER VOLID
         EX    R5,OCVOLID2   VERZW. ZUR UEBERLAGERUNG DER VOLID
         CLC   ZWVOLID1,ZWVOLID2 VOLID = AUSGEW. VOLID ?
         BNE   NXTVOLID      VERZW., WENN VOLID ^= AUSGEW. VOLID
         B     MVCVOLID
         SPACE 2
TSTVVOL  EQU   *
         LA    R6,TABPVOL1-7 ADRESSE DER VOLSER-TABELLE LADEN
NXTVVOL  EQU   *
         LA    R6,7(R6)      R6 ERHOEHEN
         TM    0(R6),X'80'   TABELLENENDE ERREICHT ?
         BO    NEXTUCB       JA, VERZWEIGEN
         CLC   UCBVOLI,1(R6) VOLID = AUSGEW. VOLID ?
         BNE   NXTVVOL       VERZW., WENN VOLID ^= AUSGEW. VOLID
MVCVOLID EQU   *
         LA    R4,UCBVOLI    ADRESSE DER
         ST    R4,ADDRVOLS       VOLSER NUMBER LADEN UND
         ST    R4,CADRUCBV       SPEICHERN
         EJECT
***********************************************************************
*                                                                     *
*      CCHHR DER VTOC BERECHNEN.                                      *
*                                                                     *
***********************************************************************
         SPACE 3
         SR    R4,R4         R4 LOESCHEN
         IC    R4,DEVCODE    DEVICE CODE LADEN
         SLA   R4,2          DEVICE CODE * 4
         LA    R5,TABTCAP    ADRESSE DER TABTCAP-TABELLE LADEN
         AR    R5,R4         ADRESSE DES TABELLENELEMENTS
         CLI   0(R5),X'80'   DEVICE RESERVED ?
         BE    FEHLDEVC      JA, VERZW. ZUR FEHLERROUTINE
         LH    R6,UCBVTOC    REL. ADRESSE DER VTOC LADEN
         SRDA  R6,32         R6 --> R7
         D     R6,0(R5)      DIV. TT / SPECIFIC TRACKS/CYLINDER
         STH   R7,CCHHR      CC-ADRESSE DER VTOC SPEICHERN
         STH   R6,CCHHR+2    HH-ADRESSE DER VTOC SPEICHERN
         MVI   CCHHR+4,X'01' R-ADRESSE DER VTOC
         SR    R5,R5         R5 LOESCHEN
         IC    R5,UCBVTOC+2  R-ADRESSE DER VTOC LADEN
         STH   R5,LFDRECNO   LAUFENDE SATZNUMMER SPEICHERN
         STH   R6,LFDTRKNO   LAUFENDE SPURNUMMER SPEICHERN
         STH   R7,LFDCYLNO   LAUFENDE ZYLINDERNUMMER SPEICHERN
         LA    R5,TABUNIT    ADRESSE DER EINHEITENTYPEN-TABELLE LADEN
         AR    R5,R4
         MVC   DALUNIT,=CL8' ' ZWISCHENSPEICHER LOESCHEN
         MVC   DALUNIT(4),0(R5) EINHEITENTYP UEBERTRAGEN
         EJECT
***********************************************************************
*                                                                     *
*      DSCB4 LESEN UND AUSWERTEN.                                     *
*                                                                     *
***********************************************************************
         SPACE 3
         OBTAIN CAMOBT       DSCB LESEN
         LTR   R15,R15       RC = 0 ?
         BNZ   FEHLOBT       NEIN, VERZW. ZUR FEHLERROUTINE
         CLI   DS4FMTID,X'F4' 1. DSCB = DSCB4 ?
         BNE   FEHLDSCB      NEIN, VERZW. ZUR FEHLERROUTINE
         MVC   MTRKNO,DS4DEVSZ+2 MAX.SPURADRESSE SPEICHERN
         MVI   MRECNO,X'00'
         MVC   MRECNO+1(1),DS4DEVT MAX. SATZADRESSE SPEICHERN
         MVC   MDS1ADDR(4),DS4HPCR HOECHSTE ADRESSE
         MVI   MDS1R,X'00'             VON DSCB1
         MVC   MDS1R+1(1),DS4HPCR+4    SPEICHERN
         EJECT
***********************************************************************
*                                                                     *
*      DSCB1 LESEN UND AUSWERTEN,                                     *
*      AUFRUF DES UNTERPROGRAMMS 'DYNALC'.                            *
*                                                                     *
***********************************************************************
         SPACE 3
NXTDSCB  EQU   *
         LH    R4,LFDRECNO   LAUFENDE SATZNUMMER LADEN
         LA    R4,1(R4)      R4 ERHOEHEN
         CH    R4,MRECNO     SPUR UEBERSCHRITTEN ?
         BNH   NXTREC        NEIN, VERZWEIGEN
         LH    R4,LFDTRKNO   LAUFENDE SPURNUMMER LADEN
         LA    R4,1(R4)      R4 ERHOEHEN
         CH    R4,MTRKNO     ZYLINDER UEBERSCHRITTEN ?
         BL    NXTTRK        NEIN, VERZWEIGEN
         LH    R4,LFDCYLNO   LAUFENDE ZYLINDERNUMMER LADEN
         LA    R4,1(R4)      R4 ERHOEHEN
         STH   R4,LFDCYLNO   NEUE ZYLINDERNUMMER
         STH   R4,CCHHR      SPEICHERN
         SR    R4,R4         R4 LOESCHEN
NXTTRK   EQU   *
         STH   R4,LFDTRKNO   NEUE SPURADRESSE
         STH   R4,CCHHR+2        SPEICHERN
         LA    R4,1          R4 = 1
NXTREC   EQU   *
         STH   R4,LFDRECNO   NEUE SATZADRESSE
         STC   R4,CCHHR+4        SPEICHERN
         CLC   LFDADDR(6),MDS1ADDR ENDE DER DSCB1 ERREICHT ?
         BH    NEXTUCB       JA, VERZW. ZUM LESEN DES NAECHSTEN UCB
         OBTAIN CAMOBT       NAECHSTEN DSCB LESEN
         LTR   R15,R15       RC = 0 ?
         BNZ   FEHLOBT       NEIN, VERZW. ZUR FEHLERROUTINE
         CLI   DS1FMTID,C'1' DSCB = DSCB1 ?
         BNE   NXTDSCB       NEIN, VERZW. ZUM LESEN DES NAECHSTEN DSCB
         TM    DS1DSORG,X'02' DATA SET PARTITIONED ?
         BZ    NXTDSCB       NEIN, VERZW. ZUM LESEN DES NAECHSTEN DSCB
         LA    R5,DS1DSNAM   ADRESSE DES DSNAME LADEN
         ST    R5,ADDRDSN        UND SPEICHERN
DYNALLOC EQU   *
         MVC   DALDDNAM,=CL8' ' DDNAME LOESCHEN
         LA    R1,DALPWKS    ADR. DER PARAMETERLISTE F. 'DYNALC' LADEN
         L     R15,DYNADDR   ADRESSE DES UNTERPROGRAMMS 'DYNALC' LADEN
         BALR  R14,R15       VERZW. ZUM UNTERPROGRAMM 'DYNALC'
*                            (DATA SET NAME ALLOCATION)
         L     R15,SAVE1R15  RC LADEN
         LTR   R15,R15       RC = 0 ?
         BNZ   FEHLDYNA      NEIN, VERZW. ZUR FEHLERROUTINE
         MVC   EIN(DCBLEN),EINCONST DCB UEBERTRAGEN
         MVC   DCBDDNAM,DALDDNAM DDNAME UEBERTRAGEN
         EJECT
***********************************************************************
*                                                                     *
*      MEMBER NAME IN DER DIRECTORY AUFSUCHEN.                        *
*                                                                     *
***********************************************************************
         SPACE 3
         LA    R1,DCBADDR    ADRESSE DER PARAMETERLISTE LADEN
         SVC   19            DATEI EROEFFNEN
         TM    DCBOFLGS,X'10' DATEI EROEFFNET ?
         BZ    FEHLOPEN      NEIN, VERZW. ZUR FEHLERROUTINE
         SPACE 1
         BLDL  EIN,BLDLLIST  ENTRY LIST FUELLEN
         ST    R15,SAVE1R15  RC SPEICHERN
         SPACE 1
         LA    R1,DCBADDR    ADRESSE DER PARAMETERLISTE LADEN
         SVC   20            DATEI SCHLIESSEN
         SPACE 1
         L     R15,SAVE1R15  BLDL-RC LADEN
         LTR   R15,R15       RC = 0 ?
         BZ    NXTMESS       JA, VERZW. ZUR NACHRICHTENROUTINE
         C     R15,=F'8'     RC = 8 ?
         BE    FEHLBLDL      JA, VERZW. ZUR FEHLERROUTINE
         B     NXTDSCB       VERZW. ZUM LESEN DES NAECHSTEN DSCB
         EJECT
***********************************************************************
*                                                                     *
*      AUFBEREITEN DER INFORMATIONSNACHRICHT.                         *
*                                                                     *
***********************************************************************
         SPACE 3
NXTMESS  EQU   *
         L     R10,SAVE1R10  R10 INITIALISIEREN
         MVC   MESSFLAG,=XL6'0' FLAG BYTES LOESCHEN
         MVC   MESSAGE(LENMSG03),MSG03 NACHRICHTENZEILE UEBERTRAGEN
         MVC   MS3DSN,DS1DSNAM DSNAME UEBERTRAGEN
         MVC   MS3VOLS,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         MVC   MS3UNIT,UCBNAME UNIT NAME UEBERTRAGEN
         UNPK  MS3TTR(7),BLDLTTR(4) TTR DES MEMBER UEBERTRAGEN
         TR    MS3TTR,TABHEXA-240 TTR AUFBEREITEN
         LA    R9,MS3INFO    ADRESSE DES INFORMATIONS-FELDES LADEN
         TM    DS1RECFM,X'C0' RECFM = U ?
         BNO   *+12          NEIN, VERZWEIGEN
         MVI   0(R9),C'U'    RECFM = U SPEZIFIZIEREN
         B     RECFMBLK      VERZWEIGEN
         TM    DS1RECFM,X'80' RECFM = F ?
         BNO   *+12          NEIN, VERZWEIGEN
         MVI   0(R9),C'F'    RECFM = F SPEZIFIZIEREN
         B     RECFMBLK      VERZWEIGEN
         MVI   0(R9),C'V'    RECFM = V SPEZIFIZIEREN
RECFMBLK EQU   *
         LA    R9,1(R9)      R9 ERHOEHEN
         TM    DS1RECFM,X'10' RECFM = BLOCKED ?
         BZ    *+12          NEIN, VERZWEIGEN
         MVI   0(R9),C'B'    RECFM = BLOCKED SPEZIFIZIEREN
         LA    R9,1(R9)      R9 ERHOEHEN
         TM    DS1RECFM,X'08' SPANNED, STANDARD ?
         BZ    *+12          NEIN, VERZWEIGEN
         MVI   0(R9),C'S'    SPANNED, STANDARD SPEZIFIZIEREN
         LA    R9,1(R9)      R9 ERHOEHEN
         TM    DS1RECFM,X'06' CONTROL CARACTER ?
         BZ    NOCONTC       NEIN, VERZWEIGEN
         TM    DS1RECFM,X'04' CONTROL CARACTER = A ?
         BZ    *+12          NEIN, VERZWEIGEN
         MVI   0(R9),C'A'    CONTROL CARACTER = A SPEZIFIZIEREN
         B     *+8           VERZWEIGEN
         MVI   0(R9),C'M'    CONTROL CARACTER = M SPEZIFIZIEREN
         LA    R9,1(R9)      R9 ERHOEHEN
         EJECT
NOCONTC  EQU   *
         MVI   0(R9),C','    KOMMA EINTRAGEN
         SR    R8,R8         R8 LOESCHEN
         ICM   R8,3,DS1BLKL  BLOCKLAENGE LADEN
         BAL   R14,CONVLEN   VERZW. ZUR UMWANDLUNG DER BLOCKLAENGE
         MVI   0(R9),C','    KOMMA EINTRAGEN
         CLI   MS3INFO,C'U'  RECFM = U ?
         BNE   *+12          NEIN, VERZWEIGEN
         MVI   1(R9),C'*'    RECL = 0 SPEZIFIZIEREN
         B     *+12          VERZWEIGEN
         ICM   R8,3,DS1RECL  SATZLAENGE LADEN
         BAL   R14,CONVLEN   VERZW. ZUR UMWANDLUNG DER SATZLAENGE
         LA    R14,NXTDSCB   RUECKSPRUNGADRESSE LADEN
         LA    R10,72(R10)   R10 ERHOEHEN
         C     R10,ADDRETB1  TABELLENENDE ERREICHT ?
         BNL   WTONACHR      JA, VERZW. ZUR AUSGABE DER NACHRICHTEN
         ST    R10,SAVE1R10  LAUFENDE ADRESSE SPEICHERN
         B     NXTDSCB       VERZW. ZUR VERARB. DES NAECHSTEN DSCB
         SPACE 2
*
*      BLOCK- UND SATZLAENGEN IN DEZIMALZAHLEN UMWANDELN.
*
         SPACE 1
CONVLEN  EQU   *
         CVD   R8,DBWORD     LAENGE UMWANDELD
         UNPK  ZWFELD,DBWORD DEZIMALZAHL ENTPACKEN
         OI    ZWFELD+14,C'0' VORZEICHEN MODIFIZIEREN
         LA    R6,ZWFELD-1   ADRESSE VON ZWFELD LADEN
         LA    R7,ZWFELD+14  ADRESSE DES LETZTEN BYTES LADEN
CONVSCHL EQU   *
         LA    R6,1(R6)      R6 ERHOEHEN
         CR    R6,R7         ZWFELD UEBERSCHRITTEN ?
         BNL   LENOK         JA, VERZWEIGEN
         CLI   0(R6),C'0'    FUEHRENDE NULL ?
         BE    CONVSCHL      JA, VERZWEIGEN
LENOK    EQU   *
         SR    R7,R6         NEIN, LAENGE DER ZAHL FESTSTELLEN
         EX    R7,MOVELEN    VERZW. ZUR UEBERTR. DER LAENGE
         AR    R9,R7         R9 ERHOEHEN
         LA    R9,2(R9)      R9 ERHOEHEN
         BR    R14           VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      ABSCHLUSSROUTINEN.                                             *
*                                                                     *
***********************************************************************
         SPACE 3
ABSCHLUS EQU   *
*        FREEMAIN R,SP=125   SPEICHERPLATZ FREIGEBEN              /XUCB
         XUCBOFF R2          WORKAREA FREIGEBEN                   /XUCB
         LA    R10,TABNACHR  ADRESSE DER NACHRICHTEN-TABELLE LADEN
         CLC   MESSFLAG,=XL6'0' SIND NOCH NACHRICHTEN OFFEN ?
         BNE   *+8           NEIN, VERZWEIGEN
         BAL   R14,WTONACHR  VERZW. ZUR AUSGABE DER NACHRICHTEN
         TM    FOUNDBIT,X'80' MEMBER GEFUNDEN ?
         BO    ENDE          JA, VERZWEIGEN
         MVC   MESSAGE(LENMSG04),MSG04 MSG04 UEBERTRAGEN
         MVC   MS1NAME,MEMBNAME MEMBER NAME UEBERTRAGEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MESSAGE) NACHRICHT AUSGEBEN
         SPACE 1
ENDE     EQU   *
         XRETURN ,R
         EJECT
***********************************************************************
*                                                                     *
*      NACHRICHTENAUSGABE.                                            *
*                                                                     *
***********************************************************************
         SPACE 3
WTONACHR EQU   *
         ST    R14,SAVE1R14  RUECKSPRUNGADRESSE SPEICHERN
         LA    R10,MSG01ZW   ADRESSE DER NACHRICHTENZEILE SPEICHERN
         MVC   MSG01ZW(LENMSG01),MSG01 NACHRICHTENZEILE UEBERTRAGEN
         MVC   MS1NAME,MEMBNAME MEMBER NAME UEBERTRAGEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG01ZW) NACHRICHT AUSGEBEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG02)  NACHRICHT AUSGEBEN
         LA    R10,TABNACHR-72 ADRESSE DER NACHRICHTEN-TAB. LADEN
NXTINMSS EQU   *
         LA    R10,72(R10)   R10 ERHOEHEN
         C     R10,ADDRETB1  TABELLENENDE ERREICHT ?
         BNL   RETINMSS      JA, VERZWEIGEN
         CLC   MESSFLAG,=X'FFFF' FLAG BYTES GESETZT ?
         BE    RETINMSS      JA, VERZWEIGEN
         MVC   MESSFLAG,=X'FFFF' FLAG BYTES SETZEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MESSAGE) NACHRICHT AUSGEBEN
         B     NXTINMSS      VERZWEIGEN
RETINMSS EQU   *
         LA    R10,TABNACHR  ADRESSE DER NACHRICHTEN-TAB. LADEN
         ST    R10,SAVE1R10      UND SPEICHERN
         MVI   FOUNDBIT,X'80' BIT FUER GEFUNDENES MEMBER SETZEN
         L     R14,SAVE1R14  RUECKSPRUNGADRESSE LADEN
         BR    R14           VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      FEHLERROUTINEN.                                                *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      ROUTINE BEI UNGUELTIGEM SYSTEM.
*
         SPACE 1
FEHLSYS  EQU   *
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG05)  NACHRICHT AUSGEBEN
         B     ENDE          VERZWEIGEN
         SPACE 2
*
*      ROUTINE BEI FEHLER IN DER PARAMETERKETTE.
*
         SPACE 1
FEHLPARM EQU   *
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG06)
         B     ENDE          VERZWEIGEN
         SPACE 2
*
*      ROUTINE BEI DYNALC-FEHLER (RC ^= 0).
*
         SPACE 1
FEHLDYNA EQU   *
         LA    R10,TABFNACH  R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG07),MSG07 MSG07 UEBERTRAGEN
         MVC   MS7VOLS,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         MVC   MS7DSN,DS1DSNAM DSNAME UEBERTRAGEN
         LA    R14,NXTDSCB   RUECKSPRUNGADRESSE LADEN
         B     RETFEHL       VERZW. ZUM AUSGANG DER FEHLERROUTINEN
         EJECT
*
*      ROUTINE BEI BLDL-FEHLER (RC = 8).
*
         SPACE 1
FEHLBLDL EQU   *
         LA    R10,TABFNACH  R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG08),MSG08 MSG08 UEBERTRAGEN
         MVC   MS8DSN,DS1DSNAM DSNAME UEBERTRAGEN
         MVC   MS8VOLS,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         LA    R14,NXTDSCB   RUECKSPRUNGADRESSE LADEN
         B     RETFEHL       VERZW. ZUM AUSGANG DER FEHLERROUTINEN
         SPACE 2
*
*      ROUTINE BEI OBTAIN-FEHLER (RC ^= 0).
*
         SPACE 1
FEHLOBT  EQU   *
         LA    R10,TABFNACH  R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG09),MSG09 MSG09 UEBERTRAGEN
         MVC   MS9VOLS,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         ST    R15,SAVE1R15  RC SPEICHERN
         UNPK  MS9RC(3),SAVE1R15+3(2) RC UEBERTRAGEN
         MVI   MS9RC+2,C','
         TR    MS9RC,TABHEXA-240 RC AUFBEREITEN
         LA    R14,NXTDSCB   RUECKSPRUNGADRESSE LADEN
         B     RETFEHL       VERZW. ZUM AUSGANG DER FEHLERROUTINEN
         EJECT
*
*      ROUTINE BEI DSCB-FEHLER (1. DSCB NICHT FORMAT 4).
*
         SPACE 1
FEHLDSCB EQU   *
         LA    R10,TABFNACH  R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG10),MSG010 NACHRICHTENZEILE UEBERTRAGEN
         MVC   MS10VOLS,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         LA    R14,NEXTUCB   RUECKSPRUNGADRESSE LADEN
         B     RETFEHL       VERZWEIGEN
         SPACE 2
*
*      ROUTINE BEI RESERVED DEVICES.
*
         SPACE 1
FEHLDEVC EQU   *
         LA    R10,TABFNACH  R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG11),MSG011 NACHRICHTENZEILE UEBERTRAGEN
         UNPK  MS11DEVC(3),DEVCODE(2) DEVICE CODE UEBERTRAGEN
         TR    MS11DEVC,TABHEXA-240 DEVICE CODE AUFBEREITEN
         LA    R14,NEXTUCB   RUECKSPRUNGADRESSE LADEN
         B     RETFEHL       VERZWEIGEN
         EJECT
*
*      ROUTINE BEI OPEN-FEHLER.
*
         SPACE 1
FEHLOPEN EQU   *
         LA    R10,TABFNACH  R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG12),MSG012 NACHRICHTENZEILE UEBERTRAGEN
         MVC   MS8DSN,DS1DSNAM DSNAME UEBERTRAGEN
         MVC   MS8VOLS,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         LA    R14,RETFOPEN  RUECKSPRUNGADRESSE LADEN
         LA    R10,72(R10)   R10 ERHOEHEN
         B     RETFEHL       VERZWEIGEN
RETFOPEN EQU   *
         MVC   DALTYPE,=CL8'UNALLOC' DYNALLOC-TYP SPEZIFIZIEREN
         OI    ADDRDSN,X'80' ENDE DER PARAMETERKETTE SPEZIFIZIEREN
         LA    R1,DALPWKS    ADR. DER PARAMETERKETTE F. 'DYNALC' LADEN
         L     R15,DYNADDR   ADRESSE DES UNTERPROGRAMMS 'DYNALC' LADEN
         BALR  R14,R15       VERZW. ZUM UNTERPROGRAMM 'DYNALC'
*                            (DYNAMIC UNALLOCATION)
         NI    ADDRDSN,X'7F' ENDE DER PARAMETERKETTE ZURUECKSETZEN
         MVC   DALTYPE,=CL8'DSALLOC' DYNALLOC-TYP NEU SPEZIFIZIEREN
         L     R15,SAVE1R15  RC LADEN
         LTR   R15,R15       RC = 0 ?
         BNZ   FEHLDYNA      NEIN, VERZW. ZUR FEHLERROUTINE
         SPACE 2
*
*      AUSGANG DER FEHLERROUTINEN.
*
         SPACE 1
RETFEHL  EQU   *
         CLI   TESTBYTE,X'FF' FLAG BYTE GESETZT ?
         BNER  R14           NEIN, VERZWEIGEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MESSAGE) NACHRICHTENZEILE AUSGEBEN
         BR    R14           VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      BEFEHLE, DIE MIT HILFE DES EXECUTE-BEFEHLS AUSGEFUEHRT WERDEN. *
*                                                                     *
***********************************************************************
         SPACE 3
MOVEPNAM EQU   *
         MVC   MEMBNAME(0),9(R11) MEMBER NAME UEBERTRAGEN
MOVEPVOL EQU   *
         MVC   1(0,R6),1(R7) VOLID IN TABELLE UEBERTRAGEN
NCVOLID1 EQU   *
         NC    ZWVOLID1(0),1(R6) VOLID UEBERLAGERN
OCVOLID2 EQU   *
         OC    ZWVOLID2(0),1(R7) VOLID UEBERLAGERN
MOVELEN  EQU   *
         MVC   1(0,R9),0(R6) LAENGE UEBERTRAGEN
         SPACE 3
***********************************************************************
*                                                                     *
*      REGISTER EQUATES.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
R0       EQU   0             MACRO- UND-PARAMETER REGISTER
R1       EQU   1             MACRO- UND-PARAMETER REGISTER
R2       EQU   2             POINTER IN CVT / UCB LOOKUP TABLE
R3       EQU   3             POINTER FUER UCB
R4       EQU   4             ARBEITSREGISTER
R5       EQU   5             ARBEITSREGISTER
R6       EQU   6             ARBEITSREGISTER
R7       EQU   7             ARBEITSREGISTER
R8       EQU   8             ARBEITSREGISTER
R9       EQU   9             ARBEITSREGISTER
R10      EQU   10            POINTER IN NACHRICHTEN-TABELLEN
R11      EQU   11            BASISREG. FUER DSECT PARMAREA / VEKTOR
R12      EQU   12            BASISREG. FUER CSECT FIND
R13      EQU   13            BASISREG. FUER DSECT ABER
R14      EQU   14            RUECKSPRUNGADRESSEN
R15      EQU   15            CSECT-ADRESSEN UND RETURN CODES
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER KONSTANTEN-SPEICHER.                            *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      PARAMETERLISTE FUER 'DYNALC' (KONSTANTEN).
*
         SPACE 1
DALPCON  DS    0F
         DC    A(0)          ADRESSE VON RETCODE
         DC    A(0)          ADRESSE DES DYNALLOC-TYPS
         DC    A(0)          ADRESSE DES DDNAME
         DC    A(0)          ADRESSE DES DSNAME
         DC    A(0)
         DC    A(DALSTATS)   ADRESSE DES DS STATUS
         DC    A(DALNDISP)   ADRESSE DER DS NORM. DISPOSITION
         DC    A(DALCDISP)   ADRESSE DER DS COND. DISPOSITION
         DC    7A(0)
         DC    A(0)          ADRESSE DER VOLSER NUMBER
         DC    4A(0)
         DC    X'80'         ENDE DER PARAMETERKETTE
         DC    AL3(0)        ADRESSE DER UNIT SPECIFICATION
DALPLEN  EQU   *-DALPCON     LAENGE DER PARAMETERLISTE
DALSTATS DC    C'SHR'        DATA SET STATUS
DALNDISP DC    CL7'KEEP'     DATA SET NORMAL DISPOSITION
DALCDISP DC    CL7'KEEP'     DATA SET CONDITIONAL DISPOSITION
         SPACE 2
*
*      CAMLIST-MACRO (KONSTANTEN).
*
         SPACE 1
CAMCON   CAMLST SEEK,0,0,0
CAMLEN   EQU   *-CAMCON      LAENGE DES CAMLIST-MACROS
         EJECT
*
*      TABELLE FUER AUFBEREITUNG VON HEXADEZIMALZAHLEN.
*
         SPACE 1
TABHEXA  DC    C'0123456789ABCDEF'
         SPACE 2
*
*      DCB-DEFINITION FUER DIE EINGABEDATEI.
*
         SPACE 1
EINCONST DCB   DDNAME=EIN00000,DSORG=PO,MACRF=(R)
DCBLEN   EQU   *-EINCONST    LAENGE DES DCB
         EJECT
***********************************************************************
*                                                                     *
*      TABELLEN FUER EINHEITENTYPEN UND KAPAZITAETEN.                 *
*                                                                     *
*      DEVICE CODES:                                                  *
*      01 - 2311    DISK STORAGE DEVICE                               *
*      02 - 2301    PARALLEL DRUM                                     *
*      03 - 2303    SERIAL DRUM                                       *
*      04 - 2302    DISK STORAGE                                      *
*      05 - 2321    DATA CELL DRIVE                                   *
*      06 - 2305/1  FIXED HEAD STORAGE / MODEL 1                      *
*      07 - 2305/2  FIXED HEAD STORAGE / MODEL 2                      *
*      08 - 2314    DISK STORAGE FACILITY                             *
*      09 - 3330/1  DISK STORAGE / MODEL 1                            *
*      0A - 3340    DISK STORAGE                                      *
*      0B - 3350    DISK STORAGE                                      *
*      0C - RESERVED                                                  *
*      0D - 3330/11 DISK STORAGE / MODEL 11                           *
*      0E - RESERVED                                                  *
*      0F - RESERVED                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      EINHEITENTYPEN.
*
         SPACE 1
TABUNIT  EQU   *
         DC    C'    '
         DC    C'2311'       01
         DC    C'2301'       02
         DC    C'2303'       03
         DC    C'2302'       04
         DC    C'2321'       05
         DC    C'2305'       06
         DC    C'2305'       07
         DC    C'2314'       08
         DC    C'3330'       09
         DC    C'3340'       0A
         DC    C'3350'       0B
         DC    C'    '       0C
         DC    C'3330'       0D
         DC    C'    '       0E
         DC    C'    '       0F
         EJECT
*
*      TRACKS / CYLINDER.
*
         SPACE 1
TABTCAP  DS    0F
         DC    X'80',XL3'0'  00
         DC    F'10'         01
         DC    F'08'         02
         DC    F'10'         03
         DC    F'46'         04
         DC    F'20'         05
         DC    F'08'         06
         DC    F'08'         07
         DC    F'20'         08
         DC    F'19'         09
         DC    F'12'         0A
         DC    F'30'         0B
         DC    X'80',XL3'0'  0C
         DC    F'19'         0D
         DC    X'80',XL3'0'  0E
         DC    X'80',XL3'0'  0F
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER AUSGABENACHRICHTEN.                             *
*                                                                     *
***********************************************************************
         SPACE 3
MSG01    WTO   'XFIND01 MEMBER 12345678 FOUND IN:',                    *
               MF=L,MCSFLAG=(REG0)
LENMSG01 EQU   *-MSG01
         SPACE 1
MSG02    WTO   'XFIND02 -----DSNAME------ VOLSER UNIT DSCB1-INFORMATION*
                -TTR--',                                               *
               MF=L,MCSFLAG=(REG0)
LENMSG02 EQU   *-MSG02
         SPACE 1
MSG03    WTO   'XFIND03                   123456 123                   *
                123456',                                               *
               MF=L,MCSFLAG=(REG0)
LENMSG03 EQU   *-MSG03
         SPACE 1
MSG04    WTO   'XFIND04 MEMBER 12345678 NOT FOUND',                    *
               MF=L,MCSFLAG=(REG0)
LENMSG04 EQU   *-MSG04
         EJECT
MSG05    WTO   'XFIND05 FUNCTION SUPPORTED FOR OS/VS2 (MVS) ONLY',     *
               MF=L,MCSFLAG=(REG0)
LENMSG05 EQU   *-MSG05
         SPACE 1
MSG06    WTO   'XFIND06 ERROR IN PARAMETER LIST',                      *
               MF=L,MCSFLAG=(REG0)
LENMSG06 EQU   *-MSG06
         SPACE 1
MSG07    WTO   'XFIND07 VOLSER=123456,DSN=12345678901234567 ALLOCATION *
               FAILURE',                                               *
               MF=L,MCSFLAG=(REG0)
LENMSG07 EQU   *-MSG07
         SPACE 1
MSG08    WTO   'XFIND08 VOLSER=123456,DSN=12345678901234567890123 BLDL *
               FAILURE',                                               *
               MF=L,MCSFLAG=(REG0)
LENMSG08 EQU   *-MSG08
         EJECT
MSG09    WTO   'XFIND09 ERROR DURING EXECUTING OBTAIN, RC=99, VOLSER=12*
               3456',                                                  *
               MF=L,MCSFLAG=(REG0)
LENMSG09 EQU   *-MSG09
         SPACE 1
MSG010   WTO   'XFIND10 1. DSCB NOT FORMAT 4, VOLSER=123456',          *
               MF=L,MCSFLAG=(REG0)
LENMSG10 EQU   *-MSG010
         SPACE 1
MSG011   WTO   'XFIND11 NO INFORMATION ABOUT DEVICE AVAILABLE, DEVCODE=*
               99',                                                    *
               MF=L,MCSFLAG=(REG0)
LENMSG11 EQU   *-MSG011
         SPACE 1
MSG012   WTO   'XFIND12 VOLSER=123456,DSN=12345678901234567890123 OPEN *
               FAILURE',                                               *
               MF=L,MCSFLAG=(REG0)
LENMSG12 EQU   *-MSG012
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER ARBEITSBEREICHE.                                *
*                                                                     *
***********************************************************************
         SPACE 3
ABER     DSECT
SAVEAREA DS    18F           SAVEAREA FUER ALLGEMEINE REGISTER
DBWORD   DS    D             ZWISCHENSPEICHER FUER DEZIMALZAHLEN
ZWFELD   DS    CL15          ZWISCHENSPEICHER FUER DEZIMALZAHLEN
ZWVOLID1 DS    CL6           ZWISCHENSPEICHER FUER VOLID
ZWVOLID2 DS    CL6           ZWISCHENSPEICHER FUER VOLID
FOUNDBIT DS    C             SCHALTER FUER GEFUNDENE MEMBER
TESTBYTE DS    C             SCHALTER FUER TEST
CLCBYTE  DS    C             SCHALTER ZUR ABFRAGE DER VOLSER NUMBER
ADDRETB1 DS    F             ADRESSE DES ENDES VON TABNACHR
ADDRETPV DS    F             ADRESSE DES ENDES VON TABPVOL
ADDREPRM DS    F             ADRESSE DES ENDES DES PARAMETERBEREICHS
UCBADDR  DS    F             ADRESSE DES UCB-VEKTORS
DYNADDR  DS    F
UCMID    DS    F             UCM-ID. DES AUFRUFERS
MDS1ADDR DS    3H            HOECHSTE ADRESSE VON DSCB1
         ORG   MDS1ADDR
MDS1CC   DS    H             MAX. ZYLINDERNUMMER (DS4HPCR)
MDS1HH   DS    H             MAX. SPURNUMMER (DS4HPCR+2)
MDS1R    DS    H             MAX. SATZNUMMER (DS4HPCR+4)
MLFDADDR DS    2H            HOECHSTE LFD. ADRESSE
         ORG   MLFDADDR
MTRKNO   DS    H             MAX. SPURNUMMER (DS4DEVSZ+2)
MRECNO   DS    H             MAX. SATZNUMMER (DS4DEVT)
LFDADDR  DS    3H            LAUFENDE ADRESSE
         ORG   LFDADDR
LFDCYLNO DS    H             LAUFENDE ZYLINDERNUMMER
LFDTRKNO DS    H             LAUFENDE SPURNUMMER
LFDRECNO DS    H             LAUFENDE SATZNUMMER
SAVE1R10 DS    F             SAVEAREA FUER R10
SAVE1R14 DS    F             SAVEAREA FUER R14
         SPACE 2
EIN      DS    10F           DCB DER EINGABEDATEI
DCBDDNAM DS    CL8           DDNAME
DCBOFLGS DS    C             FLAG BYTE
         ORG   EIN+DCBLEN
DCBADDR  DS    F             ADRESSE DES DCB
         EJECT
*
*      CAMLIST-MACRO.
*
         SPACE 1
CAMOBT   DS    0F
         DS    C             AL1(192)
         DS    C             AL1(128)
         DS    C             AL1(0)
         DS    C             AL1(0)
CADRCCHH DS    F             A(CCHHR)
CADRUCBV DS    F             A(UCBVOLI)
CADRCAMW DS    F             A(CAMWKFLD)
         DS    0H
CCHHR    DS    CL5           CCHHR-ADRESSE DES NAECHSTEN DSCB
         DS    0D
CAMWKFLD DS    CL148         WORK FIELD
         SPACE 2
*
*      DSCB-DEFINITIONEN.
*
         SPACE 1
         ORG   CAMWKFLD
DSCB1    DS    0CL140        DATA SET CONTROL BLOCK FORMAT 1
DS1DSNAM DS    CL44          DATA SET NAME
DS1FMTID DS    C             FORMAT IDENTIFIER (= C'1')
         DS    CL37
DS1DSORG DS    CL2           DATA SET ORGANISATION
DS1RECFM DS    C             DATA SET RECORD FORMAT
         DS    C
DS1BLKL  DS    CL2           DATA SET BLOCK LENGTH
DS1RECL  DS    CL2           DATA SET RECORD LENGTH
         DS    CL3
DS1DSIND DS    C             DATA SET INDICATORS
         ORG   CAMWKFLD
DSCB4    DS    0CL140        DATA SET CONTROL BLOCK FORMAT 4
         DS    CL44
DS4FMTID DS    C             FORMAT IDENTIFIER (= X'F4')
DS4HPCR  DS    CL5           HIGHEST DISK ADDRESS OF DSCB1 (CCHHR)
         DS    CL12
DS4DEVSZ DS    CL4           DEVICE SIZE
         DS    CL8
DS4DEVT  DS    C             NO.OF DSCB'S ON A TRACK
         ORG   CAMWKFLD+148
         EJECT
*
*      DIRECTORY ENTRY LIST FUER BLDL.
*
         SPACE 1
BLDLLIST DS    0F
BLDLFF   DS    H             ANZAHL DER MEMBER ENTRIES
BLDLLL   DS    H             LAENGE DER ENTRIES
MEMBNAME DS    CL8           MEMBER NAME (PARMNAME)
BLDLTTR  DS    CL3           REL. ADRESSE DES MEMBERS (TTR)
         DS    CL2
BLDLC    DS    C             MEMBER TYPE (X'80' - ALIAS)
         SPACE 2
*
*      PARAMETERLISTE FUER 'DYNALC' (ARBEITSBEREICH).
*
         SPACE 1
DALPWKS  DS    0F
ADDRRC   DS    F             ADRESSE VON RETCODE
ADDRTYPE DS    F             ADRESSE DES DYNALLOC-TYPS
ADDRDDN  DS    F             ADRESSE DES DDNAME
ADDRDSN  DS    F             ADRESSE DES DSNAME
         DS    11F
ADDRVOLS DS    F             ADRESSE DER VOLSER NUMBER
         DS    4F
ADDRUNIT DS    F             ADRESSE DER UNIT SPECIFICATION
         ORG   DALPWKS+DALPLEN
RETCODE  DS    0F            RETURN CODES VON 'DYNALC'
SAVE1R15 DS    F             RETURN CODE
SAVE1R0  DS    F             ERROR REASON CODE
SAVE1R1  DS    F             INFORMATION REASON CODE
DALDDNAM DS    CL8           DDNAME
DALUNIT  DS    CL8           UNIT SPECIFICATION
DALTYPE  DS    CL8           DYNALLOC-TYP
DALWAIT  DS    CL4           WAIT REQUEST INDICATOR
         EJECT
*
*      MASKEN-TABELLEN FUER VOLSER NUMBERS AUS DEM PARAMETERBEREICH.
*
*      AUFBAU DER TABELLE:
*      BYTE1 - LAENGE DES ANGEGEBENEN VOLSER-PARAMETERS ODER
*          X'80' BEI TABELLENENDE
*      BYTES2-7 - ANFANGSZEICHEN DER VOLSER NUMBER UND MASKENZEICHEN
*
         SPACE 1
TABPVOL1 DS    10CL7
ENDTPVOL EQU   *
         DS    CL7
TABPVOL2 DS    CL77
         SPACE 2
*
*      NACHRICHTEN-BEREICHE.
*
         SPACE 1
         DS    0F
TABNACHR DS    10CL72        TABELLE FUER NACHRICHTEN
ENDTAB1  EQU   *
TABFNACH DS    CL72          TABELLE FUER FEHLERNACHRICHTEN
MSG01ZW  DS    CL70          NACHRICHEN-ZEILE
ABERL    EQU   *-ABER        LAENGE DER ARBEITSBEREICHE
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITIONEN FUER EINE NACHRICHTENZEILE.                       *
*                                                                     *
***********************************************************************
         SPACE 3
MESSAGE  DSECT
         DS    CL4
         SPACE 2
*
*      DEFINITIONEN FUER MSG01, MSG04.
*
         SPACE 1
         DS    CL15
MS1NAME  DS    CL8           MEMBER NAME
         SPACE 2
*
*      DEFINITIONEN FUER MSG03.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL8
MS3DSN   DS    CL17          DATA SET NAME
         DS    C
MS3VOLS  DS    CL6           VOLSER NUMBER
         DS    C
MS3UNIT  DS    CL3           UNIT NAME
         DS    CL2
MS3INFO  DS    CL17          DSCB1-INFORMATION
         DS    C
MS3TTR   DS    CL6           REL. ADRESSE DES MEMBERS (TTR)
         SPACE 2
*
*      DEFINITIONEN FUER MSG07.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL15
MS7VOLS  DS    CL6           VOLUME SERIAL NUMBER
         DS    CL5
MS7DSN   DS    CL17          DATA SET NAME
         EJECT
*
*      DEFINITIONEN FUER MSG08, MSG012.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL15
MS8VOLS  DS    CL6           VOLUME SERIAL NUMBER
         DS    CL5
MS8DSN   DS    CL23
         SPACE 2
*
*      DEFINITIONEN FUER MSG09.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL42
MS9RC    DS    CL2           RETURN CODE
         DS    CL9
MS9VOLS  DS    CL6           VOLSER NUMBER
         SPACE 2
*
*      DEFINITIONEN FUER MSG010.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL37
MS10VOLS DS    CL6           VOLSER NUMBER
         SPACE 2
*
*      DEFINITIONEN FUER MSG011.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL55
MS11DEVC DS    CL2           DEVICE CODE
         SPACE 2
         ORG   MESSAGE+70
MESSFLAG DS    CL2           FLAG BYTES
         EJECT
***********************************************************************
*                                                                     *
*      UCB-DEFINITIONEN.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
UCB      DSECT
         DS    CL3
DEVSTAT  DS    C             DEVICE STATUS
         DS    CL9
UCBNAME  DS    CL3           UNIT NAME
UCBTYPE  DS    CL2           DEVICE TYPE
UCBDVCLS DS    C             DEVICE CLASS
DEVCODE  DS    C             DEVICE CODE
         DS    CL4
UCBVTOC  DS    CL4           REL. ADDR. OF VTOC FOR THIS VOL. (TTR0)
UCBVOLI  DS    CL6           VOLUME SERIAL NUMBER
UCBSTAB  DS    C             VOLUME STATUS
         SPACE 3
***********************************************************************
*                                                                     *
*      PARAMETERBEREICH.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
         XPARM
PARMENDE EQU   *
         SPACE 3
***********************************************************************
*                                                                     *
*      VEKTOR MIT UCB-ADRESSEN.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
VEKTOR   DSECT
         DS    1024H
VEKEND   DS    H             =X'FFFF'
VEKLEN   EQU   *-VEKTOR
         CVT   DSECT=YES                                          /XUCB
         EJECT
         END   FIND
./ ADD NAME=OPER
XOPER    TITLE 'EXTENDED OPER COMMAND FOR TSO'
***********************************************************************
*                                                                     *
*              P R O L O G                                            *
*                                                                     *
***********************************************************************
*                                                                     *
*              PROGRAMMER              STOEHLER / DATEV EG. NUERNBERG *
*                                               / BMW AG. MUENCHEN    *
*                                                                     *
*              CHANGES                 COPY TDCM FROM CONSOLE ADDRESS *
*                                      SPACE VIA CROSS MEMORY SERVICE.*
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*              DESCRIPTION                                            *
*                                                                     *
*              THIS PROGRAM WILL DO CONSOLE SIMULATION FOR A TSO      *
*              TERMINAL. IT GOES OUT AND FINDS A GRAPHICS (3277)      *
*              ACTIVE CONSOLE AND USES THAT CONSOLE SCREEN IMAGE      *
*              FOR OUTPUT AND THEIR UCM-ID FOR SVC 34.                *
*              THIS PROGRAM REQUIRES AUTHORIZATION TO USE THE SVC 34  *
*              INTERFACE.                                             *
*              COMMANDS ARE BROADCAST TO THE CONSOLE BEING USED.      *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*              PARAMETER               ADDRESS OF A SPECIFIC CONSOLE  *
*                                      CUU (NOT REQUIRED)             *
*                                                                     *
*              I/O                     ONLY TGET/TPUT                 *
*                                                                     *
*              EXIT                    NORMAL RETURN (AFTER YOU TYPE  *
*                                      'END')                         *
*                                                                     *
*              ERROR MESSAGES          NO ACCEPTABLE (3277) CONSOLES  *
*                                      FOUND                          *
*                                                                     *
***********************************************************************
         EJECT
XOPER    CSECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING XOPER,R12,R11
         LA    R11,2048(R12)
         LA    R11,2048(R11)
         LA    R2,SAVEAREA
         ST    R2,8(R13)
         ST    R13,4(R2)
         LR    R13,R2
         LR    R3,R1
         USING PARMAREA,R3
         CLC   REG4(3),=C'TSO'
         BNE   END2
         CLI   TEXT+4,C','    ==>  COMMAND NAME MUST BE 4 BYTES
         BNE   GOON1               NO PARM GIVEN
         MVC   UNITXXX(3),TEXT+5   SAVE IT
         CLC   UNITXXX(3),=CL3' '
         BE    GOON1
         NI    CHKUNIT+1,15        NOP
GOON1    EQU   *
         L     R1,16               CVT
         L     R1,100(R1)          UCM
         USING UCM,R1
         LM    R3,R5,UCMVEA        R3 -> TO THE FIRST UCME
*                                  R4 CONTAINS UCME LENGTH
*                                  R5 -> TO THE LAST UCME
DID0     EQU   *                   SEARCH FOR SOMETHING USEFUL
         USING UCMLIST,R3
         L     R1,UCMUCB           DEBUGGING
CHKUNIT  B     DID00               NOP IF SPECIFIC REQUEST
         CLC   UNITXXX(3),13(R1)   IS IT THE REQUESTED CONSOLE ?
         BNE   DID1                NO - SEARCH FOR NEXT
DID00    EQU   *
         MVC   UNITXXX(3),13(R1)   MOVE UNITADDRESS
         TM    UCMATR,UCMUF        IS IT ACTIVE ?
         BZ    DID1                NO - UNUSEABLE
         L     R10,UCMXB           IS IT GRAPHICS ?
         LTR   R10,R10
         BZ    DID1                NO - USELESS
         L     R1,0(R10)           FIND  THE PAGEABLE DCM (TDCM)
         LTR   R1,R1               DOES IT EXIST ?
         BZ    DID1                NO - UNUSEABLE
         MVC   XUCMID,UCMID            SAVE THIS
         CLI   UCMEDEVX,UCM32772       3270 - DEVICE ?
         BL    DID1
         CLI   UCMEDEVX,UCM32782
         BNH   GOTONE0                 YES
DID1     EQU   *
         BXLE  R3,R4,DID0          GET THE NEXT ENTRY
         TPUT  ERMSG,L'ERMSG       NO ACCEPTABLE CONSOLES ACTIVE
         B     END2                FINISH
         SPACE 3
*
* A CONSOLE HAS BEEN FOUND COPY THE SCREEN AND DISPLAY IT FOR THE USER
*        R3 ->  UCME
*        R10 -> RDCM
*
GOTONE0  EQU   *                   CLEAR SCREEN
         STFSMODE ON,INITIAL=YES
GOTONE1  EQU   *                   CLEAR SCREEN
         L     R1,0(R10)               TDCM
         LA    R0,LINES                COPY AREA
         LA    R15,MOVETDCM
         ICM   R15,8,=X'AA'
         SVC   252
         MVC   CMDBUF,BLANKS
         LA    R7,24               NUMBER OF LINES
         LA    R8,SCREEN
GOTONE2  MVC   0(2,R8),=X'1D60'    NORMAL INTENSITY
         MVC   2(79,R8),BLANKS     CLEAR
         LA    R8,81(R8)           NEXT LINE
         BCT   R7,GOTONE2          REPEAT
         SH    R8,=H'81'           LAST LINE (MODE)
         MVC   0(2,R8),=X'1DE8'    HIGH INTENSITY
         MVC   ENTRLNE(4),=X'401DC113' CURSOR POSITION
         LH    R2,LINES            NUMBER OF LINES
         LA    R7,LINES+2          POINT TO THE FIRST LINE (COPY)
         LA    R8,SCREEN           OUTPUT LINE
GOT1     EQU   *
         MVC   5(74,R8),4(R7)      MOVE A LINE
         OC    5(74,R8),BLANKS     GET IT READABLE
         LA    R8,81(,R8)          OUR SIZE
         LA    R7,84(,R7)          THEIRS
         BCT   R2,GOT1             GET EM ALL
         CLC   LINES(2),=H'20'      3081 ?
         BNL   DISP3081             YES
DISP3033 EQU   *                    3033
         LA    R8,81(,R8)          SKIP PFK LINE
DISP3081 EQU   *
         MVC   0(2,R8),=X'1D60'
         MVC   3(L'ENTRMSG,R8),ENTRMSG
         LA    R8,(3*81)(,R8)      SKIP INPUT LINE
         MVC   4(MODELEN,R8),MODEMSG
RCN0SW   NOP   RCN0
         BAL   R9,WRITE            TPUT AND TGET
CHECKCMD CLC   CMNDBUF,BLANKS      NULL INPUT ?
         BE    GOTONE1
         CLC   CMNDBUF(4),=C'END ' REQUEST TO TERMINATE THE COMMAND
         BE    END1
         LA    R1,CMND             DOCUMENT THE COMMAND
         XR    R0,R0               ON THE SELECTED CONSOLE
         IC    R0,XUCMID
         SVC   35
         LA    R1,CMND             SUBMIT THE COMMAND
         XR    R0,R0
         IC    R0,XUCMID           OUR MAN IN THE COMM TASK
         LA    R15,SVCROUT
         ICM   R15,8,=X'AA'
         SVC   252
         B     COMOKAY
SVCROUT  SVC   34
         BR    R14
COMOKAY  EQU   *
         LTR   R15,R15             RC = 0 ?
         BZ    RC0GO               YES
         OI    RCN0SW+1,240
RC0GO    STIMER WAIT,DINTVL==CL8'00000100'
         B     GOTONE1             YES
RCN0     NI    RCN0SW+1,15
         MVC   ENTRLNE-81(LERRORMS),ERRORMSG
         MVC   ENTRLNE+4(77),CMNDBUF
         MVC   CMNDBUF,BLANKS
         BAL   R9,WRITE            TPUT AND TGET
         MVI   ENTRLNE+4,C' '      CLEAR ENTRLINE
         MVC   ENTRLNE+5(76),ENTRLNE
         B     CHECKCMD            TRY THE CORRECTED COMMAND
END1     EQU   *
         LA    0,(15*256)+3         LOAD BUFFER-LENGTH & TPUT/FLAG
         LA    1,=X'C1115D7E1140403C40400011404013' LOAD BUFFER-ADDR
         SLL   1,8                      SHIFT BUFFER-LENGTH, FLAGS
         SRDL  0,8                      AND BUFFER-ADDRESS
         SVC   93                       TPUT
         STFSMODE OFF                   TPUT
END2     L     R13,4(R13)
         RETURN (14,12),RC=0       RETURN TO XCNTRLVS
         SPACE 2
WRITE    EQU   *
         TPUT  CLEAR,LSCREEN,FULLSCR
         TGET  CMDBUFP,83,ASIS
         MVC   CMNDBUF,CMDBUF
         OC    CMNDBUF,BLANKS
         BR    R9
         TITLE 'X O P E R  -  DATA AREAS'
         LTORG
SAVEAREA DC    18F'0'
ERMSG    DC    C'NO ACCEPTABLE (3277) CONSOLES FOUND'
MODEMSG  DC    C'IEE163I MODE= TSO ('
UNITXXX  DC    CL5'   ) '
MODELEN  EQU   *-MODEMSG
ENTRMSG  DC    C'IEE152I     ENTER     CANCEL     D C,K         '
ERRORMSG DC    X'1DE840'
         DC    C'INVALID COMMAND (RC FROM SVC 34 ^= 0)          '
LERRORMS EQU   *-ERRORMSG
CMND     DC    H'84',X'4000'
CMNDBUF  DC    CL80' '
CMDBUFP  DC    CL6' '
CMDBUF   DC    CL80' '
         DC    F'0'
XUCMID   DC    X'0'
BLANKS   DC    CL80' '
LINES    DC    H'0'
         DC    20CL84' '
CLEAR    DC    X'C71140403C404000114040'
SCREEN   DC    24CL81' '
ENTRLNE  EQU   SCREEN+(81*21)
LSCREEN  EQU   *-CLEAR
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'SVC-ROUTINE'
MOVETDCM DS    0F
         USING *,R6
         LR    R6,R15                  ENTRY POINT
         LR    R4,R0                   PARAMETER
         LR    R3,R1
         LA    R13,SAVESVC
         AXSET AX==H'1'
*        GET   ASID OF CONSOLE ADDRESS SPACE
         L     R5,16              CVT
         L     R5,556(R5)         ASVT
         L     R1,516(R5)         MAX ASCB
         LA    R5,524(R5)         POINTER ASCB
ASCBLOOP EQU   *
         LA    R5,4(R5)           NEXT ASCB
         BCTR  R1,0               ASCB-NUMBER - 1
         LTR   R1,R1              LAST ASCB
         BZ    NOTFOUND           ABEND
         SR    R9,R9
         ICM   R9,7,1(R5)         ASCB ADDRESS
         BZ    NOTFOUND           ABEND
         TM    0(R5),X'80'        AVAILABLE ?
         BO    ASCBLOOP           NO -> NEXT
         CLC   0(4,R9),=CL4'ASCB' ASCB ?
         BNE   NOTFOUND           ABEND
         L     R8,176(R9)         JOBNAME FIELD
         CLC   0(8,R8),=C'CONSOLE '
         BNE   ASCBLOOP
         LH    R2,36(R9)          ASID
         SSAR  R2                 SET SECONDARY ADDRESS SPACE
         LA    R5,4                    LENGTH
         SR    R2,R2                   KEY NOT RELEVANT IN SUP-MODE
         MVCP  0(R5,R4),X'30'(R3),R2
         BZ    *+6
         DC    H'0'               ABEND
         L     R8,0(R4)
         LA    R5,2                    LENGTH
         SR    R2,R2                   KEY NOT RELEVANT IN SUP-MODE
         MVCP  0(R5,R4),X'FE'(R3),R2
         BZ    *+6
         DC    H'0'               ABEND
         LA    R5,20*84                MAX.-SCREEN SIZE 20 LINES LENGTH
         LA    R9,256
MOVE     MVCP  2(R5,R4),0(R8),R2
         BZ    ENDMOVE
         AR    R4,R9
         AR    R8,R9
         SR    R5,R9
         B     MOVE
ENDMOVE  EQU   *
         LH    R2,36(R7)          ASCBASID OF MY ADDRESS SPACE
         SSAR  R2                 SET SECONDARY ADDRESS SPACE
         AXSET AX==H'0'
         BR    R14
         SPACE 3
NOTFOUND DC    H'0'               ABEND
         SPACE 3
SAVESVC  DC    18F'0'
         SPACE 3
         LTORG
         EJECT
         XPARM
*              FROM SYS1.AMODGEN
         PRINT NOGEN
         IEECUCM FORMAT=NEW,LIST=YES
         END
./ ADD NAME=SHOW
         TITLE 'XSHOW                             X-COMMAND SHOW'
SHOW     CSECT
         SPACE 3
* MVS/XA UCBSCAN                               6.7.83 BMW         /XUCB
***********************************************************************
*                                                                     *
*      PROGRAM-ID. SHOW.                                              *
*      AUTHOR. EHNERT.                                                *
*      INSTALLATION. BAYER AG, LEVERKUSEN, RECHENZENTRUM.             *
*      DATE-WRITTEN. 24. 2. 1975.                                     *
*      REMARKS.                                                       *
*                                                                     *
*          DAS X-COMMAND 'SHOW' GIBT INFORMATIONEN UEBER DIE          *
*          DATEIEN AUS, DEREN NAMEN AUF INDEXEBENE ODER VOLL-         *
*          STAENDIG SPEZIFIEZIERT WURDEN.                             *
*                                                                     *
*          ZUSAETZLICH KOENNEN FOLGENDE PARAMETER ANGEGEBEN WERDEN:   *
*          A) MSG: AUSGABE VON INFORMATIONEN UEBER SPEICHERPLATZ-     *
*             ANFORDERUNG UND -BELEGUNG SOWIE UEBER DIE DCB'S DER     *
*             DATEIEN,                                                *
*          B) EXT: AUSGABE VON INFORMATIONEN UEBER DIE EINZELNEN      *
*             SPEICHERPLATZ-AUSDEHNUNGEN (EXTENTS),                   *
*          C) DATE: AUSGABE VON INFORMATIONEN UEBER ERSTELL- UND      *
*             FALLDATUM DER DATEIEN,                                  *
*          D) CAT: AUSGABE VON KATALOG-INFORMATIONEN,                 *
*          E) BIS ZU ZEHN MASKEN FUER DIE VOLUME SERIAL NUMBERS,      *
*             MIT DEREN HILFE EINE AUSWAHL DER VOLUMES GETROFFEN      *
*             WERDEN KANN, DABEI STEHT '*' FUER JEDES ZEICHEN AN      *
*             DIESER STELLE.                                          *
*                                                                     *
*          WIRD KEINER DER PARAMETER A) BIS D) ANGEGEBEN, SO          *
*          GELTEN STANDARDMAESSIG 'MSG' UND 'CAT'. WERDEN KEINE       *
*          VOLSER-MASKEN SPEZIFIZIERT, SO WERDEN ALLE ONLINE          *
*          LIBRARIES NACH DEN DATEIEN DURCHSUCHT.                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      INITIAL PROCESSING.                                            *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      SAVE REGISTER CONTENTS,
*      GET MAIN STORAGE FOR WORK AREAS,
*      ALLOCATE BASE REGISTERS.
*
         SPACE 1
         XSAVE R11,,SHOW,WORKLEN
         SPACE 1
         USING UCB,R03                 ALLOC. BASE REG. OF UCB
         USING PARMAREA,R09            ALLOC. BASE REG. OF PARMAREA
         LR    R09,R01                 INIT. BASE REG. OF PARMAREA
         USING MESSAGE,R10             ALLOC. BASE REG. OF MESSAGE
         USING SHOW,R11,R12            ALLOC. BASE REG. OF CSECT
         LA    R12,2048(,R11)          INITIALIZE
         LA    R12,2048(,R12)              UPPER BASE REGISTER
         USING WORKAREA,R13            ALLOC. BASE REG. OF WORKAREA
         EJECT
*
*      INITIALIZE WORK AREAS.
*
         SPACE 1
         XC    CAMSEEK(48),CAMSEEK     CLEAR MACRO WORK AREAS
         MVI   CAMSEEK,X'C0'           SET FLAG BYTE
         MVI   CAMSEEK+1,X'80'         SET FLAG BYTE
         MVI   CAMSRCH,X'C1'           SET FLAG BYTE
         MVI   CAMNAME,X'44'           SET FLAG BYTE
         MVI   FLG1BYTE,FLG1INIT       INITIALIZE FLAG BYTE
         MVI   FLG2BYTE,FLG2INIT       INITIALIZE FLAG BYTE
         XC    LINECNT,LINECNT         ZERO OUTPUT LINE COUNT
         MVC   WTOR(MSG13LEN),MSG13    MOVE WTOR LINE
         LA    R02,CCHHR               LOAD ADDRESS OF CCHHR AREA
         LA    R03,VOLSERNO            LOAD ADDRESS OF VOLSER AREA
         LA    R04,OBTWKFLD            LOAD ADDRESS OF OBTWKFLD
         STM   R02,R04,CADRCCHH        STORE ADDRESSES IN CAMSEEK
         LA    R02,DSNAME              LOAD ADDRESS OF DSNAME
         LA    R04,OBTWKFLD+44         LOAD ADDRESS OF OBTWKFLD+44
         STM   R02,R04,CADRDSN         STORE ADDRESSES IN CAMSRCH
         SR    R03,R03                 ZERO REGISTER
         LA    R04,LOCWKFLD            LOAD ADDRESS OF LOCWKFLD
         STM   R02,R04,CAMDSN          STORE ADDRESSES IN CAMNAME
         LA    R02,ENDTAB1             LOAD
         LA    R03,ENDTAB2                 END ADDRESSES
         LA    R04,ENDTAB3                 OF TABLES
         LA    R05,ENDTPVOL                AND
         STM   R02,R05,ADDRETB1            STORE THEM
         MVC   UCMID,REG4              MOVE UCM-ID.
         SPACE 2
*
*      SET FLAG BYTES IN MESSAGE AREAS.
*
         SPACE 1
         LA    R02,MSGTAB              LOAD ADDRESSES OF
         LA    R03,EXTNTAB                 MESSAGE AREAS
         LA    R04,DATETAB                 AND
         STM   R02,R04,SAVE1R10            STORE THEM
         LA    R04,MSGTAB              LOAD ADDRESS OF
         LA    R06,MSGTAB+1                MESSAGE AREAS
         LA    R07,ERRORTAB-MSGTAB-1   LOAD LENGTH OF MESSAGE AREAS
         MVI   MSGTAB,X'FF'            INITIALIZE MESSAGE AREA
         LA    R05,1                   INITIALIZE
         ICM   R05,8,MSGTAB                REGISTER
         MVCL  R06,R04                 SET MESSAGE AREAS TO X'FF'
         EJECT
***********************************************************************
*                                                                     *
*      INPUT PARAMETER PROCESSING.                                    *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      PROCESSING OF DATA SET NAME(S).
*
         SPACE 1
         LA    R04,TEXT                LOAD PARAMETER START ADDRESS
         LA    R08,PARMLEN-1           LOAD LENGTH OF PARMAREA
         EX    R08,TRT                 BRANCH TO FIND SEPERATOR
         BZ    PRMERROR                BRANCH IF NO FOUND
         CLM   R02,1,TRTCOMMA          COMMA FOUND ?
         BNE   PRMERROR                IF NOT, BRANCH TO ERROR ROUTINE
         LR    R05,R01                 COMPUTE
         SR    R05,R04                      LENGTH OF TEXT
         SR    R08,R05                 COMPUTE REMAINDER LENGTH
         LA    R04,1(,R01)             LOAD ADDRESS OF NEXT BYTE
         EX    R08,TRT                 BRANCH TO FIND NEXT SEPERATOR
         BZ    PRMERROR                BRANCH IF NO FOUND
         LR    R05,R01                 COMPUTE LENGTH
         SR    R05,R04                     OF DATA SET NAME
         BNP   PRMERROR                BRANCH IF INVALID LENGTH
         CH    R05,=H'44'              LENGTH VALID ?
         BH    PRMERROR                BRANCH IF INVALID LENGTH
         MVC   HEADER(MSG00LEN),MSG00  MOVE MESSAGE SKELETON
         BCTR  R05,0                   MODIFY FOR EX COMMAND
         STH   R05,DSNMLEN             STORE LENGTH
         EX    R05,MOVEPNAM            BRANCH TO MOVE DSNAME
         BCTR  R01,0                   DECREASE ADDRESS
         CLI   0(R01),C'.'             INDEX LEVEL SPECIFIED ?
         LA    R01,1(,R01)             CORRECT LENGTH
         BE    SETINDEX                BRANCH IF YES
ENDPNAM1 EQU   *
         EJECT
*
*      PROCESSING OF FOLLOWING PARAMETERS (VOLSER MASKS,
*      'MSG', 'EXT', 'DATE', AND 'CAT').
*
         SPACE 1
         LA    R06,TABPVOL1            LOAD ADDRESS OF VOLSER TABLE
NEXTPARM EQU   *
         CLM   R02,1,TRTCOMMA          MORE PARAMETERS SPECIFIED ?
         BNE   ENDPARM                 BRANCH IF NOT
         SR    R08,R05                 COMPUTE REMAINDER LENGTH
         LA    R04,1(,R01)             LOAD ADDRESS OF NEXT BYTE
         EX    R08,TRT                 BRANCH TO FIND SEPERATOR
         BZ    PRMERROR                BRANCH IF NO FOUND
         LR    R05,R01                 COMPUTE LENGTH
         SR    R05,R04                     OF PARAMETER
         BZ    PRMERROR                TEST LENGTH
         CH    R05,=H'6'                   AND BRANCH
         BH    PRMERROR                    IF INVALID
         CLC   0(3,R04),=C'MSG'        GENERAL INFORMATION REQUESTED ?
         BNE   *+12                    BRANCH IF NOT
         OI    FLG1BYTE,FLG1GIN        ELSE SET FLAG
         B     NEXTPARM                    AND BRANCH
         CLC   0(3,R04),=C'EXT'        EXTENT INFORMATION REQUESTED ?
         BNE   *+12                    BRANCH IF NOT
         OI    FLG1BYTE,FLG1EXT        ELSE SET FLAG
         B     NEXTPARM                    AND BRANCH
         CLC   0(4,R04),=C'DATE'       DATE INFORMATION REQUESTED ?
         BNE   *+12                    BRANCH IF NOT
         OI    FLG1BYTE,FLG1DATE       ELSE SET FLAG
         B     NEXTPARM                    AND BRANCH
         CLC   0(3,R04),=C'CAT'        CATALOG INFORMATION REQUESTED ?
         BNE   *+12                    BRANCH IF NOT
         OI    FLG1BYTE,FLG1CAT        ELSE SET FLAG
         B     NEXTPARM                    AND BRANCH
         CLC   0(3,R04),=C'ALL'        ALL INFORMATIONS REQUESTED ?
         BNE   *+12                    BRANCH IF NOT
         OI    FLG1BYTE,FLG1CAT+FLG1EXT+FLG1DATE+FLG1GIN
         B     NEXTPARM                    AND BRANCH
         C     R06,ADDRETPV            END OF VOLSER TABLE ?
         BNL   PRMERROR                BRANCH IF YES
         BCTR  R05,0                   MODIFY FOR EX COMMAND
         EX    R05,MOVEPVOL            BRANCH TO MOVE VOLSER
         OI    FLG1BYTE,FLG1VOL        SET FLAG
         STC   R05,0(R06)              STORE LENGTH
         LA    R06,7(,R06)             LOAD ADDRESS OF NEXT TABLE ELEM.
         B     NEXTPARM                BRANCH
         EJECT
*
*      END OF PARAMETER PROCESSING.
*
         SPACE 1
ENDPARM  EQU   *
         TM    FLG1BYTE,FLG1INFO       INFO. PARAMETER SPECIFIED ?
         BNE   *+8                     BRANCH IF YES
         OI    FLG1BYTE,FLG1GINC       ELSE SET DEFAULT FLAGS
         MVI   0(R06),X'80'            SPECIFY END OF TABLE
         TR    TABPVOL1(77),TRTABLE    TRANSLATE DON'T CARE CHARACTER
         MVC   TABPVOL2,TABPVOL1       COPY VOLSER TABLE
         TR    TABPVOL2(77),TRTABLE    TRANSLATE DON'T CARE CHARACTER
         TM    FLG2BYTE,FLG2ON         FULL DSNAME SPECIFIED ?
         BNZ   GETUCB                  BRANCH IF NOT
         LA    R14,GETUCB              ELSE LOAD RETURN ADDRESS
         B     LOCATE                      AND BRANCH
         SPACE 2
SETINDEX EQU   *
         OI    FLG2BYTE,FLG2INLV       SET FLAG
         B     ENDPNAM1                BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      PROCESSING OF UCB VECTOR TABLE.                                *
*                                                                     *
***********************************************************************
         SPACE 3
GETUCB   EQU   *
         DROP  R09                     FREE BASE REGISTER
         XUCBSCAN R02,X'20'     SETUP FOR DASD SCAN ONLY          /XUCB
         EJECT
***********************************************************************
*                                                                     *
*      PROCESSING OF UCB INFORMATION.                                 *
*                                                                     *
***********************************************************************
         SPACE 3
NEXTUCB  EQU   *
         XUCBGET R02,ENDPROC,ENDPROC                              /XUCB
         LR    R03,R01                 LOAD UCB ADDRESS           /XUCB
         TM    DEVSTAT,X'80'           DEVICE ONLINE ?
         BZ    NEXTUCB                 BRANCH IF NOT
         TM    DEVSTAT,X'10'           UNLOAD PENDING ?
         BO    NEXTUCB                 BRANCH IF YES
         CLC   UCBVOLI,=XL6'0'         VOLID ?
         BE    NEXTUCB                 BRANCH IF NOT
         TM    UCB+17,X'08'            VIRTUAL DEVICE ?
         BO    TSTVVOL                 BRANCH IF YES
         TM    FLG1BYTE,FLG1VOL        VOLSER MASKS SPECIFIED ?
         BZ    MVCVOLID                BRANCH IF NOT
         SPACE 2
*
*      COMPARE WITH SPECIFIED VOLSER NUMBERS.
*
         SPACE 1
         SR    R05,R05                 ZERO FOR ICM COMMAND
         LA    R06,TABPVOL1-7          LOAD ADDRESS OF VOLSER TABLE 1
         LA    R07,TABPVOL2-7          LOAD ADDRESS OF VOLSER TABLE 2
NXTVOLID EQU   *
         LA    R06,7(,R06)             LOAD ADDRESS OF NEXT ELEMENT
         LA    R07,7(,R07)             LOAD ADDRESS OF NEXT ELEMENT
         TM    0(R06),X'80'            END OF TABLE ?
         BO    NEXTUCB                 BRANCH IF YES
         IC    R05,0(R06)              INSERT LENGTH
         MVC   WKVOLID1,UCBVOLI        MOVE VOLSER NUMBER
         MVC   WKVOLID2,UCBVOLI        MOVE VOLSER NUMBER
         EX    R05,NCVOLID1            BRANCH TO NC VOLSER NUMBERS
         EX    R05,OCVOLID2            BRANCH TO OC VOLSER NUMBERS
         CLC   WKVOLID1,WKVOLID2       VOLID = SPEC. VOLID ?
         BNE   NXTVOLID                BRANCH IF NOT
         B     MVCVOLID                BRANCH
         SPACE 1
TSTVVOL  EQU   *
         LA    R06,TABPVOL1-7          LOAD ADDRESS OF VOLSER TABLE 1
NXTVVOL  EQU   *
         LA    R06,7(,R06)             LOAD ADDRESS OF NEXT ELEMENT
         TM    0(R06),X'80'            END OF TABLE ?
         BO    NEXTUCB                 BRANCH IF YES
         CLC   UCBVOLI,1(R06)          VOLID = SPEC. VOLID ?
         BNE   NXTVVOL                 BRANCH IF NOT
MVCVOLID EQU   *
         MVC   VOLSERNO,UCBVOLI        MOVE VOLSER NUMBER
         TM    FLG2BYTE,FLG2ON         FULL DSNAME SPECIFIED ?
         BZ    GETDSCB1                BRANCH IF YES
         SPACE 2
*
*      COMPUTE CCHHR OF VTOC.
*
         SPACE 1
         SR    R04,R04                 ZERO FOR IC COMMAND
         IC    R04,DEVCODE             INSERT DEVICE CODE
         SLA   R04,2                   DEVICE CODE * 4
         LA    R05,TABTCAP(R04)        LOAD ADDRESS OF TABLE ELEMENT
         CLI   0(R05),X'80'            DEVICE RESERVED ?
         BE    DEVERROR                IF YES BRANCH TO ERROR ROUTINE
         LH    R06,UCBVTOC             LOAD RELATIVE ADDRESS OF VTOC
         SRDA  R06,32                  R06 -> R07
         D     R06,0(R05)              DIV. TT / SPECIFIC TRK PER CYL
         STH   R07,CCHHR               STORE CYLINDER NUMBER
         STH   R06,CCHHR+2             STORE HEADER NUMBER
         SR    R05,R05                 ZERO FOR IC COMMAND
         IC    R05,UCBVTOC+2           LOAD RECORD NUMBER OF VTOC
         STC   R05,CCHHR+4                 AND STORE IT
         STH   R05,CURRECNO            STORE CURR. RECORD NUMBER
         STH   R06,CURTRKNO            STORE CURR. TRACK NUMBER
         STH   R07,CURCYLNO            STORE CURRENT CYLINDER NUMBER
         SPACE 2
*
*      GET DSCB4.
*
         SPACE 1
         OBTAIN CAMSEEK                OBTAIN DSCB4
         LTR   R15,R15                 OBTAIN SUCCESSFUL ?
         BNZ   DEVERROR                IF NOT, BRANCH TO ERROR ROUTINE
         CLI   DS4FMTID,X'F4'          FIRST DSCB = DSCB4 ?
         BNE   DEVERROR                IF NOT, BRANCH TO ERROR ROUTINE
         MVC   MTRKNO,DS4DEVSZ+2       SAVE MAXIMUM TRACK ADDRESS
         MVI   MRECNO,X'00'            SAVE MAXIMUM
         MVC   MRECNO+1(1),DS4DEVT         RECORD ADDRESS
         MVC   MDS1ADDR(4),DS4HPCR     SAVE HIGHEST
         MVI   MDS1R,X'00'                 DISK ADDRESS
         MVC   MDS1R+1(1),DS4HPCR+4        OF DSCB1
         EJECT
***********************************************************************
*                                                                     *
*      DSCB1 PROCESSING.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
NXTDSCB  EQU   *
         MVC   CCHHR,CURADDR           RESTORE CCHHR AREA
         LH    R04,CURRECNO            LOAD CURRENT RECORD NUMBER
         LA    R04,1(R04)                  AND INCREASE IT
         CH    R04,MRECNO              RECORD NUMBER TOO HIGH ?
         BNH   NXTREC                  BRANCH IF NOT
         LH    R04,CURTRKNO            ELSE LOAD CURRENT TRACK NUMBER
         LA    R04,1(R04)                  AND INCRESE IT
         CH    R04,MTRKNO              TRACK NUMBER TOO HIGH ?
         BL    NXTTRK                  BRANCH IF NOT
         LH    R04,CURCYLNO            ELSE LOAD CURR. CYLINDER NUMBER
         LA    R04,1(R04)                  AND INCRESE IT
         STH   R04,CURCYLNO            STORE NEW
         STH   R04,CCHHR                   CYLINDER NUMBER
         SR    R04,R04                 ZERO TRCK NUMBER
NXTTRK   EQU   *
         STH   R04,CURTRKNO            STORE NEW
         STH   R04,CCHHR+2                 TRACK NUMBER
         LA    R04,1                   INITIALIZE RECORD NUMBER
NXTREC   EQU   *
         STH   R04,CURRECNO            STORE NEW
         STC   R04,CCHHR+4                 RECORD NUMBER
         CLC   CURADDR(6),MDS1ADDR     END OF VTOC ?
         BH    NEXTUCB                 BRANCH IF YES
         OBTAIN CAMSEEK                ELSE OBTAIN NEXT DSCB
         LTR   R15,R15                 SUCCESSFUL OBTAIN ?
         BNZ   OBTERROR                IF NOT, BRANCH TO ERROR ROUTINE
         CLI   DS1FMTID,C'1'           DSCB = DSCB1 ?
         BNE   NXTDSCB                 BRANCH IF NOT
         EJECT
         TM    FLG2BYTE,FLG2INLV       INDEX LEVEL SPECIFIED ?
         BZ    TESTOK                  BRANCH IF YES
         LH    R05,DSNMLEN             LOAD LENGTH OF SPEC. NAME
         EX    R05,TESTNAME            BRANCH TO TEST DSNAME
         BNE   NXTDSCB                 BRANCH IF INVALID DSNAME
TESTOK   EQU   *
         MVC   HEADER(MSG00LEN),MSG00  MOVE MESSAGE SKELETON
         MVC   DSNAME,DS1DSNAM         MOVE DATA SET NAME
         LA    R14,SETFNDFL            LOAD RETURN ADDRESS
         B     LOCATE                      AND BRANCH
         EJECT
GETDSCB1 EQU   *
         OBTAIN CAMSRCH                OBTAIN DSCB1
         LTR   R15,R15                 SUCCESSFUL OBTAIN ?
         BNZ   OBTERROR                BRANCH IF NOT
         CLC   DS1CCHHR,=XL6'0'        DUMMY DSCB1 ?
         BE    NEXTUCB                 BRANCH IF YES
SETFNDFL EQU   *
         OI    FLG1BYTE,FLG1FND        SET FLAG
         LA    R14,DSCB1MSG            LOAD ADDRESS OF NEXT ROUTINE
         TM    FLG1BYTE,FLG1DATE       DATE INFORMATION REQUESTED ?
         BO    DATEINFO                BRANCH IF YES
DSCB1MSG EQU   *
         LA    R14,DSCB1EXT            LOAD ADDRESS OF NEXT ROUTINE
         TM    FLG1BYTE,FLG1GIN        GENERAL INFORMATION REQUESTED ?
         BO    MSGINFO                 BRANCH IF YES
DSCB1EXT EQU   *
         TM    FLG2BYTE,FLG2ON         FULL DSNAME SPECIFIED ?
         BZ    *+12                    BRANCH IF YES
         LA    R14,NXTDSCB             ELSE LOAD ADDR. OF NEXT ROUTINE
         B     *+8                         AND BRANCH
         LA    R14,NEXTUCB             LOAD ADDRESS OF NEXT ROUTINE
         TM    FLG1BYTE,FLG1EXT        EXTENT INFORMATION REQUESTED ?
         BO    EXTINFO                 BRANCH IF YES
         BR    R14                     BRANCH IF NOT
         EJECT
***********************************************************************
*                                                                     *
*      FINAL PROCESSING.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
ENDPROC  EQU   *
         NI    FLG1BYTE,FLG1NWTO       CLEAR FLAG BYTE
*        FREEMAIN R,SP=125             ISSUE FREEMAIN             /XUCB
         XUCBOFF R02                   FREE WORK AREA             /XUCB
         CLI   REPLY,C'N'              LAST REPLY = NO ?
         BE    RETURN                  BRANCH IF YES
         TM    FLG1BYTE,FLG1FND        ANY DATA SET FOUND ?
         BO    OUTRMSGN                BRANCH IF YES
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,HEADER)           DISPLAY MESSAGE LINE
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MSG06)            DISPLAY MESSAGE LINE
         B     RETURN                  BRANCH
         EJECT
*
*      OUTPUT OF GENERAL INFORMATION.
*
         SPACE 1
OUTRMSGN EQU   *
         LA    R10,MSGTAB              LOAD ADDRESS OF MESSAGE TABLE
         CLI   MSGFLAG,X'00'           MESSAGES TO BE DISPLAYED ?
         BNE   OUTREXTN                BRANCH IF NOT
         BAL   R14,OUTMSGM             BRANCH IF YES
         SPACE 2
*
*      OUTPUT OF EXTENT MESSAGES.
*
         SPACE 1
OUTREXTN EQU   *
         LA    R10,EXTNTAB             LOAD ADDRESS OF MESSAGE TABLE
         CLI   MSGFLAG,X'00'           MESSAGES TO BE DISPLAYED ?
         BNE   OUTRDTEN                BRANCH IF NOT
         BAL   R14,OUTEXTM             BRANCH IF YES
         SPACE 2
*
*      OUTPUT OF DATE MESSAGES.
*
         SPACE 1
OUTRDTEN EQU   *
         LA    R10,DATETAB             LOAD ADDRESS OF MESSAGE TABLE
         CLI   MSGFLAG,X'00'           MESSAGES TO BE DISPLAYED ?
         BNE   RETURN                  BRANCH IF NOT
         BAL   R14,OUTDATM             BRANCH IF YES
         EJECT
*
*      FREEMAIN WORK AREAS,
*      RELOAD REGISTER CONTENTS,
*      RETURN.
*
         SPACE 1
RETURN   EQU   *
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MSG07)            DISPLAY END MESSAGE
         XRETURN ,R
         EJECT
***********************************************************************
*                                                                     *
*      CATALOG INFORMATION PROCESSING.                                *
*                                                                     *
***********************************************************************
         SPACE 3
LOCATE   EQU   *
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,HEADER)           DISPLAY HEAD LINE
         TM    FLG1BYTE,FLG1CAT        CATALOG INFORMATION REQUESTED ?
         BZR   R14                     BRANCH IF NOT
         ST    R14,SAVE3R14            STORE RETURN ADDRESS
         LOCATE CAMNAME                GET CATALOG INFORMATION
         LTR   R15,R15                 RETURN CODE = 0 ?
         BNZ   LOCERROR                IF NOT, BRANCH TO ERROR ROUTINE
         LH    R05,LOCWKFLD            LOAD NUMBER OF VOLUMES
         LTR   R05,R05                 DATA SET CATALOGED ?
         BZ    LOCERROR                IF NOT, BRANCH TO ERROR ROUTINE
         LA    R10,ERRORTAB            INITIALIZE BASE REGISTER
         MVC   MESSAGE(MSG01LEN),MSG01 MOVE MESSAGE SKELETON
         MVC   MS0CVOL,LOCWKFLD+259    MOVE CVOL NUMBER
         LA    R06,LOCWKFLD+6          LOAD ADDRESS OF FIRST VOLSER
         LA    R07,20                  LOAD MAXIMUM NUMBER
         CR    R05,R07                 ACT. NUMBER > MAX. NUMBER ?
         BNH   *+6                     BRANCH IF NOT
         LR    R05,R07                 ELSE LOAD MAXIMUM NUMBER
         LA    R07,3                   INITIALIZE LOOP COUNT
         CR    R07,R05                 LOOP COUNT > NO. OF VOLSERS ?
         BNH   *+6                     BRANCH IF NOT
         LR    R07,R05                 ELSE SET NEW LOOP COUNT
         LR    R08,R07                 LOAD LOOP COUNT
         LA    R04,MS0VOL2             LOAD ADDRESS IN MESSAGE
         LH    R01,LINECNT             INCREASE
         LA    R01,1(,R01)                 OUTPUT
         STH   R01,LINECNT                 LINE COUNT
         EJECT
MOVELOOP EQU   *
         MVC   0(6,R04),0(R06)         MOVE VOLSER NUMBER
         MVI   6(R04),C','             MOVE SEPERATOR
         LA    R04,7(,R04)             LOAD NEXT AVAILABLE ADDRESS
         LA    R06,12(,R06)            LOAD ADDRESS OF NEXT VOLSER
         BCT   R07,MOVELOOP            BRANCH TO LOOP
         SPACE 1
         CR    R08,R05                 ALL VOLSERS ALREADY MOVED ?
         BL    *+10                    BRANCH IF NOT
         BCTR  R04,0                   DECREASE ADDRESS
         MVI   0(R04),C' '             CLEAR SEPERATOR
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MESSAGE)          DISPLAY MESSAGE LINE
         LH    R01,LINECNT             INCREASE
         LA    R01,1(,R01)                 OUTPUT
         STH   R01,LINECNT                 LINE COUNT
         CR    R08,R05                 ALL VOLSERS ALREADY MOVED ?
         BNL   ENDLOC                  BRANCH IF YES
         SR    R05,R08                 DECREASE NUMBER OF VOLSERS
         LA    R07,7                   INITIALIZE LOOP COUNT
         CR    R07,R05                 LOOP COUNT > NO. OF VOLUMES ?
         BNH   *+6                     BRANCH IF NOT
         LR    R07,R05                 ELSE SET NEW LOOP COUNT
         LR    R08,R07                 LOAD LOOP COUNT
         LA    R04,MS0VOL1             LOAD ADDRESS IN MESSAGE
         MVC   MS0VOL1(54),MS0VOL1-1   CLEAR MESSAGE AREA
         B     MOVELOOP                BRANCH
ENDLOC   EQU   *
         L     R14,SAVE3R14            LOAD RETURN ADDRESS
         BR    R14                         AND BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      PROCESSING OF DCB AND ALLOCATION INFORMATIONS.                 *
*                                                                     *
***********************************************************************
         SPACE 3
MSGINFO  EQU   *
         ST    R14,SAVE1R14            SAVE RETURN ADDRESS
         L     R10,SAVE1R10            INIT. BASE REGISTER
         MVI   MSGFLAG,X'00'           CLEAR FLAG
         MVC   MESSAGE(MSG03LEN),MSG03 MOVE MESSAGE SKELETON
         MVC   MS3VOLID,UCBVOLI        MOVE VOLSER NUMBER
         MVC   MS3ADR,UCBNAME          MOVE UNIT NAME
         TM    DS1DSIND,DS1RACF        RACF PROTECTION ?
         BNO   *+14                    BRANCH IF NOT
         MVC   MS3MARK,=C'RF'          ELSE SPECIFY RACF PROTECTION
         B     GETDCBIN                    AND BRANCH
         TM    DS1DSIND,DS1NOPWR       NOPWREAD PROTECTION ?
         BNO   *+14                    BRANCH IF NOT
         MVC   MS3MARK,=C'NR'          ELSE SPECIFY NOPWREAD PROTECTION
         B     GETDCBIN                    AND BRANCH
         TM    DS1DSIND,DS1PSWD        PASSWORD PROTECTION ?
         BNO   GETDCBIN                BRANCH IF NOT
         MVC   MS3MARK,=C'PW'          ELSE SPECIFY PASSWORD PROTECTION
         SPACE 2
*
*      PREPARE DCB INFORMATION.
*
         SPACE 1
GETDCBIN EQU   *
         MVC   WRKDCBIN,CONDCBIN       MOVE MASK
         LA    R06,TB1DSORG-3          LOAD ADDRESS OF 1. TABLE
         LA    R09,DS1DSORG            LOAD ADDRESS OF DSORG FIELD
DSORGLP  EQU   *
         LA    R06,3(,R06)             LOAD ADDRESS OF NEXT ELEMENT
         TM    0(R06),X'FF'            END OF TABLE ?
         BO    DSORGLPE                BRANCH IF END OF 1. TABLE
         BZ    RECFM                   BRANCH IF END OF 2. TABLE
         IC    R01,0(R06)              INSERT TABLE ELEMENT
         EX    R01,TMDSORG             BRANCH TO TEST UNDER MASK
         BZ    DSORGLP                 BRANCH IF OTHER DSORG
         MVC   WRKDSORG,1(R06)         MOVE TEXT
         CLC   WRKDSORG,TB2VSAM+1      VSAM DATA SET ?
         BNE   RECFM                   BRANCH IF NOT
         MVC   WRKDSORG+2,=C'AM'
         B     RECFM                   BRANCH
DSORGLPE EQU   *
         LA    R06,TB2DSORG            LOAD ADDRESS OF 2. TABLE
         LA    R09,DS1DSORG+1          LOAD ADDRESS OF DSORG FIELD
         B     DSORGLP                 BRANCH TO LOOP
         EJECT
RECFM    EQU   *
         TM    DS1RECFM,X'C0'          RECFM = U ?
         BO    EDBLKSI                 BRANCH IF YES
         TM    DS1RECFM,X'80'          RECFM = F ?
         BNO   *+8                     BRANCH IF NOT
         MVI   WRKRECFM,C'F'           ELSE SPECIFY RECFM = F
         TM    DS1RECFM,X'40'          RECFM = V ?
         BNO   *+8                     BRANCH IF NOT
         MVI   WRKRECFM,C'V'           ELSE SPECIFY RECFM = V
         SPACE 1
         TM    DS1RECFM,X'10'          DATA SET BLOCKED ?
         BZ    *+8                     BRANCH IF NOT
         MVI   WRKRECFM+1,C'B'         ELSE SPECIFY BLOCKED
         TM    DS1RECFM,X'08'          SPANNED, STANDARD ?
         BZ    *+8                     BRANCH IF NOT
         MVI   WRKRECFM+2,C'S'         ELSE SPECIFY SPANNED, STANDARD
         TM    DS1RECFM,X'04'          ASA CONTROL CHARACTER ?
         BZ    *+8                     BRANCH IF NOT
         MVI   WRKRECFM+3,C'A'         ELSE SPECIFY ASA CONTROL CHAR.
         TM    DS1RECFM,X'02'          MC CONTROL CHARACTER ?
         BZ    *+8                     BRANCH IF NOT
         MVI   WRKRECFM+3,C'M'         ELSE SPECIFY MC CONTROL CHAR.
         SPACE 1
         LH    R04,DS1LRECL            LOAD RECORD LENGTH
         CVD   R04,DBWORD                  AND CONVERT IT TO DECIMAL
         MVC   WRKLRECL,=X'402020202120'   MOVE EDIT MASK
         ED    WRKLRECL,DBWORD+5       EDIT RECORD LENGTH
         MVI   WRKLRECL,C','           INSERT SEPERATOR
         SPACE 1
EDBLKSI  EQU   *
         LH    R04,DS1BLKL             LOAD BLOCK SIZE
         CVD   R04,DBWORD                  AND CONVERT IT TO DECIMAL
         MVC   WRKBLKSI,=X'402020202120'   MOVE EDIT MASK
         ED    WRKBLKSI,DBWORD+5       EDIT BLOCK SIZE
         MVI   WRKBLKSI,C','           INSERT SEPERATOR
         SPACE 1
         LA    R04,WRKSHFTL            LOAD SHIFT LENGTH
         LA    R06,WRKDCBIN            LOAD SHIFT START ADDRESS
         BAL   R14,SHFTLOOP            BRANCH TO SHIFT
         MVC   MS3DCBIN,WRKDCBIN       MOVE DCB INFORMATION
         EJECT
*
*      PREPARE ALLOCATION INFORMATION.
*
         SPACE 1
         SR    R04,R04                 ZERO FOR IC COMMAND
         IC    R04,DEVCODE             LOAD DEVICE CODE
         SLA   R04,2                   DEVICE CODE * 4
         LA    R08,TABTCAP(R04)        LOAD ADDRESS OF TABLE ELEMENT
         CLI   0(R08),X'80'            DEVICE RESERVED ?
         BE    DEVERROR                IF YES BRANCH TO ERROR ROUTINE
         MVC   TABELEM,2(R08)          MOVE TABLE ELEMENT
         LH    R04,DS1LSTAR            LOAD TT ADDR. OF LAST BLOCK PTR
         CLC   DS1LSTAR(5),=XL6'0'     LAST BLOCK POINTER = 0 ?
         BE    *+16                    BRANCH IF YES
         CLI   DS1LSTAR+2,X'00'        R ADDR. = 0 ?
         BE    *+8                     BRANCH IF YES
         LA    R04,1(,R04)             ELSE INCRESE TRACK ADDRESS
         CH    R04,=H'9999'            USED TRACKS > 9999 ?
         BH    *+14                    BRANCH IF YES
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS3USED,WKAREA+11       MOVE NUMBER OF USED TRACKS
         MVC   WRKALLOC,CONALLOC       MOVE MASK
         TM    DS1SCALO,X'C0'          REQUEST IN CYLINDERS ?
         BNO   REQINTRK                BRANCH IF NOT
         MVC   WRKALTYP(3),=C'CYL'     ELSE SPEC. REQUEST IN CYL.
         TM    DS1EXT1,X'FF'           EXTENT DESCRIPTION ?
         BZ    SECALLOC                BRANCH IF NOT
         BAL   R14,EXT1LEN             BRANCH TO COMPUTE EXTENT SIZE
         LTR   R07,R07                 EXTENT IN CYLINDERS ?
         BZ    ALLOCOK                 BRANCH IF YES
         LA    R06,1(,R06)             ELSE INCREASE NUMBER OF CYL.
         B     ALLOCOK                 BRANCH
         SPACE 1
REQINTRK EQU   *
         TM    DS1SCALO,X'80'          REQUEST IN TRACKS ?
         BZ    REQINBLK                BRANCH IF NOT
         MVC   WRKALTYP(3),=C'TRK'     ELSE SPEC. REQUEST IN TRK.
         TM    DS1EXT1,X'FF'           EXTENT DESCRIPTION ?
         BZ    SECALLOC                BRANCH IF NOT
         BAL   R14,EXT1LEN             BRANCH TO COMPUTE EXTENT SIZE
         MH    R06,TABELEM             COMPUTE NUMBER
         AR    R06,R07                      OF ALLOCATED TRACKS
         B     ALLOCOK                 BRANCH
         EJECT
REQINBLK EQU   *
         TM    DS1SCALO,X'40'          REQUEST IN BLOCKS ?
         BZ    ENDSEALC                BRANCH IF NOT
         LH    R04,DS1BLKL             LOAD BLOCK SIZE
         CVD   R04,DBWORD                  AND CONVERT IT TO DECIMAL
         MVC   WRKALTYP,=X'402020202120'   MOVE EDIT MASK
         ED    WRKALTYP,DBWORD+5       EDIT BLOCK SIZE
         B     SECALLOC                BRANCH
ALLOCOK  EQU   *
         CVD   R06,DBWORD              CONV. PRIM. ALLOC. QUAN. TO DEC.
         MVC   WRKPRMAL,=X'402020202120'   MOVE EDIT MASK
         ED    WRKPRMAL,DBWORD+5       EDIT PRIMARY ALLOCATION QUAN.
         MVI   WRKPRMAL,C'('           INSERT SEPERATOR
SECALLOC EQU   *
         ICM   R04,7,DS1SCALO+1        INSERT SEC. ALLOCATION QUAN.
         CVD   R04,DBWORD                  AND CONVERT IT TO DECIMAL
         MVC   WRKSECAL,=X'402020202120'   MOVE EDIT MASK
         ED    WRKSECAL,DBWORD+5       EDIT SECONDARY ALLOCATION QUAN.
         MVI   WRKSECAL,C','           INSERT SEPERATOR
ENDSEALC EQU   *
         LA    R04,WRKASLEN            LOAD SHIFT LENGTH
         LA    R06,WRKALLOC            LOAD SHIFT START ADDRESS
         BAL   R14,SHFTLOOP            BRANCH TO SHIFT
         MVC   MS3ALLOC,WRKALLOC       MOVE ALLOCATION INFORMATION
         EJECT
*
*      COMPUTE ALLOCATED TRACKS.
*
         SPACE 1
         BAL   R14,GETEXT              BRANCH TO GET EXTENT INFORMATION
         LA    R08,EXTTAB-10           LOAD ADDR. OF EXTENT INFO. TABLE
         SR    R09,R09                 ZERO
         ST    R09,CCNO                    CYLINDER COUNT AND
         ST    R09,HHNO                    TRACK COUNT
ALOTRKLP EQU   *
         LA    R08,10(,R08)            LOAD ADDRESS OF NEXT ELEMENT
         TM    0(R08),X'FF'            END OF TABLE ?
         BO    ALOTRKEN                BRANCH IF YES
         BZ    ALOTRKLP                BRANCH IF NO EXTENT DESCRIPTIONS
         MVC   DS1EXT1,0(R08)          MOVE EXTENT DESCRIPTION
         BAL   R14,EXT1LEN             BRANCH TO COMPUTE EXTENT SIZE
         L     R09,CCNO                INCREASE
         AR    R09,R06                     CYLINDER
         ST    R09,CCNO                    COUNT
         L     R09,HHNO                INCREASE
         AR    R09,R07                     TRACK
         ST    R09,HHNO                    COUNT
         B     ALOTRKLP                BRANCH
ALOTRKEN EQU   *
         L     R04,CCNO                COMPUTE
         MH    R04,TABELEM                 NUMBER OF
         A     R04,HHNO                    ALLOCATED TRACKS
         CH    R04,=H'9999'            ALLOCATED TRACKS > 9999 ?
         BH    *+14                    BRANCH IF YES
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS3TRKS,WKAREA+11       MOVE NUMBER OF ALLOC. TRACKS
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         LA    R10,68(,R10)            LOAD ADDR. OF NEXT TABLE ELEM.
         C     R10,ADDRETB1            END OF TABLE ?
         BNL   OUTMSGM                 BRANCH IF YES
         TM    FLG2BYTE,FLG2ON         FULL DSNAME SPECIFIED ?
         BNZ   OUTMSGM1                BRANCH IF NOT
         ST    R10,SAVE1R10            STORE CURRENT ADDRESS
         BR    R14                     BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      PROCESSING OF EXTENT INFORMATION.                              *
*                                                                     *
***********************************************************************
         SPACE 3
EXTINFO  EQU   *
         ST    R14,SAVE1R14            SAVE RETURN ADDRESS
         TM    FLG1BYTE,FLG1GIN        EXTENT INFO. TABLE FILLED ?
         BO    *+8                     BRANCH IF YES
         BAL   R14,GETEXT              BR. TO FILL EXTENT INFO. TABLE
         L     R10,SAVE2R10            INIT. BASE REGISTER
         SR    R05,R05                 ZERO OUTPUT LINE COUNT
         LA    R01,1                   INITIALIZE FOR CR COMMAND
         ZAP   EXTCOUNT,=P'+0'         ZERO EXTENT COUNT
         LA    R06,EXTTAB              LOAD ADDR. OF EXTENT INFO. TABLE
LOOP1    EQU   *
         TM    0(R06),X'FF'            END OF TABLE ?
         BO    ENDLOOP                 BRANCH IF YES
         LA    R05,1(,R05)             INCREASE OUTPUT LINE COUNT
         MVI   MSGFLAG,X'00'           CLEAR FLAG
         MVC   MESSAGE(MSG04LEN),MSG04 MOVE MESSAGE SKELETON
         C     R10,ADDRETB1            FIRST LINE IN MESSAGE ?
         BNE   LOOP2-8                 BRANCH IF NOT
         MVC   MS5VOLID,=C'VOLSER'     PREPARE
         MVC   MS5NOEXT,=C'NO'             HEAD LINE
         LA    R07,MS4EXT1-14          LOAD ADDRESS IN MESSAGE
         LA    R08,3                   INITIALIZE LOOP COUNT
LOOP2    EQU   *
         LA    R07,14(,R07)            LOAD NEXT ADDRESS IN MESSAGE
         AP    EXTCOUNT,=P'+1'         INCREASE EXTENT COUNT
         UNPK  0(2,R07),EXTCOUNT       STORE EXTENT COUNT
         OI    1(R07),C'0'             MODIFY SIGN
         BCT   R08,LOOP2               BRANCH
         LA    R10,68(,R10)            LOAD ADDR. OF NEXT TABLE ELEM.
         MVC   MESSAGE(MSG04LEN),MSG04 MOVE MESSAGE SKELETON
         MVC   MESSAGE+12(54),MESSAGE+11 CLEAR MESSAGE AREA
         MVI   MSGFLAG,X'00'           CLEAR FLAG
         CR    R05,R01                 SECOND OUTPUT LINE
         BNE   LOOP3-8                 BRANCH IF NOT
         MVC   MS5VOLID,UCBVOLI        MOVE VOLSER NUMBER
         LH    R04,NOEXT               LOAD NUMBER OF EXTENTS
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS5NOEXT,WKAREA+13      MOVE NUMBER OF EXTENTS
         LA    R07,MS5EXT-14           LOAD ADDRESS IN MESSAGE
         LA    R08,3                   INITIALIZE LOOP COUNT
         EJECT
LOOP3    EQU   *
         LA    R07,14(,R07)            LOAD NEXT ADDRESS IN MESSAGE
         TM    0(R06),X'FF'            EXTENT DESCRIPTION AVAILABLE ?
         BZ    EOFLOOP3                BRANCH IF NOT
         ICM   R04,3,2(R06)            LOAD CC START ADDR. OF EXTENT
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   0(3,R07),WKAREA+12      MOVE CC START ADDR. OF EXTENT
         MVI   3(R07),C'/'             INSERT SEPERATOR
         ICM   R04,3,4(R06)            LOAD HH START ADDR. OF EXTENT
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   4(2,R07),WKAREA+13      MOVE HH START ADDR. OF EXTENT
         ICM   R04,3,6(R06)            LOAD CC END ADDRESS OF EXTENT
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   7(3,R07),WKAREA+12      MOVE CC END ADDR. OF EXTENT
         MVI   10(R07),C'/'            INSERT SEPERATOR
         ICM   R04,3,8(R06)            LOAD HH END ADDR. OF EXTENT
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   11(2,R07),WKAREA+13     MOVE HH END ADDR. OF EXTENT
         MVI   6(R07),C'-'
EOFLOOP3 EQU   *
         LA    R05,1(,R05)             INCREASE LINE COUNT
         LA    R06,10(,R06)            LOAD ADDRESS OF NEXT ELEMENT
         TM    0(R06),X'FF'            END OF TABLE ?
         BO    ENDLOOP                 BRANCH IF YES
         BCT   R08,LOOP3               BRANCH
         LA    R10,68(,R10)            LOAD ADDR. OF NEXT TABLE ELEM.
         B     LOOP1                   BRANCH
ENDLOOP  EQU   *
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         LA    R10,68(,R10)            LOAD ADDR. OF NEXT TABLE ELEM.
         C     R10,ADDRETB2            END OF TABLE ?
         BNL   OUTEXTM                 BRANCH IF YES
         TM    FLG2BYTE,FLG2ON         FULL DSNAME SPECIFIED ?
         BNZ   OUTEXTM1                BRANCH IF NOT
         ST    R10,SAVE2R10            STORE CURRENT ADDRESS
         BR    R14                     BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      PROCESSING OF CREATION AND EXPIRATION DATE.                    *
*                                                                     *
***********************************************************************
         SPACE 3
DATEINFO EQU   *
         ST    R14,SAVE1R14            SAVE RETURN ADDRESS
         L     R10,SAVE3R10            INIT. BASE REGISTER
         MVI   MSGFLAG,X'00'           CLEAR FLAG
         MVC   MESSAGE(MSG05LEN),MSG05 MOVE MESSAGE SKELETON
         MVC   MS6VOLID,UCBVOLI        MOVE VOLSER NUMBER
         SR    R04,R04                 ZERO FOR IC COMMAND
         IC    R04,DS1CREDT            LOAD CREATION DATE (YEAR)
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS6CREDT(2),WKAREA+13   MOVE YEAR
         IC    R04,DS1EXPDT            LOAD EXPIRATION DATE (YEAR)
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS6EXPDT(2),WKAREA+13   MOVE YEAR
         ICM   R04,3,DS1CREDT+1        LOAD CREATION DATE (DAYS)
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS6CREDT+3(3),WKAREA+12 MOVE DAYS
         ICM   R04,3,DS1EXPDT+1        LOAD EXPIRATION DATE (DAYS)
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS6EXPDT+3(3),WKAREA+12 MOVE DAYS
         MVI   MS6EXPDT+2,C'.'         INSERT DECIMAL POINT
         MVI   MS6CREDT+2,C'.'         INSERT DECIMAL POINT
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         LA    R10,68(,R10)            LOAD ADDR. OF NEXT TABLE ELEM.
         C     R10,ADDRETB3            END OF TABLE ?
         BNL   OUTDATM                 BRANCH IF YES
         TM    FLG2BYTE,FLG2ON         FULL DSNAME SPECIFIED ?
         BNZ   OUTDATM1                BRANCH IF NOT
         ST    R10,SAVE3R10            STORE CURRENT ADDRESS
         BR    R14                     BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      ERROR ROUTINES.                                                *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      INPUT PARAMETER ERROR ROUTINE.
*
         SPACE 1
PRMERROR EQU   *
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MSG08)            DISPLAY ERROR MESSAGE
         B     RETURN                  BRANCH
         SPACE 2
*
*      OBTAIN ERROR ROUTINE.
*
         SPACE 1
OBTERROR EQU   *
         TM    FLG2BYTE,FLG2ON         FULL DSNAME SPECIFIED ?
         BNZ   *+12                    BRANCH IF NOT
         CH    R15,=H'8'               DSCB1 NOT FOUND ?
         BE    NEXTUCB                 BRANCH IF YES
         LA    R10,ERRORTAB            INIT. BASE REGISTER
         MVC   MESSAGE(MSG10LEN),MSG10 MOVE MESSAGE SKELETON
         MVC   MS11VOLI,UCBVOLI        MOVE VOLSER NUMBER
         LR    R04,R15                 LOAD RETURN CODE
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS11RC,WKAREA+13        MOVE RETURN CODE
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MESSAGE)          DISPLAY MESSAGE
         TM    FLG2BYTE,FLG2ON         FULL DSNAME SPECIFIED ?
         BNZ   NXTDSCB                 BRANCH IF NOT
         B     NEXTUCB                 BRANCH IF YES
         EJECT
*
*      ROUTINE FOR RESERVED DEVICES.
*
         SPACE 1
DEVERROR EQU   *
         LA    R10,ERRORTAB            INIT. BASE REGISTER
         MVC   MESSAGE(MSG09LEN),MSG09 MOVE MESSAGE SKELETON
         UNPK  MS10DEVC(3),DEVCODE(2)  MOVE DEVICE CODE
         TR    MS10DEVC,TABHEXA-240    EDIT DEVICE CODE
         MVC   MS10VOLI,UCBVOLI        MOVE VOLSER NUMBER
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MESSAGE)          DISPLAY MESSAGE
         B     NEXTUCB                 BRANCH
         SPACE 2
*
*      LOCATE ERROR ROUTINE.
*
         SPACE 1
LOCERROR EQU   *
         LA    R10,ERRORTAB            INIT. BASE REGISTER
         CH    R15,=H'8'               RETURN CODE = 8 ?
         BE    NONCATLG                BRANCH IF YES
         LTR   R05,R05                 CATALOG INFORMATION ?
         BZ    NONCATLG                BRANCH IF NOT
         MVC   MESSAGE(MSG11LEN),MSG11 MOVE MESSAGE SKELETON
         LR    R04,R15                 LOAD RETURN CODE
         BAL   R14,CONVDEC             BRANCH TO CONVERT TO DECIMAL
         MVC   MS12RC,WKAREA+13        MOVE RETURN CODE
WTOFMSG  EQU   *
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MESSAGE)          DISPLAY MESSAGE
         L     R14,SAVE3R14            LOAD RETURN ADDRESS
         BR    R14                         AND BRANCH
NONCATLG EQU   *
         MVC   MESSAGE(MSG12LEN),MSG12 MOVE MESSAGE SKELETON
         B     WTOFMSG                 BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE OUTPUT.                                                *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      DISPLAY GENERAL INFORMATION MESSAGES.
*
         SPACE 1
OUTMSGM  EQU   *
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,HEADER)           DISPLAY HEAD LINE
         LH    R01,LINECNT             INCREASE
         LA    R01,1(,R01)                 OUTPUT
         STH   R01,LINECNT                 LINE COUNT
OUTMSGM1 EQU   *
         ST    R14,SAVE1R14            SAVE RETURN ADDRESS
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MSG02)            DISPLAY 2. MESSAGE
         LH    R01,LINECNT             INCREASE
         LA    R01,1(,R01)                 OUTPUT
         STH   R01,LINECNT                 LINE COUNT
         LA    R10,MSGTAB-68           LOAD ADDRESS OF TABLE
         L     R09,ADDRETB1            LOAD END ADDRESS OF TABLE
         BAL   R14,OUTPUT              BRANCH TO DISPLAY SINGLE LINES
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         ST    R10,SAVE1R10                AND STORE IT
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         BR    R14                     BRANCH
         EJECT
*
*      DISPLAY EXTENT INFORMATION MESSAGES.
*
         SPACE 1
OUTEXTM  EQU   *
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,HEADER)           DISPLAY HEAD LINE
         LH    R01,LINECNT             INCREASE
         LA    R01,1(,R01)                 OUTPUT
         STH   R01,LINECNT                 LINE COUNT
OUTEXTM1 EQU   *
         ST    R14,SAVE1R14            SAVE RETURN ADDRESS
         LA    R10,EXTNTAB-68          LOAD ADDRESS OF TABLE
         LA    R09,DATETAB             LOAD END ADDRESS OF TABLE
         BAL   R14,OUTPUT              BRANCH TO DISPLAY SINGLE LINES
         LA    R10,EXTNTAB             LOAD ADDRESS OF TABLE
         ST    R10,SAVE2R10                AND STORE IT
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         BR    R14                     BRANCH
         EJECT
*
*      DISPLAY DATE INFORMATION MESSAGES.
*
         SPACE 1
OUTDATM  EQU   *
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,HEADER)           DISPLAY HEAD LINE
         LH    R01,LINECNT             INCREASE
         LA    R01,1(,R01)                 OUTPUT
         STH   R01,LINECNT                 LINE COUNT
OUTDATM1 EQU   *
         ST    R14,SAVE1R14            SAVE RETURN ADDRESS
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MSG05)            DISPLAY 2. MESSAGE
         LH    R01,LINECNT             INCREASE
         LA    R01,1(,R01)                 OUTPUT
         STH   R01,LINECNT                 LINE COUNT
         LA    R10,DATETAB-68          LOAD ADDRESS OF TABLE
         L     R09,ADDRETB3            LOAD END ADDRESS OF TABLE
         BAL   R14,OUTPUT              BRANCH TO DISPLAY SINGLE LINES
         LA    R10,DATETAB             LOAD ADDRESS OF TABLE
         ST    R10,SAVE3R10                AND STORE IT
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         BR    R14                     BRANCH
         EJECT
*
*      DISPLAY SINGLE LINES.
*
         SPACE 1
OUTPUT   EQU   *
         ST    R14,SAVE2R14            SAVE RETURN ADDRESS
         LH    R04,LINECNT             LOAD OUTPUT LINE COUNT
NEXTMSG  EQU   *
         LA    R10,68(,R10)            LOAD ADDR. OF NEXT TABLE ELEM.
         CR    R10,R09                 END OF TABLE ?
         BNL   ENDMSG                  BRANCH IF YES
         CLI   MSGFLAG,X'FF'           END OF TABLE ?
         BE    ENDMSG                  BRANCH IF YES
         MVI   MSGFLAG,X'FF'           SET FLAG
         L     R00,UCMID               LOAD UCM-ID.
         WTO   MF=(E,MESSAGE)          DISPLAY ONE MESSAGE
         LA    R04,1(,R04)             INCREASE OUTPUT LINE COUNT
         B     NEXTMSG                 BRANCH
ENDMSG   EQU   *
         STH   R04,LINECNT             STORE OUTPUT LINE COUNT
         CH    R04,=H'10'              MORE THAN 10 LINES DISPLAYED ?
         BL    ENDMSG1                 BRANCH IF NOT
         TM    FLG1BYTE,FLG1WTOR       WTOR LINE OUTPUT ?
         BZ    ENDMSG1                 BRANCH IF NOT
         XC    LINECNT,LINECNT         ZERO OUTPUT LINE COUNT
NXTREPLY EQU   *
         XC    ECB,ECB                 CLEAR EVENT CONTROL BLOCK
         MVI   REPLY,C' '              CLEAR REPLY AREA
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         LA    R04,ECB                 LOAD ADDRESS OF ECB
         LA    R05,REPLY               LOAD ADDRESS OF REPLY
         WTOR  ,(R05),1,(R04),MF=(E,WTOR)
         WAIT  ECB=ECB                 WAIT FOR COMPLETION
         CLI   REPLY,C'N'              REPLY = NO ?
         BE    ENDPROC                 BRANCH IF YES
         CLI   REPLY,C'Y'              REPLY = YES ?
         BNE   NXTREPLY                BRANCH IF NOT
ENDMSG1  EQU   *
         L     R14,SAVE2R14            LOAD RETURN ADDRESS
         BR    R14                     BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      FILL EXTENT INFORMATION TABLE.                                 *
*                                                                     *
***********************************************************************
         SPACE 3
GETEXT   EQU   *
         ST    R14,SAVE2R14            SAVE RETURN ADDRESS
         SR    R04,R04                 ZERO FOR IC COMMAND
         IC    R04,DS1NOEPV            INSERT NUMBER OF EXTENTS
         STH   R04,NOEXT                   AND STORE IT
         LA    R06,EXTTAB-10           LOAD ADDR. OF EXTENT INFO. TABLE
         SR    R05,R05                 ZERO REGISTER
         SPACE 2
*
*      GET THE FIRST THREE EXTENTS DESCRIPTIONS.
*
         SPACE 1
         LA    R07,DS1EXT1-10          LOAD ADDR. OF EXTENT DESCR.
         LA    R01,3                   MAX. NUMBER OF EXTENT DESCR.
         BAL   R14,NXTEXT              BRANCH TO MOVE EXTENT DESCR.
         CR    R05,R04                 NO EXTENT DESCR. LEFT ?
         BNL   ENDEXT                  BRANCH IF YES
         SPACE 2
*
*      GET THE FOLLOWING FOUR EXTENT DESCRIPTIONS.
*
         SPACE 1
         MVC   CCHHR,DS1PTRDS          MOVE ADDRESS OF NEXT DSCB
         OBTAIN CAMSEEK                OBTAIN NEXT DSCB
         LTR   R15,R15                 RETURN CODE = 0 ?
         BNZ   OBTERROR                IF NOT, BRANCH TO ERROR ROUTINE
         CLI   DS2FMTID,C'2'           DSCB = DSCB2 ?
         BNE   DSCB3OK                 BRANCH IF NOT
         MVC   CCHHR,DS2PTRDS          MOVE ADDRESS OF NEXT DSCB
         OBTAIN CAMSEEK                OBTAIN NEXT DSCB
         LTR   R15,R15                 RETURN CODE = 0 ?
         BNZ   OBTERROR                IF NOT, BRANCH TO ERROR ROUTINE
         EJECT
DSCB3OK  EQU   *
         LA    R15,99                  SET RETURN CODE
         CLI   DS3FMTID,C'3'           DSCB = DSCB3 ?
         BNE   OBTERROR                BRANCH IF NOT
         LA    R07,DS3EXTNT-10         LOAD ADDRESS OF EXTENT DESCR.
         LA    R01,7                   MAX. NUMBER OF EXTENT DESCR.
         BAL   R14,NXTEXT              BRANCH TO MOVE EXTENT DESCR.
         CR    R05,R04                 NO EXTENT DESCR. LEFT ?
         BNL   ENDEXT                  BRANCH IF YES
         SPACE 2
*
*      GET THE LAST NINE EXTENT DESCRIPTIONS.
*
         SPACE 1
         LA    R07,DS3ADEXT-10         LOAD ADDRESS OF EXTENT DESCR.
         LA    R01,16                  MAX. NUMBER OF EXTENT DESCR.
         BAL   R14,NXTEXT              BRANCH TO MOVE EXTENT DESCR.
         SPACE 2
ENDEXT   EQU   *
         CR    R05,R04                 ALL EXTENT DESCR. MOVED ?
         BH    *+8                     BRANCH IF YES
         LA    R06,10(,R06)            ELSE INCREASE ADDRESS IN TABLE
         MVI   0(R06),X'FF'            SPECIFY TABLE END
         L     R14,SAVE2R14            LOAD RETURN ADDRESS
         BR    R14                     BRANCH
         SPACE 2
*
*      MOVE EXTENT DESCRIPTIONS INTO TABLE.
*
         SPACE 1
NXTEXT   EQU   *
         LA    R05,1(,R05)             INCREASE NO. OF EXTENT DESCR.
         LA    R06,10(,R06)            LOAD ADDRESS OF NEXT ELEMENT
         LA    R07,10(,R07)            LOAD ADDR. OF NEXT EXT. DESCR.
         CR    R05,R04                 NO EXTENT DESCR. LEFT ?
         BHR   R14                     BRANCH IF YES
         MVC   0(10,R06),0(R07)        MOVE EXTENT DESCRIPTION
         CR    R05,R01                 EXTENT DESCR. LEFT ?
         BL    NXTEXT                  BRANCH IF YES
         BR    R14                     BRANCH IF NOT
         EJECT
***********************************************************************
*                                                                     *
*      COMPUTE SIZE OF A SINGLE EXTENT.                               *
*                                                                     *
***********************************************************************
         SPACE 3
EXT1LEN  EQU   *
         MVC   DBWORD,DS1EXT1+2        MOVE EXTENT DESCRIPTION
         LH    R04,DBWORD              LOAD CC START ADDRESS OF EXTENT
         LH    R05,DBWORD+2            LOAD HH START ADDRESS OF EXTENT
         LH    R06,DBWORD+4            LOAD CC END ADDRESS OF EXTENT
         LH    R07,DBWORD+6            LOAD HH END ADDRESS OF EXTENT
         CR    R07,R05                 HH END ADDR. > HH START ADDR. ?
         BNL   ADDROK                  BRANCH IF YES
         BCTR  R06,0                   ELSE DECREASE CC END ADDRESS
         AH    R07,TABELEM                 AND INCREASE HH END ADDRESS
ADDROK   EQU   *
         SR    R06,R04                 COMPUTE NUMBER OF CYLINDERS
         LA    R07,1(,R07)             COMPUTE NUMBER
         SR    R07,R05                     OF TRACKS
         BR    R14                     BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      CONVERT BINARY TO UNPACKED DECIMAL NUMBERS.                    *
*                                                                     *
***********************************************************************
         SPACE 3
CONVDEC  EQU   *
         CVD   R04,DBWORD              CONVERT TO DECIMAL
         UNPK  WKAREA,DBWORD           UNPACK
         OI    WKAREA+14,C'0'          MODIFY SIGN
         BR    R14                     BRANCH
         SPACE 3
***********************************************************************
*                                                                     *
*      SHIFT LEFT TO SUPPRESS BLANKS.                                 *
*                                                                     *
***********************************************************************
         SPACE 3
SHFTLOOP EQU   *
         CLI   0(R06),C' '             BLANK IN THIS PLACE ?
         BNE   NEXTCHAR                BRANCH IF NOT
         EX    R04,SHIFT               ELSE BRANCH TO SHIFT
         B     SHFTCNT                 BRANCH
NEXTCHAR EQU   *
         LA    R06,1(,R06)             LOAD ADDRESS OF NEXT CHARACTER
SHFTCNT  EQU   *
         BCT   R04,SHFTLOOP            BRANCH TO LOOP
         BR    R14                     RETURN
         EJECT
***********************************************************************
*                                                                     *
*      COMMANDS ISSUED BY EX COMMAND.                                 *
*                                                                     *
***********************************************************************
         SPACE 3
MOVEPNAM MVC   DSNAME(0),0(R04)        MOVE DATA SET NAME
MOVEPVOL MVC   1(0,R06),0(R04)         MOVE VOLSER NUMBER
NCVOLID1 NC    WKVOLID1(0),1(R06)      NC VOLID'S
OCVOLID2 OC    WKVOLID2(0),1(R07)      OC VOLID'S
TMDSORG  TM    0(R09),X'00'            DSORG FIELD = TABLE ELEMENT ?
SHIFT    MVC   0(0,R06),1(R06)         SHIFT LEFT
TRT      TRT   0(0,R04),TRTTABLE       FIND SEPERATORS
TESTNAME CLC   DS1DSNAM(0),DSNAME      TEST DATA SET NAME
         SPACE 3
***********************************************************************
*                                                                     *
*      REGISTER EQUATES.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
R00      EQU   0                       MACRO AND PARAMETER REGISTER
R01      EQU   1                       MACRO AND PARAMETER REGISTER
R02      EQU   2                       PTR. IN CVT / UCB LOOKUP TABLE
R03      EQU   3                       BASE REGISTER OF UCB
R04      EQU   4                       WORK REGISTER
R05      EQU   5                       WORK REGISTER
R06      EQU   6                       WORK REGISTER
R07      EQU   7                       WORK REGISTER
R08      EQU   8                       WORK REGISTER
R09      EQU   9                       BASE REG. OF PARMAREA / VECTOR
R10      EQU   10                      POINTER IN MESSAGE TABLES
R11      EQU   11                      1. BASE REGISTER OF CSECT
R12      EQU   12                      2. BASE REGISTER OF CSECT
R13      EQU   13                      BASE REGISTER OF WORKAREA
R14      EQU   14                      RETURN ADDRESSES
R15      EQU   15                      CSECT ADDRESSES AND RETURN CODES
         EJECT
***********************************************************************
*                                                                     *
*      CONSTANTS.                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      TRANSLATE TABLES.
*
         SPACE 1
TABHEXA  DC    C'0123456789ABCDEF'
         SPACE 1
TRTTABLE DC    64X'00',C' ',42X'00',C',',148X'00'
TRTCOMMA EQU   TRTTABLE+107
         SPACE 1
TRTABLE  DC    X'000102030405060708090A0B0C0D0E0F'
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'
         DC    X'303132333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5BFF5D5E5F' X'5C' -> X'FF'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'808182838485868788898A8B8C8D8E8F'
         DC    X'909192939495969798999A9B9C9D9E9F'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFE00' X'FF' -> X'00'
         SPACE 2
*
*      MASKS FOR UPDATE OF DCB AND ALLOCATION INFORMATIONS.
*
         SPACE 1
CONDCBIN DC    C'*   ,U   ,*    ,*     '
CONALLOC DC    C'*     ,(*    ,*    ) '
         EJECT
*
*      TRACKS / CYLINDER ON DASD DEVICES.
*
         SPACE 1
TABTCAP  DS    0F
         DC    X'8000',H'0'  00 - RESERVED
         DC    F'10'         01 - 2311    DISK STORAGE DEVICE
         DC    F'08'         02 - 2301    PARALLEL DRUM
         DC    F'10'         03 - 2303    SERIAL DRUM
         DC    F'46'         04 - 2302    DISK STORAGE
         DC    F'20'         05 - 2321    DATA CELL DRIVE
         DC    F'08'         06 - 2305/1  FIXED HEAD STORAGE / MODEL 1
         DC    F'08'         07 - 2305/2  FIXED HEAD STORAGE / MODEL 2
         DC    F'20'         08 - 2314    DISK STORAGE FACILITY
         DC    F'19'         09 - 3330/1  DISK STORAGE / MODEL 1
         DC    F'12'         0A - 3340    DISK STORAGE
         DC    F'30'         0B - 3350    DISK STORAGE
         DC    X'8000',H'0'  0C - RESERVED
         DC    F'19'         0D - 3330/11 DISK STORAGE / MODEL 11
         DC    F'15'         0E - 3380
         DC    X'8000',H'0'  0F - RESERVED
         SPACE 2
*
*      DSORG BIT AND TEXT TABLE.
*
         SPACE 1
TB1DSORG EQU   *
         DC    X'80',C'IS'   INDEXED SEQUENTIAL ORGANISATION
         DC    X'40',C'PS'   PHYSICAL SEQUENTIAL ORGANISATION
         DC    X'20',C'DA'   DIRECT ORGANISATION
         DC    X'10',C'CX'   BTAM OR QTAM LINE GROUP
         DC    X'08',C'CQ'   QTAM DIRECT ACCESS MESSAGE QUEUE
         DC    X'04',C'MQ'   QTAM PROBLEM PROGRAM MESSAGE QUEUE
         DC    X'02',C'PO'   PARTITIONED ORGANISATION
         DC    X'01',C'U '   UNMOVABLE
         DC    X'FF'         TABLE END
TB2DSORG EQU   *
         DC    X'80',C'GS'   GRAPHICS ORGANISATION
         DC    X'40',C'TX'   TCAM LINE GROUP
         DC    X'20',C'TQ'   TCAM MESSAGE QUEUE
TB2VSAM  DC    X'08',C'VS'   ACB (VSAM)
         DC    X'04',C'TR'   TCAM 3705
         DC    X'00'         TABLE END
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE SKELETONS.
*                                                                     *
***********************************************************************
         SPACE 3
MSG00    WTO   'XSHOW00 DSN=                                           *
                ',                                                     *
               MF=L,MCSFLAG=(REG0)
MSG00LEN EQU   *-MSG00
         SPACE 1
MSG01    WTO   'XSHOW01 CATALOG ON 123456 POINTS TO:                   *
                      ',                                               *
               MF=L,MCSFLAG=(REG0)
MSG01LEN EQU   *-MSG01
         SPACE 1
MSG02    WTO   'XSHOW02 VOLSER ADR --ALLOCATED-- TRKS USED DCB-INFORMAT*
               ION  M',                                                *
               MF=L,MCSFLAG=(REG0)
MSG02LEN EQU   *-MSG02
         SPACE 1
MSG03    WTO   'XSHOW02 123456 123 --ALLOCATED-- **** ****             *
                      ',                                               *
               MF=L,MCSFLAG=(REG0)
MSG03LEN EQU   *-MSG03
         EJECT
MSG04    WTO   'XSHOW04           --EXTENT01--- --EXTENT02--- --EXTENT0*
               3---',                                                  *
               MF=L,MCSFLAG=(REG0)
MSG04LEN EQU   *-MSG04
         SPACE 1
MSG05    WTO   'XSHOW05 VOLSER CREDTE EXPDTE',                         *
               MF=L,MCSFLAG=(REG0)
MSG05LEN EQU   *-MSG05
         SPACE 1
MSG06    WTO   'XSHOW06 SPECIFIED DATA SET NOT FOUND',                 *
               MF=L,MCSFLAG=(REG0)
MSG06LEN EQU   *-MSG06
         SPACE 1
MSG07    WTO   'XSHOW07 END OF PROCESSING',                            *
               MF=L,MCSFLAG=(REG0)
MSG07LEN EQU   *-MSG07
         SPACE 1
MSG08    WTO   'XSHOW08 ERROR IN  PARAMETER LIST',                     *
               MF=L,MCSFLAG=(REG0)
MSG08LEN EQU   *-MSG08
         EJECT
MSG09    WTO   'XSHOW09 VOLSER=123456, DEVICE FAILURE, DEVCODE=99',    *
               MF=L,MCSFLAG=(REG0)
MSG09LEN EQU   *-MSG09
         SPACE 1
MSG10    WTO   'XSHOW10 VOLSER=123456, OBTAIN FAILURE, RC=99',         *
               MF=L,MCSFLAG=(REG0)
MSG10LEN EQU   *-MSG10
         SPACE 1
MSG11    WTO   'XSHOW11 LOCATE FAILURE, RC=99',                        *
               MF=L,MCSFLAG=(REG0)
MSG11LEN EQU   *-MSG11
         SPACE 1
MSG12    WTO   'XSHOW12 SPECIFIED DATA SET IS NOT CATALOGED',          *
               MF=L,MCSFLAG=(REG0)
MSG12LEN EQU   *-MSG12
         SPACE 1
MSG13    WTOR  'XSHOW13 REPLY ''Y'' (YES) OR ''N'' (NO) TO CONTINUE',  *
               MF=L,MCSFLAG=(REG0)
MSG13LEN EQU   *-MSG13
         EJECT
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*      WORK AREAS.                                                    *
*                                                                     *
***********************************************************************
         SPACE 3
WORKAREA DSECT
SAVEAREA DS    18F                     SAVEAREA OF GENERAL REGISTERS
         SPACE 2
*
*      ADDRESS AREAS.
*
         SPACE 1
ADDRETB1 DS    F                       END ADDRESS OF MESSAGE TABLE 1
ADDRETB2 DS    F                       END ADDRESS OF MESSAGE TABLE 2
ADDRETB3 DS    F                       END ADDRESS OF MESSAGE TABLE 3
ADDRETPV DS    F                       END ADDRESS OF VOLSER TABLE
SAVE1R10 DS    F                       SAVEAREA 1 OF R10
SAVE2R10 DS    F                       SAVEAREA 2 OF R10
SAVE3R10 DS    F                       SAVEAREA 3 OF R10
SAVE1R14 DS    F                       SAVEAREA 1 OF R14
SAVE2R14 DS    F                       SAVEAREA 2 OF R14
SAVE3R14 DS    F                       SAVEAREA 3 OF R14
UCBADDR  DS    F                       ADDRESS OF UCB ADDR. VECTOR
         SPACE 2
*
*      MULTI CONTROL FLAG BYTES.
*
         SPACE 1
FLG1BYTE DS    C                       FLAG BYTE 1
FLG1INIT EQU   B'00000000'             INITIAL VALUE
FLG1INFO EQU   B'11110000'             INFORMATION PARAMETERS
FLG1GIN  EQU   B'10000000'             GENERAL INFORMATION
FLG1GINC EQU   B'10010000'             GENERAL AND CATALOG INFORMATION
FLG1EXT  EQU   B'01000000'             EXTENT INFORMATION
FLG1DATE EQU   B'00100000'             DATE INFORMATION
FLG1CAT  EQU   B'00010000'             CATALOG INFORMATION
FLG1VOL  EQU   B'00001000'             SPECIFIC VOLSER
FLG1WTOR EQU   B'00000100'             WTOR MESSAGE OUTPUT
FLG1NWTO EQU   B'11111011'             NO WTOR MESSAGE OUTPUT
FLG1FND  EQU   B'00000010'             DATA SET(S) FOUND
         SPACE 1
FLG2BYTE DS    C                       FLAG BYTE 2
*LG2RACF EQU   B'10000000'             RACF PROTECTION
*LG2PWRD EQU   B'01000000'             PASSWORD PROTECTION
*LG2NOPW EQU   B'00100000'             NO PASSWORD READ PROTECTION
FLG2INLV EQU   B'00010000'             SHOW - INDEX LEVEL
*LG2PROT EQU   B'11100000'             PROTECTION
FLG2ON   EQU   B'11110000'             FLAG ON
FLG2INIT EQU   B'00000000'             INITIAL VALUE
         EJECT
*
*      WORK SPACES.
*
         SPACE 1
DBWORD   DS    D                       CVD WORK AREA
ECB      DS    F                       EVENT CONTROL BLOCK
UCMID    DS    F                       UCM ID OF CALLER
CCNO     DS    F                       COUNT OF ALLOCATED CYLINDERS
HHNO     DS    F                       COUNT OF ALLOCATED TRACKS
DSNMLEN  DS    H                       LENGTH OF SPECIFIED NAME
NOEXT    DS    H                       NUMBER OF EXTENTS
TABELEM  DS    H                       ELEMENT OF TABTCAP
LINECNT  DS    H                       OUTPUT LINE COUNT
EXTCOUNT DS    CL2                     DECIMAL COUNT
         DS    0H
CCHHR    DS    CL5                     CCHHR ADDRESS OF NEXT DSCB
WKAREA   DS    CL15                    DECIMAL WORK AREA
VOLSERNO DS    CL6                     VOLSER WORK AREA
WKVOLID1 DS    CL6                     VOLSER WORK AREA
WKVOLID2 DS    CL6                     VOLSER WORK AREA
REPLY    DS    C                       REPLY AREA
WRKDCBIN DS    0CL22                   DCB INFORMATION
WRKDSORG DS    CL2                     DATA SET ORGANISATION (PART 1)
         DS    CL2                     DATA SET ORGANISATION (PART 2)
         DS    C                       SEPERATOR
WRKRECFM DS    CL4                     RECORD FORMAT
WRKBLKSI DS    CL6                     BLOCK SIZE
WRKLRECL DS    CL6                     RECORD LENGTH
         DS    C
WRKSHFTL EQU   *-WRKDCBIN-2            SHIFT LENGTH
WRKALLOC DS    0CL21                   ALLOCATION-INFORMATIONEN
WRKALTYP DS    CL6                     ALLOCATION TYPE
         DS    C                       SEPERATOR
WRKPRMAL DS    CL6                     PRIMARY ALLOCATION QUANTITY
WRKSECAL DS    CL6                     SECONDARY ALLOCATION QUANTITY
         DS    CL2
WRKASLEN EQU   *-WRKALLOC-2            SHIFT LENGTH
MDS1ADDR DS    3H                      HIGHEST DISK ADDR. OF DSCB1
         ORG   MDS1ADDR
MDS1CC   DS    H                       MAX. CYLINDER NUMBER (DS4HPCR)
MDS1HH   DS    H                       MAX. HEADER NUMBER (DS4HPCR+2)
MDS1R    DS    H                       MAX. RECORD NUMBER (DS4HPCR+4)
MCURADDR DS    2H                      MAX. CURRENT ADDRESS
         ORG   MCURADDR
MTRKNO   DS    H                       MAX. HEADER NUMBER (DS4DEVSZ+2)
MRECNO   DS    H                       MAX. RECORD NUMBER (DS4DEVT)
CURADDR  DS    3H                      CURRENT ADDRESS
         ORG   CURADDR
CURCYLNO DS    H                       CURRENT CYLINDER NUMBER
CURTRKNO DS    H                       CURRENT HEADER NUMBER
CURRECNO DS    H                       CURRENT RECORD NUMBER
         EJECT
*
*      CAMLIST MACROS (WORK AREA).
*
         SPACE 1
CAMSEEK  DS    0F
         DS    C                       AL1(192)
         DS    C                       AL1(128)
         DS    C                       AL1(0)
         DS    C                       AL1(0)
CADRCCHH DS    F                       A(CCHHR)
CADRUCBV DS    F                       A(VOLSERNO)
CADRCAMW DS    F                       A(OBTWKFLD)
         SPACE 1
CAMSRCH  DS    0F
         DS    C                       AL1(193)
         DS    C                       AL1(0)
         DS    C                       AL1(0)
         DS    C                       AL1(0)
CADRDSN  DS    F                       A(DSNAME)
CADUCBVO DS    F                       A(VOLSERNO)
CADCAMWK DS    F                       A(OBTWKFLD+44)
         SPACE 1
CAMNAME  DS    0F
         DS    C                       AL1(68)
         DS    C                       AL1(0)
         DS    C                       AL1(0)
         DS    C                       AL1(0)
CAMDSN   DS    F                       A(DSNAME)
         DS    F                       A(0)
CAMCWK   DS    F                       A(LOCWKFLD)
         SPACE 1
OBTWKFLD DS    CL140                   OBTAIN WORK AREA
         DS    0D
LOCWKFLD DS    CL265                   LOCATE WORK AREA
ENDCAMWK EQU   *
         EJECT
*
*      DATA SET CONTROL BLOCK FORMAT 1.
*
         SPACE 1
         ORG   OBTWKFLD
DSCB1    DS    0CL140
DS1DSNAM DS    CL44                    DATA SET NAME
DS1FMTID DS    C                       FORMAT IDENTIFIER (= C'1')
         DS    CL8
DS1CREDT DS    CL3                     CREATION DATE (YDD)
DS1EXPDT DS    CL3                     EXPIRATION DATE (YDD)
DS1NOEPV DS    C                       NO. OF SEPERATE EXTENTS
         DS    CL22
DS1DSORG DS    CL2                     DATA SET ORGANISATION
DS1RECFM DS    C                       DATA SET RECORD FORMAT
         DS    C
DS1BLKL  DS    CL2                     DATA SET BLOCK LENGTH
DS1LRECL DS    CL2                     DATA SET RECORD LENGTH
         DS    CL3
DS1DSIND DS    C                       DATA SET INDICATORS
DS1LVOL  EQU   X'80'                   LAST VOLUME
DS1RACF  EQU   X'C0'                   RACF PROTECTION
DS1PSWD  EQU   X'90'                   PASSWORD PROTECTION
DS1NOPWR EQU   X'94'                   NO PASSWORD READ PROTECTION
DS1OSPW  EQU   X'10'                   OS PASSWORD PROTECTION
DS1OSNPW EQU   X'10'                   OS NO PASSWORD READ PROTECTION
DS1SCALO DS    CL4                     ALLOCATION PARAMETERS
DS1LSTAR DS    CL3                     LAST BLOCK POINTER (TTR
DS1TRBAL DS    CL2                                         LL)
         DS    CL2
DS1EXT1  DS    3CL10                   DESCRIPTION OF EXTENT1 - EXTENT3
DS1PTRDS DS    CL5                     POINTER TO NEXT DSCB (CCHHR)
DS1CCHHR DS    CL5                     POINTER TO DSCB1 (CCHHR)
         EJECT
*
*      DATA SET CONTROL BLOCK FORMAT 2.
*
         SPACE 1
         ORG   OBTWKFLD
DSCB2    DS    0CL140
         DS    CL44
DS2FMTID DS    C                       FORMAT IDENTIFIER (=C'2')
         DS    CL90
DS2PTRDS DS    CL5                     POINTER TO DSCB3 (CCHHR)
         SPACE 2
*
*      DATA SET CONTROL BLOCK FORMAT 3.
*
         SPACE 1
         ORG   OBTWKFLD
DSCB3    DS    0CL140
         DS    CL4
DS3EXTNT DS    4CL10                   DESCRIPTION OF EXTENT4 - EXTENT7
DS3FMTID DS    C                       FORMAT IDENTIFIER (=C'3')
DS3ADEXT DS    9CL10                   DESCRIPTION OF EXTENT8 -EXTENT16
         SPACE 2
*
*      DATA SET CONTROL BLOCK FORMAT 4.
*
         SPACE 1
         ORG   OBTWKFLD
DSCB4    DS    0CL140
         DS    CL44
DS4FMTID DS    C                       FORMAT IDENTIFIER (=C'4')
DS4HPCR  DS    CL5                     HIGH DISK ADDR OF DSCB1
         DS    CL12
DS4DEVSZ DS    CL4                     DEVICE SIZE
         DS    CL8
DS4DEVT  DS    C                       NO. OF DSCB'S ON A TRACK
         ORG   ENDCAMWK
         EJECT
*
*      PARAMETER VOLSER TABLE.
*
*      BYTE1 - LENGTH OF PARAMETER VOLSER OR
*          X'80' IF END OF TABLE
*      BYTES2-7 - PARAMETER VOLSER NUMBER
*
         SPACE 1
TABPVOL1 DS    10CL7                   TABLE WITH MASK X'FF'
ENDTPVOL EQU   *
         DS    CL7
TABPVOL2 DS    CL77                    TABLE WITH MASK X'00'
         SPACE 2
*
*      EXTENT INFORMATION TABLE.
*
*      BYTE1: DATA SET EXTENT TYPE INDICATOR, X'FF' IF END OF TABLE
*      BYTE2: EXTENT SEQUENCE NUMBER
*      BYTE3-6: LOWER LIMIT OF THIS EXTENT (CCHH)
*      BYTE7-10: UPPER LIMIT OF THIS EXTENT (CCHH)
*
         SPACE 1
EXTTAB   DS    17CL10
         SPACE 2
*
*      MESSAGE TABLES.
*
*      BYTE1-66: WTO MESSAGE LINE
*      BYTE67:   X'00', IF MESSAGE AVAILABLE
*      BYTE68:   RESERVED
*
         SPACE 1
         DS    0F
MSGTAB   DS    10CL68                  TABLE OF GENERAL INFO. MESSAGES
ENDTAB1  EQU   *
EXTNTAB  DS    10CL68                  TABLE OF EXTENT INFO. MESSAGES
ENDTAB2  EQU   *
         DS    7CL68                   EXTENT OF EXTNTAB
DATETAB  DS    10CL68                  TABLE OF DATE INFO. MESSAGES
ENDTAB3  EQU   *
ERRORTAB DS    CL68                    ERROR MESSAGE TABLE
HEADER   DS    0CL66                   HEAD LINE
         DS    CL16
DSNAME   DS    CL44                    DATA SET NAME
         ORG   HEADER+66
WTOR     DS    CL(MSG13LEN)            WTOR LINE
         DS    0F
         SPACE 1
WORKLEN  EQU   *-WORKAREA              LENGTH OF WORK AREA
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE DESCRIPTIONS.                                          *
*                                                                     *
***********************************************************************
         SPACE 3
MESSAGE  DSECT
         SPACE 2
*
*      DESCRIPTIONS OF MSG01.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL8
MS0VOL1  EQU   *
         DS    CL11
MS0CVOL  DS    CL6                     CATALOG VOLUME SERIAL NUMBER
         DS    CL12
MS0VOL2  EQU   *
         SPACE 2
*
*      DESCRIPTIONS OF MSG03.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL8
MS3VOLID DS    CL6                     VOLUME SERIAL NUMBER
         DS    C
MS3ADR   DS    CL3                     UNIT ADDRESS
         DS    C
MS3ALLOC DS    CL13                    ALLOCATION INFORMATION
         DS    C
MS3TRKS  DS    CL4                     ALLOCATED TRACKS
         DS    C
MS3USED  DS    CL4                     USED TRACKS
         DS    C
MS3DCBIN DS    CL17                    DCB INFORMATION
MS3MARK  DS    CL2                     MARK OF PROTECTION
         EJECT
*
*      DESCRIPTIONS OF MSG04.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL8
MS4VOLID DS    CL6                     VOLUME SERIAL NUMBER
         DS    C
MS4NOEXT DS    CL2                     NUMBER OF SEPERATE EXTENTS
         DS    CL9
MS4EXT1  DS    CL2                     EXTENT NUMBER
         DS    CL12
MS4EXT2  DS    CL2                     EXTENT NUMBER
         DS    CL12
MS4EXT3  DS    CL2                     EXTENT NUMBER
         SPACE 2
*
*      DESCRIPTIONS OF MSG04.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL8
MS5VOLID DS    CL6                     VOLUME SERIAL NUMBER
         DS    C
MS5NOEXT DS    CL2                     NUMBER OF SEPERATE EXTENTS
         DS    C
MS5EXT   DS    3CL14                   EXTENT DESCRIPTION
         SPACE 2
*
*      DESCRIPTIONS OF MSG05.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL8
MS6VOLID DS    CL6                     VOLUME SERIAL NUMBER
         DS    C
MS6CREDT DS    CL6                     CREATION DATE
         DS    C
MS6EXPDT DS    CL6                     EXPIRATION DATE
         EJECT
*
*      DESCRIPTIONS OF MSG09.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL15
MS10VOLI DS    CL6                     VOLUME SERIAL NUMBER
         DS    CL26
MS10DEVC DS    CL2                     DEVICE CODE
         SPACE 2
*
*      DESCRIPTIONS OF MSG10.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL15
MS11VOLI DS    CL6                     VOLUME SERIAL NUMBER
         DS    CL21
MS11RC   DS    CL2                     RETURN CODE
         SPACE 2
*
*      DESCRIPTIONS OF MSG11.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL27
MS12RC   DS    CL2                     RETURN CODE
         SPACE 2
         ORG   MESSAGE+66
MSGFLAG  DS    C                       FLAG BYTE
         EJECT
***********************************************************************
*                                                                     *
*      UCB DESCRIPTIONS.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
UCB      DSECT
         DS    CL3
DEVSTAT  DS    C                       DEVICE STATUS
         DS    CL9
UCBNAME  DS    CL3                     UNIT NAME
UCBTYPE  DS    CL2                     DEVICE TYPE
UCBDVCLS DS    C                       DEVICE CLASS
DEVCODE  DS    C                       DEVICE CODE
         DS    CL4
UCBVTOC  DS    CL4                     REL. ADDR. OF VTOC FOR THIS VOL.
UCBVOLI  DS    CL6                     VOLUME SERIAL NUMBER
UCBSTAB  DS    C                       VOLUME STATUS
         SPACE 3
***********************************************************************
*                                                                     *
*      PARAMETER AREA.                                                *
*                                                                     *
***********************************************************************
         SPACE 3
         XPARM
PARMLEN  EQU   *-TEXT                  LENGTH OF PARAMETER AREA
         SPACE 3
***********************************************************************
*                                                                     *
*      UCB ADDRESS VECTOR.                                            *
*                                                                     *
***********************************************************************
         SPACE 3
VECTOR   DSECT
         DS    1024H
VECEND   DS    H                       =X'FFFF'
VECLEN   EQU   *-VECTOR
         CVT   DSECT=YES                                          /XUCB
         EJECT
         END   SHOW
./ ADD NAME=SPACE
         TITLE 'XSPACE                             X-COMMAND SPACE'
SPACE    CSECT
         SPACE 3
* MVS/XA UCBSCAN                                   22.6.83 BMW    /XUCB
***********************************************************************
*                                                                     *
*      PROGRAM-ID. SPACE.                                             *
*      AUTHOR. EHNERT.                                                *
*      INSTALLATION. BAYER AG, LEVERKUSEN, RECHENZENTRUM.             *
*      DATE-WRITTEN. 16. 7. 1975.                                     *
*      REMARKS.                                                       *
*                                                                     *
*          DAS X-COMMAND 'SPACE' GIBT INFORMATIONEN UEBER DEN         *
*          FREIEN SPEICHERPLATZ, DIE ANZAHL DER EXTENTS UND DEN       *
*          EXTENT DER ALS PARAMETER SPEZIFIZIERTEN VOLUMES AUS.       *
*                                                                     *
*          ALS PARAMETER KOENNEN BIS ZU ZEHN VOLSER-MASKEN ANGE-      *
*          GEBEN WERDEN, MIT DEREN HILFE EINE AUSWAHL DER VOLUMES     *
*          GETROFFEN WIRD. DABEI STEHT '*' FUER JEDES ZEICHEN AN      *
*          DIESER STELLE.                                             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      BEGINN DER VERARBEITUNG.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      SICHERN DER REGISTER,
*      SPEICHERPLATZANFORDERUNG FUER ARBEITSBEREICHE,
*      ZUORDNUNG DER BASISREGISTER.
*
         SPACE 1
         XSAVE R12,,SPACE,ABERL
         SPACE 1
         LR    R11,R1        BASISREG. FUER DSECT PARMAREA INITIALIS.
         USING UCB,R3        BASISREG. FUER DSECT UCB ZUORDNEN
         USING MESSAGE,R10   BASISREG. FUER DSECT MESSAGE ZUORDNEN
         USING PARMAREA,R11  BASISREG. FUER DSECT PARMAREA ZUORDNEN
         USING ABER,R13      BASISREG. FUER DSECT ABER ZUORDNEN
         EJECT
*
*      INITIALISIERUNG DER SPEICHER.
*
         SPACE 1
         MVI   FNDBYTE,X'00' SCHALTER LOESCHEN
         MVI   WTORBYTE,X'FF' SCHALTER SETZEN
         LA    R2,ENDTAB1    ENDADRESSEN DER
         LA    R3,ENDTPVOL       TABELLEN LADEN
         STM   R2,R3,ADDRETB1    UND SPEICHERN
         MVC   UCMID,REG4    UCM-ID. DES AUFRUFERS UEBERTRAGEN
         SPACE 2
*
*      FLAG BYTS IN NACHRICHTENBEREICHEN SETZEN.
*
         SPACE 1
         LA    R10,MSGTAB    ADRESSE DER TABELLE LADEN
         ST    R10,SAVE1R10      UND SPEICHERN
         L     R3,ADDRETB1   ENDADRESSE DER TABELLEN LADEN
CLEARMSS EQU   *
         MVC   MESSFLAG,=X'FFFF' FLAG BYTES SETZEN
         LA    R10,68(R10)   R10 ERHOEHEN
         CR    R10,R3        ENDE DER TABELLEN ERREICHT ?
         BL    CLEARMSS      NEIN, VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DER EINGABEPARAMETER.                             *
*                                                                     *
***********************************************************************
         SPACE 3
         LA    R4,TEXT+5     R4 INITIALISIEREN
         LA    R8,PARMENDE   ENDADRESSE DES PARAMETERBEREICHS LADEN
         CLI   0(R4),C','    PARAMETER ANGEGEBEN ?
         BNE   FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         LA    R6,TABPVOL1   ADRESSE DER VOLSER-TABELLE LADEN
NEXTPARM EQU   *
         SR    R5,R5         R5 LOESCHEN
         LR    R7,R4         ADRESSE DES TRENNUNGSZEICHENS LADEN
NXTBYTE  EQU   *
         LA    R4,1(R4)      R4 ERHOEHEN
         CR    R4,R8         PARAMETERBEREICH UEBERSCHRITTEN ?
         BNL   FEHLPARM      JA, VERZW. ZUR FEHLERROUTINE
         CLI   0(R4),C','    ENDE DES PARAMETERS ERREICHT ?
         BE    NEXTPVOL      JA, VERZWEIGEN
         CLI   0(R4),X'00'   ENDE DER PARAMETERKETTE ERREICHT ?
         BE    NEXTPVOL      JA, VERZWEIGEN
         CLI   0(R4),C' '    ENDE DER PARAMETERKETTE ERREICHT ?
         BE    NEXTPVOL      JA, VERZWEIGEN
         LA    R5,1(R5)      R5 ERHOEHEN
         CLI   0(R4),C'*'    BELIEBIGES ZEICHEN AN DIESER STELLE ?
         BNE   NXTBYTE       NEIN, VERZWEIGEN
         MVI   0(R4),X'FF'   ZEICHEN UMSETZEN
         B     NXTBYTE       VERZWEIGEN
         EJECT
NEXTPVOL EQU   *
         C     R6,ADDRETPV   TABELLENENDE UEBERSCHRITTEN ?
         BNL   FEHLPARM      JA, VERZWEIGEN
         C     R5,=F'6'      LAENGE DER VOLID GUELTIG ?
         BH    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         LTR   R5,R5         VOLID ANGEGEBEN ?
         BZ    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         BCTR  R5,0          R5 UM EINS VERMINDERN
         EX    R5,MOVEPVOL   VERZW. ZUR UEBERTR. DER VOLSER NUMBER
         STC   R5,0(R6)      LAENGENANGABE IN TABELLE EINTRAGEN
         LA    R6,7(R6)      R6 ERHOEHEN
         CLI   0(R4),C','    FOLGEN WEITERE PARAMETER ?
         BE    NEXTPARM      JA, VERZWEIGEN
         SPACE 2
*
*      ABSCHLUSS DER PARAMETERVERARBEITUNG.
*
         SPACE 1
         MVI   0(R6),X'80'   TABELLENENDE SPEZIFIZIEREN
         MVC   TABPVOL2,TABPVOL1 VOLSER-TABELLE KOPIEREN
         LA    R6,TABPVOL2-1 ADRESSE DER TABELLE LADEN
NEXTFF   EQU   *
         LA    R6,1(R6)      R6 ERHOEHEN
         CLI   0(R6),X'80'   TABELLENENDE ERREICHT ?
         BE    GETUCB        JA, VERZWEIGEN
         CLI   0(R6),X'FF'   TABELLENZEICHEN = X'FF' ?
         BNE   NEXTFF        NEIN, VERZWEIGEN
         MVI   0(R6),X'00'   TABELLENZEICHEN UMSETZEN
         B     NEXTFF        VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DER UCB-INFORMATIONEN.                            *
*                                                                     *
***********************************************************************
         SPACE 3
GETUCB   EQU   *
         DROP  R11           R11 FREIGEBEN
*        GETMAIN R,LV=VEKLEN,SP=125                               /XUCB
*        LR    R11,R1        BASISREG. FUER DSECT VEKTOR INIT.    /XUCB
*        USING VEKTOR,R11    BASISREG. FUER DSECT VEKTOR ZUORDNEN /XUCB
*        ST    R11,UCBADDR   VEKTOR-ADRESSE SPEICHERN             /XUCB
*        MVI   UCBADDR,X'80' OPTION BYTE SETZEN                   /XUCB
*        LA    R1,UCBADDR    ADRESSE DER VEKTOR-ADRESSE LADEN     /XUCB
*        LINK  EP=GETUCBS              LINK TO SUBROUTINE         /XUCB
*        LA    R2,VEKTOR     ADRESSE DES UCB-VEKTORS LADEN        /XUCB
*        SR    R3,R3         R3 LOESCHEN                          /XUCB
*        MVC   VEKEND,=X'FFFF' ENDE DES UCB-VEKTORS SPEZIFIZIEREN /XUCB
*        S     R2,=F'2'      R2 UM ZWEI VERMINDERN                /XUCB
         XUCBSCAN R2,X'20'   SCAN NUR FUER DASD                   /XUCB
NEXTUCB  EQU   *
*        LA    R2,2(R2)      R2 ERHOEHEN                          /XUCB
*        CLC   0(2,R2),=X'FFFF' ENDE DES UCB-VEKTORS ERREICHT ?   /XUCB
*        BE    ABSCHLUS      JA, VERZW. ZUM ABSCHLUSS DER VERARBEI/XUCB
*        ICM   R3,3,0(R2)    ADRESSE DES UCB LADEN                /XUCB
*        CLI   UCBDVCLS,X'20' DEVICE = DASD ?                     /XUCB
*        BNE   NEXTUCB       NEIN, VERZWEIGEN                     /XUCB
         XUCBGET R2,ABSCHLUS,ABSCHLUS                             /XUCB
         LR    R3,R1         ADRESSE DES UCB LADEN                /XUCB
         TM    DEVSTAT,X'80' EINHEIT ONLINE ?
         BZ    NEXTUCB       NEIN, VERZWEIGEN
         TM    DEVSTAT,X'10' EINHEIT IM UNLOAD ?
         BO    NEXTUCB       JA, VERZWEIGEN
         CLC   UCBVOLI,=XL6'0' VOLID VORHANDEN ?
         BE    NEXTUCB       NEIN, VERZWEIGEN
         TM    UCB+17,X'08'                VIRTUAL DEVICE ?
         BO    TSTVVOL                     BRANCH IF YES
         EJECT
*
*      VERGLEICH MIT SPEZIFIZIERTEN VOLSER NUMBERS.
*
         SPACE 1
         SR    R5,R5         R5 LOESCHEN
         LA    R6,TABPVOL1-7 ADRESSE DER VOLSER-TABELLE LADEN
         LA    R7,TABPVOL2-7 ADRESSE DER VOLSER-TABELLE LADEN
NXTVOLID EQU   *
         LA    R6,7(R6)      R6 ERHOEHEN
         LA    R7,7(R7)      R7 ERHOEHEN
         TM    0(R6),X'80'   TABELLENENDE ERREICHT ?
         BO    NEXTUCB       JA, VERZWEIGEN
         IC    R5,0(R6)      LAENGENANGABE LADEN
         MVC   ZWVOLID1,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         MVC   ZWVOLID2,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         EX    R5,NCVOLID1   VERZW. ZUR UEBERLAGERUNG DER VOLID
         EX    R5,OCVOLID2   VERZW. ZUR UEBERLAGERUNG DER VOLID
         CLC   ZWVOLID1,ZWVOLID2 VOLID = AUSGEW. VOLID ?
         BNE   NXTVOLID      VERZW., WENN VOLID ^= AUSGEW. VOLID
         B     MVCVOLID
         SPACE 1
TSTVVOL  EQU   *
         LA    R6,TABPVOL1-7 ADRESSE DER VOLSER-TABELLE LADEN
NXTVVOL  EQU   *
         LA    R6,7(R6)      R6 ERHOEHEN
         TM    0(R6),X'80'   TABELLENENDE ERREICHT ?
         BO    NEXTUCB       JA, VERZWEIGEN
         CLC   UCBVOLI,1(R6) VOLID = AUSGEW. VOLID ?
         BNE   NXTVVOL       VERZW., WENN VOLID ^= AUSGEW. VOLID
MVCVOLID EQU   *
         MVC   VOLSERNO,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         MVI   FNDBYTE,X'FF' FLAG BYTE SETZEN
         EJECT
***********************************************************************
*                                                                     *
*      DSCB5 LESEN UND AUSWERTEN (SVC 78).                            *
*                                                                     *
***********************************************************************
         SPACE 3
         L     R10,SAVE1R10  R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG02),MSG02 NACHRICHTENZEILE UEBERTRAGEN
         MVC   MS2VOLID,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         MVC   MS2ADR,UCBNAME UNIT NAME UEBERTRAGEN
         LA    R1,SVCWKFLD   ADRESSE DES SVC WORK FIELD LADEN
         LR    R0,R3         UCB-ADRESSE LADEN
         SVC   78            VERZW. ZUR VERARBEITUNG DES DSCB5
         MVC   MS2FRCYL,SVCFRCYL ANZ. FREIER CYLINDER UEBERTRAGEN
         MVC   MS2FRTRK,SVCFRTRK ANZ. FREIER TRACKS UEBERTRAGEN
         MVC   MS2NOEXT,SVCNOEXT ANZ. DER EXTENTS UEBERTRAGEN
         MVC   MS2MXCYL,SVCMXCYL ANZ. CYL. DES GROESSTEN EXT. UEBERTR.
         MVC   MS2MXTRK,SVCMXTRK ANZ. TRK. DES GROESSTEN EXT. UEBERTR.
         LA    R14,NEXTUCB   RUECKSPRUNGADRESSE LADEN
         MVC   MESSFLAG,=XL6'0' FLAG BYTES LOESCHEN
         LA    R10,68(R10)   R10 ERHOEHEN
         C     R10,ADDRETB1  ENDE DER TABELLE UEBERSCHRITTEN ?
         BNL   AUSMSGM       JA, VERZWEIGEN
         ST    R10,SAVE1R10  LAUFENDE ADRESSE SPEICHERN
         BR    R14           VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      ABSCHLUSSROUTINEN.                                             *
*                                                                     *
***********************************************************************
         SPACE 3
ABSCHLUS EQU   *
*        FREEMAIN R,SP=125    SPEICHERPLATZ FREIGEBEN             /XUCB
         XUCBOFF R2                                               /XUCB
         MVI   WTORBYTE,X'00' FLAG BYTE LOESCHEN
         CLI   FNDBYTE,X'FF' VOLUME(S) GEFUNDEN ?
         BE    AUSRMSGN      JA, VERZWEIGEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG03)  NACHRICHT AUSGEBEN
         B     ENDE          VERZWEIGEN
         SPACE 2
*
*      AUSGABE NOCH OFFENER INFO-NACHRICHTEN.
*
         SPACE 1
AUSRMSGN EQU   *
         LA    R10,MSGTAB    ADRESSE DER TABELLE LADEN
         CLC   MESSFLAG,=XL6'0' SIND NOCH NACHRICHTEN OFFEN ?
         BNE   ENDE          NEIN, VERZWEIGEN
         BAL   R14,AUSMSGM   JA, VERZW. ZUR NACHRICHTENAUSGABE
         EJECT
*
*      SPEICHERPLATZFREIGABE,
*      REINITIALISIERUNG DER REGISTER UND
*      RUECKSPRUNG ZUM AUFRUFENDEN PROGRAMM.
*
         SPACE 1
ENDE     EQU   *
         XRETURN ,R
         EJECT
***********************************************************************
*                                                                     *
*      FEHLERROUTINEN.                                                *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      ROUTINE BEI FEHLER IN DER PARAMETERKETTE.
*
         SPACE 1
FEHLPARM EQU   *
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG04)  NACHRICHT AUSGEBEN
         B     ENDE          VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      AUSGABE DER NACHRICHTEN.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
AUSMSGM  EQU   *
         ST    R14,SAVE1R14  RUECKSPRUNGADRESSE SICHERN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG01)  UEBERSCHRIFTENZEILE AUSGEBEN
         LA    R10,MSGTAB-68 ADRESSE DER TABELLE LADEN
         L     R9,ADDRETB1   ENDADRESSE DER TABELLE LADEN
         BAL   R14,AUSGABE   VERZW. ZUR AUSGABE WEITERER ZEILEN
         LA    R10,MSGTAB    ADRESSE DER TABELLE LADEN
         ST    R10,SAVE1R10      UND SPEICHERN
         L     R14,SAVE1R14  RUECKSPRUNGADRESSE LADEN
         CLI   WTORBYTE,X'FF' FLAG BYTE GESETZT ?
         BNER  R14           NEIN, VERZWEIGEN
NXTREPLY EQU   *
         XC    ECB,ECB       EVENT CONTROL BLOCK LOESCHEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         LA    R4,ECB        ADRESSE DES ECB LADEN
         LA    R5,REPLY      ADRESSE DES REPLY LADEN
         MVC   WTOR(LENMSG05),MSG05 NACHRICHTENZEILE UEBERTRAGEN
         WTOR  ,(R5),1,(R4),MF=(E,WTOR)
         SPACE 1
         WAIT  ECB=ECB       AUF REPLY WARTEN
         CLI   REPLY,C'N'    REPLY = NO ?
         BE    ENDE          JA, VERZWEIGEN
         CLI   REPLY,C'Y'    REPLY = YES ?
         BNE   NXTREPLY      NEIN, VERZWEIGEN
         L     R14,SAVE1R14  RUECKSPRUNGADRESSE LADEN
         BR    R14           VERZWEIGEN
         EJECT
*
*      AUSGABE EINZELNER NACHRICHTENZEILEN.
*
         SPACE 1
AUSGABE  EQU   *
         ST    R14,SAVE2R14  RUECKSPRUNGADRESSE SICHERN
NEXTMESS EQU   *
         LA    R10,68(R10)   R10 ERHOEHEN
         CR    R10,R9        ENDE DER TABELLE ERREICHT ?
         BNL   ENDMESS       JA, VERZWEIGEN
         CLC   MESSFLAG,=X'FFFF' FLAG BYTES GESETZT ?
         BE    ENDMESS       JA, VERZWEIGEN
         MVC   MESSFLAG,=X'FFFF' FLAG BYTES SETZEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MESSAGE) EINE NACHRICHTENZEILE AUSGEBEN
         B     NEXTMESS      VERZWEIGEN
ENDMESS  EQU   *
         L     R14,SAVE2R14  RUECKSPRUNGADRESSE LADEN
         BR    R14           VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      BEFEHLE, DIE MIT HILFE DES EXECUTE-BEFEHLS AUSGEFUEHRT WERDEN. *
*                                                                     *
***********************************************************************
         SPACE 3
MOVEPVOL EQU   *
         MVC   1(0,R6),1(R7) VOLID IN TABELLE UEBERTRAGEN
NCVOLID1 EQU   *
         NC    ZWVOLID1(0),1(R6) VOLID UEBERLAGERN
OCVOLID2 EQU   *
         OC    ZWVOLID2(0),1(R7) VOLID UEBERLAGERN
         EJECT
***********************************************************************
*                                                                     *
*      REGISTER EQUATES.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
R0       EQU   0             MACRO- UND-PARAMETER REGISTER
R1       EQU   1             MACRO- UND-PARAMETER REGISTER
*2       EQU   2             POINTER IN UCB-ADR. VEKTOR        XUCB BMW
R2       EQU   2             ADRESSE UCBSCAN-PARMLIST          XUCB BMW
R3       EQU   3             POINTER FUER UCB
R4       EQU   4             ARBEITSREGISTER
R5       EQU   5             ARBEITSREGISTER
R6       EQU   6             ARBEITSREGISTER
R7       EQU   7             ARBEITSREGISTER
R8       EQU   8             ARBEITSREGISTER
R9       EQU   9             ARBEITSREGISTER
R10      EQU   10            POINTER IN NACHRICHTEN-TABELLEN
R11      EQU   11            BASISREG. FUER DSECT PARMAREA / VEKTOR
R12      EQU   12            BASISREG. FUER CSECT SPACE
R13      EQU   13            BASISREG. FUER DSECT ABER
R14      EQU   14            RUECKSPRUNGADRESSEN
R15      EQU   15            CSECT-ADRESSEN UND RETURN CODES
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER AUSGABENACHRICHTEN.                             *
*                                                                     *
***********************************************************************
         SPACE 3
MSG01    WTO   'XSPACE1 VOLSER ADR FREE-SPACE EXT  LARGEST-EXTENT',    *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG02    WTO   'XSPACE2 VOLSER ADR 9999/9999  9999   9999/9999',       *
               MF=L,MCSFLAG=(REG0)
LENMSG02 EQU   *-MSG02
         SPACE 1
MSG03    WTO   'XSPACE3 SPECIFIED VOLUME(S) NOT FOUND',                *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG04    WTO   'XSPACE4 INVALID PARAMETER LIST',                       *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG05    WTOR  'XSPACE5 REPLY ''Y'' (YES) OR ''N'' (NO) TO CONTINUE',  *
               MF=L,MCSFLAG=(REG0)
LENMSG05 EQU   *-MSG05
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER ARBEITSBEREICHE.                                *
*                                                                     *
***********************************************************************
         SPACE 3
ABER     DSECT
SAVEAREA DS    18F           SAVEAREA FUER ALLGEMEINE REGISTER
         SPACE 2
*
*      ADRESSEN-SPEICHER.
*
         SPACE 1
ADDRETB1 DS    F             ENDADRESSE DER MSG-NACHRICHTEN-TABELLE
ADDRETPV DS    F             ENDADRESSE DER VOLSER-TABELLE
SAVE1R10 DS    F             SAVEAREA1 FUER R10
SAVE1R14 DS    F             SAVEAREA1 FUER R14
SAVE2R14 DS    F             SAVEAREA2 FUER R14
UCBADDR  DS    F             ADRESSE DES UCB-VEKTORS
UCMID    DS    F             UCM-ID. DES AUFRUFERS
         SPACE 2
*
*      ARBEITSSPEICHER.
*
         SPACE 1
FNDBYTE  DS    C             SCHALTER FUER GEFUNDENE VOLUMES
WTORBYTE DS    C             SCHALTER FUER WTOR
REPLY    DS    C             SPEICHER FUER REPLY
VOLSERNO DS    CL6           ZWISCHENSPEICHER FUER VOLSER NUMBER
ZWVOLID1 DS    CL6           ZWISCHENSPEICHER FUER VOLSER NUMBER
ZWVOLID2 DS    CL6           ZWISCHENSPEICHER FUER VOLSER NUMBER
ECB      DS    F             EVENT CONTROL BLOCK
WTOR     DS    15F           BEREICH FUER WTOR-NACHRICHT
SVCWKFLD DS    0F            SVC WORK FIELD
         DS    CL6
SVCFRCYL DS    CL4           ANZ. FREIER CYLINDER
         DS    CL1
SVCFRTRK DS    CL4           ANZ. FREIER TRACKS
         DS    CL1
SVCNOEXT DS    CL4           ANZ. DER EXTENTS
         DS    CL1
SVCMXCYL DS    CL4           ANZ. CYLINDER DES GROESSTEN EXTENTS
         DS    CL1
SVCMXTRK DS    CL4           ANZ. TRACKS DES GROESSTEN EXTENTS
         ORG   SVCWKFLD+80
         EJECT
*
*      MASKEN-TABELLEN FUER VOLSER NUMBERS AUS DEM PARAMETERBEREICH.
*
*      AUFBAU DER TABELLE:
*      BYTE1 - LAENGE DES ANGEGEBENEN VOLSER-PARAMETERS ODER
*          X'80' BEI TABELLENENDE
*      BYTES2-7 - ANFANGSZEICHEN DER VOLSER NUMBER UND MASKENZEICHEN
*
         SPACE 1
TABPVOL1 DS    10CL7         TABELLE MIT MASKENZEICHEN X'FF'
ENDTPVOL EQU   *
         DS    CL7
TABPVOL2 DS    CL77          TABELLE MIT MASKENZEICHEN X'00'
         SPACE 2
*
*      NACHRICHTEN-BEREICHE.
*
*      AUFBAU:
*      BYTE1-66: WTO-NACHRICHTENZEILE
*      BYTE67-68: X'0000', WENN NACHRICHTENZEILE VERFUEGBAR
*
         SPACE 1
         DS    0F
MSGTAB   DS    10CL68        TABELLE FUER NACHRICHTEN
ENDTAB1  EQU   *
         SPACE 1
ABERL    EQU   *-ABER        LAENGE DER ARBEITSBEREICHE
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITIONEN FUER EINE NACHRICHTENZEILE.                       *
*                                                                     *
***********************************************************************
         SPACE 3
MESSAGE  DSECT
         SPACE 2
*
*      DEFINITIONEN FUER MSG02.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL8
MS2VOLID DS    CL6           VOLUME SERIAL NUMBER
         DS    C
MS2ADR   DS    CL3           UNIT ADDRESS
         DS    CL1
MS2FRCYL DS    CL4           FREE CYLINDERS
         DS    CL1
MS2FRTRK DS    CL4           FREE TRACKS
         DS    CL2
MS2NOEXT DS    CL4           NO. OF EXTENTS
         DS    CL3
MS2MXCYL DS    CL4           CYLINDERS OF LARGES EXTENTS
         DS    CL1
MS2MXTRK DS    CL4           TRACKS OF LARGES EXTENT
         ORG   MESSAGE+66
MESSFLAG DS    CL2           FLAG BYTES
         EJECT
***********************************************************************
*                                                                     *
*      UCB-DEFINITIONEN.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
UCB      DSECT
         DS    CL3
DEVSTAT  DS    C             DEVICE STATUS
         DS    CL9
UCBNAME  DS    CL3           UNIT NAME
         DS    CL2
UCBDVCLS DS    C             DEVICE CLASS
DEVCODE  DS    C             DEVICE CODE
         DS    CL4
UCBVTOC  DS    CL4           REL. ADDR. OF SPACE FOR THIS VOL. (TTR0)
UCBVOLI  DS    CL6           VOLUME SERIAL NUMBER
         SPACE 3
***********************************************************************
*                                                                     *
*      PARAMETERBEREICH.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
         XPARM
PARMENDE EQU   *
         SPACE 3
***********************************************************************
*                                                                     *
*      VEKTOR MIT UCB-ADRESSEN.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
VEKTOR   DSECT
         DS    1024H
VEKEND   DS    H             =X'FFFF'
VEKLEN   EQU   *-VEKTOR
         PRINT NOGEN                                              /XUCB
         CVT   DSECT=YES     CVT MAPPING                          /XUCB
         EJECT
         END   SPACE
./ ADD NAME=UNIT
         TITLE 'XDVOL                             X-COMMAND DVOL'
DVOL     CSECT
         SPACE 3
* MVS/XA UCBSCAN                              29.6.83 BMW         /XUCB
***********************************************************************
*                                                                     *
*      PROGRAM-ID. DVOL.                                              *
*      AUTHOR. EHNERT.                                                *
*      INSTALLATION. BAYER AG, LEVERKUSEN, RECHENZENTRUM.             *
*      DATE-WRITTEN. 7. 10. 1975.                                     *
*      REMARKS.                                                       *
*                                                                     *
*          DAS X-COMMAND 'DVOL' GIBT INFORMATIONEN UEBER DEN          *
*          STATUS DER ALS PARAMETER SPEZIFIZIERTEN VOLUMES AUS.       *
*                                                                     *
*          ALS PARAMETER KOENNEN BIS ZU ZEHN VOLSER- ODER ADRESS-     *
*          MASKEN ANGEGEBEN WERDEN, MIT DEREN HILFE EINE AUSWAHL      *
*          GETROFFEN WIRD. DABEI STEHT '*' FUER JEDES ZEICHEN AN      *
*          DIESER STELLE.                                             *
*          ALS WEITERE AUSWAHL-PARAMETER KOENNEN DEVICE-TYPEN (DASD,  *
*          MSS, TAPE, UR, TP, GRAPHIC) UND DEVICE-STATUS (ONLINE,     *
*          OFFLINE, READY) ANGEGEBEN WERDEN.                          *
*                                                                     *
*          WIRD DAS PROGRAMM UNTER DEM NAMEN 'DVOL' AUFGERUFEN,       *
*          SO GELTEN DIE SPEZIFIZIERTEN PARAMETER ALS VOLSER-         *
*          MASKEN, WIRD ES UNTER DEM NAMEN 'UNIT' AUFGERUFEN,         *
*          SO GELTEN DIE SPEZIFIZIERTEN PARAMETER ALS ADRESS-         *
*          MASKEN.                                                    *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      INITIAL PROCESSING.                                            *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      SAVE REGISTER CONTENTS,
*      GETMAIN FOR WORK AREAS,
*      ALLOCATION OF BASE REGISTERS.
*
         SPACE 1
         XSAVE R12,,DVOL,WKAREALN
         SPACE 1
         LR    R11,R01                 INITIALIZE BASE REG. OF PARMAREA
         USING UCB,R03                 ALLOC. BASE REG. OF UCB
         USING UCBCMEXT,R09            ALLOC. BASE REG. OF UCB EXTENT
         USING MESSAGE,R10             ALLOC. BASE REG. OF MESSAGE
         USING PARMAREA,R11            ALLOC. BASE REG. OF PARMAREA
         USING WKAREA,R13              ALLOC. BASE REG. OF WKAREA
         EJECT
*
*      INITIALIZE WORK AREAS.
*
         SPACE 1
         MVI   VOLBYTE,X'00'           CLEAR FLAG BYTE
         MVI   VOLBYTE,X'00'           CLEAR FLAG BYTE
         MVI   WTORBYTE,X'FF'          SET FLAG
         LA    R02,ENDTAB1             LOAD ADDRESSES OF
         LA    R03,ENDTPVOL                TABLE ENDS AND
         STM   R02,R03,ADDRETB1            STORE THEM
         MVC   UCMID,REG4              MOVE UCM ID. OF CALLER
         MVC   PNAME,TEXT              MOVE NAME OF PROGRAM
         MVC   HEADER(LENMSG01),MSG01  MOVE HEAD LINE
         LA    R10,HEADER              INITIALIZE BASE REG. OF MESSAGE
         MVC   MSGNAME,PNAME           MOVE NAME OF PROGRAM
         MVC   WTOR(LENMSG05),MSG05    MOVE WTOR MESSAGE
         LA    R10,WTOR                INITIALIZE BASE REG. OF MESSAGE
         MVC   WTORNAME,PNAME          MOVE PROGRAM NAME
         MVI   CPUBYTE,X'00'           CLEAR FLAG BYTE
         MVI   PRMFLAGS,X'00'
         MVC   PRMFLAGS+1(PFLAGLEN-1),PRMFLAGS
         MVI   WTOBYTE,X'00'           CLEAR FLAG BYTE
         LA    R02,PJOBNAME            LOAD ADDRESS OF JOB NAME
         LA    R03,PSTEPNME            LOAD ADDRESS OF STEP NAME
         STM   R02,R03,PJNMEADR        STORE ADDRESSES
         OI    PSNMEADR,X'80'          INDICATE LAST ENTRY
         SPACE 2
*
*      SET FLAG BYTES IN MESSAGE AREA.
*
         SPACE 1
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         ST    R10,SAVE1R10                AND STORE IT
         L     R03,ADDRETB1            LOAD ADDRESS OF TABLE END
CLEARMSS EQU   *
         MVC   MSGFLAG,=X'FFFF'        SET FLAGS
         LA    R10,68(R10)             LOAD ADDRESS OF NEXT ENTRY
         CR    R10,R03                 TABLE END ?
         BL    CLEARMSS                BRANCH IF NOT
         SPACE 1
         L     R02,16                  LOAD ADRESSE OF CVT
         TM    116(R02),X'10'          SYSTEM = MVS ?
         BZ    GETPARM                 BRANCH IF NOT
         L     R02,764(R02)            LOAD ADRESSE OF PCCAVT
         LTR   R02,R02                 PCCAVT AVAILABLE ?
         BZ    GETPARM                 BRANCH IF NOT
         CLC   0(4,R02),=XL6'0'        CPU 0 AVAILABLE ?
         BE    *+8                     BRANCH IF NOT
         OI    CPUBYTE,X'F0'           SET FLAGS
         CLC   4(4,R02),=XL6'0'        CPU 1 AVAILABLE ?
         BE    GETPARM                 BRANCH IF NOT
         OI    CPUBYTE,X'0F'           SET FLAGS
         EJECT
***********************************************************************
*                                                                     *
*      INPUT PARAMETER PROCESSING.                                    *
*                                                                     *
***********************************************************************
         SPACE 3
GETPARM  EQU   *
         CLC   TEXT(4),=C'DVOL'        VOLUMES SPECIFIED ?
         BNE   *+8                     BRANCH IF NOT
         MVI   VOLBYTE,X'FF'           SET FLAG
         LA    R04,TEXT+4              LOAD ADDRESS OF PARAMETER CHAIN
         LA    R08,PARMEND             LOAD ADDRESS OF PARAMETER END
         LA    R06,TABPVOL1            LOAD ADDRESS OF VOLUME TABLE
         CLI   0(R04),C','             PARAMETERS SPECIFIED ?
         BNE   NOPARM                  BRANCH IF NOT
NEXTPARM EQU   *
         SR    R05,R05                 ZERO LENGTH COUNT
         LR    R07,R04                 LOAD ADDRESS OF SEPERATOR
NXTBYTE  EQU   *
         LA    R04,1(R04)              LOAD ADDRESS OF NEXT CHARACTER
         CR    R04,R08                 END OF PARAMETER CHAIN ?
         BNL   PRMERROR                IF YES, BRANCH TO ERROR ROUTINE
         CLI   0(R04),C','             END OF PARAMETER ?
         BE    NEXTPVOL                BRANCH IF YES
         CLI   0(R04),X'00'            END OF PARAMETER CHAIN ?
         BE    NEXTPVOL                BRANCH IF YES
         CLI   0(R04),C' '             END OF PARAMETER CHAIN ?
         BE    NEXTPVOL                BRANCH IF YES
         LA    R05,1(R05)              INCREASE LENGTH COUNT
         CLI   0(R04),C'*'             DON'T CARE CHARACTER ?
         BNE   NXTBYTE                 BRANCH IF NOT
         MVI   0(R04),X'FF'            TRANSLATE CHARACTER
         B     NXTBYTE                 BRANCH
         EJECT
NEXTPVOL EQU   *
         BAL   R14,SETFLAGS            BRANCH TO SET FLAGS
         C     R06,ADDRETPV            END OF TABLE ?
         BNL   PRMERROR                IF YES, BRANCH TO ERROR ROUTINE
         C     R05,=F'6'               VALID LENGTH ?
         BH    PRMERROR                IF NOT, BRANCH TO ERROR ROUTINE
         LTR   R05,R05                 VALID LENGTH ?
         BZ    PRMERROR                IF NOT, BRANCH TO ERROR ROUTINE
         BCTR  R05,0                   MODIFY FOR EX COMMAND
         EX    R05,MOVEPVOL            BRANCH TO MOVE VOLSER NUMBER
         STC   R05,0(R06)              STORE LENGTH
         LA    R06,7(R06)              LOAD ADDRESS OF NEXT ENTRY
         CLI   0(R04),C','             MORE PARAMETERS IN CHAIN ?
         BE    NEXTPARM                BRANCH IF YES
         SPACE 2
*
*      END OF PARAMETER PROCESSING.
*
         SPACE 1
ENDPARM  EQU   *
         MVI   0(R06),X'80'            INDICATE END OF TABLE
         MVC   TABPVOL2,TABPVOL1       COPY TABLE
         LA    R06,TABPVOL2-1          LOAD ADDRESS OF TABLE
NEXTFF   EQU   *
         LA    R06,1(R06)              LOAD ADDRESS OF NEXT CHARACTER
         CLI   0(R06),X'80'            END OF TABLE ?
         BE    GETFASID                BRANCH IF YES
         CLI   0(R06),X'FF'            CHARACTER = X'FF' ?
         BNE   NEXTFF                  BRANCH IF NOT
         MVI   0(R06),X'00'            TRANSLATE CHARACTER
         B     NEXTFF                  BRANCH
         SPACE 1
NOPARM   EQU   *
         MVI   NOPMFLAG,X'FF'          SET FLAG
         B     ENDPARM                 BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      SET PARAMETER FLAGS.                                           *
*                                                                     *
***********************************************************************
         SPACE 3
SETFLAGS EQU   *
         LA    R01,PRMTABLE-10         LOAD ADDRESS OF TABLE
         LA    R02,PRMFLAGS            LOAD ADDRESS OF FLAG BYTES
         SR    R03,R03                 ZERO FOR IC COMMAND
SETFLGLP EQU   *
         LA    R01,10(R01)             LOAD ADDRESS OF NEXT ENTRY
         LA    R02,1(R02)              LOAD ADRESS OF NEXT FLAG BYTE
         CLI   0(R01),X'FF'            END OF TABLE ?
         BER   R14                     RETURN IF YES
         IC    R03,0(R01)              INSERT LENGTH
         EX    R03,CLCPARM             BRANCH TO COMPARE PARAMETERS
         BNE   SETFLGLP                BRANCH IF NOT EQUAL
         MVI   PARMFLAG,X'FF'          SET FLAGS
         MVI   0(R02),X'FF'            SET FLAGS
         CLI   0(R04),C','             MORE PARAMETERS IN CHAIN ?
         BE    NEXTPARM                BRANCH IF YES
         B     ENDPARM                 BRANCH
         SPACE 2
GETFASID EQU   *
         LOAD  EP=FASID                LOAD ENTRY POINT ADDRESS
         ST    R00,FASIDADR                AND STORE IT
         EJECT
***********************************************************************
*                                                                     *
*      PROCESSING OF UCB INFORMATION.                                 *
*                                                                     *
***********************************************************************
         SPACE 3
GETUCB   EQU   *
         DROP  R11                     FREE BASE REG. OF PARMAREA
*        GETMAIN R,LV=UCBTBLEN,SP=125                             /XUCB
*        LR    R11,R01                 INITIALIZE BASE REG. OF UCB/XUCB
*        USING UCBADTAB,R11            ALLOC. BASE REG. OF UCBADTA/XUCB
*        ST    R11,UCBADDR             STORE ADDRESS OF TABLE     /XUCB
*        MVI   UCBADDR,X'80'           SET OPTION BYTE            /XUCB
*        LA    R01,UCBADDR             LOAD ADDRESS OF PARAMETER  /XUCB
*        LINK  EP=GETUCBS              LINK TO SUBROUTINE         /XUCB
*        LA    R02,UCBADTAB            LOAD ADDRESS OF TABLE      /XUCB
*        SR    R03,R03                 ZERO FOR ICM COMMAND       /XUCB
*        MVC   UCBTBEND,=X'FFFF'       SPECIFY END OF TABLE       /XUCB
*        S     R02,=F'2'               DECREASE ADDRESS           /XUCB
         XUCBSCAN R02                  CREATE WORK AREA           /XUCB
NEXTUCB  EQU   *
*        LA    R02,2(R02)              LOAD ADDRESS OF NEXT ENTRY /XUCB
*        CLC   0(2,R02),=X'FFFF'       END OF TABLE ?             /XUCB
*        BE    ENDPROC                 BRANCH IF YES              /XUCB
*        ICM   R03,3,0(R02)            LOAD ADDRESS OF UCB        /XUCB
         XUCBGET R02,ENDPROC,ENDPROC                              /XUCB
         LR    R03,R01                 LOAD ADDRESS OF UCB        /XUCB
         SPACE 1
         MVI   FNDBYTE,X'FF'           SET FLAGS
         TM    PARMFLAG,X'FF'          SELECT PARAMETERS ?
         BZ    *+8                     BRANCH IF NOT
         BAL   R14,TSTFLAGS            BRANCH TO TEST FLAGS
         TM    FNDBYTE,X'FF'           TEST OK ?
         BZ    NEXTUCB                 BRANCH IF NOT
         EJECT
*
*      COMPARISON OF UCB ADDRESSES OR VOLSER NUMBERS.
*
         SPACE 1
         SR    R05,R05                 ZERO FOR IC COMMAND
         LA    R06,TABPVOL1-7          LOAD ADDRESS OF TABLE
         LA    R07,TABPVOL2-7          LOAD ADDRESS OF TABLE
NXTVOLID EQU   *
         LA    R06,7(R06)              LOAD ADDRESS OF NEXT ENTRY
         LA    R07,7(R07)              LOAD ADDRESS OF NEXT ENTRY
         TM    0(R06),X'80'            END OF TABLE ?
         BO    TESTADDR                BRANCH IF YES
         IC    R05,0(R06)              INSERT LENGTH
         CLI   VOLBYTE,X'FF'           VOLSER NUMBERS SPECIFIED ?
         BNE   NEXTADR                 BRANCH IF NOT
         TM    UCBDVCLS,X'A0'          TAPE OR DIRECT ACCESS ?
         BZ    NEXTUCB                 BRANCH IF NOT
         CLC   UCBVOLI,=XL6'0'         VOLSER NUMBER ?
         BE    NEXTUCB                 BRANCH IF NOT
         MVC   ZWVOLID1,UCBVOLI        MOVE VOLSER NUMBER
         MVC   ZWVOLID2,UCBVOLI        MOVE VOLSER NUMBER
EXMVC    EQU   *
         EX    R05,NCVOLID1            BRANCH TO TRANSLATE VOLID
         EX    R05,OCVOLID2            BRANCH TO TRANSLATE VOLID
         CLC   ZWVOLID1,ZWVOLID2       VOLID VALID ?
         BNE   NXTVOLID                BRANCH IF NOT
         MVC   VOLSERNO,UCBVOLI        MOVE VOLSER NUMBER
         B     MVCMSG                  BRANCH
         SPACE 1
NEXTADR  EQU   *
         MVC   ZWVOLID1,UCBNAME        MOVE UNIT NAME
         MVC   ZWVOLID2,UCBNAME        MOVE UNIT NAME
         B     EXMVC                   BRANCH
         SPACE 1
TESTADDR EQU   *
         TM    TABPVOL1,X'80'          ADDRESSES OR VOLUMES SPEC. ?
         BZ    NEXTUCB                 BRANCH IF YES
         TM    VOLBYTE,X'FF'           VOLSER NUMBERS SPECIFIED ?
         BZ    MVCMSG                  BRANCH IF NOT
         TM    UCBDVCLS,X'A0'          TAPE OR DIRECT ACCESS ?
         BZ    NEXTUCB                 BRANCH IF NOT
         EJECT
***********************************************************************
*                                                                     *
*      EDIT MESSAGE LINES.                                            *
*                                                                     *
***********************************************************************
         SPACE 3
MVCMSG   EQU   *
         L     R10,SAVE1R10            INITIALIZE BASE REG. OF MESSAGE
         MVC   MESSAGE(LENMSG02),MSG02 MOVE MESSAGE SKELETON
         MVC   MSGNAME,PNAME           MOVE NAME OF PROGRAM
         MVC   MSGADR,UCBNAME          MOVE NAME OF UNIT
*        STCM  R03,3,MSGUCBAD          STORE ADDRESS OF UNIT      /XUCB
         STCM  R03,7,MSGUCBAD          STORE ADDRESS OF UNIT      /XUCB
*        UNPK  MSGUCBAD(5),MSGUCBAD(3) UNPACK ADDRESS             /XUCB
         UNPK  MSGUCBAD(7),MSGUCBAD(4) UNPACK ADDRESS             /XUCB
*        MVI   MSGUCBAD+4,C' '                                    /XUCB
         MVI   MSGUCBAD+6,C' '                                    /XUCB
         TR    MSGUCBAD,TABHEXA-240    TRANSLATE ADDRESS
         SPACE 1
         TM    UCBSTAT,UCBONLI         DEVICE ONLINE ?
         BO    TESTSYS                 BRANCH IF YES
         MVC   MSGLINE,=C'OFF'         SPECIFY OFFLINE
         B     TESTALOC                BRANCH
TESTSYS  EQU   *
         TM    CPUBYTE,X'FF'           SYSTEM = MVS ?
         BZ    TESTALOC                BRANCH IF NOT
         TM    UCBFL5,UCBALTPH         ALTERNATE PATH ?
         BNZ   TESTCPU0                BRANCH IF YES
         MVC   MSGPATH0(5),=C' N/A '   NOT APPLICATABLE
         B     TESTALOC                BRANCH
TESTCPU0 EQU   *
         TM    CPUBYTE,X'F0'           CPU 0 AVAILABLE ?
         BZ    TESTCPU1                BRANCH IF NOT
         MVC   MSGPATH0,=C'**'
         TM    UCBCHM,UCBPPA           PRIMARY PATH FROM CPU 0 ?
         BNZ   *+8                     BRANCH IF NOT
         MVI   MSGPATH0,C'1'
         TM    UCBCHM,UCBSPA           SECONDARY PATH FROM CPU 0 ?
         BNZ   TESTCPU1                BRANCH IF NOT
         MVI   MSGPATH0+1,C'1'
TESTCPU1 EQU   *
         TM    CPUBYTE,X'0F'           CPU 1 AVAILABLE ?
         BZ    TESTALOC                BRANCH IF NOT
         MVC   MSGPATH1,=C'**'
         TM    UCBCHM,UCBPPB           PRIMARY PATH FROM CPU 1 ?
         BNZ   *+8                     BRANCH IF NOT
         MVI   MSGPATH1,C'1'
         TM    UCBCHM,UCBSPB           SECONDARY PATH FROM CPU 1 ?
         BNZ   TESTALOC                BRANCH IF NOT
         MVI   MSGPATH1+1,C'1'
         EJECT
TESTALOC EQU   *
         TM    UCBSTAT,UCBALOC         DEVICE ALLOCATED ?
         BZ    TESTNRDY                BRANCH IF NOT
         MVC   MSGALLOC,=8C'.'         INITIALIZE MESSAGE AREA
         MVC   PJOBNAME,=CL8' '        CLEAR JOB NAME
         L     R09,UCBEXTPT            LOAD ADDRESS OF COMMON UCB EXT.
         LA    R09,UCBASID             LOAD ADDRESS OF ASID
         ST    R09,PASIDADR                AND STORE IT
         LA    R01,PARMLIST            LOAD ADDRESS OF PARAMETER LIST
         L     R15,FASIDADR            LOAD ADDRESS OF SUBROUTINE
         BALR  R14,R15                 BRANCH TO SUBROUTINE
         LTR   R15,R15                 VALID JOB NAME ?
         BNZ   TESTNRDY                BRANCH IF NOT
         MVC   MSGALLOC,PJOBNAME       MOVE JOB NAME
TESTNRDY EQU   *
         TM    UCBFLA,UCBNRY           DEVICE READY ?
         BZ    TSTCUBSY                BRANCH IF YES
         MVC   MSGREADY,=C'NR'         INDICATE NOT READY
TSTCUBSY EQU   *
         TM    UCBFLA,UCBCUB           CONTROL UNIT BUSY ?
         BZ    TSTDVBSY                BRANCH IF NOT
         MVC   MSGSTAT(4),=C'CUBS'     MOVE TEXT
         B     TSTDVRES                BRANCH
TSTDVBSY EQU   *
         TM    UCBFLA,UCBBSY           DEVICE BUSY ?
         BZ    TSTMOUNT                BRANCH IF NOT
         MVC   MSGSTAT(4),=C'BUSY'     MOVE TEXT
         B     TSTDVRES                BRANCH
TSTMOUNT EQU   *
         TM    UCBDMCT,UCBMOUNT        MOUNT PENDING ?
         BZ    TSTIOREC                BRANCH IF NOT
         MVC   MSGSTAT(5),=C'MOUNT'    MOVE TEXT
         EJECT
TSTIOREC EQU   *
         TM    UCBFLA,UCBQISCE         IO RECOVERY ACTIVE ?
         BZ    TSTDVRES                BRANCH IF NOT
         MVC   MSGSTAT(5),=C'IOREC'    MOVE TEXT
         B     TESTVOLI                BRANCH
TSTDVRES EQU   *
         TM    UCBFLB,UCBRESVH         DEVICE RESERVED ?
         BZ    TESTVOLI                BRANCH IF NOT
*        MVC   MSGSTAT+4(2),=C'-R'     MOVE TEXT                  /XUCB
         MVC   MSGSTAT+3(2),=C'-R'     MOVE TEXT                  /XUCB
TESTVOLI EQU   *
         TM    UCBDVCLS,X'A0'          TAPE OR DIRECT ACCESS ?
         BZ    TESTDFLT                BRANCH IF NOT
         CLC   UCBVOLI,=XL6'0'         VOLID ?
         BE    TESTSTOR                BRANCH IF NOT
         MVC   MSGVOLID,UCBVOLI        MOVE VOLSER NUMBER
TESTSTOR EQU   *
         TM    UCBSTAB,UCBBSTR         STORAGE VOLUME ?
         BZ    TESTPUB                 BRANCH IF NOT
         MVC   MSGATTR1,=C'STG/RMV'    SPECIFY STORAGE VOLUME
TESTPUB  EQU   *
         TM    UCBSTAB,UCBBPUB         PUBLIC VOLUME ?
         BZ    TESTPRV                 BRANCH IF NOT
         MVC   MSGATTR1,=C'PUB/RMV'    SPECIFY PUBLIC VOLUME
TESTPRV  EQU   *
         TM    UCBSTAB,UCBBPRV         PRIVATE VOLUME ?
         BZ    TESTRES                 BRANCH IF NOT
         MVC   MSGATTR1,=C'PRV/RMV'    SPECIFY PRIVATE VOLUME
TESTRES  EQU   *
         TM    UCBSTAT,UCBPRES         VOLUME RESIDENT ?
         BZ    TESTRSV                 BRANCH IF NOT
         MVC   MSGATTR2,=C'RES'        SPECIFY RESIDENT VOLUME
TESTRSV  EQU   *
         TM    UCBSTAT,UCBRESV         VOLUME RESERVED ?
         BZ    TESTDFLT                BRANCH IF NOT
         MVC   MSGATTR2,=C'RSV'        SPECIFY RESERVED VOLUME
TESTDFLT EQU   *
         CLI   NOPMFLAG,X'FF'          DEFAULT OUTPUT ?
         BNE   MSGOK                   BRANCH IF NOT
         CLC   MSGSTAT,=CL8' '         STATUS INFORMATION ?
         BE    NEXTUCB                 BRANCH IF NOT
         CLC   MSGSTAT(5),=C'MOUNT'    MOUNT PENDING ?
         BE    NEXTUCB                 BRANCH IF NOT
MSGOK    EQU   *
         MVI   WTOBYTE,X'FF'           SET FLAGS
         LA    R14,NEXTUCB             LOAD RETURN ADDRESS
         MVC   MSGFLAG,=XL6'0'         CLEAR FLAG BYTES
         LA    R10,68(R10)             LOAD ADDRESS OF NEXT ENTRY
         C     R10,ADDRETB1            END OF TABLE
         BNL   PUTMSG                  BRANCH IF YES
         ST    R10,SAVE1R10            SAVE CURRENT ADDRESS
         BR    R14                     RETURN
         EJECT
***********************************************************************
*                                                                     *
*      FINAL PROCESSING.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
ENDPROC  EQU   *
         XUCBOFF R02                   FREE WORK AREA             /XUCB
*        FREEMAIN R,SP=125             FREE TABLE AREA            /XUCB
         MVI   WTORBYTE,X'00'          CLEAR FLAG BYTE
         TM    WTOBYTE,X'FF'           MESSAGE OUTPUT ?
         BNZ   PUTOPMSG                BRANCH IF YES
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         MVC   MESSAGE(LENMSG03),MSG03 MOVE MESSAGE
         CLI   VOLBYTE,X'FF'           PROGRAM = X-DVOL ?
         BE    *+10                    BRANCH IF YES
         MVC   MSGNAME,PNAME           MOVE NAME OF PROGRAM
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         WTO   MF=(E,MESSAGE)          DISPLAY MESSAGE
         B     RETURN                  BRANCH
         SPACE 2
*
*      DISPLAY OF OPEN MESSAGES.
*
         SPACE 1
PUTOPMSG EQU   *
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         CLC   MSGFLAG,=XL6'0'         ALL MESSAGES DISPLAYED ?
         BNE   RETURN                  BRANCH IF NOT
         BAL   R14,PUTMSG              IF YES, BR. TO MESSAGE OUTPUT
         EJECT
*
*      FREE WORK AREAS,
*      RELOAD REGISTER CONTENTS,
*      RETURN.
*
         SPACE 1
RETURN   EQU   *
         XRETURN ,R
         SPACE 3
***********************************************************************
*                                                                     *
*      PARAMETER ERROR ROUTINE.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
PRMERROR EQU   *
         LA    R10,MSGTAB              LOAD ADDRESS OF MESSAGE TABLE
         MVC   MESSAGE(LENMSG04),MSG04 MOVE MESSAGE
         MVC   MSGNAME,PNAME           MOVE NAME OF PROGRAM
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         WTO   MF=(E,MESSAGE)          DISPLAY MESSAGE
         B     RETURN                  BRANCH
         EJECT
***********************************************************************
*                                                                     *
*      TEST DEVICES FOR SPECIFIED STATUS.                             *
*                                                                     *
***********************************************************************
         SPACE 3
TSTFLAGS EQU   *
         CLC   TAPEFLAG(5),=XL6'0'     DEVICES TYPE(S) SPECIFIED ?
         BE    TSTMSS                  BRANCH IF NOT
         LA    R04,PRMTABLE-10         LOAD ADDRESS OF TABLE
         LA    R05,PRMFLAGS            LOAD ADDRESS OF FLAG BYTES
TSTDEVLP EQU   *
         LA    R04,10(R04)             LOAD ADDRESS OF NEXT ENTRY
         CLI   1(R04),X'FF'            END OF TABLE ?
         BE    CLEARFLG                BRANCH IF YES
         LA    R05,1(R05)              LOAD ADDRESS OF NEXT FLAG BYTE
         TM    0(R05),X'FF'            FLAG ON ?
         BZ    TSTDEVLP                BRANCH IF NOT
         CLC   UCBDVCLS,1(R04)         SPECIFIED DEVICE TYPE ?
         BNE   TSTDEVLP                BRANCH IF NOT
         SPACE 1
TSTMSS   EQU *
         TM    MSSFLAG,X'FF'           VIRTUAL DEVICES SPECIFIED ?
         BZ    TSTNOMSS                BRANCH IF NOT
         TM    UCBDVCLS,UCB3DACC       DIRECT ACCESS ?
         BZ    TSTMSC                  BRANCH IF NOT
         TM    UCBTBYT2,UCBRVDEV       VIRTUAL DEVICE ?
         BZ    CLEARFLG                BRANCH IF NOT
         B     TSTPLINE                BRANCH
TSTNOMSS EQU   *
         TM    UCBDVCLS,UCB3DACC       DIRECT ACCESS ?
         BZ    TSTNOMSC                BRANCH IF NOT
         TM    UCBTBYT2,UCBRVDEV       VIRTUAL DEVICE ?
         BO    CLEARFLG                BRANCH IF YES
         B     TSTPLINE                BRANCH
TSTNOMSC EQU   *
         TM    UCBUNTYP,UCBDSM         MSC ?
         BO    CLEARFLG                BRANCH IF YES
         EJECT
TSTPLINE EQU   *
         CLC   ONLFLAG(2),=XL6'0'      ON/OFFLINE DEVICES SPECIFIED ?
         BE    TSTREADY                BRANCH IF NOT
         TM    UCBSTAT,UCBONLI         DEVICE ONLINE ?
         BZ    TSTPOFFL                BRANCH IF NOT
         TM    ONLFLAG,X'FF'           ONLINE DEVICES SPECIFIED ?
         BZ    CLEARFLG                BRANCH IF NOT
         B     TSTREADY                BRANCH
TSTPOFFL EQU   *
         TM    OFFLFLAG,X'FF'          OFFLINE DEVICES SPECIFIED ?
         BZ    CLEARFLG                BRANCH IF NOT
         SPACE 1
TSTREADY EQU   *
         TM    RDYFLAG,X'FF'           READY DEVICES SPECIFIED ?
         BZ    TSTFLGOK                BRANCH IF NOT
         TM    UCBSFLS,UCBNRY          DEVICE READY ?
         BO    CLEARFLG                BRANCH IF NOT
         SPACE 1
TSTFLGOK EQU   *
         BR    R14                     RETURN
         SPACE 2
CLEARFLG EQU   *
         MVI   FNDBYTE,X'00'           CLEAR FLAG BYTE
         BR    R14                     RETURN
TSTMSC   EQU   *
         TM    UCBUNTYP,UCBDSM         MSC ?
         BNO   CLEARFLG                BRANCH IF NOT
         B     TSTPLINE                BRANCH IF YES
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE DISPLAY.                                               *
*                                                                     *
***********************************************************************
         SPACE 3
PUTMSG   EQU   *
         ST    R14,SAVE1R14            SAVE RETURN ADDRESS
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         WTO   MF=(E,HEADER)           DISPLAY HEAD LINE
         LA    R10,MSGTAB-68           LOAD ADDRESS OF TABLE
         L     R09,ADDRETB1            LOAD ADDRESS OF TABLE END
         BAL   R14,DISPLAY             BRANCH TO DISPLAY
         LA    R10,MSGTAB              LOAD ADDRESS OF TABLE
         ST    R10,SAVE1R10                AND STORE IT
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         CLI   WTORBYTE,X'FF'          FLAG ON ?
         BNER  R14                     RETURN IF NOT
NXTREPLY EQU   *
         XC    ECB,ECB                 CLEAR EVENT CONTROL BLOCK
         MVI   REPLY,C' '              CLEAR REPLY AREA
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         LA    R04,ECB                 LOAD ADDRESS OF ECB
         LA    R05,REPLY               LOAD ADDRESS OF REPLY
         WTOR  ,(R05),1,(R04),MF=(E,WTOR)
         SPACE 1
         WAIT  ECB=ECB                 WAIT FOR COMPLETION
         CLI   REPLY,C'N'              REPLY = NO ?
         BE    RETURN                  BRANCH IF YES
         CLI   REPLY,C'Y'              REPLY = YES ?
         BNE   NXTREPLY                BRANCH IF NOT
         L     R14,SAVE1R14            LOAD RETURN ADDRESS
         BR    R14                     RETURN
         EJECT
*
*      DISPLAY SINGLE MESSAGE LINES.
*
         SPACE 1
DISPLAY  EQU   *
         ST    R14,SAVE2R14            SAVE RETURN ADDRESS
NEXTMSG  EQU   *
         LA    R10,68(R10)             LOAD ADDRESS OF NEXT ENTRY
         CR    R10,R09                 END OF TABLE ?
         BNL   ENDMSG                  BRANCH IF YES
         CLC   MSGFLAG,=X'FFFF'        FLAGS ON ?
         BE    ENDMSG                  BRANCH IF YES
         MVC   MSGFLAG,=X'FFFF'        SET FLAGS
         L     R00,UCMID               LOAD UCM ID. OF CALLER
         WTO   MF=(E,MESSAGE)          DISPLAY ONE MESSAGE
         B     NEXTMSG                 BRANCH
ENDMSG   EQU   *
         L     R14,SAVE2R14            LOAD RETURN ADDRESS
         BR    R14                     RETURN
         SPACE 3
***********************************************************************
*                                                                     *
*      COMMANDS VIA EXECUTE COMMAND.                                  *
*                                                                     *
***********************************************************************
         SPACE 3
MOVEPVOL EQU   *
         MVC   1(0,R06),1(R07)         MOVE VOLID TO TABLE
NCVOLID1 EQU   *
         NC    ZWVOLID1(0),1(R06)      TRANSLATE VOLID
OCVOLID2 EQU   *
         OC    ZWVOLID2(0),1(R07)      TRANSLATE VOLID
CLCPARM  EQU   *
         CLC   2(0,R01),1(R07)         PARAMETER = TABLE ENTRY ?
         EJECT
***********************************************************************
*                                                                     *
*      REGISTER EQUATES.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
R00      EQU   0                       MACRO AND PARAMETER REGISTER
R01      EQU   1                       MACRO AND PARAMETER REGISTER
R02      EQU   2                       PTR. IN CVT / UCB LOOKUP TABLE
R03      EQU   3                       POINTER TO UCB
R04      EQU   4                       WORK REGISTER
R05      EQU   5                       WORK REGISTER
R06      EQU   6                       WORK REGISTER
R07      EQU   7                       WORK REGISTER
R08      EQU   8                       WORK REGISTER
R09      EQU   9                       BASE OF COMMON UCB EXTENSION
R10      EQU   10                      POINTER IN MESSAGE TABLE
R11      EQU   11                      BASE OF PARMAREA / UCBADTAB
R12      EQU   12                      BASE OF CSECT DVOL
R13      EQU   13                      BASE OF WKAREA
R14      EQU   14                      RETURN ADDRESSES
R15      EQU   15                      CSECT ADRESSES AND RETURN CODES
         EJECT
***********************************************************************
*                                                                     *
*      TRANSLATE TABLE.                                               *
*                                                                     *
***********************************************************************
         SPACE 2
TABHEXA  DC    C'0123456789ABCDEF'
         SPACE 3
***********************************************************************
*                                                                     *
*      PARAMETER TABLE.                                               *
*                                                                     *
***********************************************************************
         SPACE 2
PRMTABLE EQU   *
         DC    X'03',X'80',CL8'TAPE'
         DC    X'03',X'20',CL8'DASD'
         DC    X'01',X'08',CL8'UR'
         DC    X'01',X'40',CL8'TP'
         DC    X'06',X'10',CL8'GRAPHIC'
         DC    X'02',X'FF',CL8'MSS'
         DC    X'05',X'FF',CL8'ONLINE'
         DC    X'06',X'FF',CL8'OFFLINE'
         DC    X'04',X'FF',CL8'READY'
         DC    X'FF',X'FF',CL8' '      TABLE END
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE CONSTANTS.                                             *
*                                                                     *
***********************************************************************
         SPACE 3
* MSG FORMAT CHANGE FUER 3 BYTE UCB ADRESSE                       /XUCB
*SG01    WTO   'XDVOL01 VOLSER ADR LNE -ALLOC-- -STATUS-- PATHS -ATTR--
*               UCB ',
MSG01    WTO   'XDVOL01 VOLSER ADR LNE -ALLOC-- -STATUS- PATHS -ATTR-- *
               -UCB--',                                                *
               MF=L,MCSFLAG=(REG0)
LENMSG01 EQU   *-MSG01
         SPACE 1
* MSG FORMAT CHANGE FUER 3 BYTE UCB ADRESSE                       /XUCB
*SG02    WTO   'XDVOL02        ADR ON      NO   RD        ../..   N/A
*               9999',
MSG02    WTO   'XDVOL02        ADR ON      NO   RD       ../..   N/A   *
               999999',                                                *
               MF=L,MCSFLAG=(REG0)
LENMSG02 EQU   *-MSG02
         SPACE 1
MSG03    WTO   'XDVOL03 NO OUTPUT PRODUCED',                           *
               MF=L,MCSFLAG=(REG0)
LENMSG03 EQU   *-MSG03
         SPACE 1
MSG04    WTO   'XDVOL04 INVALID PARAMETER LIST',                       *
               MF=L,MCSFLAG=(REG0)
LENMSG04 EQU   *-MSG04
         EJECT
MSG05    WTOR  'XDVOL05 REPLY ''Y'' (YES) OR ''N'' (NO) TO CONTINUE',  *
               MF=L,MCSFLAG=(REG0)
LENMSG05 EQU   *-MSG05
         SPACE 3
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*      WORK AREAS.                                                    *
*                                                                     *
***********************************************************************
         SPACE 3
WKAREA   DSECT
SAVEAREA DS    18F                     SAVEAREA OF GENERAL REGISTERS
         SPACE 2
*
*      ADRESSEN-SPEICHER.
*
         SPACE 1
ADDRETB1 DS    F                       END ADDRESS OF MESSAGE TABLE
ADDRETPV DS    F                       END ADDRESS OF VOLUME TABLE
SAVE1R10 DS    F                       SAVEAREA1 OF R10
SAVE1R14 DS    F                       SAVEAREA1 OF R14
SAVE2R14 DS    F                       SAVEAREA2 OF R14
UCBADDR  DS    F                       ADDRESS OF UCB TABLE
FASIDADR DS    F                       ADDRESS OF SUBROUTINE 'FASID'
UCMID    DS    F                       UCM ID. OF CALLER
         SPACE 2
*
*      WORK AREAS.
*
         SPACE 1
ECB      DS    F                       EVENT CONTROL BLOCK
FNDBYTE  DS    C                       FLAG
VOLBYTE  DS    C                       FLAG OF SPECIFIED VOLUMES
CPUBYTE  DS    C                       FLAG BYTE OF CPU'S
WTOBYTE  DS    C                       FLAG OF WTO
WTORBYTE DS    C                       FLAG OF WTOR
REPLY    DS    C                       REPLY AREA
VOLSERNO DS    CL6                     VOLSER NUMBER
ZWVOLID1 DS    CL6                     VOLSER NUMBER
ZWVOLID2 DS    CL6                     VOLSER NUMBER
PNAME    DS    CL4                     NAME OF PROGRAM
         SPACE 2
*
*      PARAMETER LIST OF 'FASID'.
*
         SPACE 1
PARMLIST DS    0F
PASIDADR DS    F                       ADDRESS OF ASID
PJNMEADR DS    F                       ADDRESS OF JOB NAME
PSNMEADR DS    F                       ADDRESS OF STEP NAME
PJOBNAME DS    CL8                     JOB NAME
PSTEPNME DS    CL8                     STEP NAME
         EJECT
*
*      PARAMETER FLAGS.
*
         SPACE 1
PRMFLAGS DS    0CL8
PARMFLAG DS    C                       FLAG BYTE OF PARAMETER
TAPEFLAG DS    C                       FLAG BYTE OF TAPE SELECT.
DASDFLAG DS    C                       FLAG BYTE OF DASD SELECT.
URFLAG   DS    C                       FLAG BYTE OF UR SELECT.
TPFLAG   DS    C                       FLAG BYTE OF TP SELECT.
GRAPFLAG DS    C                       FLAG BYTE OF GRAPHIC SELECT.
MSSFLAG  DS    C                       FLAG BYTE OF MSS SELECT.
ONLFLAG  DS    C                       FLAG BYTE OF ONLINE SELECT.
OFFLFLAG DS    C                       FLAG BYTE OF OFFLINE SELECT.
RDYFLAG  DS    C                       FLAG BYTE OF READY SELECT.
NOPMFLAG DS    C                       FLAG BYTE OF DEFAULT DISPLAY
PFLAGLEN EQU   *-PRMFLAGS              LENGTH OF FLAG BYTES
         SPACE 2
*
*      TABLE WITH PARAMETER ADDRESSES OR VOLSER NUMBERS.
*
*      BYTE1 - LENGTH OF PARAMETER OR X'80'
*      BYTES2-7 - PARAMETER VALUE(S)
*
         SPACE 1
TABPVOL1 DS    10CL7                   TABLE 1
ENDTPVOL EQU   *
         DS    CL7
TABPVOL2 DS    CL77                    TABLE 2
         SPACE 2
*
*      MESSAGE AREA.
*
*      BYTE1-66: WTO MESSAGE LINE
*      BYTE67-68: X'0000', IF MESSAGE LINE AVAILABLE
*
         SPACE 1
         DS    0F
MSGTAB   DS    10CL68
ENDTAB1  EQU   *
         DS    0F
HEADER   DS    CL(LENMSG01)            HEAD LINE
         DS    0F
WTOR     DS    CL(LENMSG05)            WTOR LINE
         DS    0F
         SPACE 1
WKAREALN EQU   *-WKAREA                LENGTH OF WORK AREAS
         EJECT
***********************************************************************
*                                                                     *
*      MESSAGE AREA.                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
MESSAGE  DSECT
         ORG   MESSAGE+5
MSGNAME  DS    CL4                     PROGRAM NAME
         DS    CL3
MSGVOLID DS    CL6                     VOLUME SERIAL NUMBER
         DS    CL1
MSGADR   DS    CL3                     UNIT ADDRESS
         DS    CL1
MSGLINE  DS    CL3                     ON/OFF LINE
         DS    CL1
MSGALLOC DS    CL8                     UNIT ALLOC
         DS    CL1
MSGREADY DS    CL2                     UNIT READY
         DS    CL1
*SGSTAT  DS    CL6                     UNIT STATUS                /XUCB
MSGSTAT  DS    CL5                     UNIT STATUS                /XUCB
         DS    CL1
MSGPATH0 DS    CL2                     PATHS FROM CPU 0
         DS    CL1
MSGPATH1 DS    CL2                     PATHS FROM CPU 1
         DS    CL1
MSGATTR1 DS    CL7                     ATTRIBUTES
         ORG   *-3
MSGATTR2 DS    CL3                     ATTRIBUTES
         DS    CL1
*SGUCBAD DS    CL4                     UCB ADDRESS                /XUCB
MSGUCBAD DS    CL6                     UCB ADDRESS                /XUCB
         ORG   MESSAGE+13
WTORNAME DS    CL4                     PROGRAM NAME
         ORG   MESSAGE+66
MSGFLAG  DS    CL2                     FLAG BYTES
         EJECT
***********************************************************************
*                                                                     *
*      INPUT PARAMETER AREA.                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         XPARM
PARMEND  EQU   *
         SPACE 3
***********************************************************************
*                                                                     *
*      UCB ADDRESS TABLE.                                             *
*                                                                     *
***********************************************************************
         SPACE 3
UCBADTAB DSECT
         DS    1024H
UCBTBEND DS    H             =X'FFFF'
UCBTBLEN EQU   *-UCBADTAB
         EJECT
***********************************************************************
*                                                                     *
*      UCB-DEFINITIONEN.                                              *
*                                                                     *
***********************************************************************
         SPACE 2
UCB      DSECT
         IEFUCBOB LIST=YES
         CVT DSECT=YES                 CVT MAPPING                /XUCB
         END   DVOL
./ ADD NAME=VTOC
         TITLE 'XVTOC                             X-COMMAND VTOC'
VTOC     CSECT
         SPACE 3
* MVS/XA UCBSCAN                          29.6.83 BMW             /XUCB
***********************************************************************
*                                                                     *
*      PROGRAM-ID. VTOC.                                              *
*      AUTHOR. EHNERT.                                                *
*      INSTALLATION. BAYER AG, LEVERKUSEN, RECHENZENTRUM.             *
*      DATE-WRITTEN. 24. 3. 1975.                                     *
*                                                                     *
*      AENDERUNGEN:                                                   *
*      14.09.81 -- NACH INSTALLATION VON DF/DS UND EINFUEHRUNG VON    *
*                  INDEXED-VTOC WURDE UMSTELLUNG AUF CVAF-MACROS      *
*                  NOTWENDIG. AUSSERDEM WURDE OVERHEAD BZGL. DER      *
*                  INFORMATIONEN UEBER VSAM-DS BESEITIGT.  ALLE       *
*                  AENDERUNGEN SIND IN SP. 68-71 MIT "CVAF" GEKENNZ.  *
*                                                         UPE         *
*                                                                     *
*      REMARKS.                                                       *
*                                                                     *
*          DAS X-COMMAND 'VTOC' GIBT INFORMATIONEN UEBER DIE          *
*          VTOC'S DER ALS PARAMETER SPEZIFIZIERTEN VOLUMES AUS.       *
*                                                                     *
*          ALS PARAMETER KOENNEN BIS ZU ZEHN VOLSER-MASKEN ANGE-      *
*          GEBEN WERDEN, MIT DEREN HILFE EINE AUSWAHL DER VOLUMES     *
*          GETROFFEN WIRD. DABEI STEHT '*' FUER JEDES ZEICHEN AN      *
*          DIESER STELLE.                                             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      BEGINN DER VERARBEITUNG.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      SICHERN DER REGISTER,
*      SPEICHERPLATZANFORDERUNG FUER ARBEITSBEREICHE,
*      ZUORDNUNG DER BASISREGISTER.
*
         SPACE 1
         XSAVE R12,,VTOC,ABERL
         SPACE 1
         LR    R11,R1        BASISREG. FUER DSECT PARMAREA INITIALIS.
         USING UCB,R3        BASISREG. FUER DSECT UCB ZUORDNEN
         USING MESSAGE,R10   BASISREG. FUER DSECT MESSAGE ZUORDNEN
         USING PARMAREA,R11  BASISREG. FUER DSECT PARMAREA ZUORDNEN
         USING ABER,R13      BASISREG. FUER DSECT ABER ZUORDNEN
         EJECT
*
*      INITIALISIERUNG DER SPEICHER.
*
         SPACE 1
         MVI   KOMTFLAG,X'00'      LOESCHEN FLAG KOMMENTARZEILE    CVAF
         MVC   CVAFRC,=F'0'        CVAF RETURNCODE GRUNDSTELLUNG   CVAF
         MVC   CVAFST,=F'0'        CVAF STATUSCODE GRUNDSTELLUNG   CVAF
         MVI   FNDBYTE,X'00' SCHALTER LOESCHEN
         MVI   WTORBYTE,X'FF' SCHALTER SETZEN
         LA    R2,ENDTAB1    ENDADRESSEN DER
         LA    R3,ENDTPVOL       TABELLEN LADEN
         STM   R2,R3,ADDRETB1    UND SPEICHERN
         MVC   UCMID,REG4    UCM-ID. DES AUFRUFERS UEBERTRAGEN
         SPACE 2
*
*      FLAG BYTS IN NACHRICHTENBEREICHEN SETZEN.
*
         SPACE 1
         LA    R10,MSGTAB    ADRESSE DER TABELLE LADEN
         ST    R10,SAVE1R10      UND SPEICHERN
         L     R3,ADDRETB1   ENDADRESSE DER TABELLEN LADEN
CLEARMSS EQU   *
         MVC   MESSFLAG,=X'FFFF' FLAG BYTES SETZEN
         LA    R10,MESSLGTH(R10) R10 ERHOEHEN
         CR    R10,R3        ENDE DER TABELLEN ERREICHT ?
         BL    CLEARMSS      NEIN, VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DER EINGABEPARAMETER.                             *
*                                                                     *
***********************************************************************
         SPACE 3
         LA    R4,TEXT+4     R4 INITIALISIEREN
         LA    R8,PARMENDE   ENDADRESSE DES PARAMETERBEREICHS LADEN
         CLI   0(R4),C','    PARAMETER ANGEGEBEN ?
         BNE   FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         LA    R6,TABPVOL1   ADRESSE DER VOLSER-TABELLE LADEN
NEXTPARM EQU   *
         SR    R5,R5         R5 LOESCHEN
         LR    R7,R4         ADRESSE DES TRENNUNGSZEICHENS LADEN
NXTBYTE  EQU   *
         LA    R4,1(R4)      R4 ERHOEHEN
         CR    R4,R8         PARAMETERBEREICH UEBERSCHRITTEN ?
         BNL   FEHLPARM      JA, VERZW. ZUR FEHLERROUTINE
         CLI   0(R4),C','    ENDE DES PARAMETERS ERREICHT ?
         BE    NEXTPVOL      JA, VERZWEIGEN
         CLI   0(R4),X'00'   ENDE DER PARAMETERKETTE ERREICHT ?
         BE    NEXTPVOL      JA, VERZWEIGEN
         CLI   0(R4),C' '    ENDE DER PARAMETERKETTE ERREICHT ?
         BE    NEXTPVOL      JA, VERZWEIGEN
         LA    R5,1(R5)      R5 ERHOEHEN
         CLI   0(R4),C'*'    BELIEBIGES ZEICHEN AN DIESER STELLE ?
         BNE   NXTBYTE       NEIN, VERZWEIGEN
         MVI   0(R4),X'FF'   ZEICHEN UMSETZEN
         B     NXTBYTE       VERZWEIGEN
         EJECT
NEXTPVOL EQU   *
         C     R6,ADDRETPV   TABELLENENDE UEBERSCHRITTEN ?
         BNL   FEHLPARM      JA, VERZWEIGEN
         C     R5,=F'6'      LAENGE DER VOLID GUELTIG ?
         BH    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         LTR   R5,R5         VOLID ANGEGEBEN ?
         BZ    FEHLPARM      NEIN, VERZW. ZUR FEHLERROUTINE
         BCTR  R5,0          R5 UM EINS VERMINDERN
         EX    R5,MOVEPVOL   VERZW. ZUR UEBERTR. DER VOLSER NUMBER
         STC   R5,0(R6)      LAENGENANGABE IN TABELLE EINTRAGEN
         LA    R6,7(R6)      R6 ERHOEHEN
         CLI   0(R4),C','    FOLGEN WEITERE PARAMETER ?
         BE    NEXTPARM      JA, VERZWEIGEN
         SPACE 2
*
*      ABSCHLUSS DER PARAMETERVERARBEITUNG.
*
         SPACE 1
         MVI   0(R6),X'80'   TABELLENENDE SPEZIFIZIEREN
         MVC   TABPVOL2,TABPVOL1 VOLSER-TABELLE KOPIEREN
         LA    R6,TABPVOL2-1 ADRESSE DER TABELLE LADEN
NEXTFF   EQU   *
         LA    R6,1(R6)      R6 ERHOEHEN
         CLI   0(R6),X'80'   TABELLENENDE ERREICHT ?
         BE    GETUCB        JA, VERZWEIGEN
         CLI   0(R6),X'FF'   TABELLENZEICHEN = X'FF' ?
         BNE   NEXTFF        NEIN, VERZWEIGEN
         MVI   0(R6),X'00'   TABELLENZEICHEN UMSETZEN
         B     NEXTFF        VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      VERARBEITUNG DER UCB-INFORMATIONEN.                            *
*                                                                     *
***********************************************************************
         SPACE 3
GETUCB   EQU   *
*        L     R2,16         ADRESSE DER CVT LADEN                /XUCB
         DROP  R11           R11 FREIGEBEN                        /XUCB
*        GETMAIN R,LV=VEKLEN,SP=125                               /XUCB
*        LR    R11,R1        BASISREG. FUER DSECT VEKTOR INIT.    /XUCB
*        USING VEKTOR,R11    BASISREG. FUER DSECT VEKTOR ZUORDNEN /XUCB
*        ST    R11,UCBADDR   VEKTOR-ADRESSE SPEICHERN             /XUCB
*        MVI   UCBADDR,X'80' OPTION BYTE SETZEN                   /XUCB
*        LA    R1,UCBADDR    ADRESSE DER VEKTOR-ADRESSE LADEN     /XUCB
*        LINK  EP=GETUCBS              LINK TO SUBROUTINE         /XUCB
*        LA    R2,VEKTOR     ADRESSE DES UCB-VEKTORS LADEN        /XUCB
*        SR    R3,R3         R3 LOESCHEN                          /XUCB
*        MVC   VEKEND,=X'FFFF' ENDE DES UCB-VEKTORS SPEZIFIZIEREN /XUCB
*        S     R2,=F'2'      R2 UM ZWEI VERMINDERN                /XUCB
         XUCBSCAN R2,X'20'    UCBSCAN IST FUER DASD               /XUCB
NEXTUCB  EQU   *
*        LA    R2,2(R2)      R2 ERHOEHEN                          /XUCB
*        CLC   0(2,R2),=X'FFFF' ENDE DES UCB-VEKTORS ERREICHT ?   /XUCB
*        BE    ABSCHLUS      JA, VERZW. ZUM ABSCHLUSS DER VERARBEI/XUCB
*        ICM   R3,3,0(R2)    ADRESSE DES UCB LADEN                /XUCB
*        CLI   UCBDVCLS,X'20' DEVICE = DASD ?                     /XUCB
*        BNE   NEXTUCB       NEIN, VERZWEIGEN                     /XUCB
         XUCBGET R2,ABSCHLUS,ABSCHLUS                             /XUCB
         LR    R3,R1         ADRESSE DES UCB LADEN                /XUCB
         TM    UCBSTAT,X'80' EINHEIT ONLINE ?
         BZ    NEXTUCB       NEIN, VERZWEIGEN
         TM    UCBSTAT,X'10' EINHEIT IM UNLOAD ?
         BO    NEXTUCB       JA, VERZWEIGEN
         CLC   UCBVOLI,=XL6'0' VOLID VORHANDEN ?
         BE    NEXTUCB       NEIN, VERZWEIGEN
         TM    UCB+17,X'08'                VIRTUAL DEVICE ?
         BO    TSTVVOL                     BRANCH IF YES
         EJECT
*
*      VERGLEICH MIT SPEZIFIZIERTEN VOLSER NUMBERS.
*
         SPACE 1
         SR    R5,R5         R5 LOESCHEN
         LA    R6,TABPVOL1-7 ADRESSE DER VOLSER-TABELLE LADEN
         LA    R7,TABPVOL2-7 ADRESSE DER VOLSER-TABELLE LADEN
NXTVOLID EQU   *
         LA    R6,7(R6)      R6 ERHOEHEN
         LA    R7,7(R7)      R7 ERHOEHEN
         TM    0(R6),X'80'   TABELLENENDE ERREICHT ?
         BO    NEXTUCB       JA, VERZWEIGEN
         IC    R5,0(R6)      LAENGENANGABE LADEN
         MVC   ZWVOLID1,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         MVC   ZWVOLID2,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         EX    R5,NCVOLID1   VERZW. ZUR UEBERLAGERUNG DER VOLID
         EX    R5,OCVOLID2   VERZW. ZUR UEBERLAGERUNG DER VOLID
         CLC   ZWVOLID1,ZWVOLID2 VOLID = AUSGEW. VOLID ?
         BNE   NXTVOLID      VERZW., WENN VOLID ^= AUSGEW. VOLID
         B     MVCVOLID
         SPACE 1
TSTVVOL  EQU   *
         LA    R6,TABPVOL1-7 ADRESSE DER VOLSER-TABELLE LADEN
NXTVVOL  EQU   *
         LA    R6,7(R6)      R6 ERHOEHEN
         TM    0(R6),X'80'   TABELLENENDE ERREICHT ?
         BO    NEXTUCB       JA, VERZWEIGEN
         CLC   UCBVOLI,1(R6) VOLID = AUSGEW. VOLID ?
         BNE   NXTVVOL       BRANCH IF NOT
MVCVOLID EQU   *
         MVC   VOLSERNO,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         MVI   FNDBYTE,X'FF' FLAG BYTE SETZEN
         EJECT
***********************************************************************
*                                                                     *
*      CCHHR DER VTOC BERECHNEN.                                      *
*                                                                     *
***********************************************************************
         SPACE 3
         SR    R4,R4         R4 LOESCHEN
         IC    R4,UCBTBYT4   DEVICE CODE LADEN
         SLA   R4,2          DEVICE CODE * 4
         LA    R5,TABTCAP    ADRESSE DER TABTCAP-TABELLE LADEN
         AR    R5,R4         R5 ERHOEHEN
         CLI   0(R5),X'80'   DEVICE RESERVED ?
         BE    FEHLDEVC      JA, VERZW. ZUR FEHLERROUTINE
         LH    R6,0(R5)      TABELLENELEMENTEHN
         LH    R7,2(R5)          LADEN UND
         STM   R6,R7,TRKPCYL     SPEICHERN
         LH    R6,UCBVTOC    REL. ADRESSE DER VTOC LADEN
         SRDA  R6,32         R6 --> R7
         D     R6,TRKPCYL    DIV. TT / SPECIFIC TRACKS/CYLINDER
         XC    BFLHDR(BFLHLN+BFLELN),BFLHDR CLEAR CVAF BUFFER LIST CVAF
         STCM  R7,3,CCHHR    CC-ADRESSE DER VTOC SPEICHERN
         STCM  R6,3,CCHHR+2  HH-ADRESSE DER VTOC SPEICHERN
         MVI   CCHHR+4,X'01' R-ADRESSE DER VTOC SPEICHERN
         EJECT
***********************************************************************
*                                                                     *
*      DSCB4 LESEN UND AUSWERTEN.                                     *
*                                                                     *
***********************************************************************
         SPACE 3
         MVC   CVPLMAP(CVPLNGTH),CVPL  CVAF-PARMLIST UEBERTRAGEN   CVAF
         OI    BFLHFL,BFLHDSCB     ES SOLLEN DSCBS GELESEN WERDEN  CVAF
         MVI   BFLHNOE,X'01'   NUR EIN BUFFER LIST ENTRY VORHANDEN CVAF
         OI    BFLEFL,BFLECHR      SUCHARGUMENT IST CCHHR          CVAF
         LA    R5,DSCBBUF          ADDRESSE DES DSCB-BUFFERS       CVAF
         ST    R5,BFLEBUF          STORE IN CVAF BUFFERLIST        CVAF
         MVI   BFLELTH,DSCB4LTH    READ DSCB4                      CVAF
         CVAFSEQ ACCESS=GTEQ,      CALL CVAF                       CVAF*
               UCB=(R3),                                           CVAF*
               BRANCH=NO,                                          CVAF*
               BUFLIST=BFLHDR,                                     CVAF*
               MF=(E,CVPLMAP)                                      CVAF
         LTR   R15,R15       RETURN CODE = 0 ?
         BZ    TESTFMT4      JA, VERZWEIGE ZUM NAECHSYTEN TEST     CVAF
         BAL   R14,FEHLCVAF  NEIN, VERZWEIGEN
TESTFMT4 DS    0H                                                  CVAF
         CLI   DS4IDFMT,C'4' DSCB = DSCB4 ?
         BNE   FEHLDSCB      NEIN, VERZWEIGEN
         SPACE 1
*
*       AUFBEREITEN DER MSG02 MIT VOLID, UNIT ADR. UND CCHH-ADR. VTOC
*
         L     R10,SAVE1R10  R10        INITIALISIEREN
         MVC   MESSAGE(LENMSG02),MSG02  NACHRICHTENZEILE UEBERTRAGEN
         MVC   MS2VOLID,UCBVOLI         VOLSER NUMBER UEBERTRAGEN
         MVC   MS2ADR,UCBNAME           UNIT NAME UEBERTRAGEN
         UNPK  M2CCCHHR(4),CCHHR(3)     CCC-ADRESSE UEBERTRAGEN
         UNPK  M2CCCHHR+3(3),CCHHR+3(2) HH-ADRESSE UEBERTRAGEN
         MVI   M2CCCHHR+5,C' '          R-ADRESSE LEEREN
         TR    MS2CCCHH,TABHEXA-240     ADRESSE AUFBEREITEN
         SPACE 1
*
*        -- ALTERNATE TRACKS USED?
*        -- VSAM DS ON VOLUME?
*
         LH    R5,DS4NOATK   ANZ. DER REMAINIG ALT. TRACKS LADEN
         C     R5,NOALTRK    ? WURDEN ALTERNATE TRACKS BENOETIGT
         BE    NOALTRKS      NEIN                                  CVAF
         OI    KOMTFLAG,ALTRKS  JA, FLAG SETZEN                    CVAF
NOALTRKS DS    0H                                                  CVAF
         TM    DS4VSIND,X'C0' ? VSAM INDICATORS
         BZ    NOVSAM           NEIN, VERZWEIGEN
         OI    KOMTFLAG,VSAMVOL JA, FLAG SETZEN                    CVAF
NOVSAM   EQU   *
         SPACE 1
*
*      BERECHNUNG DER ALLOCATED TRACKS DER VTOC UND
*      DER BENUTZTEN DSCB'S.
*
         MVC   DBWORD,DS4VTOCE+2 EXTENT-BESCHREIBUNG UEBERTRAGEN
         LH    R4,DBWORD+6   HH-ADRESE DES EXTENT-ENDES LADEN
         LH    R5,DBWORD     CC-ADRESE DES EXTENT-BEGINNS LADEN
         LH    R6,DBWORD+2   HH-ADRESE DES EXTENT-BEGINNS LADEN
         LH    R7,DBWORD+4   CC-ADRESE DES EXTENT-ENDES LADEN
         CR    R4,R6         R4 ^< R6 ?
         BNL   *+10          JA, VERZWEIGEN
         A     R4,TRKPCYL    R4 UM ANZ. TRACKS/CYLINDER ERHOEHEN
         BCTR  R7,0          R7 UM EINS VERMINDERN
         LA    R4,1(R4)      R4 UM EINS ERHOEHEN
         SR    R7,R5         ANZ. DER ALLOCATED CYLINDERS BERECHNEN
         SR    R4,R6         ANZ. DER ALLOCATED TRACKS BERECHNEN
         SR    R6,R6         R6 LOESCHEN
         M     R6,TRKPCYL    MULT. MIT ANZ. TRACKS/CYLINDER
         AR    R7,R4         ANZ. ALLOCATED TRACKS ADDIEREN
         SR    R4,R4         R4 LOESCHEN
         IC    R4,DS4DEVDT   ANZ. DER DSCBS PRO TRACK LADEN
         MR    R6,R4         ANZ. DER ALLOCATED DSCBS BERECHNEN
         LR    R4,R7         ANZ. DER ALLOCATED DSCBS LADEN
         ST    R4,NODSCBS         UND SPEICHERN
         C     R4,=F'99999'  ANZ. DER ALLOC. DSCBS > 99999 ?
         BH    TESTXVTC      JA, VERZWEIGEN
         BAL   R14,CONVDEC   VERZW. ZUM UMWANDELN IN DEZIMALZAHL
         MVC   MS2DSCB,ZWFELD+10 ANZ. DER ALLOC. DSCBS UEBERTR.
TESTXVTC DS    0H                                                  CVAF
         TM    CVFL1,CV1IVT        ? INDEXED VTOC ACCESSED         CVAF
         BZ    NOIXVTOC            NEIN, VERZWEIGEN                CVAF
         OI    KOMTFLAG,INDXVTOC   FLAG SETZEN                     CVAF
         TM    KOMTFLAG,INDXERR    ? INDEXED VTOC IN ERROR         CVAF
         BO    NOIXVTOC            JA, VERZWEIGEN                  CVAF
         CVAFDSM ACCESS=MAPDATA,                                   CVAF*
               MAP=VTOC,                                           CVAF*
               COUNT=YES,                                          CVAF*
               UCB=(R3),                                           CVAF*
               CTAREA=CVAFFMT0,                                    CVAF*
               BRANCH=NO,                                          CVAF*
               MF=(E,CVPLMAP)                                      CVAF
         LTR   R15,R15       RETURN CODE = 0 ?
         BZ    NODSMERR      JA, VERZWEIGEN                        CVAF
         LA    R14,NOIXVTOC  LADE RUECKKEHRADRESSE                 CVAF
         B     FEHLCVAF      VERZWEIGE ZUR FEHLERROUTINE           CVAF
NODSMERR DS    0H                                                  CVAF
         S     R4,CVAFFMT0   ANZAHL DER FREIEN DSCBS SUBTRAHIEREN  CVAF
         B     EDUSDSCB                                            CVAF
NOIXVTOC DS    0H                                                  CVAF
         SH    R4,DS4DSREC   ANZ. DER FREIEN DSCBS SUBTRAHIEREN
         TM    KOMTFLAG,INDXERR  ? VTOC INDEX FEHLERHAFT           CVAF
         BO    EDUSDSCB            JA,VERZWEIGE                    CVAF
         TM    DS4VTOCI,DS4DOCVT ? DOS CONVERTED VTOC              CVAF
         BZ    EDUSDSCB            NEIN, VERZWEIGE                 CVAF
         MVI   DSCB4DSN,C' '       SUCHE DSN='SYS1.VTOCIX.V......' CVAF
         MVC   DSCB4DSN+1(43),DSCB4DSN                             CVAF
         MVC   DSCB4DSN(13),=C'SYS1.VTOCIX.V'                      CVAF
         MVC   DSCB4DSN+13(6),UCBVOLI                              CVAF
         OI    BFLEFL,BFLECHR      SUCHARGUMENT IST CCHHR          CVAF
         LA    R5,DSCBBUF+L'DSCB4DSN ADDRESSE DES DSCB-BUFFERS     CVAF
         ST    R5,BFLEBUF          STORE IN CVAF BUFFERLIST        CVAF
         MVI   BFLELTH,DSCB4LTH-L'DSCB4DSN   READ DATA PORTION     CVAF
         CVAFDIR ACCESS=READ,      CALL CVAF                       CVAF*
               UCB=(R3),                                           CVAF*
               DSN=DSCB4DSN,                                       CVAF*
               BRANCH=NO,                                          CVAF*
               BUFLIST=BFLHDR,                                     CVAF*
               MF=(E,CVPLMAP)                                      CVAF
         LTR   R15,R15       RETURN CODE = 0 ?
         BNZ   EDUSDSCB      NEIN, NICHTGEFUNDEN                   CVAF
         OI    KOMTFLAG,INDXERR  JA, INDEXED VTOC IN ERROR         CVAF
EDUSDSCB DS    0H                                                  CVAF
         C     R4,=F'99999'   ANZ. DER BENUTZTEN DSCBS > 99999 ?
         BH    EDITKOMM      JA, VERZWEIGEN
         BAL   R14,CONVDEC   VERZW. ZUM UMWANDELN IN DEZIMALZAHL
         MVC   MS2USED,ZWFELD+10 ANZ. DER BENUTZTEN DSCBS UEBERTR.
EDITKOMM DS    0H                                                  CVAF
         SPACE 1
*
*      AUFBEREITUNG DER KOMMENTARZEILE
*
         TM    KOMTFLAG,X'FF'      ?IST KOMMENTAR AUFZUBEREITEN    CVAF
         BZ    NOCOMENT            NEIN, VERZWEIGEN                CVAF
         LA    R4,MS2COMM-1        ANFANGSADR. DER KOMMENTARSPALTE CVAF
         LA    R9,L'MS2COMM(R4)    ENDEADR. DER KOMMENTARSPALTE    CVAF
         LA    R5,KOMTAB-4         RELATIVER ANFANG DER TABELLE    CVAF
         LA    R6,8                INIT R6 FUER BCT LOOP           CVAF
         LA    R7,256              LADE INDICATOR                  CVAF
COMLOOP  DS    0H                                                  CVAF
         SRL   R7,1                DIVIDIERE INDICATOR, INHALT:    CVAF
*                                  X'80',X'40',X'20' USW.          CVAF
         EX    R7,TESTMASK         TM   KOMTFLAG,X'??'             CVAF
         BZ    ENDLOOP             NULL, KEIN KOMMENTAR            CVAF
         MVI   0(R4),C','          KOMMA NACH KOMMENTAR            CVAF
         LR    R8,R6               INDEX DER TABELLE LADEN         CVAF
         SLA   R8,2                RELATIVE TAB.-ADRESSE ERRECHNEN CVAF
         AR    R8,R5               ADDIERE ANFANGSADDRESSE TABELLE CVAF
         MVC   1(4,R4),0(R8)       UEBERTRAGE KOMMENTAR AUS TAB.   CVAF
         LA    R4,5(R4)            ERHOEHE ANFANGSADR. KOMMENT.-SP CVAF
         CR    R4,R9               ? ENDE KOMMENTARSPALTE ERREICHT CVAF
         BNL   NOCOMENT            JA, BEENDE DEN LOOP             CVAF
ENDLOOP  DS    0H                                                  CVAF
         BCT   R6,COMLOOP                                          CVAF
NOCOMENT DS    0H                                                  CVAF
         MVI   MS2COMM-1,C' '      DELETE DAS 1.KOMMA              CVAF
         MVI   KOMTFLAG,X'00'      FLAGBYTE KOMMENTARTAB. GRUNDST. CVAF
         LA    R14,NEXTUCB         RUECKSPRUNGADRESSE LADEN
         MVC   MESSFLAG,=XL6'0'    FLAG BYTES LOESCHEN
         LA    R10,MESSLGTH(R10)   R10 ERHOEHEN
         C     R10,ADDRETB1        ? ENDE DER TABELLE UEBERSCHRITTEN
         BNL   AUSMSGM             JA, VERZWEIGEN
         ST    R10,SAVE1R10        LAUFENDE ADRESSE SPEICHERN
         BR    R14                 VERZWEIGEN
***********************************************************************
*                                                                     *
*      ABSCHLUSSROUTINEN.                                             *
*                                                                     *
***********************************************************************
         SPACE 3
ABSCHLUS EQU   *
         XUCBOFF R2           WORKAREA FREIGEBEN                  /XUCB
*        FREEMAIN R,SP=125    SPEICHERPLATZ FREIGEBEN             /XUCB
         MVI   WTORBYTE,X'00' FLAG BYTE LOESCHEN
         CLI   FNDBYTE,X'FF' VOLUME(S) GEFUNDEN ?
         BE    AUSRMSGN      JA, VERZWEIGEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG03)  NACHRICHT AUSGEBEN
         B     ENDE          VERZWEIGEN
         SPACE 2
*
*      AUSGABE NOCH OFFENER INFO-NACHRICHTEN.
*
         SPACE 1
AUSRMSGN EQU   *
         LA    R10,MSGTAB    ADRESSE DER TABELLE LADEN
         CLC   MESSFLAG,=XL6'0' SIND NOCH NACHRICHTEN OFFEN ?
         BNE   ENDE          NEIN, VERZWEIGEN
         BAL   R14,AUSMSGM   JA, VERZW. ZUR NACHRICHTENAUSGABE
         EJECT
*
*      SPEICHERPLATZFREIGABE,
*      REINITIALISIERUNG DER REGISTER UND
*      RUECKSPRUNG ZUM AUFRUFENDEN PROGRAMM.
*
         SPACE 1
ENDE     EQU   *
         XRETURN ,R
         EJECT
***********************************************************************
*                                                                     *
*      FEHLERROUTINEN.                                                *
*                                                                     *
***********************************************************************
         SPACE 3
*
*      ROUTINE BEI FEHLER IN DER PARAMETERKETTE.
*
         SPACE 1
FEHLPARM EQU   *
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG04)  NACHRICHT AUSGEBEN
         B     ENDE          VERZWEIGEN
         SPACE 2
*
*      ROUTINE BEI CVAF-FEHLERN (RC ^= 0).
*
         SPACE 1
FEHLCVAF EQU   *
         ST    R14,SAVE1R14  RUECKSPRUNG SICHERN
         LA    R10,FEHLTAB   R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG06),MSG06 NACHRICHTENZEILE UEBERTRAGEN
         MVC   MS6VOLI,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         LR    R4,R15        RETURN CODE IN R4 LADEN
         BAL   R14,CONVDEC   VERZW. ZUM UMWANDELN IN DEZIMALZAHL
         MVC   MS6RC,ZWFELD+13 RETURN CODE UEBERTRAGEN
         SR    R4,R4         CLEAR R4                              CVAF
         IC    R4,CVSTAT     CVAF STATUSCODE IN R4 LADEN           CVAF
         BAL   R14,CONVDEC   VERZW. ZUM UMWANDELN IN DEZIMALZAHL   CVAF
         MVC   MS6SC,ZWFELD+12 STATUS CODE UEBERTRAGEN             CVAF
         C     R15,=F'8'     ? IST VTOC INDEX STRUCTURE INVALID    CVAF
         BNE   CHNGRETN      NEIN, AENDERE DIE RETURNADRESSE       CVAF
         OI    KOMTFLAG,INDXERR JA, SETZE FEHLERFLAG               CVAF
         B     WTOMSG06      VERZWEIGE                             CVAF
CHNGRETN DS    0H                                                  CVAF
         MVC   SAVE1R14,=A(NEXTUCB) SUCHE DEN NAECHSTEN UCB        CVAF
WTOMSG06 DS    0H                                                  CVAF
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MESSAGE) NACHRICHTENZEILE AUSGEBEN
         L     R14,SAVE1R14  LADE RUECKSPRUNG REGISTER             CVAF
         BR    R14           VERZWEIGEN                            CVAF
         SPACE 3
*
*      ROUTINE BEI RESERVED DEVICES.
*
         SPACE 1
FEHLDEVC EQU   *
         LA    R10,FEHLTAB   R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG05),MSG05 NACHRICHTENZEILE UEBERTRAGEN
         UNPK  MS5DEVC(3),UCBTBYT4(2) DEVICE CODE UEBERTRAGEN
         TR    MS5DEVC,TABHEXA-240 DEVICE CODE AUFBEREITEN
         MVC   MS5VOLI,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MESSAGE) NACHRICHTENZEILE AUSGEBEN
         B     NEXTUCB       VERZWEIGEN
         SPACE 2
*
*      ROUTINE BEI DSCB-FEHLER (1.DSCB ^= DSCB4).
*
         SPACE 1
FEHLDSCB EQU   *
         LA    R10,FEHLTAB   R10 INITIALISIEREN
         MVC   MESSAGE(LENMSG07),MSG07 NACHRICHTENZEILE UEBERTRAGEN
         MVC   MS7VOLI,UCBVOLI VOLSER NUMBER UEBERTRAGEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MESSAGE) NACHRICHTENZEILE AUSGEBEN
         B     NEXTUCB       VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      AUSGABE DER NACHRICHTEN.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
AUSMSGM  EQU   *
         ST    R14,SAVE1R14  RUECKSPRUNGADRESSE SICHERN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MSG01)  UEBERSCHRIFTENZEILE AUSGEBEN
         LA    R10,MSGTAB-MESSLGTH ARESSE DER TABELLE LADEN
         L     R9,ADDRETB1   ENDADRESSE DER TABELLE LADEN
         BAL   R14,AUSGABE   VERZW. ZUR AUSGABE WEITERER ZEILEN
         LA    R10,MSGTAB    ADRESSE DER TABELLE LADEN
         ST    R10,SAVE1R10      UND SPEICHERN
         L     R14,SAVE1R14  RUECKSPRUNGADRESSE LADEN
         CLI   WTORBYTE,X'FF' FLAG BYTE GESETZT ?
         BNER  R14           NEIN, VERZWEIGEN
NXTREPLY EQU   *
         XC    ECB,ECB       EVENT CONTROL BLOCK LOESCHEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         LA    R4,ECB        ADRESSE DES ECB LADEN
         LA    R5,REPLY      ADRESSE DES REPLY LADEN
         MVC   WTOR,MSG08    NACHRICHTENZEILE UEBERTRAGEN
         WTOR  ,(R5),1,(R4),MF=(E,WTOR)
         SPACE 1
         WAIT  ECB=ECB       AUF REPLY WARTEN
         CLI   REPLY,C'N'    REPLY = NO ?
         BE    ENDE          JA, VERZWEIGEN
         CLI   REPLY,C'Y'    REPLY = YES ?
         BNE   NXTREPLY      NEIN, VERZWEIGEN
         L     R14,SAVE1R14  RUECKSPRUNGADRESSE LADEN
         BR    R14           VERZWEIGEN
         EJECT
*
*      AUSGABE EINZELNER NACHRICHTENZEILEN.
*
         SPACE 1
AUSGABE  EQU   *
         ST    R14,SAVE2R14  RUECKSPRUNGADRESSE SICHERN
NEXTMESS EQU   *
         LA    R10,MESSLGTH(R10) R10 ERHOEHEN
         CR    R10,R9        ENDE DER TABELLE ERREICHT ?
         BNL   ENDMESS       JA, VERZWEIGEN
         CLC   MESSFLAG,=X'FFFF' FLAG BYTES GESETZT ?
         BE    ENDMESS       JA, VERZWEIGEN
         MVC   MESSFLAG,=X'FFFF' FLAG BYTES SETZEN
         L     R0,UCMID      UCM-ID. DES AUFRUFERS LADEN
         WTO   MF=(E,MESSAGE) EINE NACHRICHTENZEILE AUSGEBEN
         B     NEXTMESS      VERZWEIGEN
ENDMESS  EQU   *
         L     R14,SAVE2R14  RUECKSPRUNGADRESSE LADEN
         BR    R14           VERZWEIGEN
         EJECT
***********************************************************************
*                                                                     *
*      UMWANDLUNG VON DUALZAHLEN IN ENTPACKTE DEZIMALZAHLEN.          *
*                                                                     *
***********************************************************************
         SPACE 3
CONVDEC  EQU   *
         CVD   R4,DBWORD     INHALT VON R4 UMWANDELN
         UNPK  ZWFELD,DBWORD DEZIMALZAHL ENTPACKEN
         OI    ZWFELD+14,C'0' VORZEICHEN MODIFIZIEREN
         BR    R14           VERZWEIGEN
         SPACE 3
***********************************************************************
*                                                                     *
*      BEFEHLE, DIE MIT HILFE DES EXECUTE-BEFEHLS AUSGEFUEHRT WERDEN. *
*                                                                     *
***********************************************************************
         SPACE 3
TESTMASK EQU   *
         TM    KOMTFLAG,X'00'    TEST AUF DIV. KOMMENTARE
MOVEPVOL EQU   *
         MVC   1(0,R6),1(R7)     VOLID IN TABELLE UEBERTRAGEN
NCVOLID1 EQU   *
         NC    ZWVOLID1(0),1(R6) VOLID UEBERLAGERN
OCVOLID2 EQU   *
         OC    ZWVOLID2(0),1(R7) VOLID UEBERLAGERN
         EJECT
***********************************************************************
*                                                                     *
*      REGISTER EQUATES.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
R0       EQU   0             MACRO- UND-PARAMETER REGISTER
R1       EQU   1             MACRO- UND-PARAMETER REGISTER
R2       EQU   2             POINTER IN CVT / UCB LOOKUP TABLE
R3       EQU   3             POINTER FUER UCB
R4       EQU   4             ARBEITSREGISTER
R5       EQU   5             ARBEITSREGISTER
R6       EQU   6             ARBEITSREGISTER
R7       EQU   7             ARBEITSREGISTER
R8       EQU   8             ARBEITSREGISTER
R9       EQU   9             ARBEITSREGISTER
R10      EQU   10            POINTER IN NACHRICHTEN-TABELLEN
R11      EQU   11            BASISREG. FUER DSECT PARMAREA / VEKTOR
R12      EQU   12            BASISREG. FUER CSECT VTOC
R13      EQU   13            BASISREG. FUER DSECT ABER
R14      EQU   14            RUECKSPRUNGADRESSEN
R15      EQU   15            CSECT-ADRESSEN UND RETURN CODES
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER KONSTANTEN-SPEICHER.                            *
*                                                                     *
***********************************************************************
         SPACE 3
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
* CVAF-PARAMETERLISTE (KONSTANTE)                                  CVAF
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
         SPACE 1
CVPL     CVAFSEQ ACCESS=GTEQ,       SEQ. ZUGRIFF ZUR VTOC          CVAF*
               BRANCH=NO,                                          CVAF*
               MF=L                                                CVAF
         SPACE 2
*
*      TABELLE FUER AUFBEREITUNG VON HEXADEZIMALZAHLEN.
*
         SPACE 1
TABHEXA  DC    C'0123456789ABCDEF'
         EJECT
*                                                                  CVAF
*      TABELLE UND EQUATES ZUR AUFBEREITUNG DER KOMMENTARZEILE     CVAF
*      SOLLTEN  WEITERE KOMMENTARE GEWUENSCHT WERDEN SO IST        CVAF
*      FOLGENDES ZU TUN:                                           CVAF
*              1. AUSFUELLEN EINES  RESERVEFELDES                  CVAF
*              2. FLAG DEFINIEREN  (EQUATE)                        CVAF
*              3. WENN BEDINGUNG ERFUELLT, FLAG IN 'KOMTFLAG' SETZEN
*                                                                  CVAF
         SPACE 1                                                   CVAF
KOMTAB   DS    0H                                                  CVAF
         DC    C'****'             RESERVE
         DC    C'****'             RESERVE
         DC    C'****'             RESERVE
         DC    C'****'             RESERVE
         DC    C'XERR'             INDEX IN ERROR                  CVAF
         DC    C'XVTC'             INDEXED VTOC                    CVAF
         DC    C'ALTK'             ALTERNATE TRACKS USED           CVAF
         DC    C'VSAM'             VSAM DS ON VOLUME               CVAF
RESERV1  EQU   X'01'                                               CVAF
RESERV2  EQU   X'02'                                               CVAF
RESERV4  EQU   X'04'                                               CVAF
RESERV8  EQU   X'08'                                               CVAF
INDXERR  EQU   X'10'                                               CVAF
INDXVTOC EQU   X'20'                                               CVAF
ALTRKS   EQU   X'40'                                               CVAF
VSAMVOL  EQU   X'80'                                               CVAF
         EJECT
*
*      TRACKS PRO CYLINDER UND ALTERNATE TRACKS JE VOLUME.
*
         SPACE 1
TABTCAP  DS     0H
         DC     X'80',XL3'0'  00 - RESERVED
         DC     X'80',XL3'0'  01 - 2311    DISK STORAGE DEVICE
         DC     X'80',XL3'0'  02 - 2301    PARALLEL DRUM
         DC     X'80',XL3'0'  03 - 2303    SERIAL DRUM
         DC     X'80',XL3'0'  04 - 2302    DISK STORAGE
         DC     X'80',XL3'0'  05 - 2321    DATA CELL DRIVE
         DC     H'08',H'000'  06 - 2305/1  FIXED HEAD STORAGE / MODEL 1
         DC     H'08',H'000'  07 - 2305/2  FIXED HEAD STORAGE / MODEL 2
         DC     X'80',XL3'0'  08 - 2314    DISK STORAGE FACILITY
         DC     H'19',H'133'  09 - 3330/1  DISK STORAGE / MODEL 1
         DC     H'12',H'024'  0A - 3340    DISK STORAGE
         DC     H'30',H'150'  0B - 3350    DISK STORAGE
         DC     X'80',XL3'0'  0C - RESERVED
         DC     H'19',H'133'  0D - 3330/11 DISK STORAGE / MODEL 11
         DC     H'15',H'015'  0E - 3380    DISK STORAGE
         DC     X'80',XL3'0'  0F - RESERVED
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER AUSGABENACHRICHTEN.                             *
*      ==================================                             *
*                                                                     *
*      BEMERKUNG:                                                     *
*      ==========                                                     *
*      DIE MAXIMALEN ZEICHEN JE AUSGABEZEILE SOLLTE DIE ANZAHL 61     *
*      NICHT UEBERSCHREITEN (TRAILING BLANKS ODER HEXNULLEN ZAEHLEN   *
*      NICHT MIT).                                                    *
*      BEGRUENDUNG: NUR SO IST GEWAEHRLEISTET, DASS AUCH AUF EINEM    *
*      JES3-TERMINAL JEDE NACHRICHT NUR EINE ZEILE BEANSPRUCHT UND    *
*      DIESE NICHT AUF 2 ZEILEN AUFGETEILT WIRD.                      *
*                                                                     *
***********************************************************************
         SPACE 3
MSG01    WTO   'XVTOC01 VOLSER ADR CCCHH #DSCB #USED --------REMARKS---*
               ------',                                                *
               MF=L,MCSFLAG=(REG0)
LENMSG01 EQU   *-MSG01
         SPACE 1
MSG02    WTO   'XVTOC02 VOLSER ADR 00000 ***** *****                   *
                     ',                                                *
               MF=L,MCSFLAG=(REG0)
LENMSG02 EQU   *-MSG02
         SPACE 1
MSG03    WTO   'XVTOC03 SPECIFIED VOLUME(S) NOT FOUND',                *
               MF=L,MCSFLAG=(REG0)
         SPACE 1
MSG04    WTO   'XVTOC04 INVALID PARAMETER LIST',                       *
               MF=L,MCSFLAG=(REG0)
         EJECT
MSG05    WTO   'XVTOC05 VOLSER=123456, DEVICE FAILURE, DEVCODE=99',    *
               MF=L,MCSFLAG=(REG0)
LENMSG05 EQU   *-MSG05
         SPACE 1
MSG06    WTO   'XVTOC06 VOLSER=123456, CVAF FAILURE, RC=99, STATUS=999'*
               ,MF=L,MCSFLAG=(REG0)
LENMSG06 EQU   *-MSG06
         SPACE 1
MSG07    WTO   'XVTOC07 VOLSER=123456, 1. DSCB NOT FORMAT 4',          *
               MF=L,MCSFLAG=(REG0)
LENMSG07 EQU   *-MSG07
         SPACE 1
MSG08    WTOR  'XVTOC08 REPLY ''Y'' (YES) OR ''N'' (NO) TO CONTINUE',  *
               MF=L,MCSFLAG=(REG0)
LENMSG08 EQU   *-MSG08
         SPACE 2                                                   CVAF
BIGMSG   EQU   (LENMSG02/2*2+2) GROESST MOEGLICHE MSG AUSSER MSG08 CVAF
*                               (SOLLTE DURCH 2 TEILBAR SEIN)      CVAF
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITION DER ARBEITSBEREICHE.                                *
*                                                                     *
***********************************************************************
         SPACE 3
ABER     DSECT
SAVEAREA DS    18F           SAVEAREA FUER ALLGEMEINE REGISTER
         SPACE 2
*
*      ADRESSEN-SPEICHER.
*
         SPACE 1
ADDRETB1 DS    F             ENDADRESSE DER MSG-NACHRICHTEN-TABELLE
ADDRETPV DS    F             ENDADRESSE DER VOLSER-TABELLE
SAVE1R10 DS    F             SAVEAREA1 FUER R10
SAVE1R14 DS    F             SAVEAREA1 FUER R14
SAVE2R14 DS    F             SAVEAREA2 FUER R14
UCBADDR  DS    F             ADRESSE DES UCB-VEKTORS
UCMID    DS    F             UCM-ID. DES AUFRUFERS
         SPACE 2
*
*      ARBEITSSPEICHER.
*
         SPACE 1
KOMTFLAG DS    X             FLAGBYTE FUER KOMMENTARZEILE          CVAF
CVAFRC   DS    F             CVAF  RETURNCODE                      CVAF
CVAFST   DS    F             CVAF  STATUSCODE                      CVAF
CVAFFMT0 DS    F             CVAF  ANZAHL FORMAT 0 DSCB'S          CVAF
DBWORD   DS    D             ZWISCHENSPEICHER
ZWFELD   DS    CL15          ZWISCHENSPEICHER FUER DEZIMALZAHLEN
FNDBYTE  DS    C             SCHALTER FUER GEFUNDENE VOLUMES
WTORBYTE DS    C             SCHALTER FUER WTOR
REPLY    DS    C             SPEICHER FUER REPLY
VOLSERNO DS    CL6           ZWISCHENSPEICHER FUER VOLSER NUMBER
ZWVOLID1 DS    CL6           ZWISCHENSPEICHER FUER VOLSER NUMBER
ZWVOLID2 DS    CL6           ZWISCHENSPEICHER FUER VOLSER NUMBER
TRKPCYL  DS    F             TRACKS PRO CYLINDER
NOALTRK  DS    F             ANZAHL DER ALTERNATE TRACKS PRO UNIT
NODSCBS  DS    F             HIGH WATER MARK FUER ANZ. DER DSCBS
ECB      DS    F             EVENT CONTROL BLOCK
WTOR     DS    CL(LENMSG08)  BEREICH FUER WTOR-NACHRICHT
MRECNO   DS    H             MAX. SATZADRESSE
MTRKNO   DS    H             MAX. SPURADRESSE
         EJECT
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
* SYMBOLISCHE AUFLOESUNG DER CVAF-PARAMETERLISTE.                  CVAF
* PLATZRESERVIERUNG IM SUBPOOL 1 (R13) FUER PARMLIST U. BUFFERLIST CVAF
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
CVPLMAP  ICVAFPL DSECT=NO                                          CVAF
         ICVAFBFL DSECT=NO                                         CVAF
BFLEEND  DS    0D
         ORG   BFLEARG                                             CVAF
CCHHR    DS    CL5            UEBERLAGERUNG VON BFLEARG            CVAF
         ORG   BFLEEND                                             CVAF
DSCBBUF  DS    CL140          DSCB BUFFER                          CVAF
DSCBEND  DS    0H                                                  CVAF
         SPACE 2
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*
*      DSCB4-DEFINITIONEN.
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
         SPACE 1
         ORG    DSCBBUF                                            CVAF
DSCB4DSN DS     CL44                                               CVAF
         IECSDSL1 (4)                                              CVAF
DSCB4LTH EQU    DS4END-DSCB4DSN                                    CVAF
         EJECT
*
*      MASKEN-TABELLEN FUER VOLSER NUMBERS AUS DEM PARAMETERBEREICH.
*
*      AUFBAU DER TABELLE:
*      BYTE1 - LAENGE DES ANGEGEBENEN VOLSER-PARAMETERS ODER
*          X'80' BEI TABELLENENDE
*      BYTES2-7 - ANFANGSZEICHEN DER VOLSER NUMBER UND MASKENZEICHEN
*
         SPACE 1
TABPVOL1 DS    10CL7         TABELLE MIT MASKENZEICHEN X'FF'
ENDTPVOL EQU   *
         DS    CL7
TABPVOL2 DS    CL77          TABELLE MIT MASKENZEICHEN X'00'
         SPACE 2
*
*      NACHRICHTEN-BEREICHE.
*
*      AUFBAU:
*      BYTE 1-BIGMSG: WTO-NACHRICHTENZEILE IN DER LAENGE DER
*                     GROESSTEN MESSAGE AUSSER MSG08
*      BYTE BIGMSG+1(2): X'0000', WENN NACHRICHTENZEILE VERFUEGBAR
*
         SPACE 1
         DS    0F
MSGTAB   DS    10CL(BIGMSG+2)   TABELLE FUER NACHRICHTEN
ENDTAB1  EQU   *
FEHLTAB  DS    CL(BIGMSG+2)     TABELLE FUER FEHLERNACHRICHTEN
         SPACE 1
ABERL    EQU   *-ABER           LAENGE DER ARBEITSBEREICHE
         EJECT
***********************************************************************
*                                                                     *
*      DEFINITIONEN FUER EINE NACHRICHTENZEILE.                       *
*                                                                     *
***********************************************************************
         SPACE 3
MESSAGE  DSECT
         SPACE 2
*
*      DEFINITIONEN FUER MSG02.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL8
MS2VOLID DS    CL6           VOLUME SERIAL NUMBER
         DS    C
MS2ADR   DS    CL3           UNIT ADDRESS
         DS    C
MS2CCCHH DS    CL5           CCCHH-ADRESSE DER VTOC
         DS    C
         ORG   MS2CCCHH
M2CCCHHR DS    CL6           CCCHHR-ADRESSE DER VTOC
MS2DSCB  DS    CL5           # AVAILABLE DSCBS
         DS    C
MS2USED  DS    CL5           # USED DSCBS
         DS    C
MS2REST  EQU   *-MESSAGE
MS2COMM  DS    CL((LENMSG02-MS2REST+1)/5*5-1) DIE LAENGE DES KOMMENTARS
*              MUSS BEI LAENGE + 1 DURCH 5 TEILBAR SEIN
         EJECT
*
*      DEFINITIONEN FUER MSG05.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL15
MS5VOLI  DS    CL6           VOLUME SERIAL NUMBER
         DS    CL26
MS5DEVC  DS    CL2           DEVICE CODE
         SPACE 2
*
*      DEFINITIONEN FUER MSG06.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL15
MS6VOLI  DS    CL6           VOLUME SERIAL NUMBER
         DS    CL19
MS6RC    DS    CL2           RETURN CODE
MS6RCCOM DS    CL1
         DS    CL8
MS6SC    DS    CL3           STATUS CODE
         SPACE 2
*
*      DEFINITIONEN FUER MSG07.
*
         SPACE 1
         ORG   MESSAGE+4
         DS    CL15
MS7VOLI  DS    CL6
         SPACE 2
         ORG   MESSAGE+BIGMSG
MESSFLAG DS    CL2           FLAG BYTES
MESSLGTH EQU   *-MESSAGE                                           CVAF
         EJECT
***********************************************************************
*                                                                     *
*      UCB-DEFINITIONEN.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
UCB      DSECT
          IEFUCBOB                                                 CVAF
         EJECT
***********************************************************************
*                                                                     *
*      PARAMETERBEREICH.                                              *
*                                                                     *
***********************************************************************
         SPACE 3
         XPARM
PARMENDE EQU   *
         SPACE 3
***********************************************************************
*                                                                     *
*      VEKTOR MIT UCB-ADRESSEN.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
VEKTOR   DSECT
         DS    1024H
VEKEND   DS    H             =X'FFFF'
VEKLEN   EQU   *-VEKTOR
         CVT   DSECT=YES                                          /XUCB
         EJECT
         END   VTOC
