* STAND 05/20/81
***********************************************************************
*                                                                     *
*    PROGRAMM   = DRUABST                                             *
*                                                                     *
*    ENTRYPOINT = DRUFEHL                                             *
*                 -------                                             *
*    AUFRUF    = CALL DRUFEHL,(ERR,KARTE),VL      ODER                *
*                CALL DRUFEHL,(ERR),VL                                *
*        CL80     KARTE = FEHLERHAFTE ABSTIMMKARTE                    *
*        F        ERR   = FEHLERTYP                                   *
*        F                 BEIM RUECKSPRUNG = 0 FEHLERMELDUNG GEDRUCKT*
*                                             1 DDKARTE FUER ASTBLATT *
*                                               FEHLT                 *
*                                                                     *
*    ZWECK      = FEHLERHAFTE ABSTIMMKARTEN MIT FEHLERMELDUNG DRUCKEN *
*                                                                     *
*    ENTRYPOINT = DRUALIST                                            *
*                 --------                                            *
*    AUFRUF     = CALL DRUALIST,(ERR)                                 *
*        F        ERR   = 0   ABSTIMMLISTE GEDRUCKT                   *
*                       = 1   NICHT GEDRUCKT, DA ASTBLATT-DDKARTE FEHLT
*                                                                     *
*    ZWECK      = ABSTIMMLISTE DRUCKEN                                *
*                 DIE ZU DRUCKENDEN DATEN WERDEN DEN BEREICHEN        *
*                 KTAB UND STAB ENTNOMMEN                             *
*                                                                     *
*
*  ENTRYPOINT  DRUPARM
*
*  AUFRUF                     CALL DRUPARM,(ERR)
*
*              ERR SIEHE DRUALIST
*  ZWECK       INHALT DER DATEI MIT DDNAMEN PARMKART
*              AUSZUGSWEISE LISTEN.
*              ES WERDEN DIE KARTEN SP.1-3=VAR BZW. DAT GELISTET.
*              WIRD IN SP.1-3 EIN ANDERER INHALT GEFUNDEN,
*              WIRD DAS LISTEN DER DATEI ABGEBROCHEN.
***********************************************************************
         PRINT NOGEN
         SPACE 2
DRUFEHL  CSECT
         SPACE 1
         AREG
RA       EQU   R7                       ATAB
         SR    R0,R0          ENTRY DRUFEHL
         B     ANFANG-DRUFEHL(0,15)
         SPACE 2
*        E N T R Y P O I N T  D R U P A R M
*        ---------------------------------
*
         ENTRY DRUPARM
DRUPARM  EQU   *
         LA    R0,X'00FF'     ENTRY DRUPARM
         B     ANFANG-DRUPARM(0,15)
*              E N T R Y P O I N T   D R U A L I S T
*              -------------------------------------
*
         ENTRY DRUALIST
DRUALIST EQU   *
         LA    R0,1                     1 = ENTRY UEBER DRUALIST
*
ANFANG   EQU   *
         BALR  R15,0          REG 15 AUF ANFANG+2 SETZEN
         XSAVE R12,SAVE,DRUALIST
         STC   R0,DWORD
         XR    R10,R10                  --> FEHLERHAFTE KARTE = 0
         TM    0(R1),X'80'
         BO    PARM1                    KEINE KARTE ANDRUCKEN
         L     R10,4(R1)                --> FEHLERHAFTE KARTE
PARM1    EQU   *
         L     R11,0(R1)                --> FEHLERCODE
         ST    R11,ERRADR               --> FEHLERCODE ZWISCHENSPEICH.
         L     RA,PATAB                 --> ATAB
         USING DATAB,RA
         SPACE 2
*
*  OPEN DCB ABSTIMMBLATT
*
         CALL  OPNABLT
         B     *+4(R15)
         B     DCBOK          DCB OPEN
         B     DCB1           1.AUFRUF
         B     RETERR         DDKARTE FEHLT
*
DCB1     EQU   *
         ZAP   SEIZAE,=P'0'
         ZAP   ZEIZAE,=P'99'            ZEILENZAEHLER > 1 SEITE
         LA    R5,UHRZEIT
         LA    R4,DATUM
         CALL  TDATE,((R5),(R4))        UHRZEIT, DATUM
*  DCB OPENED
DCBOK    EQU   *
         TM    ALCOPY,RYES
         BNO   A1             NICHT LISTENKOPIE
         ZAP   ZEIZAE,=P'99'  AM ANFANG DER
         ZAP   SEIZAE,=P'0'   LISTENKOPIE
A1       EQU   *
         TM    DWORD,X'FF'              ENTRYKENNUNG PRUEFEN
         BM    ANFLIST                  ENTRY DRUALIST
         BO    DPARM01        ENTRY DRUPARM
*  FEHLERMELDUNG DRUCKEN
         MVI   ZEILE,C'0'              VORSCHUB
         LTR   R10,R10
         BZ    PUTFEHL
         MVC   ZEILE+1(80),0(R10)
         CALL ABLTPUT,MF=(E,ABLTPUTL)
PUTFEHL  EQU   *
         L     R11,ERRADR
         L     R11,0(R11)               FEHLERCODE
         C     R11,=F'15'
         BE    WRENDE                  KEIN FHLTXT DRUCKEN
         BCTR  R11,0                    FEHLERCODE -1
         LR    R9,R11
         SLA   R9,5                     * 32
         L     R11,TXTADR              CSECT FEHLERTEXTE
         AR    R11,R9                  --> FEHLERTEXT (FEHLERCODE)
         MVC   ZEILE+1(32),0(R11)      FEHLERTEXT
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         B     WRENDE
         EJECT
*        INHALT PARMKART-DATEI LISTEN
*        ---------------------------
*
DD       DC    CL8'PARMKART'
TDD1     DS    H
TDD2     DS    H
*
*  DCB PARMKART SUCHEN
*
DPARM01  EQU   *
         SR    R0,R0
         L     R1,16          -->CVT
         L     R1,0(,R1)
         L     R1,4(,R1)      -->TCB
         L     R2,12(R1)      -->TIOT
         ST    R2,DWORD
         LA    R2,24(R2)      FIRST TIOT ENTRY
DPARM012 EQU   *
         CLC   4(8,R2),DD     DDNAME
         BE    DPARM014       PARMKART GEFUNDEN
         IC    R0,0(R2)       LAENGE TIOT ENTRY
         LTR   R0,R0
         BZ    WRENDE         PARMKART FEHLT
         AR    R2,R0          NEXT TIOT ENTRY
         B     DPARM012
*
*  TIOT OFFSETS PARMKART MERKEN
*
DPARM014 EQU   *
         LR    R3,R2
         S     R3,DWORD       -->TIOT
         STH   R3,TDD1        1.TIOTOFFSET
DPARM016 EQU   *
         LR    R3,R2
         S     R3,DWORD       -->TIOT
         STH   R3,TDD2        LETZTES TIOT OFFSET
         IC    R0,0(R2)       LAENGE TIOT ENTRY
         LTR   R0,R0
         BZ    DPARM018
         AR    R2,R0          NEXT TIOT ENTRY
         CLI   4(R2),C' '
         BE    DPARM016       CONCATENATION
DPARM018 EQU   *
         L     R1,8(,R1)      -->DEB
         LTR   R1,R1
         BZ    DPARM08        KEIN DEB VORHANDEN
DPARM02  EQU   *              SCHLEIFE UEBER DEB
         L     R2,24(,R1)     -->DCB
         CLC   40(2,R2),TDD1  1.OFFSET PARMKART
         BL    DPARM04        VOR PARMKART
         CLC   40(2,R2),TDD2  LETZTES OFFSET PARMKART
         BNH   DPARM05        PARMKART DEB D.H. OPEN
*
DPARM04  EQU   *
         L     R1,4(,R1)      -->NEXT DEB
         LA    R1,0(,R1)
         LTR   R1,R1
         BNZ   DPARM02        WEITER SUCHEN
         B     DPARM08        DEB NICHT GEFUNDEN
*
DPARM05  EQU   *              PARMKART OPEN
         MVC   ZEILE(19),=C'-DDNAME = PARMKART :'
         MVC   ZEILE+20(19),=C'NICHT ABGESCHLOSSEN'
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         B     WRENDE
DPARM08  EQU   *
         OPEN  (PARMKART,(INPUT))
         TM    PARMKART+48,X'10'
         BZ    WRENDE         DDNAME PARMKART FEHLT
*
         CLI   PARMKART+17,X'4F'  PARMKART NICHT LISTEN,WENN
         BE    DPARM90            UNIT=TERMINAL
*
DPARM10  EQU   *
         LA    R3,10          MINDESTENS 10 KARTEN LISTEN
         MVC   ZEILE(20),=C'-DDNAME = PARMKART :'
         MVC   ZEILE+20(10),=C' (VAR,DAT)'
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         MVI   ZEILE,C' '    VORSCHUB
*
DPARM20  EQU   *
         GET   PARMKART,ZEILE+1
         CLC   ZEILE+1(3),=C'VAR'
         BE    DPARM30        LISTEN
         CLC   ZEILE+1(3),=C'DAT'
         BNE   DPARM20        NICHT LISTEN
*
DPARM30  EQU   *
         CP    ZEIZAE,=P'59'
         BL    DPARM40        SEITE NICHT VOLL
         LTR   R3,R3
         BNP   DPARM90        MINDESTENS 10 KARTEN GELISTET
DPARM40  EQU   *
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         SH    R3,=H'1'       KARTENZAEHLER
         B     DPARM20
*
DPARM90  EQU   *              PARMKART LISTEN BEENDET
         CLOSE (PARMKART)
         B     WRENDE
         EJECT
*              A B S T I M M L I S T E  D R U C K E N
*              --------------------------------------
*
ANFLIST  EQU   *
RK       EQU   R8                       KTAB
RS       EQU   R9                       STAB
RSE      EQU   R10                      SETAB
         SPACE 1
         USING DKTAB,RK
         USING DSTAB,RS
         USING DSETAB,RSE
         L     RK,PKTAB
         SR    RS,RS
         L     R3,ATXTP
         LTR   R3,R3          ATXTP=0 -->
         BZ    GENERG0        KEINE VAR.TEXTE
         SPACE 1
         CLC   2(2,R3),=H'0'     ANZ TEXTE UNGLEICH BLANK
         BE    WRTEXTE        NUR *-ZEILE DRUCKEN
         SPACE 1
         LH    R4,0(R3)       ANZ TEXTE
         LA    R3,8(R3)       -->TEXT
*
*  VAR.TEXTE DRUCKEN (MIT ASTTXT UEBERGEBEN)
*
         BAL   R11,LOESCH     DRUCKBEREICH LOESCHEN
WRTEXTL  EQU   *
         SPACE 1
         LA    R5,49          TEXTLAENGE-1
         MVI   ZEILE,C'0'     VORSCHUB
* AUF ASA-VORSCHUB IN TEXTEN PRUEFEN
         CLI   0(R3),C'+'
         BE    WRTXT1
         CLI   0(R3),C' '
         BE    WRTXT1
         CLI   0(R3),C'0'
         BE    WRTXT1
         CLI   0(R3),C'-'
         BNE   WRTEXT2
         SPACE 1
*  ASA-VORSCHUB AUS TEXT UEBERNEHMEN
WRTXT1   EQU   *
         MVC   ZEILE(1),0(R3) VORSCHUB
         BCTR  R5,0           TEXTLAENGE - VORSCHUB
         LA    R3,1(R3)       -->TEXT NACH VORSCHUB
         SPACE 1
WRTEXT2  EQU   *
         EX    R5,MVCTXT
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         BAL   R11,LOESCH
         BCT   R4,WRTEXT3     ANZ TEXT AUF NULL ZAEHLEN
         B     WRTEXTE        ALLE TEXTE GEDRUCKT
         SPACE 1
WRTEXT3  EQU   *
         LA    R3,1(R3,R5)
         B     WRTEXTL        SCHLEIFENANFANG
         B     WRTEXTL
*
WRTEXTE  EQU   *
         MVI   ZEILE,C'0'
         MVI   ZEILE+1,C'*'
         MVC   ZEILE+2(79),ZEILE+1  STERN-ZEILE
         SPACE 1
*  CONDITIONCODE ALS KOMMENTAR IN *-ZEILE EINSETZEN
         CLC   ATXTKZ,=H'0'
         BE    WRTXTE5        KEIN CONDCODE
         MVC   ZEILE+23(36),CONDTEXT
WRTXTE5  EQU   *
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         B     GENERG0
         SPACE 3
MVCTXT   MVC   ZEILE+10(0),0(R3)  VAR.TEXTE
CONDTEXT DC    CL36' ASTTXT SETZT CONDITION CODE = 0016 '
         SPACE 2
*  GENERELLES ABSTIMMERGEBNIS DRUCKEN
*
GENERG0  EQU   *
         TM    AERGI,SAODER   ODER-BEDINGUNG
         BNZ   TEERG0         JA
         TM    AERGE,SAODER
         BZ    TEERG1         NEIN
TEERG0   EQU   *              HINWEIS AUF ODER DRUCKEN
         MVI   ZEILE,C'0'     VORSCHUB
         MVC   ZEILE+10(34),=CL34'A C H T U N G  ODER-BEDINGUNG :'
         MVC   ZEILE+45(35),=CL35'EINGABE UND ABSTIMMUNG UEBERPRUEFEN'
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
TEERG1   EQU   *
         CLI   AERGI,SANO
         BE    TEERG                   KEINE INT.ABST.
         LA    R3,AERGI
         MVC   ZEILE+49(13),=C'I N T E R N E'
         BAL   R11,GENERG              DRUCKZEILE AUFBEREITEN
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
TEERG    EQU   *
         CLI   AERGE,SANO
         BE    PACCT
         LA    R3,AERGE
         MVC   ZEILE+49(13),=C'E X T E R N E'
         BAL   R11,GENERG              DRUCKZEILE AUFBEREITEN
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
PACCT    EQU   *              ERGEBNIS ACCOUNTING ANALYSE DRUCKEN
         MVI   ZEILE,C'0'     VORSCHUB
         CLI   ACCT,3
         BH    PACCT9         KEIN TEXT DRUCKEN
         SR    R1,R1
         IC    R1,ACCT
         SLA   R1,5
         A     R1,PTXTUPDT
         MVC   ZEILE+49(32),0(R1)  TEXT --> DRUCKBEREICH
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
PACCT9   EQU   *
         B     WRLOOP1
*
GENERG   EQU   *
         MVI   ZEILE,C'0'
         MVC   ZEILE+63(10),=C'ABSTIMMUNG'
         TM    0(R3),SAFALSE+SAIMPOS
         BNZ   GENE1
         MVC   ZEILE+74(07),=C'RICHTIG'
         B     GENERGE
GENE1    EQU   *
         MVC   ZEILE+74(06),=C'FALSCH'
         SPACE 2
GENERGE  EQU   *
         BR    R11
*        A B S T I M M S U M M E N  D R U C K E N
*        ----------------------------------------
*
WRLOOP1  EQU   *              SCHLEIFE UEBER KTAB-EINTRAGUNGEN
         CLC   KNEXT,=F'-1'
         BE    WRLOOP1X                 ENDE KTAB = 1.DURCHLAUF BEENDET
         BAL   R11,LOESCH               DRUCKBEREICH LOESCHEN
NOUEB    EQU   *
         CLI   KNEXT,KFILE
         BE    WRFILE                   FILEZEILE DRUCKEN
         CLI   KNEXT,KCOMMENT
         BE    WRCOM                    KOMMENTARZEILE DRUCKEN
         CLI   KNEXT,KSUM
         BE    WRSUM1                   SUMMENZEILE DRUCKEN
         SPACE 2
         SPACE 2
WRLOOP1E EQU   *              POINTER AUF NAECHSTE KTAB-
         L     RK,KNEXT       EINTRAGUNG SETZEN
         B     WRLOOP1
         SPACE 2
*  ABSTIMMSUMMEN GEDRUCKT
*
WRLOOP1X EQU   *
         TM    ALCOPY,RYES    LISTENKOPIE
         BO    WRENDE         JA --> VOLLSTAENDIG
         CALL  ASTFMIS        DATEINAMEN DRUCKEN
         B     WRLOOP2A       ABST.ERGEBNIS DRUCKEN
*  FILEKARTE DRUCKEN
WRFILE   EQU   *
         LA    R3,TFDDN       -->DDNAME
         CALL  ABLTFILE,((R3)),VL
         B     WRLOOP1E
         SPACE 2
*  KOMMENTAR DRUCKEN
WRCOM    EQU   *
         MVC   ZEILE(63),TKOM           KOMMENTAR + VORSCHUBZEICHEN
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
         B     WRLOOP1E
         SPACE 2
*  ABSTIMMSUMMEN DRUCKEN
WRSUM1   EQU   *
         MVC   ZEILE(34),TSBEZ          VOR,NR,TEXT
         MVC   ZEILE+57(24),TSMIT       ABST.ERG.,ABZUST.MIT
         C     RS,TSTAB                 VERGLEICHT ALTE/NEUE -->STAB
         BE    WRSUM12                  GLEICHE STAB D.H.FOLGEKARTE
         SPACE 1
         L     RS,TSTAB                 --> ZUGEHOERIGE STAB
         LTR   RS,RS
         BZ    WRSUM12                  -->STAB = 0 D.H. KEINE ZUG.STAB
         LH    R3,SDEZ                  ANZAHL D.DEZIMALSTELLEN
         LA    R3,1(R3)
         LA    R4,MASK0                 ADRESSE DER PASSENDEN
         B     WRSUM11                  EDITMASKEN SUCHEN
         LA    R4,22(R4)
WRSUM11  EQU   *
         BCT   R3,*-4
         MVC   ZEILE+35(22),0(R4)       MASKE --> DRUCKBEREICH
         LH    R3,SLENG                 LAENGE D.ABSTIMMSUMME
         BCTR  R3,0                     LAENGE - 1
         L     R2,SADR                  -->ABSTIMMSUMME
         CLI   ASPRACHE,FORTRAN
         BNE   WRSUM114
         L     R1,0(,R2)               ABST.SUM BINAER
         CVD   R1,DWORD                ABST.SUM GEPACKED
         B     WRSUM116
WRSUM114 EQU   *
         EX    R3,UPZAP                 ABST.SUMME --> DWORD
WRSUM116 EQU   *
         ED    ZEILE+35(22),DWORD       DWORD       --> DRUCKBEREICH
         SPACE 1
WRSUM12  EQU   *         ZEILE AUFBEREITET
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
         B     WRLOOP1E
         SPACE 5
*        A B S T I M M E R G E B N I S  D R U C K E N
*        --------------------------------------------
WRLOOP2A EQU   *
         L     RK,PKTAB                 -->1.KTAB-EINTRAGUNG
         MVI   KENN,X'01'               KENNUNG 1.ZEILE
         SPACE 1
WRLOOP2  EQU   *              SCHLEIFE UEBER KTAB-EINTRAGUNGEN
         CLI   KNEXT,KSUM               NUR DIE SUMMENKARTEN SIND
         BE    WRSUM2                   INTERESSANT FUER DIE LISTE
         CLI   KNEXT,C'V'
         BE    WRSUM2         V-KARTE ENTSPRICHT S-KARTE
         CLC   KNEXT,=F'-1'
         BNE   WRLOOP2E                 NICHT KTAB-ENDE
         TM    KENN,X'01'               KENNUNG 1.SUMME
         BO    WRENDE                   JA -->PGM-ENDE
         BAL   R11,WRVER                VERGLEICHSSUMME DRUCKEN
         B     WRENDE
         SPACE 1
WRLOOP2E EQU   *              POINTER AUF NAECHSTE KTAB-
         L     RK,KNEXT       EINTRAGUNG SETZEN
         B     WRLOOP2
         SPACE 2
WRSUM2   EQU   *
         CLC   TSETAB,=F'0'
         BE    WRLOOP2E                 KEINE ZUGEHOERIGE SETAB
NOUEB2   EQU   *
         TM    KENN,X'01'               1.ZEILE
         BO    WRSUM28                  JA
         C     RS,TSTAB                 NEUE STAB
         BE    WRSUM245                 NEIN
         SPACE 1
*  VERGLEICHSSUMME DER LETZTEN SUMME DRUCKEN
WRSUM21  EQU   *
         BAL   R11,WRVER                VERGLEICHSSUMME DRUCKEN
         SPACE 1
*  DATEN AUS 1.S-KARTE ==> DRUCKBEREICH
WRSUM22  EQU   *
         TM    KENN,16
         BO    WRSUM224
*  1.DURCHLAUF
WRSUM222 EQU   *
         ST    RK,KMERK
         OI    KENN,16
         B     WRSUM225
WRSUM224 EQU   *
         NI    KENN,X'FF'-16  LOESCHEN
         TM    SSUMDA,SAVAR
         BZ    WRSUM222
         TM    SAERG,SAOK
         BO    WRSUM222
         L     RK,KMERK       2.DURCHLAUF AUFSETZEN
WRSUM225 EQU   *
         LA    R6,1                     ZAEHLER ODER-BEDINGUNG
         MVI   ZEILE,C'0'               VORSCHUBZEICHEN
         L     RS,TSTAB                 --> ZUGEH.STAB
         ZAP   SVERSUM,=P'0'
         OI    KENN,2                   KENN = 1.ZEILE D. SUMME
         TM    KENN,16
         BZ    WRSUM25        2.DURCHLAUF
         SPACE 1
*                             TEXT DES ABSTIMMERG --> DRUCKBEREICH
         TM    SSUMDA,SAVAR
         BZ    WRSUM228
         MVC   ZEILE+23(3),=CL3'VAR'
WRSUM228 EQU   *
         MVC   ZEILE+1(2),SPOS          POSITIONS-NR.
         MVC   ZEILE+04(10),=C'ABSTIMMUNG'
         CLI   SAERG,SAOK               TEXT DES ABSTIMMERGEBNISSES
         BE    WRSUM23                  IN DRUCKBEREICH
         CLI   SAERG,SAFALSE
         BE    WRSUM24
         MVC   ZEILE+11(11),=C'SUMME FEHLT'
         B     WRSUM25
WRSUM23  EQU   *
         MVC   ZEILE+15(07),=C'RICHTIG'
         B     WRSUM25                  ZEILE FERTIG AUFBEREITET
WRSUM24  EQU   *
         MVC   ZEILE+15(06),=C'FALSCH'
         B     WRSUM25
         SPACE 1
*  FOLGEKARTE DER SUMME
WRSUM245 EQU   *
         L     RSE,TSETAB
         CLI   SEOP,1                   ODER ?
         BNE   WRSUM25                  KEIN NEUES ODER
         LA    R6,1(R6)                 NAECHSTE ODER-BEDINGUNG
         BAL   R11,WRVER                VERGLEICHSSUMME DRUCKEN
         SPACE 1
*  PRUEFUNG, OB SUMMEN AUSFUEHRLICH ZU DRUCKEN SIND
WRSUM25  EQU   *
         CLI   SAERG,SAOK
         BE    WRSUM252                 ABSTIMMUNG RICHTIG
         OI    KENN,4                   KENN = VERSUM.DRUCKEN
         B     WRSUM26
WRSUM252 EQU   *
         TM    SSUMDA,SAVAR
         BO    WRSUM253       VARIABLE SUMMEN LISTEN
         CLI   SAVERNR,0
         BNE   WRSUM254                 ODER-BEDINGUNG
         TM    KENN,2
         BO    WRSUM27                  1.ZEILE D.SUMME
         B     WRLOOP2E
*  VARIABLE SUMMEN
WRSUM253 EQU   *
         OI    KENN,8+4       VARIABLE SUMMEN LISTEN
         B     WRSUM26
*  ODER
WRSUM254 EQU   *
         CLM   R6,1,SAVERNR             RICHTIGE ODER-BEDINGUNG ?
         BNE   WRLOOP2E                 NEIN
         OI    KENN,4                   KENN = VERSUM.DRUCKEN
         SPACE 2
*  VERGLEICHSPOSITION DRUCKEN
WRSUM26  EQU   *
         L     RSE,TSETAB               --> ZUGEH. SETAB
         SPACE 1
*  BEI 1.DURCHLAUF NUR SADO-SUMMEN DRUCKEN
*  BEI 2.DURCHLAUF ALLE SUMMEN DRUCKEN
         TM    KENN,16
         BO    WRSUM261       1.DURCHLAUF
         NI    KENN,X'FF'-4   VERGLEICHSSUMME NICHT DRUCHEN
         TM    SEKZ,SEDO
         BZ    WRSUM262
         MVI   ZEILE+28,C'S'
         B     WRSUM262
WRSUM261 EQU   *  1.DUCHLAUF
         TM    SEKZ,SEDO
         BZ    WRLOOP2E
         SPACE 1
WRSUM262 EQU   *
         CLI   SESUMDA,SAOK             ABZUST.MIT SUMME VORHANDEN
         BE    WRSUM264                 JA
         MVC   ZEILE+52(3),=C'???'      SUMME NICHT VORHANDEN
         B     WRSUM265
WRSUM264 EQU   *
         LA    R3,SESUM                 VERGLEICHSPOSITION
         LA    R4,SEOP                  OPERATION
         LA    R5,SVERSUM               VERGLEICHSSUMME
         CALL  BERERG,((R3),(R4),(R5))
         MVC   ZEILE+30(27),MASKVS      MASKE
         ED    ZEILE+30(27),SESUM       SUMME,MIT DER ABGEST.WERDEN SOL
         SPACE 1
*  AUFBEREITETE ZEILE DRUCKEN
WRSUM265 EQU   *
         MVC   ZEILE+57(24),TSMIT ABST.ERG,ABST.MIT
WRSUM27  EQU   *              ABST.ERGEBNIS-ZEILE AUFBEREITET
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
         NI    KENN,X'FF'-(1+2+8)
         B     WRLOOP2E
         SPACE 1
WRSUM28  EQU   *         UEBERSCHRIFT DER ABSTIMMERGEBNISLISTE DRUCKEN
         NI    KENN,X'FF'-1
         MVC   ZEILE(23),=C'0** A B S T I M M U N G'
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
         MVC   ZEILE+4(19),=19CL1'-'    UNTERSTREICHUNG
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
         B     WRSUM22
         SPACE 2
*  VERGLEICHSSUMME DRUCKEN
WRVER    EQU   *
         TM    KENN,4
         BZR   R11                      VERSUM. NICHT DRUCKEN
         ST    R11,REG11V
         MVC   ZEILE+38(17),=19CL1'-'   SUMMENSTRICH
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
         MVC   ZEILE+30(27),MASKVS      MASKE
         ED    ZEILE+30(27),SVERSUM     BERECHNETE VERGLEICHSSUMME
         MVC   ZEILE+58(22),MASKD
         ZAP   ZWISUM,SSUM
         SP    ZWISUM,SVERSUM          DIFFERENZ
         ED    ZEILE+58(22),ZWISUM
         MVI   ZEILE+58,C'('
         MVI   ZEILE+80,C')'
         CALL  ABLTPUT,MF=(E,ABLTPUTL)                ZEILE DRUCKEN
         NI    KENN,X'FF'-4             KENN-DRUCKEN LOESCHEN
         ZAP   SVERSUM,=P'0'
         MVI   ZEILE,C'0'
         L     R11,REG11V
         BR    R11
         SPACE 2
*              E N D R O U T I N E
*              -------------------
*
WRENDE   EQU   *
         L     R0,VASTLIST
         CLOSE ((R0))
         L     R1,VASTLIST
         FREEPOOL (1)
         MVI   LDCBSW,DCBCLOSE
         XR    R2,R2                    FEHLERCODE = 0
RETURN   EQU   *
         L     R3,ERRADR                --> FEHLERCODE
         ST    R2,0(R3)                 FEHLERCODE
         XRETURN
         SPACE 1
RETERR   EQU   *
         LA    R2,1                     FEHLERCODE = 1
         B     RETURN
         SPACE 3
LOESCH   EQU   *
         MVI   ZEILE,C' '
         MVC   ZEILE+1(80),ZEILE        DRUCKBEREICH LOESCHEN
         BR    R11
         EJECT
*              D E F I N I T I O N E N
*              -----------------------
*
PATAB    DC    V(ATAB)                  --> ATAB
DWORD    DS    D
ERRADR   DS    A                        --> FEHLERCODE
REG11    DS    A
KMERK    DS    A
REG11V   DS    F
UPZAP    ZAP   DWORD,0(0,R2)            ABSTIMMSUM.AUF P68 ERWEITERN
KENN     DS    CL1
*        =1  1.ZEILE ABSTIMMVERGLEICH
*        =2  1.ZEILE SUMMEN
*        =4  VERGLEICHSSUMMEN DRUCKEN
*        =8  NUR SEDO-SUMMEN DRUCKEN
*        +16 1.DURCHLAUF JE SUMME
ZWISUM   DS    PL10
ABLTPUTL CALL  ,(ZEILE),MF=L
         SPACE 1
TXTADR   DC    V(FHLTXT)
PTXTUPDT DC    V(TXTUPDT)
         SPACE 1
*  EDITMASKEN
MASK0    DS    0CL22
         DC    CL22' ½½½ ½½½ ½½½ ½½½ ½À½- '  ' AAA AAA AAA AAA ABA- '
         DC    CL22' ½½½½½ ½½½ ½½½ ½À½.½- '  ' AAAAA AAA AAA ABA.A- '
         DC    CL22' ½½½½ ½½½ ½½½ ½À½.½½- '  ' AAAA AAA AAA ABA.AA- '
         DC    CL22' ½½½ ½½½ ½½½ ½À½.½½½- '  ' AAA AAA AAA ABA.AAA- '
MASKVS   DC    CL27' ½½½½ ½½½ ½½½ ½½½ ½À½.½½½- '
*                                       ' AAAA AAA AAA AAA ABA.AAA- '
MASKD    DC    CL22' ½½½½½½½½½½½½½½À½.½½½-'  ' AAAAAAAAAAAAAABA.AAA-'
*                                       A=11/0/1/8/9 -LOCHUNG
*                                       B=0/1/9      -LOCHUNG
         SPACE 2
*              DCB
PARMKART DCB   DDNAME=PARMKART,DSORG=PS,MACRF=(GM),EODAD=DPARM90
VASTLIST DC    V(ASTLIST)
         SPACE 1
SAVE     DC    18F'0'                   SAVEAREA
         LTORG
ZEILE    DC    CL81' '        DRUCKBEREICH
         PRINT GEN
         MATAB
         MKTAB
         MSTAB
FHLTXT   CSECT
*                                       FEHLERTEXTE
         DC    CL32'KARTENART FALSCH'               1
         DC    CL32'SUMMENINDEX FALSCH'             2
         DC    CL32'STAB VOLL'                      3
         DC    CL32'KTAB VOLL'                      4
         DC    CL32'ABSTIMMHINWEIS FALSCH'          5
         DC    CL32'ABSTIMMERGAENZ FALSCH'          6
         DC    CL32'POSITIONS-NR FALSCH'            7
         DC    CL32'ABSTIMMLISTEN-NR FALSCH'        8
         DC    CL32'ANZAHL DER PARAMETER FALSCH'    9
         DC    CL32'ABST.SUMME ZU GROSS'           10
         DC    CL32'ABST.SUMME NICHT GEPACKT'      11
         DC    CL32'PARAMETERUEBERGABE WAR FALSCH' 12
         DC    CL32'                            '  13FREI
         DC    CL32'NICHT ALLE SUMMEN ABGESTIMMT'  14
         DC    CL32' '                             15
         DC    CL32'NICHT ALLE UPDATE AUSGEFUEHRT' 16
         DC    CL32'TEXTINDEX/LAENGE NICHT NUM.  ' 17
         DC    CL32'V-KARTE NICHT ERWARTET       ' 18
         DC    CL32'V-KARTE UND ODER FALSCH      ' 19
         DC    CL32'KOPFKARTE FEHLT              ' 20
         EJECT
*
*  TEXTE ZU UPDATE  (INDIZIERT MIT ACCT)
*
TXTUPDT  CSECT
         DC    CL32' '
         DC    CL32'ROUTINELAUF'
         DC    CL32'WIEDERHOLUNG'
         DC    CL32'SONDERLAUF'
         DC    CL32' '
         DC    CL32' '
         DC    CL32' '
         DC    CL32' '
         DC    CL32' '
         DC    CL32' '
         DC    CL32' '
         DC    CL32'ASTLOG FEHLT'
         DC    CL32'ASTSUM FEHLT'
         DC    CL32'WEDER ROUTINE NOCH WIEDERHOLUNG'
         DC    CL32'ABSTIMMBLATT-NR FALSCH'
         DC    CL32'NICHT ROUTINEJOBNAME'
         END
