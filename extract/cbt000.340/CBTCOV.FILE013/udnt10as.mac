RZSP026A CSECT
***********************************************************************
***********************************************************************
********                                                       ********
******** B E R E C H N U N G   F U E R   R Z - A R B E I T E N ********
********                                                       ********
***********************************************************************
*********                                                      ********
*********  DAS PROGRAMM VERARBEITET DIE SMF-SAETZE:            ********
*********                     4  STEP TERMINATION RECORD       ********
*********                     5  JOB  TERMINATION RECORD       ********
*********                     6  OUTPUT WRITER RECORD          ********
*********                                                      ********
********   SORTIERFOLGE FUER EINGABEDATEI SMFBAND :            ********
********                   1.  JOB DATE STAMP                  ********
********                   2.  JOB TIME STAMP                  ********
********                   3.  JOB NAME                        ********
********                   4.  RECORD TYPE  5, 4, 6            ********
*********                                                      ********
*********                                                      ********
*********                                                      ********
***********************************************************************
***********************************************************************
         EJECT
***********************************************************************
********                                                       ********
********   P R O G R A M M E R O E F F N U N G                 ********
********                                                       ********
***********************************************************************
         SPACE 3
START    SAVE  (14,12)
         BALR  3,0
         USING *,3,4,5,6,7,8
         LA    4,4095(3)                * LADEN BASISREGISTER *********
         LA    4,1(4)
         LA    5,4095(4)
         LA    5,1(5)
         LA    6,4095(5)
         LA    6,1(6)
         LA    7,4095(6)
         LA    7,1(7)
         LA    8,4095(7)
         LA    8,1(8)
         ST    13,SAVEAREA+4            * RUECKSPRUNGADRESSE SICHERN **
         LR    12,13
         LA    13,SAVEAREA
         ST    13,8(12)
*       PARAMETER RETTEN
         L     2,0(1)                   ADR DER PARMLISTE
         TRT   2(3,2),NT
         BC    8,OKAY
         WTO   'PARMANGABE NICHT 3-STELLIG NUMERISCH',ROUTCDE=(11)
         ABEND 101,DUMP
OKAY    PACK  CPUZEIT,2(3,2)          PROZENTSATZ CPUZEIT
         CVB   2,CPUZEIT                KONVERTIERE NACH BINAER
         ST    2,CPUZEIT
         OPEN  (SMFBAND,,SMFTB,,JOBSUMAG,(OUTPUT),FEHLER,(OUTPUT))
         EJECT
*
********   EINLESEN DER PREIS-DATEI  - S M F T B -             ********
*
         SR    12,12                   EINLESEN VON SMFTB
A00      LA    0,TABB                  **
         AR    0,12                    **
         LR    11,0                     **
         GET   SMFTB,(0)               **
         CLC   0(5,11),TABVERGL         FOLGEPRUEFUNG VON SMFTB
         BNE   A001                     **
         AP    TABADD,=P'1'             **
         UNPK  TABVERGL+3(2),TABADD     **
         OI    TABVERGL+4,X'F0'         **
         LA    12,80(12)               **
         B     A00                     **
A001     NOP   *+4                      SORTIERFEHLER IN SMFTB
         ABEND 220,DUMP
EOFSMFTB L     12,=F'80'               UMWANDELN VON PREIS1 IN GE-
         L     11,=A(TABB-31)          ** PACKTE FORM
A01      AR    11,12                   **
         S     11,=F'49'               **
         CLC   6(2,11),=C'FF'          **
         BE    A0                      **
         A     11,=F'49'               **
         CLC   31(5,11),=C'TBP18'      **
         BNE   A011                    **
         PACK  19(5,11),0(4,11)        **
         B     A012                    **
A011     PACK  19(5,11),0(7,11)        **
A012     CLI   26(11),C' '              UMWANDELN DER ZUGRIFFSZEITEN
         BE    A01                      ** VON DEZIMAL IN BINAER
         PACK  CONVERT2,24(3,11)        **
         CVB   10,CONVERT2              **
         ST    10,CONVERT2              **
         MVC   24(2,11),CONVERT2+2      **
         CLI   29(11),C' '              **
         BE    A01                      **
         PACK  CONVERT2,27(3,11)        **
         CVB   10,CONVERT2              **
         ST    10,CONVERT2              **
         MVC   27(2,11),CONVERT2+2      **
         B     A01                      **
         EJECT
***********************************************************************
********                                                       ********
********   V E R A R B E I T U N G  D E R  S M F - S A E T Z E ********
********                                                       ********
***********************************************************************
         SPACE 3
*
********   VERARBEITUNG SMF-SATZ TYP 5  (JOB TERMINATION REC)  ********
*
A0       BAL   14,ULESSMF
A02      MVC   JSACCONT(212),=212C' '   LOESCHEN JOBSUMMENSATZ
         ZAP   JSVPREIS,=PL8'0'        LOESCHEN VOLLPREIS
         ZAP   JSCPUPRS,=PL8'0'
         MVC   JSCOMPLC,=2X'00'
         CLI   STRECTYP,X'05'           VERGLEICH AUF JOB TERM RECORD
         BE    A1
         B     ULESSMF0                 SORTIERFEHLER AUF SMFBAND
A1       MVC   JSINITDT,TJINITDT
         MVC   JSINITTM,TJINITTM
         MVC   FULLFELD,TJEOJTM
         L     12,FULLFELD
         MVC   FULLFELD,JSINITTM
         S     12,FULLFELD
         LTR   12,12
         BM    GG1
         B     GG2
GG1      A     12,=F'8640000'
GG2      ST    12,FULLFELD
         MVC   JSRUNGES,FULLFELD
         MVC   JOBVERGL(8),STJOBNAM     JOBNAME IN VERGLEICHSFELD
         MVC   TMEVERGL,STRECTME        READERSTARTTIME IN VERGLFELD
         CLI   TJLNGTH2,X'01'           ACCOUNT FELD VORHANDEN?
         BE    MVACCT                   BRANCH, WENN JA
         MVC   TJACCNT(4),=X'03F6F1F3'  SONST '613' DEFAULT
MVACCT   EQU   *
         MVC   JSACCONT(3),TJACCNT+1       ACCOUNTNR NACH JOBSUM
         MVC   JSJOBNAM,STJOBNAM        JOBNAME NACH JOBSUM
         MVC   JSBENUTZ,TJPROGNM       * BENUTZER NACH JOBSUM
A3       MVC   JSRDRDAT,STRDRDAT        DATUM NACH JOBSUM
         MVC   JSRDRTME,STRDRTME        ZEIT  NACH JOBSUM
         MVI   JS2540PR,X'00'           LOESCHEN DER BINAEREN ADDITIONS
         MVC   JS2540PR+1(71),JS2540PR  ** FELDER IM JOBSATZ
         MVI   JSSTORGE,X'00'           **
         MVC   JSSTORGE+1(33),JSSTORGE  **
         TM    TJABEND,X'02'           * PRUEFUNG AUF ABEND
         BNO   A31                     *
         MVI   JSABEND,C'1'            *
A31      BAL   14,ULESSMF
         EJECT
*
********   VERARBEITUNG SMF-SATZ TYP 4  (STEP TERMINATION REC) ********
*
A5       NOP   *+4                      VERARBEITEN SATZART 4
         MVC   FULLFELD,STSYSIN
         L     12,CARDSUM
         A     12,FULLFELD
         ST    12,CARDSUM
         MVC   FULLFELD,=F'0'           BERECHNUNG DER BENUTZTEN
         MVC   FULLFELD+2(2),STSTORG3   ** KERNSPEICHERGROESSE
         L     12,FULLFELD              ** ADDITION VON:
         MVC   FULLFELD+2(2),STSTORG3+2 **   HIERARCHY 0 STORAGE USED
         A     12,FULLFELD              **         ADDITIONAL STORAGE
         MVC   FULLFELD+2(2),STSTORG4   **   HIERARCHY 1 STORAGE USED
         A     12,FULLFELD              **         ADDITIONAL STORAGE
         MVC   FULLFELD+2(2),STSTORG4+2 **
         A     12,FULLFELD              **
         ST    12,FULLFELD              **
         CLC   FULLFELD+2(2),=H'20'
         BNH   A6
         CLC   FULLFELD+2(2),JSSTORGE   ** DER ERRECHNETE WERT WIRD
         BNH   A6                       ** MIT DEM IM JOBSATZ GESPEI-
         MVC   JSSTORGE,FULLFELD+2      ** CHERTEN WERT DES VORHERGE-
*                                       ** HEN SATZES 4 VERGLICHEN UND
*                                       ** DER HOEHERE WERT IN DEN
*                                       ** JOBSATZ EINGETRAGEN.
A6       MVC   UNITVERG+2(2),STLNGTH1   ADDIEREN DER DISK UND TAPE ACC.
         L     12,UNITVERG
         SH    12,=H'2'
         ST    12,UNITVERG
         SR    11,11                    **
A7       LA    12,STDVCE                **
         AR    12,11                    **
         MVC   STDVCE(8),0(12)          **
         CLI   STDVCE,X'20'             ** DISK
         BE    A8                       **
         CLI   STDVCE,X'80'             ** TAPE
         BE    A9                       **
         CLI   STDVCE,X'08'             ** UNIT RECORD
         BNE   A10                      **
         CLI   STUNITTY,X'01'           ** 2540 READER
         BE    A71                      **
         CLI   STUNITTY,X'08'           ** 1403 PRINTER
         BE    A72                      **
         CLI   STUNITTY,X'02'           ** 2540 PUNCHER
         BNE   A10                      **
         MVC   FULLFELD,STEXCP          ADDIEREN 2540 PUNCHER
         L     12,PN2540                **
         A     12,FULLFELD              **
         ST    12,PN2540                **
         B     A10                      **
A71      MVC   FULLFELD,STEXCP          ADDIEREN 2540 READER
         L     12,RD2540                **
         A     12,FULLFELD              **
         ST    12,RD2540                **
         B     A10                      **
A72      MVC   FULLFELD,STEXCP          ADDIEREN 1403 PRINTER
         L     12,PR1403                **
         A     12,FULLFELD              **
         ST    12,PR1403                **
         B     A10                      **
A8       MVC   FULLFELD,STEXCP          ** AUFSUMMIEREN DISK
         L     12,DISKSUM               **
         A     12,FULLFELD              **
         ST    12,DISKSUM               **
         B     A10                      **
A9       LA    12,TAPETAB              * AUFSUMMIEREN TAPE
         CLI   0(12),C' '              *
         BE    A94                     *
A91      CLC   0(2,12),STCHNADR        *
         BNE   A93                     *
         MVC   FULLFELD,2(12)
         L     14,FULLFELD
         MVC   FULLFELD,STEXCP
         A     14,FULLFELD
         ST    14,FULLFELD
         MVC   2(4,12),FULLFELD
A92      B     A10
A93      LA    12,6(12)
         CLI   0(12),C' '
         BNE   A91
A94      MVC   0(6,12),STCHNADR
         B     A92
TAPETAB  DS    CL102
         ORG   TAPETAB
         DC    102C' '
TAPEMULT DC    F'0'
A10      LA    11,8(11)                 ** NAECHSTE DEVICE
         C     11,UNITVERG              **
         BL    A7                       **
         LA    12,TAPETAB
A1001    CLI   0(12),C' '
         BE    A1008
         C     12,=A(TAPETAB+18)
         BNL   A1002
         MVC   FULLFELD,2(12)
         L     14,TAPESUM
         A     14,FULLFELD
         ST    14,TAPESUM
         LA    12,6(12)
         B     A1001
A1002    C     12,=A(TAPETAB+23)
         BH    A1003
         MVC   TAPEMULT,2(12)
         L     15,TAPEACC
         A     15,TAPEMULT
         ST    15,TAPEACC
         B     A1006
A1003    C     12,=A(TAPETAB+29)
         BH    A1004
         MVC   TAPEMULT,2(12)
         L     15,TAPEACC
         A     15,TAPEMULT
         ST    15,TAPEACC
         L     15,=F'2'
         B     A1005
A1004    C     12,=A(TAPETAB+35)
         BH    A1004A
         MVC   TAPEMULT,2(12)
         L     15,TAPEACC
         A     15,TAPEMULT
         ST    15,TAPEACC
         L     15,=F'4'
         B     A1005
A1004A   C     12,=A(TAPETAB+41)
         BH    A1007
         MVC   TAPEMULT,2(12)
         L     15,TAPEACC
         A     15,TAPEMULT
         ST    15,TAPEACC
         L     15,=F'8'
A1005    SR    14,14
         M     14,TAPEMULT
         ST    15,TAPEMULT
A1006    L     14,TAPE2SUM
         A     14,TAPEMULT
         ST    14,TAPE2SUM
         LA    12,6(12)
         B     A1001
A1007    MVC   TAPEMULT,2(12)
         L     15,TAPEACC
         A     15,TAPEMULT
         ST    15,TAPEACC
         L     15,=F'16'
         SR    14,14
         M     14,TAPEMULT
         A     15,TAPE2SUM
         ST    15,TAPE2SUM
A1008    NOP   *+4
         MVC   TAPETAB,=96C' '
         MVC   FULLFELD,=XL4'0'
         LA    12,STDVCE+1
         A     12,UNITVERG
         MVC   FULLFELD+1(3),0(12)
         L     12,CPUSUM
         A     12,FULLFELD
         ST    12,CPUSUM
         CLI   JSABEND,C' '            ** ROUTINE FUER ABEND-CODE
         BE    A109                    **
         CLC   STCOMPLC,=X'0000'       **
         BE    A109                    **
         TM    STCOMPLC,X'F0'          **
         BZ    A108                    **
         TM    STCOMPLC,X'80'          **
         BNO   A109                    **
A108     CLC   JSCOMPLC,STCOMPLC       **
         BH    A109                    **
         MVC   JSCOMPLC,STCOMPLC       **
A109     BAL   14,ULESSMF
         EJECT
*
********   VERARBEITUNG SMF-SATZ TYP 6  (OUTPUT WRITER REC)    ********
*
A11      NOP   *+4                      VERARBEITEN SATZART 6
         MVC   FULLFELD,OUSYSOUT        ADDIEREN SYSOUT-ACC.
         L     12,FULLFELD              **
         MVC   FULLFELD,JSWRTREC        **
         A     12,FULLFELD              **
         ST    12,FULLFELD              **
         MVC   JSWRTREC,FULLFELD        **
         CLI   OUFORMNO,C' '            **
         BE    A11A                     **
         MVC   JSFORMNR,OUFORMNO        VORDRUCKNR NACH JOB SATZ
         AP    JSVPREIS,TABB17+68(5)    **
A11A     BAL   14,ULESSMF
         EJECT
***********************************************************************
********                                                       ********
********   E R S T E L L E N  D E S  J O B V E R R E C H -     ********
********   N U N G S S A T Z E S                               ********
********                                                       ********
***********************************************************************
         SPACE 3
A12      NOP   *+4                      ENDVERARBEITUNG FUER EINEN JOB
*        CPUZEITEN MANIPULIEREN
         MVC   FULLFELD,CPUSUM
         STM   10,12,SAVE1315
         L     10,FULLFELD
         SRDA  10,32
         M     10,CPUZEIT
         D     10,=F'100'
         ST    11,CPUSUM
         LM    10,12,SAVE1315
         MVC   FULLFELD,=F'0'           BERECHNEN PREIS CPU
         MVC   FULLFELD,CPUSUM          **
         L     12,FULLFELD              **
         ZAP   PREIS,TABB04+68(5)       **
         BAL   14,UCONVERT              **
         DP    CONVERT2,=PL2'100'       **
         ZAP   JSCPUPRS,CONVERT2(6)    **
         MVC   JSSINACC,CARDSUM         BERECHNEN EINGELESENE KARTEN
         MVC   FULLFELD,CARDSUM         **
         L     12,FULLFELD              **
         ZAP   PREIS,TABB15+68(5)       **
         BAL   14,UCONVERT              **
         MVC   JSSINPRS,CONVERT2        **
         MVC   JSDSCACC,DISKSUM
         MVC   JSTPEACC,TAPESUM
         MVC   JSTAPAC2,TAPE2SUM
         MVC   JS2540PR,RD2540
         MVC   JS2540PN,PN2540
         MVC   JS1403PR,PR1403
         MVC   JSCPUTME,CPUSUM+1
         ZAP   PREIS,TABB09+68(5)       BERECHNEN EXCPS DISK
         MVC   FULLFELD,JSDSCACC        **
         L     12,FULLFELD              **
         BAL   14,UCONVERT              **
         MVC   JSDSCPRS,CONVERT2        **
         ZAP   PREIS,TABB10+68(5)       BERECHNEN EXCPS TAPE
         MVC   FULLFELD,JSTPEACC        **
         L     12,FULLFELD              **
         BAL   14,UCONVERT              **
         MVC   JSTPEPRS,CONVERT2        **
         ZAP   PREIS,TABB11+68(5)
         MVC   FULLFELD,JSTAPAC2
         L     12,FULLFELD
         BAL   14,UCONVERT
         MVC   JSTP2PRS,CONVERT2
         MVC   JSTAPAC2,TAPEACC
         MVC   FULLFELD,=F'0'           **
         L     12,DISKSUM               ** ZEIT DISC-EXCPS
         MVC   FULLFELD(2),TABB09+73    **
         MH    12,FULLFELD              **
         LR    11,12                    **
         L     12,TAPESUM               **  ZEIT TAPE-EXCPS
         A     12,TAPEACC
         MVC   FULLFELD(2),TABB10+73    **
         MH    12,FULLFELD              **
         AR    11,12                    **
         L     12,PN2540                **  ZEIT PUNCHER-EXCPS
         MVC   FULLFELD(2),TABB13+73    **
         MH    12,FULLFELD              **
         AR    11,12                    **
         L     12,RD2540                **  ZEIT READER-EXCPS
         MVC   FULLFELD(2),TABB12+73    **
         MH    12,FULLFELD              **
         AR    11,12                    **
         L     12,PR1403                **  ZEIT PRINTER-EXCPS
         MVC   FULLFELD(2),TABB14+73    **
         MH    12,FULLFELD              **
         AR    11,12                    **
         LR    12,11                    **
         BAL   14,UDIVID36              **
         LR    11,12                    **
         MVC   FULLFELD,=F'0'           **
         MVC   FULLFELD+1(3),JSCPUTME        ** BERECHNEN JOB-LAUFZEIT
         A     11,FULLFELD              **
         ST    11,FULLFELD              **
         MVC   JSRUNTME,FULLFELD        **
         MVC   FULLFELD,=F'0'           **
         MVC   FULLFELD+2(2),JSSTORGE   BERECHNEN PREIS STORAGE
         L     12,FULLFELD              **
         C     12,=F'90'                **
         BNH   A15                      **
         C     12,=F'180'               **
         BNH   A14                      **
         C     12,=F'270'               **
         BNH   A13                      **
         ZAP   PREIS,TABB08+68(5)       ** KERNSPEICHER GROESSER 270 K
         B     A16                      **
A13      ZAP   PREIS,TABB07+68(5)        ** KERNSPEICHER GROESSER 180 K
         B     A16                      **
A14      ZAP   PREIS,TABB06+68(5)        ** KERNSPEICHER GROESSER  90 K
         B     A16                      **
A15      ZAP   PREIS,TABB05+68(5)       ** KERNSPEICHER KLEINER 90 K
A16      MVC   FULLFELD,JSRUNTME        **
         L     12,FULLFELD              **
         BAL   14,UCONVERT              **
         DP    CONVERT2,=PL2'100'       **
         ZAP   JSSTOPRS,CONVERT2(6)     **
         ZAP   PREIS,TABB13+68(5)       BERECHNEN PREIS PUNCHER
         MVC   FULLFELD,JS2540PN        **
         L     12,FULLFELD             **
         BAL   14,UCONVERT              **
         MVC   JS254PNP,CONVERT2        **
         ZAP   PREIS,TABB12+68(5)       BERECHNEN PREIS READER
         MVC   FULLFELD,JS2540PR        **
         L     12,FULLFELD             **
         BAL   14,UCONVERT              **
         MVC   JS254PRP,CONVERT2        **
         ZAP   PREIS,TABB14+68(5)       BERECHNEN PREIS PRINTER
         MVC   FULLFELD,JS1403PR        **
         L     12,FULLFELD             **
         BAL   14,UCONVERT              **
         MVC   JS140PRP,CONVERT2        **
         ZAP   PREIS,TABB16+68(5)       BERECHNEN PREIS OUTPUT WRITER
         MVC   FULLFELD,JSWRTREC        **
         L     12,FULLFELD             **
         BAL   14,UCONVERT              **
         MVC   JSWRTPRS,CONVERT2        **
         ZAP   JSTPREIS,JSWRTPRS        SUMMIEREN TEILPREIS
         AP    JSTPREIS,JSSINPRS        **
         AP    JSTPREIS,JSSTOPRS        **
         AP    JSTPREIS,JSTPEPRS        **
         AP    JSTPREIS,JSDSCPRS        **
         AP    JSTPREIS,JSCPUPRS        **
         AP    JSTPREIS,JS254PNP
         AP    JSTPREIS,JS254PRP
         AP    JSTPREIS,JS140PRP
A17      AP    JSVPREIS,JSTPREIS        **
         PUT   JOBSUMAG,JOBSATZ         AUSGEBEN JOBSATZ
         ZAP   PREIS,=PL5'0'
         MVC   UNITVERG(40),=40X'00'
         MVC   TAPETAB,=96C' '
         CLI   ENDANZ,X'FF'             PROGRAMMENDE
         BNE   A02
         EJECT
***********************************************************************
********                                                       ********
********   P R O G R A M M S C H L U S S                       ********
********                                                       ********
***********************************************************************
         SPACE 3
         CLOSE (SMFBAND,,SMFTB,,JOBSUMAG,,FEHLER)
         CP    ZAEHL,=P'0'
         BNE   A18
         MVC   WTO1+36(5),=C'KEINE'
         B     WTO1
A18      UNPK  WTO1+36(5),ZAEHL
         OI    WTO1+40,X'F0'
WTO1     WTO   'AUF DIE FEHLER-DATEI WURDEN       SAETZE GESCHRIEBEN.'
         L     13,SAVEAREA+4
         RETURN (14,12),RC=0
*
***********************************************************************
         EJECT
***********************************************************************
********                                                       ********
********   U N T E R P R O G R A M M E                         ********
********                                                       ********
***********************************************************************
         SPACE 3
ULESSMF  STM   13,15,SAVE1315           UNTERPROGRAMM ZUM EINLESEN
         GET   SMFBAND,STEPSATZ         ** DES SMFBANDES
         CLC   JOBVERGL(8),=C'        ' 1. SATZ DER DATEI
         BE    ULESSMF5
         CLC   JOBVERGL,STJOBNAM
         BE    ULESSMF1                 GLEICHER JOB
         CLI   STRECTYP,X'05'
         BE    ULESSMF2                 NEUER SATZ
ULESSMF0 PUT   FEHLER,STEPSATZ          SORTIERFEHLER AUF SMFBAND
         AP    ZAEHL,=P'1'
         B     ULESSMF
ULESSMF1 CLI   STRECTYP,X'04'
         BE    ULESSMF3                 SATZART 4
         CLI   STRECTYP,X'06'
         BE    ULESSMF4                 SATZART 6
         CLI   STRECTYP,X'05'           * GEAENDERT RUPPELT
         BE    ULESSMF2                 * GEAENDERT RUPPELT
         ABEND 120,DUMP
ULESSMF2 LA    14,A12                   ADR. ENDVERARBEITUNG FUER
         B     ULESSMF6                 ** EINEN JOB
ULESSMF3 CLC   OUTREC(2),STLNGTH1      * ABPRUEFEN DER SATZLAENGE
         BH    ULESSM3A
         B     ULESSMF0
ULESSM3A LA    14,A5
         B     ULESSMF6
ULESSMF4 LA    14,A11                   ADR. VERARBEITUNG SATZART 6
         B     ULESSMF6
ULESSMF5 LA    14,A02                   ADR. VERARBEITUNG 1.SATZ
ULESSMF6 L     13,SAVE1315
         L     15,SAVE1315+8
         BR    14
***********************************************************************
         EJECT
UCONVERT STM   13,15,SAVE1315           UNTERPROGRAMM ZUR UMRECHNUNG
         CVD   12,CONVERT2              ** VON BINAER IN GEPACKT UND
         MP    CONVERT1(12),PREIS       ** ZUM MULTIPLIZIEREN DES UM-
         OI    CONVERT2+6,X'0F'         ** GEWANDELTEN WERTES MIT DEM
         AP    CONVERT1(11),=P'5'       ** IM FELD 'PREIS' ENTHALTE-
         MVO   CONVERT2,CONVERT1+1(9)   ** NEM WERT
         OI    CONVERT2+7,X'0F'
         MVC   CONVERT1,=F'0'
         LM    13,15,SAVE1315
         BR    14
CPUZEIT  DS    D
SAVE1315 DC    3F'0'
         DS    0D
         DC    F'0'                WEGEN ALIGNMENT, MUSS VOR CONVERT1
CONVERT1 DC    F'0'
CONVERT2 DC    D'0'
***********************************************************************
         SPACE 3
UDIVID36 STM   13,15,SAVE1315           UNTERPROGRAMM ZUM DIVIDIEREN
         LR    13,12                    **
         SR    12,12                    **
         D     12,=F'10'                **
         LR    12,13                    **
         LM    13,15,SAVE1315           **
         BR    14                       **
         EJECT
***********************************************************************
********                                                       ********
********   D E F I N I T I O N E N                             ********
********                                                       ********
***********************************************************************
         SPACE 3
********  SUMMENSATZ FUER EINEN JOB ***********************************
JOBSATZ  DS    0CL212
JSATZART DC    C'SMF01'                * SATZART                   ALP
JSACCONT DS    CL9                     * ACCOUNT-NUMMER            ALP
JSJOBNAM DS    CL8                     * JOBNAME                   ALP
JSBENUTZ DS    CL16                    * PROGRAMM-BENUTZER         ALP
JSRDRDAT DS    CL4                     * READER START DATE FOR JOB PAC
JSRDRTME DS    CL4                     * READER START TIME FOR JOB BIN
JSCPUTME DS    CL3                     * JOB CPU TIME              BIN
JSCPUPRS DS    CL8                     * PREIS CPU-RECHENZEIT      PAC
JS2540PR DS    CL4                     * EXCP COUNT 2540 READER    BIN
JS254PRP DS    CL8                     * PREIS EXCPS 2540 READER   PAC
JS2540PN DS    CL4                     * EXCP COUNT 2540 PUNCH     BIN
JS254PNP DS    CL8                     * PREIS EXCPS 2540 PUNCH    PAC
JS1403PR DS    CL4                     * EXCP COUNT 1403 PRINTER   BIN
JS140PRP DS    CL8                     * PREIS EXCPS 1403 PRINTER  PAC
JSDSCACC DS    CL4                     * EXCP COUNT DISK           BIN
JSDSCPRS DS    CL8                     * PREIS EXCPS DISK          PAC
JSTPEACC DS    CL4                     *EXCP COUNT TAPE            BIN
JSTPEPRS DS    CL8                     * PREIS EXCPS TAPE          PAC
JSTAPAC2 DS    CL4                     * EXCP COUNT TAPE2          BIN
JSTP2PRS DS    CL8                     * PREIS TAPE2               PAC
JSRUNTME DS    CL4                     * JOB RUN TIME  100-STEL SEC.
JSSTORGE DS    CL2                     * CORE USED                 BIN
JSSTOPRS DS    CL8                     * PREIS STORAGE             PAC
JSSINACC DS    CL4                     * CARD-IMAGE RECORDS        BIN
JSSINPRS DS    CL8                     * PREIS SYSIN CARD-I. REC.  PAC
JSWRTREC DS    CL4                     * OUTPUT WRITER RECORDS     BIN
JSWRTPRS DS    CL8                     * PREIS OUTPUT WRITER       PAC
JSTPREIS DS    CL8                     * TEILPREIS OHNE FORM NMBER PAC
JSFORMNR DS    CL4                     * OUTPUT WRITER FORM NUMBER ALP
JSVPREIS DC    PL8'0'                  * VOLLPREIS DES JOBS        PAC
JSCOMPLC DS    CL2                     * COMPLETION-CODE
JSABEND  DS    CL1                     * ABEND
JSRUNGES DS    CL4
JSINITDT DS    CL4
JSINITTM DS    CL4
         DS    CL10
*
********  EINGABESAETZE AUS DER DATEI SMFBAND
********       STEP TERMINATION RECORD - RECORD TYPE 4 -
STEPSATZ DS    0D
         DS    CL5
STRECTYP DS    CL1                     * SATZART                   BIN
STRECTME DS    CL4                     * STEP-BEENDIGUNGSZEIT      BIN
STRECDAT DS    CL4                     * STEP-BEENDIGUNGSDATUM     PAC
         DS    CL4
STJOBNAM DS    CL8                     * JOB NAME                  ALP
STRDRTME DS    CL4                     * READER START TIME         BIN
STRDRDAT DS    CL4                     * READER START DATE         PAC
         DS    CL17
STSYSIN  DS    CL4                     * NUMBER /F CARD-RECORDS    BIN
STCOMPLC DS    CL2                     * STEP COMPLETION CODE
         DS    CL17                    *
STSTORG1 DS    CL2                     * REGION REQUESTED IN K (0) BIN
STSTORG2 DS    CL2                     * REGION REQUESTED IN K (1) BIN
STSTORG3 DS    CL2                     * STORAGE USED     IN K (0) BIN
         DS    CL2
STSTORG4 DS    CL2                     * STORAGE USED     IN K (1) BIN
         DS    CL22
STLNGTH1 DS    CL2                     * LAENGE DES BEREICHES FUER     *
                                       * DIE DATEN DER EXT. UNITS  BIN
STDVCE   DS    C                       * BYTE 0 --- DEVICE CLASS
STUNITTY DS    C                       * BYTE 1 --- UNIT TYPE
STCHNADR DS    C                       * BYTE 2 --- CHANNEL ADDRES BIN
STUNIADR DS    C                       * BYTE 3 --- UNIT ADDRESS   BIN
STEXCP   DS    CL4                     * BYTE 4-7 - COUNT OF EXCPS BIN
         DS    CL1
STCPUTME DS    CL3                     * STEP CPU-TIME             BIN
         DS    1880C
*
********       JOB TERMINATION RECORD - RECORD TYPE 5 -
         ORG   STEPSATZ
JOBTRM   DS    CL6
TJEOJTM  DS    CL4
TJEOJDT  DS    CL4
JOBTRMX  DS    CL29
TJINITTM DS    CL4                     * JOB INITIATION TIME       BIN
TJINITDT DS    CL4                     * JOB INITIATION DATE       PAC
TJSYSIN  DS    CL4                     * NUMBER OF CARD-RECORDS    BIN
         DS    CL11
TJABEND  DS    CL1                     * ABEND INDICATOR
         DS    CL30                    *
TJPROGNM DS    CL20                    * PROGRAMMERS NAME
TJCPUTME DS    CL3                     * JOB CPU TIME              BIN
TJLNGTH2 DS    C                       * NUMBER OF ACCOUNT FIELDS  BIN
TJACCNT  DS    1878C                   * JOB ACCOUNTING FIELDS     ALP
*
********       OUTPUT WRITER RECORD - RECORD TYPE 6 -
         ORG   STEPSATZ
OUTREC   DS    CL51
OUSYSOUT DS    CL4                     * NUMBER OF LOG. RECORDS    BIN
         DS    CL2
OUFORMNO DS    CL4                     * FORM NUMBER               ALP
         DS    1939C
*
********* DEFINITIONEN
SAVEAREA DC   18F'0'
JOBVERGL DC    CL8'        '           * VERGLEICHSFELD JOBNAME
TMEVERGL DS    CL4                      * VERGLEICHSFELD JOBNAME
FULLFELD DS    F                        * HILFSFELD
TABB     DC    1600C' '                 * BEREICH ZUM EINLESEN VON TABB
         ORG   TABB                     **
TABB01   DS    CL80                     **
TABB02   DS    CL80                     **
TABB03   DS    CL80                     **
TABB04   DS    CL80                     **
TABB05   DS    CL80                     **
TABB06   DS    CL80                     **
TABB07   DS    CL80                     **
TABB08   DS    CL80                     **
TABB09   DS    CL80                     **
TABB10   DS    CL80                     **
TABB11   DS    CL80                     **
TABB12   DS    CL80                     **
TABB13   DS    CL80                     **
TABB14   DS    CL80                     **
TABB15   DS    CL80                     **
TABB16   DS    CL80                     **
TABB17   DS    CL80                     **
TABB18   DS    CL80                     **
TABB19   DS    CL80                     **
TABB20   DS    CL80                     **
PREIS    DC    PL7'0'
UNITVERG DC    F'0'                     * LAENGE DER UNITFELDER
DISKSUM  DC    F'0'                     * SUMME DER DISK-EXCPS
TAPESUM  DC    F'0'                     * SUMME DER TAPE-EXCPS
TAPE2SUM DC    F'0'                    * SUMME DER TAPE2-EXCPS
PN2540   DC    F'0'                    * SUMME DER PUNCHER-EXCPS
RD2540   DC    F'0'                    * SUMME DER READER-EXCPS
PR1403   DC    F'0'                    * SUMME DER PRINTER-EXCPS
CPUSUM   DC    F'0'                    * SUMME DER CPU-TIME
CARDSUM  DC    F'0'                    * SUMME DER CARD-ECCPS
TAPEACC  DC    F'0'
ENDANZ   DC    X'00'                    * ANZEIGER SMFBAND ZU ENDE
TABVERGL DC    C'TBP01'                 * VERGLEICHSFELD FUER SMFTB
TABADD   DC    PL2'1'                   * ADDITIONSFELD FUER SMFTB
ZAEHL    DC    PL3'0'
SMFBAND  DCB   DDNAME=SMFBAND,BLKSIZE=2004,DSORG=PS,EODAD=EOFSMFBD,    *
               LRECL=2000,MACRF=(GM),RECFM=VB,SYNAD=ERRSMFBD
SMFTB    DCB   DDNAME=SMFTB,DSORG=PS,EODAD=EOFSMFTB,                   *
               MACRF=(GM),SYNAD=ERRSMFTB
JOBSUMAG DCB   DDNAME=JOBSUMAG,DSORG=PS,                               *
               LRECL=212,MACRF=(PM),RECFM=FB,SYNAD=ERRJOBSM
FEHLER   DCB   DDNAME=FEHLER,BLKSIZE=2004,DSORG=PS,                    *
               LRECL=2000,MACRF=(PM),RECFM=VB,SYNAD=ERRFEHL
*
EOFSMFBD MVI   ENDANZ,X'FF'             DATEIENDE SMFBAND
         B     A12
*
*                                       SYNADAUSGAENGE
ERRSMFBD ABEND 100,DUMP
ERRSMFTB ABEND 200,DUMP
ERRJOBSM ABEND 300,DUMP
ERRFEHL  ABEND 400,DUMP
*        ABEND 101 ----- PARMANGABE NICHT 3-STELLIG NUMERISCH
*        ABEND 120 ----- FALSCHER SMF-RECORD IM BESTAND (NUR 4/5/6)
*        ABEND 220 ----- SORTIERFEHLER IN SMFTB
NT       DC    256X'FF'
         ORG   NT+X'F0'
         DC    10X'00'
         ORG
         END   RZSP026A
