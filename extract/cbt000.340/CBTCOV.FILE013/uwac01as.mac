*   02/12/83 505021301  MEMBER NAME  UWAC01AS (GAPVERS)     ASSEMBLER
**********************************************************************
*  THIS PROGRAM IS USED TO ERASE A MAGNETIC TAPE WHICH CONTAINS      *
*  'HOT' DATA AND IS TO BE BROUGHT OUT OF THE COMPUTER SHOP.         *
*  THIS PROGRAM MAKES USE OF THE 'SECURITY-ERASE' FEATURE WHICH IS   *
*  'A MILLION TIME' MORE EFFICIENT THAN PUTTING A PATTERN ONTO THE   *
*  TAPE BECAUSE IT USES ONLY ONE CCW CHAIN AND DOES NOT SET CHANNEL  *
*  AND CONTROL UNIT BUSY DURING OPERATION.                           *
*                                                                    *
*  FUNCTION:                                                         *
*   1. VOLUME LABEL IS READ AND RETAINED (NON-SL TAPE IS REJECTED)   *
*   2. A DUMMY HDR1 LABEL IS WRITTEN (LIKE THAT FROM TL OR IEHINITT) *
*   3. TAPE IS ERASED FROM HERE TO EOT-MARKER ('SILBERFISCH')        *
*   4. TO MAKE SURE NO DATA REMAIN BEYOND EOT 14 ERASE-GAP'S WRITTEN *
*   5. MESSAGES ARE PUT ABOUT STATUS OF JOB.                         *
*                                                                    *
*  ATTRIBUTES: NON-RENT, NON-REUS, PAGEABLE, XA-INDEPENDENT,         *
*              NON-PRIVILEGED                                        *
*                                                                    *
*  AUTHOR: BLECKWENN, WACKER-CHEMIE MUENCHEN    AUG. 1984            *
*                                                                    *
**********************************************************************
         EJECT
ERASE    BEG   *
**********************************************************************
*  DELETE THE LINES MARKED BY '#NORAC#' IF YOU DONT WANT TO USE      *
*  RACF HERE OR ANYWHERE AT ALL.                                     *
**********************************************************************
         RACHECK ENTITY=(AERASE),CLASS='AUTH',ATTR=READ       #NORAC#
         LTR   R15,R15             WAR WAS ?                  #NORAC#
         BNZ   ERROR4              NEE, DENKSTE ...           #NORAC#
         OPEN  (ERADCB,OUTPUT)
         TM    ERADCB+48,X'10'     WAS OPEN SUCCESSFUL
         BZ    ERROR1              NO, GO TO ERROR EXIT
         XC    ECB,ECB             CLEAR ECB
         EXCP  IOB
         WAIT  ECB=ECB             WAIT UPON COMPLETION
         CLI   ECB,X'7F'           WAS READ SUCCESSFUL
         BNE   ERROR2              NO, BRANCH
         CLC   BUFFER(4),=CL4'VOL1' SL-TAPE ?
         BNE   ERROR2              NO, ERROR
         XC    ECB,ECB             CLEAR ECB
         LA    R1,WRITCCW          SWITCH TO WRITE
         STCM  R1,7,XIOBSTAR       STORE IN IOB
         EXCP  IOB
         WAIT  ECB=ECB             WAIT UPON COMPLETION
         CLI   ECB,X'7F'           WAS WRITE SUCCESSFUL
         BNE   ERROR3              NO, ERROR
*  (ELSE EOT ('SILBERFISCH') SENSED) ***
         XC    ECB,ECB             CLEAR ECB
         LA    R1,SENSCCW          SWITCH TO SENSE
         STCM  R1,7,XIOBSTAR       STORE IN IOB
         EXCP  IOB
         WAIT  ECB=ECB             WAIT UPON COMPLETION
         CLI   ECB,X'7F'           WAS SENSE SUCCESSFUL
         BNE   ERROR3              NO, ERROR
         TM    SENSE+7,X'08'       ANYTHING OTHER THAN EOT ?
         BO    ERROR5              YES, ERROR
         XC    ECB,ECB             CLEAR ECB
         LA    R1,ERGCCW1          SWITCH TO ERASE-GAP
         STCM  R1,7,XIOBSTAR       STORE IN IOB
         EXCP  IOB
         MVC   WTO1+15(6),BUFFER+4 PUT VOLSER INTO MESSAGE
WTO1     WTO   'VOLUME XXXXXX HAS BEEN ERASED.',ROUTCDE=(2,3,11)
         SR    R3,R3               ZERO RETURN CODE
CLOSE    CLOSE (ERADCB,)           CLOSE THE STUFF
         SCHL  RET=(3)             GO HOME
*
RETURNX  LA    R3,16               ERROR RETURN CODE
         B     CLOSE               GO CLOSE
*
ERROR1   WTO   'OPEN FAILED',ROUTCDE=(11)
         B     RETURNX
*
ERROR2   WTO   'TAPE HAS NOT STANDARD LABEL',ROUTCDE=(11)
         B     RETURNX
*
ERROR3   WTO   'WRITE/ERASE FAILED',ROUTCDE=(11)
         B     RETURNX
*
ERROR4   WTO   'ISSUER NOT RACF-AUTHORISED',ROUTCDE=(9,11)    #NORAC#
         B     RETURNX                                        #NORAC#
*                                                             #NORAC#
ERROR5   WTO   'ERASE NOT COMPLETED',ROUTCDE=(11)
         B     RETURNX
*
AERASE   DC    CL8'ERASE'                                     #NORAC#
*                                                             #NORAC#
ERADCB   DCB   DDNAME=ERASE,MACRF=E
IOB      IOB   CCH=Y,UNRL=Y,ECB=ECB,CCW=REWCCW,DCB=ERADCB
ECB      DC    F'0'
REWCCW   CCW   X'07',0,X'60',5             REWIND/LOAD  AND ...
READCCW  CCW   X'02',BUFFER,X'00',80       READ VOL1 LABEL
*
WRITCCW  CCW   X'01',HDRLB1,X'60',80       WRITE DUMMY HDR1 AND ...
TMCCW    CCW   X'1F',0,X'60',5             PUT TAPEMARK AND ...
TMCCW1   CCW   X'1F',0,X'60',5             PUT TAPEMARK AND ...
ERGCCW   CCW   X'17',0,X'60',5             ERASE GAP    AND ...
ERASCCW  CCW   X'97',0,X'20',5             SECURITY ERASE
*
ERGCCW1  CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'60',5             ERASE GAP    AND ...
         CCW   X'17',0,X'20',5             ERASE GAP
*
SENSCCW  CCW   X'04',SENSE,X'20',20        SENSE
*
HDRLB1   DC    CL4'HDR1',76C'0'            DUMMY HDR1 LABEL
SENSE    DC    XL20'00'
BUFFER   DS    CL80
         REGISTER
         END
