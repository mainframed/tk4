./  ADD NAME=ULEO020S
//DATUM    JOB (1407,O230),'RECHTIEN',CLASS=4,NOTIFY=LTSOHI,
//             MSGCLASS=6,REGION=1024K
//*
//*        MEMBER  DATUM IN LTSOHI.SP01.CNTL
//*
//  EXEC ASMFCL
//ASM.SYSIN DD *
         TITLE ' EDIT CURRENT DATE'
*********************************************************************
*
*        DATUM RETURNS DATE AS FOLLOWS:
*        DAY  TT.MON YEAR  (YY.TTT)  HH:MM:SS
*
*
*********************************************************************
         SPACE 2
DATUM    START
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3                   BASIS-REGISTER
R4       EQU   4                   HPRO-WORKAREA
R5       EQU   5                   WORK
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                  WORK
R11      EQU   11                  WORK
R12      EQU   12                  WORK
R13      EQU   13                  SYSTEM
R14      EQU   14                  BALR
R15      EQU   15                  SYSTEM
BLANK    EQU   X'40'
EIN      EQU   X'FF'
AUS      EQU   X'00'
         SPACE 3
         SAVE  (14,12)
         SPACE
         BALR  R3,0
         USING *,R3
         SPACE
         ST    R13,SAVE+4          STORE ADDRESS OF HIGHER SAVEAREA
         LA    R14,SAVE
         ST    R14,8(R13)          STORE ADDRESS OF LOWER SAVEAREA
         LR    R13,R14
         L     R4,0(R1)            SAVE CALLING PGM WORKAREA
         EJECT
         PRINT NOGEN
         SPACE 3
*---------------------------------------------------------------------*
*        PROGRAMM AUSFUEHRUNG
*---------------------------------------------------------------------*
         SPACE
PARMX    LA    R8,60
         L     R1,16                       CVT ADDRESS
         UNPK  LINEH1+032(5),57(3,R1)      UNPACK YYDDD FROM CVTDATE
         OI    LINEH1+036,X'F0'
         MVC   LINEH1+031(2),LINEH1+032    SHIFT YY LEFT
         MVI   LINEH1+033,C'.'             AND INSERT PERIOD
         TM    57(R1),X'01'                IS IT AN ODD YEAR
         BO    NOTLEAP                     YES, BRANCH
         TM    57(R1),X'12'                IS IT DIVISIBLE BY 4
         BM    NOTLEAP                     NO, BRANCH
         MVI   FEBSIZE+1,29                CHANGE FEB TO 29 DAYS
NOTLEAP  MVC   DOUBLE+6(2),58(R1)          GET PACKED DDD
         XC    DOUBLE(6),DOUBLE
         CVB   R0,DOUBLE                   CONVERT DDD TO BINARY
         LA    R15,MONTHS
MONLOOP  SH    R0,0(,R15)                  REDUCE DDD BY MONTH SIZE
         BNP   MONX                        STOP IF DDD NOT POSITIVE
         LA    R15,6(,R15)                 POINT TO NEXT MONTH
         B     MONLOOP
MONX     MVC   LINEH1+020(3),2(R15)        CURRENT MONTH
         AH    R0,0(,R15)          ADD MONTH SIZE FOR CURRENT DAY
         CVD   R0,DOUBLE
         UNPK  LINEH1+016(2),DOUBLE+6(2)   TT DEC
         OI    LINEH1+017,X'F0'
         MVI   LINEH1+018,C'.'
         MVC   LINEH1+026(2),LINEH1+031    YEAR
         ST    R0,DOUBLE
         LA    R14,MONTHS-6
         SR    R15,R14
         XR    R14,R14
         D     R14,F6
         SH    R15,H2
         BP    *+8
         AH    R15,H12
         IC    R0,57(,R1)
         SRL   R0,4
         N     R0,F15
         MH    R0,H10
         IC    R1,57(,R1)
         N     R1,F15
         AR    R1,R0
         CH    R15,H10
         BNH   *+6
         BCTR  R1,R0
         MH    R15,H13
         BCTR  R15,R0
         XR    R14,R14
         D     R14,F5
         LR    R0,R1
         SRL   R0,2
         AR    R15,R0
         SH    R15,H34
         A     R15,DOUBLE
         AR    R15,R1
         XR    R14,R14
         D     R14,F7
         MH    R14,H10                     LENGTH OF ARRAY MEMBER
         LA    R14,DAYS(R14)
         MVC   LINEH1+05(L'DAYS),0(R14)    DAY OF WEEK
         LA    R1,2                        TIME
         SVC   11                          TIME
         ST    R0,DOUBLE
         OI    DOUBLE+3,X'0F'
         MVC   LINEH1+039(9),TIMEPATT
         ED    LINEH1+039(9),DOUBLE        HH:MM:SS
         MVC   0(LLINE,R4),LINEH1          MOVE DATE TO CALLING PGM
         SPACE 3
*---------------------------------------------------------------------*
*        PROGRAMM ENDE
*---------------------------------------------------------------------*
EOF      EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         EJECT
*---------------------------------------------------------------------*
*        DEFINITIONEN
*---------------------------------------------------------------------*
SAVE     DS    18F
DOUBLE   DS    D
F5       DC    F'5'
F6       DC    F'6'
F7       DC    F'7'
F15      DC    F'15'
H2       DC    H'2'
H9       DC    H'9'
H10      DC    H'10'
H12      DC    H'12'
H13      DC    H'13'
H15      DC    H'15'
H34      DC    H'34'
         SPACE
WORK     DS    CL100
TIMEPATT DC    X'4021207A20207A2020'
LINEH1   DC    C'                '
         DC    C'        19    (      )'
         DC    C'          '
LLINE    EQU   *-LINEH1
DAYS     DC    C'SONNTAG   '
         DC    C'MONTAG    '
         DC    C'DIENSTAG  '
         DC    C'MITTWOCH  '
         DC    C'DONNERSTAG'
         DC    C'FREITAG   '
         DC    C'SAMSTAG   '
         DC    X'00'
MONTHS   DC    H'31'
         DC    C'JAN '
FEBSIZE  DC    H'28'
         DC    C'FEB '
         DC    H'31'
         DC    C'MAR '
         DC    H'30'
         DC    C'APR '
         DC    H'31'
         DC    C'MAI '
         DC    H'30'
         DC    C'JUN '
         DC    H'31'
         DC    C'JUL '
         DC    H'31'
         DC    C'AUG '
         DC    H'30'
         DC    C'SEP '
         DC    H'31'
         DC    C'OKT '
         DC    H'30'
         DC    C'NOV '
         DC    H'31'
         DC    C'DEZ '
         SPACE
         LTORG
         END
//LKED.SYSLMOD DD DSN=LEO1.SYSTEM.LINKLIB(DATUM),DISP=SHR
