 SYSTEM: PROC OPTIONS(MAIN);
 /***************************************************************/
 /*    SYSTEMSTATISTIK : W O R K L O A D             17.12.74   */
 /*    AUTOR : RAINER STRACKE     ZOD/R1                        */
 /*                                                             */
 /* EINGABE :  1.  SYSIN (GET DATA)                             */
 /*            MAXMON,MAXTAG,EINHMON,EINNHTAG   (DIMENSIONEN    */
 /*                                          DER SCHAUBILDER)   */
 /*            2.  GUELTIG  (READ FILE)                         */
 /*            ERSTTAG(10 CHAR),TAG MONAT JAHR (PIC'99')        */
 /*            (ERSTER WOCHENTAG DES JAHRES UND                 */
 /*            BEREICH DER ZU BERECHENENDEN SAETZE VON SMF)     */
 /*            3. BEWERT (SMF-BAND)   (4096-STELLIGER BEREICH)  */
 /* SAETZE : 0 (IPL), 4 (STEP), 34 (TSO-STEP), 40 (EXCP-DYN)    */
 /* WERDEN AUSGEWERTET.(SORTIERT NACH START-STOP-DATUM,ZEIT)    */
 /* DIESE WERTE WURDEN DURCH EINE SORTMODIFIKATION VERTAUSCHT.  */
 /* DIE MASCHINENEINHEITEN (ME) WERDEN MIT DEM UNTERPROGRAMM    */
 /* (RECHEN) ERMITTELT UND UEBER DIE ELAPSE-TIME GLEICHVERTEILT.*/
 /* NICHT IN DEN GUELTIGKEITSBEREICH (IM FILE GUELTIG ANGEGEBEN)*/
 /* FALLENDE SMF-SAETZE WERDEN IN EINER FEHLERLISTE(L3) AUSGE-  */
 /* WERTET. 40-IGER RECORDS WERDEN PRO USER UND SESSION KUMU-   */
 /* LIERT UND EINEM 34-RECORD (MIT ZUGEHOERIGEM USER) AUF DEN   */
 /* ERSTEN EXCP-COUNT (FALLS VORHANDEN) ADDIERT.                */
 /* RECORDS (4 UND 34) WERDEN DEM U.P. RECHEN UBERGEBEN,UND ES  */
 /* WERDEN DIE ME IM FELD SUMME (FIXED(9,2)) VON DIESEM GELIE-  */
 /* FERT. TAGESUEBERLAEUFE WERDEN IM U.P. (SONDFALL) BERUECK-   */
 /* SICHTIGT.                                                   */
 /* WORKLOAD ERSTELLT FOLGENE LISTEN:                           */
 /*  1. SYPRINT FEHLER BEI FALSCHER EINGABE UND SYSTEMFEHLER    */
 /*  2. L1      EIN SCHAUBILD PRO ARBEITSTAG FUER BATCH UND TSO */
 /*             UND EINE UEBER ALLE ARBEITSTAGE VERDICHTETE TA- */
 /*             GESSTATISTIK,WOBEI PRO STUNDE AUFADDIERT        */
 /*             UND DURCH ANZAHL DER ARBEITSSTUNDEN (^= 0) DI-  */
 /*             VIDIERT WIRD.                                   */
 /*  3. L2      EIN SCHAUBILD (A02) FUER DIE MONATSSTATISTIK    */
 /*             PRO ARBEITSTAG FUER BATCH UND TSO UND EIN       */
 /*             DECKBLATT FUER DIE GESAMTE SYSTEMSTATISTIK.     */
 /*  4. L3      EIN FEHLERPROTOKOLL FUER ALLE AUSSERHALB DES    */
 /*             GUELTIGKEITSBEREICHES FALLENDEN SMF-SAETZE.     */
 /*                                                             */
 /*  W O R K L O A D   IST EIN MASS IHRER SYSTEMAUSLASTUNG.     */
 /***************************************************************/
   DCL BEWERT FILE RECORD,GUELTIG FILE RECORD
       ENV(F,RECSIZE(80),BLKSIZE(80)),
       (L1,L2,L3) FILE PRINT;
   DCL 1 SATZ4 BASED(P1),
        2 F1 CHAR(1),
        2 RECORD BIT(8),
        2 SZEITHS BIN FIXED(31) UNAL,
        2 SDATUM FIXED(7),
        2 F2 CHAR(29),
        2 EZEITHS BIN FIXED(31) UNAL,
        2 EDATUM FIXED(7),
        2 F3 CHAR(55),
        2 LE4 BIN FIXED(15) UNAL,
        2 V4 CHAR(3992), VX4(3992) CHAR(1) BASED(VP1),
       1 SATZ34 BASED(P1),
        2 F1 CHAR(2),
        2 LOGON_T BIN FIXED(31) UNAL,
        2 LOGON_D FIXED(7),
        2 F2 CHAR(4),
        2 USERID CHAR(8),
        2 LOGOFF_T BIN FIXED(31) UNAL,
        2 LOGOFF_D FIXED(7),
        2 F3 CHAR(72),
        2 LE34 BIN FIXED(15) UNAL,
        2 V34 CHAR(3992), VX34(3992) CHAR(1) BASED(VP1),
       1 SATZ40 BASED(P1),
        2 F1 CHAR(14),
        2 USER40 CHAR(8),
        2 F2 CHAR(38),
        2 LE40 BIN FIXED(15) UNAL,
        2 V40 CHAR(4034), VX40(4034) CHAR(1) BASED(VP2);
   DCL 1 DEVICE BASED(P3),
        2 CLA BIT(16),
        2 ADR CHAR(2),
        2 ZUG BIN FIXED(31) UNAL,
  UID(200) CHAR(8);
   DCL 1 EDATS,
        2 F1 CHAR(2),
        2 EJAHR PIC'99',
        2 ELTAG PIC'999',
   ERD PIC'(7)9' DEF EDATS,
       1 SDATS,
        2 F1 CHAR(2),
        2 SJAHR PIC'99',
        2 SLTAG PIC'999',
   SRD PIC'(7)9' DEF SDATS,
   CSATZ FIXED(1) BASED(Q1),
   RECHEN ENTRY;
   DCL 1 SGUEL, 2 ERSTTAG CHAR(10),
   2 VONTAG PIC'99', 2 VONMON PIC'99', 2 VONJR PIC'99',
   2 BISTAG PIC'99', 2 BISMON PIC'99', 2 BISJR PIC'99',2 R CHAR(58);
   DCL WOCHTAG(7) CHAR(10),(MINLTAG,MAXLTAG) PIC'999',
   (MAXTAG,MAXMON,EINHTAG,EINHMON,STEP,MAX1,MAXU,MAXO) FIXED(11,2);
   DCL (MON(12),STAG,SMON,ETAG,EMON,VGLTAG,VGLMON,VGLJR,UEBTAG,
   IPLSZ(24),FTAG,FMON,FJR,LASTTAG,LASTMON,LASTJR) BIN FIXED,
   (UEBSUM,SUMME) FIXED(9,2), IPLSTD(24) BIN FIXED,
   (TMONSUM,BMONSUM,KOSTMIN,UEBKOSTMIN) FIXED(11,2),
   (TMIN(1440),BMIN(1440),TUEB(1440),BUEB(1440)) FIXED(9,2);
   DCL (TESUMUEB,BESUMUEB,TTAGSUM,BTAGSUM,BTMIT,BTSTD(24),
   TSTD(24),BSTD(24),TSTDMIT(24),BSTDMIT(24),BTMONMIT,
   TSUMTAG(40),BSUMTAG(40),BTTAG(40)) FIXED(11,2);
   DCL (MPTAG(40),IPLSUM(40)) BIN FIXED,
   TAGMON(40) CHAR(2),(IKB(24),IKT(24)) FIXED(9,2),
   (UEBZEIT,EZEITM,SZEITM,STEPZEIT) FIXED(11,2);
   DCL (BFEHL,TFEHL,BSUMS,TSUMS) FIXED(11,2);
   DCL (ZUG40SUM(200),Z40FE) BIN FIXED(31),
   GLEICH CHAR(132) INIT((132)'=');
   DCL (BSTDSA,TSTDSA) FIXED(11,2);
   DCL (IBFE,ITFE,IPLFE,IPLMOSUM) BIN FIXED(31);
   ISCHALT,IK=0;
   UID=(8)'X';
   BSTDMIT,TSTDMIT=0; IPLSZ=0; IZUPF=0;
   MON(1),MON(3),MON(5),MON(7),MON(8),MON(10),MON(12)=31;
   MON(4),MON(6),MON(9),MON(11)=30;
   STAG,SMON,ETAG,EMON,VGLTAG,ITAG,IVTAG,VGLMON,VGLJR=0;
   FTAG,FMON,FJR,LASTTAG,LASTMON,LASTJR=0;
   LTZW1,LTZW2,TMONSUM,BMONSUM,EZEITM,SZEITM,UEBZEIT=0;
   ZUG40SUM=0; IZEICH=0;
   TESUMUEB,BESUMUEB,TTAGSUM,BTAGSUM=0;
   BSTDSA,TSTDSA=0;
   IKB,IKT=0;
   MINJR=999; MINLTAG=366;
   MAXJR=0; MAXLTAG=0;
   IPLFE,Z40FE,TFEHL,BFEHL,IBFE,ITFE=0;
   TMIN,BMIN,TUEB,BUEB=0;
   IPLSUM=0; IPLMOSUM=0;
   GET DATA(MAXTAG,MAXMON,EINHTAG,EINHMON);
   IF EINHMON=0 × EINHTAG=0 THEN DO;
   PUT SKIP LIST('EINHMON ODER EINHTAG GLEICH 0');
   GO TO ENDE;
   END;
 /* WERTE FUER SCHAUBILDER (1. TAG DES JAHRES,DIMENSIONEN */
   READ FILE(GUELTIG) INTO(SGUEL);
 /*GUELTIGKEITSBEREICH DER STATISTIK BZGL. DES DATUMS */
   IF MOD(BISJR,4)=0 THEN MON(2)=29; /* SCHALTJAHR */
   ELSE MON(2)=28;
   OPEN FILE(L1) PAGESIZE(70);
   OPEN FILE(L2) PAGESIZE(70);
   OPEN FILE(L3) PAGESIZE(70);
   OPEN FILE(BEWERT) INPUT; /* SMF-BAND */
   ON ENDFILE(GUELTIG) BEGIN;
   PUT SKIP LIST
 ('KEIN KARTE FUER G.BEREICH ODER KEINE FOLGEKARTE BEI JAHRESWECHSEL');
   GO TO ENDE;
   END;
   ON ENDFILE(BEWERT) BEGIN;
   ISCHALT=1;
   IF IZEICH=0 THEN GO TO ENDE;
   CALL RECHNEN;
   GO TO MONAT;
   END;
   IF VONMON=1 THEN GO TO RI1;
   DO I=1 TO (VONMON-1);
   LTZW1=LTZW1+MON(I);
   END;
 RI1: LTZW1=LTZW1+VONTAG; /* LAUFENDER TAG LTZW1 */
   IF BISMON=1 THEN GO TO RI2;
   DO I=1 TO (BISMON-1);
   LTZW2=LTZW2+MON(I);
   END;
 RI2: LTZW2=LTZW2+BISTAG;
   ON ERROR CALL PLIDUMP('T,F,C,H,B,A');
   ON SUBRG BEGIN;
   PUT SKIP LIST('UEBERLAUF EINES INDEX');
   GO TO ENDE;
   END;
   WOCHTAG(1)='MONTAG    ';
   WOCHTAG(2)='DIENSTAG  ';
   WOCHTAG(3)='MITTWOCH  ';
   WOCHTAG(4)='DONNERSTAG';
   WOCHTAG(5)='FREITAG   ';
   WOCHTAG(6)='SAMSTAG   ';
   WOCHTAG(7)='SONNTAG   ';
 WOCHE: PROC;
   DO I=1 TO 7;
   IF WOCHTAG(I) ^= ERSTTAG THEN GO TO HAS1;
   IWT=I;
   RETURN;
 HAS1: END;
   PUT SKIP LIST('KEIN GUELTIGER ERSTTAG WURDE EINGEGEBEN');
   DO I=1 TO 7;
   PUT SKIP DATA(WOCHTAG(I));
   END;
   GO TO ENDE;
   END WOCHE;
   CALL WOCHE;
 LES1:
   READ FILE(BEWERT) SET(P1); /* 1. GUELTIGER SMF-SATZ GESUCHT */
   IF RECORD ^= 0 & RECORD ^= 4 & RECORD ^= 34 & RECORD ^= 40
   THEN GO TO LES1;
   SRD=SDATUM;
   IF SJAHR > BISJR × SJAHR < VONJR
    × SLTAG +365*SJAHR > LTZW2 +365*BISJR
    × SLTAG +365*SJAHR < LTZW1 +365*VONJR THEN DO;
   IF RECORD = 34 THEN CALL U34;
   IF RECORD = 34 × RECORD = 4 THEN DO;
   Q1=ADDR(SATZ4);
   CALL RECHEN(CSATZ,SUMME);
   END;
   IF RECORD=40 THEN CALL U40;
   CALL FEHLER;
   GO TO LES1;
   END;
   IF MOD(SJAHR,4)=0 THEN MON(2)=29;
   ELSE MON(2)=28;
   CALL KALTAG(SLTAG,STAG,SMON);
   IF RECORD = 40 THEN DO;
   CALL U40;
   GO TO LES1;
   END;
   IF RECORD = 0 THEN DO;
   CALL IPL;
   GO TO LES1;
   END;
   IF RECORD = 34 THEN DO;
   ERD=LOGOFF_D;
   CALL U34;
   END;
   IF RECORD = 4 THEN ERD=EDATUM;
   CALL ERSTDAT;
   IZEICH=1;
   VGLTAG,UEBTAG=STAG;
   IVTAG=ITAG;
   VGLMON=SMON;
   VGLJR=SJAHR;
   GO TO F1;
 LES2:
   READ FILE(BEWERT) SET(P1);
   IF RECORD ^= 0 & RECORD ^= 4 & RECORD ^= 34 & RECORD ^= 40
   THEN GO TO  LES2;
   SRD=SDATUM;
   IF SJAHR > BISJR × SJAHR < VONJR
    × SLTAG +365*SJAHR > LTZW2 +365*BISJR
    × SLTAG +365*SJAHR < LTZW1 +365*VONJR THEN DO;
   IF RECORD = 34 THEN CALL U34;
   IF RECORD = 34 × RECORD = 4 THEN DO;
   Q1=ADDR(SATZ4);
   CALL RECHEN(CSATZ,SUMME);
   END;
   IF RECORD=40 THEN CALL U40;
   CALL FEHLER;
   GO TO LES2;
   END;
   IF VGLJR ^= SJAHR THEN CALL WOCHE;
   IF MOD(SJAHR,4)=0 THEN MON(2)=29; ELSE MON(2)=28;
   CALL KALTAG(SLTAG,STAG,SMON);
   CALL LASTDAT;
   IF VGLTAG ^= STAG THEN
   CALL RECHNEN;     /* TAGES-HANDLING */
   IF RECORD = 0 THEN DO;
   CALL IPL; GO TO LES2;
   END;
   IF RECORD = 40 THEN DO;
   CALL U40;
   GO TO LES2;
   END;
   IF RECORD = 4 THEN DO;
   ERD=EDATUM; GO TO F1;
   END;
   IF RECORD = 34 THEN DO;
   ERD=LOGOFF_D;
   CALL U34;
   END;
 F1:
   CALL KALTAG(ELTAG,ETAG,EMON);
   Q1=ADDR(SATZ4);
   CALL RECHEN(CSATZ,SUMME);
   IF RECORD = 34 THEN
   TMONSUM=TMONSUM+SUMME;
    ELSE BMONSUM=BMONSUM+SUMME;
   CALL ZEITBER;
   IF STAG ^= ETAG THEN DO;
   CALL SONDFALL;
   GO TO VOR1;
   END;
   IF EZEITM < SZEITM THEN GO TO LES2;
   STEPZEIT=EZEITM-SZEITM;
 VOR1:
   IF RECORD = 34 THEN
   TTAGSUM=TTAGSUM+SUMME;
   ELSE BTAGSUM=BTAGSUM+SUMME;
   IF STEPZEIT=0 THEN DO;
   STEPZEIT=1;
   IF EZEITM < 1439 THEN EZEITM=EZEITM+1;
   ELSE SZEITM=SZEITM-1;
   END;
   IF EZEITM > 1440 THEN EZEITM=1440;
   IF SZEITM=0 THEN DO;
   SZEITM=1;
   STEPZEIT=STEPZEIT-1;
   END;
   KOSTMIN=SUMME/(STEPZEIT+1);
   IF RECORD = 34 THEN DO;
   DO I=SZEITM TO EZEITM;
   TMIN(I)=TMIN(I) + KOSTMIN;
   END;
   END;
   ELSE DO;
   DO I=SZEITM TO EZEITM;
   BMIN(I)=BMIN(I) + KOSTMIN;
   END;
   END;
   GO TO LES2;
 MONAT:   IPLMOSUM=0;
   DO I=1 TO IK;
   IPLMOSUM=IPLMOSUM+IPLSUM(I);
   END;
   CALL KOPF2;  /* MONATSSTATISTIK */
   CALL SCHAU2;
   CALL KOPF3;
   CALL SCHAU1; /* VERDICHTUNG UEBER EINEN TAG */
 ENDE:
   CALL BLATT;
   CALL FEHLERL;
   CLOSE FILE(BEWERT);
   CLOSE FILE(L1);  /* SCHAU1 MIT KOPF1+KOPF3 */
   CLOSE FILE(L2);  /* SCHAU2 MIT KOPF2 */
   CLOSE FILE(L3);  /* FEHLERLISTE */
 U40: PROC;
   DO J=1 TO 200 WHILE(UID(J) ^= (8)'X');
   IF USER40 ^= UID(J) THEN GO TO UEND;
   GO TO U40WEI;
 UEND:
   END;
 U40WEI:  IF J > 200 THEN DO;
   PUT SKIP LIST('MEHR ALS 200 USER,TABELLE UEBERGELAUFEN');
   GO TO U40END;
   END;
   UID(J) = USER40;
   VP2=ADDR(V40);
   DO J1=1 TO (LE40-2) BY 8;
   P3=ADDR(VX40(J1));
   ZUG40SUM(J)=ZUG40SUM(J)+ZUG;
   END;
 U40END: END U40;
 U34: PROC;
   DO J=1 TO 200 WHILE(UID(J) ^= (8)'X');
   IF USERID=UID(J) THEN DO;
   P3=ADDR(V34);
   IF LE34 < 10 THEN DO; ZUG40SUM(J)=0; RETURN; END;
   ZUG=ZUG+ZUG40SUM(J);
   ZUG40SUM(J)=0;
   CLA='0010000000001001'B; /* 2009 -> DEVICE */
   GO TO U34END;
   END;
   END;
   U34END: END U34;
 ZEITBER: PROC;
   IF RECORD = 34 THEN DO;
   SZEITM=LOGON_T/6000;
   EZEITM=LOGOFF_T/6000;
   END;
   ELSE DO;
   SZEITM=SZEITHS/6000;
   EZEITM=EZEITHS/6000;
   END;
   END ZEITBER;
 SONDFALL: PROC;
   UEBZEIT=EZEITM;
   IF UEBZEIT=0  THEN UEBZEIT=1;
   STEPZEIT=1440-SZEITM;
   EZEITM=1440;
   UEBSUM=(SUMME*UEBZEIT)/(STEPZEIT+UEBZEIT);
   SUMME=SUMME-UEBSUM;
   UEBKOSTMIN=UEBSUM/UEBZEIT;
   DO J=1 TO UEBZEIT;
   IF RECORD = 34 THEN TUEB(J)=TUEB(J)+UEBKOSTMIN;
                  ELSE BUEB(J)=BUEB(J)+UEBKOSTMIN;
   END;
   UEBTAG=STAG+1;
   IF RECORD = 34 THEN TESUMUEB=TESUMUEB+UEBSUM;
                  ELSE BESUMUEB=BESUMUEB+UEBSUM;
   END SONDFALL;
 IPL: PROC;
   ISZ=SZEITHS/360000; /* STUNDEN-IPL-ZAEHLER */
   ISZ=ISZ+1;
   IF ISZ > 24 THEN ISZ=24;
   IPLSZ(ISZ)=IPLSZ(ISZ)+1;
   END IPL;
 ERSTDAT: PROC;
   FTAG=STAG; FMON=SMON; FJR=SJAHR;
   END ERSTDAT;
 LASTDAT: PROC;
   LASTTAG=STAG; LASTMON=SMON; LASTJR=SJAHR;
   END LASTDAT;
 FEHLER: PROC;
   IZUPF=1;
   IF SJAHR < MINJR THEN MINJR=SJAHR;
   IF SJAHR > MAXJR THEN MAXJR=SJAHR;
   IF SLTAG + 365*SJAHR < MINLTAG + 365*MINJR THEN
   MINLTAG=SLTAG;
   IF SLTAG + 365*SJAHR > MAXLTAG + 365*MAXJR THEN
   MAXLTAG=SLTAG;
   IF RECORD = 0 THEN IPLFE=IPLFE+1;
   IF RECORD = 40 THEN Z40FE=Z40FE+1;
   IF RECORD = 34 THEN DO;
   TFEHL=TFEHL+SUMME;
   ITFE=ITFE+1;
   END;
   IF RECORD=4 THEN DO;
   BFEHL=BFEHL+SUMME;
   IBFE=IBFE+1;
   END;
   END FEHLER;
 KALTAG: PROC(XLTAG,XTAG,XMON);
   DCL (XTAG,XMON) BIN FIXED, XLTAG PIC'999';
   IZ2=0;
   DO I=1 TO 12;
   IZ1=IZ2;
   IZ2=IZ2+MON(I);
   IF XLTAG <= IZ2 THEN DO;
   XMON=I;
   GO TO DRAU;
   END;
   END;
 DRAU: XTAG=XLTAG-IZ1;
   ITAG=MOD(XLTAG+IWT-1,7);
   IF ITAG=0 THEN ITAG=7;
   END KALTAG;
 RECHNEN: PROC;
 /* TAGESABRECHNUNG */
   TSUMS,BSUMS=0;
   DO I=1 TO 1440;
   TSUMS=TSUMS+TMIN(I);
   BSUMS=BSUMS+BMIN(I);
   IF MOD(I,60) ^= 0 THEN GO TO GU1;
   L=I/60;
   TSTD(L)=TSUMS;
   BSTD(L)=BSUMS;
   IF TSTD(L) >1 THEN DO; TSTDMIT(L)=TSTDMIT(L)+TSTD(L);
   IKT(L)=IKT(L)+1; END;
   IF BSTD(L) >1 THEN DO; BSTDMIT(L)=BSTDMIT(L)+BSTD(L);
   IKB(L)=IKB(L)+1; END;
   TSUMS,BSUMS=0;
 GU1: END;
   IK=IK+1;
   IF IK > 40 THEN DO; PUT SKIP LIST
   ('MEHR ALS 40 ARBEITSTAGE NICHT MOEGLICH');
   GO TO ENDE;
   END;
   TSUMTAG(IK)=TTAGSUM;
   BSUMTAG(IK)=BTAGSUM;
   CALL KOPF1;
   CALL SCHAU1;
   IF ISCHALT=1 THEN DO; ISCHALT=2; RETURN; END;
   DO I=1 TO 1440;
   TMIN(I)=TUEB(I);
   BMIN(I)=BUEB(I);
   END;
   TUEB,BUEB=0;
   TTAGSUM=TESUMUEB;
   BTAGSUM=BESUMUEB;
   BESUMUEB,TESUMUEB=0;
 SO:
   IF UEBTAG=STAG × UEBTAG=VGLTAG THEN DO;
   VGLTAG,UEBTAG=STAG;
   IVTAG=ITAG;
   VGLMON=SMON;
   VGLJR=SJAHR;
   RETURN;
   END;
   IF VGLMON ^= SMON THEN DO;
   UEBTAG=STAG;
   GO TO SO;
   END;
   END RECHNEN;
 BLATT: PROC;
   PUT PAGE FILE(L2) EDIT
   ('S M F','S M F','S M F','S M F')
   (SKIP(15),X(26),(4)(X(4),A));
   PUT FILE(L2) SKIP(3) EDIT
   (FTAG,'.',FMON,'.',FJR,'  -  ',LASTTAG,'.',LASTMON,'.',LASTJR)
   (COL(36),(5)(P'99',A),P'99');
   PUT FILE(L2) EDIT
   ('     T S O (ID,LOG,MO,TP,TG,EXCP) FUER (TSO)',
    'A    W O R K L O A D     (ME,IPL)   "  (BATCH + TSO)',
    'B    C P U L O A D (ELA,WAIT,OVH)   "  (BATCH + TSO)',
    'C    C O R E L O A D  (RF,RG,MPF)   "  (BATCH)',
    'D    E X C P               (UNIT)   "  (BATCH + TSO)',
    'E    B E L E G U N G       (UNIT)   "  (BATCH + TSO)',
    'F    S T E P E L A P S E            "  (BATCH)',
    'G    P A G E                        "  (BATCH + TSO)',
    'H    O V E R H E A D + W A I T      "  (BATCH + TSO)')
   ((9)(SKIP(3),X(18),A));
   END BLATT;
  FEHLERL: PROC; /* FEHLERLISTE */
   IF IZUPF=0 THEN DO;
   STAG,SMON,ETAG,EMON,MINJR,MAXJR=0;
   GO TO DRU;
   END;
   CALL KALTAG(MINLTAG,STAG,SMON);
   CALL KALTAG(MAXLTAG,ETAG,EMON);
  DRU: PUT PAGE FILE(L3) EDIT
   ('F E H L E R L I S T E  DER  W O R K L O A D')(SKIP(3),A);
   PUT FILE(L3) SKIP(2) EDIT
   ('MINIMALES DATUM  ',STAG,'.',SMON,'.',MINJR,'BIS  ',
    ETAG,'.',EMON,'.',MAXJR)
   (COL(10),(3)(A,P'99'),COL(40),(3)(A,P'99'));
   PUT FILE(L3) SKIP(2) EDIT
   ('RECORD  00','(IPL)',IPLFE,' X GESCHRIEBEN')
   (R(FORMF1));
   PUT FILE(L3) SKIP(2) EDIT
   ('RECORD  04','(BATCH)',IBFE,' X GESCHRIEBEN')(R(FORMF1));
   PUT FILE(L3) EDIT
   ('MIT SUMME  ',BFEHL,' (ME)')(R(FORMF2));
   PUT SKIP(2) FILE(L3) EDIT
   ('RECORD  34','(TSO)',ITFE,' X GESCHRIEBEN')(R(FORMF1));
   PUT FILE(L3) EDIT
   ('MIT SUMME  ',TFEHL,' (ME)')(R(FORMF2));
   PUT FILE(L3) SKIP(2) EDIT
   ('RECORD  40','(EXCP-DYN)',Z40FE,' X GESCHRIEBEN')(R(FORMF1));
   PUT FILE(L3) SKIP EDIT((90)'-')(COL(5),A);
   PUT FILE(L3) SKIP EDIT
   ('GESAMT',IPLFE+ITFE+IBFE+Z40FE,BFEHL+TFEHL,' (ME)')
   (SKIP,COL(5),A,COL(37),P'ZZ.ZZZ.ZZ9',COL(78),P'ZZ.ZZZ.ZZ9',A);
   PUT SKIP(2) FILE(L3) EDIT
   ('FUER DEN GUELTIGKEITSBEREICH ',FTAG,'.',FMON,'.',FJR,
    '  BIS  ',LASTTAG,'.',LASTMON,'.',LASTJR,
    'WURDEN OBIGE SMF-RECORDS IN DER WORKLOADSTATISTIK ',
    'NICHT BERUECKSICHTIGT !')
   (COL(5),(6)(A,P'99'),SKIP(2),COL(5),A,A);
 FORMF1: FORMAT(COL(5),A,COL(20),A,COL(37),P'ZZ.ZZZ.ZZ9',A);
 FORMF2: FORMAT(COL(64),A,COL(78),P'ZZ.ZZZ.ZZ9',A);
 END FEHLERL;
 KOPF1: PROC;
   BTMIT=0;
   DO I=1 TO 24;
   IPLSUM(IK)=IPLSUM(IK)+IPLSZ(I);
   BTSTD(I)=BSTD(I)+TSTD(I);
   END;
   BTMIT=(BSUMTAG(IK)+TSUMTAG(IK))/24;
   TAGMON(IK)=SUBSTR(WOCHTAG(IVTAG),1,2);
   MPTAG(IK)=VGLTAG;
   PUT FILE(L1) PAGE EDIT
   ('T A G E S S T A T I S T I K  DER SYSTEMLAST VOM ',
   WOCHTAG(IVTAG),' DEN ',VGLTAG,'.',VGLMON,'.',VGLJR,
   ' IN ME (BATCH + TSO)','A',IK+2)
   (SKIP,COL(10),A,A,(3)(A,P'99'),A,COL(123),A,P'99');
   PUT FILE(L1) SKIP(2) EDIT
   ('SUMME/TAG','SCHNITTSUMME','SUMME IPL')
   (COL(31),A,COL(56),A,COL(79),A);
   PUT FILE(L1) SKIP(2) EDIT
   ('BATCH  :',BSUMTAG(IK),'  (ME)',BSUMTAG(IK)/24,'  (ME)')
   (R(FORMK1));
   PUT FILE(L1)  EDIT
   (IPLSUM(IK))(COL(85),P'ZZ9');
   PUT SKIP FILE(L1) EDIT
   ('TSO    :',TSUMTAG(IK),'  (ME)',TSUMTAG(IK)/24,'  (ME)')
   (R(FORMK1));
   PUT FILE(L1) SKIP EDIT
   ('GESAMT :',BSUMTAG(IK)+TSUMTAG(IK),'  (ME)',
    BTMIT,'  (ME)')(R(FORMK1));
 FORMK1: FORMAT(COL(5),A,COL(21),P'ZZ.ZZZ.ZZ9V,99',A,COL(49),
                P'ZZ.ZZZ.ZZ9V,99',A);
   END KOPF1;
 SCHAU1: PROC;
   PUT FILE(L1) SKIP EDIT(GLEICH)(A);
   PUT FILE(L1) SKIP EDIT('=BATCH   ')(A);
   DO I=1 TO 24;
   PUT FILE(L1) EDIT(BSTD(I))(F(5));
   END;
   PUT FILE(L1) EDIT('=')(COL(132),A);
   PUT FILE(L1) SKIP EDIT
   ('=TSO     ',(TSTD(I) DO I=1 TO 24),'=')(A,(24)(F(5)),COL(132),A);
   PUT FILE(L1) SKIP EDIT
   ('=','(ME)','=')(A,COL(6),A,COL(132),A);
   MAX1=MAXTAG;
   MAXU=MAX1-0.5*EINHTAG;
   MAXO=MAX1+0.5*EINHTAG;
   DO STEP=MAXTAG TO EINHTAG BY -EINHTAG;
   MERK1=0;
   PUT SKIP FILE(L1) EDIT('=',MAX1,' I')(A,F(6),A);
   DO I=1 TO 24;
   IF BTMIT >= MAXU & BTMIT < MAXO THEN DO;
   PUT FILE(L1) EDIT('---')(A); MERK1=1; GO TO DOS1;
   END;
   PUT FILE(L1) EDIT((3)' ')(A);
 DOS1:
   IF BSTD(I) >= MAXU THEN DO;
   PUT FILE(L1) EDIT('**')(A); GO TO DOS2;
   END;
   IF BTSTD(I) >= MAXU THEN PUT FILE(L1) EDIT('++')(A);
   ELSE DO;
   IF MERK1=1 THEN DO; PUT FILE(L1) EDIT('--')(A); GO TO DOS2;END;
   PUT FILE(L1) EDIT('  ')(A);
   END;
 DOS2:
   END;
   PUT FILE(L1) EDIT('=')(COL(132),A);
   MAX1=MAX1-EINHTAG;
   MAXU=MAX1-0.5*EINHTAG;
   MAXO=MAX1+0.5*EINHTAG;
   END;
   PUT FILE(L1) SKIP EDIT('=       I')(A);
   DO I=1 TO 24;
   PUT FILE(L1) EDIT('---II')(A);
   END;
   PUT FILE(L1) EDIT('->=')(A);
   IF ISCHALT=2 THEN DO;
   PUT FILE(L1) EDIT
   ('=ASTD(T) ',(IKT(I) DO I=1 TO 24),'  =','=ASTD(B) ',
   (IKB(I) DO I=1 TO 24))
   (A,(24)(P'ZZZZ9'),A,SKIP,A,(24)(P'ZZZZ9')); END;
   ELSE DO;
   PUT FILE(L1) SKIP EDIT('=IPL/STD ')(A);
   PUT FILE(L1) EDIT((IPLSZ(I) DO I=1 TO 24))((24)(P'(5)Z'));
   END;
   IPLSZ=0;
   PUT FILE(L1) EDIT('  =','=        ')(A,SKIP,A);
   PUT FILE(L1) EDIT((I DO I=1 TO 24))((24)(P'ZZZZ9'));
   PUT FILE(L1) EDIT('  =','=','=') (A,SKIP,A,COL(132),A);
   PUT FILE(L1) SKIP EDIT
   ('=  BATCH **   TSO ++   SCHNITT (BATCH + TSO) ---',
    'TAGESZEIT (STD)','=')(A,COL(115),A,COL(132),A);
   PUT SKIP FILE(L1) EDIT(GLEICH)(A);
   END SCHAU1;
 KOPF3: PROC;
   BTMIT=0;
   DO I=1 TO 24;
   IF IKT(I)^=0 THEN TSTD(I)=TSTDMIT(I)/IKT(I) + 0.05;
   IF IKB(I)^=0 THEN BSTD(I)=BSTDMIT(I)/IKB(I) + 0.05;
   BTSTD(I)=BSTD(I) + TSTD(I);
   BTMIT=BTMIT+BTSTD(I);
   BSTDSA=BSTDSA+BSTD(I); TSTDSA=TSTDSA+TSTD(I);
   END;
   BTMIT=BTMIT/24;
   PUT FILE(L1) PAGE EDIT
   ('VERDICHTETE  T A G E S S T A T I S T I K  DER SYSTEMLAST VOM ',
   FTAG,'.',FMON,'.',FJR,' BIS ',LASTTAG,'.',LASTMON,'.',LASTJR,
   ' IN ME (BATCH + TSO)','A01')
   (SKIP,COL(5),(6)(A,P'99'),A,COL(123),A);
   PUT FILE(L1) SKIP(2) EDIT
   ('MITTLERE SUMME/ATAG','MITTLERE SUMME/STD')
   (COL(21),A,COL(50),A);
   PUT FILE(L1) EDIT
   ('BATCH  :',BMONSUM/IK,'  (ME)',BSTDSA/24,'  (ME)',
   'TSO    :',TMONSUM/IK,'  (ME)',TSTDSA/24,'  (ME)',
   'GESAMT :',(BMONSUM+TMONSUM)/IK,'  (ME)',
   BSTDSA/24 + TSTDSA/24,'  (ME)')
   ((3)R(FORMK3));
 FORMK3: FORMAT(SKIP,COL(5),A,COL(21),P'ZZ.ZZZ.ZZ9V,99',A,
                COL(49),P'ZZ.ZZZ.ZZ9V,99',A);
   END KOPF3;
 KOPF2: PROC;
   DO I=1 TO IK;
   BTTAG(I)=BSUMTAG(I)+TSUMTAG(I); END;
   BTMONMIT=(BMONSUM+TMONSUM)/IK;
   PUT FILE(L2) PAGE EDIT
   ('M O N A T S S T A T I S T I K  DER SYSTEMLAST VOM ',
    FTAG,'.',FMON,'.',FJR,' BIS ',LASTTAG,'.',LASTMON,'.',LASTJR,
   ' IN ME (BATCH + TSO)','A02')
   (SKIP,COL(10),(6)(A,P'99'),A,COL(123),A);
   PUT FILE(L2) SKIP(2)  EDIT
   ('SUMME/MONAT','SCHNITTSUMME/TAG','FEHLERPROTOKOLL (AUSSERHALB',
    'SUMME IPL','ARBEITSTAGE')
   (COL(23),A,COL(50),A,COL(75),A,COL(108),A,COL(119),A);
   PUT FILE(L2) SKIP EDIT
   ('DES GUELTIGKEITSBEREICHES)')(COL(76),A);
   PUT FILE(L2) SKIP EDIT
   ('BATCH  :',BMONSUM,'  (ME)',BMONSUM/IK,'  (ME)',BFEHL, ' (ME)',
   IBFE,' (R 4)',IPLMOSUM,IK)
   (R(FORMK21),COL(110),P'Z.ZZ9',COL(122),P'ZZ.ZZ9');
   PUT FILE(L2) SKIP EDIT
   ('TSO    :',TMONSUM,'  (ME)',TMONSUM/IK,'  (ME)',TFEHL,' (ME)',
   ITFE,' (R34)')
   (R(FORMK21));
   PUT FILE(L2) SKIP EDIT
   ('GESAMT :',BMONSUM+TMONSUM,'  (ME)',(BMONSUM+TMONSUM)/IK,'  (ME)',
    BFEHL+TFEHL,' (ME)',IBFE+ITFE,' ')(R(FORMK21));
 FORMK21: FORMAT (COL(5),A,COL(21),P'ZZ.ZZZ.ZZ9V,99',A,COL(49),
    P'ZZ.ZZZ.ZZ9V,99',A,COL(73),P'ZZ.ZZZ.ZZ9V,99',A,COL(91),
    P'ZZZZ.ZZ9',A);
   END KOPF2;
 SCHAU2: PROC;
   PUT FILE(L2) SKIP EDIT
   (SUBSTR(GLEICH,1,12+4*IK))(A);
   PUT FILE(L2) SKIP EDIT
   ('=BATCH/IN',((BSUMTAG(I)+50)/100 DO I=1 TO IK),'  =')
   (A,(IK)(P'ZZZ9'),A);
   PUT FILE(L2) SKIP EDIT
   ('=TSO /100',((TSUMTAG(I)+50)/100 DO I=1 TO IK),'  =')
   (A,(IK)(P'ZZZ9'),A);
    PUT FILE(L2) SKIP EDIT
   ('=','(ME)','=')(A,COL(5),A,COL(12+4*IK),A);
   MAX1=MAXMON;
   MAXU=MAX1-0.5*EINHMON;
   MAXO=MAX1+0.5*EINHMON;
   DO STEP=MAXMON TO EINHMON  BY -EINHMON;
   MERK1=0;
   PUT FILE(L2) SKIP EDIT
   ('=',MAX1,' I')(A,F(6),A);
   DO I=1 TO IK;
   IF BTMONMIT >= MAXU & BTMONMIT < MAXO THEN DO;
   PUT FILE(L2) EDIT('--')(A); MERK1=1; GO TO BOS1; END;
   PUT FILE(L2) EDIT('  ')(A);
 BOS1: IF BSUMTAG(I) >= MAXU THEN DO;
   PUT FILE(L2) EDIT('**')(A); GO TO BOS2;
   END;
   IF BTTAG(I) >= MAXU THEN
   PUT FILE(L2) EDIT('++')(A);
   ELSE DO;
   IF MERK1=1 THEN DO; PUT FILE(L2) EDIT('--')(A); GO TO BOS2;  END;
   PUT FILE(L2) EDIT('  ')(A);
   END;
 BOS2: END;
   PUT FILE(L2) EDIT('  =')(A);
   MAX1=MAX1-EINHMON;
   MAXU=MAX1-0.5*EINHMON;
   MAXO=MAX1+0.5*EINHMON;
   END;
   PUT FILE(L2) SKIP EDIT
   ('=','I',('--II' DO I=1 TO IK),'->=')
   (A,COL(9),A,(IK)(A),A);
   PUT FILE(L2) SKIP EDIT
   ('=IPL/TAG ',(IPLSUM(I) DO I=1 TO IK),'  =')
   (A,(IK)(P'BZZZ'),A);
   PUT FILE(L2) SKIP EDIT
   ('=DATUM   ',(MPTAG(I) DO I=1 TO IK),'  =')
   (A,(IK)(P'BZZ9'),A);
   PUT FILE(L2) SKIP EDIT
   ('=',(TAGMON(I) DO I=1 TO IK),'  =')
   (A,COL(10),(IK)(X(2),A),A);
   PUT FILE(L2) SKIP EDIT
   ('=','ZEIT (TAGE)','=')(A,COL(4*IK),A,COL(12+4*IK),A);
   PUT FILE(L2) SKIP EDIT
   ('=  BATCH **  TSO ++  SCHNITT (BATCH+TSO) --','=')
   (A,COL(12+4*IK),A);
   PUT FILE(L2) SKIP EDIT
   (SUBSTR(GLEICH,1,12+4*IK))(A);
   END SCHAU2;
   END SYSTEM;
