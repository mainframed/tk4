         MACRO
&LAB     DFREE &ERROR=,&FILE=,&DA=,&MEMBER=,&DISP=,&HOLD=,&SYSOUT=
.**********************************************************************
.*                                                                    *
.*       DFREE - MACRO                                                *
.*                                                                    *
.*       LOEBER, W.      30.04.1976     GAD - MUENSTER                *
.*                                                                    *
.*             DISCRIPTION OF PARAMETERS                              *
.*  R E S T  -  F O L L O W S                                         *
.*                                                                    *
.**********************************************************************
         LCLA  &POI,&LGM,&IND1,&IND2,&TU,&TP
         LCLC  &C2,&C1,&DV
&C2      SETC  'GAD'.'&SYSNDX'
.*
.* FIRST CHECK OPERANDS AND COMPUTE LENGTH FOR WORK-AEREA
&LGM     SETA  24
&POI     SETA  24
         AIF   (T'&DA NE 'O' OR T'&FILE NE 'O').A1002
.A1001   ANOP
         MNOTE 04,'MISSING KEYWORD DA, FILE, OR ERROR'
         MEXIT
.*
.A1002   ANOP
         AIF   (T'&ERROR EQ 'O').A1001
.*
         AIF   (T'&DA EQ 'O').A1009
         AIF   (K'&DA LT 47).A1004
.A1003   ANOP
         MNOTE 04,'INCORRECT ''DA'' VALUE'
         MEXIT
.*
.A1004   ANOP
         AIF   (N'&DA NE 1).A1003
.*
.A1005   ANOP
         AIF   ('&DA'(1,1) NE '''').A1008
&IND1    SETA  2
&IND2    SETA  1
.A1006   ANOP
         AIF   ('&DA'(&IND1,1) NE '.').A1007
&IND2    SETA  1
&IND1    SETA  &IND1+1
         AGO   .A1006
.*
.A1007   ANOP
         AIF   ('&DA'(&IND1,1) EQ '''').A1008
         AIF   (&IND2 GT 8).A1003
&IND2    SETA  &IND2+1
&IND1    SETA  &IND1+1
         AGO   .A1006
.*
.A1008   ANOP
&LGM     SETA  &LGM+4+50
&POI     SETA  &POI+4
.A1009   ANOP
         AIF   (T'&FILE EQ 'O').A1013
         AIF   (K'&FILE LT 11).A1011
.A1010   ANOP
         MNOTE 04,'INCORRECT ''FILE'' VALUE'
         MEXIT
.*
.A1011   ANOP
         AIF   (N'&FILE GT 1).A1010
.*
.A1012   ANOP
&LGM     SETA  &LGM+4+14
&POI     SETA  &POI+4
.*
.A1013   ANOP
         AIF   (T'&MEMBER EQ 'O').A1016
         AIF   ('&MEMBER'(1,1) NE '''').A1015
         AIF   (K'&MEMBER LT 11).A1015
.*
.A1014   ANOP
         MNOTE 04,'INCORRECT ''MEMBER'' VALUE'
         MEXIT
.*
.A1015   ANOP
&LGM     SETA  &LGM+4+14
&POI     SETA  &POI+4
.*
.A1016   ANOP
         AIF   (T'&DISP EQ 'O').A1019
         AIF   ('&DISP'(1,1) NE '''').A1018
         AIF   ('&DISP'(2,7) NE 'UNCATLG' AND '&DISP'(2,5) NE 'CATLG' A*
               ND '&DISP'(2,6) NE 'DELETE' AND '&DISP'(2,4) NE 'KEEP').*
               A1017
         AGO   .A1018
.*
.A1017   ANOP
         MNOTE 04,'INCORRECT ''DISP'' VALUE'
         MEXIT
.*
.A1018   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.*
.A1019   ANOP
         AIF   (T'&HOLD EQ 'O').A1022
         AIF   ('&HOLD'(1,1) NE '''').A1021
         AIF   ('&HOLD'(2,3) NE 'YES' AND '&HOLD'(2,2) NE 'NO').A1020
         AGO   .A1021
.*
.A1020   ANOP
         MNOTE 04,'INCORRECT ''HOLD'' VALUE'
         MEXIT
.*
.A1021   ANOP
&LGM     SETA  &LGM+4+4
&POI     SETA  &POI+4+4
.*
.A1022   ANOP
         AIF   (T'&SYSOUT EQ 'O').B1000
         AIF   ('&SYSOUT'(1,1) NE '''').A1024
         AIF   (K'&SYSOUT LT 4).A1024
.*
.A1023   ANOP
         MNOTE 04,'INCORRECT ''SYSOUT'' VALUE'
         MEXIT
.*
.A1024   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.************************ READY ********************
.*
.************** NOW GENERATION *********************
.B1000   ANOP
&LGM     SETA  &LGM+8+8
&POI     SETA  &POI+4
.*  GENERATE MACRO-LABEL
         AIF   ('&LAB' EQ '').B1005
&LAB     DS    0H
.*
.B1005   ANOP
.*  G E N E R A T E  INSTRUCTIONS
         GETMAIN R,LV=&LGM
         LR    14,1
         LR    0,1
         LA    14,0(0,14)
         LA    1,&LGM.(,0)
         XR    15,15
         MVCL  0,14
.*
         LA    1,4(,14)
         ST    1,0(,14)
         MVC   4(2,14),=X'1402'
         LA    1,24(,14)
         ST    1,12(,14)
         MVI   0(14),X'80'
.* FILE
&C1      SETC  '(,14)'
&TP      SETA  24
&TU      SETA  &POI
         AIF   (T'&FILE EQ 'O').B1015
* FILE
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00010001'
&TU      SETA  &TU+4
         AIF   ('&FILE'(1,1) NE '''').B1010
&IND1    SETA  K'&FILE-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&FILE
&TU      SETA  &TU+2+&IND1
         AGO   .B1015
.*
.B1010   ANOP
         MVC   &TU.(10,14),&FILE
&TU      SETA  &TU+10
.*
.B1015   ANOP
         AIF   (T'&DA EQ 'O').B1025
* DA
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00020001'
         AIF   ('&DA'(1,1) NE '''').B1020
&IND1    SETA  K'&DA-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&DA
&TU      SETA  &TU+2+&IND1
         AGO   .B1025
.*
.B1020   ANOP
         MVC   &TU.(46,14),&DA
&TU      SETA  &TU+46
.*
.B1025   ANOP
         AIF   (T'&MEMBER EQ 'O').B1035
* MEMBER
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00030001'
&TU      SETA  &TU+4
         AIF   ('&MEMBER'(1,1) NE '''').B1030
&IND1    SETA  K'&MEMBER-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&MEMBER
&TU      SETA  &TU+2+&IND1
         AGO   .B1035
.*
.B1030   ANOP
         MVC   &TU.(10,14),&MEMBER
&TU      SETA  &TU+10
.*
.B1035   ANOP
         AIF   (T'&DISP EQ 'O').B1050
* DISP
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'000500010001'
&TU      SETA  &TU+6
         AIF   ('&DISP'(1,1) NE '''').B1045
&DV      SETC  'X''01'''
         AIF   ('&DISP'(2,7) EQ 'UNCATLG').B1040
&DV      SETC  'X''02'''
         AIF   ('&DISP'(2,5) EQ 'CATLG').B1040
&DV      SETC  'X''04'''
         AIF   ('&DISP'(2,6) EQ 'DELETE').B1040
&DV      SETC  'X''08'''
.*
.B1040   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1050
.*
.B1045   ANOP
         MVC   &TU.(1,14),&DISP
&TU      SETA  &TU+1
.*
.B1050   ANOP
         AIF   (T'&SYSOUT EQ 'O').B1060
* SYSOUT
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'001800010001'
&TU      SETA  &TU+6
         AIF   ('&SYSOUT'(1,1) NE '''').B1055
         MVI   &TU.(14),C&SYSOUT
&TU      SETA  &TU+1
         AGO   .B1060
.*
.B1055   ANOP
         MVC   &TU.(1,14),&SYSOUT
&TU      SETA  &TU+1
.*
.B1060   ANOP
         AIF   (T'&HOLD EQ 'O').C0000
* HOLD
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         AIF   ('&HOLD'(1,1) NE '''').B1070
&DV      SETC  'X''000A0000'''
         AIF   ('&HOLD'(2,2) EQ 'NO').B1065
&DV      SETC  'X''00590000'''
.*
.B1065   ANOP
         MVC   &TU.(4,14),&DV
&TU      SETA  &TU+4
         AGO   .C0000
.*
.B1070   ANOP
         MVC   &TU.(4,14),&HOLD
&TU      SETA  &TU+4
.*******
.C0000   ANOP
*
         LA    1,&TU&C1
         ST    1,&TP&C1
         MVC   &TU.(4,14),=X'00070000'
*
         MVI   &TP.(14),X'80'
* SVC 99
         LR    1,14
         SVC   99
* ERROR-CODES
         XC    &ERROR.(4),&ERROR
         LH    1,8(,14)
         STCM  1,3,&ERROR
         LH    1,10(,14)
         STCM  1,3,&ERROR.+2
* FREEMAIN
         LA    1,&LGM.(,0)
         LTR   15,15
         BNZ   &C2.E
         FREEMAIN R,LV=&LGM.,A=(14)
&C2.E    DS    0H
         MEXIT
         MEND
