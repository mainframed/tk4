         TITLE 'DATUM JJJJMMTT TESTEN UND RESULTAT IM TESTBYTE RETOUR'
UZED13A  CSECT
         REGEQ
         SAVE  (14,12),,*
         BALR  R10,0
         USING *,R10
         ST    R13,SAVEAREA+4      ADR.DER OBEREN S'AREA IN EIGENE
         LR    R12,R13
         LA    R13,SAVEAREA        ADR.DER EIGENEN S'AREA INS R13
         ST    R13,8(R12)          ADR.DER EIG. S'AREA IN OBERE S'AREA
         LM    R3,R4,0(R1)
         USING DATUM,R3
         USING FEHLBYTE,R4
         B     A1
         SPACE 3
*        S T A R T    D A T A - T E S T
         SPACE 2
RIGHT    MVI   PARM2,C'R'               R = RICHTIGES DATUM
         SPACE 1
RETURN   L     R13,SAVEAREA+4
         RETURN (14,12)                 RETURN TO CALLING PROGRAM
         SPACE 2
*
* GENERAL NUMERIC TEST
*
A1       LA    R5,8                     ANZAHL F<R BCT
         LA    R6,JAHR
A1LOOP   CLI   0(R6),C'0'               TEST OB KLEINER NULL
         BL    WRONG
         CLI   0(R6),C'9'               TEST OB GR@SSER NEUN
         BH    WRONG
         LA    R6,1(R6)                 ADR.V.JAHR UM 1 ERH@HEN
         BCT   R5,A1LOOP                SCHLAUFE 8X DURCHLAUFEN
         SPACE 2
* INDIVIDUAL TEST  YEAR, MONTH, DAY
*
*
         CLC   JAHR,=C'1867'            OB JAHR ZU KLEIN
         BL    WRONG
         CLC   JAHR,=C'2042'            OB JAHR ZU GROSS
         BH    WRONG
         CLC   MONAT,=C'01'
         BL    WRONG
         CLC   MONAT,=C'12'             OB MONAT GR@@SER 12
         BH    WRONG
         CLC   TAG,=C'01'
         BL    WRONG
         CLC   TAG,=C'31'               OB TAG GR@@SER 31
         BH    WRONG
         CLC   MONAT,=C'02'             OB FEBRUAR
         BNE   A2
         SPACE 1
* FEBRUARY TEST AUF SCHALTJAHR
         SPACE 1
         PACK  RFELD,JAHR
         DP    RFELD,=P'4'              JAHR DIVID. DURCH 4
         CP    RFELD+4(1),=P'0'         BEI REST 0 = SCHALTJAHR
         BE    A1A
A1B      CLC   TAG,=C'28'               WENN NICHT SCHALTJAHR NUR
         BH    WRONG                    MAX. 28
         B     RIGHT
A1A      CLC   JAHR+2(2),=C'00'
         BE    A1B
         CLC   TAG,=C'29'               WENN SCHALTJAHR MAX. 29
         BH    WRONG
         B     RIGHT
         SPACE 2
A2       CLC   MONAT,=C'04'             BEI APR/JUN/SEP/NOV DARF TAG
         BE    TEST30                   NUR MAX. 30 SEIN, WIRD BEI
         CLC   MONAT,=C'06'             TEST30 GEPR<FT
         BE    TEST30
         CLC   MONAT,=C'09'
         BE    TEST30
         CLC   MONAT,=C'11'
         BE    TEST30
         B     RIGHT                    MONAT WAR JAN/MRZ/MAI/JUL/AUG/
*                                       OKT/DEZ, TAG WAR SCHON GETESTET
TEST30   CLC   TAG,=C'30'
         BNH   RIGHT
         SPACE 2
WRONG    MVI   PARM2,C'F'               F = DATUM UNG<LTIG
         B     RETURN
         SPACE 3
*
* D E F I N I T I O N E N
*
SAVEAREA DS    18F
RFELD    DC    PL5'0'
         LTORG
DATUM    DSECT
JAHR     DS    CL4
MONAT    DS    CL2
TAG      DS    CL2
FEHLBYTE DSECT
PARM2    DS    CL1
         END   UZED13A
