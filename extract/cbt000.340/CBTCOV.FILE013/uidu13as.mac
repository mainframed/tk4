F6M0     TITLE '*** SWHDATER ***        NOV 1967 - S. W. HARROLD'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                      ***  S W H D A T E R  ***                      *
*                                                                     *
*   THE DATE IS PLACED IN THE FIRST PARAMETER (12 BYTES) IN THE FORM  *
*   DD. MMM YYYY   (E.G.   25. DEZ 1967   OR    1. JAN 1968).         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 10
FMDATUM  CSECT
         SAVE  (14,12),,*
         LR    12,15
         USING FMDATUM,12
         LR    11,13
         LA    13,SAVEAREA
         ST    13,8(0,11)
         ST    11,SAVEAREA+4
         SPACE 2
         L     11,0(0,1)
         L     11,0(11)
         USING DATE,11
         SPACE 3
         TIME  ,                   GET DATE IN REGISTER 1
         SPACE 2
         ST    1,DAY               STORE 00YYDDDS
         ZAP   YEAR,=P'0'
         MVO   YEAR(4),DAY(2)      STORE 00000YYS IN YEAR
         MVI   DAY+1,X'00'               0000DDDS IN DAY
         SPACE 2
         ZAP   WK1,YEAR
         DP    WK1,=P'4'
         CP    WK1+7(1),=P'0'
         BNE   ST2                 NOT A LEAP YEAR
         SPACE 2
         CP    DAY,=P'60'
         BL    ST2                 DATE IS BEFORE FEB 29 IN LEAP YEAR
         BH    ST3                         AFTER
         ZAP   DAY,=P'29'                  EQUAL
         LA    3,DAYSINMO+5        SET REG 3 POINTER TO FEB
         B     ST4
ST3      SP    DAY,=P'1'           REDUCE DAY E.G. 366 REDUCES TO 365
         SPACE 2
ST2      LA    3,DAYSINMO          SET REG 3 POINTER TO JAN
         LH    2,=H'11'            LIMIT LOOP TO 12 TIMES
         SPACE 2
ST5      CP    DAY,0(2,3)          IS DAY IN MONTH
         BNH   ST4                 BR IF YES
         SPACE 2
         SP    DAY,0(2,3)          DECREMENT BY NO. OF DAYS IN MONTH
         AH    3,=H'5'             POINT TO NEXT MONTH'S COMPARAND
         BCT   2,ST5               LOOP AGAIN, MAX 12 TIMES
         SPACE 2
ST4      MVC   DATE+4(3),2(3)      MOVE THE MONTH-NAME, POINTED BY 3
         MVI   DATE+3,C' '
         UNPK  DATE(2),DAY+2(2)  MOVE DATE
         OI    DATE+1,X'F0'
         MVI   DATE+2,C'.'
         CLI   DATE+0,C'0'         CHECK FOR, AND DELETE, LEADING ZERO
         BNE   ST6
         MVI   DATE+0,C' '
ST6      UNPK  DATE+10(2),YEAR+2(2)  MOVE YEAR
         OI    DATE+11,X'F0'
         MVC   DATE+7(3),=C' 19'
         SPACE 3
         L     13,SAVEAREA+4
         RETURN (14,12),T,RC=0
         SPACE 5
SAVEAREA DS    18F
WK1      DS    D
DAY      DS    F
YEAR     DS    F
DAYSINMO DC    P'31'
         DC    C'JAN'
         DC    P'28'
         DC    C'FEB'
         DC    P'31'
         DC    C'MAR'
         DC    P'30'
         DC    C'APR'
         DC    P'31'
         DC    C'MAI'
         DC    P'30'
         DC    C'JUN'
         DC    P'31'
         DC    C'JUL'
         DC    P'31'
         DC    C'AUG'
         DC    P'30'
         DC    C'SEP'
         DC    P'31'
         DC    C'OKT'
         DC    P'30'
         DC    C'NOV'
         DC    P'31'
         DC    C'DEZ'
         LTORG
         SPACE 5
DATE     DSECT                     REFERENCED BY REG 11
         DS    12X
         SPACE 2
         END   FMDATUM
*/* AN FOLGENDEN TAGEN WURDE DAS PROGRAMM GETESTET */
*/* NEUERSTELLUNG   12. MAR 1975   LEVEL M  */
