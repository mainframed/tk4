//Z223359F JOB (6685,359,1),MSGCLASS=O,CLASS=R,NOTIFY=Z223359
//       EXEC ASMFCL
//ASM.SYSIN DD *
XFREE    CSECT
XFREE    AMODE 31
XFREE    RMODE 24
         REG
*
*              XA-VERSION 4.7.1984 DEMEL
*
         XSAVE R12,,XFREE,WORKL
         USING WORK,R13
         LR    R3,R1
         USING PARMAREA,R3
         LA    R2,TEXT
         MVI   DSW,X'00'
         CLC   4(4,R2),=C',ALL'
         BNE   NORM
         OI    DSW,X'40'
NORM     EQU   *
         MVC   KMSGTEXT,KONS
         L     R1,16
         TM    116(R1),X'80'      MVS-XA?
         BZ    MVSDATA            NO, 370 MODE
         SPACE
         L     R2,560(R1)               A(GDA)
         SPACE
         MVC   WTO,MESSXA1
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
         MVI   NAME,C' '
         MVC   NAME+1(55),NAME
         MVI   CONK,C'K'
         MVI   STRICH,C'-'
******** ***** ******************       PRIV EXT
         SPACE
         MVC   NAME,=CL8'EXT-PRIV'
         L     R1,168(R2)               R1 CONTAINS STARTADR
         L     R0,172(R2)               R0 CONTAINS SIZE
         BAL   R14,PREPARE
         SPACE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE 3
******** ***** ******************       CSA EXT
         MVC   NAME,=CL8'EXT-CSA'
         L     R1,124(R2)               STARTADR
         L     R0,128(R2)               SIZE
         BAL   R14,PREPARE
         SPACE
         L     R1,116(R2)               START OF FBQE
         L     R0,120(R2)               END ADR OF LAST FBQE
         BAL   R14,FRCSA
         SPACE
         L     R1,128(R2)               SIZE OF CSA-EXT
         BAL   R14,FREEPRT
         SPACE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
******** ***** ***************     EXT  PLPA
         MVC   NAME,=CL8'EXT-PLPA'
         L     R1,152(R2)               START OF SQA EXT
         A     R1,156(R2)               R1 STARTADR OF PLPA
         L     R0,124(R2)               STARTADR OF CSA
         SR    R0,R1                    R1 CONTAINS SIZE
         SPACE
         BAL   R14,PREPARE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
******** ***** *******************         SQA EXTENDED
         MVC   NAME,=CL8'EXT-SQA'
         L     R1,152(R2)               STARTADR
         L     R0,156(R2)               SIZE
         BAL   R14,PREPARE
         MVC   D,=XL8'00000000'         CLEAR WORK FIELD
         L     R1,36(R2)                FIRST DFE OF SP 245 EXT
         L     R0,40(R2)                LAST  DFE OF SP 245 EXT
         BAL   R14,FRSQA
         SPACE
         ST    R4,D                     SAVE ACCUMULATED FREESPACE
         ST    R5,D+4                   SAVE LARGEST FREESPACE
         SPACE
         L     R1,260(R2)               FIRST DFE OF SP 239 EXT
         L     R0,264(R2)               LAST  DFE OF SP 239 EXT
         BAL   R14,FRSQA
         SPACE
         A     R4,D
         C     R5,D+4
         BH    MAXF
         L     R5,D+4
MAXF     EQU   *
         L     R1,156(R2)               SIZE OF SQA-EXT
         BAL   R14,FREEPRT
         SPACE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
******** ***** ************             NUCLEUS
         MVC   NAME,=CL8'NUCLEUS'
         L     R1,144(R2)               START OF SQA
         A     R1,148(R2)               R1 CONTAINS START OF NUCLEUS
         SPACE
         L     R0,152(R2)               R0 CONTAINS START OF SQA EXT
         SR    R0,R1                    R0 CONTAINS SIZE OF NUCLEUS
         BAL   R14,PREPARE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
******** ***** *****************   SQA
         MVC   NAME,=CL8'SQA'
         L     R1,144(R2)               STARTADR
         L     R0,148(R2)               SIZE
         BAL   R14,PREPARE
         SPACE
         MVC   D,=XL8'00000000'         CLEAR WORK FIELDS
         L     R1,12(R2)                FIRST DFE OF SP 245
         L     R0,16(R2)                LAST  DFE OF SP 245
         BAL   R14,FRSQA
         SPACE
         ST    R4,D                     SAVE ACCUMULATED FREESPACE
         ST    R5,D+4                   SAVE LARGEST FREESPACE
         SPACE
         L     R1,60(R2)                FIRST DFE OF SP 226
         L     R0,64(R2)                LAST  DFE OF SP 226
         BAL   R14,FRSQA
         SPACE
         A     R4,D
         ST    R4,D
         SPACE
         C     R5,D+4
         BH    MAXF1
         L     R5,D+4
MAXF1    EQU   *
         ST    R5,D+4                   FIRST DFE OF SP 226
         L     R1,236(R2)               FIRST DFE OF SP 226
         L     R0,240(R2)               LAST  DFE OF SP 226
         BAL   R14,FRSQA
         SPACE
         A     R4,D
         SPACE
         C     R5,D+4
         BH    MAXF2
         L     R5,D+4
MAXF2    EQU   *
         L     R1,148(R2)               SIZE OF SQA
         BAL   R14,FREEPRT
         L     R0,REG4
         BAL   R14,WTOROUT
*
******** ***** ****************         PLPA
         MVC   NAME,=CL8'PLPA'
         L     R1,108(R2)               START OF CSA
         A     R1,112(R2)               R1 CONTAINS START OF PLPA
         SPACE
         L     R0,144(R2)               START OF SQA
         SR    R0,R1                    R0 CONTAINS SIZE OF PLPA
         BAL   R14,PREPARE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
******** ***** *****************        CSA
         MVC   NAME,=CL8'CSA'
         SPACE
         L     R1,108(R2)               START OF CSA
         L     R0,112(R2)               SIZE
         BAL   R14,PREPARE
         SPACE
         L     R1,100(R2)               START OF FBQE
         L     R0,104(R2)               END OF FBQE
         BAL   R14,FRCSA
         SPACE
         L     R1,112(R2)
         BAL   R14,FREEPRT
         SPACE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
******** ***** ***************          PRIVATE
         MVC   NAME,=CL8'PRIV V=V'
         SPACE
         L     R1,192(R2)               START OF PRIV AREA
         L     R0,164(R2)               SIZE
         S     R0,192(R2)
         SPACE
         BAL   R14,PREPARE
         SPACE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
         MVC   NAME,=CL8'     V=R'
         SPACE
         L     R1,192(R2)               START OF V=R REGION
         L     R0,196(R2)               SIZE
         SPACE
         BAL   R14,PREPARE
         SPACE
         L     R0,REG4
         BAL   R14,WTOROUT
         SPACE
         MVC   NAME,=CL8'SYS-AREA'
         SPACE
         L     R1,160(R2)               START OF SYSAREA
         L     R0,192(R2)               SIZE
         SPACE
         BAL   R14,PREPARE
         SPACE
         L     R0,REG4
         BAL   R14,WTOROUT
         B     END
GOON     EQU   *                        DES DYNAM. BEREICHS)
         LM    R8,R9,8(R8)        A(PQE DES DYNAM. BEREICHS)
         STM   R8,R9,DPQE
         CLC   4(4,R2),=C',V=V'
         BNE   *+6
         LR    R8,R9
         CLC   4(4,R2),=C',V=R'
         BNE   *+6
         LR    R9,R8
         STM   R8,R9,PQEAD        STORE ADDR. OF DYNAMIC AREA
         L     R9,24(R8)          REGION BEGIN
         L     R10,20(R8)         REGION SIZE
         LA    R9,0(R9)           CLEAR HIGH ORDER BIS
         LA    R10,0(R10)         CLEAR HIGH ORDER BITS
         STM   R9,R10,DYNAVALS
         LA    R7,FBQETAB
         L     R8,PQEAD
PQE2     CLC   5(3,R8),PQEAD+1
         BE    DISARET
         ICM   R9,8,=X'08'
         C     R8,DPQE
         BE    *+8
         ICM   R9,8,=X'04'
         L     R8,04(R8)
         LA    R6,FBQEND
DISALOP  EQU   *
         L     R10,8(R8)
         LA    R10,0(R10)
         ICM   R9,7,13(R8)
         TM    116(R1),X'02'      DAT?
         BO    *+8
         LA    R9,0(R8)
         STM   R9,R10,0(R7)
         LA    R7,8(R7)
         CLC   5(3,R8),PQEAD+1    END OF FBQE'S?
         BE    DISARET
         CR    R7,R6              ENDE DER TABELLE?
         BNL   DISALOP1
         L     R8,4(R8)           NEXT FBQE
         B     DISALOP
DISALOP1 EQU   *
         OI    DSW,X'80'
DISARET  EQU   *
         ST    R7,FTABEND
         L     R8,PQEAD+4
         C     R8,PQEAD
         BE    PQE2E
         ST    R8,PQEAD
         B     PQE2
PQE2E    EQU   *
         TM    DSW,X'40'
         BNO   NTALL1
         TM    116(R1),X'02'
         BO    ALLVS
         MVC   WTO,MESS1
         L     R9,DYNAVALS+4
         LR    R1,R9
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+40(4),CON
         OI    WTO+43,X'F0'
         L     R9,16
         L     R9,200(R9)
         L     R9,108(R9)
         LR    R11,R9
         L     R9,4(R9)
         L     R9,12(R9)
         LA    R9,0(R9)
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+27(4),CON
         OI    WTO+30,X'F0'
         L     R9,4(R11)
         L     R9,8(R9)
         LA    R9,0(R9)
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+16(4),CON
         OI    WTO+19,X'F0'
         L     R8,0(R11)
         LA    R8,0(R8)
         AR    R8,R1
         L     R9,16
         L     R9,164(R9)
         LA    R9,1(R9)
         SR    R9,R8
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+51(4),CON
         OI    WTO+54,X'F0'
         L     R0,REG4
         BAL   R14,WTOROUT
         B     NTALL1
ALLVS    EQU   *
         TM    116(R1),X'01'
         BO    ALLMVS
         MVC   WTO,MESS7
         L     R1,16
         L     R9,128(R1)        CVTPTR
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+16(5),CON
         OI    WTO+20,X'F0'
         L     R8,DPQE
         L     R9,20(R8)
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+27(5),CON
         OI    WTO+31,X'F0'
         L     R8,DPQE+4
         L     R9,20(R8)
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+38(5),CON
         OI    WTO+42,X'F0'
         L     R8,200(R1)       CVTABEND (SEC.CVT)
         L     R7,112(R8)       COMM TASK TCB
         L     R7,152(R7)       DPQE-8
         L     R7,8(R7)         MS-PQE
         L     R9,20(R7)        REGION SIZE
         L     R7,24(R7)        REGION BEGIN
         LA    R7,0(R9,R7)      REGION END
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+48(5),CON
         OI    WTO+52,X'F0'
         L     R6,108(R8)       GOVRFLB
         L     R6,4(R6)         SQA-DQE
         L     R9,8(R6)
         SR    R9,R7
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+59(5),CON
         OI    WTO+63,X'F0'
         L     R9,12(R6)
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+70(5),CON
         OI    WTO+74,X'F0'
         L     R0,REG4
         BAL   R14,WTOROUT
         B     NTALL1
ALLMVS   EQU   *
         MVC   WTO,MESS8
         L     R9,X'80'(,R1)        LOWEST ADDRESS NOT IN THE NUCLEUS
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+16(5),CON
         OI    WTO+20,X'F0'
         L     R4,X'230'(,R1)       ADDRESS OF GDA
         USING GDA,R4
         L     R9,PASIZE
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+27(5),CON
         OI    WTO+31,X'F0'
         L     R5,CSAPQEP
         USING PQE,R5
         L     R9,PQESIZE
         SRA   R9,10
         ST    R9,CSASZ
         CVD   R9,CON
         UNPK  WTO+38(5),CON
         OI    WTO+42,X'F0'
         L     R8,X'1A0'(,R1)         ADDRESS OF FIRST BYTE OF LPA
         L     R5,SQASPQEP
         L     R5,4(,R5)              ADDRESS OF FIRST DQE
         L     R9,8(,R5)
         SLR   R9,R8
         LA    R9,0(,R9)
         SRA   R9,10
         S     R9,CSASZ
         CVD   R9,CON
         UNPK  WTO+49(5),CON
         OI    WTO+53,X'F0'
         L     R9,12(,R5)             LENGTH OF SQA
         SRA   R9,10
         CVD   R9,CON
         UNPK  WTO+60(5),CON
         OI    WTO+64,X'F0'
         L     R0,REG4
         BAL   R14,WTOROUT
NTALL1   EQU   *
*   FBQE-LOOP
         L     R7,FTABEND
         MVC   WTO,MESS3
         LA    R8,FBQETAB
         CR    R7,R8
         BE    NOFRECOR
         LA    R9,WTO
DRLOOP   EQU   *
         BAL   R11,CONVERT
         L     R0,REG4
         BAL   R14,WTOROUT
         LA    R8,8(R8)           NEXT FBQE IN TABLE
         CR    R7,R8
         BE    DREND
         B     DRLOOP
NOFRECOR EQU   *
         MVC   WTO,MESS4
         L     R0,REG4
         BAL   R14,WTOROUT
         B     RETURN
DREND    EQU   *
         TM    DSW,X'80'
         BNO   RETURN
         MVC   WTO,MESS5
         L     R0,REG4
         BAL   R14,WTOROUT
RETURN   EQU   *
         TM    DSW,X'40'
         BNO   END
         L     R5,16      CVT
         LA    R7,0
         SR    R4,R4
         L     R5,560(R5)
         L     R5,24(R5)
         L     R5,4(R5)
         L     R5,0(R5)    A(FIRST FQE)
         LTR   R5,R5   END OF FQE-CHAIN?
         BZ    NONE
NEXTFQE  EQU   *
         L     R6,4(R5)
         LA    R6,0(R6)   CLEAR HIGH ORDER BYTE
         CR    R6,R7
         BNH   INCR
         LR    R7,R6
INCR     EQU   *
         AR    R4,R6
         L     R5,0(R5)
         LA    R5,0(R5)
         LTR   R5,R5
         BNZ   NEXTFQE
NONE     EQU   *
         MVC   WTO,MESS6
         SRL   R4,10
         CVD   R4,CON
         UNPK  WTO+12(6),CON
         OI    WTO+17,X'F0'
         CVD   R7,CON
         UNPK  WTO+59(6),CON
         OI    WTO+64,X'F0'
         L     R0,REG4
         BAL   R14,WTOROUT
END      EQU   *
         XRETURN ,R
CONVERT  EQU   *
         SR    R10,R10
         IC    R10,0(R8)
         LA    R10,KMSGTEXT(R10)
         MVC   WTO+12(4),0(R10)
         ICM   R10,7,1(R8)
         SRA   R10,10
         CVD   R10,CON
         UNPK  WTO+39(5),CON
         OI    WTO+43,X'F0'
         L     R0,4(R8)
         SRA   R0,10
         CVD   R0,CON
         UNPK  WTO+58(5),CON
         OI    WTO+62,X'F0'
         AR    R10,R0
         CVD   R10,CON
         UNPK  WTO+49(5),CON
         OI    WTO+53,X'F0'
         BR    R11
MVSDATA  EQU   *
         L     R8,X'230'(R1)      A(GLOBAL DATA AREA)
         MVC   KMSGTEXT+4(4),KMSGTEXT+8
         MVC   KMSGTEXT+8(4),=C'CSA '
         B     GOON
WTOROUT  EQU   *
         ST    R14,SAVE14
         C     R0,=CL4'TSO'
         BNE   DOWTO
         LH    R0,WTO
         SH    R0,=H'4'
         LA    R1,WTO+4
         TPUT  (1),(0),R
         L     R14,SAVE14
         BR    R14
DOWTO    EQU   *
         WTO   MF=(E,WTO)
         L     R14,SAVE14
         BR    R14
PREPARE  EQU   *
*                                       R0 CONTAINS SIZE
*                                       R1 CONTAINS STARTADR
*                                       R14 RETURNADR
         ST    R1,D
         UNPK  WORKF,D(5)
         TR    WORKF(8),TRTAB-240
         MVC   STARTADR,WORKF
         SPACE
         AR    R1,R0                    GET ENDADR
         BCTR  R1,0                     REDUCE BY ONE
         ST    R1,D                     R1 CONTAINS END ADR
         UNPK  WORKF,D(5)
         TR    WORKF(8),TRTAB-240
         MVC   ENDADR,WORKF
         SPACE
         LA    R1,1                     INCREASE BY ONE
         AR    R0,R1                    ADD ONE
         SRL   R0,10                    DIVIDE BY 1024
         CVD   R0,D                     CONVERT TO DEZIMAL
         SPACE
         MVC   SIZE,MASKE
         ED    SIZE,D+4
         SPACE
         SPACE
         MVC   ALCACC(7),=CL7' '
         MVC   FREEMAX(7),=CL7' '
         MVC   PROZ(6),=CL7' '
         BR    R14
FRSQA    EQU   *
*                        R1 POINTS TO FIRST DFE
*                        R0 POINTS TO LAST DFE
         SR    R4,R4                    R4 FREESPACE SUMM
         SR    R5,R5                    R5 FREESPACE MAX
         SPACE
FRSQAL   EQU   *
         C     R5,20(R1)                MAX FREESPACE
         BH    FRSQAH                   NO
         L     R5,20(R1)                SAVE
         SPACE
FRSQAH   EQU   *
         A     R4,20(R1)                SUMM
         CR    R0,R1                    LAST DFE
         BER   R14                      YES,RETURN
         L     R1,0(R1)                 NEXT DFE
         B     FRSQAL                   LOOP
FRCSA    EQU   *
*                        R1 POINTS TO FIRST FBQE
*                        R0 POINTS TO LAST FBQE
         SR    R4,R4                    R4 FREESPACE SUMM
         SR    R5,R5                    R5 FREESPACE MAX
         SPACE
FRCSAL   EQU   *
         C     R5,8(R1)                 MAX ONE?
         BH    FRCSAH                   NO
         SPACE
         L     R5,8(R1)
FRCSAH   EQU   *
         A     R4,8(R1)                 SUMM
         CR    R0,R1                    END OF FBQE'S?
         BER   R14                      YES
         L     R1,0(R1)                 NEXT FBQE
         B     FRCSAL                   LOOP UNTIL FIND END OF FBQE'S
         SPACE
FREEPRT  EQU   *
*                                       R4 FREESIZE ACC
*                                       R5 FREEMAX
*                                       R1 SIZE
         SRL   R5,10
         CVD   R5,D
         MVC   FREEMAX,MASKE
         ED    FREEMAX,D+5
         MVI   FREEMAX+6,C'K'
         SPACE
         LR    R5,R1                    R5 CONTAINS SIZE
         SR    R1,R4                    R1 CONTAINS ALLOC SPACE
         LR    R4,R1
         SRL   R4,10
         CVD   R4,D
         MVC   ALCACC,MASKE
         ED    ALCACC,D+5
         MVI   ALCACC+6,C'K'
         SPACE
         M     R0,=F'1000'
         SPACE
         DR    R0,R5
         CVD   R1,D
         MVC   PROZ,MASKE1
         ED    PROZ,D+6
         MVI   PROZ+5,C'%'
         BR    R14                      RETURN
         SPACE
KONS     DC    C'MVT V=V V=R'
MESS1    WTO   'XFREE01 NUC=1234K, SQS=1234K, DYNAM=1234K, LPA=1234K', *
               MCSFLAG=REG0,MF=L
MESS3    WTO   'XFREE02 *** FREE STORAGE AREA FROM 12345K TO 12345K = 1*
               2345K',MCSFLAG=(REG0),MF=L    L
MESS4    WTO   'XFREE03 NO FREE STORAGE AREAS',MCSFLAG=REG0,           *
               MF=L
MESS5    WTO   'XFREE04 TABLE-OVERFLOW, MORE FREE AREAS ARE PRESENT',  *
               MCSFLAG=REG0,MF=L       MF=L
MESS6    WTO   'XFREE05 123456 KB FREE IN SQA -- LARGEST FREE AREA HAS *
               123456 BYTES',MCSFLAG=REG0,MF=L     MF=L
MESS7    WTO   'XFREE06 NUC=12345K,V=R=12345K,V=V=12345K,MS=12345K,LPA=*
               12345K,SQA=12345K',MCSFLAG=(REG0),MF=L
MESS8    WTO   'XFREE07 NUC=12345K,V=V=12345K,CSA=12345K,LPA=12345K,SQA*
               =12345K',MCSFLAG=REG0,MF=L
MESS9    WTO   'XFREE08                                                *
                      ',MCSFLAG=REG0,MF=L
MESSXA1  WTO   'XFREEXA  NAME       SIZE     ADDRESS RANGE    ALLOC   %*
                  FREEMX',MCSFLAG=REG0,MF=L
*                       '
TRTAB    DC    C'0123456789ABCDEF'
MASKE    DC    XL8'4020202020202021'
MASKE1   DC    XL5'4021206B20'
         LTORG
WORK     DSECT
SVA      DS    18F
CON      DS    D
DYNAVALS DS    D
FBQETAB  DS    50D
FBQEND   EQU   *
FTABEND  DS    A
PQEAD    DS   2A
DPQE     DS   2A
KMSGTEXT DS    CL12
WTO      DS    CL100
         ORG   WTO
         DS    CL4
         DS    CL8
NAME     DS    CL8
SIZE     DS    CL8
CONK     DS    C
         DS    CL2
STARTADR DS    CL8
STRICH   DS    C
ENDADR   DS    CL8
ALCACC   DS    CL6
         DS    C
PROZ     DS    CL5
         DS    C
FREEMAX  DS    CL6
         DS    C
         ORG
WORKF    DC    CL9' '
WORKF1   DC    CL5' '
SAVE14   DS    F
CSASZ    DS    F
DSW      DS    X
D        DS    D
WORKL    EQU   *-WORK
         XPARM
*
GDA      DSECT ,                     GLOBAL DATA AREA,
*                                    POINTED TO BY CVT+X'230'
*                                    PRESENT ONLY IN MVS SYSTEMS
GDAFLAGS DS    BL1         GLOBAL FLAGS
GDARESV  DS    CL3
VRDREG   DS    F           DEFAULT V=R REGION SIZE
CSAPQEP  DS    A           CSA PQE POINTER
VRPQEP   DS    A           V=R PQE POINTER
PASTRT   DS    A           PRIVATE AREA START ADDRESS
PASIZE   DS    F           PRIVATE AREA SIZE
SQASPQEP DS    A           SQA SPQE POINTER
SQASPLFT DS    F           SQA SPACE LEFT UNALLOCATED
         DS    5F
CSASPQEP DS    A           FIRST CSA SPQE POINTER
*
*
PQE      DSECT ,                     PARTITION QUEUE ELEMENT,
*                                    POINTED TO BY:
PQEFFBQE DS    A           PTR TO FIRST FBQE OR, IF NONE, TO PQE
PQEBFBQE DS    A           PTR TO LAST FBQE OR, IF NONE, TO PQE
PQEFPQE  DS    A           ADDR NEXT PQE OR ZERO
PQEBPQE  DS    A           ADDR PREVIOUS PQE OR ZERO
PQETCB   DS    A           ADDR TCB FOR JOB STEP TO WHICH SPACE
*                          BELONGS
PQESIZE  DS    F           SIZE OF REGION DESCRIBED BY THIS PQE
PQEREGN  DS    A           ADDR FIRST BYTE OF REGION DESCRIBED BY
*                          THIS PQE
         DS    CL2
VMMFLGS  DS    BL1         FLAGS, SEVEN HIGH ORDER BITS ZERO
VVVRFLG  EQU   B'00000001' REAL OR VIRTUAL REGION FLAG
         DS    CL1
*
*
FBQE     DSECT ,                     FREE BLOCK QUEUE ELEMENT,
*                                    POINTED TO BY PQE
FBQFPTR  DS    A           PTR TO NEXT FBQE OR PQE
FBQBPTR  DS    A           PTR TO PREVIOUS FBQE OR PQE
FBQSIZE  DS    F           SIZE OF THIS FREE BLOCK
FBQAREA  DS    A           LOW ADDRESS OF FREE BLOCK
*
         END
//LKED.SYSLMOD DD DSN=LU.LNKLIB(XFREE),DISP=SHR
