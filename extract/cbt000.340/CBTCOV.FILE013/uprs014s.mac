***********************************************************************
**                                                                   **
**             XDAMEDT  PROGRAMMBESCHREIBUNG                         **
**             =============================                         **
**                                                                   **
**       DAS PROGRAMM XDAMEDT FUEGT IN EINE NACHRICHT VARIABLE       **
**       TEXTE EIN. DIE STELLEN DER VARIABLEN TEXTE WERDEN IN        **
**       DER NACHRICHT DURCH DIE ZEICHENKOMBINATION <NNN> GEKENN-    **
**       ZEICHNET. NNN IST EINE MAXIMAL 3-STELLIGE ZAHL, DIE DIE     **
**       MAXIMALLAENGE DES VARIABLEN TEXTES ANGIBT.                  **
**                                                                   **
**       AUFRUF:   CALL  XDAMEDT,(MESSAGE,LAENGE,VAR1,...,VARN),VL   **
**                 MESSAGE IST DIE ZU EDITIERENDE NACHRICHT.         **
**                 LAENGE  IST DIE LAENGE VON MESSAGE.               **
**                 VAR1-VARN SIND DIE VARIABLEN TEXTE, DIE IN        **
**                       MESSAGE EINGESETZT WERDEN SOLLEN.           **
**                                                                   **
**       BEISPIEL:                                                   **
**                 CALL  XDAMEDT,(MSG,LEN,DSN,ANZAHL),VL             **
**                 .                                                 **
**                 .                                                 **
**       MSG       DC    CL132'DATASET <44> ENTHAELT <9> SAETZE.'    **
**       LEN       DC    F'132'                                      **
**       DSN       DC    CL44'A.B.C'                                 **
**       ANZAHL    DC    CL9'     1234'                              **
**                                                                   **
**       INHALT VON MSG NACH DEM CALL:                               **
**       'DATASET A.B.C ENTHAELT 1234 SAETZE.'                       **
**       AUS DEN VARIABLEN WERDEN FUEHRENDE UND NACHHAENGENDE        **
**       BLANKS ENTFERNT.                                            **
**                                                                   **
**       SIND IN DEM TEXT MEHR ERSETZUNGSZEICHEN ALS VARIABLE        **
**       PARAMETER VORHANDEN, BLEIBEN DIE RESTLICHEN ERSETZUNGS-     **
**       ZEICHEN ERHALTEN.                                           **
**                                                                   **
***********************************************************************
XDAMEDT  TITLE 'AUFBEREITEN VON NACHRICHTEN.  HO 8103'
XDAMEDT  CSECT
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   R10
RB       EQU   R11
RC       EQU   R12
RD       EQU   R13
RE       EQU   R14
RF       EQU   R15
         EJECT
         SAVE  (14,12)                  INITIALISIERUNG
         LR    RA,RF
         USING XDAMEDT,RA
         LM    R3,R4,0(R1)              PARM1 + PARM2
         L     R4,0(,R4)                LAENGE
         LA    R5,8(,R1)                ADR VON PARM3
         LTR   R3,R3                    R3 UND R4
         BNP   B10800                   MUESSEN POSITIV SEIN
         LTR   R4,R4                    SONST KEINE AKTION
         BNP   B10800
         LA    R6,TEXT                  ADR VON TEXT
         LA    R7,256                   LAENGE VON TEXT
         CR    R4,R7
         BL    B10010
         LR    R4,R7                    MAXIMALE LAENGE
B10010   NULL
         MVI   TEXT,BLANK               TEXT
         MVC   TEXT+1(255),TEXT          LOESCHEN
         LR    R1,R3                    EINGABE-POINTER
         AR    R1,R4                     + LAENGE
         ST    R1,FELDENDE                 = ENDPOINTER
         SR    R1,R4
         LR    R2,R1
B10100   NULL
         CLI   0(R1),C'<'               ZEICHEN SUCHEN
         BE    B10200
B10120   NULL
         LA    R1,1(,R1)                POINTER + 1
         CL    R1,FELDENDE
         BL    B10100
         B     B10500
B10200   NULL
         SR    R1,R2                    LAENGE ERRECHNEN
         BZ    B10250
         CR    R1,R7                    PASST DAS NOCH IN TEXT
         BL    B10220
         LR    R1,R7
B10220   NULL
         SR    R7,R1                    RESTLAENGE VON TEXT
         BCTR  R1,0
         MVC   0(*-*,R6),0(R2)
         EX    R1,*-6
         LA    R6,1(R1,R6)
         LTR   R7,R7
         BZ    B10600                   ENDE
B10240   NULL
         LA    R2,1(R2,R1)
B10250   NULL
         LA    R1,1(,R2)
         LA    R0,4
B10260   NULL
         CL    R1,FELDENDE
         BE    B10500
         CLI   0(R1),C'0'               ZIFFER
         BL    B10280
         LA    R1,1(,R1)
         BCT   R0,B10260
         B     B10120
B10280   NULL
         CLI   0(R1),C'>'               ENDE VAIABLE EINGABE
         BNE   B10120
         SR    R1,R2
         BCTR  R1,0
         LTR   R1,R1
         BP    B10290
         LA    R1,1(R2,R1)
         B     B10120
B10290   NULL
         BCTR  R1,0
         PACK  WORK,1(*-*,R2)
         EX    R1,*-6
         LA    R1,3(R2,R1)
         CVB   RE,WORK
         LTR   RE,RE
         BZ    B10100
         CR    RE,R7
         BL    B10300
         LR    RE,R7
B10300   NULL
         L     RF,0(,R5)
B10310   NULL
         CLI   0(RF),BLANK
         BNE   B10315
         LA    RF,1(,RF)
         BCT   RE,B10310
         B     B10350
B10315   NULL
         BCTR  RE,0
         MVC   0(*-*,R6),0(RF)
         EX    RE,*-6
         ST    R6,TEXTPTR
         LA    R6,0(RE,R6)
B10320   NULL
         CLI   0(R6),BLANK
         BNE   B10340
         BCTR  R6,0
         CL    R6,TEXTPTR
         BH    B10320
B10340   NULL
         LA    R6,1(,R6)
B10350   NULL
         LA    R7,TEXT+256
         SR    R7,R6
         BZ    B10600
         LR    R2,R1
         TM    0(R5),X'80'
         BO    B10500
         LA    R5,4(,R5)
         B     B10100
B10500   NULL
         L     R1,FELDENDE
         SR    R1,R2                    LAENGE ERRECHNEN
         BZ    B10600
         CR    R1,R7                    PASST DAS NOCH IN TEXT
         BL    B10520
         LR    R1,R7
B10520   NULL
         SR    R7,R1                    RESTLAENGE VON TEXT
         BCTR  R1,0
         MVC   0(*-*,R6),0(R2)
         EX    R1,*-6
B10600   NULL
         BCTR  R4,0
         MVC   0(*-*,R3),TEXT
         EX    R4,*-6
B10800   NULL
         RETURN (14,12),RC=0
         EJECT
WORK     DS    D
FELDENDE DS    F
TEXTPTR  DS    F
TEXT     DS    CL256
BLANK    EQU   C' '
         END
