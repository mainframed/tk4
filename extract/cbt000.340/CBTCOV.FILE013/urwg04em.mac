         MACRO
&NAME    MSPSN &DUMMY,&COUNT=
         LCLC  &X
&X       SETC  '&SYSNDX'
         MNOTE *,'*** SPIE FIRST TIME SWITCH'
         NOP   SPEN&X              SPIE-ROUTINE BRAUCHT NUR
         OI    *-3,240             EINMALIG DURCHLAUFEN ZU WERDEN
         MNOTE *,'*** SPIE WIRD AKTIVIERT MIT INTERRUPT-TYPES ((1,15))'
&NAME    SPIE  SPIE&X,((1,15))     ACTIVATE SPIE-EXIT
         SPACE 3
SPIE&X   CSECT                     SPIE-EXIT-ROUTINE START
         USING SPIE&X,R15          ENTRY REGISTER USED AS BASE REGISTER
         MNOTE *,'*** SPIE LOAD SWITCH'
         NOP   SPEX&X              SPIE-ROUTINE BRAUCHT NUR
         OI    *-3,240             EINMALIG GELADEN ZU WERDEN
         MNOTE *,'*** SPIE ROUTINE LADEN'
         STM   R0,R15,SREG&X       SAVE REGISTER
         LA    R13,SAVE&X          LOAD SAVEAREA-ADDRESS
         LR    R12,R15             SAVE BASE-REGISTER
LOAD&X   LOAD  EP=UPSPSNAP         LOAD SPIE-ROUTINE
         LR    R15,R12             RESTORE BASE-REGISTER
         ST    R0,SPAD&X           SAVE ENTRY-POINT
         AIF   (T'&COUNT EQ 'O').A2
         AIF   (T'&COUNT NE 'N' OR '&COUNT' EQ '0').A2
         AIF   ('&COUNT' GT '255').A2
         MVI   SPAD&X,&COUNT       MOVE SPIE-COUNT INTO ROUTINE-ADDRESS
.A2      ANOP
         LM    R0,R15,SREG&X       RESTORE REGISTER
SPEX&X   L     R15,SPAD&X          LOAD SPIE-ROUTINE-ADDRESS
         BR    R15                 BRANCH INTO SPIE-ROUTINE
SPAD&X   DC    A(0)                SPIE-ROUTINE ENTRY-POINT
SREG&X   DC    16F'0'              SAVEAREA FOR REGS 0 TO 15
SAVE&X   DC    18F'0'              SAVEAREA
         DROP  R15                 RELEASE REG15 AS BASE REGISTER
&SYSECT  CSECT                     SPIE-EXIT-ROUTINE END
         SPACE 3
SPEN&X   DC    0H'0'               USER PROGRAMM CONTINUES HERE
         MEND
