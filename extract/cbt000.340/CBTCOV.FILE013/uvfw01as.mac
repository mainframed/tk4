*
*   V       V  FFFFF  W           W
*    V     V   F       W         W         HERR SEYFARTH
*     V   V    FFF      W   W   W          HERR FALLDORF
*      V V     F         W W W W           0421-672(419)
*       V      F          W   W
*
* ZWECK DES PROGRAMMMES:
*    LESEN DES JOBFILE-CONTROL-BLOCKS FUER PL/I(OPTIMIZER)- UND COBOL-
*    PROGRAMME. DORT STEHEN FUER UEBERGEBENE DDNAMEN NACH AUSFUEHRUNG
*    ALLE IM JFCB VORHANDENEN FELDER ZUR VERFUEGUNG.
*
* REGISTERBENUTZUNG:
*    R1  = ADR ARGLIST
*    R2  = ADR DD ENTRY TIOT
*    R3  = L' DD ENTRY IN TIOT
*    R4  = ADR EXIT ADR FUER JFCB
*    R5  = DEB ADR
*    R6  = UCB ADR FROM DEB
*    R7  = DCB ADR
*    R10 = BASIS REG
*
* RETURN CODES :
*    BEI RUECKSPRUNG BEFINDEN SICH IM FELD RC FOLGENDE CODES:
*    X'F0' = JFCB OK
*    X'F1' = UEBERGEBENER DDNAME NICHT VORHANDEN
*    X'F2' = DATEI MUSS VOR AUFRUF UPRO EROEFFNET SEIN
*    X'F3' = AUSFUEHRUNG RJFCB-MACRO WAR FEHLERHAFT (SIEHE REGISTER 15)
*    X'F4' = KEINE SEQUENTIELLE DATEI
*
*
*  =============================================> MUSTERPROGRAMM PL/1:
*
*PLIJFCB:
*        PROC OPTIONS (MAIN) REORDER;
*        DCL
*        RC             CHAR (1),
*        DDNAME         CHAR (8),
*        1 EXADR,
*          2 BYTEONE    BIT  (8)  INIT('00000111'B),
*          2 ADRJFC     CHAR (3),
*          2 JFCB,
*            3 JFCBDSN  CHAR (7), /* DATA SET NAME (44 BYTE)*/
*            3 FILLER1  CHAR(95), /* FILLER                 */
*            3 JFCBLKSI BIN FIXED,/* MAXIMUM BLOCKSIZE      */
*            3 JFCLRECL BIN FIXED,/* LOGICAL RECORD LENGTH  */
*            3 FILLER2  CHAR(11), /* FILLER                 */
*            3 JFCBNVOL BIT  (8), /* ANZAHL VOL-SER-NUMBERS */
*            3 JFCBVOL1 CHAR (6), /* VOL-SER-NUMBER 1       */
*            3 JFCBVOL2 CHAR (6), /* VOL-SER-NUMBER 2       */
*            3 JFCBVOL3 CHAR (6), /* VOL-SER-NUMBER 3       */
*            3 JFCBVOL4 CHAR (6), /* VOL-SER-NUMBER 4       */
*            3 JFCBVOL5 CHAR (6), /* VOL-SER-NUMBER 5       */
*            3 FILLER3  CHAR(28), /* FILLER                 */
*        UPROG ENTRY OPTIONS(ASM INTER);
*        ON ERROR CALL PLIDUMP ('SB');
*        CALL PLIRETC  (1);
*        ON ENDFILE (BSTALT) GOTO ENDE;
*        OPEN  FILE (BSTALT) RECORD INPUT;
*        DDNAME = 'BSTALT';
*        CALL UPROG (DDNAME,EXADR,RC);
*        IF RC  = '0' THEN DO;
*           PUT ('***** JOB FILE CONTROL BLOCK *****');
*           PUT DATA (EXADR);
*           CALL PLIRETC (0);
*           GOTO ENDE;
*           END;
*        SELECT (RC);
*           WHEN ('1') PUT ('***** UNCORRECT DDNAME *****');
*           WHEN ('2') PUT ('***** NO OPEN FOR FILE *****');
*           WHEN ('3') PUT ('***** LOOK AT REG 15   *****');
*           WHEN ('4') PUT ('***** NO SEQ DATA SET  *****');
*           OTHERWISE;
*           END;
*ENDE:   CLOSE FILE (BSTALT);
*        END PLIJFCB;
*
ASS      START
         ENTRY UPROG
         USING *,15
         B     UPROG
         DROP  15
         DC    C'UPROG'
         DC    AL1(5)
UPROG    EQU   *
         STM   14,11,12(13)
         BALR  10,0
         USING *,10
         LA    4,SAVE
         ST    4,8(0,13)
         ST    13,SAVE+4
         LA    13,SAVE
         ST    1,ARGLIST
         L     4,0(0,1)
         MVC   DDNAME,0(4)
         MVI   RC,C'0'
         LA    2,76                              PTR CVT ADR
         L     2,0(0,2)                          CVT ADR
         L     2,0(0,2)                          PTR TCB WORDS
         L     2,4(0,2)                          CURRENT TCB ADR
         ST    2,TCBADR
         L     2,12(0,2)                         TIOT ADR
         LA    2,24(0,2)                         FIRST DD IN TIOT
         XR    3,3
SCHLTIOT EQU   *
         IC    3,0(2)                            L' DD ENTRY
         CLC   4(8,2),DDNAME
         BE    DDOK
         AR    2,3
         CLI   0(2),X'00'
         BE    DDNO
         B     SCHLTIOT
DDNO     EQU   *
         MVI   RC,C'1'                           NO DDNAME
         B     FEHLER                            SET RC FOR PLI-PROGRAM
DDOK     EQU   *
         LH    4,18(2)                           UCB ADR
         N     4,MASK                      VS2.3 CONVERS UCB L' 16 BITS
         L     5,TCBADR                          TCB ADR
         L     5,8(0,5)                          DEB QUEUE ADR
SCHLDEB  EQU   *
         LH    6,34(0,5)                         UCB ADR
         N     6,MASK                      VS2.3 CONVERS UCB L' 16 BITS
         CR    4,6
         BE    DEBOK
         L     5,4(0,5)                          NEXT DEB ADR
         LTR   5,5                               END OF QUEUE ?
         BNZ   SCHLDEB                           NO
         MVI   RC,C'2'                           NO OPEN FOR FILE
         B     FEHLER                            SET RC FOR PLI-PROGRAM
DEBOK    EQU   *
         L     1,ARGLIST
         L     4,4(0,1)                          EXADR
         ST    4,EXADR                           DCB EXLST ADR
         L     7,24(0,5)                         DCB ADR
         TM    26(7),X'40'
         BO    SEQDAT
         MVI   RC,C'4'
         B     FEHLER
SEQDAT   EQU   *
         MVC   PLIDCBEX,36(7)
         MVC   37(3,7),=AL3(LISTE)
         LA    9,4(0,4)                          JFCB ADR
         ST    9,JFCBADR
         MVC   1(3,4),JFCBADR+1
         STM   14,13,SAVE1+12
         RDJFCB ((7))
         LTR   15,15
         BZ    RETURN
         MVI   RC,C'3'                           REG15 NOT ZERO
FEHLER   EQU   *
RETURN   EQU   *
         MVC   36(4,7),PLIDCBEX
         L     1,ARGLIST
         L     4,4(0,1)
         MVC   4(176,4),JFCBI
         L     4,8(0,1)
         MVC   0(1,4),RC
         L     13,4(13)
         LM    14,11,12(13)
         BR    14                                RETURN TO CALLER
SAVE     DC    18F'0'
DDNAME   DC    CL8'LEER'
MASK     DS    0F
         DC    X'0000FFFF'
TCBADR   DC    F'0'
ARGLIST  DC    F'0'
EXADR    DC    F'0'
JFCBADR  DC    F'0'
PLIDCBEX DC    F'0'
LISTE    DS    0F
         DC    X'07'
         DC    AL3(JFCBI)
JFCBI    DC    CL176' '
RC       DC    C'0'
SAVE1    DC    19F'0'
         END   ASS
               =AL3(LISTE)
