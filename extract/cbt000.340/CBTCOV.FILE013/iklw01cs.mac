***********************************************************************
*    NAME=     IMSCB  (IMS CONTROL BLOCKS)                            *
*                                                                     *
*    ZWECK=    INFORMATIONEN AUS IMS-STEUERBLOECKEN BEREITSTELLEN     *
*                                                                     *
*    ATTR=     REENTRANT                                              *
*                                                                     *
*    REGISTERVERWENDUNG  R2=  BASISREGISTER                           *
*                        R3=  SPEICHER R13                            *
*                        R4=  SPEICHER R1                             *
*                        R5=  ARBEITSREGISTER                         *
*                        R6=  A(PXPARMS)                              *
*                        R7=  A(PST)                                  *
*                        R8=  A(PDIR)                                 *
*                        R9=  A(PSB)                                  *
*                        R11= BASISREGISTER  IMSCB2                   *
*                        R12= ARBEITSREGISTER                         *
*                                                                     *
*********************************************SAVE
IMSCB    CSECT
         SAVE  (14,12)
         LR    R3,R13              ZWSP R13
         LR    R4,R1               ZWSP R1
         BALR  R2,R0
         USING *,R2
         GETMAIN R,LV=72
         LR    R13,R1
         ST    R13,8(,R3)
         ST    R3,4(,R13)
*********************************************MPFB ERMITTELN
A2       L     R6,16               A(CVT)
         L     R6,0(,R6)           A(NEW/OLD)
         L     R6,4(,R6)           A(CURRENT TCB)
         L     R6,112(,R6)         A(1ST SAVEAREA)
         L     R6,24(,R6)          R1=A(MPFB)
         USING PXPARMS,R6
*********************************************VERZWEIGEN JE REGION-TYP
A3       TM    RCTYP1,RCMSG        MSG-REGION
         BO    ABH
         TM    RCTYP1,RCBMP        BMP-REGION
         BO    ABH
         B     BATCH               BATCH-REGION
*********************************************RESTORE
A4       LR    R1,R13
         FREEMAIN R,LV=72,A=(1)
         LR    R13,R3
         MVI   12(R13),X'FF'
         RETURN (14,12),RC=0
***********************************************************************
* BATCH-REGION                                                        *
*********************************************REGION-TYP
BATCH    L     R5,0(,R4)           ADR REGION-TYP
         TM    RCTYP1,RCPRE        DBB-REGION
         BZ    BATCH1B
         MVC   0(3,R5),=C'DBB'
        B      BATCH1D
BATCH1B  TM    RCTYP1,RCDLI        DLI-REGION
         BZ    BATCH1C
         MVC   0(3,R5),=C'DLI'
         B     BATCH1D
BATCH1C  MVC   0(3,R5),=C'   '     REGION-TYP UNKLAR
BATCH1D  TM    0(R4),X'80'         VL-BIT TESTEN
         BO    A4
*********************************************PGM-NAME
BATCH2   L     R5,4(,R4)           ADR PGM-NAME
         MVC   0(8,R5),RCPGM
         TM    4(R4),X'80'         VL-BIT TESTEN
         BO    A4
*********************************************ADR PCB-ADR-LISTE
BATCH3   L     R5,8(,R4)           ADR ADR-FELD IM RUF. PGM
         L     R7,RCPST            A(PST)
         USING PST,R7
         L     R8,PSTPSB           A(PSB-DIRECTORY)
         USING PDIR,R8
         L     R9,PDIRADDR         A(PSB-PREFIX)
         USING PSB,R9
         LA    R12,PSBLIST         A(PCB-ADR-LISTE)
         ST    R12,0(,R5)
         TM    8(R4),X'80'         VL-BIT TESTEN
         BO    A4
*********************************************ADR SEGM-LAENGENFELD
BATCH4   L     R5,12(,R4)          ADR ADR-FELD IM RUF. PGM
         LA    R12,PSTSEGL         ADR(SEGM-LAENGENFELD)
         ST    R12,0(,R5)
         B     A4
***********************************************************************
* ABHAENGIGE REGION                                                   *
***********************************************************************
ABH      CALL  IMSCB2
         B     A4
***********************************************************************
* DEFINITIONEN                                                        *
***********************************************************************
         REG
         IDLI  PSTBASE=0,PDRBASE=0,PSBBASE=0  IMS-MACR0 (S-.DFSPCC30)
         END
