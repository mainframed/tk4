* STAND 06/22/79
****************************************************
*
*  CALL A S T F M I S
*
*  UM MIT DEM ABSTIMMBLATT VOLLSTAENDIGE UNTERLAGEN UEBER DEN
*  PROGRAMMABLAUF ZU HABEN, SOLLEN ALLE ALLOCIERTEN DATEIEN
*  ANGELISTET WERDEN.
*  FUER DIE MIT FILE-KARTEN BDSCHRIEBENEN DATEIEN GESCHIEHT
*  DIES SCHON IM PROGRAMM DRUALIST.
*  ASTFMIS LISTET DIE UEBRIGEN ALLOCIERTEN DATEIEN. DAMIT
*  DIE LISTE NICHT ZU LANG WIRD, WERDEN DIE DATEIEN NICHT
*  ANGELISTET/ANGELISTET, IN DEREN DDANEMEN EINE IN DDTAB
*  DEFINIERTE ZEICHENFOLGE VORKOMMT.
*
************************************************************
         SPACE 2
ASTFMIS  CSECT
         XSAVE 12,SAVE,ASRFMIS
         AREG
         USING DKTAB,R9
         USING DATAB,R10
         L     R10,PATAB
*
*  TIOT SUCHEN
*  ----------
*
         L     R1,16          -->CVT
         L     R1,0(,R1)
         L     R1,4(,R1)      -->TCB
         L     R3,12(,R1)     -->TIOT
         LA    R3,24(,R3)     -->FIRST TIOT ENTRY
*
*  ALLE DDNAMEN IN TIOT UEBERPRUEFEN
*  --------------------------------
*
TIOT10   EQU   *
         CLI   0(R3),X'0'
         BE    ENDE
         CLI   4(R3),C' '     DDNAME
         BE    TIOT20         CONCATINATION
         CLI   4(R3),X'00'
         BE    TIOT20         FREIGEGEBEN
         BAL   R11,EXEC       UEBERPRUEFUNG
TIOT20   EQU   *
         SR    R0,R0
         IC    R0,0(R3)       LAENGE TIOT ENTRY
         AR    R3,R0          NEXT TIOT ENTRY
         B     TIOT10
*
*  ENDE
*  ----
*
ENDE     EQU   *
         XRETURN              0
*
*  FESTSTELLEN, OB DSNAME GELISTET WERDEN SOLL
*  -------------------------------------------
*
EXEC     EQU   *
*
*  SUCHEN, OB FUER DDNAMEN FILEKARTE VORHANDEN IST
*
         L     R9,PKTAB       -->ABSTIMMKARTEN
EXEC100  EQU   *
         CLC   KNEXT,=F'-1'
         BE    EXEC200        KEINE FILEKARTE VORHANDEN
         CLI   KNEXT,KFILE
         BNE   EXEC190
         CLC   TFDDN,4(R3)
         BE    EXEC900        FILEKARTE VORHENDEN
EXEC190  EQU   *
         L     R9,KNEXT
         B     EXEC100
*
*  PRUEFUNG DES DDNAMEN, OB ER IN DDTAB DEFINIERTE NAMENSKOMPONENTEN
*  ENTHAELT
*
EXEC200  EQU   *
         LA    R2,DDTAB
    SR    R1,R1
EXEC210  EQU   *
         IC    R1,1(,R2)      LAENGE IN DDTAB
         TM    0(R2),3        KENNUNG IN DDTAB
         BM    EXEC240        1 = LINKSBUENDIG
         BZ    EXEC500        0 = DDTAB DURCHSUCHT
*  FUER RECHTSBUENDIGEN VERGLEICH ENDE DES DDNAMEN SUCHEN
         LA    R4,7+4(R3)
EXEC220  EQU   *
         CLI   0(R4),C' '
         BNE   EXEC230
         BCT   R4,EXEC220
EXEC230  EQU   *
         SR    R4,R1          -->ZU VERGLEICHENDER TEIL DES DDNAMEN
         B     EXEC250
*  VERGLEICH LINKSBUENDIG
EXEC240  EQU   *
         LA    R4,4(R3)
EXEC250  EQU   *
         EX    R1,CLC
         BE    EXEC260
         LA    R2,3(R1,R2)    -->NAECHSTER EINTRAG IN DDTAB
         B     EXEC210
*
*  NICHT DRUCKEN BEI GLEICHHEIT / DRUCKEN BEI GLEICHHEIT
*
EXEC260  EQU   *
         TM    0(R2),X'10'    KENNUNG IN DDTAB
         BO    EXEC500        DRUCKEN
         BZ    EXEC900        NICHT DRUCKEN
*
*  DSNAMEN LISTEN
*
EXEC500  EQU   *
         MVC   DDNAME,4(R3)
         CLI   ZEILE,C' '
         BE    EXEC510        UEBERSCHRIFT GEDRUCKT
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
         MVC   ZEILE+4(13),=13CL1'-'
         CALL  ABLTPUT,MF=(E,ABLTPUTL)
EXEC510  EQU   *
         CALL  ABLTFILE,(DDNAME),VL
         BR    R11
*
*  DSNAME NICHT LISTEN
*
EXEC900  EQU   *
         BR    R11
*
*  DEFINITIONEN
*  -----------
*
PATAB    DC    V(ATAB)
*
*  DDTAB = DEFINITION DER ZEICHENFOLGEN, AUF DIE DDNAME GEPRUEFT WIRD
*          1.BYTE X1 ZEICHENFOLGE AM ANFANG DES DDNAMEN PRUEFEN
*                 X2 ZEICHENFOLGE AM ENDE PRUEFEN
*                 0X BEI GLEICHHEIT NICHT LISTEN
*                 1X BEI GLEICHHEIT LISTEN
*                 00 ENDE VON DDTAB
*          2.BYTE LAENGE DER ZEICHENFOLGE
*          3.-N.BYTE ZEICHENFOLGE
DDTAB    DS    0CL1
         DC    X'11',X'4',CL5'SYSUT'
         DC    X'01',X'2',CL3'AST'
         DC    X'03',X'2',CL3'LIB'
         DC    X'01',X'01',CL2'SY'
         DC    X'01',X'2',CL3'IMS'
         DC    X'01',X'4',CL5'SORTW'
         DC    X'01',X'4',CL5'PARMK'
         DC    X'01',X'4',CL5'OP023'
         DC    X'01',X'7',CL8'PGM=*.DD'
         DC    X'01',X'4',CL5'EXITS'
         DC    X'00'
CLC      CLC   0(0,R4),2(R2)
DDNAME   DS    CL8
ABLTPUTL CALL  ,(ZEILE),VL,MF=L
ZEILE    DC    CL80'0** D A T E I E N   (OHNE FILE-KARTE)'
         MATAB
         MKTAB
         END
