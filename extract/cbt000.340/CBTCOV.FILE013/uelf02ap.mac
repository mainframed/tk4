 /** PROGRAMM ZUM LEICHTEREN BESCHREIBEN DER BILDSCHIRMFORMATE ***/
 MLMWSCH: PROC (PARM) OPTIONS (MAIN);
 DCL ATTRIB FILE INPUT SEQUENTIAL,          /*FILE-DEKLARATIONEN  */
     DATAS  FILE INPUT SEQUENTIAL,
     COMPEIN FILE OUTPUT SEQUENTIAL;

 DCL (ASTRING,DSTRING,CSTRING) CHAR(80),    /* READ/WRITE-STRINGS  */
     PARM     CHAR(20) VAR;                 /* JCL-PARAMETER-STRING*/

 DCL N        BIN FIXED (15)  INIT(0)     , /*POSITION DES SUCHERS */
     LEN      PICTURE '999'   INIT(240)   , /*FELDLAENGE ALS PIC   */
     XLEN     BIN FIXED (15)  INIT(240)   , /*FELDLAENGE ALS BIN-F.*/
     LL       PICTURE '99'    INIT(1)     , /*LINIENZAHL           */
     PP       PICTURE '99'    INIT(1)     , /*POSITIONSZAHL        */
     NR       PICTURE '99'    INIT(0)     , /*FELDNUMMER ==>F-NAME */
     ATSTR    CHAR (20) INIT('ABCDEFGHIJKLMNOPQRST'),
              /*POSITION EINES ATTRZEICHENS ==>NR.DER ATTR-KOMBINAT*/
     AT       BIN FIXED (15)  INIT(1)     , /*NR. DER ATTR-KOMBINAT*/
    ATBER(0:20) CHAR(7) INIT('A,P    ','A,P    ','A,U    ','A,M    ',
         'N,P    ','N,U    ','N,M    ','A,P,H  ','A,U,H  ','A,M,H  ',
         'N,P,H  ','N,U,H  ','N,M,H  ','A,P,I  ','A,U,I  ','A,M,I  ',
         'N,P,I  ','N,U,I  ','N,M,I  ','P,A,H  ','A,P,D  '),
              /* ATBER(AT) ==> ENDGUELTIGE ITS-ATTRIBUTBESCHREIBUNG*/
              /* ATBER(0)  ==> DEFAULT-WERT  =  'A,P    '          */
     POLYA    CHAR (1921) VAR INIT('')    , /* ATTRIBUTSTRING      */
     POLYD    CHAR (1920) VAR INIT('')    , /* DATENSTRING         */
     POLYC    CHAR (300)  VAR INIT('')    , /* AUSGABESTRING       */
     BLANK    CHAR (80)      INIT((80)' '),

     (P1,P2,P3,P4)  BIT(1)    INIT('1'B)  ; /*LIST- UND CHECKBED.   */
                                            /*KEINE SINGL-BEDINGUNG */
                                            /*ATR-ZUWEISUNG ERLAUBT */
     IF INDEX(PARM,'NOL') > 0 THEN P1='0'B; /*ZUWEISUNG DER VON     */
     IF INDEX(PARM,'NOC') > 0 THEN P2='0'B; /*DER JCL UEBERGEBENEN  */
     IF INDEX(PARM,'SIN') > 0 THEN P3='0'B; /*PARAMETER AN VARIABLEN*/


  /********************************************************************/
  /*   FALLS KEINE 'SINGL'-BEDINGUNG ANGEGEBEN,- EINLESEN UND         */
  /*   VERKETTUNG DER DATAS-DATEI ZU EINEM EINZIGEN STRING (POLYD).   */
  /********************************************************************/
  IF P3 THEN DO;
  DO K=1 TO 24;
     READ FILE (DATAS) INTO (DSTRING);
     IF P1 THEN PUT EDIT (DSTRING) (SKIP(2),COL(20),A);            /*P*/
     POLYD=POLYD××DSTRING;
  END;
  END;
     IF P1 THEN PUT PAGE ;                                         /*P*/
     %PAGE;
  /******************************************************/
  /* UEBERTRAGUNG DER FORMATKARTE IN DIE COMPEIN-DATEI  */
  /******************************************************/
  READ  FILE (ATTRIB)  INTO (DSTRING);
  WRITE FILE (COMPEIN) FROM (DSTRING);

  /*************************************************************/
  /* EINLESEN UND VERKETTUNG DER ATTRIB-DATEI ZUM POLYA-STRING */
  /*************************************************************/
  DO K=1 TO 24;
     READ FILE(ATTRIB) INTO (ASTRING);
     POLYA=POLYA××ASTRING;
     IF P1 THEN PUT EDIT (ASTRING) (SKIP(2),COL(20),A);            /*P*/
  END;
     POLYA=POLYA××'"';
     IF P1 THEN PUT EDIT (DSTRING) (PAGE,SKIP(2),COL(20),A);       /*P*/


 /************************************/
 /*  ERSTELLUNG DER FELD-KARTEN      */
 /************************************/
  DO NR=1 TO 96 WHILE (N < 1920 & XLEN > 2 );
         XLEN= INDEX(SUBSTR(POLYA,N+2),'"');
         AT = INDEX(ATSTR,SUBSTR(POLYA,N+2,1));
               IF XLEN > 240 THEN DO;
                                 XLEN =240;
                                 P4='0'B;
                                 END;
               ELSE P4='1'B;

          LEN= XLEN;
          PP = MOD((N+1),80);
          LL = ((N+1)-PP)/80+1;

 CSTRING='FIELD  NAME=MFLD'××NR××' ATR='××ATBER(AT)××' POS='××
         LL××','××PP××' LEN='××LEN××' PAD=R,NULL';
         WRITE FILE(COMPEIN) FROM (CSTRING);
         IF P1 THEN PUT EDIT (CSTRING) (SKIP(2),COL(20),A);        /*P*/

 /************************************/
 /* ZUSAMMENSTELLUNG DER DATENKARTEN */
 /************************************/
 IF P3 THEN
      POLYC='DATA MFLD'××NR××' '××SUBSTR(POLYD,N+2,XLEN-1)××'\'××BLANK;
 ELSE POLYC='DATA MFLD'××NR××'  '××SUBSTR(POLYA,N+3,XLEN-2)××'\'××BLANK;

 DO K=0 TO (XLEN+12-(MOD((XLEN+12),72)))/72 ;
     CSTRING=SUBSTR(POLYC,K*72+1,72)××'        ';
     WRITE FILE (COMPEIN) FROM (CSTRING);
         IF P1 THEN PUT EDIT (CSTRING) (SKIP(1),COL(20),A);        /*P*/
 END;
         N=N+XLEN;
 END;

     %PAGE;
 /************************************/
 /*         FEHLERMELDUNGEN          */
 /************************************/
 IF P2 THEN DO;
            IF XLEN < 3 THEN DO;
                    PUT LIST ('ERROR:FELDLAENGE<3 IN IMS VERBOTEN ');
                    END;
            IF NR = 96 THEN DO;
                    PUT LIST ('ERROR: UEBER 96 FELDER');
                    END;

 END;
     ON ENDFILE (ATTRIB) BEGIN;
      IF P2 THEN PUT LIST ('ERROR: ZU WENIG SAETZE IN ATTRIBUT-DATEI');
     END;
     ON ENDFILE (DATAS)  BEGIN;
      IF P2 THEN PUT LIST ('ERROR: ZU WENIG SAETZE IN DATAS-DATEI');
     END;

 IF P1 THEN PUT LIST ('ENDE DES PROGRAMMS MLMWSCH ');              /*P*/
 END MLMWSCH;
