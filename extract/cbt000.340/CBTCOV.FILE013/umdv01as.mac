         TITLE 'BENUTZERHANDBUCH'
A3080    START 0
         SPACE
         COPY  REGISTER
         SPACE
         USING *,15
         SAVE  (14,12),,*
         SPACE
         L     R14,=A(SAVE)
         ST    R13,4(R14)
         LR    R13,R14
         B     ANFANG
         LTORG
         SPACE
*      VERARBEITUNG DES PARM-PARAMETERS                             MUE
ANFANG   L     R1,0(R1)                PARMFELD-ADR LADEN           MUE
         LH    R2,0(R1)                LADEN LAENGE PARMFELD
         MVC   Z3+26(14),=CL14' '      KONSTANTE LOESCHEN           MUE
         MVC   Z5+26(14),=CL14' '      KONSTANTE LOESCHEN           MUE
         CH    R2,=H'28'
         BNE   TLIST
         ZAP   LISTEN,=P'1'
MVUEB    MVC   Z3+26(14),2(R1)
         MVC   Z5+26(14),16(R1)
         B     ANF
TLIST    CH    R2,=H'30'
         BE    TLIST10
TLRET    L     R13,SAVE+4
         RETURN (14,12),RC=12
TLIST10  CLI   30(R1),C'0'
         BL    TLRET
         CLI   30(R1),C'9'
         BH    TLRET
         CLI   31(R1),C'0'
         BL    TLRET
         CLI   31(R1),C'9'
         BH    TLRET
         PACK  LISTEN,30(2,R1)
         B     MVUEB
ANF      BALR  2,0                                                  MUE
         DROP  15
         USING *,2,3,4,5
         SPACE
         LA    R15,4095
         LA    R3,1(R15,R2)
         LA    R4,1(R15,R3)
         LA    R5,1(R15,R4)
         SPACE
*       TAGESDATUM
         LR    R8,R14
         LR    R9,R15
         CALL  DAYTIME,(XDAT)
         LR    R14,R8
         LR    R15,R9
         B     A1
         SPACE
XDAT     TAGZEIT
         SPACE
LISTEN   DS    PL2
*       TAB-DATEI
A1       OPEN  (DTFTAB,(INPUT))
         LA    R8,TAB-80
A2       GET   DTFTAB,KARTE
         SPACE
         LA    R8,80(R8)
         MVC   0(80,R8),KARTE
         B     A2
         SPACE
TABEND   MVI   80(R8),C'$'
         MVC   85(75,R8),=CL75'TEXT NICHT GEFUNDEN'
         CLOSE (DTFTAB)
         SPACE
*       DRUCK-DATEI
         OPEN  (DTFBS1,(OUTPUT))
*
*       KARTEN-DATEI
OPKART   OPEN  (DTFKA,(INPUT))
         SPACE
*      1.KARTE LESEN
         BAL   R14,LES
         SPACE
*      FRAGE DATUMSKARTE ?
         CLI   KARTE+78,C'0'
         BNE   FEHLER
         SPACE
*      KOPFZEILEN AUFBEREITEN AUS BEW.KARTE
         MVC   Z1+114(6),XJAHR
         MVI   Z1+120,C'/'
         MVC   Z1+121(4),XSTUNDE
         MVC   Z3+80(1),KARTE
         MVI   Z3+81,X'4B'
         MVC   Z3+82(2),KARTE+1
         MVI   Z3+84,X'4B'
         MVC   Z3+85(2),KARTE+3
         MVC   Z5+79(8),KARTE+12
         MVC   Z5+104(1),KARTE
         MVC   Z5+106(2),KARTE+1
         MVC   Z5+109(2),KARTE+3
         MVC   Z5+113(4),KARTE+5
         MVC   Z5+119(3),KARTE+9
         MVC   Z5+125(8),KARTE+72
         SPACE
         MVI   DRUBER,X'40'
         MVC   DRUBER+1(132),DRUBER
         SPACE
         BAL   R14,LES
         MVC   GRALT,KARTE
         SPACE
*      KOPFZEILEN AUS TAB. FUELLEN
         LA    R14,TAB-80
SUCH     LA    R14,80(R14)
         CLI   0(R14),C'$'
         BE    *+14
         CLC   0(5,R14),KARTE
         BNE   SUCH
         MVC   Z3+42(34),5(R14)
         MVC   Z5+42(33),39(R14)
         CLI   0(R14),C'$'
         BNE   *+16
         ZAP   LISTEN,=P'1'   FEHLENDE UEBERSCHRIFT, NUR 1 LISTE
         MVC   COND,=H'8'     COND-CODE 8
         SPACE
         BAL   R14,KOPF
         NI    KOPF+5,X'0F'
         MVI   Z1,C'1'    VORSCHUB KANAL 1
         SPACE
*       TEXTKARTEN VERARBEITEN
TEXT     CLI   KARTE+78,C'0'
         BNE   TEXT1
         SPACE
*       DATUMSKARTE VERARBEITEN
         MVC   Z5+79(8),KARTE+12
         B     LESEN
         SPACE
TEXT1    CP    ZZ,=P'0'
         BE    DRUCKZEI
         CLI   KARTE+79,C'2'
         BL    DRUCKZEI
         PACK  DOUBLE,KARTE+79(1)
         CVB   R8,DOUBLE
         BCTR  R8,0
LEERZEI  CP    ZZ,ZMAX
         BNL   DRUCKZEI
         MVI   DRUBER+14,X'4B'
         MVI   DRUBER+96,X'4B'
         MVI   DRUBER+24,C'×'
         MVI   DRUBER+87,C'×'
         AP    ZZ,=P'1'
         BAL   R14,DRUCKEN
         BCT   R8,LEERZEI
DRUCKZEI DS    0H
         CP    ZZ,ZMAX
         BL    *+8
         BAL   R14,KOPF
         MVC   DRUBER+26(60),KARTE+12
         MVI   DRUBER+14,X'4B'
         MVI   DRUBER+96,X'4B'
         MVI   DRUBER+24,C'×'
         MVI   DRUBER+87,C'×'
         MVC   DRUBER+104(1),KARTE
         MVC   DRUBER+106(2),KARTE+1
         MVC   DRUBER+109(2),KARTE+3
         MVC   DRUBER+113(4),KARTE+5
         MVC   DRUBER+119(3),KARTE+9
         MVC   DRUBER+125(8),KARTE+72
         AP    ZZ,=P'1'
         BAL   R14,DRUCKEN
         SPACE
         SPACE
*       KARTEN LESEN
LESEN    BAL   R14,LES
         SPACE
*      SEITEN-WECHSEL ?
         CLI   KARTE+79,C'*'           NEUE SEITE ?                 MUE
         BE    NEWPAGE                                              MUE
         CLC   GRALT(5),KARTE          SEITENWECHSEL BEI KAPITELW.  MUE
         BE    TEXT
         SPACE
*      ABSATZ-WECHSEL ?
NEWPAGE  EQU   *                                                    MUE
         CLC   GRALT(5),KARTE
         BNE   WEITER
WEIT2    CLI   KARTE+78,C'0'
         BNE   WEIT3
         MVC   Z5+79(8),KARTE+12
         MVC   Z5+104(1),KARTE
         MVC   Z5+106(2),KARTE+1
         MVC   Z5+109(2),KARTE+3
         MVC   Z5+113(4),KARTE+5
         MVC   Z5+119(3),KARTE+9
         MVC   Z5+125(8),KARTE+72
WEIT3    DS    0H
         BAL   R14,KOPF
         MVC   GRALT,KARTE
         B     TEXT
*      ARBEITSENDE ?
WEITER   CLI   KARTE,X'FF'
         BE    ENDE
         SPACE
*      KOPFZEILEN ERGAENZEN
         LA    R14,TAB-80
WEIT1    LA    R14,80(R14)
         CLI   0(R14),C'$'
         BE    *+14
         CLC   0(5,R14),KARTE
         BNE   WEIT1
         MVC   Z3+42(34),5(R14)
         MVC   Z5+42(33),39(R14)
         MVC   Z3+80(1),KARTE          MOVE                         MUE
         MVC   Z3+82(2),KARTE+1          NEUES KAPITEL              MUE
         MVC   Z3+85(2),KARTE+3            IN DEN KOPF              MUE
         B     WEIT2
         SPACE
*      ARBEITSENDE
ENDE     BAL   R14,UP1
         MVI   DRUBER,C'1'
         BAL   R14,DRUCKEN
         CLOSE (DTFKA)
         OI    KOPF+5,X'F0'
         SP     SZ,SZ
         SP    LISTEN,=P'1'
         CP    LISTEN,=P'1'
         BNL   OPKART
         CLOSE (DTFBS1)
         SPACE
         LH    R15,COND
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
*        **********************
         SPACE
FEHLER   MVI   DRUBER,C'1'
         MVC   DRUBER+1(L'FTEXT),FTEXT
         BAL   R14,DRUCKEN
         MVC   COND,=H'16'
         ZAP   LISTEN,=P'1'
         B     ENDE+4
         EJECT
*  U N T E R P R O G R A M M E
         SPACE
*      KARTEN LESEN
         DS    F
LES      ST    R14,*-4
         GET   DTFKA,KARTE
         SPACE
LESE     L     R14,LES-4
         BR    R14
         SPACE
KAEND    MVC   KARTE(9),=9X'FF'
         B     LESE
         SPACE
*      KOPF DRUCKEN
         DS    F
KOPF     ST    R14,*-4
         B     KOPF1
         BAL   R14,UP1       LEERZEILEN DRUCKEN
         SPACE
KOPF1    SP    ZZ,ZZ
         AP    SZ,=P'1'                SEITE ZAEHLEN                MUE
         SPACE
         PUT   DTFBS1,Z1
         SPACE
         PUT   DTFBS1,Z2
         SPACE
         PUT   DTFBS1,Z3
         SPACE
         PUT   DTFBS1,Z4
         SPACE
         PUT   DTFBS1,Z5
         SPACE
         PUT   DTFBS1,Z6
         SPACE
         PUT   DTFBS1,Z7
         MVI   DRUBER+14,X'4B'
         MVI   DRUBER+96,X'4B'
         MVI   DRUBER+24,C'×'
         MVI   DRUBER+87,C'×'
         BAL   R14,DRUCKEN
         SPACE
         L     R14,KOPF-4
         BR    R14
         SPACE
*      EINZELZEILE DRUCKEN
         DS    F
DRUCKEN  ST    R14,*-4
         SPACE
         PUT   DTFBS1,DRUBER
         SPACE
         MVI   DRUBER,X'40'
         MVC   DRUBER+1(132),DRUBER
         SPACE
         L     R14,DRUCKEN-4
         BR    R14
         SPACE
*      LEERZEILEN DRUCKEN
         DS    F
UP1      ST    R14,*-4
         SPACE
UP11     CP    ZZ,ZMAX
         BH    UP13
         MVI   DRUBER,X'40'
UP12     MVI   DRUBER+14,X'4B'
         MVI   DRUBER+96,X'4B'
         MVI   DRUBER+24,C'×'
         MVI   DRUBER+87,C'×'
         AP    ZZ,=P'1'
         BAL   R14,DRUCKEN
         B     UP11
         SPACE
UP13     DS    0H
         MVI   DRUBER+53,C'-'
         OI    SZ+2,X'0F'              VORZEICHEN IN 'F' AENDERN    MUE
         UNPK  DRUBER+54(4),SZ(3)
         MVI   DRUBER+58,C'-'
         MVI   DRUBER+14,C'.'
         MVI   DRUBER+96,C'.'
         MVI   DRUBER+24,C'×'
         MVI   DRUBER+87,C'×'
         CLI   KARTE,X'FF'
         BNE   *+10
         MVC   DRUBER+60(6),=C'(ENDE)'
         BAL   R14,DRUCKEN
         MVI   DRUBER+14,C'.'
         MVI   DRUBER+96,C'.'
         MVI   DRUBER+24,C'-'
         MVC   DRUBER+25(63),DRUBER+24
         BAL   R14,DRUCKEN
         SPACE
UP1E     L     R14,UP1-4
         BR    R14
         EJECT
*  D E F I N I T I O N E N
         SPACE
KARTE    DS    CL80
DRUBER   DS    CL133
GRALT    DS    CL12
         SPACE
*      KOPFZEILEN
Z1       DC    X'40'
         DC    C'             .         ----------'
         DC    C'---------------------------------'
         DC    C'---------------------        .   '
         DC    C'    A3080201  XXXXXX             '
Z2       DC    X'40'
         DC    C'             .         ×         '
         DC    C'      ×                          '
         DC    C'          ×         ×        .   '
         DC    C'     GRUPPE SEITE  LFD.   KENNUNG'
Z3       DC    X'40'
         DC    C'             .         × BENUTZER'
         DC    C'-INF. × XXXXXXXXXXXXXXXXXXXXXXXXX'
         DC    C'XXXXXXXXX ×  X.XX.XX×        .   '
         DC    C'                    NR.          '
Z4       DC    X'40'
         DC    C'             .         ×         '
         DC    C'      ×                          '
         DC    C'          ×         ×        .   '
         DC    C'                                 '
Z5       DC    X'40'
         DC    C'             .         ×    MDV-R'
         DC    C'L     × XXXXXXXXXXXXXXXXXXXXXX777'
         DC    C'XXXXXXXX  × XX.XX.XX×        .   '
         DC    C'                                 '
Z6       DC    X'40'
         DC    C'             .         ×         '
         DC    C'      ×                          '
         DC    C'          ×         ×        .   '
         DC    CL33' '
Z7       DC    X'40'
         DC    C'             .         ×---------'
         DC    C'---------------------------------'
         DC    C'--------------------×        .   '
         DC    CL33' '
ZZ       DC    PL2'0'
         SPACE
SZ       DC    PL3'0'                  SEITENZAEHLER                MUE
ZMAX     DC    P'56'     MAX. ZEILEN PRO SEITE
DOUBLE   DS    D
         SPACE
SAVE     DS    9D
COND     DC    H'0'
FTEXT    DC    C'ABBRUCH DATUMSKARTE FEHLT'
         EJECT
*      DCB FUER TABELLEN LESEN
DTFTAB   DCB   DDNAME=DTFTAB,                                          *
               DSORG=PS,                                               *
               MACRF=(GM),                                             *
               LRECL=80,                                               *
               EODAD=TABEND,                                           *
               RECFM=FB
         EJECT
*      DCB FUER KARTEN LESEN
DTFKA    DCB   DDNAME=DTFKA,                                           *
               DSORG=PS,                                               *
               MACRF=(GM),                                             *
               LRECL=80,                                               *
               EODAD=KAEND,                                            *
               RECFM=FB
         EJECT
*      LISTE DRUCKEN
DTFBS1   DCB   DDNAME=DTFBS1,                                          *
               DSORG=PS,                                               *
               MACRF=(PM),                                             *
               LRECL=133,                                              *
               RECFM=FBA                                            MUE
         LTORG
TAB      DS    CL16000
         END   A3080
