PTFCOMP  START
         REG
         XSAVE R12,SVA,PTFCOMP
         OPEN  (TAB1,(INPUT),TAB2,(INPUT),LIST,(OUTPUT))
         CALL  DATE,(DATUM),VL
         LA    R2,TAB1
         LA    R3,TAB2
         BAL   R4,SETUP
         BAL   R4,COMPARE
         CVD   R7,DW
         UNPK  FOOT1+11(4),DW
         OI    FOOT1+14,X'F0'
         PUT   LIST,FOOT1
         CLOSE (TAB1,,TAB2)
         OPEN  (TAB1,(INPUT),TAB2,(INPUT))
         LA    R2,TAB2
         LA    R3,TAB1
         BAL   R4,SETUP
         BAL   R4,COMPARE
         CVD   R7,DW
         UNPK  FOOT1+11(4),DW
         OI    FOOT1+14,X'F0'
         PUT   LIST,FOOT1
         CLOSE (TAB1,,TAB2,,LIST)
         XRETURN 0
SETUP    EQU   *
         GET   (R2),CARD1
         GET   (R3),CARD2
         MVC   VOLSER1,CARD1
         MVC   VOLSER2,CARD2
         MVC   DSN1(44),CARD1+6
         MVC   DSN2(44),CARD2+6
         LA    R1,EODAD1
         STCM  R1,7,33(R2)
         LA    R1,EODAD2
         STCM  R1,7,33(R3)
         PUT   LIST,HEAD1
         PUT   LIST,HEAD2
         PUT   LIST,BLANK
         PUT   LIST,HEAD3
         PUT   LIST,HEAD4
         PUT   LIST,BLANK
         GET   (R2),CARD1
         GET   (R3),CARD2
         LA    R6,50
         SR    R7,R7
         BR    R4
COMPARE  EQU   *
         CLC   CARD1(7),CARD2
SW       BL    PUT
         BH    GET2
         GET   (R2),CARD1
GET2     EQU   *
         GET   (R3),CARD2
         B     COMPARE
PUT      EQU   *
         MVC   LISTREC,BLANK
         MVC   LISTREC+1(7),CARD1
         PUT   LIST,LISTREC
         LA    R7,1(R7)
         BCT   R6,NOSKIP
         PUT   LIST,HEAD1
         PUT   LIST,HEAD2
         PUT   LIST,BLANK
         PUT   LIST,HEAD3
         PUT   LIST,HEAD4
         PUT   LIST,BLANK
         LA    R6,50
NOSKIP   EQU   *
         GET   (R2),CARD1
         B     COMPARE
EODAD1   EQU   *
         MVI   SW+1,X'40'
         BR    R4
EODAD2   EQU   *
         MVI   SW+1,X'F0'
         B     PUT
CARD1    DC    CL80' '
CARD2    DC    CL80' '
FOOT1    DC    CL133'-THERE ARE 1234 DIFFERENCES'
HEAD1    DC    CL120'1PTF-COMPARE-RUN'
DATUM    DC    CL13' '
HEAD2    DC    C' ',132CL1'-'
HEAD3    DC  C'-',C'THE FOLLOWING PTFS ARE INSTALLED IN CDS - VOLSER='
VOLSER1  DC    CL6' '
         DC    C',DSN='
DSN1     DC    CL90' '
HEAD4    DC    C' ',C'BUT NOT IN CDS - VOLSER='
VOLSER2  DC    CL6' '
         DC    C',DSN='
DSN2     DC    CL110' '
BLANK    DC    CL133' '
LISTREC  DC    CL133' '
DW       DC    D'0'
TAB1     DCB   DDNAME=TAB1,DSORG=PS,MACRF=GM,                          *
               LRECL=80,RECFM=FB,EODAD=EODAD1
TAB2     DCB   DDNAME=TAB2,DSORG=PS,MACRF=GM,                          *
               LRECL=80,RECFM=FB,EODAD=EODAD2
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      *
               LRECL=133,RECFM=FBA,BLKSIZE=133
         END
