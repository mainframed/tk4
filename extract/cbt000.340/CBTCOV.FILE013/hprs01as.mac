***********************************************************************
*        MODIFIKATION DES HASP IV SEPARATORS
*        DER SEPARATOR LIEFERT FOLGENDE DATEN IN BLOCKLETTERN:
*        JOBNAME
*        PROGRAMMERS NAME
*        INFO-FELD  (DYNAMISCH GELADEN UEBER $DM$-COMMAND 1ZEIL.DRUCK)
*        DATUM     (Z.B. 20.APR.77)
*        ZEIT      STARTZEIT DES SEPARATORS
*
*              ES WERDEN DIE STMT# P5468000 BIS P5792000 ESETZT
*
*              GLEICHZEITIGER DRUCKBEGINN/ENDE ZU EINEM JOB AUF
*              MEHREREN DRUCKERN IST ZU VERMEIDEN, DA SONST
*              ADRESSVERAENDERUNGEN IM PMESSAGE-FELD AUFTRETEN KOENNEN,
*              DIE ZU EINEM CATASTROPHIC ERROR FUEHREN
*
*        UM DAS INFORMATIONSFELD ZU AKTIVIEREN UND ZU FUELLEN, IST
*        FOLGENDER COMMAND- UND TEXTAUFBAU IN EINER LOCHKARTE NOT-
*        WENDIG:  ES WIRD DER HASP-DISPLAY-MESSAGE COMMAND BENUTZT,
*        WOBEI JEDOCH DER ZIELORT DURCH EIN $ ZU ERSETZEN IST.
*        DER EIGENTLICHE MESSAGETEXT IST IN SPALTE 16 BIS 59 ZU LOCHEN
*        (44 ZEICHEN). JE DRUCKZEILE KOENNEN 3 KARTEN MIT TEXT VER-
*        SEHEN WERDEN. DIE KARTEN SIND TABELLARISCH ZU NUMMERIEREN,
*        1. DRUCKZEILE 01 BIS 03, 2. DRUCKZEILE 04 BIS 06 (MIT FUEH-
*        RENDEN NULLEN) USW BIS 7. DRUCKZEILE 19 BIS 21.
*        DER GESAMTE INFORMATIONSFELDINHALT KANN DURCH FORTLASSEN
*        DER NUMMERIERUNG GELOESCHT WERDEN. DIE NUMMERIERUNG IST DER
*        ERSTE TEIL DES TEXTES UND IST DURCH DEN SCHLUESSELWORT-
*        PARAMETER  N=  IDENTIFIZIERT. DER KARTENSATZ WIRD UEBER DEN
*        KARTENLESER EINGELESEN.
*        KARTENAUFBAU:
*/*$DM$,'                                                   ' LOESCHEN
*/*$DM$,'N=01   HIER BEGINNT DER TEXT BIS EINSCHL.SPALTE 59 ' TEXT
***********************************************************************
PRINTID  DS    0H
         ST    PL,PCESAVEA        SAVE RETURN REGISTER
         LA    PW,PCCWCHN         GET START OF CCW CHAIN
         ST    PW,IOBSTART        STORE INTO IOB
         SPACE 2
         MVI   PMESSAGE+39,C'S'        SEPARATORPAGE ID TYPE
         CLC   2(3,R1),=C'END'
         BNE   *+8                     START SEPARATOR
         MVI   PMESSAGE+39,C'E'
         XC    PMESSAGE(9),PMESSAGE    LOESCHEN
         MVI   PMESSAGE+9,X'01'    KENNUNG 1.SEPARATORSEITE
DATE     DS    0H
         TIME  DEC                          GET TIME AND DATE
         ST    R0,PMESSAGE+16          TIME
         ST    R1,PCCWORK+4                 DATE
         MVI   PYEARTAB+4,28       ASSUME NON-LEAP YEAR
         TM    PCCWORK+5,X'01'     TEST
         BO    *+16                 FOR
         TM    PCCWORK+5,X'12'       LEAP
         BM    *+8                    YEAR
         MVI   PYEARTAB+4,29       SET UP TABLE FOR LEAP YEAR
         MVC   PMESSAGE+36(3),=X'402120'
         ED    PMESSAGE+36(3),PCCWORK+5  JAHR
         MVI   PMESSAGE+36,X'4B'
         XC    PCCWORK(6),PCCWORK  CLEAR ALL BUT JULIAN DATE
         SR    R0,R0               CLEAR
         CVB   R1,PCCWORK          CONVERT JULIAN DATE TO BINARY
         LA    PW,PYEARTAB-4       PREPARE TO SCAN CONVERSION TABLE
PRIDATE  SR    R1,R0               CONVERT
         LA    PW,4(,PW)            FROM JULIAN
         IC    R0,0(,PW)             DATE TO
         CR    R0,R1                  MONTH
         BL    PRIDATE                 AND DAY
         CVD   R1,PCCWORK          CONVERT TO DECIMAL
         MVO   PCCWORK(2),PCCWORK+6(2)  SHIFT FOR EDIT
         MVC   PMESSAGE+29(4),=X'4020204B'
         ED    PMESSAGE+29(3),PCCWORK  TAG
         MVC   PMESSAGE+33(3),1(PW)    MONAT
        $LOAD  HASPBLOK            LOAD BLOCK LETTER TABLES
STARTSEP EQU   *
         LM    PC1,PC2,PRCCWEJ    VORSCHUB AUF KANAL 1
         BAL   PL,PPPUT
         LM    PC1,PC2,PRCCWSP    LOAD SPACE CCW
         ICM   PC1,8,=X'0B'       SPACE 1
         BAL   PL,PPPUT           ADD CCW TO CHAIN
         MVC   PMESSAGE(8),PPJJNAME    AUFTRAGSNUMMER
         CLI   PMESSAGE+7,C' '
         BNE   *+10
         MVC   PMESSAGE+8(1),PPJPNAME+19    TAETIGKEITSSCHLUESSEL
         BAL   PL,BLKPRT           WRITE 12 LINES OF JOBNAME
         LM    PC1,PC2,PRCCWSP     LOAD SPACE CCW
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         SPACE 2
         MVC   PMESSAGE(9),PPJPNAME    DESTINATION
         BAL   PL,BLKPRT
         CLI   PMESSAGE+39,C'E'        SEPARATOR PAGE ID TYPE ?
         BE    NOMSG
         LA    R7,PRTMSG@
         CLI   0(R7),C' '    ALLGEMEINES INFORMATIONSFELD DRUCKEN ?
         BE    NOMSG
         LM    PC1,PC2,PRCCWSP
         ICM   PC1,8,=X'13'       SPACE 2
         BAL   PL,PPPUT
         LA    R15,7                LOOPZAEHLER
         LA    R7,PRTMSG@+1        START TEXT
PRTLOOP  MVC   BUFSTART(132),0(R7)
         ST    R15,PMESSAGE+12      SICHERN LOOPZAHLER
         LM    PC1,PC2,PRCCWID    PRINT CCW
         ALR   PC1,JCT
         BAL   PL,PPPUT           ADD CCW TO CHAIN
         LM    PC1,PC2,PCCWNOP    NOP CCW
         BAL   PL,PPPUT           ADD CCW TO CHAIN
         BAL   PL,PPWRITE         WRITE
         BAL   PL,PPCHECK         CHECK WRITE
         ST    PW,IOBSTART        RESET SIO COMP CODE
         LA    R7,PRTMSG@+925      ENDADRESSE TEXT
         L     R15,PMESSAGE+12      LADEN LOOPZAEHLER
         LR    R4,R15
         BCTR  R4,0
         MH    R4,=H'132'          RELATIVER TEXTSTART
         SR    R7,R4               ADRESSE NAECHSTE TEXTZEILE
         BCT   R15,PRTLOOP
         LM    PC1,PC2,PRCCWSP
         ICM   PC1,8,=X'13'       SPACE 2
         BAL   PL,PPPUT
         B     PRTDATE
NOMSG    DS    0H
         LM    PC1,PC2,PRCCWSP              LOAD SPACE CCW
         BAL   PL,PPPUT                     ADD CCW TO CHAIN
         BAL   PL,PPPUT
         BAL   PL,PPPUT
         ICM   PC1,8,=X'13'       SPACE 2
         BAL   PL,PPPUT
         SPACE 2
PRTDATE  DS    0H
         CLI   PMESSAGE+39,C'E'        SEPARATOR PAGE ID TYPE ?
         BE    ENDSEP
         SPACE 2
         MVC   PMESSAGE(9),PMESSAGE+30 DATUM
         BAL   PL,BLKPRT
         LM    PC1,PC2,PRCCWSP
         BAL   PL,PPPUT
         SPACE 2
         MVC   BUFSTART(9),=X'4021204B20204B2020'
         ED    BUFSTART(9),PMESSAGE+16 UHRZEIT
         MVC   PMESSAGE(8),BUFSTART+1
SPACE    EQU   *
         BAL   PL,BLKPRT
         LM    PC1,PC2,PRCCWSP    LOAD SPACE CCW
         ICM   PC1,8,=X'13'       SPACE 2
         BAL   PL,PPPUT           ADD CCW TO CHAIN
         B     PAGEEND
         SPACE 2
ENDSEP   EQU   *                   ENDSEPARATOR DRUCKEN
         MVC   PMESSAGE(9),=C'JOB ENDE '
         BAL   PL,BLKPRT
         LM    PC1,PC2,PRCCWSP
         ICM   PC1,8,=X'13'            SPACE 2
         BAL   PL,PPPUT
         MVI   BUFSTART,C' '
         MVC   BUFSTART+1(131),BUFSTART
         TIME  DEC
         ST    R0,PCCWORK
         MVC   BUFSTART+15(9),=X'4021204B20204B2020'
         ED    BUFSTART+15(9),PCCWORK
         MVC   BUFSTART(15),=C'DRUCKERENDZEIT:'
         LM    PC1,PC2,PRCCWID
         ALR   PC1,JCT
         BAL   PL,PPPUT
         LM    PC1,PC2,PCCWNOP
         BAL   PL,PPPUT
         BAL   PL,PPWRITE
         BAL   PL,PPCHECK
         ST    PW,IOBSTART
         XC    PMESSAGE(9),PMESSAGE    LOESCHEN
         B     SPACE
         SPACE 2
PAGEEND  EQU   *             2 STERNZEILEN AM BLATTENDE/ANFANG
         MVI   BUFSTART,C'*'
         MVC   BUFSTART+1(131),BUFSTART
         LM    PC1,PC2,PRCCWID
         ALR   PC1,JCT
         BAL   PL,PPPUT
         LM    PC1,PC2,PCCWNOP
         BAL   PL,PPPUT
         BAL   PL,PPWRITE
         BAL   PL,PPCHECK
         ST    PW,IOBSTART
         LM    PC1,PC2,PRCCWID
         ICM   PC1,8,=X'01'       WRITE, NO SPACE
         ALR   PC1,JCT
         BAL   PL,PPPUT
         LM    PC1,PC2,PCCWNOP
         BAL   PL,PPPUT
         BAL   PL,PPWRITE
         BAL   PL,PPCHECK
         ST    PW,IOBSTART
         TM    PCEID,PCELCLID     TEST FOR LOCAL PRINT
         BZ    DELBLK             BRANCH IF REMOTE
         CLI   PMESSAGE+9,X'01'    1. SEPARATORSEITE GEDRUCKT ?
         BNE   DELBLK
         MVI   PMESSAGE+9,X'00'
         B     STARTSEP
         SPACE 2
DELBLK   $DELETE
         SPACE 2
         LM    PC1,PC2,PRCCWEJ
         BAL   PL,PPPUT
         LM    PC1,PC2,PCCWNOP
         BAL   PL,PPPUT
         BAL   PL,PPWRITE
         BAL   PL,PPCHECK
         L     PL,PCESAVEA         RESTORE LINK REGISTER
         BR    PL                   AND EXIT
*
         TITLE 'HASP PRINT/PUNCH SERVICE -- BLOCK LETTER ROUTINE'
BLKPRT   DS    0H
         ST    PL,PMESSAGE+12      SAVE RETURN REGISTER
         MVC   PMESSAGE+20(9),PMESSAGE ORIGINAL LETTERS
         OC    PMESSAGE(9),=9X'C0' SHIFT ALL TO 4TH QUADRANT
         TR    PMESSAGE(9),OACEPROG-BUFDSECT+BLOCKTR-BLOCKA-192(BASE3)
         SR    R4,R4               LINE 0 OF 12
BLKBLD   DS    0H
         STH   R4,PMESSAGE+10      SAVE FOR LATER
         MVI   BUFSTART,C' '       INITIAL BLANK
         MVC   BUFSTART+1(131),BUFSTART BLANK BALANCE OF LINE
         SR    R4,R4               SET FOR LETER INDEX OF 0
         LA    R5,BUFSTART         1ST BLOCK LETTER IN PP11
BLKLUP   DS    0H
         LA    R7,PMESSAGE(R4)     CURRENT LETTER INDEX
         LA    R9,PMESSAGE+20(R4)
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,1,0(R7)         GET TRANSLATED LETTER INDEX
         BZ    BLKSTOR             BRANCH IF INDEX ZERO
         BCTR  R15,0               DECREMENT BY ONE
         MH    R15,=H'24'          CONVERT TO DISPLACEMENT
         AH    R15,PMESSAGE+10     SELECT FOR LINE WITHIN LETTER
         LA    R15,OACEPROG-BUFDSECT(R15,BASE3) LETTER MASK ADDR
         ICM   R15,12,0(R15)       LETTER MASK BITS
BLKSTOR  DS    0H
         LA    R7,12               BLOCK WIDTH OF 12
BLKLOOP  DS    0H
         ALR   R15,R15             SHIFT LEFT AND TEST HI BIT
         BC    12,*+10             BRANCH IF OFF
         MVC   0(1,R5),0(R9)       OVERSTORE BLANK WITH LETTER
         LA    R5,1(,R5)           INCREMENT COL NUMBER
         BCT   R7,BLKLOOP          BRANCH TO FILL 12 COL'S
         LA    R5,2(,R5)           2 BLANKS BETWEEN BLOCKS
         LA    R4,1(,R4)           STEP TO NEXT LETTER INDEX
         CH    R4,=H'9'            HAVE WE DONE 9 BLOCKS
         BL    BLKLUP              BRANCH IF NO
         LM    PC1,PC2,PRCCWID     GET PRINT CCW
         ALR   PC1,JCT             RELOCATE FOR THIS JCT
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         LM    PC1,PC2,PCCWNOP     GET NOP CCW
         BAL   PL,PPPUT            ADD NOP TO CHAIN
         BAL   PL,PPWRITE          FORCE WRITE
         BAL   PL,PPCHECK          CHECK WRITE
         ST    PW,IOBSTART         RESET SIO COMP CODE
         LH    R4,PMESSAGE+10      GET LINE COUNTER
         LA    R4,2(,R4)           STEP TO NEXT LINE
         CH    R4,=H'24'           LAST LINE FINISHED
         BL    BLKBLD              BRANCH IF NO
         L     PL,PMESSAGE+12      LOAD RETURN REGISTER
         XC    PMESSAGE(9),PMESSAGE CLEAR MESSAGE AREA
         BR    PL                  RETURN TO CALLER
         SPACE 3
         ENTRY PRTMSG@
PRTMSG@  DS    0C
         DC    7CL132' '
***********************************************************************
