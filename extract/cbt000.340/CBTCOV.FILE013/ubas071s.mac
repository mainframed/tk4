EXPDT    START
         REG
         XSAVE R12,SVA,EXPDT
         L     R2,0(R1)
         LH    R3,0(R2)
         LTR   R3,R3
         BZ    NOPARM
         CLI   7(R2),C','
         BNE   NOPARM
         CLC   2(5,R2),YEAR
         BE    NODATE
         PACK  DBLWD(9),2(3,R2)
         MVO   DB1,DBLWD
         OI    DB1+7,X'0F'
         CVB   R4,DB1
         ST    R4,DBLWD
         UNPK  YEAR(3),DBLWD(5)
         PACK  DBLWD(9),4(4,R2)
         MVO   DB1,DBLWD
         OI    DB1+7,X'0F'
         CVB   R4,DB1
         ST    R4,DBLWD
         UNPK  DAY(5),DBLWD(5)
         MVI   DAY+4,C' '
         TR    YEAR(6),CCTAB-240
NODATE   EQU   *
         SH    R3,=H'7'
         EX    R3,MVCEX1
         RDJFCB (SYSLIB)
         MVC   VOLSER,JFCB+118
         OBTAIN GETDSCB
         B     *+4(R15)
         B     OBTOK
         DC    XL4'00'
         B     NOTFND
         B     IOERR
OBTOK    EQU   *
         UNPK  CCHHR(11),WORKAREA+96(6)
         MVI   CCHHR+10,C' '
         TR    CCHHR,CCTAB-240
         OPEN  (SYSIN,(OUTPUT))
         PUT   SYSIN,CARD1
         PUT   SYSIN,CARD2
         CLOSE (SYSIN)
         LINK  EP=EXPDTZAP
RETURN   EQU   *
         XRETURN 0
NOPARM   EQU   *
         MVC   LINE,MESS1
         B     PUTERR
NOTFND   EQU   *
         MVC   LINE,MESS2
         B     PUTERR
IOERR    EQU   *
         MVC   LINE,MESS3
PUTERR   EQU   *
         OPEN  (SYSPRINT,(OUTPUT))
         PUT   SYSPRINT,LINE
         CLOSE (SYSPRINT)
         B     RETURN
CARD1    DS    0CL80
         DC    C'CCHHR '
CCHHR    DC    CL10' '
         DC    CL64' '
CARD2    DS    0CL80
         DC    C'REP 38 '
YEAR     DC    CL2'00'
DAY      DC    CL4'0000'
         DC    CL67' '
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=80,RECFM=FB,    *
               BLKSIZE=80
LINE     DC    CL80' '
DBLWD    DS    D
         DS    X
DB1      DC    D'0'
DSNAME   DC    CL44' '
MVCEX1   MVC   DSNAME(0),8(R2)
SYSLIB   DCB   DSORG=PS,MACRF=GM,DDNAME=SYSLIB,EXLST=DCBEXLST
DCBEXLST DS    0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DC    176XL1'00'
VOLSER   DC    CL6' '
GETDSCB  CAMLST SEARCH,DSNAME,VOLSER,WORKAREA
WORKAREA DS    0D
         DS    148C
CCTAB    DC    C'0123456789ABCDEF'
SYSIN    DCB   DSORG=PS,MACRF=PM,DDNAME=SYSIN
MESS1    DC    CL80'INVALID OR MISSING PARM-INFORMATION'
MESS2    DC    CL80'THE DSCB WAS NOT FOUND IN THE VTOC OF THE SPECIFIED*
                VOLUME'
MESS3    DC    CL80'A PERMANENT I/O-ERROR WAS FOUND WHEN PROCESSING THE*
                SPECIFIED VOLUME'
         END
