./       ADD   LIST=ALL,NAME=ORWG03AS
LUXCONV  START
         REG
         XSAVE R12,,LUXCONV,WORKL
         USING WORK,R13
         USING PARMAREA,R11
         LR    R11,R1
         XC    DBLWD,DBLWD
         XC    SWITCH,SWITCH
         TRT   TEXT+5(11),TRTTAB
         BZ    ERROR
         LA    R10,TEXT+5
         CR    R10,R1
         BE    ERROR
         LR    R9,R1
         SR    R9,R10
         BCTR  R9,0
         MVC   WTO,MESS3
         LA    R8,WTO+12
         EX    R9,MVCEX1
         LA    R8,2(R9,R8)
         LA    R7,1(R9)
         CLI   1(R1),C'A'
         BNE   HEXCONV
         C     R7,=F'6'
         BH    INVNUM
         MVC   0(9,R8),=C'(ADDR)  ='
         LA    R8,11(,R8)
KADDR    EQU   *
         EX    R9,TREX1
         EX    R9,TRTEX1
         BNZ   INVNUM
         EX    R7,PACKEX1
         L     R6,DBLWD
         LTR   R6,R6
         BNZ   INVNUM
         L     R6,DBLWD+4
         SRL   R6,12
         SLL   R6,2
         CVD   R6,DBLWD
         UNPK  0(5,R8),DBLWD
         OI    4(R8),X'F0'
         MVI   5(R8),C'-'
         LA    R6,4(,R6)
         CVD   R6,DBLWD
         UNPK  6(5,R8),DBLWD
         OI    10(R8),X'F0'
         MVC   12(9,R8),=C'K STORAGE'
         B     WTOROUT
HEXCONV  EQU   *
         CLI   1(R1),C'X'
         BNE   DECCONV
         MVC   0(8,R8),=C'(HEX)  ='
         LA    R8,10(R8)
KHEX     EQU   *
         EX    R9,TREX1
         EX    R9,TRTEX1
         BNZ   INVNUM
         EX    R7,PACKEX1
         L     R6,DBLWD
         LTR   R6,R6
         BNZ   INVNUM
         L     R6,DBLWD+4
         CVD   R6,DBLWD
         UNPK  0(10,R8),DBLWD
         OI    9(R8),X'F0'
         MVC   11(5,R8),=C'(DEC)'
         B     WTOROUT
DECCONV  EQU   *
         CLI   1(R1),C'D'
         BNE   KCONV
         MVC   0(8,R8),=C'(DEC)  ='
         LA    R8,10(R8)
KDEC     EQU   *
         EX    R9,TREX2
         EX    R9,TRTEX2
         BNZ   INVNUM
         EX    R7,PACKEX1
         MVO   ZWI,DBLWD
         NI    ZWI+7,X'F0'
         OI    ZWI+7,X'0C'
         CP    ZWI,PMAX
         BH    INVNUM
         CVB   R6,ZWI
         TM    SWITCH,X'80'
         BNO   DECOK
         SLL   R6,10
DECOK    EQU   *
         ST    R6,ZWI
         UNPK  0(9,R8),ZWI(5)
         MVC   8(6,R8),=C' (HEX)'
         TR    0(8,R8),CCTAB-240
         TM    SWITCH,X'80'
         BO    CONT1K
         B     WTOROUT
KCONV    EQU   *
         CLI   1(R1),C'K'
         BNE   ERROR
         C     R7,=F'5'
         BH    INVNUM
         MVC   0(4,R8),=C'K  ='
         LA    R8,6(R8)
         OI    SWITCH,X'80'
         B     KDEC
CONT1K   EQU   *
         MVC   ZWI2,0(R8)
         LA    R10,ZWI2
         MVI   16(R8),C'='
         LA    R8,19(R8)
         LA    R9,7
         LA    R7,8
         B     KHEX
RETURN   EQU   *
         XRETURN 0,R
ERROR    EQU   *
         MVC   WTO,MESS2
WTOROUT  EQU   *
         L     R0,REG4
         WTO   MF=(E,WTO)
         B     RETURN
INVNUM   EQU   *
         MVC   WTO,MESS1
         B     WTOROUT
MVCEX1   MVC   0(0,R8),0(R10)
TREX1    TR    0(0,R10),TRTAB
ZEROES   DC    16XL1'00'
TRTAB    DS    0CL256
         DC    193XL1'FF'
         DC    XL6'0A0B0C0D0E0F'
         DC    41XL1'FF'
         DC    X'00010203040506070809'
         DC    6XL1'FF'
TRTEX1   TRT   0(0,R10),ZEROES
PACKEX1  PACK  DBLWD(9),0(0,R10)
TREX2    TR    0(0,R10),TRTAB1
ZERO     DC    10XL1'00'
TRTAB1   DS    0CL256
         DC    240XL1'FF'
         DC    X'00010203040506070809'
         DC    6XL1'FF'
TRTEX2   TRT   0(0,R10),ZERO
PMAX     DC    XL8'000002147483647C'
CCTAB    DC    CL16'0123456789ABCDEF'
TRTTAB   DC    256XL1'00'
         ORG   TRTTAB+107
         DC    C','
         ORG
MESS1    WTO   'XCONV01 INVALID NUMERICS',MCSFLAG=REG0,MF=L
MESS2    WTO   'XCONV02 ERROR IN PARAMETER',MCSFLAG=REG0,MF=L
MESS3    WTO   'XCONV03                                                *
                       ',                                              *
               MCSFLAG=REG0,MF=L
         XPARM
WORK     DSECT
SVA      DS    18F
DBLWD    DS    D
ZWI      DS    D
ZWI2     DS    CL8
WTO      DS    CL80
SWITCH   DS    X
WORKL    EQU   *-WORK
         END
./       ADD   LIST=ALL,NAME=ORWG03BM
         MACRO
&SYM     XSAVE &BASE,&AREA,&ID,&LEN
.*
.*                 EXTENDED SAVE MACRO
.*                 ASSUMES ENTRYPOINT ADDRESS IN REGISTER 15
.*                 SAVES REGISTERS 14 THROUGH 12
.*                 LOADS BASE REGISTER AND CHAINES SAVEAREAS
.*                 MAY PRODUCE REENTRANT CODE
.*
         LCLA  &OFFSET,&BASEOFF,&LLID,&DISPL
         LCLA  &NREG,&REGIX
         LCLC  &NM,&REG,&BL
         LCLC  &PREV
&OFFSET  SETA  4
&NM      SETC  '&SYM'
&REG     SETC  '15'
&BL      SETC  ''
         AIF   (T'&ID EQ 'O').NOID
&LLID    SETA  K'&ID
         AIF   (&LLID NE &LLID/2*2).OVBL
&BL      SETC  ' '
&LLID    SETA  &LLID+1   BLANK
.OVBL    ANOP
&LLID    SETA  &LLID+8   DATE
&DISPL   SETA  4+1+&LLID   B,X,(DATE,ID)
&NM      B     &DISPL.(0,15)
         DC    AL1(&LLID)
         DC    C'&ID&BL'
         DC    CL8'&SYSDATE'
&OFFSET  SETA  4+4+1+&LLID   B,STM,X,(DATE,ID)
&NM      SETC  ''
.NOID    ANOP
&NM      STM   14,12,12(13)            SAVE REGS
         AIF   (T'&BASE EQ 'O').NOBASE
         AIF   ('&BASE'(1,1) NE '(').LOAD
&REGIX   SETA  1
&NREG    SETA  N'&BASE
&REG     SETC  '&BASE(&REGIX)'
         LR    &REG,15
&OFFSET  SETA  &OFFSET+2
.LOOP    ANOP
         USING *-&OFFSET+&BASEOFF,&REG
&REGIX   SETA  &REGIX+1
         AIF   ('&REGIX' GT '&NREG').SETUSE
&PREV    SETC  '&REG'
&REG     SETC  '&BASE(&REGIX)'
         LA    &REG,X'FFF'
         LA    &REG,1(&REG,&PREV)
&OFFSET  SETA  &OFFSET+8
&BASEOFF SETA  &BASEOFF+4096
         AGO   .LOOP
.LOAD    LR    &BASE,15                LOAD BASE REG
&OFFSET  SETA  &OFFSET+2
&REG     SETC  '&BASE'
.NOBASE  ANOP
         USING *-&OFFSET,&REG
.SETUSE  ANOP
         AIF   (T'&AREA NE 'O').NORENT
         AIF   (T'&LEN NE 'O').NOTZERO
         LA    0,72                    GET LENGTH
         AGO   .COMP
.NOTZERO ANOP
         LA    0,&LEN                  GET LENGTH
.COMP    ANOP
         LA    1,1                     PREPARE FOR SP 1
         SLL   1,24                    SP 1 IN HIGH ORDER BYTE
         OR    0,1                     INSERT SP 1
         GETMAIN   R,LV=(0)            GET CORE
         XC    0(3*4,1),0(1)           ZERO CHAIN ADDRESSES
         AIF   (T'&LEN NE 'O').GET
         LA    0,72                    GET LENGTH
         AGO   .NOSTH
.GET     ANOP
         LA    0,&LEN                  GET LENGTH
.NOSTH   ANOP
         STH   0,2(1)                  STORE LENGTH
         AGO   .COM
.NORENT  LA    1,&AREA                 GET SA ADDR
.COM     ST    1,8(0,13)               SA DOWN CHAIN
         ST    13,4(0,1)               SA UP CHAIN
         LR    13,1                    LOAD SA REG
         L     1,4(0,13)               GET HIGH SA
         LM    0,1,20(1)               RESTORE REGS 0 AND 1
         AIF   (T'&AREA NE 'O').TEST
         MEXIT
.TEST    AIF   (T'&AREA EQ 'U').DEFINE
         MEXIT
.DEFINE  B     &AREA+72                BRANCH AROUND SAVEAREA
&AREA    DC    18F'0'                  SAVEAREA
         MEND
./       ADD   LIST=ALL,NAME=ORWG03CM
         MACRO
&SYM     XRETURN &RC,&MODE
.*
.*                 EXTENDED RETURN MACRO
.*                 RESTORES REGISTERS
.*                 MAY PROVIDE RETURNCODE
.*
         LCLA  &C
         LCLB  &NC,&CF
         LCLC  &R,&NM
         AIF   (T'&MODE EQ 'O' OR '&MODE' EQ 'R').CONT
         MNOTE 8,'UNKNOWN OPERAND'
         MEXIT
.CONT    ANOP
&NM      SETC  '&SYM'
         AIF   (T'&RC NE 'O').CODE
&NC      SETB  (1)
         AGO   .NOCODE
.CODE    ANOP
&R       SETC  '15'
         AIF   ('&RC'(1,1) EQ '(').REG
         AIF   (T'&RC EQ 'N').FIX
&NM      L     15,&RC                  LOAD CODE
&NM      SETC  ''
         AGO   .NOCODE
.FIX     ANOP
&CF      SETB  (1)
         AGO   .NOCODE
.REG     ANOP
&R       SETC  '&RC'(2,K'&RC-2)
.NOCODE  ANOP
&NM      L     13,4(0,13)              GET HIGH SA ADDR
         AIF   (&NC OR &CF).NOSTORE
         ST    &R,16(0,13)             STORE CODE
.NOSTORE AIF   (T'&MODE EQ 'O').NORENT
         L     1,8(0,13)               GET LOW SA ADDR
         LH    0,2(1)                  GET LENGTH
         LA    15,1                    PREPARE FOR SP 1
         SLL   15,24                   SP IN HIGH ORDER BYTE
         OR    0,15                    INSERT SP 1
         FREEMAIN  R,LV=(0),A=(1)      FREE CORE
.NORENT  LM    14,12,12(13)            RESTORE REGS
         AIF   (NOT &CF).EXIT
         LA    15,&RC                  LOAD CODE
&C       SETA  &RC
         AIF   (&C/4*4 EQ &C).EXIT
         MNOTE 0,'UNUSUAL CODE'
.EXIT    MVI   12(13),X'FF'            FORTRAN REQUIREMENT
         SPM   14                      RESTORE PGM MASK AND CC
         BR    14
         MEND
./       ADD   LIST=ALL,NAME=ORWG03DM
         MACRO
         XPARM
PARMAREA DSECT
REG4     DS    F
TEXT     DS    CL124
PRMLEN   EQU   *-PARMAREA
         MEND
         MACRO
&NAME    MBASIS &RA,&RE,&RC=,&TYPE=N,&MAXMUP=
         GBLB  &MTRB000            MUPTRACE ACTIVE
         GBLB  &MTRB004(2)         MUPTRACE MBASIS
         GBLA  &MBSA001            MBASIS COUNT
         GBLA  &MBSA002            MBASIS MAXMUP
         GBLB  &MBSB001            MBASIS PRESENT
         GBLB  &MBSB002            MBASIS RC=(R15)
         GBLB  &MBSB003            MBASIS TYPE=R
         GBLC  &MBSC001(30)        MBASIS &SYSECT
         GBLC  &MBSC002(30)        MBASIS &SYSNDX
         LCLA  &M
         LCLC  &T
&MBSA001 SETA  &MBSA001+1
         AIF   (&MBSA001 GT 30).E1
         AIF   (N'&SYSLIST LE 2).L0
         MNOTE 4,'MORE THAN TWO POSITIONAL OPERANDS. ONLY FIRST TWO USE*
               D'
.L0      ANOP
         AIF   ('&TYPE' EQ 'N').M3
         AIF   (T'&MAXMUP NE 'O').M0
         MNOTE 4,'MAXMUP MISSING. 20 ASSUMED'
&MBSA002 SETA  20
         AGO   .M3
.M0      AIF   (T'&MAXMUP EQ 'N').M2
.M1      MNOTE 8,'MAXMUP INVALID. 20 ASSUMED'
&MBSA002 SETA  20
         AGO   .M3
.M2      ANOP
&M       SETA  &MAXMUP
         AIF   (&M LE 0 OR &M GT 500).M1
&MBSA002 SETA  &MAXMUP
.M3      ANOP
         AIF   (&MBSB003).L4
         AIF   ('&TYPE' EQ 'R').L2
&T       SETC  '&TYPE'
         AIF   ('&TYPE' EQ 'N' OR '&TYPE' EQ 'S' OR '&TYPE' EQ 'T').L1
         MNOTE 8,'TYPE INVALID. N ASSUMED'
&T       SETC  'N'
.L1      ANOP
&NAME    MBASISN &RA,&RE,RC=&RC,TYPE=&T
         MEXIT
.L2      ANOP
         AIF   (NOT &MBSB001).L3
         MNOTE 8,'PRIOR MBASIS WAS NOT TYPE=R'
.L3      ANOP
&MBSB002 SETB  1
&MBSB003 SETB  1
         AIF   (NOT &MTRB000).L4
         MNOTE 8,'*** TRACE WURDE SPEZIFIZIERT FUER REENTRANT CODE ***'
         MNOTE 8,'*** TRACE-ROUTINE IST NICHT REENTRANT ***'
.L4      ANOP
&NAME    MBASISR &RA,&RE,RC=(R15),TYPE=R
         MEXIT
.E1      MNOTE 12,'MORE THAN 30 MBASIS. MACRO GENERATION TERMINATED'
         MEND
./       ADD   LIST=ALL,NAME=ORWG03EM
         MACRO
         REG
*
*        REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
RA       EQU   R10
R11      EQU   11
RB       EQU   R11
R12      EQU   12
RC       EQU   R12
R13      EQU   13
RD       EQU   R13
R14      EQU   14
RE       EQU   R14
R15      EQU   15
RF       EQU   R15
         SPACE 2
         MEND
./       ADD   LIST=ALL,NAME=ORWG03FA
%%HISTORY.
AUTHOR(S).    N.N.
INSTALLATION. BASF AG
              6700 LUDWIGSHAFEN
              RECHENZENTRUM.
DATE-WRITTEN. 00/00/00.
LAST UPDATE.  07/16/74.
REMARKS.
    AUTHORISATION CODE MUST BE 1.
    MODULE CONVERTED TO OS/VS2 (MVS) BY
    BAYER AG, 5090 LEVERKUSEN, FR-RECHENZENTRUM.
%%SYNTAX.
F X,CONV,<VALUE>,<TYPE>
....<VALUE>:  DECIMAL OR HEXADECIMAL VALUE
              TO BE CONVERTED
....<TYPE>:   X×D×K
              X - ENTERED VALUE IS HEXADECIMAL
              D - ENTERED VALUE IS DECIMAL
              K - ENTERED VALUE IS DECIMAL,
                  1K = 1024
              A - ENTERED VALUE IS HEX.-ADDRESS
                  ERGEBNIS IST WERT IN 4K-BEREICHEN
%%FUNCTION.
THE COMMAND 'CONV' CONVERTS THE SPECIFIED
VALUE TO DECIMAL OR HEXADECIMAL RESPECTIVELY.
%
/*
