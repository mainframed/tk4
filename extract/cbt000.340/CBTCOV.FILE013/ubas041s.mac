COMPRS   CSECT
         REG
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING *-6,R12
         L     R7,0(0,R1)               ADRESSE DES E-BEREICHES NACH R7
         LA    R15,4                    RETURNCODE 4 WIRD GESETZT
         CLC   H257,0(R7)
         BL    BLK5
         CLC   H5,0(R7)
         BH    BLK5
         SR    R8,R8                    R8 WIRD AUF 0 GESETZT
         IC    R8,0(R7)                 ERSTER TEIL DER LAENGE WIRD
*                                       UEBERTRAGEN
         SLL   R8,8                     R8 WIRD UM 8 BITS NACH LINKS
*                                       VERSCHOBEN
         IC    R8,1(R7)                 UEBERTRAG DES 2.TEN LAENGEN-
*                                       TEILS
         LA    R9,4                     R9 WIRD MIT 4 GELADEN
         SR    R8,R9                    LAENGE DER ZEICHENKETTE
         LR    R10,R8                   R10 WIRD MIT ZEI.KET.L. GELADEN
         LA    R10,2(R10)               R10 WIRD UM 2 ERHOEHT
         LA    R9,4(R8,R7)              ENDADRESSE DER ZEICHENKETTE
         LA    R7,4(R7)                 ANFANGSADRESSE DER ZEICHENKETTE
         MVI   TABELLE+64,X'80'
         SR    R6,R6                    R6 WIRD AUF 0 GESETZT
         L     R4,4(R1)                 GESAMTANFANGSADRESSE DES
*                                       AUSGANGS
         LA    R3,5(R4)                 ANFANGSADREESE DES AUSGANGS
         LA    R15,0                    RETURNCODE WIRD AUF 0 GESTZT
BLK1     EQU   *
         BCTR  R8,0                     R8 WIRD UM 1 VERMINDERT
BLK10    EQU   *
         MVC   TABELLE(1),TABELLE+64    TABELLE
         MVC   TABELLE+1(255),TABELLE   WIRD
         XI    TABELLE+64,X'80'         INVERTIERT
         LR    R1,R9                    R1 WIRD MIT ENDADRESSE GELADEN
         EX    R8,TRANSLT               DIE ZWEI.KET.(R7) WIRD MIT TAB
*                                       GETESTET
         SR    R1,R7                    ARGUMENTADR. WIRD VON ENDADR.
*                                       SUBTRAHIERT
         BZ    BLK10
         LA    R0,0(R1,R6)              R0 WIRD MIT MOMENTANER GESAMTL.
*                                       GELADEN
         CR    R0,R10
         BNL   BLK7
BLK8     EQU   *
         CH    R1,=H'127'               UEBERTRAGSL. WIRD GEPRUEFT
         BNH   BLK2
         LA    R6,1(R6)                 ZAEHLERREG. WIRD UM 1 ERHOEHT
         MVC   0(1,R3),TABELLE+64       ERKENNUGSMASKE WIRD GESTZT
         LA    R3,1(R3)                 AUSGABEADR. WIRD UM 1 ERHOEHT
         STC   R1,0(R3)                 UEBERTRAG DER UEBERTRAGSL.
         B     BLK3
BLK2     EQU   *
         STC   R1,0(R3)                 UEBERTRAG DER UEBERTRAGSL.
         OC    0(1,R3),TABELLE+64       SETZTEN DER ERKENNUNGSMASKE
BLK3     EQU   *
         LA    R6,1(R6)                 ZAEHLERREG. WIRD UM 1 ERHOEHT
         LA    R3,1(R3)                 AUSGABEADR. WIRD UM 1 ERHOEHT
         BCTR  R1,0                     R1 WIRD UM 1 VERMINDERT
         TM    TABELLE+64,X'80'
         BZ    BLK4
         EX    R1,MOVE                  UEBERTRAG VON ZEICHEN (R7--R3)
         LA    R3,1(R1,R3)              AUSGABEADR. WIRD UM UEBER-
*                                       TRAGSLAENGE ERHOEHT
         LA    R6,1(R1,R6)              ZAEHLERREG. WIRD UM UEBER-
*                                       TRAGSLAENGE ERHOEHT
BLK4     EQU   *
         LA    R7,1(R1,R7)              ANFANGSADR. DER ZEI.KET. WIRD
*                                       ERHOEHT
         SR    R8,R1                    KONTROLLE AUF ENDE
         BP    BLK1
         BM    ABEND
         XC    0(5,R4),0(R4)            LAENGENFELDER WERDEN VORBE-
*                                       REITET
         STC   R6,4(R4)                 UEBERTRAG DER GESAMTL. NACH
*                                       BYTE 5
         LA    R6,5(R6)                 GESAMTL. WIRD UM 5 ERHOEHT
         STC   R6,1(R4)                 1.TER TEIL DER GESAMTL. WIRD
*                                       UEBERTRAGEN
         SRL   R6,8                     R6 WIRD UM 8 BITS NACH RECHTS
*                                       VERSCHOBEN
         STC   R6,0(R4)                 2.TER TEIL DER GESAMTL. WIRD
*                                       UEBERTRAGEN
BLK5     EQU   *
         LM    R0,R12,20(R13)
         BR    R14
BLK7     EQU   *
         TM    TABELLE+64,X'80'
         BZ    BLK8
         MVI   TABELLE+64,X'80'         TABELLE+64 WIRD GESETZT
         SR    R6,R6                    R6 WIRD AUF 0 GESETZT
         LA    R3,5(R4)                 R3 WIRD AUF AUSGABEADR. GESETZT
         LA    R8,3                     R8 WIRD MIT 3 GELADEN
         LNR   R8,R8                    R8 WIRD NEGATIV
         AR    R8,R10                   R8 ERHAELT ZEI.KET.L.-1
         LA    R1,1(R8)                 R1 WIRD MIT ZEI.KET.L. GELADEN
         LR    R7,R10                   R7 WIRD MIT ZEI.KET.L.+2
*                                       GELADEN
         LNR   R7,R7                    R7 WIRD NEGATIV
         LA    R7,2(R7)                 R7 WIRD UM 2 ERHOEHT
         AR    R7,R9                    R7 ERHAELT ZEI.KET.ANFANGSADR.
         B     BLK8
ABEND    EQU   *
         B     BLK5
TABELLE  DS    CL256
TRANSLT  TRT   0(0,R7),TABELLE
MOVE     MVC   0(0,R3),0(R7)
H257     DC    H'257'
H5       DC    H'5'
         END
