* STAND 05/20/81
***********************************************************************
*                                                                     *
*    PROGRAMM  = PRUEFUPD                                             *
*                                                                     *
*    AUFRUF    = CALL PRUEFUPD                                        *
*                                                                     *
*    ZWECK     = PRUEFUNG, WELCHE SUMMEN IN ABSTIMMSUMMENDATEI        *
*                EINGEFUEGT WERDEN SOLLEN  (UPDATE)                   *
*                                                                     *
*                1. FEHLT GUELTIGE ABSTIMMLISTEN-NR., WIRD UPDATE     *
*                   GENERELL VERBOTEN (AUPDTE=SUNO)                   *
*                                                                     *
*                2. DAS PROGRAMM AKALESEN (LESEN DER ASTIMMPARAMETER= *
*                   KARTEN) ERLAUBT UPDATE FUER JEDE SUMME (SUPDTE=   *
*                   SUYES).                                           *
*                   WIRD IN EINER KOMMENTARKARTE ZWISCHEN SP.11-30    *
*                   DER TEXT 'AUSGABE ' GEFUNDEN (BLANKS BIS AUF TRAI=*
*                   LING BLANKS WERDEN FUER DIE ABFRAGE ELIMINIERT),  *
*                   WIRD FUER ALLE DAVOR AUFGEFUEHRTEN SUMMEN         *
*                   UPDATE VERBOTEN (SUPDTE=SUNO).                    *
*                                                                     *
*                3. HANDELT ES SICH UM EINEN NICHT-ROUTINELAUF (JOB=  *
*                   NAME UNGLEICH ROUTINEJOBNAME), WIRD UPDATE ERLAUBT*
*                   FUER IN 'JOBTAB' DEFINIERTE JOBNAMEN
*                                                                     *
*                 4.UPDATE IST FUER ROUTINEJOBS ERLAUBT
*
*                   ROUTINEJOBNAME = LLNNNSN                          *
*                        L  ALPHA    LL NICHT CD ODER CK              *
*                        N   NUMERISCH                                *
*                        S   NUMERISCH ODER #                         *
*                  ZUSAETZLICH WIRD 5.PARAMETER IN ACCT INFO GEPRUEFT
*                   = RRR  ROUTINE UPDATE ERLAUBT
*                   = WWW  WIEDERHOLUNG UPDATE ERLAUBT
*                   = SSS  SONDERLAUF UPDATE NICHT ERLAUBT
*
*
*              SETZT  ASWITCH = ALOGID   D.H.MASCH=ID AN DSNAME
*                                        VON ASTLOG ANHAENGEN
*                             = ALOGIDNO D.H. MASCH-ID NICHT
*                                        ANHAENGEN FUER TESTJOB
*                                        Z325044N
*
***********************************************************************
PRUEFUPD CSECT
         AREG
RA       EQU   R11
RK       EQU   R10
RS       EQU   R9
         SPACE 1
         XSAVE R12,SAVE,PRUEFUPD
         SPACE 1
         USING DATAB,RA
         USING DKTAB,RK
         USING DSTAB,RS
         SPACE 1
*  DYNAMIC ALLOCATION ASTSUM-DATEI
*  (NICHT , WENN TESTJOB)
*
         L     R1,16                   -->CVT
         L     R1,0(,R1)
         L     R1,4(,R1)               -->TCB
         L     R1,12(,R1)              -->TIOT
         MVC   JOB,0(R1)               JOBNAME
         CLC   JOB,LITEST
         BE    UPDA10
         CLC   JOB,LITST1
         BE    UPDA10
         CALL  ASTALLOC
UPDA10   EQU   *
         L     RA,PATAB                 --> ATAB
         MVI   AREPEAT,RNO             KEINE WIEDERHOLUNG
         CLC   AASTC,=CL20' '
         BNE   ACCTEST
         MVI   ACCT,14                  ABSTIMMLISTEN-NR FALSCH
         B     JOBNO                    KEIN UPDATE MOEGLICH
*  5.ARGUMENT AUS JOB ACCOUNTING INFO HOLEN
*  RRR = ROUTINE
*  WWW = WIEDERHOLUNG
*  SSS = SONDERLAUF
*
ACCTEST  EQU   *
         MVI   ACCT,13        WEDER ROUTINE NOCH WIEDERHOLUNG
         XC    ARG,ARG        LOESCHEN
         LA    R3,3
         STH   R3,LN          PARAMETERLAENGE = 3
         CALL  ACCTPARM,(K5,LN,ARG)
RRR      LTR   R15,R15
         BNZ   JOBNO          5.PARAMETER FEHLT
         CLC   ARG,=C'RRR'
         BNE   TW1
         MVI   ACCT,1      ROUTINE
         B     JOBTEST
TW1      EQU   *
         CLC   ARG,=C'WWW'
         BNE   TW2
         MVI   ACCT,2      WIEDERHOLING
         B     JOBTEST
TW2      EQU   *
         CLC   ARG,=C'SSS'
         BNE   JOBNO
         MVI   ACCT,3         SONDERLAUF
         OI    ASWITCH,LCALL  OP023 SOLL AUFGERUFEN WERDEN,
*                             AUCH BEI NICHTROUTINE-JOBNAMEN
         B     JOBNO
*
*  JOBNAME PRUEFEN
JOBTEST  EQU   *
         OI    ASWITCH,LCALL  OP023 SOLL AUFGERUFEN WERDEN,
JOBIN    EQU   *
*              FUER DEFINIERTE JOBS IST UPDATE ERLAUBT
         LA    R4,JANZ        ANZ.DEF.JOBS
         LA    R3,JOBTAB      -->JOB-TABELLE
LOOPJ    EQU   *
         CLC   JOB,0(R3)
         BE    UPDA
         LA    R3,8(R3)       -->NAECHSTER EINTRAG IN JOB-TAB
         BCT   R4,LOOPJ
*
         CLC   JOB(2),AASTC   2 STELLEN JOBNAME U.ALISTNR MUESSEN
*                             GLEICH SEIN (SACHGEBIET)
         BNE   NOROUT         UNGLEICH
         CALL  MODAST,(JOB,ZTXT,K4,ERR)
         CLC   ERR,=F'0'
         BE    UPDA           ROUTINE JOBNAME
*
*
*  UPDATE DER ASTSUM NICHT ERLAUBT
*
NOROUT   EQU   *
         MVI   ACCT,15        JOBNAME FALSCH
JOBNO    EQU   *
         MVI   AUPDTE,SUNO             UPDATE NICHT ERLAUBT
         B     RETURN
UPDA     EQU   *
         CLI   ACCT,2
         BNE   *+8            KEINE WIEDERHOLUNG
         MVI   AREPEAT,RYES            = WIEDERHULING
         L     RK,PKTAB                 --> 1.EINTRAGUNG IN KTAB
         OI    ASWITCH,ALOGID MASCHID AN LOGNAME ANHAENEN
         CLC   JOB,LITEST
         BE    UP500
         CLC   JOB,LITST1
         BNE   LOOP1
UP500    EQU   *
         NI    ASWITCH,ALOGIDNO  KEINE MASCHID ANHAENGEN
         SPACE 2
*  TEXT 'AUSGABE' IN KOMMENTARKARTE SUCHEN
*
LOOP1    EQU   *
         CLC   KNEXT,=F'-1'
         BE    RETURN                   KTAB DURCHSUCHT
         CLI   KNEXT,KCOMMENT
         BNE   LOOP1E                   KEINE KOMMENTARKARTE
         MVC   ZLN,=F'20'
         LA    R3,TKOM+1                --> KOMMENTAR
         MVC   ZTXT,=CL20' '
         CALL  BLANKEX,((R3),ZTXT,ZLN)  BLANKS ELIMINIEREN
         LA    R3,ZTXT
         LA    R2,4
LOOP2    EQU   *         PRUEFEN, OB TEXT 'AUSGABE ' GLEITEND AB
         CLC   0(9,R3),=CL9'AUSGABE'    SP.2-5 IM KOMMENTAR STEHT.
         BE    LOOP1EE                  GEFUNDEN
         LA    R3,1(R3)
         BCT   R2,LOOP2
LOOP1E   EQU   *
         L     RK,KNEXT                 --> NAECHSTE KTAB-EINTRAGUNG
         B     LOOP1
         SPACE 2
LOOP1EE  EQU   *
         LR    R8,RK                    --> KOMMENT.MIT 'AUSG.'MERKEN
*  ALLE SUMMEN VOR TEXT 'AUSGABE' KENNZEICHNEN MIT UPDATE NICHT ERLAUBT
         L     RK,PKTAB
LOOP3    EQU   *
         CR    RK,R8
         BE    RETURN                   BIS 'AUSGABE' ABGEARBEITET
         CLI   KNEXT,KSUM
         BNE   LOOP3E                   KEINE SUMMENKARTE
         L     RS,TSTAB                 -->ZUGEHOERIGE STAB
         MVI   SUPDTE,SUNO              UPDATE NICHT ERLAUBT
LOOP3E   EQU   *
         L     RK,KNEXT                 --> NAECHSTE KTAB
         B     LOOP3
RETURN   EQU   *
         XRETURN
         SPACE 1
*  DEFINITIONEN
*
PATAB    DC    V(ATAB)                  --> ATAB
ZLN      DS    F
ERR      DS    F
LN       DS    H
K5       DC    H'5'
ARG      DS    CL3
ZTXT     DS    CL20
JOB      DS    CL8
K4       DC    CL1'4'
*  JOBS MIT UPDATE-ERLAUBNIS
JANZ     EQU   5
JOBTAB   DS    0CL8
         DC    CL8'H007130Z'  KIHM
         DC    CL8'H007172Z'  KIHM
         DC    CL8'Z325044A'  LINCK
LITEST   DC    CL8'Z325044N'  LINCK MIT TESTDATEIEN
LITST1   DC    CL8'Z325044M'  LINCK MIT TESTDATEIEN
         LTORG
         MATAB
         MKTAB
         MSTAB
         END
         OI    ASWITCH,ALOGID MASCH-ID AN LOGNAME HAENGEN
         CLC   JOB,LITEST
         BNE   LOOP1
         NI    ASWITCH,ALOGIDNO KEINE MASCH-ID ANHAENGEN               K
