EDTSRCTP START
         XSAVE R12,SVA,EDTSRCTP
         REG
         L     R1,0(R1)
         LH    R15,0(R1)
         LTR   R15,R15
         BZ    NOPARM
         CLC   0(2,R1),=H'5'
         BNE   NOPARM
         CLC   2(5,R1),=CL5'NOSEP'
         BNE   NOPARM
         MVI   WEICHE+1,X'F0'
NOPARM   EQU   *
         OPEN  (PRINT,(OUTPUT),AUS,(OUTPUT),CARD)
         PUT   PRINT,LINE
         MVI   LINE,C' '
NEXT     EQU   *
         GET   CARD,INP
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LINE+1(6),=C'MEMBER'
         MVC   LINE+8(8),INP
         OPEN  (EIN)
         LA    R11,NOTFND
GETP     EQU   *
         GET   EIN,REC
         CLC   REC(13),ADDCD
         BNE   GETP
         CLC   REC+13(8),INP
         BNE   GETP
         LA    R11,EOFMBR
WEICHE   NOP   GETMBR
         MVC   ZWI,INP
         MVI   INP,C'*'
         MVC   INP+1(L'INP-1),INP
         MVC   INP+36(8),ZWI
         LA    R3,5
NXTPUT   EQU   *
         PUT   AUS,INP
         BCT   R3,NXTPUT
GETMBR   EQU   *
         PUT   AUS,REC
         GET   EIN,REC
         CLC   REC(13),ADDCD
         BNE   GETMBR
EOFMBR   EQU   *
         MVC   LINE+17(28),=C'HAS BEEN SUCCESSFULLY COPIED'
PUTL     EQU   *
         PUT   PRINT,LINE
         CLOSE (EIN,REREAD)
         FREEPOOL EIN
         B     NEXT
EOFT     BR    R11
NOTFND   EQU   *
         MVC   LINE+17(23),=CL23'NOT FOUND IN INPUT'
         B     PUTL
EOFSYSIN EQU   *
         MVC   LINE,=CL132'0EOF ON SYSIN - END OF JOB'
         PUT   PRINT,LINE
         CLOSE (PRINT,,CARD,,EIN,,AUS)
         XRETURN 0
INP      DS    CL80
REC      DS    CL80
LINE     DC    CL132'1 '
ADDCD    DC    CL13'./ ADD  NAME='
ZWI      DS    CL8
CARD     DCB   DSORG=PS,DDNAME=SYSIN,MACRF=GM,EODAD=EOFSYSIN
PRINT    DCB   DSORG=PS,DDNAME=SYSPRINT,MACRF=PM,BLKSIZE=1320,         *
               LRECL=132,RECFM=FBA
EIN      DCB   DSORG=PS,DDNAME=SYSUT1,MACRF=GM,EODAD=EOFT
AUS      DCB   DSORG=PS,DDNAME=SYSUT2,MACRF=PM,LRECL=80,RECFM=FB
         END
