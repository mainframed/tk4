*          DATA SET HD122650T  AT LEVEL 002 AS OF 05/04/76
         PRINT OFF
         MACRO
&LABEL   PROG  &BYTES
         GBLA  &SCHALT
         LCLA  &REGANZ,&REGEND
         LCLC  &NAME
         PRINT NOGEN
*                                                                     *
*        BASISADRESSIERUNG - OS                                       *
*                                                                     *
         AIF   (N'&LABEL EQ 1).P01
         MNOTE 4,'CSECT-NAME FEHLT, ''CSECT1'' WIRD ANGENOMMEN'
&NAME    SETC  'CESECT1'
CSECT1   CSECT
         AGO   .P02
.P01     ANOP
&NAME    SETC  '&LABEL'
&NAME    CSECT
.P02     AIF   (N'&BYTES NE 0).P03
         MNOTE *,'PROGRAMMGROESSE FEHLT, 4096 BYTES WIRD ANGENOMMEN'
&REGANZ  SETA  1
         AGO   .P04
.P03     ANOP
&REGANZ  SETA  (&BYTES/4096)+1
.P04     ANOP
         AIF   (&REGANZ LE 10).P05
         MNOTE *,PROGRAMMGROESSE WIRD AUF 4096 BYTES GEKUERZT'
&REGANZ  SETA  10
.P05     ANOP
&REGEND  SETA  &REGANZ+2
.*
&NAME    SETC  '$@#'.'&SYSNDX'
         SAVE  (14,12)
         BALR  R3,0
         USING *,R3
         ST    R13,&NAME.3+4
         LR    R0,R2
         LR    R2,R13
         LA    R13,&NAME.3
         ST    R13,8(R2)
         LR    R2,R0
         AIF   (&REGANZ EQ 1).P10
         LM    R3,R&REGEND,&NAME.2    LADEN BAEISADRESSKONSTANTEN
         USING &NAME.1,R3             USING-ANWEISUNGEN
         USING &NAME.1+4096,R4
         AIF   (&REGANZ EQ 2).P10
         USING &NAME.1+8192,R5
         AIF   (&REGANZ EQ 3).P10
         USING &NAME.1+12288,R6
         AIF   (&REGANZ EQ 4).P10
         USING &NAME.1+16384,R7
         AIF   (&REGANZ EQ 5).P10
         USING &NAME.1+20480,R8
         AIF   (&REGANZ EQ 6).P10
         USING &NAME.1+24576,R9
         AIF   (&REGANZ EQ 7).P10
         USING &NAME.1+28672,R10
         AIF   (&REGANZ EQ 8).P10
         USING &NAME.1+32768,R11
         AIF   (&REGANZ EQ 9).P10
         USING &NAME.1+36864,R12
.P10     ANOP
         B     &NAME.1
&NAME.3  DC    18F'0'
         AIF   (&SCHALT EQ 1).P10A
&SCHALT  SETA  1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.P10A    AIF   (&REGANZ EQ 1).P11
&NAME.2  DC    A(&NAME.1)
         DC    A(&NAME.1+4096)
         AIF   (&REGANZ EQ 2).P11
         DC    A(&NAME.1+8192)
         AIF   (&REGANZ EQ 3).P11
         DC    A(&NAME.1+12288)
         AIF   (&REGANZ EQ 4).P11
         DC    A(&NAME.1+16384)
         AIF   (&REGANZ EQ 5).P11
         DC    A(&NAME.1+20480)
         AIF   (&REGANZ EQ 6).P11
         DC    A(&NAME.1+24576)
         AIF   (&REGANZ EQ 7).P11
         DC    A(&NAME.1+28672)
         AIF   (&REGANZ EQ 8).P11
         DC    A(&NAME.1+32768)
         AIF   (&REGANZ EQ 9).P11
         DC    A(&NAME.1+36864)
.P11     ANOP
&NAME.1  EQU   *
         MEND
         PRINT ON
*          DATA SET HD122640T  AT LEVEL 005 AS OF 05/04/76
         TITLE 'UHDV040S (HDVTIME) ERMITTELN TAGESZEIT UND TAGESDATUM'
***********************************************************************
***********************************************************************
****           C O P Y R I G H T                                   ****
****           HAMBURGER DATENVERARBEITUNGS G.M.B.H                ****
****           Z.HD. HERRN HUETTEL                                 ****
****           ALTER WALL 12                                       ****
****                                                               ****
****           2000 HAMBURG 11                                     ****
****           TELEFON  040 - 37 00 5-223                          ****
****                                                               ****
****           DIE GEWERBLICHE NUTZUNG DIESES PROGRAMMS DURCH      ****
****           VERKAUF ODER VERMIETUNG IST VERBOTEN                ****
****                                                               ****
****           EINE HAFTUNG FUER DIESES PROGRAMM SEITENS DER       ****
****           HDV BESTEHT NICHT                                   ***
****                                                               ***
****                                                               ***
***********************************************************************
***********************************************************************
         EJECT
UHDV040S PROG  4000
         B     START
         EJECT
* * *                                                             * * *
*                                                                     *
*              AUFBEREITEN TAGESDATUM                                 *
*                                                                     *
*              AUS TIME DEC   REGISTER 1   00 YY DD DC                *
*                             REGISTER 0   HH MM SS TH                *
*                                                                     *
*              IN  DATUM      0T TM MJ JC                             *
*                  ZEIT       HH MM SS TC                             *
*                                                                     *
* * *                                                             * * *
         EJECT
* *                                                                 * *
*                                                                     *
*              TABELLE FUER ERMITTLUNG DES MONATS / TAGES             *
*                                                                     *
* *                                                                 * *
TABTAGE  DS    0CL52         1.WERT NORM.JAHRE  2.WERT SCHALTJAHRE
         DC    PL2'0',PL2'0'      JANUAR         01
         DC    P'31',P'31'        FEBRUAR        02
         DC    P'59',P'60'        MAERZ          03
         DC    P'90',P'91'        APRIL          04
         DC    P'120',P'121'      MAI            05
         DC    P'151',P'152'      JUNI           06
         DC    P'181',P'182'      JULI           07
         DC    P'212',P'213'      AUGUST         08
         DC    P'243',P'244'      SEPTEMBER      09
         DC    P'273',P'274'      OKTOBER        10
         DC    P'304',P'305'      NOVEMBER       11
         DC    P'334',P'335'      DEZEMBER       12
         DC    4C'%'              TABELLEN-ENDE
         EJECT
* *                                                                 * *
*                                                                     *
*              HILFSFELDER                                            *
*                                                                     *
* *                                                                 * *
DSDATUM  DSECT
DATUM    DS    F             AUFBEREITETES DATUM
DSZEIT   DSECT
ZEIT     DS    F             AUFBEREITETE  ZEIT
UHDV040S CSECT
DATE     DS    F             DATUM AUS  'TIME DEC'
TIME     DS    F             ZEIT  AUS  'TIME DEC'
HILFSFLD EQU   TIME
* *                                                                 * *
*                                                                     *
*              HAUPTPROGRAMM                                          *
*                                                                     *
* *                                                                 * *
         USING DSDATUM,R8
         USING DSZEIT,R9
         DS    0H
START    EQU   *
         LM    R8,R9,0(R1)
         TIME  DEC
         ST    R1,DATE                 SICHERN DATUM
         ST    R0,TIME                 SICHERN ZEIT
         MVN   TIME+3(1),=X'0C'        VORZEICHNEN AUF 1/1000 SEKUNDE
         ZAP   ZEIT,TIME               ABSPEICHERN AUFBER. ZEIT
         MVO   DATUM,DATE(2)           JAHR UMSETZEN
         MVN   DATUM+3(1),=X'0C'       VORZEICHEN SETZEN
         ZAP   HILFSFLD,DATUM          RECHNUNG, OB SCHALTJAHR
         AP    HILFSFLD,=P'1900'
         DP    HILFSFLD+1(3),=P'4'
         LA    R10,TABTAGE+4           ADR.TABELLE LADEN
         CLI   HILFSFLD+3,X'0C'
         BNE   A1                      WENN REST, KEIN SCHALTJAHR
         LA    R10,2(R10)              SCHALTJAHR, 2.WERT BENUTZEN
A1       EQU   *                       ERMITTELN TAG + MONAT
         ZAP   HILFSFLD,=P'0'
A2       EQU   *
         AP    HILFSFLD,=P'1'          MONAT ERHOEHEN
         CP    DATE+2(2),0(2,R10)      VERGL. TAGE MIT TABELLE
         BL    A3
         BE    A3
         LA    R10,4(R10)              ADR. ERHOEHEN
         CLI   0(R10),C'%'             TAB-ENDE
         BNE   A2                      NEIN
         AP    HILFSFLD,=P'1'          MONAT 12
A3       EQU   *
         SH    R10,=H'4'               ADRESSE UM 4 ZURUECKSETZEN
         SP    DATE+2(2),0(2,R10)      TAGE - TAB = TAG
         ZAP   DATUM(2),DATE+2(2)      UEBERTR. IN DATUM
         MVZ   DATUM+2(1),HILFSFLD+3   MONAT UEBERTRAGEN
         MVN   DATUM+1(1),HILFSFLD+2
*
         L     R13,4(R13)
         RETURN (14,12)
         LTORG
         PUSH  PRINT
         PRINT OFF
         DC    C'COPYRIGHT BY HAMBURGER DATENVERARBEITUNGS GMBH '
         DC    C'ALTER WALL 12  2000 HAMBURG 11 '
         DC    C'*',C'&SYSDATE',C'*'
         PRINT ON
         POP   PRINT
         CSECT
         DC    CL21'005HD122640T 05/04/76'
         END
