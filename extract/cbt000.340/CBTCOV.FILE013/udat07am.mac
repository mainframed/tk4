./ ADD NAME=LIST
PROC 1 DATASET T(NO) COL(1:72)
 CONTROL NOMSG,NOFLUSH,PROMPT,MAIN
 GLOBAL &P1,&P2
 FREE F(ABCDE)
 ALLOC F(ABCDE) DA(&DATASET.) SHR
 SET &ERRFLAG = 0
 SET &EOFFLAG = 0
 ERROR DO
   IF &ERRFLAG = 1 THEN GOTO ERRAUSG
   IF &LASTCC = 400 THEN DO
       SET &EOFFLAG = 1
       WRITE *** END OF DATA ***
       GOTO AUSGANG
     END
   ELSE DO
       SET &ERRFLAG = 1
       WRITE **** ERROR **** CC = &LASTCC
       GOTO AUSGANG
     END
 END
 IF &T = TEST THEN CONTROL LIST CONLIST SYMLIST MSG
 SET &P1 =
 SET &P2 =
 %LISTOPS COL(&COL) T(&T)
 OPENFILE ABCDE INPUT
 ATTN DO
   WRITE ATTENTION-EXIT TAKEN
PRMTNOW: +
   WRITE ENTER NEW COL-VALUE (I.E. 41:110 OR 41); +
        ===&P1:&P2===, END OR HELP
   READ &COL
   IF &COL = END THEN GOTO AUSGANG
   IF &COL = HELP OR &COL = H THEN DO
      DATA
      HELP LIST O(COL)
      ENDDATA
     WRITE
      GOTO PRMTNOW
      END
   IF &COL = X THEN DO
     READ &COL,&T
     IF &T = TEST THEN CONTROL LIST SYMLIST CONLIST MSG
     ELSE CONTROL NOLIST NOSYMLIST NOCONLIST NOMSG
   END
   %LISTOPS COL(&COL) T(&T)
   RETURN
 END
 GETFILE ABCDE
 SET &LEN = &LENGTH(&ABCDE)
 SET &N = &SUBSTR(&EVAL(&LEN-7):&LEN,&ABCDE)
 SET &NUM = 0
 IF &DATATYPE(&N) = NUM THEN SET &NUM = 1
 SET &N = 0
XYZ: +
 DO WHILE &EOFFLAG NE 1
   SET &LEN = &LENGTH(&ABCDE)
   IF &NUM = 0 THEN SET &N = &N + 1
   ELSE SET &N = &SUBSTR(&EVAL(&LEN-4):&LEN,&ABCDE)
   IF &P2 > &EVAL(&LENGTH(&ABCDE) - 8 * &NUM) THEN +
        SET &PE = &EVAL(&LENGTH(&ABCDE) - 8 * &NUM)
   ELSE SET &PE = &P2
   IF &NUM = 1 THEN WRITE &N &SUBSTR(&P1:&PE,&ABCDE)
   ELSE WRITE &SUBSTR(&LENGTH(&N):4,0000)&N &SUBSTR(&P1:&PE,&ABCDE)
   GETFILE ABCDE
 END
AUSGANG: +
 CLOSFILE ABCDE
 FREE F(ABCDE)
ERRAUSG: +
 EXIT
