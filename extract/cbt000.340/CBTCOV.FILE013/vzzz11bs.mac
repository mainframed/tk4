* PSWSAMP IS THE SAMPLE TAKER FOR PACKLIST. THE TECHNIQUE IS TO MOVE
* THE TRACE TABLE TO AN OUTPUT RECORD. IT DOES NOT MATTER THAT THE
* MOVE LONG OF THE TABLE CAN BE INTERUPTED. THE VALUES TAKEN AT ANY
* POINT ARE A SAMPLE, WHETHER INTERUPTED OR NOT.
* THE SAMPLE WILL NOT BEGIN UNLESS THE FIRST ENTRY OF THE TABLE IS
* NOW DIFFERENT THAN THE PREVIOUS SAMPLE. THE SAMPLE RATE IS
* APPROXIMATELY ONCE EVERY 2.5 SECONDS (THE FIELD HOW LONG CAN BE
* ADJUSTED).
* THE TECHNIQUE FOR OVERALL SAMPLING AND END USE IS COVERED IN  THE
* PACKLIST PROGRAM .
*
         TITLE  'PACKLIST   SAMPLE ROUTINE'
PSWSAMP CSECT
         REG                   REGISTER EQUATES
         XSAVE  R12,SVA,PSWSAMP
         L     R4,16
         CLC   X'190'(2,R4),=X'07FB'
         BNE   TRACEOK
         WTO   'INTERNAL TRACE NOT ACTIVE - PROGRAM TERMINATED',       *
               ROUTCDE=(2,11)
         ABEND 111
TRACEOK  EQU   *
         LR    R0,R12
         LR    R1,R13
         LA    R15,SVCROUT
         SVC   250
* MESSAGE TO OPERATOR SO PROGRAM CAN BE ENDED
         WTOR  '*** PSW ACTIVITY SAMPLER, ENTER STOP TO STOP',REPLY,   X
               L'REPLY,ECB,ROUTCDE=(2,11)
* CONTROL CONSTANTS
         L     R4,84           TRACE TABLE POINTER
         LM    R4,R5,4(R4)     ENTRIES IN TRACE TABLE
         SR    R5,R4        TRACE TABLE LENGTH IN R5
         STH   R5,TAPE+82     LRECL
         STH   R5,TAPE+62     BLKSIZE
         GETMAIN R,LV=(R5)     WORK AREA
         LR    R6,R1           WORK AREA
         LR    R7,R5           LENGTH
         OPEN  (TAPE,(OUTPUT)) OUTPUT FILE
         STM   R4,R7,MOVLONG   SAVE REGISTERS FOR LOADING
REWRITE  LM    R4,R7,MOVLONG   LOAD MOVE REGISTERS
         MVCL  R6,R4           MOVE DATA FROM TRACE TABLE TO OUTPUT
         L     R6,MOVLONG+8    ADDRESS OF OUTPUT AREA
         PUT   TAPE,(R6)       EACH COPY IS SEPARATE RECORD
         CLC   REPLY,STOP      OPERATOR STOPPING SAMPLE
         BE    GOHOME          YES
WHEN     EQU   *
         STIMER WAIT,MICVL=HOWLONG   WAIT A FEW SECONDS
         CLC   28(4,R4),28(R6)       HAS ENTIRE TRACE TABLE CHANGED
         BNE   REWRITE               YES
         B     WHEN                  NO, WAIT SOME MORE
GOHOME   EQU   *
         CLOSE TAPE            CLOSE THE OUTPUT FILE
         XRETURN 0
SVCROUT  EQU   *
         LR    R12,R0
         LR    R13,R1
         SR    R1,R1
         LA    R0,14
         SVC   95
         SVC   3
MOVLONG  DC    5F'0'
STOP     DC    C'STOP'
ENDWTO   EQU   *
REPLY    DC    CL4' '
VOLSER   DS    CL6
ECB      DC    F'0'
WAITBLK  DC    F'0'
HOWLONG  DS    0D
         DC    X'0000000244400000'
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=PM,RECFM=F
         END
