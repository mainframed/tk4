*/T0518X01 JOB  (S,23556),34.HOFFM,CLASS=Z,NOTIFY=HOFFM,MSGCLASS=A
*/XDAU01G  EXEC AFC
*/SYSIN    DD   *
         TITLE 'INLINE MACROS'
***********************************************************************
**       XDACGEN INLINE MACROS                                       **
***********************************************************************
         SPACE
         MACRO
&L       FEHL  &RC4=,&RC8=,&RC12=,&RC16=,&ALL=
         AIF   ('&ALL' NE '').M100
         AIF   ('&RC4' EQ '').M200
&L       CL    RF,F04
         BE    &RC4
         AIF   ('&RC8' EQ '').M200
         CL    RF,F08
         BE    &RC8
         AIF   ('&RC12' EQ '').M200
         CL    RF,F12
         BE    &RC12
         AIF   ('&RC16' EQ '').M200
         CL    RF,F16
         BE    &RC16
         AGO   .M200
.M100    ANOP
&L       LTR   RF,RF
         BP    &ALL
.M200    ANOP
         MEND
         SPACE
         MACRO
&L       TEST  &NAME
&L       MVC   CMDNAME,=CL5'&NAME'
         TM    STCOND,SW&NAME               KOMMANDOWORT RICHTIG..
         BZ    B81030                        NEIN, FALSCH PLAZIERT
         MVC   STCOND,ST&NAME               FOLGE-KOMBINATION
         MVI   STMT,SW&NAME                 STATEMENT ID
         MEND
         SPACE
         MACRO
&LAB     SYSVAR &VAR                        TAB-SYSTEM-VARIABLE
         LCLA  &A1,&A2,&A3
         LCLC  &C1
&A1      SETA  N'&SYSLIST
&A2      SETA  1
         DS    0F
.M100    ANOP
&A3      SETA  K'&SYSLIST(&A2)-1
&C1      SETC  '&SYSLIST(&A2)'(2,&A3)
SV&C1    DC    CL7'&C1'                     VARIABLEN-NAME
&C1      SETC  T'&SYSLIST(&A2)
         DC    AL1(&A3-1)                   LAENGE VN - 1
         DC    CL1'&C1'
         DC    AL1(L'&SYSLIST(&A2)-1)       LAENGE VARIABLE - 1
         DC    AL2(&SYSLIST(&A2)-DSVARS)    DISPLACEMENT
&A2      SETA  &A2+1
         AIF   (&A2 LE &A1).M100
         MEND
XDACGEN  TITLE 'PRUEFEN UND FORMATIEREN SYSIN-STEUERKARTEN FUER XDATA.'
***********************************************************************
**                                                                   **
**       PROGRAMM  : XDACGEN                                         **
**       AUTOR     : H. HOFFMANN                                     **
**       SPRACHE   : ASSEMBLER                                       **
**       DATUM     : 30.06.77                                        **
**       VERSION   : 01/G VOM 05.03.81                               **
**                                                                   **
***********************************************************************
         SPACE 2
***********************************************************************
**                                                                   **
**       P R O G R A M M B E S C H R E I B U N G                     **
**       ---------------------------------------                     **
**                                                                   **
**       DAS UNTERPROGRAMM XDACGEN WIRD VON XDAMAIN AUFGERUFEN,      **
**       WENN DER DDNAME 'SYSIN' IN DER TIOT GEFUNDEN WURDE.         **
**                                                                   **
**       DIE STEUERKARTEN VON SYSIN WERDEN MIT EIGENEM DCB GELESEN   **
**       UND AUF FORMALE RICHTIGKEIT UEBERPRUEFT.                    **
**                                                                   **
**       DER KARTENINHALT UND FEHLERMELDUNGEN WERDEN UEBER DIE ALS   **
**       PARAMETER UEBERGEBENE DRUCKROUTINE FUER SYSPRINT GEDRUCKT.  **
**                                                                   **
**       WIRD DAS STATEMENT -PROC ERKANNT, GENERIERT XDACGEN AUS-    **
**       FUEHRBAREN MASCHINENCODE FUER DIE STEUERANWEISUNGEN.        **
**                                                                   **
**       ALS PARAMETER MUESSEN VOM RUFENDEN PROGRAMM                 **
**             1. DIE ADRESSE DER DRUCKROUTINE                       **
**             2. DIE MODAREA IN DEM FESTGELEGTEN AUFBAU             **
**             3. EIN VOLLWORT, IN DAS DER PROC-ENTRY GESTELLT WIRD  **
**       UEBERGEBEN WERDEN.                                          **
**                                                                   **
**       EIN RETURN-CODE > 4 ZEIGT AN, DASS FEHLER IN DER SYNTAX     **
**       FESTGESTELLT WURDEN. DER EVENTUELL ERZEUGTE CODE KANN       **
**       NICHT ZUR AUSFUEHRUNG VERWENDET WERDEN.                     **
**                                                                   **
***********************************************************************
         EJECT
         SPACE 6
         PRINT NOGEN
XDACGEN  CSECT
         PROST (RA,RB,RC),,REG,SAVE=SAVEAREA INITIALISIERUNG
         MVC   PARMAREA(12),0(R1)            PARAMETERLISTE SICHERN
         USING DSMOD,R6                      MOD AREA
         USING XDAU011,R15                   SYSIN EXIT
         USING DSSOUR,SREG                   SOURCE
         USING DSOBJ,OREG                    OBJECT
         MVC   SYSIN+40(8),DDNAME            DEFAULT DDNAME
         TM    PARMAREA+8,X'80'              PARM FUER DDNAME
         BO    B00010                         NEIN
         L     R6,12(,R1)                    ADR DDNAME
         MVC   SYSIN+40(8),0(R6)             DDNAME IN DCB
B00010   NULL
         L     R6,MODADR                     ADRESSE MOD-AREA
         LTR   R6,R6                         IST EINE ADRESSE VORHANDEN
         BZ    B10001                         NEIN
         LH    R1,PARMLEN                    LAENGE VON DATA
         LA    R2,80                         MAXIMALLAENGE
         CLR   R1,R2
         BNH   *+6
         LR    R1,R2
         BCTR  R1,0                          LAENGE - 1
         STC   R1,SVPARM+9                    IN LAENGE VON $PARM
         MVC   $PARM(80),PARMFLD
         XC    MODKEY,MODKEY                 MODKEY LOESCHEN
         EJECT
***********************************************************************
**       PROGRAMM START                                              **
***********************************************************************
         SPACE 2
B10001   NULL
         OPEN  (SYSIN,INPUT)                OPEN STEUERDATEI
         MVI   DB,C'0'                      VORSCHUB
B10020   NULL
         TM    COPYFLG,COPYON               IST COPY AKTIV
         BO    B10150                        JA, SYSLIB LESEN
         GET   SYSIN,EB                     LESEN STEUERKARTE
B10040   NOP   B10900                       BRANCH BEI DATA-SET-WECHSEL
B10060   NOP   B10080                       AM ANFANG DER DATEI WERDEN
         CLI   EB,BLANK                     ALLE SAETZE UEBERLESEN,
         BE    B10020                       DIE IN SPALTE 1 EIN BLANK
         OI    B10060+1,240                 HABEN (SORT-ERFORDERNIS)
B10080   NULL
         CLI   EB,C'-'                      KOMMANDO..
         BNE   B10200                        NEIN
B10100   NULL
         TM    FLAG1,ERRBIT                 LAG EIN FEHLER VOR..
         BO    B10120                        JA
         LA    R2,B10120                    FOLGE-ADR
         ST    R2,PRTADR                     BEI FEHLERN
         CLC   PARLEV,NULLS                 KL = 0
         BNE   B81200                        NEIN
         CLI   LEVEL,0                      LEVEL = 0
         BNE   B81040                        NEIN
         CLC   ENDRT,NULLS                  ENDROUTINE..
         BE    B10200                        NEIN
         L     RF,ENDRT                     ADR-ENDROUTINE
         BR    RF                            AUSFUEHREN
B10120   NULL
         XC    ENDRT,ENDRT                  LOESCHEN
         XC    PARLEV,PARLEV
         MVI   LEVEL,NULL
         NI    FLAG1,255-(ERRBIT+CATBIT+LABBIT+BISBIT+GOTOBIT)
         TM    FLAG1,ITHEN                  INTERNES THEN..
         BO    B24020                        JA
         B     B10200
         EJECT
***********************************************************************
**       LESEROUTINE SYSLIB                                          **
***********************************************************************
         SPACE 2
B10150   NULL
         SR    R2,R2                        R2 LOESCHEN FUER ADD
         A     R2,COPYRLEN                  REST LAENGE BUFFER
         BP    B10160                       DATEN AUS PUFFER
         L     R2,COPYWRKA                  WORKAREA ADR
         ST    R2,COPYRPTR                  RECORD POINTER
         READ  COPYDECB,SF,SYSLIB,(R2),'S'  BLOCK LESEN
         CHECK COPYDECB                     WAIT AND SEE
         L     R1,COPYDECB+16               IOB ADRESSE
         LH    R2,SYSLIB+62                 BLKSIZE
         SH    R2,14(,R1)                   REST-COUNT
B10160   NULL
         L     R1,COPYRPTR                  RECORD-POINTER
         MVC   EB,0(R1)                     SATZ NACH EB
         AH    R1,SYSLIB+82                 PTR + LRECL
         SH    R2,SYSLIB+82                 REST - LRECL
         ST    R1,COPYRPTR                  POINTER
         ST    R2,COPYRLEN                  RESTCOUNT
         B     B10040                       VERARBEITEN
         EJECT
***********************************************************************
**       ENTSCHLUESSELUNG DER STEUERKARTEN                           **
***********************************************************************
         SPACE 2
B10200   NULL
         TM    FLAG1,EOFBIT                 EODAD SYSIN..
         BO    B10510                       BRANCH BEI EODAD SYSIN
         MVC   DB+1(80),EB                  EB NACH DB
         TM    COPYFLG,COPYON               IST COPY AKTIV..
         BZ    B10230                        NEIN
         LA    R2,DB+83                     OFFSET IN DB
         TM    FLAG1,DEBBIT                 DEBUG AN
         BZ    B10220
         LA    R2,10(,R2)
B10220   NULL
         MVC   0(8,R2),COPYMBR              MEMBER-NAMEN NACH DB
B10230   NULL
         LTSUB B61001                       DRUCKEN
         LA    R2,B10020                    LESEN
         ST    R2,PRTADR                     NACH FEHLERN
         CLI   EB,COMMENT2                  COL 1 = '*' IST KOMMENTAR
         BE    B10020                       NEU LESEN
         MVI   EB+72,COMMENT1               BEGRENZER
         MVI   EB+73,COMMENT2               BEGRENZER
         CLI   EB,BLANK                     FOLGEKARTE
         BE    B20001                       FOLGEBEARBEITUNG
         CLI   EB,C'-'                      KOMMANDOWORT
         BNE   B81020                       FALSCHE STEUERKARTE
         LA    SREG,EB+1                    OFFSET IM EINGABEPUFFER
         LTSUB B30001                       ZEICHEN SUCHEN
         CLC   SOURCE(4),=CL5'MOD'           MOD-KOMMANDO..
         BE    B21001                        JA
         CLC   SOURCE(5),=CL5'PROC'          PROC-KOMMANDO
         BE    B22001                        JA
         CLC   SOURCE(4),=CL5'DCL'           DCL-KOMMANDO
         BE    B28001                        JA
         CLC   SOURCE(3),=CL5'IF'            IF-KOMMANDO
         BE    B23001                        JA
         CLC   SOURCE(4),=CL5'SET'           SET-KOMMANDO
         BE    B24001                        JA
         CLC   SOURCE(5),=CL5'THEN'          THEN-KOMMANDO
         BE    B24020                        JA
         CLC   SOURCE(5),=CL5'ELSE'          ELSE-KOMMANDO
         BE    B24040                        JA
         CLC   SOURCE(5),=CL5'PEND'          PEND-KOMMANDO
         BE    B27001                        JA
         CLC   SOURCE(5),=CL5'COPY'          COPY-KOMMANDO
         BE    B29001                        JA
         S     SREG,F04                      SOURCE-POINTER - 4
         B     B24001                       WIE SET
         EJECT
***********************************************************************
**       EODAD SYSIN (HOUSEKEEPING FUNKTIONEN)                       **
***********************************************************************
         SPACE 2
B10500   NULL                               EODAD SYSIN
         OI    FLAG1,EOFBIT                 EOF MERKEN
         B     B10100                       STATEMENT ABSCHLIESSEN
         SPACE
B10510   NULL
         CLC   RCODE,F04                    FEHLER..
         BH    B10595                        JA
         TM    FLAG3,PROCBIT                WAR PROC DA..
         BZ    B10595                        NEIN
         LA    R2,B10595                    FOLGE
         ST    R2,PRTADR                     ADRESSE
         TM    FLAG3,PENDBIT                WAR PEND DA..
         BZ    B27800                        JA
         SPACE
B10520   NULL
         TM    FLAG3,NEWBIT                 OUTPUT-AREA ANGELEGT..
         BZ    B10525                        NEIN
         L     R0,OUTLEN                    LAENGE
         GETMAIN R,LV=(0)                   PLATZ HOLEN
         LA    R0,0(,R1)
         ST    R0,$$NEW1                     SICHERN
         LR    R2,R0                        ADRESSE NEW-BEREICH
         L     R1,OUTLEN                    LAENGE
         LA    RE,NULLS                     LOESCHFELD
         LA    RF,1                         LAENGE
         MVCL  R0,RE                        NEW-BEREICH = X'00' (INIT)
B10525   NULL
         XTIME
         MVC   $DATE,03(R1)                 DATE
         MVC   $TIME,11(R1)                 TIME
         LA    R1,DSVARS
         L     R2,MAINPROC                  BUFFER-POINTER
         ST    R1,VARADR-XDAU011(,R2)        SICHERN
         L     R1,DCLANF                    IST VARTAB ANGELEGT..
         LTR   R1,R1
         BZ    B10540                       NEIN
         B     B10600                       INIT ROUTINE NEW-BEREICH
         EJECT
B10530   NULL
         L     R1,DCLANF                    IST VARTAB ANGELEGT..
         LTR   R1,R1
         BZ    B10540                       NEIN
         L     R0,DCLMAX                    ANZAHL PLAETZE
         MH    R0,DCLTLEN                    LAENGE
         FREEMAIN R,LV=(0),A=(1)
         SPACE
B10540   NULL
         L     R2,PROCADR                   PROC-ENTRY
         L     RF,MAINPROC                  OFFSET-AREA
         ST    RF,0(,R2)                    PROC-ENTRY PARAMETER
         TM    FLAG3,SEPBIT                 SEPARATE FILES
         BZ    B10550
         OI    0(R2),X'80'                  KENNZEICHEN PROC SEPARATE
B10550   NULL
         TM    FLAG3,SECBIT                PROC SECOND..
         BZ    B10595                       NEIN
         OI    0(R2),X'40'                  KENNZEICHEN PROC SECOND
         SPACE
B10595   NULL
         CLOSE (SYSIN)                      SYSIN SCHLIESSEN
         FREEPOOL SYSIN                     SYSIN PUFFER FREIGEBEN
         TM    COPYFLG,LIBOPEN              SYSLIB OPEN
         BZ    B10596                        NEIN
         L     R1,COPYWRKA                  IST
         LTR   R1,R1                        WORKA ANGELEGT
         BZ    B10596                        NEIN
         LH    R0,SYSLIB+62                 LAENGE
         FREEMAIN R,LV=(0),A=(1)            FREE SPACE
         CLOSE (SYSLIB)                     SYSLIB SCHLIESSEN
         SPACE
B10596   NULL
         L     RF,RCODE                     RETURN CODE
         PEND  RC=(15)
         EJECT
**********************************************************************
**       INITIALISIERUNGS-ROUTINE FUER NEW-BEREICH                  **
**********************************************************************
         SPACE
B10600   NULL
         L     R0,DCLANZ                    ANZAHL PLAETZE
         L     R2,$$NEW1                    ADRESSE NEW-BEREICH
         USING DSDCLTAB,R1
B10610   NULL
         CLI   DCLBASIS,BASR2               FELD AUS DEM NEW-BEREICH..
         BNE   B10690                        NEIN, KEIN INIT
         LR    R3,R2                        ADRESSE NEW-BEREICH
         AH    R3,DCLDISP                    + DISPLACEMENT
         CLI   DCLTYP,NTYP                  TYP=N..
         BE    B10620
         CLI   DCLTYP,CTYP                  TYP=C..
         BNE   B10640
         MVI   0(R3),BLANK                  FELD = BLANK
         B     B10630
B10620   NULL
         MVI   0(R3),C'0'                   FELD = C'0' N-FELD
B10630   NULL
         CLC   DCLLEN,NULLS                 LAENGE = 1
         BE    B10690
         LH    R4,DCLLEN                    LAENGE
         BCTR  R4,0                          - 1
         STC   R4,*+5                       IN MVC
         MVC   1(*-*,R3),0(R3)
         B     B10690                       EXIT
B10640   NULL
         MVC   *+7(1),DCLLEN+1              FELDLAENGE IN XC
         XC    0(*-*,R3),0(R3)              FELD AUF X'00' LOESCHEN
         CLI   DCLTYP,PTYP                  TYP = P
         BNE   B10690                       INIT ERLEDIGT
         AH    R3,DCLLEN                     + LAENGE
         MVI   0(R3),X'0C'                  FELD = P'0' (P-INIT)
B10690   NULL
         LA    R1,DCLTABL(,R1)              NAECHSTER PLATZ
         BCT   R0,B10610                    WEITERMACHEN
         B     B10530                       EXIT
         EJECT
B10900   NULL                               DATA-SET WECHSEL
         MVI   B10040+1,NULL                WIEDER NOP
         B     B10020                       NEU LESEN
         SPACE 3
B10950   NULL                               DCB-EXIT SYSIN
         NOP   B10960                       1. OPEN
         OI    *-3,240                       BRANCH
         OI    SYSIN+48,X'08'               UNLIKE ATTRIBUTES
         CLC   SYSIN+62(2),NULLS            BLKSIZE VORHANDEN..
         BNE   B10955                        JA
         LH    R2,SYSIN+82                  LRECL
         STH   R2,SYSIN+62                   GLEICH BLKSIZE
B10955   NULL
         BR    RE                           RETURN INS OPEN
B10960   NULL
         MVI   B10040+1,240                 BRANCH NACH GET
         BR    RE                           RETURN
         EJECT
***********************************************************************
**       FOLGEKARTE (ENTSCHLUESSELUNG FORTSETZEN)                    **
***********************************************************************
         SPACE 2
B20001   NULL                               ENTRY FOLGE-ROUTINE
         TM    FLAG1,ERRBIT                 FEHLER..
         BO    B10020                        JA, NEU LESEN
         L     SUBREG,FOLGE                 FOLGE-ADR
         LA    SREG,EB+1                    OFFSET SOURCE
         B     B30001                       ZEICHEN SUCHEN
         SPACE 3
B21001   NULL                               ENTRY MOD-ROUTINE
         CLC   MODADR,NULLS                 ADRESSE VORHANDEN..
         BE    B81020                        MOD KARTE NICHT GUELTIG
         TEST  MOD                          IST MOD HIER GUELTIG
         LA    R2,B21800                    END-ADRESSE
         ST    R2,ENDRT                      MOD-STMT
         L     R6,MODADR                    ADRESSE MODADREA
         LA    SREG,4(,SREG)                OFFSET
         LTSUB B30001                       ZEICHEN SUCHEN
B21010   NULL
         CLC   SOURCE(5),=C'DROP='          KEYWORD DROP..
         BE    B21100                        JA
         CLC   SOURCE(5),=C'SKIP='          KEYWORD SKIP..
         BE    B21110                        JA
         CLC   SOURCE(5),=C'COPY='          KEYWORD COPY..
         BE    B21120                        JA
         CLC   SOURCE(5),=C'LIST='          KEYWORD LIST..
         BE    B21130                        JA
         CLC   SOURCE(5),=C'EXIT='          KEYWORD EXIT..
         BE    B21300                        JA
         CLC   SOURCE(4),=C'KEY='           KEYWORD KEY..
         BE    B21400
         CLC   SOURCE(3),=C'T1='            1. TITELZEILE
         BE    B21500
         CLC   SOURCE(3),=C'T2='            2. TITELZEILE
         BE    B21500
         B     B81100                       FALSCHES KEYWORD
         EJECT
B21100   NULL
         LA    R7,MODDROP                   ZAEHLER
         B     B21200                       ENTSCHLUESSELN
B21110   NULL
         LA    R7,MODSKIP
         B     B21200
B21120   NULL
         LA    R7,MODCOPY
         B     B21200
B21130   NULL
         LA    R7,MODLIST
         B     B21200
         SPACE 2
B21200   NULL                               DROP,SKIP,COPY,LIST
         LA    SREG,5(,SREG)                POINTER + 5
B21210   NULL
         LTSUB B36500                       ZAHL ENTSCHL.
         FEHL  RC4=B81220,RC8=B81230
         CP    ZAHL,MAXVAL                  MAXIMAL-WERT UEBERSCHR.
         BH    B81230                        JA
         ZAP   0(L'MODDROP,R7),ZAHL         SPEICHERN
         B     B21900                       WEITERMACHEN
         SPACE 3
B21300   NULL                               EXIT ENTSCHLUESSELN
         LA    SREG,5(,SREG)                POINTER + 5
         LA    R0,9                         MAXLN + 1
         LR    R1,SREG                      POINTER NACH R1
         MVC   MODEXIT,BLANKS               LOESCHEN
B21310   NULL
         CLI   SOURCE,ALPHA                 ALPHA..
         BL    B21320                        NEIN
         LA    SREG,1(,SREG)                POINTER + 1
         BCT   R0,B21310                    WEITERMACHEN
         B     B81230                       ZU LANG
B21320   NULL
         SR    SREG,R1                      LAENGE
         BZ    B21330                        IST = 0
         BCTR  SREG,0                       LAENGE - 1
         STC   SREG,*+5                     NACH MVC
         MVC   MODEXIT(*-*),0(R1)           EXIT-NAME
         LA    SREG,1(,SREG)                 + 1
B21330   NULL
         LA    SREG,0(SREG,R1)              SREG AUF BEGRENZER
         B     B21900                       WEITER
         EJECT
B21400   NULL                               KEY ENTSCHLUESSELN
         LA    SREG,4(,SREG)                POINTER + 4
         XC    MODKEY,MODKEY                LOESCHEN
         MVI   TYP1,CTYP                    TYP = C IST DEFAULT
         LTSUB B34001                       CTYP KONSTANTEN
         FEHL  RC4=B21900,RC8=B81160,RC12=B81150,RC16=B81170
         CLC   WORK-2(2),NULLS              LAENGE = 0..
         BE    B21900                        JA
         LH    R0,WORK-2                    LN NACH R0
         BCTR  R0,0                          - 1
         STC   R0,*+5                       NACH MVC
         MVC   MODKEY(*-*),WORK             SPEICHERN
         B     B21900
         SPACE 2
B21500   NULL                               TITLE ENTSCHLUESSELN
         ST    SREG,SAVEOREG                SOURCE-POINTER SICHERN
         LA    SREG,3(,SREG)                POINTER + 3
         MVI   TYP1,CTYP                    TYP = C IST DEFAULT
         LTSUB B34001                       CTYP KONSTANTEN
         FEHL  RC4=B21900,RC8=B81160,RC12=B81150,RC16=B81170
         CLC   WORK-2(2),NULLS              LAENGE = 0..
         BE    B21900                        JA
         LH    R0,WORK-2                    LN NACH R0
         BCTR  R0,0                          - 1
         L     R2,SAVEOREG                  SOURCE-POINTER ZURUECK
         LA    R1,MODT1-1                   TITLE 1
         CLC   0(3,R2),=C'T1='
         BE    B21510
         LA    R1,MODT2-1                   TITLE 2
B21510   NULL
         MVC   1(L'MODT1,R1),0(R1)          LOESCHEN
         STC   R0,*+5                       NACH MVC
         MVC   1(*-*,R1),WORK               SPEICHERN
         B     B21900
         SPACE 2
B21800   NULL                               ENDROUTINE MOD
         B     B10120                       EXIT
         SPACE 2
B21900   NULL
         CLI   SOURCE,KOMMA                 BEGRENZER..
         BNE   B21910                        NEIN
         LA    SREG,1(,SREG)                POINTER + 1
         B     B21010                       WEITERMACHEN
B21910   NULL
         CLI   SOURCE,BLANK                 ENDE..
         BE    B21920
         CLI   SOURCE,STERN                 KOMMENTAR..
         BNE   B81240                       UNG. SONDERZEICHEN
B21920   NULL
         LA    R2,B21010                    FOLGE-ADR
         ST    R2,FOLGE                      EINSETZEN
         B     B10020                       NEU LESEN
         EJECT
***********************************************************************
**       PROC ROUTINE                                                **
***********************************************************************
         SPACE 2
B22001   NULL                               ENTRY PROC-ROUTINE
         TEST  PROC                         IST PROC GUELTIG
         TM    FLAG3,PROCBIT                WAR SCHON EINE PROC DA..
         BO    B22100
         OI    FLAG3,PROCBIT                PROC SCHALTER
         LTSUB B38001                       GETMAIN FUER EXIT
         ST    OREG,MAINPROC                ENTRY ADRESSE MAIN PROC
         LR    RF,OREG                      ENTRY POINT
         MVC   0(LNVCODE,OREG),VCODE        CODE MOVEN
         MVC   4096-LNNCODE(LNNCODE,OREG),NCODE CODE MOVEN
         LA    R2,RETURN                    RETURN-ADR
         ST    R2,CONTADR                    FUER CONTINUE
B22010   NULL
         ST    OREG,BUFANF                  PUFFER-ANFANG
         LA    RF,VARADR-XDAU011(,OREG)     ENDE OUTPUT-AREA
         ST    RF,BUFEND                    PUFFER-ENDE DYNAMISCH
         ST    RF,OBJEND                    PUFFER-ENDE STATISCH
         LA    R2,B22800                    END-ADRESSE
         ST    R2,ENDRT                      PROC STMT
         LA    SREG,5(,SREG)                OFFSET
         SPACE
B22020   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
         CLC   SOURCE(9),SEPARATE           SEP STATEMENT
         BNE   B22030                       NAECHSTES SCHLUESSELWORT
         LA    SREG,9(,SREG)                POINTER
         OI    FLAG3,SEPBIT                 KENNZEICHEN SEP-FILE
         B     B22020                       WEITER
         SPACE
B22030   NULL
         CLC   SOURCE(7),SECOND             SECOND KENNWORT..
         BNE   B22040                       NAECHSTES SCHLUESSELWORT
         LA    SREG,7(,SREG)                SOURCE-POINTER
         OI    FLAG3,SECBIT                 KENNZEICHEN PROC SECOND
         B     B22020                       WEITER
         SPACE
B22040   NULL
         CLC   SOURCE(6),DEBUG              DEBUG KENNWORT..
         BNE   B81100                        NEIN, FEHLER
         LA    SREG,6(,SREG)                SOURCE-POINTER
         OI    FLAG3,DEBBIT                 KENNZEICHEN PROC DEBUG
         B     B22020                       WEITER
         EJECT
B22100   NULL
         OI    FLAG3,PROC2BIT               FOLGE PROCEDURE
         TM    FLAG3,CONTBIT                CONTINUE MOEGLICH..
         BO    B22120
         LA    R2,B22120
         ST    R2,PRTADR
         B     B81400                       WARNUNG DRUCKEN
B22120   NULL
         NI    FLAG3,255-(PENDBIT+CONTBIT)
         LTSUB B38001                       GETMAIN FUER EXIT
         L     RF,MAINPROC                  ENTRY POINT MAINPROC
         MVC   4096-LNNCODE(LNNCODE,OREG),4096-LNNCODE(RF)
         L     R2,BUFANF                    ADRESSE HOEHERE PROC
         ST    OREG,CONTADR-XDAU011(,R2)    FOLGE-PROC ADRESSE
         L     R1,STMTABA                   OFFSET TABLE
         ST    R1,STMTABP                   ANFANGS POINTER
         LA    R2,$$OFFSET
         SR    R1,R2                         OFFSET ERRECHNEN
         STH   R1,STMTSOFF-XDAU011(,OREG)   SPEICHERN
         B     B22010
         SPACE
B22800   NULL
         LTSUB B37600                       OFFSET TABLE
         SR    R8,R8                        LOESCHEN
         TM    FLAG3,PROC2BIT               FOLGE PROC..
         BO    B22820
         LA    R8,24                        LAENGE
B22820   NULL
         LTSUB B37200                       PUFFER-ADR
         B     B10120
         EJECT
***********************************************************************
**       IF ROUTINE                                                  **
***********************************************************************
         SPACE 2
B23001   NULL                               ENTRY -IF ROUTINE
         TEST  IF                           IST IF GUELTIG..
         LTSUB B37400                       ELSE ADRESSEN EINSETZEN
         LTSUB B37600                       OFFSET TABLE
         LA    SREG,3(,SREG)                OFFSET
         LA    R2,B23800                    END-ADRESSE
         ST    R2,ENDRT                      IF-STMT
         MVC   IFADR,BUFADR                 PUFFER-POINTER
         XC    IFANZ,IFANZ                  LOESCHEN
         OI    FLAG1,LABBIT                 LABEL MOEGLICH
         MVI   LEVEL,2                      LEVEL = 2
B23010   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
         MVI   LEVEL,2                      LEVEL = 2
         CLI   SOURCE,LPAR                  KLAMMER AUF..
         BNE   B23020                        NEIN
         NI    FLAG1,255-LABBIT             LABEL NICHT MOEGLICH
         L     R2,PARLEV                    KLAMMER-LEVEL
         LA    R2,1(,R2)                     + 1
         ST    R2,PARLEV                    WIEDER SICHERN
         LA    SREG,1(,SREG)                POINTER + 1
         B     B23010                       NAECHSTES ZEICHEN
B23020   NULL
         MVI   TYP1,CTYP                    DEFAULT = CTYP
         LTSUB B30200                       WORT PRUEFEN
         CLI   SOURCE,LABPTR                WAR EIN LABEL DA..
         BNE   B23025
         LA    SREG,1(,SREG)                SOURCE POINTER + 1
         B     B23010
B23025   NULL
         NI    FLAG1,255-LABBIT             LABEL NICHT MOEGLICH
         MVC   CODE1,CODE2                  FELBESCHREIBUNG
         MVI   LEVEL,1                      LEVEL = 1
         LTSUB B30001                       ZEICHEN SUCHEN
         LA    R1,OPTAB                     OPERATOR TABELLE
         LA    R0,OPTABA                    ANZAHL
         SR    R2,R2                        LOESCHEN
B23030   NULL
         IC    R2,0(,R1)                    LAENGE AUFGREIFEN
         STC   R2,*+5                        NACH CLC
         CLC   SOURCE(*-*),1(R1)            OPERATOR VERGLEICHEN
         BE    B23040                        GUELTIG
         LA    R1,6(,R1)                    NAECHSTER PLATZ
         BCT   R0,B23030                    NEUER VERSUCH
         B     B81140                        FEHLER
B23040   NULL
         MVC   CND1(2),4(R1)                CODE
         LA    SREG,1(SREG,R2)              POINTER ERHOEHEN
         EJECT
B23100   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
         MVI   LEVEL,0                      LEVEL = 0
         L     R1,IFANZ                     IF-ANZAHL ERHOEHEN
         LA    R1,1(,R1)
         ST    R1,IFANZ                     SICHERN
         LTSUB B30200                       2. OPERANDEN PRUEFEN
         SPACE
B23200   NULL
         CLC   DTYP1,DTYP2                  TYP-VERTRAEGLICHKEIT
         BE    B23205
         CLI   TYP1,NTYP                    TYP2 = N
         BE    B23210                       WIRD WIE CHAR-TYP BEHANDELT
         CLI   TYP2,NTYP                    TYP2 = N
         BE    B23210                       WIRD WIE CHAR-TYP BEHANDELT
         B     B81190                        FEHLER
B23205   NULL
         CLI   DTYP1,CTYP                   CHARACTER..
         BE    B23210
         CLI   TYP1,PTYP                    BEI NUMERISCHEM
         BNE   B81190                        DATENTYP IST
         CLI   TYP2,PTYP                      NUR 'P'
         BNE   B81190                          GESTATTET
B23210   NULL
         MVC   OBJECT+2(2),ADR1             ADR. 1. OP
         MVC   OBJECT+4(2),ADR2             ADR. 2. OP
         OC    OBJECT+2(1),BAS1             BASIS -
         OC    OBJECT+4(1),BAS2              REGISTER
         MVC   OBJECT+6(2),BRANCH           BRANCH GERIPPE
         CLI   TYP1,PTYP                    GEPACKT..
         BE    B23220                        JA
         MVI   OBJECT,CLCCODE               CLC-CODE
         MVC   OBJECT+1(1),LEN1+1            LAENGE
         MVC   MVCLEN,NULLS                 LAENGE = 0
         B     B23230                       WEITERSUCHEN
         EJECT
B23220   NULL
         MVI   OBJECT,CPCODE                CP-CODE
         PACK  OBJECT+1(1),LEN1+1(1)         LAENGE-1
         OC    OBJECT+1(1),LEN2+1            LAENGE-2
         TM    FLAG3,DEBBIT                 DEBUG AKTIV..
         BZ    B23230                        NEIN
         LTSUB B37800                       PRINT DEBUG INFO
B23230   NULL
         LA    R8,10                        CODE-LAENGE
         LTSUB B37200                       BUFFER-ADR. ERHOEHEN
B23240   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
         CLI   SOURCE,RPAR                  KLAMMER ZU..
         BNE   B23300                        NEIN
         SR    R2,R2                        LOESCHEN
         A     R2,PARLEV                    KLAMMER-LEVEL
         BZ    B81200                        SCHON = 0
         BCTR  R2,0                         KL - 1
         ST    R2,PARLEV                    SICHERN
         LA    SREG,1(,SREG)                POINTER + 1
         B     B23240                       WEITERSUCHEN
         SPACE 2
B23300   NULL
         CLC   SOURCE(5),=CL5'THEN'         INTERNES THEN
         BNE   B23320                       NEIN
         OI    FLAG1,ITHEN                  FLAG FUER INTERNES THEN
         B     B10100                       IF ABSCHLIESSEN
B23320   NULL
         MVI   VERTYP,ANDCHAR               VERKNUEPFUNGS-TYP
         CLI   SOURCE,ANDCHAR               UND ZEICHEN
         BE    B23420
         CLC   SOURCE(3),=C'AND'            UND TEXT
         BE    B23400
         MVI   VERTYP,ORCHAR                VERKNUEPFUNGS-TYP
         CLI   SOURCE,ORCHAR                ODER ZEICHEN
         BE    B23420
         CLC   SOURCE(2),=C'OR'             ODER TEXT..
         BE    B23410
         CLI   CND1,X'88'                   OPERATOR '='
         BNE   B81210                        WENN NICHT, FEHLER
         CLI   SOURCE,KOMMA                 SERIELLES IF..
         BNE   B81210
         MVI   LEVEL,1
         B     B23420                       WIRD GEPRUEFT WIE 'ODER'
         EJECT
B23400   NULL
         LA    SREG,1(,SREG)                SREG + 1 BEI 'AND'
B23410   NULL
         CLI   SOURCE+2,ALPHA-1             ALPHA..
         BH    B81210                       FEHLER
         LA    SREG,1(,SREG)                SREG + 1
B23420   NULL
         LA    SREG,1(,SREG)                SREG + 1
         L     R5,IFADR                     ANFANG IF
         L     R0,IFANZ                     ANZAHL VERGLEICHE
         BCTR  R0,0                          - 1
         LTR   R0,R0                        ANZAHL = 0..
         BZ    B23480
         L     R2,BUFADR                    PUFFER-ADR
         S     R2,BUFANF                    DISPL.
B23430   NULL
         TM    7(R5),B'00001100'            STMT AUFGELOEST
         BZ    B23470                        JA
         CLI   VERTYP,ANDCHAR               UND VERKNUEPFUNG..
         BE    B23440
         CLC   8(2,R5),PARLEV+2             KL PRUEFEN
         BL    B23470                        KLEINER
         B     B23450
B23440   NULL
         CLC   8(2,R5),PARLEV+2             KL PRUEFEN
         BNH   B23470                        KLEINER GLEICH
         TM    7(R5),B'00001100'            JA ODER NEIN
         BM    B23460                        NEIN
         B     B23455
B23450   NULL
         TM    7(R5),B'00001100'            JA ODER NEIN
         BO    B23460                        NEIN
B23455   NULL
         MVC   8(2,R5),PARLEV+2             KL EINSETZEN
         B     B23470                       WEITER
B23460   NULL
         STH   R2,8(,R5)                    IN BRANCH
         OI    8(R5),BASRF                  BASIS-REGISTER
         NI    7(R5),255-X'0C'               LOESCHEN
B23470   NULL
         LA    R5,10(,R5)                   NAECHSTER VERGLEICH
         BCT   R0,B23430                    WEITER
B23480   NULL
         MVC   8(2,R5),PARLEV+2             KLAMMER-LEVEL
         CLI   VERTYP,ANDCHAR               UND VERKNUEPFUNG..
         BE    B23485
         MVC   7(1,R5),CND1                 BRANCH COND
         B     B23490                       WEITERMACHEN
B23485   NULL
         MVC   7(1,R5),CND1+1               BRANCH COND BEI UND
B23490   NULL
         CLI   LEVEL,1                      SERIELLES IF
         BE    B23100
         MVI   LEVEL,2                      DA MUSS NOCH WAS KOMMEN
         B     B23010
         EJECT
B23800   NULL                               ENDROUTINE IF-STMT
         LR    R1,OREG                      OBJECT POINTER
         S     R1,F04                        - 4
         MVC   1(1,R1),CND1+1               UND-COND
         OI    FLAG1,THENBIT                NUR THEN ADRESSEN
         LTSUB B37400                       THEN ADRESSEN EINSETZEN
         NI    FLAG1,255-THENBIT            WIEDER AUS
         B     B10120                       WEITER GEHT'S
         EJECT
***********************************************************************
**       THEN, ELSE, SET ROUTINEN                                    **
***********************************************************************
         SPACE 2
B24001   NULL                               ENTRY SET-ROUTINE
         TEST  SET                          IST SET GUELTIG..
         LA    SREG,4(,SREG)                OFFSET
         B     B24100                       ENTSCHLUESSELN
         SPACE
B24020   NULL                               ENTRY THEN ROUTINE
         TEST  THEN                         IST THEN GUELTIG
         LA    SREG,5(,SREG)                OFFSET
         NI    FLAG1,255-ITHEN              INTERNES THEN AUS
         LA    R2,B10020                    RETURN ADR NACH FEHLERN
         ST    R2,PRTADR                     SPEICHERN
         B     B24120                       ENTSCHLUESSELN
         SPACE
B24040   NULL                               ENTRY ELSE ROUTINE
         TEST  ELSE                         IST ELSE GUELTIG
         LA    SREG,5(,SREG)                OFFSET
         MVC   ELSEADR,BUFADR               SICHERN
         MVC   OBJECT(2),BRANCH             THEN VERZWEIGUNG
         LA    R8,4                         LAENGE
         LTSUB B37200                       PUFFER-ADR
B24100   NULL                               ENTSCHL SET,THEN,ELSE
         LTSUB B37400                       ELSE ADRESSEN EINSETZEN
B24120   NULL                               ENTSCHL SET,THEN,ELSE
         LTSUB B37600                       OFFSET TABLE
         LA    R2,B24800                    END-ADR
         ST    R2,ENDRT                     SICHERN
         XC    CODE1,CODE1                  CODE-FELD LOESCHEN
B24140   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
B24160   NULL
         XC    CODE1,CODE1                  CODE-FELD LOESCHEN
         OI    FLAG1,LABBIT                 LABEL MOEGLICH
         MVI   LEVEL,2                      LEVEL = 2
         LTSUB B31001                       SCHLUESSELWORT
         FEHL  RC4=B24200
         TM    FLAG1,GOTOBIT                WAR EIN GOTO DA..
         BO    B81180
         CLC   OBJECT(4),NULLS              KOMMANDWORT..
         BE    B25001
         OI    FLAG1,GOTOBIT                BRANCH KEIN CODE MEHR
B24170   NULL
         LA    R8,4                         LAENGE
B24180   NULL
         LTSUB B37200                       PUFFERADR
B24190   NULL
         MVI   LEVEL,NULL                   LEVEL = 0
         B     B24140                       WEITERMACHEN
         EJECT
B24200   NULL
         LTSUB B32001                       WORT
         FEHL  RC4=B81260,RC8=B81120,RC12=B81130,RC16=B81150
         NI    FLAG1,255-LABBIT             LABEL NICHT MOEGLICH
         CLI   SOURCE,LABPTR                WAR EIN LABEL DA..
         BNE   B24205
         LA    SREG,1(,SREG)                SOURCE POINTER + 1
         NI    FLAG1,255-GOTOBIT            GOTO WIEDER MOEGLICH
         B     B24190
B24205   NULL
         TM    FLAG1,GOTOBIT                WAR EIN GOTO DA..
         BO    B81180                        JA, FEHLER
         MVC   CODE1,CODE2                  NACH 1. OPERAND
         CLI   DTYP1,NTYP                   ENTSCHLUESSELN
         BE    B24400                       ARITHMETISCHE ANWEISUNGEN
         LH    R2,LEN1                      ECHTE LAENGE1
         LA    R2,1(,R2)                     NACH MVCLEN
         STH   R2,MVCLEN
B24210   NULL
         LTSUB B30001                       ZEICHEN
         MVI   LEVEL,1                      ENTSCHL.-LEVEL
         CLI   SOURCE,EQUAL
         BNE   B81140
         LA    SREG,1(,SREG)                SOURCE-POINTER + 1
B24220   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
         MVI   LEVEL,NULL                   ENTSCHL.-LEVEL
         LTSUB B30200                       2. OPERANDEN PRUEFEN
         CLC   DTYP1,DTYP2                  TYP-VERTRAEGLICHKEIT
         BE    B24230                       TYPEN SIND GLEICH
         CLI   TYP2,NTYP                    NTYP IST EIN ZWITTER
         BNE   B81190                       UNGLEICHE TYPEN
B24230   NULL
         TM    FLAG1,CATBIT                 VERKETTUNG
         BZ    B24240                        NEIN
         NI    FLAG1,255-CATBIT             AUS
         CLC   MVCLEN,NULLS                 LAENGE ERSCHOEPFT
         BE    B24250                       NUR PRUEFEN
         EJECT
B24240   NULL
         MVI   OBJECT,MVCCODE               MVC-CODE
         MVC   OBJECT+2(2),ADR1             ADR. 1. OP
         MVC   OBJECT+4(2),ADR2             ADR. 2. OP
         OC    OBJECT+2(1),BAS1             BASIS -
         OC    OBJECT+4(1),BAS2              REGISTER
         LH    R1,MVCLEN                    1. LAENGE
         LH    R2,LEN2                      2. LAENGE
         LA    R2,1(,R2)                    R2 + 1
         CLR   R1,R2                        LAENGEN VERGLEICHEN
         BNL   *+6
         LR    R2,R1                        2. LAENGE = 1. LAENGE
         BCTR  R2,0                         R1 - 1
         STC   R2,OBJECT+1                   LAENGE IN MVC
         LA    R2,1(,R2)                    R2 + 1
         SR    R1,R2                        LEN1 - LEN2
         STH   R1,MVCLEN                    RESTLAENGE
         AH    R2,ADR1                      DISPLACEMENT ERHOEHEN
         STH   R2,ADR1                       UND SPEICHERN
         LA    R8,6                         CODE-LAENGE
         LTSUB B37200                       PUFFERADRESSE
B24250   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
         CLI   SOURCE,KOMMA                 VERKETTUNG
         BE    B24260                        JA
         CLC   MVCLEN,NULLS                 REST VORHANDEN
         BE    B24160                        NEIN
         LTSUB B24900                       RESTFELD LOESCHEN
         LTSUB B37200                       PUFFERADRESSE
         B     B24160                       WEITERMACHEN
B24260   NULL
         LA    SREG,1(,SREG)                SOURCE-POINTER + 1
         OI    FLAG1,CATBIT                 VERKETTUNG
         MVI   LEVEL,1                      LEVEL = 1
         B     B24220
         EJECT
B24400   NULL
         USING DSSTACK,R2
         XC    PARLEV,PARLEV                KLAMMERLEVEL = 0
         LA    R2,STACK-STACKL+STACKL*STACKA LETZTER PLATZ IM STACK
         ST    R2,STACKPTR                   NACH STACK-PTR
         XC    STCKCODE(STACKCL),STCKCODE
         LTSUB B30001                       ZEICHEN
         MVI   LEVEL,1                      ENTSCHL.-LEVEL
         CLI   SOURCE,EQUAL
         BE    B24420
         L     R2,STACKPTR                  STACKPTR LADEN
         MVC   STCKCODE,CODE1               CODE1 IN STACK
         B     B24460                       OPERATOR PRUEFEN
B24420   NULL
         LA    SREG,1(,SREG)                SOURCE-POINTER + 1
         LTSUB B30001                       ZEICHEN
         MVI   LEVEL,1                      ENTSCHL.-LEVEL
         CLI   SOURCE,LPAR                  KLAMMER AUF..
         BNE   B24440                        NEIN
         L     R1,PARLEV                    KLAMMER-LEVEL
         LA    R1,1(,R1)                     + 1
         ST    R1,PARLEV                    UND SICHERN
         B     B24420
B24440   NULL
         LTSUB B30200                       OPERANDEN PRUEFEN
         CLI   DTYP2,CTYP                   CHARACTER..
         BNE   B24445
         CLC   LEN2,MAXLNN+2                MAXLAENGE N
         BNL   B81130                        ZU GROSS
         MVI   TYP2,NTYP                    CTYP WIRD NTYP
         MVI   DTYP2,NTYP
B24445   NULL
         LA    R1,STACK                     STACK-ADR
         L     R2,STACKPTR                  STACK-POINTER
         CLR   R2,R1                        STACK-UEBERLAUF..
         BL    B81350                        JA, FEHLER
         MVC   STCKCODE,CODE2               CODE
         MVC   STCKKLVL,PARLEV+2            KLAMMERLEVEL
B24450   NULL
         MVI   LEVEL,0                      ENTSCHL.-LEVEL
         LTSUB B30001                       ZEICHEN
         L     R2,STACKPTR                  STACK-POINTER
         CLI   SOURCE,RPAR                  KLAMMER ZU..
         BNE   B24460
         CLC   PARLEV,NULLS                 KLAMMER-LEVEL
         BE    B81200                        SCHON = 0
         LTSUB B30400                       STACKROUTINE
         L     R1,PARLEV                    KLAMMER-LEVEL
         BCTR  R1,0                          - 1
         ST    R1,PARLEV                    SICHERN
         MVC   STCKKLVL,PARLEV+2            KLAMMER-LEVEL IN STACK
         LA    SREG,1(,SREG)                SOURCE PTR + 1
         B     B24450
B24460   NULL
         CLI   SOURCE,ALPHA-1               ALPHA..
         BH    B24600                       STACK ABSCHLIESSEN
         CLI   SOURCE,SYSVAR                SYSTEMVARIABLE..
         BE    B24600                       STACK ABSCHLIESSEN
         MVI   LEVEL,1                      ENTSCHL.-LEVEL
         CLI   SOURCE,PLUS                  OPERAROR PRUEFEN
         BE    B24480
         CLI   SOURCE,MINUS
         BE    B24480
         CLI   SOURCE,MAL
         BE    B24490
         CLI   SOURCE,GETEILT
         BNE   B81140                       OPERATOR UNGUELTIG
         CLI   SOURCE+1,COMMENT1            FOLGT EIN KOMMENTAR..
         BNE   B24490                        NEIN
         CLI   SOURCE+2,COMMENT2
         BE    B24490
         LA    SREG,1(,SREG)                SOURCE-POINTER + 1
         MVI   SOURCE,C':'                  ANZEIGE FUER REMINDER
         B     B24490
B24480   NULL
         MVI   STCKOLVL,1                   OP-LEVEL = 1
         B     B24500
B24490   NULL
         MVI   STCKOLVL,2                   OP-LEVEL = 2
B24500   NULL
         MVC   STCKOPER,SOURCE              OPERATOR
         LTSUB B30400                       STACKROUTINE
         L     R2,STACKPTR
         SH    R2,STACKLN
         ST    R2,STACKPTR
         B     B24420                       WEITERSUCHEN
B24600   NULL                               RECHEN OP ABSCHLIESSEN
         CLC   PARLEV,NULLS                 KLAMMERLEVEL = 0
         BNE   B81200                        NEIN, FEHLER
         LTSUB B30400                       STACK ABARBEITEN
         LTSUB B30800                       ZUWEISUNG COMPILIEREN
         B     B24160                       WEITERMACHEN
         EJECT
B24800   NULL                               ENDROU SET,THEN,ELSE
         CLI   DTYP1,NULL                   WAREN STATEMENTS DA..
         BE    B24840                        NEIN
         CLI   DTYP1,CTYP                   CHARACTER ZUWEISUNG
         BNE   B24830                        NEIN
         CLC   MVCLEN,NULLS                 REST VORHANDEN
         BE    B24840                        NEIN
         LTSUB B24900                       RESTFELD LOESCHEN
         TM    FLAG3,DEBBIT                 DEBUG AKTIV..
         BZ    B24820                        NEIN
         LTSUB B37800                       PRINT DEBUG INFO
B24820   NULL
         LTSUB B37200                       PUFFERADRESSE
         B     B24840
B24830   NULL
         LTSUB B30400                       STACK ABARBEITEN
         LTSUB B30800                       ZUWEISUNG COMPILIEREN
B24840   NULL
         CLI   STMT,SWELSE                  LETZTES STMT = ELSE
         BNE   B10120                        NEIN
         L     R2,ELSEADR                   BRANCH NACH THEN
         L     R1,BUFADR
         S     R1,BUFANF                    DISPL
         STH   R1,2(,R2)                    DISPL.
         OI    2(R2),BASRF                  BASIS
         XC    ELSEADR,ELSEADR              LOESCHEN
         B     B10120                       WEITER
         EJECT
B24900   SOSUB                              RESTFELD LOESCHEN
         LH    R1,MVCLEN                    RESTLAENGE
         MVC   MVCLEN,NULLS                 LAENGE LOESCHEN
         MVI   OBJECT,MVICODE               MVI-CODE
         MVI   OBJECT+1,BLANK               GRUNDSTELLUNG
         CLI   TYP1,XTYP                    HEXA..
         BNE   B24920                        NEIN
         MVI   OBJECT+1,NULL                GRUNDSTELLUNG
B24920   NULL
         MVC   OBJECT+2(2),ADR1             DISPLACEMENT
         OC    OBJECT+2(1),BAS1             BASISREGISTER
         LA    R8,4                         LAENGE
         BCTR  R1,0                         LAENGE - 1
         LTR   R1,R1                        R1 = 0
         BZR   SUBREG                        JA
         BCTR  R1,0                         LAENGE - 1
         MVI   OBJECT+4,MVCCODE             MVC-CODE
         STC   R1,OBJECT+5                  MVC-LAENGE
         LH    R2,ADR1                      DISPLACEMENT 1
         LA    R2,1(,R2)                    ADR2 + 1
         STH   R2,OBJECT+6                  ADR1
         OC    OBJECT+6(1),BAS1             BASIS
         MVC   OBJECT+8(2),OBJECT+2         ADR2
         LA    R8,10                        BEFEHLSLAENGE
         EOSUB                               RETURN
         EJECT
B25001   NULL                               ENTRY CALL ROUTINE
         CLC   1(4,R1),=C'CALL'             CALL KENNWORT..
         BNE   B25300                        NEIN
         MVI   LEVEL,1                      LEVEL=1
         LTSUB B30001                       ZEICHEN SUCHEN
         LTSUB B31100                       NAMEN PRUEFEN
         FEHL  RC4=B81260,RC8=B81270
         CLI   VARNAME+8,BLANK              LAENGER ALS 8 ZEICHEN
         BNE   B81410
         CLC   UCALLADR,NULLS               XDACALL-ADR SCHON IM CODE
         BNE   B25100
         MVC   WORK(4),VXDACALL             ENTRY ADRESSE NACH WORK
         MVI   WORK-1,4                     LAENGE
         LTSUB B37001                       ADRESSE IN OBJ
         LH    R1,ADR2                      DISPLACEMENT
         STH   R1,UCALLADR                  OFFSET XDACALL-ADR
B25100   NULL
         LA    R1,UPROTAB                   UPROTABELLE
         LA    R0,UPROTABA                    ANZAHL PLAETZE
B25120   NULL
         CLC   0(8,R1),VARNAME              UPRRO SCHON GELADEN
         BE    B25160
         CLC   0(8,R1),BLANKS               PLATZ FREI..
         BE    B25140
         LA    R1,12(,R1)                   NAECHSTER PLATZ
         BCT   R0,B25120                    WEITERMACHEN
         B     B81440                       TABELLE VOLL
B25140   NULL
         LR    R2,R1                        TAB PLATZ SICHERN
         LOAD  EPLOC=VARNAME,ERRET=B81420   UPRO LADEN
         MVC   0(8,R2),VARNAME              UP-NAME IN TABELLE
         ST    R0,8(,R2)                    ENTRY ADRESSE SICHERN
         MVI   8(R2),NULL                   HIGH ORDER BYTE LOESCHEN
         LR    R1,R2                        TAB ADR IN REG 1
B25160   NULL
         MVC   OBJECT(6),CALLCODE           CODE FUER CALL
         MVC   OBJECT+2(2),UCALLADR         OFFSET
         OI    OBJECT+2,BASRF               BASISREGISTER
         MVC   OBJECT+6(4),8(R1)            ENTRY UPRO
         MVC   PARMANZ,NULLS                ANZAHL PARAMETER
         LA    R1,6(,OREG)                  ADR ANZAHL
         ST    R1,PARMANZA                   SICHERN
         LA    R8,10                        LAENGE CODE
         LTSUB B37200                       PUFFERADREASSE
         MVI   LEVEL,0                      LEVEL=0
         LTSUB B30001                       ZEICHEN SUCHEN
         CLI   SOURCE,LPAR                  KLAMMER AUF..
         BNE   B24160                       KEINE PARAMETERLISTE
         LA    SREG,1(,SREG)                POINTER HINTER KLAMMER
         MVI   LEVEL,2                      LEVEL=2
B25180   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
         CLI   SOURCE,KOMMA                 TRENNZEICHEN KOMMA..
         BNE   B25190                        NEIN
         LA    SREG,1(,SREG)                POINTER HINTER KLAMMER
         B     B25180                       WEITERSUCHEN
B25190   NULL
         CLI   SOURCE,RPAR                  KLAMMER ZU
         BE    B25200                       ENDE DER PARAMETERLISTE
         LTSUB B30200                       OPERANDEN PRUEFEN
         MVC   OBJECT(2),ADR2               DISPLACEMENT
         OC    OBJECT,BAS2                  BASISREGISTER
         LH    R1,PARMANZ                   ANZAHL PARAMETER
         LA    R1,1(,R1)                     + 1
         LA    R0,32                        MAXIMALANZAHL
         CLR   R1,R0                        UEBERSCHRITTEN
         BH    B81430                       JA
         STH   R1,PARMANZ
         LA    R8,2                         LAENGE
         LTSUB B37200                       PUFFERADREASSE
         B     B25180
B25200   NULL
         LA    SREG,1(,SREG)                POINTER HINTER KLAMMER
         MVI   LEVEL,0                      LEVEL=0
         L     R1,PARMANZA                  ADRESSE PARMANZAHL
         MVC   0(1,R1),PARMANZ+1
         B     B24140                       WEITERMACHEN
         SPACE
B25300   NULL
         CLC   1(5,R1),=C'WRITE'            WRITE KENNWORT..
         BNE   B25400                        NEIN
         MVI   LEVEL,1                      LEVEL=1
         LTSUB B30001                       ZEICHEN SUCHEN
         LA    R1,DDNAMES                   WRITE DDNAMEN
         LA    R0,DDNAMESA                  ANZAHL
         SR    R2,R2                        R2 LOESCHEN
B25320   NULL
         IC    R2,0(,R1)                    VERGLEICHSLAENGE
         STC   R2,*+5                        IN CLC
         CLC   SOURCE(*-*),2(R1)            DDNAMEN VERGLEICHEN
         BE    B25340                        JA, OK
         LA    R1,DDNAMESL(,R1)             NAECHSTER PLATZ
         BCT   R0,B25320                    WEITERSUCHEN
         B     B81100                       FALSCHES SCHLUESSELWORT
B25340   NULL
         MVI   LEVEL,0                      LEVEL=0
         LA    SREG,0(SREG,R2)              POINTER SOURCE
         MVC   OBJECT(16),BALWRITE          CODE FUER WRITE
         IC    R2,1(,R1)                    OFFSET NUMMER
         STH   R2,OBJECT+12                 IN CODE
         LA    R8,14                        LAENGE CODE
         B     B24180                       WEITER
         EJECT
B25400   NULL                               GOTO VERARBEITUNG
         OI    FLAG1,GOTOBIT                KEIN CODE MEHR
         MVI   LEVEL,1                      LEVEL=1
         LTSUB B30001                       ZEICHEN SUCHEN
         MVI   LEVEL,0                      LEVEL=0
         LTSUB B31100                       LABEL PRUEFEN
         FEHL  RC4=B81260,RC8=B81270
         MVC   OBJECT(4),BRANCH             BRANCH GERIPPE
         OI    OBJECT+2,BASRF               BR = 15
         LA    R1,LABNTAB                   LABELNAMEN
         LA    R0,LABNTABA                  ANZAHL
B25420   NULL
         CLC   VARNAME,0(R1)                NAME IN TABELLE
         BE    B25440                        JA
         CLC   BLANKS(12),0(R1)             PLATZ FREI
         BE    B25460                        JA
         LA    R1,14(,R1)                   NAECHSTER PLATZ
         BCT   R0,B25420                     WEITER
         B     B81450                       TABELLE VOLL
B25440   NULL
         CLC   12(2,R1),NULLS               ADRESSE LEER..
         BE    B25480                        JA,ADRESSE IN TAB
         OC    OBJECT+2(2),12(R1)           DISPLACEMENT IN OBJ
         B     B24170
B25460   NULL
         MVC   0(12,R1),VARNAME             LABELNAME IN TAB
B25480   NULL
         LA    R2,LABNTAB                   LABELNAMEN
         SR    R1,R2                        OFFSET
         LR    R2,R1
         LA    R1,LABATAB                   LABELADRESSEN
         LA    R0,LABATABA                  ANZAHL
B25500   NULL
         CLC   0(4,R1),NULLS                FREIER PLATZ
         BE    B25520                        JA
         LA    R1,4(,R1)                    NAECHSTER PLATZ
         BCT   R0,B25500                     WEITER
         B     B81450                       TABELLE VOLL
B25520   NULL
         STH   R2,0(,R1)                    OFFSET IN LABNTAB
         LR    R2,OREG                      OBJECT POINTER
         S     R2,BUFANF                    OFFSET
         STH   R2,2(,R1)
         B     B24170
         EJECT
B27001   NULL                               ENTRY PEND ROUTINE
B27800   NULL
         TEST  PEND                         IST PEND GUELTIG..
         OI    FLAG3,PENDBIT                PEND SCHALTER
         LTSUB B37400                       ELSE ADRESSEN EINSETZEN
         MVC   OBJECT(4),KWRET+10           RETURN-ADR
         LTSUB B37600                       OFFSET TABELLE
         LA    R8,4
         LTSUB B37200                       OBJ EINTRAG
         LA    SREG,5(,SREG)
         LA    R2,B27830
         ST    R2,PRTADR
         LA    R1,LABNTAB                   LABEL TABELLE
         LA    R0,LABNTABA                  ANZAHL
B27810   NULL
         CLC   0(12,R1),BLANKS              ENDE..
         BE    B27850
         CLC   12(2,R1),NULLS               WAR LABEL DA..
         BNE   B27840                        JA
         MVC   VARNAME,0(R1)                FEHLERHAFTER LABEL
         STM   R0,R1,WORK                   REGISTER SICHERN
         B     B81280                       FEHLER DRUCKEN
B27830   NULL
         LM    R0,R1,WORK                   REGISTER ZURUECK
B27840   NULL
         MVC   0(12,R1),BLANKS              TABELLE
         MVC   12(2,R1),NULLS                LOESCHEN
         LA    R1,14(,R1)                   NAECHSTER PLATZ
         BCT   R0,B27810                    WEITER
B27850   NULL
         XC    LABATAB(4*LABATABA),LABATAB  LOESCHEN
         LA    R2,B10120                    FOLGEADR PRINT
         ST    R2,PRTADR
         L     R1,STMTABA                   OFFSET TABLE
         S     R1,STMTABP                    ANZAHL STATEMENTS
         SRL   R1,1                           ERRECHNEN
         L     R2,BUFANF                       UND
         STH   R1,STMTSANZ-XDAU011(,R2)         SPEICHERN
         L     R2,RCODE                     RETURN-CODE
         CL    R2,F04
         BH    B27890                       NICHT DRUCKEN
         L     R2,PROCLEN                   PROC LAENGE
         S     R2,BUFEND                     - BUFEND
         A     R2,BUFADR                     + BUFADR = GESAMTLAENGE
         LA    R2,1(,R2)                    GESAMTLAENGE + 2
         SRL   R2,1                         AUF HALBWORT AUFRUNDEN
         SLL   R2,1
         CVD   R2,ZAHL                      DEZIMAL
         EJECT
B27860   NULL
         MVC   WORK(6),SBZAE                SCHABLONE
         ED    WORK(6),ZAHL+5               ED OBJECT LAENGE
         L     R2,OUTLEN                    LN NEW-AREA
         CVD   R2,ZAHL                      DEZIMAL
         MVC   WORK+6(6),SBZAE              SCHABLONE NACH WORK
         ED    WORK+6(6),ZAHL+5             ED NEW AREA LAENGE
         LA    RF,0                         MSG-NUMMER
         ST    RF,MSGPARM1
         LA    R1,WORK
         ST    R1,MSGPARM4
         LA    R1,WORK+6
         ST    R1,MSGPARM5
         LA    R1,MSGPARM                  PARM ADRESSE
         L     RF,XDAMSGS                   MSG UPRO
         BALR  RE,RF
         MVI   DB,C'0'                     VORSCHUB
         LTSUB B61001                       DRUCKEN
B27890   NULL
         LTSUB B61001                       DRUCKEN
         TM    FLAG1,EOFBIT                 EOF PROCESSING..
         BO    B10520
         B     B10020                       LESEN
         EJECT
***********************************************************************
**       DCL ROUTINE                                                 **
***********************************************************************
         SPACE 2
B28001   NULL                               ENTRY DCL-ROUTINE
         TEST  DCL                          IST DCL GUELTIG..
         LA    SREG,4(,SREG)                POINTER HINTER KENNWORT
         LA    R2,B28800                    ENDROUTINE
         ST    R2,ENDRT
B28010   NULL
         LTSUB B30001                       ZEICHEN SUCHEN
         LTSUB B31100                       VARIABLEN-NAMEN PRUEFEN
         FEHL  RC4=B81260,RC8=B81270
         L     R1,DCLANF                    ADRESSE TABELLEN ANFANG
         LTR   R1,R1                        TABELLE SCHON ANGELEGT
         BZ    B28030                        NEIN
         L     R0,DCLANZ                    ANZAHL TABELLENPLAETZE
B28020   NULL
         CLC   VARNAME,DCLNAME              IST VARIABLE SCHON DA
         BE    B81290                        JA, FEHLER
         LA    R1,DCLTABL(,R1)              NAECHSTER PLATZ
         BCT   R0,B28020                    WEITERSUCHEN
         CLC   DCLMAX,DCLANZ                IST NOCH PLATZ DA
         BH    B28100                        JA, EINSETZEN
B28030   NULL
         L     R7,DCLMAX
         LR    R9,R7                        LAENGE ALTER PLATZ
         MH    R9,DCLTLEN                   ANZAHL * LAENGE
         LA    R7,32(,R7)                   32 NEUE PLAETZE
         ST    R7,DCLMAX
         MH    R7,DCLTLEN                   ANZAHL * LAENGE
         LR    R0,R7
         GETMAIN R,LV=(0)                   PLATZ FUER VARTAB
         L     R8,DCLANF                    ANFANGS-ADR ALT
         LA    R1,0(,R1)                    HOB LOESCHEN
         ST    R1,DCLANF                    ANFANGS-ADR NEU
         LTR   R8,R8                        1. GETMAIN
         BZ    B28100                        JA, KEIN MOVE
         LR    R6,R1                        ADRESSE NEUER PLATZ
         LR    R1,R8                        ADRESSE ALT
         L     R2,DCLANZ                    ANZAHL PLAETZE
         MH    R2,DCLTLEN                    * LAENGE
         LR    R0,R2                        LAENGE ALT
         MVCL  R6,R8                        MOVE INHALT AUF NEUEN PLATZ
         FREEMAIN R,LV=(0),A=(1)            ALTEN PLATZ FREIGEBEN
B28090   NULL
         L     R1,DCLANF                    ANFANG TABELLE
         LA    R1,0(R1,R2)                  AKTUELLE ADRESSE NEU
         EJECT
B28100   NULL
         ST    R1,DCLAKT                    POINTER SICHERN
         MVC   DCLNAME,VARNAME              NAME NACH TABELLE
         MVI   LEVEL,2                      ENTSCHL.-LEVEL
         LTSUB B30001                       ZEICHEN SUCHEN
         MVI   TYP1,CTYP                    DATENTYP = C
         LTSUB B30200                       FELD PRUEFEN
         MVI   LEVEL,0
         L     R1,DCLANZ                    ANZAHL PLAETZE
         LA    R1,1(,R1)                     + 1
         ST    R1,DCLANZ                      SICHERN
         L     R1,DCLAKT                    POINTER LADEN
         MVC   DCLCODE,CODE2                ATTRIBUTE
         B     B28010
B28800   NULL                               SCHLUSS-ROUTINE
         CLI   LEVEL,NULL                   STMT ABGESCHLOSSEN
         BNE   B81040                        NEIN FEHLER
         B     B10120
         EJECT
***********************************************************************
**       COPY ROUTINE                                                **
***********************************************************************
         SPACE 2
B29001   NULL                               ENTRY COPY ROUTINE
         LA    R1,B29800                    ADRESSE NACH ERROR
         ST    R1,PRTADR
         TM    COPYFLG,COPYON               INNERES COPY
         BO    B81300                        NICHT ERLAUBT
         LR    R1,SREG                      SOURCE POINTER
         LA    R2,EB                        ADRESSE EB
         SR    R1,R2                        OFFSET IN EB
         LA    R1,COPYEB(R1)                OFFSET IN COPYEB
         MVC   COPYEB,EB                    EB NACH COPYEB
         LA    R1,5(,R1)                    POINTER HINTER SW
B29010   NULL
         CLI   0(R1),BLANK                  NAECHSTES
         BNE   B29020                        ZEICHEN
         LA    R1,1(,R1)                      SUCHEN
         B     B29010
B29020   NULL
         CLI   0(R1),COMMENT1               KOMMENTAR..
         BE    B29800                       COPY RESET
         LR    R2,R1                        ANFANG SICHERN
B29030   NULL
         CLI   0(R1),BLANK                  DELIMITER
         BE    B29040                        SUCHEN
         CLI   0(R1),KOMMA                  DELIMITER
         BE    B29040                        SUCHEN
         CLI   0(R1),COMMENT1               DELIMITER
         BE    B29040                        SUCHEN
         LA    R1,1(,R1)                    POINTER + 1
         B     B29030
         EJECT
B29040   NULL
         SR    R1,R2                        LAENGE ERMITTELN
         BZ    B81310                       SYNTAXFEHLER
         C     R1,F08                       LAENGE > 8
         BH    B81310
         BCTR  R1,0                         LAENGE - 1
         MVC   COPYMBR,BLANKS               LOESCHEN
         STC   R1,*+5                       NACH MVC
         MVC   COPYMBR(*-*),0(R2)           MEMBERNAME
         OI    COPYFLG,COPYON               COPY AKTIV
         LA    R1,1(R1,R2)                  COPY SOURCE POINTER
         ST    R1,COPYSPTR                  SICHERN
         TM    COPYFLG,LIBERROR             ERROR IN SYSLIB..
         BO    B29800
         TM    COPYFLG,LIBOPEN              SYSLIB OPEN..
         BO    B29100                       JA, FIND MEMBER
         OPEN  (SYSLIB,INPUT)               OPEN SYSLIB DATEI
         OI    COPYFLG,LIBERROR             JETZT KANN EIN ERROR KOMMEN
         TM    SYSLIB+48,X'10'              OPEN ERFOLGREICH
         BZ    B81320                        NEIN
         OI    COPYFLG,LIBOPEN              SYSLIB EROEFFNET
         TM    SYSLIB+36,B'11000000'        RECFM=U
         BO    B81330                        NICHT ERLAUBT
         TM    SYSLIB+36,B'10000000'        RECFM=F
         BZ    B81330                        NEIN
         CLC   SYSLIB+82(2),=X'0050'        LRECL=80
         BNE   B81330                        NEIN
         LH    R0,SYSLIB+62                 BLKSIZE
         GETMAIN R,LV=(0)                   PLATZ FUER WORKAREA
         LA    R1,0(,R1)
         ST    R1,COPYWRKA                  ADR COPY WORK AREA
         NI    COPYFLG,255-LIBERROR         ERROR AUS
         SPACE
B29100   NULL
         FIND  SYSLIB,COPYMBR,D             MEMBER IN PDS
         LTR   RF,RF
         BP    B81340                       NICHT GEFUNGEN
         B     B10150                       MEMBER LESEN
B29700   NULL                               EOF MEMBER
         NI    COPYFLG,255-COPYCONT         FORTSETZUNG AUS
         LA    R1,B29800                    ADRESSE NACH ERROR
         ST    R1,PRTADR
         L     R2,COPYSPTR                  SOURCE POINTER
         LA    R1,1(,R2)                    NEUER OFFSET
         CLI   0(R2),KOMMA                  FORTSETZUNG..
         BE    B29020                        JA
B29800   NULL
         TM    COPYFLG,COPYCONT             WEITERPRUEFEN..
         BO    B29700                        JA
         NI    COPYFLG,255-COPYON           COPY INAKTIV
         B     B10020                       SYSIN WEITERLESEN
         EJECT
B30001   SOSUB                              ZEICHEN SUCHEN
         LA    RE,1                         INDEX
         LA    RF,EB+71                     KARTENENDE
B30010   NULL
         CLI   SOURCE,BLANK                 LEERZEICHEN
         BNE   B30020                        NEIN
         BXLE  SREG,RE,B30010               NAECHSTES ZEICHEN
         B     B30030                        ENDE DER EINGABE
B30020   NULL
         CLI   SOURCE,COMMENT1              KOMMENTARANFANG..
         BNER  SUBREG                        NEIN
         CLI   SOURCE+1,COMMENT2            KOMMENTARANFANG
         BNER  SUBREG                        NEIN, MULT-ZEICHEN
B30030   NULL
         ST    SUBREG,FOLGE                 FOLGE-ADR
         B     B10020                       NEU LESEN
         EOSUB CODE=NO
         SPACE 3
B30100   NULL
         SR    RF,RF                        CODE = 0
         BR    SUBREG                       RETURN
B30104   NULL
         LA    RF,4                         CODE
         B     B30190                       EXIT
B30108   NULL
         LA    RF,8                         CODE
         B     B30190                       EXIT
B30112   NULL
         LA    RF,12                        CODE
         B     B30190                       EXIT
B30116   NULL
         LA    RF,16                        CODE
         B     B30190                       EXIT
B30190   NULL
         L     SREG,SAVER3                  SREG ZURUECK
         BR    SUBREG                       RETURN
         EJECT
B30200   SOSUB
         CLI   SOURCE,C'0'                  ZIFFER..
         BNL   B30210
         CLI   SOURCE,ALPHA                 APHA..
         BNL   B30220
         CLI   SOURCE,SYSVAR                DOLLAR-ZEICHEN..
         BE    B30220
         CLI   SOURCE,PLUS                  VORZEICHEN POSITIV..
         BE    B30210
         CLI   SOURCE,MINUS                 VORZEICHEN NEGATIV
         BE    B30210
         MVI   DTYP2,CTYP                   TYP IST CHARACTER
         CLI   SOURCE,QUOTE                 HOCHKOMMA
         BNE   B81240                       UNG. SONDERZEICHEN
         B     B30230
B30210   NULL
         LTSUB B36001                       NUMERISCHE KONSTANTE
         FEHL  RC4=B81250,RC8=B81150
         MVI   DTYP2,NTYP                   TYP IST NUMERISCH
         MVI   TYP2,PTYP                    TYP NACH CODE2
         B     B30260
B30220   NULL
         CLI   SOURCE+1,QUOTE               CHARACTER KONSTANTE..
         BE    B30230
         LTSUB B32001                       WORT PRUEFEN
         FEHL  RC4=B81260,RC8=B81120,RC12=B81130,RC16=B81130
         B     B30290
B30230   NULL
         LTSUB B34001                       CHAR-ROUTINE
         FEHL  RC4=B81240,RC8=B81160,RC12=B81150,RC16=B81170
         CLI   LEVEL,2                      LEVEL = 2
         BE    B30260                        JA, ERSTER OPERAND
         LA    R2,1
         AH    R2,LEN1                      LAENGE ERMITTELN
         TM    STMT,SWIF                    IF STATEMENT..
         BO    B30250                       LEN1 WIRD GENOMMEN
         CH    R2,WORK-2                    LN KONSTANTE
         BNL   B30260
B30250   NULL
         STH   R2,WORK-2
B30260   NULL
         LTSUB B37001                       WORK NACH OBJ
B30290   NULL
         EOSUB                              RETURN
         EJECT
B30400   SOSUB                             STACK-ROUTINE
         L     R2,STACKPTR                 STACK OFFSET POINTER
B30410   NULL
         LA    R1,STACK-STACKL+STACKL*STACKA LETZTER PLATZ IM STACK
         CLR   R2,R1                        VERGLEICHEN
         BNLR  SUBREG                       EXIT
         CLC   PARLEV+2(2),STCKKLVL+STACKL  KLAMMERLEVEL VERGLEICHEN
         BHR   SUBREG                       EXIT, WENN KL GROESSER
         CLC   STCKOLVL,STCKOLVL+STACKL     OPERATOR PRIORITAET
         BHR   SUBREG
B30420   NULL                              STACK-ROUTINE
         LA    RE,STCKCODE+STACKL           ADR. 1. OPERAND
         LA    RF,STCKCODE                  ADR. 2. OPERAND
         ST    OREG,SAVEOREG                OBJECT POINTER SICHERN
         STM   RE,RF,STCKREGS               STACK POINTER SICHERN
         CLI   5(RE),PTYP                   TYP-1 = P..
         BNE   B30510                        NEIN
         CLI   1(RE),X'0F'                  IST DIES EIN ZWISCHENFELD
         BE    B30550                        JA, KEINE KONVERTIERUNG
B30510   NULL
         LA    R1,SCODE1                    ADRESSE SCODE
         AH    R1,STCKSOFF                  STACKOFFSET
         MVC   CODE2,0(RE)                  FELD 1
         MVC   0(10,RE),0(R1)               E-FELD
         LA    RF,CODE2                     ADR S-FELD
         CLI   5(RF),PTYP                   TYP-1 = P..
         BNE   B30520                        NEIN
         MVI   OBJECT,ZAPCODE               CODE FUER ZAP
         LTSUB B30950                       CODE GENERIEREN
         B     B30550
         SPACE
B30520   NULL
         CLI   5(RF),NTYP                   TYP-1 = N..
         BNE   B30530                        NEIN
         MVI   OBJECT,PACKCODE              CODE FUER PACK
         LTSUB B30950                       CODE GENERIEREN
         B     B30550
         SPACE
B30530   NULL
         LTSUB B30960                       CODE GENERIEREN
         MVI   OBJECT,ZAPCODE               ZAP
         MVI   OBJECT+1,X'F7'                AUF
         MVC   OBJECT+2(2),2(RE)              LAENGE 16
         OC    OBJECT+2(1),4(RE)
         MVC   OBJECT+4(2),OBJECT+2
         LA    OREG,6(,OREG)                POINTER + 6
         EJECT
B30550   NULL
         L     RF,STCKREGS+4                ADR FELD2 AUS STACK
         LA    RE,ZCODE2                    ADR E-FELD
         CLI   5(RF),PTYP                   TYP-2 = P..
         BNE   B30570                        NEIN
         CLI   1(RF),X'0F'                  IST DIES EIN ZWISCHENFELD
         BNE   B30600                        NEIN
         MVI   OBJECT,ZAPCODE               CODE FUER ZAP
         LTSUB B30950                       CODE GENERIEREN
         B     B30590
         SPACE
B30570   NULL
         CLI   5(RF),NTYP                   TYP-2 = N..
         BNE   B30580                        NEIN
         MVI   OBJECT,PACKCODE              CODE FUER PACK
         LTSUB B30950                       CODE GENERIEREN
         B     B30590
         SPACE
B30580   NULL
         LTSUB B30960                       CODE GENERIEREN
         SPACE
B30590   NULL
         MVC   0(10,RF),0(RE)
         EJECT
B30600   NULL
         LM    RE,RF,STCKREGS              STACK REGISTER ZURUECKLADEN
         CLI   11(RE),X'02'                 MULT ODER DIV..
         BE    B30620
         CLI   10(RE),PLUS                  OPERATOR = PLUS
         BNE   B30610
         MVI   OBJECT,APCODE                CODE FUER AP
         B     B30630
B30610   NULL
         MVI   OBJECT,SPCODE                CODE FUER SP
         B     B30630
B30620   NULL
         CLI   10(RE),MAL
         BNE   B30640
         MVI   OBJECT,MPCODE                CODE FUER MP
B30630   NULL
         LTSUB B30950                       CODE GENERIEREN
         B     B30650
B30640   NULL
         MVI   OBJECT,DPCODE                CODE FUER DP
         LTSUB B30950                       CODE GENERIEREN
         MVI   OBJECT,ZAPCODE               CODE FUER ZAP
         LH    R1,0(,RE)                    LAENGE E-FELD
         SH    R1,0(,RF)                     - LAENGE S-FELD
         CLI   10(RE),GETEILT               WERT NACH E-FELD
         BE    B30644
         MVC   OBJECT+1(1),1(RF)            LAENGE VON REST
         AH    R1,2(,RE)                    DISPLACEMENT
         STH   R1,OBJECT+4
         B     B30646                       RESTCODE
B30644   NULL
         BCTR  R1,0                             - 1
         STC   R1,OBJECT+1                  = LAENGE S-FELD
         MVC   OBJECT+4(2),2(RE)            DISPLACEMENT
B30646   NULL
         PACK  WORK(1),1(1,RE)              LAENGE E-FELD
         OC    OBJECT+1(1),WORK             LAENGE E-FELD
         MVC   OBJECT+2(2),2(RE)            DISPLACEMENT
         OC    OBJECT+2(1),4(RE)            BASISREGISTER
         OC    OBJECT+4(1),4(RE)            BASIS Z-FELD
         LA    OREG,6(,OREG)                OBJECT + 6
B30650   NULL
         LR    R8,OREG                      LAENGE
         L     OREG,SAVEOREG                 DER INSTRUKTIONEN
         SR    R8,OREG                        ERRECHNEN
         EJECT
         TM    FLAG3,DEBBIT                 DEBUG AKTIV..
         BZ    B30690                        NEIN
         LTSUB B37800                       PRINT DEBUG INFO
B30690   NULL
         LTSUB B37200                       PUFFERADRESSE
         L     R2,STACKPTR                  STACK POINTER
         MVC   STCKOPER+STACKL(2),STCKOPER
         MVC   STCKKLVL+STACKL(2),PARLEV+2
         XC    STCKCODE(STACKCL),STCKCODE
         LA    R2,STACKL(,R2)
         ST    R2,STACKPTR
         B     B30410
         EOSUB CODE=NO
         EJECT
B30800   SOSUB                              ARITHMETISCHE ZUWEISUNGEN
         ST    OREG,SAVEOREG                OBJECT POINTER SICHERN
         MVC   CODE2,STACK-STACKL+STACKL*STACKA
         LA    RE,CODE2                     ADRESSE EMPFANGSFELD
         LA    RF,CODE2                     ADRESSE SENDEFELD
         CLI   TYP2,PTYP                    TYP = P..
         BNE   B30820
         CLI   TYP1,PTYP                    TYP1 AUCH GEPACKT..
         BE    B30850
         CLI   BAS2,BASR1                   OLD-FELD
         BE    B30810
         CLI   BAS2,BASR2                   NEW-FELD
         BE    B30810
         CLI   TYP1,ITYP                    WENN E-FELD = I
         BE    B30810                        DANN ZAP IN Z-FELD
         CLI   LEN2+1,X'0F'                 ZWISCHENFELD
         BNE   B30850
         LA    R1,7                         LAENGE 8
         STH   R1,LEN2
         LH    R1,ADR2                      ADRESSE + 8
         LA    R1,8(,R1)
         STH   R1,ADR2
         B     B30850
B30810   NULL
         MVI   OBJECT,ZAPCODE               ZAP-CODE
         LA    RE,ZCODE1
         LTSUB B30950                       CODE GENERIEREN
         LA    RF,ZCODE1                    ADR S-FELD
         B     B30850
B30820   NULL
         CLI   TYP2,NTYP                    NUMERISCH..
         BNE   B30830
         MVI   OBJECT,PACKCODE              PACK-CODE
         LA    RE,ZCODE1
         LA    RF,CODE2
         LTSUB B30950
         MVI   OBJECT,ZAPCODE               ZAP-CODE
         LA    RF,ZCODE1
         CLI   TYP1,NTYP
         BNE   B30850
         LA    RE,ZCODE1
         LTSUB B30950
         B     B30850
         EJECT
B30830   NULL
         CLI   TYP1,ITYP                    INTEGER..
         BE    B30840
         LA    RE,ZCODE1
         LA    RF,CODE2                     L + CVD
         LTSUB B30960                        GENERIEREN
         LA    RF,ZCODE1
         B     B30850
B30840   NULL
         LA    RE,CODE1
         LA    RF,CODE2                     L
         LTSUB B30960                        GENERIEREN
         SPACE
B30850   NULL
         CLI   TYP1,NTYP                    NUMERISCH..
         BE    B30860
         CLI   TYP1,ITYP                    INTEGER..
         BE    B30880
         MVI   OBJECT,ZAPCODE               ZAP-CODE
         LA    RE,CODE1                     ADR-CODE1
         LTSUB B30950                       CODE GENERIEREN
         B     B30890
B30860   NULL
         MVI   OBJECT,UNPKCODE              UNPK-CODE
         LA    RE,CODE1
         LTSUB B30950
         MVC   OBJECT(10),NEGTEST           CODE
*        EIN NEGATIVER WERT ERHAELT         TM    VORZEI,X'10'
*        EIN 'D'-VORZEICHEN, EIN POSITIVER  BO    *+8
*        WERT EIN 'F' VORZEICHEN            OI    VORZEI,X'F0'
         LH    R2,0(,RE)                    LAENGE
         AH    R2,2(,RE)                    DISPL.
         STH   R2,OBJECT+2                  ADR IN TM
         OC    OBJECT+2(1),4(RE)            BASIS
         STH   R2,OBJECT+10                 ADR IN OI
         OC    OBJECT+10(1),4(RE)           BASIS
         LA    R2,12(,OREG)                 BRANCH ADR
         S     R2,BUFANF                     ERMITTELN
         STH   R2,OBJECT+6                  EINSETZEN
         OI    OBJECT+6,BASRF               BASIS
         LA    OREG,12(,OREG)               POINTER + 12
         B     B30890
B30880   NULL
         LA    RE,CODE1                     ADR CODE1
         LTSUB B30980                       COMPILIEREN
B30890   NULL
         LR    R8,OREG                      OBJ-POINTER
         L     OREG,SAVEOREG                SICHERUNG
         SR    R8,OREG
         TM    FLAG3,DEBBIT                 DEBUG AKTIV..
         BZ    B30895                        NEIN
         LTSUB B37800                       PRINT DEBUG INFO
B30895   NULL
         LTSUB B37200                       PUFFER ADR
         EOSUB                              RETURN
         EJECT
B30950   SOSUB                              CODE FUER SS-FORMAT
         PACK  OBJECT+1(1),1(1,RE)          LAENGE NACH
         OC    OBJECT+1(1),1(RF)            LAENGE VON
         MVC   OBJECT+2(2),2(RE)            DISPL.
         OC    OBJECT+2(1),4(RE)            BASIS
         MVC   OBJECT+4(2),2(RF)            DISPL.
         OC    OBJECT+4(1),4(RF)            BASIS
         LA    OREG,6(,OREG)                OREG + LAENGE
         EOSUB                              RETURN
         SPACE
B30960   SOSUB                              CONVERTIEREN I-FORMAT
         MVI   OBJECT,LHCODE                LH-CODE
         CLI   1(RF),X'01'                  S-FELD = HALBWORT..
         BE    B30970
         OI    OBJECT,X'10'                 LH WIRD L
B30970   NULL
         MVC   OBJECT+2(2),2(RF)            DISPL.
         OC    OBJECT+2(1),4(RF)            BASIS
         LA    OREG,4(,OREG)                OBJECT POINTER + 4
         CLI   5(RE),ITYP                   EMPFANGSFELD = I
         BER   SUBREG
         MVI   OBJECT,CVDCODE               CVD-CODE
         MVC   OBJECT+2(2),2(RE)            DISPL.
         OC    OBJECT+2(1),4(RE)            BASIS
         LA    OREG,4(,OREG)                OBJECT POINTER + 4
         EOSUB
         SPACE
B30980   SOSUB
         CLI   5(RF),ITYP                   SENDEFELD = I
         BE    B30990
         MVI   OBJECT,CVBCODE               CVB-CODE
         MVC   OBJECT+2(2),2(RF)            DISPL.
         OC    OBJECT+2(1),4(RF)            BASIS
         LA    OREG,4(,OREG)                OBJECT POINTER + 4
B30990   NULL
         MVI   OBJECT,STHCODE               STH-CODE
         CLI   1(RE),X'01'                  EMPFANGSFELD = HALBWORT..
         BE    B30995                        JA
         OI    OBJECT,X'10'                 STH WIRD ST
B30995   NULL
         MVC   OBJECT+2(2),2(RE)            DISPL.
         OC    OBJECT+2(1),4(RE)            BASIS
         LA    OREG,4(,OREG)                OBJECT POINTER + 4
         EOSUB
         EJECT
B31001   SOSUB                              SCHLUESSELWORT PRUEFEN
         ST    SREG,SAVER3                  POINTER SICHERN
         LA    R1,KWTAB                     KEYWORD-TABELLE
         LA    R0,KWTABA                     ANZAHL
         SR    R2,R2                        LOESCHEN
B31010   NULL
         IC    R2,0(,R1)                    LAENGE
         BCTR  R2,0                          - 1
         STC   R2,*+5                        NACH CLC
         CLC   SOURCE(*-*),1(R1)            KW VERGLEICHEN
         BE    B31020                        GEFUNDEN
         LA    R1,14(,R1)                   NAECHSTER PLATZ
         BCT   R0,B31010                    NEUER VERSUCH
         B     B30104                       NICHT GEFUNDEN
B31020   NULL
         CLC   SOURCE(8),KWCONT+1           CONTINUE..
         BNE   B31040
         OI    FLAG3,CONTBIT                CONTINUE GESETZT
B31040   NULL
         LA    SREG,1(SREG,R2)              POINTER ERHOEHEN
         CLI   SOURCE,ALPHA-1               ALPHA..
         BH    B30104                        JA, FEHLER
         CLI   SOURCE,LABPTR                LABEL..
         BE    B30104                        JA, KEIN KENNWORT
         MVC   OBJECT(4),10(R1)              BRANCH-ADRESSE
         SR    RF,RF                        RC
         EOSUB
         SPACE 2
B31100   SOSUB                              VARIABLEN-NAMEN PRUEFEN
         ST    SREG,SAVER3                  POINTER SICHERN
         LA    R0,13                        LAENGE
         LR    R1,SREG                      SOURCE POINTER
B31110   NULL
         CLI   SOURCE,ALPHA                 ALPHA-ZEICHEN
         BL    B31120                        NEIN ENDE
         LA    SREG,1(,SREG)                POINTER + 1
         BCT   R0,B31110
B31120   NULL
         SR    SREG,R1                      LAENGE ERMITTELN
         BZ    B30108                        =0, FEHLER
         MVC   VARNAME(12),BLANKS           LOESCHEN
         BCTR  SREG,0                       LAENGE - 1
         STC   SREG,*+5
         MVC   VARNAME(*-*),0(R1)           NAME NACH WORK
         CLI   0(R1),C'Z'                   1. ZEICHEN ALPHA
         BH    B30108                        NEIN, FEHLER
         LTR   R0,R0
         BZ    B30104                       NAME ZU LANG
         LA    SREG,1(SREG,R1)              SREG HINTER VARIABLE
         SR    RF,RF                        RC
         CLI   SOURCE,LABPTR                IST DIES EIN LABEL..
         BE    B31200                        JA
         BR    SUBREG
         EJECT
***********************************************************************
**       LABEL PRUEFEN                                               **
***********************************************************************
         SPACE 2
B31200   NULL
         TM    FLAG1,LABBIT                 IST EIN LABEL MOEGLICH
         BZ    B81240                        NEIN, FEHLER
         LA    R1,LABNTAB                   LABELNAMEN
         LA    R0,LABNTABA                  ANZAHL
B31220   NULL
         CLC   VARNAME,0(R1)                NAME IN TABELLE
         BE    B31240                        JA
         CLC   BLANKS(12),0(R1)             PLATZ FREI
         BE    B31260                        JA
         LA    R1,14(,R1)                   NAECHSTER PLATZ
         BCT   R0,B31220                     WEITER
         B     B81450                       KEIN PLATZ MEHR
B31240   NULL
         CLC   12(2,R1),NULLS               ADRESSE LEER..
         BNE   B81290                       LABEL DOPPELT
         B     B31280
B31260   NULL
         MVC   0(12,R1),VARNAME             LABELNAME IN TAB
B31280   NULL
         LR    R2,OREG                      OBJECT POINTER
         S     R2,BUFANF                    OFFSET ERRECHNEN
         STH   R2,12(,R1)                   IN TABELLE
         LR    R5,R1                        SICHERN
         LR    R2,R1
         LA    R1,LABNTAB                   NAMENSTABELLE
         SR    R2,R1                        OFFSET
         LA    R1,LABATAB                   ADRESS TABELLE
         LA    R0,LABATABA                  ANZAHL
B31300   NULL
         CLC   0(4,R1),NULLS                PLATZ LEER..
         BE    B31320                        JA
         CH    R2,0(,R1)                    RICHTIGER LABEL
         BNE   B31320
         L     R8,BUFANF                    ANFANG
         AH    R8,2(,R1)                     + DISPLACEMENT
         OC    2(2,R8),12(R5)               DISPLACEMENT IN BRANCH
         MVC   0(4,R1),NULLS                ENTRY LOESCHEN
B31320   NULL
         LA    R1,4(,R1)                    NAECHSTER PLATZ
         BCT   R0,B31300
         EOSUB
         EJECT
B32001   SOSUB                              WORT PRUEFEN
         ST    SREG,SAVER3                  POINTER SICHERN
         XC    CODE2+4(6),CODE2+4           CODE2 LOESCHEN
         CLI   SOURCE,SYSVAR                SYSTEM-VARIABLE..
         BE    B32200
         CLC   SOURCE(4),=C'NEW.'           AUSGABE..
         BE    B32010                        JA
         CLC   SOURCE(4),=C'OLD.'           EINGABE..
         BNE   B32040                        NEIN
         MVI   BAS2,BASR1                   BASIS = 1
         B     B32020
B32010   NULL
         OI    FLAG3,NEWBIT                 NEW-AREA ANLEGEN
         MVI   BAS2,BASR2                   BASIS = 2
B32020   NULL
         LA    SREG,4(,SREG)                POINTER + 4
B32040   NULL
         CLI   SOURCE+1,C'-'                TRENNSTRICH
         BNE   B32500                        NEIN, VARIABLE
         MVI   DTYP2,CTYP                   CHARACTER TYP
         CLI   SOURCE,CTYP                  CHARAKTER
         BE    B32050                        JA
         CLI   SOURCE,XTYP                  HEXA
         BE    B32050
         MVI   DTYP2,NTYP                   NUMERISCHER TYP
         CLI   SOURCE,PTYP                  GEPACKT
         BE    B32050                        JA
         CLI   SOURCE,NTYP                  NUMERISCH..
         BE    B32050
         CLI   SOURCE,ITYP                  INTEGER..
         BNE   B32500                       VARIABLEN-NAME
B32050   NULL
         CLI   BAS2,NULL                    BASISREGISTER FESTGELEGT
         BNE   B32055                        JA
         MVI   BAS2,BASR1                   DEFAULT = OLD
B32055   NULL
         MVC   TYP2,SOURCE                  TYP SPEICHERN
         LA    SREG,2(,SREG)                POINTER + 2
         EJECT
B32060   NULL
         CLI   STMT,SWDCL                   DCL-STMT
         BNE   B32070
         CLI   SOURCE,STERN                 DISPL=*
         BNE   B32070
         LA    SREG,1(,SREG)                POINTER + 1
         LH    R2,ADR2                      DISPLACEMENT ALT
         AH    R2,LEN2                       + LAENGE ALT
         LA    R2,2(,R2)                     + 2
         B     B32080
B32070   NULL
         LTSUB B36500                       ZAHL PRUEFEN
         FEHL  RC4=B30108,RC8=B30116         FEHLER PRUEFEN
         CVB   R2,ZAHL                      ZAHL BINAER
         LTR   R2,R2                        ZAHL = 0
         BZ    B30112                       WERT = 0
B32080   NULL
         CL    R2,MAXPOS                    MAXIMAL - DISPLACEMENT
         BH    B30112                        ZU GROSS
         BCTR  R2,0                         DISPL. - 1
         STH   R2,ADR2                      NACH CODE
         CLI   SOURCE,C'-'                  TRENNSTRICH
         BE    B32100                        JA
         CLI   SOURCE,C':'                  BIS ANZEIGE..
         BNE   B30108                       NICHT NUMERISCH
         OI    FLAG1,BISBIT
         EJECT
B32100   NULL
         LA    SREG,1(,SREG)                POINTER + 1
         LTSUB B36500                       ZAHL PRUEFEN
         FEHL  RC4=B30108,RC8=B30116        LAENGE FALSCH
         CVB   R2,ZAHL                      ZAHL BINAER
         TM    FLAG1,BISBIT
         BZ    B32105
         NI    FLAG1,255-BISBIT             BIT AUS
         SH    R2,ADR2                      LAENGE ERRECHNEN
B32105   NULL
         LTR   R2,R2                        ZAHL = 0
         BNP   B30112                        JA, FEHLER
         CLI   DTYP2,CTYP
         BNE   B32120
B32110   NULL
         CL    R2,MAXLNC
         BH    B30112
         B     B32150
B32120   NULL
         CLI   TYP2,PTYP
         BNE   B32130
         CL    R2,MAXLNP
         BH    B30112
         B     B32150
B32130   NULL
         CLI   TYP2,NTYP
         BNE   B32140
         CL    R2,MAXLNN
         BH    B30112
         B     B32150
B32140   NULL
         CL    R2,MAXLNI
         BE    B32150
         CL    R2,MAXLNI2
         BNE   B30112
B32150   NULL
         BCTR  R2,0                         R2 - 1
         STH   R2,LEN2                      NACH LAENGE
B32160   NULL
         AH    R2,ADR2                      DISPLACEMENT
         LA    R2,1(,R2)                    R2 + 1
         CL    R2,MAXPOS                    MAX DISPLACEMENT
         BH    B30112                        UEBERSCHRITTEN
         SR    RF,RF                        RC
         CLI   BAS2,BASR2                   NEW-AREA..
         BNER  SUBREG                        NEIN
         CL    R2,OUTLEN                    LAENGE OUTPUT AREA
         BNHR  SUBREG                        NICHT GROESSER
         ST    R2,OUTLEN                    SPEICHERN
         B     B32590
         EJECT
B32200   NULL                               SYSTEM-VARIABLE PRUEFEN
         LA    SREG,1(,SREG)                SREG + 1
B32210   NULL
         LA    R1,SYSVARS
         LA    R0,SYSVANZ                   ANZAHL
         SR    R2,R2                        LOESCHEN
B32220   NULL
         IC    R2,7(,R1)                    LAENGE TEXT
         STC   R2,*+5                       NACH CLC
         CLC   SOURCE(*-*),0(R1)            SYSVAR..
         BE    B32230
         LA    R1,SYSVALN(,R1)              NAECHSTE
         BCT   R0,B32220
         B     B30104                       FEHLER
B32230   NULL
         LA    SREG,1(SREG,R2)              HINTER VAR
         CLI   SOURCE,ALPHA-1               ALPHA
         BH    B30104                        JA, LEIDER
         IC    R2,9(,R1)                    LAENGE VAR
         STH   R2,LEN2
         LH    R2,10(,R1)                   DISPL
         STH   R2,ADR2
         MVC   TYP2,8(R1)                   DATENTYP
         MVI   BAS2,BASR5                   BASIS-REG
         MVI   DTYP2,NTYP                   DATENTYP = N
         SR    RF,RF                        RC
         CLI   TYP2,CTYP
         BE    B32240
         CLI   TYP2,XTYP
         BNER  SUBREG
B32240   NULL
         MVI   DTYP2,CTYP
         BR    SUBREG
         EJECT
B32500   NULL                               VARIABLEN-NAMEN SUCHEN
         LTSUB B31100                       NAMEN PRUEFEN
         FEHL  RC4=B81260,RC8=B81270
         CLI   SOURCE,LABPTR                IST DIES EIN LABEL..
         BER   SUBREG
         L     R1,DCLANF                    ANFANG TABELLE
         LTR   R1,R1                        TABELLE ANGELEGT
         BZ    B81280                        NEIN
         L     R0,DCLANZ                    ANZAHL
B32510   NULL
         CLC   VARNAME,DCLNAME              VARNAMEN IN TAB SUCHEN
         BE    B32520                        GEFUNDEN
         LA    R1,DCLTABL(,R1)              NAECHSTER PLATZ
         BCT   R0,B32510
         B     B81280                       NICHT GEFUNGEN
         SPACE
B32520   NULL
         CLI   DCLBASIS,BASR5               SYS-VAR
         BE    B32530
         CLI   BAS2,NULL                    BASISREGISTER DA..
         BE    B32530                        NEIN
         MVC   CODE2(4),DCLLEN              LAENGE/DISPL
         MVC   TYP2,DCLTYP                  DATENTYP
         B     B32540
B32530   NULL
         MVC   CODE2(6),DCLLEN              LAENGE/DISPL
B32540   NULL
         MVI   DTYP2,CTYP                   CHARACTER TYP
         CLI   TYP2,CTYP                    T = C
         BE    B32545
         CLI   TYP2,XTYP                    T = X
         BE    B32545
         MVI   DTYP2,NTYP                   NUMERISCHER TYP
B32545   NULL
         LH    R2,LEN2
         CLI   SOURCE,LPAR                  KLAMMER AUF..
         BNE   B32160                        NEIN, KEIN SUBSTR
         CLI   DTYP2,CTYP                   TYP=C..
         BE    B32550                       SUBSTR MOEGLICH
         CLI   TYP2,NTYP                    NTYP KANN SUBSTR HABEN
         BNE   B32160                       SUBSTR GEHT NICHT
         EJECT
B32550   NULL
         LA    SREG,1(,SREG)                SOURCE PTR + 1
         LTSUB B36500                       ZAHL PRUEFEN
         FEHL  RC4=B81120,RC8=B81150
         CVB   R2,ZAHL                      ZAHL BINAER
         LTR   R2,R2                        ZAHL = 0
         BZ    B81130                        JA, FEHLER
         BCTR  R2,0                          - 1
         ST    R2,IDISPL                    DISPLACEMENT
         CLI   SOURCE,KOMMA                 TRENNNZEICHEN = KOMMA
         BNE   B81270                        NEIN, FEHLER
         LA    SREG,1(,SREG)                SOURCE PTR + 1
         LTSUB B36500                       ZAHL PRUEFEN
         FEHL  RC4=B81120,RC8=B81150
         CVB   R2,ZAHL                      ZAHL BINAER
         LTR   R2,R2                        ZAHL = 0
         BZ    B81130                        JA, FEHLER
         CLI   SOURCE,RPAR                  KLAMMER ZU
         BNE   B81270
         LA    SREG,1(,SREG)                SOURCE PTR + 1
         BCTR  R2,0                         R2 - 1
         LH    R1,ADR2                      DISPLACEMENT
         AH    R1,LEN2                       + LAENGE
         STH   R2,LEN2                       NEUE LAENGE SPEICHERN
         A     R2,IDISPL                     + DISPLACEMENT
         AH    R2,ADR2                       + OFFSET
         CLR   R2,R1                        SUBSTR FALSCH..
         BH    B81130                        JA
         LH    R2,ADR2                      DISPLACEMENT AUFBAUEN
         A     R2,IDISPL
         STH   R2,ADR2                      UND SPEICHERN
         LH    R2,LEN2
         B     B32160
B32590   NULL
         EOSUB
         EJECT
B34001   SOSUB                              CHAR-STRING UEBERNEHMEN
         MVC   WORKLN,MAXLNC                MAXIMAL-LAENGE
         LA    R1,TYP1                      DATENTYP
         CLI   SOURCE,QUOTE                 FEHLT DER DATENTYP..
         BE    B34010
         LR    R1,SREG                      SOURCE POINTER SICHERN
         LA    SREG,1(,SREG)
B34010   NULL
         MVI   DTYP2,CTYP                   DATENTYP = C
         CLI   0(R1),XTYP                   TYP = HEX
         BE    B35001
         MVI   TYP2,CTYP
         MVI   WORK,BLANK                   GRUND-
         MVC   WORK+1(L'WORK-1),WORK         STELLUNG
         ST    SREG,SAVER3                  POINTER SICHERN
         CLI   SOURCE,QUOTE                 HOCHKOMMA
         BNE   B30104                        NEIN
         LA    R1,1(,SREG)                  R1 = SREG + 1
         LA    R0,EB+72                     ENDE
         SR    R0,R1                        R0 = LAENGE
         LR    SREG,R1                      SREG = R1
B34020   NULL
         CLI   SOURCE,QUOTE                 HOCHKOMMA
         BNE   B34030                        NEIN
         CLI   SOURCE+1,QUOTE               DOPPELBEGRENZER
         BNE   B34040                        NEIN
         BCTR  R0,0                         LAENGE - 1
         STC   R0,*+5                        NACH MVC
         MVC   SOURCE(*-*),SOURCE+1         BEGRENZER WEG
B34030   NULL
         LA    SREG,1(,SREG)                POINTER + 1
         BCT   R0,B34020                    WEITER GEHT'S
         B     B30108                        ENDE FEHLT
B34040   NULL
         SR    SREG,R1                      LAENGE
         BZ    B34050                        GLEICH NULL
         L     R0,WORKLN                    WORKLN LADEN
         LA    R2,WORK+L'WORK               ENDE WORK
         SR    R2,R0                        OFFSET
         SR    R0,SREG                      NEUE
         BM    B30112                        ZU LANG
         ST    R0,WORKLN                     WORKLN
         BCTR  SREG,0                       LN - 1
         STC   SREG,*+5                      NACH MVC
         MVC   0(*-*,R2),0(R1)              LIT NACH WORK
         LA    SREG,1(,SREG)                SREG + 1
B34050   NULL
         LA    SREG,1(SREG,R1)              NAECHSTES ZEICHEN
         L     R1,MAXLNC                    LAENGE
         S     R1,WORKLN                     VOR WORK
         STH   R1,WORK-2                      STELLEN
         B     B30100                       RETURN
         EJECT
B35001   NULL                               ENTRY HEXA-STRING
         MVI   WORK,NULL                    GRUND
         MVC   WORK+1(L'WORK-1),WORK         STELLUNG
         MVI   TYP2,XTYP                    TYP = HEX
         ST    SREG,SAVER3                  POINTER SICHERN
         CLI   SOURCE,QUOTE                 HOCHKOMMA
         BNE   B30104                        NEIN
         LA    R1,1(,SREG)                  R1 = SREG + 1
         LA    R0,EB+72                     ENDE
         SR    R0,R1                        LAENGE
         LR    SREG,R1                      SREG = R1
B35020   NULL
         CLI   SOURCE,QUOTE                 HOCHKOMMA
         BE    B35040                        JA, ENDE
         CLI   SOURCE,C'A'                  AUF
         BL    B30116                        GUELTIGE
         CLI   SOURCE,C'9'                    HEXA
         BH    B30116                          ZEICHEN
         CLI   SOURCE,C'G'                      PRUEFEN
         BL    B35030
         CLI   SOURCE,C'0'
         BL    B30116
B35030   NULL
         LA    SREG,1(,SREG)                POINTER + 1
         BCT   R0,B35020                    WEITER
         B     B30108                       ENDE FEHLT
B35040   NULL
         SR    SREG,R1                      LAENGE
         BZ    B35060                        IST 0
         STC   SREG,*+5                     LN NACH TR
         TR    0(*-*,R1),HEXTAB              UEBERSETZEN
         LA    R0,1(,SREG)                  (LN+1/2)
         SRL   R0,1
         LA    RF,WORK+L'WORK               ENDE WORK
         L     R2,WORKLN                    MAXLN
         SR    RF,R2                        OFFSET IN WORK
         SR    R2,R0                        RESTLN
         BM    B30112                        ZU LANG
         ST    R2,WORKLN                    SPEICHERN
         LR    R2,R1                        ANFANG
B35050   NULL
         PACK  0(2,RF),0(3,R2)              PACKEN
         LA    RF,1(,RF)                    WORK-POINTER
         LA    R2,2(,R2)                    SOURCE POINTER
         BCT   R0,B35050                    WEITER
         MVI   0(RF),NULL                   LOESCHEN
B35060   NULL
         LA    SREG,1(SREG,R1)              FREIER PLATZ
         L     R1,MAXLNC                    MAX-LAENGE
         S     R1,WORKLN                    GES-LAENGE
         STH   R1,WORK-2                    WORK-LN
         SR    RF,RF                        RC
         EOSUB
         EJECT
B36001   SOSUB                              ENTRY ZAHL-LITERAL
         ST    SREG,SAVER3                  POINTER SICHERN
         MVI   VORZEI,X'0C'                 VZ = PLUS
         CLI   SOURCE,PLUS                  POSITIV
         BE    B36010                        JA
         CLI   SOURCE,MINUS                 NEGATIV
         BNE   B36020                        NEIN
         MVI   VORZEI,X'0D'                 VZ = MINUS
B36010   NULL
         LA    SREG,1(,SREG)                POINTER + 1
B36020   NULL
         SP    ZAHL,ZAHL                    GRUNDSTELLUNG
         LR    R1,SREG                      R1 = SREG
         LA    R2,16                        MAXIMAL-LAENGE
B36030   NULL
         CLI   SOURCE,C'0'                  NULL
         BL    B36040
         CLI   SOURCE,C'9'                  NEUN
         BH    B30104                       NICHT NUM.
         LA    SREG,1(,SREG)                POINTER + 1
         BCT   R2,B36030                    WEITERMACHEN
         B     B30108                       ZU LANG
B36040   NULL
         CLI   SOURCE,ALPHA-1               ALPHA-WERT..
         BH    B30104                        JA, FEHLER
         CLI   SOURCE,C'.'                  DEZIMALPUNKT
         BNE   B36050                        NEIN
         LA    R0,EB+72                     ENDE
         SR    R0,SREG                      LAENGE
         STC   R0,*+5                        NACH MVC
         MVC   SOURCE(*-*),SOURCE+1          PUNKT WEG
         B     B36030                       WEITER
B36050   NULL
         SR    SREG,R1                      LAENGE
         BZ    B30104                        IST 0
         BCTR  SREG,0                       LN - 1
         STC   SREG,*+9                      NACH PACK
         OI    *+5,X'70'                    LN ZAHL
         PACK  ZAHL(*-*),0(*-*,R1)          PACKEN
         MVN   ZAHL+7(1),VORZEI             VORZEICHEN
         LA    R0,3(,SREG)                  LAENGE + 3
         SRL   R0,1                          / 2
         STH   R0,WORK-2                    WORK-LAENGE
         LA    SREG,1(SREG,R1)              NAECHSTES ZEICHEN
         LA    R1,ZAHL+L'ZAHL               ENDE ZAHL
         SR    R1,R0                        OFFSET
         MVC   WORK(L'ZAHL),0(R1)           NACH WORK
         SR    RF,RF                        RC
         EOSUB
         EJECT
B36500   SOSUB                              ZIFFERN-ROUTINE
         ST    SREG,SAVER3                  POINTER SICHERN
         SP    ZAHL,ZAHL                    LOESCHEN
         LA    R0,16                        MAX-ANZAHL + 1
         LR    R1,SREG                      R1 = SREG
B36510   NULL
         CLI   SOURCE,C'0'                  AUF
         BL    B36520                        NUMERISCHEN
         CLI   SOURCE,C'9'                    INHALT
         BH    B36590                          PRUEFEN
         LA    SREG,1(,SREG)                POINTER + 1
         BCT   R0,B36510                    WEITER
         L     SREG,SAVER3                  POINTER ZURUECK
         LA    RF,8                         CODE
         BR    SUBREG                       RETURN
B36520   NULL
         CLI   SOURCE,ALPHA-1               ALPHA-ZEICHEN
         BH    B36590                        NICHT NUM
         SR    SREG,R1                      LAENGE
         BZ    B36590                        IST 0
         BCTR  SREG,0                       LN - 1
         STC   SREG,*+9                     NACH PACK
         OI    *+5,X'70'                    LN ZAHL
         PACK  ZAHL(*-*),0(*-*,R1)          WERT NACH ZAHL
B36530   NULL
         LA    SREG,1(SREG,R1)              POINTER
         SR    RF,RF                        RETURN-CODE
         BR    SUBREG                       RETURN
B36590   NULL
         L     SREG,SAVER3                  POINTER ZURUECK
         LA    RF,4                         RETURN-CODE
         EOSUB                              RETURN
         EJECT
B37001   SOSUB                              KONSTANTE NACH OBJECT
         CLC   WORK-2(2),NULLS              LAENGE = 0..
         BNE   B37010                        NEIN
         MVI   WORK-1,1                     LAENGE = 1
B37010   NULL
         LH    R0,WORK-2                    LAENGE
         BCTR  R0,0                          - 1
         L     R1,BUFEND                    BUFFER-ENDE
         L     R2,OBJEND                    ENDE OBJECT-BUFFER
         SR    R2,R1                        LAENGE KONSTANTEN
         BNP   B37030                       NEU AUFNEHMEN
         SH    R2,WORK-2                     - NEUE KONSTANTE
         BM    B37030                       NEU AUFNEHMEN
         LA    R2,1(,R2)                    ANZAHL VERGLEICHE
         STC   R0,B37020+1                  LAENGE FUER CLC
B37020   NULL
         CLC   0(*-*,R1),WORK               KONSTANTE
         BE    B37040                        SCHON MAL VORHANDEN
         LA    R1,1(,R1)                    NAECHSTE STELLE
         BCT   R2,B37020                     WEITERSUCHEN
         L     R1,BUFEND                    NICHT GEFUNDEN, EINSETZEN
B37030   NULL
         SH    R1,WORK-2                    - WORKLN
         CL    R1,BUFADR                    IST NOCH PLATZ
         BL    B37290                        NEIN, SCHWERER FEHLER
         ST    R1,BUFEND                    SICHERN
         STC   R0,*+5                       NACH MVC
         MVC   0(*-*,R1),WORK               NACH OBJECT
B37040   NULL
         STH   R0,LEN2                      LAENGE
         MVI   BAS2,BASRF                   BASIS
         S     R1,BUFANF
         STH   R1,ADR2                      DISPL.
         EOSUB                              RETURN
         SPACE 2
B37200   SOSUB                              PUFFER-ADR EINSETZEN
         LA    OREG,0(OREG,R8)              OREG + R8
         CL    OREG,BUFEND                  MIT ENDE-PUFFER
         BH    B37290                        KEIN PLATZ
         ST    OREG,BUFADR                  SICHERN
         BR    SUBREG                       RETURN
B37290   NULL
         LA    R2,B10595                    END-ADR
         ST    R2,PRTADR                    NACH FOLGE
         B     B81050                       FEHLER DRUCKEN
         EOSUB CODE=NO
         EJECT
B37400   SOSUB                              ELSE-ADR EINSETZEN
         SR    R0,R0                        LOESCHEN
         A     R0,IFANZ                     ANZAHL AUFNEHMEN
         BZR   SUBREG                       KEIN AUSSENSTAND
         L     R1,IFADR
         L     R2,BUFADR
         S     R2,BUFANF                    DISPL
B37410   NULL
         TM    7(R1),B'00001100'            OFFENE ADR
         BZ    B37450                        NEIN
         BM    B37420
         TM    FLAG1,THENBIT                WENN THENBIT AN, WERDEN
         BO    B37450                        NUR THEN ADRESSEN ERSETZT
B37420   NULL
         NI    7(R1),255-X'0C'              LOESCHEN
         STH   R2,8(,R1)                    DISPL.
         OI    8(R1),BASRF                  BASIS-REGISTER
B37450   NULL
         LA    R1,10(,R1)                   NAECHSTER PLATZ
         BCT   R0,B37410                    WEITER
         EOSUB                              RETURN
         SPACE 2
B37600   SOSUB                              OFFSET TABELLE EINTRAGEN
         AP    STMTCNT,P1                   STMT NUMMER + 1
         TM    FLAG3,DEBBIT                 DEBUG AKTIV
         BZ    B37650
         MVI   DB,PLUS                      KEIN VORSCHUB
         MVC   DB+85(6),SBZAE               SCHABLONE
         ED    DB+85(6),STMTCNT             STMT-NUMMER
         MVC   DB+83(4),=C'STMT'            TEXT
         LTSUB B61001                       DRUCKEN
B37650   NULL
         L     R1,STMTABA                   OFFSET IN TABELLE
         CLC   0(2,R1),=X'FFFF'              VOLL..
         BER   SUBREG                       JA, KEIN EINTRAG MEHR
         LR    R0,OREG                      OFFSET
         S     R0,BUFANF                     ERRECHNEN
         STH   R0,0(,R1)                    SPEICHERN
         LA    R1,2(,R1)                    NAECHSTER PLATZ
         ST    R1,STMTABA                   ADRESSE SICHERN
         EOSUB                              RETURN
         EJECT
B37800   SOSUB                              DEBUG DRUCKEN
         MVC   DB+83(3),=C'LOC'
         L     R1,BUFADR                    OBJECT PTR
         S     R1,BUFANF                     LOCATION
         ST    R1,WORK
         UNPK  DB+87(5),WORK+2(3)           HEX AUFBEREITUNG
         MVI   DB+91,BLANK
         TR    DB+87(4),TRTAB-240           UEBERSETZEN
         LTSUB B61001                       DRUCKEN
         EOSUB
         SPACE
B38001   SOSUB                              GETMAIN FUER PROC
         L     R0,PROCLEN                   PROCLAENGE = 4096 BYTES
         GETMAIN R,LV=(0)                   PLATZ HOLEN
         LA    OREG,0(,R1)                  ADRESSE IN REG 2
         LR    R0,OREG                      COPY IN R4
         L     R1,PROCLEN                   LAENGE
         LA    RE,NULLS                     PROC-AREA
         LA    RF,1                          AUF X'00'
         MVCL  R0,RE                          LOESCHEN
         EOSUB
         SPACE
B61001   SOSUB                              DRUCK-ROUTINE
         L     RF,PRTROU                    ADR DRUCKROUTINE XDATA
         CALL  (15),(DB)                    DRUCKEN
         EOSUB                              RETURN
         EJECT
B81020   NULL  E,'UNGUELTIGE STEUERKARTE'
         LA    RF,20                        MSG-NUMMER
         B     B81900                       DRUCKEN
B81030   NULL  E,'<4> STATEMENT AN DIESER STELLE UNGUELTIG'
         LA    RF,30                        MSG-NUMMER
         B     B81045                       DRUCKEN
B81040   NULL  E,'LETZTES <4> STATEMENT NICHT ABGESCHLOSSEN'
         LA    RF,40                        MSG-NUMMER
B81045   NULL
         LA    R1,CMDNAME                   COMMAND-NAME
         ST    R1,MSGPARM4
         B     B81900                       DRUCKEN
B81050   NULL  E,'INTERNER SPEICHERPLATZ VOLL. FORTSETZUNG MIT NEUEM -P+
               ROC STATEMENT'
         LA    RF,50                        MSG-NUMMER
         B     B81900                       DRUCKEN
B81100   NULL  E,'<8> IST EIN UNGUELTIGES SCHLUESSELWORT'
         LA    R0,8                         MAXIMALLAENGE
         LTSUB B82001                       KAESE NACH WORK
         LA    RF,100                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81120   NULL  E,'SATZPOSITION ODER -LAENGE NICHT NUMERISCH'
         LA    RF,120                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81130   NULL  E,'SATZPOSITION ODER -LAENGE ZU GROSS ODER NULL'
         LA    RF,130                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81140   NULL  E,'<8> IST EIN UNGUELTIGER OPERATOR'
         LA    R0,8                         MAXIMALLAENGE
         LTSUB B82001                       KAESE NACH WORK
         LA    RF,140                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81150   NULL  E,'DATENKONSTANTE ZU LANG'
         LA    RF,150                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81160   NULL  E,'ENDE DER ZEICHENKETTE FEHLT'
         LA    RF,160                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81170   NULL  E,'UNGUELTIGE ZEICHEN IN EINER HEXA-KETTE'
         LA    RF,170                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81180   NULL  E,'CODE NACH VERZWEIGUNGSBEFEHL WIRD NICHT AUSGEFUEHRT'
         LA    RF,180                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81190   NULL  E,'DATENTYP <1> UND <1> NICHT VERTRAEGLICH'
         LA    R1,TYP1
         ST    R1,MSGPARM4
         LA    R1,TYP2
         ST    R1,MSGPARM5
         LA    RF,190                       MSG-NUMMER
         B     B81900                       DRUCKEN
         EJECT
B81200   NULL  E,'UNAUSGEWOGENE KLAMMERNPAARE'
         LA    RF,200                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81210   NULL  E,'<8> IST EIN UNGUELTIGER VERKNUEPFUNGSTYP'
         LA    R0,8                         MAXIMALLAENGE
         LTSUB B82001                       KAESE NACH WORK
         LA    RF,210                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81220   NULL  E,'WERT NACH SCHLUESSELWORT NICHT NUMERISCH'
         LA    RF,220                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81230   NULL  E,'WERT NACH SCHLUESSELWORT ZU LANG'
         LA    RF,230                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81240   NULL  E,'TEXT ''<20>'' ENTHAELT UNGUELTIGE SYNTAX'
         LA    R0,20                        MAXIMALLAENGE
         LTSUB B82001                       KAESE NACH WORK
         LA    RF,240                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81250   NULL  E,'DATENKONSTANTE NICHT NUMERISCH'
         LA    RF,250                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81260   NULL  E,'FELDNAME <20> ZU LANG'
         LA    R0,20                        MAXIMALLAENGE
         LTSUB B82001                       KAESE NACH WORK
         LA    RF,260                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81270   NULL  E,'FELDNAME <20> SYNTAKTISCH FALSCH'
         LA    R0,20                        MAXIMALLAENGE
         LTSUB B82001                       KAESE NACH WORK
         LA    RF,270                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81280   NULL  E,'FELDNAME <12> NICHT DEFINIERT'
         LA    RF,280                       MSG-NUMMER
         B     B81295                       DRUCKEN
B81290   NULL  E,'FELDNAME <12> IST SCHON DEFINIERT'
         LA    RF,290                       MSG-NUMMER
B81295   NULL
         LA    R1,VARNAME
         ST    R1,MSGPARM4
         B     B81900                       DRUCKEN
B81300   NULL  E,'COPY IN EINEM MEMBER IST NICHT ERLAUBT'
         LA    RF,300                       MSG-NUMMER
         OI    COPYFLG,COPYCONT             COPY GEHT WEITER
         B     B81900                       DRUCKEN
B81310   NULL  E,'SYNTAXFEHLER IM COPY STATEMENT'
         LA    RF,310                       MSG-NUMMER
         B     B81900                       DRUCKEN
         EJECT
B81320   NULL  E,'SYSLIB DATASET KANN NICHT EROEFFNET WERDEN'
         LA    RF,320                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81330   NULL  E,'SYSLIB DATASET ATTRIBUTE SIND FALSCH'
         LA    RF,330                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81340   NULL  E,'MEMBER <8> NICHT GEFUNDEN'
         LA    RF,340                       MSG-NUMMER
         LA    R1,COPYMBR
         ST    R1,MSGPARM4
         OI    COPYFLG,COPYCONT             COPY GEHT WEITER
         B     B81900                       DRUCKEN
B81350   NULL  E,'STACK UEBERLAUF. RECHENOPERATIONEN ZU TIEF GESCHACHTE+
               LT'
         LA    RF,350                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81400   NULL  W,'DIESE PROCEDURE KANN NICHT AUSGEFUEHRT WERDEN'
         LA    RF,400                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81410   NULL  E,'NAME DES UNTERPROGRAMMES ZU LANG'
         LA    RF,410                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81420   NULL  E,'MODUL <8> KANN NICHT GELADEN WERDEN'
         LA    RF,420                       MSG-NUMMER
         LA    R1,VARNAME
         ST    R1,MSGPARM4
         B     B81900                       DRUCKEN
B81430   NULL  E,'MEHR ALS 32 PARAMETER IM CALL STATEMENT'
         LA    RF,430                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81440   NULL  E,'MEHR ALS 8 VERSCHIEDENE UNTERPROGRAMME'
         LA    RF,440                       MSG-NUMMER
         B     B81900                       DRUCKEN
B81450   NULL  E,'LABEL TABELLE VOLL'
         LA    RF,450                       MSG-NUMMER
         B     B81900                       DRUCKEN
         SPACE
***********************************************************************
         SPACE
B81900   NULL
         ST    RF,MSGPARM1                  MSG-NUMMER
         LA    R1,MSGPARM                   PARAMETERLISTE
         L     RF,XDAMSGS                   ENTRY-ADRESSE
         BALR  RE,RF                        LINK
         CL    RF,RCODE                     IST RC SCHON GROESSER..
         BNH   B81910                        NEIN
         ST    RF,RCODE
B81910   NULL
         L     SUBREG,PRTADR                RETURN-ADR
         CLC   RCODE,F08
         BL    B61001
         OI    FLAG1,ERRBIT                 FEHLER SCHALTER
         B     B61001                       DRUCKEN
         EJECT
***********************************************************************
**       UNGUELTIGEN SOURCE-TEXT FUER FEHLERNACHRICHT ERMITTELN      **
***********************************************************************
         SPACE 2
B82001   SOSUB                              UNGUELIGE SOURCE NACH WORK
         MVC   WORK(20),BLANKS              LOESCHEN
         LR    R1,SREG                      POINTER SICHERN
B82020   NULL
         CLI   SOURCE,BLANK                 ENDE..
         BE    B82040
         CLI   SOURCE,COMMENT1              ENDE..
         BNE   B82030
         CLI   SOURCE+1,COMMENT2            ENDE..
         BE    B82040
B82030   NULL
         LA    SREG,1(,SREG)                SOURCE POINTER + 1
         BCT   R0,B82020
B82040   NULL
         SR    SREG,R1                      LAENGE
         BZR   SUBREG                        = 0
         BCTR  SREG,0                        - 1
         MVC   WORK(*-*),0(R1)              MOVE NACH WORK
         EX    SREG,*-6
         LA    SREG,1(R1,SREG)              SOURCE POINTER
         LA    R1,WORK
         ST    R1,MSGPARM4                  PARM
         EOSUB
         EJECT
***********************************************************************
**       PATCHAREA--PATCHAREA--PATCHAREA--PATCHAREA--PATCHAREA       **
***********************************************************************
         SPACE 2
         B     XDAPATCH                    BRANCH IN PATCHAREA
         DC    CL8'XDAPATCH'               TEXT FUER DUMP
XDAPATCH DC    64XL1'EE'                     PATCHAREA
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
         EJECT
**********************************************************************
**       D E F I N I T I O N E N    (ADRESSIERUNG MIT R13)          **
**********************************************************************
         SPACE
SAVEAREA DC    18F'0'                       SAVEAREA SAVEAREA SAVEAREA
         SPACE
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,                         +
               RECFM=FB,LRECL=80,                                      +
               EODAD=B10500,EXLST=DCBEXIT
         SPACE
SYSLIB   DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=R,                         +
               EODAD=B29700
         SPACE 2
         DFSUB SUBREG=R9
         SPACE 2
STMTABA  DC    A($$OFFSET)                  ADRESSE OFFSET TABELLE
STMTABP  DC    A($$OFFSET)                  ANFANG OFFSET TABELLE
DCBEXIT  DC    X'85',AL3(B10950)            DCB-EXIT SYSIN
PARLEV   DC    F'0'                         KLAMMER-LEVEL
STACKPTR DC    F'0'                         STACKPOINTER
STCKREGS DC    2F'0'                        STACK-REGISTER
FOLGE    DC    A(B81020)                    ADR. FOLGE-ROUTINE
ENDRT    DC    F'0'                         ADR. END-ROUTINE STMT
IFADR    DC    F'0'                         ANFANG VERGLEICHE
IFANZ    DC    F'0'                         ANZAHL VERGLEICHE
ELSEADR  DC    F'0'                         BRANCH FUER ELSE
WORKLN   DC    F'0'                         ARBEITSFELD
NULLS    DC    F'0'                         KONSTANTE
IDISPL   DC    F'0'                         DISPLACEMENT BEI SUBSTR
         SPACE
F04      DC    F'4'                         KONSTANTEN
F08      DC    F'8'
F12      DC    F'12'
F16      DC    F'16'
         SPACE
MAINPROC DC    F'0'                         ADRESSE MAIN PROC
BUFADR   DC    F'0'                         ADR. OBJECT BUFFER
BUFANF   DC    F'0'                         ANF. OBJECT BUFFER
BUFEND   DC    F'0'                         ENDE OBJECT BUFFER
OBJEND   DC    F'0'                         ENDE BUFFER STATISCH
PARMAREA DS    0CL12                        PARAMETERLISTE
PRTROU   DC    F'0'                         ADR. DRUCKROUTINE XDATA
MODADR   DC    F'0'                         ADR. MOD-AREA
PROCADR  DC    F'0'                         ADR. PROC-ENTRY
PRTADR   DC    F'0'                         RETURN NACH FEHLERN
OUTLEN   DC    F'0'                         LAENGE OUTPUT-AREA
PROCLEN  DS    0F                           LAENGE PROCAREA
         EJECT
MAXPOS   DC    F'4096'                      MAX DISPLACEMENT
MAXLNC   DC    F'256'                       MAXLN CHARAKTER
MAXLNP   DC    F'8'                         MAXLN GEPACKT
MAXLNN   DC    F'15'
MAXLNI   DC    F'4'
MAXLNI2  DC    F'2'
MAXLN    DC    F'0'                         MAXLN KONSTANTE
         SPACE
SAVEOREG DC    F'0'
SAVER3   DC    F'0'                         SAVE SREG
RCODE    DC    F'0'                         RETURN-CODE
         SPACE
DCLANF   DC    F'0'                         DCL-TAB ANFANG
DCLAKT   DC    F'0'                         DCL-TAB AKTUELLE ADR
DCLANZ   DC    F'0'                         DCL-TAB ANZAHL PLAETZE
DCLMAX   DC    F'0'                         DCL-TAB MAX PLAETZE
         SPACE
STACK    DC    XL14'0',AL2(SCODE1-SCODE1)
STACKL   EQU   16
STACKCL  EQU   14
         DC    XL14'0',AL2(SCODE2-SCODE1)
         DC    XL14'0',AL2(SCODE3-SCODE1)
         DC    XL14'0',AL2(SCODE4-SCODE1)
         DC    XL14'0',AL2(SCODE5-SCODE1)
         DC    XL14'0',AL2(SCODE6-SCODE1)
         DC    XL14'0',AL2(SCODE7-SCODE1)
         DC    XL14'0',AL2(SCODE8-SCODE1)
STACKA   EQU   (*-STACK)/STACKL
STACKLN  DC    AL2(STACKL)
         SPACE
MSGPARM  DS    0F                           MSG PARAMETERLISTE
MSGPARM1 DC    A(0)                         MSGNUMMER
MSGPARM2 DC    A(DB+1)                      DRUCKBEREICH
MSGPARM3 DC    A(MSGLEN)                    LAENGE
MSGPARM4 DC    A(BLANKS)                    VARIABEL
MSGPARM5 DC    A(BLANKS)                    VARIABEL
XDAMSGS  DC    V(XDAMSGS)                   XDATA MESSAGE UPRO
MSGLEN   DC    A(120)                       LAENGE
         SPACE
PARMANZA DC    F'0'
PARMANZ  DC    H'0'
UCALLADR DC    H'0'
UPROTAB  DS    0CL12
         DC    CL8' ',XL4'00'
         DC    CL8' ',XL4'00'
         DC    CL8' ',XL4'00'
         DC    CL8' ',XL4'00'
         DC    CL8' ',XL4'00'
         DC    CL8' ',XL4'00'
         DC    CL8' ',XL4'00'
         DC    CL8' ',XL4'00'
UPROTABA EQU   (*-UPROTAB)/12
         EJECT
CODE1    DS    0CL10                        TAB 1. OPERAND
LEN1     DC    XL2'0'                       LAENGE
ADR1     DC    XL2'0'                       ADRESSE
BAS1     DC    XL1'0'                       BASISREGISTER
TYP1     DC    XL1'0'                       DATENTYP
CND1     DC    XL1'0'                       BRANCH-COND ODER
         DC    XL1'0'                       BRANCH-COND UND
DTYP1    DC    XL1'0'                       DATENTYP C ODER N
         DC    XL1'0'                       RESERVE
         SPACE
CODE2    DS    0CL10                        TAB 2. OPERAND
LEN2     DC    XL2'0'                       LAENGE
ADR2     DC    XL2'0'                       ADRESSE
BAS2     DC    XL1'0'                       BASISREGISTER
TYP2     DC    XL1'0'                       DATENTYP
CND2     DC    XL1'0'                       BRANCH-COND ODER
         DC    XL1'0'                       BRANCH-COND UND
DTYP2    DC    XL1'0'                       DATENTYP C ODER N
         DC    XL1'0'                       RESERVE
         SPACE
ZCODE1   DS    0CL10                        ZWISCHENCODE
         DC    H'7'                         LAENGE
         DC    AL2($$ZCODE1-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
         SPACE
ZCODE2   DS    0CL10
         DC    H'7'
         DC    AL2($$ZCODE2-DSVARS)         DISPL
         DC    XL1'50'
         DC    CL1'P'
         DC    XL4'0'                       RESERVE
LABATAB  DS    0F
         DC    50F'0'
LABATABA EQU   (*-LABATAB)/4
LABNTAB  DS    0F
         DC    20XL14'4040404040404040404040400000'
LABNTABA EQU   (*-LABNTAB)/14
         SPACE
VXDACALL DC    V(XDACALL)              V-KONSTANTE DES CALL DRIVERS
         EJECT
***********************************************************************
**       STACK-CODE ZWISCHENFELDER                                   **
***********************************************************************
         SPACE
SCODE1   DS    0CL10                        ZWISCHENCODE
         DC    H'15'                        LAENGE
         DC    AL2($$SCODE1-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
SCODE2   DS    0CL10                        ZWISCHENCODE
         DC    H'15'                        LAENGE
         DC    AL2($$SCODE2-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
SCODE3   DS    0CL10                        ZWISCHENCODE
         DC    H'15'                        LAENGE
         DC    AL2($$SCODE3-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
SCODE4   DS    0CL10                        ZWISCHENCODE
         DC    H'15'                        LAENGE
         DC    AL2($$SCODE4-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
SCODE5   DS    0CL10                        ZWISCHENCODE
         DC    H'15'                        LAENGE
         DC    AL2($$SCODE5-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
SCODE6   DS    0CL10                        ZWISCHENCODE
         DC    H'15'                        LAENGE
         DC    AL2($$SCODE6-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
SCODE7   DS    0CL10                        ZWISCHENCODE
         DC    H'15'                        LAENGE
         DC    AL2($$SCODE7-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
SCODE8   DS    0CL10                        ZWISCHENCODE
         DC    H'15'                        LAENGE
         DC    AL2($$SCODE8-DSVARS)         DISPL
         DC    XL1'50'                      BASISREGISTER
         DC    CL1'P'                       TYP
         DC    XL4'0'                       FREI
         EJECT
***********************************************************************
**       CODE FUER SYSIN-EXIT                                        **
***********************************************************************
         SPACE
         PUSH  USING
         USING DSVARS,R5
VCODE    STM   R2,R12,28(R13)              USER-REGISTER SICHERN
         L     R5,VARADR                   ADRESSE SYSVARS
         L     R2,$$NEW1                   ADRESSE NEW-AREA
         AP    $$ZRECNO,$$KON1              SATZZAEHLER + 1
         ZAP   $RECNO,$$ZRECNO               UND RECNO ZUWEISEN
LNVCODE  EQU   *-VCODE
NCODE    DC    F'0'                        ADRESSE SYSVARS
         DC    2F'0'                       FREI FUER POINTER
         DC    A(0)                        ADRESSE FOLGE-PROCEDURE
         DC    H'0'                        STATEMENT OFFSET
         DC    H'0'                        ANZAHL STATEMENTS
         DC    H'0'                        RESERVE
         L     R15,CONTADR                 FOLGEADR
         BR    R15                         VERZWEIGEN
         SP    $$ZRECNO,$$KON1             RECNO - 1 BEI INSERT
         LR    R1,R2
         LA    R0,12                       RETURN-CODE
         B     EXIT
         LA    R0,8                        RETURN-CODE
         B     EXIT
         LA    R0,4                        RETURN-CODE
         B     EXIT
         LR    R1,R2                       NEW-POINTER NACH R1
         SR    R0,R0                       RETURN-CODE
         LM    R2,R12,28(R13)              RESTORE REGISTERS
         LR    R15,R0                      RETURN-CODE NACH R15
         BR    R14                         RETURN
         SPACE
         DROP  R5
         POP   USING
LNNCODE  EQU   *-NCODE
         EJECT
***********************************************************************
**       SONSTIGE DEFINITIONEN                                       **
***********************************************************************
         SPACE
         DS    0D
ZAHL     DC    PL8'0'                       ZIFFERN-ENTSCHL.
MAXVAL   DC    PL5'999999999'               MAXIMALWERT
P1       DC    P'1'
         DC    H'0'                         VORSPANN WORK
WORK     DC    XL256'0'                     WORK-BEREICH
         DC    H'0'                         NACHSPANN WORK
         SPACE
HEXTAB   DC    256XL1'0'                    TRANSLATE-TABELLE
         ORG   HEXTAB+C'A'
         DC    X'AABBCCDDEEFF'
         ORG   HEXTAB+C'0'
         DC    X'00112233445566778899'
         ORG
TRTAB    DC    C'0123456789ABCDEF'
         SPACE
BRANCH   B     *-*                          CODE
         SPACE
BALWRITE LR    R0,RF           CODE         ENTRY-POINT MERKEN
         LR    R1,R2           FUER         NEW POINTER
         LA    RF,12           WRITE        RETURN-CODE = INSERT
         LR    R2,RE           STMT         RETURN ADR
         BALR  RE,R2           ----         RETURN INS XDATA
         DC    H'0'            ----         DDNAME OFFSET
         SPACE
NEGTEST  TM    0(R0),X'10'                  CODE
         BO    0(R0)                        CODE
         OI    0(R0),X'F0'                  CODE
         SPACE
CALLCODE L     R9,*-*                       CODE
         BALR  R8,R9
         SPACE
SEPARATE DC    CL9'SEPARATE'                SCHLUESSELWORT PROC
SECOND   DC    CL7'SECOND'                    DITTO
DEBUG    DC    CL6'DEBUG'                     DITTO
         EJECT
COPYRPTR DC    F'0'                         COPY RECORD POINTER
COPYSPTR DC    F'0'                         COPY SOURCE POINTER
COPYWRKA DC    F'0'                         WORK AREA POINTER
COPYRLEN DC    F'0'                         REST LAENGE
COPYMBR  DC    CL8' '                       MEMBERNAME
         SPACE
COPYFLG  DC    XL1'0'                       COPY FLAG
         SPACE
LIBERROR EQU   B'10000000'                  SYSLIB ERROR
LIBOPEN  EQU   B'01000000'                  SYSLIB OPEN
COPYON   EQU   B'00100000'                  COPY AKTIV
COPYCONT EQU   B'00010000'                  PRUEFUNG GEHT WEITER
         SPACE
MVCLEN   DC    H'0'                         RESTLAENGE BEI ZUWEISUNG
DCLTLEN  DC    AL2(DCLTABL)                 LAENGE DCL TABELLENPLATZ
VARNAME  DC    CL12' '                      VARIABLEN-NAME
BLANKS   DC    CL40' '
EB       DC    CL80' '                      EINGABEBEREICH SYSIN
COPYEB   DC    CL80' '                      EINGABEBEREICH COPY
DB       DC    CL121' '                     DRUCKBEREICH
ZAEEIN   DC    PL5'0'                       SATZZAEHLER
STMTCNT  DC    PL3'0'                       STATEMENT ZAEHLER
VORZEI   DC    XL1'0C'                      VORZEICHEN ZAHL
OPER     DC    XL1'0'                       OPERATOR
LEVEL    DC    XL1'0'                       ENTSCHL.-LEVEL
STMT     DC    XL1'0'                       STATEMENT ID
VERTYP   DC    XL1'0'                       VERKNUEPFUNGS-TYP
CMDNAME  DC    CL4' '                       COMMAND-NAME
DDNAME   DC    CL8'SYSIN'                   DDNAME FUER SYSIN (DEFAULT)
SBZAE    DC    X'402020202120'              SCHABLONE
         SPACE 3
STCOND   DC    AL1(SWMOD+SWPROC)            FOLGE-MODUS
STMOD    DC    AL1(SWMOD+SWPROC)
STPROC   DC    AL1(SWDCL+SWSET+SWIF+SWPEND)
STDCL    DC    AL1(SWDCL+SWSET+SWIF+SWPEND)
STIF     DC    AL1(SWTHEN)
STSET    DC    AL1(SWSET+SWIF+SWPEND+SWDCL)
STTHEN   DC    AL1(SWSET+SWIF+SWELSE+SWPEND+SWDCL)
STELSE   DC    AL1(SWSET+SWIF+SWPEND+SWDCL)
STPEND   DC    AL1(SWMOD+SWPROC)
         EJECT
**********************************************************************
**       T A B E L L E N                                            **
**********************************************************************
         SPACE
KWTAB    NULL                               KEYWORD-TABELLE
KWRET    DC    AL1(6),CL9'RETURN'           KEYWORD
         B     RETURN                       BRANCH-ADR
         DC    AL1(6),CL9'DELETE'
         B     DELETE                       BRANCH-ADR
         DC    AL1(6),CL9'FINISH'
         B     FINISH                       BRANCH-ADR
         DC    AL1(6),CL9'INSERT'
         B     INSERT                       BRANCH-ADR
         DC    AL1(7),CL9'REPLACE'
         B     REPLACE                      BRANCH-ADR
KWCONT   DC    AL1(8),CL9'CONTINUE'
         B     CONTINUE                     BRANCH-ADR
         DC    AL1(5),CL9'WRITE'
         DC    2H'0'                        DUMMY
         DC    AL1(4),CL9'CALL'
         DC    2H'0'                        DUMMY
         DC    AL1(4),CL9'GOTO'
         DC    2H'0'                        DUMMY
KWTABA   EQU   (*-KWTAB)/14                 ANZAHL PLAETZE
         SPACE
OPTAB    NULL                               OPERATOR TABELLE
         DC    AL1(0),CL3'= ',X'887C'
         DC    AL1(2),CL3'GT',X'28DC'
         DC    AL1(2),CL3'LT',X'48BC'
         DC    AL1(2),CL3'EQ',X'887C'
         DC    AL1(1),CL3'<=',X'D82C'
         DC    AL1(2),CL3'LE',X'D82C'
         DC    AL1(2),CL3'NG',X'D82C'
         DC    AL1(1),CL3'^>',X'D82C'
         DC    AL1(1),CL3'>=',X'B84C'
         DC    AL1(2),CL3'GE',X'B84C'
         DC    AL1(1),CL3'^<',X'B84C'
         DC    AL1(2),CL3'NL',X'B84C'
         DC    AL1(1),CL3'^=',X'788C'
         DC    AL1(2),CL3'NE',X'788C'
         DC    AL1(0),CL3'> ',X'28DC'
         DC    AL1(0),CL3'< ',X'48BC'
OPTABA   EQU   (*-OPTAB)/6                  ANZAHL PLAETZE
         EJECT
DDNAMES  DS    0F                           DDNAMEN WRITE
         DC    AL1(7),AL1(00),CL8'SYSUT21 '
DDNAMESL EQU   (*-DDNAMES)
         DC    AL1(7),AL1(01),CL8'SYSUT22 '
         DC    AL1(7),AL1(02),CL8'SYSUT23 '
         DC    AL1(7),AL1(03),CL8'SYSUT24 '
         DC    AL1(7),AL1(04),CL8'SYSUT25 '
         DC    AL1(7),AL1(05),CL8'SYSUT26 '
         DC    AL1(7),AL1(06),CL8'SYSUT27 '
         DC    AL1(7),AL1(07),CL8'SYSUT28 '
         DC    AL1(6),AL1(08),CL8'SYSUT2  '
         DC    AL1(6),AL1(09),CL8'SYSIS2  '
         DC    AL1(5),AL1(10),CL8'PRINT   '
DDNAMESA EQU   (*-DDNAMES)/DDNAMESL
         EJECT
**********************************************************************
**       E Q U A L  W E R T E                                       **
**********************************************************************
         SPACE
NULL     EQU   0
BLANK    EQU   C' '
SREG     EQU   R3
OREG     EQU   R4
BASR1    EQU   X'10'
BASR2    EQU   X'20'
BASR5    EQU   X'50'
BASRF    EQU   X'F0'
QUOTE    EQU   C''''
LPAR     EQU   C'('
RPAR     EQU   C')'
EQUAL    EQU   C'='
PLUS     EQU   C'+'
MINUS    EQU   C'-'
MAL      EQU   C'*'
GETEILT  EQU   C'/'
ORCHAR   EQU   C''
ANDCHAR  EQU   C'&&'
STERN    EQU   C'*'
COMMENT1 EQU   C'/'
COMMENT2 EQU   C'*'
KOMMA    EQU   C','
LABPTR   EQU   C':'
ALPHA    EQU   C'A'
CTYP     EQU   C'C'
XTYP     EQU   C'X'
PTYP     EQU   C'P'
ITYP     EQU   C'I'
NTYP     EQU   C'N'
SYSVAR   EQU   C'$'
APCODE   EQU   X'FA'
CPCODE   EQU   X'F9'
CLCCODE  EQU   X'D5'
CVBCODE  EQU   X'4F'
CVDCODE  EQU   X'4E'
DPCODE   EQU   X'FD'
MVCCODE  EQU   X'D2'
MVICODE  EQU   X'92'
LHCODE   EQU   X'48'
MPCODE   EQU   X'FC'
PACKCODE EQU   X'F2'
SPCODE   EQU   X'FB'
STHCODE  EQU   X'40'
UNPKCODE EQU   X'F3'
ZAPCODE  EQU   X'F8'
         EJECT
**********************************************************************
**       E Q U A L  W E R T E                                       **
**********************************************************************
         SPACE
         SPACE
SWDCL    EQU   B'10000000'
SWMOD    EQU   B'01000000'
SWPROC   EQU   B'00100000'
SWIF     EQU   B'00010000'
SWSET    EQU   B'00001000'
SWTHEN   EQU   B'00000100'
SWELSE   EQU   B'00000010'
SWPEND   EQU   B'00000001'
         SPACE 2
**********************************************************************
**       F L A G S                                                  **
**********************************************************************
         SPACE
FLAG1    DC    B'00000000'
         SPACE
ERRBIT   EQU   B'10000000'          1 = FEHLER BEIM ENTSCHLUESSELN
BISBIT   EQU   B'01000000'          1 = VON - BIS IM FELD (C-1:5)
GOTOBIT  EQU   B'00100000'          1 = BRANCH STMT WERDEN NICHT AUSGEF
CATBIT   EQU   B'00010000'          1 = VERKETTUNG MOEGLICH
LABBIT   EQU   B'00001000'          1 = LABEL IST MOEGLICH
EOFBIT   EQU   B'00000100'          1 = EODAD SYSIN
THENBIT  EQU   B'00000010'          1 = NUR THEN ADRESSEN AUFLOESEN
ITHEN    EQU   B'00000001'          1 = INTERNES THEN
         SPACE
FLAG3    DC    B'00000000'
         SPACE
PROCBIT  EQU   B'10000000'          1 = -PROC STATEMENT WAR DA
PROC2BIT EQU   B'01000000'          1 = FOLGE PROCEDURE
PENDBIT  EQU   B'00100000'          1 = -PEND STATEMENT WAR DA
SEPBIT   EQU   B'00010000'          1 = -PROC SEPARATE
SECBIT   EQU   B'00001000'          1 = -PROC SECOND
DEBBIT   EQU   B'00000100'          1 = -PROC DEBUG
CONTBIT  EQU   B'00000010'          1 = CONTINUE PROC
NEWBIT   EQU   B'00000001'          1 = NEW-BEREICH ERFORDERLICH
         SPACE
*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
         EJECT
***********************************************************************
         LTORG
***********************************************************************
         EJECT
***********************************************************************
***      BESCHREIBUNG DER XDATA SYSTEM-VARIABLEN                    ***
***********************************************************************
         SPACE
SYSVARS  DS     0F
         PRINT  GEN
         SYSVAR $RECNO
SYSVALN  EQU    *-SYSVARS
         PRINT  NOGEN
         SYSVAR $DATE,$TIME
SVPARM   SYSVAR $PARM
         SYSVAR $CNT0,$CNT1,$CNT2,$CNT3,$CNT4
         SYSVAR $CNT5,$CNT6,$CNT7,$CNT8,$CNT9
         SYSVAR $FLG0,$FLG1,$FLG2,$FLG3,$FLG4
         SYSVAR $FLG5,$FLG6,$FLG7,$FLG8,$FLG9
         SYSVAR $FILE,$RETCD
         SYSVAR $LASTCC,$MAXCC
SYSVANZ  EQU   (*-SYSVARS)/SYSVALN
         EJECT
DSVARS   DS    0D                           SYSVAR - MACRO
         COPY  XDAVARS
LNVARS   EQU   *-DSVARS
         DS    0H ****************************************************
         EJECT
**********************************************************************
**       D U M M Y  -  S E C T I O N S                              **
**********************************************************************
         SPACE
DSMOD    DSECT                              MOD-PARAMETER
MODDROP  DS    CL5
MODSKIP  DS    CL5
MODCOPY  DS    CL5
MODLIST  DS    CL5
MODEXIT  DS    CL8
PARMLEN  DS    CL2
PARMFLD  DS    CL120
         ORG   PARMLEN
MODKEY   DS    CL256
MODHDR1  DS    0CL133
         DS    CL19
MODT1    DS    CL114
MODHDR2  DS    0CL133
         DS    CL19
MODT2    DS    CL114
         SPACE 2
DSSOUR   DSECT                              SOURCE AREA
SOURCE   DS    CL1
         SPACE 2
DSOBJ    DSECT                              OBJECT-AREA
OBJECT   DS    CL1
         SPACE 2
DSSTACK  DSECT                              STACK DSECT
STCKCODE DS    CL10                         CODE
STCKOPER DS    CL1                          OPERATOR
STCKOLVL DS    CL1                          OP-LEVEL (X'01' ODER X'02')
STCKKLVL DS    CL2                          KLAMMERLEVEL
STCKSOFF DS    AL2                          S-CODE OFFSET
         SPACE 2
DSDCLTAB DSECT                              VARIABLEN-NAMEN TABELLE
DCLNAME  DS    CL12
DCLCODE  DS    0CL6
DCLLEN   DS    CL2
DCLDISP  DS    CL2
DCLBASIS DS    CL1
DCLTYP   DS    CL1
DCLTABL  EQU   *-DCLNAME
         EJECT
XDAU011  DSECT
         STM   R2,R12,28(R13)              USER-REGISTER SICHERN
         L     R5,VARADR                   ADRESSE SYSVARS
         L     R2,$$NEW1                   ADRESSE NEW-AREA
         AP    $$ZRECNO,$$KON1              SATZZAEHLER + 1
         ZAP   $RECNO,$$ZRECNO               UND RECNO ZUWEISEN
         SPACE 2
OBJAREA  DC    4000XL1'0'                   OBJECT-AREA
         SPACE 2
VARADR   DC    F'0'                        ADRESSE SYSVARS
         DC    2F'0'                       FREI FUER POINTER
CONTADR  DC    A(RETURN)                   ADRESSE FOLGE-PROCEDURE
STMTSOFF DC    H'0'                        STATEMENT OFFSET
STMTSANZ DC    H'0'                        ANZAHL STATEMENTS
         DC    H'0'                        RESERVE
CONTINUE L     R15,CONTADR                 FOLGEADR
         BR    R15                         VERZWEIGEN
INSERT   SP    $$ZRECNO,$$KON1             RECNO - 1 BEI INSERT
         LR    R1,R2
         LA    R0,12                       RETURN-CODE
         B     EXIT
FINISH   LA    R0,8                        RETURN-CODE
         B     EXIT
DELETE   LA    R0,4                        RETURN-CODE
         B     EXIT
REPLACE  LR    R1,R2                       NEW-POINTER NACH R1
RETURN   SR    R0,R0                       RETURN-CODE
EXIT     LM    R2,R12,28(R13)              RESTORE REGISTERS
         LR    R15,R0                      RETURN-CODE NACH R15
         BR    R14                         RETURN
         SPACE 3
         END   XDACGEN
