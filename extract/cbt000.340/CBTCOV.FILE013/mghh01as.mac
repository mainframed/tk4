         MACRO
&DDNM    DSD   &SYSOUT=,               SYSOUT-CLASS,SYSOUT-PGM,FORMS   *
               &DDNAME=,               DDNAME ALTERNATIV ZU &DDNM      *
               &DSN=,                  DATASET-NAME                    *
               &DISP=,                 DATASET-DISPOSITION             *
               &SPACE=,                SPEICHERPLATZ DASD              *
               &VOLSER=,               VOL=SER-ANGABE                  *
               &UNIT=,                 UNIT                            *
               &LABEL=,                LABEL                           *
               &FREE=,                 FREE=CLOSE/END                  *
               &OUTLIM=,               SYSOUT-LIMIT                    *
               &COPIES=,               SYSOUT-KOPIENZAHL               *
               &DEST=,                 OUTPUT-DESTINATION              *
               &DEN=,                  TAPE-DENSITY                    *
               &DSORG=,                DCB-DSORG DATEIORGANISATION     *
               &RECFM=,                DCB-SATZFORMAT                  *
               &LRECL=,                DCB-SATZLAENGE                  *
               &BLKSIZE=,              DCB-BLOCK-LAENGE                *
               &RELNO=,                RELATIVE ALLOC-NUMMER           *
               &RETDDN=,               RETURN DDNAME                   *
               &RETDSN=,               RETURN DSNAME                   *
               &RTDSORG=,              RETURN DCB-DSORG                *
               &RETVOL=,               RETURN VOL=SER                  *
               &RETMB=,                RETURN MEMBERNAME               *
               &RETDSP1=,              RETURN DISP - 1.PARAMETER       *
               &RETDSP2=,              RETURN DISP - 2.PARAMETER       *
               &RETDSP3=,              RETURN DISP - 3.PARAMETER       *
               &RETLIM=,               RETURN ANZ. FREE VOR NEU-ALLOC  *
               &RETDYNA=,              RETURN DYNALLOC-ATRIBUT         *
               &RETLENT=,              RETURN KENNUNG LAST ENTRY       *
               &RETDSTY=,              RETURN DATASET-TYPE (DUMMY USW) *
               &DDNLOC=,               MOVE DDNAME IN DYNALLOC-ENTRY   *
               &DSNLOC=,               MOVE DSN IN DYNALLOC-ENTRY      *
               &MBLOC=,                MOVE MEMBER IN DYNA.-ENTRY      *
               &DSP1LOC=,              MOVE DISP 1.PARAM. IN ENTRY     *
               &SCLSLOC=,              MOVE SYSOUT-CLASS IN ENTRY      *
               &RLNOLOC=,              MOVE RELNO-ALLOC  IN ENTRY      *
               &MVEDDN=,               RETURN DDNAME + MOVE IN AREA    *
               &TYPE=ALLOC,            DYNALLOC BY DSNAME              *
               &DOC=NO                 KEINE DOKUMENTATION DRUCKEN
         LCLA  &CNT,&CT,&CT1
         LCLA  &I,&J
         LCLA  &LNG
         LCLA  &X80,&X40,&X20,&X10,&X08,&X04,&X02,&X01
         LCLB  &DSP1EQ,&DSOREQ
         LCLB  &CODE
         GBLB  &DSP1EQG,&DSOREQG
         LCLC  &IHB
         LCLC  &TPT(200)
         LCLC  &TXT(200)
         LCLC  &NM1(50),&NM2(50),&NM3(50)
         LCLC  &PARM,&P,&HEX,&TYP,&PTEQ(50),&TXEQ(50),&RMVC(50)
.*********************************************************************
         AIF   ('&DOC' NE 'YES').DOC100     KEINE DOCUMENTATION DRUCKEN
**********************************************************************
*
*        DIESES MAKRO GENERIERT DIE PARAMETERLISTE UND DEN AUFRUF DES
*        DYNALLOC-SVC (SVC 99),WIE IM JOB MANAGEMENT-SPL GC28-627 BE-
*        SCHRIEBEN.
*
*        DIESES MAKRO GENERIERT NUR EINEN REUS-CODE UND KANN NICHT
*        FUER REENTRANT-PROGRAMME VERWENDET WERDEN.
*
*        ES WURDEN WEITGEHEND DIE SCHLUESSELWORTE UND POSITIONSPARAME-
*        TER DES JCL-DD-STATEMENTS VERWENDET.AUSNAHMEN SIND GESCHACH-
*        TELTE SCHLUESSELWORTE (DCB=DEN=3),DIE ALS PARAMETER VON ASSEM-
*        BLER-MAKROS NICHT ERLAUBT SIND.
*
*        ES WURDE NUR EINE AUSWAHL DER WICHTIGSTEN PARAMETER IMPLEMEN-
*        TIERT,UM DEN MAKRO-UMFANG IN GRENZEN ZU HALTEN.BEI BEDARF SIND
*        ERWEITERUNGEN GEPLANT.
*
*        ES ERFOLGEN KEINE PRUEFUNGEN,OB WIDERSPRUECHLICHE PARAMETER
*        ANGEGEBEN SIND.AUSNAHMEN SIND DDNAME UND EINIGE TYPE=INFO
*        PARAMETER.ES WIRD AUCH NICHT AUF VOLLSTAENDIGKEIT DER ANGABEN
*        ABGEPRUEFT.
*
*        IM GEGENSATZ ZUM DD-STATEMENT IST BEI DYNALLOC AUCH DIE RUECK-
*        GABE VON INFORMATIONEN (Z.B. DSNAME BEI GEGEBENEM DDNAMEN)
*        MOEGLICH.
*
*        ABGESEHEN VOM TYPE-PARAMETER SIND 4 GRUPPEN VON PARAMETERN
*        VORHANDEN:
*
*          1.  KONSTANTE ALS INPUT-PARAMETER WIE IN DER JCL.
*              Z.B.  SYSOUT=A,DDNAME=SYSUT1
*          2.  INPUT-PARAMETER,DIE IN DATENFELDERN IM PROGRAMM STEHEN
*              Z.B. MEMBERNAMEN UND DIE PER MVC/MVCL IN DEN ENTSPRE-
*              CHENDEN SVC-99-TEXTBLOCK IM MAKRO UEBERTRAGEN WERDEN.
*          3.  RUECKGABE-PARAMETER,DEREN 2-BYTE LAENGENFELD IM SVC99-
*              TEXTBLOCK DURCH EQU-DEFINITIONEN ADRESSIERBAR SIND.
*          4.  RUECKGABE-PARAMETER,DIE OHNE LAENGENANGABE IN DER JE-
*              WEILS RICHTIGEN LAENGE (Z.B. 8 BYTES BEIM DDNAMEN) PER
*              MVCL IN BELIEBIGE DATENFELDER UEBERTRAGEN WERDEN. ALS
*              BEISPIEL KANN DER DDNAME IN DAS DDNAME-FELD EINES DCBS
*              EINGESETZT WERDEN.
*
*        NACH DURCHLAUFEN DES MAKROS STEHT IM DM REGISTER 15 DER
*        RETURN-CODE DES DYNALLOC-MAKROS:
*          0   SUCCESSFUL COMPLETION
*          4   ERROR ON DYNALLOC
*          8   INSTALLATION-EXIT-ROUTINE HAT EINEN RETURNCODE UNGLEICH
*              0 ZURUECKGEGEBEN
*          12  DIE GENERIERTE PARAMETERLISTE DES SVC-99 IST FALSCH.
*              (ODER DURCH UEBERSCHREIBEN ZERSTOERT)
*
*        BEI EINEM RETURNCODE UNGLEICH 0 IM R15 WIRD DER ERRORCODE IN
*        DAS REGISTER 0 GELADEN:
*              IN DEN OBEREN 2 BYTES DEN ERROR-REASON-CODE
*              IN DEN UNTEREN 2 BYTES DER INFORMATION-REASON-CODE
*              DIE BESCHREIBUNG DIESER BEIDEN CODES IST DEM JOB-MANAGE-
*              MENT-SPL ZU ENTNEHMEN.
***********************************************************************
*        PARAMETER DER GRUPPE 1: (JCL-EQUIVALENTE)
*
*  DDNAME      DER DDNAME KANN ENTWEDER IM NAMENSFELD DES DSD-MAKROS
*              Z.B. SYSUT1 DSD DSN=..... ODER ALS DDNAME-PARAMETER
*              Z.B.  DSD DDNAME=SYSUT1 ANGEGEBEN SEIN. IN EINEM MAKRO-
*              AUFRUF DARF NUR EINE DER BEIDEN MOEGLICHKEITEN VERWEN-
*              DET WERDEN.
*
*  SYSOUT      SYSOUT=(CLASS,SYSOUT-PROGRAMM,FORMS-NAME)
*              ENTSPRICHT DER JCL-KONVENTION.BEISPIELE:
*                 SYSPRINT DSD SYSOUT=A
*                 INTRDR   DSD SYSOUT=(A,INTRDR)
*                 FORMS    DSD SYSOUT=(U,,123)
*
*  DSN         DSN=DSNAME  BZW  DSN=DSNAME(MEMBER)
*              ENTSPRICHT DER JCL-KONVENTION.
*              BEISPIELE:
*                 SYSLIB DSD DSN=SYS1.MACLIB
*                 SYSLMOD DSD DSN=SYS1.LINKLIB(IEFBR14)
*
*  DISP        DISP=(PARM1,PARM2,PARM3)
*              ENTSPRICHT DER JCL-KONVENTION.
*              PARM1=NEW/OLD/MOD/SHR
*              PARM2=PARM3=UNCATLG/CATLG/DELETE/KEEP
*              BEISPIELE:
*              SYSLIB DSD DSN=SYS1.MACLIB,DISP=SHR
*              SYSUT1 DSD UNIT=SYSDA,DISP=(OLD,KEEP,DELETE),DSN=SCRATCH
*
*  SPACE       SPACE=(TRK/CYL/BLKLNG,(PRIM,SEK,DIR),RLSE,CONTIG,ROUND)
*              ENTSPRICHT DEN JCL-KONVENTIONEN
*              BEISPIEL:
*              PDS DSD DSN=SYS1.PROCLIB,SPACE=(CYL,(10,10,50),,CONTIG),
*                      DISP=(NEW,CATLG),UNIT=SYSDA,VOLSER=SYSRES
*              AN STELLE VON 'CONTIG' IST AUCH 'MXIG' ODER 'ALX' MOEG-
*              LICH.
*
*  VOL=SER     VOLSER=(LISTE VON VOLUME-NAMEN)
*              ENTSPRICHT DER VOL=SER-JCL-ANGABE
*              BEISPIEL:
*              SYSWORK DSD VOLSER=(SYSRES,IPOCAT,SPOOL1),.....
*              SIEHE AUCH SPACE-BEISPIEL.
*
*  UNIT        UNIT=(3350,UNIT-COUNT)
*              ENTSPRICHT DEN JCL-KONVENTIONEN
*              BEISPIELE:
*              TAPE DSD UNIT=(3400-4,2),.....
*              LOCAL DSD UNIT=067
*              DISK  DSD UNIT=SYSDA,.....
*
*  LABEL       LABEL=(LABEL-NR,SL/NL/BLP USW.,NOPWREAD/PASSWORD,IN/OUT)
*              ENTSPRICHT DEN JCL-KONVENTIONEN.
*              ALS 2.PARAMETER IST IMPLEMENTIERT:
*                  SL/NL/BLP/NSL/SUL/LTM/AL/AUL
*              BEISPIELE:
*              TAPE DSD UNIT=TAPE,LABEL=(1,BLP),VOLSER=NL1600
*              FT11F001 DSD UNIT=SYSDA,LABEL=(,,,IN),.....
*
*  FREE        FREE=CLOSE/END
*              ENTSPRICHT DEN JCL-KONVENTIONEN.
*              BEISPIEL:
*              SORTIN DSD UNIT=TAPE,FREE=CLOSE,.......
*
*  OUTLIM      OUTLIM=ANZAHL-ZEILEN
*              ENTSPRICHT DEN JCL-KONVENTIONEN.
*              BEISPIEL:
*              SYSPRINT DSD SYSOUT=A,OUTLIM=10000
*
*  COPIES      COPIES=ANZAHL-KOPIEN
*              ENTSPRICHT DEN JCL-KONVENTIONEN
*              BEISPIEL:
*              PRINT DSD SYSOUT=A,COPIES=3
*
*  DEST        DEST=ROUTING-DESTINATION
*              ENTSPRICHT DEN JCL-KONVETIONEN.
*              BEISPIEL:
*              LISTE1 DSD SYSOUT=A,DEST=RMT1
*
*  DEN         DEN=TAPE-DENSITY
*              ENTSPRICHT DEN JCL-KONVENTIONEN.WEGEN DER IM ASSEMBLER
*              NICHT ERLAUBTEN GESCHACHTELTEN KEYWORDS ENTFAELLT DCB=.
*              BEISPIEL:
*              TAPE DSD UNIT=TAPE,DEN=4,.......
*
*  DSORG       DSORG=DATEI-ORGANISATION
*              ENTSPRICHT TEILWEISE DEN JCL-KONVENTIONEN.AUSNAHMEN:
*              DCB= ENTFAELLT UND AMP='AMORG' WIRD DURCH DSORG=VSAM
*              ERSETZT.
*              IMPLEMENTIERT SIND FOLGENDE DSORG-ANGABEN:
*              DSORG=VSAM/TQ/TX/GS/PO/POU/MQ/CQ/CX/DA/DAU/PS/PSU
*              BEISPIEL:
*              BDAM DSD UNIT=SYSDA,VOLSER=IPOCAT,BLKSIZE=3000,DSORG=DA,
*                       RECFM=F,SPACE=(CYL,1)
*
*  RECFM       RECFM=F/V/U + M/A + S + B + T
*              BIS AUF DIE FEHLENDE DCB=-ANGABE ENTSPRECHEND DEN JCL-
*              KONVENTIONEN.
*              BEISPIEL:
*              SYSPRINT DSD SYSOUT=A,RECFM=VBA,BLKSIZE=1374,LRECL=137
*
*  LRECL       LRECL=X ODER LRECL=SATZLAENGE
*              BIS AUF DIE FEHLENDE DCB=-ANGABE ENTSPRICHT DIESER PARA-
*              METER DEN JCL-KONVENTIONEN.
*              BEISPIEL SIEHE RECFM.
*
*  BLKSIZE     BLKSIZE=BLOCKLAENGE
*              BIS AUF DIE FEHLENDE DCB=-ANGABE ENTSPRICHT DIESER PARA-
*              METER DEN JCL-KONVENTIONEN.
*              BEISPIEL SIEHE RECFM.
*
***********************************************************************
*        PARAMETER DER GRUPPE 2 :
*        DIE PARAMETER DIESER GRUPPE SIND VOM PROGRAMMIERER ZU VERWAL-
*        TENDE DATENFELDER IM PROGRAMM. SIE SIND VOM PROGRAMMIERER MIT
*        DATEN ZU FUELLEN.VOR JEDEM DATENFELD STEHT GRUNDSAETZLICH EIN
*        2 BYTES LANGES LAENGENFELD,WELCHES DIE LAENGE DES NACHFOLGEN-
*        DEN DATENFELDES ENTHAELT.ENTSPRECHEND DIESEM LAENGENFELD ER-
*        FOLGT DIE UEBERTRAGUNG PER EX-MVC IN DIE GENERIERTEN TEXT-
*        BLOECKE.DA EINE UEBERPRUEFUNG NICHT ERFOLGT,KOENNEN FEHLER-
*        HAFTE LAENGENFELDER ZU ZERSTOERUNGEN IN DER SVC-99-PARMLISTE
*        FUEHREN.ALS PARAMETER IST DIE ADRESSE DES LAENGENFELDES ANZU-
*        GEBEN. DIE ADRESSE KANN AUCH IN EINES DER REGISTER 2-12 GELA-
*        DEN UND DIESES IN KLAMMERN GESETZT ALS PARAMETER ANGEGEBEN
*        WERDEN.
*
*        BEISPIEL: DSN UND MEMBERNAMEN IN PROGRAMMFELDERN.
*        LA R2,DSNL
*        DSD DSNLOC=(R2),MBLOC=MBLN
*        .
*        DSNL DC AL2(L'DSN)
*        DSN  DC C'SYS1.MACLIB'
*        MBLN DC AL2(L'MB)
*        MB   DC C'DSD'
*
*  DSN         DSNLOC=ADRESSE
*              DER UNTER DER ANGEGEBENEN ADRESSE GESPEICHERTE DSNAME
*              (MAX 44 BYTES LANG) WIRD EINSCHL. LAENGENFELD IN DEN
*              GENERIERTEN TEXTBLOCK UEBERTRAGEN.
*              BEISPIEL:
*              SYSLIB DSD DSNLOC=DSN,DISP=SHR
*              DSN    DC  AL2(L'DSNM)
*              DSNM   DC  C'SYS1.MACLIB'
*
*  MEMBER      MBLOC=ADRESSE
*              DER UNTER DER ANGEGEBENEN ADRESSE GESPEICHERTE MEMBER-
*              NAME (MAX 8 BYTES LANG) WIRD EINSCHL. LAENGENFELD IN
*              DEN GENERIERTEN TEXTBLOCK UEBERTRAGEN.
*              BEISPIEL:
*              SYSIN  DSD DSN=SYS1.PROCLIB,DISP=SHR,MBLOC=MEMBER
*              MEMBER DC  AL2(L'MBNM)
*              MBNM   DC  CL8'FORTHCLG'
*
*  DISP        DSP1LOC=ADRESSE         DISP=OLD/MOD/NEW/SHR IM PROGRAMM
*              HIER IST EIN 1 BYTE LANGES DATENFELD MIT VORGESETZTEM
*              AL2(1)-LAENGENFELD ZU FUELLEN. ZUR ERLEICHTERUNG WERDEN
*              AUTOMATISCH EQU'S FUER OLD/MOD/NEW/SHR GENERIERT.DIESE
*              KOENNEN ALS OPERANDEN VON AL1 ODER MVI-INSTRUKTIONEN
*              VERWENDET WERDEN.
*              BEISPIEL:
*                     MVI DISP1,OLD    (MODIFIKATION VON DISP=SHR NACH
*              SYSLIB DSD DSN=SYS1.MACLIB,DSP1LOC=DISP       DISP=OLD)
*              DISP   DC  AL2(1)
*              DISP1  DC  AL1(SHR)
*
*  SYSOUT      SCLSLOC=ADRESSE         SYSOUT-KLASSE ALS PGM-DATEN
*              EIN 1-BYTE-DATENFELD MIT VORGESETZTEM AL2(1)-LAENGENFELD
*              IST ZU FUELLEN.
*              BEISPIEL:
*                     MVI CLS,C'A'     AUF SYSOUT=A AENDERN
*              PRINT  DSD SCLSLOC=CLASS
*              CLASS  DC  AL2(L'CLS)
*              CLS    DC  C'X'         SYSOUT=X
*
***********************************************************************
*
*              PARAMETER DER GRUPPE 3 - VON DYNALLOC ZURUECKGEGEBENE
*              WERTE. HIER SIND NUR DIE FUER TYPE=ALLOC (STANDARDWERT)
*              ZULAESSIGEN PARAMETER BESCHRIEBEN.DIE FUER TYPE=INFO
*              (DYNAMIC INFORMATION RETRIVAL) ZULAESSIGEN PARAMETER
*              SIND NACHFOLGEND BESCHRIEBEN.
*              DIE ALS PARAMETER ANGEGEBEN NAMEN WERDEN IM NAMENSFELD
*              EINES EQU'S EINGESETZT. DIE DADURCH DEFINIERTEN ADRESSEN
*              ZEIGEN AUF DAS 2-BYTE LAENGENFELD EINES SVC-99-TEXT-
*              BLOCKES.DAS LAENGENFELD ENTHAELT NACH DURCHLAUF DES
*              MAKROS DIE AKTUELLE LAENGE DES DARAN ANSCHLIESSENDEN
*              DATENFELDES.
*              BEISPIEL:
*              DSD  RETDDN=DDN  ERGIBT DAS EQUIVALENT ZU:
*
*              DDN   DC AL2(DDNAMEN-LAENGE),C'DDNAME'
*
*  DDNAME      RETDDN=EQU-NAME
*              NACH DURCHLAUF DES DYNALLOC-SVC ENTHAELT DAS DURCH DEN
*              EQU-NAMEN ADRESSIERTE FELD DIE LAENGE (1-8 BYTES) DES
*              VOM SYSTEM GENERIERTEN DDNAMENS IN EINEM AL2-FELD SOWIE
*              UNMITTELBAR ANSCHLIESSEND DEN DDNAMEN ALS CHAR-FELD.
*              FALLS DAS UEBERTRAGEN DES DDNAMENS AUF 8 STELLEN MIT
*              LEERSPALTEN ERGAENZTEN BEISPIELSWEISE IN DAS DDNAMEN-
*              FELD EINES DCBS GEWUENSCHT WIRD,KANN DER MVEDON-PARAME-
*              TER HIERZU BENUTZT WERDEN.
*              BEISPIEL:
*                   DSD  SYSOUT=A,RETDDN=DDNM
*              MIT FOLGENDEN BEFEHLEN KANN DER DDNAME IN EINEN DCB
*              UEBERTRAGEN WERDEN:
*                   LH  R1,DDNM             LAENGE DYNALLOC-DDNAME
*                   ICM R1,8,=C' '          FUELLZEICHEN BLANK
*                   LA  R0,DDNM+2           ADRESSE DYNALLOC-DDNAME
*                   LA  R14,DCB+DCBDDNAM-IHADCB  ADRESSE DDNAMENFELD IM
*                   LA  R15,8               LAENGE DDNAMENFELD IM DCB
*                   MVCL R14,R0             MOVE DDNAME IN DCB
*                   .
*              DCB  DCB DDNAME=X,DSORG=.......
*                   DCBD ........
*
*  DSN         RETDSN=EQU-NAME
*              NACH DURCHLAUF DES DYNALLOC-SVCS ENTHAELT DAS DURCH DEN
*              EQU-NAMEN ADRESSIERTE FELD DIE LAENGE (1-44 BYTES)
*              DES VOM SYSTEM GENERIERTEN DSNAMENS IN EINEM AL2-FELD
*              SOWIE UNMITTELBAR ANSCHLIESSEND DEN DSNAMEN ALS
*              CHAR-FELD.FUER EINE WEITERVERWENDUNG DES DSN GILT SINN-
*              GEMAESS DAS BEISPIEL DES RETDDN-PARAMETERS.
*              BEISPIEL: (NUR DSD-AUFRUF)
*              SYSUT1 DSD UNIT=SYSDA,SPACE=(CYL,1),RETDSN=DSNM
*
*  DSORG       RTDSORG=EQU-NAME
*              NACH DEM DURCHLAUF DES DYNALLOC-SVCS ENTHAELT DAS DURCH
*              DEN EQU-NAMEN ADRESSIERTE FELD DIE LAENGE IN EINEM
*              AL2(2)-FELD SOWIE UNMITTELBAR ANSCHLIESSEND EINEN 2-BYTE
*              HEX-CODE,DER DIE DSORG DER ZUGEORDNETEN DATEI ENTHAELT.
*              ZUR ERLEICHTERUNG DER AUSWERTUNG DIESES CODES WERDEN
*              INNERHALB DES MACROS EQU-CODES (Z.B. VSAM EQU X'0008')
*              FUER FOLGENDE DSORGS GENERIERT:
*              VSAM,PO,POU,DA,DAU,PS,PSU,IS,ISU
*              BEI ANDEREN DSORGS IST DIE HEX-VERSCHLUESSELUNG DEM JOB-
*              MANAGEMENT-SPL ZU ENTNEHMEN.
*              BEISPIEL:
*              SYSLIB DSD DSN=SYS1.MACLIB,DISP=SHR,RTDSORG=DSORG
*              ABFRAGE AUF DSORG=PO:
*                     CLC  DSORG+2(2),=AL2(PO)
*                     BNE  ....
*
***********************************************************************
*       PARAMETER DER GRUPPE 4 - VON DYNALLOC ZURUECKGEGEBENE WERTE,
*       DIE OHNE LAENGENFELD IN DER JEWEILS ERFORDERLICHEN LAENGE AUF-
*       GEFUELLT MIT BLANKS ODER ANDEREN ERFORDERLICHEN ZEICHEN IN EIN
*       VOM BENUTZER SPEZIFIZIERTES FELD UEBERTRAGEN WERDEN.ALS BEI-
*       SPIEL KANN DER DDNAME IN EINEN DCB GEBRACHT WERDEN. DIE UEBER-
*       TRAGUNG WIRD NUR DANN DURCHGEFUEHRT,WENN DER RETURNCODE DES
*       DYNALLOC-SVCS IM REGISTER 15 GLEICH 0 IST.
*
*  DDNAME      MVEDDN=ADRESSE ODER EIN REGISTER (2-12) IN KLAMMERN,WEL-
*              CHES DIE FELDADRESSE ENTHAELT.DER NACH DURCHLAUF DES
*              DYNALLOC-SVCS ZURUECKGEGEBENE DDNAME WIRD IN DAS VOM
*              ANWENDER ANGEBENE FELD UEBERTRAGEN. DER DDNAME WIRD BEI
*              DER UEBERTRAGUNG (MVCL) BEI BEDARF MIT LEERSPALTEN AUF
*              8 STELLEN AUFGEFUELLT.
*              BEISPIELE:
*                   DSD  SYSOUT=A,MVEDDN=DDNM
*                   LA   R2,DDNM
*                   DSD  SYSOUT=A,MVEDDN=(R2)
*
*              PRINT DCB  .....
*                    DCBD .....
*              DDNM  EQU  PRINT+DCBDDNAM-IHADCB
*
***********************************************************************
*         DYNAMIC INFORMATION RETRIVAL                                *
***********************************************************************
*         HIERZU IST DIE ANGABE TYPE=INFO ERFORDERLICH. DIE AUSWAHL DER
*         INFORMATIONEN KANN UEBER EINEN DDNAMEN,EINEN DSNAMEN ODER
*         UEBER EINE RELATIVE ALLOCATIONS-NUMMER ERFOLGEN.
*         ABGESEHEN VON DEN 3 INPUT-PARAMETERN,DIE DER GRUPPE 1+2 AN-
*         GEHOEREN,SIND ALLE ANDEREN RETURNPARAMETER DER GRUPPE 3 ZUZU-
*         ORDNEN. DETAILS UEBER DIE GRUPPEN UND BEISPIELE SIEHE VOR-
*         STEHENDE BESCHREIBUNG UEBER PARAMETER MIT TYPE=ALLOC (=ALLOC
*         BY DSNAME).
*
*  DDNAME      INFORMATIONSRUECKGABE FUER DIE EINEM BESTIMMTEN DDNAMEN
*              ZUGEORDNETE DATEI. ZUSAETZLICHE ANGABE VON DSN ODER REL-
*              ALLOC-NR IST NICHT MOEGLICH.AEHNLICH WIE BEI TYPE=ALLOC
*              SIND MEHRERE MOEGLICHKEITEN FUER DIE ANGABE EINES
*              DDNAMENS VORHANDEN:
*              1) DURCH LABEL DES DSD-MARROS:
*                 SYSUT1 DSD TYPE=INFO,.....
*              2) DURCH DEN DDNAME-PARAMETER:
*                        DSD TYPE=INFO,DDNAME=SYSUT1,.....
*              3) DURCH DEN DDNLOC-PARAMETER:(PARAMETER DER GRUPPE 2)
*                        DSD TYPE=INFO,DDNLOC=DDN,.....
*                     ODER:
*                        LA  R2,DDN
*                        DSD TYPE=INFO,DDNLOC=(R2),.....
*                 DDN    DC  AL2(L'DDNM)
*                 DDNM   DC  C'DDNAME'
*
*  DSNAME      INFORMATIONS-RUECKGABE UEBER EINE DATEI MIT DEM ANGEGE-
*              BENEN DSNAMEN.EIN DDNAME ODER EINE RELATIVE ALLOC-NR
*              DARF NICHT ANGEGEBEN WERDEN.
*              BEISPIEL:
*                   DSD TYPE=INFO,DSN=SYS1.MACLIB,.....
*
*  RELATIVE ALLOCATION-NUMMER
*              INFORMATIONS-RUECKGABE UEBER EINE DATEI MIT DER ANGEGE-
*              BENEN RELATIVEN NR. IM ZUSAMMENHANG MIT RETLENT (RETURN
*              LAST ENTRY-KENNUNG) WIRD EIN SEQUENTIELLES ABARBEITEN
*              ALLER ALLOC-ENTRIES MOEGLICH.EIN DDNAME ODER DSNAME
*              DARF NICHT ANGEGEBEN WERDEN.
*              BEISPIEL KONSTANTE RELNR:
*                 DSD TYPE=INFO,RELNO=10,RETDDN=DDNM
*              BEISPIEL VARIABLE RELNR:
*                 LA  R2,10
*                 STH R2,RELNR
*                 DSD TYPE=INFO,RLNOLOC=RELN,RETLENT=LAST
*                 CLI LAST+2,0          LETZTER ENTRY ( X'80')
*                 BNE .....             JA
*              RELN  DC AL2(2)          LAENGENFELD
*              RELNR DS AL2             RELATIVE NUMMER
*              DIE ADRESSE KANN AUCH IM REGISTER UEBERGEBEN WERDEN:
*                    LA R2,RELN         ADRESSE LAENGENFELD
*                    DSD TYPE=INFO,RLNOLOC=(R2),.....
*
***********************************************************************
*        PARAMETER DER GRUPPE3 - VON DYNALLOC ZURUECKGEGEBENE WERTE.
*        DETAILLIERTE BESCHREIBUNG DER GRUPPE 3 SIEHE UNTER TYPE=ALLOC.
*
*
*  DDNAME      RETDDN=EQU-NAME
*              NACH DURCHLAUF DES DYNALLOC-SVCS ENTHAELT DAS DURCH DEN
*              EQU-NAMEN DEFINIERTE FELD DIE LAENGE IN EINEM AL2-FELD
*              UND UNMITTELBAR ANSCHLIESSEND DEN VOM SYSTEM ERMITTELTEN
*              DDNAMEN.
*              BEISPIEL: UEBERTRAGUNG DES DDNAMENS IN EINEN DCB
*                   DSD DSN=SYS1.MACLIB,TYPE=INFO,RETDDN=DDNM
*                   LH  R1,DDNM        LAENGE DYNALLOC-DDNAME
*                   ICM R1,8,=C' '     FUELLZEICHEN BLANK
*                   LA  R0,DDNM        ADRESSE DYNALLOC-DDNAME
*                   LA  R14,DCB+DCBDDNAM-IHADCB    ADRESSE DDNAME DCB
*                   LA  R15,8          LAENGE DDNAMENFELD IM DCB
*                   MVCL R14,R0        MOVE DDNAME IN DCB
*              DCB  DCB  .....         DCB
*                   DCBD .....         DCB-DSECT
*
*  DSNAME      RETDSN=EQU-NAME
*              SINNGEMAESS WIE DDNAME,JEDOCH WIRD EIN BIS ZU 44 BYTE
*              LANGER DSNAME ZURUECKGEGEBEN.EIN EVTL VORHANDENER MEMBER
*              NAME IST MIT RETMB=.... GETRENNT ANZUFORDERN
*              BEISPIEL: UEBERTRAGUNG DSN IN USER-FELD
*              SYSLID DSD TYPE=INFO,RETDSN=DSNM
*                     LH  R1,DSNM      LAENGE DYNALLOC-DSN
*                     BCTR R1,0        LAENGE-1 FUER MVC
*                     EX R1,MVC        MOVE DSN IN AREA
*              MVC    MVC  DSNME(1),DSNM+2       EX-MVC
*              DSNME  DC  CL44' '
*
*  MEMBER      RETMB=EQU-NAME
*              SINNGEMAESS WIE RETDDN,JEDOCH MAX 8 BYTE MEMBERNAMEN.
*              WENN KEIN MEMBERNAMEN VORHANDEN,WIRD DIE LAENGE VOM
*              DYNALLOC MIT AL2(0) ZURUECK-GEGEBEN.
*              BEISPIEL:
*              SYSLIB DSD TYPE=INFO,RETMB=MBNM
*                     LH  R1,MBNM      LAENGE MEMBERNAME
*                     LTR R1,R1        MEMBERNAME VORHANDEN
*                     BNP KEIN MEMBERNAME
*                     BCTR R1,0        LAENGE-1 FUER MVC
*                     EX   R1,MVC      MOVE MEMBERNAMEN IN AREH
*              MVC    MVC  MBNME(1),MBNM+2       EX-MVC
*              MBNME  DC   CL8' '
*
*  DISP=(DISP1,DISP2,DISP3)
*
*  DISP1       RETDSP1=EQU-NAME
*              NACH DURCHLAUF DES DYNALLOC-SVCS ENTHAELT DAS DURCH DEN
*              ANGEGEBENEN EQU-NAMEN ADRESSIERTE FELD EIN AL2(1)-LAEN-
*              GENFELD UND UNMITTELBAR ANSCHLIESSEND EIN 1 BYTE-FELD
*              MIT DER DISP1-ANGABE.ZUR ERLEICHTERUNG DER AUSWERTUNG
*              WERDEN AUTOMATISCH EQUS FUER OLD/MOD/NEW/SHR GENERIERT.
*              SIE KOENNEN IN CLI-INSTRUKTIONEN VERWENDET WERDEN.
*              BEISPIEL:
*              SYSLIB DSD  TYPE=INFO,RETDSP1=DISP1
*              DSP1   EQU  DISP1+2,1   SKIP LAENGENFELD
*                     CLI  DSP1,OLD    DISP=OLD
*                     BE   ... DISP=OLD
*                     CLI  DSP1,MOD    DISP=MOD
*                     BE   ... DISP=MOD
*
* DISP2        RETDSP2=EQU-NAME
*              HIER GILT SINNGEMAESS DAS GLEICHE WIE FUER RETDSP1.EINE
*              AUTOMATISCHE EQU-GENERIERUNG IST NICHT IMPLEMENTIERT.SIE
*              MUSS,FALLS ERFORDERLICH,VOM PROGRAMMIERER VORGENOMMEN
*              WERDEN.(SIEHE BEISPIEL)
*              BEISPIEL:
*              SYSIN   DSD TYPE=INFO,RETDSP2=DISP2
*              DSP2    EQU DISP2+2,1   SKIP LAENGENFELD
*              UNCATLG EQU X'01'       DISP=(,UNCATLG)
*              CATLG   EQU X'02'       DISP=(,CATLG)
*              DELETE  EQU X'04'       DISP=(,DELETE)
*              KEEP    EQU X'08'       DISP=(,KEEP)
*              PASS    EQU X'10'       DISP=(,PASS)
*                      CLI DSP2,KEEP
*                      BE  ... DISP=(,KEEP)
*                      CLI DSP2,PASS
*                      BE  ... DISP=(,PASS)
*                      USW.
*
*  DISP3       RETDSP3=EQU-NAME
*              DIE RUECKGABEWERTE SIND IDENTISCH MIT RETDSP2. DIE DORT
*              VERWENDETEN EQUS KOENNEN AUCH HIER BENUTZT WERDEN.
*              BEISPIEL: (EQUS VON DISP2 WERDEN BENUTZT)
*              SYSUT1  DSD TYPE=INFO,RETDSP3=DISP3
*              DSP3    EQU DISP3+2,1   SKIP LAENGENFELD
*                      CLI DSP3,UNCATLG
*                      BE  ... DISP=(,,UNCATLG)
*                      USW.
*
*  DSORG       RTDSORG=EQU-NAME
*              BESCHREIBUNG SIEHE RTDSORG-PARAMETER BEI TYPE=ALLOC
*
*  ANZAHL FREE VOR NEUEM ALLOC - TEST,OB ALLOC MOEGLICH
*              RETLIM=EQU-NAME
*              DIE RUECKGABE DER ANZAHL ERFOLGT IN EINEM AL2-FELD ALS
*              BINAERES HALBWORT.DAS OBLIGATE LAENGENFELD ENTHAELT
*              AL2(2).
*              BEISPIEL:
*                   DSD TYPE=INFO,RETLIM=LIMIT
*              LIM  EQU LIMIT+2,2      SKIP LAENGENFELD
*                   LH  R1,LIM
*                   LTR R1,R1          DYNALLOC MOEGLICH
*                   BNZ .. KEIN DYNALLOC MOEGLICH
*
*  DYNALLOC-ATTRIBUTE:
*              RETDYNA=EQU-NAME
*              DIE RUECKGABE ERFOLGT ALS EINZELNE BITS IN EINEM 1-BYTE-
*              FELD.DAS OBLIGATE 2-BYTE-LAENGENFELD ENTHAELT AL2(1).
*              FUER DIE BITS WERDEN VOM DSD-MAKRO KEINE EQUS DEFINIERT.
*              DAS BEISPIEL ENTHAELT JEDOCH EINE EQU-DEFINITION FUER
*              ALLE DEFINIERTEN BITS.DIE RESTLICHEN BITS SIND
*              'RESERVED'.
*              BEISPIEL:
*              *     EQU DEFINITIONEN DER BITS
*              PERCONC  EQU X'80'      '1',WENN PERMANENT CONCATENATED
*              INUSE    EQU X'40'      '1',WENN ALLOCATION IN USE
*              PERALC   EQU X'20'      '1',WENN PERMANENT ALLOCATED
*              CONVERT  EQU X'10'      '1',WENN CONVERTIBLE ALLOCATION
*              DYNALC   EQU X'08'      '1',WENN DYNAMISCH ALLOCATED
*              *
*              SYSLIB DSD TYPE=INFO,RETDYNA=ATTRIB
*              ATTR   EQU ATTRIB+2,1   SKIP LAENGENFELD
*              *
*              *      TEST,OB PERMANENT ALLOCATED UND IN USE
*              *
*                     TM   ATTR,PERALC+INUSE
*                     BO   ... BEDINGUNG ERFUELLT
*
*  LAST-ENTRY IN DYNALLOC-ENTRIES
*              RETLENT=EQU-NAME
*              DIE RUECKGABE ERFOLGT IN EINEM 1-BYTE-FELD.DAS OBLIGATE
*              LAENGENFELD ENTHAELT AL2(1).ES WERDEN VOM DSD-MAKRO KEI-
*              NE EQUS FUER DEN DATENWERT GENERIERT. DAS BEISPIEL ENT-
*              HAELT EQU-DEFINITIONEN FUER DIE BEIDEN MOEGLICHEN RUECK-
*              GABEWERTE.DER RETLENT-PARAMETER ERMOEGLICHT ZUSAMMEN
*              MIT DEM RLNOLOC-PARAMETER DAS SEQUENTIELLE UNTERSUCHEN
*              ALLER VORHANDENEN ALLOCATIONS.
*              BEISPIEL:
*              *      EQU-DEFINITIONEN FUER MOEGLICHE RUECKGABEWERTE:
*              LASTENT EQU X'80'       WENN LETZTER ALLOC-ENTRY
*              OTHER   EQU X'00'       SONSTIGE ENTRIES
*              *
*                      DSD TYPE=INFO,RETLENT=LASTENTR,RLNOLOC=.....
*              LAST    EQU LASTENTR+2  SKIP LAENGENFELD
*                      CLI LAST,LASTENT      LETZTER ENTRY
*                      BE  LETZTER ENTRY
*
*  DATASET-TYPE - DUMMY,TERM=TS,SYSIN,SYSOUT,SONSTIGE
*              RETDSTY=EQU-NAME
*              DIE RUECKGABE ERFOLGT IN EINEM 1-BYTE-FELD.DAS OBLIGATE
*              LAENGENFELD ENTHAELT AL2(1).ES WERDEN VOM DSD-MAKRO
*              KEINE EQUS FUER DEN DATENWERT GENERIERT. DAS BEISPIEL
*              ENTHAELT EQUS FUER DIE MOEGLICHEN RUECKGABE-WERTE.
*              BEISPIEL:
*              *        EQU-DEFINITIONEN FUER DATENWERTE
*              DUMMY    EQU X'80'      DUMMY-ALLOCATION
*              TERM     EQU X'40'      TERM=TS-ALLOCATION
*              SYSIN    EQU X'20'      SYSIN-FILE
*              SYSOUT   EQU X'10'      SYSOUT-FILE
*              SONST    EQU X'00'      SONSTIGE ALLOCATION
*              *
*              SYSUT1   DSD TYPE=INFO,RETDSTY=TYPE
*              TYP      EQU TYPE+2,1   SKIP LAENGENFELD
*                       CLI  TYP,DUMMY           DUMMY-FILE
*                       BE   DUMMYFI             JA
*                       CLI  TYP,TERM            TERM=TS-FILE
*                       BE   TERMTS              JA
*                       USW.
**********************************************************************
         MEXIT
.DOC100  ANOP
.**********************************************************************
.*       ZUWEISUNGEN UND PRUEFUNGEN
.**********************************************************************
&IHB     SETC  'IHB'.'&SYSNDX'(2,3)
&CNT     SETA  0
&CT      SETA  0
&CT1     SETA  0
&DSP1EQ  SETB  0
&HEX     SETC  '0123456789ABCDEF'
.*
.*       PRUEFEN AUF TYPE=ALLOC
.*
         AIF   ('&TYPE' NE 'ALLOC').FR100
&TYP   SETC  'X''01'''
.**********************************************************************
.*       DDNAME - TEST UND GENERIERUNG
.**********************************************************************
         AIF   ('&DDNM' EQ '' AND '&DDNAME' EQ '').DD100
         AIF   ('&DDNAME' EQ '').DD050
         AIF   ('&DDNM' EQ '').DD010
         MNOTE 8,'DDNAME-LABEL UND DDNAME-PARAMETER ANGEGEBEN,DDNAME-LAB
               BEL IGNORIERT'
.DD010   ANOP
&LNG     SETA  K'&DDNAME
&P       SETC  '&DDNAME'
         AGO   .DD090
.DD050   ANOP
&LNG     SETA  K'&DDNM
&P       SETC  '&DDNM'
.DD090   ANOP
&CNT     SETA  &CNT+1                  ZAEHLER ERHOEHEN
&TPT(&CNT) SETC '&IHB'.'AA'           TEXTPOINTER FUER DDNAME
&TXT(&CNT) SETC 'X''0001'',AL2(1),AL2('.'&LNG'.'),C'''
&TXT(&CNT) SETC '&TXT(&CNT)'.'&P'.''''
.DD100   ANOP
.**********************************************************************
.*       SYSOUT=(CLASS,SYSOUT-PROGRAMM,FORMS-NAME)
.**********************************************************************
&I       SETA  N'&SYSOUT                ANZAHL PARAMETER
         AIF   (&I EQ 0).DD200          SYSOUT FEHLT
         AIF   ('&SYSOUT(1)' EQ '').DD110     CLASS FEHLT
&CNT     SETA  &CNT+1                   ZAEHLER+1
&TPT(&CNT) SETC '&IHB'.'AB'           TEXTPOINTER SYSOUT-CLASS
&TXT(&CNT) SETC 'X''0018'',AL2(1),AL2(1),CL1'''.'&SYSOUT(1)'
&TXT(&CNT) SETC '&TXT(&CNT)'.''''
.DD110   ANOP
         AIF   (&I LT 2).DD200         NUR 1 PARAMETER
         AIF   ('&SYSOUT(2)' EQ '').DD120        2.PARAMETER FEHLT
&CNT     SETA  &CNT+1
&LNG     SETA  K'&SYSOUT(2)
&TPT(&CNT) SETC '&IHB'.'AC'           TEXTPOINTER SYSOUT-PGM
&TXT(&CNT) SETC 'X''0019'',AL2(1),AL2('.'&LNG'.'),C'''
&TXT(&CNT) SETC '&TXT(&CNT)'.'&SYSOUT(2)'.''''
.DD120   ANOP
         AIF   (&I LT 3).DD200         NUR 2 PARAMETER
         AIF   ('&SYSOUT(3)' EQ '').DD200        3.PARAMETER FEHLT
&CNT     SETA  &CNT+1
&LNG     SETA  K'&SYSOUT(3)
&TPT(&CNT) SETC '&IHB'.'AD'           FORMS-NAME-TEXT-POINTER
&TXT(&CNT) SETC 'X''001A'',AL2(1),AL2('.'&LNG'.'),C'''
&TXT(&CNT) SETC '&TXT(&CNT)'.'&SYSOUT(3)'.''''
.DD200   ANOP
.**********************************************************************
.*       DSNAME - GENERIERUNG
.**********************************************************************
         AIF ('&DSN' EQ '').DD300      DSN NICHT ANGEGEBEN
&LNG     SETA  K'&DSN
.*
.*       MEMBERNAMEN PRUEFEN
.*
         AIF   ('&DSN'(&LNG,1) NE ')').DD250
&I       SETA  &LNG-1                            ANFANG MEMBERNAMEN
.DD210   ANOP                                    SUCHEN='('
         AIF   ('&DSN'(&I,1) EQ '(').DD220
&I       SETA  &I-1
         AIF   (&I EQ 0).ERR10                   '(' NICHT GEFUNDEN
         AGO   .DD210
.DD220   ANOP
.*
.*       CODE FUER MEMBERNAMEN GENERIEREN
.*
&CNT     SETA  &CNT+1
&LNG     SETA  &LNG-&I-1
&TPT(&CNT) SETC '&IHB'.'AE'            MEMBER-POINTER
&TXT(&CNT) SETC 'X''0003'',AL2(1),AL2('.'&LNG'.'),C'''
&TXT(&CNT) SETC '&TXT(&CNT)'.'&DSN'(&I+1,&LNG).''''
.*
.*       CODE FUER DSNAME MIT ANHAENGENDEM MEMBER GENERIEREN
.*
&CNT     SETA  &CNT+1
&LNG     SETA  &I-1
&TPT(&CNT) SETC '&IHB'.'AF'
&TXT(&CNT) SETC 'X''0002'',AL2(1),AL2('.'&LNG'.'),C'''
&TXT(&CNT) SETC '&TXT(&CNT)'.'&DSN'(1,&LNG).''''
         AGO   .DD300
.*
.*       CODE FUER DSNAMEN OHNE ANHAENGENDEN MEMBERNAMEN GENERIEREN
.*
.DD250   ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AF'
&TXT(&CNT) SETC 'X''0002'',AL2(1),AL2('.'&LNG'.'),C'''
&TXT(&CNT) SETC '&TXT(&CNT)'.'&DSN'.''''
.**********************************************************************
.*       DISP=(NEW,KEEP,KEEP)
.**********************************************************************
.DD300   ANOP
&I       SETA  N'&DISP                 ANZAHL DISP-PARAMETER
         AIF   (&I EQ 0).DD600         DISP FEHLT
         AIF   ('&DISP(1)' NE '' AND '&DISP(1)' NE 'NEW').DD310
&PARM    SETC  'X''04'''
         AGO   .DD350
.DD310   ANOP
         AIF   ('&DISP(1)' NE 'OLD').DD320
&PARM    SETC  'X''01'''
         AGO   .DD350
.DD320   ANOP
         AIF   ('&DISP(1)' NE 'MOD').DD330
&PARM    SETC  'X''02'''
         AGO   .DD350
.DD330   ANOP
         AIF   ('&DISP(1)' NE 'SHR').ERR20
&PARM    SETC  'X''08'''
.DD350   ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AG'
&TXT(&CNT) SETC 'X''0004'',AL2(1),AL2(1),'.'&PARM'
.*
.*       DISP 2.PARAMETER  UNCATLG,CATLG,DELETE,KEEP
.*
         AIF   (&I LT 2).DD600         2.+ 3. PARAMETER FEHLT
         AIF   ('&DISP(2)' EQ '').DD500    2. PARAMETER FEHLT
         AIF   ('&DISP(2)' NE 'UNCATLG').DD410
&PARM    SETC  'X''01'''
         AGO   .DD450
.DD410   ANOP
         AIF   ('&DISP(2)' NE 'CATLG').DD420
&PARM    SETC  'X''02'''
         AGO   .DD450
.DD420   ANOP
         AIF   ('&DISP(2)' NE 'DELETE').DD430
&PARM    SETC  'X''04'''
         AGO   .DD450
.DD430   ANOP
         AIF   ('&DISP(2)' NE 'KEEP').ERR30
&PARM    SETC  'X''08'''
.DD450   ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AH'
&TXT(&CNT) SETC 'X''0005'',AL2(1),AL2(1),'.'&PARM'
.*
.*       DISP 3.PARAMETER      UNCATLG,CATLG,DELETE,KEEP
.*
.DD500   ANOP
         AIF   (&I LT 3).DD600         3.PARAMETER FEHLT
         AIF   ('&DISP(3)' EQ '').DD600
         AIF   ('&DISP(3)' NE 'UNCATLG').DD510
&PARM    SETC  'X''01'''
         AGO   .DD550
.DD510   ANOP
         AIF   ('&DISP(3)' NE 'CATLG').DD520
&PARM    SETC  'X''02'''
         AGO   .DD550
.DD520   ANOP
         AIF   ('&DISP(3)' NE 'DELETE').DD530
&PARM    SETC  'X''04'''
         AGO   .DD550
.DD530   ANOP
         AIF   ('&DISP(3)' NE 'KEEP').ERR40
&PARM    SETC  'X''08'''
.DD550   ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AI'
&TXT(&CNT) SETC 'X''0006'',AL2(1),AL2(1),'.'&PARM'
.DD600   ANOP
.**********************************************************************
.*       SPACE=(TRK/CYL/BLKLNG,(PRIM,SEK,DIR),RLSE,CONTIG,ROUND)
.**********************************************************************
.*
.*       TRK/CYL/BLKLNG
.*
&I       SETA  N'&SPACE
         AIF   (&I EQ 0).DD1300
&P       SETC  '&SPACE(1)'
         AIF   ('&P' NE '').DD610
         MNOTE 8,'1.PARAMETER SPACE FEHLT,SPACE NICHT GENERIERT'
         AGO   .DD1300
.DD610   ANOP
         AIF   ('&P' NE 'TRK').DD620
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AJ'
&TXT(&CNT) SETC 'X''0007'',AL2(0)'
         AGO   .DD700
.DD620   ANOP
         AIF   ('&P' NE 'CYL').DD630
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AK'
&TXT(&CNT) SETC 'X''0008'',AL2(0)'
         AGO   .DD700
.DD630   ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AL'
&TXT(&CNT) SETC 'X''0009'',AL2(1),AL2(3),AL3('.'&P'.')'
.DD700   ANOP
.*
.*       PRIMARY SPACE-PARAMETER
.*
&PARM    SETC  '&SPACE(2)'
         AIF   ('&PARM' NE '').DD710
         MNOTE 8,'2.PARAMETER SPACE FEHLT,SPACE NICHT GENERIERT'
         AGO   .DD1300
.DD710   ANOP
&LNG     SETA  K'&PARM                 LAENGE PARM
         AIF   ('&PARM'(1,1) EQ '(').DD720
&P       SETC  '&PARM'                 OHNE KLAMMERN NUR 1 PARAMETER
&PARM    SETC  ''                      FUER FOLGEABFRAGEN
         AGO   .DD790
.DD720   ANOP
&LNG     SETA  &LNG-2                  LAENGE OHNE KLAMMERN
&PARM    SETC  '&PARM'(2,&LNG)         KLAMMERN ENTFERNEN
&J       SETA  0                       ZAEHLVARIABLE
.DD730   ANOP
&J       SETA  &J+1
         AIF   (&J GT &LNG).DD740      KEIN KOMMA GEFUNDEN
         AIF   ('&PARM'(&J,1) EQ ',').DD750
         AGO   .DD730                  WEITERSUCHEN
.DD740   ANOP
&P       SETC  '&PARM'                 NUR 1 PARAMETER IN KLAMMER
&PARM    SETC  ''                      FUER WEITERE ABFRAGEN
         AGO   .DD790
.DD750   ANOP
&P       SETC  '&PARM'(1,&J-1)         TEIL VOR DEM KOMMA
&PARM    SETC  '&PARM'(&J+1,&LNG-&J)    TEIL NACH DEM KOMMA
.DD790   ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AM'
&TXT(&CNT) SETC 'X''000A'',AL2(1),AL2(3),AL3('.'&P'.')'
.*
.*       SECONDARY SPACE - PARAMETER
.*
.DD800   ANOP
         AIF   ('&PARM' EQ '').DD1000      2.+3.PARAMETER FEHLT
&LNG     SETA  K'&PARM
&J       SETA  0                       SCHLEIFEN-ZAEHLER
.DD810   ANOP
&J       SETA  &J+1
         AIF   (&J GT &LNG).DD820
         AIF   ('&PARM'(&J,1) EQ ',').DD830
         AGO   .DD810
.DD820   ANOP
&P       SETC  '&PARM'                 KEIN KOMMA GEFUNDEN
&PARM    SETC  ''                      SPAETERE ABFRAGE
         AGO   .DD890
.DD830   ANOP
&P       SETC  '&PARM'(1,&J-1)         TEIL VOR DEM KOMMA
&PARM    SETC  '&PARM'(&J+1,&LNG-&J)
.DD890   ANOP
         AIF   ('&P' EQ '').DD900
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AN'
&TXT(&CNT) SETC 'X''000B'',AL2(1),AL2(3),AL3('.'&P'.')'
.*
.*       DIRECTORY SPACE - PARAMETER
.*
.DD900   ANOP
         AIF   ('&PARM' EQ '').DD1000        DIR - PARAMETER FEHLT
&LNG     SETA  K'&PARM
&J       SETA  0                       SCHLEIFEN-ZAEHLER
.DD910   ANOP
&J       SETA  &J+1
         AIF   (&J GT &LNG).DD920
         AIF   ('PARM'(&J,1) EQ ',').DD930
         AGO   .DD910
.DD920   ANOP
&P       SETC  '&PARM'                 KEIN KOMMA GEFUNDEN
         AGO   .DD990
.DD930   ANOP
&P       SETC  '&PARM'(1,&J-1)         TEIL VOR DEM KOMMA
.DD990   ANOP
         AIF   ('&P' EQ '').DD1000
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AO'
&TXT(&CNT) SETC 'X''000C'',AL2(1),AL2(3),AL3('.'&P'.')'
.*
.*       RLSE - PARAMETER
.*
.DD1000  ANOP
         AIF   (&I LT 3).DD1300        RESTLICHE PARAMETER FEHLEN
&P       SETC  '&SPACE(3)'
         AIF   ('&P' EQ '').DD1100    RLSE FEHLT
         AIF   ('&P' EQ 'RLSE').DD1010
         MNOTE  8,'RLSE-PARAMETER IM SPACE FALSCH: &P ,IGNORIERT'
         AGO   .DD1100
.DD1010  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AP'
&TXT(&CNT) SETC 'X''000D'',AL2(0)'
.*
.*       CONTIG/MXIG/ALX - PARAMETER
.*
.DD1100  ANOP
         AIF   (&I LT 4).DD1300        RESTLICHE PARAMETER FEHLEN
&P       SETC  '&SPACE(4)'
         AIF   ('&P' EQ '').DD1200     PARAMETER FEHLT
         AIF   ('&P' NE 'CONTIG').DD1110
&PARM    SETC  'X''08'''
         AGO   .DD1150
.DD1110  ANOP
         AIF   ('&P' NE 'MXIG').DD1120
&PARM    SETC  'X''04'''
         AGO   .DD1150
.DD1120  ANOP
         AIF   ('&P' NE 'ALX').DD1130
&PARM    SETC  'X''02'''
         AGO   .DD1150
.DD1130  ANOP
         MNOTE 8,'FALSCHE CONTIG/MXIG/ALX-ANGABE IN SPACE: &P ,IGNORIER*
               T'
         AGO   .DD1200
.DD1150  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AQ'
&TXT(&CNT) SETC 'X''000E'',AL2(1),AL2(1),'.'&PARM'
.*
.*       ROUND - PARAMETER
.*
.DD1200  ANOP
         AIF   (&I LT 5).DD1300        ROUND FEHLT
&P       SETC  '&SPACE(5)'
         AIF   ('&P' EQ '').DD1300     ROUND FEHLT
         AIF   ('&P' EQ 'ROUND').DD1210
         MNOTE 8,'ROUND-PARAMETER IN SPACE FALSCH: &P ,IGNORIERT'
         AGO   .DD1300
.DD1210  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AR'
&TXT(&CNT) SETC 'X''000F'',AL2(0)'
.**********************************************************************
.*       VOL=SER - PARAMETER        LISTE VON VOLUMEN-NAMEN
.**********************************************************************
.DD1300  ANOP
&I       SETA  N'&VOLSER
         AIF   (&I EQ 0).DD1400        VOLSER NICHT ANGEGEBEN
&J       SETA  1
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AS'
&P         SETC 'X''0010'',AL2('.'&I'.')'
.*
.DD1310  ANOP                          VOLSERS IN SCHLEIFE ABARBEITEN
&PARM    SETC  '&VOLSER(&J)'
&LNG     SETA  K'&PARM
&P       SETC  '&P'.',AL2('.'&LNG'.'),C'''.'&PARM'.''''
&J       SETA  &J+1
         AIF   (&J LE &I).DD1310
&TXT(&CNT) SETC '&P'
.**********************************************************************
.*       UNIT - PARAMETER (3350,UNIT-COUNT)
.**********************************************************************
.DD1400  ANOP
&I       SETA  N'&UNIT
         AIF   (&I EQ 0).DD1500        UNIT NICHT ANGEGEBEN
.*
.*       UNIT - 1.PARAMETER  SYSDA / 3350 / 384
.*
&P       SETC  '&UNIT(1)'
         AIF   ('&P' EQ '').DD1410     1.PARAMETER FEHLT
&LNG     SETA  K'&P
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AT'
&TXT(&CNT) SETC 'X''0015'',AL2(1),AL2('.'&LNG'.'),C'''.'&P'.''''
.DD1410  ANOP
.*
.*       UNIT-PARAMETER 2  -  UNIT-COUNT
.*
         AIF   (&I LT 2).DD1500        RESTLICHE PARAMETER FEHLEN
&P       SETC  '&UNIT(2)'
         AIF   ('&P' EQ '').DD1420     PARAMETER FEHLT
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AU'
&TXT(&CNT) SETC 'X''0016'',AL2(1),AL2(1),AL1('.'&P'.')'
.DD1420  ANOP
.**********************************************************************
.*       LABEL=(LABEL-NR,SL/NL/BLP,NOPWREAD/PASSWORD,IN/OUT)
.**********************************************************************
.DD1500  ANOP
&I       SETA  N'&LABEL
         AIF   (&I EQ 0).DD1900        LABEL NICHT ANGEGEBEN
.*
.*       LABEL-PARAMETER  -  1.PARAMETER  - SEQUENCE-NUMBER
.*
&P       SETC  '&LABEL(1)'
         AIF   ('&P' EQ '').DD1510     SEQ-NR  FEHLT
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AV'
&TXT(&CNT) SETC 'X''001F'',AL2(1),AL2(2),AL2('.'&P'.')'
.DD1510  ANOP
.*
.*       LABEL-PARAMETER  -  2.PARAMETER  -  SL/NL/BLP/NSL/SUL/LTM/AL
.*                                                                /AUL
         AIF   (&I LT 2).DD1900        RESTLICHE PARAMETER FEHLEN
&P       SETC  '&LABEL(2)'
         AIF   ('&P' EQ '').DD1700     PARAMETER FEHLT
         AIF   ('&P' NE 'SL').DD1610
&PARM    SETC  'X''02'''
         AGO   .DD1690
.DD1610  ANOP
         AIF   ('&P' NE 'NL').DD1620
&PARM    SETC  'X''01'''
         AGO   .DD1690
.DD1620  ANOP
         AIF   ('&P' NE 'BLP').DD1630
&PARM    SETC  'X''10'''
         AGO   .DD1690
.DD1630  ANOP
         AIF   ('&P' NE 'NSL').DD1640
&PARM    SETC  'X''04'''
         AGO   .DD1690
.DD1640  ANOP
         AIF   ('&P' NE 'SUL').DD1650
&PARM    SETC  'X''0A'''
         AGO   .DD1690
.DD1650  ANOP
         AIF   ('&P' NE 'LTM').DD1660
&PARM    SETC  'X''21'''
         AGO   .DD1690
.DD1660  ANOP
         AIF   ('&P' NE 'AL').DD1670
&PARM    SETC  'X''40'''
         AGO   .DD1690
.DD1670  ANOP
         AIF   ('&P' NE 'AUL').DD1680
&PARM    SETC  'X''48'''
         AGO   .DD1690
.DD1680  ANOP
         MNOTE 8,'2.LABEL-PARAMETER NICHT KORREKT: &P ,IGNORIERT'
         AGO   .DD1700
.DD1690  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AW'
&TXT(&CNT) SETC 'X''001E'',AL2(1),AL2(1),'.'&PARM'
.*
.*       LABEL-PARAMETER - 3.PARAMETER - NOPWREAD/PASSWORD
.*
.DD1700  ANOP
         AIF   (&I LT 3).DD1900        RESTLICHE PARAMETER FEHLEN
&P       SETC  '&LABEL(3)'
         AIF   ('&P' EQ '').DD1800     PASSWORD-PARAMETER FEHLT
         AIF   ('&P' NE 'PASSWORD').DD1710
&PARM    SETC  'X''10'''
         AGO   .DD1790
.DD1710  ANOP
         AIF   ('&P' NE 'NOPWREAD').DD1720
&PARM    SETC  'X''30'''
         AGO   .DD1790
.DD1720  ANOP
         MNOTE 8,'3.LABEL-PARAMETER NICHT KORREKT: &P ,IGNORIERT'
         AGO   .DD1800
.DD1790  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AX'
&TXT(&CNT) SETC 'X''0020'',AL2(1),AL2(1),'.'&PARM'
.*
.*       LABEL-PARAMETER - 4.PARAMETER - IN/OUT
.*
.DD1800  ANOP
         AIF   (&I LT 4).DD1900        RESTLICHE PARAMETER FEHLEN
&P       SETC  '&LABEL(4)'
         AIF   ('&P' EQ '').DD1900     IN/OUT NICHT ANGEGEBEN
         AIF   ('&P' NE 'IN').DD1810
&PARM    SETC  'X''80'''
         AGO   .DD1890
.*
.DD1810  ANOP
         AIF   ('&P' NE 'OUT').DD1820
&PARM    SETC  'X''40'''
         AGO   .DD1890
.*
.DD1820  ANOP
         MNOTE 8,'4.LABEL-PARAMETER NICHT KORREKT: &P ,IGNORIERT'
         AGO   .DD1900
.DD1890  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AY'
&TXT(&CNT) SETC 'X''0021'',AL2(1),AL2(1),'.'&PARM'
.**********************************************************************
.*       FREE = CLOSE/END
.**********************************************************************
.DD1900  ANOP
&P       SETC  '&FREE'
         AIF   ('&P' EQ '').DD2000     NICHT ANGEGEBEN
         AIF   ('&P' EQ 'CLOSE').DD1990
         AIF   ('&P' EQ 'END').DD2000
         MNOTE 8,'FREE-PARAMETER INCORRECT: &P,CLOSE/END ERLAUBT,IGNORI*
               ERT'
         AGO   .DD2000
.DD1990  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AZ'
&TXT(&CNT) SETC 'X''001C'',AL2(0)'
.**********************************************************************
.*       OUTLIM      OUTPUT-LIMIT FUER SYSOUT-DATEIEN
.**********************************************************************
.DD2000  ANOP
&P       SETC  '&OUTLIM'
         AIF   ('&P' EQ '').DD2100     OUTLIM FEHLT
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BA'
&TXT(&CNT) SETC 'X''001B'',AL2(1),AL2(3),AL3('.'&P'.')'
.**********************************************************************
.*       COPIES      ANZAHL KOPIEN SYSOUT-DATEI
.**********************************************************************
.DD2100  ANOP
&P       SETC  '&COPIES'
         AIF   ('&P' EQ '').DD2200     COPIES FEHLT
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BB'
&TXT(&CNT) SETC 'X''001D'',AL2(1),AL2(1),AL1('.'&P'.')'
.**********************************************************************
.*       DEST        DESTINATION SYSOUT-DATEI   Z.B. RMT5
.**********************************************************************
.DD2200  ANOP
&P       SETC   '&DEST'
         AIF   ('&P' EQ '').DD2300     DEST FEHLT
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BC'
&LNG     SETA  K'&P
&TXT(&CNT) SETC 'X''0058'',AL2(1),AL2('.'&LNG'.'),C'''.'&P'.''''
.**********************************************************************
.*       DEN - PARAMETER  - SCHREIBDICHTE FUER MAGNETBAENDER -  0-4
.**********************************************************************
.DD2300  ANOP
&P       SETC  '&DEN'
         AIF   ('&P' EQ '').DD2400
         AIF   ('&P' NE '0').DD2310
&PARM    SETC  'X''03'''               7 TRK / 200 BPI
         AGO   .DD2390
.DD2310  ANOP
         AIF   ('&P' NE '1').DD2320
&PARM    SETC  'X''43'''               7 TRK / 556 BPI
         AGO   .DD2390
.DD2320  ANOP
         AIF   ('&P' NE '2').DD2330
&PARM    SETC  'X''83'''               7/9 TRK / 800 BPI
         AGO   .DD2390
.DD2330  ANOP
         AIF   ('&P' NE '3').DD2340
&PARM    SETC  'X''C3'''               9 TRK / 1600 BPI
         AGO   .DD2390
.DD2340  ANOP
         AIF   ('&P' NE '4').DD2350
&PARM    SETC  'X''D3'''               9 TRK / 6250 BPI
         AGO   .DD2390
.DD2350  ANOP
         MNOTE 8,'DEN-PARAMETER NICHT KORREKT: &P , IGNORIERT , 0-4 ZUL*
               AESSIG'
         AGO   .DD2400
.DD2390  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BD'
&TXT(&CNT) SETC 'X''003B'',AL2(1),AL2(1),'.'&PARM'
.**********************************************************************
.*       DSORG = VSAM/TQ/TX/GS/PO/POU/MQ/CQ/CX/DA/DAU/PS/PSU
.**********************************************************************
.DD2400  ANOP
&P       SETC  '&DSORG'
         AIF   ('&P' EQ '').DD2600     DSORG NICHT ANGEGEBEN
         AIF   ('&P' NE 'VSAM').DD2410
&PARM    SETC  'X''0008'''             VSAM
         AGO   .DD2590
.DD2410  ANOP
         AIF   ('&P' NE 'TQ').DD2420
&PARM    SETC  'X''0020'''             TCAM MESSAGE QUEUE
         AGO   .DD2590
.DD2420  ANOP
         AIF   ('&P' NE 'TX').DD2430
&PARM    SETC  'X''0040'''             TCAM LINE - GROUP
         AGO   .DD2590
.DD2430  ANOP
         AIF   ('&P' NE 'GS').DD2440
&PARM    SETC  'X''0080'''             GRAPHICS
         AGO   .DD2590
.DD2440  ANOP
         AIF   ('&P' NE 'PO').DD2450
&PARM    SETC  'X''0200'''             PARTITIONED DATASET
         AGO   .DD2590
.DD2450  ANOP
         AIF   ('&P' NE 'POU').DD2460
&PARM    SETC  'X''0300'''             PARTITIONED DATASET UNMOVABLE
         AGO   .DD2590
.DD2460  ANOP
         AIF   ('&P' NE 'MQ').DD2470
&PARM    SETC  'X''0400'''             TCAM MSG PROZESSING QUEUE
         AGO   .DD2590
.DD2470  ANOP
         AIF   ('&P' NE 'CQ').DD2480
&PARM    SETC  'X''0800'''             TCAM PLATTEN-QUEUE
         AGO   .DD2590
.DD2480  ANOP
         AIF   ('&P' NE 'CX').DD2490
&PARM    SETC  'X''1000'''             TCAM/BTAM LINE GROUP
         AGO   .DD2590
.DD2490  ANOP
         AIF   ('&P' NE 'DA').DD2500
&PARM    SETC  'X''2000'''             BDAM
         AGO   .DD2590
.DD2500  ANOP
         AIF   ('&P' NE 'DAU').DD2510
&PARM    SETC  'X''2100'''             BDAM UNMOVABLE
         AGO   .DD2590
.DD2510  ANOP
         AIF   ('&P' NE 'PS').DD2520
&PARM    SETC  'X''4000'''             BSAM/QSAM
         AGO   .DD2590
.DD2520  ANOP
         AIF   ('&P' NE 'PSU').DD2530
&PARM    SETC  'X''4100'''             BSAM/QSAM UNMOVABLE
         AGO   .DD2590
.DD2530  ANOP
         MNOTE 8,'DSORG NICHT KORREKT ANGEGEBEN: &P ,IGNORIERT'
         AGO   .DD2600
.DD2590  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BE'
&TXT(&CNT) SETC 'X''003C'',AL2(1),AL2(2),'.'&PARM'
.**********************************************************************
.*       RECFM = F/V/U + M/A + S + B + T
.**********************************************************************
.DD2600  ANOP
&PARM    SETC  '&RECFM'
         AIF   ('&PARM' EQ '').DD2800     RECFM FEHLT
&LNG     SETA  K'&PARM                 LAENGE STRING
&J       SETA  0                       LAUFVARIABLE
&X80     SETA  0
&X40     SETA  0
&X20     SETA  0
&X10     SETA  0
&X08     SETA  0
&X04     SETA  0
&X02     SETA  0
&X01     SETA  0
.DD2610  ANOP
&J       SETA  &J+1                    J=J+1
         AIF   (&J GT &LNG).DD2790     ALLES ABGEARBEITET
&P       SETC  '&PARM'(&J,1)           1 ZEICHEN ISOLIEREN
         AIF   ('&P' NE 'F').DD2620
         AIF   (&X80 EQ 1 OR &X40 EQ 1).DD2615
&X80     SETA  1
         AGO   .DD2610
.DD2615  ANOP
         MNOTE 8,'RECFM-PARAMETER INCORRECT,F/V/U SCHLIESSEN SICH GEGEN*
               EINANDER AUS. &P IGNORIERT'
         AGO   .DD2610
.DD2620  ANOP
         AIF   ('&P' NE 'V').DD2630
         AIF   (&X80 EQ 1 OR &X40 EQ 1).DD2615
&X40     SETA  1
         AGO   .DD2610
.DD2630  ANOP
         AIF   ('&P' NE 'U').DD2640
         AIF   (&X80 EQ 1 OR &X40 EQ 1).DD2615
&X80     SETA  1
&X40     SETA  1
         AGO   .DD2610
.DD2640  ANOP
         AIF   ('&P' NE 'M').DD2650
         AIF   (&X04 EQ 1).DD2645
&X02     SETA  1
         AGO   .DD2610
.DD2645  ANOP
         MNOTE 8,'RECFM=A UND RECFM=M ANGEGEBEN, &P IGNORIERT'
         AGO   .DD2610
.DD2650  ANOP
         AIF   ('&P' NE 'A').DD2660
         AIF   (&X02 EQ 1).DD2645
&X04     SETA  1
         AGO   .DD2610
.DD2660  ANOP
         AIF   ('&P' NE 'S').DD2670
&X08     SETA  1
         AGO   .DD2610
.DD2670  ANOP
         AIF   ('&P' NE 'B').DD2680
&X10     SETA  1
         AGO   .DD2610
.DD2680  ANOP
         AIF   ('&P' NE 'T' AND '&P' NE 'D').DD2690
&X20     SETA  1
         AGO   .DD2610
.DD2690  ANOP
         MNOTE 8,'RECFM=&P FALSCH ODER NICHT IMPLEMENTIERT,IGNORIERT'
         AGO   .DD2610
.DD2790  ANOP
&J       SETA  1+8*&X80+4*&X40+2*&X20+&X10
&I       SETA  1+8*&X08+4*&X04+2*&X02+&X01
&P       SETC  '&HEX'(&J,1).'&HEX'(&I,1)
&P       SETC  'X'''.'&P'.''''
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BF'
&TXT(&CNT) SETC 'X''0049'',AL2(1),AL2(1),'.'&P'
.**********************************************************************
.*       LRECL= X ODER SATZLAENGE
.**********************************************************************
.DD2800  ANOP
&P       SETC  '&LRECL'
         AIF   ('&P' EQ '').DD2900     LRECL FEHLT
         AIF   ('&P' NE 'X').DD2810
&PARM    SETC  'X''8000'''
         AGO   .DD2890
.DD2810  ANOP
&PARM    SETC  'AL2('.'&P'.')'
.DD2890  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BG'
&TXT(&CNT) SETC 'X''0042'',AL2(1),AL2(2),'.'&PARM'
.**********************************************************************
.*       BLKSIZE = BLOCKLAENGE
.**********************************************************************
.DD2900  ANOP
&P       SETC  '&BLKSIZE'
         AIF   ('&P' EQ '').DD3000     BLKSIZE FEHLT
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BH'
&TXT(&CNT) SETC 'X''0030'',AL2(1),AL2(2),AL2('.'&P'.')'
.DD3000  ANOP
.**********************************************************************
.*       RETDDN - RETURN DDNAMEN
.**********************************************************************
&P       SETC  '&RETDDN'
         AIF   ('&P' EQ '').DD3100     RETDDN FEHLT
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'BI'
&TXT(&CNT) SETC 'X''0055'',AL2(1),AL2(8),CL8'' '''
&PTEQ(&CT) SETC '&P'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC '&P'.',=AL2(8)'
.**********************************************************************
.*       RETDSN - RETURN DSNAME
.**********************************************************************
.DD3100  ANOP
&P       SETC  '&RETDSN'
         AIF   ('&P' EQ '').DD3200     RETDSN FEHLT
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'BJ'
&TXT(&CNT) SETC 'X''0056'',AL2(1),AL2(44),CL44'' '''
&PTEQ(&CT) SETC '&P'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC '&P'.',=AL2(44)'
.**********************************************************************
.*       RETDSORG - RETURN DSORG
.**********************************************************************
.DD3200  ANOP
&P       SETC  '&RTDSORG'
         AIF   ('&P' EQ '').DD3300     RETDSORG FEHLT
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'BK'
&TXT(&CNT) SETC 'X''0057'',AL2(1),AL2(2),X''0000'''
&PTEQ(&CT) SETC '&P'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC '&P'.',=AL2(2)'
         AIF   (&DSOREQG).DD3300       EQUS BEREITS GENERIERT
&DSOREQG SETB  1
&DSOREQ  SETB  1
.DD3300  ANOP
.**********************************************************************
.*       DSNLOC - DSNAMEN IN TEXTBLOCK MOVEN
.**********************************************************************
&P       SETC  '&DSNLOC'
         AIF   ('&P' EQ '').DD3400     NICHT ANGEGEBEN
         AIF   ('&P'(1,1) NE '(').DD3310
&P       SETC  '0'.'&P'                0(REG)
.DD3310  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BL'
&TXT(&CNT) SETC 'X''0002'',AL2(1),AL2(44),CL44'' '''
&CT1     SETA  &CT1+1
&NM1(&CT1) SETC '&P'
&NM2(&CT1) SETC '&IHB'.'BM'
&NM3(&CT1) SETC '&TPT(&CNT)'
.DD3400  ANOP
.**********************************************************************
.*       MBLOC - MEMBERNAMEN IN TEXTBLOCK MOVEN
.**********************************************************************
&P       SETC  '&MBLOC'
         AIF   ('&P' EQ '').DD3500     NICHT ANGEGEBEN
         AIF   ('&P'(1,1) NE '(').DD3410
&P       SETC  '0'.'&P'                0(REG)
.DD3410  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BN'
&TXT(&CNT) SETC 'X''0003'',AL2(1),AL2(8),CL8'' '''
&CT1     SETA  &CT1+1
&NM1(&CT1) SETC '&P'
&NM2(&CT1) SETC '&IHB'.'BO'
&NM3(&CT1) SETC '&TPT(&CNT)'
.DD3500  ANOP
.**********************************************************************
.*       DSP1LOC - DISP=OLD/MOD/NEW/SHR IN TEXTBLOCK MOVEN
.**********************************************************************
&P       SETC  '&DSP1LOC'
         AIF   ('&P' EQ '').DD3600     NICHT ANGEGEBEN
         AIF   ('&P'(1,1) NE '(').DD3510
&P       SETC  '0'.'&P'                0(REG)
.DD3510  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BP'
&TXT(&CNT) SETC 'X''0004'',AL2(1),AL2(1),X''00'''
&CT1     SETA  &CT1+1
&NM1(&CT1) SETC '&P'
&NM2(&CT1) SETC '&IHB'.'BQ'
&NM3(&CT1) SETC '&TPT(&CNT)'
         AIF   (&DSP1EQG).DD3600       EQUS BEREITS GENERIERT
&DSP1EQG SETB  1
&DSP1EQ  SETB  1
.DD3600  ANOP
.*********************************************************************
.*       SCLSLOC - SYSOUTKLASSE IN TEXTBLOCK MOVEN
.*********************************************************************
&P       SETC  '&SCLSLOC'
         AIF   ('&P' EQ '').DD3700     NICHT ANGEGEBEN
         AIF   ('&P'(1,1) NE '(').DD3610
&P       SETC  '0'.'&P'                0(REG)
.DD3610  ANOP
&I       SETA  N'&SYSOUT               PRUEFEN,OB TEXTBLOCK BEREITS
         AIF   (&I EQ 0).DD3620        GENERIERT
         AIF   ('&SYSOUT(1)' NE '').DD3630
.DD3620  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AB'            SYSOUT-CLASS-TEXTPOINTER
&TPT(&CNT) SETC 'X''0018'',AL2(1),AL2(1),CL1''A'''
.DD3630  ANOP
&CT1     SETA  &CT1+1
&NM1(&CT1) SETC '&P'
&NM2(&CT2) SETC '&IHB'.'BS'
&NM3(&CT3) SETC '&IHB'.'AB'
.DD3700  ANOP
.*********************************************************************
.*       MVEDDN .  WIE RETDDN,JEDOCH DDNAMEN CL8 IN ANGEGEB. AREA MOVEN
.*********************************************************************
&P       SETC  '&MVEDDN'
         AIF   ('&P' EQ '').DD3800     NICHT ANGEGEBEN
&CODE    SETB  1                       TRAILER GENERIEREN
         AIF   ('&RETDDN' NE '').DD3800     TEXT BEREITS GENERIERT
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'BI'
&TXT(&CNT) SETC 'X''0055'',AL2(1),AL2(8),CL8'' '''
.DD3800  ANOP
         AGO   .GEN100
.**********************************************************************
.*
.*       DYNAMIC UNALLOCATION
.*
.**********************************************************************
.FR100   ANOP
         AIF   ('&TYPE' NE 'FREE').IN100
         MNOTE 8,' TYPE = FREE NICHT IMPLEMENTIERT'
         MEXIT
.**********************************************************************
.*
.*       DYNAMIC INFORMATION RETRIVAL
.*
.**********************************************************************
.IN100   ANOP
         AIF   ('&TYPE' NE 'INFO').TYPERR
&TYP     SETC  'X''07'''
.*********************************************************************
.*       DDNAME - TEST UND GENERIERUNG
.*********************************************************************
         AIF   ('&DDNM' EQ '' AND '&DDNAME' EQ '').IN200
         AIF   ('&DDNAME' EQ '').IN120
         AIF   ('&DDNM' EQ '').IN110
         MNOTE 8,'DDNAME-LABEL UND DDNAME-PARAMETER ANGEGEBEN,DDNAME-LA*
                BEL IGNORIERT'
.IN110   ANOP
&LNG     SETA  K'&DDNAME
&P       SETC  '&DDNAME'
         AGO   .IN190
.IN120   ANOP
&LNG     SETA  K'&DDNM
&P       SETC  '&DDNM'
.IN190   ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AA'
&TXT(&CNT) SETC 'X''0001'',AL2(1),AL2('.'&LNG'.'),C'''.'&P'.''''
.IN200   ANOP
.*********************************************************************
.*       DSNAME - GENERIERUNG
.*********************************************************************
         AIF   ('&DSN' EQ '').IN300
         AIF   ('&DDNM' NE '' OR '&DDNAME' NE '').IN210
         AIF   ('&RELNO' EQ '').IN220
.IN210   ANOP
         MNOTE 8,'DSN DARF FUER TYPE=INFO NICHT ZUSAMMEN MIT DDNAME ODE*
               R RELNO ANGEGEBEN WERDEN,DSN IGNORIERT'
         AGO   .IN300
.IN220   ANOP
&LNG     SETA  K'&DSN
.*
.*       MEMBERNAMEN PRUEFEN   (AM ENDE DSN EINE KLAMMER)
.*
         AIF   ('&DSN'(&LNG,1) NE ')').IN250
         MNOTE 8,'MEMBERANGABE FUER TYPE=INFO NICHT ERLAUBT,DSN WIRD IG*
               NORIERT'
         AGO   .IN300
.*
.*       DSNAME GENERIEREN
.*
.IN250   ANOP
&P       SETC  '&DSN'
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AB'
&TXT(&CNT) SETC 'X''0002'',AL2(1),AL2('.'&LNG'.'),C'''.'&P'.''''
.IN300   ANOP
.*********************************************************************
.*       RETURN  DDNAME
.*********************************************************************
         AIF   ('&RETDDN' EQ '').IN400
&P       SETC  '&RETDDN'
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AC'
&TXT(&CNT) SETC 'X''0004'',AL2(1),AL2(8),CL8'' '''
&PTEQ(&CT) SETC '&P'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC '&P'.',=AL2(8)'
.IN400   ANOP
.*********************************************************************
.*       RETURN DSNAME
.*********************************************************************
         AIF   ('&RETDSN' EQ '').IN500
&P       SETC  '&RETDSN'
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AD'
&TXT(&CNT) SETC 'X''0005'',AL2(1),AL2(44),CL44'' '''
&PTEQ(&CT) SETC '&P'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC '&P'.',=AL2(44)'
.IN500   ANOP
.*********************************************************************
.*       RETURN MEMBERNAME
.*********************************************************************
         AIF   ('&RETMB' EQ '').IN600
&P       SETC  '&RETMB'
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AE'
&TXT(&CNT) SETC 'X''0006'',AL2(1),AL2(8),CL8'' '''
&PTEQ(&CT) SETC '&P'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC '&P'.',=AL2(8)'
.IN600   ANOP
.*********************************************************************
.*       RETURN DISP=OLD/MOD/NEW/SHR
.*********************************************************************
         AIF   ('&RETDSP1' EQ '').IN700
&P       SETC  '&RETDSP1'
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AF'
&TXT(&CNT) SETC 'X''0007'',AL2(1),AL2(1),X''00'''
&PTEQ(&CT) SETC '&P'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC ''
         AIF   (&DSP1EQG).IN700        EQUS BEREITS GENERIERT
&DSP1EQG SETB  1
&DSP1EQ  SETB  1
.IN700   ANOP
.*********************************************************************
.*       RETURN NORMAL DISP=UNCATLG/CATLG/DELETE/KEEP/PASS
.*********************************************************************
         AIF   ('&RETDSP2' EQ '').IN800
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AG'
&TXT(&CNT) SETC 'X''0008'',AL2(1),AL2(1),X''00'''
&PTEQ(&CT) SETC '&RETDSP2'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC ''
.IN800   ANOP
.*********************************************************************
.*       RETURN ABNORMAL DISP=UNCATLG/CATLG/DELETE/KEEP/PASS
.*********************************************************************
         AIF   ('&RETDSP3' EQ '').IN900
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AH'
&TXT(&CNT) SETC 'X''0009'',AL2(1),AL2(1),X''00'''
&PTEQ(&CT) SETC '&RETDSP3'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC ''
.IN900   ANOP
.*********************************************************************
.*       RETURN DSORG=VSAM,TQ,TX,GS,PO,DA,PS,IS  USW
.*********************************************************************
         AIF   ('RTDSORG' EQ '').IN1000
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AI'
&TXT(&CNT) SETC 'X''000A'',AL2(1),AL2(2),X''0000'''
&PTEQ(&CT) SETC '&RTDSORG'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC ''
         AIF   (&DSOREQG).IN1000       EQUS BEREITS GENERIERT
&DSOREQG SETB  1
&DSOREQ  SETB  1
.IN1000  ANOP
.*********************************************************************
.*       RETURN ANZAHL FREE VOR NEUEM ALLOC (TEST,OB ALLOC MOEGLICH)
.*********************************************************************
         AIF   ('&RETLIM' EQ '').IN1100
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AJ'
&TXT(&CNT) SETC 'X''000B'',AL2(1),AL2(2),AL2(0)'
&PTEQ(&CT) SETC '&RETLIM'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC ''
.IN1100  ANOP
.*********************************************************************
.*       RETURN DYNALLOC-ATTRIBUTE:IN USE,PERMANENT ALLOC,CONCATENATED
.*********************************************************************
         AIF   ('&RETDYNA' EQ '').IN1200
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AK'
&TXT(&CNT) SETC 'X''000C'',AL2(1),AL2(1),X''00'''
&PTEQ(&CT) SETC '&RETDYNA'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC ''
.IN1200  ANOP
.*********************************************************************
.*       RETURN LAST-ENTRY SPEZIFIKATION / FUER RELNO-SPEZIFIKATIONEN
.*********************************************************************
         AIF   ('&RETLENT' EQ '').IN1300
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AL'
&TXT(&CNT) SETC 'X''000D'',AL2(1),AL2(1),X''00'''
&PTEQ(&CT) SETC '&RETLENT'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC ''
.IN1300  ANOP
.*********************************************************************
.*       RETURN DATASET-TYPE:DUMMY,TERM=TS,SYSIN,SYSOUT,SONSTIGE
.*********************************************************************
         AIF   ('&RETDSTY' EQ '').IN1400
&CNT     SETA  &CNT+1
&CT      SETA  &CT+1
&TPT(&CNT) SETC '&IHB'.'AM'
&TXT(&CNT) SETC 'X''000E'',AL2(1),AL2(1),X''00'''
&PTEQ(&CT) SETC '&RETDSTY'
&TXEQ(&CT) SETC '&TPT(&CNT)'.'+4,2'
&RMVC(&CT) SETC ''
.IN1400  ANOP
.*********************************************************************
.*         RELATIVE NUMMER STATT DDNAME ODER DSNAME ZUR ENTRY-DEFINI
.*********************************************************************
         AIF   ('&RELNO' EQ '').IN1500
         AIF   ('&DDNM' NE '' OR '&DDNAME' NE '').IN1410
         AIF   ('&DSN' EQ '').IN1420
.IN1410  MNOTE 8,'RELNO ZUSAMMEN MIT DDNAME/DSN ANGEGEBEN,RELNO IGNOR.'
         AGO   .IN1500
.IN1420  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AN'
&TXT(&CNT) SETC 'X''000F'',AL2(1),AL2(2),AL2('.'&RELNO'.')'
.IN1500  ANOP
.*********************************************************************
.*         RELATIVE NUMMER STATT DDNAME ODER DSNAME ZUR ENTRY-DEFINI
.*********************************************************************
&P       SETC  '&RLNOLOC'
         AIF   ('&P' EQ '').IN1600
         AIF   ('&RELNO' NE '').IN1510
         AIF   ('&DDNM' NE '' OR '&DDNAME' NE '').IN1520
         AIF   ('&DSN' EQ '').IN1530
.IN1510  MNOTE 8,'RELNO ZUSAMMEN RLNOLOC ANGEGEBEN,RLNOLOC IGNORIERT'
         AGO   .IN1600
.IN1520  MNOTE 8,'RLNOLOC ZUSAMMEN MIT DDNAME/DSN ANGEGEBEN,RLNOLOC IGNO
               ORIERT'
         AGO   .IN1600
.IN1530  ANOP
         AIF   ('&P'(1,1) NE '(').IN1540
&P       SETC  '0'.'&P'                0(REG)
.IN1540  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AN'
&TXT(&CNT) SETC 'X''000F'',AL2(1),AL2(2),AL2(0)'
&CT1     SETA  &CT1+1
&NM1(&CT1) SETC '&P'
&NM2(&CT1) SETC '&IHB'.'AO'
&NM3(&CT1) SETC '&TPT(&CNT)'
.IN1600  ANOP
.**********************************************************************
.*       DDNLOC - DDNAMEN IN TEXTBLOCK MOVEN
.**********************************************************************
&P       SETC  '&DDNLOC'
         AIF   ('&P' EQ '').IN1700     NICHT ANGEGEBEN
         AIF   ('&P'(1,1) NE '(').IN1610
&P       SETC  '0'.'&P'                0(REG)
.IN1610  ANOP
&CNT     SETA  &CNT+1
&TPT(&CNT) SETC '&IHB'.'AA'
&TXT(&CNT) SETC 'X''0001'',AL2(1),AL2(8),CL8'' '''
&CT1     SETA  &CT1+1
&NM1(&CT1) SETC '&P'
&NM2(&CT1) SETC '&IHB'.'AP'
&NM3(&CT1) SETC '&TPT(&CNT)'
.IN1700  ANOP
.**********************************************************************
.*
.*       CODE GENERIEREN
.*
.**********************************************************************
.GEN100  ANOP
*
*        BRANCH AROUND CONSTANTS
*
         CNOP  0,4
         BAL   R1,&IHB.A               LOAD PARMREGISTER
*
*        REQUEST-BLOCK-POINTER
*
         DC    X'80',AL3(&IHB.B)
*
*        SVC 99 INPUT-REQUEST-BLOCK
*
&IHB.B   DC    AL1(20),&TYP,X'0000'   LENGTH,VERB-CODE,FLAGS
         DC    XL4'00'                 ERROR+INFO-REASON-CODE
         DC    A(&IHB.C)               TEXT POINTER-LISTE
         DC    XL4'00'                 RESERVED
         DC    XL4'00'                 FLAGS
*
*        TEXT-POINTER
*
&IHB.C   DS    0F
&I       SETA  0                       LAUFVARIABLE = 0
.TXT100  ANOP
&I       SETA  &I+1                    I=I+1
         AIF   (&I EQ &CNT).TXT200
         DC    A(&TPT(&I))            TEXT-POINTER
         AGO   .TXT300
.TXT200  ANOP
         DC    X'80',AL3(&TPT(&I))    TEXT-POINTER
.TXT300  ANOP
         AIF   (&I LT &CNT).TXT100
*
*        TEXTBLOECKE FUER SVC 99
*
&I       SETA  0                       LAUFVARIABLE=0
.TXT500  ANOP
&I       SETA  &I+1                    I=I+1
&TPT(&I) DC  &TXT(&I)
         AIF   (&I LT &CNT).TXT500
         AIF   (&CT EQ 0).TXT700
*
*        RETURN - EQUS
*
&I       SETA  0
.TXT600  ANOP
&I       SETA  &I+1
         AIF   (&I GT &CT).TXT700
&P       SETC  '&PTEQ(&I)'
&P       EQU   &TXEQ(&I)
         AGO   .TXT600
.TXT700  ANOP
         AIF   (NOT &DSP1EQ).TXT710
*
*        DISP-EQUS
*
OLD      EQU   X'01'                   DISP=OLD
MOD      EQU   X'02'                   DISP=MOD
NEW      EQU   X'04'                   DISP=NEW
SHR      EQU   X'08'                   DISP=SHR
.TXT710  ANOP
         AIF   (NOT &DSOREQ).TXT720
*
*        DSORG-EQUS
*
VSAM     EQU   X'0008'                 VSAM
PO       EQU   X'0200'                 PARTITIONED DATASET
POU      EQU   X'0300'                 PARTITIONED DATASET UNMOVABLE
DA       EQU   X'2000'                 BDAM
DAU      EQU   X'2100'                 BDAM UNMOVABLE
PS       EQU   X'4000'                 BSAM/QSAM
PSU      EQU   X'4100'                 BSAM/QSAM UNMOVABLE
IS       EQU   X'8000'                 BISAM/QISAM
ISU      EQU   X'8100'                 BISAM/QISAM UNMOVABLE
.TXT720  ANOP
         AIF   (&CT1 EQ 0).TXT900
*
*        EX - MVCS
*
&I       SETA  0
.TXT800  ANOP
&I       SETA  &I+1
         AIF   (&I GT &CT1).TXT900
&NM2(&I) MVC   &NM3(&I)+4(1),&NM1(&I)
         AGO   .TXT800
.TXT900  ANOP
*
*        GENERIERUNG RESTLICHER CODE
*
&IHB.A   DS    0H
.*
.*       GENERIERUNG RETURN-MVCS
.*
&I       SETA  0
.TXT1000 ANOP
&I       SETA  &I+1
         AIF   (&I GT &CT).TXT1100
         AIF   ('&RMVC(&I)' EQ '').TXT1000
         MVC   &RMVC(&I)
         AGO   .TXT1000
.TXT1100 ANOP
.*
.*       GENERIERUNG LOC-CODE
.*
         AIF   (&CT1 EQ 0).TXT1300
&I       SETA  0
.TXT1200 ANOP
&I       SETA  &I+1
         AIF   (&I GT &CT1).TXT1300
         LH    R15,&NM1(&I)
         LA    R15,1(R15)
         EX    R15,&NM2(&I)
         AGO   .TXT1200
.TXT1300 ANOP
.*
.*       SVC 99
.*
         SVC   99                      DYNALLOC SVC
         L     R0,&IHB.B+4             LOAD ERROR/INFO-CODE
         AIF   (NOT &CODE).TXT2000               TRAILER NICHT GENER.
         LTR   R15,R15                 DYNALLOC IN ORDNUNG
         BNZ   &IHB.D                  NEIN,KEIN MOVE
.*
.*       GENERIERUNG MVEDDN - MVCL
.*
&P       SETC  '&MVEDDN'
         AIF   ('&P' EQ '').TXT1400    MVEDDN FEHLT
         AIF   ('&P'(1,1) NE '(' ).TXT1310       KEIN REGISTER ANGEG.
&LNG     SETA  K'&P                              LAENGE
&P       SETC  '&P'(2,&LNG-2)                    KLAMMERN UM REG. ENTF.
         LR    R14,&P                  ADRESSE TO-FELD
         AGO   .TXT1320
.TXT1310 ANOP
         LA    R14,&P                  ADRESSE TO-FELD
.TXT1320 ANOP
         LA    R15,8                   LAENGE  TO-FELD
         LA    R0,&IHB.BI+6            ADRESSE FROM-FELD
         LH    R1,&IHB.BI+4            LAENGE  FROM-FELD
         ICM   R1,8,=C' '              FUELLZEICHEN
         MVCL  R14,R0                  MOVE DDNAMEN IN AREA
.TXT1400 ANOP
.*
.*       ENDE MVCL - GENERIERUNG
.*
         SR    R15,R15                 RC WIEDER AUF 0 SETZEN
&IHB.D   EQU   *
.TXT2000 ANOP
.*
.*       ENDE TRAILER - GENERIERUNG
.*
.GEN2000 ANOP
         MEXIT
.**********************************************************************
.*       FEHLERNACHRICHTEN
.**********************************************************************
.ERR10   ANOP
         MNOTE 8,'DSNAMEN:&DSN *** KLAMMER VOR MEMBER FEHLT'
         MEXIT
.*
.ERR20   ANOP
         MNOTE 8,'DISP-PARAMETER:&DISP(1) NICHT KORREKT'
         MEXIT
.*
.ERR30   ANOP
         MNOTE 8,'2.DISP-PARAMETER:&DISP(2) NICHT KORREKT'
         MEXIT
.*
.ERR40   ANOP
         MNOTE 8,'3.DISP-PARAMETER:&DISP(3) NICHT KORREKT'
         MEXIT
.TYPERR  ANOP
         MNOTE 8,'TYPE-ANGABE: &TYPE NICHT IMPLEMENTIERT'
         MEXIT
         MEND
