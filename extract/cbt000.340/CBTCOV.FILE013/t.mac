         TITLE 'ENTRY AND ROUTINES FOR MVT AND SVM'
ALLOC    START
         REG
         XSAVE R12,,ALLOC,WORKL
         USING WORK,R13
         USING PARMAREA,R11
         XC    ORIGIN,ORIGIN
         XC    SWITCH,SWITCH
         LR    R11,R1
         LA    R1,VEKTOR
         ST    R1,VEKADDR
         MVI   VEKADDR,X'80'
         LA    R1,VEKADDR
         LINK  EP=GETUCBS
         MVC   VEKEND,=X'FFFF'
         L     R4,16
         CLI   TEXT+6,C','
         BE    JOB
         LA    R4,VEKTOR
         SR    R5,R5
NEXTA    EQU   *
         BAL   R7,SEARCH
         CLC   TEXT+6(3),13(R5)
         BNE   NEXTA
         TM    3(R5),X'08'
         BO    ALLOCON
         MVC   WTO(LENMESS2),MESS2
         BAL   R14,WTOROUT
         B     RETURN
ALLOCON  EQU   *
         L     R2,16        LOAD CVT ADDRESS
         TM    116(R2),X'01'     MVS?
         BO    MVSALLOC          YES.
         L     R2,160(R2)
         MVC   WTO(LENMESS3),MESS3
         BAL   R14,WTOROUT
         MVC   WTO(LENMESS4),MESS4
         LA    R9,WTO+12
         LA    R4,6
         SR    R7,R7
LTIOT    EQU   *
         L     R3,12(R2)
         LA    R3,0(R3)
         LTR   R3,R3
         BZ    NEXTTCB
         LA    R6,24(R3)
STARTDD  EQU   *
         IC    R7,0(R6)
         LTR   R7,R7
         BZ    NEXTTCB
         SH    R7,=H'16'
         SRL   R7,2
         LTR   R7,R7
         BNP   NEXTTCB
         LA    R6,16(R6)
UCBTEST  EQU   *
         LH    R8,2(R6)
         CR    R8,R5
         BE    MOVENAM
         LA    R6,4(R6)
         BCT   R7,UCBTEST
         B     STARTDD
MOVENAM  EQU   *
         TM    SWITCH,X'80'
         BO    NOTFIRST
         OI    SWITCH,X'80'
         MVC   WTO+12(56),WTO+11
NOTFIRST EQU   *
         L     R14,ORIGIN
NEXT     EQU   *
         LTR   R14,R14
         BZ    GETMAIN
         CLC   4(8,R14),0(R3)
         BE    NEXTTCB
         L     R14,0(R14)
         B     NEXT
GETMAIN  EQU   *
         GETMAIN R,LV=12
         MVC   0(4,R1),ORIGIN
         ST    R1,ORIGIN
         MVC   4(8,R1),0(R3)
         MVC   0(8,R9),0(R3)
         LA    R9,9(R9)
         BCT   R4,NEXTTCB
         BAL   R14,WTOROUT
         NI    SWITCH,X'7F'
         LA    R4,6
         LA    R9,WTO+12
NEXTTCB  EQU   *
         L     R2,116(R2)
         LTR   R2,R2
         BNZ   LTIOT
JOBRET   EQU   *
         TM    SWITCH,X'80'
         BNO   RETURN
         BAL   R14,WTOROUT
RETURN   EQU   *
         L     R6,ORIGIN
NFREE    EQU   *
         LTR   R6,R6
         BZ    RET1
         L     R5,0(R6)
         FREEMAIN R,LV=12,A=(R6)
         LR    R6,R5
         B     NFREE
RET1     EQU   *
         XRETURN 0,R
SEARCH   EQU   *
         CLC   0(2,R4),=X'FFFF'  END OF LOOK UP TABLE
         BE    END               YES (MVS REL 3.0)
         ICM   R5,3,0(R4)        INSERT UCB-ADD
         LA    R4,2(R4)
         BZ    SEARCH
         BR    R7
END      EQU   *
         MVC   WTO(LENMESS1),MESS1
         BAL   R14,WTOROUT
         B     RETURN
WTOROUT  EQU   *
         L     R0,REG4
         WTO   MF=(E,WTO)
         BR    R14
JOB      EQU   *
         L     R2,16
         TM    116(R2),X'01'      MVS?
         BO    MVSJOBS            YES.
         L     R4,160(R4)
JOBNTCB  EQU   *
         L     R5,12(R4)
         CLC   0(8,R5),TEXT+7
         BE    JOBFND
         L     R4,116(R4)
         LA    R4,0(R4)
         LTR   R4,R4
         BNZ   JOBNTCB
JOBM7    EQU   *
         MVC   WTO(LENMESS7),MESS7
         MVC   WTO+16(8),TEXT+7
         BAL   R14,WTOROUT
         B     RETURN
JOBFND   EQU   *
         LA    R5,0(R5)
         LTR   R5,R5
         BNZ   JOBTIOT
JOBNTIOT EQU   *
         MVC   WTO(LENMESS6),MESS6
         MVC   WTO+16(8),TEXT+7
         BAL   R14,WTOROUT
         B     RETURN
JOBTIOT  EQU   *
         LA    R5,24(R5)
         SR    R7,R7
         CLI   0(R5),X'00'
         BNE   JOBNSYS
         CLI   28(R4),X'00'
         BNE   JOBNTIOT
         L     R5,136(R4)
         L     R5,12(R5)
         B     JOBFND
JOBNSYS  EQU   *
         LH    R6,18(R5)
         CLI   2(R6),X'FF'
         BNE   JOBM7
         TM    3(R6),X'88'
         BNO   JOBM7
         MVC   WTO(LENMESS5),MESS5
         MVC   WTO+16(8),TEXT+7
         BAL   R14,WTOROUT
         MVC   WTO(LENMESS4),MESS4
         LA    R9,WTO+12
         LA    R4,14
JOBNTRY  EQU   *
         IC    R7,0(R5)
         SH    R7,=H'16'
         SRL   R7,2
         LA    R6,16(R5)
JOBNUNT  EQU   *
         LH    R8,2(R6)
         CLI   2(R8),X'FF'
         BNE   JOBIUNT
         TM    3(R8),X'88'
         BNO   JOBIUNT
         L     R10,ORIGIN
JOBNNTRY EQU   *
         LTR   R10,R10
         BZ    JOBGTMN
         CH    R8,4(R10)
         BE    JOBIUNT
         L     R10,0(R10)
         B     JOBNNTRY
JOBIUNT  EQU   *
         LA    R6,4(R6)
         BCT   R7,JOBNUNT
         LR    R5,R6
         CLI   0(R5),X'00'
         BE    JOBRET
         B     JOBNTRY
JOBGTMN  EQU    *
         GETMAIN R,LV=12
         MVC   0(4,R1),ORIGIN
         ST    R1,ORIGIN
         STH   R8,4(R1)
         TM    SWITCH,X'80'
         BO    JOBNFST
         OI    SWITCH,X'80'
         MVC   WTO+12(56),WTO+11
JOBNFST  EQU   *
         MVC   0(3,R9),13(R8)
         LA    R9,4(R9)
         BCT   R4,JOBIUNT
         BAL   R14,WTOROUT
         NI    SWITCH,X'7F'
         LA    R4,14
         LA    R9,WTO+12
         B     JOBIUNT
         EJECT
MESS1    WTO   'XALLOC1 UNIT NOT FOUND IN UCBS',MF=L,                  *
               MCSFLAG=REG0
LENMESS1 EQU   *-MESS1
MESS2    WTO   'XALLOC2 UNIT IS NOT ALLOCATED',MCSFLAG=REG0,           *
               MF=L
LENMESS2 EQU   *-MESS2
MESS3    WTO   'XALLOC3 UNIT IS ALLOCATED TO FOLLOWING JOB(S):',       *
               MCSFLAG=REG0,MF=L
LENMESS3 EQU   *-MESS3
MESS4    WTO   'XALLOC4 NO JOB(S) FOUND                                *
                        ',MCSFLAG=REG0,MF=L
LENMESS4 EQU   *-MESS4
MESS5    WTO   'XALLOC5 JOB 12345678 HAS ALLOCATED FOLLOWING UNITS:',  *
               MCSFLAG=(REG0),MF=L
LENMESS5 EQU   *-MESS5
MESS6    WTO   'XALLOC6 JOB 12345678 HAS NO UNITS ALLOCATED          ',*
               MCSFLAG=(REG0),MF=L
LENMESS6 EQU   *-MESS6
MESS7    WTO   'XALLOC7 JOB 12345678 NOT ACTIVE',                      *
               MCSFLAG=(REG0),MF=L
LENMESS7 EQU   *-MESS7
MESS8    WTO   'XALLOC8 LAST TASK ALLOCATED TO UNIT ... IS:',          *
               MCSFLAG=(REG0),MF=L
LENMESS8 EQU   *-MESS8
MESS9    WTO   'XALLOC9 ADDRESS-SPACE COULD NOT BE LOCATED.',          *
               MCSFLAG=(REG0),MF=L
LENMESS9 EQU   *-MESS9
         LTORG
         TITLE 'MVS EXTENSION FOR RELEASE 2.0'
MVSALLOC EQU   *
         MVC   WTO(LENMESS8),MESS8
         MVC   WTO+40(3),TEXT+6
         BAL   R14,WTOROUT
         MVC   WTO(LENMESS9),MESS9
         L     R2,X'14'(R5)       LOAD UCBEXTP
         LH    R3,X'0E'(R2)       ALLOCATED ASID
         LTR   R3,R3              ASID=00?
         BNZ   LOOKAS
NOJOBSFD EQU   *
         BAL   R14,WTOROUT
         B     RET1
LOOKAS   EQU   *
         STH   R3,ASID
         L     R2,16
         L     R2,X'22C'(R2)    ASVT
         LA    R2,X'20C'(R2)    FIRST ASCB PTR
ASCBLP1  EQU   *
         SR    R4,R4
         ICM   R4,7,1(R2)       LAST ENTRY?
         BZ    NOJOBSFD         YES
         TM    0(R2),X'80'      AVAILABLE ASCB?
         BO    NEXTAS1
         CLC   ASID,X'24'(R4)   ASID FOUND?
         BNE   NEXTAS1
         MVC   WTO+12(60),WTO+11     ERASE LINE
         MVC   WTO+12(30),=CL30'JBNI=........  JBNS=........ '
         L     R9,X'AC'(R4)
         LTR   R9,R9             PTR ZERO?
         BZ    MVSU001           YES
         MVC   WTO+17(8),0(R9)   JBNI
MVSU001  EQU   *
         L     R9,X'B0'(R4)
         LTR   R9,R9
         BZ    AS1OUT
         MVC   WTO+32(8),0(R9)   JBNS
AS1OUT   EQU   *
         BAL   R14,WTOROUT
         B     RET1
NEXTAS1  EQU   *
         LA    R2,4(R2)           STEP TO NEXT PTR
         B     ASCBLP1
         EJECT
MVSJOBS  EQU   *
         L     R2,X'22C'(R2)       ADDRESS SPACE VECTOR TABLE
         LA    R2,X'20C'(R2)       FIRST ASCB PTR
ASCBLP2  EQU   *
         SR    R4,R4
         ICM   R4,7,1(R2)
         BZ    JOBM7
         TM    0(R2),X'80'
         BO    NEXTAS2             ASCB NOT USED
         ICM   R9,7,X'AC'+1(R4)
         BZ    MVSJ001
         CLC   0(8,R9),TEXT+7
         BE    AS2FOUND
MVSJ001  EQU   *
         ICM   R9,7,X'B0'+1(R4)
         BZ    NEXTAS2
         CLC   0(8,R9),TEXT+7
         BNE   NEXTAS2
AS2FOUND EQU   *
         MVC   ASID,X'24'(R4)
         B     GETUNITS
NEXTAS2  EQU   *
         LA    R2,4(R2)
         B     ASCBLP2
GETUNITS EQU   *
         LA    R4,VEKTOR           UCB LOOK UP TABLE
         SR    R5,R5
NEXTUCB2 EQU   *
         CLC   0(2,R4),=X'FFFF'  END OF LOOK UP TABLE
         BE    TESTSW2           YES (MVS REL 3.0)
         ICM   R5,3,0(R4)        INSERT UCB-ADD
         LA    R4,2(R4)            STEP TO NEXT ENTRY
         BZ    NEXTUCB2            ZERO ENTRY.
         TM    3(R5),X'88'         UCB ALLOCATED?
         BNO   NEXTUCB2            NO - SKIP UCB
         ICM   R2,7,X'14'+1(R5)    LOAD UCBEXTP
         BZ    NEXTUCB2
         CLC   ASID,X'0E'(R2)      COMPARE AGAINST ASCB-ID FIELD
         BNE   NEXTUCB2
         OI    SWITCH,X'08'        SET FOUND SW
         TM    SWITCH,X'04'        FIRST LINE WTO'ED?
         BO    MVSJ010             YES.
         OI    SWITCH,X'04'
         MVC   WTO(LENMESS5),MESS5
         MVC   WTO+16(8),TEXT+7
         MVC   WTO+29(26),=C'THE FOLLOWING UCBS MARKED:'
         BAL   R14,WTOROUT
         MVC   WTO(LENMESS4),MESS4
         MVC   WTO+12(MESS5-MESS4-12),WTO+11
         LA    R9,WTO+12
         LA    R6,MAXUNITS         MAX. UNITS/LINE
MVSJ010  EQU   *
         MVC   0(3,R9),13(R5)      MOVE UNIT ADDRESS
         LA    R9,4(R9)
         BCT   R6,NEXTUCB2
         BAL   R14,WTOROUT
         NI    SWITCH,X'F7'        RESET LINE SWITCH
         MVC   WTO+12(MESS5-MESS4-12),WTO+11
         LA    R9,WTO+12
         LA    R6,MAXUNITS         MAX. UNITS/LINE
         B     NEXTUCB2
TESTSW2  TM    SWITCH,X'08'
         BZ    MVSJ011
         BAL   R14,WTOROUT
MVSJ011  EQU   *
         MVC   WTO(LENMESS6),MESS6
         MVC   WTO+29(27),=C'NO UCBASID FIELD ASSIGNED'
         TM    SWITCH,X'04'        ANY UNIT FOUND?
         BZ    JOBNTIOT+6            NO.
         B     RET1                YES - NORMAL RETURN.
*
         EJECT
MAXUNITS EQU   13                 MAX. UNITS/LINE MVS
         XPARM
WORK     DSECT
         DS    18F
WTO      DS    CL100
ORIGIN   DS    F
VEKADDR  DS    F
SWITCH   DS    X
ASID     DS    H
VEKTOR   DS    1024H
VEKEND   DS    H
WORKL    EQU   *-WORK
         END
