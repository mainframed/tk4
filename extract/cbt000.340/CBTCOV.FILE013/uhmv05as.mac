SP26 TITLE 'C H E C K P O I N T   S C H R E I B E N'
***********************************************************************
*                                                                     *
*   SP26      CHECKPOINT SCHREIBEN                                    *
*   SP26      PROGRAMMIERER: STEINER             DATUM: 31.10.75      *
*                                                                     *
***********************************************************************
         EJECT
SP26     ANFANG EP=SP26
         EJECT
         USING IHADCB,R11
         LA    R11,SYSCHK
         TM    SWT,SWTFIRST       1. AUFRUF
         BO    P100               NEIN
         OI    SWT,SWTFIRST       MM *1. AUFRUF ERFOLGT *
         LOAD  EP=SP54
         ST    R0,ASP54           A(SP54)
         LR    R15,R0             DDNAME
         LA    R1,SYSCHK0         IN TIOT
         BALR  R14,R15            SUCHEN
         LTR   R15,R15            GEFUNDEN ?
         BNZ   P010               NEIN
         MVC   DCBDDNAM,SYSCHK0
         OPEN (SYSCHK,(OUTPUT))   KENNZATZ SCHREIBEN
         CLOSE (SYSCHK)
P010     LA    R2,5               SCHLEIFENZAEHLER
         LA    R9,DDNTAB          A(DDNAMEN-TABELLE)
P020     LR    R1,R9              A(DDNAME)
         L     R15,ASP54
         BALR  R14,R15            DDNAME IN TIOT SUCHEN
         LTR   R15,R15            GEFUNDEN ?
         BNZ   P030               NEIN
         LA    R9,8(,R9)          A(NAECHSTER DDNAME)
         BCT   R2,P020
P030     MVI   0(R9),C'*'
        DELETE EP=SP54
         XC    ASP54,ASP54
P100     L     R9,ADDN
         CLI   0(R9),C'*'         DD-KARTE VORHANDEN ?
         BE    P150               NEIN
P110     MVC   DCBDDNAM,0(R9)
         CHKPT SYSCHK             CHECKPOINT ZIEHEN
         LA    R9,8(,R9)          A(NAECHSTER DDNAME)
         ST    R9,ADDN
         ENDE  RC=(15)            RC VON CHECKPOINT
P150     LA    R9,DDNTAB
         ST    R9,ADDN
         CLI   0(R9),C'*'         DD-KARTE VORHANDEN ?
         BNE   P110               JA
         WTO   'CHECKPOINT KONNTE NICHT GEZOGEN WERDEN, DA KEINE DD-KAR*
               TE(N) VORHANDEN'
         ENDE  RC=24
         EJECT
***********************************************************************
*        D E F I N I T I O N E N                                      *
***********************************************************************
DDNTAB   DC    CL8'SYSCHK1'
         DC    CL8'SYSCHK2'
         DC    CL8'SYSCHK3'
         DC    CL8'SYSCHK4'
         DC    CL8'SYSCHK5'
DDNTABE  DC    C'*'
SYSCHK0  DC    CL8'SYSCHK0'
SWT      DC    B'00000000'
SWTFIRST EQU   X'80'
ASP54    DC    A(0)               A(SP54)
ADDN     DC    A(DDNTAB)
SYSCHK   DCB   DDNAME=SYSCHK1,DSORG=PS,MACRF=W,RECFM=U
         DCBD  DSORG=LR
         END
