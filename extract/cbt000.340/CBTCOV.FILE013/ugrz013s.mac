         TITLE 'FCB2LIST - CHECK SYSTEM LOCATION'
***      GENOSSENSCHAFTS-RECHENZENTRALE NORDDEUTSCHLAND GMBH.
***      SANDKRUGERSTRASSE
***      2900 OLDENBURG
***      TEL. 0441/400200(W.SCHWARZ)
FCB2LIST CSECT
*        THIS FUNCTION IS USED TO RE-GEN ZZFCB2-PARAMETER OF AN FCB
*        GENERATED WITH THE GRZ FCB2GEN-MACRO
*        THIS FUNCTION CAN BE USED VIA PPA, TSO(CALL) OR JCL-EXEC
*        REQUESTED PARAMETERS ARE :
*        LIST  ALL FCBS ARE LISTED ON SYSOUT
*        FCB-NAME THE SPECIFIED FCB IS
*        A) VIA PPA-FUNCTION DISPLAYED ON THE CALLERS SCREEN
*        B) VIA TSO(CALL) DISPLAYED ON THE CALLERS SCREEN
*        C) VIA JCL-EXEC PRINTED ON SYSOUT
*
*        FOR B) AND C) ONLY ONE FCB-NAME IS GENERATED IN ONE CALL
*
        EJECT
         ZZ000
         LR    R10,R01
         ZZRELNO
         LA    R05,3
         LA    R06,IDTAB-10
RZID     LA    R06,10(R06)
         CLC   0(1,R01),0(R06)
         BE    RZFOUND
         BCT   R05,RZID
         B     GETPARM
         SPACE 3
IDTAB    DC    CL10'AAHLTEN'
         DC    CL10'HHAMBURG'
         DC    CL10'OOLDENBURG'
         SPACE 3
RZFOUND  MVC   RZ,1(R06)
         SPACE 3
         DATUM SYMBOL=D
         SPACE 3
         ZEIT  SYMBOL=T
         TITLE 'FCB2LIST - CHECK REQUEST-TYPE AND PARM'
GETPARM  LM    R11,R12,0(R10)
*        R11   X'80',AL3(PARM)   X'80' NOT PPA
*    OR  R11   X'XX',AL3(PARM)
*        R12   AL1(CNID)
         L     R15,0(R10)
         LH    R14,0(R15)
         LTR   R14,R14
         BZ    NOPARM
         CLM   R14,1,PLENGTH+1
         BH    PLLOAD
         B     PEX
         DS    0H
PLENGTH  DC    AL2(4)
PMVC     MVC   PARM(0),2(R15)
PLLOAD   LH    R14,PLENGTH
PEX      BCTR  R14,0
         EX    R14,PMVC
         B     PGMSTART
NOPARM   TM    0(R10),128          EXEC OR TSO
         BO    NOTPPA              YES
         SR    R00,R00             PPA
         IC    R00,0(R12)
         ST    R00,CNID            CONSOLE ID(FOR PPA)
         LA    R07,MESS4
         BAL   R14,ERRWTO80
         B     DEFAULTP            DEFAULT FCB-LIST REQUEST
NOTPPA   OPEN  (PRT,OUTPUT)        SYSOUT
         BAL   R14,TITLEPRT
         MVI   P1CC,C'-'
         MVC   P1DATA,MESS4
         PUT   PRT,P1AREA
         MVI   PRTIND,C'O'         OPEN INDICATED
         LA    R07,MESS4
         BAL   R14,TPUTROUT
DEFAULTP MVC   PARM,NORM
PGMSTART TM    0(R10),128
         BZ    HOTSTART
         CLI   PRTIND,C'O'
         BE    HOTSTART
         OPEN  (PRT,OUTPUT)
HOTSTART OPEN  (SNAP,OUTPUT)
         TITLE 'FCB2LIST - READ IMAGELIB DIRECTORY'
         L     R00,GMLNG1
         GETMAIN R,LV=(0)
         ST    R01,GMADDR
         OPEN  (DIR)
         L     R05,GMADDR
         SR    R04,R04
READN    READ  DIRECB,SF,DIR,KEY,'S'
         CHECK DIRECB
         LA    R03,PDSAR
         B     READN2
READN1   L     R06,8(R03)
         SLL   R06,27
         SRL   R06,27
         LA    R03,12(R03)
         AR    R03,R06
         AR    R03,R06
READN2   CLC   0(4,R03),FCB2           FCB-IMAGE NAME
         BNE   READN3
         LA    R04,1(R04)
         C     R04,=A(399)
         BH    FCB2RERR
         MVC   0(8,R05),0(R03)
         MVC   8(8,R05),LASTMEMB       LAST OUTPUT MEMBER IND
         LA    R05,8(R05)
READN3   CLC   0(8,R03),KEY
         BNE   READN1
         CLC   0(8,R03),LASTMEMB       LAST INPUT  MEMBER IND
         BNE   READN
EOFDIR   CLOSE (DIR)
         EJECT
*        ZZSNAP
         OPEN  (DAT)
         L     R00,GMLNG2
         GETMAIN R,LV=(0)
         ST    R01,A2
         CLC   PARM,=C'LIST'
         BE    LISTFCB
         TITLE 'FCB2LIST - PREPARE OUTPUT FOR SPECIFIC IMAGE'
SETR5    L     R05,GMADDR
ENDCHCK  CLC   0(8,R05),LASTMEMB
         BE    NFMESS                  IMAGE NOT FOUND
         CLC   4(4,R05),PARM
         BE    LOADFCB
INCR5    LA    R05,8(R05)
         B     ENDCHCK
LOADFCB  MVC   FCB2ID(8),0(R05)
         ST    R05,A3
         LOAD  EPLOC=FCB2ID,DCB=DAT
         LR    R09,R00                 EP-ADDR OF IMAGE
         ST    R09,A1
         MVI   A3,X'80'
         CALL  FCB2GEN,MF=(E,A1)
         LTR   R15,R15                   TEST RETURN CODE
         BNZ   FCB2GERR
         DELETE EPLOC=FCB2ID
         TM    0(R10),128
         BO    TPUT
         BAL   R14,WTOROUT
NEWTURN  L     R00,CNID
         MVC   PARM,=CL4' '
         WTOR  'GRZ730-FCB2LIST  SPECIFY NEXT FCB-IMAGE OR EOJ',       *
               PARM,4,ECB,MCSFLAG=REG0
         WAIT  ECB=ECB
         XC    ECB,ECB
         OC    PARM,=CL4' '
         CLC   PARM(3),=C'EOJ'
         BE    EOJFCB2
         CLC   PARM,4(R05)
         BH    INCR5
         B     SETR5
         SPACE 3
TPUT     BAL   R14,TPEXROUT
         CLOSE (PRT)
         B     EOJFCB2
         TITLE 'FCB2LIST - PREPARE OUTPUT FOR IMAGES PRINT'
LISTFCB  L     R05,GMADDR
LASTCHCK CLC   0(8,R05),LASTMEMB
         BE    ENDLIST
         BAL   R14,TITLEPRT
         MVC   FCB2ID(8),0(R05)
         ST    R05,A3
         LOAD  EPLOC=FCB2ID,DCB=DAT
         LR    R09,R00
         ST    R09,A1
         MVI   A3,X'80'
         CALL  FCB2GEN,MF=(E,A1)
         LTR   R15,R15
         BNZ   FCB2GERR
         DELETE EPLOC=FCB2ID
         MVI   P1CC,C'0'
         MVC   MESSAGE,MESS1
         MVC   WTONAME(4),4(R05)
         MVC   P1DATA,MESSAGE
         PUT   PRT,P1AREA
         MVI   P1CC,C'+'
         MVC   MESSAGE,BLANCS
         MVC   WTONAME-4(8),0(R05)
         MVC   P1DATA,MESSAGE
         PUT   PRT,P1AREA
         LA    R04,10
         L     R06,A2
LISTLOOP CLC   0(4,R06),=XL4'0'
         BE    NEXTR6L
         MVI   P1CC,C'0'
         MVC   P1DATA,0(R06)
         PUT   PRT,P1AREA
NEXTR6L  LA    R06,80(R06)
         BCT   R04,LISTLOOP
         LA    R05,8(R05)
         B     LASTCHCK
         SPACE 3
ENDLIST  CLOSE (PRT)
         TITLE 'FCB2LIST - EOJFCB2 BLOCK'
EOJFCB2  L     R01,GMADDR
         L     R00,GMLNG1
         FREEMAIN R,LV=(0),A=(1)
         L     R01,A2
         L     R00,GMLNG2
         FREEMAIN R,LV=(0),A=(1)
         CLOSE (DAT)
         LA    R15,0
RETURN   ZZFFF RC=(15)
         TITLE 'FCB2LIST - SUBROUTINES'
         DC    F'0'
ERRWTO80 ST    R14,*-4
         L     R00,CNID
         MVC   ERRWTO+8(80),0(R07)
ERRWTO   WTO   '                                                       *
                                        ',MCSFLAG=REG0
         L     R14,ERRWTO80-4
         BR    R14
         EJECT
         DC    F'0'
POSTMSG  ST    R14,*-4
         L     R00,WTO1SAVE
         MVC   WTOTXT+8(80),0(R07)
WTOTXT   WTO   ('                                                      *
                                         ',D),MCSFLAG=REG0,DESC=9
         L     R14,POSTMSG-4
         BR    R14
         EJECT
         DC    F'0'
TPUTROUT ST    R14,*-4
         L     R08,TPLENGTH        L=80
         TPUT  (R07),(R08)
         L     R14,TPUTROUT-4
         BR    R14
         EJECT
         DC    F'0'
WTOROUT  ST    R14,*-4
         ZZ7MODES KEY=ZERO
         L     R00,CNID
         MVC   WTO1+40(4),PARM
WTO1     WTO   ('FCB-LIST UTILITY',C),('FCB2XXXX',L),                  *
               AREAID=A,MCSFLAG=REG0,DESC=(8,9)
         STCM  R01,7,WTO1SAVE
         STCM  R01,1,WTO1SAVE+3
         MVC   MESSAGE,MESS1
         MVC   WTONAME(4),PARM
         LA    R07,MESSAGE
         BAL   R14,POSTMSG
         LA    R04,10
         L     R06,A2
WTOLOOP  CLC   0(4,R06),=XL4'0'
         BE    NEXTR6W
         LR    R07,R06
         BAL   R14,POSTMSG
NEXTR6W  LA    R06,80(R06)
         BCT   R04,WTOLOOP
         L     R00,WTO1SAVE
         WTO   ('FCB-LIST UTILITY -- END',DE),AREAID=A,MCSFLAG=REG0,   *
               DESC=9
         ZZ7MODES KEY=NZERO
         L     R14,WTOROUT-4
         BR    R14
         EJECT
         DC    F'0'
TPEXROUT ST    R14,*-4
         BAL   R14,TITLEPRT
         MVI   P1CC,C'0'
         MVC   MESSAGE,MESS1
         MVC   WTONAME(4),PARM
         MVC   P1DATA,MESSAGE
         PUT   PRT,P1AREA
         MVI   P1CC,C'-'
         LA    R07,MESSAGE
         BAL   R14,TPUTROUT
         LA    R04,10
         L     R06,A2
TPEXLOOP CLC   0(4,R06),=XL4'0'
         BE    NEXTR6WT
         MVC   P1DATA,0(R06)
         PUT   PRT,P1AREA
         MVI   P1CC,C' '
         LA    R07,P1DATA
         BAL   R14,TPUTROUT
NEXTR6WT LA    R06,80(R06)
         BCT   R04,TPEXLOOP
         L     R14,TPEXROUT-4
         BR    R14
         TITLE 'FCB2LIST - ERROR CONDITION HANDLING'
PDSERR   MVC   MESSAGE(80),MESS2
         MVC   WTONAME-4(8),0(R03)
         TM    0(R10),128
         BO    PDSERRTP
         LA    R07,MESSAGE
         BAL   R14,ERRWTO80
         B     PDSERRC
PDSERRTP LA    R07,MESSAGE
         BAL   R14,TPUTROUT
         MVI   P1CC,64
         MVC   P1DATA,MESSAGE
         PUT   PRT,P1AREA
         CLOSE (PRT)
PDSERRC  ZZSNAP
         CLOSE (SNAP)
         L     R01,GMADDR
         L     R00,GMLNG1
         FREEMAIN R,LV=(0),A=(1)
         CLOSE (DIR)
         LA    R15,12
         B     RETURN
         EJECT
NFMESS   MVC   MESSAGE(80),MESS3
         MVC   WTONAME(4),PARM
         TM    0(R10),128
         BO    NFMSGTSO
         LA    R07,MESSAGE
         BAL   R14,ERRWTO80
         B     NEWTURN
NFMSGTSO LA    R07,MESSAGE
         BAL   R14,TPUTROUT
         ZZSNAP
         CLOSE (SNAP)
         BAL   R14,TITLEPRT
         MVI   P1CC,64
         MVC   P1DATA,MESSAGE
         PUT   PRT,P1AREA
         CLOSE (PRT)
         B     EOJFCB2
         EJECT
FCB2RERR MVC   MESS6+38(8),0(R03)
         MVC   MESSAGE,MESS6
         LA    R15,16
         B     LOADRC
FCB2GERR MVC   MESSAGE,MESS5
         MVC   WTONAME(4),4(R05)
LOADRC   LA    R07,RCTAB
GETRC    LM    R00,R01,0(R07)
         CR    R15,R00
         BE    TSTREQ
         LA    R07,8(R07)
         B     GETRC
         SPACE 3
RCTAB    DC    A(4,PS)
         DC    A(8,WS)
         DC    A(16,FS)
PS       DC    C'POSTABLE SHORTAGE'
WS       DC    C'WORKAREA SHORTAGE'
FS       DC    C'FCB2AREA SHORTAGE'
         SPACE 3
TSTREQ   MVC   RCODE,0(R01)
         TM    0(R10),128
         BO    GERRTSO
         LA    R07,MESSAGE
         BAL   R14,ERRWTO80
         ZZSNAP
         CLOSE (SNAP)
         B     EOJFCB2
GERRTSO  LA    R07,MESSAGE
         BAL   R14,TPUTROUT
         ZZSNAP
         CLOSE (SNAP)
         BAL   R14,TITLEPRT
         MVI   P1CC,C'-'
         MVC   P1DATA,MESSAGE
         PUT   PRT,P1AREA
         CLOSE (PRT)
         B EOJFCB2
         EJECT
         DC    F'0'
TITLEPRT ST    R14,*-4
         L     R15,PAGE
         LA    R15,1(R15)
         ST    R15,PAGE
         CVD   R15,DWORD
         OI    DWORD+7,X'0F'
         UNPK  PAGENO(3),DWORD+6(2)
         MVC   DATE(8),D
         MVC   TIME(5),T
         PUT   PRT,TITLE
         L     R14,TITLEPRT-4
         BR    R14
         TITLE 'FCB2LIST - AREAS AND CONSTANTS'
DWORD    DC    D'0'
ECB      DC    F'0'
CNID     DC    F'0'
PAGE     DC    F'0'
TPLENGTH DC    A(80)
WTO1SAVE DC    F'0'
         DC    6CL6'PDSD A'
KEY      DC    XL8'00'
         DC    X'0000'
PDSAR    DC    XL254'00'
         DC    6CL6'PDSD E'
*
A1       DS    A
A2       DS    A
A3       DS    A
*
GMADDR   DS    F
GMLNG1   DC    A(3200)
GMLNG2   DC    A(1600)
*
P1AREA   DS    0CL81
P1CC     DC    C'1'
P1DATA   DC    CL80' '
PRTIND   DC    C' '
*
MESSAGE  DS    0CL80
         DC    CL29' '
WTONAME  DC    CL4' '
         DC    CL23' '
RCODE    DC    CL17' '
         DC    CL7' '
*
FCB2     DC    C'FCB2'
NORM     DC    C'NORM'
LASTMEMB DC    8XL1'FF'
FCB2ID   DC    CL8' '           CURRENT REQUESTED FCB-IMAGE
*
TITLE    DS    0CL81
         DC    C'1GRZ/RZ '
RZ       DC    CL9' '
         DC    C'  FCB-LIST UTILITY'
         DC    CL20' '
DATE     DC    CL8' '
         DC    C'  '
TIME     DC    CL5' '
         DC    C'   PAGE '
PAGENO   DC    CL3' '
*
MESS1    DC    CL80'THE REQUESTED FCB-IMAGE  FCB2     HAS THE FOLLOWING*
                FORMAT              FCB2LIST'
*
MESS2    DC    CL80'NO USER-DATA FOUND FOR                             *
                                    FCB2LIST'
*
MESS3    DC    CL80'    *ERROR*        IMAGE          NOT FOUND        *
                                    FCB2LIST'
*
MESS4    DC    CL80'INVALID PARAMETER SPECIFIED, ''NORM'' ASSUMED,     *
                                    FCB2LIST'
*
MESS5    DC    CL80'THE REQUESTED FCB-IMAGE  FCB2     COULD NOT BE PRIN*
               TED,                 FCB2LIST'
*
MESS6    DC    CL80'YOUR REQUEST COULD NOT BE SERVICED ---             *
                                    FCB2LIST'
*
PARM     DC    CL4' '
*
BLANCS   DC    CL80' '
         TITLE 'FCB2LIST - DCB'
DIR      DCB   DDNAME=IMAGELIB,DSORG=PS,MACRF=R,EODAD=EOFDIR,          *
               RECFM=F,LRECL=256,BLKSIZE=256,KEYLEN=8
         EJECT
DAT      DCB   DDNAME=IMAGELIB,DSORG=PO,MACRF=R
         EJECT
PRT      DCB   DDNAME=FCBOUT,DSORG=PS,MACRF=(PM),                      *
               RECFM=FBA,LRECL=81
         END   FCB2LIST
