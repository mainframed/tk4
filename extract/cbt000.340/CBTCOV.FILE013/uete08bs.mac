*
* PROGRAM INITIALISATION
*
         PRINT NOGEN
SYSTXMSG LPCID TITLE='APPLICATION PROGRAM MESSAGE HANDLER'
         LCNVE SYSTXMSG,12
         INIT  10,(XMSGCSCT,XMSGCSLE)       GET PROGRAM WORK AREA
         USING XMSGCSCT,10                  GET ADDRESSIBILITY
         LR    9,1                          GET ADDRESS OF PARM AREA
         USING PARMAREA,9                   GET ADDRESSIBILTY
         MVC   ACTFLAGS,ACTION              SAVE ACTION BITS
         EJECT
*
* GENERAL PROGRAM LOGIC
*
         ICM   2,15,MSGLEN                  GET MESSAGE POINTER
         SRDA  2,24                         GET MESSAGE LENGTH IN REG2
         SRL   3,8                          GET MESSAGE ADDRESS IN REG3
         CH    2,=H'120'                    Q. MESSAGE LENGTH > 120
         BNH   *+14                         A. NO- MESSAGE LENGTH OK.
         MVC   RETURN,=X'0008'              SET PROGRAM RETURN CODE
         B     EXIT                         GET END OF PROGRAM
         BCTR  2,0                          MINUS ONE FOR MVC
         EX    2,MSGMVC                     MOVE MESSAGE INTO WORK AREA
         BAL   14,ISRTMSG                   CHECK MSG PARM REQUEST
         TM    ACTFLAGS,ACTFME              Q. REQUEST IN ERROR
         BO    EXIT                         A. YES- END OF PROGRAM
         BAL   14,EXCMD                     EXECUTE REQUESTED CMD'S
EXIT     BAL   14,ANALYZE                   CHECK MESSAGE ERROR'S
         LA    0,XMSGCSLE                   GET TOTAL LENGTH OF CSECT
         FREEMAIN R,LV=(0),A=(10)           DROP PROGRAM WORK AREA
         LEXIT RC=(11)                      END OF PROGRAM
         SPACE
MSGMVC   MVC   MSGWORK(0),0(3)              SAVE MESSAGE INTO WORKAREA
         EJECT
*
* CHECK FOR MESSAGE PARM REQUEST AND CALL MESSAGE PARM INSERT ROUTINE
*
ISRTMSG  STM   14,13,GRSAREA1
         TM    ACTFLAGS,ACTFVL              Q. MESSAGE PARM REQUESTED
         BO    IMSGEND                      A. NO- END OF SUBROUTINE
         LA    1,APARMS                     GET ADDR. OF NEXT PARM LIST
         LA    2,PARM01                     GET ADDR. OF 1. MSG PARM
         ST    2,0(1)                       AND SAVE IT IN PARM LIST
         LA    2,MSGWORK                    GET ADDRESS OF WORK AREA
         ST    2,4(1)                       SAVE START ADDR. OF MSG
         ICM   2,8,MSGLEN                   GET INPUT MESSAGE LENGTH
         SRL   2,24                         CLEAR HIGH ORDER
         STC   2,4(1)                       AND SAVE IT IN PARM LIST
         CALL  SYSTIMSG                     CALL MSG-INSERT ROUTINE
         STCM  15,3,RETURN                  SAVE RETURN CODE
         CH    15,=H'4'                     Q. RETURN CODE > 4
         BNH   IMSGEND                      END OF SUBROUTINE
         OI    ACTFLAGS,ACTFME              MESSAGE IN ERROR
IMSGEND  LM    14,13,GRSAREA1
         BR    14                           RETURN TO THE CALLER
         EJECT
*
* MESSAGE EXECUTE COMMAND ROUTINE
*
EXCMD    STM   14,13,GRSAREA2
         TM    ACTFLAGS,ACTFWAO             Q. WRITE TO ANY OPERATORS
         BNO   WRITEMSG                     A. NO- GET WRITE MESSAGE
         L     1,16                         LOCATE CVT
         L     1,0(1)
         L     1,4(1)                       POINT TO TCB
         L     1,180(1)                     POINT TO JSCB
         L     1,256(1)                     POINT TO CSCB
         TM    28(1),X'01'                  Q. TIME SHARING TASK
         BNO   WTMCOP                       A. NO- WRITE TO MSTCONS
         OI    ACTFLAGS,ACTFWTT             WRITE TO TSO OPERATOR
         B     WRITEMSG                     GET WRITE THE MESSAGE
WTMCOP   OI    ACTFLAGS,ACTFWSO             WRITE TO CONSOLE OPERATOR
WRITEMSG TM    ACTFLAGS,ACTFRM+ACTFWSO+ACTFMRR+ACTFWTT+ACTFWJL
         BZ    FOCMDEND
         BAL   14,WTO                       CALL WTO CMD ROUTINE
         BAL   14,WTOR                      CALL WTOR CMD ROUTINE
         BAL   14,TPUT                      CALL TPUT CMD ROUTINE
         BAL   14,TGET                      CALL TGET CMD ROUTINE
         BAL   14,WTL                       CALL WTL CMD ROUTINE
         BAL   14,RMSG                      CALL RETURN CMD ROUTINE
         B     WRITEMSG                     MAKE LOOP FOR NEXT REQUEST
FOCMDEND LM    14,13,GRSAREA2
         BR    14                           RETURN TO THE CALLER
         EJECT
*
* MESSAGE ERROR ANALYZE / GENERATE USER INFORMATION MESSAGES
*
ANALYZE  STM   14,13,GRSAREA1
         LH    11,RETURN                    GET PROGRAM RETURN CODE
         CH    11,=H'4'                     Q. PGM RETURN > 4
         BNH   AEND                         A. NO- END OF SUBROUTINE
         MVC   MSGWORK(120),=CL120'(        ) -SYNTAX ERROR IN MESSAGE *
               OR MESSAGE PARM'
GMSGID   ICM   2,14,MSGPTR                  GET ADDRESS OF MESSAGE
         SRL   2,8                          CLEAR HIGH ORDER
         MVC   MSGWORK+1(7),0(2)            GET MESSAGE PROJECT NAME
         OI    ACTFLAGS,ACTFWAO             WRITE TO ANY OPERATOR
         CLC   =X'000000',RPLAREA+1         Q. RETURN AREA DEFINED
         BE    *+8                          A. NO- SEND THE ERROR MSG
         OI    ACTFLAGS,ACTFRM              RETURN THE MESSAGE
         BAL   14,EXCMD                     EXECUTE REQUESTED CMD'S
AEND     LM    14,13,GRSAREA1
         BR    14                           RETURN TO THE CALLER
         EJECT
*
* WRITE THE MESSAGE TO THE TSO TERMINAL
*
TPUT     STM   14,13,GRSAREA3
         TM    ACTFLAGS,ACTFWTT             Q. TSO MESSAGE REQUESTED
         BNO   TPUTEND                      A. NO- END OF SUBROUTINE
         TM    ACTFLAGS,ACTFMRR             Q. MESSAGE REPLY REQUESTED
         BO    TPUTEND                      A. YES- END OF SUBROUTINE
         LA    2,MSGWORK                    GET ADDRESS OF MESSAGE
         TPUT  (2),120                      SEND REQUESTED MESSAGE
         NI    ACTFLAGS,255-ACTFWTT         TURN OFF TSO INICATOR
TPUTEND  LM    14,13,GRSAREA3
         BR    14                           RETURN TO THE CALLER
         SPACE
*
* WRITE THE MESSAGE TO THE SYSTEM OPERATOR
*
WTO      STM   14,13,GRSAREA3
         TM    ACTFLAGS,ACTFWSO             Q. WTO MESSAGE REQUESTED
         BNO   WTOEND                       A. NO- END OF SUBROUTINE
         TM    ACTFLAGS,ACTFMRR             Q. MESSAGE REPLY REQUESTED
         BO    WTOEND                       A. YES- END OF SUBROUTINE
         MVC   WOMSG+4(120),MSGWORK         MOVE MSGAREA INTO WTLAREA
         ICM   1,7,RCDAD                    GET ADDRESS OF MCSFLAG
         L     0,0(1)                       GET MCSFLAG
         WTO   MF=(E,WOMSG)                 SEND OPERATOR MESSAGE
         NI    ACTFLAGS,255-ACTFWSO         TURN OFF WTO INDICATOR
WTOEND   LM    14,13,GRSAREA3
         BR    14                           RETURN TO THE CALLER
         EJECT
*
* WRITE THE MESSAGE INTO THE SYSTEM LOG
*
WTL      STM   14,13,GRSAREA3
         TM    ACTFLAGS,ACTFWJL             Q. WTL MESSAGE REQUESTED
         BNO   WTLEND                       A. NO- END OF SUBROUTINE
         MVC   WLMSG+4(120),MSGWORK         MOVE MSGAREA INTO WTLAREA
         WTL   MF=(E,WLMSG)
         NI    ACTFLAGS,255-ACTFWJL         TURN OFF WTL INDICATOR
WTLEND   LM    14,13,GRSAREA3
         BR    14                           RETURN TO THE CALLER
         SPACE
*
* RETURN REQUESTED MESSAGE INTO CALLERS MESSAGE AREA
*
RMSG     STM   14,13,GRSAREA3
         TM    ACTFLAGS,ACTFRM              Q. RETURN MESSAGE REQUESTED
         BNO   RMSGEND                      A. NO- END OF SUBROUTINE
         ICM   2,15,RPLAREA                 GET REPLY AREA POINTER
         SRDA  2,24                         GET AREA LENGTH IN REG2
         SRL   3,8                          GET AREA ADDRESS IN REG3
         CH    2,=H'120'                    Q. BUFFER LEN > 120
         BNH   EXMVC                        A. NO- EX MSG TRANSLATE
         LA    2,120                        GET DEFAULT MAX LENGTH
         MVC   RETURN,=X'0004'              SET PROGRAM RETURN CODE
EXMVC    BCTR  2,0                          MINUS ONE FOR MVC
         EX    2,RMSGMVC                    MOVE REQUESTED MESSAGE
         NI    ACTFLAGS,255-ACTFRM          TURN OFF RETURN INDICATOR
RMSGEND  LM    14,13,GRSAREA3
         BR    14                           RETURN TO THE CALLER
         SPACE
RMSGMVC  MVC   0(0,3),MSGWORK               MOVE REQUESTED MESSAGE
         EJECT
*
* WRITE MESSAGE TO THE SYSTEM OPERATOR WITH REPLY
*
WTOR     STM   14,13,GRSAREA3
         TM    ACTFLAGS,ACTFWSO+ACTFMRR     Q. WTOR MESSAGE REQUEST
         BNO   WTOREND                      A. NO- END OF SUBROUTINE
         MVC   WORMSG+12(120),MSGWORK       MOVE MESSAGE INTO WTLAREA
         XC    WTORECB,WTORECB              CLEAR ECB
         ICM   2,15,RPLAREA                 GET REPLY AREA POINTER
         SRDA  2,24                         GET AREA LENGTH IN REG2
         SRL   3,8                          GET AREA ADDRESS IN REG3
         CH    2,=H'115'                    Q. MAX. LENGTH > 115
         BNH   EXWTOR                       A. NO- SEND OPER REQUEST
         LA    2,115                        GET MAX. REPLY LENGTH
         MVC   RETURN,=X'0004'              SET PROGRAM RETURN CODE
EXWTOR   ICM   1,7,RCDAD                    GET ADDRESS OF MCSFLAG
         L     0,0(1)                       GET MCSFLAG
         WTOR  ,(3),(2),WTORECB,MF=(E,WORMSG)
         WAIT  ECB=WTORECB                  WAIT FOR MSG RESPONSE
         BCTR  2,0                          MINUS ONE FOR MVC
         EX    2,WTOROC                     MAKE ALL CAPITALS
         NI    ACTFLAGS,255-ACTFWSO-ACTFMRR   TURN OFF WTOR REQUEST
WTOREND  LM    14,13,GRSAREA3
         BR    14                           RETURN TO THE CALLER
         SPACE
WTOROC   OC    0(0,3),BLANK                 MAKE ALL CAPITALS
         EJECT
*
* WRITE MESSAGE TO THE TSO USER WITH REPLY
*
TGET     STM   14,13,GRSAREA3
         TM    ACTFLAGS,ACTFWTT+ACTFMRR     Q. TGET MESSAGE REQUEST
         BNO   TGETEND                      A. NO- END OF SUBROUTINE
         LA    3,MSGWORK                    GET ADDRESS OF MESSAGE
         TPUT  (3),120                      SEND TSO MESSAGE
         ICM   2,15,RPLAREA                 GET REPLY AREA POINTER
         SRDA  2,24                         GET AREA LENGTH IN REG2
         SRL   3,8                          GET AREA ADDRESS IN REG3
         CH    2,=H'115'                    Q. MAX. LENGTH > 115
         BNH   EXTGET                       A. NO- SEND TSO USER REQ.
         LA    2,115                        GET MAX. REPLY LENGTH
         MVC   RETURN,=X'0004'              SET PROGRAM RETURN CODE
EXTGET   TGET  (3),(2)                      GET REPLY MESSAGE
         BCTR  2,0                          MINUS ONE FOR EXECUTE
         EX    2,TGETOC                     MAKE ALL CAPITALS
         NI    ACTFLAGS,255-ACTFWTT-ACTFMRR  TURN OFF TPUT/TGET REQUEST
TGETEND  LM    14,13,GRSAREA3
         BR    14                           RETURN TO THE CALLER
         SPACE
TGETOC   OC    0(0,3),BLANK                 MAKE ALL CAPITALS
         EJECT
*
* DEFINITIONEN / LITERALS
*
         LTORG
         EJECT
*
* DEFINITIONEN
*
XMSGCSCT CSECT
GRSAREA1 DS    16A                          INTERNAL SAVE AREA 1. LVL
GRSAREA2 DS    16A                          INTERNAL SAVE AREA 2. LVL
GRSAREA3 DS    16A                          INTERNAL SAVE AREA 3. LVL
APARMS   DS    10A                          PARAMETER LIST
CMDADDR  DS    A                            ADDRESS OF CMD SUBROUTINE
WTORECB  DS    F                            ECB AREA
RETURN   DS    H                            PROGRAM RETURN CODE
BLANK    DC    CL120' '                     CLEAR AREA
MSGWORK  DC    CL120' '                     MESSAGE WORK AREA
WLMSG    WTO   '1234567890123456789012345678901234567890123456789012345*
               67890123456789012345678901234567890123456789012345678901*
               234567890   ',MF=L
WOMSG    WTO   '1234567890123456789012345678901234567890123456789012345*
               67890123456789012345678901234567890123456789012345678901*
               234567890   ',MCSFLAG=REG0,MF=L
WORMSG   WTOR  '1234567890123456789012345678901234567890123456789012345*
               67890123456789012345678901234567890123456789012345678901*
               234567890 ',MSGWORK,115,WTORECB,MCSFLAG=REG0,MF=L
         EJECT
ACTFLAGS DC    B'00000000'                  ACTION INDICATOR BYTE
ACTFRM   EQU   B'00000001'                  REURN THE MESSAGE
ACTFWSO  EQU   B'00000010'                  WRITE TO SYSTEM OPERATOR
ACTFMRR  EQU   B'00000100'                  MESSAGE REPLY REQUEST
ACTFWTT  EQU   B'00001000'                  WRITE TO TIME SHARING TASK
ACTFWJL  EQU   B'00010000'                  WRITE TO JES-LOG
ACTFWAO  EQU   B'00100000'                  WRITE TO ANY OPERATORS
ACTFME   EQU   B'01000000'                  MESSAGE ERROR
ACTFVL   EQU   B'10000000'                  USED FOR VL BIT
         SPACE
XMSGCSLE EQU   *-XMSGCSCT                   TOTAL LENGTH OF CSECT
         SPACE
PARMAREA DSECT
RPLAREA  DS    AL4                          ADDRESS OF REPLY AREA
MSGLEN   DS    AL1                          LENGTH OF INPUT MESSAGE
MSGPTR   DS    AL3                          ADDRESS OF INPUT MESSAGE
ACTION   DS    AL1                          MESSAGE ACTION BITS
RCDAD    DS    AL3                          ADDRESS OF ROUTING CODE
PARM01   DS    AL4                          FIRST ADDRESS OF PARMS
         SPACE
         END
