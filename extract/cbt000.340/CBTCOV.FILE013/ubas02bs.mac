ZS905ABT CSECT
         REG
         XSAVE (R11,R12),SVA,ZS905ABT ZIMPELMANN
         L     R3,0(R1)
         LA    R3,0(R3)
         LTR   R3,R3
         BZ    CLOSDAT
         USING Y1,R3
         L     R5,Y1TADR
         AH    R5,Y1ESIZ
         ST    R5,TABADR
         L     R5,8(R5)
         ST    R5,CHTABADR
         SPACE 1
         L     R5,Y1TADR
         LH    R10,0(R5)              LAENGE DER EINGABE SPEICHERN
         LA    R5,2(R5)
         LA    R6,0
         LA    R7,TABFLD
         SR    R8,R8
LOOP1    EQU   *
         IC    R8,0(R7)
         LTR   R8,R8
         BNZ   LOOP1A
         LA    R9,13
         B     FEHLER                  UNDEFINIERTER PARAMETER
LOOP1A   EQU   *
         LA    R7,1(R7)
         BCTR  R8,0
         EX    R8,EXCLC1
         BE    PARMGEF
         LA    R7,1(R8,R7)
         LA    R6,1(R6)
         B     LOOP1
         SPACE 2
PARMGEF  EQU   *
         LA    R8,1(R8)
         LA    R5,1(R8,R5)
         SR    R10,R8
         SLL   R6,2                    MULTIPLY BY 4
         LA    R4,AROUTAB(R6)
         L     R15,0(R4)
         BR    R15
         EJECT
DBDROUT  EQU   *
         LA    R8,0
         LA    R7,PARMDBD
         SR    R6,R6
LOOPDBD  EQU   *
         IC    R6,0(R7)
         LTR   R6,R6
         BNZ   LOOPDBD1
         LA    R9,14                   FEHLERHAFTER PARM IN DBDKART
         B     FEHLER
LOOPDBD1 EQU   *
         LA    R7,1(R7)
         BCTR  R6,0
         EX    R6,EXCLC1
         BE    GEFDBD
         LA    R7,1(R6,R7)
         LA    R8,1(R8)
         B     LOOPDBD
         SPACE 1
GEFDBD   EQU   *
         LA    R5,1(R6,R5)
         LTR   R8,R8
         BNZ   RMDPARM                 RMD-KARTE
         LA    R8,L'BLDMBR
         SR    R1,R1
         SR    R2,R2
         EX    R8,EXTRT1
         BNZ   NOFEHL1
         LA    R9,15                   DBD-NAME ZU LANG
         B     FEHLER
NOFEHL1  EQU   *
         SR    R1,R5
         BCTR  R1,0
         EX    R1,EXMVC1
         CALL  ZS018,(DBDAREA),VL      DBD-INFO HOLEN
         LH    R7,P1RC
         LTR   R7,R7                   RETURN-CODE = 0 ????
         BZ    DBDFOUND                JA
         LA    R7,5
         CH    R7,P1RC                 RETURN-CODE = 5 ????
         BE    DDMISS                  JA
         LA    R9,21
         B     FEHLER
DDMISS   EQU   *
         LA    R9,68
         B     FEHLER
DBDFOUND EQU   *
         CLI   P1ORGF,P1HDAM                       HDAM-DATENBANK ?
         BE    HDAMDB
         LA    R9,57                   RMR NUR BEI HDAM-DB
         B     FEHLER
HDAMDB   EQU   *
         MVC   RMNM,P1RMR              GET RANDOMIZING-NAME
         MVC   Y1AKPT,P1APNO           GET ANKERPUNKTE/BLOCK
         MVC   Y1RAAB,P1RAANO          GET ANZAHL BLOECKE
         B     DBDEND
         SPACE 2
RMDPARM  EQU   *
         MVI   TAB1+C')',X'00'
         MVI   TAB1+C',',X'04'
         LA    R6,L'RMNM
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT1
         BNZ   NOFEHL2
         LA    R9,16                   RANDOMIZING-NAME > 8 STELLEN
         B     FEHLER
NOFEHL2  EQU   *
         MVI   TAB1+C')',X'04'
         MVI   TAB1+C',',X'00'
         SR    R1,R5
         BCTR  R1,0
         EX    R1,EXMVC2
         LA    R5,2(R1,R5)
         MVI   TAB2+C')',X'04'
         MVI   TAB2+C',',X'0C'
         LA    R6,3                    ANKERPUNKTFELD MAX. 3 STELLEN
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT2
         BNZ   NOFEHL3
         LA    R9,17                   ANKERPUNKTEFELD > 3 STELLEN
         B     FEHLER
NOFEHL3  EQU   *
         B     *(R2)
         LA    R9,18                   ANKERPKTEFELD NICHT NUMERISCH
         B     FEHLER
         SR    R1,R5
         BCTR  R1,0
         EX    R1,EXPACK1
         CVB   R2,PACKF1
         STH   R2,Y1AKPT
         LA    R5,2(R1,R5)
         MVI   TAB2+C',',X'04'
         MVI   TAB2+C')',X'0C'
         LA    R6,6                   RAABFELD MAX. 6 STELLEN
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT2
         BNZ   NOFEHL4
         LA    R9,19                   RAABFELD ZU GROSS
         B     FEHLER
NOFEHL4  EQU   *
         B     *(R2)
         LA    R9,20                   RAABFELD NICHT NUMERISCH
         B     FEHLER
         SR    R1,R5
         BCTR  R1,0
         EX    R1,EXPACK1
         CVB   R2,PACKF1
         ST    R2,Y1RAAB
DBDEND   EQU   *
         OPEN  (USERLIB)
         LA    R7,USERLIB
         TM    DCBOFLGS-IHADCB(R7),DCBOFOPN
         BO    USERDA
         MVI   Y1RTCE,Y1RYES           AUF ENDE SETZEN
         LA    R9,67
         B     FEHLER
USERDA   EQU   *
         MVC   BLDMBR,RMNM
         BLDL  USERLIB,BLDLIST
         LTR   R15,R15
         BZ    RMRFOUND
         XC    Y1RAAB,Y1RAAB          LOESCHEN RAAS ----> KEINE KEYVAB
         LA    R9,58                  RAND. ROUTINE NICHT EXISTENT
         B     FEHLER
RMRFOUND EQU   *
         LOAD  DE=BLDMBR,DCB=USERLIB
         ST    R0,Y1ARMR
         CLOSE (USERLIB)
         B     ENDE
         EJECT
POTROUT  EQU   *
         LTR   R10,R10
         BNZ   PRMVGL
         LA    R9,22                   POINT-KARTE LEER
         B     FEHLER
PRMVGL   EQU   *
         BCTR  R10,0
         LA    R7,POITAB
         SR    R8,R8
         LA    R6,0
POTLOOP  EQU   *
         IC    R8,0(R7)
         LA    R7,1(R7)
         LTR   R8,R8
         BNZ   POTLOOPA
         LA    R9,23           PARAMETER IN POINT-KARTE NICHT ZULAESSIG
         B     FEHLER
POTLOOPA EQU   *
         BCTR  R8,0
         EX    R8,EXCLC1
         BE    GEFPOI
         LA    R6,1(R6)
         LA    R7,1(R8,R7)
         B     POTLOOP
         SPACE 2
GEFPOI   EQU   *
         LA    R8,1(R8)
         LA    R5,0(R8,R5)
         SR    R10,R8
         L     R1,TABADR
         L     R4,4(R1)               NAECHSTES FREIES TABELEMENT
         USING BLSOTAB,R4
         SLL   R6,2                   MULTIPLY BY 4
         LA    R7,ADRPOI(R6)
         L     R15,0(R7)
         BR    R15
         SPACE 2
BLKPOI   EQU   *
         LA    R6,7                   BLOCKNUMMER MAX.7 STELLEN
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT3
         BNZ   NOFEHL5
         LA    R9,24                  BLOCKNR > 7 STELLEN
         B     FEHLER
NOFEHL5  EQU   *
         B     *(R2)
         LA    R9,25                  POINT-KARTE HAT MEHRERE BLOECKE
         B     FEHLER
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         BAL   R15,HDROUT
         ST    R9,BLKVON
         ST    R9,BLKBIS
         LTR   R10,R10
         BZ    POINTEND
         LA    R9,26                  POINT-KARTE HAT MEHRERE PARAMETER
         B     FEHLER
         SPACE 3
RBAPOI   EQU   *
         LA    R6,10                  RBA-NUMMER MAX.10 STELLEN
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT3
         BNZ   NOFEHL9
         LA    R9,27                  RBA-NR > 10 STELLEN
         B     FEHLER
NOFEHL9  EQU   *
         B     *(R2)
         LA    R9,28                  POINT-KARTE HAT MEHRERE RBAS
         B     FEHLER
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         BAL   R15,HDROUT
         LH    R7,Y1BLKS
         SR    R8,R8
         DR    R8,R7
         ST    R9,BLKVON
         ST    R9,BLKBIS
         STH   R8,OFFVON
         LTR   R10,R10
         BZ    POINTEND
         LA    R9,26                  POINT-KARTE HAT MEHRERE PARAMETER
         B     FEHLER
         SPACE 3
KEYPOI   EQU   *
         L     R1,Y1RAAB
         LTR   R1,R1
         BNZ   DBDDA
         LA    R9,29
         B     FEHLER
DBDDA    EQU   *
         BAL   R15,KEYROUT
         LTR   R10,R10
         BZ    POINTEND
         LA    R9,26          POINT-KARTE NUR 1 KEY-PARAMETER
         B     FEHLER
         SPACE 3
POINTEND EQU   *
         MVI   Y1POKZ,Y1PYES
         B     ENDE
         EJECT
PRTROUT  EQU   *
         LTR   R10,R10
         BNZ   PRTVGL
         LA    R9,31                   PRINT-KARTE LEER
         B     FEHLER
PRTVGL   EQU   *
         L     R1,TABADR
         L     R4,4(R1)
         USING BLSOTAB,R4
         L     R15,CHTABADR
         LA    R1,BTABEND
         CR    R15,R1
         BNL   GENSIZ6
         LA    R9,59           EINGABE-SIZE ZU KLEIN
         B     FEHLER
GENSIZ6  EQU   *
         BCTR  R10,0
         LA    R7,POITAB
         SR    R8,R8
         LA    R6,0
PRTLOOP  EQU   *
         IC    R8,0(R7)
         LA    R7,1(R7)
         LTR   R8,R8
         BZ    VORPNT1
         BCTR  R8,0
         EX    R8,EXCLC1
         BE    GEFPRT
         LA    R6,1(R6)
         LA    R7,1(R8,R7)
         B     PRTLOOP
         SPACE 2
GEFPRT   EQU   *
         LA    R5,1(R8,R5)
         LA    R8,1(R8)
         SR    R10,R8
         SLL   R6,2                   MULTIPLY BY 4
         LA    R1,ADRPRT(R6)
         L     R15,0(R1)
         BR    R15
         SPACE 2
VORPNT1  EQU   *
         MVC   OFFBIS,Y1BLKS
         SR    R1,R1
         STH   R1,OFFVON
         STH   R1,ZWILEN
VORPOINT EQU   *
         LA    R7,PARMTAB
         LA    R6,0
         SR    R8,R8
PRMLOOP1 EQU   *
         IC    R8,0(R7)
         LTR   R8,R8
         BNZ   PRMLP2
         LA    R9,34                PARAMETER NICHT ZULAESSIG
         B     FEHLER
PRMLP2   EQU   *
         LA    R7,1(R7)
         BCTR  R8,0
         EX    R8,EXCLC1
         BE    GEFPRM1
         LA    R7,1(R8,R7)
         LA    R6,1(R6)
         B     PRMLOOP1
GEFPRM1  EQU   *
         CLI   Y1POKZ,Y1PYES
         BE    GEFPRM2
         LA    R9,32              KEIN POINT GESETZT !!!!!!!
         B     FEHLER
GEFPRM2  EQU   *
         SLL   R6,2             MULTIPLY BY 4
         LA    R9,ADRPARM1(R6)
         L     R15,0(R9)
         BR    R15
PARMOFF1 EQU   *
         BAL   R15,OFFROUT
         B     PARMEND1
PARMLEN1 EQU   *
         BAL   R15,LENROUT
         B     PARMEND1
PARMCOU1 EQU   *
         BAL   R15,CNTROUT
         L     R1,BLKVON
         BCTR  R1,0
         AR    R9,R1
         ST    R9,BLKBIS
PARMEND1 EQU   *
         LTR   R10,R10
         LA    R7,PRTEND
         BZ    PRTPEND
         LA    R5,2(R5)
         BCTR  R10,0
         B     VORPOINT
         EJECT
BLKPRT   EQU   *
         MVC   ETRGANZ,=H'1'          ANZAHL BLOCKEINTRAEGE = 1
BLKPRT1  EQU   *
         LA    R7,7
         SR    R1,R1
         MVC   OFFBIS,Y1BLKS
         STH   R1,OFFVON
         STH   R1,ZWILEN
         ST    R1,VALADR
         SR    R2,R2
         EX    R7,EXTRT5
         BNZ   GETLBLK
         LA    R9,24                   BLOCKNR > 7 STELLEN
         B     FEHLER
GETLBLK  EQU   *
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         B     *(R2)
         B     MEHRBLK                 BLKNR,
         B     VBBLK                   BLKNR-
         B     BLKANZ                  BLKNR/
         BAL   R15,HDROUT
         ST    R9,BLKVON
         ST    R9,BLKBIS
         LTR   R10,R10
         BZ    PRTEND
         B     WEITPRM
MEHRBLK  EQU   *
         BAL   R15,HDROUT
         ST    R9,BLKVON
         ST    R9,BLKBIS
         B     ENDABFR
VBBLK    EQU   *
         BAL   R15,HDROUT
         ST    R9,BLKVON              BLKVON IN TABELLE SPEICHERN
         LA    R5,1(R5)               - ZEICHEN UEBERSPRINGEN
         LA    R7,7
         SR    R1,R1
         SR    R2,R2
         EX    R7,EXTRT3
         BNZ   NOFEHL12
         LA    R9,24                  BLOCKNR > 7 STELLEN
         B     FEHLER
NOFEHL12 EQU   *
         SR    R1,R5
         LR    R6,R1
         BAL   R15,HDROUT
         ST    R9,BLKBIS
         CLC   BLKBIS,BLKVON
         BNL   ENDABFR
         LA    R9,33                 BLOCK-BIS < BLOCK-VON
         B     FEHLER
BLKANZ   EQU   *
         BAL   R15,HDROUT
         ST    R9,BLKVON
         LA    R5,1(R5)              / ZEICHEN UEBERSPRINGEN
         LA    R7,7
         SR    R1,R1
         SR    R2,R2
         EX    R7,EXTRT3
         BNZ   NOFEHL13
         LA    R9,24                  BLOCKNR > 7 STELLEN
         B     FEHLER
NOFEHL13 EQU   *
         SR    R1,R5
         LR    R6,R1
         BAL   R15,HDROUT
         L     R1,BLKVON
         BCTR  R1,0
         AR    R9,R1
         ST    R9,BLKBIS
ENDABFR  EQU   *
         LTR   R10,R10              ENDE DER EINGABEKARTE ????
         BZ    PRTEND               JA,ENDE ERREICHT
         CLI   0(R5),C')'           WEITERE PARAMETER VORHANDEN ???
         BE    WEITPRM              JA
         LA    R4,BTABEND           NAECHSTES TABELLENELEMENT
         L     R15,CHTABADR         ADRESSE TABELLENENDE
         LA    R1,BTABEND           ADRESSE ENDE DES TABELEMENTS
         CR    R15,R1               REICHT TABELLENGROESSE NOCH AUS??
         BNL   GENSIZ1              JA
         LA    R9,59                EINGABE-SIZE ZU KLEIN
         B     FEHLER
GENSIZ1  EQU   *
         LH    R6,ETRGANZ
         LA    R6,1(R6)             ANZAHL BLKEINTRAEGE UM 1 ERHOEHEN
         STH   R6,ETRGANZ
         LA    R5,1(R5)
         B     BLKPRT1
         SPACE 3
WEITPRM  EQU   *
         LA    R5,2(R5)             ) + BLANK UEBERSPRINGEN
         BCTR  R10,0
         LA    R7,PARMTAB
         LA    R6,0
         SR    R8,R8
PRMLOOP  EQU   *
         IC    R8,0(R7)
         LTR   R8,R8
         BNZ   PRMLP1
         LA    R9,34                PARAMETER NICHT ZULAESSIG
         B     FEHLER
PRMLP1   EQU   *
         LA    R7,1(R7)
         BCTR  R8,0
         EX    R8,EXCLC1
         BE    GEFPARM
         LA    R7,1(R8,R7)
         LA    R6,1(R6)
         B     PRMLOOP
GEFPARM  EQU   *
         SLL   R6,2             MULTIPLY BY 4
         LA    R9,ADRPARM2(R6)
         L     R15,0(R9)
         BR    R15
PARMOFF  EQU   *
         BAL   R15,OFFROUT
         B     PARMEND
PARMLEN  EQU   *
         BAL   R15,LENROUT
         B     PARMEND
PARMCOU  EQU   *
         BAL   R15,CNTROUT
         L     R1,BLKVON
         BCTR  R1,0
         AR    R9,R1
         ST    R9,BLKBIS
PARMEND  EQU   *
         LTR   R10,R10
         LA    R7,PRTBEND
         BZ    PRTPEND
         B     WEITPRM
         SPACE 3
PRTPEND  EQU   *
         LH    R9,ZWILEN
         LTR   R9,R9
         BZR   R7
         LH    R1,OFFVON
         AR    R9,R1
         CH    R9,Y1BLKS
         BNH   NOFEHL14
         LA    R9,35                   OFFSET+LENGTH > BLOCKSIZE
         B     FEHLER
NOFEHL14 EQU   *
         STH   R9,OFFBIS
         BR    R7
         SPACE 3
PRTBEND  EQU   *
         LH    R1,ETRGANZ       LADEN ANZAHL BLOCKEINTRAEGE
         BCTR  R1,0
         LTR   R1,R1
         BZ    PRTEND           NUR 1 BLOCKEINTRAG
         LH    R8,OFFVON        LADEN OFFSET-VON
         LH    R5,OFFBIS        LADEN OFFSET-BIS
         MH    R1,BTABLG        LAENGE TABELEMENT * ANZAHL BLOCKEINTR.
         LR    R7,R4            TABENDEADRESSE FUER LETZTEN EINTRAG
         SR    R4,R1            TABANFANGSADRESSE FUER 1. EINTRAG
         LH    R6,BTABLG        LAENGE TABELEMENT
EINTLOOP EQU   *
         STH   R8,OFFVON
         STH   R5,OFFBIS
         BXLE  R4,R6,EINTLOOP
         SR    R4,R6
         B     PRTEND
         SPACE 3
RBAPRT   EQU   *
         LA    R7,10
         SR    R1,R1
         MVC   OFFBIS,Y1BLKS
         STH   R1,OFFVON
         ST    R1,VALADR
         SR    R2,R2
         EX    R7,EXTRT5
         BNZ   GETLRBA
         LA    R9,27                   RBANR > 10 STELLEN
         B     FEHLER
GETLRBA  EQU   *
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         B     *(R2)
         B     MEHRRBA
         B     VBRBA
         B     RBAANZ
         BAL   R15,HDROUT
         LH    R7,Y1BLKS
         SR    R8,R8
         DR    R8,R7
         ST    R9,BLKBIS
         ST    R9,BLKVON
         LTR   R10,R10
         BZ    PRTEND
         STH   R8,OFFVON
         B     LENVGL3
MEHRRBA  EQU   *
         BAL   R15,HDROUT
         LH    R7,Y1BLKS
         SR    R8,R8
         DR    R8,R7
         ST    R9,BLKBIS
         ST    R9,BLKVON
         B     RBAEND
VBRBA    EQU   *
         BAL   R15,HDROUT
         LH    R7,Y1BLKS
         SR    R8,R8
         DR    R8,R7
         STH   R8,OFFVON
         ST    R9,BLKVON
         LA    R5,1(R5)
         LA    R7,10
         SR    R1,R1
         SR    R2,R2
         EX    R7,EXTRT3
         BNZ   NOFEHL18
         LA    R9,27                  RBANR > 10 STELLEN
         B     FEHLER
NOFEHL18 EQU   *
         SR    R1,R5
         LR    R6,R1
         BAL   R15,HDROUT
         LH    R7,Y1BLKS
         SR    R8,R8
         DR    R8,R7
         LA    R8,1(R8)
         C     R9,BLKVON
         BNL   ANZEINTR
         LA    R9,33                 BLOCK-BIS < BLOCK-VON
         B     FEHLER
ANZEINTR EQU   *
         MVC   BLKBIS,BLKVON
         L     R1,BLKVON
         SR    R9,R1
         LTR   R9,R9
         BNZ   VERBLK
         STH   R8,OFFBIS
         CLC   OFFBIS,OFFVON
         BNL   RBAEND
         LA    R9,56                  OFFBIS < OFFVON
         B     FEHLER
VERBLK   EQU   *
         LA    R4,BTABEND
         L     R15,CHTABADR
         LA    R2,BTABEND
         CR    R15,R2
         BNL   GENSIZ2
         LA    R9,59           EINGABE-SIZE ZU KLEIN
         B     FEHLER
GENSIZ2  EQU   *
         BCTR  R9,0
         LTR   R9,R9
         BNZ   BLKMEHR
         LA    R1,1(R1)
         ST    R1,BLKVON
         ST    R1,BLKBIS
         STH   R9,OFFVON
         STH   R8,OFFBIS
         ST    R9,VALADR
         B     RBAEND
BLKMEHR  EQU   *
         AR    R9,R1
         LA    R1,1(R1)
         ST    R1,BLKVON
         MVC   OFFBIS,Y1BLKS
         SR    R2,R2
         STH   R2,OFFVON
         ST    R2,VALADR
         ST    R9,BLKBIS
         LA    R4,BTABEND
         L     R15,CHTABADR
         LA    R1,BTABEND
         CR    R15,R1
         BNL   GENSIZ3
         LA    R9,59           EINGABE-SIZE ZU KLEIN
         B     FEHLER
GENSIZ3  EQU   *
         STH   R2,OFFVON
         ST    R2,VALADR
         STH   R8,OFFBIS
         LA    R9,1(R9)
         ST    R9,BLKVON
         ST    R9,BLKBIS
         B     RBAEND
RBAANZ   EQU   *
         BAL   R15,HDROUT
         LH    R7,Y1BLKS
         SR    R8,R8
         DR    R8,R7
         ST    R9,BLKVON
         LA    R5,1(R5)
         LA    R7,10
         SR    R1,R1
         SR    R2,R2
         EX    R7,EXTRT3
         BNZ   NOFEHL19
         LA    R9,27                  RBANR > 10 STELLEN
         B     FEHLER
NOFEHL19 EQU   *
         SR    R1,R5
         LR    R6,R1
         BAL   R15,HDROUT
         L     R1,BLKVON
         BCTR  R1,0
         AR    R9,R1
         ST    R9,BLKBIS
RBAEND   EQU   *
         LTR   R10,R10
         BZ    PRTEND
         LA    R4,BTABEND
         L     R15,CHTABADR
         LA    R1,BTABEND
         CR    R15,R1
         BNL   GENSIZ4
         LA    R9,59           EINGABE-SIZE ZU KLEIN
         B     FEHLER
GENSIZ4  EQU   *
         LA    R5,1(R5)
         B     RBAPRT
         SPACE 3
KEYPRT   EQU   *
         L     R1,Y1RAAB
         LTR   R1,R1
         BNZ   DBDDA1
         LA    R9,29
         B     FEHLER
DBDDA1   EQU   *
         SR    R1,R1
         STH   R1,OFFVON
         ST    R1,VALADR
         MVC   OFFBIS,Y1BLKS
         BAL   R15,KEYROUT
         LTR   R10,R10
         BZ    PRTEND
         CLI   0(R5),C')'
         BE    LENVGL3
         LA    R5,1(R5)
         LA    R4,BTABEND
         L     R15,CHTABADR
         LA    R1,BTABEND
         CR    R15,R1
         BNL   GENSIZ5
         LA    R9,59           EINGABE-SIZE ZU KLEIN
         B     FEHLER
GENSIZ5  EQU   *
         B     DBDDA1
LENVGL3  EQU   *
         LA    R5,2(R5)
         BCTR  R10,0
         CLC   PLENGTH,0(R5)
         BE    GEFLEN
         LA    R9,39
         B     FEHLER
GEFLEN   EQU   *
         BAL   R15,LENROUT
         LA    R7,LENEND          ZWILEN SPEICHERN
         B     PRTPEND
LENEND   EQU   *
         LTR   R10,R10
         BZ    PRTEND
         LA    R9,39              BEI KEY NUR LENGTH-PARM ERLAUBT
         B     FEHLER
         SPACE 3
PRTEND   EQU   *
         MVI   Y1POKZ,Y1PNO       POINT-KENNZEICHEN LOESCHEN
         LA    R6,BTABEND
         L     R4,TABADR
         ST    R6,4(R4)
         CLI   Y1TFLG,Y1TYES
         BE    TERMEND
         B     ENDE
         EJECT
CHGROUT  EQU   *
         LTR   R10,R10
         BNZ   CHNGVGL
         LA    R9,43            CHANGE-KARTE LEER
         B     FEHLER
CHNGVGL  EQU   *
         L     R1,TABADR
         L     R4,4(R1)
         USING BLSOTAB,R4
         BCTR  R10,0
         LA    R7,CHNGTAB
         SR    R8,R8
         LA    R6,0
CHNGLOOP EQU   *
         IC    R8,0(R7)
         LA    R7,1(R7)
         LTR   R8,R8
         BNZ   NOFEHL20
         LA    R9,44             PARAMETER IN CHNG-KARTE UNZULAESSIG
         B     FEHLER
NOFEHL20 EQU   *
         BCTR  R8,0
         EX    R8,EXCLC1
         BE    GEFCHNG
         LA    R6,1(R6)
         LA    R7,1(R8,R7)
         B     CHNGLOOP
         SPACE 2
GEFCHNG  EQU   *
         LA    R8,1(R8)
         LA    R5,0(R8,R5)
         SR    R10,R8
         MVC   OFFBIS,Y1BLKS
         SR    R1,R1
         STH   R1,OFFVON
         SLL   R6,2                   MULTIPLY BY 4
         LA    R7,ADRCHNG(R6)
         L     R15,0(R7)
         BR    R15
         SPACE 3
BLKCHNG  EQU   *
         LA    R6,7
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT3
         BNZ   NOFEHL21
         LA    R9,24                   BLOCKNR > 7 STELLEN
         B     FEHLER
NOFEHL21 EQU   *
         B     *(R2)
         LA    R9,45                   CHNG-KARTE HAT MEHRERE BLOECKE
         B     FEHLER
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         BAL   R15,HDROUT
         ST    R9,BLKVON
         ST    R9,BLKBIS
         LTR   R10,R10
         BNZ   OFFVGL
         LA    R9,46                   CHNG-KARTE UNVOLLSTAENDIG
         B     FEHLER
         SPACE 3
RBACHNG  EQU   *
         LA    R6,10
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT3
         BNZ   NOFEHL22
         LA    R9,27                   RBA-NR > 10 STELLEN
         B     FEHLER
NOFEHL22 EQU   *
         B     *(R2)
         LA    R9,47                   CHNG-KARTE HAT MEHRERE RBAS
         B     FEHLER
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         BAL   R15,HDROUT
         LH    R7,Y1BLKS
         SR    R8,R8
         DR    R8,R7
         ST    R9,BLKVON
         ST    R9,BLKBIS
         STH   R8,OFFSET
         LTR   R10,R10
         BNZ   VALEINT
         LA    R9,46                   CHNG-KARTE UNVOLLSTAENDIG
         B     FEHLER
         SPACE 3
OFFVGL   EQU   *
         LA    R5,2(R5)
         BCTR  R10,0
         CLC   POFFSET,0(R5)
         BE    OFFDA
         LA    R9,48                   OFFSET FEHLT IN DER CHNG-KARTE
         B     FEHLER
OFFDA    EQU   *
         BAL   R15,OFFROUT
         MVC   OFFSET,OFFVON           OFFSET ABSPEICHERN
         SR    R1,R1
         STH   R1,OFFVON               GANZE BLOCK DRUCKEN
         LTR   R10,R10
         BNZ   VALEINT
         LA    R9,46                   CHNG-KARTE UNVOLLSTAENDIG
         B     FEHLER
         SPACE 3
VALEINT  EQU   *
         LA    R5,2(R5)
         BCTR  R10,0
         CLC   OLDVAL,0(R5)
         BE    OLDVDA
         LA    R9,49                   OLDVALUE-PARAMETER FEHLT
         B     FEHLER
OLDVDA   EQU   *
         LA    R7,L'OLDVAL
         LA    R5,0(R7,R5)
         SR    R10,R7
         LA    R7,255
         SR    R1,R1
         SR    R2,R2
         EX    R7,EXTRT1
         BNZ   NOFEHL23
         LA    R9,50                   OLDVALUE > 256 STELLEN
         B     FEHLER
NOFEHL23 EQU   *
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'2'              KZ FUER VALUE-EINTRAG
         BAL   R15,HDROUT
         LH    R6,VALLEN
         SLL   R6,1                    LAENGE MAL 2
         LA    R8,L'OFFSET
         LA    R2,L'VALLEN
         AR    R8,R2
         AR    R6,R8
         L     R8,CHTABADR
         SR    R8,R6
         LA    R1,BTABEND
         CR    R8,R1
         BNL   NOFEHL24
         LA    R9,59                   SIZE ZU KLEIN
         B     FEHLER
NOFEHL24 EQU   *
         ST    R8,VALADR
         ST    R8,CHTABADR
         LA    R7,L'OFFSET
         MVC   0(L'OFFSET,R8),OFFSET
         LA    R8,0(R7,R8)
         LA    R7,L'VALLEN
         MVC   0(L'VALLEN,R8),VALLEN
         LA    R8,0(R7,R8)
         LH    R7,VALLEN
         BCTR  R7,0
         EX    R7,EXMVC4
         LA    R8,1(R7,R8)
         MVC   OVALLEN,VALLEN
         LTR   R10,R10
         BNZ   NEWVALRT
         LA    R9,51                   NEWVALUE-PARAMETER FEHLT
         B     FEHLER
NEWVALRT EQU   *
         LA    R5,2(R5)
         BCTR  R10,0
         CLC   NEWVAL,0(R5)
         BE    NEWVDA
         LA    R9,44
         B     FEHLER
NEWVDA   EQU   *
         LA    R7,L'NEWVAL
         LA    R5,0(R7,R5)
         SR    R10,R7
         LA    R7,255
         SR    R1,R1
         SR    R2,R2
         EX    R7,EXTRT1
         BNZ   NOFEHL25
         LA    R9,52                   NEWVALUE > 256 STELLEN
         B     FEHLER
NOFEHL25 EQU   *
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'2'
         BAL   R15,HDROUT
         CLC   VALLEN,OVALLEN
         BE    NOFEHL26
         LA    R9,53             NEW- UND OLDVALUE VERSCH. LAENGE
         B     FEHLER
NOFEHL26 EQU   *
         LTR   R10,R10
         BZ    CHNGEND
         LA    R9,44         PARAMETER IN CHANGE-KARTE UNZULAESSIG
         B     FEHLER
         SPACE 2
CHNGEND  EQU   *
         LH    R7,VALLEN
         BCTR  R7,0
         EX    R7,EXMVC4
         LA    R6,BTABEND
         L     R8,CHTABADR
         L     R4,TABADR
         ST    R6,4(R4)
         ST    R8,8(R4)
         CLI   Y1TFLG,Y1TYES
         BE    TERMEND
         B     ENDE
         EJECT
TERMEND  EQU   *
         MVI   Y1RTCE,Y1RTRM           RETURNCODE AUF RT-TERM SETZEN
         B     ENDE
ENDROUT  EQU   *
         MVI   Y1RTCE,Y1RYES           RETURNCODE AUF ENDE SETZEN
         B     ENDE
         EJECT
KEYROUT  EQU   *
         ST    R15,KEYADR
         LA    R7,L'KEY-2
         SR    R1,R1
         SR    R2,R2
         EX    R7,EXTRT3
         BNZ   NOFEHL10
         LA    R9,30                   KEY > 256 STELLEN
         B     FEHLER
NOFEHL10 EQU   *
         SR    R1,R5
         LR    R6,R1
         BCTR  R1,0
         MVI   KEYKZ,C'1'
         BAL   R15,HDROUT
         LH    R1,VALLEN
         BCTR  R1,0                    GET EXECUTIVE KEY-LENGTH
         STC   R1,KEYL
         L     R2,Y1ARMR
         LA    R1,MYDACS
         USING DMBDACS,R1
         ST    R2,DMBDAEP
         MVC   DMBDARAP,Y1AKPT        ANKERPUNKTE
         MVC   DMBDABLK,Y1RAAB        BLOCKANZAHL
         MVC   DMBDAKL,KEYL           KEY-LENGTH
         SR    R0,R0
         LA    R7,PST
         LA    R9,KEY
         L     R15,DMBDAEP
         SLL   R15,8
         SRL   R15,8
         BALR  R14,R15
         L     R8,DMBDACP
         SRDA  R8,8                    VERSCHWINDEN ANKERPUNKT
         SRL   R9,24                   GET ANKERPUNKT
         MVC   ANKDEZ,MASKE1
         CVD   R9,DOUBLE
         ED    ANKDEZ,DOUBLE+6
         MVC   BLKDEZ,MASKE2
         CVD   R8,DOUBLE
         ED    BLKDEZ,DOUBLE+3
         CALL  ZS905WRF,((R3),AREA),VL
         ST    R8,BLKVON
         ST    R8,BLKBIS
         L     R15,KEYADR
         BR    R15
         EJECT
* UMRECHNUNG DER DEZIMAL- ODER HEXADEZIMALZAHL IN REGISTER 9
* ANFANGSADRESSE DER ZAHL(R5),LAENGE DER ZAHL(R6)
*
* KEY-KZ=0   BLKNR,LAENGE,RBA,OFFSET,COUNT   DEFAULT=HEXA
* KEY-KZ=1   KEY                             DEFAULT=CHARACTER
* KEY-KZ=2   VALUE-EINTRAG                   DEFAULT=HEXA
HDROUT   EQU   *
         ST    R15,HDRADR
         SR    R10,R6
         LA    R7,2
         CLC   0(2,R5),=X'C47D'
         BE    DEZIMAL
         CLC   0(2,R5),=X'E77D'
         BE    HEXA
         CLC   0(2,R5),=X'C37D'
         BE    CHARACT
         CLI   KEYKZ,C'1'
         BE    DEFKEY
         B     HEXA1
HEXA     EQU   *
         LA    R5,0(R7,R5)
         SR    R6,R7
HEXA1    EQU   *
         BCTR  R6,0
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT4
         BZ    NOFEHL7
         LA    R9,40                  HEXADEZIMALE UNGUELTIG
         B     FEHLER
NOFEHL7  EQU   *
         LA    R6,1(R6)
         EX    R6,EXTR1
         CLI   KEYKZ,C'0'
         BE    NOKPACK
         SRDA  R6,1                   DURCH 2 TEILEN
         LTR   R7,R7                  DURCH 2 TEILBAR ????????
         BP    GERADE
         LA    R9,55
         B     FEHLER
GERADE   EQU   *
         STH   R6,VALLEN
         SLL   R6,1                   MAL 2 (ALTER WERT)
         LA    R9,14
         LA    R1,KEY
HEXLOOP  EQU   *
         LA    R2,7
         CR    R6,R9
         BNH   RESTPACK
         SLL   R2,4
         OR    R2,R9
         EX    R2,EXPACK3
         SRL   R2,4
         LA    R1,0(R2,R1)
         LA    R5,0(R9,R5)
         SR    R6,R9
         B     HEXLOOP
RESTPACK EQU   *
         LR    R2,R6
         SRL   R2,1                    DURCH 2 TEILEN
         SLL   R2,4
         OR    R2,R6
         EX    R2,EXPACK3
         B     HDREND
NOKPACK  EQU   *
         EX    R6,EXPACK2
         L     R9,PACKF1+4
         B     HDREND
DEZIMAL  EQU   *
         LA    R5,0(R7,R5)
         SR    R6,R7
         STH   R6,VALLEN
         BCTR  R6,0
         SR    R1,R1
         SR    R2,R2
         EX    R6,EXTRT2
         BZ    NOFEHL8
         LA    R9,41                   DEZIMALZAHL UNGUELTIG
         B     FEHLER
NOFEHL8  EQU   *
         CLI   KEYKZ,C'0'
         BE    NOKEY1
         EX    R6,EXMVC3
         LA    R6,1(R6)
         B     HDREND
NOKEY1   EQU   *
         EX    R6,EXPACK1
         CVB   R9,PACKF1
         LA    R6,1(R6)
         B     HDREND
CHARACT  EQU   *
         CLI   KEYKZ,C'0'
         BE    DEZIMAL
         LA    R5,0(R7,R5)
         SR    R6,R7
DEFKEY   EQU   *
         STH   R6,VALLEN
         BCTR  R6,0
         EX    R6,EXMVC3
         LA    R6,1(R6)
HDREND   EQU   *
         LA    R5,0(R6,R5)
         BCTR  R10,0
         L     R15,HDRADR
         BR    R15
         EJECT
OFFROUT  EQU   *
         ST    R15,OFFADR
         LA    R7,L'POFFSET
         LA    R5,0(R7,R5)
         SR    R10,R7
         SR    R1,R1
         SR    R2,R2
         LA    R7,10
         EX    R7,EXTRT1
         BNZ   NOFEHL15
         LA    R9,36                   OFFSET-PARAMETER > 10 STELLEN
         B     FEHLER
NOFEHL15 EQU   *
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         BAL   R15,HDROUT
         STH   R9,OFFVON
         CLC   OFFBIS,OFFVON
         BNL   OFFOK
         LA    R9,42                  OFFSET > BLOCKSIZE
         B     FEHLER
OFFOK    EQU   *
         L     R15,OFFADR
         BR    R15
         SPACE 3
LENROUT  EQU   *
         ST    R15,LENADR
         LA    R7,L'PLENGTH
         LA    R5,0(R7,R5)
         SR    R10,R7
         SR    R1,R1
         SR    R2,R2
         LA    R7,7
         EX    R7,EXTRT1
         BNZ   NOFEHL16
         LA    R9,37                   LENGTH-PARAMETER > 7 STELLEN
         B     FEHLER
NOFEHL16 EQU   *
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         BAL   R15,HDROUT
         STH   R9,ZWILEN
         L     R15,LENADR
         BR    R15
         SPACE 3
CNTROUT  EQU   *
         ST    R15,CNTADR
         LA    R7,L'PCOUNT
         LA    R5,0(R7,R5)
         SR    R10,R7
         SR    R1,R1
         SR    R2,R2
         LA    R7,7
         EX    R7,EXTRT1
         BNZ   NOFEHL17
         LA    R9,38                   COUNT-PARAMETER > 7 STELLEN
         B     FEHLER
NOFEHL17 EQU   *
         SR    R1,R5
         LR    R6,R1
         MVI   KEYKZ,C'0'
         BAL   R15,HDROUT
         L     R15,CNTADR
         BR    R15
         EJECT
CLOSDAT  EQU   *
         DELETE EPLOC=RMNM
         B     ENDE
FEHLER   EQU   *
         STH   R9,Y1MSGC
         CALL  ZS905FHL,((R3)),VL
         CLI   Y1TFLG,Y1TYES
         BE    ENDE
         LA    R1,59
         CR    R1,R9
         BNH   ENDE
         XC    Y1MSGC,Y1MSGC
ENDE     EQU   *
         XRETURN 0
         EJECT
USERLIB  DCB   DDNAME=USERLIB,DSORG=PO,                                *
               MACRF=R
         EJECT
TABFLD   DS    0CL1
         DC    AL1(L'DBDFLD)
DBDFLD   DC    C'DBD'
         DC    AL1(L'POTFLD)
POTFLD   DC    C'POINT'
         DC    AL1(L'PNTFLD)
PNTFLD   DC    C'PRINT'
         DC    AL1(L'CHGFLD)
CHGFLD   DC    C'CHANGE'
         DC    AL1(L'ENDFLD)
ENDFLD   DC    C'END'
         DC    AL1(0)
         SPACE 2
AROUTAB  DS    0F
         DC    A(DBDROUT)
         DC    A(POTROUT)
         DC    A(PRTROUT)
         DC    A(CHGROUT)
         DC    A(ENDROUT)
         SPACE 1
PARMDBD  DS    0CL1
         DC    AL1(L'NAMFLD)
NAMFLD   DC    C'NAME('
         DC    AL1(L'RDMFLD)
RDMFLD   DC    C'RDM('
         DC    AL1(0)
         SPACE 1
POITAB   DS    0CL1
         DC    AL1(L'BLKFLD)
BLKFLD   DC    C'BLK('
         DC    AL1(L'CIFLD)
CIFLD    DC    C'CI('
         DC    AL1(L'RBAFLD)
RBAFLD   DC    C'RBA('
         DC    AL1(L'KYFELD)
KYFELD   DC    C'KEY('
         DC    AL1(0)
         SPACE 1
CHNGTAB  DS    0CL1
         DC    AL1(L'BLKFLD1)
BLKFLD1  DC    C'BLK('
         DC    AL1(L'CIFLD1)
CIFLD1   DC    C'CI('
         DC    AL1(L'RBAFLD1)
RBAFLD1  DC    C'RBA('
         DC    AL1(0)
         SPACE 1
PARMTAB  DS    0CL1
         DC    AL1(L'POFFSET)
POFFSET  DC    C'OFFSET('
         DC    AL1(L'PLENGTH)
PLENGTH  DC    C'LENGTH('
         DC    AL1(L'PCOUNT)
PCOUNT   DC    C'COUNT('
         DC    AL1(0)
         SPACE 1
ADRPOI   DS    0F
         DC    A(BLKPOI)
         DC    A(BLKPOI)
         DC    A(RBAPOI)
         DC    A(KEYPOI)
         SPACE 1
ADRPRT   DS    0F
         DC    A(BLKPRT)
         DC    A(BLKPRT)
         DC    A(RBAPRT)
         DC    A(KEYPRT)
         SPACE 1
ADRCHNG  DS    0F
         DC    A(BLKCHNG)
         DC    A(BLKCHNG)
         DC    A(RBACHNG)
         SPACE 1
ADRPARM1 DS    0F
         DC    A(PARMOFF1)
         DC    A(PARMLEN1)
         DC    A(PARMCOU1)
         SPACE 1
ADRPARM2 DS    0F
         DC    A(PARMOFF)
         DC    A(PARMLEN)
         DC    A(PARMCOU)
         SPACE 1
ZWILEN   DS    H
OFFSET   DS    H
VALLEN   DS    H
OVALLEN  DS    H
ETRGANZ  DS    H
HDRADR   DS    F
TABADR   DS    F
CHTABADR DS    F
LENADR   DS    F
OFFADR   DS    F
CNTADR   DS    F
KEYADR   DS    F
KEYKZ    DC    CL1'0'
KEYL     DC    CL1'0'
OLDVAL   DC    C'OLDVALUE('
NEWVAL   DC    C'NEWVALUE('
RMNM     DC    CL8' '
BLDLIST  DS    0F
         DC    H'1'
         DC    H'58'
BLDMBR   DC    CL8' '
         DS    CL3
         DS    CL47
         CNOP  0,8
PACKF1   DC    PL8'0'
         DC    CL1'0'
MASKE1   DC    XL4'40212020'
MASKE2   DC    XL10'40212020202020202020'
DOUBLE   DS    D
AREA     DS    0CL80
         DC    CL1' '
         DC    C'DER KEY BEFINDET SICH IN BLK-NR :'
BLKDEZ   DS    CL10
         DC    C'   ANKERPKT : '
ANKDEZ   DS    CL4
         DC    CL73' '
PST      DC    8F'0'
KEY      DS    CL257
TAB1     DS    0CL256
         DC    256XL1'00'
         ORG   TAB1+C')'
         DC    XL1'04'
         ORG
TAB2     DS    0CL256
         DC    256XL1'04'
         ORG   TAB2+C','
         DC    XL1'0C'
         ORG   TAB2+C'0'
         DC    10XL1'00'
         ORG
TAB3     DS    0CL256
         DC    256XL1'00'
         ORG   TAB3+C','
         DC    XL1'04'
         ORG   TAB3+C')'
         DC    XL1'0C'
         ORG
TAB4     DS    0CL256
         DC    256XL1'04'
         ORG   TAB4+C'A'
         DC    6XL1'00'
         ORG   TAB4+C'0'
         DC    10XL1'00'
         ORG
TAB5     DS    0CL256
         DC    256XL1'00'
         ORG   TAB5+C','
         DC    XL1'04'
         ORG   TAB5+C'-'
         DC    XL1'08'
         ORG   TAB5+C'/'
         DC    XL1'0C'
         ORG   TAB5+C')'
         DC    XL1'10'
         ORG
TRTAB1   DC    256AL1(*-TRTAB1)
         ORG   TRTAB1+C'A'
         DC    XL6'0A0B0C0D0E0F'
         ORG   TRTAB1+C'0'
         DC    XL10'00010203040506070809'
EXCLC1   CLC   0(0,R7),0(R5)
EXTRT1   TRT   0(0,R5),TAB1
EXTRT2   TRT   0(0,R5),TAB2
EXTRT3   TRT   0(0,R5),TAB3
EXTRT4   TRT   0(0,R5),TAB4
EXTRT5   TRT   0(0,R5),TAB5
EXMVC1   MVC   P1DBNM(0),0(R5)
EXMVC2   MVC   RMNM(0),0(R5)
EXMVC3   MVC   KEY(0),0(R5)
EXMVC4   MVC   0(0,R8),KEY
EXPACK1  PACK  PACKF1,0(0,R5)
EXPACK2  PACK  PACKF1(9),0(0,R5)
EXPACK3  PACK  0(0,R1),0(0,R5)
EXTR1    TR    0(0,R5),TRTAB1
         LTORG
         EJECT
DBDAREA  ZS018PRM SEG=N,DS=NO,PREFIX=P1
         EJECT
BLSOTAB  DSECT
BLKVON   DS    F
BLKBIS   DS    F
OFFVON   DS    H
OFFBIS   DS    H
VALADR   DS    F
BTABEND  DS    0CL1
         EJECT
Y1       ZS905COM
         EJECT
         PRINT NOGEN
         IDBD
         DCBD
         EJECT
         IDLI  DMBBASE=0
ZS905ABT CSECT
BTABLG   DC    AL2(BTABEND-BLSOTAB)
MYDACS   DS    (DMBDACP+L'DMBDACP-DMBDACS)CL1
         END
