         MACRO
&NAME    MUPAUS &LABEL,&REG
         GBLB  &MTRB000            MUPTRACE ACTIVE
         GBLB  &MTRB002(2)         MUPTRACE MUPAUS
         GBLB  &MBSB003            MBASIS TYPE=R
         GBLA  &MUEA001            MUPEIN COUNT
         GBLC  &MUEC001(500)       MUPEIN &NAME
         LCLA  &D,&G,&W
         LCLC  &AL1,&N,&L
&G       SETA  &MUEA001
         AIF   ('&LABEL' NE '').L2
         AIF   (&G NE 0).L1
         MNOTE 12,'*** ''MUPAUS'' OHNE ''MUPEIN''. GENERIERUNG ABGEBROC*
               HEN ***'
         MEXIT
.L1      ANOP
&L       SETC  '&MUEC001(&G)'
         AGO   .L3
.L2      ANOP
&L       SETC  '&LABEL'
.L3      AIF   (&G EQ 0).A1
&N       SETC  '&MUEC001(&G)'
         AIF   ('&L' EQ '&N').A1
         MNOTE 4,'*** LETZTES ''MUPEIN'' WAR ''&N''. ES WIRD ''&L'' VER*
               WANDT ***'
.A1      ANOP
         AIF   ('&REG' EQ '').A2
&AL1     SETC  '&REG'
         AGO   .A3
.A2      ANOP
&AL1     SETC  'R14'
.A3      ANOP
         AIF   (&MBSB003).RE1
&W       SETA  5
&NAME    L     &AL1,&L-4           LOAD RETURN-ADDRESS
         AIF   (NOT &MTRB002(1)).TR1
.TR0     ANOP
         STM   R14,R0,84(R13)      SAVE USER REGISTER
         L     R15,72(0,R13)       LOAD TRACE-ROUTINE ADDRESS
         LA    R0,&L-&W            LOAD ID-ADDRESS
         ICM   R0,8,77(R13)        SET MACRO TYPE
         BALR  R14,R15             BRANCH TO TRACE-ROUTINE
         LM    R14,R0,84(R13)      RESTORE USER REGISTER
         AIF   (&MBSB003).RE3
.TR1     ANOP
         MVI   &L-4,255            SET RETURN-INDICATOR
         BR    &AL1                RETURN TO CALLER
         MEXIT
.RE1     ANOP
&N       SETC  '&L'
&G       SETA  &MUEA001
.G1      AIF   ('&MUEC001(&G)' EQ '&N').G2
&G       SETA  &G-1
         AIF   (&G NE 0).G1
         AGO   .E1
.G2      ANOP
&W       SETA  1
&D       SETA  72+4*(&G-1)
         AIF   (NOT &MTRB000).RE2
&D       SETA  72+24+4*(&G-1)
.RE2     ANOP
&NAME    L     &AL1,&D.(0,R13)     LOAD RETURN-ADDRESS
         AIF   (&MTRB002(1)).TR0
.RE3     ANOP
         MVI   &D.(R13),255        SET RETURN-INDICATOR
         BR    &AL1                RETURN TO CALLER
         MEXIT
.E1      MNOTE 12,'*** &LABEL NICHT VORHER DEFINIERT. GENERIERUNG ABGEB*
               ROCHEN ***'
         MEND
