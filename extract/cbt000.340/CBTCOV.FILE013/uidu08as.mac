         TITLE 'BESTIMMUNG DES WOCHENTAGES'
*
* DIESES PROGRAMM BESTIMMT DEN WOCHENTAG DES DURCH DAS PROGRAMM
* 'FMDATUM' ERMITTELTEN TAGESDATUMS.
*
WEEKEND  CSECT
         SPACE
*
* LINKAGE CONVENTION
*
         STM   14,12,12(13)       RETTEN REG.14-12 DES RUFENDEN PROGR.
         LR    14,13              RETTEN A(SAVE-AREA DES RUF.PROGR.)
         BALR  3,0                BASISREGISTER 3
         USING *,3                GELADEN
         LA    13,SAVE            LADEN A(SAVE-AREA DES GERUF.PROGR.)
         ST    14,4(13)           RETTEN A(SAVE-AREA DES RUF.PROGR.)
         ST    13,8(14)           RETTEN A(SAVE-AREA DES GERUF.PROGR.)
         B     SAVE+18*4          VERZWEIGEN ZUR NAECHSTEN INSTRUKTION
         SPACE
SAVE     DS    18F                SAVE-AREA
         SPACE
*
* AUFRUF DES PROGRAMMS 'FMDATUM' ZUR BEREITSTELLUNG DES TAGESDATUMS
*
         L     4,0(1)             LADEN LOCATOR-ADRESSE
         L     4,0(4)             LADEN PARAMETER-ADRESSE
         SPACE
         LINK  EP=FMDATUM,PARAM=ADATUM
         SPACE
         ZAP   ANZ,=P'0'
         EJECT
*
* AUF DER BASIS VON MONTAG, DEN 31.12.1973 WIRD DER WOCHENTAG ERMITTELT
*
         PACK  JAHR,DATJAHR       PACKEN JAHR AUS 'FMDATUM'
         CVB   5,JAHR             JAHR NACH REG.5
         CLC   DATMON,=C'JAN'     MONAT AUS 'FMDATUM' = 'JANUAR' ?
         BE    M1                 JA --> KEIN ZUSAETZLICHER TAG
         CLC   DATMON,=C'FEB'     MONAT AUS 'FMDATUM' = 'FEBRUAR' ?
         BE    M1                 JA --> KEIN ZUSAETZLICHER TAG
         DP    JAHR,=P'4'         PRUEFEN JAHR AUS 'FMDATUM' AUF
         CP    JAHR+7(1),=P'0'    SCHALTJAHR
         BNE   M1                 KEIN ZUSAETZLICHER TAG
         AP    ANZ,=P'1'          ZUSAETZLICHER TAG, DA SCHALTJAHR
M1       SH    5,=H'1975'         AUFBEREITEN REG.5 FUER 'TABJAHR'
         BM    M2                 REG.5 NEGATIV
         MH    5,=H'3'            BESTIMMUNG ELEMENT AUS 'TABJAHR'
         LA    6,TABJAHR+0(5)     A(ELEMENT) NACH REG.6
         AP    ANZ,0(3,6)         ANZAHL TAGE AUS 'TABJAHR'
M2       LA    5,12               SETZEN ZAEHLER FUER 'TABMON'
         LA    6,TABMON           A(TABMON) NACH REG.6
M3       CLC   DATMON,0(6)        SUCHEN MONAT AUS 'FMDATUM' IN 'TABMON
         BE    M4                 MONAT GEFUNDEN
         LA    6,5(6)             NAECHSTES ELEMENT AUS 'TABMON'
         BCT   5,M3               WEITERSUCHEN MONAT IN 'TABMON'
M4       AP    ANZ,3(2,6)         ANZAHL TAGE AUS 'TABMON'
         PACK  TAG,DATTAG         PACKEN TAG AUS 'FMDATUM'
         AP    ANZ,TAG            ADDIEREN TAG AUS 'FMDATUM'
         DP    ANZ,=P'7'          ZUR BESTIMMUNG DES WOCHENTAGES WIRD
         ZAP   DW,ANZ+7(1)        DIE ANZAHL DER TAGE ZWISCHEN DEM
         CVB   5,DW               BASISDATUM UND DEM AUS 'FMDATUM'
         MH    5,=H'2'            ERHALTENEN DATUM DURCH '7' GETEILT.
         LA    6,TABTAG+0(5)      DER DABEI GEWONNENE REST WIRD VERDOP-
         MVC   0(2,4),0(6)        PELT UND ZEIGT IN 'TABTAG' AUF DEN
*                                 WOCHENTAG.
         L     13,4(13)           LADEN A(SAVE-AREA DES RUF.PROGR.)
         LM    14,12,12(13)       WIEDERHERSTELLEN DER REGISTER-STAENDE
         BR    14                 RUECKSPRUNG
         EJECT
*
* TABELLEN ZUR BESTIMMUNG DER DIFFERENZ AN TAGEN ZWISCHEN DEM
* BASISDATUM UND DEM TAGESDATUM
*
TABMON   DC    C'JAN'
         DC    PL2'0'             KUMULIERTE TAGE: 0
         DC    C'FEB'
         DC    P'31'              KUMULIERTE TAGE: JANUAR
         DC    C'MAR'
         DC    P'59'              KUMULIERTE TAGE: JANUAR - FEBRUAR
         DC    C'APR'
         DC    P'90'              KUMULIERTE TAGE: JANUAR - MAERZ
         DC    C'MAI'
         DC    P'120'             KUMULIERTE TAGE: JANUAR - APRIL
         DC    C'JUN'
         DC    P'151'             KUMULIERTE TAGE: JANUAR - MAI
         DC    C'JUL'
         DC    P'181'             KUMULIERTE TAGE: JANUAR - JUNI
         DC    C'AUG'
         DC    P'212'             KUMULIERTE TAGE: JANUAR - JULI
         DC    C'SEP'
         DC    P'243'             KUMULIERTE TAGE: JANUAR - AUGUST
         DC    C'OKT'
         DC    P'273'             KUMULIERTE TAGE: JANUAR - SEPTEMBER
         DC    C'NOV'
         DC    P'304'             KUMULIERTE TAGE: JANUAR - OKTOBER
         DC    C'DEZ'
         DC    P'334'             KUMULIERTE TAGE: JANUAR - NOVEMBER
         EJECT
*              KUM.TAGE BEI JAHR AUS 'FMDATUM'
TABJAHR  DC    PL3'365'           1975
         DC    PL3'730'           1976
         DC    P'1096'            1977 S
         DC    P'1461'            1978
         DC    P'1826'            1979
         DC    P'2191'            1980
         DC    P'2557'            1981 S
         DC    P'2922'            1982
         DC    P'3287'            1983
         DC    P'3652'            1984
         DC    P'4018'            1985 S
         DC    P'4383'            1986
         DC    P'4748'            1987
         DC    P'5113'            1988
         DC    P'5479'            1989 S
         DC    P'5844'            1990
         DC    P'6209'            1991
         DC    P'6574'            1992
         DC    P'6940'            1993 S
         DC    P'7305'            1994
         DC    P'7670'            1995
         DC    P'8035'            1996
         DC    P'8401'            1997 S
         DC    P'8766'            1998
         DC    P'9131'            1999
         SPACE
TABTAG   DC    C'MO'              MONTAG
         DC    C'DI'              DIENSTAG
         DC    C'MI'              MITTWOCH
         DC    C'DO'              DONNERSTAG
         DC    C'FR'              FREITAG
         DC    C'SA'              SAMSTAG
         DC    C'SO'              SONNTAG
         EJECT
*
* PARAMETER FUER 'FMDATUM'
*
         DS    0F
ADATUM   DC    X'80'
         DC    AL3(DATUM)
DATUM    DS    0CL12
DATTAG   DS    CL2                TAG (NUMERISCH)
         DS    CL2
DATMON   DS    CL3                MONAT (ALPHABETISCH, BUCHSTABEN 1-3)
         DS    C
DATJAHR  DS    CL4                JAHR (NUMERISCH)
*
* SONSTIGE FELDER
*
         DS    0D
JAHR     DS    CL8                JAHR AUS 'FMDATUM' (GEPACKT)
ANZ      DC    PL8'0'             ANZAHL DER TAGE
DW       DS    CL8                REST NACH DIVISION DURCH '7'
TAG      DS    CL2                TAG AUS 'FMDATUM' (GEPACKT)
         SPACE
         LTORG
         SPACE
         END   WEEKEND
