./ ADD NAME=UICR02
)ATTR
  # TYPE(OUTPUT) INTENS(HIGH) JUST(LEFT)
)BODY
%MASTERKATALOG-MANAGEMENT-----------------------------------------------
+
+
+   IN WELCHEM VSAM-MASTER-KATALOG
+   WOLLEN SIE KATALOGISIEREN BZW.
+   ENTKATALOGISIEREN ?
+
+
+  %===>_CAT   +          #OUT
+
+  %1+  KATALOGISIEREN     IN SYS1.MCAT32B    +  ( 3033 )
+  %2+  KATALOGISIEREN     IN SYS1.MCAT32A    +  (  158 )  <--
+
+  %3+  ENTKATALOGISIEREN  IN SYS1.MCAT32B    +  ( 3033 )
+  %4+  ENTKATALOGISIEREN  IN SYS1.MCAT32A    +  (  158 )  <--
+
+ DSNAME:_DSN                                       +   (OHNE HOCHKOMMA
+ VOLUME:_VOL   +          (NUR BEI %1+ ODER %2+ ERLAUBT)
+
%----------------------------------------------------------------------
)PROC
  VER (&CAT,LIST,1,2,3,4)
  VER (&CAT,NONBLANK)
  VER (&DSN,DSNAME)
  VER (&DSN,NONBLANK)
  VER (&VOL,NAME)
)END
      <-----------  CLIST FUER OBIGES PANEL   ---------------->
PROC 0
     SET &CAT =
     SET &DSN =
     SET &VOL =
CSP1: -
     ISPEXEC CONTROL DISPLAY REFRESH
     ISPEXEC DISPLAY PANEL(CSP1)
     IF &LASTCC = 8  THEN GOTO P999               /* PF3/PF15-TASTE  */
/*   WRITE  CAT= &CAT    VOL= &VOL    DSN= &DSN
     IF &CAT    = 1  ×  -
        &CAT    = 3  THEN SET &MCAT = 32B         /* 3033            */
     IF &CAT    = 2  ×  -
        &CAT    = 4  THEN SET &MCAT = 32A         /*  158            */
     IF &CAT    < 3  THEN GOTO P100
     GOTO P200
P100: -
     IF &VOL    > A  THEN GOTO P110
     SET &OUT   = &STR(BITTE VOL SER EINGEBEN)
     GOTO CSP1
P110: -
CONTROL MSG
     DEF NVSAM (NAME('&DSN') DEVT(3350) VOL(&VOL) OWNER(&SYSUID) ) -
     CAT('SYS1.MCAT&MCAT')
     SET &OUT   = &STR(FUNKTION  &CAT  MIT CC &LASTCC BEENDET)
CONTROL NOMSG
     GOTO CSP1
P200: -
     IF &VOL    =    THEN GOTO P210
     SET &OUT   = &STR(VOL SER MUSS BLANK SEIN)
     GOTO CSP1
P210: -
CONTROL NOMSG
     DEL ('&DSN') NVSAM NSCR CAT('SYS1.MCAT&MCAT')
     SET &OUT   = &STR(FUNKTION  &CAT  MIT CC &LASTCC BEENDET)
CONTROL MSG
     GOTO CSP1
P999: -
END
