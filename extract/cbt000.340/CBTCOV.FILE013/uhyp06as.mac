MOD      TITLE 'MODSCAN POSITIONSPARAMETER   UHYP06AS  = MODSCAN'
         MACRO
&NAME    MCST  &TYP=NORMAL,&PCBIO=
         GBLB  &MCST
         GBLC  &MCSTN
         LCLA  &A1,&A2,&A3
         LCLC  &C1
         EJECT
*********************************************
*        GENERIERUNG DES STARTMACROS        *
         AIF   ('&TYP' EQ 'IMS').MACST2
         AIF   ('&TYP' EQ 'MINI').MACST3
*        FUER EIN NORMALES PROGRAMM         *
.MACST1  ANOP
*********************************************
*------- LEVEL = 27 -------*
         AIF   (&MCST).ERROR1
&MCST    SETB  1
&NAME    CSECT
&MCSTN   SETC  '&NAME'
MCST1    SAVE  (14,12),,STARTPUNKT&NAME
         SPACE
*        BASISREGISTER LADEN
         SPACE
         BALR  R9,0
         USING *,R9,R10,R11,R12
MCST2    LM    R10,R12,MCST10
         SPACE
*        SAVEAREA VERKETTUNG
         SPACE
         ST    R13,SAVEAREA+4
         LR    R14,R13
         LA    R13,SAVEAREA
         ST    R13,8(R14)
         AIF   ('&TYP' EQ 'MINI').MACST50
         LR    R3,R1
         SPACE
*        PROGRAMMNAME
         SPACE
         MACPROGN
         ST    R1,MCST112
         SPACE
*        TSOFLAG SICHERN (SVS + MVS)
         SPACE
         L     R1,16 CVT-ADDRESS
         S     R1,MCSTFW4
         CLC   0(2,R1),MCSTREL SVS OR MVS
         BE    MCSTTSO1 IF EQUAL (017), THEN SVS
         A     R1,MCSTFW4                          MVS
         L     R1,0(R1) WORD-LIST                  MVS
         L     R1,12(R1) CURRENT ASCB              MVS
         CLC   172(4,R1),MCSTFW0                   MVS
         BNZ   MCST20 IF NOT ZERO, THEN BATCH-USER MVS
         B     MCSTTSO2 TSO-USER                   MVS
MCSTTSO1 A     R1,MCSTFW4                       SVS
         L     R1,0(R1) WORD-LIST               SVS
         L     R1,4(R1) CURRENT TCB             SVS
         MVC   MCSTTSO,148(R1) TCBTSFLAG        SVS
*        UNTERBRECHUNGSADRESSE
         SPACE
         TM    MCSTTSO,X'80' TSO OR BATCH-USER
         BZ    MCST20 IF ZERO, THEN BATCH-USER
MCSTTSO2 L     R1,MCSTPMSK SET PROGRAM-
         SPM   R1          MASK FOR
         B     MCST20A     TSO-USER
MCST20   SPIE  MCSTERR,((1,15))
         ST    R1,MCSTPICA
         AIF   (&A1 NE 1).MACST9
         SPACE
*        PCB ADRESSEN
         SPACE
MCST20A  LA    R2,MCSTPCB1-72
MCST21   LA    R2,72(R2)
         L     R0,0(R3)
         ST    R0,4(R2)
         LA    R3,4(R3)
         LTR   R0,R0
         BNM   MCST21
         MVI   4(R2),0
         MVC   MCSTPCB0+4(4),4(R2)
         SR    R3,R3
.MACST9  ANOP
         SPACE
*        REPLACUNG
         SPACE
         AIF   (&A1 NE 1).MACST9A
         ST    R3,MCST116
         AGO   .MACST9B
.MACST9A ANOP
MCST20A  ST    R3,MCST116
.MACST9B ANOP
         LINK  EP=MODREP,PARAM=(MCST100),VL=1
         DELETE EP=MODREP
         SPACE
*        TESTHILFSPROGRAMME
         SPACE
         TM    MCST108,X'80'
         BZ    MCST99
         LOAD  EP=MODPRT
         ST    R0,MCSTPRT
         LOAD  EP=MODSEK
         ST    R0,MCSTSEK
         LOAD  EP=MODTRC
         ST    R0,MCSTTRC
MCST9    TM    MCST205,X'01'
         BZ    MCST99
         BAL   R2,MODTRC1
         B     MCST99
         SPACE
*        DEFINITIONEN
         SPACE
         DS    0F
MCSTRC   DC    XL3'0'                       RETURNCODE-FIELD
MCSTRCF  DC    X'0'
MCSTPAKA DC    CL52' '
MCST10   DC    A(MCST2+4096,MCST2+8192,MCST2+12288)
MCSTDCB  DCB   DSORG=PS,BLKSIZE=141,MACRF=PM,RECFM=VBM,                X
               LRECL=137,BUFNO=2,EROPT=ACC,DDNAME=DRUCKER
         SPACE
*        ERROR-ROUTINE
         SPACE
MCSTERR  BALR  R2,0
         USING *,R2
         ST    R1,MCSTERR1
         ST    R13,MCSTERR3+4
         LR    R14,R13
         LA    R13,MCSTERR3
         ST    R13,8(R14)
         LINK  EP=MODERR,PARAM=(MCSTERR1)
         CLOSE (MCSTDCB)
         CLI   MCST116,0
         BE    MCSTERR0
         ABEND 4000,DUMP
MCSTERR0 ABEND 4000
MCSTERR1 DC    F'0'
MCSTERR2 DC    A(MCSTNAME)
         DC    A(MCST116)
         DC    A(MCST1)
MCSTERR3 DC    18F'0'
         DROP  R2
         SPACE
*        EOJ ROUTINE
         SPACE
MCSTEOJ  MVI   MCST144,X'00'
         AIF   (&A1 NE 1).MACST40
         TM    MCST108,X'80'
         BZ    MCST41
         LA    R1,MCSTPCB0
         BAL   R2,MICALLUP
         L     R4,MCSTIMS1
         L     R4,276(R4)
         MCTF  31,(R4),108
.MACST40 ANOP
MCST41   TM    MCSTDCB+48,16
         BZ    MCST33
         CLOSE (MCSTDCB)
MCST33   TM    MCSTTSO,X'80'
         BO    MCST34
         L     R3,MCSTPICA
         SPIE  MF=(E,(3))
MCST34   L     R13,4(R13)
         L     R15,MCSTRC                   LOAD RETURNCODE
         RETURN (14,12),,RC=(15)
         SPACE
*        ANSPRUNG MODPRT
         SPACE
MODPRT1  CLI   MCST204,X'00'
         BE    6(R2)
         STM   R14,R2,MCST30
         MVI   MCST144,X'00'
         L     R2,MCST30+16
         LA    R1,MCSTDCB
         L     R15,MCSTPRT
         BALR  R14,R15
         SPACE
*        RUECKSPRUNG MODPRT
         SPACE
         LM    R14,R2,MCST30
         BAL   R2,MODTRC1
         LM    R14,R2,MCST30
         B     6(R2)
         SPACE
*        ANSPRUNG MODSEK
         SPACE
MODSEK1  TM    MCST108,X'80'
         BZ    2(R2)
         STM   R14,R2,MCST30
         MVI   MCST144,X'00'
         L     R2,MCST30+16
         LA    R0,MCST200
         L     R15,MCSTSEK
         BALR  R14,R15
         SPACE
*        RUECKSPRUNG MODSEK
         SPACE
         LTR   R15,R15
         BNZ   MCSTEOJ
         BAL   R2,MODTRC1
         LM    R14,R2,MCST30
         B     2(R2)
         SPACE
*        ANSPRUNG MODTRC
         SPACE
MODTRC1  CLI   MCST205,X'00'
         BE    0(R2)
         STM   R13,R1,MCST124
         MVC   MCST144(1),MCST205
         L     R15,MCSTTRC
         LR    R0,R2
         LA    R1,MCST124
         BALR  R14,R15
         SPACE
*        RUECKSPRUNG MODTRC
         SPACE
         LM    R13,R1,MCST124
         BR    R2
         SPACE
*        PARAMETER FUER TESTMODULE
         SPACE
         DS    0F
MCST100  DC    FL1'9'
MCST101  DC    XL3'0'
MCST104  DC    FL1'4'
MCST105  DC    AL3(MCST1)
MCST108  DC    X'0'      TESTBYTE
MCST109  DC    AL3(MCST200)
MCST112  DC    F'0' MCSTNAME
MCST116  DC    F'0' REG 3 (A(PARM) ODER 0)
MCST120  DC    A(MCSTPAKA) PARMKARTENBEREICH
MCST124  DC    A(SAVEAREA)
MCST128  DC    F'0'
MCST132  DC    F'0'
MCST136  DC    F'0'
MCST140  DC    F'0'
MCST144  DC    A(MCSTDCB)
         SPACE
MCST30   DC    5F'0'
MCST31   DC    X'0'
MCST32   DC    X'0'
         SPACE
*        TESTBEREICH
         SPACE
         DS    0F
MCST200  DC    F'0'      TESTWORT
MCST204  DC    X'0'
MCST205  DC    X'0'
MCST206  DC    X'0'
         DC    X'0'
         DC    XL14'0'
         DC    12X'FF'
MCST234  DC    X'0'
         DC    X'0'
         DC    XL14'0'
         DC    12X'FF'
MCST262  DC    X'0'
         DC    X'0'
         DC    XL14'0'
         DC    12X'FF'
         SPACE
*        ALLGEMEINE DEFINITIONEN
         SPACE
SAVEAREA DC    18F'0'
MCSTNAME EQU   MCST112
MCSTPICA DC    F'0'
MCSTPMSK DC    X'0F0000'
MCSTTSO  DC    X'0'
MCSTPRT  DC    F'0'
MCSTSEK  DC    F'0'
MCSTTRC  DC    F'0'
MCSTPARM DC    A(MCSTPAKA+4)
MCSTFW0  DC    F'0'
MCSTFW4  DC    F'4'
MCSTREL  DC    C'01' RELEASE 01.7
         AIF   (&A1 NE 1).MACST7
         SPACE
*        IMS - BEREICHE
         SPACE
MCSTPCB0 DC    A(MCSTGSCD)
         DC    A(0)
         DC    X'80'
         DC    AL3(MCSTIMS1)
&A2      SETA  N'&PCBIO
.MACST10 ANOP
&A3      SETA  &A3+1
&C1      SETC  'MCSTPCB&A3'
&C1      DC    A(MCSTIMSF)
         DC    A(0)
         DC    A(&PCBIO(&A3))
         DC    15F'0'
&A2      SETA  &A2-1
         AIF   (&A2 NE 0).MACST10
         DS    0F
MCSTIMS1 DC    XL8'0'
MCSTIMSF DC    CL4' '
MCSTGU   DC    CL4'GU'
MCSTGN   DC    CL4'GN'
MCSTGNP  DC    CL4'GNP'
MCSTGHU  DC    CL4'GHU'
MCSTGHN  DC    CL4'GHN'
MCSTGHNP DC    CL4'GHNP'
MCSTISRT DC    CL4'ISRT'
MCSTDLET DC    CL4'DLET'
MCSTREPL DC    CL4'REPL'
MCSTGSCD DC    CL4'GSCD'
         DC    A(MCSTERR)
         SPACE 3
         MIPARAM
         EJECT
&A2      SETA  N'&PCBIO
.MACST8  ANOP
         MIPCB
&A2      SETA  &A2-1
         AIF   (&A2 NE 0).MACST8
&NAME    CSECT
.MACST7  ANOP
         SPACE
*        EQUATE DEFINITIONEN
         SPACE
         MCEQU
         SPACE
         MCDAT
         SPACE
*        REPLACEBEREICH
         SPACE
MCSTREP  DS    CL200
         SPACE
MCST99   EQU   *
         EJECT
         MEXIT
.MACST2  ANOP
         AIF   ('&PCBIO' EQ '').ERROR2
&A1      SETA  1
         AIF   (N'&PCBIO GT 9).ERROR3
*        FUER EIN IMS PROGRAMM              *
         AGO   .MACST1
.MACST3  ANOP
*        KURZVERSION                        *
         AGO   .MACST1
.MACST50 ANOP
         B     MCST99
         SPACE
*        EOJ - ROUTINE
         SPACE
MCSTEOJ  L     R13,4(R13)
         L     R15,MCSTRC                   LOAD RETURNCODE
         RETURN (14,12),,RC=(15)
MCSTEOJ1 L     R13,4(R13)
         L     R15,MCSTRC                   LOAD RETURNCODE
         L     R14,12(R13)
         L     R0,20(R13)
         LM    R2,R12,28(R13)
         BR    R14                      RETURN
         SPACE
*        ALLGEMEINE DEFINITIONEN
         SPACE
         DS    0F
MCSTRC   DC    XL3'0'                       RETURNCODE-FIELD
MCSTRCF  DC    X'0'
MCST10   DC    A(MCST2+4096,MCST2+8192,MCST2+12288)
SAVEAREA DC    18F'0'
         MCEQU
MCST99   EQU   *
         EJECT
         MEXIT
.ERROR1  MACMES 1,MCST
         MEXIT
.ERROR2  MACMES 2,MCST
         MEXIT
.ERROR3  MACMES 3,MCST
         MEND
         MACRO
&NAME    MACPROGN
&NAME    L     R1,16 CVT-ADDRESS
         L     R1,0(R1) TCB-LISTE
         L     R1,4(R1) TCB-ADDRESS
         L     R1,0(R1) RB-ADDRESS
         L     R1,12(R1) CDE-ADDRESS
         LA    R1,8(R1) PROGRAMNAME
         MEND
         MACRO
&NAME    MIPARAM
*********************************************
*        PARAMETERLISTE FUER IMS-CALL       *
*********************************************
         SPACE
         AIF   ('&NAME' EQ '').MAC1
&NAME    EQU   *
.MAC1    ANOP
MICALLUP TM    8(R1),X'80'
         BO    MICALLU2
         LA    R14,12(R1)
MICALLU1 L     R15,0(R14)
         CLI   13(R15),C'('
         BNE   MICALLU4
         LA    R0,4
         SLR   R15,R0
         LR    R0,R14
         BALR  R14,R15
         LR    R14,R0
         L     R15,0(R14)
MICALLU4 LTR   R15,R15
         BM    MICALLU2
         LA    R14,4(R14)
         B     MICALLU1
MICALLU2 L     R15,MCSTIMS
         ST    R1,MCSTSAVE
         BALR  R14,R15
         L     R1,MCSTSAVE
         LA    R0,7
         L     R14,4(R1)
         LA    R15,MCSTATUS
MICALLU3 CLC   10(2,R14),0(R15)
         BE    MICALLOK
         LA    R15,2(R15)
         BCT   R0,MICALLU3
         L     R4,MCST112
         MVC   MCST13+8(8),0(R4)
         MVC   MCST13+22(L'MCSTSTME),MCSTSTME
         MVC   MCST13+38(2),10(R14)
         WTO   MF=(E,MCST13)
         MCABEND 3600,DUMP
MICALLOK BR    R2
MCST13   WTO   '*** PROGNAME 3600                   ',MF=L
MCSTATUS DC    C'  GAGBGEGKIILB'
MCSTIMS  DC    V(ASMTDLI)
MCSTSAVE DC    F'0'
MCSTSTME DC    C'IMS-STATUSCODE: XX'
         MEND
         MACRO
         MCEQU
         GBLB  &MCEQU
         AIF   (&MCEQU).NOTE1
&MCEQU   SETB  1
*------- LEVEL = 03 -------$
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEXIT
.NOTE1   MACMES 1,MCEQU
         MEND
         MACRO
&NAME    MCDAT &BER
         GBLB  &MCDAT
         AIF   (&MCDAT).MCDAT4
&MCDAT   SETB  1
*        DATUMSPRUEFUNG
* DAS DATUM IST UNGEPACKT IN DER FORM TT.MM.JJ (8 BYTES)
*------- LEVEL = 05 -------*
UPDAT    EQU   *
         AIF   ('&BER' EQ '').MCDAT2
         AIF   ('&BER'(1,1) EQ '(').MCDAT3
&NAME    LA    1,&BER
.MCDAT1  ANOP
UPDAT0   ZAP   UPDATV2+2(2),UPDATK28
UPDAT1   STM   0,2,UPDATVF1
         MVC   UPDATVD1(2),0(1)
         MVC   UPDATVD1+2(2),3(1)
         MVC   UPDATVD1+4(2),6(1)
         LA    2,UPDATVD1
         LA    0,6
UPDAT2   CLI   0(2),C'0'
         BL    UPDAT4
         CLI   0(2),C'9'
         BH    UPDAT4
         LA    2,1(2)
         BCT   0,UPDAT2
         PACK  UPDATV1,0(2,1)
         CLI   UPDATV2+2,X'03'
         BE    UPDAT3
         PACK  UPDATVD1(3),6(2,1)
         DP    UPDATVD1(3),UPDATK4
         TM    UPDATVD1+2,X'F0'
         BNZ   UPDAT3
         ZAP   UPDATV2+2(2),UPDATK29
UPDAT3   PACK  UPDATVD1,3(2,1)
         CP    UPDATVD1,UPDATK12
         BH    UPDAT4
         CVB   2,UPDATVD1
         SLA   2,1
         BZ    UPDAT4
         LA    2,UPDATV2-2(2)
         CP    UPDATV1,0(2,2)
         BH    UPDAT4
         BE    UPDAT5
UPDAT6   CP    UPDATV1,UPDATV2+7(1)
         BE    UPDAT4
         LM    0,2,UPDATVF1
         B     4(2)
UPDAT5   CLI   UPDATV2+2,X'03'
         BNE   UPDAT6
         MVI   1(1),X'F0'
         B     UPDAT6
UPDAT4   LM    0,2,UPDATVF1
         BR    2
UPDAT30  ZAP   UPDATV2+2(2),UPDATK30
         B     UPDAT1
UPDATK4  DC    P'4'
UPDATK12 DC    P'12'
UPDATK28 DC    P'28'
UPDATK29 DC    P'29'
UPDATK30 DC    P'30'
UPDATV1  DC    PL2'0'
UPDATV2  DC    P'31,28,31,30,31,30,31,31,30,31,30,31'
UPDATVF1 DC    3F'0'
UPDATVD1 DS    D
         MEXIT
.MCDAT2  AIF   ('&NAME' EQ '').MCDAT1
&NAME    EQU   *
         AGO   .MCDAT1
.MCDAT3  ANOP
&NAME    LR    1,&BER(1)
         AGO   .MCDAT1
.MCDAT4  MACMES 1,MCDAT
         MEND
         MACRO
         MACMES &CODE,&MAKRO
.***********************************************************
.*                                                         *
.*       MAKRO ZUR GENERIERUNG VON MNOTE-BEMERKUNGEN       *
.*             STANDARDTEXT IST   *** &MAKRO               *
.*                                                         *
.* CODE  TEXT                                              *
.* ****  ****                                              *
.*                                                         *
.*   1   MAKRO BEREITS EINGESETZT                          *
.*   2   PARAMETER FEHLT                                   *
.*   3   FEHLER IN PARAMETERANGABE                         *
.*                                                         *
.***********************************************************
         AIF   ('&CODE' EQ '' OR '&MAKRO' EQ '').NOTE1
         AIF   ('&CODE' EQ '0' OR '&CODE' GT '3').NOTE2
         AIF   ('&CODE' EQ '1').M1
         AIF   ('&CODE' EQ '2').M2
         AIF   ('&CODE' EQ '3').M3
.M1      MNOTE '*** &MAKRO MAKRO BEREITS EINGESETZT'
         MEXIT
.M2      MNOTE '*** &MAKRO PARAMETER FEHLT'
         MEXIT
.M3      MNOTE '*** &MAKRO FEHLER IN PARAMETERANGABE'
         MEXIT
.NOTE1   MACMES 2,MACMES
         MEXIT
.NOTE2   MACMES 3,MACMES
         MEND
         MACRO
&NAME    MCABEND &CODE,&DUMP
*-------LEVEL = 01-------*
         GBLA  &MACTMR
         GBLB &MCST
         LCLC &C
&C       SETC '&SYSNDX'
         AIF   (NOT &MCST).ERROR0
         MACTMR &CODE
         AIF   (&MACTMR EQ 3).ERROR2
         AIF   ('&DUMP' EQ '').MAC1
         AIF   ('&DUMP' NE 'DUMP').ERROR3
&NAME    STM   14,2,MC&C+12
         AIF   (&MACTMR EQ 0).MAC2
         CVD   &CODE(1),MC&C+4
         MVC   MC&C+4(3),MC&C+9
.MAC2    BALR 14,0
         ST    14,MC&C+8
         LA    1,MC&C
         B     MCSTERR
         CNOP 4,8
MC&C     DC    F'0'
         AIF   (&MACTMR EQ 0).MAC3
         DC    PL3'0'
.MAC4    DC    X'0'
         DC    F'0'
         DC    5F'0'
         MEXIT
.ERROR0 MNOTE 'KEIN MCST'
         MEXIT
.ERROR2 MACMES 2,MCABEND
         MEXIT
.ERROR3 MACMES 3,MCABEND
         MEXIT
.MAC1    ANOP
&NAME    ABEND &CODE
         MEXIT
.MAC3    DC    PL3'&CODE'
         AGO   .MAC4
         MEND
         MACRO
         MACTMR &A
         GBLA  &MACTMR
         LCLA  &B
         LCLC  &C
.********************************************************
.*                                                      *
.*       TEST DES OPERANDEN AUF REGISTER 2 BIS 12       *
.*       ANZEIGECODE IST DAS GLOBALE SETA-SYMBOL        *
.*                                                      *
.*       MACTMR = 0  WENN KEIN REGISTER (CHAR.AUSDRUCK) *
.*              = 1  WENN NICHT REG 2 - REG 12          *
.*              = 2  WENN REG 2 - REG 12                *
.*              = 3  WENN OPERAND NICHT VORHANDEN       *
.*                                                      *
.********************************************************
&MACTMR  SETA  0
         AIF   ('&A' NE '').MACTMR0
&MACTMR  SETA  3
         MEXIT
.MACTMR0 AIF   ('&A'(1,1) NE '(').MACTMR3
&MACTMR  SETA  1
         AIF   ('&A'(2,1) EQ 'R').MACTMR2
&C       SETC '&A'(2,K'&A-2)
.MACTMR1 ANOP
&B       SETA  &C
         AIF   (&B LT 2).MACTMR3
         AIF   (&B GT 12).MACTMR3
&MACTMR  SETA  2
         MEXIT
.MACTMR2 ANOP
&C       SETC '&A'(3,K'&A-3)
         AGO   .MACTMR1
.MACTMR3 MEND
         EJECT
MODSCAN  MCST  TYP=MINI
         SPACE 5
***********************************************************************
*                                                                     *
*        REGISTERVERWENDUNG                                           *
*        R0    LAENGE PARAMETERLISTE                                  *
*        R1    ADRESSE PARAMETERLISTE                                 *
*        R2    UP SPRUNG                                              *
*        R3    FELDANFANG                                             *
*        R4    FELDENDE                                               *
*        R5    FELDLAENGE                                             *
*        R6    ADRESSE IM SCAN INTERFACE                              *
*        R7    ENDE DER PARAMETERLISTE                                *
*        R8    RESTLAENGE                                             *
*        R9    ***                                                    *
*        R10   * BASIS-                                               *
*        R11   * -REGS                                                *
*        R12   ***                                                    *
*        R13   SAVEAREA                                               *
*        R14                                                          *
*        R15                                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         SPACE 5
***********************************************************************
*                                                                     *
* A      STEUERUNG                                                    *
*                                                                     *
***********************************************************************
         SPACE 3
A1       B     B1
A2       CLI   MCSTRCF,0
         BNE   A6
         B     C1
A3       LTR   R8,R8
         BZ    A5
         B     F1
A4       LH    R0,GSB10
         BCTR  R0,0
         STH   R0,GSB10
         CLI   0(R4),C' '
         BE    A7
         LTR   R0,R0
         BNZ   A3
         MVI   1(R6),X'0'
A5       L     R1,GSB12
         CLI   GSB8,0
         BE    A6
         MVI   MCSTRCF,4
A6       B     MCSTEOJ1
A7       MVI   0(R6),X'FF'
         B     A5
         SPACE 5
***********************************************************************
*                                                                     *
* B      UEBERGABEWERTE                                               *
*                                                                     *
***********************************************************************
         SPACE 3
B1       MVC   GSB0(8),0(R1)
         L     R3,GSB4
         MVC   GSB10,KH16
         LH    R2,GSB0
         LTR   R2,R2
         BNP   B5
         STH   R2,GSB10
B5       LH    R2,GSB10
         LA    R2,1(R2)
         SLL   R2,3
         L     R1,GSB12
         LTR   R1,R1
         BZ    B2
         C     R2,GSB16
         BNH   B3
         L     R0,GSB16
         FREEMAIN R,LV=(0),A=(1)
B2       LR    R0,R2
         XC    GSB12(8),GSB12
         GETMAIN EC,LV=(0),A=GSB12
         LTR   R15,R15
         BNZ   B4
         ST    R2,GSB16
B3       B     A2
B4       MVI   MCSTRCF,8
         B     B3
         SPACE 5
***********************************************************************
*                                                                     *
* C      GRUNDSTELLUNG                                                *
*                                                                     *
***********************************************************************
         SPACE 3
C1       XC    GSB8(2),GSB8
         LH    R0,GSB10
         L     R6,GSB12
C2       XC    0(8,R6),0(R6)
         LA    R6,8(R6)
         BCT   R0,C2
         MVC   0(2,R6),KFF
         LH    R8,GSB2
         LR    R7,R3
         AR    R7,R8
         L     R6,GSB12
         B     A3
         SPACE 5
***********************************************************************
*                                                                     *
* F      FELDBEARBEITUNG                                              *
*                                                                     *
***********************************************************************
         SPACE 3
F1       MVI   GSB9,0
         B     FA1
F2       B     FB1
F3       CR    R4,R7
         BE    F8
         CLI   GSB9,1
         BE    F4
         CLI   0(R4),C','
         BE    F9
         CLI   0(R4),C' '
         BNE   F5
F9       LA    R3,1(R4)
F6       LR    R8,R7
         SR    R8,R3
F7       B     A4
F4       LA    R4,1(R4)
         MVI   GSB9,0
         B     F3
F5       SH    R6,KH8
         MVI   0(R6),X'FE'
         MVI   1(R6),4
         LA    R6,8(R6)
         MVI   GSB8,X'FF'
         LR    R3,R4
         B     F6
F8       SR    R8,R8
         B     F7
         SPACE 5
***********************************************************************
*                                                                     *
* FA     FELDENDE SUCHEN                                              *
*                                                                     *
***********************************************************************
         SPACE 3
FA1      CLI   0(R3),C'('
         BE    FA4
         MVI   TRTTAB+X'5D',0
         MVI   TRTTAB+X'6B',C','
FA2      LR    R4,R7
         ST    R8,FAVF1
         CH    R8,KH256
         BNH   FA5
         LA    R8,256
FA5      EQU   *
         BCTR  R8,0
         EX    R8,KTRT
         BE    FA3
         LR    R4,R1
FA3      L     R8,FAVF1
         B     F2
FA4      MVI   TRTTAB+X'5D',C')'
         MVI   TRTTAB+X'6B',0
         MVI   GSB9,1
         B     FA2
FAVF1    DC    F'0'
         SPACE 5
***********************************************************************
*                                                                     *
* FB     SIF EINTRAG                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
FB1      LR    R5,R4
         SR    R5,R3
         CLI   0(R3),C'('
         BNE   FB2
         LA    R3,1(R3)
         BCTR  R5,0
         CR    R4,R7
         BE    FB3
         MVI   0(R6),X'AA'
FB2      STH   R5,2(R6)
         ST    R3,4(R6)
         LA    R6,8(R6)
         B     F3
FB3      MVI   0(R6),X'FE'
         MVI   1(R6),1
         MVI   GSB8,X'FF'
         B     FB2
         SPACE 5
***********************************************************************
         SPACE 3
KH8      DC    H'8'
KH16     DC    H'16'
KH256    DC    H'256'
KFF      DC    X'FFFF'
KTRT     TRT   0(1,R3),TRTTAB
KMVC     MVC   0(1,R6),0(R3)
TRTTAB   DC    XL64'0'
         DC    C' '
         DC    XL191'0'
         SPACE 5
***********************************************************************
*                                                                     *
* GESAMTSTEUERBEREICH                                                 *
*                                                                     *
***********************************************************************
         SPACE 3
         DS    0F
GSB0     DC    H'0'
GSB2     DC    H'0'
GSB4     DC    F'0'
GSB8     DC    X'0'
GSB9     DC    X'0'
GSB10    DC    H'16'
GSB12    DC    F'0'
GSB16    DC    F'0'
         END
