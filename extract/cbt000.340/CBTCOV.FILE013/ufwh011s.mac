         TITLE 'IMSDUPUN    DATENBANK AUSZUG - UNLOAD'
***********************************************************************
***                                                                 ***
***      DIESES PROGRAMM ERSTELLT AUSZUEGE AUS DATENBANKEN          ***
***      NACH VORGEGEBENEN SUCHBEGRIFFEN. DER BENUTZER HAT DIE      ***
***      MOEGLICHKEIT SICH NACH JEDEM CALL UEBER EINE BENUTZER-     ***
***      ROUTINE STEUERUNG GEBEN ZU LASSEN. DIE EINZELNEN SEGMENTE  ***
***      WERDEN AUF BAND AUSGEGEBEN UND DANN MIT HILFE EINES        ***
***      ANDEREN PROGRAMMS ZU EINER NEUEN DATENBANK AUFGEBAUT.      ***
***                                                                 ***
***********************************************************************
***                                                                 ***
***      AUTHOR:           WOLFGANG SCHEH                           ***
***                        HOECHST AG                               ***
***                        ADV / K707                               ***
***                        6230 FRANKFURT/M 80                      ***
***                        TEL. 0611/305-5604                       ***
***                                                                 ***
***********************************************************************
***                                                                 ***
***      GETESTET MIT IMSVS 1.1.4                                   ***
***                                                                 ***
***********************************************************************
         SPACE 2
DLITCBL  CSECT
         SAVE  (14,12)            REGISTER RETTEN
         LR    3,15               BASIS-REGISTER ZUORDNEN
         USING DLITCBL,3,4
         L     4,ADR1             ADRESSE IN BASISREG. 4
         LA    12,SAVEAREA
         ST    13,4(12)           SAVEAREAS UEBERGEBEN
         ST    12,8(13)
         LR    13,12
         L     5,16               ADR. CVT
         L     5,0(5)             ADR. NEW/OLD
         L     5,4(5)             ADR. CURRENT TCB
         L     5,TCBFSA(5)        ADR. 1ST. SAVEAREA
         L     5,24(5)            ADR. DFSLI000
         USING PXPARMS,5
         L     5,PCPST            ADR. PST
         DROP  5
         ST    5,ADRPST           PST. ADR. SPEICHERN
         OPEN  (KART,(INPUT),LIST,(OUTPUT),BAND,(OUTPUT))
         LA    5,EBER
         MVC   DBER+10(28),=C'AUFLISTUNG DER STEUERKARTEN:'
         MVI   DBER,C'1'          VORSCHUB BLATTANFANG
         BAL   12,DRUCK           UEBERSCHRIFT DRUCKEN
         MVI   DBER+10,C'*'
         MVC   DBER+11(27),DBER+10
         BAL   12,DRUCK
         MVI   DBER,C'-'
A0       BAL   12,KARTLES         1. KARTE LESEN
         BAL   12,KOMPRIM         BLANKS RAUSFILTERN
         CLC   EBER(3),=C'DBD'    DBD-KARTE?
         BNE   A4
         MVC   EDBD,EBER+4        DBD-NAME SPEICHERN
         B     A0
A4       CLC   EBER(3),=C'MOD'    MOD-KARTE VORHANDEN?
         BNE   A5
         MVC   MODNAME,EBER+4     MODUL-NAME SPEICHERN
         LA    5,MODNAME          ADR. EPOINT-NAME LADEN
         LOAD  EPLOC=(5)          MODUL LADEN
         ST    0,EPUNKT           ENTRY-POINT SPEICHERN
         NI    CALLWEI+1,X'0F'
         B     A0
A5       BAL   12,PCBSUCH         PCB SUCHEN
         BAL   12,MAXSEGL         GROESSTE SEGL. FESTSTELLEN
         BAL   12,ANALYSE         STEUERKARTEN ANALYSIEREN
         L     5,ADRPCB           PCB-ADR LADEN
         L     11,MAINADR         ADR. GETMAIN-BEREICH LADEN
         LA    6,14               SATZLAENGE LADEN UND
         SLL   6,16               IN DIE VORDEREN 2 BYTES BRINGEN
         ST    6,0(11)            LAENGE IN MAINBEREICH STELLEN
         MVC   4(8,11),0(5)       DBD-NAME AUS PCB UEBERTRAGEN
         MVC   12(2,11),SEGL      MAX. SEGL. IN VORSPANN STELLEN
         PUT   BAND,(11)          BAND SCHREIBEN
GETK1    L     5,ADRJCB           ADR. JCB LADEN
         USING JCB,5
         L     5,JCBSDB1          ADR. 1. SDB LADEN
         USING SDB,5
         MVC   SSA1,SDBSYM        SEGMENT-NAME IN SSA STELLEN
         MVC   KEYLEN,SDBKEYLN    SCHLUESSEL-LAENGE-1 SPEICHERN
         DROP  5
         CLI   TB2,X'00'
         BNE   KEYVER             KEY-VERARBEITUNG
         BAL   12,KARTLES         KARTE LESEN
         BAL   12,KOMPRIM         BLANKS KOMPRIMIEREN
         LA    5,EBER
         BAL   12,SNRPRUEF        SATZNR. PRUEFEN UND NULLEN UNTERDR.
         LA    5,EBER             ADR. EBER LADEN
         BAL   12,NULLDR          NULLEN UNTERDRUECKEN
         MVC   EBER(80),ZWSP      ZWSP IN EBER STELLEN
         MVC   ZWSP,ZWSP-1        ZWSP LOESCHEN
         BAL   12,NUMPRF          ZAEHLER AUFBEREITEN
***      VERARBEITUNG DER SATZNUMMERN
         MVI   SSAKL,C' '         LINKE KLAMMER IM SSA-LOESCHEN
A32      BAL   12,GUCALL          GU/GN CALL AUFSETZEN
         L     5,ADRPCB           PCB-ADR. LADEN
         CLC   10(2,5),=C'  '     CALL OK.
         BE    A32A
         CLC   10(2,5),=C'GE'     DBENDE
         BE    DBENDE
         B     F9                 FEHLER
A32A     AP    RSEGZAE,=P'1'      ZAEHLER F. ROOTSEGM + 1
WEI10    NOP   A31
         LA    5,GN               GU IN GN AENDERN
         ST    5,PL1
         OI    WEI10+1,X'F0'      WEI10 AUF BRANCH SETZEN
         L     5,ADRPCB
A31      CP    RSEGZAE,ZAEHL1     VGL ROOTSEGMZAEHLER GEGEN ZAEHL1
         BL    A32
         BH    A33
A34      BAL   12,SATZAUFB        SATZ AUFBEREITEN
         BAL   12,GNPCALL         GNP-CALL AUFSETZEN
         L     5,ADRPCB           PCB-ADR. LADEN
         CLC   10(2,5),=C'  '     CALL OK.
         BE    A34
         CLC   10(2,5),=C'GE'     NICHT GEFUNDEN
         BE    A35
         CLC   10(2,5),=C'GK'     OK.
         BE    A34
         CLC   10(2,5),=C'GA'     OK.
         BE    A34
         CLC   10(2,5),=C'GP'     WENN NUR ROOTSEGM. SENSITIV
         BE    A35
         B     F9
A33      CP    RSEGZAE,ZAEHL2     ROOTSEGMZAEHLER GEGEN 2.ZAEHLER VGL.
         BNH   A34
         B     GETK
A35      CLI   TB1,X'D0'          INKREMENT VORHANDEN
         BNE   A32
         AP    ZAEHL1,ZAEHL3      INKREMENT ADDIEREN
         B     A32
         SPACE 2
***      VERARBEITUNG NACH ANGEGEBENEN SCHLUESSELN
***
KEYVER   MVI   SSAKL,C'('         LINKE SSA-KLAMMER SETZEN
         USING SDB,5
         TM    SDBORGN,SDBORGHD   ABFRAGE OB HDAM ORGANISATION
         BZ    A42
         OI    HDWEI+1,X'F0'      HDAM-WEICHE AUF BRANCH
A42      L     5,SDBPSDB          ADR. PSDB LADEN
         USING DMBPSDB,5
         L     5,DMBFDBA          ADR. FDB LADEN
         USING FDB,5
         MVC   SSAKEY,FDBSYMBL    FELDNAME IN SSA STELLEN
         DROP  5
         LA    5,EBER             ADR. EBER LADEN
         BAL   11,KEYAUFB         SCHLUESSEL-AUFBEREITUNG
         CLI   TB2,X'80'          NUR EIN SCHLUESSEL ANGEFORDERT?
         BNE   A63
         MVI   SSAOP,C' '         SSA.OPERAND < LOESCHEN
A63      IC    6,KEYLEN           SCHLUESSEL-LAENGE INSERT
         EX    6,CLC4             GEGEN LETZTEN SCHLUESSEL VGL.
         BH    F18
HDWEI    NOP   HDAMVER            HDAM-VERARBEITUNG
A90      BAL   12,GUCALL          GU-CALL
         L     5,ADRPCB
         CLC   10(2,5),=C'  '     STC ABFRAGEN
         BE    A79
         CLC   10(2,5),=C'GE'
         BE    F17
         B     F9
A79      BAL   12,SATZAUFB        SATZ AUFBAUEN
         BAL   12,GNPCALL         GNP-CALL
         L     5,ADRPCB
         CLC   10(2,5),=C'  '     STC ABFRAGEN
         BE    A79
         CLC   10(2,5),=C'GA'
         BE    A79
         CLC   10(2,5),=C'GK'
         BE    A79
         CLC   10(2,5),=C'GP'     WENN NUR ROOTSEGM. SENSITIV
         BE    A7910
         CLC   10(2,5),=C'GE'
         BNE   F9
A7910    AP    ANZZAE,=P'1'       ANZAHL ERHOEHEN
         CLI   HDWEI,X'F0'        HDAM-VERARBEITUNG
         BNE   A791
         MVI   SSAKL,C' '         LINKE KLAMMER LOESCHEN
A791     TM    TB2,X'40'          2. KEY VORHANDEN
         BO    A81
         TM    TB2,X'20'          ANZAHL VORHANDEN
         BNO   GETK
A81      LA    5,GN               ADR. GET NEXT INSTRUKTION LADEN
         ST    5,PL1              FUNKTION IN PARM-LISTE SPEICHERN
A84      BAL   12,GUCALL          GET NEXT-CALL AUFSETZEN
         L     5,ADRPCB
         CLC   10(2,5),=C'  '     STC ABFRAGEN
         BE    A85
         CLC   10(2,5),=C'GB'     DB-ENDE?
         BE    DBENDE
A85      TM    TB2,X'10'          INKREMENT VORHANDEN
         BO    A86
         TM    TB2,X'20'          ANZAHL VORHANDEN?
         BNO   A87
         AP    ANZZAE,=P'1'       ZAEHLER+1
         CP    ANZZAE,ZAEHL4      ANZAHL ERREICHT?
         BH    GETK
         B     A88
A87      TM    TB2,X'40'          2.KEY VORHANDEN
         BNO   GETK
         SR    6,6                REG. LOESCHEN
         IC    6,KEYLEN           KEY-LAENGE IN REG.
         EX    6,CLC2             2. KEY ERREICHT?
         BH    GETK
A88      BAL   12,SATZAUFB        SATZ AUFBEREITEN
         BAL   12,GNPCALL         GNP-CALL AUFSETZEN
         L     5,ADRPCB
         CLC   10(2,5),=C'  '     STC ABFRAGEN
         BE    A88
         CLC   10(2,5),=C'GA'
         BE    A88
         CLC   10(2,5),=C'GK'
         BE    A88
         CLC   10(2,5),=C'GP'     WENN NUR ROOTSEGM. SENSITIV
         BE    A84
         CLC   10(2,5),=C'GE'
         BNE   F9
         B     A84
A86      TM    TB2,X'20'          ANZAHL VORHANDEN?
         BNO   A89
         CP    ANZZAE,ZAEHL4      ANZAHL ERREICHT?
         BH    GETK
A89      AP    INKZAE,=P'1'       INKZAE + 1
         SR    6,6                REG. LOESCHEN
         IC    6,KEYLEN           SCHLUESSELLAENGE LADEN
         EX    6,CLC2             2. KEY ERREICHT?
         BH    GETK               WENN JA NAECHSTE KARTE LESEN
         CP    INKZAE,ZAEHL3      INKREMENT ERREICHT?
         BNE   A84
         SP    INKZAE,INKZAE      INKZAE LOESCHEN
         AP    ANZZAE,=P'1'
         B     A88
         SPACE 2
HDAMVER  CLI   TB2,X'D0'          KEY,KEY,INK?
         BE    F15                BEI HDAM UNSINNIG
         TM    TB2,X'40'          2.KEY VORHANDEN
         BNO   A90
         MVI   SSAKL,C' '         LINKE KLAMMER LOESCHEN
A93      BAL   12,GUCALL          GU/GN CALL AUFSETZEN
         L     5,ADRPCB
         CLC   10(2,5),=C'  '
         BE    A91
         CLC   10(2,5),=C'GB'     DB-ENDE
         BE    DBENDE
         CLC   10(2,5),=C'GE'
         BE    DBENDE
         B     F9
A91      NOP   A92
         LA    5,GN               GU IN GN AENDERN
         ST    5,PL1
         OI    A91+1,X'F0'        A91 AUF BRANCH
         L     5,ADRPCB           ADRPCB. LADEN
A92      SR    6,6                REG.6 LOESCHEN
         IC    6,KEYLEN           KEYL. IN REG.6
         EX    6,CLC3             SCHLUESSEL = KEY1
         BL    A93
         EX    6,CLC2             SCHLUESSEL = KEY2
         BH    A93
A94      BAL   12,SATZAUFB        SATZ AUFBEREITEN
         BAL   12,GNPCALL         GNP-CALL AUFSETZEN
         L     5,ADRPCB
         CLC   10(2,5),=C'  '     STC ABFRAGEN
         BE    A94
         CLC   10(2,5),=C'GA'
         BE    A94
         CLC   10(2,5),=C'GK'
         BE    A94
         CLC   10(2,5),=C'GE'
         BE    A93
         CLC   10(2,5),=C'GP'     WENN NUR ROOTSEGM. SENSITIV
         BE    A93
         CLC   10(2,5),=C'GB'     DB-ENDE
         BE    DBENDE
         B     F9
         SPACE 2
DBENDE   MVC   DBER+10(70),=C'ES WURDEN INSGESAMT          SEGMENTE AUF/
                DIE ARBEITSDATEI GESCHRIEBEN'
         MVC   DBER+30(8),=X'4020202020202120'  SCHABLONE
         ED    DBER+30(8),SEGZAE  SEGMENTZAEHLER AUFBEREITEN
         MVI   DBER,C'0'          2 ZEILEN VORSCHUB
         BAL   12,DRUCK
         MVI   DBER,C'-'
         MVC   DBER+10(21),=C'ENDE DER VERARBEITUNG'
         BAL   12,DRUCK
         CLOSE (KART,,LIST,,BAND)
         CLI   CALLWEI+1,X'00'    IST EIN LADEMODUL VORHANDEN
         BNE   RETURN             NEIN
         MVI   SCHBYTE,C'9'       ENDE-KENNZEICHEN SETZEN
         L     15,EPUNKT          ENTRYPOINT LADEMODUL IN R15
         L     6,MAINADR
         LA    6,4(6)
         ST    6,PL3              ADR. EA-BEREICH IN PARM-LISTE
         LA    6,SCHBYTE
         ST    6,PL3+4            ADR. SCHBYTE IN PARM-LISTE
         OI    PL3+4,X'80'        HIGH ORDER BIT ON
         LA    1,PL3              ADR. PARM-LISTE IN R1
         BALR  14,15              LINK ZUM UNTERPROGRAMM
RETURN   L     13,4(13)
         RETURN (14,12),RC=0
         SPACE 2
***      FEHLERROUTINEN
***
F1       MVC   DBER+10(27),=C'KEIN GUELTIGER PCB GEFUNDEN'
         BAL   12,DRUCK
         B     DBENDE
F2       MVC   DBER+10(38),=C'KEINE GUELTIGE STEUERKARTENANWEISUNGEN'
         BAL   12,DRUCK
         B     DBENDE
F3       MVC   DBER+10(71),=C'GETMAIN-MAKRO-INSTRUKTION KONNTE NICHT ER/
               FOLGREICH ABGESCHLOSSEN WERDEN'
         BAL   12,DRUCK
         B     DBENDE
F4       MVC   DBER+10(29),=C'SNR GROESSER ALS ACHT STELLEN'
         BAL   12,DRUCK
         B     DBENDE
F5       MVC   DBER+10(19),=C'SNR NICHT NUMERISCH'
         BAL   12,DRUCK
         B     DBENDE
F6       MVC   DBER+10(77),=C'ANZAHL DER PARAMETER IN DER DATENKARTE ST/
               IMMT NICHT MIT VORLAUFKARTE UEBEREIN'
         BAL   12,DRUCK
         B     DBENDE
F7       MVC   DBER+10(58),=C'PARAMETER IN DEN DATENKARTEN BEGINNEN MIT/
                BLANK ODER KOMMA'
         BAL   12,DRUCK
         B     DBENDE
F8       MVC   DBER+10(47),=C'2.PARAMETER GLEICH ODER KLEINER ALS 1.PAR/
               AMETER'
         BAL   12,DRUCK
         B     DBENDE
F9       MVC   DBER+10(16),=C'*** STC =    ***'
         MVC   DBER+20(2),10(5)   STC UEBERTRAGEN
         MVI   DBER,C'-'          VORSCHUBZEICHEN
         BAL   12,DRUCK
         B     DBENDE
F11      MVC   DBER+10(32),=C'KEIN KOMMA NACH EINEM SCHLUESSEL'
         BAL   12,DRUCK
         B     DBENDE
F12      MVC   DBER+10(44),=C'INKREMENT ODER ANZAHL GROESSER ALS 8 STEL/
               LEN'
         BAL   12,DRUCK
         B     DBENDE
F13      MVC   DBER+10(69),=C'KEINE ANZAHL ODER INKREMENT IN DATENKARTE/
                TROTZ ANGABE IN STEUERKARTE'
         BAL   12,DRUCK
         B     DBENDE
F14      MVC   DBER+10(37),=C'ANZAHL ODER INKREMENT NICHT NUMERISCH'
         BAL   12,DRUCK
         B     DBENDE
F15      MVC   DBER+10(36),=C'KEY,KEY,INK-ANGABE BEI HDAM UNSINNIG'
         BAL   12,DRUCK
         B     DBENDE
F16      MVC   DBER+10(41),=C'ES SIND NUR MAX. 8 DATENKARTEN ZUGELASSEN/
               '
         BAL   12,DRUCK
         B     DBENDE
F17      MVC   DBER+10(39),=C'STC=GE NACH 1.CALL ODER KEY NICHT VORH.'
         BAL   12,DRUCK
         CLI   TB2,X'80'
         BE    GETK
         B     DBENDE
F18      MVC   DBER+10(52),=C'BEI WECHSEL DER STEUERANWEISUNG TRAT FOLG/
               EFEHLER AUF'
         BAL   12,DRUCK
         MVC   DBER+10(58),=C'ANGEFORDERTER KEY IST KLEINER ALS DER ZUL/
               ETZT VERARBEITETE'
         BAL   12,DRUCK
         MVI   DBER+10,C'*'
         MVC   DBER+11(57),DBER+10
         BAL   12,DRUCK
         B     HDWEI
         SPACE 2
***      SUCHROUTINE ZUM AUFSUCHEN DES GEWUENSCHTEN PCB
PCBSUCH  L     5,ADRPST           ADR. PST IN REG. LADEN
         USING PST,5
         L     5,PSTPSB           ADR. PSB-DIRECTORY
         USING PDIR,5
         L     5,PDIRADDR         ADR. PSB
         ST    5,ADRPSB           ADR. PSB SPEICHERN
         LR    6,5                ADR. PSB IN REG. 6
         USING PSB,6
         CLC   PSBDBOFF,=X'FFFF'
         BE    A1A
         LH    8,PSBDBOFF         ADR. 1. DB-PCB ERMITTELN
         AR    5,8
A1A      LA    5,PSBLIST-PSB(5)   DISTANZ ZU PSB-LIST ADDIEREN
         L     7,0(5)             ADR. 1. PCB LADEN
         CLI   PSBCODE,PSBPL1     ABFRAGE OB PL1 PROGRAMM
         BNE   A1
         LA    7,60(7)            PL1-DOPEVEKTOR UEBERSPRINGEN
A1       CLC   EDBD,=CL8' '       KEINE DBD-ANGABE?
         BE    A2
         CLC   0(8,7),EDBD        GEWUENSCHTER DBD?
         BE    A2
         CLI   0(5),X'80'         LETZTER PCB?
         BE    F1                 FEHLERNACHRICHT AUSGEBEN
         LA    5,4(5)             AUF NAECHSTE PCB-ADRESSE GEHEN
         L     7,0(5)             PCB-ADR IN REG.7 LADEN
         B     A1
         DROP  5,6
         SPACE 2
A2       LA    7,0(7)             HIGH  ORDER BIT AUSKNIPSEN
         ST    7,ADRPCB           ADRESSE DES GEFUNDENEN PCB SPEICHERN
         ST    7,PL2+4
         ST    7,PL1+4            ADRESSE IN PARM-LISTE EINSETZEN
         BR    12
         SPACE 2
KARTLES  LA    5,EBER
GETKART  GET   KART,(5)           LESE- UND DRUCKROUTINE
         MVC   DBER+10(72),EBER
DRUCK    PUT   LIST,DBER
         MVC   DBER,DBER-1
         BR    12
         SPACE 2
***      ROUTINE ZUM AUSSTEUERN VON BLANKS
KOMPRIM  LA    6,EBER             ADR. EBER KARTE
         LA    7,ZWSP             ADR. ZWSP ZUR KOMPRIMIERUNG
VGL1     CLI   0(6),C' '          ABFRAGE AUF BLANK
         BE    A3
         MVC   0(1,7),0(6)        ZEICHEN IN ZWSP STELLEN
         LA    7,1(7)             ADR ZWSP+1
A3       LA    6,1(6)             ADR EBER+1
         C     6,=A(EBER+71)      ENDE EBER?
         BL    VGL1
         MVC   EBER(80),ZWSP      KOMPR. DATEN IN EBER STELLEN
         MVC   ZWSP,ZWSP-1        ZWSP LOESCHEN
         BR    12
         SPACE 2
***      ANALYSE DER AUSWAHLSTEUERKARTEN
ANALYSE  MVI   TB1,X'D0'          KENNZEICHEN SNR,SNR,INK SETZEN
         MVI   TB2,X'00'          KENNZEICHEN KEY LOESCHEN
         CLC   EBER(11),V1        SNR,SNR,INK?
         BCR   8,12
         MVI   TB1,X'C0'          KENNZEICHEN SNR,SNR SETZEN
         CLC   EBER(7),V1         SNR,SNR?
         BCR   8,12
         MVI   TB1,X'80'          KENNZEICHEN SNR SETZEN
         CLC   EBER(3),V1         SNR?
         BCR   8,12
         MVI   TB1,X'00'          KENNZEICHEN SNR LOESCHEN
         MVI   TB2,X'D0'          KENNZEICHEN KEY,KEY,INK SETZEN
         CLC   EBER(11),V2        KEY,KEY,INK
         BCR   8,12
         MVI   TB2,X'C0'          KENNZEICHEN KEY,KEY SETZEN
         CLC   EBER(7),V2         KEY,KEY?
         BCR   8,12
         MVI   TB2,X'B0'          KENNZEICHEN KEY,ANZ,INK SETZEN
         CLC   EBER(11),V3        KEY,ANZ,INK?
         BCR   8,12
         MVI   TB2,X'A0'          KENNZEICHEN KEY,ANZ SETZEN
         CLC   EBER(7),V3         KEY,ANZ?
         BCR   8,12
         MVI   TB2,X'80'          KENNZEICHEN KEY SETZEN
         CLC   EBER(3),V2         KEY?
         BCR   8,12
         B     F2                 FEHLER** KEINE GUELTIGE STEUERANWEISG
         SPACE 2
***      MAXIMALE SEGMENTLAENGE AUS DEM DMB ERMITTELN
***      ZUR PLATZANFORDERUNG FUER GETMAIN MACRO
MAXSEGL  L     5,ADRPCB           PCB-ADR. LADEN
         L     5,16(5)            JCB-ADR. LADEN
         ST    5,ADRJCB           JCB-ADR. SPEICHERN
         USING JCB,5
         L     5,JCBSDB1          ADR. DES 1. SDB LADEN
         USING SDB,5
         L     5,SDBDDIR          DMB-DIR. ADR. LADEN
         USING DDIR,5
         L     5,DDIRADDR         ADR. DMB-PREFIX LADEN
         ST    5,ADRDMBP          ADR. DMB-PREFIX SPEICHERN
         SR    6,6                REG. 6 LOESCHEN
         USING DMB,5
         IC    6,DMBLDDCB         ANZAHL DCB+1 IN REG. 6
         MVC   SEGL,DMBDCBMX      1. MAX. SEGMENTLAENGE SPEICHERN
         BCT   6,A8               ANZAHL DCB-1
LA1      LA    5,DMBDCBLN(5)      ADR. NAECHSTER AMPB LADEN
         CLC   SEGL,DMBDCBMX      SEGMENT-LAENGE VERGLEICHEN
         BNL   A8
         MVC   SEGL,DMBDCBMX      SEGMENTLAENGE SPEICHERN
A8       BCT   6,LA1              ANZAHL DCB-1
         LH    2,SEGL             MAX SEGL. IN R2 STELLEN
***      SEGMENTLAENGE,BLKSIZE,LRECL UND SEGM.NAME ADD.
         LA    2,18(2)            18 BYTES VORSPANN ADDIEREN
         STH   2,SEGL             NEUE SEGMENTLAENGE SPEICHERN
         LA    5,BAND             ADR. BAND-DCB LADEN
         USING IHADCB,5
         CH    2,DCBBLKSI         SEGM.LAENGE GEGEN MAX.BLKSIZE VGL.
         BL    A10
         STH   2,DCBBLKSI         GROESSERE SEGM.LAENGE IN DCB BRINGEN
         DROP  5
A10      LR    0,2
         GETMAIN R,LV=(0)         HAUPTSPEICHER-BEREICH ANFORDERN
         C     15,=F'0'           RETURN-CODE ABFRAGEN
         BNE   F3
         ST    1,MAINADR          ADR. MAINBEREICH SPEICHERN
         LA    1,12(1)            GETMAIN-BEREICH+4 F. SL-FELD,+8=NAME
         ST    1,PL1+8            ADR. IN PARAMETERLISTE STELLEN
         ICM   1,8,=X'80'         LINKES BIT IM REGISTER SETZEN
         ST    1,PL2+8
         BR    12
         SPACE 2
***      PRUEFUNG DER SATZNUMMERN AUF MAX. 8 STELLEN LAENGE
***      UND UNTERDRUECKEN DER FUEHRENDEN NULLEN
SNRPRUEF SR    6,6                REG.6=0
         LA    7,8                REG.7=8
A12      CLI   0(5),C','          KOMMA?
         BE    A11
         CLI   0(5),C' '          BLANK?
         BNE   A13
A11      CR    6,7                REG.6 GROESSER REG.7
         BH    F4
         CLI   0(5),C' '          BLANK?
         BCR   8,12
         SR    6,6                REG. 6 LOESCHEN
         B     A131
A13      LA    6,1(6)             REG.6+1
A131     LA    5,1(5)             ADR. +1
         B     A12
         SPACE 2
***      NULLEN UNTERDRUECKEN
NULLDR   LA    6,ZWSP             ADR  ZWSP LADEN
A15      CLI   0(5),C'0'          ZIFFER = 0
         BE    A16
A17      CLI   0(5),C','          KOMMA?
         BE    A18
         CLI   0(5),C' '          BLANK?
         BCR   8,12
         MVC   0(1,6),0(5)        ZIFFER IN ZWSP UEBERTRAGEN
         LA    5,1(5)             ADR. EBER+1
         LA    6,1(6)             ADR. ZWSP+1
         B     A17
A16      LA    5,1(5)             ADR. EBER+1
         B     A15
A18      MVC   0(1,6),0(5)        KOMMA  IN ZWSP STELLEN
         LA    5,1(5)             ADR +1
         LA    6,1(6)             ADR +1
         B     A15
         SPACE 2
***      ZIFFERN AUF NUMERISCH PRUEFEN UND ZAEHLER BESCHICKEN
NUMPRF   LA    5,EBER             ADR. EBER LADEN
         LA    7,EBER
         LA    8,ZAEHL1           ADR. ZAEHLER LADEN
         LA    9,64               2. HALBBYTE FUER PACKINSTRUKTION
         SR    6,6                REG. ZUM ZEICHEN ZAEHLEN LOESCHEN
A22      CLI   0(5),C','          KOMMA?
         BE    F7
         CLI   0(5),C' '          BLANK?
         BE    F7
A224     LA    5,1(5)             ADR. EBER+1
         LA    6,1(6)             ZAEHLER+1
         C     5,=A(EBER+71)
         BNL   A2241
         CLI   0(5),C','          KOMMA?
         BE    A24
         CLI   0(5),C' '          BLANK?
         BNE   A224
A2241    OI    W1+1,X'F0'         W1 AUF BRANCH SETZEN
A20      BCTR  6,0                REG. 6-1
         EX    6,MVN1             ZIFFERNTEIL IN NUMERISCHFELD STELLEN
         EX    6,CLC1             FELD NUMERISCH?
         BNE   F5
         AR    6,9                2.HALBBYTE FUER PACK SETZEN
         EX    6,PACK             FELD PACKEN
W1       NOP   A23
         LA    5,1(5)             EBER+1
         LR    7,5                EBERADR. IN REG.7 STELLEN
         SR    6,6                REG.6 LOESCHEN
         LA    8,5(8)             ZAEHLERADR. AUF NAECHSTEN ZAEHLER
         B     A22
A23      CP    KOMZAE,=P'0'       KOMMA-ZAEHLER=0?
         BNE   A25
         CLI   TB1,X'80'          AUSWAHLKARTE RICHTIG?
         BNE   F6
         BR    12
A25      CP    KOMZAE,=P'1'       KOMMA-ZAEHLER=1?
         BNE   A26
         CLI   TB1,X'C0'          AUSWAHLKARTE RICHTIG?
         BNE   F6
         B     A30
A26      CP    KOMZAE,=P'2'       KOMMA-ZAEHLER=2?
         BNE   F6
         CLI   TB1,X'D0'          AUSWAHLKARTE RICHTIG?
         BNE   F6
A30      CP    ZAEHL1,ZAEHL2      1. ZAEHLER KLEINER ALS 2. ZAEHLER?
         BCR   4,12
         B     F8
A24      AP    KOMZAE,=P'1'       KOMMA-ZAEHLER+1
         CP    KOMZAE,=P'3'       KOMMA-ZAEHLER=3
         BE    F6
         B     A20
         SPACE 3
***      BANDSATZ AUFBAUEN UND AUSGEBEN. AUFBAU:
***      4 STELLEN SL-FELD, 8 BYTES SEGMENTNAME, SEGMENT
SATZAUFB L     6,MAINADR          ADR. E/A-BEREICH LADEN
         L     7,ADRPST           PST-ADR. LADEN
         USING PST,7
         LH    7,PSTSEGCR         SEGL. IN REG.
         DROP  7
         LA    7,12(7)            SEGL.+ SLFELD UND 8 BYTE SEGM-NAME
         SLL   7,16               LAENGE UM 2 BYTES NACH LINKS SH1FTEN
         ST    7,0(6)             LAENG IN E/A-BEREICH STELLEN
         MVC   4(8,6),20(5)       SEGM-NAME AUS PCB-NAME-FB
CALLWEI  B     A401               WENN MOD-KARTE VORHANDEN = NOP
         L     15,EPUNKT          ENTRYPOINT LADEMODUL
         LA    6,4(6)             E/A-BEREICH OHNE SL-FELD
         ST    6,PL3              IN PARM-LISTE SPEICHERN
         LA    6,SCHBYTE          ADR. SCHALTER-BYTE LADEN
         ST    6,PL3+4            UND IN PARM-LISTE STELLEN
         OI    PL3+4,X'80'        HIGH ORDER BIT SETZEN
         L     6,MAINADR          ADR. E/A-BEREICH LADEN
         LA    1,PL3              ADR. PARM-LISTE IN R.1
         BALR  14,15              LINK ZUM MODUL
         CLI   SCHBYTE,C'1'       SOLL SATZ UEBERLESEN WERDEN?
         BE    A40
A401     PUT   BAND,(6)           BANDSATZ SCHREIBEN
         AP    SEGZAE,=P'1'       SEGMENTZAEHLER+1
A40      MVI   SCHBYTE,C' '       SCHALTERBYTE
         BR    12
         SPACE 2
***      ROUTINE ZUM KOMPRIMIEREN VON ANZAHL UND INKREMENT
***      AM ENDE EINES EINGELESENEN SCHLUESSELS
RESTKOMP L     7,RET5             ADR. EBER ANFANG ANZ/INK LADEN
         ST    5,RET5             ANFANG RETTEN
         LR    6,5
A62      CLI   0(5),C' '
         BE    A61
         LA    5,1(5)             ADR.+1
         CR    5,7                BEREICHSENDE ERREICHT
         BL    A62
A61      LR    6,5
A65      LA    6,1(6)
         CR    6,7                EBER ENDE?
         BCR   10,12
         CLI   0(6),C' '
         BE    A65
         MVC   0(1,5),0(6)        ZEICHEN NACH VORNE UEBERTRAGEN
         MVI   0(6),C' '          ALTE STELLE AUF BLANK SETZEN
         LA    5,1(5)             ADR.+1
         B     A65
         SPACE 2
***      AUFBEREITUNG VON SCHLUESSEL,ANZAHL UND INKREMENT
***
KEYAUFB  SR    9,9
         IC    9,KEYLEN
A55      BAL   12,GETKART         KARTE LESEN IN EBER
         AP    KZAE,=P'1'         KARTENZAEHLER +1
         CP    KZAE,=P'8'         KARTENZAHL =8
         BH    F16                BEI MEHR ALS 8 = FEHLERMELDUNG
         CLI   71(5),C' '         FOLGEKARTE VORHANDEN?
         BNE   A52
         LA    5,71(5)            ENDADRESSE EBER ERMITTELN
         ST    5,RET5             ADR. RETTEN
         LA    5,EBER
         EX    9,MVC1             SCHLUESSEL 1 IN SSA STELLEN
         CLI   TB2,X'80'          NUR 1. KEY VORHANDEN?
         BE    A60
         C     9,=F'70'           KEY-LAENGE = 71 STELLEN
         BE    A53
         AR    5,9                ADR.EBER+KEYL.
         LA    5,1(5)             ADR EBER+1 STELLEN
         CLI   0(5),C','          KOMMA?
         BNE   F11
         LA    5,1(5)             EBER+1
         TM    TB2,X'40'          2.KEY VORHANDEN
         BNO   A58
A56      EX    9,MVC2             2.KEY IN KE1BER STELLEN
         TM    TB2,X'10'          INK. VORHANDEN
         BNO   A60
         C     9,=F'70'           KEYL=71
         BE    A57
         AR    5,9                EBER+KEYL
         LA    5,1(5)             EBER+1 ZEICHEN
         CLI   0(5),C','          KOMMA?
         BNE   F11
         LA    5,1(5)             EBER+1
A58      BAL   12,RESTKOMP        REST KOMPRIMIEREN
         SR    8,8                REG. LOESCHEN
         L     5,RET5             LETZTE EBER-ADR. IN R.5
         CLI   0(5),C' '          BLANK?
         BE    F13
         CLI   0(5),C','          KOMMA?
         BE    F13
A72      LA    5,1(5)             EBERADR+1
         LA    8,1(8)             ZEICHENZAEHLER+1
A73      CLI   0(5),C' '          BLANK?
         BE    A71
         CLI   0(5),C','          KOMMA?
         BNE   A72
A75      C     8,=F'8'
         BH    F12
         BCTR  8,0                REG.8-1
         L     7,RET5             ANF.ADR. EBER LADEN
         EX    8,MVN1             NUMERISCH PRUEFUNG
         EX    8,CLC1
         BNE   F14
         LR    9,8
         LA    8,ZAEHL4
         LA    10,64              REGISTER ZUM PACK MODIFIZIEREN
         AR    9,10
         EX    9,PACK             ANZAHL PACKEN
         SR    8,8                REG. LOESCHEN
         TM    TB2,X'10'          INK. VORHANDEN
         BC    14,A60
         LA    5,1(5)
         ST    5,RET5             EBER-ADR. RETTEN
         B     A73
A71      TM    TB2,X'10'          INK. VORHANDEN
         BNO   A75
         C     8,=F'8'            ANZ. ODER INK. GR. 8 STELLEN
         BH    F12
         BCTR  8,0                REG.8-1
         L     7,RET5             ADR. EBERANF  LADEN
         EX    8,MVN1             NUMERISCH PRUEFUNG
         EX    8,CLC1
         BNE   F14
         LR    9,8
         LA    8,ZAEHL3           INK. PACKEN
         LA    10,64              REGISTER ZUM PACK MODIFIZIEREN
         AR    9,10
         EX    9,PACK
         B     A60
A52      LA    5,71(5)            EBER AUF NAECHSTEN FREIEN
         B     A55                PLATZ POSITIONIEREN
A60      LA    8,SSAVGLW+1        ADR. SCHL.-ANFANG + 1 LADEN
         SR    9,9                R.9 LOESCHEN
         IC    9,KEYLEN           SCHLUESSELLAENGE IN REG.9
         AR    8,9                ADR + SCHL.LAENGE (-1)
         MVI   0(8),C')'          RECHTE KLAMMER SETZEN
         BR    11                 RETOUR
A53      AR    5,9                EBER+KEYL
         LA    5,1(5)
         B     A56
A57      AR    5,9
         LA    5,1(5)
         B     A58
         SPACE 2
GETK     MVI   SSAKL,C'('         LINKE KLAMMER SETZEN
         MVI   SSAOP,C'>'         SSA-OPERATIONS-CODE WIEDER HERSTELLEN
         MVC   ANZZAE(45),LOEF    ZAEHLER LOESCHEN
         IC    6,KEYLEN           SCHLUESSEL-LAENGE INSERT
         EX    6,MVC3             SCHLUESSEL IN KEYBER1 STELLEN
         LA    5,GU               ADR. GU-INSTRUKTION LADEN
         ST    5,PL1              IN PARMLISTE SPEICHERN
         MVC   KEYBER2,KEYBER2-1  BEREICH LOESCHEN
         MVC   EBER(250),EBER-1   EBER LOESCHEN
         MVC   EBER+250(250),EBER+249
         MVC   EBER+500(140),EBER+499
         BAL   12,KARTLES
         BAL   12,KOMPRIM
         BAL   12,ANALYSE
         B     GETK1
         SPACE 2
GUCALL   LA    1,PL1              ADR. PARM-LISTE 1
         L     15,VCBLTDLI        SPRUNG-ADR ENTRY-POINT
         BALR  14,15              LINK
         BR    12
GNPCALL  LA    1,PL2              ADR. PL2
         L     15,VCBLTDLI        ENTRY-POINT
         BALR  14,15              LINK
         BR    12
         EJECT
KART     DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),RECFM=FB,LRECL=80,     /
               EODAD=DBENDE
         EJECT
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBA,          /
               BLKSIZE=931,LRECL=133
         EJECT
BAND     DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(PM),RECFM=VB,LRECL=3152,  /
               BLKSIZE=3156
         EJECT
***      DEFINITIONEN, BEREICHE, KONSTANTE, ZAEHLER ETC.
         SPACE 2
SAVEAREA DC    18F'0'             SAVEAREA
ADR1     DC    A(DLITCBL+4096)
         SPACE 2
TCBFSA   EQU   112                DISPLACEMENT IM TCB
         SPACE 2
ADRPST   DC    F'0'               PST-ADR
ADRPSB   DC    F'0'               PSB-ADR
ADRPCB   DC    F'0'               PCB-ADR
ADRJCB   DC    F'0'               JCB-ADR
ADRDMBP  DC    F'0'               DMB-PREFIX ADR.
MAINADR  DC    F'0'               GET MAIN-BEREICHSADRESSE
EPUNKT   DC    F'0'               EINGANGSPUNKTADR. DES LADEMODUL
EDBD     DC    CL8' '             NAME DES GESUCHTEN DBD
MODNAME  DC    CL8' '             NAME DES USER-MODULS
VCBLTDLI DC    V(CBLTDLI)         EP-ADR. IMS
PL2      DC    A(GNP)             GET NEXT WITHIN PARENT
         DC    F'0'               PCB-ADR
         DC    F'0'               GET-MAIN-ADR
PL3      DC    F'0'               PARM-LISTE FUER LADEMODUL  E/A-ADR
         DC    F'0'               ADR. DES SCHALTERBYTES
PL1      DC    A(GU)              PARAMETERLISTE FUER IMS-CALL
         DC    F'0'               ADR PCB
         DC    F'0'               ADR GETMAIN BEREICH
         DC    X'80'              HIGH ORDER BIT/LETZTES SSA IM CALL
         DC    AL3(SSA1)          ADR SSA
         DS    0F
GU       DC    CL4'GU'            GET UNIQUE
GNP      DC    CL4'GNP'           GET NEXT WITHIN PARENT
GN       DC    CL4'GN'            GET NEXT
SSA1     DC    CL8' '             SEGMENT-SUCHARGUMENT NAME
SSAKL    DC    C'('
SSAKEY   DC    CL8' '             SSA-SCHLUESSEL
SSAOP    DC    C'>='              VERGLEICHSOPERATOR
SSAVGLW  DC    CL256' '           VERGLEICHSWERT
         DC    C')'
         DC    C' '
EBER     DC    640C' '            EINLESEBEREICH KARTEN
         SPACE 2
         DC    C' '
DBER     DC    CL133' '           DRUCKBEREICH
         SPACE 2
         DC    C' '
ZWSP     DC    CL80' '            ZWSP FUER EINGABE-KARTEN
TB1      DC    X'00'              TESTBYTES ZUR IDENTIFIKATION
TB2      DC    X'00'              DER EINGABE-STEUERKARTEN
V1       DC    C'SNR,SNR,INK'     VERGLEICHSWERTE FUER
V2       DC    C'KEY,KEY,INK'     DIE STEUERKARTEN
V3       DC    C'KEY,ANZ,INK'
SEGL     DC    H'0'               SPEICHER GROESSTE SEGM.LAENGE
SCHBYTE  DC    C' '               SCHALTERBYTE FUER MODULE
KEYLEN   DC    C' '               SCHLUESSEL-LAENGE
RET5     DC    F'0'               REG.5 RETTEFELD
NUMFELD  DC    C'00000000'        PRUEFFELD NUMERISCH
         DC    C' '
KEYBER1  DC    CL256' '           ZWSP. FUER ZULETZT VERARB. KEY
         DC    C' '
KEYBER2  DC    CL256' '           ZWSP F. 2.KEY
LOEF     DC    XL5'C'             LOESCHFELD FUER ZAEHLER
ANZZAE   DC    XL5'C'
INKZAE   DC    XL5'C'             INKREMENT-ZAEHLER
ZAEHL1   DC    XL5'C'             SNR1
ZAEHL2   DC    XL5'C'             SNR2
ZAEHL3   DC    XL5'C'             INKREMENT
ZAEHL4   DC    XL5'C'             ANZAHL
RSEGZAE  DC    XL5'C'             ROOT-SEGMENT-ZAEHLER
KOMZAE   DC    XL5'C'             KOMMAZAEHLER
KZAE     DC    XL5'C'             KARTENZAEHLER
SEGZAE   DC    XL4'C'             SEGMENTZAEHLER
MVN1     MVN   NUMFELD(0),0(7)    2.HALBBYTE F. NUMERISCH PRFG. UEBERTR
CLC1     CLC   NUMFELD(0),0(7)    PRFG. OB FELD = NUMERISCH
MVC1     MVC   SSAVGLW(0),0(5)    KEY IN SSA STELLEN
MVC2     MVC   KEYBER2(0),0(5)    2.KEY SPEICHERN
MVC3     MVC   KEYBER1(0),36(5)   KEY AUS KEY-FB-AREA UEBERTR.
PACK     PACK  0(0,8),0(0,7)      FELD IN ZAEHLER PACKEN
CLC2     CLC   36(0,5),KEYBER2    2.SCHLUESSEL VGL.
CLC3     CLC   36(0,5),SSAVGLW    KEYFB GEGEN KEY.SSA VGL.
CLC4     CLC   KEYBER1(0),SSAVGLW KEY VERGLEICHEN
         SPACE 3
         PRINT NOGEN
         IDLI  PSTBASE=0,         DSECT PST                            *
               PDRBASE=0,         DSECT PDIR                           *
               PSBBASE=0,         DSECT PSB                            *
               JCBBASE=0,         DSECT JCB                            *
               SDBBASE=0,         DSECT SDB                            *
               DDRBASE=0,         DSECT DDIR                           *
               FDBBASE=0,         DSECT FDB                            *
               DMBBASE=0          DSECT DMB
         PARMBLK DSECT
         DCBD  DSORG=PS,DEVD=TA
         END
