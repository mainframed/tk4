UT183600 TITLE 'AUSGABE VON BAND-KENNSAETZEN'
***********************************************************************
*                                                                     *
* ERSTELLUNGSDATUM:      25.03.1976                                   *
*                                                                     *
* AUTOR:                 DR. MEDER                                    *
*                                                                     *
* PROGRAMMFUNKTIONEN:                                                 *
*                                                                     *
*        DAS PROGRAMM UT183600                                        *
*                                                                     *
*        - LIEST BANDKENNSAETZE MIT EXCP                              *
*                                                                     *
*        - GIBT LABEL-INFORMATIONEN WAHLWEISE AUF KONSOLE ODER        *
*          DRUCKER AUS                                                *
*                                                                     *
*        - KANN STANDARD-LABEL-, DOS-LABEL-, UND NOLABEL-BAENDER      *
*          MIT EINER ODER MEHREREN DATEIEN VERARBEITEN                *
*          MAXIMALE DATEIENZAHL: SL: 50, DOS-SL: 70, NL: 200          *
*                                                                     *
*        - ERMITTELT DIE WAHRSCHEINLICHE BLOCKGROESSE BEI             *
*          NOLABEL- UND DOS-LABEL-BAENDERN                            *
*                                                                     *
*        - ERKENNT AUSNAHME-BEDINGUNGEN (FALSCHE ETIKETTIERUNG USW.)  *
*                                                                     *
*        - ERZEUGT BEI FEHLERHAFTEM BANDAUFBAU EINEN SNAP-DUMP        *
*                                                                     *
* ZU UEBERGEBENDE PARM-WERTE:                                         *
*                                                                     *
*        WERT 1:    (DEFAULT: JD)                                     *
*                                                                     *
*          ND: VOL-SER-INP: NEIN, OUTPUT: DRUCKER, WIEDERANLAUF: NEIN *
*          JD: VOL-SER-INP: JCL , OUTPUT: DRUCKER, WIEDERANLAUF: NEIN *
*          KD: VOL-SER-INP: KONS, OUTPUT: DRUCKER, WIEDERANLAUF: WAHLW*
*          KK: VOL-SER-INP: KONS, OUTPUT: KONSOLE, WIEDERANLAUF: WAHLW*
*                                                                     *
*        WERT 2:    (DEFAULT: HDR)                                    *
*                                                                     *
*          HDR: GELESEN WERDEN NUR DIE VORSAETZE DER ERSTEN DATEI     *
*          ALL: GELESEN WERDEN ALLE KENNSAETZE                        *
*                                                                     *
***********************************************************************
         EJECT
         MACRO
&NAME    CALLSNAP &A,&B
         LCLA  &AX
         LCLC  &C,&D
&AX      SETA  1
         SPACE 3
&NAME    B     CSN2&SYSNDX
CSN1&SYSNDX    DC &A          .    FUNCTION
         CNOP  0,4
CSN2&SYSNDX BAL 1,CSN3&SYSNDX
         AIF   ('&B'  NE '').W
         DC    AL1(128)
         DC    AL3(CSN1&SYSNDX)
CSNV&SYSNDX DC V(UR123720)    .    SNAP-MODUL
CSN3&SYSNDX DS 0H
         AGO   .ENDE
.W2      ANOP
CSN3&SYSNDX DS 0H
         LA    14,CSN1&SYSNDX
         ST    14,CSNP&SYSNDX
.LOOP1   ANOP
         AIF   ('&B(&AX)' EQ '').ENDE1
         AIF   ( &AX GT 10).NOTE1
&C        SETC '&B(&AX)'
         AIF   ('&C'(1,1) EQ '(' ).KL
         LA    14,&B(&AX)
.LOOP2   ANOP
         ST    14,CSNP&SYSNDX+&AX*4
&AX      SETA  &AX+1
         AGO  .LOOP1
.W       ANOP
CSNP&SYSNDX  DC 11F'0'        .    PARM-LIST
CSNV&SYSNDX  DC V(UR123720)   .    SNAP-MODUL
         AGO   .W2
.KL      AIF   ('&C'(3,1) EQ ')' ).KL21
         AIF   ('&C'(4,1) EQ ')' ).KL22
         AIF  ('&C'(5,1) EQ ')' ).KL23
         AIF  ('&C'(6,1) EQ ')' ).KL24
         AIF  ('&C'(7,1) EQ ')' ).KL25
         MNOTE 12,'REGISTER LAENGER ALS 5 BYTES'
         AGO   .LOOP2
.KL21    ANOP
&D       SETC  '&C'(2,1)
         LR    14,&D
         AGO   .LOOP2
.KL22    ANOP
&D       SETC  '&C'(2,2)
         LR    14,&D
         AGO   .LOOP2
.KL23    ANOP
&D       SETC  '&C'(2,3)
         LR    14,&D
         AGO   .LOOP2
.KL24    ANOP
&D       SETC  '&C'(2,4)
         LR    14,&D
         AGO   .LOOP2
.KL25    ANOP
&D       SETC  '&C'(2,5)
         LR    14,&D
         AGO   .LOOP2
.NOTE1   ANOP
         MNOTE *,'MAX. 5 BEREICHE ERLAUBT, REST IGNORIERT'
.ENDE1   MVI   CSNP&SYSNDX+(&AX-1)*4,128
.ENDE    L     15,CSNV&SYSNDX
         BALR  14,15
         SPACE 3
         MEND
         MACRO
&NAME    DATE0 &A,&B
.*
.*
*
*              WENN REG.  2 EIN BASIS-REGISTER, GIB
*              ERNEUT EIN USING NACH DIESER MACRO
*
         LCLC  &L
&L       SETC  'DSE'.'&SYSNDX'
         AIF   ('&L' EQ '').MEX
         BALR  2,0
         USING *,2
&NAME    STM   4,8,&L.H
         AIF   ('&B' EQ '').NEXT
         PACK  &L.E,0(5,1)
         L     1,&L.E+4
         AGO   .NEXTZ
.NEXT    ANOP
         TIME
.NEXTZ   ANOP
         ST    1,&L.S
         L     1,&L.S
         LR    6,1
         SRDL  6,44
         STM   6,7,&L.E
         OI    &L.E+7,X'0F'
         UNPK  &A+5(3),&L.E+6(2)
         MVI   &A+5,C'.' XXXXX.JJ
         CVB   6,&L.E
         ST    6,&L.E
         TM    &L.E+3,X'03' TEST OB SCHALTJAHR
         BZ    &L.A
         MVI   &L.F+3,X'1C' NEIN, FEBRUAR = 28
         MVC   &L.G,=F'365' JAHR = 365
         B     &L.B
&L.A     MVI   &L.F+3,X'1D' JA, FEBRUAR = 29
         MVC   &L.G,=F'366' JAHR = 366
&L.B     LR    4,1
         SRDL  4,16
         SRA   4,8
         SRA   5,16
         STM   4,5,&L.E
         CVB   4,&L.E TTT, BINAER
         C     4,=F'1' TEST OB DATUM GUELTIG
         BL    &L.D NEIN
         C     4,&L.G
         BH    &L.D NEIN
         SR    7,7
         LA    8,&L.F-2
&L.C     LA    8,2(8)
         AH    7,=H'1'
         SH    4,0(0,8)
         BP    &L.C
         AH    4,0(0,8)
         CVD   7,&L.E
         UNPK  &A+2(3),&L.E+6(2)
         OI    &A+4,X'F0'
         MVI   &A+2,C'.' XX.MM.JJ
         CVD   4,&L.E
         UNPK  &L.E(3),&L.E+6(2)
         MVC   &A+0(2),&L.E+1 TT.MM.JJ
         OI    &A+1,X'F0'
         B     &L.I
&L.D     MVC   &A+0(8),=C'XX.XX.XX' UNGUELTIGES DATUM
         B     &L.I
&L.E     DS    D
&L.F     DC    X'001F001C001F001E001F001E001F001F001E001F001E001F'
&L.G     DS    F
&L.S     DS    F
&L.H     DC    5F'0' SAVE-AREA FUER MACRO
         LTORG
&L.I     LM    4,8,&L.H
         DROP  2
         AGO   .END
.MEX     MNOTE 8,'*** OPERAND FEHLT ***'
         MEXIT
.END     MEND
         MACRO
 IEFJFCBN
***********************************************************************
*
*                  JFCB
*
***********************************************************************
INFMJFCB EQU   *
JFCBDSNM DS    CL44                         DATA SET NAME
JFCBELNM DS    CL8                          ELEMENT NAME OR VERSION
JFCBTSDM DS    CL1                          TASK SCHEDULER - DATA
*                                           MANAGEMENT INTERFACE BYTE
JFCBSYSC DS    CL13                         SYSTEM CODE
JFCBLTYP DS    CL1                          LABEL TYPE AND USER'S-LABEL
*                                           INDICATOR
         DS    CL1                          NOT USED
JFCBFLSQ DS    CL2                          FILE SEQUENCE NUMBER
JFCBVLSQ DS    CL2                          VOLUME SEQUENCE NUMBER
JFCBMASK DS    CL8                          DATA MANAGEMENT MASK
JFCBCRDT DS    CL3                          DATA SET CREATION DATE
JFCBXPDT DS    CL3                          DATA SET EXPIRATION DATE
JFCBIND1 DS    CL1                          INDICATOR BYTE 1
JFCBRLSE EQU   64                            BITS 0 AND 1 - EXTERNAL
*                                            STORAGE RELEASE INDICATOR
JFCBLOCT EQU   16                            BITS 2 AND 3 - DATA SET
*                                            HAS BEEN LOCATED
JFCBNEWV EQU   4                             BITS 4 AND 5 - NEW VOLUME
*                                            ADDED TO DATA SET
JFCBPMEM EQU   1                             BITS 6 AND 7 - DATA SET IS
*                                            A MEMBER OF A PODS OR GDG
JFCBIND2 DS    CL1                          INDICATOR BYTE 2
JFCBSTAT EQU   64                            BITS 0 AND 1 - DATA SET
*                                            STATUS (NEW, OLD, OR MOD)
JFCBSCTY EQU   16                            BITS 2 AND 3 - DATA SET
*                                            SECURITY INDICATOR
JFCBUFNO DS    0AL1
JFCBUFRQ DS    AL1
JFCBFTEK DS    0BL1
JFCBFALN DS    BL1
JFCBUFL  DS    AL2
JFCEROPT DS    BL1
JFCTRTCH DS    0BL1
JFCKEYLE DS    0AL1
JFCMODE  DS    0BL1
JFCCODE  DS    0BL1
JFCSTACK  DS   0BL1
JFCPRTSP DS    BL1
JFCDEN   DS    BL1
JFCLIMCT DS    AL3
JFCDSORG DS    BL2
JFCRECFM DS    BL1
JFCOPTCD DS    BL1
JFCBLKSI DS    AL2
JFCLRECL DS    AL2
JFCNCP   DS    AL1
JFCNTM   DS    AL1
JFCRKP   DS    AL2
JFCCYLOF DS    AL1
JFCDBUFN DS    AL1
JFCINTVL DS    AL1
JFCCPRI  DS    BL1
JFCSOWA  DS    AL2
JFCBNTCS DS    CL1                          NUMBER OF OVERFLOW TRACKS
JFCBNVOL DS    CL1                          NUMBER OF VOLUME SERIAL
*                                           NUMBERS
JFCBVOLS DS    CL30                         VOLUME SERIAL NUMBERS (THE
*                                           FIRST FIVE)
JFCBEXTL DS    CL1                          LENGTH OF BLOCK OF EXTRA
*                                           VOLUME SERIAL NUMBERS
*                                           (BEYOND FIVE)
JFCBEXAD DS    CL3                          TRACK ADDRESS OF BLOCK OF
*                                           EXTRA VOLUME SERIAL NUMBERS
JFCBPQTY DS    CL3                          PRIMARY QUANTITY OF D.A.
*                                           STORAGE REQUIRED
JFCBCTRI DS    CL1                          INDICATES WHETHER CYLINDERS
*                                           TRACKS, OR RECORDS ARE
*                                           SPECIFIED IN JFCBPQTY AND
*                                           JFCBSQTY
JFCBSQTY DS    CL3                          SECONDARY QUANTITY OF D.A.
*                                           STORAGE REQUIRED
JFCBIND3 DS    CL1                          INDICATOR BYTE 3
JFCBCNTG EQU   64                            BITS 0 AND 1 - CONTIGUOUS
*                                            STORAGE INDICATOR
JFCBMXIG EQU   16                           BITS 2 AND 3 - MAXIMUM
*                                            AVAILABLE EXTENT INDICATOR
JFCBALXI EQU   4                             BITS 4 AND 5 - ALL EXTENTS
*                                            INDICATOR
JFCBRNDC EQU   1                             BITS 6 AND 7 - ROUND
*                                            CYLINDER INDICATOR
JFCBDQTY DS    CL3                          QUANTITY OF D.A. STORAGE
*                                           REQUIRED FOR A DIRECTORY
JFCBSPNM DS    CL3                          CORE ADDRESS OF THE JFCB
*                                           WITH WHICH CYLINDERS ARE
*                                           SPLIT
JFCBABST DS    CL2                          RELATIVE ADDRESS OF FIRST
*                                           TRACK TO BE ALLOCATED
JFCBSBNM DS    CL3                          CORE ADDRESS OF THE JFCB
*                                           FROM WHICH SPACE IS TO BE
*                                           SUBALLOCATED
JFCBDRLH DS    CL3                          AVERAGE DATA RECORD LENGTH
JFCBVLCT DS    CL1                          VOLUME COUNT
JFCBSPTN DS    CL1                          NUMBER OF TRACKS PER
*                                           CYLINDER TO BE USED BY THIS
*                                           DATA SET WHEN SPLIT
*                                           CYLINDERS IS INDICATED
JFCBLGTH EQU 176                       LENGTH OF JFCB
JFCBEND  EQU   *
         MEND
         MACRO
         IECDSECT
         SPACE 1
*                   THIS MACRO IS USED TO DEFINE THE WORK AREA
*                        FOR ALL MODULES OF OPEN,CLOSE,TCLOSE
*                        AND END OF VOLUME FOR O/S 360
         SPACE 1
*                   THIS MACRO DEFINES A WORK AREA WITH THE
*                        FOLLOWING FORMAT
         SPACE 1
*                        1.LABELS AND DSCB
*                             LABELS
*                                  VOLUME LABEL
*                                  FILE LABEL 1
*                                  FILE LABEL 2
*                             DSCB
*                                  FORMAT 1
*                                  FORMAT 3 KEY
*                                  FORMAT 3 DATA
*                                  CORE ADDRESS OF NEXT DSCB
*                             MESSAGE AREA
*                                                            100 BYTES
*                        2.JFCB
*                                                            176 BYTES
*                        3.ECB
*                                                              4 BYTES
*                        4.IOB
*                                                             40 BYTES
*                        5.DEB
*                                                             44 BYTES
*                        6.DCB
*                                                              4 BYTES
*                        7.CCW'S
*                                                             96 BYTES
         SPACE 1
*                                                  TOTAL *** 464 BYTES
         SPACE 2
* ***
* ***
* ***
* ***
         SPACE 1
*                   VOLUME LABEL
         SPACE 1
DXLBL    DS    0CL80
VOLLABI  DS    CL3                 LABEL IDENTIFIER
VOLNO    DS    CL1                 VOLUME LABEL NUMBER
VOLSERNO DS    CL6
VOLSEC   DS    CL1
         DS    0CL10               RESERVED
VOLVTOC  DS    CL5
         DS    CL5
         DS    CL10                RESERVED
         DS    CL10                RESERVED
VOLOWNER DS    CL10                OWNER NAME AND ADDRESS CODE
         DS    CL29                RESERVED
         SPACE 1
*                   FILE LABEL 1
         SPACE 1
         ORG   DXLBL
FL1LABI  DS    CL3                 LABEL IDENTIFIER
FL1NO    DS    CL1                 FILE LABEL NUMBER
FL1ID    DS    CL17                FILE IDENTIFIER
FL1FILSR DS    CL6                 FILE SERIAL NUMBER
FL1VOLSQ DS    CL4                 VOLUME SEQUENCE NUMBER
FL1FILSQ DS    CL4                 FILE SEQUENCE NUMBER
FL1GNO   DS    CL4                 GENERATION NUMBER
FL1VNG   DS    CL2                 VERSION NUMBER OF GENERATION
FL1CREDT DS    CL6                 CREATION DATE
FL1EXPDT DS    CL6                 EXPIRATION DATE
FL1FSEC  DC    C'0'                FILE SECURITY INDICATOR
FL1BLKCT DS    CL6                 BLOCK COUNT
FL1SYSCD DS    CL13                SYSTEM CODE
FL1RES   DS    0CL7                RESERVED FOR FUTURE USE
         DS    CL1
FL1RES1  DS    CL6
         SPACE 1
*                   FILE LABEL 2
         SPACE 1
         ORG   FL1ID
FL2RECFM DS    CL1                 RECORD FORMAT
FL2BLKL  DS    CL5                 BLOCK LENGTH
FL2LRECL DS    CL5                 BLOCKING FACTOR/RECORD LENGTH
FL2DEN   DS    CL1                 DENSITY
FL2FILP  DS    CL1                 FILE POSITION
FL2JSID  DS    0CL17               JOB/STEP IDENTIFICATION
FL2JOBD  DS    CL8                 JOB IDENTIFICATION
FL2JSSP  DC    C'/'                SLASH
FL2STEPD DS    CL8                 STEP IDENTIFICATION
FL2TRTCH DS    CL2                 TAPE RECORDING TECHNIQUE
FL2CNTRL DS    CL1                 CARRAIGE CONTROL CHARACTER
FL2RES   DS    CL43                RESERVED FOR FUTURE USE
         SPACE 1
*                   DATA SET CONTROL BLOCK
         SPACE 1
         ORG   DXLBL
DXDSCB   DS    0CL96
DSCFMTID DC    C'1'
DSCFILSR DS    CL6                 FILE SERIAL NUMBER
DSCVOLSR DS    CL2
DSCCREDT DS    CL3                 CREATION DATE IN DISCONTINOUS BIN
DSCEXPDT DS    CL3                 EXPIRATION DATE IN DISCONTINOUS BIN
DSCNOEXT DS    CL1
DSCBLDBL DS    CL1
         DS    CL1
DSCSYSCD DS    CL13                SYSTEM CODE
         DS    CL7
DSCFILTY DS    CL2                 FILE TYPE
DSCRECFM DS    CL1                 RECORD FORMAT
DSCOPTCD DS    CL1                 OPTION CODE
DSCBLKL  DS    CL2                 BLOCK LENGTH
DSCLRECL DS    CL2                 RECORD LENGTH
DSCKEYL  DS    CL1                 KEY LENGTH
DSCRKP   DS    CL2                 KEY LOCATION
DSCDSIND DS    CL1
DSCSCALO DS    CL4
DSCLSTAR DS    CL5
DSCTRBAL DS    CL2
DSCEXTYP DS    CL1                 EXTENT TYPE INDICATOR
DSCEXTSQ DS    CL1                 EXTENT SEQUENCE NUMBER
DSCLOWLM DS    CL4
DSCUPPLM DS    CL4
DSCEXT1  DS    CL10
DSCEXT2  DS    CL10
DSCNEXT  DS    CL5                 POINTER TO NEXT RECORD
DSCCORE  DS    CL4                 CORE ADDRESS OF NEXT DSCB RECORD
DSCBEND  EQU   *
         SPACE 1
*                   DATA SET CONTROL BLOCK -FORMAT 3- KEY PORTION
         SPACE 1
         ORG   DXDSCB
DXDSCB3K DS    0CL40
DSCBF3C  DC    X'03030303'
DSCBEXSK DS    0CL40
DSCBEXTY DS    CL1                 EXTENT TYPE INDICATOR
DSCBEXSQ DS    CL1                 EXTENT SEQUENCE NUMBER
DSCBLLMT DS    CL4                 CCHH LOWER LIMIT
DSCBULMT DS    CL4                 CCHH UPPER LIMIT
DSCBEX2  DS    CL10                ADDITIONAL EXTENT
DSCBEX3  DS    CL10                ADDITIONAL EXTENT
DSCBEX4  DS    CL10                ADDITIONAL EXTENT
         SPACE 1
*                   DATA SET CONTROL BLOCK -FORMAT 3- RECORD PORTION
         SPACE 1
         ORG   DXDSCB
DSCBFMID DC    C'3'                FORMAT ID
DSCBEXSD DS    0CL90               ADDITIONAL EXTENTS
DSCBEX5  DS    CL10                ADDITIONAL EXTENT
DSCBEX6  DS    CL10                ADDITIONAL EXTENT
DSCBEX7  DS    CL10                ADDITIONAL EXTENT
DSCBEX8  DS    CL10                ADDITIONAL EXTENT
DSCBEX9  DS    CL10                ADDITIONAL EXTENT
DSCBEXA  DS    CL10                ADDITIONAL EXTENT
DSCBEXB  DS    CL10                ADDITIONAL EXTENT
DSCBEXC  DS    CL10                ADDITIONAL EXTENT
DSCBEXD  DS    CL10                ADDITIONAL EXTENT
DSCBNEXT DS    CL5                 CCHHR OF NEXT FORMAT 3 DSCB
         SPACE 1
*                   MESSAGE AREA
         SPACE 1
         ORG   DXDSCB
REPLYLTH DS    CL1
REPLYADR DS    CL3
REPLYECB DS    CL4
MSGLSTSZ DS    CL4
MESSAGEA DS    CL60
REPLY    DS    CL10
*
         ORG   MESSAGEA
*
*              DEFINITION OF LENGTH OF MESSAGE COMPONENTS
*
MSERL    EQU   3                        MESSAGE SERIAL NUMBER LENGTH
MINSTL   EQU   6                        MSG INSTRUCTION LTH INC MSG SER
MUNL     EQU   3                        MESSAGE UNIT NAME LENGTH
MVOLL    EQU   6                        MESSAGE VOLUME SERIAL LENGTH
* MTXTL        LENGTH MAY BE DEFINED BY EACH MODULE TO FIT REQUIREMENT
* MSGLTH       LENGTH OF FULL MSG DEFINED BY EACH MODULE
*              MESSAGE FORMAT IS 'IEC000A M 000,00000  (TEXT)         '
MSGIOSUP DC    CL3'IEC'                 I/O SUPPORT MESSAGE IDENTITY
MSGSER   DS    0CL3                     MESSAGE SERIAL NUMBER
         ORG   MSGSER+MSERL-1
MSGSERLO DS    CL1                      VOLUME SERIAL LO ORDER BYTE
         ORG   MSGSER
MSGINSTR DC    CL6'000A M'              MESSAGE INSTRUCTION INCL MSGSER
         ORG   MSGINSTR+MINSTL-1
MSGACTN  DS    CL1                      MESSAGE ACTION REQD BY OPERATOR
         DC    C' '
MSGUN    DC    CL3'000'                 UNIT NAME THAT MSG REFERS TO
         DC    C','
MSGVOLSR DC    CL6'000000'              VOLUME SERIAL THAT MSG REFRS TO
         DC    C','
MSGTEXT  DS    0CL38
         SPACE 1
*                   JOB FILE CONTROL BLOCK
         SPACE 1
         ORG   DSCBEND
DXJBF    DS    0CL176
         IEFJFCBN
         SPACE 1
*                   EVENT CONTROL BLOCK
         SPACE 1
DXECB    DS    0CL4
         DC    X'00000000'
         SPACE 1
*                   INPUT/OUTPUT BLOCK
         SPACE 1
DXIOB    DS    0CL32
IOBFLAG1 DC    X'00'
IOBFLAG2 DC    X'00'
IOBSENSE DS    0H
IOBSENS0 DS    CL1
IOBSENS1 DS    CL1                 SENSE BYTE "1"
IOBECBPT DS    XL1
         DC    AL3(DXECB)
IOBCSW   DS    0D
IOBCOMAD DC    X'00000000'         KEY,0000,COMMAND ADDRESS
IOBSTAT0 DC    X'00'               STATUS BYTE 0
IOBSTAT1 DC    X'00'               STATUS BYTE 1
IOBCNT   DC    X'0000'             COUNT
IOBSIOCC DS    XL1
IOBSTART DC    AL3(DXCCW)
IOBWGHT  DS    XL1
IOBDCBPT DC    AL3(DXDCB)
         DS    XL1
         DS    XL3
IOBINCAM DC    X'0000'
IOBERRCT DS    XL2
DXDAADDR DS    D                   DIRECT ACCESS ADDRESS (MBBCCHHR)
         SPACE 1
*                   DATA EXTENT BLOCK
         SPACE 1
DYYYY    DS    0CL44
DXDEB    EQU   DYYYY-4
DXDEBDEB DC    X'00000000'
DXDEBOFL DS    0CL1
DXDEBIRB DC    X'00000000'
DXDEBSYS DC    X'00000000'
DXDEBUSR DC    X'00000000'
DXDEBECB DC    X'00000000'
DXDEBID  DS    0CL1
DXDEBDCB DC    AL4(DXDCB)
DXDCBAD  EQU   DXDEBDCB
DXDEBAPP DS    CL4
DXDEBMOD DS    0CL1
DXDEBUCB DS    F
DXDEBBIN DS    H
DXDEBSCC DS    H
DXDEBSHH DS    H
DXDEBECC DS    H
DXDEBEHH DS    H
DXDEBNTR DS    H
         SPACE 1
*                   DATA CONTROL BLOCK
         SPACE 1
DXXXX    DS    0F
DXDCB    EQU   DXXXX-44            POINTER TO RELATIVE BEGINNING OF DCB
DXDCBDEB DC    A(DXDEB)
         SPACE 1
*                   CHANNEL CONTROL WORDS
         SPACE 1
         CNOP  0,8
DXCCW    DS    0CL96
DXCCW1   DS    D
DXCCW2   DS    D
DXCCW3   DS    D
DXCCW4   DS    D
DXCCW5   DS    D
DXCCW6   DS    D
DXCCW7   DS    D
DXCCW8   DS    D
DXCCW9   DS    D
DXCCW10  DS    D
DXCCW11  DS    D
DXCCW12  DS    D
         SPACE 1
DSECTSIZ EQU   464                 CORE AREA REQUIRED FOR THIS MACRO
         MEND
         MACRO
&NAME    QINIT &BASEREG=13,&CSECT=CSECT,&EQU=(,R),&SAVE=(,SAVEAREA),   *
               &RETURN=(,RETURN)
.*
.*
.*       MACRO  QINIT                   INITIATE AN ASSEMBLER-PROGRAM
.*---------------------------------------------------------------------
.*
.*       THE PURPOSE OF THIS MACRO IS TO GENERATE THE NECESSARY
.*       INSTRUCTIONS FOR A CSECT, REGISTER EQUATES, SAVE-MACRO,
.*       DEFINING AND ESTABLISHING A BASE REGISTER, DEFINING A
.*       SAVEAREA AND CHAINING IT TO THE NEXT HIGHER ONE AND FINALLY
.*       TO GENERATE THE RETURN MACRO INCLUDING RESTORING OF SAVE-
.*       AREA ADDRESSES
.*
.*       ONLY KEYWORD OPERANDS ARE USED BY QINIT
.*
.*       THE SUBLIST-OPERANDS CAN BE CODED AS FOLLOWING:
.*          1. IF BOTH SUBLIST-OPERANDS ARE NEEDED, WRITE THE OPERANDS
.*             ENCLOSED IN PARANTHESES, SEPARATED BY A COMMA
.*          2. IF ONLY THE SECOND SUBLIST-OPERAND IS NEEDED, WRITE
.*             ENCLOSED IN PARANTHESES A COMMA FOLLOWED BY THIS OPERAND
.*             THE FIRST SUBLIST-OPERAND WILL BE OMITTED
.*          3. IF ONLY THE FIRST SUBLIST-OPERAND IS NEEDED, WRITE
.*            A) ONLY THIS OPERAND WITHOUT PARANTHESES, IF THE SECOND
.*               SUBLIST-OPERAND SHOULD BE THE DEFAULT VALUE     OR
.*            B) ENCLOSED IN PARANTHESES THIS OPERAND, FOLLOWED BY A
.*               COMMA, IF THE SECOND SUBLIST-OPERAND SHOULD BE OMITTED
.*
.*       ALL OPERANDS ARE OPTIONAL
.*
.*       IF NO OPERANDS ARE SPEZIFIED, THE DEFAULT VALUES WILL BE
.*       ASSUMED
.*
.*       POSSIBLE OPERANDS:
.*
.*       BASEREG=N       WHERE N IS ANY NUMBER FROM 1 TO 15
.*                       THIS REGISTER IS ESTABLISHED AS A BASE
.*                       REGISTER
.*
.*                       REGISTER 0 IS RECOCNIZED AS AN INVALID VALUE.
.*                       FOR 1,14 AND 15 WARNINGS ARE PRINTED.
.*                       EXCEPT FOR 13 AND 15 THE VALUE IS
.*                       LOADED INTO THE REGISTER BY MEANS OF A
.*                       BALR INSTRUCTION
.*
.*                       FOR 13 THE SAVEAREA ADDRESS IS LOADED
.*                       (IF THE SAVE IS SUPPRESSED (SEE KEYWORD SAVE)
.*                       AND BASEREG=13, THE VALUE IS LOADED BY MEANS
.*                       OF A BALR INSTRUCTION)
.*
.*                       FOR 15 IT IS ASSUMED THAT IT STILL CONTAINS
.*                       THE VALUE OF * (E.G. THE EP-ADDRESS),
.*                       ONLY A USING WILL BE GENERATED
.*
.*                       THE BASE REGISTER IS ALSO EQUATED TO THE
.*                       SYMBOL 'BASEREG'
.*
.*                       IF THIS OPERAND IS OMITTED 13 IS ASSUMED
.*
.*       BASEREG=        IF THE OPERAND IS NULLIFIED NO BASEREGISTER
.*                       WILL BE DEFINED
.*
.*       CSECT=START     A START INSTRUCTION IS USED INSTEAD OF CSECT
.*
.*                       IF OMITTED, A CSECT-INSTRUCTION WILL BE
.*                       GENERATED
.*
.*       CSECT=          IF NULLIFIED NO CSECT INSTRUCTION IS GENERATED
.*
.*       EQU=(ONLY,REG)  IF 'ONLY' IS CODED, ONLY EQUATE STATEMENTS
.*                       WILL BE GENERATED. ALL OTHER OPERANDS ARE BY-
.*                       PASSED
.*
.*                       INSTEAD OF 'REG' ANY VALID NAME FOR REGISTER-
.*                       EQUATES CAN BE CODED
.*
.*                       IF THE EQU-OPERAND IS OMITTED, THE DEFAULT
.*                       VALUE WILL BE ASSUMED (EQU=(,R))
.*
.*       EQU=            IF NULLIFIED, NO EQUATES WILL BE GENERATED
.*
.*       SAVE=(ID,LABEL) INSTEAD OF 'ID' ANY VALID CHARACTERS ALLOWED
.*                       IN THE ID-FIELD OF A SAVE MACRO CAN BE CODED
.*
.*                       IF THE ID-SUBOPERAND IS OMITTED AND A CSECT-
.*                       NAME IS GENERATED, THIS NAME WILL BE SUBSTITU-
.*                       TED
.*
.*                       INSTEAD OF 'LABEL' ANY SYMBOL VALID IN A NAME-
.*                       FIELD CAN BE CODED. THIS SYMBOL WILL APPEAR IN
.*                       THE NAME FIELD OF THE SAVEAREA
.*
.*                       IF 'LABEL' IS OMITTED, NO NAME WILL BE
.*                       GENERATED
.*
.*                       IF INSTEAD OF 'LABEL'  'NO' IS CODED NO SAVE-
.*                       AREA IS GENERATED AND SAVE-AREA-CHAINING IS
.*                       SUPPRESSED
.*
.*       SAVE=           IF NULLIFIED, THE GENERATION OF A SAVE-ROUTINE
.*                       WILL BE SUPPRESSED
.*
.*       RETURN=(RC,LABEL) INSTEAD OF 'RC' ANY VALID OPERAND FOR THE
.*                       RC-PARAMETER OF THE RETURN MACRO INSTRUCTION
.*                       CAN BE CODED. IF 'RC' IS OMITTED, ZERO WILL
.*                       BE ASSUMED
.*
.*                       INSTEAD OF 'LABEL' ANY SYMBOL VALID IN A NAME-
.*                       FIELD CAN BE CODED. THIS SYMBOL WILL APPEAR IN
.*                       THE NAME FIELD OF THE RETURN INSTRUCTION. IF
.*                       'LABEL' IS OMITTED THE NAME 'RETURN' APPEARS
.*                       IN THE NAME FIELD
.*
.*                       IF INSTEAD OF 'RC'  (15) IS CODED REGISTER
.*                       15 IS SUPPOSED TO CONTAIN A RETURN-CODE AND
.*                       WILL NOT BE RESTORED UPON RETURN
.*
.*                       IF THE RETURN-OPERAND IS OMITTED, RC = 0
.*                       WILL BE GENERATED AND THE NAME 'RETURN'
.*                       IS ASSUMED
.*
.*       RETURN=         IF NULLIFIED, NO RETURN STATEMENTS WILL BE
.*                       GENERATED
.*
.*
.*--------------------------------------------------------------------*
.*                                                                    *
.*                                                                    *
         GBLC  &DFTR                    = '' OR 'R' DEPENDINGON EQUATE*
.*                                      MAY BE USED INSIDE ALL MACROS *
.*                                      CONCATENATED WITH REGISTER
.*                                      OPERANDS SO THAT REGISTER     *
.*                                      TRACING IS PROVIDED ALSO INSIDE
.*                                      MACROS.                       *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         LCLC  &SANAME                  SAVEAREA NAME
         LCLC  &RETURNC                 RETURN-ROUTINE NAME
         LCLC  &BASE                    = 'BASEREG' OR VALUE OF BASE
         LCLC  &R                       = '' OR 'R' DEPENDING ON EQUATE
         LCLC  &BRANCH                  LABEL FOR BRANCH AROUND RETURN
         LCLA  &SAR                     SAVE REGISTER FOR R13
         LCLA  &VAL                     VALUE OF BASE
         LCLB  &BASEB                   BASEREG OPERAND PRESENT
         LCLB  &BASEBSP                 BASEREG IS 13 OR 15
         LCLB  &CSECTB                  CSECT OPERAND PRESENT
         LCLB  &EQUB                    EQU OPERAND PRESENT
         LCLB  &IDB                     ID OPERAND PRESENT
         LCLB  &RETURNB                 RETURN OPERAND PRESENT
         LCLB  &RCB                     RC OPERAND PRESENT
         LCLB  &SAVEB                   SAVE OPERAND PRESENT
         LCLB  &SAVEARB                 SAVEAR OPERAND PRESENT
         LCLB  &NS                      NO SAVE IF ON
.*
.*       VALIDATE OPERANDS AND SET INDICATORS
.*
         AIF   ('&SAVE(2)' NE 'NO').AREA1
         AIF   ('&BASEREG(1)' EQ '13').AREA0
         AGO   .AREA1
.AREA0   ANOP
&BASE    SETC  '15'
&VAL     SETA  15
&NS      SETB  1
.AREA1   ANOP
         AIF   ('&BASEREG' EQ '').VAL10
         AIF   (N'&BASEREG NE 1).VAL5
         AIF   (T'&BASEREG(1) NE 'N').VAL5
         AIF   (&NS).VAL4
&VAL     SETA  &BASEREG(1)
.VAL4    ANOP
         AIF   (&VAL LT 1 OR &VAL GT 15).VAL5
&BASEB   SETB  1
         AIF   (&VAL NE 13 AND &VAL NE 15).VAL10
&BASEBSP SETB  1
         AGO   .VAL10
.VAL5    ANOP
         MNOTE 8,'INVALID BASEREG OPERAND &BASEREG'
.VAL10   ANOP
         AIF   ('&CSECT' EQ '').VAL20
         AIF   (N'&CSECT NE 1).VAL15
         AIF   ('&CSECT' NE 'START' AND '&CSECT' NE 'CSECT').VAL15
&CSECTB  SETB  1                        INDICATE CSECT
         AGO   .VAL20
.VAL15   ANOP
         MNOTE 8,'INVALID CSECT OPERAND &CSECT'
.VAL20   ANOP
         AIF   ('&EQU' EQ '').VAL30
         AIF   (N'&EQU GT 2).VAL25
         AIF   ('&EQU(1)' NE '' AND '&EQU(1)' NE 'ONLY').VAL25
         AIF   (N'&EQU EQ 1 AND '&EQU' EQ 'ONLY').VAL22
         AIF   (N'&EQU EQ 2 AND '&EQU(2)' EQ '').VAL22
&R       SETC  '&EQU(2)'
         AGO   .VAL23
.VAL22   ANOP
&R       SETC  'R'
.VAL23   ANOP
&EQUB    SETB  1                        INDICATE EQUATES
         AIF   ('&EQU(1)' EQ 'ONLY').CSECT15
         AGO   .VAL30
.VAL25   ANOP
         MNOTE 8,'INVALID EQU OPERAND &EQU'
.VAL30   ANOP
         AIF   ('&SAVE' EQ '').VAL80
         AIF   (N'&SAVE GT 2).VAL70
&SAVEB   SETB  1
         AIF   (N'&SAVE NE 2).VAL40
         AIF   (T'&SAVE(2) EQ 'O').VAL60
&SANAME  SETC  '&SAVE(2)'
         AGO   .VAL50
.VAL40   ANOP
&SANAME  SETC  'SAVEAREA'
.VAL50   ANOP
&SAVEARB SETB  1
.VAL60   ANOP
         AIF   (T'&SAVE(1) EQ 'O').VAL80
&IDB     SETB  1
         AGO   .VAL80
.VAL70   ANOP
         MNOTE 8,'INVALID SAVE OPERAND &SAVE'
.VAL80   ANOP
         AIF   ('&RETURN' EQ '').VAL120
         AIF   (N'&RETURN GT 2).VAL110
&RETURNB SETB  1
         AIF   (N'&RETURN NE 2).VAL90
         AIF   (T'&RETURN(2) EQ 'O').VAL90
&RETURNC SETC  '&RETURN(2)'
         AGO   .VAL100
.VAL90   ANOP
&RETURNC SETC  'RETURN'
.VAL100  ANOP
         AIF   (T'&RETURN(1) EQ 'O').VAL120
&RCB     SETB  1
         AGO   .VAL120
.VAL110  ANOP
         MNOTE 8,'INVALID RETURN OPERAND &RETURN'
.VAL120  ANOP
         AIF   (&BASEB+&CSECTB+&EQUB+&RETURNB+&SAVEB NE 0).VAL130
         MNOTE 12,'NO VALID OPERANDS SPECIFIED'
         MEXIT
.VAL130  ANOP
&BASE    SETC  '&VAL'
.*
.* GENERATE CSECT INSTRUCTION
.*
         SPACE 5
         AIF   (NOT &CSECTB).CSECT10   NO CSECT OR START
         AIF   ('&CSECT' EQ 'START').CSECT5
&NAME    CSECT
         AGO   .CSECT15
.CSECT5  ANOP
&NAME    START
         AGO   .CSECT15
.CSECT10 AIF   ('&NAME' EQ '').CSECT90
&NAME    DS    0H
.CSECT15 ANOP
         SPACE 5
.CSECT90 ANOP
.*
.* GENERATE EQUATES
.*
         AIF   (NOT &EQUB).EQU90
&DFTR    SETC  '&EQU(2)'                EQU-PREFIX
*---------------------------------------------------------------------*
*                                                                     *
*        E Q U A T E S                                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
&R.0     EQU   0
&R.1     EQU   1
&R.2     EQU   2
&R.3     EQU   3
&R.4     EQU   4
&R.5     EQU   5
&R.6     EQU   6
&R.7     EQU   7
&R.8     EQU   8
&R.9     EQU   9
&R.10    EQU   10
&R.11    EQU   11
&R.12    EQU   12
&R.13    EQU   13
&R.14    EQU   14
&R.15    EQU   15
         AIF   ('&EQU(1)' NE 'ONLY').EQU10
         EJECT
         MEXIT
.EQU10   AIF   (NOT &BASEB).EQU80
         SPACE 3
BASEREG  EQU   &R&BASE
&BASE    SETC  'BASEREG'
         AIF   (&VAL NE 1 AND &VAL LT 14).EQU80
***                      CAUTION:       BASE REGISTER SPECIFICATION
***                                     IS NOT CONFORM TO OS-/360
***                                     LINKAGE CONVENTIONS
.EQU80   ANOP
         EJECT
.EQU90   ANOP
.*
.* ESTABLISH BASE AND GENERATE SAVE ROUTINE
.*
         AIF   (NOT (&BASEB OR &SAVEB)).SAVE90
*---------------------------------------------------------------------*
*                                                                     *
         AIF (NOT &BASEB).SAVE05
*        E S T A B L I S H   B A S E                                  *
.SAVE05  AIF   (NOT &SAVEB).SAVE10
         AIF   ('&SAVE(2)' EQ 'NO').NOTXT
*        S A V E   A N D                                              *
*        C H A I N   S A V E   A R E A S                              *
.SAVE10  ANOP
.NOTXT   ANOP
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         AIF   (&SAVEB).SAVE20          SAVE SPECIFIED
         AIF   (&VAL EQ 15).SAVE15
         BALR  &BASE,0                  SET BASE TO VALUE OF *+2
         SPACE 3
.SAVE15  USING *,&BASE                  DEFINE BASE REGISTER
         AGO   .SAVE85
.SAVE20  ANOP
         AIF   (&VAL NE 15).SAVE25
         USING *,&BASE                  INDICATE BASE
         SPACE 3
         AGO   .SAVE30
.SAVE25  ANOP
         AIF   (&VAL NE 13).SAVE30
         USING *,&R.15                  INTERMEDIATE BASE
         SPACE 3
.SAVE30  ANOP
         AIF   (NOT &IDB).SAVE35        NO ID SPECIFIED
         SAVE  (14,12),,&SAVE(1)
         AGO   .SAVE37
.SAVE35  ANOP
         SAVE  (14,12),,&NAME
.SAVE37  ANOP
         AIF   ('&SAVE(2)' NE 'NO').SAVE39
         AIF   (&VAL EQ 15).SAVE85
         BALR  &BASE,0                      BASE *
         USING *,&BASE     .
         AGO   .SAVE85
.SAVE39  ANOP
         AIF   (&BASEBSP OR NOT &BASEB).SAVE40
         SPACE 3
         BALR  &BASE,0                  SET BASE TO VALUE OF *+2
         USING *,&BASE                  INDICATE BASE
         SPACE 3
.SAVE40  ANOP
&SAR     SETA  2
         AIF   (&VAL NE 2).SAVE45       PREVENT DESTROYMENT OF BASE
&SAR     SETA  3
.SAVE45  ANOP
         LR    &R.&SAR,&R.13            SAVE HIGHER SA ADDRESS
         CNOP  0,4
         BAL   &R.13,*+76               BRANCH AROUND SAVEAREA
         SPACE 3
         AIF   (&VAL NE 13).SAVE50
         DROP  &R.15                    DROP INTERMEDIATE BASE
         USING *,&BASE                  CONTAINS SA-ADDR. AND IS BASE
         SPACE 3
.SAVE50  ANOP
&SANAME  DC    18F'0'                   SAVE AREA
         SPACE 3
         ST    &R.13,8(,&R&SAR)         LOGICAL LOWER ADDR IN HIGHER
         AIF   ('&SANAME' NE '').SAVE55
         ST    &R&SAR,4(,&R.13)         AND HIGHER ADDR IN LOWER PGM
         AGO   .SAVE85
.SAVE55  ANOP
         ST    &R&SAR,&SANAME+4         AND HIGHER ADDR IN LOWER PGM
.SAVE85  ANOP
         AIF   (NOT &RETURNB).SAVE90
         SPACE 3
.SAVE90  ANOP
.*
.* GENERATE RETURN ROUTINE
.*
         AIF   (NOT &RETURNB).RETUR90
         AIF   (NOT (&BASEB OR &SAVEB)).RETUR10
&BRANCH  SETC  '#X'.'&SYSNDX'
         B     &BRANCH                  BRANCH AROUND RETURN ROUTINE
         SPACE 5
.RETUR10 ANOP
*---------------------------------------------------------------------*
*                                                                     *
*        R E T U R N   R O U T I N E                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         AIF   ('&SAVE(2)' EQ 'NO').RETUR17
         AIF   (NOT &SAVEB).RETUR17     IF NO SAVE DO NOT RESTORE R13
         AIF   (&SAVEARB).RETUR15
&RETURNC L     &R.13,4(,&R.13)          RESTORE HIGHER SA ADDRESS
         AGO   .RETUR20
.RETUR15 ANOP
&RETURNC L     &R.13,&SANAME+4          RESTORE HIGHER SA ADDRESS
         AGO   .RETUR20
.RETUR17 ANOP
&RETURNC DS    0H
.RETUR20 ANOP
         AIF   (N'&RETURN GT 1).RETUR21
         AIF   ('&RETURN' EQ '(15)').RETUR22
         AGO   .RETUR2A
.RETUR21 ANOP
         AIF   ('&RETURN(1)' EQ '(15)').RETUR22
.RETUR2A ANOP
         AIF   (NOT &RCB).RETUR25
         RETURN (14,12),T,RC=&RETURN(1)
         AGO   .RETUR30
.RETUR22 ANOP
         RETURN (14,12),T,RC=(15)
         AGO   .RETUR30
.RETUR25 ANOP
         RETURN (14,12),T,RC=0
.RETUR30 ANOP
         SPACE 5
         AIF   (NOT (&SAVEB OR &BASEB)).EX
&BRANCH  DS    0H
         AGO   .EX
.RETUR90 AIF   (NOT (&BASEB OR &SAVEB)).EX
         SPACE 5
.EX      ANOP
         MEND
         EJECT
UT183600 CSECT
***********************************************************************
*                                                                     *
*              R E G I S T E R - D E F I N I T I O N                  *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3              BCT-REGISTER FUER STEUERLEISTE
R4       EQU   4              REGISTER FUER INTERRUPT-ADRESSE
R5       EQU   5              ARBEITS-REGISTER
R6       EQU   6              ARBEITS-REGISTER
R7       EQU   7              ARBEITS-REGISTER
R8       EQU   8              USING-REG. FUER JFCB UND IOB  (IECDSECT)
R9       EQU   9              USING-REG. FUER LABEL-TABELLE (IECDSECT)
R10      EQU   10             BASIS-REGISTER
R11      EQU   11             BASIS-REGISTER
R12      EQU   12             BASIS-REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 8
***********************************************************************
*                                                                     *
*              P R O G R A M M - I N I T I A L I S I E R U N G        *
*                                                                     *
***********************************************************************
         SAVE  (14,12),,*
         BALR  R12,R0
         USING *,R12,R11,R10
ANF      B     ANF1
ADDKON   DC    A(ANF+8192,ANF+4096)
SAVEAREA DC    18F'0'
         SPACE 1
ANF1     EQU   *
         LM    R10,R11,ADDKON
         ST    R13,SAVEAREA+4
         LA    R13,SAVEAREA
         LA    R8,KONAREA
         USING KONDSECT,R8
         LA    R9,TABELLE
         USING PUFFER,R9
         EJECT
***********************************************************************
*                                                                     *
*              S  T  E  U  E  R  L  E  I  S  T  E                     *
*                                                                     *
***********************************************************************
         SPACE 1
         BAL   R14,VORROUT              VORROUTINE
         LTR   R15,R15                  ABFRAGEN RETURNCODE
         BNZ   ANF3
         SPACE 1
ANF2     EQU   *
         BAL   R14,VOLSERRT             VOL-SER-EINGABE UEBER WTOR
         LTR   R15,R15                  ABFRAGEN RETURNCODE
         BNZ   RETURN
ANF3     EQU   *
         SR    R3,R3
         IC    R3,JFCBNVOL              REGISTER 3 FUER BCT-SCHLEIFE
         SPACE 1
ANF4     EQU   *
         BAL   R14,GRUNDST              SCHALTER AUF GRUNDSTELLUNG
         SPACE 1
         BAL   R14,EINGABE              LABEL LESEN
         CH    R15,=H'12'
         BL    ANF5
         LR    R1,R15
         BAL   R14,DUMPROUT             SNAP-DUMP AUSGEBEN
         B     BANDNEU
ANF5     EQU   *
         CH    R15,=H'8'
         BE    BANDNEU
         CH    R15,=H'4'
         BE    ANF4                     GLEICHES BAND NEU ANFORDERN
         SPACE 1
         BAL   R14,AUSGABE              LABEL AUSGEBEN
         SPACE 1
BANDNEU  EQU   *
         MVC   JFCBVOLS(24),JFCBVOLS+6  MODIFIZIEREN JFCB
         BCT   R3,ANF4                  NAECHSTES BAND ANFORDERN
         CLI   OUTSCHA,C'3'             WIEDERANLAUF?
         BNL   ANF2
         SPACE 1
RETURN   EQU   *
         BAL   R14,ENDROUT              SCHLUSS-ROUTINE
         SPACE 1
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=0
         EJECT
***********************************************************************
*                                                                     *
*              E I N G A B E - S T E U E R U N G                      *
*                                                                     *
***********************************************************************
         DC    2F'0'
EINGABE  EQU   *
         ST    R14,*-4
         SPACE 1
         OPEN  (BAND),TYPE=J
         SPACE 1
         BAL   R14,READLAB              LESEN VOL1
         LTR   R15,R15                  OK GELAUFEN?
         BZ    WEITER1
         CH    R15,=H'12'               LESEFEHLER
         BL    WEITER
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     EINGEND                  RETURNCODE: 12, 16 ODER 20
WEITER   EQU   *
         MVI   LABELTYP,C'N'            LABELTYP NL
         LR    R5,R15                   UMLADEN RETURNCODE
         CLC   JFCBVOLS(2),=C'NL'       NOLABEL-BAND ERWARTET?
         BE    WEITER0
         CLI   SERSCHA,C'U'             UNBEKANNTE VOL-SER?
         BE    WEITER0
         MVC   SERIAL,=C'NOLAB '
         BAL   R14,OPFEHLER
         LTR   R15,R15
         BNZ   EINGEND                  RETURNCODE: 4 ODER  8
WEITER0  EQU   *
         CH    R5,=H'4'                 FUEHRENDE TAPEMARK?
         BE    WEITER05
         BAL   R14,BACKSPAC             BACKSPACE-ONE-BLOCK
         BAL   R14,NOLABRT
         B     EINGEND                  RETURNCODE: 0, 12, 16, 20, 24
WEITER05 EQU   *
         MVC   PUFFER,MELDG9            MELDUNG: FUEHRENDE TAPEMARK
         MVC   PUFFER+24(6),SERIALNO
         MVI   LABELTYP,C'N'            LABELTYP NL
         MVI   LTMSCHA,C'Y'             SCHALTER 'FUEHRENDE TAPEMARK'
         LA    R9,80(R9)
         BAL   R14,NOLABRT
         B     EINGEND                  RETURNCODE: 0, 12, 16, 20, 24
WEITER1  EQU   *
         MVC   IDENTIFY,=C'VOL1'
         BAL   R14,LABELCHK
         LTR   R15,R15                  VOLUME-LABEL GEFUNDEN?
         BZ    WEITER2
         CLC   JFCBVOLS(2),=C'NL'       NOLABEL-BAND ERWARTET?
         BE    WEITER15
         CLI   SERSCHA,C'U'             UNBEKANNTE VOL-SER?
         BE    WEITER15
         MVC   SERIAL,=C'NOLAB '
         BAL   R14,OPFEHLER
         LTR   R15,R15
         BNZ   EINGEND                  RETURNCODE: 4 ODER  8
WEITER15 EQU   *
         MVI   LABELTYP,C'N'            LABELTYP NL
         BAL   R14,BACKSPAC
         BAL   R14,NOLABRT
         B     EINGEND                  RETURNCODE: 0, 12, 16, 20, 24
WEITER2  EQU   *
         MVC   SERIALNO,VOLSERNO
         CLI   SERSCHA,C'U'             VOL-SER UNBEKANNT?
         BNE   WEITER20
         CLI   OUTSCHA,C'4'             KONSOL-OUTPUT?
         BNE   WEITER22
         MVC   TEXTWT6+26(6),SERIALNO
         LA    R1,TEXTWT6               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              ERMITTELTE VOL-SER AUSGEBEN
         B     WEITER21
WEITER20 EQU   *
         BAL   R14,VOLCHECK
         LTR   R15,R15
         BNZ   EINGEND                  RETURNCODE: 4 ODER  8
         CLI   LABSCHA,C'A'             LABELSCHALTER AUF 'ALLES'
         BE    WEITER22
         CLI   VOLSCHA,C'F'             BAND FALSCH ETIKETTIERT?
         BNE   WEITER21
         MVI   LABSCHA,C'B'            'ALLE LABEL DIESES BANDES'
         B     WEITER22
WEITER21 EQU   *
         CLI   OUTSCHA,C'4'             KONSOL-OUTPUT?
         BNE   WEITER22
         BAL   R14,SLFRAGE              ALLE LABEL VERLANGT?
         SPACE 1
WEITER22 EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN HDR1
         LTR   R15,R15                  FEHLER?
         BZ    WEITER23
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'8'
         BH    EINGEND                  RETURNCODE: 12, 16 ODER 20
         LA    R15,12
         B     EINGEND                  RETURNCODE: 12
WEITER23 EQU   *
         MVC   IDENTIFY,=C'HDR1'
         BAL   R14,LABELCHK
         LTR   R15,R15                  HDR1 GELESEN?
         BZ    WEITER3
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     EINGEND                  RETURNCODE:12
WEITER3  EQU   *
         CLI   PUFFER+4,C'0'
         BE    WEITER35
         CLI   PUFFER+4,C' '
         BNE   WEITER4
WEITER35 EQU   *
         CLC   PUFFER+5(75),PUFFER+4    HDR1 DUMMY-SATZ?
         BNE   WEITER4
         MVC   PUFFER,MELDG6            FEHLERMELDUNG IN TABELLE
         MVC   PUFFER+24(6),SERIALNO
         B     EINGEND                  RETURNCODE: 0
         SPACE 1
WEITER4  EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN HDR2
         LTR   R15,R15
         BZ    WEITER5
         CH    R15,=H'4'                TAPE-MARK?
         BE    DOSLABEL
         LA    R4,*+2                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'8'
         BH    EINGEND                  RETURNCODE: 12, 16 ODER 20
         LA    R15,12
         B     EINGEND                  RETURNCODE: 12
WEITER5  EQU   *
         MVC   IDENTIFY,=C'HDR2'
         BAL   R14,LABELCHK
         LTR   R15,R15                  HDR2 GELESEN?
         BZ    SLABEL
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     EINGEND                  RETURNCODE:12
         SPACE 1
SLABEL   EQU   *
         CLI   LABSCHA,C'H'             NUR HEADER VERLANGT?    '
         BNE   SLABEL1
         AP    DSZAEHL,=P'1'            DATASET-ZAEHLER AUF 1
         LA    R15,0
         B     EINGEND                  RETURNCODE: 0
SLABEL1  EQU   *
         BAL   R14,LESENSL              LESEN SL-BAND
         B     EINGEND                  RETURNCODE: 0, 12, 16 ODER 20
         SPACE 1
DOSLABEL EQU   *
         MVI   LABELTYP,C'D'            LABELTYP DOS-SL
         BAL   R14,LESENDSL             LESEN DOS-SL-BAND
         SPACE 2
EINGEND  EQU   *
         ST    R15,EINGABE-8            SICHERN RETURNCODE
         SPACE 1
         CLOSE (BAND,DISP)
         SPACE 1
         L     R15,EINGABE-8            LADEN RETURNCODE
         L     R14,EINGABE-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              A U S G E B E N   S N A P - D U M P                    *
*                                                                     *
***********************************************************************
         DC    F'0'
DUMPROUT EQU   *
         ST    R14,*-4
         LA    R9,80(R9)
         CH    R1,=H'12'
         BNE   PHYSDUMP
         MVC   PUFFER,MELDG5            FEHLERMELDUNG IN TABELLE
         MVC   PUFFER+24(6),SERIALNO
         B     KONSMLDG
         SPACE 1
PHYSDUMP EQU   *
         MVC   PUFFER,MELDG11           FEHLERMELDUNG IN TABELLE
         MVC   PUFFER+24(6),SERIALNO
         CH    R1,=H'16'
         BNE   PHYSD1
         MVC   PUFFER+53(5),=C'READ '
         B     KONSMLDG
PHYSD1   EQU   *
         CH    R1,=H'20'
         BNE   PHYSD2
         MVC   PUFFER+53(5),=C'SENSE'
         B     KONSMLDG
PHYSD2   EQU   *
         MVC   PUFFER+53(5),=C'SKIP '
         SPACE 1
KONSMLDG EQU   *
         LA    R1,PUFFER+16             TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT
         LA    R9,80(R9)
         MVC   PUFFER,MELDG8            HINWEIS AUF INTERRUPT-ADRESSE
         SPACE 1
         L     R5,4(R13)                LADEN ADRESSE HIGHER SAVEAREA
         L     R5,16(R5)                LADEN ENTRYPOINT-ADRESSE
         CALLSNAP C'CRB',((R5),79(R9))  AUSGEBEN SNAP-DUMP
         L     R14,DUMPROUT-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              A U S G A B E - S T E U E R U N G                      *
*                                                                     *
***********************************************************************
         DC    F'0'
AUSGABE  EQU   *
         ST    R14,*-4
         LA    R4,0(R9)                 ADRESSE DES LETZTEN EINTRAGS R4
         LA    R9,TABELLE               ADRESSE TABELLE IN REGISTER 9
         CLI   OUTSCHA,C'4'             KONSOL-OUTPUT?
         BNE   AUSDRUCK
         CLI   LABELTYP,C'N'                      NOLABEL-BAND?
         BNE   AUSKON0
         CLI   LTMSCHA,C'N'             KEINE FUEHRENDE TAPEMARK (NL)?
         BE    AUSKON4
AUSKON0  EQU   *
         CLI   PUFFER,C'*'              FEHLERMELDUNG IM PUFFER?
         BNE   AUSKON1
         LA    R1,PUFFER+16             TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              FEHLERMELDUNG AUF KONSOLE
         CLI   LTMSCHA,C'F'             FEHLERBEDINGUNG BEI NL?
         BNE   AUSKON4
         B     AUSDRUCK
AUSKON1  EQU   *
         CLI   VOLSCHA,C'F'             BAND FALSCH ETIKETTIERT?
         BNE   AUSKON3
         LA    R1,PUFFER+16             TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              MELDUNG AUF KONSOLE
         LA    R1,TEXTWT7               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              HINWEIS AUF AUFLISTUNG
         B     AUSDRUCK
AUSKON3  EQU   *
         LA    R9,80(R9)
         CLI   PUFFER,C'*'              FEHLERMELDUNG IN TABELLE?
         BNE   AUSKON4
         LA    R1,PUFFER+16             TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              FEHLERMELDUNG AUF KONSOLE
         B     AUSDRUCK
AUSKON4  EQU   *
         CP    DSZAEHL,=P'1'            NUR EIN DATASET?
         BE    AUSKON5
         BAL   R14,OPFRAGEN
         B     AUSPRT
AUSKON5  EQU   *
         CLI   LABELTYP,C'N'            NOLABEL-BAND?
         BNE   AUSKON6
         BAL   R14,NLKON
         B     AUSPRT
AUSKON6  EQU   *
         CLI   LABELTYP,C'D'            DOS-SL-BAND?
         BNE   AUSKON7
         BAL   R14,DOSKON
         B     AUSPRT
AUSKON7  EQU   *
         BAL   R14,SLKON
AUSPRT   EQU   *
         MVC   TEXTWT1+8(6),SERIALNO
AUSPAP   EQU   *
         LA    R1,TEXTWT1               TEXTADRESSE IN REG 1
         MVI   WTOANTLG,X'01'           ANTWORTLAENGE
         BAL   R14,WTORROUT             PAPIER VERLANGT?
         OI    ANTWORT,C' '
         CLI   ANTWORT,C'N'             KEIN PAPIER VERLANGT?
         BE    AUSEND
         CLI   ANTWORT,C'Y'             PAPIER VERLANGT?
         BE    AUSDRUCK
         LA    R1,TEXTWT2               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              FALSCHE ANTWORT
         B     AUSPAP
         SPACE 1
AUSDRUCK EQU   *
         LA    R9,TABELLE
         BAL   R14,UEBERSCH             UEBERSCHRIFT DRUCKEN
         CLI   LABSCHA,C'H'             NUR HEADER VERLANGT?    '
         BE    AUSDR0
         BAL   R14,DSMDRUCK             ANZAHL DER DATASETS DRUCKEN
AUSDR0   EQU   *
         CLI   LABELTYP,C'N'            NOLABEL-BAND?
         BNE   AUSDR1
         BAL   R14,NLDRUCK              NOLABEL-INFORMATIONEN DRUCKEN
         B     AUSEND
AUSDR1   EQU   *
         BAL   R14,VOLDRUCK             VOLUME-LABEL DRUCKEN
         LTR   R15,R15                  FEHLERMELDUNG IM PUFFER?
         BNZ   AUSEND
         LA    R9,80(R9)
         BAL   R14,LAB1DRU              HDR1 DRUCKEN
         LTR   R15,R15                  FEHLERMELDUNG IM PUFFER?
         BNZ   AUSEND
         CLI   LABELTYP,C'D'            DOS-LABEL-BAND?
         BNE   AUSDR2
         BAL   R14,DSLDRUCK             DOS-LABEL-DRUCKEN
         B     AUSEND
AUSDR2   EQU   *
         BAL   R14,SLDRUCK              SL-LABEL DRUCKEN
AUSEND   EQU   *
         L     R14,AUSGABE-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              V  O  R  R  O  U  T  I  N  E                           *
*                                                                     *
***********************************************************************
         DC    F'0'
VORROUT  EQU   *
         ST    R14,*-4
         L     R1,0(R1)                 LADEN ADRESSE PARAMETERLISTE
         LH    R5,0(R1)                 LAENGE PARAMETERLISTE REG 5
         CH    R5,=H'2'
         BL    VORROUT4
         LA    R6,3(R1)
         CLI   2(R1),C','               KOMMA?
         BE    VORRT34
         SPACE 1
         CLC   2(2,R1),=C'KK'
         BNE   VORROUT1
         MVI   OUTSCHA,C'4'        VOL-SER-IN: KONSOLE, OUTPUT: KONSOLE
         B     VORROUT4
VORROUT1 EQU   *
         CLC   2(2,R1),=C'KD'
         BNE   VORROUT2
         MVI   OUTSCHA,C'3'        VOL-SER-IN: KONSOLE, OUTPUT: DRUCKER
         B     VORROUT3
VORROUT2 EQU   *
         CLC   2(2,R1),=C'ND'
         BNE   VORROUT3
         MVI   OUTSCHA,C'1'        VOL-SER-IN: NEIN   , OUTPUT: DRUCKER
         MVI   SERSCHA,C'U'        UNBEKANNTE VOL-SER
         SPACE 1
VORROUT3 EQU   *
         SH    R5,=H'4'
         BNP   VORROUT4
VORRT32  EQU   *
         CLI   0(R6),C','               KOMMA?
         LA    R6,1(R6)
         BE    VORRT34
         BCT   R5,VORRT32
         B     VORROUT4
VORRT34  EQU   *
         CLC   0(3,R6),=C'ALL'          ALLE LABEL VERLANGT?
         BNE   VORROUT4
         MVI   LABSCHA,C'A'        LABELSCHALTER AUF 'ALLES'
         SPACE 1
VORROUT4 EQU   *
         NI    BAND+52,B'11111011' DCB=OPTCD=Z-BIT AUS (ANGABE IN JCL)
         RDJFCB (BAND)             JFCB LESEN
         OPEN  (DRUCKER,(OUTPUT))  DRUCKER EROEFFNEN
         SPACE 1
         CLI   OUTSCHA,C'3'        VOL-SER-EINGABE UEBER KONSOLE?
         BL    VORROUT5
         LA    R15,0               LADEN RETURNCODE
         B     VOREND
VORROUT5 EQU   *
         CLI   OUTSCHA,C'1'
         BNE   VORROUT6
         MVC   JFCBVOLS(6),=C'@@@@@@'
         LA    R15,4                    LADEN RETURNCODE
         B     VOREND
VORROUT6 EQU   *
         CLC   JFCBVOLS(6),=C'@@@@@@'   KEINE VOL-SER IN JCL?
         BE    VORROUT7
         LA    R15,4                    LADEN RETURNCODE
         B     VOREND
VORROUT7 EQU   *
         MVI   OUTSCHA,C'3'
         LA    R1,TEXTWT9               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT
         LA    R15,0                    LADEN RETURNCODE
VOREND   EQU   *
         L     R14,VORROUT-4
         BR    R14
         SPACE 8
***********************************************************************
*                                                                     *
*              S  C  H  L  U  S  S  -  R  O  U  T  I  N  E            *
*                                                                     *
***********************************************************************
         DC    F'0'
ENDROUT  EQU   *
         ST    R14,*-4
         CLOSE (DRUCKER)
         L     R14,ENDROUT-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              PROGRAMM AUF GRUNDSTELLUNG SETZEN                      *
*                                                                     *
***********************************************************************
         DC    F'0'
GRUNDST  EQU   *                        SCHALTER AUF GRUNDSTELLUNG
         ST    R14,*-4
         MVI   VOLSCHA,C'R'
         MVI   LABELTYP,C'S'            LABELTYP SL
         MVI   DSSCHA,C'A'
         MVI   LTMSCHA,C'N'
         MVI   NLSTOP,C'W'
         CLI   OUTSCHA,C'4'             KONSOL-OUTPUT?
         BE    GRUNDST0
         CLI   LABSCHA,C'B'
         BNE   GRUNDST1
GRUNDST0 EQU   *
         MVI   LABSCHA,C'H'
GRUNDST1 EQU   *
         LA    R9,TABELLE
         SP    DSZAEHL,DSZAEHL          DATASET-ZAEHLER AUF 0
         SP    SEITENZ,SEITENZ          SEITENZAEHLER AUF NULL
         MVC   SERIALNO,JFCBVOLS
         L     R14,GRUNDST-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              VOL-SER-EINGABE DURCH DEN KONSOLOPERATOR               *
*                                                                     *
***********************************************************************
         DC    4F'0'
VOLSERRT EQU   *                        ERMITTELN VOLSER, FUELLEN JFCB
         ST    R14,*-4
         STM   R4,R6,VOLSERRT-16
VOLSER1  EQU   *
         MVI   ANTWORT,X'FF'
         MVC   ANTWORT+1(L'ANTWORT-1),ANTWORT     LOESCHEN ANTW.BEREICH
         LA    R1,TEXTWT10              TEXTADRESSE IN REG 1
         MVI   WTOANTLG,X'22'           ANTWORTLAENGE:34
         BAL   R14,WTORROUT
         LA    R5,34
         LA    R6,ANTWORT
VOLSER2  EQU   *
         OI    0(R6),C' '               UMSETZUNG: GROSS-KLEIN
         LA    R6,1(R6)
         BCT   R5,VOLSER2
         SPACE 1
         CLI   ANTWORT,C'?'             UNBEKANNTE VOL-SER?
         BNE   VOLSER25
         CLI   ANTWORT+1,X'FF'
         BNE   VOLSER25
         MVI   SERSCHA,C'U'             SCHALTER: BANDNUMMER UNBEKANNT
         MVC   JFCBVOLS(6),=C'@@@@@@'
         MVI   JFCBNVOL,X'01'
         LA    R15,0                    LADEN RETURNCODE
         B     VOLSREND
VOLSER25 EQU   *
         CLC   ANTWORT(2),=X'E2FF'      S FUER STOP GEGEBEN?
         BE    VOLSER27
         CLC   ANTWORT(5),=X'E2E3D6D7FF'        'STOP' EIMGEGEBEN?
         BNE   VOLSER3
VOLSER27 EQU   *
         LA    R15,4                    LADEN RETURNCODE
         B     VOLSREND
VOLSER3  EQU   *
         LA    R6,ANTWORT
         SR    R5,R5
         LA    R4,JFCBVOLS
VOLSER4  EQU   *
         TRT   0(6,R6),TRTTAB2          TEST AUF ALPHANUMERISCH
         BZ    VOLSER5
         MVC   SERIAL,0(R6)
         BAL   R14,NONALPHA
         LTR   R15,R15
         BNZ   VOLSER1
VOLSER5  EQU   *
         MVC   0(6,R4),0(R6)            VOLSER IN JFCB EINSTELLEN
         LA    R5,1(R5)
         CLI   6(R6),X'FF'              ENDE DER ANTWORT?
         BNE   VOLSER6
         STC   R5,JFCBNVOL              ANZAHL DER VOLUMES IN JFCB
         MVI   SERSCHA,C'B'
         LA    R15,0                    LADEN RETURNCODE
         B     VOLSREND
VOLSER6  EQU   *
         CLI   6(R6),C','               KOMMA GEFUNDEN?
         BNE   VOLSER7
         LA    R6,7(R6)
         LA    R4,6(R4)
         B     VOLSER4
VOLSER7  EQU   *
         LA    R1,TEXTWT2               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT
         B     VOLSER1
VOLSREND EQU   *
         LM    R4,R6,VOLSERRT-16
         L     R14,VOLSERRT-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              RUECKFRAGE BEI NICHT-ALPHAMERISCHER VOL-SER            *
*                                                                     *
***********************************************************************
         DC    2F'0'
NONALPHA EQU   *
         ST    R14,*-4
         MVC   NONALPHA-8(4),WTOANTLG   ALTE ANTWORTADRESSE SICHERN
         MVC   TEXTWT11+17(6),SERIAL    VOLSER IN TEXTWT
         LA    R14,ANTWORT2
         ST    R14,WTOANTLG             NEUE ANTWORTADRESSE
         MVI   WTOANTLG,X'01'           ANTWORTLAENGE
NONAL1   EQU   *
         LA    R1,TEXTWT11              TEXTADRESSE IN REG 1
         BAL   R14,WTORROUT
         OI    ANTWORT2,C' '
         CLI   ANTWORT2,C'Y'            OK?
         BNE   NONAL2
         LA    R15,0                    LADEN RETURNCODE
         B     NONALEND
NONAL2   EQU   *
         CLI   ANTWORT2,C'N'
         BNE   NONAL3
         LA    R15,4                    LADEN RETURNCODE
         B     NONALEND
NONAL3   EQU   *
         LA    R1,TEXTWT2               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              FALSCHE ANTWORT
         B     NONAL1
NONALEND EQU   *
         MVC   WTOANTLG(4),NONALPHA-8   ALTER STAND DER WTOLIST
         L     R14,NONALPHA-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              ANFRAGE AN OPERATOR: HEADER ODER ALLE LABEL? (SL)      *
*                                                                     *
***********************************************************************
         DC    F'0'
SLFRAGE  EQU   *
         ST    R14,*-4
         MVC   TEXTWT12+8(6),SERIALNO
SLFR1    EQU   *
         LA    R1,TEXTWT12              TEXTADRESSE IN REG 1
         MVI   WTOANTLG,X'01'           ANTWORTLAENGE:1
         BAL   R14,WTORROUT             HEADER ODER ALLES?
         OI    ANTWORT,C' '
         CLI   ANTWORT,C'A'             ALLES VERLANGT?
         BNE   SLFR2
         MVI   LABSCHA,C'A'             LABELSCHALTER AUF ALLES
         B     SLFREND
SLFR2    EQU   *
         CLI   ANTWORT,C'H'             NUR HEADER VERLANGT?
         BNE   SLFR3
         MVI   LABSCHA,C'H'             LABELSCHALTER AUF HEADER
         B     SLFREND
SLFR3    EQU   *
         LA    R1,TEXTWT2               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              FALSCHE ANTWORT
         B     SLFR1
SLFREND  EQU   *
         L     R14,SLFRAGE-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              ANFRAGE AN OPERATOR: NUR ERSTE DATEI? (NL)             *
*                                                                     *
***********************************************************************
         DC    F'0'
NLFRAGE  EQU   *
         ST    R14,*-4
         MVC   TEXTWT8+8(6),SERIALNO
NLFR1    EQU   *
         LA    R1,TEXTWT8               TEXTADRESSE IN REG 1
         MVI   WTOANTLG,X'01'           ANTWORTLAENGE:1
         BAL   R14,WTORROUT             NUR EIN DATASET?
         OI    ANTWORT,C' '
         CLI   ANTWORT,C'Y'             NUR EIN DATASET?
         BNE   NLFR2
         MVI   LABSCHA,C'H'             LABELSCHALTER AUF HEADER
         B     NLFREND
NLFR2    EQU   *
         CLI   ANTWORT,C'N'             ALLE DATASETS?
         BNE   NLFR3
         MVI   LABSCHA,C'A'             LABELSCHALTER AUF ALLES
         B     NLFREND
NLFR3    EQU   *
         LA    R1,TEXTWT2               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              FALSCHE ANTWORT
         B     NLFR1
NLFREND  EQU   *
         L     R14,NLFRAGE-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              LESE-STEUERUNG FUER STANDARD-LABEL-BAENDER             *
*                                                                     *
***********************************************************************
         DC    F'0'
LESENSL  EQU   *                        LESEN STANDARDLABEL
         ST    R14,*-4
         B     SLDS1
ANFSL    EQU   *
         BAL   R14,SKIPFILE             UEBERSPRINGEN TAPEMARK
         CH    R15,=H'20'               FEHLER?
         BL    SL1
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SL1      EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN HDR1
         CH    R15,=H'8'                FEHLER?
         BL    SL2
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'12'
         BNL   ENDESL
         LA    R15,12
         B     ENDESL
SL2      EQU   *
         CH    R15,=H'4'                TAPE-MARK GELESEN?
         BNE   SL3
         SH    R9,=H'80'                ADRESSE DES LETZTEN EINTRAGS R9
         LA    R15,0                    LADEN RETURNCODE
         B     ENDESL                   BAND-ENDE
SL3      EQU   *
         MVC   IDENTIFY,=C'HDR1'
         BAL   R14,LABELCHK
         LTR   R15,R15                  HDR1 GELESEN?
         BZ    SL4
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SL4      EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN HDR2
         LTR   R15,R15                  FEHLER?
         BZ    SL45
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'12'
         BNL   ENDESL
         LA    R15,12
         B     ENDESL
SL45     EQU   *
         MVC   IDENTIFY,=C'HDR2'
         BAL   R14,LABELCHK
         LTR   R15,R15                  HDR2 GELESEN?
         BZ    SLDS1
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SLDS1    EQU   *
         BAL   R14,SKIPFILE             UEBERSPRINGEN TAPE-MARK
         CH    R15,=H'12'               FEHLER?
         BL    SL5
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SL5      EQU   *
         BAL   R14,SKIPFILE             UEBERSPRINGEN DATEN
         CH    R15,=H'20'               FEHLER?
         BL    SL6
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SL6      EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN EOF1 BZW EOV1
         LTR   R15,R15                  FEHLER?
         BZ    SL65
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'12'
         BNL   ENDESL
         LA    R15,12
         B     ENDESL
SL65     EQU   *
         MVC   IDENTIFY,=C'EOF1'
         BAL   R14,LABELCHK
         CH    R15,=H'12'               KEIN EOF1 BZW EOV1?
         BNE   SL7
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SL7      EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN EOF2 BZW EOV2
         LTR   R15,R15                  FEHLER?
         BZ    SL75
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'12'
         BNL   ENDESL
         LA    R15,12
         B     ENDESL
SL75     EQU   *
         MVC   IDENTIFY,=C'EOF2'
         BAL   R14,LABELCHK
         CH    R15,=H'12'               KEIN EOF2 BZW EOV2?
         BNE   SL8
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SL8      EQU   *
         AP    DSZAEHL,=P'1'
         CH    R15,=H'4'                EOV2 GELESEN?
         BNE   SL9
         LA    R15,0                    LADEN RETURNCODE
         B     ENDESL
SL9      EQU   *
         CP    DSZAEHL,=P'50'
         BL    ANFSL
         SPACE 1
         BAL   R14,SKIPFILE             UEBERSPRINGEN TAPE-MARK
         CH    R15,=H'20'               FEHLER?
         BL    SL10
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SL10     EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN HDR1
         CH    R15,=H'8'                FEHLER?
         BL    SL11
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'12'
         BNL   ENDESL
         LA    R15,12
         B     ENDESL
SL11     EQU   *
         CH    R15,=H'4'                TAPEMARK GELESEN?
         BNE   SL12
         SH    R9,=H'80'                ADRESSE DES LETZTEN EINTRAGS R9
         LA    R15,0                    LADEN RETURNCODE
         B     ENDESL
SL12     EQU   *
         MVC   IDENTIFY,=C'HDR1'
         BAL   R14,LABELCHK
         LTR   R15,R15                  HDR1 GELESEN?
         BZ    SL13
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDESL
SL13     EQU   *
         MVI   DSSCHA,C'W'              MEHR ALS 50 DATASETS
         SH    R9,=H'80'                ADRESSE DES LETZTEN EINTRAGS R9
ENDESL   EQU   *
         L     R14,LESENSL-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              LESE-STEUERUNG FUER DOS-SL-BAENDER                     *
*                                                                     *
***********************************************************************
         DC    2F'0'
LESENDSL EQU   *                        LESEN DOS-LABEL
         ST    R14,*-4
         ST    R5,LESENDSL-8
         B     DSLDS1
ANFDSL   EQU   *
         BAL   R14,SKIPFILE             UEBERSPRINGEN TAPEMARK
         CH    R15,=H'20'               FEHLER?
         BL    DSL1
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDEDSL
DSL1     EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN HDR1
         CH    R15,=H'8'                FEHLER?
         BL    DSL2
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'12'
         BNL   ENDEDSL
         LA    R15,12
         B     ENDEDSL
DSL2     EQU   *
         CH    R15,=H'4'                TAPE-MARK GELESEN
         BNE   DSL3
         SH    R9,=H'80'                ADRESSE DES LETZTEN EINTRAGS R9
         LA    R15,0                    LADEN RETURNCODE
         B     ENDEDSL                  BAND-ENDE
DSL3     EQU   *
         MVC   IDENTIFY,=C'HDR1'
         BAL   R14,LABELCHK
         LTR   R15,R15                  HDR1 GELESEN?
         BZ    DSL4
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDEDSL
DSL4     EQU   *
         BAL   R14,SKIPFILE             UEBERSPRINGEN TAPE-MARK
         CH    R15,=H'12'               FEHLER?
         BL    DSLDS1
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDEDSL
DSLDS1   EQU   *
         BAL   R14,PRUEFRT              LAENGENPRUEFUNG
         CH    R15,=H'16'               LESEFEHLER?
         BL    DSL42
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDEDSL
DSL42    EQU   *
         CH    R15,=H'4'                TAPE-MARK GELESEN?
         BNL   DSL5
         CLI   LABSCHA,C'H'             NUR HEADER VERLANGT?    '
         BE    DSL51
         BAL   R14,SKIPFILE             UEBERSPRINGEN DATEN
         CH    R15,=H'20'               FEHLER?
         BL    DSL51
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDEDSL
DSL5     EQU   *
         L     R5,BLOCKZHL
         LTR   R5,R5                    KEINE DATEN VORHANDEN?
         BNZ   DSL51
         MVC   PUFFER,MELDG7            MELDUNG IN TABELLE
         MVC   PUFFER+59(8),=C'NO DATA '
         B     DSL52
DSL51    EQU   *
         BAL   R14,BLOCKSIZ
         MVC   PUFFER,MELDG7            MELDUNG IN TABELLE
         MVC   PUFFER+59(8),BLKSIZE
DSL52    EQU   *
         CLI   LABSCHA,C'H'             NUR HEADER VERLANGT?    '
         BNE   DSL53
         LA    R15,0                    LADEN RETURNCODE
         AP    DSZAEHL,=P'1'            DATASET-ZAEHLER AUF 1
         B     ENDEDSL
DSL53    EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN EOF1 BZW EOV1
         LTR   R15,R15                  FEHLER?
         BZ    DSL54
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'12'
         BNL   ENDEDSL
         LA    R15,12
         B     ENDEDSL
DSL54    EQU   *
         MVC   IDENTIFY,=C'EOF1'
         BAL   R14,LABELCHK
         CH    R15,=H'12'               KEIN EOF1 BZW EOV1?
         BNE   DSL6
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDEDSL
DSL6     EQU   *
         AP    DSZAEHL,=P'1'
         CH    R15,=H'4'                EOV1 GELESEN?
         BNE   DSL7
         LA    R15,0                    LADEN RETURNCODE
         B     ENDEDSL
DSL7     EQU   *
         CP    DSZAEHL,=P'70'
         BL    ANFDSL
         SPACE 1
         BAL   R14,SKIPFILE             UEBERSPRINGEN TAPEMARK
         CH    R15,=H'20'               FEHLER?
         BL    DSL8
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDEDSL
DSL8     EQU   *
         LA    R9,80(R9)
         BAL   R14,READLAB              LESEN HDR1
         CH    R15,=H'8'                FEHLER?
         BL    DSL9
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         CH    R15,=H'12'
         BNL   ENDEDSL
         LA    R15,12
         B     ENDEDSL
DSL9     EQU   *
         CH    R15,=H'4'                TAPE-MARK GELESEN?
         BNE   DSL10
         SH    R9,=H'80'                ADRESSE DES LETZTEN EINTRAGS R9
         LA    R15,0                    LADEN RETURNCODE
         B     ENDEDSL
DSL10    EQU   *
         MVC   IDENTIFY,=C'HDR1'
         BAL   R14,LABELCHK
         LTR   R15,R15                  HDR1 GELESEN?
         BZ    DSL11
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     ENDEDSL
DSL11    EQU   *
         MVI   DSSCHA,C'W'
         SH    R9,=H'80'                ADRESSE DES LETZTEN EINTRAGS R9
ENDEDSL  EQU   *
         L     R5,LESENDSL-8
         L     R14,LESENDSL-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              LESE-STEUERUNG FUER NOLABEL-BAENDER                    *
*                                                                     *
***********************************************************************
         DC    2F'0'
NOLABRT  EQU   *                        NOLABEL-ROUTINE
         ST    R14,*-4
         ST    R5,NOLABRT-8
         CLI   OUTSCHA,C'4'             KONSOL-OUTPUT?
         BNE   NOLAB1
         BAL   R14,NLFRAGE              NUR EIN DATASET?
NOLAB1   EQU   *
         BAL   R14,PRUEFRT              LAENGENPRUEFUNG
         CH    R15,=H'16'               LESEFEHLER?
         BL    NOLAB15
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     NOLABRET
NOLAB15  EQU   *
         CH    R15,=H'4'                TAPEMARK GELESEN?
         BL    NOLAB3
         L     R5,BLOCKZHL
         LTR   R5,R5                    KEINE DATEN GEFUNDEN?
         BZ    NOLAB7
         CH    R15,=H'8'                TAPE INDICATE?
         BNE   NOLAB5
         ZAP   DSZAEHL,DSZAEHL          DSZAEHLER AUF NULL?
         BNZ   NOLAB2
         LA    R4,*-10
         LA    R15,12                   LADEN RETURNCODE
         B     NOLABRET
NOLAB2   EQU   *
         MVI   NLSTOP,C'S'
         B     NOLAB5
NOLAB3   EQU   *
         CLI   LABSCHA,C'A'             ALLE DATEIEN VERLANGT?
         BNE   NOLAB5
         BAL   R14,SKIPFILE
         LTR   R15,R15                  IN ORDNUNG?
         BZ    NOLAB5
         CH    R15,=H'12'
         BE    NOLAB4
         LR    R4,R14                   INTERRUPT-ADRESSE IN REG 4
         B     NOLABRET
NOLAB4   EQU   *
         MVI   NLSTOP,C'S'
NOLAB5   EQU   *
         CP    DSZAEHL,=P'200'          BEREITS 200 DATASETS?
         BL    NOLAB6
         MVI   DSSCHA,C'W'              ZU VIELE DATASETS
         SH    R9,=H'80'                ADRESSE DES LETZTEN EINTRAGS R9
         LA    R15,0                    LADEN RETURNCODE
         B     NOLABRET
NOLAB6   EQU   *
         BAL   R14,BLOCKSIZ             BLOCKSIZE ERMITTELN
         AP    DSZAEHL,=P'1'
         MVC   PUFFER,MELDG3            MELDUNG IN PUFFER
         MVC   PUFFER+24(6),SERIALNO
         UNPK  PUFFER+43(3),DSZAEHL
         OI    PUFFER+45,X'F0'
         MVC   PUFFER+55(8),BLKSIZE
         LA    R15,0                    LADEN RETURNCODE
         CLI   LABSCHA,C'A'             ALLE DATASETS VERLANGT?
         BNE   NOLABRET
         LA    R9,80(R9)
         CLI   NLSTOP,C'S'              BANDENDE?
         BNE   NOLAB1
         SPACE 1
         MVC   PUFFER,MELDG10           FOLGEBAND-MELDUNG
         MVC   PUFFER+24(6),SERIALNO
         B     NOLABRET
NOLAB7   EQU   *
         LA    R15,0                    LADEN RETURNCODE
         SH    R9,=H'80'                ADRESSE DES LETZTEN EINTRAGS R9
         ZAP   DSZAEHL,DSZAEHL          DS-ZAEHLER AUF NULL
         BNZ   NOLABRET
         MVI   LTMSCHA,C'F'
         MVC   PUFFER+35(4),=C'ZWEI'    2 FUEHRENDE TAPEMARKS
         MVI   PUFFER+58,C'S'
NOLABRET EQU   *
         L     R5,NOLABRT-8
         L     R14,NOLABRT-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              LESEN LABEL MIT EXCP                                   *
*                                                                     *
***********************************************************************
         DC    F'0'
READLAB  EQU   *                        LESEN LABEL MIT EXCP
         ST    R14,*-4
         XC    BANDECB,BANDECB          LOESCHEN BANDECB
         MVC   LABELCCW,READCCW         CCW ZUM LESEN
         STCM  R9,7,LABELCCW+1          ADRESSE PUFFER IN CCW EINSETZEN
         EXCP  BANDIOB
         WAIT  ECB=BANDECB
         LA    R15,0                    LADEN RETURNCORDE
         CLI   BANDECB,X'7F'            OK GELAUFEN?
         BE    READEND
         LA    R15,4                    LADEN RETURNCODE
         TM    IOBSTAT0,X'01'           UNIT EXCEPTION?
         BNZ   READEND
         LA    R15,16                   LADEN RETURNCODE
         TM    IOBSTAT1,X'40'           FALSCHE LAENGE?
         BZ    READEND
         MVC   LABELCCW,SENSECCW        CCW FUER SENSE
         XC    BANDECB,BANDECB          BANDECB LOESCHEN
         EXCP  BANDIOB
         WAIT  ECB=BANDECB
         LA    R15,20                   LADEN RETURNCODE
         CLI   BANDECB,X'7F'            OK GELAUFEN?
         BNE   READEND
         LA    R15,8                    LADEN RETURNCODE
         TM    SENSEBYT+4,X'20'         TAPE INDICATE?
         BZ    READEND
         LA    R15,12                   LADEN RETURNCODE
READEND  EQU   *
         L     R14,READLAB-4
         BR    R14
*        RETURNCODES (REGISTER 15):
*        0:    IN ORDNUNG (80-STELLIGER SATZ GELESEN)
*        4:    TAPE-MARK GELESEN
*        8:    BLOCKLAENGE UNGLEICH 80
*        12:   TAPE INDICATE (ZUSAMMEN MIT FALSCHER BLOCKLAENGE)
*        16:   SONSTIGER FEHLER IM READ
*        20:   FEHLER IM SENSE
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              UEBERSPRINGEN DATEN                                    *
*                                                                     *
***********************************************************************
         DC    F'0'
SKIPFILE EQU   *                        UEBERSPRINGEN TAPEMARK
         ST    R14,*-4
         OI    IOBFLAG1,X'40'           COMMAND-CHAINING-BIT AUF 1
         LA    R14,SKIPCCW
         ST    R14,IOBSIOCC             ADRESSE SKIPCCW IN IOB
SKIP1    EQU   *
         XC    BANDECB,BANDECB          BANDECB LOESCHEN
         EXCP  BANDIOB
         WAIT  ECB=BANDECB
         CLI   BANDECB,X'7F'            OK GELAUFEN?
         BE    SKIP1
         LA    R15,24                   LADEN RETURNCODE
         TM    IOBSTAT0,X'01'           UNIT EXCEPTION?
         BZ    SKIPEND                  RETURNCODE: 24
         LA    R14,LABELCCW
         ST    R14,IOBSIOCC             ADRESSE LABELCCW IN IOB
         NI    IOBFLAG1,B'10111111'     COMMAND-CHAINING-BIT AUF 0
         XC    BANDECB,BANDECB          LOESCHEN BANDECB
         MVC   LABELCCW,SENSECCW        CCW FUER SENSE
         EXCP  BANDIOB
         WAIT  ECB=BANDECB
         LA    R15,20                   LADEN RETURNCODE
         CLI   BANDECB,X'7F'            SENSE OK GELAUFEN?
         BNE   SKIPEND
         LA    R15,0                    LADEN RETURNCODE
         TM    SENSEBYT+4,X'20'         TAPE INDICATE?
         BZ    SKIPEND
         LA    R15,12                   LADEN RETURNCODE
SKIPEND  EQU   *
         L     R14,SKIPFILE-4
         BR    R14
*        RETURNCODES (REGISTER 15)
*        0:    IN ORDNUNG (BAND STEHT HINTER TAPEMARK)
*        12:   TAPE-INDICATE (BAND STEHT HINTER REFLEKTORMARKE)
*        20:   FEHLER IM SENSE
*        24:   FEHLER IM SPACE FORWARD FILE
         EJECT                                                        *
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              LESEN DATEI-SAETZE UND SPEICHERN RESTCOUNT             *
*                                                                     *
***********************************************************************
         DC    3F'0'
PRUEFRT  EQU   *
         ST    R14,*-4
         STM   R5,R6,PRUEFRT-12
         LA    R6,LAENGTAB
         LA    R5,0
PRUEF1   EQU   *
         XC    BANDECB,BANDECB          LOESCHEN BANDECB
         MVC   LABELCCW,PRUEFCCW        CCW ZUR LAENGENPRUEFUNG
         EXCP  BANDIOB
         WAIT  ECB=BANDECB
         CLI   BANDECB,X'7F'            OK GELAUFEN?
         BNE   PRUEF2
         MVC   0(2,R6),IOBCNT           IOBCNT IN LAENGTAB
         LA    R6,2(R6)
         LA    R5,1(R5)
         LA    R15,0                    LADEN RETURNCODE
         CH    R5,=H'10'
         BE    PRUEFEND
         B     PRUEF1
PRUEF2   EQU   *
         LA    R15,16                   LADEN RETURNCODE
         TM    IOBSTAT0,X'01'           UNIT EXCEPTION?
         BZ    PRUEFEND
         MVC   LABELCCW,SENSECCW        CCW FUER SENSE
         XC    BANDECB,BANDECB          BANDECB LOESCHEN
         EXCP  BANDIOB
         WAIT  ECB=BANDECB
         LA    R15,20                   LADEN RETURNCODE
         CLI   BANDECB,X'7F'            OK GELAUFEN?
         BNE   PRUEFEND
         LA    R15,4                    LADEN RETURNCODE
         TM    SENSEBYT+4,X'20'
         BZ    PRUEFEND
         LA    R15,8
PRUEFEND EQU   *
         ST    R5,BLOCKZHL
         LM    R5,R6,PRUEFRT-12
         L     R14,PRUEFRT-4
         BR    R14
         SPACE 1
*        RETURNCODES (REGISTER 15):
*        0:    IN ORDNUNG
*        4:    TAPE-MARK GELESEN
*        8:    TAPE-INDICATE (ZUSAMMEN MIT TAPEMARK)
*        16:   FEHLER IM READ
*        20:   FEHLER IM SENSE
         SPACE 10
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              BACKSPACE ONE BLOCK                                    *
*                                                                     *
***********************************************************************
         DC    F'0'
BACKSPAC EQU   *                        BAND ZURUECKSETZEN
         ST    R14,*-4
         MVC   LABELCCW,BACKCCW         CCW ZUM ZURUECKSETZEN
         XC    BANDECB,BANDECB          BANDECB LOESCHEN
         EXCP  BANDIOB
         WAIT  ECB=BANDECB
         L     R14,BACKSPAC-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              PRUEFUNG AUF STANDARD-LABEL                            *
*                                                                     *
***********************************************************************
         DC    F'0'
LABELCHK EQU   *                        PRUEFUNG AUF STANDARD-LABEL
         ST    R14,*-4
         LA    R15,0                    LADEN RETURNCODE
         CLC   IDENTIFY,VOLLABI         RICHTIGER LABEL?
         BE    LCHKEND
         LA    R15,12                   LADEN RETURNCODE
         CLC   IDENTIFY(3),=C'EOF'      TRAILER-LABEL VERLANGT?
         BNE   LCHKEND
         MVI   IDENTIFY+2,C'V'          UMSETZEN IDENTIFIER AUF EOV
         CLC   IDENTIFY,VOLLABI         EOV-LABEL?
         BNE   LCHKEND
         LA    R15,4                    LADEN RETURNCODE EOV
LCHKEND  EQU   *
         L     R14,LABELCHK-4
         BR    R14
         SPACE 8
***********************************************************************
*                                                                     *
*              S O L L - I S T - V E R G L E I C H   V O L - S E R    *
*                                                                     *
***********************************************************************
         DS    F'0'
VOLCHECK EQU   *                        PRUEFUNG AUF RICHTIGE VOLSERNO
         ST    R14,*-4
         MVC   SERIAL,VOLSERNO
         LA    R15,0                    LADEN RETURNCODE
         CLC   SERIAL,JFCBVOLS
         BE    VOLEND
         BAL   R14,OPFEHLER
         LTR   R15,R15                  RICHTIGES BAND?
         BNZ   VOLEND
         MVI   VOLSCHA,C'F'             SCHALTER AUF FALSCHE VOLSERNO
         MVC   PUFFER,MELDG4            FEHLERMELDUNG IN TABELLE
         MVC   PUFFER+43(6),JFCBVOLS
         MVC   PUFFER+58(6),SERIAL
VOLEND   EQU   *
         L     R14,VOLCHECK-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              ERMITTELN BLOCKSIZE AUS RESTCOUNT-WERTEN               *
*                                                                     *
***********************************************************************
         DC    4F'0'
BLOCKSIZ EQU   *
         ST    R14,*-4
         STM   R5,R7,BLOCKSIZ-16
         L     R5,BLOCKZHL
         SH    R5,=H'2'
         LA    R7,LAENGTAB
         LTR   R5,R5
         BNP   BLOCKSZ2
BLOCKSZ1 EQU   *
         CLC   0(2,R7),2(R7)
         BNE   BLOCKSZ3
         BCT   R5,BLOCKSZ1
BLOCKSZ2 EQU   *
         SR    R5,R5
         LH    R5,LAENGTAB
         L     R6,=F'32760'
         SR    R6,R5
         CVD   R6,DSDOWO
         UNPK  BLKSIZE(5),DSDOWO
         OI    BLKSIZE+4,X'F0'
         MVC   BLKSIZE+5(3),=C'   '
         B     BLOCKEND
BLOCKSZ3 EQU   *
         MVC   BLKSIZE,=C'VARIABEL'
BLOCKEND EQU   *
         LM    R5,R7,BLOCKSIZ-16
         L     R14,BLOCKSIZ-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              ANFRAGE AN OPERATOR: RICHTIGES BAND AUFGELEGT?         *
*                                                                     *
***********************************************************************
         DC    F'0'
OPFEHLER EQU   *                        ABFRAGE AUF OPERATORFEHLER
         ST    R14,*-4
         L     R14,BAND+44              LADEN DEB-ADRESSE
         L     R14,32(R14)              LADEN UCB-ADRESSE
         MVC   TEXTWT3+24(3),13(R14)    UEBERTRAGEN EINHEITEN-ADRESSE
         MVC   TEXTWT3+40(6),JFCBVOLS   UEBERTRAGEN VOLSERNO(SOLL)
         MVC   TEXTWT3+60(6),SERIAL     UEBERTRAGEN VOLSERNO(IST)
         LA    R1,TEXTWT3               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              KONSOLE: FALSCHE VOLSERNO
FEHLWTOR EQU   *
         LA    R1,TEXTWT4               TEXTADRESSE IN REG 1
         MVI   WTOANTLG,X'01'           ANTWORTLAENGE
         BAL   R14,WTORROUT             RICHTIGES BAND ,YES ODER NO
         OI    ANTWORT,C' '
         CLI   ANTWORT,C'Y'
         BNE   OPFHLR1
         LA    R15,0                    LADEN RETURNCODE: RICHT. BAND
         B     OPEND
OPFHLR1  EQU   *
         CLI   ANTWORT,C'N'
         BNE   OPFHLR2
         LA    R15,4                    LADEN RETURNCODE: FALSCHES BAND
         B     OPEND
OPFHLR2  EQU   *
         CLI   ANTWORT,C'W'
         BNE   OPFHLR3
         LA    R15,8                    LADEN RETURNCODE: WEITER
         B     OPEND
OPFHLR3  EQU   *
         LA    R1,TEXTWT2               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              FALSCHE ANTWORT
         B     FEHLWTOR
OPEND    EQU   *
         L     R14,OPFEHLER-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              WTO-ROUTINE                                            *
*                                                                     *
***********************************************************************
         DC    F'0'
WTOROUT  EQU   *
         ST    R14,*-4
         BAL   R14,WTOFUELL             WTO FUELLEN
         WTO   MF=(E,WTOLIST)           WTO AUSFUEHREN
         L     R14,WTOROUT-4
         BR    R14
         SPACE 10
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              WTOR-ROUTINE                                           *
*                                                                     *
***********************************************************************
         DC    2F'0'
WTORROUT EQU   *
         ST    R14,*-4
         ST    R5,WTORROUT-8
         BAL   R14,WTOFUELL             WTO FUELLEN
         L     R5,WTOECBAD              ECB-ADRESSE IN REG 5
         XC    0(4,R5),0(R5)            ECB LOESCHEN
         WTOR  MF=(E,WTORLIST)          WTOR AUSGEBEN
         WAIT  ECB=(R5)
         L     R5,WTORROUT-8
         L     R14,WTORROUT-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              FUELLEN  WTO(R)-PARAMETER-LISTE                        *
*                                                                     *
***********************************************************************
         DC    3F'0'
WTOFUELL EQU   *
         ST    R14,*-4
         STM   R5,R6,WTOFUELL-12
         LA    R5,47                    STANDARD-TEXT-LAENGE - 1
         BCTR  R1,R0
         LA    R6,TABELLE-1
         CR    R1,R6                    STANDARD-TEXT?
         BNL   WTOF1
         SR    R5,R5
         IC    R5,0(R1)                 TEXTLAENGE IN REG 5
         BCTR  R5,R0
WTOF1    EQU   *
         STC   R5,WTOMVC+1
WTOMVC   MVC   WTOTEXT(0),1(R1)         TEXT UEBERTRAGEN
         AH    R5,=H'21'
         STH   R5,WTOLIST               TEXTLAENGE IN WTO EINSTELLEN
         LA    R6,WTOLIST
         AR    R6,R5
         MVC   0(2,R6),=X'0000'         DESCRIPTOR-CODES
         MVC   2(2,R6),ROUTCODE         ROUTING-CODES
         LM    R5,R6,WTOFUELL-12
         L     R14,WTOFUELL-4
         BR    R14
         SPACE 6
***********************************************************************
*                                                                     *
*              P A R A M E T E R - L I S T E   W T O (R)              *
*                                                                     *
***********************************************************************
WTORLIST DS    0F                       LIST-FORM FUER WTO(R)
WTOANTLG DS    XL1                      ANTWORT-LAENGE
WTOANTAD DC    AL3(ANTWORT)             ANTWORT-ADRESSE
WTOECBAD DC    A(WTORECB)               ECB-ADRESSE
         SPACE 1
WTOLIST  DS    H                        TEXTLAENGE+4
         DC    X'8000'                  MCS-FLAGS
WTOIDENT DC    CL16'**** LABEL **** '   WTO-IDENTIFIER
WTOTEXT  DC    108X'00'                 TEXT
         SPACE 1
ROUTCODE DC    B'1010000000000000'      ROUTING-CODES,DEFAULT=(1,3)
         EJECT
***********************************************************************
*                                                                     *
*              FRAGE AN OPERATOR: WELCHE DATEI GEWUENSCHT?            *
*                                                                     *
***********************************************************************
         DC    F'0'
OPFRAGEN EQU   *                        FRAGE: WELCHER DATASET?
         ST    R14,*-4
         MVC   TEXTWT5+8(6),SERIALNO
         UNPK  TEXTWT5+20(3),DSZAEHL    DATASET-ANZAHL
         OI    TEXTWT5+22,X'F0'
         CLI   LABELTYP,C'N'            NOLABEL-BAND?
         BE    OPFR1
         CLI   LABELTYP,C'D'            DOS-SL-BAND?
         BE    OPFR2
         MVC   TEXTWT5+15(3),=C' SL'
         B     FRAGE1
OPFR1    EQU   *
         MVC   TEXTWT5+15(3),=C' NL'
         B     FRAGE1
OPFR2    EQU   *
         MVC   TEXTWT5+15(3),=C'DOS'
FRAGE1   EQU   *
         MVC   ANTWORT(4),=4X'FF'       HEX FF IN ANTWORTBEREICH
         LA    R1,TEXTWT5               TEXTADRESSE IN REG 1
         MVI   WTOANTLG,X'03'           ANTWORTLAENGE
         BAL   R14,WTORROUT             KONSOLE: WELCHER DATASET
         OI    ANTWORT,C' '
         CLI   ANTWORT,C'W'             ANTWORT: 'WEITER'?
         BE    OPFREND
         TRT   ANTWORT(4),TRTTAB        TEST AUF NUMERISCH
         BZ    ANTFAL
         CLI   0(R1),X'FF'              FF GEFUNDEN?
         BNE   ANTFAL
         LA    R2,ANTWORT+1
         SR    R1,R2                    LAENGENSCHLUESSEL VON DSANTW
         EX    R1,PACK                  PACKEN DSANTW IN DSDOWO
         CP    DSDOWO,=P'1'             DSANTW ZULAESSIG?
         BL    ANTFAL
         CP    DSDOWO,DSZAEHL
         BH    ANTFAL
         CLI   LABELTYP,C'N'            NOLABEL-BAND?
         BE    OPFR4
         CLI   LABELTYP,C'D'            DOS-SL-BAND?
         BE    OPFR3
         BAL   R14,SLKON                KONSOLAUSGABE SL
         B     FRAGE1
OPFR3    EQU   *
         BAL   R14,DOSKON               KONSOLAUSGABE DOS-SL
         B     FRAGE1
OPFR4    EQU   *
         BAL   R14,NLKON                KONSOLAUSGABE NL
         B     FRAGE1
ANTFAL   EQU   *
         LA    R1,TEXTWT2               TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              FALSCHE ANTWORT
         B     FRAGE1
         SPACE 1
PACK     PACK  DSDOWO,ANTWORT(0)        PACK-ANWEISUNG FUER EXEC
         SPACE 1
OPFREND  EQU   *
         L     R14,OPFRAGEN-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              KONSOLAUSGABE STANDARD-LABEL                           *
*                                                                     *
***********************************************************************
         DC    2F'0'
SLKON    EQU   *
         ST    R14,*-4
         ST    R5,SLKON-8
         LA    R9,TABELLE+80
         CP    DSZAEHL,=P'1'
         BE    SLKON1
         CVB   R5,DSDOWO                ADRESSE LABEL BERECHNEN
         SH    R5,=H'1'
         MH    R5,=H'320'
         AR    R9,R5                    ADRESSE DES GEW LABEL IN REG 9
SLKON1   EQU   *
         MVC   SLWTOZL1+24(6),SERIALNO       WTO FUELLEN
         MVC   SLWTOZL1+52(4),FL1FILSQ
         MVC   SLWTOZL2+7(17),FL1ID
         MVC   SLWTOZL2+59(5),FL1CREDT+1
         LA    R9,80(R9)
         CLI   FL2DEN,C'2'
         BNE   SLKON2
         MVC   SLWTOZL1+36(8),TEXT2312
         B     SLKON5
SLKON2   EQU   *
         CLI   FL2DEN,C'3'
         BNE   SLKON3
         MVC   SLWTOZL1+36(8),TEXT2313
         B     SLKON5
SLKON3   EQU   *
         CLI   FL2DEN,C'4'
         BNE   SLKON4
         MVC   SLWTOZL1+36(8),TEXT2314
         B     SLKON5
SLKON4   EQU   *
         MVC   SLWTOZL1+36(8),=8C' '
SLKON5   EQU   *
         MVC   SLWTOZL2+35(17),FL2JSID
         MVC   SLWTOZL3+9(1),FL2RECFM
         CLI   FL2RES+1,C'R'            BLOCKED AND SPANNED RECORDS?
         BE    SLKON52
         MVC   SLWTOZL3+10(1),FL2RES+1
         MVC   SLWTOZL3+11(1),FL2CNTRL
         MVI   SLWTOZL3+12,C' '
         B     SLKON54
SLKON52  EQU   *
         MVC   SLWTOZL3+10(2),=C'BS'    BLOCKED AND SPANNED
         MVC   SLWTOZL3+12(1),FL2CNTRL
SLKON54  EQU   *
         MVC   SLWTOZL3+21(5),FL2LRECL
         MVC   SLWTOZL3+36(5),FL2BLKL
         CLI   LABSCHA,C'H'             NUR HEADER VERLANGT?
         BNE   SLKON6
         MVC   SLWTOZL3+52(6),=C'XXXXXX'
         B     SLWTO
SLKON6   EQU   *
         LA    R9,80(R9)
         MVC   SLWTOZL3+52(6),FL1BLKCT
         CNOP  2,4
SLWTO    EQU   *
         SR    R0,R0                    REGISTER 0 FUER MLWTO LOESCHEN
         WTO   ('**** LABEL **** BAND-NR:      , SL,         , DS-NR:  *
                          **',D),('** DSN=                 , JOB/STEP: *
                               , CRDT:      **',D),('** RECFM=    , LRE*
               CL=     , BLKSIZE=     , BLKCOUNT=             **',DE), *
               ROUTCDE=(1,3)
SLWTOZL1 EQU   SLWTO+10
SLWTOZL2 EQU   SLWTO+89
SLWTOZL3 EQU   SLWTO+160
         L     R5,SLKON-8
         L     R14,SLKON-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              KONSOLAUSGABE DOS-STANDARD-LABEL                       *
*                                                                     *
***********************************************************************
         DC    2F'0'
DOSKON   EQU   *
         ST    R14,*-4
         ST    R5,DOSKON-8
         LA    R9,TABELLE+80
         CP    DSZAEHL,=P'1'
         BE    DOSKON1
         CVB   R5,DSDOWO                ADRESSE LABEL BERECHNEN
         SH    R5,=H'1'
         MH    R5,=H'240'
         AR    R9,R5                    ADRESSE DES GEW. LABEL IN REG 9
DOSKON1  EQU   *
         MVC   DOSWTOZ1+24(6),SERIALNO       WTO FUELLEN
         MVC   DOSWTOZ1+46(4),FL1FILSQ
         MVC   DOSWTOZ1+57(5),FL1CREDT+1
         MVC   DOSWTOZ2+7(17),FL1ID
         LA    R9,80(R9)
         MVC   DOSWTOZ2+34(8),PUFFER+59
         CLI   LABSCHA,C'H'             NUR HEADER VERLANGT?
         BNE   DOSKON2
         MVC   DOSWTOZ2+53(6),=C'XXXXXX'
         B     DOSWTO
DOSKON2  EQU   *
         LA    R9,80(R9)
         MVC   DOSWTOZ2+53(6),FL1BLKCT
         CNOP  2,4
DOSWTO   EQU   *
         SR    R0,R0                    REGISTER 0 FUER MLWTO LOESCHEN
         WTO   ('**** LABEL **** BAND-NR:      , DOS-SL, DS-NR:    , CR*
               DT:      **',D),('** DSN=                 , BLKSIZE:    *
                   , BLKCOUNT:          **',DE),ROUTCDE=(1,3)
DOSWTOZ1 EQU   DOSWTO+10
DOSWTOZ2 EQU   DOSWTO+87
         L     R5,DOSKON-8
         L     R14,DOSKON-4
         BR    R14
         SPACE 10
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              KONSOLAUSGABE  NOLABEL                                 *
*                                                                     *
***********************************************************************
         DC    2F'0'
NLKON    EQU   *
         ST    R14,*-4
         ST    R5,NLKON-8
         LA    R9,TABELLE               ADRESSE TABELLE IN REG 9
         CLI   LTMSCHA,C'Y'             FUEHRENDE TAPEMARK?
         BNE   NLKON1
         LA    R9,80(R9)
NLKON1   EQU   *
         CP    DSZAEHL,=P'1'
         BE    NLKON2
         CVB   R5,DSDOWO                ADRESSE IN TABELLE BERECHNEN
         SH    R5,=H'1'
         MH    R5,=H'80'
         AR    R9,R5                    ADRESSE DER INF. IN REG9
NLKON2   EQU   *
         LA    R1,PUFFER+16             TEXTADRESSE IN REG 1
         BAL   R14,WTOROUT              WTO AUSGEBEN
         L     R5,NLKON-8
         L     R14,NLKON-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D R U C K S T E U E R U N G   S L - B A N D            *
*                                                                     *
***********************************************************************
         DC    F'0'
SLDRUCK  EQU   *                        SL-BAND LABEL DRUCKEN
         ST    R14,*-4
         B     SLDRDS1
ANFSLDR  EQU   *
         BAL   R14,UEBERSCH             UEBERSCHRIFT DRUCKEN
         LA    R9,80(R9)
         BAL   R14,LAB1DRU              HDR1 DRUCKEN
SLDRDS1  EQU   *
         LA    R9,80(R9)
         BAL   R14,LAB2DRU              HDR2 DRUCKEN
         CR    R4,R9                    ENDE DER TABELLE ERREICHT?
         BNH   SLDREND
         LA    R9,80(R9)
         BAL   R14,LAB1DRU              EOF1 BZW EOV1 DRUCKEN
         LA    R9,80(R9)
         BAL   R14,LAB2DRU              EOF2 BZW EOV2 DRUCKEN
         CR    R4,R9
         BH    ANFSLDR
         SPACE 1
SLDREND  EQU   *
         L     R14,SLDRUCK-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D R U C K S T E U E R U N G   D O S - B A N D          *
*                                                                     *
***********************************************************************
         DC    F'0'
DSLDRUCK EQU   *                        DOS-SL-BAND LABEL DRUCKEN
         ST    R14,*-4
         B     DSLDRDS1
ANFDSLDR EQU   *
         BAL   R14,UEBERSCH             UEBERSCHRIFT DRUCKEN
         LA    R9,80(R9)
         BAL   R14,LAB1DRU              HDR1 DRUCKEN
DSLDRDS1 EQU   *
         LA    R9,80(R9)
         BAL   R14,LAB2DRU              BLOCKSIZE AUSDRUCKEN
         CR    R4,R9                    ENDE DER TABELLE ERREICHT?
         BNH   DSLDREND
         LA    R9,80(R9)
         BAL   R14,LAB1DRU              EOF1 BZW EOV1 DRUCKEN
         CR    R4,R9
         BH    ANFDSLDR
         SPACE 1
DSLDREND EQU   *
         L     R14,DSLDRUCK-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D R U C K S T E U E R U N G   N L - B A N D            *
*                                                                     *
***********************************************************************
         DC    2F'0'
NLDRUCK  EQU   *
         ST    R14,*-4
         ST    R5,NLDRUCK-8
         LA    R5,10
         B     NLDR2
NLDR1    EQU   *
         BAL   R14,UEBERSCH             UEBERSCHRIFT DRUCKEN
         LA    R5,10
NLDR2    EQU   *
         MVI   DBER,C'0'                VORSCHUBZEICHEN IN DRUCKBEREICH
         MVI   DBER+1,C' '
         MVC   DBER+2(131),DBER+1       DRUCKBEREICH LOESCHEN
         BAL   R14,DRUCKRT              LEERZEILE DRUCKEN
         MVC   DBER+25(80),PUFFER
         BAL   R14,DRUCKRT              MELDUNG DRUCKEN
         CR    R4,R9
         BE    NLDREND
         LA    R9,80(R9)
         BCT   R5,NLDR2
         B     NLDR1
NLDREND  EQU   *
         L     R5,NLDRUCK-8
         L     R14,NLDRUCK-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              AUFBEREITEN UEBERSCHRIFT                               *
*                                                                     *
***********************************************************************
         DC    F'0'
UEBERSCH EQU   *
         ST    R14,*-4
         AP    SEITENZ,=P'1'            SEITENZAEHLER ERHOEHEN
         MVI   DBER,C' '
         MVC   DBER+1(L'DBER-1),DBER    DRUCKBEREICH LOESCHEN
         MVI   DBER,C'1'                VORSCHUBZEICHEN IN DRUCKBEREICH
         BAL   R14,DRUCKRT              LEERZEILE DRUCKEN
         MVI   DBER,C'-'                VORSCHUBZEICHEN IN DRUCKBEREICH
         MVC   COL110(5),=C'SEITE'
         UNPK  COL110+6(3),SEITENZ
         OI    COL110+8,X'F0'
         BAL   R14,DRUCKRT              LEERZEILE DRUCKEN
         MVI   DBER+1,C' '
         MVC   DBER+2(L'DBER-2),DBER+1  DRUCKBEREICH LOESCHEN
         MVI   COL30,C'*'
         MVC   COL30+1(L'HEADTEXT+3),COL30        STERNZEILE
         BAL   R14,DRUCKRT              STERNZEILE DRUCKEN
         MVI   DBER,C' '                VORSCHUBZEICHEN IN DRUCKBEREICH
         MVI   COL30+1,C' '
         MVC   COL30+2(L'HEADTEXT+1),COL30+1      DBER TEILW. LOESCHEN
         BAL   R14,DRUCKRT              2.ZEILE DRUCKEN
         MVC   COL30+2(L'HEADTEXT),HEADTEXT       KOPFZEILE IN DBER
         CLI   SERSCHA,C'U'             UNBEKANNTE VOL-SER?
         BE    UEBER1
         MVC   COL30-4+L'HEADTEXT(6),JFCBVOLS
UEBER1   EQU   *
         BAL   R14,DRUCKRT              UEBERSCHRIFT DRUCKEN
         MVC   COL30+2(L'HEADTEXT+1),COL30+1      DBER TEILW. LOESCHEN
         BAL   R14,DRUCKRT              3.ZEILE DRUCKEN
         MVC   COL30+1(L'HEADTEXT+3),COL30        STERNZEILE
         BAL   R14,DRUCKRT              STERNZEILE DRUCKEN
         L     R14,UEBERSCH-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              AUFBEREITEN VOL1-KENNSATZ                              *
*                                                                     *
***********************************************************************
         DC    F'0'
VOLDRUCK EQU   *                        AUSDRUCKEN VOLUME LABEL
         ST    R14,*-4
         MVI   DBER,C' '
         MVC   DBER+1(L'DBER-1),DBER    DRUCKBEREICH LOESCHEN
         MVI   DBER,C'0'                VORSCHUBZEICHEN IN DRUCKBEREICH
         BAL   R14,DRUCKRT              LEERZEILE DRUCKEN
         CLI   PUFFER,C'*'
         BNE   SERFEHL
         MVC   COL25(80),PUFFER         FEHLERMELDUNG IN DRUCKBEREICH
         BAL   R14,DRUCKRT              MELDUNG DRUCKEN
         LA    R15,4                    LADEN RETURNCODE
         B     VOLDREND
SERFEHL  EQU   *
         CLI   VOLSCHA,C'F'             BAND FALSCH ETIKETTIERT?
         BNE   SERDRUCK
         MVC   COL20(80),PUFFER         MELDUNG IN DRUCKBEREICH
         BAL   R14,DRUCKRT              MELDUNG DRUCKEN
         LA    R15,0                    LADEN RETURNCODE
         B     VOLDREND
SERDRUCK EQU   *
         MVC   COL20(L'VOLLABI+1),VOLLABI         VOL1 IN DBER
         MVC   COL50(L'VOLTEXT),VOLTEXT
         MVC   COL50+L'VOLTEXT+2(L'VOLSERNO),VOLSERNO
         BAL   R14,DRUCKRT              VOLSERNO DRUCKEN
         LA    R15,0                    LADEN RETURNCODE
VOLDREND L     R14,VOLDRUCK-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              AUFBEREITEN HDR1, EOF1, EOV1 - KENNSATZ                *
*                                                                     *
***********************************************************************
         DC    F'0'
LAB1DRU  EQU   *                        AUSDRUCKEN LABEL TYP 1
         ST    R14,*-4
         MVI   DBER,C' '
         MVC   DBER+1(L'DBER-1),DBER    DRUCKBEREICH LOESCHEN
         MVI   DBER,C'0'                VORSCHUBZEICHEN IN DRUCKBEREICH
         BAL   R14,DRUCKRT              LEERZEILE DRUCKEN
         CLI   PUFFER,C'*'
         BNE   DRUCK11
         MVC   COL25(80),PUFFER         FEHLERMELDUNG IN DRUCKBEREICH
         BAL   R14,DRUCKRT              MELDUNG DRUCKEN
         LA    R15,4                    LADEN RETURNCODE
         B     LAB1END
DRUCK11  EQU   *
         MVC   COL20(L'FL1LABI+1),FL1LABI         1.ZEILE IN DBER
         MVC   COL30(L'TEXT111),TEXT111
         MVC   COL30+L'TEXT111+2(L'FL1ID),FL1ID
         MVC   COL70(L'TEXT112),TEXT112
         MVC   COL70+L'TEXT112+2(L'FL1FILSR),FL1FILSR
         BAL   R14,DRUCKRT              1.ZEILE DRUCKEN
         MVC   DBER+2(L'DBER-2),DBER+1            LOESCHEN DBER
         MVC   COL30(L'TEXT121),TEXT121           2.ZEILE IN DBER
         MVC   COL30+L'TEXT121+2(L'FL1VOLSQ),FL1VOLSQ
         MVC   COL70(L'TEXT122),TEXT122
         MVC   COL70+L'TEXT122+2(L'FL1FILSQ),FL1FILSQ
         BAL   R14,DRUCKRT              2.ZEILE DRUCKEN
         MVC   DBER+2(L'DBER-2),DBER+1            LOESCHEN DBER
         MVC   COL30(L'TEXT131),TEXT131           3.ZEILE IN DBER
         MVC   COL30+L'TEXT131+1(L'FL1CREDT),FL1CREDT
         MVI   COL30+L'TEXT131+1+L'FL1CREDT,C'/'
         LA    R1,FL1CREDT+1
         BAL   R14,DATE0                CREATION DATE UMRECHNEN
         MVC   COL30+L'TEXT131+2+L'FL1CREDT(L'DATUM),DATUM
         MVC   COL70(L'TEXT132),TEXT132
         MVC   COL70+L'TEXT132+1(L'FL1EXPDT),FL1EXPDT
         MVI   COL70+L'TEXT132+1+L'FL1EXPDT,C'/'
         LA    R1,FL1EXPDT+1
         BAL   R14,DATE0                EXPIRATION DATE UMRECHNEN
         MVC   COL70+L'TEXT132+2+L'FL1EXPDT(L'DATUM),DATUM
         BAL   R14,DRUCKRT              3.ZEILE DRUCKEN
         MVC   DBER+2(L'DBER-2),DBER+1            LOESCHEN DBER
         MVC   COL30(L'TEXT141),TEXT141           4.ZEILE IN DBER
         MVC   COL30+L'TEXT141+2(L'FL1SYSCD),FL1SYSCD
         CLC   FL1LABI,=C'HDR'
         BE    DRUCK14
         MVC   COL70(L'TEXT142),TEXT142
         MVC   COL70+L'TEXT142+2(L'FL1BLKCT),FL1BLKCT
DRUCK14  EQU   *
         BAL   R14,DRUCKRT              4.ZEILE DRUCKEN
         MVC   DBER+2(L'DBER-2),DBER+1            LOESCHEN DBER
         MVC   COL30(L'TEXT151),TEXT151           5.ZEILE IN DBER
         CLI   FL1FSEC,C'0'
         BNE   DRUCK151
         MVC   COL30+L'TEXT151+2(L'TEXT1510),TEXT1510
         B     DRUCK15
DRUCK151 EQU   *
         CLI   FL1FSEC,C'1'
         BNE   DRUCK153
         MVC   COL30+L'TEXT151+2(L'TEXT1511),TEXT1511
         B     DRUCK15
DRUCK153 EQU   *
         CLI   FL1FSEC,C'3'
         BNE   DRUCK15
         MVC   COL30+L'TEXT151+2(L'TEXT1513),TEXT1513
DRUCK15  EQU   *
         BAL   R14,DRUCKRT              5.ZEILE DRUCKEN
         LA    R15,0                    LADEN RETURNCODE
LAB1END  EQU   *
         L     R14,LAB1DRU-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              AUFBEREITEN HDR2, EOF2, EOV2 - KENNSATZ                *
*                                                                     *
***********************************************************************
         DC    F'0'
LAB2DRU  EQU   *                        AUSDRUCKEN LABEL TYP 2
         ST    R14,*-4
         MVI   DBER,C' '
         MVC   DBER+1(L'DBER-1),DBER    DRUCKBEREICH LOESCHEN
         MVI   DBER,C'0'                VORSCHUBZEICHEN IN DRUCKBEREICH
         BAL   R14,DRUCKRT              LEERZEILE DRUCKEN
         CLI   PUFFER,C'*'              DOS-MELDUNG?
         BNE   DRUCK2
         MVC   COL25(80),PUFFER         DOS-MELDUNG IN DRUCKBEREICH
         BAL   R14,DRUCKRT              MELDUNG DRUCKEN
         B     LAB2END
DRUCK2   EQU   *
         MVC   COL20(L'FL1LABI+1),FL1LABI         1.ZEILE IN DBER
         MVC   COL30(L'TEXT211),TEXT211
         MVC   COL30+L'TEXT211+2(1),FL2RECFM
         CLI   FL2RES+1,C'R'            BLOCKED AND SPANNED RECORDS?
         BE    DRUCK202
         MVC   COL30+L'TEXT211+3(1),FL2RES+1
         MVC   COL30+L'TEXT211+4(1),FL2CNTRL
         MVI   COL30+L'TEXT211+5,C' '
         B     DRUCK204
DRUCK202 EQU   *
         MVC   COL30+L'TEXT211+3(2),=C'BS'       BLOCKED AND SPANNED
         MVC   COL30+L'TEXT211+5(1),FL2CNTRL
DRUCK204 EQU   *
         CLI   FL2RECFM,C'F'
         BNE   DRUCK21
         CLI   FL2RES+1,C'B'
         BE    DRUCK206
         CLI   FL2RES+1,C'R'
         BNE   DRUCK21
DRUCK206 EQU   *
         PACK  RF1,FL2BLKL
         PACK  RF2,FL2LRECL
         ZAP   RF2,RF2
         BZ    DRUCK21
         DP    RF1,RF2
         MVC   COL70(L'TEXT212),TEXT212
         UNPK  COL70+L'TEXT212+2(5),RF1(L'RF2)
         OI    COL70+L'TEXT212+6,X'F0'
DRUCK21  EQU   *
         BAL   R14,DRUCKRT              1.ZEILE DRUCKEN
         MVC   DBER+2(L'DBER-2),DBER+1            LOESCHEN DBER
         MVC   COL30(L'TEXT221),TEXT221           2.ZEILE IN DBER
         MVC   COL30+L'TEXT221+2(L'FL2BLKL),FL2BLKL
         MVC   COL70(L'TEXT222),TEXT222
         MVC   COL70+L'TEXT222+2(L'FL2LRECL),FL2LRECL
         BAL   R14,DRUCKRT              2.ZEILE DRUCKEN
         MVC   DBER+2(L'DBER-2),DBER+1            LOESCHEN DBER
         MVC   COL30(L'TEXT231),TEXT231           3.ZEILE IN DBER
         CLI   FL2DEN,C'2'
         BNE   DRU2313
         MVC   COL30+L'TEXT231+2(L'TEXT2312),TEXT2312
         B     DRUCK231
DRU2313  EQU   *
         CLI   FL2DEN,C'3'
         BNE   DRU2314
         MVC   COL30+L'TEXT231+2(L'TEXT2313),TEXT2313
         B     DRUCK231
DRU2314  EQU   *
         CLI   FL2DEN,C'4'
         BNE   DRUCK231
         MVC   COL30+L'TEXT231+2(L'TEXT2314),TEXT2314
DRUCK231 EQU   *
         MVC   COL70(L'TEXT232),TEXT232
         MVC   COL70+L'TEXT232+2(L'FL2JSID),FL2JSID
         BAL   R14,DRUCKRT              3.ZEILE DRUCKEN
         CLC   FL1LABI,=C'EOV'
         BNE   LAB2END
         MVC   DBER+2(L'DBER-2),DBER+1            LOESCHEN DBER
         BAL   R14,DRUCKRT              LEERZEILE DRUCKEN
         MVC   COL30(L'TEXT3),TEXT3
         BAL   R14,DRUCKRT              FOLGEBAND-MELDUNG DRUCKEN
LAB2END  EQU   *
         L     R14,LAB2DRU-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              AUFBEREITEN ANZAHL DER DATEIEN                         *
*                                                                     *
***********************************************************************
         DC    F'0'
DSMDRUCK EQU   *
         ST    R14,*-4
         MVI   DBER,C' '
         MVC   DBER+1(L'DBER-1),DBER    DRUCKBEREICH LOESCHEN
         MVI   DBER,C'0'                VORSCHUBZEICHEN IN DRUCKBEREICH
         BAL   R14,DRUCKRT              LEERZEILE DRUCKEN
         MVC   COL47(L'DSMTEXT),DSMTEXT DRUCKBEREICH FUELLEN
         CLI   DSSCHA,C'W'              MEHR ALS 50,70,200 DATASETS
         BE    DSMDR1
         UNPK  COL47+L'DSMTEXT+2(3),DSZAEHL       DS-ANZAHL IN DBER
         OI    COL47+L'DSMTEXT+4,X'F0'
         MVC   COL47+L'DSMTEXT+6(L'DSMTEXT1),DSMTEXT1
         B     DSMDR5
DSMDR1   EQU   *
         MVC   COL47+L'DSMTEXT+2(L'DSMTEXT2),DSMTEXT2
         CLI   LABELTYP,C'N'            NOLABEL-BAND?
         BE    DSMDR3
         CLI   LABELTYP,C'D'            DOS-SL-BAND?
         BE    DSMDR2
         MVC   COL47+L'DSMTEXT+3+L'DSMTEXT2(3),=C' 50'
         B     DSMDR4
DSMDR2   EQU   *
         MVC   COL47+L'DSMTEXT+3+L'DSMTEXT2(3),=C' 70'
         B     DSMDR4
DSMDR3   EQU   *
         MVC   COL47+L'DSMTEXT+3+L'DSMTEXT2(3),=C'200'
DSMDR4   EQU   *
         MVC   COL47+L'DSMTEXT+7+L'DSMTEXT2(L'DSMTEXT1),DSMTEXT1
DSMDR5   EQU   *
         BAL   R14,DRUCKRT              DATASET-ANZAHL DRUCKEN
         L     R14,DSMDRUCK-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              DRUCKEN ZEILE                                          *
*                                                                     *
***********************************************************************
         DC    F'0'
DRUCKRT  EQU   *                        DRUCKEN DBER
         ST    R14,*-4
         PUT   DRUCKER,DBER
         L     R14,DRUCKRT-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              U  N  T  E  R  R  O  U  T  I  N  E                     *
*                                                                     *
*              DATE0-MAKRO: AUFBEREITEN DATUM                         *
*                                                                     *
***********************************************************************
         DC    F'0'
DATE0    EQU   *
         ST    R14,*-4
         DATE0 DATUM,0(R1)
         L     R14,DATE0-4
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*              D  E  F  I  N  I  T  I  O  N  E  N                     *
*                                                                     *
***********************************************************************
OUTSCHA  DC    C'2'                     SCHALTER F. IN-OUTPUT-STEUERUNG
*        1:    OUTPUT: DRUCKER, VOL-SER-INP: NEIN, KEIN WIEDERANLAUF
*        2:    OUTPUT: DRUCKER, VOL-SER-INP: JCL , KEIN WIEDERANLAUF
*        3:    OUTPUT: DRUCKER, VOL-SER-INP: KONS, WIEDERANLAUF
*        4:    OUTPUT: KONSOLE, VOL-SER-INP: KONS, WIEDERANLAUF
LABSCHA  DC    C'H'                     LABEL-LESESTEUERUNG
*        H:    NUR DIE HEADER DER ERSTEN DATEI WERDEN GELESEN
*        A:    ALLE LABEL (GILT BEI DRUCKOUTPUT FUER DEN GANZEN LAUF)
*        B:    ALLE LABEL (GILT BEI DRUCKOUTPUT FUER DAS EINE BAND)
VOLSCHA  DC    C'R'                     SCHALTER F. BANDETIKETTIERUNG
*        R:    BAND IST RICHTIG ETIKETTIERT
*        F:    BAND IST FALSCH ETIKETTIERT
LABELTYP DC    C'S'                     SCHALTER FUER LABELTYP
*        S:    STANDARD-LABEL
*        D:    DOS-STANDARD-LABEL
*        N:    NOLABEL
DSSCHA   DC    C'A'                     SCHALTER FUER DATASET-ANZAHL
*        A:    DIE TABELLE REICHTE FUER ALLE LABEL AUS
*        W:    TABELLE ZU KLEIN, NICHT ALLE LABEL GELESEN
LTMSCHA  DC    C'N'                     SCHALTER FUER FUEHRENDE TAPEMK
*        N:    KEINE FUEHRENDE TAPEMARK BEI NOLABEL
*        Y:    FUEHRENDE TAPEMARK BEI NOLABEL
*        F:    FEHLERBEDINGUNG BEI NOLABEL
SERSCHA  DC    C'B'                     SCHALTER FUER UNBEK. VOL-SER
*        B:    VOLUME-SERIAL-NUMBER EINGEGEBEN (JCL ODER KONSOLE)
*        U:    VOLUME-SERIAL-NUMBER UNBEKANNT
NLSTOP   DC    C'W'                     SCHALTER FUER LESESTOP NL
*        W:    BAND NOCH NICHT ZU ENDE, WEITERLESEN
*        S:    BAND ZU ENDE, STOP, FOLGEBAND-MELDUNG
ANTWORT  DS    CL35                     ANTWORTBEREICH FUER WTOR
ANTWORT2 DS    CL1                      ANTWORTBEREICH FUER 'NONALPHA'
IDENTIFY DS    CL4                      TAPE-LABEL-IDENTIFIER
DSDOWO   DS    D                        DOPPELWORT FUER CVB, CVD
SERIALNO DS    CL6                      SPEICHER FUER VOL-SER
SERIAL   DS    CL6                      SPEICHER FUER VOL-SER
DATUM    DS    CL8                      AUSGABEFELD FUER DATE0
RF1      DS    CL6                      RECHENFELD  FUER DP
RF2      DS    CL3                      RECHENFELD  FUER DP
SEITENZ  DC    PL2'0'                   SEITENZAEHLER
DSZAEHL  DC    PL2'0'                   DATASET-ZAEHLER
BLKSIZE  DS    CL8                      BLOCKGROESSE BEI NL-DATEI
BLOCKZHL DS    F                        ANZAHL GEPRUEFTER BLOECKE (NL)
LAENGTAB DS    10H                      RESTCOUNT-TABELLE
SENSEBYT DS    CL5                      ERSTE FUENF SENSEBYTES
         EJECT
DBER     DC    CL133' '                 DRUCKBEREICH
COL20    EQU   DBER+20
COL25    EQU   DBER+25
COL30    EQU   DBER+30
COL47    EQU   DBER+47
COL50    EQU   DBER+50
COL70    EQU   DBER+70
COL110   EQU   DBER+110
         SPACE 2
TRTTAB   DC    240X'FF',10X'00',6X'FF'  TABELLE FUER TRT
TRTTAB2  DC    91X'FF',X'00',4X'FF',X'00',26X'FF',2X'00',68X'FF',9X'00'*
               ,7X'FF',9X'00',8X'FF',8X'00',6X'FF',10X'00',6X'FF'
         SPACE 2
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*              K  O  N  T  R  O  L  L  B  L  O  E  C  K  E            *
*                                                                     *
***********************************************************************
KONAREA  DS    0F                       BEREICH FUER KONTROLLBLOECKE
JFCBAREA DS    CL176                    JFCB-BEREICH
BANDECB  DC    F'0'                     ECB FUER EXCP
BANDIOB  DS    0CL40                    IOB FUER EXCP
         DC    X'02'                    FLAG-BYTE 1
         DC    XL4'00'
         DC    AL3(BANDECB)             ECB-ADRESSE
         DC    XL9'00'
         DC    AL3(LABELCCW)            CCW-ADRESSE
         DC    X'00'
         DC    AL3(BAND)                DCB-ADRESSE
         DC    XL16'00'
LABELCCW DS    D
READCCW  CCW   X'02',TABELLE,X'00',80   CCW ZUM LESEN
SENSECCW CCW   X'04',SENSEBYT,X'20',5   CCW FUER SENSE
PRUEFCCW CCW   X'02',*,X'30',32760      CCW ZUR LAENGENPRUEFUNG
BACKCCW  CCW   X'27',*,X'10',1          CCW ZUM ZURUECKSETZEN
SKIPCCW  CCW   X'37',*,X'40',1          CCW'S FUER SKIPFILE
         CCW   X'37',*,X'40',1
         CCW   X'37',*,X'40',1
         CCW   X'37',*,X'40',1
         CCW   X'37',*,X'40',1
         CCW   X'37',*,X'40',1
         CCW   X'37',*,X'40',1
         CCW   X'37',*,X'40',1
         CCW   X'37',*,X'40',1
         CCW   X'37',*,X'00',1
EXLIST07 DS    0F                       EXIT-LIST FUER RDJFCB
         DC    X'07'
         DC    AL3(JFCBAREA)
         DC    X'80'                    ENDE DER EXIT-LIST
WTORECB  DC    F'0'                     ECB FUER WTOR
         SPACE 2
BAND     DCB   DDNAME=BAND,MACRF=(E),EXLST=EXLIST07,                   *
               DSORG=PS,IOBAD=BANDIOB,DEVD=TA,OPTCD=Z
         SPACE 2
DRUCKER  DCB   DDNAME=DRUCKER,DSORG=PS,DEVD=PR,LRECL=133,BLKSIZE=133,  *
               MACRF=(PM),RECFM=FBA
         EJECT
***********************************************************************
*                                                                     *
*              A  U  S  G  A  B  E  -  T  E  X  T  E                  *
*                                                                     *
***********************************************************************
MELDG3   DC    CL80'*************** BAND-NR:      ,NOLAB,DS-NR:   ,BLKS*
               IZE=          ***************'
MELDG4   DC    CL80'VOL1      ***** FALSCHE VOLSERNO, ETIKETT:       , *
               LABEL:        ***************'
MELDG5   DC    CL80'*************** BAND NR       , BANDORGANISATION FE*
               HLERHAFT,DUMP ***************'
MELDG6   DC    CL80'*************** BAND NR       , LEERES, INITIALISIE*
               RTES SL-BAND  ***************'
MELDG7   DC    CL80'*************** DOS-SL-BAND, KEIN HDR2, ERMITTELTE *
               BLKSIZE=         ************'
MELDG8   DC    CL80'***** REGISTER 4 ENTHAELT DIE ADRESSE DER INSTRUKTI*
               ON, DIE DEN FEHLER FESTSTELLT'
MELDG9   DC    CL80'*************** BAND NR        HAT EINE FUEHRENDE T*
               APEMARK       ***************'
MELDG10  DC    CL80'*************** BAND NR        BESITZT EIN FOLGEBAN*
               D             ***************'
MELDG11  DC    CL80'*************** BAND NR       , PHYSISCHER FEHLER I*
               M      , DUMP ***************'
         SPACE 2
HEADTEXT DC    C'TAPE-LABEL-INFORMATIONEN FUER DAS BAND MIT DER ETIKETT*
               NUMMER ......'
DSMTEXT  DC    C'***** ANZAHL DER DATASETS:'
DSMTEXT1 DC    C'*****'
DSMTEXT2 DC    C'MEHR ALS'
VOLTEXT  DC    C'VOLUME SERIAL NUMBER:'
TEXT111  DC    C'DATA SET NAME:'
TEXT112  DC    C'DER DATASET BEGINNT AUF VOL.:'
TEXT121  DC    C'VOLUME SEQUENCE NUMBER:'
TEXT122  DC    C'DATA SET SEQUENCE NUMBER:'
TEXT131  DC    C'CREATION DATE:'
TEXT132  DC    C'EXPIRATION DATE:'
TEXT141  DC    C'SYSTEM CODE:'
TEXT142  DC    C'BLOCK COUNT:'
TEXT151  DC    C'DATA SECURITY:'
TEXT1510 DC    C'KEIN PASSWORD ERFORDERLICH'
TEXT1511 DC    C'PASSWORD ERFORDERLICH ZUM LESEN, SCHREIBEN, LOESCHEN'
TEXT1513 DC    C'PASSWORD ERFORDERLICH ZUM SCHREIBEN UND LOESCHEN'
TEXT211  DC    C'RECORD FORMAT:'
TEXT212  DC    C'BLOCKUNGSFAKTOR:'
TEXT221  DC    C'BLOCKLAENGE:'
TEXT222  DC    C'LOGISCHE SATZLAENGE:'
TEXT231  DC    C'ZEICHENDICHTE:'
TEXT2312 DC    C' 800 BPI'
TEXT2313 DC    C'1600 BPI'
TEXT2314 DC    C'6250 BPI'
TEXT232  DC    C'JOB/STEP:'
TEXT3    DC    C'ES EXISTIERT EIN FOLGEBAND'
         SPACE 2
         DC    AL1(L'TEXTWT1)
TEXTWT1  DC    C'BAND NR       , LISTE GEWUENSCHT?    Y FUER JA, N FUER*
                NEIN EINGEBEN'
         DC    AL1(L'TEXTWT2)
TEXTWT2  DC    C'FALSCHE ANTWORT'
         DC    AL1(L'TEXTWT3)
TEXTWT3  DC    C'RICHTIGES BAND AUF UNIT     ? VERLANGT:        , ERMIT*
               TELT:       '
         DC    AL1(L'TEXTWT4)
TEXTWT4  DC    C'Y FUER RICHTIGES, N FUER FALSCHES BAND, ODER W FUER WE*
               ITER EINGEBEN'
         DC    AL1(L'TEXTWT5)
TEXTWT5  DC    C'BAND NR       ,   ,     DATEIEN.     W FUER WEITER ODE*
               R DATEI-NR=XXX EINGEBEN'
         DC    AL1(L'TEXTWT6)
TEXTWT6  DC    C'DAS BAND HAT DIE VOLSERNO       '
         DC    AL1(L'TEXTWT7)
TEXTWT7  DC    C'AUFLISTUNG ALLER LABEL AUF DRUCKER'
         DC    AL1(L'TEXTWT8)
TEXTWT8  DC    C'BAND NR       ,NL,NUR EINE BZW DIE ERSTE DATEI? Y FUER*
                JA, N FUER NEIN EINGEBEN'
         DC    AL1(L'TEXTWT9)
TEXTWT9  DC    C'VOL-SER-ANGABE FEHLT IN JCL'
         DC    AL1(L'TEXTWT10)
TEXTWT10 DC    C'S FUER STOP, ? FUER UNBEKANNTE VOL SER, ODER BIS ZU 5 *
               VOL-SER=XXXXXX,XXXXXX,... EINGEBEN'
         DC    AL1(L'TEXTWT11)
TEXTWT11 DC    C'IST DIE VOL-SER:        IN ORDNUNG? Y FUER JA, N FUER *
               NEIN EINGEBEN'
         DC    AL1(L'TEXTWT12)
TEXTWT12 DC    C'BAND NR       , A FUER ALLE LABEL ODER H FUER HEADER E*
               INGEBEN'
         EJECT
***********************************************************************
*                                                                     *
*              T A B E L L E   F U E R   K E N N S A E T Z E          *
*                                                                     *
***********************************************************************
         DS    0D
         DC    C'****************  LABEL-TABELLE ****************'
TABELLE  DS    CL17060                  TABELLE FUER TAPE-LABEL
         SPACE 10
***********************************************************************
*                                                                     *
*              DUMMY-SECTION FUER LABEL UND KONTROLLBLOECKE           *
*                                                                     *
***********************************************************************
         DSECT
PUFFER   DS    0CL80
KONDSECT EQU   PUFFER+100
         IECDSECT
         SPACE 2
         END
