YZ011A00 CSECT
         PRINT NOGEN
         SPACE
*              ***************************************************
*              *                                                 *
*              *    AUSSUCHEN  VON  DATENSAETZEN  AUS  EINEM     *
*              *    DATENBESTAND  ANHAND  VON  MERKMALEN  DIE    *
*              *    ALS  STEUERKARTEN  IN  DAS  PROGRAMM         *
*              *    EINGEHEN.                                    *
*              *                                                 *
*              *                                                 *
*              *    AUFBAU  DER  INTERNE  SUCH  TABELLE          *
*              *                                                 *
*              *    2  STELLEN  BINAR  MASCHINE  DISP  IN SATZ   *
*              *    1  STELLE   BINAR  MASCHINE  LAENGE  FELD    *
*              *    1  STELLE   CHAR   MASCHINE  OP CODE         *
*              *    1  STELLE   CHAR   MASCHINE  BEDIENUNG       *
*              *    X  STELLEN         KONSTANTE                 *
*              *                                                 *
*              *    FALLS  DER  TABELLEN  GROSE  NICHT  AUSREICHT*
*              *    KANN  UBER DEN  PARM  EINTRAGUNG  DER        *
*              *    BEREICH  VERGROSSERT                         *
*              *                                                 *
*              *   BEI  PROGRAMM AENDERUNGS  WUNSCHE             *
*              *   ODER FEHLER  BITTE H.  DAVER TEL 6772         *
*              *   ANRUEFFEN                                     *
*              *                                                 *
*              *                                                 *
*              ***************************************************
         EJECT
         SAVE  (14,12),,YZ011A25
         LR    3,15
         USING YZ011A00,3,4
         LA    4,4000(3)
         LA    4,96(4)
         LA    2,SAVE
         ST    13,SAVE+4
         ST    2,8(13)
         LR    13,2
         USING SAVE,13
         B     B10000
         CNOP  0,8
         SPACE 3
E0       DC    X'05'
         DC    AL3(B90000)
E1       DC    X'87'
         DC    AL3(JFCB)
E2       DC    X'05'
         DC    AL3(B91000)
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
         SPACE 3
EFRD     DCB   DDNAME=SYSIN,MACRF=GM,DSORG=PS,EODAD=B70900,EXLST=E1
EFX      DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=B49000,EXLST=E0,  *
               BFTEK=A
EFXIS    DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=IS,EODAD=B49000,EXLST=E1
AFX      DCB   DDNAME=SYSUT2,MACRF=PL,DSORG=PS,EXLST=E2,BFTEK=A
AFXIS    DCB   DDNAME=SYSUT2,MACRF=PL,DSORG=IS,EXLST=E2
AFX1     DCB   DDNAME=SYSUT3,MACRF=PL,DSORG=PS,EXLST=E2,BFTEK=A
AFX1IS   DCB   DDNAME=SYSUT3,MACRF=PL,DSORG=IS,EXLST=E2
AFD      DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=FBM,LRECL=133,  *
               BLKSIZE=1330,EXLST=E1
AFD1     DCB   DDNAME=SYSTERM,MACRF=PM,DSORG=PS,RECFM=FM,LRECL=133,    *
               BLKSIZE=133,EXLST=E1
         EJECT
B10000   L     5,0(1)                  RETTEN  ADRS  PARM
         OPEN  (AFD,(OUTPUT))
         TM    AFD+48,X'10'
         BZ    B10070
         RDJFCB EFX
         LTR   15,15
         BNZ   B10070
         MVC   KOPF2+22(40),JFCB
         TM    JFCB+98,X'80'           DSORG  IST
         BZ    B10010                  NICHT  IS  BEI SYSUT1
         LA    2,EFXIS
         ST    2,ADRIN                 RETTEN ADR  RICHTIGE  DCB SYSUT1
         SPACE
B10010   L     2,ADRIN                 LADEN  ADR  RICHTIGE  DCB SYSUT1
         OPEN  ((2))
         LH    14,82(2)
         ST    14,LRECL                RETTEN  LRECL
         RDJFCB AFD1
         LTR   15,15
         BNZ   B10030
         SPACE
         OPEN  (AFD1,OUTPUT)
         LA    15,AFD1
         ST    15,ADRLIST
         SPACE
B10030   L     1,ADRLIST
         PUT   (1),W1BER
         MVI   W1BER,X'11'
         MVC   W1BER+11(23),=C'CONTROL  CARDS  LISTING'
         L     1,ADRLIST
         PUT   (1),W1BER
         MVI   W1BER,X'09'
         CALL  DATUM,(JFCB,JFCB+6,JFCB+12),VL
         MVC   HDR1(2),JFCB+12         STUNDEN
         MVC   HDR1+3(2),JFCB+14       MINUTEN
         MVC   HDR1+7(2),JFCB          TAG
         MVC   HDR1+10(2),JFCB+2       MONAT
         MVC   HDR1+13(2),JFCB+4       JAHR
         RDJFCB EFRD
         LTR   15,15
         BNZ   B10900
         OPEN  EFRD
         SPACE
B10040   CLI   1(5),X'00'
         BE    B10050                  KEINE  PARM  GETMAIN MIT
         CLC   2(6,5),=C'ONLINE '
         BNE   B10045
         MVI   SCH,C'1'
         B     B10050
B10045   SR    7,7
         IC    7,1(5)
         BCTR  7,0
         LA    5,2(5)
         L     15,ANUM
         EX    7,NUMER
         BNZ   B10050
         EX    7,B10060
         SPACE
B10050   CVB   0,TABL
         LR    5,0
         GETMAIN R,LV=(0)
         ST    1,ATAB
         ST    1,ATABEND
         AR    1,5
         ST    1,TABL
         B     B10100
         SPACE
B10060   PACK  TABL,0(0,5)
         SPACE
B10070   ABEND 013
         EJECT
*                   LESEN  UND  VERARBEITUNG  VON  SYSIN
         SPACE 3
B10100   BAL   12,B70000               LESEN
         NOP   B10120
         OI    *-3,X'F0'
         CLC   0(6,5),=C'ONLINE'
         BNE   B10120
         MVI   SCH,C'1'
         B     B10100
         SPACE 3
B10120   CLC   0(6,5),=C'PRINT='
         BE    PRINT
         CLC   0(8,5),=C'STOPAFT='
         BE    STOP
         CLC   0(5,5),=C'SKIP='
         BE    SKIP
         CLC   0(9,5),=C'STARTAFT='
         BE    START
         CLC   0(6,5),=C'TITLE='
         BE    TITLE
         CLC   0(7,5),=C'SEARCH '
         BE    B10230
         CLC   0(7,5),=C'CANCEL '
         BE    CANCEL
         CLC   0(8,5),=C'LINECNT='
         BE    LINE
         CLC   0(5,5),=C'INFO '
         BE    INFO
         CLC   0(5,5),=C'INFO,'
         BE    INFO
         CLC   0(7,5),=C'MAXOFL='
         BE    MAX
         CLI   0(5),C' '
         BE    B10100                  FOLGE  KARTE  VORHANDEN
         SPACE
         BAL   12,FEHL01               FALSCHE KEYWORT
         LA    5,1(5)                   NACHSTE BYTE IN KEY WORT
         L     15,ADEL
         TRT   0(80,5),0(15)
         BZ    B10100                   KEIN KOMMA NEUE KARTE
         LA    6,W1BER+82               ADDR SPALTE 72
         LA    5,1(1)                   ADDR BYTE NACH KOMMA
         CR    5,6
         BL    B10120                   SUCHEN NEUE KEYWORT
         B     B10100
         SPACE
B10130   LA    5,W1BER+83              ADR  LETZTE  ZULASSIGE  BYTE +1
         CR    6,5
         BL    B10100
         BAL   12,FEHL06
         B     B10100
         EJECT
B10200   BAL   12,B70000               LESEN
         CLC   0(7,5),=C'SEARCH '
         BE    B10210
         CLC   0(7,5),=C'CANCEL '
         BE    CANCEL
         BAL   12,FEHL04
         B     B10200
         SPACE
B10210   L     11,ATABEND
         MVI   0(11),C'O'
         LA    11,1(11)
         ST    11,ATABEND
         SPACE
B10230   LA    5,7(5)
         OI    CON02+1,X'F0'           DEFAULT  TEXT  AUS
         SPACE
B10240   CLI   0(5),C' '
         BNE   B10250
         LA    5,1(5)
         B     B10240
         SPACE
B10250   CLI   0(5),C'('
         BNE   B10255
         L     11,ATABEND
         MVI   0(11),C'('
         LA    11,1(11)
         ST    11,ATABEND
         MVI   BED,C'O'
         AP    ZKL,=P'1'
         LA    5,1(5)
         SPACE
B10255   CLC   0(7,5),=C'FIELD=('
         BE    FIELD
         CLC   0(7,5),=C'CANCEL '
         BE    CANCEL
         BAL   12,FEHL05
         B     B10290
         SPACE
B10260   CLI   0(5),C' '               ZURUECK VON FIELD R5  ADR NACH )
         BE    B10310
         CLI   0(5),C','
         BE    B10270
         BAL   12,FEHL06
         B     B10290
         SPACE
B10270   L     11,ATABEND
         MVC   0(1,11),BED
         LA    11,1(11)
         ST    11,ATABEND
         LA    5,1(5)
         LA    6,W1BER+83
         CR    5,6
         BL    B10280
         BE    B10290
         BAL   12,FEHL02
         B     B10290
         SPACE
B10280   CLI   0(5),C' '
         BE    B10290
         B     B10250
         SPACE
B10290   LA    14,B10300
         ST    14,ADRATEND
         BAL   12,B70000               LESEN
         LA    14,B10900
         ST    14,ADRATEND
         B     B10240
         SPACE
B10300   BAL   12,FEHL07
         CLI   SCH,C'1'
         BNE   B10900                  KEIN ONLINE
         LA    12,B10290
         B     B10950
         SPACE
B10310   CP    ZKL,=P'0'
         BE    B10200                  (   GLEICH   )
         BAL   12,FEHL15
         CLI   SCH,C'1'
         BNE   B10200                  KEIN  ONLINE
         SPACE
B10320   BAL   12,B70000               LESEN
         L     11,ATABEND
         B     FLD915
         EJECT
B10900   MVC   W1BER+11(80),BLK132
         SPACE
CON02    NOP   B10910                  B  WENN  SEARCH  VORHANDEN
         L     1,ADRLIST
         PUT   (1),W1BER
         MVC   W1BER+11(17),=C'N0  SEARCH  CARDS'
         L     1,ADRLIST
         PUT   (1),W1BER
         MVC   W1BER+11(31),=C'ALL  RECORDS  WILL  BE  WRITTEN'
         OI    CON08+1,X'F0'           SEARCH  ROUTINE  AUSSCHALTEN
         SPACE
B10910   CP    ZKL,=P'0'
         BZ    B10920
         BAL   12,FEHL15
         CLI   SCH,C'1'
         BNE   B10920                  KEIN ONLINE
         LA    12,B10320
         B     B10950
         SPACE
B10920   L     1,ADRLIST
         PUT   (1),W1BER
         MVC   W1BER+11(80),BLK132
         MVC   W1BER+11(14),=C'END  OF  SYSIN'
         L     1,ADRLIST
         PUT   (1),W1BER
         MVC   W1BER+11(14),BLK132
         MVI   W1BER,X'09'
         TM    EFRD+48,X'10'
         BZ    B10930
         CLOSE EFRD
         FREEPOOL EFRD
B10930   EQU   *
         CLI   SCH,C'1'
         BE    B11000                  ONLINE
CON03    B     B11000                  NOP BEI FEHLER IN CONTROL CARD
         L     2,ADRIN
         CLOSE ((2),,AFD)
         ABEND 2222
         SPACE
B10950   CLOSE EFRD
         FREEPOOL EFRD
         OPEN  EFRD
         BR    12
         SPACE 3
PACK     PACK  JFCB(8),0(0,5)
NUMER    TRT   0(0,5),0(15)
         EJECT
CANCEL   L     2,ADRIN
         CLOSE ((2))
         LA    15,16
         ST    15,CONDCODE
         B     B49200
         EJECT
*
*               EINGANG   R5   1. ZIFFER
*                         JFCB+8(4)  MAX ANZAHL ZIFFEREN
*                         JFCB+12(4) MIN VALUE
*
DATACHK  L     15,ANUM
         SR    2,2
         TRT   0(80,5),0(15)
         B     *(2)
         B     DATA1                    COMMA/BLANK
         B     DATA2                    KEIN ZIFFER
         SPACE
DATA1    LA    6,0(1)                   CLEAR HIGH ORDER BYTE
         LA    7,0(1)
         SR    7,5                      LAENGE DATA
         BNP   DATA3                    KEINE DATA
         C     7,JFCB+8
         BH    DATA3                    ZU LANG
         BCTR  7,0
         EX    7,PACK
         CP    JFCB+4(4),JFCB+12(4)
         BL    DATA3                    NICHT MINDEST VALUE
         LA    15,W1BER+82              SPALTE 72 IN EINGABE
         CR    6,15
         BCR   4,12                     BL
         LA    12,B10100
         B     FEHL06                   NEUE KARTE LESEN
         SPACE
DATA2    L     15,ADEL
         MVI   64(15),X'FF'          AUCH BLANCK ALS DELIMITER
         L     7,1(1)                   BYTE NACH NICHT ZIFFER
         TRT   0(80,7),0(15)
         MVI   64(15),X'00'             RESET TAB
         SPACE
DATA3    LA    6,0(1)                   ADDR DELIMITER
         ST    12,JFCB+16               SAVE 12
         BAL   12,FEHL03
         L     12,JFCB+16               RESTORE 12
         CLI   SCH,C'1'                 ONLINE ?
         BE    B10100                   JA
         BR    12
         EJECT
INFO     NOP   FEHL01                   BAL 12 NACH 1. DURCHLAUF
         OI    CON04A+1,X'F0'           AKTIVIERN INFO
         OI    CON11A+1,X'F0'           AKTIVIERN INFOÖG
         MVC   INFO(2),=X'45C0'
         LA    6,4(5)
         CLI   0(6),C' '
         BE    B10130                   KEIN KEY WORT MEHR
         LA    5,1(6)
         B     B10120                   NACHSTE KEY WORT
         EJECT
FEHL01   MVC   W2BER+92(30),=C'01  INVALID/DUPLICATE  KEYWORD'
         B     FEHL90
         SPACE
FEHL02   MVC   W2BER+92(28),=C'02  DATA  BEYOND  COLUMN  72'
         B     FEHL90
         SPACE
FEHL03   MVC   W2BER+92(30),=C'03  INVALID  DATA  IN  KEYWORD'
         B     FEHL90
         SPACE
FEHL04   MVC   W2BER+92(30),=C'04  SEARCH  STATEMENT  MISSING'
         B     FEHL90
         SPACE
FEHL05   MVC   W2BER+92(29),=C'05  FIELD  STATEMENT  MISSING'
         B     FEHL90
         SPACE
FEHL06   MVC   W2BER+92(22),=C'06  DELIMITER  MISSING'
         B     FEHL90
         SPACE
FEHL07   MVC   W2BER+92(31),=C'07  CONTINUATION  CARD  MISSING'
         B     FEHL90
         SPACE
FEHL08   MVC   W2BER+92(19),=C'08  INVALID  LENGTH'
         B     FEHL90
         SPACE
FEHL09   MVC   W2BER+92(25),=C'09  INVALID  DISPLACEMENT'
         B     FEHL90
         SPACE
FEHL10   MVC   W2BER+92(34),=C'10  FIELD  BEYOND  LOGICAL  RECORD'
         B     FEHL90
         SPACE
FEHL11   MVC   W2BER+92(20),=C'11  INVALID  COMPARE'
         B     FEHL90
         SPACE
FEHL12   MVC   W2BER+92(23),=C'12  INVALID  DATA  TYPE'
         B     FEHL90
         SPACE
FEHL13   MVC   W2BER+92(28),=C'13  DATA  HAS  WRONG  LENGTH'
         B     FEHL90
         SPACE
FEHL14   MVC   W2BER+92(25),=C'14  OUT  OF  TABEL  SPACE'
         B     FEHL90
         SPACE
FEHL15   MVC   W2BER+92(27),=C'15  UNBALANCED  PARENTHESIS'
         B     FEHL90
         SPACE
FEHL16   MVC   W2BER+92(25),=C'16  INVALID  LITERAL  TYP'
         B     FEHL90
         SPACE
FEHL90   LR    15,5                     ADDR FEHLER FELD
         LA    15,133(15)               ADDR FUER MAKIERUNG IN W2
         LA    14,W2BER+1
         CR    15,14
         BL    FEHL99
         LA    14,W2BER+82              ADDR BI
         CR    15,14
         BH    FEHL99
         MVC   0(3,15),=C'***'
         SPACE
FEHL99   L     1,ADRLIST
         PUT   (1),W2BER
         MVC   W2BER+1(132),BLK132
         NI    CON03+1,X'0F'           FEHLER ENDE  AKTIVIERN
         BR    12
         EJECT
FLDCHK   L     15,ADEL
         TRT   0(80,5),0(15)            SUCHEN KOMMA
         BNE   FLDCHK2                  GEFUNDEN
         SPACE
FLDCHK1  LA    12,B10290                KEIN DELIM IN KARTE
         B     FEHL06                   NEUE KARTE
         SPACE
FLDCHK2  LA    6,0(1)                   ADDR DELIM
         LR    7,6
         CR    6,10                     DELIM IN KARTE
         BNL   FLDCHK1                  NEIN
         SR    7,5                      ANZ BYTES
         BCR   13,8                     KEIN BYTES     BNP
         C     7,=F'4'
         BCR   2,8                      ZU VIEL BYTES   BH
         L     15,ANUM
         BCTR  7,0
         EX    7,NUMER
         BCR   7,8                      NICHT NUMERIC   BNZ
         EX    7,PACK
         CVB   9,JFCB
         LTR   9,9
         BCR   8,8                      VALUE 0         BZ
         C     9,JFCB+8
         BCR   2,8                      VALUE ZU GROSS   BH
         NC    SCHF,JFCB+12             FEHLER BIT LOESCHEN
         BR    12
         EJECT
FIELD    LA    5,7(5)                   ADDR 1. STELLE NACH (
         L     11,ATABEND
         LA    10,W1BER+82
         MVI   SCHF,X'C0'               FEHLER BITS
*
*             AUFBAU LAENGE
*
         LA    8,FEHL08                 ADDR FEHLER ROUTINE
         MVC   JFCB+8(4),=F'256'        MAX VALUEE
         MVI   JFCB+12,X'7F'            LOESCH MASKE
         BAL   12,FLDCHK
         BCTR  9,0
         STC   9,2(11)                  RETTEN LAENGE
*
*
*           AUFBAU  DISPLACEMENT
*
*
         LA    5,1(6)                   1. STELLE DISP
         LA    8,FEHL09                 ADDR FEHLER ROUTINE
         MVC   JFCB+8(4),=F'9999'       MAX VALUEE
         MVI   JFCB+12,X'BF'            LOESCH MASKE
         BAL   12,FLDCHK
         BCTR  9,0
         ST    9,JFCB
         MVC   0(2,11),JFCB+2           RETTEN MASCH DISP
         SR    8,8
         IC    8,2(11)                  MASCH LAENGE VON LITERAL
         LA    9,1(8,9)                 RELATIVE ADDR LEZTE BYTE
         C     9,LRECL                  FELD AUSERHALB LRECL
         BNH   FLD010                   NEIN
         OI    SCHF,X'40'               FEHLER BIT SETZEN
         BAL   12,FEHL10
         EJECT
*
*            AUFBAU BEDINUNG
*
FLD010   LA    5,1(6)                   1. STELLE BEDINUG
         L     15,ADEL
         LA    12,B10290                NEU KARTE NACH FEHLER
         TRT   0(80,5),0(15)
         BZ    FEHL06                   KEIN DELIMTER
         CR    1,10
         BNL   FEHL06                   NACH STELLE 71
         LA    7,0(1)             ADDR DELIM
         SR    7,5                      LANGE DATA
         C     7,=F'2'
         BE    FLD020
         BAL   12,FEHL11
         MVI   SCHF,X'20'               SETZEN FEHLER
         B     FLD100
         SPACE
FLD020   CLC   0(2,5),=C'GT'
         BNE   *+12
         MVI   4(11),X'20'
         B     FLD100
         CLC   0(2,5),=C'LT'
         BNE   *+12
         MVI   4(11),X'40'
         B     FLD100
         CLC   0(2,5),=C'NE'
         BNE   *+12
         MVI   4(11),X'70'
         B     FLD100
         CLC   0(2,5),=C'EQ'
         BNE   *+12
         MVI   4(11),X'80'
         B     FLD100
         CLC   0(2,5),=C'GE'
         BNE   *+12
         MVI   4(11),X'B0'
         B     FLD100
         CLC   0(2,5),=C'LE'
         BNE   *+12
         MVI   4(11),X'D0'
         B     FLD100
         BAL    12,FEHL11
         MVI    SCHF,X'20'              SETZEN FEHLER BIT
         EJECT
*
*          VERARBEITEN LITERAL
*
FLD100   MVI   3(11),X'D5'              VERGLICHS CODE
         LA    5,3(5)                   ADDR DATA TYP
         CLI   0(5),C'C'
         BE    FLD200
         CLI   0(5),C'X'
         BE    FLD300
         CLI   0(5),C'P'
         BE    FLD400
         LA    12,B10290
         B     FEHL12
         SPACE 6
FLD200   CLI   1(5),X'7D'                   HOCH  KOMMA
         BE    FLD210
         LA    12,B10290
         B     FEHL16
         SPACE
FLD210   SR    7,7
         IC    7,2(11)                      MASCHINE  LAENGE VON KONST
         LA    6,3(7,5)                     ADDR  HOCH KOMMA
         CLI   0(6),X'7D'
         BE    FLD220
         LA    12,B10290
         B     FEHL13
         SPACE
FLD220   STC   7,*+5
         MVC   5(0,11),2(5)            RETTEN  KONSTANTE
         LA    5,1(6)                  ADDR  NACH HOCH KOMMA
         B     FLD900
         EJECT
FLD300   CLI   1(5),X'7D'
         BE    FLD310
         LA    12,B10290
         B     FEHL06
         SPACE
FLD310   SR    7,7
         IC    7,2(11)                 MASCHINE  LAENGE
         SLL   7,1                     X2
         LA    7,1(7)                  MACH LAENGE IN KARTE
         LA    6,3(7,5)
         CLI   0(6),X'7D'
         BE    FLD320
         LA    12,B10290
         B     FEHL13
         SPACE
FLD320   L     15,ATRT
         STC   7,*+5
         TR    2(0,5),0(15)
         SRL   7,1                     LAENGE  IN  TABBLE
         SPACE
FLD330   PACK  JFCB(8),2(15,5)
         STC   7,*+5
         MVC   5(0,11),JFCB
         C     7,=F'7'
         BL    FLD340
         LA    11,7(11)
         LA    5,14(5)
         S     7,=F'7'
         B     FLD330
         SPACE
FLD340   LA    5,1(6)
         B     FLD900
         EJECT
FLD400   CLI   1(5),X'7D'              VERARBEITUNG  P  KONSTANTEN
         BE    FLD410
         LA    12,B10290
         B     FEHL06
         SPACE
FLD410   SR    7,7
         IC    7,2(11)
         C     7,=F'8'
         BL    FLD420
         LA    12,B10290
         B     FEHL08
         SPACE
FLD420   SLL   7,1                     BAND  LAENGE  X 2
         LA    7,1(7)                  MAX  ZULASIGE  KONSTANTEN LAENGE
         LA    6,3(5)                  ADR  2  ZIFFER
         SR    8,8
         SPACE
FLD430   CLI   0(6),X'7D'
         BE    FLD440
         LA    8,1(8)
         LA    6,1(6)
         BCT   7,FLD430
         LA    12,B10290
         B     FEHL13
         SPACE
FLD440   IC    7,2(11)
         SLL   7,4
         OR    7,8
         STC   7,*+5
         PACK  5(0,11),2(0,5)
         SRL   7,4
         LA    5,1(6)
         MVI   3(11),C'9'
         B     FLD900
         EJECT
FLD900   CLI   0(5),C')'
         BE    FLD910
         LA    12,B10290
         B     FEHL06
         SPACE
FLD910   LA    5,1(5)
         LA    11,6(7,11)
FLD915   CLI   0(5),C')'
         BNE   FLD920
         MVI   0(11),C')'
         LA    5,1(5)
         LA    11,1(11)
         MVI   BED,C'U'
         SP    ZKL,=P'1'
         BZ    FLD920
         BAL   12,FEHL15
         SPACE
FLD920   CLI   SCHF,X'00'
         BNE   FLD930                   FEHELR IN FLD
         ST    11,ATABEND
         LA    11,20(11)
         C     11,TABL
         BL    B10260
         BAL   12,FEHL14
         B     B10900
         SPACE
FLD930   CLI   SCH,C'1'                 ONLINE ?
         BE    B10290
         B     B10260                   NEIN
         EJECT
PRINT    NOP   FEHL01                BAL 12 NACH 1. DURCHLAUF
         LA    5,6(5)                1. STELLE NACH =
         LR    6,5
         SPACE
PRINT1   CLI   0(6),C' '             SUCHEN DELIMITER
         BE    PRINT2
         CLI   0(6),C','
         BE    PRINT2
         LA    6,1(6)
         B     PRINT1
         SPACE
PRINT2   NOP   PRINT8                B NACH 1. DURCHLAUF
         LR    7,6
         SR    7,5                   LAENGE KEY DATA
         C     7,=F'2'
         BL    PRINT9                WRONGLENGTH
         C     7,=F'4'
         BE    PRINT9          WRONG LENGTH
         C     7,=F'6'
         BH    PRINT9          WRONG LENGTH
         CLC   0(3,5),=C'RNE'
         BNE   PRINT3
         OI    CON14+1,X'F0'
         NI    B50110+1,X'0F'
         LA    5,3(5)
         SPACE
PRINT3   CLC   0(2,5),=C'CH'
         BNE   PRINT4
         NI    CON23+1,X'0F'             2. KOPF AKTIVIERN
         OI    CON18+1,X'F0'             PRINT CH ACTIVIERN
         B     PRINT6
         SPACE
PRINT4   CLC   0(2,5),=C'CX'
         BNE   PRINT5
         OI    CON20+1,X'F0'               AKTIVIERN CX ROUTINE
         B     PRINT6
         SPACE
PRINT5   CLC   0(2,5),=C'XE'
         BNE   PRINT9                      FEHLER
         OI    CON19+1,X'F0'               AKTIVIERN XE ROUTINE
         SPACE
PRINT6   LA    5,2(5)
         CR    5,6
         BE    PRINT7
         CLI   0(5),C'C'
         BNE   PRINT9
         NI    CON17A+1,X'0F'            AKTIVIERN COUNT DRUCKEN
         SPACE
PRINT7   MVC   PRINT(2),=X'45C0'            PRINT AUF BAL 12
         OI    PRINT2+1,X'F0'
         OI    CON04+1,X'F0'
         NI    CON16+1,X'0F'            AKTIVIERN DRUCK ROUTINEN
         MVC   CON04+2(2),SB11040
         SPACE
PRINT8   CLI   0(6),C' '
         BE    B10130                       KEIN KEY WORT MEHR
         LA    5,1(6)
         B     B10120                   NACHSTE KEY WORT
         SPACE
PRINT9   BAL   12,FEHL03
         CLI   SCH,C'1'
         BNE   PRINT7                 KEIN ONLINE
*
*
*        RESET ALLE WEICHEN
*
*
         OI    CON23+1,X'F0'
         NI    CON18+1,X'0F'
         NI    CON14+1,X'0F'
         OI    B50110+1,X'F0'
         NI    CON20+1,X'0F'
         NI    CON19+1,X'0F'
         OI    CON17A+1,X'F0'
         B     PRINT8
         EJECT
LINE     NOP   FEHL01                   BAL 12 NACH 1. DURCHLAUF
         LA    5,8(5)                   1.STELLE NACH =
         MVC   JFCB+8(4),=F'7'          MAX ZIFFERN ANZ
         ZAP   JFCB+12(4),=P'30'        MINDEST WERT
         BAL   12,DATACHK
         NOP   LINE1
         OI    *-3,X'F0'
         MVC   LINE(2),=X'45C0'         LINE AUF BAL 12
         MVC   LINECNT,JFCB+4
         SPACE
LINE1    CLI   0(6),C' '
         BE    B10130                   KEIN KEY WORT MEHR
         LA    5,1(6)
         B     B10120                   NACHSTE KEY WORT
         SPACE 9
MAX      NOP   FEHL01                   BAL 12 NACH 1. DURCHLAUF
         LA    5,7(5)                   1.STELLE NACH =
         MVC   JFCB+8(4),=F'7'          MAX ZIFFERN ANZ
         ZAP   JFCB+12(4),=P'0'         MINDEST WERT
         BAL   12,DATACHK
         NOP   MAX1
         OI    *-3,X'F0'
         MVC   MAX(2),=X'45C0'          MAX AUF BAL 12
         CVB   9,JFCB
         ST    9,OFLCNT
         OI    CON04B+1,X'F0'              AKTIVIERN MAXOFL
         OI    CON11B+1,X'F0'              AKTIVIERN MAXOFL
         SPACE
MAX1     CLI   0(6),C' '
         BE    B10130                   KEIN KEY WORT MEHR
         LA    5,1(6)
         B     B10120                   NACHSTE KEY WORT
         EJECT
SKIP     NOP   FEHL01                   BAL 12 NACH 1. DURCHLAUF
         LA    5,5(5)                   1.STELLE NACH =
         MVC   JFCB+8(4),=F'7'          MAX ZIFFERN ANZ
         ZAP   JFCB+12(4),=P'1'         MINDEST WERT
         BAL   12,DATACHK
         NOP   SKIP1
         OI    *-3,X'F0'
         MVC   SKIP(2),=X'45C0'         SKIP AUF BAL 12
         CVB   9,JFCB
         ST    9,RETSKIP
         ST    9,NRSKIP
         NI    CON12+1,X'0F'                AKTIVIERN SKIP ROUTINE
         SPACE
SKIP1    CLI   0(6),C' '
         BE    B10130                   KEIN KEY WORT MEHR
         LA    5,1(6)
         B     B10120                   NACHSTE KEY WORT
        SPACE 9
START    NOP   FEHL01                   BAL 12 NACH 1. DURCHLAUF
         LA    5,9(5)                   1.STELLE NACH =
         MVC   JFCB+8(4),=F'7'          MAX ZIFFERN ANZ
         ZAP   JFCB+12(4),=P'1'         MINDEST WERT
         BAL   12,DATACHK
         NOP   START1
         OI    *-3,X'F0'
         MVC   START(2),=X'45C0'        START AUF BAL 12
         CVB   9,JFCB
         ST    9,NRSTART
         NI    CON06+1,X'0F'                AKTIVIERN STARTAFT ROUTINE
         SPACE
START1   CLI   0(6),C' '
         BE    B10130                   KEIN KEY WORT MEHR
         LA    5,1(6)
         B     B10120                   NACHSTE KEY WORT
         EJECT
STOP     NOP   FEHL01                   BAL 12 NACH 1. DURCHLAUF
         LA    5,8(5)                   1.STELLE NACH =
         MVC   JFCB+8(4),=F'7'          MAX ZIFFERN ANZ
         ZAP   JFCB+12(4),=P'1'         MINDEST WERT
         BAL   12,DATACHK
         NOP   STOP1
         OI    *-3,X'F0'
         MVC   STOP(2),=X'45C0'         STOP AUF BAL 12
         ZAP   NRSTOP,JFCB+4(4)
         NI    CON21+1,X'0F'             AKTIVIERN STOPAFT ROUTINE
         SPACE
STOP1    CLI   0(6),C' '
         BE    B10130                   KEIN KEY WORT MEHR
         LA    5,1(6)
         B     B10120                   NACHSTE KEY WORT
         EJECT
TITLE    NOP   FEHL01                  BAL  12  NACH 1. DURCHLAUF
         CLI   6(5),X'7D'              HOCH KOMMA
         LA    12,B10100
         BNE   FEHL01
         SR    7,7
         LA    5,7(5)                  1.  STELLE NACH HOCH KOMMA
         LA    6,1(5)                  2. STELLE NACH HOCH KOMMA
         SPACE
TITLE1   CLI   0(6),X'7D'              HOCH  KOMMA
         BE    TITLE2
         LA    7,1(7)                  MACH LAENG  TITLE
         LA    6,1(6)
         C     7,=F'66'
         BNH   TITLE1
         LA    12,B10100
         B     FEHL06
         SPACE
TITLE2   MVC   KOPF2+2(89),KOPF2+1
         STC   7,*+5                   LAENGE IN MVC
         MVC   KOPF2+10(0),0(5)
         MVC   TITLE(2),=X'45C0'       BEFEHL AUF BAL
         LA    6,1(6)                 STELLE NACH  HOCH KOMMA
         CLI   0(6),C' '
         BE    B10130                 KEIN KEY MEHR IN KARTE
         LA    5,1(6)                 1. STELLE NAECHSTE KEY
         B     B10120
         EJECT
B11000   MVC   JFCB(8),=C'NULLFILE'
         RDJFCB AFX
         CLC   JFCB(8),=C'NULLFILE'
         BE    B11020                  KEIN  SYSUT2  VORHANDEN
         OI    CON04+1,X'F0'           FEHLER  ROUTINE  AUSSCHALTEN
         NI    CON13+1,X'0F'           SYSUT2  AUSGABE  AKTIVIERN
         NI    CON10+1,X'0F'           CLOSE  AKTIVIERN
         TM    JFCB+98,X'80'           DSORG  IST
         BZ    B11010                  NICHT  IS
         LA    11,AFXIS
         ST    11,ADROUT
         SPACE
B11010   L     11,ADROUT               ADR  RICHTIGE  SYSUT2
         L     2,ADRIN
         OPEN  ((11),OUTPUT)
         TM    36(11),X'40'             IST OUTPUT RECFM U/V
         BNO   B11020
         NI    CON13A+1,X'0F'
         SPACE
B11020   MVC   JFCB(8),=C'NULLFILE'
         RDJFCB AFX1
         CLC   JFCB(8),=C'NULLFILE'
         BE    CON04                   KEINE  SYSUT3  VORHANDEN
         OI    CON04+1,X'F0'           FEHLER  ROUTINE  AUSSCHALTEN
         NI    CON15+1,X'0F'           SYSUT3  AUSGABE  AKTIVIERN
         NI    CON11+1,X'0F'           CLOSE  AKTIVIERN
         TM    JFCB+98,X'80'           DSORG  IST
         BZ    B11030                  NICHT  IS
         LA    11,AFX1IS
         ST    11,ADROUT1
         SPACE
B11030   L     11,ADROUT1              ADR  RICHTIGE  SYSUT3
         L     2,ADRIN
         OPEN   ((11),OUTPUT)
         TM    36(11),X'40'             IST OUTPUT RECFM U/V
         BNO   CON04
         NI    CON15A+1,X'0F'
         SPACE
CON04    NOP   CON06                   B BEI OUTPUT  B11040 BEI PRINT
         L     2,ADRIN
         CLOSE ((2))
CON04A   NOP   B80000                   B  BEI INFO
CON04B   NOP   B80500                   B  BEI MAXOFL
         MVC   W2BER+11(29),=C'NO  OUTPUT  DEVICE  REQUESTED'
         BAL   12,FEHL99
         CLI   SCH,C'1'
         BNE   CON03                   KEIN ONLINE
         OPEN  EFRD
         B     B10100
         SPACE
B11040   LA    12,CON06
         L     2,ADRIN
         LH    11,82(2)                LRECL  VON  EINGABE
         EJECT
*              ***************************************************
*              *                                                 *
*              *    ERRECHNEN  ANZAHL  DRUCKZEILEN  FUER  EIN    *
*              *    SATZ  BEI PRINT = CH                         *
*              *    BEI  RECFM=U/V  WIRD  DER  ROUTINE  PRO      *
*              *    SATZ  DUCHLAUFEN                             *
*              *    BEI  RECFM=F  NUR  EINMAL                    *
*              *                                                 *
*              ***************************************************
         SPACE
B11050   EQU   *
         SR    10,10
         BCTR  11,0
         D     10,=F'120'
         ST    10,REST
         ST    11,MAL
         BR    12
         EJECT
CON06    B     B20000                  NOP  BEI  STARTAFT
         L     5,NRSTART
         MVC   B90100+2(2),SB12010
         SPACE
B12000   L     1,ADRIN
         GET   (1)
         SPACE
B12010   B     B12020                  NOP  BEI  DATEI WECSHEL
         OI    *-3,X'F0'               UND
         B     B12000                  CONCATINATION
         SPACE
B12020   AP    ZEIN,=P'1'
         BCT   5,B12000                UEBERLESEN  N  SAETZE
         MVC   B90100+2(2),SB20003
         B     B20000
         EJECT
B20000   L     1,ADRIN
         GET   (1)
         SPACE
B20003   B     B20005                  NOP  BEI  DATEI  WECSHEL
         OI    *-3,X'F0'               UND
         RDJFCB EFX
         MVC   W1BER+1(132),BLK132
         PUT   AFD,W1BER
         MVC   W1BER+11(20),=C'END  OF  INPUT  DATA'
         MVC   W1BER+40(53),KOPF2+9
         PUT   AFD,W1BER
         MVC   KOPF2+22(40),JFCB
         ZAP   ZZEILE,=P'100'
         B     B20000                  CONCATINATION
         SPACE
B20005   AP    ZEIN,=P'1'
         SPACE
B20010   LA    5,0(1)
         ST    5,ALREC
         L     5,ATAB                  ANFANG  SUCH  KRITERIERN
         L     6,ATABEND               ENDE  SUCH  KRITERIERN
         SPACE
CON08    NOP   B50000                  B  WENN  KEIN  SEARCH  KARTE
         CLI   0(5),C'('
         BNE   B20020
         AP    ZKL,=P'1'               UEBERLESEN  (  MIT
         LA    5,1(5)                  ZKL  KORECTUR
         B     CON08
         SPACE
B20020   L     10,ALREC
         MVC   JFCB(2),0(5)            DISP  1.  OPERAND
         AH    10,JFCB
         IC    7,2(5)                  LAENGE  1.  OPERAND  MASCHINEL
         MVC   COMP(1),3(5)            VERGLEICH AUF CLC/CP SETZEN
         MVZ   BRANCH+1(1),4(5)        BEDIENUNGS SCHLUESSEL SETZEN
         LA    8,5(5)                  ADR  CONSTANTE
         SR    9,9
         IC    9,2(5)                  LAENGE  CONSTANTE   MASCHINEL
         LA    5,6(9,5)                ADR  BYTE  NACH  KONSTANTE
         CLI   COMP,C'9'               IST  VERGLEICH  EIN  CP
         BNE   B20030                  NEIN
         SLL   7,4
         OR    7,9                     REG7  HAT  LAENGE  FUER COMP
         SPACE
B20030   EX    7,COMP
BRANCH   NOP   B20100                  BEDIENUNG  ERFUELLT
         B     B20200                  BEDIENUNG  NICHT  ERFUELLET
         SPACE 3
COMP     CLC   0(0,10),0(8)
         EJECT
B20100   CLI   0(5),C')'
         BNE   B20110                  UEBERLESEN  )  MIT
         LA    5,1(5)
         SP    ZKL,=P'1'               ZKL  KOREKTUR
         BNZ   B20100
         SPACE
B20110   CR    5,6                     ENDE  SUCH KRITERIEN
         BNL   B50000                  AUSGABE
         CLI   0(5),C'O'
         BE    B20120
         LA    5,1(5)                  UND  VERBINDUNG  WEITER  SUCHEN
         B     CON08
         SPACE
B20120   CP    ZKL,=P'0'               KEIN  KLAMMER AUF
         BE    B50000                  SATZ  AUSGEBEN
         LA    5,1(5)
         SPACE
B20130   CLI   0(5),C'('               UEBERLESEN  BIS  )   FUER
         BNE   B20140                  ALLE  ZWISCHENLIEGENDE  (
         AP    ZKL1,=P'1'              EIN  )  UEBERLESEN
         LA    5,1(5)
         B     B20130
         SPACE
B20140   BAL   12,B61000               SPRINGEN UM 1 BEDINGUNG
         SPACE
B20150   CLI   0(5),C')'
         BE    B20160
         LA    5,1(5)
         B     B20130
         SPACE
B20160   CP    ZKL1,=P'0'
         BE    B20100
         LA    5,1(5)
         SP    ZKL1,=P'1'
         B     B20150
         EJECT
B20200   CLI   0(5),C')'               )  UEBERLESEN
         BNE   B20210                  MIT  2  KOREKTUR
         LA    5,1(5)
         SP    ZKL,=P'1'
         BNZ   B20200
         SPACE
B20210   CR    5,6                     SUCH  KRITERIEN  ZU ENDE
         BNL   B50100
         CLI   0(5),C'U'
         BE    B20220                  UND  VERBINDUNG  UEBERLESEN
         LA    5,1(5)
         B     CON08                   BEI  ODER  WEITER SUCHEN
         SPACE
B20220   LA    5,1(5)                  BEI  (  BIS  )  UEBERLESEN UND
         CLI   0(5),C'('               ALLE KLAMMERN PAAR  AUCH
         BE    B20230
         BAL   12,B61000               WEITER  UM  1  BEDIENUNG
         B     B20200
         SPACE
B20230   AP    ZKL1,=P'1'
         LA    5,1(5)
         SPACE
B20240   CLI   0(5),C'('
         BE    B20230
         BAL   12,B61000
         SPACE
B20250   CLI   0(5),C')'
         BE    B20260
         LA    5,1(5)
         B     B20240
         SPACE
B20260   LA    5,1(5)
         SP    ZKL1,=P'1'
         BZ    B20200
         B     B20250
         EJECT
B49000   MVC   W1BER+1(132),BLK132
         PUT   AFD,W1BER
         MVC   W1BER+11(20),=C'END  OF  INPUT  DATA'
         MVC   W1BER+40(53),KOPF2+9
         MVI   W1BER,X'19'
         PUT   AFD,W1BER
         SPACE
B49100   L     2,ADRIN
         CLOSE ((2))
         ED    SYS1,ZEIN               RECORD COUNT  SYSUT1
         ED    SYS2,ZAUS               RECORD COUNT  SYSUT2
         ED    SYS3,ZAUS1              RECORD COUNT  SYSUT3
         SPACE
CON10    B     CON11                   NOP  WENN  SYSUT2  VORHANDEN
         L     2,ADROUT
         CLOSE ((2))
         CP    ZAUS,=P'0'              RECORD  COUNT FUER
         BNE   CON11                   SYSUT2  UNGLEICH  0
         LA    14,4                    CONDCODE  AUF   4  SETZEN
         ST    14,CONDCODE             BEI RECORD COUNT = 0
         SPACE
CON11    B     B49200                  NOP  WENN  SYSUT3  VORHANDEN
         L     2,ADROUT1
         CLOSE ((2))
         CP    ZAUS1,=P'0'             RECORD  COUNT  FUER
         BNE   B49200                  SYSUT3  UNGLEICH  0
         L     14,CONDCODE
         LA    14,8(14)                CONDCODE  UM  8  ERHOEHEN
         ST    14,CONDCODE             BEI RECORD  COUNT  = 0
         SPACE
B49200   L     1,ADRLIST
         PUT   (1),TXT
CON11A   NOP   B80000                   B BEI INFO
CON11B   NOP   B80500                   B BEI MAXOFL
B49250   CLOSE AFD
         TM    AFD1+48,X'10'
         BZ    B49300                  KEIN SYSTERM DD KARTE
         CLOSE AFD1
         SPACE
B49300   L     15,CONDCODE
         L     13,SAVE+4
         RETURN (14,12),RC=(15)
         EJECT
B50000   EQU   *
CON12    B     CON13                   NOP  BEI  SKIP
         L     14,NRSKIP               SCHREIBEN  JEDE  N.
         BCT   14,B50010               SATZ
         L     14,RETSKIP
         ST    14,NRSKIP
         B     CON13                   SCHREIBEN  SYSUT2
         SPACE
B50010   ST    14,NRSKIP
         B     B20000                  NEUE  SATZ  LESEN
         SPACE
CON13    B     CON14                   NOP  BEI  SYSUT2  AUSGABE
         L     1,ADROUT
CON13A   B     B50020               NOP WENN SYS2 RECFM=U/V
         L     14,ADRIN             EINGABE DCB
         LH    14,82(14)            EINGABE LAENGE
         STH   14,82(1)
B50020   BAL   12,B50900
         AP    ZAUS,=P'1'
CON14    NOP   B50300                  B  BEI  PRINT  RNE
         B     B50200
         SPACE
B50100   EQU   *
CON15    B     B50110                  NOP  BEI  SYSUT3  AUSGABE
         L     1,ADROUT1
CON15A   B     B50105               NOP WENN SYS3 RECFM=U/V
         L     14,ADRIN             EINGABE DCB
         LH    14,82(14)            EINGABE LAENGE
         STH   14,82(1)
B50105   BAL   12,B50900
         AP    ZAUS1,=P'1'
B50110   B     B50300                  NOP  BEI  PRINT  RNE
         SPACE
B50200   EQU   *
CON16    B     B50300                  NOP  BEI  PRINT
CON17    B     B50210                  NOP  BEI  RECFM=U/V UND PRINT=CH
         L     2,ADRIN
         LH    11,82(2)                LRECL  VON  EINGABE  SATZ
         BAL   12,B11050               ERRECHE  ANZAHL  DRUCKZEILEN
         SPACE
B50210   AP    ZPRINT,=P'1'
         SPACE
CON17A   B     CON18                    NOP BEI DRUCKEN COUNT
         MVI   W1BER,X'09'
         MVC   W1BER+1(132),BLK132
         MVC   W1BER+5(13),=C'RECORD  COUNT'
         UNPK  W1BER+20(7),ZEIN
         OI    W1BER+26,X'F0'
         AP    ZZEILE,=P'1'             SICHER STELLEN
         BAL   12,B60000                DAS COUT NICHT VON
         SP    ZZEILE,=P'1'             DATA GETRENT WIRD
         SPACE
CON18    NOP   B51000                  B  BEI  PRINT   CH
CON19    NOP   B52000                  B  BEI  PRINT   XE
CON20    NOP   B53000                  B  BEI  PRINT   CX
         SPACE
B50300   EQU   *
CON21    B     B20000                  NOP  BEI  STOPAFT
         CLC   NRSTOP,ZPRINT
         BNH    B50310
         CLC   NRSTOP,ZAUS
         BNH    B50310
         CLC   NRSTOP,ZAUS1
         BNH    B50310
         B     B20000
B50310   MVC   W1BER+1(132),BLK132
         PUT   AFD,W1BER
         MVI   W1BER,X'19'
         MVC   W1BER+11(27),=C'REQUESTED  RECORDS  WRITTEN'
         MVC   W1BER+40(53),KOPF2+9
         PUT   AFD,W1BER
         B     B49100
         SPACE
B50900   L     10,ALREC                ADDR EINGABE
         L     11,ADRIN                EINGABE DCB
         LH    11,82(11)               LAENGE EINGABE
         LH    9,82(1)                 LAENGE AUSGABE
         PUT   (1)
         LR    8,1                     ADDR AUSGABE
         ICM   11,8,=C' '              PADDING FUER AUSGABE
         MVCL  8,10
         BR    12
         EJECT
B51000   L     5,ALREC                 AUFBEREITUNG   CH  DRUCKAUSGABE
         L     11,MAL
         MVC   W1BER+1(132),BLK132
         SPACE
B51010   LTR   11,11
         BZ    B51020
         MVC   W1BER+1(120),0(5)
         L     15,ATAB01
         TR    W1BER+1(120),0(15)
         BAL   12,B60000               DRUCKEN  AUSGABE
         LA    5,120(5)
         BCT   11,B51010
         SPACE
B51020   MVC   CON22+1(1),REST+3
         MVC   CON22A+1(1),REST+3
         LA    11,W1BER+2
         A     11,REST
         MVC   W1BER+1(132),BLK132
CON22    MVC   W1BER+1(0),0(5)         AUFBEREITUNG  REST
         L     15,ATAB01
CON22A   TR    W1BER+1(0),0(15)
         MVI   0(11),C'*'
         BAL   12,B60000               DRUCKEN  AUSGABE
         B     B50300
         EJECT
B52000   L     5,ALREC              ADR INPUT
         L     2,ADRIN              ADR  DCB
         LH    6,82(2)              LAENGE EINGABE
         BCTR  6,0
         SPACE
B52010   C     6,=F'56'             LETZTE ZEILE
         BL    B52030               JA
B52015   LA    15,14                BLK COUNTER
         LA    14,W1BER+1           ADR  AUSG
         SPACE
B52020   UNPK  0(9,14),0(5,5)
         MVI   8(14),X'EF'
         LA    5,4(5)
         LA    14,9(14)
         BCT   15,B52020
         B     B52050               WRITE
         SPACE
B52030   MVI   W1BER+1,X'EF'
         MVC   W1BER+2(125),W1BER+1
         LA    14,W1BER+1
         STC   6,*+5
         MVC   JFCB(0),0(5)
         LA    5,JFCB
         SPACE
B52040   UNPK  0(9,14),0(5,5)
         MVI   8(14),X'EF'
         LA    5,4(5)
         LA    14,9(14)
         S     6,=F'4'
         BNM   B52040
         SPACE
         AR    14,6
         AR    14,6
         LCR   15,6
         BCTR  15,0
         AR    15,15
         STC   15,*+5
         MVC   1(0,14),W3BER
         MVI   W1BER,X'11'
         SPACE
B52050   L     15,AHEX
         TR    W1BER+1(126),0(15)
         BAL   12,B60000
         S     6,=F'56'
         BNM   B52010
         SPACE
         MVI   W1BER,X'09'
         AP    ZZEILE,=P'1'
         B     B50300
         EJECT
B53000   L     5,ALREC               EINGABE SATZ
         L     2,ADRIN               ADR  DCB
         LH    6,82(2)               LAENGE SATZ
         BCTR  6,0                   MASCH LAENGE
         MVI   W1BER+91,C'*'
         MVI   W1BER+132,C'*'
         SPACE
B53010   C     6,=F'39'              LAST LINE
         BL    B53030                JA
         BH    B53015
         MVI   W1BER,X'11'
B53015   MVC   W1BER+92(40),0(5)     FULLEN CHAR TEIL
         LA    5,40(5)               WEITER IN EINGABE
         LA    15,10                 BLOCK COUNTER FUER BCT
         LA    14,W1BER+1            ADR HEX OUTPUT
         LA    7,W1BER+92
         SPACE
B53020   UNPK  0(9,14),0(5,7)        *
         MVI   8(14),X'EF'           *
         LA    7,4(7)                *  FULLEN HEX TEIL
         LA    14,9(14)              *
         BCT   15,B53020             *
         B     B53050
         SPACE
B53030   MVC   W1BER+1(132),W3BER
         STC   6,*+5
         MVC   W1BER+92(0),0(5)      FULLEN REST BYTES
         LA    14,W1BER+1
         LA    7,W1BER+92
         SPACE
B53040   UNPK  0(9,14),0(5,7)
         MVI   8(14),X'EF'
         LA    7,4(7)
         LA    14,9(14)
         S     6,=F'4'
         BNM   B53040
         SPACE
         AR    14,6
         AR    14,6
         LCR   15,6                  LOESCHEN EXTRA BYTES
         BCTR  15,0
         AR    15,15
         STC   15,*+5
         MVC   1(0,14),W3BER
         MVI   W1BER,X'11'
         SPACE
B53050   L     15,ADEC
         TR    W1BER+92(40),0(15)
         L     15,AHEX
         TR    W1BER+1(90),0(15)
         BAL   12,B60000
         S     6,=F'40'
         BNM   B53010
         SPACE
         MVI   W1BER,X'09'
         AP    ZZEILE,=P'1'
         B     B50300
         EJECT
B60000   CP    ZZEILE,LINECNT
         BL    B60010
         AP    ZSEITE,=P'1'
         MVC   HDR1+21(6),MEDIT
         ED    HDR1+21(6),ZSEITE
         PUT   AFD,BLKTEXT
         PUT   AFD,KOPF2
         ZAP   ZZEILE,=P'0'
CON23    B     B60010                  NOP  BEI  PRINT  CH
         PUT   AFD,KOPF1
         ZAP   ZZEILE,=P'2'
         SPACE
B60010   PUT   AFD,W1BER
         AP    ZZEILE,=P'1'
         BR    12
         SPACE 3
B61000   SR    14,14
         IC    14,2(5)                 MACH  LAENGE  KONST
         LA    5,6(14,5)
        BR    12
         EJECT
*
*              LESEN   EINGABE   DRUCKEN
*              UEBERLESEN WENN EINGABE BLANK
*
*              BEI EXIT HAT R5 DIE ADDR DES 1. DATENBYTE
*
B70000   GET   EFRD,W1BER+11
         CLC   W1BER+11(72),BLK132    GELESENE KARTE IST BLANK
         BE    B70000                 NEUE KARTE LESEN
         L     1,ADRLIST
         PUT   (1),W1BER
         LA    5,W1BER+11
         CLI   W1BER+11,C'*'
         BE    B70000
         SPACE
B70100   CLI   0(5),C' '
         BCR   7,12
         LA    5,1(5)
         B     B70100
         SPACE
B70900   L     12,ADRATEND
         BR    12
         SPACE
ADRATEND DC    A(B10900)
         EJECT
B80000   MVI   EFXIS+50,X'20'
         RDJFCB EFXIS
         OPEN   EFXIS                   PGM TEIL FUER PARM = INFO
         MVC   INFO1+18(40),JFCB        DSNAME
         L     5,EFXIS+156              PRIME RECORDS
         CVD   5,JFCB
         UNPK  INFO2+21(9),JFCB+3(5)
         OI    INFO2+29,X'F0'
         LH    5,EFXIS+214             OVERFLOW RECORDS
         CVD   5,JFCB
         UNPK  INFO2+42(7),JFCB+4(4)
         OI    INFO2+48,X'F0'
         LH    5,EFXIS+206             FREIE TRKS
         CVD   5,JFCB
         UNPK  INFO3+23(5),JFCB+5(3)
         OI    INFO3+27,X'F0'
         LH    5,EFXIS+224             VOLLE CYLOFL AREAS
         CVD   5,JFCB
         UNPK  INFO4+19(5),JFCB+5(3)
         OI    INFO4+23,X'F0'
         MVI   W1BER,X'89'
         MVC   W1BER+1(132),BLK132
         PUT   AFD,W1BER
         MVI   W1BER,X'09'
         LA    5,INFO1
         LA    6,3                    BCT COUNT
         L     7,ADRLIST
         SPACE
B80100   MVC   W1BER+5(58),0(5)
         PUT   (7),W1BER
         LA    5,58(5)
         BCT   6,B80100
         MVI   W1BER,X'89'
         MVC   W1BER+5(58),0(5)
         PUT   (7),W1BER
         CLOSE EFXIS
         B     CON11B
         SPACE 3
INFO1    DC    CL58'INFO FOR DATA-SET'
INFO2    DC    CL58'RECORD NUMBER  PRIME XXXXXXXXX   OVERFLOW XXXXXXX'
INFO3    DC    CL58'FREE TRKS IN OVERFLOW  XXXXX'
INFO4    DC    CL58'USED CYLOFL AREAS  XXXXX'
         SPACE 6
B80500   MVI   EFXIS+50,X'20'           MACRF AUF READ
         OPEN  EFXIS
         LH    5,EFXIS+214              OVERFLOW RECORDS
         C     5,OFLCNT
         BNH   B80510                   OFL COUNT NICHT UEBERSCHRITTEN
         L     15,CONDCODE
         LA    15,20(15)
         ST    15,CONDCODE
         SPACE
B80510   CLOSE EFXIS
         B     B49250
         EJECT
B90000   NOP   B90100                   DCB  EXIT  FUER EINGABE
         OI    *-3,X'F0'
         OI    48(1),X'08'
         B     B90200
B90100   NI    B20003+1,X'0F'
        LH    11,82(1)              LRECL
        BAL   12,B11050             FUELLEN COUNT FUER DRUCK
         OI    CON17+1,X'F0'            RESET
B90200   TM    36(1),X'40'
         BCR   14,14                 NICHT U/V
         NI    CON17+1,X'0F'
         BR    14
         SPACE 3
B91000   CLI   36(1),X'00'              DCB  EXIT FUER AUSGABE
         BNE   B91100
         MVC   36(1,1),36(2)            RECFM
B91100   CLC   62(2,1),=H'0'
         BNE   B91200
         MVC   62(2,1),62(2)            BLKSIZE
B91200   CLC   82(2,1),=H'0'
         BCR   7,14
         MVC   82(2,1),82(2)          LRECL
         BR    14
         EJECT
SAVE     DC    18F'0'
BED      DC    C'U'
BLKTEXT  DC    X'89'
BLK132   DC    CL132' '
         SPACE
KOPF1    DC    X'11'
         DC    12CL10'....5....0'
         DC    CL12' '
         SPACE
KOPF2    DC    X'11'
         DC    CL90' INPUT  DATASET-NAME='
HDR1     DC    CL27'  .      /  /    PAGE'
         DC    CL15' '
         SPACE
LINECNT  DC    PL4'66'
MEDIT    DC    X'402020202020'
NRSTOP   DC    PL4'0'
SCH      DC    C'0'
SCHF     DC    X'00'
         SPACE
TXT      DC    X'09'
         DC    CL10' '
         DC    CL22'RECORD COUNT    SYSUT1'
SYS1     DC    X'4021202020202020'
         DC    CL10'    SYSUT2'
SYS2     DC    X'4021202020202020'
         DC    CL10'    SYSUT3'
SYS3     DC    X'4021202020202020'
         DC    CL66' '
         SPACE
W1BER    DC    X'89'
         DC    CL132' '
         SPACE
W2BER    DC    X'11'
         DC    CL132' '
         SPACE
W3BER    DC    90X'EF'
         DC    CL41'*'
         DC    C'*'
         SPACE
ZAUS     DC    PL4'0'
ZAUS1    DC    PL4'0'
ZEIN     DC    PL4'0'
ZPRINT   DC    PL4'0'
ZZEILE   DC    PL4'100'
ZKL      DC    P'0'
ZKL1     DC    P'0'
ZSEITE   DC    PL3'0'
         SPACE 3
         DS    0D
TABL     DC    PL8'1500'
         EJECT
ADEC     DC    A(DEC)
ADEL     DC    A(DEL)
ADRIN    DC    A(EFX)
ADRLIST  DC    A(AFD)
ADROUT   DC    A(AFX)
ADROUT1  DC    A(AFX1)
AHEX     DC    A(HEX-239)
ALREC    DC    F'0'
ANUM     DC    A(NUM)
ATAB     DC    F'0'
ATABEND  DC    F'0'
ATAB01   DC    A(TAB01)
ATRT     DC    A(TRTP-192)
CONDCODE DC    F'0'
LRECL    DC    F'0'
MAL      DC    F'0'
NRSKIP   DC    F'0'
NRSTART  DC    F'0'
OFLCNT   DC    F'0'
REST     DC    F'0'
RETSKIP  DC    F'0'
SB11040  DC    S(B11040)
SB12010  DC    S(B12010+1)
SB20003  DC    S(B20003+1)
         EJECT
         LTORG
         EJECT
DEC      DC    64C'.'
         DC    C' '
         DC    128C'.'
         DC    C'ABCDEFGHI'
         DC    7C'.'
         DC    C'JKLMNOPQR'
         DC    8C'.'
         DC    C'STUVWXYZ'
         DC    6C'.'
         DC    C'0123456789'
         DC    6C'.'
         SPACE
HEX      DC    CL17' 0123456789ABCDEF'
         SPACE 2
NUM      DC    64X'08'
         DC    X'04'                     SPACE
         DC    42X'08'
         DC    X'04'                    COMMA
         DC    132X'08'
         DC    10X'00'                  0-9
         DC    6X'08'
         SPACE 3
TRTP     DC    XL16'00FAFBFCFDFEFF000000000000000000'
         DC    XL32'00'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9000000000000'
         SPACE 2
TAB01    DC    CL74' '
         DC    CL16'Ö.<(+×&&'
         DC    CL17'!$*);^-/'
         DC    CL15',%_>?'
         DC    CL71':#@''="'
         DC    CL16'ABCDEFGHI'
         DC    CL17'JKLMNOPQR'
         DC    CL14'STUVWXYZ'
         DC    CL16'0123456789'
         SPACE
DEL      DC    107X'00'
         DC    X'FF'                    COMMA
         DC    148X'00'
         SPACE
         END
