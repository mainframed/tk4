IEFACTRT CSECT
         XSAVE BASE,,IEFACTRT,WORKL
         USING WORK,R13
         USING DEVICES,DEVBAS
         USING MSGNAGES,MSGNBAS
         USING PREISTAB,PREISBAS
         USING CUED,CUEDBAS
         XC    CLEARANF(CLEARLEN),CLEARANF
         ST    R1,ORGR1SVA
         L     R1,0(R1)
         TM    29(R1),X'01'
         BO    XRET
         ST    LCTREG,LCTRSVA
         LA    WORK2,MSG               LCT VORBEREITEN
         ST    WORK2,36(LCTREG)        FUER VERWENDUNG
         MVC   42(2,LCTREG),=H'118'    DER ROUTINE 'IEFYS'
         L     MSGNBAS,=V(ACTRTMSG)
         ST    MSGNBAS,MSGBASSV
         L     WORK2,ORGR1SVA
         MVC   COMEXTAB,0(WORK2)
         L     WORK3,COMEXTAB
         MVC   COMUEDAT,32(WORK3)
         L     WORK3,36(WORK2)
         LA    WORK3,4(WORK3)
         ST    WORK3,RECADDR
         XC    30(2,WORK3),30(WORK3)
         L     WORK4,4(R13)
         TM    23(WORK4),X'10'
         BO    JOBEND
         MVI   MSG,C'*'
         MVC   MSG+1(117),MSG
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         MVC   MSGN,MSGN1
         L     WORK4,ORGR1SVA
         L     WORK5,28(WORK4)         STEP-NO.
         SR    R15,R15
         IC    R15,1(WORK5)
         CVD   R15,DBLWD
         UNPK  MSGN(2),DBLWD
         OI    MSGN+1,X'F0'
         LA    WORK3,MSGN+31
         L     WORK4,COMEXTAB
         LA    WORK2,12(WORK4)
         BAL   LINK,GETDAT
         MVC   MSGN+83(4),16(WORK4)
         L     R15,16
         SH    R15,=H'6'
         UNPK  MSGN+63(4),0(3,R15)
         MVI   MSGN+66,C' '
         MVC   PREISLOC,=CL8'PREIS'
         MVC   DEVLOC,=CL8'DEV'
         MVC   PREISLOC+5(2),16(WORK4)
         MVC   DEVLOC+3(2),16(WORK4)
         L     WORK5,RECADDR
         TM    83(WORK5),X'02'
         BO    TSTCC
RETCD    EQU   *
         MVC   MSGN+91(11),=CL11'RC ='
         MVC   DBLWD(2),51(WORK5)
         LH    R15,DBLWD
         CVD   R15,DBLWD
         UNPK  MSGN+96(3),DBLWD
         OI    MSGN+98,X'F0'
         B     NOCC
TSTCC    EQU   *
         TM    51(WORK5),X'80'
         BNO   SYSCC
         MVI   MSGN+96,C'U'
         MVC   DBLWD(2),51(WORK5)
         LH    R15,DBLWD
         SLL   R15,20
         SRL   R15,20
         CVD   R15,DBLWD
         UNPK  MSGN+99(3),DBLWD
         OI    MSGN+101,X'F0'
         B     NOCC
SYSCC    EQU   *
         UNPK  MSGN+99(4),51(3,WORK5)
         MVI   MSGN+102,X'40'
         TR    MSGN+99(3),CCTAB-240
NOCC     EQU   *
         BAL   LINK,WRITE
         MVC   MSGN,MSGN2
         MVC   MSGN+6(8),0(WORK4)
         LA    WORK2,39(WORK5)
         LA    WORK3,MSGN+32
         BAL   LINK,GETZEIT
         LOAD  EPLOC=PREISLOC
         ST    R0,PRBASSVA
         LR    PREISBAS,R0
         L     WORK2,CPUPR
         LA    WORK3,MSGN+63
         BAL   LINK,EDPREIS
         LR    WORK9,WORK2
         L     R12,ORGR1SVA
         L     WORK2,20(R12)
         L     WORK2,0(WORK2)
         SRL   WORK2,8
         MH    WORK2,CPULF
         SR    WORK3,WORK3
        D     WORK3,=F'1000'
         LA    WORK3,MSGN+84
         BAL   LINK,EDZTSTD
         MR    R2,WORK9
         D     R2,=F'360000'  CPUPR IN PF/STD
         L     R14,COMEXTAB
         CLI   30(R14),X'00'
         BE    TSTSTPNO
         CLI   30(R14),X'FF'
         BE    ZRORST
         MVI   30(R14),X'FF'
         B     GETMAIN
ZRORST   EQU   *
         MVI   30(R14),X'00'
         B     BERDA
TSTSTPNO EQU   *
         L     R14,28(R12)
         CLI   1(R14),X'01'
         BH    BERDA
GETMAIN  GETMAIN R,LV=16,SP=255
         ST    R1,COMUEDAT
         L     R12,COMEXTAB
         ST    R1,32(R12)
         XC    0(16,R1),0(R1)
         LR    CUEDBAS,R1
BERDA    EQU   *
         BAL   LINK,ACCDMSTP
         CVD   WORK2,DBLWD
         ED    MSGN+102(10),DBLWD+4
         BAL   LINK,WRITE
*                ERRECHNEN DER LAUFZEIT
         L     R12,RECADDR
         LH    WORK3,102(R12)
         BCTR  WORK3,0
         BCTR  WORK3,0
         SRL   WORK3,3
         LTR   WORK3,WORK3
         BZ    ENDLZ
         LOAD  EPLOC=DEVLOC
         ST    R0,DEVBASSV
         LR    DEVBAS,R0
         LA    WORK4,104(R12)
NXTDEV   EQU   *
         CLC   0(2,WORK4),DEVTYPE
         BE    ACCDEV
         LA    DEVBAS,40(DEVBAS)
         CLC   DEVCHNC(40),=XL40'00'
         BNE   NXTDEV
INCRUCB  LA    WORK4,8(WORK4)
         L     DEVBAS,DEVBASSV
         BCT   WORK3,NXTDEV
         B     TCTEND
ACCDEV   EQU   *
         CLC   4(4,WORK4),=F'0'
         BE    TSTBEL
         TM    DEVSWT,X'20'
         BNO   KOK
         OI    SWITCH,X'20'
KOK      EQU   *
         OI    SWITCH,X'40'
         L     R15,DEVEXCPC
         A     R15,4(WORK4)
         ST    R15,DEVEXCPC
TSTBEL   EQU   *
         TM    DEVSWT,X'80'
         BNO   INCRUCB
         CLI   DEVTYPE,X'20'
         BNE   INCRBEL
         L     WORK2,16
         L     WORK2,40(WORK2)
NXTUCB   EQU   *
         BAL   WORK7,SEARCH
         UNPK  DBLWD(4),2(3,WORK4)
         TR    DBLWD(3),CCTAB-240
         CLC   DBLWD(3),13(WORK5)
         BE    UNITFND
         B     NXTUCB
SEARCH   EQU   *
         LH    WORK5,0(WORK2)
         LTR   WORK5,WORK5
         BM    INCRUCB
         LA    WORK2,2(WORK2)
         BZ    SEARCH
         BR    WORK7
UNITFND  EQU   *
         TM    34(WORK5),X'10'     PRIVATE- DASD-VOL  ?
         BNO   INCRUCB
         TM    3(WORK5),X'04'   PERM. RESIDENT?
         BO    INCRUCB
INCRBEL  EQU   *
         LA    R15,104(R12)
NXTPRIV  EQU   *
         CR    R15,WORK4
         BE    INCRBELX
         CLC   2(2,WORK4),2(R15)
         BE    INCRUCB
         LA    R15,8(R15)
         B     NXTPRIV
INCRBELX EQU   *
         L     R15,RECADDR
         SR    R1,R1
         CLI   DEVTYPE,X'80'
         BNE   NOTAP
         IC    R1,31(R15)
         LA    R1,1(R1)
         STC   R1,31(R15)
         B     NOLST
NOTAP    EQU   *
         CLI   DEVTYPE,X'20'
         BNE   NODISK
         IC    R1,30(R15)
         LA    R1,1(R1)
         STC   R1,30(R15)
         B     NOLST
NODISK   EQU   *
         CLC   DEVTYPE,=XL2'0810'
         BNE   NOLST
         OI    SWITCH,X'01'
NOLST    EQU   *
         LH    R15,DEVBELC
         LA    R15,1(R15)
         STH   R15,DEVBELC
         B     INCRUCB
TCTEND   EQU   *
         TM    SWITCH,X'40'
         BNO   ENDLZ
         L     DEVBAS,DEVBASSV
DEVNEXT  EQU   *
         CLC   DEVCHNC(40),=XL40'00'
         BE    ENDLZ
         L     R14,DEVEXCPC
         L     R15,ZGSU
         AR    R15,R14
         ST    R15,ZGSU
         MH    R14,DEVZF
         A     R14,ERRLZ
         ST    R14,ERRLZ
         LA    DEVBAS,40(DEVBAS)
         B     DEVNEXT
ENDLZ    EQU   *
         L     WORK2,ERRLZ
         SR    R2,R2
         D     R2,=F'100'
         L     R1,ORGR1SVA
         L     R1,20(R1)
         L     WORK5,0(R1)    STEP RUN TIME
         SRL   WORK5,8
        MH    WORK5,CPULF
         SR    WORK4,WORK4
        D     WORK4,=F'1000'
         AR    WORK2,WORK5
         L     R15,RECADDR
         SR    R1,R1
         IC    R1,30(R15)
         MH    R1,MNTDSK
         AR    WORK2,R1
         SR    R1,R1
         IC    R1,31(R15)
         MH    R1,MNTTAP
         AR    WORK2,R1
         TM    SWITCH,X'01'
         BNO   NOLST1
         LH    R1,MNTLST
         AR    WORK2,R1
NOLST1   EQU   *
         ST    WORK2,ERRLZ
         CLC   14(3,R15),=CL3'ONL'
         BNE   BATCH
         MVC   DBLWD,2(R15)
         L     WORK2,DBLWD
         MVC   DBLWD,39(R15)
         C     WORK2,DBLWD
         BH    TMEOK11
         A     WORK2,=F'8640000'
TMEOK11  EQU   *
         S     WORK2,DBLWD
         ST    WORK2,ERRLZ
BATCH    EQU   *
         MVC   MSGN,MSGN3
         L     WORK4,ORGR1SVA
         L     WORK5,4(WORK4)
         MVC   MSGN+6(8),0(WORK5)
         L     WORK5,RECADDR
         LA    WORK2,2(WORK5)
         LA    WORK3,MSGN+32
         BAL   LINK,GETZEIT
         BAL   LINK,WRITE
         MVC   MSGN,MSGN4
         MVC   MSGN+6(8),54(WORK5)
         MVC   DBLWD,2(WORK5)
         L     WORK2,DBLWD
         MVC   DBLWD,39(WORK5)
         L     WORK3,DBLWD
         CR    WORK3,WORK2
         BNH   TMEOK
         A     WORK2,=F'8640000'
TMEOK    EQU   *
         SR    WORK2,WORK3
         LA    WORK3,MSGN+29
         BAL   LINK,EDZTSTD
         BAL   LINK,WRITE
         MVC   MSGN,MSGN5
         SR    R15,R15
         IC    R15,53(WORK5)
         SH    R15,=H'11'
         SRL   R15,4
         CVD   R15,DBLWD
         UNPK  MSGN+8(2),DBLWD
         OI    MSGN+9,X'F0'
         LA    WORK3,MSGN+26
         L     WORK4,ORGR1SVA
         L     WORK2,24(WORK4)
         CLI   0(WORK2),X'00'
         BE    JOBACC
         BAL   LINK,GETACC
         B     ACCDA
JOBACC   EQU   *
         L     WORK2,16(WORK4)
         CLI   0(WORK2),X'00'
         BE    NOACC
         BAL   LINK,GETACC
         B     ACCDA
NOACC    EQU   *
         MVC   MSGN+28(4),=CL2'NA'
         MVC   MSGN+40(2),=CL2'NA'
ACCDA    EQU   *
         L     WORK2,ERRLZ
         LA    WORK3,MSGN+84
         BAL   LINK,EDZTSTD
         L     WORK2,LZPR
         LA    WORK3,MSGN+68
         BAL   LINK,EDPREIS
         M     R2,ERRLZ
         D     R2,=F'360000'   PREIS IN PF F. ERRLZ
         CVD   WORK2,DBLWD
         ED    MSGN+102(10),DBLWD+4
         BAL   LINK,ACCDMSTP
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         BAL   LINK,WRITE
         TM    SWITCH,X'40'
         BZ    NOIO
         MVC   MSGN,MSGN6
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         BAL   LINK,WRITE
         TM    SWITCH,X'20'
         BZ    NOPREISZ
         MVC   MSGN,MSGN7
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         L     DEVBAS,DEVBASSV
NEWLINE  LA    WORK2,MSGN
         LA    WORK3,6
PRTDVPR  EQU   *
         TM    DEVSWT,X'20'
         BNO   NOTKOPF
         MVC   0(4,WORK2),DEVNAME
         MVI   4(WORK2),C':'
         LH    WORK4,DEVZGPR
         CVD   WORK4,DBLWD
         MVC   6(2,WORK2),=C'0.'
         UNPK  8(5,WORK2),DBLWD
         OI    12(WORK2),X'F0'
         LA    WORK2,16(WORK2)
NOTKOPF  EQU   *
         LA    DEVBAS,40(DEVBAS)
         CLC   DEVCHNC(40),=XL40'00'
         BE    WRKOPF1
         BCT   WORK3,PRTDVPR
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         B     NEWLINE
WRKOPF1  BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         BAL   LINK,WRITE
NOPREISZ EQU   *
         MVC   MSGN,MSGN8
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         L     DEVBAS,DEVBASSV
         LA    WORK4,MSGN
         LA    WORK5,3
         L     R7,RECADDR
         LH    R6,102(R7)
         BCTR  R6,0
         BCTR  R6,0
         SRL   R6,3
         LTR   R6,R6
         BZ    WRBLANK
         LA    R7,104(R7)
XA       EQU   *
         CLC   DEVTYPE,0(R7)
         BE    XC
         LA    DEVBAS,40(DEVBAS)
         CLC   DEVCHNC(40),=XL40'00'
         BE    XD
         B     XA
XC       EQU   *
         L     WORK2,4(R7)
         LTR   WORK2,WORK2
         BZ    XD
         MVC   0(4,WORK4),DEVNAME
         UNPK  5(4,WORK4),2(3,R7)
         MVI   8(WORK4),X'40'
         TR    5(3,WORK4),CCTAB-240
         CVD   WORK2,DBLWD
         UNPK  9(7,WORK4),DBLWD
         OI    15(WORK4),X'F0'
         MH    WORK2,DEVZGPR
         L     R14,EAPRSU
         AR    R14,WORK2
         ST    R14,EAPRSU
         SR    R2,R2
         D     R2,=F'1000'
         MVC   16(10,WORK4),=CL10' 슬 습.슬'
         CVD   WORK2,DBLWD
         ED    16(10,WORK4),DBLWD+4
         LA    WORK4,33(WORK4)
         BCT   R5,XD
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         LA    WORK4,MSGN
         LA    WORK5,3
XD       EQU   *
         LA    R7,8(R7)
         L     DEVBAS,DEVBASSV
         BCT   R6,XA
         LA    R15,MSGN
         CR    WORK4,R15
         BE    WRBLANK
         BAL   LINK,WRITE
WRBLANK  EQU   *
         BAL   LINK,CLEARMSG
         BAL   LINK,WRITE
         MVC   MSGN,MSGN9
         L     WORK4,ZGSU
         CVD   WORK4,DBLWD
         UNPK  MSGN+63(9),DBLWD
         OI    MSGN+71,X'F0'
         L     WORK2,EAPRSU
         SR    R2,R2
         D     R2,=F'1000'
         BAL   LINK,ACCDMSTP
         CVD   WORK2,DBLWD
         ED    MSGN+102(10),DBLWD+4
         BAL   LINK,WRITE
NOIO     EQU   *
         L     WORK2,RECADDR
         CLC   47(4,WORK2),=F'0'
         BE    NOSYSIN
         MVC   DBLWD,47(WORK2)
         L     WORK2,DBLWD
         CVD   WORK2,DBLWD
         MVC   MSGN,MSGN10
         ED    MSGN+40(7),DBLWD+5
         LH    WORK5,SINPR
         CVD   WORK5,DBLWD
         UNPK  MSGN+70(5),DBLWD
         OI    MSGN+74,X'F0'
         MH    WORK2,SINPR
         SR    R2,R2
         D     R2,=F'1000'
         BAL   LINK,ACCDMSTP
         CVD   WORK2,DBLWD
         ED    MSGN+102(10),DBLWD+4
         BAL   LINK,WRITE
NOSYSIN  EQU   *
*               BELEGUNGSPREISE AUSDRUCKEN
         L     DEVBAS,DEVBASSV
         LTR   DEVBAS,DEVBAS
         BZ    NOBEL
BELPRNXT EQU   *
         TM    DEVSWT,X'80'
         BNO   INCRDVBL
         CLC   DEVBELC,=H'0'
         BE    INCRDVBL
         MVC   MSGN,MSGN12
         LH    WORK2,DEVBELC
         CVD   WORK2,DBLWD
         UNPK  MSGN+63(2),DBLWD
         OI    MSGN+64,X'F0'
         M     WORK3,DEVBELPR     PREIS F. XX EINHEITEN IN PF. F. 1 STD
         M     WORK3,ERRLZ
         D     R2,=F'360000'
         BAL   LINK,ACCDMSTP
         CVD   WORK2,DBLWD
         ED    MSGN+102(10),DBLWD+4
         MVC   MSGN+66(15),DEVTEXT
         L     WORK4,DEVBELPR
         CVD   WORK4,DBLWD
         ED    MSGN+85(7),DBLWD+5
         BAL   LINK,WRITE
INCRDVBL EQU   *
         LA    DEVBAS,40(DEVBAS)
         CLC   DEVCHNC(40),=XL40'00'
         BNE   BELPRNXT
NOBEL    EQU   *
         BAL   LINK,CLEARMSG
         MVI   MSGN+98,C'-'
         MVC   MSGN+99(13),MSGN+98
         BAL   LINK,WRITE
         MVC   MSGN,MSGN13
         L     WORK2,DMSTEP
         CVD   WORK2,DBLWD
         LR    WORK7,WORK2
         ED    MSGN+102(10),DBLWD+4
         BAL   LINK,WRITE
         L     WORK7,COMUEDAT
         A     WORK2,DMSUMME
         ST    WORK2,DMSUMME
         BAL   LINK,CLEARMSG
         MVI   MSGN+98,C'='
         MVC   MSGN+99(13),MSGN+98
         BAL   LINK,WRITE
         MVI   MSG,C'*'
         MVC   MSG+1(117),MSG
         BAL   LINK,WRITE
         L     R15,RECADDR
         LH    R14,102(R15)
         BCTR  R14,0
         BCTR  R14,0
         SRL   R14,3
         LTR   R14,R14
         BZ    DELPREIS
         L     DEVBAS,DEVBASSV
NXTXC    EQU   *
         XC    0(10,DEVBAS),0(DEVBAS)
         LA    DEVBAS,40(DEVBAS)
         CLC   DEVCHNC(40),=XL40'00'
         BNE   NXTXC
         DELETE EPLOC=DEVLOC
DELPREIS EQU   *
         DELETE EPLOC=PREISLOC
RETURN   EQU   *
XRET     XRETURN 0,R
JOBEND   EQU   *
         L     R14,RECADDR
         CLI   38(R14),X'00'
         BE    XRET
         MVI   MSG,C'*'
         MVC   MSG+1(117),MSG
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         MVC   MSGN,MSGN15
         L     WORK4,COMEXTAB
         MVC   MSGN+4(8),0(WORK4)
         LA    WORK2,12(WORK4)
         LA    WORK3,MSGN+25
         BAL   LINK,GETDAT
         MVC   MSGN+77(4),16(WORK4)
         L     R15,16
         SH    R15,=H'6'
         UNPK  MSGN+57(4),0(3,R15)
         MVI   MSGN+60,C' '
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         BAL   LINK,WRITE
         MVC   MSGN,MSGN16
         L     WORK5,RECADDR
         LA    WORK2,39(WORK5)
         LA    WORK3,MSGN+19
         BAL   LINK,GETZEIT
         MVC   MSGN+53(1),71(WORK5)
         L     CUEDBAS,COMUEDAT
         BAL   LINK,WRITE
         MVC   MSGN,MSGN17
         LA    WORK2,2(WORK5)
         LA    WORK3,MSGN+19
         BAL   LINK,GETZEIT
         SR    R15,R15
         IC    R15,38(WORK5)
         CVD   R15,DBLWD
         UNPK  MSGN+52(2),DBLWD
         OI    MSGN+53,X'F0'
         BAL   LINK,WRITE
         MVC   MSGN,MSGN18
         MVC   DBLWD,2(WORK5)
         L     WORK2,DBLWD
         MVC   DBLWD,39(WORK5)
         L     WORK3,DBLWD
         CR    WORK3,WORK2
         BNH   *+8
         A     WORK2,=F'8640000'
         SR    WORK2,WORK3
         LA    WORK3,MSGN+16
         BAL   LINK,EDZTSTD
         LA    WORK2,22(WORK5)
         LA    WORK3,MSGN+44
         BAL   LINK,GETZEIT
         MVC   DBLWD,39(WORK5)
         L     WORK3,DBLWD
         MVC   DBLWD,54(WORK5)
         L     WORK2,DBLWD
         CR    WORK3,WORK2
         BH    *+8
         A     WORK3,=F'8640000'
         SR    WORK3,WORK2
         LR    WORK2,WORK3
         LA    WORK3,MSGN+86
         BAL   LINK,EDZTSTD
         BAL   LINK,WRITE
         MVC   MSGN,MSGN19
         MVC   DBLWD,113(WORK5)
         L     WORK2,DBLWD
         SRL   WORK2,8
         LA    WORK3,MSGN+16
         BAL   LINK,EDZTSTD
         LA    WORK2,54(WORK5)
         LA    WORK3,MSGN+44
         BAL   LINK,GETZEIT
         LA    WORK2,117(WORK5)
         LA    WORK3,MSGN+67
         BAL   LINK,GETACC
         SR    R15,R15
         IC    R15,53(WORK5)
         CVD   R15,DBLWD
         UNPK  MSGN+97(2),DBLWD
         OI    MSGN+98,X'F0'
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         BAL   LINK,WRITE
         MVC   MSGN,MSGN20
         L     WORK2,DMSUMME
         CVD   WORK2,DBLWD
         ED    MSGN+28(12),DBLWD+3
         L     R15,16
         CLI   116(R15),X'13'
         BNE   TVS1
         MVC   MSGN+84(5),=CL5'  VS2'
         B     REL
TVS1     EQU   *
         CLI   116(R15),X'22'
         BNE   TVSMP
         MVC   MSGN+84(5),=CL5'  VS1'
         B     REL
TVSMP    EQU   *
         CLI   116(R15),X'17'
         BNE   SYSUKNO
         MVC   MSGN+84(5),=CL5'VS2MP'
         B     REL
SYSUKNO  EQU   *
         MVC   MSGN+84(5),=CL5'*****'
REL      EQU   *
         LA    R14,4
         SR    R15,R14
         MVC   MSGN+95(2),0(R15)
         MVI   MSGN+97,C'.'
         MVC   MSGN+98(1),2(R15)
         BAL   LINK,WRITE
         BAL   LINK,CLEARMSG
         MVI   MSGN+26,C'='
         MVC   MSGN+27(13),MSGN+26
         BAL   LINK,WRITE
         MVI   MSG,C'*'
         MVC   MSG+1(117),MSG
         BAL   LINK,WRITE
         L     R1,COMUEDAT
         FREEMAIN R,LV=16,A=(1),SP=255
         L     R15,COMEXTAB
         XC    32(4,R15),32(R15)
         B     RETURN
ACCDMSTP EQU   *
         L     R0,DMSTEP
         AR    R0,WORK2
         ST    R0,DMSTEP
         BR    LINK
WRITE    EQU   *
         LR    R0,R14
         L     R15,=V(IEFYS)
         L     R12,LCTRSVA
         LA    R13,SVA1
         BALR  R14,R15
         SH    R13,=H'72'
         LR    R14,R0
         BR    R14
EDPREIS  EQU   *    WORK2 = DM IN PFENNIGEN
*                   WORK3 = OUTPUT-LOCATION
         CVD   WORK2,DBLWD
         MVO   DBLWD,DBLWD(7)
         ED    0(8,WORK3),DBLWD+5
         MVI   8(WORK3),C'0'
         BR    LINK
GETDAT   EQU   *     WORK2= A(DATUM(PACKED)) WORK3 = A(OUTPUT)
         UNPK  10(3,WORK3),1(2,WORK2)
         MVI   12(WORK3),X'40'
         XC    DBLWD,DBLWD
         LH    R1,0(WORK2)
         SLL   R1,4
         ST    R1,DBLWD+4
         OI    DBLWD+7,X'0C'
         CVB   R1,DBLWD
         SR    R0,R0
         D     R0,=F'4'
         LTR   R0,R0
         BNZ   NOSCHALT
         MVC   MONATE+2(2),=H'29'
NOSCHALT EQU   *
         XC    DBLWD,DBLWD
         MVC   DBLWD+6(2),2(WORK2)
         CVB   WORK0,DBLWD
         ST    WORK0,DBLWD+4
         LA    WORK12,MONATE
         SR    WORK0,WORK0
         SR    R15,R15
NEXTMON  EQU   *
         AH    WORK0,0(R15,WORK12)
         C     WORK0,DBLWD+4
         BL    INCRMON
         SH    WORK0,0(R15,WORK12)
         SRL   R15,1
         LA    R15,1(R15)              R15 = MONAT
         L     WORK12,DBLWD+4
         SR    WORK12,WORK0            WORK12 = TAG
         CVD   WORK12,DBLWD
         UNPK  0(2,WORK3),DBLWD
         OI    1(WORK3),X'F0'
         CVD   R15,DBLWD
         UNPK  4(2,WORK3),DBLWD
         OI    5(WORK3),X'F0'
         BR    LINK
INCRMON  EQU   *
         LA    R15,2(R15)
         B     NEXTMON
CLEARMSG EQU   *
         MVI   MSG+1,X'40'
         MVC   MSG+2(115),MSG+1
         BR    LINK
GETZEIT  EQU   *  WORK2 = A(ZEIT), WORK3= A(OUTPUT-LOC.)
         MVC   DBLWD,0(WORK2)
         SR    R0,R0
         L     R1,DBLWD
         D     R0,=F'100'         ZEIT IN SEC.
CONT     EQU   *
         SR    R0,R0
         D     R0,=F'60'
         CVD   R0,DBLWD
        UNPK  8(2,WORK3),DBLWD
         OI    9(WORK3),X'F0'
         SR    R0,R0
         D     R0,=F'60'          SEKUNDEN
         CVD   R0,DBLWD
         UNPK  5(2,WORK3),DBLWD
        OI    6(WORK3),X'F0'
         CVD   R1,DBLWD
         UNPK  2(2,WORK3),DBLWD
         OI    3(WORK3),X'F0'
         BR    LINK
EDZTSTD  EQU   *
         LR    R1,WORK2
         SR    R0,R0
         D     R0,=F'100'
         CVD   R0,DBLWD
         UNPK  11(2,WORK3),DBLWD
         OI    12(WORK3),X'F0'
         B     CONT
GETACC   EQU   *
         SR    R15,R15
         CLI   0(WORK2),X'04'
         BH    L4
         IC    R15,0(WORK2)
         B     ARBOK
MVCEX01  MVC   0(0,WORK3),1(WORK2)
L4       EQU   *
         LA    R15,4
ARBOK    EQU   *
         BCTR  R15,0
         EX    R15,MVCEX01
         LA    WORK2,2(R15,WORK2)
         CLI   0(WORK2),X'00'
         BE    KZNA
         SR    R1,R1
         IC    R1,0(WORK2)
         LA    WORK2,1(R1,WORK2)
         CLI   0(WORK2),X'00'
         BE    KZNA
         MVC   15(1,WORK3),1(WORK2)
         BR    LINK
KZNA     EQU   *
         MVC   14(2,WORK3),=CL2'NA'
         BR    LINK
         REG
LINK     EQU   14
LCTREG   EQU   12
BASE     EQU   11
PREISBAS EQU   10
DEVBAS   EQU   9
MSGNBAS  EQU   8
CUEDBAS  EQU   7
WORK0    EQU   0
WORK2    EQU   3
WORK3    EQU   2
WORK4    EQU   4
WORK5    EQU   5
WORK7    EQU   7
WORK9    EQU   9
WORK12   EQU   12
CCTAB    DC    C'0123456789ABCDEF'
         LTORG
MONATE   DS    0H
         DC    H'31'
         DC    H'28'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DEVTAB
         PREISTAB
CUED     DSECT
DMSUMME  DS    F        SUMME ALLER PREISE
MSGNAGES DSECT
MSGN1    DS    CL113
MSGN2    DS    CL113
MSGN3    DS    CL113
MSGN4    DS    CL113
MSGN5    DS    CL113
MSGN6    DS    CL113
MSGN7    DS    CL113
MSGN8    DS    CL113
MSGN9    DS    CL113
MSGN10   DS    CL113
MSGN12   DS    CL113
MSGN13   DS    CL113
MSGN15   DS    CL113
MSGN16   DS    CL113
MSGN17   DS    CL113
MSGN18   DS    CL113
MSGN19   DS    CL113
MSGN20   DS    CL113
WORK     DSECT
SVA      DS    18F
SVA1     DS    45F
CLEARANF EQU   *
DBLWD    DS    D
ZGSU     DS    F             SUMME DER E/A-OP.
ERRLZ    DS    F            ERRECHNETE LAUFZEIT IN 1/100 SEC.
EAPRSU   DS    F            SUMME DER PREISE FUER E/A-OP.
MSG      DS    CL132
         ORG   MSG+3
MSGN     DS    CL113
         ORG
SWITCH   DS    X
DEVBASSV DS    F            SVA FUER BASISREG. V. DSECT DEVICES
PRBASSVA DS    F            SVA FUER BASISREG. V. DSECT PREISTAB
LCTRSVA  DS    F            SVA FUER A(LCT)
ORGR1SVA DS    F            SVA FUER URSPRUENGL. REG. 1
MSGBASSV DS    F            SVA FUER BASISREG. V. DSECT MSGNAGES
COMEXTAB DS    F            A(COMMON-EXIT-TABLE)
COMUEDAT DS    F            A(COMMON-USER-EXIT-DATA)
RECADDR  DS    F            ADDR. DES TYP 5 BZW. TYP 4 SMF-RECORDS
DMSTEP   DS    F
PREISLOC DS    CL8
DEVLOC   DS    CL8
CLEARLEN EQU   *-CLEARANF
WORKL    EQU   *-WORK
         DROP
ACTRTMSG CSECT
MESS1    DC    CL113'음. STEP DES JOBS, GELAUFEN AM XX. XX. 19XX AUF DE*
               R ANLAGE 370/000 BASF-SYSTEM-NR. XX.     CC = S  000'
MESS2    DC    CL113'JOB   12345678   UHRZEIT-ANFANG = 음:슬:슬        *
                CPU-ZEIT (DM  습.슬/STD)      =  XX.XX.XX,XX = DM 슬 *
               습.슬'
MESS3    DC    CL113'STEP  12345678   UHRZEIT-ENDE   = 음:슬:슬'
MESS4    DC    CL113'PROGR 12345678   LAUFZEIT  =   XX.XX.XX,XX'
MESS5    DC    CL113'DPRTY = 습       ARB-NR =          KZ =  X        *
                ERR. LAUFZEIT (DM  습.슬/STD) =  XX.XX.XX,XX = DM 슬 *
               습.슬'
MESS6    DC    CL113'------------------------------------  E / A   EINH*
               EITEN  ------------------------------------'
MESS7    DC    CL113'PREISE IN DM FUER JE EINE E/A-OPERATION:'
MESS8    DC    CL113'TYPE ADR E/A-OP.     DM          TYPE ADR E/A-OP. *
                   DM          TYPE ADR E/A-OP.     DM'
MESS9    DS    0CL113
         DC    CL42' '
         DC    CL71'PREIS FUER INSGESAMT  슬 습 E/A-OPERATION(EN)   *
                    = DM 슬 습.슬'
MESS10   DS    0CL113
         DC    CL41' '
         DC    CL72'슬 습 SYSIN-KARTE(N) A DM 0.음슬                 *
                     = DM 슬 습.슬'
MESS12   DS    0CL113
         DC    CL42' '
         DC    CL71'BELEGUNGSKOSTEN FUER XX                 (DM 습.슬/*
               STD) = DM 슬 습.슬'
MESS13   DS    0CL113
         DC    CL42' '
         DC    CL71'SUMME INCL. CPU-, LAUFZEIT- UND REGION-KOSTEN      *
                    = DM 슬 습.슬'
MESS15   DC    CL113'JOB 12345678 GELAUFEN AM XX. XX. 19XX AUF DER ANLA*
               GE 370/000 BASF-SYSTEM-NR. XX.'
MESS16   DC    CL113'JOB-START-ZEIT =     음:슬:슬   JOB-KLASSE        *
               =  X'
MESS17   DC    CL113'JOB-ENDE -ZEIT =     음:슬:슬   ANZAHL DER STEPS  *
               = 습'
MESS18   DC    CL113'JOB-LAUFZEIT   =  XX.XX.XX,XX   SYSIN-START = 음:*
               :슬    QUEUE-VERWEILZEIT =           XX.XX.XX,XX'
MESS19   DC    CL113'JOB-CPU-ZEIT   =  XX.XX.XX,XX   SYSIN-ENDE  = 음:*
               :슬    ARB-NR =          KZ =  X       PRTY = XX'
MESS20   DC    CL113'FUER DIESEN JOB WERDEN    DM 슬슬 습.슬    VERREC*
               HNET. (OHNE SYSOUT)   OS-VERSION: 12345/REL. 1234'
         END
