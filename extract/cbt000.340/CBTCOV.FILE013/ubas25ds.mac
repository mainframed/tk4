//ASMHTIM  JOB (9999,999,1),MSGCLASS=T,
//   CLASS=R
//LUHTIM EXEC ASMFCL,LEPRM=',REUS'
//ASM.SYSIN DD *
LUHTIM   CSECT
         SPACE
**********************************************************************
*
*   PROGRAMMER :  H. FRANZ - BASF AG
*
**********************************************************************
         SPACE
         XSAVE R12,SVA,LUHTIM
         REG
         SR    R3,R3          CLEAR WORK REG 3
         LA    R4,8           SET MAX REQUEST COUNT
         LR    R2,R1          SAVE PARAMETERLIST ADDR
         SPACE 2
GETNUMB  EQU   *
         LA    R3,1(R3)       ADD 1 TO REQUEST REG
         TM    4(R2),X'80'    LAST REQUEST ?
         BO    GETROUT        YES
         LA    R2,8(R2)       GET NEXT ADDR
         BCT   R4,GETNUMB
         SPACE
         B     PARMERR
         SPACE 2
GETROUT  EQU   *
         ST    R3,REQCNT      SAVE REQUEST NUMBER
         L     R2,0(R1)       GET FIRST PARAMETER ADDR
         CLI   0(R2),C' '     TEST WHICH ROUTINE IS NEEDED ?
         BE    TIMEBIN
         LR    R5,R1          SAVE PARAMETER POINTER
         EJECT
******************************************
*                                        *
*     TESTEN DER EINGABEDATEN AUF        *
*     NUMMERISCH ENTPACKT                *
*                                        *
******************************************
         SPACE
TESTIN   EQU   *
         TRT   0(6,R2),TRTIN
         BNZ   INERROR        ERROR IN INPUT
         SPACE
         LA    R5,8(R5)       GET ADDR OF NEXT REQUEST
         L     R2,0(R5)       GET ADDR OF DATA AREA
         BCT   R3,TESTIN
         SPACE
         L     R3,REQCNT      GET REQUEST NUMBER
         L     R2,0(R1)       GET FIRST PARAMETER ADDR
         SPACE 2
******************************************
*                                        *
*     UMRECHNUNGS - ROUTINE              *
*     HHMMSS ==> 1/100 SECONDS           *
*                                        *
******************************************
         SPACE
TIMEDEC  EQU   *
         XC    DOUBLEW,DOUBLEW    CLEAR WORK FIELD
         PACK  DOUBLEW,0(2,R2)  PACK HOURS
         CVB   R14,DOUBLEW      GET HOURS IN BINARY
         MH    R14,=H'3600'   GET HOURS IN SECONDS
         PACK  DOUBLEW,2(2,R2)  PACK MINUTES
         CVB   R15,DOUBLEW      GET MINUTES IN BINARY
         MH    R15,=H'60'     GET MINUTES IN SECONDS
         AR    R15,R14        ADD SECONDS
         PACK  DOUBLEW,4(2,R2)  PACK SECONDS
         CVB   R14,DOUBLEW      GET SECONDS IN BINARY
         AR    R15,R14        ADD SECONDS
         MH    R15,=H'100'    GET TIME IN 1/100 SECONDS
         L     R2,4(R1)       LOAD FIRST PARAMETER ADDR
         ST    R15,0(R2)      STORE TIME BACK
         LA    R1,8(R1)       GET ADDR OF NEXT REQUEST
         L     R2,0(R1)       GET ADDR OF NEXT REQUEST
         BCT   R3,TIMEDEC
         SPACE
         SR    R5,R5
         B     XRETURN
         EJECT
******************************************
*                                        *
*     UMRECHNUNGS - ROUTINE              *
*     TIME IN 1/100 SEC. ==> HHMMSS      *
*                                        *
******************************************
         SPACE
TIMEBIN  EQU   *
         L     R2,4(R1)       GET ADDR OF TIME
         XC    DOUBLEW,DOUBLEW CLEAR WORK FIELD
         L     R15,0(R2)      GET TIME IN 1/100 SECONDS
         L     R2,0(R1)       GET ADDR OF DATA AREA
         SR    R14,R14        CLEAR WORK REG 14
         D     R14,=F'100'    GET TIME IN SECONDS
         SR    R14,R14        CLEAR WORK REG 14
         D     R14,=F'60'     GET TIME IN MINUTES
         CVD   R14,DOUBLEW    GET SECONDS IN DECIMAL
         UNPK  DOUBLEW(4),DOUBLEW+6(2)
         OI    DOUBLEW+3,X'F0' MAKE PRINTABLE
         MVC   4(2,R2),DOUBLEW+2 STORE SECONDS BACK
         SR    R14,R14        CLEAR WORK REG 14
         D     R14,=F'60'     GET TIME IN HOURS
         CVD   R14,DOUBLEW    GET MINUTES IN DECIMAL
         UNPK  DOUBLEW(4),DOUBLEW+6(2)
         OI    DOUBLEW+3,X'F0' MAKE PRINTABLE
         MVC   2(2,R2),DOUBLEW+2 STORE MINUTES BACK
         XC    DOUBLEW,DOUBLEW CLEAR WORK FIELD
         CVD   R15,DOUBLEW    GET HOURS IN DECIMAL
         UNPK  DOUBLEW(4),DOUBLEW+6(2)
         OI    DOUBLEW+3,X'F0' MAKE PRINTABLE
         MVC   0(2,R2),DOUBLEW+2 STORE HOURS BACK
         LA    R1,8(R1)
         BCT   R3,TIMEBIN
         SPACE
         SR    R5,R5          CLEAR RETURN REG
         EJECT
******************************************
*                                        *
*     RETURN TO CALLER                   *
*                                        *
******************************************
         SPACE
XRETURN  EQU   *
         XRETURN (R5)
         SPACE 2
INERROR  EQU   *
         LA    R5,8           SET ERROR RETURN CODE
         SPACE
         WTO   'LUHTIM01 EINGABEDATEN NICHT ENTPACKT',ROUTCDE=11
         B     XRETURN
         SPACE 2
PARMERR  EQU   *
         LA    R5,8           SET ERROR RETURN CODE
         SPACE
         WTO   'LUHTIM02 FEHLER IN PARAMETERLISTE',ROUTCDE=11
         B     XRETURN
         EJECT
******************************************
*                                        *
*    D E F I N I T I O N E N             *
*                                        *
******************************************
         SPACE
DOUBLEW  DS    D
         SPACE
REQCNT   DS    F
         SPACE
*****************************************************
TRTIN    DS    0XL256
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'40404040404040404040404040404040'  0
         DC    X'40404040404040404040404040404040'  1
         DC    X'40404040404040404040404040404040'  2
         DC    X'40404040404040404040404040404040'  3
         DC    X'40404040404040404040404040404040'  4
         DC    X'40404040404040404040404040404040'  5
         DC    X'40404040404040404040404040404040'  6
         DC    X'40404040404040404040404040404040'  7
         DC    X'40404040404040404040404040404040'  8
         DC    X'40404040404040404040404040404040'  9
         DC    X'40404040404040404040404040404040'  A
         DC    X'40404040404040404040404040404040'  B
         DC    X'40404040404040404040404040404040'  C
         DC    X'40404040404040404040404040404040'  D
         DC    X'40404040404040404040404040404040'  E
         DC    X'00000000000000000000404040404040'  F
*****************************************************
         SPACE
         LTORG
         END
//LKED.SYSLMOD DD DSN=XX.XXXXXX(LUHTIM),DISP=SHR
//
