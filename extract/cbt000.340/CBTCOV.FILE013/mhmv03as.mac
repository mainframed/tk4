         MACRO
&NAME    TRTAB &PARM
.*
.*       TRTAB GENERIERT 256 BYTES GROSSE TABELLEN FUER TRT UND TR
.* DIE TABELLEN WERDEN IN HEXADEZIMALER DARSTELLUNG GENERIERT
.*
.* PARAMETER:
.* ND    ALLE NICHT DRUCKBAREN ZEICHEN WERDEN DURCH X'00' ERSETZT
.* N1    ES WIRD EINE TABELLE ZUM PRUEFEN GEPACKTER ZAHLEN OHNE
.*       VORZEICHEN GENERIERT
.* N2    ES WIRD EINE TABELLE ZUM PRUEFEN DES VORZEICHENBYTES
.*       GEPACKTER ZAHLEN GENERIERT
.* NUM   ES WIRD EINE TABELLE ZUM PRUEFEN UNGEPACKTER ZAHLEN GENERIERT
.* ALPHA ES WIRD EINE TABELLE ZUM PRUEFEN AUF ALPHANUMERISCHE ZEICHEN
.*       GENERIERT
.* XX    ALLE NICHT DRUCKBAREN ZEICHEN WERDEN DURCH DAS ZEICHEN
.*       X'XX' ERSETZT
.*
.* JEDE BELIEBIGE STELLE IN DER TABELLE KANN DURCH EIN GEWUENSCHTES
.* HEXADEZIMALES ZEICHEN ERSETZT WERDEN:
.*       (VON STELLE,LAENGE,ZEICHEN)
.*       VON DER GEWUENSCHTEN STELLE WIRD IN DER ANGEGEBENEN LAENGE
.*       DAS ENTSPRECHENDE ZEICHEN IN DIE TABELLE EINGEFUEGT
.*       ES KOENNEN BIS ZU 90 KOMBINATIONEN ANGEGEBEN WERDEN.
.*
         GBLC  &P(255),&PP,&HX
         LCLA  &Z1,&Z2,&Z3,&Z4,&Z5,&VON,&VONH,&LG,&LGH,&BIS,&ANZ,&ANZH
         LCLB  &SCH1,&SCH2,&SCH3
         LCLC  &X(2)
&Z1      SETA  1 LADEN ANFANGSWERTE
&Z2      SETA  1
&Z3      SETA  1
&Z4      SETA  1
&Z5      SETA  1
&SCH3    SETB  (T'&PARM EQ 'O') PARAMETER VORHANDEN ?
         AIF   (&SCH3 EQ 1).S02 NEIN
         AIF   ('&PARM' EQ 'NUM').S30
         AIF   ('&PARM' EQ 'ALPHA').S35
         AIF   ('&PARM' EQ 'ND').S26
         AIF   ('&PARM' EQ 'N1' OR '&PARM' EQ 'N2').S17
&SCH1    SETB  ('&PARM'(1,1) EQ '(')
&SCH2    SETB  (K'&PARM EQ 2) PARAMETER ZWEISTELLIG ?
.S02     ANOP
&X(1)    SETC  '01234567' KONSTANTEN FUER TABELLEN -
&X(2)    SETC  '89ABCDEF' GRUNDSTELLUNG
.S03     ANOP
&P(&Z1)  SETC  '&X(&Z2)'(&Z3,1).'&X(&Z4)'(&Z5,1)
&Z1      SETA  &Z1+1
         AIF   (&Z1 EQ 256).S05
&Z5      SETA  &Z5+1
         AIF   (&Z5 LT 9).S03
&Z4      SETA  &Z4+1
&Z5      SETA  1
.S04     AIF   (&Z4 LT 3).S03
&Z3      SETA  &Z3+1
&Z4      SETA  1
&Z5      SETA  1
         AIF   (&Z3 LT 9).S03
&Z2      SETA  &Z2+1
&Z3      SETA  1
         AIF   (&Z2 LT 3).S03
.S05     ANOP
&PP      SETC  'FF'
         AIF   (&SCH1 EQ 1).S06
         AIF   (&SCH2 EQ 1).S22
         AIF   (&SCH3 EQ 1).S25
&HX      SETC  '&PARM'
         MNOTE *,'TEXT BEGINNING ''&HX'' SKIPPED'
         AGO   .S25
.S06     ANOP
&ANZH    SETA  N'&SYSLIST(1)
&ANZ     SETA  &ANZH/3
&VONH    SETA  1
&Z1      SETA  1
&Z2      SETA  1
.S07     ANOP
&VON     SETA  &SYSLIST(1,&Z1)
         AIF   (&VONH GT &VON).S165
&VONH    SETA  &VON
         AIF   (&VON LT 1).S10
         AIF   (&VON GT 256).S10
&Z1      SETA  &Z1+1
&LG      SETA  &SYSLIST(1,&Z1)
         AIF   (&LG GT 257-&LGH).S11
&LGH     SETA  &LGH+&LG
&Z1      SETA  &Z1+1
&HX      SETC  '&SYSLIST(1,&Z1)'
         AIF   (T'&SYSLIST(1,&Z1-2) EQ 'O' OR T'&SYSLIST(1,&Z1-1) EQ 'OX
               ' OR T'&SYSLIST(1,&Z1) EQ 'O').S12
         AGO   .S13
.S10     IHBERMAC 36,,&VON
         MEXIT
.S11     IHBERMAC 44,,&LG
         MEXIT
.S12     IHBERMAC 171
         MEXIT
.S13     ANOP
&BIS     SETA  &VON+&LG
         AIF   (&BIS LE 257).S14
         AGO   .S11
.S14     AIF   (&VON LT 256).S15
&PP      SETC  '&HX'
         AGO   .S16
.S15     ANOP
&P(&VON) SETC  '&HX'
.S16     ANOP
&VON     SETA  &VON+1
         AIF   (&VON LT &BIS).S14
&ANZ     SETA  &ANZ-1
         AIF   (&ANZ EQ 0).S25
&Z1      SETA  &Z1+1
         AIF   (&ANZ GT 0).S07
.S165    IHBERMAC 192
         MEXIT
.S17     AIF   (T'&NAME NE 'O').S18
TAB&SYSNDX EQU *
         AGO   .S191
.S18     ANOP
&NAME    EQU *
.S191    AIF   ('&PARM' EQ 'N2').S20
.S192    DC    X'00000000000000000000FFFFFFFFFFFF'
&Z1      SETA  &Z1+1
         AIF   (&Z1 LT 11).S192
         AGO   .S21
.S20     DC    X'FFFFFFFFFFFFFFFFFFFFFFFF0000FF00'
&Z1      SETA  &Z1+1
         AIF   (&Z1 LT 11).S20
.S21     ANOP
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
&Z1      SETA  &Z1+1
         AIF   (&Z1 LT 17).S21
         MEXIT
.S22     ANOP
&HX      SETC  '&SYSLIST(1,1)'
         SUBTRTAB (75,82,91,99,108,113,123,129,194,203,210,219,227,235,X
               241,251,256)
&P(65)   SETC  '40'
.S25     ANOP
&NAME    DC    X'&P(1)&P(2)&P(3)&P(4)&P(5)&P(6)&P(7)&P(8)&P(9)&P(10)&P(X
               11)&P(12)&P(13)&P(14)&P(15)&P(16)'
         DC    X'&P(17)&P(18)&P(19)&P(20)&P(21)&P(22)&P(23)&P(24)&P(25)X
               &P(26)&P(27)&P(28)&P(29)&P(30)&P(31)&P(32)'
         DC    X'&P(33)&P(34)&P(35)&P(36)&P(37)&P(38)&P(39)&P(40)&P(41)X
               &P(42)&P(43)&P(44)&P(45)&P(46)&P(47)&P(48)'
         DC    X'&P(49)&P(50)&P(51)&P(52)&P(53)&P(54)&P(55)&P(56)&P(57)X
               &P(58)&P(59)&P(60)&P(61)&P(62)&P(63)&P(64)'
         DC    X'&P(65)&P(66)&P(67)&P(68)&P(69)&P(70)&P(71)&P(72)&P(73)X
               &P(74)&P(75)&P(76)&P(77)&P(78)&P(79)&P(80)'
         DC    X'&P(81)&P(82)&P(83)&P(84)&P(85)&P(86)&P(87)&P(88)&P(89)X
               &P(90)&P(91)&P(92)&P(93)&P(94)&P(95)&P(96)'
         DC    X'&P(97)&P(98)&P(99)&P(100)&P(101)&P(102)&P(103)&P(104)&X
               P(105)&P(106)&P(107)&P(108)&P(109)&P(110)&P(111)&P(112)'
         DC    X'&P(113)&P(114)&P(115)&P(116)&P(117)&P(118)&P(119)&P(12X
               0)&P(121)&P(122)&P(123)&P(124)&P(125)&P(126)&P(127)&P(12X
               8)'
         DC    X'&P(129)&P(130)&P(131)&P(132)&P(133)&P(134)&P(135)&P(13X
               6)&P(137)&P(138)&P(139)&P(140)&P(141)&P(142)&P(143)&P(14X
               4)'
         DC    X'&P(145)&P(146)&P(147)&P(148)&P(149)&P(150)&P(151)&P(15X
               2)&P(153)&P(154)&P(155)&P(156)&P(157)&P(158)&P(159)&P(16X
               0)'
         DC    X'&P(161)&P(162)&P(163)&P(164)&P(165)&P(166)&P(167)&P(16X
               8)&P(169)&P(170)&P(171)&P(172)&P(173)&P(174)&P(175)&P(17X
               6)'
         DC    X'&P(177)&P(178)&P(179)&P(180)&P(181)&P(182)&P(183)&P(18X
               4)&P(185)&P(186)&P(187)&P(188)&P(189)&P(190)&P(191)&P(19X
               2)'
         DC    X'&P(193)&P(194)&P(195)&P(196)&P(197)&P(198)&P(199)&P(20X
               0)&P(201)&P(202)&P(203)&P(204)&P(205)&P(206)&P(207)&P(20X
               8)'
         DC    X'&P(209)&P(210)&P(211)&P(212)&P(213)&P(214)&P(215)&P(21X
               6)&P(217)&P(218)&P(219)&P(220)&P(221)&P(222)&P(223)&P(22X
               4)'
         DC    X'&P(225)&P(226)&P(227)&P(228)&P(229)&P(230)&P(231)&P(23X
               2)&P(233)&P(234)&P(235)&P(236)&P(237)&P(238)&P(239)&P(24X
               0)'
         DC    X'&P(241)&P(242)&P(243)&P(244)&P(245)&P(246)&P(247)&P(24X
               8)&P(249)&P(250)&P(251)&P(252)&P(253)&P(254)&P(255)&PP'
         MEXIT
.S26     ANOP
&P(&Z1)  SETC  '00'
&Z1      SETA  &Z1+1
         AIF   (&Z1 LT 256).S26
&PP      SETC  '00'
&HX      SETC  'FF'
         SUBTRTAB (75,82,91,99,108,113,123,129,194,203,210,219,227,235,X
               241,251,256)
&P(65)   SETC  '00'
         AGO   .S25
         MEXIT
.S30     ANOP
&NAME    DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'00000000000000000000FFFFFFFFFFFF'
         MEXIT
.S35     ANOP
&NAME    DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FF000000000000000000FFFFFFFFFFFF'
         DC    X'FF000000000000000000FFFFFFFFFFFF'
         DC    X'FFFF0000000000000000FFFFFFFFFFFF'
         DC    X'00000000000000000000FFFFFFFFFFFF'
         MEND
