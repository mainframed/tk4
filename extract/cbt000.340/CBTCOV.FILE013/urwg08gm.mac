         MACRO
&NAME    MLINK &ROUT,&ADDR,&PARM,&LINK=BALR,&TYPE=C
         GBLB  &MTRB003(2)         MUPTRACE MLINK
         GBLB  &MBSB003            MBASIS TYPE=R
         LCLA  &A,&B,&C,&D,&E,&F,&G,&H
         LCLB  &P,&S,&T,&TW
         LCLC  &Q,&R,&AD
         AIF   ('&LINK' NE 'BALR' AND '&LINK' NE 'SVC').ERR4
         AIF   ('&TYPE' NE 'C' AND '&TYPE' NE 'D').ERR5
         AIF   ('&ROUT' EQ '' AND '&LINK' EQ 'SVC').ERR3
         AIF   ('&ADDR' EQ '' AND '&LINK' NE 'SVC').ERR1
&F       SETA  N'&SYSLIST-2
&P       SETB  (&F GT 0)
         AIF   (&MBSB003 AND &P).ERR2
&S       SETB  ('&LINK' EQ 'SVC')
&T       SETB  ('&TYPE' EQ 'D')
         AIF   (NOT &P).A10
         CNOP  0,4
.A10     ANOP
         AIF   ('&NAME' EQ '').A15
&NAME    EQU   *
.A15     ANOP
         AIF   (&P).B10
.A20     ANOP
         AIF   ('&ROUT' EQ '').A30
         AIF   (&S).A25
         MNOTE *,'*** ES WIRD ZUR ROUTINE ''&ROUT'' VERZWEIGT ***'
         AGO   .A30
.A25     ANOP
         MNOTE *,'*** ES WIRD ZUR ROUTINE ''&ROUT'' VERZWEIGT UEBER LIN*
               K-SVC ***'
.A30     ANOP
         AIF   (&S).A50
&AD      SETC  '&ADDR'
         AIF   ('&AD' EQ '(15)' OR '&AD' EQ '(R15)').A50
         AIF   ('&AD'(1,1) NE '(').A40
         LR    R15,&AD             LADEN ENTRY-PUNKT DER ROUTINE IN R15
         AGO   .A50
.A40     ANOP
         L     R15,&AD             LADEN ENTRY-PUNKT DER ROUTINE IN R15
.A50     ANOP
         AIF   (NOT &MTRB003(1)).TR5
         STM   R14,R0,84(R13)      SAVE USER REGISTER
         L     R15,72(0,R13)       LOAD TRACE-ROUTINE ADDRESS
         BAL   R0,*+14             LOAD ID-ADDRESS
         AIF   ('&ROUT' EQ '').TR3
         DC    CL10'R1&ROUT'       IDENTIFIER 1
         AGO   .TR4
.TR3     ANOP
         DC    CL10'A1&AD'         IDENTIFIER 1
.TR4     ANOP
         ICM   R0,8,78(R13)        SET MACRO TYPE
         BALR  R14,R15             BRANCH TO TRACE-ROUTINE
         LM    R14,R0,84(R13)      RESTORE USER REGISTER
.TR5     ANOP
         AIF   (&S).TR51
         BALR  R14,R15             VERZWEIGEN ZUM ENTRY-PUNKT
         AGO   .TR52
.TR51    ANOP
         LINK  EP=&ROUT            VERZWEIGEN ZUM ENTRY-PUNKT
.TR52    ANOP
         AIF   (NOT &MTRB003(1)).TR8
         STM   R14,R0,84(R13)      SAVE USER REGISTER
         L     R15,72(0,R13)       LOAD TRACE-ROUTINE ADDRESS
         BAL   R0,*+14             LOAD ID-ADDRESS
         AIF   ('&ROUT' EQ '').TR6
         DC    CL10'R2&ROUT'       IDENTIFIER 2
         AGO   .TR7
.TR6     ANOP
         DC    CL10'A2&ADDR'       IDENTIFIER 2
.TR7     ANOP
         ICM   R0,8,78(R13)        SET MACRO TYPE
         BALR  R14,R15             BRANCH TO TRACE-ROUTINE
         LM    R14,R0,84(R13)      RESTORE USER REGISTER
.TR8     ANOP
         MEXIT
.B10     ANOP
&B       SETA  2
&G       SETA  N'&SYSLIST
&H       SETA  0
&R       SETC  'MLN&SYSNDX'
.B20     AIF   ('&SYSLIST(&B+1)' EQ '').B30
         AIF   ('&SYSLIST(&B+1)'(1,1) NE '/').B25
&H       SETA  K'&SYSLIST(&B+1)-1
         AIF   (&H EQ 0).B30
&Q       SETC  '&SYSLIST(&B+1)'(2,&H)
&TW      SETB  1
         AGO   .D15
.B25     ANOP
&H       SETA  K'&SYSLIST(&B+1)
&Q       SETC  '&SYSLIST(&B+1)'
         AIF   ('&Q'(1,1) NE '(').D10
         ST    &Q,&R+&C            SPEICHERN REGISTER IN PARAMETERLIST
         AGO   .B30
.D10     ANOP
         AIF   (NOT &T).B30
.D15     ANOP
         LA    R1,&Q               LADEN PARAMETER-ADRESSE
         ST    R1,&R+&C            SPEICHERN PARAM-ADR IN PARAMETERLIST
.B30     ANOP
         AIF   (&B+1 EQ &G).B40
&B       SETA  &B+1
&C       SETA  &C+4
&H       SETA  0
&TW      SETB  0
         AGO   .B20
.B40     ANOP
         AIF   ('&SYSLIST(&B+1)' EQ '').B50
         AIF   (&H EQ 0).B50
         AIF   (&T OR &TW).D20
         AIF   ('&Q'(1,1) NE '(').B50
.D20     ANOP
         MVI   &R.V,X'80'          SETZEN KENNZEICHEN LETZTER PARAMETER
&TW      SETB  0
.B50     ANOP
         BAL   R1,&R.L             LADEN ADRESSE PARAMETERLISTE IN R1
&R       EQU   *                   START PARAMETERLISTE
&D       SETA  2
         AIF   (&F GT 1).C10
&D       SETA  1
         AGO   .C50
.C10     ANOP
         AIF   ('&SYSLIST(&D+1)' EQ '').C20
         AIF   ('&SYSLIST(&D+1)'(1,1) NE '/').C15
&H       SETA  K'&SYSLIST(&D+1)-1
         AIF   (&H EQ 0).C20
&Q       SETC  '&SYSLIST(&D+1)'(2,&H)
         DC    A(0)                SYMBOL PARAMETER AUS DSECT
         AGO   .C40
.C15     ANOP
&Q       SETC  '&SYSLIST(&D+1)'
         AIF   ('&Q'(1,1) EQ '(').C30
         AIF   (&T).D30
         DC    A(&Q)               SYMBOL PARAMETER
         AGO   .C40
.D30     ANOP
         DC    A(0)                SYMBOL PARAMETER TYPE=D
         AGO   .C40
.C20     ANOP
         DC    A(0)                FEHLENDER PARAMETER
         AGO   .C40
.C30     ANOP
         DC    A(&Q)               REGISTER PARAMETER
.C40     ANOP
         AIF   (&D+2 EQ &G).C50
&D       SETA  &D+1
         AGO   .C10
.C50     ANOP
         AIF   ('&SYSLIST(&D+2)' EQ '').C60
         AIF   ('&SYSLIST(&D+2)'(1,1) NE '/').C55
&H       SETA  K'&SYSLIST(&D+2)-1
         AIF   (&H EQ 0).C60
&Q       SETC  '&SYSLIST(&D+2)'(2,&H)
&R.V     DC    X'80'               KENNZEICHEN LETZTER PARAMETER
         DC    AL3(0)              SYMBOL PARAMETER AUS DSECT
         AGO   .C80
.C55     ANOP
&Q       SETC  '&SYSLIST(&D+2)'
         AIF   ('&Q'(1,1) EQ '(').C70
         AIF   (&T).D40
         DC    X'80'               KENNZEICHEN LETZTER PARAMETER
         DC    AL3(&Q)             SYMBOL PARAMETER
         AGO   .C80
.D40     ANOP
&R.V     DC    X'80'               KENNZEICHEN LETZTER PARAMETER
         DC    AL3(0)              SYMBOL PARAMETER TYPE=D
         AGO   .C80
.C60     ANOP
         DC    X'80'               KENNZEICHEN LETZTER PARAMETER
         DC    AL3(0)              FEHLENDER PARAMETER
         AGO   .C80
.C70     ANOP
&R.V     DC    X'80'               KENNZEICHEN LETZTER PARAMETER
         DC    AL3(&Q)             REGISTER PARAMETER
.C80     ANOP
&R.L     EQU   *                   ENDE PARAMETERLISTE
         AGO   .A20
.ERR1    MNOTE 12,'*** ADRESS-PARAMETER FEHLT ***'
         MEXIT
.ERR2    MNOTE 12,'*** MLINK IN REENTRANT CODE. GENERIERUNG ABGEBROCHEN*
                ***'
         MEXIT
.ERR3    MNOTE 12,'*** ROUTINEN-NAME FEHLT ***'
         MEXIT
.ERR4    MNOTE 12,'*** LINK-PARAMETER NICHT ''BALR'' ODER ''SVC'' ***'
         MEXIT
.ERR5    MNOTE 12,'*** TYPE-PARAMETER NICHT ''C'' ODER ''D'' ***'
         MEND
