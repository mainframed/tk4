* STAND 04/01/76
***********************************************************************
*                                                                     *
*    PROGRAMM  = CENQAST                                              *
*                                                                     *
*    1.AUFRUF  = CALL CENQAST,(TYP)                                   *
*                                                                     *
*      CL1  TYP   E = EXCLUSIVE USE
*                 S = SHARED USE
*
*    ZWECK     = ENQUEUE FUER ABSTIMMSUMMENDATEI                      *
*                MVT                   ENQUEUE
*                MVT, SHARED DEVICE      RESERVE
*                MVS                   ENQUEUE
*                MVS, SHARED DEVICE    SWAP VERBIETEN,SOLANGE RESERVE
*                SHARED D.H. DEVICE ALS SHARABLE DEFINIERT
*                                                                     *
*    2.AUFRUF  = CALL CDEQAST                                         *
*                                                                     *
*    ZWECK     = DEQUEUE FUER ABSTIMMSUMMENDATEI                      *
*                                                                     *
***********************************************************************
CENQAST  CSECT
         XSAVE R12,SAVE,CENQAST
         AREG
         LR    R11,R1                  -->ARGLIST RETTEN
***********************************************************************
*
BRANCHR  B     CALL1                   1.AUFRUF, SPRUNGADRESSE AENDERN
*        B     NOSWAP                  SWAP VERBIETEN, DANN RESERVE
*        B     DORES                   ENQUEUE BZW. RESERVE
*        B     RETURNR                 SOFORTIGER RUECKSPRUNG
*
***********************************************************************
*
*  1.AUFRUF, SETZEN DER ADRESSE IN BRANCHR/D FUER WEITERE AUFRUFE
*
***********************************************************************
CALL1    EQU   *
*  UCB ADRESSE SUCHEN
         TIOTSCAN DDN
         LTR   R15,R15                 -->DD-ENTRY IN TIOT
         BNZ   UCBOK
*
*  DDKARTE ASTSUM FEHLT --> SOFORTIGER RUECKSPRUNG
*
         LH    R4,PRETURNR
         STH   R4,BRANCHR+2            BRANCH-ADR IN CENQAST
         B     RETURNR
*
*  UCB GEFUNDEN
*
UCBOK    EQU   *
         LH    R4,PDODEQ               DEQ MUSS DURCHGEFUEHRT WERDEN
         STH   R4,BRANCHD+2            BRANCH-ADR IN CDEQAST
         LA    R3,16(R15)
         LH    R3,2(R3)                --UCB
         ST    R3,UCB
*
*  PRUEFUNG, OB ASTSUM AUF SHARED DEVICE
*
         TM    17(R3),X'20'            20 = SHARED
         BO    SHARED                  SHARED DEVICE --> RESERVE
*
*  DEVICE NOT SHARED --> ENQUEUE ANSTATT RESERVE
*
         NI    RES+2,X'F7'             RESERVE-BIT LOESCHEN
         OI    RES+2,X'40'             ENQUEUE-BIT SETZEN
*
*  BRANCHR SPRINGT DIRECT NACH ENQUEUE BZW. RESERVE
*
CHANGE2  EQU   *
         LH    R4,PDORES
         STH   R4,BRANCHR+2
         B     DORES
*
*  DEVICE SHARED --> RESERVE
*
SHARED   EQU   *
*
*  PRUEFUNG, OB MVS, DANN MUSS SWAP VERBOTEN WERDEN
*
         L     R15,16                  -->CVT
         TM    116(R15),X'01'          01 = MVS
         BNO   CHANGE2                 NICHT MVS
*
*  MVS UND SHARED DEVICE --> SWAP MUSS VERBOTEN WERDEN SOLANGE RESERVE
*
         LH    R4,PNOSWAP
         STH   R4,BRANCHR+2            BRANCH-ADR IN CENQAST
         LH    R4,PSWAP                NACH RESERVE MUSS SWAP MOEGL.
         STH   R4,BRANCHD2+2           BRANCH-ADR IN CDEQAST
         B     NOSWAP
***********************************************************************
*
*  VERARBEITUNGSTEIL : ENQUEUE / RESERVE
*
***********************************************************************
NOSWAP   EQU   *
*
*  SWAP UNTERSAGEN BEI MVS, SHARED DEVICE
*
         LA     R0,41                  SYSEVENT-CODE 41 (DONT SWAP)
         LA    R15,SVC                 -->USER SVC ROUTINE
         SVC   250                     FUEHRT ROUTINE IN R15 DURCH
*                                      D.H. SVC 95 (SYSEVENT)
*
*  ENQUEUE BZW. RESERVE
*
DORES    EQU   *
*
*  EXCLUSIVE/SHARED USE BIT IN ENQUEUE BZW. RESERVE SETZEN
*
         NI    RES+2,X'7F'             BIT 1 LOESCHEN (DH.EXCLUSIV)
         L     R3,0(R11)               -->ARG
         CLI   0(R3),C'E'              E = EXCLUSIVE
         BE     EXEC
         OI    RES+2,X'80'             SHARED USE BIT SETZEN
EXEC     EQU   *
         RESERVE MF=(E,RES)            ENQUEUE BZW. RESERVE
*
*  RUECKSPRUNG CENQAST
*
RETURNR  EQU   *
         XRETURN 0
*
*  ADRESSEN DEFINITION IN CENQAST
*
PNOSWAP  DC    S(NOSWAP)
PDORES   DC    S(DORES)
PRETURNR DC    S(RETURNR)
***********************************************************************
*
         DROP  R12
         ENTRY CDEQAST
*
***********************************************************************
CDEQAST  EQU   *
         XSAVE R12,SAVE,CDEQAST
***********************************************************************
*
BRANCHD  B     RETURND                 ENQ/RES NICHT DURCHGEFUEHRT
*        B     DODEQ                   DEQUEUE
*
***********************************************************************
*  DEQUEUE
*
DODEQ    EQU   *
         DEQ   MF=(E,RES)
*
BRANCHD2 B     RETURND                 RUECKSPRUNG
*        B     SWAP                    MVS --> SWAP WIEDER ZULASSEN
*
*  SWAP WIEDER ZULASSEN
*
SWAP     EQU   *
         LA    R0,42                   SYSEVENT-CODE 42 (ALLOW SWAP)
         LA    R15,SVC                 -->USER SVC ROUTINE
         SVC   250                     FUEHRT ROUTINE IN R15 DURCH
*                                      D.H. SVC 95 (SYSEVENT)
*
*  RUECKSPRUNG CDEQAST
*
RETURND  EQU   *
         XRETURN 0
*
*  ADRESSEN DEFINITIONEN IN CDEQAST
*
PDODEQ   DC    S(DODEQ)
PSWAP    DC    S(SWAP)
*
*  DEFINITIONEN FUER CENQAST UND CDEQAST
*
SAVE     DS    18A
DDN      DC    CL8'ASTSUM'             DDNAME
UCB      DS    A                       -->UCB
RES      RESERVE (EN1,EN2,E,8,SYSTEMS),UCB=UCB,MF=L
EN1      DC    CL8'ASTSUM'
EN2      DC    CL8'ASTPGM'
***********************************************************************
*
*  USER SVC ROUTINE
*
***********************************************************************
SVC      SVC   95                      SYSEVENT SVC
         SVC   3                       EXEIT
         END
