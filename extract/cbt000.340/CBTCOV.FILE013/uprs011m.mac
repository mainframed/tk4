 ./ ADD  NAME=DFSUB
         MACRO
&LABEL   DFSUB &SUBREG=14
         GBLA  &SBRGA01,&SBRGA02
         GBLC  &SBRGC01
         LCLA  &A1
         LCLC  &C1
SUBREG   EQU   &SUBREG             EQUATE LINK REGISTER
         AIF   ('&LABEL' EQ '').M100
&LABEL   DS    0F
.M100    ANOP
         AIF   (&SBRGA01 EQ 0).M150
         MNOTE 8,'SUBROUTINE &SBRGC01 NOT CLOSED'
.M150    ANOP
&A1      SETA  &A1+1
         AIF   (&A1 GT &SBRGA02).M200
&C1      SETC  'SBRSVE&A1'
&C1      DC    F'0'
         AGO   .M100
.M200    ANOP
&SBRGA02 SETA  0
         MEND
 ./ ADD  NAME=EOSUB
         MACRO
&LABEL   EOSUB &CODE=YES
         GBLA  &SBRGA01
         AIF   (&SBRGA01 EQ 1).M100
         MNOTE 8,'USE EOSUB ONLY TO CLOSE A SUBROUTINE'
         MEXIT
.M100    ANOP
&SBRGA01 SETA  &SBRGA01-1
         AIF   ('&CODE'(1,1) EQ 'Y').M200
         MEXIT
.M200    ANOP
&LABEL   BR    SUBREG              RETURN
         MEND
 ./ ADD  NAME=LTSUB
         MACRO
&LABEL   LTSUB &ENTRY
         GBLA  &SBRGA01,&SBRGA02
         GBLC  &SBRGC01
         GBLC  &SBRGCT1(16)
         LCLA  &A1,&A2
         AIF   ('&ENTRY' NE '').M100
         MNOTE 8,'ENTRY-NAME OF SUBROUTINE NOT SPECIFIED'
         MEXIT
.M100    ANOP
&A2      SETA  16                  ANZAHL TABELLENPLAETZE
         AIF   (&SBRGA01 EQ 0).M400  KEIN AUFRUF AUS SUBROUTINE
.M110    ANOP
&A1      SETA  &A1+1
         AIF   (&A1 GT &SBRGA02).M200
         AIF   ('&SBRGC01' EQ '&SBRGCT1(&A1)').M300
         AGO   .M110
.M200    ANOP
         AIF   (&A1 GT &A2).M600
&SBRGA02 SETA  &A1
&SBRGCT1(&A1)  SETC  '&SBRGC01'
.M300    ANOP
&LABEL   ST    SUBREG,SBRSVE&A1    SAVE SUBREG
         BAL   SUBREG,&ENTRY       LINK TO SUBROUTINE
         L     SUBREG,SBRSVE&A1    RELOAD SUBREG
         MEXIT
.M400    ANOP
&LABEL   BAL   SUBREG,&ENTRY       LINK TO SUBROUTINE
         MEXIT
.M600    ANOP
         MNOTE 12,'TOO MANY SUBROUTINE-CALLS. EXPAND MACRO TABLE'
         MEND
 ./ ADD  NAME=MVT
         MACRO
&LAB     MVT   &TO,&TEXT
         LCLA  &A1
&A1      SETA  K'&TEXT-2
         AIF   ('&TO'(1,1) EQ '(').REGFORM
&LAB     MVC   &TO.(&A1),=CL&A1&TEXT
         MEXIT
.REGFORM ANOP
&LAB     MVC   0(&A1,&TO(1)),=CL&A1&TEXT
         MEND
 ./ ADD  NAME=NULL
         MACRO
&NAME    NULL
&NAME    DS    0H                                              - NULL
         MEND
 ./ ADD  NAME=PEND
         MACRO
&LABEL   PEND  &RENT,&RC=0
&LABEL   DS    0H
         AIF   ('&RENT' NE 'RELEASE').M00
         LH    0,0(,13)                    LAENGE GETMAIN
         LR    1,13                        ADRESSE
         FREEMAIN R,LV=(0),A=(1)
.M00     ANOP
         L     13,4(0,13)                   LADEN HOEHERE SAVE-AREA
         AIF   ('&RC'(1,1) EQ '(').M10
         RETURN (14,12),RC=&RC
         MEXIT
.M10     ANOP
         AIF   ('&RC(1)' EQ '15').M20
         LR    15,&RC(1)                    RETURN-CODE LADEN
.M20     ANOP
         RETURN (14,12),RC=(15)
         MEND
 ./ ADD  NAME=PROST
         MACRO
&LABEL   PROST &BR,&ID,&EQ,&DYNAM=,&SAVE=
         LCLA  &A1,&A2
         LCLC  &C1
&A1      SETA  N'&BR
         AIF   ('&EQ' NE 'REG').M10
R0       EQU   0                            REGISTER EQUAL SETZEN
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*       REGISTER IN HEXAFORM
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
.M10     ANOP
         AIF   ('&ID' EQ 'NO').M20
&LABEL   B     0032(0,15)                   BRANCH AROUND CONSTANTS
         DC    AL1(27)
         AIF   ('&LABEL' EQ '').M100
         DC    CL9'&LABEL'                  PROGRAM-ID
         AGO   .M110
.M100    ANOP
         DC    CL9'&SYSECT'                 PROGRAM-ID
.M110    ANOP
&C1      SETC  '&SYSDATE'(4,3).'&SYSDATE'(1,3).'&SYSDATE'(7,2)
         DC    CL9'&C1'                     DATE OF COMPILATION
         DC    CL9'&SYSTIME'                TIME OF COMPILATION
         SAVE  (14,12)
         AGO   .M30
.M20     ANOP
&LABEL   DS    0H
.M30     ANOP
         BALR  &BR(1),0                     BASISADRESSE LADEN
         AIF   (&A1 GT 1).M40
         USING *,&BR(1)                     BENUTZE BASISREGISTER
         AGO   .M50
.M40     ANOP
&A2      SETA  K'&BR
&A2      SETA  &A2-2
&C1      SETC  '&BR'(2,&A2)
         USING *,&C1                        BENUTZE BASISREGISTER
.M50     ANOP
         AIF   ('&DYNAM' EQ '').M5050
         LA    0,&DYNAM                     LAENGE FUER GETMAIN
         GETMAIN R,LV=(0)
         LR    14,1                         ADRESSE UMLADEN
         LA    0,&DYNAM
         STH   0,0(,14)
         LM    0,1,20(13)                   REGISTER 0 + 1 RESTOREN
         AGO   .M5060
.M5050   ANOP
         AIF   ('&SAVE' NE '').M5055
         CNOP  0,4                          VOLLWORTGRENZE
         BAL   14,*+76                      UM SAVE-AREA HERUM
         DC    18F'0'                       SAVE-AREA
         AGO   .M5060
.M5055   ANOP
         CNOP  0,4                          VOLLWORTGRENZE
         B     *+8
         DC    A(&SAVE)
         L     14,*-4                      SAVEAREA ADRESSE
         USING &SAVE,13
.M5060   ANOP
         ST    13,4(0,14)                   SAVE-
         ST    14,8(0,13)                    AREA-
         LA    13,0(0,14)                     KETTUNG
&A2      SETA  2
.M60     AIF   (&A2 GT &A1).M70
         LA    &BR(&A2),4095(0,0)           MAXIMALES DISPLACEMENT
         LA    &BR(&A2),1(&BR(&A2),&BR(&A2-1)) LADEN BASIS+4096
&A2      SETA  &A2+1
         AGO   .M60
.M70     ANOP
         MEND
 ./ ADD  NAME=SOSUB
         MACRO
&LABEL   SOSUB &SE=
         GBLA  &SBRGA01
         GBLC  &SBRGC01
         AIF   ('&SE' NE '').M200
         AIF   ('&LABEL' NE '').M100
         MNOTE 8,'LABEL IN FIRST ENTRY NOT SPECIFIED'
         MEXIT
.M100    ANOP
         AIF   (&SBRGA01 EQ 0).M150
         MNOTE 8,'SUBROUTINE &SBRGC01 NOT CLOSED'
&SBRGA01 SETA  0
.M150    ANOP
&SBRGC01 SETC  '&LABEL'
&SBRGA01 SETA  &SBRGA01+1
.M200    ANOP
&LABEL   DS    0H                  ENTRY-POINT TO SUBROUTINE
         MEND
 ./ ADD  NAME=XDAVARS
         SPACE
***********************************************************************
***            DEFINITION DER SYSTEM-VARIABLEN FUER XDATA           ***
***********************************************************************
         SPACE
$$FREE1  DC    3D'0'                   RESERVE
$$NEW1   DC    F'0'                    ADRESSE NEW-AREA
$$FREE2  DC    5F'0'                   RESERVE
$$ZCODE1 DC    PL8'0'                  WORK-FIELD
$$ZCODE2 DC    PL8'0'                  WORK-FIELD
$$SCODE1 DC    PL16'0'                 STACK-RECHENFELD
$$SCODE2 DC    PL16'0'                 STACK-RECHENFELD
$$SCODE3 DC    PL16'0'                 STACK-RECHENFELD
$$SCODE4 DC    PL16'0'                 STACK-RECHENFELD
$$SCODE5 DC    PL16'0'                 STACK-RECHENFELD
$$SCODE6 DC    PL16'0'                 STACK-RECHENFELD
$$SCODE7 DC    PL16'0'                 STACK-RECHENFELD
$$SCODE8 DC    PL16'0'                 STACK-RECHENFELD
$DATE    DC    CL8' '                  TAGESDATUM
$TIME    DC    CL8' '                  UHRZEIT
$RECNO   DC    PL8'0'                  SATZZAEHLER
$$ZRECNO DC    PL7'0'                  SATZZAEHLER INTERN
$$KON1   DC    PL1'1'                  GEPACKTE 1
$LASTCC  DC    PL3'0'                  RETURN CODE AUS CALL
$MAXCC   DC    PL3'0'                  HOECHSTER RETURN CODE
$LIBNO   DC    CL2' '                  EINGABE LIBRARY-NR
$CNT0    DC    PL8'0'                  COUNTER
$CNT1    DC    PL8'0'                  COUNTER
$CNT2    DC    PL8'0'                  COUNTER
$CNT3    DC    PL8'0'                  COUNTER
$CNT4    DC    PL8'0'                  COUNTER
$CNT5    DC    PL8'0'                  COUNTER
$CNT6    DC    PL8'0'                  COUNTER
$CNT7    DC    PL8'0'                  COUNTER
$CNT8    DC    PL8'0'                  COUNTER
$CNT9    DC    PL8'0'                  COUNTER
$FLG0    DC    PL1'0'                  FLAG
$FLG1    DC    PL1'0'                  FLAG
$FLG2    DC    PL1'0'                  FLAG
$FLG3    DC    PL1'0'                  FLAG
$FLG4    DC    PL1'0'                  FLAG
$FLG5    DC    PL1'0'                  FLAG
$FLG6    DC    PL1'0'                  FLAG
$FLG7    DC    PL1'0'                  FLAG
$FLG8    DC    PL1'0'                  FLAG
$FLG9    DC    PL1'0'                  FLAG
$FILE    DC    CL1' '                  FILE
$RETCD   DC    PL3'0'                  RETURN-CODE USER
$PARM    DC    CL1' '                  DATA-FELD AUS PARM
         DC    CL79' '                 DATA-FELD AUS PARM
$MEMBER  DC    CL8' '                  PDS-MEMBERNAME
$ALIAS   DC    CL1' '                  A = ALIAS
$LRECL   DC    PL3'0'                  SATZLAENGE
$$OFFSET DC    255H'0',X'FFFF'         STATEMENT OFFSET TABLE
$$VAREND DS    0D
         SPACE 2
 ./ ADD  NAME=XTIME
         MACRO
&LABEL   XTIME
.*       AUFBEREITEN VON DATUM UND UHRZEIT  HOFFMANN
*
&LABEL   DS    0H
*
*        MONDD.MM.YYHH.MM.SS
*
         TIME  DEC
         STM   0,1,XTIMEF02+4     STORE FROM REGISTERS
         MVO   XTIMEF03,XTIMEF02+8(2)  MOVE YEAR
         DP    XTIMEF03,XTIMEF04  OLYMPICAL YEAR..
         CLI   XTIMEF03+2,X'0C'   IF ZERO
         BNE   *+8                NO
         MVI   XTIMEFEB+1,X'29'   DAYS OF FEB.
         LA    1,XTIMETAB
XTIME100 DS    0H
         MVO   XTIMEF03,1(1,1)    MOVE DAYS OF MONTH
         CP    XTIMEF02+10(2),XTIMEF03+1(2)
         BNP   XTIME200
         SP    XTIMEF02+10(2),XTIMEF03+1(2)
         LA    1,5(0,1)           POINT TO NEXT PLACE
         B     XTIME100
XTIME200 DS    0H
         MVC   XTIMEF02+3(1),XTIMEF02+9 MOVE YEAR
         MVO   XTIMEF02(3),XTIMEF02+10(2) MOVE DAY
         MVC   XTIMEF02+2(1),0(1) MOVE MONTH
         MVI   XTIMEF02+7,X'0C'
         MVC   XTIMEF01(12),XTIMEF05
         MVC   XTIMEF01+12(8),XTIMEF05+4
         ED    XTIMEF01,XTIMEF02
         MVC   XTIMEF01(3),2(1)
         BAL   1,XTIMEEND
*
*
XTIMEF01 DS    CL20
XTIMEF02 DC    3F'0'
XTIMEF03 DC    PL3'0'
XTIMEF04 DC    PL1'4'
XTIMEF05 DC    XL12'40202120204B20204B202020'
XTIMETAB DS    0CL60
XTIMEJAN DC    XL2'0131',CL3'JAN'
XTIMEFEB DC    XL2'0228',CL3'FEB'
XTIMEMRZ DC    XL2'0331',CL3'MRZ'
XTIMEAPR DC    XL2'0430',CL3'APR'
XTIMEMAI DC    XL2'0531',CL3'MAI'
XTIMEJUN DC    XL2'0630',CL3'JUN'
XTIMEJUL DC    XL2'0731',CL3'JUL'
XTIMEAUG DC    XL2'0831',CL3'AUG'
XTIMESEP DC    XL2'0930',CL3'SEP'
XTIMEOKT DC    XL2'1031',CL3'OKT'
XTIMENOV DC    XL2'1130',CL3'NOV'
XTIMEDEZ DC    XL2'1231',CL3'DEZ'
XTIMEEND DS    0H
*
         MEND
