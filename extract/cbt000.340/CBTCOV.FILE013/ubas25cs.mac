//ASM017   JOB (9999,999,9),
// CLASS=R,MSGCLASS=T
//ASM     EXEC ASMFCL
//ASM.SYSLIB DD DSN=XXXXXXX.CATOUT.ASM,DISP=SHR   ----> BASF MACROS
//        DD DSN=SYS1.MACLIB,DISP=SHR
//        DD DSN=SYS1.AMODGEN,DISP=SHR
//ASM.SYSIN  DD *
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*         HAUPTROUTINE OP017 LINK ZUR SORTIERUNG / CALL COBAST        *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*     PROGRAMMER:  H. LEONHARDT / BASF AG                             *
*     DATE      :  JANUARY 1984                                       *
*     MODIFIED  :  M. GIMPEL    / BASF AG  (APRIL 1984)               *
*                                                                     *
***********************************************************************
*        O P 0 1 7
*        ---------
*
*        SMF-SAETZE  #61, #65 UND #66
*                     3D   41      42
*                  CATLG  UNCATLG  RECATLG
*
*        AUSFILTERN UND SORTIEREN VON SMF-SAETZEN ZUR CATALOG-RECOVERY
*        UND UPDATE DER ALIAS-EINTRAEGE IM MASTER-CATALOG
*
*
***********************************************************************
         EJECT
OP017    CSECT
         REG
         XSAVE R12,SVASMF,OP017
         EJECT
         LA    R1,SORTPARM                  PARAMETER FOR SORT
         SPACE
         LINK  EP=SORT                      LINK TO SORTROUTINE
         SPACE
*        L     R2,ADASTE15                  GET COBAST-COUNTERS
*        L     R3,ADASTE35
         SPACE
*        CALL  COBAST,(ASTRCODE,ASTBZEIT,(R2),(R3)),VL
         SPACE
         XRETURN 0
         EJECT
ADASTE15 DC    V(ASTE15)
ADASTE35 DC    V(ASTE35)
         SPACE
ASTRCODE DS    H
ASTBZEIT DS    CL15
         SPACE
SORTPARM DS    0F
         DC    X'80',AL3(SOPADRS)
         DC    H'0'
SOPADRS  DC    H'24'                        LENGTH
         DC    A(SOFIA)                     START OF SORT-FIELD-STM
         DC    A(SOFIE)                     END OF       "
         DC    A(SORECA)                    START OF SORT-RECORD-STM
         DC    A(SORECE)                    END OF          "
         DC    V(E15)                       ADR OF INPUT ROUTINE
         DC    V(E35)                       ADR OF OUTPUT ROUTINE
         SPACE
SOFIA    DC    C' SORT FIELDS=(11,4,PD,A,7,4,BI,A) '
SOFIE    EQU   *
         SPACE
SORECA   DC    C' RECORD TYPE=V,LENGTH=(32767,8192,8192,18,300) '
SORECE   EQU   *
         SPACE
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*       EINGABEROUTINE E15 GET RECORDS TO SORTIERUNG                  *
*                                                                     *
*                                                                     *
***********************************************************************
E15      CSECT
         ENTRY ASTE15                       ONLY FOR COMMON COUNTER
         ENTRY DATE15
         ENTRY TIME15
         ENTRY CATLG15
         XSAVE R12,SVAE15,E15
         EJECT
FIRST15  NOP   NFIRST15
         OI    FIRST15+1,X'F0'
         SPACE 2
         OPEN  (PARAM,(INPUT))
         GET   PARAM,KARTE15
         MVC   C15,CATLG
         CLOSE (PARAM)
         SPACE 2
         OPEN  (SMFLOG,(INPUT))
         LA    R2,SMFLOG
         ST    R2,GETADR                    ADRESS OF SMFSATZ
         SPACE 2
NFIRST15 EQU   *
         L     R2,GETADR
         GET   (R2)                         GET SMFSATZ (LOCATE)
         ST    R1,SVREC
         USING SMF4REC,R1
         SPACE
         AP    INZAE,=PL5'+1'               ADD ONE TO INPUT ZAE
         SPACE
         CLI   SMF4RTY,X'04'                TYPE 4 ?
         BE    SMF04                        YES
         SPACE
         CLI   SMF4RTY,X'3D'                TYPE 61?
         BE    SMFSORT                      YES
         SPACE
         CLI   SMF4RTY,X'41'                TYPE 65?
         BE    SMFSORT                      YES
         SPACE
         CLI   SMF4RTY,X'42'                TYPE 66?
         BE    SMFSORT                      YES
         SPACE 2
UEBLES   EQU   *
         SPACE
         AP    UEZAE,=PL5'+1'
         B     NFIRST15
         SPACE 2
SMF04    EQU   *
         SPACE
         CLC   JOBNME,SMF4JBN              JOBNAME?
         BNE   UEBLES
         CLC   STEPNME,SMF4STMN            STEPNAME?
         BNE   UEBLES
         SPACE
         CLI   KZ,C'1'                    PLATTENDUMP OK?
         BE    UEBLES                     NEIN/ DATUM UND ZEIT BLEIBEN
*                                         AUF 0, D.H. ALLE SAETZE
*                                         WERDEN AUSGEWAEHLT
         SPACE
         CLC   D15,SMF4DTE
         BH    UEBLES
         BL    SMF04A
         CLC   T15,SMF4TME
         BH    UEBLES
         SPACE 2
SMF04A   EQU   *
         MVC   T15,SMF4TME                MOVE TIME
         MVC   D15,SMF4DTE                MOVE DATE
         B     UEBLES
         SPACE
SMFSORT  EQU   *
         L     R1,SVREC
         AP    SOZAE,=P'1'
         EJECT
XRET15   EQU   *
         L     R15,RCODE15                  XSAVE WITH KEEP R1 !!
         L     R13,4(0,R13)
         L     R14,12(R13)
         L     R0,20(R13)
         LM    R2,R12,28(R13)
         SPM   R14
         BR    R14
         SPACE 2
EOFLOG   EQU   *
         CLOSE (SMFLOG)
         SPACE
         OPEN  (SMFDAT,(INPUT))
         LA    R2,SMFDAT
         ST    R2,GETADR
         B     NFIRST15
         SPACE 2
EOF15    EQU   *
         CLOSE (SMFDAT)
         LA    R1,8
         ST    R1,RCODE15
         B     XRET15
         EJECT
ASTE15   EQU   *
INZAE    DC    PL5'0'
UEZAE    DC    PL5'0'
SOZAE    DC    PL5'0'
         SPACE
RCODE15  DC    F'12'
         SPACE
SVREC    DS    1F
GETADR   DS    1F
         SPACE
DATE15   EQU   *
D15      DC    PL4'0'
TIME15   EQU   *
T15      DC    F'0'
CATLG15  EQU   *
C15      DC    CL44' '
         SPACE 2
KARTE15  DS    0CL80
         DS    CL4
KZ       DS    CL1                                  0 OR 1
         DS    CL1
CATLG    DS    CL44
         DS    CL1
JOBNME   DS    CL8
         DS    CL1
STEPNME  DS    CL8
         DS    CL12
         SPACE 2
         PRINT NOGEN
SMFLOG   DCB   DDNAME=SMFLOG,DSORG=PS,MACRF=GL,LRECL=32767,RECFM=VBS,  X
               EODAD=EOFLOG,BFTEK=A
SMFDAT   DCB   DDNAME=SMFDAT,DSORG=PS,MACRF=GL,LRECL=32767,RECFM=VBS,  X
               EODAD=EOF15,BFTEK=A
PARAM    DCB   DSORG=PS,RECFM=FB,LRECL=80,                             X
               MACRF=GM,DDNAME=PARAM
         PRINT GEN
         SPACE 3
         LTORG
         SPACE 3
SMF4REC  DSECT
         IFASMFR 4
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*         E35 ROUTINE ERSTELLEN UND UPDATE DES MASTER-CATALOGS        *
*                                                                     *
*                                                                     *
***********************************************************************
E35      CSECT
         ENTRY ASTE35
         XSAVE (R12,R2),SVAE35,E35
         EJECT
         L     R1,0(R1)                     NEXT SENTENCE
         ST    R1,SORR1
         SPACE 2
FIRST35  NOP   NFIRST35
         OI    FIRST35+1,X'F0'              RESET BRANCH
         SPACE 2
         LA    R5,ZAEHL61
         USING ZAE,R5
         SPACE 2
         L     R11,CATLG35
         MVC   C35,0(R11)
         MVC   FCAT,C35
         L     R11,DATE35
         MVC   D35,0(R11)
         L     R11,TIME35
         MVC   T35,0(R11)
         SPACE 2
         OPEN  (SMFLST,(OUTPUT),FEHLLST,(OUTPUT),SMFDCB,(OUTPUT))
         SPACE 3
NFIRST35 EQU   *
         SPACE 2
         L     R1,SORR1                     GET SENTENCE
         LTR   R1,R1                        EOF ?
         BZ    ENDE                         YES
         SPACE
         AP    EZAE,=P'1'
         LR    R6,R1                         ADRESS SMF-RECORD
         USING SMFREC,R6
         SPACE 2
         LA    R7,SMF61END+2                 ADRESS CTLG-RECORD
         SPACE 2
         MVI   ESCH,C'0'
         LA    R8,1
         SPACE 2
         CLI   SMF61RTY,X'3D'                #61 ?
         BE    SMF61
         CLI   SMF61RTY,X'41'                #65 ?
         BE    SMF65
         CLI   SMF61RTY,X'42'                #66 ?
         BE    SMF66
         AP    UZAE,=P'1'                    WRONG REC#
         B     RET1                          GET REC FROM SORT
         SPACE 3
SMF61    EQU   *                             CATALOG
         SPACE 1
         LA    R5,ZAEHL61
         BAL   R14,SELE
         SPACE 1
         CLI   ESCH,C'1'                     SELECTED?
         BE    RET1                          NO
         SPACE 1
         CLI   ESCH,C'2'                     KENNZEICHEN ALIAS
         BE    ALIDEF                        EIGENE VERARBEITUNG
         SPACE 1
         B     SMFPUT                        PUT SELECTED SMF RECORD
         SPACE 3
SMF65    EQU   *                            UNCATALOG
         SPACE 1
         LA    R5,ZAEHL65
         BAL   R14,SELE
         SPACE 1
         CLI   ESCH,C'1'                     SELECTED?
         BE    RET1                          NO
         SPACE 1
         CLI   ESCH,C'2'                     KENNZEICHEN ALIAS
         BE    ALIDEL                        EIGENE VERARBEITUNG
         SPACE 1
         B     SMFPUT
         SPACE 3
SMF66    EQU   *                            RECATALOG
         SPACE 1
         LA    R5,ZAEHL66
         BAL   R14,SELE
         CLI   ESCH,C'0'
         BE    SMFPUT
         SPACE 1
         B     RET1
         SPACE 3
ALIDEF   EQU   *                             DEFINE ALIAS
         MVC   FACT,=CL8'DEF AL  '
         MVC   AACT,=C'DEF ALIAS'
         MVI   ALUSER,C'C'                   DEFINE ALIAS
         B     ALIPR1
         SPACE 3
ALIDEL   EQU   *                             DELETE ALIAS
         MVC   FACT,=CL8'DEL AL  '
         MVC   AACT,=C'DEL ALIAS'
         MVI   ALUSER,C'D'                   DEFINE ALIAS
         SPACE 3
ALIPR1   EQU   *                             DELETE ALIAS
         CLI   SMF61CRC+4,C'U'              CONNECTOR NAME CELL?
         BE    ALICALL
         SPACE 1
         MVC   FMSG,FMSGL
         MVC   FMSG(L'FCON),FCON
         B     ALIERR1
         SPACE 2
ALICALL  EQU   *
         MVC   ALENT,SMF61ENM
         MVC   ALREL,SMF61CRC+9
         MVC   ALCAT,SMF61CNM
         AP    ZALIAS,=P'1'
         MVI   ISCH,C'0'
         SPACE 1
         LINK  EP=IDCAMS,PARAM=(HW0,HW0,HW0,ALIOL),VL=1
         SPACE 1
         LTR   R15,R15                      RETURN-CODE
         BNZ   ALIERR
         SPACE 1
         BAL   R14,PRT                     PRINT RECORD
         SPACE 3
         B     RET1                          GET REC FROM SORT
         SPACE 3
ALIERR   EQU   *                                 ERROR
         MVC   FMSG,FMSGL
         MVC   FMSG(L'FALIAS),FALIAS
         SPACE 1
ALIERR1  EQU   *
         MVC   FDSN,SMF61CRC+9
         MVC   FVOL,=C'ALIAS='
         MVC   FDEV(12),SMF61ENM
         SPACE 1
         CVD   R15,DOWO                RETURN CODE
         MVC   FRC,MASK
         ED    FRC,DOWO+6              RETURN CODE
         SPACE 1
         B     FEHL1
         EJECT
******************************************************************
*                                                                *
*        PRUEFEN, OB DER SATZ VERARBEITET WERDEN SOLL            *
*                                                                *
*        SOLLEN ALLE SAETZE AUSGEWAEHLT WERDEN, SO WURDEN        *
*        DIE VERGLEICHSFELDER 'DATUM' UND 'ZEIT' AUF 0 GESETZT   *
*                                                                *
******************************************************************
         SPACE 2
SELE     EQU   *
         ST    R14,SVAUR                 STORE REG TO RETURN
         AP    ZE,=P'1'
         CLC   SMF61CNM,C35             COMPARE NAME OF CATLG
         BE    SELEDAT
         SPACE 1
         CLI   SMF61CRC+4,C'U'           ALIAS-RECORD?
         BNE   SELERR                    NO
         SPACE 1
         CLC   SMF61CRC+9(44),C35        COMPARE NAME OF ENTRY
         BNE   SELERR                    NO
         SPACE 1
         MVI   ESCH,C'2'                 KENNZEICHEN ALIAS
         B     SELEDAT1                  SKIP TEST-COMMANDS
         SPACE 2
SELEDAT  EQU   *
* *********************************************************
*    FOR TEST
*    CHANGE NAME OF CATLG TO BE TESTED IN NAME OF TESTCATLG
* *********************************************************
*        MVC   SMF61CNM,=CL44'SYS1.CATLG.VDBTEST'    NAME OF TESTCAT
* *********************************************************
         SPACE
SELEDAT1 EQU   *
         CLC   SMF61DTE,D35              COMPARE DATE
         BL    SELERR
         SPACE 1
         CLC   SMF61DTE,D35
         BH    SELEEND
         SPACE 1
         CLC   SMF61TME,T35             COMPARE TIME
         BNL   SELEEND
         SPACE 1
SELERR   EQU   *
         AP    UZAE,=P'1'
         AP    ZUE,=P'1'
         MVI   ESCH,C'1'
         SPACE 1
SELEEND  EQU   *
         L     R14,SVAUR
         BR    R14
         EJECT
******************************************************************
*                                                                *
*        KONTROLLISTE DER FEHLERFREIEN KATALOG-AKTIONEN          *
*                                                                *
******************************************************************
         SPACE 2
PRT      EQU   *                           KONTROLL-LISTE
         ST    R14,SVAUR
         SPACE 2
PRT1     EQU   *
         MVC   ADSN,SMF61ENM             MOVE DSN
         SPACE 2
PRT2     EQU   *
         CP    ZEIL,=P'60'
         BL    PRT5
         AP    SEITE,=P'1'
         MVC   FS,MASK
         ED    FS,SEITE+3
         MVC   UEB,=C'KONTROLLISTE'
         MVC   LST,=C'01'
         ZAP   ZEIL,=P'0'
         PUT   SMFLST,FREC1
         PUT   SMFLST,FREC1A
         MVC   ATXT1,=C'DSN= '
         MVC   ADSN,SMF61ENM             MOVE DSN
         SPACE 2
PRT5     EQU   *                        ALIAS-RECORD
         MVC   ATXT1,=C'ALI= '
         MVC   ATXT2,=C'CAT= '
         MVC   AVOL(34),SMF61CRC+9
         SPACE 2
PRT6     EQU   *
         UNPK  ZDATE,SMF61DTE
         MVC   ADTE(2),ZDATE+2          MOVE DATE
         MVC   ADTE+2(1),=C'.'
         MVC   ADTE+3(3),ZDATE+4
         SPACE 1
         MVC   TMEBIN,SMF61TME
         MVC   TMEDEZ,=CL6' '
         SPACE 1
         CALL  LUHTIM,(TMEDEZ,TMEBIN),VL
         SPACE 1
         MVC   ATME(2),TMEDEZ         MOVE TIME
         MVI   ATME+2,C'.'
         MVC   ATME+3(2),TMEDEZ+2
         MVI   ATME+5,C'.'
         MVC   ATME+6(2),TMEDEZ+4
         SPACE 1
         PUT   SMFLST,AREC
         SPACE 1
         AP    ZEIL,=P'2'
         SPACE 1
         CLI   ESCH,C'2'
         BE    PRT7
         SPACE 1
         MVC   AREC+1(61),BLANK
         LA    R7,VOLELE(R7)
         BCT   R8,PRT2
         B     PRT4
         SPACE 2
PRT7     EQU   *
         PUT   SMFLST,FREC2A
         PUT   SMFLST,FREC2B
         AP    ZEIL,=P'2'
         MVC   FREC2A+1(132),BLANK2
         MVC   FREC2B+1(132),BLANK2
         MVC   AREC+1(132),BLANK2
         MVC   ATXT2,=C'VOL= '
         MVC   ATXT3,=C'DEV= '
         MVC   ATXT4,=C'SEQ= '
         MVC   ATXT5,=C'DATE= '
         MVC   ATXT6,=C'TIME= '
         SPACE 2
PRT4     EQU   *
         AP    AZAE,=P'1'
         AP    ZA,=P'1'
         L     R14,SVAUR
         BR    R14
         EJECT
******************************************************************
*                                                                *
*        FEHLERLISTE                                             *
*                                                                *
******************************************************************
         SPACE 2
FEHL     EQU   *                           FEHLER
         MVC   FDSN,SMF61ENM             MOVE DSN
         SPACE 2
FEHL1    EQU   *
         CP    FZEI,=P'60'
         BL    FEHL2
         AP    FSEIT,=P'1'
         MVC   FS,MASK
         ED    FS,FSEIT+3
         MVC   UEB,=C'FEHLERLISTE '
         MVC   LST,=C'02'
         ZAP   FZEI,=P'0'
         PUT   FEHLLST,FREC1
         PUT   FEHLLST,FREC1A
         PUT   FEHLLST,FREC3
         SPACE 2
FEHL2    EQU   *
         UNPK  ZDATE,SMF61DTE
         MVC   FDTE(2),ZDATE+2
         MVC   FDTE+2(1),=C'.'          MOVE DATE
         MVC   FDTE+3(3),ZDATE+4
         SPACE 1
         MVC   TMEBIN,SMF61TME
         MVC   TMEDEZ,=CL6' '
         SPACE 1
         CALL  LUHTIM,(TMEDEZ,TMEBIN),VL
         SPACE 1
         MVC   FTME(2),TMEDEZ         MOVE TIME
         MVI   FTME+2,C'.'
         MVC   FTME+3(2),TMEDEZ+2
         MVI   FTME+5,C'.'
         MVC   FTME+6(2),TMEDEZ+4
         SPACE 1
         PUT   FEHLLST,FREC2
         PUT   FEHLLST,FREC2A
         PUT   FEHLLST,FREC2B
         AP    FZEI,=P'4'
         SPACE 2
FEHL4    EQU   *
         AP    FZAE,=P'1'
         AP    ZF,=P'1'
         MVI   ESCH,C'0'
         MVC   FREC2+1(132),BLANK2
         MVC   FREC2A+1(132),BLANK2
         MVC   FREC2B+1(132),BLANK2
         SPACE 2
         B     RET1
         EJECT
SMFPUT   EQU   *
         AP    SZAE,=P'1'
         AP    ZS,=P'1'
         L     R0,SORR1           GET ADDR OF SMF-REC
         PUT   SMFDCB,(R0)        PUT LOCATE
         SPACE 3
RET1     EQU   *
         XRETURN RCODE35                  RETURN TO SORT/ NEW RECORD
         SPACE 2
ENDE     EQU   *
         CLOSE (FEHLLST,,SMFDCB)
         SPACE 3
         MVC   UEB,=C'UEBERSICHT  '
         MVC   LST,=C'01'
         AP    SEITE,=P'1'
         MVC   FS,MASK
         ED    FS,SEITE+3
         PUT   SMFLST,FREC1
         MVC   TALIAS(11),=C'DAVON ALIAS'
         MVC   TSMFOUT+5(6),=C'SMFSEL'
         PUT   SMFLST,ZREC
         MVI   TASA,C' '
         MVC   ZREC+1(132),BLANK2
         PUT   SMFLST,ZREC
         MVI   TASA,C'0'
         MVC   TXT,=C'GESAMTANZAHL'
         MVC   TE,MASK2
         ED    TE,EZAE
         MVC   TUE,MASK2
         ED    TUE,UZAE
         MVC   TA,MASK2
         ED    TA,AZAE
         MVC   TF,MASK2
         ED    TF,FZAE
         MVC   TALIAS,=CL10' '
         MVC   TSMFOUT,MASK2
         ED    TSMFOUT,SZAE
         PUT   SMFLST,ZREC
         LA    R5,ZAEHL61
         MVC   TXT,=C'SMFREC #61  '
         BAL   R14,END1
         LA    R5,ZAEHL65
         MVC   TXT,=C'SMFREC #65  '
         BAL   R14,END1
         LA    R5,ZAEHL66
         MVC   TXT,=C'SMFREC #66  '
         BAL   R14,END1
         SPACE 3
XRET     EQU   *
         CLOSE (SMFLST)
         LA    R1,8
         ST    R1,RCODE35                   SET FINAL RETCODE
         B     RET1
         SPACE 3
END1     EQU   *                          ABSTIMMUNG
         ST    R14,SVAUR
         MVC   TE,MASK2
         ED    TE,ZE
         MVC   TUE,MASK2
         ED    TUE,ZUE
         MVC   TA,MASK2
         ED    TA,ZA
         MVC   TF,MASK2
         ED    TF,ZF
         MVC   TALIAS,MASK2
         ED    TALIAS,ZALIAS
         MVC   TSMFOUT,MASK2
         ED    TSMFOUT,ZS
         PUT   SMFLST,ZREC
         L     R14,SVAUR
         BR    R14
         EJECT
*        D A T E I - D A T E N
*        ---------------------
         SPACE 2
         PRINT NOGEN
SMFLST   DCB   DSORG=PS,RECFM=FBA,LRECL=133,                           X
               MACRF=PM,DDNAME=SMFLST
FEHLLST  DCB   DSORG=PS,RECFM=FBA,LRECL=133,                           X
               MACRF=PM,DDNAME=FEHLLST
SMFDCB   DCB   DSORG=PS,RECFM=VB,LRECL=13064,BLKSIZE=13068,            X
               MACRF=PM,DDNAME=SMFSEL
         PRINT GEN
         SPACE 3
*
*        ZAEHLER
*
FZEI     DC    PL2'999'
ZEIL     DC    PL2'999'
         SPACE
ASTE35   EQU   *
EZAE     DC    PL5'0'      SMF-REC AN E35
AZAE     DC    PL5'0'      SATZZAHL VERARBEITET
FZAE     DC    PL5'0'      SATZZAHL FEHLERHAFT
UZAE     DC    PL5'0'      SATZZAHL UEBERLESEN
SZAE     DC    PL5'0'      SATZZAHL AN SMFDCB (OUTPUT)
FSEIT    DC    PL5'0'
SEITE    DC    PL5'0'
         SPACE
ATXTKENN DC    H'-1'
ATXTFEH  DC    CL50' '
         SPACE 2
ZAEHL61  DC    6PL5'0'
ZAEHL65  DC    6PL5'0'
ZAEHL66  DC    6PL5'0'
         SPACE 2
DATE35   DC    V(DATE15)
TIME35   DC    V(TIME15)
CATLG35  DC    V(CATLG15)
         SPACE 2
SORR1    DS    1F
RCODE35  DC    F'4'
SVAUR    DC    F'0'
VOLADR   DC    F'0'
VOLANZ   DC    F'0'
DOWO     DC    D'0'
         SPACE 2
D35      DC    PL4'0'
T35      DC    F'0'
C35      DC    CL44' '
         SPACE 2
ESCH     DC    CL1'0'
ISCH     DC    CL1'0'
ZDATE    DC    CL7'0'
BLANK    DC    CL61' '
BLANK2   DC    CL132' '
MASK     DC    X'40202120'
MASK2    DC    X'40202020202020202120'
         SPACE 2
FNVSAM   DC    C'KEIN KENNZEICHEN NVSAM'
FREN     DC    C'KEIN RENAME (KEIN R IN SMF66FNC)'
FVDSN    DC    C'VERSCHIEDENE DSN BEI FUNKTION R'
FFVOL    DC    C'FALSCHE VOLUME-CELL KENNUNG'
FSVC     DC    C'FEHLER BEI SVC26'
FCON     DC    C'FALSCHE CONNECTOR-CELL KENNUNG'
FALIAS   DC    C'FEHLER BEI IDCAMS'
         SPACE 3
FREC2A   DS    0CL133
FASA2A   DC    CL1' '
FMSG2A   DC    CL132' '
         SPACE 2
FREC2B   DS    0CL133
FASA2B   DC    CL1' '
FMSG2B   DC    CL132' '
         SPACE 2
*     FELDER FUER IDCAMS
         SPACE 1
ALENT    DC    CL44' '
ALREL    DC    CL44' '
ALCAT    DC    CL44' '
         SPACE 1
SYSPRINL DC    CL10'DDSYSPRINT'
SYSINL   DC    CL10'DDSYSIN'
HW0      DC    H'0'
         SPACE 1
ALIOL    DS    0F
         DC    F'2'
         DC    A(SYSPRINL)
         DC    V(ALIOOUT)
         DC    A(ALUSER)
         DC    A(SYSINL)
         DC    V(ALIOIN)
         DC    A(ALUSER)
         SPACE 1
ALUSER   DC    C' '                                 'C' OR 'D'
         SPACE 3
*        FELDER FUER 'CONVERT TIME BIN TO DEZ'
         SPACE 1
TMEBIN   DC    F'0'
TMEDEZ   DC    CL6' '
         SPACE 2
*
*        LITERALBEREICH
*
         LTORG
         SPACE 2
*
*        AUSGABE
*
AREC     DS    0CL133
AASA     DC    CL1'0'
         DC    1C' '
AACT     DS    CL9
         DC    1C' '
ATXT1    DC    CL5'DSN= '
ADSN     DS    CL44
         DC    C' '
ATXT2    DC    CL5'VOL= '
AVOL     DS    CL6
         DC    3C' '
ATXT3    DC    CL5'DEV= '
ADEV     DS    CL8
         DC    3C' '
ATXT4    DC    CL5'SEQ= '
ASEQ     DS    CL4
         DC    3C' '
ATXT5    DC    CL6'DATE= '
ADTE     DS    CL6
         DC    3C' '
ATXT6    DC    CL6'TIME= '
ATME     DS    CL8
         SPACE 2
*
*        FEHLERSATZ
*
FREC1    DS    0CL133
         DC    CL1'1'
         DC    CL10' OP01701 L'
LST      DC    CL2' '
         DC    CL15' '
UEB      DC    CL12'FEHLERLISTE '
         DC    CL83' '
         DC    CL5'SEITE'
FS       DC    CL4' '
         DC    CL1' '
         SPACE 2
FREC1A   DS    0CL133
         DC    CL1'0'
         DC    CL27' '
         DC    CL18'AKTUALISIEREN VON '
FCAT     DC    CL44' '
         DC    CL43' '
         SPACE 2
FREC3    DS    0CL133
         DC    CL42'0 FEHLERMELDUNG'
         DC    CL10'DATUM'
         DC    CL6'ZEIT'
         DC    CL11'RC AKTION'
         DC    CL45'DATEI'
         DC    CL7'VOLSER'
         DC    CL9'DEV'
         DC    CL3'SEQ'
         SPACE 2
FREC2    DS    0CL133
FASA     DC    CL1'0'
FMSGL    DC    CL1' '
FMSG     DC    CL39' '
FDTE     DC    CL6' '
         DC    CL1' '
FTME     DC    CL8' '
FRC      DC    CL4' '
         DC    CL1' '
FACT     DC    CL8' '
FDSN     DC    CL44' '
         DC    CL1' '
FVOL     DC    CL6' '
         DC    CL1' '
FDEV     DC    CL8' '
FSEQ     DC    CL4' '
         SPACE 2
ZREC     DS    0CL133
TASA     DC    CL1'0'
TXT      DC    CL12' '
         DC    CL20' '
TE       DC    CL10'   EINGABE'
         DC    CL2' '
TUE      DC    CL10'   UEBERL.'
         DC    CL2' '
TA       DC    CL10'   VERARB.'
         DC    CL2' '
TF       DC    CL10'    FEHLER'
         DC    CL2' '
TALIAS   DC    CL10' '
         DC    CL2' '
TSMFOUT  DC    CL10' '
         DC    CL30' '
         SPACE 3
VOLDEF   DSECT
VOLLEN   DS    CL2
VOLTYP   DS    CL1
         DS    CL1
VOLSERN  DS    CL6
VOLDEVTP DS    CL4
VOLFLAG1 DS    CL1
         DS    CL1
         DS    CL4
VOLFLSEQ DS    CL2
         DS    CL6
VOLELE   EQU   *-VOLLEN
         SPACE 3
CATREC   DSECT
CATDEV   DS    XL4
CATVOL   DS    CL6
CATSEQ   DS    H
CATELE   EQU   *-CATDEV
         SPACE 3
ZAE      DSECT
ZE       DS    PL5           TOTAL NO OF RECORDS FOR TYPE X
ZUE      DS    PL5           NOT SELECTED
ZA       DS    PL5           OK
ZF       DS    PL5           ERROR
ZALIAS   DS    PL5           ALIAS
ZS       DS    PL5           OUTPUT SMFDCB
         SPACE 3
*       EINGABE
         SPACE 2
SMFREC   DSECT
         IFASMFI6 61
         EJECT
*******************************************************************
*                                                                 *
*        EXIT FUER IDCAMS (AUFBAU U. UEBERGABE DER PARAMETER)     *
*                                                                 *
*******************************************************************
         SPACE 2
ALIOIN   CSECT
         SPACE 1
         XSAVE R12,INSVA,ALIOIN
         EJECT
         CNOP  0,4
         BAL   R9,*+8
         DC    V(E35)
         L     R9,0(R9)
         USING E35,R9
         SPACE 3
         L     R2,0(R1)                 USERDATA ('C' OR 'D')
         L     R3,4(R1)                 FLAGS
         L     R4,8(R1)                 RETURN RECADR
         SPACE 1
         XR    R15,R15
         CLI   0(R3),X'08'              IS IT GET?
         BE    ALIOIN1
         SPACE 1
         MVI   IOINFLAG,C' '
         B     ALRET
         SPACE 2
ALIOIN1  EQU   *
         CLI   IOINFLAG,C' '           FIRST CARD?
         BE    ALIOIN2                 YES
         SPACE 1
         CLI   IOINFLAG,C'C'           SECOND CARD?
         BE    ALIOIN3                 YES
         SPACE 1
         LA    R15,4                   END
         B     ALRET
         SPACE 2
ALIOIN2  EQU   *
         MVI   IOINFLAG,C'C'            THIS IS THE FIRST CARD
         SPACE 1
         MVC   IOINRC1,=CL80' '
         LA    R8,IOINRC1
         SPACE 1
         CLI   0(R2),C'D'               KENNZEICHEN 'DELETE'?
         BE    ALDEL
         SPACE 3
ALDEF    EQU   *                        DEFINE ALIAS
         MVC   0(L'ALCTXT,R8),ALCTXT    BUILD DEFINE-ALIAS-REC
         LA    R8,L'ALCTXT(R8)
         SPACE 1
         LA    R1,ALENT                 ADRESS OF FIELD
         LA    R11,8                    MAX. LENGTH OF FIELD
         BAL   R14,ALLEN
         SPACE 1
         EX    R5,MVCENT                MOVE ALIAS
         LA    R8,0(R5,R8)
         SPACE 1
         MVC   0(L'RELCTXT,R8),RELCTXT  MOVE TEXT
         LA    R8,L'RELCTXT(R8)
         SPACE 1
         LA    R1,ALREL                 ADRESS OF FIELD
         LA    R11,44                   MAX. LENGTH OF FIELD
         BAL   R14,ALLEN
         SPACE 2
         EX    R5,MVCREL                MOVE USER-CATALOG
         LA    R8,0(R5,R8)
         SPACE 1
         MVC   0(L'TCTXT,R8),TCTXT      MOVE TEXT
         B     ALIOIN4
         SPACE 3
ALDEL    EQU   *                        DELETE ALIAS
         MVC   0(L'ALDTXT,R8),ALDTXT    BUILD DELETE-ALIAS-REC
         LA    R8,L'ALDTXT(R8)
         MVC   0(L'ALENT,R8),ALENT      MOVE ALIAS
         LA    R8,L'ALENT(R8)
         MVC   0(L'TDTXT,R8),TDTXT      MOVE TEXT
         B     ALIOIN4
         SPACE 3
ALIOIN3  EQU   *                        SECOND CARD (DEFINE/DEL)
         MVI   IOINFLAG,C'E'            'EOF' (LAST CARD)
         MVC   IOINRC1,=CL80' '
         LA    R8,IOINRC1
         SPACE 2
         MVC   0(L'CATTXT,R8),CATTXT    MOVE TEXT
         LA    R8,L'CATTXT(R8)
         LA    R1,ALCAT                 ADRESS OF FIELD
         LA    R11,44                   MAX. LENGTH OF FIELD
         BAL   R14,ALLEN
         SPACE 2
         EX    R5,MVCCAT                MOVE CATALOG
         LA    R8,0(R5,R8)
         SPACE 1
         MVC   0(L'PASSTXT,R8),PASSTXT  MOVE TEXT
         B     ALIOIN4
         SPACE 3
ALLEN    EQU   *
         ST    R14,STORE
         SR    R5,R5
         SPACE 2
ALLEN1   EQU   *
         CLI   0(R1),C' '
         BE    ALLEN2
         SPACE 1
         CR    R5,R11
         BNL   ALLEN2
         SPACE 1
         LA    R1,1(R1)                NEXT BYTE OF FIELD
         LA    R5,1(R5)                TRUE LENGTH OF NAME
         B     ALLEN1
         SPACE 2
ALLEN2   EQU   *
         L     R14,STORE
         BR    R14
         SPACE 3
ALIOIN4  EQU   *
         MVC   0(8,R4),IOINRCA          ADRESS OF RECORD
         XR    R15,R15
         SPACE 2
ALRET    EQU   *
         XRETURN (R15)                  RETURN WITH RETURN-CODE
         EJECT
MVCENT   MVC   0(0,R8),ALENT
MVCREL   MVC   0(0,R8),ALREL
MVCCAT   MVC   0(0,R8),ALCAT
         SPACE 3
IOINRCA  DC    A(IOINRC1)
IOINRCAL DC    F'80'
         SPACE 2
STORE    DC    F'0'
         SPACE 2
*      SATZBESCHREIBUNG DEFINE ALIAS  UND DELETE ALIAS
         SPACE 1
IOINRC1  DS    0CL80
         DC    CL80' '
         SPACE 2
ALCTXT   DC    CL20' DEFINE ALIAS (NAME('
RELCTXT  DC    CL9') RELATE('                           DEFINE
TCTXT    DC    CL5')) - '
         SPACE 1
ALDTXT   DC    CL8' DELETE '                            DELETE
TDTXT    DC    CL9' ALIAS - '
         SPACE 1
CATTXT   DC    CL9' CATALOG('                           DEF/DEL
PASSTXT  DC    CL9'/MASTCAT)'
         SPACE 2
IOINFLAG DC    C' '
         SPACE 2
         LTORG
         EJECT
*******************************************************************
*                                                                 *
*        EXIT FUER IDCAMS (MESSAGES)                              *
*                                                                 *
*******************************************************************
         SPACE 2
ALIOOUT  CSECT
         SPACE 1
         XSAVE R12,OUTSVA,ALIOOUT
         EJECT
         CNOP  0,4
         BAL   R9,*+8
         DC    V(E35)
         L     R9,0(R9)
         USING E35,R9
         SPACE 3
         L     R2,0(R1)                 USERDATA ('C' OR 'D')
         L     R3,4(R1)                 FLAGS
         L     R4,8(R1)                 RETURN RECADR
         SPACE 2
         CLI   0(R3),X'0C'              IS IT PUT?
         BNE   ALOUTRET                 NO
         SPACE 1
         CLC   2(2,R3),=X'0000'
         BE    ALOUTRET
         SPACE 1
         CLC   2(2,R3),=X'FFFF'
         BE    ALOUTRET
         SPACE 1
         CLC   2(2,R3),=X'FFFE'
         BE    ALOUTRET
         SPACE 1
         L     R4,0(R4)
         CLC   1(7,R4),=C'IDC3003'
         BE    ALOUTRET
         SPACE 1
         CLI   ISCH,C'2'
         BE    ALOUTRET
         SPACE 1
         LR    R5,R4
         SR    R6,R6
         SPACE 1
ALIOOUT2 EQU   *
         LA    R5,1(R5)
         CLI   0(R5),X'00'
         BE    ALIOOUT3
         SPACE 1
         LA    R6,1(R6)
         CH    R6,=H'132'
         BL    ALIOOUT2
         SPACE 2
ALIOOUT3 EQU   *
         BCTR  R6,0
         SPACE 1
         CLI   ISCH,C'1'
         BE    ALIOOUT1
         SPACE 1
         EX    R6,MOVE1
         MVI   ISCH,C'1'
         B     ALOUTRET
         SPACE 2
ALIOOUT1 EQU   *
         EX    R6,MOVE2
         MVI   ISCH,C'2'
         SPACE 2
ALOUTRET EQU   *
         XRETURN 0
         SPACE 3
MOVE1    MVC   FMSG2A(0),1(R4)
MOVE2    MVC   FMSG2B(0),1(R4)
         SPACE 3
         LTORG
         SPACE 2
         END
//LKED.SYSLMOD DD DSN=XXXXXXX.CATOUT.LOAD(OP017),DISP=SHR
//LKED.SYSLIB  DD DSN=XXXXXXX.CATOUT.LOAD,DISP=SHR
//
