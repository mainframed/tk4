         TITLE 'DVS050HU   JCL ABRUF VON PECS-JOBLIB FUER AENDERUNGEN'
DVS050HU CSECT
         SPACE
***********************************************************************
*
*
*        ZWECK:     DIESES PROGRAMM RUFT JOB-JCL VON DER PECS-JOBLIB
*                   AB FUER JCL-AENDERUNGEN GROESSEREN UMFANGS
*
*
*
*        AUTHOR:            ARMIN HUST
*                           HOECHST AG
*                           ADV / K706
*                           6230 FRANKFURT/M 80
*                           TEL  0611 / 305-2550
*
*
*        DATE WRITTEN:      DEC 1977
*
*
***********************************************************************
*
*        SOFTWARE-UMGEBUNG:
*        OS VS2 3.7
*        PECS 2.7 - 2.B
*
***********************************************************************
*
*        HINWEIS FUER MODIFIKATIONEN:
*        DIE REGISTER 11 UND 12 SIND UNBENUTZT
*
***********************************************************************
*
*        RETURN CODES
*          0      ALLES OKAY
*          4      1. KEINE AUSWAHLKARTEN EINGELESEN
*                 2. TABELLE DER JOBNAMEN ZU KLEIN (ZUR ZEIT 7500)
*                 3. KEINE JOBS AUSGEGEBEN
*                 4. TABELLE FUER PROC-STATEMENT ZU KLEIN (Z. Z. 50)
*
***********************************************************************
         EJECT
***********************************************************************
*
*        ADRESSIERBARKEIT FUER DAS MODUL UEBER
*        <R2> , <R3>  ADRESSIEREN BIS ZUR SAVEAREA
*        <R13>  ADRESSIERT VON SAVEAREA BIS ENDE
*
***********************************************************************
         USING *,2,3
START    SAVE  (14,12)
         LR    2,15               <2> = ENTRY POINT
         B     START1
         DS    0F
BASREG2  DC    A(START+4096)
*
*
START1   L     3,BASREG2           BASISREGISTER 2 LADEN
         LA    9,SAVEAREA
         ST    9,8(13)
         ST    13,SAVEAREA+4       SAVEAREA CHAINING
         LR    13,9
         USING SAVEAREA,13
         LR    5,1                     SAVE PARM-ADRESS
         OPEN  (EXTDATEI,(INPUT),                                      *
               LOADLIB,(INPUT),                                        *
               AUSGABEN,(OUTPUT),                                      *
               MELDUNG1,(OUTPUT),                                      *
               MELDUNG2,(OUTPUT),                                      *
               AUSWAHL1,(OUTPUT))
         MVI   PECS3,X'00'        DIESE VIER SCHALTER ZEIGEN AN,
         MVI   JCLTYP,X'00'       WELCHE PARM-EINTRAGUNG VORHANDEN
         MVI   COMP,X'00'         IST. X'00' HEISST, DIE ENTSPRECHENDE
         MVI   WTO,X'00'          PARMEINTRAGUNG IST NICHT VORHANDEN
         L     5,0(5)
         LH    6,0(5)
         LTR   6,6
         BZ    A50
         LA    5,2(5)
         SPACE 5
A10      CLC   0(5,5),=C'PECS3'   WELCHE PARMEINTRAGUNG IST VORHANDEN?
         BNE   A11                ENTSPRECHENDEN SCHALTER AUF X'FF'
         MVI   PECS3,X'FF'        SETZEN
         B     A20
A11      CLC   0(6,5),=C'JCLTYP'
         BNE   A12
         MVI   JCLTYP,X'FF'
         B     A20
A12      CLC   0(4,5),=C'COMP'
         BNE   A13
         MVI   COMP,X'FF'
         B     A20
A13      CLC   0(3,5),=C'WTO'
         BNE   A14
         MVI   WTO,X'FF'
         B     A20
A14      EQU   *
A20      LA    5,1(5)              NAECHSTES ZEICHEN VON PARM ADRESS.
         BCT   6,A10
         SPACE 5
A50      SR    8,8                 INDEX = 0
         ST    8,JOBZAUS           JOBZAUS = 0
         ST    8,SUCHTIND
         ST    8,PROCIND           PROCIND = 0
         ZAP   AUSZAE,=P'0'        AUSZAE = 0
         ZAP   ZEIZAE,=P'0'
         ZAP   PECSZAE,=P'0'
         LA    6,EXTAB             EXTAB = SPACE
         LA    7,L'EXTAB
         SR    8,8
         L     9,LOEZ              BLANK LADEN
         MVCL  6,8
         EXTRACT JOBID,,FIELDS=TIOT   GET JOBNAME
         L     6,JOBID
         MVC   JOBNAME,0(6)
         CLI   JOBNAME8,C' '      BEI TSO-USER WIRD 'WTO' IN DER
         BNE   A80                PARMEINTARGUNG IGNORIERT
         MVI   WTO,X'00'
A80      MVI   SCHE,X'00'         ENDE-SCHALTER RUECKSETZEN
         LA    7,L'EXTAB1         LAENGE VON EXTAB1
         ST    7,LEXTAB1
         LA    7,80
         ST    7,LPROCTAB
         LA    7,8
         ST    7,LSUCHTAB
         EJECT
***********************************************************************
*
*   PRO JOBNAME WERDEN 8 BYTES IN DER SUCHTAB BENOETIGT
*   JETZT SIND MAXIMAL 7500 JOBS MOEGLICH
*   FUER  DIE PROCTAB (TABELLE FUER DAS PROC-STATEMENT) WERDEN
*   4000 BYTES BENOETIGT (50 PROC-STATEMENT-SAETZE)
*
***********************************************************************
         GETMAIN R,LV=64000
         ST    1,PROCTAB             ADRESSE DER PROCTAB
         LA    1,4000(0,1)
         ST    1,SUCHTAB             ADRESSE DER SUCHTAB
         SPACE 5
***********************************************************************
*
*        DATUM UND UHRZEIT BERECHNEN
*
***********************************************************************
         TIME  DEC,ZONE=LT
         ST    1,DATEP
         UNPK  DAT+1(5),DATEP
         OI    DAT+5,X'F0'
         MVC   DAT(2),DAT+1
         MVI   DAT+2,C'.'
         LA    5,X'0C'
         LR    1,0
         SRL   1,4
         OR    1,5
         ST    1,ZEITP
         UNPK  ZEIT+2(6),ZEITP
         OI    ZEIT+7,X'F0'
         MVC   ZEIT(2),ZEIT+2
         MVI   ZEIT+2,C'.'
         MVC   ZEIT+3(2),ZEIT+4
         MVI   ZEIT+5,C'.'
         SPACE 5
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD9),MELD9
         MVC   ZEILE+7(8),JOBNAME
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         MVC   ZEILE1+6(6),DAT
         MVC   ZEILE1+22(8),ZEIT
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD19),MELD19
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVI   VOR,C'-'
         MVC   ZEILE1,BLANKS
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVI   VOR,C' '
         MVI   ES,X'00'           ENDE-SCHALTER ZURUECKSETZEN
         MVI   SCHALL,C'N'        SCHALTER ALL ZURUECKSETZEN
EINS1    LA    8,1(0,8)           INDEX = INDEX + 1
         SPACE 2
***********************************************************************
*
*  GROESSE DER EXTAB (TABELLE DER AUSWAHLKARTEN)
*  Z.Z. 100
*
***********************************************************************
         SPACE 2
         L     7,=F'100'
         CR    8,7                 INDEX > 100 ?
         BH    ZWEI
DREI     GET   EXTDATEI,EXBER      LESEN EXTDATEI
         CLC   EXBER1,EJOB         STELLE1-4 = 'JOB=' ?
         BNE   VIER
         CLC   EXBER2,ALL          JOB=ALL ?
         BNE   DREI01
         MVI   SCHALL,C'J'         SCHALTER-ALL SETZEN
         B     DREI10
         SPACE 2
DREI01   CLI   EXBER2+7,C' '       STELLE 8 BLANK ?
         BNE   DREI10              NEIN
         CLI   EXBER2+6,C' '       JA - DANN MUSS STELLE 7 BLANK SEIN
         BE    DREI1
         MVC   ZEILE,BLANKS
         MVC   ZEILE1+5(80),EXBER
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD16),MELD16
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         B     DREI                NAECHSTEN SATZ LESEN
         SPACE 2
DREI1    CLI   EXBER2+5,C' '       STELLE 6 BLANK ?
         BNE   DREI10              NEIN
         CLI   EXBER2+4,C' '       JA - DANN MUSS STELLE 5 BLANK SEIN
         BE    DREI10
         MVC   ZEILE,BLANKS
         MVC   ZEILE1+5(80),EXBER
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD16),MELD16
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         B     DREI                 NAECHSTEN SATZ LESEN
         SPACE 2
DREI10   LA    7,EXTAB             <7> = ADRESSE VON EXTAB
         LR    5,8                 <5> = INDEX
         M     4,LEXTAB1           <5> = INDEX * L'EXTAB1
         LA    7,0(7,5)            <7> = A(EXTAB) + <5>
         S     7,LEXTAB1           <7> = <7> - L'EXTAB1
         MVC   0(8,7),EXBER2       JOBNAME AUS EINGABE SPEICHERN
         B     EINS1
         SPACE 2
VIER     MVC   ZEILE,BLANKS        'JOB=' FEHLT
         MVC   ZEILE1+5(80),EXBER  FALSCHE AUSWAHLKARTE
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD1),MELD1
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         B     DREI
         SPACE 2
ZWEI     GET   EXTDATEI,EXBER      EXTAB ZU KLEIN
         MVC   ZEILE,BLANKS
         MVC   ZEILE1+5(80),EXBER
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD2),MELD2
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         B     ZWEI                NACHLESEN BIS EOF
         SPACE 2
ENDEEXT  BCTR  8,0            ENDE AUF DATEI EXTDATEI
         ST    8,INDEX        INDEX = INDEX - 1
         LTR   8,8            TEST, OB AUSWAHLKARTEN
         BNZ   SORT           EINGELESEN WURDEN
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD8),MELD8
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         L     13,4(13)
         RETURN (14,12),RC=4    CONDITION-CODE = 4
         EJECT
***********************************************************************
*
*   SORTIERUNG DER EINGELESENEN JOBNAMEN IN AUFSTEIGENDER FOLGE
*
*    R 8  =  I
*    R 7  =  K
*    R 9  =  J
*
*   SORTIERVERFAHREN:  SORTIEREN DURCH AUSWAHL
*
***********************************************************************
SORT     SR    8,8           I = 0
SORT01   LA    8,1(0,8)      I = I + 1
         C     8,INDEX       I = INDEX ?
         BE    WEITER
         LR    7,8           K = I
         LA    9,1(0,8)      J = I + 1
SORT02   LA    6,EXTAB
         LR    5,7
         BCTR  5,0
         M     4,LEXTAB1
         LA    6,0(5,6)
         LA    10,EXTAB
         LR    5,9
         BCTR  5,0
         M     4,LEXTAB1
         LA    10,0(5,10)        <10> = EXTAB(J)
         CLC   0(8,6),0(10)      EXTAB(K) > EXTAB(J)
         BL    SORT03
         LR    7,9
SORT03   LA    9,1(0,9)
         L     6,INDEX
         LA    6,1(0,6)
         CR    9,6
         BNE   SORT02
         LA    6,EXTAB
         LR    5,8
         BCTR  5,0
         M     4,LEXTAB1
         LA    6,0(5,6)          <6> = EXTAB(I)
         LA    10,EXTAB
         LR    5,7
         BCTR  5,0
         M     4,LEXTAB1
         LA    10,0(5,10)       <10> = EXTAB(K)
         MVC   ZWISCHEN,0(6)
         MVC   0(8,6),0(10)
         MVC   0(8,10),ZWISCHEN
         B     SORT01
***********************************************************************
*
*    ENDE DER SORTIERUNG
*
***********************************************************************
         EJECT
***********************************************************************
*
*    LADEN UND ERSTER AUFRUF VON  SVRPJ0
*     (MIT INDIZIERTER AUSWAHL, FALLS JOBNAMEN EINGEGEBEN WURDEN)
*     (MIT 'ALL', FALLS JOB=ALL EINGEGEBEN WURDE)
*
***********************************************************************
         SPACE 2
WEITER   CLI   PECS3,X'FF'          BEI UMSTELLUNG NACH PECS3
         BNE   A100                 WIRD '-LOGON XXXX' GESCHRIEBEN
         MVC   AUSBER(80),LOGON
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
A100     MVI   VOR,C'-'
         MVC   ZEILE1,BLANKS
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVI   VOR,C' '
         SR    8,8
         ST    8,LIND
         LOAD  EP=SVRPJ0,DCB=LOADLIB
         ST    0,EPSVRPJO
         CLI   SCHALL,C'J'      JOB=ALL ?
         BNE   WEITER1          NEIN
         MVC   PROCNAME,ALL
WEITER1  CLI   SCHALL,C'J'      JOB=ALL ?
         BNE   WEIT01           NEIN
         CLI   TERM,X'FF'       JA - WENN SVRPJ0 ENDE MELDET, DANN
         BE    ENDEPECS         PROGRAMMENDE
         SR    9,9
         ST    9,LIND
WEIT01   L     9,LIND
         LA    9,1(0,9)
         ST    9,LIND
         C     9,INDEX          ENDE WENN ALLE EINGABEKARTEN
         BH    ENDEPECS         ABGEARBEITET SIND
         XC    CB,CB
         CLI   SCHE,X'FF'       WENN SCHE = X'FF', WERDEN DIE ENDE
         BNE   WEIT1            SAETZE FUER JEDEN JOB GESCHRIEBEN
         CLI   PECS3,X'FF'
         BNE   B50
         MVC   AUSBER(80),SAVE
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         B     B100
B50      MVC   AUSBER(80),EJ
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         MVC   AUSBER(80),EN
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'2'
B100     MVI   SCHE,X'00'
WEIT1    LA    7,EXTAB
         BCTR  9,0
         M     8,LEXTAB1
         LA    7,0(9,7)
         CLI   SCHALL,C'J'           JOB = ALL ?
         BE    WEITER2               JA
         MVC   PROCNAME,0(7)         NEIN - IMMER MIT INDIZIERTER
         MVC   PROCNAME+6(2),=C'..'         AUSWAHL LESEN
         CLC   PROCNAME+4(2),=C'  '
         BNE   WEITER2
         MVC   PROCNAME+4(2),=C'..'
WEITER2  L     15,EPSVRPJO
         CALL  (15),(PROCNAME,JCLREC,IDENT,CB)
         AP    PECSZAE,=P'1'
         CLI   TERM,X'FF'
         BNE   ZEHN1
         CLI   SCHALL,C'J'
         BE    ENDEPECS
         B     WEITER1
         SPACE
***********************************************************************
*
*  BEI ARBEITSGEBIETSWECHSEL WIRD EIN WTO MIT ROUTING-CODE 15
*  AUSGEGEBEN
*
***********************************************************************
         SPACE 2
ZEHN1    CLI   WTO,X'00'
         BE    ZEHN10
         CLC   ZWISCHEN(4),PROCNAME
         BE    ZEHN10
         MVC   ARBWTO+10(8),JOBNAME
         MVC   ARBWTO+46(4),PROCNAME
         LA    8,ARBWTO
         WTO   MF=(E,(8))
ZEHN10   MVC   ZWISCHEN,PROCNAME  ZWISCHEN=PROCNAME
         SPACE
***********************************************************************
*
*
*    TEST, OB DER GELESENE PROC-NAME IN DER EXTAB VORHANDEN IST.
*         FALLS JA,   SCHE AUF X'FF' SETZEN.
*         FALLS NEIN, SCHE AUF X'00' SETZEN.
*
*
***********************************************************************
         SPACE 2
         SR    8,8                 I = 0
         ST    8,PROCIND
         CLI   SCHALL,C'J'
         BE    ZEHN6
ZEHN2    LA    8,1(0,8)            I = I + 1
         C     8,INDEX             I > INDEX ?
         BH    ZEHN4
         LA    7,EXTAB             ADRESSRECHNUNG
         LR    5,8
         M     4,LEXTAB1
         LA    7,0(7,5)
         S     7,LEXTAB1
         L     6,=F'7'
         EX    6,VERGL1            VERGLEICH 8-STELLIG
         BE    ZEHN6
         CLC   6(2,7),SPACE2       STELLE7-8 = SPACE ?
         BNE   ZEHN2
         S     6,=F'2'
         EX    6,VERGL1            VERGLEICH 6-STELLIG
         BE    ZEHN6
         CLC   4(2,7),SPACE2       STELLE5-6 = SPACE ?
         BNE   ZEHN2
         S     6,=F'2'
         EX    6,VERGL1            VERGLEICH 4-STELLIG
         BE    ZEHN6
         B     ZEHN2
ZEHN4    MVI   SCHE,X'00'         PROCNAME NICHT IN EXTAB
         MVI   NEXTPROC,X'FF'
         SR    8,8
ZEHN5    LA    8,1(0,8)           EXTAB ABGEARBEITET ?
         C     8,INDEX            DANN PROGRAMMENDE
         BH    ENDEPECS
         LA    7,EXTAB
         LR    5,8
         BCTR  5,0
         M     4,LEXTAB1
         LA    7,0(7,5)
         CLC   0(4,7),PROCNAME
         BL    ZEHN5
         B     ELF1
         SPACE
***********************************************************************
*
*        DURCH DIE INDIZIERTE AUSWAHL KANN ES VORKOMMEN, DASS EIN
*        JOBNAME MEHRFACH ZUR VERFUEGUNG GESTELLT WIRD. DESHALB WERDEN
*        ALLE AUSGEGEBENEN JOBNAMEN IN DER 'SUCHTAB' GESPEICHERT UND
*        VOR DER AUSGABE WIRD GEPRUEFT, OB DER JOBNAME SCHON IN DER
*        SUCHTAB VORHANDEN IST
*
***********************************************************************
         SPACE
ZEHN6    MVI   SCHE,X'FF'         JOBNAME IN SUCHTAB : UP SUCHEN
         MVI   NEXTPROC,X'00'     SETZT SCHE AUF X'00' UND NEXTPROC
         BAL   10,SUCHEN          AUF X'FF'
         CLI   NEXTPROC,X'00'     JOBNAME NICHT IN SUCHTAB : UP SUCHEN
         BNE   ELF1               SETZT SCHE AUF X'FF' UND NEXTPROC
         CLI   PECS3,X'FF'        AUF X'00'
         BNE   C50
         MVC   AUSBER(80),ADD
         MVC   AUSBER+5(8),PROCNAME
         B     C100
C50      MVC   AUSBER(80),LOADJCL
C100     AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         MVI   AUSBER,C' '
         MVC   AUSBER+1(79),AUSBER
         MVC   AUSBER(6),USER
         MVC   AUSBER+6(10),JCLREC+24
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         CLI   PECS3,X'FF'
         BNE   C200
         MVI   AUSBER+5,C' '
C200     PUT   AUSGABEN,AUSBER
         MVI   AUSBER,C' '
         MVC   AUSBER+1(79),AUSBER
         MVC   AUSBER(6),DESC
         MVC   AUSBER+6(30),JCLREC+34
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         CLI   PECS3,X'FF'
         BNE   C300
         MVI   AUSBER+5,C' '
C300     PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'3'
         L     6,JOBZAUS
         LA    6,1(0,6)
         ST    6,JOBZAUS
         L     8,SUCHTAB
         L     7,SUCHTIND
         LR    9,7
         LA    9,1(0,9)
         ST    9,SUCHTIND
         SPACE
***********************************************************************
*
*    GROESSE DER JOBNAMEN-TABELLE
*    Z.Z.  7500
*
***********************************************************************
         SPACE
         C     7,=F'7500'
         BNE   ZEHN7
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD17),MELD17
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         L     13,4(13)
         RETURN (14,12),RC=4    JOBNAMEN-TABELLE ZU KLEIN
         SPACE 2
ZEHN7    M     6,LSUCHTAB
         LA    8,0(7,8)
         MVC   0(8,8),PROCNAME
         MVC   ZEILE,BLANKS
         MVC   ZEILE1+5(8),PROCNAME
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
ELF1     L     15,EPSVRPJO
         CALL  (15),(PROCNAME,JCLREC,IDENT,CB)
         CLI   TERM,X'FF'          EOF ?
         BE    WEITER1
         AP    PECSZAE,=P'1'       PECSZAE = PECSZAE + 1
         CLC   PROCNAME,ZWISCHEN   PROCNAME = ZWISCHEN ?
         BNE   ELF2
         CLI   SCHE,X'FF'
         BNE   ELF1
         CLI   JCLTYPE,C'H'
         BNE   ELF3
***********************************************************************
*
*        HISTORY-STATEMENT VERARBEITEN
*
***********************************************************************
         MVC   AUSBER(5),HST
         MVC   AUSBER+5(75),JCLREC
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         CLI   PECS3,X'FF'
         BNE   D50
         MVI   AUSBER+4,C' '
         TR    AUSBER(80),PECS3TAB   CONVERT AMPERSAND TO DOLLAR
D50      PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         B     ELF1
ELF2     CLI   SCHE,X'FF'
         BNE   ZEHN1
***********************************************************************
*
*        JOB-ENDEBEHANDLUNG
*
***********************************************************************
         CLI   PECS3,X'FF'
         BNE   D100
         MVC   AUSBER(80),SAVE
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         B     D200
D100     MVC   AUSBER(80),EJ
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         MVC   AUSBER(80),EN
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'2'
D200     MVI   SCHE,X'00'
         B     ZEHN1
ELF3     CLI   JCLTYPE,C'P'
         BNE   ELF35
***********************************************************************
*
*        PROC-STATEMENT VERARBEITEN
*
***********************************************************************
         MVI   SCHPROC,X'FF'
ELF31    L     7,PROCIND
         LA    7,1(0,7)
         C     7,=F'51'
         BNL   ELF32
         ST    7,PROCIND
         BCTR  7,0
         M     6,LPROCTAB
         L     8,PROCTAB
         LA    8,0(7,8)
         MVC   0(80,8),JCLREC
         B     ELF1
ELF32    MVI   VOR,C'-'            TABELLE FUER PROC-STATEMENT
         MVC   ZEILE1,BLANKS       ZU KLEIN
         MVC   ZEILE1(L'MELD15),MELD15
         MVC   ZEILE1+17(8),PROCNAME
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVI   ES,X'FF'             FEHLERSCHALTER SETZEN
         B     ENDEPECS
ELF35    CLI   JCLTYPE,C'J'
         BNE   ELF350
***********************************************************************
*
*        JOB-STATEMENT VERARBEITEN
*
***********************************************************************
         CLI   COMP,X'FF'            VERGLEICH : MEMBERNAME AUS
         BNE   ELF351                PECS-JOBLIB MIT JOBNAME AUS
         CLC   PROCNAME,JCLREC+2     JCL-STATEMENT
         BE    ELF351
         MVC   AUSWZ,BLANKS
         MVC   AUSWZ(L'MELD18),MELD18
         MVC   AUSWZ+4(8),PROCNAME
         MVC   AUSWZ+45(8),JCLREC+2
         PUT   AUSWAHL1,AUSWZ
         B     ELF351
ELF350   CLI   JCLTYPE,X'00'         FOLGESTATEMENT FUER JOB- UND
         BNE   ELF352                PROC-STATEMENT VERARBEITEN
         CLI   SCHPROC,X'FF'
         BE    ELF31
         B     ELF37
ELF351   MVI   SCHPROC,X'00'
         MVC   AUSBER(80),JCLREC
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         CLI   JCLTYP,X'FF'
         BNE   ELF3510
         MVC   AUSBER+80(2),=C'J '   JOB-STATEMENT KENNZEICHNEN
ELF3510  PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         L     15,EPSVRPJO           JOB-FOLGE-STATEMENT LESEN
         CALL  (15),(PROCNAME,JCLREC,IDENT,CB)
         AP    PECSZAE,=P'1'
         CLI   JCLTYPE,X'00'         JOB-FOLGE-STATEMENT IST UNGLEICH
         BE    ELF351                X'00', DANN PROC-STATEMENT
ELF352   MVI   SCHPROC,X'00'         AUSGEBEN
         L     8,PROCTAB
         SR    7,7
ELF36    LA    7,1(0,7)
         C     7,PROCIND
         BH    ELF361
         LR    5,7
         BCTR  5,0
         M     4,LPROCTAB
         LA    6,0(5,8)
         MVC   AUSBER(80),0(6)
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         CLI   JCLTYP,X'FF'
         BNE   ELF360
         MVC   AUSBER+80(2),=C'P '   PROC-STATEMENT KENNZEICHNEN
ELF360   PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         B     ELF36
ELF361   SR    7,7
         ST    7,PROCIND
         MVC   AUSBER(80),JCLREC
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         CLI   JCLTYP,X'FF'
         BNE   ELF369
***********************************************************************
*
*        EXEC-STATEMENT, DATEN-KARTE, DD-STATEMENT KENNZEICHNEN
*        (JEWEILS NUR DAS ERSTE STATEMENT, FORTSTEZUNGSKARTEN
*        WERDEN NICHT GEKENNZEICHNET)
*
***********************************************************************
         CLI   JCLTYPE,C'E'
         BNE   ELF362
         MVC   AUSBER+80(2),=C'E '
         B     ELF369
ELF362   CLI   JCLTYPE,C'I'
         BNE   ELF363
         MVC   AUSBER+80(2),=C'I '
         B     ELF369
ELF363   CLI   JCLTYPE,C'D'
         BNE   ELF369
         MVC   AUSBER+80(2),=C'D '
ELF369   PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         B     ELF1
ELF37    MVC   AUSBER(80),JCLREC
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         CLI   JCLTYP,X'FF'
         BNE   ELF375
***********************************************************************
*
*        EXEC-STATEMENT, DATEN-KARTE, DD-STATEMENT KENNZEICHNEN
*        (JEWEILS NUR DAS ERSTE STATEMENT, FORTSTEZUNGSKARTEN
*        WERDEN NICHT GEKENNZEICHNET)
*
***********************************************************************
         CLI   JCLTYPE,C'E'
         BNE   ELF372
         MVC   AUSBER+80(2),=C'E '
         B     ELF375
ELF372   CLI   JCLTYPE,C'I'
         BNE   ELF373
         MVC   AUSBER+80(2),=C'I '
         B     ELF375
ELF373   CLI   JCLTYPE,C'D'
         BNE   ELF375
         MVC   AUSBER+80(2),=C'D '
ELF375   PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         B     ELF1
         SPACE 5
***********************************************************************
*
*        ENDE AUF PECS-JOBLIB ERREICHT
*        ENTWEDER BEI INDIZIERTER AUSWAHL NACHDEM ALLE EINGABEKARTEN
*        IN DER EXTAB VERARBEITET WURDEN
*        ODER WENN SVRPJ0 BEI 'ALL' ENDE MELDET
*
***********************************************************************
ENDEPECS SR    8,8
         C     8,JOBZAUS
         BE    ENDE5
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD5),MELD5
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         CLI   SCHE,X'FF'          WURDE DER LETZTE JOB AUS PECS-JOBL.
         BNE   ENDE4               AUF AUSGABE GESCHRIEBEN ?
         CLI   PECS3,X'FF'         JA - DANN ENDEBEHANDLUNG
         BNE   E100
         MVC   AUSBER(80),SAVE
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         B     ENDE4
E100     MVC   AUSBER(80),EJ
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         MVC   AUSBER(80),EN
         AP    ZEIZAE,=P'10'
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'2'
ENDE4    MVC   AUSBER(80),EM         ENDMAIN-STATEMENT FUER PECS2
         AP    ZEIZAE,=P'10'         END-STATEMENT FUER PECS3
         UNPK  AUSBER+80(8),ZEIZAE
         OI    AUSBER+87,X'F0'
         CLI   PECS3,X'FF'
         BNE   F100
         MVI   AUSBER+2,C'N'
F100     PUT   AUSGABEN,AUSBER
         AP    AUSZAE,=P'1'
         MVC   ZEILE1,BLANKS
         MVC   ZEILE1(L'MELD6),MELD6
         L     7,JOBZAUS
         CVD   7,DW
         UNPK  ZEILE1+21(8),DW
         OI    ZEILE1+28,X'F0'
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         B     ENDE6
ENDE5    MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD12),MELD12
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVI   ES,X'FF'           FEHLERSCHALTER SETZEN
ENDE6    MVI   VOR,C'-'
         MVC   ZEILE1,BLANKS
         MVC   ZEILE1(L'MELD13),MELD13
         UNPK  ZEILE1+40(8),PECSZAE
         OI    ZEILE1+47,X'F0'
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         MVC   ZEILE1(L'MELD14),MELD14
         UNPK  ZEILE1+40(8),AUSZAE
         OI    ZEILE1+47,X'F0'
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD10),MELD10
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         MVC   ZEILE,BLANKS
         SR    8,8
ENDE1    LA    8,1(0,8)
         C     8,INDEX
         BH    ENDE2
         LA    7,EXTAB
         LR    5,8
         BCTR  5,0
         M     4,LEXTAB1
         LA    7,0(7,5)
         MVC   ZEILE1+10(8),0(7)
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         B     ENDE1
ENDE2    MVC   ZEILE,BLANKS
         MVC   ZEILE(L'MELD11),MELD11
         L     7,INDEX
         CVD   7,DW
         UNPK  ZEILE1+37(8),DW
         OI    ZEILE1+44,X'F0'
         PUT   MELDUNG2,ZEILE
         PUT   MELDUNG1,ZEILE
         CLOSE (EXTDATEI,,AUSGABEN,,MELDUNG2,,MELDUNG1,,LOADLIB,,      *
               AUSWAHL1,)
         CLI   ES,X'FF'        FEHLERSCHALTER GESETZT ?
         BE    ENDE3
         L     13,4(13)
         RETURN (14,12),RC=0
ENDE3    L     13,4(13)
         RETURN (14,12),RC=4
VERGL1   CLC   0(1,7),ZWISCHEN
         EJECT
***********************************************************************
*
*
*    DIE TABELLE, DIE ALLE JOBNAMEN ENTHAELT WIRD
*    DURCHSUCHT, OB DER AKTUELLE NAME SCHON IN IHR ENTHALTEN
*    IST.
*    FALLS JA, WIRD SCHE AUF X'00' GESETZT, NEXTPROC AUF X'FF'
*        <R10> = BAL - REGISTER
*
*
***********************************************************************
         SPACE 2
SUCHEN   SR    7,7
SUCH1    LA    7,1(0,7)
         C     7,SUCHTIND
         BH    SUCHENDE
         LR    5,7
         BCTR  5,0
         M     4,LSUCHTAB
         L     8,SUCHTAB
         LA    8,0(5,8)
         CLC   0(8,8),PROCNAME
         BNE   SUCH1
         MVI   SCHE,X'00'
         MVI   NEXTPROC,X'FF'
SUCHENDE BR    10
         EJECT
***********************************************************************
         SPACE 2
         PRINT NOGEN
         SPACE 2
EXTDATEI DCB   DDNAME=EXTDATEI,                                        *
               DSORG=PS,                                               *
               MACRF=GM,                                               *
               LRECL=80,                                               *
               EODAD=ENDEEXT,                                          *
               RECFM=FB
         SPACE 2
AUSGABEN DCB   DDNAME=AUSGABEN,                                        *
               DSORG=PS,                                               *
               MACRF=PM,                                               *
               LRECL=88,                                               *
               RECFM=FB
         SPACE 2
MELDUNG2 DCB   DDNAME=MELDUNG2,                                        *
               DSORG=PS,                                               *
               MACRF=PM,                                               *
               LRECL=133,                                              *
               BLKSIZE=3990,                                           *
               RECFM=FBA
         SPACE 2
MELDUNG1 DCB   DDNAME=MELDUNG1,                                        *
               DSORG=PS,                                               *
               MACRF=PM,                                               *
               LRECL=133,                                              *
               BLKSIZE=3990,                                           *
               RECFM=FBA
         SPACE 2
LOADLIB  DCB   DDNAME=LOADLIB,                                         *
               DSORG=PO,                                               *
               MACRF=(R)
         SPACE 2
AUSWAHL1 DCB   DDNAME=AUSWAHL1,                                        *
               DSORG=PS,                                               *
               MACRF=PM,                                               *
               LRECL=80,                                               *
               RECFM=FB
         SPACE 2
         PRINT GEN
***********************************************************************
         EJECT
         DS    0F
EPSVRPJO DS    F'0'        ENTRY-POINT VON 'SVRPJ0'
JOBZAUS  DS    F           ZAEHLER FUER AUSGEGEBENE JOBS
JOBID    DS    F           ADRESSE TIOT
INDEX    DS    F           ZAEHLER FUER EINGEGEBENE AUSWAHLKARTEN
IND      DS    F
PROCTAB  DS    F           ADRESSE TABELLE FUER PROC-STATEMENT
PROCIND  DS    F           INDEX FUER PROCTAB
LEXTAB1  DS    F           LAENGE EINES ELEMENTES IN EXTAB  ( 8 )
LPROCTAB DS    F           LAENGE EINES ELEMENTES IN PROCTAB ( 80 )
LSUCHTAB DS    F           LAENGE EINES ELEMENTES IN SUCHTAB ( 8 )
SUCHTAB  DS    F           ADRESSE TABELLE FUER GESCHRIEBENE JOBNAMEN
SUCHTIND DS    F           INDEX FUER SUCHTAB
LIND     DS    F           LAUFENDER INDEX FUER EXTAB
DATEP    DS    F           DATUM GEPACKT
ZEITP    DS    F           UHRZEIT GEPACKT
         DS    0D
DW       DS    D           DOPPELWORT FUER CVD / CVB
AUSZAE   DS    CL8         ZEILENNUMMERIERUNG FUER AUSGABE VON JCL
PECSZAE  DS    CL8         ZAEHLER GELESENE SAETZE VON PECS-JOBLIB
ZEIZAE   DS    CL8         ZEILENZAEHLER FUER AUSGABE VON JCL
ZWISCHEN DS    CL8         ZWISCHENSPEICHER FUER PROCNAME AUS JOBLIB
EXTAB    DS    0CL800      TABELLE FUER AUSWAHLKARTEN
EXTAB1   DS    100CL8
SPACE    DC    C' '
SPACE2   DC    C'  '
         DS    0F
LOEZ     DC    X'40000000' BLANK ZUM LOESCHEN DER TABELLEN MIT MVCL
EJOB     DC    CL4'JOB='
ALL      DC    CL8'ALL     '
BLANKS   DC    CL133' '
         SPACE
ZEILE    DS    0CL133      AUSGABEBEREICH FUER 'MELDUNG1' / 'MELDUNG2'
VOR      DS    CL1         VORSCHUBZEICHEN
ZEILE1   DS    CL132       DRUCKZEILE
AUSWZ    DS    CL80        AUSGABEBEREICH FUER 'AUSWAHL1'
         EJECT
         LTORG
         SPACE 2
SAVEAREA DS    18F         SAVEAREA
         SPACE
AUSBER   DS    CL88        AUSGABEBEREICH FUER 'AUSGABEN'
         SPACE
EXBER    DS    0CL80       EINGABEBEREICH FUER 'EXTDATEI'
EXBER1   DS    CL4
EXBER2   DS    CL8
EXBER3   DS    CL68
         SPACE 3
***********************************************************************
*
*        UEBERGABEPARAMETER FUER UP 'SVRPJ0'
*
***********************************************************************
         SPACE
PROCNAME DS    CL8            MEMBERNAME AUS PECS-JOBLIB
JCLREC   DS    CL80           JCL-STATEMENT
IDENT    DS    0CL8           STATEMENT-IDENTIFIER-BYTES
JCLTYPE  DS    XL1                 RECORD TYPE
PARMKEY  DS    XL1                 PARAMETER KEY
POSITEM  DS    XL1                 POSITION OF ITEM
REFBACK  DS    XL1                 REFERBACK KEY
POSCOMMA DS    XL1                 POSITION OF COMMA
STMTNR   DS    XL2                 STATEMENT NUMBER
UPDT     DS    XL1                 LAST UPDATED RECORD
CB       DS    0CL5           CONTROL-BYTES
NEWPROC  DS    XL1
NEXTPROC DS    XL1                 X'FF' = GET NEXT PROC OF JOBLIB
TERM     DS    XL1                 X'FF' = END OF MEMBER
NOTFOUND DS    XL1                 X'FF' = MEMBER NOT FOUND
TERMIO   DS    XL1
         SPACE 5
SCHE     DS    CL1       ZEIGT AN, OB JOB-ENDE-SAETZE '-EJ', '-EN'
*                        GESCHRIEBEN WERDEN MUESSEN, WENN JOB GEFUNDEN
SCHALL   DS    CL1       C'J' WENN JOB=ALL, SONST C'N'
ES       DS    CL1       X'FF' WENN FEHLER AUFGETRETEN, DANN
*                        RETURN-CODE = 4
PECS3    DS    CL1       SCHALTER FUER AUSGABE FUER PECS3
JCLTYP   DS    CL1       SCHALTER FUER AUSGABE VON JCLTYP
COMP     DS    CL1       SCHALTER FUER AUSGABE VON NICHT UEBEREIN
*                        STIMMENDEN JOBNAMEN
WTO      DS    CL1       SCHALTER FUER WTO BEI ARBEITSGEBIETSWECHSEL
SCHPROC  DS    CL1       SOLANGE PROC-STATEMENT VERARBEITET WIRD,
*                        ENTHAELT SCHPROC X'FF'
DAT      DS    CL6       TAGESDATUM 'YY.DDD'
ZEIT     DS    CL8       UHRZEIT 'HH.MM.SS'
JOBNAME  DS    0CL8      JOBNAME AUS TIOT
         DS    CL7
JOBNAME8 DS    CL1       BLANK, WENN VON TSO-USER AUFGERUFEN
         SPACE 2
***********************************************************************
*
*        TABELLE ZUM UEBERSETZEN VON AMPERSAND X'50' IN
*        DOLLAR X'5B', WENN AUSGABE FUER PECS3
*        (NUR HISTORY-STATEMENTS WERDEN UEBERSETZT)
*
***********************************************************************
PECS3TAB DS    0CL256
         DC    XL16'000102030405060708090A0B0C0D0E0F'
         DC    XL16'101112131415161718191A1B1C1D1E1F'
         DC    XL16'202122232425262728292A2B2C2D2E2F'
         DC    XL16'303132333435363738393A3B3C3D3E3F'
         DC    XL16'404142434445464748494A4B4C4D4E4F'
         DC    XL16'5B5152535455565758595A5B5C5D5E5F'
         DC    XL16'606162636465666768696A6B6C6D6E6F'
         DC    XL16'707172737475767778797A7B7C7D7E7F'
         DC    XL16'808182838485868788898A8B8C8D8E8F'
         DC    XL16'909192939495969798999A9B9C9D9E9F'
         DC    XL16'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    XL16'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         SPACE 2
ARBWTO   WTO   '****  XXXXXXXX  PECSOUT     ARBEITSGEBIET XXXX',       *
               ROUTCDE=15,MF=L
         SPACE 2
***********************************************************************
*
*        ALLE PECS-CONTROL-STATEMENTS BEGINNEN MIT '-'
*
***********************************************************************
LOADJCL  DC    CL80'-LOAD-JCL,NOPCALL '
USER     DC    CL6'-USER='
DESC     DC    CL6'-DESC='
HST      DC    CL5'-HST='
EJ       DC    CL80'-EJ'
EN       DC    CL80'-EN'
EM       DC    CL80'-EM'
SAVE     DC    CL80'-SAVE'
LOGON    DC    CL80'-LOGON ....'
ADD      DC    CL80'-ADD ........,JCL'
         SPACE 2
***********************************************************************
*
*        AUSGABEMELDUNGEN AUF DATEIEN 'MELDUNG2' , 'MELDUNG1'
*
***********************************************************************
MELD1    DC    C'     FALSCHE AUSWAHLKARTE EINGEGEBEN '
MELD2    DC    C'     KARTE NICHT VERARBEITET, TABELLE ZU KLEIN '
MELD4    DC    C'     EOF BEIM ERSTEN LESEN DER PECS-JOBLIB '
MELD5    DC    C' DIESE JOBS WURDEN AUF PLATTE GESCHRIEBEN '
MELD6    DC    C'ANZAHL DER JOBS    '
MELD8    DC    C'     ES WURDEN KEINE AUSWAHLKARTEN EINGELESEN '
MELD9    DC    C'1      ........ - LAUFPROTOKOLL '
MELD10   DC    C'-      FOLGENDE AUSWAHLKARTEN WURDEN VERARBEITET '
MELD11   DC    C'      ANZAHL DER AUSWAHLKARTEN   '
MELD12   DC    C' ES WURDEN KEINE JOBS AUF PLATTE GESCHRIEBEN '
MELD13   DC    C'     SAETZE PECS-JOBLIB GELESEN '
MELD14   DC    C'     SAETZE AUF AUSGABE GESCHRIEBEN '
MELD15   DC    C'   *****   JOB =              TABELLE PROC ZU KLEIN   *
               *****'
MELD16   DC    C'     JOBNAME DARF NUR 4-, 6- ODER 8-STELLIG SEIN'
MELD17   DC    C'     TABELLE FUER JOBNAMEN IST ZU KLEIN'
MELD18   DC    C'JOB=........ (MEMBERNAME)     JOBNAME IN JCL=........'
MELD19   DC    C'       MODUL DVS050HU UMGEWANDELT AM &SYSDATE'
         SPACE 2
         END   START
