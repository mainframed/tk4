 SMF: PROC OPTIONS(MAIN);
   DCL (MONA,MONE,TAGA,TAGE) PIC'99';
   DCL ZA BIN FIXED;
   DCL USERTAB(50) CHAR(8),
   (CPUTAB(50),MAINTAB(50),ASZEITTAB(50))  FIXED(11,3),
   (TGETTAB(50),TPUTTAB(50),EXCPTAB(50),EXCP_DYN_TAB(50))
   FIXED(11),
   (ZWMIN,ZWMAX) FIXED(7);
   DCL ZWU CHAR(8),
   (ZWMAIN,ZWCPU,ZWAS) FIXED(11,3),
   (ZWGET,ZWPUT,ZWEX,ZWEXD) FIXED(11);
   DCL (CPUSUM,ASZEITSUM,MAINSUM) FIXED(11,3),
   (TGETSUM,TPUTSUM,EXCPSUM,EXCP_DYN_SUM) FIXED(11);
   DCL ID CHAR(8);
   DCL (ASZEITZW,CPUZW,MAINZW) FIXED(15,4);
   DCL ZWACC  CHAR(8);
   DCL BEWERT FILE RECORD;
   DCL UA FILE RECORD;
   RUA=(4)' ';
   ZWMIN=99365;
   ZWMAX=0;
   USERTAB=(8)'X';
   TGETTAB,TPUTTAB,EXCPTAB,EXCP_DYN_TAB,MAINTAB,ASZEITTAB,
   CPUTAB=0;
   CPUSUM,ASZEITSUM,MAINSUM,TGETSUM,TPUTSUM,EXCPSUM,
   EXCP_DYN_SUM=0;
   DCL MON(12) BIN FIXED;
   MON(1),MON(3),MON(5),MON(7),MON(8),MON(10),MON(12)=31;
   MON(4),MON(6),MON(9),MON(11)=30;
   DCL 1 UIDTAB(50),
        2 UID CHAR(8),
        2 VNR CHAR(8),
        2 RUA CHAR(4);
   DCL 1 S35 BASED(P),
        2 RES BIT(8),
        2 TYP35 BIT(8),
        2 LOGOFF_TIME BIN FIXED(31) UNAL, /* SEC/100 */
        2 LOGOFF_DATE FIXED(7),
        2 S1 CHAR(4),
        2 USERID CHAR(8),
        2 LOGON_TIME BIN FIXED(31) UNAL,   /*SEC/100 */
        2 LOGON_DATE FIXED(7),             /* 00YYDDDF */
        2 S2 CHAR(13),
        2 TPUT BIN FIXED(31) UNAL,
        2 TGET BIN FIXED(31) UNAL,
        2 S3 CHAR(62),
        2 CPU CHAR(3),
        2 ACCANZ BIT(8),
        2 ACCLEN BIT(8),
        2 ACCNR CHAR(8),
        2 R35 CHAR(3970);
   DCL 1 S34 BASED(P),
        2 RES BIT(8),
        2 TYP34 BIT(8),
        2 S1 CHAR(12),
        2 USERID CHAR(8),
        2 S2 CHAR(17),
        2 MAIN BIN FIXED(31) UNAL, /* MAIN ST. OCCUPANCY TIME */
        2 S3 CHAR(59),
        2 LEN34 BIN FIXED(15) UNAL,
        2 R34 CHAR(3992);
   DCL 1 RS34 BASED(P),
        2  R1 CHAR(104),
        2 RX34(3992) CHAR(1);
   DCL 1 S40 BASED(P),
        2 RES BIT(8),
        2 TYP40 BIT(8),
        2 T1 CHAR(12),
        2 USERID CHAR(8),
        2 T2 CHAR(38),
        2 LEN40 BIN FIXED(15) UNAL,
        2 R40 CHAR(4034);
   DCL 1 RS40 BASED(P),
        2 R1 CHAR(62),
        2 RX40(4034) CHAR(1);
   DCL 1 DEVICE BASED(P1),
        2 CLASS BIT(8),
        2 UNIT_TYP BIT(8),
        2 UNIT_ADR BIN FIXED(15) UNAL,
        2 EXCP BIN FIXED(31) UNAL;
   DCL 1 DAT1,
        2 F1 CHAR(2),
        2 JAHR1 PIC'99',
        2 TAG1 PIC'999',
   DATUM1 PIC'(7)9' DEF DAT1;
   DCL 1 DAT2,
        2 F2 CHAR(2),
        2 JAHR2 PIC'99',
        2 TAG2 PIC'999',
   DATUM2 PIC'(7)9' DEF DAT2;
   ZA=0;
   OPEN FILE(BEWERT) INPUT;
   ON ENDFILE(BEWERT) BEGIN;
   OPEN FILE(UA) OUTPUT;
   ZA=1;
   GO TO ENDE;
   END;
   ON ENDFILE(SYSIN) GO TO UNT;
 REA:  GET DATA (TAGA,MONA,TAGE,MONE);
 LESEN: READ FILE(BEWERT) SET(P);
   IF TYP34 ^= 34 & TYP35 ^= 35 & TYP40 ^= 40
   THEN GO TO LESEN;
   IF TAGA ^= 0 THEN DO;
   DATUM1=LOGON_DATE;
   IF MOD(JAHR1,4) = 0 THEN MON(2)=29;
   ELSE MON(2)=28;
   LTAG=TAG1;
   CALL KALTAG;
   IF IMONAT > MONE THEN GO TO ENDE;
   IF IMONAT = MONE & KTAG > TAGE THEN GO TO ENDE;
   IF IMONAT < MONA THEN GO TO LESEN;
   IF IMONAT = MONA & KTAG < TAGA THEN GO TO LESEN;
   END;
   IF TYP34 = 34 THEN GO TO T34;
   IF TYP35 = 35 THEN GO TO T35;
   IF TYP40 = 40 THEN GO TO T40;
   GO TO LESEN;
 T35: /* CPU,TPUT,TGET,ASZEIT */
   IF LOGON_DATE < ZWMIN THEN ZWMIN=LOGON_DATE;
   IF LOGOFF_DATE > ZWMAX THEN ZWMAX=LOGOFF_DATE;
   ID=S35.USERID;
   CALL US;
   VNR(IUSER)=ACCNR;
   CPUZW=UNSPEC(CPU);
   CPUZW=CPUZW/360000 + 0.0005;
   CPUTAB(IUSER)=CPUTAB(IUSER) + CPUZW;
   TGETTAB(IUSER)=TGETTAB(IUSER) + TGET;
   TPUTTAB(IUSER)=TPUTTAB(IUSER) + TPUT;
   IF LOGOFF_TIME < LOGON_TIME THEN
   LOGOFF_TIME=LOGOFF_TIME + 24*360000;
   ASZEITZW=LOGOFF_TIME - LOGON_TIME;
   ASZEITZW=ASZEITZW/360000 + 0.0005;
   ASZEITTAB(IUSER)=ASZEITTAB(IUSER) + ASZEITZW;
   GO TO LESEN;
 T34: /* EXCPS,MAIN STORAGE OCCUPANCY TIME */
   ID=S34.USERID;
   CALL US;
   MAINZW=MAIN;
   MAINZW=MAINZW/360000 + 0.0005;
   MAINTAB(IUSER)=MAINTAB(IUSER) + MAINZW;
   DO J= 1 TO (LEN34-2) BY 8;
   P1=ADDR(RX34(J));
   EXCPTAB(IUSER)=EXCPTAB(IUSER) + EXCP;
   END;
   GO TO LESEN;
 T40: /* EXCP-DYN */
   ID=S40.USERID;
   CALL US;
   DO J=1 TO (LEN40-2) BY 8;
   P1=ADDR(RX40(J));
   EXCP_DYN_TAB(IUSER)=EXCP_DYN_TAB(IUSER) + EXCP;
   END;
   GO TO LESEN;
 ENDE:
   DO I=1 TO 50;
   CPUSUM=CPUSUM+CPUTAB(I);
   ASZEITSUM=ASZEITSUM + ASZEITTAB(I);
   MAINSUM=MAINSUM + MAINTAB(I);
   TGETSUM=TGETSUM + TGETTAB(I);
   TPUTSUM=TPUTSUM + TPUTTAB(I);
   EXCPSUM=EXCPSUM + EXCPTAB(I);
   EXCP_DYN_SUM=EXCP_DYN_SUM + EXCP_DYN_TAB(I);
   END;
   DATUM1=ZWMIN;
   IF MOD(JAHR1,4)=0 THEN MON(2)=29;
   ELSE MON(2)=28;
   LTAG=TAG1;
   CALL KALTAG;
   ITAG1=KTAG;
   I1=IMONAT;
   DATUM2=ZWMAX;
   LTAG=TAG2;
   CALL KALTAG;
   ITAG2=KTAG;
   I2=IMONAT;
   PUT PAGE EDIT
   ('S M F   A U S W E R T U N G   D E S   T S O',(43)'=')
   (SKIP(3),COL(30),A,SKIP,COL(30),A);
   PUT EDIT('FUER DEN ZEITRAUM  ',ITAG1,'.',I1,'.',JAHR1,
   '  BIS  ',ITAG2,'.',I2,'.',JAHR2)
   (SKIP(2),COL(30),(6)(A,P'99'));
   PUT EDIT((42)'=')(SKIP,COL(30),A);
   PUT SKIP(2) EDIT('MAIN STORAGE')(COL(42),A);
  PUT SKIP EDIT('USERID','CPUZEIT(STD) LOGONZEIT(STD) OCCUPANCY',
   'TGET','TPUT','EXCP''S','EXCP''S DYN')
  (COL(3),A,COL(14),A,COL(59),A,COL(73),A,COL(86),A,COL(99),A);
   PUT SKIP EDIT('TIME (STD)') (COL(42),A);
   PUT SKIP EDIT((99)'*')(COL(12),A);
 /* SORTIEREN NACH USERID */
  DO I=1 TO 49 WHILE(USERTAB(I) ^=(8)'X');
   DO J=I+1 TO 50 WHILE(USERTAB(J) ^=(8)'X');
   IF USERTAB(J) < USERTAB(I) THEN DO;
   ZWACC=VNR(I);
   ZWU=USERTAB(I);
   ZWCPU=CPUTAB(I);
   ZWMAIN=MAINTAB(I);
   ZWGET=TGETTAB(I);
   ZWPUT=TPUTTAB(I);
   ZWAS=ASZEITTAB(I);
   ZWEX=EXCPTAB(I);
   ZWEXD=EXCP_DYN_TAB(I);
   VNR(I)=VNR(J);
   USERTAB(I)=USERTAB(J);
   CPUTAB(I)=CPUTAB(J);
   MAINTAB(I)=MAINTAB(J);
   TGETTAB(I)=TGETTAB(J);
   TPUTTAB(I)=TPUTTAB(J);
   ASZEITTAB(I)=ASZEITTAB(J);
   EXCPTAB(I)=EXCPTAB(J);
   EXCP_DYN_TAB(I)=EXCP_DYN_TAB(J);
   VNR(J)=ZWACC;
   USERTAB(J)=ZWU;
   CPUTAB(J)=ZWCPU;
   MAINTAB(J)=ZWMAIN;
   TGETTAB(J)=ZWGET;
   TPUTTAB(J)=ZWPUT;
   ASZEITTAB(J)=ZWAS;
   EXCPTAB(J)=ZWEX;
   EXCP_DYN_TAB(J)=ZWEXD;
   END;
   END;
   END;
   DO J=1 TO 50 WHILE(USERTAB(J) ^= (8)'X');
   UID(J)=USERTAB(J);
   WRITE FILE(UA) FROM(UIDTAB(J));
   PUT SKIP EDIT
   (USERTAB(J),' *  ',CPUTAB(J),'  *  ',ASZEITTAB(J),'  *  ',
    MAINTAB(J),'  *',TGETTAB(J),'  *',TPUTTAB(J),'  *',
    EXCPTAB(J),'  *',EXCP_DYN_TAB(J),'  *')
    (COL(3),A,(3)(A,P'Z.ZZ9V,999'),(4)(A,P'ZZZ.ZZZ.ZZ9'),A);
   END;
   PUT SKIP EDIT((99)'*')(COL(12),A);
   PUT SKIP EDIT('GESAMT  ',' *  ',CPUSUM,'  *  ',ASZEITSUM,
   '  *  ',MAINSUM,'  *',TGETSUM,'  *',TPUTSUM,'  *',
   EXCPSUM,'  *',EXCP_DYN_SUM,'  *')
   (COL(3),A,(3)(A,P'Z.ZZ9V,999'),(4)(A,P'ZZZ.ZZZ.ZZ9'),A);
   PUT SKIP EDIT((99)'*')(COL(12),A);
 /* INITIALISIEREN DER RECHENFELDER */
   ZWMIN=99365;
   ZWMAX=0;
   USERTAB=(8)'X';
   TGETTAB,TPUTTAB,EXCPTAB,EXCP_DYN_TAB,MAINTAB,ASZEITTAB,
   CPUTAB=0;
   CPUSUM,ASZEITSUM,MAINSUM,TGETSUM,TPUTSUM,EXCPSUM,
   EXCP_DYN_SUM=0;
   IF ZA=0 THEN GO TO REA;
 UNT: CLOSE FILE(BEWERT);
      CLOSE FILE(UA);
 US: PROC;
   DO IK=1 TO 50;
   IF ID = USERTAB(IK) THEN DO;
   IUSER=IK;
   GO TO W1;
   END;
   IF USERTAB(IK) = (8)'X' THEN DO;
   IUSER=IK;
   USERTAB(IK)=ID;
   GO TO W1;
   END;
   END;
   PUT SKIP LIST('KEIN USERID'); GO TO REA;
 W1: END US;
 KALTAG: PROC;
   IZAE2=0;
   DO IL=1 TO 12;
   IZAE1=IZAE2;
   IZAE2=IZAE2 + MON(IL);
   IMONAT=IL;
   IF LTAG <= IZAE2 THEN GO TO D;
   END;
 D: KTAG=LTAG - IZAE1;
   END KALTAG;
 ZEIL: PROC;
   PUT SKIP EDIT('*')(COL(12),A);
   DO IZ=1 TO 7;
   PUT EDIT('*')(COL(12+14*IZ),A);
   END;
   END ZEIL;
   END SMF;
