ALX01    CSECT
         MBASIS R3
         MSPSN COUNT=10
         USING IHADCB,R2
         USING INFMJFCB,R8
         MMACODE
         RDJFCB (SALBEST)              LESEN JOB-FILE-CONTROL-BLOCK
         RDJFCB (DALBEST)
         OPEN  (SALBEST,(INPUT))   EROEFFNEN EING.-BESTAND (SEQ)
         OPEN  (DALBEST,(OUTPUT))  EROEFFNEN AUSG.-BESTAND (ISFMS)
         OPEN  (AUSG,(OUTPUT))     EROEFFNEN FEHLERLISTE
         LA    R2,DALBEST          LADE DCB-ADRESSE
         LA    R8,ZLIST2           LADE JFCB-ADRESSE
         MVI   IOPLA,C' '          I/O FEHLERSCHALTER LOESCHEN PLATTE
         MVI   IOBAND,C' '         I/O FEHLERSCHALTER LOESCHEN BAND
         EJECT
***********************************************************************
*        LESEN SALBEST UND NACH DALBEST AUSGEBEN.                     *
***********************************************************************
         SPACE 1
A1       GET   SALBEST             LESEN LOCATE-MODUS SEQ
         ST    R1,RETT1            INHALT R1 RETTEN
         LR    R9,R1               REG UMLADEN
         CLI   IOBAND,C' '         ABFRAGE PERMANENTER LESEFEHLER
         BNE   B1
         PUT   DALBEST,(R9)        SCHREIBEN MOVE-MODUS ISFMS
         AP    ZAEHL,=P'1'
         CLI   IOPLA,C' '          ABFRAGE PERMANENTER SCHREIBFEHLER
         BNE   C1
         LR    R7,R9                REG UMLADEN
         B     A1
***********************************************************************
*        I/O ERROR DALBEST                                            *
***********************************************************************
         SPACE 1
C1       BAL   R14,WTO
        MVI   ZRC,X'20'           RC = 20
         MVI   DBER,C' '           DRUCKZONE LOESCHEN
         MVC   DBER+1(132),DBER
         TM    DCBEXCD2,DCBEXDUP   TEST DOPPELTER SATZ
         BO    D1
         TM    DCBEXCD2,DCBEXSEQ   TEST FOLGEFEHLER
         BO    E1
         B     F10                 E/O ERROR
***********************************************************************
*        DOPPELTE SAETZE IM BESTAND (DALBEST).                        *
***********************************************************************
         SPACE 1
D1       MVI   DBER,ZVV1           VORSCH.KANAL1
         BAL   R14,PUT
         MVI   DBER,ZTN3           ZEILENVORSCHUB
         MVC   DBER+1(54),WARN1    WARNUNG
         BAL   R14,PUT
         B     MOVE
***********************************************************************
*        SCHLUESSEL NICHT AUFSTEIGEND (DALBEST).                      *
***********************************************************************
         SPACE 1
E1       MVI   DBER,ZVV1
         BAL   R14,PUT
         MVI   DBER,ZTN3
         MVC   DBER+1(52),WARN2
         BAL   R14,PUT
         B     MOVE
***********************************************************************
*       I/O ERROR SALBEST.                                            *
***********************************************************************
         SPACE 1
B1       BAL   R14,WTO
         MVI   DBER,C' '           DRUCKZONE LOESCHEN
         MVC   DBER+1(132),DBER
         MVI   DBER,ZVV1           VORSCH. KANAL 1
         BAL   R14,PUT
         MVI   DBER,ZTN2           2 ZEILENVORSCHUEBE NACH DRUCK
         MVC   DBER+1(55),WARN     WARNUNG
         BAL   R14,PUT
         B     ENDE1
***********************************************************************
*       I/O ERROR DALBEST(SIEHE WTO MESSAGE).                         *
***********************************************************************
         SPACE 1
F10      MVI   DBER,ZVV1
         BAL   R14,PUT
         MVI   DBER,ZTN3
         MVC   DBER+1(57),WARN3
         BAL   R14,PUT
         B     ENDE1
***********************************************************************
*        AUSDRUCK DER FEHLERHAFTEN SAETZE.                            *
***********************************************************************
         SPACE 1
MOVE     ZAP   ZREFE,ZAEHL
         BAL   R14,U40              AUSDRUCK EN FALSCHER SAETZE
         ZAP   ZREFE,ZAEHL
         AP    ZREFE,=P'1'
         L     R9,RETT1
         LR    R7,R9
         BAL   R14,U40              AUSDRUCK EN FALSCHER SAETZE
         LA    R8,ZLIST2           ADRESSE JFCB SALBEST LADEN
         B     ENDE1
***********************************************************************
*        P R O G R A M M E N D E                                      *
***********************************************************************
         SPACE 1
ENDE     WTO   '**KGALX01**ERFOLGREICHES INDEX LADEN*JOBENDE**'
         MVI   DBER,ZVV1
         BAL   R14,PUT
         MVI   DBER,ZTN3
         MVC   DBER+1(47),WARN4
         BAL   R14,PUT
         B     ENDE2
ENDE1    WTO   '**KGALX01**UNERFOLGREICHES INDEX LADEN*JOBENDE**'
ENDE2    MVI   DBER+1,C'*'
         MVI   DBER,ZTN1
         MVC   DBER+2(93),DBER+1
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVI   DBER+94,C'*'
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVI   DBER+94,C'*'
         MVC   DBER+86(4),=C'ZEIT'
         MVC   DBER+72(10),=C'ERST.DATUM'
         MVC   DBER+62(6),=C'SAETZE'
         MVC   DBER+47(4),=C'NACH'
         MVC   DBER+21(11),=C'LABEL   VON'
         MVC   DBER+4(6),=C'DSNAME'
         BAL   R14,PUT
         MVI   DBER+94,C'*'
         MVI   DBER+1,C'*'
         BAL   R14,PUT
         MVC   DBER+56(12),=X'402020204B2020204B202120'
         ED    DBER+56(12),ZAEHL
         MVC   DBER+28(13),JFCBVOLS
         MVC   DBER+3(24),JFCBDSNM
         LA    R10,0
         LH    R10,JFCBFLSQ
         CVD   R10,LABELNR
         MVC   DBER+21(4),=X'40202120'
         ED    DBER+21(4),LABELNR+6
         LA    R8,ZLIST1           LADE ADRESSE JFCB DALBEST
         MVC   DBER+46(6),JFCBVOLS
         MVC   DBER+74(7),=X'4020204B202020'
         TIME  DEC
         ST    R1,LABELNR+4
         ED    DBER+74(7),LABELNR+5
         ST    R0,LABELNR+4
         MVO   LABELNR,LABELNR(7)
         OI    LABELNR+7,X'0F'
         MVC   DBER+82(10),=X'402020204B20204B2020'
         ED    DBER+82(10),LABELNR+4
         MVI   DBER+1,C'*'
         MVI   DBER+94,C'*'
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVI   DBER+94,C'*'
         BAL   R14,PUT
         MVI   DBER+1,C'*'
         MVC   DBER+2(93),DBER+1
         MVI   DBER,ZTN3
         BAL   R14,PUT
         CLOSE (SALBEST)
         CLOSE (DALBEST)
         CLOSE (AUSG)
         MRETURN RC=ZRC            PROGRAMMENDE
***********************************************************************
*        FEHLERNACHRICHT AUSGEBEN                                     *
***********************************************************************
         SPACE 1
WTO      MUPEIN
         WTO   '**KGALX01**ACHTUNG FEHLER*NAEHERES SIEHE SYSOUT**'
         MUPAUS
***********************************************************************
*        AUSGABE FEHLERLISTE                                          *
*                                                                     *
***********************************************************************
         SPACE 1
U25      MUPEIN
PUT      EQU   U25
         PUT   AUSG,ZDBER               SATZ AUSGEBEN
         MVI   ZVSZ,ZTN1                ZTN1 EINSTELLEN
         MVI   ZDBER,C' '               DRUCKBEREICH
         MVC   ZDBER+1(L'ZDBER-1),ZDBER LOESCHEN
         MUPAUS
         TITLE 'DATEN- ODER CONTROL-BLOCKS HEX AUFBEREITEN UND AUSDRUCK*
               EN'
***********************************************************************
*                                                                     *
*        DATENSATZ HEXADIZIMAL AUFBEREITEN UND AUSGEBEN               *
*                                                                     *
*              ES WERDEN FOLGENDE REGISTERWERTE ERWARTET              *
*                                                                     *
*                   R8 = DATENDRESSE                                  *
*                   R10= DATENLAENGE                                  *
*                                                                     *
***********************************************************************
         SPACE 1
U30      MUPEIN
U30A00   LA    R5,ZDB+1
         LA    R6,4                     COUNT = 4
U30A05   C     R5,=A(ZDB+120)           TEST DRUCKZEILE VOLL
         BH    U30A10                   JA
         UNPK  0(3,R5),0(2,R8)          ENTPACKEN UND
         TR    0(2,R5),ZTRTAB-240       UEBERSETZEN
         MVI   2(R5),C' '
         BCT   R6,*+12                  BR BEI COUNT DT 0
         LA    R6,4                     COUNT = 4
         LA    R5,2(0,R5)               ADREESE DRUCKBER. ERHOEHEN
         LA    R5,2(0,R5)               ADRESSEN
         LA    R8,1(0,R8)               ERHOEHEN
         BCT   R10,U30A05               LOOP = DATENLAENGE
         C     R5,=A(ZDB+1)             TEST LEERZEILEN
         BE    U30A15                   JA
         BAL   R14,U25                  LETZTE ZEILE AUSGEBEN
         B     U30A15
U30A10   BAL   R14,U25                  ZEILE AUSGEBEN
         LA    R6,4                     COUNT = 4
         B     U30A00
U30A15   BAL   R14,U25                  LEERZEILE
         MUPAUS
         TITLE 'SNAP DER FALSCHEN SAETZE BEI FOLGEFEHLER'
***********************************************************************
*                                                                     *
*        AUFBEREITEN DER SAETZE BEI FOLGEFEHLER                       *
*                                                                     *
*              ES WERDEN FOLGENDE INFORMATION AUSGEGEBEN              *
*                                                                     *
*                   DSNAME                                            *
*                   RKP              * NUR BEI                        *
*                   KEYLEN           * ISAM-DATEIEN                   *
*                   LRECL                                             *
*                   SATZ-NR                                           *
*                   DATENSAETZ HEXADEZIMAL AUFBEREITET                *
*                                                                     *
***********************************************************************
         SPACE 1
U40      MUPEIN
         LA    R8,ZLIST1                LADE JFCB EINGABE DATEI
         MVC   ZDB+1(44),JFCBDSNM
         MVC   ZDB+46(11),=CL11'FOLGEFEHLER'
         MVC   ZDB+58(7),=CL7'SATZ-NR'
         MVC   ZDB+65(12),=X'402020204B2020204B202120'
         ED    ZDB+65(12),ZREFE+3
         TM    DCBDSORG,DCBDSGIS        TEST DCB = IS
         BNO   U40A05                   NEIN
         LH    R14,DCBRKP               RKP
         CVD   R14,ZREFE                CONVERTIEREN
         MVC   ZDB+80(5),=CL5'RKP ='    UND
         MVC   ZDB+85(4),=XL4'40202120' AUFBEREITEN
         ED    ZDB+85(4),ZREFE+6
         MVC   ZDB+92(8),=CL8'KEYLEN =' KEYLEN
         MVC   ZDB+101(4),=XL4'40202120'
         IC    R14,DCBKEYLE
         CVD   R14,ZREFE
         ED    ZDB+101(4),ZREFE+6
U40A05   LH    R14,DCBLRECL
         CVD   R14,ZREFE
         MVC   ZDB+109(7),=CL7'LRECL =' LRECL
         MVC   ZDB+116(4),=XL4'40202120'
         ED    ZDB+116(4),ZREFE+6
         MVI   ZVSZ,ZTN2
         BAL   R14,U25                  ZEILE AUSGABEN
         LH    R10,DCBLRECL
         LR    R8,R7
         BAL   R14,U30                  AUSGEBEN 1. SATZ
         MVI   ZRC+3,20                 RC = 20 EINSTELLEN
U40A95   MUPAUS
         TITLE 'Q S A M - S Y N A D - R O U T I N E'
BANDFEHL MSYNAD QSAM,IOBAND,,SALBEST
         TITLE 'Q I S A M - S Y N A D - R O U T I N E'
ISAMFEHL MSYNAD  QISAM,IOPLA,,DALBEST
         TITLE 'D E F I N I T I O N E N'
DBER     DS    0CL133
ZDBER    DS    0CL133
ZVSZ     DC    AL1(ZTN1)
ZDB      DC    CL132' '
ZREFE    DC    D'12'
ZAEHL    DC    PL5'0'
IOBAND   DC    X'00'
IOPLA    DC    X'00'
RETT1    DS    F
ZRC      DC    F'0'                     RETURN-CODE
WARN     DC    CL55'A C H T U N G...INPUT/OUTPUT-ERROR-BAND...JOBENDE'
WARN1    DC    CL54'A C H T U N G...DOPPELTE SAETZE IM BESTAND...JOBENDC
               E..'
WARN2    DC    CL52'A C H T U N G...SORTIERFEHLER IM BESTAND...JOBENDE.C
               .'
WARN3    DC    CL57'A C H T U N G...INPUT/OUTPUT-ERROR-PLATTE...JOBENDEC
               '
WARN4    DC    CL47'KGALX01 ... ERFOLGREICHES ENDE ... PROGRAMMENDE'
ZTRTAB   DC    C'0123456789ABCDEF'
         DS    0D
LABELNR  DC    XL8'C'
EXLIST1  DC    AL1(135),AL3(ZLIST1)
ZLIST1   DC    CL176' '                 JFCB DALBEST
EXLIST2  DC    AL1(135),AL3(ZLIST2)
ZLIST2   DC    CL176' '                 JFCB SALBEST
         LTORG
DALBEST  DCB   DDNAME=DALBEST,DSORG=IS,MACRF=(PM),SYNAD=ISAMFEHL,      X
               EXLST=EXLIST1
SALBEST  DCB   DDNAME=SALBEST,DSORG=PS,MACRF=(GL),EODAD=ENDE,          C
               SYNAD=BANDFEHL,EXLST=EXLIST2
AUSG     DCB   DDNAME=AUSG,DSORG=PS,MACRF=(PM),LRECL=121,              *
               BLKSIZE=121,RECFM=FM,BUFNO=2
         DCBD  DSORG=(IS,PS),DEVD=(DA,TA)
JFCB     DSECT
         IEFJFCBN LIST=YES
         END
