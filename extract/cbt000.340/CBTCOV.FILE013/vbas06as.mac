*          DATA SET CBT1229    AT LEVEL 001 AS OF 02/15/81
PSWDCHG  TITLE 'TSO PASSWORD CHANGE COMMAND'
         PRINT ON
         SPACE
*  THIS COMMAND IS DESIGNED TO ALLOW THE INDIVIDUAL TSO USER TO
*  CHANGE THEIR OWN LOGON PASSWORDS AT WILL.  IT WILL CHANGE ONLY
*  THE PASSWORD OF THE USERID/PASSWORD THEY LOGGED ON WITH.
*  THIS COMMAND IS REENTRANT AND CAN BE PLACED IN LPALIB IF DESIRED.
*  IT WILL DYNAMICALLY ALLOCATE AND DEALLOCATE THE UADS DATASET.
*  PASSWORD PROTECTED UADS DATASETS ARE SUPPORTED, OR IF UADS IS
*  NOT PASSWORD PROTECTED,  NO CHANGES ARE NECESSARY TO THIS CODE.
*
*
*  ASSUMPTIONS
*
*               ONE OR MORE PASSWORDS PER TSO USERID
*               ( THIS COMMAND WILL NOT ADD A PASSWORD IF NONE THERE )
*               ( NOR WILL IT DELETE A PASSWORD ENTRY )
*
*  CHANGES FOR CUSTOMIZATION
*
*               NAME OF UADS DATASET      ( UADSDSNM )
*                     ( DEFAULT - SYS1.UADS )
*               PASSWORD OF UADS DATASET  ( UADSPSWR )
*                     ( DEFAULT - XYZBASFU )
*
*  RESTRICTIONS
*
*               THIS COMMAND WILL PRESENTLY CHANGE ONLY THE FIRST
*               PASSWORD IF MULTIPLE PASSWORDS ARE USED.  THE CODE
*               TO VERIFY AND CHANGE THE PASSWORD USED AT LOGON TIME
*               IS IN PLACE BUT COMMENTED OUT.  TO MAKE THIS CODE
*               WORK, AN SVC TO PUT THE COMMAND IN SUPERVISORY STATE
*               IS NEEDED AT LABEL SUPERSVC AND A RETURN TO PROBLEM
*               STATE SVC AT LABEL PROBLSVC.
*
*  USE
*
*               ENTER:   CHNGPSWD NNNNNNNN
*               WHERE NNNNNNNN IS THE NEW PASSWORD WHICH MUST START
*               WITH AN ALPHA CHARACTER AND MAY BE FOLLOWED BY UP TO
*               7 ALPHAMERIC CHARACTERS
*
*  INSTALLATION
*
*               THIS COMMAND SHOULD BE ASSEMBLED AND LINKEDITED INTO
*               A LIBRARY THAT IS ON THE LINKLIST OR INTO LPALIB.  IT
*               DOES NOT REQUIRE AUTHORIZATION AS IT IS, HOWEVER, IF
*               THE SUPERVISORY SVC IS INCLUDED, AUTHORIZATION WILL
*               BE NEEDED.  THE NAME OF THE MODULE/COMMAND IS PSWRDCHG.
         EJECT
PSWRDCHG CSECT
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3        - BASE REG
R4       EQU   4        - ==> CPPL
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13       - ==> GOTTEN AREA
R14      EQU   14
R15      EQU   15
         SPACE 2
         XSAVE R3,SVA,CHNGPSWD
         LR        R0,R3
         LA        R15,SVCROUT
         SVC       250
         XRETURN 0
SVCROUT  EQU       *
         LR        R3,R0
         SPACE
         LR    R4,R1         PRESERVE ADDRESS OF CPPL
         USING CPPL,R4
         SPACE
         GETMAIN R,LV=AREALEN GET AREA FOR REGISTER SAVE AND WORK
         SPACE
         LR    R10,R1        SAVE POINTER TO GOTTEN AREA
         MVI   0(R10),X'00'  CLEAR GOTTEN AREA TO 0
         MVC   1(AREALEN-1,R10),0(R10)
         ST    R13,4(R10)    CHAIN SAVE AREA ADDRESSES
         ST    R10,8(R13)
         LR    R13,R10
         USING WORKAREA,R13  ESTABLISH ADDRESSABILITY TO SAVE/WORK AREA
         EJECT
*                            BUILD SVC99 REQUEST BLOCKS IN GOTTEN AREA
         SPACE
         TPUT  PSWDMSG,30
         TGET  OLDPSWD,8
         OC    OLDPSWD(8),=CL8' '
         L     R14,16        LOAD CVT ADDRESS
         L     R14,0(R14)    LOAD CVTTCBP
         L     R14,12(R14)   LOAD CURRENT ASCB ADDRESS
         L     R14,60(R14)   LOAD TSB ADDRESS
         CLC   OLDPSWD,88(R14) SEE IF LOGON PASSWORD
         BE    OPSOK
         TPUT  PSWDMSG2,70
         B     RETURN
OPSOK    LA    R5,RB1        BUILD S99 RB PTR
         ST    R5,APRB1
         OI    APRB1,S99RBPND
         SPACE
         USING S99RB,R5
         MVI   S99RBLN,X'14' BUILD S99 RB
         MVI   S99VERB,S99VRBAL
         MVI   S99FLG11,S99NOCNV
         LA    R6,RB1P1
         ST    R6,S99TXTPP
         DROP  R5
         SPACE 2
         LA    R5,RB1TU1
         ST    R5,RB1P1
         USING S99TUNIT,R5   BUILD RB1TU1 - DDNAME
         MVI   S99TUKEY+1,DALDDNAM
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,L'UADSDDNM
         MVC   S99TUPAR(8),UADSDDNM
         SPACE 2
         LA    R5,RB1TU2     BUILD RB1TU2 - DSNAME
         ST    R5,RB1P2
         MVI   S99TUKEY+1,DALDSNAM
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,L'UADSDSNM
         MVC   S99TUPAR(L'UADSDSNM),UADSDSNM
         SPACE 2
         LA    R5,RB1TU3     BUILD RB1TU3 - MEMBER NAME
         ST    R5,RB1P3
         MVI   S99TUKEY+1,DALMEMBR
         MVI   S99TUNUM+1,X'01'
         L     R6,CPPLPSCB   GET ADDRESS OF PSCB - GET TSO USERID
         SR    R7,R7
         IC    R7,7(R6)      LOAD LENGTH OF USERID
         LA    R8,1(R7)      ADD 1 TO LENGTH FOR SUFFIX - 0
         STC   R8,S99TULNG+1 STORE LENGTH OF MEMBER NAME
         BCTR  R7,0          MAKE LENGTH FOR EX MOVE INSTRUCTION
         EX    R7,MOVEUID    EXECUTE MOVE USERID
         LA    R8,S99TUPAR(R7) POINT TO LAST CHAR IN USERID
         MVI   1(R8),C'0'    MOVE SUFFIX - 0 - AFTER USERID
         EJECT
         LA    R5,RB1TU4     BUILD RB1TU4 - STATUS
         ST    R5,RB1P4
         MVI   S99TUKEY+1,DALSTATS
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'01'
         MVI   S99TUPAR,DA08SHR
         SPACE 2
         LA    R5,RB1TU5     BUILD RB1TU5 - DISPOSITION
         ST    R5,RB1P5
         MVI   S99TUKEY+1,DALNDISP
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,X'01'
         MVI   S99TUPAR,DA08KEEP
         SPACE 2
         LA    R5,RB1TU50    BUILD RB1TU50 - PASSWORD OF UADS DATASET
         ST    R5,RB1P50
         OI    RB1P50,S99RBPND
         MVI   S99TUKEY+1,DALPASSW
         MVI   S99TUNUM+1,X'01'
         MVI   S99TULNG+1,L'UADSPSWR
         MVC   S99TUPAR(L'UADSPSWR),UADSPSWR
         DROP  R5
         EJECT
         LA    R1,APRB1      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK SVC99 RETURN CODE
         BNZ   ALLOCERR
         SPACE
         OPEN  (PSWDCHG#,(UPDATE))
         SPACE
         GET   PSWDCHG#      GET UADS DS MEMBER
         SPACE
         LR    R5,R1         SAVE POINTER TO UADS RECORD
         L     R6,CPPLCBUF   POINT TO COMMAND BUFFER
         LH    R7,0(R6)      GET LENGTH OF COMMAND
         LH    R8,2(R6)      GET DISPLACEMENT OF OPERAND
         LA    R9,4(R6,R8)   POINT R9 TO OPERAND (NEW PASSWORD)
         SR    R7,R8         GET LENGTH OF OPERAND
         SH    R7,=H'4'      SUBTRACT LENGTH OF LL FIELD IN BUFFER
         CLI   0(R9),X'7D'   CHECK FOR STARTING QUOTE
         BNE   CHKALPHA
         BCTR  R7,0          DECREMENT LEN OF PASSWORD FOR START QUOTE
         LA    R10,0(R7,R9)  POINT TO END OF PASSWORD
         LA    R9,1(R9)      INCREMENT OVER START QUOTE
         CLI   0(R10),X'7D'  CHECK FOR END QUOTE
         BNE   CHKALPHA
         BCTR  R7,0          DECREMENT LEN OF PASSWORD FOR END QUOTE
         EJECT
CHKALPHA EQU   *
         CH    R7,=H'8'      CHECK FOR MAX LENGTH
         BNH   REDUCELN
         SPACE
         LA    R7,8          DEFAULT TO MAX LENGTH OF 8
         SPACE
REDUCELN BCTR  R7,0          DECREMENT FOR MOVE INSTRUCTION OPERAND
         LTR   R7,R7         CHECK FOR OPERAND PRESENT
         BM    NOPSWRDI
         L     R10,24(R5)    GET OFFSET TO PSWRD OFFSET BLOCK
         LA    R12,0(R5,R10) POINT TO OFFSET BLOCK
         SPACE
GETOFSET L     R10,8(R12)    GET OFFSET TO PASSWORD DATA BLOCK
         LTR   R10,R10       CHECK FOR EXISTING PASSWORD
         BZ    NOPSWRD
         SPACE
         L     R11,0(R12)    LOAD MULTIPLE PASSWORD CHAIN FIELD
         LTR   R11,R11       SEE IF LAST PASSWORD IN CHAIN
         BM    SETADDR       ... YES - UPDATE THIS ONE
         EJECT
*                            THIS CODE IS PROVIDED FOR USE IF MULTIPLE
*                            PASSWORDS ARE SUPPORTED AND THE
*                            INSTALLATION WISHES TO CHANGE THE
*                            PASSWORD THAT THE TSO USER LOGGED ON WITH.
         SPACE 2
SUPERSVC EQU   *             PLACE SUPERVISORY SVC HERE
         SPACE
         L     R14,16        LOAD CVT ADDRESS
         L     R14,0(R14)    LOAD CVTTCBP
         L     R14,12(R14)   LOAD CURRENT ASCB ADDRESS
         L     R14,60(R14)   LOAD TSB ADDRESS
         LA    R10,4(R5,R10) SET ADDRESS OF PASSWORD
         CLC   0(8,R10),88(R14) SEE IF LOGON PASSWORD
         BE    CLEAROLD      ... YES - CHANGE THIS ONE
         LA    R12,0(R5,R11) RESET POINTER TO NEXT PASSWORD
         B     GETOFSET
         EJECT
*                            CHANGE OLD PASSWORD TO NEW AND REWRITE
*                            UADS MEMBER
         SPACE
SETADDR  LA    R10,4(R5,R10) SET ADDRESS OF PASSWORD TO CHANGE
         SPACE
CLEAROLD MVC   0(8,R10),=CL8' ' CLEAR OLD PASSWORD TO BLANKS
         SPACE
PROBLSVC EQU   *             PLACE PROBLEM STATE SVC HERE
         SPACE
         EX    R7,MOVEPSWD   MOVE IN NEW PASSWORD
         EX    R7,ORPSWD     MAKE SURE IT IS IN UPPER CASE
         SPACE
         PUTX  PSWDCHG#      REWRITE UPDATED UADS MEMBER
         SPACE
         TPUT  CHANGED,L'CHANGED
         EJECT
EXIT     CLOSE PSWDCHG#
         SPACE
         LA    R5,RB1        SET UP FOR DEALLOCATION OF UADS DS
         USING S99RB,R5
         MVI   S99VERB,S99VRBUN SET TO DEALLOCATE
         DROP  R5
         SPACE
         OI    RB1P3,S99RBPND MAKE MEMBER NAME (POINTER 3) LAST IN LIST
         SPACE
         LA    R1,APRB1      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK SVC99 RETURN CODE
         BNZ   DALLOCER
         SPACE
RETURN   LR    R1,R13        GET POINTER TO GOTTEN AREA FOR FREEMAIN
         LA    R0,AREALEN    AND LENGTH TO FREE
         FREEMAIN R,LV=(0),A=(1) FREE GOTTEN AREA
         SPACE
         SVC 3
         EJECT
*                            WRITE ERROR MESSAGES BACK TO TERMINAL
         SPACE
ALLOCERR TPUT  ALERMSG,L'ALERMSG
         SPACE
         B     RETURN
         SPACE 2
DALLOCER TPUT  DALERMSG,L'DALERMSG
         SPACE
         B     RETURN
         SPACE 2
NOPSWRD  TPUT  NPSWRDM,L'NPSWRDM
         SPACE
         B     EXIT
         EJECT
NOPSWRDI TPUT  NPSWRDMI,L'NPSWRDMI
         SPACE
         B     EXIT
         SPACE 2
BADPSWRD TPUT  BADPSMSG,L'BADPSMSG
         SPACE
         B     EXIT
         EJECT
*                         EXECUTED INSTRUCTIONS
         SPACE
         USING S99TUNIT,R5
MOVEUID  MVC   S99TUPAR(1),0(R6)  MOVE TSO USERID TO SVC99 PARM FIELD
         DROP  R5
         SPACE
CHKALNUM TRT   1(1,R9),TRTALN     CHECK NEW PASSWORD FOR ALPHANUMERIC
         SPACE
MOVEPSWD MVC   0(1,R10),0(R9)     MOVE NEW PASSWORD TO UADS ENTRY
         SPACE
ORPSWD   OC    0(1,R10),=CL8' '   MAKE SURE PASSWORD IS UPPER CASE
         SPACE 2
*                         TERMINAL MESSAGES
         SPACE
CHANGED  DC    C'PASSWORD SUCCESSFULLY CHANGED'
ALERMSG  DC    C'UNABLE TO ALLOCATE PASSWORD DATA SET'
DALERMSG DC    C'UNABLE TO DEALLOCATE PASSWORD DATA SET'
NPSWRDM  DC    C'NO CURRENT PASSWORD - NOT ADDED'
NPSWRDMI DC    C'NO PASSWORD SPECIFIED - NOT CHANGED'
BADPSMSG DC    C'PASSWORD CONTAINS INVALID CHARACTERS - NOT CHANGED'
         SPACE 2
*                         UADS DATA SET CONSTANTS
         SPACE
UADSDDNM DC    C'PSWDCHG#'  UADS DDNAME
UADSDSNM DC    C'SYS1.UADS' UADS DSNAME
UADSPSWR DC    C'XYZBASFU'  UADS PASSWORD
PSWDMSG  DC    CL30'ENTER CURRENT PASSWORD'
PSWDMSG2 DC    CL70'CHNGPSWD REJECTED - CURRENT PASSWORD IS INVALID'
OLDPSWD  DC    CL8' '
         SPACE 2
         LTORG
         EJECT
*                         TRANSLATE AND TEST TABLES
         SPACE
*                    0 1 2 3 4 5 6 7 8 9 A B C D E F
         SPACE
TRTAL    DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 0    ALPHA ONLY
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 1
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 2
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 3
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 4
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 5
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 6
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 7
         DC    XL16'FF000000000000000000FFFFFFFFFFFF' 8
         DC    XL16'FF000000000000000000FFFFFFFFFFFF' 9
         DC    XL16'FFFF0000000000000000FFFFFFFFFFFF' A
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' B
         DC    XL16'FF000000000000000000FFFFFFFFFFFF' C
         DC    XL16'FF000000000000000000FFFFFFFFFFFF' D
         DC    XL16'FFFF0000000000000000FFFFFFFFFFFF' E
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' F
         SPACE
*                    0 1 2 3 4 5 6 7 8 9 A B C D E F
         SPACE
TRTALN   DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 0    ALPHANUMERIC
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 1
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 2
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 3
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 4
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 5
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 6
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 7
         DC    XL16'FF000000000000000000FFFFFFFFFFFF' 8
         DC    XL16'FF000000000000000000FFFFFFFFFFFF' 9
         DC    XL16'FFFF0000000000000000FFFFFFFFFFFF' A
         DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' B
         DC    XL16'FF000000000000000000FFFFFFFFFFFF' C
         DC    XL16'FF000000000000000000FFFFFFFFFFFF' D
         DC    XL16'FFFF0000000000000000FFFFFFFFFFFF' E
         DC    XL16'00000000000000000000FFFFFFFFFFFF' F
         SPACE
*                    0 1 2 3 4 5 6 7 8 9 A B C D E F
         EJECT
PSWDCHG# DCB   DDNAME=PSWDCHG#,EODAD=EXIT,MACRF=(GL),BUFNO=1,DSORG=PS
         EJECT
WORKAREA DSECT            DSECT FOR GOTTEN AREA
         SPACE
REGAREA  DS    18F        REGISTER SAVE AREA
         SPACE
APRB1    DS    F          POINTER TO SVC99 REQUEST BLOCKS
         SPACE
RB1      DS    5F         SVC 99 REQUEST BLOCKS
         SPACE
RB1P1    DS    F          POINTER TO SVC99 TEXT UNIT 1
RB1P2    DS    F          POINTER TO SVC99 TEXT UNIT 2
RB1P3    DS    F          POINTER TO SVC99 TEXT UNIT 3
RB1P4    DS    F          POINTER TO SVC99 TEXT UNIT 4
RB1P5    DS    F          POINTER TO SVC99 TEXT UNIT 5
RB1P50   DS    F          POINTER TO SVC99 TEXT UNIT 50
         SPACE
RB1TU1   DS    XL14              SVC99 TEXT UNIT  1 - DDNAME
RB1TU2   DS    XL(6+L'UADSDSNM)  SVC99 TEXT UNIT  2 - DSNAME
RB1TU3   DS    XL14              SVC99 TEXT UNIT  3 - MEMBER NAME
RB1TU4   DS    XL7               SVC99 TEXT UNIT  4 - DS STATUS
RB1TU5   DS    XL7               SVC99 TEXT UNIT  5 - DS DISPOSITION
RB1TU50  DS    XL14              SVC99 TEXT UNIT 50 - DS PASSWORD
         SPACE
AREALEN  EQU   *-WORKAREA
         EJECT
         IKJCPPL
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
         IKJDAP08
         SPACE
         END
