PRPCHSRC START
         REG
         XSAVE R12,,PRPCHSRC,WORKL
         USING WORK,R13
         XC    UCMID(4),UCMID
         LR    R3,R1
         L     R15,16
         TM    116(R15),X'01'    MVS?
         BNO   NOMVS
         L     R15,0(R15)
         L     R15,4(R15)
         L     R15,180(R15)    A(JSCB)
         L     R15,256(R15)   A(CSCB)
         B     MVS
NOMVS    EQU   *
         EXTRACT ANSWER,FIELDS=COMM
         L     R15,ANSWER
         LA    R14,32
         SR    R15,R14
MVS      EQU   *
         CLI   4(R15),X'04'    WAS START-COMMAND?
         BNE   NOSTART
         MVC   UCMID+3(1),29(R15)
NOSTART  EQU   *
         LA    R15,JFCB1
         ST    R15,DCBEX1
         MVI   DCBEX1,X'87'
         LA    R15,JFCB
         ST    R15,DCBEX
         MVI   DCBEX,X'87'
         L     R1,0(R3)
         LH    R15,0(R1)
         LTR   R15,R15
         BZ    NOPARM
         LR    R3,R1
         LA    R4,2(R3)
         MVC   END,0(R3)
         CALL  SCAN,((R4),ANF,END,DPL,NUMBER),VL
         LTR   R15,R15
         BNZ   TSTADD
         OI    SWITCH,X'20'
TSTADD   EQU   *
         CALL  SCAN,((R4),ANF,END,DPL,NOADD),VL
         LTR   R15,R15
         BNZ   TSTNDC
         OI    SWITCH,X'10'
TSTNDC   EQU   *
         CALL  SCAN,((R4),ANF,END,DPL,NOEXPND),VL
         LTR   R15,R15
         BNZ   NOPARM
         MVI   WEICHE2+1,X'F0'
NOPARM   EQU   *
         RDJFCB SYSUT1
         MVC   JFCB1,JFCB
         OI    JFCB+86,X'01'
         LA    R4,60
         SR    R5,R5
         SR    R7,R7
         OPEN  (SYSIN,,PDS,,SYSUT2,(OUTPUT),SYSUT3,(OUTPUT),           *
               SYSPRINT,(OUTPUT))
         PUT   SYSPRINT,ASA
         TM    SYSIN+48,X'10'
         BO    GET
         MVC   SYSIN+49(3),=AL3(WTOR)
         OI    SWITCH,X'80'
GET      EQU   *
         GET   SYSIN,INPUT
         XC    ADDRLIST,ADDRLIST
         MVC   INP(4),=X'00540000'
         CALL  KEYSCAN,(INP)
         LTR   R15,R15
         BNZ   ERROR
         LR    R2,R1
         ST    R2,ADDRLIST
         LA    R2,2(R2)
NEXTMBR  EQU   *
         BAL   R14,ANALYZE
         NI    SWITCH,X'F7'
         OPEN  (SYSUT1),TYPE=J
WEICHE   NOP   CLEAR
         TM    SYSUT1+36,X'80'
         BNO   INVINP
         TM    SYSUT1+36,X'40'
         BO    INVINP
         CLC   SYSUT1+82(2),=H'80'
         BNE   INVINP
         MVI   WEICHE+1,X'F0'
CLEAR    EQU   *
         SR    R3,R3
NEXTREC  EQU   *
         GET   SYSUT1,ZWI
         CLC   ZWI(80),=80XL1'00'
         BE    NEXTREC
         MVC   INPUT,ZWI
WEICHE2  NOP   ALLOK
         CLI   ZWI,X'00'
         BNE   ALLOK
         OI    SWITCH,X'08'
         LA    R6,ZWI   CONTINUE-ADDR IN INPUT-REC.
         SR    R9,R9    ACCUMULATED LENGTH
NOREAD   EQU   *
         MVC   SPBITS,2(R6)
         XC    2(2,R6),2(R6)
         CLI   SPBITS,X'00'
         BNE   SPANNED
         LH    R15,0(R6)
         BCTR  R15,0
         EX    R15,MVCEX2   ZWI --> ZWI1
CALL     EQU   *
         CALL  DCMPRS,(ZWI1,INP),VL
         LTR   R15,R15
         BZ    DCMPROK
         ABEND 222,DUMP
DCMPROK  EQU   *
         B     ALLOK
SPANNED  EQU   *
         CLI   SPBITS,X'01'
         BNE   TSTMIDL
         LH    R15,0(R6)
         BCTR  R15,0
         EX    R15,MVCEX2
         B     NEXTREC
TSTMIDL  EQU   *
         CLI   SPBITS,X'03'
         BNE   TSTLAST
         LA    R14,ZWI1
         AH    R14,0(R14)
         LH    R15,0(R6)
         SH    R15,=H'5'
         EX    R15,MVCEX3
         LH    R15,0(R6)
         SH    R15,=H'4'
         AH    R15,ZWI1
         STH   R15,ZWI1
         B     NEXTREC
TSTLAST  EQU   *
         CLI   SPBITS,X'02'
         BE    SPOK
         ABEND 111,DUMP
SPOK     EQU   *
         LA    R14,ZWI1
         AH    R14,0(R14)
         LH    R15,0(R6)
         SH    R15,=H'5'
         EX    R15,MVCEX3
         LH    R15,0(R6)
         SH    R15,=H'4'
         AH    R15,ZWI1
         STH   R15,ZWI1
         B     CALL
ALLOK    EQU   *
         MVC   OUTLIST,INPUT
         TM    SWITCH,X'20'
         BNO   NOCNT
         LA    R3,10(R3)
         CVD   R3,DBLWD
         UNPK  OUTLIST+72(8),DBLWD
         OI    OUTLIST+79,X'F0'
NOCNT    EQU   *
         PUT   SYSUT2,ASA
         PUT   SYSUT3,OUTLIST
         MVI   ASA,C' '
         BCT   R4,INCRSP
         MVI   ASA,C'1'
         LA    R4,60
INCRSP   EQU   *
         TM    SWITCH,X'08'
         BNO   NEXTREC
         AH    R9,0(R6)
         LR    R15,R9
         SR    R14,R14
         IC    R14,SPBITS+1
         SR    R14,R15
         CH    R14,=H'5'
         BL    NEXTREC
         AH    R6,0(R6)
         CLC   0(4,R6),=F'0'
         BE    NEXTREC
         B     NOREAD
EODMBR   EQU   *
         CLOSE SYSUT1
         FREEPOOL SYSUT1
         MVI   ASA,C' '
         MVC   OUTLIST,MESS6
         MVC   OUTLIST+15(8),JFCB+44
         PUT   SYSPRINT,ASA
         MVI   ASA,C'1'
         LA    R4,60
         B     NEXTMBR
EODSYSIN EQU   *
         MVI   ASA,C' '
         MVC   OUTLIST(80),ASA
         MVC   OUTLIST(20),MESS4
         PUT   SYSPRINT,ASA
         CLOSE (SYSIN,,SYSUT1,,SYSUT2,,SYSUT3,,PDS,,SYSPRINT)
         XRETURN 0,R
WTOR     EQU   *
         ST    R14,SAV14
         MVC   WTO,MESS1
         XC    ECB,ECB
         LR    R2,R0
         MVC   0(80,R2),=CL80' '
         L     R0,UCMID
         WTOR  ,(R2),80,ECB,MF=(E,WTO)
         WAIT  ECB=ECB
         OC    0(80,R2),=CL80' '
         CLC   0(4,R2),=CL4'.END'
         BE    EODSYSIN
         L     R14,SAV14
         BR    R14
ANALYZE  EQU   *
         ST    R14,SAV14
NXTPRM   EQU   *
         CLC   0(2,R2),=X'FFFF'
         BNE   CONT
FREE     EQU   *
         CLC   ADDRLIST(4),=F'0'
         BE    GET
         L     R2,ADDRLIST
         LH    R6,0(R2)
         FREEMAIN R,LV=(R6),A=(R2)
         B     GET
CONT     EQU   *
         IC    R5,0(R2)
         CH    R5,=H'12'
         BH    ERROR
         BCTR  R5,0
         MVC   NAME,=CL8' '
         EX    R5,MVCEX1
         LA    R8,0(R5,R2)
         BCTR  R8,0
         BCTR  R8,0
         CLC   0(4,R8),=C'.ALL'
         BNE   NOTALL
         SR    R8,R2
         BCTR  R8,0
         LTR   R8,R8
         BNZ   OK
         MVI   NOP1+1,X'00'
         MVI   NOP2+1,X'F0'
OK       EQU   *
         BCTR  R8,0
         TM    SWITCH,X'40'
         BO    READ
         OPEN  (DIRECT),TYPE=J
         OI    SWITCH,X'40'
READ     EQU   *
         READ  DECB,SF,DIRECT,DIRCTBLK,'S'
         CHECK DECB
         EX    R8,CLCEX1
NOP1     BH    READ
         LA    R9,DIRCTBLK+10
         SR    R10,R10
         LA    R11,2
         MVC   NEXTMBR+2(2),BRADDR1
         ST    R9,SAVE9
ENTRY1   EQU   *
         L     R9,SAVE9
         MVC   NAME(8),0(R9)
         CLC   NAME(8),=8XL1'FF'
         BE    EODIRECT
         IC    R10,11(R9)
         SLL   R10,27
         SRL   R10,26
         LA    R11,12(R10,R11)
         CH    R11,DIRCTBLK+8
         BNE   NOTEOB
         MVC   NEXTMBR+2(2),BRADDR3
NOTEOB   EQU   *
         LA    R9,12(R10,R9)
         ST    R9,SAVE9
         EX    R8,CLCEX2
NOP2     BE    RET
         B     NEXTMBR
EODIRECT EQU   *
         CLOSE DIRECT
         NI    SWITCH,X'BF'
         MVC   NEXTMBR+2(2),BRADDR2
         MVI   NOP1+1,X'20'
         MVI   NOP2+1,X'80'
         IC    R7,0(R2)
         LR    R8,R7
         SRL   R8,1
         SLL   R8,1
         CR    R7,R8
         BNE   LENOK1
         LA    R5,1(R5)
LENOK1   EQU   *
         LA    R2,6(R5,R2)
         B     NXTPRM
NOTALL   EQU   *
         IC    R7,0(R2)
         LR    R8,R7
         SRL   R8,1
         SLL   R8,1
         CR    R7,R8
         BNE   LENOK
         LA    R5,1(R5)
LENOK    EQU   *
         LA    R2,6(R5,R2)
         FIND  PDS,NAME,D
         B     *+4(R15)
         B     RET
         B     INCR
         B     IOERR
RET      EQU   *
         MVC   JFCB+44(8),NAME
         MVI   ASA,C'1'
         MVC   OUTLIST,REPL
         MVC   OUTLIST+13(8),NAME
         PUT   SYSUT2,ASA
         TM    SWITCH,X'10'
         BO    WROK
         PUT   SYSUT3,OUTLIST
WROK     EQU   *
         MVI   ASA,C' '
         TM    SWITCH,X'20'
         BNO   NONUMB
         TM    SWITCH,X'10'
         BO    NONUMB
         MVC   OUTLIST,NMBR
         PUT   SYSUT2,ASA
         PUT   SYSUT3,OUTLIST
NONUMB   EQU   *
         L     R14,SAV14
         BR    R14
INCR     EQU   *
         MVI   ASA,C' '
         MVC   OUTLIST,MESS2
         MVC   OUTLIST+15(8),NAME
         PUT   SYSPRINT,ASA
         TM    SWITCH,X'80'
         BNO   NXTPRM
         MVC   WTO,MSS2
         MVC   WTO+19(8),NAME
         L     R0,UCMID
         WTO   MF=(E,WTO)
         B     NXTPRM
IOERR    EQU   *
         MVI   ASA,C' '
         MVC   OUTLIST,MESS3
         PUT   SYSPRINT,ASA
         TM    SWITCH,X'80'
         BNO   NXTPRM
         MVC   WTO,MSS3
         L     R0,UCMID
         WTO   MF=(E,WTO)
         B     NXTPRM
ERROR    EQU   *
         MVI   ASA,C' '
         MVC   OUTLIST,MESS5
         PUT   SYSPRINT,ASA
         MVC   OUTLIST,INPUT
         PUT   SYSPRINT,ASA
         TM    SWITCH,X'80'
         BNO   FREE
         MVC   WTO,MSS5
         L     R0,UCMID
         WTO   MF=(E,WTO)
         B     FREE
INVINP   EQU   *
         MVI   ASA,C' '
         MVC   OUTLIST,MESS7
         PUT   SYSPRINT,ASA
         TM    SWITCH,X'80'
         BNO   FREE
         MVC   WTO,MSS7
         L     R0,UCMID
         WTO   MF=(E,WTO)
         B     FREE
PDS      DCB   DSORG=PO,MACRF=(R),DDNAME=SYSUT1,LRECL=80
SYSIN    DCB   DDNAME=SYSIN,RECFM=FBS,LRECL=80,DSORG=PS,MACRF=(GM),    *
               EODAD=EODSYSIN
SYSUT2   DCB   DDNAME=SYSUT2,RECFM=FBSA,LRECL=81,BLKSIZE=2997,         *
               DSORG=PS,MACRF=(PM)
SYSUT3   DCB   DDNAME=SYSUT3,RECFM=FBS,LRECL=80,DSORG=PS,              *
               MACRF=(PM)
SYSUT1   DCB   DDNAME=SYSUT1,LRECL=80,DSORG=PS,MACRF=(GM),             *
               EODAD=EODMBR,EXLST=DCBEX
DIRECT   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(R),RECFM=F,LRECL=256,     *
               BLKSIZE=256,EODAD=EODIRECT,KEYLEN=8,EXLST=DCBEX1
SYSPRINT DCB   DDNAME=SYSPRINT,RECFM=FBSA,LRECL=81,BLKSIZE=2997,       *
               DSORG=PS,MACRF=(PM)
MESS1    WTOR  'PRTPCH1 ENTER MEMBER-NAME(S) OR ''.END''',             *
               MCSFLAG=REG0,MF=L
MESS2    DC    CL80'PRTPCH2 MEMBER 12345678 NOT FOUND IN LIBRARY'
MSS2     WTO   'PRTPCH2 MEMBER 12345678 NOT FOUND IN LIBRARY',         *
               MCSFLAG=REG0,MF=L
MESS3    DC    CL80'PRTPCH3 PERMANENT I/O-ERROR FOUND DURING DIRECTORY *
               SEARCH'
MSS3     WTO   'PRTPCH3 PERMANENT I/O-ERROR FOUND DURING DIRECTORY SEAR*
               CH',MCSFLAG=REG0,MF=L
MESS4    DC    CL20'PRTPCH4 END OF JOB'
MESS5    DC    CL80'PRTPCH5 INPUT FORMAT ERROR. THE FOLLOWING CARD WILL*
                BE SKIPPED'
MSS5     WTO   'PRTPCH5 INPUT FORMAT ERROR',MCSFLAG=REG0,MF=L
MESS6    DC    CL80'PRTPCH6 MEMBER 12345678 HAS BEEN SUCCESSFULLY PROCE*
               SSED'
MESS7    DC    CL80'PRTPCH7 SYSUT1 NOT RECFM=F AND/OR LRECL=80'
MSS7     WTO   'PRTPCH7 SYSUT1 NOT RECFM=F AND/OR LRECL=80',           *
               MCSFLAG=REG0,MF=L
MVCEX1   MVC   NAME(0),1(R2)
MVCEX2   MVC   ZWI1(0),0(R6)
MVCEX3   MVC   0(0,R14),4(R6)
CLCEX1   CLC   NAME(0),DIRCTBLK
CLCEX2   CLC   NAME(0),1(R2)
ANF      DC    H'1'
END      DC    H'0'
DPL      DC    H'0'
NUMBER   DC    H'6',CL6'NUMBER'
NOADD    DC    H'5',CL5'NOADD'
NOEXPND  DC    H'7',CL7'NOEXPND'
BRADDR1  DC    S(ENTRY1)
BRADDR2  DC    S(ANALYZE)
BRADDR3  DC    S(READ)
DBLWD    DS    D
SPBITS   DS    CL2
INP      DC    H'80,0'
INPUT    DS    CL80
DCBEX1   DS    0F
         DC    X'87'
         DC    AL3(0)
DCBEX    DS    0F
         DC    X'87'
         DC    AL3(0)
ASA      DC    C'1'
OUTLIST  DC    CL80' '
ADDRLIST DS    F
NAME     DS    CL12
SWITCH   DC    X'00'
REPL     DC    CL80'./ ADD  NAME=12345678'
NMBR     DC    CL80'./ NUMBER NEW1=10,INCR=10'
ANSWER   DS    F
ZWI1     DS    CL90
         LTORG
         DS    0D
DIRCTBLK DS    CL264
WORK     DSECT
         DS    18F
WTO      DS    CL80
ECB      DS    F
SAV14    DS    F
JFCB     DS    CL176
JFCB1    DS    CL176
SAVE9    DS    F
UCMID    DS    F
ZWI      DS    CL90
WORKL    EQU   *-WORK
         END
