ESDSEEK  CSECT
         REG
         XSAVE R12,SAVE,SEEKV01
         USING IHADCB,R3
         L     R3,0(,R1)               DCB ADDRESS
         TM    DCBOFLGS,X'10'          OPENED?
         BZ    PREP                    NO
         LA    R9,NPROC                REFUSE PROCESSING
         B     XRET1
         EJECT
PREP     EQU   *
         L     R6,4(R1)                ADDRESS OF TABLE OF CSECTS
         OPEN  ((R3)),MF=(E,OPEN)
         TM    DCBOFLGS,X'10'          OPEN SUCCESSFUL?
         BO    CLTAB                   YES.
ERRDCB   EQU   *
         LA    R9,DCBE                 INDICATE 'DCB NOT CORRECT'
         B     XRET1
         SPACE 3
CLTAB    EQU   *
         LH    R5,0(R6)                TAB LENGTH
         LA    R2,2(R6)
         SRA   R5,4                    DIVIDE BY 16 (ENTRY LENGTH)
         BP    CLEAR                   RESULT POSITIVE?
         LA    R9,TABE                 INDICATE 'TAB ERROR'
         B     XRET1
         SPACE
CLEAR    EQU   *
         XC    8(8,R2),8(R2)           CLEAR RESULT FIELDS
         LA    R2,16(,R2)              POINT TO NEXT ENTRY
         BCT   R5,CLEAR
         EJECT
SORTESD  EQU   *
         GETMAIN VU,LA=MREQ,A=MOBT
         L     R4,MOBT                 MAIN STORAGE OBTAINED
         L     R5,MOBT+4               LENGTH
         AR    R5,R4                   END ADDRESS
         ST    R5,MOBT+4
         SPACE
READ     EQU   *
         READ  DECB,SF,(R3),(R4),'S'
         CHECK DECB
         CLI   0(R4),CESD              CESD BLOCK?
         BE    WORK                    YES.
         TM    FLAG,CESD               FIRST BLOCK?
         BO    FREE                    NO.
         LA    R9,ESDE
         B     XRET
         SPACE 3
COMPRESS MVC   0(0,R4),0(R5)
         SPACE 3
WORK     EQU   *
         OI    FLAG,CESD
         LH    R11,4(,R4)              ID OF FIRST ESD IN BLOCK
         LH    R10,6(,R4)              LENGTH OF ESD BLOCK
         LA    R5,8(,R4)               ADDRESS OF FIRST ESD
         SPACE
EX       EQU   *
         EX    R10,COMPRESS
         LR    R5,R4
VLD      EQU   *
         LR    R4,R5
         STC   R11,12(,R5)
         S     R10,F16
         BM    READ
         CLI   8(R5),LR                TYP=LR?
         BE    ACCE
         CLI   8(R5),SD                TYP=SD?
ACCE     EQU   *
         LA    R5,16(,R5)
         LA    R11,1(,R11)             ID OF NEXT ESD
         BNE   EX
         B     VLD
         SPACE 2
FREE     EQU   *
         L     R5,MOBT+4               END OF GETMAIN AREA
         SR    R5,R4                   GET NO. OF BYTES NOT NEEDED
         ST    R4,MOBT+4               END ADDRESS OF TAB
         FREEMAIN R,A=(R4),LV=(R5)
         SPACE
         L     R5,MOBT                 GET START OF GETMAIN AREA AGAIN
         SR    R4,R5                   GET SIZE OF TABLE
         SRL   R4,4                    GET NO.OF TAB ENTRIES
         STH   R4,PARAM                STORE IT INTO SORT PARM
         CALL  LUSORT,((R5),PARAM)
         EJECT
EP       EQU   *
         LH    R7,0(,R6)               LENGTH OF NAME TAB
         LA    R8,2(,R6)               START OF NAME TAB
         SRL   R7,4                    NO.OF ENTRIES
         NI    FLAG,255-IR
         SPACE
COMP     EQU   *
         TM    8(R8),ALL               ALL RESULTS OBTAINED?
         BO    NEXT                    YES
         OI    FLAG,IR
         CLC   0(8,R5),0(R8)
         BE    FILLTAB
         CLI   12(R8),0
         BE    NEXT
         CLI   8(R5),LR
         BNE   NEXT
         SPACE
         L     R1,8(,R8)               LOAD TYP + EPA (FROM CSECT TAB)
         LA    R1,0(,R1)               ERASE TYP
         L     R2,8(,R5)               LOAD TYP + EPA (FROM ESD)
         LA    R2,0(,R2)               ERASE TYP
         SR    R2,R1                   GET DIFFERENCE
         BZ    NEXT                    IF ZERO DIFF, IT WAS AN ALIAS..
*                                      IGNORE IT,
         IC    R1,15(,R8)
         ST    R2,12(,R8)              ELSE PUT IT INTO TAB
         STC   R1,12(,R8)
         OI    8(R8),ALL               INDICATE 'ALL RESULTS OBTAINED'
         SPACE
NEXT     EQU   *
         LA    R8,16(,R8)
         BCT   R7,COMP
         SR    R9,R9
         TM    FLAG,IR                 CSECT-TAB SATISFIED?
         BZ    XRET                    YES
         SPACE 2
         LA    R5,16(,R5)              NEXT CESD ENTRY
         C     R5,MOBT+4               END?
         BL    EP                      NO.
         LA    R9,NFILL
XRET     EQU   *
         L     R1,MOBT                 GETMAIN START
         L     R2,MOBT+4               GETMAIN END
         SR    R2,R1                   LENGTH
         FREEMAIN R,LV=(R2),A=(R1)
XRET1    EQU   *
         XRETURN (R9)
         EJECT
FILLTAB  EQU   *
         MVC   8(8,R8),8(R5)           MOVE RESULTS TO CSECT-TAB
         CLI   8(R8),SD                ALL VALID?
         BNE   NEXT                    NO.(LR)
         OI    8(R8),ALL
         B     NEXT
         EJECT
*D E F I N I T I O N S
*
         SPACE 3
FLAG     DC    X'00'
NE       EQU   X'80'
CESD     EQU   X'20'
IR       EQU   X'10'
         SPACE 2
SD       EQU   0
LR       EQU   3
ALL      EQU   128
***********************************************************************
* RETURN CODES:
NPROC    EQU   4
DCBE     EQU   8
TABE     EQU   12
ESDE     EQU   16
NFILL    EQU   32
         SPACE 2
OPEN     OPEN  (,),MF=L
         SPACE 3
AREA     DC    XL248'0'
         EJECT
MREQ     DC    F'3480',F'5120'         MIN. LENGTH REQUIRED:
*                                                   15 256-BYTES-BLOCK
*                                      MAX. LENGTH REQUIRED:
*                                                   20 256-BYTES-BLOCK
MOBT     DS    2F
F16      DC    F'16'
         SPACE 3
PARAM    DS    0F
         DS    H                       NO.OF ENTRIES
         DC    H'16'                   ENTRY LENGTH
* 1. SORT FIELD:
         DC    H'10'                   START
         DC    H'1'                    ASCENDING
         DC    H'12'                   END
* 2. SORT FIELD:
         DC    H'9'
         DC    H'1'
         DC    H'9'
*
ZERO     DC    H'0'
         LTORG
         DCBD  DSORG=PS
         END
