         TITLE 'LADEN VON SPOOL-DATEIEN (SPOOL-OFF-LOAD-FACILITY)'
LSPOOL   CSECT
         USING *,15
         B     M00100
         DC    CL20' LSPOOL - 11.09.1984'
         DS    0H
M00100   EQU   *
         DROP  15
         SPACE
**********************************************************************
*                                                                    *
*  DIESES PROGRAMM DIENT ZUM LADEN DER VORHER MIT DSPOOL ENTLADENEN  *
*  SPOOL-DATA-SETS. IN DER DATEI 'SYS2.DSPOOL.CTL' WERDEN DIE        *
*  DS-NAMEN DER BAENDER, AUF DIE ENTLADEN WURDE, FESTGEHALTEN.       *
*  DAS PROGRAMM LSPOOL ZEIGT AN, WELCHE BAENDER NOCH NICHT WIEDER    *
*  ZURUECKGELADEN WURDEN, UND FRAGT DEN OPERATOR, WELCHES BAND       *
*  ZURUECKGELADEN WERDEN SOLL. AUFGRUND DER OPERATOR-ANTWORT WIRD    *
*  DER ENTSPRECHENDE LOADER MODIFIZIERT UND GESTARTET.               *
*  NACH ERFOLGREICHEM LADEN WIRD DER LOADER GESTOPPT UND EIN UPDATE  *
*  AUF 'SYS2.DSPOOL.CTL' GEMACHT, DASS DIE DATEIEN ZURUECKGELADEN    *
*  WORDEN SIND.                                                      *
*                                                                    *
**********************************************************************
*                        *
*  FRANCKE - 28.02.1983  *
*                        *
**************************
         EJECT
         STM   14,12,12(13)
         LR    3,15
         USING LSPOOL,3
         SPACE
       GETMAIN RC,LV=72,SP=0
         SPACE
         LTR   15,15
         BZ    M00200
         SPACE
         ABEND 1                GETMAIN NICHT ERFOLGREICH
         SPACE
M00200   EQU   *
         ST    1,8(13)          SAVE AREA DIESES PROGRAMMS
         ST    13,4(1)          BACK CHAIN
         LR    13,1
         L     1,4(13)          SAVE AREA DES RUFENDEN PROGRAMMS
         LM    0,1,20(1)        RESTORE REGISTER 0 UND 1
         L     12,0(1)
         CLC   0(2,12),=X'0000'
         BE    M02000           KEIN PARAMETER VORHANDEN
         CLC   0(2,12),=X'0004'
         BE    M00800
         SPACE
         WTO   'LSPOOL - PARAMETER UNGUELTIG'
         SPACE
         B     M91000
         EJECT
M00800   EQU   *
         L     12,2(12)
         MVC   TCMD+2(1),0(12)        COMMUNICATION CHAR FOR SECONDARY
         MVC   SCMD+2(1),0(12)        SUBSYSTEM
         MVC   PCMD+2(1),0(12)
M02000   EQU   *
         SPACE
         OPEN  (CTL,,OUT,(OUTPUT))
         EJECT
*****************************************************************
*                                                               *
*  EINLESEN ALLER SAETZE UND SPEICHERN IN STAB                  *
*                                                               *
*****************************************************************
M02500   EQU   *
         LA    6,STAB
         LA    7,11
         USING SATZ,6
M02600   EQU   *
         READ  DECB,SF,CTL,SATZ,'S',MF=E
         SPACE
         CHECK DECB
         SPACE
         LA    6,255(6)
         BCT   7,M02600
         DROP  6
         SPACE
M02800   EQU   *
         CLOSE (CTL)
         EJECT
*****************************************************************
*                                                               *
*  ANZEIGEN ALLER NOCH NICHT ZURUECKGELADENEN BAENDER           *
*                                                               *
*****************************************************************
M02900   EQU   *
         SLR   5,5
         NI    NOP010+1,X'00'
         LA    7,11
         LA    6,STAB
         USING SATZ,6
M03000   EQU   *
         CLI   STATUS,C'D'
         BNE   M03500
M03200   EQU   *
NOP010   NOP   M03300
         OI    NOP010+1,X'F0'
         SPACE
         WTO   'DIE FOLGENDEN BAENDER SIND NOCH NICHT GELADEN WORDEN:',*
               ROUTCDE=2,DESC=2
         SPACE
         ST    1,WTOIDS(5)
         LA    5,4(5)
M03300   EQU   *
         MVC   WTOBER(LWTO1),WTO1
         MVC   WTOBER+4(100),SATZ
         MVC   WTOBER+4(3),=C'LOD'
         SPACE
         WTO   MF=(E,WTOBER)
         SPACE
         ST    1,WTOIDS(5)
         LA    5,4(5)
M03500   EQU   *
         LA    6,255(6)
         BCT   7,M03000
         DROP  6
         EJECT
         LTR   5,5
         BNZ   M04000
*************************************************************
*                                                           *
*  ES SIND ALLE BAENDER SCHON EINMAL ZURUECKGELADEN WORDEN  *
*                                                           *
*************************************************************
         WTO   'ES SIND ALLE BAENDER ZURUECKGELADEN WORDEN',ROUTCDE=2, *
               DESC=2
         SPACE
         ST    1,WTOIDS
         OI    WTOIDS,X'80'
         MVI   REPLY,C' '
         MVC   REPLY+1(31),REPLY
         XC    WECB,WECB
         SPACE
         WTOR  'BITTE DS-NAMEN DES ZU LADENDEN BANDES ODER END EINGEBEN*
               ',REPLY,32,WECB,ROUTCDE=1
         SPACE
         WTO   'END ERST NACH BEENDIGUNG ALLER LADE-VORGAENGE EINGEBEN'*
               ,ROUTCDE=2,DESC=2
         SPACE
         WAIT  ECB=WECB
         SPACE
         DOM   MSGLIST=WTOIDS
         EJECT
         CLC   REPLY(3),=C'END'
         BE    M05200
****************************************
*                                      *
*  MODIFIZIEREN DER OPERATOR-COMMANDS  *
*                                      *
****************************************
         LA    6,8
         MH    6,=H'255'
         LA    6,STAB(6)
         USING SATZ,6
         MVI   TCMD+6,C'9'
         MVI   SCMD+6,C'9'
         MVI   PCMD+6,C'9'
         MVI   TCMD+19,C' '
         MVC   TCMD+20(32),TCMD+19
         MVC   TCMD+19(32),REPLY
         MVI   SATZ,C' '
         MVC   SATZ+1(254),SATZ
         MVC   DMPX,=C'LOD9'
         MVC   DSNAME,REPLY
M03700   EQU   *
         B     M05000
         DROP  6
         EJECT
************************************************************
*                                                          *
*  DER OPERATOR GIBT AN, WELCHES BAND GELADEN WERDEN SOLL  *
*                                                          *
************************************************************
M04000   EQU   *
         XC    WECB,WECB
         SPACE
         WTOR  'BITTE MIT LOADER-NUMMER ODER END ANTWORTEN',REPLY,3,   *
               WECB,ROUTCDE=1
         SPACE
         WTO   'END ERST NACH BEENDIGUNG ALLER LADE-VORGAENGE EINGEBEN'*
               ,ROUTCDE=2,DESC=2
         SPACE
         WAIT  ECB=WECB
         SPACE
         LA    5,WTOIDS-4(5)
         OI    0(5),X'80'
         SPACE
         DOM   MSGLIST=WTOIDS
         SPACE
         CLC   REPLY(3),=C'END'
         BE    M05200
         EJECT
         LA    7,11
         LA    6,STAB
         USING SATZ,6
M04100   EQU   *
         CLC   DMPX+3(1),REPLY
         BE    M04200
         LA    6,255(6)
         BCT   7,M04100
         B     M04300
         SPACE
M04200   EQU   *
         CLI   STATUS,C'D'
         BE    M04900
         CLI   STATUS,C'L'
         BE    M04400
         SPACE
M04300   EQU   *
         WTO   'DIE EINGABE WAR UNGUELTIG'
         SPACE
         B     M02900
         EJECT
**********************************************************************
*                                                                    *
*  DAS VOM OPERATOR ANGEGEBENE BAND IST SCHON EINMAL GELADEN WORDEN  *
*                                                                    *
**********************************************************************
M04400   EQU   *
         WTO   'FOLGENDES BAND IST SCHON ZURUECKGELADEN WORDEN:',      *
               ROUTCDE=2,DESC=2
         SPACE
         ST    1,WTOIDS
         MVC   WTOBER(LWTO1),WTO1
         MVC   WTOBER+4(100),SATZ
         MVC   WTOBER+4(3),=C'LOD'
         SPACE
         WTO   MF=(E,WTOBER)
         SPACE
         ST    1,WTOIDS+4
         OI    WTOIDS+4,X'80'
         XC    WECB,WECB
         SPACE
         WTOR  'SOLL ES NOCH EINMAL GELADEN WERDEN? BITTE MIT YES, NO O*
               DER END ANTWORTEN',REPLY,3,WECB,ROUTCDE=1
         SPACE
         WTO   'END ERST NACH BEENDIGUNG ALLER LADE-VORGAENGE EINGEBEN'*
               ,ROUTCDE=2,DESC=2
         EJECT
         WAIT  ECB=WECB
         SPACE
         DOM   MSGLIST=WTOIDS
         SPACE
         CLC   REPLY(3),=C'END'
         BE    M05200
         CLC   REPLY(2),=C'NO'
         BE    M02900
         CLC   REPLY(3),=C'YES'
         BE    M04900
         SPACE
         WTO   'ANTWORT FALSCH'
         SPACE
         B     M04400
         SPACE
         EJECT
****************************************
*                                      *
*  MODIFIZIEREN DER OPERATOR-COMMANDS  *
*                                      *
****************************************
M04900   EQU   *
         MVC   TCMD+6(1),DMPX+3
         MVC   SCMD+6(1),DMPX+3
         MVC   PCMD+6(1),DMPX+3
         MVI   TCMD+19,C' '
         MVC   TCMD+20(32),TCMD+19
         MVC   TCMD+19(19),DSNAME
         SPACE
**************************
*                        *
*  UMSETZEN DES LOADERS  *
*                        *
**************************
M05000   EQU   *
         SR    0,0
         LA    1,TLOD
         SVC   250                    ISSUE OPERATOR-COMMAND
         SPACE
         LA    1,1
         LNR   1,1
         LA    0,1
         SVC   34                     DELETE CIB
         SPACE
*************************
*                       *
*  STARTEN DES LOADERS  *
*                       *
*************************
         SR    0,0
         LA    1,SLOD
         SVC   250                    ISSUE OPERATOR-COMMAND
         SPACE
         LA    1,1
         LNR   1,1
         LA    0,1
         SVC   34                     DELETE CIB
         EJECT
**********************************************************
*                                                        *
*  OPERATOR UEBER DURCHGEFUEHRTE TAETIGKEIT INFORMIEREN  *
*                                                        *
**********************************************************
         MVC   WTOBER(LWTO2),WTO2
         MVC   WTOBER+14(1),DMPX+3
         MVC   WTOBER+40(19),DSNAME
         SPACE
         WTO   MF=(E,WTOBER)
         SPACE
**************************************
*                                    *
*  SCHREIBEN SATZ AUF KONTROLLDATEI  *
*                                    *
**************************************
M10000   EQU   *
         USING SATZ,6
         SPACE
         TIME
         SPACE
         ST    1,DW
         UNPK  LOADDAT,DW+1(3)
         OI    LOADDAT+4,X'F0'
         ST    0,DW
         UNPK  DW,DW(4)
         MVC   LOADTIME,DW+1
         MVI   STATUS,C'L'
         SPACE
         PUT   OUT,SATZ
         SPACE
         B     M02900
         DROP  6
         EJECT
*************************
*                       *
*  STOPPEN DES LOADERS  *
*                       *
*************************
M05200   EQU   *
         ZAP   DW,=P'1'
M05250   EQU   *
         UNPK  PCMD+6(1),DW+7(1)
         OI    PCMD+6,X'F0'
         SR    0,0
         LA    1,PLOD
         SVC   250                    ISSUE OPERATOR-COMMAND
         SPACE
         LA    1,1
         LNR   1,1
         LA    0,1
         SVC   34                     DELETE CIB
         SPACE
         AP    DW,=P'1'
         CP    DW,=P'10'
         BL    M05250
         EJECT
**************************************************
*                                                *
*  ZURUECKSCHREIBEN DER SAETZE IN KONTROLLDATEI  *
*                                                *
**************************************************
         SPACE
         OPEN  (CTL,(OUTPUT))
         SPACE
         LA    7,11
         LA    6,STAB
         USING SATZ,6
M05400   EQU   *
         SPACE
         WRITE DECB,SF,CTL,SATZ,'S',MF=E
         SPACE
         CHECK DECB
         SPACE
         LA    6,255(6)
         BCT   7,M05400
         EJECT
M09000   EQU   *
         CLOSE (CTL)
         SPACE
         B     M90000
         EJECT
*******************
*                 *
*  PROGRAMM-ENDE  *
*                 *
*******************
M90000   EQU   *
         CLOSE (OUT)
         SPACE
M91000   EQU   *
         L     13,4(13)
         L     1,8(13)
         SPACE
      FREEMAIN RC,LV=72,A=(1),SP=0
         SPACE
         LM    14,12,12(13)
         SLR   15,15
         BR    14
         EJECT
******************
*                *
*  DEFINITIONEN  *
*                *
******************
DW       DS    D
WTOIDS   DS    10F
WECB     DS    F
TLOD     DS    0F
         DC    H'56',H'0'
TCMD     DC    CL52' $TLODX,HOLD=N,DSN=I.SPOOLOFF.GXXXXV00 '
SLOD     DS    0F
         DC    H'12',H'0'
SCMD     DC    C' $SLODX '
PLOD     DS    0F
         DC    H'12',H'0'
PCMD     DC    C' $PLODX '
REPLY    DC    CL32' '
WTOBER   DS    CL108
         EJECT
         READ  DECB,SF,MF=L
         SPACE
WTO1     WTO   'LOD1 I.SPOOLOFF.G0001V00 000000 D 83050 080000 ..... ..*
               .... ........................................',         *
               ROUTCDE=2,DESC=2,MF=L
LWTO1    EQU   *-WTO1
         SPACE
WTO2     WTO   'DER LOADERX IST GESTARTET. DAS BAND I.SPOOLOFF.GXXXXV00*
                WIRD GELADEN',ROUTCDE=2,DESC=2,MF=L
LWTO2    EQU   *-WTO2
         SPACE
         LTORG
         EJECT
OUT      DCB   DDNAME=OUT,DSORG=PS,MACRF=PM
         EJECT
CTL      DCB   DDNAME=CTL,DSORG=PS,MACRF=(R,W),EODAD=M02800
         EJECT
SATZ     DS    0CL255
DMPX     DS    CL4
         DS    C
DSNAME   DS    CL19
         DS    C
VOLSER   DS    CL6
         DS    C
STATUS   DS    C
         DS    C
DUMPDAT  DS    CL5
         DS    C
DUMPTIME DS    CL6
         DS    C
LOADDAT  DS    CL5
         DS    C
LOADTIME DS    CL6
         DS    C
DUMPPARM DS    CL195
         SPACE
STAB     DS    11CL255
         SPACE
         DROP  3
         END   LSPOOL
