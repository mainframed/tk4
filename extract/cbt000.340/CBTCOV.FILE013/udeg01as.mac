RESC     START 0
         PRINT NOGEN
         SPACE 5
BEGIN    STM   14,12,12(13)
         LR    2,15
         USING BEGIN,2,3,4,5,6
         LM    3,6,YYBASE
         ST    13,YYSAVE+4
         LR    7,13
         LA    13,YYSAVE
         ST    13,8(7)
         L     14,0(1)
         LH    15,0(14)
         LA    14,2(14)
         LTR   15,15
         BZ    YYEND
         CLI   0(14),C'X'
         BNE   YYFPARM
         SH    15,=H'2'
         BNP   YYEND
         STC   15,YYPMVC+1
YYPMVC   MVC   PARM(1),1(14)
YYEND    EQU   *
         B     ANFANG
YYFPARM  WTO   'FEHLER 0'
EXIT3    ABEND 333
EXIT4    ABEND 444,DUMP
RETURN   L     13,YYSAVE+4
         L     15,YYCOND
         RETURN (14,12),RC=(15)
         SPACE 3
YYCOND   DC    F'0'
YYSAVE   DC    18A(0)
YYBASE   DC    A(BEGIN+1*4096,BEGIN+2*4096,BEGIN+3*4096,BEGIN+4*4096)
PARM     DC    CL100' '
         EJECT
I1OLD    DCB   DSORG=PS,MACRF=(GM),DDNAME=DDI1OLD,EODAD=I1OLDEOF
         SPACE 2
O1NEW    DCB   DSORG=PS,MACRF=(GM),DDNAME=DDO1NEW,EODAD=O1NEWEOF
         SPACE 4
ISOLD    DCB   DSORG=IS,MACRF=(GM),DDNAME=DDISOLD,EODAD=ISOLDEOF
         SPACE 2
OSNEW    DCB   DSORG=IS,MACRF=(GM),DDNAME=DDOSNEW,EODAD=OSNEWEOF
         EJECT
ANFANG   EQU   *
         SPACE 2
* SHOW DCB EXLST = JFCBEXIT      EROEFFNEN DATEIEN
         SPACE
         CLC   PARM(2),=C'IS'
         BE    ISDCB
         SPACE
         LA    R10,I1OLD
         USING IHADCB,R10
         MVC   DCBEXLST+1(3),=AL3(JFCBEXIT)
         LA    R10,O1NEW
         USING IHADCB,R10
         MVC   DCBEXLST+1(3),=AL3(JFCBEXIT)
         SPACE 2
         OPEN  (I1OLD,,O1NEW)
         LA    R7,PARMPS
         B     POS51
         SPACE 5
ISDCB    LA    R10,ISOLD
         USING IHADCB,R10
         MVC   DCBEXLST+1(3),=AL3(JFCBEXIT)
         LA    R10,OSNEW
         USING IHADCB,R10
         MVC   DCBEXLST+1(3),=AL3(JFCBEXIT)
         SPACE 2
         OPEN  (ISOLD,,OSNEW)
         LA    R7,PARMIS
         EJECT
* DSNAMEN UCBTYP VOLSER ERMITTELN
         SPACE 2
POS51    EQU   *
         L     R8,0(0,R7)              A(DCB ALTE DATEI)
         L     R9,4(0,R7)              A(DSN ALTE DATEI)
         BAL   R10,URDJFCB
         SPACE 2
         L     R8,8(0,R7)              A(DCB NEUE DATEI)
         L     R9,12(0,R7)             A(DSN NEUE DATEI)
         BAL   R10,URDJFCB
         SPACE 5
         L     R8,0(0,R7)              A(DCB ALTE DATEI)
         L     R9,16(0,R7)             A(VOLLIST ALTE DATEI)
         BAL   R10,UUCB
         SPACE 2
         L     R8,8(0,R7)              A(DCB NEUE DATEI)
         L     R9,20(0,R7)             A(VOLLIST NEUE DATEI)
         BAL   R10,UUCB
         SPACE 5
         CLC   PARM(2),=C'IS'
         BE    CLOSEIS
         CLOSE (I1OLD,,O1NEW)
         B     POS52
         SPACE 2
CLOSEIS  CLOSE (ISOLD,,OSNEW)
         EJECT
POS52    EQU   *
         SPACE 2
**
* ALLE FOLGENDEN MACROS SIND IN DER BROSCHUERE
* OS/VS2 SYSTEM PROGRAMMING LIBRARY:DATA MANAGEMENT BESCHRIEBEN
* GC26-3830   (30-66)
**
         SPACE 2
* SCRATCH ALTE DATEI
         SPACE
         SR    R0,R0                   SHOW VOLUME MUST BE MOUNTED
         SPACE
**
         SCRATCH  DCBOLD
**
         SPACE
         LTR   R15,R15
         BNZ   MSG1
         CLI   STCOLD+1,X'00'
         BNE   MSG2
         SPACE 5
* UNCAT ALTE DATEI
         SPACE
**
         CATALOG  UNCATOLD
**
         SPACE
         LTR   R15,R15
         BNZ   MSG3
         SPACE 5
* RENAME NEUE DATEI IN DSN DER ALTEN DATEI
         SPACE
         SR    R0,R0                   SHOW VOLUME MUST BE MOUNTED
         SPACE
**
         RENAME  DCBNEW
**
         SPACE
         LTR   R15,R15
         BNZ   MSG4
         CLI   STCNEW+1,X'00'
         BNE   MSG5
         SPACE 5
* CAT UMBENANNTE NEUE DATEI
         SPACE
         MVC   STCNEW,=H'0'            SEQUENCE#
         SPACE
**
         CATALOG  CATNEW
**
         LTR   R15,R15
         BNZ   MSG6
         SPACE 5
* UNCAT NEUE DATEI
         SPACE
**
         CATALOG  UNCATNEW
**
         SPACE
         LTR   R15,R15
         BNZ   MSG7
         SPACE 5
         B     RETURN
         EJECT
MSG1     WTO   'FEHLER 1'
         B     EXIT4
         SPACE 2
MSG2     WTO   'FEHLER 2'
         B     EXIT4
         SPACE 2
MSG3     WTO   'FEHLER 3'
         B     EXIT4
         SPACE 2
MSG4     WTO   'FEHLER 4'
         B     EXIT4
         SPACE 2
MSG5     WTO   'FEHLER 5'
         B     EXIT4
         SPACE 2
MSG6     WTO   'FEHLER 6'
         B     EXIT4
         SPACE 2
MSG7     WTO   'FEHLER 7'
         B     EXIT4
         EJECT
**
* URDJFCB
*
*        INPUT : R8 DCB-ADRESSE
*                R9 A(DSNAME)
*
*        OUTPUT: DSNAME AUS JFCB
*
*        RETURN: R10
*
**
         SPACE 2
URDJFCB  EQU   *
         ST    R8,RDJFCBAD     <*******DCB-ADR
         MVI   RDJFCBAD,X'80'  *   <***OPTION BYTE
         CNOP  0,4             *   *   ALIGN LIST TO FULLWORD
         BAL   1,*+8           *   *   LOAD R1 WITH LIST ADDR.
RDJFCBAD DC    AL1(0)          *  <*   OPTION BYTE
         DC    AL3(0)         <*       DCB ADDRESS
         SVC   64                      ISSUE RDJFCB SVC
         SPACE 2
         MVC   0(44,R9),JFCB
         SPACE 2
         BR    R10
         SPACE 5
         PRINT GEN
* AUFLOESUNG   RDJFCB
         SPACE
         RDJFCB  I1OLD
         SPACE
         PRINT NOGEN
         EJECT
**
* UUCB
*
*        INPUT : R8 DCB-ADRESSE
*                R9 A(VOLUME LIST)
*                PARM DATASETORGANIZATION
*
*        OUTPUT: UCBTYP
*                VOLSER  AUS UCB
*
*        RETURN: R10
*
**
         SPACE 2
UUCB     EQU   *
         AH    R8,=H'44'               DCB + 44 ***> DEB
         L     R8,0(0,R8)              A(DEB)
         AH    R8,=H'32'               DEB BASIC SECTION UEBERSPRINGEN
         CLC   PARM(2),=C'IS'
         BNE   UUCB1
         AH    R8,=H'16'               DEB ISAM SECTION UEBERSPRINGEN
UUCB1    L     R8,0(0,R8)              A(UCB)
         SPACE 2
         MVC   2(4,R9),16(R8)          UCBTYP
         MVC   6(6,R9),28(R8)          VOLSER
         SPACE 2
         BR    R10
         EJECT
*
**
***     D E F I N I T I O N E N
**
*
         SPACE 2
* CAMLST SCRATCH (ALTE DATEI)
         SPACE
DCBOLD   CAMLST  SCRATCH,DSNOLD,,VOLLIOLD,,OVRD
         SPACE 5
* CAMLST UNCAT   (ALTE DATEI)
         SPACE
UNCATOLD CAMLST  UNCAT,DSNOLD
         SPACE 5
* CAMLST RENAME  (NEUE DATEI)
         SPACE
DCBNEW   CAMLST  RENAME,DSNNEW,DSNOLD,VOLLINEW
         SPACE 5
* CAMLST CAT     (NEUE DATEI)
         SPACE
CATNEW   CAMLST  CAT,DSNOLD,,VOLLINEW
         SPACE 5
* CAMLST UNCAT   (NEUE DATEI)
         SPACE
UNCATNEW CAMLST  UNCAT,DSNNEW
         EJECT
JFCBEXIT DS    0F
         DC    X'07'                   SHOW JFCBEXIT
         DC    AL3(JFCB)               A(JFCB)
         DC    X'80'                   END OF LIST
         SPACE 5
JFCB     DC    CL176' '                JOB FILE CONTROL BLOCK
         SPACE 5
         DCBD  DSORG=PS,DEVD=DA
RESC     CSECT
         SPACE 5
PARMPS   DC    A(I1OLD)
         DC    A(DSNOLD)
         DC    A(O1NEW)
         DC    A(DSNNEW)
         DC    A(VOLLIOLD)
         DC    A(VOLLINEW)
         SPACE 3
PARMIS   DC    A(ISOLD)
         DC    A(DSNOLD)
         DC    A(OSNEW)
         DC    A(DSNNEW)
         DC    A(VOLLIOLD)
         DC    A(VOLLINEW)
         SPACE 5
DSNOLD   DC    CL44' '                 DSN ALTE DATEI
         SPACE 3
VOLLIOLD DC    H'1'                    ANZAHL VOLUMES
UCBTOLD  DC    XL4'00'                 DEVICE CODE (UCBTYP)
VOLOLD   DC    CL6' '                  VOLSER
STCOLD   DC    H'0'                    SCRATCH STATUS CODE
         SPACE 5
DSNNEW   DC    CL44' '                 DSN NEUE DATEI
         SPACE 3
VOLLINEW DC    H'1'                    ANZAHL VOLUMES
UCBTNEW  DC    XL4'00'                 DEVICE CODE (UCBTYP)
VOLNEW   DC    CL6' '                  VOLSER
STCNEW   DC    H'0'                    RENAME STATUS CODE BZW. VOLSEQ#
         SPACE 5
R0       EQU   0
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R15      EQU   15
         SPACE 5
         CNOP  0,4
I1OLDEOF EQU   *
O1NEWEOF EQU   *
ISOLDEOF EQU   *
OSNEWEOF WTO   'FEHLER 8'
         B     EXIT4
         SPACE 5
         END   BEGIN
