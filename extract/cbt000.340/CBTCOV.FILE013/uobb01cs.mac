S9210605 CSECT 0
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3                  BASISREGISTER
R4       EQU   4                  LENGTH OF DIRECTORY BLOCK
R5       EQU   5                  LENGTH COUNT
R6       EQU   6                  TABADDRESS
R7       EQU   7                  WORKAREAINDEX
R8       EQU   8                  NUMBER OF HALFWORDS
R9       EQU   9                  MEMBERCOUNT TABELLE
R10      EQU   10                 SAVEREGISTER FOR TABADDRESS
R11      EQU   11                 TABADDRESS FOR ALIASCOMPARE
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*
*********************************
*
* DIESES PROGRAMM WIRD IN STEP2 DES VON PROGRAMM S9210604
* ERZEUGTEN JOBS AUFGERUFEN. DAS PROGRAMM LISTET DIRECTORY-
* ENTRIES UND ALIAS-MEMBERS.
*
* PARM='PROCEDURE-LIBRARY NAME'
* EXAMPLE: PARM='SYS1.PROCLIB'
*
*
* PROGRAMMIERER: LUCNY
* DATE: 79 05 29
* LEVEL 00
*
*********************************
*
*
         SAVE  (14,12),,LUCNY
         LR    R3,R15
         USING S9210605,R3
         ST    R13,SAVE+4
         LR    R12,R13
         LA    R13,SAVE
         ST    R13,8(R12)
         ST    R1,PARAM           SAVE PARMADDRESS
         B     START
*
*
* SAVEAREAS AND DCB
*
*
SAVE     DC    18F'0'
PARAM    DC    F'0'
DIRECT   DCB   DSORG=PS,MACRF=GM,DDNAME=DIRECT,RECFM=F,BLKSIZE=256,    *
               EODAD=ERROR,LRECL=256
LIST     DCB   DSORG=PS,MACRF=PM,DDNAME=LIST,RECFM=FBA,LRECL=133,      *
               BLKSIZE=1330
*
         EJECT
START    EQU   *
         L     R2,0(R1)
         SR    R4,R4
         LH    R4,0(R2)           LENGTH OF PARM IN R4
         BCTR  R4,0
         EX    R4,MVC1            MOVE PDSNAME TO HEADLINE
*
         OPEN  DIRECT
         OPEN  (LIST,OUTPUT)
         SR    R9,R9
         LA    R6,TAB
         LA    R10,TAB           SAVE TABADDR TO R10
*
*
*
*
READ     EQU   *
         GET   DIRECT,WORK
         LH    R4,WORK            LENGTH OF DIR.BLOCK
         LA    R5,2               COUNT IN BEGINPOSITION
         LA    R7,WORK+2          WORKINDEX
LOOP     EQU   *
         CR    R5,R4              BLOCKEND?
         BNL    READ
         CLC  0(8,R7),=8X'FF'       LAST DIRECT-MEMBER
         BE   OUTPUT
*
*
*
MVCTAB   MVC   0(4,R6),8(R7)     TTRADDRESS TO TAB
         MVC   4(8,R6),0(R7)     MEMBERNAME TO TAB
         LA   R9,1(R9)            MEMBERCOUNT
*
*
         CH   R9,=H'500'          MAXIMAL 500MEMBERS !!!!!!!!!
         BE   OUTPUT
*
*
         SR   R8,R8
         NI   11(R7),X'1F'        LOESCHEN ALIAS-BIT
         IC   R8,11(R7)           R8=NUMBER OF USERHALFWORDS
         AR   R8,R8
         LA   R7,12(R7)           R7=BUMP WORK-INDEX
         AR   R7,R8
         LA   R5,12(R5)           R5=BUMP COUNT
         AR   R5,R8
         LA   R6,12(R6)            R6=BUMP TABADDRESS
         B    LOOP
*
         EJECT
OUTPUT   EQU   *
         PUT   LIST,HEADLINE
         LR    R6,R10              R6 TO TABADDRESS
WRITE    EQU   *
         CLC   4(8,R6),=8X'FF'     END OF TAB
         BE    ENDE
         TM    3(R6),X'80'         ALIAS ?
         BO    ALIAS
         MVI   DBER+1,X'40'
         MVC   DBER+2(131),DBER+1
         MVC   DBER+1(8),4(R6)     MOVE MEMBER TO DBER
         PUT   LIST,DBER
         MVI   ASA,X'40'
         LA    R6,12(R6)
         B     WRITE
*
*
ALIAS    EQU   *
         LR    R11,R10            SET R11 TO TABANFANG
LOOP2    EQU   *
         CLC   0(3,R11),=3X'FF'
         BNE   ON
*
* ALIAS WITHOUT EXISTING MEMBER
*
         MVI   DBER+1,X'40'
         MVC   DBER+2(131),DBER+1
         MVC   DBER+1(8),4(R6)
         MVC   DBER+10(35),=C' IS AN ALIAS FOR NO EXISTING MEMBER'
         PUT   LIST,DBER
         LA    R6,12(R6)
         B     WRITE
*
*
ON       EQU   *
         CLC   0(3,R11),0(R6)         COMPARE TTR-ADDRESS
         BE    EQUAL
         LA    R11,12(R11)            BUMP TABADDRESS
         B     LOOP2
*
*
EQUAL    EQU   *
         CLC   4(8,R6),4(R11)         EQUAL MEMBER
         BNE   WTALIAS
         LA    R11,12(R11)
         B     LOOP2
*
*
WTALIAS  EQU   *
         MVI   DBER+1,X'40'
         MVC   DBER+2(131),DBER+1
         MVC   DBER+1(8),4(R6)
         MVC   DBER+10(23),=C' IS AN ALIAS FOR MEMBER'
         MVC   DBER+34(8),4(R11)
         PUT   LIST,DBER
         LA    R6,12(R6)
         B     WRITE
*
*
*
ENDE     EQU   *
*
*
         CLOSE (DIRECT,,LIST)
         L     R13,SAVE+4
         RETURN (14,12),RC=0
*
*
*
ERROR    EQU   *
         WTO   'INPUT FOR S9210605 FAILED',ROUTCDE=11
         B     ENDE
*
*
* DEFINITIONEN
*
*
HEADLINE DC    CL133'1         DIRECTORY INFORMATION FROM '
MVC1     MVC   HEADLINE+37(1),2(R2)
WORK     DC    64CL4'WORK'
DBER     DS    0CL133
ASA      DC    C'-'
         DC    33CL4'DBER'
         LTORG
TAB      DC   501XL12'FFFFFFFFFFFFFFFFFFFFFFFF'
         END   S9210605
