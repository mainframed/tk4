* STAND 04/14/81
         MACRO
         BALIN &NAME,&ENTRY
         AIF   (T'&ENTRY NE 'O').E1
         DS    0F
&NAME    B     *+8
         DS    XL4
         ST    RSPRG,*-4
         MEXIT
.E1      ANOP
         B     &NAME+4
&NAME    ST    RSPRG,&ENTRY+4
         MEND
         MACRO
&ENTER   BALOUT &NAME,&RET
&ENTER   L     RSPRG,&NAME+4
         AIF   (T'&RET EQ 'O').GO
         LA    RSPRG,&RET.(RSPRG)
.GO      ANOP
         BR    RSPRG
         MEND
         TITLE 'V S A M  I / O'
VSAMIO   CSECT
****************************************
*
*  C A L L   V S A M I O,(OP,KEY)
*
*  FUEHRT VSAM I/O-ZUGRIFFE AUF ASTSUM DURCH
*
*  OP  H    DURCHZUFUEHRENDE I/O OPERATION
*           H'0'  OPEN
*           H'4'  GET UPDATE
*           H'8'  PUT UPDATE
*           H'12' CLOSE
*
*  KEY CL10 SCHLUESSEL BEI GET/PUT
*
*  RETURNCODE REGISTER 15
*           0   OK
*           1   SCHLUESSEL NICHT AUF DATEI
*           3   DDKARTE FEHLT
*           4   SONSTIGE VSAM FEHLER
*
**********************************
         REG
ROP      EQU   R10            OPERATION
RERR     EQU   R11            RETURNCODE
RSPRG    EQU   R9             REGISTER BEI BAL
         SPACE 1
         XSAVE R12,SAVE,VSAMIO
         SPACE 1
         ST    R1,ARGADR      -->ARGUMENTENLISTE
         SR    RERR,RERR      RETURNCODE
         SPACE 1
*
* SPRUNG LAUT OPERATION
*
         L     ROP,0(R1)      -->OPERATION
         LH    ROP,0(ROP)     OPERATION
         B     *+4(ROP)
         SPACE 1
         B     OPEN010
         B     GET010
         B     PUT010
         B     CLOSE010
         TITLE 'OPEN ASTSUM'
OPEN010  EQU   *
         OPEN  (ASTSUM)
         SPACE 1
         C     R15,=F'4'      WARNING
         BNH   RETURN         OK
         SPACE 1
         BAL   RSPRG,ERROR
         B     ABBRUCH        FEHLER
         B     RETURN         DATEI SCHON OFFEN
         LA    RERR,3         DDKARTE FEHLT
         B     RETURN
         TITLE 'GET UPDATE'
GET010   EQU   *
         BAL   RSPRG,MOD010   ARGUMENTE IN RPL SETZEN
         SPACE 1
         GET   RPL=ASTRPL
               SPACE          1
         LTR   R15,R15
         BZ    RETURN         OK
         SPACE 1
         BAL   RSPRG,ERROR
         B     ABBRUCH        FEHLER
         LA    RERR,1         SARZ NICHT AUF DATEI
         B     RETURN
         TITLE 'PUT UPDATE'
PUT010   EQU   *
         BAL   RSPRG,MOD010   ARGUMENTE IN RPL SETZEN
         SPACE 1
         PUT   RPL=ASTRPL
         SPACE 1
         LTR   R15,R15
         BZ    RETURN         OK
         SPACE 1
         BAL   RSPRG,ERROR
         B     ABBRUCH        FEHLER
         TITLE 'CLOSE ASTSUM'
CLOSE010 EQU   *
         CLOSE (ASTSUM)
         SPACE 1
         LTR   R15,R15
         BZ    RETURN         OK
         SPACE 1
         BAL   RSPRG,ERROR
         B     ABBRUCH        FEHLER
         B     RETURN         DATEI SCHON GESCHLOSSEN
         EJECT
         TITLE 'RUECKSPRUNG INS AUFRUFENDE PROGRAMM'
*
*  VSAM-I/O FEHLER
*
ABBRUCH  EQU   *
         L     R3,=V(VSAMKZ)
         MVI   0(R3),1        KENNUNG VSAM FEHLER
         SPACE 1
         LA    RERR,4
         SPACE 1
*
*  RUECKSPRUNG
*
RETURN   EQU   *
         XRETURN (RERR)
         TITLE 'ARGUMENTE IN RPL SETZEN'
         BALIN MOD010
         L     R3,ARGADR
         L     R5,4(R3)      -->SCHLUESSEL
         SPACE 1
         MODCB RPL=ASTRPL,ARG=(R5)
         SPACE 1
         LTR   R15,R15
         BZ    MOD090         OK
         SPACE 1
         MVC   ERRMOD,=CL4'MOD'
         BAL   RSPRG,ERROR
         B     ABBRUCH        FEHLER
         SPACE 1
MOD090   BALOUT MOD010,0
         TITLE 'FEHLERBEHANDLUNG'
         BALIN ERROR
         ST    R15,ERRREG
         XC    ERRFLAG,ERRFLAG
         SPACE 1
*
*  SPRUNG LAUT OPERATION
*
         B     *+4(ROP)
         B     ERR020         OPEN
         B     ERR010         GET
         B     ERR010         PUT
         B     ERR020         CLOSE
         SPACE 1
ERR010   SHOWCB RPL=ASTRPL,AREA=ERRFLAG,LENGTH=4,FIELDS=FDBK
         SPACE 1
         B     ERR030
         SPACE 1
ERR020   SHOWCB ACB=ASTSUM,AREA=ERRFLAG,LENGTH=4,FIELDS=ERROR
         SPACE 1
ERR030   EQU   *
         LTR   R15,R15
         BZ    ERR100         SHOWCB OK
         SPACE 1
         MVC   ERRMOD,=CL4'SHOW'
         SPACE 1
*
*  ERRFLAG, DIE NICHT FEHLER SIND, ABFRAGEN
*
ERR100   EQU   *
         CLC   ERRMOD,=CL4' '
         BNE   ERR200         MODCB/SHOWCB
         SPACE 1
         B     *+4(ROP)
         B     ERR110         OPEN
         B     ERR120         GET
         B     ERR200         PUT
         B     ERR130         CLOSE
         SPACE 2
*
*  BEI OPEN  DDKARTE FEHLT / SCHON OFFEN
*
ERR110   EQU   *
         CLC   ERRFLAG,=F'168'
         BE    RETURN         WARNING ASTSUM IST OPEN
*        BE    ERR115
         CLC   ERRFLAG,=F'128'
         BNE   ERR112
         BALOUT ERROR,8       DDKARTE FEHLT
ERR112   EQU   *
         CLC   ERRFLAG,=F'4'
         BNE   ERR200         FEHLER
         BALOUT ERROR,4       DATEI SCHON OFFEN
         SPACE 1
         SPACE 1
*  ASTSUM BELEGT --> WARTEN
ERR115   EQU   *
         LA    R1,QCB
         SVC   249
         STIMER  WAIT,BINTVL=ZEIT
         B     OPEN010
ZEIT     DC    F'2000'        20 SEKUNDEN
QCB      DC    H'29,0'
         DC    CL25' F X,QCB,OPV.ASTSUM.DATA '
         DS    0H             ALIGNMENT
*
*  BEI GET   RECORD NOT FOUND
*
ERR120   EQU   *
         CLC   ERRFLAG,=F'16'
         BNE   ERR200
         BALOUT ERROR,4       SATZ FEHLT
         SPACE 1
*
*  BEI CLOSE   DATEI SCHON GESCHLOSSEN
*
ERR130   EQU   *
         CLC   ERRFLAG,=F'4'
         BNE   ERR200         FEHLER
         BALOUT ERROR,4       SCHON GESCHLOSSEN
*
*  FEHLERMELDUNG AUFBAUEN
*
ERR200   EQU   *
         L     R3,ERRTXT(ROP)
         ST    R3,ERROP
         SPACE 1
         L     R3,ERRREG
         CVD   R3,DWORD
         OI    DWORD+7,X'0F'
         UNPK  ERROP15,DWORD+6(2)
         SPACE 1
         L     R3,ERRFLAG
         CVD   R3,DWORD
         OI    DWORD+7,X'0F'
         UNPK  ERRCODE,DWORD+6(2)
         SPACE 1
         SPACE 1
         SPACE 1
         CVD   R15,DWORD
         OI    DWORD+7,X'0F'
         UNPK  ERRMOD15,DWORD+6(2)
         SPACE 1
         CVD   R0,DWORD
         OI    DWORD+7,X'0F'
         UNPK  ERRMOD0,DWORD+6(2)
         SPACE 1
         CALL  DRUFEHL,(F15,ERRMSG),VL
         SPACE 1
ERR900   EQU   *
         MVC   ERRMOD,=CL4' '
         BALOUT ERROR,0
         TITLE 'DEFINITIONEN'
ARGADR   DS    F              -->ARGUMENTE
ERRREG   DS    F              REGISTER 15 NACH VSAM-OP
F15      DC    F'15'
ERRTXT   DC    CL16'OPENGET PUT CLOS'
DWORD    DS    D
ERRFLAG  DS    F              VSAM FEHLERCODE
SATZLN   EQU   4+14+26*15
         ENTRY ASTSATZ
ASTSATZ  DS    CL(SATZLN)
         SPACE 2
*
*  FEHLERMELDUNG
*
ERRMSG   DC    CL80' '
         ORG   ERRMSG
ERROP    DS    CL4
         DC    CL7' REG15 '
ERROP15  DS    CL3
         DC    CL7' ERROR '
ERRCODE  DS    CL3
         DC    CL1' '
ERRMOD   DC    CL4' '
         DC    CL1' '
ERRMOD15 DS    CL3
         DC    CL1' '
ERRMOD0  DS    CL3
         ORG
         SPACE 2
*
*  ACB FUER ASTSUM
*
ASTSUM   ACB   AM=VSAM,                                                *
               DDNAME=ASTSUM,                                          *
               MACRF=(KEY,DIR,OUT)
         SPACE 2
*
*  RPL FUER ASTSUM UPDATE
*
ASTRPL   RPL   ACB=ASTSUM,                                             *
               AM=VSAM,                                                *
               KEYLEN=10,                                              *
               AREA=ASTSATZ,                                           *
               AREALEN=SATZLN,                                         *
               OPTCD=(KEY,DIR,UPD,FKS,SYN,KEQ,MVE)
         SPACE 1
         LTORG
         END
