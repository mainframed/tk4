IEFUJV   CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3             BASIS-REGISTER
BASE     EQU   3             BASIS-REGISTER
R4       EQU   4             A(COMMON EXIT PARAMETER AREA)
R5       EQU   5             A(JCL-KARTE)
R6       EQU   6             A(KARTENART)
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12            BASIS-REGISTER FUER WORKAREA
R13      EQU   13            A(SAVE-AREA DES RUFENDEN PGM)
SAVE     EQU   13            A(SAVE-AREA DES RUFENDEN PGM)
R14      EQU   14            A(RUECKSPRUNG)
R15      EQU   15            RETURN-CODE
*
*---------------------------------------------------------------------
*        REENTRANT EROEFFNUNG
*
         SAVE  (14,12),,*    IN SAVE AREA DES RUFENDEN PROGRAMMES
         LR    R3,R15        BASISREGISTER
         USING IEFUJV,R3
         LR    R11,R1        PARM REG RETTEN
         GETMAIN R,LV=GLEN,SP=0 FUER SAVEAREA
         LTR   R15,R15       GETMAIN OK ?
         BNZ   NOCORE        NEIN
         XC    0(GLEN,R1),0(R1) GETMAIN FELD LOESCHEN
         ST    R1,8(R13)     A(NEUE SAVE AREA)
         ST    R13,4(R1)     A(ALTE SAVE AREA)
         LR    R13,R1        A(NEUE SAVE AREA) IN R13
         USING WORKAREA,R12  BASISREG. FUER WORKAREA
         USING GMAIN,R13     BASISREG. FUER SAVE AREA
         LR    R1,R11        PARM. REG. ZURUECKLADEN
*
*--------------------------------------------------------------------
*        PROGRAMM-BEGINN
*
         LM    R4,R6,0(R1)   R4=A(COMMON) R5=A(KARTE) R6=A(KARTENART)
         CLI   0(R6),X'20'   GESAMTE JCL DURCH INTERPRETER ?
         BE    RETURN        JA
         TM    29(R4),X'01'  FOREGROUND (TSO) ?
         BO    RETURN        JA
         L     R12,20(R4)    A(WORKAREA) AUS USER-ID-FIELD
         C     R12,=X'40404040' GETMAIN SCHON DURCHGEFUEHRT ?
         BE    GETW          NEIN
         C     R12,=X'00000000' GETMAIN SCHON DURCHGEFUEHRT (JES2/3) ?
         BNE   EOF           JA
GETW     GETMAIN R,LV=WLEN,SP=0 FUER WORKAREA
         LTR   R15,R15       GETMAIN OK ?
         BNZ   NOCORE2       NEIN
         XC    0(WLEN,R1),0(R1) GETMAIN FELD LOESCHEN
         LA    R1,0(R1)      1. BYTE IN R1 LOESCHEN
         ST    R1,20(R4)     A(WORKAREA) NACH USER-ID-FIELD
         LR    R12,R1        A(WORKAREA)
         MVC   WTOLIST(WTOLEN),WTO   WTO-LISTFORM IN WORKAREA STELLEN
         MVC   WTOLIST+23(8),0(R4)   JOBNAME IN WTO-TEXT STELLEN
EOF      CLI   0(R6),X'10'   GESAMTE JCL DURCH KONVERTER ?
         BNE   CARD          NEIN
         C     R12,=X'40404040' WAR GETMAIN ERFOLGREICH ?
         BE    RETURN        NEIN
         C     R12,=X'00000000' WAR GETMAIN ERFOLGREICH ?
         BE    RETURN        NEIN
         FREEMAIN R,LV=WLEN,A=(R12) WORKAREA
         MVC   20(4,R4),=X'40404040' LOESCHE USER-ID-FIELD
         B     RETURN
*
CARD     EQU   *
         LH    R0,WSEQ       JCL
         AH    R0,=H'1'      KARTEN
         STH   R0,WSEQ       ZAEHLEN
*
*****    MVC   72(2,R5),=C'KA'    KARTENART
*****    MVC   74(1,R5),0(R6)     NACH SPALTE 73-75
*****    OI    74(R5),X'F0'       VORZEICHEN
*
         CLI   0(R6),X'01'   JOBKARTE ?
         BE    JOBCONT       JA
*
*----------------------------------------------------------------------
*        VERZWEIGEN GEMAESS VORHERIGER KARTE
*
         SR    R15,R15
         IC    R15,WMERK1    VOM LETZTEN DURCHLAUF GESETZTER INDIKATOR
         B     *+4(R15)
         B     RETURN        INDIKATOR X'00'
         B     MVCMSG1       INDIKATOR X'04' = ERROR-MSG 1
         B     MVCMSG2       INDIKATOR X'08' = ERROR-MSG 2
         B     MVCMSG3       INDIKATOR X'0C' = ERROR-MSG 3
         B     MVCMSG4       INDIKATOR X'10' = ERROR-MSG 4
         B     MVCMSG5       INDIKATOR X'14' = ERROR-MSG 5
         B     MVCMSG6       INDIKATOR X'18' = ERROR-MSG 6
         B     MVCMSG7       INDIKATOR X'1C' = ERROR-MSG 7
         B     MVCMSG8       INDIKATOR X'20' = ERROR-MSG 8
*
*
JOBCONT  EQU   *             KEIN RETURN WEGEN MSGLEVEL
*OBCONT  CLI   WMERK2,X'04'  TIME-PARAMETER ERLEDIGT ?
*        BE    RETURN        JA
         LA    R1,3(R5)      REGISTER
         LA    R2,20(R5)     SETZEN
         LA    R7,=C' JOB M' FUER
         LA    R8,6          SEEKLTRL
         BAL   R14,SEEKLTRL  WURDE DER JOB
         LTR   R15,R15       VON DER KONSOLE GESTARTET?
         BZ    RETURN        JA
*
*----------------------------------------------------------------------
*        ACCOUNT-FELD AUF FACHNR. UND  PARAMETER AUF NUMERISCH PRUEFEN
*
         LH    R0,WSEQ
         CH    R0,=H'1'      1. JCL-STMNT. IM JOB ?
         BNE   PARANF1       NEIN
         LA    R1,7(R5)                 SUCHEN DER ERSTEN
         LA    R2,70(R5)                KLAMMER DES
         LA    R7,=C'('                 ACCOUNT-FELDES
         LA    R8,1
         BAL   R14,SEEKLTRL
         LTR   R15,R15                  GEFUNDEN?
         BNZ   CANCEL7                  NEIN
         LR    R11,R1                   ERSTER ACCOUNT-PARAMETER
         TRT   0(2,R11),TRANSTAB        NUMERISCH?
         BC    7,CANCEL7                NEIN
         LA    R1,3(R11)                ZWEITEN PARAMETER
         LA    R7,=C','                 UEBERSPRINGEN
         LA    R8,1
         BAL   R14,SEEKLTRL             UND DEN
         LR    R10,R1                   DRITTEN
         TRT   0(4,R10),TRANSTAB
         BC    7,CANCEL7                UND VIERTEN
         TRT   5(2,R10),TRANSTAB        ACCOUNT-PARAMETER
         BC    7,CANCEL7                AUF NUMERISCH PRUEFEN
         CLC   8(6,R10),=C'RELOAD'      SPOOLTRANSFER ?
         BNE   FACHNR                   NEIN, WEITERE ACCOUNTPRUEFUNG
         MVC   8(6,R10),=C'999000'      SONST PROJEKTNR. WIEDEREINSET-
         B     PARANF                   ZEN, ACCOUNTPRUEFUNG BEENDEN
FACHNR   TRT   8(6,R10),TRANSTAB        PROJEKTNR. NUMERISCH?
         BC    7,CANCEL7
         CLC   14(3,R10),=C',,,'        RICHTIGE ANZAHL VON KOMMATA?
         BNE   CANCEL7                  NEIN
         LA    R1,14(R10)               ZWEITE
         LA    R11,17(R10)              KLAMMER
         LA    R10,18(R10)              DES ACCOUNT-FELDES
         LA    R7,=C')'                 SUCHEN
         LA    R8,1
         BAL   R14,SEEKLTRL
         LTR   R15,R15                  GEFUNDEN?
         BNZ   CANCEL7                  NEIN
         SR    R1,R10                   LAENGE DER FACHNUMMER
         C     R1,=F'3'                 ZU GROSS?
         BH    CANCEL7                  JA
         C     R1,=F'1'                 FACHNR VORHANDEN?
         BL    CANCEL7                  NEIN
         BCTR  R1,0
         EX    R1,EXTRT                 WENN JA, IST SIE NUMERISCH?
         BC    7,CANCEL7                NEIN
*
*----------------------------------------------------------------------
*        PARAMETER-BEREICH ERMITTELN ( WPARANF - WPAREND )
*
PARANF   LA    R1,3(R5)      ANFANG
         LA    R2,20(R5)     DES
         LA    R7,=C' JOB'   PARAMETER-
         LA    R8,4          BEREICHES
         BAL   R14,SEEKLTRL  SUCHEN
         LTR   R15,R15
         BZ    PARANF2
         B     CANCEL5
PARANF1  LA    R1,2(R5)      ANF.-ADR. IN FOLGEKARTE
PARANF2  LA    R2,20(R5)
PARANF3  CLI   1(R1),C' '    PARAMETER-ANFANG ?
         BNE   NOBL          JA
         LA    R1,1(R1)
         CR    R1,R2
         BL    PARANF3
         B     CANCEL5       JCL-ERROR
NOBL     LA    R1,1(R1)      A(1. ZEICHEN IM PARAMETER-BEREICH)
         ST    R1,WPARANF    NACH WORKAREA
*
         LR    R11,R1        ANF.-ADR. FUER SUCHEN DES HOCHKOMMA
         LA    R2,70(R5)     1. BLANK
ENDBLANK LA    R7,=C' '      HINTER
         LA    R8,1          PARAMETER-BEREICH-ANFANG
         BAL   R14,SEEKLTRL  SUCHEN
         LTR   R15,R15       BLANK GEFUNDEN ?
         BNZ   NOBLANK       NEIN
         LR    R2,R1         A(LETZTES
         BCTR  R2,0          ZEICHEN
         BCTR  R2,0          UNGLEICH BLANK)
         LR    R1,R11        ANF.-ADR. FUER SUCHEN DES HOCHKOMMA
         LA    R7,=C''''     WURDE BLANK
         LA    R8,1          ZWISCHEN
         BAL   R14,SEEKLTRL  HOCHKOMMA KODIERT ?
         LTR   R15,R15       1. HOCHKOMMA GEFUNDEN ?
         BZ    HKOMMA2       JA
         LR    R1,R2         A(LETZTES ZEICHEN IM PAR.-BEREICH)
         B     STPAREND
HKOMMA2  LA    R2,70(R5)     A(SPALTE 71)
         LA    R8,1
         BAL   R14,SEEKLTRL  2. HOCHKOMMA SUCHEN
         LTR   R15,R15       2. HOCHKOMMA GEFUNDEN ?
         BNZ   CANCEL6       PAR. NICHT VOLLSTAENDIG IM JCL-STMNT.
         LR    R11,R1        ANF.-ADR. FUER SUCHEN DES HOCHKOMMA
         B     ENDBLANK
NOBLANK  BCTR  R1,0          A(SPALTE 71)
STPAREND ST    R1,WPAREND    A(LETZTES ZEICHEN IM PAR.-BEREICH)
         CLI   0(R1),C','    WIRD FOLGEKARTE ERWARTET ?
         BNE   LOESCH        NEIN
         MVI   WMERK4,X'04'  INDIKATOR: NAECHSTE KARTE = FOLGEKARTE
         B     MSGLEVEL
LOESCH   MVI   WMERK4,X'00'  INDIKATOR LOESCHEN
*
*----------------------------------------------------------------------
*        MSGLEVEL-PARAMETER SUCHEN
*
MSGLEVEL TM    WMERK3,X'10'  MSGLEVEL BEREITS GEFUNDEN ?
         BO    TWMERK2       JA
         L     R1,WPARANF    ANF.ADR. FUER MSGLEVEL-PARAMETER SUCHEN
         L     R2,WPAREND    END-ADR. FUER MSGLEVEL-PARAMETER SUCHEN
         LA    R7,=C'MSGLEVEL=' ADR. DES LITERALS NACH R7
SEEKMSGL LA    R8,9          LAENGE DES LITERALS NACH R8
         BAL   R14,SEEKLTRL  'MSGLEVEL=' SUCHEN
         LTR   R15,R15       MSGLEVEL-PARAMETER GEFUNDEN ?
         BNZ   TWMERK2       NEIN
         OI    WMERK3,X'10'  MERKE: MSGLEVEL-PARAMETER GEFUNDEN
         CLI   0(R1),C'('    KLAMMER-NOTATION?
         BNE   MSGLVL0       NEIN
         LA    R7,=C')'      ADR. DES LITERALS NACH R7, LAENGE NACH R8,
         LA    R8,1          R1 UND R2 STEHEN BEREITS RICHTIG
         BAL   R14,SEEKLTRL  ')' SUCHEN
         LTR   R15,R15       GEFUNDEN?
         BNZ   CANCEL5       NEIN, JCL-ERROR
         BCTR  R1,0          R1 UM 2 ZURUECKSETZEN AUF DIE
         BCTR  R1,0          ADRESSE DES 2. MSGLEVEL-PARAMETERS
MSGLVL0  EQU   *
         MVC   24(1,R4),0(R1) INDIK.: KEINE STEPINFOS (F. IEFACTRT)
TWMERK2  TM    WMERK2,X'04'  TIME-PARAMETER OK?
         BO    RETURN        JA
*
*----------------------------------------------------------------------
*        CLASS-PARAMETER SUCHEN
*
CLASS    TM    WMERK3,X'04'  CLASS BEREITS GEFUNDEN ?
         BO    TIME          JA
         L     R1,WPARANF    ANF.ADR. FUER CLASS-PARAMETER SUCHEN
         L     R2,WPAREND    END-ADR. FUER CLASS-PARAMETER SUCHEN
         LA    R7,=C'CLASS=' ADR. DES LITERALS NACH R7
SEEKCLS  LA    R8,6          LAENGE DES LITERALS NACH R8
         BAL   R14,SEEKLTRL  'CLASS=' SUCHEN
         LTR   R15,R15       CLASS-PARAMETER GEFUNDEN ?
         BNZ   TIME          NEIN
         SH    R1,=H'7'      A(ZEICHEN VOR 'CLASS=')
         CLI   0(R1),C'G'    MSGCLASS ?
         BNE   NOMSGCLS      NEIN
         AH    R1,=H'7'      A(ZEICHEN NACH 'CLASS=')
         B     SEEKCLS
NOMSGCLS MVC   WCLAS(1),7(R1) CLASS NACH WORKAREA
         MVC   25(1,R4),7(R1) CLASS NACH CEPA FUER IEFUJI
         OI    WMERK3,X'04'  MERKE: CLASS-PARAMETER GEFUNDEN
*
*----------------------------------------------------------------------
*        TIME-PARAMETER SUCHEN
*
TIME     TM    WMERK3,X'08'  TIME BEREITS GEFUNDEN ?
         BO    TESTALL       JA
         L     R1,WPARANF    ANF.ADR. FUER TIME-PARAMETER SUCHEN
         L     R2,WPAREND    END-ADR. FUER TIME-PARAMETER SUCHEN
         LA    R7,=C'TIME='  ADR. DES LITERALS NACH R7
         LA    R8,5          LAENGE DES LITERALS NACH R8
         BAL   R14,SEEKLTRL  'TIME=' SUCHEN
         LTR   R15,R15       TIME PARAMETER GEFUNDEN ?
         BNZ   TESTALL       NEIN
         ST    R1,WATIME     A(ZEICHEN HINTER 'TIME=')
         L     R2,WPAREND    LAENGE JOB-PARAMETER
         LA    R2,1(R2)      HINTER 'TIME='
         SR    R2,R1         NACH
         ST    R2,WPARLANG   WPARLANG
         MVC   WTIME(10),0(R1) TIME NACH WORKAREA
         MVC   WTIMESEQ(2),WSEQ JCL-KARTEN-NR. DES TIME-PAR.
         OI    WMERK3,X'08'  MERKE: TIME-PARAMETER GEFUNDEN
*
*----------------------------------------------------------------------
*        VERZWEIGEN GEMAESS VORHANDENEN PARAMETERN
*
TESTALL  SR    R15,R15
         IC    R15,WMERK3
         B     *+4(R15)
         B     NPAR          KEIN PARAMETER GEFUNDEN
         B     CPAR          CLASS-PARAMETER GEFUNDEN
         B     TPAR          TIME-PARAMETER GEFUNDEN
         B     CTPAR         CLASS- UND TIME-PARAMETER GEFUNDEN
         B     NPAR          MSGLEVEL GEFUNDEN
         B     CPAR          MSGLEVEL U. CLASS GEFUNDEN
         B     TPAR          MSGLEVEL U. TIME GEFUNDEN
         B     CTPAR         MSGLEVEL, CLASS U. TIME GEFUNDEN
*
*----------------------------------------------------------------------
*        KEIN PARAMETER GEFUNDEN
*
NPAR     CLI   WMERK4,X'04'  WIRD FOLGEKARTE ERWARTET ?
         BE    RETURN        JA
         B     CANCEL8
*
*----------------------------------------------------------------------
*        NUR CLASS-PARAMETER VORHANDEN
*
CPAR     CLI   WMERK4,X'04'  WIRD FOLGEKARTE ERWARTET ?
         BE    RETURN        JA
SETTIME  LA    R1,JOBCLASS   A(TABELLEN-ANFANG)
         SR    R2,R2
         IC    R2,WCLAS      JOBKLASSE
         SH    R2,=H'193'    ALS DISPLACEMENT
         BM    CANCEL5       (JCL-ERROR)
         SLL   R2,2          ZUR ADRESSIERUNG
         LA    R1,0(R2,R1)   VERWENDET
         L     R1,0(R1)      A(ZUGEHOERIGER TIME-PARAMETER)
         LTR   R1,R1         SPEZ. TIME-PAR. VORGESCHRIEBEN ?
         BNZ   CPAR2         JA
         LA    R1,DEFAULT    A(DEFAULT TIME-PARAMETER)
CPAR2    LA    R7,70(R5)     A(SPALTE 71)
         S     R7,WPAREND    L'FREIES FELD AM ENDE DES JCL-STMNTS.
         CH    R7,4(R1)      HAT TIME-PARAMETER PLATZ ?
         BL    CANCEL3       NEIN
         L     R2,WPAREND    A(PARAMETER-BEREICH-ENDE)
         LH    R7,4(R1)      LAENGE DES TIME-PARAMETERS
         BCTR  R7,0          LAENGE-1 FUER EXMVC
         EX    R7,EXMVC1     TIME-PARAMETER EINSETZEN
         LA    R15,2(R7,R2)  A(HINTER EINGEFUEGTEM TIME-PAR.)
         LA    R1,70(R5)     A(SPALTE 71)
         CR    R15,R1        KAEME BLANK HINTER SPALTE 71 ?
         BH    SETIMEOK      JA
         MVI   0(R15),C' '   BLANK ALS TRENNUNG VOM KOMMENTAR-BEREICH
         B     SETIMEOK      TIME-PARAMETER ERLEDIGT
*
*----------------------------------------------------------------------
*        NUR TIME-PARAMETER VORHANDEN
*
TPAR     CLI   WMERK4,X'04'  WIRD FOLGEKARTE ERWARTET ?
         BE    RETURN        JA
         B     CANCEL8
*
*----------------------------------------------------------------------
*        CLASS- UND TIME-PARAMETER VORHANDEN
*
CTPAR    CLI   WTIME,C'0'    TIME=0 ?
         BE    CANCEL2       JA
         LA    R1,JOBCLASS   A(TABELLEN-ANFANG)
         SR    R2,R2
         IC    R2,WCLAS      JOBKLASSE
         SH    R2,=H'193'    ALS DISPLACEMENT
         BM    CANCEL5       (JCL-ERROR)
         SLL   R2,2          ZUR ADRESSIERUNG
         LA    R1,0(R2,R1)   VERWENDET
         L     R1,0(R1)      A(ZUGEHOERIGER TIME-PARAMETER)
         LTR   R1,R1         SPEZ. TIME-PAR. VORGESCHRIEBEN ?
         BZ    SETIMEOK      NEIN
         BAL   R14,TIMEKONT  VERGLEICHEN TIME-PARAMETER
         L     R1,WCLASSAD   MIT VORGESHCRIEBENEM
         CLC   WNORMTIM(4),0(R1) TIME-PARAMETER
         BNH   SETIMEOK
         CLC   WTIMESEQ(2),WSEQ TIME-PAR. IN LFD. JCL-KARTE ?
         BNE   CANCEL1       NEIN
         ST    R1,WAVTIME    A(VORGESCHR. TIME-PAR.)
*
*----------------------------------------------------------------------
*        TIME-PARAMETER KORRIGIEREN
*
KORRTIME L     R11,WATIME    A(TIME-PAR. IN JOBKARTE)
         LA    R2,10(R11)    END-ADR. FUER SUCHEN
         C     R2,WPAREND    ADR. AUSSERHALB PARAMETER-BEREICH ?
         BNH   KORR1         NEIN
         L     R2,WPAREND    END-ADR. FUER SUCHEN
KORR1    LR    R1,R11        A(HINTER 'TIME=')
         CLI   0(R1),C'('    TIME-PAR. MIT KLAMMERN KODIERT ?
         BNE   KORR3         NEIN
KORR2    LA    R1,1(R1)      A(NAECHSTES ZEICHEN)
         CLI   0(R1),C')'    PARAMETER-ENDE GEFUNDEN ?
         BE    KORR4         JA
         CR    R1,R2         ENDE ERREICHT ?
         BNH   KORR2         NEIN
         B     CANCEL2       ')' FEHLT
KORR3    LA    R1,1(R1)      A(NAECHSTES ZEICHEN)
         CLI   0(R1),C','    PARAMETER-ENDE GEFUNDEN ?
         BE    KORR5         JA
         CLI   0(R1),C' '    PARAMETER-ENDE GEFUNDEN ?
         BE    KORR5         JA
         CR    R1,R2         ENDE ERREICHT ?
         BNH   KORR3         NEIN
         B     CANCEL2       TIME-PAR. ZU LANG
KORR4    LA    R1,1(R1)      A(1. ZU VERSCHIEBENDES
KORR5    LR    R7,R1         ZEICHEN) IN R7
         SR    R11,R7        L'TIME-PAR. IN JOBKARTE
         LPR   R11,R11
         L     R8,WAVTIME    A(VORGESCHR. TIME-PAR.)
         LH    R8,4(R8)      L'VORGESCHR. TIME-PAR.
         SH    R8,=H'6'      L' OHNE ',TIME='
         CR    R8,R11        VERGL. VORGESCHR. MIT KODIERTER LAENGE
         BE    INSERT        GLEICHE LAENGE
         BL    LINKS         VORGESCHR. TIME-PAR. < KODIERTER TIME-PAR.
RECHTS   SR    R8,R11        INCREMENT IN R8
         LR    R1,R7         A(1. ZU VERSCH. ZEICHEN)
         L     R2,WPAREND    A(LETZTES ZU VERSCH. ZEICHEN)
         BAL   R14,SHIFTR    VERSCHIEBE NACH RECHTS
         LTR   R15,R15       VERSCHIEBUNG O.K. ?
         BNZ   CANCEL4       NEIN
         LA    R15,1(R8,R2)  A(HINTER PARAMETER-BEREICH)
         LA    R1,70(R5)     A(SPALTE 71)
         CR    R15,R1        KAEME BLANK HINTER SPALTE 71 ?
         BH    INSERT        JA
         MVI   0(R15),C' '   BLANK ALS TRENNUNG VOM KOMMENTAR-BEREICH
         B     INSERT
LINKS    SR    R11,R8        INCREMENT IN R11
         LR    R10,R11       UND IN R10
         LR    R1,R7         A(1. ZU VERSCH. ZEICHEN)
         SR    R1,R11        WOHIN-ADR. FUER MVC IN R1
         L     R11,WPAREND   LAENGE
         SR    R11,R7        FUER
         BP    LINKS1        VERSCHIEBUNG
         LR    R11,R10       NACH
         BCTR  R11,0         LINKS
LINKS1   EX    R11,EXMVC2    VERSCHIEBE NACH LINKS
         L     R1,WPAREND    BLANKS
         SR    R1,R10        AUF
         MVI   1(R1),C' '    DIE
         SH    R10,=H'1'     RESTLICHEN
         BNP   INSERT        STELLEN
         EX    R10,EXMVC4
INSERT   L     R7,WAVTIME    A(VORGESCHR. TIME-PAR.)
         LH    R8,4(R7)      L'VORGESCHR. TIME-PAR.
         SH    R8,=H'6'      L' OHNE ',TIME='
         BCTR  R8,0          L' FUER EX
         L     R1,WATIME     A(TIME-PAR. IN JOBKARTE)
         EX    R8,EXMVC3     VORGESCHR. TIME-PAR. EINFUEGEN
*
*----------------------------------------------------------------------
*        SETZE INDIKATOR FUER TIME-PARAMETER ERLEDIGT
*
SETIMEOK MVI   WMERK2,X'04'  MERKE: TIME-PARAMETER ERLEDIGT
*
*----------------------------------------------------------------------
*        REENTRANT ABSCHLUSS
*
RETURN   SR    R15,R15       RETURN CODE 0
STORERC  LR    R5,R15        RETURN CODE
         LR    R1,R13        A(GETMAIN FELD)
         L     R13,4(R13)    A(ALTE SAVE AREA)
         FREEMAIN R,LV=GLEN,A=(R1)
         LR    R15,R5        RETURN CODE
STORERCF ST    R15,16(SAVE)  RETURN CODE NACH SAVEAREA D.RUF.PGM.
         RETURN (14,12)
*
***********************************************************************
*
CANCEL   LA    R15,4         RETURN CODE 4
         MVI   WMERK1,X'00'  INDIKATOR LOESCHEN
         B     STORERC
*
NOCORE   WTO   '--- IEFUJV --- GETMAIN FUER SAVEAREA IN SUBPOOL 255 NIC*
               HT ERFOLGREICH',ROUTCDE=(11,2)
         LA    R15,0
         B     STORERCF
*
NOCORE2  WTO   '--- IEFUJV --- GETMAIN FUER WORKAREA IN SUBPOOL 255 NIC*
               HT ERFOLGREICH',ROUTCDE=(11,2)
         B     RETURN
*
***********************************************************************
*
EXMVC1   MVC   1(0,R2),6(R1) FUER EXECUTE-INSTR.
EXMVC2   MVC   0(0,R1),0(R7) FUER EXECUTE-INSTR.
EXMVC3   MVC   0(0,R1),12(R7) FUER EXECUTE-INSTR.
EXMVC4   MVC   2(0,R1),1(R1) FUER EXECUTE-INSTR.
EXCLC1   CLC   0(0,R1),0(R7) FUER EXECUTE-INSTR.
EXCLC2   CLC   WTIME(0),8(R1) FUER EXECUTE-INSTR.
EXCLC3   CLC   2(0,R5),0(R8) FUER EXECUTE-INSTR.
PACKEN   PACK  WBTIMEPA(8),0(0,R2)
EXTRT    TRT   0(0,R11),TRANSTAB
TESTZAHL TRT   0(0,R2),TRANSTAB
*
***********************************************************************
TIMEKONT ST    R1,WCLASSAD
         ST    R14,WRETURN
         LA    R1,WTIME
         CLI   0(R1),C'('    BEGINNT TIME-PARAMETER MIT '('
         BNE   NURMIOK       NEIN
         LA    R1,1(R1)
         ST    R1,WBTIME     A(BEGINN TIME-PARAMETER HINTER '('
         LA    R2,WTIME+9    ')' IM TIME-
         LA    R7,=C')'      PARAMETER HINTER
         LA    R8,1
         BAL   R14,SEEKLTRL '(' SUCHEN
         LTR   R15,R15      ')' GEFUNDEN ?
         BNZ   CANCEL2      NEIN
         BCTR  R1,0         A(LETZTES ZEICHEN IM TIME-PARAMETER)
         ST    R1,WETIME   = A(')')
         L     R1,WBTIME    KOMMA IM TIME-
         L     R2,WETIME    PARAMETER ZWISCHEN
         LA    R7,=C','     '(' UND
         LA    R8,1         ')' SUCHEN
         BAL   R14,SEEKLTRL
         LTR   R15,R15      KOMMA GEFUNDEN?
         BNZ   NURMIMK      NEIN
         L     R2,WETIME    LAENGE TIME-PARAMETER
         SR    R2,R1        SEKUNDEN NACH
         ST    R2,WLSEK     WLSEK
         L     R2,WBTIME
         SR    R1,R2        LAENGE TIME-PARAMETER
         BCTR  R1,0         MINUTEN NACH
         ST    R1,WLMIN     WLMIN
         B     MULTTIME
NURMIMK  SR    R1,R1        LAENGE SEKUNDEN-PARAMETER
         ST    R1,WLSEK     =0
         L     R1,WBTIME    LAENGE TIME-PARAMETER
         L     R2,WETIME    MINUTEN
         SR    R2,R1        NACH
         ST    R2,WLMIN     WLMIN
         B     MULTTIME
NURMIOK  SR    R1,R1        LAENGE SEKUNDEN-PARAMETER
         ST    R1,WLSEK     =0
         LA    R1,WTIME     1. KOMMA
         ST    R1,WBTIME    SUCHEN
         LA    R2,WTIME+4   NACH
         LA    R7,=C','     'TIME='
         LA    R8,1
         BAL   R14,SEEKLTRL
         LTR   R15,R15
         BNE   SUCHBLAN     1. BLANK SUCHEN IM BEREICH
         LA    R2,0(R1)     ZWISCHEN 'TIME='
         LA    R1,2         UND 1. KOMMA NACH TIME-PARAMETER
         SR    R2,R1
         LA    R1,WTIME
         LA    R7,=C' '
         LA    R8,1
         BAL   R14,SEEKLTRL
         LTR   R15,R15
         BNE   BLANKNO
         BCTR  R1,0
BLANKNO  LA    R2,WTIME     KOMMMA NACH 'TIME=' IST MASSGEBEND
         SR    R1,R2        FUER LAENGE TIME-PARAMETER
         ST    R1,WLMIN
         B     MULTTIME
SUCHBLAN LA    R1,WTIME    KEIN KOMMA GEFUNDEN
         LA    R2,WTIME+4  NACH 'TIME='
         LA    R7,=C' '    BLANK SUCHEN
         LA    R8,1        NACH
         BAL   R14,SEEKLTRL 'TIME='
         LTR   R15,R15
         BNE   SPALTE71
         BCTR  R1,0
         LA    R2,WTIME
         SR    R1,R2
         ST    R1,WLMIN
         B     MULTTIME
SPALTE71 L     R1,WPARLANG  WEDER KOMMA NOCH BLANK GEFUNDEN
         ST    R1,WLMIN     SPALTE 71
MULTTIME SR    R1,R1
         ST    R1,WNORMTIM  WNORMTIM=0
         CL    R1,WLMIN     LAENGE MINUTEN GROESSER NULL?
         BNL   ADDSEK       NEIN
         L     R1,WLMIN      LAENGE TIME-PARAMETER NACH R1
         BCTR  R1,0          FUER PACKEN MINUTEN
         LA    R2,3          LAENGE MINUTEN KLEINER 5?
         CLR   R1,R2
         BH    CANCEL2       NEIN
         L     R2,WBTIME
         EX    R1,TESTZAHL   MINUTEN NUMERISCH CODIERT?
         BC    7,CANCEL2     NEIN
         EX    R1,PACKEN     PACKEN MINUTEN
         CVB   R9,WBTIMEPA    MINUTEN BINAER
         LA    R2,60          UMRECHEN MINUTEN
         MR    R8,R2          IN SEKUNDEN
         ST    R9,WNORMTIM
ADDSEK   SR    R1,R1          LAENGE SEKUNDEN-PARAMETER
         CL    R1,WLSEK       GROESSER 0?
         BE    TIMEENDE       NEIN
         L     R1,WLSEK       LAENGE SEKUNDEN-PARMETER-1 NACH R1
         BCTR  R1,0           FUER ENTPACKEN SEKUNDEN
         LA    R2,1           LAENGE SEKUNDEN KLEINER 3
         CLR   R1,R2
         BH    CANCEL2        NEIN
         L     R7,WBTIME
         L     R8,WLMIN
         LA    R2,1(R7,R8)    R2=A(BEGINN SEKUNDEN)
         EX    R1,TESTZAHL    SEKUNDEN NUMERISCH CODIERT?
         BC    7,CANCEL2      NEIN
         EX    R1,PACKEN      PACKEN SEKUNDEN
         CVB   R9,WBTIMEPA    SEKUNDEN BINAER
         L     R7,WNORMTIM    MINUTEN BINAER
         AR    R7,R9          SEKUNDEN+MINUTEN BINAER
         ST    R7,WNORMTIM    NACH WNORMTIM
TIMEENDE L     R14,WRETURN
         BR    R14
***********************************************************************
         EJECT
***********************************************************************
*
*        VERSCHIEBEN EINES FELDES NACH RECHTS (INNERHALB L=71)
*
*        R1 = ADR. DES 1. ZU VERSCHIEBENDEN ZEICHENS
*        R2 = ADR. DES LETZTEN ZEICHENS IM FELD
*        R5 = ADR. FELD-ANFANG
*        R8 = INCREMENT
*        R14= RUECKSPRUNG-ADR.
*
*        R15= 0 --> VERSCHIEBUNG AUSGEFUEHRT
*        R15= 4 --> INCREMENT GROESSER ALS FREIES FELD AM ENDE
*
*
SHIFTR   LA    R15,71(R5)    ADR. FELD-ENDE+1
         SR    R15,R2        L'FREIES FELD +1
         BNP   LRC4
         CR    R15,R8        INCREMENT > FREIES FELD ?
         BNP   LRC4          JA
         LR    R15,R2
LOOPIC   IC    R0,0(R15)     ZEICHEN VON SPALTE N
         STC   R0,0(R8,R15)  AUF SPALTE N+R8
         BCTR  R15,0         ADR. DAVORSTEHENDES ZEICHEN
         CR    R15,R1        VERSCHIEBUNG VOLLSTAENDIG ?
         BNL   LOOPIC        NEIN
         SR    R15,R15       RETURN CODE 0
         BR    R14
LRC4     LA    R15,4         RETURN CODE 4
         BR    R14
*
***********************************************************************
         EJECT
***********************************************************************
*
*        LITERAL INNERHALB EINES FELDES SUCHEN
*
*        R1 = ANF.ADR IM FELD, AUF DER DAS SUCHEN BEGINNEN SOLL
*        R2 = END ADR IM FELD, AUF DER DAS SUCHEN BEENDET WERDEN SOLL
*        R7 = ADR.DES LITERALS
*        R8 = LAENGE DES LITERALS
*        R14= RUECKSPRUNG-ADR.
*
*        R15=0 --> LITERAL GEFUNDEN --> R1 ZEIGT AUF DIE NAECHSTE ADR.
*        R15=4 --> LITERAL NICHT GEFUNDEN
*
*
SEEKLTRL BCTR  R8,0          LAENGE -1 FUER EX
LOOPCLC  EX    R8,EXCLC1     LITERAL GEFUNDEN ?
         BNE   NOTEQUAL      NEIN
         SR    R15,R15       RETURN CODE 0
         LA    R1,1(R8,R1)   FELD-ADR. HINTER DEM LITERAL NACH R1
         BR    R14           LITERAL GEFUNDEN
NOTEQUAL LA    R1,1(R1)      NACHSTE FELD-ADR. FUER VERGLEICH NACH R1
         CLR   R1,R2         END-ADR. IM FELD UEBERSCHRITTEN ?
         BNH   LOOPCLC       NEIN
NOTFOUND LA    R15,4         RETURN CODE 4
         BR    R14           LITERAL NICHT GEFUNDEN
*
         EJECT
***********************************************************************
*        INDIKATOR FUER DIE VERARB. DES NAECHSTEN JCL-STMNTS. SETZEN
*
CANCEL1  MVI   WMERK1,X'04'  INDIKATOR FUER ERROR-MSG 1
         MVC   WTOLIST+33(56),MSG1+10
         B     MESSAGE
*
CANCEL2  MVI   WMERK1,X'08'  INDIKATOR FUER ERROR-MSG 2
         MVC   WTOLIST+33(56),MSG2+10
         B     MESSAGE
*
CANCEL3  MVI   WMERK1,X'0C'  INDIKATOR FUER ERROR-MSG 3
         MVC   WTOLIST+33(56),MSG3+10
         B     MESSAGE
*
CANCEL4  MVI   WMERK1,X'10'  INDIKATOR FUER ERROR-MSG 4
         MVC   WTOLIST+33(56),MSG4+10
         B     MESSAGE
*
CANCEL5  MVI   WMERK1,X'14'  INDIKATOR FUER ERROR-MSG 5
         MVC   WTOLIST+33(56),MSG5+10
         B     MESSAGE
*
CANCEL6  MVI   WMERK1,X'18'  INDIKATOR FUER ERROR-MSG 6
         MVC   WTOLIST+33(56),MSG6+10
         B     MESSAGE
*
CANCEL7  MVI   WMERK1,X'1C'  INDIKATOR FUER ERROR-MSG 7
         MVC   WTOLIST+33(56),MSG7+10
         B     MESSAGE
*
CANCEL8  MVI   WMERK1,X'20'  INDIKATOR FUER ERROR-MSG 8
         MVC   WTOLIST+33(56),MSG8+10
*
*        WTO IN EXECUTE-FORM
*
MESSAGE  WTO   MF=(E,WTOLIST)        WTOLISTE IN WORKAREA AUSFUEHREN
         B     SETIMEOK              WMERK2 SETZEN ALS INDIKATOR FUER:
*                            KEINE WEITERE VERARBEITUNG DES JOB-STMTS.
*
***********************************************************************
*        FEHLERMELDUNG AUF DAS JCL-STMNT. BRINGEN
*
MVCMSG1  MVC   6(74,R5),MSG1 JCL-STMT. UEBERSCHREIBEN
         B     CANCEL
*
MVCMSG2  MVC   6(74,R5),MSG2 JCL-STMT. UEBERSCHREIBEN
         B     CANCEL
*
MVCMSG3  MVC   6(74,R5),MSG3 JCL-STMT. UEBERSCHREIBEN
         B     CANCEL
*
MVCMSG4  MVC   6(74,R5),MSG4 JCL-STMT. UEBERSCHREIBEN
         B     CANCEL
*
MVCMSG5  MVC   6(74,R5),MSG5 JCL-STMT. UEBERSCHREIBEN
         B     CANCEL
*
MVCMSG6  MVC   6(74,R5),MSG6 JCL-STMT. UEBERSCHREIBEN
         B     CANCEL
*
MVCMSG7  MVC   6(74,R5),MSG7 JCL-STMT. UEBERSCHREIBEN
         B     CANCEL
*
MVCMSG8  MVC   6(74,R5),MSG8 JCL-STMT. UEBERSCHREIBEN
         B     CANCEL
*
***********************************************************************
*        FEHLERMELDUNGEN, DIE AUF DEM NAECHSTEN JCL-STMNT. ERSCHEINEN
*
MSG1     DC    C'  ----->  VORGESCHRIEBENEN TIME-PARAMETER VERWENDEN   *
                             <-----'
MSG2     DC    C'  ----->  FALSCHER TIME-PARAMETER IN DER JOBKARTE     *
                             <-----'
MSG3     DC    C'  ----->  KEIN PLATZ FUER TIME-PARAMETER, JOB-FOLGEKAR*
               TE VERWENDEN  <-----'
MSG4     DC    C'  ----->  KEIN PLATZ FUER TIME-PARAMETER-KORREKTUR    *
                             <-----'
MSG5     DC    C'  ----->  JCL-ERROR IN DER JOBKARTE                   *
                             <-----'
MSG6     DC    C'  ----->  PARAMETER NICHT VOLLSTAENDIG                *
                             <-----'
MSG7     DC    C'  ----->  FEHLER IM ACCOUNT-FELD                      *
                             <-----'
MSG8     DC    C'  ----->  KEIN CLASS-PARAMETER VORHANDEN              *
                             <-----'
*
***********************************************************************
*        WTO IN LISTFORM (FUER JES2-BETRIEB, ERSCHEINEN NICHT BEI JES3)
*
WTO      WTO   '--- IEFUJV --- JOB         :                           *
                                             ',ROUTCDE=11,MF=L
WTOLEN   EQU   *-WTO
*
***********************************************************************
         EJECT
***********************************************************************
*        JOBKLASSEN UND ZUGEHOERIGE TIME-PARAMETER-ADR. -
*        BEI A(0) IST KEIN BESTIMMTER TIME-PAR. VORGESCHRIEBEN.
*
JOBCLASS DS    0F                 REIHENFOLGE EINHALTEN
CLASSA   DC    A(TMEA)            1
CLASSB   DC    A(TMEB)            2
CLASSC   DC    A(0)               3
CLASSD   DC    A(0)               4
CLASSE   DC    A(0)               5
CLASSF   DC    A(0)               6
CLASSG   DC    A(TMEG)            7
CLASSH   DC    A(0)               8
CLASSI   DC    A(0)               9
DUMMY1   DC    7A(0)              10
CLASSJ   DC    A(0)               11
CLASSK   DC    A(0)               12
CLASSL   DC    A(0)               13
CLASSM   DC    A(TMEM)            14
CLASSN   DC    A(TMEN)            15
CLASSO   DC    A(TMEO)            16
CLASSP   DC    A(TMEP)            17
CLASSQ   DC    A(0)               18
CLASSR   DC    A(0)               19
DUMMY2   DC    8A(0)              20
CLASSS   DC    A(TMES)            21
CLASST   DC    A(0)               22
CLASSU   DC    A(0)               23
CLASSV   DC    A(0)               24
CLASSW   DC    A(0)               25
CLASSX   DC    A(TMEX)            26
CLASSY   DC    A(0)               27
CLASSZ   DC    A(0)               28
DUMMY3   DC    6A(0)              29
CLASS0   DC    A(0)               30
CLASS1   DC    A(0)               31
CLASS2   DC    A(0)               32
CLASS3   DC    A(0)               33
CLASS4   DC    A(0)               34
CLASS5   DC    A(0)               35
CLASS6   DC    A(0)               36
CLASS7   DC    A(0)               37
CLASS8   DC    A(0)               38
CLASS9   DC    A(TME9)            39
DUMMY4   DC    6A(0)              40
*
***********************************************************************
*        FUER BESTIMMTE JOBKLASSEN VORGESCHRIEBENE TIME-PARAMETER
*
*        BINAERWERT, LAENGE, TIME-PARAMETER
*
SPEZTIME DS    0F
TMEA     DC    F'120',H'7',C',TIME=2'
TMEB     DC    F'120',H'7',C',TIME=2'
TMEG     DC    F'600',H'8',C',TIME=10'
TMEM     DC    F'600',H'8',C',TIME=10'
TMEN     DC    F'120',H'7',C',TIME=2'
TMEO     DC    F'4',H'10',C',TIME=(,4)'
TMEP     DC    F'600',H'8',C',TIME=10'
TMES     DC    F'4',H'10',C',TIME=(,4)'
TMEX     DC    F'15',H'11',C',TIME=(,15)'
TME9     DC    F'540',H'7',C',TIME=9'
*
***********************************************************************
*        DEFAULT TIME-PARAMETER
*
DEFAULT  DC    F'0',H'7',C',TIME=2'
*
***********************************************************************
*        TABELLE PRUEFEN TIME-PARAMETER AUF ZAHL
TRANSTAB DC    240X'40',10X'00',6X'40'
*
***********************************************************************
         EJECT
***********************************************************************
*        DUMMYSECTION FUER REENTRANT CONVENTION
*
GMAIN    DSECT
SAVEAREA DS    18F
GLEN     EQU   *-GMAIN
*
***********************************************************************
*        DUMMYSECTION FUER WORKAREA (PRO JOB IN CONVERTER-PHASE)
*
WORKAREA DSECT
WAVTIME  DS    F                 A(VORGESCHR. TIME-PAR.)
WATIME   DS    F                 A(TIME-PARAMETER IN DER JOBKARTE)
WTIME    DS    CL10              TIME-PAR. AUS JOBKARTE
WTIMESEQ DS    H                 JCL-KARTE-NR DES TIME-PARAMETERS
WCLAS    DS    CL1               CLASS-PAR. AUS JOBKARTE
WCLASSAD DS    F
WPARANF  DS    F                 A(PAR.-BEREICH-ANFANF)
WPARLANG DS    F
WPARSUCH DS    F
WPAREND  DS    F                 A(PAR.-BEREICH-ENDE)
WSEQ     DS    H                 LFD. NR DER JCL-KARTE
WRETURN  DS    F
*
WMERK1   DS    CL1               X'04' - X'18' ERROR MESSAGES 1-6
WMERK2   DS    CL1               X'04' TIME PAR. ERLEDIGT
WMERK3   DS    CL1               X'04' CLASS-PAR. GEFUNDEN
*                                X'08' TIME-PAR. GEFUNDEN
*                                X'0C' CLASS- UND TIME-PAR. GEFUNDEN
WMERK4   DS    CL1               X'04' NAECHSTE KARTE = FOLGEKARTE
WLMIN    DS    F
WLSEK    DS    F
WBTIME   DS    F
WETIME   DS    F
WBTIMEPA DS    D
WNORMTIM DS    F
WTOLIST  DS    CL(WTOLEN)        PLATZHALTER FUER WTO-LISTFORM
*
WLEN     EQU   *-WORKAREA
*
***********************************************************************
         END
