********************************************************************
* ZUM EINFUEGEN IN EIGENE MACRO-BIBLIOTHEK KOENNEN DIE SCHLAFENDEN *
* 'ADD'-ANWEISUNGEN MIT "CHANGE '*/' './' " AKTIVIERT WERDEN.      *
********************************************************************
*/ ADD NAME=ANF
         MACRO
&NAME    ANF   &ID,&MODE=REUS,&WKSIZE=0,&SP=0,&BASE=12
         GBLC  &DY0ID
         LCLA  &A,&B,&D
         LCLC  &E,&F
* ANF VERSION 2.4 (SUPPORTS PRNT MACRO AND EQUATED WORKSIZE)
         AIF   ((T'&WKSIZE NE 'N') AND (T'&WKSIZE NE 'U')).ERROR4
         AIF   (T'&SP NE 'N').ERROR5
         AIF   (&SP GT 255).ERROR6
         AIF   (T'&BASE NE 'N').ERROR7
         AIF   ((&BASE GT 12) OR (&BASE LT 4)).ERROR8
         AIF   ('&MODE' NE 'REUS').RENT
         AIF   ('&ID' EQ '').NULLID
         AIF   ('&ID' EQ '*').SPECID
&D       SETA  K'&ID
         AIF   (&D GT 8).ERROR1
&A       SETA  ((K'&ID+4)/4)*4+80
&E       SETC  '&ID'
         CNOP  0,4
&NAME    B     &A.(0,15)           BRANCH AROUND ID AND SA
         DC    AL1(&D)             LENGTH OF ID
         DC    CL&D'&E'
         AGO   .CONTA
.NULLID  ANOP
         CNOP  0,4
&NAME    B     80(0,15)            BRANCH AROUND SA
         AGO   .CONTA
.SPECID  AIF   ('&NAME' EQ '').CSECTN
&E       SETC  '&NAME'
&A       SETA  1
.CONTB   AIF   ('&E'(1,&A) EQ '&E').CONTC
&A       SETA  &A+1
         AGO   .CONTB
.CONTC   ANOP
         AIF   ('&MODE' EQ 'RENT').CONTE
&B       SETA  ((&A+4)/4)*4+80
         CNOP  0,4
&NAME    B     &B.(0,15)           BRANCH AROUND ID AND SA
         DC    AL1(&A)             LENGTH OF ID
         DC    CL&A'&E'
         AGO   .CONTA
.CSECTN  AIF   ('&SYSECT' EQ '').ERROR2
&E       SETC  '&SYSECT'
&A       SETA  1
         AGO   .CONTB
.ERROR2  MNOTE 12,'CSECT NAME NOT SPECIFIED'
         AGO   .NULLID
.CONTA   ANOP
&DY0ID   SETC  'REUS'
&F       SETC  '&SYSNDX'
SAVE&F   DC    19F'0'              SAVE AREA
         STM   14,12,12(13)        SAVE CALLER'S REGISTER
         LR    &BASE,15            LOAD BASE
         LA    3,SAVE&F            ADDRESS OF NEW SAVE AREA
         ST    3,8(13)             FORWARD CHAIN
         L     3,32(13)            RESTORE REG 3
         ST    13,SAVE&F.+4        BACKWARD CHAIN
         LA    13,SAVE&F           LOAD SAVE AREA REG
         MEXIT
.ERROR1  MNOTE 12,'IDENTIFICATION FIELD TOO LONG'
         MEXIT
.RENT    ANOP
&DY0ID   SETC  'RENT'
         AIF   ('&MODE' NE 'RENT').ERROR3
         AIF   ('&ID' EQ '').RNULL
         AIF   ('&ID' EQ '*').SPECID
&D       SETA  K'&ID
         AIF   (&D GT 8).ERROR1
&A       SETA  ((&D+2)/2)*2+4
&E       SETC  '&ID'
&NAME    B     &A.(0,15)           BRANCH AROUND ID
         DC    AL1(&D)             LENGTH OF ID
         DC    CL&D'&E'
         STM   14,12,12(13)        SAVE CALLER'S REGISTER
         AGO   .CONTD
.RNULL   ANOP
&NAME    STM   14,12,12(13)        SAVE CALLER'S REGISTER
         AGO   .CONTD
.CONTE   ANOP
&B       SETA  ((&A+2)/2)*2+4
&NAME    B     &B.(0,15)           BRANCH AROUND ID
         DC    AL1(&A)             LENGTH OF ID
         DC    CL&A'&E'
         STM   14,12,12(13)        SAVE CALLER'S REGISTER
.CONTD   ANOP
         LR    &BASE,15            LOAD BASE
         CNOP  0,4                 ALIGN ON FULL WORD BOUNDARY
         BAL   1,*+8               BRANCH AROUND SUBPOOL AND LENGTH
         DC    AL1(&SP)            SUBPOOL ID
         DC    AL3(&WKSIZE+80)     LENGTH VALUE
         L     0,0(0,1)            LOAD SP AND LENGTH
         SVC   10                  ISSUE GETMAIN SVC
         XC    0(80,1),0(1)        CLEAR CHAIN FIELDS AND SAVE AREA
         MVI   0(1),X'FF'          INDICATE LAST ON CHAIN
         MVC   4(4,1),*-20         SAVE LENGTH AN SUBPOOL
         ST    13,12(1)            BACKWARD CHAIN
         LR    15,13               SAVE OLD SAVE AREA POINTER
         LA    13,8(1)             LOAD NEW SA REGISTER
         ST    13,8(15)            FORWARD CHAIN
         LM    15,1,16(15)         RESTORE ALL REGISTER
         MEXIT
.ERROR3  MNOTE 12,'MODE=&MODE INVALID'
         MEXIT
.ERROR4  MNOTE 12,'WKSIZE=&WKSIZE SHOULD BE SELF-DEFINING TERM OR NAME *
               FIELD OF AN ''EQU'''
         MEXIT
.ERROR5  MNOTE 12,'SP=&SP SHOULD BE A SELF DEFINING TERM'
         MEXIT
.ERROR6  MNOTE 12,'SP=&SP GREATER 255 SPECIFIED'
         MEXIT
.ERROR7  MNOTE 12,'BASE=&BASE SHOULD BE A SELF DEFINING TERM'
         MEXIT
.ERROR8  MNOTE 12,'BASE CAN BE ONLY 4 TO 12'
         MEND
*/ ADD NAME=BEG
         MACRO
&NAME    BEG   &ID,&MODE=REUS,&WKSIZE=0,&SP=0,&BASE=12
&NAME    CSECT
         USING *,&BASE
         ANF   &ID,MODE=&MODE,WKSIZE=&WKSIZE,SP=&SP,BASE=&BASE
         MEND
*/ ADD NAME=EINTRR
         MACRO
&NAME    EINTRR &ID,&MODE=REUS,&WKSIZE=0,&SP=0,&BASE=12
         GBLC  &DY0ID
         LCLA  &A,&B,&D
         LCLC  &E,&F,&G
         AIF   ('&DY0ID' EQ '').ERROR11
         AIF   ('&DY0ID' NE 'RENT').ERROR12
         AIF   ((T'&WKSIZE NE 'N') AND (T'&WKSIZE NE 'U')).ERROR4
         AIF   (T'&SP NE 'N').ERROR5
         AIF   (&SP GT 255).ERROR6
         AIF   (T'&BASE NE 'N').ERROR7
         AIF   ((&BASE GT 12) OR (&BASE LT 4)).ERROR8
         AIF   ('&MODE' NE 'RENT').ERROR3
         AIF   ('&ID' EQ '').RNULL
         AIF   ('&ID' EQ '*').RNULL
&D       SETA  K'&ID
         AIF   (&D GT 8).ERROR1
&A       SETA  ((&D+2)/2)*2+4
&E       SETC  '&ID'
         ENTRY &NAME
&NAME    B     &A.(0,15)           BRANCH AROUND ID
         DC    AL1(&D)             LENGTH OF ID
         DC    CL&D'&E'
         STM   14,12,12(13)        SAVE CALLER'S REGISTER
         AGO   .CONTD
.RNULL   ANOP
         ENTRY &NAME
&NAME    STM   14,12,12(13)        SAVE CALLER'S REGISTER
.CONTD   ANOP
         LA    14,&NAME.-&SYSECT
         LR    &BASE,15
         SR    &BASE,14
         CNOP  0,4                 ALIGN ON FULL WORD BOUNDARY
         BAL   1,*+8               BRANCH AROUND SUBPOOL AND LENGTH
         DC    AL1(&SP)            SUBPOOL ID
         DC    AL3(&WKSIZE+80)     LENGTH VALUE
         L     0,0(0,1)            LOAD SP AND LENGTH
         SVC   10                  ISSUE GETMAIN SVC
         XC    0(80,1),0(1)        CLEAR CHAIN FIELDS AND SAVE AREA
         MVI   0(1),X'FF'          INDICATE LAST ON CHAIN
         MVC   4(4,1),*-20         SAVE LENGTH AN SUBPOOL
         ST    13,12(1)            BACKWARD CHAIN
         LR    15,13               SAVE OLD SAVE AREA POINTER
         LA    13,8(1)             LOAD NEW SA REGISTER
         ST    13,8(15)            FORWARD CHAIN
         LM    15,1,16(15)         RESTORE ALL REGISTER
         MEXIT
.ERROR3  MNOTE 12,'MODE=&MODE INVALID'
         MEXIT
.ERROR4  MNOTE 12,'WKSIZE=&WKSIZE SHOULD BE SELF-DEFINING TERM OR NAME *
               FIELD OF AN ''EQU'''
         MEXIT
.ERROR5  MNOTE 12,'SP=&SP SHOULD BE A SELF DEFINING TERM'
         MEXIT
.ERROR6  MNOTE 12,'SP=&SP GREATER 255 SPECIFIED'
         MEXIT
.ERROR7  MNOTE 12,'BASE=&BASE SHOULD BE A SELF DEFINING TERM'
         MEXIT
.ERROR8  MNOTE 12,'BASE CAN BE ONLY 4 TO 12'
         MEXIT
.ERROR11 MNOTE 12,'EINTR NOT PRECEDED BY ANF'
         MEXIT
.ERROR12 MNOTE 12,'PRECEEDING ''ANF'' MACRO DOES NOT SPECIFY ''RENT''.'
         MEND
*/ ADD NAME=IOB
         MACRO
&NAME    IOB   &DCH=N,&CCH=N,&UNRL=Y,&ECB=0,&CCW=0,&DCB=0,&INCR=0
         LCLA  &FLAG
&FLAG    SETA  0
         SPACE 2
*        INPUT OUTPUT BLOCK
         SPACE 2
&NAME    DS    0F
         AIF   ('&DCH' NE 'Y').CONT1
&FLAG    SETA  &FLAG+8
.CONT1   AIF   ('&CCH' NE 'Y').CONT2
&FLAG    SETA  &FLAG+4
.CONT2   ANOP
&FLAG    SETA  &FLAG*16
         AIF   ('&UNRL' NE 'Y').CONT3
&FLAG    SETA  &FLAG+2
.CONT3   ANOP
XIOBFLG1 DC    AL1(&FLAG)          IOBFLAG1
XIOBFLG2 DC    XL1'00'             IOBFLAG2
XIOBSEN0 DC    XL1'00'             IOBSENS0
XIOBSEN1 DC    XL1'00'             IOBSENS1
XIOBECBC DC    XL1'00'             IOBECBCC
XIOBECBP DC    AL3(&ECB)           IOBECBPT
XIOBFLG3 DC    XL1'00'             IOBFLAG3
XIOBCSW  DC    XL7'00'             IOBCSW
XIOBSIOC DC    XL1'00'             IOBSIOCC
XIOBSTAR DC    AL3(&CCW)           IOBSTART
         DC    XL1'00'             RESERVED
XIOBDCBP DC    AL3(&DCB)           IOBDCBPT
XIOBREST DC    XL1'00',AL3(0)      IOBRESTR
XIOBINCA DC    AL2(&INCR)          IOBINCAM
XIOBERRC DC    AL2(0)              IOBERRCT
XIOBSEEK DC    XL8'00'             IOBSEEK
         MEND
*/ ADD NAME=PUTMSG
         MACRO
&NAME    PUTMSG &TEXT,&TPUT=
         GBLB  &FTSW
         GBLC  &XPUT
&GNAME   SETC  'PTM'.'&SYSNDX'
&LAB1    SETC  '&GNAME.A'
&LAB2    SETC  '&GNAME.B'
&LAB3    SETC  '&GNAME.C'
&NAME    B     &LAB1
         AIF (&FTSW).SKIP
&XPUT    SETC  '&TPUT'
         AIF ('&TPUT' NE '').XTPUT
&XPUT    SETC  'PUT'.'&SYSNDX'
.XTPUT   ANOP
&XPUT    TPUT  (1),(0)
         BR    14
&FTSW    SETB  1
         AGO   .SKIP1
.SKIP    ANOP
         AIF ('&TPUT' EQ '').SKIP1
         MNOTE *,'''TPUT'' OPERAND ON NON-FIRST OCCURENCE IGNORED.'
.SKIP1   ANOP
&LAB2    DC    C&TEXT
&LAB3    EQU   *-&LAB2
&LAB1    DS    0H
         LA    0,&LAB3
         LA    1,&LAB2
         BAL   14,&XPUT
         MEND
*/ ADD NAME=REGISTER
         MACRO
         REGISTER
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
*/ ADD NAME=SCHL
         MACRO
&NAME    SCHL  &RET=,&MODE=REUS
         AIF   ('&MODE' NE 'REUS').RENT
&NAME    L     13,4(13)            RELOAD SA-REG
         AIF   ('&RET' NE '').RETCODE
         LM    14,12,12(13)        RELOAD REGISTERS
         BR    14                  RETURN TO CALLER
         MEXIT
.RETCODE AIF   ('&RET'(1,1) EQ '(').REGIST
         AIF   (T'&RET NE 'N').ADDRESS
         LA    15,&RET             LOAD REG 15 WITH RET CODE
         AGO   .FURTHER
.REGIST  AIF   ('&RET(1)' EQ '15').FURTHER
         LR    15,&RET(1)          LOAD REG 15 WITH SPECIF. REGISTER
         AGO   .FURTHER
.ADDRESS L     15,&RET             LOAD REG 15 FROM STORAGE
.FURTHER LM    0,12,20(13)         RESTORE REG 0 TO 12
         L     14,12(13)           RESTORE REG 14
         BR    14                  RETURN TO CALLER
         MEXIT
.RENT    ANOP
&NAME    DS    0H
         AIF   ('&MODE' NE 'RENT').ERROR1
         LA    0,8                 LENGTH OF PREFIX
         LR    1,13                SAVE OLD SA POINTER
         L     13,4(13)            RESTORE REG 13
         SR    1,0                 POINT TO PREFIX
SCH@&SYSNDX DS 0H
         L     14,0(1)             LOAD CHAIN POINTER
         L     0,4(1)              LOAD SUBPOOL AND LENGTH
         LA    1,0(1)              CLEAR HIGH ORDER BYTE
         SVC   10                  ISSUE FREEMAIN SVC
         LA    1,0(14)             GET CHAIN POINTER
         LTR   1,1                 LAST IN CHAIN
         BNZ   SCH@&SYSNDX         NO, BRANCH
         AIF   ('&RET' EQ '').CONTA
         AIF   ('&RET'(1,1) EQ '(').CONTB
         AIF   (T'&RET NE 'N').CONTC
         LA    15,&RET             LOAD RETURN CODE
         AGO   .CONTD
.CONTA   ANOP
         LM    14,12,12(13)        RESTORE REGISTERS
         AGO   .CONTE
.CONTB   ANOP
         LR    15,&RET(1)          LOAD RETURN CODE
         AGO   .CONTD
.CONTC   ANOP
         L     15,&RET             LOAD RETURN CODE
.CONTD   ANOP
         LM    0,12,20(13)         RESTORE REGISTER 0 TO 12
         L     14,12(13)           RESTORE REGISTER 14
.CONTE   ANOP
         BR    14                  RETURN TO CALLER
         MEXIT
.ERROR1  MNOTE 12,'MODE=&MODE INVALID'
         MEND
*/ ADD NAME=SETAMODE
         MACRO
&NAME    SETAMODE &MODE,&REG=1
.*  BLECKWENN, WACKER MUENCHEN DID IT.
         LCLC  &RX
&RX      SETC  '&REG'
&GNAME   SETC  'SET'.'&SYSNDX'
&LAB1    SETC  '&GNAME.A'
&LAB2    SETC  '&GNAME.B'
&LAB3    SETC  '&GNAME.C'
         SPLEVEL TEST
         GBLC  &SYSSPLV
         AIF   (&SYSSPLV NE 2).NONXA
         AIF   (T'&REG NE 'N').REGERR
         AIF   (&REG EQ 0 OR &REG GT 12).REGERR
         AIF   (&MODE EQ 24).MOD24
         AIF   (&MODE EQ 31).MOD31
         MNOTE 16,'MODE-OPERAND WRONG OR UNDEFINED.'
         MEXIT
.REGERR  ANOP
         MNOTE 16,'INVALID REGISTER SPECIFICATION.'
         MEXIT
.MOD24   ANOP
&NAME    LA    &RX,&LAB1           LOAD REG WITHOUT 1ST BIT
         BSM   0,&RX               SET ACCORDING AMODE (24)
&LAB1    DS    0H                  CONTROL COMES BACK HERE
         MEXIT
.MOD31   ANOP
&NAME    L     &RX,&LAB2           LOAD INDIRECT WITH 1ST BIT ON
         BSM   0,&RX               SET ACCORDING AMODE (31)
&LAB2    DC    A(&LAB3+X'80000000')    INDIRECT ADDRESS
&LAB3    DS    0H                  CONTROL COMES BACK HERE
         MEXIT
.NONXA   ANOP
         MNOTE 4,'MACLIB IS NOT ON XA-LEVEL. MACRO IGNORED.'
         MEND
*/ ADD NAME=XUCBGET
         MACRO
&NAME    XUCBGET &WKREG,&ENDUCB,&ERROR
.*
.* (VON BMW MUENCHEN UEBERNOMMEN)
.*
.* DIE ZUEINANDER GEHOERENDEN MACROS
.*       XUCBSCAN  XUCBGET  XUCBOFF
.*    UNTERSTUETZEN DIE ANWENDUNG DER UCBSCAN ROUTINE 'IOSVSUCB'.
.*    UCBSCAN IST AB MVS/SP 1.3 UND MIT MVS/SP 2 (XA) VERFUEGBAR.
.*
.* XUCBSCAN WKREG<,DEVC>
.*          GETMAIN UND INITIALISIERN VON PARM LIST UND WORKBEREICH.
.*          LADEN WKREG UND LOESCHEN DES WORKBEREICH.
.*          XUCBSCAN MUSS VOR XUCBGET ODER XUCBOFF  E I N M A L  AUF-
.*          GERUFEN WORDEN SEIN.
.* XUCBGET WKREG,ENDUCB,ERROR
.*          STELLT PRO AUFRUF DIE ADRESSE EINES UCB IN REG 1 ZUR VER-
.*          FUEGUNG. BEIM ERSTEN AUFRUF NACH XUCBSCAN DEN ERSTEN UCB,
.*          DANACH DEN ZWEITEN, DRITTEN USW.
.* XUCBOFF WKREG
.*          FREEMAIN VON PARM LIST UND WORKBEREICH.
.*          NACH XUCBOFF DARF XUCBGET NICHT MEHR GEGEBEN WERDEN.
.*
.*    WKREG   EIN GP REGISTER. ENTHAELT NACH XUCBSCAN DIE PARM LIST
.*            ADDRESSE. WKREG MUSS FUER DIE SCANDAUER BEI AUFRUF
.*            VON XUCBGET ODER XUCBOFF DIESE ADDRESSE ENTHALTEN.
.*    DEVC    OPTIONAL, BESCHRAENKT UCBSCAN AUF ANGEGEBENEN
.*            UCB DEVICE CODE (X'20'=DASD, X'80'=TAPE  ETC.)
.*    ENDUCB  ANLAUFADRESSE NACH LETZTEM UCB:   IOSVSUCB RC=4
.*    ERROR   ANLAUFADRESSE NACH SCAN FEHLER:   IOSVSUCB RC>4
.*
.* ZUM AUFFINDEN VON IOSVSUCB MUSS CVT ALS DSECT DEFINIERT SEIN:
.*           CVT DSECT=YES
.*
&NAME    LR    1,&WKREG                  ADDR PARM LIST
         USING CVT,15
         L     15,CVTPTR                 ADDR CVT
         L     15,CVTUCBSC               ADDR IOSVSUCB
         DROP  15
         BALR  14,15                     GO TO ROUTINE
         LTR   15,15                     TEST RETURN CODE
         BZ    *+18                       0  - UCB FOUND
         LA    1,4
         CR    15,1
         BNH   &ENDUCB                    4  - END UCB CHAIN
         B     &ERROR                    >4  - ERROR
         L     1,12(&WKREG)              RECEIVED UCB ADDRESS
         MEND
*/ ADD NAME=XUCBOFF
         MACRO
&NAME    XUCBOFF &WKREG
.*
.* (VON BMW MUENCHEN UEBERNOMMEN)
.*
.* DIE ZUEINANDER GEHOERENDEN MACROS
.*       XUCBSCAN  XUCBGET  XUCBOFF
.*    UNTERSTUETZEN DIE ANWENDUNG DER UCBSCAN ROUTINE 'IOSVSUCB'.
.*    UCBSCAN IST AB MVS/SP 1.3 UND MIT MVS/SP 2 (XA) VERFUEGBAR.
.*
.* XUCBSCAN WKREG<,DEVC>
.*          GETMAIN UND INITIALISIERN VON PARM LIST UND WORKBEREICH.
.*          LADEN WKREG UND LOESCHEN DES WORKBEREICH.
.*          XUCBSCAN MUSS VOR XUCBGET ODER XUCBOFF  E I N M A L  AUF-
.*          GERUFEN WORDEN SEIN.
.* XUCBGET WKREG,ENDUCB,ERROR
.*          STELLT PRO AUFRUF DIE ADRESSE EINES UCB IN REG 1 ZUR VER-
.*          FUEGUNG. BEIM ERSTEN AUFRUF NACH XUCBSCAN DEN ERSTEN UCB,
.*          DANACH DEN ZWEITEN, DRITTEN USW.
.* XUCBOFF WKREG
.*          FREEMAIN VON PARM LIST UND WORKBEREICH.
.*          NACH XUCBOFF DARF XUCBGET NICHT MEHR GEGEBEN WERDEN.
.*
.*    WKREG   EIN GP REGISTER. ENTHAELT NACH XUCBSCAN DIE PARM LIST
.*            ADDRESSE. WKREG MUSS FUER DIE SCANDAUER BEI AUFRUF
.*            VON XUCBGET ODER XUCBOFF DIESE ADDRESSE ENTHALTEN.
.*    DEVC    OPTIONAL, BESCHRAENKT UCBSCAN AUF ANGEGEBENEN
.*            UCB DEVICE CODE (X'20'=DASD, X'80'=TAPE  ETC.)
.*    ENDUCB  ANLAUFADRESSE NACH LETZTEM UCB:   IOSVSUCB RC=4
.*    ERROR   ANLAUFADRESSE NACH SCAN FEHLER:   IOSVSUCB RC>4
.*
.* ZUM AUFFINDEN VON IOSVSUCB MUSS CVT ALS DSECT DEFINIERT SEIN:
.*           CVT DSECT=YES
.*
&NAME    LR    1,&WKREG                  ADDR PARM LIST AND WORK AREA
         LA    0,124                     LG PARM LIST AND WORK AREA
         FREEMAIN  R,LV=(0),A=(1)
         MEND
*/ ADD NAME=XUCBSCAN
         MACRO
&NAME    XUCBSCAN &WKREG,&DEVC
.*
.* (VON BMW MUENCHEN UEBERNOMMEN)
.*
.* DIE ZUEINANDER GEHOERENDEN MACROS
.*       XUCBSCAN  XUCBGET  XUCBOFF
.*    UNTERSTUETZEN DIE ANWENDUNG DER UCBSCAN ROUTINE 'IOSVSUCB'.
.*    UCBSCAN IST AB MVS/SP 1.3 UND MIT MVS/SP 2 (XA) VERFUEGBAR.
.*
.* XUCBSCAN WKREG<,DEVC>
.*          GETMAIN UND INITIALISIERN VON PARM LIST UND WORKBEREICH.
.*          LADEN WKREG UND LOESCHEN DES WORKBEREICH.
.*          XUCBSCAN MUSS VOR XUCBGET ODER XUCBOFF  E I N M A L  AUF-
.*          GERUFEN WORDEN SEIN.
.* XUCBGET WKREG,ENDUCB,ERROR
.*          STELLT PRO AUFRUF DIE ADRESSE EINES UCB IN REG 1 ZUR VER-
.*          FUEGUNG. BEIM ERSTEN AUFRUF NACH XUCBSCAN DEN ERSTEN UCB,
.*          DANACH DEN ZWEITEN, DRITTEN USW.
.* XUCBOFF WKREG
.*          FREEMAIN VON PARM LIST UND WORKBEREICH.
.*          NACH XUCBOFF DARF XUCBGET NICHT MEHR GEGEBEN WERDEN.
.*
.*    WKREG   EIN GP REGISTER. ENTHAELT NACH XUCBSCAN DIE PARM LIST
.*            ADDRESSE. WKREG MUSS FUER DIE SCANDAUER BEI AUFRUF
.*            VON XUCBGET ODER XUCBOFF DIESE ADDRESSE ENTHALTEN.
.*    DEVC    OPTIONAL, BESCHRAENKT UCBSCAN AUF ANGEGEBENEN
.*            UCB DEVICE CODE (X'20'=DASD, X'80'=TAPE  ETC.)
.*    ENDUCB  ANLAUFADRESSE NACH LETZTEM UCB:   IOSVSUCB RC=4
.*    ERROR   ANLAUFADRESSE NACH SCAN FEHLER:   IOSVSUCB RC>4
.*
.* ZUM AUFFINDEN VON IOSVSUCB MUSS CVT ALS DSECT DEFINIERT SEIN:
.*           CVT DSECT=YES
.*
&NAME    LA    0,124                     LG PARM LIST AND WORK AREA
         GETMAIN   R,LV=(0)
         LR    &WKREG,1                  ADDR PARM LIST AND WORK AREA
         XC    0(124,&WKREG),0(&WKREG)   CLEAR PARM LIST AND WORK AREA
         LA    1,24(&WKREG)              ADDR WORK AREA -
         ST    1,0(&WKREG)               INTO PARM LIST
         LA    1,19(&WKREG)              ADDR OF DEVICE CODE -
         ST    1,4(&WKREG)               INTO PARM LIST
         LA    1,12(&WKREG)              ADDR OF UCB ADDR TO BE RE-
         ST    1,8(&WKREG)               CEIVED INTO PARM LIST
         MVI   8(&WKREG),X'80'           INDICATE END OF PARM LIST
         AIF   (T'&DEVC NE 'O').A1
         MEXIT
.A1      ANOP
         MVI   19(&WKREG),&DEVC          DEFINED DEVICE CODE
         MEND
