Y004     TITLE 'SATZANZAHL PRO SPUR FUER 3330'
Y004     CSECT
MCST1    B     20(0,15)  BRANCH AROUND ID
         DC    AL1(14)  LENGTH OF IDENTIFIER
         DC    CL8'STARTPUN'  IDENTIFIER
         DC    CL6'KTY004'
         SPACE
         STM   14,12,12(13)  SAVE REGISTERS
         SPACE
*        BASISREGISTER LADEN
         SPACE
         BALR  R9,0
         USING *,R9,R10,R11,R12
MCST2    LM    R10,R12,MCST10
         SPACE
*        SAVEAREA VERKETTUNG
         SPACE
         ST    R13,SAVEAREA+4
         LR    R14,R13
         LA    R13,SAVEAREA
         ST    R13,8(R14)
         B     MCST99
         SPACE
*        EOJ - ROUTINE
         SPACE
MCSTEOJ  L     R13,4(R13)
         L     R15,MCSTRC  LOAD RETURNCODE
         L     R14,12(R13,R0) RESTORE REGISTER 14
         LM    R0,R12,20(R13)  RESTORE THE REGISTERS
         BR    R14  RETURN
MCSTEOJ1 L     R13,4(R13)
         L     R15,MCSTRC  LOAD RETURNCODE
         L     R14,12(R13)
         L     R0,20(R13)
         LM    R2,R12,28(R13)
         BR    R14  RETURN
         SPACE
*        ALLGEMEINE DEFINITION
         SPACE
         DS    0F
MCSTRC   DC    XL3'0' RETURNCODE-FIELD
MCSTRCF  DC    X'0'
MCST10   DC    A(MCST2+4096,MCST2+8192,MCST2+12288)
SAVEAREA DC    18F'0'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
MCST99   EQU   *
A1       LA    R8,HYPPRINT
         OPEN  ((R8),OUTPUT)
A8       LA    R7,KZ10
         BAL   R2,UA2
A4       BAL   R3,UC1
A2       B     C1
A6       AP    DC3,K1
         AP    DA0,K1
         CP    DC3,K26
         BL    A2
         B     B1
A3       CP    DC1,K17
         BE    A7
         CP    DC1,K33
         BNE   A4
         L     R0,VF1
         BCT   R0,D1
         B     A5
A7       MVI   DC0,1
         MVC   KZ3+48(4),KC5
         ZAP   DA0,K20
         B     A4
A5       CLOSE ((R8))
         B     MCSTEOJ
* B      DRUCKFUSS
B1       LA    R7,KZ7
         BAL   R2,UA2
         LA    R7,KZ9
         BAL   R2,UA2
         AP    DC1,K1
         B     A3
* C      SATZANZAHLEN FUER EINE SL UND ALLE BLFK
C1       LA    R4,DA4
         LA    R0,2
         ZAP   DB0,K1
C2       BAL   R2,UB1
         ZAP   1(2,R4),DB14+4(2)
         MVI   0(R4),C' '
         LA    R4,3(R4)
         ZAP   DB0,K5
         BCT   R0,C2
         ZAP   DB0,K10
         LA    R0,16
C3       BAL   R2,UB1
         ZAP   1(2,R4),DB14+4(2)
         MVI   0(R4),C' '
         LA    R4,3(R4)
         AP    DB0,K1
         BCT   R0,C3
         B     CA1
C4       B     CB1
C5       B     A6
* CA     ERMITTLUNG DER KENNZEICHEN X UND Y
CA1      LA    R4,DA4
         LA    R0,18
         ZAP   DC5,K0
         ZAP   DA2,1(2,R4)
CA2      CP    DA2,1(2,R4)
         BNL   CA3
         ZAP   DA2,1(2,R4)
CA3      LA    R4,3(R4)
         BCT   R0,CA2
         LA    R4,DA4
CA4      CP    DA2,1(2,R4)
         BE    CA7
         C     R4,AK1
         BNL   CA6
         CP    1(2,R4),4(2,R4)
         BL    CA5
         CP    1(2,R4),DC5
         BNH   CA5
         ZAP   DC5,1(2,R4)
         MVI   0(R4),C'Y'
CA5      LA    R4,3(R4)
         C     R4,AK1
         BNH   CA4
CA6      B     C4
CA7      MVI   0(R4),C'X'
         B     CA6
* CB     DRUCKAUFBEREITUNG EINER SL-ZEILE
CB1      MVC   KZ8(4),KS1
         ED    KZ8(4),DA0
         MVI   KZ8+5,C'I'
         LA    R0,18
         LA    R4,DA4
         LA    R7,KZ8+6
CB2      MVC   0(4,R7),KS1
         ED    0(4,R7),1(R4)
         MVC   5(1,R7),0(R4)
         LA    R4,3(R4)
         LA    R7,7(R7)
         BCT   R0,CB2
         LA    R7,KZ8
         MVI   KZ8,X'09'
         BAL   R2,UA1
         LA    R7,KZ4
         MVI   KZ4+5,C'I'
         BAL   R2,UA1
         B     C5
* D      GRUNDSTELLUNG
D1       ST    R0,VF1
         MVC   KZ3+48(4),KC7
         ZAP   DA0,K20
         ZAP   DC1,K1
         ZAP   DB0,K1
         MVI   DC0,0
         B     A8
* UA     DRUCKAUSGABE
UA1      PUT   (R8),(R7)
         MVI   1(R7),C' '
         MVC   2(131,R7),1(R7)
         BR    R2
UA2      PUT   (R8),(R7)
         BR    R2
* UB     ERRECHNUNG DER SATZANZAHL
UB1      ZAP   DB2,DA0
         MP    DB2,DB0
         AP    DB2,K135
         CLI   DC0,0
         BE    UB2
         AP    DB2,K58
UB2      ZAP   DB6,K13165
         DP    DB6,DB2
         ZAP   DB14,DB6(4)
         MP    DB14,DB0
         BR    R2
* UC     DRUCKKOPF
UC1      LA    R7,KZ1
         BAL   R2,UA2
         LA    R7,KZ2
         BAL   R2,UA2
         LA    R7,KZ3
         MVC   KZ3+128(4),KS1
         ED    KZ3+128(4),DC1
         BAL   R2,UA2
         LA    R7,KZ2
         BAL   R2,UA2
         LA    R7,KZ1
         MVC   KZ1,X'11'
         BAL   R2,UA2
         MVC   KZ1,X'09'
         LA    R7,KZ4
         LA    R5,KC1
         LA    R6,3
UC2      MVC   4(2,R7),0(R5)
         BAL   R2,UA1
         LA    R5,2(R5)
         BCT   R6,UC2
         MVC   KZ4+4(125),KC4
         BAL   R2,UA1
         MVC   KZ4+2(4),KC6
         BAL   R2,UA1
         LA    R7,KZ7
         BAL   R2,UA2
         ZAP   DC3,K1
         BR    R3
* K      KONSTANTEN
K0       DC    P'0'
K1       DC    P'1'
K2       DC    P'2'
K5       DC    P'5'
K10      DC    P'10'
K17      DC    P'17'
K20      DC    P'20'
K26      DC    P'26'
K33      DC    P'33'
K58      DC    P'58'
K135     DC    P'135'
K13165   DC    P'13165'
KS1      DC    X'40202020'             2P-SCHABLONE
AK1      DC    A(V1+55)                LETZTES FELD IN V1
KC1      DC    C'BI'
KC2      DC    C'LI'
KC3      DC    C'FI'
KC4      DC    C'KI   1      5'
         DC    CL5' '
         DC    C'10'
         DC    CL5' '
         DC    C'11'
         DC    CL5' '
         DC    C'12'
         DC    CL5' '
         DC    C'13'
         DC    CL5' '
         DC    C'14'
         DC    CL5' '
         DC    C'15'
         DC    CL5' '
         DC    C'16'
         DC    CL5' '
         DC    C'17'
         DC    CL5' '
         DC    C'18'
         DC    CL5' '
         DC    C'19'
         DC    CL5' '
         DC    C'20'
         DC    CL5' '
         DC    C'21'
         DC    CL5' '
         DC    C'22'
         DC    CL5' '
         DC    C'23'
         DC    CL5' '
         DC    C'24'
         DC    CL5' '
         DC    C'25'
         DC    CL4' '
KC5      DC    C' MIT'
KC6      DC    C'SL I'
KC7      DC    C'OHNE'
* V      BEREICHE
VF1      DC    F'0'
V1       DS    0CL58
DA0      DC    P'20'
DA2      DC    PL2'0'
DA4      DS    CL54
V2       DS    0CL20
DB0      DC    PL2'1'
DB2      DC    PL4'0'
DB6      DC    PL8'0'
DB14     DC    PL6'0'
V3       DS    0CL5
DC0      DC    X'0'
DC1      DC    PL2'1'
DC3      DC    PL2'1'
DC5      DC    PL2'0'
* KZ     DRUCKZEILEN
KZ1      DC    X'09'
         DC    CL9' '
         DC    58C'*'
         DC    CL65' '
KZ2      DC    X'09'
         DC    CL9' '
         DC    C'*'
         DC    CL56' '
         DC    C'*'
         DC    CL65' '
KZ3      DC    X'09'
         DC    CL9' '
         DC    C'*'
         DC    C'   SATZANZAHL PRO SPUR'
         DC    C' AUF IBM 3330 (OHNE SCHLUESSEL)   *'
         DC    CL55' '
         DC    C'BLATT'
         DC    CL5' '
KZ4      DC    X'09'
         DC    CL132' '
KZ7      DC    X'11'
         DC    132C'-'
KZ8      DC    X'09'
         DC    CL132' '
KZ9      DC    X'89'
         DC    CL9' '
         DC    C'X = MAXIMALE SATZANZAHL'
         DC    CL7' '
         DC    C'Y = GUENSTIGER BLOCKUNGSFAKTOR'
         DC    CL63' '
KZ10     DC    X'89'
         DC    CL132' '
HYPPRINT DCB   DSORG=PS,LRECL=133,MACRF=PM,DDNAME=HYPPRINT,RECFM=FM,   X
               BLKSIZE=133
         END
