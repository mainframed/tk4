BASPRDMP START
         REG
         XSAVE R12,SVA,BASPRDMP
         OPEN  (LOG,(OUTPUT))
         RDJFCB TAPE
         RDJFCB SYSUT2
         NI    DMPDSN+52,X'F7'
         NI    JFCB+52,X'F7'
         OI    DMPDSN+76,X'80'
         OI    JFCB+76,X'80'
NEXTDS   EQU   *
         LOCATE CAMLOC
         LTR   R15,R15
         BNZ   ENDE
         OPEN  (TAPE),TYPE=J
         GET   TAPE
         LR    R3,R1
         CLC   20(60,R3),=CL60' '
         BE    PRT
         MVC   WTO1+25(11),DMPDSN
WTO1     WTO   'PRDMP01 TITLE ON             IS:',ROUTCDE=2,DESC=2
         ST    R1,DOMLST
         MVC   WTO2+16(60),20(R3)
WTO2     WTO   'PRDMP02                                                *
                                              ',ROUTCDE=2,DESC=2
         ST    R1,DOMLST+4
         OI    DOMLST+4,X'80'
RETRY    EQU   *
         XC    ECB,ECB
         WTOR  'PRDMP03 REPLY U TO PRINT, OTHERWISE REPLY CLEAR',      *
               REPLY,5,ECB,ROUTCDE=2
         WAIT  ECB=ECB
         OC    REPLY,=CL5' '
         CLI   REPLY,C'U'
         BNE   TSTCL
         DOM   MSGLIST=DOMLST
         B     PRT
TSTCL    EQU   *
         CLC   REPLY,=CL5'CLEAR'
         BNE   RETRY
         DOM   MSGLIST=DOMLST
         CLOSE TAPE
         OPEN  (TAPE,(OUTPUT)),TYPE=J
CLOSE    EQU   *
         CLOSE TAPE
         B     LEER
PRT      EQU   *
         CLOSE TAPE
         OPEN  (SYSIN,(OUTPUT))
         PUT   SYSIN,REC1
         PUT   SYSIN,REC2
         PUT   SYSIN,REC3
         CLOSE SYSIN
         TIOTSCAN SYSUT
         LTR   R15,R15
         BNZ   OK
ABEND    ABEND 111,DUMP
OK       EQU   *
         LR    R4,R15
         MODESET KEY=ZERO
         MVC   4(8,R4),=CL8'DUMMY'
         MODESET KEY=NZERO
         LA    R1,PARM
         LINK  EP=AMDPRDMP
         TIOTSCAN DUMMY
         LR    R4,R15
         MODESET KEY=ZERO
         MVC   4(8,R4),=CL8'SYSUT2'
         MODESET KEY=NZERO
         OPEN  (SYSIN,(OUTPUT))
         PUT   SYSIN,REC3
         CLOSE SYSIN
         LA    R1,PARM
         LINK  EP=AMDPRDMP
         CLI   MESS1,X'FF'
         BE    NOHEAD
         CALL  TDATE,(MESS1+40,MESS1+29),VL
         PUT   LOG,MESS1
         MVI   MESS1,X'FF'
NOHEAD   EQU   *
         RDJFCB SYSUT2
         MVC   MESS2+1(11),DMPDSN
         MVC   MESS2+23(6),JFCB+118
         PUT   LOG,MESS2
         MVI   MESS2,X'40'
         NI    JFCB+52,X'F7'
         OI    JFCB+76,X'80'
         CLI   JFCB+69,X'00'
         BNE   NZ
         MVI   JFCB+69,X'01'
NZ       EQU   *
         LH    R4,JFCB+68
         LA    R4,1(R4)
         STH   R4,JFCB+68
         OPEN  (SYSUT2,(OUTPUT)),TYPE=J
         CLOSE (SYSUT2,LEAVE)
LEER     EQU   *
         PACK  DBLWD,DMPDSN+9(2)
         AP    DBLWD,=P'1'
         UNPK  DMPDSN+9(2),DBLWD
         OI    DMPDSN+10,X'F0'
         B     NEXTDS
ENDE     EQU   *
         CLI   MESS1,X'FF'
         BNE   NOMSG
         PUT   LOG,MESS3
NOMSG    EQU   *
         CLOSE LOG
         XRETURN 0
CAMLOC   CAMLST NAME,DMPDSN,,AREA
DMPDSN   DC    CL176'SYS1.DUMP00'
DBLWD    DC    D'0'
AREA     DS    CL265
DCBEX1   DS    0F
         DC    X'87'
         DC    AL3(DMPDSN)
DCBEX2   DS    0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
REC1   DC  CL80' O SUMMARY,F,L,Q,CPUDATA,CVTMAP,P S=(000000,007FFF,FF00*
               00,FFFFFF)'
REC2     DC    CL80' G'
REC3     DC    CL80' END'
DOMLST   DC    2F'0'
REPLY    DC    CL5' '
ECB      DC    F'0'
PARM     DC    A(STR)
STR      DC    H'0'
DUMMY    DC    CL8'DUMMY'
SYSUT    DC    CL8'SYSUT2'
MESS1    DC    CL80'1BASPRDMP EXECUTION LOG      XX.XX.19XX/HH.MM.SS'
MESS2    DC    CL80'-SYS1.DUMPXX COPIED TO 123456  (DSN=SYS1.DARDMP)'
MESS3    DC    CL80'-END OF EXECUTION'
         PRINT ON,NOGEN
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=(GL,PM),EXLST=DCBEX1,        *
               EODAD=CLOSE
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(GL,PM),EXLST=DCBEX2
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(PM),LRECL=80,              *
               RECFM=FB,BLKSIZE=240
LOG      DCB   DDNAME=LOG,DSORG=PS,MACRF=(PM),LRECL=80,                *
               RECFM=FA,BLKSIZE=80
         END
