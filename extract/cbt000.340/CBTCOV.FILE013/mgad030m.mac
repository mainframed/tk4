         MACRO
&LAB     DALLO &DA=,&FILE=,&DISP=,&SYSOUT=,&ERROR=,                    *
               &UNIT=,&VOLUME=,&FREE=,&MEMBER=,&QNAME=,&SPACE=,        *
               &FORM=,&OUTLIM=,&COPIES=,&LABEL=,&EXPDT=,&RETPD=,       *
               &FCB=,&HOLD=,&BLKSIZE=,&LRECL=,&RECFM=,&UCS=,&DSORG=,   *
               &USING=,&BFALN=,&BUFNO=,&DEN=,&EROPT=,&KEYLEN=,&OPTCD=
.**********************************************************************
.*                                                                    *
.*       DALLO - MACRO                                                *
.*                                                                    *
.*       LOEBER, W.      22.04.76     GAD - MUENSTER                  *
.*                                                                    *
.*             DISCRIPTION OF PARAMETERS                              *
.*  R E S T   F O L L O W S                                           *
.*                                                                    *
.**********************************************************************
         LCLA  &LGM,&IND1,&IND2,&MULT,&TP,&TU,&FILEPR,&DSNPR
         LCLC  &TAB,&C1,&DV,&TAB1(11),&TAB2(11),&C2
&C2      SETC  'GAD'.'&SYSNDX'
.*
.* FIRST CHECK OPERANDS AND COMPUTE LENGTH FOR WORK-AEREA
&LGM     SETA  24
&POI     SETA  24
         AIF   (T'&DA NE 'O' OR T'&FILE NE 'O').A1002
.A1001   ANOP
         MNOTE 04,'MISSING KEYWORD DA, FILE, OR ERROR'
         MEXIT
.*
.A1002   ANOP
         AIF   (T'&ERROR EQ 'O').A1001
.*
         AIF   (T'&DA EQ 'O').A1009
         AIF   (K'&DA LT 47).A1004
.A1003   ANOP
         MNOTE 04,'INCORRECT ''DA'' VALUE'
         MEXIT
.*
.A1004   ANOP
         AIF   (N'&DA GT 2).A1003
         AIF   (N'&DA EQ 1).A1005
         AIF   (K'&DA(1) GT 1).A1003
         AIF   ('&DA(1)' NE 'R').A1003
         AIF   ('&DA(2)'(1,1) EQ '''').A1003
         AGO   .A1008
.*
.A1005   ANOP
         AIF   ('&DA'(1,1) NE '''').A1008
&IND1    SETA  2
&IND2    SETA  1
.A1006   ANOP
         AIF   ('&DA'(&IND1,1) NE '.').A1007
&IND2    SETA  1
&IND1    SETA  &IND1+1
         AGO   .A1006
.*
.A1007   ANOP
         AIF   ('&DA'(&IND1,1) EQ '''').A1008
         AIF   (&IND2 GT 8).A1003
&IND2    SETA  &IND2+1
&IND1    SETA  &IND1+1
         AGO   .A1006
.*
.A1008   ANOP
&LGM     SETA  &LGM+4+50
&POI     SETA  &POI+4
.A1009   ANOP
         AIF   (T'&FILE EQ 'O').A1013
         AIF   (K'&FILE LT 13).A1011
.A1010   ANOP
         MNOTE 04,'INCORRECT ''FILE'' VALUE'
         MEXIT
.*
.A1011   ANOP
         AIF   (N'&FILE GT 2).A1010
         AIF   (N'&FILE EQ 1).A1012
         AIF   ('&FILE(1)' NE 'R').A1010
         AIF   (K'&FILE(2) GT 8).A1010
.*
.A1012   ANOP
&LGM     SETA  &LGM+4+14
&POI     SETA  &POI+4
.*
.A1013   ANOP
         AIF   (N'&DISP LT 4).A1015
.A1014   ANOP
         MNOTE 04,'INCORRECT ''DISP'' VALUE'
         MEXIT
.*
.A1015   ANOP
         AIF   (T'&DISP(1) EQ 'O').A1016
         AIF   ('&DISP(1)'(1,1) NE '''').A1016
         AIF   ('&DISP(1)' NE '''OLD''' AND '&DISP(1)' NE '''MOD''' AND*
                '&DISP(1)' NE '''NEW''' AND '&DISP(1)' NE '''SHR''').A1*
               014
.A1016   ANOP
         AIF   (T'&DISP(2) EQ 'O').A1017
         AIF   ('&DISP(2)'(1,1) NE '''').A1017
         AIF   ('&DISP(2)' NE '''UNCATLG''' AND '&DISP(2)' NE '''CATLG'*
               '' AND '&DISP(2)' NE '''DELETE''' AND '&DISP(2)' NE '''K*
               EEP''').A1014
.A1017   ANOP
         AIF   (T'&DISP(3) EQ 'O').A1018
         AIF   ('&DISP(3)'(1,1) NE '''').A1018
         AIF   ('&DISP(3)' NE '''UNCATLG''' AND '&DISP(3)' NE '''CATLG'*
               '' AND '&DISP(3)' NE '''DELETE''' AND '&DISP(3)' NE '''K*
               EEP''').A1014
.A1018   ANOP
&MULT    SETA  N'&DISP
&POI     SETA  &POI+4*&MULT
&LGM     SETA  &LGM+(4*&MULT)+(7*&MULT)
.*
.A1019   ANOP
         AIF   (T'&SYSOUT EQ 'O').A1022
         AIF   ('&SYSOUT'(1,1) NE '''').A1021
         AIF   (K'&SYSOUT LT 4).A1021
.A1020   ANOP
         MNOTE 04,'INCORRECT ''SYSOUT'' VALUE'
         MEXIT
.A1021   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.*
.A1022   ANOP
         AIF   (T'&UNIT EQ 'O').A1025
         AIF   ('&UNIT'(1,1) NE '''').A1024
         AIF   (K'&UNIT LT 9).A1024
.A1023   ANOP
         MNOTE 04,'INCORRECT ''UNIT'' VALUE'
         MEXIT
.*
.A1024   ANOP
&LGM     SETA  &LGM+4+12
&POI     SETA  &POI+4
.*
.A1025   ANOP
         AIF   (T'&VOLUME EQ 'O').A1028
         AIF   ('&VOLUME'(1,1) NE '''').A1027
         AIF   (K'&VOLUME LT 9).A1027
.A1026   ANOP
         MNOTE 04,'INCORRECT ''VOLUME'' VALUE'
         MEXIT
.A1027   ANOP
&LGM     SETA  &LGM+4+12
&POI     SETA  &POI+4
.*
.A1028   ANOP
         AIF   (T'&FREE EQ 'O').A1031
         AIF   ('&FREE'(2,5) EQ 'CLOSE').A1030
.A1029   ANOP
         MNOTE 04,'INCORRECT ''FREE'' VALUE'
         MEXIT
.A1030   ANOP
&LGM     SETA  &LGM+4+4
&POI     SETA  &POI+4
.*
.A1031   ANOP
         AIF   (T'&MEMBER EQ 'O').A1034
         AIF   ('&MEMBER'(1,1) NE '''').A1033
         AIF   (K'&MEMBER LT 11).A1033
.A1032   ANOP
         MNOTE 04,'INCORRECT ''MEMBER'' VALUE'
         MEXIT
.A1033   ANOP
&LGM     SETA  &LGM+4+14
&POI     SETA  &POI+4
.*
.A1034   ANOP
         AIF   (T'&QNAME EQ 'O').A1037
         AIF   ('&QNAME'(1,1) NE '''').A1036
         AIF   (K'&QNAME LT 11).A1036
.A1035   ANOP
         MNOTE 04,'INCORRECT ''QNAME'' VALUE'
         MEXIT
.A1036   ANOP
&LGM     SETA  &LGM+4+14
&POI     SETA  &POI+4
.*
.A1037   ANOP
         AIF   (T'&SPACE EQ 'O').A1047
         AIF   ('&SPACE'(1,1) EQ '(').A1039
.A1038   ANOP
         MNOTE 04,'INCORRECT ''SPACE'' VALUE'
         MEXIT
.A1039   ANOP
         AIF   (N'&SPACE GT 5 OR N'&SPACE LT 2).A1038
&LGM     SETA  &LGM+4+9
&POI     SETA  &POI+4
         AIF   (T'&SPACE(2) EQ 'O').A1038
         AIF   (N'&SPACE(2) GT 3).A1038
         AIF   (T'&SPACE(2,1) EQ 'O').A1038
&LGM     SETA  &LGM+4+9
&POI     SETA  &POI+4
         AIF   (T'&SPACE(2,2) EQ 'O').A1040
&LGM     SETA  &LGM+4+9
&POI     SETA  &POI+4
.A1040   ANOP
         AIF   (T'&SPACE(2,3) EQ 'O').A1041
&LGM     SETA  &LGM+4+9
&POI     SETA  &POI+4
.A1041   ANOP
         AIF   (T'&SPACE(3) EQ 'O').A1043
         AIF   ('&SPACE(3)' NE '''RLSE''').A1038
.A1042   ANOP
&LGM     SETA  &LGM+4+4
&POI     SETA  &POI+4
.A1043   ANOP
         AIF   (T'&SPACE(4) EQ 'O').A1045
         AIF   ('&SPACE(4)'(1,1) NE '''').A1044
         AIF   ('&SPACE(4)'(2,6) NE 'CONTIG' AND '&SPACE(4)'(2,4) NE 'M*
               XIG' AND '&SPACE(4)'(2,3) NE 'ALX').A1038
.A1044   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.A1045   ANOP
         AIF   (T'&SPACE(5) EQ 'O').A1047
         AIF   ('&SPACE(5)'(2,5) NE 'ROUND').A1038
.A1046   ANOP
&LGM     SETA  &LGM+4+4
&POI     SETA  &POI+4
.A1047   ANOP
         AIF   (T'&FORM EQ 'O').A1050
         AIF   ('&FORM'(1,1) NE '''').A1049
         AIF   (K'&FORM LT 7).A1049
.A1048   ANOP
         MNOTE 04,'INCORRECT ''FORM'' VALUE'
         MEXIT
.A1049   ANOP
&LGM     SETA  &LGM+4+10
&POI     SETA  &POI+4
.*
.A1050   ANOP
         AIF   (T'&OUTLIM EQ 'O').A1051
&LGM     SETA  &LGM+4+9
&POI     SETA  &POI+4
.*
.A1051   ANOP
         AIF   (T'&COPIES EQ 'O').A1052
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.*
.A1052   ANOP
         AIF   (N'&LABEL LT 5).A1054
.A1053   ANOP
         MNOTE 04,'INCORRECT ''LABEL'' VALUE'
         MEXIT
.A1054   ANOP
         AIF   (T'&LABEL(1) EQ 'O').A1055
&LGM     SETA  &LGM+4+8
&POI     SETA  &POI+4
.*
.A1055   ANOP
         AIF   (T'&LABEL(2) EQ 'O').A1057
         AIF   ('&LABEL(2)'(1,1) NE '''').A1056
         AIF   ('&LABEL(2)'(2,2) NE 'SL' AND '&LABEL(2)'(2,3) NE 'SUL' *
               AND '&LABEL(2)'(2,2) NE 'AL' AND '&LABEL(2)'(2,3) NE 'AU*
               L' AND '&LABEL(2)'(2,3) NE 'NSL' AND '&LABEL(2)'(2,2) NE*
                'NL' AND '&LABEL(2)'(2,3) NE 'BLP' AND '&LABEL(2)'(2,3)*
                NE 'LTM').A1053
.A1056   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.*
.A1057   ANOP
         AIF   (T'&LABEL(3) EQ 'O').A1059
         AIF   ('&LABEL(3)'(1,1) NE '''').A1058
         AIF   ('&LABEL(3)'(2,8) NE 'PASSWORD' AND '&LABEL(3)'(2,8) NE *
               'NOPWREAD').A1053
.A1058   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.*
.A1059   ANOP
         AIF   (T'&LABEL(4) EQ 'O').A1061
         AIF   ('&LABEL(4)'(1,1) NE '''').A1060
         AIF   ('&LABEL(4)'(2,2) NE 'IN' AND '&LABEL(4)'(2,3) NE 'OUT')*
               .A1053
.A1060   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.A1061   ANOP
         AIF   (T'&EXPDT EQ 'O').A1064
         AIF   ('&EXPDT'(1,1) NE '''').A1063
         AIF   (K'&EXPDT EQ 7).A1063
.*
.A1062   ANOP
         MNOTE 04,'INCORRECT ''EXPDT'' OR ''RETPD'' VALUE'
         MEXIT
.A1063   ANOP
&LGM     SETA  &LGM+4+11
&POI     SETA  &POI+4
.A1064   ANOP
         AIF   (T'&RETPD EQ 'O').A1066
         AIF   ('&RETPD'(1,1) NE '''').A1065
         AIF   (K'&RETPD GT 6).A1062
.*
.A1065   ANOP
&LGM     SETA  &LGM+4+8
&POI     SETA  &POI+4
.*
.A1066   ANOP
         AIF   (T'&FCB EQ 'O').A1072
         AIF   (N'&FCB LT 3).A1068
.A1067   ANOP
         MNOTE 04,'INCORRECT ''FCB'' VALUE'
         MEXIT
.A1068   ANOP
         AIF   (T'&FCB(1) EQ 'O').A1070
         AIF   ('&FCB(1)'(1,1) NE 'O').A1069
         AIF   (K'&FCB(1) GT 6).A1067
.*
.A1069   ANOP
&LGM     SETA  &LGM+4+10
&POI     SETA  &POI+4
.A1070   ANOP
         AIF   (T'&FCB(2) EQ 'O').A1072
         AIF   ('&FCB(2)'(1,1) NE '''').A1071
         AIF   ('&FCB(2)'(2,5) NE 'ALIGN' AND '&FCB(2)'(2,6) NE 'VERIFY*
               ').A1067
.A1071   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.A1072   ANOP
         AIF   (T'&HOLD EQ 'O').A1075
         AIF   ('&HOLD' EQ '''YES''').A1074
.A1073   ANOP
         MNOTE 04,'INCORRECT ''HOLD'' VAUE'
         MEXIT
.A1074   ANOP
&LGM     SETA  &LGM+4+4
&POI     SETA  &POI+4
.*
.A1075   ANOP
         AIF   (T'&BLKSIZE EQ 'O').A1076
&LGM     SETA  &LGM+4+8
&POI     SETA  &POI+4
.*
.A1076   ANOP
         AIF   (T'&LRECL EQ 'O').A1077
&LGM     SETA  &LGM+4+8
&POI     SETA  &POI+4
.*
.A1077   ANOP
         AIF   (T'&RECFM EQ 'O').A1083
         AIF   ('&RECFM'(1,1) NE '''').A1082
         AGO   .A1079
.A1078   ANOP
         MNOTE 04,'INCORRECT ''RECFM'' VALUE'
         MEXIT
.*
.A1079   ANOP
&TAB     SETC  'RMAGSBTDVFU'
&IND1    SETA  2
&IND2    SETA  1
.*
.A1080   ANOP
         AIF   ('&RECFM'(&IND1,1) EQ '&TAB'(&IND2,1)).A1081
         AIF   (&IND2 EQ 11).A1078
&IND2    SETA  &IND2+1
         AGO   .A1080
.*
.A1081   ANOP
&IND1    SETA  &IND1+1
         AIF   ('&RECFM'(&IND1,1) EQ '''').A1082
         AIF   ('&RECFM'(&IND1,1) EQ '').A1078
&IND2    SETA  1
         AGO   .A1080
.*
.A1082   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.A1083   ANOP
         AIF   (T'&UCS(1) EQ 'O').A1086
         AIF   ('&UCS(1)'(1,1) NE '''').A1085
         AIF   (K'&UCS(1) LT 7).A1085
.A1084   ANOP
         MNOTE 04,'INCORRECT ''UCS'' VALUE'
         MEXIT
.A1085   ANOP
&LGM     SETA  &LGM+4+10
&POI     SETA  &POI+4
.*
.A1086   ANOP
         AIF   (T'&UCS(2) EQ 'O').A1087
         AIF   ('&UCS(2)'(2,4) NE 'FOLD').A1084
&LGM     SETA  &LGM+4+4
&POI     SETA  &POI+4
.*
.A1087   ANOP
         AIF   (T'&UCS(3) EQ 'O').A1088
         AIF   ('&UCS(3)'(2,5) NE 'VERFY').A1084
&LGM     SETA  &LGM+4+4
&POI     SETA  &POI+4
.*
.A1088   ANOP
         AIF   (T'&USING EQ 'O').A1091
         AIF   ('&USING'(1,1) NE '''').A1090
         AIF   (K'&USING LT 11).A1090
.A1089   ANOP
         MNOTE 04,'INCORRECT ''USING'' VALUE'
         MEXIT
.A1090   ANOP
&LGM     SETA  &LGM+4+14
&POI     SETA  &POI+4
.*
.A1091   ANOP
         AIF   (T'&BFALN EQ 'O').A1093
         AIF   ('&BFALN'(1,1) NE '''').A1092
         AIF   ('&BFALN'(1,3) EQ '''D''' OR '&BFALN'(1,3) EQ '''F''').A*
               1092
         MNOTE 04,'INCORRECT ''BFALN'' VALUE'
         MEXIT
.A1092   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.*
.A1093   ANOP
         AIF   (T'&BUFNO EQ 'O').A1095
         AIF   ('&BUFNO'(1,1) NE '''').A1094
         AIF   (K'&BUFNO LT 4).A1094
         MNOTE 04,'INCORRECT ''BUFNO'' VALUE'
         MEXIT
.A1094   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.*
.A1095   ANOP
         AIF   (T'&DEN EQ 'O').A1099
         AIF   ('&DEN'(1,1) NE '''').A1098
         AIF   (K'&DEN EQ 3).A1097
.A1096   ANOP
         MNOTE 04,'INCORRECT ''DEN'' VALUE'
         MEXIT
.A1097   ANOP
         AIF   ('&DEN'(2,1) NE '0' AND '&DEN'(2,1) NE '1' AND '&DEN'(2,*
               1) NE '2' AND '&DEN'(2,1) NE '3' AND '&DEN'(2,1) NE '4')*
               .A1096
.A1098   ANOP
&LGM     SETA  &LGM+4+7
&POI     SETA  &POI+4
.*
.A1099   ANOP
.************************ READY ********************
.*
.************** NOW GENERATION *********************
.B1000   ANOP
&LGM     SETA  &LGM+8+8
&POI     SETA  &POI+4
.*  GENERATE MACRO-LABEL
         AIF   ('&LAB' EQ '').B1005
&LAB     DS    0H
.*
.B1005   ANOP
.*  G E N E R A T E  INSTRUCTIONS
         GETMAIN R,LV=&LGM
         LR    14,1
         LR    0,1
         LA    14,0(0,14)
         LA    1,&LGM.(,0)
         XR    15,15
         MVCL  0,14
.*
         LA    1,4(,14)
         ST    1,0(,14)
         MVC   4(2,14),=X'1401'
         LA    1,24(,14)
         ST    1,12(,14)
         MVI   0(14),X'80'
.* FILE
&C1      SETC  '(,14)'
&TP      SETA  24
&TU      SETA  &POI
         AIF   (T'&FILE EQ 'O').B1020
* FILE
         LA    1,&TU&C1
         ST    1,&TP&C1
         AIF   (N'&FILE EQ 2).B1015
         AIF   ('&FILE'(1,1) NE '''').B1010
&IND1    SETA  K'&FILE-2
         MVC   &TU.(4,14),=X'00010001'
&TU      SETA  &TU+4
         MVC   &TU.(2,14),=H'&IND1.'
&TU      SETA  &TU+2
         MVC   &TU.(&IND1.,14),=C&FILE
&TU      SETA  &TU+&IND1
&TP      SETA  &TP+4
         AGO   .B1020
.*
.B1010   ANOP
         MVC   &TU.(4,14),=X'00010001'
&TU      SETA  &TU+4
         MVC   &TU.(10,14),&FILE
&TU      SETA  &TU+4
&TP      SETA  &TP+4
         AGO   .B1020
.*
.B1015   ANOP
         MVC   &TU.(14,14),=X'0055000100084040404040404040'
&TU      SETA  &TU+14
&TP      SETA  &TP+4
&FILEPR  SETA  &TU-10
.*
.B1020   ANOP
         AIF   (T'&DA EQ 'O').B1035
* DA
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         AIF   (N'&DA EQ 2).B1030
         MVC   &TU.(4,14),=X'00020001'
&TU      SETA  &TU+4
.*
         AIF   ('&DA'(1,1) NE '''').B1025
&IND1    SETA  K'&DA-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&DA
&TU      SETA  &TU+2+&IND1
         AGO   .B1035
.*
.B1025   ANOP
         MVC   &TU.(46,14),&DA
&TU      SETA  &TU+46
         AGO   .B1035
.*
.B1030   ANOP
         MVC   &TU.(6,14),=X'00560001002C'
         MVI   &TU.+6(14),X'40'
         MVC   &TU.+7(43,14),&TU.+6(14)
&DSNPR   SETA  &TU+4
&TU      SETA  &TU+50
.*
.B1035   ANOP
         AIF   (T'&MEMBER EQ 'O').B1045
* MEMBER
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00030001'
&TU      SETA  &TU+4
         AIF   ('&MEMBER'(1,1) NE '''').B1040
&IND1    SETA  K'&MEMBER-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&MEMBER
&TU      SETA  &TU+2+&IND1
         AGO   .B1045
.*
.B1040   ANOP
         MVC   &TU.(10,14),&MEMBER
&TU      SETA  &TU+10
.*
.B1045   ANOP
         AIF   (T'&DISP(1) EQ 'O').B1060
* DISP(1)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'000400010001'
&TU      SETA  &TU+6
         AIF   ('&DISP(1)'(1,1) NE '''').B1055
&DV      SETC  'X''01'''
         AIF   ('&DISP(1)'(2,3) EQ 'OLD').B1050
&DV      SETC  'X''02'''
         AIF   ('&DISP(1)'(2,3) EQ 'MOD').B1050
&DV      SETC  'X''04'''
         AIF   ('&DISP(1)'(2,3) EQ 'NEW').B1050
&DV      SETC  'X''08'''
.*
.B1050   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1060
.*
.B1055   ANOP
         MVC   &TU.(1,14),&DISP(1)
&TU      SETA  &TU+1
.*
.B1060   ANOP
         AIF   (T'&DISP(2) EQ 'O').B1075
* DISP(2)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'000500010001'
&TU      SETA  &TU+6
         AIF   ('&DISP(2)'(1,1) NE '''').B1070
&DV      SETC  'X''01'''
         AIF   ('&DISP(2)'(2,7) EQ 'UNCATLG').B1065
&DV      SETC  'X''02'''
         AIF   ('&DISP(2)'(2,5) EQ 'CATLG').B1065
&DV      SETC  'X''04'''
         AIF   ('&DISP(2)'(2,6) EQ 'DELETE').B1065
&DV      SETC  'X''08'''
.*
.B1065   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1075
.*
.B1070   ANOP
         MVC   &TU.(1,14),&DISP(2)
&TU      SETA  &TU+1
.*
.B1075   ANOP
         AIF   (T'&DISP(3) EQ 'O').B1090
* DISP(3)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'000600010001'
&TU      SETA  &TU+6
         AIF   ('&DISP(3)'(1,1) NE '''').B1085
&DV      SETC  'X''01'''
         AIF   ('&DISP(3)'(2,7) EQ 'UNCATLG').B1080
&DV      SETC  'X''02'''
         AIF   ('&DISP(3)'(2,5) EQ 'CATLG').B1080
&DV      SETC  'X''04'''
         AIF   ('&DISP(3)'(2,6) EQ 'DELETE').B1080
&DV      SETC  'X''08'''
.*
.B1080   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1090
.*
.B1085   ANOP
         MVC   &TU.(1,14),&DISP(3)
&TU      SETA  &TU+1
.*
.B1090   ANOP
         AIF   (T'&SPACE(1) EQ 'O').B1110
* SPACE(1)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         AIF   ('&SPACE(1)'(1,1) EQ '''').B1095
         MVC   &TU.(9,14),&SPACE(1)
&TU      SETA  &TU+9
         AGO   .B1110
.*
.B1095   ANOP
         AIF   ('&SPACE(1)'(2,3) EQ 'TRK').B1100
         AIF   ('&SPACE(1)'(2,3) EQ 'CYL').B1105
         MVC   &TU.(6,14),=X'000900010003'
         L     1,=F&SPACE(1)
         STCM  1,7,&TU.+6(14)
&TU      SETA  &TU+9
         AGO   .B1110
.*
.B1100   ANOP
         MVC   &TU.(4,14),=X'00070000'
&TU      SETA  &TU+4
         AGO   .B1110
.*
.B1105   ANOP
         MVC   &TU.(4,14),=X'00080000'
&TU      SETA  &TU+4
.*
.B1110   ANOP
         AIF   (T'&SPACE(2,1) EQ 'O').B1120
* SPACE(2,1)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         AIF   ('&SPACE(2,1)'(1,1) NE '''').B1115
         MVC   &TU.(6,14),=X'000A00010003'
         L     1,=F&SPACE(2,1)
         STCM  1,7,&TU.+6(14)
&TU      SETA  &TU+9
         AGO   .B1120
.*
.B1115   ANOP
         MVC   &TU.(6,14),=X'000A00010003'
         MVC   &TU.+6(3,14),&SPACE(2,1).+1
&TU      SETA  &TU+9
.*
.B1120   ANOP
         AIF   (T'&SPACE(2,2) EQ 'O').B1130
* SPACE(2,2)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'000B00010003'
&TU      SETA  &TU+6
         AIF   ('&SPACE(2,2)'(1,1) NE '''').B1125
         L     1,=F&SPACE(2,2)
         STCM  1,7,&TU.(14)
&TU      SETA  &TU+3
         AGO   .B1130
.*
.B1125   ANOP
         MVC   &TU.(3,14),&SPACE(2,2).+1
&TU      SETA  &TU+3
.*
.B1130   ANOP
         AIF   (T'&SPACE(2,3) EQ 'O').B1140
* SPACE(2,3)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'000C00010003'
&TU      SETA  &TU+6
         AIF   ('&SPACE(2,3)'(1,1) NE '''').B1135
         L     1,=F&SPACE(2,3)
         STCM  1,7,&TU.(14)
&TU      SETA  &TU+3
         AGO   .B1140
.*
.B1135   ANOP
         MVC   &TU.(3,14),&SPACE(2,3).+1
&TU      SETA  &TU+3
.*
.B1140   ANOP
         AIF   (T'&SPACE(3) EQ 'O').B1145
* RLSE
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'000D0000'
&TU      SETA  &TU+4
.*
.B1145   ANOP
         AIF   (T'&SPACE(4) EQ 'O').B1160
* CONTIG,MIXG,ALX
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'000E00010001'
&TU      SETA  &TU+6
         AIF   ('&SPACE(4)'(1,1) NE '''').B1155
&DV      SETC  'X''02'''
         AIF   ('&SPACE(4)'(2,3) EQ 'ALX').B1150
&DV      SETC  'X''04'''
         AIF   ('&SPACE(4)'(2,4) EQ 'MIXG').B1150
&DV      SETC  'X''08'''
.*
.B1150   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1160
.*
.B1155   ANOP
         MVC   &TU.(1,14),&SPACE(4)
&TU      SETA  &TU+1
.*
.B1160   ANOP
         AIF   (T'&SPACE(5) EQ 'O').B1165
* ROUND
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'000F0000'
&TU      SETA  &TU+4
.*
.B1165   ANOP
         AIF   (T'&VOLUME EQ 'O').B1175
* VOLUME
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00100001'
&TU      SETA  &TU+4
         AIF   ('&VOLUME'(1,1) NE '''').B1170
&IND1    SETA  K'&VOLUME-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&VOLUME
&TU      SETA  &TU+2+&IND1
         AGO   .B1175
.*
.B1170   ANOP
         MVC   &TU.(8,14),&VOLUME
&TU      SETA  &TU+8
.*
.B1175   ANOP
         AIF   (T'&UNIT EQ 'O').B1185
* UNIT
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00150001'
&TU      SETA  &TU+4
         AIF   ('&UNIT'(1,1) NE '''').B1180
&IND1    SETA  K'&UNIT-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&UNIT
&TU      SETA  &TU+2+&IND1
         AGO   .B1185
.*
.B1180   ANOP
         MVC   &TU.(8,14),&UNIT
&TU      SETA  &TU+8
.*
.B1185   ANOP
         AIF   (T'&SYSOUT EQ 'O').B1200
* SYSOUT
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(2,14),=X'0018'
&TU      SETA  &TU+2
         AIF   ('&SYSOUT'(1,1) NE '''').B1195
         AIF   ('&SYSOUT'(2,1) NE '*').B1190
         MVC   &TU.(2,14),=X'0000'
&TU      SETA  &TU+2
         AGO   .B1200
.*
.B1190   ANOP
         MVC   &TU.(4,14),=X'00010001'
         MVI   &TU.+4(14),C&SYSOUT
&TU      SETA  &TU+5
         AGO   .B1200
.*
.B1195   ANOP
         MVC   &TU.(5,14),&SYSOUT
&TU      SETA  &TU+5
.*
.B1200   ANOP
         AIF   (T'&FORM EQ 'O').B1210
* FORM
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'001A0001'
&TU      SETA  &TU+4
         AIF   ('&FORM'(1,1) NE '''').B1205
&IND1    SETA  K'&FORM-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&FORM
&TU      SETA  &TU+2+&IND1
         AGO   .B1210
.*
.B1205   ANOP
         MVC   &TU.(6,14),&FORM
&TU      SETA  &TU+6
.*
.B1210   ANOP
         AIF   (T'&OUTLIM EQ 'O').B1220
* OUTLIM
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'001B00010003'
&TU      SETA  &TU+6
         AIF   ('&OUTLIM'(1,1) NE '''').B1215
         L     1,=F&OUTLIM
         STCM  1,7,&TU.(14)
&TU      SETA  &TU+3
         AGO   .B1220
.*
.B1215   ANOP
         MVC   &TU.(3,14),&OUTLIM.+1
&TU      SETA  &TU+3
.*
.B1220   ANOP
         AIF   (T'&FREE EQ 'O').B1225
* FREE
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'001C0000'
&TU      SETA  &TU+4
.*
.B1225   ANOP
         AIF   (T'&COPIES EQ 'O').B1235
* COPIES
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'001D00010001'
&TU      SETA  &TU+6
         AIF   ('&COPIES'(1,1) NE '''').B1230
         L     1,=F&COPIES
         STC   1,&TU.(,14)
&TU      SETA  &TU+1
         AGO   .B1235
.*
.B1230   ANOP
         MVC   &TU.(1,14),&COPIES.+3
&TU      SETA  &TU+1
.*
.B1235   ANOP
         AIF   (T'&LABEL(1) EQ 'O').B1245
* LABEL(1)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'001F00010002'
&TU      SETA  &TU+6
         AIF   ('&LABEL(1)'(1,1) NE '''').B1240
         MVC   &TU.(2,14),=H&LABEL(1)
&TU      SETA  &TU+2
         AGO   .B1245
.*
.B1240   ANOP
         MVC   &TU.(2,14),&LABEL(1)
&TU      SETA  &TU+2
.*
.B1245   ANOP
         AIF   (T'&LABEL(2) EQ 'O').B1260
* LABEL(2)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'001E00010001'
&TU      SETA  &TU+6
         AIF   ('&LABEL(2)'(1,1) NE '''').B1255
&DV      SETC  'X''01'''
         AIF   ('&LABEL(2)'(2,2) EQ 'NL').B1250
&DV      SETC  'X''02'''
         AIF   ('&LABEL(2)'(2,2) EQ 'SL').B1250
&DV      SETC  'X''04'''
         AIF   ('&LABEL(2)'(2,3) EQ 'NSL').B1250
&DV      SETC  'X''08'''
         AIF   ('&LABEL(2)'(2,3) EQ 'SUL').B1250
&DV      SETC  'X''10'''
         AIF   ('&LABEL(2)'(2,3) EQ 'BLP').B1250
&DV      SETC  'X''21'''
         AIF   ('&LABEL(2)'(2,3) EQ 'LTM').B1250
&DV      SETC  'X''40'''
         AIF   ('&LABEL(2)'(2,2) EQ 'AL').B1250
&DV      SETC  'X''48'''
.*
.B1250   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1260
.*
.B1255   ANOP
         MVC   &TU.(1,14),&LABEL(2)
&TU      SETA  &TU+1
.*
.B1260   ANOP
         AIF   (T'&LABEL(3) EQ 'O').B1275
* LABEL(3)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'002000010001'
&TU      SETA  &TU+6
         AIF   ('&LABEL(3)'(1,1) NE '''').B1270
&DV      SETC  'X''10'''
         AIF   ('&LABEL(3)'(2,8) EQ 'PASSWORD').B1265
&DV      SETC  'X''30'''
.*
.B1265   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1275
.*
.B1270   ANOP
         MVC   &TU.(1,14),&LABEL(3).+3
&TU      SETA  &TU+1
.*
.B1275   ANOP
         AIF   (T'&LABEL(4) EQ 'O').B1290
* LABEL(4)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'002100010001'
&TU      SETA  &TU+6
         AIF   ('&LABEL(4)'(1,1) NE '''').B1285
&DV      SETC  'X''40'''
         AIF   ('&LABEL(4)'(2,3) EQ 'OUT').B1280
&DV      SETC  'X''80'''
.*
.B1280   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1290
.*
.B1285   ANOP
         MVC   &TU.(1,14),&LABEL(4).+3
&TU      SETA  &TU+1
.*
.B1290   ANOP
         AIF   (T'&EXPDT EQ 'O').B1300
* EXPDT
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'002200010005'
&TU      SETA  &TU+6
         AIF   ('&EXPDT'(1,1) NE '''').B1295
         MVC   &TU.(5,14),=C&EXPDT
&TU      SETA  &TU+5
         AGO   .B1300
.*
.B1295   ANOP
         MVC   &TU.(5,14),&EXPDT
&TU      SETA  &TU+5
.*
.B1300   ANOP
         AIF   (T'&RETPD EQ 'O').B1310
* RETPD
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'002300010002'
&TU      SETA  &TU+6
         AIF   ('&RETPD'(1,1) NE '''').B1305
         MVC   &TU.(2,14),=H&RETPD
&TU      SETA  &TU+2
         AGO   .B1310
.*
.B1305   ANOP
         MVC   &TU.(2,14),&RETPD
&TU      SETA  &TU+2
.*
.B1310   ANOP
         AIF   (T'&FCB(1) EQ 'O').B1320
* FCB(1)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00250001'
         AIF   ('&FCB(1)'(1,1) NE '''').B1315
&IND1    SETA  K'&FCB(1)-2
         MVC   &TU.+4(2,14),=H'&IND1.'
         MVC   &TU.+6(&IND1.,14),=C&FCB(1)
&TU      SETA  &TU+6+&IND1
         AGO   .B1320
.*
.B1315   ANOP
         MVC   &TU.+4(6,14),&FCB(1)
&TU      SETA  &TU+10
.*
.B1320   ANOP
         AIF   (T'&FCB(2) EQ 'O').B1335
* FCB(2)
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'002600010001'
&TU      SETA  &TU+6
         AIF   ('&FCB(2)'(1,1) NE '''').B1330
&DV      SETC  'X''04'''
         AIF   ('&FCB(2)'(2,6) EQ 'VERIFY').B1325
&DV      SETC  'X''08'''
.*
.B1325   ANOP
         MVI   &TU.(14),&DV
&TU      SETA  &TU+1
         AGO   .B1335
.*
.B1330   ANOP
         MVC   &TU.(1,14),&FCB(2)
&TU      SETA  &TU+1
.*
.B1335   ANOP
         AIF   (T'&HOLD EQ 'O').B1340
* HOLD
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00590000'
&TU      SETA  &TU+4
.*
.B1340   ANOP
         AIF   (T'&QNAME EQ 'O').B1350
* QNAME
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(4,14),=X'00270001'
&TU      SETA  &TU+4
         AIF   ('&QNAME'(1,1) NE '''').B1345
&IND1    SETA  K'&QNAME-2
         MVC   &TU.(2,14),=H'&IND1.'
         MVC   &TU.+2(&IND1.,14),=C&QNAME
&TU      SETA  &TU+2+&IND1
         AGO   .B1350
.*
.B1345   ANOP
         MVC   TU.(10,14),&QNAME
&TU      SETA  &TU+10
.*
.B1350   ANOP
         AIF   (T'&LRECL EQ 'O').B1360
* LRECL
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'004200010002'
&TU      SETA  &TU+6
         AIF   ('&LRECL'(1,1) NE '''').B1355
         MVC   &TU.(2,14),=H&LRECL
&TU      SETA  &TU+2
         AGO   .B1360
.*
.B1355   ANOP
         MVC   &TU.(2,14),&LRECL
&TU      SETA  &TU+2
.*
.B1360   ANOP
         AIF   (T'&BLKSIZE EQ 'O').B1370
* BLKSIZE
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(6,14),=X'003000010002'
&TU      SETA  &TU+6
         AIF   ('&BLKSIZE'(1,1) NE '''').B1365
         MVC   &TU.(2,14),=H&BLKSIZE
&TU      SETA  &TU+2
         AGO   .B1370
.*
.B1365   ANOP
         MVC   &TU.(2,14),&BLKSIZE
&TU      SETA  &TU+2
.*
.B1370   ANOP
         AIF   (T'&RECFM EQ 'O').C0000
* RECFM
         LA    1,&TU&C1
         ST    1,&TP&C1
&TP      SETA  &TP+4
         MVC   &TU.(7,14),=X'00490001000100'
&TU      SETA  &TU+6
         AIF   ('&RECFM'(1,1) EQ '''').B1375
         MVC   &TU.(1,14),&RECFM
&TU      SETA  &TU+1
         AGO   .C0000
.*
.B1375   ANOP
&TAB1(01) SETC 'M'
&TAB2(01) SETC 'X''02'''
&TAB1(02) SETC 'R'
&TAB2(02) SETC 'X''02'''
&TAB1(03) SETC 'A'
&TAB2(03) SETC 'X''04'''
&TAB1(04) SETC 'G'
&TAB2(04) SETC 'X''04'''
&TAB1(05) SETC 'S'
&TAB2(05) SETC 'X''08'''
&TAB1(06) SETC 'B'
&TAB2(06) SETC 'X''10'''
&TAB1(07) SETC 'D'
&TAB2(07) SETC 'X''20'''
&TAB1(08) SETC 'T'
&TAB2(08) SETC 'X''20'''
&TAB1(09) SETC 'V'
&TAB2(09) SETC 'X''40'''
&TAB1(10) SETC 'F'
&TAB2(10) SETC 'X''80'''
&TAB1(11) SETC 'U'
&TAB2(11) SETC 'X''C0'''
.*
&IND1    SETA  2
.B1380   ANOP
         AIF   ('&RECFM'(&IND1,1) EQ '''').B1400
&IND2    SETA  1
.B1385   ANOP
         AIF   ('&TAB1(&IND2)' NE '&RECFM'(&IND1,1)).B1395
         OI    &TU.(14),&TAB2(&IND2)
.B1390   ANOP
&IND1    SETA  &IND1+1
         AGO   .B1380
.*
.B1395   ANOP
         AIF   (&IND2 EQ 11).B1390
&IND2    SETA  &IND2+1
         AGO   .B1385
.*
.B1400   ANOP
&TU      SETA  &TU+1
.*******
.C0000   ANOP
*
         LA    1,&TU&C1
         ST    1,&TP&C1
         MVC   &TU.(4,14),=X'00520000'
*
         MVI   &TP.(14),X'80'
* SVC 99
         LR    1,14
         SVC   99
* ERROR-CODES
         XC    &ERROR.(4),&ERROR
         LH    1,8(,14)
         STCM  1,3,&ERROR
         LH    1,10(,14)
         STCM  1,3,&ERROR.+2
.*
         AIF   (T'&FILE EQ 'O').C0005
         AIF   (N'&FILE EQ 1).C0005
* RETURN FILE
         MVI   &FILE(2).,X'40'
         MVC   &FILE(2).+1(9),&FILE(2)
         MVC   &FILE(2).(2),&FILEPR.(14)
         XR    1,1
         ICM   1,3,&FILEPR.(14)
         LTR   1,1
         BZ    &C2.B
         BCTR  1,0
         EX    1,&C2.A
         B     &C2.B
&C2.A    MVC   &FILE(2).+2(0),&FILEPR.+2(14)
&C2.B    DS    0H
.*
.C0005   ANOP
         AIF   (T'&DA EQ 'O').C0010
         AIF   (N'&DA EQ 1).C0010
* RETURN DA
         MVI   &DA(2).,X'40'
         MVC   &DA(2).+1(45),&DA(2)
         MVC   &DA(2).(2),&DSNPR.(14)
         XR    1,1
         ICM   1,3,&DSNPR.(14)
         LTR   1,1
         BZ    &C2.D
         BCTR  1,0
         EX    1,&C2.C
         B     &C2.D
&C2.C    MVC   &DA(2).+2(0),&DSNPR.+2(14)
&C2.D    DS    0H
.*
.C0010   ANOP
* FREEMAIN
         LA    1,&LGM.(,0)
         LTR   15,15
         BNZ   &C2.E
         FREEMAIN R,LV=&LGM.,A=(14)
&C2.E    DS    0H
         MEXIT
         MEND
