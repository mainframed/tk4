         PRINT NOGEN
DASD     CSECT
         ENTRY VOLUME,VOL
         SAVE (14,12)
         LR    BASE,R15
         USING DASD,BASE
         BAL   LNKRG,INIT
         B     DISKGO
*
* COMMON INITIATE ROUTINE
INIT     LR    WA,R1
         GETMAIN R,LV=WKLNGTH
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WKAREA,R13
         XR    UCBL,UCBL
         L     WA,0(WA)           GET COMMAND BUFFER
         LH    WB,0(WA)           GET LENGTH
         LR    WC,WB              GET LENGTH
         S     WC,=F'5'           MINUS RCW - 1
         EX    WC,UPSHIFT         UPSHIFT COMMAND BUFFER
         B     *+10
UPSHIFT  OC    4(0,WA),=CL20' '
         LH    WC,2(WA)           GET OFFSET
         SR    WB,WC
         CH    WB,=H'4'           IS OFFSET AT END
         BE    NOPARMS
         LA    WA,4(WC,WA)        POINT OUT
         BR    LNKRG
NOPARMS  XR    WA,WA              SAY 0
         BR    LNKRG
*
DISKGO   DS    0H
         LTR   WA,WA
         BZ    ALLDASD
         B     SINGLE
ALLDASD  DS    0H
         TPUT  HDR1,LHDR1         PUT OUT HEADING
UCBLOOP  LA    LCNT,7             NO. PER LINE
         LA    WB,OUTA            PTR. FOR FORMATTING LINE
         MVI   OUTA,X'40'
         MVC   OUTA+1(79),OUTA CLEAR OUTAREA
LINETEST BCT LCNT,NEXTDD
         B     DPUTLINE
NEXTDD   DS    0H
         BAL   LNKRG,USCAN        GET DASD UCB
         LTR   UCBPTR,UCBPTR
         BZ    DFINISH            YES
         MVC   0(3,WB),UCBNAME(UCBPTR)  GET DEV ADDR
         MVI   3(WB),C'-'
         MVC   4(6,WB),SRTEVOLI(UCBPTR)  GET VOL SER
         MVI   10(WB),C','
         LA    WB,12(,WB)         +
         B     LINETEST           GOTO NEXT ENTRY
DFINISH  DS    0H
         BCTR  WB,0
         BCTR  WB,0
         LA    WA,OUTA            GET START
         SR    WB,WA              GET LENGTH
         TPUT  OUTA,(WB)
         MVC   OUTA(30),=CL30'NN SPINDLES OFFLINE'
         CVD   OFF,DWORK
         UNPK  OUTA(2),DWORK+6(2)
         OI    OUTA+1,X'F0'
         LA    WB,30
         TPUT  OUTA,(WB)
RETURN   LR    R1,R13
         L     R13,4(R13)
         FREEMAIN R,A=(1),LV=WKLNGTH
         RETURN (14,12),RC=0
* PUT A LINE OUT
DPUTLINE DS    0H
         LA    WA,OUTA
         BCTR  WB,0
         BCTR  WB,0
         SR    WB,WA
         TPUT  OUTA,(WB)
         B     UCBLOOP
* SINGLE ADD PROCESSING
SINGLE   DS    0H
         MVC   DEVADDR,0(WA)
SLOOP    BAL   LNKRG,USCAN
         LTR   UCBPTR,UCBPTR      AT END
         BNZ   SINGON
         TPUT  ERR3,LERR3
         B     RETURN
SINGON   DS    0H
         CLC   DEVADDR,UCBNAME(UCBPTR)
         BE    HITADDR
         B     SLOOP
HITADDR  DS    0H
         MVC   OUTA(80),=CL80'VVVVVV, (REM,NOX), NOT-IN-USE'
         MVC   OUTA(6),SRTEVOLI(UCBPTR)  PUT IN VOLSER
         B     VSTATUS
         EJECT
* VOLUME PROCESSING ENTRY
VOLUME   DS    0H
VOL      DS    0H
         SAVE  (14,12)
         USING VOLUME,R15
         CNOP  0,4
         L     BASE,*+8
         B     *+8
         DC    A(DASD)
         DROP  R15
         BAL   LNKRG,INIT
         LTR   WA,WA
         BNZ   VALVOL
         TPUT  ERR1,LERR1
         B     RETURN
VALVOL   DS    0H
         MVC   VOLSER,0(WA)
VLOOP    BAL   LNKRG,USCAN
         LTR   UCBPTR,UCBPTR
         BNZ   VOLOK
         TPUT  ERR2,LERR2
         B     RETURN
VOLOK    DS    0H
         CLC   VOLSER,SRTEVOLI(UCBPTR)
         BE    HITVOL
         B     VLOOP
HITVOL   DS    0H
         MVC   OUTA(80),=CL80'ON CUU, (REM,NOX), NOT-IN-USE'
         MVC   OUTA+3(3),UCBNAME(UCBPTR)
VSTATUS  DS    0H
         TM    SRTESTAT(UCBPTR),X'08'  ALLOCATED
         BZ    *+10
         MVC   OUTA+19(10),=CL10'ALLOCATED'
         TM    SRTESTAT(UCBPTR),X'20'  RESERVED
         BZ    *+10
         MVC   OUTA+9(3),=C'RES'
         TM    SRTESTAT(UCBPTR),X'04'
         BZ    *+10
         MVC   OUTA+9(3),=C'PVT'
         TM    SRTESTAB(UCBPTR),X'10'  PRIVATE
         BZ    *+10
         MVC   OUTA+13(3),=C'PVT'
         TM    SRTESTAB(UCBPTR),X'08'  PUBLIC
         BZ    *+10
         MVC   OUTA+13(3),=C'PUB'
         TM    SRTESTAB(UCBPTR),X'04'  ?
         BZ    *+10
         MVC   OUTA+13(3),=C'STR'
* PUT IT OUT ...
         LA    WB,30              SET LENGTH
         TPUT  OUTA,(WB)
         B     RETURN
USCAN    DS    0H
         LTR   UCBL,UCBL          FIRST TIME
         BNZ   USCANOK
* IST ENTRY
         L     WA,16
         L     UCBL,CVTILK2(WA)   GET UCBLIST PTR
         SR    OFF,OFF            CLEAR COUNTER
         MVC   LASTADDR,=X'0000'  INIT. POINTER
USCANOK  DS    0H
         LH    UCBPTR,0(UCBL)     GET UCB ADDRESS
         LTR   UCBPTR,UCBPTR
         BZ    UNEXT              DUMMY ENTRY
         BM    UCBEND             END OF LIST
         CH    UCBPTR,LASTADDR    IS IT HIGHER ?
         BL    UNEXT              NO, GET OUT
         STH   UCBPTR,LASTADDR    MOVE IN THIS ADDRESS
         CLI   UCBTYP+2(UCBPTR),X'20'  IS IT DASD ?
         BNE   UNEXT              NO.
         TM    SRTESTAT(UCBPTR),X'80'  IS IT ONLINE ?
         BZ    DOFF               NO.
         CLC   SRTEVOLI(6,UCBPTR),=XL6'00'  VAILS VOLSER
         BNE   UHIT               YES.
DOFF     LA    OFF,1(,OFF)        POINT TO NEXT
UNEXT    DS    0H
         LA    UCBL,2(UCBL)       POINT TO NEXT
         B     USCANOK
UHIT     DS    0H
         LA    UCBL,2(UCBL)       POINT TO NEXT
* VAILD DASD, ONLINE, VOLSER - RETURN
         BR    LNKRG
UCBEND   XR    UCBPTR,UCBPTR      CLEAR
         BR    LNKRG
         LTORG
HDR1     DC    C'DASD DISPLAY(VERS. 16.09.77) -'
LHDR1    EQU   *-HDR1
ERR1     DC    C'VOLSER REQUIRED - RESTART'
LERR1    EQU   *-ERR1
ERR2     DC    C'THAT VOLSER NOT CURRENTLY ONLINE'
LERR2    EQU   *-ERR2
ERR3     DC    C'THAT IS NOT AN ONLINE DASD ADDR'
LERR3    EQU   *-ERR3
UCBTYP   EQU   16
UCBNAME  EQU   13
UCBADDR  EQU   4
SRTESTAT EQU   3
SRTESTAB EQU   34
SRTEVOLI EQU   28
CVTILK2  EQU   40
R1       EQU   1
R13      EQU   13
R15      EQU   15
LNKRG    EQU   14
BASE     EQU   12
WA       EQU   2
WB       EQU   3
WC       EQU   6
UCBL     EQU   4
UCBPTR   EQU   5
LCNT     EQU   7
OFF      EQU   11
WKAREA   DSECT
SAVE     DS    18F
VOLSER   DS    CL6
DEVADDR  DS    CL3
LASTADDR DS    H
DWORK    DS    D
OUTA     DS    CL80
WKLNGTH  EQU   *-WKAREA
         END   DASD
