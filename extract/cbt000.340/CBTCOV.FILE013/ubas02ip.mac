* PROCESS  A, X, STG, AG,M,OF,NEST,MAR(2,72,1),MI('.'),
      LC(49),OPT(TIME)   ;
0  ZS018:  PROC(PAREA1,PAREA2,PAREA3)
                    OPTIONS(COBOL NOMAP)  REORDER;
          PDBD=ADDR(PAREA1);
          PARMDB=O1DBNM;
          IF O1DSKZ='1' THEN
             PDATA=ADDR(PAREA2);
          IF O1SEGKZ='0' THEN
             GOTO ANFANG;
          ELSE IF O1DSKZ='0' THEN
                  PSEGM=ADDR(PAREA2);
               ELSE PSEGM=ADDR(PAREA3);
          GOTO ANFANG;
0  ZS018P:  ENTRY(PAR1,PAR2,PAR3);
          PDBD=PAR1;
          PARMDB=O1DBNM;
          IF O1DSKZ='1' THEN
             PDATA=PAR2;
          IF O1SEGKZ='0' THEN
             GOTO ANFANG;
          ELSE IF O1DSKZ='0' THEN
                  PSEGM=PAR2;
               ELSE PSEGM=PAR3;
-  /*** DBD-BLOCKS *************************************************/
   /*---------------------------------------------------------------*/
   %INCLUDE ZSINC;
   ZSINC(1,2) %INCLUDE ZS01801;
   ZSINC(1,2) %INCLUDE ZS01802;
   ZSINC(1,2) %INCLUDE ZS01803;
   ZSINC(1,4) %INCLUDE ZS057PM;
0  DCL  1 VECTAB         BASED(DBD),    /* VECTOR TABLE POINTERS TO */
          2 PPREF        PTR,           /*   PREFIX                 */
          2 PSEG         PTR,           /*   SEGTAB                 */
          2 PFLD         PTR,           /*   FLDTAB                 */
          2 PLCHLD       PTR,           /*   LCHILDTAB              */
          2 PEXTDBD      PTR,           /*   EXT-DBD-TAB            */
          2 PSORTAB      PTR,           /*   SOURCE SEGM.TAB        */
          2 PDUM2        PTR,           /*                          */
          2 PINDX        PTR,           /*   INDXTAB                */
          2 PDMANX       PTR,           /*   DMANXTAB   /360 ONLY   */
0         DBD            PTR,           /* DBD ADDRESS              */
0       1 OPTIONS        BASED(DBD),    /* SPECIAL OPTIONS          */
          2 DUM1         BIT(1),        /*                          */
          2 DOSCOMP      BIT(1),        /*   DOS COMPAT FOR INDEX   */
0       1 PREFIX         BASED(PPREF)   /* PREFIX ***************** */
                         ALIGNED,       /* ------------------------ */
          2 DBDNAME      CHAR(8),       /*   DBD NAME               */
          2 DUM1         CHAR(2),       /*                          */
          2 NRSEG        BIN FIXED,     /*   NR OF SEGMENTS         */
          2 ACCESSBYTE UNAL,            /*   ACCESS METHOD          */
             5 ACC1      BIT(4),        /*                          */
             5 ACVSAM    BIT(1),        /*     ON=VSAM   OFF=I/OSAM */
             5 ACCESS    BIT(3),        /*     IMS ACCESS METHOD    */
                                        /*   001 = HISAM            */
                                        /*   010 = HISAM            */
                                        /*   111 = INDEX            */
                                        /*   101 = HDAM             */
                                        /*   110 = HIDAM            */
                                        /*100+ACVSAM=INDEX KSDS ONLY*/
          2 NRDSG        BIT(8),        /*   NR OF DATASET GROUPS   */
          2 NREXTDB      BIN FIXED,     /*   NR OF EXT DBDNAMES     */
          2 RMRNAME      CHAR(8),       /*   RAND.MOD. NAME         */
          2 NRLCHLD      BIN FIXED,     /*   NR OF ANCHOR POINTS    */
          2 NRANCHOR     BIN FIXED,     /*   NR OF ANCHOR POINTS    */
          2 RAABLOCKS    BIN FIXED(31), /*   NR OF ROOT-AREA-BLKS   */
          2 DUM3         CHAR(4),       /*                          */
          2 DMANTABS(0:439)             /*   DMAN ENTRIES PER DSG   */
                         CHAR(1),
0       1 PREFIXS (16)   BASED(PPREF),  /* PREFIX FOR SHARED SECIND */
          2 SHARDBD      CHAR(8),       /*   DBDNAMES               */
          2 DUM1         CHAR(28),      /*                          */
0       1 PREFIXLGT      BIN FIXED(31)  /* LENGTH OF PREFIX ENTRY   */
                         INIT(36),      /*                          */
0       1 DMANTAB        BASED(PDMAN)   /* DATA SET GROUP ENTRY *** */
                         ALIGNED,       /* ------------------------ */
          2 DMDDNAME     CHAR(8),       /*   DDNAME                 */
          2 DMDEVICE     CHAR(4),       /*   DEVICE TYPE            */
          2 DMDSG        BIT(8),        /*   DSG NR                 */
          2 DMNRSEG      BIT(8),        /*   NR OF SEGMENTS         */
          2 DUM1         CHAR(10),      /*                          */
          2 DMLRECLI     BIN FIXED,     /*   LRECL PRIM.DATASET     */
          2 DMBLKSI      BIN FIXED,     /*   BLKSIZE PRIM.DATASET   */
          2 DMLRECLO     BIN FIXED,     /*   LRECL OVERFLOW/ DATA   */
          2 DMBLKSO      BIN FIXED,     /*   BLOCKSIZE OVFLO/ DATA  */
          2 DMDDNAME2    CHAR(8),       /*   DDNAME OVERFLOW        */
          2 FREEBL       BIT(8),        /*   FREE BLOCK FACTOR      */
          2 FREESP       BIT(8),        /*   FREE PERCENTAGE /BLOCK */
          2 DUM3         CHAR(4),       /*                          */
0       1 DMANTABLG      BIN FIXED(31)  /* LENGTH OF 1 DMANTABENTRY */
                         INIT(44),      /*   40   FOR  IMS/360      */
                                        /*   44   FOR  IMS/VS       */
          PDMAN          PTR,           /* DMAN ENTRY ADDRESS       */
0       1 DMANXTAB       BASED(PDMANX), /* FREE-SPACE  IMS/360      */
          2 FREEBL360    BIT(8),        /*   FREE BLOCK FACTOR      */
          2 FREESP360    BIT(8),        /*   FREESPACE PERCENTAGE   */
0       1 LCHILD (255)   BASED(PLCHLD)  /* LOG CHILD TABLE ******** */
                         ALIGNED,       /* ------------------------ */
          2 LSEGNAME     CHAR(8),       /*   SEGMENT NAME           */
          2 LFLAG1  UNAL,               /*                          */
             5 BIT0      BIT(1),        /*   0:  LOFFSET TO EXTDBD  */
                              /* 1: LOFFSET TO SEGTAB */   /* GY4304 */
             5 BIT1      BIT(1),        /*   LCHILD INSERT RULE     */
             5 BIT2      BIT(1),        /*   LCHILD INSERT RULE     */
             5 BIT3      BIT(1),        /*   LCHILD INSERT RULE     */
             5 DUM1      BIT(4),        /*                          */
          2 DUM1A        BIT(8),        /*                          */
          2 LOFFSET      BIN FIXED,     /*   OFFSET   (EXTDBD)      */
          2 LFLAG2  UNAL,               /*                          */
             5(BIT0,                    /*                          */
               BIT1,                    /*                          */
               BINDXING,                /*   INDEX PTR TO           */
               BPKEY,                   /*   LP    KEY ON DISK      */
               BLP,                     /*   LP    PTR              */
               BINDXED,                 /*   INDEX PTR FROM         */
               BLCF,                    /*   LCF   PTR TO LCHILD    */
               BLCL    ) BIT(1),        /*   LCL   PTR TO LCHILD    */
          2 DUM2         CHAR(1),       /*                          */
          2 PAIREDOFF    BIN FIXED,     /*   OFFSET TO PAIRED SEG   */
          2 LINDXFLD     CHAR(8),       /*   INDEXED FIELD NAME     */
0       1 LCHILDLG       BIN FIXED(31)  /* LENGTH OF 1 LCHILD ENTRY */
                         INIT(24),      /*                          */
0       1 EXTDBD(255)    BASED(PEXTDBD),/* EXT DATABASE TABLE ***** */
                                        /* ------------------------ */
          2 EXTDBNAME    CHAR(8),       /*   DBDNAME                */
          2 DUM1         CHAR(4),
0       1 EXTDBDLG       BIN FIXED(31)  /* LENGTH OF 1 EXTDBD ENTRY */
                         INIT(12),      /*                          */
0       1 DBDSORTB(509)  BASED(PSORTAB) /* SOURCE SEGM. TABLE       */
                         ALIGNED,       /* ------------------------ */
          2 DBDSORNM     CHAR(8),       /*   SEGMENT NAME           */
          2 DBDSSFLG UNAL,              /*   DATA OPTION:           */
             5(DBDSSK,                  /*      = KEY               */
               DBDSSP,                  /*      = PATH              */
               DBDSSD)   BIT(1),        /*      = DATA              */
             5 DUM1      BIT(5),        /*                          */
          2 DUM2         CHAR(1),       /*                          */
          2 DBDSSDBO     BIN FIXED,     /*   OFFSET TO EXTDBD       */
0       1 DBDSORSZ       BIN FIXED(31)  /* LENGTH OF 1 SOURCE.SEGM. */
                         INIT(12),      /*        ENTRY             */
0       1 XDFLD (1000)   BASED(PFLD)    /* FIELD TABLE - XDFLD/DFLD */
                         ALIGNED,       /* ------------------------ */
          2 FLDNAME      CHAR(8),       /*   FIELDNAME              */
          2 OFFSINDX     BIN FIXED,     /*   OFFSET TO INDX-TAB     */
          2 FFLAG   UNAL,               /*                          */
             5(BIT0,                    /*                          */
               BSEQFLD,                 /*   KEYFIELD DEF (DFLD)    */
               BNOUNI,                  /*   NON-UNIQUE KEYFIELD    */
               BXDFLD,                  /*   XDFLD-ENTRY (SEC.INDX) */
               BCONST,                  /*   CONSTANT PRESENT       */
               BIT5,                    /*                          */
               BPACK,                   /*   PACKED                 */
                                        /*   HEX+PACKED=CHARACTER   */
               BHEX)     BIT(1),        /*   HEX                    */
          2 FLGTH1       BIT(8),        /*   LENGTH - 1             */
          2 INDSOSEG     CHAR(8),       /*   INDEX SOURCE SEGNAME   */
          2 DUM1         CHAR(4),       /*                          */
0       1 XDFLDLG        BIN FIXED(31)  /* LENGTH OF 1 XDFLD-ENTRY  */
                         INIT(24),
0       1 INDXT          BASED(PINDX)   /* INDX TABLE ************* */
                         ALIGNED,       /* ------------------------ */
          2 DUM0         CHAR(4),       /*                          */
          2 INDXTT(1000),               /*   REP. TABLE             */
           3 DUM1        CHAR(12),      /*                          */
           3 IFLAG       BIT(8),        /*                          */
           3 CONST       CHAR(1),       /*   CONSTANTBYTE (SECINDX) */
           3 DUM2        CHAR(2),       /*                          */
0       1 INDXTLG        BIN FIXED(31)  /* LENGTH OF 1 INDX-ENTRY   */
                         INIT(16),
0       1 SEGTABS(0:12239)    CHAR(1)   /* SEGMENT TABLE STRING     */
                         BASED(PSEG),
0       1 SEGTAB         BASED(PSEGT)   /* SEGMENT TABLE ********** */
                         ALIGNED,       /* ------------------------ */
          2 DSGS         BIT(8),        /*   DATA SET               */
          2 SEGCOD       BIT(8),        /*   SEGMENT CODE           */
          2 PARENT       BIT(8),        /*   PARENT SEGMENT CODE    */
          2 DUM1         CHAR(1),       /*                          */
          2 NRLC         BIT(8),        /*   NR OF LCHILD ENTRIES   */
          2 NRFLD        BIT(8),        /*   NR OF FIELD ENTRIES    */
          2 SEGLGMAX     BIN FIXED,     /*   SEGMENT LGTH DATA MAX  */
          2 DUM3         CHAR(4),       /*                          */
          2 SEGNAME      CHAR(8),       /*   NAME                   */
          2 FLAG1   UNAL,               /*   POINTERS               */
             5(BCTR,                    /*        CTR               */
               BT,                      /*        TWIN              */
               BTB,                     /*        TWIN BACK         */
               BPP,                     /*        PHYS PARENT       */
               BLT ,                    /*        LOG TWIN          */
               BLTB,                    /*        LOG TWIN BACK     */
               BLP,                     /*        LOG PARENT        */
               BH      ) BIT(1),        /*        HIERARCH          */
          2 FLAG2   UNAL,               /*                          */
             5 (B1,                     /*   SEGMENT UPDATE RULES   */
                B2,                     /*                          */
                B3,                     /*                          */
                B4,                     /*                          */
                B5,                     /*                          */
                B6,                     /*                          */
                B7,                     /*                          */
                B8)      BIT(1),        /*                          */
          2 FLAG3   UNAL,               /*                          */
             5 BPAIRED   BIT(1),        /*   SEG IS PAIRED          */
             5 DUM4      BIT(3),        /*                          */
             5 BDBLE     BIT(1),        /*   PCF + PCL TO THIS SEG  */
             5 DUM5      BIT(3),        /*                       G  */
          2 NRPCHLD      BIT(8),        /* NR PHYS.CH. DIRECT POINT */
          2 OFFSLCH      BIN FIXED(31), /*   OFFSET TO LCHLD ENTRY  */
          2 DBDSSN       BIN FIXED,     /*   NO SOURCE SEGM. ENTRIES*/
          2 DBDSSOFF     BIN FIXED,     /*   OFFSET SOURCE SEGM. TAB*/
          2 OFFSFLD      BIN FIXED(31), /*   OFFSET TO FLDTAB       */
          2 PREFIXLG     BIN FIXED,     /*   SEGMENT PREFIX LENGTH  */
          2 SEGLGMIN     BIN FIXED,     /*   SEGMENT LGTH DATA MIN  */
          2 DUM7         CHAR(4),       /*                          */
          2 FLAG5  UNAL,                /*                          */
             5 DUM8      BIT(4),                           /* GY4311 */
             5 CPSEG     BIT(1),        /* ON = COMPACT */ /* GY4311 */
             5 VLSEG     BIT(1),        /*   ON = VAR LENGTH SEG    */
             5 DUM9      BIT(2),        /*                          */
          2 DUM10        CHAR(3),       /*                          */
0       1 SEGTABLG       BIN FIXED(31)  /* LENGTH OF 1 SEGTAB-ENTRY */
                         INIT(48),      /*   40   FOR  IMS/360      */
                                        /*   48   FOR  IMS/VS       */
          PSEGT          PTR;           /* ADDRESS OF 1 SEGTAB ENTR */
0  DCL    1 LAREA1       BIN FIXED  STATIC INIT(32),
          1 LAREA2       BIN FIXED  STATIC INIT(14),
          1 LAREA3       BIN FIXED  STATIC INIT(34);
0  DCL    (IFLD,GFLD,
           IFELD)        BIN FIXED STATIC INIT(0);
0  DCL    (PAR1,PDATA,
          PAR2,PAR3,
          PSEGM,PDBD)    PTR,           /* ZEIGER                   */
          (IDAT,ISEG)    BIN FIXED,     /* LOOP VARIABLES           */
          PARMDB         CHAR(8),
          PAREA1         CHAR(1),
          PAREA2         CHAR(1),
          PAREA3         CHAR(1);
0  DCL   (NULL,ADDR,
          MOD)           BUILTIN;
0  DCL    PARMFUNC       CHAR(4)  INIT('PARM');
0  DCL    ZS057          ENTRY OPTIONS(ASM INTER);
0  DCL    LUDUMP         ENTRY OPTIONS(ASM INTER);
0  DCL    PLIXOPT    CHAR(50) VAR INIT('NR,ISA(6K),NOSTAE,NOSPIE')
                         STATIC EXTERNAL;
-
-  /*---------------------------------------------------------------*/
   /******     S T A R T      E X E C U T I O N    ******************/
   /*---------------------------------------------------------------*/
0  ANFANG:
        ON ERROR BEGIN;
          CALL LUDUMP;
        END;
0  /*** FELDER INITIALISIERUNG **************************************/
          LAREA1=32;
          LAREA2=14;
          LAREA3=34;
          O1RC=0;
0  /*** LOAD DBD CONTROL BLOCK **************************************/
0         PARMBL.UP_NAME   = PARMDB;                 /* DBDNAME     */
          PARMBL.FUNC      = 'LOAD';                 /* FUNC = LOAD */
          PARMBL.DD_NAME   = 'DBD     ';             /* DDNAME      */
          PARMBL.RC        = 0;                      /* RETURNCODE  */
          PARMBL.UP_LAE    = 0;                      /* MODULLAENGE */
          CALL ZS057(PARMBL,PARMFUNC);          /* LADEN DBD-MODUL  */
          SELECT(PARMBL.RC);                    /* PRUEFEN RET-CODE */
             WHEN(0,4);
             WHEN(16) DO;
                       O1RC = 5;                /* DD-KARTE FEHLT   */
                       GOTO ENDE;
                      END;
           OTHERWISE DO;
                      O1RC = 4;                 /* DBD-MODUL NICHT  */
                      GOTO ENDE;                /* GELADEN          */
                     END;
          END;
          DBD    = PARMBL.UP_LOAD_PTR;          /* ADDR DBD-MODUL   */
          O1DBNM = PARMDB;
0  /*** AUFBAU OUT1                                     *************/
   /*---------------------------------------------------------------*/
          IF LAREA1 > O1LN THEN        /*   LAENGE AUSREICHEND      */
             DO;
               O1RC=1;
               GOTO ENDOFDBD;
             END;
0         IF ACVSAM THEN               /*** VSAM IS ACCESS METHOD ***/
               O1ZGRM='1';
          ELSE O1ZGRM='0';
0         O1RMR='';       /* RANDOM.ROUT.*/
          O1APNO=0;      /* ANKERPUNKTE    */
          O1RAANO=0;     /* ROUT-ADR.   */
          IF ACCESS='101'B THEN        /* HDAM           */
             DO;
               O1ORGF='0';
               O1RMR=RMRNAME;      /* RAND. ROUT. */
               O1APNO=NRANCHOR;    /* ANKERPUNKTE */
               O1RAANO=RAABLOCKS;  /* ROUT-ADR.   */
             END;
          ELSE IF ACCESS='110'B THEN     /* HIDAM         */
                  O1ORGF='1';
               ELSE IF ACCESS='111'B × ACCESS='100'B THEN  /*INDEX */
                       O1ORGF='2';
                    ELSE IF ACCESS='001'B × ACCESS='010'B THEN
                            O1ORGF='3';
          O1DSNO=NRDSG;                 /* ANZAHL DATASETS*/
          IFLD=0;
          IF O1DSKZ='0' THEN
             GOTO SEGABFR;
0  /*** LOOP UEBER ALLE DATASETS                        *************/
   /*---------------------------------------------------------------*/
          LAREA2=LAREA2*NRDSG+2;       /*   + LAENGENFELD           */
          IF LAREA2 > O2DSLN THEN      /*   LAENGE AUSREICHEND      */
             DO;
               O1RC=2;
               GOTO ENDOFDBD;
             END;
0         DO IDAT=1 TO NRDSG;
             PDMAN=ADDR(DMANTABS((IDAT-1)*DMANTABLG));
             O2DDN(IDAT)=DMDDNAME;
             O2BLKSZ(IDAT)=DMBLKSI;
             O2LRECL(IDAT)=DMLRECLI;
             O2SEGNO(IDAT)=DMNRSEG;
          END;                    /*LOOP-ENDE DATASETS  */
0  SEGABFR:
          IF O1SEGKZ='0' THEN
             GOTO ENDOFDBD;
0  /*** LOOP UEBER ALLE SEGMENTE                        *************/
   /*---------------------------------------------------------------*/
          LAREA3=LAREA3*NRSEG+2;       /*   + LAENGE LAENGENANGABE  */
          IF LAREA3 > O3SGLN THEN      /*   LAENGE AUSREICHEND      */
             DO;
               O1RC=3;
               GOTO ENDOFDBD;
             END;
             PSEGT=ADDR(SEGTABS(0));
             IFLD=1;
             GFLD=0;
0         DO ISEG=1 TO NRSEG;
             PSEGT=ADDR(SEGTABS((ISEG-1)*SEGTABLG));
             O3SEGNM(ISEG)=SEGNAME;
             O3SDSNO(ISEG)=DSGS;
             O3SEGCD(ISEG)=SEGCOD;
             O3PSGCD(ISEG)=PARENT;
             O3SLMAX(ISEG)=SEGLGMAX;
             IF O1ORGF='2' & ACVSAM & ^DOSCOMP & MOD(PREFIXLG,2)=0
                THEN PREFIXLG=PREFIXLG-1;
             O3SLPRX(ISEG)=PREFIXLG;
             IF SEGLGMIN=0 THEN
                O3SLMIN(ISEG)=SEGLGMAX;
               ELSE
                O3SLMIN(ISEG)=SEGLGMIN;
             O3FLDNO(ISEG)=NRFLD;
             IF NRFLD=0 THEN
                DO;
                  O3KEYNM(ISEG)='';
                  O3KEYLN(ISEG)=0;
                  O3KEYOF(ISEG)=0;
                END;
             ELSE DO;
                    GFLD=GFLD+NRFLD;
                    DO IFELD=IFLD TO GFLD;
                     IF BSEQFLD(IFELD) THEN
                        LEAVE;
                    END;
                    IF IFELD > GFLD THEN
                     DO;
                      O3KEYNM(ISEG)=' ';
                      O3KEYLN(ISEG)=0;
                      O3KEYOF(ISEG)=0;
                     END;
                    ELSE
                     DO;
                      O3KEYNM(ISEG)=FLDNAME(IFELD);
                      O3KEYLN(ISEG)=FLGTH1(IFELD)+1;
                      O3KEYOF(ISEG)=OFFSINDX(IFELD)+1;
                     END;
                    IFLD=IFLD+NRFLD;
                  END;
          END;                 /*LOOP-ENDE SEGMENTE  */
0  ENDOFDBD:
          PARMBL.FUNC = 'DLET';                 /* DELETE DBD-MODUL */
          CALL ZS057(PARMBL,PARMFUNC);
          IF PARMBL.RC ^= 0 THEN
             O1RC = 6;
0  ENDE:
-  END    ZS018;
