* STAND 01/12/78
***********************************************************************
*                                                                     *
*    PROGRAMM  = PUTASUM                                              *
*                                                                     *
*    AUFRUF    = CALL PUTASUM,(ANR,BZT,WERT)                          *
*        CL10    ANR  = ABSTIMMSUMMEN-NR, FUER DIE NEUE PERIODENSUMME *
*                       AN SATZ ANZUFUEGEN IST                        *
*        CL15    BZT  = BERICHTSZEIT                                  *
*        PL10    WERT = ABSTIMMSUMME                                  *
*                RETURNCODE IM REGISTER 15                            *
*                     = 0 OK                                          *
*                     = 1 ABST.SUM.SATZ NICHT AUF DATEI               *
*                     = 3 DD-KARTE FEHLT                              *
*                     = 4 I/O-FEHLER BEIM LESEN                       *
*                     = 5 I/O-FEHLER BEIM SCHREIBEN                   *
*                     = 6 DD-KARTE ASTLOG FEHLT                       *
*    ZWECK     = ABSTIMMSUMME FUER NEUE PERIODE AN EXISTIERENDEM      *
*                ABST.SUM.SATZ ANFUEGEN UND AELTESTE PERIODE ENT=     *
*                FERNEN                                               *
*
*        LOGDATEI  DAS SCHREIBEN IN 'ASTSUM' IST NUR ERLAUBT,
*              WENN DIE DATEI 'ASTLOG' ZUGEORDNET IST.
*              SIE IST EINE ZENTRALE DATEI, IN DIE ALLE
*              VERAENDERUNGEN AUF 'ASTSUM' MIT MOD ANGEHAENGT
*              WERDEN.
*
*              BEFINDET SICH 'ASTSUM' AUF EINEM SHARED DEVICE UND WIRD
*              VON MEHREREN SYSTEMEN AUF SIE ZUGEGRIFFEN,
*              MUSS FUER JEDES SYSTEM EINE EIGENE LOGDATEI
*              ANGELEGT WERDEN.
*              UM EINDEUTIGE NAMEN FUER 'ASTLOG' ZU HABEN,
*              FALLS DIESE EBENFALLS AUF SHARED DEVICE LIEGEN,
*              FUEGT 'PUTASUM' AN DEN DSNAMEN LAUT DDKARTE
*              EINE 2-STELLIGE SYSTEM-ID (AUS SMCA) AN.
*
*              DAS ERWEITERN DES DSNAMES UNTERBLEIBT,
*              WENN IN 'ASWITCH' NICHT 'LOGID' GESETZT.
*              (SETZTEN ASWITCH IN PRUEFUPD)
***********************************************************************
PUTASUM  CSECT
         XSAVE 12,SAVE,PUTASUM
         AREG
         SPACE 2
         USING DATAB,R7
         L     R7,PATAB                --> ATAB
         LR    R11,R1                   AUFRUFLISTE ZWISCHENSPEICHERN
         XC    ERR,ERR
         USING DASATZ,R9
*  IM JFCB VON ASTLOG DISP=SHR DURCH DISP=MOD ERGAENZEN
         TM    JFCB,X'FF'
         BNO   OPNLOG                  NICHT 1.AUFRUF
         RDJFCB (ASTLOG,(OUTPUT))
         NI    JFCB+87,X'0F'
         OI    JFCB+87,X'80'           MOD-BIT
         OI    JFCB+52,X'08'           NICHT IN JOBQUEUE ZURUECKSCHR.
**************************************
         TM    ASWITCH,ALOGID
         BZ    OPNLOG         SYSTEM-ID NICHT ANFUEGEN
**************************************
         L     R4,16                    -->CVT
        L     R4,196(R4)                -->SMCA
*  SYSTEM-IDENTIFICATION ALS ERWEITERUNG DES DSNAME VON ASTLOG
*  ANFUEGEN Z.B. SYS1.ASTLOG MASCH 02 --> SYS1.ASTLOG02
         LA    R5,JFCB
         LA    R6,40          MAX LAENGE DSNAME
LPX      EQU   *  ENDE DES ASTLOG DSNAME SUCHEN
         CLI   0(R5),C' '
         BE    LPXE
         LA    R5,1(R5)
         BCT   R6,LPX
LPXE     EQU   *  ENDE DES DSNAME
         MVC   0(2,R5),16(R4)    SYSTEM-ID ANHAENGEN
OPNLOG   EQU   *                .
         CALL  CENQAST,(KE)
         OPEN  (ASTLOG,(OUTPUT)),TYPE=J  LOGBAND
         TM    ASTLOG+48,X'10'
         BNZ   LOGOK1                  OPENED
         MVC   ERR,=F'6'               DD-KARTE ASTLOG FEHLT -->
         BAL   R10,DEQ                 KEIN UPDATE MOEGLICH
         B     RETURN
LOGOK1   EQU   *
*  OPEN  ASTSUM
         L     R9,PDCBAST               --> DCB FUER ASTSUM
         OPEN  ((R9))
         TM    48(R9),X'10'
         BNZ   DCBOK
         MVC   ERR,=F'3'                DD-KARTE FEHLT
         BAL   R10,CLSLOG               DEQUEUE
         B     RETURN
DCBOK    EQU   *
* SATZ VOM ASTSUM-DATEI LESEN LAUT ABSTIMMSUM-NR.
         L     R3,0(R11)                --> ABST.SUM-NR
         READ  DECBAST,KU,(R9),SATZ,'S',(R3),MF=E
         WAIT  ECB=DECBAST
         TM    DECBAST+24,X'FD'
         BNL   INOK                     SATZ RICHTIG GELESEN
         TM    DECBAST+24,X'80'
         BZ    FHLIN
NOSATZ   EQU   *
         MVC   ERR,=F'1'                SATZ NICHT AUF DATEI
         B     FHLIO
FHLIN    EQU   *
         MVC   ERR,=F'4'                I/O-FEHLER BEI READ
         B     FHLIO
         SPACE 2
INOK     EQU   *
* ABST.SUMME IN SATZ EINFUEGEN
         L     R4,4(R11)                --> BERICHTSZEIT
         LA    R9,SATZ+16               --> GELESENER SATZ
         TM    LFLAG,X'FF'
         BO    NOSATZ                  SATZ IST GELOESCHT
         LH    R3,NPER                  ANZAHL PERIODEN
         BCTR  R3,0
         MH    R3,=H'26'
         LA    R9,0(R3,R9)              --> JUENGSTE PERIODE
         MVI   XFLAG,WIEDER            WIEDERHOLUNGSLAUF
         CLI   AREPEAT,RNO             KEINE WIEDERHOLUNG
         BE    NOWIEDER
         CLI   AREPEAT,RYES            WIEDERHOLUNG
         BE    WIEDERH
*              PRUEFUNG AUF WIEDERHOLUNG
         CLC   BERZT,0(R4)              BERICHTSZEIT-VERGLEICH
         BE    WIEDERH                  GLEICH --> WIEDERHOLUNGSLAUF
NOWIEDER EQU   *
         MVI   XFLAG,NEU               ADD
         LA    R9,SATZ+16
         BCTR  R3,0                     LAENGE DER ZU UEBERNEHM.PERIOD.
         EX    R3,MVCPER                AELTESTE PERIODE UEBERSPEICHERN
         LA    R9,1(R3,R9)              --> NEUE EINTRAGUNG
WIEDERH  EQU   *
         MVC   BERZT,0(R4)              BERICHTSZEIT --> NEUE EINTR.
         LR    R8,R9                   R9 ZWISCHENSPEICHERN
         MVC   FLAG,XFLAG
         L     R4,8(R11)                --> ABSTIMMSUMME
         MVC   AWERT,0(R4)              ABST.SUMME --> NEUE EINTR.
         L     R3,0(R11)                --> ABST.SUM.NR
         L     R9,PDCBAST               --> DCB
         WRITE DECBAST,K,MF=E
         WAIT  ECB=DECBAST
         TM    DECBAST+24,X'FD'
         BZ    OUTOK
         MVC   ERR,=F'5'
OUTOK    EQU   *
         LR    R9,R8                   -->DASATZ
         MVC   LFLAG(VORLN),SATZ+16    VORSPANN VOR LFD.PERIODE
         MVC   LFLAG+1(1),XFLAG        VORGANGSKENNUNG
         MVC   NPER,=H'1'              BEI A  PERIODE = +1
         CLI   XFLAG,NEU
         BE    PUTLOG
         XC    NPER,NPER               BEI W  PERIODE = 0
PUTLOG   EQU   *
         S     R9,=F'8'                AN ANFANG DES LOGSATZES
         TIME  DEC                     UHRZEIT UND DATUM
         STM   R0,R1,LFLAG             (GEPACKT) ANFUEGEN
         OI    LFLAG+3,X'0F'
         PUT   ASTLOG,LFLAG
FHLIO    EQU   *
         BAL   R10,CLOSE
RETURN   EQU   *
         L     15,ERR
         XRETURN (R15)
         SPACE 1
CLOSE    EQU   *                        CLOSE
         L     R9,PDCBAST
         CLOSE ((R9))
CLSLOG   EQU   *
         CLOSE (ASTLOG)                LOGBAND
DEQ      CALL  CDEQAST
         BR    R10
         SPACE 2
* DEFINITIONEN
PATAB    DC    V(ATAB)                 -->ATAB
KE       DC    CL1'E'
PDCBAST  DC    V(DCBAST)                --> DCB
ERR      DS    F
MVCPER   MVC   PERDEF(0),PEREND
         DS    0F
SATZ     DS    CL(16+170+12)
XFLAG    DS    CL1
         SPACE 1
         READ  DECBAST,K,,SATZ,'S',MF=L
ASTLOG   DCB   DSORG=PS,MACRF=(PM),DDNAME=ASTLOG,EXLST=EXLST
JFCB     DC    176XL1'FF'       .
EXLST    DS    0F
         DC    X'87'
         DC    AL3(JFCB)
         MATAB
         MASATZ
         END
