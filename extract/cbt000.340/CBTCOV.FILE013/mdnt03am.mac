         MACRO
&MERKMAL BASER &ANZAHL,&RC
* LADEN DER BASISREGISTER
          LCLC  &M
         LCLA  &REG,&KOR,&POS,&LG,&REG1,&ANZREG,&LG1,&POS1
         LCLC  &US,&USI,&STERN,&US1,&US2,&USI1
&ANZREG  SETA  &ANZAHL
          AIF   (N'&SYSLIST LT 1).ERROR1
&M    SETC  'M&SYSNDX.M'
         AIF   (&ANZREG LT 1 OR &ANZREG GT 11).ERROR2
&REG     SETA  13-&ANZREG
&KOR     SETA  2-&ANZREG
         AIF   (&ANZREG LE 2).WEITER
&KOR     SETA  &KOR-3+&ANZREG
.WEITER  ANOP
&POS     SETA  24-&ANZREG*2+&KOR
&LG      SETA  25-&POS
&STERN   SETC  '*,'
&US      SETC  '2,3,4,5,'
&US1     SETC  '6,7,8,9,'
&US2     SETC  '10,11,12'
         AIF   (&POS GE 17).SUB16
         AIF   (&POS GE 9).SUB8
&LG1     SETA  9-&POS
&USI1    SETC  '&US'(&POS,&LG1)
         AGO   .GEN
.SUB8    ANOP
&POS1    SETA  &POS-8
&LG1     SETA  17-&POS
&USI1    SETC  '&US1'(&POS1,&LG1)
&US1     SETC  ''
         AGO   .GEN
.SUB16   ANOP
&POS1    SETA  &POS-16
&LG1     SETA  25-&POS
&USI1    SETC  '&US2'(&POS1,&LG1)
&US1     SETC  ''
&US2     SETC  ''
.GEN     ANOP
&MERKMAL SAVE  (14,12)
         BALR  &REG,0
         USING &STERN&USI1&US1&US2
.FREND   AIF   (&REG EQ 12).ENDE
&REG1    SETA  &REG+1
         LA    &REG1,4095(&REG)
         LA    &REG1,1(&REG1)
&REG     SETA  &REG+1
         AGOB  .FREND
.ENDE    CNOP  2,4
         B    &M.01
&M.00     DS    18F
&M.01    ST    13,&M.00+4
         LA 13,&M.00
         AIF   ('&RC'NE'').END2
         B     START
END99    L     13,&M.00+4
         RETURN  (14,12)
         MEXIT
.END2    ANOP
         B     START
END99    L     13,&M.00+4
         RETURN (14,12),RC=(15)
         MEXIT
.ERROR1  MNOTE 7,'OPERANDEN-ANZAHL LT 1'
         MEXIT
.ERROR2  MNOTE 7,'REGISTER-ANZAHL LT 1 OR GT 11'
         MEXIT
         MEND
