         TITLE ' JCL - VERZEICHNIS  '
         PRINT NOGEN
*
*        PGM-NAME    :  JCLXREFP
*        DATE        :  07.11.79
*
*  LAST MODIFICATION :  ........
*  REASON            :  NONE
*
*  LAST COMPILE      :  07.11.79
*  LAST LINK         :  13.11.79     LIB = SYS2.LINKLIB
*  LAST SAVE         :  14.11.79
*
*  PGM-INFORMATION   :  DER DURCH DAS PGM 'JCLXREF' ERSTELLTE OUTPUT
*                       AUSDRUCKEN.
*
JCLXREFP INIT  BASIS=(2,11)
*
         L     R1,0(R1)
         LH    R3,0(R1)
         BCTR  R3,R0
         STC   R3,MVCPARM+1
MVCPARM  MVC   PARM,2(R1)
*
         OPEN  (SYSUT1,,SYSPRINT,OUTPUT)
         CALL  UZED10A,(DATUM)
LESEN    EQU   *
         GET   SYSUT1
         LA    R1,44(R1)
         MVC   XLINE,0(R1)
*
         CLC   XDSN(2),=C'* '
         BE    LESEN
         CLC   XDSN(7),=C'SYSOUT '
         BE    LESEN
         CLC   XDSN(6),=C'DUMMY '
         BE    LESEN
         CLC   XDSN(5),=C'DATA '
         BE    LESEN
         CLC   XDSN(2),=C'  '
         BE    LESEN
         CLC   XDSN(1),=X'50'
         BE    LESEN
*
************************************
******** ZEILE AUFBEREITEN *********
************************************
*
         CLC   XGEN(1),=C' '
         BNE   GOGEN1
         CLC   ZWDSNAME,XDSN
         BNE   MOVEDSN1
         MVI   PDSNAME,C' '
         MVC   PDSNAME+1(43),PDSNAME
         B     MOVEZW
MOVEDSN1 EQU   *
         MVC   PDSNAME,XDSN
         B     MOVEZW
*
GOGEN1   EQU   *
         CLC   ZWDSNAME(30),XDSN
         BNE   MOVEDSN2
         MVI   PDSNAME,C' '
         MVC   PDSNAME+1(43),PDSNAME
         B     MOVEGEN1
MOVEDSN2 EQU   *
         MVC   PDSNAME,XDSN
MOVEGEN1 MVI   PDSNAME+34,C'('
         MVC   COUNT,MASKE
         ED    COUNT,XGEN+2
         MVC   PDSNAME+35(1),XGEN
         MVC   PDSNAME+36(4),COUNT
         MVI   PDSNAME+40,C')'
*
MOVEZW   MVC   ZWDSNAME,XDSN
*
*
GODISP   EQU   *
         LA    R3,XDISP
         LA    R4,3
         LA    R5,PDISP1
         LA    R6,DISPTAB
         LA    R7,10
         B     DISPL02
DISPL01  EQU   *
         LA    R5,5(R5)
         LA    R3,1(R3)
         LA    R6,DISPTAB
         LA    R7,10
DISPL02  EQU   *
         CLC   0(1,R3),0(R6)
         BE    DISPL03
         LA    R6,6(R6)
         BCT   R7,DISPL02
DISPL03  MVC   0(5,R5),1(R6)            DISPOSITION
         BCT   R4,DISPL01
*
*
         MVC   PUNIT,=6CL1' '
         LA    R3,UNITTAB
         LA    R4,9
GOUNIT   EQU   *
         CLC   XUNIT,0(R3)
         BE    MOVEUNIT
         LA    R3,7(R3)
         BCT   R4,GOUNIT
         B     GOSERIAL
MOVEUNIT EQU   *
         MVC   PUNIT,1(R3)              UNIT
*
*
GOSERIAL EQU   *
         CLI   XSER,C'('
         BNE   MOVESER
         MVC   PSER(6),=C'* ? * '
         B     MOVENAME
MOVESER  MVC   PSER,XSER                SERIAL
*
*
MOVENAME MVC   PMEMBER,XMEMB            MEMBER-NAME
         MVC   PJOBNAME,XJOBNM          JOB-NAME
         MVC   PSTEPNM,XSTEPNM          STEP-NAME
         MVC   COUNT,MASKE
         ED    COUNT,XSTEPCNT
         MVC   PSTEPCNT,COUNT+1         STEP-COUNT
         MVC   PPGMMBR,XPGMMBR          PGM-NAME  /  MBR-NAME
         MVC   PPROC,XPROC              PROCEDURE
*
*
         EJECT
PUTEN    EQU   *
         CP    ZZ,=P'0'
         BE    PUTTITEL
         CP    ZZ,=P'58'
         BH    PUTTITEL
         B     PUTLINE
PUTTITEL EQU   *
         ZAP   ZZ,=P'0'
         AP    SZ,=P'1'
         MVC   SEITE,MASKE
         ED    SEITE,SZ
         MVI   TCC,X'8B'
         PUT   SYSPRINT,TITEL
         MVI   TCC,X'09'
         PUT   SYSPRINT,TITEL
         PUT   SYSPRINT,BLANK
         PUT   SYSPRINT,BLANK
         PUT   SYSPRINT,TLINE
         PUT   SYSPRINT,BLANK
PUTLINE  EQU   *
         PUT   SYSPRINT,PLINE
         AP    ZZ,=P'1'
         B     LESEN
*
ENDLESEN EQU   *
         CLOSE (SYSUT1,,SYSPRINT)
ENDE2    L     R13,S0001+4
         L     R15,RETCODE
         RETURN (14,12),RC=(15)
         EJECT
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=ENDLESEN
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      *
               RECFM=FBM,LRECL=133,BLKSIZE=133
*
XLINE    DS  0CL110
XLCC     DS  CL1
XMEMB    DC  CL8' '
XJOBNM   DC  CL8' '
XSTEPNM  DC  CL8' '
XSTEPCNT DC  PL2'0'
*
XDDNM    DC  CL8' '
XDSN     DC  CL44' '
XGEN     DS  0CL4
         DC  CL1' '
         DC  PL3'0'
XDISP    DC  CL3' '
XUNIT    DC  CL1' '
XSER     DC  CL6' '
*
XPROC    DC  CL8' '
XPGMMBR  DC  CL8' '
XEMPTY   DC  CL2' '
***********************************************
*    X D I S P                                *
*                                             *
*    SHR       =  1                           *
*    NEW (,)   =  2 -----------------         *
*    OLD       =  3                  ×        *
*    KEEP      =  4 -----------------×-       *
*    MOD       =  5                  × ×      *
*    DELETE    =  6 -----------------×-×-     *
*    CATLG     =  7                  × × ×    *
*    UNCATLG   =  8                  × × ×    *
*    PASS      =  9                  × × ×    *
**** DISP=(NEW,KEEP,DELETE)   DISP=  2 4 6    *
*                                             *
DISPTAB  DS    0CL60                          *
         DC    CL6'1SHR  '                    *
         DC    CL6'2NEW  '                    *
         DC    CL6'3OLD  '                    *
         DC    CL6'4KEEP '                    *
         DC    CL6'5MOD  '                    *
         DC    CL6'6DEL  '                    *
         DC    CL6'7CAT  '                    *
         DC    CL6'8UCAT '                    *
         DC    CL6'9PASS '                    *
         DC    CL6'      '                    *
***********************************************
*
************************************
*    TAPE      =  1                *
*    TAPE08    =  2                *
*    TAPE16    =  3                *
*    3330-1    =  4                *
*    3350      =  5                *
*    SYSDA     =  6                *
*    DISK      =  7                *
*    UNIT=AFF                      *
*    ODER ANDERES    = F           *
*                                  *
UNITTAB  DS    0CL63               *
         DC    CL7'       '
         DC    CL7'1TAPE  '        *
         DC    CL7'2TAPE08'        *
         DC    CL7'3TAPE16'        *
         DC    CL7'43330-1'        *
         DC    CL7'53350  '        *
         DC    CL7'6SYSDA '        *
         DC    CL7'7DISK  '        *
         DC    CL7'F* ? * '        *
************************************
*
PLINE    DS    0CL133
PLCC     DC    XL1'09'
PDSNAME  DS    CL44
PDISP1   DS    CL5
PDISP2   DS    CL5
PDISP3   DS    CL5
         DC    CL3' '
PUNIT    DS    CL6
         DC    CL1' '
PSER     DS    CL6
         DC    CL1' '
PMEMBER  DS    CL8
         DC    CL1' '
PJOBNAME DS    CL8
         DC    CL1' '
PSTEPNM  DS    CL8' '
         DC    CL2' ('
PSTEPCNT DS    CL3
         DC    CL2') '
         DC    CL1' '
PPGMMBR  DS    CL8
         DC    CL1' '
PPROC    DS    CL8
         DC    CL10' '
*
*
TLINE    DS    0CL133
TLCC     DC    XL1'09'
TDSNAME  DC    CL10'FILE-NAME '
         DC    CL34' '
TDISP1   DC    CL11'DISPOSITION'
         DC    CL7' '
TUNIT    DC    CL6'UNIT  '
         DC    CL1' '
TSER     DC    CL6'SERIAL'
         DC    CL1' '
TMEMBER  DC    CL8'MEMBER  '
         DC    CL1' '
TJOBNAME DC    CL8'JOBNAME '
         DC    CL1' '
TSTEPNM  DC    CL8'STEPNAME'
         DC    CL1' '
TSTEPCNT DC    CL7'COUNT  '
TPGMMBR  DC    CL8'PGM/MBR '
         DC    CL1' '
TPROC    DC    CL9'PROCEDURE'
         DC    CL10' '
*
TITEL    DS    0CL133
TCC      DS    XL1
         DC    CL7'*****  '
         DC    CL31'J C L  -  V E R Z E I C H N I S'
         DC    CL7'  *****'
         DC    CL15' '
         DC    CL5'LIB: '
PARM     DC    CL40' '
         DC    CL7'DATUM: '
DATUM    DS    CL8
         DC    CL8'  SEITE:'
SEITE    DS    CL4
*
BLANK    DS    0CL133
         DC    XL1'09'
         DC    CL132' '
*
*
SZ       DC    PL2'0'
ZZ       DC    PL2'0'
MASKE    DC    XL4'40202120'
COUNT    DS    CL4
ZWDSNAME DC    CL44' '
RETCODE  DC    F'0'
         END
