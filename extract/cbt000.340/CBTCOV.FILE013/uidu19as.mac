PARM SCANCOL=72 SUPPRESS=ALL
*
*  RMFEASY3:   R M F   M O N T H L Y   R E P O R T
*  31/07/79
*
*              FILE DEFINITION
FILE FILEA
*
*              INPUT AREA
INPUTW 1 80 A
*
ISYSID 49 4 A
IDATE 53 6 N
IDAY 53 2 N
IMONTH 55 2 N
IYEAR 57 2 N
ISHIFT 59 1 B
IRECID 80 1 A
*
*              INPUT WORK AREA
IWORKA W-1 80 A
*              MEASUREMENT DURATION
CYCLNG W-1 4 B
*              SERVICE RATES
*
TSOSR W-3 2 B
TPSR W-7 2 B
TESTSR W-11 2 B
BATCHSR W-15 2 B
*
*              CHANNEL BUSY TABLE
*
CH1 W-17 1 B
CH2 W-21 1 B
CH3 W-25 1 B
CH4 W-29 1 B
CH5 W-33 1 B
CH6 W-37 1 B
*              CHANNEL BUSY / CPU WAIT
CHBCPUW W-41 4 B
*              " OF SAMPLES
SAMPLE W-45 4 B
*              SYSTEM ID
SYSID W-49 4 A
*              DATE
DATE W-53 6 ND
DAY W-53 2 N
MONTH W-55 2 N
YEAR W-57 2 N
*              SHIFT KEY  1 = 0 - 8.00, 2 = 8 - 16.00, 3 = 16 - 24.00 H
SHIFTCHP W-59 1 B
*              CPU WAIT TIME
CPUWAIT W-60 4 B
*              INTERVAL DURATION IN SECONDS
CPUINT W-64 4 P
*              " OF ASID'S IN MEMORY (MPL)
MPL W-69 1 B
*              " OF BATCH ASID'S
BATCH W-71 1 B
*              " OF TIME SHARING USERS
TSO W-73 1 B
*              " OF PAGES / SECOND
PAGE W-75 1 B
*              TSO ACTIVE TIME (SECONDS)
TSOACT W-76 4 B
*              RECORD ID
RECID W-80 1 A
*
*              TOTALS AREA
*
*              SERVICE RATES
TSOSRS W-81 4 B
TSOSRP W-83 2 B
TPSRS W-85 4 B
TPSRP W-87 2 B
TESTSRS W-89 4 B
TESTSRP W-91 2 B
BATCHSRS W-93 4 B
BATCHSRP W-95 2 B
*
*              CHANNEL BUSY PERCENTAGES
*
CH1S W-97 4 B
CH1P W-100 1 B
CH2S W-101 4 B
CH2P W-104 1 B
CH3S W-105 4 B
CH3P W-108 1 B
CH4S W-109 4 B
CH4P W-112 1 B
CH5S W-113 4 B
CH5P W-116 1 B
CH6S W-117 4 B
CH6P W-120 1 B
*
*              DATE LOOK UP FIELD (MONTH/DAY)
TABDATE W-121 4 N
TABMM W-121 2 N
TABDD W-123 2 N
*              " OF ASID'S IN MEMORY (MPL)
MPLS W-125 2 B
MPLP W-126 1 B
*              " OF BATCH ASID'S
BATCHS W-127 2 B
BATCHP W-128 1 B
*              " OF TIME SHARING USERS
TSOS W-129 2 B
TSOP W-130 1 B
*              " OF PAGES / SECOND
PAGES W-131 2 B
PAGEP W-132 1 B
*
*              TAB FUNCTION
TABFUNC W-133 3 A
MODDAY W-133 2 N
*              CHANNEL BUSY / CPU WAIT
CHBCPUWS W 4 B
*              " OF SAMPLES
SAMPLES W 4 B
*              CPU WAIT TIME
CPUWAITS W 4 B
*              INTERVAL DURATION IN SECONDS
CPUINTS W 4 P
*              TSO ACTIVE TIME (SECONDS)
TSOACTS W 4 B
*              TOTAL MEASUREMENT DURATION
CYCLNGS W 4 B
*              SHIFT INFO
SHIFT W 11 A
*              FIRST TIME SWITCH
SMFFTSW W 1 B
*
*              HEADLINE SWITCH
HEADLINE W 1 B
*
*              PAGE LINE COUNTER
ZZ W 1 B
*
*              LINE COUNTER
NUMBER W 2 B
*
*              TSO ACTIVE TIME (% OF INTERVAL TIME)
TSOACT% W 1 B
*
*              TSO SERVICE
TSOSERV% W 1 B
*
*              TEST SERVICE
TESTSERV% W 1 B
*
*              TP SERVICE
TPSERV% W 1 B
*
*              BATCH SERVICE
BATCHSERV% W 1 B
*
*              CHANNEL BUSY / CPU WAIT PERCENTAGE
CHBCPUW% W 1 B
*
*              CPU WAIT TIME PERCENTAGE
CPU W 1 B
*
*
TOTSRATE W 2 B
*
*
RECIDS W 1 B
*
DAYP W 2 N
*
CYCLENGTH W 4 B
*
9999
*
FILE DAYTAB TABLE
*
9999
*
*
*             MODIFY WEEK END DAYS USING THE EXTERNAL DAY TABLE
*             PERFORM SORT
*
IF SORTED EQ YES
GO TO SORTOK
ELSE
TABMM = IMONTH
TABDD = IDAY
*             LOOK UP TABLE
TABFUNC = DAYTAB TABDATE
*
IF MODDAY EQ *
FLUNK
ELSE
*            INCREASE WEEKEND DAYS BY 40 TO SEPARATE THEM FROM THE REST
IDAY = IDAY + MODDAY
*
IF SORTED EQ NO THEN
GO TO SORTNOK
*
SORTOK:
*
*              TEST FOR CONTROL BREAK
IF EOF EQ YES THEN GO TO MINORBREAK
*
IF MONTH NQ IMONTH OR SYSID NQ ISYSID OR SHIFTCHP NQ ISHIFT THEN
GO TO MINORBREAK
*
IF DAY EQ IDAY THEN GO TO FIRSTTIME
*
MINORBREAK:
*
IF SMFFTSW EQ 0 THEN GO TO FIRSTTIME
*
IF RECIDS NQ 3 THEN
PRINT SP2 DAY-3 (RECORDS OUT OF SEQUENCE  )-8
RECIDS = 0
GO TO MAJORBREAK
*
IF FILE EQ FILE THEN
RECIDS = 0
*
*              CALCULATE SERVICE PERCENTAGES
TOTSRATE = TSOSR + TESTSR + TPSR + BATCHSR
TSOSERV% = TSOSR X 100.0 / TOTSRATE + 0.5
TESTSERV% = TESTSR X 100.0 / TOTSRATE + 0.5
TPSERV% = TPSR X 100.0 / TOTSRATE + 0.5
BATCHSERV% = BATCHSR X 100.0 / TOTSRATE + 0.5
*
*              CALCULATE CPU WAIT TIME PERCENTAGE
CPU = CPUWAIT X 100.0 / CYCLENGTH + 0.5
*
*              CALCULATE PERCENTAGE OF TRANSACT ACTIVE TIME
*
IF TSOACT GT CPUINT THEN
TSOACT = CPUINT
IF FILE EQ FILE
TSOACT% = TSOACT X 100.0 / CPUINT + 0.5
CPUINTS = CPUINTS + CPUINT
*
*
*              CALCULATE PERCENTAGES OF CH BUSY AND CH BUSY CPU WAIT
*
CHBCPUWS = CHBCPUWS + CHBCPUW
CH1S = CH1S + CH1
CH2S = CH2S + CH2
CH3S = CH3S + CH3
CH4S = CH4S + CH4
CH5S = CH5S + CH5
CH6S = CH6S + CH6
CHBCPUW% = CHBCPUW X 100.0 / SAMPLE + 0.5
SAMPLES = SAMPLES + SAMPLE
*
IF SHIFTCHP = 1 THEN
SHIFT = (0 - 8 UHR  )
IF SHIFTCHP = 2 THEN
SHIFT = (8 - 16 UHR )
IF SHIFTCHP = 3 THEN
SHIFT = (16 - 24 UHR)
*
*             PRODUCE HEADLINES
*
IF HEADLINE EQ 0 OR ZZ GT 56 THEN
*
PRINT CC1 (AUSWERTUNG)-1 MONTH-12 (.)-14 YEAR-15 SYSID-20 SHIFT-27
*
PRINT SP2 ( CPU )-7 (TSO BAT)-16 MORE
          (------TSO------ --BATCH---)-28  MORE
          (----TP---- ---TEST---)-55  MORE
          TOTAL-77 (CPU WAIT)-84 MORE
          (----CHANNEL BUSY  %----)-93
*
PRINT (TAG   WT  MPL MPL MPL PAG)-2  MORE
      (S-% S-RATE ACT% S-% S-RATE S-% S-RATE)-28 MORE
      (S-% S-RATE S-RATE)-66  MORE
      (CH BUSY)-84 (CH1 CH2 CH3 CH4 CH5 CH6)-93
*
PRINT ( )
*
*             INIT PAGE LINE COUNT
ZZ = 5
*             SET HEADLINE SWITCH TO  1
HEADLINE = 1
*
IF HEADLINE EQ 2 THEN
*
PRINT SP2 ( )
ZZ = ZZ + 2
HEADLINE = 1
*
IF DAY GT 40 THEN
DAYP = DAY - 40
ELSE
DAYP = DAY
IF FILE EQ FILE THEN
*
*             PRINT INTERVAL LINE
*
PRINT DAYP-3 CPU-8 MPL-12 TSO-16 BATCH-20 PAGE-24 MORE
      TSOSERV%-28 TSOSR-33 TSOACT%-40 MORE
      BATCHSERV%-44 BATCHSR-49 TPSERV%-55 TPSR-60 MORE
      TESTSERV%-66 TESTSR-71 TOTSRATE-78 CHBCPUW%-87 CH1-93 MORE
      CH2-97 CH3-101 CH4-105 CH5-109 CH6-113
*
*             UPDATE LINE COUNTERS
NUMBER = NUMBER + 1
ZZ = ZZ + 1
*
*             ACCUMULATE VALUES
*
CPUWAITS = CPUWAITS + CPUWAIT
MPLS = MPLS + MPL
TSOS = TSOS + TSO
BATCHS = BATCHS + BATCH
PAGES = PAGES + PAGE
TSOSRS = TSOSRS + TSOSR
BATCHSRS = BATCHSRS + BATCHSR
TPSRS = TPSRS + TPSR
TESTSRS = TESTSRS + TESTSR
TSOACTS = TSOACTS + TSOACT
CYCLNGS = CYCLNGS + CYCLENGTH
*
*
MAJORBREAK:
*              TEST FOR MAJOR CONTROL BREAK
IF EOF EQ YES THEN GO TO TOTCALC
IF MONTH NQ IMONTH OR SYSID NQ ISYSID OR SHIFTCHP NQ ISHIFT THEN
HEADLINE = 0
GO TO TOTCALC
IF DAY LT 40 AND IDAY GT 40 THEN
HEADLINE = 2
ELSE GO TO FIRSTTIME
*
TOTCALC:
*
IF SMFFTSW EQ 0 THEN GO TO FIRSTTIME
*
ELSE
*
*            TOTAL CALCULATIONS
*
CPU = CPUWAITS X 100.0 / CYCLNGS + 0.5
MPLS = MPLS X 1.0 / NUMBER + 0.5
TSOS = TSOS X 1.0 / NUMBER + 0.5
BATCHS = BATCHS X 1.0 / NUMBER + 0.5
PAGES = PAGES X 1.0 / NUMBER + 0.5
TSOSRS = TSOSRS / NUMBER
BATCHSRS = BATCHSRS / NUMBER
TPSRS = TPSRS / NUMBER
TESTSRS = TESTSRS / NUMBER
TOTSRATE = TSOSRS + BATCHSRS + TPSRS + TESTSRS
TSOSERV% = TSOSRS X 100.0 / TOTSRATE + 0.5
BATCHSERV% = BATCHSRS X 100.0 / TOTSRATE + 0.5
TPSERV% = TPSRS X 100.0 / TOTSRATE + 0.5
TESTSERV% = TESTSRS X 100.0 / TOTSRATE + 0.5
*
IF TSOACTS GT CPUINTS THEN
TSOACTS = CPUINTS
IF FILE EQ FILE THEN
TSOACT% = TSOACTS X 100 / CPUINTS
*
*
*              CALCULATE PERCENTAGES OF CH BUSY AND CH BUSY / CPU WAIT
*
CHBCPUW% = CHBCPUWS X 100.0 / SAMPLES + 0.5
*
CH1S = CH1S X 1.0 / NUMBER + 0.5
CH2S = CH2S X 1.0 / NUMBER + 0.5
CH3S = CH3S X 1.0 / NUMBER + 0.5
CH4S = CH4S X 1.0 / NUMBER + 0.5
CH5S = CH5S X 1.0 / NUMBER + 0.5
CH6S = CH6S X 1.0 / NUMBER + 0.5
*
*             PRINT TOTAL LINE
*
PRINT SP2 CPU-8 MPLP-12 TSOP-16 BATCHP-20 PAGEP-24 MORE
      TSOSERV%-28 TSOSRP-33 TSOACT%-40 MORE
      BATCHSERV%-44 BATCHSRP-49 TPSERV%-55 TPSRP-60 MORE
      TESTSERV%-66 TESTSRP-71 TOTSRATE-78 CHBCPUW%-87 CH1P-93 MORE
      CH2P-97 CH3P-101 CH4P-105 CH5P-109 CH6P-113
*
*
*             CLEAR ACCUMULATION FIELDS
*
CLEAR:
IF FILE EQ FILE THEN
MPLS = 0
BATCHS = 0
TSOS = 0
PAGES = 0
TSOSRS = 0
BATCHSRS = 0
TPSRS = 0
TESTSRS = 0
TSOACTS = 0
CPUINTS = 0
SAMPLES = 0
CHBCPUWS = 0
CH1S = 0
CH2S = 0
CH3S = 0
CH4S = 0
CH5S = 0
CH6S = 0
CPUWAITS = 0
CYCLNGS = 0
*
NUMBER = 0
*
*
*
FIRSTTIME:
IF EOF EQ YES THEN
STOP
*
ELSE
*             TEST RECORD ID AND MOVE RECORD VALUES TO WORKA
*
SMFFTSW = 1
*
IWORKA = INPUTW
*
IF IRECID NQ 1 THEN GO TO REC2
IF RECIDS LT 2 THEN
CYCLENGTH = CYCLNG
RECIDS = 1
ELSE
RECIDS = 2
*
REC2:
IF IRECID NQ 2 THEN FLUNK
IF RECIDS EQ 1,3 THEN
RECIDS = 3
FLUNK
ELSE
RECIDS = 2
FLUNK
*
SORTNOK:
IF FILE EQ FILE
SORT IMONTH ISYSID ISHIFT IDATE IRECID
*
