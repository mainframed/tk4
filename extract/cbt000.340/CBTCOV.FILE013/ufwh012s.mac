         TITLE 'IMSDUPRE    DATENBANK AUSZUG - RELOAD'
***********************************************************************
***                                                                 ***
***      DAS PROGRAMM IMSDUPRE ERSTELLT EINE NEUE DATENBANK MIT     ***
***      HILFE DES VOM PROGRAMM IMSDUPUN ERSTELLTEN DATENBANDES.    ***
***      WIRD KEIN NEUER DBD ANGEGEBEN, SO WIRD DER GLEICHE WIE     ***
***      ZUM ENTLADEN DER DATENBANK BENUTZT.                        ***
***                                                                 ***
***********************************************************************
***                                                                 ***
***      AUTHOR:           WOLFGANG SCHEH                           ***
***                        HOECHST AG                               ***
***                        ADV / K707                               ***
***                        6230 FRANKFURT/M 80                      ***
***                        TEL. 0611/305-5604                       ***
***                                                                 ***
***********************************************************************
***                                                                 ***
***      GETESTET MIT IMSVS 1.1.4                                   ***
***                                                                 ***
***********************************************************************
         SPACE 2
DLITCBL  CSECT
         SAVE  (14,12)            REGISTER RETTEN
         LR    3,15               BASIS-REGISTER ZUORDNEN
         USING DLITCBL,3
         LA    12,SAVEAREA
         ST    13,4(12)           SAVEAREAS UEBERGEBEN
         ST    12,8(13)
         LR    13,12
         L     5,16               ADR. CVT
         L     5,0(5)             ADR. NEW/OLD
         L     5,4(5)             ADR. CURRENT TCB
         L     5,TCBFSA(5)        ADR. 1ST. SAVEAREA
         L     5,24(5)            ADR. DFSL×000
         USING PXPARMS,5
         L     5,PCPST            ADR. PST
         DROP  5
         ST    5,ADRPST           PST. ADR. SPEICHERN
         OPEN  (KART,(INPUT),BAND,(INPUT),LIST,(OUTPUT))
         MVC   DBER+10(28),=C'AUFLISTUNG DER STEUERKARTEN:'
         MVI   DBER,C'1'          VORSCHUB BLATTANFANG
         BAL   12,DRUCK           UEBERSCHRIFT DRUCKEN
         MVI   DBER+10,C'*'
         MVC   DBER+11(27),DBER+10
         BAL   12,DRUCK
         MVI   DBER,C'-'
         LA    1,KART             ADRESSE SYSIN-DCB LADEN
         USING IHADCB,1
         TM    DCBOFLGS,X'10'     OPEN ERFOLGREICH
         BZ    EOFK               NEIN, KEINE SYSINKARTE VORHANDEN
         DROP  1
         BAL   12,KARTLES         1. KARTE LESEN
         BAL   12,KOMPRIM         BLANKS RAUSFILTERN
         CLC   EBER(3),=C'DBD'    DBD-KARTE?
         BNE   F1
         MVC   EDBD,EBER+4        DBDNAME SPEICHERN
B1       GET   BAND,BEBER         BAND LESEN
         MVC   SEGL(2),BEBER+12   GROESSTE SEGMENTLAENGE SPEICHERN
         LH    0,SEGL             SEGL. IN REG. 0 SPEICHERN
         LA    5,BAND             ADR. BAND-DCB LADEN
         USING IHADCB,5
         CH    0,DCBBLKSI         BLKSIZE IM DCB PRUEFEN
         BL    A10
         STH   0,DCBBLKSI         NEUE BLKSIZE IN DCB STELLEN
         DROP  5
A10      GETMAIN R,LV=(0)
         C     15,=F'0'           RETURN-CODE OK
         BNE   F1A
         LR    9,1
         LA    9,12(9)            ADR. SEGMENTANFANG LADEN
         ST    9,PL1+8            ADR. IO-BEREICH IN PARM-LISTE STELLEN
         LR    9,1                ADR. EINLESEBEREICH LADEN
         CLC   EDBD,=CL8' '       DBD VORGEGEBEN?
         BNE   B2
         MVC   EDBD,BEBER+4       DBDNAME VOM BAND SPEICHERN
B2       BAL   12,PCBSUCH         PCB SUCHEN
         L     5,ADRPCB           ADR. PCB LADEN
GETB     GET   BAND,(9)           BAND LESEN
         AP    SEGZAE,=P'1'       SEGMENTZAEHLER +1
         MVC   SSANAM,4(9)        SEGMENTNAME IN SSA STELLEN
         LA    1,PL1              ADR.PARMLISTE IN R.1
         L     15,VCBLTDLI        ENTRY-POINT LADEN
         BALR  14,15              LINK
         CLC   10(2,5),=C'  '     STC BLANK?
         BE    GETB
         CLC   10(2,5),=C'II'     STC II = DOPPELT. SCHLUESSEL ABFANGEN
         BE    F4
         B     F2
         SPACE 2
EOFK     CP    KZAE,=P'0'         KARTENZAEHLER = 0
         BNE   B1
         MVC   DBER+10(31),=C'KEINE STEUERANWEISUNG VORHANDEN'
         BAL   12,DRUCK
         B     B1
         SPACE 2
F3       MVC   DBER+10(39),=C'DBD KONNTE IM PSB NICHT GEFUNDEN WERDEN'
         BAL   12,DRUCK
         B     EOFB1
F1       MVC   DBER+10(38),=C'KEINE DBD-STEUERANWEISUNG IN DER KARTE'
         BAL   12,DRUCK
         B     EOFB1
F1A      MVC   DBER+10(25),=C'GETMAIN NICHT ERFOLGREICH'
         BAL   12,DRUCK
         B     EOFB1
F4       MVC   DBER+10(38),=C'BEIM INSERT TRAT DER STATUSCODE    AUF'
         MVC   DBER+42(2),10(5)
         BAL   12,DRUCK
         B     GETB
F2       MVC   DBER+10(38),=C'BEIM INSERT TRAT DER STATUSCODE    AUF'
         MVC   DBER+42(2),10(5)
         BAL   12,DRUCK
         SPACE 2
DBENDE   MVI   DBER,C'0'          VORSCHUBZEICHEN
         MVC   DBER+10(70),=C'ES WURDEN INSGESAMT          SEGMENTE IN /
               DIE NEUE DATENBANK EINGEFUEGT'
         MVC   DBER+30(8),=X'4020202020202120'  SCHABLONE
         ED    DBER+30(8),SEGZAE  SEGMENTZAEHLER AUFBEREITEN
         BAL   12,DRUCK
EOFB1    MVC   DBER+10(21),=C'ENDE DER VERARBEITUNG'
         MVI   DBER,C'-'
         BAL   12,DRUCK
         CLOSE (KART,,LIST,,BAND)
         L     13,4(13)
         RETURN (14,12),RC=0
         SPACE 2
***      SUCHROUTINE ZUM AUFSUCHEN DES GEWUENSCHTEN PCB
PCBSUCH  L     5,ADRPST           ADR. PST IN REG. LADEN
         USING PST,5
         L     5,PSTPSB           ADR. PSB-DIRECTORY
         USING PDIR,5
         L     5,PDIRADDR         ADR. PSB
         ST    5,ADRPSB           ADR. PSB SPEICHERN
         LR    6,5                ADR. PSB IN REG. 6
         USING PSB,6
         CLC   PSBDBOFF,=X'FFFF'
         BE    A1A
         LH    8,PSBDBOFF         ADR. 1. DB-PCB ERMITTELN
         AR    5,8
A1A      LA    5,PSBLIST-PSB(5)   DISTANZ ADDIEREN
         L     7,0(5)             ADR. 1. PCB LADEN
A1       TM    PSBCODE,PSBPL1     ABFRAGE OB PLI PROGRAMM
         BNO   A11
         LA    7,60(7)            PL1-DOPEVEKTOR UEBERSPRINGEN
A11      CLC   0(8,7),EDBD        GEWUENSCHTER DBD?
         BE    A2
         TM    0(5),X'80'         LETZTER PCB?
         BO    F3                 FEHLERNACHRICHT AUSGEBEN
         LA    5,4(5)             AUF NAECHSTE PCB-ADRESSE GEHEN
         L     7,0(5)             PCB-ADR IN REG.7 LADEN
         B     A1
         DROP  5,6
         SPACE 2
A2       LA    7,0(7)             HIGH ORDER BIT AUSKNIPSEN
         ST    7,ADRPCB           ADRESSE DES GEFUNDENEN PCB SPEICHERN
         ST    7,PL1+4            ADRESSE IN PARM-LISTE EINSETZEN
         BR    12
         SPACE 2
KARTLES  LA    5,EBER
         GET   KART,(5)           LESE- UND DRUCKROUTINE
         AP    KZAE,=P'1'         KARTENZAEHLER +1
         MVC   DBER+10(72),EBER
DRUCK    PUT   LIST,DBER
         MVC   DBER,DBER-1
         BR    12
         SPACE 2
***      ROUTINE ZUM AUSSTEUERN VON BLANKS
KOMPRIM  LA    6,EBER             ADR. EBER KARTE
         LA    7,ZWSP             ADR. ZWSP ZUR KOMPRIMIERUNG
VGL1     CLI   0(6),C' '          ABFRAGE AUF BLANK
         BE    A3
         MVC   0(1,7),0(6)        ZEICHEN IN ZWSP STELLEN
         LA    7,1(7)             ADR ZWSP+1
A3       LA    6,1(6)             ADR EBER+1
         C     6,=A(EBER+72)      ENDE EBER?
         BL    VGL1
         MVC   EBER,ZWSP          KOMPRIMIERTE DATEN IN EBER ZURUECK
         MVC   ZWSP,ZWSP-1        ZWSP LOESCHEN
         BR    12
         EJECT
KART     DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),RECFM=FB,LRECL=80,     /
               EODAD=EOFK
         SPACE 2
LIST     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBA,          /
               BLKSIZE=931,LRECL=133
         SPACE 2
BAND     DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GM),RECFM=VB,LRECL=3152,  /
               BLKSIZE=3156,EODAD=DBENDE
         EJECT
***      DEFINITIONEN, BEREICHE, KONSTANTE, ZAEHLER ETC.
         SPACE 2
SAVEAREA DC    18F'0'             SAVEAREA
         SPACE 2
TCBFSA   EQU   112                DISPLACEMENT IM TCB
         SPACE 2
ADRPST   DC    F'0'               PST-ADR
ADRPSB   DC    F'0'               PSB-ADR
ADRPCB   DC    F'0'               PCB-ADR
SEGL     DC    H'0'               MAXIMALE SEGMENTLAENGE
EDBD     DC    CL8' '             NAME DES GESUCHTEN DBD
VCBLTDLI DC    V(CBLTDLI)         EP-ADR. IMS
         SPACE 2
PL1      DC    A(ISRT)            PARM-LISTE FUER IMS-CALL
         DC    F'0'               ADR. PCB
         DC    F'0'               ADR. IO/BEREICH
         DC    X'80'              HIGH ORDER BIT
         DC    AL3(SSANAM)        SEGMENT-NAME
         DS    0F
ISRT     DC    C'ISRT'            FUNKTIONSCODE
SSANAM   DC    CL8' '             SSA-NAME
         DC    C' '
EBER     DC    CL80' '            EINLESEBEREICH KARTEN
         SPACE 2
         DC    C' '
DBER     DC    CL133' '           DRUCKBEREICH
         SPACE 2
         DC    C' '
ZWSP     DC    CL80' '            ZWSP FUER EINGABE-KARTEN
         DC    C' '
BEBER    DC    100C' '            EINLESEBEREICH BAND
SEGZAE   DC    XL4'C'             SEGMENTZAEHLER
KZAE     DC    XL4'C'             KARTENZAEHLER
         PRINT NOGEN
         IDLI  PSTBASE=0,         DSECT PST                            *
               PSBBASE=0,         DSECT PSB                            *
               PDRBASE=0          DSECT PDIR
         PARMBLK DSECT
         DCBD  DSORG=PS,DEVD=TA
         END
