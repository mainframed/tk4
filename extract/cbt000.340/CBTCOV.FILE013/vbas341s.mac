DASDI    START
         XSAVE R12,,DASDI,WORKL
         REG
         USING WORK,R13
WTOR     EQU   *
         XC    ECB,ECB
         MVC   WTO,MESS1
         MVC   REPLY,=CL12' '
         WTOR  ,REPLY,12,ECB,MF=(E,WTO)
         WAIT  ECB=ECB
         OC    REPLY,=CL12' '
         CLI   REPLY+1,C','
         BNE   ERROR
         CLI   REPLY+5,C','
         BNE   ERROR
         CLI   REPLY,C'L'
         BE    PROCLAB
         CLI   REPLY,C'A'
         BNE   ERROR
         MVC   CARD,ANALCD
         MVC   CARD+14(3),REPLY+2
         MVC   CARD+45(6),REPLY+6
         B     PUT
PROCLAB  EQU   *
         MVC   CARD,LABELCD
         MVC   CARD+12(3),REPLY+2
         MVC   CARD+25(6),REPLY+6
PUT      EQU   *
         OPEN  (SYSIN,(OUTPUT))
         PUT   SYSIN,CARD
         CLOSE SYSIN
         LINK  EP=IEHDASDR,PARAM=(OPTLIST),VL=1
         LTR   R15,R15
         BZ    RETURN
         WTO   'DASDI01 IEHDASDR RETURN-CODE NOT ZERO',ROUTCDE=(2,4)
RETURN   EQU   *
         XRETURN ,R
ERROR    EQU   *
         MVC   WTO,MESS2
         WTO   MF=(E,WTO)
         B     WTOR
         CNOP  2,4
OPTLIST  DC    H'0'
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(PM),RECFM=F,BLKSIZE=80,    *
               LRECL=80,BUFNO=1
MESS2    WTO   'DASDI03 ERROR IN PARAMETER, REPEAT REPLY',MF=L,        *
               ROUTCDE=(2,4)
ANALCD   DC    CL80' ANALYZE TODD=CUU,VTOC=01,EXTENT=18,NEWVOLID='
LABELCD  DC    CL80' LABEL TODD=   ,NEWVOLID=     '
MESS1    WTOR  'DASDI00 REPLY FUNCTION, CUU AND VOLSER',MF=L,          *
               ROUTCDE=(2,4)
WORK     DSECT
SVA      DS    18F
WTO      DS    CL80
ECB      DS    F
REPLY    DS    CL12
CARD     DS    CL80
WORKL    EQU   *-WORK
         END
