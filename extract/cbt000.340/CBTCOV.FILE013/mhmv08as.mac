         MACRO
&NAME    SM02  &P
         LCLA  &NPARM,&N,&M
&NPARM   SETA  N'&P
&N       SETA  1
         L     R1,0(,R1)
         LH    R15,0(,R1) .       PARAMETERLAENGE
         LTR   R0,R15 .           LAENGE = 0
         BZ    P&SYSNDX.90 .          JA
         LA    R1,2(,R1) .        R0 : RESTLAENGE D. PARAMETERLISTE
.A002    LR    R14,R1
         SR    R15,R15
         CLI   0(R14),C',' .      PARAMETERENDE ?
         BE    *+20 .             JA
         LA    R15,1(,R15) .      R15 : LAENGE D. EINZELPARAMETERS
         LA    R14,1(,R14)
         BCTR  R0,0 .             ENDE DER PARAMETERLISTE ?
         LTR   R0,R0
         BH    *-20
         BCTR  R15,0 .            LAENGE EINZELPARAMETER - 1
         LTR   R15,R15
&M       SETA  30
         AIF   (T'&P(&N) EQ 'P').A005
&M       SETA  22
         AIF   (T'&P(&N) EQ 'F').A005
         AIF   (T'&P(&N) EQ 'G').A005
         AIF   (T'&P(&N) EQ 'H').A005
&M       SETA  14
.A005    BL    *+&M
&M       SETA  L'&P(&N)
         AIF   (T'&P(&N) EQ 'F').A010
         AIF   (T'&P(&N) EQ 'G').A010
         AIF   (T'&P(&N) EQ 'H').A010
         AIF   (T'&P(&N) EQ 'P').A020
.*       CHARACTER KONSTANTE
         STC   R15,*+5
         MVC   &P(&N),0(R1)
         AGO   .A050
.*       BINAER
.A010    EX    R15,P&SYSNDX.PCK
         CVB   R1,P&SYSNDX.D
         ST    R1,P&SYSNDX.D
         MVC   &P(&N),P&SYSNDX.D+4-&M
         AGO   .A050
.*       GEPACKT
.A020    EX    R15,P&SYSNDX.PCK
         CLI   0(R1),C'-' .       NEGATIV ?
         BNE   *+12 .             NEIN
         NI    P&SYSNDX.D+7,X'F0'
         OI    P&SYSNDX.D+7,X'0D'
         ZAP   &P(&N),P&SYSNDX.D
.A050    ANOP
         LA    R1,1(,R14)
         LH    R14,P&SYSNDX.H .       ANZ. VERSCHL. PARAMETER
         LA    R14,1(,R14)
         STH   R14,P&SYSNDX.H
         BCTR  R0,0
         LTR   R0,R0 .            ENDE DER PARAMETERLISTE ?
         BNH   P&SYSNDX.90 .          JA
&N       SETA  &N+1
         AIF   (&N LE &NPARM).A002
         LA    R15,&NPARM .            MEHR PARAMETER ALS VORGESEHEN
         LNR   R15,R15
         B     P&SYSNDX.90+4
P&SYSNDX.H   DC    H'0'
P&SYSNDX.D   DS D
P&SYSNDX.PCK PACK P&SYSNDX.D,0(,R1)
P&SYSNDX.90  LH    R15,P&SYSNDX.H .       ANZ. VERSCHL. PARAMETER
         MEND
