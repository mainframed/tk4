SYSTSO   TITLE 'SYSTSO,ASHEGE,ANZEIGEN PROGRAMM-VORSPANN'
*
**********
**********  BESCHREIBUNG *******
* MIT DIESEM PROGRAMM WIRD DER VORSPANN EINES AUF DER
* SYS1.SYSSOUR GESPEICHERTEN SYMBOLISCHEN PROGRAMMES
* AUF DEM BILDSCHIRM ANGEZEIGT.
*
* DER AUFRUF ERFOLGT MIT DEM COMMAND  SHEAD.
* ANSCHLIESSEND MUSS DER PROGRAMMNAME EINGEGEBEN WERDEN.
* DAS PROGRAMM IST REENTERABLE GESCHRIEBEN.
*
*  R0
*  R1    WORK
*  R2    WORK
*  R3    WORK
*  R4    WORK
*  R5    WORK
*  R6    WORK
*  R7    WORK
*  R8    BASIS FUER DSECT DEFINITIONEN
*  R9    POINTER TO PDL   ANSWER AREA FOR PARSE
*  R10   POINTER TO DAPB  DAIR PARAMETER BLOCK
*  R11   POINTER TO CPPL  COMMAND PROCESSOR PARAMETER LIST
*  R12   BASIS
*  R13   BASIS
*  R14   SPRUNG
*  R15   RETURN
*
         EJECT
SHEAD  CSECT
         PRINT GEN
         GADBEGIN R12,SAVE=RENT
         SPACE
         LR    R11,R1                   ADRESSE VON CPPL
         USING CPPL,R11
         SPACE 3
         GETMAIN R,LV=LDEFIN,SP=1       DEFINITIONS-BEREICH
*
         LR    R2,R1                    LOESCHE DEFINITIONS-BEREICH
         LA    R3,LDEFIN
         SR    R5,R5
         MVCL  R2,R4
*
         LR    R8,R1                    ADRESSIERUNG DER DEFINITIONEN
         USING DEFIN,R8
*
*  PRUEFUNG DER EINGABE DURCH PARS-ROUTINE
*
         LA    R1,PARMLIST              PARSE-PARAMETER-LIST
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT           USER-PROFILE-TABLE
         MVC   PPLECT,CPPLECT           ENVIRONMENT-CONTROL-TABLE FUER
*                                       PUTLINE,GETLINE,PUTGET
         LA    R2,ECB                   COMMAND-PROCESSOR-ECB
         ST    R2,PPLECB
*
         MVC   PPLPCL,APCL              PARAMETER-CONTROL-LIST FUER
*                                       IKJPARM (PARSE)
         LA    R2,ADDRPDL               PARAMETER-DESCRIPTOR-LIST
*                                       IN IKJPARM
         ST    R2,PPLANS                ANSWER AREA
         SPACE
         MVC   PPLCBUF,CPPLCBUF         COMMAND-BUFFER
*                                       USER-WORK-AREA BLEIBT FREI
         SPACE
         LINK  EP=IKJPARS
         LTR   R15,R15
         BZ    PARSGOOD
         LA    R1,100(R15)              PARSE-ROUTINE LAEUFT FALSCH
         ABEND (R1)
         SPACE 2
PARSGOOD DS    0H
         L     R9,ADDRPDL               PARAMETER-DESCRIPTOR-LIST
         USING IKJPARMD,R9
*
* ALLOCATE THE LIBRARIAN MASTER-DATASET AND LOAD DAIR AND FAIR
*
         LA    R10,DAPB                 DAIR-PARAMETER-BLOCK
         USING DAPB08,R10               FOR ENTRY-CODE X'08'
         LOAD  EP=IKJDAIR               LADE DAIR
         ST    R0,ADAIR                 SICHERN ENTRY-POINT
         LA    R1,PARMLIST              COMPLETE DAIR-PARAMETER-LIST
         USING DAPL,R1
         MVC   DAPLPSCB,CPPLPSCB        PROTECTED-STEP-CONTROL-BLOCK
         ST    R10,DAPLDAPB
         XC    DAPB(L'DAPB),DAPB
         MVI   DA08CD+1,X'08'           SET ENTRY-CODE 08
         LA    R2,DSNBUF                ADDRESS OF DSN-BUFFER
         ST    R2,DA08PDSN
*
         LOAD  EP=TSOFAIR               LADE FAIR
         ST    R0,AFAIR                 SICHERN ENTRY-POINT
         LA    R1,AMASTOPN
         LR    R15,R0
         BALR  R14,R15                  GET ENTRY MASTOPEN/MOD/CLOSE
*
         MVC   IOPL(12),PARMLIST        SETZE IN IOPL UPT,ECT,ECB
         LA    R3,PGPB
         ST    R3,IOPLIOPB              UND POINTER ZUM PGPB
         LOAD  EP=IKJPTGT               LADE PUTGET-ROUTINE
         ST    R0,APUTGET               SICHERN ENTRY-POINT
*
         LOAD  EP=IKJPUTL               LADE PUTLINE-ROUTINE
         ST    R0,APUTL                 SICHERN ENTRY-POINT
*
         MVC   DSN(L'DSN),=CL50' '
         MVC   DSN(12),=C'SYS1.SYSSOUR' DSN-MASTER
         MVI   DSNLEN+1,X'0C'           DSN-MASTER-LAENGE
         MVC   DA08DDN,=C'MASTER  '     SET DD-NAME
         MVC   DA08UNIT,BLANKS          SET UNIT TO BLANKS
         MVC   DA08SER,BLANKS           SET VOL-SER-NR TO BLANKS
         MVC   DA08MNM,BLANKS           SET MEMBER-NAME TO BLANKS
         MVI   DA08DSP1,DA08SHR         SET DISPOSITION
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         LA    R1,PARMLIST              DAIR-PARAMETER-LIST
         L     R15,ADAIR
         BALR  R14,R15                  INVOKE DAIR
         LTR   R15,R15
         BZ    DAIRGOOD
         LA    R1,200(R15)              ALLOCATION HAT NICHT GEKLAPPT
         ABEND (R1)
         SPACE 2
*
* OPEN MASTER UND SUCHE DAS GEWUENSCHTE MODUL
*
DAIRGOOD DS    0H
         XC    FAIRPARM(4),FAIRPARM     CLEAR PARAMETER FOR OPEN
         LA    R2,OPFLAG
         ST    R2,FAIRPARM+4
         OI    FAIRPARM+4,X'80'
         LA    R1,FAIRPARM
         L     R15,AMASTOPN
         BALR  R14,R15
         NI    FAIRPARM+4,X'7F'
         SPACE 2
         CLI   OPFLAG,C'0'
         BE    OPENGOOD
         LA    R1,300(R15)              OPEN HAT NICHT GEKLAPPT
         ABEND (R1)
         SPACE 2
OPENGOOD DS    0H
         LH    R2,PMODNAME+4            LAENGE DES MODUL-NAMENS
         BCTR  R2,0
         L     R3,PMODNAME              ADRESSE DES MODUL-NAMENS
SPMNM    MVC   FMODNAME,BLANKS
         EX    R2,UEBMNM                SPEICHERN MODUL-NAME
*
*
         LA    R3,FMODNAME
         ST    R3,FAIRPARM
         LA    R3,MFLAG
         ST    R3,FAIRPARM+4
         LA    R3,FAIRINFO
         ST    R3,FAIRPARM+8
         LA    R3,0
         ST    R3,FAIRPARM+12
         LA    R3,MJCL
         ST    R3,FAIRPARM+16
         OI    FAIRPARM+16,X'80'
         LA    R1,FAIRPARM
         L     R15,AMASTMOD
         BALR  R14,R15
         NI    FAIRPARM+16,X'7F'
         CLI   MFLAG,C'0'
         BE    MODGOOD                  MODULE WURDE GEFUNDEN
         CLI   MFLAG,C'1'
         BE    MODNOTF                  MODULE NICHT GEFUNDEN
         LA    R1,400(R15)
         ABEND (R1)
         SPACE 2
UEBMNM   MVC   FMODNAME(0),0(R3)        SPEICHERN MODULE-NAMEN
         SPACE
*
* AUSGABE DER HISTORY UND JCL AUF BILDSCHIRM
*
MODGOOD  DS    0H
         ST    R1,MLEVEL
         MVC   ZHEAD(38),KHEAD
         MVC   ZBLANK1(5),KBLANK
         MVC   ZMODULE(49),KMODULE
         MVC   ZDESC(49),KDESC
         MVC   ZMAST(49),KMAST
         MVC   ZADD(49),KADD
         MVC   ZUPD(49),KUPD
         MVC   ZACC(49),KACC
         MVC   ZNUPD(49),KNUPD
         MVC   ZBLANK2(5),KBLANK
         MVC   ZPASS(49),KPASS
         MVC   ZLANG(49),KLANG
         MVC   ZLEVEL(49),KLEVEL
         MVC   ZPROGR(49),KPROGR
         MVC   ZPROC(49),KPROC
         MVC   ZPROC2(49),KPROC2
         MVC   ZACCT(49),KACCT
         MVC   ZDDN(49),KDDN
         MVC   ZDSN(49),KDSN
         MVC   ZMNM(49),KMNM
         MVC   ZDISP(49),KDISP
         MVC   ZJOB(49),KJOB
         MVC   ZCLASS(49),KCLASS
         MVC   ZMSG(49),KMSG
*
         LA    R2,ABLANK1
         ST    R2,AHEAD
         LA    R2,AMODULE
         ST    R2,ABLANK1
         LA    R2,ADESC
         ST    R2,AMODULE
         LA    R2,AMAST
         ST    R2,ADESC
         LA    R2,AADD
         ST    R2,AMAST
         LA    R2,AUPD
         ST    R2,AADD
         LA    R2,AACC
         ST    R2,AUPD
         LA    R2,ANUPD
         ST    R2,AACC
         LA    R2,ABLANK2
         ST    R2,ANUPD
         LA    R2,APASS
         ST    R2,ABLANK2
         LA    R2,ALANG
         ST    R2,APASS
         LA    R2,ALEVEL
         ST    R2,ALANG
         LA    R2,APROGR
         ST    R2,ALEVEL
         LA    R2,APROC
         ST    R2,APROGR
         LA    R2,APROC2
         ST    R2,APROC
         LA    R2,AACCT
         ST    R2,APROC2
         LA    R2,ADDN
         ST    R2,AACCT
         LA    R2,ADSN
         ST    R2,ADDN
         LA    R2,AMNM
         ST    R2,ADSN
         LA    R2,ADISP
         ST    R2,AMNM
         LA    R2,AJOB
         ST    R2,ADISP
         LA    R2,ACLASS
         ST    R2,AJOB
         LA    R2,AMSG
         ST    R2,ACLASS
         LA    R2,0
         ST    R2,AMSG
*
*
         MVC   TMODULE(8),FMODNAME
         MVC   TPROGR(15),FPROGR
         MVC   TPASS(4),FPASS
         MVC   TLANG(3),FLANG
MODG5    DS    0H
         MVC   TDESC(30),FDESC
         MVC   TADD(27),MDAT
         MVC   TADD+4(2),FADD+2
         MVC   TADD+14(2),FADD
         MVC   TADD+24(2),FADD+4
         MVC   TUPD(27),MDAT
         MVC   TUPD+4(2),FUPD+2
         MVC   TUPD+14(2),FUPD
         MVC   TUPD+24(2),FUPD+4
         MVC   TUPD+28(8),=C'UHRZEIT '
         MVC   TUPD+36(2),FUPD+6
         MVI   TUPD+38,X'4B'
         MVC   TUPD+39(2),FUPD+8
         MVC   TPROC(55),MJCL+54
         MVC   TPROC2(55),MJCL+250
         MVC   TCLASS(1),MJCL+109
         MVC   TMSG(1),MJCL+320
         MVC   TDSN(44),MJCL+139
         MVC   TACCT(8),MJCL+195
         MVC   TJOB(8),MJCL+310
         MVC   TMNM(8),MJCL+183
         MVC   TDISP(4),MJCL+191
         MVC   TDDN(8),MJCL+131
         MVC   TMAST(12),=C'SYS1.SYSSOUR'
         LH    R2,FACC
         CVD   R2,DOUBLE
         MVC   TACC(9),MASK
         ED    TACC(9),DOUBLE+4
         LH    R2,FNUPD
         CVD   R2,DOUBLE
         MVC   TNUPD(9),MASK
         ED    TNUPD(9),DOUBLE+4
         CLI   MLEVEL+3,X'01'
         BE    MODG3
         CLI   MLEVEL+3,X'00'
         BE    MODG2
         MVC   TLEVEL(5),=C'PROD2'
         B     MODG1
*
MODG3    MVC   TLEVEL(5),=C'PROD1'
         B     MODG1
*
MODG2    MVC   TLEVEL(4),=C'TEST'
*
MODG1    DS    0H
*
         MVC   PGPB(12),PUTBLOK
         L     R15,APUTL
*
         PUTLINE OUTPUT=(AHEAD,MULTLIN,DATA),ENTRY=(15),MF=(E,IOPL)
*
         LTR   R15,R15
         BZ    PUTGOOD
         LA    R1,400(R15)              PUTLINE HAT NICHT GEKLAPPT
         ABEND (R1)
         SPACE 2
PUTGOOD  DS    0H
         LA    R3,FHIST                 HISTORY-KARTEN-BEREICH
         ST    R3,FAIRPARM
         LA    R3,HFLAG
         ST    R3,FAIRPARM+4
         OI    FAIRPARM+4,X'80'
         LA    R1,FAIRPARM
         L     R15,AMSTHIST
         BALR  R14,R15                  LESE HISTORY-KARTE
         NI    FAIRPARM+4,X'7F'
         CLI   HFLAG,C'1'
         BE    EXIT                     KEINE HISTORY-KARTE VORHANDEN
         CLI   HFLAG,C'0'
         BE    HISTGOOD
         LA    R1,600(R15)              GET HISTORY HAT NICHT GEKLAPPT
         ABEND (R1)
*
HISTGOOD DS    0H
         LA    R0,75
         LA    R1,FHIST+5
         MVC   FHDATE-8(8),FHDATE
         TPUT  (R1),(R0)
         LTR   R15,R15
         BZ    PUTGOOD                  GET NEXT HISTORY-CARD
         LA    R1,700(R15)
         ABEND (R1)                     TPUT HAT NICHT GEKLAPPT
*
EXIT     DS    0H
         LR    R1,R8
         FREEMAIN R,LV=LDEFIN,A=(R1),SP=1
*
         L     R15,AMASTCLS             CLOSE THE MASTER
         BALR  R14,R15
*
         GADEOJ
         SPACE 2
MODNOTF  DS    0H                       MODULE NICHT GEFUNDEN
*
         LA    R2,0                     FILL OUTPUT-LINE-DESCRIPTOR
         ST    R2,OLD1
         LA    R2,1
         ST    R2,OLD1SEGN
         LA    R2,MSG1
         ST    R2,OLD1M1
         MVC   PGPB(16),PGBLOK
         L     R15,APUTGET
         PUTGET OUTPUT=(OLD1SEGN,SINGLE,TERM),ENTRY=(15),MF=(E,IOPL)
*
         LTR   R15,R15
         BZ    PGTGOOD
         LA    R1,500(R15)              PUTGET HAT NICHT GEKLAPPT
         ABEND R1
         SPACE 2
PGTGOOD  DS    0H
         L     R3,PGPB+12               ADR DER ANTWORT
         LH    R2,0(R3)                 LAENGE DER EINGABE
         SH    R2,=H'4'
         CH    R2,=H'8'
         BH    MODNOTF                  EINGABE IST ZU LANG
*
         LA    R3,4(R3)
         BCTR  R2,0
         OC    0(8,R3),BLANKS
         B     SPMNM
         SPACE 3
*
* KONSTANTEN FUER BILDSCHIRM-OUTPUT
*
KHEAD    DC    H'38,0',C'LISTING OF PROGRAM-HISTORY AND JCL'
KBLANK   DC    H'5,0',C' '
KMODULE  DC    H'49,0',C'MODULE NAME                                  '
KDESC    DC    H'49,0',C'DESCRIPTION                                  '
KMAST    DC    H'49,0',C'MASTER FILE                                  '
KADD     DC    H'49,0',C'ADDED                                        '
KUPD     DC    H'60,0',C'LAST UPDATE                                  '
KACC     DC    H'49,0',C'ACCESSES                                     '
KNUPD    DC    H'49,0',C'UPDATES                                      '
KPASS    DC    H'49,0',C'PASSWORD                                     '
KLANG    DC    H'49,0',C'LANGUAGE                                     '
KLEVEL   DC    H'49,0',C'PROG-LEVEL                                   '
KPROGR   DC    H'49,0',C'PROGRAMMER                                   '
KPROC    DC    H'74,0',C'PROCEDURE                                    '
KPROC2   DC    H'74,0',C'PROCEDURE 2                                  '
KACCT    DC    H'49,0',C'ACCT NUMBER                                  '
KDDN     DC    H'49,0',C'DDNAME                                       '
KDSN     DC    H'63,0',C'DSNAME                                       '
KMNM     DC    H'49,0',C'MEMBER NAME                                  '
KDISP    DC    H'49,0',C'DISP                                         '
KJOB     DC    H'49,0',C'JOB NAME                                     '
KCLASS   DC    H'49,0',C'CLASS                                        '
KMSG     DC    H'49,0',C'MSG                                          '
*
*
MDAT     DC    C'TAG     MONAT     JAHR     '
MASK     DC    X'4020202020202021'
*
PUTBLOK  PUTLINE OUTPUT=(1,MULTLIN,DATA),MF=L
*
*
APCL     DC    A(PCL)                   ADR PARAMETER-CONTROL-LISTE
*
MSG1     DC    H'29,0',C' NO SUCH MODULE. REENTER-'
*
BLANKS   DC    CL8' '
*
PGBLOK   PUTGET OUTPUT=(1,SINGLE,TERM),MF=L
*
*
         LTORG *
         SPACE 3
*
*  DEFINITIONEN, ADRESSIERUNG UEBER REGISTER 8
*
DEFIN    DSECT
         SPACE
PARMLIST DS    7F                       PPL PARSE-PARAMETER-LIST ODER
*                                       DAPL DAIR-PARAMETER-LIST
         SPACE
ECB      DS    F'0'                     COMMAND-PROCESSOR-ECB
ADDRPDL  DS    A                        ANSWER AREA FOR PARSE -
*                                       ADDRESS OF PDL
DAPB     DS    CL76                     DAIR PARAMETERBLOCK
         SPACE
DSNBUF   DS    0CL46                    DSNAME-BUFFER FOR DAIR
DSNLEN   DS    H                        LAENGE MASTER-NAME
DSN      DS    CL44                     DSNAME MASTER
         SPACE
ADAIR    DS    A                        ADRESSE ENTRY-POINT DAIR
         SPACE
AFAIR    DS    A                        ADRESSE ENTRY-POINT FAIR
AMASTOPN DS    A                        ENTRIES DER FAIR-ROUTINEN
AMASTMOD DS    A
AMASTRCD DS    A
AMSTHIST DS    A
AMASTCLS DS    A
         SPACE
FAIRPARM DS    5A                       FAIR PARAMETER-LIST
FMODNAME DS    CL8                      MODULE NAME
FAIRINFO DS    0CL100                   INFORMATIONS-BEREICH
FPASS    DS    CL4
FADD     DS    CL6
FUPD     DS    CL10
         DS    CL3
FPROGR   DS    CL15
FPROC    DS    CL8
FDESC    DS    CL30
         DS    CL6
FACC     DS    CL2
FNUPD    DS    CL2
         DS    CL2
FLANG    DS    CL3
         DS    CL9
*
         DS    0F
FHIST    DS    0CL88                    HISTORY-KARTEN-BEREICH
         DS    CL80
FHDATE   DS    CL8
*
MLEVEL   DS    A                        PROGRAMM-LEVEL
*
MFLAG    DC    C'0'                     MEMBER WOS FOUND
OPFLAG   DC    C'0'                     OPEN MASTER WAS O.K.
HFLAG    DC    C'0'                     HISTORY-CARD WAS FOUND
         SPACE 2
MJCL     DS    0CL360                   THE MODULES JOB-CONTROL-CARDS
         DS    CL360
*
APUTGET  DS    A                        ADR OF PUTGET-ROUTINE
APUTL    DS    A                        ADR OF PUTLINE-ROUTINE
*
PGPB     DS    4A                       PUTGET PARAMETER-BLOCK
*
*
IOPLADS  DS    4F
*
DOUBLE   DS    2F
*
* OUTPUT-ZEILEN FUER PUTLINE
*
         DS    0F
AHEAD    DS    A
ZHEAD    DS    H
         DS    H
         DS    CL34                     UEBERSCHRIFT
*
ABLANK1  DS    A
ZBLANK1  DS    H
         DS    H
TBLANK1  DS    CL1                      LEERZEILE
*
AMODULE  DS    A
ZMODULE  DS    H
         DS    H
         DS    CL14                     MODULE-NAME
TMODULE  DS    CL30
*
ADESC    DS    A
ZDESC    DS    H
         DS    H
         DS    CL14                     DESCRIPTION
TDESC    DS    CL30
*
AMAST    DS    A
ZMAST    DS    H
         DS    H
         DS    CL14                     MASTER FILE
TMAST    DS    CL30
*
AADD     DS    A
ZADD     DS    H
         DS    H
         DS    CL14                     ADDED TO MASTER
TADD     DS    CL30
*
AUPD     DS    A
ZUPD     DS    H
         DS    H
         DS    CL14                     LAST UPDATE
TUPD     DS    CL41
*
ABLANK2  DS    A
ZBLANK2  DS    H
         DS    H
TBLANK2  DS    CL1                      LEERZEILE
*
AACC     DS    A
ZACC     DS    H
         DS    H
         DS    CL14                     NUMBER OF ACCESSES
TACC     DS    CL30
*
ANUPD    DS    A
ZNUPD    DS    H
         DS    H
         DS    CL14                     NUMBER OF UPDATES
TNUPD    DS    CL30
*
APASS    DS    A
ZPASS    DS    H
         DS    H
         DS    CL14                     PASSWORD
TPASS    DS    CL30
*
ALANG    DS    A
ZLANG    DS    H
         DS    H
         DS    CL14                     LANGUAGE
TLANG    DS    CL30
*
ALEVEL   DS    A
ZLEVEL   DS    H
         DS    H
         DS    CL14                     PROGRAMM-LEVEL
TLEVEL   DS    CL41
*
APROGR   DS    A
ZPROGR   DS    H
         DS    H
         DS    CL14                     PROGRAMMER
TPROGR   DS    CL30
*
APROC    DS    A
ZPROC    DS    H
         DS    H
         DS    CL14                     PROCEDUR
TPROC    DS    CL55
*
APROC2   DS    A
ZPROC2   DS    H
         DS    H
         DS    CL14           PROCEDURE 2
TPROC2   DS    CL55
*
AACCT    DS    A
ZACCT    DS    H
         DS    H
         DS    CL14                     ACCOUNTING-NR
TACCT    DS    CL30
*
ADDN     DS    A
ZDDN     DS    H
         DS    H
         DS    CL14                     DD-NAME
TDDN     DS    CL30
*
ADSN     DS    A
ZDSN     DS    H
         DS    H
         DS    CL14                     DS-NAME
TDSN     DS    CL44
*
AMNM     DS    A
ZMNM     DS    H
         DS    H
         DS    CL14                     MEMBER-NAME
TMNM     DS    CL30
*
ADISP    DS    A
ZDISP    DS    H
         DS    H
         DS    CL14                     DISPOSITION
TDISP    DS    CL30
*
AJOB     DS    A
ZJOB     DS    H
         DS    H
         DS    CL14                     JOB-NAME
TJOB     DS    CL30
*
ACLASS   DS    A
ZCLASS   DS    H
         DS    H
         DS    CL14                     CLASS
TCLASS   DS    CL30
*
AMSG     DS    A
ZMSG     DS    H
         DS    H
         DS    CL14                     MSG-CLASS
TMSG     DS    CL30
*
*
IOPL     DS    0CL16                    I/O PARAMETER LIST
IOPLUPT  DS    A
IOPLECT  DS    A
IOPLECB  DS    A
IOPLIOPB DS    A
*
OLD1     DS    A                        OUTPUT LINE DESCRIPTOR
OLD1SEGN DS    F
OLD1M1   DS    A
*
LDEFIN   EQU   *-DEFIN
         SPACE 3
*
*  DUMMY-SECTIONS
*
         IKJCPPL
         SPACE 3
         IKJPPL
         SPACE 3
         IKJDAPL
         SPACE 3
         IKJDAP08
         SPACE 3
         IKJDAP18
         SPACE 3
DUMMY    CSECT
PCL      IKJPARM
PMODNAME IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,  *
               PROMPT='MODULE NAME',                                   *
               HELP='NAME EINES PROGRAMMES AUF DER SYS1.SYSSOUR'
         IKJENDP
         END
  PUNCH ' NAME TESTHEAD(R)'
         END
