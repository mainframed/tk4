COPY     TITLE 'TAPECOPY  IBM,KUENNEMANN  COPY TAPES AT ALL'
* FULL TAPE COPY
TAPECOPY CSECT
         GADBEGIN
 SPACE 2
DEN2     EQU   XF83                     DENSITY 2
DEN3     EQU   XFC3                     DENSITY 3
DEN4     EQU   XFD3                     DENSITY 4
MS800    EQU   XFCB                     MODESET CMD FOR 800 BPI
MS1600   EQU   XFC3                     FOR 1600 BPI
MS6250   EQU   XFD3                     FOR 6250 BPI
 SPACE 2
         OPEN  (INPUT,(INPUT),OUTPUT,(OUTPUT))
         TESTOPEN INPUT
         TM    OUTPUT+48,16             TEST AVAILABLE
         BO    OUTAVAIL                 SKIP WTO
         WTO   MF=(E,OUTNA)             WRITE NOTE
OUTAVAIL DS    0H
         CLI   OUTPUT+18,DEN2           800 BPI
         BE    SETDEN2
         CLI   OUTPUT+18,DEN3           1600 BPI
         BE    SETDEN3
         CLI   OUTPUT+18,DEN4           6250 BPI
         BE    SETDEN4
         WTO   MF=(E,DENWTO)            UNKNOWN DENS
         B     SETDEN3
SETDEN2  MVI   WRMODE,MS800             SET 800 BPI
         B     GETMAIN
SETDEN3  MVI   WRMODE,MS1600            SET 1600 BPI (DEFAULT)
         B     GETMAIN
SETDEN4  MVI   WRMODE,MS6250            SET 6250 BPI
 SPACE 2
GETMAIN  DS    0H
 SPACE 2
         GETMAIN R,LV=32768,SP=0  GET BUFFER SPACE
         STCM  R1,7,READCCW+1           STORE IN CCW
         STCM  R1,7,WRITECCW+1          STORE IN CCW
BLKSUMM  EQU   R8
         SR    BLKSUMM,BLKSUMM          ZERO BLOCKLEN
         SPACE 2
READ01   BAL   R9,READ                  GET A RECORD
         BAL   R9,CHECKR                CHECK FOR COMPLETION
         AP    RCOUNT,=PL1'1'
         BAL   R9,CALCLEN               GET READ LEN
         TM    OUTPUT+48,16             TEST IF OUTPUT AVAIL
         BNO   READ01                   NO SKIP WRITE
         BAL   R9,WRITE                 WRITE OUT
         BAL   R9,CHECKW                CHECKFOR COMPLETION
         AP    WCOUNT,=PL1'1'
         B     READ01
 SPACE 2
READ     DS    0H
         XC    ECB01,ECB01              CLEAR ECB
         EXCP  IOB01                    EXEC CHANNEL PGM
         BR    R9
 SPACE 2
CHECKR   DS    0H
         WAIT  ECB=ECB01                AWAIT CP TO COMPLETE
         CLI   ECB01,XF7F               SUCCESSFULL ?
         BNE   CHECKTM           TAPE MARK ?
         BR    R9
 SPACE 2
WRITE    DS    0H
         XC    ECB02,ECB02              CLEAR WRITE ECB
         EXCP  IOB02                    EXEC CP FOR WRITE
         BR    R9                       RETURN
 SPACE 2
CHECKW   DS    0H
         WAIT  ECB=ECB02                WAIT FOR WRITE TO CP
         CLI   ECB02,XF7F               SUCCESSFULL ?
         BNE   ABENDW                   NO ABEND
         BR    R9
 SPACE 2
CALCLEN  DS    0H
         L     R3,IOBCSW+3              GET REST LEN FROM CSW
         LA    R3,0(,R3)                CLEAR HIGH ORDER BYTE
         L     R2,MAXLEN                GET LEN FROM READ CSW
         SR    R2,R3                  R2 SHOULD HAVE THE READ COUNT
         LTR   R2,R2                    MAY BE ZERO
         BNP   CHECKTM                  CHECK FOR TM READ
         STCM  R2,7,WRITECCW+5          SAVE IN WRITE CSW
         AR    BLKSUMM,R2               SUMMARIZE BLOCKLEN
         BR    R9
MAXLEN   DC    AL4(32767)               MAX READ LEN
 SPACE 2
SKIPBLK  DS    0H                       SKIP ONE BLOCK FORW
         XC    FSBECB,FSBECB            CLEAR ECB
         EXCP  FSBIOB                   EXEC CHANNEL PGM
         WAIT  ECB=FSBECB               WAIT FOR COMPLETION
         CLI   FSBECB,XF7F              CHECK FOR COMPLETION
         BE    SKIPOK                   DONE
         WTO   MF=(E,SKPWTO)            TERM WITH ERROR MSG
         ABEND 001,DUMP                 EXIT
SKIPOK   DS    0H
         BR    R9                       RETURN TO CALLER
 SPACE 2
CHECKTM  DS    0H
         TM    IOBCSW+3,UX              UNIT EXCEPTION ?
         BNO   ABENDR                   NO ABEND
         CLC   RCOUNT,=PL4'0'           COUNT ZERO?
         BE    CLOSE
         TM    OUTPUT+48,16             OUTPUT DCB OPEN
         BNO   NOTM                     NO SKIP WTM
         XC    WTMECB,WTMECB
         EXCP  WTMIOB                   WRITE TAPE MARK
         WAIT  ECB=WTMECB
         CLI   WTMECB,XF7F
         BNE   ABENDW                   INVALID
NOTM     DS    0H
         AP    FCOUNT,=PL1'1'           UP FILE COUNT
         UNPK  CFILE(4),FCOUNT(4)       CURRENT FILE COUNT
         OI    CFILE+3,XFF0
         UNPK  CREAD(6),RCOUNT(4)       CURRENT READS
         OI    CREAD+5,XFF0
         CVD   BLKSUMM,BCOUNTX          GET IT PACKED
         MP    BCOUNT(16),=P'100'      MULT BY 100
         DP    BCOUNT(16),RCOUNT        DIVIDE BY READS
         MVC   BAVRG(9),MASK            MOVE MSK
         ED    BAVRG(9),BCOUNT+8        EDIT
         MVC   BCOUNT(8),=8X'00'        REINIT BCOUNT
         SR    BLKSUMM,BLKSUMM          REINIT SUMM REG
         WTO   MF=(E,CWTO)              WRITE MSG
         AP    RSUMM,RCOUNT
         AP    WSUMM,WCOUNT
         SP    RCOUNT,RCOUNT
         SP    WCOUNT,WCOUNT
         B     READ01                   ENTER READ AGAIN
 SPACE 2
CLOSE    DS    0H
         BAL   R9,STAT
         CLOSE (INPUT,,OUTPUT)
         GADEOJ
 SPACE 2
ABENDR   DS    0H
         WTO   MF=(E,RERROR)            WRITE READ ERROR MSG
         XC    WTOECB,WTOECB
ASKACT   WTOR  MF=(E,RACTION)           WHAT TO DO
         WAIT  ECB=WTOECB               WAIT FOR ANSWER
         CLC   ANSWER(3),=C'END'        CHECK FOR END
         BE    CLOSE                    OK CLOSE
         CLC   ANSWER(2),=C'GO'         CHECK FOR GO
         BNE   ANSERR                   ERROR ON INPUT
         BAL   R9,SKIPBLK               SKIP ONE BLOCK
         B     READ01                   REENTER READ
         SPACE 2
ANSERR   WTO   MF=(E,ANSWTO)            WRITE NOTE AND HELP
         B     ASKACT                   ASK FOR ACTION
ABENDW   DS    0H
         BAL   R9,STAT
         ABEND 222,DUMP
 EJECT
STAT     DS    0H
         UNPK  FILES(4),FCOUNT(4)       GET FILE COUNT
         OI    FILES+3,XFF0
         UNPK  READS(6),RSUMM(4)       READ COUNT
         OI    READS+5,XFF0
         UNPK  WRITS(6),WSUMM(4)        WRITE COUNT
         OI    WRITS+5,XFF0
         WTO   MF=(E,FWTO)
         WTO   MF=(E,RWTO)
         WTO   MF=(E,WWTO)
         BR    R9                       RETURN
         DS    0F
         DC    CL8'COUNTS'
FCOUNT   DC    PL4'0'                   FILE COUNT
RCOUNT   DC    PL4'0'                   READ COUNT
WCOUNT   DC    PL4'0'                   WRITE COUNT
RSUMM    DC    PL4'0'
WSUMM    DC    PL4'0'
         SPACE 2
CWTO     WTO   '**** FILE NR:  XXXX , READS XXXXXX , AVERG BLOCK SIZE :*
                XXXXX.XX ',ROUTCDE=(11),MF=L
FWTO     WTO   '**** NUMBER OF FILES PROCESSED : XXXX',                *
               ROUTCDE=(11),MF=L
RWTO     WTO   '**** NUMBER OF TOTAL READS :   XXXXXX',                *
               ROUTCDE=(11),MF=L
WWTO     WTO   '**** NUMBER OF TOTAL WRITES:   XXXXXX',                *
               ROUTCDE=(11),MF=L
OUTNA    WTO   '**** OUTPUT DD N/A, OUTPUT IGNORED ',                  *
               ROUTCDE=(11),MF=L
RERROR   WTO   'TAPECOPY  PERMANENT I/O ERROR ON INPUT TAPE ',         *
               ROUTCDE=(3,4,5,11),MF=L
RACTION  WTOR  'TAPECOPY ENTER  END  OR  GO',                          *
               ANSWER,4,WTOECB,ROUTCDE=(1,3,4,5,11),MF=L
ANSWTO   WTO   'TAPECOPY ENTER END TO TERMINATE OR GO TO SKIP ONE BLOCK*
               ',ROUTCDE=(3,4,5,11),MF=L
SKPWTO   WTO   'TAPECOPY PERMANENT I/O ERROR DURING SKIP BLOCK OPERATIO*
               N, TC TERMINATED',ROUTCDE=(3,4,5,11),MF=L
DENWTO   WTO   'TAPECOPY UNKNOWN DENSITY, DEFAULT (1600) USED',        *
               ROUTCDE=(3,4,5,11),MF=L
         SPACE 2
CFILE    EQU   CWTO+19                  OFFSET TO FILE COUNT
CREAD    EQU   CWTO+32
BAVRG    EQU   CWTO+60                  AVERAGE BLOCK SIZE
FILES    EQU   FWTO+37
READS    EQU   RWTO+35
WRITS    EQU   WWTO+35
         SPACE 2
MASK     DC    X'4020202021204B2020'    EDIT MASK
ANSWER   DC    CL10' '                  ANSWER AREA FOR WTORS
BCOUNT   DC    D'0'                     FIRST HALF BLOCK CNT
BCOUNTX  DC    D'0'                     SECOND HALF
         LTORG
         DC    CL4'ECBS'
ECB01    DC    F'0'                     ECB FOR READ
ECB02    DC    F'0'                     ECB FOR WRITE
WTMECB   DC    F'0'
FSFECB   DC    F'0'
WTOECB   DC    F'0'                     ECB FOR WTORS
FSBECB   DC    F'0'                     ECB FOR FSB IOB
 SPACE 2
         DC    CL4'IOBR'
IOB01    DC    X'42',AL3(0)             WORD 1
IOBECBPT DC    A(ECB01)                 ADDRESS OF ECB
IOBFLAG3 DC    X'00'
IOBCSW   DC    AL3(0),AL4(0)            CHANNEL STATUS WORD
IOBSTART DC    A(READCHAN)              START OF READ CHAIN
IOBDCBPT DC    A(INPUT)                 DCB ADDRESS
         DC    2F'0'                    REST OF IOB
 SPACE 2
         DC    CL4'IOBW'
IOB02    DC    X'42',AL3(0)             WORD 1
         DC    A(ECB02)                 ADDRESS OF ECB
         DC    X'00'
         DC    AL3(0),AL4(0)            CHANNEL STATUS WORD
         DC    A(WRITCHAN)              START OF WRITECHAIN
         DC    A(OUTPUT)                DCB ADDRESS
         DC    2F'0'                    REST OF IOB
 SPACE 2
         DC    CL4'WTMI'
WTMIOB   DC    X'42',AL3(0)             WORD 1
         DC    A(WTMECB)                ADDRESS OF ECB
         DC    X'00'
         DC    AL3(0),AL4(0)            CHANNEL STATUS WORD
         DC    A(WTMCHAN)               START OF WTM  CHAIN
         DC    A(OUTPUT)                DCB ADDRESS
         DC    2F'0'                    REST OF IOB
 SPACE 2
         DC    CL4'FSBI'
FSBIOB   DC    X'42',AL3(0)             WORD 1
         DC    A(FSBECB)                ADDRESS OF ECB
         DC    X'00'
         DC    AL3(0),AL4(0)            CHANNEL STATUS WORD
         DC    A(FSBCHAN)               START OF FSB  CHAIN
         DC    A(INPUT)                 DCB ADDRESS
         DC    2F'0'                    REST OF IOB
 SPACE 2
         DS    0D                       ALIGN ON DOUBLE WORD
 SPACE 2
CD       EQU   X'80'                    CHAIN DATA
CC       EQU   X'40'                    CHAIN COMMAND
SLI      EQU   X'20'                    SUPPRESS INC LEN
SKIP     EQU   X'10'                    SUPPRESS DATA TRANSF
PCI      EQU   X'08'                    GENERATE PCI
IDA      EQU   X'04'                    USE IDA LIST
         SPACE 2
ATT      EQU   128                      ATTENTION
SM       EQU   64                       STATUS MODIFIER
CUE      EQU   32
BSY      EQU   16                       BUSY
CE       EQU   8                        CHANNEL END
DE       EQU   4                        DEVICE END
UC       EQU   2                        UNIT CHECK
UX       EQU   1                        UNIT EXCEPTION
 SPACE 2
READCHAN DC    X'03',AL3(0),AL1(CC),AL3(1)
READCCW  DC    X'02',AL3(0),AL1(SLI),AL3(32767)
 SPACE 2
WRMODE   DS    0D                       WILL BE MODIFIED TO MODE SET
WRITCHAN DC    X'03',AL3(0),AL1(CC),AL3(1)
WRITECCW DC    X'01',AL3(0),AL1(CC+SLI),AL3(0)
WRITENOP DC    X'03',AL3(0),AL1(00),AL3(1)
 SPACE 2
WTMCHAN  DC    X'03',AL3(0),AL1(CC),AL3(1)
WTMCCW   DC    X'1F',AL3(0),AL1(SLI),AL3(1)
 SPACE 2
FSBCHAN  DC    X'03',AL3(0),AL1(CC),AL3(1)
FSBCCW   DC    X'27',AL3(0),AL1(CC),AL3(1)
         DC    X'37',AL3(0),AL1(CC),AL3(1)
         DC    X'37',AL3(0),AL1(SLI),AL3(1)
 EJECT
INPUT    DCB   DDNAME=INPUT,DSORG=PS,MACRF=E
OUTPUT   DCB   DDNAME=OUTPUT,DSORG=PS,MACRF=E
         END
