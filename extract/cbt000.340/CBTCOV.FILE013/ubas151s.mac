TPTP     START
         REG
         XSAVE R12,SVA,TPTP
         USING FUNCTAB,R2
         LR    R2,R1
         LINK  EP=DESETUP,PARAM=((R2),SYSUT1,SYSUT2),VL=1
         B     *+4(R15)
         B     *+8
         B     NODE
         CLC   FUNCFNAM,=CL8'TPTP8I'
         BNE   TST8O
         MVI   SYSUT1+18,X'83'
         B     DE1
TST8O    EQU   *
         CLC   FUNCFNAM,=CL8'TPTP8O'
         BNE   TST88
         MVI   SYSUT2+18,X'83'
         B     DE1
TST88    EQU   *
         CLC   FUNCFNAM,=CL8'TPTP88'
         BNE   DE1
         MVI   SYSUT1+18,X'83'
         MVI   SYSUT2+18,X'83'
DE1      EQU   *
         MVC   JFCB(44),=CL44'TPTP.INPUT.TPTP.INPUT'
         MVC   JFCB1(44),=CL44'TPTP.OUTPUT.TPTP.OUTPUT'
         XC    ECB,ECB
         WTOR  'TPTP000 ENTER 5 CHAR. BLKSIZE OR ''U''',REPLY,5,ECB,   *
               ROUTCDE=(2,3)
         WAIT  ECB=ECB
         OC    REPLY,=CL5' '
         CLI   REPLY,C'U'
         BE    DEFAULT
         PACK  DBLWD,REPLY(5)
         CVB   R15,DBLWD
STORE    EQU   *
         STH   R15,SYSUT1+62
         STH   R15,SYSUT2+62
         B     NODE
DEFAULT  EQU   *
         LH    R15,=H'4096'
         B     STORE
NODE     EQU   *
         SR    R4,R4
         SR    R5,R5
         CLC   JFCB1+68(2),=H'1'
         BNH   SEQOKZ
         CLC   JFCB+68(2),=H'1'
         BH    SEQOKZ
         OI    SWITCH,IGNVOL1
SEQOKZ   EQU   *
         MVI   JFCB1+69,X'00'
         MVI   JFCB+69,X'00'
         OPEN  (SYSUT2,,SYSUT1),TYPE=J
         GET   SYSUT2
         CLC   SYSUT2+82(2),=H'80'
         BNE   NLOUT
         CLC   0(4,R1),=CL4'VOL1'
         BNE   NLOUT
         MVC   VOLSER,4(R1)
         B     GETUT1
NLOUT    EQU   *
         OI    SWITCH,OUTNL
GETUT1   EQU   *
         GET   SYSUT1
         CLC   SYSUT1+82(2),=H'80'
         BNE   NLIN
         CLC   0(4,R1),=CL4'VOL1'
         BE    CLSTST
NLIN     EQU   *
         OI    SWITCH,INNL
         TM    SWITCH,OUTNL
         BO    CLSTST
         WTO   'TPTP004 UNABLE TO COPY NL-INPUT TO SL-OUTPUT',         *
               ROUTCDE=(2,3)
         B     RETURN
CLSTST   EQU   *
         CLOSE (SYSUT2,REREAD,SYSUT1,REREAD)
         LA    R15,EODAD
         ST    R15,SYSUT1+32
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT))
         RDJFCB SYSUT1
         RDJFCB SYSUT2
         SR    R3,R3
NEXTREC  EQU   *
         GET   SYSUT1
         TM    SWITCH,INNL
         BO    PUTX
         CLI   FLIPFLOP,X'02'
         BE    COUNT
         CLC   0(4,R1),=CL4'VOL1'
         BNE   TSTHDR1
         MVC   4(6,R1),VOLSER
         TM    SWITCH,OUTNL
         BO    NEXTREC
         TM    SWITCH,IGNVOL1
         BNO   PUTX
         NI    SWITCH,255-IGNVOL1
         B     NEXTREC
TSTHDR1  EQU   *
         CLC   0(4,R1),=CL4'HDR1'
         BNE   TSTHDR2
         MVC   21(6,R1),VOLSER
         B     TSTOUTL
TSTHDR2  EQU   *
         CLC   0(4,R1),=CL4'HDR2'
         BNE   TSTEOF1
         MVI   FLIPFLOP,X'01'
         B     TSTOUTL
TSTEOF1  EQU   *
         CLC   0(4,R1),=CL4'EOF1'
         BNE   TSTEOF2
         MVC   21(6,R1),VOLSER
         CVD   R4,DBLWD
         UNPK  54(6,R1),DBLWD
         OI    59(R1),X'F0'
         B     TSTOUTL
TSTEOF2  EQU   *
         CLC   0(4,R1),=CL4'EOF2'
         BE    TSTOUTL
         CLC   0(4,R1),=CL4'EOV1'
         BNE   TSTEOV2
         TM    SWITCH,OUTNL
         BO    RETURN
         MVC   21(6,R1),VOLSER
         B     TSTOUTL
TSTEOV2  EQU   *
         CLC   0(4,R1),=CL4'EOV2'
         BNE   TSTUHL
         MVI   FLIPFLOP,X'00'
         B     TSTOUTL
TSTUHL   EQU   *
         CLC   0(3,R1),=CL3'UHL'
         BE    TSTOUTL
         CLC   0(3,R1),=CL3'UTL'
         BE    TSTOUTL
         TM    SWITCH,OUTNL
         BO    NEXTREC
LBLERR   EQU   *
         WTO   'TPTP005 INVALID LABEL ENCOUNTERED. COPY TO NL-TAPE',   *
               ROUTCDE=(2,3)
         B     RETURN
TSTOUTL  EQU   *
         TM    SWITCH,OUTNL
         BNO   PUTX
         BO    NEXTREC
COUNT    EQU   *
         LA    R4,1(R4)
PUTX     EQU   *
         MVC   SYSUT2+82(2),SYSUT1+82
         PUTX  SYSUT2,SYSUT1
         B     NEXTREC
EODAD    EQU   *
         TM    SWITCH,INNL
         BO    CLSEOD
         CLI   FLIPFLOP,X'00'
         BNE   GOON
         CVD   R3,DBLWD
         UNPK  WTO1+16(3),DBLWD
         OI    WTO1+18,X'F0'
WTO1     WTO   'TPTP002 123 FILES HAVE BEEN COPIED',ROUTCDE=(2,3)
         B     RETURN
GOON     EQU   *
         IC    R5,FLIPFLOP
         LA    R5,1(R5)
         STC   R5,FLIPFLOP
         CLI   FLIPFLOP,X'04'
         BNE   NORST
         MVI   FLIPFLOP,X'00'
NORST    EQU   *
         CLI   FLIPFLOP,X'02'
         BNE   CLSEOD
         SR    R4,R4
CLSEOD   EQU   *
         CLOSE (SYSUT1,LEAVE,SYSUT2,LEAVE)
         LA    R3,1(R3)
         LH    R15,JFCB+68
         LTR   R15,R15
         BNZ   OK
         LA    R15,1(R15)
OK       EQU   *
         LA    R15,1(R15)
         STH   R15,JFCB+68
         TM    SWITCH,OUTNL
         BNO   OK2
         TM    SWITCH,INNL
         BO    OK2
         CLI   FLIPFLOP,X'03'
         BNE   OPEN
OK2      EQU   *
         LH    R15,JFCB1+68
         LTR   R15,R15
         BNZ   OK3
         LA    R15,1(R15)
OK3      EQU   *
         LA    R15,1(R15)
         STH   R15,JFCB1+68
OPEN     EQU   *
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT)),TYPE=J
         B     NEXTREC
SYNAD    EQU   *
         LA    R1,SYSUT1
         SYNADAF ACSMETH=QSAM
         ST    R14,SAVE14
         LA    R2,46(R1)
         MVC   0(4,R2),=XL4'004A8000'
         MVC   4(9,R2),=CL9'TPTP001'
         MVC   74(4,R2),=XL4'00006000'
         WTO   MF=(E,(R2))
         CVD   R3,DBLWD
         UNPK  WTO+16(3),DBLWD
         OI    WTO+18,X'F0'
WTO      WTO   'TPTP002 XXX FILES HAVE BEEN COPIED',ROUTCDE=(2,3)
WTOR     EQU   *
         XC    ECB,ECB
         WTOR  'TPTP003 ENTER OPTION (ABE, SKP OR ACC) FOR TPTP',      *
               ANTW,3,ECB,ROUTCDE=(2,3)
         WAIT  ECB=ECB
         NI    SYSUT1+84,X'1F'
         OC    ANTW,=CL3' '
         CLC   ANTW,=CL3'ABE'
         BNE   TSTSKP
         OI    SYSUT1+84,X'20'
         B     EXIT
TSTSKP   EQU   *
         CLC   ANTW,=CL3'SKP'
         BNE   TSTACC
         OI    SYSUT1+84,X'40'
         B     EXIT
TSTACC   EQU   *
         CLC   ANTW,=CL3'ACC'
         BNE   WTOR
         OI    SYSUT1+84,X'80'
EXIT     EQU   *
         SYNADRLS
         L     R14,SAVE14
         RETURN
RETURN   EQU   *
         CLOSE (SYSUT1,,SYSUT2)
         XRETURN 0
DCBXL1   DS    0F
         DC    X'87'
         DC    AL3(JFCB1)
JFCB1    DS    CL176
DCBXL    DS    0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
ECB      DS    F
REPLY    DS    CL5
ANTW     DS    CL3
DBLWD    DS    D
SAVE14   DS    F
SWITCH   DC    X'00'
OUTNL    EQU   X'40'
INNL     EQU   X'80'
IGNVOL1  EQU   X'20'
VOLSER   DC    CL6' '
FLIPFLOP DC    X'00'
SYSUT1   DCB   DSORG=PS,DDNAME=SYSUT1,MACRF=(GL),RECFM=U,              *
               SYNAD=SYNAD,EXLST=DCBXL,EODAD=NLIN,OPTCD=Z
SYSUT2   DCB   DSORG=PS,DDNAME=SYSUT2,MACRF=(PM,GL),RECFM=U,           *
               EXLST=DCBXL1,EODAD=NLOUT
         DEFNCTAB
         END
