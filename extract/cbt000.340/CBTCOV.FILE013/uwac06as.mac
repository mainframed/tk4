*   05/09/84 505022312  MEMBER NAME  UWAC06AS (VERSAND)     ASSEMBLER
CLEAN    TITLE 'CHAIN CLEANING PROGRAM FOR 3211 AND 3203 PRINTERS'
**********************************************************************
*  KETTEN-REINIGUNGS-PROGRAMM FUER DRUCKER. VORLAGE IST DAS PROGRAMM *
*  'IEBPTRCP' AUS OS/VS1. DIESE VERSION IST GESTRAFFT UND BEDIENT    *
*  N U R  3211- UND 3205-DRUCKER. ES LAEUFT SOWOHL UNTER MVS/370 ALS *
*  UNTER MVS/XA.                                                     *
*  DER AUFRUF ERFOLGT MIT 'S CLEAN,U=...' UBER DIE FOLGENDE PROZEDUR:*
*                                                                    *
*     //CLEAN    PROC U=                                             *
*     //IEFPROC  EXEC  PGM=CLEAN                                     *
*     //IEFRDER DD UNIT=&U                                           *
*                                                                    *
*  AUTOR: BLECKWENN, WACKER-CHEMIE MUENCHEN (NACH IBM-VORLAGE).      *
*                                                                    *
**********************************************************************
CLEAN    BEG   *
         OPEN  (DCB,(OUTPUT))      OPEN THE PRINTER
         LA    R4,DCB              DCB ADDRESS
         TM    DCB+48,X'10'        OPEN SUCCESSFUL ?
         BZ    ERROR               NO, QUIT
         L     R3,44(,R4)          DEB ADDRESS
         ICM   R4,7,33(R3)         UCB ADDRESS
         USING UCBCMSEG,R4
         TM    UCBDVCLS,X'08'      UNIT RECORD DEVICE ?
         BZ    INVDEV              NO, MESSAGE AND QUIT
         CLI   UCBTBYT4,X'0B'      3203 PRINTER ?
         BE    FOUND               YES, GO ON
         CLI   UCBTBYT4,X'09'      3211 PRINTER ?
         BNE   INVDEV              NO, MESSAGE AND QUIT
         DROP  R4
FOUND    MVC   REPLY,BLANK4        BLANK REPLY AREA FOR WTOR
         WTO   'IEB451A REMOVE THE PRINTER RIBBON AND REPLACE THE PAPER*
                WITH PART NO. 451529'
         WTOR  'IEB452A REPLY DONE OR STOP',REPLY,4,ECBWTOR1,          *
               ROUTCDE=1,DESC=2
         WAIT  ECB=ECBWTOR1        WAIT FOR WTOR
RESPEC   XC    ECBWTOR2(4),ECBWTOR2  BLANK ECB FOR EVENTUAL 2ND WTOR
         OC    REPLY,BLANK4        MAKE UPPERCASE CHARACTERS
         CLC   REPLY,STOP          IS IT 'STOP' ?
         BE    ISSTOP              YES, CLOSE AND QUIT
         CLC   REPLY,DONE          IS IT 'DONE' ?
         BE    ISDONE              YES, GO ON
         MVC   REPLY,BLANK4        BLANK REPLY AREA
         WTOR  'IEB453A INVALID REPLY-RESPECIFY DONE OR STOP',         *
               REPLY,4,ECBWTOR2,ROUTCDE=1,DESC=2
         WAIT  ECB=ECBWTOR2        WAIT FOR WTOR
         B     RESPEC              ANALYSE REPLY
*
ISDONE   EXCP  IOBREAD             READ UCS BUFFER
         WAIT  ECB=ECBREAD         WAIT FOR READ COMPLETION
         CLI   ECBREAD,X'7F'       READ SUCCESSFUL ?
         BE    EXCP1OK             YES, GO ON
         WTO   'IEB456I UCS BUFFER READ FAILED'
         B     ERROR               ELSE MESSAGE AND QUIT
EXCP1OK  EXCP  IOBLOAD             LOAD UCS BUFFER
         WAIT  ECB=ECBLOAD         WAIT FOR LOAD COMPLETION
         CLI   ECBLOAD,X'7F'       LOAD SUCCESSFUL ?
         BE    EXCP2OK             YES, GO ON
         WTO   'IEB457I UCS BUFFER LOAD FAILED'
         B     ERROR               ELSE MESSAGE AND QUIT
EXCP2OK  LA    R5,BUFFER2          INIT START ADDRESS FOR WRITE
         LA    R4,1                INIT LOOP COUNT 1
LOOP1    LA    R3,1                INIT LOOP COUNT 2
LOOP2    STCM  R5,7,CCWWRIT+1      CHANGE START ADDRESS FOR WRITE
         EXCP  IOBWRIT             WRITE BUFFER
         WAIT  ECB=ECBWRIT         WAIT FOR COMPLETION
         LA    R5,1(,R5)           INCR START ADDRESS FOR WRITE
         LA    R3,1(,R3)           INCR LOOP COUNT 2
         CH    R3,=H'6'            LOOP 2 FINISHED ?
         BH    ENDLOOP2            YES, SPACE AND CYCLE LOOP 1
         B     LOOP2               ELSE CYCLE LOOP 2
ENDLOOP2 EXCP  IOBSPAC             PUT SINGLE SPACE
         WAIT  ECB=ECBSPAC         WAIR FOR COMPLETION
         LA    R4,1(,R4)           INCR LOOP COUNT 1
         CH    R4,=H'40'           LOOP 1 FINISHED ?
         BNH   LOOP1               NO, CYCLE LOOP 1
         WTO   'IEB454A THE PRINT TRAIN HAS BEEN CLEANED'
         LA    R1,BUFFER0          ADDRESS OF UCS BUFFER
         STCM  R1,7,CCWLOAD+1      PUT INTO IOB
         EXCP  IOBLOAD             RESTORE UCS BUFFER
         WAIT  ECB=ECBLOAD         WAIT FOR COMPLETION
         CLI   ECBLOAD,X'7F'       RESTORE SUCCESSFUL ?
         BE    ISSTOP              YES, FINISH
         WTO   'IEB458I UCS BUFFER RESTORE FAILED'
         B     ERROR               ELSE MESSAGE AND QUIT
ISSTOP   CLOSE (DCB)               CLOSE THE DCB
         SR    R15,R15             RETURN CODE ZERO
         B     RETURN              GO HOME
INVDEV   EQU   *                   INVALID DEVICE TYPE
         WTO   'IEB455I INVALID DEVICE SPECIFICATION ON UNIT PARAMETER'
ERROR    TM    DCB+48,X'10'        HAS DCB BEEN OPENED ?
         BZ    RETURN2             NO, BRANCH
         CLOSE (DCB)               ELSE CLOSE DCB
RETURN2  LA    R15,12              RETURN CODE TWELVE
RETURN   SCHL  RET=(R15)           EXIT
*
IOBREAD  DC    F'0'                IOB FOR UCS BUFFER READ
         DC    A(ECBREAD)
         DC    2F'0'
         DC    A(CCWREAD)
         DC    A(DCB)
         DC    4F'0'
IOBLOAD  DC    F'0'                IOB FOR UCS BUFFER LOAD/RESTORE
         DC    A(ECBLOAD)
         DC    2F'0'
         DC    A(CCWLOAD)
         DC    A(DCB)
         DC    4F'0'
IOBWRIT  DC    F'0'                IOB FOR WRITE
         DC    A(ECBWRIT)
         DC    2F'0'
         DC    A(CCWWRIT)
         DC    A(DCB)
         DC    4F'0'
IOBSPAC  DC    F'0'                IOB FOR SINGLE SPACE
         DC    A(ECBSPAC)
         DC    2F'0'
         DC    A(CCWSPAC)
         DC    A(DCB)
         DC    4F'0'
ECBWTOR1 DC    F'0'                ECB FOR 1ST WTOR
ECBWTOR2 DC    F'0'                ECB FOR 2ND WTOR
*
ECBREAD  DC    F'0'                ECB FOR UCS READ
ECBLOAD  DC    F'0'                ECB FOR UCS LOAD/RESTORE
ECBWRIT  DC    F'0'                ECB FOR WRITE
ECBSPAC  DC    F'0'                ECB FOR SPACING
*
CCWREAD  CCW   X'0A',BUFFER0,X'20',304      READ UCS BUFFER
*
CCWLOAD  CCW   X'FB',BUFFER1,X'60',304      LOAD UCS BUFFER(NO FOLD)
         CCW   X'23',*,X'60',1              UNFOLD
         CCW   X'73',*,X'20',1              BLOCK DATA CHECK
*
CCWWRIT  CCW   X'01',BUFFER2,X'20',132      WRITE
*
CCWSPAC  CCW   X'0B',BUFFER2,X'20',1        SPACE ONE LINE
*
         ORG   *-40                SAVE SPACE FOR EXCP DCB
DCB      DCB   DDNAME=IEFRDER,MACRF=E
REPLY    DC    F'0'                WTOR REPLY AREA
BUFFER0  DC    76F'0'              UCS BUFFER
BUFFER1  DC    63AL1(*-BUFFER1+1)
         DC    177AL1(*-BUFFER1+2)
         DC    64C'0'
BUFFER2  DC    63AL1(*-BUFFER2+1)
         DC    177AL1(*-BUFFER2+2)
BUFFER2A DC    63AL1(*-BUFFER2A+1)
         DC    177AL1(*-BUFFER2A+2)
DONE     DC    C'DONE'
STOP     DC    C'STOP'
BLANK4   DC    C'    '
         REGISTER
UCB      DSECT
         IEFUCBOB
         END
