RZSYS63  CSECT
         MBASIS R3,R4,RC=(R15)
         MSPSN
         BAL   R14,UTIME
         OPEN  (I1KART,(INPUT))
         OPEN  (O1AEND,(OUTPUT))
         OPEN  (O3LIST,(OUTPUT))
         RDJFCB (O2MOD)
         MVI   JFCBMOD+87,X'80'
         OPEN  (O2MOD,(OUTPUT)),TYPE=J
LESEN    GET   I1KART,EINGABE
         MVI   SCHA,C'1'
KART1    NOP   WRITE
         LA    R5,EINGABE
         CLC   0(2,R5),=C'./'
         MVI   RETT15+3,4
         BNE   FEHLER
ERH      LA    R5,1(R5)
         CLC   0(5,R5),=CL5' ADD '
         BE    ADD
         CLC   0(5,R5),=CL5'NAME='
         BE    NAMEMV1
         CLC   0(4,R5),=CL4'SSI='
         BNE   FRAGE
         B     FRAGE
NAMEMV1  LA    R6,6(R5)
         SR    R7,R7
SUCHLN   CLC   0(1,R6),=CL1','
         BE    STELL
         CLC   0(1,R6),=CL1' '
         BE    STELL
         AH    R7,=H'1'
         LA    R6,1(R6)
         B     SUCHLN
STELL    STC   R7,NAMEMV+1
         MVC   NAME,=CL8'        '
NAMEMV   MVC   NAME,5(R5)
WEINAME  NOP   FRAGEZ
FRAGE    C     R5,=A(EINGABE+72)
         BL    ERH
         RDJFCB  (SYSUT1)
         B     WRITE
ADD      MVC   OPTLIST+2(3),=CL3'NEW'
         B     ERH
FEHLER   WTO   '*** RZSYS41 *** CONTROLKARTE FALSCH',ROUTCDE=11
         B     CLOSE
WRITE    CLC   EINGABE(2),=CL2'./'      TEST CONTROLKARTE
         BNE   PUT                      NEIN
         LA    R5,EINGABE+2             SUCHEN
         LA    R1,65                    SSI ANGABE
VERGL    CLC   0(4,R5),=CL4'SSI='       IN CONTROLKARTE
         BNE   WRITE1
         MVC   6(2,R5),U2DAT+2          TAG - MONAT - JAHR
         MVC   8(2,R5),U2DAT+5          IN
         MVC   10(2,R5),U2DAT+8         SSI EINSTELLEN
         MVC   SSI,4(R5)                UEBERTRAGEN NEUES SSI
         B     PUT
WRITE1   LA    R5,1(0,R5)               ADRESSE ERHOEHEN
         BCT   R1,VERGL                 LOOP
PUT      PUT   O1AEND,EINGABE
         MVI   KART1+1,C'0'
UMENQ    NOP   PUTMOD
         ENQ   (NAMEMOD,JFCBMOD,E,44,SYSTEM)
         MVI   UMENQ+1,C'0'
PUTMOD   EQU   *
         PUT   O2MOD,EINGABE
         B     LESEN
ENDEIN   EQU   *
         CLI   SCHA,C'0'
         BE    KEINUPD
         MVI   JFCBMOD+87,X'08'
         CLOSE O2MOD
         DEQ   (NAMEMOD,JFCBMOD,44,SYSTEM)
         CLOSE I1KART
         CLOSE O1AEND
         ENQ   (NAMEUPD,JFCBUPD,E,44,SYSTEM)
         LINK  EP=IEBUPDTE,PARAM=(OPTLIST,DDNMELST),VL=1
         ST    R15,RETT15
         OPEN  (SYSPRINT,(INPUT))
         ZAP   ZZEIL,=X'001C'
         ZAP   ZSEIT,=X'000C'
         MVI   WEINAME+1,X'F0'
GETMESS  GET   SYSPRINT,MESSUPD
         MVI   FRAGEZ+1,X'00'
ERSTUEB  NOP   TESTZEIL
         MVI   ERSTUEB+1,X'F0'
         B     DRUEBER
FRAGSTAT LA    R5,MESSUPD
WEITER   LA    R5,1(R5)
         CLC   0(5,R5),=CL5' ADD '
         BNE   FRACHAN
MVIUEB   MVI   FRAGEZ+1,X'F0'
         ZAP   ZSEIT,=X'000C'
         B     FRANAME
FRACHAN  CLC   0(8,R5),=CL8' CHANGE '
         BE    MVIUEB
FRANAME  CLC   0(5,R5),=CL5'NAME='
         BE    NAMEMV1
         C     R5,=A(MESSUPD+120)
         BL    WEITER
         B     FRAGEZ
TESTZEIL EQU   *
         CLC   MESSUPD+8(2),=CL2'./'
         BNE   FRAGEZ
ERSTCON  NOP   FRAGSTAT
         MVI   ERSTCON+1,X'F0'
FRAGEZ   NOP   DRUEBER
         CP    ZZEIL,=X'060C'
         BNE   TESTMESS
DRUEBER  EQU   *
         ZAP   ZZEIL,=X'001C'
         AP    ZSEIT,=X'001C'
         MVI   UEBER,X'8B'
         PUT   O3LIST,UEBER
         MVC   U1DSN,JFCBUPD
         MVC   U1MEMB,NAME
         MVC   U1VOL,JFCBUPD+118
         MVC   U1PAGE,=XL4'40202020'
         EDMK  U1PAGE,ZSEIT
         SH    R1,=H'6'
         MVC   0(5,R1),=C'SEITE'
         MVI   UEBER,X'11'
         PUT   O3LIST,UEBER
         PUT   O3LIST,UEBER3
TESTMESS CLC   MESSUPD+110(8),=CL8'RENUMBER'
         BE    GETMESS
         CLC   MESSUPD+102(8),=CL8'RENUMBER'
         BE    GETMESS
         CLC   MESSUPD+7(7),=CL7' SYSIN '
         BE    GETMESS
*  IN  MSG ASA- VORSCHUB ERSETZEN DURCH MASCH-CODE -09-
WRMESS   MVI  MESSUPD,X'09'
         PUT   O3LIST,MESSUPD
         AP    ZZEIL,=X'001C'
         B GETMESS
ENDMESS  EQU   *
         L     R15,RETT15
         CL    R15,=F'12'
         BH    ABEND4
         B     VERZW(R15)
VERZW    B     NORMAL
         B     FEHLER       RETURN-CODE 4
         B     NOMEMB       RETURN-CODE 8
         B     UNCORR       RETURN-CODE 12
ABEND4   ABEND 444
NOMEMB   WTO   '*** RZSYS41 *** MEMBER NOT FOUND ***',ROUTCDE=11
         B     CLOSE
UNCORR   WTO   '*** RZSYS41 *** UNDEFINED ERROR ***',ROUTCDE=11
         B     CLOSE
NORMAL   EQU   *
         DEQ   (NAMEUPD,JFCBUPD,44,SYSTEM)
CLOSE    EQU   *
         MRETURN RC=RETT15
KEINUPD  EQU   *
         WTO   '*** RZSYS41 *** KEINE EINGABE FUER UPDATE',ROUTCDE=11
         B     CLOSE
         EJECT
UTIME    MTIME ,U2TIME,U2DAT,UPRO=YES
         EJECT
FELD     DS  CL1
SCHA     DC    CL1'0'
FECB     DC  F'0'
SSI      DC    CL8' '
NAME     DC    CL8' '
RETT15   DC    F'0'
         DS    0F
JFCBUPD  DS    CL176
JFCBMOD  DS    CL176
LISTUPD  DC    X'87'
         DC    AL3(JFCBUPD)
LISTMOD  DC    X'87'
         DC    AL3(JFCBMOD)
NAMEUPD  DC    CL8'NAMEUPD '
NAMEMOD  DC    CL8'NAMEMOD '
DRUCK    DS    0CL81
VOR      DC    X'09'
EINGABE  DS    CL80
AUSGABE  DS    CL80
         DS    0F
         DS    H
FDATE    DS    F
FZEIT    DS    F
UEBER    DS    0CL121
         DC    X'13'
         DC    CL40' '
         DC    CL11'UPDATE AUF '
U1VOL    DC    CL6'XXXXXX'
         DC    CL6', DSN='
U1DSN    DC    CL32' '
         DC    CL7'MEMBER='
U1MEMB   DC    CL8'XXXXXXXX'
         DC    CL1' '
         DC    CL5' '                   SEITE DURCH EDMK
U1PAGE   DC    XL4'40202020'
UEBER3   DS    0CL121
         DC    X'11'
         DC    CL40' '
         DC    CL10'       AM'
U2DAT    DC    CL10' '
         DC    CL4'  UM'
U2TIME   DC    CL10' '
         DC    CL46' '
OPTLIST  DC    X'0003'
         DC    CL3'MOD'
         DS    0F
         DS    H
DDNMELST DC    X'0000'
ZZEIL    DC    X'000C'
ZSEIT    DC    X'000C'
MESSUPD  DS    CL121
         LTORG
         EJECT
SYSPRINT  DCB  DDNAME=SYSPRINT,MACRF=(GM),DSORG=PS,EODAD=ENDMESS
I1KART   DCB   DDNAME=I1KART,MACRF=(GM),DSORG=PS,EODAD=ENDEIN
O1AEND   DCB    DDNAME=SYSIN,MACRF=(PM),DSORG=PS
O2MOD    DCB   DDNAME=O2MOD,MACRF=(PM),DSORG=PS,EXLST=LISTMOD
O3LIST   DCB   DDNAME=O3LIST,MACRF=(PM),DSORG=PS
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=(R),DSORG=PO,EXLST=LISTUPD
         END
