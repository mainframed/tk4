SMFTYP04 CSECT
         BASER 2             BASE=R12,R11
         PRINT NOGEN
         REG
         EJECT
***********************************************************************
*                                                                     *
*             PROGRAMMABLAUF                                          *
*                                                                     *
***********************************************************************
         SPACE
START    EQU   *             PROGRAMM - ANFANG
         LM    R2,R7,0(R1)   PARAMETER-UEBERNAHME
         STM   R2,R7,APARM   PARAMETER IN VOLLWORTE SPEICHERN
         LR    R10,R2
         USING COMMON,R10
         L     R3,0(R3)
         USING IFASMF04,R3   BASE=R3 FUER DUMMY-SECTION TYP 04
         SPACE 2
         TM    COMEODID,X'FF' LETZTER  TYP 04 - SATZ  ??
         BO    NEWINTV       JA, VERZWEIGE ZUR ROUTINE BEI NEUEM INTV.
         EJECT
***********************************************************************
*             BEI ERSTEM DURCHLAUF BEGINN DER PLOTZEIT FESTLEGEN      *
***********************************************************************
         SPACE
FIRST    NOP   EVERY         NUR ERSTER DURCHLAUF
         MVC   FW1,SMF4TME   END OF STEP-TIME UEBERTRAGEN
         L     R1,COMINTV    INTV.-ZEIT NACH REG 1
         A     R1,FW1        INTV.-ZEIT  + EOS-TIME
         ST    R1,HIGHTIME   ZEITEN IN ZWISCHENFELD SPEICHERN
         OI    FIRST+1,X'F0' SCHALTER SETZEN FUER WEITERE DURCHL.
         MVC   ZDATE,SMF4DTE
         CALL  DATUM,(FDAT)
         MVC   MDAT,FDAT     DATUM IN UEBERSCHRIFT UEBERTRAGEN
         EJECT
***********************************************************************
*             VERARBEITUNG BEI WEITEREN DURCHLAEUFEN                  *
***********************************************************************
         SPACE
EVERY    EQU   *
         L     R1,C04STEPS     ANZAHL STEPS NACH REG 1
         LA    R1,1(R1)        ANZAHL  + EINS
         ST    R1,C04STEPS     SPEICHERN
         CLC   ZDATE,SMF4DTE    TAGESDATUM  GLEICH ?
         BE    CLCTIME
         CLC   HIGHTIME,F24UHR
         BL    NEWINTV         NEUES PLOTINTERVALL
         L     R1,HIGHTIME
         S     R1,F24UHR
         ST    R1,HIGHTIME
         MVC   ZDATE,SMF4DTE   GUELTIGES TAGESDATUM UEBERTRAGEN
         B     CLCTIME
         SPACE 2
CLCTIME  EQU   *
         SPACE
         CLC   HIGHTIME,SMF4TME NEUER PLOTINTERVALL ?
         BNH   NEWINTV       JA, VERZW. ZUR  VERARBTG. BEI NEUEM INTV.
         EJECT
***********************************************************************
*   KARTEN MIT 'DD *' ODER 'DD DATA' EINGELESEN, KUMMULIEREN          *
***********************************************************************
         SPACE
EINZEL   EQU   *
         SPACE
         MVC   FW1,SMF4NCI   CARD IMAGES
         L     R1,ZCARD      KUMMULIERTE KARTEN ADDIEREN
         A     R1,FW1        KARTEN VON LETZTEM SATZ ADDIEREN
         ST    R1,ZCARD      KUMMUL.+ KARTEN AUS LETZTEM SATZ SPEICHERN
         SPACE 3
***********************************************************************
*   INITIATOR - ZEIT ERMITTELN                                        *
***********************************************************************
         SPACE
         MVC   FW1,SMF4PPST  PROBL.-PGM-START-TIME UEBERTRAGEN
         L     R1,FW1        INS REG 1
         MVC   FW1,SMF4SIT   START INITIATOR-TIME
         S     R1,FW1        DIFF = INITIATION-TIME
         LTR   R1,R1
         BM    UNGUELTG
         A     R1,ZINIT      INITIATION-TIME ADDIEREN
         ST    R1,ZINIT      INITIATION-TIME KUMMULIEREN
UNGUELTG EQU   *
         SPACE 3
***********************************************************************
* VARIABLEN TEIL VOM SMF-TYP 04 VERARBEITEN                           *
* EXCPS  PRO  DEVICE                                                  *
***********************************************************************
         SPACE
         MVC   HW1,SMF4LENN  LENGTH OF EXCP PORTION OF RECORD UEBERTR.
         LH    R5,HW1        LOOP-COUNTER FUER DEVICE-ENTRIES
         SRA   R5,3          DIV. DURCH 8 - REST GEHT VERLOREN
         LTR   R5,R5         MEHRERE DEVICES ??
         BZ    NOEXCPS       NEIN
         EJECT
***********************************************************************
*    TABELLE  DER  DEVICES  ERSTELLEN                                 *
***********************************************************************
         SPACE
         DROP  R3            BSAE=R3  FALLEN LASSEN
         USING IFASMF04,R7   BASE=R7 FUER DUMMY-SECTION
         LR    R7,R3         REG. UMLADEN
         SPACE 2
NEWENTRY EQU   *
         SPACE
         LA    R4,SMFEXCP    ADRESSE DES TABELLENANFANGS LADEN
         SPACE
ENTRYCLC EQU   *
         SPACE
         CLC   0(4,R4),=4X'00' IST TAB.-PLATZ NOCH FREI ?
         BE    ENTRYFRE      JA, VERZWEIGE ZUM EINTRAGEN
         CLC   0(4,R4),SMF4DEVC IST DEVICE SCHON IN TABELLE EINGETR. ?
         BE    ENTRYFND      JA, VERZWEIGE ZUM KUMMULIEREN
         CLC   0(4,R4),=4X'FF' TABELLENENDE ?
         BE    ENTRYFND      JA, VERZWEIGE ZUM KUMMULIEREN
         SPACE 2
         LA    R4,8(R4)      NEUEN TABELLENPLATZ AUFBAUEN
         B     ENTRYCLC      VERZWEIGE ZUM TABELLENDURCHSUCHEN
         SPACE 3
***********************************************************************
*        FREIEN ENTRY GEFUNDEN                                        *
***********************************************************************
         SPACE
ENTRYFRE EQU   *
         SPACE
         MVC   0(8,R4),SMF4DEVC DEVICE IN TABELLE EINTRAGEN
         B     EXCPLOOP      VERZWEIGE BEI MEHREREN DEVICES
         SPACE 3
***********************************************************************
*         DEVICE ENTRY SCHON VORHANDEN (EXCPS KUMMULIEREN)            *
***********************************************************************
         SPACE
ENTRYFND EQU   *
         SPACE
         L     R1,4(R4)      EXCPS ADDIEREN
         A     R1,SMF4EXCP   KUMMULIERTE EXCPS ADDIEREN
         ST    R1,4(R4)      KUMMULIERTE EXCPS SPEICHERN
         EJECT
***********************************************************************
*         BEI MEHREREN  DEVICES                                       *
***********************************************************************
         SPACE
EXCPLOOP EQU   *
         SPACE
         LA    R7,8(R7)      R7 AUF NAECHSTEN ENTRY EINSTELLEN
         BCT   R5,NEWENTRY   SIND NOCH DEVICE-ENTRIES ZU VERARBEITEN ?
         SPACE 3
***********************************************************************
*         KEIN DEVICE EINTRAG VORHANDEN                               *
***********************************************************************
         SPACE
NOEXCPS  EQU   *
         SPACE
         DROP  R7            BASE=R7  FALLEN LASSEN
         EJECT
***********************************************************************
*         C P U  - ZEITEN ERMITTELN                                   *
***********************************************************************
         SPACE
         USING IFASMF04,R3   BASE=R3
         SPACE
         MVC   HW1,SMF4LENN  LENGTH OF EXCP PORTION OF RECORD UEBERTR.
         LR    R7,R3         UMLADEN
         AH    R7,HW1        LENGTH OF RELOC.SECTION TO BASE ADDIEREN
         SH    R7,=H'2'      SUBTRACT 2 BYTES BECAUSE SMF4SETM --> ORG
*                            ON SMF4DEVC IN DUMMY SECTION
         DROP  R3
         SPACE 3
         USING IFASMF04,R7   BASE=R7
         SPACE
         XC    FW1,FW1       LOESCHEN
         MVC   FW1+1(3),SMF4SETM CPU-ZEIT UEBERTRAGEN
         L     R1,ZCPU       KUMMULIERTE CPU-ZEIT INS REG 1
         A     R1,FW1        ZEITEN ADDIEREN
         ST    R1,ZCPU       NEUE ZEIT SPEICHERN
         EJECT
***********************************************************************
*         PAGING - RATE ERMITTELN                                     *
***********************************************************************
         SPACE
         XC    HW1,HW1       LOESCHEN
         MVC   HW1+1(1),SMF4LNTH TOTAL LENGTH OF CPU + ACC.-SECTION
*                            UEBERTRAGEN
         LH    R1,HW1        INS REG 1 LADEN
         SH    R1,=H'4'      SUBTRACT LENGTH OF CPU PORTION AND BYTE
*                            FOR NUMBER OF ACC.-FIELDS
         AR    R7,R1         BASE=BASE + LENGTH-4
         SPACE 2
*         PAGE - IN
         SPACE
         MVC   FW1,SMF4PGIN  PAGE-IN RATE NACH VOLLWORT
         L     R1,FW1        INS REG 1 LADEN
         A     R1,ZPAGEIN    KUMMULIERTE PAGE-IN RATE ADDIEREN
         ST    R1,ZPAGEIN    KUMMULIERTE PAGE-IN RATE SPEICHERN
         SPACE 3
*         PAGE - OUT
         SPACE
         MVC   FW1,SMF4PGOT  PAGE-OUT RATE NACH VOLLWORT
         L     R1,FW1        INS REG 1 LADEN
         A     R1,ZPAGEOUT   KUMMULIERTE PAGE-OUT RATE ADDIEREN
         ST    R1,ZPAGEOUT   KUMMULIERTE PAGE-OUT RATE SPEICHERN
         SPACE 3
         B     END99
         EJECT
***********************************************************************
*    BEGINN DER ROUTINE BEI GRUPPENWECHSEL (NEUER PLOTINTERVALL)
***********************************************************************
         SPACE
*        ERSTELLUNG DES PLOTSATZES FUER AUSWEISUNG DER CPU-BELASTG.
         SPACE
NEWINTV  EQU   *
         SPACE
         MVI   CPUFOR,X'01'  DATA FORMAT = 01  (CHAR)
         MVC   CPUDTE,ZDATE    DATUM IN PLOTSATZ-BEREICH
         MVC   CPUTME,HIGHTIME ZEIT IN PLOTSATZ-BEREICH
         MVC   CPUZEIT,ZCPU    CPU-ZEIT IN PLOTSATZ-BEREICH
         CLC   ZCPU,C04CPUMX   HIGHWATERMARK FUER DIE CPU-ZEIT SUCHEN
         BL    PLOTPAGE        NICHT GEFUNDEN
         MVC   C04CPUMX,ZCPU   HOECHSTE CPU-ZEIT
         SPACE 3
***********************************************************************
*        ERSTELLUNG DES PLOTSATZES FUER AUSWEISUNG VON PAGING         *
***********************************************************************
         SPACE
PLOTPAGE EQU   *
         SPACE
         XC    FW1,FW1
         MVI   PAGEFOR,X'01'   DATA FORMAT = 01  (CHAR)
         MVC   PAGEDTE,ZDATE   DATUM IN PLOTSATZ-BEREICH
         MVC   PAGETME,HIGHTIME ZEIT IN PLOTSATZ-BEREICH
         MVC   FW1,ZPAGEOUT    PAGE-OUT RATE UEBERTRAGEN
         L     R1,FW1          INS REG 1 LADEN
         A     R1,ZPAGEIN      PAGE-IN RATE ADDIEREN
         ST    R1,FW1          IN EIN VOLLWORT SPEICHERN
         MVC   PAGING,FW1      PAGING-RATE IN PLOTSATZ-BEREICH
         C     R1,C04PAGEX     HOECHSTE PAGING-RATE SUCHEN
         BL    PLOTDEV         NICHT GEFUNDEN
         ST    R1,C04PAGEX     HOECHSTE PAGING-RATE
         EJECT
***********************************************************************
*        ERSTELLUNG DES PLOTSATZES FUER AUSWEISUNG DER EXCP'S PRO DEV.*
***********************************************************************
         SPACE
PLOTDEV  EQU   *
         SPACE
         LA    R4,SMFEXCP      REG 4 AUF TABELLENANFANG
         SPACE
DEVLOOP  EQU   *
         SPACE
         LA    R5,SMFEXCP2     AUF TABELLENANFANG ZUM KUMMULIEREN
         CLC   0(4,R4),=4X'00' KEIN TABELLENEINTRAG ?
         BE    LOETAB1         AUSSPRUNG AUS TAB.-BERARBEITUNG
         SPACE 2
*    EXCPS  IN DER SUMMENTABELLE KUMMULIEREN
         SPACE
CLCSUMEX EQU   *
         SPACE
         CLC   0(4,R4),0(R5)   EINTRAG IDENTISCH ?
         BE    SUMME           ADDIEREN
         CLC   0(4,R5),=4X'00'
         BE    SUMME
         CLC   0(4,R5),=4X'FF' TABELLENENDE ?
         BE    SUMLAST         ADDIEREN
         LA    R5,8(R5)        AUF NAECHSTEN EINTRAG STELLEN
         B     CLCSUMEX        LOOP
         SPACE
SUMME    EQU   *
         SPACE
         MVC   0(4,R5),0(R4)
SUMLAST  EQU   *
         L     R1,4(R4)        EXCPS INS REG 1
         A     R1,4(R5)        EXCPS AUS LETZTEM SATZ ADDIEREN
         ST    R1,4(R5)        IN SUMMENTABELLE SPEICHERN
         MVI   EXCPFOR,X'00'   DATA FORMAT = '00'  (HEX)
         MVC   EXCPDEV(4),0(R4) DEVICE UEBERTRAGEN
         MVC   EXCPDTE,ZDATE   DATUM UEBERTRAGEN
         MVC   EXCPTME,HIGHTIME ZEIT UEBERTRAGEN
         MVC   EXCPS,4(R4)     EXCPS UEBERTRAGEN
         CLC   EXCPS,C04EXCPX  HAEUFIGSTE EXCPS SUCHEN
         BL    WEITER          NICHT GEFUNDEN
         MVC   C04EXCPX,EXCPS  GEFUNDEN
         SPACE
WEITER   EQU   *
         SPACE
         CLC   0(4,R4),=4X'FF' TABELLENDE ?
         BE    LOETAB1         AUSSPRUNG AUS TAB.-VERARBEITUNG
         LA    R4,8(R4)        UM EINEN EINTRAG ERHOEHEN
         L     R2,APLOT03      * SCHREIBEN   DER
         PUT   (R2),PLTEXCP    *     PLOTSAETZE FUER EXCPS
         B     DEVLOOP         ANDERE DEVICES VERARBEITEN
LOETAB1  EQU   *
         XC    SMFEXCP(248),SMFEXCP
         MVC   ENDEXCP1-8(4),=4X'FF'
         EJECT
***********************************************************************
*    SCHREIBEN DER ANDEREN PLOTSAETZE                                 *
***********************************************************************
         SPACE 2
EXCPEND  EQU   *
         SPACE
         L     R2,APLOT01      *  SCHREIBEN DES PLOTSATZES
         PUT   (R2),PLTPAGE    *        FUER DAS PAGING
         L     R2,APLOT02      *   SCHREIBEN DES
         PUT   (R2),PLTCPU     *     PLOTSATZES FUER CPU-ZEIT
         SPACE 3
***********************************************************************
*    GESAMT  C P U  - ZEIT ERMITTELN                                  *
***********************************************************************
         SPACE
         L     R1,ZCPU         CPU-ZEIT EINES INTV. INS REG 1
         A     R1,C04CPU       BISHER KUMMUL. ZEIT DAZU ADDIEREN
         ST    R1,C04CPU       NEU KUMMUL. ZEIT SPEICHERN
         XC    ZCPU,ZCPU       LOESCHEN
         SPACE 3
***********************************************************************
*    PAGE -  I N  - GESAMT ERMITTELN                                  *
***********************************************************************
         SPACE
         L     R1,ZPAGEIN      PAGE-IN RATE EINES INTV. INS REG 1
         A     R1,C04PAGEI     BISHER KUMMUL. PAGE-IN RATE DAZU ADD.
         ST    R1,C04PAGEI     NEUE KUMMUL. PAGE-IN RATE SPEICHERN
         XC    ZPAGEIN,ZPAGEIN LOESCHEN
         SPACE 3
***********************************************************************
*    PAGE -  O U T  -  GESAMT  ERMITTELN                              *
***********************************************************************
         SPACE
         L     R1,ZPAGEOUT     PAGE-OUT RATE EINES INTV. INS REG 1
         A     R1,C04PAGEO     BISHER KUMMUL. PAGE-OUT RATE DAZU ADD.
         ST    R1,C04PAGEO     NEU KUMMUL. PAGE-OUT RATE SPEICHERN
         XC    ZPAGEOUT,ZPAGEOUT LOESCHEN
         TM    COMEODID,X'FF'
         BO    EODTYP04
***********************************************************************
*    PLOTINTERVALL-ZEIT  NEU  SETZEN                                  *
***********************************************************************
         SPACE
         DROP  R7
         USING IFASMF04,R3
         L     R1,COMINTV      *  TIME - INTERVALL INS REG 1
         MVC   FW1,SMF4TME
         A     R1,FW1
         ST    R1,HIGHTIME     NEU ZEIT SPEICHERN
         MVC   ZDATE,SMF4DTE   DATUM UEBERTRAGEN
         B     EINZEL
         EJECT
***********************************************************************
*        E  O  D   -   ROUTINE                                        *
***********************************************************************
         SPACE 2
EODTYP04 EQU   *
         SPACE
*   DRUCKEN DER UEBERSCHRIFT
         L     R2,ALISTE
         PUT   (R2),UE1-1
         PUT   (R2),UE1
         PUT   (R2),STRICH
         SPACE
*     DIE  ANZAHL  DER  STEPS  DRUCKEN
         SPACE
         MVC   EZ01M,MSTEP     MASKE UEBERTRAGEN
         L     R1,C04STEPS
         CVD   R1,DW1
         ED    EZ01M,DW1+4
         MVC   EZ01+23(24),=C'STEPS  WURDEN  INITIERT ' TEXT
         L     R2,ALISTE
         PUT   (R2),EZ01
         SPACE 3
***********************************************************************
*     GESAMT  C P U -ZEIT DRUCKEN                                     *
***********************************************************************
         SPACE
         L     R1,C04CPU       GES. CPU-ZEIT INS REG 1
         BAL   R14,UCONV       ZUM INTERNE UPRO
         CVD   R1,DW1          KONVERTIEREN
         MVI   EZ01+1,C' '
         MVC   EZ01+2(131),EZ01+1
         MVC   EZ01M,MCPU      MASKE UEBERTRAGEN
         ED    EZ01M,DW1+4     CPU-ZEIT AUFBEREITEN
         MVC   EZ01+23(30),=C'STUNDEN  GESAMTE  C P U - ZEIT' TEXT
         L     R2,ALISTE
         PUT   (R2),EZ01
         XC    DW1,DW1         LOESCHEN
         SPACE 3
***********************************************************************
*     DURCHSCHNITTLICHE INIT.-ZEIT PRO STEP DRUCKEN                   *
***********************************************************************
         SPACE
         L     R8,ZINIT        KUMMUL. INIT.-ZEIT NACH REG 8
         SRDA  R8,32           REG  8  LOESCHEN
         D     R8,C04STEPS     DIV. DURCH ANZAHL STEPS
         LR    R1,R9           UMLADEN DER REG. FUER SPRUNG NACH UCONV
         BAL   R14,UCONV       ZUM INTERNEN UPRO
         CVD   R1,DW1          KONVERTIEREN
         MVI   EZ01+1,C' '
         MVC   EZ01+2(131),EZ01+1
         MVC   EZ01M,MINIT     MASKE UEBERTRAGEN
         ED    EZ01M,DW1+4     DURCHSCHN.INIT.-ZEIT AUFBEREITEN
         MVC   EZ01+23(59),=C'SEKUNDEN DURCHSCHNITTLICHE  INITIALISIERU*
               NGSZEIT  PRO  STEP'  TEXT
         L     R2,ALISTE
         PUT   (R2),EZ01
         XC    DW1,DW1         LOESCHEN
         EJECT
***********************************************************************
*      PAGE  -  I N  RATE  DRUCKEN                                    *
***********************************************************************
         SPACE
         L     R1,C04PAGEI     KUMMUL. PAGE-IN RATE INS REG 1
         CVD   R1,DW1          KONVERTIEREN
         MVI   EZ01+1,C' '
         MVC   EZ01+2(131),EZ01+1
         MVC   EZ01M,MSTEP     MASKE UEBERTRAGEN
         ED    EZ01M,DW1+4     ANZAHL PAGE-IN AUFBEREITEN
         MVI   EZ01+23,C' '    24.STELLE BLANK
         MVC   EZ01+24(108),EZ01+23 RESTZEILE LOESCHEN
         MVC   EZ01+23(17),=C'PAGES ''IN'' GESAMT'  TEXT
         L     R2,ALISTE
         PUT   (R2),EZ01
         SPACE 3
***********************************************************************
*      PAGE - O U T  RATE  DRUCKEN                                    *
***********************************************************************
         SPACE
         XC    DW1,DW1         LOESCHEN
         L     R1,C04PAGEO     KUMMUL. PAGE-OUT RATE NACH REG 1
         CVD   R1,DW1          KONVERTIEREN
         MVI   EZ01+1,C' '
         MVC   EZ01+2(131),EZ01+1
         MVC   EZ01M,MSTEP     MASKE UEBERTRAGEN
         ED    EZ01M,DW1+4     ANZAHL PAGE-OUT AUFBEREITEN
         MVC   EZ01+23(18),=C'PAGES ''OUT'' GESAMT'  TEXT
         L     R2,ALISTE
         PUT   (R2),EZ01
         SPACE 3
***********************************************************************
*      ANZAHL KARTEN MIT 'DD *' ODER 'DD DATA' EINGELESEN, DRUCKEN    *
***********************************************************************
         SPACE
         XC    DW1,DW1         LOESCHEN
         L     R1,ZCARD        KUMMUL. KARTEN NACH REG 1
         CVD   R1,DW1          KONVERTIEREN
         MVI   EZ01+1,C' '
         MVC   EZ01+2(131),EZ01+1
         MVC   EZ01M,MSTEP     MASKE UEBERTRAGEN
         ED    EZ01M,DW1+4     ANZAHL KARTEN AUFBEREITEN
         MVC   EZ01+23(52),=C'KARTEN  WURDEN  MIT ''DD *'' ODER ''DD DA*
               TA'' EINGELESEN'  TEXT
         L     R2,ALISTE
         PUT   (R2),EZ01
         EJECT
***********************************************************************
*        SORTIERUNG  DER  DEVICE - EINTRAEGE  (AUFSTEIGEND)
***********************************************************************
         SPACE 2
         LA    R4,SMFEXCP2     REG 4 AUF TABELLENANFANG
         SPACE
S04      EQU   *
         SPACE
         LA    R5,8(R4)        REG 5 AUF NAECHSTEN EINTRAG
         SPACE
S02      EQU    *
         SPACE
         CLC   0(4,R4),0(R5)   DEVICES VERGLEICHEN
         BNH   S01
         MVC   DEVFLD,0(R4)
         MVC   0(8,R4),0(R5)
         MVC   0(8,R5),DEVFLD
         SPACE
S01      EQU   *
         SPACE
         CLC   0(4,R5),=4X'FF'
         BE    S03
         LA    R5,8(R5)        REG 5  UM EINEN EINTRAG WEITER
         B     S02
         SPACE
S03      EQU   *
         SPACE
         LA    R4,8(R4)        AUF NAECHSTEN EINTRAG STELLEN
         CLC   0(4,R4),=4X'FF' TABELLENENDE ?
         BNE   S04             WEITER SORTIEREN
         SPACE
*******       BEACHTE :               *********************************
*******       ---------               *********************************
*        TABELLE  IST  JETZT  AUFSTEIGEND  SORTIERT
***********************************************************************
         EJECT
***********************************************************************
*        DRUCKEN DER ZEILEN FUER DIE EXCPS PRO DEVICE                 *
***********************************************************************
         SPACE 2
         LA    R4,SMFEXCP2
         SPACE
VERGL    EQU   *
         SPACE
         CLC   0(4,R4),=4X'00' KEIN EINTRAG ?
         BNE   AUFBER
         SPACE
ERHOEHE  EQU   *
         SPACE
         LA    R4,8(R4)
         CLC   0(4,R4),=4X'FF'    TABELLENENDE
         BNE   VERGL
         MVI   S,C'E'             SCHALTER SETZEN FUER SUMME
         B     SUM
         SPACE
AUFBER   EQU   *
         SPACE
         CLC   0(4,R4),=4X'FF'
         BE    ENDE1
         CLC   4(4,R4),=4X'00' KEINE EXCPS ?
         BE    ERHOEHE
         UNPK  EZ02DEV,2(3,R4) UNIT-ADDR. INS AUSGABEFELD
         MVI   EZ02DEV+4,X'40'
         TR    EZ02DEV(4),TRTAB-X'F0'
         MVI   EZ02DEV,X'40'
         L     R1,4(R4)        EXCPS  NACH  REG  1
         XC    DW1,DW1         LOESCHEN
         CVD   R1,DW1          KONVERTIEREN
         MVC   EZ02M,MEXCP     MASKE UEBERTRAGEN
         ED    EZ02M,DW1+3     EXCPS AUFBEREITEN
         L     R2,ALISTE
         PUT   (R2),EZ02
         MVC   DEVFLD,0(R4)
         L     R1,DEVSUM
         A     R1,4(R4)
         ST    R1,DEVSUM
         LA    R4,8(R4)        AUF NAECHSTEN EINTRAG STELLEN
         CLC   0(3,R4),DEVFLD
         BE    DEVEQ
SUM      EQU   *
         L     R1,DEVSUM
         XC    DW1,DW1
         CVD   R1,DW1
         MVC   SUMZM,MEXCP
         ED    SUMZM,DW1+3
         L     R2,ALISTE
         PUT   (R2),SUMZEI
         XC    DEVSUM,DEVSUM
         CLI   S,C'E'             SCHALTER ABFRAGEN
         BE    ENDE1
         SPACE
DEVEQ    EQU   *
         SPACE
         CLC   0(4,R4),=4X'FF' TABELLENENDE ?
         BE    ENDE1
         B     VERGL
         SPACE 2
ENDE1    EQU   *
         L     R1,4(R4)        EXCPS DES LETZTEN EINTRAGS NACH REG 1
         C     R1,=F'0'
         BNE   LOESCH
         MVC   EZ02M,=C'       KEINE'
         B     MOVE
LOESCH   EQU   *
         XC    DW1,DW1
         CVD   R1,DW1          KONVERTIEREN
         MVC   EZ02M,MEXCP     MASKE UEBERTRAGEN
         ED    EZ02M,DW1+3     EXCPS D. LETZTEN EINTRAGS AUFBEREITEN
MOVE     EQU   *
         MVC   EZ02+38(22),=C'ANDEREN  DEVICES      '  TEXT
         L     R2,ALISTE
         PUT   (R2),EZ02
         SPACE 2
         B     END99
         EJECT
***********************************************************************
*         INTERNES UPRO  'U C O N V'
***********************************************************************
         SPACE 2
UCONV    EQU   *
         SPACE
         STM   R2,R6,SVUCONV
         LR    R2,R1
         SRDA  R2,32
         D     R2,=F'100'      HUNDERTSTEL SEK. ----> SEKUNGEN
         XR    R2,R2           SEK. IN REG 3, LOESCHEN REG 2
         LA    R4,60           REG 4 = 60
         DR    R2,R4           SEK. : 60 = MIN.
         LR    R6,R2           SEK. NACH REG 6
         XR    R2,R2           REG 2 LOESCHEN
         DR    R2,R4           MIN : 60 = STD.
         MH    R2,=H'100'      MIN * 100 (STELLEN-AUSRICHTUNG)
         ALR   R6,R2           ADD MIN UND SEK.
         MH    R3,=H'10000'    STD * 10000  (STELLEN-AUSRICHTUNG)
         ALR   R6,R3           ADD STD,MIN,SEK
         LR    R1,R6
         LM    R2,R6,SVUCONV
         BR    R14
         SPACE
SVUCONV  DC    5F'0'
         SPACE 3
         EJECT
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        D E F I N I T I O N E N                                      *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*         VOLLWORTE FUER DIE PARAMETERUEBERNAHME                      *
***********************************************************************
*
APARM    DC    F'0'          *   DIESE VOLLWORTE
ASATZ    DC    F'0'          *   IN IHRER
ALISTE   DC    F'0'          *   REIHENFOLGE BITTE
APLOT01  DC    F'0'          *   NICHT VERTAUSCHEN
APLOT02  DC    F'0'          *   (PARAMETERUEBERNAHME
APLOT03  DC    F'0'          *   AUS HAUPTPROGRAMM)
*
         EJECT
***********************************************************************
*         HILFSFELDER                                                 *
***********************************************************************
*
HW1      DC    H'0'
HIGHTIME DC    F'0'
FW1      DC    F'0'
ZCARD    DC    F'0'
ZINIT    DC    F'0'
ZCPU     DC    F'0'
ZPAGEIN  DC    F'0'
ZPAGEOUT DC    F'0'
DW1      DC    D'0'
F24UHR   DC    X'0083D600'   24.00 UHR
DEVSUM   DC    F'0'
DEVFLD   DC    D'0'
ZDATE    DC    F'0'
FDAT     DC    XL8'0000000000000000'
S        DC    CL1'A'             SCHALTER
         SPACE
********   UEBERSCHRIFT    ******
*
         DC    XL1'8B'
UE1      DS    0CL133
         DC    XL1'09'
         DC    CL5' '
         DC    CL30'AUS  PROGRAMM  -RZSP916A-     '
         DC    CL42'STATISTIK  UEBER  DIE  B E L A S T U N G  '
         DC    CL18'DES  SYSTEMS  /370'
         DC    CL2' '
         DC    CL5'VOM: '
MDAT     DC    CL8' '
         DC    CL12' '
         DC    CL10'SEITE:   1'
         SPACE
STRICH   DS    0CL133
         DC    XL1'11'
         DC    132CL1'-'
         EJECT
***********************************************************************
*         EINZELZEILEN                                                *
***********************************************************************
*
EZ01     DS    0CL133
EZ01V    DC    XL1'09'         SPACE 1 AFTER WRITE
         DC    CL10' '
EZ01M    DS    CL10
         DC    CL2' '
         DC    CL110' '
*
*
EZ02     DS    0CL133
         DC    XL1'09'
         DC    CL8' '
EZ02M    DS    CL12
         DC    CL2' '
         DC    CL30'ZUGRIFFE  AUF  DER  DEVICE  * '
EZ02DEV  DS    CL5
         DC    CL2' *'
         DC    CL74' '
         SPACE 2
SUMZEI   DS    0CL133
         DC    XL1'09'
         DC    CL9' '
         DC    CL3'---'
         DC    CL50'--------------------------------------------------'
SUMZM    DS    CL12
         DC    CL2' '
         DC    CL37'ZUGRIFFE  AUF  DIESEM  KANAL.        '
         DC    CL19' '
         EJECT
***********************************************************************
*        MASKEN FUER DIE DRUCKAUFBEREITUNG                            *
***********************************************************************
*
MSTEP    DC    X'40204B2020204B202120'
MINIT    DC    X'402020204B20204B2020'
MCPU     DC    X'402120204B20204B2020'
MEXCP    DC    X'402020204B2020204B202120'
         EJECT
***********************************************************************
*      DEVICE TABELLE FUER DIE  E X C P ' S  PRO DEVICE               *
***********************************************************************
*
         DS    0F            AUF VOLLWORTGRENZE AUSRICHTEN
SMFEXCP  DC    30XL8'0000000000000000'
         DC    4X'FF'        LETZTER EINTRAG
         DC    4X'00'        ***
*
*
*        AUFBAU :   FUER 30 DEVICES 8-BYTE ENTRIES VORGESEHEN
*
*
*      ×__×__×__×__×__×__×__×__×
*       .  .  .     .
*       .  .  .     ............. 4 BYTES EXCP ZAEHLER  (BINAER)
*       .  .  .
*       .  .  ................... 2 BYTES CHANNEL AND UNIT ADRESS (BIN)
*       .  .
*       .  ...................... 1 BYTE  UNIT TYPE (BINAER)
*       .
*       ......................... 1 BYTE  DEVICE CLASS (BINAER)
*
ENDEXCP1 EQU   *
         SPACE 3
         DS    0F            AUF VOLLWORTGRENZE AUSRICHTEN
SMFEXCP2 DC    30XL8'0000000000000000'
         DC    4X'FF'        LETZTER EINTRAG
         DC    4X'00'        ***
         SPACE 2
*        DIE TABELLE  'SMFEXCP2'  HAT DEN GLEICHEN AUFBAU WIE'SMFEXCP'
         EJECT
***********************************************************************
*        PLOTSAETZE                                                   *
***********************************************************************
         SPACE 2
PLTEXCP  DS    0CL18
EXCPFOR  DS    CL1
EXCPDEV  DS    CL5
EXCPDTE  DS    CL4
EXCPTME  DS    CL4
EXCPS    DS    CL4
*
PLTCPU   DS    0CL18
CPUFOR   DS    CL1
CPU      DC    CL5'CPU  '
CPUDTE   DS    CL4
CPUTME   DS    CL4
CPUZEIT  DS    CL4
*
PLTPAGE  DS    0CL18
PAGEFOR  DS    CL1
PAGE     DC    CL5'PAGE '
PAGEDTE  DS    CL4
PAGETME  DS    CL4
PAGING   DS    CL4
         EJECT
TRTAB    DC    C'0123456789ABCDEF'
         SPACE 3
COMMON   DSECT
COMINTV  DS    F
         ORG   *-4
COMEODID DS    CL1
         ORG
C04PAGEX DS    F
C04CPUMX DS    F
C04EXCPX DS    F
         DS    5F
C04PAGEI DS    F
C04PAGEO DS    F
C04CPU   DS    F
C04STEPS DS    F
         EJECT
***********************************************************************
*        DUMMY - SECTION  ALS MACRO-AUFRUF FUER SMF SATZ TYP 04       *
***********************************************************************
         SPACE 2
         PRINT GEN
         SPACE
IFASMF04 DSECT
         IFASMFR 04
         EJECT
         END   SMFTYP04
