./  ADD        NAME=VXXX01AS
ERRLOG   DIESES PROGRAMM SCHREIBT AUS EINTRAGUNGEN IN DEN FEHLERLOG
         SMFSAETZE.
ERRLOG   CSECT
         SMAIN R12,R11,R10,R9
         B     BEGIN
         DC    C'A PRODUCT OF SIMANDLS SOFTWARE INNOVATION'
BEGIN    DS    0H
         L     R1,8(R1)
         MVC   USERID,0(R1)
PUTMENUE TPUT  BUFF1,ENDBUFF1-BUFF1,FULLSCR FORMAT SCREEN
TGET     EQU   *
         LA    R3,6
         LA    R4,INBUFF
CLEAR    MVC   0(250,R4),=XL250'00'
         LA    R4,250(R4)
         BCT   R3,CLEAR
         TGET  INBUFF,1500,ASIS             GET INPUT
         LA    R3,INBUFF+3                  LOAD A(FIRST FIELD)
         CLC   0(3,R3),=X'114CE9'           BEGIN OF ERROR ?
         BE    BEGINN                       YES, ->
         CLC   0(3,R3),=X'114D4E'           END OF ERROR ?
         BE    ENDE                         YES, ->
         CLC   0(3,R3),=X'114EC5'           COMMENT ?
         BE    KOMM                         YES, ->
ENDINPUT EQU   *                            END OF FORMATTING
         CLI   INBUFF,X'F3'                 PF3 KEY DEPRESSED ?
         BE    RETURN                       NO, ->
         TPUT  BUFF2,ENDBUFF2-BUFF2,FULLSCR
         B     TGET
RETURN   EQU   *
         TPUT  BUFF3,ENDBUFF3-BUFF3,FULLSCR
         L     R13,4(R13)                   YES, RETURN
         RETURN (14,12),RC=0
BEGINN   EQU   *
         LA    R3,3(R3)           LOAD A(DATA)
         LR    R4,R3              SAVE A(DATA)
BEGINN1  EQU   *
         CLI   0(R4),X'11'        SBA PRESENT (BEGIN OF NEXT FIELD) ?
         BE    BEGINN2            YES, ->
         CLI   0(R4),X'00'        END OF INPUT ?
         BE    BEGINN2            YES, ->
         LA    R4,1(R4)           CURRENT ADDRESS + 1
         B     BEGINN1            -> FIND END OF FIELD
BEGINN2  EQU   *
         SR    R4,R3              END OF FIELD - BEGIN = LENGTH
         BCTR  R4,R0              LENGTH -1 FOR MVC
         EX    R4,BEGMVC          MOVE TO SMFDATA
         EX    R4,OC1
         MVC   SMFDATA(21),=C'BEGINN DER STOERUNG: ' MOVE TO SMFDATA
         BAL   R14,WRITESMF       -> WRITE SMFRECORD
         AR    R3,R4              BEGINN + LENGTH -1
         LA    R3,1(R3)           +1 = A(NEXT FIELD OR END OF INPUT)
         CLI   0(R3),X'00'        END OF INPUT ?
         BE    ENDINPUT           YES, ->
         CLC   0(3,R3),=X'114D4E' NEXT FIELD END OF ERROR ?
         BE    ENDE               YES, ->
         CLC   0(3,R3),=X'114EC5' COMMENT FIELD ?
         BE    KOMM               YES, ->
         B     ENDINPUT           -> END OF INPUT
ENDE     EQU   *                  PROCESSING QUITE SIMILAR PROCESSING
         LA    R3,3(R3)           LOAD A(DATA)
         LR    R4,R3              SAVE A(DATA)
ENDE1    EQU   *
         CLI   0(R4),X'11'        SBA PRESENT ? (BEGINN OF NEXT FIELD)
         BE    ENDE2              YES, ->
         CLI   0(R4),X'00'        END OF DATA ?
         BE    ENDE2              YES, ->
         LA    R4,1(R4)           CURRENT ADDRESS+1
         B     ENDE1              -> FIND END OF FIELD
ENDE2    EQU   *
         SR    R4,R3              END OF FIELD - BEGINN = LENGTH
         BCTR  R4,R0              -1 FOR MVC
         EX    R4,ENDEMVC         MOVE TO SMFDATA
         EX    R4,OC2
         MVC   SMFDATA(19),=C'ENDE DER STOERUNG: '  MOVE TO SMFDATA
         BAL   R14,WRITESMF       WRITE SMFRECORD
         AR    R3,R4              BEGIN OF FIELD+LENGTH-1
         LA    R3,1(R3)           +1
         CLI   0(R3),X'00'        END OF INPUT?
         BE    ENDINPUT           YES, ->
         CLC   0(3,R3),=X'114EC5' NEXT FIELD COMMENT ?
         BE    KOMM               YES, ->
         B     ENDINPUT           ->
KOMM     EQU   *
         LA    R3,3(R3)           LOAD ADDRESS DATA
         LA    R4,80              LOAD LENGTH OF SMFDATAFIELD
         LR    R5,R3              SAVE BEGIN OF DATA
         LR    R6,R3              SET POINTER TO BEGINN OF DATA
KOMM1    LTR   R3,R3              END OF DATA ?
         BZ    ENDINPUT           YES, ->
         CLI   0(R3),X'00'
         BE    KOMM4
         CLI   0(R3),C' '
         BNE   KOMM2
         LR    R6,R3
KOMM2    LA    R3,1(R3)
         BCT   R4,KOMM1
         CR    R6,R3
         BNE   KOMM3
         LA    R6,1(R3)
         LA    R3,1(R3)
KOMM3    EQU   *
         LA    R3,1(R6)
         B     KOMM5
KOMM4    EQU   *
         LR    R6,R3
         XR    R3,R3
KOMM5    EQU   *
         SR    R6,R5
         BCTR  R6,R0
         EX    R6,KOMMVC
         EX    R6,OC3
         BAL   R14,WRITESMF
         LA    R4,80
         LR    R5,R3
         B     KOMM1
WRITESMF EQU   *
         ST    R14,SAVE14
         GETTD ZEIT,DATUM
         TIME  BIN
         STCM  0,15,TIME
         STCM  1,15,DATE
         LA    R1,SMFREC
SMFSVC   SVC   252
         MVC   SMFDATA(80),=CL80' '
         L     R14,SAVE14
         BR    R14
SAVE14   DS    F
BEGMVC   MVC   SMFDATA+21(0),0(R3)
ENDEMVC  MVC   SMFDATA+19(0),0(R3)
KOMMVC   MVC   SMFDATA(0),0(R5)
OC1      OC    SMFDATA+21(0),=CL80' '
OC2      OC    SMFDATA+19(0),=CL80' '
OC3      OC    SMFDATA(0),=CL80' '
SMFREC   DS    0F
LENGTH   DC    AL2(SMFEND-SMFREC)
RESERVED DC    H'0'
SYSIND   DC    B'00000010'
RECTYP   DC    X'81'
TIME     DC    XL4'00'
DATE     DC    XL4'00'
SYSID    DC    C'L158'
         DC    C' '
USERID   DC    CL7' '
         DC    C' '
DATUM    DC    CL8' '
         DC    CL2' '
ZEIT     DC    CL8' '
         DC    CL1' '
SMFDATA  DC    CL80' '
SMFEND   EQU   *
INBUFF   DC    6XL250'00'
BUFF1    DC    XL1'C7'    WCC
         DC    XL1'11'    SBA
         DC    XL2'5D7F'  LAST ADDRESS ON SCREEN
         DC    XL1'11'    SBA
         DC    XL2'4040'  FIRST ADDRESS ON SCREEN
         DC    XL1'3C'    REPEAT TO ADDRESS
         DC    XL2'5D7F'  END ADDRESS
         DC    XL1'00'    CHARACTER TO BE REPEATED
         DC    XL1'1D'   START FIELD
         DC    XL1'F8'   ATTRIBUTES PROT HIGH SEL SKIP
         DC    XL1'11'   SBA
         DC    XL2'405A' 1/27
         DC    CL18'OESTZ TP-FEHLERLOG'
         DC    XL1'11'   SBA
         DC    XL2'C260' 3/1
         DC    XL1'1D'   START FIELD
         DC    XL1'60'   ATTRIBUTES  PROT
         DC    CL79'BITTE TRAGEN SIE JENE STOERUNGEN, DIE WAEHREND DIES*
               ER ODER VORHERGEGANGENER    '
         DC    CL80' SITZUNGEN AUFGETRETEN SIND, EIN.'
         DC    CL80' IN DAS FELD ''BEGINN'' DATUM UND UHRZEIT DES BEGIN*
               NS DER STOERUNG.'
         DC    CL80' IN DAS FELD ''ENDE'' DATUM UND UHRZEIT DER BEHEBUN*
               G DER STOERUNG.'
         DC    CL80' IN DAS FELD ''FEHLERBESCHREIBUNG'' EINE KURZE BESC*
               HREIBUNG DES FEHLERS.'
         DC    CL80' NACH IHRER EINTRAGUNG ODER WENN SIE NICHTS EINTRAG*
               EN WOLLEN, DRUECKEN SIE   '
         DC    CL80' BITTE DIE PF3-TASTE, UM DIE SITZUNG ZU BEENDEN.'
         DC    XL1'11' SBA
         DC    XL2'4C60' 11/1
         DC    XL1'1D'   START FIELD
         DC    XL1'E8'   ATTRIBUTES PROT HIGH SEL
         DC    CL7'BEGINN:'
         DC    XL1'1D'   START FIELD
         DC    XL1'40'   ATTRIBUTES UNPROT
         DC    XL1'11'   SBA
         DC    XL2'4DC7' 11/40
         DC    XL1'1D'   START FIELD
         DC    XL1'F8'   PROT SKIP HIGH SEL
         DC    CL5'ENDE:'
         DC    XL1'1D'   START FIELD
         DC    XL1'40'   ATTRIBUTES UNPROT
         DC    XL1'11'    SBA
         DC    XL2'4D6F' 11/80
         DC    XL1'1D'   START FIELD
         DC    XL1'F4'   PROT SKIP HIGH
         DC    XL1'1D'   START FIELD
         DC    XL1'F8'   PROT SKIP HIGH
         DC    CL19'FEHLERBESCHREIBUNG:'
         DC    XL1'1D'   START FIELD
         DC    XL1'40'   UNPROT
         DC    XL1'11'   SBA
         DC    XL2'4CE9' 12/21
         DC    XL1'13'   INSERT CURSOR
ENDBUFF1 EQU   *
BUFF2    EQU   *
         DC    X'C7'     WCC
         DC    X'11'    SBA
         DC    X'5D7F'
         DC    X'11'    SBA
         DC    X'4040'
         DC    X'12'    RUA
         DC    X'5D7F'
         DC    X'11'    SBA'
         DC    X'4CE9'
         DC    X'13'
ENDBUFF2 EQU   *
BUFF3    EQU   *
         DC    X'C1'
         DC    X'11'
         DC    X'5D7E'
         DC    X'11'
         DC    X'4040'
         DC    X'3C'
         DC    X'5D7F'
         DC    X'00'
         DC    X'4040'
         DC    X'13'
ENDBUFF3 EQU   *
BUFF4    EQU   *
         DC    C'ENQU-DEQU - FEHLER. IHRE MELDUNG KONNTE NICHT GESPEICH*
               ERT WERDEN.'
ENDBUFF4 EQU   *
BUFF5    DC    C'ALLOCATION-FEHLER. IHRE MELDUNG KONNTE NICHT GESPEICHE*
               RT WERDEN.'
ENDBUFF5 EQU   *
         END   ERRLOG
