SYSBOSS  TITLE 'SYSBOSS,FIEDLER,COMMAND PROCESSOR'
SYSBOSS  CSECT
* THIS MODULE SETS TIMER INTERVALS
* ON COMPLETION OF AN INTERVAL
*  THE COMMANDS ARE SCHEDULED VIA SVC 245 TO SVC 34
* THE COMMAND AREA (SYSCMDS) IS GOTTEN BY LOAD
* THE COMMANDAREA MUST BE OF THE FORM
*        DC    C'TIME01020304           '
*        DC    C'COMMAND AS FROM CONS   '
*        DC    C'AS MANY AS YOU LIKE    '
* SINGLE COMMANDS AT ONE TIME ARE SEPARATED BY 10 SECONDS
* TIME MUST BE CONSECUTIVE UP
         EJECT
         GADBEGIN
         CLC   XFPARM(2),=C'NO'  SEE IF NO TCAM START
         BE    A00     ROUND TCAM START
         SR    R0,R0
         LA    R1,COMMAND      START TCAM
         SVC   245
A00      EQU   *
         LOAD  EP=SYSCMDS
         LR    R12,R0   SAVE  COMMAND AREA
         TIME  DEC,ZONE=LT,ERRET=TIMEERR
         ST    R0,F1    SAVE TOD
         UNPK  ELAPS(7),F1  BRING ONLY HHMMSS TO UNPK DEC
         MVC   ELAPS+6(2),=C'00'  KILL REST OF ELAPS FIELD
A10      LR    R2,R12   POINT TO COMMAND ORIGIN
A20      CLC   =C'TIME',0(R2)  SEE NEXT TIME
         BNE   A30     NO NEXT
         CLC   ELAPS,4(R2)  SEE IF TIME ELAPSED
         BL    B10       FOUND
A30      LA    R2,23(R2)  NEXT PLACE
         CLI   0(R2),C'*'   SEE IF TABLE END
         BNE   A20    NO NEXT COMMAND
A40      MVC   ELAPS,=C'00000000'  SET ELAPS TIME TO ZERO
         B     A10     GO TO TABLE BEGIN
B10      MVC   ELAPS,4(R2)    MOVE NEW ELAPS TIME
         STIMER  WAIT,TOD=ELAPS,ERRET=TIMEERR
B20      LA    R2,23(R2) SEE IF COMMAND
         CLI   0(R2),C'*'  OR TAB END
         BE    A40   YES TO BEGIN
         CLC   =C'TIME',0(R2)  OR TIME
         BE    A20  GO TO TEST ELAPS TIME
         MVC   COMAREA,0(R2) MOVE COMMAND FOR EXECUTION
         SR    R0,R0
         LA    R1,COMMAND
         SVC   245
         STIMER  WAIT,ERRET=TIMEERR,DINTVL=DELAY
         B     B20  GO TO TEST NEXT COMMAND
TIMEERR  ABEND 1,DUMP   TIMER ERROR
         EJECT
F1       DC    F'0'   SAVE TOD
ELAPS    DC    D'0'    ELAPS TIME
DELAY    DC    C'00001000'   DELAY TIME
COMMAND  DC    H'26',H'0'
COMAREA  DC    CL23'S SYSUBOSS,JOB=TCAM    '   AREA FOR THE COMMAND
         END
