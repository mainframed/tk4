* STAND 06/22/79
***********************************************************************
*                                                                     *
*    PROGRAMM  = GETASUM                                              *
*                                                                     *
*    AUFRUF    = CALL GETASUM,(ERR,ANR,NACH,PER,BZT),VL               *
*        F       ERR = FEHLERCODE = 0 KEIN FEHLER                     *
*                                 = 1 ABST.SUM.SATZ NICHT AUF DATEI   *
*                                 = 2 ABST.PERIODE NICHT GESPEICHERT  *
*                                 = 3 DD-KARTE FEHLT                  *
*                                 = 4 SONST.FEHLER BEIM LESEN         *
*        CL10    ANR = ABSTIMMSUMMEN-NR,FUER DIE SATZ ZU LESEN IST    *
*        CL26    NACH= EMPFANGSFELD FUER GEWUENSCHTE PERIODE          *
*        H       PER = NR.DER GEWUENSCHTEN PERIODE                    *
*                        = 0 LAUT BZT ZU BESTIMMEN                    *
*                        = 1 LETZTE    PERIODE                        *
*                        = 2 VORLETZTE PERIODE                        *
*                        USW.                                         *
*        CL15    BZT = BERICHTSZEIT (NUR BEI PER=0)                   *
*                                                                     *
*    ZWECK     = EINLESEN DES ABSTIMMSUMMENSATZES (KEY = ANR)         *
*                UND UEBERGABE DER GEWUENSCHTEN PERIODENSUMME         *
*                IN NACH                                              *
*                                                                     *
***********************************************************************
GETASUM  CSECT
         XSAVE 12,SAVE,GETASUM
         AREG
         LR    R11,R1                   --> ARGLISTE RETTEN
         USING LFLAG,R9
         LA    R9,SATZ+16
         XC    ERR,ERR
         CALL  CENQAST,(KS)
         OPEN  (DCBAST)
         TM    DCBAST+48,X'10'
         BNZ   DCBOK
         MVC   ERR,=F'3'      DDKARTE FEHLT
         BAL   R10,DEQ                 DEQUEUE
         B     RETURN
DCBOK    EQU   *
* LESEN  ABSTIMMSUMME
         L     R3,4(R11)                --> ASUMNR
         LA    R2,DCBAST
         READ  DECBASUM,K,(R2),,,(R3),MF=E
         WAIT  ECB=DECBASUM
         TM    DECBASUM+24,X'FD'
         BNL   READOK                   KEIN FEHLER
         TM    DECBASUM+24,X'80'
         BO    LESFHL                   ASUMNR NICHT AUF DATEI
         MVC   ERR,=F'4'                SONST.LESEFEHLER
         B     CLOSE
LESFHL   EQU   *
         MVC   ERR,=F'1'                LESEFEHLER
         B     CLOSE
* ABSTIMMSUMME GELESEN
READOK   EQU   *
         L     R10,12(R11)              --> GEWUENSCHTE PERIODE
         XR    R3,R3
         XR    R4,R4
         LH    R4,NPER                  ANZ.PER.IM SATZ
         LH    R3,0(R10)                GEWUENSCHTE PERIODE
         LTR   R3,R3
         BZ    PERUN                    =0 --> BERICHTSZEIT VORGEGEB.
         CR    R3,R4
         BNH   PEROK
PERNO    EQU   *
         MVC   ERR,=F'2'                GEWUENSCHTE PERIODE NICHT AUF
         B     CLOSE                    DATEI
PEROK    EQU   *
         SR    R4,R3
         MH    R4,=H'26'
         LA    R9,LFLAG(R4)             -->GEWUENSCH.BERZT.+SUMME
         B     PERMVC
* GEWUENSCHTE PERIODE LAUT BERICHTSZEIT SUCHEN
PERUN    EQU   *
         L     R5,16(R11)               --> GEWUENSCHTE BERZEIT
PERLOOP  EQU   *
         CLC   BERZT,0(R5)              BERZEIT-VERGLEICH
         BE    PERFND
         BCTR  R4,0                     ZAEHLER UEBER PERIODEN
         BZ    PERNO
         LA    R9,PERLN(R9)             -->NAECHSTE PERIODE
         B     PERLOOP
PERFND   EQU   *
         STH   R4,0(R10)                PERIODE LAUT BERZEIT
PERMVC   EQU   *
         L     R4,8(R11)                --> EMPFANGSFELD
         MVC   0(PERLN,R4),PERDEF       BERZEIT+SUMME -->AUFRUFENDE PGM
         SPACE 2
CLOSE    EQU   *
         BAL   R10,CLSAST
RETURN   EQU   *
         L     R10,0(R11)               -->RETURNCODE
         MVC   0(4,R10),ERR             RETURNCODE SETZEN
         XRETURN 0
CLSAST   EQU   *
         CLOSE (DCBAST)
DEQ      EQU   *
         CALL  CDEQAST
         BR    R10
         SPACE 2
* DEFINITIONEN
*
ERR      DS    F
         READ  DECBASUM,KU,,SATZ,'S',MF=L
         ENTRY DCBAST
DCBAST   DCB   DDNAME=ASTSUM,DSORG=IS,MACRF=(RU,WU),RECFM=F
KS       DC    CL1'S'
SATZ     DS    CL(16+170)
         MASATZ
         END
