./ ADD UALS04AS
***********************************************************************
*                                                                     *
*  LISTVTOC DIENT ZUM DRUCKEN DES LETZTEN OPEN INPUT UND OPEN OUTPUT  *
*  DATUMS AUS DEN FORMAT 1 DSCB'S DES VTOC'S VON MAGNETPLATTEN IBM    *
*  MODELLE 3330 , 3330-1 UND 3350.                                    *
*                                                                     *
*  UEBER STEUERKARTEN WIRD ANGEGEBEN WELCHE MAGNETPLATTEN AUSGEDRUCKT *
*  WERDEN SOLLEN. DIESE MAGNETPLATTEN WERDEN DANN VON DEM PROGRAMM    *
*  DYNAMISCH ALLOCIERT, SO DASS DAFUER KEINE DD-STATEMENTS NOTWENDIG  *
*  SIND.                                                              *
*                                                                     *
*  DAS PROGRAMM LISTVTOC BESTEHT AUS FOLGENDEN CSECTS :               *
*                                                                     *
*  LISTVTOC / UCBSCAN / READVTOC                                      *
*                                                                     *
*  HINWEISE FUER DIE UMWANDLUNG :                                     *
*                                                                     *
*  FOLGENDE MAKROS MUESSEN IN DER MACLIB AUS DER DATEI SYS1.AMODGEN   *
*  VORHANDEN SEIN : IECDSECS / DCBD / IEFUCBOB / IECSDSL1             *
*                                                                     *
*  DAS PROGRAMM BEFINDET SICH AUF DER SOFTLIB.                        *
*                                                                     *
*  JOB CONTROL FUER DIE ANWENDUNG :                                   *
*                                                                     *
*             //JOBNAME JOB (ACCOUNTING)                              *
*             //STEP     EXEC PGM=LISTVTOC                            *
*             //SYSPRINT DD SYSOUT=A                                  *
*             //SYSIN    DD *                                         *
*             VOL=VOLUME(,DATE=99.366(,TEST))                         *
*             ....                                                    *
*             /*                                                      *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
LISTVTOC CSECT
         USING *,15                    FIRST ADDRESSABILITY
         B     START                   SURROUND CSECT IDENTIFICATION
*
IDR      DC    C' CSECT LISTVTOC V01 01/02/1978 SAUTTER ALLIANZ-LEBEN'
         DS    0H
*
START    EQU   *
         STM   14,12,12(13)            SAVE REGISTERS
         LR    12,13                   SAVE ADDR OF CALLING SAVEAREA
         LA    13,SAVEA                LOAD ADDRESS OF OWN SAVEAREA
         ST    12,4(13)                SAVE CALLING SAVEAREA ADDRESS
         ST    13,8(12)                OWN SAVEA ADDR TO CALLER
         BALR  3,0                     ESTABLISH ADDRESSABILITY
         USING *,3
*
* END OF LINKAGE CONVENTIONS
*
         SPACE
*
* P R O G R A M   I N I T I A T I O N
*
* OPENING THE DATA SETS
*
         MVC   LINE,LINE-1             CLEAR PRINTER LINE
         USING IHADCB,RHADCB           ESTABLISH DSECT ADDRESSABILITY
         LA    RHADCB,SYSIN            SYSIN DCB DSCET
         OPEN  (SYSIN)                 INITIALIZE INPUT DCB
         TM    DCBOFLGS,DCBOFOPN          OPEN SUCCESSFULLY COMPLETED
         BNO   OPERR1                  NO, TERMINATE PROCESSING
*
         LA    RHADCB,SYSPRINT         SYSPRINT DCB DSECT
         OPEN  (SYSPRINT,(OUTPUT))     INITIALIZE PRINTER DCB
         TM    DCBOFLGS,DCBOFOPN          OPEN SUCCESSFULLY COMPLETED
         BNO   OPERR2                  NO, TERMINATE PROCESSING
*
         DROP  RHADCB
*
* GET THE SYSIN CONTROLL STATEMENTS
*
         LA    6,TABVOLSER        ADDRESS OF VOLSER TAB
INTGET   GET   SYSIN
         CLC   0(4,1),=C'VOL='
         BNE   INERROR
         MVC   0(6,6),4(1)        MOVE VOLSER INTO TAB
         CLC   10(6,1),=C',DATE='     SHOULD WE COMPARE THE DATE ?
         BNE   NODATE
         MVC   6(6,6),16(1)       MOVE DATE TO TABLE
         MVI   12(6),C'D'         DATE INDICATOR
         CLC   22(5,1),=C',TEST'  SHOULD WE TEST THE DATES ?
         BNE   NOTEST
         MVI   12(6),C'T'         TEST INDICATOR
NODATE   EQU   *
NOTEST   LA    6,13(,6)           POINT TO NEXT ELEMENT
         B     INTGET             GET NEXT CONTROL STMT
INERROR  EQU   *
         MVC   WTO+35(27),0(1)    MOVE CONTROL STMT TO WTO
WTO      WTO   'INVALID CONTROL STATEMENT ''VOL=VOLSER,DATE=XX.XXX,TEST*
               '' IGNORED, PROCESSING CONTINUED.',ROUTCDE=(11)
         B     INTGET             GO ON
*
* LOADING THE SUBROUTINES
*
CLOSE    EQU   *
*
         CLOSE SYSIN                   CLOSE SYSIN DATASET
*
         LA    1,TABVOLSER             FIRST VOLSER ELEMENT
         ST    1,ATABVOL
*
* TO KNOW WHICH DASD VOLUMES ARE MOUNTED IT IS NECESSARY TO SCAN  *
* THE UCB TABLE                                                       *
*
UCBCALL  EQU   *
CALLUCB  CALL  UCBSCAN,(UCBALA,UCBA),VL
         B     RETPT1                  BRANCH AROUND PARAMETERLIST
*
* PARAMETERS USED BY UCBSCAN
*
UCBALA   DC    A(ALA)                  LOCATOR 1
UCBA     DC    A(UCBFLD)               LOCATOR 2
ALA      DC    F'0'                    UCB ADDRESS LIST ADDRESS
UCBFLD   DC    CL256' '                UCB FIELD
         DS    0H                      ALIGNMENT
*
RETPT1   EQU   *
         B     *+4(15)                 BRANCH DEPENDS ON RETURN CODE
         B     UCBOK                   UCB FOUND
         B     PROGEND                 END OF UCB TABLE
*
UCBOK    EQU   *
         USING UCB,RUCB                UCB DUMMY SECTION
         LA    RUCB,UCBFLD             UCB ADDRESS
         CLC   DEV3330,UCBTYP+2        IS DEVICE 3330 DISK
         BE    TSTAT                   YES, TEST STATUS
         CLC   DEV33301,UCBTYP+2       IS DEVICE 3330 MOD 11 DISK
         BNE   TSTAT                   YES, TEST STATUS
         CLC   DEV3350,UCBTYP+2        IS DEVICE 3350 DISK
         BNE   UCBCALL                 NO, GET NEXT UCB
*
TSTAT    EQU   *
         TM    UCBSTAT,UCBONLI         DEVICE OFFLINE
         BZ    UCBCALL                 YES, GET NEXT UCB
         TM    UCBSTAT,UCBCHGS         PENDING OFFLINE
         BO    UCBCALL                 YES, GET NEXT UCB
         MVC   VOLSER,UCBVOLI           VOLSERNO TO NEXT PARMLIST
         EJECT
*
* COMPARE UCB VOLSER AND SYSIN VOLSER, IF EQUAL USE THIS UCB
*
         L     5,ATABVOL               ADDRESS OF VOLSER TABLE ELEMENT
*
SCANVOL  EQU   *
         CLC   VOLSER,0(5)             VOLSER EQUAL
         BE    VTOCSTRT                YES, GET VTOC
         CLI   0(5),X'FF'              END OF TABLE
         BE    PROGEND                 TERMINATE PROGRAMM
         B     UCBCALL                 NEXT UCB
         DROP  RUCB
         SPACE 2
*
VTOCSTRT EQU   *
         MVC   PARM,12(5)              COMPARE OR TEST DATE OR NOTHING
SETEND   MVC   COMPDATE,6(5)           MOVE COMPARE DATE
         LA    5,13(,5)                NEXT TABLE ELEMENT ADDRESS
         ST    5,ATABVOL
         MVC   DISKADR,=CL5'VTOC'      INDICATE START OF VTOC SCAN
         USING DSCB1,RDSCB1
         LA    RDSCB1,DSCB                  DSCB DSECT
*
VTOCREAD EQU   *
CALLVTOC CALL  READVTOC,(VOLSERA,DISKADRA,DSCBA,ABSTRA),VL
         B     RETPT2                  BRANCH AROUND PARMLIST
*
* THE FOLLOWING FIELDS ARE USED BY READVTOC
*
VOLSERA  DC    A(VOLSER)               LOCATOR 3
DISKADRA DC    A(DISKADR)              LOCATOR 4
DSCBA    DC    A(DSCB)                 LOCATOR 5
ABSTRA   DC    A(ABSTR)                LOCATOR 6
VOLSER   DC    CL6' '                  VOLUME SERIAL NUMBER
DISKADR  DC    CL5'VTOC'               VTOC REQUESTED/ACTUAL DSCB ADDR
         DS    0H                      ALIGNMENT
DSCB     DC    CL140' '                DATA SET CONTROL BLOCK
ABSTR    DC    CL5' '                  FOR ABSTR REQUEST
         DS    0H                      ALIGNMENT
*
RETPT2   EQU   *
         B     *+4(15)                 BRANCH DEPENDS ON RETURN CODE
         B     OK2                     DSCB FOUND
         B     UCBCALL                 VTOC END
         B     ABEND                   RC=8 VOLUME WAS NOT MOUNTED
         B     ABEND                   RC=12 INCORR FORMAT IDENTIFIER
         B     ABEND                   RC=16 PERMANENT I/O ERROR
         B     ABEND                   RC=20 INVALID WORKAREA POINTER
*
* TO LOCATE THE DATA SETS ALL FORMAT 1 DSCB'S MUST BE READ
*
OK2      EQU   *
         CLI   DS1FMTID,X'F1'          FORMAT 1 DSCB
         BE    PRINT                   YES, THEN USE IT TO PRINT
         CLI   DS1FMTID,X'F4'          FORMAT 4 DSCB
         BE    UCBCALL                 YES, FETCH NEXT UCB
         B     VTOCREAD                READ NEXT DSCB
         SPACE 2
*
* PRINT DSN, CREATION DATE, EXPIRATION DATE, DATE OF LAST UPDATE
* AND LAST UPDATE COUNTER.
*
PRINT    EQU   *
         CLC   HEADVOL,VOLSER     NEW VOLSER
         BE    CHKLNE             YES, THEN CHECK LINE COUNTER
         ZAP   PAGECOUNT,=P'1'    SET PAGE COUNTER TO ONE
         B     PHEAD              AND PRINT THE HEADLINES
CHKLNE   CP    LNECOUNT,=P'40'    40 LINES PRINTED
         BNE   PLINE              NO, THEN PRINT LINE
PHEAD    MVC   HEADVOL,VOLSER     MOVE VOLSER TO HEADLINE
         UNPK  HEADPAGE,PAGECOUNT MOVE IN PAGE COUNTER
         OI    HEADPAGE+3,X'F0'
         CLI   PARM,C'D'          LAST BACKUP DATE
         BNE   COMPT              NO, COMPARE TEST
         MVC   HEADMSG1,=C'LAST BACKUP'
         MVC   HEADMSG2(5),=C'DATE '
         MVC   HEADMSG2+5(6),COMPDATE  MOVE IN LAST BACKUP DATE
         B     PUT
COMPT    CLI   PARM,C'T'
         BNE   BLANKS
         MVC   HEADMSG1,=C' TEST DATE '
         MVC   HEADMSG2(11),=CL11' '
         MVC   HEADMSG2+3(6),COMPDATE  MOVE IN TEST DATE
         B     PUT
BLANKS   MVC   HEADMSG1,=CL11' '
         MVC   HEADMSG2,=CL11' '
PUT      PUT   SYSPRINT,BLANKLNE  SKIP TO CHANNEL 1 IMMEDIATELY
         PUT   SYSPRINT,HEADLNE1  PRINT HEADLINE 1
         PUT   SYSPRINT,HEADLNE2  PRINT HEADLINE 2
         PUT   SYSPRINT,HEADLNE3  PRINT HEADLINE 3
         AP    PAGECOUNT,=P'1'    ADD ONE TO PAGE COUNTER
         ZAP   LNECOUNT,=P'0'     SET COUNT TO ZERO
PLINE    EQU   *
         MVC   DSN,DS1DSNAM       DATA SET NAME TO PRINTER
         XC    DW,DW              CLEAR DOUBLE WORD
         MVC   DW+7(1),DS1CREDT   TAKE YEAR OF CREATION DATE
         L     RWORK,DW+4
         CVD   RWORK,DW           CONVERT IT TO DECIMAL
         UNPK  CREADT(2),DW+6(2)
         OI    CREADT+1,X'F0'
         MVI   CREADT+2,C'.'      POINT BETWEEN YEAR AND DAY
         XC    DW,DW              CLEAR DOUBLE WORD
         MVC   DW+6(2),DS1CREDT+1 TAKE DAY OF CREATION DATE
         L     RWORK,DW+4
         CVD   RWORK,DW           CONVERT IT TO DECIMAL
         UNPK  CREADT+3(3),DW+6(2)
         OI    CREADT+5,X'F0'
         XC    DW,DW              CLEAR DOUBLE WORD
         MVC   DW+7(1),DS1EXPDT   TAKE YEAR OF EXPIRATION DATE
         L     RWORK,DW+4
         CVD   RWORK,DW           CONVERT IT TO DECIMAL
         UNPK  EXPDT(2),DW+6(2)
         OI    EXPDT+1,X'F0'
         MVI   EXPDT+2,C'.'       POINT BETWEEN YEAR AND DAY
         XC    DW,DW              CLEAR DOUBLE WORD
         MVC   DW+6(2),DS1EXPDT+1 TAKE DAY OF EXPIRATION DATE
         L     RWORK,DW+4
         CVD   RWORK,DW           CONVERT IT TO DECIMAL
         UNPK  EXPDT+3(3),DW+6(2) EXPIRATION DATE TO PRINTER
         OI    EXPDT+5,X'F0'
         UNPK  OINPUT(3),DS1SYSCD+13(2) MOVE OPEN INPUT YEAR
         OI    OINPUT+1,X'F0'
         MVI   OINPUT+2,C'.'      POINT BETWEEN YEAR AND DAY
         UNPK  OINPUT+3(3),DS1SYSCD+13+1(2) MOVE OPEN INPUT YEAR
         OI    OINPUT+5,X'F0'
         UNPK  OOUTPUT(3),DS1SYSCD+13+3(2) MOVE OPEN OUTPUT YEAR
         OI    OOUTPUT+1,X'F0'
         MVI   OOUTPUT+2,C'.'      POINT BETWEEN YEAR AND DAY
         UNPK  OOUTPUT+3(3),DS1SYSCD+13+3+1(2) MOVE OPEN OUTPUT DAY
         OI    OOUTPUT+5,X'F0'
*
         CLI   PARM,C'D'
         BNE   COMPT1
         CLC   COMPDATE,OOUTPUT CLC SYSIN DATE / LAST OPEN OUTPUT DATE
         BH    *+4+6+4            BRANCH AROUND SET STAR
         MVC   STARS,=C'***'      SET STARS TO THIS DATASET NAME
         B     *+4+6              BRANCH AROUND RESET STAR
         MVC   STARS,=C'   '      RESET STARS TO BLANKS
*
COMPT1   CLI   PARM,C'T'
         BNE   LINE09
         MVC   STARS,=C'   '      RESET STARS TO BLANKS
         CLC   COMPDATE,OINPUT    CLC TEST DATE / LAST OPEN INPUT DATE
         BL    LINE09
         CLC   COMPDATE,OOUTPUT   CLC TEST DATE / LAST OPEN OUTPUT DATE
         BL    LINE09
         MVC   STARS,=C'***'      SET STARS TO THIS DATASET NAME
*
LINE09   MVI   LINE,X'09'         SPACE 1 LINE AFTER WRITE
*
* HERE ALL ZERO AND WROG DATES ARE SET TO '  --  '
*
         CLC   EXPDT,=C'00.000'
         BNE   *+4+6
         MVC   EXPDT,=C'  --  '
         CLC   OINPUT,=C'00.000'
         BE    *+4+4+4+4+4        MOVE IN '  --  '
         CLI   OINPUT,C'7'
         BE    *+4+4+4+6          SKIP MOVE
         CLI   OINPUT,C'8'
         BE    *+4+6              SKIP MOVE
         MVC   OINPUT,=C'  --  '
         CLC   OOUTPUT,=C'00.000'
         BE    *+4+4+4+4+4        MOVE IN '  --  '
         CLI   OOUTPUT,C'7'
         BE    *+4+4+4+6          SKIP MOVE
         CLI   OOUTPUT,C'8'
         BE    *+4+6              SKIP MOVE
         MVC   OOUTPUT,=C'  --  '
*
         AP    LNECOUNT,=P'1'     ADD 1 TO COUNT
         PUT   SYSPRINT,LINE      PRINT THE LINE
         B     VTOCREAD                READ NEXT DSCB
         SPACE
PROGEND  EQU   *
         CLOSE (SYSPRINT)
         XR   15,15
*
RETURN   EQU   *
         LA    13,SAVEA                LOAD ADDRESS OF OWN SAVEAREA
         L     13,4(13)                LOAD ADDRESS OF CALLING SAVEAREA
         LM    2,12,28(13)             RESTORE
         L     14,12(13)               THE
         L     0,20(13)                REGISTERS
         BR    14                      RETURN
*
OPERR1   EQU   *
         WTO   'LISTVTOC : SYSIN DD-STATEMENT MISSING.                 *
               LISTVTOC TERMINATED. USER CODE : 0016',ROUTCDE=(11)
         LA    15,16                   RETURN CODE 16 - SEVERE ERROR
         B     RETURN                  TERMINATE PROGRAM
OPERR2   EQU   *
         WTO   'LISTVTOC : SYSPRINT DD-STATEMENT MISSING.              *
               LISTVTOC TERMINATED. USER CODE : 0016',ROUTCDE=(11)
         LA    15,16                   RETURN CODE 16 - SEVERE ERROR
         B     RETURN                  TERMINATE PROGRAM
         SPACE 2
*
*  EACH ERROR IS HANDLED BY THIS ROUTINE
*
ABEND    EQU   *
         ABEND (15),DUMP  BECAUSE OF ERROR
         EJECT
*
* DEFINED SPACE AND WORKAREAS
*
SAVEA    DC    18F'0'                  SAVEAREA
DW       DC    D'0'  WORKAREA
ATABVOL  DC    A(0)                    ADDRESS OF NEXT TABLE ELEMENT
DEV3330  DC    XL2'2009'               DEVICE TYPE DA 3330
DEV33301 DC    XL2'200D'               DEVTYPE 3330 MOD 11
DEV3350  DC    XL2'200B'               DEVTYPE 3350
COMPDATE DC    CL6' '                  COMPARE DATE
PARM     DC    CL1' '
LNECOUNT DC    PL2'40'                 PRINT LINE COUNT
PAGECOUNT DC   PL3'1'                  PAGE COUNT
TABVOLSER DC   100XL6'FFFFFFFFFFFF'    TABLE OF VOLSER NUMBERS
*
BLANKLNE DS    0CL133
         DC    X'8B'                   SKIP TO CHANNEL 1 IMMEDIATELY
*
HEADLNE1 DS    0CL133
         DC    X'11'                   SPACE 2 LINES AFTER WRITE
         DC    CL53' '
         DC    CL19'LISTVTOC OF VOLUME '
HEADVOL  DC    CL06'VOLSER'
         DC    CL45' '
         DC    CL5'PAGE '
HEADPAGE DC    CL4'0001'
HEADLNE2 DS    0CL133
         DC    X'09'              SPACE 1 LINE AFTER WRITE
         DC    CL46'DATASET NAME'
         DC    CL09'CREATION'
         DC    CL11'EXPIRATION'
         DC    CL11'LAST DATE'
         DC    CL12' LAST DATE'
HEADMSG1 DC    CL11'LAST BACKUP'
         DC    CL32' '
HEADLNE3 DS    0CL133
         DC    X'11'              SPACE 2 LINES AFTER WRITE
         DC    CL46'            '
         DC    CL09'  DATE  '
         DC    CL11'   DATE   '
         DC    CL11'OPEN INPUT'
         DC    CL12'OPEN OUTPUT'
HEADMSG2 DC    CL11'DATE XX.XXX'
         DC    CL32' '
         DC    C' '  BLANK TO CLEAR PRINTER LINE
LINE     DS    0CL133             PRINTER LINE
         DS    CL1                CONTROL CHARACTER
DSN      DS    CL44              DATA SET NAME
         DS    CL3
CREADT   DS    CL6               CREATION DATE
         DS    CL4
EXPDT    DS    CL6               EXPIRATION DATE
         DS    CL5
OINPUT   DS    CL6               LAST DATE OPEN INPUT
         DS    CL5
OOUTPUT  DS    CL6               LAST DATE OPEN OUTPUT
         DS    CL8
STARS    DS    CL3
         DS    CL36
*
* END OF CONTINGUOUS DEFINITIONS
*
         EJECT
*
* REGISTER EQUATES
*
RHADCB   EQU  12                      DCB DSECT BASE REGISTER
RUCB     EQU   12                      UCB DSECT BASE REG
RDSCB1   EQU   12                      DSCB1 DSECT BASE REG
RWORK    EQU   10                      COMMON WORK REG
         EJECT
*
* LITERALS
*
         LTORG
         EJECT
*
* DATA CONTROL BLOCKS
*
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GL),DSORG=PS,EODAD=CLOSE,           *
               RECFM=FB,LRECL=80,BLKSIZE=80
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,                    *
               RECFM=FBM,LRECL=133,BLKSIZE=133
*
* D U M M Y   S E C T I O N S
*
         PRINT NOGEN
         IECDSECS ,,,,,,,,,,,,,,,,,,,DCB,,UCB,DSCB1,                   *
               EXPAND=YES
         PRINT GEN
         END   LISTVTOC
UCBSCAN  CSECT
*
*        REGISTER EQUATES
*
R1       EQU   1
R2       EQU   2
R4       EQU   4
R5       EQU   5
R12      EQU   12
R15      EQU   15
*
         USING *,R15                   FIRST ADDRESSABILITY
         B     START                   SURROUND CSECT IDENTIFICATION
         DC    C' CSECT UCBSCAN  V01 01/02/1978 SAUTTER ALLIANZ-LEBEN'
START    DS    0H
         STM   14,12,12(13)            SAVE REGISTERS
         LR    12,13                   SAVE ADDR OF CALLING SAVEAREA
         LA    13,SAVEA                LOAD ADDRESS OF OWN SAVEAREA
         ST    12,4(13)                SAVE CALLING SAVEAREA ADDRESS
         ST    13,8(12)                OWN SAVEA ADDR TO CALLER
         BALR  3,0                     ESTABLISH ADDRESSABILITY
         USING *,3
         USING CVT,R12
         L     R2,0(R1)                FIRST LOCATOR ADDRESS
         L     R2,0(R2)                FIRST PARAMETER ADDRESS
         L     R4,0(R2)                OLD UCB ADDR LIST ADDR
         LTR   R4,R4                   ANY ADDRESS PRESENT
         BNZ   NEXTADDR                YES, CONTINUE SCAN
         L     R12,CVTPTR              LOAD POINTER TO CVT
         L     R4,CVTILK2              LOAD POINTER TO UCB ADDRESS LIST
*
ADDRLOAD EQU   *
         LH    R5,0(R4)                LOAD UCB ADDRESS
         LTR   R5,R5                   ADDRESS PRESENT
         BNZ   FOUND                   CHECK IF END OF TABLE
*
NEXTADDR EQU   *
         LA    R4,2(R4)                POINT TO NEXT ADDRESS
         B     ADDRLOAD                LOAD NEXT UCB ADDRESS
*
FOUND    EQU   *
         CH    R5,ENDCON               LAST ENTRY OF TABLE
         BE    END                     YES, LOAD RETURNCODE 4
         LA    R15,0                   RC=0 UCB PRESENT
         ST    R4,0(R2)                UCB ADDR LIST ADDR TO CALLER
         L     R2,4(R1)                SECOND LOCATOR ADDRESS
         L     R2,0(R2)                SECOND PARAMETER ADDRESS
         SLL   R5,16                   ZERO
         SRL   R5,16                   LEFT HALFWORD
         MVC   0(256,R2),0(R5)         UCB TO CALLER
         B     RETURN                  RETURN TO CALLER
*
END      EQU   *
         LA    R15,4                   RC=4 END OF UCB TABLE
         XC    0(4,R2),0(R2)           RESET FOR NEXT STARTUP
         L     R2,4(R1)                SECOND LOCATOR ADDRESS
         L     R2,0(R2)                SECOND PARAMETER ADDRESS
         MVC   0(3,R2),ENDIND          INDICATE END OF TABLE
*
RETURN   EQU   *                       RETURN TO CALLER
         LA    13,SAVEA                LOAD ADDRESS OF OWN SAVEAREA
         L     13,4(13)                LOAD ADDRESS OF CALLING SAVEAREA
         LM    2,12,28(13)             RESTORE
         L     14,12(13)               THE
         L     0,20(13)                REGISTERS
         BR    14                      RETURN
SAVEA    DC    18F'0'                  SAVE AREA
ENDCON   DC    X'FFFF'                 SIGNALS END OF UCB ADDR LIST TAB
ENDIND   DC    C'END'                  SIGNALS END OF UCB ADDR LIST TAB
         PRINT NOGEN
         CVT   DSECT=YES
         PRINT GEN
         END   UCBSCAN
READVTOC CSECT
*
*        REGISTER EQUATES
*
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         USING *,R15                   FIRST ADDRESSABILITY
         B     START                   SURROUND CSECT IDENTIFICATION
         DC    C' CSECT READVTOC V01 01/02/1978 SAUTTER ALLIANZ-LEBEN'
START    DS    0H
         STM   14,12,12(13)            SAVE REGISTERS
         LR    12,13                   SAVE ADDR OF CALLING SAVEAREA
         LA    13,SAVEA                LOAD ADDRESS OF OWN SAVEAREA
         ST    12,4(13)                SAVE CALLING SAVEAREA ADDRESS
         ST    13,8(12)                OWN SAVEA ADDR TO CALLER
         BALR  3,0                     ESTABLISH ADDRESSABILITY
         USING *,3
         LR    R2,R1                   SAVE PARMADDR
         L     R5,0(R2)                FIRST LOCATOR ADDRESS
         L     R5,0(R5)                FIRST PARAMETER ADDRESS
         MVC   VOLSER,0(R5)            VOLUME SERIAL NUMBER
         L     R5,4(R2)                SECOND LOCATOR ADDRESS
         L     R5,0(R5)                SECOND PARAMETER ADDRESS
         L     R6,12(R2)               FOURTH LOCATOR ADDRESS
         L     R6,0(R6)                FOURTH PARAMETER ADDRESS
         CLC   VTOC,0(R5)              VTOC START REQUESTED
         BE    OBTAIN1                 YES, READ FORMAT4 DSCB
         MVC   CCHHR,0(R5)             GET OLD DSCB ADDRESS
         CLC   ABSTR,0(R6)             SEEK WITH UNMODIFIED TRACK ADDR
         BE    OBTAIN2                 YES, READ WITHOUT CCHHR MODIFIC
         B     NEXTDSCB                SEARCH NEXT DSCB
*
OBTAIN1  EQU   *
         XC    ALA,ALA                 RESET CURRENT UCB ADDRESS
*
UCBCALL  EQU   *
         L     R15,=V(UCBSCAN)         UCBSCAN ENTRY POINT ADDRESS
         CALL  (15),(UCBALA,UCBA),VL
         B     *+4(R15)
         B     OK                      UCB FOUND
         B     VTOCEND                 END OF UCB TABLE
*
OK       EQU   *
         USING UCBOB,RUCB              UCB DSECT ADDRESSABILITY
         LA    RUCB,UCBAREA            ADDRESS OF UCB
         CLC   UCBTYP+2(2),=X'2009'    3330
         BE    TESTVOL
         CLC   UCBTYP+2(2),=X'200B'    3350
         BE    TESTVOL
         CLC   UCBTYP+2(2),=X'200D'    3330-1
         BNE   UCBCALL                 DEVICE TYPE NOT ACCEPTABLE
*
TESTVOL  EQU   *
         CLC   VOLSER,UCBVOLI          THIS VOLUME REQUESTED
         BNE   UCBCALL                 NO, GET NEXT UCB
         DROP  RUCB                    END OF UCB ADDRESSABILITY
         OBTAIN CAMLST1
         B     *+4(R15)                CHECK RETURN CODE
         B     FOUND1                  NORMAL END
         B     MOUNTERR                VOLUME WAS NOT MOUNTED
         B     FOUND1                  F1 DSCB MISSING MEANS F4 DSCB
         B     IOERR                   PERMANENT I/O ERROR
         B     INVAL                   INVALID WORKAREA POINTER
*
FOUND1   EQU   *
         LA    RDSCB,IECSDSL4          ADDRESS OF DSCB
         USING IECSDSL4,RDSCB          DSCB ADDRESSABILITY
         CLI   DS4IDFMT,X'F4'          VERIFY FORMAT4 DSCB
         BNE   F4MISS                  ERROR IF NOT FORMAT4
         MVC   DSCBNO,DS4DEVDT         NUMBER OF DSCB'S ON TRACK
         MVC   CCHHR,DS4HPCHR          HIGHEST FORMAT1 DSCB ADDRESS
         MVC   TRKNUM,DS4DEVSZ+2       NUMBER OF TRACKS ON CYLINDER
         CLC   F4,0(R6)                FORMAT4 DSCB REQUESTED
         BE    PASSF4                  YES, PASS DSCB TO CALLER
         DROP  RDSCB
*
OBTAIN2  EQU   *
         OBTAIN CAMLST2
         B     *+4(R15)                CHECK RETURN CODE
         B     FOUND2                  DSCB FOUND
         B     MOUNTERR                VOLUME WAS NOT MOUNTED
         B     F1MISS                  FORMAT1 DSCB NOT FOUND
         B     IOERR                   PERMANENT I/O ERROR
         B     INVAL                   INVALID WORKAREA POINTER
         B     OUTEX                   SEEK OUT OF VTOC
*
FOUND2   EQU   *
         LA    RDSCB,IECSDSL1          DSCB ADDRESS
         USING IECSDSL1,RDSCB          FORMAT1 DSCB ADDRESSABILITY
         CLI   DS1FMTID,X'F1'          FORMAT1 DSCB
         BE    PASSDATA                PASS DSCB TO CALLER
         CLI   DS1FMTID,X'F4'          END (START) OF VTOC
         BE    VTOCEND                 TERMINATE SCAN
         CLC   ABSTR,0(R6)             SEEK WITH UNMODIFIED TRACK ADDR
         BE    PASSDATA                YES, PASS DSCB TO CALLER
*
NEXTDSCB EQU   *
         XR    R4,R4                   CLEAR WORKREG
         CLI   R,X'01'                 FIRST DSCB ON TRACK
         BE    SUBTRK                  YES, CHANGE TRACK
         IC    R4,R                    CURRENT DSCB RECORD NUMBER
         BCTR  R4,0                    NEXT LOWER RECORD
         STC   R4,R                    STORE RECORD NUMBER
         B     OBTAIN2                 NEXT DSCB
*
SUBTRK   EQU   *
         ICM   R4,15,CCHH              CCHH PART OF ADDRESS
         BCTR  R4,0                    NEXT LOWER TRACK
         STCM  R4,15,CCHH              AND/OR CYLINDER
         MVC   R,DSCBNO                HIGHEST POSSIBLE RECORD NO.
         CLC   HH,FCON                 HAS CYL CHANGED
         BNE   OBTAIN2                 NO, READ NEXT DSCB
         MVC   HH,TRKNUM               HIGHEST TRACK ADDRESS
         XR    R4,R4                   CLEAR WORKREG
         ICM   R4,3,HH                 TRACK ADDRESS
         BCTR  R4,0                    DECREASE BY ONE
         STCM  R4,3,HH                 AND SAVE IT
         B     OBTAIN2                 NEXT DSCB
*
PASSDATA EQU   *
         LA    R15,0                   RETURN CODE ZERO
         MVC   0(5,R5),CCHHR           DSCB ADDRESS TO CALLER
*
DSCBMOVE EQU   *
         L     R5,8(R2)                3RD LOCATOR
         L     R5,0(R5)                3RD PARAMETER ADDRESS
         MVC   0(140,R5),IECSDSL1      DSCB TO CALLER
         B     END                     RETURN TO CALLER
*
VTOCEND  EQU   *
         LA    R15,4                   RETURN CODE 4
         MVC   0(5,R5),=CL5'END'       INDICATE VTOC END
         B     DSCBMOVE                RETURN WITH F4DSCB TO CALLER
*
PASSF4   EQU   *
         LA    R15,0                   RETURN CODE ZERO
         MVC   0(5,R5),=CL5'VTOC'      TO PERMIT NEXT ENTRY
         L     R5,8(R2)                3RD LOCATOR
         L     R5,0(R5)                3RD PARAMETER ADDRESS
         XC    0(140,R5),0(R5)         RESET OLD DATA
         MVC   0(96,R5),IECSDSL4       DSCB4 TO CALLER
         B     END                     RETURN TO CALLER
*
MOUNTERR EQU   *
         LA    R15,8                   RC=8 VOLUME WAS NOT MOUNTED
         MVC   0(5,R5),=CL5'MOUNT'     INDICATE MOUNT ERROR
         B     END                     RETURN TO CALLER
*
F1MISS   EQU   *
*
F4MISS   EQU   *
         LA    R15,12                  RC=12 INCORR FORMAT IDENTIFIER
         MVC   0(5,R5),=CL5'FORM'      INDICATE INCORR FORMAT ID
         B     END                     RETURN TO CALLER
*
IOERR    EQU   *
         LA    R15,16                  RC=16 PERMANENT I/O-ERROR
         MVC   0(5,R5),=CL5'IOERR'     INDICATE I/O-ERROR
         B     END                     RETURN TO CALLER
*
OUTEX    EQU   *
         LA    R15,20                  RC=20 OUT OF VTOC
         MVC   0(5,R5),=CL5'OUTEX'     INDICATE OUT OF VTOC
         B     END                     RETURN TO CALLER
*
INVAL    EQU   *
         LA    R15,24                  RC=24 INVALID WORKAREA POINTER
         MVC   0(5,R5),=CL5'POINT'     INDICATE INVAL WORKA POINTER
*
END      EQU   *
         LA    13,SAVEA                LOAD ADDRESS OF OWN SAVEAREA
         L     13,4(13)                LOAD ADDRESS OF CALLING SAVEAREA
         LM    2,12,28(13)             RESTORE
         L     14,12(13)               THE
         L     0,20(13)                REGISTERS
         BR    14                      RETURN
         EJECT
CAMLST1  CAMLST SEARCH,F4DSN,VOLSER,IECSDSL4
CAMLST2  CAMLST SEEK,CCHHR,VOLSER,IECSDSL1
F4DSN    DC    44X'04'                 DSNAME OF F4 DSCB
CCHHR    DS    0CL5                    ABSOLUTE DISK ADDRESS
CCHH     DS    0CL4                    CYLINDER AND HEAD
         DS    CL2
HH       DS    CL2                     HEAD ADDRESS
R        DS    CL1                     RECORD NUMBER
VOLSER   DS    CL6                     VOLUME SERIAL NUMBER
DSCBNO   DS    CL1                     NUMBER OF DSCB'S ON TRACK
TRKNUM   DS    CL2                     NUMBER OF TRACKS ON CYLINDER
VTOC     DC    CL4'VTOC'               TO INDICATE VTOC REQUESTED
ABSTR    DC    CL5'ABSTR'              TO INDICATE SEEK REQUEST
F4       DC    CL2'F4'                 TO INDICATE F4DSCB REQUESTED
FCON     DC    XL2'FFFF'               OWERFLOW CONSTANT
UCBALA   DC    A(ALA)                  UCBSCAN PARAMETER POINTER
UCBA     DC    A(UCBAREA)              UCBSCAN PARAMETER POINTER
ALA      DC    A(0)                    UCBSCAN PARAMETER
UCBAREA  DC    256X'00'                UCB AREA
RUCB     EQU   7                       UCB BASE REGISTER
RDSCB    EQU   7                       DSCB BASE REGISTER
SAVEA    DC    18F'0'                  SAVE AREA
         LTORG
         PRINT NOGEN
         IECSDSL1 (4)
         DS    CL44                    PADD WORKAREA TO 140 CHAR
         IECSDSL1 (1)
         DSECT
         IEFUCBOB
         PRINT GEN
         END   READVTOC
