 TITLE 'S P 2 9  -  L I S T V T O C   I N   A L P H A B E T I S C H E R*
                  R E I H E N F O L G E'
***********************************************************************
*                                                                     *
*   SP29      LISTVTOC IN ALPHBET.REIHENFOLGE                         *
*                                                                     *
*   SP29      PROGRAMMIERER: FEHLING             DATUM: 20.12.71      *
*   SP29      PROGRAMMIERER: SENCZEK        AEND.DATUM: 12.11.72      *
*   SP29      PROGRAMMIERER: STEINER        AEND.DATUM: 07.12.72      *
*   SP29      PROGRAMMIERER: STEINER        AEND.DATUM: 19.12.72      *
*   SP29      PROGRAMMIERER: HERR LUEHMANN  AEND.DATUM: 25.05.73      *
*   SP29      PROGRAMMIERER: FEHLING        AEND.DATUM: 11.11.74      *
*   SP29      PROGRAMMIERER: FEHLING        AEND.DATUM: 20.03.75      *
*   SP29      PROGRAMMIERER: LUETTIG        AEND.DATUM: 05.04.75      *
*   SP29      PROGRAMMIERER: LUETTIG        AEND.DATUM: 24.09.76      *
*                                                                     *
***********************************************************************
*                                                                     *
*   SP29 LIEST ALLE FORMAT 1 DSCB'S DER DA-EINHEIT (EN) UND LISTET    *
*        SIE SORTIERT NACH DSNAMEN AN.                                *
*                                                                     *
*        FOLGENDE PARM-ANGABEN SIND ZULAESSIG:                        *
*        PARM=VOLSER              LIST NUR FUER ANGEGEBENEN VOLUME    *
*        PARM=ALL                 LIST ALLER ONLINE DA-DEV.           *
*                                                                     *
*   DDNAMEN = SORTIN      BLKSIZE= N*140                              *
*             SORTOUT     BLKSIZE= N*140,RECFM=FB,LRECL=140           *
*             SORTWK01                                                *
*             SORTWK02                                                *
*             SORTWK03                                                *
*             SORTLIB                                                 *
*             SYSOUT                                                  *
*             DRUCK       BLKSIZE= N*133                              *
*                                                                     *
*   AUFGERUFENE UNTER PROGRAMME:                                      *
*        IERRCO00                 SORT/MERGE                          *
*        SP44                     KERNSPEICHERSORT                    *
*                                                                     *
***********************************************************************
         EJECT
SP29     ANFANG R,EP=SP29                                             *
         USING UCBOB,R4
         L     R1,0(,R1)
         LR    R11,R1
         LOAD  EP=SP44            KERNSPEICHERSORT
         ST    R0,ASP44
         LR    R1,R11
         LH    R15,0(,R1)
         CH    R15,=H'6'
         BE    P010
         CH    R15,=H'3'
         BL    P005
         CLC   2(3,R1),=C'ALL'
         BNE   P005
         OI    SWT,X'80'          VERARB. ALLER ONLINE DA-DEV.
         B     P010
P005     EQU   *
         ABEND 1003,DUMP
P010     MVC   SERNO,2(R1)
P015     EQU   *
         L     R6,16              CVT-ADDR.
         L     R5,40(,R6)         PTR. TO UCB-LOOKUP IN R5
P020     LH    R4,0(,R5)          PTR. TO UCB IN R4
         LTR   R4,R4
         BNZ   P040               BRANCH, IF UCB USED
P030     LA    R5,2(,R5)          GET POINTER TO NEXT UCB
         B     P020
P040     BP    P050               BRANCH IF NOT LASTUCB
         TM    SWT,X'80'
         BO    P041
         ABEND 1003,DUMP          GEFORDERTE PLATTE NICHT MONTIERT
P041     MVC   SERLO,SERHI
         CLC   SERLO,=6X'FF'
         BE    ENDE
         MVC   SERHI,=6X'FF'
         B     P058
P045     EQU   *
         CLC   SERLO,SRTEVOLI
         BNL   P030
         CLC   SERHI,SRTEVOLI
         BNH   P030
         MVC   SERHI,SRTEVOLI
         MVC   SERNO,SRTEVOLI
         ST    R4,DEBVMOD
         MVC   DEVCODE,16(R4)
         MVC   TTR,SRTEFSCT
         B     P030
P050     TM    UCBTBYT3,UCB3DACC
         BZ    P030               BRANCH, IF NOT DA-DEV.
         TM    SRTESTAT,SRTEONLI
         BZ    P030               BRANCH, IF NOT ONLINE
         CLI   UCBTBYT4,X'05'     2321 ?
         BE    P030               JA
         ST    R5,UCBLOOK
         TM    SWT,X'80'
         BO    P045
         CLC    SERNO,SRTEVOLI
         BNE   P030               BRANCH, IF WRONG VOLUME
P055     MVC   SERNO,SRTEVOLI
         ST    R4,DEBVMOD         SET UCB-ADDR INTO SKELETON-UCB
         MVC   DEVCODE,16(R4)     SET DEV.CODE INTO VOL.-LIST
         MVC   TTR,SRTEFSCT       REL.ADDR OF VTOC
P058     EQU   *
         OPEN  (SORTIN,OUTPUT)
P060     EQU   *
         BAL   R14,U000           TTR -> MBBCCHAR
         MVC  CCHHR,CONVADDR+3
         OBTAIN CAMOBT            READ A DSCB
         ST    R15,RETCODE
         LTR   R15,R15
         BZ    P070
         CH    R15,=H'4'
         BE    P030
         CH    R15,=H'8'
         BNE   P110
P070     CLI   FORMAT,C'4'        VTOC DECRIPTOR
         BE    P100
         CLI   FORMAT,C'5'
         STM   R5,R10,SPEI1
         BE    P085
         LM    R5,R10,SPEI1
         CLI   FORMAT,C'1'
         BNE   P075
         PUT   SORTIN,DSNAME
         AP    ZLR,=P'1'
P075     CLC   CCHHR,CCHHRSV      FERTIG ?
         BNL   P120               BRANCH IF YES
         LH    R8,TTR+2           GET LATEST TTR
         LA    R8,256(R8)         INCREMENT R
         CH    R8,RPT             MAX NO OF R REACHED
         BNH   P080               BRANCH IF NO
         MVI   TTR+2,X'01'        RESET R
         LH    R8,TTR
         LA    R8,1(,R8)
         STH   R8,TTR
         B     P060               CONVERSION ROUTINE
P080     STH   R8,TTR+2
         B     P060               CONVERSION ROUTINE
P085     SR    R10,R10
         SR    R9,R9
         SR    R8,R8
P090     LA    R5,8
         LA    R6,DS5AVEXT
P091     MVC   W1+2(2),2(R6)      ANZAHL FREIE CYLS NACH W1
         MVC   W2+3(1),4(R6)      ANZAHL FREIE TRKS NACH W2
         BAL   R7,P094
         LA    R6,5(,R6)          R6 AUF ADRESSE DES FOLGENDEN EXTENT
         BCT   R5,P091
         LA    R5,18
         LA    R6,DS5MAVET
P092     MVC   W1+2(2),2(R6)      ANZAHL FREIE CYLS NACH W1
         MVC   W2+3(1),4(R6)      ANZAHL FREIE TRKS NACH W2
         BAL   R7,P094
         LA    R6,5(,R6)          R6 AUF ADRESSE DES FOLGENDEN EXTENT
         BCT   R5,P092
         CLC   DS5PTRDS,=X'0000000000'  FOLGEN WEITERE DSCB FORMAT 5
         BE    P096               NEIN !
         MVC   CCHHR,DS5PTRDS
         OBTAIN CAMOBT2           READ NEXT DSCB FORMAT 5
         CLI   DS5FMTID,C'5'
         BNE   P098
         B     P090
P094     AH    R8,W1+2            ANZAHL FREIE CYLS AUF R8 ADDIEREN
         AH    R9,W2+2            ANZAHL FREIE TRKS AUF R9 ADDIEREN
         BR    R7
P096     CVD   R8,D1
         MVC   Z04+46(7),=X'4020204B202120'
         ED    Z04+46(7),D1+5
         CVD   R9,D2
         MVC   Z04+74(7),=X'4020204B202120'
         ED    Z04+74(7),D2+5
         LM    R5,R10,SPEI1
         B     P075
P098     MVC   Z04+85(21),=C'NEXT DSCB 5 NOT FOUND'
         B     P096
P100     EQU   *                  FORMAT 4
         MVC   CCHHRSV,HIWATER    SAVE HIGH WATER MARK
         MVC   RPT,RECPTR         SAVE NO.OF DSCB'S PER TRACK
         MVI   RPT+1,0
         B     P075
P110     ABEND 1000,DUMP
P120     CLOSE (SORTIN)
P128     CVB   R1,DZLR
         ST    R1,ANZSP44         ANZAHL SAETZE
         MH    R1,=H'140'         ANZ. * LAENGE F. FORMAT1-DSCB
         LTR   R1,R1              MINDESTENS 1 DATASET VORHANDEN ?
         BNH   P345               NEIN
         ST    R1,P130+4          KERNSPEICHERBEDARF F. SORT.
         ST    R1,P360+4          F. FREEMAIN
P130     GETMAIN EC,LV=0,A=ASORTBER
         LTR   R15,R15
         BNZ   P140               GETMAIN ERFOLGLOS -> SORT/MERGE
*        SORTIERUNG PER SP44 IM KERNSPEICHER
         USING IHADCB,R1
         LA    R1,SORTOUT
         MVC   DCBDDNAM,=C'SORTIN  '
         MVC   DCBEODAD+1(3),=AL3(P134)
         OPEN  (SORTOUT,INPUT)    DATEI DER UNSORTIERTEN SAETZE
         L     R2,ASORTBER
         ST    R2,STR2
P132     EQU   *
         GET   SORTOUT,(R2)       UNSORTIERTE SAETZE FUER SP44
         LA    R2,140(R2)         SORTIERBEREICH BEREITSTELLEN
         B     P132
P134     EQU   *                  EOF F. SYSUT2
         CP    ZLR,=P'1'
         BNH   P138
         LA    R1,APARSP44        PARAMETERLISTE
         L     R15,ASP44
         BALR  R14,R15            CALL KERNSPEICHERSORT
P138     OI    SWT,X'08'          BIT4 = 1 : KEIN LESEN
         B     P146
*        SORTIERUNG DURCH SORT MIT EIN-/AUSGABE
P140     EQU   *
         LA    R1,SORLIST
         LINK  EP=SORT,MF=(E,(1))
         USING IHADCB,R1
         LA    R1,SORTOUT
         MVC   DCBDDNAM,=C'SORTOUT '
         MVC   DCBEODAD+1(3),=AL3(P350)
         OPEN  (SORTOUT,INPUT)    DATEI DER SORTIERTEN SAETZE
         NI    SWT,X'F7'          BIT4 = 0 : LESEN
P146     EQU   *
         LA    R1,DRUCK
         TM    DCBOFLGS,X'10'
         BO    P148
         OPEN  (DRUCK,OUTPUT)
P148     EQU   *
P150     TM    SWT,X'08'
         BO    P152               SORTIERUNG PER SP44 -> KEIN LESEN
         GET   SORTOUT,DSNAME
         B     P154
P152     EQU   *                  SORTIERUNG PER SP44
         SP    ZLR,=P'1'
         BM    P350               ALLE SAETZE VERARBEITET
         L     R2,STR2
         MVC   DSNAME(140),0(R2)  NAECHSTER SATZ AUS SORTIERBEREICH
         LA    R2,140(R2)         NAECHSTER SATZ IM SORTIERBEREICH
         ST    R2,STR2
P154     EQU   *
         MVC   Z,=CL132' '
         MVC   ZDSNAME,DSNAME
         SR    R4,R4              CRATION DATE
         IC    R4,DS1CREDT        JAHR (BINAER)
         CVD   R4,D
         MVO   F(3),D+6(2)
         LH    R4,DS1CREDT+1      MASCHINENTAG (BINAER)
         CVD   R4,D
         TM    DS1DSORG+1,X'80'   VSAM-DATEI ?                  5.4.75*
         BO    P185               JA                            5.4.75*
         MVC   ZCREDT,=C'******'  MERKMAL,DASS EBEN GEPR. BIT ^ ZIEHT *
         CLC   =C'VSAMDSET.',DSNAME VSAMDATEI (UNIQUE) ?        5.4.75*
         BE    P185               JA                            5.4.75*
         CLC   =C'Z999999',DSNAME VSAMDATEI (SUBALLOCATED) ?    5.4.75*
         BE    P185               JA                            5.4.75*
         ZAP   F+2(2),D           F = 00JJTTT+ (GEPACKT)
         L     R1,F
         TAG   ZCREDT,R,K,MF=(E,TAG)
         SR    R4,R4
         IC    R4,DS1EXPDT
         CVD   R4,D
         MVO   F(3),D+6(2)
         MVC   D(2),DS1EXPDT+1
         LH    R4,D
         CVD   R4,D
         ZAP   F+2(2),D
         L     R1,F
         TAG   ZEXPDT,R,K,MF=(E,TAG)
         CLC   ZEXPDT,=C'000100'
         BH    *+10
         MVC   ZEXPDT,=CL6' '
         SR    R4,R4
         IC    R4,DS1NOEPV        NO OF EXTENTS
         CVD   R4,D
         MVC   ZNOEPV,=X'40202020'
         ED    ZNOEPV,D+6
         TM    DS1DSORG,X'80'     DSORG
         BZ    P160
         MVC   ZDSORG,=C'IS'      ISAM
         B     P200
P160     TM    DS1DSORG,X'40'
         BZ    P170
         MVC   ZDSORG,=C'PS'      SAM
         B     P200
P170     TM    DS1DSORG,X'20'
         BZ    P180
         MVC   ZDSORG,=C'DA'      DIRECT
         B     P200
P180     TM    DS1DSORG,X'02'
         BO    P190
         MVC   ZDSORG-2(5),=C'UNDEF' UNDEFINIERT
         B     P210
P185     MVC   ZDSORG-1(4),=C'VSAM' VSAM-DATEI                        *
         B     P290               TRACKS ERRECHNEN                    *
P190     MVC   ZDSORG,=C'PO'      PARTITIONED
P200     TM    DS1DSORG,X'01'
         BZ    P210
         MVI   ZDSORG+2,C'U'      UNMOVABLE
P210     EQU   *                  RECFM
         LA    R4,ZRECFM+1        POINTER C
         TM    DS1RECFM,X'C0'
         BNO   P220
         MVI   ZRECFM,C'U'        UNDEFINED
         B     P230
P220     MVI   ZRECFM,C'F'
         TM    DS1RECFM,X'80'     FIXED
         BO    P230
         MVI   ZRECFM,C'V'        VARIABLE
P230     EQU   *
         TM    DS1RECFM,X'10'     BLOCKED
         BZ    P240
         MVI   0(R4),C'B'
         LA    R4,1(,R4)
P240     TM    DS1RECFM,X'20'     TRACK OVERFLOW
         BZ    P250
         MVI   0(R4),C'T'
         LA    R4,1(,R4)
P250     TM    DS1RECFM,X'08'     STANDARD / SPANNED
         BZ    P260
         MVI   0(R4),C'S'
         LA    R4,1(,R4)
P260     TM    DS1RECFM,X'04'     ASA CONTROL CHAR
         BZ    P270
         MVI   0(R4),C'A'
         LA    R4,1(,R4)
P270     TM    DS1RECFM,X'02'     MASCHINE CONTROL CHAR
         BZ    P280
         MVI   0(R4),C'M'
P280     EQU   *
         LH    R4,DS1BLKL         BLKSIZE
         CVD   R4,D
         MVC   ZBLKL,=X'402020202120'
         ED    ZBLKL,D+5
         LH    R4,DS1LRECL        LRECL
         CVD   R4,D
         MVC   ZLRECL,=X'402020202120'
         ED    ZLRECL,D+5
P290     EQU   *                  TRACKS ERRECHNEN
         STM   R6,R8,SPEI1
         SLR   R10,R10            TRACKZAEHLER PRO DATEI
         MVC   F,=X'00000000'
         MVC   F+3(1),DS1NOEPV    ANZAHL EXTENTS
         L     R4,F                  NACH REGISTER 4
         MVC   D,DS1EXT1+2        1. EXTENT
         BAL   R5,P330
         MVC   D,DS1EXT2+2        2. EXTENT
         BAL   R5,P330
         MVC   D,DS1EXT3+2        3. EXTENT
         BAL   R5,P330
         MVC   CCHHR,DS1PTRDS
         B     P295
P294     EQU   *
         MVC   CCHHR,DS3ADEXT+90
P295     EQU   *
         OBTAIN CAMOBT1           READ DSCB FORMAT 3
         CLI   DS3FMTID,C'2'      FORMAT 2 DSCB ?
         BE    P294               JA
         CLI   DS3FMTID,C'3'
         BNE   P340
         LA    R8,4               EXTENT 1 - 4 DES DSCB FORMAT 3
         LA    R9,DS3EXTNT        ADRESSE DES 4. EXTENT NACH REG. 9
P300     MVC   D,2(R9)
         BAL   R5,P330            VERARBEITUNG VON EXTENT 4 - 7
         LA    R9,10(,R9)
         BCT   R8,P300
         LA    R8,9               EXTENT 5 -13 DES DSCB FORMAT 3
         LA    R9,DS3ADEXT        ADRESSE DES 8. EXTENT NACH REG. 9
P301     MVC   D,2(R9)
         BAL   R5,P330            VERARBEITUNG VON EXTENT 8 - 16
         LA    R9,10(,R9)
         BCT   R8,P301
P302     CVD   R10,D
         SR    R10,R10
         MVC   ZTRKS,=X'402020202020'
         ED    ZTRKS,D+5
P310     EQU   *
         LM    R6,R8,SPEI1
P320     EQU   *
         BAL   R2,P400
         B     P150
P330     SR    R15,R15
         IC    R15,DEVCODE+3
         MH    R15,=Y(L'DEVDESCR)
         LH    R6,D+4             BERECHNUNG DER EXTENT-
         MH    R6,DEVDESCR-L'DEVDESCR+4(R15) OBERGRENZE IN TRACKS
         AH    R6,D+6
         LH    R7,D               BERECHNUNG DER EXTENT-
         MH    R7,DEVDESCR-L'DEVDESCR+4(R15) UNTERGRENZE IN TRACKS
         AH    R7,D+2
         SR    R6,R7
         AH    R6,=H'1'
         AR    R6,R10
         SR    R10,R10
         AR    R10,R6             ANZAHL TRACKS NACH REG. 10 SPEICHERN
         SH    R4,=H'1'
         BZ    P302               ANZAHL EXTENTS DRUCKEN
         BL    P310
         BR    R5
P340     MVC   ZTRKS-3(9),=C'DSCB3NOTF'
         B     P320
P345     MVC   Z(10),=C'NO DATASET'
         BAL   R2,P400
P350     PUT   DRUCK,Z03          UNTERSTREICHEN
         MVC   Z04+18(7),=X'4020204B202120'
         ED    Z04+18(7),DSZ
         PUT   DRUCK,Z04
         ZAP   ZZ,=P'0'
         ZAP   DSZ,=P'0'
         ZAP   DZLR,=P'0'
         TM    SORTOUT+48,X'10'   NO DATASET ?
         BZ    P370               JA
         CLOSE (SORTOUT)
         CLC   ASORTBER,=F'0'
         BE    P370               NUR 1 SATZ ODER SORT PER IERRCO00
P360     FREEMAIN E,LV=0,A=ASORTBER
         MVC   ASORTBER,=F'0'
P370     EQU   *
         NI    SWT,X'F7'
         L     R5,UCBLOOK
         TM    SWT,X'80'
         BO    P015
ENDE     EQU   *
         CLOSE (DRUCK)
         ENDE  RC=0
         SPACE 2
P400     SP    ZZ,=P'1'
         BP    P410
         MVC   CTL,SK1
         PUT   DRUCK,CTL
         MVC   Z00SER,SERNO
         PUT   DRUCK,Z00
         MVC   CTL,WSP1
         PUT   DRUCK,Z01
         ZAP   ZZ,=P'60'
P410     PUT   DRUCK,CTL
         AP    DSZ,=P'1'          ERHOEHEN DATA-SET-ZAEHLER
         XC    Z,Z
         BR    R2
         EJECT
         EJECT
***********************************************************************
*        T T R   I N   M B B C C H H R   U M W A N D E L N            *
***********************************************************************
         DC    A(0)
U000     ST    R14,*-4
         SR    R1,R1
         IC    R1,DEVCODE+3       UCB TYPE
         MH    R1,=Y(L'DEVDESCR)
         MVC   CONVADDR+7(1),TTR+2
         SR    R15,R15
         ICM   R15,3,TTR
         SR    R14,R14
         LH    R0,DEVDESCR-L'DEVDESCR+4(R1)
         DR    R14,R0
         STCM  R14,3,CONVADDR+5
         STCM  R15,3,CONVADDR+3
         L     R14,U000-4
         BR    R14
***********************************************************************
*        D E F I N I T I O N E N                                      *
***********************************************************************
         SPACE 1
         LTORG
         DS    0F
DEVDESCR DS    13BL8
         ORG   DEVDESCR
*                GAP  FAKTOR TRK/CYL BYTE/TRK  UNIT  TYPE
         DC    H'061',H'537',H'0010',H'03625'  2311   1
         DC    H'133',H'512',H'0008',H'20483'  2301   2
         DC    H'108',H'512',H'0010',H'04892'  2303   3
         DC    H'061',H'537',H'0046',H'04984'  2302   4
         DC    H'084',H'537',H'0020',H'02000'  2321   5
         DC    H'430',H'512',H'0008',H'13706'  2305-1 6
         DC    H'198',H'512',H'0008',H'14462'  2305-2 7
         DC    H'101',H'534',H'0020',H'07294'  2314   8
         DC    H'135',H'512',H'0019',H'13030'  3330   9
         DC    H'000',H'000',H'0000',H'00000'  UNGLT  10
         DC    H'000',H'000',H'0000',H'00000'  UNGLT  11
         DC    H'000',H'000',H'0000',H'00000'  UNGLT  12
         DC    H'135',H'512',H'0019',H'13030'  3330-1 13
         DS    0D                 DSCB FORMAT 1
DSNAME   DS    CL44
FORMAT   DS    CL1
HIWATER  DS    CL5
         DS    CL3
DS1CREDT DS    CL3
DS1EXPDT DS    CL3
DS1NOEPV DS    CL1
RECPTR   EQU   DSNAME+74
         ORG   DSNAME+82
DS1DSORG DS    CL2
DS1RECFM DS    CL1
DS1OPTCD DS    CL1
DS1BLKL  DS    HL2
DS1LRECL DS    HL2
DS1KEYL  DS    CL1
DS1RKP   DS    HL2
         DS    CL12
DS1EXT1  DS    CL10
DS1EXT2  DS    CL10
DS1EXT3  DS    CL10
DS1PTRDS DS    CL5
         ORG   DSNAME
DSCB5    DS    0CL140             DSCB FORMAT 5
         DS    CL4
DS5AVEXT DS    CL40               8 AVAILABLE EXTENTS
DS5FMTID DS    CL1                FORMAT
DS5MAVET DS    CL90               18 MORE AVAILABLE EXTENTS
DS5PTRDS DS    CL5                ADRESSE DES NAECHSTEN DSCB FORMAT 5
         ORG   DSNAME+140
         DS    0D                 DSCB FORMAT 3
KEY      DS    CL4
DS3EXTNT DS    CL40
DS3FMTID DS    CL1
DS3ADEXT DS    CL90
         DS    CL5
VOLLIST  DC    H'1'
DEVCODE  DS    CL4
SERNO    DS    CL6
         DC    H'0'
         DS    0F
DEB      DS    CL16
         DC    X'01'
         DS    CL15
DEBVMOD  DS    CL4
         DC    XL12'FFF'
SORLIST  DS    0F
         DC    X'80'
         DC    AL3(PARLIST)
         DS    H
PARLIST  DC    Y(PAREND-PARLIST-2)
         DC    A(SORTA,SORTE,RECA,RECE)
         DC    2F'0'
         DC   2X'FF'
         DC    C'AP'
PAREND   EQU   *
DZLR     DS    0D
         DC    F'0'
ZLR      DC    PL4'0'
SORTA    DC    C' SORT FIELDS=(1,44,CH,A)'
SORTE    DC    C' '
RECA     DC    C' RECORD TYPE=F,LENGTH=(140,140,140)'
RECE     DC    C' '
APARSP44 DS    0F                 PARAMETERLISTE F. SP44
         DC    X'80'
         DC    AL3(PARMSP44)
PARMSP44 DS    0F                 PARAMETER F. SP44
ASORTBER DC    A(0)               A(SORTIERBEREICH)
ANZSP44  DC    F'0'               ANZAHL ELEMENTE
         DC    F'44'              LAENGE SORTIERFELD
         DC    F'0'               REL. POSITION DES DSNAMEN
         DC    F'140'             LAENGE ELEMENT
STR2     DC    F'0'               A(NAECHSTER SATZ IM  SORTIERBEREICH)
UCBLOOK  DC    A(0)
SERLO    DC    XL6'00'
SERHI    DC    XL6'FFFFFFFFFFFF'
SWT      DC    X'00'              BIT0 =1: ALL ONLINE DA-DEV.
*                                 BIT4 =1: SP44 -> KEIN LESEN
         AMCODE
ZZ       DC    PL2'-1'            ZEILENFEHLER
TAG      TAG   MF=L
CTL      DS    CL1
Z        DS    0CL132
ZDSNAME  DS    CL20
         DS    CL1
ZCREDT   DS    CL6
         DS    CL4
ZEXPDT   DS    CL6
         DS    CL4
ZNOEPV   DS    CL4
         DS    CL5
ZDSORG   DS    CL2
         DS    CL8
ZRECFM   DS    CL3
         DS    CL6
ZBLKL    DS    CL6
         DS    CL3
ZLRECL   DS    CL6
         DS    CL3
ZTRKS    DS    CL6
         ORG   Z+132
Z00      DC    X'11',C'VOL=SER='
Z00SER   DC    CL6' '
         DC    CL118' '
Z01      DC    X'11',C'   DSNAME',CL13' '
        DC CL110'CREDT     EXPDT      EXT  DSORG     RECFM      BLKSIZE*
                   LRECL  TRACKS'
Z03      DC    X'09',93C'-'
         DC    CL50' '
Z04      DC    X'09',C'ANZAHL DATA SETS:       ; ANZAHL FREIE CYLS:    *
                    ; ANZAHL FREIE TRKS:        .                      *
                                           '
D        DS    D
W1       DC    F'0'
W2       DC    F'0'
D1       DC    D'0'
D2       DC    D'0'
ASP44    DC    A(0)
F        DS    F
SPEI1    DC    F'0'
SPEI2    DC    F'0'
SPEI3    DC    F'0'
SPEI4    DC    F'0'
SPEI5    DC    F'0'
SPEI6    DC    F'0'
DSZ      DC    PL3'0'             DEF. DES DATA-SET-ZAEHLERS
RETCODE  DS    F
TTR      DS    F
RPT      DS    F
CCHHR    DS    CL5                LATEST CCHHR
CCHHRSV  DS    CL5                HI WATER MARK SAVE AREA
CONVADDR DS    CL8
         SPACE
CAMOBT   CAMLST SEEK,CCHHR,SERNO,DSNAME
CAMOBT1  CAMLST SEEK,CCHHR,SERNO,KEY
CAMOBT2  CAMLST SEEK,CCHHR,SERNO,DSCB5
         LTORG
DRUCK    DCB   DDNAME=DRUCK,DSORG=PS,MACRF=PM,RECFM=FBM,LRECL=133
SORTIN   DCB   DDNAME=SORTIN,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=140
SORTOUT  DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=GM,RECFM=FB,LRECL=140,    *
               EODAD=P350
DS1      DSECT
         IEFUCBOB
         DCBD  DSORG=PS
         END
