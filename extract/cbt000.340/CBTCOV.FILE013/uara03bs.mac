UR211300 TITLE 'WRITE PANVALET-DIRECTORY-INFORMATION TO SYSTEM LOG'
***********************************************************************
*                                                                     *
* AUTOR:        DR.MEDER                                              *
*               ARAL AG, ABT. CSB                                     *
*               WITTENER STR. 45                                      *
*               4630 BOCHUM                                           *
*               TEL. 0234/3153360                                     *
*                                                                     *
* PROGRAMMFUNKTIONEN:                                                 *
*                                                                     *
*        DIE ROUTINE UR211300                                         *
*                                                                     *
*             - WIRD VON ASSEMBLER- UND COBOL-PROGRAMMEN DYNAMISCH    *
*               GERUFEN UND SCHREIBT DIE IHR UEBERGEBENEN PARAMETER   *
*               PAN-NAME, PAN-LEVEL UND AENDERUNGSDATUM ZUSAMMEN      *
*               MIT UHRZEIT, JOB-, STEP- UND PROCSTEP-NAME AUF DAS    *
*               SYSTEM-LOG.                                           *
*                                                                     *
*             - IST VOLL REENTRANT CODIERT, UND KANN DAMIT ZUR VER-   *
*               MEIDUNG VON JOB-CONTROL-AENDERUNGEN (STEPLIB) UND     *
*               FETCH-ZEITEN RESIDENT GEMACHT WERDEN.                 *
*                                                                     *
***********************************************************************
         EJECT
         MACRO
         REGDEF &EQU
         LCLA  &CNT
         LCLC  &R
&CNT     SETA  0
         AIF   ('&EQU' EQ '').A10
&R       SETC  '&EQU'
.A1      ANOP
&R&CNT   EQU   &CNT
&CNT     SETA  &CNT+1
         AIF   (&CNT EQ 16).A20
         AGO   .A1
.A10     ANOP
&R       SETC  'R'
         AGO   .A1
.A20     ANOP
         MEND
         EJECT
UR211300 CSECT
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*              REGISTER EQUATES                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         REGDEF
         SPACE 4
*---------------------------------------------------------------------*
*                                                                     *
*              PROGRAMM - INITIALISIERUNG                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         SAVE  (14,12),,*
         SPACE 2
         BALR  R12,R0
         USING *,R12                   BASISREGISTER
         SPACE 2
         USING IEFTIOT1,R11            BASISREGISTER FUER IEFTIOT1
         SPACE 2
         USING WORKAREA,R10            BASISREGISTER FUER WORKAREA
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*              AUFBEREITEN UND AUSFUEHREN WTL                         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         L     R9,0(R1)                LADEN ADRESSE PARAMETERLISTE
         SPACE 2
         GETMAIN R,LV=WORKEND-WORKAREA    WORKAREA ANFORDERN
         SPACE 1
         LR    R10,R1                  ADRESSE WORKAREA IN REG 10
         SPACE 2
         MVC   ZEXLIST(KEXEND-KEXLIST),KEXLIST  MOVE EXTRACT-LIST
         MVC   ZWTL(KWTLEND-KWTL),KWTL    WTL-MASKE UEBERTRAGEN
         SPACE 2
         MVC   ZWTLEVEL,0(R9)          UEBERTRAGEN LEVEL IN WTL
         MVC   ZWTNAME,3(R9)           UEBERTRAGEN NAME  IN WTL
         MVC   ZWTDATUM,13(R9)         UEBERTRAGEN DATUM IN WTL
         SPACE 2
         TIME  DEC
         SPACE 1
         ST    R0,ZDOWO                ZEIT SPEICHERN
         UNPK  ZDOWO,ZDOWO(4)          ZEIT ENTPACKEN
         MVC   ZWTSTD,ZDOWO+1          STUNDE UEBERTRAGEN
         MVC   ZWTMIN,ZDOWO+3          MINUTE UEBERTRAGEN
         MVC   ZWTSEK,ZDOWO+5          SEKUNDE UEBERTRAGEN
         EJECT
         LA    R1,ZEXLIST
         LA    R8,ZATIOT
         SPACE 1
         EXTRACT (8),MF=(E,(1))
         SPACE 1
         L     R11,ZATIOT              ADRESSE TIOT LADEN
         MVC   ZWTJOB,TIOCNJOB         JOBNAME UEBERTRAGEN
         MVC   ZWTSTEP,TIOCSTEP        STEPNAME UEBERTRAGEN
         MVC   ZWTPROC,TIOCSTEP+8      PROCSTEPNAME UEBERTRAGEN
         SPACE 2
         WTL   MF=(E,ZWTL)             AUSFUEHREN WRITE TO LOG
         SPACE 2
         FREEMAIN R,LV=WORKEND-WORKAREA,A=(10)  WORKAREA FREIGEBEN
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*              RETURN                                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         RETURN (14,12),T,RC=0
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*              LISTFORM FUER EXTRACT-MACRO                            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
KEXLIST  EXTRACT ,FIELDS=TIOT,MF=L
KEXEND   EQU   *
         SPACE 4
*---------------------------------------------------------------------*
*                                                                     *
*              MASKE FUER  W T L                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
KWTL     DS    0F                      LISTFORM FUER WTL
KWTLG    DC    AL2(KWTLEND-KWTL)       LAENGE TEXT
KWTFL    DC    XL2'0000'               MCS-FLAGS
KWTST    DC    CL12'************'
KWTB1    DC    CL2' '
KWTSTD   DC    CL2' '                  STUNDE
KWTP1    DC    C'.'
KWTMIN   DC    CL2' '                  MINUTE
KWTP2    DC    C'.'
KWTSEK   DC    CL2' '                  SEKUNDE
KWTT1    DC    C' JOB  '
KWTJOB   DC    CL8' '                  JOBNAME
KWTS1    DC    C'/'
KWTSTEP  DC    CL8' '                  STEPNAME
KWTS2    DC    C'/'
KWTPROC  DC    CL8' '                  PROCSTEPNAME
KWTT2    DC    C', PROGRAM '
KWTNAME  DC    CL10' '                 PROGRAMM-NAME
KWTT3    DC    C', LEVEL '
KWTLEVEL DC    CL3' '                  AENDERUNGSLEVEL
KWTT4    DC    C', DATE MAINT '
KWTDATUM DC    CL8' '                  DATUM LETZTE AENDERUNG
KWTLEND  EQU   *
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*              DUMMY-SECTION FUER WORKAREA                            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
WORKAREA DSECT
ZDOWO    DS    D                       DOPPELWORT FUER TIME-MACRO
ZATIOT   DS    F                       ZWISCHENSPEICHER TIOTADRESSE
         SPACE 1
ZWTL     DS    0F                      LISTFORM FUER WTL
ZWTLG    DS    XL(L'KWTLG)             LAENGE TEXT
ZWTFL    DS    XL(L'KWTFL)             MCS-FLAGS
ZWTST    DS    CL(L'KWTST)
ZWTB1    DS    CL(L'KWTB1)
ZWTSTD   DS    CL(L'KWTSTD)            STUNDE
ZWTP1    DS    CL(L'KWTP1)
ZWTMIN   DS    CL(L'KWTMIN)            MINUTE
ZWTP2    DS    CL(L'KWTP2)
ZWTSEK   DS    CL(L'KWTSEK)            SEKUNDE
ZWTT1    DS    CL(L'KWTT1)
ZWTJOB   DS    CL(L'KWTJOB)            JOBNAME
ZWTS1    DS    CL(L'KWTS1)
ZWTSTEP  DS    CL(L'KWTSTEP)           STEPNAME
ZWTS2    DS    CL(L'KWTS2)
ZWTPROC  DS    CL(L'KWTPROC)           PROCSTEPNAME
ZWTT2    DS    CL(L'KWTT2)
ZWTNAME  DS    CL(L'KWTNAME)           PROGRAMM-NAME
ZWTT3    DS    CL(L'KWTT3)
ZWTLEVEL DS    CL(L'KWTLEVEL)          AENDERUNGSLEVEL
ZWTT4    DS    CL(L'KWTT4)
ZWTDATUM DS    CL(L'KWTDATUM)          DATUM LETZTE AENDERUNG
         SPACE 1
ZEXLIST  DS    0F                      EXTRACT-LISTFORM
         DS    CL(KEXEND-KEXLIST)
WORKEND  DS    0D
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*              DUMMY-SECTION FUER  T I O T                            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
IEFTIOT1 DSECT
         IEFTIOT1
         END
