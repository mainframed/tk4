*          DATA SET HD122620T  AT LEVEL 004 AS OF 05/04/76
***********************************************************************
***********************************************************************
****           C O P Y R I G H T                                   ****
****           HAMBURGER DATENVERARBEITUNGS G.M.B.H                ****
****           Z.HD. HERRN HUETTEL                                 ****
****           ALTER WALL 12                                       ****
****                                                               ****
****           2000 HAMBURG 11                                     ****
****           TELEFON  040 - 37 00 5-223                          ****
****                                                               ****
****           DIE GEWERBLICHE NUTZUNG DIESES PROGRAMMS DURCH      ****
****           VERKAUF ODER VERMIETUNG IST VERBOTEN                ****
****                                                               ****
****           EINE HAFTUNG FUER DIESES PROGRAMM SEITENS DER       ****
****           HDV BESTEHT NICHT                                   ***
****                                                               ***
****                                                               ***
***********************************************************************
***********************************************************************
         EJECT
UHDV020S CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING *,R15
         STM   R14,R12,12(R13)
         BALR  R3,0
         USING *,R3
         L     R1,0(,R1)
         LM    R7,R11,0(R1)      R7 =A(SORTIERFELD)
*                                R8 =ANZAHL ELEMENTE
*                                R9 =LAENGE SORTIERFELD
*                                R10=RELATIVE POSITION SORTEIRFELD
*                                R11=LAENGE ELEMENT
         SR    R7,R11             A(SORTIERFELD - ELEMENTLAENGE)
         SR    R0,R0
         STH   R11,H
         STH   R10,CLC+2
         STH   R10,CLC+4
         MVZ   CLC+2(1),=X'E0'
         MVZ   CLC+4(1),=X'F0'
         BCTR  R9,0
         LA    R4,255
         LR    R6,R8             R6=K R8=N
P010     SRA   R6,1              K=K / 2 BEI 0=ENDE
         BZ    ENDE
         LR    R5,R8
         SR    R5,R6             R5=N-K
         LA    R2,1              R2=J
P020     CR    R2,R5             J <=N-K
         BH    P010              JA
         LTR   R1,R2             R1=I
P030     EQU   *
         LA    R12,0(R1,R6)      R12=L
         MH    R12,H
         LA    R14,0(R7,R12)     R14 = A(L)
         LR    R15,R1            R15 = A(I)
         MH    R15,H
         AR    R15,R7
         EX    R9,CLC             CLC 0(,R14),0(R15)
         BH    P060
         BL    P035
         AH    R0,=H'1'
         B     P060
*        ELEMENTE DREHEN
P035     LR    R12,R11
         BCTR  R12,0
P040     CR    R12,R4
         BH    P050
         EX    R12,XC1            XC   0(,R14),0(R15)
         EX    R12,XC2            XC   0(,R15),0(R14)
         EX    R12,XC1
P045     SR    R1,R6
         BH    P030
         B     P060
P050     EQU   *
         EX    R4,XC1
         EX    R4,XC2
         EX    R4,XC1
         LA    R14,256(,R14)
         LA    R15,256(,R15)
         SH    R12,=H'256'
         BNM   P040
         B     P045
P060     LA    R2,1(,R2)
         B     P020
ENDE     LR    R15,R0
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
CLC      CLC   0(,R14),0(R15)
H        DC    H'0'
XC1      XC    0(,R14),0(R15)
XC2      XC    0(,R15),0(R14)
         PUSH  PRINT
         PRINT OFF
         DC    C'COPYRIGHT BY HAMBURGER DATENVERARBEITUNGS GMBH '
         DC    C'ALTER WALL 12  2000 HAMBURG 11 '
         DC    C'*',C'&SYSDATE',C'*'
         PRINT ON
         POP   PRINT
         CSECT
         DC    CL21'004HD122620T 05/04/76'
         END
