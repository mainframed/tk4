* STAND 05/20/81
***********************************************************************
*                                                                     *
*    PROGRAMM  = AKALESEN                                             *
*                                                                     *
*    AUFRUF    = CALL AKALESEN                                        *
*        REGISTER 15 = FEHLERCODE  = 0 KEIN FEHLER                    *
*                                  = 4 FEHLERHAFTE EINGABEKARTE       *
*                                  = 8 ASTPARM ENTHAELT KEINE DATEN   *
*                                                                     *
*    ZWECK     = EINLESEN DER ABSTIMMPARAMETERKARTEN (DDNAME=ASTPARM),*
*                PRUEFUNG AUF GUELTIGE ANGABEN,                       *
*                ABSPEICHERN DER DATEN IN KTAB UND STAB               *
*                                                                     *
***********************************************************************
AKALESEN CSECT
         PRINT NOGEN
         XSAVE 12,SAVE,AKALESEN
         AREG
RA       EQU   R7             ATAB
RK       EQU   R8             KTAB
RS       EQU   R9             STAB
RSE      EQU   R10            SETAB
RB       EQU   R11            BAL-REGISTER
         SPACE 2
         TITLE 'V O R S P A N N'
         XC    ERRZWI,ERRZWI
         OPEN  (ASTKT,(INPUT))
         TM    ASTKT+48,X'10'
         BZ    EOF8           ASTPARM FEHLT
*  ADRESSEN VON ATAB, KTAB, STAB IN REGISTER LADEN
         USING DATAB,RA
         L     RA,PATAB       --> ATAB
         L     RK,PKTAB       --> KTAB
         L     RS,PSTAB       --> STAB
         XR    RSE,RSE        -->SETAB=0 BEI 1.SUKARTE
         USING DKTAB,RK
         USING DSTAB,RS
         USING DSETAB,RSE
         LA    R3,PSTAB
         ST    R3,SLAST
         SPACE
         TITLE 'L E S E N  A B S T I M M - K A R T E'
LESEN    GET   ASTKT,EPARM
         MVI   KAS,C' '
LESEN1   EQU   *
*  PRUEFEN KARTENART
         CLI   EKA,C'K'
         BE    BEARBK
         TM    KZKART,JA
         BZ    EOF8           KOPFKARTE FEHLT
         CLI   EKA,KFILE
         BE    BEARBF
         CLI   EKA,KSUM
         BE    BEARBS
         CLI   EKA,C'V'
         BE    BEARBV
*        CLI   EKA,C'I'       KARTENART INTERNE ABST. WIRD
*        BE    BEARBI
         CLI   EKA,KCOMMENT
         BE    BEARBST
         CLI   EKA,KLISTE
         BE    BEARBL
         LA    R2,1           KARTENART FALSCH
FHLMSG   EQU   *
         ST    R2,ERR
         BAL   R11,PRFEHL               FEHLERMELDUNG DRUCKEN
         MVI   ERRZWI,X'04'             FEHLER
         B     LESEN
         SPACE 2
         TITLE 'K O P F K A R T E  A B S P E I C H E R N'
BEARBK   NOP   EOF            ) 2.KOPFKARTE D.H.
         OI    BEARBK+1,X'F0' ) DATENENDE
         OI    KZKART,JA
         MVC   APGM,EKPGM
         MVC   AUEB,EKUEB
         MVC   ALCNR,EKCOPY   LISTENNR FUER KOPIE
         MVC   AASTOLD,EKAST           ALTE ALIST-NR
         MVC   EKAST,=CL10' '
         MVC   ERR,=F'9'               )  ABSTIMMLISTEN-NR =
         CALL  BLANKEX,(EKPGM,EKAST,ERR)   )  PROGRAMMNAME
         MVC   AAST,EKAST
*  ABSTIMMBLATT-NR NORMIEREN
         LA    R2,AASTC
         CALL  MODAST,(EKAST,(R2),K1,ERR)
*              1.ARG   EINGEGEBNE  A-BLATT-NR
*              2.ARG   NORM. A-BLATT-NR
*              3.ARG   = 1 D.W. A-BLATT-NR IST EINGABE
*              4.ARG   FEHLERCODE  0 = OK   NOTO = FEHLER
         CLC   ERR,=F'0'
         BE    BEARBK9
         MVC   AASTC,=CL10' '
         LA    R2,8           ALISTEN-NR FALSCH
         B     FHLMSG
*
BEARBK9  EQU   *  NACH KOPFKARTE LESEN ABBRECHEN,
*                 WENN NUR VAR.TEXTE DRUCKEN
         CLC   ATXTP,=F'0'
         BE    LESEN
         LH    R3,ATXTKZ
         CH    R3,=H'0'
         BH    EOF
         B     LESEN
         SPACE 2
         TITLE 'I N T E R N E  A B S T I M M U N G - KARTE'
BEARBI   EQU   *
         MVI   DRUBER,C' '
         MVC   DRUBER+1(79),DRUBER
         MVC   DRUBER(2),EPARM+10      POS.NR --> DRUCKBEREICH
         CLI   AERGI,SANO
         BNE   *+10                    TEXT NUR IN 1.ZEILE
         MVC   DRUBER+04(18),=C'INTERNE ABSTIMMUNG'
         ZAP   ISUM,=P'0'              ERG.SUMME D.ABSTIMMALG.=0
         LA    R3,EPARM+18             -->ABSTIMMALGORITHMUS
LOOPI    EQU   *             VERGLEICHSSUMME BERECHNEN
         CLI   0(R3),C' '
         BE    LOOPIE                  KEINE WEITERE SUM.ZU VERKNUEPF.
         LA    R4,1(,R3)
         CALL  SUCHADR,((R4),DWORD,DWORD+4,ERR),VL
         CLC   ERR,=F'0'
         BNE   IFALSCH                 ABSTIMMSUMME FEHLT
         L     R4,DWORD                -->ABSTIMMSUMME
         LH    R5,DWORD+4              LAENGE ABSTIMMSUMME
         BCTR  R5,0                    LAENGE FUER EX
         CLI   ASPRACHE,FORTRAN
         BNE   NOFORT
         L     R1,0(R4)                ABST.SUM BINAER
         CVD   R1,DWORD                ABST.SUM GEPACKT
         LA    R4,DWORD                -->GEPACKTE ABST.SUM
NOFORT   EQU   *
         XR    R2,R2                   --> ADDITION
         CLI   0(R3),C'+'
         BE    *+8
         LA    R2,6                    --> SUBTRAKTION
         EX    R5,OP(R2)               ADD. BZW. SUB.
         LA    R3,5(R3)                -->NAECHSTE ANGABE IM ALGORITHM
         B     LOOPI
LOOPIE   EQU   *             SUMME LINKS VON = SUCHEN
         CLI   AERGI,SAFALSE
         BE    *+8
         MVI   AERGI,SAOK
         CALL  SUCHADR,(EPARM+13,DWORD,DWORD+4,ERR),VL
         CLC   ERR,=F'0'
         BNE   IFALSCH                 SUMME FEHLT
         L     R4,DWORD                -->SUMME
         LH    R5,DWORD+4              LAENGE SUMME
         BCTR  R5,0
         CLI   ASPRACHE,FORTRAN
         BNE   NOFORT2
         L     R1,0(R4)                ABST.SUM BINAER
         CVD   R1,DWORD                ABST.SUM GEPACKT
         LA    R4,DWORD                -->GEPACKTE ABST.SUM
NOFORT2  EQU   *
         EX    R5,CP                   VERGLEICH BEIDER SUMMEN
         BE    IKARTEND
IFALSCH  EQU   *             INTERNE ABSTIMMUNG FALSCH
         MVC   DRUBER+23(06),=C'FALSCH'
         MVI   AERGI,SAFALSE           INT.ABST.FALSCH
IKARTEND EQU   *             ERGEBNIS DER INT.ABST. DRUCKEN
         MVC   ERR,=F'15'
         CALL  DRUFEHL,(ERR,DRUBER),VL
         B     LESEN
         SPACE 2
         TITLE 'F-KARTE'
BEARBF   EQU   *    FILE-KARTE
         LA    R2,LKTABF(RK)            LAENGE KTAB
         BAL   RB,KTABTEST             PRUEFUNG AUF PLATZ IN KTAB
         MVC   TFDDN,EFDDN              DSNAME
         MVC   DDALT,EFDDN
         MVI   TFCLEAR,RNO
         CLI   EFCLEAR,C' '
         BE    BEARBF5
         MVI   TFCLEAR,RYES   TAPE CLEAR
         OI    ASWITCH,CCALL  CLEARTAPE AUFRUFEN
BEARBF5  EQU   *
         XC    SUKAOK,SUKAOK
         B     BEARBEND
         TITLE 'LISTENKARTE'
*
BEARBL   EQU   *   LISTEN KARTE
         LA    R2,LKTABL(RK)  LAENGE KTAB
         BAL   RB,KTABTEST    PRUEFUNG AUF PLATZ IN KTAB
         XC    SUKAOK,SUKAOK
         MVC   TLDDN,DDALT    DDNAME
         MVC   TLNR,ELNR      LISTEN-NR.
         OI    ASWITCH,LKARTE L-KARTE VORHANDEN
         LA    R4,TLSADR
         LA    R5,TLSLENG
         MVC   DWORD(1),ESDEZ
         CALL  SUCHADR,(ESTABIX,(R4),(R5),ERR,DWORD),VL
         LH    R3,DWORD
         STH   R3,TLSDEZ      DEZIMALSTELLEN DER SUMME
         MVI   KAS,C'S'
         CLC   ERR,=F'0'
         BE    BEARBEND       OK
         CLC   ESTABIX(4),=CL10' '
         BNE   IXFALSCH
         MVI   KAS,C'*'
         LA    R3,0           WENN DER SUMMENINDEX
         STH   R3,TLSDEZ      FUER DIE SEITENANZAHL
         LA    R3,1           FALSCH IST, WIRD EINE
         STH   R3,TLSLENG     ABSTIMMSUMME = -1 ANGENOMMEN
         LA    R3,=P'-1'      --> AUF DEM DRUCKAUFTRAG
         ST    R3,TLSADR      WIRD DIE SEITENZAHL =
         CLI   ASPRACHE,FORTRAN     BLANK GEDRUCKT
         BNE   BEARBEND
         LA    R3,4
         STH   R3,TLSLENG
         LA    R3,=F'-1'
         ST    R3,TLSADR
         B     BEARBEND
         TITLE 'KOMMENTARKARTE'
*
BEARBST  EQU   *    KOMMENTARKARTE
         MVI   KAS,C' '
         LA    R2,LKTABK(RK)           LAENGE KTAB
         BAL   RB,KTABTEST             PRUEFUNG AUF PLATZ IN KTAB
         MVC   TKOM,ETEXT               KOMMENTAR
         XC    SUKAOK,SUKAOK
         B     BEND10
         TITLE 'V-KARTE'
BEARBV   EQU   *
         TM    EVAR,JA
         BO    BEARBS
         SPACE 1
*  V-KARTE LAUT S-KARTE NICHT ERLAUBT
BEARBV5  EQU   *
         LA    R2,18
         B     FHLMSG
         TITLE 'S-KARTE'
BEARBS   EQU   *    SUMMENKARTE
         MVI   KAS,C' '
         LA    R2,LKTABS(RK)            LAENGE
         BAL   RB,KTABTEST             PRUEFUNG AUF PLATZ IN KTAB
         MVC   TSBEZ,ESBEZ              SUMMENTEXT
         MVC   TSMIT,ESMIT              TEXT ABZUST.MIT
         XC    TSETAB,TSETAB            -->SETAB = 0
         XC    TSTAB,TSTAB              --> STAB = 0
         B     MVCSTAB
         TITLE 'EINTRAG IN KTAB ABSCHLIESSEN'
SUKAENDE EQU   *
         ST    RS,TSTAB       --> IN KTAB AUF STAB
SUKAEND1 EQU   *
         LA    R2,LKTABS(RK)            LAENGE
         B     BEND10
BEARBEND EQU   *
         NI    EVAR,X'FF'-JA
BEND10   EQU   *
         ST    R2,KNEXT                 --> NAECHSTE KTAB
         MVC   KNEXT(1),EKA  KARTENKENNUNG --> KTAB
         ST    RK,KLAST                --> KTAB MERKEN
         LR    RK,R2
         CLI   KAS,C' '       STEHT IN KAS EIN KARTENTYP,
         BE    LESEN          WIRD DIE LETZTE KARTE
         MVC   EKA,KAS        NOCH EINMAL VERARBEITEN
         B     LESEN1         MIT DIESEM KARTENTYP
KTABTEST EQU   *
         C     R2,PKTABE
         BLR   RB                      PLATZ IN KTAB --> RUECHSPRUNG
*  KTAB ERWEITERN
         L     R3,GMLK        ALTE GETM.LAENGE
         A     R3,GMLKK
         ST    R3,GMLK        NEUE GETM.LAENGE
         ST    R3,GMLK+4
         GETMAIN VC,LA=GMLK,A=GMEK
         LTR   R15,R15                 RETURNCODE
         BZ    KTABNEU                 KSP ZUGEORDNET
         LA    R2,4                     KTAB VOLL
         B     NOSPACE
KTABNEU  EQU   *    NEUE KTAB-POINTER SETZEN
         L     R2,GMEK                 --> KSP NEU
         ST    R2,KTNEU                 --> 1.EINTR. NEUE KTAB
         LR    R3,R2
         A     R3,GMEK+4               + LAENGE KSP =
         ST    R3,PKTABE               NEUES KTAB-ENDE
         L     R3,KLAST                --> LETZTE KTAB
         ST    R3,KTALT                 --> LETZTE EINTR.ALTE KTAB
         ICM   R2,8,0(R3)              KARTENKENNUNG
         ST    R2,0(,R3)               KNEXT IN LEZTER KTAB AENDERN
         LR    RK,R2                   --> NEUE KTAB
         S     RB,=F'8'
         BR    RB                      RUECKSPRUNG RB - 8
         SPACE 2
         TITLE 'STAB UND SETAB AUFBAUEN'
*              D A T E N  A U S  S U M M E N K A R T E  I N
*              S T A B  U N D  S E T A B  A B S P E I C H E R N
*              ------------------------------------------------
MVCSTAB  EQU   *                   PLATZ IN STAB VORHANDEN
         LTR   RSE,RSE
         BZ    BSUMALLG                 1.SUMMENKARTE
         CLC   ESTABIX(4),=C'    '      SUMMENINDEX VORHANDEN ?
         BE    BSUMMIT                  NEIN --> FOLGEKARTE
*  PRUEFEN, OB PLATZ FUER NEUE STAB-EINTRAGUNG VORHANDEN IST
         SPACE 1
*  V-KARTE MIT SUMMENINDEX NICHT ERLAUBT
         NI    EVAR,X'FF'-JA
         CLI   EKA,C'V'
         BE    BEARBV5        FEHLER
         SPACE 1
         LA    R2,LSTAB(RSE)       LAENGE STAB + ZUSAETZL. SNEXT
         BAL   RB,STABTEST             PRUEFUNG AUF PLATZ IN STAB
         B     MVCSTAB1            GENUEGEND PLATZ
*  ABBRUCH DES KARTENLESENS, DA STAB VOLL
STABVOLL EQU   *
         LA    R2,3                STAB VOLL
NOSPACE  EQU   *
         ST    R2,ERR
         BAL   R11,PRFEHL               FEHLERMELDUNG DRUCKEN
         MVI   ERRZWI,X'04'             FEHLER
         B     EOF                 SPRUNG ZUR BEARB. KARTENENDE
MVCSTAB1 EQU   *
         MVC   ERR,=F'7'                KENNUNG = POS-NR FALSCH
         MVC   DWORD(2),ESBEZ+1         POS-NR AUF NUMERISCH PRUEFEN
         NC    DWORD(2),=X'F0F0'
         CLC   DWORD(2),=X'F0F0'
         BNE   SKFEHLER                 NICHT NUMERISCH
         CLC   ESBEZ+1(2),SPOS          POS-NR AUFSTEIGEND
*        BNH   SKFEHLER                 NEIN
         ST    RS,SLAST                --> STAB MERKEN
         ST    RSE,SNEXT                ALTE EINTRAGUNG ABSCHLIESSEN
         LR    RS,RSE
BSUMALLG EQU   *                   STAB-FELDER FUELLEN
         NI    EVAR,X'FF'-JA
         MVC   SPOS,ESBEZ+1        POSITIONS-(ZEILEN-)NR
         MVI   SAERG,SANO               NICHT ABGESTIMMT
         MVI   SUPDTE,SUYES                   UPDATE D- SUMMEN
         ZAP   SSUM,=PL1'0'             SUMMEN INI=
         ZAP   SVERSUM,=PL1'0'          TIALISIEREN
         MVI   SSUMDA,SANO
         MVI   SAVERNR,0               LOESCHEN
         MVC   SIX,ESTABIX              TABELLENINDEX D.ABST.SUMME
*  SUMMENADRESSE UND DEZIMALSTELLEN
         LA    R3,ESTABIX             1.ARGUMENT TAB.INDEX
         LA    R4,SADR                  2. ARGUMENT SUMMENADRESSE
         LA    R5,SLENG                 3. ARGUMENT SUMMENLAENGE
         MVC   DWORD(1),ESDEZ
         CALL  SUCHADR,((R3),(R4),(R5),ERR,DWORD),VL
         CLC   ERR,=F'0'
         BE    IXOK                   ADRESSE GEFUNDEN
IXFALSCH EQU   *
         LA    R2,2                   SUMMENINDEX FALSCH
         ST    R2,ERR
SKFEHLER EQU   *
         BAL   R11,PRFEHL               FEHLERMELDUNG DRUCKEN
         MVI   ERRZWI,X'04'             FEHLER
         XC    SUKAOK,SUKAOK            SUKARTE ABGEWIESEN
         B     LESEN                  D-H. SUMMENKARTE WIRD UEBERGANGEN
IXOK     EQU   *
         LH    R2,DWORD
         STH   R2,SDEZ        DEZIMALSTELLEN SUMME
         MVC   ESTABIX(5),=CL10' '
         XC    NSETAB,NSETAB          ANZAHL SETAB = 0
         OI    SUKAOK,X'01'             SUKARTE OK
         LA    RSE,LSTAB(RS)            --> 1.SETAB-FELD
         B     BSUMMIT1
*  ANGABEN UEBER SUMMEN, MIT DENEN ABGEST.WERDEN SOLL IN
*  SETAB ABSPEICHERN
BSUMMIT  EQU   *
         CLI   SUKAOK,0                 STAB ANGELEGT ?
         BE    LESEN                    NEIN
BSUMMIT1 EQU   *
         CLC   ESMIT+14(10),=CL10' '
         BE    SUKAENDE                 KEINE ABSTIMMANGABEN
         SPACE 1
*  PRUEFUNG, OB V-KARTEN ERLAUBT SIND
         CLC   ESMIT+14(10),=CL10'*ABSTVAR'
         BNE   BSUMMI10       NEIN
         OI    EVAR,JA
         OI    SSUMDA,SAVAR  VARIABLE SUMMEN
         B     SUKAENDE
BSUMMI10 EQU   *
         LA    R2,LSETAB(RSE)
         BAL   RB,STABTEST             PRUEFUNG AUF PLATZ IN STAB
*  ANGABE ABZUSTIMMEN-MIT PRUEFEN U.IN STANDARTFORMAT UMFORMEN
         LA    R2,SEMITTXT
         CALL  MODAST,(ESMIT+14,(R2),K2,ERR)
*              1.ARG = EINGEGEBENE ANGABE ABZUSTIMMEN-MIT
*              2.ARG = NORMIERTE ANGABE
*              3.ARG = AZUMIT IST EINGABE
*              4.ARG = FEHLERCODE  0 = OK   NOTO = FEHLER
         CLC   ERR,=F'0'
         BE    BSUMMIT2                 ANGABE OK
         LA    R2,5                     ABZUSTIMMEN-MIT FALSCH
         ST    R2,ERR
         BAL   R11,PRFEHL               FEHLERMELDUNG DRUCKEN
         MVI   ERRZWI,X'04'             FEHLER
         B     SUKAENDE                 D.H. ABST.HINWEIS NICHT BEACHT.
BSUMMIT2 EQU   *
*  ABSTIMMERGAENZUNG ANALYSIEREN
*  OPERATION UND PERIODE BESTIMMEN
         LA    R2,SEOP
         LA    R3,SEPER
         MVC   DRUBER(14),ESMIT
         CALL  ANALYERG,(DRUBER,(R2),(R3),ERR)
*              1.ARG  ABSTIMMERGAENZUNG
*              2.ARG  CODE FUER RECHENOPERATION
*              3.ARG  INDEX DER PERIODE,MIT DER ABZUSTIMMEN IST
         TM    SEOP,ODER
         BZ    BSUMMI20       OK
         SPACE 1
*  ODER UND VARIABLE SUMMEN NICHT GLEICHZEITIG ERLAUBT
         TM    EVAR,JA
         BZ    BSUMMI20       OK
         LA    R2,19          FEHLER-NR
         B     BSUMMI25
BSUMMI20 EQU   *
         CLC   ERR,=F'0'
         BE    BSUMMIT3
         LA    R2,6                     ABSTIMMERGAENZUNG FALSCH
BSUMMI25 EQU   *
         ST    R2,ERR
         CALL  DRUFEHL,(ERR,EPARM),VL   FEHLERMELDUNG DRUCKEN
         MVI   ERRZWI,X'04'             FEHLER
         CLC   ESBEZ+1(2),=CL2'  '
         BNE   SUKAENDE       1.SUMMENKARTE NICHT UEBERGEHEN
         B     LESEN                    D.H. KARTE WIRD UEBERGANGEN
BSUMMIT3 EQU   *
*  PRUEFUNG AUF INTERNE ABSTIMMUNG
         CLC   SEMITTX1,AASTC
         BNE   EXTOK1                   BEZUG AUF ANDERES ABSTIMMBLATT
         CLC   SEPER,=H'1'              LETZTE PERIODE ?
         BNE   EXTOK                    NEIN
*  INTERNE ABSTIMMUNG --> ADRESSE DER ABST.SUMME SUCHEN
         L     R3,PSTAB                 --> 1.STAB
         DROP  RS
         USING DSTAB,R3
LOOPINT  EQU   *              STAB AUF POSITIONS-NR. DURCHSUCHEN
         CR    R3,RS
         BE    INTFALSE                 LFD. STAB NICHT ERREICHT
         CLC   SEMITTX2,SPOS            VERGLEICH D. POS.NR
         BE    INTOK
         L     R3,SNEXT                 --> NAECHSTE STAB
         B     LOOPINT
INTFALSE EQU   *
         LA    R2,5                     INT.ABSTIMMHINWEIS FALSCH
          B     FHLMSG         FEHLERMELDUNG DRUCKEN
INTOK    EQU   *              INTERER ABSTIMMHINWEIS AUF VORANG.POS.
         ST    R3,SEMITTXT              --> STAB, DIE AZUMIT BESCHREIBT
         DROP  R3
         USING DSTAB,RS
         MVI   SATYP,SAINT              ABST.TYP = INTERN
         B     BSUMMITE
*  EXTERNE ABSTIMMUNG
EXTOK    EQU   *              BEZUG AUF GLEICHES AST-BLATT VORPERIODE
         LH    R2,SEPER                 PERIODE RICHTIG STELLEN
         SH    R2,=H'1'
         STH   R2,SEPER
EXTOK1   EQU   *
         MVI   SATYP,SAEXT              ABST.TYP = EXTERN
BSUMMITE EQU   *
         ZAP   SESUM,=PL1'0'            SUMME LOESCHEN
         MVI   SESUMDA,SANO
         SPACE 1
*  SETZEN KENNUNG OB S BZW V KARTE
         CLI   EKA,C'S'
         BNE   DOS910
         OI    SEKZ,SEDO      S
         B     DOS920
DOS910   EQU   *
         OI    SEKZ,SEDOERR   V
DOS920   EQU   *
         LH    R2,NSETAB
         AH    R2,=H'1'
         STH   R2,NSETAB                ANZAHL SETAB + 1
         ST    RSE,TSETAB               -->SETAB, DIE ZU KTAB GEHOERT
         LA    RSE,LSETAB(RSE)          --> NAECHSTE SETAB
         B     SUKAENDE
         TITLE 'PLATZ IN STAB PRUEFEN'
*  VERFUEGBAREN PLATZ IN STAB PRUEFEN UND ZUSAETZLICHEN PLATZ ANF.
STABTEST EQU   *
         C     R2,PSTABE
         BLR   RB                      PLATZ IN STAB --> RUECKSPRUNG
*  STAB ERWEITERN
         L     R3,GMLS        ALTE GETM.LAENGE
         A     R3,GMLSK
         ST    R3,GMLS        NEUE GETM.LAENGE
         ST    R3,GMLS+4
         GETMAIN VC,LA=GMLS,A=GMES
         LTR   R15,R15
         BNZ   STABVOLL                KEIN KSP ERHALTERN
*  NEUE STAB-POINTER SETZEN
         L     R2,GMES                 --> KSP-NEU
         LR    R3,R2
         A     R3,GMES+4               KSP-LAENGE
         ST    R3,PSTABE               NEUES STAB-ENDE
         CLC   ESTABIX(4),=C'    '
         BNE   STABG5                  LETZTE POS.NR VOLLST.BEARB
*  UNVOLLSTAENDIGE STAB-EINTRAGUNG IN NEUEN BEREICH UEBERTRAGEN
         LR    R3,RSE
         SR    R3,RS                   LAENGE DER EINTRAGUNG
         LR    R4,RS
         LR    R5,R3                   MVC-LAENGE
         STM   R2,R3,DWORD
         MVCL  R2,R4                   MOVE IN NEUEN BEREICH
         LM    R2,R3,DWORD
         L     R4,SLAST                -->LETZTE VOLLSTAENDIGE EINTR.
         ST    R2,0(,R4)               SNEXT IN LETZTER EINTR.AENDERN
         ST    RS,DWORD+4               -->UNVOLLST.STAB
         LR    R1,R2
         SR    R1,RS
         ST    R1,DWORD                 DIFF  STAB-NEU / -ALT
         LR    RS,R2
         LA    R2,0(R3,R2)             --> NEUE EINTRAGUNG STAB
         B     CHGKTAB
STABG5   EQU   *
         LR    RSE,R2                  --> NEUE EINTRAGUNG STAB
         S     RB,=F'8'
         BR    RB                      RUECKSPRUNG RB - 8
*
*  STAB-POINTER IN KTAB AUF STAB IN NEUEN BEREICH VERAENDERN
CHGKTAB  EQU   *
         LR    R5,RK                    --> LFD.KTAB
LPKTAB   EQU   *
         LA    R6,LKTABS
         DROP  RK
         USING DKTAB,R5
         SR    R5,R6                    --> VORHERIGE KTAB
         C     R5,KTNEU
         BNL   *+14
         L     R5,KTALT                 SPRUNG VON NEUER ZU ALTER KTAB
         XC    KTNEU,KTNEU
         CLC   TSTAB,DWORD+4
         BNE   LPKTABE                  ALLE ZUGEHOERI.STAB VERARBEITET
         L     R1,TSTAB
         A     R1,DWORD                 + DIFF
         ST    R1,TSTAB
         L     R1,TSETAB
         LTR   R1,R1
         BZ    LPKTABE
         A     R1,DWORD                 + DIFF
         ST    R1,TSETAB
         B     LPKTAB
LPKTABE  EQU   *
         DROP  R5
         USING DKTAB,RK
         B     STABG5
         TITLE 'K A R T E N E N D E'
EOF      EQU   *              LETZTE POINTER SETZEN
         MVC   KNEXT,=F'-1'             KTAB-ENDE   --> SNEXT = -1
         LTR   RSE,RSE
         BZ    EOF1                     KEINE SUKSRTE EINGELESEN
         ST    RSE,SNEXT                --> LETZTE STAB-EINTRAGUNG
         LR    RS,RSE
EOF1     EQU   *
         MVC   SNEXT,=F'-1'             STAB-ENDE   --> SNEXT = -1
         CLOSE (ASTKT)
*
         XR    R15,R15                  FEHLERCODE = 0
         IC    R15,ERRZWI           FEHLERCODE = FEHLERZWISPEICHER
         TM    KZKART,JA
         BO    EOF9
EOF8     EQU   *
         MVC   ERR,=F'20'    KOPFKARTE FEHLT
         BAL   R11,PRFEHL
         LA    R15,8          ASTPARM OHNE DATEN
EOF9     EQU   *
         XRETURN (R15)
*
PRFEHL   EQU   *
         CALL  DRUFEHL,(ERR,EPARM),VL
         BR    R11
*
         TITLE 'DEFINITIONEN'
JA       EQU   X'01'
DWORD    DS    D                        DOPPELWORD
ERR      DS    F                        FEHLERKENNZEICHNUNG
ERRZWI   DS    XL1                      FEHLERZWISCHENSPEICHER
SUKAOK   DC    XL1'00'
EVAR     DC    XL1'00'
K1       DC    CL1'1'        KONSTANTE
K2       DC    CL1'2'              KONSTANTE
PATAB    DC    V(ATAB)             -->ATAB
GMLKK    DC    F'2048'        GETMAIN KTAB KONSTANTE
GMLK     DC    F'0'           GETMAIN-LAENGE MIN KTAB
         DC    F'0'                    GETMAIN-LAENGE MAX KTAB
GMEK     DS    A                       GETMAIN-ADRESSE    KTAB
         DS    F                       GETMAIN-LAENGE     KTAB
KLAST    DS    A                       --> LETZTE KTAB-EINTRAGUNG
SLAST    DS    A                       --> LETZTE STAB-EINTRAGUNG
GMLSK    DC    F'4096'        GETMAIN STAB KONSTANTE
GMLS     DC    F'0'                    GETMAIN-LAENGE MIN STAB
         DC    F'0'                    GETMAIN-LAENGE MAX STAB
GMES     DS    A                       GETMAIN-ADRESSE    STAB
         DS    F                       GETMAIN-LAENGE     STAB
KTALT    DC    F'0'                     --> LETZTE EINTR.ALTE KTAB
KTNEU    DC    F'0'                     --S U.EINTR. NEUE KTAB
KZKART   DC    X'00'
ISUM     DS    PL10                    VERGLEICHSSUMME INT.ABST.
KAS      DS    CL1
DDALT    DC    CL8' '
DRUBER   DS    CL80                    DRUCKBEREICH
OP       AP    ISUM,0(0,R4)
         SP    ISUM,0(0,R4)
CP       CP    ISUM,0(0,R4)
         TITLE 'DEFINITION EINGABEKARTEN'
EPARM    DC    CL80' '
         ORG   EPARM
EIDENT   DS    CL8                 KARTENKENNZEICHNUNG (GLEICHE ANGABE)
EKA      DS    CL1                 KARTENART
ETEXT    DS    CL66
ESORT    DS    CL5                 SORTIERFELD
         ORG   EPARM+9             KOPF-PARAMETER
EKPARM   DS    0CL66
         DS    CL1
EKPGM    DS    CL9                 PROGRAMM-NAME
EKAST    DS    CL8                 ABSTIMMLISTEN-NR
EKUEB    DS    CL33                UEBERSCHRIFTSTEXT
         DS    CL9
EKCOPY   DS    CL3            LISTENNR FUER KOPIE
         DS    CL3
         ORG   EPARM+9             FILE-PARAMETER
EFPARM   DS    0CL9
         DS    CL1
EFDDN    DS    CL8                 DS-NAME
         DS    CL21
EFCLEAR  DS    CL1
         ORG   EPARM+9             SUMMEN-PARAMETER
ESBEZ    DS    CL34                SUMMEN-POSITIONS-NR UND TEXT
         ORG   EPARM+39
ELNR     DS    CL3            LISTEN NR
         DS    CL1
ESTABIX  DS    CL2                 TABELLEN-INDEX DER SUMME
ESSUMIX  DS    CL2                 INDEX IN TABELLE
ESDEZ    DS    CL1                 DEZIMALSTELLEN DER SUMME
ESMIT    DS    CL24                ABSTIMMERGAENZUNG + ABZUSTIMMEN MIT
         ORG
         EJECT
*              DCB-DEFINITION
*              --------------
ASTKT    DCB   DDNAME=ASTPARM,EODAD=EOF,DSORG=PS,MACRF=(GM)
         LTORG
*              DSECT-DEFINITIONEN
*              ------------------
         PRINT GEN
         TITLE 'KTAB'
         MKTAB
         LA    R2,LKTABK(RK)            LAENGE KTAB
         TITLE 'STAB UND SETAB'
         MSTAB
         TITLE 'MATAB'
         MATAB
         END
40905,5
         CLI   EKA,C'S'
         BNE   DOS910         V_KARTE
         OI    SEKZ,SEDO
         B     DOS920
DOS910   EQU   *
         OI    SEKZ,SEDOERR
DOS920   EQU   *
