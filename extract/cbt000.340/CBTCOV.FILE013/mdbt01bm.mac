         MACRO
&NAME    TRC   &REG,&TABNAME=
         GBLB  &TABCOK
         GBLC  &TRCTABN
         LCLC  &TN
         AIF   (NOT &TABCOK).NOTAB
&NAME    DS    0H
         ST    14,&TRCTABN+16
         ST    15,&TRCTABN+20
         ST    1,&TRCTABN+24
         AGO   .TESTR1
.NOTAB   ANOP
&NAME    CNOP  2,4
         ST    14,*+90
         ST    15,*+90
         ST    1,*+90
.TESTR1  AIF   ('&REG(1)'(1,1) EQ '(').LR1
         LA    14,&REG(1)
         AGO   .TESTR2
.LR1     LR    14,&REG(1)
         NOPR  0
.TESTR2  AIF   ('&REG(2)'(1,1) EQ '(').LR2
         LA    15,&REG(2)
         AGO   .TESTR3
.LR2     LR    15,&REG(2)
         NOPR  0
.TESTR3  AIF   ('&REG(3)'(1,1) EQ '(').LR3
         LA    1,&REG(3)
         AGO   .PROCESS
.LR3     LR    1,&REG(3)
         NOPR  0
.PROCESS MVO   0(2,14),0(1,15)         ADJUST OFFSET
         MVN   1(1,14),0(15)           SET NUMERIC CHAR
         NI    1(14),X'0F'             FLIP UNWANTED BITS OFF
         AIF   (NOT &TABCOK).NOTAB1
         TR    0(2,14),&TRCTABN        TRANSLATE THE BYTE
         AGO   .NEXT
.NOTAB1  TR    0(2,14),*+34            TRANSLATE THE BYTE
.NEXT    LA    14,2(0,14)              BUMP UP REGISTER
         LA    15,1(0,15)              BUMP UP REGISTER
         BCT   1,*-30                  CONVERT AGAIN IF REG. NZERO
         AIF   (NOT &TABCOK).NOTAB2
         L     14,&TRCTABN+16
         L     15,&TRCTABN+20
         L     1,&TRCTABN+24
         MEXIT
.NOTAB2  L     14,*+32
         L     15,*+32
         L     1,*+32
         B     *+32                    BRANCH TO EXECUTABLE INSTR.
&TN      SETC  '&TABNAME'
         AIF   ('&TN' NE '').NAME
&TN      SETC  'TRC&SYSNDX'
.NAME    ANOP
&TN      DC    C'0123456789ABCDEF'     CHARACTER STRING FOR TR INSTR.
         DS    XL12
&TABCOK  SETB  1
&TRCTABN SETC  '&TN'
         MEND
