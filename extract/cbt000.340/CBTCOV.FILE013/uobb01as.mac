S9210604 CSECT 0
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3                  BASISREGISTER
R4       EQU   4                  LENGTH OF DIRECTORY BLOCK
R5       EQU   5                  LENGTH COUNT
R6       EQU   6                  TABADDRESS
R7       EQU   7                  WORKAREAINDEX
R8       EQU   8                  NUMBER OF HALFWORDS
R9       EQU   9                  MEMBERCOUNT TABELLE
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*
*********************************
*
* DIESES PROGRAMM ERZEUGT EINEN JOB ,DER IN STEP1 DIRUSE
* AUFRUFT ,IN STEP2 DAS PROGRAMM S9210605 UND IN STEP3 DAS
* IEBPTPCH UTILITY. SYSIN-STATEMENTS SIND FUER STEP3 IN
* ALPHABETISCHER REIHENFOLGE GEORDNET.
*
* PARM='PROCEDURE-LIBRARY NAME'
* EXAMPLE: PARM='SYS1.PROCLIB'
*
*
* PROGRAMMIERER: LUCNY
* DATE: 79 05 23
* LEVEL 00
*
*********************************
*
*
         SAVE  (14,12),,LUCNY
         LR    R3,R15
         USING S9210604,R3
         ST    R13,SAVE+4
         LR    R12,R13
         LA    R13,SAVE
         ST    R13,8(R12)
         ST    R1,PARAM           SAVE PARMADDRESS
         B     START
*
*
* SAVEAREAS AND DCB
*
*
SAVE     DC    18F'0'
PARAM    DC    F'0'
DIRECT   DCB   DSORG=PS,MACRF=GM,DDNAME=DIRECT,RECFM=F,BLKSIZE=256,    *
               EODAD=ERROR,LRECL=256
JCL      DCB   DSORG=PS,MACRF=PM,DDNAME=JCL
*
         EJECT
START    EQU   *
         L     R2,0(R1)
         SR    R4,R4
         LH    R4,0(R2)           LENGTH OF PARM IN R4
         BCTR  R4,0
         EX    R4,MVC5                   PARM TO DD-CARD
         EX    R4,MVC6                   PARM TO DD-CARD
         EX    R4,MVC3
         LA    R5,LINE1B+30
         LA    R5,1(R4,R5)
         MVC   0(1,R5),=X'7D'    BRING PARM TO EXEC-CARD
         EX    R4,MVC4
         LA    R5,LINE1F+29
         LA    R5,1(R4,R5)
         MVC   0(1,R5),=X'7D'    BRING PARM TO EXEC-CARD
         CH    R4,=H'19'
         BL    MVSHORT
         MVC   LINE1A(20),2(R2)   MOVE PARM TO JOBCARD
         B     MVLN4
MVSHORT  EQU   *
         EX    R4,MVC1
MVLN4    EQU   *
         EX    R4,MVC2
*
         OPEN  DIRECT
         OPEN  (JCL,OUTPUT)
         SR    R9,R9
         LA    R6,TAB
*
*
*
*
READ     EQU   *
         GET   DIRECT,WORK
         LH    R4,WORK            LENGTH OF DIR.BLOCK
         LA    R5,2               COUNT IN BEGINPOSITION
         LA    R7,WORK+2          WORKINDEX
LOOP     EQU   *
         CR    R5,R4              BLOCKEND?
         BNL    READ
         CLC  0(8,R7),=8X'FF'       LAST DIRECT-MEMBER
         BE   OUTPUT
*
*
*
ALIAS    EQU  *
         TM   11(R7),X'80'        ALIAS?
         BO   NOTAB               NO
         MVC  0(8,R6),0(R7)       MOVE MEMBERNAME TO TAB
         LA   R9,1(R9)            MEMBERCOUNT
         LA   R6,8(R6)                R6=BUMP TABADDRESS
*
         CH   R9,=H'500'          MAXIMAL 500MEMBERS !!!!!!!!!
         BE   OUTPUT
*
*
NOTAB    EQU  *
         SR   R8,R8
         NI   11(R7),X'1F'        LOESCHEN ALIAS-BIT
         IC   R8,11(R7)           R8=NUMBER OF USERHALFWORDS
         AR   R8,R8
         LA   R7,12(R7)           R7=BUMP WORK-INDEX
         AR   R7,R8
         LA   R5,12(R5)           R5=BUMP COUNT
         AR   R5,R8
         B    LOOP
*
*
         EJECT
OUTPUT   EQU   *
         CVD   R9,DEC
         UNPK  LINE7+25(3),DEC+6(2)
         UNPK  LINE7+37(3),DEC+6(2)
         OI    LINE7+27,X'F0'
         OI    LINE7+39,X'F0'
         PUT   JCL,LINE1
         PUT   JCL,LINE1B
         PUT   JCL,LINE1C
         PUT   JCL,LINE1D
         PUT   JCL,LINE1E
         PUT   JCL,LINE1F
         PUT   JCL,LINE1G
         PUT   JCL,LINE1H
         PUT   JCL,LINE1I
         PUT   JCL,LINE2
         PUT   JCL,LINE3
         PUT   JCL,LINE4
         PUT   JCL,LINE5
         PUT   JCL,LINE6
         PUT   JCL,LINE7
         LA    R6,TAB
MEMLOOP  EQU   *
         MVI   WORK,X'40'
         MVC   WORK+1(79),WORK
         MVC   WORK(L'LINE8),LINE8
         MVC   WORK+13(8),0(R6)
         PUT   JCL,WORK
         MVI   WORK,X'40'
         MVC   WORK+1(79),WORK
         MVC   WORK(80),LINE9
         PUT   JCL,WORK
         LA    R6,8(R6)
         BCT   R9,MEMLOOP
*
*
ENDE     EQU   *
         PUT   JCL,LINE10
         CLOSE (DIRECT,,JCL)
         L     R13,SAVE+4
         RETURN (14,12),RC=0
*
*
*
ERROR    EQU   *
         WTO   'INPUT FOR S9210604 FAILED',ROUTCDE=11
         B     ENDE
*
         EJECT
*
*
* DEFINITIONEN
*
*
LINE1    DC    C'//S921LIST JOB 921S,'''
LINE1A   DC    C'                    '''
         DC    CL50' '
LINE1B   DC    CL80'//S1 EXEC PGM=UOBB01BS,PARM=''P'
LINE1C   DC    CL80'//STEPLIB DD DSN=SYS1.TELELIB,DISP=SHR'
LINE1D   DC    CL80'//PLA DD DISP=SHR,DSN='
LINE1E   DC    CL80'//DRU DD SYSOUT=A'
LINE1F   DC    CL80'//S2 EXEC PGM=UOBB01CS,PARM='''
LINE1G   DC    CL80'//STEPLIB DD DSN=SYS1.OSLIB,DISP=SHR'
LINE1H   DC    CL80'//DIRECT DD DISP=SHR,DSN='
LINE1I   DC    CL80'//LIST DD SYSOUT=A'
LINE2    DC    CL80'//S3 EXEC PGM=IEBPTPCH'
LINE3    DC    CL80'//SYSPRINT DD SYSOUT=Z'
LINE4    DC    CL80'//SYSUT1 DD DISP=SHR,DSN='
LINE5    DC    CL80'//SYSUT2 DD SYSOUT=A'
LINE6    DC    CL80'//SYSIN DD *'
LINE7    DC    CL80' PRINT TYPORG=PO,MAXFLDS=000,MAXNAME=000'
LINE8    DC    C' MEMBER NAME='
LINE9    DC    CL80'                             RECORD FIELD=(80,1)'
LINE10   DC    CL80'//'
         DS    0H
WORK     DC    64CL4'WORK'
BYTE     DC    X'00'
MVC1     MVC   LINE1A(1),2(R2)
MVC2     MVC    LINE4+25(1),2(R2)
MVC3     MVC   LINE1B+30(1),2(R2)
MVC4     MVC   LINE1F+29(1),2(R2)
MVC5     MVC   LINE1D+22(1),2(R2)
MVC6     MVC   LINE1H+25(1),2(R2)
DEC      DC    D'0'
         LTORG
*
TAB      DC    500CL8'TABELLET'
         END   S9210604
