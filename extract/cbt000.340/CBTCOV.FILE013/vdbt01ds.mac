IEFUTL   CSECT
R0       EQU   0                  INDIKATOR FUER JOB/STEP TIME LIMIT
R1       EQU   1                  A(A(COMMON EXIT PARAMETER AREA))
R2       EQU   2
R3       EQU   3                  BASISREG. FUER IEFUTL
R4       EQU   4                  BASISREG. FUER USERAREA
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                 R10 = R0 BEIM ENTRY POINT
R11      EQU   11                 R11 = R1 BEIM ENTRY POINT
R12      EQU   12
R13      EQU   13                 A(SAVE AREA) = BASISREG. FUER DUMMY
R14      EQU   14
R15      EQU   15                 RETURN CODE
FR0      EQU   0
FR1      EQU   1
FR2      EQU   2
*
***********************************************************************
*        REENTRANT LINKAGE CONVENTIONS
*
         SAVE  (14,12)            IN RUFENDES PGM
         LR    R3,R15             BASISREG. LADEN
         USING IEFUTL,R3
         B     ANFANG
         DC    CL8'IEFUTL'
         DC    CL8'&SYSTIME',CL8'&SYSDATE'
ANFANG   LR    R10,R0             INDIKATOR RETTEN
         LR    R11,R1             A(COMMON EXIT PAR. AREA PNTR.) RETTEN
         GETMAIN R,LV=LDUMMY,SP=255
         LTR   R15,R15            GETMAIN O.K. ?
         BNZ   NOCORE             NEIN
         ST    R13,4(R1)          A(ALTE SAVE AREA) IN NEUE SAVE AREA
         ST    R1,8(R13)          A(NEUE SAVE AREA) IN ALTE SAVE AREA
         LR    R13,R1             A(NEUE SAVE AREA) BASISREG.FUER DUMMY
         USING DUMMY,R13
*
***********************************************************************
*
*
         L     R5,0(R11)          A(COMMON EXIT PARAMETER AREA)
         L     R4,32(R5)          A(USER AREA) GETMAIN WAR IN IEFUJI
         USING USERAREA,R4        BASIS REGISTER FUER USERAREA
         LTR   R4,R4              USER AREA VORHANDEN ?
         BNZ   BRTAB              JA
         WTO   '----> TIME-PARAMETER KANN NICHT VERLAENGERT WERDEN - KE*
               INE USER AREA VORHANDEN (IEFUTL) <----',ROUTCDE=(10,11)
         B     CANCEL
*
BRTAB    B     *+4(R10)
         B     JOBLIM             R10=0 --> JOB CPU TIME LIMIT
         B     STEPLIM            R10=4 --> STEP CPU TIME LIMIT
         B     WAITLIM            R10=8 --> JOB WAIT TIME LIMIT
*
***********************************************************************
*        JOB TIME LIMIT
*
JOBLIM   LE    FR0,FACTOR         FAKTOR
         CE    FR0,=E'1.0'        BEI FAKTOR <= 1.0
         BNH   CANCEL             KEINE VERLAENGERUNG
         L     R12,0(R11)         A(COMMON EXIT PARAMETER AREA)
         CLC   USTEPNR(1),28(R12) WURDE JOB CPU-ZEIT IM GLEICHEN STEP
*                                 BEREITS VERLAENGERT ?
         BE    CANCEL             JA
         CLI   JOBIND,X'01'       WURDE JOB CPU-ZEIT BEREITS VERL. ?
         BNE   INDJVERL           NEIN
*
         L     R9,JOBVERL         RESTL. VERLAENGERUNG
         LTR   R9,R9              > 0 ?
         BH    VERL               JA
         B     CANCEL             NEIN
*
INDJVERL OI    JOBIND,X'01'       IND. FUER VERLAENGERUNG
         L     R6,JOBTIME         IN VORHERIGEN STEPS VERBR. CPU-ZEIT
*        WTO   '---> IEFUTL <--- JOB CPU TIME VERLAENGERT',ROUTCDE=(11)
         B     ASCBCPU
*
***********************************************************************
*        STEP TIME LIMIT
*
STEPLIM  LE    FR0,FACTOR         FAKTOR
         CE    FR0,=E'1.0'        BEI FAKTOR <= 1.0
         BNH   CANCEL             KEINE VERLAENGERUNG
         TM    STEPIND,X'01'      WURDE STEP-CPU-ZEIT BEREITS VERLAENG.
         BO    CANCEL             JA
         OI    STEPIND,X'01'      IND. FUER VERLAENGERUNG
         SR    R6,R6
         WTO   '---> IEFUTL <--- STEP CPU ZEIT VERLAENGERT - JOB WIRD E*
               VTL. ZU FRUEH ABGEBROCHEN',ROUTCDE=(11)
         B     ASCBCPU
*
***********************************************************************
*        WAIT TIME LIMIT
*
WAITLIM  EQU   *
         LA    R2,IDCNT           ANHAND VON
         LA    R7,IDTAB           IDTAB PRUEFEN,
IDLOOP   CLC   0(8,R5),0(R7)      OB EINE USERID
         BNE   NEXTID             VORLIEGT, DIE NICHT ZU CANCELN IST
         B     ONLJ               JA, WAITTIME VERLAENGERN
NEXTID   LA    R7,8(R7)
         BCT   R2,IDLOOP
*
         TM    29(R5),X'01'       PRUEFEN, OB FOREGROUND JOB
         BO    CANCEL             FALLS JA, CANCELN
         LA    R2,STCCNT          PRUEFEN, OB STARTED TASK AUS
         LA    R1,STCTAB          TABELLE VORLIEGT
STCLOOP  LM    R6,R7,0(R1)
         EX    R6,EXCLC
         BNE   NEXTSTC
         B     ONLJ               JA, WAIT TIME VERLAENGERN
NEXTSTC  LA    R1,8(R1)
         BCT   R2,STCLOOP
*                                 SONST PRUEFEN AUF ONLINE-JOB
         CLI   ACCPAR+4,X'00'     BEREICHS-PARAMETER VORHANDEN ?
         BE    BKEY               NEIN
         MVC   KEY(2),ACCPAR+14   SCHLUESSEL-PARAMETER
         B     LACNT
BKEY     MVC   KEY(2),ACCPAR+11   SCHLUESSEL-PARAMETER
LACNT    LA    R6,ONLCNT          ANZAHL ONLINE SCHLUESSEL
         LA    R5,ONLKEYS-2       A(1. ONLINE SCHLUESSEL)-2
TONLJ    LA    R5,2(R5)           A(ONLINE SCHLUESSEL)
         CLC   0(2,R5),KEY        ONLINE JOB ?
         BE    ONLJ               JA
         BCT   R6,TONLJ           NEIN
         B     CANCEL             KEIN ONLINE JOB
ONLJ     L     R9,WAITADD         WAIT TIME EXTENSION
         LA    R8,8               RETURN CODE FUER VERLAENGERUNG
         B     RETURN
*
***********************************************************************
*
ASCBCPU  L     R2,ASCB            A(USER'S ASCB)
         A     R6,=F'50'          RUNDEN FUER D
         SRDA  R6,32              IN R7 SCHIEBEN
         D     R6,=F'100'         R7: CPUZEIT V. VORIGEN STEPS
         A     R7,80(R2)          TIME LIMIT DES LFDN. STEPS ADDIEREN
         MVC   DFLWA(LFLWA),FLWA  WORKAREA FUER GLEITKOMMA-ARITHMETIK
*
         LE    FR0,FACTOR         FAKTOR          1
         SE    FR0,=E'1.0'        FAKTOR - 1.0    2
         LR    R0,R7              ZU MULT. WERT   3
         LPR   R1,R0                              4
         ST    R1,DFLWA+20                        5
         LD    FR2,DFLWA+16                       6
         AD    FR2,DFLWA          GLEITKOMMA-     7
         LTR   R0,R0              ARITHMETIK      8
         BALR  R14,0              NACH            9
         BC    11,6(R14)          LIST-AUSGABE    10
         LCDR  FR2,FR2            DES             11
         MER   FR2,FR0            G1-COMPILERS    12
         AE    FR2,=E'0.9'                        13
         SDR   FR0,FR0            K=(F-1.0)*I+0.9 14
         LER   FR0,FR2                            15
         AW    FR0,DFLWA+32                       16
         STD   FR0,DFLWA+24                       17
         L     R0,DFLWA+28                        18
         LTDR  FR0,FR0                            19
         BALR  R14,0                              20
         BC    11,6(R14)                          21
         LCR   R0,R0                              22
         LR    R9,R0              ERGEBNIS IN R9  23
         ST    R9,JOBVERL         GESAMT-VERL.
         OI    JOBIND,X'01'       IND. FUER VERLAENGERUNG
*
VERL     LA    R8,8               RETURN CODE FUER EXTENSION IN SEC
         L     R12,0(R11)         A(COMMON EXIT PARAMETER AREA)
         MVC   USTEPNR(1),28(R12) STEP-NR IN DEM CPU VERLAENGERT WURDE
*
***********************************************************************
*
RETURN   LR    R12,R13            A(DUMMY)
         L     R13,4(R13)         A(ALTE SAVE AREA)
         FREEMAIN R,LV=LDUMMY,A=(R12),SP=255
         ST    R9,24(R13)         EXTENSION (SEC) IN ALTE SAVE AREA R1
STORERC  ST    R8,16(R13)         RETURN CODE IN ALTE SAVE AREA R15
         RETURN (14,12)
*
EXCLC    CLC   0(0,R5),0(R7)
*
***********************************************************************
*
CANCEL   SR    R8,R8              RETURN CODE FUER CANCEL
         LR    R9,R11             URSPR. R1
         B     RETURN
*
NOCORE   WTO   '----> KEIN PLATZ FUER SAVE AREA IN LSQA (IEFUTL)',     *
               ROUTCDE=(10,11)
         SR    R8,R8              RETURN CODE FUER CANCEL
         B     STORERC
*
***********************************************************************
         EJECT
***********************************************************************
*        FELDER DIE NICHT VERAENDERT WERDEN
*
*        SCHLUESSEL FUER ONLINE ARBEITEN
ONLKEYS  DC    C'10'              1
         DC    C'12'              2
         DC    C'15'              3
         DC    C'20'              4
         DC    C'25'              5
         DC    C'52'              6
         DC    C'55'              7
ONLCNT   EQU   (*-ONLKEYS)/2      8
*
*        STARTED TASKS
*        LAENGE-1 FUER EXCLC, A(TEXT)
*
IDTAB    DS    0F
         DC    CL8'E9T06   '
         DC    CL8'E9T01   '
         DC    CL8'E9T0B   '
         DC    CL8'E9T85   '
         DC    CL8'        '
IDCNT    EQU   (*-IDTAB)/8
*
STCTAB   DS    0F
STC1     DC    F'2',A(STCA1)
STC2     DC    F'2',A(STCA2)
STC3     DC    F'3',A(STCA3)
STC4     DC    F'3',A(STCA4)
STC5     DC    F'3',A(STCA5)
STC6     DC    F'2',A(STCA6)
STC7     DC    F'3',A(STCA7)
STC8     DC    F'3',A(STCA8)
STC9     DC    F'4',A(STCA9)
STC10    DC    F'1',A(STCA10)
STC11    DC    F'5',A(STCA11)
STC12    DC    F'5',A(STCA12)
STCCNT   EQU   (*-STCTAB)/8
*
STCA1    DC    C'JES'
STCA2    DC    C'RMF'
STCA3    DC    C'TCAM'
STCA4    DC    C'VTAM'
STCA5    DC    C'VSPC'
STCA6    DC    C'IMS'
STCA7    DC    C'XCNT'
STCA8    DC    C'SDSI'
STCA9    DC    C'OMEGA'
STCA10   DC    C'TS'
STCA11   DC    C'VSTSIO'
STCA12   DC    C'VPRINT'
*
*
WAITADD  DC    F'86400'           WAIT TIME EXTENSION
*
***********************************************************************
*
FLWA     DS    0F                 WORKAREA
F1       DC    4F'0'              FUER
F2       DC    X'4E000000'        GLEITKOMMA-
F3       DC    3F'0'              ARITHMETIK
F4       DC    X'4E000000'        NACH LIST-AUSGABE
F5       DC    5F'0'              DES FORTRAN G1 COMPILERS
LFLWA    EQU   *-FLWA
*
         LTORG
***********************************************************************
*        FELDER DIE VERAENDERT WERDEN
*
DUMMY    DSECT
SAVE     DS    18F                NEUE SAVE AREA
DFLWA    DS    14F
KEY      DS    1H
LDUMMY   EQU   *-DUMMY
**********************************************************************
         COPY  USERAREA
*
         END   IEFUTL
