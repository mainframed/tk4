LFDN     TITLE 'TABELLEN-INDEX FINDEN  NACH HALBIERUNGS-METHODE'
*        ENTER LINKAGE
*        CALL  'LFDNRDEF' USING TABELLE, PARAMETER
*              (NUR EINMAL JE TABELLE AUFRUFEN)
*        CALL  'LFDNR' USING PARAMETER, SUCHARGUMENT, INDEX
*        ENTER COBOL
*        01  TABANFANG.
*            02  ZEILE  OCCURS N  TIMES DEPENDING ON ZEILEN-ANZAHL.
*        01  PARAMETER USAGE COMPUTATIONAL.
*            02 ZEILEN-ANZAHL,   PICTURE 9(4)  VALUE
*            02  ZEILEN-LAENGE,   PICTURE 9(4)  VALUE
*            02  SUCHBEGRIFF-VON, PICTURE 9(4)  VALUE
*            02  SUCHBEGRIFF-BIS, PICTURE 9(4)  VALUE
*            02  FILLER OCCURS 9, PICTURE 9(4).
*        77  SUCHARGUMENT
*        77  INDEX, COMPUTATIONAL PICTURE 9999.
*        88  NICHT-GEFUNDEN  VALUE ZERO.
LFDNRDEF START
         ENTRY LFDNRD
LFDNRD   EQU   LFDNRDEF
         USING *,15
         USING PARAM,6
         STM   3,6,32(13)
         L     6,4(0,1)
         LA    5,0           REG.5 = 0
         LA    3,1           REG.3 = 1
E1       CH    3,TABZAE      IF REG.3 LESS THAN TABZAE
         BH    E11              GO TO E11
         SLA   3,1           REG.3 = 2 * REG.3
         LA    5,1(0,5)      REG.5 = 1 + REG.5
         B     E1            GO TO E1
E11      STH   5,SUZAE       SUZAE = REG.5
         LR    5,3           REG.5 = REG.3
         SRA   3,1           REG.3 = REG.3 / 2
         STH   3,INDEX
         MH    3,TABLAE      REG.3 = TABLAE * REG.3
         LR    4,3           REG.4 = REG.3
         SRA   4,1           REG.4 = REG.4 / 2        = 1. SPRUNGWEITE
         SH    3,TABLAE      REG.3 = REG.3 - TABLAE
         A     3,0(0,1)      REG.3 = TAB-ANFANG + REG.3
         SH    5,TABZAE      REG.5 = REG.5 - TABZAE
         BCTR  5,0           REG.5 = REG.5 - 1
         STH   5,AUSDAT+8
         MH    5,TABLAE      REG.5 = TABLAE * REG.5   = VERSCHIEB-DIFF.
         STH   5,AUSDAT+10
         AH    3,VON
         BCTR  3,0           REG.3 = VON - 1 + REG.3  = 1. SUCHADRESSE
         STM   3,4,AUSDAT
         LH    3,NACH
         SH    3,VON
         STH   3,FLDLNGE     FLDLNGE = NACH - VON     = FELDLAENGE
         LM    3,6,32(13)
         BR    14
*
PARAM    DSECT
TABZAE   DS    H
TABLAE   DS    H
VON      DS    H
NACH     DS    H
SUZAE    DS    H
INDEX    DS    H
AUSDAT   DS    3F
FLDLNGE  DS    H
LFDNR    CSECT
         USING *,15
         STM   2,10,28(13)
         L     6,0(0,1)      REG.6 = BASE-ADR OF PARAMETERS
         SR    3,3
         CH    3,TABZAE
         BE    V10-2
         L     2,4(0,1)      REG.2 = LOC. OF  SUCHARGUMENT
         LM    3,4,AUSDAT
         LH    5,SUZAE
         LH    10,FLDLNGE
         LA    7,1           REG.7 = SCHALTER
         LH    8,INDEX
         LR    9,8
V1       SRA   9,1
         EX    10,COMP       IF ARGUMENT LESS THAN TABELLE
         BE    V10           IF EQUAL
         BH    V11           IF GREATER
         SR    3,4           REG.3 = REG.3 - REG.4
         SR    8,9
         BCTR  7,0
         B     V13
V11      BCT   7,V12
         SH    8,AUSDAT+8
         SH    3,AUSDAT+10
V12      AR    3,4           REG.3 = REG.3 - REG.4
         AR    8,9
V13      SRA   4,1
         BCT   5,V1
         SR    8,8
V10      L     9,8(0,1)
         STH   8,0(0,9)
         LM    2,10,28(13)
         BR    14
COMP     CLC   0(0,2),0(3)
         END
//LKED.SYSLMOD DD DSN=LU.SBRLIB,DISP=SHR
    ALIAS    LFDNR,LFDNRD,LFDNRDEF
     NAME    LULFDNR(R)
