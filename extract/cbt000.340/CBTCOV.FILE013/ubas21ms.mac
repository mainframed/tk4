* STAND 06/22/79
***************************************************
*
*        PROGRAM  ACCTPARM
*
*        AUFRUF   CALL ACCTPARM,(N,LN,ARG)
*
*        ZWECK LIEFERT UNTER MVS N-TEN PARAMETER DER
*              JOB ACCOUNTING INFORMATION
*
*        H     N    N-TER PARAMETER
*        H     LN   LAENGE DES AUSGABEBEREICHS
*        CL(LN)  ARG  AUSGABEBEREICH FUER PARAMETER
*
*        REGISTER 15 BEI RUECKSPRUNG 0 = OK
*                                    4 = FEHLER = PARM FEHLT
*                                        NICHT MVS, PARM ZU LANG
*
*****************************************************************
ACCTPARM CSECT
         AREG
         XSAVE R12,SAVE,ACCTPARM
*
*  ADRESSEN DER ARGUMENTE IN REGISTERN MERKEN
*
         L     R8,0(R1)     A(N)
         L     R9,4(R1)     A(LN)
         L     R10,8(R1)    A(ARG)
*
*  ACCOUNTING INFORMATION SUCHEN
*
         L     R3,16          A(CVT)
         TM    116(R3),X'01'
         BNO   FEHLER         NICHT MVS
         L     R3,0(,R3)      A(TCB WORDS)
         L     R3,0(,R3)      A(CURRENT TCB)
         L     R3,180(,R3)    A(JSCB)
         L     R3,260(,R3)    A(JCT)
         ICM   R3,7,56(R3)    A(AVT)
*
AANF     EQU   47             DISPLACEMENT ACCT.INF
*
         CLC   AANF(1,R3),1(R8)
         BL    FEHLER         EINTRAG FEHLT
*
*  SUCHE DES N-TEN EINTRAGS
*
         SR    R5,R5
         LH    R4,0(R8)       N
         BCTR  R4,0           N - 1
         LA    R3,AANF+1(,R3) A(1.EINTRAG)
         LTR   R4,R4
         BZ    LOOPE
*
LOOP     EQU   *
         IC    R5,0(R3)       LAENGE DES EINTRAGS
         LA    R3,1(R5,R3)    A(NAECHSTER EINTRAG)
         BCT   R4,LOOP
*
*  N-TER EINTRAG GEFUNDEN
*
LOOPE    EQU   *
         IC    R4,0(R3)       LAENGE
         LTR   R4,R4
         BZ    FEHLER         LAENGE=0 -->EINTRAG FEHLT
         SR    R15,R15        RETURNCODE = 0
         CH    R4,0(,R9)      VERGLEICH MIT LN
         BNH   OK             LAENGE <= LN
         LH    R4,0(,R9)      LN
         LA    R15,4
OK       EQU   *
         STH   R4,0(,R9)      LN AUSGABE
         BCTR  R4,0           MVC LAENGE
         EX    R4,ARGMVC      ARG
*
*  ENDE
*
ENDE     EQU   *
         XRETURN (R15)
*
*  FEHLER
*
FEHLER   EQU   *
         LA    R15,4
         B     ENDE
*
*
ARGMVC   MVC   0(0,R10),1(R3)
         END
