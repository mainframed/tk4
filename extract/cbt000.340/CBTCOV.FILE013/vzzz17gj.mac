PROC 0
GLOBAL &HEXVALUE
GLOBAL &VALUE
/**********************************************************************/
/*     H     H   EEEEEEE    AAAAA    DDDDDD    EEEEEEE   RRRRRR       */
/*     H     H   E         A     A   D     D   E         R     R      */
/*     HHHHHHH   EEEEEEE   AAAAAAA   D     D   EEEEEEE   RRRRRR       */
/*     H     H   E         A     A   D     D   E         R   R        */
/*     H     H   EEEEEEE   A     A   DDDDDD    EEEEEEE   R     R      */
/**********************************************************************/
/*  CLIST NAME: VTAM25            REL/DATE: 2.1/08.15.79              */
/*                                                                    */
/*  DOC:  THIS CLIST FINDS THE START AND END ADDRESSES OF ALL VTAM    */
/*          BUFFERS AND CREATES EQUATES OF THE FORM IOSTART:IOEND     */
/*  SYMBOLS EQUATED: ATCVT=ATCVT, BPDTY=BUFFER DIRECTORY PTR,         */
/*                   LPE=LP BPCP.                                     */
/*  SYMBOLS FOUND:   ATCVT, BPDTY, LPE.                               */
/*  RETURN: THIS CLIST RETURNS CONTROL TO THE CALLING CLIST.          */
/*  DEPENDENCIES: 1) THIS CLIST REQUIRES THAT COMMAND                 */
/*                   CLEAR BE INSTALLED IN YOUR ENVIRONMENT.        */
/*                2) THIS CLIST REQUIRES THAT CLIST 'HEX' BE          */
/*                   INSTALLED IN YOUR ENVIRONMENT.                   */
/*  USE: THIS CLIST WILL FIND THE START AND END OF THE VTAM           */
/*        BUFFERS AND EQUATE THE ADDRESSES.                           */
/*SJM******************************************************************/
  SETD PRINT TERMINAL                         /* CLEAR THE SCREEN     */
  WRITENR DO YOU WISH THE PRINT OPTION? (Y)/N ===>
  READ &ANS
  IF &ANS=N THEN DO
  SETD NOPRINT TERMINAL
  END
SETD NOVERIFY
  CLEAR
WRITE  ++ THIS CLIST FINDS THE START AND END ADDRS OF ALL VTAM BUFFERS
WRITE  ++ AND CREATES EQUATES OF THE FORM   IOSTART : IOEND
WRITE
EQ ATCVT 408.%
EQ BPDTY ATCVT+80%               /* BUFFER DIRECTORY PTR */
 SET &LPX=&STR(ABCDEF)
 SET &LPY=&STR(123456)
 SET &OFFSET=152
 SET &BASE=152
 SET &DOT=&STR(.)
 SET &BUMP=0
SPIN: SET &OFFSET=&BASE+16*&BUMP
EQ LPE BPDTY+&OFFSET.N%          /* PTR TO LP BPCB       */
IF &BUMP=0 THEN GOTO JUMP
IF &BUMP=1 THEN SET &TYPE=&STR(IO)
IF &BUMP=2 THEN SET &TYPE=&STR(DATA)
IF &BUMP=3 THEN SET &TYPE=&STR(LP)
IF &BUMP=4 THEN SET &TYPE=&STR(FMCB)
IF &BUMP=5 THEN SET &TYPE=&STR(NP)
IF &BUMP=6 THEN SET &TYPE=&STR(LF)
IF &BUMP=7 THEN SET &TYPE=&STR(CRPL)
IF &BUMP=8 THEN SET &TYPE=&STR(UECB)
IF &BUMP=9 THEN SET &TYPE=&STR(SF)
IF &BUMP=10 THEN SET &TYPE=&STR(SP)
IF &BUMP=11 THEN SET &TYPE=&STR(ACE)
EVAL LPE+9 LEN(3)                /* GET START OF LPBUF AREA           */
SET &LPX=&LASTCC
%HEX &LPX
SET &LPX=&HEXVALUE
EVAL LPE+D LEN(3)                /* GET END OF LPBUF AREA             */
SET &LPY=&LASTCC
%HEX &LPY
SET &LPY=&HEXVALUE
 NOTE '(START : END) ADDRS OF  &TYPE    BUFFERS ARE   (&LPX: &LPY)'
 EQ &TYPE.START &LPX.&DOT
 EQ &TYPE.END &LPY.&DOT
JUMP: IF &BUMP<11 THEN DO
  SET &BUMP=&BUMP+1
  DROPSYM (LPE)
  GOTO SPIN
  END
  SETD VERIFY
FUNC5END: WRITE VTAM25 COMPLETED.
