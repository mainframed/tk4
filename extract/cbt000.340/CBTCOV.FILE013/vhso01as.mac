*DISPLAY ON CONSOLE
* INTERVAL 30 SEC
* CPU TIME  %
* PAGING RATE N/SEC
* SWAP      NUMBER OF SWAPPED ADDRESSPACES
* VIO       NUMBER OF VIO-DATA SETS IN USE
HILL     TITLE 'DISPLAY CPU-USAGE AND PAGING-RATE AT FIXED INTERVALS'
SYS021   START
         B     72(0,15)
         DC    17F'0'
         STM   14,12,12(13)
         ST    13,4(0,15)
         ST    15,8(0,13)
         LR    13,15
         USING SYS021,13
         L     2,X'4C'(0,0)             LOAD CVT ADDRESS
         SH    2,=H'4'                  SUBTRACT 4
         CLC   0(2,2),=C'01'            TEST IF SVS
         BNE   MVS                      NO - GO TO MVS CODING
         L     2,X'4C'(0,0)             LOAD CVT ADDRESS
         L     3,X'01B8'(0,2)           LOAD SYSWSAVE ADDRESS
         LA    3,8(0,3)                 LOAD WAIT-TIME ADDRESS
         ST    3,ADDRWAIT               STORE WAIT-TIME ADDRESS
         L     3,X'0164'(0,2)           LOAD PVT ADDRESS
         LA    3,X'0124'(0,3)           LOAD PVTNPOUT/PVTNPIN ADDRESS
         ST    3,ADDRPAGE               STORE PVTNPOUT/PVTNPIN ADDRESS
GETINFO  L     2,ADDRWAIT               LOAD WAIT-TIME ADDRESS
         L     3,ADDRPAGE               LOAD PVTNPOUT/PVTNPIN ADDRESS
         MVC   NEWWAIT(8),0(2)          STORE NEW WAIT-TIME
         MVC   NEWPAGE(8),0(3)          STORE NEW PVTNPOUT/PVTNPIN
         STCK  NEWTIME                  STORE NEW TIME - TOD-FORMAT
         CLC   NEWWAIT(8),OLDWAIT       SKIP CALCULATIONS AND ...
         BL    NEWTOOLD                   DISPLAY IF ...
         CLC   NEWPAGE(4),OLDPAGE           ACCUMULATORS HAVE ...
         BL    NEWTOOLD                       BEEN RESET BY ...
         CLC   NEWPAGE+4(4),OLDPAGE+4           EXPIRATION OF SMF ...
         BL    NEWTOOLD                           10-MINUTE INTERVAL
         LM    2,3,NEWTIME              LOAD NEW TIME
         SL    3,OLDTIME+4              SUBTRACT OLD TIME ...
         BC    3,NCELAPSE                 FROM NEW TIME ...
         BCTR  2,0                          TO GET ELAPSED TIME IN ...
NCELAPSE SL    2,OLDTIME                      R2 & R3
         STM   2,3,ELAPTIME             STORE ELAPSED TIME
         LM    2,3,NEWWAIT              LOAD NEW WAIT TIME
         SL    3,OLDWAIT+4              SUBTRACT OLD WAIT TIME ...
         BC    3,NCWAIT                   FROM NEW WAIT TIME ...
         BCTR  2,0                          TO GET NET WAIT TIME IN ...
NCWAIT   SL    2,OLDWAIT                      R2 & R3
         SRDL  2,12                     R2/R3 = WAIT-TIME IN MICROSEC.
         LM    4,5,ELAPTIME             LOAD ELAPSED TIME
         SRDL  4,12                     R4/R5 = ELAPSED-TIME IN MICROS.
         D     4,=F'100'                R5 = ELAPSED-TIME/100
         DR    2,5                      R3 = WAIT-TIME X 100 / ELAPSED
         LA    2,100
         SR    2,3                      R2 = % CPU-TIME
         CVD   2,WORKP
         UNPK  WORKC+5(3),WORKP+6(2)
         OI    WORKC+7,X'F0'
         MVC   WTO+21(3),WORKC+5        STORE % CPU-TIME IN WTO
         L     3,NEWPAGE
         A     3,NEWPAGE+4
         S     3,OLDPAGE
         S     3,OLDPAGE+4              R3 = NET PAGE-INS AND PAGE-OUTS
         M     2,=F'1000000'            R2/R3 = NET PAGES X 1000000
         LM    4,5,ELAPTIME
         SRDL  4,12                     R5 = ELAPSED-TIME IN MICROSEC.
         DR    2,5                      R3 = PAGES/SECOND
         CVD   3,WORKP
         UNPK  WORKC+5(3),WORKP+6(2)
         OI    WORKC+7,X'F0'
         MVC   WTO+35(3),WORKC+5        STORE PAGING RATE IN WTO
WTO      WTO   'SYS021 - CPU=   % - PAGING=   /SEC',ROUTCDE=2,DESC=4
NEWTOOLD MVC   OLDPAGE(8),NEWPAGE       SAVE NEW INFORMATION FOR ...
         MVC   OLDTIME(8),NEWTIME         NEXT CALCULATION ...
         MVC   OLDWAIT(8),NEWWAIT           AND DISPLAY
         STIMER WAIT,BINTVL=BINTVL      WAIT FOR NEXT CALCULATION
         B     GETINFO                  GO GET INFO FOR NEXT CALC.
ADDRPAGE DS    F                        ADDRESS OF PVTNPOUT/PVTNPIN
ADDRWAIT DS    F                        ADDRESS ACCUMULATED WAIT-TIME
BINTVL   DC    F'3000'                  ** DISPLAY EVERY 30 SECONDS **
ELAPTIME DS    D                        DISPLAY INTERVAL ELAPSED TIME
NEWPAGE  DS    D                        NEW PVTNPOUT/PVTNPIN
NEWTIME  DS    D                        NEW TIME - TOD-FORMAT
NEWWAIT  DS    D                        NEW ACCUMULATED WAIT TIME
OLDPAGE  DC    0D'0',8X'FF'             OLD PVTNPOUT/PVTNPIN
OLDTIME  DS    D                        OLD TIME - TOD-FORMAT
OLDWAIT  DC    0D'0',8X'FF'             OLD ACCUMULATED WAIT TIME
WORKC    DS    D                        WORK-AREA - CHARACTERS
WORKP    DS    D                        WORK-AREA - PACKED
         EJECT
MVS      L     2,X'0210'(0,0)           LOAD LCCA ADDRESS FROM PSA
         L     3,X'4C'(0,0)             LOAD CVT ADDRESS
         L     3,X'0164'(0,3)           LOAD PVT ADDRESS
         MVC   MNEWWAIT(8),X'0268'(2)   STORE NEW WAIT-TIME FROM LCCA
         MVC   MNEWPAGE(8),X'E8'(3)     STORE NEW COUNTS FOR PAGING
         MVC   MNEWVIOP(8),X'F0'(3)     STORE NEW COUNTS FOR VIO
         MVC   MNEWSWAP(8),X'FC'(3)     STORE NEW COUNTS FOR SWAPPING
         STCK  MNEWTIME                 STORE NEW TIME - TOD-FORMAT
         CLC   MNEWWAIT(8),MOLDWAIT     *
         BL    SAVEINFO                 *
         CLC   MNEWPAGE(4),MOLDPAGE     *
         BL    SAVEINFO                 *
         CLC   MNEWPAGE+4(4),MOLDPAGE+4 *
         BL    SAVEINFO                 *    SKIP CALCULATIONS AND
         CLC   MNEWVIOP(4),MOLDVIOP     *    DISPLAY IF COUNTERS HAVE
         BL    SAVEINFO                 *    BEEN RESET BY SMF OR MF/1
         CLC   MNEWVIOP+4(4),MOLDVIOP+4 *
         BL    SAVEINFO                 *
         CLC   MNEWSWAP(4),MOLDSWAP     *
         BL    SAVEINFO                 *
         CLC   MNEWSWAP+4(4),MOLDSWAP+4 *
         BL    SAVEINFO                 *
         LM    2,3,MNEWTIME             LOAD NEW TIME
         SL    3,MOLDTIME+4             SUBTRACT OLD TIME ...
         BC    3,MNCELAP                  FROM NEW TIME ...
         BCTR  2,0                          TO GET ELAPSED TIME IN ...
MNCELAP  SL    2,MOLDTIME                     R2 & R3
         STM   2,3,MELPTIME             STORE ELAPSED TIME
         LM    2,3,MNEWWAIT             LOAD NEW WAIT TIME
         SL    3,MOLDWAIT+4             SUBTRACT OLD WAIT-TIME ...
         BC    3,MNCWAIT                  FROM NEW WAIT-TIME ...
         BCTR  2,0                          TO GET NET WAIT-TIME IN ...
MNCWAIT  SL    2,MOLDWAIT                     R2 & R3
         SRDL  2,12                     R2/R3 = WAIT-TIME IN MICROSEC.
         LM    4,5,MELPTIME             LOAD ELAPSED TIME
         SRDL  4,12                     R4/R5 = ELAPSED-TIME IN MICROS.
         D     4,=F'100'                R5 = ELAPSED-TIME / 100
         DR    2,5                      R3 = WAIT-TIME X 100 / ELAPSED
         LA    2,100
         SR    2,3                      R2 = % CPU-TIME
         CVD   2,MWORKP
         UNPK  MWORKC+5(3),MWORKP+6(2)
         OI    MWORKC+7,X'F0'
         MVC   MWTO+12(3),MWORKC+5      STORE % CPU-TIME IN WTO
         L     3,MNEWPAGE
         A     3,MNEWPAGE+4
         S     3,MOLDPAGE
         S     3,MOLDPAGE+4             R3 = NET PAGE-INS/OUTS - PAGING
         M     2,=F'1000'               R2/R3 = NET PAGES X 1000
         LM    4,5,MELPTIME
         SRDL  4,12                     R4/R5 = ELAPSED-TIME IN MICROS.
         D     4,=F'1000'               R5 = ELAPSED-TIME IN MILLISEC.
         DR    2,5                      R3 = PAGES/SECOND
         CVD   3,MWORKP
         ZAP   MPAGINGP(8),MWORKP(8)    INITIALIZE TOTAL PAGING COUNT
         UNPK  MWORKC+5(3),MWORKP+6(2)
         OI    MWORKC+7,X'F0'
         MVC   MWTO+58(3),MWORKC+5      STORE PAGING-RATE IN WTO
         L     3,MNEWVIOP
         A     3,MNEWVIOP+4
         S     3,MOLDVIOP
         S     3,MOLDVIOP+4             R3 = NET PAGE-INS/OUTS - VIO
         M     2,=F'1000'               R2/R3 = NET PAGES X 1000
         LM    4,5,MELPTIME
         SRDL  4,12                     R4/R5 = ELAPSED-TIME IN MICROS.
         D     4,=F'1000'               R5 = ELAPSED-TIME IN MILLISEC.
         DR    2,5                      R3 = PAGES/SECOND
         CVD   3,MWORKP
         AP    MPAGINGP(8),MWORKP(8)    INCREMENT TOTAL PAGING COUNT
         UNPK  MWORKC+5(3),MWORKP+6(2)
         OI    MWORKC+7,X'F0'
         MVC   MWTO+48(3),MWORKC+5      STORE PAGING-RATE IN WTO
         L     3,MNEWSWAP
         A     3,MNEWSWAP+4
         S     3,MOLDSWAP
         S     3,MOLDSWAP+4             R3 = NET PAGE-INS/OUTS - SWAP
         M     2,=F'1000'               R2/R3 = NET PAGES X 1000
         LM    4,5,MELPTIME
         SRDL  4,12                     R4/R5 = ELAPSED-TIME IN MICROS.
         D     4,=F'1000'               R5 = ELAPSED-TIME IN MILLISEC.
         DR    2,5                      R3 = PAGES/SECOND
         CVD   3,MWORKP
         AP    MPAGINGP(8),MWORKP(8)    INCREMENT TOTAL PAGING COUNT
         UNPK  MWORKC+5(3),MWORKP+6(2)
         OI    MWORKC+7,X'F0'
         MVC   MWTO+40(3),MWORKC+5      STORE PAGING-RATE IN WTO
         UNPK  MWORKC+5(3),MPAGINGP+6(2)
         OI    MWORKC+7,X'F0'
         MVC   MWTO+26(3),MWORKC+5      STORE TOTAL PAGING-RATE IN WTO
MWTO     WTO   'CPU=   %   PAGING=   /SEC (SWAP=    VIO=    OTHER=   )'*
               ,ROUTCDE=2,DESC=4
SAVEINFO MVC   MOLDTIME(8),MNEWTIME     SAVE NEW INFORMATION ...
         MVC   MOLDWAIT(8),MNEWWAIT       FOR NEXT ...
         MVC   MOLDPAGE(8),MNEWPAGE         CALCULATION ...
         MVC   MOLDVIOP(8),MNEWVIOP           AND ...
         MVC   MOLDSWAP(8),MNEWSWAP             DISPLAY
         STIMER WAIT,BINTVL=INTERVAL    WAIT FOR NEXT CALCULATION
         B     MVS                      GO BACK TO START AGAIN
INTERVAL DC    F'3000'                  ** DISPLAY EVERY 30 SECONDS **
MELPTIME DS    D                        DISPLAY INTERVAL ELAPSED TIME
MNEWPAGE DS    D                        NEW PVTNPIN/PVTNPOUT
MNEWSWAP DS    D                        NEW PVTSPIN/PVTSPOUT
MNEWTIME DS    D                        NEW TIME - TOD-FORMAT
MNEWVIOP DS    D                        NEW PVTVAMI/PVTVAMO
MNEWWAIT DS    D                        NEW LCCAWTIM
MOLDPAGE DC    0D'0',8X'FF'             OLD PVTNPIN/PVTNPOUT
MOLDSWAP DC    0D'0',8X'FF'             OLD PVTSPIN/PVTSPOUT
MOLDTIME DC    0D'0',8X'FF'             OLD TIME - TOD-FORMAT
MOLDVIOP DC    0D'0',8X'FF'             OLD PVTVAMI/PVTVAMO
MOLDWAIT DC    0D'0',8X'FF'             OLD LCCAWTIM
MPAGINGP DS    D                        PACKED TOTAL PAGING IN INTERVAL
MWORKC   DS    D                        WORK-AREA - CHARACTERS
MWORKP   DS    D                        WORK-AREA - PACKED
         END
