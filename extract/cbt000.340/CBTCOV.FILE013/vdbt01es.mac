IEFU83   CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3        BASIS REG.
R4       EQU   4        BASIS REG. FOR 'USERAREA'
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10       A(SMF RECORD)
R11      EQU   11
R12      EQU   12
R13      EQU   13       A(SAVEAREA)
R14      EQU   14
R15      EQU   15
*
T40      EQU   X'28'    RECORD TYPE 40
DISK     EQU   X'20'    DISK DEVICE CLASS
TAPE     EQU   X'80'    TAPE DEVICE CLASS
RC0      EQU   0        RETURN CODE 0
RC4      EQU   4        RETURN CODE 4
*
********************************************************************
*
         SAVE  (14,12),,*         SAVE REGS IN CALLER'S SAVEAREA
         LR    R3,R15             LOAD BASE REG.
         USING IEFU83,R3          USE BASE REG.
         L     R10,0(R1)          A(SMF RECORD)
*
********************************************************************
*        CHECK RECORD TYPE 40
*
         CLI   5(R10),T40
         BE    TYP40
         LA    R6,RC0
         B     STORERC
*
********************************************************************
*        TEST RECORD LENGTH
*
TYP40    LH    R5,0(R10)     LOAD RDW IN R5
         CH    R5,=H'66'     LENGTHDEVICEENTRY FIELD AVAILABLE ?
         BL    WTO1          NO
         LH    R6,64(R10)    LOAD LENGTHDEVICEENTRY IN R6  (OFFSET 60)
         AH    R6,=H'64'     LDE + 64 SHOULD BE LENGTH IN RDW
         CR    R5,R6         COMPARE LENGTH IN RDW AND 64 + LDE
         BL    WTO1
*
********************************************************************
*        CHECK TCBTCT IF 0 NO SMF-TASK RETURN0
*
         L     R4,16             A(CVT)
         L     R4,0(,R4)         A(TCBWORDS)
         L     R4,4(,R4)         A(TCB)
         L     R4,164(,R4)       A(TCT)
         LTR   R4,R4             CHECK TCT IF 0
         BZ    RETURN0
*
*********************************************************************
*        CHECK A(USERAREA) IF 0 GETMAIN FAILED IN IEFUJI
*
         L     R4,28(,R4)        A(TCTJMR)
         L     R4,32(,R4)        A(USERAREA)
         LTR   R4,R4             TEST IF 0
         BZ    RETURN0           GETMAIN FAILED IN IEFUJI
         USING USERAREA,R4
*
*********************************************************************
*        DISK- AND TAPE-EXCP'S IN USERAREA ADDIEREN UND DIE
*        ANZAHL DER VERSCHIEDENEN BANDEINHEITEN BESTIMMEN
*
         LH    R6,64(R10)        ANZAHL DER
         BCTR  R6,0              DEVICE SECTIONS
         BCTR  R6,0              NACH REGISTER 7
         SRDA  R6,35             BRINGEN
         LA    R2,66(R10)        A(1. DEVICE CLASS)
*
CDISK    CLI   0(R2),DISK
         BE    ISDISK
         CLC   2(2,R2),VIO
         BNE   CTAPE
ISDISK   L     R0,STEP40D        ADD
         A     R0,4(R2)          DISK-EXCP'S
         ST    R0,STEP40D        PER STEP
         B     TOTAL
*
CTAPE    CLI   0(R2),TAPE
         BNE   TOTAL
         LA    R5,TAPETAB
         LA    R6,TABLEN
COMPARE  CLC   0(2,R5),=X'0000'
         BE    NEWUNIT
         CLC   0(2,R5),2(R2)
         BE    ADDEXCP
         LA    R5,2(R5)
         BCT   R6,COMPARE
NEWUNIT  MVC   0(2,R5),2(R2)
ADDEXCP  L     R0,STEP40T        ADD
         A     R0,4(R2)          TAPE-EXCP'S
         ST    R0,STEP40T        PER STEP
*
TOTAL    EQU   *
*------- L     R0,TOTEXCPS       ADD
*------- A     R0,4(R2)          TOTAL EXCP'S
*------- ST    R0,TOTEXCPS       PER TYPE 40 RECORD
         LA    R2,8(R2)          A(NEXT DEVICE CLASS)
         BCT   R7,CDISK          LOOP
*
***********************************************************************
*
*  FALLS DIE SUMMATION IN TOTEXCPS EINGESETZT WERDEN SOLL,MUSS DIESES
*  FELD IN DIE USERAREA GESTELLT WERDEN, WEIL IEFU84 KEIN GETMAIN
*  MACHEN KANN
*
*------- L     R7,TOTEXCPS       IF TOTAL EXCP'S
*------- LTR   R7,R7             EQUAL ZERO
*------- BZ    RETURN4           DO NOT BUFFER SMF RECORD
*
         B     RETURN0
*
*********************************************************************
*
RETURN4  LA    R6,RC4             DO NOT BUFFER SMF RECORD
         B     STORERC
RETURN0  LA    R6,RC0             BUFFER SMF RECORD
STORERC  ST    R6,16(R13)         RETURN CODE TO CALLER'S SAVEAREA
         RETURN (14,12)
*
         EJECT
*********************************************************************
*
WTO1     EQU   *
*WTO1    WTO   '----> IEFU83 <---- VERSTUEMMELTER SMF-SATZ TYPE 40'
         B     RETURN4
*
*********************************************************************
*
VIO      DC    X'0FFF'  VIO; X'00000FFF' IM SMF-SATZ
*
*********************************************************************
*
*  MUSSTE HERAUSGENOMMEN WERDEN, DA IEFU84 KEINE SVC'S ABSETZEN DARF.
*
*  GMAIN    DSECT
*  SAVEAREA DS    18F
*  TOTEXCPS DS    F          TOTAL EXCP'S - IF 0 DO NOT BUFFER SMF REC.
*
*  GLEN     EQU   *-GMAIN
*
         EJECT
**********************************************************************
*
         COPY  USERAREA
         END
