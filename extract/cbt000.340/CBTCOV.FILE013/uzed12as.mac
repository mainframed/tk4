         TITLE 'DATUM UMFORMEN VON JJJJMMTT AUF YYDDD'
*              ********************************************************
*              *                                                      *
*              *   AUS EINEM DATUM IN DER FORM 'JJJJMMTT' (CHAR. 8)   *
*              *   WIRD EIN DATUM IN DER FORM 'YYDDD' (FIXED 5)       *
*              *   ERSTELLT. DER BENUTZER MUSS 2 PARAMETERS <BERGEBEN:*
*              *   - CALL UZED12A,(CHARDATUM,OSDATUM) -               *
*              *   DAS FELD 'OSDATUM IST MIT FIXED 5 ZU DEFINIEREN,   *
*              *   IM FELD 'CHARDATUM' MUSS EIN G<LTIGES DATUM        *
*              *   STEHEN.                                            *
*              *                                                      *
*              ********************************************************
         EJECT
UZED12A  CSECT
         REGEQ
         SAVE  (14,12),,*
         BALR  R2,0
         USING *,R2
         MVC   8(4,R13),=A(SAVE)
         ST    R13,SAVE+4
         BAL   R13,BEGIN
SAVE     DC    18F'0'
         LTORG
*
BEGIN    EQU   *
         LM    R5,R6,0(R1)         PARAMETERS <BERNEHMEN (PTR TO ADR)
         USING INPUT,R5
         USING OUTPUT,R6
         SPACE 4
         PACK  RESULT,IDAT+2(2)    LETZTE 2 STELLEN VOM JAHR
         MP    RESULT,=P'250'      X 25 IST GLEICH WIE :4
         CLI   RESULT+3,X'00'      OB SCHALTJAHR
         BNE   NOSCHALT
         LA    R4,TABSCH
         B     GOON
NOSCHALT LA    R4,TABMON
GOON     ZAP   RESULT,=P'0'
         PACK  ZWFELD,IDAT+4(2)    MONAT PACKEN
DECREM   SP    ZWFELD,=P'1'        VOM MONAT 1 SUBTRAHIEREN
         BZ    ADD
         AP    RESULT,0(2,R4)      ANZ.TAGE PRO GANZEN MONAT ADDIEREN
         LA    R4,2(,R4)
         B     DECREM
         SPACE 3
ADD      EQU   *                   ANZ. TAGE PRO GANZE MONATE SIND NUN
*                                  ADDIERT, NUN FEHLEN NOCH DIE TAGE
*                                  VOM ANGEBROCHENEN MONAT.
         PACK  ZWFELD,IDAT+6(2)    TAGE PACKEN
         AP    RESULT,ZWFELD
*                                  'RESULT' IST NUN 000000DDDV GEPACKT
         PACK  ZWFELD,IDAT+2(2)    LETZTE 2 STELLEN VOM JAHR PACKEN
         MVO   ZWFELD1,ZWFELD
         MVC   RESULT+2(1),ZWFELD1+1
         OI    RESULT+4,X'0F'      VORZ. RICHTIG SETZEN
         MVC   ODAT,RESULT+2
         L     R13,SAVE+4
         RETURN (14,12)
         SPACE 4
RESULT   DS    CL5
ZWFELD   DS    CL2
ZWFELD1  DS    CL3
TABMON   DC    PL2'31'             JAN
         DC    PL2'28'             FEB.
         DC    PL2'31'             MRZ.
         DC    PL2'30'             APR.
         DC    PL2'31'             MAI.
         DC    PL2'30'             JUN.
         DC    PL2'31'             JUL.
         DC    PL2'31'             AUG.
         DC    PL2'30'             SEP.
         DC    PL2'31'             OKT.
         DC    PL2'30'             NOV.
         DC    PL2'31'             DEZ.
TABSCH   DC    PL2'31'             JAN
         DC    PL2'29'             FEB.
         DC    PL2'31'             MRZ.
         DC    PL2'30'             APR.
         DC    PL2'31'             MAI.
         DC    PL2'30'             JUN.
         DC    PL2'31'             JUL.
         DC    PL2'31'             AUG.
         DC    PL2'30'             SEP.
         DC    PL2'31'             OKT.
         DC    PL2'30'             NOV.
         DC    PL2'31'             DEZ.
         SPACE 4
         LTORG
INPUT    DSECT
IDAT     DS    CL8
*
OUTPUT   DSECT
ODAT     DS    CL3
         END
