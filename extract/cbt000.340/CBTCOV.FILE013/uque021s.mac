PRNTTAPE CSECT
         SAVE  (14,12),,*
         BALR  2,0
         USING *,2,3,7,8
A0000000 EQU   *
         L     R3,BASE
         LM    R7,R8,BASE+4
         ST    R13,SAVE+4
         LA    R13,SAVE
         STM   R0,R1,STORE
         B     A0000001
BASE     DC    A(A0000000+4096)
         DC    A(A0000000+2*4096)
         DC    A(A0000000+3*4096)
SAVE     DC    18F'0'
A0000001 EQU   *
         OPEN  (SYSUT2,OUTPUT)
         LM    R0,R1,STORE
         B     P000
G000     EQU   *
         MVC   GETCAW(4),AREAD
         EXCP  GETIOB
         WAIT  ECB=GETECB
G001     TM    GETECB,X'7F'
         BM    G010
         L     R4,READ+4
         LA    R4,0(R4)
         L     R5,GETCSW+4
         LA    R5,0(R5)
         SR    R4,R5
         CH    R4,=H'80'
         BNE   G030
         LA    R5,EIN
         CLC   0(4,R5),=C'VOL1'
         BNE   G030
         CLI   ZVOL,C' '
         BE    G002
         CLC   ZVOL,4(R5)
         BE    G002
         MVC   MESS05+24(6),ZVOL
         MVC   MESS05+48(6),4(R5)
         MVC   Z1(54),MESS05
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
         ZAP   ZZ,=P'101'
         ZAP   SZ,=P'0'
         CLOSE MF=(E,OPEN2)
         OPEN  MF=(E,OPEN2)
         B     G000
G002     EQU   *
         MVI   SWLAB,C'R'
         MVC   LABI+40(3),0(R5)
         MVC   LABN+40(1),3(R5)
         MVC   V101+40(6),4(R5)
         MVC   V102+40(1),10(R5)
         MVC   V103+40(10),11(R5)
         MVC   V104+40(10),21(R5)
         MVC   V105+40(10),31(R5)
         MVC   V106+40(10),41(R5)
         MVC   V107+40(29),51(R5)
         LA    R11,V101
         LA    R10,V107
G005     ZAP   ZZ,=P'100'
         BAL   R12,W000
         B     G040
G010     EQU   *
         TM    GETCSW+4,X'01'
         BO    G020
G015     MVC   Z1(28),=C'-***, ERROR ON TAPE --- CSW:'
         MVC   Z1+47(2),=C'AT'
         MVC   Z1+56(6),=C', ECB:'
         MVC   Z1+72(2),=C'AT'
         MVC   Z1+81(15),=C', INPUT-AREA AT'
         LA    R4,GETCSW
         ST    R4,STORE
         UNPK  DOWO(9),STORE(5)
         MVC   Z1+50(6),DOWO+2
         TR    Z1+50(6),TAB-240
         LA    R4,GETECB
         ST    R4,STORE
         UNPK  DOWO(9),STORE(5)
         MVC   Z1+75(6),DOWO+2
         TR    Z1+75(6),TAB-240
         LA    R4,EIN
         ST    R4,STORE
         UNPK  DOWO(9),STORE(5)
         MVC   Z1+97(6),DOWO+2
         TR    Z1+97(6),TAB-240
         UNPK  DOWO(9),GETCSW(5)
         MVC   Z1+29(8),DOWO
         TR    Z1+29(8),TAB-240
         UNPK  DOWO(9),GETCSW+4(5)
         MVC   Z1+38(8),DOWO
         TR    Z1+38(8),TAB-240
         UNPK  DOWO(9),GETECB(5)
         MVC   Z1+63(8),DOWO
         TR    Z1+63(8),TAB-240
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
         MVC   Z1(40),=C'-***, LAST READ RECORD (FIRST 18 BYTES):'
         UNPK  Z1+41(13),EIN(7)
         UNPK  Z1+53(13),EIN+6(7)
         UNPK  Z1+65(13),EIN+12(7)
         MVI   Z1+77,C' '
         TR    Z1+41(36),TAB-240
         MVC   Z1+80(18),EIN
         BAL   R12,W000
         MVC   MESS00+33(4),=C'0008'
         CLI   DUMP,C'0'
         BE    E800
         ZAP   XX,=P'0'
         LA    R4,21
G017     EQU   *
         BAL   R12,W000
         BCT   R4,G017
         ABEND 1,DUMP
G020     EQU   *
         MVC   Z1(14),=C'-***, TAPEMARK'
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
G030     EQU   *
         MVC   Z1(32),=C'-***, NON STANDARD LABEL ON TAPE'
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
         B     E800
G040     EQU   *
         MVC   GETCAW(4),AREAD
         EXCP  GETIOB
         WAIT  ECB=GETECB
         TM    GETECB,X'7F'
         BM    G060
G050     EQU   *
         LA    R5,EIN
         MVC   LABI+40(3),0(R5)
         MVC   LABN+40(1),3(R5)
         CLC   0(4,R5),=C'HDR1'
         BE    G070
         CLC   0(4,R5),=C'HDR2'
         BE    G080
         CLC   0(4,R5),=C'EOF1'
         BE    G070
         CLC   0(4,R5),=C'EOF2'
         BE    G080
         CLC   0(4,R5),=C'EOV1'
         BE    G070
         CLC   0(4,R5),=C'EOV2'
         BE    G080
         AP    BAD,=P'1'
         CP    BAD,=P'5'
         BH    G057
         MVC   Z1(22),=C'-***,  NO LABEL-RECORD'
         MVI   SWX,C'2'
         B     D060
G055     EQU   *
         MVI   SWX,C'0'
         B     G040
G057     EQU   *
         MVC   GETCAW(4),AFSF
         EXCP  GETIOB
         WAIT  ECB=GETECB
         MVC   Z1(48),=C'-***, NON LABEL-RECORDS SKIPPED TO NEXT TAPEMA*
               RK'
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
         B     G090
G060     EQU   *
         TM    GETCSW+4,X'01'
         BO    G090
         B     G015
G070     EQU   *
         MVC   SWLAB,2(R5)
         MVC   H101+40(17),4(R5)
         MVC   H102+40(6),21(R5)
         MVC   H103+40(4),27(R5)
         MVC   H104+40(4),31(R5)
         MVC   H105+40(4),35(R5)
         MVC   H106+40(2),39(R5)
         MVC   H107+40(6),41(R5)
         MVC   H108+40(6),47(R5)
         MVC   H109+40(1),53(R5)
         MVC   H110+40(6),54(R5)
         MVC   H111+40(13),60(R5)
         MVC   H112+40(7),73(R5)
         LA    R11,H101
         LA    R10,H112
         B     G005
G080     EQU   *
         MVC   SWLAB,2(R5)
         MVC   H201+40(1),4(R5)
         MVC   H202+40(5),5(R5)
         MVC   H203+40(5),10(R5)
         MVC   H204+40(1),15(R5)
         MVC   H205+40(1),16(R5)
         MVC   H206+40(17),17(R5)
         MVC   H207+40(2),34(R5)
         MVC   H208+40(1),36(R5)
         MVC   H209+40(1),37(R5)
         MVC   H210+40(1),38(R5)
         MVC   H211+40(41),39(R5)
         LA    R11,H201
         LA    R10,H211
         B     G005
G090     EQU   *
         ZAP   BAD,=P'0'
         MVC   Z1(14),=C'-***, TAPEMARK'
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
         CLI   SWLAB,C'R'
         BE    D000
         CLI   SWLAB,C'V'
         BE    G130
         LA    R6,1(R6)
         CH    R6,LAB
         BH    E800
         BCTR  R6,R0
         MVC   GETCAW(4),AREAD
         EXCP  GETIOB
         WAIT  ECB=GETECB
         TM    GETECB,X'7F'
         BM    G100
         B     G050
G100     EQU   *
         TM    GETCSW+4,X'01'
         BO    G110
         B     G015
G110     EQU   *
         MVC   Z1(14),=C'-***, TAPEMARK'
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
G120     MVC   Z1(17),=C'-***, END OF DATA'
         BAL   R12,W000
         B     E800
G130     EQU   *
         MVC   Z1(19),=C'-***, END OF VOLUME'
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
         B     E800
D000     EQU   *
         LA    R6,1(R6)
         CLI   PALL,C'0'
         BE    D000A
         STH   R6,PRNT
D000A    EQU   *
         CH    R6,PRNT
         BE    D020
D001     EQU   *
         CH    R6,LAB
         BH    E800
         BL    D010
         CLI   LAB+2,C'0'
         BE    E800
D010     EQU   *
         MVC   GETCAW(4),AFSF
         EXCP  GETIOB
         WAIT  ECB=GETECB
D012     MVC   Z1(14),=C'-***, DATA-SET'
         MVC   Z1+14(8),=X'4020202020202020'
         CVD   R6,DOWO
         ED    Z1+14(8),DOWO+4
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
D015     MVC   Z1(14),=C'-***, TAPEMARK'
         BAL   R12,W000
         B     G040
D020     EQU   *
         CLI   H109+40,C'1'
         BNE   D025
         MVC   Z1+23(31),=C'PROTECTED, PRINTING NOT ALLOWED'
         MVC   PRNT(2),=H'0'
         B     D001
D025     EQU   *
         CLC   PRNT+2(2),=H'0'
         BNE   D150
         MVC   GETCAW(4),AFSF
         EXCP  GETIOB
         WAIT  ECB=GETECB
D030     EQU   *
         MVC   GETCAW(4),ABSB
         LA    R4,2
D042     EQU   *
         EXCP  GETIOB
         WAIT  ECB=GETECB
         BCT   R4,D042
         LH    R4,PRNT+8
         MVC   GETCAW(4),ABSB
D050     EQU   *
         EXCP  GETIOB
         WAIT  ECB=GETECB
         TM    GETCSW+4,X'01'
         BO    D100
         BCT   R4,D050
         LH    R4,PRNT+8
D055     EQU   *
         BCTR  R4,R0
         MVC   GETCAW(4),AREAD
         EXCP  GETIOB
         WAIT  ECB=GETECB
         TM    GETECB,X'7F'
         BM    D120
         MVC   Z1(12),=C'-***, RECORD'
         LTR   R4,R4
         BZ    D130
         MVC   Z1+13(6),=C'LAST -'
         MVC   Z1+19(8),=X'4020202020202020'
         CVD   R4,DOWO
         ED    Z1+19(8),DOWO+4
D060     EQU   *
         ZAP   XX,ZZ
         AP    XX,=P'7'
         CP    XX,=P'66'
         BNH   D070
         ZAP   ZZ,=P'69'
D070     EQU   *
         ZAP   XX,=P'3'
         LA    R11,Z1
         MVI   SWY,C'1'
         BAL   R12,W000
         STM   R4,R5,STORE
         L     R4,READ+4
         LA    R4,0(R4)
         L     R5,GETCSW+4
         LA    R5,0(R5)
         SR    R4,R5
         CVD   R4,DOWO
         MVC   U3+12(8),=X'4020202020202020'
         ED    U3+12(8),DOWO+4
         LA    R11,U3
         ZAP   XX,=P'2'
         MVI   SWY,C'1'
         BAL   R12,W000
         MVI   Z2,C'0'
         LA    R5,EIN
         AR    R4,R5
         ST    R4,EEND
         MVC   0(25,R4),U1+40
         LA    R4,Z2+79
         ST    R4,ZEND
         LA    R9,Z2+13
         ZAP   DISPL,=P'0'
         MVI   SW1,C'1'
         MVI   SWP,C'0'
         MVI   SWZ,C'0'
         LA    R5,EIN
X000     EQU   *
         LA    R9,11(R9)
         C     R9,ZEND
         BNE   X010
X005     MVC   Z2+14(6),=X'F02020202020'
         ED    Z2+14(6),DISPL
         MVC   Z2+21(2),=C'**'
         AP    DISPL,=P'25'
         MVI   SWZ,C'0'
         MVI   Z2+14,X'40'
         LA    R11,Z2
         BAL   R12,W000
         ZAP   XX,=P'1'
         LA    R9,Z2+24
         CLI   SWP,C'1'
         BE    X030
X010     EQU   *
         CLI   SWZ,C'1'
         BE    X015
         MVC   Z2+107(25),0(R5)
         TR    Z2+107(25),TAB1
         MVI   Z2+132,C'*'
         MVI   Z2+106,C'*'
         MVI   SWZ,C'1'
X015     EQU   *
         UNPK  0(11,R9),0(6,R5)
         TR    0(10,R9),TAB-240
         MVI   10(R9),C' '
         LA    R5,5(R5)
         C     R5,EEND
         BL    X000
         BE    X020
         S     R5,EEND
         LA    R10,5
         SR    R10,R5
         AR    R10,R10
         LA    R5,0(R10,R9)
         MVC   0(9,R5),=C'         '
X020     EQU   *
         MVI   SWP,C'1'
         B     X005
X030     EQU   *
         MVI   Z1,C' '
         MVC   Z1+1(132),Z1
         LM    R4,R5,STORE
         MVI   SW1,C'0'
         CLI   SWX,C'1'
         BE    D210
         CLI   SWX,C'2'
         BE    G055
         B     D055
D100     EQU   *
         MVI   RALL,C'1'
         MVC   PRNT+4(2),=X'7FFF'
         MVC   PRNT+8(2),=H'0'
         MVI   LAB+2,C'1'
         MVC   GETCAW(4),AFSB
         LA    R4,2
D110     EQU   *
         EXCP  GETIOB
         WAIT  ECB=GETECB
         BCT   R4,D110
         B     D020
D120     EQU   *
         TM    GETCSW+4,X'01'
         BO    D140
         B     G015
D130     EQU   *
         MVC   Z1+23(4),=C'LAST'
         B     D060
D140     EQU   *
         LA    R11,Z1
         ZAP   XX,=P'3'
         B     D015
D150     EQU   *
         CLC   PRNT+2(2),=H'1'
         BE    D180
         LH    R4,PRNT+2
         BCTR  R4,R0
         MVC   GETCAW(4),AFSB
D160     EQU   *
         EXCP  GETIOB
         WAIT  ECB=GETECB
         TM    GETCSW+4,X'01'
         BO    D245
         BCT   R4,D160
D180     EQU   *
         LH    R4,PRNT+2
D190     EQU   *
         CLI   RALL,C'1'
         BE    D200
         CH    R4,PRNT+4
         BE    D250
D200     EQU   *
         MVC   GETCAW(4),AREAD
         EXCP  GETIOB
         WAIT  ECB=GETECB
         TM    GETECB,X'7F'
         BM    D230
         MVC   Z1(12),=C'-***, RECORD'
         MVC   Z1+19(8),=X'4020202020202020'
         CVD   R4,DOWO
         ED    Z1+19(8),DOWO+4
         MVI   SWX,C'1'
         B     D060
D210     EQU   *
         MVI   SWX,C'0'
         LA    R4,1(R4)
         B     D190
D230     EQU   *
         TM    GETCSW+4,X'01'
         BO    D240
         B     G015
D240     EQU   *
         MVC   PRNT(11),ZPRNT
         ZAP   XX,=P'3'
         LA    R11,Z1
         B     D015
D245     EQU   *
         CLC   PRNT+8(2),=H'0'
         BE    D012
         B     D030
D250     EQU   *
         CLC   PRNT+8(2),=H'0'
         BE    D260
         MVC   GETCAW(4),AFSF
         EXCP  GETIOB
         WAIT  ECB=GETECB
         TM    GETECB,X'7F'
         BM    G015
         MVC   Z1(12),=C'-***, RECORD'
         MVC   Z1+19(8),=X'4020202020202020'
         CVD   R4,DOWO
         ED    Z1+19(8),DOWO+4
         B     D030
D260     EQU   *
         MVC   PRNT(11),ZPRNT
         CH    R6,LAB
         BNE   D270
         CLI   LAB+2,C'0'
         BE    E800
D270     EQU   *
         MVC   GETCAW(4),AFSF
         EXCP  GETIOB
         WAIT  ECB=GETECB
         B     D015
         EJECT
W000     EQU   *                        WRITER-ROUTINE
         CP    ZZ,=P'66'
         BH    W010
W004     AP    ZZ,XX
W005     LR    R0,R11
         PUT   SYSUT2
         CLI   SWY,C'1'
         BE    W008
         MVI   0(R11),C' '
         MVC   1(132,R11),0(R11)
         BR    R12
W008     EQU   *
         MVI   SWY,C'0'
         BR    R12
W010     EQU   *
         AP    SZ,=P'1'
         MVC   U1+128(5),=X'2020202020'
         ED    U1+127(6),SZ
         PUT   SYSUT2,U1
         PUT   SYSUT2,U2
         PUT   SYSUT2,U4
         CP    ZZ,=P'100'
         BE    W025
         CLI   SW1,C'0'
         BE    W020
         CLI   SW1,C'2'
         BE    W050
         MVI   Z1,C'-'
         MVC   Z1+28(11),=C'(CONTINUED)'
         PUT   SYSUT2,Z1
         PUT   SYSUT2,U3
         MVI   0(R11),C'0'
         ZAP   ZZ,=P'10'
         B     W005
W020     EQU   *
         ZAP   ZZ,=P'3'
         B     W004
W025     EQU   *
         PUT   SYSUT2,HEAD
         PUT   SYSUT2,HEADX
         PUT   SYSUT2,LABI
         PUT   SYSUT2,LABN
W030     EQU   *
         LR    R0,R11
         PUT   SYSUT2
         CR    R11,R10
         BNL   W040
         LA    R11,133(R11)
         B     W030
W040     EQU   *
         PUT   SYSUT2,HEADX
         ZAP   ZZ,=P'101'
         BR    R12
W050     EQU   *
         MVI   SW1,C'0'
         ZAP   ZZ,=P'3'
         BR    R12
E800     EQU   *
         CLOSE MF=(E,OPEN2)
E900     EQU   *
         MVC   Z1(50),MESS00
         ZAP   XX,=P'3'
         LA    R11,Z1
         BAL   R12,W000
         MVC   Z1(50),MESS04
         LA    R11,Z1
         BAL   R12,W000
         CLOSE (SYSUT2)
         PACK  DOWO,MESS00+33(4)
         CVB   R12,DOWO
         L     R13,SAVE+4
         ST    R12,16(R13)
         LM    R14,R12,12(R13)
         BR    R14
TAB      DC    C'0123456789ABCDEF'
STORE    DC    2F'0'
         DS    0D
GETIOB   EQU   *
         DC    X'02'
         DC    AL3(0)
         DC    A(GETECB)
GETCSW   DC    2F'0'
GETCAW   DC    A(READ)
         DC    A(GETDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
         DC    2F'0'
GETECB   DC    F'0'
READ     CCW   X'02',0,X'20',32767
BSB      CCW   X'27',0,X'20',32767
FSB      CCW   X'37',0,X'20',32767
FSF      CCW   X'3F',0,X'20',32767
RUN      CCW   X'0F',0,X'20',32767
GETDCB   DCB   DDNAME=SYSUT1,EXLST=JFCBLST,MACRF=(E),IOBAD=GETIOB
JFCBLST  EQU   *
         DC    X'87'
         DC    AL3(PJFCB)
         DS    0D
PJFCB    EQU   *
         IEFJFCBN
OPEN2    OPEN  (GETDCB),MF=L
AREAD    DC    A(READ)
ABSB     DC    A(BSB)
AFSB     DC    A(FSB)
AFSF     DC    A(FSF)
ARUN     DC    A(RUN)
SWLAB    DC    C'0'
EEND     DC    F'0'
ZEND     DC    F'0'
BAD      DC    P'0'
DISPL    DC    PL3'0'
SWP      DC    C'0'
SWX      DC    C'0'
SWZ      DC    C'0'
TAB1     DC    64C'.'
         DC    C' '
         DC    128C'.'
         DC    C'ABCDEFGHI'
         DC    7C'.'
         DC    C'JKLMNOPQR'
         DC    8C'.'
         DC    C'STUVWXYZ'
         DC    6C'.'
         DC    C'0123456789'
         DC    6C'.'
HEAD     DC    C'-'
         DC    C'FIELD IDENTIFICATION        LENGTH     CONTE'
         DC    C'NTS                                  COMMENT'
         DC    C'                                            '
HEADX    DC    C'-'
         DC    132C'*'
LABI     DC    C'-'
         DC    C'LABEL IDENTIFIER             (03)      XXX  '
         DC    88C' '
LABN     DC    C'-'
         DC    C'LABEL NUMBER                 (01)      X    '
         DC    88C' '
V101     DC    C'-'
         DC    C'VOLUME SERIAL NUMBER         (06)      XXXXX'
         DC    CL88'X'
V102     DC    C'-'
         DC    C'RESERVED                     (01)      X    '
         DC    CL88' '
V103     DC    C'-'
         DC    C'VTOC POINTER (DA ONLY)       (10)      XXXXX'
         DC    CL88'XXXXX'
V104     DC    C'-'
         DC    C'RESERVED                     (10)      XXXXX'
         DC    CL88'XXXXX'
V105     DC    C'-'
         DC    C'RESERVED                     (10)      XXXXX'
         DC    CL88'XXXXX'
V106     DC    C'-'
         DC    C'OWNER NAME AND ADDRESS CODE  (10)      XXXXX'
         DC    CL88'XXXXX'
V107     DC    C'-'
         DC    C'RESERVED                     (29)      XXXXX'
         DC    CL88'XXXXXXXXXXXXXXXXXXXXXXXX'
H101     DC    C'-'
         DC    C'DATA SET IDENTIFIER          (17)      XXXXX'
         DC    CL88'XXXXXXXXXXXX'
H102     DC    C'-'
         DC    C'DATA SET SERIAL NUMBER       (06)      XXXXX'
         DC    CL88'X'
H103     DC    C'-'
         DC    C'VOLUME SEQUENCE NUMBER       (04)      XXXX '
         DC    CL88' '
H104     DC    C'-'
         DC    C'DATA SET SEQUENCE NUMBER     (04)      XXXX '
         DC    CL88' '
H105     DC    C'-'
         DC    C'GENERATION NUMBER            (04)      XXXX '
         DC    CL88' '
H106     DC    C'-'
         DC    C'VERSION NUMBER               (02)      XX   '
         DC    CL88' '
H107     DC    C'-'
         DC    C'CREATION DATE                (06)      XXXXX'
         DC    C'X                                     YYDDD '
         DC    CL44' '
H108     DC    C'-'
         DC    C'EXPIRATION DATE              (06)      XXXXX'
         DC    C'X                                     YYDDD '
         DC    CL44' '
H109     DC    C'-'
         DC    C'DATA SET SECURITY            (01)      X    '
         DC    C'                                     0 = NO '
         DC    C'SECURITY PROTECTION                         '
         DC    CL45' '
         DC    C'                                     1 = SEC'
         DC    C'URITY PROTECTION (FOR READ,WRITE OR DELETE) '
         DC    CL45' '
         DC    C'                                     3 = SEC'
         DC    C'URITY PROTECTION (FOR WRITE OR DELETE)      '
H110     DC    C'0'
         DC    C'BLOCK COUNT                  (06)      XXXXX'
         DC    CL88'X'
H111     DC    C'-'
         DC    C'SYSTEM CODE                  (13)      XXXXX'
         DC    CL88'XXXXXXXX'
H112     DC    C'-'
         DC    C'RESERVED                     (07)      XXXXX'
         DC    CL88'XX'
H201     DC    C'-'
         DC    C'RECORD FORMAT                (01)      X    '
         DC    C'                                     F = FIX'
         DC    C'ED LENGTH                                   '
         DC    45C' '
         DC    C'                                     V = VAR'
         DC    C'IABLE LENGTH                                '
         DC    45C' '
         DC    C'                                     U = UND'
         DC    C'EFINED LENGTH                               '
H202     DC    C'0'
         DC    C'BLOCK LENGTH                 (05)      XXXXX'
         DC    88C' '
H203     DC    C'-'
         DC    C'RECORD LENGTH                (05)      XXXXX'
         DC    88C' '
H204     DC    C'-'
         DC    C'TAPE DENSITY                 (01)      X    '
         DC    C'                                     0 =  20'
         DC    C'0 BPI                                       '
         DC    45C' '
         DC    C'                                     1 =  55'
         DC    C'6 BPI                                       '
         DC    45C' '
         DC    C'                                     2 =  80'
         DC    C'0 BPI                                       '
         DC    45C' '
         DC    C'                                     3 = 160'
         DC    C'0 BPI                                       '
H205     DC    C'0'
         DC    C'DATA SET POSITION            (01)      X    '
         DC    C'                                     0 = NO '
         DC    C'VOLUME SWITCH HAS OCCURRED                  '
         DC    45C' '
         DC    C'                                     1 = A V'
         DC    C'OLUME SWITCH PREVIOUSLY OCCURRED            '
H206     DC    C'0'
         DC    C'JOB / JOBSTEP IDENTIFICATION (17)      XXXXX'
         DC    C'XXXXXXXXXXXX                                '
         DC    44C' '
H207     DC    C'-'
         DC    C'TAPE RECORDING TECHNIQUE     (02)      XX   '
         DC    C'                                     T  = OD'
         DC    C'D PARITY WITH TRANSLATION                   '
         DC    45C' '
         DC    C'                                     C  = OD'
         DC    C'D PARITY WITH CONVERSION                    '
         DC    45C' '
         DC    C'                                     E  = EV'
         DC    C'EN PARITY WITH NO TRANSLATION               '
         DC    45C' '
         DC    C'                                     ET = EV'
         DC    C'EN PARITY WITH TRANSLATION                  '
         DC    45C' '
         DC    C'                                        = OD'
         DC    C'D PARITY WITH NO TRANSLATION OR CONVERSION  '
H208     DC    C'0'
         DC    C'PRINTER CONTROL CHARACTERS   (01)      X    '
         DC    C'                                     A = CON'
         DC    C'TAINS ASCII CONTROL CHARACTERS              '
         DC    45C' '
         DC    C'                                     M = CON'
         DC    C'TAINS MACHINE CONTROL CHARACTER             '
         DC    45C' '
         DC    C'                                       = DOE'
         DC    C'S NOT CONTAIN CONTROL CHARACTER             '
H209     DC    C'0'
         DC    C'RESERVED                     (01)      X    '
         DC    88C' '
H210     DC    C'-'
         DC    C'BLOCK ATTRIBUTE              (01)      X    '
         DC    C'                                     B = BLO'
         DC    C'CKED RECORDS                                '
         DC    45C' '
         DC    C'                                     S = SPA'
         DC    C'NNED RECORDS                                '
         DC    45C' '
         DC    C'                                     R = BLO'
         DC    C'CKED AND SPANNED RECORDS                    '
         DC    45C' '
         DC    C'                                       = NOT'
         DC    C' BLOCKED AND NOT SPANNED RECORDS            '
H211     DC    C'0'
         DC    C'RESERVED                     (41)      XXXXX'
         DC    C'XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX         '
         DC    44C' '
U2       DS    0CL133
         DC    C' '
PARM     DC    CL132' '
U1       DC    C'1'
         DC    C'QUELLE SUPPORT UTILITY---PRNTTAPE---        '
         DC    C'                                            '
         DC    C'DATE: XX.XXX   TIME: XX.XX.XX     PAGE XXXXX'
U3       DC    C'0'
         DC    C'***, LENGTH XXXXXX  ** 00         05        '
         DC    C' 10         15         20                   '
         DC    C'                                            '
Z1       DC    CL133' '
U4       DC    CL133' '
SWY      DC    C'0'
XX       DC    PL2'0'
Z2       DC    CL133' '
         DC    CL9' '
MESS00   DC    C'-***, HIGHEST CONDITION-CODE WAS 0000             '
MESS04   DC    C'-***, END OF JOB ---PRNTTAPE---                   '
ZVOL     DC    CL6' '
CC       DC    X'00'
DUMP     DC    C'0'
DOWO     DC    D'0'
ZC       DC    X'00'
LAB      DC    H'1'
         DC    C'0'
PALL     DC    C'0'
PRNT     DC    H'0'                     DS-SEQ
         DC    H'0'                     FROM
         DC    H'0'                     TO
         DC    H'0'                     NUMBER
         DC    H'0'                     LAST
RALL     DC    C'0'
ZPRNT    DC    CL11' '
ZZ       DC    PL2'101'
SZ       DC    PL3'0'
SW1      DC    C'0'
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=(PM),DSORG=PS,                      *
               LRECL=133,BLKSIZE=665,BUFNO=2,RECFM=FBA
         LTORG
         EJECT
         DS    0H
P000     EQU   *                        ANALYZE PARAMETERS
         PARMPROG PARM
         TIME
         ST    R1,DOWO+4
         UNPK  DOWO(5),DOWO+5(3)
         MVC   U1+95(2),DOWO
         MVC   U1+98(3),DOWO+2
         ST    R0,STORE
         UNPK  DOWO(7),STORE(4)
         MVC   U1+110(2),DOWO
         MVC   U1+113(2),DOWO+2
         MVC   U1+116(2),DOWO+4
         LA    R4,PARM
P010     EQU   *
         CLI   0(R4),C' '
         BE    P900
         CLI   0(R4),C','
         BNE   P020
         LA    R4,1(R4)
P020     EQU   *
         CLC   0(4,R4),=C'VOL='
         BE    P100
P030     EQU   *
         CLC   0(6,R4),=C'LABEL='
         BE    P200
P040     EQU   *
         CLC   0(6,R4),=C'PRINT='
         BE    P300
P045     EQU   *
         CLC   0(4,R4),=C'DUMP'
         BE    P500
         SPACE 3
P050     EQU   *                        INVALID PARAMETER-KEYWORD
         MVC   U4(50),MESS01
         B     P440
         EJECT
P100     EQU   *                        ANALYZE PARAMETER 'VOL'
         MVC   P020(10),NOP
         LA    R4,4(R4)
         CLI   0(R4),C','
         BE    P010
         CLI   0(R4),C' '
         BE    P900
         ST    R2,STORE1
         TRT   0(7,R4),TALPHA
         BC    12,P105
         L     R2,STORE1
         MVC   ZVOL,0(R4)
         LA    R4,6(R4)
         B     P010
P105     EQU   *
         L     R2,STORE1
P110     EQU   *                        INVALID VALUE IN PARM-LIST
         MVC   U4(50),MESS02
         B     P440
         EJECT
P200     EQU   *                        ANALYZE PARAMETER 'LABEL'
         MVC   P030(10),NOP
         LA    R4,6(R4)
         CLI   0(R4),C','
         BE    P010
         CLI   0(R4),C' '
         BE    P900
         CLC   0(3,R4),=C'ALL'
         BE    P210
         CLC   0(6,R4),=C'HEADER'
         BE    P220
         CLC   0(7,R4),=C'TRAILER'
         BE    P230
         MVI   TNUM+93,X'FF'
         MVI   TNUM+126,X'7E'
         LA    R6,LAB
         BAL   R12,V000
         CLI   0(R4),C','
         BE    P010
         CLI   0(R4),C' '
         BE    P900
         LA    R4,1(R4)
         CLC   0(6,R4),=C'HEADER'
         BE    P220
         CLC   0(7,R4),=C'TRAILER'
         BE    P230
         B     P050
P210     EQU   *
         MVC   LAB,=X'7FFF'
         LA    R4,3(R4)
         B     P240
P220     EQU   *
         LA    R4,6(R4)
         B     P010
P230     EQU   *
         LA    R4,7(R4)
P240     EQU   *
         MVI   LAB+2,C'1'
         B     P010
         EJECT
P300     EQU   *                        ANALYZE PARAMETER 'PRINT'
         MVC   P040(10),NOP
         LA    R4,6(R4)
         MVC   PRNT(2),=H'1'
         CLI   0(R4),C','
         BE    P010
         CLI   0(R4),C'('
         BE    P310
         CLI   0(R4),C' '
         BE    P900
         B     P320
P310     EQU   *
         LA    R4,1(R4)
         MVI   TNUM+93,X'5D'
P320     EQU   *
         MVI   TNUM+126,X'FF'
P330     EQU   *
         CLC   0(6,R4),=C'DSSEQ='
         BE    P370
P340     CLC   0(5,R4),=C'FROM='
         BE    P380
P350     CLC   0(7,R4),=C'NUMBER='
         BE    P390
P360     CLC   0(5,R4),=C'LAST='
         BE    P400
         B     P050
P370     EQU   *
         MVC   P330(10),NOP
         LA    R4,6(R4)
         CLC   0(3,R4),=C'ALL'
         BE    P375
         LA    R6,PRNT
         BAL   R12,V000
         B     P410
P375     EQU   *
         MVC   PRNT(2),=X'7FFF'
         MVI   PALL,C'1'
         MVI   SWLAB+2,C'1'
         LA    R4,3(R4)
         B     P410
P380     EQU   *
         MVC   P340(10),NOP
         MVC   PRNT+2(2),=H'1'
         LA    R4,5(R4)
         LA    R6,PRNT+2
         BAL   R12,V000
         MVC   PRNT+4(2),PRNT+2
         B     P410
P390     EQU   *
         MVC   P350(10),NOP
         MVC   PRNT+6(2),=H'1'
         LA    R4,7(R4)
         CLC   0(3,R4),=C'ALL'
         BE    P395
         LA    R6,PRNT+6
         BAL   R12,V000
         B     P410
P395     EQU   *
         MVI   RALL,C'1'
         LA    R4,3(R4)
       B       P410
P400     EQU   *
         MVC   P360(10),NOP
         MVC   PRNT+8(2),=H'1'
         LA    R4,5(R4)
         MVC   PRNT+8(2),=H'1'
         LA    R6,PRNT+8
         BAL   R12,V000
P410     EQU   *
         CLI   TNUM+93,C')'
         BNE   P010
         CLI   0(R4),C','
         BNE   P420
         LA    R4,1(R4)
         B     P330
P420     EQU   *
         CLI   0(R4),C')'
         BNE   P430
         LA    R4,1(R4)
         B     P010
P430     EQU   *                        SYNTAX ERROR IN PARAMETER
         MVC   U4(50),MESS03
P440     EQU   *
         MVI   SW1,C'2'
         BAL   R12,W000
         MVC   MESS00+33(4),=C'0004'
         B     E900
P500     EQU   *
         MVC   P045(10),NOP
         LA    R4,4(R4)
         MVI   DUMP,C'1'
         B     P010
P900     EQU   *
         LA    R4,U4+1
         CLI   ZVOL,C' '
         BE    P910
         MVC   U4+1(4),=C'VOL='
         MVC   U4+5(6),ZVOL
         MVI   U4+11,C','
         LA    R4,11(R4)
P910     EQU   *
         MVC   0(6,R4),=C'LABEL='
         CLI   PALL,C'0'
         BE    P912
         MVC   PRNT(2),=X'7FFF'
P912     EQU   *
         LH    R5,LAB
         LH    R6,PRNT
         CR    R5,R6
         BE    P916
         BH    P915
         MVC   LAB,PRNT
         MVI   LAB+2,C'0'
         B     P916
P915     EQU   *
         CLC   LAB,=X'7FFF'
         BNE   P920
         MVC   6(3,R4),=C'ALL'
         LA    R4,9(R4)
         B     P940
P916     EQU   *
         CLC   PRNT+8(2),=H'0'
         BE    P915
         MVI   LAB+2,C'1'
         B     P915
P920     EQU   *
         LH    R5,LAB
         CVD   R5,DOWO
         MVN   DOWO+7(1),=X'FF'
         UNPK  6(6,R4),DOWO+4(4)
         MVI   12(R4),C'='
         CLI   LAB+2,C'1'
         BE    P930
         MVC   13(6,R4),=C'HEADER'
         LA    R4,19(R4)
         B     P940
P930     EQU   *
         MVC   13(7,R4),=C'TRAILER'
         LA    R4,20(R4)
P940     EQU   *
         CLI   PALL,C'1'
         BE    P943
         CLC   PRNT(2),=H'0'
         BE    P960
         MVC   0(14,R4),=C',PRINT=(DSSEQ='
         LH    R5,PRNT
         CVD   R5,DOWO
         MVN   DOWO+7(1),=X'FF'
         UNPK  14(6,R4),DOWO+4(4)
         LA    R4,20(R4)
P941     EQU   *
         CLC   PRNT+2(2),=H'0'
         BNE   P944
         CLC   PRNT+6(2),=H'0'
         BNE   P942
         CLC   PRNT+8(2),=H'0'
         BNE   P946
         MVC   PRNT+6(2),=H'1'
P942     EQU   *
         MVC   PRNT+2(2),=H'1'
         B     P946
P943     EQU   *
         MVC   0(17,R4),=C',PRINT=(DSSEQ=ALL'
         LA    R4,17(R4)
         B     P941
P944     EQU   *
         CLC   PRNT+6(2),=H'0'
         BNE   P946
         MVC   PRNT+6(2),=H'1'
P946     EQU   *
         CLC   PRNT+2(2),=H'0'
         BE    P950
         MVC   0(6,R4),=C',FROM='
         LH    R5,PRNT+2
         CVD   R5,DOWO
         MVN   DOWO+7(1),=X'FF'
         UNPK  6(6,R4),DOWO+4(4)
         MVC   12(8,R4),=C',NUMBER='
         CLI   RALL,C'1'
         BE    P995
         LH    R5,PRNT+6
         CVD   R5,DOWO
         MVN   DOWO+7(1),=X'FF'
         UNPK  20(6,R4),DOWO+4(4)
         AH    R5,PRNT+2
         STH   R5,PRNT+4
         LA    R4,26(R4)
P950     EQU   *
         CLC   PRNT+8(2),=H'0'
         BE    P955
         MVC   0(6,R4),=C',LAST='
         LH    R5,PRNT+8
         CVD   R5,DOWO
         MVN   DOWO+7(1),=X'FF'
         UNPK  6(6,R4),DOWO+4(4)
         LA    R4,12(R4)
P955     EQU   *
         MVI   0(R4),C')'
         LA    R4,1(R4)
P960     EQU   *
         MVC   ZPRNT(11),PRNT
         CLI   DUMP,C'0'
         BE    P965
         MVC   0(5,R4),=C',DUMP'
         LA    R4,5(R4)
P965     EQU   *
         MVC   1(27,R4),=C'  *** OPTIONS IN EFFECT ***'
         RDJFCB MF=(E,OPEN2)
         MVI   JFCBLTYP,X'10'           SET BLP
         MVC   JFCBFLSQ,=X'0001'        SET SEQUENCE NUMBER TO 1
         MVI   JFCBIND2,X'40'           SET DISP TO OLD
         MVI   JFCRECFM,X'C0'           SET RECFM TO U
         MVC   JFCBLKSI,=X'7FFF'
         MVC   JFCLRECL,=X'7FFF'
         CLI   JFCBVOLS,X'FF'
         BNE   P967
         MVC   JFCBVOLS(6),=C'      '
P967     EQU   *
         CLI   ZVOL,C' '
         BE    P970
         MVC   JFCBVOLS(6),ZVOL
P970     EQU   *
         CLI   JFCBVOLS,C' '
         BNE   P990
         MVC   JFCBVOLS(6),=C'XXXXXX'
P980     EQU   *
         MVI   JFCBVOLS+6,C' '
         MVC   JFCBVOLS+7(23),JFCBVOLS+6
         MVI   JFCBVLCT,X'01'
         MVI   JFCBNVOL,X'01'
         MVI   JFCBMASK+5,X'00'
         OPEN  MF=(E,OPEN2),TYPE=J
         LA    R1,EIN
         ST    R1,0(R1)
         MVC   READ+1(3),1(R1)
         SR    R6,R6
         B     G000
P990     EQU   *
         MVC   ZVOL,JFCBVOLS
         B     P980
P995     EQU   *
         MVC   PRNT+8(2),=H'0'
         MVC   20(3,R4),=C'ALL'
         LA    R4,23(R4)
         B     P950
         EJECT
V000     EQU   *                        GET VALUE FROM PARM-LIST
         ST    R2,STORE1
         TRT   0(7,R4),TNUM
         BC    6,V010
         B     V050
V010     EQU   *
         STC   R2,ZC
         CLI   ZC,X'FF'
         BE    V050
         CR    R4,R1
         BNE   V020
         SR    R5,R5
         B     V060
V020     EQU   *
         LR    R5,R1
         SR    R5,R4
         BCTR  R5,R0
         STC   R5,ZC
         MVN   V030+1(1),ZC
V030     EQU   *
         PACK  DOWO,0(1,R4)
         AR    R4,R5
         LA    R4,1(R4)
         CVB   R5,DOWO
         LTR   R5,R5
         BNZ   V040
         B     V060
V040     EQU   *
         STH   R5,0(R6)
         B     V060
V050     EQU   *
         L     R2,STORE1
         B     P110
V060     EQU   *
         L     R2,STORE1
         BR    R12
         EJECT
NOP      DC    5XL2'0700'
TALPHA   DC    64XL1'FF'
         DC    X'40'
         DC    42XL1'FF'
         DC    X'6B'
         DC    85XL1'FF'
         DC    9XL1'00'
         DC    7XL1'FF'
         DC    9XL1'00'
         DC    8XL1'FF'
         DC    8XL1'00'
         DC    6XL1'FF'
         DC    10XL1'00'
         DC    6XL1'FF'
TNUM     DC    64XL1'FF'
         DC    X'40'
         DC    42XL1'FF'
         DC    X'6B'
         DC    132XL1'FF'
         DC    10XL1'00'
         DC    6XL1'FF'
MESS01   DC    C'-***, INVALID KEYWORD IN THE PARAMETERLIST        '
MESS02   DC    C'-***, INVALID VALUE IN THE PARAMETERLIST          '
MESS03   DC    C'-***, SYNTAX ERROR IN THE PARAMETERLIST           '
MESS05   DC   C'-***, REQUESTED VOLUME: XXXXXX, MOUNTED VOLUME: XXXXXX'
         REGDEF R
STORE1   DC    F'0'
         LTORG
         ORG   P000
         DS    0D
EIN      DS    CL32795
         END
