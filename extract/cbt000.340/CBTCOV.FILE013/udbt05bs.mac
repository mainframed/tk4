         PRINT NOGEN
VORCAT   ANFASS R3
         L     R2,0(0,R1)               ADR. DER PARAMETERLISTE IN R2
         LH    R7,0(0,R2)               LAENGE DES PARAMETERS NACH R7
         BCTR  R7,0                     NAMENSLAENGE MINUS 1
         EX    R7,MOVE                  PARAMETER WEGSPEICH.
         MVC   ALTGEN,GEN               PARAM. NACH ALTGEN
         LA    R7,1(R7)                 ECHTE NAMENSLAENGE NACH R7
*
*        AUFSUCHEN DES CVOLS, IN DEM DIE GDG KATALOGISIERT IST
*
         LA    R4,GEN
         LA    R5,44
LOOP     CLI   0(R4),C'.'               ENDE DES HIGH-LEVEL-INDEX
         BE    BLANK                    SUCHEN
         LA    R4,1(R4)
         BCT   R5,LOOP
BLANK    MVI   0(R4),C' '               UND
         BCTR  R5,0                     LOW-LEVEL-
         BCTR  R5,0                     -INDIZES
         EX    R5,MVCBLANK              AUSBLANKEN
*
         LOCATE INDSN                   STELLT CVOL-NAME NACH GEN
         LTR   R15,R15
         BNZ   FEHLER
         MVC   CVOL,GEN+9               VOLSER UEBERTRAGEN FUER KAT
         MVC   GEN,ALTGEN               DSNAME WIEDERHERSTELLEN
*
*        KATALOGEINTRAG D. NEUSTEN GENERATION LESEN, ENTKATALOGISIEREN
*
         LOCATE INDSN                   LESEN DES KATALOG-EINTRAGS
         LTR   R15,R15                  PRUEFEN RUECKKEHR-CODE
         BNZ   FEHLER                   WENN NICHT 0 , BRANCH FEHLER
*
         MVC   ALTGEN(44),GEN
         LA    R4,GEN                   ADR. DSN(0) GENERATION
         LA    R5,ALTGEN
         SH    R7,=H'3'                 LAENGE DATEI-GEN ABZIEHEN
         AR    R4,R7                   + LAENGE DSN
         AR    R5,R7
*
*        NEUSTE GENERATIONS-NR. + 1 FUER RENAME UND KATALOGISIERUNG
*
         MVC   GENR(8),1(R5)            UEBERTRAGEN GENERATIONS-NR
         PACK  REF(4),GENR+1(4)         PACKEN GEN.NR
         AP    REF(4),=P'1'             GEN.-NR.=GEN.-NR. + 1
         UNPK  GENR+1(4),REF+2(2)       ENTPACKEN GEN.NR
         OI    GENR+4,X'F0'             VORZEICHEN ENTFERNEN
         MVC   1(8,R5),GENR             GEN.NR NACH ALTGEN UEBERTRAGEN
*
*        KATALOG LESEN, BIS AELTESTE GEN. GEFUNDEN, ENTKATALOGISIEREN
*
NEXTGEN  MVC   VOLUME(14),LOCAREA       ALTE VOLUME-INFO. NACH VOLUME
         MVC   GENR(8),1(R4)            UEBERTRAGEN GENERATIONS-NR
         PACK  REF(4),GENR+1(4)         PACKEN GEN.NR
         SP    REF(4),=P'1'             GEN.-NR.=GEN.-NR.-1
         UNPK  GENR+1(4),REF+2(2)       ENTPACKEN GEN.NR
         OI    GENR+4,X'F0'             VORZEICHEN ENTFERNEN
         MVC   1(8,R4),GENR             GEN.NR NACH ALTGEN UEBERTRAGEN
*
         LOCATE INDSN                   EXIST. DIESE GEN. IM KATALOG?
         LTR   R15,R15
         BZ    NEXTGEN                  JA, NAECHSTE GEN. NEHMEN
*
         AP    REF(4),=P'1'             NEIN, AUF DIE AELTESTE
         UNPK  GENR+1(4),REF+2(2)       KATALOGISIERTE
         OI    GENR+4,X'F0'             GENERATION ZURUECKSETZEN
         MVC   1(8,R4),GENR             UND DIESE
         CATALOG ENTKAT                 ENTKATALOGISIEREN
         LTR   R15,R15                  PRUEFEN RUECKKEHR-CODE
         BNZ   FEHLER
*
*        DIE ENTKATALOGISIERTE GENERATION WIRD NUN RENAMED
*        ZU GENERATION "+1" UND ANSCHLIESSEND KATALOGISIERT
*
         RENAME RENLIST                 RENAME DURCHFUEHREN
         CLC   STATUS(2),=H'0'          ERFOLGREICH?
         BNE   FEHLER                   NEIN
*
         CATALOG KAT                    JA, UNTER NEUEM NAMEN KATALOG.
         LTR   R15,R15                  PRUEFEN RUECKKEHR-CODE
         BNZ   FEHLER
         B     RETURN
*
FEHLER   EQU   *
         WTO   'GDG NICHT IM KATALOG GEFUNDEN ODER FEHLER BEIM UMKATALO*
               GISIEREN',ROUTCDE=11
         B     RETURN
*
MOVE     MVC   GEN(0),2(R2)             DS-NAMEN WEGSPEICHERN
MVCBLANK MVC   1(0,R4),0(R4)            BLANKEN DER LOW-LEVEL-INDEXES
INDSN    CAMLST NAME,GEN,,LOCAREA       FUER LOCATE KATALOG-EINTRAG
GEN      DC    CL44' '                  DS-NAME FUER CAMLST
ALTGEN   DC    CL44' '
LOCAREA  DS    0D
         DS    265C                     BLOCK FUER KATALOG. DS-NAMEN
*
ENTKAT   CAMLST UNCAT,GEN               AELTESTE GEN. ENTKATALOGIS.
*
KAT      CAMLST CATBX,ALTGEN,CVOL,VOLUME   GEN. "+1" KATALOGISIEREN
*
RENLIST  CAMLST RENAME,GEN,ALTGEN,VOLUME   RENAME D. AELTESTEN GEN.
*
VOLUME   DS    H                        VOL. COUNT
         DS    XL4                      VOLUME TYPE
         DS    CL6                      VOLUME SERIAL NUMBER
STATUS   DS    H                        DATA SET SEQUENCE NUMBER BZW.
*                                       STATUS CODE NACH RENAME-OP.
REF      DC    CL4'0'
GENR     DC    CL8'0'
CVOL     DC    CL6'XXXXXX'
         END
