//Z223068M JOB (6685,478,1),
//   TIME=1,CLASS=1,MSGCLASS=O
/*JOBPARM SYSAFF=*
//ASM  EXEC ASMFCL,LEPRM=',AC=1,NCAL'
//ASM.SYSIN DD *
MONITOR  START
MONITOR  AMODE 31
MONITOR  RMODE 24
         REG
         XSAVE (R12,R11),,MONITOR,WORKL
         USING WORK,R13
         USING DMNDSCT,R5
         LA    R2,CLEAR
         LR    R4,R2
         LA    R3,CLEARLL
         SR    R5,R5
         MVCL  R2,R4
         L     R2,16
         MVC   MODE,116(R2)     SAVE MODE
         L     R2,X'C4'(R2)     SMCA
         L     R2,X'80'(R2)     SMCA-USER
         MVC   IMSAFF,20(R2)
         MVC   TSOAFF,24(R2)
         MVI   HEAD3,C'*'
         MVC   HEAD3+1(77),HEAD3
         MVI   HEAD4,C'-'
         MVC   HEAD4+1(77),HEAD4
         L     R1,0(R1)
         LH    R2,2(R1)
         LA    R1,2(R2,R1)
         OC    0(8,R1),=CL8' '
         CLC   2(4,R1),=CL4'AUTO'
         BNE   NOAUTO
         MVI   AUTO,X'FF'
NOAUTO   EQU   *
*        L     R2,16               CVT
*        L     R2,0(R2)
*        L     R2,12(R2)           ASCB
*        L     R2,108(R2)          ASXB
*        L     R1,4(R2)            FIRST TCB
*        LA    R15,TCBSVC1
*        SVC   250
         L     R2,16
         L     R2,0(R2)
         L     R2,4(R2)           CURRENT TCB
         L     R2,12(R2)          TIOT
         CLC   0(5,R2),=CL5'Z134M'
         BNE   TIMELOOP
         MVC   RESETCMD+12(1),5(R2)
         LA    R1,RESETCMD
         SVC   249
         LA    R15,DSWPSVC
         SVC   250
TIMELOOP EQU   *
         XC    LINECT(4),LINECT
         OI    FUNCIND,DISPSUPP
         CALL  SDIMSMON,(TEST,TEST),VL
         LTR   R15,R15
         BNZ   NOIMS
         MVC   MAXLIN(4),=F'14'
         MVI   IMS+1,X'00'
         B     IMSACT
NOIMS    EQU   *
         MVC   MAXLIN(4),=F'17'
         MVI   IMS+1,X'F0'
IMSACT   EQU   *
         TIME  DEC
         ST    R0,FULLWD
         UNPK  DBLWD,FULLWD
         MVC   HEAD1(2),DBLWD+1
         MVC   HEAD1+3(2),DBLWD+3
         MVC   HEAD1+6(2),DBLWD+5
         L     R2,16
         L     R3,604(R2)               RMCT
         L     R3,24(R3)                WMST
         MVC   HEAD1+22(2),4(R3)        IPS VALUE
         MVC   IPS,4(R3)
         SPACE
         TM    MODE,X'80'                XA?
         BNO   APVT                      NO, 370, GET PVT
         L     R3,X'490'(R2)             RCE
         MVC   P2IN,X'44'(R3)            PAGE IN
         MVC   P2OUT,X'58'(R3)           PAGE OUT
         MVC   P2VIN,X'54'(R3)           VIO IN
         MVC   P2VOUT,X'64'(R3)          VIO OUT
         MVC   P2VREC,X'30'(R3)          VIO RECLAIM
         MVC   P2SIN,X'50'(R3)           SWAP IN
         MVC   P2SOUT,X'60'(R3)          SWAP OUT
         MVC   P2REC,X'34'(R3)           RECLAIM
         SPACE
         L     R2,X'25C'(R2)             RMCT
         L     R2,8(R2)                  ICT
         L     R3,X'3C'(R2)              I/O INTERRUPTS VIA TPI
         A     R3,X'40'(R2)              I/O INTERRUPTS VIA SLIH
         ST    R3,XAICT2                 SAVE # OF INTERRUPTS
         B     ECAT                      SKIP 370-MODE
         SPACE
APVT     EQU   *
         L     R3,X'164'(R2)     A(PVT)
         MVC   PAGEVAL2,X'F8'(R3)
         LR    R3,R2
         SH    R3,=H'4'
         CLC   0(3,R3),=CL3'038'
         BNE   ECAT
         L     R2,X'41C'(R2)   ADDRESS OF CST (CHANNEL SET TABLE)
         LA    R9,6    MAX NO. OF SCT'S
         LA    R14,CATS2
NXTSCT   EQU   *
         TM    20(R2),X'80'
         BNO   CAT0
         L     R3,0(R2)
         LTR   R3,R3
         BNZ   SCAT0
CAT0     XC    0(256,R14),0(R14)
         B     INCRCST
SCAT0    EQU   *
         MVC   0(256,R14),0(R3)
INCRCST  EQU   *
         LA    R2,32(R2)   GET NEXT CST-ENTRY
         LA    R14,256(R14)    GET NEXT CAT-ENTRY
         BCT   R9,NXTSCT
ECAT     EQU   *
         STCK  TOD2
FIRST    NOP   COMP
         MVI   FIRST+1,X'F0'
         B     PUTHEAD1
COMP     EQU   *
         BAL   R9,BTODDIFF
         LA    R3,WAIT2
         BAL   R9,GETWAIT
         L     R1,16
         L     R1,604(R1)    A(RMCT)
         L     R1,4(R1)      A(CCT)
         LH    R1,104(R1)    LONG-TERM CPU-UTILISATION
         SRL   R1,8
         CVD   R1,DBLWD
         UNPK  HEAD1+36(2),DBLWD
         OI    HEAD1+37,X'F0'
         CH    R1,=H'99'
         BNH   WEITER
ASTX     MVC   HEAD1+36(2),=C'99'
WEITER   EQU   *
         L     R1,PAGEVAL2+0    PAGE-IN
         S     R1,PAGEVAL1+0
         L     R2,PAGEVAL2+4    PAGE-OUT
         S     R2,PAGEVAL1+4
         AR    R1,R2
         L     R2,PAGEVAL2+8    VIO-IN
         S     R2,PAGEVAL1+8
         AR    R1,R2
         L     R2,PAGEVAL2+12   VIO-OUT
         S     R2,PAGEVAL1+12
         AR    R1,R2
         L     R2,PAGEVAL2+20   SWAP-IN
         S     R2,PAGEVAL1+20
         AR    R1,R2
         L     R2,PAGEVAL2+24   SWAP-OUT
         S     R2,PAGEVAL1+24
         AR    R1,R2
         LTR   R1,R1
         BNM   *+6
         SR    R1,R1
         M     R0,=F'1024'
         D     R0,TODDIFF
         CVD   R1,DBLWD
         UNPK  HEAD1+45(3),DBLWD
         OI    HEAD1+47,X'F0'
         CH    R1,=H'999'
         BNH   GETIOS
         MVC   HEAD1+45(3),=C'***'
GETIOS   EQU   *
         TM    MODE,X'80'         XA?
         BNO   IOS370             NO
         SPACE
         XR    R4,R4
         XR    R5,R5
         L     R3,XAICT1
         L     R5,XAICT2          GET NO OF INTERRUPTS
         CR    R3,R5
         BH    IOSSTAR
         BE    IOSDIF
         SR    R5,R3
         B     IOSDIF
         SPACE
IOS370   EQU   *
         LA    R1,CATS1
         LA    R2,CATS2
         BAL   R9,IOSUM
IOSDIF   EQU   *
         M     R4,=F'1024'
         D     R4,TODDIFF
         CVD   R5,DBLWD
         UNPK  HEAD1+56(3),DBLWD
         OI    HEAD1+58,X'F0'
         CH    R5,=H'999'
         BNH   PUTHEAD1
IOSSTAR  EQU   *
         MVC   HEAD1+56(3),=C'***'
PUTHEAD1 EQU   *
         SR    R15,R15
         SR    R14,R14
         L     R4,16            LOAD ADDR. OF CVT
         L     R4,X'22C'(R4)  LOAD ADDR. OF ASVT
         L     R8,X'204'(R4)    LOAD MAX. NO. OF ASCBS
         LA    R5,X'210'(R4)    A(FIRST ENTRY)
NXTTSU   EQU   *
         TM    0(R5),X'80'   ASSIGNED?
         BO    NOTASS
         L     R9,0(R5)
         L     R7,X'90'(R9)   A(OUCB)
         TM    18(R7),X'20'
         BO    COUNTTSU
         L     R4,X'B0'(R9)
         CLC   0(4,R4),=CL4'INIT'
         BNE   NOTASS
         L     R4,X'AC'(R9)
         LTR   R4,R4
         BZ    NOTASS
         LA    R14,1(R14)
         B     NOTASS
COUNTTSU LA    R15,1(R15)
NOTASS   EQU   *
         LA    R5,4(R5)
         BCT   R8,NXTTSU
         CVD   R15,DBLWD
         UNPK  HEAD1+67(3),DBLWD
         OI    HEAD1+69,X'F0'
         CVD   R14,DBLWD
         UNPK  HEAD1+29(2),DBLWD
         OI    HEAD1+30,X'F0'
         SR    R14,R14
         SR    R15,R15
         L     R4,16
         L     R4,X'25C'(R4)   A(RMCT)
         L     R4,X'E4'(R4)    A(RCT)
         XR    R15,R15
         TM    MODE,X'80'      XA?
         BNO   UIC370          NO
         ICM   R15,3,X'36'(R4)  AVERAGE UIC XA
         B     UICCVD
UIC370   EQU   *
         ICM   R15,3,X'3E'(R4)    AVERAGE UIC IN SYSTEM
UICCVD   EQU   *
         CVD   R15,DBLWD
         UNPK  HEAD1+75(3),DBLWD
         OI    HEAD1+77,X'F0'
         CH    R15,=H'999'
         BNH   UICOK
         MVC   HEAD1+75(3),=C'***'
UICOK    EQU   *
         L     R4,16
         L     R4,604(R4)           RMCT
         L     R5,88(R4)            WAMT
         LA    R1,2         = TSO PERFORMANCE GROUP
         SLL   R1,2
         L     R6,80(R1,R5)
NOSE1    EQU   *
         AR    R5,R6
         SR    R14,R14
         L     R15,0(R5)
         LTR   R15,R15
         BZ    NO0C9
         L     R15,12(R5)
         D     R14,0(R5)
         SR    R14,R14
         D     R14,=F'1000'
         C     R15,=F'9'
         BH    RTEX
NO0C9    EQU   *
         CVD   R15,DBLWD
         SRDL  R14,32
         D     R14,=F'10'
         CVD   R15,DOUBLEX
         UNPK  HEAD1+13(1),DBLWD
         OI    HEAD1+13,X'F0'
         UNPK  HEAD1+15(2),DOUBLEX
         OI    HEAD1+16,X'F0'
         B     RTOK
RTEX     EQU   *
         MVC   HEAD1+13(4),=C'9.99'
RTOK     EQU   *
         L     R5,=V(DMNCSCT)
         L     R4,16
         L     R4,0(R4)
         L     R4,4(R4)
         L     R4,12(R4)
         CLC   0(4,R4),=CL4'Z134'
         BNE   NODMN
         MVC   DMNTXT,IPSBCMD
         L     R4,16
         L     R4,196(R4)           SMCA
         TIME  DEC
         SRL   R0,24
         ST    R0,TIME
         CH    R0,=H'23'
         BH    SETIPSB
         CH    R0,=H'6'
         BH    NOIPSB
SETIPSB  CLI   IPS,C'B'
         BE    NOIPSB1
         MVC   DMNTXT+8(1),17(R4)    SYSID
         LA    R1,DMNCMD
         SVC   249
         B     NOIPSB1
NOIPSB   EQU   *
         CLC   16(4,R4),IMSAFF
         BNE   NOIPSB1
         L     R0,TIME
         CH    R0,=H'22'
         BH    NOIPSB1
         CLI   IPS,C'A'
         BE    NOIPSB1
         MVC   DMNTXT+7(2),=C'A5'
         LA    R1,DMNCMD
         SVC   249
NOIPSB1  EQU   *
         CLC   16(4,R4),TSOAFF
         BNE   NODMN
         L     R4,16
         L     R4,0(R4)
         L     R4,4(R4)
         L     R4,12(R4)
         CLC   0(4,R4),=CL4'Z134'
         BNE   NODMN
         L     R4,COUNT
         SH    R4,=H'1'
         ST    R4,COUNT
         LTR   R4,R4
         BNZ   NODMN
         LA    R4,10
         ST    R4,COUNT
         L     R0,TIME
         CH    R0,=H'24'  = 18 UHR
         BNL   NODMN
         CH    R0,=H'7'
         BL    NODMN
         L     R5,=V(DMNCSCT)
         CLI   HEAD1+13,C'1'
         BL    SETA
         CLC   IPS,=C'O2'
         BE    NOTIPS
         MVC   DMNTXT,TIPS
         LA    R1,DMNCMD
         SVC   249
         B     NODMN
NOTIPS   EQU   *
         BAL   R4,SDMNO
         B     NODMN
SETA     EQU   *
         BAL   R4,SDMNA
NODMN    EQU   *
         L     R4,16
         L     R4,X'22C'(R4)    A(ASVT)
         LA    R5,X'210'(R4)    1. ENTRY
         LA    R7,TIMEBLK-8
         BAL   R9,TIMELP1
         STCK  TOD1
         NI    FUNCIND,255-DISPSUPP
         SPACE
         TM    MODE,X'80'
         BO    NODMNXA             MODE=XA
         STIMER WAIT,BINTVL=ONESECND
         B     CLOCK
         SPACE
NODMNXA  EQU   *
         SPLEVEL SET=2
         STIMER WAIT,BINTVL=ONESECND
         SPLEVEL SET=1
         SPACE
CLOCK    EQU   *
         STCK  TOD2
         TPUT  BUFF,12,FULLSCR
         TPUT  HEAD1,78,EDIT,WAIT,NOHOLD
         LA    R5,HEAD3
         TPUT  (R5),78,EDIT,WAIT,NOHOLD
         TPUT  HEAD2,78,EDIT,WAIT,NOHOLD
         LA    R5,HEAD4
         TPUT  (R5),78,EDIT,WAIT,NOHOLD
         MVI   LINE,X'00'
         MVI   LINE+40,X'00'
         LA    R5,X'210'(R4)
         LA    R7,TIMEBLK
         SR    R10,R10
         BAL   R9,TIMELP1
         BAL   R9,BTODDIFF
         LTR   R10,R10
         BNZ   *+8
         LA    R10,1
         ST    R10,TIMESUM
         LA    R5,X'210'(R4)
         L     R7,X'204'(R4)
         MH    R7,=H'4'
         AR    R7,R5
         ST    R7,MAXADR
         LA    R10,TIMEBLK
ASVTLOOP EQU   *
         TM    0(R5),X'80'
         BO    NXTENTY
         L     R6,0(R5)
         BAL   R9,DAASCB
NXTENTY  EQU   *
         LA    R5,4(R5)
         LA    R10,16(R10)
         C     R5,MAXADR
         BL    ASVTLOOP
         CLI   LINE,X'00'
         BE    NOPUTL
         MVI   LINE+40,C' '
         LA    R3,LINE
         TPUT  (R3),78,EDIT,WAIT,NOHOLD
NOPUTL   EQU   *
IMS      B     OVER1
         LA    R3,17
         S     R3,LINECT
         CALL  SDIMSMON,(SDEXIT,(R3)),VL
OVER1    EQU   *
         L     R2,16
         SPACE
         TM    MODE,X'80'                XA?
         BNO   APVT1                     NO, 370, GET PVT
         L     R3,X'490'(R2)             RCE
         MVC   P1IN,X'44'(R3)            PAGE IN
         MVC   P1OUT,X'58'(R3)           PAGE OUT
         MVC   P1VIN,X'54'(R3)           VIO IN
         MVC   P1VOUT,X'64'(R3)          VIO OUT
         MVC   P1VREC,X'30'(R3)          VIO RECLAIM
         MVC   P1SIN,X'50'(R3)           SWAP IN
         MVC   P1SOUT,X'60'(R3)          SWAP OUT
         MVC   P1REC,X'34'(R3)           RECLAIM
         SPACE
         L     R2,X'25C'(R2)             RMCT
         L     R2,8(R2)                  ICT
         L     R3,X'3C'(R2)              I/O INTERRUPTS VIA TPI
         A     R3,X'40'(R2)              I/O INTERRUPTS VIA SLIH
         ST    R3,XAICT1                 SAVE # OF INTERRUPTS
         B     EECAT                     SKIP 370-MODE
         SPACE
APVT1    EQU   *
         L     R3,X'164'(R2)             PVT
         MVC   PAGEVAL1,X'F8'(R3)
         LR    R3,R2
         SH    R3,=H'4'
         CLC   0(3,R3),=CL3'038'
         BNE   EECAT
         L     R2,X'41C'(R2)    ADDRESS OF CST (CHANNEL SET TABLE)
         LA    R9,6
         LA    R14,CATS1
NXTSCT1  EQU   *
         TM    20(R2),X'80'  VALID DATA IN THIS CST?
         BNO   CAT1
         L     R3,0(R2)   ADRESS OF CAT
         LTR   R3,R3
         BNZ   SCAT00
CAT1     XC    0(256,R14),0(R14)
         B     INCRCST1
SCAT00   EQU   *
         MVC   0(256,R14),0(R3)
INCRCST1 EQU   *
         LA    R2,32(R2)
         LA    R14,256(R14)
         BCT   R9,NXTSCT1
EECAT    EQU   *
         LA    R3,WAIT1
         BAL   R9,GETWAIT
         STCK  TOD1
         CLI   AUTO,X'FF'
         BE    AUTO1
         TGET  BUFFER,4
         OC    BUFFER,=CL4' '
         CLC   BUFFER,=CL4'EXIT'
         BNE   TIMELOOP
         XRETURN 0,R
AUTO1    EQU   *
         TM    MODE,X'80'
         BO    AUTO1XA             MODE=XA
         STIMER WAIT,BINTVL=BINTVL
         B     TIMELOOP
         SPACE
AUTO1XA  EQU   *
         SPLEVEL SET=2
         STIMER WAIT,BINTVL=BINTVL
         SPLEVEL SET=1
         B     TIMELOOP
         SPACE
BTODDIFF EQU   *
         LM    R0,R1,TOD1
         LM    R14,R15,TOD2
         SLR   R15,R1
         LA    R1,1
         BC    3,*+6
         SLR   R14,R1
         SLR   R14,R0
         SRDA  R14,22
         ST    R15,TODDIFF
         BR    R9
IOSUM    EQU   *
         SR    R5,R5
         XC    FULLWD(4),FULLWD
         LA    R14,96
NEXTCH   EQU   *
         TM    0(R2),X'70'
         BNZ   INCRCH
         MVC   FULLWD+2(2),2(R2)     CHANNEL SIO COUNT
         L     R4,FULLWD
         TM    0(R1),X'70'
         BNZ   DIFFOK
         MVC   FULLWD+2(2),2(R1)
         L     R3,FULLWD
         CR    R3,R4
         BE    INCRCH
         BNH   SUB
         A     R4,=X'0000FFFF'
SUB      EQU   *
         SR    R4,R3
DIFFOK   EQU   *
         AR    R5,R4
INCRCH   EQU   *
         LA    R1,16(R1)
         LA    R2,16(R2)
         BCT   R14,NEXTCH
         BR    R9
TIMELP1  EQU   *
         LA    R8,ANZTBLK
         C     R8,X'204'(R4)     MAX NO OF ADDR SPACES
         BNH   *+8
         L     R8,X'204'(R4)
TIMELP1A EQU   *
         TM    0(R5),X'80'    ASCB ASSIGNED?
         BO    TIMELP1Z
         L     R6,0(R5)       ASCB
         LM    R14,R15,64(R6)     ELAPSES JOB STEP TIMING
         LM    R0,R1,X'C8'(R6)    ACCUMULATED SRB TIME
         SRDA  R14,22
         SLDA  R0,10
         TM    FUNCIND,DISPSUPP
         BO    TIMELP1S
         L     R1,0(R7)
         CR    R15,R1
         BL    TIMELP1D
         SR    R1,R15
         S     R0,4(R7)
         SR    R0,R1
         AR    R10,R0
         B     TIMELP1R
TIMELP1Z SR    R15,R15
TIMELP1D SR    R0,R0
TIMELP1R EQU   *
TIMELP1S STM   R15,R0,8(R7)
         LA    R7,16(R7)
         LA    R5,4(R5)
         BCT   R8,TIMELP1A
         BR    R9
DAASCB   EQU   *
         LA    R14,LINE
         CLI   LINE,X'00'
         BE    NOINCR
         LA    R14,LINE+40
NOINCR   EQU   *
         LM    R7,R8,X'AC'(R6)
         CLC   0(4,R8),=CL4'INIT'
         BNE   NOINIT
         LTR   R7,R7
         BZR   R9
         LA    R0,8
         SLR   R7,R0
         MVC   0(8,R14),8(R7)
         MVC   9(8,R14),64(R7)
         B     DPRTYEDT
NOINIT   EQU   *
         L     R7,X'38'(R6)
         CLI   0(R8),C'*'
         BER   R9
         CLI   X'1C'(R7),X'02'
         BNER  R9
         MVC   0(8,R14),16(R7)
         MVC   9(8,R14),8(R7)
DPRTYEDT EQU   *
         CLC   0(3,R14),=CL3'JES'
         BE    XX1
         CLC   0(3,R14),=CL3'JPF'
         BE    XX1
         CLC   0(6,R14),=CL6'XCNTRL'
         BE    XX1
         CLC   0(3,R14),=CL3'LLA'
         BE    XX1
         CLC   0(4,R14),=CL4'MEMO'
         BE    XX1
         CLC   0(8,R14),=CL8'Z223RMF'
         BNE   TSTMON
         B     XX1
TSTMON   EQU   *
         CLC   0(8,R14),=CL8'ADMPRINT'
         BE    XX1
         CLC   0(4,R14),=CL4'GDDM'
         BE    XX1
         CLC   0(8,R14),=CL8'SASWTR'
         BE    XX1
         CLC   0(8,R14),=CL8'Z227PHO'
         BE    XX1
         CLC   0(8,R14),=CL8'C206414P'
         BE    XX1
         CLC   0(4,R14),=CL4'X240'
         BE    XX1
         CLC   0(8,R14),=CL8'XH905HCF'
         BE    XX1
         CLC   0(8,R14),=CL8'XH608ITS'
         BE    XX1
         CLC   0(8,R14),=CL8'APWTR'
         BE    XX1
         CLC   0(8,R14),=CL8'Z223WTR'
         BE    XX1
         CLC   0(8,R14),=CL8'Z223QCM'
         BE    XX1
         CLC   0(3,R14),=CL3'VCI'
         BE    XX1
         CLC   0(6,R14),=CL6'Z240NP'
         BE    XX1
         CLC   0(7,R14),=CL7'Z240COD'
         BE    XX1
         CLC   0(8,R14),=CL8'XROSCOE'
         BE    XX1
         CLC   0(8,R14),=CL8'Z227PRT'
         BE    XX1
         CLC   0(8,R14),=CL8'Z227COM'
         BE    XX1
         CLC   0(8,R14),=CL8'Z221TWR'
         BE    XX1
         CLC   0(4,R14),=CL4'TCAS'
         BE    XX1
         CLC   0(4,R14),=CL4'TCAM'
         BE    XX1
         CLC   0(3,R14),=CL3'NET'
         BE    XX1
         CLC   0(8,R14),=CL8'TOP'
         BE    XX1
         CLC   0(3,R14),=CL3'IMS'
         BE    XX1
         CLC   0(4,R14),=CL4'NCCF'
         BE    XX1
         CLC   0(4,R14),=CL4'NPDA'
         BE    XX1
         CLC   0(3,R14),=CL3'RMF'
         BE    XX1
         CLC   0(4,R14),=CL4'STAM'
         BNE   XX2
         MVI   STAMSW,X'FF'
XX1      EQU   *
         MVI   0(R14),X'00'
         MVI   1(R14),C' '
         MVC   2(29,R14),1(R14)
         BR    R9
XX2      EQU   *
         SR    R0,R0
         L     R7,144(R6)    GET ADDR OF OUCB
         IC    R0,24(R7)   GET PERFORMANCE-GROUP
         L     R1,16
         TM    196(R1),X'80'
         BNO   NOSE2
         IC    R0,181(R7)     PERFORMANCE GROUP
         LTR   R0,R0
         BNZ   NOSE2
         IC    R0,183(R7)
NOSE2    EQU   *
         CVD   R0,DBLWD
         OI    DBLWD+7,X'0F'
         UNPK  18(3,R14),DBLWD
         LH    R0,X'98'(R6)    ALLOCATED PAGE FRAME COUNT
         SLA   R0,2
         CVD   R0,DBLWD
         MVC   MASKAREA,=X'402020202120'
         ED    MASKAREA,DBLWD+5
         MVC   22(4,R14),MASKAREA+2
         MVI   26(R14),C'K'
         TM    X'66'(R6),X'04'
         BZ    *+10
         MVC   22(5,R14),=C' SWAP'
         L     R1,12(R10)
         M     R0,=F'400'
         D     R0,TIMESUM
         LA    R1,2(R1)
         SRA   R1,2
         CVD   R1,DBLWD
         MVC   MASKAREA(4),=X'40202120'
         ED    MASKAREA(4),DBLWD+6
         MVC   28(2,R14),MASKAREA+2
         MVI   30(R14),C'%'
         L     R0,8(R10)
         SRDA  R0,22
         D     R0,=F'1000000'
         SR    R0,R0
         D     R0,=F'60'
         MH    R1,=H'100'
         AR    R1,R0
         CVD   R1,DBLWD
         C     R1,=F'9959'
         BH    NOVAL
         MVC   MASKAREA-1(7),=X'402020207D2120'
         ED    MASKAREA-1(7),DBLWD+5
         MVC   32(5,R14),MASKAREA+1
         B     OUTPUT
NOVAL    EQU   *
         MVC   32(5,R14),=C'**''**'
OUTPUT   EQU   *
         CLI   LINE+40,X'00'
         BER   R9
         MVI   LINE+38,C'×'
         CLC   LINECT,MAXLIN
         BNE   TPUT
         MVI   LINE+38,C'+'
TPUT     EQU   *
         LA    R14,LINE
         TPUT  (R14),78,EDIT,WAIT,NOHOLD
         MVI   LINE,X'40'
         MVC   LINE+1(77),LINE
         MVI   LINE,X'00'
         MVI   LINE+40,X'00'
         L     R14,LINECT
         LA    R14,1(R14)
         C     R14,MAXLIN
         BH    NOPUTL
         ST    R14,LINECT
         BR    R9
GETWAIT  EQU   *
         SETAUTH ON
         MODESET KEY=ZERO,MODE=SUP
         L     R15,16
         L     R15,768(R15)   A(LCCAVT)
         SR    R0,R0
         SR    R1,R1
         L     R15,0(R15)    A(CPU0 LCCA)
         LTR   R15,R15
         BZ    GETCPU2
         AL    R1,620(R15)
         BC    12,*+8
         AL    R0,=F'1'
         AL    R0,616(R15)
GETCPU2  EQU   *
         L     R15,16
         L     R15,768(R15)
         L     R15,8(R15)
         LTR   R15,R15
         BZ    ENDCPU
         AL    R1,620(R15)
         BC    12,*+8
         AL    R0,=F'1'
         AL    R0,616(R15)
ENDCPU   EQU   *
         STM   R0,R1,0(R3)
         MODESET KEY=NZERO,MODE=PROB
         SETAUTH OFF
         BR    R9
DSWPSVC  EQU   *
         SR    R1,R1
         LA    R0,14
         SVC   95
         BR    R14
*  TCBSVC1  EQU   *
*           OI    168(R1),X'80'      CHANGE TCBUSER
*           BR    R14
RETURN   EQU   *
         XRETURN 0,R
SDMNO    EQU   *
         CLI   DMNACT,C'O'
         BR    R4    DONT CHANGE DOMAINS - DELETE STMT IF YOU WILL
         BER   R4
         MVC   DMNTXT,DMNO1
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNO2
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNO3
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNO4
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNO5
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNO6
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNO7
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNO8
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNO9
         LA    R1,DMNCMD
         SVC   249
         MVI   DMNACT,C'O'
         BR    R4
SDMNA    EQU   *
         CLI   DMNACT,C'A'
         BR    R4      DONT CHANGE DOMAINS - DELETE STMTS IF YOU WILL
         BER   R4
         MVC   DMNTXT,DMNA1
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNA2
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNA3
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNA4
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNA5
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNA6
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNA7
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNA8
         LA    R1,DMNCMD
         SVC   249
         MVC   DMNTXT,DMNA9
         LA    R1,DMNCMD
         SVC   249
         MVI   DMNACT,C'A'
         BR    R4
         EJECT
*     KONSTANTEN
*
RESETCMD DS    0H
         DC    H'26,0'
         DC    CL22' E Z134MXX,PERFORM=255 '
HEAD1    DC    CL78'HH:MM:SS- RT=X.XX,IPS=XX,JOB=XX,CPU=XX%,PAGE=XXX/SE*
               C,IO=XXX/SEC,TS=XXX,UIC=XXX'
HEAD2    DC    CL78'JOBNAME  STEPNAME PRF  REAL CPU ACC-T × JOBNAME  STE
               EPNAME PRF  REAL CPU ACC-T'
WAIT1    DC    D'0'
WAIT2    DC    D'0'
WAIT     DC    F'0'
CPUNO    DC    F'0'
DBLWD    DC    D'0'
DOUBLEX  DC    D'0'
MAXLIN   DC    F'19'
TEST     DC    CL4'TEST'
BINTVL   DC    F'2900'
BUFF     DC    X'C1115D7E114040133C404000'
TOD1     DC    D'0'
TOD2     DC    D'0'
         SPACE
PAGEVAL1 DS    0XL32
P1IN     DC    XL4'00'             PAGE IN
P1OUT    DC    XL4'00'             PAGE OUT
P1VIN    DC    XL4'00'             VIO IN
P1VOUT   DC    XL4'00'             VIO OUT
P1VREC   DC    XL4'00'             # OF VIO RECLAIMS
P1SIN    DC    XL4'00'             SWAP IN
P1SOUT   DC    XL4'00'             SWAP OUT
P1REC    DC    XL4'00'             # OF PAGES RECL, EXCL SWAP RECLAIMS
         SPACE
PAGEVAL2 DS    0XL32
P2IN     DC    XL4'00'             PAGE IN
P2OUT    DC    XL4'00'             PAGE OUT
P2VIN    DC    XL4'00'             VIO IN
P2VOUT   DC    XL4'00'             VIO OUT
P2VREC   DC    XL4'00'             # OF VIO RECLAIMS
P2SIN    DC    XL4'00'             SWAP IN
P2SOUT   DC    XL4'00'             SWAP OUT
P2REC    DC    XL4'00'             # OF PAGES RECL, EXCL SWAP RECLAIMS
         SPACE
FULLWD   DC    F'0'
TODDIFF  DC    F'0'
FUNCIND  DC    X'00'
STAMSW   DC    X'00'
RMFSW    DC    X'00'
DISPSUPP EQU   X'01'
AUTO     DC    X'00'
BUFFER   DC    CL4' '
ONESECND DC    F'100'
ANZTBLK  EQU   512
TIMESUM  DS    F
LINECT   DC    F'0'
COUNT    DC    F'1'
DMNCMD   DC    H'40,0'
DMNTXT   DC    CL36' '
DMNACT   DC    C' '
MODE     DC    X'00'          SAVE MODE
MAXADR   DC    F'0'
         DS    X
MASKAREA DS    CL6
SDEXIT   DS    0H
         XSAVE R12,SDEXITSV,IMSMONEX
         LR    R2,R1
SDNXTLN  EQU   *
         L     R3,0(R2)
         TPUT  (R3),78
         TM    0(R2),128
         BO    SDXRET
         LA    R2,4(R2)
         B     SDNXTLN
SDXRET   XRETURN 0
         LTORG
TIMEBLK  DS    0CL16
TIMECPU1 DS    F
TIMESRB1 DS    F
TIMECPU2 DS    F
TIMEDIFF DS    F
         DS    (ANZTBLK-1)CL(L'TIMEBLK)
WORK     DSECT
SVA      DS    18F
TIME     DS    F
IPS      DS    CL2
LINE     DC    CL78' '
HEAD3    DC    78CL1'*'
HEAD4    DC    78CL1'-'
CLEAR    EQU   *
XAICT1   DS    F
XAICT2   DS    F
CATS1    DS    6XL256
CATS2    DS    6XL256
IMSAFF   DS    CL4
TSOAFF   DS    CL4
CLEARLL  EQU   *-CLEAR
WORKL    EQU   *-WORK
DMNDSCT  DSECT
DMNO1    DS    CL36
DMNO2    DS    CL36
DMNO3    DS    CL36
DMNO4    DS    CL36
DMNO5    DS    CL36
DMNO6    DS    CL36
DMNO7    DS    CL36
DMNO8    DS    CL36
DMNO9    DS    CL36
DMNA1    DS    CL36
DMNA2    DS    CL36
DMNA3    DS    CL36
DMNA4    DS    CL36
DMNA5    DS    CL36
DMNA6    DS    CL36
DMNA7    DS    CL36
DMNA8    DS    CL36
DMNA9    DS    CL36
TIPS     DS    CL36
IPSBCMD  DS    CL36
DMNCSCT  CSECT
DMNO1X   DC    CL36' SD 1,MIN=255,MAX=255,DOBJ=1'
DMNO2X   DC    CL36' SD 2,MIN=30,MAX=255,DOBJ=2'
DMNO3X   DC    CL36' SD 3,MIN=10,MAX=35,DOBJ=3'
DMNO4X   DC    CL36' SD 4,MIN=6,MAX=12,DOBJ=6'
DMNO5X   DC    CL36' SD 5,MIN=4,MAX=8,DOBJ=6'
DMNO6X   DC    CL36' SD 6,MIN=2,MAX=6,DOBJ=6'
DMNO7X   DC    CL36' SD 7,MIN=6,MAX=12,DOBJ=5'
DMNO8X   DC    CL36' SD 8,MIN=4,MAX=8,DOBJ=5'
DMNO9X   DC    CL36' SD 9,MIN=2,MAX=4,DOBJ=6'
DMNA1X   DC    CL36' SD 1,MIN=255,MAX=255,DOBJ=1'
DMNA2X   DC    CL36' SD 2,MIN=30,MAX=255,DOBJ=2'
DMNA3X   DC    CL36' SD 3,MIN=10,MAX=35,DOBJ=3'
DMNA4X   DC    CL36' SD 4,MIN=6,MAX=20,DOBJ=4'
DMNA5X   DC    CL36' SD 5,MIN=4,MAX=15,DOBJ=5'
DMNA6X   DC    CL36' SD 6,MIN=2,MAX=10,DOBJ=6'
DMNA7X   DC    CL36' SD 7,MIN=6,MAX=20,DOBJ=4'
DMNA8X   DC    CL36' SD 8,MIN=4,MAX=15,DOBJ=4'
DMNA9X   DC    CL36' SD 9,MIN=2,MAX=10,DOBJ=5'
TIPSCMD  DC    CL36' T IPS=O2'
IPSB     DC    CL36' T IPS=BX'
         END
//LKED.SYSLMOD DD DSN=Z223068.PGMS.LOAD(MONITOR),DISP=SHR
//    EXEC LKED
//LKED.SYSLMOD DD DSN=LU.LNKLIB,DISP=SHR
//LKED.SYSIN   DD *
  INCLUDE PGMS(MONITOR)
  INCLUDE SYSLMOD(MONITOR)
  ENTRY MONITOR
  NAME MONITOR(R)
//LKED.PGMS   DD DSN=Z223068.PGMS.LOAD,DISP=SHR
//*    SDIMSMON OBJECT-CODE AUF SD.HHHH.OBJECT(SD932MN)
//
