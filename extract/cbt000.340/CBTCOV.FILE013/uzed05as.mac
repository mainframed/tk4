         TITLE 'LOAD MODULE MAP MIT LE-DATUM'
PDV065A  CSECT
*              PROGRAMM-AUFRUF
*              ---------------
*                                           (,PARM=TT.MM.JJ)
*              //S010     EXEC  PGM=PDV065A (,PARM=XTT.MM.JJ)
*              //SYSPRINT DD    SYSOUT=C
*              //SYSUT1   DD    DSN=LIBRARY,DISP=SHR
*
*                               PARAMETER:  X = V - VOR DIESEM DATUM
*                               ----------  X = N - NACH DIESEM DATUM
*                                           X =   - GENAU AN DIESEM DAT
*
*
*              REGISTER-VERWENDUNG
*              -------------------
*
R0       EQU   0
R1       EQU   1
R2       EQU   2         BASIS
R3       EQU   3         PARM TEST / INDICATOR SAVE OF DIRECTORY ENTRY
R4       EQU   4         DIRECTORY RECORD 0 PARM TEST
R5       EQU   5         DCB-ADDR VON MEMBER
R6       EQU   6         MEMBER RECORD
R7       EQU   7         BAL-REG FUER TITEL
R8       EQU   8         LENGTH COUNT FOR USED BYTES OF DIRECTORY REC.
R9       EQU   9         ADDR OF NEXT ENTRY IN DIRECTORY REC.
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         PRINT NOGEN
         SAVE  (14,12),,*
         BALR  R2,0
         USING *,R2
         MVC   8(4,R13),=A(SAVE)
         ST    R13,SAVE+4
         CNOP  0,4
         BAL   R13,BEGIN
SAVE     DC    18F'0'
         LTORG
*
*
BEGIN    EQU   *
         L     R1,0(R1)            PARM POINTER
         LH    R3,0(R1)            LENGTH
         LA    R4,2(R1)            ADDR
         LTR   R3,R3
         BZ    JFCB
         SPACE 4
***********************************************************************
*                                                                     *
*  PARM- TEST,  DATE TO OS-DATE, OPEN, RDJFCB FOR PDS                 *
*                                                                     *
***********************************************************************
         SPACE 3
         CLI   0(R4),C'V'
         BNE   TESTN
         MVI   WAS,C'V'
         MVC   UTIT1+31(6),=C'BEFORE'
         MVC   UTIT1+38(8),1(R4)
         LA    R4,1(R4)
DATTEST  EQU   *
         MVC   TDAT+2(2),6(R4)     JAHR
         MVC   TDAT+4(2),3(R4)     MONAT
         MVC   TDAT+6(2),0(R4)     TAG
         CALL  UZED13A,(TDAT,FEHL) DATUM FORMELL TESTEN
         CLI   FEHL,C'R'
         BNE   WRONG
         CALL  UZED12A,(TDAT,ODAT) DATE TO OS-FORM YYDDD PACKED
         B     JFCB
*
TESTN    EQU   *
         CLI   0(R4),C'N'
         BNE   TESTA
         MVI   WAS,C'N'
        MVC   UTIT1+31(5),=C'AFTER'
         MVC   UTIT1+37(8),1(R4)
         LA    R4,1(R4)
         B     DATTEST
*
TESTA    EQU   *
         MVI   WAS,C'A'
         MVC   UTIT1+31(2),=C'AT'
         MVC   UTIT1+34(8),0(R4)
         B     DATTEST
*
*
JFCB     EQU   *
         XR    R15,R15
         RDJFCB (DIRECT)
         CH    R15,=H'0'
         BNE   NODD                SYSUT1-DD CARD NOT FOUND
         MVC   TITEL+38(30),JFCBAREA    DSNAME OF LIBRARY TO TITEL
         CALL  UZED10A,(DATUM)
         MVC   TITEL+76(8),DATUM
         OPEN  (DIRECT,,MEMBER,,SYSPRINT,(OUTPUT))
         GETMAIN R,LV=19200
         ST    R1,POOLADR
         BAL   R7,TITELR
         SPACE 4
***********************************************************************
*                                                                     *
*  READ DIRECTORY OF PDS DESCRIBED ON THE 'SYSUT1' DD-STATEMENT       *
*                                                                     *
***********************************************************************
         SPACE 3
READ     EQU   *
         READ  DIRDECB,SF,DIRECT,DIRAREA,'S'
         CHECK DIRDECB
         LA    R4,DIRAREA+8        KEY UEBERSPRINGEN
         LH    R8,0(R4)            TOTAL USED BYTES OF DIR.BLOCK
         SH    R8,=H'2'            MINUS LENGTH ITSELF
         LA    R4,2(R4)            FIRST MEMBERNAME
*
NEXT     EQU   *
         MVI   SWALIAS,C' '
         MVI   SWPUT,C' '
         CLC   0(8,R4),=8X'FF'     LAST ENTRY?
         BE    ENDE
         IC    R3,11(R4)           INDICATORS
         N     R3,=X'0000001F'     CLEAR EXCEPT LENGTH OF USER DATA
         SLA   R3,1                MAL 2 = ANZAHL BYTES
         LR    R9,R4
         LA    R9,12(R9)           JUMP OVER FIX 12 BYTES
        LA    R9,0(R3,R9)         NEXT ENTRY
         TM    11(R4),X'80'        ALIAS ENTRY?
         BNO   RMBR
         MVI   SWALIAS,C'A'
         B     WRITE
*
GOON     EQU   *
         SH    R8,=H'12'
         BZ    READ
         SR    R8,R3
         BZ    READ
         LR    R4,R9
         B     NEXT
         SPACE 4
***********************************************************************
*                                                                     *
*  READ MEMBER OF PDS DESCRIBED ON THE 'SYSUT1' DD-STATEMENT          *
*                                                                     *
***********************************************************************
         SPACE 3
RMBR     EQU   *
         LA    R5,MEMBER
         MVI   TYPE,X'00'
         FIND  (5),(4),D
*                                  R4 - ADDR MEMBERNAME IM DIR.BLOCK
*                                  R5 - ADDR DCB
*
RESD     EQU   *
         L     R6,POOLADR          GET SUBPOOL-ADDR
         READ  DECB,SF,MEMBER,(6),19100
         CHECK DECB
         CLI   TYPE,X'80'          WAS LAST REC. LAST OF MODULE?
         BE    TSTPUT
         CLI   0(R6),X'80'         IDR-ENTRY?
         BNE   RESD
         MVC   TYPE(1),2(R6)       SAVE SUBTYPE WITH END-INDICATOR
         NI    TYPE,X'80'          CLEAR ALL EXCEPT END-INDICATOR
         NI    2(R6),X'0F'         CLEAR ALL EXCEPT SUBTYPE
         CLI   2(R6),X'02'         LKED-ENTRY?
         BNE   RESD
*
         TM    17(R6),X'0F'        GUELTIGES VORZEICHEN IM
         BO    TSTA0               LINKAGE EDITOR DATUM?
         TM    17(R6),X'0C'
         BO    TSTA0
         MVC   DATUM,=8C'*'
         MVI   SWPUT,C'1'
         B     WRITE
*
TSTA0    EQU   *
         CLI   WAS,C' '            PARM DA?
         BE    TSTA1
         CLI   WAS,C'V'
         BNE   TSTA2
         CP    15(3,R6),ODAT       LE-DATE MUSS KLEINER SEIN ALS PARM
         BNL   GOON
TSTA1    EQU   *
         MVI   SWPUT,C'1'
         MVC   PDAT+1(3),15(R6)
         CALL  UZED11A,(PDAT,ADAT) LE-DATE TO CHAR TT.MM.JJ
         B     WRITE
TSTA2    EQU   *
         CLI   WAS,C'N'
         BNE   TSTA3
         CP    15(3,R6),ODAT       LE-DATE MUSS GROESSER SEIN ALS PARM
         BNH   GOON
         B     TSTA1
TSTA3    EQU   *
         CP    15(3,R6),ODAT       LE-DATE MUSS GELICH SEIN WIE PARM
         BNE   GOON
         B     TSTA1
*
TSTPUT   EQU   *
         CLI   SWPUT,C' '
         BNE   GOON
         B     WRITE
*
*
WRITE    EQU   *
         CP    ZZEL,=P'60'
         BNH   WRITE1
         BAL   R7,TITELR
WRITE1   EQU   *
         MVI   CTL,X'09'           SPACE 1 AFTER'
         MVC   MODNAME,0(R4)
         CLI   SWALIAS,C' '
         BE    WRITE2
         MVC   ZEILE+36(13),=C'*** ALIAS ***'
         B     WRITE4
WRITE2   CLI   SWPUT,C' '
         BNE   WRITE3
         MVC   ZEILE+36(41),=C'MEMBER CONTAINS NO IDR-ENTRY WITH LE-DAT*
               A'
         B     WRITE4
WRITE3   MVC   LEDATE,DATUM
WRITE4   PUT   SYSPRINT,ZEILE
         MVI   SWPUT,C'1'
         AP    ZZEL,=P'1'
         MVI   ZEILE,C' '          CLEAR ZEILE
         MVC   ZEILE+1(96),ZEILE
         B     GOON
*
*
TITELR   EQU   *
         MVI   CTL,X'8B'           CHANNEL 1 DIRECT
         PUT   SYSPRINT,ZEILE
         ZAP   ZZEL,=P'0'
         AP    SZEL,=P'1'
         MVI   ZEILE,C' '
         MVC   ZEILE+1(96),ZEILE
         MVC   TITEL+94(3),=X'202120'
         ED    TITEL+94(3),SZEL
         PUT   SYSPRINT,TITEL
         AP    ZZEL,=P'3'
         CLI   WAS,C' '
         BE    TITELR1
         PUT   SYSPRINT,UTIT1
        AP    ZZEL,=P'2'
TITELR1  EQU   *
         PUT   SYSPRINT,UTIT2
         AP    ZZEL,=P'1'
         BR    R7
*
*
*
WRONG    EQU   *
         WTO   'PARM FALSCH,GANZE LIBRARY WIRD VERARBEITET',ROUTCDE=11
         MVI   WAS,C' '
         MVC   STORE15,=F'4'
        B     JFCB
*
NODD     EQU   *
         WTO   'SYSUT1-KARTE FEHLT',ROUTCDE=11
         ABEND 1
*
*
*
ENDE     EQU   *
        CLOSE (DIRECT,,MEMBER,,SYSPRINT)
         FREEMAIN R,LV=19200,A=POOLADR
         XR    R15,R15
         L     R15,STORE15
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
         EJECT
***********************************************************************
*                                                                     *
*  DEFINITIONEN                                                       *
*                                                                     *
***********************************************************************
         SPACE 3
DIRECT   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(R),EODAD=ENDE,KEYLEN=8,   *
               LRECL=256,BLKSIZE=256,RECFM=F,EXLST=LIST1
*
MEMBER   DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=(R),EODAD=TSTPUT,          *
               RECFM=U,BLKSIZE=13030
*
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),                    *
               LRECL=97,BLKSIZE=1940,RECFM=FBM
*
         DS    0D
LIST1    DC    AL1(128+7),AL3(JFCBAREA)
         DS    0F
JFCBAREA DS    CL176
DIRAREA  DS    CL264
STORE15  DC    F'0'
DATUM   DS    CL8
ADAT     DC    A(DATUM)
PDAT     DC    PL4'0'
TDAT     DC    CL8'19      '
ODAT     DS    CL3
*
TITEL    DS    0CL97
         DC    X'11'               SPACE 2 AFTER
         DC    CL96'PDV065A:  LOAD MODULE MAP OF LIBRARY               *
                                  DATE              PAGE    '
*
UTIT1    DS    0CL97
         DC    X'19'               SPACE 3 AFTER
         DC    CL96'          ONLY MODULES LINKED'
*
UTIT2    DS    0CL97
         DC    X'11'
         DC    CL96'          MODULE        LE-DATE'
*
ZEILE    DS    0CL97
CTL      DC    X'09'               SPACE 1 AFTER
         DS    CL10
MODNAME  DS    CL8
         DS    CL6
LEDATE   DS    CL8
         DS    CL64
*
SZEL     DC    PL2'0'
ZZEL     DC    PL2'70'
*
FEHL     DC    CL1' '
WAS      DC    CL1' '
TYPE     DC    CL1' '
SWALIAS  DC    CL1' '
SWPUT    DC    CL1' '
         DS    0D
POOLADR  DC    F'0'
         LTORG
         END   PDV065A
