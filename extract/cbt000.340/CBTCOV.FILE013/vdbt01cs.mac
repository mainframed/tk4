IEFUJI   CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3             BASISREG. FUER IEFUJI
R4       EQU   4             A(COMMON EXIT PARAMETER AREA)
R5       EQU   5             A(PROGRAMMER'S NAME)
R6       EQU   6             A(JOB ACCOUNTING FIELDS)
R7       EQU   7
R8       EQU   8
R9       EQU   9             BASISREG. FUER USERAREA
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13            A(SAVEAREA)
R14      EQU   14
R15      EQU   15
*
**********************************************************************
*        REENTRANT LINKAGE CONVENTIONS
*
         SAVE  (14,12),,*    IN SAVE AREA DES RUFENDEN PROGRAMMES
         LR    R3,R15        BASISREGISTER
         USING IEFUJI,R3
         LR    R11,R1        A(PARAMETER-LISTE) RETTEN
         GETMAIN R,LV=GLEN,SP=255      SAVE AREA U. VERAE. FELDER
         LTR   R15,R15       GETMAIN OK ?
         BNZ   NOCORE        NEIN
         XC    0(GLEN,R1),0(R1) GETMAIN FELD LOESCHEN
         ST    R1,8(R13)     A(NEUE SAVE AREA) NACH ALTE SAVE AREA
         ST    R13,4(R1)     A(ALTE SAVE AREA) NACH NEUE SAVE AREA
         LR    R13,R1        A(NEUE SAVE AREA) IN R13
         USING GMAIN,R13     BASISREG. FUER SAVE AREA U. VERAE. FELDER
         LR    R1,R11        A(PARAMETER-LISTE) ZURUECKLADEN
*
***********************************************************************
*        PARAMETER-ADRESSEN LADEN
*
         L     R4,0(R1)      ADDR. COMMON PARAMETER AEREA
         L     R5,4(R1)      ADDR. PROGRAMMERS NAME
         L     R6,12(R1)     ADDR. JOB ACCOUNTING FIELDS
*
***********************************************************************
*        USERAREA ANLEGEN
*
         GETMAIN R,LV=LUSER,SP=255   FUER USER AREA
         LTR   R15,R15            (FREEMAIN ERFOLGT IN IEFACTRT)
         BNZ   NOCORE2
         LR    R9,R1              BASISREG.
         USING USERAREA,R9        FUER USERAREA
*
         XC    USERAREA(LUSER),USERAREA  USERAREA AUF X'00' LOESCHEN
         MVC   RESERV2(1),25(R4)  JOBCLASS IN USERAREA UEBERTRAGEN
         ST    R1,32(R4)          A(USERAREA) NACH JOBMGT-RECORD
         XC    32(1,R4),32(R4)    HIGH-ORDER-BYTE LOESCHEN
         CLI   24(R4),C'0'        MSGLEVEL=(.,0) ?
         BNE   MVCACCP            NEIN
         OI    32(R4),X'80'       JA, INDIKATOR: KEINE STEPINFO'S
MVCACCP  MVC   ACCPAR(28),0(R6)   ACC.PAR.
         MVC   20(8,R4),0(R5)     PROGRAMMER'S NAME IN JOBMGT-RECORD
         MVC   PGMRNAME(20),0(R5) PROGRAMMER'S NAME IN USERAREA
         L     R11,16             A(CVT)
         L     R11,0(R11)         TCB POINTER LIST
         L     R11,12(R11)        A(ASCB)
         ST    R11,ASCB           NACH USERAREA
         STIDP CPUID
         MVC   CPUIDW,CPUID
         NC    CPUIDW(6),CPUMASK  SERIAL NR. LOESCHEN
         LA    R2,CPUCOUNT        ANZAHL CPUID'S IN TABELLE
         LA    R10,CPUTAB-12      A(1.CPUID)-12
IDTEST   LA    R10,12(R10)        A(CPUID)
         CLC   0(6,R10),CPUIDW    CPUID GEFUNDEN ?
         BE    FACT               JA
         BCT   R2,IDTEST          NEIN
         WTO   '--- IEFUJI --- CPUID NICHT IN TABELLE'
FACT     MVC   FACTOR(4),8(R10)
         LR    R10,R1             R1 RETTEN
*
         TIME  BIN
         ST    R0,JOBINITT       INIT-ZEIT FUER $DA
         TM    29(R4),X'01'      TSO-FOREGROUND ?
         BO    RETURN            JA --> KEIN SMFSATZ
*
***********************************************************************
*        SMFSATZ TYPE 129 ERZEUGEN
*
         ST    R0,TIME
         ST    R1,DATE
         MVC   HDR,HDR129       MOVE HEADER IN SMF RECORD
         MVC   RELMOD(4),16(R4) MOVE RELEASE+MODEL
         MVC   CEPA(36),0(R4)   MOVE COMMON EXIT PARAMETER AREA
         MVC   PRNAME(20),0(R5) MOVE PROGRAMMER'S NAME
         MVC   ACCFLDS(28),0(R6) MOVE ACC.FIELDS
*
         SMFWTM SMF129            VERLANGT AUTORISIERUNG
*
***********************************************************************
*
RETURN   L     R5,RC         RETURN CODE
         LR    R1,R13        A(GETMAIN FELD)
         L     R13,4(R13)    A(ALTE SAVE AREA)
         FREEMAIN R,LV=GLEN,A=(R1),SP=255
         LR    R15,R5        RETURN CODE
STORERC  ST    R15,16(R13)   RETURN CODE NACH SAVEAREA D.RUF.PGM.
         RETURN (14,12)
*
***********************************************************************
*
NOCORE   WTO   '--- IEFUJI --- GETMAIN FUER SAVEAREA IN SUBPOOL 255 NIC*
               HT ERFOLGREICH',ROUTCDE=(11,2)
         LA    R15,4             RETURN CODE FUER CANCEL
         B     STORERC
*
NOCORE2  WTO   '--- IEFUJI --- GETMAIN FUER USERAREA IN SUBPOOL 255 NIC*
               HT ERFOLGREICH',ROUTCDE=(11,2)
         MVC   RC(4),=F'4'        RETURN CODE FUER CANCEL
         B     RETURN
*
***********************************************************************
         EJECT
***********************************************************************
*        FELDER DIE NICHT VERAENDERT WERDEN
*
*
HDR129   DC    X'006600000081'
*
CPUTAB   DS    0F          ---> TABELLE AUFSTEIGEND NACH FAKTOR
*        VERSION CODE FROM CPUID, X'000000', MODEL, H'0', FACTOR
         DC    X'020000000580',H'0',E'1.0'   AMDAHL 5860
         DC    X'080000000470',H'0',E'1.0'   AMDAHL V8
         DC    X'030000003081',H'0',E'1.0'   IBM 3081
*        DC    X'000000003033',H'0',E'1.15'  IBM 3033U
*        DC    X'010000003033',H'0',E'1.32'  IBM 3033N (1.15*1.15)
*        DC    X'000000000158',H'0',E'7.475' IBM 158   (1.15*6.5)
*        DC    X'000000000145',H'0',E'23.92' IBM 145   (1.15*6.5*3.2)
         DC    X'000000000000',H'0',E'1.0'   FALLS CPUID NICHT GEFUNDEN
CPUCOUNT EQU   (*-CPUTAB)/12
CPUMASK  DC    X'FF000000FFFF'
*
         LTORG
***********************************************************************
*        FELDER DIE VERAENDERT WERDEN
*
GMAIN    DSECT
SAVEAREA DS    18F
*
CPUIDW   DS    D
RC       DS    F                RETURN-CODE
*
SMFDSECT DS    1H            ALIGN TIME AND DATE ON FULLWORD BOUNDARY
SMF129   EQU   *
HDR      DS    CL6           SMF REC.LENGTH AND TYPE (X'006600000081')
TIME     DS    1F
DATE     DS    1F
RELMOD   DS    CL4           RELEASE/MODEL
CEPA     DS    CL36          COMMON EXIT PARAMETER AREA
PRNAME   DS    CL20          PROGRAMMER'S NAME
ACCFLDS  DS    CL28          ACC,FIELDS
*
GLEN     EQU   *-GMAIN
*
***********************************************************************
         EJECT
**********************************************************************
*
         COPY  USERAREA
         END
