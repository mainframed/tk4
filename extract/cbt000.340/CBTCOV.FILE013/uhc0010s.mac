 ./       NUMBER SEQ1=100,INCR=100
MFP      TITLE 'MFP MULTI-FUNKTION-PROGRAM       PROGRAMM-INIT'
MFP      CSECT
         ASSHIST MVS*OHNE.PRINT1X
*              REG  2 BASIS.REGISTER
*              REG  3 BASIS.REGISTER
*              REG  4 ADDR.WTOR-ECB  IN PARAMETER-AREA
*              REG  5 ADDR.WTOR-AREA IN PARAMETER-AREA
*              REG  8 WORK-REGISTER / HDRUCK FUER EXCP-CCW
*                                     IHADCB
*              REG  9 WORK-REGISTER / HLESEN FUER NUM. PRUEFEN
*              REG 10 WORK-REGISTER / HDRUCK FUER D1BDER
*                                   / ADDR. DES ACCOUNTING-SATZ
*              REG 11 WORK-REGISTER
*              REG 12
*              REG 13 ADDR. SAVEAREA
*              REG 14 LINKAGE
*              REG 15 LINKAGE
ANFANG   SAVE  (14,12)
         BALR  3,0
         USING  ANF1,3,12,11
ANF1     L     12,ANF2
         L     11,ANF3
         USING IHADCB,8
         REG
         LR    6,13                    UMLADEN HSA-ADDR.
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         ATTACH EP=IGC00240,STAI=(EXIT),GSPV=127,PARAM=(PARM240)
         EXTRACT ANSWER,FIELDS=COMM
         L     4,ANSWER
         LA    4,4(4)
         QEDIT ORIGIN=(4)
         EXTRACT ANSWER,FIELDS=COMM
         L     4,ANSWER
         LA    4,4(4)
         QEDIT ORIGIN=(4),CIBCTR=5
         L     4,ANSWER
         L     15,0(4)
         LA    14,24
         SR    15,14
WAITX    MVC   CPOEVENT+12(4),0(4)
         OI    SCHALTER,B'00001000'
         B     HAPRO
HSPOOLEX L     13,SAVEAREA+4
         RETURN (14,12)
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       STEUERUNGS-ROUTINE'
HAPRO    L     4,CPOEVENT+12
         TM    0(4),X'40'
         BO    SUBCON
         TM    ECBDRU1,X'40'           KANAL-PGM FUER DRUCKER BEENDET?
         BO    SUBD1                   JA, BRANCH ZUR DRUCK-ROUTINE
         TM    ECBDRU2,X'40'           KANAL-PGM FUER DRUCKER BEENDET?
         BO    SUBD2                   JA, BRANCH ZUR DRUCK-ROUTINE
         TM    ECBLES1,X'40'           KANAL-PGM FUER LESER BEENDET?
         BO    SUBA1                   JA, BRANCH ZUR LESE-ROUTINE
         TM    ECBSTA1,X'40'           KANAL-PGM FUER STANZER BEENDET?
         BO    SUBS1                   JA, BRANCH ZUR STANZ-ROUTINE
         TM    TIMERECB,X'40'          TIMER-INTERVAL ABGELAUFEN?
         BZ    WAIT                    NEIN, BRANCH ZUR MULTIPLE-WAIT
         XC    TIMERECB(4),TIMERECB
         XC    WTOR1ECB(4),WTOR1ECB    LEOSCHEN GEFUNDEN-MESSAGE ECB
         LA    1,WTOR1
         SVC   35                      WTOR GEFUNDEN-MESSAGE
         WAIT  ECB=WTOR1ECB
         CLI   CONBER,C'U'
         BNE   HAPRO
         B     CANF
WAIT     WAIT  ECBLIST=CPOEVENT        MULTIPLE-WAIT
         B     HAPRO
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       OPERATOR-CONTROL'
SUBCON   EXTRACT ANSWER,FIELDS=COMM
         L     6,ANSWER
         L     6,4(6)
CONT     EQU   *
         CLI   4(6),X'40'
         BE    HSPOOLEX
         MVC   MODULENM,=CL8' '
         LH    4,14(6)
         LTR   4,4
         BNP   WARTE
         LH    5,14(6)
         BCTR  5,0
         STC   5,MFPROUT+11
         LA    1,16(4,6)
         BCTR  4,0
         EX    4,TRTEX1
         LA    5,16(6)
         SR    1,5
         LTR   1,1
         BNP   WARTE
         BCTR  1,0
         CLI   17(6),C'/'
         BE    MFPROUT
         EX    1,MVCEX1
ATTACH   EQU   *
         GETMAIN R,LV=128,SP=127
         MVI   0(1),X'40'
         MVC   1(127,1),0(1)
         XC    0(4,1),0(1)
         MVC   3(1,1),12(6)
         EX    4,MVCEX2
MODTEST  EQU   *
         LA    4,MODTAB
LOOPY    CLC   0(4,4),=X'FFFFFFFF'
         BE    WTOX
         CLC   0(8,4),MODULENM     IS MODULE AVAILABLE
         BE    JOIN                YES
         LA    4,8(4)              INCREASE TABLE AND
         B     LOOPY               CONTINUE SEARCH
MODTAB   DS    0D
         DC    CL8'SHOW    '
         DC    CL8'SPACE   '
         DC    CL8'SCR     '
         DC    CL8'RENAM   '
         DC    CL8'HDRUCK  '
         DC    CL8'DATASET '
         DC    X'FFFFFFFF'
WTOX     WTO   'MFP FUNCTION NOT AVAILABLE'
         B     WARTE
JOIN     EQU   *
         ATTACH EPLOC=MODULENM,STAI=(EXIT),GSPV=127
WARTE    EQU   *
         L     4,ANSWER
         LA    4,4(4)
         L     5,0(4)
         QEDIT ORIGIN=(4),BLOCK=(5)
         EXTRACT ANSWER,FIELDS=COMM
         L     4,ANSWER
         L     6,4(4)
         LA    6,0(6)
         LTR   6,6
         BZ    WAITX
         B     CONT
MVCEX1   MVC   MODULENM(0),16(6)
MVCEX2   MVC   4(0,1),16(6)
TRTEX1   TRT   16(0,6),TRTAB
ANSWER   DS    F
MODULENM DS    CL8
TRTAB    DC    256XL1'00'
         ORG   TRTAB+64
         DC    X'40'
         ORG   TRTAB+107
         DC    X'6B'
         ORG
MFPROUT  MVI   CONBER+2,X'40'
         MVC   CONBER+3(69),CONBER+2
         MVC   CONBER+2(70),16(6)      MOVE EINGABE IN ARBEITS-BEREICH
         CLI   AKTION,C'S'             'START' EINE FUNKTION?
         BE    CANF                    JA, BRANCH ZUR START-ROUTINE
         CLI   AKTION,C'P'             'PAUSE' EINE FUNKTION?
         BE    CEND                    JA, BRANCH ZUR STOP-ROUTINE
SUBCON0  WTO   'MFP EINGABE-FEHLER',ROUTCDE=13
         B     SUBCON2
SUBCON1  TM    SCHALTER,B'00110101'    SIND NOCH FUNKTIONEN AKTIV?
         BZ    HSPOOLEX
         B     SUBCON0
*
SUBCON2  B     WARTE
         EJECT
CANF     LINK  EP=IGC01240,PARAM=(PARM240)
         B     SWITCHA(15)
SWITCHA  B     OPA1
         B     OPD1
         B     OPS1
         B     OPD2
         B     SUBCON0
         EJECT
CEND     CLC   FUNKTION(6),=C'HLESEN'  ENDE  EINER LESE-FUNKTION ?
         BE    CONA1E
         CLC   FUNKTION(3),LISTE00W+16 ENDE EINER DRUCK-FUNKTION ?
         BE    COND1E
         CLC   FUNKTION(3),LIST100W+16 ENDE EINER DRUCK-FUNKTION ?
         BE    COND2E
         CLC   FUNKTION(6),=C'HSTANZ'  ENDE  EINER STANZ-FUNKTION ?
         BE    CONS1E
         B     SUBCON2
CONA1E   TM    SCHALTER,B'00000001'
         BZ    SUBCON2
         B     EOFA1
CONS1E   TM    SCHALTER,B'00000100'
         BZ    SUBCON2
         B     EOFS1
COND1E   TM    SCHALTER,B'00010000'
         BZ    SUBCON2
         L     10,D1BDBER
         MVC   COND1EW+19(27),0(10)
COND1EW  WTO   'MFP SUCHNR=                           ',ROUTCDE=13
         MVI   SCRATCH+1,X'F0'
         B     EOFD1
COND2E   TM    SCHALTER,B'00100000'
         BZ    SUBCON2
         L     10,D2BDBER
         MVC   COND2EW+19(27),0(10)
COND2EW  WTO   'MFP SUCHNR=                           ',ROUTCDE=13
         MVI   SCRATCH1+1,X'F0'
         B     EOFD2
CEOF     LINK  EP=IGC02240,PARAM=(PARM240)
         B     SWITCHE(15)
SWITCHE  B     SUBCON2
         B     SUBCON0
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       EXIT - ROUTINEN'
         USING *,15
NOBUFFER LR    8,1
         MVI   DCBBUFNO,X'00'          RESET BUFNO
         RETURN
         DROP  15
         CNOP  0,4
DCBEXIT  DC    X'05'
         DC    AL3(NOBUFFER)
         DC    X'87'
         DC    AL3(INFMJFCB)
DCBSYNEX LR    8,1                     LOAD IHADCB-REG WITH DCB-ADDR.
         SLL   8,8
         SRL   8,8
         C     8,=A(D1BD)              FEHLERHAFTER DCB = DRUCKDATEI
*        BNE   DCBSYNE1                NO, BRANCH
         B     DCBSYNE1                NO, BRANCH
         L     10,D1BDBER              LADE ADRESSE DRUCKBEREICH UND
         L     9,DCBIOBA               JA, LADE IOB-ADDR.
         TM    21(9),B'01000000'       TEST AUF INCORRECT-LENGTH
         BZ    DCBSYNE1                NO, BRANCH
         LH    9,22(0,9)               JA, LADE RESIDUAL-COUNT
         LA    10,1599(0,10)           ERHOEHE AUF BEREICHS-ENDE
         MVI   0(10),X'03'             MOVE NOP-COMMAND
         BCT   10,*+4                  VERMINDERE ADRESSE
         BCT   9,*-8                   DRUCKBEREICH AUFGEFUELLT
         RETURN
DCBSYNE1 EQU   *
         MVI   SVC68+12,X'02'          MOVE ACCESS-METHOD = BSAM
         TM    DCBMACRF,X'80'          TEST MACRF=EXCP ?
         BZ    *+12                    NO  ,BRANCH
         MVI   SVC68+12,X'00'          YES, MOVE ACCESS-METHOD = EXCP +
         L     1,DCBIOBAD                   LOAD R1 WITH IOBAD
         L     8,DCBEODAD              LOAD R8 WITH EOFAD
SVC68    SYNADAF ACSMETH=BSAM          EXECUTE SVC 68
         MVC   SVC68W+8(50),68(1)
SVC68W   WTO    '                                                  ',  =
               ROUTCDE=13
         SYNADRLS
         BR    8                       BRANCH ZUR EODAD-ADDRESSE
WTOSPOOL WTO   'MFP HXXXXX ENDED,YYYYYY=ZZZZZZZ',ROUTCDE=13
         BR    10
EXIT     XSAVE  15,,EXIT,WORKL
         USING ARBEITSB,R13
         LA    R15,12
         CR    R0,R15
         BNE   CROK
         LR    R3,R1
         B     CRNOK
CROK     EQU   *
         L     R3,4(R1)
CRNOK    EQU   *
         SLL   R3,8
         SRL   R3,8
         LR    R4,R3
         SLL   R4,20
         SRL   R4,20
         CR    R4,R3
         BNE   SYS
         CVD   R3,DBLWD
         UNPK  CC,DBLWD
         OI    CC+2,X'F0'
         B     DETACH
SYS      EQU   *
         SRL   R3,12
         LTR   9,9
         BZ    DCBSYNE1
         ST    R3,DBLWD
         UNPK  CC(4),DBLWD(5)
         TR    CC,TBL-240
DETACH   EQU   *
         MVC   WTO,MESS7
         MVC   WTO+39(3),CC
         CR    R0,R15
         BNE   MOVNAM
         MVC   WTO+17(8),=CL8'NO NAME'
         B     WTO1XIT
MOVNAM   EQU   *
         L     R1,88(R1)
         L     R1,12(R1)
         MVC   WTO+17(8),8(R1)
WTO1XIT  EQU   *
         WTO   MF=(E,WTO)
         XRETURN 0,R
MESS7    WTO   'MFP     TASK 12345678 ABENDED. CC= XXX',MSGTYP=Y,      =
               MCSFLAG=(REG0),MF=L
TBL      DC    C'0123456789ABCDEF'
         DROP  15
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       ACCOUNTING-ROUTINE'
ACCOUNT  ST    14,SAVE
         TIME  TU
         ST    0,8(0,10)
         ST    1,0(0,10)
         L     2,16               LADE ADDR.OF CVT
         L     2,196(2)           LADE ADDR.OF SMCA
         MVC   SMFMFP+14(4),16(2) MOVE SYSID AND MODEL-ID
         TIME  BIN
         ST    0,SMFMFP+6         SPEICHERN ZEIT
         ST    1,SMFMFP+10        SPEICHERN DATUM
         MVC   FSATZ(28),0(10)    MOVE FUNKTIONS-SATZ
         CLC   16(8,10),BLANK44
         BNE   ACCOUNT0
         MVC   ACCWTOR+16(3),28(10)
         NI    ECBCONSL,X'00'
ACCWTOR  WTOR  'XXX ACCOUNTING-INFO FEHLT',FSATZ+16,8,ECBCONSL,        =
               ROUTCDE=13
         WAIT  ECB=ECBCONSL
ACCOUNT0 EQU   *
         SMFWTM SMFMFP
         LTR   15,15
         BZ    ACCOUNT1
         WTO   'MFP FEHLER BEI SMFWTM',ROUTCDE=13
ACCOUNT1 EQU   *
         SP    12(4,10),12(4,10)
         L     14,SAVE
         BR    14
         CNOP  2,4
SMFMFP   DC    H'102'             SATZLAENGE
         DC    H'0'
         DC    H'129'             RESERVED AND RECORD-TYPE
         DC    F'0'               ZEIT
         DC    PL4'0'             DATUM
         DC    CL2' '             SYSID
         DC    CL2' '             MODEL-ID
FSATZ    DC    PL4'0'             DATUM FUNKTIONS-START
         DC    F'0'               STARTZEIT DER FUNKTION
         DC    F'0'               ENDZEIT DER FUNKTION
         DC    PL4'0'             ANZAHL DER VERARBEITETEN SAETZE
         DC    CL8' '             PGM-NR.DES ERSTELLUNGS-PGM
         DC    CL2' '             FUNKTIONS-KZ
         DC    CL6' '             EIN-/AUSGABE-EINHEIT
         DC    CL44' '              DSNAME
         DC    CL6' '             VOLSER
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       COMMUNICATION-AREA'
         CNOP  0,8
PARM240  EQU   *             PARAMETER-LISTE AN IGC00240
SCHALTER DC    B'00000000'   BIT 0 = TIMER-EXIT
*                            BIT 1 = SUCHDATEI
*                            BIT 2 = HINITT
*                            BIT 3 = HDRUCK
*                            BIT 4 = WTOR PENDING
*                            BIT 5 = HSTANZ
*                            BIT 6 = DISPLAY ERROR-MESSAGE
*                            BIT 7 = HLESEN
CONBER   DS    0CL30         CONSOL-EINGABE-BEREICH
         DC    CL2' '
AKTION   DC    C' '
         DC    C' '
FUNKTION DC    CL8' '
OPTION1  EQU   FUNKTION+6
OPTION2  EQU   FUNKTION+7
         DC    C' '
INPUT    DC    CL3' '
         DC    C'-'
OUTPUT   DC    CL3' '
         DC    C','
VOLSER   DC    CL6' '
SEARCHKZ DC    C','
SEARCHDS DC    CL44' '
HLESEN   DC    CL82' '       FUNKTIONS-SATZ HLESEN
HDRUCK   DC    CL82' '       FUNKTIONS-SATZ HDRUCK
HDRUCK1  DC    CL82' '       FUNKTIONS-SATZ HDRUCK1
HSTANZ   DC    CL82' '       FUNKTIONS-SATZ HSTANZ
A1ZAE    EQU   HLESEN+12
D1ZAE    EQU   HDRUCK+12
D2ZAE    EQU   HDRUCK1+12
S1ZAE    EQU   HSTANZ+12
TIMERECB DC    F'0'
DCBEOF   DC    A(DCBEOF)     ADRRESSE DES DCB'S FUER FUNKTIONS-ENDE
SAVE     DS    1F
         DC    H'0'
UCBLIST  DS    0F
         DC    X'8000'
         DC    H'0'
VOL1     DS    0CL80         VOLUME-LABEL IMAGE
         DC    C'VOL1'
VOL1SER  DC    C'OSXXXX'
         DC    C'0'
         DC    69CL1'0'
VOLIST   DC    H'1'          NUMBER OF VOLUMES
VOLSERNO DC    XL6'00'       VOLUME-SERIAL-NUMBER
CAM2     CAMLST SCRATCH,JFCBDSNM,,VOLIST
WTO1     DC    CL72' '                 BEREICH FUER OPERATOR-CONTROL
WTOR1ECB DC    F'0'          EVENT-CONTROL-BLOCK FOR WTOR
WTOR1    DC    AL1(1)        REPLY LENTH
         DC    AL3(CONBER)   REPLY ADDRESS
         DC    A(WTOR1ECB)   ECB   ADRESS
         DC    AL2(53)       MESSAGE LENGTH
         DC    AL2(0)
DWORT    DC    D'0'
LISTNR   DC    X'0000'
         DC    X'000000000000'
LISTNR1  DC    X'0000'
         DC    X'000000000000'
ECBLES1  DC    X'00000000'               E C B
ECBDRU1  DC    X'00000000'
ECBDRU2  DC    X'00000000'
ECBSTA1  DC    X'00000000'              E C B
ECBCONSL DC    X'00000000'                  E C B
SUCHNR   DC    CL27' '
         DC    C'      '
         DS    0H
SUCHNR1  DC    CL27' '
         DC    CL6' '
D1BDBER  DS    1F
D2BDBER  DS    F
BLANK44  DC    CL44' '
BIN00    DC    X'0000000000000000000000000000000000000000000000'
BINFF    DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
MRDJFCB  RDJFCB (MRDJFCB),MF=L
         IEFJFCBN
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       KONSTANTEN+ZWISCHENSP'
CONVZSP  DC    D'0'                    DOPPELWORT FUER CVB UND CVD
R8ZSP    DC    F'0'          ZWISCHENSPEICHER FUER REGISTER
CPOEVENT DC    A(ECBLES1)    ECB-ADDR. FUER FUNKTION = HLESEN
         DC    A(ECBSTA1)    ECB-ADDR. FUER FUNKTION = HSTANZ
         DC    A(ECBDRU1)    ECB-ADDR. FUER FUNKTION = HDRUCK
         DC    A(0)          ECB-ADDR. FUER OPERATOR-EINGABE DYN.MOD.
         DC    A(TIMERECB)   ECB-ADDR. FUER FUNKTION = SEARCH
         DC    X'80'
         DC    AL3(ECBDRU2)  ECB-ADDR.FUER FUNKTION = HDRUCK1
ANF2     DC    A(ANF1+4096)  ADDR. DES NAECHSTEN 4K-BLOCKS
ANF3     DC    A(ANF1+8192)
SAVEAREA DS    18F           SAVEAREA FUER MFP
PARMFELD DC    CL40' '       PARMFELD FUER FUNKTION HLESENP
PARMF    DC    CL40' '       ZWISCHENSPEICHER FUER PARMFELD HLESENP
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       KARTE - BAND/PLATTE'
OPA1     MVC   MRDJFCB+1(3),=AL3(A1BD)
         MVC   A1ZAE(4),=X'0000001C'
         RDJFCB MF=(E,MRDJFCB)
         MVC   JFCBVOLS(6),VOLSER      MOVE VOL-SER-NO TO JFCB
         OPEN  (READER1,(INPUT))
         EXCP  LESEN1
         WAIT  ECB=ECBLES1
         NI    ECBLES1,X'00'
         CLC   ABFBER1(3),=C'MFP'
         BE    OPA1A
         WTO   'MFP VORLAUFKARTE FEHLT',ROUTCDE=13
         B     EOFA1                   BRANCH TO CLOSE HLESEN
OPA1A    MVC   JFCBDSNM(44),BLANK44    LOESCHE DSN IN JFCB
         MVC   WTO1+31(8),ABFBER1+62   MOVE DEPENDING JOBNAME
         MVC   JFCBDSNM(11),ABFBER1+8
         MVC   HLESEN+16(8),JFCBDSNM+3
         MVI   HLESEN+16,C'P'
OPA10    CLI   ABFBER1+19,X'40'        AUF NUMERISCH PRUEFEN ?
         BE    OPA11                   NO, BRANCH
         MVI   SUBA1P4+1,X'F0'
         MVI   OPA12+1,X'F0'
         MVI   LESEN1,X'40'
         MVC   PARMFELD,ABFBER1+19
         MVC   PARMF,ABFBER1+19
OPA11    EQU   *
         CLC   UCBLIST+2(2),=X'0000'
         BE    OPENA1
         XC    DWORT(8),DWORT
         PACK  DWORT+6(2),ABFBER1+59(3) UMWANDELN DER ANGEFORDERTEN
         CVB   1,DWORT                 SPACE-REQUEST FUER JFCB
         ST    1,DWORT
         MVC   JFCBPQTY,DWORT+1
         LA    1,UCBLIST
         LA    0,INFMJFCB
         SVC   32
OPENA1   EQU   *
         OPEN  (A1BD,(OUTPUT)),TYPE=J
         MVC   HLESEN+34(44),JFCBDSNM
         MVC   HLESEN+78(6),JFCBVOLS
         AP    A1ZAE(4),=P'1'          INCREMENT CARD-COUNTER
OPA12    NOP   OPA13
         EXCP  LESEN1
         B     SUBCON2
OPA13    MVC   LESEN1+17(3),=AL3(CCWLES1R)
         EXCP  LESEN1
         B     SUBCON2
         EJECT
*                       **  EXCP - READER
SUBA1    CLI   ECBLES1,X'7F'           EXCP OK?
         BE    SUBA10                  JA, GOTO VERARBEITUNG
         TM    LESEN1+12,B'00000001'   TEST AUF END-OF-FILE
         BO    EOFA1                   JA, GOTO END-OF-FILE-ROUTINE
         LA    1,READER1               NO, LADE DCB-ADDR.
         B     DCBSYNEX                GOTO SYNAD-ROUTINE
SUBA10   NI    ECBLES1,X'00'            SCRATCH COMPLETION BIT
         AP    A1ZAE(4),=P'1'
         MVC   PARMFELD,PARMF
         MVI   SUBA1P3+1,X'00'
SUBA11   CLI   PARMFELD,X'40'
         BE    SUBA1P3
         MVI   SUBA1P5+1,X'00'
         LA    9,PARMFELD
SUBA1P1  PACK  CONVZSP+6(2),0(2,9)
         OI    CONVZSP+7,X'0C'
         CVB   10,CONVZSP
         LA    8,ABFBER1-1
         LA    8,0(8,10)
         MVI   0(9),X'F0'
         MVC   1(1,9),3(9)
         TM    3(9),X'F0'
         BO    SUBA1P11
         OI    1(9),X'F0'
         MVI   0(9),X'F1'
SUBA1P11 PACK  CONVZSP+6(2),0(2,9)
         OI    CONVZSP+7,X'0C'
         CVB   10,CONVZSP
         B     SUBA1C0
SUBA1P2  LA    9,5(0,9)
         MVI   SUBA1C1+1,X'00'
         CLI   0(9),X'40'
         BNE   SUBA1P1
SUBA1P3  NOP   SUBA1P4
         WRITE A1BDECB,SF,A1BD,ABFBER1
         CHECK A1BDECB
SUBA1P4  NOP   SUBA1P5
         EXCP  LESEN1
         B     HAPRO
SUBA1P5  NOP   SUBA1P6
         MVC   LESEN1+17(3),=AL3(CCWLES12)
         EXCP  LESEN1
         B     HAPRO
SUBA1P6  MVC   LESEN1+17(3),=AL3(CCWLES13)
         EXCP  LESEN1
         B     HAPRO
SUBA1C0  CLI   0(8),X'40'
         BNE   SUBA1C1
         MVI   0(8),X'F0'
         B     SUBA1C2
SUBA1C1  NOP   SUBA1C11
         MVI   SUBA1C1+1,X'F0'
         CLI   0(8),X'C0'
         BL    A1FEHLER
         CLI   0(8),X'CA'
         BL    SUBA1C2
         CLI   0(8),X'D0'
         BL    A1FEHLER
         CLI   0(8),X'DA'
         BL    SUBA1C2
SUBA1C11 CLI   0(8),X'F0'
         BL    A1FEHLER
         CLI   0(8),X'F9'
         BH    A1FEHLER
SUBA1C2  BCT   8,SUBA1C3
SUBA1C3  BCT   10,SUBA1C0
         B     SUBA1P2
A1FEHLER MVI   SUBA1P5+1,X'F0'
         MVI   SUBA1P3+1,X'F0'
         B     SUBA1C2
EOFA1    MVC   DCBEOF+2(2),A1BD+40
         CLOSE (READER1)
         CLOSE (A1BD)
         NI    ECBLES1,X'00'
EOFA1WTO MVC   WTOSPOOL+12(6),=C'HLESEN'
         MVC   WTOSPOOL+25(6),=C'KARTEN'
         UNPK  WTOSPOOL+32(7),A1ZAE(4)
         MVC   WTOSPOOL+8(3),HLESEN+28
         OI    WTOSPOOL+38,X'F0'
         BAL   10,WTOSPOOL
         MVI   OPA12+1,X'00'
         MVI   SUBA1P4+1,X'00'
         MVI   LESEN1,X'00'
         MVC   LESEN1+17(3),=AL3(CCWLES1)
         LA    10,HLESEN
         LA    15,ACCOUNT
         BALR  14,15
         B     CEOF
ABFBER1  DS    CL80
READER1  DCB   DSORG=PS,MACRF=(E),DDNAME=LESER,IOBAD=LESEN1,DEVD=RD,   C
               EODAD=EOFA1
A1BD     DCB   DSORG=PS,MACRF=(W),DDNAME=HREAD,BLKSIZE=80,RECFM=F,     =
               EXLST=DCBEXIT,SYNAD=DCBSYNEX,EODAD=EOFA1
         CNOP  0,4
LESEN1   DS    0CL40
         DC    B'00000000'        FLAG1
         DC    X'00'              FLAG2
         DC    2X'00'
         DC    X'00'              ECBCODE
         DC    AL3(ECBLES1)       ECBADR
         DC    X'00'              FLAG3
         DC    7X'00'             CHSTATUS
         DC    X'00'              SIOCC
         DC    AL3(CCWLES1)       STARTADR
         DC    X'00'
         DC    AL3(READER1)       DCBADR
         DC    4X'00'
         DC    X'0001'            BLINCREMENT
         DC    2X'00'             ERRORCOU
         DC    8X'00'
CCWLES1  CCW   X'42',ABFBER1,X'00',80    C C W
CCWLES1R CCW   X'C2',ABFBER1,X'00',80 CCW-READ
CCWLES12 CCW   X'63',ABFBER1,X'60',80 CCW--SS 2
         CCW   X'C2',ABFBER1,X'00',80 CCW-READ
CCWLES13 CCW   X'A3',ABFBER1,X'60',80 CCW--SS 3
         CCW   X'C2',ABFBER1,X'00',80 CCW-READ
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       BAND/PLATTE - DRUCKER'
OPD1     MVI   SUBD1W,X'03'
         MVC   D1ZAE(4),=X'0000000C'
         MVC   STOP07+16(3),INPUT
         MVC   SETWTOR1+16(3),OUTPUT
         MVC   COND1EW+8(3),INPUT
         MVC   LISTE00W+16(3),OUTPUT
         MVC   MRDJFCB+1(3),=AL3(D1BD)
         RDJFCB MF=(E,MRDJFCB)
         MVC   JFCBDSNM,VOL1
         MVC   JFCBNVOL,VOL1+45
         MVC   JFCBVOLS,VOL1+46
         CLI   SEARCHKZ,C','           IST DSNAME VORGEGEBEN ?
         BNE   *+12
         MVI   SCRATCH+1,X'00'
         MVI   SEARCHKZ,C' '
         OPEN  (D1BD,(INPUT)),TYPE=J
         MVC   HDRUCK+34(44),JFCBDSNM
         MVC   HDRUCK+78(6),JFCBVOLS
         OPEN  (PRINT1,(OUTPUT))
         GETMAIN EC,LV=1600,A=D1BDBER
         MVI   D1BDBER,X'09'
         L     10,D1BDBER
         MVC   D1BDECB+12(4),D1BDBER
         LA    10,28(0,10)
         LA    8,CCWDRU1
OPD12    ST    10,8(0,8)
         LA    10,160(0,10)
         LA    8,16(0,8)
         C     8,=A(CCWDRU1+160)
         BL    OPD12
         MVI   D1BDBER,X'00'
         NI    ECBDRU1,X'00'
         NI    ECBCONSL,X'00'
         MVC   SUCHNR,BLANK44
         MVI   SUBD1SW3+1,X'F0'
         LA    10,JFCBDSNM
         LA    8,40
OPD15    CLC   0(5,10),=C'.DR.L'
         BE    OPD151
         LA    10,1(0,10)
         BCT   8,OPD15
         B     OPD1X
OPD151   MVC   SETPRT1+15(4),5(10)
OPD1X    MVI   EXCP1+1,X'00'
OPD14    EQU   *
         CLI   OPTION2,X'40'
         BE    SUBD10
         NI    ECBCONSL,X'00'
STOP07   WTOR  'MFP SUCHNR=?',SUCHNR,27,ECBCONSL,ROUTCDE=13
         WAIT  ECB=ECBCONSL
         CLI   SUCHNR,C'U'
         BE    SUBD10
         MVI   SUBD1SW3+1,X'00'
         CLI   SUCHNR,C'='
         BNE   OPD13
         MVI   SUBD1VGL+1,X'70'
         MVC   SUCHNR(27),SUCHNR+1
OPD13    EQU   *
         CLI   SUCHNR,C'B'
         BNE   SUBD10
         XC    LISTNR(8),LISTNR
         PACK  LISTNR+5(3),SUCHNR+1(4)
         CVB   8,LISTNR
         STH   8,SUCHNR
         STH   8,LISTNR
         MVC   SUCHNR+2(23),SUCHNR+6
         B     SUBD10
         EJECT
SUBD1    EQU   *
         CLI   ECBDRU1,X'7F'
         BNE   ERRD1                   IF ERROR GOTO FEHLER-ENDE
SUBD10   NI    ECBDRU1,X'00'
         MVC   DRUCK1+17(3),=AL3(CCWDRU1)
         READ  D1BDECB,SF,D1BD,D1BDBER,'S'
         CHECK D1BDECB
         LA    8,CCWDRU1
         L     10,D1BDBER
SUBD1SW1 NOP   SUBD1SW2
TX03     CLC   0(4,10),=X'03030303'    TEMPORARY TEST FOR X'03'
         BNE   TX031
         MVI   0(8),X'03'
         MVI   8(8),X'03'
         LA    8,16(8)
         LA    10,160(0,10)
         C     8,=A(CCWDRU1+160)
         BL    TX03
         B     SUBD10
TX031    EQU   *
         MVI   SUBD1SW1+1,X'F0'
         TM    0(10),B'10000000'       TEST AUF SELEKTIVES DRUCKEN
         BO    SUBD1SW2                NO, BRANCH
         MVI   SUBD1SW2+1,X'00'
         CLI   SUBD1SW3+1,X'00'
         BE    *+10
         MVC   LISTNR(2),0(10)
SUBD1SW2 B     SUBD14
         CLC   0(2,10),LISTNR
         BE    SUBD12
SUBD111  MVI   0(8),X'03'
         MVI   8(8),X'03'
SUBD112  LA    8,16(8)
         LA    10,160(0,10)
         C     8,=A(CCWDRU1+160)
         BL    SUBD1SW2
         BAL   14,EXCP1
         B     SUBCON2
SUBD12   CLC   2(23,10),BIN00
         BNE   SUBD13
SUBD121  MVI   0(8),X'03'
         MVI   8(8),X'03'
         CLI   27(10),C'C'
         BNE   *+8
         MVI   SUBD1SW4+1,X'00'
         MVC   LISTE00W+20(28),28(10)
         MVC   HDRUCK+16(8),152(10)    MOVE ACCOUNTING-INFORMATION
         MVI   4(8),X'20'
         BAL   14,EXCP1
         WAIT  ECB=ECBDRU1
         MVI   4(8),X'60'
         CLI   ECBDRU1,X'7F'
         BNE   ERRD1                   IF ERROR GOTO FEHLER-ENDE
         NI    ECBDRU1,X'00'
         NI    ECBCONSL,X'00'
LISTE00W WTOR  'MFP STOP 10 FORMW. AUF RZ 9002/X',CONBER,1,ECBCONSL,   =
               ROUTCDE=13
         WAIT  ECB=ECBCONSL
         NI    ECBCONSL,X'00'
         CLI   CONBER,C'E'             BEENDEN DES DRUCKS ?
         BE    EOFD1                   JA, BRANCH ZUM ENDE
         LA    15,LISTE00W+20
         LA    14,23
LISTE001 CLC   0(2,15),=C'L='
         BE    LISTE002
         LA    15,1(0,15)
         BCT   14,LISTE001
         B     LISTE003
LISTE002 MVC   SETPRT1+15(4),2(15)
LISTE003 LA    15,LISTE00W+20
         LA    14,23
LISTE004 CLC   0(2,15),=C'K='
         BE    LISTE005
         LA    15,1(0,15)
         BCT   14,LISTE004
         B     LISTE00X
LISTE005 MVC   SETPRT1+8(4),2(15)
LISTE00X MVI   EXCP1+1,X'00'
         ST    8,R8ZSP
         MVC   DRUCK1+17(3),R8ZSP+1
         B     SUBD112
SUBD13   CLC   2(23,10),BINFF
         BNE   SUBD14
SUBD131  MVC   LISTNR(2),28(10)
SUBD132  MVI   0(8),X'03'
         MVI   8(8),X'03'
         LA    8,16(8)
         C     8,=A(CCWDRU1+160)
         BL    SUBD132
         BAL   14,EXCP1
         WAIT  ECB=ECBDRU1
         CLI   ECBDRU1,X'7F'
         BNE   ERRD1                   IF ERROR GOTO FEHLER-ENDE
         NI    ECBDRU1,X'00'
         CLOSE (D1BD,REREAD)
         OPEN  (D1BD,(INPUT))
         B     SUBD10
SUBD14   CLC   0(2,10),=C'00'
         BE    SUBD121
         CLC   2(23,10),BIN00
         BE    SUBD121
SUBD1SW3 NOP   SUBD1SW4
         CLC   0(25,10),SUCHNR
SUBD1VGL BL    SUBD111
         MVI   SUBD1SW3+1,X'F0'
SUBD1SW4 B     SUBD141                 WEICHE OFF BEI 1410 + ASA-CODE
         LA    9,CCTAB                 LADE ADDRESS OF VORSCHUB-TABELLE
         CLC   1(1,9),27(10)
         BE    *+12                    SUCHE PAARIGES VORSCHUB-ZEICHEN
         LA    9,2(0,9)
         B     *-14
         MVC   27(1,10),0(9)           MOVE MASCHINEN-VORSCHUB-ZEICHEN
SUBD141  CLI   27(10),X'03'
         BE    *+12
         TM    27(10),B'00000010'
         BO    SUBD15
         MVC   0(1,8),SUBD1W
         MVC   8(1,8),27(10)
         MVI   SUBD1W,X'03'
         B     SUBD151
SUBD15   MVC   0(1,8),27(10)
         MVI   8(8),X'01'
         MVI   SUBD1W,X'0B'
SUBD151  AP    D1ZAE(4),=P'1'
         MVI   27(10),X'03'
         B     SUBD112
ERRD1    MVI   SCRATCH+1,X'F0'         VERHINDERE SCRATCH DRUCK-DATEI
         LA    1,PRINT1                LOAD R1 WITH DCB-ADDR.
         B     DCBSYNEX                GOTO SYNAD-ROUTINE
EOFD1    MVC   WTOSPOOL+12(6),=C'HDRUCK'
         MVC   WTOSPOOL+25(6),=C'ZEILEN'
         UNPK  WTOSPOOL+32(7),D1ZAE(4)
         OI    WTOSPOOL+38,X'F0'
         MVC   WTOSPOOL+8(3),HDRUCK+31
         BAL   10,WTOSPOOL
EOFD11   MVC   DCBEOF+2(2),D1BD+40
         FREEMAIN E,LV=1600,A=D1BDBER
SCRATCH  B     SCRATCHX
         MVC   MRDJFCB+1(3),=AL3(D1BD)
         RDJFCB MF=(E,MRDJFCB)
         MVC   VOLSERNO(6),JFCBVOLS
         SR    0,0
         SCRATCH CAM2
SCRATCHX MVI   SCRATCH+1,X'F0'
         CLOSE (PRINT1)
         CLOSE (D1BD)
         XC    LISTNR(8),LISTNR
         MVI   SUBD1SW1+1,X'00'
         MVI   SUBD1SW2+1,X'F0'
         MVI   SUBD1SW3+1,X'00'
         MVI   SUBD1SW4+1,X'F0'
         MVI   SUBD1VGL+1,X'40'
         XC    STOP07+16(3),STOP07+16
         NI    ECBDRU1,X'00'
         LA    10,HDRUCK
         LA    15,ACCOUNT
         BALR  14,15
         B     CEOF
EXCP1    NOP   EXCP12
         ST    14,DWORT
         CLC   SETPRT1+15(4),=C'XXXX'
         BNE   SETPRT1
SETWTOR1 WTOR  'XXX STOPSATZ FEHLT EINGABE:L=XXXX,K=XXXX',             =
               SETWTORA,13,ECBCONSL,ROUTCDE=13
         WAIT  ECB=ECBCONSL
         NI    ECBCONSL,X'00'
         LA    15,SETWTORA
         LA    14,7
SETWTOR2 CLC   0(2,15),=C'L='
         BE    SETWTOR3
         LA    15,1(0,15)
         BCT   14,SETWTOR2
         B     SETWTOR1
SETWTOR3 MVC   SETPRT1+15(4),2(15)
         LA    15,SETWTORA
         LA    14,7
SETWTOR4 CLC   0(2,15),=C'K='
         BE    SETWTOR5
         LA    15,1(0,15)
         BCT   14,SETWTOR4
         B     SETWTORX
SETWTOR5 MVC   SETPRT1+8(3),3(15)
SETWTORX MVI   EXCP1+1,X'00'
         MVC   SETWTORA(13),BLANK44
SETPRT1  SETPRT PRINT1,FCB=(9080,V),UCS=P11,OPTCD=B
         MVC  SETPRT1+8(3),=C'P11'
         MVC   SETPRT1+15(4),=C'XXXX'
         L     14,DWORT
         MVI   EXCP1+1,X'F0'
         LTR   15,15
         BZ    EXCP12
         ABEND 4095,DUMP
SETWTORA DC    CL13' '
EXCP12   EXCP  DRUCK1
         BR    14
CCWDRU1  CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+28,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+188,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+348,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+508,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+668,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+828,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+988,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+1148,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+1308,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+1468,X'20',132
SUBD1W   DC    X'03'
PRINT1   DCB   DSORG=PS,MACRF=(E),DDNAME=PRINTER,IOBAD=DRUCK1,         =
               DEVD=PR,EODAD=EOFD1
D1BD     DCB   DSORG=PS,MACRF=(RC),DDNAME=LISTE,RECFM=F,               =
               BLKSIZE=1600,EODAD=EOFD1,LRECL=160,                     =
               EXLST=DCBEXIT,SYNAD=DCBSYNEX
         CNOP  0,4
DRUCK1   DS    0CL40
         DC    B'01000000'
         DC    4X'00'
         DC    AL3(ECBDRU1)
         DC    9X'00'
         DC    AL3(CCWDRU1)
         DC    X'00'
         DC    AL3(PRINT1)
         DC    4X'00'
         DC    X'0001'
         DC    10X'00'
         TITLE 'MFP MULTI-FUNKTION-PROGRAM    2. BAND/PLATTE - DRUCKER'
OPD2     MVI   SUBD2W,X'03'
         MVC   D2ZAE(4),=X'0000000C'
         MVC   LIST100W+16(3),OUTPUT
         MVC   STOP071+16(3),INPUT
         MVC   ZETWTOR1+16(3),OUTPUT
         MVC   COND2EW+8(3),INPUT
         MVC   MRDJFCB+1(3),=AL3(D2BD)
         RDJFCB MF=(E,MRDJFCB)
         MVC   JFCBDSNM,VOL1
         MVC   JFCBNVOL,VOL1+45
         MVC   JFCBVOLS,VOL1+46
         CLI   SEARCHKZ,C','           IST DSNAME VORGEGEBEN ?
         BNE   *+12
         MVI   SCRATCH1+1,X'00'
         MVI   SEARCHKZ,C' '
         OPEN  (D2BD,(INPUT)),TYPE=J
         MVC   HDRUCK1+34(44),JFCBDSNM
         MVC   HDRUCK1+78(6),JFCBVOLS
         OPEN  (PRINT2,(OUTPUT))
         OPEN  (PRINT2X,(OUTPUT))
         GETMAIN EC,LV=1600,A=D2BDBER
         MVI   D2BDBER,X'09'
         L     10,D2BDBER
         MVC   D2BDECB+12(4),D2BDBER
         LA    10,28(0,10)
         LA    8,CCWDRU2
OPD22    ST    10,8(0,8)
         LA    10,160(0,10)
         LA    8,16(0,8)
         C     8,=A(CCWDRU2+160)
         BL    OPD22
         MVI   D2BDBER,X'00'
         NI    ECBDRU2,X'00'
         NI    ECBCONSL,X'00'
         MVC   SUCHNR1,BLANK44
         MVI   SUBD2SW3+1,X'F0'
         LA    10,JFCBDSNM
         LA    8,40
OPD25    CLC   0(5,10),=C'.DR.L'
         BE    OPD251
         LA    10,1(0,10)
         BCT   8,OPD25
         B     OPD2X
OPD251   MVC   SETPRT2+15(4),5(10)
OPD2X    MVI   EXCP2+1,X'F0'
OPD24    EQU   *
         CLI   OPTION2,X'40'
         BE    SUBD20
         NI    ECBCONSL,X'00'
STOP071  WTOR  'MFP SUCHNR=?',SUCHNR1,27,ECBCONSL,ROUTCDE=13
         WAIT  ECB=ECBCONSL
         CLI   SUCHNR1,C'U'
         BE    SUBD20
         MVI   SUBD2SW3+1,X'00'
         CLI   SUCHNR1,C'='
         BNE   OPD23
         MVI   SUBD2VGL+1,X'70'
         MVC   SUCHNR1(27),SUCHNR1+1
OPD23    EQU   *
         CLI   SUCHNR1,C'B'
         BNE   SUBD20
         XC    LISTNR1(8),LISTNR1
         PACK  LISTNR1+5(3),SUCHNR1+1(4)
         CVB   8,LISTNR1
         STH   8,SUCHNR1
         STH   8,LISTNR1
         MVC   SUCHNR1+2(23),SUCHNR1+6
         B     SUBD20
         EJECT
SUBD2    EQU   *
         CLI   ECBDRU2,X'7F'
         BNE   ERRD2                   IF ERROR GOTO FEHLER-ENDE
SUBD20   NI    ECBDRU2,X'00'
         MVC   DRUCK2+17(3),=AL3(CCWDRU2)
         READ  D2BDECB,SF,D2BD,D2BDBER,'S'
         CHECK D2BDECB
         LA    8,CCWDRU2
         L     10,D2BDBER
SUBD2SW1 NOP   SUBD2SW2
TX103    CLC   0(4,10),=X'03030303'    TEMPORARY TEST FOR X'03'
         BNE   TX1031
         MVI   0(8),X'03'
         MVI   8(8),X'03'
         LA    8,16(8)
         LA    10,160(0,10)
         C     8,=A(CCWDRU2+160)
         BL    TX103
         B     SUBD20
TX1031    EQU   *
         MVI   SUBD2SW1+1,X'F0'
         TM    0(10),B'10000000'       TEST AUF SELEKTIVES DRUCKEN
         BO    SUBD2SW2                NO, BRANCH
         MVI   SUBD2SW2+1,X'00'
         CLI   SUBD2SW3+1,X'00'
         BE    *+10
         MVC   LISTNR1(2),0(10)
SUBD2SW2 B     SUBD24
         CLC   0(2,10),LISTNR1
         BE    SUBD22
SUBD211  MVI   0(8),X'03'
         MVI   8(8),X'03'
SUBD212  LA    8,16(8)
         LA    10,160(0,10)
         C     8,=A(CCWDRU2+160)
         BL    SUBD2SW2
         BAL   14,EXCP2
         B     SUBCON2
SUBD22   CLC   2(23,10),BIN00
         BNE   SUBD23
SUBD221  MVI   0(8),X'03'
         MVI   8(8),X'03'
         CLI   27(10),C'C'
         BNE   *+8
         MVI   SUBD2SW4+1,X'00'
         MVC   LIST100W+20(28),28(10)
         MVC   HDRUCK1+16(8),152(10)    MOVE ACCOUNTING-INFORMATION
         MVI   4(8),X'20'
         BAL   14,EXCP2
         WAIT  ECB=ECBDRU2
         MVI   4(8),X'60'
         CLI   ECBDRU2,X'7F'
         BNE   ERRD2                   IF ERROR GOTO FEHLER-ENDE
         NI    ECBDRU2,X'00'
         NI    ECBCONSL,X'00'
LIST100W WTOR  'MFP STOP 10 FORMW. AUF RZ 9002/X',CONBER,1,ECBCONSL,   =
               ROUTCDE=13
         WAIT  ECB=ECBCONSL
         NI    ECBCONSL,X'00'
         CLI   CONBER,C'E'             BEENDEN DES DRUCKS ?
         BE    EOFD2                   JA, BRANCH ZUM ENDE
         LA    15,LIST100W+20
         LA    14,23
LIST1001 CLC   0(2,15),=C'L='
         BE    LIST1002
         LA    15,1(0,15)
         BCT   14,LIST1001
         B     LIST1003
LIST1002 MVC   SETPRT2+15(4),2(15)
LIST1003 LA    15,LIST100W+20
         LA    14,23
LIST1004 CLC   0(2,15),=C'K='
         BE    LIST1005
         LA    15,1(0,15)
         BCT   14,LIST1004
         B     LIST100X
LIST1005 MVC   SETPRT2+8(4),2(15)
LIST100X MVI   EXCP2+1,X'00'
         ST    8,R8ZSP
         MVC   DRUCK2+17(3),R8ZSP+1
         B     SUBD212
SUBD23   CLC   2(23,10),BINFF
         BNE   SUBD24
SUBD231  MVC   LISTNR1(2),28(10)
SUBD232  MVI   0(8),X'03'
         MVI   8(8),X'03'
         LA    8,16(8)
         C     8,=A(CCWDRU2+160)
         BL    SUBD232
         BAL   14,EXCP2
         WAIT  ECB=ECBDRU2
         CLI   ECBDRU2,X'7F'
         BNE   ERRD2                   IF ERROR GOTO FEHLER-ENDE
         NI    ECBDRU2,X'00'
         CLOSE (D2BD,REREAD)
         OPEN  (D2BD,(INPUT))
         B     SUBD20
SUBD24   CLC   0(2,10),=C'00'
         BE    SUBD221
         CLC   2(23,10),BIN00
         BE    SUBD221
SUBD2SW3 NOP   SUBD2SW4
         CLC   0(25,10),SUCHNR1
SUBD2VGL BL    SUBD211
         MVI   SUBD2SW3+1,X'F0'
SUBD2SW4 B     SUBD241                 WEICHE OFF BEI 1410 + ASA-CODE
         LA    9,CCTAB                 LADE ADDRESS OF VORSCHUB-TABELLE
         CLC   1(1,9),27(10)
         BE    *+12                    SUCHE PAARIGES VORSCHUB-ZEICHEN
         LA    9,2(0,9)
         B     *-14
         MVC   27(1,10),0(9)           MOVE MASCHINEN-VORSCHUB-ZEICHEN
SUBD241  CLI   27(10),X'03'
         BE    *+12
         TM    27(10),B'00000010'
         BO    SUBD25
         MVC   0(1,8),SUBD2W
         MVC   8(1,8),27(10)
         MVI   SUBD2W,X'03'
         B     SUBD251
SUBD25   MVC   0(1,8),27(10)
         MVI   8(8),X'01'
         MVI   SUBD2W,X'0B'
SUBD251  AP    D2ZAE(4),=P'1'
         MVI   27(10),X'03'
         B     SUBD212
ERRD2    MVI   SCRATCH1+1,X'F0'         VERHINDERE SCRATCH DRUCK-DATEI
         LA    1,PRINT2                LOAD R1 WITH DCB-ADDR.
         B     DCBSYNEX                GOTO SYNAD-ROUTINE
EOFD2    MVC   WTOSPOOL+12(6),=C'HDRUCK'
         MVC   WTOSPOOL+25(6),=C'ZEILEN'
         UNPK  WTOSPOOL+32(7),D2ZAE(4)
         OI    WTOSPOOL+38,X'F0'
         MVC   WTOSPOOL+8(3),HDRUCK1+31
         BAL   10,WTOSPOOL
EOFD21   MVC   DCBEOF+2(2),D2BD+40
         FREEMAIN E,LV=1600,A=D2BDBER
SCRATCH2 B     SCRATCHY
         MVC   MRDJFCB+1(3),=AL3(D2BD)
         RDJFCB MF=(E,MRDJFCB)
         MVC   VOLSERNO(6),JFCBVOLS
         SR    0,0
         SCRATCH CAM2
SCRATCHY MVI   SCRATCH2+1,X'F0'
         CLOSE (PRINT2)
         CLOSE (PRINT2X)
         CLOSE (D2BD)
         XC    LISTNR1(8),LISTNR1
         MVI   SUBD2SW1+1,X'00'
         MVI   SUBD2SW2+1,X'F0'
         MVI   SUBD2SW3+1,X'00'
         MVI   SUBD2SW4+1,X'F0'
         MVI   SUBD2VGL+1,X'40'
         NI    ECBDRU2,X'00'
         XC    STOP071+16(3),STOP071+16
         LA    10,HDRUCK1
         LA    15,ACCOUNT
         BALR  14,15
         B     CEOF
EXCP2    NOP   EXCP22
         ST    14,DWORT
         CLC   SETPRT2+15(4),=C'XXXX'
         BNE   SETPRT2
ZETWTOR1 WTOR  'XXX STOPSATZ FEHLT EINGABE:L=XXXX,K=XXXX',             =
               SETWTORA,13,ECBCONSL,ROUTCDE=13
         WAIT  ECB=ECBCONSL
         NI    ECBCONSL,X'00'
         LA    15,SETWTORA
         LA    14,7
ZETWTOR2 CLC   0(2,15),=C'L='
         BE    ZETWTOR3
         LA    15,1(0,15)
         BCT   14,ZETWTOR2
         B     ZETWTOR1
ZETWTOR3 MVC   SETPRT2+15(4),2(15)
         LA    15,SETWTORA
         LA    14,7
ZETWTOR4 CLC   0(2,15),=C'K='
         BE    ZETWTOR5
         LA    15,1(0,15)
         BCT   14,ZETWTOR4
         B     ZETWTORX
ZETWTOR5 MVC   SETPRT2+8(3),3(15)
ZETWTORX MVI   EXCP2+1,X'00'
         MVC   SETWTORA(13),BLANK44
SETPRT2  SETPRT  PRINT2X,FCB=(9080,V),UCS=P11,OPTCD=B
         MVC   SETPRT2+8(3),=C'P11'
         MVC   SETPRT2+15(4),=C'XXXX'
         L     14,DWORT
         MVI   EXCP2+1,X'F0'
         LTR   15,15
         BZ    EXCP22
         ABEND 4095,DUMP
EXCP22   EXCP  DRUCK2
         BR    14
CCWDRU2  CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+28,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+188,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+348,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+508,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+668,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+828,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+988,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+1148,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+1308,X'60',132
         CCW   X'03',D1BDBER,X'60',1
         CCW   X'09',D1BDBER+1468,X'20',132
SUBD2W   DC    X'03'
PRINT2   DCB   DSORG=PS,MACRF=(E),DDNAME=PRINTER1,IOBAD=DRUCK2,        =
               DEVD=PR,EODAD=EOFD2
PRINT2X  DCB   DSORG=PS,MACRF=(W),DDNAME=PRINTER1
D2BD     DCB   DSORG=PS,MACRF=(RC),DDNAME=LISTE1,RECFM=F,              =
               BLKSIZE=1600,EODAD=EOFD2,LRECL=160,                     =
               EXLST=DCBEXIT,SYNAD=DCBSYNEX
         CNOP  0,4
DRUCK2   DS    0CL40
         DC    B'01000000'
         DC    4X'00'
         DC    AL3(ECBDRU2)
         DC    9X'00'
         DC    AL3(CCWDRU2)
         DC    X'00'
         DC    AL3(PRINT2)
         DC    4X'00'
         DC    X'0001'
         DC    10X'00'
         EJECT
         COPY  CNTRLCOD                INSERT VORSCHUB-TABELLE
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       BAND/PLATTE - STANZER'
OPS1     MVC   S1ZAE(4),=X'0000000C'
         CLC   JFCBDSNM(7),=C'HSTANZ.'
         BE    OPENS1
         MVC   MRDJFCB+1(3),=AL3(S1BD)
         RDJFCB MF=(E,MRDJFCB)
         MVC   JFCBVOLS(6),VOLSER
         MVC   JFCBDSNM(44),BLANK44
         MVC   JFCBDSNM(8),=C'HSTANZBD'
         CLI   SEARCHKZ,C','
         BNE   *+18
         MVI   SCRATCH1+1,X'00'
         MVC   JFCBDSNM(44),SEARCHDS
         MVI   SEARCHKZ,C','
OPENS1   EQU   *
         OPEN  (S1BD,(INPUT)),TYPE=J
         MVC   HSTANZ+34(44),JFCBDSNM
         MVC   HSTANZ+78(6),JFCBVOLS
         OPEN  (PUNCH1,(OUTPUT))
         MVC   SUCHNRS1,BLANK44
         MVC   S1WTOR+16(3),OUTPUT
         CLI   OPTION2,X'40'
         BE    OPS1GET
         NI    ECBCONSL,X'00'
S1WTOR   WTOR  'MFP SUCHNR= ',SUCHNRS1,27,ECBCONSL,ROUTCDE=13
         WAIT  ECB=ECBCONSL
         CLI   SUCHNRS1,C'U'
         BNE   *+12
         MVI   SUCHNRS1,C' '
         B     OPS1GET
         CLI   SUCHNRS1+2,C','
         BNE   OPS1GET
         PACK  S1CONV,SUCHNRS1(2)
         CVB   8,S1CONV
         ST    8,S1CONV
         LA    8,S1BDBER-1
         A     8,S1CONV
         ST    8,S1CONV
OPS1GET  LA    15,S1READ
         BALR  14,15
         MVC   HSTANZ+16(8),BLANK44
         CLC   S1BDBER(2),=X'FFFF'
         BNE   S1CLC
         MVC   HSTANZ+16(8),S1BDBER+2
         MVC   S1BDBER(10),BLANK44
         B     OPS1GET
S1CLC    L     8,S1CONV
         LTR   8,8
         BZ    S1EXCP
         CLC   0(24,8),SUCHNRS1+3
         BL    OPS1GET
         XC    S1CONV,S1CONV
         NI    ECBSTA1,X'3F'
S1EXCP   EXCP  STANZ1
         B     SUBCON2
         EJECT
SUBS1    CLI   ECBSTA1,X'7F'           TEST AUF STANZ-FEHLER
         BNE   ERRS1                   JA, BRANCH ZUM FEHLER-ENDE
         LA    15,S1READ
         BALR  14,15
         NI    ECBSTA1,X'00'
         EXCP  STANZ1
         B     HAPRO
ERRS1    MVI   SCRATCH1+1,X'F0'        VERHINDERE SCRATCH STANZ-DATEI
         LA    1,PUNCH1                LOAD R1 WITH DCB-ADDR.
         B     DCBSYNEX                GOTO SYNAD-ROUTINE
EOFS1    EQU   *
         MVC   DCBEOF+2(2),S1BD+40
         CLOSE (S1BD)
         CLOSE (PUNCH1)
         NI    ECBSTA1,X'00'
         MVC   WTOSPOOL+12(6),=C'HSTANZ'
         MVC   WTOSPOOL+25(6),=C'KARTEN'
         UNPK  WTOSPOOL+32(7),S1ZAE(4)
         OI    WTOSPOOL+38,X'F0'
         MVC   WTOSPOOL+8(3),HSTANZ+31
         BAL   10,WTOSPOOL
         LA    10,HSTANZ
         LA    15,ACCOUNT
         BALR  14,15
SCRATCH1 B     SCRATCHA
         MVC   MRDJFCB+1(3),=AL3(S1BD)
         RDJFCB MF=(E,MRDJFCB)
         MVC   VOLSERNO(6),JFCBVOLS
         SR    0,0
         SCRATCH CAM2
SCRATCHA MVI   SCRATCH1+1,X'F0'
         B     CEOF
*
S1READ   ST    14,SAVE
         READ  S1BDECB,SF,S1BD,S1BDBER
         CHECK S1BDECB
         CLC   S1BDBER+2(23),BIN00
         BNE   S1READ1
         CLI   S1BDBER+27,C'1'
         BNE   S1READ1
         MVC   STANZWTO+20(28),S1BDBER+28
         NI    ECBCONSL,X'00'
STANZWTO WTOR  'MFP STOP 05 KARTW. AUF RZ XXXX/X',CONBER,1,ECBCONSL,   =
               ROUTCDE=13
         WAIT  ECB=ECBCONSL
         CLI   CONBER,C'E'
         BE    EOFS1
         MVI   S1BDBER,X'40'           LOESCHE STOPSATZ
         MVC   S1BDBER+1(79),S1BDBER   UND STANZE LEERKARTE
S1READ1  EQU   *
         AP    S1ZAE(4),=P'1'
         L     14,SAVE
         BR    14
S1BDBER  DS    CL88
PUNCH1   DCB   DSORG=PS,MACRF=(E),DDNAME=STANZER,IOBAD=STANZ1,         =
               DEVD=PC,EODAD=EOFS1
S1BD     DCB   DSORG=PS,MACRF=(R),DDNAME=HSTANZ,RECFM=F,BLKSIZE=80,    =
               EODAD=EOFS1,EXLST=DCBEXIT,SYNAD=DCBSYNEX
         CNOP  0,4
STANZ1   DS    0CL40
         DC    B'00000000'
         DC    X'00'
         DC    2X'00'
         DC    X'00'
         DC    AL3(ECBSTA1)
         DC    X'00'
         DC    7X'00'
         DC    X'00'
         DC    AL3(CCWSTA1)
         DC    X'00'
         DC    AL3(PUNCH1)
         DC    4X'00'
         DC    X'0001'
         DC    2X'00'
         DC    8X'00'
CCWSTA1  CCW   X'41',S1BDBER,X'00',80
S1CONV   DC    D'0'
SUCHNRS1 DC    CL27' '
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       LITERAL-POOL'
         LTORG
ARBEITSB DSECT
SVA1     DS    18F
WTO      DS    CL50
DBLWD    DS    D
CC       DS    CL3
         DS    X
WORKL    EQU   *-ARBEITSB
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       DEFINE DCB-NAMES'
DCB      DCBD  DSORG=(BS)
         END   ANFANG
