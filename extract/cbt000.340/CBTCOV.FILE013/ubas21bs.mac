* STAND 04/01/76
***********************************************************************
*                                                                     *
*    PROGRAMM = ANALYERG                                              *
*                                                                     *
*    AUFRUF   = CALL ANALYERG,(AERG,OP,PER,ERR)                       *
*        CL14   AERG = ABSTIMMERGAENZUNG (PERIODE+OP.IN VERSCHLUES.FORM
*        H      OP   =  1  ADDITION                                   *
*                       2  SUBTRAKTION                                *
*        H      PER  =  1  LETZTE PERIODE                             *
*                       2  VORLETZTE PER.                             *
*        F      ERR  =  0  OK                                         *
*                       1  FEHLER IN AERG                             *
*                                                                     *
*    ZWECK    = OPERATION UND PERIODE FUER ABSTIMMUNG                 *
*               AUS DER ABSTIMMERGAENZUNG ENTNEHMEN                   *
*                                                                     *
***********************************************************************
         SPACE 2
ANALYERG CSECT
         XSAVE 12,SAVE,ANALYERG
         SPACE 1
         AREG
         SPACE 2
         LR    R11,R1                    --> ARGUMENTENLISTE
         L     R10,0(,R11)              -->ABSTIMMERGAENZUNG
         SPACE 1
*  OPERATION ENTSCHLUESSELN
         CLI   13(R10),C' '
         BE    OPADD
         CLI   13(R10),C'+'
         BE    OPADD
         CLI   13(R10),C'-'
         BE    OPSUB
         SPACE 2
*  OPERATION FALSCH
         L     R10,12(,R11)             -->RETURNCODE
         MVC   0(4,R10),=F'1'           RETURNCODE = 1
         B     RETURN
         SPACE 1
*  OPERATIONSINDEX SETZEN
OPADD    EQU   *              ADDITION
         LA    R2,1
         B     OPOK
OPSUB    EQU   *              SUBTRAKTION
         LA    R2,2
         SPACE 2
OPOK     EQU   *
         L     R9,4(,R11)               -->OPERATIONSINDEX
         STH   R2,0(,R9)                OPERATIONSINDEX
         SPACE 1
*  PERIODE, MIT DER ABGEST.WERDEN SOLL, BESTIMMEN
         MVC   LEN,=F'13'                LAENGE
         CALL  BLANKEX,((R10),(R10),LEN)     BLANK ENFERNEN
         CLC   LEN,=F'1'
         BL    TESTPER8
         LR    R3,R10                  --> AERG.KOMPRIMIERT
         CLC   0(4,R10),=C'ODER'       ODER-BEDINGUNG  ?
         BNE   TESTPER0                NEIN
         LA    R10,4(,R10)             -->PERIODE
         MVI   0(R9),ODER
TESTPER0 EQU   *                       GENERATION SUCHEN
         A     R3,LEN                  + LAENGE AERG.
         SH    R3,=H'2'                --->PER-HINWEIS (VERMUTET)
         XR    R4,R4                   EX-LAENGE = 0
         CLI   0(R3),C'-'
         BE    TESTPER2                PER-HINWEIS GEFUNDEN
         LA    R4,X'01'                EX-LAENGE = 1
         BCTR  R3,0                    -->PER-HINWEIS (VERMUTET)
         CLI   0(R3),C'-'
         BNE   TESTPER8                KEIN PER-HINWEIS GEFUNDEN
TESTPER2 EQU   *
         MVC   ZWITEST,1(R3)           PER-NR
         EX    R4,NC1                  PER-NR AUF
         EX    R4,CLC1                 NUM.PRUEFEN
         BNE   TESTPER8                NICHT NUMERISCH
         EX    R4,PACK1
         CVB   R2,DWORD                PER-NR BINAER
         LA    R2,1(R2)
         B     PEROK
TESTPER8 EQU   *                       VOR ALS GEN.ANGABE SUCHEN
         CLC   0(3,R10),=CL3'VOR'
         BE    PER2
         SPACE 2
*  PERIODENINDEX SETZEN
PER1     EQU   *              LETZTE PERIODE
         LA    R2,1
         B     PEROK
PER2     EQU   *              VORLETZTE PERIODE
         LA    R2,2
PEROK    EQU   *
         L     R9,8(,R11)               -->PERIODENINDEX
         STH   R2,0(,R9)                PERIODENINDEX
         SPACE 2
         SPACE 2
* RUECKSPRUNG
         L     R10,12(R11)
         XC    0(4,R10),0(R10)          RETURNCODE = 0
RETURN   EQU   *
         XRETURN 0
         SPACE 5
*  DEFINITIONEN
*
NC1      NC    ZWITEST(0),=X'F0F0'
CLC1     CLC   ZWITEST(0),=X'F0F0'
PACK1    PACK  DWORD,1(0,R3)
***********************************************************************
*                                                                     *
*    ENTRYPOINT = BERERG                                              *
*                                                                     *
*    AUFRUF     = CALL BERERG,(OPERAND,OPERATOR,ERGEBNIS)             *
*        PL10     OPERAND                                             *
*        H        OPERATOR = 1 ADDITION                               *
*                            2 SUBTRAKTION                            *
*        PL10     ERGEBNIS                                            *
*                                                                     *
*    ZWECK      = DER OPERAND WIRD LAUT OPERATIONSFELD MIT            *
*                 DEM ERGEBNISFELD VERKNUEPFT                         *
*                                                                     *
***********************************************************************
         EJECT
         ENTRY BERERG
BERERG   EQU   *
         DROP  R12
         XSAVE R12,SAVE,BERERG
         SPACE 1
         L     R3,0(R1)                 --> OPERAND
         L     R4,8(R1)                 --> ERGEBNIS
         L     R5,4(R1)                 --> OPERATOR
         SPACE 1
         CLI   1(R5),1
         BE    ADD                      ADDITION
         CLI   1(R5),2
         BE    SUB                      SUBTRAKTION
RETURNB  EQU   *
         XRETURN 0
         SPACE 2
*  ADDITION
ADD      EQU   *
         AP    0(10,R4),0(10,R3)
         B     RETURNB
         SPACE 2
*  SUBTRAKTION
SUB      EQU   *
         SP    0(10,R4),0(10,R3)
         B     RETURNB
SAVE     DC    18F'0'
DWORD    DS    D
LEN      DS    F
ZWITEST  DS    CL2
ODER     EQU   1
         END
