 ./       NUMBER SEQ1=100,INCR=100
MFP2     TITLE 'MFP2 MULTI-FUNKTION-PROGRAM      UNALLOACTION-ROUTINE'
         PRINT ON,NOGEN
IGC02240 CSECT
*              REG 2 FUER BASIS-REGISTER
*              REG 3 ADRESSE COMMUNICATION VECTOR TABLE
*              REG 4 ADRESSE TASK-CONTROL-BLOCK
*              REG 5 BEREICHE IM DYN. HAUPTSPEICHER
*              REG 6 TIOTADR
*              REG 7 UCB-ADRESSE
*              REG  8 ARBEITSREGISTER
*              REG  9 ARBEITSREGISTER
*              REG 10 ARBEITSREGISTER
*              REG 13 SAVEAREA
*              REG 14 LINKAGE
*              REG 15 LINKAGE
         USING CVT,3
         USING TCB,4
         USING DUMMYS,5
         USING TIOT,6
         USING UCB,7
WORK2    EQU   7
CVTR     EQU   8
WORK1    EQU   9
INDEXR   EQU   10
         SAVE  (14,12)
         BALR  2,0
         USING *,2
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         SR    15,15
         L     5,0(1)
         L     3,16
         L     4,CVTTCBP
         L     4,4(4)
CEOF     L     6,TCBTIO
         AH    6,DCBEOF+2
         CLC   4(8,6),=C'HREAD   '
         BE    CONA1E
         CLC   4(8,6),=C'LISTE   '
         BE    COND1E
         CLC   4(8,6),=C'LISTE1  '
         BE    COND2E
         CLC   4(8,6),=C'HSTANZ  '
         BE    CONS1E
         WTO   'MFP UNGUELTIGER DCB',ROUTCDE=13
CEOF2    B     SUBCON0
CONA1E   TM    SCHALTER,B'00000001'
         BZ    SUBCON0
         MVC   DDNAME,=C'HREAD   '
         BAL   11,UNALLOC              BRANCH ZUR DEALLOCATION-ROUTINE
         MVC   ONLINE+17(3),UCBNAME
CONA1E1  MVC   DDNAME,=C'LESER   '
         BAL   11,UNALLOC              BRANCH ZUR DEALLOCATION-ROUTINE
         MVI   ONLINE+20,C'-'
         MVC   ONLINE+21(3),UCBNAME
         NI    SCHALTER,B'11111110'     A1-AUS
         B     SUBCON2
CONS1E   TM    SCHALTER,B'00000100'
         BZ    SUBCON0
         MVC   DDNAME,=C'HSTANZ  '
         BAL   11,UNALLOC              BRANCH ZUR DEALLOCATION-ROUTINE
         MVC   ONLINE+17(3),UCBNAME
CONS1E1  MVC   DDNAME,=C'STANZER '
         BAL   11,UNALLOC              BRANCH ZUR DEALLOCATION-ROUTINE
         MVI   ONLINE+20,C'-'
         MVC   ONLINE+21(3),UCBNAME
         NI    SCHALTER,B'11111011'     S1-AUS
         B     SUBCON2
COND1E   TM    SCHALTER,B'00010000'
         BZ    SUBCON0
         MVC   DDNAME,=C'LISTE   '
         BAL   11,UNALLOC              BRANCH ZUR DEALLOCATION-ROUTINE
         MVC   ONLINE+17(3),UCBNAME
COND1E1  MVC   DDNAME,=C'PRINTER '
         BAL   11,UNALLOC              BRANCH ZUR DEALLOCATION-ROUTINE
         MVI   ONLINE+20,C'-'
         MVC   ONLINE+21(3),UCBNAME
         NI    SCHALTER,B'11101111'
         B     SUBCON2
COND2E   TM    SCHALTER,B'00100000'
         BZ    SUBCON0
         MVC   DDNAME,=C'LISTE1  '
         BAL   11,UNALLOC              BRANCH ZUR DEALLOCATION-ROUTINE
         MVC   ONLINE+17(3),UCBNAME
COND2E1  MVC   DDNAME,=C'PRINTER1'
         BAL   11,UNALLOC              BRANCH ZUR DEALLOCATION-ROUTINE
         MVI   ONLINE+20,C'-'
         MVC   ONLINE+21(3),UCBNAME
         NI    SCHALTER,B'11011111'
         B     SUBCON2
SUBCON0  LA    15,4
SUBCON2  ST    15,SAVE
ONLINE   WTO   'MFP UNIT XXX-YYY NOW ONLINE'
         L     15,SAVE
         XC    DDNAME(8),DDNAME
         L     13,SAVEAREA+4
         RETURN (14,12),RC=(15)
UNALLOC  EQU   *
         L     6,TCBTIO
         LA    6,24(0,6)
UNALLOC1 CLC   0(8,6),DDNAME
         BE    P8
         LA    6,1(0,6)
         B     UNALLOC1
P8       LH    7,14(0,6)
         CLI   18(7),X'20'
         BE    UNALLOCX
         MVC   14(2,6),=X'00'
         OI    SRTESTAT,B'10000000'    SET DEVICE ONLINE
UNALLOCX BR    11                      BRANCH RETURN
         TITLE 'MFP2 MULTI-FUNKTION-PROGRAM      LITERAL-POOL'
SAVEAREA DS    18F
DDNAME   DC    C'XXXXXXXX'
UNITNAME DC    C'XXX'
         LTORG
         PRINT ON,GEN
         TITLE 'MFP MULTI-FUNKTION-PROGRAM       COMMUNICATION-AREA'
DUMMYS   DSECT
PARM240  EQU   *             PARAMETER-LISTE AN IGC00240
SCHALTER DC    B'00000000'   BIT 0 = TIMER-EXIT
*                            BIT 1 = SUCHDATEI
*                            BIT 2 = HINITT
*                            BIT 3 = HDRUCK
*                            BIT 4 = WTOR PENDING
*                            BIT 5 = HSTANZ
*                            BIT 6 = DISPLAY ERROR-MESSAGE
*                            BIT 7 = HLESEN
CONBER   DS    0CL30         CONSOL-EINGABE-BEREICH
         DC    CL2' '
AKTION   DC    C' '
         DC    C' '
FUNKTION DC    CL8' '
OPTION1  EQU   FUNKTION+6
OPTION2  EQU   FUNKTION+7
         DC    C' '
INPUT    DC    CL3' '
         DC    C'-'
OUTPUT   DC    CL3' '
         DC    C','
VOLSER   DC    CL6' '
SEARCHKZ DC    C','
SEARCHDS DC    CL44' '
HLESEN   DS    CL82          FUNKTIONS-SATZ HLESEN
HDRUCK   DS    CL82          FUNKTIONS-SATZ HDRUCK
HDRUCK1  DS    CL82          FUNKTIONS-SATZ HDRUCK1
HSTANZ   DS    CL82          FUNKTIONS-SATZ HSTANZ
A1ZAE    EQU   HLESEN+12
D1ZAE    EQU   HDRUCK+12
D2ZAE    EQU   HDRUCK1+12
S1ZAE    EQU   HSTANZ+12
TIMERECB DC    X'00000000'
DCBEOF   DC    A(DCBEOF)     ADRRESSE DES DCB'S FUER FUNKTIONS-ENDE
SAVE     DS    1F
         DC    H'0'
UCBLIST  DS    0F
         DC    X'8000'
         DC    H'0'
VOL1     DS    0CL80         VOLUME-LABEL IMAGE
         DC    C'VOL1'
VOL1SER  DC    C'OSXXXX'
         DC    C'0'
         DC    69CL1'0'
VOLIST   DC    H'1'          NUMBER OF VOLUMES
         TITLE 'MFP3 MULTI-FUNKTION-PROGRAM      C V T '
CVT      DSECT
         CVT   SYS=INT
         TITLE 'MFP3 MULTI-FUNKTION-PROGRAM      T C B '
TCB      TCB
         TITLE 'MFP3 MULTI-FUNKTION-PROGRAM      U C B '
UCB      DSECT
         IEFUCBOB
         TITLE 'MFP3 MULTI-FUNKTION-PROGRAM      T I O T '
TIOT     DSECT
         IEFTIOT1
         END
