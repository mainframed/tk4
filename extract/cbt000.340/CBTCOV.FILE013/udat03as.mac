         TITLE 'SPTSO01 - TSOMCP-EXIT'
*
*        P R O L O G
*
*        PROGRAMMIERER     STOEHLER
*
*        ERSTELLUNGSDATUM  OKTOBER 1976
*
*        KURZBESCHREIBUNG  DAS MODULE SPTSO01 BEKOMMT VOM TSOMH AUS DEM
*                          MCP KONTROLLE BEI JEDEM TCAM-PUFFER. DER
*                          PUFFER WIRD ANALYSIERT UND AUFGRUND VON LOG-
*                          COMMANDS WERDEN ENTSPRECHENDE AKTIVITAETEN
*                          DURCHGEFUEHRT.
*
*        PARAMETER         R1 = A(CURRENT BUFFER)
*                          R15 = EP-ADDR. VON SPTSO01
*                          R14 = A(NOPR-INSTRUCTION)
*                                0700 = INCOMMING SUBGROUP
*                                0701 = OUTGOING SUBGROUP
*
*        RETURN            R15  = 0  -> OKAY
*                          R15 ^= 0  -> A(PARAMETER-LISTE) (DS 2A)
*                                       1.ADDR. = MSGGEN-AREA
*                                       2.ADDR. = MHPUT-AREA
*
*        SAEMTLICHE ANGABEN FUER EINEN BENUTZER DER LOG-FUNKTIONEN
*        STEHEN IN DER TABELLE 'LCBTAB'.
*        DIE ANZAHL DER TABELLEN-GLIEDER SOLLTE DER ANZAHL TERMINALS
*        ENTSPRECHEN ODER GROESSER SEIN.
*        DER AUFBAU DER TABELLE KANN NACHFOLGENDER DSECT ENTNOMMEN
*        WERDEN.
*
LCBTABLE DSECT
LCBADDR  DS    XL3
LCBBIT1  DS    X
LCBBIT2  DS    X
LCBQUAL  DS    CL3
LCBDCBA  DS    A
LCBCOUNT DS    H
LCBEOV   DS    X
         DS    X
*
*        DIE SCHALTER 'LCBBIT1' UND 'LCBBIT2' HABEN FOLGENDE
*        BEDEUTUNGEN:
*
READYBIT EQU   128                      ON WENN IM READY-STATUS
LOGINPR  EQU   64                       ON WENN LOG IN PROCESS
HELPUNT  EQU   16                       ON WENN HELP-COMMAND GEGEBEN
BUFTRUNC EQU   8                        ON WENN AUSGABEPUFFER TRUNC.
LISTCOMM EQU   4                        ON WENN LIST-COMMAND GEGEBEN
SPFBIT   EQU   2                        ON WENN SPF-COMMAND GEGEBEN
DUMPBIT  EQU   1                        ON WENN LOG DUMP GEGEBEN
*
TIMEBIT  EQU   128                      ON WENN LOG TIME GEGEBEN
PA1SPF   EQU   64                       ON WENN IN SPF PA1 GEGEBEN
DUMMYENT EQU   32                       ON WENN NUR DUMMY-ENTRY
PFKBIT   EQU   16                       ON WENN LOG PFK GEGEBEN
*
*        NACHFOLGENDE GLOBALS SIND LEDIGLICH DEM SCHNELLEREN AENDERN
*        VON KEYLEN UND BUFSIZE AN VERSCHIEDENEN STELLEN DIESE
*        PROGRAMMS VORBEHALTEN.
*
*
         GBLA  &KEYLEN,&BUFSIZE
         GBLC  &QUALIFY,&UNIT
         SPACE 2
&KEYLEN  SETA  XXX                      *** AUS MCP UEBERNEHMEN ***
&BUFSIZE SETA  XXXX                     *** AUS MCP UEBERNEHMEN ***
&QUALIFY SETC  'TSL'   FIRST INDEX-LEVEL DER LOG-DATEI
&UNIT    SETC  '3330'   UNITNAME F. DYNAMIC. ALLOC. DER LOG-DATEI
         EJECT
SPTSO01  CSECT
         B     100(15)
         DC    CL12'SPTSO01/0277'
SV01     DC    18F'0'
         DC    A(SV01+8192,SV01+4096,SV01)
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING SV01,R13,R12,R11
         USING LCBTABLE,R4
         STM   14,12,12(13)
         L     12,96(15)
         ST    12,8(13)
         ST    13,4(12)
         LM    R11,R13,88(15)
         SPACE 3
         XC    RETCODE(12),RETCODE
         NI    S,255-16-8
         CLI   1(R14),1
         BE    A01A
         LH    2,18(1)
         LA    5,45(1)
         SH    2,=H'33'
         BNP   PFKFUNKT
         MVC   MHPUTC,42(1)
A00      EQU   *
         SH    R2,=H'4'
         BM    A01
         EX    R2,SCANTRT
         BE    A01
         EJECT
         NOP   A00B
         OI    *-3,240
         L     R15,16                  CVT
         SH    R15,=H'4'
         CLC   0(2,R15),=C'03'
         BNE   DISABLE
         MVI   0(R13),C'T'
TSOSTART DMPRINT
         L     R1,=V(GMTSR15A)
         ST    R1,GMTSR15
A00B     EQU   *
         TM    0(R1),X'A2'
         BO    A00X
         TM    0(R1),X'93'
         BO    A00A
         TM    0(R1),X'96'
         BO    A00A
         B     A01
A00X     EQU   *
         MVC   ZD,0(R1)
         OC    ZD,BLANKS
         CLC   ZD(3),=C'SPF'
         BNE   A01
         OI    S,8
         LA    R3,=C'OFF '
         B     LOGR1
A00A     EQU   *
         MVC   ZD,0(R1)
         OC    ZD,BLANKS
         CLC   ZD(2),=C'L '
         BE    A00Z
         CLC   ZD(4),=C'LIST'
         BE    A00Z
         CLC   ZD(4),=C'OUT '
         BE    A00Z
         CLC   ZD(7),=C'OUTPUT '
         BE    A00Z
         CLC   ZD(3),=C'LOG'
         BNE   A01
         ST    R1,SCANSAVE
         LA    R2,3(R5)
         SR    R1,R2
         STH   R1,SCANLEN
         CLC   ZD(4),=C'LOG '
         BE    LOGR
         CLC   ZD(6),=C'LOGOFF'
         BE    LOGOFF
         CLC   ZD(5),=C'LOGON'
         BE    LOGON
         B     A01
         EJECT
A00Z     EQU   *
         OI    S,16                    LIST-COMMAND
A01      EQU   *
         L     R1,4(R13)
         L     R1,24(R1)
A01A     MVC   LCBADR,13(1)
         LA    R14,CREATE
         BAL   R10,SUCHTAB
A02      EQU   *
         TM    LCBBIT1,LOGINPR
         BO    LOG1
         TM    LCBBIT1,SPFBIT
         BO    SPFEND
RETURN   EQU   *
         LA    R0,240                  ID
EVERDUMP NOP   DUMP
S1       NOP   LOGEND1
S2       NOP   SESSCANC
S3       NOP   STPRINT
RETURNX  DMEND RC=RETCODE
         SPACE 5
ABEND    EQU   *
TSOABEND DMPRINT REGS,LCBTAB(10*16)
         LR    R2,R12
         L     R3,=A(SHIFTBER)
         LA    R3,&BUFSIZE.(R3)
TSOABND2 DMPRINT (2)
         XC    RETCODE(12),RETCODE
DISABLE  EQU   *
         LR    R1,R13
         SH    R1,=H'16'
         MVC   0(4,R1),IEFBR14
         B     RETURNX
IEFBR14  SR    15,15
         BR    14
         EJECT
LOGR     EQU   *
         BAL   R14,EDITPARM
LOGR1    EQU   *
         L     R1,4(R13)
         L     R1,24(R1)
         MVC   MHPUT(6),=X'0000000F0000'
         MVC   MHPUTC,42(R1)
         MVC   MHPUTD+2(4),LOG
         MVI   MHPUTD,64
         MVI   MHPUTD+1,64
         L     R15,16
         SH    R15,=H'4'
         CLC   0(2,R15),=C'03'
         BNE   *+8
         MVI   MHPUTD,C'%'
         CLC   0(3,R3),=C'ON '
         BE    LOGEIN
         CLC   0(2,R3),=C'S '
         BE    STATUS
         MVC   LCBADR,13(R1)
         LA    R14,A01
         BAL   R10,SUCHTAB
         CLC   0(5,R3),=C'DUMP '
         BE    LOGDUMP
         CLC   0(5,R3),=C'TIME '
         BE    LOGTIME
         CLC   0(4,R3),=C'PFK '
         BE    LOGPFK
         CLC   0(4,R3),=C'OFF '
         BE    LOGAUS
         TM    LCBBIT1,READYBIT      READY-BIT NACHFOLGENDE COMMANDS
         BO    LOGR2                           KOENNEN NUR IM READY-
*                                              MODE GEGEBEN WERDEN.
         TM    LCBBIT2,DUMMYENT
         BO    INVALID
         TM    LCBBIT1,LOGINPR
         BO    INVALID
LOGR2    EQU   *
         CLC   0(6,R3),=C'PRINT '
         BE    LOGPRINT
         CLC   0(4,R3),=C'DEL '
         BE    LOGDEL
         CLC   0(5,R3),=C'EDIT '
         BE    LOGEDIT
         B     INVALID
         SPACE 3
LOGEIN   EQU   *
         MVC   LCBADR,13(R1)
         LA    R14,TOOMUCH
         BAL   R10,SUCHTAB
         TM    LCBBIT1,LOGINPR
         BO    INVALID
         TM    LCBBIT2,DUMMYENT
         BZ    LOGEIN0
         NI    LCBBIT2,255-DUMMYENT
         LR    R3,R1
         GETMAIN EC,LV=6520,A=ZF
         LTR   R15,R15
         BNZ   TOOMUCH
         FREEMAIN E,LV=6520,A=ZF
         LR    R1,R3
         BAL   R9,CREATDCB
         BAL   R10,SORTTAB
         LA    R0,*
         LA    R14,ABEND
         BAL   R10,SUCHTAB
         SPACE 2
LOGEIN0  EQU   *
         OI    LCBBIT1,LOGINPR
         MVC   AUSG(2),=H'44'
         MVC   AUSG+4(40),=CL40'1***LOG*** BEGIN '
         BAL   R10,DATUM
         MVC   AUSG+22(22),DATETIME
         BAL   R10,PUTR
         MVI   AUSG+4,C'-'
         MVI   MHPUTD+1,C'V'
         TM    S,64
         BZ    LOGEIN01
         NI    S,255-64
         MVI   MHPUTD+1,C'X'
LOGEIN01 EQU   *
         MVC   MHPUTD+6(3),LCBQUAL
         SPACE 2
LOGEXIT  EQU   *
         BAL   R10,NOPROC
         LA    R4,MHPUT
         ST    R4,RC2
         LA    R4,RC1
         ST    R4,RETCODE
         B     A01
         SPACE 3
LOGAUS   EQU   *
         TM    LCBBIT1,LOGINPR
         BZ    LOGAUS0
         BAL   R8,LOGEND
         NI    LCBBIT1,255-LOGINPR
         BAL   R9,LOGENDMS
         TM    S,8
         BO    LOGAUS1
         MVI   MHPUTD+1,C'Y'
         MVI   MHPUT+3,11
         B     LOGEXIT
LOGAUS0  EQU   *
         TM    S,8
         BZ    INVALID
LOGAUS1  EQU   *
         OI    LCBBIT1,SPFBIT          SPF-BIT
         B     A01
         SPACE 2
LOGEDIT  EQU   *
         OI    S,32
         B     LOGDEL
LOGPRINT EQU   *
         LA    R14,AUSG+4
         LH    R15,AUSG
LOGPR1   CLC   0(5,R14),=C'CLASS'
         BE    LOGPR2
         LA    R14,1(R14)
         BCT   R15,LOGPR1
         B     LOGPR3
LOGPR2   OI    S,2
         MVC   CLASSS,0(R14)
LOGPR3   EQU   *
         OI    S,128
LOGDEL   EQU   *
         TM    LCBBIT2,DUMMYENT
         BO    INVALID
         BAL   R8,LOGEND
         L     R5,LCBDCBA
         BAL   R9,LOGENDMS
         CLOSE ((R5))
         FREEPOOL ((R5))
         MVC   MHPUTD+6(3),LCBQUAL
         TM    S,128
         BZ    LOGEXT1
         MVI   MHPUTD+1,C'Z'
         NI    S,255-128
         TM    S,2
         BZ    LOGEXT2
         NI    S,255-2
         MVI   MHPUTD+9,64
         MVC   MHPUTD+10,CLASSS
         MVI   MHPUT+3,25
         B     LOGEXT2
LOGEXT1  EQU   *
         TM    S,32
         BZ    LOGEXT1A
         MVI   MHPUTD+1,C'S'
         NI    S,255-32
         B     LOGEXT2
LOGEXT1A EQU   *
         MVI   MHPUTD+1,C'W'
LOGEXT2  EQU   *
         BAL   R9,CLEARDCB
         B     LOGEXIT
         SPACE 2
LOGEND   EQU   *
         OI    S1+1,240
         ST    R8,SAVER8
         B     A02
LOGEND1  EQU   *
         NI    S1+1,15
         L     R8,SAVER8
         BR    R8
         SPACE 2
LOGDUMP  EQU   *
         MVI   MHPUTD+1,C'T'
         MVI   MHPUT+3,11
         TM    LCBBIT1,DUMPBIT
         BO    LOGDUMP1
         OI    LCBBIT1,DUMPBIT
         B     LOGEXIT
LOGDUMP1 EQU   *
         NI    LCBBIT1,255-DUMPBIT
         B     LOGEXIT
         SPACE 2
LOGTIME  EQU   *
         MVI   MHPUTD+1,C'T'
         MVI   MHPUT+3,11
         TM    LCBBIT2,TIMEBIT
         BO    LOGTIME1
         OI    LCBBIT2,TIMEBIT
         B     LOGEXIT
LOGTIME1 EQU   *
         NI    LCBBIT2,255-TIMEBIT
         B     LOGEXIT
         SPACE 2
LOGPFK   EQU   *
         MVI   MHPUTD+1,C'T'
         MVI   MHPUT+3,11
         TM    LCBBIT2,PFKBIT
         BO    LOGPFK1
         OI    LCBBIT2,PFKBIT
         B     LOGEXIT
LOGPFK1 EQU    *
         NI    LCBBIT2,255-PFKBIT
         B     LOGEXIT
         SPACE 3
INVALID  EQU   *
         LA    R14,INVALIDM
         BAL   R10,NOPROCE
         B     LOGEXIT
         SPACE 5
CREATE   EQU   *
         LA    R5,LCBTAB
         LH    R6,LCBANZ
         LA    R4,ZD
         CH    R6,LCBANZ1
         BNL   TOOMANY
         LR    R4,R6
         LA    R6,1(R6)
         STH   R6,LCBANZ
         MH    R4,=H'16'
         LA    R4,0(R5,R4)
         XC    LCBADDR(16),LCBADDR
         MVC   LCBADDR,13(R1)
         OI    LCBBIT2,DUMMYENT
         OI    LCBBIT1,READYBIT
         BAL   R10,SORTTAB
         B     RETURN
TOOMANY  EQU   *
         B     RETURNX
         SPACE 5
CLEAR    EQU   *
         TM    S,4
         NI    S,255-4
         BO    CLEAR1
         MVC   LCBADDR,=3X'FF'
         BAL   R10,SORTTAB
         LH    R4,LCBANZ
         BCTR  R4,0
         STH   R4,LCBANZ
         BR    R9
CLEAR1   XC    LCBBIT1(13),LCBBIT1
         OI    LCBBIT2,DUMMYENT
         OI    LCBBIT1,READYBIT
         BR    R9
         EJECT
CREATDCB EQU   *
         GETMAIN R,LV=104
         ST    R1,LCBDCBA
         MVC   0(96,R1),LOGDCB
NEWQUAL  EQU   *
         TIME
         ST    R0,ZD
         SR    R1,R1
         IC    R1,ZD
         NI    ZD,0
         MH    R1,ZD
         MVC   ZD+1(1),ZD+2
         MH    R1,ZD
         LR    R2,R1
         SRDA  R2,32
         D     R2,=F'36'
         STC   R2,ZF+1
         NI    ZD+2,0
         MH    R1,ZD+2
         LR    R2,R1
         SRDA  R2,32
         D     R2,=F'36'
         STC   R2,ZF+2
         OR    R2,R3
         SRDA  R2,32
         D     R2,=F'36'
         STC   R2,ZF
         TR    ZF(3),TRANSL
         MVC   LCBQUAL,ZF
         L     R3,LCBDCBA
         MVC   45(3,R3),LCBQUAL
         MVC   QUALIF(3),LCBQUAL
         MVC   DDNAME,40(R3)
         LA    R1,S99RBPTR
         DYNALLOC
         LTR   R15,R15
         BNZ   NEWQUAL
         MVI   96(R3),X'11'
         MVC   97(3,R3),=AL3(DCBABEND)
         MVI   100(R3),X'86'
         MVC   101(3,R3),=AL3(DCBEOVX)
         LA    R5,96(R3)
         ST    R5,ZF
         MVC   37(3,R3),ZF+1
         OI    S,64
         L     R2,LCBDCBA
         OPEN ((R2),(OUTPUT))
         TM    48(R2),16
         BOR   R9
         LA    R15,FEHL5
         ST    R15,RC1
         LA    R15,RC1
         ST    R15,RETCODE
         BAL   R9,CLEARDCB
         B     RETURN
         EJECT
TOOMUCH  EQU   *
         BAL   R10,TMUCH
         B     A01
TMUCH    EQU   *
         LA    R2,FEHL1
         ST    R2,RC1
         MVI   MHPUT+3,11
         MVI   MHPUTD+1,C'R'
         LA    R2,MHPUT
         ST    R2,RC2
         LA    R2,RC1
         ST    R2,RETCODE
         BR    R10
         SPACE 5
LOGON    EQU   *
         OI    S,4
LOGOFF   EQU   *
         L     R1,4(R13)
         L     R1,24(R1)
         MVC   LCBADR,13(R1)
         LA    R14,CREATE
         BAL   R10,SUCHTAB
         TM    LCBBIT2,DUMMYENT
         BO    LOGONOFF
         CLI   ZD+7,C'F'
         BE    LOGONOFF
         MVC   MHPUT(6),=X'0000000C0000'
         MVC   MHPUTC,42(R1)
         MVC   MHPUTD(6),=C'%ULOG '
         NI    S,255-4
         B     LOGEXIT
LOGONOFF EQU   *
         BAL   R9,CLEAR
         B     RETURN
         SPACE 5
SPFEND   EQU   *
         L     R15,4(R13)
         L     R15,12(R15)
         TM    1(R15),1
         BO    SPFEND1
         NI    LCBBIT2,255-PA1SPF
         LH    R15,18(R1)
         SH    R15,=H'33'
         BNP   SPFEND2
         B     RETURN
SPFEND1  CLC   48(5,R1),=C'READY'
         BNE   RETURN
         TM    LCBBIT2,PA1SPF
         BO    RETURN
         NI    LCBBIT1,255-SPFBIT      SPF-BIT
         TM    LCBBIT2,DUMMYENT
         BO    RETURN
         OI    LCBBIT1,LOGINPR
         MVC   AUSG(2),=H'43'
         MVC   AUSG+4(39),=CL39'-***LOG*** CONT '
         BAL   R10,DATUM
         MVC   AUSG+21(22),DATETIME
         BAL   R10,PUTR
         MVI   AUSG+4,C'-'
         B     A01
SPFEND2  EQU   *
         LA    R15,42(R1)
         CLI   0(R15),X'6C'             PA1
         BNE   RETURN
         OI    LCBBIT2,PA1SPF
         B     RETURN
         EJECT
CLEARDCB EQU   *
         L     R1,LCBDCBA
         FREEMAIN R,LV=104,A=(1)
         XC    LCBBIT1(13),LCBBIT1
         OI    LCBBIT2,DUMMYENT
         OI    LCBBIT1,READYBIT
         BR    R9
         SPACE 5
LOGENDMS EQU   *
         MVC   AUSG(2),=H'42'
         MVC   AUSG+4(38),=CL38'-***LOG*** END '
         BAL   R10,DATUM
         MVC   AUSG+20(22),DATETIME
         TM    S,8
         BZ    LOGENDM1
         MVC   AUSG+15(3),=C'INT'
         BAL   R10,PUTR
         MVC   AUSG(2),=H'42'
         MVC   AUSG+4(38),=CL38'-LOG018I SPF IN PROCESS - LOG STOPPED'
LOGENDM1 EQU   *
         BAL   R10,PUTR
         BR    R9
         SPACE 5
NOPROC   EQU   *
         CLI   MHPUTD+1,C'X'
         BNE   NOPROC1
         MVC   XLOGQU,QUALIF
         LA    R14,XLOGMESS
         B     NOPROCE
NOPROC1  EQU   *
         CLI   MHPUTD+1,C'V'
         BNE   NOPROC2
         MVC   VLOGQU,QUALIF
         LA    R14,VLOGMESS
         B     NOPROCE
NOPROC2  EQU   *
         CLI   MHPUTD+1,C'Y'
         BNE   NOPROC3
         LA    R14,YLOGMESS
         B     NOPROCE
NOPROC3  EQU   *
         CLI   MHPUTD+1,C'R'
         BNE   NOPROC4
         LA    R14,RLOGMESS
         B     NOPROCE
NOPROC4  EQU   *
         CLI   MHPUTD+1,C'T'
         BNER  R10
         LA    R14,TLOGMESS
NOPROCE  EQU   *
         ST    R14,RC1
         MVC   MHPUT(6),=X'000000070000'
         MVC   MHPUTC,42(R1)
         MVI   MHPUTD,64
         TM    LCBBIT1,LOGINPR
         BZR   R10
         OI    S3+1,240
         BR    R10
         EJECT
SORTTAB  EQU   *
         CLC   LCBANZ,=H'1'
         BNH   SORTTABE
         DMTSORT LCBTAB,LCBANZ,16,FIELDS=(0,3,,A)
SORTTABE BR    R10
         EJECT
SUCHTAB  EQU   *
         LH    R4,LCBANZ
         LTR   R4,R4
         BCR   8,R14
         DMTSUCH LCBTAB,                                               X
               LCBANZ,                                                 X
               16,                                                     X
               LCBADR,                                                 X
               REG=4,                                                  X
               NFOUND=(R14)
         BR    R10
         EJECT
DATUM    EQU   *
         DMDAT DATUM=DATETIME,ZEIT=DATETIME+10
         BR    R10
         SPACE 5
EDITPARM EQU   *
         MVC   AUSG+4(256),BLANKS
         L     R1,4(R13)
         L     R1,24(R1)
         LH    R2,18(R1)
         SH    R2,=H'37'
         SH    R2,SCANLEN
         L     R3,SCANSAVE
         EX    R2,MOVEPARM
         EX    R2,OCINSTR
         OI    AUSG+4,64
         STH   R2,AUSG
         LR    R3,R2
         EX    R3,TRTPARM
         BE    A01
         LR    R4,R1
         LA    R5,AUSG+4
         AR    R3,R5
         SR    R3,R4
         LR    R1,R4
         EX    R3,TRTPARM1
         LR    R3,R1
         BR    R14
         EJECT
LOG1     EQU   *
         TM    LCBBIT1,DUMPBIT
         BZ    *+8
         OI    EVERDUMP+1,240
         L     R14,4(R13)
         L     R14,12(R14)
         XC    SHIFTLEN,SHIFTLEN
         TM    1(14),1
         BO    OUTPUT
         LA    R5,45(R1)
         LH    3,18(1)
         SH    3,=H'33'
         BNP   STEUER
         NI    LCBBIT1,255-READYBIT-HELPUNT-LISTCOMM
         TM    S,16
         BZ    LOG1B
         OI    LCBBIT1,LISTCOMM
         NI    S,255-16
LOG1B    EQU   *
         TM    0(5),X'11'
         BO    LOG1A
         CLI   0(5),2
         BE    STEUER
         B     DUMP
LOG1A    EQU   *
         LA    R3,2(R3)                 WG/RDW + ASA
         STH   R3,AUSG
         SH    R3,=H'6'
         EX    3,MOVE
         EX    3,OCINSTR
         BAL   10,PUTR
         CLC   AUSG+5(2),=C'H '
         BE    HELPBIT
         CLC   AUSG+5(5),=C'HELP '
         BNE   RETURN
HELPBIT  OI    LCBBIT1,HELPUNT
         MVC   AUSG(HELPEND-HELPMSG),HELPMSG
         BAL   R10,PUTR
         B     RETURN
         EJECT
OUTPUT   EQU   *
         NI    LCBBIT1,255-READYBIT
         TM    LCBBIT1,HELPUNT
         BO    RETURN
         CLC   18(R1),=H'0'
         BE    RETURN
         LH    R8,18(R1)
         LA    R3,42(R1)
         LA    R2,&KEYLEN
         CH    R2,18(R1)
         BNL   BUFF1
         BAL   R14,SHIFT
         LH    R8,SHIFTLEN
         B     BUFF2
BUFF1    EQU   *
         SH    R8,=H'30'
BUFF2    EQU   *
         MVC   AUSG+5(3),BLANKS
         CLI   0(R3),X'C1'
         BNE   SESSCAN
         LA    R3,1(R3)
         BCTR  R8,0
         CLI   3(R3),X'1D'
         BE    SBA
         LA    R3,1(R3)
         BCTR  R8,0
         CLC   11(17,R3),=C'SESSION CANCELLED'
         BNE   FIELD
         BCTR  R3,0
         LA    R8,1(R8)
         OI    S2+1,240
         B     SF
SBA      EQU   *
         SR    R0,R0
         CLI   0(R3),X'11'
         BNE   DUMP
         LA    R3,3(R3)
         SH    R8,=H'3'
         CLI   0(R3),X'13'
         BE    READY
SF       EQU   *
         CLI   0(R3),X'1D'
         BNE   DUMP
         CLI   1(R3),X'40'
         BNE   FIELD
         LA    R3,2(R3)
         BCTR  R8,0
         BCTR  R8,0
         B     SBA
READY    EQU   *
         NI    FIELDEND+1,15
         CLC   AUSG+8(6),=C'READY '
         BNE   RETURN
         OI    LCBBIT1,READYBIT
         B     RETURN
SESSCAN  EQU   *
         CLC   18(R1),=H'30'
         BNH   RETURN
         CLI   0(R3),X'C3'
         BNE   DUMP
* SESSION CANCELLED
         OI    S2+1,240
         LA    R3,1(R3)
         BCTR  R8,0
         B     SBA
SESSCANC EQU   *
         L     R5,LCBDCBA
         BAL   R9,LOGENDMS
         CLOSE ((R5))
         FREEPOOL ((R5))
         MVC   FEHL6+42(3),LCBQUAL
         LA    R15,FEHL6
         ST    R15,RC1
         LA    R15,RC1
         ST    R15,RETCODE
         BAL   R9,CLEARDCB
         NI    S2+1,15
         B     RETURN
         SPACE 2
FIELD    EQU   *
         SR    R2,R2
         LA    R0,255
         CR    R8,R0
         BL    FIELDTRT
         LR    R15,R8
         LR    R8,R0
FIELDTRT EQU   *
         EX    R8,TRTEXEC
         BE    DUMP
         B     *+4(R2)
         B     DUMP
         B     FIELD11
FIELDX   EQU   *
         LA    R1,0(R1)
         LR    R2,R1
         SR    R1,R3
         CR    R8,R0
         BNE   FIELDX1
         LR    R8,R15
FIELDX1  EQU   *
         SR    R8,R1
         BP    FIELDX2
         AR    R1,R8
         OI    FIELDEND+1,240
FIELDX2  EQU   *
         SH    R1,=H'3'
         BM    LEER
         EX    R1,MOVEOUT
         EX    R1,OCINOUT
         LA    R1,9(R1)
         STH   R1,AUSG
FIELDEX  NOP   FIELD11E
         LR    R3,R2
         BAL   R10,PUTR
FIELDEND NOP   READY
         B     SF
LEER     EQU   *
         MVC   AUSG(2),=H'6'
         B     FIELDEX
FIELD11  EQU   *
         CLI   3(R1),X'13'
         BNE   DUMP
         OI    FIELDEX+1,240
         B     FIELDX
FIELD11E EQU   *
         NI    FIELDEX+1,15
         LR    R3,R2
         B     SBA
         EJECT
SHIFT    EQU   *
         ST    R14,SAVE14
         LA    R14,SHIFTTAB
         ST    R1,0(R14)
         LA    R14,4(R14)
         LH    R3,18(R1)
         LA    R15,&BUFSIZE
         CR    R3,R15
         BH    DUMP
         L     R15,=A(SHIFTBER)
         MVC   0(&KEYLEN-30,R15),42(R1)
         LA    R15,&KEYLEN-30(R15)
         L     R1,8(R1)
         ST    R1,0(R14)
         LA    R14,4(R14)
         LA    R2,12(R1)
         SH    R3,=H'&KEYLEN'
SHIFT1   EQU   *
         CH    R3,=H'&KEYLEN'
         BNH   SHIFT2
         MVC   0(&KEYLEN,R15),0(R2)
         LA    R15,&KEYLEN.(R15)
         L     R1,8(R1)
         CLC   4(4,R14),=F'-1'
         BE    SHIFT1A
         ST    R1,0(R14)
         LA    R14,4(R14)
SHIFT1A  EQU   *
         LA    R2,12(R1)
         SH    R3,=H'&KEYLEN'
         B     SHIFT1
SHIFT2   EQU   *
         BCTR  R3,0
         EX    R3,SHIFTMVC
         LA    R15,1(R3,R15)
         L     R3,=A(SHIFTBER)
         SR    R15,R3
         STH   R15,SHIFTLEN
         SR    R15,R15
         ST    R15,0(R14)
         L     R14,SAVE14
         BR    R14
         EJECT
DUMP     EQU   *
         NI    EVERDUMP+1,15
         STM   R0,R15,SAVEAREA
         CH    R0,=H'255'
         BE    ENDBUFF
DUMP0    EQU   *
         MVC   AUSG(2),=H'32'
         MVC   AUSG+4(28),=C'-LOG015I DUMP FORMAT PRINTED'
         BAL   10,PUTR
         CLC   SHIFTLEN,=H'0'
         BNE   DUMP1
         L     R6,4(R13)
         L     R6,24(R6)
         ST    R6,SHIFTTAB
         SR    R7,R7
         ST    R7,SHIFTTAB+4
DUMP1    EQU   *
         ZAP   ZAE,=P'0'
         LA    R6,SHIFTTAB-4
         MVC   TLAENG,=H'64'
         MVC   ZNAME(16),=C'REGISTERS 0 - 15'
         B     DUMP2
DUMP1A   EQU   *
         OI    ZAE+1,15
         UNPK  ZAEHL,ZAE
DUMP2    EQU   *
         L     R7,0(R6)
         LTR   R7,R7
         BZ    DUMP4
         ST    R6,DUMP2A
         L     R14,=A(SAVETSR)
         ST    4,0(R14)
         MVC   AUSG(2),=H'137'
         ZAP   ZZZ,=P'0'
         ZAP   ZBZ,=P'0'
         LA    R1,TSR15PRM              .
         L     R15,GMTSR15              .
         B     DUMP3                    AB HIER NICHTS EINFUEGEN !!!
DUMP2A   DS    A                        .
         DS    A                        .
DUMP3    EQU   *                        .
         BALR  R14,R15                  .
         NOP   0(0)                     BIS HIERHER NICHTS EINFUEGEN !
         MVC   TLAENG,=AL2(12+&KEYLEN.)
         MVC   ZNAME,=C'TCAM-BUFFER '
         LA    R6,4(R6)
         AP    ZAE,=P'1'
         B     DUMP1A
DUMP4    EQU   *
         MVI   AUSG+4,C'-'
         CLC   SHIFTLEN,=H'0'
         BE    RETURN
         LA    R6,SHIFTTAB
         L     R7,=A(SHIFTBER)
         ST    R7,0(R6)
         SR    R7,R7
         ST    R7,4(R6)
         MVC   TLAENG,SHIFTLEN
         XC    SHIFTLEN,SHIFTLEN
         MVC   ZNAME(16),=C'SHIFT - AREA    '
         B     DUMP2
         EJECT
ENDBUFF  EQU   *
         L     R0,=A(SHIFTBER)
         CR    R3,R0
         BL    TCAMBUFF
         LR    R1,R0
         LA    R0,&BUFSIZE.(R1)
         CR    R3,R0
         BH    TCAMBUFF
         AH    R1,SHIFTLEN
         SR    R1,R3
         B     MOVEBUFF
TCAMBUFF EQU   *
         L     R1,4(R13)
         L     R1,24(R1)
         LH    R0,18(R1)
         LA    R1,12(R1)
         AR    R1,R0
         SR    R1,R3
MOVEBUFF EQU   *
         SH    R1,=H'3'
         BM    DUMP0
         EX    R1,MOVEOUT
         EX    R1,OCINOUT
         LA    R1,9(R1)
         STH   R1,AUSG
         BAL   R10,PUTR
         B     RETURN
         EJECT
STEUER   EQU   *
         LA    6,STTAB
         SH    5,=H'3'
STKZ1    EQU   *
         CLC   0(1,5),0(6)
         BE    STKZ2
         CLI   0(6),255
         BE    HEXSTKZ
         LA    6,19(6)
         B     STKZ1
STKZ2    EQU   *
         CLI   0(R6),X'7D'
         BE    STKZ2A
         CLI   0(R6),X'6D'
         BE    STKZ2A
         NI    LCBBIT1,255-READYBIT-HELPUNT-LISTCOMM
         B     STKZ2B
STKZ2A   EQU   *
         TM    LCBBIT1,HELPUNT+LISTCOMM
         BNZ   RETURN
STKZ2B   EQU   *
         MVC   AUSG+5(9),1(6)
         CLI   10(R6),0
         BE    STKZ3
         MVC   AUSG+14(9),=C' INTERN: '
         MVC   AUSG+23(8),11(6)
         MVC   AUSG(2),=H'31'
         B     STKZ4
STKZ3    EQU   *
         MVC   AUSG(2),=H'14'
STKZ4    BAL   10,PUTR
         B     RETURN
HEXSTKZ  MVC   AUSG+5(9),1(6)
         UNPK  ZF(3),0(2,5)
         TR    ZF(2),TRANS-240
         MVC   AUSG+9(2),ZF
         B     STKZ3
         EJECT
PUTR     EQU   *
         L     1,LCBDCBA
         LA    0,AUSG
         LH    2,AUSG
         TM    LCBBIT2,TIMEBIT
         BO    PUTTIME
PUTR0    EQU   *
         SH    2,=H'138'
         BNM   PUTR2
PUTR1    L     15,48(1)
         BALR  14,15
         LH    R14,LCBCOUNT
         LA    R14,1(R14)
         CH    R14,=H'1024'
         BNL   PUTR1B
         STH   R14,LCBCOUNT
         B     PUTR1C
PUTR1B   EQU   *
         SH    R14,=H'1024'
         STH   R14,LCBCOUNT
         L     R1,LCBDCBA
         BAL   R14,DCBEOVX
PUTR1C   EQU   *
         MVI   AUSG+4,64
         XC    AUSG(4),AUSG
         BR    10
PUTR2    MVC   AUSG(2),=H'137'
         B     PUTR1
         SPACE 2
PUTTIME  EQU   *
         MVC   TAUSG1,AUSG
         LH    R1,AUSG
         LA    R1,12(R1)
         STH   R1,TAUSG1
         MVC   AUSG+1(4),BLANKS
         TIME
         STCM  R0,7,ZF
         UNPK  ZTIME,ZF
         MVC   TAUSGT,ZTSCHAB
         TR    TAUSGT,ZTIME
         LA    R0,TAUSG
         LH    R2,TAUSG
         L     R1,LCBDCBA
         B     PUTR0
         EJECT
DCBABEND EQU   *
         CLI   0(R13),C'T'
         BE    DCBAB1
         L     R13,4(R13)
         LM    R2,R12,28(R13)
         BR    R15
DCBAB1   EQU   *
         MVC   SAVEDCBA(16),0(R1)
         TM    3(R1),4
         BZ    DCBAE
         MVI   3(R1),0
         UNPK  ZD(7),0(4,R1)
         TR    ZD(6),TRANS-240
         MVI   ZD+3,C'-'
         LA    R2,FEHL2
         MVC   27(6,R2),ZD
         ST    R2,RC1
         MVC   LCBADR,5(R1)
         DMTSUCH LCBTAB,                                               X
               LCBANZ,                                                 X
               16,                                                     X
               LCBADR,                                                 X
               ORG=NS,                                                 X
               REG=4,                                                  X
               POS=9,                                                  X
               NFOUND=DCBAE
         LA    R2,RC1
         ST    R2,RETCODE
         XC    LCBBIT1(13),LCBBIT1
         OI    LCBBIT2,DUMMYENT
         OI    LCBBIT1,READYBIT
         MVI   3(R1),4
DCBAE    EQU   *
         B     RETURNX
         EJECT
DCBSYNAD EQU   *
         LA    R14,RETURN
         MVC   ZD1(4),=X'00100404'
         ST    R1,ZD1+4
         LA    R1,ZD1
         B     DCBABEND
DCBEOVX  EQU   *
         ST    R1,ZF
         MVC   LCBADR,ZF+1
         DMTSUCH LCBTAB,                                               X
               LCBANZ,                                                 X
               16,                                                     X
               LCBADR,                                                 X
               ORG=NS,                                                 X
               REG=4,                                                  X
               POS=9,                                                  X
               NFOUND=(R14)
         TR    LCBEOV,TRANS00
         CLI   LCBEOV,4
         BL    DCBEOVE
         CLI   LCBEOV,7
         BL    DCBEOV1
         LA    R15,FEHL4
         MVC   40(3,R15),LCBQUAL
         ST    R15,RC1
         XC    LCBBIT1(13),LCBBIT1
         OI    LCBBIT2,DUMMYENT
         OI    LCBBIT1,READYBIT
         B     RETURNX
DCBEOV1  EQU   *
         LA    R15,FEHL3
         ST    R15,RC1
         LA    R15,RC1
         ST    R15,RETCODE
DCBEOVE  EQU   *
         BR    R14
         EJECT
STATUS   EQU   *
* AUFBEREITEN DER STATUS-MELDUNG (MSGGEN)
         MVC   MHPUT(6),=X'000000070000'
         MVC   MHPUTC,42(R1)
         MVI   MHPUTD,64
         LA    R14,MHPUT
         ST    R14,RC2
         MVC   LCBADR,13(R1)
         LA    R14,RETURNX
         BAL   R10,SUCHTAB
         TM    LCBBIT2,DUMMYENT
         BO    STAUS
         MVC   STATUS2,LCBQUAL
         TM    LCBBIT1,LOGINPR
         BO    STATU1
         MVC   STATUS1,=C'AUS'
         B     STATU2
STATU1   EQU   *
         MVC   STATUS1,=C'EIN'
STATU2   EQU   *
         SR    R14,R14
         IC    R14,LCBEOV
         SLL   R14,10
         AH    R14,LCBCOUNT
         CVD   R14,ZD1
         OI    ZD1+7,15
         UNPK  STATUS3,ZD1
         UNPK  STATUS4(5),LCBBIT1(3)
         UNPK  STATUS4+4(7),LCBDCBA+1(4)
         TR    STATUS4,TRANS-240
         MVI   STATUSE,C''''
         MVC   STATUSMS+1(1),STATUSMS
         TM    LCBBIT1,LOGINPR
         BZ    STATEX
         OI    S3+1,240
         B     STATEX
STAUS    EQU   *
         MVC   STATUS1,=C'AUS'
         MVI   STATUSMS+1,12
STATEX   EQU   *
         LA    R14,STATUSMS+1
         ST    R14,RC1
*
         LA    R14,RC1
         ST    R14,RETCODE
         B     A01
         SPACE 2
STPRINT  EQU   *
         NI    S3+1,15
         L     R14,RC1
         SR    R10,R10
         IC    R10,0(R14)
         LA    R10,3(R10)
         STH   R10,AUSG
         BCTR  R10,0
         EX    R10,MOVESTA
         MVI   AUSG+8,C'*'
         BAL   R10,PUTR
         B     RETURN
         EJECT
PFKFUNKT EQU   *
         LA    R6,STTAB
         LA    R5,42(R1)
PFK01    EQU   *
         CLC   0(1,R5),0(R6)
         BE    PFK02
         CLI   0(R6),255
         BE    A00
         LA    R6,19(R6)
         B     PFK01
PFK02    EQU   *
         CLI   10(R6),0
         BE    A00
         MVC   LCBADR,13(R1)
         LA    R14,A00
         BAL   R10,SUCHTAB
         TM    LCBBIT1,SPFBIT
         BO    A00                      = SPF-BIT ON
         TM    LCBBIT2,PFKBIT
         BO    A00
         MVC   MHPUT(6),=X'0000000E0000'
         MVC   MHPUTD(8),11(R6)
         LA    R6,MHPUT
         ST    R6,RC2
         LA    R6,RC1
         ST    R6,RETCODE
         B     A00
         TITLE 'SPTSO01 - DEFINITIONEN'
SHIFTMVC MVC   0(0,R15),0(R2)
MOVE     MVC   AUSG+5(0),3(5)
OCINSTR  OC    AUSG+5(0),BLANKS
MOVEOUT  MVC   AUSG+8(0),2(R3)
OCINOUT  OC    AUSG+8(0),BLANKS
MOVEPARM MVC   AUSG+4(0),0(R3)
MOVESTA  MVC   AUSG+4(0),2(R14)
TRTPARM  TRT   AUSG+4(0),TRTBL
TRTPARM1 TRT   0(0,R4),TRTEXT
TRTEXEC  TRT   2(0,R3),TRT1D
SCANTRT  TRT   3(0,R5),TRTEXT
LOG      DC    C'LOG '
STTAB    DS    0F
         DC    X'7D',C'**ENTER**',AL1(0),CL8'        '
         DC    X'6D',C'**CLEAR**',AL1(0),CL8'        '
         DC    X'6C',C'***PA1***',AL1(0),CL8'        '
         DC    X'6E',C'**CNCL **',AL1(1),CL8'?       '
         DC    X'7A',C'**PFK10**',AL1(2),CL8'GO      '
         DC    X'7B',C'**PFK11**',AL1(3),CL8'YES     '
         DC    X'7C',C'**PFK12**',AL1(3),CL8'END     '
         DC    X'F1',C'**PFK 1**',AL1(8),CL8'%PF1PROC'
         DC    X'F2',C'**PFK 2**',AL1(8),CL8'%PF2PROC'
         DC    X'F3',C'**PFK 3**',AL1(8),CL8'%PF3PROC'
         DC    X'F4',C'**PFK 4**',AL1(8),CL8'%PF4PROC'
         DC    X'F5',C'**PFK 5**',AL1(8),CL8'%PF5PROC'
         DC    X'F6',C'**PFK 6**',AL1(8),CL8'%PF6PROC'
         DC    X'F7',C'**PFK 7**',AL1(8),CL8'%PF7PROC'
         DC    X'F8',C'**PFK 8**',AL1(8),CL8'%PF8PROC'
         DC    X'F9',C'**PFK 9**',AL1(8),CL8'%PF9PROC'
         DC    X'01',C'**TEST **',AL1(0),CL8'        '
         DC    X'FF',C'**X''  ''**'    LAST ENTRY
TRANS    DC    C'0123456789ABCDEF'
BLANKS   DC    CL256' '
TRTBL    DS    0F
         DC    64X'0',X'1',191X'0'
TRTEXT   DS    0F
         DC    64X'1',X'0',191X'1'
TRT1D    DS    0F
         DC    17X'0',X'4',11X'0',X'8',226X'0'
TRANSL   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'
TRANS00  DC    X'010203040506070809'
HELPMSG  DS    0H
         DC    AL2(HELPEND-HELPMSG)
         DC    H'0'
         DC    C'    LOG005I HELP-OUTPUT NOT PRINTED '
HELPEND  DS    0C
INVALIDM DC    AL1(INVALEND-INVALIDM-1)
         DC    C'       LOG000E INVALID USE OF LOG-COMMAND'
INVALEND DS    0C
XLOGMESS DC    AL1(XLOGME-XLOGMESS-1)
         DC    C'       LOG008I HARDCOPY-LOG TO &QUALIFY..LOG#'
XLOGQU   DS    CL3
XLOGME   DS    0C
VLOGMESS DC    AL1(VLOGME-VLOGMESS-1)
         DC    C'       LOG013I LOG TO &QUALIFY..LOG#'
VLOGQU   DS    CL3
         DC    C' AGAIN'
VLOGME   DS    0C
YLOGMESS DC    AL1(YLOGME-YLOGMESS-1)
         DC    C'       LOG014I LOG-WRITING STOPPED'
YLOGME   DS    0C
RLOGMESS DC    AL1(RLOGME-RLOGMESS-1)
         DC    C'       LOG020I TOOMUCH LOG-USERS ACTIVE '
RLOGME   DS    0C
TLOGMESS DC    AL1(TLOGME-TLOGMESS-1)
         DC    C'       LOG021I DUMP, PFK OR TIME BIT CHANGED'
TLOGME   DS    0C
STATUSMS DC    AL1(STATUSE-STATUSMS-1)
         DS    AL1
         DC    C'       S='
STATUS1  DS    CL3
         DC    C' D='
STATUS2  DS    CL3
         DC    C' R='
STATUS3  DS    CL8
         DC    C' / X'''
STATUS4  DS    CL10
STATUSE  DS    0C
FEHL1    DC    AL1(FEHL2-FEHL1-1)
         DC    C'LOG001E LOG-DATASET IS NOT AVAILABLE'
FEHL2    DC    AL1(FEHL3-FEHL2-1)
         DC    C'LOG002E LOG-DATASET ABEND ...... '
FEHL3    DC    AL1(FEHL4-FEHL3-1)
         DC    C'LOG003W LOG-DATASET NEAR FULL * * * * *'
FEHL4    DC    AL1(FEHL5-FEHL4-1)
         DC    C'LOG004E LOG-DATASET FULL - ENTER ''ZLOG ...'''
FEHL5    EQU   *
         DC    AL1(FEHL6-FEHL5-1)
         DC    C'LOG006E LOG-DATASET OPEN-ERROR'
FEHL6    EQU   *
         DC    AL1(FEHL7-FEHL6-1)
         DC    C'LOG019I TO PRINT LOG-DATASET '
         DC    C'ENTER ''ZLOG ...'''
FEHL7    EQU   *
         EJECT
         LTORG
         EJECT
RETCODE  DS    F
RC1      DS    F
RC2      DS    F
ZF       DS    F
ZD       DS    D
ZD1      DS    D
ZTIME    DS    CL7
         DC    C'.,'
ZTSCHAB  DC    X'0001070203080405'
SAVE14   DS    F
SAVER8   DS    F
SAVEAREA DC    16F'0'
SCANSAVE DS    A
SCANLEN  DS    H
SHIFTLEN DS    H
         DC    A(SAVEAREA)             HINTER DIESER DEFINITION NICHTS
*                                      EINFUEGEN !!!
SHIFTTAB DS    (&BUFSIZE./&KEYLEN.)F
         DC    F'-1'
TAUSG    DS    0F
TAUSG1   DS    CL5
TAUSGT   DS    0CL8
         DS    CL7
AUSG     DS    0F
         DS    H
         DC    H'0'
         DS    CL256
MHPUT    DS    0F
         DC    XL6'0'
MHPUTC   DS    XL6
MHPUTD   DS    CL80
S        DC    X'0'
CLASSS   DS    CL9
DATETIME DC    CL22' '
SAVEDCBA DC    4F'0'
DUMPEX   DC    A(DUMPEXIT)
GMTSR15  DS    A
ZAE      DC    PL2'0'
TSR15PRM DS    0F                       * AB HIER NICHTS AENDERN
         DC    X'FD',AL4(DUMPEX-48)     *
ZZZ      DC    PL2'0'                   *
ZBZ      DC    PL2'0'                   *
         DC    AL1(16)                  *
ZNAME    DS    CL12                     *
ZAEHL    DS    CL4                      *
         DC    X'D000'                  *
TLAENG   DS    AL2                      *
         DC    X'FF'                    * BIS HIER NICHTS AENDERN
*              DYNALLOC PARAMETER LIST
S99RBPTR DS    0F
         DC    X'80',AL3(S99RB)
S99RB    DS    0F
S99RBLN  DC    AL1(20)
S99VERB  DC    X'01'                   DSN-ALLOC
S99FLAG1 DC    H'0'
S99ERROR DC    H'0'
S99INFO  DC    H'0'
S99TXTPP DC    A(S99TUPL)
         DC    F'0'
S99FLAG2 DC    F'0'
S99TUPL  DS    0F
         DC    A(DDN,DISP1,DISP2,DISP3,UNIT)
         DC    A(SPACE1,SPACE2,SPACE3,SPACE4,DSN)
         DC    AL1(128),AL3(FREE)
DDN      DS    0H
         DC    X'0001',H'1',H'8'
DDNAME   DS    CL8
DISP1    DS    0H
         DC    X'0004',H'1',H'1',X'4'
DISP2    DS    0H
         DC    X'0005',H'1',H'1',X'2'
DISP3    DS    0H
         DC    X'0006',H'1',H'1',X'2'
UNIT     DS    0H
         DC    X'0015',H'1',H'8',CL8'&UNIT'
SPACE1   DS    0H
         DC    X'0009',H'1',H'3',X'000C30'
SPACE2   DS    0H
         DC    X'000A',H'1',H'3',X'000032'
SPACE3   DS    0H
         DC    X'000B',H'1',H'3',X'00000A'
SPACE4   DS    0H
         DC    X'000D',H'0'
DSN      DS    0H
         DC    X'0002',H'1',AL2(L'DSNAME+4)
DSNAME   DC    C'&QUALIFY..LOG#'
QUALIF   DC    CL4' '
FREE     DS    0H
         DC    X'001C',H'0'
         PRINT NOGEN
LOGDCB   DCB   DDNAME=TSLOGXXX,                                        X
               DSORG=PS,                                               X
               MACRF=PM,                                               X
               RECFM=VBA,                                              X
               LRECL=137,                                              X
               BLKSIZE=3120,                                           X
               SYNAD=DCBSYNAD
         PRINT GEN
LCBADR   DS    AL3
         DS    0H
LCBANZ1  DC    AL2((SHIFTBER-LCBTAB)/16)
LCBANZ   DC    H'0'
LCBTAB   DS    0F
         DC    10XL16'0'
SHIFTBER DS    CL&BUFSIZE
         EJECT
DUMPEXIT DS    0F
         USING *,15
         STM   4,7,20(13)
         L     4,SAVETSR
         LR    R6,R0
         L     7,=A(AUSG)
         MVC   4(133,7),0(R6)
         LH    1,LCBCOUNT
         LA    1,1(1)
         STH   1,LCBCOUNT
         L     1,LCBDCBA
         LR    0,7
         LM    4,7,20(13)
         L     15,48(1)
         BR    15
SAVETSR  DS    F
         END
