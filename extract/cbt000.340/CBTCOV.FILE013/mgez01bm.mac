./       ADD   NAME=RETU
         MACRO
&NAME    RETU  &RC=0,&PARAM=NOT
.*
.*       USER WRITTEN MACRO
.*       PROGRAM END ROUTINE
.*
         GBLB  &BEGIN,&PL1PROG
         GBLB  &SASTATC,&PRVEXIT,&FLPSAVE,&SUBROUT
         LCLC  &R
         LCLA  &WK,&WK1,&WK2
         LCLA  &LV
         LCLB  &SW,&SW1,&SW2,&SW3
&SW      SETB  ('&PARAM' EQ 'YES' OR '&RC'(1,1) EQ '(')
&SW1     SETB  (&PRVEXIT AND &SUBROUT)
&SW2     SETB  (&SW AND &SW1)
&LV      SETA  72
         AIF   (&BEGIN).START
&SASTATC SETB  1
.START   AIF   (NOT &FLPSAVE).C0X
&LV      SETA  &LV+32
.*
.*       SAVE PARAMS, STORE RC AND RESTORE OLD INTERRUPTION HANDLING
.*
.C0X      AIF  (&PL1PROG).PL1
.C0      AIF   (&SASTATC AND &SW1 AND NOT &SW).S01
         AIF   (&SASTATC AND NOT &SW1).S0
&SW3     SETB  1
&R       SETC  'R14'
         AGO   .C1
.S0      ANOP
&SW3     SETB  0
&R       SETC  'R13'
         AGO   .C1
.S01     ANOP
&NAME    L     R1,&LV.(,R13) LOAD OLD PICA ADDRESS
         SVC   14 ISSUE SPIE SVC
         L     R13,4(,R13) LOAD OLD SAVEAREA ADDRESS
         AGO   .C5
.C1      ANOP
&NAME    L     &R,4(,R13) LOAD OLD SAVEAREA ADDRESS
         AIF   ('&PARAM' NE 'YES').C2
         STM   R0,R1,20(&R) SAVE PARAMS
.C2      AIF   ('&RC'(1,1) NE '(').C3
         AIF   ('&RC(1)' LT '13').RCX0
&WK      SETA  &RC(1)-14
         AGO   .RCX1
.RCX0    AIF   ('&RC(1)' EQ '13' OR '&RC(1)' EQ '14').RCXMN
          AIF   (&PL1PROG AND '&RC(1)' EQ '12').PL1MN
&WK      SETA  &RC(1)+2
         AGO   .RCX1
.RCXMN   MNOTE 8,'REGISTER &RC(1) SHOULD NOT BE USED FOR RETURN CODE'
         AGO   .C3
.PL1MN   MNOTE 8,'REGISTER 12 SHOULD NOT BE USED IN A SUBROUTINE TO PL1/
               '
         MEXIT
.RCX1    ANOP
&WK      SETA  &WK*4
&WK      SETA  &WK+12
         ST    R&RC(1),&WK.(,&R) STORE RC
.C3      AIF   (NOT &SUBROUT OR NOT &PRVEXIT).C4X
         L     R1,&LV.(,R13) LOAD OLD PICA ADDRESS
         SVC   14 ISSUE SPIE SVC
&LV      SETA  &LV+4
.C4X     AIF   (NOT &PL1PROG).C4
         L     R1,&LV.(,R13) RESTORE
         ST    R14,0(,R1) OLD PRV CONTENTS
&LV      SETA  &LV+4
.C4      AIF   (&SASTATC).S1
.*
.*       RELEASE DYNAMIC SAVEAREA
.*
         LA    R0,&LV LOAD SAVEAREA LENGTH
         LR    R1,R13 LOAD SAVEAREA ADDRESS
         SVC   10 ISSUE FREEMAIN SVC
.S1      AIF   (NOT &SW3).C5
         LR    R13,R14 LOAD OLD SAVEAREA ADDRESS
.*
.*       RESTORE THE REGISTERS
.*
.C5      AIF   (NOT &FLPSAVE OR NOT &SUBROUT).C6
         LD    0,72(,R13) RESTORE
         LD    2,80(,R13) FLOATING
         LD    4,88(,R13) POINT
         LD    6,96(,R13) REGISTERS
.C6      AIF   (&SASTATC AND NOT &PL1PROG).C6X
         XC    8(4,R13),8(R13) DROP SAVEAREA CHAIN
         AIF   (NOT &PL1PROG).C6X
         LM    R14,R11,12(R13) RESTORE GENERAL REGISTERS
         AGO   .C6Y
.C6X     LM    R14,R12,12(R13) RESTORE GENERAL REGISTERS
.C6Y     AIF   ('&RC' EQ '(15)').C7
        AIF   ('&RC'(1,1) EQ '(').C60
         AIF   ('&RC' EQ '0').C60
         AIF   ('&RC' GT '4095').RCLARGE
         LA    R15,&RC LOAD RC INTO REG 15
         AGO   .C7
.C60     XR    R15,R15 SET RC ZERO
.C7      BR    R14 RETURN
        MEXIT
.RCLARGE MNOTE 4,'RC=&RC GREATER THAN 4095'
         AGO   .C7
         MEXIT
.PL1     ANOP
&SW3     SETB  1
&R       SETC  'R14'
         AGO   .C1
         MEND
