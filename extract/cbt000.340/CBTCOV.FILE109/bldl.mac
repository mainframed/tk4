BLDLIST  TITLE 'DCMS DYNAMIC BLDL TABLE DISPLAY'
         MACRO
&NAME    #FRAME &DIR,&N,&IMAGE
&NAME    DS   0H .
         AIF    ('&DIR' EQ 'FORWARD').FOR
         AIF    ('&DIR' EQ 'F').FOR
         AIF    ('&DIR' EQ 'BACKWARD').BAK
         AIF    ('&DIR' EQ 'B').BAK
         MNOTE 9,'DIRECTION SPECIFICATION MISSING OR INVALID'
         MEXIT
.FOR     ANOP
         L     R15,#SCR&N .    TO LENGTH
         LR    R1,R15 .        FROM LENGTH
         LA    R14,&IMAGE .    TRMIO IMAGE ADDRESS (TO)
         L     R0,#NOW&N  .    CURRENT STACK FRAME (FROM)
         MVCL  R14,R0
         L     R0,#TOP&N .     POINT TO TOP
         L     R1,#NOW&N .     POINT TO
         A     R1,#SCR&N .              NEW CURRENT
         ST    R1,#NOW&N .                         AND LIMIT IT
         C     R1,#END&N .     -RESETTING
         BNH   *+8 .                      IF
         ST    R0,#NOW&N .                   AT END
         MEXIT
.BAK     ANOP
         MEND
BLDL     DCMSTART R,LV=16384
*
*        CODE TO PRINT DYNAM-BLDL STATISTICS
*
*
*   DYNAM-BLDL LEVEL  CODE  DEPENDENCIES
*        SEE '$$$' LABELS IN IGC018XX CODE
*
$$$DIS04 EQU   X'318'         ADDR(FCOUNT/MCOUNT/LINKTAB)
*
*   END OF CODE LEVEL DEPENDENCIES
*
*
**
***      EQUATE REGISTERS AND SET UP DYNAMIC SAVEAREA
**
*
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
DLBL     #STAK BUILD,FRAME=20
         LA    1,SCREEN
         ST    1,SYSPRINT
         SPACE 2
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
         SPACE
         @PUT   SYSPRINT,BLDLMSG
         @PUT   SYSPRINT,BLANKS
         @PUT   SYSPRINT,BLANKS
         @PUT   SYSPRINT,BLANKS
         @PUT   SYSPRINT,BLANKS
         @PUT   SYSPRINT,BLANKS
*
**
***      FIND IGC018XX IN CORE AND CHECK CODE LEVEL
**
*
         L     R3,X'10'       CVT
         L     R3,X'C8'(,R3)  SSCVT
         L     R3,X'84'(,R3)  SVCTABLE
         LA    R3,X'90'(,R3)  BLDL ENTRY
         L     R2,0(R3)                 CODE
         CLC   =C'01-04-77',12(R2) Q.IS THIS SUPPORTED LEVEL
         BNE   UNSUPORT       NO ,TELL USER & ABEND
         EJECT
*
**
***      MOVE TABLE FROM IGC018XX TO OUR AREA
**
*
         LA    R2,$$$DIS04(R2)
         LA    R3,LINKTAB
         L     R4,SORT-LINKTAB(R2)
         ST    R4,SORT
         L     R4,HITS-LINKTAB(R2)
         ST    R4,HITS
*
         LM    R5,R7,TABSTAT
         L     R9,TABSTAT-LINKTAB(R2)
MOVEDATA MVC   0(64,R5),0(R9)
         AR    R9,R6
         BXLE  R5,R6,MOVEDATA
         EJECT
*
**
***      MAIN PROCESSING
**
*
         USING ENTRY,R5
         LM    R5,R7,TABSTAT
PRINTIT  MVC   OUTLINE+1(79),OUTLINE CLEAR OUT LINE
         L     R9,RECENT      Q.ANY ACTION
         LTR   R9,R9          ELSE DONT PRINT LINE
         BZ    PRINTED
         TM    CTLFLAG,X'80'  SUMMARY ONLY
         BO    PRINTED
         LH    R9,RECENT
         LTR   R9,R9
         BZ    PRECENT
         CVD   R9,DWD
         UNPK  OUTLINE+3(5),DWD+5(3)
         OI    OUTLINE+7,X'F0'
PRECENT  DS    0H
         LH    R9,TOTAL MG
         LTR   R9,R9
         BZ    PTOTAL
         CVD   R9,DWD
         UNPK  OUTLINE+9(5),DWD+5(3)
         OI    OUTLINE+13,X'F0'
PTOTAL   DS    0H
         MVC   OUTLINE+16(8),DATA
         SR    R9,R9
         IC    R9,DATA+59     CONCAT NO
         IC    R9,CHARTABL(R9)
         STC   R9,OUTLINE+26
         UNPK  OUTLINE+35(7),DATA+8(4)
         UNPK  OUTLINE+41(7),DATA+11(4)
         UNPK  OUTLINE+47(7),DATA+14(4)
         UNPK  OUTLINE+53(7),DATA+17(4)
         UNPK  OUTLINE+59(7),DATA+20(4)
         UNPK  OUTLINE+65(7),DATA+23(4)
         UNPK  OUTLINE+71(7),DATA+26(4)
         MVI   OUTLINE+75,C' '
         NC    OUTLINE+35(40),=40X'0F'
         TR    OUTLINE+35(40),CHARTABL
         @PUT   DLBL,OUTLINE
PRINTED  BXLE  R5,R6,PRINTIT
         LH    R9,SORT
         CVD   R9,DWD
         UNPK  SUMMARY+10(5),DWD+5(3)
         OI    SUMMARY+14,X'F0'
         LH    R9,HITS
         CVD   R9,DWD
         UNPK  SUMMARY+20(5),DWD+5(3)
         OI    SUMMARY+24,X'F0'
         LH    R9,MISSES
         CVD   R9,DWD
         UNPK  SUMMARY+31(5),DWD+5(3)
         OI    SUMMARY+35,X'F0'
         @PUT SYSPRINT,SUMMARY
         EJECT
SHOWN    EQU   *
         OI  IOCBFLGC,IOCBCRAO
         TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY K9=DCMSOVER,DEF=FINISHD,ATTN=FINISHD,                   X
               K8=FORWARD,K7=BACKWARD,K12=FINISHD
FORWARD  #FRAME FORWARD,DLBL,L2
         B     SHOWN
BACKWARD #FRAME BACKWARD,DLBL,L2
         B     FINISHD
         EJECT
SYSPRINT DC    F'0'
SYSLIST  DC    F'0'
*
**
***      WTO WRONG LEVEL IN IGC018XX CODE
**
*
UNSUPORT DS    0H
         @PUT   SYSPRINT,BADMSG
         B     SHOWN
         EJECT
         LTORG
DWD      DC    D'0'
SUMMARY  DC    CL80'    SORTS=     ,HIT=     ,MISS=                   '
OUTLINE  DC    CL80' '
BLDLMSG  ZB  'DYNAMIC BLDL TABLE STATISTICS'
BADMSG   ZS   'BLDLDCMS - WRONG LEVEL OF IGC018XX CODE'
CHARTABL DC    C'0123456789ABCDEF'
CTLFLAG  DS    X'00'          CONTROL FLAG FROM PARMS
*                             X'80' - PRINT SUMMARY ONLY
FINISHD  DCMSTOP RC=(15),SCREEN=YES
NOWSTAK  DS    F
ENDSTAK  DS    F
STACK    DS    F
* *
* *
BLDL     CSECT
ENT      EQU   100
ENTX     EQU   37
LINKTAB  DS    0D
TABSTAT  DC    A(START)
TABSIZE  DC    F'64'
TABEND   DC    A(LASTENT)
TABLAST  DC    A(LASTENT)
TABSLOT  DC    A(START+ENTX*64)
SORT     DC    H'0'
ENTRIES  DC    AL2(ENT)
HITS     DC    H'0'
MISSES   DC    H'0'
START    DC    ((ENT-1)*64)X'0'
LASTENT  DC    64X'0'
LINKEND  DS    0F
*
**
***      TABLE ENTRY DSECT
**
*
ENTRY    DSECT
RECENT   DS    H
TOTAL    DS    H
DATA     DS    0F
         EJECT
*
**
***      DSECT FOR GETMAINED WORK AREA
**
*
DLBL     #STAKCB DSECT=YES
SAVEAREA DSECT
SAVE1    DS    18F
GETEND1  DS    0D
GETSIZE1 EQU   GETEND1-SAVE1
         END
