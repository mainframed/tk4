TOP20    DCMSTART R,LV=4096
USER     #STAK GET,FRAME=20
RESTART  EQU   *
         L     R9,CVTPTR
         USING CVT,R9
         L     R11,CVTOPCTP        R7->RMCT
         USING IRARMCNS,R11
         L     R9,CVTASCBH (R9)   R9->ASCB'S
         DROP  R9
         LA    R8,20              FIRST FEW ASCB'S
         EJECT
*  SCAN THROUGH THE FIRST FEW ASCB'S ON DISPATCHING QUEUE AND
*  CALCULATE CPU (TCB+SRB) UTILIZATION.  ASSUME THESE ASCB ARE NON-
*  TS0.
CPU200   EQU   *
         L     R1,ASCBTSB(R9)
         LA    R1,0(R1)
         LTR   R1,R1
         BNZ   CPU888
*        ***   CODE ADDED TO SKIP TSO ASIDS
         UNPK  DOUBLE+5(3),ASCBDP(2,R9)  DISP PRIORITY
         TR    DOUBLE+5(2),HEXER
         MVC   CPUDP(2),DOUBLE+5
         SPACE
         LH    R1,ASCBASID(R9)    ASID
         CVD   R1,DOUBLE
         MVC   CPUASID(4),PAT2
         ED    CPUASID(4),DOUBLE+6
         SPACE
         L     R1,ASCBEJST(R9)    CPU TIME (TCB+SRB) IN 1.048576 SEC
         CVD   R1,DOUBLE          PRINT CPU TIME
         MVC   CPUTCBR,PAT3
         ED    CPUTCBR,DOUBLE+4
         M     R0,=F'10485760'    CONVERT TO MILLISECONDS X 10,000
         SPACE
         L     R2,ASCBOUCB(R9)    R2->OUCB
         USING OUCB,R2
         L     R3,RMCTTOD         SRM'S TOD
         S     R3,OUCBTMO (R2)   ELAPSED TIME FOR TRANSACTION IN MS
         DR    R0,R3              XXX.XX% CPU UTILIZATION (TCB)
         SPACE
         CVD   R1,DOUBLE          CONVERT AND SEND TO TERMINAL
         MVC   CPUTCB(7),PAT1
         ED    CPUTCB(7),DOUBLE+5
         SPACE
         L     R1,ASCBSRBT(R9)    SAME FOR SRB
         CVD   R1,DOUBLE
         MVC   CPUSRBR,PAT3
         ED    CPUSRBR,DOUBLE+4
         M     R0,=F'10485760'
         DR    R0,R3
         CVD   R1,DOUBLE
         MVC   CPUSRB(7),PAT1
         ED    CPUSRB(7),DOUBLE+5
         SPACE
         SR    R2,R2
         D     R2,=F'60000'       GET TIME IN MINUTES
         SR    R2,R2
         D     R2,=F'60'          SPLIT INTO HH MM
         CVD   R2,DOUBLE
         MVC   CPUMM(4),PAT2
         ED    CPUMM(4),DOUBLE+6
         CVD   R3,DOUBLE
         MVC   CPUHH(4),PAT2
         ED    CPUHH(4),DOUBLE+6
         MVC   CPUMM(4),CPUMM+2
         EJECT
         L     R1,ASCBJBNI(R9)    GET NAME
         LTR   R1,R1
         BNZ   *+8
         L     R1,ASCBJBNS(R9)
         MVC   CPUJOB(8),0(R1)
         SPACE
         #PUT  USER,CPU
         SPACE 2
         L     R9,4(R9)           NEXT ASCB ON QUEUE
         LTR   R9,R9
         BZ    CPU900             SKIP IF NONE
         BCT   R8,CPU200          GO DO NEXT
         B     CPU900
CPU888   EQU   *
         L     R9,4(R9)           NEXT ASCB ON QUEUE
         LTR   R9,R9
         BZ    CPU900             SKIP IF NONE
         B     CPU200
         SPACE
CPU900   EQU   *
FORWARD  EQU   *
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
         #FRAME FORWARD,USER,L3
DOSCREEN EQU   *
         MVC   SCREEN(79),TITLE
         TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY DEF=TOP20XIT,K9=RESTART,K3=RESTART,                     X
               K12=TOP20XIT,K7=BACKWARD,K8=FORWARD
BACKWARD #FRAME BACKWARD,USER,L3
         B     DOSCREEN
         EJECT
TITLE    DC    CL80'JOB       ASID TCBCPU '
CPU      EQU   *
CPUJOB   DC    CL8' ',C' ('
CPUASID  DC    CL4' ',C')'
CPUTCB   DC    CL7' ',C'%'
CPUTCBR  DC    CL9' ',C' (TCB) '
CPUSRB   DC    CL7' ',C'%'
CPUSRBR  DC    CL9' ',C' (SRB)  DP='
CPUDP    DC    CL2' '
CPUHH    DC    CL4' ',C':'
CPUMM    DC    CL6' '
         DC    CL10' '
         SPACE
PAT1     DC    X'402020214B2020'
PAT2     DC    X'40212020'
PAT3     DC    X'40202020206B202120'
         SPACE 2
HEXER    EQU   *-X'F0'
         DC    C'0123456789ABCDEF'
         SPACE 2
ASCBDP   EQU   X'2B'              ASCB DISP PRIORITY
ASCBASID EQU   X'24'              ASCB ASID
ASCBEJST EQU   X'40'              ASCB TCB TIME
ASCBJBNI EQU   X'AC'              INIT JOB NAME POINTER
ASCBJBNS EQU   X'B0'              STC JOB NAME POINTER
ASCBOUCB EQU   X'90'              OUCB POINTER
ASCBSRBT EQU   X'C8'              ASCB SRB TIME
ASCBTSB  EQU   X'3C'              ASCB TSB POINTER FOR TS
*VTASCBH EQU   X'234'             POINTER TO ASCB DISPATCH QUEUE
*VTPTR   EQU   X'10'              POINTER TO CVT
*VTOPCTP EQU   X'25C'             POINTER TO RMCT
*UCBTMO  EQU   X'48'              TRANS START TIME (SRM TIME)
*MCTTOD  EQU   X'7C'              TIME OF DAY (SRM TIME)
         EJECT
TOP20XIT  DCMSTOP SCREEN=YES
USER     #STAKCB DSECT=NO
         EJECT
         RMCNS
         EJECT
*        IHAASCB
         EJECT
         CVT  LIST=YES,DSECT=YES
         EJECT
         END
