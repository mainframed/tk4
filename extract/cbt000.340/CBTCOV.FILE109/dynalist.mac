         MACRO
&NAME    CLEAR &AREA
         LCLC  &L
&L       SETC  'L'''
&NAME    DS    0H
         MVI   &AREA,C' '
         MVC   &AREA+1(&L&AREA-1),&AREA
         MEND
         EJECT
DYNALIST DCMSTART R,LV=8192
NAME     #STAK GET,SIZE=60,FRAME=18,ENTRY=72
DATA     #STAK GET,SIZE=600,FRAME=18,ENTRY=72
         SPACE 2
         EJECT
         L     R1,16
         L     R8,40(R1)
         LR    R1,R8
UCBSCAN  ICM   R0,3,0(R1)
         CLI   0(R1),X'FF'
         LA    R1,2(,R1)
         BNE   UCBSCAN
         SR    R1,R8                    NUMBER OF UCB SLOTS+1
         SRL   R1,1
         BCTR  R1,0
         LA    R0,31
         AR    R1,R0                    ROUND COUNT TO WORD BOUNDARY
         OR    R1,R0
         XR    R1,R0
         SRL   R1,3                     TOTAL WORD COUNT
         LR    R9,R1                    SAVE COUNT
         EJECT
LOAD     DS    0H
         LOAD  EP=DEVNAMET
         LR    R10,R0
         LOAD  EP=DEVMASKT
         LR    R11,R0
         SPACE 6
         LA    R14,NAMETAB             CLEAR
         L     R15,=A(ENDTAB-NAMETAB)       NAME
         LA    R0,BLANKS                        TABLE
         LA    R1,1                                  TO
         ICM   R1,8,BLANKS                             BLANKS
         MVI   NAMETAB,C' '
         MVCL  R14,R0
         SPACE 3
         LA    R1,NAMETAB       COPY NAMES
         L     R0,0(,R10)                  INTO
         LA    R15,4(,R10)                     NAME STACK
TABLSCAN MVC   0(8,R1),0(R15)
         MVI   8(R1),C' '
         LA    R1,9(R1)           INCREMENT OUTPUT STAK
         LA    R15,12(,R15)       INCREMENT NAMET POINTER
         BCT   R0,TABLSCAN
         EJECT
         LA    R2,NAMETAB                      NAME
NAMESOUT EQU   *
         MVC   LINE(72),0(R2)     EIGHT
         #PUT  NAME,LINE           NAMES
         LA    R2,72(R2)
         CLI   0(R2),C' '
         BNE   NAMESOUT
         EJECT
SCREEN0  EQU   *
NAME     #STAK RESET,CLEAR=NO
SCREEN1  EQU   *
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
         #FRAME FORWARD,NAME,L2
         MVC   SCREEN(79),TITLE
         MVC   L22(79),MSGENTER
         MVC   INPLINE(79),BLANKS
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO
         TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY DEF=ONTER,K9=SCREEN0,K8=SCREEN1,K12=EXITLIST,           X
               ENTER=ONTER,K7=SCREEN0
         SPACE 4
ONTER    DS    0H
         CLC   INPLINE(8),BLANKS
         BNE   GET2
         B     SCREEN1
         EJECT
GET2     DS    0H
DATA     #STAK  RESET
         CLEAR LINE
         MVC   LINE(80),0(R1)
         MVI   LINE-1,C' '
         LA    R2,INPLINE
         LA    R14,1
         LR    R1,R2
         LA    R15,11(R2)
FIND     CLI   0(R1),C' '
         BNE   FIND1
         BXLE  R1,R14,FIND
         B     SCREEN1
FIND1    LR    R2,R1
FIND2    CLI   0(R1),C' '
         BE    FIND3
         BXLE  R1,R14,FIND2
FIND3    SR    R1,R2
         CLEAR UNITNAME
         BCTR  R1,0
         MVC   UNITNAME(0),0(R2)
         EX    R1,*-6
         CLC   END,UNITNAME
         BE    EODAD
         L     R0,0(,R10)
         LA    R15,4(,R10)
NAMESCAN CLC   UNITNAME,0(R15)
         BE    HAVENAME
         LA    R15,12(,R15)
         BCT   R0,NAMESCAN
         #PUT   DATA,=CL72' ******* INVALID UNIT GROUP'
         B     GET
HAVENAME LH    R0,0(,R11)               GET NUMBER OF MASK ENTRIES
         LA    R1,4(,R11)
FINDMASK CLC   8(4,R15),4(R1)
         BE    HAVEMASK
         LA    R1,8(R1,R9)
         BCT   R0,FINDMASK
         #PUT   DATA,=CL72' ******* NO MASK ENTRY FOUND'
         B     GET
HAVEMASK DS    0H
         MVI   LINE-1,C'-'
         LA    R2,7(R1,R9)              END OF MASK ENTRY
         L     R3,0(,R1)                NUMBER OF BITS THIS ENTRY
         LA    R4,8(,R1)
         SR    R7,R7
NEXTBYTE LA    R5,8
         LA    R6,X'80'
NEXTBIT  TM    0(R4),0
         EX    R6,NEXTBIT
         BZ    NOTTHIS
         CLEAR LINE
         LA    R14,0(R7,R8)             DOUBLE INDEX
*                                       TO LOOKUP ENTRY
         SR    R15,R15
         ICM   R15,3,0(R14)
*        LH    R15,0(,R15)
         MVC   LINE+4(3),13(R15)
         TM    18(R15),X'20'            DIRECT ACCESS?
         BZ    NOTDA                    NO
         TM    3(R15),X'80'             ONLINE?
         BZ    NOTONL
         CLI   28(R15),X'00'            YES, VOLID KNOWN?
         BE    NOTDA                    NO
         MVI   LINE+8,C'-'
         MVC   LINE+10(6),28(R15)
         B     NOTDA
NOTONL   EQU   *
         MVI   LINE+8,C'-'
         MVC   LINE+10(7),=CL7'OFFLINE'
         SPACE 2
NOTDA    DS    0H
         #PUT   DATA,LINE-1
         BCT   R3,NOTTHIS
         B     GET
NOTTHIS  DS    0H
         LA    R7,2(,R7)
         SPACE 2
         SRL   R6,1
         BCT   R5,NEXTBIT
         LA    R4,1(,R4)
         CR    R4,R2                    MASK BIT COUNT ERROR?
         BH    TOOMANY                  YES
         B     NEXTBYTE
TOOMANY  #PUT   DATA,=CL72'TOO MANY BITS IN MASK COUNT'
         B     GET
EODAD    DS    0H
         B     EXITLIST
GET      EQU   *
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
         #FRAME FORWARD,DATA,L2
         MVC   SCREEN(79),TITLE
         MVC   L22(79),MSGTRANS
PUTLINE  TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY DEF=ONTER,K9=SCREEN0,K12=EXITLIST,ENTER=ONTER,          X
               K8=GET,K7=BACKWRDS
BACKWRDS DS    0H
         #FRAME BACKWARD,DATA,L2
         B     PUTLINE
         EJECT
TITLE    DC    CL79'                    UNIT NAME DISPLAY'
MSGTRANS DC    CL79'DEPRESS PFK 9 TO SELECT ANOTHER UNITNAME'
MSGENTER DC    CL79'ENTER EIGHT-BYTE UNIT GROUP NAME'
END      DC    C'END'
EXITLIST DCMSTOP SCREEN=YES
UNITNAME DC    CL8' '
         DC    C' '
LINE     DC    CL80' '
NAMETAB  DS    0F
         DC    100CL9' '
ENDTAB   EQU   *
NAME     #STAKCB DSECT=NO
DATA     #STAKCB DSECT=NO
         END
