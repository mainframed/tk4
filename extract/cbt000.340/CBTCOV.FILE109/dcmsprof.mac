PROF     TITLE 'DCMS USER PROFILE PROMPTER'
DCMSPROF DCMSTART R,LV=4096,STAE=NO
         L     R11,0(R1)
         USING TDCB,R11
         L     R10,TDCBDCDA
         USING DCDA,R10
         L     R9,TDCBDUPT
         USING DUPT,R9
         CLC   DCDADCDA,=CL4'DCDA'
         BE    GOODCDA
         EX    0,*
GOODCDA  EQU   *
         CLC   DUPTDUPT,=CL4'DUPT'
         BE    GOODCBS
         EX    0,*
GOODCBS  EQU   *
*
*        ACQUIRE DEFAULT PROFILE SCREEN IMAGE
*
         GETIMAGE PROF,SCREEN
         LA    R2,18                   ADJUST
         C     R2,DUPTSNUM                   STACK
         BH    STNUMOK                            DISPLAY
         B     FORCE20
STNUMOK  EQU   *
         L     R2,DUPTSNUM             AND
FORCE20  EQU   *
         LA    R3,L4+63                SHOW
         L     R4,DUPTSTOP
STAKLOOP EQU   *
         CLI   0(R4),C' '              THE COMMAND STACK
         BE    STAKEND                     OR THE TOP TWENTY
         MVC   2(8,R3),0(R4)                  ENTRIES
         C     R4,DUPTSNOW       IS THIS THE CURRENT STAK ENTRY?
         BNE   STAKDOWN          IF NOT SKIP FLAGGING
         MVI   0(R3),C'>'
STAKDOWN LA    R4,8(R4)
         LA    R3,79(R3)
         BCT   R2,STAKLOOP
STAKEND  MVC   0(8,R3),=CL8'--------'
*        COMMAND STACK FORMATTING FINISHED HERE
         EJECT
*                                                                     *
*                                                                     *
*        F L A G   W O R D    F O R M A T T E D    H E R E            *
*                                                                     *
*                                                                     *
         SPACE
FLAGMAPS EQU   *
         LA    R2,L3+15          FIRST FLAG BIT
         L     R5,DUPTFLAG       GET ALL 32 BITS
         LA    R3,8              DO A BYTE:
FLGLOOP1 EQU   *
         SR    R4,R4             EMPTY TEST REG
         SLDL  R4,1
         LTR   R4,R4
         BNZ   BITON1
         MVC   0(3,R2),=CL3'NO '
BITON1   EQU   *
         LA    R2,79(R2)
         BCT   R3,FLGLOOP1
         LA    R2,79(R2)
         LA    R3,8              DO A BYTE:
FLGLOOP2 EQU   *
         SR    R4,R4             EMPTY TEST REG
         SLDL  R4,1
         LTR   R4,R4
         BNZ   BITON2
         MVC   0(3,R2),=CL3'NO '
BITON2   EQU   *
         LA    R2,79(R2)
         BCT   R3,FLGLOOP2
         LA    R2,L3+35          FIRST FLAG BIT
         LA    R3,8              DO A BYTE:
FLGLOOP3 EQU   *
         SR    R4,R4             EMPTY TEST REG
         SLDL  R4,1
         LTR   R4,R4
         BNZ   BITON3
         MVC   0(3,R2),=CL3'NO '
BITON3   EQU   *
         LA    R2,79(R2)
         BCT   R3,FLGLOOP3
         LA    R2,79(R2)
         LA    R3,8              DO A BYTE:
FLGLOOP4 EQU   *
         SR    R4,R4             EMPTY TEST REG
         SLDL  R4,1
         LTR   R4,R4
         BNZ   BITON4
         MVC   0(3,R2),=CL3'NO '
BITON4   EQU   *
         LA    R2,79(R2)
         BCT   R3,FLGLOOP4
         EJECT
NUMBERS  EQU   *
         LA    R8,L3+40
         L     R2,DUPTINTV
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   0(10,R8),=CL10'INTERVAL'
         MVC   10(4,R8),=CL4' = <'
         MVC   14(4,R8),DECWORK4
         OC    14(4,R8),=XL4'F0F0F0F0'
         MVI   18(R8),C'>'
         LA    R8,158(R8)
         L     R2,DUPTSAMP
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   0(10,R8),=CL10'SAMPLE  '
         MVC   10(4,R8),=CL4' = <'
         MVC   14(4,R8),DECWORK4
         OC    14(4,R8),=XL4'F0F0F0F0'
         MVI   18(R8),C'>'
*
         LA    R8,158(R8)
         L     R2,DUPTSNUM
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   0(10,R8),=CL10'STACK SIZE'
         MVC   10(4,R8),=CL4' = <'
         MVC   14(4,R8),DECWORK4
         OC    14(4,R8),=XL4'F0F0F0F0'
         MVI   18(R8),C'>'
*
DISPLAY  EQU   *
         TRMIO IOCB,IMAGE=SCREEN,FORMAT=FORMAT
         PFKEY DEF=EXIT,K12=EXIT,K9=DCMSOVER,ENTER=CHANGES
         B     EXIT
CHANGES  EQU   *
         CLC   L3+54,=C'0010'     ALTER
         BL    CH2                     INTERVAL
         CLC   L3+54,=C'9999'                  VALUE
         BH    CH2                                  IF
         PACK  DOUBLE(8),L3+54(4)                     IT
         CVB   R14,DOUBLE                               WAS
         ST    R14,DUPTINTV                                CHANGED
CH2      EQU   *
         CLC   L5+54,=C'0001'     ALTER
         BL    CH3                     SAMPLE
         CLC   L5+54,=C'9999'                VALUE
         BH    CH3                                IF
         PACK  DOUBLE(8),L5+54(4)                   IT
         CVB   R14,DOUBLE                             WAS
         ST    R14,DUPTSAMP                              CHANGED
CH3      EQU   *
         LA    R2,18                   ADJUST
         C     R2,DUPTSNUM                   STACK
         BH    CH3OK                            DISPLAY
         B     CH3X20
CH3OK    EQU   *
         L     R2,DUPTSNUM             AND
CH3X20   EQU   *
         LA    R3,L4+63                SHOW
         L     R4,DUPTSTOP
CH3KLOOP EQU   *
*        CLI   0(R4),C' '              THE COMMAND STACK
*        BE    CH3KEND                     OR THE TOP TWENTY
         CLI   0(R3),C'-'              THE COMMAND STACK
         BE    CH3KEND                     OR THE TOP TWENTY
         MVC   0(8,R4),2(R3)                  ENTRIES
         CLI   0(R3),C'>'        IS THIS THE CURRENT STAK ENTRY?
         BNE   CH3KDOWN          IF NOT SKIP FLAGGING
         ST    R4,DUPTSNOW          ELSE RESET CURRENT POINTER
CH3KDOWN LA    R4,8(R4)
         LA    R3,79(R3)
         BCT   R2,CH3KLOOP
CH3KEND  EQU   *
*        COMMAND STACK RECOVERY   FINISHED HERE
         B     DCMSOVER
FORMAT   SFMT
SCREEN   FIELD 79,ALPHA,INTEN=HI
L2       FIELD 79,ALPHA
L3       FIELD 79,ALPHA,CURSOR=AFTER
L4       FIELD 79,ALPHA
L5       FIELD 79,ALPHA
L6       FIELD 79,ALPHA
L7       FIELD 79,ALPHA
L8       FIELD 79,ALPHA
L9       FIELD 79,ALPHA
L10      FIELD 79,ALPHA
L11      FIELD 79,ALPHA
L12      FIELD 79,ALPHA
L13      FIELD 79,ALPHA
L14      FIELD 79,ALPHA
L15      FIELD 79,ALPHA
L16      FIELD 79,ALPHA
L17      FIELD 79,ALPHA
L18      FIELD 79,ALPHA
L19      FIELD 79,ALPHA
L20      FIELD 79,ALPHA
L21      FIELD 79,ALPHA
         SFEND
EXIT     DCMSTOP SCREEN=NO
         DSGEN FORMAT
         DCMSDATA
         DCMSUPT
         EJECT
         TDCB
         END
