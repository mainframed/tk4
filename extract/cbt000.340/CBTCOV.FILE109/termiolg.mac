TERMIOLG TITLE 'SYSLOG   TERMIO SUPPORT FOR TSO '
TERMIOLG CSECT ,                                                 BFG001
         SAVE  (14,12),,TERMIOLG_&SYSDATE_&SYSTIME               BFG001
         DCMSREGS ,                                              BFG001
         LR    R12,R15             RESET BASE REG                BFG001
         USING TERMIOLG,R12
         LR    R10,R1
         USING IOCB,R10
         L     R11,IOCBTDCB        POINT AT TDCB                 BFG001
         USING TDCB,R11                                          BFG001
         LTR   R11,R11             IF NO TDCB                    BFG001
         BZ    QUITLOG              THEN EXIT                    BFG001
         NI    IOCBFLGL,255-IOCBLNOW   TURN OFF SINGLE SCREEN    BFG001
         TM    IOCBFLGL,IOCBLBAD   IS IT BROKEN                  BFG001
         BO    QUITLOG             IF SO THEN END                BFG001
         CLC   TDCBLGDC,=F'0'      IS IT ACTIVE                  BFG001
         BNE   CONTINU             IF SO THEN CONTINUE           BFG001
         OI    IOCBFLGL,IOCBLBAD   FLAG AS BROKEN                BFG001
         B     QUITLOG             OUT                           BFG001
CONTINU  EQU   *
         GETMAIN R,LV=LOGSCRLN                                   BFG001
         LR    R15,R1              RESET ADDR TO REG             BFG001
         USING LOGELEM,R15         SET BASE                      BFG001
         SR    R0,R0               .                             BFG001
         SR    R1,R1               .                             BFG001
         LA    R8,LOGBUF           .                             BFG001
         ICM   R1,8,=C' '          .                             BFG001
         L     R9,=A(24*80)        .                             BFG001
         MVCL  R8,R0               .  CLEAR BUFFER OUT           BFG001
         L     R9,IOCBFMT
         L     R8,IOCBIMG
         LH    R7,0(R9)
         LH    R6,2(R9)
         AR    R6,R9
         LA    R9,6(R9)
         LA    R1,LOGBUF
FORMLOOP EQU   *
         LH    R2,2(R9)            TO OFFSET
         AR    R2,R1
         LH    R4,4(R9)            FROM OFFSET
         AR    R4,R8
         LH    R3,6(R9)            GET LENGTH
         LR    R5,R3               IN = OUT (LENGTH
         LA    R5,0(R5)            PADCHAR
*
         MVCL  R2,R4
*
         LA    R9,8(R9)            NEXT FORMAT ENTRY
         CR    R9,R6
         BL    FORMLOOP
*
*        LOGOUT VERSION OF SCREEN COMPLETED HERE
*
*                                                                BFG001
*        NOW FILL IN THE REST OF THE LOG ELEMENT                 BFG001
*                                                                BFG001
         MVC   LOGETIME,IOCBTIME  MOVE IN TIMESTAMP              BFG001
         MVC   LOGENAME,IOCBNAME  MOVE IN MONITOR NAME           BFG001
         MVC   LOGETDCB,IOCBTDCB   SET ADDR OF TDCB              BFG001
         LA    R1,LOGEPUT          SET PUT FUNCTION CODE         BFG001
         ST    R1,LOGEFUNC                                       BFG001
         LA    R1,LOGSCRLN         GET LENGTH OF ELEMENT         BFG001
         ST    R1,LOGELEN          SAVE ADDR                     BFG001
*                                                                BFG001
*        NOW QUEUE AND POST THE LOGTASK                          BFG001
*                                                                BFG001
         L     R3,TDCBLGST         GET LOG STACK HEAD ADDR       BFG001
         L     R1,0(R3)            GET ADDR OF TOP ELEMENT       BFG001
         LA    R2,LOGELEN          POINT TO NEW ELEMENT          BFG001
         ST    R1,LOGELINK         SAVE ADDR OF OLD ONE          BFG001
         CS    R1,R2,0(R3)         TRY TO ADD TO CH              BFG001
         BNE   *-8                 IF BUSY THEN TRY AGAIN        BFG001
         L     R1,TDCBLGEC         POINT AT ECB                  BFG001
         POST  (R1)                POST THE DUMMY                BFG001
*                                                                BFG001
*        NOW EXIT BUT DO NOT FREE ALL THE WORKAREA               BFG001
*                                                                BFG001
         RETURN (14,12)                                          BFG001
         EJECT                                                   BFG001
QUITLOG  RETURN (14,12)                                          BFG001
         EJECT
IOCB DSECT
         IOCB
         EJECT
         LOGELEM SCREEN=YES            GEN LOG ELEMENT           BFG001
LOGBUF   EQU   LOGESCRN                                          BFG001
LOGSCRLN EQU   *-LOGELEM                                         BFG001
         TDCB  ,                                                 BFG001
         END
