TERMIOWT TITLE 'HARDCOPY TERMIO SUPPORT FOR TSO '
         DCMSREGS
         EJECT
TERMIOWT CSECT
         USING TERMIOWT,R11
         LR    R11,R15
*        TERMIO# SAVED THE REGISTERS
         LR    R10,R1              POINT TO IOCB
         USING IOCB,R10
         GETMAIN R,LV=WORKLEN,SP=2
         ST    R13,4(R1)           CHAIN
         ST    R1,8(R13)           THE
         LR    R13,R1              SAVE AREAS
         USING DCMSWORK,R13
         SPACE 3
         TM    IOCBFLGS,IOCBFNWR+IOCBFNRD
         BO    QUITLOG
         TM    IOCBFLGS,IOCBFNWR
         BO    NOWRITE
         L     R9,IOCBFMT
         L     R8,IOCBIMG
         LH    R7,0(R9)
         LH    R6,2(R9)
         AR    R6,R9
         LA    R9,6(R9)
         LA    R1,LOGBUF
FORMLOOP EQU   *
         LH    R2,2(R9)            TO OFFSET
         AR    R2,R1
         LH    R4,4(R9)            FROM OFFSET
         AR    R4,R8
         LH    R3,6(R9)            GET LENGTH
         LR    R5,R3               IN = OUT (LENGTH
         LA    R5,0(R5)            PADCHAR
*
         MVCL  R2,R4
*
         LA    R9,8(R9)            NEXT FORMAT ENTRY
         CR    R9,R6
         BL    FORMLOOP
*
*        LOGOUT VERSION OF SCREEN COMPLETED HERE
*
         LA    R2,LOGBUF
         LA    R3,23
         LA    R4,80
         MVC   WTL(WLEN),WTX .        MOVE WTO LIST  N
LOGLOOP  EQU   *
         MVI   WTL+4,C'>'
         MVC   WTL+4(80),0(R2)
         WTO   MF=(E,WTL),ROUTCDE=(2,11)
         MVC   WTL+4(80),SPACEZ
         AR    R2,R4
         BCT   R3,LOGLOOP
         MVC   WTL+4(24),IOCBTIME
         WTO   MF=(E,WTL),ROUTCDE=(2,11)
NOWRITE  EQU   *
         MVI   IOCBAID,AIDPA1
         B     QUITLOG
*
         EJECT
SLASH    DC    80C'-'
SPACEZ   DC    CL160' '
         DS    0F .
QUITLOG  DS     0H .
         SR    R15,R15
DCMSRCD  EQU   *
         LR    R2,R13
         L     R1,4(R13)
         ST    R15,16(R1)
         LR    R13,R1
         FREEMAIN R,LV=WORKLEN,A=(R2),SP=2
         LM    14,12,12(13)
         BR    R14
         LTORG
WTX WTO  '                                                             X
                              ',                                       X
               MF=L,ROUTCDE=(2,11)
WLEN     EQU   *-WTX
         EJECT
IOCB DSECT
         IOCB
         EJECT
DCMSWORK DSECT
DCMSAVE  DS    18F .
PARMADDR DS    F
OUTPUTL  DS    F .
WTL WTO  '                                                             X
                              ',                                       X
               MF=L,ROUTCDE=(2,11)
LOGBUF   DS    28CL80 .
WORKLEN  EQU   *-DCMSWORK
         END
