         MACRO
&L       CVD3  &BIN,&DEC
&L       LH    R1,&BIN
         CVD   R1,DOUBLE
         MVC   &DEC,PAT3
         ED    &DEC,DOUBLE+6
         MEND
         SPACE
         MACRO
&L       CVD3C &BIN,&DEC
&L       LH    R1,&BIN
         SRL   R1,4
         CVD   R1,DOUBLE
         MVC   &DEC,PAT3
         ED    &DEC,DOUBLE+6
         MEND
         EJECT
SRMTIME  DCMSTART R,LV=4096,IOCB=NO
         L     R1,0(R1)     GET FIRST PARAMETER
         LR    R10,R1
         USING #SRM,R10
         L     R11,CVTPTR
         L     R11,CVTOPCTP-CVT(,R11)
*        USING RMCNS,R11
RESTART  EQU   *
#SRM    #STAK RESET
         SPACE
*
*                                 TIME INTERVAL FOR TIMED ALGORS
*
         L     R7,RMCTEPAT(R11)   R7->ALGOR RMEP'S
         SPACE
         MVC   #TT1#SRM,TITL1
         MVC   #TT2#SRM,TITL2
         MVC   #TT3#SRM,TITL3
*        LA    R9,NAMES
         LA    R6,22
         #PUT #SRM,BLANKS
TIMELOOP EQU   *
         LA    R9,EPNAMES
NAMELOOP CLC   3(3,R9),RMEPFLG(R7)
         BE    SRM100
         LA    R9,E#L(R9)
         CLI   0(R9),C'Z'
         BE    SRM500
         B     NAMELOOP
SRM100   EQU   *
         MVC   OUTLINE(79),BLANKS     CLEAR LINE
         MVC   OUTLINE(79),7(R9)            COPY SKELETON
         TM    RMEPACT(R7),X'04'
         BNO   SRM101
         MVC   OUTLINE+61(9),=CL9'CRITICAL'   AS CRITICAL
SRM101   EQU   *
         TM    RMEPACT(R7),X'01'
         BO    SRM102
         MVC   OUTLINE+70(9),=CL9'ALGORITHM'     ALGORITHM
         B     SRM103
SRM102   EQU   *
         MVC   OUTLINE+70(9),=CL9'ACTION'
SRM103   EQU   *
         TM    RMEPACT(R7),X'02'
         BO    SRM150
         MVC   OUTLINE+10(9),=CL9'NOT TIMED'    FLAG AS NOT TIMED
         B     SRM200
         SPACE 2
SRM150   EQU   *  MVC   TID(3),3(R3)  SET ROUTINE ID
         L     R15,RMEPINT(R7)    TIME INTERVAL
         CVD   R15,DOUBLE
         MVC   TINT,PAT4
         ED    TINT,DOUBLE+4
         MVC   OUTLINE+10(8),TINT+1
         SPACE
SRM200   EQU   *
*
*
         #PUT   #SRM,OUTLINE
         SPACE 2
SRM500   L     R7,RMEPBCK(R7)      NEXT RMEP
         LTR   R7,R7
         BZ    SRMTIMX             SIF HAVE ONE
         BCT   R6,TIMELOOP
         B     SRMTIMX
         EJECT
* /* *************************************************************** */
* /*                                                                 */
* /*   ROUTINES WHICH PERFORM ALGORITHMIC PROCESSING FOR THE SYSTEM  */
* /*   RESOURCES MANAGER.  THEY ARE INVOKED BY THE CNTRL ALGORITHM   */
* /*   ON THE BASIS OF REQUEST BITS IN THE RMCTALA-RMCTALR FLDS OF   */
* /*   THE SYSTEM RESOURCES MANAGER CONTROL TABLE.  THE IRAEPNMP     */
* /*   MACRO DEFINES THE BIT FLAG ASSOCIATED WITH EACH ROUTINE.      */
* /*                                                                 */
* /* *************************************************************** */
* /*
EPNAMES  DC    CL3'IL1'
         DC   XL4'40000000'          ROUTINE FLAG MASK
 DC CL79'IOM  IL1             I/O LOAD BALANCING LCH MONITOR         '
         DC    CL3'CL1'
         DC   XL4'20000000'          ROUTINE FLAG MASK
 DC CL79'CPM  CL1             CPU UTILIZATION MONITOR                '
         DC    CL3'SQA'
         DC   XL4'10000000'          ROUTINE FLAG MASK
 DC CL79'STM  SQA             SQA SHORTAGE NOTIFICATION              '
         DC    CL3'CAN'
         DC   XL4'08000000'          ROUTINE FLAG MASK
 DC CL79'CTL                                                         '
         DC    CL3'A05'
         DC   XL4'04000000'          ROUTINE FLAG MASK
 DC CL79'CTL                                                         '
         DC    CL3'AP1'
         DC   XL4'02000000'          ROUTINE FLAG MASK
 DC CL79'CPM  AP1             APG REORDER PROCESSOR                  '
         DC    CL3'PR1'
         DC   XL4'01000000'          ROUTINE FLAG MASK
 DC CL79'STM  PR1             PAGE REPLACEMENT NORMAL PROCESSING     '
         DC    CL3'EQ1'
         DC   XL4'00800000'          ROUTINE FLAG MASK
 DC CL79'CPM  EQ1             ENQ/DEQ ALGORITHM TIME MONITOR         '
         DC    CL3'A09'
         DC   XL4'00400000'          ROUTINE FLAG MASK
 DC CL79'CTL                                                         '
         DC    CL3'A10'
         DC   XL4'00200000'          ROUTINE FLAG MASK
 DC CL79'CTL                                                         '
         DC    CL3'ASM'
         DC   XL4'00100000'          ROUTINE FLAG MASK
 DC CL79'STM  ASM             AUX STORAGE SHORTAGE MONITOR           '
         DC    CL3'MS6'
         DC   XL4'00080000'          ROUTINE FLAG MASK
 DC CL79'STM  MS6             MAIN STORAGE OCCUPANCY LONG WAIT DETECT'
         DC    CL3'PR5'
         DC   XL4'00040000'          ROUTINE FLAG MASK
 DC CL79'STM  PR5             PAGE REPLACEMENT-FRAME SHORTAGE STEALS '
         DC    CL3'A14'
         DC   XL4'00020000'          ROUTINE FLAG MASK
 DC CL79'CTL                                                         '
         DC    CL3'MS2'
         DC   XL4'00008000'          ROUTINE FLAG MASK
 DC CL79'STM  MS2             REAL PAGE  (FRAME) SHORTAGE PREVENTION '
         DC    CL3'RM1'
         DC   XL4'00004000'          ROUTINE FLAG MASK
 DC CL79'RMR  RM1             RESOURCE MANAGER PERIODIC MONITORING   '
         DC    CL3'RM2'
         DC   XL4'00002000'          ROUTINE FLAG MASK
 DC CL79'RMR  RM2             RESOURCE MANAGER MPL ADJUSTMENT        '
         DC    CL3'WM2'
         DC   XL4'00001000'          ROUTINE FLAG MASK
 DC CL79'WLM  WM2             SWAPPABLE USER EVALUATION              '
         DC    CL3'CAP'
         DC   XL4'00000800'          ROUTINE FLAG MASK
 DC CL79'CTL  CAP             SWAP ANALYSIS ALGORITHM                '
         DC    CL3'A22'
         DC   XL4'00000400'          ROUTINE FLAG MASK
 DC CL79'CTL                                                         '
         DC    CL3'MS9'
         DC   XL4'00000200'          ROUTINE FLAG MASK
 DC CL79'STM  MS9             LOGICAL SWAP LNG WAIT DETECTION        '
         DC    CL3'AP2'
         DC   XL4'00000100'          ROUTINE FLAG MASK
 DC CL79'CPM  AP2             ASCBCHAP COMMON SERVICES               '
EPNMEND  DC    CL3'ZZZ'
         DC   XL4'00000000'          ROUTINE FLAG MASK
 DC CL79'                                                            '
E#L      EQU   *-EPNMEND
         EJECT
TIME     DS   0CL80
         ORG  TIME
TID      DC   CL3' '
TINT     DC   CL9' '
         ORG
TITL1    DS    0CL79
 DC CL79'               S R M    S U B R O U T I N E S               '
TITL2    DC    CL79' '
TITL3    DS    0CL79
 DC CL79'MOD  RTNE    TIME    --------FUNCTION OF ROUTINE -----------'
NAMES     EQU  *
 DC CL79'                                                            '
         EJECT
         SPACE 2
PAT2     DC    X'402020202120'    XXXXX
PAT3     DC    X'40202120'        XXX
PAT4     DC    X'40202021204B202020'
         SPACE
RMCTEPAT EQU   X'38'
RMEPBCK  EQU   X'0C'
RMEPFLG  EQU   X'08'
RCCUICTL EQU   X'04'
RCCUICTH EQU   X'06'
RCCCPUTL EQU   X'08'
RCCCPUTH EQU   X'0A'
RCCPTRTL EQU   X'0C'
RCCPTRTH EQU   X'0E'
RCCASMTL EQU   X'10'
RCCASMTH EQU   X'12'
RCCTOTUT EQU   X'14'
RCCLCHUT EQU   X'16'
RCVCPUA  EQU   X'28'
RCVUICA  EQU   X'26'
RCVASMQA EQU   X'2C'
RCVPTRA  EQU   X'2E'
RCVTOTDF EQU   X'48'
RMCTCCT  EQU   X'04'
CCVCPUT  EQU   X'7A'
*VTRLSTG EQU   X'358'
*
*
RMEPINT  EQU   X'1C'
RMEPACT  EQU   X'0B'
SRMTIMX  DCMSTOP
PARMPTR  DS    F .
OUTLINE  DS    CL80
#SRM    #STAKCB
*        EJECT
*MCNS    RMCNS
         EJECT
         CVT  LIST=YES,DSECT=YES
         END
