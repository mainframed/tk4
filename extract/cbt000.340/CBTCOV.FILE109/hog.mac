HOG      TITLE 'HOG  TO FIND THE HOG CAUSING ALL THE PAGING'
         SPACE 5
***********************************************************************
*
*        THIS PROGRAMS PURPOSE IS TO DISPLAY UPTO 12 JOBS AND THE
*        PAGING INFORMATION FROM THEIR OUXB.  THIS WILL HELP FIND
*        THOSE JOBS THAT HOG ALL THE PAGING ACTIVITY.
*
***********************************************************************
         SPACE 3
***********************************************************************
*
*        PFK ASSIGNMEMT
*
*              PFK 3               ALLOWS THE JOBNAMES TO BE CHANGED IF
*                                  DEFAULT ONES(FIRST 12 INCORE JOBS)
*                                  ARE NOT THE DESIRED ONES.  THIS IS
*                                  DONE BY CHANGING THE JOBNAME AND
*                                  HITTING ENTER.  THE DISPLAY WILL
*                                  RESTART AUTOMATICALLY.
*
***********************************************************************
         SPACE 3
*                                                GUY L. ALBERTELLI
*                                                B F GOODRICH
*                                                500 S. MAIN ST.
*                                                AKRON, OHIO    44318
*                                                216-379-4071
         EJECT
HOG      DCMSTART R,LV=4096
         EJECT
*
**       DEFAULT USINGS
*
         USING ASCB,R4
         USING OUXB,R5
         USING DSP,R10
         USING RMCNS,R11
MAXJOBS  EQU   12                  DO 12 JOBS AT A TIME
         SPACE 2
*
**       INITIALIZE THE DISPLAY CONTROL AREA
*
         LA    R15,MAXJOBS         SET TO MAX JOBS
         LA    R10,DSPAREA         START OF AREA
         LA    R1,79*2+11          INITIAL DISPLACEMENT INTO LINE
INIT0    ST    R1,DSPINDX          INDEX INTO LINE FOR THIS JOB
         LA    R1,12(R1)           GO TO NEXT JOB
         C     R1,=A(83+79*2)      END OF FIRST LINE
         BNE   *+8                 NOPE
         LA    R1,79*13+11         GO TO SECOND ROW
         LA    R10,DSPLEN(R10)     INCREMENT AREA POINTER
         BCT   R15,INIT0           KEEP GOING
         SPACE 2
*
**       SELECT THE FIRST 12 JOBS IN CORE
*
         LA    R15,MAXJOBS         SET NUMBER OF JOBS
         LA    R10,DSPAREA
         USING ASVT,R2             TEMP USING FOR INITIALIZATION
         L     R1,16               GET CVT
         L     R2,CVTASVT-CVTMAP(R1) ACCESS ASVT
         L     R3,ASVTMAXU         GET NUMBER OF ADDRESS SPACES
         LA    R2,ASVTENTY         POINT TO ASVT ENTRIES
INIT1    TM    0(R2),ASVTAVAL      IS THIS ONE ACTIVE
         BO    INIT2               NOPE
         L     R4,0(R2)            GET ADDR OF ASCB
         TM    ASCBRCTF,ASCBTMNO+ASCBOUT+ASCBTMLW  IS IT IN CORE
         BNZ   INIT2               NOPE
         CLI   ASCBDSP1,0          IS IT DISPATCHABLE
         BNE   INIT2               NOPE
         ICM   R1,15,ASCBJBNI      GET NAME OF JOB IF ANY
         BNZ   *+8                 AROUND IF GOOD
         L     R1,ASCBJBNS         GET NAME OF STARTED TASK
         MVC   DSPJOBNM,0(R1)      SAVE NAME
         XC    DSPPIN(9*4),DSPPIN    CLEAR THE COUNTERS
         ST    R4,DSPASCB          SAVE ADDR OF ASCB
         MVC   DSPOUXB,ASCBOUXB    AND ADDR OF OUXB
         LA    R10,DSPLEN(R10)     FOUND A JOB
         BCT   R15,INIT2           IF SPACE LEFT KEEP GOING
         B     INITDONE            OTHERWISE EXIT
INIT2    LA    R2,4(R2)            BUMP UP ASVT ADDR
         BCT   R3,INIT1            KEEP GOING
*
**       HAVE NOT FOUND 12 JOBS SO INIT REST OF DSP AREA
*
INIT3    MVC   DSPJOBNM,=CL8'**GONE**'
         XC    DSPASCB(8),DSPASCB  CLEAR AREA
         LA    R10,DSPLEN(R10)     GO TO NEXT
         BCT   R15,INIT3
         DROP  R2
*
**       INITIALIZATION DONE EXCEPT FOR TIME VALUE
*
INITDONE TIME  BIN
         ST    R0,PREVTIME         SAVE TIME OF LAST INTERVAL
         MVC   INTERV,=F'100'      SET PREVIOUS INTERVAL AT 1 SEC
         B     GO1                 KEEP GOING
         EJECT
*
**       NORMAL START FOR DISPLAY LOOP
*
GO       TIME  BIN                 GET TIME OF DAY
         L     R1,PREVTIME         GET PREVIOUS TIME OF DAY
         ST    R0,PREVTIME         SAVE CURRENT INTERVAL
         SR    R0,R1               COMPUTE INTERVAL
         ST    R0,INTERV           SAVE HUNDREDTHS OF SEC IN INTERVAL
*
**       LOOP ENTRY FOR STARTUP AND MODIFICATION
*
GO1      LA    R1,24*79            GET SCREEN SIZE
         LR    R3,R1               MOVE SIZE
         LA    R0,SCREEN           POINT AT TO
         LA    R2,FORM             POINT AT FORM
         MVCL  R0,R2               INIT SCREEN
         LA    R10,DSPAREA         POINT AT DSP AREA
         LA    R6,MAXJOBS          NUMBER TO DO
*
**       NOW FORMAT THE ENTRIES FOR ONE JOB
*
LP1      L     R9,DSPINDX          GET INDEX INTO BUFFER
         LA    R9,SCREEN(R9)       GET BUFFER ADDRESS
         MVC   0(8,R9),DSPJOBNM    SET JOBNAME
         LA    R9,79(R9)           POINT TO NEXT LINE
***      R9 NOW POINTS TO L4 OR L15 AND 1ST CHAR OF JOB NAME
         ICM   R4,15,DSPASCB       IS THERE AN ASCB
         BZ    ENDONE              IF NOT THE DONE WITH THIS JOB
         CLC   ASCBASCB,=C'ASCB'   IS THIS STILL ASCB
         BNE   GONE                NOPE SO SHOW GONE
         L     R5,DSPOUXB          POINT AT OUXB
         TM    ASCBRCTF,ASCBTMNO+ASCBOUT+ASCBTMLW  IS ASID STILL IN
         BNZ   CLEAR               IF NOT SHOW OUT
         CLI   ASCBDSP1,0          IS IT DISPATCHABLE
         BNE   CLEAR
         ICM   R1,15,ASCBJBNI      NOW CHECK NAMES MATCH
         BNZ   *+8
         L     R1,ASCBJBNS
         CLC   DSPJOBNM,0(R1)      SAME JOB ???
         BE    FORMOUXB            FORMAT DATA
*
**       CHANGED JOB NAME
*
         MVC   DSPJOBNM,0(R1)      SET NEW NAME
         MVC   DSPOUXB,ASCBOUXB    SET NEW ADDR
         XC    DSPPIN(9*4),DSPPIN  CLEAR COUNTERS
         B     LP1                 GO AND REFORMAT FOR CHANGED NAME
*
**       NOW FORMAT THE OUXB CHANGES
*
FORMOUXB DS    0H
         LA    R5,OUXBPIN          POINT AT FIRST WORD
         LA    R4,DSPPIN            FOR BOTH AREAS
         LA    R3,9                NUMBER OF LINES
LP2      L     R1,0(R5)            GET NEW VALUE
         S     R1,0(R4)            GET CHANGE
         MVC   0(4,R4),0(R5)       SAVE NEW VALUE
         XR    R0,R0               CLEAR THE REG
         M     R0,=F'1000'         COMPUTE PAGES/SEC BASED ON INTERVAL
         D     R0,INTERV
         CVD   R1,WORK             GET DECIMAL
         MVC   0(7,R9),=X'40202020214B20'   SET MASK
         ED    0(7,R9),WORK+5
         LA    R4,4(R4)            UPDATE POINTERS
         LA    R5,4(R5)
         LA    R9,79(R9)
         BCT   R3,LP2              TRY FOR NEXT VALUE
*
**       FINSHED THIS JOB NOW FOR NEXT
*
ENDONE   LA    R10,DSPLEN(R10)
         BCT   R6,LP1              DO NEXT JOB
         B     SYSWIDE             GO AND FORMAT SYSTEMWIDE INFO
         SPACE 4
*
**       MARK JOB AS OUT
*
CLEAR    MVC   79+3(3,R9),=C'OUT'
         XC    DSPPIN(9*4),DSPPIN  CLEAR COUNTERS
         B     ENDONE              DONE WITH THIS ONE
         SPACE 3
*
**       JOB HAS DISAPPEARED
*
GONE     MVC   DSPJOBNM,=CL8'**GONE**'
         XC    DSPASCB(8),DSPASCB  CLEAR POINTERS
         XC    DSPPIN(9*4),DSPPIN
         B     LP1
         EJECT
*
**       FORMAT SYSTEM WIDE INFO
*
SYSWIDE  DS    0H
         L     R11,16              POINT AT CVT
         L     R11,CVTOPCTP-CVTMAP(R11) NOW AT RMCT
         LH    R1,RCVCPUA          GET 'CPU'
         SRL   R1,4                DIVIDE BY 16
         ST    R1,DECWORK1
         BAL   R14,@DECIMAL
         MVC   L1+40(4),DECWORK4
         LH    R1,RCVPTR           GET 'PAGE' RATE
         ST    R1,DECWORK1
         BAL   R14,@DECIMAL
         MVC   L1+51(4),DECWORK4
         LH    R1,RCVASMQA         GET AVG 'ASMQ'
         ST    R1,DECWORK1
         BAL   R14,@DECIMAL
         MVC   L1+62(4),DECWORK4
         LH    R1,RCVUICA          GET 'UIC' AVG
         ST    R1,DECWORK1
         BAL   R14,@DECIMAL
         MVC   L1+73(4),DECWORK4
         B     DISPLAY
         EJECT
*
**       NOW DISPLAY THE SCREEN AND WAIT AND REFRESH IT
*
DISPLAY  DS    0H
         OI    IOCBFLGD,IOCBDYON   TURN ON DELAY
         OI    IOCBFLGC,IOCBCRAO   AND FORCE RETURN OF PFK
         TRMIO IOCB,WRITE,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY K3=MODE,K9=DCMSOVER,K12=EXIT,ATTN=EXIT,DEF=GO
         EJECT
*
**       CHANGE MODE TO ALLOW JOBNAME TO BE CHANGED
*
MODE     DS                        0H
         MVC   L2(79),MODMSG      TELL WHAT TO MODIFY
         NI    IOCBFLGD,255-IOCBDYON   TURN OFF DELAY
         OI    IOCBFLGC,IOCBCRAO   FORCE RETURN OF PFK
         TRMIO IOCB,FORMAT=FMT2,IMAGE=SCREEN
         PFKEY K9=DCMSOVER,K12=EXIT,ATTN=EXIT,ENTER=MODE0,DEF=MODE0
         SPACE 5
*
**       NOW CHECK FOR MODIFIED JOBNAMES
*
MODE0    LA    R10,DSPAREA
         LA    R15,MAXJOBS         SET UP REGISTERS FOR OUTER LOOP
MODE1    L     R9,DSPINDX          GET SCREEN INDEX FOR THIS JOB
         LA    R9,SCREEN(R9)       POINT TO JOBNAME
         OC    0(8,R9),=CL8' '     MAKE UPPER CASE
         CLC   DSPJOBNM,0(R9)      WAS NAME CHANGED
         BE    MODE4               NOPE
         USING ASVT,R2             TEMP USING FOR INITIALIZATION
         L     R1,16               GET CVT
         L     R2,CVTASVT-CVTMAP(R1) ACCESS ASVT
         L     R3,ASVTMAXU         GET NUMBER OF ADDRESS SPACES
         LA    R2,ASVTENTY         POINT TO ASVT ENTRIES
MODE2    TM    0(R2),ASVTAVAL      IS THIS ONE ACTIVE
         BO    MODE3               NOPE
         L     R4,0(R2)            GET ADDR OF ASCB
         TM    ASCBRCTF,ASCBTMNO+ASCBOUT+ASCBTMLW  IS IT IN CORE
         BNZ   MODE3               NOPE
         CLI   ASCBDSP1,0          IS IT DISPATCHABLE
         BNE   MODE3               NOPE
         ICM   R1,15,ASCBJBNI      GET NAME OF JOB IF ANY
         BNZ   *+8                 AROUND IF GOOD
         L     R1,ASCBJBNS         GET NAME OF STARTED TASK
         CLC   0(8,R1),0(R9)       IS THIS THE JOB
         BNE   MODE3               NOPE
         MVC   DSPJOBNM,0(R1)      YES SO SAVE JOBNAME
         ST    R4,DSPASCB            AND ASCB ADDR
         MVC   DSPOUXB,ASCBOUXB           AND OUXB ADDR
         XC    DSPPIN(9*4),DSPPIN  CLEAR COUNTERS
         B     MODE4               CHECK NEXT JOB NAME
MODE3    LA    R2,4(R2)            GET TO NEXT ASVT ENTRY
         BCT   R3,MODE2            AND TRY AGAIN
MODE4    LA    R10,DSPLEN(R10)     MOVE TO NEXT JOB
         BCT   R15,MODE1           TRY AGAIN
         B     GO                  ****NONE FOR NOW
         EJECT
*
**       SCREEN FORMAT AND INITIALIZATION AREA
*
         SPACE 3
MODMSG   DC    CL79'CHANGE THE JOBNAMES ONLY AND HIT ENTER'
FORM     DC    CL79'PAGING HOGS             SYSTEM WIDE CPU=XXXX  PAGE=*
               XXXX  ASMQ=XXXX   UIC=XXXX'
         DC    CL79' '
         DC    CL79' '
         DC    CL79'PAGE IN'
         DC    CL79'PAGE OUT'
         DC    CL79'PAGE REC'
         DC    CL79'VIO  IN'
         DC    CL79'VIO  OUT'
         DC    CL79'VIO  REC'
         DC    CL79'COMN IN'
         DC    CL79'COMN REC'
         DC    CL79'STOLEN'
         DC    CL79' '
         DC    CL79' '
         DC    CL79'PAGE IN'
         DC    CL79'PAGE OUT'
         DC    CL79'PAGE REC'
         DC    CL79'VIO  IN'
         DC    CL79'VIO  OUT'
         DC    CL79'VIO  REC'
         DC    CL79'COMN IN'
         DC    CL79'COMN REC'
         DC    CL79'STOLEN'
         DC    CL79' '
FORMAT   SFMT
L1       FIELD 79
L2       FIELD 79
L3       FIELD 79
L4       FIELD 79,INTEN=HI
L5       FIELD 79,INTEN=HI
L6       FIELD 79,INTEN=HI
L7       FIELD 79,INTEN=LO
L8       FIELD 79,INTEN=LO
L9       FIELD 79,INTEN=LO
L10      FIELD 79,INTEN=HI
L11      FIELD 79,INTEN=HI
L12      FIELD 79,INTEN=HI
L13      FIELD 79
L14      FIELD 79
L15      FIELD 79,INTEN=HI
L16      FIELD 79,INTEN=HI
L17      FIELD 79,INTEN=HI
L18      FIELD 79,INTEN=LO
L19      FIELD 79,INTEN=LO
L20      FIELD 79,INTEN=LO
L21      FIELD 79,INTEN=HI
L22      FIELD 79,INTEN=HI
L23      FIELD 79,INTEN=HI
L24      FIELD 79
         SFEND
FMT2     SFMT
L1       FIELD 79
L2       FIELD 79
L3       FIELD 79,ALPHA
L4       FIELD 79
L5       FIELD 79
L6       FIELD 79
L7       FIELD 79
L8       FIELD 79
L9       FIELD 79
L10      FIELD 79
L11      FIELD 79
L12      FIELD 79
L13      FIELD 79
L14      FIELD 79,ALPHA
L15      FIELD 79
L16      FIELD 79
L17      FIELD 79
L18      FIELD 79
L19      FIELD 79
L20      FIELD 79
L21      FIELD 79
L22      FIELD 79
L23      FIELD 79
L24      FIELD 79
         SFEND
         EJECT
*
**       EXIT CODE FOR THIS MODULE
*
EXIT     DCMSTOP SCREEN=NO
         EJECT
*
**       DISPLAY AREA CONTROLLER DSECT
*
         SPACE 3
DSP      DSECT
DSPJOBNM DS    CL8                 JOBNAME
DSPASCB  DS    A                   ADDR OF ASCB
DSPOUXB  DS    A                   ADDR OF OUXB
DSPINDX  DS    F                   DISPLACEMENT INTO SCREEN FOR START
DSPPIN   DS    F                   PAGES IN
DSPPOUT  DS    F                   PAGES OUT
DSPPREC  DS    F                   PAGES RECLAIMED IN PRIVATE AREA
DSPVAMI  DS    F                   VIO PAGES IN
DSPVAMO  DS    F                   VIO PAGES OUT
DSPVAMR  DS    F                   VIO PAGES RECLAIMED
DSPCAPI  DS    F                   COMMON AREA PAGES IN
DSPCAPR  DS    F                   COMMON AREA PAGES RECLAIMED
DSPSTCT  DS    F                   PAGES STOLEN FROM THIS ADDRESS SPACE
DSPLEN   EQU   *-DSP
         SPACE 5
DCMSWORK DSECT
PREVTIME DS    F                   TIME OF END OF PREV PERIOD
INTERV   DS    F                   CURRENT INTERVAL
WORK     DS    D
DSPAREA  DS    (MAXJOBS)XL(DSPLEN) DISPLAY CONTROLLER AREA
SCREEN   EQU   *
         DSGEN FORMAT
DCMSLEN  EQU   *-DCMSWORK
         EJECT
*
**       SYSTEM DSECTS    CVT,ASCB,ASVT,OUXB
*
         PRINT NOGEN
         CVT     DSECT=YES
         IHAASCB
         IHAASVT
RMCNS    RMCNS
         END
