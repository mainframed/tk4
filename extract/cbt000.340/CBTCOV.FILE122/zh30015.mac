++  PTF  (ZH30015).
++  VER  (Z037)
    PRE  (UZ81200)
/*
 MOD AFCCH15 - FORCE DEFERED MOUNTING ON DUALDENS OUTPUT TAPES.
               THIS CAUSES MVS TO ISSUE THE MOUNT MESSAGE WHICH
               GIVES DENSITY INFORMATION SO OPERATORS KNOW WHICH
               SERIES SCRATCH TAPE TO MOUNT.
*/.
++  SRCUPD  (IATUX06)  DISTLIB (AJES3SRC)  BASE (FIXED).
./  CHANGE  NAME=IATUX06
         USING IATYIDD,R13         ADDRESSABILITY TO INT W A    AFCCH15
         STM   R0,R15,IDDUSAVE     SAVE ALL REGS                AFCCH15
         SPACE 2                                                AFCCH15
*-------------------------------------------------------------* AFCCH15
*  MOD TO FORCE DEFER'ED MOUNTING FOR SCRATCH TAPES REQUESTED * AFCCH15
*  ON DUAL DENSITY DRIVES.  THIS WILL CAUSE INITIAL MOUNT MSG * AFCCH15
*  TO BE ISSUED BY OS/MVS OPEN, GIVING OPERATOR DENSITY INFO. * AFCCH15
*-------------------------------------------------------------* AFCCH15
         SPACE 1                                                AFCCH15
         TM    DDLFLG6,DDLDSCR     SCRATCH REQUEST?             AFCCH15
         BC    ALLOFF,DFRCKDUN     NO, FORGET IT.               AFCCH15
         L     R3,DDLUNIT          GET POINTER TO UNIT FIELD    AFCCH15
         LA    R4,UNITNMS          GET ADDR OF TABLE            AFCCH15
LOOPTBL1 CLI   0(R4),X'FF'         END OF TABLE?                AFCCH15
         BC    EQ,DFRCKDUN         YES, ALL DONE                AFCCH15
         CLC   0(8,R3),0(R4)       IS UNITNAME ON TABLE?        AFCCH15
         BC    EQ,FORCEDFR         YES, BRANCH                  AFCCH15
         LA    R4,8(R4)            BUMP TABLE POINTER           AFCCH15
         B     LOOPTBL1            GO CHECK NEXT                AFCCH15
FORCEDFR EQU   *                                                AFCCH15
         TM    IDDFTRC1,IDDCISE    MVS CONVERTER/INTERPRETER?   AFCCH15
         BC    ALLOFF,DFRCKDUN     NO, MUST BE FOR ASP MAIN     AFCCH15
         OI    DDLFLG6,DDLDEFER    TURN ON DEFER'ED MOUNT       AFCCH15
*   DDLUNIQ IS LTTR IF ASPMAIN. MUST USE RIGET A LA IATIIRM.    AFCCH15
         L     R5,DDLUNIQ          GET SIOT ADDRESS             AFCCH15
         OI    73(R5),X'02'        TURN ON DEFER MOUNT IN SIOT  AFCCH15
DFRCKDUN EQU   *                                                AFCCH15
         LM    R0,R15,IDDUSAVE     RESTORE REGS                 AFCCH15
UNITNMS  DC    CL8'TAPE1600'       TABLE OF UNIT NAMES          AFCCH15
         DC    CL8'TAPE800 '         REQUIRING DEFER'ED         AFCCH15
         DC    CL8'3400-6'             MOUNTING                 AFCCH15
         DC    CL8'3400-4'                                      AFCCH15
         DC    CL8'3400-3'                                      AFCCH15
UNITNEND DC    X'FF'               DELIMITER                    AFCCH15
