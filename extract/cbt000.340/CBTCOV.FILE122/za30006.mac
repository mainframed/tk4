++  PTF  (ZA30006).
++  VER  (Z037)
    PRE  (UZ81200)
/*
 MOD AFCPT06 - ALLOW A PROBLEM PROGRAM TO ISSUE A JES3 COMMAND BY
               ISSUING A WTO IN THE FOLLOWING FORM;
               JESCMD '*---JES3 COMMAND---'            OR
               ASPCMD '*---JES3 COMMAND---'
*/.
++  SRCUPD  (IATUX31)  DISTLIB (AJES3SRC)  BASE (FIXED).
./  CHANGE  NAME=IATUX31
MSGDSECT DSECT                MESSAGE MAPPING DSECT             AFCPT06
MSGLNTH  DS    X                                                AFCPT06
         SPACE 2                                                AFCPT06
MSGJS    DS    0CL128         JES3 MESSAGES                     AFCPT06
MSGJSNUM DS    CL7            MESSAGE NUMBER                    AFCPT06
MSGJSTXT DS    CL121          MESSAGE TEXT                      AFCPT06
         ORG   MSGJS                                            AFCPT06
         SPACE 2                                                AFCPT06
MSGVS    DS    0CL128         MVS MESSAGES                      AFCPT06
MSGVSPRE DS    CL7            CPUID PREFIX                      AFCPT06
MSGVSJNM DS    CL8            MESSAGE JOB NAME                  AFCPT06
         DS    C                                                AFCPT06
MSGVSACT DS    C              OS ACTION BYTE                    AFCPT06
MSGVSNUM DS    CL7            MESSAGE NUMBER                    AFCPT06
MSGVSTXT DS    CL104          MESSAGE TEXT                      AFCPT06
         ORG   MSGJS                                            AFCPT06
         SPACE 2                                                AFCPT06
MSGVT    DS    0CL128         MVT MESSAGES                      AFCPT06
MSGVTPRE DS    CL7            R=CPUID PREFIX                    AFCPT06
MSGVTJNM DS    CL8            MESSAGE JOB NAME                  AFCPT06
MSGVTACT DS    C              OS ACTION BYTE                    AFCPT06
MSGVTNUM DS    CL7            MESSAGE NUMBER                    AFCPT06
MSGVTTXT DS    CL104          MESSAGE TEXT                      AFCPT06
         EJECT                                                  AFCPT06
*        XC    USEREXIT,USEREXIT   PREVENT FURTHER ENTRY        AFCPT06
         LR    R9,R1          R9=COUNT & MESSAGE                AFCPT06
         USING MSGDSECT,R9                                      AFCPT06
         LR    R8,R0          R8=CONSOLE ID/DESTINATION         AFCPT06
         CLC   MSGVSNUM(7),JESCMD  MESSAGE TO BE PROCESSED?     AFCPT06
         BE    JCMD1                                            AFCPT06
         CLC   MSGVSNUM(7),ASPCMD                         ?     AFCPT06
         BE    JCMD1                                            AFCPT06
         CLC   MSGVTNUM(7),JESCMD                         ?     AFCPT06
         BE    JCMD                                             AFCPT06
         CLC   MSGVTNUM(7),ASPCMD                         ?     AFCPT06
         BE    JCMD                                             AFCPT06
         B     NORMRETN       NO-DON'T PROCESS IT               AFCPT06
         SPACE 3                                                AFCPT06
*                                                               AFCPT06
* PROBLEM PROGRAM ISSUED A JES3 COMMAND VIA A WTO               AFCPT06
*                                                               AFCPT06
JCMD     LA    R2,MSGVTTXT    R2=COMMAND TO BE ISSUED           AFCPT06
         B     JCMD2          CONTINUE                          AFCPT06
JCMD1    LA    R2,MSGVSTXT    R2=COMMAND TO BE ISSUED           AFCPT06
JCMD2    CLI   0(R2),C''''    IS IT VALID?                      AFCPT06
         BNE   NORMRETN       NO-IGNORE IT                      AFCPT06
JCMD3    AGETMAIN SIZE=F88,BUSY=JCMD4   GET SOME SPACE          AFCPT06
         LR    R7,R1          R7=GETMAINED AREA                 AFCPT06
         LA    R3,80          MAX LENGTH OF COMMAND             AFCPT06
         B     JCMD5          CONTINUE                          AFCPT06
JCMD4    AWAIT ECFMASK=(R0),ECFADD=(R0),NORMAL=JCMD3  WAIT      AFCPT06
JCMD5    MVC   1(1,R1),1(R2)  MOVE 1 CHARACTER TO WORK AREA     AFCPT06
         CLC   2(2,R2),=X'7D00'  END OF COMMAND?                AFCPT06
         BE    JCMD6          YES                               AFCPT06
         CLC   2(2,R2),=X'7D40'  END OF COMMAND?                AFCPT06
         BE    JCMD6          YES                               AFCPT06
         LA    R1,1(,R1)      BUMP POINTER                      AFCPT06
         LA    R2,1(,R2)      BUMP POINTER                      AFCPT06
         BCT   R3,JCMD5       LOOP THROUGH COMMAND              AFCPT06
JCMD6    LA    R2,80          MAX LENGTH AGAIN                  AFCPT06
         SR    R2,R3          R2=LENGTH OF COMMAND              AFCPT06
         LA    R2,1(,R2)      R2=INTERCOM LENGTH                AFCPT06
         STC   R2,0(R7)       SAVE LENGTH                       AFCPT06
         LTR   R2,R2          ANY COMMAND?                      AFCPT06
         BZ    JCMD7          NO-SKIP INTERCOM                  AFCPT06
         INTERCOM CONS=DUMMY,TEXT=(R7),MSG=NO SEND COMMAND      AFCPT06
JCMD7    APUTMAIN SIZE=F88,AREA=(R7)    FREE THE AREA           AFCPT06
         LR    R0,R8          RESTORE R0                        AFCPT06
         LR    R1,R9          RESTORE R1                        AFCPT06
         LA    R15,8                                            AFCPT06
         B     RETURN                                           AFCPT06
         SPACE 3                                                AFCPT06
NORMRETN SR    R15,R15        NORMAL RETURN                     AFCPT06
RETURN   DS    0H                                               AFCPT06
         EJECT                                                  AFCPT06
F88      DC    F'88'                                            AFCPT06
JESCMD   DS    C'JESCMD '                                       AFCPT06
ASPCMD   DC    C'ASPCMD '                                       AFCPT06
         LTORG                                                  AFCPT06
