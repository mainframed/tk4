//ACTRT EXEC ASMFC
//SYSOBJ  DD  DSN=&&IEFACT,UNIT=VIO,DISP=(,PASS),
//  SPACE=(CYL,(5,1)),DCB=(RECFM=F,LRECL=80,BLKSIZE=80)
//SYSIN DD *
 TITLE '*** SMF ACCOUNTING ROUTINE EXIT FOR STEP TERM COND CODE DISPLAYX
                ***'
         PUNCH '++  PTF  (ZH00001)  .'
         PUNCH '++  VER  (Z037)  .'
         PUNCH '++  MOD  (IEFACTRT).'
IEFACTRT CSECT
         IATYREG
         EJECT
         STM   R14,R12,12(R13)     SAVE REGS
         LR    R12,R15             SET UP BASE
         USING IEFACTRT,R12        TELL ASSEMBLER
         LR    R11,R1              SAVE PARM LIST POINTER
         LR    R10,R0              SAVE REG 0
         GETMAIN R,SP=255,LV=72    GET CORE FOR SAVE AREA
         ST    R13,4(R1)           SET UP STANDARD
         ST    R1,8(R13)             SAVE AREA
         LR    R13,R1                  LINKAGE
         SPACE 3
*  DISPLAY STEP TERM COND CODE ON CONSOLE IF APPROPRIATE
         SPACE 1
         CH    R10,=H'12'          IS ENTRY FOR STEP TERM?
         BNE   NEXT                NO, DON'T DO MSG
         L     R2,36(R11)          GET POINTER TO TYPE 4 RECORD
         LTR   R2,R2               IS THERE ONE?
         BZ    NEXT                NO, CAN'T DO MSG
         LA    R2,4(R2)            ADJUST PAST RDW
         CLI   14(R2),C'Z'         DOES JOBNAME START WITH 'Z'?
         BE    CKIND               YES, GO CHECK FURTHER
         CLI   54(R2),C'I'         DOES PGMNAME START WITH 'I'?
         BNE   NEXT                GO TERM - NO MSG
CKIND    TM    83(R2),X'03'        TEST ABEND/FLUSH BITS
         BNZ   NEXT                BRANCH OUT - NOT NORMAL COMPLETION
         GETMAIN R,SP=255,LV=66    GET CORE FOR WTOMSG & WORK AREA
         LR    R3,R1               SAVE ADDR OF WORK AREA/GOTTEN CORE
         LA    R4,8(R3)            R4=ADDR OF WTO MSG
         MVC   0(58,R4),WTOMSG     MOVE IN WTO PATTERN
         MVC   12(8,R4),14(R2)     MOVE JOBNAME TO MSG
         MVC   21(8,R4),62(R2)     MOVE STEPNAME TO MSG
         LH    R5,51(R2)           GET COND CODE
         CVD   R5,0(R3)            CONVERT IT
         UNPK  50(4,R4),5(3,R3)      INTO MSG
         MVZ   53(1,R4),52(R4)     FIX UP LO-ORDER BYTE
         LR    R1,R4               GET WTO PARM ADDR IN R1
         SVC   35                  WTO SVC
         FREEMAIN R,A=(3),SP=255,LV=66
         B     NEXT
         SPACE 1
WTOMSG   WTO   'AFC142I JOBNAME .STEPNAME STEP TERM COND CODE=XXXX',   X
               ROUTCDE=2,MF=L
         DS    0H
         SPACE 3
*******************************************************
*  PLACE CODE HERE TO ADD FUNCTION TO THIS MODULE     *
*       (REG 10 CONTAINS CONTENTS OF REG 0 AT ENTRY)  *
*       (REG 11 POINTS TO ADDRESS LIST)               *
*******************************************************
         SPACE 1
NEXT     EQU   *
         SPACE 3
*  STANDARD TERMINATION
         SPACE 1
TERM     LR    R1,R13
         L     R13,4(R13)
         FREEMAIN R,A=(1),SP=255,LV=72
         LM    R14,R12,12(R13)
         SR    R15,R15
         ST    R15,8(R13)
         BR    R14
         END
//ZH00001  EXEC  MVSSMP
//SMPPTFIN  DD  DSN=&&IEFACT,DISP=(OLD,DELETE)
//SYSIN  DD  *
  RECEIVE .
  APPLY  S  (ZH00001) .
  ACCEPT S (ZH00001).
