++  PTF  (ZH30016).
++  VER  (Z037)
    PRE  (UZ81200)
/*
 MOD AFCCH16 - AUTOMATICALLY ASSIGN PERFORMANCE GROUPS BASED ON AFAFC
               STANDARDS.
*/.
++  SRCUPD  (IATUX05)  DISTLIB (AJES3SRC)  BASE (FIXED).
./  CHANGE  NAME=IATUX05
*              IEFASCTB            R/W     (SCT MAPPING)        AFCCH16
SCT      DSECT                                                  AFCCH16
         IEFASCTB                                               AFCCH16
         EJECT                                                  AFCCH16
         USING IATYIDD,R13                                      AFCCH16
         TM    IDDFTRC1,IDDCISE    WAS CI SPECIFIED?            AFCCH16
         BC    ALLOFF,NORMRET      NO, GOBACK                   AFCCH16
         STM   R0,R15,IDDUSAVE     SAVE REGS                    AFCCH16
         L     R9,IDDCCSCT         GET                          AFCCH16
         SRL   R9,8                  ADDRESSABILITY             AFCCH16
         LA    R9,16(R9)               TO SCT                   AFCCH16
         USING SCT,R9              TELL ASSEMBLER               AFCCH16
         SPACE 2                                                AFCCH16
*************************************************************** AFCCH16
*  USER MOD TO AUTOMATICALLY ASSIGN PERFORMANCE GROUPS        * AFCCH16
*************************************************************** AFCCH16
         SPACE 1                                                AFCCH16
         TM    IDDGTRC1,IDDGSMF    STARTED TASK?                AFCCH16
         BC    ALLOFF,STRTSK       YES, BRANCH                  AFCCH16
         TM    IDDGTRC1,IDDGTSOL   TSO LOGON?                   AFCCH16
         BC    ALLOFF,BATCH        NO,BRANCH - MUST BE BATCH    AFCCH16
         MVI   SCTPRFMF,X'02'      * PGN2 - TSO LOGON      *    AFCCH16
         B     RETURN                                           AFCCH16
         SPACE 2                                                AFCCH16
STRTSK   EQU   *                                                AFCCH16
         CLC   IDDJOBNM,=C'OSTIQ   ' OSTIQ?                     AFCCH16
         BC    EQ,OSTIQ                                         AFCCH16
         CLC   SCTPGMNM,=C'IPCNTRL ' INTERPOL?                  AFCCH16
         BC    EQ,INTERPOL                                      AFCCH16
OTHSTTSK MVI   SCTPRFMF,X'07'      * PGN7 - OTH STRTD TSKS *    AFCCH16
         B     RETSTSK                                          AFCCH16
INTERPOL MVI   SCTPRFMF,X'06'      * PGN6 - INTERPOL       *    AFCCH16
         B     RETURN                                           AFCCH16
OSTIQ    MVI   SCTPRFMF,X'05'      * PGN5 - OSTIQ          *    AFCCH16
         B     RETURN                                           AFCCH16
         SPACE 2                                                AFCCH16
BATCH    EQU   *                                                AFCCH16
         CLC   SCTPGMNM,=C'FDR     ' FDR?                       AFCCH16
         BC    EQ,FDR                                           AFCCH16
         CLI   IDDJOBNM,C'Z'         ADOS?                      AFCCH16
         BC    EQ,ADOS                                          AFCCH16
SORTCHK  LA    R8,SORTTBL                                       AFCCH16
SORTLOOP CLI   0(R8),X'FF'         END OF TABLE?                AFCCH16
         BC    EQ,COMPCHK                                       AFCCH16
         CLC   SCTPGMNM,0(R8)        SORT?                      AFCCH16
         BC    EQ,SORTCLAS                                      AFCCH16
         LA    R8,8(R8)            BUMP TABLE POINTER           AFCCH16
         B     SORTLOOP            LOOP THE TABLE               AFCCH16
SORTCLAS CLI   IDDJCLAS,C'B'         PRODUCTION SORT?           AFCCH16
         BC    EQ,SORTPROD                                      AFCCH16
         MVI   SCTPRFMF,X'16'      * PGN22 - TEST SORTS    *    AFCCH16
         B     SORTRGN                                          AFCCH16
COMPCHK  LA    R8,COMPTBL                                       AFCCH16
COMPLOOP CLI   0(R8),X'FF'         END OF TABLE?                AFCCH16
         BC    EQ,BATCHTYP                                      AFCCH16
         CLC   SCTPGMNM,0(R8)        COMPILE?                   AFCCH16
         BC    EQ,COMPILE                                       AFCCH16
         LA    R8,8(R8)            BUMP TABLE POINTER           AFCCH16
         B     COMPLOOP            LOOP THE TABLE               AFCCH16
FDR      MVI   SCTPRFMF,X'14'      * PGN20 - FDR           *    AFCCH16
         B     RETURN                                           AFCCH16
SORTPROD MVI   SCTPRFMF,X'15'      * PGN21 - PROD SORTS    *    AFCCH16
SORTRGN  LH    R7,=H'500'                                       AFCCH16
         CH    R7,SCTMSSZE         IS REGION > 500K?            AFCCH16
         BC    GE,RETURN           NO, BRANCH                   AFCCH16
         STH   R7,SCTMSSZE         * INSURE REGION NOT > 500K * AFCCH16
         B     RETURN                                           AFCCH16
COMPILE  MVI   SCTPRFMF,X'17'      * PGN23 - COMPILES      *    AFCCH16
         B     RETURN                                           AFCCH16
         SPACE 2                                                AFCCH16
BATCHTYP EQU   *                                                AFCCH16
         CLC   IDDJOBNM(6),=C'XRTELE'   TELE?                   AFCCH16
         BC    EQ,TELE                                          AFCCH16
         CLI   IDDJCLAS,C'B'         PRODUCTION?                AFCCH16
         BC    NE,TESTCLAS                                      AFCCH16
PRODCLAS CLC   SCTPGMNM,=C'C19AS   ' PROD JUMPS UPDATE?         AFCCH16
         BC    EQ,PRODUPDT                                      AFCCH16
         CLC   SCTPGMNM,=C'C19GR   ' MONTH-END RESTRUCTURE?     AFCCH16
         BC    EQ,PRODUPDT                                      AFCCH16
         CLC   SCTPGMNM,=C'C19MRC  ' MASS RATE?                 AFCCH16
         BC    EQ,PRODUPDT                                      AFCCH16
         CLC   IDDJOBNM,=C'JRAAGD  ' TELE-CREATED HOTJOB        AFCCH16
         BC    EQ,ADOS                                          AFCCH16
         CLC   SCTPGMNM,=C'A0F01D  ' A0F CARD-TO-TAPE?          AFCCH16
         BC    EQ,CKBD                                          AFCCH16
         LA    R8,CKBDTBL                                       AFCCH16
CKBDLOOP CLI   0(R8),X'FF'         END OF TABLE?                AFCCH16
         BC    EQ,PRODOTHR                                      AFCCH16
         CLC   SCTPGMNM,0(R8)        CHECK/BOND WRITING PGM?    AFCCH16
         BC    EQ,CKBD                                          AFCCH16
         LA    R8,8(R8)            BUMP TABLE POINTER           AFCCH16
