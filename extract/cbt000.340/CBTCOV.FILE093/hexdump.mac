HEXDUMP  TITLE 'QUEUE COMMAND - HEX DUMP OF PASSED DATA AREA'     ONL01
HEXDUMP  QSTART ,                  START HEXDUMP ASSEMBLY         ONL01
         L     R10,QVCKPT     LOAD BASE REG
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R8,QVPRINT     LOAD BASE REG
         USING QCPRINT,R8     BASE REG FOR DISPLAY WORK AREA
         LA    R8,QPLINE      LOAD BASE REG
         USING WORK,R8        BASE REG FOR PRINT LINE AREA
***********************************************************************
*                                                                     *
*   INPUT:                                                            *
*        R1    POINTER TO DATA AREA TO BE DUMPED                      *
*        R0    OFFSET AND LENGTH OF AREA TO DUMP (2 BYTES EACH)       *
*              (OFFSET IS USED FOR PRINT DISPLACEMENT FIELD ONLY)     *
*              (AS AN EXAMPLE, THE DUMP OF A JCT IS MOST USEFUL       *
*              IF THE OFFSET IS SET TO THE LENGTH OF ITS              *
*              ASSOCIATED IOB)                                        *
*                                                                     *
***********************************************************************
         LR    R6,R1          POINTER TO START OF DATA AREA
         LR    R3,R0          PRINT OFFSET FOR AREA
         SRL   R3,16          MOVE TO PROPER POSITION
         LR    R4,R0          LENGTH TO DUMP
         SLL   R4,16          THROW AWAY OFFSET
         SRL   R4,16          MOVE LENGTH TO PROPER POSITION
***********************************************************************
*                                                                     *
*   FORMAT THE AREA                                                   *
*                                                                     *
***********************************************************************
         MVC   QDHLINE,QBLANK BLANK THE TITLE LINE
         LA    R5,WORKLINE    POINT TO LINE IN WORK AREA
         ST    R5,QDMSGA      STORE ADDR OF PRINT LINE
         MVC   QDMLNG,=H'80'  LENGTH OF MESSAGE
         CLC   QDLNELEN,=H'132' LONG ENOUGH FOR DOUBLE LINE?
         BL    *+4+6          NO, SKIP RESET OF LENGTH
         MVC   QDMLNG,=H'132' LENGTH OF MESSAGE
         SPACE 1
LOOP     MVC   WORKLINE,QBLANK  BLANK THE WORK AREA
         STH   R3,QDWORD      GET OFFSET
         UNPK  OFFSET(5),QDWORD(3) CONVERT TO HEX
         MVI   OFFSET+L'OFFSET,C' ' CLEAR GARBAGE BYTE
         TR    OFFSET,TABLEP  TRANSLATE TO PRINTABLE CHARACTERS
         SPACE 1
         LR    R2,R6          POINTER TO DATA AREA
         LA    R1,4           NUMBER OF WORDS IN LINE AREA
         LA    R14,O1         POINTER TO FIRST OUTPUT AREA
         LA    R15,P1         POINTER TO EBCDIC AREA
         MVI   PS1,C'*'       SET STARS
         MVI   PS2,C'*'       SET STARS
         CLC   QDLNELEN,=H'132' SHORT LINES?
         BL    LOOP1          YES, SKIP SETTING ALTERNATE PRINT AREA
         MVI   PS1,C' '       FIX OTHER FLAG
         MVI   PS2,C' '       FIX OTHER FLAG
         LA    R15,P1A        ALTERNATE EBCDIC AREA
         MVI   PS1A,C'*'      SET STARS
         MVI   PS2A,C'*'      SET STARS
         SPACE 1
LOOP1    UNPK  0(9,R14),0(5,R2)  UNPACK DATA TO PRINT LINE
         TR    0(8,R14),TABLEP TRANSLATE TO PRINTABLE CHARACTERS
         MVI   8(R14),C' '    CLEAR WASTE BYTE
         MVC   0(4,R15),0(R2) COPY DATA TO PRINT AREA
         TR    0(4,R15),PRTAB FIX UNPRINTABLES
         LA    R2,4(,R2)      NEXT DATA AREA
         LA    R3,4(,R3)      BUMP OFFSET
         LA    R14,9(,R14)    NEXT HEX AREA
         LA    R15,4(,R15)    NEXT PRINT AREA
         SH    R4,=H'4'       DROP BY PROCESSED LENGTH
         BNP   PRINT1
         BCT   R1,LOOP1       LOOP FOR ALL FOUR WORDS
         SPACE 1
         CLC   QDLNELEN,=H'132' SHORT LINES?
         BL    PRINT1         YES, PRINT WHAT WE HAVE
         LA    R14,O5         POINT TO OUTPUT AREA
         LA    R15,P5A        POINT TO OUTPUT AREA
         LA    R1,4           NUMBER OF WORDS
         SPACE 1
LOOP2    UNPK  0(9,R14),0(5,R2)  UNPACK DATA TO PRINT LINE
         TR    0(8,R14),TABLEP TRANSLATE TO PRINTABLE CHARACTERS
         MVI   8(R14),C' '    CLEAR WASTE BYTE
         MVC   0(4,R15),0(R2) COPY DATA TO PRINT AREA
         TR    0(4,R15),PRTAB FIX UNPRINTABLES
         LA    R2,4(,R2)      NEXT DATA AREA
         LA    R3,4(,R3)      BUMP OFFSET
         LA    R14,9(,R14)    NEXT HEX AREA
         LA    R15,4(,R15)    NEXT PRINT AREA
         SH    R4,=H'4'       DROP BY PROCESSED LENGTH
         BNP   PRINT1
         BCT   R1,LOOP2       LOOP FOR ALL FOUR WORDS
         SPACE 1
PRINT1   L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        GO TO IT
         LR    R15,R6         ADDR OF DATA JUST DUMPED
         LR    R6,R2          ADDRESS OF NEXT TO DUMP
         SR    R2,R15         LENGTH JUST DUMPED
         BCTR  R2,0           DROP FOR EXECUTE
         SPACE 1
SKIP     DS    0H
         LTR   R4,R4          TEST REMAINING LENGTH
         BNP   STOP           YES. GO HOME.
         EX    R2,CLC         IS THIS RECORD THE SAME AS PREVIOUS?
         BNE   LOOP           NO, PRINT IT
         LA    R6,1(R2,R6)    BUMP TO NEXT AREA
         LA    R3,1(R2,R3)    BUMP OFFSET
         SR    R4,R2          DROP BY LENGTH DUMPED
         BCTR  R4,0           FIX FOR EXECUTE STUFF
         B     SKIP           SKIP PRINTING DUPS
         SPACE 1
CLC      CLC   0(*-*,R15),0(R6)  TEST FOR SAME DATA
***********************************************************************
*                                                                     *
*   RETURN                                                            *
*                                                                     *
***********************************************************************
STOP     QSTOP
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
* TABLE TO REMOVE UNPRINTABLES
PRTAB    DC    CL64' '
         DC    192AL1(*-PRTAB)
* TABLE FOR HEX UNCONVERT
TABLEP   EQU   *-240
         DC    C'0123456789ABCDEF'
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
WORK     DSECT ,
WORKLINE DS    CL132          LINE TO PRINT
         ORG   WORKLINE       BACK UP TO WORK LINE
OFFSET   DS    CL4            OFFSET INTO AREA
         DS    XL3            SPACER
O1       DS    CL8            OUTPUT HEX AREA
         DS    X              SPACER
O2       DS    CL8            OUTPUT HEX AREA
         DS    X              SPACER
O3       DS    CL8            OUTPUT HEX AREA
         DS    X              SPACER
O4       DS    CL8            OUTPUT HEX AREA
         DS    XL3            SPACER
O5       DS    CL8            OUTPUT HEX AREA
         DS    X              SPACER
O6       DS    CL8            OUTPUT HEX AREA
         DS    X              SPACER
O7       DS    CL8            OUTPUT HEX AREA
         DS    X              SPACER
O8       DS    CL8            OUTPUT HEX AREA
         DS    XL2            SPACER
PS1A     DS    C              STAR FOR PRINT AREA
P1A      DS    CL4            PRINT AREA
P2A      DS    CL4            PRINT AREA
P3A      DS    CL4            PRINT AREA
P4A      DS    CL4            PRINT AREA
P5A      DS    CL4            PRINT AREA
P6A      DS    CL4            PRINT AREA
P7A      DS    CL4            PRINT AREA
P8A      DS    CL4            PRINT AREA
PS2A     DS    C              STAR FOR PRINT AREA
         ORG   O5-1           BACK UP FOR SHORT LINES
PS1      DS    C              STAR FOR PRINT AREA
P1       DS    CL4            PRINT AREA
P2       DS    CL4            PRINT AREA
P3       DS    CL4            PRINT AREA
P4       DS    CL4            PRINT AREA
PS2      DS    C              STAR FOR PRINT AREA
         ORG   ,              BACK TO NORMAL ADDRESSING
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
