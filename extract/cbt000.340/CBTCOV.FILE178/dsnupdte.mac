*          DATA SET GU037000   AT LEVEL 003 AS OF 05/29/74
         TITLE 'GU037000 DSN REGISTER MASTER UPDATE'
         PRINT NOGEN
GU037000 SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     05/23/74.
*              REMARKS.  UPDATE DATA SET REGISTER MASTER.
*              FILES.
*                   VFDSNOLD - OLD MASTER INPUT.
*                   VFDSNNEW - NEW MASTER OUTPUT.
*                   VFDSNTRN - TRANSACTIONS INPUT.
*                   VFDSNRPT - UPDATE REPORT OUTPUT.
*              RECORD LAYOUT.
*                   BYTE   DESCRIPTION
*                   01-06   VOLUME
*                   07-50   DSN
*                   51-53   OWNER ID
*                   54-80   DESCRIPTION OR 'DELETE'
*
*
*
*
*
         USING OLDREC,R8
         USING TRNREC,R9
         OPEN  (VFDSNOLD,,VFDSNTRN,,VFDSNNEW,(OUTPUT),VFDSNRPT,        X
               (OUTPUT))
         PERFORM R100INIT,R190EXIT
         DO    WHILE,(CLI,EOJ,TRUE,NE)
               IF   (CLC,TRNKEY,OLDKEY,EQ)
               THEN BEGIN
                    IF   (CLC,TRNKEY,EOFKEY,EQ)
                    THEN BEGIN
                         MVI  EOJ,TRUE
                    THEN END
                    ELSE BEGIN
                         IF   (CLC,TRNDESC(6),=C'DELETE',EQ)
                         THEN BEGIN
                              PERFORM R200DEL,R290EXIT
                         THEN END
                         ELSE BEGIN
                              PERFORM R300CHNG,R390EXIT
                         ELSE END
                    ELSE END
               THEN END
               ELSE BEGIN
                    IF   (CLC,TRNKEY,OLDKEY,L)
                    THEN BEGIN
                         PERFORM R400ADD,R490EXIT
                    THEN END
                    ELSE BEGIN
                         PERFORM R500COPY,R590EXIT
                    ELSE END
               ELSE END
         DO    END
         PERFORM R600TOTL,R690EXIT
         CLOSE (VFDSNOLD,,VFDSNNEW,,VFDSNTRN,,VFDSNRPT)
         SUBEXIT
TRUE     EQU   0
FALSE    EQU   1
EOJ      DC    AL1(FALSE)
         TITLE 'R100 - INITIALIZE'
R100INIT PENTRY
         PERFORM R700GETO,R790EXIT
         PERFORM R800GETT,R890EXIT
         DTIME TIMEDATE
R190EXIT PEXIT
         TITLE 'R200 - DELETE'
R200DEL  PENTRY
         MVC   DDELREC,OLDREC
         LA    R2,DELLINE
         AP    PDEL,=P'1'
         PERFORM R1000RPT,R1090EXT
         PERFORM R700GETO,R790EXIT
         PERFORM R800GETT,R890EXIT
R290EXIT PEXIT
DELLINE  DS    0CL133
         DC    C' ',CL15'DELETED'
DDELREC  DC    CL80' '
         FILL  DELLINE
PDEL     DC    PL3'0'
         TITLE 'R300 - CHANGE'
R300CHNG PENTRY
         AP    PCHNG,=P'1'
         MVC   DOLDREC,OLDREC
         LA    R2,DOLDLINE
         PERFORM R1000RPT,R1090EXT
         MVC   DTRNREC,TRNREC
         LA    R2,DTRNLINE
         PERFORM R1000RPT,R1090EXT
         LA    R2,TRNREC
         PERFORM R900PUTN,R990EXIT
         PERFORM R700GETO,R790EXIT
         PERFORM R800GETT,R890EXIT
R390EXIT PEXIT
DOLDLINE DS    0CL133
         DC    C' ',CL15'OLD RECORD'
DOLDREC  DC    CL80' '
         FILL  DOLDLINE
DTRNLINE DS    0CL133
         DC    C' ',CL15'NEW RECORD'
DTRNREC  DC    CL80' '
         FILL  DTRNLINE
PCHNG    DC    PL3'0'
         TITLE 'R400 - ADD'
R400ADD  PENTRY
         IF    (CLC,TRNKEY,NEWKEY,NH)
         THEN  BEGIN
               MVC  DBADREC,TRNREC
               LA   R2,DBADLINE
               PERFORM R1000RPT,R1090EXT
               AP   PERR,=P'1'
         THEN  END
         ELSE  BEGIN
               AP   PADD,=P'1'
               MVC  DADDREC,TRNREC
               LA   R2,DADDLINE
               PERFORM R1000RPT,R1090EXT
               LA   R2,TRNREC
               PERFORM R900PUTN,R990EXIT
         ELSE  END
         PERFORM R800GETT,R890EXIT
R490EXIT PEXIT
DBADLINE DS    0CL133
         DC    C' ',CL15'OUT OF SEQ'
DBADREC  DC    CL80' '
         FILL  DBADLINE
PERR     DC    PL3'0'
DADDLINE DS    0CL133
         DC    C' ',CL15'ADDED RECORD'
DADDREC  DC    CL80' '
         FILL  DADDLINE
PADD     DC    PL3'0'
         TITLE 'R500 - COPY'
R500COPY PENTRY
         LA    R2,OLDREC
         PERFORM R900PUTN,R990EXIT
         PERFORM R700GETO,R790EXIT
R590EXIT PEXIT
         TITLE 'R600 - TOTALS'
R600TOTL PENTRY
         EDIT  TO=DOLD,FROM=POLD,MASK=' ZZ,ZZZ'
         EDIT  TO=DTRN,FROM=PTRN,MASK=' ZZ,ZZZ'
         EDIT  TO=DOUT,FROM=POUT,MASK=' ZZ,ZZZ'
         EDIT  TO=DADD,FROM=PADD,MASK=' ZZ,ZZZ'
         EDIT  TO=DCHNG,FROM=PCHNG,MASK=' ZZ,ZZZ'
         EDIT  TO=DDEL,FROM=PDEL,MASK=' ZZ,ZZZ'
         EDIT  TO=DERR,FROM=PERR,MASK=' ZZ,ZZZ'
         LA    R2,DTOTLINE
         PERFORM R1000RPT,R1090EXT
R690EXIT PEXIT
DTOTLINE DS    0CL133
         DC    C'0',C'OLD REC IN'
DOLD     DC    CL7' ',C' TRN REC IN'
DTRN     DC    CL7' ',C' MST REC OUT'
DOUT     DC    CL7' ',C' ADDITIONS'
DADD     DC    CL7' ',C' CHANGES'
DCHNG    DC    CL7' ',C' DELETES'
DDEL     DC    CL7' ',C' ERRORS'
DERR     DC    CL7' '
         FILL  DTOTLINE
         TITLE 'R700 - GET OLD MASTER'
R700GETO PENTRY
         GET   VFDSNOLD
         LR    R8,R1
         AP    POLD,=P'1'
         B     R790EXIT
R710EOF  TAG
         LA    R8,EOFKEY
R790EXIT PEXIT
VFDSNOLD DCB   DSORG=PS,MACRF=GL,DDNAME=VFDSNOLD,EODAD=R710EOF
POLD     DC    PL3'0'
         TITLE 'R800 - GET TRANSACTION'
R800GETT PENTRY
         GET   VFDSNTRN
         LR    R9,R1
         AP    PTRN,=P'1'
         B     R890EXIT
R810EOF  TAG
         LA    R9,EOFKEY
R890EXIT PEXIT
VFDSNTRN DCB   DSORG=PS,MACRF=GL,DDNAME=VFDSNTRN,EODAD=R810EOF
PTRN     DC    PL3'0'
         TITLE 'R900 - PUT NEW MASTER'
R900PUTN PENTRY
         MVC   NEWKEY,0(R2)
         PUT   VFDSNNEW,(R2)
         AP    POUT,=P'1'
R990EXIT PEXIT
POUT     DC    PL3'0'
VFDSNNEW DCB   DSORG=PS,MACRF=PM,DDNAME=VFDSNNEW,RECFM=FB,LRECL=80
         TITLE 'R1000 - REPORT DRIVER'
R1000RPT PENTRY
         IF    (SP,LINE,=P'1',NP)
         THEN  BEGIN
               ZAP  LINE,MAXLINE
               AP   PAGE,=P'1'
               EDIT TO=DPAGE,FROM=PAGE,MASK=' ZZZ'
               LA   R0,HEAD1
               PERFORM R1100PRT,R1190EXT
               LA   R0,SPACE
               PERFORM R1100PRT,R1190EXT
         THEN  END
         ELSE  CONTINUE
         LR    R0,R2
         PERFORM R1100PRT,R1190EXT
R1090EXT PEXIT
LINE     DC    PL2'0'
MAXLINE  DC    PL2'55'
PAGE     DC    PL2'0'
HEAD1    DS    0CL133
         DC    C'1',C'DATA SET REGISTER MASTER UPDATE REPORT GU037000 '
TIMEDATE DC    CL22' ',C' PAGE'
DPAGE    DC    CL4' '
         FILL  HEAD1
SPACE    DC    CL133' '
         TITLE 'R1100 - PRINT LINE'
R1100PRT PENTRY
         PUT   VFDSNRPT
R1190EXT PEXIT
VFDSNRPT DCB   DSORG=PS,MACRF=PM,DDNAME=VFDSNRPT,RECFM=FA,BLKSIZE=133
         TITLE 'COMMON DATA'
         EQUAL
         TITLE 'RECORD LAYOUTS'
EOFKEY   DC    0CL50' ',50X'FF'
NEWKEY   DC    0CL50' ',50X'00'
OLDREC   DSECT
OLDVOL   DS    CL6
OLDDSN   DS    CL44
         ORG   OLDREC
OLDKEY   DS    CL50
OLDOWNER DS    CL3
OLDDESC  DS    CL27
TRNREC   DSECT
TRNVOL   DS    CL6
TRNDSN   DS    CL44
         ORG   TRNREC
TRNKEY   DS    CL50
TRNOWNER DS    CL3
TRNDESC  DS    CL27
         END   GU037000
