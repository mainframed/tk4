*          DATA SET GU011860   AT LEVEL 006 AS OF 07/13/78
*          DATA SET GU011860   AT LEVEL 005 AS OF 12/09/75
         TITLE 'GU011860 - CREATE PDS FILE FROM SEQ. FILE'
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/24/75.
*              REMARKS.  CREATE PDS FROM QSAM FILE.
*              FILES.
*                   SYSUT1 - QSAM INPUT WITH LRECL=80 AND
*                            MEMBER IN POSITIONS 73-80.
*
*                   SYSUT2 - PDS OUTPUT. MEMBERS ARE SEQUENCED
*                            IN 73-80 IN INCREMENTS OF 10.
*
*
*              ERRORS.
*                     101 - STOW ERROR
*
*              REGS.
*                    R5 - BUFFER
*                    R6 - RECORD
*                    R7 - END BUFFER+1
*                    R8 - BUFFER LENGTH
*
*
         TITLE 'MAINLINE'
         PRINT NOGEN
GU011860 SUBENTRY
         OPEN  (SYSUT1,,SYSUT2,(OUTPUT))
         LH    R0,DCBBLKSI-IHADCB+SYSUT2
         LR    R7,R0
         LR    R8,R0               R8=LENGTH
         GETMAIN R,LV=(0)
         LA    R5,0(R1)            R5=START
         AR    R7,R5               R7=END+1
         PM    GET
         DO    WHILE,(CLI,EOF,TRUE,NE)
               IF   (CLC,MEMBER,CMEMBER,NE)
                    PM   CLOSEM
                    PM   OPENM
               FI
               PM   PUT
               PM   GET
         OD
         PM    CLOSEM
         CLOSE (SYSUT1,,SYSUT2)
         SUBEXIT
SYSUT1   SDCB  R=FB,L=80,M=GM,E=GETEOF
SYSUT2   DCB   DSORG=PO,MACRF=W,DDNAME=SYSUT2,RECFM=FB,LRECL=80
EOF      LOGICAL FALSE,(TRUE,FALSE)
MLIST    DS    0D
CMEMBER  DC    XL8'00'
TTRZ     DC    XL4'00'
         TITLE 'GET RECORD'
GET      PENTRY
         GET   SYSUT1,RECORD
         PEXIT
GETEOF   MVI   EOF,TRUE
         PEXIT
RECORD   DS    0CL80
         DC    72C' '
MEMBER   DC    XL8'00'
         TITLE 'CLOSEM - COMPLETE OUTPUT OF LAST MEMBER'
CLOSEM   PENTRY
         IF    (CLC,CMEMBER,=8X'00',EQ)      NO MEMBER
               PEXIT
         FI
         LR    R2,R6
         IF    (SR,R2,R5,NZ)
               PM   WRITE     LAST BLOCK IF ANY
         FI
         STOW  SYSUT2,MLIST,R
         IF    (LTR,R15,R15,NZ),AND,(CH,R15,=H'8',NE)
               ABEND 101,DUMP
         FI
         PEXIT
         TITLE 'OPENM - PREPARE TO OUTPUT MEMBER'
OPENM    PENTRY
         MVC   CMEMBER,MEMBER
         LR    R6,R5
         MVI   FIRSTBLK,TRUE       SET SWITCH FOR NOTE
         ZAP   PSEQ,=P'0'
         PEXIT
FIRSTBLK LOGICAL FALSE,(TRUE,FALSE)
PSEQ     DC    PL4'0'
PINC     DC    PL2'10'
         TITLE 'PUT LOGICAL RECORD'
PUT      PENTRY
         AP    PSEQ,PINC
         EDIT  TO=MEMBER,FROM=PSEQ,MASK=' 9999999'
         MVI   MEMBER,C'0'
         MVC   0(80,R6),RECORD
         AAI   R6,80
         IF    (CLR,R6,R7,NL)
               LR   R2,R8
               PM   WRITE
               LR   R6,R5
         FI
         PEXIT
         TITLE 'WRITE A BLOCK'
WRITE    PENTRY
         STH   R2,SYSUT2+DCBBLKSI-IHADCB
         WRITE DECB,SF,SYSUT2,(R5),'S'
         CHECK DECB
         IF    (CLI,FIRSTBLK,TRUE,EQ)
               MVI  FIRSTBLK,FALSE
               NOTE SYSUT2
               ST   R1,TTRZ
         FI
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
         TITLE 'DSECTS'
         DCBD  DSORG=(PS,PO)
         END
