*          DATA SET GU687000   AT LEVEL 002 AS OF 08/29/75
         TITLE 'GU687000 - DYNAMIC UCB DEVICE TYPE ZAP UTILITY'
*              AUTHOR.   DON HIGGINS.
*              DATE.     08/29/75.
*              REMARKS.  ZAP UCB DEVICE TYPE FIELDS DYNAMICALLY BY
*                        UCB NAME TO PROTECT TCAM FROM UCB CHANGES
*                        DUE TO SYSGENS, I/O GENS, PTFS, APARS, ETC.
*              FILES.
*
*                   SYSIN - CONTROL CARDS
*                             1-3  UCB ADDRESS (02B, 071, ETC.)
*                             5-12 DEVICE TYPE CODE (51114011, ETC)
*
*                   SYSOUT - LISTING OF ZAPS AND ANY ERRORS
         TITLE 'MAIN'
         PRINT NOGEN
GU687000 SUBENTRY
         PM    GET
         DO    WHILE,(CLI,EOF,TRUE,NE)
               PM   LIST
               PM   ZAPUCB
               PM   GET
         OD
         IF    (CLI,ERROR,TRUE,EQ)
               SUBEXIT RC=16
         FI
         SUBEXIT
         TITLE 'GET'
GET      PENTRY
         ACCEPT CARD
         IF    (LTR,R15,R15,NZ)
               MVI  EOF,TRUE
         FI
         PEXIT
         TITLE 'LIST'
LIST     PENTRY
         DISPLAY LINE
         PEXIT
         TITLE 'ZAP'
ZAPUCB   PENTRY
         IF    (TRT,EUCB,EHEX,NE),OR,(TRT,EDEVTYP,EHEX,NE)
               MVI  ERROR,TRUE
               MVC  CARD,=CL80'INVALID UCB (1-3) OR DEVICE TYPE (4-12)'
               PM   LIST
               PEXIT
         FI
         L     R2,16
         L     R2,CVTILK2(R2)
         DO    WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    LH   R3,0(R2)
                    IF   (CLC,UCBNAME(3,R3),EUCB,EQ)
                         PM   SUPERZAP
                         PEXIT
                    FI
               FI
               AAI  R2,2
         OD
         MVI   ERROR,TRUE
         MVC   CARD,=CL80'UCB NOT FOUND'
         PM    LIST
         PEXIT
         TITLE 'SUPERZAP'
SUPERZAP PENTRY
         TR    EDEVTYP,THEX
         PACK  BDEVTYP(5),EDEVTYP(9)
         IF    (CLC,UCBTYP(4,R3),BDEVTYP,EQ)
               MVC  CARD,=CL80'ZAP ALREADY APPLIED'
               PM   LIST
               PEXIT
         FI
         MODESET KEY=ZERO
         MVC   UCBTYP(4,R3),BDEVTYP
         MODESET KEY=NZERO
         MVC   CARD,=CL80'ZAP SUCCESSFUL'
         PM    LIST
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
EOF      LOGICAL FALSE,(TRUE,FALSE)
ERROR    LOGICAL FALSE,(TRUE,FALSE)
LINE     DS    0CL133
         DC    C'0'
CARD     DS    0CL80
EUCB     DC    CL3' ',C' '
EDEVTYP  DC    CL8' '
         FILL  LINE
EHEX     TRTTAB NUMERIC
         ORG   EHEX+C'A'
         DC    6X'00'
         ORG
THEX     DC    256AL1(*-THEX)
         ORG   THEX+C'A'
         DC    X'FAFBFCFDFEFF'
         ORG
BDEVTYP  DC    XL5'00'
CVTILK2  EQU   X'28' FE 88
UCBNAME  EQU   13   FE 125
UCBTYP   EQU   16   FE 125
         END
