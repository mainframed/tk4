*          DATA SET TP408000   AT LEVEL 002 AS OF 04/28/75
         TITLE 'TP408000 - TAPE $DT DISPLAY PROCESSOR'
*              AUTHOR.   DON HIGGINS.
*              DATE.     04/25/75.
*              REMARKS.  DISPLAY ALL ONLINE TAPE DRIVE STATUS INFO.
         PRINT NOGEN
TAPE     SUBENTRY
         LM    R3,R5,0(R1)
         MVC   TERM,0(R3)
         PM    OPENUCB
         PM    GETUCB
         DO    WHILE,(LTR,R1,R1,P)
               IF   (CLI,UCBTYP+2(R1),X'80',EQ),  TAPE                 X
               AND,(TM,SRTESTAT(R1),X'88',O)      ONLINE+ALLOCATED
                    PM   LISTTAPE
               FI
               PM   GETUCB
         OD
         SUBEXIT
         TITLE 'LIST TAPE INFO FOR CURRENT UCB'
LISTTAPE PENTRY
         MVC   DUCB,UCBNAME(R1)
         MVC   DVOL,SRTEVOLI(R1)
         IF    (TM,UCBFL1(R1),X'40',O)
               MVC  DMSG,=CL25'INTERVENTION REQUIRED'
         ELSE
               MVC  DMSG,=CL25' '
         FI
         TPPUTMSG TERM,TAPEMSG
         PEXIT
         TITLE 'UCB SEQUENTIAL ACCESS ROUTINES'
OPENUCB  PENTRY
         L     R1,16
         L     R1,CVTILK2(R1)
         SH    R1,=H'2'
         ST    R1,AUCB
         PEXIT
GETUCB   PENTRY
         L     R15,AUCB
         AAI   R15,2
         LH    R1,0(R15)
         DO    WHILE,(LTR,R1,R1,NM)
               IF   (LTR,R1,R1,P)
                    ST   R15,AUCB
                    PEXIT
               FI
               AAI  R15,2
               LH   R1,0(R15)
         OD
         ZR    R1
         PEXIT
CVTILK2  EQU   X'28' FE PG 88
AUCB     DC    A(0)
         TITLE 'COMMON DATA'
         EQUAL
TERM     DC    CL8' '
UCBTYP   EQU   16 FE 125
SRTESTAT EQU   3  FE 125
UCBNAME  EQU   13 FE 125
UCBFL1   EQU   6  FE 125
SRTEVOLI EQU   28 FE 131
TAPEMSG  DS    0CL50
DUCB     DC    CL3' ',C' '
DVOL     DC    CL6' ',C' '
DMSG     DC    CL25' '
         FILL  TAPEMSG
         END
