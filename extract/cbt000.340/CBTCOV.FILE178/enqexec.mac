*          DATA SET GU860000   AT LEVEL 002 AS OF 05/06/75
         TITLE 'GU860000 - ENQUE AND EXEC PROGRAM'
*              AUTHOR.   DON HIGGINS.
*              DATE.     04/08/75.
*              REMARKS.  ENQUE WITH USER SPECIFIED QNAME AND RNAME.
*                        THEN EXECUTE USER SPECIFIED PROGRAM VIA LINK.
*              PARM.
*
*                        1-8       QNAME
*                        9-16      RNAME
*                        17-24     PGMNAME
*              EXAMPLE.
*
*               EXEC PGM=GU860000,PARM='CAMASTERUPDATE CA000500'
*
*                        THE ABOVE EXAMPLE ENQUES ON THE SAME
*                        QNAME AND RNAME AS THE FIP SUBROUTINES AND
*                        THEN EXECUTES THE CA DUMP PROGRAM CA000500.
*                        THIS WOULD PREVENT THE DUMP FROM RUNNING
*                        WHILE ANY OTHER PROGRAM WAS UPDATING.
         TITLE 'MAIN'
GU860000 SUBENTRY
         L     R2,0(R1)
         IF    (CLC,0(2,R2),=H'24',NE)
               WTO  'INVALID QNAME, RNAME, OR PGMNAME IN PARM'
               SCALL ABTERM,(TERM)
         FI
         MVC   QNAME,2(R2)
         MVC   RNAME,10(R2)
         MVC   PGMNAME,18(R2)
         ENQ   (QNAME,RNAME,E,,SYSTEM)
         LOAD  EPLOC=PGMNAME
         LR    R15,R0
         LA    R1,=A(NULLPARM)
         BALR  R14,R15
         SUBEXIT RC=(R15)
         TITLE 'COMMON DATA'
         EQUAL
TERM     DC    F'3001'
QNAME    DC    CL8' '
RNAME    DC    CL8' '
PGMNAME  DC    CL8' '
NULLPARM DC    H'0'
         END
