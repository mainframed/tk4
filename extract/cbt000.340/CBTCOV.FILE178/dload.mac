*          DATA SET SR311000   AT LEVEL 001 AS OF 04/21/78
         TITLE 'SR311000 DYNAMIC LOAD'
*              AUTHOR.   DON HIGGINS.
*              DATE.     04/21/78.
*              REMARKS.  THIS MODULE LOADS A MODULE ONLY.
*                        THE LOADING SEARCH IS AS FOLLOWS.
*                             1.   IF THE JOB PACK AREA CONTAINS
*                                  THE MODULE, ITS ADDRESS IS RETURNED.
*
*                             2.   IF THE DDNAME (MODULE NAME) IS FOUND
*                                  IT IS LOADED FROM IT.
*
*                             3.   IF THE DDNAME (DCALLLIB) IS FOUND
*                                  IT IS LOADED FROM IT.
*                             4    A SIMPLE LOAD IS TRIED.
*              CALL. CALL DLOAD('XXXXXXXX')
*                      ADDRESS RETURNED IN R0
*
         TITLE 'MAIN'
DLOAD    SUBENTRY
         L     R2,0(R1)
         FINDCDE EPLOC=0(R2)
         IF    (LTR,R1,R1,NZ)
               L    R3,CDENTPT(R1)
         ELSE
               FINDDD 0(R2)
               IF   (LTR,R1,R1,NZ)
                    MVC  MODLIB+DCBDDNAM-IHADCB(8),0(R2)
                    OPEN (MODLIB,(INPUT))
                    LOAD EPLOC=(R2),DCB=MODLIB
                    LR   R3,R0
                    CLOSE (MODLIB)
               ELSE
                    FINDDD =CL8'DCALLLIB'
                    IF   (LTR,R1,R1,NZ)
                         IF   (TM,DCALLLIB+DCBOFLGS-IHADCB,X'40',Z)
                              OPEN (DCALLLIB,(INPUT))
                         FI
                         LOAD EPLOC=(R2),DCB=DCALLLIB
                         LR   R3,R0
                    ELSE
                         LOAD EPLOC=(R2)
                         LR   R3,R0
                    FI
               FI
         FI
         LR    R0,R3
         SUBEXIT
         TITLE 'COMMON DATA'
         EQUAL
CDENTPT  EQU   16   FE 280
MODLIB   DCB   DSORG=PO,MACRF=R,DDNAME=X
DCALLLIB DCB   DSORG=PO,MACRF=R,DDNAME=DCALLLIB
         TITLE 'DSECTS'
         DCBD  DSORG=PO
         END
