C          DATA SET GU011600   AT LEVEL 001 AS OF 10/04/72
      SUBROUTINE PLOTXY(X,Y,NUM,LIST,LINES,INDEX)
C
C     XY PLOT PROGRAM BY DON HIGGINS
C
C     DESCRIPTION OF ARGUMENTS
C        X NAME OF ARRAY CONTAINING X COORDINATES
C        Y NAME OF ARRAY CONTAINING Y COORDINATES
C      NUM - NUMBER OF POINTS X,Y TO BE PLOTTED
C     LIST - IF LIST IS NOT ZERO, A LIST OF THE POINTS TO BE PLOTTED
C            WILL BE PRINTED.
C     LINES - NUMBER OF LINES OVER WHICH Y VALUES ARE TO BE SCALED.
C     INDEX - IF INDEX IS ZERO, THE PLOT WILL START 2 LINES DOWN
C             FROM THE PRESENT LOCATION ON THE PAGE.
C             OTHERWISE THE PLOT WILL START ON A NEW PAGE TITLED
C             CHART INDEX.
C
      DIMENSION X(1),Y(1),IY(100),IPLOT(100),SCALE(11)
      DATA NPRT/6/
      DATA NBLK/' '/,NSYM/'*'/
    1 FORMAT('1',67X,'XY PLOT',
     * /59X,'POINT COORDINATE LISTING',
     * //60X,'X',20X,'Y',
     * //(40X,I10,1PE20.8,E20.8))
    4 FORMAT('1',60X,'CHART',I5)
    5 FORMAT(//26X,102('*'))
    6 FORMAT(3X,1PE20.8,3X,'*',100A1,'*')
    7 FORMAT(26X,102('*'),
     * /27X,'*',8X,'*',9(9X,'*'),
     * /22X,1PE10.3,10E10.3)
    8 FORMAT(//27X,'INPUT ERROR',5X,'YMAX',1PE15.8,10X,'YMIN',E15.8,
     * 10X,'POINTS',I5)
C
C     TEST FOR LIST IF DESIRED
C
      IF(LIST.NE.0)WRITE(NPRT,1)(I,X(I),Y(I),I=1,NUM)
C
C     FIND HIGH AND LOW LIMITS OF Y
C
      SMLY=Y(1)
      BIGY=Y(1)
      DO 10 I=1,NUM
      IF(Y(I).GT.BIGY)BIGY=Y(I)
   10 IF(Y(I).LT.SMLY)SMLY=Y(I)
C
C     PRINT ERROR IF Y IS CONSTANT OR NUMBER OF POINTS IS ZERO
C
      IF(BIGY.EQ.SMLY)GO TO 90
C
C     COMPUTE X AND Y SCALE FACTORS
C
      SY=FLOAT(LINES-1)/(BIGY-SMLY)
      SX=99./FLOAT(NUM-1)
C
C     CLEAR ARRAY WHICH WILL CONTAIN SCALED Y VALUES
C
      DO 20 I=1,100
   20 IY(I)=0
C
C     INITIALIZE POINT-P, PLOT COLUMN-NCOL, AND DATA INDEX-NY
C
      P=0.
      NCOL=1
      NY=1
C
C     INCREMENT POINT COUNTER
C
   30 P=P+1.
C
C     COMPUTE SCALED Y VALUE
C
      IY(NCOL)=(BIGY-Y(NY))*SY+1.5
      IF(NUM.GE.100)GO TO 40
C
C     COMPUTE THE NEXT COLUMN TO CONTAIN A PLOT SYMBOL
C
      NCOL=SX*P
      GO TO 50
C
C     COMPUTE THE INDEX OF THE NEXT DATA VALUE TO BE SCALED
C
   40 NY=P/SX
   50 NCOL=NCOL+1
      NY=NY+1
      IF(NCOL.LE.100)GO TO 30
C
C     TEST IF INDEXING IS DESIRED
C
      IF(INDEX.NE.0)WRITE(NPRT,4)INDEX
      WRITE(NPRT,5)
C
C     LOAD PLOT SYMBOLS
C
      DO 70 L=1,LINES
      DO 60 NCOL=1,100
      IPLOT(NCOL)=NBLK
      IF(IY(NCOL).EQ.L)IPLOT(NCOL)=NSYM
   60 CONTINUE
C
C     COMPUTE VALUE OF Y FOR THE CURRENT LINE OF THE PLOT
C
      VALUE=BIGY-FLOAT(L-1)/SY
C
C     PRINT VALUE AND PLOT ARRAY
      WRITE(NPRT,6)VALUE,IPLOT
   70 CONTINUE
C
C     COMPUTE SCALE FACTOR FOR SCALED VALUES OF X
C
      SX=(X(NUM)-X(1))/10.
C
C     LOAD SCALED X ARRAY
C
      DO 80 I=1,11
      SCALE(I)=X(1)+SX*FLOAT(I-1)
   80 CONTINUE
      WRITE(NPRT,7)SCALE
      RETURN
   90 WRITE(NPRT,8)BIGY,SMLY,NUM
      RETURN
      END
