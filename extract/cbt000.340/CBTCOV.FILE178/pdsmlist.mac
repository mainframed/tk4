*          DATA SET GU011800   AT LEVEL 006 AS OF 07/13/78
*          DATA SET GU011800   AT LEVEL 005 AS OF 12/01/75
GU011800 SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/13/73.
*              REMARKS.  LIST PDS MEMBERS.
*              FILES.
*                  PDSLIB    LIBRARY TO BE LISTED.
*                  PDSMEM    LIBRARY TO BE LISTED.
*                  SYSPRINT  LIST ALL OR SELECTED MEMBERS
*                  PDSCTL    SELECTION CONTROL FILE
*                              COL. 01-08 LOW KEY
*                              COL. 09-16 HIGH KEY OR BLANK
*                  PDSOUT    LIST SELECTION CONTROL RECORDS
*
*              MAINTENANCE.
*
*                  07/13/78 DSH - USE IOB CSW COUNT TO DETERMINE
*                                 END OF BLOCK INSTEAD OF USING
*                                 TEST FOR INITIAL HEX ZEROS.
*
         OPEN  (SYSPRINT,(OUTPUT),PDSMEM)
         LH    R6,PDSMEM+62        R6=BLK SIZE
         LR    R0,R6
         GETMAIN R,LV=(0)
         LR    R5,R1               R5=BLOCK AREA
         CALL  GETPDSDD,(JFCB,DE)
         LT    R2,JFCB             R2=A(JFCB)
         BZ    NOPDSLIB
         MVC   PDSNAME,0(R2)
         MVC   PDSVOL,118(R2)
         DTIME DATETIME
L1       TAG
         CALL  GETPDSDD,(JFCB,DE)
         LT    R3,DE
         BZ    EOF
         MVC   MEMBER,0(R3)
         ZAP   LINE,=P'0'
         ZAP   REC,=P'0'
         MVC   TTRK(3),8(R3)
         FIND  PDSMEM,TTRK,C
L2       TAG
         READ  DECB,SF,PDSMEM,(R5),'S'
         CHECK DECB
         LR    R8,R5               R8=A(BLOCK)
         LR    R9,R6               R9=MAX BLOCK SIZE
         L     R1,PDSMEM+DCBIOBA-IHADCB   R1=A(IOB)
         SH    R9,22(R1)                  R9=ACTUAL BLOCK SIZE READ
         LA    R9,0(R8,R9)                R9=A(END OF BLOCK+1)
L3       TAG
         MVC   RECORD,0(R8)
         SP    LINE,=P'1'
         BP    PRINT
         ZAP   LINE,=P'60'
         AP    PAGE,=P'1'
         EDIT  TO=DPAGE,FROM=PAGE,MASK=' ZZZZZ'
         PUT   SYSPRINT,HEAD1
         PUT   SYSPRINT,HEAD2
         PUT   SYSPRINT,HEAD3
PRINT    TAG
         AP    REC,=P'1'
         EDIT  TO=DREC,FROM=REC,MASK=' ZZZZZ'
         PUT   SYSPRINT,DLINE
         LA    R8,80(R8)
         CLR   R8,R9
         BL    L3
         B     L2
NOPDSLIB TAG
         WTO   'PDSLIB DD MISSING'
EOF      TAG
         CLOSE (SYSPRINT,,PDSMEM)
         SUBEXIT
         TITLE 'COMMON DATA'
         EQUAL
SYSPRINT SDCB  M=PM,R=FBA,L=133
PDSMEM   DCB   DSORG=PO,MACRF=R,DDNAME=PDSMEM,EODAD=L1,                X
               RECFM=FB,LRECL=80
JFCB     DC    A(0)
DE       DC    A(0)
LINE     DC    PL2'0'
PAGE     DC    PL3'0'
REC      DC    PL3'0'
TTRK     DC    XL4'00'
HEAD1    DS    0CL133
         DC    C'1'
MEMBER   DC    CL8' ',10C' '
PDSNAME  DC    CL44' ',2C' '
PDSVOL   DC    CL6' ',5C' '
DATETIME DC    CL22' ',C'  PAGE'
DPAGE    DC    CL6' '
         FILL  HEAD1
HEAD2    DS    0CL133
         DC    CL5'0',C'RECORD',C'  '
         DC    C'1...5...10...15...20...25...30...35...40'
         DC    C'...45...50...55...60...65...70...75...80'
         FILL  HEAD2
HEAD3    DC    CL133' '
DLINE    DS    0CL133
         DC    CL5' '
DREC     DC    CL6' ',C'  '
RECORD   DC    CL80' '
         FILL  DLINE
         TITLE 'DSECTS'
         DCBD  DSORG=PS
         END   GU011800
