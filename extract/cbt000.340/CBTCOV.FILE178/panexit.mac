*          DATA SET TO027000   AT LEVEL 002 AS OF 05/11/78
***********************************************************************
**********************      P A N E X I T      ************************
***********************************************************************
*                                                                     *
*  'PANEXIT' IS THE GENERALIZED INTERFACE PROGRAM USED AS THE LINK    *
*  BETWEEN ALL THE PANVALET PROGRAMS AND RESIDENT EXIT ROUTINES.      *
*                                                                     *
*  AN EXIT ROUTINE MUST EXIST FOR EACH PAN PROGRAM; THE SAME ROUTINE  *
*  MAY BE USED IF VALID ALIASES EXISTS.                               *
*                                                                     *
*  THE EXIT IS "LOADED" BY THIS INTERFACE AND STORED IN A WORK AREA   *
*  FOR ALL FUTURE CALLS.  THERE IS NO DELETE; THE OPERATING SYSTEM    *
*  WILL TAKE CARE OF THAT.                                            *
*                                                                     *
*  IF THE LOAD OPERATION FAILS THE WHOLE PAN#N PROGRAM WILL FAIL      *
*  WITH AN 806 ABEND SO IT IS NECESSARY TO PROVIDE AN EXIT FOR        *
*  EACH PAN PROGRAM THIS EXIT INTERFACE IS LINKEDITED INTO.  THE      *
*  FORMAT OF THE EXIT NAME IN "PAN$EXIT" WHERE THE $ IS REPLACED      *
*  WITH THE NUMBER OF THE PAN PROGRAM IN EXECUTION.  FOR EXAMPLE      *
*  IF PAN#2 IS OPERATING AND THE INTERFACE IS LINKEDITED INTO IT      *
*  A LOAD FOR PAN2EXIT WILL BE ISSUED; IT BETTER BE IN THE STEPLIB    *
*  OR THE LNKLST.                                                     *
*                                                                     *
*  NOTE -- THE FIRST VERSION OF THIS PROGRAM USED A BLDL TO DETERMINE *
*     IF THE EXIT WAS AVAILABLE BEFORE ISSUING THE SOMETIMES          *
*     FATAL LOAD SVC.  UNFORTUNATELY IT WAS IMPOSSIBLE TO USE         *
*     AN IDENTIFY TO ESTABLISH THE EXIT.  THEREFORE IT WAS            *
*     DECIDED TO ACCEPT THE LEAST OF THE TWO EVILS AND BLINDLY        *
*     ISSUE THE LOAD.  THE POSSIBLY MISSING EXIT WILL BE QUICKLY      *
*     IDENTIFIED DURING TESTING (HOPEFULLY).                          *
*                                                                     *
*                                                                     *
*  UPON ENTRY INTO THIS INTERFACE FROM ITS PANVALET CALLING PROGRAM,  *
*  REGISTER ONE (1) WILL CONTAIN AN ADDRESS THAT WILL POINT TO A      *
*  THREE-ADDRESS PARM FIELD.                                          *
*                                                                     *
*     THE FIRST ADDRESS OF THIS PARM FIELD WILL POINT TO THE PANVALET *
*        I/O AREA.                                                    *
*                                                                     *
*     THE SECOND ADDRESS OF THIS PARM FIELD WILL POINT TO A 3-BYTE    *
*        CODE.                                                        *
*                                                                     *
*        THE FIRST BYTE OF THIS CODE WILL BE THE NUMBER OF THE        *
*           PANVALET CALLING PROGRAM.                                 *
*        THE SECOND BYTE OF THIS CODE WILL ALLOW FOR COMMUNICATION    *
*           BETWEEN THE PANVALET CALLING PROGRAM AND ITS EXITS AS TO  *
*           WHAT IS HAPPENING; (IE), ACCESS OF EXIT PRIOR TO OR AFTER *
*           READ OR WRITE, END OF INPUT STREAM, END OF JOB, OR ALLOW  *
*           INSERTION OR DELETION OF AN INPUT OR OUTPUT DATA RECORD.  *
*        THE THIRD BYTE OF THIS CODE IS EITHER A 'Y' OR 'N',          *
*           DEPENDING ON WHETHER OR NOT THE PANVALET CALLING PROGRAM  *
*           IS TO RETURN CONTROL TO ITS CALLED EXIT IMMEDIATELY.      *
*                                                                     *
*     THE THIRD ADDRESS OF THIS PARM FIELD WILL POINT TO AN 80-BYTE   *
*        AREA IDENTICAL TO THE 0-UP PANVALET DIRECTORY.               *
*                                                                     *
*                                                                     *
*  LAST MODIFIED - FRED D BRISARD 2/4/77                              *
*  LAST MODIFIED - RICHARD PADGETT - 2/25/76                          *
*                                                                     *
*  REGISTER USAGE :                                                   *
*  'R0' -------- USED IN THE LOAD MACRO TO STORE THE SECURITY EXIT'S  *
*                ENTRY ADDRESS.                                       *
*  '$PRMADDR' -- USED TO STORE REGISTER ONE (1).                      *
*  '$PRM2 ------ USED TO STORE THE SECOND PARM ADDRESS.               *
*  'R14' ------- RETURN REGISTER.                                     *
*  'R15' ------- LINK REGISTER TO SECURITY EXITS.                     *
*                                                                     *
***********************************************************************
         SPACE 3
PANEXIT  SUBENTRY
         EJECT
*******************   E Q U A T E S   *******************
         SPACE 1
R0       EQU   0
R1       EQU   1
$PRMADDR EQU   2
$PRM2    EQU   3
R14      EQU   14
R15      EQU   15
         EJECT
***********************************************************************
*                                                                     *
*  THIS AREA OF THE PROGRAM WILL DETERMINE WHETHER OR NOT AN EXIT     *
*  ADDRESS HAS BEEN STORED.  IF AN ADDRESS HAS BEEN STORED, A BRANCH  *
*  TO THAT EXIT IS TAKEN IMMEDIATELY.  IF AN ADDRESS HAS NOT BEEN     *
*  STORED, A LOAD WILL THEN BE ISSUED TO BRING THE MODULE INTO        *
*  STORAGE IF IT IS NOT ALREADY THERE.                                *
*                                                                     *
***********************************************************************
         SPACE 1
         L     R15,PANLOAD         LOAD EXIT ADDRESS
         LTR   R15,R15             TEST FOR EXISTENCE
         BNZ   PANBRNCH            TO BALR
         SPACE 1
         LR    $PRMADDR,R1         STORE REGISTER 1
         L     $PRM2,4(,$PRMADDR)  LOAD SECOND PARM
         SPACE 1
         MVC   PANMODL+3(1),1($PRM2)    MOVE PAN PROG # TO EXIT TYPE
         SPACE 2
***********************************************************************
*                                                                     *
*  THE LOAD MACRO WILL STORE THE EXIT'S ADDRESS IN R0.                *
*  THEN R0 WILL BE STORED IN "PANADDR".                               *
*                                                                     *
***********************************************************************
         SPACE 1
PANLDEX  LOAD  EPLOC=PANMODL       LOAD EXIT INTO PROTECTED STORAGE
         SPACE 2
         ST    R0,PANLOAD          SAVE ADDRESS
         LR    R1,$PRMADDR         RESTORE PARM ADDRESSES
         LR    R15,R0              LOAD CALLING REGISTER
         XC    PANWORK,PANWORK     CLEAR WORK AREA FOR CALLED EXIT
         B     PANBRNCH            GO CALL THE ROUTINE
         EJECT
***********************************************************************
*                                                                     *
*  RETURN TO PANVALET CALLING PROGRAM AT THIS POINT.                  *
*                                                                     *
***********************************************************************
         SPACE 1
EXIT     SUBEXIT                   EXIT MODULE
         SPACE 2
         DS    0F,H                ALIGNMENT
         SPACE 3
***********************************************************************
*                                                                     *
*  BRANCH TO THE APPROPRIATE EXIT ROUTINE.                            *
*                                                                     *
***********************************************************************
         SPACE 1
PANBRNCH BALR  R14,R15             TO EXIT MODULE
         B     EXIT
         EJECT
***********************************************************************
*                                                                     *
*  WORK AREAS.                                                        *
*                                                                     *
*  THE WORK AREA LABELED BY PANWORK PROVIDES FOR A UNIQUE EXIT        *
*  ROUTINE LINKAGE.  R14 WILL POINT TO THE RETURN ADDRESS AND         *
*  R14+8 WILL POINT TO A 100 BYTE WORK AREA ALIGNED ON A FULLWORD     *
*  FOR USE BY THE EXIT.  THIS ALLOWS FOR REENTRANT EXITS EVEN         *
*  THOUGH PAN ITSELF IS NOT.                                          *
*                                                                     *
***********************************************************************
         SPACE 1
PANLOAD  DC    A(0)                STORAGE FOR EXIT ADDRESS
         SPACE 1
PANWORK  DC    XL100'00'           WORK AREA
         ORG   PANWORK             REUSE THE WORK AREA
         SPACE 1
PANMODL  DC    CL8'PAN1EXIT'       EXIT TYPE (FROM PAN PROG #)
         END
