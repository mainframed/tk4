*          DATA SET GU029100   AT LEVEL 006 AS OF 04/09/74
         TITLE 'GU029100 - MAINLINE'
         PRINT NOGEN
GU029100 SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     04/08/74.
*              REMARKS.  CHECK TO SEE IF IT IS TIME TO REORGANIZE
*                        AN ISAM OR PDS FILE.  IF ISAM CHECK PARM
*                        AGAINST OVERFLOW RECORDS COUNT.  IF PDS
*                        CHECK PARM AGAINST RELITAVE EOF TRACK COUNT.
*                        IF IT IS REORG TIME, RETURN CODE IS SET TO 4.
*
         L     R2,0(R1)            R2=A(PARM)
         OPEN  (SYSPRINT,(OUTPUT))
         PERFORM R100PARM,R190EXIT
         CLI   FEOJ,TRUE
         BE    EOJ
         PERFORM R200JFCB,R290EXIT
         CLI   FEOJ,TRUE
         BE    EOJ
         PERFORM R300DSCB,R390EXIT
         CLI   FEOJ,TRUE
         BE    EOJ
         PERFORM R400LIST,R490EXIT
         PERFORM R500CHK,R590EXIT
         CLOSE (SYSPRINT)
         IF    (CLI,REORG,TRUE,EQ)
               SUBEXIT RC=4
         ELSE  CONTINUE
EOJ      TAG
         SUBEXIT
TRUE     EQU   0
FALSE    EQU   1
FEOJ     DC    AL1(FALSE)
REORG    DC    AL1(FALSE)
         TITLE 'R100 - EDIT AND PACK PARM COUNT'
R100PARM PENTRY
         LH    R3,0(R2)
         IF    (CH,R3,=H'16',H)
               LA   R2,ERRMSG1
               PERFORM R600RPT,R690EXIT
               MVI  FEOJ,TRUE
               B    R190EXIT
         ELSE  CONTINUE
         IF    (LTR,R3,R3,P)
         THEN  BEGIN
               BCTR R3,0
               IF   (EX,R3,EDIT,NE)
                    LA   R2,ERRMSG2
                    PERFORM R600RPT,R690EXIT
                    MVI  FEOJ,TRUE
                    B    R190EXIT
               ELSE CONTINUE
               EX   R3,PACK
         THEN  END
         ELSE  BEGIN
               ZAP  PARM,=P'0'
         ELSE  END
R190EXIT PEXIT
EDIT     TRT   2(0,R2),EDITTAB
EDITTAB  DC    240X'FF',10X'00',6X'FF'
PACK     PACK  PARM,2(0,R2)
PARM     DC    D'0'
ERRMSG1  DC    CL133' INVALID PARM - TOO LONG'
ERRMSG2  DC    CL133' INVALID PARM - NOT NUMERIC'
         TITLE 'R200 - READ JFCB FOR SYSUT1'
R200JFCB PENTRY
         RDJFCB SYSUT1
R290EXIT PEXIT
SYSUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=SYSUT1,                        X
               EXLST=EXLST
EXLST    DC    0F'0',X'87',AL3(JFCB)
JFCB     DC    0F'0',XL176'00'
DSNAME   EQU   JFCB
VOLUME   EQU   JFCB+118
         TITLE 'R300 - OBTAIN DSCB 1 AND 2'
R300DSCB PENTRY
         OBTAIN CAMLST1
         IF    (LTR,R15,R15,NZ)
               LA   R2,ERRMSG3
               PERFORM R600RPT,R690EXIT
               MVI  FEOJ,TRUE
               B    R390EXIT
         ELSE  CONTINUE
         TB    DS1DSORG,(0)
         BZ    R390EXIT
         OBTAIN CAMLST2
         IF    (LTR,R15,R15,NZ)
               LA   R2,ERRMSG4
               PERFORM R600RPT,R690EXIT
               MVI  FEOJ,TRUE
               B    R390EXIT
         ELSE  CONTINUE
R390EXIT PEXIT
CAMLST1  CAMLST SEARCH,DSNAME,VOLUME,DSCB1
CAMLST2  CAMLST SEEK,DS1PTRDS,VOLUME,DSCB2
DSCB1    DC    0F'0',XL148'00'
DS1DSORG EQU   DSCB1-44+82
DS1LSTAR EQU   DSCB1-44+98
DS1PTRDS EQU   DSCB1-44+135
DSCB2    DC    0F'0',XL148'00'
DS2OVRCT EQU   DSCB2+128
ERRMSG3  DC    CL133' ERROR OBTAINING DSCB1'
ERRMSG4  DC    CL133' ERROR OBTAINING DSCB2'
         TITLE 'R400 - LIST INFO'
R400LIST PENTRY
         LA    R2,DLINE
         MVC   DL,=CL8'DSNAME'
         MVC   DD(44),DSNAME
         PERFORM R600RPT,R690EXIT
         MVC   DLINE+1(132),DLINE
         MVC   DL,=CL8'VOLUME'
         MVC   DD(6),VOLUME
         PERFORM R600RPT,R690EXIT
         MVC   DLINE+1(132),DLINE
         MVC   DL,=CL8'DSORG'
         MVC   DD(2),=C'**'
         IF    (TM,DS1DSORG,X'80',O)
               MVC  DD(2),=C'IS'
               PERFORM R600RPT,R690EXIT
               MVC  DLINE+1(132),DLINE
               MVC  DL,=CL8'CUR-REC'
               ZR   R0
               ICM  R0,3,DS2OVRCT
               CVD  R0,PWORK
               EDIT TO=DD,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'
               PERFORM R600RPT,R690EXIT
               MVC  DLINE+1(132),DLINE
         ELSE  CONTINUE
         IF    (TM,DS1DSORG,X'02',O)
               MVC  DD(2),=C'PO'
               PERFORM R600RPT,R690EXIT
               MVC  DLINE+1(132),DLINE
               MVC  DL,=CL8'CUR-TRK'
               ZR   R0
               ICM  R0,3,DS1LSTAR
               CVD  R0,PWORK
               EDIT TO=DD,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'
               PERFORM R600RPT,R690EXIT
               MVC  DLINE+1(132),DLINE
         ELSE  CONTINUE
         MVC   DL,=CL8'MAX-CNT'
         EDIT  TO=DD,FROM=PARM+4,MASK=' Z,ZZZ,Z99'
         PERFORM R600RPT,R690EXIT
         MVC   DLINE+1(132),DLINE
R490EXIT PEXIT
DLINE    DS    0CL133
         DC    C' '
DL       DC    CL8' ',CL5' '
DD       DC    CL44' '
         FILL  DLINE
PWORK    DC    0D'0',PL8'0'
         TITLE 'R500 - CHECK IF TIME FOR REORG'
R500CHK  PENTRY
         CP    PARM,PWORK
         BH    R590EXIT
         MVI   REORG,TRUE
R590EXIT PEXIT
         TITLE 'R600 - PRINT REPORT'
R600RPT  PENTRY
         LR    R0,R2
         PUT   SYSPRINT
R690EXIT PEXIT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FA,BLKSIZE=133
         TITLE 'COMMON DATA'
         EQUAL
         END   GU029100
