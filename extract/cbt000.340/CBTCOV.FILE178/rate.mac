*          DATA SET TP412000   AT LEVEL 008 AS OF 07/28/75
         TITLE 'TP412000 - DISPLAY I/O RATES AND CPU USAGE'
*              AUTHOR.   DON HIGGINS.
*              DATE.     07/21/75.
*              REMARKS.  DISPLAY I/O RATES AND CPU USAGE BASED ON
*                        THE PERIOD SINCE LAST CALL.
*
*              REFERENCES.
*                   1.   IEASMFEX MACRO WHICH ASSEMBLES IEATLEXT CSECT
*                        WHICH CONTAINS IEAQWAIT AND SYSWSAVE.
*                   2.   IEAVTI00 FISCHE WHICH UPDATES SMCA FROM
*                        SYSWSAVE AND PVT.  SYSWSAVE AND PVT DATA
*                        IS RESET EVERY TIME THE SMF 10 MIN TQE
*                        EXPIRES.  IT THEN POSTS SMF TO DUMP INFO.
*                        THE SMCA IS RESET AFTER EACH DUMP IS COMPLETE.
         TITLE 'MACROS'
         MACRO
         CRATE &N
.*                  CALCULATE EXCP/MIN FOR CALCJOB ROUTINE
         L     R1,R&N
         S     R1,T&N
         MH    R1,=H'6000'
         ZR    R0
         D     R0,PERIOD
         CVD   R1,PWORK
         EDIT  TO=D&N,FROM=PWORK+5,MASK=' ZZZ99'
         MEND
         TITLE 'MAINLINE'
         PRINT NOGEN
TP412000 SUBENTRY
         LM    R3,R5,0(R1)
         MVC   TERM,0(R3)
         PM    SYSRATES
         PM    JOBRATES
         MVC   OLDBTIME,NEWBTIME
         SUBEXIT
         TITLE 'SYSRATES - DISPLAY CPU AND PAGE RATES'
SYSRATES PENTRY
         L     R2,16
         L     R3,CVTSMCA(R2)
         L     R4,CVTSYSWT(R2)
         L     R5,CVTPVTP(R2)
         DO    UNTIL,(CLR,R6,R2,EQ) COLLECT DATA UNINTERRUPTED
               L    R6,SMCATEXP(R3)     LAST TQE TIME
               LM   R0,R1,8(R4)         ACC WAIT TIME IN MICS
               STM  R0,R1,NEWWAIT
               L    R14,PVTNPOUT(R5)
               A    R14,PVTNPIN(R5)
               A    R14,PVTSPOUT(R5)
               A    R14,PVTSPIN(R5)
               ST   R14,NEWPAGE         PAGE COUNT
               L    R2,SMCATEXP(R3)
         OD
         TIME  BIN
         ST    R0,NEWBTIME
         LM    R0,R1,NEWWAIT
         IF    (CL,R2,SMFBTIME,EQ) IS MY OLD ACC STILL GOOD
               IF   (SL,R1,OLDWAIT+4,M)
                    SI   R0,1
               FI
               SL   R0,OLDWAIT
               S    R14,OLDPAGE
               L    R15,NEWBTIME
               S    R15,OLDBTIME
         ELSE
               ST   R2,SMFBTIME
               L    R15,NEWBTIME
               SR   R15,R2
         FI
         MVC   OLDWAIT,NEWWAIT
         MVC   OLDPAGE,NEWPAGE
         SRDL  R0,12
         DR    R0,R15
         L     R0,=F'10000'
         SR    R0,R1     CPU IN % WITH 2 DECIMAL PLACES
         CVD   R0,PWORK
         EDIT  TO=DCPU,FROM=PWORK+5,MASK=' Z99.99'
         CVD   R15,PWORK
         EDIT  TO=DSEC,FROM=PWORK+5,MASK=' Z99.99'
         MH    R14,=H'100'
         LR    R1,R14
         ZR    R0
         DR    R0,R15
         CVD   R1,PWORK
         EDIT  TO=DPAGE,FROM=PWORK+5,MASK=' ZZZ99'
         TPPUTMSG TERM,SYSMSG
         PEXIT
         TITLE 'JOBRATES - DISPLAY  EXCP RATES FOR JOBS'
JOBRATES PENTRY
         NC    JOBFLAGS,JOBRESET
         L     R1,16
         MVC   NEXTTCB,CVTHEAD(R1)
         L     R0,NEWBTIME
         S     R0,OLDBTIME
         ST    R0,PERIOD
         PM    GETJOB
         DO    WHILE,(LTR,R1,R1,NZ)     R1=TCB FOR USER JOB
               PM   SCANTCT
               PM   MATCHJOB
               IF   (LTR,R1,R1,NZ)      R1=JOB TABLE ENTRY
                    PM   CALCJOB
               ELSE
                    PM   ADDJOB
               FI
               PM   GETJOB
         OD
         LA    R1,JOBFLAGS
         DO    WHILE,(CLI,0(R1),X'FF',NE)
               IF   (TM,0(R1),X'80',Z)  HAS JOB BEEN UPDATED
                    MVI  0(R1),X'00'    NO, FREE ENTRY
               FI
               AAI  R1,1
         OD
         PEXIT
         TITLE 'GETJOB - FIND NEXT USER TCB'
GETJOB   PENTRY
         L     R1,NEXTTCB
         DO    WHILE,(LTR,R1,R1,NZ)
               MVC  NEXTTCB,TCBTCB(R1)
               IF   (CLC,TCBTCT(4,R1),=4X'00',NZ),                     X
               AND,(CLC,TCBNTC(4,R1),=4X'00',Z),                       X
               AND,(CLC,TCBLTC(4,R1),=4X'00',Z)
                    L    R2,TCBTIO(R1)
                    IF   (CLC,0(7,R2),=CL7'HOSINIT',NE)
                         IF   (CLI,7(R2),C' ',NE) IS IT STD 8 CAHR NAME
                              PEXIT
                         FI
                    FI
               FI
               L    R1,NEXTTCB
         OD
         PEXIT
         TITLE 'SCANTCT - COLLECT EXCPS FOR JOB'
SCANTCT  PENTRY
         L     R2,TCBTCT(R1)  R2=TCT
         L     R3,TCBTIO(R1)  R3=TIOT
         MVC   RJOB,0(R3)
         MVC   RSTEP,8(R3)
         XC    REXCPS,REXCPS       CLEAR EXCP FIELDS
         L     R2,TCTIOTBL(R2)
         LA    R4,8(R2)
         LH    R5,0(R4)       TIOT OFFSET FROM TCT IO DD ENTRY
         DO    WHILE,(LTR,R5,R5,NZ)
               PM   TCTDD
               AAI  R4,4
               LH   R5,0(R4)
         OD
         PEXIT
         TITLE 'TCTDD - PROCESS TCT DD ENTRIES'
TCTDD    PENTRY
         LH    R5,2(R4)       TCT OFFSET
         LA    R5,0(R5,R2)    TCT IO DD ENTRY
         ZR    R6
         IC    R6,2(R5)
         DO    WHILE,(LTR,R6,R6,NZ)
               PM   TCTEXCP
               SI   R6,1
               AAI  R5,16
         OD
         PEXIT
         TITLE 'TCTEXCP - PROCESS EXCP ENTRY FOR CURRENT DD'
TCTEXCP  PENTRY
         LH    R1,0(R5)       UCB ADDRESS
         IF    (LTR,R1,R1,Z)
               PEXIT
         FI
         L     R0,4(R5)       EXCP COUNT FOR UCB
         IF    (LTR,R0,R0,Z)
               PEXIT
         FI
         IF    (CLI,UCBTYP+2(R1),X'20',EQ)   DISK
               A    R0,RDISK
               ST   R0,RDISK
               PEXIT
         FI
         IF    (CLI,UCBTYP+2(R1),X'80',EQ)   TAPE
               A    R0,RTAPE
               ST   R0,RTAPE
               PEXIT
         FI
         IF    (CLI,UCBTYP+2(R1),X'08',EQ),  SPOOL                     X
               AND,(CLI,UCBTYP(R1),X'50',EQ)
               A    R0,RSPOOL
               ST   R0,RSPOOL
               PEXIT
         FI
         A     R0,ROTHER
         ST    R0,ROTHER
         PEXIT
         PEXIT
         TITLE 'MATCHJOB TO ENTRY IN TABLE'
MATCHJOB PENTRY
         LA    R2,JOBFLAGS
         LA    R1,JOBTABLE
         DO    WHILE,(CLI,0(R2),X'FF',NE)
               IF   (TM,0(R2),X'40',O)
                    IF   (CLC,RJOB,0(R1),EQ)
                         IF   (CLC,RSTEP,8(R1),EQ)
                              MVI  0(R2),X'C0'
                         ELSE
                              MVI  0(R2),X'00'
                              ZR   R1
                         FI
                         PEXIT
                    FI
               FI
               AAI  R2,1
               LA   R1,LENTRY(R1)
         OD
         ZR    R1
         PEXIT
         TITLE 'CALCJOB - CALC EXCP RATE FOR JOB STEP AND PRINT IT'
CALCJOB  PENTRY
         MVC   DJOB,RJOB
         MVC   DSTEP,RSTEP
         LR    R2,R1
         USING TDSECT,R2
         CRATE DISK
         CRATE TAPE
         CRATE SPOOL
         CRATE OTHER
         MVC   TDSECT(LENTRY),RECORD
         DROP  R2
         TPPUTMSG TERM,EXCPMSG
         PEXIT
         TITLE 'ADDJOB - ADD NEW TABLE ENTRY'
ADDJOB   PENTRY
         LA    R2,JOBFLAGS
         LA    R1,JOBTABLE
         DO    WHILE,(CLI,0(R2),X'FF',NE)
               IF   (TM,0(R2),X'40',Z)
                    MVI  0(R2),X'C0'
                    MVC  0(LENTRY,R1),RECORD
                    PEXIT
               FI
               LA   R1,LENTRY(R1)
               AAI  R2,1
         OD
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
CVTHEAD  EQU   X'A0' FE 88
TCBTCB   EQU   X'74' FE 289
TCBTIO   EQU   12    FE 289
TCBTCT   EQU   X'A4' FE 291
TCBNTC   EQU   X'80' FE 290
TCBLTC   EQU   X'88' FE 290
TCTIOTBL EQU   12    FE 118
UCBNAME  EQU   13    FE 125
UCBTYP   EQU   16    FE 125
SRTEVOLI EQU   28    FE 131
CVTSMCA  EQU   X'0C4' SDA 16
CVTSYSWT EQU   X'1B8' SDA 20
CVTPVTP  EQU   X'164' SDA 18
SMCATEXP EQU   X'84'  SDA 212
PVTNPOUT EQU   X'124' PLM 826
PVTNPIN  EQU   X'128' PLM 826
PVTSPOUT EQU   X'12C' PLM 826
PVTSPIN  EQU   X'130' PLM 826
NEWBTIME DC    F'0'
OLDBTIME DC    F'0'
SMFBTIME DC    F'0'
OLDPAGE  DC    F'0'
NEWPAGE  DC    F'0'
OLDWAIT  DC    D'0'
NEWWAIT  DC    D'0'
PWORK    DC    D'0'
TERM     DC    CL8' '
NEXTTCB  DC    A(0)
         DS    0F
JOBTABLE DC    10XL32'00'
RECORD   DS    0F
RJOB     DS    CL8
RSTEP    DS    CL8
REXCPS   DS    0XL16
RDISK    DS    F
RTAPE    DS    F
RSPOOL   DS    F
ROTHER   DS    F
LENTRY   EQU   *-RECORD
JOBFLAGS DC    XL10'00'  BIT 0 = UPDATEED   BIT 1 = IN USE
         DC    X'FF' END OF TABLE
JOBRESET DC    10X'7F'
PERIOD   DC    F'0'
EXCPMSG  DS    0CL60
DJOB     DC    CL8' ',C' '
DSTEP    DC    CL8' ',C' D/M'
DDISK    DC    CL6' ',C' T/M'
DTAPE    DC    CL6' ',C' S/M'
DSPOOL   DC    CL6' ',C' U/M'
DOTHER   DC    CL6' '
         FILL  EXCPMSG
SYSMSG   DS    0CL60
         DC    C'CPU='
DCPU     DC    CL7' ',C' %  '
         DC    C'PAGES/SEC='
DPAGE    DC    CL6' ',C'  '
         DC    C'PERIOD='
DSEC     DC    CL7' ',C' SEC'
         FILL  SYSMSG
         TITLE 'DSECTS'
TDSECT   DSECT
TJOB     DS    CL8
TSTEP    DS    CL8
TEXCPS   DS    0XL16
TDISK    DS    F
TTAPE    DS    F
TSPOOL   DS    F
TOTHER   DS    F
         END
