*          DATA SET SR312000   AT LEVEL 001 AS OF 09/09/78
         TITLE 'SR312000 - SBREAK AND EBREAK BREAK CONTROL SUBROUTINES'
*
*              PGM-ID. SR312000 ALIAS SBREAK AND EBREAK
*              AUTHOR. DON HIGGINS.
*              DATE.   09/09/78.
*              REMARKS. BREAK CONTROL SUBROUTINES.
*
*              CALL SBREAK(ECB)    SET BREAK EXIT
*
*                 ECB IS 4 BYTE FIELD WHICH IS POSTED EACH TIME THE
*                 BREAK KEY IS STRUCK IF IT HAS NOT BEEN ALREADY
*                 POSTED.  THIS FIELD CAN BE INITIALIZED TO ZERO
*                 AND SIMPLY TESTED FOR NON-ZERO WITHIN PROCESSING
*                 LOOPS TO DETECT THAT THE BREAK KEY HAS BEEN HIT.
*                 A SUBTASK CAN WAIT ON THIS ECB TO WAKE UP WHEN
*                 THE BREAK KEY IS HIT.  SBREAK CLEARS ECB INITIALLY
*                 BUT THE USER MUST CLEAR IT AFTER EACH BREAK EXIT.
*
*              CALL EBREAK(ECB)   END BREAK EXIT
*
*
*
SBREAK   SUBENTRY
         L     R2,0(R1)           R2=A(ECB)
         ST    R2,AECB            SAVE A(ECB)
         MVC   0(4,R2),=F'0'      CLEAR ECB FIRST TIME ONLY
         STAX  BREAK
         SUBEXIT
         DROP
         TITLE 'BREAK CONTROL EXIT'
BREAK    TAG
         USING *,R15
         STM   R14,R2,EXITSAVE
         BALR  R2,0
         USING *,R2
         L     R1,AECB
         IF    (TM,0(R1),X'40',Z)   POST IF NOT ALREADY POSTED
               POST (1)
         FI
         LM    R14,R2,EXITSAVE
         BR    R14
         DROP
         EQUAL
AECB     DC    A(0)
EXITSAVE DC    5F'0'
         TITLE 'EBREAK - CANCEL BREAK CONTROL EXIT'
EBREAK   SUBENTRY
         STAX
         SUBEXIT
         END
