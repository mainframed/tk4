*          DATA SET GU017510   AT LEVEL 002 AS OF 10/18/73
***********************************************************************
*        FPC PROGRAM NUMBER= GU017510
*
*            PROGRAM NAME  = QSAMQSAM
*
*            AUTHOR        = F. G. WILSON
*
*            DATE WRITTEN  = 10/10/73
*
*            PROGRAM DESCRIPTION
*
*                 SYNOPSIS:   THIS PROGRAM USES QSAM TO COPY ANY FILE
*                             WITH DSORG=PS. GET LOCATE IS USED FOR
*                             THE INPUT FILE AND PUT LOCATE IS USED
*                             FOR THE OUTPUT. DCB INFORMATION MUST BE
*                             INCLUDED ON THE JCL CARD DEFINING THE
*                             OUTPUT FILE.
*
*                 FILES:
*                         INPUT:  SYSUT1
*
*                         OUTPUT: SYSUT2
*
*                 SWITCHES: NONE
*
*                 OPTIONS:  NONE
*
*                 LIMITATIONS:    ONLY USED FOR FILES WITHOUT KEYS
*                                 WHICH CAN BE PROCESSED BY QSAM
*
*                 ERROR HANDLING: NORMAL OS FACILITIES - BY CODING
*                                 THE EROPT DCB SUBPARAMETER CAN BE
*                                 USED TO COPY TAPES WITH BAD BLOCKS
*
*                 UNUSUAL CODING: NONE
*
*                 RESTARTS      : NONE
*
*                 CHANGES       : NONE
*
***********************************************************************
         PRINT NOGEN
QSAMQSAM SUBENTRY
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT))   OPEN FILES
M005GETL TAG
         GET   SYSUT1                   GET INPUT RECORD
         LR    R3,R1                    SAVE PTR TO INPUT RECORD
         MVC   SYSUT2+82(2),SYSUT1+82   SET LRECL FOR OUTPUT RECORD
         PUT   SYSUT2                   GET BUFFER ADDR FOR OUTPUT RECD
         LH    R4,SYSUT1+82             INPUT REC LEN INTO REG 4
M010TEST TAG
         CH    R4,=H'256'               CAN 1 MORE MOVE DO IT?
         BH    M015MOVE                 NO
         SH    R4,=H'1'                 YES - SET MACH LEN FOR MOVE
         EX    R4,MOVEREC               LAST MOVE
         B     M005GETL                 GO GET ANOTHER INPUT RECORD
M015MOVE TAG
         MVC   0(256,R1),0(R3)          MOVE FROM INPUT BUF TO OUTPUT
         SH    R4,=H'256'               REDUCE LENGTH REMAINING
         AH    R3,=H'256'               BUMP INPUT BUFFER POINTER
         AH    R1,=H'256'               BUMP OUTPUT BUFFER POINTER
         B     M010TEST                 GO SEE HOW MANY MORE MOVES REQ
M020EOFS TAG
         CLOSE (SYSUT1,,SYSUT2)
         SUBEXIT
         EQUREGS
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=M020EOFS
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PL,DSORG=PS
MOVEREC  MVC   0(1,R1),0(R3)
         END   QSAMQSAM
