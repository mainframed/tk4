*          DATA SET GU027400   AT LEVEL 010 AS OF 03/19/74
         TITLE 'GU027400 - LIST INSTRUCTION AND MACRO USAGE'
         PRINT NOGEN
GU027400 SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     03/13/74.
*              REMARKS.  LIST NUMBER OF TIMES EACH INSTRUCTION OR
*                        MACRO IS USED IN BAL SOURCE PGM.
*
*              LIMITATIONS.
*                   TABLE CURRENTLY WILL ONLY HANDLE 500 OPS/MACROS.
*                   A STATEMENT WHICH IS NOT A COMMENT OR A
*                   CONTINUATION, AND HAS NO OPERATOR OR HAS
*                   AN OPERATOR OVER 8 CHARACTERS LONG WILL BE
*                   COUNTED UNDER THE PSEUDO OP '*ERRORS*'.
*
         OPEN  (SYSIN,,SYSPRINT,(OUTPUT))
         PERFORM R100INT1,R190EXIT
M010     TAG
         PERFORM R200SELM,R290EXIT
         CLI   EOF,TRUE
         BE    M020
         PERFORM R300ADDM,R390EXIT
         B     M010
M020     TAG
         PERFORM R400INT2,R490EXIT
M030     TAG
         PERFORM R500FIND,R590EXIT
         CLI   EOF,TRUE
         BE    M040
         PERFORM R600RPT1,R690EXIT
         B     M030
M040     TAG
         CVD   R12,PWORK
         EDIT  TO=DTOTAL,FROM=PWORK+4,MASK=' Z,ZZZ,ZZZ'
         LA    R0,DLINE2
         PERFORM R700PRNT,R790EXIT
         CLOSE (SYSIN,,SYSPRINT)
         SUBEXIT
         TITLE 'R100 - INITIALIZE FOR SELECTION ROUTINE'
R100INT1 PENTRY
         ZR    R9
         L     R10,=A(TAB)
         SH    R10,LENTRY
R190EXIT PEXIT
         TITLE 'R200 - SELECTION ROUTINE'
R200SELM PENTRY
         GET   SYSIN
         LR    R3,R1               R3=A(RECORD)
         B     R220
R210EOF  TAG
         MVI   EOF,TRUE
         B     R290EXIT
R220     TAG
         CLI   CONT,TRUE           WAS LAST RECORD CONTINUED
         BNE   R230                NO, SKIP
         CLI   71(R3),C' '         YES, IS THIS ALSO CONTINUED
         BNE   R200SELM            YES, TRY AGAIN
         MVI   CONT,FALSE          NO,  TURN OFF CONT AND TRY AGAIN
         B     R200SELM
R230     TAG
         CLI   0(R3),C'*'          IS IT COMMENT
         BE    R200SELM            YES, SKIP
         CLI   71(R3),C' '         IS THIS START OF CONT
         BE    R240
         MVI   CONT,TRUE
R240     TAG
         MVC   OP,=CL8' '
         ZR    R1
         CLI   0(R3),C' '
         BE    R250
         TRT   0(80,R3),FINDBLK
         BZ    R260
         LR    R3,R1
R250     TAG
         TRT   0(80,R3),FINDTEXT
         BZ    R260
         LR    R3,R1
         TRT   0(9,R3),FINDBLK
         BZ    R260
         SR    R1,R3
         BCTR  R1,0
         EX    R1,MVCOP
         B     R290EXIT
MVCOP    MVC   OP(0),0(R3)
R260     TAG
         MVC   OP,=CL8'*ERRORS*'
R290EXIT PEXIT
         TITLE 'R300 - ADD NEW INSTRUCTION/MACRO ROUTINE'
R300ADDM PENTRY
         LM    R2,R3,LOWHIGH       R2=LOW    R3=HIGH
R310     TAG
         CLR   R2,R3               IS ENTRY IN TABLE
         BH    R340                NO, GO ADD
         LA    R4,0(R2,R3)         YES, R4 = (LOW + HIGH) / 2
         SRL   R4,1
         LR    R5,R4
         MH    R5,LENTRY
         LA    R5,0(R10,R5)        R5=A( TAB( R4))
         CLC   0(8,R5),OP          IS THIS ENTRY EQUAL TO OP
         BL    R320
         BH    R330
         L     R1,8(R5)            YES, INCR COUNT
         AAI   R1,1
         ST    R1,8(R5)
         B     R390EXIT
R320     TAG
         LA    R2,1(R4)            R2= R4 +1
         B     R310
R330     TAG
         LR    R3,R4               R3= R4 -1
         BCTR  R3,0
         B     R310
R340     TAG
         LR    R5,R2
         MH    R5,LENTRY
         LA    R5,0(R10,R5)        R5=A(ENTRY TO BE ADD HERE)
         CL    R9,MAXENTRY
         BNL   ERR1
         AAI   R9,1
         ST    R9,LOWHIGH+4
         LR    R6,R9
         SR    R6,R3
         SI    R6,1
         MH    R6,LENTRY           RL=LENGTH OF MVCL
         L     R7,=A(SAVETAB)
         LR    R0,R7
         LR    R1,R6
         LR    R14,R5
         LR    R15,R6
         MVCL  R0,R14              MOVE REST OF ENTRIES TO SAVE AREA
         MVC   0(8,R5),OP
         MVC   8(4,R5),=A(1)
         LR    R0,R5
         AH    R0,LENTRY
         LR    R1,R6
         LR    R14,R7
         LR    R15,R6
         MVCL  R0,R14              MOVE REST OF ENTRIES BACK BEHIND
R390EXIT PEXIT
ERR1     ABEND 201,DUMP            TABLE CAPACITY EXCEEDED
         TITLE 'R400 - INITIALIZE FOR ASCENDING FIND ROUTINE'
R400INT2 PENTRY
         MVI   EOF,FALSE
         L     R11,LOWHIGH+4
         ZR    R12
R490EXIT PEXIT
         TITLE 'R500 - FIND ENTRIES IN ASCENDING ORDER FOR LIST'
R500FIND PENTRY
         SH    R11,=H'1'
         BM    R510EOF
         AH    R10,LENTRY
         B     R590EXIT
R510EOF  TAG
         MVI   EOF,TRUE
R590EXIT PEXIT
         TITLE 'R600 - LIST ENTRIES'
R600RPT1 PENTRY
         MVC   DOP,0(R10)
         L     R0,8(R10)
         AR    R12,R0
         CVD   R0,PWORK
         EDIT  TO=DCOUNT,FROM=PWORK+4,MASK=' Z,ZZZ,ZZZ'
         SP    LINE,=P'1'
         BP    R610
         ZAP   LINE,MAXLINE
         AP    PAGE,=P'1'
         EDIT  TO=DPAGE,FROM=PAGE,MASK=' ZZZ'
         DTIME TIMEDATE
         LA    R0,HEAD1
         PERFORM R700PRNT,R790EXIT
         LA    R0,HEAD2
         PERFORM R700PRNT,R790EXIT
         LA    R0,SPACE
         PERFORM R700PRNT,R790EXIT
R610     TAG
         LA    R0,DLINE1
         PERFORM R700PRNT,R790EXIT
R690EXIT PEXIT
         TITLE 'R700 - PRINT ROUTINE'
R700PRNT PENTRY
         PUT   SYSPRINT
R790EXIT PEXIT
         TITLE 'DATA DEFINITIONS'
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=R210EOF,           X
               RECFM=FB,LRECL=80
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FA,BLKSIZE=133
         EQUAL
OP       DC    CL8' ',X'FF'
TRUE     EQU   0
FALSE    EQU   1
EOF      DC    AL1(FALSE)
CONT     DC    AL1(FALSE)
PWORK    DC    D'0'
LINE     DC    PL2'0'
MAXLINE  DC    PL2'55'
PAGE     DC    PL2'0'
HEAD1    DS    0CL133
         DC    C'1',C'INSTRUCTION AND MACRO USE COUNTS',CL10' '
TIMEDATE DC    CL22' ',CL10' ',C'PAGE'
DPAGE    DC    CL4' '
         FILL  HEAD1
HEAD2    DC    CL133'0OPERATOR --- COUNT'
SPACE    DC    CL133' '
DLINE1   DS    0CL133
         DC    C' '
DOP      DC    CL8' '
DCOUNT   DC    CL10' '
         FILL  DLINE1
DLINE2   DS    0CL133
         DC    C'0',CL8'TOTAL'
DTOTAL   DC    CL10' '
         FILL  DLINE2
FINDBLK  DC    64X'00',X'FF',191X'00'
FINDTEXT DC    64X'FF',X'00',191X'FF'
LENTRY   DC    H'12'
LOWHIGH  DC    A(1,0)
MAXENTRY DC    A(500)
         DC    C' START TAB '
TAB      DS    0F
         DS    XL(500*12)
         DC    C' START SAVETAB '
SAVETAB  DS    0F
         DS    XL(500*12)
         END   GU027400
