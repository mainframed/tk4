*          DATA SET GU024900   AT LEVEL 006 AS OF 11/29/73
         PRINT NOGEN
PDSCLIST SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/22/73.
*              REMARKS.  PRINT TWO REPORTS FROM ANY PDS LOAD MODULE
*                        LIBRARY.
*                          1. LIST ALL CSECT NAMES WITHIN EACH MEMBER.
*                          2. LIST ALL MEMBERS WHICH REFERENCE EACH
*                             CSECT NAME.
*              FILES.
*                   PDSLIB -  PDS LIBRARY
*                   PDSMEM -  PDS LIBRARY
*                   LSTMEMBR- MEMBER REPORT
*                   LSTCSECT- CSECT  REPORT
*                   NAMEIN  - FILE CREATED AND SORTED FOR CSECT REPORT
*                   NAMEOUT  - OUTPUT FROM SORT FOR CSECT REPORT
*              REFERENCES.
*                   LINKAGE EDITOR F PLM  Y28-6667-0 PG 171
*                   FE HANDBOOK S229-3169-3 PG 96
*              REGISTERS
*                   R2 - PDSLIB JFCB ADDRESS
*                   R3 - PDSLIB DE   ADDRESS
*                   R4 - REPORT DCB
*                   R5 - PDSMEM BUFFER ADDRESS
*                   R6 - LENGTH CESD TEXT
*                   R7 - A(CURRENT CESD ENTRY)
*                   R8 - A(CURRENT PRINT LINE ENTRY)
*                   R9 - FREE
*                   R10- FREE
*                   R11- FREE
*                   R12- PRINT ROUTINE LINKAGE
         OPEN  (PDSMEM,(INPUT),LSTMEMBR,(OUTPUT),NAMEIN,(OUTPUT))
         LH    R0,PDSMEM+DCBBLKSI
         GETMAIN R,LV=(0)
         LR    R5,R1
         SCALL GETPDSDD,(JFCB,DE)
         LT    R2,JFCB
         BZ    NOPDSLIB
         MVC   PDSDSN,0(R2)
         MVC   PDSVOL,118(R2)
         DTIME TIMEDATE
         LA    R4,LSTMEMBR
         LA    R8,DLIST            R8=A(CSECT NAME LIST)
*
*              PRINT MEMBER REPORT AND CREATE NAMEFILE FOR SORT
*
M1       TAG
         SCALL GETPDSDD,(JFCB,DE)
         LT    R3,DE
         BZ    S1                  AT END OF DIRECTORY, GO SORT
         MVC   DNAME,0(R3)
         MVC   MEMBER,0(R3)
         MVC   TTRK(3),8(R3)
         FIND  PDSMEM,TTRK,C
M2       TAG
         READ  DECB,SF,PDSMEM,(R5),'S'
         CHECK DECB
         CLI   0(R5),CESD
         BE    M2CESD
         TB    0(R5),(0,1,2,3)
         BZ    M2EXIT              EXIT IF CONTROL RECORD (NO MORE ESD)
         B     M2                  SKIP SYM OR SCATTER RECORDS
M2CESD   TAG
         LH    R6,6(R5)            R6=LENGTH OF ESD DATA
         LA    R7,8(R5)            R7=A(CURRENT ESD ENTRY)
M3       TAG
         LR    R0,R2               SAVE R2 FROM TRT
         TRT   8(1,R7),CESDTYPE
         LR    R2,R0
         BZ    M3NEXT
         CLC   0(8,R7),=CL8' '
         BE    M3NEXT
         MVC   0(8,R8),0(R7)
         MVC   CSECT,0(R7)
         PUT   NAMEIN,NAMEREC
         AP    SIZE,=P'1'
         LA    R8,9(R8)
         CL    R8,=A(DLISTEND)
         BL    M3NEXT
         BAL   R12,PRINT
M3NEXT   TAG
         SH    R6,=H'16'
         BNP   M2
         LA    R7,16(R7)
         B     M3
M2EXIT   TAG
         CLC   DLINE+1(132),DLINE
         BE    M1
         BAL   R12,PRINT
         B     M1
*
*              SORT  MEMBER NAMES WITHIN CSECT NAME
*
S1       TAG
         CLOSE (NAMEIN,,PDSMEM,,LSTMEMBR)
         EDIT  TO=DSIZE,FROM=SIZE,MASK='099999'
         LINK  EP=SORT,PARAM=(SORTPARM)
         OPEN  (NAMEOUT,(INPUT),LSTCSECT,(OUTPUT))
*
*              PRINT CSECT REPORT
*
         LA    R4,LSTCSECT
         ZAP   PAGE,=P'0'
         ZAP   LINE,=P'0'
         MVC   HEAD2,HEAD2ALT
C1       TAG
         GET   NAMEOUT,NAMEREC
         CLC   CSECT,LASTC
         BE    CADD
CFLUSH   TAG
         CLC   DLINE+1(132),DLINE
         BE    CNEW
         BAL   R12,PRINT
CNEW     TAG
         MVC   DNAME,CSECT
         MVC   LASTC,CSECT
         LA    R8,DLIST
CADD     TAG
         MVC   0(8,R8),MEMBER
         LA    R8,9(R8)
         CL    R8,=A(DLISTEND)
         BL    C1
         BAL   R12,PRINT
         B     C1
CEND     TAG
         CLC   DLINE+1(132),DLINE
         BE    CEND2
         BAL   R12,PRINT
CEND2    TAG
         CLOSE (NAMEOUT,,LSTCSECT)
EXIT     TAG
         SUBEXIT
NOPDSLIB TAG
         WTO   'NO PDSLIB DD'
         B     EXIT
PRINT    TAG
         SP    LINE,=P'1'
         BP    PRINTD
         ZAP   LINE,=P'60'
         AP    PAGE,=P'1'
         EDIT  TO=DPAGE,FROM=PAGE,MASK=' ZZZ'
         PUT   (R4),HEAD1
         PUT   (R4),HEAD2
         PUT   (R4),HEAD3
PRINTD   TAG
         PUT   (R4),DLINE
         MVC   DLINE+1(132),DLINE
         LA    R8,DLIST
         BR    R12
         EQUAL
DCBBLKSI EQU   62
PDSMEM   DCB   DSORG=PO,MACRF=R,DDNAME=PDSMEM,EODAD=M2EXIT
LSTMEMBR DCB   DSORG=PS,MACRF=PM,DDNAME=LSTMEMBR,RECFM=FA,BLKSIZE=133
LSTCSECT DCB   DSORG=PS,MACRF=PM,DDNAME=LSTCSECT,RECFM=FA,BLKSIZE=133
NAMEIN   DCB   DSORG=PS,MACRF=PM,DDNAME=NAMEIN,                        X
               RECFM=FB,LRECL=16
NAMEOUT  DCB   DSORG=PS,MACRF=GM,DDNAME=NAMEOUT,EODAD=CEND
JFCB     DC    A(0)
DE       DC    A(0)
HEAD1    DS    0CL133
         DC    C'1',20C' '
PDSDSN   DC    CL44' ',10C' '
PDSVOL   DC    CL6' ',10C' '
TIMEDATE DC    CL22' ',C' PAGE'
DPAGE    DC    CL3' '
         FILL  HEAD1
HEAD2    DS    0CL133
         DC    C'0MEMBER  ',13C' CSECT   '
         FILL  HEAD2
HEAD2ALT DS    0CL133
         DC    C'0CSECT   ',13C' MEMBER  '
         FILL  HEAD2ALT
HEAD3    DC    CL133' '
DLINE    DS    0CL133
         DC    C' '
DNAME    DC    CL8' ',C' '
DLIST    DC    13CL9' '
DLISTEND EQU   *
         FILL  DLINE
LINE     DC    PL2'0'
PAGE     DC    PL2'0'
SIZE     DC    PL3'0'
NAMEREC  DS    0CL16
CSECT    DC    CL8' '
MEMBER   DC    CL8' '
TTRK     DC    XL4'00'
CESD     EQU   X'20'
SD       EQU   X'00'
PC       EQU   X'04'
CM       EQU   X'05'
PR       EQU   X'06'
ENTAB    EQU   X'14'
AUTOCESD EQU   X'80'    THIS IS NOT DOCUMENTED IN THE PLM
CESDTYPE DC    AL1(1,0,0,0,1,1,1),13AL1(0),AL1(1),107AL1(0)
         DC    AL1(1),127AL1(0)
         CNOP  2,4
SORTPARM DC    AL2(PARMEND-*-2)
         DC    A(BSORT,ESORT,BREC,EREC,0,0),C'NAME'
PARMEND  EQU   *
BSORT    DC    C' SORT FIELDS=(1,16,BI,A),SIZE='
DSIZE    DC    CL6' '
ESORT    DC    C' '
BREC     DC    C' RECORD TYPE=F,LENGTH=16'
EREC     DC    C' '
LASTC    DC    CL8' '
         END   PDSCLIST
