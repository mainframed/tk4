*          DATA SET GU952000   AT LEVEL 009 AS OF 02/15/78
         TITLE 'PANDIR - GU952000 PDS DIRECTORY LIST TSO COMMAND'
*
*              AUTHOR. DON HIGGINS.
*              DATE.   02/10/78.
*              REMARKS. LIST ALL OR PART OF PDS DIRECTORY FOR
*                       ONE OR MORE LIBRARIES.  SHOW MEMBER NAME,
*                       TTR, SSI, AND SIZE IF LOAD MODULE TYPE.
*              INPUT.
*                       PDSLIBXX - ANY NUMBER OF PDS LIBRARIES
*                       CAN BE SCANNED BY DEFINING EACH ONE WITH
*                       SEPARATE DD WHERE XX IS ANY TWO CHARACTERS.
*
*                       PARM = UP TO 8 CHARACTERS DEFINING SEARCH KEY
*                       OR * TO INDICATE LIST ALL.
*              OUTPUT.
*                       TPUT FOR TSO
*                       SYSPRINT FOR BATCH EXECUTION.
*
         TITLE 'MAINLINE'
GU952000 SUBENTRY
         L     R1,0(R1)
         LH    R3,0(R1)
         LA    R4,2(R1)
         SH    R3,=H'1'
         DTIME TIMEDATE
         SCALL SROSDATE,(GDATE)
         L     R1,PSAAOLD
         IF    (ICM,R0,15,ASCBTSB(R1),Z)
               MVI   MODE,BATCH
               OPEN  (SYSPRINT,(OUTPUT))
         FI
         LA    R0,MSG1
         PM    PUT
         PM    GETPDS
         STAX  STAXRTN,OBUF=(STAXMSG,80)
         DO    WHILE,(CLC,AJFCB,=F'0',NE)
               PM   LISTFILE
               PM   GETPDS
               DO   WHILE,(CLC,ADE,=F'0',NE)
                    L    R5,ADE
                    IF   (EX,R3,CLCKEY,EQ),OR,(CLI,0(R4),C'*',EQ)
                         PM   LISTDE
                    FI
                    IF   (CLI,ATTN,TRUE,EQ)
                         B    ATTNEXIT
                    FI
                    PM   GETPDS
               OD
               PM   GETPDS
         OD
         LA    R0,MSG4
         PM    PUT
ATTNEXIT TAG
         IF    (CLI,MODE,BATCH,EQ)
               CLOSE     (SYSPRINT)
         FI
         SUBEXIT
CLCKEY   CLC   0(0,R4),0(R5)
         TITLE 'GETPDS'
GETPDS   PENTRY
         SCALL GETPDSDD,(AJFCB,ADE)
         PEXIT
         TITLE 'LISTFILE'
LISTFILE PENTRY
         L     R1,AJFCB
         MVC   DSN,0(R1)
         MVC   DSNVOL,118(R1)
         LA    R0,MSG2
         PM    PUT
         PEXIT
         TITLE 'LISTDE'
LISTDE   PENTRY
         MVC   MEMBER,0(R5)
         LA    R1,8(R5)
         PM    EDITHEX
         MVC   TTR,DWORK
         NI    11(R5),X'1F'
         IF    (CLI,11(R5),11,GE)  LOAD MODULE DE
               LA   R1,22(R5)
               PM   EDITHEX
               MVC  HSIZE,DWORK
               ZR   R0
               ICM  R0,7,22(R5)
               CVD  R0,PWORK
               EDIT TO=DSIZE,FROM=PWORK+4,MASK=' ZZZZZ99'
               IF   (CLI,11(R5),13,GE) LOAD MODULE SSI
                    LA   R1,34(R5)
                    PM   EDITHEX
                    MVC  SSI,DWORK
               ELSE
                    MVC  SSI,=8C'*'
               FI
         ELSE
               MVC  HSIZE,=6C'*'
               MVC  DSIZE,=8C'*'
               IF   (CLI,11(R5),2,EQ) NON-LOAD MODULE SSI
                    LA   R1,12(R5)
                    PM   EDITHEX
                    MVC  SSI,DWORK
               ELSE
                    MVC  SSI,=8C'*'
               FI
         FI
         LA    R0,MSG3
         PM    PUT
         PEXIT
         TITLE 'PUT'
PUT      PENTRY
         IF    (CLI,MODE,BATCH,EQ)
               PUT  SYSPRINT
         ELSE
               LR   R1,R0
               LA   R0,80
               TPUT (1),(0),R
         FI
         PEXIT
         TITLE 'EDITHEX'
EDITHEX  PENTRY
         UNPK  DWORK,0(5,R1)
         TR    DWORK(8),HEX-240
         PEXIT
         TITLE 'STAXRTN'
STAXRTN  TAG
         USING *,R15
         MVI   ATTN,TRUE
         BR    R14
         DROP  R15
ATTN     LOGICAL    FALSE,(TRUE,FALSE)
STAXMSG  DC    CL80'PDSDIR SCAN ABORTED BY ATTN KEY'
         TITLE 'COMMON DATA'
         EQUAL
AJFCB    DC    A(0)
ADE      DC    A(0)
MODE     LOGICAL    TSO,(TSO,BATCH)
SYSPRINT DCB   DSORG=PS,MACRF=PM,RECFM=F,BLKSIZE=80,DDNAME=SYSPRINT
PSAAOLD  EQU   X'224' PG 528
ASCBTSB  EQU   X'3C' PG 69
PWORK    DC    D'0'
DWORK    DC    CL9' '
HEX      DC    C'0123456789ABCDEF'
MSG1     DS    0CL80
         DC    C'PDSDIR GU952000 V'
         DC    CL8'&SYSDATE',C'  '
GDATE    DC    CL8' '
TIMEDATE DC    CL22' '
         FILL  MSG1
MSG2     DS    0CL80
DSN      DC    CL44' ',C' '
DSNVOL   DC    CL6' '
         FILL  MSG2
MSG3     DS    0CL80
         DC    C'  '
MEMBER   DC    CL8' ',C' TTR='
TTR      DC    CL6' ',C' SSI='
SSI      DC    CL8' ',C' SIZE='
HSIZE    DC    CL6' ',C' ('
DSIZE    DC    CL8' ',C')'
         FILL  MSG3
MSG4     DS    0CL80
         DC    C'PDSDIR END OF SCAN'
         FILL  MSG4
         END
