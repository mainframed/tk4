*          DATA SET CS312000   AT LEVEL 007 AS OF 11/18/76
         TITLE 'CS312000 - SMF REPORT POST PROCESSOR'
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/15/76.
*              REMARKS.  SMF REPORT POST PROCESSOR
*
*                   1.   COPY SELECTED REPORTS WITH ID
*                   2.   CREATE MASTER REPORT
*                   3.   CREATE MASTER MICROFICHE TAPE.
*
*              FILES.
*                   CS312S01 - INPUT CONTROL CARDS
*
*                             01-03 - REQUESTOR ID
*                             04-04 - OPTION CODE
*
*                                       E - EXCLUDE
*                                       S - SELECT
*                                       M - COPY ALL FOR MASTER COPY
*                                       F - MICROFICHE COPY
*
*                             05-80 - UP TO 19 REPORT CODES
*
*                   CS312S02 - OUTPUT COPIES
*                   CS312S03 - INPUT COPY
*                   JOBSTATS - EZFICHE STATISTICS PER TH 30.06.26
*                   FICHEOUT - OUTPUT MICROFICHE TAPE
*                   PARMCRDS - EZFICHE INPUT CONTROL CARDS
*
         TITLE 'MAIN'
CS312000 SUBENTRY
         OPEN  (CS312S02,(OUTPUT),CS312S01)
         PM    GETCTL
         DO    WHILE,(CLI,EOF1,TRUE,NE)
               PM   SELECT
               PM   GETCTL
         OD
         CLOSE (CS312S01,,CS312S02)
         SUBEXIT
CS312S01 SDCB  M=GM,R=FB,L=80,E=EOFRTN1
CS312S02 SDCB  M=PM,R=FBA,L=133
CS312S03 SDCB  M=GM,R=FB,L=133,E=EOFRTN3
EOF1     LOGICAL FALSE,(TRUE,FALSE)
EOF3     LOGICAL FALSE,(TRUE,FALSE)
CTLREC   DS    0CL80
CTLID    DC    CL3' '
CTLOPT   DC    C' '
CTLRPTS  DC    19C'XX'
         FILL CTLREC
EOFRPTS  DC    X'FFFF'   HIGH VALUE FOR END OF REPORT SELECTIONS
         TITLE 'GETCTL - READ SELECT CONTROL CARD'
GETCTL   PENTRY
         GET   CS312S01,CTLREC
         PEXIT
EOFRTN1  MVI   EOF1,TRUE
         PEXIT
         TITLE 'SELECT - PROCESS SELECT CARD'
SELECT   PENTRY
         OPEN  (CS312S03)
         IF    (CLI,CTLOPT,C'F',EQ)
               SCALL FICHEOPN,(CS312S02,LINE)
         FI
         LA    R2,CTLRPTS
         MVI   EOF3,FALSE
         MVI   MODE,NOPRINT
         PM    GETRPT
         DO    WHILE,(CLI,EOF3,TRUE,NE)
               IF   (CLC,CTLKEY1,LINE+PRTKEY1,EQ),                     X
               AND,(CLC,CTLKEY2,LINE+PRTKEY2,EQ),                      X
               AND,(CLI,LINE,C'1',EQ)
                    MVI  HEADING,TRUE
                    MVC  LINE+PRTID(3),CTLID
                    OI   LINE+PRTCTL,C'0'    FIX FORT ZERO SURPRESS
               ELSE
                    MVI  HEADING,FALSE
               FI
               DOCASE CTLOPT
                    CASE C'S',C'E'
                         IF   (CLI,HEADING,TRUE,EQ)
                              DO   WHILE,(CLC,0(2,R2),LINE+PRTCTL,L)
                                   AAI  R2,2
                              OD
                              IF   (CLC,0(2,R2),LINE+PRTCTL,EQ)
                                   MVI  MODE,PRINT
                              ELSE
                                   MVI  MODE,NOPRINT
                              FI
                         FI
                         IF   (CLI,MODE,PRINT,EQ)
                              IF   (CLI,CTLOPT,C'S',EQ)
                                   PM   PUTRPT
                              FI
                         ELSEIF (CLI,CTLOPT,C'E',EQ)
                              PM   PUTRPT
                         FI
                    ESAC
                    CASE C'M'
                         PM   PUTRPT
                    ESAC
                    CASE C'F'
                         PM   PUTRPT
                    ESAC
               ESACOD
               PM   GETRPT
         OD
         IF    (CLI,CTLOPT,C'F',EQ)
               SCALL FICHECLS,(CS312S02)
         FI
         CLOSE (CS312S03)
         PEXIT
HEADING  LOGICAL FALSE,(TRUE,FALSE)
MODE     LOGICAL NOPRINT,(PRINT,NOPRINT)
CTLKEY1  DC    C'CS'
CTLKEY2  DC    C'R'
PRTKEY1  EQU   1
PRTKEY2  EQU   6
PRTCTL   EQU   7
PRTID    EQU   90
LINE     DC    CL133' '
         TITLE 'GETRPT - GET PRINT LINE'
GETRPT   PENTRY
         GET   CS312S03,LINE
         PEXIT
EOFRTN3  MVI   EOF3,TRUE
         PEXIT
         TITLE 'PUTRPT - PUT PRINT LINE'
PUTRPT   PENTRY
         PUT   CS312S02,LINE
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
         END
