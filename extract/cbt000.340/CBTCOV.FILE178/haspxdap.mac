*          DATA SET GU019600   AT LEVEL 004 AS OF 03/17/76
         TITLE 'GU019600 HASPXDAP - HASP SPOOL I/O ROUTINE'
         PRINT NOGEN
HASPXDAP SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     03/12/73.
*              REMARKS.  READ HASP SPOOL RECORDS VIA XDAP.
*              ARGUMENTS.
*                  MTTR  - 4 BYTE HASP MTTR.
*                  AREA  - ADDRESS OF AREA TO CONTAIN RECORD.
*                  LAREA - LENGTH OF AREA
*
*              REV1 03/15/76 DSH - SUPPORT RELATIVE TRACK CONVERSION.
*                                  HIGH BIT MEANS RELATIVE.
*
         LM    R2,R4,0(R1)
SKIP     NOP   CVTMTTR
         OI    SKIP+1,X'F0'
         OPEN  (SPOOL1,,SPOOL2)
         LOADTCB R1
         L     R1,TCBDEB(R1)
         DO    WHILE,(LTR,R1,R1,NZ),                                   X
               AND,(CLC,DEBDCB(3,R1),=AL3(SPOOL1),NE)
               L    R1,DEBDEB(R1)
         OD
         LH    R0,DEBSTRCC(R1)
         MH    R0,TRKCYL+2
         AH    R0,DEBSTRHH(R1)
         STH   R0,STRTT
CVTMTTR  TAG
         IF    (TM,0(R2),X'80',O)
               NI   0(R2),X'7F'
               ZR   R0
               ICM  R0,3,1(R2)
               AH   R0,STRTT
               STCM R0,3,1(R2)
         FI
         MVC   MTTR,0(R2)
         MVC   MBBCCHHR+7(1),MTTR+3
         L     R1,MTTR
         SLL   R1,8
         SRL   R1,16
         SR    R0,R0
* REV1 01/28/74 DSH - DEVICE DEPENDENT TRKS/CYL
         D     R0,TRKCYL
         STH   R0,CCHH+2
         STH   R1,CCHH
         MVC   MBBCCHHR+3(4),CCHH
         SR    R1,R1
         IC    R1,MTTR
         SLL   R1,2
         L     R2,DCBLIST(R1)
         MVI   ECB,X'00'
         XDAP  ECB,RI,(R2),(R3),(R4),,MBBCCHHR
         WAIT  ECB=ECB
         CLI   ECB,X'7F'
         BE    EXIT
         ABEND 111,DUMP
EXIT     TAG
         SUBEXIT
         EQUAL
* REV1 DEVICE DEPENDENT TRKS/CYL
TRKCYL   DC    F'19'
* REV2 DATA
TCBDEB   EQU   8    FE   259
DEBDCB   EQU   25   FE 179
DEBDEB   EQU   4    FE 179
DEBSTRCC EQU   38   FE 181
DEBSTRHH EQU   40   FE 181
STRTT    DC    H'0'
MTTR     DC    F'0'
CCHH     DC    F'0'
MBBCCHHR DC    XL8'00'
DCBLIST  DC    A(SPOOL1,SPOOL2)
SPOOL1   DCB   DSORG=DA,MACRF=E,DDNAME=SPOOL1
SPOOL2   DCB   DSORG=DA,MACRF=E,DDNAME=SPOOL2
         END
