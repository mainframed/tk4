*          DATA SET GU424000   AT LEVEL 001 AS OF 02/26/79
         TITLE 'GU424000 - WTOR SUBROUTINE'
*
*              PGM-ID. GU424000 ALIAS FPC.SUBLIB(WTOR).
*              AUTHOR. DON HIGGINS.
*              DATE.   02/21/79.
*              REMARK. CALL WTOR(MSG,LMSG,REPLY,LREPLY)
*
*                      MSG  - MESSAGE TO DISPLAY ON MAIN CONSOLE
*
*                      LMSG - 4 BYTE BINARY LENGTH OF MESSAGE
*
*                      REPLY- AREA FOR OPERATOR REPLY
*
*                      LREPLY - 4 BYTE BINARY LENGTH OF REPLY
*
*
*
*
*
WTOR     SUBENTRY
         LM    R2,R5,0(R1)
         L     R3,0(R3)
         L     R5,0(R5)
         ICM   R3,8,=C' '
         LA    R6,WTORMSG
         LA    R7,L'WTORMSG
         MVCL  R6,R2
         XC    WTORECB,WTORECB
         WTOR  ,(R4),(R5),WTORECB,MF=(E,WTORLIST)
         WAIT  ECB=WTORECB
         BCTR  R5,0
         EX    R5,UCASE
         SUBEXIT
UCASE    OC    0(0,R4),=120C' '  FORCE UPPER CASE REPLY
         EQUAL
WTORLIST DS    0F
         DC    AL1(0),AL3(0)  REPLY LENGTH AND ADDRESS
         DC    A(0)           ECB ADDRESS
         DC    AL2(WTOREND-*,0)
WTORMSG  DC    CL100' '
WTOREND  EQU   *
WTORECB  DC    F'0'
         END
