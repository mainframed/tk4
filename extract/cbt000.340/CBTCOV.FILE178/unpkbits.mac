*          DATA SET GU013500   AT LEVEL 004 AS OF 11/09/72
UNPKBITS CSECT
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/02/72.
*              REMARKS.  UNPACK BIT SWITCHES INTO BYTE SWITCHES.
*
*                        CALL UNPKBITS (BITS,NUMBITS,BYTES)
*
*                         BITS - BIT ARRAY WITH 8 BITS PER BYTE
*
*                         NUMBITS - FULL WORD COMPUTATIONAL NUMBER OF
*                                   BITS TO UNPACK. IT MUST BE A
*                                   MULTIPLE OF 8.
*
*                        BYTES - BYTE ARRAY TO CONTAIN 0 OR 1 VALVES.
*
*              REGISTERS
*                R1 - A(BIT ARRAY)
*                R2 - F'1'
*                R3 - A(BYTE ARRAY)
*                R4 - F'8'
*                R5 - A(BYTE ARRAY+NUMBITS-1)
*                R6 - WORK
*                R7 - WORK
*
         USING *,R15
         SAVE  (1,7)
         LM    R1,R3,0(R1)
         L     R2,0(R2)            R2=NUMBITS
         LA    R5,0(R2,R3)         R5=A(BYTE ARRAY+NUMBITS)
         BCTR  R5,R0
         LA    R2,1                R2=F'1'
         LA    R3,0(R3)            R3=A(BYTE ARRAY WITHOUT VL BIT)
         LA    R4,8                R4=F'8'
         SR    R6,R6               NEED BITS 0-23 ZERO
         SR    R7,R7               NEED BITS 0-3 ZERO
LOOP     TAG
         IC    R6,0(R1)            R6=BITS 0-7 IN POS 24-31
         SRDA  R6,4                R6=BITS 0-3 IN POS 28-31, AND
*                                  R7=BITS 4-7 IN POS 0-3
         SLL   R6,2                R6=BITS 0-3 IN POS 26-29 (MULT X 4)
         SRL   R7,26               R7=BITS 4-7 IN POS 26-29 (MULT X 4)
         LA    R7,TAB(R7)          R7=A(TABLE PATTERN FOR LAST 4 BITS)
         MVC   4(4,R3),0(R7)
         LA    R7,TAB(R6)          R7=A(TABLE PATTERN FOR FIRST 4 BITS)
         MVC   0(4,R3),0(R7)
         AR    R1,R2
         BXLE  R3,R4,LOOP
         RETURN (1,7)
TAB      EQU   *
         DC    C'0000'
         DC    C'0001'
         DC    C'0010'
         DC    C'0011'
         DC    C'0100'
         DC    C'0101'
         DC    C'0110'
         DC    C'0111'
         DC    C'1000'
         DC    C'1001'
         DC    C'1010'
         DC    C'1011'
         DC    C'1100'
         DC    C'1101'
         DC    C'1110'
         DC    C'1111'
         EQUREGS
         END
