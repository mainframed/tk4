*          DATA SET TO030000   AT LEVEL 030 AS OF 06/22/78
*          DATA SET TO030000   AT LEVEL 023 AS OF 06/21/78
*          DATA SET TO030000   AT LEVEL 021 AS OF 06/09/78
*          DATA SET TO030000   AT LEVEL 018 AS OF 06/09/78
         TITLE 'TO030000 - DATE COMMAND PROCESSOR'
*-------------------------------------------------------------------
*
*  PROGRAM     TO030000 (JULGRE,GREJUL) COMMAND PROCESSOR
*
*  REMARKS     THIS COMMAND PROCESSOR WILL CONVERT A JULIAN DATE
*              TO GREGORIAN DATE WITH DAY OF WEEK:
*                 INPUT:     JULGRE J(YYDDD)
*                 OUTPUT:    JULIAN=YYDDD
*                            GREGORIAN=MM/DD/YY
*                            CALENDAR=WEEKDAY MONTH DD, 19YY
*
*              IT WILL ALSO CONVERT A GREGORIAN DATE TO A JULIAN
*              DATE AS FOLLOWS:
*                 INPUT:     JULGRE G(MMDDYY)
*                 OUTPUT:    GREGORIAN=MM/DD/YY
*                            JULIAN=YYDDD
*                            CALENDAR=WEEKDAY MONTH DD, 19YY
*
*              IF NO OPERANDS ARE SPECIFIED, THE COMMAND WILL DEFAULT
*              TO THE CURRENT DATE.
*
*
*
*  REV  06/05/78  W R WILLITS
*  1.  CONVERT USF SUBROUTINE CALLS TO FPC SUBROUTINE CALLS.
*  2.  CHANGE BAL ROUTINES TO INLINE CODE.
*  3.  UTILIZE SOME STRUCTURED CODE INSTEAD OF B*+
*  4.  CHANGE ALL OPTIONS TO SHOW ALL THREE FORMATS.
*
*-------------------------------------------------------------------
         SPACE 2
         PUNCH ' ALIAS JULGRE &SYSDATE &SYSTIME IGNORE IEW0731'
         EJECT
TO030000 SUBENTRY BASES=(R12,R11)
         SPACE 2
         TITLE 'PARAMETERS FOR PARSE'
         PRINT NOGEN
         LR    R9,R1             SET UP CPPL ADDRESSABILITY
         USING CPPL,R9           USE R9 FOR BASE
         LM    R2,R5,CPPL        PICK-UP PTRS FROM CPPL
         ST    R2,PPLCBUF        --> CMD BUFFER
         ST    R3,PPLUPT         --> UPT
         ST    R5,PPLECT         --> ECT
         XC    ECB,ECB           CLEAR ECB FOR PARSE
         LA    R2,ECB            STORE IT IN
         ST    R2,PPLECB         PPL LIST
         LA    R7,PARSANSW       LOAD ANSWER AREA ADDRESS
         ST    R7,PPLANSW        STORE IN PPL LIST
         L     R2,PCLADDR        LOAD PCL ADDR AND
         ST    R2,PPLPCL         STORE IN PPL
         XC    PPLUSER,PPLUSER   CLEAR USER
         LA    R1,PPL            SET ADR IN R1 FOR LINK
         LINK  EP=IKJPARS
         IF    (LTR,R15,R15,NZ)
               MVC   TMSG+10(20),=CL20'IKJPARS NON-ZERO RC'
               PM    TPUT
               B     RETURN
         FI
         DROP  R9
         L     R9,0(,R7)               PDL ADDRESS
         USING PDL,R9
         SPACE 2
         IF    (OC,JUL,JUL,NZ)
               MVI   DEFSW,X'00'             TURN OFF DEFAULT SWITCH
               L     R2,JULIAN               A(JULIAN DATE PDE)
               MVC   JULUNPK,0(R2)           SAVE JULIAN DATE FOR MSG
               PACK  JARG,0(5,R2)            SET UP SRCVTJUL CALL
               PACK  WDATE,0(5,R2)           SET UP GU719100 CALL
               IF  (CLC,2(3,R2),=C'001',LT),OR,(CLC,2(3,R2),=C'366',GT)
                     MVC   TMSG+3(3),JULUNPK+2
                     MVC   TMSG+6(50),JERR
                     PM    TPUT
               ELSE
                     PM    JUL2GRE
               FI
         FI
         SPACE 2
         IF    (OC,GREG,GREG,NZ)
               MVI   DEFSW,X'00'             TURN OFF DEFAULT SWITCH
               L     R2,GRGORIAN             A(GREGORIAN PDE)
               MVC   GARG,0(R2)              SET UP SRCVTGRE CALL
               IF    (CLC,0(2,R2),=C'12',GT)
                     MVC   TMSG+3(2),0(R2)
                     MVC   TMSG+5(50),GERRM
                     PM    TPUT
                     MVI   GERRSW,C'1'
               FI
               IF    (CLC,2(2,R2),=C'31',GT)
                     MVC   TMSG+3(2),2(R2)
                     MVC   TMSG+5(50),GERRD
                     PM    TPUT
                     MVI   GERRSW,C'1'
               FI
               IF   (CLC,0(2,R2),=C'02',EQ),AND,(CLC,2(2,R2),=C'29',GT)
                     MVC   TMSG+3(50),GERRF
                     PM    TPUT
                     MVI   GERRSW,C'1'
               FI
               IF    (CLI,GERRSW,C'1',NE)
                     PM    GRE2JUL
               FI
         FI
         SPACE 2
         IF    (TM,DEFSW,X'FF',O)
               L     R4,16
               MVC   TMSG+3(20),TODAYEQ
               PM    TPUT
               UNPK  JULUNPK,57(3,R4)        SAVE JULIAN DATE FOR MSG
               OI    JULUNPK+4,C'0'          REMOVE SIGN
               MVC   JARG,57(R4)             SET UP SRCVTJUL CALL
               MVC   WDATE+1(3),57(R4)       SET UP GU719100 CALL
               PM    JUL2GRE
         FI
         SPACE 2
RETURN   SUBEXIT
         TITLE 'PERFORMED ROUTINES'
JUL2GRE  PENTRY
         CALL  SRCVTJUL,(JARG,GREGBACK),VL
         CALL  GU719100,(WDATE),VL
         MVC   TMSG+3(7),JULEQ
         MVC   TMSG+10(5),JULUNPK      MOVE JULIAN DATE
         MVC   TMSG+20(10),GREGEQ
         MVC   TMSG+30(8),GREGBACK     FORMATTED GREGORIAN DATE
         PM    DAYMO                   GET DAY AND MONTH FROM TABLES
         MVC   TMSG+42(9),CALENDEQ
         MVC   TMSG+51(9),0(R6)        MOVE THE DAY FROM TABLE
         MVC   TMSG+61(3),0(R8)        MOVE THE MONTH FROM TABLE
         MVC   TMSG+65(2),GDAY         DD
         MVC   TMSG+67(4),CENTURY      19
         MVC   TMSG+71(2),GYR          YY
         PM    TPUT
         PEXIT
         SPACE 2
GRE2JUL  PENTRY
         CALL  SRCVTGRE,(GARG),VL
         MVC   GMO,0(R2)
         MVC   GDAY,2(R2)
         MVC   GYR,4(R2)
         MVC   TMSG+3(10),GREGEQ
         MVC   TMSG+13(8),GREGBACK     GREGORIAN TO OUTMSG
         PACK  WDATE,GARG+1(5)         SET UP GU719100 CALL
         CALL  GU719100,(WDATE),VL
         PM    DAYMO                   GET DAY AND MONTH FROM TABLES
         MVC   TMSG+42(9),CALENDEQ
         MVC   TMSG+51(9),0(R6)        DAY OF WEEK FROM TABLE
         MVC   TMSG+61(3),0(R8)        MONTH FROM TABLE
         MVC   TMSG+65(2),GDAY         DD
         MVC   TMSG+67(4),CENTURY      19
         MVC   TMSG+71(2),GYR          YY
         MVC   TMSG+26(7),JULEQ
         MVC   TMSG+33(5),GARG+1       JULIAN TO OUTMSG
         PM    TPUT
         PEXIT
         SPACE 2
DAYMO    PENTRY
         MVC   DBLWD+4(4),WDATE        PACK DAY AND
         CVB   R5,DBLWD                CONVERT IT BINARY
         MH    R5,=H'9'                COMPUTE OFFSET IN DAY TABL
         LA    R6,DAYTABL-9
         LA    R6,0(R5,R6)             --> CORRECT ENTRY
         PACK  DBLWD,GMO
         CVB   R5,DBLWD
         BCTR  R5,0                    ADJUST BY ONE
         MH    R5,=H'3'                COMPUTE OFFSET INTO TABL
         LA    R8,MOTABL
         LA    R8,0(R5,R8)             --> CORRECT ENTRY
         PEXIT
         SPACE 2
TPUT     PENTRY
         LA    R1,TMSG                 ADDR OF MESSAGE
         LA    R0,L'TMSG               LENGTH OF
         TPUT  (1),(0),R
         MVI   TMSG,X'40'              CLEAR MSG LINE
         MVC   TMSG+1(80),TMSG
         PEXIT
         TITLE 'WORK AREAS AND CONSTANTS'
         PRINT GEN
CALENDEQ DC    CL9'CALENDAR='
CENTURY  DC    CL4', 19'
TODAYEQ  DC    CL20'TODAY''S DATE IS:'
GREGEQ   DC    CL10'GREGORIAN='
JULEQ    DC    CL7'JULIAN='
JERR     DC    CL50' IS NOT A VALID JULIAN DAY'
GERRM    DC    CL50' IS NOT A VALID MONTH'
GERRD    DC    CL50' IS NOT A VALID DAY OF THE MONTH'
GERRF    DC    CL50' FEB USUALLY DOESN''T HAVE MORE THAN 28 OR 29 DAYS'
         SPACE 2
         DS    0F
TRTBL    DC    CL240' '
         DC    X'00000000000000000000',CL6' '
         SPACE 2
JULUNPK  DC    CL5' '
DEFSW    DC    X'FF'
GERRSW   DC    C'0'
         SPACE 2
JARG     DC    XL3'00000C'
GREGBACK DS    0CL8
GMO      DC    CL2' '
         DC    CL1'/'
GDAY     DC    CL2' '
         DC    CL1'/'
GYR      DC    CL2' '
         SPACE 2
GARG     DC    CL6' '
         SPACE 2
WDATE    DC    XL4'0000000C'
DBLWD    DC    D'0'
         SPACE 2
DAYTABL  DS    0F
         DC    CL9'MONDAY'              WDATE = 1
         DC    CL9'TUESDAY'                     2
         DC    CL9'WEDNESDAY'                   3
         DC    CL9'THURSDAY'                    4
         DC    CL9'FRIDAY'                      5
         DC    CL9'SATURDAY'                    6
         DC    CL9'SUNDAY'                      7
         SPACE 2
MOTABL   DS    0F
         DC    CL3'JAN'
         DC    CL3'FEB'
         DC    CL3'MAR'
         DC    CL3'APR'
         DC    CL3'MAY'
         DC    CL3'JUN'
         DC    CL3'JUL'
         DC    CL3'AUG'
         DC    CL3'SEP'
         DC    CL3'OCT'
         DC    CL3'NOV'
         DC    CL3'DEC'
         SPACE 2
TMSG     DC    CL80' '
         SPACE 2
PPL      DS    0H
PPLUPT   DS    A
PPLECT   DS    A
PPLECB   DS    A
PPLPCL   DS    A
PPLANSW  DS    A
PPLCBUF  DS    A
PPLUSER  DS    A
         SPACE 2
PARSANSW DS    A
ECB      DS    F
PCLADDR  DC    V(PCL)
         SPACE 2
         EQUAL
         TITLE 'IKJ DSECTS'
         IKJCPPL
TO030000 CSECT
         EJECT
PCL      IKJPARM   DSECT=PDL
TO030000 CSECT
         EJECT
JUL      IKJKEYWD
         EJECT
         IKJNAME   'J',SUBFLD=JULDATE
         EJECT
GREG     IKJKEYWD
         EJECT
         IKJNAME   'G',SUBFLD=GRGDATE
         EJECT
JULDATE  IKJSUBF
         EJECT
JULIAN   IKJIDENT  'JULIAN DATE',MAXLNTH=5,FIRST=NUMERIC,OTHER=NUMERIC,C
               PROMPT=' 5 DIGIT JULIAN DATE (YYDDD) FOR CONVERSION TO GC
               REGORIAN DATE'
         EJECT
GRGDATE  IKJSUBF
         EJECT
GRGORIAN IKJIDENT  'GREGORIAN DATE',MAXLNTH=6,FIRST=NUMERIC,           C
               OTHER=NUMERIC,PROMPT=' 6 DIGIT GREGORIAN DATE (MMDDYY) FC
               OR CONVERSION TO JULIAN'
         EJECT
         IKJENDP
         SPACE 2
         END   TO030000
