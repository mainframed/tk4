*          DATA SET GU000500   AT LEVEL 004 AS OF 03/31/74
         TITLE 'FPC COPYSOME UTILITY PROGRAM V2 07/72'
         PRINT NOGEN
GU000500 SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     06/10/71.
*              REMARKS.  COPYSOME IS A GENERALIZED COPY PROGRAM
*                        WHICH WILL COPY RECORDS WHICH ARE LOW,
*                        EQUAL, OR HIGH TO SPECIFIED KEYS.
*
*                        THE PROGRAM WILL ACCEPT ANY NUMBER OF KEYS
*                        UP TO THE LIMITS OF MEMORY.  THE KEY CONTROL
*                        CARDS MUST BE DEFINED IN A SYSIN DATA SET AS
*                        FOLLOWS
*
*                          COLUMN  DESCRIPTION
*                            1-2     AL - ACCEPT IF RECORD KEY LOW
*                                    AE - ACCEPT IF RECORD KEY EQUAL
*                                    AH - ACCEPT IF RECORD KEY HIGH
*                                    RL - REJECT IF RECORD KEY LOW
*                                    RE - REJECT IF RECORD KEY EQUAL
*                                    RH - REJECT IF RECORD KEY HIGH
*                                    RB - REJECT ALL RECORDS BEFORE
*                                    RA - REJECT ALL RECORDS AFTER
*                                    AB - ACCEPT ALL RECORDS BEFORE
*                                    AA - ACCEPT ALL RECORDS AFTER
*                                    OR - REVERSE AND/OR LOGIC
*
*                            3-6     DECIMAL RECORD OFFSET TO KEY
*                                    FROM ZERO TO 4095
*
*                            7-9     KEY LENGTH IN BYTES (1 TO 70)
*
*                           10       KEY INPUT FORMAT
*                                     C - CHARACTER
*                                     H - HEXADECIMAL (TWO DIGITS/BYTE)
*
*                           11-80    KEY DATA
*
*                        THE INPUT FILE IS SYSUT1.
*                        THE OUTPUT FILE IS SYSUT2.
*                        RECORD KEY OFFSETS MUST INCLUDE 4 BYTE
*                        CONTROL WORDS FOR VARIABLE LENGTH RECORDS.
*                        SPANNED RECORDS ARE PROCESSED BY SEGMENT
*                        DUE TO GET LOCATE - PUT MOVE MODE ACCESS.
*                        THE OUTPUT MESSAGE FILE IS SYSPRINT.
*                        NORMAL AND/OR LOGIC.
*                            1.  ACCEPT RECORDS WHICH MEET ALL
*                                AL, AE, AND AH TESTS.
*                            2.  REJECT RECORDS WHICH FAIL ANY
*                                RL, RE, OR RH TESTS.
*                        REVERSE AND/OR LOGIC.
*                            1.  ACCEPT RECORDS WHICH MEET ANY
*                                AL, AE, AH TESTS.
*                            2.  REJECT RECORDS WHICH FAIL ALL
*                                RL, RE, RH TESTS.
*                        ONLY ONE OR CONTROL CARD MAY BE PRESENT AND
*                        IT MUST BE THE FIRST CARD.
*
*                        RETURN CONDITION CODES
*                            0 - AT LEAST 1 RECORD WRITTEN ON SYSUT2
*                            4 - NO RECORDS WRITTEN ON SYSUT2
*
*
*              REVISIONS:
*              REV-1  03/29/74 FRED WILSON   ADD CODE TO PERMIT
*                   CONCATENATION OF INPUT FILES WITH UNLIKE DCB
*                   ATTRIBUTES (SYSUT1 ONLY)
*
*
*
         EJECT
         OPEN  (SYSIN,(INPUT),SYSPRINT,(OUTPUT),SYSUT1,(INPUT),        X
               SYSUT2,(OUTPUT))
         TIME  DEC
         STM   R0,R1,PDATA
         EDIT  TO=TIMEDATE,FROM=PDATA,MASK=' 99/99/99/99FZZ99.999'
         PUT   SYSPRINT,HEAD1      PRINT PROGRAM ID AND TIME STAMP
         PUT   SYSPRINT,LINE
         MVI   LINE,C' '
KEYLOOP  GET   SYSIN,KEYREC        READ, EDIT, AND PRINT CONTROL CARDS
         PUT   SYSPRINT,LINE       PRINT CARD
         CLC   KEYTEST(2),=C'OR'
         BE    SETOR
         OI    SETOR+1,X'F0'
         LM    R7,R9,=A(TESTTAB,LENTRY,TESTTAB+LTAB-LENTRY)
TESTLOOP CLC   KEYTEST(2),0(R7)    LOOK UP KEY COMPARE TYPE
         BE    GOODTEST
         BXLE  R7,R8,TESTLOOP
         PUT   SYSPRINT,ERR1       INVALID COMPARE TYPE
         MVI   STATUS,NOGO
GOODTEST TAG
         AH    R7,OPTION           ADD ZERO OR TWO DEPENDING ON OR
         MVC   BCRREJ+1(1),2(R7)   SET REJECT MASK AND REGISTER
         MVC   BCRACPT+1(1),3(R7)  SET ACCEPT MASK AND REGISTER
         TRT   KEYRKP(4),EDITDEC   EDIT RELATIVE KEY POSITION
         BC    8,GOODRKP
BADRKP   PUT   SYSPRINT,ERR2       NOT NUMERIC OR GREATER THAN 4095
         MVI   STATUS,NOGO
         B     GETLNG
SETOR    TAG
         NOP   BADOR
         MVC   OPTION,=H'2'        SET OFFSET TO OR LOGIC
         MVC   TESTLIST,=A(NEXTREC-4)
         B     KEYLOOP
BADOR    TAG
         PUT   SYSPRINT,ERR7
         MVI   STATUS,NOGO
         B     KEYLOOP
GOODRKP  CLC   KEYRKP(4),=C'4095'
         BH    BADRKP
         PACK  PWORK(8),KEYRKP(4)
         CVB   R0,PWORK
         ST    R0,PWORK
         OI    PWORK+2,X'10'       OR IN R1 BASE FOR RECORD
         MVC   COMPARE+2(2),PWORK+2 SET CLC BASE AND DISPLACEMENT
GETLNG   TRT   KEYLNG(3),EDITDEC
         BC    8,GOODLNG
BADLNG   PUT   SYSPRINT,ERR3       NOT NUMERIC KEY LENGTH OR NOT 1-256
         MVI   STATUS,NOGO
         B     GETKEY
GOODLNG  CLC   KEYLNG(3),=C'070'
         BH    BADLNG
         CLC   KEYLNG(3),=C'001'
         BL    BADLNG
         PACK  PWORK(8),KEYLNG(3)
         CVB   R1,PWORK
         BCTR  R1,R0
         STC   R1,COMPARE+1        SET COMPARE LENGTH
         LR    R10,R1              SAVE LENGTH-1
GETKEY   CLI   KEYF,C'C'           IS KEY CHARACTER OR HEX FORMAT
         BE    BUILD
         CLI   KEYF,C'H'
         BE    CONVERT
         PUT   SYSPRINT,ERR4       NOT C OR H
         MVI   STATUS,NOGO
         B     KEYLOOP
CONVERT  CLI   STATUS,NOGO         CONVERT KEY FROM EBCDIC HEX TO HEX
         BE    KEYLOOP
         LA    R3,1(R1)
         CLC   KEYLNG(3),=C'035'
         BNH   SETLNG
         PUT   SYSPRINT,ERR6       HEX KEY CANNOT EXCEED 35 BYTES
         MVI   STATUS,NOGO
         B     KEYLOOP
SETLNG   TAG
         SLL   R1,1
         LA    R1,1(R1)            R1=(KEY LENGTH - 1) * 2 + 1
         LA    R4,KEY
         LA    R5,KEY
         EX    R1,TRTHEX
         BC    8,GOODHEX
         PUT   SYSPRINT,ERR5       KEY CHARACTERS NOT 0-9 OR A-F
         B     KEYLOOP
GOODHEX  EX    R1,TRHEX
CVTLOOP  IC    R1,0(R4)
         SLL   R1,4
         STC   R1,0(R5)
         OC    0(1,R5),1(R4)
         LA    R5,1(R5)
         LA    R4,2(R4)
         BCT   R3,CVTLOOP
BUILD    CLI   STATUS,NOGO         CHECK ALL CONTROLS IF ERRORS
         BE    KEYLOOP
         LA    R0,15(R10)          R0=CORE NEEDED FOR COMPARE BLOCK
         GETMAIN R,LV=(0)          GET AREA
         MVC   0(4,R1),TESTLIST    LINK COMPARE BLOCKS VIA A(NEXT BLOCK
         ST    R1,TESTLIST         SET POINTER TO FIRST BLOCK
         MVC   4(10,R1),TESTCODE   MOVE 3 INSTRUCTIONS TO BLOCK
         EX    R10,MOVEKEY         MOVE KEY TO BLOCK
         B     KEYLOOP
ENDKEYS  CLI   STATUS,NOGO         IF ANY ERRORS, SKIP COPY
         BE    ENDCOPY
         LM    R6,R12,REGLIST
NEXTREC  TAG
         GET   SYSUT1              READ A RECORD
         CLI   REGET,X'FF'         HAS EOV JUST OPENED NEW FILE?   REV1
         BNE   NOREGET             NO                              REV1
         MVI   REGET,X'00'         YES - RESET FLAG BYTE AND       REV1
         B     NEXTREC             REISSUE GET                     REV1
NOREGET  TAG                                                       REV1
         AP    PIN,=PL1'1'         COUNT RECORDS IN
         LA    R2,TESTLIST
NEXTTEST L     R2,0(R2)            R2=A(NEXT TEST BLOCK)
         B     4(R2)               BRANCH TO TEST CODE
RASUB    MVI   11(R2),X'FA'        SET BCR TO TERMINATE ON NEXT RECORD
         B     NEXTTEST
AASUB    MVI   11(R2),X'F9'        SET BCR TO ACCEPT ALL RECORDS
         B     NEXTREC
RBSUB    MVI   11(R2),X'F9'        SET BCR TO ACCEPT ALL RECORDS
         B     NEXTTEST
PUTREC   TAG
         LR    R2,R1
         TM    SYSUT2+36,X'C0'          TEST OUTPUT FORMAT U
         BNO   NOTU
         MVC   SYSUT2+82(2),SYSUT1+82   SET LRECL FOR TYPE U BLOCK
NOTU     TAG
         PUT   SYSUT2,(2)          WRITE RECORD
         AP    POUT,=PL1'1'        COUNT RECORDS OUT
         B     NEXTREC
***********************************************************************
*        DCB EXIT ROUTINE FOR SYSUT1  USED TO PERMIT CONCATENATION OF
*        FILES WITH UNLIKE DCB ATTRIBUTES
**********************************************************************
DCBEXIT  TAG                                                       REV1
         USING *,R15                                               REV1
         NOP   NOTFIRST            BRANCH NOPD 1ST TIME            REV1
         OI    SYSUT1+48,X'08'     SET CONCATENATION BIT           REV1
         OI    DCBEXIT+1,X'F0'     ACTIVATE BRANCH AFTER 1ST TIME  REV1
         BR    R14                 GO BACK                         REV1
NOTFIRST TAG                                                       REV1
         MVI   REGET,X'FF'         SET REISSUE FLAG                REV1
         BR    R14                                                 REV1
         DROP  R15                                                 REV1
ENDCOPY  TAG
ABSUB    EQU   ENDCOPY
         EDIT  TO=DIN,FROM=PIN,MASK=' ZZZZZ99'
         EDIT  TO=DOUT,FROM=POUT,MASK=' ZZZZZ99'
         PUT   SYSPRINT,TOTALS
         CLOSE (SYSIN,,SYSPRINT,,SYSUT1,,SYSUT2)
         CP    POUT,=PL1'1'
         BL    RC4
RC1      SUBEXIT RC=0
RC4      SUBEXIT RC=4
         EJECT
*
*              DATA
*
         EQUREGS
         LTORG
STATUS   DC    AL1(GO)
GO       EQU   0
NOGO     EQU   1
PDATA    DC    2F'0'
HEAD1    DC    C'1'
         DC    C'COPYSOME UTILITY PROGRAM'
TIMEDATE DC    C' HH/MM/SS/TH   YY.DDD'
         DC    CL100' '
LINE     DC    C'0'
KEYREC   DS    0CL80
KEYTEST  DC    CL2'TT'
KEYRKP   DC    CL4'RRRR'
KEYLNG   DC    CL3'LLL'
KEYF     DC    CL1'F'
KEY      DC    70CL1'K'
         DC    CL55' '
ERR1     DC    CL049'0INVALID COMPARE TYPE - MUST BE AL,AE,AH,RL,RE,RH'
         DC    CL100',RB,RA,AB,AA'
ERR2     DC    CL133'0INVALID RKP IN COL 3-6 - MUST BE 0000-4095'
ERR3     DC    CL133'0INVALID KEY LENGTH IN COL 7-9 - MUST BE 1-70'
ERR4     DC    CL133'0INVALID KEY TYPE IN COL 10 - MUST BE C OR H'
ERR5     DC    CL133'0INVALID HEX CHARACTER IN KEY'
ERR6     DC    CL133'0INVALID KEY LENGTH FOR HEX - MUST BE 000-035'
ERR7     DC    CL133'0INVALID OR CARD - MUST BE FIRST CONTROL CARD'
TESTTAB  TAG
         DC    C'AL',X'B8F9B9F6'
         DC    C'AE',X'78F979F6'
         DC    C'AH',X'D8F9D9F6'
         DC    C'RL',X'48F949F6'
         DC    C'RE',X'88F989F6'
         DC    C'RH',X'28F929F6'
         DC    C'AB',X'8AF98AF9'
         DC    C'AA',X'8BF88BF8'
         DC    C'RB',X'8CF88CF8'
         DC    C'RA',X'87F987F9'
LTAB     EQU   *-TESTTAB
LENTRY   EQU   6
TESTCODE DS    0H
COMPARE  CLC   0(0,R1),14(R2)      COMPARE KEY
BCRREJ   BCR   0,R8                BRANCH TO A(R8) IF REJECTED
BCRACPT  BCR   15,R9               BRANCH TO A(R9) IF ACCEPTED
PWORK    DC    D'0'
OPTION   DC    H'0'
EDITDEC  DC    240XL1'FF',10XL1'00',6XL1'FF'
TRTHEX   TRT   KEY(0),EDITHEX
TRHEX    TR    KEY(0),CUTHEX
MOVEKEY  MVC   14(0,R1),KEY
EDITHEX  DC    193XL1'FF',6XL1'00',41XL1'FF',10XL1'00',6XL1'FF'
CUTHEX   DC    193XL1'00',X'0A0B0C0D0E0F',41XL1'00'
         DC    X'00010203040506070809',6XL1'00'
TESTLIST DC    A(PUTREC-4)
REGLIST  DC    A(PUTREC,RASUB,NEXTREC,NEXTTEST,ABSUB,AASUB,RBSUB)
DCBLIST  DS    0F
         DC    X'05'
         DC    AL3(DCBEXIT)
         DC    X'80000000'
PIN      DC    PL4'0'
POUT     DC    PL4'0'
TOTALS   DC    C'0'
         DC    C'TOTAL RECORDS READ'
DIN      DC    C' ZZZZZZZ'
         DC    C'  TOTAL RECORDS WRITTEN'
DOUT     DC    C' ZZZZZZZ'
         DC    CL100' '
*
*              FILES
*
REGET    DC    X'00'                                               REV1
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=ENDKEYS,           X
               RECFM=F,BLKSIZE=80
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      X
               RECFM=FA,BLKSIZE=133
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=ENDCOPY,      REV1X
               EXLST=DCBLIST
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM
         END   GU000500
