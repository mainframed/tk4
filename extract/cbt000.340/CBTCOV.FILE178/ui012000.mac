*          DATA SET UI012000   AT LEVEL 009 AS OF 03/27/79
*          DATA SET UI012000   AT LEVEL 006 AS OF 10/25/78
         TITLE 'UI012000 - PROGRAM SECURITY FILE INTERFACE'
*              AUTHOR.   DON HIGGINS.
*              DATE.     06/20/77.
*              REMARKS.  THIS SUBROUTINE DETERMINES IF A PROGRAM
*                        REQUIRES AUTHORIZATION TO USE IT.
*              CALL.     CALL DCALL('UI012000',JOB,USERID,PGM,UISTAT)
*
*                        JOBNAME - NAME OF JOB USING PROGRAM
*
*
*                        USERID - USER IDENTIFICATION NUMBER
*
*                        PGM - NAME OF PROGRAM
*
*                        UISTAT - 4 BYTE BINARY STATUS
*
*                          0 -  AUTHORIZATION OK
*
*                          1 -  INVALID PGM NAME (AANNNXXX)
*
*                          2 -  NOT AUTHORIZED IN USER MASTER
*
*                          3 -  NOT AUTHORIZED FOR RESTRICTED PGM
*
*                          4 -  NOT AUTHORIZED FOR JOB
*              MAINTENANCE.
*
*              11/01/77 DSH - CHANGE RW AND RO TABLES TO 2 BYTES.
*              10/25/78 DSH - FIX TRT TABLE BUG FOR NON-NUMERIC PREFIX.
*              03/23/79 DSH - SURPRESS CODE 4 IF SPECIFICALLY AUTH.
*              03/27/79 DSH - ADD LEVEL CONTROL OPTION LIKE UI011.
*
         TITLE 'MAINLINE'
UI012000 SUBENTRY BASES=(R13,R8)
         LM    R3,R6,0(R1)
         USING ARG1,R3  JOB
         USING ARG2,R4  USERID
         USING ARG3,R5  PGM
         USING ARG4,R6  UISTAT
         IF    (CLI,FIRST,TRUE,EQ)
                MVI  FIRST,FALSE
                PM   INIT
         FI
         PM    GETAC
         PM    VALIDATE
         MVC   UISTAT,FSTATUS
         SUBEXIT
         TITLE 'INIT - INITIALIZE PGM TREE STRUCTURE'
INIT     PENTRY
         OPEN  (UI012S01)
         ZR    R11
         PM    GET
         DO    WHILE,(CLI,EOF,TRUE,NE)
               LR   R12,R1
               USING REC,R12
               DOCASE    RECTYPE
                    CASE C'1'
                         PM   ADDPGM
                    ESAC
                    CASE C'2'
                         PM   ADDULIST
                    ESAC
                    CASE C'D'
                         ABEND 901,DUMP
                    ESAC
                    CASE ELSE
                         WTO  'UI012 - INVALID RECORD TYPE - IGNORED'
                    ESAC
               ESACOD
               PM   GET
         OD
         CLOSE (UI012S01)
         PEXIT
GET      PENTRY
         GET   UI012S01
         PEXIT
UI012EOF MVI   EOF,TRUE
         PEXIT
ADDPGM   PENTRY
         ZR    R11
         MVC   PGMAC,RECAC
         NC    PGMAC,=X'03'
         MVC   PREFIX,RECPGM
         PM    GETTAB1
         LR    R10,R1
         IF    (CLI,RECPGM+2,C' ',EQ)
               NC   0(1,R10),=X'FC'
               OC   0(1,R10),PGMAC
               PEXIT
         FI
*
*        BUILD BLOCK FOR NEW TREE ENTRY
*
         IF    (TRT,RECPGM,BLANK,EQ)
               LA   R1,L'RECPGM
         ELSE
               LA   R0,RECPGM
               SR   R1,R0
         FI
         TR    RECPGM,TRQBLK     CHANGE ? TO BLANK
         LR    R2,R1
         LA    R0,LBLK(R2)
         PM    GETBLK
         LR    R11,R1
         USING BLK,R11
         ST    R2,BLKLNG
         MVC   BLKAC(1),PGMAC
         LA    R0,RECPGM
         LR    R1,R2
         LA    R14,BLKPGM
         LR    R15,R2
         MVCL  R14,R0
*
*        CHAIN BLOCK INTO PREFIX TABLE OR GET PTR TO EXISTING TREE
*
         IF    (TM,0(R10),X'FC',Z)
               L    R1,PGMPTR
               AAI  R1,4
               IF   (CL,R1,=A(64*4),NL)
                    LA   R1,4
               FI
               ST   R1,PGMPTR
               OC   0(1,R10),PGMPTR+3
               L    R0,PGMINDEX-4(R1)
               IF   (LTR,R0,R0,Z)
                    ST   R11,PGMINDEX-4(R1)
                    PEXIT
               FI
         FI
         ZR    R1
         IC    R1,0(R10)
         SRL   R1,2
         SLL   R1,2
         L     R9,PGMINDEX-4(R1)
*
*        SCAN TREE AND ADD BLOCK
*
         DO    FOREVER
               LA   R14,BLKPGM
               L    R15,BLKLNG
               LA   R0,BLKPGM-BLK(R9)
               L    R1,BLKLNG-BLK(R9)
               IF   (CLR,R1,R15,L)     COMPARE ON SHORTEST KEY
                    LR   R15,R1
               ELSE
                    LR   R1,R15
               FI
               IF   (CLCL,R14,R0,L)
                    L    R1,BLKLOW-BLK(R9)
                    IF   (LTR,R1,R1,Z)
                         ST   R11,BLKLOW-BLK(R9)
                         PEXIT
                    FI
                    LR   R9,R1
               ELSEIF (H)
                    L    R1,BLKHIGH-BLK(R9)
                    IF   (LTR,R1,R1,Z)
                         ST   R11,BLKHIGH-BLK(R9)
                         PEXIT
                    FI
                    LR   R9,R1
               ELSE
                    IF   (CLC,BLKLNG,BLKLNG-BLK(R9),LE)
                         WTO 'UI012S01 - QUALIFIER OUT OF ORDER'
                         PEXIT
                    FI
                    L    R1,BLKEQUAL-BLK(R9)
                    IF   (LTR,R1,R1,Z)
                         ST   R11,BLKEQUAL-BLK(R9)
                         PEXIT
                    FI
                    LR   R9,R1
               FI
         OD
         TITLE 'ADDULIST - ADD USER LIST TO PREVIOUS PGM BLK'
ADDULIST PENTRY
         IF    (LTR,R11,R11,Z)
               WTO  'UI012 - NO PREVIOUS PGM FOR USER LIST - IGNORED'
               PEXIT
         FI
         USING BLK,R11
         LA    R2,RECULIST
         USING UDATA,R2
         DO    WHILE,(CLI,UDAC,C' ',NE)
               LA   R0,LULIST
               PM   GETBLK
               USING ULIST,R1
               MVC  ULNEXT,BLKULIST
               ST   R1,BLKULIST
               PACK PWORK,UDID
               CVB  R0,PWORK
               ST   R0,ULID
               MVC  ULAC(1),UDAC
               NC   ULAC(1),=X'03'
               LA   R2,LUDATA(R2)
         OD
         PEXIT
         TITLE 'GETBLK - GET BLOCK OF MEMORY WITH ZEROS'
GETBLK   PENTRY
         LM    R14,R15,MSAVE
         AH    R0,=H'7'
         SRL   R0,3
         SLL   R0,3
         IF    (SR,R15,R0,M)
               ST   R0,RSAVE
               LA   R0,LMBLK
               GETMAIN R,LV=(0)
               LR   R14,R1
               LR   R0,R1
               LA   R1,LMBLK
               ZR   R15
               MVCL R0,R14
               LA   R15,LMBLK
               L    R0,RSAVE
               SR   R15,R0
         FI
         LR    R1,R14
         AR    R14,R0
         STM   R14,R15,MSAVE
         PEXIT
         TITLE 'GETTAB1 - SET R1 TO TAB1 ENTRY FOR PGM PREFIX'
GETTAB1  PENTRY
         TR    PREFIX,TRTAB
         ZR    R1
         IC    R1,PREFIX+1
         MH    R1,=H'26'
         AH    R1,PREFIX-1
         LA    R1,PFXTAB-27(R1)
         PEXIT
         TITLE 'GETAC - GET ACCESS CODE FOR PGM'
GETAC    PENTRY
         ZR    R11
         MVC   PREFIX,PGM
         PM    GETTAB1
         LR    R10,R1
         ZR    R1
         IC    R1,0(R10)
         IF    (TM,0(R10),X'FC',Z)
               IF   (TRT,PGM+2(3),NUMERIC,NE)
                    MVI  PGMAC,3
               ELSE
                    STC  R1,PGMAC
               FI
               PEXIT
         FI
         SRL   R1,2
         SLL   R1,2
         L     R11,PGMINDEX-4(R1)
         USING BLK,R11
         PM    FINDBLK
         IF    (LTR,R11,R11,NZ)
               IC   R1,BLKAC
               STC  R1,PGMAC
               PEXIT
         FI
         IF   (TRT,PGM+2(3),NUMERIC,NE)
               MVI  PGMAC,3
         ELSE
               IC    R1,0(R10)
               LA    R0,3
               NR    R1,R0
               STC   R1,PGMAC
         FI
         PEXIT
         TITLE 'FINDBLK - FIND PGM BLOCK IN TREE STRUCTURE'
FINDBLK  PENTRY
         ZR    R12
         DO    WHILE,(LTR,R11,R11,NZ)
               LA   R14,BLKPGM
               L    R15,BLKLNG
               LA   R0,PGM
               LR   R1,R15
               IF   (CLCL,R0,R14,L)
                    L    R11,BLKLOW
               ELSEIF (H)
                    L    R11,BLKHIGH
               ELSE
                    LR   R12,R11
                    L    R11,BLKEQUAL
               FI
         OD
         LR    R11,R12
         PEXIT
         TITLE 'VALIDATE - CHECK IF ACCESS IS VALID FOR USER'
VALIDATE PENTRY
         DOCASE PGMAC
           CASE 1
                MVI  STATUS,2
                PM   CHKULIST
                IF   (CLI,STATUS,0,GT)
                     PM   CHKUSER
                FI
           ESAC
           CASE 2
                MVI  STATUS,3
                PM   CHKULIST
           ESAC
           CASE 3
                MVI  STATUS,1
                PM   CHKULIST
                IF   (CLI,JOB+7,C'P',EQ),AND,(CLI,STATUS,1,EQ)
                     MVI  STATUS,4
                FI
           ESAC
           CASE ELSE
                IF   (CLI,PGMAC,0,EQ)
                     MVI  STATUS,0
                ELSE
                     ABEND 201,DUMP
                FI
           ESAC
         ESACOD
         PEXIT
         TITLE 'CHKULIST - CHECK USER LIST TO AUTHORIZE USER FOR PGM'
CHKULIST PENTRY
         IF    (LTR,R11,R11,Z)
               PEXIT
         FI
         L     R1,BLKULIST
         USING ULIST,R1
         DO    WHILE,(LTR,R1,R1,NZ)
               IF    (CLC,USERID+1(3),ULID+1,EQ)
                     MVI  STATUS,0
                     PEXIT
               FI
               L     R1,ULNEXT
         OD
         PEXIT
         TITLE 'GETUSER - GET USER AUTHORIZATION LISTS'
GETUSER  PENTRY
         IF    (CLC,LUSER,USERID,NE)
               MVC  LUSER,USERID
               LA   R1,FIPLIST2
               SCALL DCALL
               LA   R1,FIPLIST3
               SCALL DCALL
         FI
         PEXIT
         TITLE 'CHKUSER - CHECK USER MASTER AUTHORIZATION LISTS'
CHKUSER  PENTRY
         PM    GETUSER
         LA    R1,RWLIST
         LA    R2,RWLIST+40
         DO    WHILE,(CLR,R1,R2,L)
               IF   (CLC,0(2,R1),PGM,EQ)
                    MVI  STATUS,0
                    PEXIT
               FI
               AAI  R1,2
         OD
         LA    R1,ROLIST
         LA    R2,ROLIST+40
         DO    WHILE,(CLR,R1,R2,L)
               IF   (CLC,0(2,R1),PGM,EQ)
                    MVI  STATUS,0
                    PEXIT
               FI
               AAI  R1,2
         OD
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
FIRST    LOGICAL    TRUE,(TRUE,FALSE)
PGMINDEX DC    64A(0)    INDEX TO PGM TREE STRUCTURE
PGMPTR   DC    F'0' POINTER TO LAST INDEX ASSIGNED
PGMAC    DC    X'00'
EOF      LOGICAL    FALSE,(TRUE,FALSE)
UI012S01 SDCB  R=FB,L=80,E=UI012EOF,M=GL
BLANK    TRTTAB SCAN,FIND=C' '
NUMERIC  TRTTAB NUMERIC
PWORK    DC    D'0'
DWORK    DC    CL4' '
MSAVE    DC    2F'0'
RSAVE    DC    F'0'
LMBLK    EQU   4000
PFXTAB   DC    (26*26)X'01'
         DC    H'0'
         ORG   *-1
PREFIX   DC    CL2' '
FSTATUS  DC    F'0'
STATUS   EQU   FSTATUS+3
TRQBLK   DC    256AL1(*-TRQBLK)
         ORG   TRQBLK+C'?'
         DC    C' '
         ORG
TRTAB    DC    256X'01'
         ORG   TRTAB+C'A'
         DC    AL1(1,2,3,4,5,6,7,8,9)
         ORG   TRTAB+C'J'
         DC    AL1(10,11,12,13,14,15,16,17,18)
         ORG   TRTAB+C'S'
         DC    AL1(19,20,21,22,23,24,25,26)
         ORG
FIPLIST2 DC    A(FIPNAME,OP11,TYPE4,LUSER,RWLIST,FIPSTAT)
FIPLIST3 DC    A(FIPNAME,OP12,TYPE4,LUSER,ROLIST,FIPSTAT)
FIPNAME  DC    CL8'UI010000'
OP11     DC    F'11'
OP12     DC    F'12'
TYPE4    DC    F'4'
LUSER    DC    F'0'
FIPSTAT  DC    F'0'
RWLIST   DC    20H'0'
ROLIST   DC    20H'0'
OPENED   LOGICAL    FALSE,(TRUE,FALSE)
         TITLE 'DSECTS'
ULIST    DSECT
ULNEXT   DS    A
ULID     DS    F
ULAC     EQU   ULID
LULIST   EQU   *-ULIST
UDATA    DSECT
UDAC     DS    C
UDID     DS    CL4
LUDATA   EQU   *-UDATA
REC      DSECT
RECTYPE  DS    C
*
*  1 - ADD PGM
*  2 - ADD USER LIST TO PREVIOUS PGM
*
*
RECAC    DS    C
*
*              0 - NO CONTROL
*              1 - CONTROL BY PGM LIST OR USER MASTER LISTS
*              2 - CONTROL BY PGM LIST ONLY
*              3 - INVALID PGM - CONTROL BY JOB (SETS STATUS 1 OR 4)
*
RECPGM   DS    CL8
RECULIST EQU   RECAC
BLK      DSECT
BLKLOW   DS    A
BLKHIGH  DS    A
BLKEQUAL DS    A
BLKULIST DS    A
BLKLNG   DS    F
BLKAC    DS    X
BLKPGM   DS    0CL8
LBLK     EQU   *-BLK
ARG1     DSECT
JOB      DS    CL8
ARG2     DSECT
USERID   DS    F
ARG3     DSECT
PGM      DS    CL8
ARG4     DSECT
UISTAT   DS    F
         END
