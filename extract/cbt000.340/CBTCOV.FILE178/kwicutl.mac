*          DATA SET GU460000   AT LEVEL 006 AS OF 10/03/74
         TITLE 'COPYKWIC - GENERATE KEY WORD IN CONTEXT RECORDS'
*              AUTHOR.   DON HIGGINS.
*              DATE.     09/26/74.
*              REMARKS.  THIS UTILITY PROGRAM WILL R%AD ANY SEQUENTIAL
*                        FILE AND GENERATE KWIC OUTPUT RECORDS FOR
*                        EACH INPUT RECORD BASED ON A FIXED LENGTH
*                        STRING WITHIN EACH RECORD DEFINED BY THE
*                        USER.
*              FILES.
*                        SYSIN - CONTROL CARD FILE.
*                                  1-5  OFFSET TO STRING (0-99999)
*                                  6-10 LENGTH OF STRING (1-99999)
*                        SYSPRINT - ERROR MESSAGES AND RECORD COUNTS
*
*                        SYSUT1 - INPUT QSAM FILE
*
*                        SYSUT2 - OUTPUT QSAM FILE
*
*                        SKIPKEYS - INVALID KWIC DEFINITION FILE.
*                                   (KEYS ON 80 BYTE RECORDS DELIMITED
*                                    BY BLANKS.)
*              NOTES.
*                        1.  SPANNED RECORDS NOT SUPPORTED CORRECTLY.
*                        2.  INVALID OR MISSING CONTROL CARD SETS RC=16
         TITLE 'MAIN PROGRAM'
         PRINT NOGEN
COPYKWIC SUBENTRY
         OPEN  (SYSIN,,SYSUT1,,SYSUT2,(OUTPUT),SYSPRINT,(OUTPUT))
         DTIME TIMEDATE
         LA    R0,HEADING
         PM    PRINT
         PM    GETCTL
         PM    READ
         DO    WHILE,(CLI,EOF,FALSE,EQ)
               XC   NSHIFT,NSHIFT
               XC   LKEY,LKEY
               PM   CALLKWIC
               DO   WHILE,(OC,LKEY,LKEY,NZ)
                    PM   WRITE
                    PM   CALLKWIC
               OD
               PM   READ
         OD
         EDIT  TO=DIN,FROM=PIN,MASK=' Z,ZZZ,Z99'
         EDIT  TO=DOUT,FROM=POUT,MASK=' Z,ZZZ,Z99'
         LA    R0,TOTALS
         PM    PRINT
         CLOSE (SYSIN,,SYSUT1,,SYSUT2,,SYSPRINT)
         SUBEXIT
EOF      LOGICAL FALSE,(TRUE,FALSE)
PIN      DC    PL4'0'
POUT     DC    PL4'0'
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=EOFCTL
SYSUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=SYSUT1,EODAD=EOFUT1
SYSUT2   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT2
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FA,BLKSIZE=133
HEADING  DS    0CL133
         DC    C'1COPYKWIC UTILITY PROGRAM'
TIMEDATE DC    CL22' '
         FILL  HEADING
TOTALS   DS    0CL133
         DC    C'0RECORDS READ'
DIN      DC    CL10' ',C' RECORDS WRITTEN'
DOUT     DC    CL10' '
         FILL  TOTALS
OFFSET   DC    F'0'
PWORK    DC    D'0'
ERRCTL   DS    0CL133
         DC    C'0INVALID OR MISSING CONTROL CARD - JOB ABORTED'
         FILL  ERRCTL
         TITLE 'CALL KWIC SHIFT SUBROUTINE'
CALLKWIC PENTRY
         LA    R1,KWICARGS
         L     R15,=V(KWIC)
         BALR  R14,R15
         PEXIT
KWICARGS DS    0F
ASTRING  DC    A(0)
         DC    A(LSTRING,NSHIFT,LKEY)
LSTRING  DC    A(0)
NSHIFT   DC    A(0)
LKEY     DC    A(0)
ARECORD  DC    A(0)
         TITLE 'PRINT LINE'
PRINT    PENTRY
         PUT   SYSPRINT
         PEXIT
         TITLE 'GETCTL READ AND EDIT CONTROL CARD'
GETCTL   PENTRY
         GET   SYSIN
         OC    0(10,R1),=10C'0'
         IF    (TRT,0(10,R1),NUMERIC,EQ)
               PACK PWORK,0(5,R1)
               CVB  R0,PWORK
               ST   R0,OFFSET
               PACK PWORK,5(5,R1)
               CVB  R0,PWORK
               ST   R0,LSTRING
               PEXIT
         FI
EOFCTL   TAG
         LA    R0,ERRCTL
         PM    PRINT
         SUBEXIT RC=16
NUMERIC  DC    256X'FF'
         ORG   NUMERIC+C'0'
         DC    10X'00'
         ORG
         TITLE 'READ SYSUT1'
READ     PENTRY
         GET   SYSUT1
         AP    PIN,=P'1'
         ST    R1,ARECORD
         A     R1,OFFSET
         ST    R1,ASTRING
         PEXIT
EOFUT1   MVI   EOF,TRUE
         PEXIT
         TITLE 'WRITE SYSUT2'
WRITE    PENTRY
         L     R0,ARECORD
         PUT   SYSUT2
         AP    POUT,=P'1'
         PEXIT
         TITLE 'COMMON DATA AND EQUATES'
         EQUAL
         END
