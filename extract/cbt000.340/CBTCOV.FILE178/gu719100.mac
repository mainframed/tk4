*          DATA SET GU719100   AT LEVEL 003 AS OF 06/09/77
         TITLE 'GU719100 - RETURN DAY OF WEEK AS INTEGER 1-7'
*              AUTHOR.   R. A. GRAVES.
*              DATE.     06/08/77.
*              REMARKS.  RETURN DAY OF WEEK AS 1-7.  NOTE THAT THIS
*                        ROUTINE ONLY WORKS FOR 1975-1999.
*
*                        THIS ROUTINE REQUIRES THE DATE TO BE PASSED
*                        AS A FULL WORD ARGUMENT IN A PACKED DECIMAL
*                        FORMAT (YYDDD).
*                        EXAMPLE:  0077065F
*
*                        DAY OF WEEK IS RETURNED IN PACKED FORMAT.
*                        EXAMPLE:  0000003F  DAY OF WEEK = WEDNESDAY.
         PRINT NOGEN
GU719100 SUBENTRY
         L     R2,0(R1)       R2=ADDRESS OF FULL WORD BINARY ARG.
         L     R3,0(R2)
         ST    R3,PWORK+4
         LR    R1,R3
         SRL   R1,20          R1=FIRST DIGIT OF YEAR
         MH    R1,=H'10'
         NI    PWORK+5,X'0F'
         AH    R1,PWORK+4     R1=TWO DIGIT YEAR IN BINARY
         IC    R1,YEAR(R1)    R1=DAY ADJUSTMENT FOR CURRENT YEAR
         MVI   PWORK+5,0
         CVB   R0,PWORK       R0=DAY OF YEAR FROM SYSTEM
         AR    R1,R0          R1=ADJUSTED DAY OF YEAR
         ZR    R0
         D     R0,=F'7'       R0=ADJUSTED DAY MODULO SEVEN
         LR    R1,R0
         IC    R0,DAY(R1)
         CVD   R0,PWORK
         MVC   0(4,R2),PWORK+4
         SUBEXIT
         EQUAL
PWORK    DC    D'0'
DAY      DC    AL1(1,2,3,4,5,6,7)
         ORG   *-75           SAVE SPACE FOR YEARS 1900-1974
YEAR     DS    XL75
         DC    AL1(1,2,4,5,6,0,2,3,4,5,0,1,2,3,5,6,0,1,3,4,5,6,1,2,3)
         END
