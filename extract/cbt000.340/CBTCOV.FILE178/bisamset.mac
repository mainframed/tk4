*          DATA SET GU650000   AT LEVEL 015 AS OF 09/20/77
**********************************************************************
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/08/74.
*              ARGUMENTS.
*
*                   CALL BISAMSET(DECB)
*
*                        DECB - ADDRESS OF DECB USED IN PRIOR
*                               BISAM READ WHICH CAUSED NO
*                               RECORD FOUND CONDITION.
*              FUNCTION.
*
*                   THE RECORD POINTER IN THE DECB WILL BE SET TO
*                   THE RECORD IN THE BLOCK WHICH IS CLOSEST TO THE
*                   READ KEY BUT NOT HIGH IF POSSIBLE.
*              LIMITATIONS.
*
*                  1.    ISAM DCB MUST HAVE FOLLOWING ATTRIBUTES.
*                            A.  DSORG=IS
*                            B.  MACRF=R
*                            C.  RECFM=VB
*
*  REV  08/31/77  W R WILLITS
*       1.  CHANGE DECB DSECT NAMES
*       2.  CHANGE IF STATEMENT TO CHECK ONLY 1 BYTE FOR NO REC FOUND
*       3.  PULL NON ESSENTIAL CODE
*
*  REV  09/19/77  W R WILLITS
*  1.  ADD CHECK FOR END OF BLOCK.  A SHORT BLOCK (1 RECORD) CAUSED
*      A LOOP TO OCCUR WHICH GU640000 DETECTED VIA STIMER AND FORCED
*      AN ABEND (SNAP) 0C1 BY PLACING X'00' IN DO-UNTIL CODE.
*
*
**********************************************************************
         TITLE 'MAINLINE'
         PRINT GEN
BISAMSET SUBENTRY
         L     R11,0(R1)
         USING DECBD,R11
         L     R12,DECBDCBA
         USING IHADCB,R12
         L     R4,DECBAREA               R4=A(BLOCK)
         AH    R4,12(R4)                 R4=A(END OF BLOCK)
         L     R10,DECBAREA              R10=A(BLOCK)
         LA    R10,16(R10)               R10=A(CURRENT RECORD)
         LR    R9,R10                    R9=A(LAST LOW KEY RECORD)
         IF    (TM,DECBEXC1,X'80',O),AND,(CLC,0(2,R10),=X'0000',NE)
               ZR    R8
               IC    R8,DCBKEYLE         R8=KEY LENGTH
               LH    R7,DCBRKP           R7=KEY OFFSET
               L     R6,DECBKEY          R6=A(READ KEY)
*
*  THE FOLLOWING LOOP WILL BE PERFORMED UNTIL A RECORD HIGHER THAN
*  READ KEY IS FOUND...OR...THE END OF THE BLOCK IS REACHED.
*
               DO    UNTIL,(CLR,R10,R4,NL),OR,(EX,R8,COMPARE,H)
                     LR   R9,R10         R9=A(LAST LOW KEY RECORD)
                     AH   R10,0(R10)     R10=A(NEXT RECORD)
                     LA   R5,0(R10,R7)   R0=A(KEY IN NEXT RECORD)
               OD
         SPACE 2
         FI
         ST    R9,DECBREC
         SPACE 2
         SUBEXIT
         SPACE 2
COMPARE  CLC   0(*-*,R5),0(R6)
         TITLE 'COMMON DATA'
         PRINT GEN
         EQUAL
         TITLE 'DECB DSECT'
DECBD    DSECT
DECBECB  DS    F
DECBTYPE DS    H
DECBALNG DS    H
DECBDCBA DS    A
DECBAREA DS    A
DECBREC  DS    A
DECBKEY  DS    A
DECBEXC1 DS    C
         TITLE 'ISAM DCB DSECT'
         DCBD  DSORG=IS
         EJECT
         END
