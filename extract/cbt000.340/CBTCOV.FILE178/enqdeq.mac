*          DATA SET GU713000   AT LEVEL 003 AS OF 08/17/79
*          DATA SET GU713000   AT LEVEL 002 AS OF 05/14/75
         TITLE 'GU713000 - ENQ AND DEQ SERIAL RESOURCE SUBROUTINES'
*              AUTHOR.   DON HIGGINS.
*              DATE.     05/13/75.
*              REMARKS.  ENQ OR DEQ A SERIALLY REUSEABLE RESOURCE.
*              CALLS.
*                   1.   CALL ENQ(QNAME,RNAME)
*                   2.   CALL DEQ(QNAME,RNAME)
*                   3.   CALL TENQ(QNAME,RNAME,STATUS)
*
*                        QNAME - 8 CHARACTER MAJOR RESOURCE NAME.
*                        RNAME - 8 CHARACTER MINOR RESOURCE NAME.
*                        STATUS- 4 BYTE BINARY STATUS CODE.
*                          0   -   ENQ RESOURCE NOT IN USE
*                          1   -   ENQ RESOURCE IN USE
*
*              MAINTENANCE.
*
*                 08/15/79 - DSH - TENQ OPTION ADDED AND ENTIRE
*                                  MODULE MADE REENTRANT.
*
*
         TITLE 'ENQ - UNCONDITIONAL ENQ'
RWAENQ   DSECT
RWALENQ  ENQ   (0,0,E,8,SYSTEM),MF=L
LRWAENQ  EQU   *-RWAENQ
ENQ      SUBENTRY BASES=(R12),RENT=YES,RWA=RWAENQ,RWALNG=LRWAENQ
         LM    R2,R3,0(R1)
         MVC   RWALENQ(LRWAENQ),LENQ
         ENQ   ((R2),(R3),E,8,SYSTEM),MF=(E,RWALENQ)
         SUBEXIT
         EQUAL
LENQ     ENQ   (0,0,E,8,SYSTEM),MF=L
         TITLE 'TENQ - CONDITONAL ENQ'
RWATENQ  DSECT
RWALTENQ ENQ   (0,0,E,8,SYSTEM),RET=CHNG,MF=L
LRWATENQ EQU   *-RWATENQ
TENQ     SUBENTRY BASES=(R12),RENT=YES,RWA=RWATENQ,RWALNG=LRWATENQ
         LM    R2,R4,0(R1)
         MVC   RWALTENQ(LRWATENQ),LTENQ
         ENQ   ((R2),(R3),E,8,SYSTEM),RET=CHNG,MF=(E,RWALTENQ)
         IF    (LTR,R15,R15,Z)
               MVC  0(4,R4),=F'1'
         ELSE
               MVC  0(4,R4),=F'0'
         FI
         SUBEXIT
         LTORG
LTENQ    ENQ   (0,0,E,8,SYSTEM),RET=CHNG,MF=L
         TITLE 'DEQ'
RWADEQ   DSECT
RWALDEQ  DEQ   (0,0,8,SYSTEM),MF=L
LRWADEQ  EQU   *-RWADEQ
DEQ      SUBENTRY BASES=(R12),RENT=YES,RWA=RWADEQ,RWALNG=LRWADEQ
         LM    R2,R3,0(R1)
         MVC   RWALDEQ(LRWADEQ),LDEQ
         DEQ   ((R2),(R3),8,SYSTEM),MF=(E,RWALDEQ)
         SUBEXIT
         LTORG
LDEQ     DEQ   (0,0,8,SYSTEM),MF=L
         END
