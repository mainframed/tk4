*          DATA SET TP407000   AT LEVEL 005 AS OF 04/25/75
         TITLE 'TP407000 - DISK INQUIRY'
*              AUTHOR.   DON HIGGINS.
*              DATE.     02/12/75.
*              REMARKS.  DISK INQUIRY FOR MPS.
*              ARGUMENTS
*                  1.    TERM - 8 BYTE TERMINAL ID.
*                  2.    PARM FIELD.
*                         BLANK - LIST ALL ONLINE DISKS
*                        VOLUME- LIST STATUS OF SPECIFIED VOLUME
*                        UNIT - LIST STATUS OF SPECIFIED UNIT
*                  3.    PARM LENGTH - FULL WORD BINARY
*
*              REFERENCES.
*                  1.    GU640000MPS SUPERVISOR
*                  2.    S229-3169-3  FE HANDBOOK.
         PRINT NOGEN
DISK     SUBENTRY
         LM    R3,R5,0(R1)
         MVC   TERM,0(R3)
         L     R5,0(R5)
         IF    (LTR,R5,R5,Z)
               PM   LISTALL
         ELSE
               IF   (CH,R5,=H'3',EQ)
                    PM   LISTUNIT
               ELSE
                    IF   (CH,R5,=H'6',EQ)
                         PM   LISTVOL
                    ELSE
                         TPPUTMSG TERM,'DISK INVALID PARAMETER'
                    FI
               FI
         FI
         SUBEXIT
         TITLE 'LIST ALL ONLINE DISKS'
LISTALL  PENTRY
         PM    OPENDISK
         PM    GETDISK
         DO    WHILE,(CLI,EOF,TRUE,NE)
               IF   (TM,SRTESTAT(R1),X'80',O) ONLINE
                    PM   LISTDISK
               FI
               PM   GETDISK
         OD
         PEXIT
         TITLE 'LIST SINGLE UNIT'
LISTUNIT PENTRY
         PM    OPENDISK
         PM    GETDISK
         DO    WHILE,(CLI,EOF,TRUE,NE)
               IF   (CLC,0(3,R4),UCBNAME(R1),EQ)
                    PM   LISTDISK
                    PEXIT
               FI
               PM   GETDISK
         OD
         TPPUTMSG TERM,'UNIT NOT FOUND'
         PEXIT
         TITLE 'LIST SINGLE VOLUME'
LISTVOL  PENTRY
         PM    OPENDISK
         PM    GETDISK
         DO    WHILE,(CLI,EOF,TRUE,NE)
               IF   (TM,SRTESTAT(R1),X'80',O),                         X
               AND,(CLC,0(6,R4),SRTEVOLI(R1),EQ)
                    PM   LISTDISK
                    PEXIT
               FI
               PM   GETDISK
         OD
         TPPUTMSG TERM,'VOLUME NOT MOUNTED'
         PEXIT
         TITLE 'OPEN SEQUENTIAL UCB DISK SEARCH'
OPENDISK PENTRY
         MVI   EOF,FALSE
         L     R1,16
         L     R1,CVTILK2(R1)
         SH    R1,=H'2'
         ST    R1,AUCB
         PEXIT
         TITLE 'GET NEXT DISK UCB'
GETDISK  PENTRY
         L     R15,AUCB
         AAI   R15,2
         LH    R1,0(R15)
         DO    WHILE,(LTR,R1,R1,NM)
               IF   (LTR,R1,R1,P),AND,(CLI,UCBTYP+2(R1),X'20',EQ)
                    ST   R15,AUCB
                    PEXIT
               FI
               AAI  R15,2
               LH   R1,0(R15)
         OD
         MVI   EOF,TRUE
         PEXIT
         TITLE 'LIST DISK STATUS'
LISTDISK PENTRY
         IF    (TM,SRTESTAT(R1),X'80',Z) OFFLINE
               MVC  DOFFUCB,UCBNAME(R1)
               TPPUTMSG TERM,DOFFMSG
               PEXIT
         FI
         MVC   DUCB,UCBNAME(R1)
         MVC   DVOL,SRTEVOLI(R1)
         MVC   DMSTAT,=CL3' '
         IF    (TM,SRTESTAT(R1),X'20',O) RESERVED
               MVI  DMSTAT,C'R'
         FI
         IF    (TM,SRTESTAT(R1),X'08',O) ALLOCATED
               MVI  DMSTAT+1,C'A'
         FI
         IF    (TM,SRTESTAT(R1),X'04',O) PERMANENTLY MOUNTED
               MVI  DMSTAT+2,C'P'
         FI
         IF    (TM,SRTESTAB(R1),X'10',O) PRIVATE
               MVC  DUSTAT,=C'PV'
         ELSE
               IF   (TM,SRTESTAB(R1),X'08',O) PUBLIC
                    MVC  DUSTAT,=C'PB'
               ELSE
                    MVC  DUSTAT,=C'ST'
               FI
         FI
         ZR    R0
         IC    R0,SRTEUSER(R1)
         CVD   R0,PWORK
         EDIT  TO=DUSERS,FROM=PWORK+6,MASK=' Z99'
         ZR    R0
         IC    R0,SRTEDMCT(R1)
         CVD   R0,PWORK
         EDIT  TO=DDCBS,FROM=PWORK+6,MASK=' Z99'
         ZR    R0
         ICM   R0,3,UCBSKA+3(R1)
         CVD   R0,PWORK
         EDIT  TO=DSCYL,FROM=PWORK+6,MASK=' Z99'
         TPPUTMSG TERM,DISKMSG
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
TERM     DC    CL8' '
EOF      LOGICAL FALSE,(TRUE,FALSE)
CVTILK2  EQU   40   FE PG 88
SRTESTAT EQU   3    FE PG 125
UCBNAME  EQU   13   FE PG 125
UCBTYP   EQU   16   FE PG 125
SRTEVOLI EQU   28   FE PG 133
SRTESTAB EQU   34   FE PG 133
SRTEDMCT EQU   35   FE PG 133
UCBSKA   EQU   48
SRTEUSER EQU   56         133
AUCB     DC    A(0)
DOFFMSG  DS    0CL12
DOFFUCB  DC    CL3' '
         DC    C' OFFLINE'
         FILL  DOFFMSG
DISKMSG  DS    0CL65
DUCB     DC    CL3' ',C' '         UCB
DVOL     DC    CL6' ',C' MSTAT '   VOLUME
DMSTAT   DC    CL3' ',C' USTAT '   R-RESERVED P-PERM MOUNT A-ALLOCATED
DUSTAT   DC    CL2' ',C' USERS'    PV-PRIVATE PB-PUBLIC ST-STORAGE
DUSERS   DC    CL4' ',C' DCBS'
DDCBS    DC    CL4' ',C' LAST-SEEK'
DSCYL    DC    CL4' '
         FILL  DISKMSG
PWORK    DC    D'0'
         END
