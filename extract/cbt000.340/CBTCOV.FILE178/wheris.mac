*          DATA SET TO009000   AT LEVEL 033 AS OF 03/23/78
         TITLE 'WHERIS - LOCATE LOCAL 3270 TSO USER'
*---------------------------------------------------------------------
*
*        DATE.     02/21/77.
*
*        AUTHOR.   W R WILLITS
*
*        REMARKS.  WHERIS WILL DISPLAY THE PHYSICAL LOCATION OF
*                  ANY TSO USER LOGGED ON TO A LOCAL 3270 DEVICE.
*
*                  INPUT WILL BE THE COMMAND NAME AND THE USERID.
*                    WHERIS OT---RS
*
*                  THIS PROGRAM WILL LOAD THE TABLE DATA IN TC009001
*                  WHICH IS MAINTAINED VIA QED WHENEVER TERMINALS
*                  AND/OR LINES ARE CHANGED.
*
*
*                  1.  IT IS REENTRANT AND CAN BE PLACED IN
*                      PLPA FOR COMMON USE.
*                  2.  THE PROGRAM DOES REQUIRE PROGRAM
*                      AUTHORIZATION FOR MODESET KEY=ZERO.
*
*              REFERENCES.
*                  GC28-0751-0 OS/VS2 BEBUGGING HANDBOOK VOL. 1
*                  GC28-0709-0 OS/VS2 DEBUGGING HANDBOOK VOL. 2
*                  GC28-0886-0 OS/VS2 MVS PERFORMANCE NOTEBOOK
*                  GC28-0648-2 OS/VS2 TSO GUIDE TO COMMAND PROCESSORS
*                  GC28-0683-1 OS/VS2 SUPR. SERVICES MACRO INSTR.
*                  SY28-0715-1 OS/VS2 SYSTEM LOGIC LIBRARY
*
*
*---------------------------------------------------------------------
*
         PUNCH ' ALIAS WHERIS   &SYSDATE &SYSTIME IGNORE IEW0731'
*
         TITLE 'MAINLINE'
         PRINT GEN
WHERIS   SUBENTRY BASES=(R12),RENT=YES,STACK=10,                       X
               PSTACK=R11,PSAVE=NO,PCHECK=NO
         PRINT NOGEN
         PM    INIT                    INITIALIZE WORK AREA
         PM    PARSE                   PARSE FOR OPERAND USERID
         PM    PORT                    FIND UCB FOR EACH TSO USER
RETURN   TAG
         LA    R0,WHERDLEN
         LR    R1,R10
         FREEMAIN R,LV=(0),A=(1)
         SUBEXIT
         TITLE 'INIT - INITIALIZE WORK AREA'
INIT     PENTRY
         LA    R0,WHERDLEN             LENGTH OF WORK AREA
         LR    R3,R1                   SAVE TMP R1
         GETMAIN R,LV=(0)
         LR    R10,R1                  R10=A(USER WORK AREA)
         LR    R1,R3                   RESTORE R1 TO GET CCPL FROM TMP
*                                         GETMAIN SCREWED UP R1 AND TMP
*                                         CCPL IS REQUIRED FOR IOPL/PPL
         LR    R2,R12                  BASE FOR THIS PROGRAM
         A     R2,=A(WHERD-(WHERIS+8)) 2ND OPERAND MVCL
         LA    R3,WHERDLEN             1ST OPERAND MVCL LENGTH
         LR    R4,R10                  1ST OPERAND MVCL (GETMAIN AREA)
         LR    R5,R3                   2ND OPERAND MVCL LENGTH
         MVCL  R4,R2                   INITIALIZE GETMAIN AREA
         USING WHERD,R10               BASE REG FOR WORK AREA
         LR    R0,R12
         PEXIT
         TITLE 'PARSE - SET UP TSO CONTROL BLOCKS AND PARSE'
         PRINT GEN
PARSE    PENTRY
         SPACE 2
COMMON   #TSOBLKS DSECT,                                               C
               ECB=WHERECB,                                            C
               CPPL=(R9,R1),                                           C
               IOPL=(R8,WHERIOPL,WHERIOPB),                            C
               PPL=(R7,WHERPPL,USERPCL,WHERANS)
         TITLE 'LINK TO PARSE '
         PRINT NOGEN
*--------------------
*  LINK TO IKJPARS
*--------------------
         LA    R1,PPL
         LINK  EP=IKJPARS
         IF    (LTR,R15,R15,NZ)
               PUTLINE OUTPUT=(PARSTEXT,,,DATA),MF=(E,IOPL)
               B     RETURN
         FI
         L     R7,WHERANS               ANSWER AREA ADDR
         USING WHERPDL,R7
         LA    R7,USERNUM               USER PDE
         MVC   UPDE,=CL7' '             CLEAR USER ID WORK AREA
         SR    R0,R0
         LA    R1,UPDE
         ST    R1,AUPDE                  A(UPDE)
         L     R15,0(R7)                 A(INPUT)
         LH    R14,4(R7)                 LENGTH OF INPUT
         IF    (LTR,R14,R14,Z)
               PUTLINE OUTPUT=(MVCTEXT,,,DATA),MF=(E,IOPL)
               B     RETURN
         FI
         AR    R0,R14                    ADD TO LENGTH
         BCTR  R14,0                     REDUCE BY ONE
         EX    R14,MOVEX                 EXECUTE MOVE R15 TO R1
         MVC   MSG01UID(7),UPDE          MOVE PARSE PARM TO MSG AREA
         DROP  R7
         SPACE 2
         PEXIT
         PRINT NOGEN
         TITLE 'PORT - SCAN TSO USERS ONLY'
PORT     PENTRY
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R4,ASVTMAXU(R2)
         LOAD  EP=TO009001
         LR    R6,R0                         ADDR OF TC009001 TABLE
         SH    R6,=H'80'                     SET UP FOR DO UNTIL BELOW
         ST    R6,SAVETABL                   A(TO009001 TABLE)
         DO    WHILE,(SH,R4,=H'1',NM)
               IF   (ICM,R5,15,0(R3),P)
                    PM   PROCESS
                    IF   (CLC,USERID(7),MSG01UID,EQ)
                         L    R6,SAVETABL
*               -----------------------------------------------------
                         DO   UNTIL,(CLC,5(3,R6),MSG01UCB,EQ),OR,      C
               (CLI,5(R6),X'FF',EQ)
                              LA   R6,80(R6)
                         OD
*               -----------------------------------------------------
                         IF   (CLC,5(3,R6),MSG01UCB,EQ)
                              MVC  MSG01LOC,26(R6)
                         ELSE
                              MVC  MSG01LOC,NOTFOUND
                         FI
                         PM   PUT
                         PEXIT
                    FI
               FI
               AAI  R3,4
         OD
         IF    (LTR,R4,R4,M)
               MVC  MSG01UID+8(37),NOTLOGON
               PM   PUT
         FI
         PEXIT
         TITLE 'PROCESS - PROCESS ONE ASCB ENTRY FOR PORT'
PROCESS  PENTRY
         IF    (ICM,R1,15,ASCBTSB(R5),Z)
               PEXIT
         FI
         L     R1,ASCBJBNS(R5)
         IF    (CLI,0(R1),C'A',NL)
               MVC  USERID,0(R1)
         ELSE
               MVC  USERID,=CL8'*LOGON*'
         FI
         L     R6,ASCBTSB(R5)
         MODESET KEY=ZERO
         UNPK  DWORK(5),TSBLINE(3,R6)
         MODESET KEY=NZERO
         TR    DWORK+1(3),HEX-240
         MVC   MSG01UCB,DWORK+1
         PEXIT
         TITLE 'PUT'
PUT      PENTRY
         LA    R0,MSG01
         LR    R1,R0                    A(MSG01)
         ICM   R1,8,=X'00'
         LA    R0,80                    MSGLEN
         TPUT  (1),(0),R
         IF    (LTR,R15,R15,NZ)
               ABEND 101,DUMP
         FI
         PEXIT
         TITLE 'LITERALS AND EQUATES'
         PRINT GEN
         EQUAL
         TITLE 'TABLE OF LOCAL 3270 LOCATIONS'
*------------------------------------------------------------------
*  UCB 080-08F BELONG TO THE COURIER CONTROLLER
*  UCB 0A0-0AF BELONG TO THE TCI CONTROLLER
*  UCB 0B0-0BF BELONG TO THE TCI CONTROLLER
*------------------------------------------------------------------
*  THE TABLE IS IN TO009001 AND IS LOADED ABOVE.
         TITLE 'IKJPARM SET UP'
USERPCL  IKJPARM DSECT=WHERPDL
         SPACE 2
USERNUM  IKJPOSIT USERID,PROMPT='USERID'
         SPACE 2
         IKJENDP
         TITLE 'CONSTANTS AREA'
         SPACE 2
PARSTEXT #TSOMSG 'PARSE ERROR DETECTED'
MVCTEXT  #TSOMSG 'MVC ERROR DETECTED'
         SPACE 2
MINUCB   DC   CL3'080'
HEX      DC   C'0123456789ABCDEF'
NOTFOUND DC   CL37' DEVICE NOT FOUND IN TABLE    '
NOTLOGON DC   CL37' IS NOT LOGGED ON'
         SPACE 2
MOVEX    MVC   0(0,R1),0(R15)
         SPACE 2
MODLIOPB PUTLINE MF=L
IOPBLEN  EQU   *-MODLIOPB
         TITLE 'WHERIS DYNAMIC WORK AREA DSECT'
WHERD    DS    0D
         SPACE 2
MSG01    DS    0CL80                    BEGIN OF MSG DISPLAY AREA
         DC    CL1' '
MSG01UID DC    CL8' '
         DC    CL18'IS LOGGED ON LINE '
MSG01UCB DC    CL3' '
         DC    CL3' - '
MSG01LOC DC    CL37' '
         DC    CL1' '
         FILL  MSG01
MSG01LEN EQU   *-MSG01                  END OF MSG DISPLAY AREA
         SPACE 2
USERID   DC   CL8' '                    USER ID FOR PORT
         SPACE 2
SAVETABL DS    F                        A(TO009001 TABLE)
WHERECB  DS    F
WHERIOPL DS    0F
         ORG   *+IOPLLEN
WHERPPL  DS    0F
         ORG   *+PPLLEN
WHERANS  DS    A
         SPACE 2
WHERIOPB PUTLINE MF=L
         SPACE 2
SAVLEN   DC    H'0'
DWORK    DC    CL5' '
UPDE     DC    CL7' '
AUPDE    DC    A(UPDE)
         SPACE 2
WHERDLEN EQU   *-WHERD
         TITLE 'SYSTEM CONTROL BLOCK EQUATES'
*------------------
*        ASVT
*------------------
ASVTMAXU EQU   X'204'    87 NUMBER OF ASCB ENTRIES
ASVTENTY EQU   X'210'    87 START  OF ASCB ENTRIES
*------------------
*        ASCB
*------------------
ASCBCSCB EQU   X'38'     69 COMMAND SCHED. CONTROL BLOCK CHAIN
ASCBTSB  EQU   X'3C'     69 TERMINAL STATUS BLOCK FOR TSO ONLY
ASCBEJST EQU   X'40'     69 UNSIGNED 64 BIT TCB CPU TIME
ASCBFLG1 EQU   X'67'     70 FLAG FIELD
ASCBDSP1 EQU   X'72'     70 DISPATCH FLAG FIELD
ASCBOUCB EQU   X'90'     71 SRM USER CONTROL BLOCK
ASCBFMCT EQU   X'98'     71 PAGE FRAME COUNT - WORKING SET
ASCBJBNI EQU   X'AC'     72 JOBNAME OF INITS
ASCBJBNS EQU   X'B0'     72 JOBNAME OF STCS
ASCBSRBT EQU   X'C8'     72.1 SRB CPU TIME
*------------------------------------------
*        CVT - COMMUNICATION VECTOR TABLE
*------------------------------------------
CVTASCVT EQU   X'22C'   197 ADDRESS SPACE VECTOR TABLE
*---------------
*        PSA
*---------------
PSAAOLD  EQU   X'224'   528 CURRENT ASCB
*----------------------------------------------------------------
*        TSB - TERMINAL STATUS BLOCK IN PROTECTED CSA STORAGE
*----------------------------------------------------------------
TSBLINE  EQU   X'52'    739  3705 LINE ADDRESS
         SPACE 2
         END   WHERIS
