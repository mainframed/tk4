*          DATA SET GU003700   AT LEVEL 018 AS OF 06/09/80
*          DATA SET GU003700   AT LEVEL 017 AS OF 03/05/80
         TITLE 'GU003700 - LIST DSCB INFORMATION FOR DATA SET'
*              PGM-ID. GU003700.
*              AUTHOR. DON HIGGINS.
*              DATE.   02/21/80.
*              REMARKS.THIS PROGRAM WAS WRITTEN TO REPLACE ISAMSTAT
*                      WHICH WAS A PROGRAM WRITTEN BY IBM CE ABOUT
*                      1970 TO DISPLAY INFO. ON CA.MASTER.PIE1-4
*                      ISAM MASTER FILES.  GIVEN A FULLY QUALIFIED
*                      DATA SET NAME IN THE PARM FIELD, THIS PROGRAM
*                      WILL DISPLAY FORMATTED DATA SET CONTROL BLOCK
*                      INFORMATION ON SYSPRINT OUTPUT FILE.
*              PARM.
*
*                   1-44 CHARACTER FULLY QUALIFIED DATA SET NAME
*
*              FILES.
*
*                   SYSPRINT - OUTPUT PRINT FILE
*
*                      (OUTPUT IS FIXED 80 RECORDS WITHOUT
*                       CHARRIAGE CONTROL FOR USE ON TSO TERMINALS)
*
*              NOTES.
*
*                1.  THIS PROGRAM USES OS CATALOG LOCATE AND OBTAIN
*                    MVS SYSTEM MACROS TO GET DSN INFORMATION.
*
*                2.  IF DDNAME=DUMPDSCB IS PRESENT, PROGRAM TAKES
*                    A SNAP DUMP OF EACH DSCB PROCESSED.
*
*              MAINTENANCE.
*                06/09/80 FIX DSC2 PROCESSING TO HANDLE SINGLE EXT.
*
GU003700 SUBENTRY
         L     R2,0(R1)
         LH    R3,0(R2) LENGTH OF DSN
         IF    (SH,R3,=H'1',NM)
               EX    R3,MVCDSN
         FI
         FINDDD =CL8'DUMPDSCB'
         IF    (LTR,R1,R1,NZ)
               OPEN (DUMPDSCB,(OUTPUT))
               MVI  DUMP,TRUE
         FI
         OPEN  (SYSPRINT,(OUTPUT))
         LOCATE LOCLIST
         IF    (LTR,R15,R15,NZ)
               LR   R12,R15
               LA   R0,=CL80'GU003700 LOCATE FAILED'
               PM   PUT
               CLOSE (SYSPRINT)
               SUBEXIT RC=(R15)
         FI
         MVC   L1VOL,LOCAREA+2+4
         OBTAIN DS1LIST
         IF    (LTR,R15,R15,NZ)
               LR   R12,R15
               LA   R0,=CL80'GU003700 DS1 OBTAIN FAILED'
               PM   PUT
               CLOSE (SYSPRINT)
               SUBEXIT RC=(R12)
         FI
         PM    FDSCB1
         IF    (TM,DS1DSORG,X'80',O)
               OBTAIN DS2LIST
               IF   (LTR,R15,R15,NZ)
                    LR   R12,R15
                    LA   R0,=CL80'GU003700 DS2 OBTAIN FAILED'
                    PM   PUT
                    CLOSE (SYSPRINT)
                    SUBEXIT RC=(R12)
               FI
               PM   FDSCB2
         FI
         CLOSE (SYSPRINT)
         IF    (CLI,DUMP,TRUE,EQ)
               CLOSE (DUMPDSCB)
         FI
         SUBEXIT
MVCDSN   MVC   L1DSN(0),2(R2) MOVE DSN FROM PARM VIA EX
         TITLE 'FDSCB1 - FORMAT DSCB1'
FDSCB1   PENTRY
         IF    (CLI,DUMP,TRUE,EQ)
               SNAP  DCB=DUMPDSCB,ID=1,STORAGE=(DSCB1,DSCB1+140)
         FI
         IF    (TM,DS1DSORG,X'80',O)
               MVC  L1DSORG,=C'IS'
         ELSEIF (TM,DS1DSORG,X'40',O)
               MVC  L1DSORG,=C'PS'
         ELSEIF (TM,DS1DSORG,X'20',O)
               MVC  L1DSORG,=C'DA'
         ELSEIF (TM,DS1DSORG,X'02',O)
               MVC  L1DSORG,=C'PO'
         ELSE
               MVC  L1DSORG,=C'**'
         FI
         LA    R0,L1LINE
         PM    PUT
         PEXIT
         TITLE 'FDSCB2 FORMAT ISAM DSCB2 INFORMATION'
FDSCB2   PENTRY
         IF    (CLI,DUMP,TRUE,EQ)
               SNAP  DCB=DUMPDSCB,ID=2,STORAGE=(DSCB2,DSCB2+140)
         FI
         ICM   R0,15,DS2PRCTR
         CVD   R0,PWORK
         EDIT  TO=L2PRCTR,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'
         LH    R0,DS2OVRCT
         CVD   R0,PWORK
         EDIT  TO=L2OVRCT,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'
         LA    R0,L2LINE
         PM    PUT
         IF    (CLC,DS2LPRAD(1),DS1EXT1,EQ)
               LA   R1,DS1EXT1
         ELSEIF (CLC,DS2LPRAD(1),DS1EXT1+10,EQ)
               LA   R1,DS1EXT1+10
         ELSEIF (CLC,DS2LPRAD(1),DS1EXT1+20,EQ)
               LA   R1,DS1EXT1+20
         ELSE
               LA   R0,=CL80'GU003700 ISAM FILE HAS OVER 3 EXTENTS'
               PM   PUT
               CLOSE (SYSPRINT)
               SUBEXIT RC=16
         FI
         ZR    R2
         ICM   R2,3,2(R1)  R2= STARTING PRIME CYL
         ZR    R3
         ICM   R3,3,6(R1)  R3= ENDING PRIME CYL
         ZR    R4
         ICM   R4,3,DS2LPRAD+3 R4= EOF PRIME CYL
         SR    R3,R2
         AAI   R3,1
         CVD   R3,PWORK
         EDIT  TO=L3TOT,FROM=PWORK+6,MASK=' Z99'
         SR    R4,R2
         AAI   R4,1
         CVD   R4,PWORK
         EDIT  TO=L3USED,FROM=PWORK+6,MASK=' Z99'
         MH    R4,=H'100'
         LR    R1,R4
         ZR    R0
         DR    R0,R3
         CVD   R1,PWORK
         EDIT  TO=L3PCT,FROM=PWORK+6,MASK=' Z99'
         LA    R0,L3LINE
         PM    PUT
         PEXIT
         TITLE 'PUT - PRINT LINE'
PUT      PENTRY
         PUT   SYSPRINT
         PEXIT
         EQUAL
DUMP     LOGICAL FALSE,(TRUE,FALSE)
PWORK    DC    D'0'
LOCLIST  CAMLST NAME,L1DSN,,LOCAREA
LOCAREA  DC    0D'0',265X'00'
DS1LIST  CAMLST SEARCH,L1DSN,L1VOL,DSCB1+44
DS2LIST  CAMLST SEEK,DS1PTRDS,L1VOL,DSCB2
DSCB1    DC    0D'0',140X'00',44X'00'
DSCB2    DC    0D'0',140X'00'
DS1DSORG EQU   DSCB1+X'52' DSORG
DS1PTRDS EQU   DSCB1+X'87' FORWARD DSCB SEEK POINTER
DS1EXT1  EQU   DSCB1+X'69' FIRST OF 3 10 BYTE EXTENTS MTCCHHCCHH
DS2PRCTR EQU   DSCB2+X'43' ISAM PRIME RECORDS
DS2LPRAD EQU   DSCB2+X'5D' ISAM LAST PRIME DATA RECORD MBBCCHHR
DS2OVRCT EQU   DSCB2+X'80' ISAM OVERFLOW RECORDS
L1LINE   DS    0CL80
         DC    C'FILE='
L1DSN    DC    CL44' ',C' VOLUME='
L1VOL    DC    CL6' ',C' TYPE='
L1DSORG  DC    CL2'  '
         FILL  L1LINE
L2LINE   DS    0CL80
         DC    C'ISAM PRIME RECORDS='
L2PRCTR  DC    CL10' '
         DC    C'    OVERFLOW='
L2OVRCT  DC    CL10' '
         FILL  L2LINE
L3LINE   DS    0CL80
         DC    C'ISAM TOTAL PRIME CYLINDERS='
L3TOT    DC    CL5' ',C'   CYL.USED='
L3USED   DC    CL5' ',C'   % USED='
L3PCT    DC    CL3' '
         FILL  L3LINE
SYSPRINT DCB   DSORG=PS,MACRF=PM,RECFM=F,BLKSIZE=80,DDNAME=SYSPRINT
DUMPDSCB DCB   DSORG=PS,MACRF=W,RECFM=VBA,BLKSIZE=882,LRECL=125,       X
               DDNAME=DUMPDSCB
         END
