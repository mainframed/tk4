*          DATA SET GU026100   AT LEVEL 003 AS OF 04/08/74
GU026100 SUBENTRY
         TITLE 'GU026100 - MAINLINE'
         PRINT NOGEN
*              AUTHOR.   DON HIGGINS.
*              DATE.     01/23/74.
*              REMARKS.  READ ENTIRE VTOC FOR EACH DD NAMED VCXXXXXX
*                        AND CREATE SEQUENTIAL FILE WITH FOLLOWING
*                        RECORD FORMAT.
*                            6 VOLUME SERIAL
*                           44 DATA SET NAME
*                            1 FORMAT NUMBER OF DSCB
*                          140 DATA SET CONTROL BLOCK
*                            8 TIME AND DATE OF VTOC READ
*                         ----
*                          199
*
*              REGISTERS.
*                  R2 - CURRENT TIOT ENTRY
*                  R3 - CURRENT TIOT DD LENGTH
         OPEN  (VFSYSUT2,(OUTPUT))
         PERFORM R100INIT,R190EXIT
M010     TAG
         PERFORM R200NEXT,R290EXIT
         CLI   EOJ,TRUE
         BE    M020
         PERFORM R300COPY,R390EXIT
         B     M010
M020     TAG
         CLOSE (VFSYSUT2)
         SUBEXIT
         TITLE 'R100 - INITIALIZE'
R100INIT PENTRY
         L     R2,16
         L     R2,0(R2)
         L     R2,4(R2)
         L     R2,12(R2)
         LA    R2,24(R2)
         ZR    R3
R190EXIT PEXIT
         TITLE 'R200 - NEXT DD ROUTINE'
R200NEXT PENTRY
         LA    R2,0(R3,R2)
         IC    R3,0(R2)
         SRC   R3
         BZ    R210
         CLC   4(2,R2),=C'VC'
         BE    R220
         B     R200NEXT
R210     TAG
         MVI   EOJ,TRUE
         B     R290EXIT
R220     TAG
         MVC   UCBADDR+1(3),17(R2)
R290EXIT PEXIT
         TITLE 'R300 - COPY ALL OF VTOC FOR CURRENT DD'
R300COPY PENTRY
         L     R1,UCBADDR
         MVC   TTR0,SRTEFSCT(R1)
         MVC   VOLUME,SRTEVOLI(R1)
         MVI   MAXH+3,19
         CLC   UCBTYP(4,R1),=X'30502009'  IS IT 3330
         BNE   ERR7                       NO, GO LEARN ABOUT NEW DEVICE
         PERFORM R600CVTR,R690EXIT     TTR0 TO MBBCCHHR
         PERFORM R400READ,R490EXIT     READ VTOC DSCB
         CLI   DSCBTYPE,C'4'
         BNE   ERR4
         MVC   DSN,=CL44' FORMAT4.DSCB'
         PERFORM R500WRIT,R590EXIT
         MVC   MAXR,DS4DEVDT
         MVC   NEXTF6,DS4F6PTR
         MVC   MAXCCHHR,DS4HPCHR
         MVI   TTR0+2,2
         MVI   MBBCCHHR+7,2
         ZAP   PSEQ,=P'0'
R310     TAG
         PERFORM R400READ,R490EXIT
         CLI   DSCBTYPE,C'5'
         BNE   ERR5
         MVC   DSN,=CL44' FORMAT5.DSCB'
         AP    PSEQ,=P'1'
         EDIT  TO=DSN+13,FROM=PSEQ,MASK=' 999'
         PERFORM R500WRIT,R590EXIT
         MVC   MBBCCHHR+3(5),DSXPTRDS
         CLC   MBBCCHHR+3(5),=XL5'00'
         BNE   R310
         CLC   NEXTF6,=XL5'00'
         BE    R330
         MVC   MBBCCHHR+3(5),NEXTF6
         ZAP   PSEQ,=P'0'
R320     TAG
         PERFORM R400READ,R490EXIT
         CLI   DSCBTYPE,C'6'
         BNE   ERR6
         MVC   DSN,=CL44' FORMAT6.DSCB'
         AP    PSEQ,=P'1'
         EDIT  TO=DSN+13,FROM=PSEQ,MASK=' 999'
         PERFORM R500WRIT,R590EXIT
         MVC   MBBCCHHR+3(5),DSXPTRDS
         CLC   MBBCCHHR+3(5),=XL5'00'
         BNE   R320
R330     TAG
         CLC   MAXR,TTR0+2
         BH    R340
         MVI   TTR0+2,1
         LH    R1,TTR0
         AAI   R1,1
         STH   R1,TTR0
         B     R350
R340     TAG
         IC    R1,TTR0+2
         AAI   R1,1
         STC   R1,TTR0+2
R350     TAG
         PERFORM R600CVTR,R690EXIT
         MVC   SAVEF1,MBBCCHHR+3
         PERFORM R400READ,R490EXIT
         CLI   DSCBTYPE,C'1'
         BNE   R370
         MVC   DSN,DSCBDSN
         PERFORM R500WRIT,R590EXIT
         CLC   DSXPTRDS(5),=XL5'00'
         BE    R370
         MVC   MBBCCHHR+3(5),DSXPTRDS
R360     TAG
         PERFORM R400READ,R490EXIT
         CLI   DSCBTYPE,C'2'
         BE    R365
         CLI   DSCBTYPE,C'3'
         BNE   ERR23
R365     TAG
         PERFORM R500WRIT,R590EXIT
         MVC   MBBCCHHR+3(5),DSXPTRDS
         CLC   DSXPTRDS(5),=XL5'00'
         BNE   R360
R370     TAG
         CLC   MAXCCHHR,SAVEF1
         BH    R330
R390EXIT PEXIT
         TITLE 'R400 - READ VTOC RECORD VIA OBTAIN'
R400READ PENTRY
         OBTAIN CAMLST
         SRC   R15
         BNZ   ERR001
         MVC   DSCB,DSCBWORK
R490EXIT PEXIT
         TITLE 'R500 - WRITE RECORD TO OUTPUT FILE'
R500WRIT PENTRY
         TIME  DEC
         STM   R0,R1,TEMP
         MVC   TIME,TEMP
         MVC   FORMAT,DSCBTYPE
         PUT   VFSYSUT2,RECORD
R590EXIT PEXIT
         TITLE 'R600 - CONVERT TTR TO MBBCCHHR'
R600CVTR PENTRY
         MVC   MBBCCHHR+7(1),TTR0+2
         ZR    R0
         LH    R1,TTR0
         D     R0,MAXH
         STCM  R0,3,MBBCCHHR+5
         STCM  R1,3,MBBCCHHR+3
R690EXIT PEXIT
         TITLE 'ERROR ABENDS'
ERR001   ABEND 001,DUMP
ERR23    ABEND 223,DUMP
ERR4     ABEND 204,DUMP
ERR5     ABEND 205,DUMP
ERR6     ABEND 206,DUMP
ERR7     ABEND 207,DUMP
         TITLE 'DATA DIVISION'
VFSYSUT2 DCB   DSORG=PS,MACRF=PM,DDNAME=VFSYSUT2,                      X
               RECFM=FB,LRECL=199
         EQUAL
EOJ      DC    AL1(FALSE)
FALSE    EQU   0
TRUE     EQU   1
UCBADDR  DC    A(1)
MAXH     DC    F'0'
TTR0     DC    F'0'
MBBCCHHR DC    D'0'
RECORD   DS    0CL199
VOLUME   DC    CL6' '
DSN      DC    CL44' '
FORMAT   DC    C' '
DSCB     DC    CL140' '
TIME     DC    CL8' '
NEXTF6   DC    XL5'00'
SAVEF1   DC    XL5'00'
MAXCCHHR DC    XL5'00'
MAXR     DC    AL1(1)
PSEQ     DC    PL2'0'
TEMP     DC    D'0'
UCBTYP   EQU   16
SRTEFSCT EQU   36            FE 133
SRTEVOLI EQU   28
DS4DEVDT EQU   DSCB+74
DS4F6PTR EQU   DSCB+100
DSXPTRDS EQU   DSCB+135
DS4HPCHR EQU   DSCB+45
DSCBDSN  EQU   DSCB
DSCBTYPE EQU   DSCB+44
CAMLST   CAMLST SEEK,MBBCCHHR+3,VOLUME,DSCBWORK
DSCBWORK DC    D'0',XL148'00'
         END   GU026100
