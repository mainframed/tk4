*          DATA SET TO012000   AT LEVEL 008 AS OF 05/09/78
*          DATA SET TO012000   AT LEVEL 007 AS OF 04/04/78
         TITLE 'TERMSET -- TERMINAL INITIALIZATION COMMAND'
*----------------------------------------------------------------------
*
*                  T E R M S E T
*
*        TERMINAL INTIALIZATION COMMAND
*        THIS COMMAND CHECKS VIA A GTSIZE FOR A DISPLAY TYPE DEVICE.
*        IF NOT THE LINESIZE IS SET TO 80.
*
*        FUTURE ENHANCEMENTS COULD ALLOW FOR LINESIZE TO BE SET
*        DEPENDENT ON THE DEFAULT TYPE OF TERMINAL FOR A PARTICULAR
*        USER.
*
*        BY    FRED D BRISARD
*              TECHNICAL SUPPORT DEPARTMENT
*              COMPUTER SERVICES DIVISION
*              FLORIDA POWER CORPORATION
*              ST PETERSBURG, FLORIDA
*              2 MAY 1977
*    REVISIONS:
*        1.    VERNON P DIXON ON AUGUST 23, 1977
*              ISSUE 'STATTN TENS=120' TO PROVIDE BREAK FOR 3270
*              USERS AFTER 120 SECONDS LOCKED KEYBOARD TIME.
*
*        2.    FRED D BRISARD ON 27 NOVEMBER 1977
*              TO INSERT THE PROGRAMMERS NAME INTO THE
*              JCTPNAME FIELD SO THAT IT WILL BE PRINTED
*              ONTO THE HEADER OF SPUN DATASETS.
*
*        3.    FRED D BRISARD ON 30 JANUARY 1978.
*              INSERT THE PROGRAMMERS NAME INTO THE JCTPNAME
*              FIELD FROM THE UIDTABLE LOAD MODULE.
*              (SHOULD USE A VSAM GSR FILE SOME DAY.)
*
*  REV  03/16/78  W R WILLITS
*  1.  NEW NAME OF THE PROGRAM IS TO012000 BUT IS LKED AS TERMSETA
*      OR TERMSETB.  THE LKED NAME MUST BE CHANGED TO THE ALTERNATE
*      WHENEVER TESTING A NEW VERSION SO AS NOT TO DISTURB THE
*      CURRENT TSO USERS.
*      A CONSTANT OF 'UNKNOWN, CALL 4776' WILL BE ENTERED INTO THE
*      PROGRAMMER NAME FIELD IF NO ENTRY IS FOUND IN THE TO011000
*      TABLE (WHICH IS EXTRACTED FROM THE CSD USER FILE)
*      WHENEVER UPDATES ARE MADE TO FILE 'UI.UI005D01', A MAINT RUN
*      MUST BE PERFORMED TO UPDATE THE TABLE IN TO011000.
*      A CLIST (UIDTABLE) HAS BEEN ESTABLISHED IN 'LM.LIBMAINT' FOR
*      USE BY AUTHORIZED PERSONNEL.
*
*
*----------------------------------------------------------------------
         EJECT
         PRINT NOGEN
         PUNCH ' ALIAS TERMSET  &SYSDATE &SYSTIME IGNORE IEW0731'
TERMSET  SUBENTRY  BASES=12,RENT=YES,RWALNG=0,RWA=WORK
         GTSIZE
         IF    (LTR,R0,R0,Z)           IF NOT DISPLAY DEVICE
               STSIZE  SIZE=80
         ELSE
               STATTN TENS=120
         FI
         SPACE 2
*--------------------------------------
*  PLUG PROGRAMMER NAME INTO JCTPNAME
*--------------------------------------
         LOAD  EP=TO011000             GET USERID TABLE
         LR    R6,R0                   SAVE TABLE ADDRESS
         SPACE 2
         L     R3,PSATCBO+4            A(CURRENT TCB)
         L     R3,TCBJSCB(,R3)         A(JSCB)
         L     R3,JSCBSSIB(,R3)        A(SSIB)
         L     R3,SSIBSUSE(,R3)        A(SJB)
         L     R3,SJBJCT(,R3)          A(JCT)
         IF    (CLC,=CL4'JCT',JCTID(R3),EQ)
               LH  R5,JCTUSEID+6(R3)
               IF   (LTR,R5,R5,P),AND,(CH,R5,0(,R6),NH)
                    SLL   R5,1      MULTIPLY BY 2
                    IF    (LH,R5,0(R5,R6),NZ) IF OFFSET NOT ZERO
                          LA    R5,0(R5,R6)   GET ADDRESS OF NAME
                          MODESET  KEY=ZERO
                          MVC   JCTPNAME(,R3),0(R5)
                          MODESET  KEY=NZERO
                    ELSE
                          MODESET  KEY=ZERO
                          MVC   JCTPNAME(,R3),=CL20'UNKNOWN-CALL 4776'
                          MODESET  KEY=NZERO
                    FI
               FI
         FI
         SUBEXIT  RENT=YES,RWALNG=0
         TITLE 'MISC CONSTANTS AND WORK AREAS'
PSATCBO  EQU   536
TCBJSCB  EQU   180
JSCBSSIB EQU   316
SSIBSUSE EQU   32
SJBJCT   EQU   96
JCTID    EQU   88,4
JCTPNAME EQU   164,20
JCTUSEID EQU   528,8
         SPACE 2
         PRINT GEN
         EQUAL
         END
