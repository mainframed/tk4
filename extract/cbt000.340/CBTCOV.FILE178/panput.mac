*          DATA SET TO025000   AT LEVEL 015 AS OF 06/21/79
PANP     TITLE 'PANPUT -- TSO COMMAND PROCESSOR'
         MACRO
&LABEL   $MSG  &TEXT,&RC=NO,&DC=NO,&SFX=NO,&DSN=NO
         LCLA  &LNTH
         LCLB  &RCI,&DCI,&SFXI,&DSNI
         AIF   ('&RC' NE 'YES').NORC
&RCI     SETB  1
.NORC    ANOP
         AIF   ('&DC' NE 'YES').NODC
&DCI     SETB  1
.NODC    ANOP
         AIF   ('&SFX' NE 'YES').NOSFX
&SFXI    SETB  1
.NOSFX   ANOP
         AIF   ('&DSN' NE 'YES').NODSN
&DSNI    SETB  1
.NODSN   ANOP
&LNTH    SETA  K'&TEXT-3
&LABEL   DC    H'&LNTH',B'&RCI&DCI&SFXI&DSNI.0000',C&TEXT
         MEND
         SPACE 2
         MACRO
         CPM   &PROC
         IF    (CLI,ERR,0,EQ)
               PM    &PROC
         FI
         MEND
         SPACE 2
         MACRO
         XE    &PIN,&PEN,&TN
         LCLA  &L
&L       SETA  K'&TN-1
         DC    CL5'&PIN',CL10'&PEN',AL1(&L),CL5'&TN'
         MEND
         SPACE 2
         MACRO
&L       CALLPAM  &EP,&PARMS
         LCLC  &OFFSET
         AIF   ('&EP' NE 'POPEN').NPOPEN
&OFFSET  SETC  '0'
.NPOPEN  AIF   ('&EP' NE 'PSRCH').NPSRCH
&OFFSET  SETC  '8'
.NPSRCH  AIF   ('&EP' NE 'PREAD').NPREAD
&OFFSET  SETC  '16'
.NPREAD  AIF   ('&EP' NE 'PCLOSE').NPCLOSE
&OFFSET  SETC  '24'
.NPCLOSE ANOP
&L       L     15,PAMEP
         LA    15,&OFFSET.(,15)
         CALL  (15),&PARMS,VL,MF=(E,CALLMFL)
         MEND
         SPACE 2
         MACRO
         WORKORG &WORK,&REG
         GBLA  &STKSIZE
         USING &WORK-(72+&STKSIZE),&REG
         MEND
         TITLE 'PANPUT TSO PAN INTERFACE'
*--------------------------------------------------------------------
*
*  INSTALLATION  FLORIDA POWER CORPORATION
*
*  PROGRAM       TO025000(PANPUT,PANP)
*
*  AUTHOR        FRED D BRISARD
*
*  DATE          1976
*
*  COMMENTS      THIS PROGRAM WILL RETRIEVE ANY FILE FROM TSO CATALOG
*                AND PLACE IT INTO THE SPECIFIED PAN LIBRARY.  NEW
*                PAN MEMBERS WILL REQUIRE THE 'ADD' OPERAND AND THE
*                APPROPRIATE PAN TYPE TO BE ENTERED.  THE PAN TYPE
*                MUST MATCH THE TSO SUFFIX EQUIVALENT AS INDICATED IN
*                THE TABLE FOUND IN THE COPYINIT ROUTINE OF THIS
*                PROGRAM.  COMMAND FOMAT IS AS FOLLOWS:
*
*                    PANPUT NAME ADD(TYPE)
*                    PANPUT NAME
*
*                OPERANDS ARE AS FOLLOWS:
*
*                    LIB('LIB')    (PAN LIBRARY, DEFAULT IS FPC.WORK)
*                    DA(DSN)       ANY TSO FILE - SEE HELP DOCUMENT
*
*  REV  04/28/78  W R WILLITS
*  1.  ALLOW PLI OR PL1 TSO SUFFIX FOR PL/I PAN TYPE
*
*  REV  01/26/79  W R WILLITS
*  1.  REMOVE ALPHA RESTRICTION ON PDLTYPE IKJIDENT (OTHER=ANY)
*      TO ALLOW SPECIAL CHAR INPUT (E.G.: COBOL-72 OR PL/1)
*
*
*
*--------------------------------------------------------------------
         PUNCH ' ALIAS PANPUT &SYSDATE &SYSTIME IGNORE IEW0731'
         PUNCH ' ALIAS PANP   &SYSDATE &SYSTIME IGNORE IEW0731'
PANPUT   SUBENTRY BASES=(12,11),RENT=YES,RWA=WORK,RWALNG=WORKL,        X
               STACK=20
         SPACE 3
         PRINT NOGEN
         MVC   WORKID,=C'PANPUTWK' SET WORK AREA ID
         SPACE 2
         LR    R10,R1              ESTABLISH CPPL
         USING CPPL,R10              ADDRESSIBILITY
         USING PDL,R7              PDL ADDRESSIBLITY
         SPACE 3
         SR    R0,R0               CLEAR ERROR AND
         ST    R0,ERR              STATUS INDICATORS
         SPACE 3
         PM    PARSE               PERFORM PARSE OF OPERANDS
         SPACE 2
         CPM   STAXINIT            CONDITIONALLY INIT ATTN EXIT
         SPACE 2
         LOAD  EP=PAM              LOAD PAM MODULE
         ST    R0,PAMEP            SAVE PAM ENTRY POINT
         SPACE 2
         CPM   PLIBINIT            INIT PRIMARY PAN LIB
         CPM   PLIBSRCH            SEARCH PRIMARY PAN LIB
         PM    PLIBCLOS            CLOSE BUT DO NOT DEALLOCATE PRIM LIB
         CPM   SLIBINIT            INIT SECONDARY PAN LIB
         CPM   SLIBSRCH            SEARCH SECONDARY PAN LIB
         PM    SLIBTERM            CLOSE AND DEALLOCATE SECONDARY LIB
         SPACE 2
         DELETE  EP=PAM            DELETE PAM MODULE
         SR    R0,R0               SHOW PAM NO LONGER AVAILABLE
         ST    R0,PAMEP
         SPACE 2
         CPM   DSINIT              INIT SOURCE DATASET
         CPM   COPYINIT            INIT COPY FUNCTION
         CPM   COPY                PERFORM COPY FUNCTION
         PM    COPYTERM            TERMINATE AND CLEAN-UP COPY
         PM    DSTERM              DEALLOCATE SOURCE DATASET
         PM    PLIBTERM            DEALLOCATE PRIMARY PAN LIB
         PM    STAXTERM            TERMINATE ATTN EXIT
         SPACE 5
         IKJRLSA ANSWER            RELEASE PDL
         SPACE 3
         SUBEXIT RC=0
         EJECT
*
*  PARSE ROUTINE
*
         SPACE 2
PARSE    PENTRY
         SPACE 2
         LA    R1,PARSEPL          ESTABLISH PPL
         USING PPL,R1                ADDRESSIBILITY
         SPACE 2
         L     R0,CPPLUPT          INITIALIZE THE PARSE
         ST    R0,PPLUPT             PARAMETER LIST
         L     R0,CPPLECT
         ST    R0,PPLECT
         LA    R0,ECB
         ST    R0,PPLECB
         L     R0,=V(PCL)
         ST    R0,PPLPCL
         LA    R0,ANSWER
         ST    R0,PPLANS
         L     R0,CPPLCBUF
         ST    R0,PPLCBUF
         SR    R0,R0
         ST    R0,PPLUWA
         ST    R0,ECB
         ST    R0,ANSWER
         SPACE 1
         DROP  R1                  DROP PPL
         SPACE 2
         CALLTSSR  EP=IKJPARS,MF=(E,(1)) INVOKE PARSE
         IF    (LTR,R15,R15,NZ)    TEST FOR BAD RETURN CODE
               LA    R1,PARSEMSG
               PM    TERMPUT       OUTPUT ERROR MESSAGE
               OI    ERR,PARSERR   INDICATE PARSE ERROR
         FI
         L     R7,ANSWER           -->  PDL
         SPACE 2
         L     R1,PDLPANDS         ADDRESS OF PAN DSNAME
         IF    (CLI,0(R1),C'*',EQ) IF PAN DSNAME IS AN ASTERISK
               MVI   PDLCTLKY+1,1  FORCE CONTROL MODE
         FI
         SPACE 2
         PEXIT
         SPACE 2
PARSEMSG $MSG  'PARSE ERROR CODE:',RC=YES,SFX=YES
         EJECT
*
*  ATTENTION EXIT INITIALIZATION ROUTINE
*
         SPACE 2
STAXINIT PENTRY
         SPACE 1
         MVC   STAXMFL(STAXREFL),STAXREF   INIT STAX LIST
         LA    R2,ERR              POINT TO ERROR INDICATOR
         STAX  STAXEXIT,USADDR=(2),MF=(E,STAXMFL)  ISSUE STAX
         IF    (LTR,R15,R15,Z)     TEST RETURN CODE
               OI    STATUS,STAXACTV   INDICATE STAX ACTIVE
         ELSE
               LA    R1,STAXERRM   STAX ERROR MESSAGE
               PM    TERMPUT
               OI    ERR,TSOCP     SET TSO CTRL PGM ERROR
         FI
         SPACE 2
         PEXIT
         SPACE 2
STAXREF  STAX  STAXEXIT,MF=L
STAXREFL EQU   *-STAXREF           LENGTH OF STAX LIST FORM
         SPACE 1
STAXERRM $MSG  'ERROR INITIATING ATTN EXIT..RC:',RC=YES,SFX=YES
         EJECT
*
*   PANVALET PRIMARY LIBRARY INITIALIZATION
*
*  THIS ROUTINE ALLOCATES THE PANVALET LIBRARY SPECIFIED IN THE LIB
*  KEYWORD OR 'FPC.WORK' IF NO LIB KEYWORD IS SUPPLIED.  IT THEN IN-
*  VOKES PAM POPEN TO OPEN THE LIBRARY AND READY IT FOR A LATER
*  SEARCH.
*
         SPACE 3
PLIBINIT PENTRY
         SPACE 2
         IF    (TM,PDLLIB+6,X'80',Z)   IF LIB NOT SPECIFIED
               LA    R1,=C'FPC.WORK'   DEFAULT LIBRARY NAME
               ST    R1,PDLLIB     SET IN PDL
               LA    R1,8          DEFAULT LIBRARY NAME LENGTH
               STH   R1,PDLLIB+4
               OI    PDLLIB+6,X'C0'    FLAG PDL ACTIVE
         FI
         LA    R1,PDLLIB           LIBRARY PDL ADDRESS
         PM    ALLOCSHR            ATTEMPT TO ALLOCATE THE LIB
         IF    (LTR,R15,R15,NZ)    IF RET CODE NOT ZERO
               PM    DAIRRC        ANALYZE DAIR RET CODE
               OI    ERR,PAMERR    FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         OI    LIBSTAT,PLIBALC      INDICATE PRIMARY LIB ALLOCATED
         USING DAPB08,R1
         MVC   PLIBDDNM,DA08DDN    SAVE DDNAME
         IF    (TM,DA08DSO,X'20',Z)    IF DSORG NOT DA
               LA    R1,PAMERR24   ISSUE ERROR MESSAGE
               PM    TERMPUT
               OI    ERR,PAMERR    FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         SPACE 1
         DROP  R1
         SPACE 2
         IF    (CLI,PDLCTLKY+1,1,EQ)   IF CONTROL INPUT
               PEXIT               EXIT DO NOT OPEN PRIMARY LIBRARY
         FI
         SPACE 2
         SR    R0,R0               CLEAR
         ST    R0,PAMACTN          SET UP OPEN PARMS
         MVC   PAMDDNM,PLIBDDNM
         MVC   PAMBKUP,NOENTRY
         CALLPAM POPEN,(PAMACTN,PAMDDNM,PAMBKUP)
         IF    (CLI,PAMACTN3,0,NE) IF PAM ACTN CODE NOT ZERO
               PM    PAMANAL       ANALYZE PAM RET CODE
               OI    ERR,PAMERR    FLAG ERROR
               IF    (CLI,PAMACTN3,24,EQ)  IF NOT A PAN LIB
                     OI    LIBSTAT,PLIBOPN INDICATE OPEN
               FI
               PEXIT               EXIT IMMEDIATELY
         FI
         OI    LIBSTAT,PLIBOPN      FLAG PRIMARY LIB OPEN
         PEXIT
         EJECT
*
*   PRIMARY PANVALET LIBRARY SEARCH ROUTINE
*
*  THIS ROUTINE SEARCHS THE PRIMARY PANVALET LIBRARY FOR THE
*  PAN-DSNAME SPECIFIED.  IF IT IS FOUND THE 0-UP DIRECTORY
*  ENTRY IS STORED IN PLIBDIR AND THE PRIMARY LIB DATASET
*  FOUND BIT IN THE LIBSTAT FIELD IS TURNED ON.
*  THIS INFORMATION WILL BE USED LATER TO DETERMINE VARIOUS
*  OPERATIONAL OPTIONS.
*
         SPACE 3
PLIBSRCH PENTRY
         SPACE 2
         IF    (TM,LIBSTAT,PLIBOPN,Z)  IF PRIMARY LIB NOT OPEN
               PEXIT               EXIT IMMEDIATELY...COULD BE CONTROL
         FI
         SPACE 2
         SR    R0,R0               PREPARE PARAMETERS FOR PSRCH
         ST    R0,PAMACTN
         MVC   PLIBDIR,BLANKS
         MVC   PAMCMNT,NOENTRY
         MVC   PAMSUBST,NOENTRY
         MVC   PAMNAME2,NOENTRY
         MVC   PAMNAME1,BLANKS
         L     R1,PDLPANDS
         LH    R2,PDLPANDS+4
         BCTR  R2,0
         EX    R2,MVCPNM1
*        MVC   PAMNAME1(*-*),0(R1)
         CALLPAM  PSRCH,                                               X
               (PAMACTN,PLIBDIR,PAMNAME1,PAMNAME2,PAMCMNT,PAMSUBST)
         IF    (CLI,PAMACTN3,0,EQ) IF PAM ACTN CODE ZERO
               OI    LIBSTAT,PLIBDSF   INDICATE DATASET FOUND
         ELSEIF  (CLI,PAMACTN3,23,NE)  ELSE IF PAM CODE OTHER THAN 23
               PM    PAMANAL       ANALYZE ERROR
               OI    ERR,PAMERR    FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         PEXIT
         SPACE 2
MVCPNM1  MVC   PAMNAME1(*-*),0(R1)
         EJECT
*
*   PRIMARY PANVALET LIBRARY CLOSE ROUTINE
*
*  THIS ROUTINE CLOSES THE PRIMARY PANVALET LIBRARY IF OPEN
*  VIA A CALL TO PCLOSE.  IN ADDITION THE PAM MODULE WHICH
*  IS LOADED DYNAMICALLY BY A SPECIAL POPEN INTERFACE IS
*  IS DELETED.
*
         SPACE 3
PLIBCLOS PENTRY
         SPACE 2
         IF    (TM,LIBSTAT,PLIBOPN,O)   IF PRIMARY LIB OPEN
               SR    R0,R0             CLOSE
               ST    R0,PAMACTN
               CALLPAM PCLOSE,(PAMACTN)
               NI    LIBSTAT,255-PLIBOPN  INDICATE PLIB CLOSED
         FI
         PEXIT
         EJECT
*
*   SECONDARY PANVALET LIBRARY INITIALIZATION
*
*  THIS ROUTINE ALLOCATES A SECONDARY PANVALET LIBRARY IF ONE IS
*  LOGICALLY ASSOCIATED WITH THE PRIMARY PANVALET LIBRARY PREVIOUSLY
*  SPECIFIED.  INFORMATION FROM THIS ADDITIONAL LIBRARY IS USED
*  LATER TO MAINTAIN VARIOUS TYPES OF COORDINATION BETWEEN THE
*  PAIRED LIBRARIES.
*  CURRENTLY ONLY FPC.WORK AND FPC.SOURCE ARE PAIRED.
*  FUTURES INCLUDE FPC.PROCLIB AND FPC.TEST.PROCLIB.
*
         SPACE 3
SLIBINIT PENTRY
         SPACE 2
         IF    (CLI,PDLCTLKY+1,1,EQ)   IF CONTROL SPECIFIED
               PEXIT               EXIT...NO LIBRARY SEARCH REQUIRED
         FI
         SPACE 2
         L     R1,PDLLIB           PDL FOR PRIMARY LIBRARY
         IF    (CLC,=C'FPC.WORK',0(R1),EQ) IF LIBNAME = FPC.WORK
               LA    R0,=C'FPC.SOURCE' BUILD A DUMMY PDL
               LA    R1,10
         ELSEIF  (CLC,=C'FPC.SOURCE',0(R1),EQ) IF LIBNAME = FPC.SOURCE
               LA    R0,=C'FPC.WORK'   BUILD A DUMMY PDL
               LA    R1,8
         SPACE 2
*  ADD ADDITIONAL PAIRED LIBRARY CODE HERE
         SPACE 2
         ELSE
               PEXIT               EXIT IF NO PAIRED LIBRARY
         FI
         ST    R0,PDLDUMMY         BUILD A DUMMY PDL
         STH   R1,PDLDUMMY+4       FOR ALLOCATION
         MVC   PDLDUMMY+6(2),=X'C000'
         XC    PDLDUMMY+8(8),PDLDUMMY+8 CLEAR MEMBER AREA
         LA    R1,PDLDUMMY         LIBRARY PDL ADDRESS
         PM    ALLOCSHR            ATTEMPT TO ALLOCATE THE LIB
         IF    (LTR,R15,R15,NZ)    IF RET CODE NOT ZERO
               PM    DAIRRC        ANALYZE DAIR RET CODE
               OI    ERR,PAMERR    FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         OI    LIBSTAT,SLIBALC      INDICATE SECONDARY LIB ALLOCATED
         USING DAPB08,R1
         MVC   SLIBDDNM,DA08DDN    SAVE DDNAME
         DROP  R1
         SR    R0,R0               CLEAR
         ST    R0,PAMACTN          SET UP OPEN PARMS
         MVC   PAMDDNM,SLIBDDNM
         MVC   PAMBKUP,NOENTRY
         CALLPAM POPEN,(PAMACTN,PAMDDNM,PAMBKUP)
         IF    (CLI,PAMACTN3,0,NE) IF PAM ACTN CODE NOT ZERO
               PM    PAMANAL       ANALYZE PAM RET CODE
               OI    ERR,PAMERR    FLAG ERROR
               IF    (CLI,PAMACTN3,24,EQ)  IF NOT A PAN LIB
                     OI    LIBSTAT,PLIBOPN INDICATE OPEN
               FI
               PEXIT               EXIT IMMEDIATELY
         FI
         OI    LIBSTAT,SLIBOPN      FLAG PRIMARY LIB OPEN
         PEXIT
         EJECT
*
*   SECONDARY PANVALET LIBRARY SEARCH ROUTINE
*
*  THIS ROUTINE SEARCHS THE SECONDARY PANVALET LIBRARY FOR THE
*  PAN-DSNAME SPECIFIED.  IF IT IS FOUND THE 0-UP DIRECTORY
*  ENTRY IS STORED IN SLIBDIR AND THE SECONDARY LIB DATASET
*  FOUND BIT IN THE LIBSTAT FIELD IS TURNED ON.
*  THIS INFORMATION WILL BE USED LATER TO DETERMINE VARIOUS
*  OPERATIONAL OPTIONS.
*
         SPACE 3
SLIBSRCH PENTRY
         SPACE 2
         IF    (TM,LIBSTAT,SLIBOPN,Z)  IF SEC LIB NOT OPEN
               PEXIT               EXIT IMMEDIATELY
         FI
         SR    R0,R0               PREPARE PARAMETERS FOR PSRCH
         ST    R0,PAMACTN
         MVC   SLIBDIR,BLANKS
         MVC   PAMCMNT,NOENTRY
         MVC   PAMSUBST,NOENTRY
         MVC   PAMNAME2,NOENTRY
         MVC   PAMNAME1,BLANKS
         L     R1,PDLPANDS
         LH    R2,PDLPANDS+4
         BCTR  R2,0
         EX    R2,MVCPNM1
*        MVC   PAMNAME1(*-*),0(R1)
         CALLPAM PSRCH,                                                X
               (PAMACTN,SLIBDIR,PAMNAME1,PAMNAME2,PAMCMNT,PAMSUBST)
         IF    (CLI,PAMACTN3,0,EQ) IF PAM ACTN CODE ZERO
               OI    LIBSTAT,SLIBDSF   INDICATE DATASET FOUND
         ELSEIF  (CLI,PAMACTN3,23,NE)  ELSE IF PAM CODE OTHER THAN 23
               PM    PAMANAL       ANALYZE ERROR
               OI    ERR,PAMERR    FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         PEXIT
         EJECT
*
*   SECONDARY PANVALET LIBRARY CLOSE AND DEALLOCATE ROUTINE
*
*  THIS ROUTINE CLOSES THE SECONDARY PANVALET LIBRARY IF OPEN
*  VIA A CALL TO PCLOSE.  IN ADDITION THE PAM MODULE WHICH
*  IS LOADED DYNAMICALLY BY A SPECIAL POPEN INTERFACE IS
*  IS DELETED.  IF THE SECONDARY LIB IS ALLOCATED IT IS DE-
*  ALLOCATED.
*
         SPACE 3
SLIBTERM PENTRY
         SPACE 2
         IF    (TM,LIBSTAT,SLIBOPN,O)    IF SECONDARY LIB OPEN
               SR    R0,R0
               ST    R0,PAMACTN
               CALLPAM PCLOSE,(PAMACTN)
               NI    LIBSTAT,255-SLIBOPN    INDICATED SLIB CLOSED
         FI
         IF    (TM,LIBSTAT,SLIBALC,O)   IF SECONDARY LIB ALLOCATED
               LA    R1,SLIBDDNM   DE-ALLOCATE SLIB
               PM    UNALLOC
               PM    DAIRRC        ANALYZE POSSIBLE RET CODE
               NI    LIBSTAT,255-SLIBALC    INDICATED SLIB DE-ALLOCATED
         FI
         PEXIT
         EJECT
*
*  TSO SOURCE DATASET INITIALIZATION ROUTINE
*
*  THIS ROUTINE ALLOCATES THE TSO SOURCE DATASET USING A SHARED
*  DISPOSITION.  IF THE NAME IS PROVIDED USING THE DATASET KEYWORD
*  IT IS USED AS IS.  IF NO NAME IS PROVIDED ONE IS GENERATED
*  USING THE PAN-DSNAME PLUS AN APPROPRIATE QUALIFIER IF POSSIBLE.
*  THE QUALIFIER IS OBTAINED FROM THE TYPE KEYWORD IF IT IS AN
*  ADD OR FROM THE PAN DIRECTORY IF IT IS AN UPDATE.  IF NO
*  QUALIFIER CAN BE DEDUCED, THEN WE SIMPLY DO NOT APPEND ONE
*  AND USE   USERID.PANDSN   .
*
         SPACE 3
DSINIT   PENTRY
         SPACE 2
         IF    (TM,PDLDS+6,X'80',Z)  IF DSNAME KEYWORD NOT SPECIFIED
               MVC   DSDFLT,BLANKS CLEAR WORK AREA
               L     R1,PDLPANDS   PAN DSNAME ADDRESS
               LH    R2,PDLPANDS+4 PAN DSNAME LENGTH
               IF    (CLI,PDLPANDS+5,8,H)  IF DSNAME LENGTH > 8
                     LA    R2,8    TRUNCATE TO 8
               FI
               BCTR  R2,0          DECREMENT FOR EXECUTE
               EX    R2,MVCDSWRK   MOVE DSNAME TO WORK AREA
*              MVC   DSDFLT(*-*),0(R1) MODEL FOR MVCDSWRK
               LA    R2,DSDFLT+1(R2)   END OF DSNAME + 1
               SR    R3,R3         CLEAR
               IF    (CLI,PDLAUKY+1,1,EQ)  IF ADD KEYWORD SPECIFIED
                     IF    (TM,PDLTYPE+6,X'80',O)  TEST FOR TYPE KEYWD
                           L     R3,PDLTYPE    QUALIFIER ADDR
                           LH    R4,PDLTYPE+4  QUALIFIER LENGTH
                           BCTR  R4,0          DECREMENT FOR MVC
                           LA    R6,TYPETBL-TYPETBLL  TYPE XREF
                           DO    UNTIL,(EX,R4,CLCDSSFX,EQ),OR,         X
               (CLI,0(R6),X'FF',EQ)
*              DO UNTIL TYPE FOUND OR END OF TABLE
                                 LA    R6,TYPETBLL(,R6) NEXT ENTRY
                           OD
                           IF    (CLI,0(R6),X'FF',EQ) IF END OF TBL
                                 SR    R3,R3   INDICATE NO SUFFIX
                           ELSE
                                 LA    R3,16(,R6)  TSO DS TYPE
                                 IC    R4,15(,R6)  LENGTH
                           FI
                     FI
               ELSE  ,             ELSE ASSUME UPDATE
                     IF    (TM,LIBSTAT,PLIBDSF,O)  IF DSF IN PRIM LIB
                           LA    R3,PLIBDIR+18
                     ELSEIF (TM,LIBSTAT,SLIBDSF,O) ELSE IF DSF IN SEC
                           LA    R3,SLIBDIR+18
                     FI
                     IF    (LTR,R3,R3,NZ)  IF PAN DS FOUND
                           LA    R6,TYPETBL-TYPETBLL TYPE TABLE START
                           DO    UNTIL,(CLC,0(5,R3),0(R6),EQ),OR,      X
               (CLI,0(R6),255,EQ)
                                 LA    R6,TYPETBLL(,R6) NEXT ENTRY
                           OD
                           IF    (CLI,0(R6),255,EQ) IF END OF XREF
                                 SR    R3,R3  SHOW NO QUAL
                           ELSE  ,                  QUALIFIER FOUND
                                 LA    R3,16(,R6)  QUAL ADDR
                                 SR    R4,R4  QUAL LENGTH
                                 IC    R4,15(,R6)
                           FI
                     FI
               FI
               IF    (LTR,R3,R3,NZ)     IF QUALIFIER FOUND
                     MVI   0(R2),C'.'  SET SEPARATOR
                     EX    R4,MVCDSSFX MOVE DATASET QUAL
*                    MVC   1(*-*,R2),0(R3)
                     LA    R2,2(R4,R2) END OF DSNAME + 1
               FI
               LA    R3,DSDFLT     START OF DSNAME
               SR    R2,R3         LENGTH OF DSNAME
               STH   R2,PDLDS+4    SAVE LENGTH IN PDL
               ST    R3,PDLDS      SAVE ADDR OF DSNAME
               OI    PDLDS+6,X'80' INDICATE DSNAME GIVEN
         FI
         LA    R1,PDLDS            ADDR OF DSNAME PDL
         PM    ALLOCSHR            ALLOCATE DATASET
         IF    (LTR,R15,R15,NZ)    IF RET CODE NOT ZERO
               PM    DAIRRC        CALL DAIR ERROR ANALYSIS
               OI    ERR,DSERR     FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         USING DAPB08,R1
         OI    STATUS,INALC        INDICATE INPUT DATASET ALLOCATED
         MVC   DSDDNM,DA08DDN      SAVE DDNAME OF DATASET
         SPACE 2
         IF    (TM,DA08DSO,X'02',O),AND,(TM,PDLDS+14,X'80',Z)
*              IF DS IS PARTITIONED AND NO MEMBER SPECIFIED
               DROP  R1            PDS SPECIFIED BUT NO MEMBER
               LA    R1,DSERRM1    ERROR MESSAGE
               PM    TERMPUT       OUTPUT IT
               OI    ERR,DSERR     FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         PEXIT
         SPACE 3
MVCDSWRK MVC   DSDFLT(*-*),0(R1)   MVC MODELS
MVCDSSFX MVC   1(*-*,R2),0(R3)
CLCDSSFX CLC   0(*-*,R3),5(R6)
         SPACE 1
DSERRM1  $MSG  'SOURCE DATASET SPECIFIED IS PARTITIONED, IT MUST BE SEQX
               UENTIAL OR A PDS MEMBER'
         EJECT
*
*  COPY INITIALIZATION ROUTINE
*
*  THIS ROUTINE IS RESPONSIBLE FOR BUILDING NECESSARY PANVALET
*  CONTROL CARDS FOR USE BY THE EXIT DEPENDING ON THE INPUT PARMS.
*  IN ADDITION IT WILL ALLOCATE THE DUMMY DATASET AND ATTRIBUTE
*  LIST FOR PANVALET SYSPRINT AND BUILD THE PARM LIST OF DDNAMES
*  TO BE USED FOR THE DYNAMIC LINK TO PAN#1.  ALSO THE PANEXIT
*  IS IDENTIFIED AS PAN1EXIT FOR DYNAMIC INVOCATION BY A SPECIAL
*  PANEXIT INTERFACE.
*
         SPACE 3
COPYINIT PENTRY
         SPACE 2
         IF    (CLI,PDLCTLKY+1,1,NE)   IF NOCONTROL SPECIFIED
               MVC   PANINPRE,BLANKS   BLANK OUT WORK AREAS
               MVC   PANINPST,BLANKS
               IF    (TM,LIBSTAT,PLIBDSF,O)    IF DATASET FOUND IN PLIB
                     IF    (CLI,PDLAUKY+1,1,EQ)    IF ADD SPECIFIED
                           LA    R1,COPYIM1    ERROR IF SO
                           PM    TERMPUT
                           OI    ERR,UERR  FLAG AS USER ERROR
                           PEXIT   EXIT IMMEDIATELY
                     FI
                     MVC   PANINPRE(27),=C'++UPDATE XXXXXXXXXX,000,ALL'
                     MVC   PANINPRE+9(10),PLIBDIR+65  SET PAN DSNAME
                     MVC   PANINPRE+20(3),PLIBDIR+10  SET LEVEL NUMBER
                     OI    PANINPRE+22,C'0'    FIX ZONE
                     OI    PANSTAT,PREIN   INDICATE PRE-INPUT AVAILABLE
               ELSEIF  (TM,LIBSTAT,SLIBDSF,O)  ELSE IF DS FOUND IN SLIB
                     IF    (CLI,PDLAUKY+1,1,EQ)    IF ADD SPECIFIED
                           LA    R1,COPYIM2    ERROR IF SO
                           PM    TERMPUT
                           OI    ERR,UERR  FLAG AS USER ERROR
                           PEXIT   EXIT IMMEDIATELY
                     FI
                     MVC   PANINPRE(16),=C'++ADD XXXXXXXXXX'
                     MVC   PANINPRE+6(10),SLIBDIR+65   SET PAN DSNAME
                     LA    R5,TYPETBL-TYPETBLL  TYPE TABLE START
                     DO    UNTIL,(CLC,0(5,R5),SLIBDIR+18,EQ),OR,       X
               (CLI,0(R5),255,EQ)
*              DO UNTIL QUALIFIER FOUND OR END OF TABLE
                           LA    R5,TYPETBLL(,R5)  NEXT ELEMENT
                     OD
                     IF    (CLI,0(R5),255,NE)  IF NOT END OF TABLE
                           MVI   PANINPRE+16,C','  SET COMMA
                           MVC   PANINPRE+17(10),5(R5)  SET TYPE
                     FI
                     MVC   PANINPST(23),=C'++LEVEL XXXXXXXXXX,001,'
                     MVC   PANINPST+8(10),SLIBDIR+65  SET PAN DSNAME
                     PACK  DWORK,SLIBDIR+10(3) LEVEL NUMBER
                     AP    DWORK,=P'1'       + 1
                     UNPK  PANINPST+23(3),DWORK+6(2)
                     OI    PANINPST+25,C'0'    FIX SIGN
                     OI    PANSTAT,PREIN+POSTIN  FLAG DATA AVAILABLE
               ELSE  ,             DATASET NOT FOUND IN EITHER LIBRARY
                     IF    (CLI,PDLAUKY+1,1,NE)    IF UPDATE SPECIFIED
                           LA    R1,COPYIM3    ERROR
                           PM    TERMPUT
                           OI    ERR,UERR  FLAG AS USER ERROR
                           PEXIT   EXIT IMMEDIATELY
                     FI
                     MVC   PANINPRE(6),=C'++ADD '
                     L     R2,PDLPANDS    SET PAN DSNAME IN INPUT CARD
                     LH    R3,PDLPANDS+4
                     BCTR  R3,0
                     EX    R3,MVCINIT1 MVC   PANINPRE+6(*-*),0(R2)
                     LA    R2,PANINPRE+7(R3)   NEXT AVAILABLE BYTE
                     IF    (TM,PDLTYPE+6,X'80',O)  IF TYPE SPECIFIED
                           L     R3,PDLTYPE    LOAD CHAR STRING ADDR
                           LH    R4,PDLTYPE+4   AND LENGTH
                           BCTR  R4,0
                           LA    R5,TYPETBL-TYPETBLL  TYPE TABLE START
                           DO    UNTIL,(CLI,0(R5),255,EQ),OR,          X
               (EX,R4,CLCINIT1,EQ)    CLC   5(*-*,R5),0(R3)
*              DO UNTIL END OF TABLE OR ENTRY EQUAL COMPARE
                                 LA    R5,TYPETBLL(,R5) NEXT ENTRY
                           OD
                           IF    (CLI,0(R5),255,NE)  IF ENTRY FOUND
                                 MVI   0(R2),C','
                                 MVC   1(10,R2),5(R5)  MOVE PAN TYPE
                            ELSE ,     ISSUE ERROR MSG FOR BAD TYPE
                                 LA    R1,BADTYPMG  BAD TYPE MSG
                                 PM    TERMPUT
                                 OI    ERR,UERR  FLAG AS USER ERROR
                                 PEXIT      EXIT IMMEDIATELY
                           FI
                     FI
                     OI    PANSTAT,PREIN  SET PRE INPUT EXIT FLAG
               FI
         ELSE ,                    CONTROL SPECIFIED
               OI    PANSTAT,CONTROL   FLAG FOR EXIT
         FI
*
*  ALLOCATE A DUMMY SYSPRINT DATA SET
*
         SR    R1,R1
         PM    ALLOCNUL            ALLOCATE NULL DS
         IF    (LTR,R15,R15,NZ)    IF DAIR ERROR
               PM    DAIRRC
               OI    ERR,DSERR     FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         USING DAPB08,R1
         MVC   OUTDDNM,DA08DDN     SAVE DDNAME
         DROP  R1
         OI    STATUS,OUTALC       INDICATE SYSOUT NULL ALLOCATED
*
*  BUILD LINK PARAMETER LIST
*
         MVC   PANPARM(PANMDLL),PANMDL  SET MODEL
         MVC   PANPARM1,DSDDNM     SYSIN DDNAME
         MVC   PANPARM2,OUTDDNM    SYSPRINT DDNAME
         MVC   PANPARM3,PLIBDDNM   PANDD1 DDNAME
*
*  IDENTIFY PAN EXIT FOR DYNAMIC LOAD BY SPECIAL PAN EXIT INTERFACE
*
         L     R2,=V(PANEXIT)      PAN EXIT ROUTINE ADDRESS
         IDENTIFY  EP=PAN1EXIT,ENTRY=(R2)
         IF    (LTR,R15,R15,NZ)    IF IDENTIFY ERROR
               LA    R1,IDENTERR   ISSUE ERROR MESSAGE
               PM    TERMPUT
               OI    ERR,TSOCP     FLAG ERROR INDICATOR
               PEXIT               EXIT IMMEDIATELY
         FI
         SPACE 2
         LA    R1,=CL8'PANDD2'     DEALLOCATE PANDD2 AND
         PM    UNALLOC             SYSPUNCH IF PREVIOUSLY
         LA    R1,=CL8'SYSPUNCH'   ALLOCATED
         PM    UNALLOC
         SPACE 2
         PEXIT
         SPACE 3
* CONSTANTS AND MESSAGES
MVCINIT1 MVC   PANINPRE+6(*-*),0(R2)
CLCINIT1 CLC   5(*-*,R5),0(R3)
COPYIM1  $MSG  'ADD SPECIFIED BUT PAN DSNAME ALREADY EXISTS IN PANLIB'
COPYIM2  $MSG  'ADD SPECIFIED BUT PAN DSNAME ALREADY IN PAIRED LIBRARY'
COPYIM3  $MSG  'UPDATE SPECIFIED BUT PAN DSNAME NOT IN PAN LIBRARY'
BADTYPMG $MSG  'INVALID PANVALET DATASET TYPE CODE SPECIFIED'
IDENTERR $MSG  'PAN1EXIT IDENTIFY ERROR  RC =',RC=YES,SFX=YES
         SPACE 2
PANMDL   DS    0H                  PAN#1 PARM LIST MODEL
         DC    AL2(PANMDLL-2)      PARM LIST LENGTH
         DC    CL8' ',C',',CL8' ',C',',CL8' '
PANMDLL  EQU   *-PANMDL
         SPACE 2
*  ------------------------------------------------------------
*  ×   PANLIB TYPE   ×   CONTROL CARD TYPE   ×   TSO SUFFIX   ×
*  ------------------------------------------------------------
TYPETBLL EQU   21                  TYPE TABLE ELEMENT LENGTH
TYPETBL  DS    0C                  PAN DATASET TYPE VALIDATION TABLE
         XE    AUTOC,AUTOCODER,DATA
         XE    ASMB,BAL,ASM
         XE    ASMB,ALC,ASM
         XE    COBOL,COBOL,COBOL
         XE    ANSCB,ANSCOBOL,COBOL
         XE    COB72,COBOL-72,DATA
         XE    FORT,FORTRAN,FORT
         XE    PL/1,PL/1,PL1
         XE    RPG,RPG,DATA
         XE    OBJCT,OBJECT,OBJ
         XE    JCL,JCL,CNTL
         XE    DATA,DATA,DATA
         XE    OTHER,OTHER,DATA
         XE    USER1,USER780,DATA
         XE    USER2,USER180,DATA
         DC    X'FF'
         EJECT
*
*  COPY ROUTINE
*
*  THIS ROUTINE LINKS TO PAN#1.  PAN#1 HAS A SPECIAL PAN EXIT INTERFACE
*  MODULE WHICH LOADS A MODULE PAN1EXIT WHICH WAS PREVIOUSLY IDENTIFIED
*  IN COPYINIT.  PAN1EXIT WILL CONTROL THE EXECUTION OF THE PAN#1
*  PROGRAM FROM THE "BACKEND".  SEE PANEXIT FOR FURTHER INFORMATION.
*  ON TERMINATION OF PAN#1, THE RETURN CODE WILL BE CHECKED AND A MSG
*  ISSUED IF IT IS NON-ZERO.
         SPACE 3
COPY     PENTRY
         LA    R2,PANPARM          PARM LIST ADDRESS
         OI    PANSTAT,PANACTV     INDICATE PAN#1 IS ACTIVE
         LINK  EP=PAN#1,PARAM=((R2)),VL=1,MF=(E,CALLMFL)
         NI    PANSTAT,255-PANACTV INDICATE RETURN FOR PAN#1
         IF    (LTR,R15,R15,NZ)    IF ERROR FROM PAN#1
               LA    R1,PANRTNCD   ISSUE ERROR MESSAGE
               PM    TERMPUT
         FI
         PEXIT
         SPACE 2
PANRTNCD $MSG  'PANVALET RETURN CODE =',RC=YES
         EJECT
*
*  COPY TERMINATION ROUTINE
*
*  THIS ROUTINE CONDITIONALLY FREES THE SYSPRINT DUMMY DATASET
*  AND THE SYSPRINT ATTRIBUTE LIST.
         SPACE 3
COPYTERM PENTRY
         IF    (TM,STATUS,OUTALC,O)    IF SYSPRINT ALLOCATED
               LA    R1,OUTDDNM    FREE IT
               PM    UNALLOC
               NI    STATUS,255-OUTALC SHOW SYSPRINT DEALLOCATED
         FI
         PEXIT
         EJECT
*
*  INPUT DATASET TERMINATION ROUTINE
*
         SPACE 3
DSTERM   PENTRY
         SPACE 2
         IF    (TM,STATUS,INALC,O) IF INPUT DATASET ALLOCATED
               LA    R1,DSDDNM     DEALLOCATE IT
               PM    UNALLOC
               NI    STATUS,255-INALC  SHOW INPUT DEALLOCATED
         FI
         PEXIT
         EJECT
*
*  PRIMARY LIBRARY TERMINATION ROUTINE
*
         SPACE 3
PLIBTERM PENTRY
         SPACE 2
         IF    (TM,LIBSTAT,PLIBALC,O)  IF PRIMARY PAN LIB ALLOCATED
               LA    R1,PLIBDDNM   DEALLOCATE IT
               PM    UNALLOC
               NI    LIBSTAT,255-PLIBALC
         FI
         PEXIT
         EJECT
*
*  ATTENTION EXIT TERMINATION
*
         SPACE 2
STAXTERM PENTRY
         SPACE 1
         IF    (TM,STATUS,STAXACTV,O)
               STAX
               NI    STATUS,255-STAXACTV   DENOTE STAX NOT ACTIVE
         FI
         PEXIT
         EJECT
*
*  GENERAL TERMINAL MESSAGE PUT ROUTINE
*
         SPACE 2
TERMPUT  PENTRY
         SPACE 2
         LH    R14,0(,R1)          LENGTH OF MESSAGE- 1
         EX    R14,MVCTMSG         MOVE MODEL TO WORK AREA
         SPACE 1
         LA    R14,TERMSG+1(R14)   END OF MESSAGE
         IF    (TM,2(R1),X'80',O)
               N     R15,TERMMASK  MASK WITH 0000FFFF
               CVD   R15,DWORK     PLACE RC IN MESSAGE
               MVC   0(6,R14),=X'402020202120' EDIT RC INTO
               ED    0(6,R14),DWORK+5  MESSAGE TEXT
               LA    R14,6(,R14)   END OF MODIFIED MSG
         FI
         SPACE 1
         IF    (TM,2(R1),X'40',O)  TEST FOR DAIR RET CODE
               UNPK  0(5,R14),0(3,R15)
               TR    0(4,R14),TMSGTR-240   TRANSLATE TO HEX
               LA    R14,4(,R14)
         FI
         SPACE 1
         IF    (TM,2(R1),X'20',O)  TEST FOR SUFFIX
               MVC   0(L'MSGSFX,R14),MSGSFX
               LA    R14,L'MSGSFX(,R14)
         FI
         SPACE 2
         IF    (TM,2(R1),X'10',O)  TEST FOR DSNAME PRINT
               STM   R2,R5,TERMSAVE SAVE REGISTERS TEMPORARILY
               LR    R2,R0         USE ADDRESSIBLE REG
               IF    (TM,6(R2),X'80',O)    TEST FOR DSNAME PRESENT
                     LH    R3,4(,R2)   GET LENGTH
                     BCTR  R3,0    DECREMENT FOR EX
                     L     R4,0(,R2)   DSNAME ADDR
                     EX    R3,MVCDSNMG MOVE DSNAME TO MESSAGE
                     LA    R14,1(R3,R14)   ADDR OF END OF MSG
               FI
               IF    (TM,14(R2),X'80',O)   TEST FOR MEMBER PRESENT
                     LH    R3,12(,R2)  GET LENGTH
                     BCTR  R3,0    DECREMENT FOR EX
                     L     R4,8(,R2)   MEMBER NAME ADDRESS
                     EX    R3,MVCMBRMG MOVE MEMBER TO MESSAGE
                     MVI   0(R14),C'('
                     LA    R14,2(R3,R14)   NEW END OF MESSAGE
                     MVI   0(R14),C')'
                     LA    R14,1(,R14)
               FI
               LM    R2,R5,TERMSAVE
         FI
         SPACE 2
         LR    R0,R14
         LA    R1,TERMSG
         SR    R0,R1               LENGTH OF MESSAGE
         LA    R1,TERMSG           START OF MESSAGE
         SPACE 1
         TPUT  (1),(0),R
         SPACE 1
         PEXIT
         SPACE 2
MSGSFX   DC    C'...NOTIFY CSD TECH SUPPORT'
TERMMASK DC    0F'0',X'0000FFFF'
*
MVCTMSG  MVC   TERMSG(*-*),3(R1)
MVCDSNMG MVC   0(*-*,R14),0(R4)
MVCMBRMG MVC   1(*-*,R14),0(R4)
         SPACE 1
TMSGTR   DC    0F'0',C'0123456789ABCDEF'
         EJECT
*
*  SHARED DISPOSITION DATA SET ALLOCATION ROUTINE
*
         SPACE 2
ALLOCSHR PENTRY
         SPACE 2
         MVC   DAIRWORK(DA08MDLL),DA08MDL MOVE MODEL TO WORK
         LA    R9,DAIRWORK         ESTABLISH DA08
         USING DA08CD,R9             ADDRESSIBILITY
         MVC   DA08DSP1(3),=X'080808'  DISP=(,CATLG,DELETE)
         IF    (TM,6(R1),X'40',Z)
               OI    DA08CTL,X'20' PREFIX WITH USERID
         FI
         IF    (TM,14(R1),X'80',O) TEST FOR MEMBER NAME
               LH    R2,12(,R1)    MOVE MEMBER NAME TO DA08
               BCTR  R2,0
               L     R3,8(,R1)
               EX    R2,MVCMBRNM
         FI
         SPACE 1
         LH    R2,4(,R1)           DSNAME LENGTH
         L     R3,0(,R1)           --> DSN
         STH   R2,DSNWORKL         SET IN WORK AREA
         BCTR  R2,0                DEC FOR EX
         EX    R2,MVCDSN           MOVE DSN TO WORK AREA
         LA    R1,DSNWORK          SET DSN POINTER IN DAPB
         ST    R1,DA08PDSN
         LR    R1,R9
         PM    DAIR                PERFORM COMMON DAIR PROCESSING
         SPACE 2
         LR    R1,R9               RETURN ADDRESS OF DAPB
         DROP  R9                  DROP DAPB ADDRESSIBILITY
         SPACE 2
         PEXIT
MVCDSN   MVC   DSNWORKC(*-*),0(R3)
         USING DAPB08,R9
MVCMBRNM MVC   DA08MNM(*-*),0(R3)
         DROP  R9
         EJECT
*
*  ALLOCATE DUMMY DATA SET ROUTINE W/ATTRLIST
*
*  R1 = 0   TO DENOTE A NULL DATASET REQUEST
*  REG USE R15,R0,R1,R9
         SPACE 3
ALLOCNUL PENTRY
         SPACE 2
         MVC   DAIRWORK(DA08MDLL),DA08MDL  SET MODEL
         LA    R9,DAIRWORK         ESTABLISH DAPB08
         USING DAPB08,R9             ADDRESSIBILITY
         MVC   DA08DSP1(4),=X'04040404'    NEW,DELETE,DELETE  NULLFILE
         LR    R1,R9
         DROP  R9
         PM    DAIR                PERFORM DAIR FUNCTION
         LR    R1,R9               RETURN DAPB ADDRESS
         PEXIT
         EJECT
*
*  DE-ALLOCATION ROUTINE
         SPACE 2
UNALLOC  PENTRY
         SPACE 2
         MVC   DAIRWORK(DA18MDLL),DA18MDL  INIT DAPB WITH MODEL
         MVC   DAIRWORK+DA18DDN-DAPB18(8),0(R1)   SET DDNAME
         LA    R1,DAIRWORK
         PM    DAIR                PERFORM DAIR FUNCTION
         SPACE 2
         PEXIT
         EJECT
*
*  DAIR RETURN CODE ANALYSIS ROUTINE
*
         SPACE 2
DAIRRC   PENTRY
         SPACE 2
         IF    (LTR,R15,R15,Z)     TEST FOR FALSE ENTRY
               PEXIT               EXIT IF SO
         ELSEIF  (CH,R15,=H'16',EQ)    TEST FOR NO TIOT ROOM
               LA    R1,DRC16      ERROR MSG
         ELSEIF  (CH,R15,=H'24',EQ)    TEST FOR CONCAT GROUP
               LA    R1,DRC24      ERROR MSG
         ELSEIF  (CH,R15,=H'8',EQ),OR,(CH,R15,=H'12',EQ)
               LA    R1,DRC8       ERROR MSG
               LA    R15,DAIRWORK+4    DAIR ERROR CODE ADDR
         ELSE
               LA    R1,DRCELSE    ALL ELSE MESSAGE
         FI
         PM    TERMPUT             OUTPUT MESSAGE
         PEXIT
         SPACE 2
         PEXIT
         SPACE 2
DRC8     $MSG  'DAIR ERROR CODE:',DC=YES
DRC16    $MSG  'NO ALLOCATION ROOM..DO A FREEALL AND TRY AGAIN'
DRC24    $MSG  'DSNAME IS CONCATENATED..DO A FREEALL AND TRY AGAIN'
DRCELSE  $MSG  'DAIR RETURN CODE:',RC=YES,SFX=YES
         EJECT
*
*  PAM RETURN CODE ANALYSIS ROUTINE
*
         SPACE 2
PAMANAL  PENTRY
         SPACE 2
         IF    (CLI,PAMACTN3,0,EQ)
               PEXIT               EXIT IF NO ERROR
         ELSEIF  (CLI,PAMACTN3,23,EQ)
               LA    R1,PAMERR23   FLAG PAN DATA SET NOT FOUND
         ELSEIF  (CLI,PAMACTN3,24,EQ)  IF BAD LIBRARY
               LA    R1,PAMERR24   BAD LIBRARY MESSAGE
         ELSEIF  (CLI,PAMACTN3,245,EQ)
               LA    R1,PAMERRF5   FLAG PARAMETER LIST ERROR
         ELSE
               LH    R15,PAMACTN+2 FORMAT ALL OTHER ERROR MSGS
               LA    R1,PAMERRM           WITH RETURN CODE IN PAMACTN
         FI
         SPACE 2
         PM    TERMPUT
         SPACE 2
         PEXIT
         SPACE 2
PAMERR23 $MSG  'PAN DSNAME SPECIFIED NOT IN LIBRARY'
PAMERR24 $MSG  'LIBRARY NAME SPECIFIED IS NOT A PANVALET LIBRARY'
PAMERRF5 $MSG  'PAM PARM ERROR',SFX=YES
PAMERRM  $MSG  'PANVALET ERROR CODE  PV',RC=YES,SFX=YES
         EJECT
*
*  COMMON DAIR INTERFACE ROUTINE
*
         SPACE 2
DAIR     PENTRY
         SPACE 2
         LA    R8,DAIRPL           ESTABLISH DAPL
         USING DAPL,R8               ADDRESSIBILITY
         SPACE 1
         ST    R1,DAPLDAPB         INITITALIZE THE DYNAMIC ALLOCATION
         L     R0,CPPLUPT          PARAMETER LIST
         ST    R0,DAPLUPT
         L     R0,CPPLECT
         ST    R0,DAPLECT
         LA    R0,ECB
         ST    R0,DAPLECB
         L     R0,CPPLPSCB
         ST    R0,DAPLPSCB
         SR    R0,R0
         ST    R0,ECB              RESET THE ECB
         SPACE 2
         LR    R1,R8
         CALLTSSR  EP=IKJDAIR,MF=(E,(1))
         DROP R8
         SPACE 2
         PEXIT
         EJECT
*
*  ATTENTION EXIT ROUTINE
*
         SPACE 2
STAXEXIT DS    0H
         L     R2,8(,R1)           ERROR INDICATOR ADDRESS
         OI    0(R2),ATTN          FLAG ATTENTION EXIT
         BR    R14                 RETURN TO PROCESS
         EJECT
*
*  CONSTANTS AND LITERALS
*
         PRINT GEN
         EQUAL
         SPACE 2
DA08MDL  DC    0F'0',X'0008',3X'0000',A(0),3CL8' ',4F'0'
         DC    2CL8' ',4X'00',XL3'00',X'00',CL8' '
DA08MDLL EQU   *-DA08MDL
         SPACE 2
DA18MDL  DC   0F'0',X'0018',3X'0000',A(0),2CL8' ',CL2' ',X'0810',CL8' '
DA18MDLL EQU   *-DA18MDL
         SPACE 2
YES      DC    CL8'YES'
NOENTRY  DC    C'NO-ENTRY'
BLANKS   DC    CL80' '
COMMENT  DC    C'COMMENT '
         EJECT
*
*  PARSE CONTROL LIST
*
         SPACE 2
PCL      IKJPARM DSECT=PDL
         SPACE 2
PDLPANDS IKJIDENT 'PAN DATA-SET NAME',MAXLNTH=10,OTHER=ALPHANUM,       X
               ASTERISK
         SPACE 2
PDLLIBKY IKJKEYWD
         IKJNAME 'LIBRARY',SUBFLD=PDLLIBSF
         SPACE 2
PDLDSKY  IKJKEYWD
         IKJNAME 'DATASET',SUBFLD=PDLDSSF
         SPACE 2
PDLAUKY  IKJKEYWD
         IKJNAME 'ADD',SUBFLD=PDLTYPSF
         IKJNAME 'UPDATE'
         SPACE 2
PDLCTLKY IKJKEYWD
         IKJNAME  'CONTROL'
         IKJNAME  'NOCONTROL'
         SPACE 2
PDLLIBSF IKJSUBF
PDLLIB   IKJPOSIT DSNAME,PROMPT='PANVALET LIBRARY NAME'
         SPACE 2
PDLDSSF  IKJSUBF
PDLDS    IKJPOSIT DSNAME
         SPACE 2
PDLTYPSF IKJSUBF
PDLTYPE  IKJIDENT 'PANVALET DATASET TYPE',MAXLNTH=10,                  X
               PROMPT='PANVALET DATASET TYPE',OTHER=ANY
         SPACE 2
         IKJENDP
         EJECT
*
*  GETMAINED WORK AREA
*
         SPACE 2
WORK     DSECT
*
*  DSECT IDENTIFICATION....PANEXIT SEARCHES FOR THIS
*
WORKID   DS    CL8
         SPACE 2
*
*  CONTROL FLAGS FOR THIS COMMAND PROCESSOR
*
ERR      DS    X                   ERROR CONTROL INDICATOR
PARSERR  EQU   X'80'                 PARSE ROUTINE ERROR
PANERR   EQU   X'40'                 PANVALET INTERFACE ERROR
PAMERR   EQU   X'40'                 PAM INTERFACE ERROR
DSERR    EQU   X'20'                 TSO DATASET ERROR
TSOCP    EQU   X'10'                 TSO CTRL PGM GEN ERROR
UERR     EQU   X'08'                 USER SPECIFICATION ERROR
ATTN     EQU   X'01'                 ATTN INDICATOR
         SPACE 2
STATUS   DS    X                   FILE STATUS INDICATOR
INALC    EQU   X'80'                 IN DATASET ALLOCATED
OUTALC   EQU   X'40'                 OUT DATASET ALLOCATED
STAXACTV EQU   X'10'                 ATTN EXIT ACTIVE
         SPACE 2
LIBSTAT  DS    X                   PANVALET LIBRARY STATUS INDICATOR
PLIBALC  EQU   X'80'                 PRIMARY LIBRARY ALLOCATED
PLIBOPN  EQU   X'40'                 PRIMARY LIBRARY OPEN
PLIBDSF  EQU   X'20'                 DATASET FOUND IN PRIMARY LIBRARY
SLIBALC  EQU   X'08'                 SECONDARY LIBRARY ALLOCATED
SLIBOPN  EQU   X'04'                 SECONDARY LIBRARY OPEN
SLIBDSF  EQU   X'02'                 DATASET FOUND IN SECONDARY LIBRARY
         SPACE 2
PANSTAT  DS    X                   PAN#1 INFORMATION FLAGS
PANACTV  EQU   X'80'                 PAN#1 ACTIVE
PREIN    EQU   X'40'                 PRE INPUT AVAILABLE
POSTIN   EQU   X'20'                 POST INPUT AVAILABLE
CONTROL  EQU   X'10'                 CONTROL INPUT SPECIFIED
EOF      EQU   X'08'                 EXIT END OF FILE INDICATOR
FLUSH    EQU   X'04'                 PAN#1 OUTPUT FLUSH INDICATOR
         SPACE 2
*
*  PARSE CONTROL BLOCKS
*
PARSEPL  DS    7A                  PARSE PARAMETER LIST
ANSWER   DS    A                   PARSE ANSWER AREA
ECB      DS    A                   PARSE AND DAIR ECB
         SPACE 2
*
*  DAIR CONTROL BLOCKS
*
DAIRPL   DS    5A                  DAIR PARAMETER LIST
DAIRWORK DS    21A                 WORK AREA FOR ALL DAIR PARM BLOCKS
DSNWORK  DS    0H                  DSNAME WORK AREA
DSNWORKL DS    H
DSNWORKC DS    CL44
         SPACE 2
*
*  PAM WORK AREAS
*
PAMEP    DS    A                   ADDRESS OF PAM ENTRY POINT
PAMACTN  DS    F                   ACTION
PAMACTN3 EQU   *-1
PLIBDIR  DS    CL80                PRIMARY LIB DIRECTORY
SLIBDIR  DS    CL80                SECONDARY LIB DIRECTORY
PAMNAME1 DS    CL22                NAME1
PAMNAME2 DS    CL11                NAME2
PAMCMNT  DS    CL52                COMMENT
PAMSUBST DS    CL27                SUBSET
PAMDDNM  DS    CL8                 PAM INTERFACE LIB DDNAME
PLIBDDNM DS    CL8                 PRIMARY LIB DDNAME
SLIBDDNM DS    CL8                 SECONDARY LIB DDNAME
PAMBKUP  DS    CL8                 BACKUP TAPE
         SPACE 2
         DS    0D
PANINPRE DS    CL80                PAN#1 PRE INPUT
PANINPST DS    CL80                PAN#1 POST INPUT
PANPARM  DS    0CL(PANMDLL)        PAN#1 PARM AREA
         DS    H                     PARM LENGTH
PANPARM1 DS    CL8,CL1
PANPARM2 DS    CL8,CL1
PANPARM3 DS    CL8
         SPACE 2
*
*  MISC CONTROL BLOCKS  AND WORK AREAS
*
         SPACE 2
PDLDUMMY DS    4F                  DUMMY PDL WORK AREA
         SPACE 2
DSDFLT   DS    CL16                DATASET NAME WORK AREA
DSDDNM   DS    CL8                 DATASET DDNAME SAVE AREA
         SPACE 2
OUTDDNM  DS    CL8                 SYSPRINT DATASET DDNAME
         SPACE 2
DWORK    DS    D                   WORK AREA
TERMSAVE DS    4F                  TERMPUT TEMP REG SAVEAREA
         SPACE 2
CALLMFL  CALL  ,(0,0,0,0,0,0),MF=L CALL WORK AREA
TERMSG   DS    CL160               TERM MESSAGE AREA
         SPACE 2
STAXMFL  DS    0F,XL(STAXREFL)     STAX PARAMETER LIST
         SPACE 2
WORKL    EQU   *-WORK
         PRINT GEN
         TITLE 'PARSE DSECT'
         IKJCPPL
         IKJPPL
         TITLE 'DAIR DSECT'
         IKJDAPL
         IKJDAP08
         IKJDAP18
         IKJDAP34
DAPB34L  EQU   *-DAPB34            DAPB 34 LENGTH
         IKJDACB
DAIRACBL EQU   *-DAIRACB           ACB LENGTH
         TITLE 'PAN#1 EXIT'
*
*  PAN#1 EXIT ROUTINE
*
*  THIS ROUTINE GETS CONTROL FROM PAN#1 THROUGH A SPECIAL PAN EXIT
*  INTERFACE AND AN IDENTIFY DONE IN COPYINIT.  IT WILL PASS THE
*  INPUT SET UP IN THE WORK AREA AND ALSO ABORT IF ATTN IS ENTERED.
*
         PRINT NOGEN
PANEXIT  CSECT
         SPACE 2
         DROP  R10,R11,R12,R13
         SPACE 2
         LR    R12,R15             ROUTINE ADDRESSIBILITY
         USING PANEXIT,R12
         LR    R11,R14             EXIT WORK ADDRESSIBILITY
         USING EXITWORK,R11
         LM    R8,R9,0(R1)         EXIT PARM POINTERS
         USING IOAREA,R8           IOAREA ADDRESSIBILITY
         USING IOCODES,R9          IOCODES ADDRESSIBILITY
         WORKORG WORK,R10          WORK AREA ADDRESSIBILITY
         SPACE 2
         L     R10,PUTWORK         PANPUT WORK AREA ADDRESS
         IF    (LTR,R10,R10,Z)     IF FIRST TIME THROUGH
               LR    R10,R13       CHAIN BACK THRU SAVE AREAS
               DO    UNTIL,(CLC,=C'PANPUTWK',WORKID,EQ)
*                                  DO UNTIL WORK AREA IS FOUND
                     L     R10,4(,R10) BACK TO PREVIOUS SAVE AREA
               OD
               ST    R10,PUTWORK   SAVE FOR LATER CALLS
         FI
         SPACE 3
         IF    (CLI,IOCODE,C'1',EQ),AND,(TM,PANSTAT,EOF,O)
*              IF PRE INPUT AND EOF IS SIGNALED
               MVI   IOCODE,C'G'   CAUSE PAN#1 TERMINATION
               NI    PANSTAT,255-EOF   CLEAR INDICATOR
         FI
         IF    (CLI,IOCODE,C'1',EQ),AND,(TM,PANSTAT,PREIN,O)
*              IF PRE INPUT AND PRE INPUT IS AVAILABLE
               NI    PANSTAT,255-PREIN RESET INDICATOR
               MVC   PANINPUT,PANINPRE   MOVE INPUT
               MVI   IOCODE,C'A'   INDICATE INPUT AVAILABLE
         FI
         IF    (CLI,IOCODE,C'2',EQ),AND,(CLC,=C'++',IOAREA,EQ),        X
               AND,(TM,PANSTAT,CONTROL,Z)
*              IF INPUT AND ++ CARD AND NOCONTROL SPECIFIED
               MVI   IOCODE,C'B'   SKIP INPUT
         FI
         IF    (CLI,IOCODE,C'2',EQ),AND,(TM,ERR,ATTN,O)
*              IF INPUT AND ATTN FLAGGED
               MVI   IOCODE,C'G'   REQUEST IMMEDIATE TERMINATION
         FI
         IF    (CLI,IOCODE,C'7',EQ),AND,(TM,PANSTAT,POSTIN,O)
*              IF EOF AND POST INPUT AVAILABLE
               NI    PANSTAT,255-POSTIN    RESET INDICATOR
               MVC   PANINPUT,PANINPST   SET POST INPUT
               MVI   IOCODE,C'A'   SET IOCODE TO READ
               OI    PANSTAT,EOF   SET EOF
         FI
         IF    (CLI,IOCODE,C'3',EQ)   IF OUTPUT
               IF    (CLC,=C'   P A N',IOAREA+1,EQ)  IF PAN FILE STATS
                     OI    PANSTAT,FLUSH   FLUSH REST OF OUTPUT
               FI
               IF    (TM,PANSTAT,FLUSH,Z)  IF FLUSH NOT ACTIVE
                     LA    R1,IOAREA+1
                     LA    R0,120
                     TPUT  (1),(0),R
               FI
               MVI   IOCODE,C'F'   SUPPRESS PRINT
         FI
         SPACE 3
         BR    R11                 RETURN TO PAN EXIT INTERFACE
         DROP  R8,R9,R10,R11,R12
         SPACE 2
         LTORG
         SPACE 3
IOAREA   DSECT
PANINPUT DS    CL80
IOCODES  DSECT
IOCODE   DS    C
PANID    DS    C
IORTN    DS    C
EXITWORK DSECT
         DS    2A                  RESERVED
PUTWORK  DS    A
         SPACE 5
PANPUT   CSECT
         SPACE 5
         CVT   LIST=NO,DSECT=YES
         SPACE 5
         END   PANPUT
