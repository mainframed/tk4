*          DATA SET TP410000   AT LEVEL 005 AS OF 06/27/75
         TITLE 'TP410000 - $DE DISPLAY ERROR CONDITIONS'
*              AUTHOR.   DON HIGGINS.
*              DATE.     06/23/75.
*              REMARKS.  SCAN FOR ERRORS AND DISPLAY ANY FOUND.
*                        THE FOLLOWING SCANS ARE MADE.
*
*                        1.   TCB ERRORS
*                             A.   ABEND
*                             B.   FORCED-WAIT
*                                  1.   ABEND
*                                  2.   SER
*                                  3.   RQE
*                                  5.   SMF TIMER EXIT
*                                  6.   PAGING
*                                  7.   MUST-COMPLETE
*                                  8.   REGION
*                                  9.   DAR
*                                 10.   DDR
*
*
*
*
*
*                        2.   UCB ERRORS
*                             A.   I/O ERROR ROUTINE IN CONTROL
*                             B.   LAST SENSE ERRORS
*                                  1.   COMMAND REJECT
*                                  3.   BUS OUT
*                                  4.   EQUIPMENT CHECK
*                                  5.   DATA CHECK
*
         TITLE 'MACROS'
         MACRO
         TCBW  &OFF,&MASK,&MSG
         IF    (TM,&OFF.(R2),&MASK,O)   TCB NON-DISP BIT TESTS
               MVC  DWAIT,=CL20&MSG
               PM   PWAIT
               PEXIT
         FI
         MEND
         MACRO
         UCBS  &MASK,&MSG
         IF    (TM,UCBSNS(R3),&MASK,O)  UCB SENSE BIT TESTS
               MVC  DSENSE,=CL20&MSG
               PM   PSENSE
         FI
         MEND
         TITLE 'MAINLINE'
         PRINT NOGEN
TP410000 SUBENTRY
         LM    R3,R5,0(R1)
         MVC   TERM,0(R3)
         MVI   ERRORS,NONE
         PM    SCANTCB
         PM    SCANUCB
         IF    (CLI,ERRORS,NONE,EQ)
               TPPUTMSG TERM,'NO ERRORS FOUND'
         FI
         SUBEXIT
         TITLE 'SCANTCB - SCAN TCBS FOR ERRORS'
SCANTCB  PENTRY
         L     R2,16
         L     R2,CVTHEAD(R2)
         DO    WHILE,(LTR,R2,R2,NZ)
               PM   CHECKTCB
               L    R2,TCBTCB(R2)
         OD
         PEXIT
         TITLE 'CHECKTCB - CHECK A TCB FOR ERRORS'
CHECKTCB PENTRY
         IF    (CLC,TCBCMPF(4,R2),=4X'00',NE)
               UNPK DWORK(9),TCBCMPF(5,R2)
               TR   DWORK(8),HEX-240
               MVC  DABEND,DWORK
               LR   R1,R2
               PM   GETJOBN
               MVC  ABENDJOB,0(R1)
               TPPUTMSG TERM,ABENDMSG
               MVI  ERRORS,SOME
               PEXIT
         FI
         TCBW  32,X'80','ABDUMP'
         TCBW  32,X'40','SER1'
         TCBW  32,X'20','I/O RQE EXHAUSTED'
         TCBW  32,X'08','SMF TIMER EXIT'
         TCBW  32,X'01','ABEND DUMP I/O'
         TCBW  33,X'80','TASK TERMINATED'
         TCBW  33,X'40','ABNORMAL WAIT'
         TCBW  33,X'20','EXCESSIVE PAGING'
         TCBW  33,X'10','ATTACHED DISP=NO'
         TCBW  33,X'0C','MUST-COMPLETE'
         TCBW  33,X'02','INITIATOR REGION'
         TCBW  173,X'C0','DAR'
         TCBW  173,X'30','RMS/SER'
         TCBW  173,X'08','DDR'
         PEXIT
PWAIT    PENTRY
         LR    R1,R2
         PM    GETJOBN
         MVC   DWAITJOB,0(R1)
         TPPUTMSG TERM,WAITMSG
         MVI   ERRORS,SOME
         PEXIT
         TITLE 'SCANUCB - SCAN UCBS FOR ERRORS'
SCANUCB  PENTRY
         L     R2,16
         L     R2,CVTILK2(R2)
         DO    WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    LH   R3,0(R2)
                    SRL  R3,1
                    SLL  R3,17
                    SRL  R3,16
                    PM   CHECKUCB
               FI
               AAI  R2,2
         OD
         PEXIT
         TITLE 'CHECKUCB - CHECK A UCB FOR ERRORS'
CHECKUCB PENTRY
         IF    (TM,SRTESTAT(R3),X'88',NO) ONLINE AND ALLOCATED
               PEXIT
         FI
         IF    (CLI,UCBTYP+2(R3),X'40',EQ)
               IF   (TM,UCBFL1(R3),X'01',O)
                    MVC  DUCBER,UCBNAME(R3)
                    TPPUTMSG TERM,TPERRMSG
                    MVI  ERRORS,SOME
               ELSE
                    PEXIT
               FI
         FI
         UCBS  X'80','COMMAND REJECT'
         UCBS  X'20','BUS OUT'
         UCBS  X'10','EQUIPMENT CHECK'
         UCBS  X'08','DATA CHECK'
         PEXIT
PSENSE   PENTRY
         MVC   DUCBS,UCBNAME(R3)
               TPPUTMSG TERM,UCBMSG
               MVI  ERRORS,SOME
         PEXIT
         TITLE 'GET JOBNAME IN R1 GIVEN TCB IN R1'
GETJOBN  PENTRY
         L     R14,TCBTIO(R1)
         IF    (LTR,R14,R14,Z)
               LA   R1,=CL8'SYSTEM'
               PEXIT
         FI
         IF    (CLC,0(8,R14),=CL8'INIT',EQ)
               L    R1,TCBLTC(R1)
               L    R14,TCBTIO(R1)
         FI
         IF    (CLC,0(7,R14),=CL7'HOSINIT',EQ),                        X
               AND,(CLC,8(8,R14),=CL8' ',NE)
               LA   R1,8(R14)
         ELSE
               LR   R1,R14
         FI
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
TERM     DC    CL8' '
ERRORS   LOGICAL NONE,(SOME,NONE)
CVTHEAD  EQU   X'A0'
TCBTCB   EQU   X'74'
TCBTIO   EQU   12
TCBLTC   EQU   X'88'
TCBCMPF  EQU   X'10'
DWORK    DC    CL9' '
HEX      DC    C'0123456789ABCDEF'
ABENDMSG DS    0CL60
ABENDJOB DC    CL8' '
         DC    C' TASK HAS ABENDED '
DABEND   DC    CL8' '
         FILL  ABENDMSG
WAITMSG  DS    0CL60
DWAITJOB DC    CL8' ',C' IS IN FORCED-WAIT DUE TO '
DWAIT    DC    CL20' '
         FILL  WAITMSG
CVTILK2  EQU   X'28'
SRTESTAT EQU   3
UCBFL1   EQU   6
UCBNAME  EQU   13
UCBTYP   EQU   16
UCBSNS   EQU   22
TPERRMSG DS    0CL60
DUCBER   DC    CL3' ',C' IS IN TP ERROR RECOVERY'
         FILL  TPERRMSG
UCBMSG   DS    0CL60
DUCBS    DC    CL3' ',C' LAST SENSE RETURNED '
DSENSE   DC    CL20' '
         FILL  UCBMSG
         END
