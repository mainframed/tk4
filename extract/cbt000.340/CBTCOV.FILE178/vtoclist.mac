*          DATA SET GU026300   AT LEVEL 013 AS OF 09/06/78
*          DATA SET GU026300   AT LEVEL 012 AS OF 05/11/78
*          DATA SET GU026300   AT LEVEL 011 AS OF 08/12/75
GU026300 TITLE 'GU026300 - VTOC LIST MAINLINE'
GU026300 SUBENTRY BASES=(R13,R12)
*              AUTHOR.   DON HIGGINS.
*              DATE.     02/10/74.
*              REMARKS.  LIST SELECTED VTOCS FROM VTOC MASTER FLIE.
*              REV1 08/12/75 DSH - ADD OPEN COUNT SUPPLIED BY OS/MOD.
*              REV2 05/10/78 PWM - ADD 3350 CAPABILITY
*
         OPEN  (VFMASTER,,VFSELECT,,VFREPORT,(OUTPUT),                 X
               VFRSPACE,(OUTPUT))
         LA    R2,SHEAD1
         PM      R500PSPA
         LA    R2,SPACE
         PM      R500PSPA
         PM      R100RSEL
M010     TAG
         PM      R200RMST
         TM    SWITCH,EOFMSTON            WAS END-OF-FILE ON MASTER HIT
         BO    M050
M020     TAG
         CLI   FORMAT,C'4'              IS THIS A FORMAT FOUR DSCB
         BNE   M010
M030     TAG
         CLC   VOLUME,LOWVOL
         BL    M010
         CLC   VOLUME,HIGHVOL
         BNH   M040
         PM      R100RSEL
         TM    SWITCH,EOFSELON          WAS EOF ON SELECT FILE REACHED
         BNO   M030
         B     M050
M040     TAG
         PM      R300P456
         PM      R400P123
         TM    SWITCH,EOFMSTON          WAS EOF ON MASTER FILE REACHED
         BNO   M020
M050     TAG
         CLOSE (VFMASTER,,VFSELECT,,VFREPORT,,VFRSPACE)
         SUBEXIT
         TITLE 'R100 - SELECT VOLUME TO LIST'
R100RSEL PENTRY
         TM    SWITCH,EOFSELON        WAS EOF ON SELECT FILE REACHED
         BO    R190EXIT
         GET   VFSELECT,SELREC
         CLC   HIGHVOL,=CL6' '
         BNE   R190EXIT
         MVC   HIGHVOL,LOWVOL
         B     R190EXIT
R110EOF  TAG
         OI    SWITCH,EOFSELON        SET EOF ON SELECT FILE ON
R190EXIT PEXIT
         TITLE 'R200 - READ VTOC MASTER'
R200RMST PENTRY
         GET   VFMASTER,MSTREC
         B     R290EXIT
R210EOF  TAG
         OI    SWITCH,EOFMSTON         SET EOF ON MASTER FILE ON
R290EXIT PEXIT
         TITLE 'R300 - PRINT INFO FROM FORMAT 4, 5, 6 DSCBS'
R300P456 PENTRY
         ZAP   PAGE,=P'0'
         ZAP   LINE,=P'0'
         MVC   DVOLUME,VOLUME
         EDIT  TO=TIMEDATE,FROM=TIME,MASK=' 99/99/99.99F ZZ99.999'
         SCALL SRCVTJUL,(TIME+5,MMXDDXYY)
         MVC   NTC(2),DS4TRKSZ          SET TRK/CYL CONSTANT
         CLC   DS4DEVSZ(2),DASD3330     IS IT A 3330
         BNE   R345CHK                   NO, CHECK FOR 3350
         MVC   DTYPE,=C'3330'
         OI    SWITCH,D3330             PROCESSING A 3330 TYPE DASD
         NI    SWITCH,D3350OFF          TURN OFF 3350 DASD SWITCH
R303FLOW EQU   *
         LM    R2,R5,=A(CTAB,555,0,0)   CLEAR CTAB AREA
         MVCL  R2,R4
         ZR    R2            FREE CYLS
         ZR    R3            FREE TRKS
R305     TAG
         PM      R200RMST
         CLI   FORMAT,C'5'
         BNE   ERR202
R310     TAG
         LA    R4,DS3EXTNT
         LA    R5,8
R320     TAG
         BAL   R14,R350EXT
         AAI   R4,5
         BCT   R5,R320
         LA    R4,DS3ADEXT
         LA    R5,18
R330     TAG
         BAL   R14,R350EXT
         AAI   R4,5
         BCT   R5,R330
R340     TAG
         CLC   DS5PTRDS(5),=XL5'00'
         BE    R370
         B     R305
R345CHK  EQU   *
         CLC   DS4DEVSZ(2),DASD3350    IS IT A 3350
         BNE   ERR201                   NO, LETS QUIT THEN
         MVC   DTYPE,=C'3350'
         OI    SWITCH,D3350             PROCESSING A 3350 TYPE DASD
         NI    SWITCH,D3330OFF          TURN OFF 3330 SWITCH
         B     R303FLOW
         SPACE
ERR202   ABEND 202,DUMP
ERR201   ABEND 201,DUMP
ERR206   ABEND 206,DUMP
R350EXT  TAG
         ZR    R6
         ICM   R6,3,0(R4)          R6=REL TRK
         ZR    R7
         ICM   R7,3,2(R4)          R7=FULL CYL
         ZR    R8
         ICM   R8,1,4(R4)          R8=TRKS
         LR    R1,R6
         ZR    R0
         D     R0,FNTC
         LA    R9,CTAB(R1)         R9=STARTING CYL
         AR    R2,R7
         AR    R3,R8
         MH    R7,NTC
         AR    R7,R8               R7=TOTAL TRKS IN EXT
         LH    R1,NTC
         SR    R1,R0
R360NEXT TAG
         CLR   R1,R7
         BNH   *+6
         LR    R1,R7
         ZR    R0
         CL    R9,=A(CTAB+555)
         BH    ERR205
         IC    R0,0(R9)
         AR    R0,R1               ADD FREE TRKS IN THIS CYL
         CH    R0,NTC
         BH    ERR204
         STC   R0,0(R9)
         AAI   R9,1
         SR    R7,R1
         LH    R1,NTC
         BP    R360NEXT
         BZ    0(R14)
ERR203   ABEND 203,DUMP TRKS LEFT NEG.
ERR204   ABEND 204,DUMP FREE TRKS/CYL GT NTC
ERR205   ABEND 205,DUMP GT 404 CYLS
R370     TAG
         CVD   R2,PWORK
         EDIT  TO=DFCYL,FROM=PWORK+5,MASK=' ZZZ99'
         CVD   R3,PWORK
         EDIT  TO=DFTRK,FROM=PWORK+5,MASK=' ZZZ99'
         MVC   SVOL,DVOLUME
         MVC   SFCYL,DFCYL
         MVC   SFTRK,DFTRK
         MVC   SDATE,MMXDDXYY
         LA    R2,SLINE1
         PM      R500PSPA
         TM    SWITCH,D3330            ARE WE DOING A 3330
         BO    R380                     YES
         TR    CTAB+000(200),TRTAB50    NO, OTHER CHOICE IS 3350
         TR    CTAB+200(200),TRTAB50
         TR    CTAB+400(155),TRTAB50
         MVC   DC000,CTAB+000
         MVC   DC100,CTAB+100
         MVC   DC200,CTAB+200
         MVC   DC300,CTAB+300
         MVC   DC400,CTAB+400
         MVC   DC500,CTAB+500
         B     R390EXIT
R380     EQU   *
         TR    CTAB+000(200),TRTAB30
         TR    CTAB+200(204),TRTAB30
         MVC   DC000,CTAB+000
         MVC   DC100,CTAB+100
         MVC   DC200,CTAB+200
         MVC   DC300,CTAB+300
         MVC   DC400(4),CTAB+400
R390EXIT PEXIT
         TITLE 'R400 - PRINT INFO FROM 1, 2, 3 DSCBS'
R400P123 PENTRY
         PM      R200RMST
         TM    SWITCH,EOFMSTON          REACHED EOF ON MASTER FILE YET
         BO    R485                      YES, PRINT EOV AND EXIT
         CLI   FORMAT,C'4'
         BE    R485
         CLI   FORMAT,C'1'
         BNE   ERR205              INVALID DSCB AT ATHIS POINT
         MVC   DDSN,DSN
         LA    R1,DS1CREDT
         BAL   R14,R480DATE
         MVC   DCREDT,YYDDD
         LA    R1,DS1EXPDT
         BAL   R14,R480DATE
         MVC   DEXPDT,YYDDD
         IF    (CLC,DEXPDT,=CL5' ',EQ)
               MVC  DEXPDT,DCREDT
         FI
         IF    (CLC,DS1OPENC(4),=4C' ',EQ)
               MVC  DS1OPENC(4),=4X'00'
         FI
         ICM   R0,15,DS1OPENC
         CVD   R0,PWORK
         EDIT  TO=DOPENC,FROM=PWORK+5,MASK=' ZZ,Z99'
         MVC   DDSORG(3),=C'UN '
         TB    DS1DSORG,(7)
         BZ    *+8
         MVI   DDSORG+2,C'U'
         TB    DS1DSORG,(0)
         BZ    *+14
         MVC   DDSORG,=C'IS'
         B     R410
         TB    DS1DSORG,(1)
         BZ    *+14
         MVC   DDSORG,=C'PS'
         B     R410
         TB    DS1DSORG,(2)
         BZ    *+14
         MVC   DDSORG,=C'DA'
         B     R410
         TB    DS1DSORG,(3)
         BZ    *+14
         MVC   DDSORG,=C'CX'
         B     R410
         TB    DS1DSORG,(6)
         BZ    *+14
         MVC   DDSORG,=C'PO'
         B     R410
         TB    DS1DSORG+1,(0)
         BZ    *+14
         MVC   DDSORG,=C'GS'
         B     R410
         TB    DS1DSORG+1,(1)
         BZ    *+14
         MVC   DDSORG,=C'TX'
         B     R410
         TB    DS1DSORG+1,(2)
         BZ    *+14
         MVC   DDSORG,=C'TQ'
         B     R410
         TB    DS1DSORG+1,(5)
         BZ    *+14
         MVC   DDSORG,=C'TR'
         B     R410
R410     TAG
         TB    DS1RECFM,(0)
         BO    R412
         TB    DS1RECFM,(1)
         BO    R414
         MVC   DRECFM,=CL4' '    00
         B     R430
R412     TAG
         TB    DS1RECFM,(1)
         BO    R416
         MVC   DRECFM,=CL4'F' 10
         B     R420
R414     TAG
         MVC   DRECFM,=CL4'V' 01
         B     R420
R416     TAG
         MVC   DRECFM,=CL4'U'  11
R420     TAG
         LA    R1,DRECFM+1
         TB    DS1RECFM,(2)
         BZ    *+12
         MVI   0(R1),C'T'
         AAI   R1,1
         TB    DS1RECFM,(3)
         BZ    *+12
         MVI   0(R1),C'B'
         AAI   R1,1
         TB    DS1RECFM,(4)
         BZ    *+12
         MVI   0(R1),C'S'
         AAI   R1,1
         TB    DS1RECFM,(5,6)
         BZ    R430
         MVI   0(R1),C'A'
         TB    DS1RECFM,(5)
         BO    R430
         MVI   0(R1),C'M'
R430     TAG
         CLC   DS1LRECL,=XL2'8000'
         BNE   *+14
         MVC   DLRECL,=C' 32K +'
         B     R440
         ZR    R0
         ICM   R0,3,DS1LRECL
         CVD   R0,PWORK
         EDIT  TO=DLRECL,FROM=PWORK+5,MASK=' ZZZZZ'
R440     TAG
         ZR    R0
         ICM   R0,3,DS1BLKL
         CVD   R0,PWORK
         EDIT  TO=DBLKSZ,FROM=PWORK+5,MASK=' ZZZZZ'
         MVC   DOPTCD,=CL6' '
         LA    R2,DOPTCD
         TB    DS1DSORG,(0)
         BO    R450
         TB    DS1OPTCD,(0)
         BZ    *+12
         MVI   0(R2),C'W'
         AAI   R2,1
         TB    DS1OPTCD,(1)
         BZ    *+12
         MVI   0(R2),C'U'
         AAI   R2,1
         TB    DS1OPTCD,(2)
         BZ    *+12
         MVI   0(R2),C'C'
         AAI   R2,1
         TB    DS1OPTCD,(4)
         BZ    *+12
         MVI   0(R2),C'Q'
         AAI   R2,1
         ZR    R0
         ICM   R0,3,DS1LSTAR
         CVD   R0,PWORK
         EDIT  TO=DEOFTRK,FROM=PWORK+5,MASK=' ZZZZZ'
         B     R460
R450     TAG
         TB    DS1OPTCD,(0)
         BZ    *+12
         MVI   0(R2),C'W'
         AAI   R2,1
         TB    DS1OPTCD,(1)
         BZ    *+12
         MVI   0(R2),C'F'
         AAI   R2,1
         TB    DS1OPTCD,(2)
         BZ    *+12
         MVI   0(R2),C'M'
         AAI   R2,1
         TB    DS1OPTCD,(3)
         BZ    *+12
         MVI   0(R2),C'I'
         AAI   R2,1
         TB    DS1OPTCD,(4)
         BZ    *+12
         MVI   0(R2),C'Y'
         AAI   R2,1
         TB    DS1OPTCD,(6)
         BZ    *+12
         MVI   0(R2),C'L'
         AAI   R2,1
         TB    DS1OPTCD,(7)
         BZ    *+12
         MVI   0(R2),C'R'
         AAI   R2,1
         MVC   DEOFTRK,=CL6' '
         ZR    R0
         IC    R0,DS1KEYL
         CVD   R0,PWORK
         EDIT  TO=DKEYL,FROM=PWORK+6,MASK=' Z99'
         ICM   R0,3,DS1RKP
         CVD   R0,PWORK
         EDIT  TO=DRKP,FROM=PWORK+5,MASK=' ZZZ99'
R460     TAG
         LA    R3,DS1EXT1
         PM      R600PEXT
         LA    R3,DS1EXT2
         PM      R600PEXT
         LA    R3,DS1EXT3
         PM      R600PEXT
         CLC   DS1PTRDS(5),=XL5'00'
         BNE   R462
         CLC   DDSN,DSN      HAS DSN INFO BEEN PRINTED YET
         BNE   R400P123      YES, MUST HAVE AN EXTENT
         LA    R2,DLINE1     NO, PRINT MODEL GDS INFO WITH NO EXT.
         PM      R700PRNT
         B     R400P123
R462     TAG
         PM      R200RMST
         TM    SWITCH,EOFMSTON           REACHED EOF ON MASTER FILE YET
         BO    ERR206              SHOULD BE F2-F3
         CLI   FORMAT,C'2'
         BE    R475
         CLI   FORMAT,C'3'
         BNE   ERR206
         LA    R3,DS3EXTNT
         LA    R4,4
R465     TAG
         PM      R600PEXT
         AAI   R3,10
         BCT   R4,R465
         LA    R3,DS3ADEXT
         LA    R4,9
R470     TAG
         PM      R600PEXT
         AAI   R3,10
         BCT   R4,R470
         CLC   DS3PTRDS(5),=XL5'00'
         BNE   R462
         B     R400P123
R475     TAG
         ZR    R0
         IC    R0,DS2LPRAD
         CVD   R0,PWORK
         EDIT  TO=DEOFM,FROM=PWORK+6,MASK=' Z99'
         ICM   R0,3,DS2LPRAD+3
         CVD   R0,PWORK
         EDIT  TO=DEOFCC,FROM=PWORK+6,MASK=' Z99'
         ICM   R0,3,DS2LPRAD+5
         CVD   R0,PWORK
         EDIT  TO=DEOFH,FROM=PWORK+6,MASK=' Z99'
         ZR    R0
         IC    R0,DS2DVIND
         CVD   R0,PWORK
         EDIT  TO=DNTM,FROM=PWORK+6,MASK=' Z99'
         IC    R0,DS2NOLEV
         CVD   R0,PWORK
         EDIT  TO=DLVL,FROM=PWORK+6,MASK=' Z99'
         ICM   R0,3,DS2NOBYT
         CVD   R0,PWORK
         EDIT  TO=DCORE,FROM=PWORK+5,MASK=' ZZZ99'
         ICM   R0,15,DS2PRCTR
         CVD   R0,PWORK
         EDIT  TO=DPREC,FROM=PWORK+4,MASK=' ZZZZZ99'
         ZR    R0
         ICM   R0,3,DS2OVRCT
         CVD   R0,PWORK
         EDIT  TO=DOREC,FROM=PWORK+5,MASK=' ZZZ99'
         LA    R2,SPACE
         PM      R700PRNT
         LA    R2,DLINE2
         PM      R700PRNT
         LA    R2,SPACE
         PM      R700PRNT
         CLC   DS2PTRDS(5),=XL5'00'
         BZ    R400P123
         B     R462
R480DATE TAG
         MVC   YYDDD,=CL5' '
         CLC   0(3,R1),=XL3'00'
         BE    0(R14)
         ZR    R0
         IC    R0,0(R1)
         MH    R0,=H'1000'
         MVC   PWORK(2),1(R1)
         AH    R0,PWORK
         CVD   R0,PWORK
         EDIT  TO=YYDDD-1,FROM=PWORK+5,MASK=' 99999'
         BR    R14
R485     TAG
         LA    R2,DLINE3
         PM      R700PRNT
R490EXIT PEXIT
         TITLE 'R500 - PUT RECORD TO SPACE REPORT'
R500PSPA PENTRY
         PUT   VFRSPACE,(R2)
R590EXIT PEXIT
         TITLE 'R600 - DECODE EXTENT AND PRINT'
R600PEXT PENTRY
*
*              DECODE CURRENT EXTENT (R3) AND PRINT IF REAL PG 114
*
         CLI   0(R3),0
         BE    R690EXIT
         ZR    R0
         IC    R0,1(R3)
         CVD   R0,PWORK
         EDIT  TO=DEXT-1,FROM=PWORK+6,MASK=' Z99'
         TB    0(R3),(5)
         BZ    *+8
         MVI   DEXT,C'I'
         TB    0(R3),(6)
         BZ    *+8
         MVI   DEXT,C'O'
         TB    0(R3),(7)
         BZ    *+8
         MVI   DEXT,C'P'
         ZR    R0
         ICM   R0,3,2(R3)
         CVD   R0,PWORK
         EDIT  TO=DSTRCYL,FROM=PWORK+6,MASK=' Z99'
         LR    R5,R0
         MH    R5,NTC
         ICM   R0,3,4(R3)
         AR    R5,R0               STARTING REL TRKS
         CVD   R0,PWORK
         EDIT  TO=DSTRTRK,FROM=PWORK+6,MASK=' Z99'
         ICM   R0,3,6(R3)
         CVD   R0,PWORK
         EDIT  TO=DENDCYL,FROM=PWORK+6,MASK=' Z99'
         LR    R6,R0
         MH    R6,NTC
         ICM   R0,3,8(R3)
         CVD   R0,PWORK
         EDIT  TO=DENDTRK,FROM=PWORK+6,MASK=' Z99'
         AR    R0,R6
         SR    R0,R5
         AH    R0,=H'1'
         CVD   R0,PWORK
         EDIT  TO=DTOTAL,FROM=PWORK+5,MASK=' ZZZ99'
         LA    R2,DLINE1
         PM      R700PRNT
         MVC   DLINE1,SPACE
R690EXIT PEXIT
         TITLE 'R700 - PUT RECORD TO VTOC REPORT WITH PAGE CONTROL'
R700PRNT PENTRY
*  MAKE SEPARATE PRINT ROUTINES IF MORE DEVICES ARE ADDED
         STM   R2,R3,SAV23
         SP    LINE,ONE                 COUNT ONE LINE PRINTED
         BP    R710                      NO FORMS OVERFLOW
         ZAP   LINE,=P'50'              NUMBER LINES REMAINING
         AP    PAGE,ONE                 COUNT PAGES PRINTED
         EDIT  TO=DPAGE,FROM=PAGE,MASK=' ZZZ'
         LA    R2,HEAD1                 PRINT HEADINGS
         BAL   R3,R780
         LA    R2,HEAD2                  AND QUICK MAP
         BAL   R3,R780
         LA    R2,HEAD3
         BAL   R3,R780
         LA    R2,HEAD4
         BAL   R3,R780
         LA    R2,HEAD5
         BAL   R3,R780
         LA    R2,HEAD6
         BAL   R3,R780
         LA    R2,HEAD7
         BAL   R3,R780
         LA    R2,HEAD8
         BAL   R3,R780
         TM    SWITCH,D3330            ARE WE DOING A 3330 TYPE DASD
         BO    R705                     YES, SKIP ONE LINE OF PRINT
         MVC   HEAD9+7(2),=C'99'       SET FOR 3350 CYLINDERS ON PACK
         LA    R2,HEAD9
         BAL   R3,R780
         MVC   HEAD9+7(2),=C'04'       SET FOR 3330 CYLINDERS ON PACK
         LA    R2,HEAD10
         BAL   R3,R780
         SP    LINE,ONE                COUNT THE EXTRA LINE
         LA    R2,HEAD11
         BAL   R3,R780
         B     R708
R705     EQU   *
         MVC   DC400+4(96),HEAD4+11     SET ASTERISKS IN LAST LINE
         LA    R2,HEAD9
         BAL   R3,R780
         MVC   DC400+4(96),SPACE        SET BACK TO BLANKS
         MVC   HEAD11+18(50),SPACE      SET TO BLANKS
         LA    R2,HEAD11
         BAL   R3,R780
         MVC   HEAD11+18(50),HEAD4+11    SET BACK TO ASTERISKS
R708     EQU   *
         LA    R2,HEAD12
         BAL   R3,R780
         LA    R2,HEAD13
         BAL   R3,R780
R710     TAG
         L     R2,SAV23
         BAL   R3,R780
         L     R3,SAV23+4
         B     R790EXIT
R780     TAG
         PUT   VFREPORT,(R2)
         BR    R3
R790EXIT PEXIT
         TITLE 'COMMON DATA'
VFMASTER DCB   DSORG=PS,MACRF=GM,DDNAME=VFMASTER,EODAD=R210EOF
VFSELECT DCB   DSORG=PS,MACRF=GM,DDNAME=VFSELECT,EODAD=R110EOF
VFREPORT DCB   DSORG=PS,MACRF=PM,DDNAME=VFREPORT,RECFM=FA,BLKSIZE=133
VFRSPACE DCB   DSORG=PS,MACRF=PM,DDNAME=VFRSPACE,RECFM=FA,BLKSIZE=133
         EQUAL
SWITCH   DC    X'00'
EOFMSTON EQU   X'01'                   END OF FILE ON MASTER
EOFSELON EQU   X'02'                   END OF FILE ON SELECT
D3330    EQU   X'04'                   3330 DISK PROCESSED ON
D3350    EQU   X'08'                   3350 DISK PROCESSED ON
D3330OFF EQU   X'FF'-X'04'             3330 DASD PROCESSED OFF
D3350OFF EQU   X'FF'-X'08'             3350 DASD PROCESSED OFF
ONE      DC    PL1'1'
DASD3330 DC    H'411'
DASD3350 DC    H'560'
PAGE     DC    PL2'0'
LINE     DC    PL2'0'
MSTREC   DS    0CL199
VOLUME   DC    CL6' '
DSN      DC    CL44' '
FORMAT   DC    C' '
DSCB     DC    CL140' '
TIME     DC    CL8' '
SELREC   DS    0CL80
LOWVOL   DC    0CL6' ',6X'00'
HIGHVOL  DC    0CL6' ',6X'FF'
CTAB     DC    555X'00'
TRTAB30  DC    C'.',18C'Y',C'0'        TRANSLATE TABLE FOR 3330
TRTAB50  DC    C'.',29C'Y',C'0'        TRANSLATE TABLE FOR 3350
HEAD1    DS    0CL133
         DC    C'1'
DVOLUME  DC    CL6' ',20C' '
DTYPE    DC    CL4' ',C' VOLUME TABLE OF CONTENTS AS OF'
TIMEDATE DC    CL22' ',C'   ('
MMXDDXYY DC    CL8' ',C')',20C' ',C'PAGE'
DPAGE    DC    CL4' '
         FILL  HEAD1
HEAD2    DS    0CL133
         DC    C'0',20C' ',C'SPACE ALLOCATION MAP  ( 0 - UNALLOCATED'
         DC    C'  . - ALLOCATED  Y - PARTIALLY ALLOCATED)'
         FILL  HEAD2
HEAD3    DS    0CL133
         DC    C'0   CYL     '
         DC   C'0....5...10...15...20...25...30...35...40...45...50'
         DC    C'...55...60...65...70...75...80...85...90...95..99'
         FILL  HEAD3
HEAD4    DS    0CL133
         DC    C' ',10C' ',102C'*',C'   TOTAL FREE SPACE'
         FILL  HEAD4
HEAD5    DS    0CL133
         DC    C' '
         DC    C' 000-099  *'
DC000    DC    CL100' ',C'*'
         FILL  HEAD5
HEAD6    DS    0CL133
         DC    C' '
         DC    C' 100-199  *'
DC100    DC    CL100' ',C'*    CYL    TRKS'
         FILL  HEAD6
HEAD7    DS    0CL133
         DC    C' '
         DC    C' 200-299  *'
DC200    DC    CL100' ',C'*'
         FILL  HEAD7
HEAD8    DS    0CL133
         DC    C' '
         DC    C' 300-399  *'
DC300    DC    CL100' ',C'* '
DFCYL    DC    CL6' ',C' '
DFTRK    DC    CL6' '
         FILL  HEAD8
HEAD9    DS    0CL133
         DC    C' '
         DC    C' 400-404  *'
DC400    DC    CL100' ',C'* '
         FILL  HEAD9
HEAD10   DS    0CL133
         DC    C' '
         DC    C' 500-554  *'
DC500    DC    CL55' ',45C'*'
         FILL  HEAD10
HEAD11   DS    0CL133
         DC    C' '
         DC    C' ',9C' ',57C'*'
         FILL  HEAD11
HEAD12   DS    0CL133
         DC    C' ',87C' ',C'   EOF     ',6C' ',C'START    END'
         DC    3C' ',C'TOTAL'
         DC    C'   OPEN'
         FILL  HEAD12
HEAD13   DS    0CL133
         DC    C' ',14C'-',C' DATA SET NAME ',15C'-'
         DC    C' CREDT EXPDT DSORG RECFM LRECL BLKSZ OPTCD'
         DC    C'  REL. TRK   EXT CYL TRK CYL TRK  TRKS'
         DC    C'  COUNT'
         FILL  HEAD13
DLINE1   DS    0CL133
         DC    C' '
DDSN     DC    CL44' ',C' '
DCREDT   DC    CL5' ',C' '
DEXPDT   DC    CL5' ',C'   '
DDSORG   DC    CL2' ',C'   '
DRECFM   DC    CL3' ',C' '
DLRECL   DC    CL6' '
DBLKSZ   DC    CL6' ',C' '
DOPTCD   DC    CL6' ',CL1' '
DEOFTRK  DC    CL6' ',5C' '
DEXT     DC    CL3' '
DSTRCYL  DC    CL4' '
DSTRTRK  DC    CL4' '
DENDCYL  DC    CL4' '
DENDTRK  DC    CL4' '
DTOTAL   DC    CL6' '
DOPENC   DC    CL7' '
         FILL  DLINE1
SHEAD1   DS    0CL133
         DC    C'1',C'VOLUME FREE-CYLS FREE-TRKS  DATE'
         FILL  SHEAD1
SLINE1   DS    0CL133
         DC    C' '
SVOL     DC    CL6' ',C'  '
SFCYL    DC    CL6' ',4C' '
SFTRK    DC    CL6' ',3C' '
SDATE    DC    CL8' '
         FILL  SLINE1
SPACE    DC    CL133' '
         DC    C' '
YYDDD    DC    CL5' '
FNTC     DC    F'00'                    # TRACKS/CYLINDER ON DASD
NTC      EQU   FNTC+2
SAV23    DC    2F'0'
PWORK    DC    D'0'
DLINE2   DS    0CL133
         DC    C'  ISAM RKP'
DRKP     DC    CL6' ',C'  KEYL'
DKEYL    DC    CL4' ',C'  EOF(MCCH)'
DEOFM    DC    CL4' '
DEOFCC   DC    CL4' '
DEOFH    DC    CL4' ',C'  NTM'
DNTM     DC    CL4' ',C'  LVLS'
DLVL     DC    CL4' ',C'  INDEX-BYTES'
DCORE    DC    CL6' ',C'  PRIME-REC'
DPREC    DC    CL8' ',C' OVERFLOW-REC'
DOREC    DC    CL6' '
         FILL  DLINE2
DLINE3   DS    0CL133
         DC    C'0'
DEOVVOL  DC    CL13' ',C'END OF VTOC'
         FILL  DLINE3
*
* DSCB EQUS FROM OS/VS2 SYSTEM DATA AREAS SY28-0606-0
*
DS4DEVSZ EQU   DSCB+62   124
DS4TRKSZ EQU   DSCB+64                 # TRKS/CYL ON DASD
DS5PTRDS EQU   DSCB+135  128
DS1CREDT EQU   DSCB+53   112
DS1EXPDT EQU   DSCB+56   112
DS1OPENC EQU   DSCB+71  OPEN COUNT UPDATED BY OS MOD
DS1DSORG EQU   DSCB+82   112
DS1RECFM EQU   DSCB+84   112
DS1LRECL EQU   DSCB+88   113
DS1BLKL  EQU   DSCB+86   113
DS1OPTCD EQU   DSCB+85   113   SEE ALSO PG 34 + 46
DS1LSTAR EQU   DSCB+98   114
DS1EXT1  EQU   DSCB+105  114
DS1EXT2  EQU   DSCB+115  115
DS1EXT3  EQU   DSCB+125  115
DS1PTRDS EQU   DSCB+135  115
DS3EXTNT EQU   DSCB+4    122
DS3ADEXT EQU   DSCB+45   122
DS3PTRDS EQU   DSCB+135  122
DS1RKP   EQU   DSCB+91   113
DS1KEYL  EQU   DSCB+90   113
DS2LPRAD EQU   DSCB+93   119
DS2DVIND EQU   DSCB+46   118
DS2NOLEV EQU   DSCB+45   118
DS2NOBYT EQU   DSCB+64   118
DS2PRCTR EQU   DSCB+67   118
DS2OVRCT EQU   DSCB+128  119
DS2PTRDS EQU   DSCB+135  119
         END   GU026300
