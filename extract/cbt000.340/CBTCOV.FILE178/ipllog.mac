*          DATA SET GU012000   AT LEVEL 006 AS OF 08/03/76
         PRINT NOGEN
GU012000 SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     07/01/72.
*              REMARKS.  LIST IPL LOG AND/OR RESET
*
* REV1 03/28/74 DON HIGGINS - SKIP BLANK RECORDS LEFT DUE TO SYS FAIL.
*
* REV2 01/28/76 DSH - CHANGE SYSPRINT DCB TO SDCB FOR OPTIONAL BLOCK
* REV3 08/03/76 DSH - ONLY PRINT UP TO LAST ENTRY
         L     R2,0(R1)
         OC    0(2,R2),0(R2)
         BZ    LIST
         CLI   2(R2),C'R'
         BE    RESET
LIST     TAG
         OPEN  (LOGFILE,(INPUT),SYSPRINT,(OUTPUT))
         GET   LOGFILE,RECORD
         L     R2,NEXTBLK
         ZR    R3
         TIME  DEC
         STM   R0,R1,PWORK
         EDIT  TO=TIMEDATE,FROM=PWORK,MASK=' 99/99/99.99F ZZ99.999'
NEXT     TAG
         GET   LOGFILE,RECORD
         AAI   R3,1
         CLC   RECORD(4),=CL4' '
         BE    NEXT
         CLR   R2,R3
         BL    EOF
         AP    COUNT,=P'1'
         EDIT  TO=DCOUNT,FROM=COUNT,MASK=' ZZZ'
         EDIT  TO=DTIME,FROM=IPLTIME,MASK=' 99/99/99.99F ZZ99.999'
         MVC   DR,REASON
         SP    LINE,=P'1'
         BP    DETAIL
         ZAP   LINE,MAXLINE
         AP    PAGE,=P'1'
         EDIT  TO=DPAGE,FROM=PAGE,MASK=' ZZZ'
         PUT   SYSPRINT,H1
         PUT   SYSPRINT,H2
         PUT   SYSPRINT,H3
DETAIL   TAG
         PUT   SYSPRINT,IPLMSG
         B     NEXT
RESET    TAG
         OPEN  (IPLFILE,(UPDAT))
         READ  DECB1,DI,IPLFILE,RECORD,LREC,0,BLK1+1
         CHECK DECB1
         MVC   NEXTBLK,=F'0'
         TIME  DEC
         STM   R0,R1,IPLTIME
         WRITE DECB2,DI,IPLFILE,RECORD,LREC,0,BLK1+1
         CHECK DECB2
         CLOSE (IPLFILE)
         SUBEXIT
EOF      TAG
         CLOSE (LOGFILE,,SYSPRINT)
         SUBEXIT
         LTORG
         EQUAL
IPLFILE  DCB   DSORG=DA,MACRF=(RIC,WIC),DDNAME=IPLFILE,                X
               RECFM=F,BLKSIZE=LREC,OPTCD=R
LOGFILE  DCB   DSORG=PS,MACRF=GM,DDNAME=LOGFILE,EODAD=EOF
SYSPRINT SDCB  M=PM,R=FBA,L=133
LREC     EQU   112
RECORD   DS    0F
NEXTBLK  DC    F'0'
IPLTIME  DC    XL8'00'
REASON   DC    CL100' '
BLK1     DC    F'0'
PWORK    DC    D'0'
COUNT    DC    PL2'0'
LINE     DC    PL2'0'
MAXLINE  DC    PL2'50'
PAGE     DC    PL2'0'
H1       DS    0CL133
         DC    C'1',CL30' '
         DC    C'FPC 370-158 IPL LOG',CL30' '
TIMEDATE DC    CL22' ',CL10' ',C' PAGE'
DPAGE    DC    CL4' '
         FILL  H1
H2       DS    0CL133
         DC    C'0',C'COUNT',CL8' ',C'IPL TIME/DATE'
         DC    CL10' ',C'OPERATOR ID AND REASON FOR IPL'
         FILL  H2
H3       DC    CL133'0'
IPLMSG   DS    0CL133
         DC    C' '
DCOUNT   DC    CL4' '
         DC    CL4' '
DTIME    DC    CL22' '
         DC    CL2' '
DR       DC    CL100' '
LASTBLK  DC    F'0'
         END
