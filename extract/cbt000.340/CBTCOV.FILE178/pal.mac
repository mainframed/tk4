*          DATA SET TP413000   AT LEVEL 010 AS OF 08/25/75
         TITLE 'TP413000 - DISPLAY PAL INFO'
*              AUTHOR.   DON HIGGINS.
*              DATE.     08/14/75.
*              REMARKS.  DISPLAY PAL INFO (SEE GU726000 FOR MOD RTN).
*              REFERENCES.
*                   1.   GC38-1002-1 VS2 MESSAGES (IEA101A)
*                   2.   SY27-7244-0 VS2 SUPR. PLM
*                   3.   GC28-0600-2 VS2 PLANNING AND USE
*                   4.   SY28-0606-0 VS2 SYSTEM DATA AREAS
         TITLE 'MAIN'
         PRINT NOGEN
TP413000 SUBENTRY
         LM    R3,R5,0(R1)
         MVC   TERM,0(R3)
         PM    DISPLAY
         SUBEXIT
         TITLE 'DISPLAY'
DISPLAY  PENTRY
         L     R2,16
         L     R3,CVTPVTP(R2)
         LH    R0,PVTLTH(R3)
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZZZ',TO=DLTH
         LH    R0,PVTREPCT(R3)
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZZZ',TO=DREPCT
         LH    R0,PVTLRLIM(R3)
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZZZ',TO=DLRC
         LH    R0,PVTHRLIM(R3)
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZZZ',TO=DHRC
         LH    R0,PVTLALIM(R3)
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZZZ',TO=DLRD
         LH    R0,PVTHALIM(R3)
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZZZ',TO=DHRD
         LH    R0,PVTIMEAD(R3)
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZZZ',TO=DMTIM
         TPPUTMSG TERM,DL1
         TPPUTMSG TERM,DL2
         L     R4,PVTPDT(R3)
         ZR    R5
         IC    R5,PVTNPDTE(R3)
         DO    WHILE,(LTR,R5,R5,NZ)
               PM   PDEVICE
               SI   R5,1
               LA   R4,PDTLEN(R4)
         OD
         PEXIT
         TITLE 'PDEVICE - PRINT PAGE DEVICE INFO'
PDEVICE  PENTRY
         ZR    R0
         IC    R0,PDTDN(R4)
         CVD   R0,PWORK
         EDIT  TO=DDN,FROM=PWORK+7,MASK=' 9'
         L     R6,PDTIOB(R4)
         L     R7,IOBDCB(R6)
         L     R8,DCBDEB(R7)
         L     R9,DEBUCB(R8)
         MVC   DVOL,SRTEVOLI(R9)
         MVC   DUNIT,UCBNAME(R9)
         LH    R0,DEBSTRCC(R8)
         CVD   R0,PWORK
         EDIT  TO=DSCYL,FROM=PWORK+6,MASK=' Z99'
         LH    R0,DEBENDCC(R8)
         CVD   R0,PWORK
         EDIT  TO=DECYL,FROM=PWORK+6,MASK=' Z99'
         LH    R0,PDTAPC(R4)  R0= AVIALABLE PAGES
         MH    R0,=H'4'
         CVD   R0,PWORK
         EDIT  TO=DFREE,FROM=PWORK+5,MASK=' ZZZ99'
         LH    R0,DEBTRKS(R8)
         MH    R0,=H'3'       R0=MAXIMUM PAGES
         SH    R0,PDTAPC(R4)  R0=USED PAGES
         MH    R0,=H'4'       R0=USED MEMORY IN K
         CVD   R0,PWORK
         EDIT  TO=DCURK,FROM=PWORK+5,MASK=' ZZZ99'
         TPPUTMSG TERM,DL3
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
TERM     DC    CL8' '
CVTPVTP  EQU   X'164' SDA 18
PVTLTH   EQU   4         PLM 820
PVTREPCT EQU   6         PLM 820
PVTLRLIM EQU   458       PLM 820
PVTHRLIM EQU   456       PLM 820
PVTLALIM EQU   462       PLM 820
PVTHALIM EQU   460       PLM 820
PVTIMEAD EQU   464       PLM 820
PVTPDT   EQU   152  PLM 823
PVTNPDTE EQU   152  PLM 823
PDTDN    EQU   0    PLM 810
PDTIOB   EQU   X'1C'
IOBDCB   EQU   X'14'
DCBDEB   EQU   X'2C'
DEBUCB   EQU   X'20'
SRTEVOLI EQU   28
UCBNAME  EQU   13
DEBSTRCC EQU   38
DEBENDCC EQU   42
DEBTRKS  EQU   46
PDTAPC   EQU   2    PLM 810
PDTLEN   EQU   32   PLM 810
PWORK    DC    D'0'
DL1      DS    0CL60
         DC    C'LTH='
DLTH     DC    CL6' ',C' REPCT='
DREPCT   DC    CL6' ',C' LRC='
DLRC     DC    CL6' ',C' HRC='
DHRC     DC    CL6' '
         FILL  DL1
DL2      DS    0CL60
         DC    C'LRD='
DLRD     DC    CL6' ',C' HRD='
DHRD     DC    CL6' ',C' MTIM='
DMTIM    DC    CL6' '
         FILL  DL2
DL3      DS    0CL60
         DC    C'PDEV'
DDN      DC    CL2' ',C' '
DVOL     DC    CL6' ',C' '
DUNIT    DC    CL3' ',C' CYLS'
DSCYL    DC    CL4' ',C'-'
DECYL    DC    CL4' ',C' FREE='
DFREE    DC    CL6' ',C'K USED='
DCURK    DC    CL6' '
         DC    C'K'
         FILL  DL3
         END
