*          DATA SET GU011100   AT LEVEL 002 AS OF 10/24/75
         TITLE 'SNV - STORE NUMERIC VALUE SUBROUTINE'
*              PGM-ID.   GU011100 ALIAS SNV.
*              AUTHOR.   DON HIGGINS.
*              DATE.     1967.
*              REMARKS.  STORE INTEGER VALUE OF A STRING OF CHARACTERS.
*              CALL.
*                        CALL SNV(OFF1,OFF2,ARRAY,VALUE)
*
*                        OFF1 - INTEGER OFFSET TO START OF STRING
*                        OFF2 - INTEGER OFFSET TO END   OF STRING
*                        ARRAY- NAME OF ARRAY CONTAINING STRING
*                        VALUE- INTEGER SET TO VALUE OF STRING.
*              RESTRICTIONS.
*                        1.   STRING MAY ONLY CONTAIN BLANKS,0-9,
*                             AND - FOR MINUS. NO DECIMALS OR ALPHAS.
*                        2.   INVALID CHARACTERS ARE PROCESSED AS
*                             IF THEY WERE VALID.
*
*
*
*
*
*
SNV      CSECT
         USING *,15
         STM   14,7,12(13)
RESTORE  LM    2,5,0(1)
         N     5,=X'00FFFFFF'
         LR    6,4
         A     4,0(,2)
         A     6,0(,3)
         MVI   NEGFLAG,X'00'
         SR    7,7
RELOAD   CR    4,6
         BH    TEST
         CLI   0(4),X'60'
         BH    LOAD
         BE    FLAG
         LA    4,1(,4)
         B     RELOAD
FLAG     MVI   NEGFLAG,X'FF'
         B     NEXT
LOAD     MH    7,=H'10'
         MVN   WORK+1(1),0(4)
         AH    7,WORK
NEXT     LA    4,1(,4)
         CR    4,6
         BNH   LOAD
TEST     TM    NEGFLAG,X'FF'
         BC    14,STORE
         LNR   7,7
STORE    ST    7,0(,5)
         CLI   12(1),X'00'
         BNE   RETURN
         LA    1,16(,1)
         B     RESTORE
RETURN   MVI   12(13),X'FF'
         LM    2,7,28(13)
         BCR   15,14
NEGFLAG  DS    XL1
WORK     DC    H'0'
         LTORG
         END
