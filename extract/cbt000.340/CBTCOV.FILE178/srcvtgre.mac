*          DATA SET GU008700   AT LEVEL 001 AS OF 09/21/72
*        IDENTIFICATION DIVISION.
*        PROGRAM-ID.  'CONVERT GREGORIAN DATE'
*        AUTHOR.   'HENRY JOHNSON'
*        INSTALLATION.  FLORIDA POWER CORPORATION
*        DATE WRITTEN.  04-02-68
*        REMARKS.
*              PURPOSE: THIS ROUTINE CONVERTS THE DATE
*                       021368 TO 068044
*                       COBOLAID HAS THE NECESSARY LEVELS
*                         FOR THE WORKING STORAGE SECTION.
*                         THE FOLLOWING CARD INCLUDED IN
*                         YOUR WORKING STORAGE SECTION WILL
*                         COPY THE PICTURE:
*                       01  GREDATE COPY 'CVTGREDT'
*                     * 01  GREDATE.
*                     *     02  GMONTH                 PICTURE 99.
*                     *     02  GDAY                   PICTURE 99.
*                     *     02  GYEAR                  PICTURE 99.
*                        (* - PICTURE IN COBOLAID)
*
*                       EXAMPLE OF THE CALL:
*                        ENTER LINKAGE.
*                        CALL 'SRCVTGRE' USING GREDATE.
*                        ENTER COBOL.
*              MODIFIED:    DATE             BY WHOM
*                        10/19/70      DON HIGGINS
*
SRCVTGRE SUBENTRY
         L     3,0(1)                   LOAD CALLING PARM ADDRESS
         MVC   MONTH,ZERO
         MVC   DAY,ZERO
         MVC   CVDAY,ZERO
         MVC   WORK1,WORK1-1
         MVC   WORK2,WORK2-1
         MVC   YEAR,YEAR-1
         MVC   FEB,=X'028F'
         MVC   WORK,0(3)                MOVE GRE DATE TO WORK AREA
         MVC   YEAR(2),WORK+4
         PACK  YEAR,YEAR
         TM    YEAR+1,X'01'             TEST YEAR FOR ODD
         BO    PACKIT                   YES, NOT LEAP YEAR
         TM    YEAR+1,X'12'             NO, TEST BIT 3 AND 6
         BM    PACKIT                   IF MIXED NOT LEAP YEAR
         MVC   FEB,=X'029F'             SET NO OF DAYS IN FEB=29
PACKIT   PACK  MONTH,WORK(2)            PACK MONTH
         PACK  DAY,WORK+2(2)            PACK DAY
         LA    4,CALNDR                 CALENDAR POINTER TO JANUARY
AGAIN    CP    MONTH,ONE                COMPARE MONTH TO 1
         BE    ADDAY                    YES, GO TO ADD DAY OF MONTH
         AP    CVDAY,0(2,4)             ADD MONTH
         SP    MONTH,ONE                REDUCE MONTH BY 1
         AH    4,=H'2'
         B     AGAIN                    LOOP
ADDAY    AP    CVDAY,DAY                ADD DAY BACK
         UNPK  WORK1,CVDAY              UNPACK DAY OF YEAR
         MVC   WORK2+1(2),WORK+4        MOVE YEAR
         MVC   WORK2+3(3),WORK1         MOVE DAY OF YEAR
         MVI   WORK2,C'0'               FILL WITH ZERO
         MVZ   WORK2+5(1),ZONE
         MVC   0(6,3),WORK2             MOVE CONVERTED DATE
         SUBEXIT
WORK     DC    CL6'0'
CALNDR   DC    X'031F'                  NUMBER OF DAYS IN JAN
FEB      DC    X'028F'                  NUMBER OF DAYS IN FEB
         DC    X'031F'                  NUMBER OF DAYS IN MAR
         DC    X'030F'                  NUMBER OF DAYS IN APR
         DC    X'031F'                  NUMBER OF DAYS IN MAY
         DC    X'030F'                  NUMBER OF DAYS IN JUN
         DC    X'031F'                  NUMBER OF DAYS IN JUL
         DC    X'031F'                  NUMBER OF DAYS IN AUG
         DC    X'030F'                  NUMBER OF DAYS IN SEP
         DC    X'031F'                  NUMBER OF DAYS IN OCT
         DC    X'030F'                  NUMBER OF DAYS IN NOV
         DC    X'031F'                  NUMBER OF DAYS IN DEC
MONTH    DC    X'000F'
DAY      DC    X'000F'
CVDAY    DC    X'000F'
ONE      DC    X'001F'
ZERO     DC    X'000F'
         DC    C'0'
WORK1    DC    CL3'0'
WORK2    DC    CL6'0'
YEAR     DC    C'000'
ZONE     DC    X'F1'
         END
