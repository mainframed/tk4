*          DATA SET GU710000   AT LEVEL 004 AS OF 04/10/75
         TITLE 'GU710000 - SRTBLMGR - GENERALTABLE LOADER'
*              AUTHOR.   DON HIGGINS.
*              DATE.     03/17/75.
*              REMARKS.  LOAD ANY NUMBER OF TABLES SPECIFIED BY CALLER.
*                        CALLER PASSES ONE OR MORE TABLE DEFINITION
*                        AREAS EACH DEFINED AS FOLLOWS.
*
*                   CALL 'SRTBLMGR' USING TAB1 TAB2 ETC.
*
*                   01   TAB1.
*                        05   TAB1-DDNAME PIC X(8) VALUE 'DDTAB1'.
*                        05   TAB1-MAX-ENT PIC 9(7) C-3 VALUE 10.
*                        05   TAB1-ENT-LNG PIC 9(7) C-3 VALUE 23.
*                        05   TAB1-AREA OCCURS 10 TIMES.
*                             10   CODE PIC X(3).
*                             10   DATA PIC X(20).
*                   EACH TABX-MAX-ENT IS SET TO THE ACTUAL # OF ENTRIES
*                   LOADED FOR EACH TABLE.
*
         TITLE 'MAINLINE'
         PRINT NOGEN
SRTBLMGR SUBENTRY
         LR    R3,R1
         PM    LOAD
         DO    WHILE,(TM,0(R3),X'80',NO)
               AAI  R3,4
               PM   LOAD
         OD
         SUBEXIT
         TITLE 'LOAD A TABLE'
LOAD     PENTRY
         L     R4,0(R3)            R4=A(TABLE DEFINITION AREA)
         USING TABDSECT,R4
         LA    R5,TABDCB
         USING IHADCB,R5
         MVC   DCBDDNAM,TABDDNAM   MOVE THIS TABLE'S DDNAME TO DCB
         OPEN  (TABDCB)
         ZAP   PWORK,TABLNGRC
         CVB   R7,PWORK            R7=ENTRY LENGTH FOR THIS TABLE
         IF    (CH,R7,DCBLRECL,NE)
               MVC  DMSG,=CL30'INVALID RECORD LENGTH'
               B    ABORT
         FI
         LA    R8,TABAREA          R8=A(NEXT TABLE ENTRY FOR MVCL)
         MVI   EOF,FALSE
         ZAP   PENT,=P'0'
         PM    GET
         DO    WHILE,(CLI,EOF,TRUE,NE)
               IF   (CP,PENT,TABMAXRC,NL)
                    MVC  DMSG,=CL30'TABLE CAPACITY EXCEEDED'
                    B    ABORT
               FI
               LR   R9,R7
               LR   R0,R1
               LR   R1,R7
               MVCL R8,R0
               AP   PENT,=P'1'
               PM   GET
         OD
         ZAP   TABMAXRC,PENT
         CLOSE (TABDCB)
         FREEPOOL TABDCB
         PEXIT
         TITLE 'GET NEXT TABLE RECORD FROM FILE'
GET      PENTRY
         GET   TABDCB
         PEXIT
GETEOF   MVI   EOF,TRUE
         PEXIT
         TITLE 'ABORT JOB DUE TO TABLE LOADING ERROR'
ABORT    TAG
         MVC   DNAME,TABDDNAM
         WTO   MF=(E,WTOLIST)
         ABEND 2999
         TITLE 'COMMON DATA'
         EQUAL
TABDCB   DCB   DSORG=PS,MACRF=GL,DDNAME=X,EODAD=GETEOF,RECFM=FB
EOF      LOGICAL FALSE,(TRUE,FALSE)
PENT     DC    PL4'0'
PWORK    DC    D'0'
WTOLIST  DC    0F'0',AL2(WTOEND-*,0)
         DC    C'SRTBLMGR LOADING OF FILE '
DNAME    DC    CL8' ',C' ABORTED DUE TO '
DMSG     DC    CL30' '
WTOEND   EQU   *
         DCBD  DSORG=PS
TABDSECT DSECT
TABDDNAM DS    CL8
TABMAXRC DS    PL4
TABLNGRC DS    PL4
TABAREA  DS    0X
         END
