*          DATA SET TO010000   AT LEVEL 018 AS OF 03/21/78
*          DATA SET TO010000   AT LEVEL 014 AS OF 03/17/78
*-----------------------------------------------------------------
*
*  PROGRAM:        TC010000
*
*  AUTHOR:         W R WILLITS
*
*  DATE WRITTEN:   MARCH, 1978
*
*  FUNCTION:       THIS PROGRAM WILL ACCESS THE CSD USER FILE
*                  UI.UI205D01 TO PULL THE USER NUMBER AND USER
*                  NAME.  THIS INFO WILL BE WRITTEN TO ANOTHER
*                  FILE FOR SUBSEQUENT MERGE WITH TO011000 WHICH
*                  IS THE UIDTABLE PROGRAM USED BY TERMSET AT
*                  LOGON TO FIND THE USER NAME FOR TSO PRINTOFF
*                  AND DUPLICAT PAGE HEADERS.
*
*                  INPUT:    UI.UI005D01
*                  OUTPUT:   TO.TO010D01.UIDNAME
*
*
*
*-----------------------------------------------------------------
         TITLE 'EXTRACT UID AND USER NAME'
         PRINT NOGEN
TO010000 SUBENTRY
         OPEN  (UIDFILE,,UIDNAME,(OUTPUT))
         GET   UIDFILE,RECORD                        RECORD ZERO
         DO    UNTIL,(CLI,RECORD,C'1',EQ)
               MVC   UNAME,BLANKS                    CLEAR NAME AREA
               GET   UIDFILE,RECORD
               IF   (CLI,RECORD,X'FF',NE),AND,(CLI,RECORD,C'1',NE),AND,X
               (CLI,RECORD+9,C'D',NE)
                     MVC   UNUM,RECORD+1
                     LA    R2,19                     MOVE 20 CHARS
                     LA    R3,RECORD+44              FIRST NAME
                     LA    R4,RECORD+27              LAST NAME
                     LA    R5,UNAME
                     DO    UNTIL,(CLI,0(R3),C' ',EQ),OR,(LTR,R2,R2,Z)
                           MVC   0(1,R5),0(R3)
                     IF    (CLI,0(R5),C' ',EQ)
                           MVC   0(1,R5),0(R4)       FIRST INITIAL
                           MVI   1(R5),C'.'
                           MVC   2(1,R5),1(R4)       SECOND INITIAL
                           MVI   3(R5),C'.'
                           SH    R2,=H'4'            DECREMENT R2
                           LA    R5,4(R5)
                     ELSE
                           LA    R3,1(R3)
                           LA    R5,1(R5)
                           BCTR  R2,0                DECREMENT R2
                     FI
                     OD
                     MVI   0(R5),C' '
                     LA    R5,1(R5)
                     DO    UNTIL,(C,R2,=F'16',L)
                           BCTR  R2,0               IF 1ST LT 3 CHARS
                     OD
                     DO    UNTIL,(LTR,R2,R2,Z)
                           MVC   0(1,R5),2(R4)
                           LA    R4,1(R4)
                           LA    R5,1(R5)
                           BCTR  R2,0
                     OD
                     LA    R6,UNAME
                     TR    0(20,R6),TRTBL           TRANS ALL SPECL
                     PUT   UIDNAME,UIDBUILD
               FI
         OD
         MVC   UIDBUILD,BLANKS
         MVC   UNUM(5),=CL5'EJECT'
         PUT   UIDNAME,UIDBUILD
         MVC   UNUM(5),=CL5'UIDEF'
         PUT   UIDNAME,UIDBUILD
         MVC   UNUM(5),=CL5'END'
         PUT   UIDNAME,UIDBUILD
         CLOSE (UIDFILE,,UIDNAME)
         SUBEXIT
         TITLE 'MISC CONSTANTS AND WORK AREAS'
         PRINT GEN
RECORD   DS   CL408
         SPACE 2
UIDBUILD DS   0CL80
         DC   CL9' ',CL6'UI'
UNUM     DC   CL3' ',C','''
UNAME    DC   CL20' ',C''''
         FILL UIDBUILD
         SPACE 2
BLANKS   DC    CL80' '
         SPACE 2
TRTBL    DS    0CL256
         DC    CL64' '
         DC    X'40404040404040404040404B40404040'       DOT (.)
         DC    CL112' '
         DC    X'40C1C2C3C4C5C6C7C8C9404040404040'       A-I
         DC    X'40D1D2D3D4D5D6D7D8D9404040404040'       J-R
         DC    X'4040E2E3E4E5E6E7E8E9404040404040'       S-Z
         DC    X'F0F1F2F3F4F5F6F7F8F9404040404040'       0-9
         SPACE 2
         EQUAL
         SPACE 2
         PRINT NOGEN
UIDNAME  DCB   DSORG=PS,MACRF=PM,DDNAME=UIDNAME
UIDFILE  DCB   DSORG=PS,MACRF=GM,DDNAME=UIDFILE
         SPACE 2
         END   TO010000
