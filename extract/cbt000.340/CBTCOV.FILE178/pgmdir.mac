*          DATA SET GU104000   AT LEVEL 004 AS OF 10/23/79
         TITLE 'GU104000 - LIST PROGRAM DIRECTORY (PGMDIR)'
*
*        PGM-ID. GU104000    (MODIFIED FROM GU101000 IN A HURRY)
*        AUTHOR. DON HIGGINS.
*        DATE.   10/23/79
*        REMARKS.
*                READ SPECIFIC MEMBERS OF FPC.WORK PAN.VALET
*                LIBRARY TO DISPLAY SPECIFIED RANGE OF PROGRAMS
*                IN THE FPC PROGRAM DIRECTORY.
*        PARM.
*
*                0 TO 8 BYTE PREFIX OF PGMS TO BE LISTED.
*        FILES.
*
*                  1.  PANDD1   - PAN LIBRARY (FPC.WORK)
*                  3.  SYSPRINT - SYSOUT DISPLAY
*
*        REFERENCES.
*
*                  1.  PANVALET SYSTEM MANAGEMENT MANUAL.
*
*
*
*
*
*
         TITLE 'MAINLINE'
GU101000 SUBENTRY
         L     R1,0(R1)
         LH    R2,0(R1)
         ST    R2,CLCLKEY+4
         ST    R2,CLCLKEY+12
         LA    R1,2(R1)
         ST    R1,CLCLKEY+8
         OPEN  (SYSPRINT,(OUTPUT))
         LOAD  EP=PAM
         ST    R0,APAM
         MVC   ACTION,=F'0'
         L     R15,APAM
         LA    R15,POPEN(R15)
         CALL  (15),(ACTION,DDNAME,BACKUP),VL
         PM    CHKOP
         PM    DISPLAY
         MVC   ACTION,=F'0'
         L     R15,APAM
         LA    R15,PCLOSE(R15)
         CALL  (15),(ACTION),VL
         PM    CHKOP
         CLOSE (SYSPRINT)
         SUBEXIT
         TITLE 'DISPLAY RANGE OF PROGRAMS'
DISPLAY  PENTRY
         MVI   PEOF,FALSE
         MVC   NAME(8),MEM1
         PM    GETPAN
         DO    WHILE,(CLI,PEOF,TRUE,NE)
               LM   R14,R1,CLCLKEY
               IF   (CLCL,R14,R0,EQ)
                    MVC  DKEY,KEY
                    MVC  DDESC,DESC
                    LA   R0,DETAIL
                    PM   PRINT
               FI
               PM   GETPAN
         OD
         PEXIT
         TITLE 'GETPAN- READ PAN MEMBER'
GETPAN   PENTRY
         MVC   ACTION,=F'0'
         MVC   COMMENT(8),=CL8'NO-ENTRY'
         L     R15,APAM
         LA    R15,PREAD(R15)
         CALL  (15),(ACTION,RECORD,NAME,INCLUDES,COMMENT),VL
         PM    CHKOP
         IF    (CLC,RECORD(2),=C'$*',EQ)
               IF   (CLC,NAME(8),MEM1,EQ)
                    MVC  NAME(8),MEM2
                    B    GETPAN  (I WOULD DO IT RIGHT, TIME PERMITTING)
               ELSEIF (CLC,NAME(8),MEM2,EQ)
                    MVC  NAME(8),MEM3
                    B    GETPAN
               ELSEIF (CLC,NAME(8),MEM3,EQ)
                    MVC  NAME(8),MEM4
                    B    GETPAN
               ELSEIF (CLC,NAME(8),MEM4,EQ)
                    MVI   PEOF,TRUE
               FI
         FI
         PEXIT
         TITLE 'CHKOP - CHECK PAN OPERATION'
CHKOP    PENTRY
         IF    (CLC,ACTION,=F'0',EQ)
               PEXIT
         FI
         L     R15,ACTION
         SUBEXIT RC=(15)
PRINT    PENTRY
         PUT   SYSPRINT
         PEXIT
         EQUAL
MEM1     DC    CL8'LM720100'
MEM2     DC    CL8'LM720200'
MEM3     DC    CL8'LM720300'
MEM4     DC    CL8'LM720400'
ACTION   DC    F'0'
DDNAME   DC    CL8'PANDD1'
BACKUP   DC    CL8'NO-ENTRY'
RECORD   DC    CL80' '
         ORG   RECORD+2
KEY      DS    CL8
DESC     DS    CL62
         ORG
CLCLKEY  DC    A(KEY,0,0,0)
NAME     DC    CL22' '
INCLUDES DC    CL8'YES'
COMMENT  DC    CL52'NO-ENTRY'
EOF      LOGICAL FALSE,(TRUE,FALSE)
PEOF     LOGICAL FALSE,(TRUE,FALSE)
APAM     DC    A(0)
POPEN    EQU   0
PSRCH    EQU   8
PREAD    EQU   16
PCLOSE   EQU   24
SYSPRINT DCB   DSORG=PS,RECFM=F,BLKSIZE=80,DDNAME=SYSPRINT,MACRF=PM
DETAIL   DS    0CL80
         DC    C' '
DKEY     DC    CL8' ',C'  '
DDESC    DC    CL62' '
         FILL  DETAIL
         END
