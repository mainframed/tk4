*          DATA SET TO002000   AT LEVEL 019 AS OF 05/09/78
*          DATA SET TO002000   AT LEVEL 018 AS OF 03/14/78
*          DATA SET TO002000   AT LEVEL 017 AS OF 02/24/78
*          DATA SET TO002000   AT LEVEL 016 AS OF 02/22/78
*          DATA SET TO002000   AT LEVEL 015 AS OF 02/22/78
         TITLE 'PORTS - TSO COMMAND TO DISPLAY TSO USER LINES'
*---------------------------------------------------------------------
*
*        DATE.     02/21/77.
*
*        AUTHOR.   W R WILLITS
*
*        REMARKS.  WHOAMI WILL DISPLAY THE CURRENT TSO USER AND
*                  AND THE USERS LINE SUBCHANNEL ADDRESS.  THIS
*                  PROGRAM IS A COMBINATION OF THE OLD WHOAMI
*                  (TO002000 - AUTHOR UNKNOWN) AND THE NEW PORTS
*                  (TO008000 - AUTHOR DSH)
*
*                  1.  IT IS REENTRANT AND CAN BE PLACED IN
*                      PLPA FOR COMMON USE.
*                  2.  THE PROGRAM DOES REQUIRE PROGRAM
*                      AUTHORIZATION FOR MODESET KEY=ZERO.
*                  3.  NOTE PROGRAM CONTAINS NO RLDS TO
*                      REDUCE I/O AND PROCESSING TIME FOR
*                      FETCH WHEN EXECUTING OUT OF PDS
*                      RATHER THAN PLPA.
*
*              REFERENCES.
*                  GC28-0751-0 OS/VS2 BEBUGGING HANDBOOK VOL. 1
*                  GC28-0709-0 OS/VS2 DEBUGGING HANDBOOK VOL. 2
*                  GC28-0886-0 OS/VS2 MVS PERFORMANCE NOTEBOOK
*                  GC28-0648-2 OS/VS2 TSO GUIDE TO COMMAND PROCESSORS
*                  GC28-0683-1 OS/VS2 SUPR. SERVICES MACRO INSTR.
*                  SY28-0715-1 OS/VS2 SYSTEM LOGIC LIBRARY
*
*
*---------------------------------------------------------------------
*
         PUNCH ' ALIAS WHOAMI &SYSDATE &SYSTIME IGNORE IEW0731'
*
         TITLE 'MAINLINE'
         PRINT NOGEN
WHOAMI   SUBENTRY BASES=(R12),RENT=YES,STACK=10,                       X
               PSTACK=R11,PSAVE=NO,PCHECK=NO
         SPACE 2
         PM    INIT                    INITIALIZE WORK AREA
         PM    WHO                     FIND USER ID AND SYSTEM ID
         PM    PORT                    FIND UCB FOR EACH TSO USER
         LA    R0,LWORK
         LR    R1,R10
         FREEMAIN R,LV=(0),A=(1)
         SUBEXIT
         TITLE 'INIT - INITIALIZE WORK AREA'
INIT     PENTRY
         LR    R4,R1                   SAVE PARM FOR GET
         LA    R0,LWORK                LENGTH OF WORK AREA
         GETMAIN R,LV=(0)
         LR    R10,R1                  R10=A(USER WORK AREA)
         EXTRACT EXWORK,'S',FIELDS=(TJID)
         LR    R0,R12
         A     R0,=A(WORK-(WHOAMI+8))
         LA    R1,LWORK
         LR    R2,R10
         LR    R3,R1
         MVCL  R2,R0                   INITIALIZE WORK AREA
         USING WORK,R10                BASE REG FOR WORK AREA
         ST    R4,APARM
         LR    R0,R12
         L     R2,PSAAOLD
         PEXIT
         TITLE 'WHOAMI CODE TO FIND CURRENT USER THIS DEVICE'
WHO      PENTRY
         LH    R3,EXWORK+2             PUT THE ASID IN REG3
         L     R4,16                   POINT TO THE CVT
         L     R4,556(R4)              POINT TO THE ASVT
         LA    R4,528(R4)              POINT TO THE FIRST ASVT ENTRY
         BCTR  R3,0                    REDUCE ASID BY 1
         SLL   R3,2                    4*(ASID-1)
         AR    R4,R3                   POINT TO ADDRESS OF ASVT ENTRY
         L     R4,0(R4)                R4 POINTS TO THE ASCB
         L     R5,176(R4)              LOAD ADDRESS OF JOB NAME
         MVC   MSG01UID,0(R5)           MOVE USERID TO MESSAGE
         L     R2,16                   POINT TO THE CVT
         L     R2,196(R2)              POINT TO THE SYS MANG CNTL AREA
         MVC   MSG01SYS,16(R2)          MOVE IN SYSTEM ID
         PEXIT
         TITLE 'WHO - SCAN TSO USERS ONLY'
PORT     PENTRY
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R4,ASVTMAXU(R2)
         DO    WHILE,(SH,R4,=H'1',NM)
               IF   (ICM,R5,15,0(R3),P)
                    PM   PROCESS
                    IF   (CLC,DAJOB,MSG01UID,EQ)
                         LA   R0,MSG01
                         PM   PUT
                         PEXIT
                    FI
               FI
               AAI  R3,4
         OD
         PEXIT
         TITLE 'PROCESS - PROCESS ONE ASCB ENTRY FOR PORT'
PROCESS  PENTRY
         IF    (ICM,R1,15,ASCBTSB(R5),Z)
               PEXIT
         FI
         L     R1,ASCBJBNS(R5)
         IF    (CLI,0(R1),C'A',NL)
               MVC  DAJOB,0(R1)
         ELSE
               MVC  DAJOB,=CL8'*LOGON*'
         FI
         L     R6,ASCBTSB(R5)
         MODESET KEY=ZERO
         UNPK  DWORK(5),TSBLINE(3,R6)
         MODESET KEY=NZERO
         TR    DWORK+1(3),HEX-240
         MVC   MSG01UCB,DWORK+1
         PEXIT
         TITLE 'PUT'
PUT      PENTRY
         LR    R1,R0                    A(MSG01)
         ICM   R1,8,=X'00'
         LA    R0,80                    MSGLEN
         TPUT  (1),(0),R
         IF    (LTR,R15,R15,NZ)
               ABEND 101,DUMP
         FI
         PEXIT
         TITLE 'LITERALS AND EQUATES'
         PRINT GEN
         EQUAL
         TITLE 'WORK AREA'
WORK     DS    0D                       BEGIN OF WORK AREA CSECT
         SPACE 2
MSG01    DS    0CL80                    BEGIN OF MSG DISPLAY AREA
         DC    CL1' '
         DC    CL9'USERID - '
MSG01UID DC    CL8' '
         DC    CL18'    LINE NUMBER - '
MSG01UCB DC    CL3' '
         DC    CL16'    ON SYSTEM - '
MSG01SYS DC    CL4' '
         DC    CL1' '
         FILL  MSG01
MSG01LEN EQU   *-MSG01                  END OF MSG DISPLAY AREA
         SPACE 2
EXWORK   DC    F'0'                     EXTRACT USER ID FOR WHO
APARM    DC    A(0)
         SPACE 2
DAJOB    DC   CL8' '                    USER ID FOR PORT
         SPACE 2
DWORK    DC   CL5' '
HEX      DC   C'0123456789ABCDEF'
         SPACE 2
LWORK    EQU   *-WORK                   END OF WORK AREA CSECT
         TITLE 'SYSTEM CONTROL BLOCK EQUATES'
*------------------
*        ASVT
*------------------
ASVTMAXU EQU   X'204'    87 NUMBER OF ASCB ENTRIES
ASVTENTY EQU   X'210'    87 START  OF ASCB ENTRIES
*------------------
*        ASCB
*------------------
ASCBCSCB EQU   X'38'     69 COMMAND SCHED. CONTROL BLOCK CHAIN
ASCBTSB  EQU   X'3C'     69 TERMINAL STATUS BLOCK FOR TSO ONLY
ASCBEJST EQU   X'40'     69 UNSIGNED 64 BIT TCB CPU TIME
ASCBFLG1 EQU   X'67'     70 FLAG FIELD
ASCBDSP1 EQU   X'72'     70 DISPATCH FLAG FIELD
ASCBOUCB EQU   X'90'     71 SRM USER CONTROL BLOCK
ASCBFMCT EQU   X'98'     71 PAGE FRAME COUNT - WORKING SET
ASCBJBNI EQU   X'AC'     72 JOBNAME OF INITS
ASCBJBNS EQU   X'B0'     72 JOBNAME OF STCS
ASCBSRBT EQU   X'C8'     72.1 SRB CPU TIME
*------------------------------------------
*        CVT - COMMUNICATION VECTOR TABLE
*------------------------------------------
CVTASCVT EQU   X'22C'   197 ADDRESS SPACE VECTOR TABLE
*---------------
*        PSA
*---------------
PSAAOLD  EQU   X'224'   528 CURRENT ASCB
*----------------------------------------------------------------
*        TSB - TERMINAL STATUS BLOCK IN PROTECTED CSA STORAGE
*----------------------------------------------------------------
TSBLINE  EQU   X'52'    739  3705 LINE ADDRESS
         SPACE 2
         END   WHOAMI
