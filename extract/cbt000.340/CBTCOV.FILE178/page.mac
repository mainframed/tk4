*          DATA SET TP403000   AT LEVEL 003 AS OF 12/30/74
         TITLE 'PAGE - PRINT PAGING STATISTICS AT CALLING TERMINAL'
*              AUTHOR.   DON HIGGINS.
*              DATE.     12/17/74.
*              REMARKS.  PRINT CURRENT PAGING STATISTICS FROM PVT
         PRINT NOGEN
PAGE     SUBENTRY
         LM    R2,R4,0(R1)
         MVC   TERM,0(R2)
         L     R2,16
         L     R2,CVTPVTP(R2)
         USING PVT,R2
         LH    R0,PVTAPC
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZ99',TO=DAPC
         LH    R0,PVTFXC
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZ99',TO=DFXC
         LH    R0,PVTLFXC
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZ99',TO=DLFXC
         LH    R0,PVTSQACT
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZ99',TO=DSQACT
         LH    R0,PVTINTSK
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZ99',TO=DINTSK
         L     R0,PVTTSOU
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZ99',TO=DTSOU
         L     R0,PVTTSOCM
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZ99',TO=DTSOCM
         LH    R0,PVTBATCM
         CVD   R0,PWORK
         EDIT  FROM=PWORK+5,MASK=' ZZZ99',TO=DBATCM
         SCALL TPPUT,(TERM,MSG,LMSG)
         SUBEXIT
         EQUAL
PWORK    DC    D'0'
TERM     DC    CL8' '
LMSG     DC    A(L'MSG)
MSG      DS    0CL120
         DC    C'PVT DATA - APC'
DAPC     DC    CL6' ',C' FXC'
DFXC     DC    CL6' ',C' LFXC'
DLFXC    DC    CL6' ',C' SQACT'
DSQACT   DC    CL6' ',C' INTSK'
DINTSK   DC    CL6' ',C' TSOU'
DTSOU    DC    CL6' ',C' TSOCM'
DTSOCM   DC    CL6' ',C' BATCM'
DBATCM   DC    CL6' '
         FILL MSG
CVTPVTP  EQU   356
PVT      DSECT
PVTFLAG1 DS    X
PVTRPLSW DS    X
PVTAPC   DS    H    AVAILABLE PAGE COUNT
PVTLTH   DS    H    LOW THRESHOLD FOR REPLACEMENT OF REAL PAGES
PVTREPCT DS    H    NUMBER TO REPLACE
PVTFLAG2 DS    X
PVTSQACR DS    X    PAGES TO REPLENISH SQA
PVTPAPC  DS    H    PRIMARY PAGES ON AUX. STORAGE
PVTLAPC  DS    H    LOW THRESHOLD FOR EXTERNAL STORAGE
PVTFLAG3 DS    X
PVTBAKUP DS    X    BACKUP % TSO
PVTTOTAX DS    F    UNCOMMITTED EXTERNAL PAGES TOTAL
PVTSUCM  DS    F    UNCOMMITTED EXTERNAL PAGES SYSTEM
PVTMAXT  DS    F    MAX TSO EXTERNAL PAGES
PVTTSOU  DS    F    PAGES IN USE BY TSO
PVTTSOCM DS    F    PAGES COMM TO TSO
PVTMAXB  DS    H    MAX BATCH EXT. PAGES
PVTBATCM DS    H    COMM. BATCH PAGES
PVTLOGB  DS    H    LOGON BUFFER
PVTTSBU  DS    H    TSO BUFFER
PVTFXC   DS    H    SYS FIXED PAGES
PVTLFXC  DS    H    SYS LONG-TERM FIXED PAGES
PVTLFXL  DS    H    LONG FIX LIMIT
PVTSFXL  DS    H    SVC-FIX LIMIT
PVTBFXL  DS    H    BRANCH ENTRY FIX LIMIT
PVTTBASE DS    H    FIX THRESHOLD BASE
PVTSQACT DS    H    SQA/LSQA PAGES
PVTLFXTF DS    H    LONG FIX THRESHOLD FACTOR
PVTSFXTF DS    H    SVC FIX THRESHOLD
PVTBFXTF DS    H    BRANCH FIX THRESHOLD
PVTINTSK DS    H    TASKS NON-DISP DUE TO PAGING
PVTDYNA  DS    H    DYNAMIC AREA FOR TSO
         END
