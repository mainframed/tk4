*          DATA SET GU026900   AT LEVEL 013 AS OF 09/22/76
         TITLE 'GU026900 - MAINLINE'
GU026900 SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     04/17/74.
*              REMARKS.  LIST DATA SET AND CATALOG BY DATA SET.
*              FILES.
*                        VFDSNMST - SORTED DATA SET MASTER FILE.
*                        VFMASTER - SORTED VTOC MASTER FILE.
*                        VFDSNRPT - DATA SET AND CATALOG REPORT.
*                        SYSCATLG - SYSTEM CATALOG.
*                        CCXXXXXX - CONNECTED CATALOGS  (XXXXXX = VOL)
*
*
*              REGISTERS.
*                        R0-R7 WORK
*                        R8    VFDSNMST RECORD
*                        R9    VFMASTER RECORD
*                        R10   CURRENT CATALOG RECORD
*                        R13   BASE AND SAVE AREA
*                        R14-R15 WORK
*
*              REV 1 DSH 09/22/76 - CHANGE GETCATLG TO GU027000
         OPEN  (VFDSNMST,(INPUT),VFMASTER,(INPUT),VFDSNRPT,(OUTPUT))
         USING DSNREC,R8
         USING MSTREC,R9
         USING CLGREC,R10
         DTIME TIMEDATE
         PERFORM R100RDSN,R190EXIT
         PERFORM R200RMST,R290EXIT
         PERFORM R300RCLG,R390EXIT
         DO    WHILE,(CLI,EOJ,TRUE,NE)
           IF    (CLC,DSNKEY,MSTKEY,EQ)
           THEN  BEGIN
*                                  DSN=MST
             IF    (CLC,DSNKEY,CLGKEY,EQ)
             THEN  BEGIN
*                                  DSN=MST=CLG
               IF    (CLC,DSNKEY,EOFKEY,EQ)
               THEN  BEGIN
*                                  DSN=MST=CLG=EOF
                 MVI   EOJ,TRUE
               THEN  END
               ELSE  BEGIN
*                                  DSN=MST=CLG<>EOF
                 PERFORM R400PDSN,R490EXIT
                 PERFORM R500PMST,R590EXIT
                 PERFORM R600PCLG,R690EXIT
                 PERFORM R700PRPT,R790EXIT
               ELSE  END
             THEN  END
             ELSE  BEGIN
*                                  DSN=MST<>CLG
               IF    (CLC,DSNKEY,CLGKEY,LT)
               THEN  BEGIN
*                                  DSN=MST<CLG
                PERFORM R400PDSN,R490EXIT
                PERFORM R500PMST,R590EXIT
                PERFORM R700PRPT,R790EXIT
               THEN  END
               ELSE  BEGIN
*                                  DSN=MST>CLG
                PERFORM R600PCLG,R690EXIT
                PERFORM R700PRPT,R790EXIT
               ELSE  END
             ELSE  END
           THEN  END
           ELSE  BEGIN
*                                  DSN<>MST
             IF    (CLC,DSNKEY,MSTKEY,LT)
             THEN  BEGIN
*                                  DSN<MST
               IF    (CLC,DSNKEY,CLGKEY,EQ)
               THEN  BEGIN
*                                  DSN=CLG<MST
                 PERFORM R400PDSN,R490EXIT
                 PERFORM R600PCLG,R690EXIT
                 PERFORM R700PRPT,R790EXIT
               THEN  END
               ELSE  BEGIN
*                                  DSN<>CLG<MST
                 IF   (CLC,DSNKEY,CLGKEY,LT)
                 THEN BEGIN
*                                  DSN<CLG<MST
                   PERFORM R400PDSN,R490EXIT
                   PERFORM R700PRPT,R790EXIT
                 THEN END
                 ELSE BEGIN
*                                  CLG<DSN<MST
                   PERFORM R600PCLG,R690EXIT
                   PERFORM R700PRPT,R790EXIT
                 ELSE END
               ELSE  END
             THEN  END
             ELSE  BEGIN
*                                  MST<DSN
               IF    (CLC,MSTKEY,CLGKEY,EQ)
               THEN  BEGIN
*                                  MST=CLG<DSN
                 PERFORM R500PMST,R590EXIT
                 PERFORM R600PCLG,R690EXIT
                 PERFORM R700PRPT,R790EXIT
               THEN  END
               ELSE  BEGIN
*                                  MST<>CLG<DSN
                 IF    (CLC,MSTKEY,CLGKEY,LT)
                 THEN  BEGIN
*                                  MST<CLG<DSN
                   PERFORM R500PMST,R590EXIT
                   PERFORM R700PRPT,R790EXIT
                 THEN  END
                 ELSE  BEGIN
*                                  CLG<MST<DSN
                   PERFORM R600PCLG,R690EXIT
                   PERFORM R700PRPT,R790EXIT
                 ELSE  END
               ELSE  END
             ELSE  END
           ELSE  END
         DO    END
         CLOSE (VFDSNMST,,VFMASTER,,VFDSNRPT)
         SUBEXIT
TRUE     EQU   0
FALSE    EQU   1
EOJ      DC    AL1(FALSE)
         TITLE 'R100 - READ DATA SET MASTER FILE'
R100RDSN PENTRY
         LA    R8,EOFREC
         MVC   DSNKEY,EOFREC
         GET   VFDSNMST
         LR    R8,R1
         MVC   DSNKEY(44),DSNDSN
         MVC   DSNKEY+44(6),DSNVOL
R190EXIT PEXIT
VFDSNMST DCB   DSORG=PS,MACRF=GL,DDNAME=VFDSNMST,EODAD=R190EXIT
         TITLE 'R200 - READ VTOC MASTER FILE'
R200RMST PENTRY
         LA    R9,EOFREC
         MVC   MSTKEY,EOFREC
         GET   VFMASTER
         LR    R9,R1
         CLI   MSTTYP,C'1'        IS THIS FORMAT 1
         BNE   R200RMST            NO, GO TRY AGAIN
         MVC   MSTKEY(44),MSTDSN
         MVC   MSTKEY+44(6),MSTVOL
R290EXIT PEXIT
VFMASTER DCB   DSORG=PS,MACRF=GL,DDNAME=VFMASTER,EODAD=R290EXIT
         TITLE 'R300 - READ SYSTEM AND CONNECTED CATALOG'
R300RCLG PENTRY
         LA    R10,EOFREC
         MVC   CLGKEY,EOFREC
         SCALL GU027000,(ADCLGREC,R390EXIT)
         L     R10,ADCLGREC
         MVC   CLGKEY(44),CLGDSN
         MVC   CLGKEY+44(6),CLGVOL
R390EXIT PEXIT
ADCLGREC DC    A(EOFREC)
         TITLE 'R400 - SET UP DSN MASTER INFO FOR PRINT'
R400PDSN PENTRY
         MVC   DDSN,DSNDSN
         MVC   DVOL,DSNVOL
         MVC   DPGMR,DSNPGMR
         MVC   DDESC,DSNDESC
         MVC   DTYPE,=CL5'DSN'
         MVI   DSTATUS+1,C'R'           REGISTED
         PERFORM R100RDSN,R190EXIT
R490EXIT PEXIT
         TITLE 'R500 - SETUP VTOC INFO FOR PRINT'
R500PMST PENTRY
         MVC   DDSN,MSTDSN
         MVC   DVOL,MSTVOL
         MVC   DTYPE,=CL5'DSN'
         LA    R1,MSTCREDT
         BAL   R14,R580DATE
         MVC   DCREDT,YYDDD
         LA    R1,MSTEXPDT
         BAL   R14,R580DATE
         MVC   DEXPDT,YYDDD
         MVI   DSTATUS+2,C'A'           ALLOCATED
         PERFORM R200RMST,R290EXIT
         B     R590EXIT
R580DATE TAG
         MVC   YYDDD,=CL5' '
         CLC   0(3,R1),=XL3'00'
         BCR   EQ,R14
         ZR    R0
         IC    R0,0(R1)
         MH    R0,=H'1000'
         MVC   PWORK(2),1(R1)
         AH    R0,PWORK
         CVD   R0,PWORK
         EDIT  TO=YYDDD-1,FROM=PWORK+5,MASK=' 99999'
         BR    R14
R590EXIT PEXIT
         TITLE 'R600 - SETUP CATALOG INFO FOR PRINT'
R600PCLG PENTRY
         MVC   DDSN,CLGDSN
         MVC   DVOL,CLGVOL
         MVI   DSTATUS,C'C'        CATALOGED
         IF    (CLI,CLGTYP,C'1',EQ)
               MVC  DTYPE,=CL5'DSN'
               ZR   R0
               ICM  R0,3,CLGDSNSQ
               CVD  R0,PWORK
               EDIT TO=DDSNSEQ,FROM=PWORK+6,MASK=' ZZZ'
               ICM  R0,3,CLGVOLSQ
               CVD  R0,PWORK
               EDIT TO=DVOLSEQ,FROM=PWORK+6,MASK=' ZZZ'
               B    R680
         ELSE  CONTINUE
         IF    (CLI,CLGTYP,C'2',EQ)
               MVC  DTYPE,=CL5'CONNX'
               B    R680
         ELSE  CONTINUE
         IF    (CLI,CLGTYP,C'3',EQ)
               MVC  DTYPE,=CL5'GDS'
               IF        (TM,CLGGDSOP,X'02',O)
                         MVI   DSTATUS+3,C'D'  DELETE OPTION
               ELSE      CONTINUE
               IF        (TM,CLGGDSOP,X'01',O)
                         MVI   DSTATUS+4,C'E'  EMPTY OPTION
               ELSE      CONTINUE
               ZR   R0
               IC   R0,CLGGDSMX
               CVD  R0,PWORK
               EDIT TO=DGDSMAX,FROM=PWORK+6,MASK=' ZZZ'
               ICM  R0,3,CLGGDSCR
               CVD  R0,PWORK
               EDIT TO=DGDSCUR,FROM=PWORK+6,MASK=' ZZZ'
               B    R680
         ELSE  CONTINUE
         IF    (CLI,CLGTYP,C'4',EQ)
               MVC  DTYPE,=CL5'ALIAS'
               B    R680
         ELSE  CONTINUE
         IF    (CLI,CLGTYP,C'5',EQ)
               MVC  DTYPE,=CL5'INDEX'
               B    R680
         ELSE  CONTINUE
         ABEND 101,DUMP            INVALID CATALOG TYPE
R680     TAG
         PERFORM R300RCLG,R390EXIT
R690EXIT PEXIT
         TITLE 'R700 - PRINT LINE WITH PAGE CONTROL AND CLEAR IT'
R700PRPT PENTRY
         IF    (SP,LINE,=P'1',NP)
               IF        (CP,PAGE,=P'0',GT)
                         LA        R0,FOOT1
                         PERFORM   R800PRNT,R890EXIT
               ELSE      CONTINUE
               ZAP       LINE,MAXLINE
               AP        PAGE,=P'1'
               EDIT      TO=DPAGE,FROM=PAGE,MASK=' ZZZ'
               LA        R0,HEAD1
               PERFORM   R800PRNT,R890EXIT
               LA        R0,HEAD2
               PERFORM   R800PRNT,R890EXIT
               LA        R0,SPACE
               PERFORM   R800PRNT,R890EXIT
         ELSE  CONTINUE
         IF    (CLC,DDSN,LASTDSN,L)  IS IT OUT OF SEQ
               SP        LINE,=P'1'
               LA        R0,DERR1
               PERFORM   R800PRNT,R890EXIT
         ELSE  CONTINUE
         MVC   LASTDSN,DDSN
         MVC   LASTTYPE,DTYPE
         LA    R0,DLINE1
         PERFORM R800PRNT,R890EXIT
         MVC   DLINE1,SPACE
R790EXIT PEXIT
         TITLE 'R800 - PUT PRINT LINE'
R800PRNT PENTRY
         PUT   VFDSNRPT
R890EXIT PEXIT
VFDSNRPT DCB   DSORG=PS,MACRF=PM,DDNAME=VFDSNRPT,RECFM=FA,BLKSIZE=133
         TITLE 'COMMON DATA'
         EQUAL
EOFREC   DC    0XL50'00',50X'FF'
         ORG   EOFREC
EOFKEY   DS    CL50
HEAD1    DS    0CL133
         DC    C'1',10C' ',C'FPC DATA SET AND CATALOG STATUS REPORT'
         DC    10C' ',C'GU026900',05C' '
TIMEDATE DC    CL22' ',CL10' ',C'PAGE'
DPAGE    DC    CL4' '
         FILL  HEAD1
HEAD2    DS    0CL133
         DC    C'0'
         DC    19C'-',C' NAME ',19C'-',C' VOLUME  TYPE STATUS CREDT'
         DC    C' EXPDT DSEQ VSEQ GMAX GCUR PGMR DESCRIPTION'
         FILL  HEAD2
FOOT1    DS    0CL133
         DC    C'0'
         DC    C'C - CATALOGED   A - ALLOCATED   R - REGISTERED'
         DC    C'  D - DELETE   E - EMPTY'
         FILL  FOOT1
SPACE    DC    CL133' '
DLINE1   DS    0CL133
         DC    C' '
DDSN     DC    CL44' ',C' '
DVOL     DC    CL6' ',C' '
DTYPE    DC    CL5' ',C'  '
DSTATUS  DC    CL4' ',C'  '
DCREDT   DC    CL5' ',C' '
DEXPDT   DC    CL5' ',C' '
DDSNSEQ  DC    CL4' ',C' '
DVOLSEQ  DC    CL4' ',C' '
DGDSMAX  DC    CL4' ',C' '
DGDSCUR  DC    CL4' ',C' '
DPGMR    DC    CL3' ',C'  '
DDESC    DC    CL27' '
         FILL  DLINE1
LASTDSN  DC    CL44' '
LASTTYPE DC    CL5' '
DERR1    DS    0CL133
         DC    C' ********** SEQUENCE ERROR DUE TO MISSING '
         DC    C'CONNECT INDEX ********'
         FILL  DERR1
YYDDD    DC    CL5' '
PWORK    DC    D'0'
MAXLINE  DC    PL2'55'
LINE     DC    PL2'0'
PAGE     DC    PL2'0'
DSNKEY   DC    CL50' '
MSTKEY   DC    CL50' '
CLGKEY   DC    CL50' '
         TITLE 'FILE RECORD DSECTS'
DSNREC   DSECT
DSNVOL   DS    CL6
DSNDSN   DS    CL44
DSNPGMR  DS    CL3
DSNDESC  DS    CL27
MSTREC   DSECT
MSTVOL   DS    CL6
MSTDSN   DS    CL44
MSTTYP   DS    CL1
MSTDSCB  DS    CL140
MSTCREDT EQU   MSTDSCB+53
MSTEXPDT EQU   MSTDSCB+56
MSTTIME  DS    CL8
CLGREC   DSECT
CLGVOL   DS    CL6
CLGDSN   DS    CL44
CLGTYP   DS    CL1
CLGDEV   DS    CL4
CLGDSNSQ DS    CL2
CLGVOLSQ DS    CL2
CLGGDSOP DS    CL1
CLGGDSMX DS    CL1
CLGGDSCR DS    CL2
         END   GU026900
