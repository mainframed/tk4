*          DATA SET GU022900   AT LEVEL 020 AS OF 09/13/73
         TITLE 'TEST 370 INSTRUCTION SIMULATOR'
*              PROGRAM-ID. GU022900
*              AUTHOR.   DON HIGGINS.
*              DATE.     07/14/73.
*              REMARKS.  TEST 370 INSTRUCTION SIMULATOR.
*
*
*              REFERENCES.
*                   IBM 370 PRINCIPLES OF OPERATION GA22-7000-3
*
*              REGISTER USAGE.
*                  R0-R8     WORK
*                  R9-R11    TEST ROUTINE BASES
*                  R12       TEST ROUTINE EXIT
*                  R13       SAVE AREA AND MAINLINE BASE
*                  R14-R15   WORK
*
*              FILES.
*                  SYSPRINT  TEST COMPLETION MESSAGES.
*                  SYSUDUMP  USER ABEND IF ANY TEST FAILED.
*
         TITLE 'TEST HOUSEKEEPING MACROS'
*
*              TSTART        TEST ROUTINE LINKAGE MACRO
*
         MACRO
&N       TSTART
         LTORG
         TITLE '&N-TEST ROUTINE'
&N       TAG
         USING &N,R11
         LA    R10,4095(R11)
         USING &N+4095,R10
         LA    R9,4095(R10)
         USING &N+4095+4095,R9
         MSG   '&N-START OF TEST'
         MEND
*
*              MSG           PRINT MESSAGE ON SYSPRINT
*
         MACRO
&N       MSG   &M
         LCLA  &ML
&ML      SETA  K'&M-2
&N       MVC   LINE+1(&ML),=C&M
         BAL   R14,PRINT
         MEND
*
*              TCC           TEST CONDITION CODE AND PRINT MSG OR ABEND
*
         MACRO
&N       TCC   &M,&TEXT
         GBLA  &NTCC
&NTCC    SETA  &NTCC+1
&N       BC    &M,MSG&NTCC
         LA    R1,&NTCC
         B     ABEND
MSG&NTCC MSG   &TEXT
         MEND
*
*              CC3           SET CONDITION CODE 3 USING TM ALL ONES
*
         MACRO
&N       CC3
&N       TM    *+1,X'FF'
         MEND
*
*              CC0           SET CONDITION CODE 0 USING TMALL ZEROS
*
         MACRO
&N       CC0
&N       CLI   *+1,X'00'
         MEND
         TITLE 'TEST INSTRUCTION MACROS'
*
*              TCLCL         TEST MACRO FOR CLCL
*
         MACRO
&N       TCLCL  &M,&A1,&L1,&A2,&L2,&LEQ,&MSG,&PAD=C' '
&N       CC3
         CNOP  0,4
         LM    R14,R1,*+8
         B     *+20
         DC    A(&A2),&PAD,AL3(&L2),A(&A1,&L1)
         CLCL  R0,R14
         STM   R14,R1,CLCLSAVE
         TCC   &M,&MSG
         AIF   (&L1 LT &LEQ).Z1
         CLC   CLCLSAVE+8(4),=A(&A1+&LEQ)
         TCC   8,'R0 OK'
         CLC   CLCLSAVE+12(4),=A(&L1-&LEQ)
         AGO   .T1
.Z1      ANOP
         CLC   CLCLSAVE+8(4),=A(&A1+&L1)
         TCC   8,'R0 OK'
         CLC   CLCLSAVE+12(4),=A(0)
.T1      ANOP
         TCC   8,'R1 OK'
         CLI   CLCLSAVE+4,&PAD
         TCC   8,'R15 PAD OK'
         AIF   (&L2 LT &LEQ).Z15
         CLC   CLCLSAVE(4),=A(&A2+&LEQ)
         TCC   8,'R14 OK'
         CLC   CLCLSAVE+5(3),=AL3(&L2-&LEQ)
         AGO   .T15
.Z15     ANOP
         CLC   CLCLSAVE(4),=A(&A2+&L2)
         TCC   8,'R14 OK'
         CLC   CLCLSAVE+5(3),=AL3(0)
.T15     ANOP
         TCC   8,'R15 OK'
         MEND
*
*              TCLM          TEST MACRO FOR CLM
*
         MACRO
&N       TCLM  &MI,&MC,&CLD
&N       L     R2,=C'1234'
         CC3
         CLM   R2,&MI,=C&CLD
         TCC   &MC,'CLM TEST OK-&MI,&MC'
         MEND
*
*              TICM          TEST MACRO FOR ICM
*
         MACRO
&N       TICM  &MI,&ILD,&MC,&CLD,&MSG
&N       L     R2,=AL1(1,2,3,4)
         ICM   R2,&MI,=AL1&ILD
         TCC   &MC,&MSG
         CL    R2,=AL1&CLD
         TCC   8,'ICM R2 OK'
         MEND
*
*              TMVCL         TEST MACRO FOR MVCL
*
         MACRO
&N       TMVCL &A1,&L1,&A2,&L2,&MSG,&PAD=C' '
         LCLA  &LM,&LP,&AM,&AP
&N       CC3
         CNOP  0,4
         LM    R14,R1,*+8
         B     *+20
         DC    A(&A2),&PAD,AL3(&L2),A(&A1,&L1)
         MVCL  R0,R14
         STM   R14,R1,MVCLSAVE
         AIF   (&L1 EQ &L2).C0
         AIF   (&L1 GT &L2).C2
         TCC   4,&MSG
.L1LE    ANOP
&LM      SETA  &L1
         CLC   MVCLSAVE+8(4),=A(&A1+&L1)
         TCC   8,'R0 OK'
         CLC   MVCLSAVE+12(4),=A(0)
         TCC   8,'R1 OK'
         CLC   MVCLSAVE(4),=A(&A2+&L1)
         TCC   8,'R14 OK'
         CLI   MVCLSAVE+4,&PAD
         TCC   8,'PAD OK'
         CLC   MVCLSAVE+5(3),=AL3(&L2-&L1)
         TCC   8,'R15 OK'
         AGO   .TM
.C0      ANOP
         TCC   8,&MSG
         AGO   .L1LE
.C2      ANOP
&LM      SETA  &L2
         TCC   2,&MSG
         CLC   MVCLSAVE+8(4),=A(&A1+&L1)
         TCC   8,'R0 OK'
         CLC   MVCLSAVE+12(4),=A(0)
         TCC   8,'R1 OK'
         CLC   MVCLSAVE(4),=A(&A2+&L2)
         TCC   8,'R14 OK'
         CLI   MVCLSAVE+4,&PAD
         TCC   8,'PAD OK'
         CLC   MVCLSAVE+5(3),=AL3(0)
         TCC   8,'R15 OK'
&LP      SETA  &L1-&L2
&AP      SETA  &L2
.LOOPP   ANOP
         AIF   (&LP LT 257).LASTP
         CLI   &A1+&AP,&PAD
         BNE   TCLC&SYSNDX
         CLC   &A1+&AP+1(255),&A1+&AP
         BNE   TCLC&SYSNDX
&LP      SETA  &LP-256
&AP      SETA  &AP+256
         AGO   .LOOPP
.LASTP   ANOP
         AIF   (&LP EQ 0).TM
         CLI   &A1+&AP,&PAD
         BNE   TCLC&SYSNDX
         AIF   (&LP EQ 1).TM
         CLC   &A1+&AP+1(&LP-1),&A1+&AP
         BNE   TCLC&SYSNDX
.TM      ANOP
         AIF   (&LM LT 257).LASTM
         CLC   &A1+&AM.(256),&A2+&AM
         BNE   TCLC&SYSNDX
&LM      SETA  &LM-256
&AM      SETA  &AM+256
         AGO   .TM
.LASTM   ANOP
         AIF   (&LM EQ 0).ENDM
         CLC   &A1+&AM.(&LM),&A2+&AM
         BNE   TCLC&SYSNDX
.ENDM    ANOP
         CC0
TCLC&SYSNDX TCC 8,'MOVE FIELDS OK'
         MEND
*
*              TSTCM         TEST MACRO FOR STCM
*
         MACRO
&N       TSTCM &MI,&CLD,&ILD,&MSG
&N       MVC   STCM1,=AL1(9,9,9,9)
         L     R2,=AL1&ILD
         STCM  R2,&MI,STCM1
         CLC   STCM1,=AL1&CLD
         TCC   8,&MSG
         MEND
         TITLE 'TEST MAINLINE'
TEST370  SUBENTRY BASES=(R13)
         OPEN  (SYSPRINT,(OUTPUT))
         L     R11,=A(CDS)   PG127
         BALR  R12,R11
         L     R11,=A(CLCL)  PG 129
         BALR  R12,R11
         L     R11,=A(CLM)   PG 129
         BALR  R12,R11
         L     R11,=A(CS)    PG 126
         BALR  R12,R11
         L     R11,=A(ICM)   PG 133
         BALR  R12,R11
         L     R11,=A(MC)    PG 136
         BALR  R12,R11
         L     R11,=A(MVCL)  PG 137
         BALR  R12,R11
         L     R11,=A(SRP)    PG 158
         BALR  R12,R11
         L     R11,=A(STCK)   PG 145
         BALR  R12,R11
         L     R11,=A(STCM)  PG 145
         BALR  R12,R11
         MSG   'END OT TESTS'
         CLOSE (SYSPRINT)
         SUBEXIT
PRINT    TAG
         ST    R14,PSAVE
         PUT   SYSPRINT,LINE
         MVC   LINE+1(132),LINE
         L     R14,PSAVE
         BR    R14
ABEND    TAG
         O     R1,=X'80000000' OR IN DUMP BIT IN ABEND REG
         SVC   13
PSAVE    DS    F
LINE     DC    CL133' '
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FA,BLKSIZE=133
CDS      TSTART
         LM    R2,R5,CDS1
         CC3
         CDS   R2,R4,CDS1
         TCC   8,'CDS EQ CC OK'
         STM   R4,R5,CDS2
         CLC   CDS2,CDS1
         TCC   8,'CDS STORE OK'
         CC3
         CDS   R2,R4,CDS1
         TCC   4,'CDS NE CC OK'
         STM   R2,R3,CDS2
         CLC   CDS2,CDS1
         TCC   8,'CDS  LOAD OK'
         BR    R12
CDS1     DC    0D'0',16AL1(*-CDS1+1)
CDS2     DC    XL8'00'
CLCL     TSTART
         TCLCL 8,CLC1,5,CLC2,5,5,'CLC1.EQ.CLC2'
         TCLCL 2,CLC3,5,CLC4,5,3,'CLC3.GT.CLC4'
         TCLCL 4,CLC4,5,CLC3,5,3,'CLC4.LT.CLC3'
         TCLCL 8,CLC5,300,CLC6,300,300,'CLC5.EQ.CLC6'
         TCLCL 2,CLC7,300,CLC8,300,275,'CLC7.GT.CLC8'
         TCLCL 4,CLC8,300,CLC7,300,275,'CLC8.LT.CLC7'
         TCLCL 8,CLC9,10,CLC10,5,10,'CLC9.EQ.CLC10'
         TCLCL 8,CLC10,5,CLC9,10,10,'CLC10.EQ.CLC9'
         TCLCL 2,CLC11,10,CLC12,5,7,'CLC11.GT.CLC12'
         TCLCL 4,CLC12,5,CLC11,10,7,'CLC12.LT.CLC11'
         TCLCL 8,CLC13,300,CLC14,1,300,'CLC13.EQ.CLC14'
         TCLCL 8,CLC14,1,CLC13,300,300,'CLC14.EQ.CLC13'
         TCLCL 2,CLC13,301,CLC14,1,300,'CLC13.GT.CLC14'
         TCLCL 4,CLC14,1,CLC13,301,300,'CLC14.LT.CLC13'
         BR    R12
CLCLSAVE DS    4F
CLC1     DC    C'12345'
CLC2     DC    C'12345'
CLC3     DC    C'ABCDE'
CLC4     DC    C'ABCAE'
CLC5     DC    200X'00',100AL1(*-CLC5-200)
CLC6     DC    200X'00',100AL1(*-CLC6-200)
CLC7     DC    200X'11',75AL1(*-CLC7-200),C'B',24X'FF'
CLC8     DC    200X'11',75AL1(*-CLC8-200),C'A',24X'FF'
CLC9     DC    C'ABCDE',5C' '
CLC10    DC    C'ABCDE'
CLC11    DC    C'12345  89A'
CLC12    DC    C'12345'
CLC13    DC    300C' ',C'2'
CLC14    DC    C' '
CLM      TSTART
         TCLM  00,8,'1234'
         TCLM  01,8,'4999'
         TCLM  02,8,'3999'
         TCLM  03,8,'3499'
         TCLM  04,8,'2999'
         TCLM  05,8,'2499'
         TCLM  06,8,'2399'
         TCLM  07,8,'2349'
         TCLM  08,8,'1999'
         TCLM  09,8,'1499'
         TCLM  10,8,'1399'
         TCLM  11,8,'1349'
         TCLM  12,8,'1299'
         TCLM  13,8,'1249'
         TCLM  14,8,'123'
         TCLM  14,8,'1239'
         TCLM  15,8,'1234'
         TCLM  01,4,'5999'
         TCLM  02,4,'5999'
         TCLM  04,4,'3999'
         TCLM  08,4,'2999'
         TCLM  01,2,'3999'
         TCLM  02,2,'2999'
         TCLM  04,2,'1999'
         TCLM  08,2,'0999'
         BR    R12
CS       TSTART
         LM    R2,R3,CS1
         CC3
         CS    R2,R3,CS1
         TCC   8,'CS EQ CC OK'
         CL    R3,CS1
         TCC   8,'CS STORE OK'
         CC3
         CS    R2,R3,CS1
         TCC   4,'CS NE CC OK'
         CL    R2,CS1
         TCC   8,'CS LOAD OK'
         BR    R12
CS1      DC    0F'0',8AL1(*-CS1+1)
ICM      TSTART
         TICM  00,(9,9,9,9),8,(1,2,3,4),'ICM 0'
         TICM  01,(8,9,9,9),2,(1,2,3,8),'ICM 1'
         TICM  02,(8,9,9,9),2,(1,2,8,4),'ICM 2'
         TICM  03,(7,8,9,9),2,(1,2,7,8),'ICM 3'
         TICM  04,(8,9,9,9),2,(1,8,3,4),'ICM 4'
         TICM  05,(7,8,9,9),2,(1,7,3,8),'ICM 5'
         TICM  06,(7,8,9,9),2,(1,7,8,4),'ICM 6'
         TICM  07,(6,7,8,9),2,(1,6,7,8),'ICM 7'
         TICM  08,(8,9,9,9),2,(8,2,3,4),'ICM 8'
         TICM  09,(7,8,9,9),2,(7,2,3,8),'ICM 9'
         TICM  10,(7,8,9,9),2,(7,2,8,4),'ICM 10'
         TICM  11,(6,7,8,9),2,(6,2,7,8),'ICM 11'
         TICM  12,(7,8,9,9),2,(7,8,3,4),'ICM 12'
         TICM  13,(6,7,8,9),2,(6,7,3,8),'ICM 13'
         TICM  14,(6,7,8,9),2,(6,7,8,4),'ICM 14'
         TICM  15,(5,6,7,8),2,(5,6,7,8),'ICM 15'
         TICM  12,(0,0,0,0),8,(0,0,3,4),'ICM 12 WITH ZEROS'
         TICM  08,(128,0,0,0),4,(128,2,3,4),'ICM 8 WITH HIGH BIT'
         TICM  01,(128,0,0,0),4,(1,2,3,128),'ICM 1 WITH HIGH BIT'
         BR    R12
MC       TSTART
         MC    *,0
         MC    *,1
         MC    *,2
         MC    *,3
         MSG   'MC END OK'
         BR    R12
MVCL     TSTART
         TMVCL MVCL1,1,MVCL2,1,'MVCL1 CC OK'
         TMVCL MVCL3,256,MVCL4,256,'MVCL2 CC OK'
         TMVCL MVCL5,257,MVCL6,257,'MVCL5 CC OK'
         TMVCL MVCL7,512,MVCL8,514,'MVCL7 CC OK'
         TMVCL MVCL3,256,MVCL9,1,'MVCL9 CC OK',PAD=C'E'
         TMVCL MVCL8,514,MVCL4,256,'MVCL8 CC OK',PAD=C'X'
         CC0
         LM    R2,R5,=A(MVCL3+1,255,MVCL3,255)
         MVCL  R2,R4
         TCC   1,'OVERLAP TEST OK'
         TMVCL MVCL1,0,MVCL2,0,'MVCL 0 COUNT OK'
         TMVCL MVCL8,514,0,0,'MVCL ALL PAD OK',PAD=C'Z'
         BR    R12
MVCLSAVE DS    4F
MVCL1    DC    C'A'
MVCL2    DC    C'B'
MVCL3    DC    256X'00'
MVCL4    DC    256X'FF'
MVCL5    DC    257X'EE'
MVCL6    DC    257X'AA'
MVCL7    DC    512X'00'
MVCL8    DC    514X'11'
MVCL9    DC    C'A'
SRP      TSTART
         CC3
         SRP   SRP1,1,0
         TCC   2,'SRP1 CC OK'
         CP    SRP1,=P'1230'
         TCC   8,'SRP1 OK'
         CC3
         SRP   SRP2,2,0
         TCC   4,'SRP2 CC OK'
         CP    SRP2,=P'-12300'
         TCC   8,'SRP2 OK'
         CC3
         SRP   SRP3,64-1,5
         TCC   2,'SRP3 CC OK'
         CP    SRP3,=P'12'
         TCC   8,'SRP3 OK'
         CC3
         SRP   SRP4,64-2,7
         TCC   4,'SRP4 CC OK'
         CP    SRP4,=P'-13'
         TCC   8,'SRP4 OK'
         CC3
         SRP   SRP5,1,0
         TCC   8,'SRP5 CC OK'
         CLI   SRP5,X'0C'
         TCC   8,'SRP5 OK'
         BR    R12
SRP1     DC    P'0123'
SRP2     DC    P'-00123'
SRP3     DC    P'123'
SRP4     DC    P'-1232'
SRP5     DC    X'0F'
STCK     TSTART
         CC0
         STCK  STCK1
         TCC   1,'STCK CC OK'
         CLC   STCK1,=XL8'00'
         TCC   8,'STCK OK'
         BR    R12
STCK1    DC    0XL8'FF',8X'FF'
STCM     TSTART
         TSTCM 00,(9,9,9,9),(1,2,3,4),'STCM  0'
         TSTCM 01,(8,9,9,9),(1,2,3,8),'STCM  1'
         TSTCM 02,(8,9,9,9),(1,2,8,4),'STCM  2'
         TSTCM 03,(7,8,9,9),(1,2,7,8),'STCM  3'
         TSTCM 04,(8,9,9,9),(1,8,3,4),'STCM  4'
         TSTCM 05,(7,8,9,9),(1,7,3,8),'STCM  5'
         TSTCM 06,(7,8,9,9),(1,7,8,4),'STCM  6'
         TSTCM 07,(6,7,8,9),(1,6,7,8),'STCM  7'
         TSTCM 08,(8,9,9,9),(8,2,3,4),'STCM  8'
         TSTCM 09,(7,8,9,9),(7,2,3,8),'STCM  9'
         TSTCM 10,(7,8,9,9),(7,2,8,4),'STCM 10'
         TSTCM 11,(6,7,8,9),(6,2,7,8),'STCM 11'
         TSTCM 12,(7,8,9,9),(7,8,3,4),'STCM 12'
         TSTCM 13,(6,7,8,9),(6,7,3,8),'STCM 13'
         TSTCM 14,(6,7,8,9),(6,7,8,4),'STCM 14'
         TSTCM 15,(5,6,7,8),(5,6,7,8),'STCM 15'
         BR    R12
STCM1    DC    XL4'00'
         LTORG
         TITLE 'TEST PGM EQUATES'
         EQUAL
         END   TEST370
