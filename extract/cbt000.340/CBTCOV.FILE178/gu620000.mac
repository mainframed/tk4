*          DATA SET GU620000   AT LEVEL 003 AS OF 11/06/74
         TITLE 'GU620000 - BYNAME CALL PROCESSOR'
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/03/74.
*              REMARKS.  PROVIDE DYNAMIC CALL BY NAME FACILITY FOR
*                        ANY SUBROUTINE MODULE.  THIS ROUTINE
*                        WILL LOAD ANY MODULE ON FIRST CALL OF
*                        THAT MODULE AND BRANCH DIRECTLY TO IT
*                        ON SUCCEEDING CALLS.
*                        OPTIONAL LOAD LIBRARY MAY BE SPECIFIED BY
*                        PROVIDING CALLLIB DD FOR STEP.
*              ARGUMENTS.
*
*                   CALL BYNAME (NAME,ARG1,ARG2, ETC.)
*
*                   NAME IS LABEL OF FIELD CONTAINING 8 CHARACTER NAME
*                   OF MODULE TO BE LOADED AND EXECUTED.
*
*                   ARG1, ARG2, ETC. ARE NORMAL ARGUEMNTS  FOR NAMED
*                   MODULE.  THE NAMED MODULE WILL FUNCTION AS IF IT
*                   WERE CALLED WITH THE FOLLOWING STATEMENT.
*                        CALL NAME (ARG1,ARG2, ETC.)
*              LIMITATIONS.
*                  1.    MODULE NAMES AND THEIR ENTRY POINTS ARE
*                        STORED IN A CHAINED LIST AND SCANNED FOR
*                        A MATCH ON EACH CALL.  OVERHEAD FOR CALL
*                        IS 16 INSTRUCTIONS PLUS 6 FOR EACH ACTIVE
*                        MODULE ON SAME CHAIN LIST.  A HASHED DIRECTORY
*                        IS USED TO REDUCE LENGTH OF CHAINS.
         PRINT NOGEN
GU620000 CSECT
         USING GU620000,R15
         STM   R14,R2,RSAVE
         LR    R2,R15
         DROP  R15
         USING GU620000,R2
         L     R1,0(R1)
         LM    R14,R15,0(R1)
         SLL   R14,4
         ALR   R15,R14
         ZR    R14
         D     R14,=A(PRIME)
         SLL   R14,2
         LPR   R14,R14
         L     R15,CHAINTAB(R14)
         DO    WHILE,(LTR,R15,R15,P)
               IF   (CLC,8(8,R15),0(R1),EQ)
                    L    R15,4(R15)
                    L    R14,RSAVE
                    LM   R0,R2,RSAVE+8
                    AAI  R1,4
                    BR   R15
               FI
               L    R15,0(R15)
         OD
         LA    R14,CHAINTAB(R14)
         ST    R14,ADCHAIN
         MVC   NAME,0(R1)
         IF    (CLI,LIBRARY,UNKNOWN,EQ)
               FINDDD =CL8'CALLLIB'
               IF   (LTR,R1,R1,P)
                    OPEN (CALLLIB)
                    MVI  LIBRARY,OPEN
               ELSE
                    MVI  LIBRARY,CLOSED
               FI
         FI
         IF    (CLI,LIBRARY,OPEN,EQ)
               LOAD EPLOC=NAME,DCB=CALLLIB
         ELSE
               LOAD EPLOC=NAME
         FI
         ST    R0,ENTRY
         LI    R0,16
         GETMAIN R,LV=(0)
         L     R14,ADCHAIN
         MVC   0(4,R1),0(R14)
         ST    R1,0(R14)
         MVC   4(4,R1),ENTRY
         MVC   8(8,R1),NAME
         L     R15,ENTRY
         L     R14,RSAVE
         LM    R0,R2,RSAVE+8
         AAI   R1,4
         BR    R15
         TITLE 'COMMON DATA'
         EQUAL
RSAVE    DC    5F'0'
PRIME    EQU   31
CHAINTAB DC    (PRIME)A(0)
ADCHAIN  DC    A(0)
ENTRY    DC    A(0)
NAME     DC    CL8' '
LIBRARY  LOGICAL UNKNOWN,(UNKNOWN,OPEN,CLOSED)
CALLLIB  DCB   DSORG=PO,MACRF=(R),DDNAME=CALLLIB
         END
