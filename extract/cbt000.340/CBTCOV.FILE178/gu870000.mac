*          DATA SET GU870000   AT LEVEL 002 AS OF 04/11/75
         TITLE 'TTIMER (GU870000) - RETURN INTERVAL FOR ANY TASK'
*              AUTHOR.   DON HIGGINS.
*              DATE.     04/11/75.
*              REMARKS.  RETURN TIME INTERVAL REMAINING FOR ANY
*                        SPECIFIED TASK TCB.  IF NO TASK TCB IS
*                        SPECIFIED, THE CPU TIME REMAINING FOR
*                        THE CALLING PROGRAMS INITIATOR TCB IS
*                        RETURNED.
*              ARGUMENTS.
*
*                        ATCB - ADDRESS OF TCB OR ZERO.
*                        INTERVAL - TIME REMAINING IN 0.01 SEC
*                                   (IF NO TQE FOR TASK ANSWER IS 0)
*              LIMITATIONS.
*
*                        1.  TO PREVENT HAVING TO USE SUPERVISOR
*                            STATE TO READ CPU CLOCK, THIS ROUTINE
*                            ALWAYS GIVES TIME REMAINING AT LAST
*                            TASK SWITCH. THIS USUALLY IS WITHIN
*                            3 SECONDS.
         TITLE 'MAIN'
         PRINT NOGEN
TTIMER   SUBENTRY
         LM    R3,R4,0(R1)    A(ATCB,INTERVAL)
         L     R1,0(R3)
         IF    (LTR,R1,R1,Z)  IS THERE A TCB SPECIFIED
               LOADTCB R1          NO, GET MY INIT TCB
               L    R1,TCBJSTCB(R1)
               IF   (LTR,R1,R1,NZ)
                    L    R1,TCBOTC(R1)
               FI
               IF   (LTR,R1,R1,Z)
                    ZR   R0
                    ST   R0,0(R4)  NO ANSWER
                    SUBEXIT
               FI
         FI
         L     R2,TCBTME(R1)
         IF    (LTR,R2,R2,Z),      IS THERE A VALID TQE FOR TCB        X
               OR,(CLM,R1,7,TQEFLGS+1(R2),NE), IS IT VALID FOR TCB     X
               OR,(TM,TQEFLGS(R2),X'08',NZ)    IS IT STILL ALIVE
               ZR   R0
               ST   R0,0(R4)  NO, SET ANSWER = 0
               SUBEXIT
         FI
         IF    (TM,TQEFLGS(R2),X'03',NZ)  IS IT TASK TQE
               LM   R6,R7,TQESAV(R2)    NO GET REMAINDER
         ELSE
               LM   R6,R7,TQEVAL(R2)    YES, USE REMAINDER AT LAST
         FI                             TASK SWITCH
*
*              CONVERT MICS TO TU'S
*
         ZR    R0
         IF    (C,R6,HALFDAY1,H)
               IF   (SL,R7,HALFDAY1+4,M)
                    BCTR R6,0
               FI
               SL   R6,HALFDAY1
               L    R0,TM12
         FI
         SLDL  R6,12
         D     R6,MICPERTU
         ALR   R0,R7
*
*              CONVERT TH'S TO 0.01 SEC
*
         LR    R1,R0
         ZR    R0
         D     R0,=F'384'
         ST    R1,0(R4)      SET ANSWER
         SUBEXIT
         TITLE 'COMMON DATA'
         EQUAL
TCBJSTCB EQU   X'7C' FE 290
TCBOTC   EQU   X'84' FE 290
TCBTME   EQU   X'78' FE 290
TQEFLGS  EQU   X'00' FE 123
*
* VS2 REL 1.7 ONLY ( SEE IEAVST00 TTIMER)
*
TQEVAL   EQU   X'10'     DOUBLE WORD WITH BIT 52 = 1 MICRO-SECOND
TQESAV   EQU   X'20'     DOUBLE WORD WITH BIT 52 = 1 MICRO-SECOND
         DS    0D
HALFDAY1 DC    X'0000A0EEBB000000' USEC IN 12 HOURS
TM12     DC    X'62E08000'         TU'S IN 12 HOURS
MICPERTU DC    X'1A0AAAAB'         26.04166 SHIFTED 12 BITS
         END
