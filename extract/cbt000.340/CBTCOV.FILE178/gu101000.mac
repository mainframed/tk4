*          DATA SET GU101000   AT LEVEL 017 AS OF 07/06/78
*          DATA SET GU101000   AT LEVEL 013 AS OF 07/03/78
*          DATA SET GU101000   AT LEVEL 010 AS OF 05/23/78
         TITLE 'GU101000 - READ ONLY VERSION OF PAN#1'
*
*        PGM-ID. GU101000
*        AUTHOR. DON HIGGINS
*        DATE.   05/18/78
*        REMARKS.
*                READ ONLY VERSION OF PAN#1 WHICH SUPPORTS THE
*                FOLLOWING PAN#1 COMMANDS  (++ OR --)
*
*                  1.  ++WRITE WORK,NAME
*                  2.  ++EJECT
*                  3.  ++WRITE PRINT,NAME
*                  4.  ++INSERT WORK
*                      ($+ TO ++, $- TO --, AND $/ TO //)
*
*                WHERE NAME IS ANY MEMBER OF PAN.VALET LIBRARY.
*
*        PARM.
*
*                IF PARM IS EQUAL TO 'IEBUPDTE', THE SYSIN CONTROL
*                FILE IS PROCESSED AS FOLLOWS
*
*                  1.  ALL RECORDS STARTING WITH * ARE IGNORED.
*                  2.  ALL OTHER RECORDS ARE PROCESSED AS FOLLOWS
*                      A.  COLUMN 1-8 CONTAIN PAN MEMBER NAME
*                          TO BE WRITTEN TO WORK FILE (PANDD2)
*
*                      B.  COLUMN 10 -16 CONTAIN ALIAS FOR IEBUPDTE
*                          ./ ADD NAME= STATEMENT INSERTED IN FRONT
*                          OF EACH MEMBER WRITTEN TO WORK. IF ALIAS
*                          IS BLANK, PAN NAME IS USED.
*        FILES.
*
*                  1.  PANDD1   - PAN LIBRARY (FPC.WORK, FPC.SOURCE)
*                  2.  PANDD2   - WORK FILE FOR ++WRITE WORK
*                  3.  SYSPRINT - SYSOUT FOR ++WRITE PRINT
*                  4.  SYSIN    - COMMAND INPUT
*
*        REFERENCES.
*
*                  1.  PANVALET SYSTEM MANAGEMENT MANUAL.
*
*
*
*
*
*
         TITLE 'MAINLINE'
GU101000 SUBENTRY
         L     R1,0(R1)
         IF    (CLC,0(2,R1),=H'8',EQ),AND,(CLC,2(8,R1),=C'IEBUPDTE',EQ)
               MVI  IEBUPDTE,TRUE
               PM   CHKWORK
         FI
         OPEN  (SYSIN,,SYSPRINT,(OUTPUT))
         LOAD  EP=PAM
         ST    R0,APAM
         L     R15,APAM
         LA    R15,POPEN(R15)
         CALL  (15),(ACTION,DDNAME,BACKUP),VL
         MVC   DOP,=CL8'POPEN'
         PM    CHKOP
         IF    (CLC,ACTION,=F'0',NE)
               MVI  EOF,TRUE
         FI
         PM    GET
         DO    WHILE,(CLI,EOF,TRUE,NE)
               PM  PROCESS
               PM  GET
         OD
         MVC   ACTION,=F'0'
         L     R15,APAM
         LA    R15,PCLOSE(R15)
         CALL  (15),(ACTION),VL
         MVC   DOP,=CL8'PCLOSE'
         PM    CHKOP
         EDIT TO=DSYSIN,FROM=PSYSIN,MASK=' Z,ZZZ,Z99'
         EDIT TO=DPRINT,FROM=PPRINT,MASK=' Z,ZZZ,Z99'
         EDIT TO=DPANDD1,FROM=PPANDD1,MASK=' Z,ZZZ,Z99'
         EDIT TO=DPANDD2,FROM=PPANDD2,MASK=' Z,ZZZ,Z99'
         EDIT TO=DERROR,FROM=PERROR,MASK=' Z,ZZZ,Z99'
         LA   R0,DTOTAL
         PM   PUT
         IF    (CLI,WORKOPEN,TRUE,EQ)
               CLOSE (PANDD2,,SYSIN,,SYSPRINT)
         ELSE
               CLOSE (SYSIN,,SYSPRINT)
         FI
         IF    (CP,PERROR,=P'0',NE)
               SUBEXIT RC=4
         FI
         SUBEXIT
         TITLE 'GET - GET NEXT COMMAND FROM SYSIN'
GET      PENTRY
         IF    (CLI,EOF,TRUE,EQ),OR,(CLI,SKIPREAD,TRUE,EQ)
               MVI  SKIPREAD,FALSE
               PEXIT
         FI
         GET   SYSIN,COMMAND
         AP    PSYSIN,=P'1'
         PEXIT
GETEOF   MVI   EOF,TRUE
         PEXIT
         TITLE 'PROCESS - PROCESS COMMAND'
PROCESS  PENTRY
         IF    (CLI,COMMAND,C'*',NE),OR,(CLI,IEBUPDTE,TRUE,NE)
               MVC   DLINE,COMMAND
               PM    PRINT
         FI
         IF    (CLC,COMMAND(2),=C'--',EQ)
               IF   (CLI,ERROR,TRUE,EQ)
                    MVC  DLINE,=CL80'GU101000 - COMMAND SURPRESSED'
                    PM   PRINT
                    PEXIT
               FI
               MVC  COMMAND(2),=C'++'
         FI
         IF    (CLC,COMMAND(13),=C'++WRITE WORK,',EQ)
               MVC  NAME(8),COMMAND+13
               PM   WWORK
         ELSEIF (CLC,COMMAND(14),=C'++WRITE PRINT,',EQ)
               PM   WPRINT
         ELSEIF (CLC,COMMAND(7),=C'++EJECT',EQ)
               ZAP LINE,=P'0'
         ELSEIF (CLC,COMMAND(13),=C'++INSERT WORK',EQ)
               PM  IWORK
         ELSEIF (CLI,IEBUPDTE,TRUE,EQ)
           IF  (CLI,COMMAND,C'*',NE)
               IF   (CLC,PDSNAME,=CL8' ',EQ)
                    MVC  ADDNAME,PANNAME
               ELSE
                    MVC  ADDNAME,PDSNAME
               FI
               LA   R0,ADDREC
               PM   PUTWORK
               MVC  NAME(8),PANNAME
               PM   WWORK
           FI
         ELSE
               MVI  ERROR,TRUE
               AP   PERROR,=P'1'
               MVC  DLINE,=CL80'GU101000 - INVALID INPUT COMMAND'
               PM  PRINT
         FI
         PEXIT
         TITLE 'PRINT - PRINT 80 BYTE RECORD OR MESSAGE ON SYSPRINT'
PRINT    PENTRY
         IF    (SP,LINE,=P'1',NP)
               ZAP  LINE,=P'50'
               AP   PAGE,=P'1'
               EDIT TO=DPAGE,FROM=PAGE,MASK=' ZZZ99'
               SCALL     SROSDATE,(GDATE)
               DTIME     TIMEDATE
               LA   R0,HEAD1
               PM   PUT
               LA   R0,HEAD2
               PM   PUT
         FI
         LA    R0,DETAIL
         PM    PUT
         PEXIT
         TITLE 'WWORK - WRITE PAN MEMBER ON PANDD2'
WWORK    PENTRY
         PM    CHKWORK
         MVI   PEOF,FALSE
         PM    GETPAN
         DO    WHILE,(CLI,PEOF,TRUE,NE)
               LA   R0,RECORD
               PM   PUTWORK
               PM   GETPAN
         OD
         IF    (CLC,ACTION,=F'0',EQ)
               MVC   DLINE,=CL80'GU101000 WRITE SUCCESSFUL'
               PM    PRINT
         ELSE
               MVC   DLINE,=CL80'GU101000 WRITE FAILED'
               PM    PRINT
         FI
         PEXIT
         TITLE 'CHKWORK - OPEN WORK FILE IF NOT OPEN'
CHKWORK  PENTRY
         IF    (CLI,WORKOPEN,TRUE,NE)
               OPEN (PANDD2,(OUTPUT))
               MVI  WORKOPEN,TRUE
         FI
         PEXIT
         TITLE 'GETPAN- READ PAN MEMBER'
GETPAN   PENTRY
         MVC   ACTION,=F'0'
         MVC   COMMENT(8),=CL8'NO-ENTRY'
         L     R15,APAM
         LA    R15,PREAD(R15)
         CALL  (15),(ACTION,RECORD,NAME,INCLUDES,COMMENT),VL
         MVC   DOP,=CL8'PREAD'
         PM    CHKOP
         IF    (CLC,RECORD(2),=C'$*',EQ)
               MVI   PEOF,TRUE
         ELSE
               AP   PPANDD1,=P'1'
         FI
         PEXIT
         TITLE 'CHKOP - CHECK PAN OPERATION'
CHKOP    PENTRY
         IF    (CLC,ACTION,=F'0',EQ)
               PEXIT
         FI
         L     R0,ACTION
         CVD   R0,PWORK
         EDIT  TO=DACTION,FROM=PWORK+6,MASK=' Z99'
         MVC   DLINE,DERRMSG
         PM    PRINT
         MVI   PEOF,TRUE
         MVI   ERROR,TRUE
         AP    PERROR,=P'1'
         PEXIT
         TITLE 'WPRINT - PRINT PAN MEMBER ON SYSPRINT'
WPRINT   PENTRY
         MVC   NAME(8),COMMAND+14
         MVI   PEOF,FALSE
         PM    GETPAN
         DO    WHILE,(CLI,PEOF,TRUE,NE)
               MVC  DLINE,RECORD
               PM   PRINT
               PM   GETPAN
         OD
         PEXIT
         TITLE 'IWORK - INSERT FOLLOWING DATA CARDS ON WORK FILE'
IWORK    PENTRY
         PM    CHKWORK
         PM    GET
         DO    WHILE,(CLI,EOF,TRUE,NE),AND,(CLC,COMMAND(2),=C'++',NE)
               MVC  DLINE,COMMAND
               PM   PRINT
               IF   (CLC,COMMAND(2),=C'$+',EQ)
                    MVC  COMMAND(2),=C'++'
               ELSEIF (CLC,COMMAND(2),=C'$-',EQ)
                    MVC  COMMAND(2),=C'--'
               ELSEIF (CLC,COMMAND(2),=C'$/',EQ)
                    MVC  COMMAND(2),=C'//'
               FI
               LA   R0,COMMAND
               PM   PUTWORK
               PM   GET
         OD
         MVI   SKIPREAD,TRUE
         PEXIT
         TITLE 'PUT - PUT RECORD ON SYSPRINT'
PUT      PENTRY
         PUT   SYSPRINT
         AP    PPRINT,=P'1'
         PEXIT
         TITLE 'PUTWORK - PUT RECORD ON PANDD2'
PUTWORK  PENTRY
         PUT   PANDD2
         AP    PPANDD2,=P'1'
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
COMMAND  DC    CL80' '
         ORG   COMMAND
PANNAME  DS    CL8
         DS    C
PDSNAME  DS    CL8
         ORG
IEBUPDTE LOGICAL FALSE,(TRUE,FALSE)
ADDREC   DS    0CL80
         DC    C'./ ADD NAME='
ADDNAME  DC    CL8' '
         FILL  ADDREC
ACTION   DC    F'0'
DDNAME   DC    CL8'PANDD1'
BACKUP   DC    CL8'NO-ENTRY'
RECORD   DC    CL80' '
NAME     DC    CL22' '
INCLUDES DC    CL8'YES'
COMMENT  DC    CL52'NO-ENTRY'
ERROR    LOGICAL FALSE,(TRUE,FALSE)
EOF      LOGICAL FALSE,(TRUE,FALSE)
PEOF     LOGICAL FALSE,(TRUE,FALSE)
WORKOPEN LOGICAL FALSE,(TRUE,FALSE)
SKIPREAD LOGICAL FALSE,(TRUE,FALSE)
APAM     DC    A(0)
POPEN    EQU   0
PSRCH    EQU   8
PREAD    EQU   16
PCLOSE   EQU   24
SYSIN    DCB   DSORG=PS,RECFM=FB,LRECL=80,MACRF=GM,DDNAME=SYSIN,       X
               EODAD=GETEOF
SYSPRINT DCB   DSORG=PS,RECFM=FA,BLKSIZE=133,DDNAME=SYSPRINT,MACRF=PM
PANDD2   DCB   DSORG=PS,RECFM=FB,LRECL=80,DDNAME=PANDD2,MACRF=PM
LINE     DC    PL2'0'
PAGE     DC    PL3'0'
HEAD1    DS    0CL133
         DC    C'1'
         DC    C'GU101000 READ ONLY PAN#1 REPORT '
TIMEDATE DC    CL22' ',C'   '
GDATE    DC    CL8' ',C'   PAGE'
DPAGE    DC    CL6' '
         FILL  HEAD1
HEAD2    DC    CL133' '
DETAIL   DS    0CL133
         DC    CL10' '
DLINE    DC    CL80' '
         FILL  DETAIL
PSYSIN   DC    PL4'0'
PPRINT   DC    PL4'0'
PPANDD1  DC    PL4'0'
PPANDD2  DC    PL4'0'
PERROR   DC    PL4'0'
DTOTAL   DS    0CL133
         DC    C'0TOTALS SYSIN='
DSYSIN   DC    CL10' ',C'  SYSPRINT='
DPRINT   DC    CL10' ',C'  PANDD1='
DPANDD1  DC    CL10' ',C'  PANDD2='
DPANDD2  DC    CL10' ',C'  ERRORS='
DERROR   DC    CL10' '
         FILL  DTOTAL
PWORK    DC    D'0'
DERRMSG  DS    0CL80
         DC    C'*** GU101000 PAM ERROR IN '
DOP      DC    CL8' ',C' RC='
DACTION  DC    CL4' ',C'  ***'
         FILL  DERRMSG
         END
