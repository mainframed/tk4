*          DATA SET CS313000   AT LEVEL 011 AS OF 09/01/79
*          DATA SET CS313000   AT LEVEL 010 AS OF 01/06/77
         TITLE 'CS313000 - PANVALET CHANGE REPORT'
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/29/76.
*              REMARKS.  LIST ALL CHANGES TO A PANVALET FILE BASED
*                        ON TWO 0-UP DIRECTORY FILES CREATED BY PAN#2.
*
*              FILES.
*                        CS313S01 - NEWEST DIRECTORY FILE.
*                        CS313S02 - OLDEST DIRECTORY FILE.
*                        CS313R01 - CHANGE REPORT
*                        CS314S01 - PROGRAM MASTER FILE
*              REFENCES.
*
*                   PANVALET SYSTEM MGT. MANUAL V9 OSMP9-7605 PG 52.
*
*              PARM.
*
*                   XX - REPORT NUMBER FOR TITLE
*
*                   DSN - PAN FILE DSNAME FOR REPORT TITLE
*
*        MAINTENANCE.
*
*        09/01/79 DSH - FIX EXTRA DETAIL LINE AT END OF REPORT
*                       AND REPORT DIFFERENT MAINT. DATES AS
*                       UPDATES REGARDLESS OF LEVEL.
*
         TITLE 'MAIN'
CS313000 SUBENTRY
         L     R1,0(R1)
         MVC   RPTNO,2(R1)
         LA    R0,4(R1)
         LH    R1,0(R1)
         SH    R1,=H'2'
         LA    R14,DFILE
         LR    R15,R1
         MVCL  R14,R0
         OPEN  (CS313S01,,CS313S02,,CS313R01,(OUTPUT))
         SCALL DCALL,(FIP,FOPEN,FRRO,FILE,FKEY,FREC,FSTAT)
         PM    GET1
         PM    GET2
         SCALL SROSDATE,(MMDDYY)
         DTIME TAD
         DO    WHILE,(CLI,EOF,TRUE,NE)
               IF   (CLC,NAME1,NAME2,EQ)
                    IF   (CLC,LVL1,LVL2,NE),OR,(CLC,DATE1,DATE2,NE)
                         PM   UPDATE
                    FI
                    PM   GET1
                    PM   GET2
               ELSEIF (H)
                    PM   DELETE
                    PM   GET2
               ELSE
                    PM   ADD
                    PM   GET1
               FI
         OD
         EDIT  TO=DOLD,FROM=POLD,MASK=' ZZZ99'
         EDIT  TO=DADD,FROM=PADD,MASK=' ZZZ99'
         EDIT  TO=DDELETE,FROM=PDELETE,MASK=' ZZZ99'
         EDIT  TO=DUPDATE,FROM=PUPDATE,MASK=' ZZZ99'
         EDIT  TO=DNEW,FROM=PNEW,MASK=' ZZZ99'
         LA    R0,TOTALS
         PM    PUT
         SCALL DCALL,(FIP,FCLOSE,FRRO,FILE,FKEY,FREC,FSTAT)
         CLOSE (CS313S01,,CS313S02,,CS313R01)
         SUBEXIT
CS313S01 SDCB  M=GM,R=FB,L=80,E=EOF1
CS313S02 SDCB  M=GM,R=FB,L=80,E=EOF2
CS313R01 SDCB  M=PM,R=FBA,L=133
EOF      LOGICAL FALSE,(TRUE,FALSE)
REC1     DS    0CL80
NAME1    DS    CL10
LVL1     DS    CL3
         DS    CL13
DATE1    DS    CL8
         FILL  REC1
REC2     DS    0CL80
NAME2    DS    CL10
LVL2     DS    CL3
         DS    CL13
DATE2    DS    CL8
         FILL  REC2
EOFREC   DC    80X'FF'
POLD     DC    PL3'0'
PADD     DC    PL3'0'
PDELETE  DC    PL3'0'
PUPDATE  DC    PL3'0'
PNEW     DC    PL3'0'
TOTALS   DS    0CL133
         DC    C'0'
         DC    C'OLD TOTAL='
DOLD     DC    CL6' ',C'  ADDS='
DADD     DC    CL6' ',C'   DELETES='
DDELETE  DC    CL6' ',C'   UPDATES='
DUPDATE  DC    CL6' ',C'   NEW TOTAL='
DNEW     DC    CL6' '
         FILL  TOTALS
HEADING1 DS    0CL133
         DC    C'1'
         DC    C'CS313R'
RPTNO    DC    C'XX',C' SMF ANALYSIS REPORT '
MMDDYY   DC    CL8' ',C'  '
TAD      DC    CL22' ',CL4' '
DPAGE    DC    CL6' '
         FILL  HEADING1
HEADING2 DS    0CL133
         DC    C'0'
         DC    C'PAN CHANGES TO LIBRARY '
DFILE    DC    CL44' '
         FILL  HEADING2
HEADING3 DS    0CL133
         DC    C'0'
         DC    C'--NAME-- CHANGE  OLD-LVL OLD-MDATE NEW-LVL NEW-MDATE'
         DC    C' JOBNAME  MAINT-PGMR      PROGRAM DESCRIPTION'
         FILL  HEADING3
HEADING4 DC    CL133' '
PAGE     DC    PL3'0'
PLINE    DC    PL2'0'
         TITLE 'GET1/GET2/PUT - I/O ROUTINES'
GET1     PENTRY
         GET   CS313S01,REC1
         AP    PNEW,=P'1'
         PEXIT
EOF1     MVC   REC1,EOFREC
         IF    (CLC,REC2,EOFREC,EQ)
               MVI  EOF,TRUE
         FI
         PEXIT
GET2     PENTRY
         GET   CS313S02,REC2
         AP    POLD,=P'1'
         PEXIT
EOF2     MVC   REC2,EOFREC
         IF    (CLC,REC1,EOFREC,EQ)
               MVI  EOF,TRUE
         FI
         PEXIT
PUT      PENTRY
         PUT   CS313R01
         PEXIT
         TITLE 'UPDATE/DELETE/ADD/PRINT - REPORT ROUTINES'
UPDATE   PENTRY
         AP    PUPDATE,=P'1'
         MVC   DCHANGE,=CL6'UPDATE'
         MVC   DNAME,NAME1
         MVC   DOLDLVL,LVL2
         MVC   DNEWLVL,LVL1
         MVC   DOLDMD,DATE2
         MVC   DNEWMD,DATE1
         PM    PRINT
         PEXIT
ADD      PENTRY
         AP    PADD,=P'1'
         MVC   DCHANGE,=CL6'ADD'
         MVC   DNAME,NAME1
         MVC   DNEWLVL,LVL1
         MVC   DNEWMD,DATE1
         PM    PRINT
         PEXIT
DELETE   PENTRY
         AP    PDELETE,=P'1'
         MVC   DCHANGE,=CL6'DELETE'
         MVC   DNAME,NAME2
         MVC   DOLDLVL,LVL2
         MVC   DOLDMD,DATE2
         PM    PRINT
         PEXIT
PRINT    PENTRY
         IF    (SP,PLINE,=P'1',NP)
               ZAP  PLINE,=P'50'
               AP   PAGE,=P'1'
               EDIT TO=DPAGE,FROM=PAGE,MASK=' ZZZ99'
               LA   R0,HEADING1
               PM   PUT
               LA   R0,HEADING2
               PM   PUT
               LA   R0,HEADING3
               PM   PUT
               LA   R0,HEADING4
               PM   PUT
         FI
         MVC   FKEY,DNAME
         SCALL DCALL,(FIP,FREAD,FRRO,FILE,FKEY,FREC,FSTAT)
         IF    (CLI,FSTAT+3,1,EQ)
               MVC   DJOB,JOB
               MVC   DMPGMR,MPG
               MVC   DPDESC,DSC
         FI
         LA    R0,DETAIL
         PM    PUT
         MVC   DETAIL,HEADING4
         PEXIT
DETAIL   DS    0CL133
         DC    C' '
DNAME    DC    CL8' ',C' '
DCHANGE  DC    CL6' ',CL3' '
DOLDLVL  DC    CL3' ',CL5' '
DOLDMD   DC    CL8' ',C'  '
DNEWLVL  DC    CL3' ',CL5' '
DNEWMD   DC    CL8' ',C' '
DJOB     DC    CL8' ',C' '
DMPGMR   DC    CL15' ',C' '
DPDESC   DC    CL40' '
         FILL  DETAIL
         TITLE 'COMMON DATA'
         EQUAL
         DS    0D
FIP      DC    CL8'SR203000'
FKEY     DC    CL8' '
FILE     DC    CL8'CS314S01'
FREAD    DC    F'1'
FOPEN    DC    F'3'
FCLOSE   DC    F'4'
FRRO     DC    F'4'
FSTAT    DC    F'0'
FREC     DS    CL200
PGM      EQU   FREC+4*1-4
DSC      EQU   FREC+4*3-4
MPG      EQU   FREC+4*13-4
JOB      EQU   FREC+4*26-4
         END
