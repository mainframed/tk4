*          DATA SET GU017300   AT LEVEL 014 AS OF 01/11/73
DEBUGAID SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     12/12/72.
*              REMARKS.  AID IN DEBUGGING A PROGRAM AS FOLLOWS:
*                         1.  ABEND 0C7
*                             A.  PRINT INS LOCATION AND IMAGE
*                             B.  PRINT SENDING DATA LOCATION AND IMAGE
*                             C.  NOP THE INSTRUCTION
*                             D.  CONTINUE PROGRAM
*                             E.  PRINT WARNING MSG ON CONSOLE ON
*                                 FIRST OCCURRENCE ONLY.
*
*              REFERENCES.
*                   GC28-6550 SYS. PROG. GUIDE
*
*                   GC28-6646 SPPR AND DATA MNGT SERVICES
         L     R1,0(R1)
         LH    R2,0(R1)
         BCTR  R2,R0
         CL    R2,=F'7'
         BH    *+8
         EX    R2,MOVEPGM          GET PGM TO DEBUG FROM PARM
         OPEN  (DEBUGMSG,(OUTPUT))
         WTO   'DEBUG ACTIVE'
         SPIE  ABENDRTN,(7)
         LINK  EPLOC=PGMNAME       EXECUTE DEBUG PGM
         CLOSE (DEBUGMSG)
         SUBEXIT
MOVEPGM  MVC   PGMNAME(0),2(R1)
ABENDRTN TAG
         DROP  R13
         USING *,R15
         STM   R0,R15,MYSAVE
         LA    R13,NEWSAVE
         BALR  R12,0
         DROP  R15
         USING *,R12
         LR    R2,R1
         USING PIE,R2
STARTWTO TAG
         NOP   ENDWTO
         WTO   '0C7 HAS OCCURED - DEBUG FIXUP TAKEN'
         OI    STARTWTO+1,X'F0'
ENDWTO   TAG
         L     R1,OPSW+4           R1=A(INTERRUPTION)
         LR    R3,R1
         SRL   R3,30
         SLL   R3,1                R3=INSTRUCTION LENGTH
         LA    R4,0(R1)
         SR    R4,R3               R4=A(INS)
         LR    R1,R4
         BAL   R11,TAREA           TRANSLATE AREA TO HEX
         L     R5,CVTADDR          R5=A(CVT)
         L     R5,0(R5)            R5=A(TCB WORDS)
         L     R5,4(R5)            R5=A(MYTCB)
         L     R5,0(R5)            R5=A(LAST ACTIVE RB)
RBLOOP   TAG
         CLC   PGMNAME,0(R5)       IS THIS MY RB
         BE    SUB
         L     R5,28(R5)           NO, NEXT RB
         B     RBLOOP
SUB      TAG
         LA    R0,32(R5)           R0=A(LOAD MODULE)
         SR    R1,R0               R1=A(REL INS)
         BAL   R11,TADDR           TRANSLATE ADDR TO HEX
         MVC   MSG,=CL30'FAILING INS DUE TO 0C7 AT REL'
         BAL   R11,WRITE
         BCTR  R3,R0
         EX    R3,MOVENOP          NOP THE FAILING INS
         LM    R0,R15,MYSAVE
         BR    R14
MOVENOP  MVC   0(0,R4),=XL6'070007000700'
TAREA    TAG
         UNPK  DAREA(9),0(5,R1)
         UNPK  DAREA+8(9),4(5,R1)
         MVI   DAREA+16,C' '
         TR    DAREA(16),HEX-240        8 BYTE AREA IN HEX
         BR    R11
TADDR    TAG
         ST    R1,FWORD
         UNPK  DADDR(7),FWORD+1(4)
         MVI   DADDR+6,C' '
         TR    DADDR(6),HEX-240         3 BYTE ADDRESS IN HEX
         BR    R11
WRITE    TAG
         PUT   DEBUGMSG,MSGLINE
         BR    R11
         LTORG
         EQUREGS
DEBUGMSG DCB   DSORG=PS,MACRF=PM,DDNAME=DEBUGMSG,                      X
               RECFM=FA,BLKSIZE=133
PGMNAME  DC    CL8' '
MYSAVE   DC    16F'0'
NEWSAVE  DC    18F'0'
FWORD    DC    F'0'
MSGLINE  DS    0CL133
         DC    C'0'
MSG      DC    CL30' ',C' ADDRESS '
DADDR    DC    CL6' ',C' HEX '
DAREA    DC    CL16' '
         FILL  MSGLINE
HEX      DC    C'0123456789ABCDEF'
PIE      DSECT
PICADDR  DS    F
OPSW     DS    XL8
R14R2    DS    5F
CVTADDR  EQU   X'4C'
         END   DEBUGAID
