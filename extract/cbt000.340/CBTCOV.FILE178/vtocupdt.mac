*          DATA SET GU026700   AT LEVEL 003 AS OF 08/06/74
VTOCUPDT SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     02/10/74.
*              REMARKS.  UPDATE VTOC MASTER FROM NEW VTOC FILE AND
*                        DELETE FILE.
         OPEN  (VFOLDMST,,VFNEWMST,(OUTPUT),VFNEWTRN,,VFDELETE)
         USING NEWREC,R2
         USING OLDREC,R3
         USING TRNREC,R4
         USING DELREC,R5
         PERFORM R100RDEL,R190EXIT
         PERFORM R200RTRN,R290EXIT
M010     TAG
         PERFORM R300ROLD,R390EXIT
M020     TAG
         CLC   TRNVOL,OLDVOL
         BL    M030
         BE    M040
         LA    R2,OLDREC
         PERFORM R400PNEW,R490EXIT
         B     M010
M030     TAG
         LA    R2,TRNREC
         PERFORM R400PNEW,R490EXIT
         PERFORM R200RTRN,R290EXIT
         B     M020
M040     TAG
         CLC   OLDVOL,EOFVOL
         BNE   M010
         CLOSE (VFOLDMST,,VFNEWMST,,VFNEWTRN,,VFDELETE)
         SUBEXIT
R100RDEL PENTRY
         GET   VFDELETE
         LR    R5,R1
         B     R190EXIT
R110EOF  TAG
         LA    R5,EOFVOL
R190EXIT PEXIT
R200RTRN PENTRY
         GET   VFNEWTRN
         LR    R4,R1
         B     R290EXIT
R210EOF  TAG
         LA    R4,EOFVOL
R290EXIT PEXIT
R300ROLD PENTRY
         GET   VFOLDMST
         LR    R3,R1
R310     TAG
         CLC   OLDVOL,DELVOL
         BE    R300ROLD
         BL    R390EXIT
         PERFORM R100RDEL,R190EXIT
         B     R310
R320EOF  TAG
         LA    R3,EOFVOL
R390EXIT PEXIT
R400PNEW PENTRY
         PUT   VFNEWMST,NEWREC
R490EXIT PEXIT
         EQUAL
VFOLDMST DCB   DSORG=PS,MACRF=GL,DDNAME=VFOLDMST,EODAD=R320EOF
VFNEWTRN DCB   DSORG=PS,MACRF=GL,DDNAME=VFNEWTRN,EODAD=R210EOF
VFDELETE DCB   DSORG=PS,MACRF=GL,DDNAME=VFDELETE,EODAD=R110EOF
VFNEWMST DCB   DSORG=PS,MACRF=PM,DDNAME=VFNEWMST
EOFVOL   DC    0XL6'FF',6X'FF'
NEWREC   DSECT
NEWVOL   DS    CL6
OLDREC   DSECT
OLDVOL   DS    CL6
TRNREC   DSECT
TRNVOL   DS    CL6
DELREC   DSECT
DELVOL   DS    CL6
         END   VTOCUPDT
