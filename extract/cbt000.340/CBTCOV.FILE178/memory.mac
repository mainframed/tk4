*          DATA SET TP404000   AT LEVEL 004 AS OF 12/31/74
         TITLE 'MEMORY - PRINT MEMORY USAGE STATISTICS'
*              AUTHOR.   DON HIGGINS.
*              DATE.     12/17/74.
*              REMARKS.  PRINT MEMORY STATISTICS FROM PVT AND VS2 R1.7
         PRINT NOGEN
MEMORY   SUBENTRY
         LM    R2,R4,0(R1)
         MVC   TERM,0(R2)
         L     R2,16
         L     R2,CVTPVTP(R2)
         USING PVT,R2
         L     R0,PVTTSOCM
         AH    R0,PVTBATCM
         MH    R0,=H'4'
         CVD   R0,PWORK
         EDIT  TO=DALLOC,FROM=PWORK+5,MASK=' ZZZ99'
         LH    R0,PVTFXC
         AH    R0,PVTLFXC
         AH    R0,PVTSQACT
         L     R1,16
         L     R1,CVTNUCB(R1)
         SRL   R1,12
         AR    R0,R1          ADD PAGES IN NUC
         MH    R0,=H'4'
         LR    R2,R0
         CVD   R0,PWORK
         EDIT  TO=DFIXED,FROM=PWORK+5,MASK=' ZZZ99'
         LI    R0,1000
         SR    R0,R2
         CVD   R0,PWORK
         EDIT  TO=DPAGE,FROM=PWORK+5,MASK=' ZZZ99'
         SCALL TPPUT,(TERM,MSG,LMSG)
         SUBEXIT
         EQUAL
PWORK    DC    D'0'
TERM     DC    CL8' '
LMSG     DC    A(L'MSG)
MSG      DS    0CL120
         DC    C'VIRTUAL MEMORY ALLOCATED='
DALLOC   DC    CL6' ',C'K  REAL FIXED='
DFIXED   DC    CL6' ',C'K  REAL PAGEABLE='
DPAGE    DC    CL6' ',C'K'
         FILL MSG
CVTPVTP  EQU   356
CVTNUCB  EQU   128
PVT      DSECT
PVTFLAG1 DS    X
PVTRPLSW DS    X
PVTAPC   DS    H    AVAILABLE PAGE COUNT
PVTLTH   DS    H    LOW THRESHOLD FOR REPLACEMENT OF REAL PAGES
PVTREPCT DS    H    NUMBER TO REPLACE
PVTFLAG2 DS    X
PVTSQACR DS    X    PAGES TO REPLENISH SQA
PVTPAPC  DS    H    PRIMARY PAGES ON AUX. STORAGE
PVTLAPC  DS    H    LOW THRESHOLD FOR EXTERNAL STORAGE
PVTFLAG3 DS    X
PVTBAKUP DS    X    BACKUP % TSO
PVTTOTAX DS    F    UNCOMMITTED EXTERNAL PAGES TOTAL
PVTSUCM  DS    F    UNCOMMITTED EXTERNAL PAGES SYSTEM
PVTMAXT  DS    F    MAX TSO EXTERNAL PAGES
PVTTSOU  DS    F    PAGES IN USE BY TSO
PVTTSOCM DS    F    PAGES COMM TO TSO
PVTMAXB  DS    H    MAX BATCH EXT. PAGES
PVTBATCM DS    H    COMM. BATCH PAGES
PVTLOGB  DS    H    LOGON BUFFER
PVTTSBU  DS    H    TSO BUFFER
PVTFXC   DS    H    SYS FIXED PAGES
PVTLFXC  DS    H    SYS LONG-TERM FIXED PAGES
PVTLFXL  DS    H    LONG FIX LIMIT
PVTSFXL  DS    H    SVC-FIX LIMIT
PVTBFXL  DS    H    BRANCH ENTRY FIX LIMIT
PVTTBASE DS    H    FIX THRESHOLD BASE
PVTSQACT DS    H    SQA/LSQA PAGES
PVTLFXTF DS    H    LONG FIX THRESHOLD FACTOR
PVTSFXTF DS    H    SVC FIX THRESHOLD
PVTBFXTF DS    H    BRANCH FIX THRESHOLD
PVTINTSK DS    H    TASKS NON-DISP DUE TO PAGING
PVTDYNA  DS    H    DYNAMIC AREA FOR TSO
         END
