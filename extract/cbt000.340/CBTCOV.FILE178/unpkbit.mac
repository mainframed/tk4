*          DATA SET GU013700   AT LEVEL 002 AS OF 11/07/72
UNPKBIT  CSECT
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/04/72.
*              REMARKS.  SET A BYTE TO A 0 OR 1 VALUE ACCORDING TO
*                        THE 0-1 SETTING OF A BIT IN A PACKED BIT
*                        SWITCH ARRAY.
*
*                        CALL  UNPKBIT(BITS,NUMBIT,BYTE)
*
*                         BITS - PACKED BIT SWITCH ARRAY
*
*                         NUMBIT-FULL WORD COMPUTATIONAL SUBSCRIPT
*                                OF THE BIT TO BE TESTED. STARTS
*                                WITH 1 BEING THE FIRST BIT IN THE
*                                FIRST BYTE OF THE BIT ARRAY.
*
*                         BYTE - ONE BYTE TO CONTAIN VALUE OF 0 OR 1
*                                ACCORDING TO SETTING OF ABOVE BIT.
*              REGISTERS
*                R1 - A(BIT ARRAY), A(BIT ARRAY + (NUMBIT-1)/8)
*                R2 - A(NUMBIT)
*                R3 - A(BYTE SWITCH OUTPUT)
*                R4 - (NUMBIT-1)/8
*                R5 - REMAINDER*4
         USING *,R15
         SAVE  (1,5)
         LM    R1,R3,0(R1)
         L     R4,0(R2)
         BCTR  R4,R0               R4=NUMBIT-1
         SR    R5,R5               R5=F'0'
         SRDA  R4,3                R4=(NUMBIT-1)/8
         SRL   R5,27               R5=REMAINDER*4
         LA    R1,0(R1,R4)         R1=A(BYTE WITH THE BIT WE EANT)
         MVI   0(R3),C'0'          SET BIT READOUT TO 0
         EX    R0,TMTAB(R5)        IS THE BIT REALLY 0
         BZ    *+8                 YES, SKIP
         MVI   0(R3),C'1'          NO, SET BIT READOUT TO 1
         RETURN (1,5)
TMTAB    TAG
         TM    0(R1),X'80'
         TM    0(R1),X'40'
         TM    0(R1),X'20'
         TM    0(R1),X'10'
         TM    0(R1),X'08'
         TM    0(R1),X'04'
         TM    0(R1),X'02'
         TM    0(R1),X'01'
         EQUREGS
         END
