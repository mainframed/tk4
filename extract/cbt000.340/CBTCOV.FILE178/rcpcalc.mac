*          DATA SET CS318000   AT LEVEL 001 AS OF 10/25/77
         TITLE 'RCPCALC - SVS VERSION OF RATE CALCULATION'
*              AUTHOR.   DON HIGGINS.
*              DATE.     10/04/77.
*              REMARKS.  SVS VERSION OF RATE CALCULATION. SEE
*                        PAT LOOK FOR INFO ON MVS VERSION.
*                        THIS PGM MODIFIED FROM FPC.MVS.ASM(RCPCALC).
RCPCALC  SUBENTRY
         LR     $PARM,R1
         USING  RCPPARM,$PARM
         LA     $RATE,RCPRATE  USE CANNED RATE TABLE FOR SVS ********
         USING  RCPRATE,$RATE
         L     $CPU,RCPCPU                     --> CPU TIME
         SPACE 2
* $TOT HAS THE ACCUMULATED CHARGES
* CHARGE FOR CPU = CPU TIME/MINUTE  * AMT OF CPU TIME
         L     R1,RT$CPU           COST OF CPU/MINUTE
         MR    R0,$CPU             MULTIPLY BY CPU TIME
         D     R0,=F'600' (1 MIN/60 SEC)*(1 SEC/100 CS)*(10 MILS/1CENT)
         LR    $TOT,R1             START ACCUMULATING CHARGES
         SPACE 2
*    CHARGE FOR TAPE EXCP'S = COST OF TAPE EXCPS/1000 * # OF TAPE EXCPS
         L     R1,RCPTEXCP         NUMBER OF TAPE EXCPS
         M     R0,RT$TEXCP         MULTIPLY BY COST OF TAPE EXCPS/1000
         D     R0,=F'100'          (10 MILS/1 CENT)/(1000 EXCPS)
         AR    $TOT,R1             ACCUMULATE CHARGES
         SPACE 2
* CHARGE FOR DISK EXCP'S = COST OF DISK EXCPS/1000 * #OF DISK EXCPS
         L     R1,RCPDEXCP         NUMBER OF DISK EXCPS
         M     R0,RT$DEXCP         MULTIPLY BY COST OF DISK EXCPS/1000
         SR    R0,R0
         D     R0,=F'100'          (10 MILS/1 CENT)/(1000 EXCPS)
         AR    $TOT,R1             ACCUMULATE CHARGES
         SPACE 2
* COMPUTED TIME = CPU TIME + SIGMA(AVERAGE DEVICE EXCP TIME * EXCPS)
         L     R1,RTDISKTM         AMT OF TIME PER DISK EXCP
         M     R0,RCPDEXCP         MULTIPLY BY # OF DISK EXCPS
         LR    $WB,R1              START ACCUMULATING COMPUTED TIME
         L     R1,RTTAPETM         CALCULATE TIME FOR TAPE EXCPS
         M     R0,RCPTEXCP
         AR    $WB,R1             ACCUMULATE COMPUTED TIME
         L     R1,RTRDRTM         CALCULATE TIME FOR READER EXCPS
         M     R0,RCPRDR
         AR    $WB,R1             ACCUMULATE COMPUTED TIME
         L     R1,RTPRNTTM        CALCULATE TIME FOR PRINTER EXCPS
         M     R0,RCPPRNTR
         AR    $WB,R1             ACCUMULATE COMPUTED TIME
         L     R1,RTPNCHTM        CALCULATE TIME FOR PUNCH EXCPS
         M     R0,RCPPNCH
         AR    $WB,R1             ACCUMULATE COMPUTED TIME
         L     R1,RTSCNRTM        CALCULATE TIME FOR SCANNER EXCPS
         M     R0,RCPSCNR
         AR    $WB,R1             ACCUMULATE COMPUTED TIME
         SR    $WA,$WA
         D     $WA,=F'10'         1 MS/10 CS
         AR    $WB,$CPU           ADD ACTUAL CPU TIME TO COMPUTED TIME
         SPACE 2
*  CHARGE FOR DISK ALLOCATION =
*    COST OF DISK ALLOC * COMPUTED TIME * # DISK DRIVES ALLOCATED
         L     R1,RT$DALLC        COST OF DISK ALLOCATION
         MR    R0,$WB             MULTIPLY BY COMPUTED TIME
         SR    R0,R0
         D     R0,=F'600'         DIVISION FOR THE FOLLOWING ANAYLSIS:
*                       (10 MILS/1 CENT)*(1 MIN/60 SEC)*(1 SEC/100 CS)
         M     R0,RCPDALLC        MULTIPLY BY # OF DISK DRIVES ALLOC
         AR    $TOT,R1            ACCUMULATE CHARGES
         SPACE 2
*  CHARGE FOR TAPE ALLOCATION =
*    COST OF TAPE ALLOC * COMPUTED TIME * # TAPE DRIVES ALLOCATED
         L     R1,RT$TALLC        COST OF TAPE ALLOCATION
         MR    R0,$WB             MULTIPLY BY COMPUTED TIME
         SR    R0,R0
         D     R0,=F'600'         DIVISION FOR THE FOLLOWING ANAYLSIS:
*                       (10 MILS/1 CENT)*(1 MIN/60 SEC)*(1 SEC/100 CS)
         M     R0,RCPTALLC        MULTIPLY BY # OF TAPE DRIVES ALLOC
         AR    $TOT,R1            ACCUMULATE CHARGES
         SPACE 2
* CHARGE FOR CORE=COST OF CORE/100K FOR EACH COMP.MINUTE * AMT OF CORE
         L     R1,RCPCORE          AMOUNT OF CORE USED
         M     R0,RT$CORE          MULTIPLY BY COST OF CORE
         MR    R0,$CPU             MULTIPLY BY CPU TIME FOR SVS *******
         D     R0,=F'60000'        DIVIDE FOR THE FOLLOWING ANAYLSIS:
*           (1 MIN/60 SEC)*(1 SEC/100 CS)*(CORE/100K)*(10 MILS/1 CENT)
         AR    $TOT,R1             ACCUMULATE CHARGES
         SPACE 2
*    CHARGE FOR READER = COST OF READER/1000 CARDS * # OF CARDS READ
         L     R1,RCPRDR           NUMBER OF CARDS READ
         M     R0,RT$RDR           MULTIPLY BY COST OF CARDS/1000
         SR    R0,R0
         D     R0,=F'100'          (10 MILS/1 CENT)/1000 CARDS
         AR    $TOT,R1             ACCUMULATE CHARGES
         SPACE 2
*    CHARGE FOR PRINTER = COST OF PRINTER/1000 LINES * # OF LINES
         L     R1,RCPPRNTR         NUMBER OF LINES PRINTED
         M     R0,RT$PRNTR         MULTIPLY BY COST OF LINES/1000
         SR    R0,R0
         D     R0,=F'100'          (10 MILS/1 CENT)/1000 LINES
         AR    $TOT,R1             ACCUMULATE CHARGES
         SPACE 2
* CHARGE FOR PUNCH = COST OF CARD PUNCHED/1000 CARDS * # OF CARDS
         L     R1,RCPPNCH          NUMBER OF CARDS PUNCHED
         M     R0,RT$PNCH          MULTIPLY BY COST OF CARDS/1000
         SR    R0,R0
         D     R0,=F'100'          (10 MILS/1 CENT)/1000 CARDS
         AR    $TOT,R1             ACCUMULATE CHARGES
         SPACE 2
* CHARGE FOR SCANNER = COST OF SCANNER/1000 EXCPS * # OF EXCPS
         L     R1,RCPSCNR          NUMBER OF EXCPS
         M     R0,RT$SCNR          MULTIPLY BY COST OF EXCPS/1000
         SR    R0,R0
         D     R0,=F'100'          (10 MILS/1 CENT)/1000 EXCPS
         AR    $TOT,R1             ACCUMULATE CHARGES
         SPACE 2
*  ROUND TO NEAREST CENT AND RETURN CHARGE TO PARAMETER LIST
         AH    $TOT,=H'5'         ADD 5 MILS
         SR    $TOTA,$TOTA           AND
         D     $TOTA,=F'10'       ROUND TO NEAREST CENT
         ST    $TOT,RCPCHG         RETURN JOB CHARGE
         SUBEXIT
         EJECT
         EQUAL
         SPACE 2
$WA      EQU   2                  WORK REGISTER - EVEN
$WB      EQU   3                  WORK REGISTER - ODD
$CPU     EQU   7                  ACTUAL CPU TIME
$TOTA    EQU   8                  COMPLEMENT REGISTER
$TOT     EQU   9                  ACCUMULATOR FOR CHARGES
$RATE    EQU   10                 RCPRATE DSECT ADDRESSIBILITY
$PARM    EQU   11                 RCPPARM DSECT ADDRESSIBILITY
$BASE    EQU   12                 RCPCALC CSECT ADDRESSIBILITY
*
*        CANNED RATE TABLE FOR SVS ********************************
*
RCPRATE  DS    0F                 STATIC CHARGES AND TIME
RTMAP    DS    0F             RATE TABLE START
RTID     DC    C'RCPT'        TABLE INDENTIFICATION
RTGMPARM DC    AL1(245),AL3(RTLTH)  GETMAIN PARM
RTDISKTM DC    F'38'          TIME PER DISK EXCP (MS)
RTTAPETM DC    F'18'          TIME PER TAPE EXCP (MS)
RTRDRTM  DC    F'60'          TIME PER READER EXCP (MS)
RTPNCHTM DC    F'170'         TIME PER PUNCH EXCP (MS)
RTPRNTTM DC    F'56'          TIME PER PRINTER EXCP (MS)
RTSCNRTM DC    F'150'         TIME PER SCANNER EXCP (MS)
RT$CPU   DC    F'377'         COST OF CPU/MINUTE  (.01)            SVS
RT$CORE  DC    F'10'          COST OF CORE/100K PER CPU-MIN (.01)  SVS
RT$DEXCP DC    F'27'          COST OF 1000 DISK EXCPS (.01)
RT$TEXCP DC    F'35'          COST OF 1000 TAPE EXCPS (.01)
RT$RDR   DC    F'80'          COST OF 1000 CARDS READ (.01)
RT$PRNTR DC    F'26'          COST OF 1000 LINES PRINTED (.01)
RT$PNCH  DC    F'26'          COST OF 1000 CARDS PUNCHED (.01)
RT$SCNR  DC    F'221'         COST OF 1000 SCANNER EXCPS (.01)
RT$DALLC DC    F'2'      COST OF DISK DRIVE ALLOC/COMPUTED MIN  (.01)
RT$TALLC DC    F'3'      COST OF TAPE DRIVE ALLOC/COMPUTED MIN  (.01)
RTLTH    EQU   *-RTMAP        RATE TABLE LENGTH
         SPACE 5
RCPPARM  DSECT                    PARAMETERS OF JOB TO BE CALCULATED
         RCPPARM
         END
