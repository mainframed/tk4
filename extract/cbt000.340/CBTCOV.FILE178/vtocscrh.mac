*          DATA SET GU026200   AT LEVEL 018 AS OF 03/25/77
*          DATA SET GU026200   AT LEVEL 017 AS OF 03/24/77
*          DATA SET GU026200   AT LEVEL 016 AS OF 03/23/77
*          DATA SET GU026200   AT LEVEL 015 AS OF 03/23/77
*          DATA SET GU026200   AT LEVEL 013 AS OF 10/20/76
*              AUTHOR.   DON HIGGINS.
*              DATE.     02/07/74.
*              REMARKS.  SCRATCH EXPIRED DATA SETS.
*
* REVISION 1 BY V.P. DIXON ON MAY 23,1974 FOR HASP VERSION 4.
* CHANGED COMPARE FOR VERSION CHECK TO ' V 4.0' FOR A LENGTH OF 6 VICE
*  ' V 3.1' FOR A LENGTH OF 8.
*
* CHANGED $PCEORG OFFSET FROM X'A8' TO X'E4' FOR VERSION 4.0.
*
*  REV  10/20/76  W R WILLITS
*  1.  DO NOT SCRATCH TEMP DSN FOR TSO
*  2.  DO NOT SCRATCH TEMP DSN FOR BRDR
*  3.  DO NOT SCRATCH TEMP DSN FOR HOSRDR
*
*  REV  03/22/77  W R WILLITS
*  1.  USE CVTHJES TO LOCATE HASP.
*  2.  ADD MODESET TO GET OUT OF SUPR AND KEY ZERO.
*  3.  CHANGE XPCEJOBN OFFSET FORM X'6C' TO X'74'
*
*
*
*
*
GU026200 SUBENTRY
         OPEN  (VFDSNMST,,VFNEWTRN,,VFSCHRPT,(OUTPUT))
M010     TAG
         PM    R100RMST
M020     TAG
         PM    R200RTRN
M030     TAG
         CLC   MSTVDSN,TRNVDSN
         BH    M040
         BE    M060
         PM    R100RMST
         B     M030
         SPACE 2
M040     TAG
*                                       CHECK FOR SCRATCH NOW
         MVI   DSNTYPE,PERM
         MVI   DSNSTAT,EXPIRED
         CLC   TRNDSN(3),=C'SYS'
         BNE   M050
         CLI   TRNDSN+18,C'V'
         BNE   M050
         MVI   DSNTYPE,TEMP
         CLC   TRNDSN+23(2),=C'OT'      TSO?
         BNE   M045
M043     TAG
         MVI   DSNSTAT,ACTIVE           DO NOT SCRATCH
         B     M050
M045     TAG
         CLC   TRNDSN+23(4),=C'BRDR'    BRDR?
         BE    M043
         CLC   TRNDSN+23(6),=C'HOSRDR'  HOSRDR?
         BE    M043
         PM    R300CHKJ
         CLI   DSNSTAT,ACTIVE
         BE    M050
         PM    R400SCRH
         CLI   SCRHCOND,0
         BE    M050
         MVI   DSNSTAT,NOTFND
M050     TAG
         PM    R500LIST
         B     M020
M060     TAG
         CLC   MSTVDSN,EOFVDSN
         BNE   M010
         CLOSE (VFDSNMST,,VFNEWTRN,,VFSCHRPT)
         SUBEXIT
         EJECT
R100RMST PENTRY
         GET   VFDSNMST
         MVC   MSTVDSN,0(R1)
         B     R190EXIT
R110EOF  TAG
         MVC   MSTVDSN,EOFVDSN
R190EXIT PEXIT
         EJECT
R200RTRN PENTRY
         GET   VFNEWTRN
         CLI   50(R1),C'1'         IS IT FORMAT 1 DSCB
         BNE   R200RTRN
         MVC   TRNVDSN,0(R1)
         B     R290EXIT
R210EOF  TAG
         MVC   TRNVDSN,EOFVDSN
R290EXIT PEXIT
         EJECT
R300CHKJ PENTRY
         MVC   JOBNAME,TRNDSN+23
         LA    R1,JOBNAME
R305     TAG
         AAI   R1,1
         CLI   0(R1),C'.'
         BNE   R305
         MVC   0(7,R1),=CL7' '
         MVI   JOBNAME+8,C'.'
*  REV  03/22/77  W R WILLITS
         MODESET KEY=ZERO,MODE=SUP
         L     R1,16
         L     R2,CVTHJES(R1)           CVTHJES = X'14C'
         L     R2,$PCEORG(R2)           $PCEORG = X'E4'-8
R320     TAG
         LT    R2,PCENEXT(R2)           PCENEXT = X'08'
         BZ    R330                     JOB NOT ACTIVE
         CLI   PCEID+1(R2),PCEXEQID
         BNE   R320
         CLC   XPCEJOBN(8,R2),JOBNAME
         BNE   R320
         MVI   DSNSTAT,ACTIVE
         B     R390EXIT
R330     TAG
         MVI   DSNSTAT,SCRTCH
*  REV  03/22/77  W R WILLITS
R390EXIT TAG
         MODESET KEY=NZERO,MODE=PROB
         PEXIT
         EJECT
R400SCRH PENTRY
         MODESET KEY=ZERO,MODE=SUP
         L     R1,16
         L     R1,0(R1)
         L     R1,4(R1)
         L     R1,12(R1)
         LA    R1,24(R1)
R410     TAG
         CLC   4(2,R1),=C'VC'
         BNE   R420
         MVC   UCBADDR+1(3),17(R1)
         LT    R2,UCBADDR
         BZ    R420
         CLC   28(6,R2),TRNVOL
         BNE   R420
         MVC   UCBTYP,16(R2)
         B     R430
R420     TAG
         ZR    R2
         IC    R2,0(R1)
         LA    R1,0(R1,R2)
         CLI   0(R1),0
         BNE   R410
ERR201   ABEND 201,DUMP
R430     TAG
         MVI   SCRHCOND,0
         MVC   VOL,TRNVOL
         ZR    R0
         SCRATCH CAMLST
         CLM   R15,1,=X'00'
         BE    R490EXIT
         CLM   R15,1,=X'08'
         BNE   ERR202
*  REV 03/22/77  W R WILLITS
R490EXIT TAG
         MODESET KEY=NZERO,MODE=PROB
         PEXIT
         SPACE 2
ERR202   ABEND 202,DUMP
         EJECT
R500LIST PENTRY
         SP    LINE,=P'1'
         BP    R510
         DTIME TIMEDATE
         AP    PAGE,=P'1'
         EDIT  TO=DPAGE,FROM=PAGE,MASK=' ZZZ'
         PUT   VFSCHRPT,HEAD1
         PUT   VFSCHRPT,SPACE
         ZAP   LINE,=P'60'
R510     TAG
         MVC   DVOL,TRNVOL
         MVC   DDSN,TRNDSN
         MVC   DTYPE,=C'PERM'
         CLI   DSNTYPE,PERM
         BE    *+10
         MVC   DTYPE,=C'TEMP'
         MVC   DSTATUS,=CL9'EXPIRED'
         CLI   DSNSTAT,EXPIRED
         BE    R520
         MVC   DSTATUS,=CL9'SCRATCHED'
         CLI   DSNSTAT,SCRTCH
         BE    R520
         MVC   DSTATUS,=CL9'ACTIVE'
         CLI   DSNSTAT,ACTIVE
         BE    R520
         MVC   DSTATUS,=CL9'NOT FOUND'
R520     TAG
         PUT   VFSCHRPT,DLINE
R590EXIT PEXIT
         EJECT
         EQUAL
         EJECT
VFDSNMST DCB   DSORG=PS,MACRF=GL,DDNAME=VFDSNMST,EODAD=R110EOF
VFNEWTRN DCB   DSORG=PS,MACRF=GL,DDNAME=VFNEWTRN,EODAD=R210EOF
VFSCHRPT DCB   DSORG=PS,MACRF=PM,DDNAME=VFSCHRPT,RECFM=FA,BLKSIZE=133
         EJECT
MSTVDSN  DS    0CL50
MSTVOL   DC    CL6' '
MSTDSN   DC    CL44' '
TRNVDSN  DS    0CL50
TRNVOL   DC    CL6' '
TRNDSN   DC    CL44' '
DSNTYPE  DC    AL1(PERM)
PERM     EQU   0
TEMP     EQU   1
DSNSTAT  DC    AL1(EXPIRED)
EXPIRED  EQU   0
ACTIVE   EQU   1
SCRTCH   EQU   2
NOTFND   EQU   3
EOFVDSN  DC    0XL50'FF',50X'FF'
UCBADDR  DC    F'0'
CVTHJES  EQU   X'14C'                   CVT HASP JES
CDNAME   EQU   X'08'
$PCEORG  EQU   X'DC'                    X'E4'-8
PCENEXT  EQU   X'08'
PCEID    EQU   X'4A'
PCEXEQID EQU   X'03'
*  REV  03/22/77  W R WILLITS
XPCEJOBN EQU   X'74'
         SPACE 2
JOBNAME  DC    CL8' ',CL8'.'
CAMLST   CAMLST SCRATCH,TRNDSN,,VOLLST,,OVRD
VOLLST   DC    H'1'
UCBTYP   DC    XL4'00'
VOL      DC    CL6' '
         DC    X'00'
SCRHCOND DC    X'00'
LINE     DC    PL2'0'
PAGE     DC    PL2'0'
         SPACE 2
HEAD1    DS    0CL133
         DC    C'1',10C' ',C'EXPIRED/SCRATCHED DATA SET LIST '
TIMEDATE DC    CL22' ',20C' ',C'PAGE'
DPAGE    DC    CL4' '
         FILL  HEAD1
         SPACE 2
SPACE    DC    CL133' '
         SPACE 2
DLINE    DS    0CL133
         DC    C' '
DVOL     DC    CL6' ',C' '
DDSN     DC    CL44' ',C' '
DTYPE    DC    CL4' ',C' '
DSTATUS  DC    CL9' '
         FILL  DLINE
         SPACE 2
         END   GU026200
