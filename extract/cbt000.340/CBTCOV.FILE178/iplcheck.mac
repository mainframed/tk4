*          DATA SET GU012100   AT LEVEL 003 AS OF 04/26/77
*          DATA SET GU012100   AT LEVEL 002 AS OF 04/11/77
*          DATA SET GU012100   AT LEVEL 001 AS OF 04/23/73
IPLCHECK SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     07/02/72.
*              REMARKS.  VERIFY IPL TIME AND LOG REASON FOR IPL.
*
*              REV1 04/11/77 DSH - CORRECT SEQUENCE OF UPDATE TO
*                                  PREVENT SKIPPING RECORDS DUE TO
*                                  PARTIAL IPLS.
*
*
         OPEN  (IPLFILE,(UPDAT))
L1       TAG
         READ  DECB1,DI,IPLFILE,RECORD,LREC,0,BLK1+1
         CHECK DECB1
         EDIT  TO=DOLDT,FROM=IPLTIME,MASK=' 99/99/99.99F ZZ99.999'
         WTO   MF=(E,LIPLMSG)
         TIME  DEC
         STM   R0,R1,PTIME
         EDIT  TO=DNEWT,FROM=PTIME,MASK=' 99/99/99.99F ZZ99.999'
         WTO   MF=(E,NIPLMSG)
CHKHIGH  CLC   IPLTIME+5(3),PTIME+5
         BH    BADTIME
         BL    CHKLOW
         CLC   IPLTIME(4),PTIME
         BH    BADTIME
CHKLOW   SP    PTIME+5(3),=P'1'
         OI    PTIME+7,X'0F'
         CLC   IPLTIME+5(3),PTIME+5
         BNL   SETLOG
VTIME    XC    WTORECB,WTORECB
         WTOR  'VERIFY AND/OR RESET TIME - REPLY RETRY OR OK',         X
               ANS,5,WTORECB
         WAIT  ECB=WTORECB
         OC    ANS,=CL5' '
         CLC   ANS(2),=C'OK'
         BNE   L1
         B     SETLOG
BADTIME  TAG
         WTO   'NEW IPL DATE IS NOT WITHIN NORMAL LIMITS ********'
         B     VTIME
SETLOG   TAG
         TIME  DEC
         STM   R0,R1,IPLTIME
         L     R1,NEXTBLK
         LA    R1,1(R1)
         ST    R1,NEXTBLK
         CLC   NEXTBLK,MAXBLK
         BNH   WB1
         MVC   NEXTBLK,=F'1'
WB1      TAG
         XC    WTORECB,WTORECB
         MVC   REASON,=CL100' '
         WTOR  'ENTER OPERATOR ID AND REASON FOR IPL',REASON,100,      X
               WTORECB
         WAIT  ECB=WTORECB
         OC    REASON,=CL100' '
         WRITE DECB3,DI,IPLFILE,RECORD,LREC,0,NEXTBLK+1
         CHECK DECB3
         WRITE DECB2,DI,IPLFILE,RECORD,LREC,0,BLK1+1
         CHECK DECB2
         SUBEXIT
         LTORG
         EQUREGS
IPLFILE  DCB   DSORG=DA,MACRF=(RIC,WIC),DDNAME=IPLFILE,                X
               RECFM=F,BLKSIZE=LREC,OPTCD=R
PTIME    DC    D'0'
WTORECB  DC    F'0'
LIPLMSG  DC    0F'0',AL2(LIPLEND-*,0)
         DC    C'LAST IPL DATE -'
DOLDT    DC    CL22' '
LIPLEND  EQU   *
NIPLMSG  DC    0F'0',AL2(NIPLEND-*,0)
         DC    C'THIS IPL DATE -'
DNEWT    DC    CL22' '
NIPLEND  EQU   *
LREC     EQU   112
RECORD   DS    0F
NEXTBLK  DC    F'0'
IPLTIME  DC    XL8'00'
REASON   DC    CL100' '
ANS      DC    CL5' '
MAXBLK   DC    F'600'
BLK1     DC    F'0'
         END   IPLCHECK
