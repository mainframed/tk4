*          DATA SET TP409000   AT LEVEL 010 AS OF 06/11/75
         TITLE 'TP409000 - MPS DISPLAY OF JOBS IN QCB WAIT'
*              AUTHOR.   DON HIGGINS.
*              DATE.     05/07/75.
*              REMARKS.  DISPLAY JOBS AND TSO USERS IN QCB WAITS.
*                        THIS PROGRAM WAS COPIED OUT OF GU920000 TSOMON
*
*                        REV1 06/02/75 LIST WTORS OUTSTANDING
*
*
*              REV2 06/10/75 LIST RQES AND MOUNTS OUTSTANDING
*
*              REFERENCES.
*                   1.   FE HANDBOOK S229-3169-3
*                   2.   OS/VS2 SUPR PLM SY27-7244-0 DIAG. 3.9 PG 93
*                   3.   FISCHE 5742-SC1C5 IEAVENQ1 IGC056
*                   4.   A SYSTEM DUMP HUNG IN ALLOCATION.
*                   5.   OLD OS SUPR. SERV. DISCUSSION OF ENQ/DEQ.
*                   6.   OS/VS2 JOB MGT PLM SY28-0620-0 PG 566+597
         TITLE 'MAINLINE'
         PRINT NOGEN
GU920000 SUBENTRY
         L     R1,0(R1)
         MVC   TERM,0(R1)
         PM    SCANQCB
         PM    SCANWTOR
         PM    SCANRQE
         PM    SCANUCB
         SUBEXIT
         TITLE 'SCAN QCBS FOR WAITING TASKS'
SCANQCB  PENTRY
         L     R2,16
         L     R2,CVTABEND(R2)     SECONDARY CVT
         L     R2,SCVTQCBO(R2)     DUMMY QCB (IEAQQCBO)
         USING QCBMAJ,R2
         L     R2,QMJNEXT
         DO    WHILE,(LTR,R2,R2,NZ)
               L    R3,QMJMINOR
               USING QCBMIN,R3
               DO   WHILE,(LTR,R3,R3,NZ)
                    L    R4,QMNQEL
                    USING QEL,R4
                    LR   R5,R4     SAVE QEL OF OWNER
                    IF   (TM,QELCODE,QELSHAR,O)
                         MVI  TYPE,SHARE
                    ELSE
                         MVI  TYPE,EXCLUDE
                    FI
                    ICM  R4,7,QELNQEL+1
                    DO   WHILE,(LTR,R4,R4,NZ)
                         IF   (TM,QELCODE,QELSHAR,Z)
                              MVI  TYPE,EXCLUDE
                         FI
                         IF   (CLI,TYPE,EXCLUDE,EQ)
                              PM   DISPLAY
                         FI
                         ICM  R4,7,QELNQEL+1
                    OD
                    L    R3,QMNNEXT
               OD
               L    R2,QMJNEXT
         OD
         PEXIT
         TITLE 'DISPLAY ENQ WAIT INFO FOR OPERATOR AND TSO USER'
DISPLAY  PENTRY
*              R2   QCBMAJ
*              R3   QCBMIN
*              R4   QEL FOR WAITING TASK
*              R5   QEL FOR OWNER TASK
*
         LR    R14,R4
         PM    GETJOB
         MVC   WAITJOB,JOBNAME
         LR    R14,R5
         PM    GETJOB
         MVC   OWNERJOB,JOBNAME
         MVC   QNAME,QMJNAME
         ZR    R1
         IC    R1,QMNLNM
         SI    R1,1
         EX    R1,MVCRNAME
         TPPUTMSG TERM,DLINE
         PEXIT
MVCRNAME MVC   RNAME(0),QMNNAME
         TITLE 'GET JOB NAME FROM QEL IN R14'
GETJOB   PENTRY
         USING QEL,R14
         L     R1,QELTCB
         IF    (LTR,R1,R1,NP) IS THIS TSO TAS
               L    R1,16
               L    R1,CVTTSCVT(R1)
               L    R1,TSCVTTJB(R1)
               ZR   R0
               IC   R0,QELTJID2
               MH   R0,=H'48'
               AR   R1,R0
               MVC  JOBNAME,TJBUSER(R1)
         ELSE
               L    R1,TCBTIO(R1)  NO, GET NAME FROM TIOT
               IF   (CLC,TIOJOB(7,R1),=CL7'HOSINIT',EQ)
                    MVC  JOBNAME,TIOSTEP(R1)
               ELSE
                    MVC  JOBNAME,TIOJOB(R1)
               FI
         FI
         PEXIT
         TITLE 'SCAN FOR WTORS OUTSTANDING'
SCANWTOR PENTRY
         L     R2,16          CVT
         L     R2,CVTCUCB(R2) UCM
         L     R2,UCMRPYQ(R2) ORE (RQE IN MVT)
         DO    WHILE,(LTR,R2,R2,NZ)
               L    R1,OREWQE(R2)
               LA   R0,WQETXT(R1)
               L    R1,WQENBR(R1)
               ST   R1,WTORMSGL
               LA   R14,WTORMSG
               LR   R15,R1
               MVCL R14,R0
               IF   (CLC,WTORMSG+23(6),=C'MPS AT',NE)
                    TPPUTMSG TERM,WTORMSG,WTORMSGL
               FI
               L    R2,ORELKP(R2)
         OD
         PEXIT
         TITLE 'SCAN FOR RQES OUTSTANDING'
SCANRQE  PENTRY
         L     R2,16
         L     R2,CVTIOQET(R2)     R2=A(RQE TABLE)
         DO    WHILE,(CLC,0(2,R2),=X'0001',NE)
               LM   R3,R6,0(R2)    LOAD RQE
               IF   (LTR,R4,R4,P)  IS IT ACTIVE
                    SRL  R3,1      DROP IOS ERROR BIT
                    SLL  R3,17
                    SRL  R3,16     R3=A(UCB)
                    IF    (TM,UCBTYP+2(R3),X'FF'-X'80'-X'20',Z)
                         PM   LISTRQE   LIST TAPE OR DISK RQES
                    FI
               FI
               AAI  R2,20
         OD
         PEXIT
         TITLE 'LIST ACTIVE TAPE AND DISK RQES'
LISTRQE  PENTRY
         MVC   DUCB,UCBNAME(R3)
         MVC   DVOL,SRTEVOLI(R3)
         LR    R1,R6
         PM    GETJOBN
         MVC   DJOB,0(R1)
         TPPUTMSG TERM,RQEMSG
         PEXIT
         TITLE 'GET JOBNAME IN R1 GIVEN TCB IN R1'
GETJOBN  PENTRY
         L     R14,TCBTIO(R1)
         IF    (LTR,R14,R14,Z)
               LA   R1,=CL8'SYSTEM'
               PEXIT
         FI
         IF    (CLC,0(8,R14),=CL8'INIT',EQ)
               L    R1,TCBLTC(R1)
               L    R14,TCBTIO(R1)
         FI
         IF    (CLC,0(7,R14),=CL7'HOSINIT',EQ),                        X
               AND,(CLC,8(8,R14),=CL8' ',NE)
               LA   R1,8(R14)
         ELSE
               LR   R1,R14
         FI
         PEXIT
         TITLE 'SCAN UCBS FOR MOUNTS AND AVR'
SCANUCB  PENTRY
         MVI   AVR,FALSE
         L     R2,16
         L     R2,CVTILK2(R2)
         DO    WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    LH   R3,0(R2)
                    IF   (TM,UCBTYP+2(R3),X'FF'-X'80'-X'20',Z)
                         IF   (TM,SRTEDMCT(R3),X'80',O)
                              IF   (TM,UCBFL1(R3),X'40',O),            X
               OR,(CLI,UCBTYP+2(R3),X'20',EQ)
                                   PM   MOUNT
                              FI
                         ELSE
                                   IF   (CLI,UCBATI(R3),X'0C',EQ)
                                        MVI  AVR,TRUE
                                   FI
                         FI
                    FI
               FI
               AAI  R2,2
         OD
         IF    (CLI,AVR,TRUE,EQ)
               TPPUTMSG TERM,'AVR MOUNT PENDING'
         FI
         PEXIT
MOUNT    PENTRY
         MVC   DMUCB,UCBNAME(R3)
         MVC   DMVOL,SRTEVOLI(R3)
         TPPUTMSG TERM,MOUNTMSG
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
CVTILK2  EQU   X'28' FE 88
SRTEDMCT EQU   X'23' FE 131
UCBFL1   EQU   X'06' FE 125
UCBATI   EQU   X'0B' FE 125
AVR      LOGICAL FALSE,(TRUE,FALSE)
UCBNAME  EQU   X'0D' FE 125
MOUNTMSG DS    0CL60
         DC    C'MOUNT PENDING FOR '
DMUCB    DC    CL3' ',C' '
DMVOL    DC    CL6' '
         FILL  MOUNTMSG
CVTIOQET EQU   X'78' FE PG 90
UCBTYP   EQU   16   FE PG 125
SRTEVOLI EQU   28   FE PG 131
TCBLTC   EQU   X'88' FE PG 290
RQEMSG   DS    0CL60
DJOB     DC    CL8' ',C' WAITING FOR I/O ON '
DUCB     DC    CL3' ',C' '
DVOL     DC    CL6' '
         FILL  RQEMSG
CVTCUCB  EQU   X'64' FE 89 UCM
UCMRPYQ  EQU   X'1C' FE 137  ORE/RQE
ORELKP   EQU   X'00' JOB PLM 566
OREWQE   EQU   X'0C' JOB PLM 566
WQENBR   EQU   X'04' JOB PLM 597
WQETXT   EQU   X'08' JOB PLM 597
WTORMSG  DC    CL128' '
WTORMSGL DC    A(0)
CVTABEND EQU   X'C8' FE PG 91
SCVTQCBO EQU   X'14' FE PG 93
TYPE     LOGICAL SHARE,(SHARE,EXCLUDE)
CVTTSCVT EQU   X'E4' FE PG 91
TSCVTTJB EQU   X'00' FE PG 340
TJBUSER  EQU   X'1C' FE PG 347
TCBTIO   EQU   X'0C' FE PG 289
TIOJOB   EQU   X'00' FE PG 121
TIOSTEP  EQU   X'08' FE PG 121
JOBNAME  DC    CL8' '
TERM     DC    CL8' '
DLINE    DS    0CL60
WAITJOB  DC    CL8' ',C' WAITING FOR '
OWNERJOB DC    CL8' ',C' TO FREE '
QNAME    DC    CL8' ',C' '
RNAME    DC    CL256' '
         COPY  QCBD
         END
