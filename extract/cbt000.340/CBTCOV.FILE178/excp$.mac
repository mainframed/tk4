*          DATA SET TP411000   AT LEVEL 004 AS OF 07/10/75
         TITLE 'TP411000 - DISPLAY EXCPS FOR FILES DYNAMICALLY'
*              AUTHOR.   DON HIGGINS.
*              DATE.     07/07/75.
*              REMARKS.  DISPLAY EXCPS COUNTED FOR EACH FILE OF
*                        EACH TASK AT TIME OF CALL.  SELECTION
*                        CAN BE RESTRICTED TO JOB OR SINGLE FILE.
*
*              CALL.
*                        CALL EXCPS  (,JOBNAME)  (,DDNAME)
*
*                        JOBNAME - LIST EXCPS FOR ALL FILES IN JOB
*
*                        DDNAME - LIST ONLY EXCPS FOR THIS FILE IN JOB
         TITLE 'MAINLINE'
         PRINT NOGEN
TP411000 SUBENTRY
         LM    R3,R5,0(R1)
         L     R5,0(R5)
         MVC   TERM,0(R3)
         IF    (LTR,R5,R5,Z)
               MVI  ONEJOB,FALSE
               MVI  ONEDD,FALSE
         ELSE
               IF   (CLI,0(R4),C'''',EQ)
                    AAI  R4,1
               FI
               MVC  JOBNAME,0(R4)
               MVI  ONEJOB,TRUE
               IF   (CLI,8(R4),C',',EQ)
                    MVC  DDNAME,9(R4)
                    MVI  ONEDD,TRUE
               ELSE
                    MVI  ONEDD,FALSE
               FI
         FI
         L     R1,16
         L     R1,CVTHEAD(R1)
         MVI   NONE,TRUE
         DO    WHILE,(LTR,R1,R1,NZ)
               MVC  NEXTTCB,TCBTCB(R1)
               PM   SCANTCT
               L    R1,NEXTTCB
         OD
         IF    (CLI,NONE,TRUE,EQ)
               TPPUTMSG TERM,'JOBNAME OR DDNAME NOT FOUND'
         FI
         SUBEXIT
         TITLE 'SCANTCT - SCAN TCT FOR EXCP INFO'
SCANTCT  PENTRY
         L     R2,TCBTCT(R1)
         IF    (LTR,R2,R2,Z),                                          X
               OR,(CLC,TCBNTC(4,R1),=4X'00',NE),                       X
               OR,(CLC,TCBLTC(4,R1),=4X'00',NE)
               PEXIT
         FI
         L     R3,TCBTIO(R1)
         IF    (CLC,0(7,R3),=CL7'HOSINIT',EQ)
               PEXIT
         FI
         MVC   STEPNAME,8(R3)
         IF    (CLI,ONEJOB,FALSE,EQ)
               MVC  JOBNAME,0(R3)
         ELSE
               IF   (CLC,JOBNAME,0(R3),NE)
                    PEXIT
               FI
         FI
         L     R2,TCTIOTBL(R2)
         LA    R4,8(R2)
         LH    R5,0(R4)       TIOT OFFSET FROM TCT IO DD ENTRY
         DO    WHILE,(LTR,R5,R5,NZ)
               PM   TCTDD
               AAI  R4,4
               LH   R5,0(R4)
         OD
         PEXIT
         TITLE 'TCTDD - PROCESS TCT DD ENTRIES'
TCTDD    PENTRY
         LA    R1,0(R5,R3)    TIOT DD ENTRY
         IF    (CLI,ONEDD,FALSE,EQ)
               IF   (CLI,4(R1),C' ',NE)
                    MVC  DDNAME,4(R1)
               FI
         ELSE
               IF   (CLC,DDNAME,4(R1),NE)
                    PEXIT
               FI
         FI
         LH    R5,2(R4)       TCT OFFSET
         LA    R5,0(R5,R2)    TCT IO DD ENTRY
         ZR    R6
         IC    R6,2(R5)
         DO    WHILE,(LTR,R6,R6,NZ)
               PM   TCTEXCP
               SI   R6,1
               AAI  R5,16
         OD
         PEXIT
         TITLE 'TCTEXCP - PROCESS EXCP ENTRY FOR CURRENT DD'
TCTEXCP  PENTRY
         LH    R1,0(R5)       UCB ADDRESS
         IF    (LTR,R1,R1,Z)
               PEXIT
         FI
         L     R0,4(R5)       EXCP COUNT FOR UCB
         IF    (LTR,R0,R0,Z)
               PEXIT
         FI
         CVD   R0,PWORK
         EDIT  TO=DEXCP,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'
         MVC   DUCB,UCBNAME(R1)
         IF    (TM,UCBTYP+2(R1),X'FF'-X'80'-X'20',Z)
               MVC  DVOL,SRTEVOLI(R1)   TAPE OR DISK VOLUME
         ELSE
               IF   (CLI,UCBTYP+2(R1),X'08',EQ)   UNIT-RECORD
                    MVC  DVOL,=CL6'UR'
                    IF   (CLI,UCBTYP+3(R1),X'01',EQ)
                         MVC  DVOL,=CL6'READ'     HASP CARD INPUT
                    ELSE
                         IF   (CLI,UCBTYP+3(R1),X'02',EQ)
                              MVC  DVOL,=CL6'PUNCH'    HASP PUNCH
                         ELSE
                              IF   (CLI,UCBTYP+3(R1),X'08',EQ)
                                   MVC  DVOL,=CL6'PRINT' HASP PRINT
                              FI
                         FI
                    FI
               ELSE
                    MVC  DVOL,=CL6'TP'
               FI
         FI
         TPPUTMSG TERM,EXCPMSG
         MVI   NONE,FALSE
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
TCBTCT   EQU   X'A4' FE 291
TCBTIO   EQU   12    FE 289
TCBNTC   EQU   X'80' FE 290
TCBLTC   EQU   X'88' FE 290
TCTIOTBL EQU   12    FE 118
UCBNAME  EQU   13    FE 125
UCBTYP   EQU   16    FE 125
SRTEVOLI EQU   28    FE 131
CVTHEAD  EQU   X'A0' FE 88
TCBTCB   EQU   X'74' FE 289
EXCPMSG  DS    0CL60
JOBNAME  DC    CL8' ',C' '
STEPNAME DC    CL8' ',C' '
DDNAME   DC    CL8' ',C' '
DUCB     DC    CL3' ',C' '
DVOL     DC    CL6' ',C' EXCPS'
DEXCP    DC    CL10' '
         FILL  EXCPMSG
ONEJOB   LOGICAL FALSE,(TRUE,FALSE)
ONEDD    LOGICAL FALSE,(TRUE,FALSE)
NONE     LOGICAL FALSE,(TRUE,FALSE)
NEXTTCB  DC    A(0)
TERM     DC    CL8' '
PWORK    DC    D'0'
         END
