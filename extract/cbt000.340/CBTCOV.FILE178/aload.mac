*          DATA SET GU010900   AT LEVEL 002 AS OF 10/24/75
         TITLE 'ALOAD SUBROUTINE SECTION'
*              PGMID.    GU010900 ALIAS ALOAD.
*              AUTHOR.   DON HIGGINS.
*              DATE.     1967.
*              REMARKS.  MOVE 1 BYTE FROM LOW-ORDER END OF FIRST
*                        ARGUMENT WORD TO THE ADDRESS CALCULATED
*                        BY ADDING THE VALUE OF THE SECOND ARGUMENT
*                        TO THE ADDRESS OF THE THIRD ARGUMENT.
*              CALL.
*                        CALL ALOAD(VALUE,OFFSET,ARRAY)
*
*                        VALUE - FULL WORD FROM WHICH LOW ORDER
*                               BYTE WILL BE MOVED.
*
*                        OFFSET- FULL WORD INTEGER OFFSET TO BE
*                                ADDED TO ARRAY ADDRESS. (FIRST
*                                BYTE OF ARRAY IS OFFSET 3)
*
*                        ARRAY - NAME OF ARRAY OR VARIABLE
*              EXAMPLE.
*                        CALL ALOAD(64,0,BLANK)
*
*                        MOVE BLANK TO FIRST BYTE OF BLANK.
ALOAD    CSECT
         USING *,15
         STM   14,4,12(13)
RELOAD   LM    2,4,0(1)
         N     4,=X'00FFFFFF'
         A     4,0(,3)
         MVC   0(1,4),3(2)
         CLI   8(1),X'00'
         BNE   ENDLOAD
         A     1,=F'12'
         B     RELOAD
ENDLOAD  LM    2,4,28(13)
         MVI   12(13),X'FF'
         BCR   15,14
         LTORG
         END
