*          DATA SET GU429000   AT LEVEL 012 AS OF 03/16/79
         TITLE 'GU429000 - EXEC COMMAND PROCESSOR SUBROUTINE'
*
*              PGM-ID. GU429000  FPC.SUBLIB(EXEC)
*              AUTHOR. DON HIGGINS.
*              DATE.   02/28/79.
*              REMARKS.EXEC COMMAND PROCESSOR SUBROUTINE TO
*                      EXECUTE ASSEMBLER INSTRUCTIONS AND
*                      SUPPORT DYNAMIC STORAGE CONTROL.
*              CALLS.
*                      CALL EXEC(OP1,OPR11,OPR12,,,OPR1N,
*                                OP2,OPR21,OPR22,,,OPR2N,
*                                       ,
*                                       ,
*                                OPN,OPRN1,OPRN2,,,OPRNN)
*
*                        OP  - OPERATION CODE
*
*                        OPR - REQUIRED OPERANDS FOR SPECIFIC
*                              OPERATION
*
*
*              NOTES.
*
*                1.  NOTE THAT CALCULATION OF ALL OPERANDS OCCURS
*                    AT CALL TIME, AND THEREFORE MODIFICATION OF
*                    AN OPERAND WILL NOT AFFECT OTHER ARGUMENTS
*                    USING THE SAME OPERAND.  FOR EXAMPLE:
*                      CALL EXEC(LA,ITAB,TAB,0,
*                                MVI,ITAB+240,244)
*                    WILL NOT MOVE A '4' TO TAB+C'0' BECAUSE
*                    THE ARGUMENT ITAB+240 WAS CALCULATED BEFORE
*                    THE LA WAS EXECUTED.  TRICKY TIRCKY..
*
*
*
*
*
*
*
*
*              CURRENT COMMANDS SUPPORTED.
*
*                   OPERATION   OPERANDS
*
*                14  MVCL         ADDR1,LEN1,ADDR2,LEN2,PAD
*
*                15  CLCL         ADDR1,LEN1,ADDR2,LEN2,PAD,CODE
*
*                20  NR           REG1,REG2
*
*                21  CLR          REG1,REG2,CODE
*
*                22  OR           REG1,REG2
*
*                23  XR           REG1,REG2
*
*                25  CR           REG1,REG2,CODE
*
*                26  AR           REG1,REG2
*
*                27  SR           REG1,REG2
*
*                28  MR           REG1,REG2
*
*                29  DR           REG1,REG2
*
*                65  LA           ADDR,AREA,INDEX
*
*                66  STC          REG,ADDR
*
*                67  IC           REG,ADDR
*
*                78  CVD          REG,ADDR
*
*                79  CVB          REG,ADDR
*
*                80  ST           REG,ADDR
*
*                84  N            REG,ADDR
*
*                85  CL           REG,ADDR,CODE
*
*                86  O            REG,ADDR
*
*                87  X            REG,ADDR
*
*                88  L            REG,ADDR
*
*               136  SRL          REG,COUNT
*
*               137  SLL          REG,COUNT
*
*               145  TM           ADDR,MASK,CODE
*
*               146  MVI          ADDR,MASK
*
*               148  NI           ADDR,MASK
*
*               149  CLI          ADDR,MASK,CODE
*
*               150  OI           REG,MASK
*
*               151  XI           REG,MASK
*
*               189  CLM          REG,MASK,ADDR,CODE
*
*               190  STCM         REG,MASK,ADDR
*
*               191  ICM          REG,MASK,ADDR,CODE
*
*               210  MVC          ADDR1,LEN,ADDR2
*
*               212  NC           ADDR1,LEN,ADDR2
*
*               213  CLC          ADDR1,LEN,ADDR2,CODE
*
*               214  OC           ADDR1,LEN,ADDR2
*
*               215  XC           ADDR1,LEN,ADDR2
*
*               220  TR           ADDR1,LEN,ADDR2
*
*               221  TRT          ADDR1,LEN,ADDR2,REG1,REG2,CODE
*
*               222  ED           ADDR1,LEN,ADDR2,CODE
*
*               223  EDMK         ADDR1,LEN,ADDR2,REG,CODE
*
*               242  PACK         ADDR1,LEN1,ADDR2,LEN2
*
*               243  UNPK         ADDR1,LEN1,ADDR2,LEN2
*
*               244  HTB          HEX8,BINARY4 (HEX TO BINARY)
*
*               245  BTH          BINARY4,HEX8 (BINARY TO HEX)
*
*               248  ZAP          ADDR1,LEN1,ADDR2,LEN2
*
*               249  CP           ADDR1,LEN1,ADDR2,LEN2,CODE
*
*               250  AP           ADDR1,LEN1,ADDR2,LEN2
*
*               251  SP           ADDR1,LEN1,ADDR2,LEN2
*
*               252  MP           ADDR1,LEN1,ADDR2,LEN2
*
*               253  DP           ADDR1,LEN1,ADDR2,LEN2
*
*               254  GMAIN        ADDR,LEN
*
*               255  FMAIN        ADDR,LEN
*
*              OPERAND DESCRIPTIONS
*
*                    ADDR   - FULL WORD CONTAINING ADDRESS OF AN AREA
*
*                    INDEX  - FULL BINARY INDEX VALUE ADDED TO ADDR TO
*                             CALCULATE TARGET OPERAND ADDRESS
*
*                    AREA   - ADDRESS OF AREA (LA STORES AREA ADDRESS
*                             PLUS CONTENTS OF INDEX IN ADDR. LA IS
*                             USED TO GET INDIRECT ADDRESS OF STATIC
*                             STORAGE AREA FOR USE IN EXEC COMMANDS.)
*
*                    REG    - FULL WORD USED AS REGISTER IN INSTRUCTION
*
*                    LEN    - FULL WORD BINARY LENGTH OF AREA
*
*                    PAD    - BYTE CONTAINING PAD CHARACTER
*
*                    CODE   - FULL WORD SET TO CONDITION CODE VALUE
*
*                    MASK   - FULL WORD CONTAINING MASK 0-15 VALUE
*                             OR IMMEDIATE DATA
*
*                    COUNT  - FULL WORD BINARY COUNT OF BITS TO SHIFT
*
*                    HEX8   - AREA FOR 8 BYTE EBCDIC HEX CODE
*
*                    BINARY4- AREA FOR 4 BYTE BINARY HEX CODE
*
         TITLE 'MAINLINE'
EXEC     SUBENTRY BASES=(R13,R12)
         LR    R2,R1
         DO    UNTIL,(TM,0(R1),X'80',O)
               L     R3,0(R2)
               L     R1,0(R3)
               LA    R0,255
               IF    (CLR,R0,R1,LT)
                     B   ERROR01
               FI
               DOCASE (R1)
                 CASE ELSE
                   B   ERROR01
                 ESAC
                 CASE  14
                   PM  MVCL14
                 ESAC
                 CASE  15
                   PM  CLCL15
                 ESAC
                 CASE  20
                   PM  NR20
                 ESAC
                 CASE  21
                   PM  CLR21
                 ESAC
                 CASE  22
                   PM  OR22
                 ESAC
                 CASE  23
                   PM  XR23
                 ESAC
                 CASE  25
                   PM  CR25
                 ESAC
                 CASE  26
                   PM  AR26
                 ESAC
                 CASE  27
                   PM  SR27
                 ESAC
                 CASE  28
                   PM  MR28
                 ESAC
                 CASE  29
                   PM  DR29
                 ESAC
                 CASE  65
                   PM  LA65
                 ESAC
                 CASE  66
                   PM  STC66
                 ESAC
                 CASE  67
                   PM  IC67
                 ESAC
                 CASE  78
                   PM  CVD78
                 ESAC
                 CASE  79
                   PM  CVB79
                 ESAC
                 CASE  80
                   PM  ST80
                 ESAC
                 CASE  84
                   PM  N84
                 ESAC
                 CASE  85
                   PM  CL85
                 ESAC
                 CASE  86
                   PM  O86
                 ESAC
                 CASE  87
                   PM  X87
                 ESAC
                 CASE  88
                   PM  L88
                 ESAC
                 CASE  136
                   PM  SRL136
                 ESAC
                 CASE  137
                   PM  SLL137
                 ESAC
                 CASE  145
                   PM  TM145
                 ESAC
                 CASE  146
                   PM  MVI146
                 ESAC
                 CASE  148
                   PM  NI148
                 ESAC
                 CASE  149
                   PM  CLI149
                 ESAC
                 CASE  150
                   PM  OI150
                 ESAC
                 CASE  151
                   PM  XI151
                 ESAC
                 CASE  189
                   PM  CLM189
                 ESAC
                 CASE  190
                   PM  STCM190
                 ESAC
                 CASE  191
                   PM  ICM191
                 ESAC
                 CASE  210
                   PM  MVC210
                 ESAC
                 CASE  212
                   PM  NC212
                 ESAC
                 CASE  213
                   PM  CLC213
                 ESAC
                 CASE  214
                   PM  OC214
                 ESAC
                 CASE  215
                   PM  XC215
                 ESAC
                 CASE  220
                   PM  TR220
                 ESAC
                 CASE  221
                   PM  TRT221
                 ESAC
                 CASE  222
                   PM  ED222
                 ESAC
                 CASE  223
                   PM  EDMK223
                 ESAC
                 CASE  242
                   PM  PACK242
                 ESAC
                 CASE  243
                   PM  UNPK243
                 ESAC
                 CASE 244
                   PM HTB244
                 ESAC
                 CASE 245
                   PM BTH245
                 ESAC
                 CASE 248
                   PM ZAP248
                 ESAC
                 CASE 249
                   PM CP249
                 ESAC
                 CASE 250
                   PM AP250
                 ESAC
                 CASE 251
                   PM SP251
                 ESAC
                 CASE 252
                   PM MP252
                 ESAC
                 CASE 253
                   PM DP253
                 ESAC
                 CASE  254
                   PM  GMAIN254
                 ESAC
                 CASE  255
                   PM  FMAIN255
                 ESAC
               ESACOD
               LA  R2,4(R1)
         OD
         SUBEXIT
         TITLE 'ERROR ROUTINES'
ERROR01  WTP   'GU429000 EXEC - INVALID OPERATION CODE'
         ABEND 1,DUMP
         TITLE 'MVCL 14 ADDR1,LEN1,ADDR2,LEN2,PAD'
MVCL14   PENTRY
         LM   R4,R8,4(R2)
         L    R4,0(R4)
         L    R5,0(R5)
         L    R6,0(R6)
         L    R7,0(R7)
         ICM  R7,8,0(R8)
         MVCL R4,R6
         LA   R1,20(R2)
         PEXIT
         TITLE 'CLCL 15 ADDR1,LEN1,ADDR2,LEN2,PAD,CODE'
CLCL15   PENTRY
         LM   R4,R9,4(R2)
         L    R4,0(R4)
         L    R5,0(R5)
         L    R6,0(R6)
         L    R7,0(R7)
         ICM  R7,8,0(R8)
         CLCL R4,R6
         BALR R1,0
         SLL  R1,2
         SRL  R1,30
         ST   R1,0(R9)
         LA   R1,24(R2)
         PEXIT
         TITLE 'NR 20 REG1,REG2'
NR20     PENTRY
         LM    R3,R4,4(R2)
         NC    0(4,R3),0(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'CLR 21 REG,ADDR,CODE'
CLR21    PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         CLR   R3,R4
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R5)
         LA    R1,12(R2)
         PEXIT
         TITLE 'OR 22 REG1,REG2'
OR22     PENTRY
         LM    R3,R4,4(R2)
         OC    0(4,R3),0(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'XR 23 REG1,REG2'
XR23     PENTRY
         LM    R3,R4,4(R2)
         XC    0(4,R3),0(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'CR 25 REG,ADDR,CODE'
CR25     PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         CR    R3,R4
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R5)
         LA    R1,12(R2)
         PEXIT
         TITLE 'AR 26 REG1,REG2'
AR26     PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         A     R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'SR 27 REG1,REG2'
SR27     PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         S     R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'MR 28 REG1,REG2'
MR28     PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         M     R0,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'DR 29 REG1,REG2'
DR29     PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         ZR    R0
         D     R0,0(R4)
         ST    R1,0(R3)
         ST    R0,0(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'LA 65 ADDR,AREA,INDEX'
LA65     PENTRY
         LM    R3,R5,4(R2)
         L     R5,0(R5)
         LA    R1,0(R4,R5)
         ST    R1,0(R3)
         LA    R1,12(R2)
         PEXIT
         TITLE 'STC 66 REG,ADDR'
STC66    PENTRY
         LM    R3,R4,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         STC   R3,0(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'IC 67 REG,ADDR'
IC67     PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         L     R4,0(R4)
         IC    R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'CVD 78 REG,ADDR'
CVD78    PENTRY
         LM    R3,R4,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         CVD   R3,0(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'CVB 79 REG,ADDR'
CVB79    PENTRY
         LM    R3,R4,4(R2)
         L     R4,0(R4)
         CVB   R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'ST 80 REG,ADDR'
ST80     PENTRY
         LM    R3,R4,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         ST    R3,0(R4)
         ST    R3,0(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'N 84 REG,ADDR'
N84      PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         L     R4,0(R4)
         N     R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'CL 85 REG,ADDR,CODE'
CL85     PENTRY
         LM    R3,R5,4(R2)
         L     R4,0(R4)
         CLC   0(4,R3),0(R4)
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R5)
         LA    R1,12(R2)
         PEXIT
         TITLE 'O 86 REG,ADDR'
O86      PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         L     R4,0(R4)
         O     R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'X 87 REG,ADDR'
X87      PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         L     R4,0(R4)
         X     R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'L 88 REG,ADDR'
L88      PENTRY
         LM    R3,R4,4(R2)
         L     R4,0(R4)
         L     R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'SRL 136 REG,COUNT'
SRL136   PENTRY
         LM    R3,R4,4(R2)
         L     R4,0(R4)
         L     R1,0(R3)
         SRL   R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'SLL 137 REG,COUNT'
SLL137   PENTRY
         LM    R3,R4,4(R2)
         L     R4,0(R4)
         L     R1,0(R3)
         SLL   R1,0(R4)
         ST    R1,0(R3)
         LA    R1,8(R2)
         PEXIT
         TITLE 'TM 145 ADDR,MASK,CODE'
TM145    PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         EX    R4,TMINS
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R5)
         LA    R1,12(R2)
         PEXIT
TMINS    TM    0(R3),*-*
         TITLE 'MVI 146 ADDR,MASK'
MVI146   PENTRY
         LM    R3,R4,4(R2)
         L     R3,0(R3)
         MVC   0(1,R3),3(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'NI 148 ADDR,MASK'
NI148    PENTRY
         LM    R3,R4,4(R2)
         L     R3,0(R3)
         NC    0(1,R3),3(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'CLI 149 ADDR,MASK,CODE'
CLI149   PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         CLC   0(1,R3),3(R4)
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R5)
         LA    R1,12(R2)
         PEXIT
         TITLE 'OI 150 ADDR,MASK'
OI150    PENTRY
         LM    R3,R4,4(R2)
         L     R3,0(R3)
         OC    0(1,R3),3(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'XI 151 ADDR,MASK'
XI151    PENTRY
         LM    R3,R4,4(R2)
         L     R3,0(R3)
         XC    0(1,R3),3(R4)
         LA    R1,8(R2)
         PEXIT
         TITLE 'CLM 189 REG,MASK,ADDR,CODE'
CLM189   PENTRY
         LM    R3,R6,4(R2)
         L     R1,0(R3)
         L     R4,0(R4)
         L     R5,0(R5)
         EX    R4,CLMINS
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R6)
         LA    R1,16(R2)
         PEXIT
CLMINS   CLM   R1,*-*,0(R5)
         TITLE 'STCM 190 REG,MASK,ADDR'
STCM190  PENTRY
         LM    R3,R5,4(R2)
         L     R1,0(R3)
         L     R4,0(R4)
         L     R5,0(R5)
         EX    R4,STCMINS
         LA    R1,12(R2)
         PEXIT
STCMINS  STCM  R1,*-*,0(R5)
         TITLE 'ICM 191 REG,MASK,ADDR,CODE'
ICM191   PENTRY
         LM    R3,R6,4(R2)
         L     R1,0(R3)
         L     R4,0(R4)
         L     R5,0(R5)
         EX    R4,ICMINS
         ST    R1,0(R3)
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R6)
         LA    R1,16(R2)
         PEXIT
ICMINS   ICM   R1,*-*,0(R5)
         TITLE 'MVC 210 ADDR1,LEN,ADDR2'
MVC210   PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,MVCINS
         LA    R1,12(R2)
         PEXIT
MVCINS   MVC   0(*-*,R3),0(R5)
         TITLE 'NC 212 ADDR1,LEN,ADDR2'
NC212    PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,NCINS
         LA    R1,12(R2)
         PEXIT
NCINS    NC    0(*-*,R3),0(R5)
         TITLE 'CLC 213 ADDR1,LEN,ADDR2,CODE'
CLC213   PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,CLCINS
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R6)
         LA    R1,16(R2)
         PEXIT
CLCINS   CLC   0(*-*,R3),0(R5)
         TITLE 'OC 214 ADDR1,LEN,ADDR2'
OC214    PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,OCINS
         LA    R1,12(R2)
         PEXIT
OCINS    OC    0(*-*,R3),0(R5)
         TITLE 'XC 215 ADDR1,LEN,ADDR2'
XC215    PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,XCINS
         LA    R1,12(R2)
         PEXIT
XCINS    XC    0(*-*,R3),0(R5)
         TITLE 'TR 220 ADDR1,LEN,ADDR2'
TR220    PENTRY
         LM    R3,R5,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,TRINS
         LA    R1,12(R2)
         PEXIT
TRINS    TR    0(*-*,R3),0(R5)
         TITLE 'TRT 221 ADDR1,LEN,ADDR2,REG1,REG2,CODE'
TRT221   PENTRY
         LM    R3,R8,4(R2)
         LR    R9,R2
         ZR    R1
         ZR    R2
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,TRTINS
         ST    R1,0(R6)
         ST    R2,0(R7)
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R8)
         LR    R2,R9
         LA    R1,24(R2)
         PEXIT
TRTINS   TRT   0(*-*,R3),0(R5)
         TITLE 'ED 222 ADDR1,LEN,ADDR2,CODE'
ED222    PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,EDINS
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R6)
         LA    R1,16(R2)
         PEXIT
EDINS    ED    0(*-*,R3),0(R5)
         TITLE 'EDMK 223 ADDR1,LEN,ADDR2,REG,CODE'
EDMK223  PENTRY
         LM    R3,R7,4(R2)
         ZR    R1
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         L     R5,0(R5)
         EX    R4,EDMKINS
         ST    R1,0(R6)
         BALR  R1,0
         SLL   R1,2
         SRL   R1,30
         ST    R1,0(R7)
         LA    R1,20(R2)
         PEXIT
EDMKINS  EDMK  0(*-*,R3),0(R5)
         TITLE 'PACK 242 ADDR1,LEN1,ADDR2,LEN2'
PACK242  PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         SLL   R4,4
         L     R5,0(R5)
         L     R6,0(R6)
         BCTR  R6,0
         AR    R4,R6
         EX    R4,PACKINS
         LA    R1,16(R2)
         PEXIT
PACKINS  PACK  0(*-*,R3),0(*-*,R5)
         TITLE 'UNPK 243 ADDR1,LEN1,ADDR2,LEN2'
UNPK243  PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         SLL   R4,4
         L     R5,0(R5)
         L     R6,0(R6)
         BCTR  R6,0
         AR    R4,R6
         EX    R4,UNPKINS
         LA    R1,16(R2)
         PEXIT
UNPKINS  UNPK  0(*-*,R3),0(*-*,R5)
         TITLE 'HTB 244 HEX8,BINARY4'
HTB244   PENTRY
         LM    R3,R4,4(R2)
         MVC   DWORK(8),0(R3)
         TR    DWORK(8),EBCHEX
         PACK  PWORK(5),DWORK(9)
         MVC   0(4,R4),PWORK
         LA    R1,8(R2)
         PEXIT
EBCHEX   DC    256AL1(*-EBCHEX)
         ORG   EBCHEX+C'A'
         DC    X'FAFBFCFDFEFF'
         ORG
DWORK    DC    CL9' '
PWORK    DC    CL5' '
         TITLE 'BTH 245 BINARY4,HEX8'
BTH245   PENTRY
         LM    R3,R4,4(R2)
         MVC   PWORK(4),0(R3)
         UNPK  DWORK(9),PWORK(5)
         TR    DWORK(8),HEXEBC-240
         MVC   0(8,R4),DWORK
         LA    R1,8(R2)
         PEXIT
HEXEBC   DC    C'0123456789ABCDEF'
         TITLE 'ZAP 248 ADDR1,LEN1,ADDR2,LEN2'
ZAP248   PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         SLL   R4,4
         L     R5,0(R5)
         L     R6,0(R6)
         BCTR  R6,0
         AR    R4,R6
         EX    R4,ZAPINS
         LA    R1,16(R2)
         PEXIT
ZAPINS   ZAP   0(*-*,R3),0(*-*,R5)
         TITLE 'CP 249 ADDR1,LEN1,ADDR2,LEN2'
CP249    PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         SLL   R4,4
         L     R5,0(R5)
         L     R6,0(R6)
         BCTR  R6,0
         AR    R4,R6
         EX    R4,CPINS
         LA    R1,16(R2)
         PEXIT
CPINS    CP    0(*-*,R3),0(*-*,R5)
         TITLE 'AP 250 ADDR1,LEN1,ADDR2,LEN2'
AP250    PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         SLL   R4,4
         L     R5,0(R5)
         L     R6,0(R6)
         BCTR  R6,0
         AR    R4,R6
         EX    R4,APINS
         LA    R1,16(R2)
         PEXIT
APINS    AP    0(*-*,R3),0(*-*,R5)
         TITLE 'SP 251 ADDR1,LEN1,ADDR2,LEN2'
SP251    PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         SLL   R4,4
         L     R5,0(R5)
         L     R6,0(R6)
         BCTR  R6,0
         AR    R4,R6
         EX    R4,SPINS
         LA    R1,16(R2)
         PEXIT
SPINS    SP    0(*-*,R3),0(*-*,R5)
         TITLE 'MP 252 ADDR1,LEN1,ADDR2,LEN2'
MP252    PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         SLL   R4,4
         L     R5,0(R5)
         L     R6,0(R6)
         BCTR  R6,0
         AR    R4,R6
         EX    R4,MPINS
         LA    R1,16(R2)
         PEXIT
MPINS    MP    0(*-*,R3),0(*-*,R5)
         TITLE 'DP 253 ADDR1,LEN1,ADDR2,LEN2'
DP253    PENTRY
         LM    R3,R6,4(R2)
         L     R3,0(R3)
         L     R4,0(R4)
         BCTR  R4,0
         SLL   R4,4
         L     R5,0(R5)
         L     R6,0(R6)
         BCTR  R6,0
         AR    R4,R6
         EX    R4,DPINS
         LA    R1,16(R2)
         PEXIT
DPINS    DP    0(*-*,R3),0(*-*,R5)
         TITLE 'GMAIN 254 ADDR,LEN - GETMAIN AREA OR SET AREA=0'
GMAIN254 PENTRY
         LM    R3,R4,4(R2)
         L     R5,0(R4)
         GETMAIN RC,LV=(R5)
         IF    (LTR,R15,R15,NZ)
               ZR   R0
               ST   R0,0(R3)
         ELSE
               ST    R1,0(R3)
         FI
         LA    R1,8(R2)
         PEXIT
         TITLE 'FMAIN 255 ADDR,LEN - FREEMAIN AREA'
FMAIN255 PENTRY
         LM    R3,R4,4(R2)
         L     R1,0(R3)
         L     R0,0(R4)
         FREEMAIN R,LV=(0),A=(1)
         LA    R1,8(R2)
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
         END
