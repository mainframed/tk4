*          DATA SET GU013600   AT LEVEL 003 AS OF 11/09/72
PACKBITS CSECT
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/02/72.
*              REMARKS.  PACK BYTE SWITCHES INTO BIT SWITCHES.
*
*                        CALL PACKBITS (BITS,NUMBITS,BYTES)
*
*                         BITS - BIT ARRAY WITH 8 BITS PER BYTE
*
*                         NUMBITS - FULL WORD COMPUTATIONAL NUMBER OF
*                                   BITS  TO PACK.  IT MUST BE A
*                                   MULTIPLE OF 8.
*
*                         BYTE - BYTE ARRAY CONTAINING VALUES OF
*                                0 OR 1.
*
*              REGISTERS
*                R1 - A(BIT ARRAY)
*                R2 - F'1'
*                R3 - A(BYTE ARRAY)
*                R4 - F'8'
*                R5 - A(BYTE ARRAY+NUMBITS-1
*
*
         USING *,R15
         SAVE  (1,5)
         LM    R1,R3,0(R1)
         L     R2,0(R2)            R2=NUMBITS
         LA    R5,0(R2,R3)         R5=A(BYTE ARRAY+NUMBITS)
         BCTR  R5,R0
         LA    R2,1                R2=F'1'
         LA    R3,0(R3)            R3=A(BYTE ARRAY WITHOUT VL BIT)
         LA    R4,8                R4=F'8'
LOOP     TAG
         MVI   0(R1),X'00'         SET BITS TO ZERO
         CLI   0(R3),C'1'          IS IT A ONE
         BNE   *+8                 NO, SKIP
         OI    0(R1),X'80'         YES, SET BIT TO ONE
         CLI   1(R3),C'1'
         BNE   *+8
         OI    0(R1),X'40'
         CLI   2(R3),C'1'
         BNE   *+8
         OI    0(R1),X'20'
         CLI   3(R3),C'1'
         BNE   *+8
         OI    0(R1),X'10'
         CLI   4(R3),C'1'
         BNE   *+8
         OI    0(R1),X'08'
         CLI   5(R3),C'1'
         BNE   *+8
         OI    0(R1),X'04'
         CLI   6(R3),C'1'
         BNE   *+8
         OI    0(R1),X'02'
         CLI   7(R3),C'1'
         BNE   *+8
         OI    0(R1),X'01'
         AR    R1,R2
         BXLE  R3,R4,LOOP
         RETURN (1,5)
         EQUREGS
         END
