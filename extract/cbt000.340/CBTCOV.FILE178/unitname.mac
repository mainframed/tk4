*          DATA SET GU024100   AT LEVEL 009 AS OF 11/09/73
         MACRO
&N       PR    &REC
         LA    R2,&REC
         PERFORM R050PUT,R060EXIT
         MEND
         MACRO
&N       PM    &MSG
&N       LA    R2,=CL80&MSG
         PERFORM R050PUT,R060EXIT
         MEND
UNITNAME SUBENTRY BASES=(R13,R12)
*              AUTHOR.   DON HIGGINS.
*              DATE.     10/08/73.
*              REMARKS.  GENERATE SOURCE PROGRAM WHICH CAN BE USED
*                        TO MODIFY AND RECREATE DEVNAMET AND DEVMASKT
*                        CSECTS USING UNITNAME MACROS.
*                        INPUT
*                          1. PROGRAM ISSUES LOAD FOR DEVNAMET AND
*                                                     DEVMASKT.
*                          2. PROGRAM USES HOST SYSTEM UCB LOOKUP
*                             TABLE AND UCBS.
*                          3. PROGRAM WRITES OPTIONAL PARM FIELD
*                             DATA AS FIRST OUTPUT RECORD.
*                        OUTPUT
*                          1. SOURCE DECK TO CREATE CSECTS ON SYSPUNCH.
*                          2. SOURCE LIST ON SYSPRINT.
*              REGISTERS.
*                   R2 - OUTPUT RECORD ADDRESS
*                   R3 - CVT,DEVNAMET
*                   R4 - DEVMASKT
*                   R5 - NAME COUNT
*                   R6 - BIT COUNT
*                   R7 - BIT ADDR
*                   R8 - UCB ADDR ADDR
*                   R9 - BIT WORK
*                   R10- BIT WORK COUNT
*                   R11- UCB ADDR
*                   R12- BASE
*                   R13- BASE
         L     R3,0(R1)
         LH    R2,0(R3)
         OPEN  (SYSPUNCH,(OUTPUT),SYSPRINT,(OUTPUT))
         SRC   R2
         BZ    M010
         SI    R2,1
         STC   R2,*+5
         MVC   CTLREC(0),2(R3)
         PR    CTLREC
M010     TAG
         PERFORM R100UCBS,R190EXIT      UCBMAP  MACRO  DEFINITION
         PERFORM R200UNIT,R290EXIT      UNITNAME MACRO DEFINITION
         PERFORM R300GEN,R390EXIT
         PERFORM R400MACS,R590EXIT      MACRO STATEMENTS
         CLOSE (SYSPUNCH,,SYSPRINT)
         SUBEXIT
R050PUT  TAG
         PUT   SYSPRINT,(R2)
         PUT   SYSPUNCH,(R2)
R060EXIT PEXIT
R100UCBS PENTRY
         PM    '* DEVNAMET AND DEVMASKT GENERATOR'    1 COMMENT
         PM    '          MACRO'                      2 MACRO
         PM    '          UCBMAP'                     3 MODEL
         L     R3,CVTADDR
         L     R1,CVTILK2(R3)
R110     TAG
         CLC   0(2,R1),=X'FFFF'
         BE    R120
         AAI   R1,2
         B     R110
R120     TAG
         S     R1,CVTILK2(R3)
         SRL   R1,1                R1=NUM UCBS
         CVD   R1,PWORK
         AAI   R1,31               NBITS+31
         SRL   R1,5                (NBITS+31)/32
         SLL   R1,2                ((NBITS+31)/32)*4
         ST    R1,NBYTES
         EDIT  TO=MAPSIZE,FROM=PWORK+6,MASK='0999'
         PR    GBLCREC                                4 GBLC &UCB(XXX)
         PR    GBLAREC                                5 GBLA &NUCBS
         MVC   NUCBS,MAPSIZE
         PR    NUCBREC                                6 &NUCBS SETA XXX
         L     R3,CVTILK2(R3)
R130     TAG
         CLC   0(2,R3),=X'FFFF'
         BE    R150
         AP    UCBSUB,=P'1'
         CLC   0(2,R3),=X'0000'
         BE    R140
         EDIT  TO=DSUB,FROM=UCBSUB,MASK='0999'
         LH    R1,0(R3)
         MVC   DUCB,13(R1)
         PR    UCBREC                               7-N &UCB(N) SETC
R140     TAG
         AAI   R3,2
         B     R130
R150     TAG
         PM    'DEVNAMET CSECT'
         PM    '         DC    A(NAMES)'
         PM    'DEVMASKT CSECT'
         PM    '         DC    AL2(NAMES,NAMES+1)'
         PM    '         MEND'
R190EXIT PEXIT
R200UNIT PENTRY
         PM    '          MACRO'                      1 MACRO
         PM    '&&NMAC    UNITNAME &&NAME=,&&UNIT=,&&CODE=' 2 MODEL
         PR    GBLCREC                            '   3 GBLC
         PR    GBLAREC                                4 GBLA
         PM    '          LCLA     &&S1,&&S2,&&S3,&&N,&&NB' 5 LCLA
         PM    '          LCLB     &&B(32)        '   6 LCLB
         PM    '&&N       SETA     N''&&UNIT'         7 SETA
         PM    '          AIF   (''&&NAME''(2,8) EQ ''LASTMASK'').DEV'
         PM    '&&NAMES   SETA     &&NAMES+1'
         PM    'DEVNAMET  CSECT'                      8 CSECT
         PM    '&&NMAC    DC       CL8&&NAME'         9 NAME
         PM    '          DC       XL4''&&CODE'''    10 CODE
         PM    '.DEV      ANOP'
         PM    'DEVMASKT  CSECT'                     11 CSECT
         PM    '          DC       A(&&N)'           12 BIT COUNT
         PM    '          DC       XL4''&&CODE'''    13 CODE
         PM    '.L1       ANOP'
         PM    '&&S1      SETA     &&S1+1'
         PM    '          AIF      (&&S1 GT &&N).FINISH'
         PM    '.L2       ANOP'
         PM    '&&S2      SETA     &&S2+1'
         PM    '          AIF      (&&S2 GT &&NUCBS).ERR'
         PM    '.L3       ANOP'
         PM    '&&S3      SETA     &&S3+1'
         PM    '          AIF      (&&S3 GT 32).GEN'
         PM    '.COMP     ANOP'
         PM    '  AIF (''&&UCB(&&S2)'' NE ''&&UNIT(&&S1)'').L2'
         PM    '&&B(&&S3) SETB     1'
         PM    '          AGO      .L1'
         PM    '.ERR      MNOTE   8,''INVALID UCB OR SEQ. ERR.'''
         PM    '          MEXIT'
         PM    '.GEN      ANOP'
         PM    '*'
         PM    ' DC BL1''&&B(01)&&B(02)&&B(03)&&B(04)&&B(05)&&B(06)&&B(X
               07)&&B(08)'''
         PM    ' DC BL1''&&B(09)&&B(10)&&B(11)&&B(12)&&B(13)&&B(14)&&B(X
               15)&&B(16)'''
         PM    ' DC BL1''&&B(17)&&B(18)&&B(19)&&B(20)&&B(21)&&B(22)&&B(X
               23)&&B(24)'''
         PM    ' DC BL1''&&B(25)&&B(26)&&B(27)&&B(28)&&B(29)&&B(30)&&B(X
               31)&&B(32)'''
         PM    '&&NB     SETA  &&NB+32'
         PM    '&&S3      SETA     32'
         PM    '.L4       ANOP'
         PM    '&&B(&&S3) SETB     0'
         PM    '&&S3      SETA     &&S3-1'
         PM    '          AIF      (&&S3 GT 0).L4'
         PM    '&&S3     SETA 1'
         PM    '          AIF      (&&S1 LE &&N).COMP'
         PM    '.FINISH  ANOP'
         PM    '         AIF   (&&NB LT &&NUCBS).GEN'
         PM    '.EXIT     ANOP'
         PM    '          MEND'
R290EXIT PEXIT
R300GEN  PENTRY
         PM    '          MACRO'
         PM    '          GENEQU'
         PR    GBLCREC
         PR    GBLAREC
         PM    'NAMES     EQU      &&NAMES'
         PM    '          MEND'
R390EXIT PEXIT
R400MACS PENTRY
         PM    '          UCBMAP'
         LOAD  EP=DEVNAMET
         LR    R3,R0
         LOAD  EP=DEVMASKT
         LR    R4,R0
         LH    R5,2(R4)            NUMBER OF MASKS
         AAI   R3,4
         AAI   R4,4
R410     TAG
         MVC   MNAME,0(R3)
         CLI   MNAME,C'0'
         BL    R112
         MVI   MNAME,C'D'
         MVC   MNAME+1(7),0(R3)
         MVI   MNAME+5,C'M'
R112     TAG
         MVC   UNAME,0(R3)         NAME
         CH    R5,=H'1'
         BNE   R415
         MVC   MNAME,=CL8'LASTMASK'
         MVC   UNAME,=CL8'LASTMASK'
R415     TAG
         UNPK  DWORK(9),4(5,R4)
         TR    DWORK(8),TRTAB-240
         MVC   UCODE,DWORK         CODE
         L     R6,0(R4)            BIT COUNT
         LA    R7,8(R4)
         L     R8,CVTADDR
         L     R8,CVTILK2(R8)
         MVI   USTAT,NONE
R420     TAG
         IC    R9,0(R7)            LOAD BYTE OF MAP
         SLL   R9,24
         LI    R10,8
R430     TAG
         SRC   R9                  IS BIT ON
         BM    R450                YES, GO PROCESS
R440     TAG
         AAI   R8,2                NEXT UCB
         SLL   R9,1                NEXT BIT
         BCT   R10,R430            LOOP FOR 8 BITS
         AAI   R7,1                NEXT BYTE
         B     R420
R450     TAG
         LH    R11,0(R8)           LOAD UCB ADDR
         CLI   USTAT,NONE          IS IT FIRST
         BNE   R470                NO, SKIP
         MVC   UNIT1,13(R11)
         MVI   USTAT,ONE
         BCT   R6,R440             IF NOT LAST, RETURN
         PR    UMACL1
R460     AAI   R3,12
         AAI   R4,8           SKIP BIT COUNT AND CODE
         A     R4,NBYTES      SKIP BIT MAP
         BCT   R5,R410        IF NOT LAST NAME, RETURN
         B     R510LAST       EXIT
R470     TAG
         CLI   USTAT,ONE      IS IT SECOND
         BNE   R490           NO, SKIP
         MVI   UNIT1+3,C','
         MVI   UCONT1,C'X'
         PR    UMACL1
         MVI   UNIT1+3,C')'
         MVI   UCONT1,C' '
R480     TAG
         MVC   UADDR,=A(UMACL2+15)
         MVC   UMACL2+15(3),13(R11)
         MVI   USTAT,MORE
R485     BCT   R6,R440        IF NOT LAST, RETURN
         L     R1,UADDR
         MVI   3(R1),C')'
         PR    UMACL2
         MVC   UMACL2+1(79),UMACL2
         B     R460
R490     TAG
         L     R1,UADDR
         CL    R1,=A(UMACL2+61)
         BNH   R500
         MVI   3(R1),C','
         MVI   UMACL2+71,C'X'
         PR    UMACL2
         MVC   UMACL2+1(79),UMACL2
         B     R480
R500     TAG
         MVI   3(R1),C','
         AAI   R1,4
         ST    R1,UADDR
         MVC   0(3,R1),13(R11)
         B     R485
R510LAST TAG
         PM    '          GENEQU'
         PM    '          END'
R590EXIT PEXIT
SYSPUNCH DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPUNCH,RECFM=FB,LRECL=80
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FB,LRECL=80
         EQUAL
CVTADDR  EQU   X'4C'
CVTILK2  EQU   X'28'
UCBSUB   DC    PL2'0'
CTLREC   DC    CL80' '
UCBREC   DS    0CL80
         DC    C'&&UCB('
DSUB     DC    CL4' ',C') SETC '''
DUCB     DC    CL3' ',C''''
         FILL  UCBREC
GBLCREC  DS    0CL80
         DC    CL10' ',C'GBLC  &&UCB('
MAPSIZE  DC    CL4' ',C')'
         FILL  GBLCREC
NUCBREC  DS    0CL80
         DC    C'&&NUCBS  SETA '
NUCBS    DC    CL4' '
         FILL  NUCBREC
GBLAREC  DC    CL80'          GBLA  &&NUCBS,&&NAMES'
UMACL1   DS    0CL71
MNAME    DC    CL8' ',C'  UNITNAME NAME='''
UNAME    DC    CL8' ',C''',CODE='
UCODE    DC    CL8' ',C',UNIT=('
UNIT1    DC    CL3' ',C')'
         FILL  UMACL1
UCONT1   DC    C' ',CL8' '
PWORK    DC    D'0'
DWORK    DC    CL9' '
TRTAB    DC    C'0123456789ABCDEF'
NBYTES   DC    F'0'                LENGTH OF BIT MAP ROUNDED UP TO WORD
USTAT    DC    AL1(NONE)
NONE     EQU   0
ONE      EQU   1
MORE     EQU   2
UMACL2   DC    CL80' '
UADDR    DC    F'0'
         END   UNITNAME
