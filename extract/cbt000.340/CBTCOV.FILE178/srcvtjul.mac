*          DATA SET GU008800   AT LEVEL 001 AS OF 09/21/72
*      IDENTIFICATION DIVISION.
*      PROGRAM-ID.  'CONVERT JULIAN DATE'
*      AUTHOR.  'HENRY JOHNSON'
*      INSTALLATION.  FLORIDA POWER CORPORATION
*      DATE WRITTEN 09-17-68
*      REMARKS.
*          PURPOSE: THIS ROUTINE CONVERTS THE JULIAN DATE PASSED
*                   FROM THE CALLING PROGRAM TO GREGORIAN.
*                   2 PARAMETERS MUST BE SUPPLIED BY THE CALLING
*                   PROGRAM.
*                        PARM 1 - LOCATION OF THE 3 BYTE JULIAN DATE
*                        PARM 2 - LOCATION OF THE 8 BYTE AREA IN THE
*                           CALLING PROGRAM TO STORE GREGORIAN DATE.
*                          (09/17/68)
*                   EXAMPE OF CALL:
*                     ENTER LINKAGE.
*                     CALL 'SRCVTJUL' USING PARM1, PARM2.
*                     ENTER COBOL.
*
*          MODIFIED       DATE           BY WHOM
*                        10/19/70      DON HIGGINS
*
*
SRCVTJUL SUBENTRY
         L     6,0(1)                   LOAD THE FIRST PARM
         L     3,4(1)                   LOAD THE SECOND PARM
         MVC   WORK+1(3),0(6)           MOVE JULIAN TO WORK
         L     1,WORK                   LOAD JULIAN DATE
         TM    WORK+1,X'01'             TEST YEAR FOR ODD
         BO    PHSIN2X                  YES, NOT LEAP YEAR
         TM    WORK+1,X'12'             NO,TEST BIT 3 AND 6
         BM    PHSIN2X                  IF MIXED NOT LEAP YEAR
         MVI   FEB,X'1D'                SET NO OF DAYS IN FEB=29
PHSIN2X  XC    DWORD(8),DWORD           CLEAR WORK AREA
         STH   1,DWORD+6                CONVERT DAY OF YEAR
         CVB   0,DWORD                  TO BINARY
         LA    R11,1                     MONTH COUNTER = 1
         LA    12,CALNDR                CALENDAR POINTER TO JANUARY
         SR    14,14                    CLEAR
PHSIN3X  IC    14,0(12)                 SUBT NO OF DAYS IN A MONTH
         SR    0,14                         FROM DAY OF YEAR
         BC    12,PHSIN4X               DONE IF RESULT 0 OR -
         LA    R11,1(R11)               MONTH COUNTER= MONTH CNTR +1
         LA    12,1(12)                 CALENDAR POINTER = NEXT MONTH
         B     PHSIN3X
PHSIN4X  AR    0,14                     ADD LAST MONTH BACK
         BAL   10,CNVRTX                CONVERT MONTH
         MVC   DATE(2),DWORD+2          AND MOVE IT IN DATE AREA
         LR    R11,R0                   CONVERT
         BAL   10,CNVRTX                DAY
         MVC   DATE+3(2),SEQ+2          AND MOVE IT IN DAY AREA
         MVO   WORK+2(2),WORK(2)        PUT SIGN ON YEAR
         EX    0,PHSIN2X                CLEAR WORK AREA
         MVC   DWORD+6(2),WORK+2        MOVE YEAR WITH SIGN TO WORK
         CVB   R11,DWORD                     AREA  AND
         BAL   10,CNVRTX                     CONVERT
         MVC   DATE+6(2),SEQ+2          MOVE YEAR INTO DATE AREA
         CLI   DATE,X'40'
         BNE   NEXT
         MVI   DATE,X'F0'
NEXT     CLI   DATE+3,X'40'
         BNE   RET
         MVI   DATE+3,X'F0'
RET      MVC   0(8,3),DATE              MOVE CONVERTED DATE TO CALLING
         MVI   FEB,X'1C'      RESET FEB AFTER LEAP YEAR
         SUBEXIT
CNVRTX   CVD   R11,DWORD                 CONVERT BINARY WORD
         UNPK  DWORD(4),DWORD+5(3)        IN REG 13
         OI    DWORD+3,X'F0'            UNPACKED FORM IN WORD SEQ
         MVC   SEQ,DWORD
         LA    14,DWORD
SUPPRESS TM    0(14),X'0F'              IS THIS A ZERO
         BCR   4,10                     NO, RETURN
         MVI   0(14),X'40'              YES, REPLACE WITH BLANK
         LA    14,1(14)
         B     SUPPRESS                 LOOP
CALNDR   DC    X'1F'                    NUMBER OF DAYS IN JAN
FEB      DC    X'1C'                    NUMBER OF DAYS IN FEB
         DC    X'1F'                    NUMBER OF DAYS IN MAR
         DC    X'1E'                    NUMBER OF DAYS IN APR
         DC    X'1F'                    NUMBER OF DAYS IN MAY
         DC    X'1E'                    NUMBER OF DAYS IN JUN
         DC    X'1F'                    NUMBER OF DAYS IN JUL
         DC    X'1F'                    NUMBER OF DAYS IN AUG
         DC    X'1E'                    NUMBER OF DAYS IN SEP
         DC    X'1F'                    NUMBER OF DAYS IN OCT
         DC    X'1E'                    NUMBER OF DAYS IN NOV
         DC    X'1F'                    NUMBER OF DAYS IN DEC
WORK     DC    F'0'
DATE     DC    C'00/00/00'              DATE BASE
SEQ      DC    F'0'                     NUMBER OF LEADING ZEROS
DWORD    DC    D'0'                     NUMBER WITH ZERO SUPPRESSED
         EQUREGS
         END
