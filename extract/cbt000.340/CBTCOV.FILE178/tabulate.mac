C          DATA SET GU024500   AT LEVEL 003 AS OF 10/28/73
C     PGM.     GU024500.
C     AUTHOR.  DON HIGGINS.
C     DATE.    10/26/73.
C     REMARKS. GENERALIZED TABULATOR WITH 2 LEVELS AND 12 COLUMNS.
C     FILES.
C         FT05F001 - INPUT TRANSACTIONS.
C         FT06F001 - OUTPUT REPORT.
C     INPUT.
C         COL. DESC.
C          1    OPERATION CODE
C                1 - LEVEL 1 DETAIL LINE
C                2 - LEVEL 1 TOTAL LINE
C                3 - LEVEL 2 DETAIL LINE
C                4 - LEVEL 2 TOTAL LINE
C                8 - FORCE NEW PAGE
C                9 - RESET ACCUMULATORS AND LOAD NEW TITLE AND COL DESC
C         2-20  DESCRIPTION OF DETAIL OR TITLE LINE (OP=1,2,3,4)
C         21-80 12 - 5 DIGIT DETAIL LINE ENTRIES (OP=1,3)
C         1-80  PAGE TITLE CARD MUST FOLLOW EACH OP=9
C         1-80  COLUMN TITLE CARD MUST FOLLOW ABOVE FOR OP=9
C                2-20 - DETAIL LINE DESC COLUMN HEADING
C                21-80 - 12 - 5 CHARACTER COLUMN TITLES.
      IMPLICIT INTEGER (A-Z)
      DIMENSION D1(12),T1(12),T(12)
      DIMENSION TPAGE(20),TCOL(29),DESC(5)
      DATA NRDR/5/,NPRT/6/
      DATA NPAGE/0/,NLINE/0/,MLINE/60/
      DATA T1/12*0/,T/12*0/,NCOL/12/
      DATA TPAGE/20*' '/,TCOL/29*' '/,DESC/5*' '/
C
C     PROCESS LOOP
C
   10 READ(NRDR,11,END=30)OP,DESC,D1
   11 FORMAT(I1,4A4,A3,12I5)
      GO TO (100,200,300,400,20,20,20,800,900),OP
   20 WRITE(NPRT,21)OP,DESC,D1
   21 FORMAT(' INVALID TRANSACTION ',I1,4A4,A3,12I5)
      GO TO 10
C
C     END OF JOB
C
   30 CALL EXIT
C
C     PAGE CONTROL AFTER EACH TRANSACTION
C
   50 CONTINUE
      IF(NLINE.GT.0)GO TO 10
      NLINE=MLINE
      NPAGE=NPAGE+1
   70 WRITE(NPRT,71)TPAGE,NPAGE,TCOL
   71 FORMAT('1',25X,20A4,5X,'PAGE',I5
     * //1X,4A4,A3,5X,12(2X,A4,A1),'  TOTAL1  TOTAL2'/)
      GO TO 10
C
C     DETAIL1
C
  100 CONTINUE
      TD1=0
      DO 110 I=1,NCOL
      TD1=TD1+D1(I)
      T1(I)=T1(I)+D1(I)
      T(I)=T(I)+D1(I)
  110 CONTINUE
      TT=TT+TD1
  120 WRITE(NPRT,121)DESC,D1,TD1
  121 FORMAT(' ',5X,4A4,A3,12I7,I8)
      NLINE=NLINE-1
      GO TO 50
C
C     TOTAL1
C
  200 CONTINUE
      TT1=0
      DO 210 I=1,NCOL
      TT1=TT1+T1(I)
  210 CONTINUE
  220 WRITE(NPRT,221)DESC,T1,TT1
  221 FORMAT(1X,4A4,A3,5X,12I7,8X,I8)
      DO 230 I=1,NCOL
      T1(I)=0
  230 CONTINUE
      NLINE=NLINE-1
      GO TO 50
C
C     DETAIL2
C
  300 CONTINUE
      TD1=0
      DO 320 I=1,NCOL
      TD1=TD1+D1(I)
      T(I)=T(I)+D1(I)
  320 CONTINUE
      TT=TT+TD1
  330 WRITE(NPRT,331)DESC,D1,TD1
  331 FORMAT(' ',4A4,A3,5X,12I7,8X,I8)
      NLINE=NLINE-1
      GO TO 50
C
C     TOTAL2
C
  400 CONTINUE
  410 WRITE(NPRT,411)DESC,T,TT
  411 FORMAT(117X,8('-')
     * /1X,4A4,A3,5X,12I7,8X,I8)
      XT=0
      DO 420 I=1,NCOL
      XT=XT+T(I)
      T1(I)=0
      T(I)=0
  420 CONTINUE
      IF(XT.EQ.TT)GO TO 440
  430 WRITE(NPRT,431)XT,TT
  431 FORMAT('0PROGRAM ERROR - CROSS TOTALS DO NOT EQUAL',2I8/)
  440 CONTINUE
      TT=0
      NLINE=NLINE-2
      GO TO 50
C
C     NEW PAGE
C
  800 CONTINUE
      NLINE=0
      GO TO 50
C
C     LOAD TITLE, COLUMN HEADINGS, AND RESET TOTALS
C
  900 CONTINUE
  910 READ(NRDR,911)TPAGE,TCOL
  911 FORMAT(20A4/1X,4A4,A3,12(A4,A1))
      DO 920 I=1,NCOL
      T1(I)=0
      T(I)=0
  920 CONTINUE
      TT=0
      NLINE=0
      NPAGE=0
      GO TO 50
      END
