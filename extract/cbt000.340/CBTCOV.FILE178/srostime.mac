*          DATA SET GU009300   AT LEVEL 002 AS OF 12/28/72
*      IDENTIFICATION DIVISION.
*      PROGRAM-ID.  'TIME-DATE SUB-ROUTINE'
*      AUTHOR.  'HENRY JOHNSON'
*      INSTALLATION.  FLORIDA POWER CORPORATION
*      DATE WRITTEN 10-11-67
*      REMARKS.
*          PURPOSE: THIS ROUTINE CAN BE CALLED WHEN THE
*                   TIME AND DATE ARE NEEDED. THE FORMAT
*                   OF THIS INFORMATION IS AS FOLLOWS:
*                       ELEVEN BYTES ZONED DECIMAL
*                             YYDDDHHMMSSTH
*                         YY-YEAR
*                         DDD-DAY OF YEAR
*                         HH-HOUR
*                         MM-MINUTES
*                         SS-SECONDS
*                         TH-TENTHS & HUNDREDS OF A SECOND
*                   COBOLAID HAS THE NECESSARY LEVELS FOR
*                   THE WORKING STORAGE SECTION.
*                     SEQ-TIME COPY 'CATIMEDT'
*                   01  SEQ-TIME                      PICTURE 9(13).
*                   01  DATETIME REDEFINES SEQ-TIME.
*                       02  YEAR                      PICTURE 99.
*                       02  DAY                       PICTURE 999.
*                       02  HOUR                      PICTURE 99.
*                       02  MINUTES                   PICTURE 99.
*                       02  SECONDS                   PICTURE 99.
*                       02  TEN-HUN                   PICTURE 99.
*                   EXAMPLE OF CALL NECESSARY TO GET TIME AND DATE.
*                   ENTER LINKAGE.
*                   CALL 'SROSTIME' USING SEQ-TIME.
*                   ENTER COBOL.
*          MODIFIED:    DATE             BY WHOM
* REV1 DON HIGGINS 12/27/72 - SUBENTRY-SUBEXIT TO FIX RETURN CODE
SROSTIME SUBENTRY
         L     3,0(1)                   LOAD ADDRESS OF PARAMETER
         TIME  DEC                      TIME MACRO TIME IN 0 DATE IN 1
         ST    0,TIME                   STORE TIME -HHMMSSTH-
         ST    1,DATE                   STORE DATE -00YYDDDC-
         MVC   TIMOUT(4),TIME
         MVI   TIMOUT+4,X'0C'
         UNPK  WORK1,TIMOUT(5)
         MVC   WORK+5(8),WORK1
         UNPK  WORK1,DATE(4)            UNPACK DATE
         MVC   WORK(5),WORK1+4
         MVC   0(13,3),WORK             STORE TIME & DATE IN CALLING PG
         SUBEXIT
         DS    0F
TIME     DS    CL4
DATE     DS    CL4
TIMOUT   DS    CL5
WORK     DS    CL13
WORK1    DS    CL9
         LTORG
         END
