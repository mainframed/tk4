*          DATA SET GU000300   AT LEVEL 001 AS OF 09/08/76
         TITLE 'TAPESTRY OS TAPE UTILITY PROGRAM V1 09/01/71'
         PRINT NOGEN
         MACRO
&NAME    TMCIO &BYTE,&MASK,&TEXT
&NAME    TM    &BYTE,&MASK
         BNO   TM&SYSNDX
         CIO   &TEXT
TM&SYSNDX DS   0H
         MEND
         MACRO
&NAME    CIO   &TEXT,&AREA,&LAREA
         LCLA  &LTEXT
&LTEXT   SETA  K'&TEXT-3
         AIF   (T'&AREA EQ 'O').WTO
.WTOR    ANOP
&NAME    LA    R0,=A(&LTEXT,&AREA,&LAREA)
         LA    R1,=C&TEXT
         BAL   R14,CIO
         MEXIT
.WTO     ANOP
&NAME    LA    R0,=A(&LTEXT,0)
         LA    R1,=C&TEXT
         BAL   R14,CIO
         MEND
         MACRO
&NAME    PIO   &AREA
&NAME    LA    R1,&AREA
         BAL   R14,PIO
         MEND
         TITLE 'PROGRAM DESCRIPTION'
GU000300 SUBENTRY BASES=(R13,R9,R10,R11)
*              AUTHOR.   DON HIGGINS.
*              DATE.     08/10/71.
*              REVISIONS:
*                 # 1 BY V. P. DIXON ON 09-08-76.
*                   TURNED ON SILI BIT IN SENSECCW DUE TO 3420
*                   RETURNING SENSE INFORMATION LONGER THAN
*                   6 BYTES EXPECTED.
*              REMARKS.  TAPE UTILITY FOR USE UNDER OS.
*
*              BF - BACKSPACE FILE
*              BS - BACK SPACE RECORD
*              BY - TERMINATE
*              CI - CLOSE INPUT TAPE FILE
*              CO - CLOSE OUTPUT SEQUENTIAL FILE
*              DE - DIAGNOSE ERROR
*              DF - DEFINE FILE
*              FS - FIELD SELECT FOR DUMP AND LIST
*              HI - LIST FUNCTIONS
*              LE - EXIT TO NEXT COMMAND RECORD IF LAST READ HIT TM
*              LL - LOOP BACK TO BEGINNING OF COMMAND RECORD
*              OI - OPEN INPUT TAPE FILE
*              OO - OPEN OUTPUT SEQUENTIAL FILE
*              RT - READ TEST
*              RW - REWIND TAPE
*              SD - SET DENSITY TO 1600 PE OR 0800 NRZI
*              SF - SKIP FILE
*              SK - SELECT KEY CONTROL
*              SR - SKIP RECORD
*              TC - TAPE COPY
*              TD - TAPE TO PRINTER DUMP IN EBCDIC AND HEX
*              TL - TAPE TO PRINTER LIST IN EBCDIC ONLY
         TITLE 'ONE TIME HOUSEKEEPING AND DD LIST'
         L     R2,0(R1)            R2=A(PARM LENGTH)
         OPEN  (PRINT,(OUTPUT))
         TIME  DEC                 ON FIRST ENTRY ONLY
         STM   R0,R1,PTIME
         EDIT  TO=TIMEDATE,FROM=PTIME,MASK=' 99/99/99.99F ZZ99.999'
         LH    R1,0(R2)            R1=PARM LENGTH
         LTR   R1,R1
         BNP   FIRSTMSG
         MVI   CSTATUS,REMOTE      SET REMOTE CONTROL STATUS
         CH    R1,=H'8'
         BH    PARMERR
         BCTR  R1,R0
         EX    R1,MVCPARM          MOVE DDNAME TO REMOTE DCB
         OPEN  (INPUTDCB,(INPUT))
         B     FIRSTMSG
PARMERR  TAG
         CIO   'REMOTE CONTROL DDNAME IS TOO LONG'
         B     BY
MVCPARM  MVC   INPUTDCB+40(0),2(R2)
FIRSTMSG TAG
         CIO   'TAPESTRY V1M4 06/13/75 FOR HELP ENTER HI'
*
*              LIST DDNAMES, UNIT, AND VOLUMES
*
         L     R1,X'4C'            R1=A(CVT)
         L     R1,0(R1)               A(TCB WORDS)
         L     R1,4(R1)               A(CURRENT TCB)
         L     R1,12(R1)              A(TIOT)
         LA    R4,24(R1)                A(TIOT+24)
         ST    R4,DDADDR
         SR    R2,R2
         CIO   'DDNAMES  UNIT  VOL'
DDLIST   TAG
         IC    R2,0(R4)
         LTR   R2,R2
         BZ    DDEXIT
         MVC   DDLINE(8),4(R4)          DDNAME
         L     R3,16(R4)                R3=A(UCB)
         MVC   DDLINE+9(3),13(R3)       UNIT
         TM    18(R3),X'A0'
         BZ    DDCIO
         MVC   DDLINE+13(6),28(R3)      VOLUME FOR DISK OR TAPE
DDCIO    TAG
         TR    DDLINE,VALIDEBC
         LA    R0,=A(L'DDLINE-1,0)
         LA    R1,DDLINE
         BAL   R14,CIO
         MVC   DDLINE,DDLINE-1
         AR    R4,R2
         B     DDLIST
DDEXIT   TAG
         MVI   CLIST,OFF
         TITLE 'COMMAND PROCESSOR'
*
*              OUTSTANDANG WTOR
*
SETSTOP  TAG
         CLI   CSTATUS,REMOTE
         BE    NEXT
         XC    STOPECB,STOPECB
         WTOR  'REPLY ANY CHARACTER TO CANCEL FUNCTION',STOP,1,STOPECB
*
*              GET NEXT FUNCTION
*
NEXT     TAG
         TM    STOPECB,X'40'
         BO    STOPPED
         CIO   'ENTER NEXT FUNCTION',OP,2
TESTOP   TAG
         CLC   OP,=C'BF'
         BE    BF
         CLC   OP,=C'BS'
         BE    BS
         CLC   OP,=C'BY'
         BE    BY
         CLC   OP,=C'CI'
         BE    CI
         CLC   OP,=C'CO'
         BE    CO
         CLC   OP,=C'DE'
         BE    DE
         CLC   OP,=C'DF'
         BE    DF
         CLC   OP,=C'FS'
         BE    FS
         CLC   OP,=C'HI'
         BE    HI
         CLC   OP,=C'LE'
         BE    LE
         CLC   OP,=C'LL'
         BE    LL
         CLC   OP,=C'RT'
         BE    RT
         CLC   OP,=C'RW'
         BE    RW
         CLC   OP,=C'SD'
         BE    SD
         CLC   OP,=C'SF'
         BE    SF
         CLC   OP,=C'SK'
         BE    SK
         CLC   OP,=C'SR'
         BE    SR
         CLC   OP,=C'OI'
         BE    OI
         CLC   OP,=C'OO'
         BE    OO
         CLC   OP,=C'TC'
         BE    TC
         CLC   OP,=C'TD'
         BE    TD
         CLC   OP,=C'TL'
         BE    TL
         CLC   OP,=C'OC'
         BE    OC
         CLC   OP,=C'CC'
         BE    CC
*
*              INVALID FUNCTION - RETRY
*
         CIO   'INVALID FUNCTION'
         B     NEXT
         TITLE 'BF - BACKSPACE FILE'
BF       TAG
         CIO   'BACKSPACE FILE SELECTED'
         MVC   CCWTAPE,=AL3(BFCCW)
         BAL   R12,EXCP
         BP    BFERR
         CIO   'BACKSPACE FILE COMPLETE'
         B     NEXT
BFERR    CIO   'BACKSPACE FILE ERROR'
         B     NEXT
         TITLE 'BS - BACKSPACE FILE'
BS       TAG
         CIO   'BACKSPACE SELECTED'
         MVC   CCWTAPE,=AL3(BSCCW)
         BAL   R12,GETCNT
         LR    R2,R0
BSLOOP   TAG
         BAL   R12,EXCP
         BP    BSERR
         BCT   R2,BSLOOP
         CIO   'BACKSPACE BLOCKS COMPLETE'
         B     NEXT
BSERR    CIO   'BACKSPACE BLOCK ERROR'
         B     NEXT
         TITLE 'BY - TERMINATE PROGRAM'
*
*              TERMINATE PROGRAM
*
BY       TAG
         CIO   'BY FOLKS'
         SUBEXIT
         TITLE 'CI - CLOSE INPUT FILE ROUTINE'
*
*              CLOSE INPUT ROUTINE
*
CI       TAG
         CLI   ISTATUS,OPEN
         BNE   CIEXIT
         MVI   ISTATUS,CLOSED
         CIO   'CLOSE INPUT FILE SELECTED'
         CLOSE (TAPEDCB)
CIEXIT   TAG
         CIO   'INPUT FILE CLOSED'
         CLC   OP,=C'CI'
         BE    NEXT
         B     TESTOP
         TITLE 'CONSOLE LIST ON/OFF COMMANDS'
OC       TAG
         MVI   CLIST,ON
         CIO   'CONSOLE LIST ON'
         B     NEXT
CC       TAG
         MVI   CLIST,OFF
         CIO   'CONSOLE LIST OFF'
         B     NEXT
         TITLE 'CO - CLOSE OUTPUT FILE ROUTINE'
*
*              CLOSE OUTPUT ROUTINE
*
CO       TAG
         CLI   OSTATUS,OPEN
         BNE   COEXIT
         CIO   'CLOSE OUTPUT FILE SELECTED'
         MVI   OSTATUS,CLOSED
         CLOSE (OUTDCB)
COEXIT   TAG
         CIO   'OUTPUT FILE CLOSED'
         CLC   OP,=C'CO'
         BE    NEXT
         B     TESTOP
         TITLE 'DE - DIAGNOSE ERROR'
DE       TAG
         CIO   'DIAGNOSE ERROR SELECTED'
         CIO   'CSW STATUS FOLLOWING LAST CP'
         TMCIO CSWTAPE+3,X'80','ATTENTION'
         TMCIO CSWTAPE+3,X'40','STATUS MODIFIER'
         TMCIO CSWTAPE+3,X'20','CONTROL UNIT END'
         TMCIO CSWTAPE+3,X'10','BUSY'
         TMCIO CSWTAPE+3,X'08','CHANNEL END'
         TMCIO CSWTAPE+3,X'04','DEVICE END'
         TMCIO CSWTAPE+3,X'02','UNIT CHECK'
         TMCIO CSWTAPE+3,X'01','UNIT EXCEPTION'
         TMCIO CSWTAPE+4,X'80','PROGRAM-CONTROL INTERRUPT'
         TMCIO CSWTAPE+4,X'40','INCORRECT LENGTH'
         TMCIO CSWTAPE+4,X'20','PROGRAM CHECK'
         TMCIO CSWTAPE+4,X'10','PROTECTION CHECK'
         TMCIO CSWTAPE+4,X'08','CHANNEL DATA CHECK'
         TMCIO CSWTAPE+4,X'04','CHANNEL CONTROL CHECK'
         TMCIO CSWTAPE+4,X'02','INTERFACE CONTROL CHECK'
         TMCIO CSWTAPE+4,X'01','CHAINING CHECK'
         MVC   CCWTAPE,=AL3(SENSECCW)
         BAL   R12,EXCP
         BP    SNSERR
         CIO   'SENSE BYTE STATUS'
         TMCIO SENSE+0,X'80','COMMAND REJECT'
         TMCIO SENSE+0,X'40','INTERVENTION REQUIRED'
         TMCIO SENSE+0,X'20','BUS-OUT CHECK'
         TMCIO SENSE+0,X'10','EQUIPMENT CHECK'
         TMCIO SENSE+0,X'08','DATA CHECK'
         TMCIO SENSE+0,X'04','OVERRUN'
         TMCIO SENSE+0,X'02','WORD COUNT ZERO'
         TMCIO SENSE+0,X'01','DATA CONVERTER CHECK'
         TMCIO SENSE+1,X'80','NOISE'
         TMCIO SENSE+1,X'40','SELECTED AND READY'
         TMCIO SENSE+1,X'20','NOT READY'
         TMCIO SENSE+1,X'10','7-TRACK FEATURE INSTALLED'
         TMCIO SENSE+1,X'08','LOAD POINT'
         TMCIO SENSE+1,X'04','SELECTED AND WRITE STATUS'
         TMCIO SENSE+1,X'02','FILE PROTECTED'
         TMCIO SENSE+1,X'01','INCOMPATABLE DENSITY'
         TMCIO SENSE+3,X'80','VERTICAL REDUNDANCY CHECK'
         TMCIO SENSE+3,X'40','LONGITUDINAL REDUNDANCY CHECK'
         TMCIO SENSE+3,X'30','SKEW'
         TMCIO SENSE+3,X'20','CYCLI REDUNDANCY CHECK'
         TMCIO SENSE+3,X'10','SKEW REGISTER VRC'
         TMCIO SENSE+3,X'80','READ REGISTER VRC'
         TMCIO SENSE+3,X'40','PHASE ENCODING'
         TMCIO SENSE+3,X'20','BACKWARD STATUS'
         TMCIO SENSE+3,X'10','DATA REGISTER PARITY'
         TMCIO SENSE+4,X'80','ECHO CHECK'
         TMCIO SENSE+4,X'40','REJECT TU'
         TMCIO SENSE+4,X'20','READ CLOCK VRC'
         TMCIO SENSE+4,X'10','WRITE CLOCK VRC'
         TMCIO SENSE+4,X'08','START READ CHECK'
         TMCIO SENSE+4,X'04','C SEQUENCE CHECK'
         TMCIO SENSE+4,X'02','B SEQUENCE CHECK'
         TMCIO SENSE+4,X'01','A SEQUENCE CHECK'
         CIO   'END OF ERROR DIAGNOSES'
         B     NEXT
SNSERR   TAG
         CIO   'SENSE EXCP ERROR'
         B     NEXT
         TITLE 'DF - DEFINE FILE CHARACTERISTICS'
*
*              DEFINE FILE CHARACTERISTICS
*
DF       TAG
         CIO   'ENTER RECORD FORMAT U,F,FB,V,VB',RTYPE,2
         CLI   RTYPE,C'U'
         BE    DFOK
         CLI   RTYPE,C'F'
         BE    DFR
         CLI   RTYPE,C'V'
         BE    DFOK
         CIO   'INVALID FORMAT'
         B     DF
DFR      TAG
         CIO   'ENTER RECORD LENGTH XXXX',XXXX,4
         TRT   XXXX,EDITDEC
         BC    7,DFR
         PACK  PWORK,XXXX
         CVB   R0,PWORK
         ST    R0,LREC
DFOK     TAG
         B     NEXT
         TITLE 'FS - FIELD SELECT FOR DUMP AND LIST'
*
*              FIELD SELECT FOR DUMP AND LIST
*
FS       TAG
         MVI   FSTATUS,OFF
         CIO   'FIELD SELECT - REPLY YES OR NO',XXXX,4
         CLI   XXXX,C'N'
         BE    NEXT
         CIO   'ENTER FIELD OFFSET XXXX',XXXX,4
         TRT   XXXX,EDITDEC
         BC    7,FS
         PACK  PWORK,XXXX
         CVB   R0,PWORK
         ST    R0,OFFSET
GETFL    TAG
         CIO   'ENTER FIELD LENGTH XXXX',XXXX,4
         TRT   XXXX,EDITDEC
         BC    7,GETFL
         PACK  PWORK,XXXX
         CVB   R0,PWORK
         ST    R0,FLENGTH
         MVI   FSTATUS,ON
         B     NEXT
         TITLE 'HI - LIST FUNCTIONS OF TAPESTRY'
*
*              LIST FUNCTIONS
*
HI       TAG
         CIO   'HI, MY NAME IS TAPESTRY'
         CIO   'I PERFORM THE FOLLOWING FUNCTIONS UPON REQUEST'
         CIO   'BF - BACKSPACE FILE'
         CIO   'BS - BACK SPACE PHYSICAL RECORD'
         CIO   'BY - TERMINATE PROGRAM'
         CIO   'CI - CLOSE INPUT TAPE FILE'
         CIO   'CO - CLOSE OUTPUT FILE'
         CIO   'DE - DIAGNOSE ERROR'
         CIO   'DF - DEFINE FILE CHARACTERISTICS (U ASSUMED)'
         CIO   'FS - FIELD SELECT FOR DUMP AND LIST'
         CIO   'HI - LIST FUNCTIONS'
         CIO   'LE - EXIT TO NEXT COMMAND RECORD IF LAST READ HIT TM'
         CIO   'LL - LOOP BACK TO BEGINNING OF COMMAND RECORD'
         CIO   'OI - OPEN INPUT TAPE FILE FOR EXCP I/O'
         CIO   'OO - OPEN OUTPUT SEQUENTIAL FILE FOR QSAM I/O'
         CIO   'RT - READ TEST'
         CIO   'RW - REWIND TAPE'
         CIO   'SF - SKIP FILE (TAPE MARK)'
         CIO   'SK - SET KEY CONTROL FOR DUMP, LIST, OR COPY'
         CIO   'SR - SKIP PHYSICAL RECORD'
         CIO   'TC - TAPE COPY TO ANY SEQUENTIAL FILE'
         CIO   'TD - TAPE TO PRINTER DUMP IN EBCDIC AND HEX'
         CIO   'TL - TAPE TO PRINTER LIST IN EBCDIC ONLY'
         B     NEXT
         TITLE 'LE - EXIT TO NEXT COMMMAD RECORD IF LAST READ HIT TM'
LE       TAG
         IF    (CLI,READTM,TRUE,EQ)
               CIO  'LE EXIT DUE TO TAPE MARK '
               MVC  NEXTQ,=A(BLANK)
         ELSE
               CIO  'LE EXIT SURPRESSED'
         FI
         MVI   READTM,TRUE
         B     NEXT
         TITLE 'LL - LOOP BACK TO BEGINNING OF COMMAND RECORD'
LL       TAG
         IF    (CLI,LOOP,FALSE,EQ)
               CIO  'LOOP BACK SUCCESSFUL'
               MVC  NEXTQ,=A(ANS)
         ELSE
               CIO  'LOOP BACK SURPRESSED'
         FI
         MVI   LOOP,TRUE
         B     NEXT
         TITLE 'OI - OPEN INPUT FILE ROUTINE'
*
*              OPEN INPUT ROUTINE
*
OI       TAG
         CLI   ISTATUS,CLOSED
         BNE   CI
         CIO   'ENTER DDNAME OF INPUT TAPE FILE',DDNAME,8
         BAL   R12,CHKDD
         BP    OI
         MVC   TAPEDCB+40(8),DDNAME
         OPEN  (TAPEDCB,(INPUT))
         CIO   'INPUT TAPE FILE OPEN'
         MVI   ISTATUS,OPEN
         CLC   OP,=C'OI'
         BE    NEXT
         B     TESTOP
         TITLE 'OO - OPEN OUTPUT FILE ROUTINE'
*
*              OPEN OUTPUT ROUTINE
*
OO       TAG
         CLI   OSTATUS,CLOSED
         BNE   CO
         CIO   'ENTER DDNAME OF OUTPUT FILE',DDNAME,8
         BAL   R12,CHKDD
         BP    OO
         MVC   OUTDCB+40(8),DDNAME
         OPEN  (OUTDCB,(OUTPUT))
         CIO   'OUTPUT FILE OPEN'
         MVI   OSTATUS,OPEN
         CLC   OP,=C'OO'
         BE    NEXT
         B     TESTOP
         TITLE 'RT - READ TEST'
*
*              RT - READ TEST
*
RT       TAG
         CLI   ISTATUS,CLOSED
         BE    OI
         MVC   CCWTAPE,=AL3(READCCW)
         CIO   'READ TEST SELECTED'
         BAL   R12,SETREC
RTLOOP   TAG
         BAL   R12,GETREC
         B     RTLOOP
         TITLE 'RW - REWIND TAPE'
RW       TAG
         MVC   CCWTAPE,=AL3(RWCCW)
         CIO   'REWIND SELECTED'
         BAL   R12,EXCP
         BP    RWERR
         CIO   'REWIND COMPLETE'
         B     NEXT
RWERR    CIO   'REWIND ERROR'
         B     NEXT
         TITLE 'SD - SET DENSITY TO 1600 PE OR 0800 NRZI'
*
*              SD - SET INPUT TAPE DENSITY TO 1600 PE OR 0800 NRZI
*
SD       TAG
         CIO   'SET DENSITY SELECTED'
SDGET    TAG
         CIO   'ENTER 1600 OR 0800',XXXX,4
         TRT   XXXX,EDITDEC
         BC    7,SDGET
         CLC   XXXX(4),=C'1600'
         BE    SD1600
         CLC   XXXX(4),=C'0800'
         BE    SD0800
         B     SDGET
SD1600   TAG
         MVC   CCWTAPE,=AL3(SD16CCW)
         B     SDIO
SD0800   TAG
         MVC   CCWTAPE,=AL3(SD08CCW)
SDIO     TAG
         BAL   R12,EXCP
         BP    DE
         CIO   'SET DENSITY COMPLETE'
         B     NEXT
         TITLE 'SF - SKIP FILE'
SF       TAG
         CIO   'SKIP FILE SELECTED'
         MVC   CCWTAPE,=AL3(SFCCW)
         BAL   R12,EXCP
         BP    SFERR
         CIO   'SKIP FILE COMPLETE'
         B     NEXT
SFERR    CIO   'SKIP ERROR'
         B     NEXT
         TITLE 'SK - SET KEY CONTROL FOR DUMP, LIST, OR COPY'
*
*              SK - SET KEY CONTROL FOR DUMP, LIST, OR COPY
*
SK       TAG
         MVI   KSTATUS,OFF
         CIO   'SET KEY - REPLY YES OR NO',XXXX,4
         CLI   XXXX,C'N'
         BE    NEXT
         MVI   KSTATUS,ON
SKOFFSET TAG
         CIO   'ENTER KEY OFFSET XXXX',XXXX,4
         TRT   XXXX,EDITDEC
         BC    7,SKOFFSET
         PACK  PWORK,XXXX
         CVB   R2,PWORK            R2=OFFSET
         CH    R2,=H'4095'
         BH    SKOFFSET
SKLENGTH TAG
         CIO   'ENTER KEY LENGTH IN BYTES XXXX',XXXX,4
         TRT   XXXX,EDITDEC
         BC    7,SKLENGTH
         PACK  PWORK,XXXX
         CVB   R3,PWORK            R3=LENGTH
         CH    R3,=H'16'
         BH    SKLENGTH
         LTR   R3,R3
         BNP   SKLENGTH
         BCTR  R3,R0               R3=LENGTH-1
SKTYPE   TAG
         CIO   'ENTER KEY TYPE - HEX OR CHAR',XXXX,4
         CLI   XXXX,C'H'
         BE    SKKEY
         CLI   XXXX,C'C'
         BNE   SKTYPE
SKKEY    TAG
         CIO   'ENTER KEY',WORK,32
         MVC   KEY(16),WORK
         CLI   XXXX,C'H'
         BNE   SKC
         EX    R3,SKTRTHEX
         BC    7,SKKEY
         TR    WORK(32),C1C6-193
         PACK  KEY(8),WORK(15)
         PACK  KEY+7(8),WORK+14(15)
         PACK  KEY+14(3),WORK+28(5)
SKC      TAG
         CIO   'ENTER DESIRED TYPE OF KEY CONTROL',XXXX,2
         LM    R5,R7,=A(KCTAB,6,KCTAB+LKCTAB-5)
SKCL     TAG
         CLC   XXXX(2),0(R5)       TEST FOR EQUAL KEY CONTROL CODE
         BE    SKHIT
         BXLE  R5,R6,SKCL
         B     SKC
SKHIT    TAG
         ST    R5,KTYPAD
         MVC   KBRANCH(4),2(R5)    SET KEY CONTROL BRANCH
         ST    R2,BWORK
         OI    BWORK+2,X'20'
         MVC   KTEST+4(2),BWORK+2  SET CLC OFFSET FROM R2
         STC   R3,KTEST+1          SET CLC LENGTH
         B     NEXT
SKTRTHEX TRT   WORK(0),VALIDHEX
         TITLE 'SR - SKIP RECORD'
SR       TAG
         CIO   'SKIP RECORD SELECTED'
         MVC   CCWTAPE,=AL3(SRCCW)
         BAL   R12,GETCNT
         LR    R2,R0
SRLOOP   TAG
         BAL   R12,EXCP
         BP    SRERR
         BCT   R2,SRLOOP
         CIO   'SKIP RECORD COMPLETE'
         B     NEXT
SRERR    TAG
         CIO   'SKIP ERROR'
         B     NEXT
         TITLE 'TC - TAPE COPY'
*
*              TAPE COPY TO SEQUENTIAL FILE
*
TC       TAG
         CLI   ISTATUS,CLOSED
         BE    OI
         CLI   OSTATUS,CLOSED
         BE    OO
         MVC   CCWTAPE,=AL3(READCCW)
         CIO   'TAPE COPY SELECTED'
         BAL   R12,SETREC
TCL1     TAG
         BAL   R12,GETREC
         TM    OUTDCB+36,X'C0'     TEST OUTPUT FORMAT U
         BNO   TCP1
         STH   R3,OUTDCB+82        SET LRECL FOR EACH TYPE U BLOCK
TCP1     PUT   OUTDCB,(2)
         B     TCL1
         TITLE 'TD - TAPE DUMP'
*
*              TAPE DUMP IN HEX AND EBCDIC
*
TD       TAG
         CLI   ISTATUS,CLOSED
         BE    OI                  OPEN INPUT FILE IF CLOSED
         MVC   CCWTAPE,=AL3(READCCW)    SET IOB CCW ADDRESS
         CIO   'TAPE DUMP SELECTED'
         BAL   R12,SETREC          SETUP READ RECORD ROUTINE
         ZAP   LINE,MAXLINES
         MVI   PSTATUS,LISTDATA
TDL1     TAG
         BAL   R12,GETREC          R2=A(RECORD) R3=LENGTH
         LA    R4,0                R4=HEX DUMP RELATIVE ADDRESS IN HEX
         EDIT TO=TDBLK,FROM=CBLK+1,MASK=' ZZZZZ'
         EDIT TO=TDREC,FROM=CREC+1,MASK=' ZZZZZ'
         PIO   SPACE
TDL2     TAG
         ST    R4,BWORK
         UNPK  HEXADDR(7),BWORK+1(4)
         MVI   HEXADDR+6,C' '
         TR    HEXADDR,F0FF-240
         MVC   EBCDIC,0(R2)
         UNPK  WORK+00(15),00(8,R2)
         UNPK  WORK+14(15),07(8,R2)
         UNPK  WORK+28(15),14(8,R2)
         UNPK  WORK+42(15),21(8,R2)
         UNPK  WORK+56(09),28(5,R2)
         TR    WORK(64),F0FF-240
         CH    R3,=H'32'
         BNL   MOVEHEX
         LR    R5,R3
         SLL   R5,1
         LA    R1,WORK(R5)
         MVI   0(R1),C' '
         MVC   1(63,R1),0(R1)      BLANK OUT HEX WORK AREA
         LA    R1,EBCDIC(R3)
         MVI   0(R1),X'00'         ZERO FIRST BYTE BEYOND RECORD
         LA    R5,31
         SR    R5,R3
         BZ    MOVEHEX
         BCTR  R5,R0
         EX    R5,MVCZERO
MOVEHEX  TAG
         MVC   HEX+00(8),WORK+00
         MVC   HEX+09(8),WORK+08
         MVC   HEX+18(8),WORK+16
         MVC   HEX+27(8),WORK+24
         MVC   HEX+39(8),WORK+32
         MVC   HEX+48(8),WORK+40
         MVC   HEX+57(8),WORK+48
         MVC   HEX+66(8),WORK+56
         TR    EBCDIC,VALIDEBC
         PIO   HEXLINE
         MVC   TDBLK,TDBLK-1
         MVC   TDREC,TDREC-1
         LA    R2,32(R2)
         LA    R4,32(R4)
         SH    R3,=H'32'
         BP    TDL2
         B     TDL1
MVCZERO  MVC   1(0,R1),0(R1)       ZERO OUT EBCDIC AREA BEYOND RECORD
         TITLE 'TL - TAPE LIST IN EBCDIC'
*
*              TAPE LIST IN EBCDIC
*
TL       TAG
         CLI   ISTATUS,CLOSED
         BE    OI
         MVC   CCWTAPE,=AL3(READCCW)
         CIO   'TAPE LIST SELECTED'
         BAL   R12,SETREC
         ZAP   LINE,MAXLINES
         MVI   PSTATUS,LISTDATA
TLL1     TAG
         BAL   R12,GETREC
         EDIT  TO=TLBLK,FROM=CBLK,MASK=' ZZZZZZZ'
         EDIT  TO=TLREC,FROM=CREC,MASK=' ZZZZZZZ'
         PIO   SPACE
TLL2     TAG
         CH    R3,MAXLINE
         BL    TLLAST
         LH    R4,MAXLINE
TLSETL   TAG
         BCTR  R4,R0
         EX    R4,TLMVC
         EX    R4,TLTR
         PIO   TLINE
         MVC   TLINE,TLINE-1
         AH    R2,MAXLINE
         SH    R3,MAXLINE
         BP    TLL2
         B     TLL1
TLLAST   TAG
         LR    R4,R3
         B     TLSETL
TLMVC    MVC   TLDATA(0),0(R2)
TLTR     TR    TLDATA(0),VALIDEBC
         TITLE 'SETREC - INITIALIZE SEQUENTIAL READ ROUTINE'
*
*              SET UP GETREC ROUTINE TO START AT FIRST RECORD IN
*              FIRST BLOCK ON TAPE AT CURRENT POSITION AND READ XXXX
*              RECORDS
SETREC   TAG
         CLI   KSTATUS,ON
         BNE   CHECKFS
         L     R1,KTYPAD
         MVC   KBRANCH(4),2(R1)    RESTORE KEY CONTROL BRANCH
         CIO   'KEY CONTROL ACTIVE'
CHECKFS  TAG
         CLI   FSTATUS,ON
         BNE   SETGETR
         CIO   'FIELD SELECT ACTIVE'
SETGETR  TAG
         CIO   'ENTER RECORDS XXXX, CONT, OR ALL',XXXX,4
         CLC   XXXX(4),=C'CONT'
         BE    SETEXIT
         CLC   XXXX(3),=C'ALL'
         BE    SETALL
         TRT   XXXX,EDITDEC
         BC    8,PACKBLK
         B     SETREC
SETALL   ZAP   MREC,=P'9999999'
         B     SETCNT
PACKBLK  TAG
         PACK  MREC,XXXX           SET MAXIMUM RECORD COUNT
SETCNT   TAG
         ZAP   CREC,=P'0'
         ZAP   CBLK,=P'0'
         XC    RECREGS(20),RECREGS
SETEXIT  TAG
         ZAP   MRETRY,=P'0'
         BR    R12
         TITLE 'GETREC - SEQUENTIAL READ ROUTINE'
*
*              RETURN ADDRESS OF NEXT RECORD IN R2 AND LENGTH IN R3
*
GETREC   TAG
         MVI   READTM,FALSE
         MVI   LOOP,FALSE
         AP    CREC,=P'1'
         CP    CREC,MREC
         BP    GRMAX
         ST    R12,RECLINK
         STM   R4,R6,RECSAVE
         LM    R2,R6,RECREGS
         AR    R2,R3
         CLR   R2,R6
         BL    GETLNG
         BH    GRTERR
READBLK  BAL   R12,EXCP
         BP    GRIOERR
         AP    CBLK,=P'1'
SETBAD   LA    R4,BLOCK                 R4=A(BLOCK)
         LH    R5,READCCW+6
         SH    R5,CSWTAPE+5             R5=LENGTH OF BLOCK
         LA    R6,0(R4,R5)              R6=A(BLOCK+ LENGTH)
         LR    R2,R4
         CLI   RTYPE,C'V'
         BNE   GETLNG
         MVC   HWORD,0(R2)
         CH    R5,HWORD                 IF VARIABLE VERIFY BLOCK LENGTH
         BNE   GRTERR
         CLI   RTYPE+1,C'B'             IF VB SKIP BLOCK LENGTH
         BNE   GETLNG
         LA    R2,4(R2)
GETLNG   TAG
GRV      CLI   RTYPE,C'V'
         BNE   GRF
         MVC   HWORD,0(R2)
         LH    R3,HWORD
         LTR   R3,R3
         BNP   GRTERR
         LA    R1,0(R3,R2)
         CLR   R1,R6
         BH    GRTERR
         B     GREXIT
GRF      CLI   RTYPE,C'F'
         BNE   GRU
         L     R3,LREC
         B     GREXIT
GRU      CLI   RTYPE,C'U'
         BNE   GRTERR
         LR    R3,R5
         B     GREXIT
GRTERR   TAG
         CIO   'FUNCTION TERMINATED DUE TO INVALID RECORD FORMAT'
         B     COUNTS
GREXIT   TAG
         L     R12,RECLINK
         STM   R2,R6,RECREGS
         LM    R4,R6,RECSAVE
         CLI   KSTATUS,OFF
         BE    GRSK
KTEST    CLC   KEY(0),0(R2)
KBRANCH  B     GRSK
GRSK     TAG
         CLI   FSTATUS,ON
         BNE   GRAB
         A     R2,OFFSET
         L     R3,FLENGTH
GRAB     TAG
         BR    R12
GRIOERR  TAG
         B     *(15)
         B     GRUE                UNIT EXCEPTION
         B     GRUC                UNIT CHECK
GRUE     TAG
         MVI   READTM,TRUE
         CIO   'FUNCTION TERMINATED BY TAPE MARK'
         B     COUNTS
GRMAX    CIO   'FUNCTION TERMINATED BY RECORD COUNT'
         B     COUNTS
GRUC     TAG
         CP    MRETRY,=P'0'
         BP    IOCONT
IOERR    TAG
         CIO   'I/O ERROR - REPLY RETRY OR CANCEL',IO,6
         CLI   IO,C'C'
         BE    IOCANCEL
         CLI   IO,C'R'
         BNE   IOERR
IORETRY  TAG
         CIO   'ENTER RETRIES PER SKIP',XXXX,4
         TRT   XXXX,EDITDEC
         BC    7,IORETRY
         PACK  MRETRY,XXXX
         ZAP   CRETRY,MRETRY
IOCONT   TAG
         SP    CRETRY,=P'1'
         BP    READBLK
         ZAP   CRETRY,MRETRY
         MVC   CCWTAPE,=AL3(SRCCW)
         BAL   R12,EXCP
         MVC   CCWTAPE,=AL3(READCCW)
         BP    GRIOERR
         CIO   'PHYSICAL RECORD SKIPPED'
         B     READBLK
IOCANCEL TAG
         CIO   'FUNCTION TERMINATED BY OPERATOR AFTER I/O ERROR'
COUNTS   TAG
         SP    CREC,=P'1'
         EDIT  TO=DBLK,FROM=CBLK,MASK=' ZZZZZZZ'
         EDIT  TO=DREC,FROM=CREC,MASK=' ZZZZZZZ'
         LA    R0,=A(L'CLINE-1,0)
         LA    R1,CLINE
         BAL   R14,CIO
         B     NEXT
KRB      TAG
         BNE   GETREC
         NI    KBRANCH+1,X'0F'
         B     GRSK
KRA      TAG
         MVC   KBRANCH(4),KBEXIT
         B     GRSK
KAA      TAG
         BNE   GETREC
         NI    KBRANCH+1,X'0F'
         B     GETREC
KEXIT    TAG
         CIO   'FUNCTION TERMINATED DUE TO KEY CONTROL'
         B     COUNTS
KBEXIT   B     KEXIT
         TITLE 'GETCNT - GET COUNT OF PHYSICAL RECORDS TO MOVE'
*
*              GET RECORD COUNT
*
GETCNT   TAG
         CIO   'ENTER PHYSICAL RECORD COUNT XXXX',XXXX,4
         TRT   XXXX,EDITDEC
         BC    8,GOODCNT
         CIO   'INVALID COUNT'
         B     GETCNT
GOODCNT  TAG
         PACK  PWORK,XXXX
         CVB   R0,PWORK
         BR    R12
         TITLE 'OPERATOR CANCEL ROUTINE'
*
*              OPERATOR HAS CANCELLED FUNCTION
*
STOPPED  TAG
         MVI   PIOEXIT+1,X'FE'
         CIO   'FUNCTION TERMINATED BY OPERATOR'
         MVI   PIOEXIT+1,X'0E'
         TM    STOPECB,X'40'
         BO    SETSTOP
         B     NEXT
         TITLE 'EXECUTE CHANNEL PROGRAM ROUTINE'
EXCP     TAG
         TM    STOPECB,X'40'
         BO    STOPPED
         CLI   ISTATUS,CLOSED
         BE    OI
         XC    TAPEECB,TAPEECB
         EXCP  TAPEIOB
         WAIT  ECB=TAPEECB
         SR    R15,R15
         CLI   TAPEECB,X'7F'
         BE    EXCPGOOD
         TM    CSWTAPE+3,X'02'
         BO    EXCPERR
         TM    CSWTAPE+3,X'01'
         BNO   EXCPERR
         AH    R15,=H'4'                RC=4 INDICATES UNIT EXCEPTION
         BR    R12
EXCPERR  AH    R15,=H'8'                RC=8 INDICATES UNIT CHECK
         BR    R12
EXCPGOOD AH    R15,=H'0'                RC=0 INDICATES EXCP OK
         BR    R12
         TITLE 'CIO - CONSOLE I/O ROUTINE'
*
*              CONSOLE I/O ROUTINE
*
CIO      TAG
         STM   R14,R6,CIOSAVE
         LR    R2,R0
         LM    R2,R4,0(R2)
         EX    R2,CIOMVC1          MOVE MESSAGE TO WTO AREA
         EX    R2,CIOMVC2          MOVE MESSAGE TO PRINT AREA
         CLI   PSTATUS,LISTDATA
         BNE   CIOP1
         MVI   PSTATUS,LISTIO
         ZAP   LINE,MAXLINES
CIOP1    TAG
         PIO   CIOLINE
         MVC   CIOLINE,CIOLINE-1
         LA    R2,5(R2)
         STH   R2,CIOLMSG
         LTR   R3,R3
         BZ    CIOWTO
CIOGET   TAG
         L     R5,NEXTQ
         CLI   0(R5),C' '
         BE    CIOWTOR              IF NO QUEUED COMMAND GO TO WTOR
         LR    R6,R5
CIOL1    TAG
         LA    R5,1(R5)            LOCATE END OF QUEUED COMMAND
         CLI   0(R5),C' '
         BE    CIODEQUE
         CLI   0(R5),C','
         BE    CIODEQUE
         B     CIOL1
CIODEQUE TAG
         LA    R1,1(R5)
         ST    R1,NEXTQ            SET POINTER TO NEXT QUEUED COMMAND
         SR    R5,R6
         CLR   R5,R4               CHECK IF QUEUED COMMAND IS TOO BIG
         BH    CIOERR
         CLI   QSTATUS,ON
         BE    CIOS1
         CLI   CSTATUS,REMOTE
         BE    CIOS1
         CLI   CLIST,OFF
         BE    CIOS1
         WTO   MF=(E,CIOLMSG)      WTO THE WTOR MESSAGE PART
CIOS1    TAG
         MVI   QSTATUS,OFF
         LA    R1,6(R5)
         STH   R1,CIOLMSG          SET LENGTH OF REPLY WTO
         BCTR  R5,R0
         MVC   CIOMSG(2),=C'  '
         BCTR  R4,R0
         EX    R5,CIOMVC3          MOVE QUEUED COMMAND TO WTO AREA
         EX    R4,CIOXC            ZERO INPUT AREA
         EX    R5,CIOMVC4          MOVE QUEUED COMMAND TO INPUT AREA
         EX    R4,CIOTR            CONVERT INPUT AREA TO UPPER CASE
         EX    R5,CIOMVC5          MOVE QUEUED COMMAND TO PRINT AREA
         CLI   CSTATUS,REMOTE
         BE    CIOS2
         CLI   CLIST,OFF
         BE    CIOS2
         WTO   MF=(E,CIOLMSG)      WTO REPLY FROM QUEUE
CIOS2    TAG
         PIO   CIOLINE
         MVC   CIOLINE,CIOLINE-1
         B     CIOEXIT
CIOERR   TAG
         CLI   CSTATUS,REMOTE
         BE    CIOS3
         CLI   CLIST,OFF
         BE    CIOS3
         WTO   'INVALID QUEUED COMMAND LENGTH'
CIOS3    TAG
CIOWTOR  TAG
         MVI   QSTATUS,ON
         CLI   CSTATUS,REMOTE
         BE    CIOINPUT
         XC    CIOECB,CIOECB
         XC    ANS,ANS
         WTOR  MF=(E,CIOWTORL)
         WAIT  ECB=CIOECB
CIOTRANS TAG
         TR    ANS,UPPEREBC
         LA    R1,ANS
         ST    R1,NEXTQ
         B     CIOGET
CIOWTO   TAG
         CLI   CSTATUS,REMOTE
         BE    CIOS4
         CLI   CLIST,OFF
         BE    CIOS4
         WTO   MF=(E,CIOLMSG)
CIOS4    TAG
CIOEXIT  TAG
         LM    R14,R6,CIOSAVE
         BR    R14
CIOINPUT TAG
         GET   INPUTDCB,ANS
         B     CIOTRANS
CIOXC    XC    0(0,R3),0(R3)        ZERO INPUT AREA
CIOTR    TR    0(0,R3),UPPEREBC
CIOMVC1  MVC   CIOLINE+1(0),0(R1)
CIOMVC2  MVC   CIOMSG(0),0(R1)
CIOMVC3  MVC   CIOMSG+2(0),0(R6)
CIOMVC4  MVC   0(0,R3),0(R6)
CIOMVC5  MVC   CIOLINE+3(0),0(R6)
         TITLE 'PIO - PUT I/O ROUTINE FOR PRINTER SUPPORT'
PIO      TAG
         STM   R14,R2,PIOSAVE
         CP    LINE,MAXLINES
         BL    PIOLINE
PIOHEAD  TAG
         ZAP   LINE,=P'0'
         AP    PAGE,=P'1'
         EDIT  TO=DPAGE,FROM=PAGE,MASK=' ZZZZZ'
         PUT   PRINT,PIOH1         PRINT HEADING
         PUT   PRINT,SPACE
         CLI   PSTATUS,LISTIO
         BE    PIOLINE
         CLC   OP,=C'TD'
         BNE   PIOTL
         PUT   PRINT,TDH1          PRINT TAPE DUMP HEADING IF OP=TD
         B     PIOLINE
PIOTL    CLC   OP,=C'TL'
         BNE   PIOLINE
         PUT   PRINT,TLH1
         PUT   PRINT,TLH2
PIOLINE  TAG
         L     R2,PIOSAVE+12       R2=R1=A(PRINT LINE)
         PUT   PRINT,(2)           PRINT LINE
         AP    LINE,=P'1'
         LM    R14,R2,PIOSAVE
PIOEXIT  BCR   0,R14
         TM    STOPECB,X'40'
         BO    STOPPED
         BR    R14
         TITLE 'DDNAME VERIFY ROUTINE'
*
*              CHECK DDNAME ROUTINE
*
CHKDD    TAG
         L     R1,DDADDR                R1=A(TIOT+24)
         SR    R2,R2
CHKDDL   TAG
         IC    R2,0(R1)
         LTR   R2,R2
         BZ    CHKERR
         CLC   DDNAME,4(R1)
         BE    CHKOK
         AR    R1,R2
         B     CHKDDL
CHKERR   CIO   'DDNAME NOT FOUND'
         LTR   R15,R1
         BR    R12
CHKOK    SR    R15,R15
         BR    R12
         TITLE 'FILE DCB SECTION'
*
*              FILE SECTION
*
TAPEDCB  DCB   DSORG=PS,MACRF=E,DEVD=TA,DDNAME=XXXXXXXX
OUTDCB   DCB   DSORG=PS,MACRF=PM,DDNAME=YYYYYYYY
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT,RECFM=FBA,LRECL=133
INPUTDCB DCB   DSORG=PS,MACRF=GM,DDNAME=X,LRECL=80,EODAD=BY
TAPEIOB  IOB   FLAGS=X'02',ECB=TAPEECB,CCW=READCCW,DCB=TAPEDCB,ID=TAPE
TAPEECB  DC    F'0'
         TITLE 'DATA SECTION'
*
*              DATA SECTION
*
         EQUREGS
EQ       EQU   8
BLANK    DC    C' '
READTM   LOGICAL FALSE,(TRUE,FALSE)
LOOP     LOGICAL FALSE,(TRUE,FALSE)
READCCW  CCW   X'02',BLOCK,X'20',L'BLOCK     READ A BLOCK
SENSECCW CCW   X'04',SENSE,X'20',6      READ SIX  SENSE BYTES
RWCCW    CCW   X'07',BLOCK,X'00',1      REWIND TAPE
SRCCW    CCW   X'37',BLOCK,X'00',1      FORWARD SPACE BLOCK
SFCCW    CCW   X'3F',BLOCK,X'00',1      FORWARD SPACE FILE
BSCCW    CCW   X'27',BLOCK,X'00',1      BACKSPACE BLOCK
BFCCW    CCW   X'2F',BLOCK,X'00',1      BACKSPACE FILE
SD16CCW  CCW   X'C3',BLOCK,X'00',1     SET 1600 BPI PE MODE
SD08CCW  CCW   X'CB',BLOCK,X'00',1     SET 0800 BPI NRZI MODE
SPACE    DC    CL133' '
HEXLINE  DS    0CL133
         DC    C' '
TDBLK    DC    CL6' '
         DC    C'  '
TDREC    DC    CL6' '
         DC    C' '
HEXADDR  DC    CL6' '
         DC    C'  '
HEX      DC    CL74' '
         DC    C' *'
EBCDIC   DC    CL32' '
         DC    C'*'
         FILL  HEXLINE
         DC    C' '
TLINE    DS    0CL133
         DC    C' '
TLBLK    DC    CL8' '
         DC    C' '
TLREC    DC    CL8' '
         DC    C' '
TLDATA   DC    CL100' '
         FILL  TLINE
PIOH1    DS    0CL133
         DC    C'1'
         DC    C'TAPESTRY TAPE UTILITY LIST OF OPERATOR I/0  '
         DC    C'AND REQUESTED DATA'
         DC    CL20' '
TIMEDATE DC    CL22' '
         DC    CL10'     PAGE '
DPAGE    DC    CL6' '
         FILL  PIOH1
TDH1     DS    0CL133
         DC    C' '
         DC    C' BLOCK  RECORD ADDRESS'
         DC    34C' ',C'HEX DATA',48C' ',C'EBCDIC'
         FILL  TDH1
TLH1     DS    0CL133
         DC    C' '
         DC    C'   BLOCK   RECORD'
         DC    C' '
         DC       C'          1         2         3         4         '
         DC       C'5         6         7         8         9         '
         FILL  TLH1
TLH2     DS    0CL133
         DC    19C' ',10C'0123456789'
         FILL  TLH2
CIOLINE  DC    CL133' '
         DC    C' '
DDLINE   DC    CL19' '
CLINE    DS    0CL36
         DC    C'TOTAL BLOCKS'
DBLK     DC    CL8' '
         DC    C' RECORDS'
DREC     DC    CL8' '
MAXLINE  DC    H'100'
HWORD    DC    H'0'
NEXTQ    DC    A(ANS)
DDADDR   DC    F'0'
BWORK    DC    F'0'
PIOSAVE  DC    5F'0'
RECREGS  DC    5F'0'
RECSAVE  DC    3F'0'
RECLINK  DC    F'0'
CIOSAVE  DC    9F'0'
CIOECB   DC    F'0'
STOPECB  DC    F'0'
LREC     DC    F'0'
OFFSET   DC    F'0'
FLENGTH  DC    F'0'
KTYPAD   DC    F'0'
LKCTAB   EQU   10*6
KCTAB    TAG
         DC    C'AE'
         BNE   GETREC
         DC    C'AL'
         BNL   GETREC
         DC    C'AH'
         BNH   GETREC
         DC    C'RE'
         BE    GETREC
         DC    C'RL'
         BL    GETREC
         DC    C'RH'
         BH    GETREC
         DC    C'RB'
         B     KRB
         DC    C'RA'
         BE    KRA
         DC    C'AB'
         BE    KEXIT
         DC    C'AA'
         B     KAA
PWORK    DC    D'0'
PTIME    DC    D'0'
CIOWTORL DS    0F
CIOANSAD DC    AL1(L'ANS),AL3(ANS)
         DC    A(CIOECB)
CIOLMSG  DC    AL2(0)
         DC    AL2(0)
CIOMSG   DC    CL100' '
F0FF     DC    C'0123456789ABCDEF'
VALIDEBC DC    64C'.',C' ',128C'.',C'ABCDEFGHI',7C'.'
         DC    C'JKLMNOPQR',8C'.',C'STUVWXYZ',6C'.',C'0123456789',6C'.'
VALIDHEX DC    193X'FF',6X'00',41X'FF',10X'00',6X'FF'
C1C6     DC    X'FAFBFCFDFEFF',41X'00',C'0123456789'
EDITDEC  DC    240X'FF',10X'00',6X'FF'
UPPEREBC DC    107C' ',C',',21C' ',C'ABCDEFGHI',7C' ',C'JKLMNOPQR'
         DC    8C' ',C'STUVWXYZ',23C' ',C'ABCDEFGHI'
         DC    7C' ',C'JKLMNOPQR',8C' ',C'STUVWXYZ',6C' '
         DC    C'0123456789',6C' '
STOP     DC    C'X'
OP       DC    C'XX'
ONLINE   EQU   0
REMOTE   EQU   1
CSTATUS  DC    AL1(ONLINE)         COMMAND SOURCE STATUS
OPEN     EQU   1
CLOSED   EQU   0
ISTATUS  DC    AL1(CLOSED)         INPUT FILE STATUS
OSTATUS  DC    AL1(CLOSED)         OUTPUT FILE STATUS
LISTIO   EQU   0
LISTDATA EQU   1
CLIST    DC    AL1(ON)             CONSOLE LIST ON/OFF SWITCH
PSTATUS  DC    AL1(LISTIO)         PRINTER MODE STATUS
ON       EQU   1
OFF      EQU   0
FSTATUS  DC    AL1(OFF)            FIELD SELECT STATUS
KSTATUS  DC    AL1(OFF)            KEY CONTROL STATUS
QSTATUS  DC    AL1(ON)             COMMAND QUEUE STATUS
ANS      DC    CL80' '
         DC    C' '
WORK     DC    XL128'00'
KEY      DC    XL33'00'
SENSE    DC    XL6'00'
RTYPE    DC    C'U '
LINE     DC    P'60'
MAXLINES DC    P'60'
XXXX     DC    CL4' '
PAGE     DC    P'0000'
MREC     DC    PL4'0'
CREC     DC    PL4'0'
CBLK     DC    PL4'0'
MRETRY   DC    PL3'0'
CRETRY   DC    PL3'0'
IO       DC    CL6' '
DDNAME   DC    CL8' '
         LTORG
         DC    D'0'
BLOCK    DS    XL25000
         END   GU000300
