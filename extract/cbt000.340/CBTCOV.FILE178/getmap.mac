*          DATA SET GU022500   AT LEVEL 018 AS OF 03/05/76
         TITLE 'GU022500 GETMAP - LOAD TABLE WITH PAGE STATUS'
*              AUTHOR.   DON HIGGINS.
*              DATE.     05/21/75.
*              REMARKS.  LOAD TABLE WITH STATUS OF EACH 4K VIRTUAL
*                        PAGE IN OS/VS2 R1.7 SYSTEM.
*
*              REV 1 02/29/76 DSH - ADD TABLE OF REAL PAGE STATUS CODES
*              CALL ARGUMENTS.
*
*                             TIME - 32 BYTE TIME AND DATE
*
*                             INDEX- STATUS DESCRIPTION TABLE
*                                      1. CONTAINS A 40 BYTE DESC.
*                                         FOR EACH STATUS CODE.
*
*                             TABLE- 4096 - HALH WORD PAGE STATUS
*
*                             BITS 0-7 INDEX TO PAGE DESCRIPTION
*                             BITS 8-15 PAGE TYPE AS FOLLOWS
*                              1 - VIRTUAL FREE
*                              2 - NUCLEUS FIXED
*                              3 - SQA
*                              4 - LPA
*                              5 - LSQA
*                              6 - REGION
*                             HALF WORD IS COMPLIMENTED TO
*                             INDICATE THAT IT IS CURRENTLY REAL
*
*                             FRAME - 4096 - HALF WORD FRAME STATUS
*
*                             BITS 0-7 INDEX TO PAGE DESCRIPTION
*                             BITS 815 FRAME TYPE
*                              1 - PAGABLE
*                              2 - FIXED
*
*                              NTYPES - FULL WORD # OF INDEX ENTRIES
*                                           CODE ENTRIES.
*
*              REFERENCES.
*                  1.    OS/VS2 SUPR PLM SY27-7244
*                  2.    OS/VS2 SYSTEM DATA AREAS  SY28-0606
*                  3.    A 568 PAGE SYSTEM DUMP (ANY ONE WILL DUE)
*
         MACRO
         INDEX &DESC
         LR    R1,R9
         AAI   R9,1
         MH    R1,=H'40'
         LA    R1,0(R1,R11)
         AIF   (T'&DESC EQ 'U').STRING
         MVC   0(40,R1),&DESC
         MEXIT
.STRING ANOP
         MVC   0(40,R1),=CL40&DESC
         MEND
         TITLE 'MAINLINE'
         PRINT NOGEN
GETMAP   SUBENTRY
         LM    R10,R12,0(R1)
         ST    R1,ARGLIST
         SPIE  RESET,(4)      RATHER THAN RUN DISABLED, IGNORE 0C4'S
         ST    R1,SAVEPICA
         ZAP   MAXRESET,=P'5'      UP TO A POINT THAT IS
RESTART  TAG
         DTIME  TIMEDATE
         SCALL SROSDATE,(MMDDYY)
         MVC   0(32,R10),TIME      RETURN TIME AND DATE INFO
         ZR    R9             R9= CURRENT STATUS CODE#
         PM    VIRTUAL
         PM    NUC
         PM    SQA
         PM    LPA
         PM    TASKS
         PM    REAL
         L     R1,ARGLIST
         L     R1,16(R1)      LAST ARG
         ST    R9,0(R1)       SET NTYPES
         L     R1,SAVEPICA
         SPIE  MF=(E,(1))
         SUBEXIT
RESET    TAG
         USING *,R15
         MVC   PSW,4(R1)
         IF    (SP,MAXRESET,=P'1',Z)
               ABEND 101,DUMP
         FI
         MVC   9(3,R1),=AL3(RESTART)
         BR    R14
         DROP  R15
         TITLE 'VIRTUAL - INITIALIZE TABLE AS VIRTUAL FREE PAGES'
VIRTUAL  PENTRY
         INDEX 'VIRTUAL FREE PAGES'
         LR    R2,R12
         LR    R3,R2
         A     R3,=A(2*4096)
         DO    WHILE,(CLR,R2,R3,L)
               STC  R9,0(R2)
               MVI  1(R2),TVIRTUAL
               AAI  R2,2
         OD
         PEXIT
         TITLE 'REAL - SET STATUS OF CURRENT REAL PAGES'
REAL     PENTRY
         L     R2,16
         L     R2,CVTPVTP(R2)      RZ-PVT
         L     R3,PVTPFTP(R2)      R3=PFT ORGIN
         LH    R5,PVTFPFN(R2)
         LA    R5,0(R5,R3)         R5=A(FIRST REAL PFTE)
         LH    R6,PVTLPFN(R2)
         LA    R6,0(R6,R3)         R6=A(LAST REAL PFTE)
         L     R7,ARGLIST
         L     R7,12(R7)      A(FRAME TABLE)
         LR    R8,R7
         AH    R8,=AL2(4096*2)
         LH    R0,=XL2'0202'  NUCLEUS OWNER INDEX AND FIX CODE
         DO    WHILE,(CLR,R3,R5,L)
               STH  R0,0(R7)
               AAI  R7,2
               AAI  R3,16
         OD
         DO    WHILE,(CLR,R5,R6,NH)
               ZR   R1
               ICM  R1,3,PFTVBN(R5)
               SRL  R1,3           R1= 2 * PAGE NUMBER (REL- TO 0)
               IC   R2,0(R1,R12)   A(PAGE TABLE ENTRY FOR THIS FRAME)
               STC  R2,0(R7)       SET OWNER INDEX IN FRAME TABLE
               IF   (TM,PFTFLAG1(R5),PFTFIXES,NZ) IS IT FIXED
                    MVI  1(R7),TFIX
               ELSE
                    MVI  1(R7),TPAGE
               FI
               AAI  R7,2
               LA   R1,0(R1,R12)
               LH   R0,0(R1)
               LCR  R0,R0     MAKE WORK NEGATIVE TO INDICATE REAL
               STH  R0,0(R1)
               AAI  R5,16
         OD
         LH    R0,=XL2'0101'  FREE AND PAGEABLE (NOT REAL)
         DO    WHILE,(CLR,R7,R8,L)
               STH  R0,0(R7)
               AAI  R7,2
         OD
         PEXIT
         TITLE 'NUC - SET STATUS OF NUCLEUS PAGES'
NUC      PENTRY
         INDEX 'NUCLEUS FIXED PAGES'
         L     R2,16
         L     R2,CVTPVTP(R2)      R2=PVT
         LR    R4,R12
         LH    R5,PVTFPFN(R2)      R5=OFFSET TO FIRST REAL PFTE
         ZR    R6                  R6=NVC PAGE PFT OFFSET(CURRENT)
         DO    WHILE,(CLR,R6,R5,L) FIX NUC PAGES NOT IN PFT
               STC  R9,0(R4)
               MVI  1(R4),TNUC
               LH   R0,0(R4)
               LCR  R0,R0
               STH  R0,0(R4)
               AAI  R4,2
               AAI  R6,16          INCR OFFSET XXX0
         OD
         PEXIT
         TITLE 'SQA - SET STATUS OF VIRTUAL SQA PAGES'
SQA      PENTRY
         INDEX 'SQA VIRTUAL FREE PAGES'
         L     R2,16               R2=CVT
         L     R2,CVTABEND(R2)     R2=SCVT
         L     R2,SCVTMSSQ(R2)     R2=A(GOVRFLB)
         L     R3,SQABOUND(R2)     R3=A(LOW END OF SQA)
         SRL   R3,11
         LA    R3,0(R3,R12)
         LR    R4,R12
         A     R4,=A(2*4096)
         DO    WHILE,(CLR,R3,R4,L)
               STC  R9,0(R3)
               MVI  1(R3),TSQA
               AAI  R3,2
         OD
         PEXIT
         TITLE 'LPA - SET STATUS OF VIRTUAL LINK PACK AREA'
LPA      PENTRY
         INDEX 'LPA VIRTUAL PAGES'
         L     R2,16               R2=CVT
         L     R3,CVTLPDIR(R2)     R3=A(LPA START)
         L     R2,CVTABEND(R2)     R2=SCVT
         L     R2,SCVTMSSQ(R2)     R2=GOVERFLB
         L     R4,SQABOUND(R2)     R4=A(LPA END)
         LA    R3,0(R3)
         SRL   R3,11
         LA    R3,0(R3,R12)
         SRL   R4,11
         LA    R4,0(R4,R12)
         DO    WHILE,(CLR,R3,R4,L)
               STC  R9,0(R3)
               MVI  1(R3),TLPA
               AAI  R3,2
         OD
         PEXIT
         TITLE 'TASKS   - SET STATUS OF VIRTUAL TASK PAGES'
TASKS    PENTRY
         L     R2,16
         L     R2,CVTHEAD(R2)      TOP TCB
         L     R1,TCBTIO(R2)
         DO    WHILE,(LTR,R1,R1,Z) SKIP DOWN TO MASTER SCHED. TCB
               L    R2,TCBTCB(R2)
               L    R1,TCBTIO(R2)
         OD
         LR    R3,R2
         PM    LSQA
         PM    REGION    MASTER SCH.
         LR    R8,R2     SAVE MOTHER TCB
         L     R2,TCBLTC(R2)       R2=A(LAST MS DAUGHTER TASK)
         DO    WHILE,(LTR,R2,R2,NZ)
               IF   (CLC,TCBPQE(4,R2),TCBPQE(R8),NE)   SKIP MS SUBTASKS
                    LR   R3,R2
                    PM   LSQA
                    PM   REGION
               FI
               L    R2,TCBNTC(R2)
         OD
         PEXIT
         TITLE 'LSQA - SET STATUS OF TASK LSAQ PAGES'
LSQA     PENTRY
         LR    R1,R3
         PM    GETJOB
         MVC   LSQAJOB,0(R1)
         INDEX LSQANAME
         L     R4,TCBLSQAP(R3)     LSQA SPQE
         L     R4,SPDQEAD(R4)      DQE
         L     R5,DQEBLKAD(R4)     LSQA START
         LR    R6,R5
         A     R6,DQELNTH(R4)      LSQA END
         SRL   R5,11
         LA    R5,0(R5,R12)
         SRL   R6,11
         LA    R6,0(R6,R12)
         DO    WHILE,(CLR,R5,R6,L)
               STC  R9,0(R5)
               MVI  1(R5),TLSQA
               AAI  R5,2
         OD
         PEXIT
         TITLE 'REGION  - SET STATUS OF TASK REGION PAGES'
REGION   PENTRY
         L     R4,TCBPQE(R3)       R4=DUMMY PQE
         DO    WHILE,(LTR,R4,R4,Z)
               L    R3,TCBLTC(R3)
               IF   (LTR,R3,R3,Z)
                    PEXIT
               FI
               L    R4,TCBPQE(R3)
         OD
         LR    R1,R3
         PM    GETJOB
         MVC   REGNJOB,0(R1)
         INDEX REGNNAME
         L     R4,PQEFPQE(R4)      R4=REGION PQE
         L     R5,PQEREGN(R4)      R5=START
         LR    R6,R5
         A     R6,PQESIZE(R4)      R6=END
         SRL   R5,11
         LA    R5,0(R5,R12)
         SRL   R6,11
         LA    R6,0(R6,R12)
         DO    WHILE,(CLR,R5,R6,L)
               STC  R9,0(R5)
               MVI  1(R5),TREGION
               AAI  R5,2
         OD
         PEXIT
         TITLE 'GET JOBNAME IN R1 GIVEN TCB IN R1'
GETJOB   PENTRY
         L     R14,TCBTIO(R1)
         IF    (LTR,R14,R14,Z)
               LA   R1,=CL8'SYSTEM'
               PEXIT
         FI
         IF    (CLC,0(8,R14),=CL8'INIT',EQ)
               L    R1,TCBLTC(R1)
               L    R14,TCBTIO(R1)
         FI
         IF    (CLC,0(7,R14),=CL7'HOSINIT',EQ),                        X
               AND,(CLC,8(8,R14),=CL8' ',NE)
               LA   R1,8(R14)
         ELSE
               LR   R1,R14
         FI
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
ARGLIST  DC    A(0)
MAXRESET DC    P'0'
SAVEPICA DC    A(0)
TIME     DS    0CL32
MMDDYY   DC    CL8' ',C'  '
TIMEDATE DC    CL22' '
TVIRTUAL EQU   1
TNUC     EQU   2
TSQA     EQU   3
TLPA     EQU   4
TLSQA    EQU   5
TREGION  EQU   6
*
*              FRAME CODES
*
TPAGE    EQU   1
TFIX     EQU   2
PFTFLAG1 EQU   8    PLM 812
PFTFIXES EQU   B'00110100' PLM 812  LSQA, LNGFX, OR BADPG
TCBPQE   EQU   X'98'     SDA  220
TCBNTC   EQU   X'80'     SDA  219
TCBLTC   EQU   X'88'     SDA  219
TCBTCB   EQU   X'74'     SDA  219
TCBTIO   EQU   X'0C'     SDA  216
CVTHEAD  EQU   X'A0'     SDA  15
CVTPVTP  EQU   X'0164'   SDA  18   PAGE VECTOR TABLE
PVTPFTP  EQU   X'58'     PLM  882  FRAME PAGE TABLE (APPARENT ORIGIN)
PVTFPFN  EQU   X'5C'     PLM  882  FIRST FPT ENTRY OFFSET (XXX0)
PVTLPFN  EQU   X'5E'     PLM  882  LAST  FPT ENTRY OFFSET (XXX0)
PFTVBN   EQU   X'02'     PLM  812 VIRTUAL PAGE OFFSET (XXX0)
CVTABEND EQU   X'C8'     SDA  16   SECONDARY CVT
SCVTMSSQ EQU   X'6C'     SDA  206  GOVRFLB (MAI STORAGE QUEVES)
SQABOUND EQU   X'00'     PLM  785  LOW END OF SQA
CVTLPDIR EQU   X'0168'   PLM  761  LPA DIRECTORY
REGNNAME DS    0CL40
         DC    C'REGION FOR JOB '
REGNJOB  DS    CL8' '
         FILL  REGNNAME
LSQANAME DS    0CL40
         DC    C'LSQA   FOR JOB '
LSQAJOB  DC    CL8' '
         FILL  LSQANAME
         DC    C' SPIE PSW SAVE'
PSW      DC    CL8' '
TCBLSQAP EQU   X'E0'     SDA  223
DQEBLKAD EQU   8         PLM  770
DQELNTH  EQU   12        PLM  770
SPDQEAD  EQU   4         PLM  863
PQEFPQE  EQU   8         PLM  818
PQESIZE  EQU   20        PLM  818
PQEREGN  EQU   24        PLM  818
         END
