*          DATA SET TO023000   AT LEVEL 006 AS OF 01/26/79
*          DATA SET TO023000   AT LEVEL 005 AS OF 10/10/78
PANG     TITLE 'PANGET -- TSO COMMAND PROCESSOR'
         MACRO
&LABEL   $MSG  &TEXT,&RC=NO,&DC=NO,&SFX=NO,&DSN=NO
         LCLA  &LNTH
         LCLB  &RCI,&DCI,&SFXI,&DSNI
         AIF   ('&RC' NE 'YES').NORC
&RCI     SETB  1
.NORC    ANOP
         AIF   ('&DC' NE 'YES').NODC
&DCI     SETB  1
.NODC    ANOP
         AIF   ('&SFX' NE 'YES').NOSFX
&SFXI    SETB  1
.NOSFX   ANOP
         AIF   ('&DSN' NE 'YES').NODSN
&DSNI    SETB  1
.NODSN   ANOP
&LNTH    SETA  K'&TEXT-3
&LABEL   DC    H'&LNTH',B'&RCI&DCI&SFXI&DSNI.0000',C&TEXT
         MEND
         SPACE 2
         MACRO
         CPM   &PROC
         IF    (CLI,ERR,0,EQ)
               PM    &PROC
         FI
         MEND
         SPACE 2
         MACRO
&L       CALLPAM  &EP,&PARMS
         LCLC  &OFFSET
         AIF   ('&EP' NE 'POPEN').NPOPEN
&OFFSET  SETC  '0'
.NPOPEN  AIF   ('&EP' NE 'PSRCH').NPSRCH
&OFFSET  SETC  '8'
.NPSRCH  AIF   ('&EP' NE 'PREAD').NPREAD
&OFFSET  SETC  '16'
.NPREAD  AIF   ('&EP' NE 'PCLOSE').NPCLOSE
&OFFSET  SETC  '24'
.NPCLOSE ANOP
&L       L     15,PAMEP
         LA    15,&OFFSET.(,15)
         CALL  (15),&PARMS,VL,MF=(E,CALLMFL)
         MEND
         EJECT
         TITLE 'PANPUT TSO PAN INTERFACE'
*--------------------------------------------------------------------
*
*  INSTALLATION  FLORIDA POWER CORPORATION
*
*  PROGRAM       TO023000 (PANGET,PANG)
*
*  AUTHOR        FRED D BRISARD
*
*  DATE          1976
*
*  COMMENTS      THIS PROGRAM WILL RETRIEVE ANY FILE FROM ANY PANLIB
*                AND WRITE IT TO A PHYS SEQ DATA SET.  THE DSN SUFFIX
*                WILL BE DETERMINED BY THE PAN DATA TYPE AS INDICATED
*                IN THE TABLE FOUND IN THE COPYINIT ROUTINE OF THIS
*                PROGRAM.  COMMAND FOMAT IS AS FOLLOWS:
*
*                    PANGET NAME
*                    PANG   NAME
*
*                OPERANDS ARE AS FOLLOWS:
*
*                    LIB('LIB')    (PAN LIBRARY, DEFAULT IS FPC.WORK)
*                    DA(DSN)       ANY TSO FILE - SEE HELP DOCUMENT
*
*
*   REV  08/10/78   RHJONES
*   1.  ++INCLUDE RETURNS RETCODE 4 PER SPECS
*       MOD TO CONTINUE PROCESSING AS IN BATCH
*
*   REV  10/10/78  W R WILLITS  ID=BRW001
*   1.  ADD VOL(TSOSV1) TO DAIR CONTROL BLOCK.  THIS FIX SHOULD
*       BE TEMPORARY DUE TO PCF BUG ALLOCATING NEW DATA SETS TO
*       VOLUMES OTHER THAN PCFVOL USERVOL.
*
*  REV  01/26/79  W R WILLITS
*  1.  ADD COB72 TO SUFXREF TABLE
*
*
*--------------------------------------------------------------------
         SPACE 2
         PUNCH ' ALIAS PANGET &SYSDATE &SYSTIME IGNORE IEW0731'
         PUNCH ' ALIAS PANG   &SYSDATE &SYSTIME IGNORE IEW0731'
         EJECT
PANGET   SUBENTRY BASES=(12,11),RENT=YES,RWA=WORK,RWALNG=WORKL,        X
               STACK=20
         SPACE 3
         PRINT NOGEN
         LR    R10,R1              ESTABLISH CPPL
         USING CPPL,R10              ADDRESSIBILITY
         USING PDL,R9              PDL ADDRESSIBLITY
         SPACE 3
         MVI   ERR,0               INIT ERR INDICATOR
         MVI   STATUS,0            INIT STATUS INDICATOR
         SPACE 3
         PM    PARSE               PERFORM PARSE OF OPERANDS
         SPACE 2
         CPM   STAXINIT            CONDITIONALLY INIT ATTN EXIT
         SPACE 1
         LOAD  EPLOC=PAM           LOAD PAM MODULE INTO STORAGE
         ST    R0,PAMEP            SAVE ENTRY POINT ADDRESS
         SPACE 1
         CPM   PANINIT             CONDITIONALLY INIT PAM
         CPM   DSINIT              CONDITIONALLY INIT RECEIVING DATASET
         CPM   COPY                CONDITIONALLY COPY PAN TO DATASET
         PM    DSCLOSE             CLOSE AND DEALLOCATE RECEIVING DS
         SPACE 2
         PM    PANCLOSE            CLOSE AND DEALLOCATE PAN LIBRARY
         SPACE 1
         DELETE  EPLOC=PAM         DELETE PAM MODULE
         SR    R0,R0               SHOW PAM DELETED
         ST    R0,PAMEP
         SPACE 2
         PM    STAXTERM            TERMINATE ATTN EXIT
         SPACE 5
         IKJRLSA ANSWER            RELEASE PDL
         SPACE 3
         SUBEXIT RENT=YES,RC=0
         SPACE 2
PAM      DC    CL8'PAM'
         EJECT
*
*  PARSE ROUTINE
*
         SPACE 2
PARSE    PENTRY
         SPACE 2
         LA    R1,PARSEPL          ESTABLISH PPL
         USING PPL,R1                ADDRESSIBILITY
         SPACE 2
         L     R0,CPPLUPT          INITIALIZE THE PARSE
         ST    R0,PPLUPT             PARAMETER LIST
         L     R0,CPPLECT
         ST    R0,PPLECT
         LA    R0,ECB
         ST    R0,PPLECB
         L     R0,=V(PCL)
         ST    R0,PPLPCL
         LA    R0,ANSWER
         ST    R0,PPLANS
         L     R0,CPPLCBUF
         ST    R0,PPLCBUF
         SR    R0,R0
         ST    R0,PPLUWA
         ST    R0,ECB
         ST    R0,ANSWER
         SPACE 1
         DROP  R1                  DROP PPL
         SPACE 2
         CALLTSSR EP=IKJPARS,MF=(E,(1))  INVOKE PARSE
         IF    (LTR,R15,R15,NZ)    TEST FOR BAD RETURN CODE
               LA    R1,PARSEMSG
               PM    TERMPUT       OUTPUT ERROR MESSAGE
               OI    ERR,PARSERR   INDICATE PARSE ERROR
         FI
         L     R9,ANSWER           -->  PDL
         SPACE 2
         PEXIT
         SPACE 2
PARSEMSG $MSG  'PARSE ERROR CODE:',RC=YES,SFX=YES
         EJECT
*
*  ATTENTION EXIT INITIALIZATION ROUTINE
*
         SPACE 2
STAXINIT PENTRY
         SPACE 1
         MVC   STAXMFL(STAXREFL),STAXREF   INIT STAX LIST
         LA    R2,ERR              POINT TO ERROR INDICATOR
         STAX  STAXEXIT,USADDR=(2),MF=(E,STAXMFL)  ISSUE STAX
         IF    (LTR,R15,R15,Z)     TEST RETURN CODE
               OI    STATUS,STAXACTV   INDICATE STAX ACTIVE
         ELSE
               LA    R1,STAXERRM   STAX ERROR MESSAGE
               PM    TERMOUT
               OI    ERR,TSOCP     SET TSO CTRL PGM ERROR
         FI
         SPACE 2
         PEXIT
         SPACE 2
STAXREF  STAX  STAXEXIT,MF=L
STAXREFL EQU   *-STAXREF           LENGTH OF STAX LIST FORM
         SPACE 1
STAXERRM $MSG  'ERROR INITIATING ATTN EXIT..RC:',RC=YES,SFX=YES
         EJECT
*
*  PANVALET LIBRARY INITIALIZATION ROUTINE
*
         SPACE 2
PANINIT  PENTRY
         SPACE 2
         IF    (TM,PDLLIB+6,X'80',O)   TEST FOR LIBRARY SPECIFIED
               LA    R1,PDLLIB      LOAD PARSE PDL IF SO
         ELSE
               LA    R1,PANLIBDF   ELSE USE DEFAULT DUMMY PDL
         FI
         SPACE 2
         PM    ALLOCSHR            ALLOCATE THE PANLIB
         SPACE 1
         SPACE 1
         IF    (LTR,R15,R15,NZ)    TEST FOR BAD RETURN CODE
               PM    DAIRRC        CHECK FOR SUCCESS
               OI    ERR,PANERR    INDICATE PAN ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         SPACE 2
         USING DAPB08,R1           DAPB08 ADDRESSIBILITY
         OI    STATUS,PANALC       INDICATE PANLIB ALLOCATED
         MVC   PANDDNM,DA08DDN     SAVE DDNAME
         SPACE 2
         IF    (TM,DA08DSO,X'20',Z) IF DS ORGANIZATION NOT DA
               DROP  R1            DROP DAPB ADDRESSIBILITY
               LA    R1,PANERR24   ISSUE ERROR MSG IF SO
               PM    TERMPUT
               OI    ERR,PANERR    FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         SPACE 2
         SR    R0,R0               CLEAR
         ST    R0,PANACTN          INIT PAM FIELDS FOR A POPEN
         MVC   PANBKUP,NOENTRY
         CALLPAM POPEN,(PANACTN,PANDDNM,PANBKUP)
         SPACE 2
         IF    (CLI,PANACTN3,0,NE) IF PAM ERROR OCCURED
               PM    PANANAL       ANALYZE PAN RETURN
               OI    ERR,PANERR    INDICATE PAN ERROR
               IF    (CLI,PANACTN3,24,EQ)  IF BAD LIBRARY FORMAT
                     OI    STATUS,PANOPN   SHOW PAN LIB OPEN
               FI
               PEXIT
         FI
         OI    STATUS,PANOPN       SHOW PAN LIB OPEN
         SPACE 2
         SR    R0,R0               CLEAR
         ST    R0,PANACTN          INIT PAM FIELDS FOR A PSRCH
         MVC   PANDIR,BLANKS
         MVC   PANNAME1,BLANKS
         L     R1,PDLPANDS
         LH    R2,PDLPANDS+4
         BCTR  R2,0
         EX    R2,MVCPANDS
         MVC   PANNAME2,NOENTRY
         MVC   PANCMNT,NOENTRY
         MVC   PANSUBST,NOENTRY
         SPACE 2
         CALLPAM  PSRCH,                                               X
               (PANACTN,PANDIR,PANNAME1,PANNAME2,PANCMNT,PANSUBST)
         SPACE 1
         IF    (CLI,PANACTN3,0,NE) TEST PAN ACTION CODE
               PM    PANANAL       ANALYZE RETURN CODE
               OI   ERR,PANERR     SET ERROR INDICATOR
               PEXIT
         FI
         PEXIT
         SPACE 2
MVCPANDS MVC   PANNAME1(*-*),0(R1)
         EJECT
*
*  RECEIVING DATA SET ALLOCATION AND INITIALIZATION
*
         SPACE 2
DSINIT   PENTRY
         SPACE 2
         IF    (TM,PDLDS+6,X'80',Z)    TEST FOR DSN SPECIFIED
               MVC   DSDFLT,BLANKS     INIT FIELD
               MVC   DSDFLT(8),PANDIR  BUILD DEFAULT DATASET NAME
               LA    R1,DSDFLT+7   FROM PAN DSNAME + PAN SUFFIX
               DO    WHILE,(CLI,0(R1),C' ',EQ)
                     BCTR  R1,0
               OD
               LA    R2,SUFXREF    SUFFIX TABLE
               DO    WHILE,(CLI,0(R2),255,NE)
                     IF    (CLC,0(5,R2),PANDIR+18,EQ)
                           MVI   1(R1),C'.'
                           MVC   2(5,R1),5(R2)
                     FI
                     LA    R2,10(,R2)  NEXT ENTRY IN SUFFIX TABLE
               OD
               LA    R1,DSDFLT+L'DSDFLT  DETERMINE LENGTH OF
*                                      GENERATED DATA SET NAME
               DO    UNTIL,(CLI,0(R1),C' ',NE)
                     BCTR  R1,0
               OD
         SPACE 2
               LA    R0,DSDFLT     DETERMINE LENGTH OF DSNAME
               SR    R1,R0
               LA    R1,1(,R1)
               STH   R1,PDLDS+4    SAVE LENGTH IN PDL
               ST    R0,PDLDS      DATA SET NAME
               OI    PDLDS+6,X'80' FLAG PDL AS ACTIVE
         FI
         SPACE 2
         L     R1,PDLDS            DSNAME ADDR
         IF    (CLI,0(R1),C'*',EQ) TEST FOR TERMINAL OUTPUT
               OI    STATUS,TERMOUT  SET TERM OUTPUT INDICATOR
               MVC   PANRECD,BLANKS
               MVC   PANRECD(L'PANNAME1),PANNAME1
               PEXIT
         FI
         SPACE 2
         PACK  DWORK,PANDIR+47(8)  NUMBER OF RECORDS
         CVB   R0,DWORK            PASS TO ALLOCNEW
         LA    R1,PDLDS            PDL FOR RECEIVING DATA SET
         PM    ALLOCNEW            TRY TO ALLOCATE THE DATA SET NEW
         IF    (LTR,R15,R15,NZ)    IF FAIL TRY SHARE
               LA                  R1,PDLDS
               PM    ALLOCSHR
               IF    (LTR,R15,R15,NZ)  IF ERROR
                     PM   DAIRRC   ANALYZE RET CODE
                     OI    ERR,DSERR   SET ERROR INDICATOR
                     PEXIT         EXIT IMMEDIATELY
               FI
               USING DAPB08,R1
               IF    (TM,DA08DSO,X'20',O)   IF DSORG IS DIRECT
                     MVC   DSDDNM,DA08DDN   SAVE DDNAME
                     DROP  R1
                     LA    R1,DSDDNM
                     PM    UNALLOC          DE-ALLOCATE THE DATASET
                     LA    R1,DSORGMSG      ISSUE ERROR MESSAGE
                     PM    TERMPUT
                     OI    ERR,DSERR        FLAG ERROR
                     PEXIT                  EXIT IMMEDIATELY
               FI
               USING DAPB08,R1
               IF    (TM,DA08DSO,X'02',O),AND,(TM,PDLDS+14,X'80',Z)
*              IF DSORG IS PO AND NO MEMBER SPECIFIED
                     MVC   DSDDNM,DA08DDN   SAVE DDNAME
                     DROP  R1
                     LA    R1,DSDDNM
                     PM    UNALLOC     DE-ALLOCATE THE DATASET
                     L     R2,PDLDS    DATASET NAME
                     IF    (CLC,=C'FPC.PROCPDS',0(R2),EQ)
*              IF FPC.PROCPDS IS OUTPUT DATASET
                           MVI   PANDIR+7,C'T'  FORCE TO TEST
                     FI
                     LA    R1,PANDIR   REALLOCATE WITH MEMBER
                     ST    R1,PDLDS+8
                     LA    R1,8
                     STH   R1,PDLDS+12
                     OI    PDLDS+14,X'80'
                     LA    R1,PDLDS
                     PM    ALLOCSHR    REALLOCATE WITH MEMBER
                     IF    (LTR,R15,R15,NZ)  CHECK RETURN CODE
                           PM    DAIRRC      EXIT IF BAD
                           PEXIT
                     FI
                FI
         FI
         SPACE 2
         OI    STATUS,DSALC        INDICATE TSO DS ALLOCATED
         MVC   DSDDNM,DAIRWORK+DA08DDN-DAPB08  SAVE DDNAME
         SPACE 2
         MVC   DSDCB(DSDCBL),DSDCBMFL MOVE MODEL DCB TO WORK AREA
         MVC   DSDCB+40(8),DSDDNM  SET DDNAMEIN DCB
         LA    R1,DSDCB            SET UP FOR OPEN
         O     R1,DSDCBOPN
         ST    R1,DSDCBOC
         OPEN  MF=(E,DSDCBOC)      TRY TO OPEN IT
         IF    (TM,DSDCB+48,X'10',Z)
               LA    R1,DSOPENM    OPEN ERROR MESSAGE
               LA    R0,PDLDS      DSNAME PDL ADDR
               PM    TERMPUT
               OI    ERR,DSERR     FLAG ERROR
               PEXIT               EXIT IMMEDIATELY
         FI
         SPACE 2
         OI    STATUS,DSOPN        INDICATE TSO DATA SET OPEN
         SPACE 2
         MVC   PANRECD,PANUPDMF    BUILD INITIAL RECORD
         MVC   PANRECD+9(10),PANDIR+65 OF ++UPDATE MODULE,LEVEL,ALL
         MVC   PANRECD+20(3),PANDIR+10 FORM TO BE USED
         OI    PANRECD+22,C'0'     FOR REINPUT TO PAN LATER
*        THE PUT WILL BE DONE IN THE COPY ROUTINE
         SPACE 2
         PEXIT
         SPACE 2
*  ---------------------------------
*  ×   PAN TYPE   ×   DSN SUFFIX   ×
*  ---------------------------------
SUFXREF  DS    0F
         DC    C'ASMB ASM  '
         DC    C'COBOLCOBOL'
         DC    C'ANSCBCOBOL'
         DC    C'FORT FORT '
         DC    C'PL/1 PL1  '
         DC    C'JCL  CNTL '
         DC    C'DATA DATA '
         DC    C'COB72DATA '
         DC    X'FF'               END OF TABLE
         SPACE 2
DSOPENM  $MSG  'DATASET OPEN FAILED FOR',DSN=YES,SFX=YES
DSORGMSG $MSG  'INVALID OUTPUT DATASET ORGANIZATION'
         EJECT
*
*  COPY PAN DATA SET TO TSO DATA SET ROUTINE
*
         SPACE 2
COPY     PENTRY
         SPACE 2
         IF    (CLI,PDLNUMKY+1,1,EQ)   TEST FOR NUM KEYWORD
               L     R1,PDLINC     GET INCREMENT VALUE
               LH    R2,PDLINC+4
               BCTR  R2,0
               EX    R2,PACKINC    PLACE INTO WORK AREA
               ZAP   LINECRNT,=P'0'    RESET CURRENT LINE #
         ELSE
               ZAP   LINEINCR,=P'0'    DISABLE LINE COUNT OPTION
         FI
         SPACE 2
         DO    UNTIL,(CLC,=C'$* ',PANRECD,EQ)  LOOP UNTIL PAN EOF
               IF    (CP,LINEINCR,=P'0',NE)  TEST FOR LINE # OPTION
                     AP    LINECRNT,LINEINCR
                     UNPK  PANRECD+72(8),LINECRNT
                     OI    PANRECD+79,C'0'
               FI
         SPACE 1
               IF    (TM,ERR,ATTN,O)   TEST FOR ATTN
                     PEXIT         EXIT FROM COPY IF ATTN
               FI
         SPACE 2
               IF    (TM,STATUS,TERMOUT,O)
                     LA    R0,78   USE 78 BYTE FOR 3270
                     LA    R1,PANRECD
                     TPUT  (1),(0),R
               ELSE
                     IF    (CLC,=C'++UPDATE ',PANRECD,NE)
                           PUT   DSDCB,PANRECD
                     FI
               FI
         SPACE 2
               SR    R0,R0         SET UP FOR PAM PREAD
               ST    R0,PANACTN
               IF    (CLI,PDLEXPKY+1,1,EQ) TEST FOR EXPAND
                     MVC   PANINCLD,YES
               ELSE
                     MVC   PANINCLD,NOENTRY
               FI
               MVC   PANCMNT,NOENTRY
               CALLPAM  PREAD,                                         X
               (PANACTN,PANRECD,PANNAME1,PANINCLD,PANCMNT)
               IF    (CLI,PANACTN3,0,NE),AND,(CLI,PANACTN3,4,NE)
                     PM    PANANAL CHECK FOR POSSIBLE ERROR
                     OI    ERR,PANERR  FLAG ERROR
                     PEXIT         EXIT IMMEDIATELY
               FI
         OD
         PEXIT
         SPACE 2
PACKINC  PACK  LINEINCR,0(*-*,R1)
         EJECT
*
*  TSO DATA SET CLOSE AND DEALLOCATION ROUTINE
*
         SPACE 2
DSCLOSE  PENTRY
         SPACE 2
         IF    (TM,STATUS,DSOPN,O)
               LA    R1,DSDCB      SET UP TO CLOSE DATASET
               O     R1,DSDCBCLS
               ST    R1,DSDCBOC
               CLOSE MF=(E,DSDCBOC)
         FI
         SPACE 2
         IF    (TM,STATUS,DSALC,O) DEALLOCATE DATA SET IF NECESSARY
               LA    R1,DSDDNM
               PM    UNALLOC
               PM    DAIRRC
         FI
         SPACE 2
         PEXIT                     EXIT
         EJECT
*
*  PAN LIBRARY CLOSE ROUTINE
*
         SPACE 2
PANCLOSE PENTRY
         SPACE 2
         IF    (TM,STATUS,PANOPN,O)
               SR    R0,R0         SET UP FOR PAM PCLOSE
               ST    R0,PANACTN
               CALLPAM PCLOSE,(PANACTN)
         FI
         SPACE 2
         IF    (TM,STATUS,PANALC,O)
               LA    R1,PANDDNM    DE-ALLOCATE PANVALET LIBRAY
               PM    UNALLOC
               PM    DAIRRC        PROCESS POSSIBLE DAIR ERROR
         FI
         DELETE EP=PAM             DELETE PAM LOAD MODULE
         SPACE 2
         PEXIT
         EJECT
*
*  ATTENTION EXIT TERMINATION
*
         SPACE 2
STAXTERM PENTRY
         SPACE 1
         IF    (TM,STATUS,STAXACTV,O)
               STAX
         FI
         PEXIT
         EJECT
*
*  GENERAL TERMINAL MESSAGE PUT ROUTINE
*
         SPACE 2
TERMPUT  PENTRY
         SPACE 2
         LH    R14,0(,R1)          LENGTH OF MESSAGE- 1
         EX    R14,MVCTMSG         MOVE MODEL TO WORK AREA
         SPACE 1
         LA    R14,TERMSG+1(R14)   END OF MESSAGE
         IF    (TM,2(R1),X'80',O)
               N     R15,TERMMASK  MASK WITH 0000FFFF
               CVD   R15,DWORK     PLACE RC IN MESSAGE
               MVC   0(6,R14),=X'402020202120' EDIT RC INTO
               ED    0(6,R14),DWORK+5  MESSAGE TEXT
               LA    R14,6(,R14)   END OF MODIFIED MSG
         FI
         SPACE 1
         IF    (TM,2(R1),X'40',O)  TEST FOR DAIR RET CODE
               UNPK  0(5,R14),0(3,R15)
               TR    0(4,R14),TMSGTR-240   TRANSLATE TO HEX
               LA    R14,4(,R14)
         FI
         SPACE 1
         IF    (TM,2(R1),X'20',O)  TEST FOR SUFFIX
               MVC   0(L'MSGSFX,R14),MSGSFX
               LA    R14,L'MSGSFX(,R14)
         FI
         SPACE 2
         IF    (TM,2(R1),X'10',O)  TEST FOR DSNAME PRINT
               STM   R2,R5,TERMSAVE SAVE REGISTERS TEMPORARILY
               LR    R2,R0         USE ADDRESSIBLE REG
               IF    (TM,6(R2),X'80',O)    TEST FOR DSNAME PRESENT
                     LH    R3,4(,R2)   GET LENGTH
                     BCTR  R3,0    DECREMENT FOR EX
                     L     R4,0(,R2)   DSNAME ADDR
                     EX    R3,MVCDSNMG MOVE DSNAME TO MESSAGE
                     LA    R14,1(R3,R14)   ADDR OF END OF MSG
               FI
               IF    (TM,14(R2),X'80',O)   TEST FOR MEMBER PRESENT
                     LH    R3,12(,R2)  GET LENGTH
                     BCTR  R3,0    DECREMENT FOR EX
                     L     R4,8(,R2)   MEMBER NAME ADDRESS
                     EX    R3,MVCMBRMG MOVE MEMBER TO MESSAGE
                     MVI   0(R14),C'('
                     LA    R14,2(R3,R14)   NEW END OF MESSAGE
                     MVI   0(R14),C')'
                     LA    R14,1(,R14)
               FI
               LM    R2,R5,TERMSAVE
         FI
         SPACE 2
         LR    R0,R14
         LA    R1,TERMSG
         SR    R0,R1               LENGTH OF MESSAGE
         LA    R1,TERMSG           START OF MESSAGE
         SPACE 1
         TPUT  (1),(0),R
         SPACE 1
         PEXIT
         SPACE 2
MSGSFX   DC    C'...NOTIFY CSD TECH SUPPORT'
TERMMASK DC    0F'0',X'0000FFFF'
*
MVCTMSG  MVC   TERMSG(*-*),3(R1)
MVCDSNMG MVC   0(*-*,R14),0(R4)
MVCMBRMG MVC   1(*-*,R14),0(R4)
         SPACE 1
TMSGTR   DC    0F'0',C'0123456789ABCDEF'
         EJECT
*
*  SHARED DISPOSITION DATA SET ALLOCATION ROUTINE
*
         SPACE 2
ALLOCSHR PENTRY
         SPACE 2
         MVC   DAIRWORK(DA08MDLL),DA08MDL MOVE MODEL TO WORK
         LA    R8,DAIRWORK         ESTABLISH DA08
         USING DA08CD,R8             ADDRESSIBILITY
         MVC   DA08DSP1(3),=X'080808'  DISP=(,CATLG,DELETE)
         IF    (TM,6(R1),X'40',Z)
               OI    DA08CTL,X'20' PREFIX WITH USERID
         FI
         IF    (TM,14(R1),X'80',O) TEST FOR MEMBER NAME
               LH    R2,12(,R1)    MOVE MEMBER NAME TO DA08
               BCTR  R2,0
               L     R3,8(,R1)
               EX    R2,MVCMBRNM
         FI
         SPACE 1
         LH    R2,4(,R1)           DSNAME LENGTH
         L     R3,0(,R1)           --> DSN
         STH   R2,DSNWORKL         SET IN WORK AREA
         BCTR  R2,0                DEC FOR EX
         EX    R2,MVCDSN           MOVE DSN TO WORK AREA
         LA    R1,DSNWORK          SET DSN POINTER IN DAPB
         ST    R1,DA08PDSN
         LR    R1,R8
         PM    DAIR                PERFORM COMMON DAIR PROCESSING
         SPACE 2
         LR    R1,R8               RETURN ADDRESS OF DAPB
         DROP  R8                  DROP DAPB ADDRESSIBILITY
         SPACE 2
         PEXIT
         EJECT
*
*   NEW DISPOSITION DATA SET ALLOCATION ROUTINE
*
         SPACE 2
ALLOCNEW PENTRY
         SPACE 2
         MVC   DAIRWORK(DA08MDLL),DA08MDL MOVE DA08 MODEL TO WORK
         LA    R8,DAIRWORK         AND PROVIDE
         USING DA08CD,R8           ADDRESSIBILITY
         SPACE 2
         MVC   DA08BLK,=F'4240'    INITIALIZE BLOCK
         IF    (LTR,R3,R0,Z)       TEST FOR SPACE ESTIMATE
               MVC   DA08PQTY,=F'30'   USE (4240,(30,30))
               MVC   DA08SQTY,=F'30'
         ELSE
               SR    R2,R2         CLEAR FOR DIVIDE
               D     R2,=F'53'     DIVIDE BY RECORDS PER BLOCK
               LA    R3,1(,R3)     ROUND UP
               ST    R3,DA08PQTY   USE AS PRIMARY QUANITY
               SR    R2,R2
               D     R2,=F'10'     USE 10% OF PRIMARY AS SECONDARY
               LA    R3,1(,R3)      ROUND UP
               ST    R3,DA08SQTY    USE AS SECONDARY QUAN
         FI
         SPACE 1
         MVC   DA08SER,=CL8'TSOSV1'      VOL=SER=TSOSV1         BRW001
         MVC   DA08DSP1(4),=X'04020450'  (NEW,CATLG,DELETE)   RLSE
         IF    (TM,6(R1),X'40',Z)  TEST FOR QUOTES
               OI   DA08CTL,X'20'  REQ USERID PREFIX
         FI
         IF    (TM,14(R1),X'80',O) TEST FOR MEMBER
               LH    R2,12(,R1)    MOVE MEMBER NAME TO DAIR BLOCK
               BCTR  R2,0
               EX    R2,MVCMBRNM
               MVC   DA08DQTY,=F'10'   SET DIRECTORY SPACE
         FI
         LH    R2,4(,R1)           MOVE DSNAME TO DAIR BLOCK
         L     R3,0(,R1)
         STH   R2,DSNWORKL
         BCTR  R2,0
         EX    R2,MVCDSN
         LA    R2,DSNWORK          SET DSN ADDRESS IN DAIR BLOCK
         ST    R2,DA08PDSN
         SPACE 2
         LR    R1,R8
         PM    DAIR                TO COMMON DAIR ROUTINE
         SPACE 2
         LR    R1,R8               RETURN ADDRESS OF DAPB
         DROP  R8                  DROP DA08
         SPACE 1
         PEXIT
MVCDSN   MVC   DSNWORKC(*-*),0(R3)
         USING DAPB08,R8
MVCMBRNM MVC   DA08MNM(*-*),0(R3)
         DROP  R8
         EJECT
*
*  DE-ALLOCATION ROUTINE
         SPACE 2
UNALLOC  PENTRY
         SPACE 2
         MVC   DAIRWORK(DA18MDLL),DA18MDL  INIT DAPB WITH MODEL
         MVC   DAIRWORK+DA18DDN-DAPB18(8),0(R1)   SET DDNAME
         LA    R1,DAIRWORK
         PM    DAIR                PERFORM DAIR FUNCTION
         SPACE 2
         LA    R1,DAIRWORK         RETURN ADDRESS OF DAPB
         SPACE 2
         PEXIT
         EJECT
*
*  DAIR RETURN CODE ANALYSIS ROUTINE
*
         SPACE 2
DAIRRC   PENTRY
         SPACE 2
         IF    (LTR,R15,R15,Z)     TEST FOR FALSE ENTRY
               PEXIT               EXIT IF SO
         ELSEIF  (CH,R15,=H'16',EQ)    TEST FOR NO TIOT ROOM
               LA    R1,DRC16      ERROR MSG
         ELSEIF  (CH,R15,=H'24',EQ)    TEST FOR CONCAT GROUP
               LA    R1,DRC24      ERROR MSG
         ELSEIF  (CH,R15,=H'8',EQ),OR,(CH,R15,=H'12',EQ)
               LA    R1,DRC8       ERROR MSG
               LA    R15,DAIRWORK+4    DAIR ERROR CODE ADDR
         ELSE
               LA    R1,DRCELSE    ALL ELSE MESSAGE
         FI
         PM    TERMPUT             OUTPUT MESSAGE
         PEXIT
         SPACE 2
         PEXIT
         SPACE 2
DRC8     $MSG  'DAIR ERROR CODE:',DC=YES
DRC16    $MSG  'NO ALLOCATION ROOM..DO A FREEALL AND TRY AGAIN'
DRC24    $MSG  'DSNAME IS CONCATENATED..DO A FREEALL AND TRY AGAIN'
DRCELSE  $MSG  'DAIR RETURN CODE:',RC=YES,SFX=YES
         EJECT
*
*  PAM RETURN CODE ANALYSIS ROUTINE
*
         SPACE 2
PANANAL  PENTRY
         SPACE 2
         IF    (CLI,PANACTN3,0,EQ)
               PEXIT               EXIT IF NO ERROR
         ELSEIF  (CLI,PANACTN3,23,EQ)
               LA    R1,PANERR23   FLAG PAN DATA SET NOT FOUND
         ELSEIF  (CLI,PANACTN3,24,EQ)  IF BAD PAN LIB FORMAT
               LA    R1,PANERR24   BAD PAN LIB ERROR MSG
         ELSEIF  (CLI,PANACTN3,245,EQ)
               LA    R1,PANERRF5   FLAG PARAMETER LIST ERROR
         ELSE
               LH    R15,PANACTN+2 FORMAT ALL OTHER ERROR MSGS
               LA    R1,PANERRM           WITH RETURN CODE IN PANACTN
         FI
         SPACE 2
         PM    TERMPUT
         SPACE 2
         PEXIT
         SPACE 2
PANERR23 $MSG  'PAN DSNAME SPECIFIED NOT IN LIBRARY'
PANERR24 $MSG  'LIBRARY NAME SPECIFIED IS NOT A PANVALET LIBRARY'
PANERRF5 $MSG  'PAM PARM ERROR',SFX=YES
PANERRM  $MSG  'PANVALET ERROR CODE  PV',RC=YES,SFX=YES
         EJECT
*
*  COMMON DAIR INTERFACE ROUTINE
*
         SPACE 2
DAIR     PENTRY
         SPACE 2
         LA    R7,DAIRPL           ESTABLISH DAPL
         USING DAPL,R7               ADDRESSIBILITY
         SPACE 1
         ST    R1,DAPLDAPB         INITITALIZE THE DYNAMIC ALLOCATION
         L     R0,CPPLUPT          PARAMETER LIST
         ST    R0,DAPLUPT
         L     R0,CPPLECT
         ST    R0,DAPLECT
         LA    R0,ECB
         ST    R0,DAPLECB
         L     R0,CPPLPSCB
         ST    R0,DAPLPSCB
         SR    R0,R0
         ST    R0,ECB              RESET THE ECB
         SPACE 2
         LR    R1,R7
         CALLTSSR  EP=IKJDAIR,MF=(E,(1))
         DROP R7
         SPACE 2
         PEXIT
         EJECT
*
*  ATTENTION EXIT ROUTINE
*
         SPACE 2
STAXEXIT DS    0H
         L     R2,8(,R1)           ERROR INDICATOR ADDRESS
         OI    0(R2),ATTN          FLAG ATTENTION EXIT
         BR    R14                 RETURN TO PROCESS
         TITLE 'CONSTANTS AND LITERALS'
DSDCBMFL DCB   MACRF=(PM),DSORG=PS,DDNAME=DSDCB,RECFM=FB,LRECL=80
DSDCBL   EQU   *-DSDCBMFL
DSDCBOPN OPEN  (0,OUTPUT),MF=L
DSDCBCLS CLOSE 0,MF=L
         SPACE 3
         PRINT GEN
DA08MDL  DC    0F'0',X'0008',3X'0000',A(0),3CL8' ',4F'0'
         DC    2CL8' ',4X'00',XL3'00',X'00',CL8' '
DA08MDLL EQU   *-DA08MDL
         SPACE 2
DA18MDL  DC   0F'0',X'0018',3X'0000',A(0),2CL8' ',CL2' ',X'0810',CL8' '
DA18MDLL EQU   *-DA18MDL
         SPACE 2
PANLIBDF DC    A(DFLTLIB),AL2(L'DFLTLIB),X'C0',X'00'
         DC    A(0),AL2(0),X'00',X'00'
DFLTLIB  DC    C'FPC.WORK'
         SPACE 2
YES      DC    CL8'YES'
NOENTRY  DC    C'NO-ENTRY'
BLANKS   DC    CL80' '
PANUPDMF DC    CL80'++UPDATE XXXXXXXXXX,NNN,ALL'
         EQUAL
         EJECT
*
*  PARSE CONTROL LIST
*
         SPACE 2
PCL      IKJPARM DSECT=PDL
         SPACE 2
PDLPANDS IKJIDENT 'PAN DATA-SET NAME',MAXLNTH=10,OTHER=ALPHANUM,       X
               PROMPT='PANVALET DATA-SET NAME'
         SPACE 2
PDLLIBKY IKJKEYWD
         IKJNAME 'LIBRARY',SUBFLD=PDLLIBSF
         SPACE 2
PDLDSKY  IKJKEYWD
         IKJNAME 'DATASET',SUBFLD=PDLDSSF
         SPACE 2
PDLEXPKY IKJKEYWD
         IKJNAME 'EXPAND'
         IKJNAME 'NOEXPAND'
         SPACE 2
PDLNUMKY IKJKEYWD
         IKJNAME 'NUMBER'
         IKJNAME 'NONUMBER'
         SPACE 2
PDLINCKY IKJKEYWD  DEFAULT='INCREMENT'
         IKJNAME 'INCREMENT',SUBFLD=PDLINCSF
         SPACE 2
PDLLIBSF IKJSUBF
PDLLIB   IKJPOSIT DSNAME,PROMPT='PANVALET LIBRARY NAME'
         SPACE 2
PDLDSSF  IKJSUBF
PDLDS    IKJPOSIT DSTHING,                                             X
               PROMPT='NAME OF DATA-SET TO RECEIVE PAN DATA-SET'
         SPACE 2
PDLINCSF IKJSUBF
PDLINC   IKJIDENT 'INCREMENT VALUE',MAXLNTH=5,FIRST=NUMERIC,           X
               OTHER=NUMERIC,DEFAULT='10'
         SPACE 2
         IKJENDP
         TITLE 'GETMAINED WORK AREAS'
WORK     DSECT
*
*  CONTROL FLAGS FOR THIS COMMAND PROCESSOR
*
ERR      DS    X                   ERROR CONTROL INDICATOR
PARSERR  EQU   X'80'                 PARSE ROUTINE ERROR
PANERR   EQU   X'40'                 PANVALET INTERFACE ERROR
DSERR    EQU   X'20'                 TSO DATASET ERROR
TSOCP    EQU   X'10'                 TSO CTRL PGM GEN ERROR
ATTN     EQU   X'01'                 ATTN INDICATOR
         SPACE 2
STATUS   DS    X                   FILE STATUS INDICATOR
PANALC   EQU   X'80'                 PAN LIBRARY ALLOCATED
PANOPN   EQU   X'40'                 PAN LIBRARY OPEN
DSALC    EQU   X'20'                 TSO DATASET ALLOCATED
DSOPN    EQU   X'10'                 TSO DATASET OPEN
TERMOUT  EQU   X'08'                 OUTPUT TO TERMINAL
STAXACTV EQU   X'04'                 ATTN EXIT ACTIVE
         SPACE 2
*
*  PARSE CONTROL BLOCKS
*
PARSEPL  DS    7A                  PARSE PARAMETER LIST
ANSWER   DS    A                   PARSE ANSWER AREA
ECB      DS    A                   PARSE AND DAIR ECB
         SPACE 2
*
*  DAIR CONTROL BLOCKS
*
DAIRPL   DS    5A                  DAIR PARAMETER LIST
DAIRWORK DS    21A                 WORK AREA FOR ALL DAIR PARM BLOCKS
DSNWORK  DS    0H                  DSNAME WORK AREA
DSNWORKL DS    H
DSNWORKC DS    CL44
         SPACE 2
*
*  PAM WORK AREAS
*
PAMEP    DS    A                   PAM ENTRY POINT ADDRESS
         SPACE 1
PANACTN  DS    F                   ACTION
PANACTN3 EQU   *-1
PANDIR   DS    CL80                DIRECTORY
PANNAME1 DS    CL22                NAME1
PANNAME2 DS    CL11                NAME2
PANCMNT  DS    CL52                COMMENT
PANSUBST DS    CL27                SUBSET
PANRECD  DS    CL80                OUTPUT RECORD FROM PAM
PANINCLD DS    CL8                 INCLUDE
PANDDNM  DS    CL8                 DDNAME
PANBKUP  DS    CL8                 BACKUP TAPE
         SPACE 2
*
*  MISC CONTROL BLOCKS  AND WORK AREAS
*
DSDCB    DS    0F,XL(DSDCBL)       DCB AREA FOR TSO DATASET
DSDCBOC  DS    A                   OPEN/CLOSE WORK AREA
         SPACE 2
LINECRNT DS    PL5                 CURRENT LINE NUMBER
LINEINCR DS    PL3                 LINE NUMBER INCREMENT
         SPACE 2
DSDFLT   DS    CL16                DATASET NAME WORK AREA
DSDDNM   DS    CL8                 DATASET DDNAME SAVE AREA
         SPACE 2
DWORK    DS    D                   WORK AREA
TERMSAVE DS    4F                  TERMPUT TEMP REG SAVEAREA
CALLMFL  CALL  ,(0,0,0,0,0,0),MF=L CALL WORK AREA
TERMSG   DS    CL160               TERM MESSAGE AREA
         SPACE 2
STAXMFL  DS    0F,XL(STAXREFL)     STAX PARAMETER LIST
         SPACE 2
WORKL    EQU   *-WORK
         TITLE 'PARSE DSECTS'
         IKJCPPL
         IKJPPL
         TITLE 'DAIR DSECTS'
         IKJDAPL
         IKJDAP08
         IKJDAP18
         TITLE 'CVT DSECT'
         CVT   DSECT=YES,LIST=NO
         SPACE 5
         END   PANGET
