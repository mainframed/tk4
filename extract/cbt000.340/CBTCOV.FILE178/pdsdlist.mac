*          DATA SET GU009700   AT LEVEL 009 AS OF 10/29/74
LISTPDS  SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     09/30/70.
*              REMARKS.  PRINT ALPHABETICAL LIST OF PDS DIRECTORY
**
*                        PDSLIBXX - ANY NUMBER OF DD'S WHERE XX IS
*                                   ANY UNIQUE CHARACTERS. DD'S MUST
*                                   DEFINE PDS LIBRARIES FOR WHICH THE
*                                   DIRECTORIES ARE TO BE LISTED.
*
*                        SYSOUT   - DIRECTORY LISTING (13X60 PER PAGE)
*
*                        SYSPUNCH - RECORD FOR EACH DE WITH THE
*                                   FOLLOWING INFO.
*                                      1-8   MEMBER NAME
*                                     10-53  LIBRARY NAME
*                                     55-60  LIBRARY VOLUME
*                                     62-72  TIME HH/MM/SS.HH
*                                     73-78  DATE YY.DDD
*
         OPEN  (SYSOUT,(OUTPUT),SYSPUNCH,(OUTPUT))
L1       TAG
         PERFORM FINDDIR,DIREXIT   FIND START OF DIRECTORY
         CLI   EOFSW,C'T'          IS IT EOF
         BE    EXIT                YES, EXIT
L2       TAG
         PERFORM CLEARTAB,CLREXIT  CLEAR NAME TABLE
         PERFORM LOADPAGE,LPEXIT   LOAD NAMES FOR UP TO A PAGE
         PERFORM PRNTPAGE,PPEXIT   PRINT PAGE
         CLI   DSW,C'T'            IS IT END OF THIS DIRECTORY
         BNE   L2                  NO, REPEAT PAGE PROCESSING
         B     L1                  YES, GET NEXT DIRECTORY
EXIT     TAG
         CLOSE (SYSOUT,,SYSPUNCH)
         SUBEXIT
FINDDIR  PENTRY
         SCALL GETPDSDD,(ADDRJFCB,ADDRDE)
         LT    R2,ADDRJFCB
         BNZ   JFCBMOVE
         MVI   EOFSW,C'T'
         B     DIREXIT
JFCBMOVE TAG
         MVC   PDSNAME,0(R2)
         MVC   PDSVOL,118(R2)
         DTIME TIMEDATE
         MVC   PCHDSN,PDSNAME
         MVC   PCHVOL,PDSVOL
         MVC   PCHTIME,TIMEDATE+1
         MVC   PCHDATE,TIMEDATE+16
DIREXIT  PEXIT
CLEARTAB PENTRY
         LA    R1,30
         LA    R3,NAMES
L5       TAG
         MVI   0(R3),C' '
         MVC   1(207,R3),0(R3)
         LA    R3,208(R3)
         BCT   R1,L5
CLREXIT  PEXIT
LOADPAGE PENTRY
         LA    R3,NAMES-8
         ZAP   COUNT,=P'780'
L6       TAG
         SCALL GETPDSDD,(ADDRJFCB,ADDRDE)
         LT    R2,ADDRDE
         MVI   DSW,C'T'
         BZ    LPEXIT
         MVI   DSW,C'F'
         MVC   PCHMEM,0(R2)
         PUT   SYSPUNCH,PCHREC
         CLC   0(8,R3),0(R2)
         BL    MOVENAME
         MVC   BADNAME,0(R2)
         PUT   SYSOUT,SEQERR
MSGSW    NOP   MOVENAME
         WTO   'DIRECTORY ERROR - NOTIFY SYSTEM PROGRAMMER *****'
         OI    MSGSW+1,X'F0'
MOVENAME TAG
         MVC   8(8,R3),0(R2)
         LA    R3,8(R3)
         SP    COUNT,=P'1'
         BP    L6
LPEXIT   PEXIT
PRNTPAGE PENTRY
         PUT   SYSOUT,HEADING
         PUT   SYSOUT,SKIP2
         LA    R6,60
         LA    R7,NAMES
L7       TAG
         LR    R3,R7
         LA    R4,13
         LA    R5,NAMELINE
L8       TAG
         MVC   1(8,R5),0(R3)
         LA    R5,10(R5)
         LA    R3,480(R3)
         BCT   R4,L8
         PUT   SYSOUT,NAMELINE
         LA    R7,8(R7)
         BCT   R6,L7
PPEXIT   PEXIT
         EQUAL
ADDRJFCB DC    F'0'
ADDRDE   DC    F'0'
SYSPUNCH DCB   DSORG=PS,MACRF=PM,LRECL=80,RECFM=FB,BLKSIZE=80,         C
               DDNAME=SYSPUNCH
SYSOUT   DCB   DSORG=PS,MACRF=PM,LRECL=133,BLKSIZE=133,RECFM=FBA,      C
               DDNAME=SYSOUT
*
*              RECORDS
*
PCHREC   DS    0CL80
PCHMEM   DC    CL8' ',C' '
PCHDSN   DC    CL44' ',C' '
PCHVOL   DC    CL6' ',C' '
PCHTIME  DC    CL11' ',C' '
PCHDATE  DC    CL6' '
         FILL  PCHREC
HEADING  DS    0CL133
         DC    CL01'1'
PDSNAME  DC    CL44' '
         DC    C'  '
PDSVOL   DC    CL6' ',C'  '
         DC    CL23'DIRECTORY ENTRY LISTING'
         DC    CL10' '
TIMEDATE DC    CL22' '
         DC    CL23' '
SKIP2    DC    CL133'0'
NAMELINE DC    CL133' '
SEQERR   DS    0CL133
         DC    C'0'
BADNAME  DC    CL8' '
         DC    C' OUT OF SEQUENCE IN FOLLOWING DIRECTORY'
         FILL  SEQERR
*
*              DATA
*
EOFSW    DC    C'F'
DSW      DC    C'F'
PDATA    DC    2F'0'
COUNT    DC    PL2'0'
         DC    XL8'00'  LOW VALUES FOR START OF SEQUENCE CHECK
NAMES    DS    CL6240                   780 8 CHARACTER NAME ARRAY
NAMESEND EQU   *
         END   LISTPDS
