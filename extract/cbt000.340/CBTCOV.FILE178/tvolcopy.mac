*          DATA SET GU021200   AT LEVEL 012 AS OF 04/21/76
         TITLE 'TVOLCOPY - GU021200 COPY COMPLETE SL TAPE VOLUME'
         PRINT NOGEN
TVOLCOPY SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     04/02/73.
*              REMARKS.  COPY CONTENTS OF ONE TAPE VOLUME TO ANOTHER,
*                        AND LIST LABEL RECORDS, BLOCK COUNTS, AND
*                        TAPE MARKS FOR VOLUME.
*              FILES.
*
*                   SYSPRINT - REPORT
*                   SYSUT1   - INPUT TAPE VOLUME
*                   SYSUT2   - OUTPUT TAPE VOLUME
*
*              NOTES.
*                1. IF SYSUT2 IS DUMMY,   PGM SIMPLY LISTS LABEL RECS.
*                2. BOTH TAPES MUST SPECIFY BYPASS LABEL PROCESSING.
*                3. THE PROGRAM VERIFIES BOTH LABELS.
*                4. END OF VOLUME IS DEFINED BY TWO TAPE MARKS OR
*                   BY EOV2 FOLLOWED BY TAPE MARK.
*                5. NOTE LENGTH OF 'BLOCK' FOR MAXIMUM BLOCK SIZE.
*              REFERENCES.
*                  1.  TAPE LABELS CG28-6680
*                  2.  2400 TAPE UNITS GA22-6866.
*
* REV1 11/13/73 DON HIGGINS
* CHANGE OC TO CLC TO PREVENT OC4 UNDER MVT
*
*              REV1 07/14/75 DSH - CHANGE SYSUT2 OPEN PROCESS
*                                  TO HANDLE FILE PROTECT POLITELY.
*
* REV # 2 BY V.P. DIXON ON 4/21/76
*             FIXED SNSCCW TO ALLEVIATE WRONG LENGTH INDICATION ON
*             SENSE COMMAND TO 3420 TYPE TAPE DRIVES BY SETTING
*             BIT 34 IN CCW (X'20').
*
*
*
M010     TAG
         OPEN  (SYSUT1,(INPUT),SYSPRINT,(OUTPUT))
         DTIME TIMEDATE
         PERFORM R100UCBS,R195EXIT SCAN TIOT AND UCBS FOR FILE INFO
         CLI   STATUS,NOGO
         BE    M080EXIT
         CLI   FUNCTION,COPY
         BNE   M020
         OPEN  (SYSUT2,(INPUT))
M020     TAG
         PERFORM R200CHKL,R290EXIT VERIFY TAPE LABELS
         CLI   STATUS,NOGO
         BE    M080EXIT
         CLI   FUNCTION,COPY
         BNE   M060LIST
M030COPY TAG
         PERFORM R300READ,R390EXIT READ TM, LABEL, OR DATA FROM SYSUT1
         PERFORM R400RPRT,R490EXIT PRINT OR COUNT INFO READ
         CLI   TYPE,EOT
         BE    M050CLOS
         PERFORM R500WRIT,R590EXIT WRITE ON SYSUT2
         B     M030COPY
M050CLOS CLOSE (SYSUT2)
         B     M080EXIT
M060LIST TAG
         PERFORM R300READ,R390EXIT
         PERFORM R400RPRT,R490EXIT
         CLI   TYPE,EOT
         BE    M080EXIT
         B     M060LIST
M080EXIT TAG
         CLOSE (SYSUT1,,SYSPRINT)
         SUBEXIT
         EJECT
*
*              SCAN TIOT AND UCBS
*
R100UCBS PENTRY
         L     R1,CVTADDR          CVT
         L     R1,0(R1)            TCB WORDS
         L     R1,4(R1)            MY TCB
         L     R1,12(R1)           MY TIOT
         AAI   R1,24
         ZR    R2
R110SCAN IC    R2,0(R1)
         SRC   R2
         BZ    R170ERR1            DD MISSING
         CLC   4(8,R1),=CL8'SYSUT1'
         BE    R130UT1
         CLC   4(8,R1),=CL8'SYSUT2'
         BE    R140UT2
R120NEXT TAG
         AR    R1,R2
         B     R110SCAN
R130UT1  TAG
         TB    1(R1),(0,5)
         BNZ   R180ERR2            NOT BLP
         L     R3,16(R1)           UCB
         CLI   18(R3),X'80'
         BNE   R190ERR3            NOT TAPE
         MVC   UT1VOL,28(R3)
         CLI   FUNCTION,UNKNOWN
         BE    R120NEXT
         B     R195EXIT
R140UT2  TAG
* REV1 - NEXT 1
         CLC   17(3,R1),=3X'00'
         BZ    R160DUMY
         TB    1(R1),(0,5)
         BNZ   R180ERR2
         L     R3,16(R1)
         CLI   18(R3),X'80'
         BNE   R190ERR3
         MVC   UT2VOL,28(R3)
         MVI   FUNCTION,COPY
         MVC   FDESC,=CL4'COPY'
R150     CLC   UT1VOL,=CL6' '
         BE    R120NEXT
         B     R195EXIT
R160DUMY TAG
         MVI   FUNCTION,LIST
         MVC   FDESC,=CL4'LIST'
         B     R150
R170ERR1 TAG
         LA    R2,ERRMSG1
R175     PERFORM R600PRNT,R690EXIT
         MVI   STATUS,NOGO
         B     R195EXIT
R180ERR2 TAG
         LA    R2,ERRMSG2
         B     R175
R190ERR3 TAG
         LA    R2,ERRMSG3
         B     R175
R195EXIT PEXIT
         EJECT
*
*              VERIFY LABELS OF BOTH TAPES
*
R200CHKL PENTRY
         XCP   UT1IOB,RLBLCCW,R220WTO1
         CLC   BLOCK(4),=C'VOL1'
         BNE   R220WTO1            NOT AN SL INPUT TAPE
         CLC   BLOCK+4(6),UT1VOL
         BNE   R220WTO1            WRONG SL  INPUT TAPE
         MVC   LABDESC,=C'SYSUT1'
         MVC   LABELREC,BLOCK
         LA    R2,LABLINE
         PERFORM R600PRNT,R690EXIT
         CLI   FUNCTION,COPY
         BNE   R280
R210     XCP   UT2IOB,RLBLCCW,R240WTO2
         CLC   BLOCK(4),=C'VOL1'
         BNE   R240WTO2            NOT AN SL OUTPUT TAPE
         CLC   BLOCK+4(6),UT2VOL
         BNE   R240WTO2            WRONG SL  OUTPUT TAPE
         XCP   UT2IOB,SNSCCW,R240WTO2
         IF    (TM,SENSE+1,X'02',O)
               WTO  'OUTPUT TAPE IS FILE PROTECTED'
               B    R242
         FI
         MVC   LABDESC,=C'SYSUT2'
         MVC   LABELREC,BLOCK
         LA    R2,LABLINE
         PERFORM R600PRNT,R690EXIT
         B     R280
R220WTO1 TAG
         WTO   MF=(E,UT1WTO)
         CLOSE (SYSUT1)
         OPEN  (SYSUT1,(INPUT))
         B     R200CHKL
R240WTO2 TAG
         WTO   MF=(E,UT2WTO)
R242     TAG
         CLOSE (SYSUT2)
         OPEN  (SYSUT2,(INPUT))
         B     R210
R280     TAG
         MVC   LABDESC,LABDESC-1
R290EXIT PEXIT
         EJECT
*
*              READ BLOCK, TAPE MARK, OR LABEL FROM INPUT TAPE
*
R300READ PENTRY
         CLI   TYPE,TM
         BNE   R307NEXT
         CLI   LASTTYPE,TM
         BE    R305EOT
         CLI   LASTTYPE,LABEL
         BNE   R307NEXT
         CLC   LASTLAB,=C'EOV2'
         BNE   R307NEXT
R305EOT  TAG
         MVI   TYPE,EOT
         B     R390EXIT
R307NEXT TAG
         MVC   LASTTYPE,TYPE
         XCP   UT1IOB,READCCW,R310
R310     TAG
         TB    CSWUT1+3,7          IS IT UNIT EXCEPTION - TAPE MARK
         BO    R340TM
         TB    CSWUT1+3,6          IS IT UNIT CHECK - I/O ERROR
         BO    R350IOER
         OC    CSWUT1+5(2),CSWUT1+5     IS COUNT ZERO
         BZ    R350IOER                 YES, TREAT IT AS ERROR
         L     R1,=A(L'BLOCK)
         SH    R1,CSWUT1+5
         STH   R1,WRITECCW+6
         CH    R1,=H'80'
         BNE   R320DATA
         CLC   BLOCK(3),=C'VOL'
         BE    R330LAB
         CLC   BLOCK(3),=C'HDR'
         BE    R330LAB
         CLC   BLOCK(3),=C'EOF'
         BE    R330LAB
         CLC   BLOCK(3),=C'EOV'
         BE    R330LAB
R320DATA TAG
         MVI   TYPE,DATA
         B     R390EXIT
R330LAB  TAG
         MVI   TYPE,LABEL
         MVC   LASTLAB,BLOCK
         B     R390EXIT
R340TM   TAG
         MVI   TYPE,TM
         B     R390EXIT
R350IOER TAG
         MVI   TYPE,IOERR
         B     R390EXIT
R390EXIT PEXIT
         EJECT
*
*              PRINT REPORT OF RECORDS COPIED
*
R400RPRT PENTRY
         CLI   TYPE,DATA
         BE    R450DATA
         CP    BCOUNT,=P'0'
         BZ    R410
         EDIT  TO=DBCOUNT,FROM=BCOUNT,MASK=' Z,ZZZ,ZZZ'
         LA    R2,DATALINE
         PERFORM R600PRNT,R690EXIT
         ZAP   BCOUNT,=P'0'
R410     TAG
         ZR    R1
         IC    R1,TYPE
         B     *+4(R1)
         B     R440LABL
         B     R430TM
         B     R420IOER
         B     R450DATA           THIS IS A FILLER ONLY
         B     R460EOT
R420IOER TAG
         LA    R2,IOERLINE
         PERFORM R600PRNT,R690EXIT
         AP    ERRCNT,=P'1'
         B     R490EXIT
R430TM   TAG
         LA    R2,TMLINE
         PERFORM R600PRNT,R690EXIT
         B     R490EXIT
R440LABL TAG
         MVC   LABELREC,BLOCK
         LA    R2,LABLINE
         PERFORM R600PRNT,R690EXIT
         B     R490EXIT
R450DATA TAG
         AP    BCOUNT,=P'1'
         B     R490EXIT
R460EOT  TAG
         EDIT  TO=DERRCNT,FROM=ERRCNT,MASK='Z99'
         LA    R2,ERRTOT
         PERFORM R600PRNT,R690EXIT
R490EXIT PEXIT
         EJECT
*
*              WRITE BLOCK, TAPE MARK, OR LABEL ON OUTPUT TAPE
*
R500WRIT PENTRY
         CLI   TYPE,DATA
         BNE   R520
         XCP   UT2IOB,WRITECCW,R510ERR4
         B     R590EXIT
R510ERR4 TAG
         IF    (TM,CSWUT2+3,X'01',O) IS IT UNIT EXCEPTION (EOT MARKER)
               LA   R2,ERRMSG5
               PM   R600PRNT
               WTO  'SYSUT2 TAPE IS SHORTER TNAN SYSUT1'
               ABEND 113
         FI
         LA    R2,ERRMSG4          I/O ERROR ON WRITE
         PERFORM R600PRNT,R690EXIT
         WTO   'PERMANENT WRITE ERROR - JOB ABORTED'
         ABEND 111,DUMP
R520     TAG
         ZR    R1
         IC    R1,TYPE
         B     *+4(R1)
         B     R530LABL
         B     R540TM
         B     R550IOER
         B     R560EOT       THIS IS FILLER FOR DATA TYPE
         B     R560EOT
R530LABL TAG
         XCP   UT2IOB,WRITECCW,R510ERR4
         B     R590EXIT
R540TM   TAG
         XCP   UT2IOB,WTMCCW,R510ERR4
         B     R590EXIT
R550IOER TAG
         B     R590EXIT            IGNORE INPUT I/O ERROR
R560EOT  TAG
         ABEND 112,DUMP  SHOULD NOT OCCUR
R590EXIT PEXIT
         EJECT
*
*              PRINT SUBROUTINE
*
R600PRNT PENTRY
         SP    LINE,=P'2'
         BP    R610
         ZAP   LINE,MAXLINE
         AP    PAGE,=P'1'
         EDIT  TO=DPAGE,FROM=PAGE,MASK=' ZZZ'
         PUT   SYSPRINT,HEAD1
R610     TAG
         PUT   SYSPRINT,(R2)
R690EXIT PEXIT
         EJECT
*
*              DATA
*
         EQUAL
FUNCTION DC    AL1(UNKNOWN)
LIST     EQU   0
COPY     EQU   1
UNKNOWN  EQU   2
STATUS   DC    AL1(GO)
GO       EQU   0
NOGO     EQU   1
TYPE     DC    AL1(LABEL)
LABEL    EQU   0
TM       EQU   4
IOERR    EQU   8
DATA     EQU   12
EOT      EQU   16
LASTTYPE DC    AL1(LABEL)
CVTADDR  EQU   X'4C'
BCOUNT   DC    PL4'0'
LINE     DC    PL2'0'
MAXLINE  DC    PL2'60'
PAGE     DC    PL2'0'
LASTLAB  DC    CL4' '
ERRCNT   DC    PL2'0'
ERRTOT   DS    0CL133
         DC    C'0'
FDESC    DC    CL4' ',C' COMPLETED WITH'
DERRCNT  DC    CL4' ',C' ERRORS'
         FILL  ERRTOT
ERRMSG1  DC    CL133'0MISSING SYSUT2 DD STATEMENT'
ERRMSG2  DC    CL133'0SYSUT1 AND SYSUT2 MUST SPECIFY BLP'
ERRMSG3  DC    CL133'0SYSUT1 AND/OR SYSUT2 IS NOT A TAPE'
ERRMSG4  DC    CL133'0PERMANENT WRITE ERROR - JOB ABORTED'
ERRMSG5  DC    CL133'0SYSUT2 TAPE SHORTER THAN SYSUT1 - JOB ABORTED'
UT1WTO   DC    0F'0',AL2(UT1WEND-*,0)
         DC    C'WRONG TAPE - MOUNT '
UT1VOL   DC    CL6' '
UT1WEND  EQU   *
UT2WTO   DC    0F'0',AL2(UT2WEND-*,0)
         DC    C'WRONG TAPE - MOUNT '
UT2VOL   DC    CL6' '
UT2WEND  EQU   *
LABLINE  DS    0CL133
         DC    C'0LABEL '
LABDESC  DC    CL6' ',CL4' '
LABELREC DC    CL80' '
         FILL  LABLINE
DATALINE DS    0CL133
         DC    C'0DATA BLOCKS'
DBCOUNT  DC    CL10' '
         FILL  DATALINE
IOERLINE DC    CL133'0I/O ERROR - 1 OR MORE BLOCKS MAY BE LOST'
TMLINE   DC    CL133'0TAPE MARK'
HEAD1    DS    0CL133
         DC    C'1'
         DC    C'TVOLCOPY UTILITY PROGRAM GU021200 V1M0'
TIMEDATE DC    CL22' ',CL10'  PAGE'
DPAGE    DC    CL4' '
         FILL  HEAD1
         EJECT
*
*              CCW'S
*
RLBLCCW  CCW   X'02',BLOCK,X'00',80     READ 80 BYTE LABEL RECORD
ULCCW    CCW   X'0F',BLOCK,X'20',0      REWIND AND UNLOAD
READCCW  CCW   X'02',BLOCK,X'20',L'BLOCK READ A BLOCK
WRITECCW CCW   X'01',BLOCK,X'20',*-*    WRITE A BLOCK
WTMCCW   CCW   X'1F',BLOCK,X'00',1      WRITE A TAPE MARK
SNSCCW   CCW   X'04',SENSE,X'20',6       READ SENSE BYTES
SENSE    DC    XL6'00'
         EJECT
*
*              FILESDCB'S, IOB'S, ECB'S, AND BUFFER
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FA,BLKSIZE=133
SYSUT1   DCB   DSORG=PS,MACRF=E,DEVD=TA,DDNAME=SYSUT1
SYSUT2   DCB   DSORG=PS,MACRF=E,DEVD=TA,DDNAME=SYSUT2
UT1IOB   IOB   FLAGS=X'02',ECB=UT1ECB,CCW=READCCW,DCB=SYSUT1,ID=UT1
UT2IOB   IOB   FLAGS=X'02',ECB=UT2ECB,CCW=READCCW,DCB=SYSUT2,ID=UT2
UT1ECB   DC    F'0'
UT2ECB   DC    F'0'
         DS    0D
BLOCK    DS    XL25000
         END   TVOLCOPY
