*          DATA SET GU011850   AT LEVEL 008 AS OF 07/13/78
         TITLE 'GU011850 - COPY ALL OR SELECTED PDS MEMBERS TO QSAM'
*              AUTHOR.   DON HIGGINS.
*              DATE.     07/10/78.
*              REMARKS.  COPY SELECTED PDS MEMBERS TO QSAM FILE.
*              FILES.
*                  PDSLIB    PDS LIBRARY WITH LRECL=80
*                  PDSMEM    PDS LIBRARY WITH LRECL=80
*                  SYSPRINT  CONTROL LISTING
*                  SYSUT2    OUTPUT QSAM FILE WITH MEMBER NAME IN 73-80
*                  PDSCTL    SELECTION CONTROL FILE FOR GETPDSDD
*                              COL. 01-08 LOW KEY
*                              COL. 09-16 HIGH KEY OR BLANK
*                  PDSOUT    OUTPUT LIST OF SELECTION RECORDS
*              PARM.
*                  'IEBUPDTE' - INSERT ADD BEFORE EACH MEMBER
*
GU011850 SUBENTRY
         L     R1,0(R1)
         IF    (CLC,0(2,R1),=H'8',EQ),AND,(CLC,2(8,R1),=C'IEBUPDTE',EQ)
               MVI  IEBUPDTE,TRUE
         FI
         OPEN  (PDSMEM,,SYSPRINT,(OUTPUT),SYSUT2,(OUTPUT))
         PM    INIT
         PM    GETMEM
         DO    WHILE,(CLI,EOFMEM,TRUE,NE)
               PM   PUTMEM
               PM   GETMEM
         OD
         PM    STAT
         CLOSE (PDSMEM,,SYSPRINT,,SYSUT2)
         SUBEXIT
         TITLE 'INIT - INITIALIZE BUFFER AND GETPDSDD'
INIT     PENTRY
         DTIME DATETIME
         SCALL SROSDATE,(GDATE)
         LH    R0,PDSMEM+DCBBLKSI-IHADCB
         GETMAIN R,LV=(0)
         LA    R10,0(R1)
         SCALL GETPDSDD,(JFCB,DE)
         L     R1,JFCB
         IF    (LTR,R1,R1,Z)
               SUBEXIT RC=16  PDSLIB NOT DEFINED
         FI
         MVC   PDSNAME,0(R1)
         MVC   PDSVOL,118(R1)
         PEXIT
         TITLE 'GETMEM - GET NEXT MEMBER DIRECTORY ENTRY'
GETMEM   PENTRY
         CALL  GETPDSDD,(JFCB,DE)
         L     R1,DE
         IF    (LTR,R1,R1,Z)
               MVI  EOFMEM,TRUE
               PEXIT
         FI
         MVC   MEMBER,0(R1)
         MVC   TTRK(3),8(R1)
         PEXIT
         TITLE 'PUTMEM - PUT PDS MEMBER TO QSAM FILE'
PUTMEM   PENTRY
         AP    PMEM,=P'1'
         ZAP   PREC,=P'0'
         FIND  PDSMEM,TTRK,C
         IF    (CLI,IEBUPDTE,TRUE,EQ)
               MVC  ADDNAME,MEMBER
               LA   R0,ADDREC
               PM   PUTREC
         FI
         MVI   EOFBLK,FALSE
         PM    GETBLK
         DO    WHILE,(CLI,EOFBLK,TRUE,NE)
               PM   PUTBLK
               PM   GETBLK
         OD
         EDIT  TO=DREC,FROM=PREC,MASK=' ZZ,Z99'
         AP    PTOT,PREC
         PM    PRINT
         PEXIT
         TITLE 'GETBLK - READ BLOCK FROM PDS MEMBER FILE'
GETBLK   PENTRY
         READ  DECB,SF,PDSMEM,(R10),'S'
         CHECK DECB
         L     R1,PDSMEM+DCBIOBA-IHADCB    R1=A(IOB)
         LH    R9,PDSMEM+DCBBLKSI-IHADCB   R9=MAX BLOCK SIZE
         SH    R9,22(R1)                   R9=ACTUAL BLOCK SIZE
         AR    R9,R10                      R9=A(END OF BLOCK)
         PEXIT
EOFRTN   MVI   EOFBLK,TRUE
         PEXIT
         TITLE 'PUTBLK - WRITE RECORDS FROM BLOCK'
PUTBLK   PENTRY
         LR    R2,R10
         DO    WHILE,(CLR,R2,R9,L)
               MVC  72(8,R2),MEMBER
               AP   PREC,=P'1'
               LR   R0,R2
               PM   PUTREC
               AAI  R2,80
         OD
         PEXIT
         TITLE 'PRINT - PRINT LIST OF MEMBERS WRITTEN'
PRINT    PENTRY
         IF    (SP,LINE,=P'1',NP)
               AP   PAGE,=P'1'
               ZAP  LINE,=P'50'
               EDIT TO=DPAGE,FROM=PAGE,MASK=' ZZZZZ'
               LA   R0,HEAD1
               PM   PUT
               LA   R0,HEAD2
               PM   PUT
         FI
         LA    R0,DETAIL
         PM    PUT
         PEXIT
         TITLE 'PUT - PUT PRINT LINE'
PUT      PENTRY
         PUT   SYSPRINT
         PEXIT
         TITLE 'PUTREC - PUT RECORD TO SYSUT2'
PUTREC   PENTRY
         PUT   SYSUT2
         PEXIT
         TITLE 'STAT - PRINT TOTAL LINE'
STAT     PENTRY
         EDIT  TO=DMEM,FROM=PMEM,MASK=' ZZ,Z99'
         EDIT  TO=DTOT,FROM=PTOT,MASK=' Z,ZZZ,Z99'
         LA    R0,TOTAL
         PM    PUT
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
EOFMEM   LOGICAL FALSE,(TRUE,FALSE)
EOFBLK   LOGICAL FALSE,(TRUE,FALSE)
IEBUPDTE LOGICAL FALSE,(TRUE,FALSE)
SYSPRINT SDCB  M=PM,R=FBA,L=133
SYSUT2   SDCB  M=PM,R=FB,L=80
PDSMEM   DCB   DSORG=PO,MACRF=R,DDNAME=PDSMEM,EODAD=EOFRTN,            X
               RECFM=FB,LRECL=80
JFCB     DC    A(0)
DE       DC    A(0)
PTOT     DC    PL4'0'
PMEM     DC    PL3'0'
PREC     DC    PL3'0'
LINE     DC    PL2'0'
PAGE     DC    PL3'0'
REC      DC    PL3'0'
TTRK     DC    XL4'00'
HEAD1    DS    0CL133
         DC    C'1GU011850  '
PDSNAME  DC    CL44' ',2C' '
PDSVOL   DC    CL6' ',5C' '
GDATE    DC    CL8' ',C' '
DATETIME DC    CL22' ',C'  PAGE'
DPAGE    DC    CL6' '
         FILL  HEAD1
HEAD2    DC    CL133' '
DETAIL   DS    0CL133
         DC    C' '
MEMBER   DC    CL8' ',C' RECORDS='
DREC     DC    CL7' '
         FILL  DETAIL
TOTAL    DS    0CL133
         DC    C'0TOTAL MEMBERS='
DMEM     DC    CL7' ',C'    TOTAL RECORDS='
DTOT     DC    CL10' '
         FILL  TOTAL
ADDREC   DS    0CL80
         DC    C'./ ADD NAME='
ADDNAME  DC    CL8' '
         FILL  ADDREC
         TITLE 'DSECTS'
         DCBD  DSORG=PO
         END   GU011850
