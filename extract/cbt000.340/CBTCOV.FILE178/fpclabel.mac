*          DATA SET GU001400   AT LEVEL 003 AS OF 01/10/78
         PRINT NOGEN
         MACRO
&N       XCP   &IOB,&CCW,&ERR
&N       LA    R0,&CCW
         ST    R0,&IOB+16
         EXCP  &IOB
         L     R1,&IOB+4
         WAIT  ECB=(1)
         TM    &IOB+12,X'03'
         BNZ   &ERR
         TM    &IOB+13,X'FF'
         BNZ   &ERR
         MEND
FPCLABEL SUBENTRY
*              PROGRAM.  GU001400.
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/25/71.
*              REMARKS.  CREATE AND TEST FPC TAPES FOR USE UNDER OS.
*              MAINTENANCE.
*                  REV # 1 BY V P DIXON ON 1-10-78.
*                       1. CHANGE TAPE DENSITY FROM 1600 (C3) TO
*                          6250 (D3) IN ''SDCCW'' CCW.
*                       2. CHANGE ''WRITECCW'' AND ''RDBKCCW'' BLOCK-
*                          SIZE TO 16K VERSUS 64K SO CHANNEL ISN''T
*                          TIED UP.
*
         OPEN  (PRINT,(OUTPUT))
         WTO   'FPC TAPE LABEL AND TESTING UTILITY'
         TIME  DEC
         STM   R0,R1,PWORK
         EDIT  TO=TIMEDATE,FROM=PWORK,MASK=' 99/99/99.99F ZZ99.999'
         PUT   PRINT,HEAD1
         PUT   PRINT,SPACE
COMMAND  TAG
         XC    ANS,ANS
         XC    WTORECB,WTORECB
         WTOR  'ENTER - SL,XXXXXX (,TEST) - NL (,TEST) - END',         X
               ANS,14,WTORECB
         WAIT  ECB=WTORECB
         TR    ANS,UPPEREBC
         MVC   CLINE+1(14),ANS
         PUT   PRINT,CLINE
         CLC   ANS(3),=C'END'
         BE    FINISH
         CLC   ANS(2),=C'NL'
         BE    NL
         CLC   ANS(2),=C'SL'
         BE    SL
         B     WHAT
NL       TAG
         XC    SERIAL,SERIAL
         BAL   R12,SETUP
         XCP   TAPEIOB,NLCCW,IOERR  WRITE TAPE MARK TO INSURE NL
         CLC   ANS+2(5),=C',TEST'
         BNE   OK
         B     TEST
SL       TAG
         TRT   ANS+3(6),VALIDDEC
         BC    7,WHAT
         MVC   SERIAL,ANS+3
         BAL   R12,SETUP
         XCP   TAPEIOB,SLCCW,IOERR  WRITE STANDARD LABEL
         CLC   ANS+9(5),=C',TEST'
         BNE   OK
         B     TEST
SETUP    TAG
         OPEN  (TAPE,(INPUT))
         XCP   TAPEIOB,SDCCW,IOERR  SET DENSITY
         XCP   TAPEIOB,SENSECCW,IOERR
         TM    SENSE+1,X'08'           IS TAPE AT LOAD
         BNO   IOERR
         BR    R12
WHAT     TAG
         PUT   PRINT,WHATLINE
         WTO   'WHAT'
         B     COMMAND
OK       TAG
         PUT   PRINT,OKLINE
         WTO   'OK'
NEXT     TAG
         CLOSE (TAPE)
         B     COMMAND
TEST     TAG
L1       TAG
         XCP   TAPEIOB,WRITECCW,L1END
         B     L1
L1END    TAG
         TM    CSWTAPE+3,X'01'     IS IT UE
         BNO   IOERR
L2       TAG
         XCP   TAPEIOB,RDBKCCW,L2END
         B     L2
L2END    TAG
         TM    CSWTAPE+3,X'01'    IS IT UE
         BNO   IOERR
         B     OK
IOERR    TAG
         PUT   PRINT,ERRLINE
         WTO   'I/O ERROR - FUNCTION TERMINATED'
         B     NEXT
FINISH   TAG
         CLOSE (PRINT)
         SUBEXIT
         EJECT
*
*              DATA
*
         EQUREGS
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT,RECFM=FBA,LRECL=133
TAPE     DCB   DSORG=PS,MACRF=E,DEVD=TA,DDNAME=TAPE
TAPEIOB  IOB   FLAGS=X'02',ECB=TAPEECB,CCW=SDCCW,DCB=TAPE,ID=TAPE
TAPEECB  DC    F'0'
SDCCW    CCW   X'D3',0,X'20',1     SET DENSITY TO 6250 PE
WRITECCW CCW   X'01',0,X'20',X'4000'    WRITE 16K BLOCKS TO END OF REEL
RDBKCCW  CCW   X'0C',0,X'10',X'4000'    READ BACKWARD WITH SUPRESS
SLCCW    CCW   X'01',VOL1,X'60',80 WRITE VOL1 RECORD
         CCW   X'01',HDR1,X'60',80 WRITE HDR1 RECORD
NLCCW    CCW   X'1F',0,X'00',1     WRITE TAPE MARK
SENSECCW CCW   X'04',SENSE,X'20',6      READ 6 SENSE BYTES
WTORECB  DC    F'0'
PWORK    DC    D'0'
HEAD1    DS    0CL133
         DC    C'1',10C' ',C'FPC TAPE LABEL AND TESTING UTILITY'
TIMEDATE DC    CL22' '
         FILL  HEAD1
SPACE    DC    CL133' '
UPPEREBC DC    107C' ',C',',21C' ',C'ABCDEFGHI',7C' ',C'JKLMNOPQR'
         DC    8C' ',C'STUVWXYZ',23C' ',C'ABCDEFGHI',7C' '
         DC    C'JKLMNOPQR',8C' ',C'STUVWXYZ',6C' ',C'0123456789',6C' '
ANS      DC    CL14' '
CLINE    DC    CL133' '
OKLINE   DC    CL133'  OK'
ERRLINE  DC    CL133'  I/O ERROR - FUNCTION TERMINATED'
VALIDDEC DC    240X'FF',10X'00',6X'FF'
WHATLINE DC    CL133'  WHAT'
VOL1     DS    0CL80
         DC    C'VOL1'
SERIAL   DC    CL6'999999'
         DC    C'0'
         DC    CL30' '
         DC    CL10'FLAPWRCORP'
         DC    CL29' '
HDR1     DC    CL80'HDR1'
SENSE    DC    XL6'00'
         LTORG
         END   FPCLABEL
