*          DATA SET GU009900   AT LEVEL 004 AS OF 02/13/76
         TITLE 'GU009900 - TEST IEFUTL EXIT'
*              AUTHOR.
*              DATE.
*              REMARKS.  TEST IEFUTL EXIT. SEE GU008500.
*              FILES.
*                   SYSIN - CONTROL CARDS.
*
*                        1-8 JOB NAME
*                          9 EXIT TYPE
*                             0 - JOB CPU
*                             4 - STEP CPU
*                             8 - JOB WAIT
*
*                   SYSPRINT - OUTPUT MESSAGES.
         TITLE 'MAINLINE'
         PRINT NOGEN
GU009900 SUBENTRY
         OPEN  (SYSIN,,SYSPRINT,(OUTPUT))
         LOAD  EP=GU008500
         ST    R0,AIEFUTL
         MODESET MODE=SUP,KEY=ZERO
         PM    GET
         DO    WHILE,(CLI,EOF,TRUE,NE)
               PM   TEST
               PM   GET
         OD
         CLOSE (SYSIN,,SYSPRINT)
         SUBEXIT
SYSIN    SDCB  R=FB,L=80,M=GM,E=EOFRTN
SYSPRINT SDCB  R=FBA,L=133,M=PM
EOF      LOGICAL FALSE,(TRUE,FALSE)
AIEFUTL  DC    A(0)
         TITLE 'GET'
GET      PENTRY
         GET   SYSIN,CARD
         PEXIT
EOFRTN   MVI   EOF,TRUE
         PEXIT
CARD     DS    0CL80
JOB      DC    CL8' '
TYPE     DC    C' '
         FILL  CARD
         TITLE 'TEST'
TEST     PENTRY
         MVC   DCARD,CARD
         LA    R0,L1
         PM    PRINT
         PACK  PWORK,TYPE
         CVB   R0,PWORK
         LA    R1,ASMFCET
         L     R15,AIEFUTL
         BALR  R14,R15
         CVD   R1,PWORK
         EDIT  TO=DPERIOD,FROM=PWORK+3,MASK=' ZZZZZZZ99'
         CVD   R15,PWORK
         EDIT  TO=DOPTION,FROM=PWORK+6,MASK=' Z99'
         LA    R0,L2
         PM    PRINT
         PEXIT
L1       DS    0CL133
         DC    C'0INPUT '
DCARD    DC    CL80' '
         FILL  L1
PWORK    DC    D'0'
ASMFCET  DC    A(JOB)
L2       DS    0CL133
         DC    C'0OUTPUT   R1=PERIOD='
DPERIOD  DC    CL10' '
         DC    C'   R15=OPTION='
DOPTION  DC    CL4' '
         FILL  L2
         TITLE 'PRINT'
PRINT    PENTRY
         PUT   SYSPRINT
         PEXIT
         TITLE 'COMMON'
         EQUAL
         END
