*          DATA SET GU013800   AT LEVEL 002 AS OF 11/07/72
PACKBIT  CSECT
*              AUTHOR.   DON HIGGINS.
*              DATE.     11/04/72.
*              REMARKS.  SET BIT ON-OFF IN PACKED BIT SWITCH ARRAY.
*
*                        CALL PACKBIT(BITS,NUMBIT,BYTE)
*
*                         BITS - PACKED BIT SWITCH ARRAY
*
*                         NUMBIT-FULL WORD COMPUTATIONAL SUBSCRIPT OF
*                                THE BIT TO BE SET ON OR OFF. STARTS
*                                WITH 1 BEING THE FIRST BIT IN THE
*                                FIRST BYTE OF THE BIT ARRAY.
*
*                         BYTE  -ONE BYTE CONTAINING 0 OR 1 VALUE
*                                INDICATING DESIRED VALUE OF BIT.
*
*              REGISTERS
*                R1 - A(BIT ARRAY), A(BIT ARRAY + (NUMBIT-1)/8)
*                R2 - A(NUMBIT)
*                R3 - A(BYTE SWITCH INPUT)
*                R4 -(NUMBIT-1)/8
*                R5 - REMAINDER*4
*
         USING *,R15
         SAVE  (1,5)
         LM    R1,R3,0(R1)
         L     R4,0(R2)            R4=NUMBIT
         BCTR  R4,R0               R4=NUMBIT-1
         SR    R5,R5               R5=F'0'
         SRDA  R4,3                R4=(NUMBIT-1)/8
         SRL   R5,27               R5=REMAINDER*4
         LA    R1,0(R1,R4)         R1=A(BYTE WITH THE BIT WE WANT)
         EX    R0,NITAB(R5)        TURN BIT OFF
         CLI   0(R3),C'1'          SHOULD IT BE ON
         BNE   *+8                 NO, SKIP
         EX    R0,OJTAB(R5)        YES, TURN BIT ON
         RETURN (1,5)
NITAB    TAG                       SET BIT OFF
         NI    0(R1),X'7F'
         NI    0(R1),X'BF'
         NI    0(R1),X'DF'
         NI    0(R1),X'EF'
         NI    0(R1),X'F7'
         NI    0(R1),X'FB'
         NI    0(R1),X'FD'
         NI    0(R1),X'FE'
OJTAB    TAG                       SET BIT ON
         OI    0(R1),X'80'
         OI    0(R1),X'40'
         OI    0(R1),X'20'
         OI    0(R1),X'10'
         OI    0(R1),X'08'
         OI    0(R1),X'04'
         OI    0(R1),X'02'
         OI    0(R1),X'01'
         EQUREGS
         END
