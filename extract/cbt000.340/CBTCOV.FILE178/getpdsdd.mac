*          DATA SET GU024600   AT LEVEL 005 AS OF 12/14/73
GETPDSDD SUBENTRY
*              AUTHOR.   DON HIGGINS.
*              DATE.     10/30/73 2 AM WITH SORE THROAT.
*              REMARKS.  SUBROUTINE TO OBTAIN ADDRESS OF PDS INFO
*                        IN THE FOLLOWING SEQUENCE.
*                             CALL   ARGUMENT VALUES
*                              1       A(JFCB FOR FIRST DD NAMED
*                                        PDSLIBXX. ZERO IF NONE FOUND.)
*                              2       A(NEXT DE ENTRY FOR PDSLIBXX)
*                              N       A(LAST DE FOR PDSLIBXX)
*                              N+1     A(ZERO TO INDICATE END OF DE'S)
*                              N+2     A(NEXT JFCB FOR NEXT DD WITH
*                                        PDSLIBXX. ZERO IF NONE FOUND)
*                        FIRST ARGUMENT IS JFCB ADDR OR ZERO.
*                        SECOND ARGUMENT IS ADDRESS OF DE OR ZERO.
*                        SUBROUTINE MUST NOT BE CALLED AGAIN AFTER ZERO
*                        IS RETURNED INSTEAD OF JFCB ADDRESS.
* REV1 DSH 10/08/73 ADD OPTIONAL CONTROL CARD FACILITY
* A CARD FILE NAMED PDSCTL AND A SYSOUT FILE PDSOUT MAY BE ADDED.
* IF PDSCTL IS USED, IT MAY DEFINE DEGINNING (COL. 1-8) AND ENDING
* (COL. 9-16) MEMBER NAMES TO BE RETURNED TO THE CALLING PGM.
* IF 9-16 IS BLANK, THE MEMBER NAMED IN COL. 1-8 ONLY WILL BE RETURNED.
* MULTIPLE CARDS MUST BE INSEQUENCE.  ALL CARDS AND ANY SEQUENCE
* ERRORS WILL BE LISTED ON PDSOUT.  ANY ERROR WILL FORCE EOF/EOJ.
* IF PDSCTL DD IS DUMMY, OR OMITTED, OR IS VOID OF RECORDS,
* ALL NAMES WILL BE RETURNED.
* IF 1 OR MORE RECORDS ARE PRESENT, ONLY DE'S WITHIN RANGE WILL BE
* RETURNED.  THIS OPTION WILL FUNCTION ONLY FOR THE FIRST PDSLIB.
* ADDITIONAL PDSLIB'S WILL BE IGNORED.
*              REGISTERS.
*                        R0-R2 WORK
*                        R3-R4 ARGUMENT ADDRESSES
*                        R5    DCB ADDRESS FOR IHADCB
*                        R13   BASE
*                        R14-R15 LINKAGE
         LM    R3,R4,0(R1)         ARG ADDRESSES
M010     NOP   M020
         OI    M010+1,X'F0'        SET FIRST TIME NOP TO BRANCH
         PERFORM R100SET,R190EXIT
         B     M030
M020     TAG
         OC    ADDRJFCB,ADDRJFCB
         BZ    M060                ABEND IF ZERO JFCB HAS BEEN RETURNED
         OC    ADDRDE,ADDRDE
         BNZ   M040                GET NEXT DE IF LAST DE NOT ZERO
         CLOSE (PDSDCB)
M030     TAG
         PERFORM R200DD,R290EXIT   GET NEXT DD
         B     M050                EXIT WITH JFCB OR ZERO ADDRESS
M040     TAG
         SCALL GETPDSDE,(PDSDCB,M070,ADDRDE)
* REV1 - TO M050
         CLI   CTLSTAT,ACTIVE
         BNE   M050
R045     TAG
         L     R2,ADDRDE           IS MEMBER IN CTL RANGE
         CLC   0(8,R2),CTLLOW
         BL    M040
         CLC   0(8,R2),CTLHIGH
         BNH   M050
         PERFORM R300GCTL,R390EXIT GET NEXT CTL CARD
         CLI   CTLSTAT,CTLEOF
         BE    M070
         B     R045
M050     TAG
         MVC   0(4,R3),ADDRJFCB
         MVC   0(4,R4),ADDRDE
         SUBEXIT
M060     ABEND 101,DUMP
M070     TAG
* REV1 - M080
         CLI   CTLSTAT,INACTIVE    IS CTL MODE ACTIVE
         BE    M080                NO, ALLOW MULTIPLE PDSLIB DD'S
         MVC   NEXTDD,=A(TIOTEND)  FORCE END OF TIOT TO FORCE EOJ
M080     TAG
         MVC   ADDRDE,=A(0)        SET END OF DIRECTORY INDICATOR
         B     M050
R100SET  PENTRY
         L     R1,16               CVT
         L     R1,0(R1)            TCB WORDS
         L     R1,4(R1)            MY TCB
         L     R1,12(R1)           MY TIOT
         LA    R1,24(R1)           MY FIRST DD ENTRY IN TIOT
         ST    R1,NEXTDD
* REV1 - UP TO R190EXIT
R110     TAG
         LR    R2,R1
         ZR    R1
         IC    R1,0(R2)
         SRC   R1
         BZ    R190EXIT
         LA    R1,0(R1,R2)
         CLC   4(8,R2),=CL8'PDSCTL'
         BNE   R110
         OPEN  (PDSCTL,(INPUT),PDSOUT,(OUTPUT))
         PUT   PDSOUT,HEAD1
         PUT   PDSOUT,HEAD2
         PERFORM R300GCTL,R390EXIT
R190EXIT PEXIT
R200DD   PENTRY
         MVC   ADDRJFCB,=A(0)      SET LAST JFCB ZERO
         MVC   ADDRDE,=A(1)        SET LAST DE NOT ZERO
R210     TAG
         L     R2,NEXTDD
         ZR    R1
         IC    R1,0(R2)
         SRC   R1
         BZ    R290EXIT            EXIT  WITH ZERO IF END OF TIOT
         LA    R1,0(R1,R2)
         ST    R1,NEXTDD
         CLC   4(6,R2),=C'PDSLIB'
         BNE   R210
         LA    R5,PDSDCB
         USING IHADCB,R5
         MVC   DCBDDNAM,4(R2)
         RDJFCB PDSDCB
         OPEN  (PDSDCB,(INPUT)),TYPE=J
         MVC   ADDRJFCB,=A(JFCB)
R290EXIT PEXIT
* REV1 - R300-390
R300GCTL PENTRY
         GET   PDSCTL
         LR    R2,R1
         PUT   PDSOUT,(R2)         LIST CTL CARD AS IS
         MVI   CTLSTAT,ACTIVE      SET CTL MODE ACTIVE (AT LEAST 1 CD)
         CLC   CTLHIGH,0(R2)       IS NEW LOW LESS THAN LAST HIGH
         BNL   R320ERR             YES,PRINT SEQ. ERR.
         MVC   CTLREC(16),0(R2)    SAVE NEW LOW AND HIGH
         CLC   CTLHIGH,=CL8' '     IS NEW HIGH BLANK
         BNE   R310                NO, GO TEST IT AGAINST NEW LOW
         MVC   CTLHIGH,CTLLOW      SET HIGH EQUAL TO LOW
         B     R390EXIT
R310     TAG
         CLC   CTLLOW,CTLHIGH      IS HIGH LESS THAN LOW
         BNH   R390EXIT            NO, EXIT
R320ERR  TAG
         PUT   PDSOUT,CTLERR       PRINT SEQ. EROR AND FORCE EOF/EOJ
R330EOF  TAG
         CLI   CTLSTAT,INACTIVE    IS THIS EOF ON FIRST READ
         BE    R390EXIT            YES,LEAVE CTL MODE INACTIVE
         MVI   CTLSTAT,CTLEOF      NO,  SET EOF STATUS TO FORCE EOF/EOJ
R390EXIT PEXIT
         EQUAL
NEXTDD   DC    A(1)
ADDRJFCB DC    A(1)
ADDRDE   DC    A(1)
JFCBLST  DC    0F'0',X'87',AL3(JFCB)
JFCB     DC    0F'0',XL176'00'
INACTIVE EQU   0                   PDSCTL MISSING OR EMPTY - FULL LIST
ACTIVE   EQU   1                   PDSCTL IN USE - SELECTIVE LIST
CTLEOF   EQU   2                   PDSCTL AT EOF - FORCE EOF DE/JFCB
CTLSTAT  DC    AL1(INACTIVE)
TIOTEND  DC    X'00'
CTLREC   DS    0CL80
CTLLOW   DS    CL8
CTLHIGH  DS    CL8
         FILL  CTLREC
HEAD1    DC    CL80'GETPDSDD PDSCTL CONTROL CARD LISTING'
HEAD2    DC    CL80'LLLLLLLLHHHHHHHH'
CTLERR   DC    CL80'*** ABOVE CARD FORCED EOF DUE TO SEQ. ERR. ***'
PDSOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=PDSOUT,RECFM=F,BLKSIZE=80
PDSCTL   DCB   DSORG=PS,MACRF=GL,DDNAME=PDSCTL,EODAD=R330EOF,          X
               RECFM=FB,LRECL=80
PDSDCB   DCB   DSORG=PS,MACRF=GL,DDNAME=PDSLIBXX,                      X
               RECFM=F,LRECL=256,BLKSIZE=256,EXLST=JFCBLST
         DCBD  DSORG=PS
         END
