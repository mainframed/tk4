*          DATA SET UI010000   AT LEVEL 018 AS OF 04/05/77
*          DATA SET UI010000   AT LEVEL 016 AS OF 03/23/77
*          DATA SET UI010000   AT LEVEL 009 AS OF 03/04/77
*          DATA SET UI010000   AT LEVEL 008 AS OF 03/03/77
         TITLE 'UI010000 - USER MASTER FILE INTERFACE SUBROUTINE'
*              PGM-ID.   UI010000.
*              AUTHOR.   DON HIGGINS.
*              DATE.     02/22/77.
*              REMARKS.  THIS SUBROUTINE IS TO BE THE COMMON
*                        INTERFACE TO THE CSD USER INFORMATION
*                        MASTER FILE UI.UI005D01.  THIS ROUTINE
*                        USES A STANDARD QSAM/BDAM ACCESS METHOD
*                        INTERFACE SUBROUTINE SR204000.
*              CALL.
*                        CALL DCALL('UI005000',OP,TYPE,USERID,REC,STAT)
*
*                        DCALL IS A DYNAMIC CALL INTERFACE (SR201000)
*                        WHICH CAN BE USED BY ANY COBOL, FORTRAN, OR
*                        ASSEMBLER PROGRAM TO DYNAMICALLY LOAD AND
*                        CALL A SUBROUTINE WHICH HAS BEEN COMPILED
*                        AND LINKED INTO FPC.SUBLIB.
*
*                        OP - OPERATION CODE - 4 BYTE BINARY
*
*                             1 - READ BASIC
*                             2 - WRITE BASIC
*                            10 - READ 20 BYTE PGMR NAME FIELD
*                            11 - READ 10 ELEMENT READ/WRITE PREFIXES
*                            12 - READ 10 ELEMENT READ ONLY  PREFIXES
*                            13 - READ 4 BYTE BINARY DEPARTMENT NO.
*                            14 - READ 25 BYTE MAILING ADDRESS
*                            15 - READ 27 BYTE NAME FIELD
*                            16 - READ 14 PHONE NO. X=NNN-NNN-NNNN
*                            17 - READ  6 OFFICE EXT. NO. X=NNNN
*
*
*                             3 - OPEN
*                             4 - CLOSE
*
*                        TYPE - TYPE OF ACCESS - 4 BYTE BINARY
*
*                             1 - SEQUENTIAL READ ONLY
*                             2 - SEQUENTIAL WRITE ONLY
*                             3 - SEQUENTIAL UPDATE
*                             4 - RANDOM READ ONLY
*                             5 - RANDOM UPDATE
*
*                        USERID - USER IDENTIFICATION NUMBER - 4 BYTE
*
*                             0000 - RESERVED
*                             0001 - 0999 INDIVIDUAL USERS
*                             1000 - 1299 PRODUCTION SYSTEMS
*                             1300 - 1365 NON FPC USER ADDRESSES
*
*                        REC - AREA FOR DATA - SIZE DEPENDS ON OP
*
*                             OP      SIZE
*
*                              1,2    408 BYTES
*
*                         STAT - STAUTS CODE - 4 BYTE BINARY
*
*                             1 - OPERATION SUCCESSFUL
*                             2 - NO RECORD FOUND
*                             3 - END OF FILE
*
*
*
*
*
*
*
         TITLE ' MAINLINE'
UI010000 SUBENTRY
         LA    R12,REC
         USING UIMASTER,R12
         LM    R2,R7,0(R1)
         MVC   OP,3(R2)
         MVC   TYPE,3(R3)
         MVI   STATUS,1
         L     R2,0(R2)
         DOCASE (R2)
               CASE 1
                 PM READB
               ESAC
               CASE 2
                 PM WRITEB
               ESAC
               CASE 3
                 PM OPEN
               ESAC
               CASE 4
                 PM CLOSE
               ESAC
               CASE 10
                 PM RPGMR
               ESAC
               CASE 11
                 PM RUIRW
               ESAC
               CASE 12
                 PM RUIRO
               ESAC
               CASE 13
                 PM RDEPT
               ESAC
               CASE 14
                 PM CMAIL
               ESAC
               CASE 15
                 PM RNAME
               ESAC
               CASE 16
                 PM RPHONE
               ESAC
               CASE 17
                 PM REXT
               ESAC
               CASE ELSE
                 ABEND 102,DUMP
               ESAC
         ESACOD
         MVC   0(4,R6),FSTATUS
         SUBEXIT
         TITLE 'READB - READ BASIC'
READB    PENTRY
         PM    READ
         LM    R14,R1,=A(0,L'REC,REC,L'REC)
         LR    R14,R5
         MVCL  R14,R0
         PEXIT
         TITLE 'WRITEB - WRITE BASIC'
WRITEB   PENTRY
         LM    R14,R1,=A(0,L'REC,REC,L'REC)
         LR    R14,R5
         MVCL  R0,R14
         PM    WRITE
         PEXIT
         TITLE 'OPEN'
OPEN     PENTRY
         MVI   OP,3
         LA    R1,FIPLIST
         SCALL DCALL
         MVI   OPENED,TRUE
         PEXIT
         TITLE 'CLOSE'
CLOSE    PENTRY
         LA    R1,FIPLIST
         SCALL DCALL
         PEXIT
         TITLE 'MISC READ FUNCTIONS'
CMAIL    PENTRY
         PM    READ
         IF    (CLC,0(4,R4),=F'1000',LT)
               MVC   3(25,R5),MNGADDR
               MVC   0(3,R5),MMAIL
         ELSE
               MVC   0(28,R5),=CL28'B-3 COMPUTER SERVICES'
         FI
         PEXIT
RPGMR    PENTRY
         PM    READ
         MVI   0(R5),C'X'
         IF    (CLC,0(4,R4),=F'1000',LT)
               MVC   1(19,R5),MINIT
         ELSE
               MVC   1(19,R5),MSYSNAME
         FI
         PEXIT
RNAME    PENTRY
         PM    READ
         IF    (CLC,0(4,R4),=F'1000',LT)
               MVC   0(27,R5),MINIT
         ELSE
               MVC   0(27,R5),MSYSNAME
         FI
         PEXIT
RUIRW    PENTRY
         PM    READ
         MVC   0(40,R5),MRWPFX  R/W PREFIXES
         PEXIT
RUIRO    PENTRY
         PM    READ
         MVC   0(40,R5),MROPFX  READ ONLY PREFIXES
         PEXIT
RDEPT    PENTRY
         IF    (CLC,0(4,R4),=F'1000',NL)  FORCE CSD DEPT FOR SYS IDS
               LA   R0,3300
               ST   R0,0(R5)
               PEXIT
         FI
         PM    READ
         UNPK  DWORK(5),MDEPTNO
         OI    DWORK+4,X'F0'
         TR    DWORK(5),HEXTAB-240
         PACK  PWORK,DWORK(5)
         CVB   R0,PWORK
         ST    R0,0(R5)
         PEXIT
RPHONE   PENTRY
         IF    (CLC,0(4,R4),=F'1000',GE)
               MVC  0(14,R5),=CL14'NO PHONE'
               PEXIT
         FI
         PM    READ
         MVC   0(1,R5),MPHTYPE
         EDIT  TO=DWORK,FROM=MPHAREA,MASK=' ZZZ'
         MVC   1(4,R5),DWORK+1
         EDIT  TO=DWORK,FROM=MPHEXCH,MASK='-ZZZ'
         MVC   5(4,R5),DWORK
         EDIT  TO=DWORK,FROM=MPHNUM,MASK='-ZZZZZ'
         MVC   9(5,R5),DWORK+1
         PEXIT
REXT     PENTRY
         IF    (CLC,0(4,R4),=F'1000',GE)
               MVC  0(6,R5),=CL14'NO PH.'
               PEXIT
         FI
         PM    READ
         MVC   0(1,R5),MPHEXTT
         EDIT  TO=DWORK,FROM=MPHEXT,MASK=' ZZZZZ'
         MVC   1(5,R5),DWORK+1
         PEXIT
         TITLE 'READ'
READ     PENTRY
         IF    (CLC,USERID,0(R4),EQ)
               PEXIT
         FI
         IF    (CLI,OPENED,TRUE,NE)
               PM   OPEN
         FI
         MVI   OP,1
         MVC   USERID,0(R4)
         LA    R1,FIPLIST
         SCALL DCALL
         PEXIT
         TITLE 'WRITE'
WRITE    PENTRY
         IF    (CLI,OPENED,TRUE,NE)
               PM   OPEN
         FI
         MVI   OP,2
         MVC   USERID,0(R4)
         LA    R1,FIPLIST
         SCALL DCALL
         PEXIT
         TITLE 'COMMON DATA'
         EQUAL
OPENED   LOGICAL FALSE,(TRUE,FALSE)
FIPLIST  DC    A(FIPNAME,FOP,FTYPE,FILENAME,USERID,REC,FSTATUS)
FIPNAME  DC    CL8'SR204000'
FILENAME DC    CL8'UI005D01'
USERID   DC    F'-1'
PWORK    DC    D'0'
DWORK    DC    XL8'00'
HEXTAB   DC    C'0123456789000000'
FSTATUS  DC    F'0'
STATUS   EQU   FSTATUS+3
REC      DS    XL408
FOP      DC    F'0'
OP       EQU   FOP+3
FTYPE    DC    F'0'
TYPE     EQU   FTYPE+3
         TITLE 'USER MASTER FILE RECORD DSECT'
UIMASTER DSECT
MUSERID  DS    XL4
MRECTYPE DS    X
MREGDAT  DS    PL4
MSTATUS  DS    X
MSTATDAT DS    PL4
MTSOSTAT DS    X
MTSODAT  DS    PL4
MLASTFMC DS    X
MLASTFMT DS    XL3
MLASTDAT DS    PL4
*
MUSER    EQU   *     START OF USER DATA DEPENDENT ON TYPE OF REC
*
MINIT    DS    XL2
MLAST    DS    XL15
MNICK    DS    XL10
MMAIL    DS    XL3
MPHTYPE  DS    X
MPHAREA  DS    PL2
MPHEXCH  DS    PL2
MPHNUM   DS    PL3
MPHFILL  DS    XL2
         ORG   MPHTYPE+1
MPHMICRO DS    XL9
MPHEXTT  DS    X
MPHEXT   DS    PL3
MDEPTNO  DS    PL3
MPAYNO   DS    PL3
MRA      DS    PL3
MNGADDR  DS    XL25
MNGID    DS    PL3
MRWPFX   DS    10XL4
MROPFX   DS    10XL4
         ORG   MUSER
MSYSAREA DS    X
MSYSPFX  DS    XL2
MSYSNICK DS    XL10
MSYSUSID DS    PL3
MSYSOPFX DS    XL2
MSYSSTAT DS    X
MSYSTYPE DS    X
MSYSNAME DS    XL40
         ORG   MUSER
MADADDR1 DS    XL25
MADADDR2 DS    XL25
MADADDR3 DS    XL25
MADADDR4 DS    0XL25
MADCITY  DS    XL18
MADSTATE DS    XL2
MADZIP   DS    XL5
         END
