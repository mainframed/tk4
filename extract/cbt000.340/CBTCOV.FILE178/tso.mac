*          DATA SET TP417000   AT LEVEL 005 AS OF 03/11/76
         TITLE 'TP417000 - TSO DISPLAY ROUTINE FOR MPS'
*              AUTHOR.   DON HIGGINS.
*              DATE.     03/01/76.
*              REMARKS.  DISPLAY FOLLOWING INFO FOR TSO
*                             1.   TSO SYSTEM STATUS
*                                  A.   ACTIVE/INACTIVE     SCVT
*                                  B.   MAX/CURRENT USERS   TSCVT 112
*                                  C.   REGIONS
*                                  D.   CPU USAGE
*                                  E.   BRDR ACTIVE/INACTIVE
*                             2.   USER STATUS
*                                  A.   USERID
*                                  B.   ELAPSED TIME
*                                  C.   LINEID AND STATUS
*
*              REFERENCES.
*                   1.   SY28-0649-0 TSO PLM
*                   2.   SY28-0606-0 SYSTEM DATA AREAS
*
*
*
*
*
*
         TITLE 'MAINLINE'
         PRINT NOGEN
TP417000 SUBENTRY
         LM    R3,R5,0(R1)
         MVC   TERM,0(R3)
         L     R2,16
         L     R2,CVTTSCVT(R2)
         IF    (LTR,R2,R2,Z)
               TPPUTMSG TERM,'TSO NOT ACTIVE'
               SUBEXIT
         FI
         IF    (TM,TSCVTFL1(R2),X'F0',NZ)
               TPPUTMSG TERM,'TSO IS STOPPING'
               SUBEXIT
         FI
         PM    TSO
         PM    REGIONS
         PM    USERS
         SUBEXIT
         TITLE 'TSO SYSTEM STATUS'
TSO      PENTRY
         LH    R0,TSCVTCUS(R2)     CURRENT USERS
         CVD   R0,PWORK
         EDIT  TO=DWORK,FROM=PWORK+6,MASK=' Z99'
         MVC   DCUS,DWORK+2
         LH    R0,TSCVTLUS(R2)     MAXIMUM USERS
         CVD   R0,PWORK
         EDIT  TO=DWORK,FROM=PWORK+6,MASK=' Z99'
         MVC   DLUS,DWORK+2
         LH    R0,TSCVTCTR(R2)
         CVD   R0,PWORK            REGIONS
         EDIT  TO=DWORK,FROM=PWORK+6,MASK=' Z99'
         MVC   DRGNS,DWORK+2
         L     R3,TSCVTICB(R2)     R3=A(DPA) SEE PLM PGS 132,112,88,87
         L     R3,DPADCA(R3)       R3=A(DCA)
         ZR    R0
         IC    R0,DCABKDG(R3)
         CVD   R0,PWORK
         EDIT  TO=DWORK,FROM=PWORK+6,MASK=' Z99'
         MVC   DBG,DWORK+2
         PM    CHKBRDR
         TPPUTMSG TERM,TSOMSG
         PEXIT
         TITLE 'CHKBRDR - VERIFY STATUS OF BATCH READER TASK'
CHKBRDR  PENTRY
         MVC   DBRDR,=CL10'BRDR DOWN'
         L     R1,16
         L     R1,CVTHEAD(R1)      TOP SYSTEM TCB
         L     R1,TCBOTC(R1)       MS IS MOTHER
         L     R1,TCBLTC(R1)       LAST DAUGHTER
         DO    WHILE,(LTR,R1,R1,NZ)
               L    R15,TCBTIO(R1)
               IF   (LTR,R15,R15,NZ),AND,(CLC,0(8,R15),=CL8'BRDR',EQ)
                    MVC  DBRDR,=CL10'BRDR ACTIVE'
               FI
               L    R1,TCBNTC(R1)
         OD
         PEXIT
         TITLE 'REGION STATUS MESSAGES'
REGIONS  PENTRY
         L     R3,TSCVTRCB(R2)
         LH    R4,TSCVTCTR(R2)
         DO    WHILE,(LTR,R4,R4,NZ)
               PM   REGION
               LA   R3,LRCB(R3)
               SI   R4,1
         OD
         PEXIT
         TITLE 'REGION MSG'
REGION   PENTRY
         ZR    R0
         IC    R0,RCBNMBR(R3)
         CVD   R0,PWORK
         EDIT  TO=DRGN,FROM=PWORK+7,MASK='09'
         LH    R0,RCBRSIZE(R3)
         SLL   R0,1
         CVD   R0,PWORK
         EDIT  TO=DRSIZE,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCBCUSE(R3)
         CVD   R0,PWORK
         EDIT  TO=DRUSE,FROM=PWORK+6,MASK=' Z99'
         MVC   DRCUSER,=CL8'SYSTEM'
         IF    (LTR,R0,R0,NZ)
               LH   R1,RCBTJID(R3)
               MH   R1,TSCVTSZU(R2)
               A    R1,TSCVTTJB(R2)
               MVC  DRCUSER,TJBUSER(R1)
         FI
         MVC   DRSTATUS,=CL10'ACTIVE'
         IF    (TM,RCBSWTCH(R3),X'18',NZ)
               MVC  DRSTATUS,=CL10'MCH/CHKS'
         ELSE
               IF   (TM,RCBFLG2(R3),X'04',O)
                    MVC  DRSTATUS,=CL10'RGN ABEND'
               ELSE
                    IF   (TM,RCBFLG2(R3),X'40',O)
                         MVC  DRSTATUS,=CL10'STOPPING'
                    ELSE
                         IF   (TM,RCBFLG2(R3),X'18',NZ)
                              MVC  DRSTATUS,=CL10'STARTING'
                         ELSE
                              IF   (TM,RCBFLG(R3),X'F0',NZ)
                                   MVC  DRSTATUS,=CL10'SWAPPING'
                              FI
                         FI
                    FI
               FI
         FI
         TPPUTMSG TERM,RGNMSG
         PEXIT
         TITLE 'USERS'
USERS    PENTRY
         L     R3,TSCVTTJB(R2)
         LH    R4,TSCVTNTJ(R2)
         L     R5,TSCVTICB(R2)
         L     R5,DPADCA(R5)
         L     R5,DCAUQEL0(R5)
         DO    WHILE,(LTR,R4,R4,NM)
               PM  USER
               AH  R3,TSCVTSZU(R2)
               LA   R5,LUQEL(R5)
               SI  R4,1
         OD
         PEXIT
         TITLE 'USER'
USER     PENTRY
         IF    (TM,TJBSTAT(R3),X'80',O)
               PEXIT
         FI
         MVC   DUSER,TJBUSER(R3)
         UNPK  DWORK(5),TJBLINE(3,R3)
         TR    DWORK(4),HEX-240
         MVC   DULINE,DWORK+1
         TIME  TU
         SL    R0,UQELITOD(R5)
         LR    R1,R0               R1=CONNECT TIME IN TU'S (26US)
         ZR    R0
         D     R0,=A(384*60*100)   R1=MINUTES
         CVD   R1,PWORK
         EDIT  TO=DUCONN,FROM=PWORK+6,MASK=' Z99'
         LH    R0,UQELSIZE(R5)
         SLL   R0,1
         CVD   R0,PWORK
         EDIT  TO=DUWSK,FROM=PWORK+6,MASK=' Z99'
         PM    USERSTAT
         TPPUTMSG TERM,USERMSG
         PEXIT
         TITLE 'USER STATUS'
USERSTAT PENTRY
         MVC   DUSTAT,=CL8' '
         IF    (TM,TJBSTAT(R3),X'40',O)      IN REGION
               IF   (TM,TJBSTAT(R3),X'20',O)
                    MVC  DUSTAT,=CL8'LOGON'
                    PEXIT
               FI
               IF   (TM,TJBSTAT(R3),X'18',NZ)
                    MVC  DUSTAT,=CL8'USER I/O'
                    PEXIT
               FI
               IF   (TM,TJBSTAT(R3),X'07',NZ)
                    MVC  DUSTAT,=CL8'LOGOFF'
                    PEXIT
               FI
               IF  (TM,TJBSTAT2(R3),X'02',O)
                    MVC  DUSTAT,=CL8'READY'
                    PEXIT
               FI
         FI
         IF    (TM,TJBSTAT2(R3),X'80',O)
               MVC  DUSTAT,=CL8'HUNG-UP'
               PEXIT
         FI
         IF    (TM,TJBSTAT2(R3),X'20',O)
               MVC  DUSTAT,=CL8'ABEND'
               PEXIT
         FI
         IF    (TM,TJBSTAT2(R3),X'10',O)
               MVC  DUSTAT,=CL8'MCK-HUNG'
               PEXIT
         FI
         IF    (TM,UQELFLGS(R5),X'40',O)
               MVC  DUSTAT,=CL8'TERM I/O'
               PEXIT
         FI
         IF    (TM,UQELFLGS(R5),X'30',NZ)
               MVC  DUSTAT,=CL8'SWAPPING'
               PEXIT
         FI
         PEXIT
         TITLE 'COMMON'
         EQUAL
TERM     DC    CL8' '
HEX      DC    C'0123456789ABCDEF'
PWORK    DC    D'0'
DWORK    DC    CL10' '
TSOMSG   DS    0CL60
         DC    C'TSO USERS='
DCUS     DC    CL2' ',C' MAX='
DLUS     DC    CL2' ',C' RGNS='
DRGNS    DC    CL2' ',C' BG='
DBG      DC    CL2' ',C'% '
DBRDR    DC    CL10' '
         FILL  TSOMSG
RGNMSG   DS    0CL60
         DC    C'RGN='
DRGN     DC    C'  ',C' SIZE='
DRSIZE   DC    CL4' ',C'K USERS='
DRUSE    DC    CL4' ',C' CUSER='
DRCUSER  DC    CL8' ',C' '
DRSTATUS DC    CL10' '
         FILL  RGNMSG
USERMSG  DS    0CL60
         DC    C'USER='
DUSER    DC    CL8' ',C' CONNECT='
DUCONN   DC    CL4' ',C' MIN  LINE='
DULINE   DC    CL3' ',C' WS='
DUWSK    DC    CL4' ',C'K '
DUSTAT   DC    CL8' '
         FILL  USERMSG
CVTTSCVT EQU   228  SDA  16
TSCVTFL1 EQU   14   PLM  112
TSCVTCUS EQU   20   PLM  112
TSCVTLUS EQU   22   PLM  112
TSCVTCTR EQU   28   PLM  112
TSCVTICB EQU   44   PLM  112
DPADCA   EQU   16   PLM  88
DCATSTME EQU   24   PLM  87
DCABKDG  EQU   3    PLM 87
CVTHEAD  EQU   160  SDA  15
TCBOTC   EQU   132  SDA  219
TCBLTC   EQU   136  SDA  219
TCBNTC   EQU   128  SDA  219
TCBTIO   EQU   12   SDA  216
TSCVTRCB EQU   4    PLM  112
LRCB     EQU   76   PLM  101
RCBNMBR  EQU   18   PLM  99
RCBRSIZE EQU   14   PLM  99
RCBCUSE  EQU   22   PLM  99
RCBTJID  EQU   12   PLM  99
TSCVTTJB EQU   0    PLM  112
TSCVTSZU EQU   26   PLM  112
TJBUSER  EQU   28   PLM  118
RCBSWTCH EQU   59   PLM  101
RCBFLG2  EQU   20   PLM  99
RCBFLG   EQU   19   PLM  99
TSCVTNTJ EQU   24   PLM  112
TJBSTAT  EQU   6    PLM  117
TJBLINE  EQU   46   PLM  118
DCAUQEL0 EQU   40   PLM  87
LUQEL    EQU   28   PLM  123
UQELITOD EQU   24   PLM  123
UQELSIZE EQU   16   PLM  123
TJBSTAT2 EQU   7    PLM  123
UQELFLGS EQU   4    PLM  123
         END
