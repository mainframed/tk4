DS@DEALC TITLE 'DE-ALLOCATE A DATASET'
***********************************************************************
*                                                                     *
* MODULE: DS@DEALC                                                    *
*                                                                     *
* DESCRIPTION: DEALLOCATE A DATA SET VIA DDNAME DEALLOCATION          *
*                                                                     *
* INPUT PARAMETERS                                                    *
*                                                                     *
*  R1 --> POINTS TO ADDRESS OF PARAMETER BLOCK (DS@ALCPB)             *
*         THE DDNAME TO UNALLOCATE IS IN THE PARAMETER BLOCK          *
*                                                                     *
* REGISTER USAGE                                                      *
*                                                                     *
*  R0-R10 WORK REGISTERS                                              *
*  R11    PROGRAM BASE                                                *
*  R12    BASE FOR PARAMETER BLOCK                                    *
*  R13-R15 STANDARD USAGE                                             *
*                                                                     *
* NOTE:                                                               *
*  THE CALLER MUST PROVIDE THE WORKAREA FOR THIS PROGRAM (IT SHOULD   *
*  BE PART OF A DSA FOR THIS PROGRAM TO BE REENTRANT/REUSEABLE). THE  *
*  LENGTH OF THIS AREA IS GIVEN BY THE EQUATED SYMBOL 'LNGDSA'.       *
*                                                                     *
* RETURN CODE                                                         *
*  R15 =  ALWAYS ZERO                                                 *
*                                                                     *
* ATTRIBUTES                                                          *
*  REENTRANT AND REUSEABLE                                            *
*                                                                     *
* AUTHOR:   PHIL HEROLD, SYSTEMS DEVELOPER                            *
*           SAS INSTITUTE                                             *
*           CARY, NC                                                  *
*                                                                     *
* NOTE:  SAS INSTITUTE IS NOT RESPONSIBLE FOR THE SUPPORT OF THIS     *
*        PROGRAM--PLEASE DO NOT CALL SAS INSTITUTE TECHNICAL SUPPORT. *
*        HOWEVER, THE AUTHOR WILL BE GLAD TO PROVIDE ANY ASSISTANCE   *
*        NEEDED.                                                      *
*                                                                     *
*                                                                     *
* ACKNOWLEDGEMENT:  THIS PROGRAM WAS ORIGINALLY DEVELOPED AT FIRST    *
*                   COMPUTER SERVICES, CHARLOTTE, NORTH CAROLINA, AND *
*                   WAS MODIFIED FOR USE IN THE ENVIRONMENT IN WHICH  *
*                   IT NOW RUNS.  THE AUTHOR WOULD LIKE TO ESPECIALLY *
*                   THANK DAVE LASALLE FOR HIS SUPPORT AND ENCOURAGE- *
*                   MENT.                                             *
*                                                                     *
***********************************************************************
         COPY  DS@MACS
         TITLE 'INPUT PARAMETER BLOCK'
***********************************************************************
*               P A R A M E T E R     B L O C K                       *
***********************************************************************
         SPACE
         DS@ALCPB DSECT=YES
         TITLE 'VARIOUS OTHER DSECTS'
***********************************************************************
*                  O T H E R      D S E C T S                         *
***********************************************************************
         PRINT NOGEN
         IEFZB4D0
         IEFZB4D2
         TITLE 'WORKAREA (DSA)'
***********************************************************************
*                 D S A       D E F I N I T I O N S                   *
***********************************************************************
         SPACE
DS@ALDSA DSECT                        DSA FOR THIS PROGRAM
SAVEAREA DC    18F'0'                 SAVE AREA FOR REGISTERS
         SPACE
SVC99RB  DC    X'80',AL3(0)           SVC 99 REQUEST BLOCK POINTER
         SPACE
REQBLK   DS    0D                     SVC 99 REQUEST BLOCK
RBLN     DC    AL1(20)
RBVERB   DC    AL1(1)
RBFLAG1  DC    AL2(0)
RBERROR  DC    XL2'0'
RBINFO   DC    XL2'0'
RBTXTPP  DC    A(0)
RBRSVD   DC    F'0'
RBFLAG2  DC    F'0'
         SPACE
ATUDDNAM DC    Y(DUNDDNAM),AL2(1),AL2(8),CL8' '
ATUUNALC DC    Y(DUNUNALC),AL2(0)
         DS    0F
TXTPTRS  DC    8F'0'                  TEXT UNIT PTR
         SPACE
         DS    0D
LNGDSA   EQU   *-DS@ALDSA
         TITLE 'MAINLINE CODE'
**********************************************************************
*                       D S @ D E A L C                              *
**********************************************************************
         SPACE
DS@DEALC CSECT
         PRINT GEN
         USING *,R15
         DS@START ,
         PRINT NOGEN
         LR    R11,R15                LOAD FIRST BASE REGISTER
         DROP  R15                    DROP TEMPORARY BASE
         USING DS@DEALC,R11
         L     R12,0(R1)              ADDRESS PARAMETER BLOCK
         USING DS@ALCPB,R12
         L     R1,ALCPBDS@            ADDRESS WORK AREA
         ST    R13,4(0,1)             CHAIN SAVE AREAS
         ST    R1,8(0,13)
         LR    R13,R1
         USING DS@ALDSA,R13
         MVC   SAVEAREA(4),=C'DEAL'
         EJECT
BLDPARM  DS    0H
         MVC   REQBLK(REQBLKL),REQBLKM MOVE IN REQUEST BLOCK MASK
         MVC   ATUDDNAM(TXTUNITL),TXTUNITS MOVE IN TEXT UNITS MASK
         LA    R1,REQBLK              ADDRESS REQUEST BLOCK
         ST    R1,SVC99RB             SAVE FOR SVC99
         MVI   SVC99RB,X'80'          SET VL INDICATOR
         LA    R2,TXTPTRS             ADDRESS TEXT UNIT POINTERS
         ST    R2,RBTXTPP             SAVE IN REQUEST BLOCK
         LA    R1,ATUDDNAM            ADDRESS TEXT UNIT
         ST    R1,0(R2)               SAVE TEXT UNIT FOR DDNAME
         LA    R2,4(,R2)              ADDRESS NEXT TEXT UNIT SPOT
         LA    R1,ATUUNALC            ADDRESS NEXT TEXT UNIT
         ST    R1,0(R2)               SAVE IN REQUEST BLOCK
         MVI   0(R2),X'80'            SET VL INDICATOR
         MVI   RBVERB,S99VRBUN        SET VERB FOR UNALLOCATION
         MVC   ATUDDNAM+6(8),ALCPBDDN MOVE DDNAME FROM PARM BLOCK
         LA    R1,SVC99RB             ADDRESS PARAMETER LIST
         SVC   99                     ISSUE SVC 99
         L     R13,4(,R13)
         RETURN (14,12),T,RC=(15)     RETURN TO CALLER
         TITLE 'STATIC DEFINITIONS'
**********************************************************************
*              S T A T I C       D E F I N I T I O N S               *
**********************************************************************
         SPACE
REQBLKM  DS    0D                     SVC 99 REQUEST BLOCK
         DC    AL1(20)
         DC    AL1(1)
         DC    AL2(0)
         DC    XL2'0'
         DC    XL2'0'
         DC    A(0)
         DC    F'0'
         DC    F'0'
REQBLKL  EQU   *-REQBLKM             LENGTH OF REQUEST BLOCK
         SPACE
TXTUNITS DC    Y(DUNDDNAM),AL2(1),AL2(8),CL8' '
         DC    Y(DUNUNALC),AL2(0)
TXTUNITL EQU   *-TXTUNITS
BLANKS   DC    CL8' '
         LTORG
         END   DS@DEALC
