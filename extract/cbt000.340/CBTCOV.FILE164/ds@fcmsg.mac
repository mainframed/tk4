DS@FCMSG TITLE 'DS@FCMSG - MESSAGE SYSTEM FOR DS@FLSTC'
DS@FCMSG CSECT
         SPACE
***********************************************************************
*                                                                     *
*  PROGRAM-ID:  DS@FCMSG                                              *
*                                                                     *
*  FUNCTION:                                                          *
*      MESSAGE SUBSYSTEM FOR DS@FLSTC (FULISTC - FULL SCREEN LIST CAT)*
*                                                                     *
*  INPUT:                                                             *
*      INPUT IS THE MESSAGE PARAMETER BLOCK, WHICH CONTAINS THE       *
*      MESSAGE TABLE ID , THE MESSAGE ID (A MULTIPLE OF 4), AND A     *
*      DSA ADDRESS.  IF THE DSA ADDRESS IS NOT GIVEN, THIS ROUTINE    *
*      WILL GET A DSA AND FREE IT FOR EACH INVOCATION.  IF THE DSA    *
*      ADDRESS IS TO BE GIVEN EACH TIME, IT SHOULD BE THE SAME AREA,  *
*      AND SHOULD BE RESERVED FOR EXCLUSIVE USE OF THE MESSAGE SUB-   *
*      SYSTEM.  IT ALSO SHOULD BE ZEROED FOR THE FIRST CALL.          *
*                                                                     *
*  REGISTER USAGE:                                                    *
*      R11 - BASE REGISTER                                            *
*      R13-R15 STANDARD USAGE                                         *
*                                                                     *
*  SPECIAL NOTES:                                                     *
*      THIS PROGRAM IS REENTRANT, REUSEABLE.                          *
*                                                                     *
***********************************************************************
         COPY  DS@MACS
         TITLE 'OTHER EQUATES'
***********************************************************************
*                  O T H E R    E Q U A T E S                         *
***********************************************************************
         SPACE
FLAGON   EQU   255
FLAGOFF  EQU   0
         TITLE 'MESSAGE PARAMETER BLOCK'
***********************************************************************
*              P A R A M E T E R      B L O C K                       *
***********************************************************************
         SPACE
         DS@MSGPB DSECT=YES
         TITLE 'MESSAGE FORMAT'
***********************************************************************
*                 M E S S A G E      F O R M A T                      *
***********************************************************************
         SPACE
MSGFORM  DSECT                        MAPS MESSAGE IN TABLE(S) BELOW
MSGHELP  DS    CL8                      NAME OF HELP SCREEN (OR BLANK)
MSGALARM DS    CL3                      ALARM (YES OR NO)
MSGLLNG  DS    AL1                      LENGTH OF LONG MESSAGE
MSGSMSG  DS    CL24                     SHORT MESSAGE
MSGLMSG  DS    0C                       LONG MESSAGE
         TITLE 'DSA'
***********************************************************************
*                          D S A    M A P                             *
***********************************************************************
         SPACE
DSA      DSECT
SAVEAREA DS    18F                    SAVE AREA
DV14SAVE DS    F                      REG 14 SAVE AREA FOR DEFVARS SUB.
GM14SAVE DS    F                      REG 14 SAVE AREA FOR GETMSG  SUB.
BM14SAVE DS    F                      REG 14 SAVE AREA FOR BLDMSG  SUB.
MSG@     DC    A(0)                   ADDRESS OF CURRENT MESSAGE
VARTBLA  DC    3A(0)                  PARM LIST FOR VARIABLE SUBROUTINE
         SPACE
NOT1STM  DC    AL1(0)                 NOT FIRST MSG CALL IF FLAGON
ALLOCDSA DC    AL1(0)                 WE ALLOCATED OUR DSA
         SPACE
CALLPL   CALL  ,(,,,,),MF=L           CALL PARM LIST
         SPACE
*                                     THE FOLLOWING ARE VDEFINED
DS@SMSG  DC    CL24' '                  SHORT MESSAGE
DS@LMSG  DC    CL78' '                  LONG MESSAGE
DS@ALARM DC    C'YES'                   ALARM (YES OR NO)
DS@HELP  DC    CL8' '                   NAME OF HELP PANEL
         SPACE
LNGTBL   DC    4F'0'                  LENGTH FOR ABOVE VARIABLES
         SPACE
VARCN    DC    CL8' '                 NAME OF VARIABLE TO VCOPY
VARCL    DC    F'0'                   LENGTH OF VARIABLE COPIED
VARC@    DC    A(0)                   ADDRESS OF VARIABLE COPIED
         SPACE
DSALNG   EQU   *-DSA
         TITLE 'MAINLINE CODE'
***********************************************************************
*              D S @ F C M S G     P R O G R A M                      *
***********************************************************************
         SPACE
DS@FCMSG CSECT
         PRINT GEN
         USING *,R15
         DS@START ,
         PRINT GEN
         DROP  R15                    DROP TEMPORARY BASE
         LR    R11,R15                SET BASE REGISTER
         USING DS@FCMSG,R11
         L     R12,0(R1)              ADDRESS PARAMETER BLOCK
         USING DS@MSGPB,R12
         L     R1,MSGDSA@             GET DSA ADDRESS
         LA    R3,DSALNG              GET DSA LENGTH
         LTR   R1,R1                  IF WE DON'T HAVE DSA, THEN
         BNZ   MSG00
         GETMAIN RC,LV=(R3)             GETMAIN HAD BETTER WORK
         LR    R2,R1                    GET DSA ADDRESS
         SLR   R5,R5                    CLEAR FOR MVCL
         MVCL  R2,R4                    AND ZERO IT OUT
         USING DSA,R1                   TEMPORARY USING
         MVI   ALLOCDSA,FLAGON          SAY WE ALLOCATED OUR DSA
         DROP  R1
MSG00    DS    0H
         ST    R13,4(0,R1)            SET BACKWARD CHAIN
         ST    R1,8(0,R13)            SET FORWARD CHAIN
         LR    R13,R1                 GET DSA BASE
         USING DSA,R13
         MVC   SAVEAREA(4),=C'MSGS'   SET OUR DSA IDENTIFIER
         CLI   NOT1STM,FLAGON         IF FIRST MSG, THEN
         BE    MSG10
         DEFINE DEFVTBL,LNGTBL,DS@SMSG  DEFINE DIALOG VARIABLES
MSG10    DS    0H
         BAL   R14,GETMSG             GO GET THE MESSAGE TEXTS
         LA    R2,=CL8'DS@M001'       ASSUME HELP SCREEN MESSAGE
         CLC   DS@HELP,=CL80' '       IF ^HELP SCREEN, THEN
         BNE   *+8
         LA    R2,=CL8'DS@M002'         NO HELP SCREEN MESSAGE
         CALL  ISPLINK,(=CL8'SETMSG',(R2)),VL,MF=(E,CALLPL)
**********************************************************************
*     FREE OUR DSA, IF WE ALLOCATED IT.                              *
**********************************************************************
         CLI   ALLOCDSA,FLAGON        IF WE ALLOCATED OUR DSA
         BNE   RETURN
         LA    R2,MSGVARS
         CALL  ISPLINK,(=CL8'VDELETE',(R2)),VL,MF=(E,CALLPL)
         LR    R2,R13                   COPY DSA ADDRESS
         LA    R1,DSALNG                GET DSA LENGTH
         L     R3,4(0,R13)              COPY REGISTER 13
         FREEMAIN RC,LV=(R1),A=(R2)     FREE OUR DSA
         LR    R13,R3                   RESTORE PREVIOUS SAVE AREA
         B     RETURN1                  WE ARE DONE
RETURN   DS    0H
         L     R13,4(0,R13)            RESTORE REGISTER 13
RETURN1  DS    0H
         L     R14,12(0,R13)           RESTORE RETURN REGISTER
         LM    R0,R12,20(R13)          RESTORE CALLER'S REGISTERS
         BR    R14                     AND LEAVE
         SPACE
MSGVARS  DC    CL8'DS@SMSG DS@LMSG DS@ALARM DS@HELP'
         TITLE 'GET THE MESSAGE'
**********************************************************************
*              G E T M S G         S U B R O U T I N E               *
*                                                                    *
*    THIS SUBROUTINE GETS THE MESSAGE FROM THE PROPER MESSAGE TABLE, *
*  AND PERFORMS ANY VARIABLE SUBSTITUTION, SETTING THE DIALOG        *
*  VARIABLES ALREADY DEFINED THAT ARE PART OF OUR MESSAGE.  (DIALOG  *
*  MANAGER WILL NOT SUBSTITUTE DIALOG VALUES INTO A MESSAGE THAT IS  *
*  ITSELF DEFINED AS A DIALOG VARIABLE, HENCE WE HAVE TO DO IT OUR-  *
*  SELVES.  THIS MAY BE A BUG, BUT I DOUBT IT).                      *
*                                                                    *
**********************************************************************
         SPACE
GETMSG   DS    0H
         ST    R14,GM14SAVE           SAVE RETURN REGISTER
         LM    R2,R3,MSGTB            GET INPUT PARMS
         C     R2,MAXMSGTB            IF MSGTBID GREATER THAN MAX
         BNH   *+6
         DC    H'0'                     PROGRAMMING ERROR, SO DIE
         BCTR  R2,0                   WE ARE ZERO BASED HERE
         BCTR  R3,0
         SLL   R2,2                   GET OFFSET INTO THE TWO TABLES
         SLL   R3,2
         LA    R4,MSGTBL@             ADDRESS TABLE OF MESSAGE TBLS
         AR    R4,R2                  ADD OFFSET
         L     R4,0(R4)               ADDRESS CORRECT MESSAGE TABLE
         AR    R4,R3                  ADD OFFSET
         L     R4,0(R4)               NOW WE HAVE BASE FOR MESSAGE
         ST    R4,MSG@                SAVE IT
         USING MSGFORM,R4
         MVC   DS@ALARM,MSGALARM      MOVE ALARM VALUE IN
         MVC   DS@HELP,MSGHELP        MOVE IN HELP SCREEN NAME
         MVC   DS@SMSG,=CL80' '       BLANK OUT THESE FIELDS
         MVC   DS@LMSG,=CL80' '
         LA    R3,MSGSMSG             ADDRESS SHORT MESSAGE FOR SUB
         LA    R4,DS@SMSG             ADDRESS PLACE FOR MESSAGE
         L     R5,=AL4(L'MSGSMSG)     SET LENGTH FOR SCAN
         L     R6,=AL4(L'DS@SMSG)     SET MAX LENGTH FOR SUB.
         BAL   R14,BLDMSG             BUILD THE MESSAGE
         L     R4,MSG@                RESTORE MESSAGE BASE
         SLR   R5,R5                  CLEAR REG FOR IC
         IC    R5,MSGLLNG             SET LENGTH FOR SCAN
         LA    R3,MSGLMSG             ADDRESS SHORT MESSAGE FOR SUB
         LA    R4,DS@LMSG             ADDRESS PLACE FOR MESSAGE
         L     R6,=AL4(L'DS@LMSG)     SET MAX LENGTH FOR SUB.
         BAL   R14,BLDMSG             BUILD THE MESSAGE
         L     R14,GM14SAVE           RESTORE RETURN REGISTER
         BR    R14                    RETURN TO CALLER
         TITLE 'BUILD THE MESSAGE, SUBSTITUTING ANY VARIABLES'
**********************************************************************
*              B L D M S G         S U B R O U T I N E               *
*                                                                    *
*    THIS SUBROUTINE BUILDS A MESSAGE.  UPON ENTRY, R3 POINTS TO THE *
*  MESSAGE IN THE TABLE, R4 POINTS TO THE PLACE TO PUT THE BUILT     *
*  MESSAGE, R5 CONTAINS THE LENGTH OF THE MESSAGE IN THE TABLE, AND  *
*  R6 CONTAINS THE MAXIMUM LENGTH FOR THE BUILT MESSAGE.  BASICALLY, *
*  THIS ROUTINE SCANS THE MESSAGE FOR VARIABLES TO BE SUBSTITUTED    *
*  (INDICATED BY THE &, OF COURSE).  WHEN ONE IS FOUND, WE VCOPY THE *
*  VARIABLE, AND PLACE IT IN THE OUTPUT MESSAGE AREA ALONG WITH THE  *
*  REST OF THE TEXT.                                                 *
*                                                                    *
**********************************************************************
         SPACE
BLDMSG   DS    0H
         ST    R14,BM14SAVE           SAVE RETURN REGISTER
         AR    R5,R3                  CALCULATE ENDING INPUT ADDRESS
BLDMLP1  DS    0H
         CR    R3,R5                  DO WHILE (LENGTH LEFT > 0)
         BNL   BLDMSGX
         LTR   R6,R6                    IF NO LENGTH FOR OUTPUT, THEN
         BZ    BLDMSGX                    LEAVE
         LR    R1,R5                    ELSE COPY STARTING POINT
         SR    R1,R3                    CALCULATE LENGTH FOR TRT
         BCTR  R1,0                     GET EXECUTE LENGTH
         EX    R1,TRTAMPX
         BZ    BLDMLP1X                 IF & FOUND, THEN
         LR    R2,R1                      COPY ADDRESS OF &
         SR    R1,R3                      CALCULATE LENGTH UP TO &
         CR    R1,R6                      IF LENGTH > THEN OUTPUT LEFT,
         BNH   *+6
         LR    R1,R6                        SET IT TO OUTPUT LNG LEFT
         SR    R6,R1                      ADJUST OUTPUT LENGTH LEFT
         LTR   R1,R1                      IF & NOT IN 1ST POSITION,
         BZ    BLDML03
         BCTR  R1,0                         MAKE IT AN EXECUTE LENGTH
         EX    R1,MVCMSG                    MOVE TEXT B4 & TO OUTPUT
         LA    R4,1(R1,R4)                  GET PAST TEXT JUST MOVED
BLDML03  DS    0H                         ELSE
         LA    R3,1(,R2)                  GET CURRENT POSITION
         CLI   0(R3),C'&&'                IF DOUBLE &
         BNE   BLDML05
         MVI   0(R4),C'&&'                  MOVE SINGLE & TO OUTPUT
         LA    R4,1(,R4)                    GET PAST & IN OUTPUT
         LA    R3,1(,R3)                    GET PAST && IN INPUT
         B     BLDMLGO
BLDML05  DS    0H                         ELSE
         LR    R1,R5                        READY TO EXTRACT VAR. NAME
         SR    R1,R3                        CALCULATE LENGTH LEFT
         BCTR  R1,0                         MAKE IT AN EXECUTE LENGTH
         EX    R1,TRTNONAX                  FIND NON-ALPHA AFTER NAME
         BNZ   BLDML07                      IF NOT FOUND, THEN
         LR    R2,R5                          ACT LIKE WE'RE DONE
         B     BLDML10
BLDML07  DS    0H                           ELSE (GOT A BLANK)
         LR    R2,R1                          ADDRESS IT
         SR    R1,R3                          GET LENGTH OF VAR. NAME
         LTR   R1,R1                        IF BLANK FOLLOWING &,
         BZ    BLDMLGO                        NOTHING TO MOVE
         BCTR  R1,0                         SET EX LENGTH
BLDML10  DS    0H
         MVC   VARCN,=CL80' '               BLANK OUT RECEIVING FIELD
         EX    R1,MVCVARCN                  MOVE TO VARIABLE
         LR    R3,R2                        GET PAST VAR. NAME IN MSG
         LA    R7,VARCN                     ADDRESS VAR NAME
         LA    R8,VARCL                     ADDRESS PLACE FOR LENGTH
         LA    R9,VARC@                     ADDRESS PLACE FOR ADDR
         CALL  ISPLINK,(=CL8'VCOPY',(R7),(R8),(R9),=CL8'LOCATE'),VL,   +
               MF=(E,CALLPL)
         L     R1,VARCL                     GET LENGTH OF VARIABLE
         LTR   R1,R1                        IF FOUND, THEN
         BZ    BLDMLGO
         CR    R1,R6                          BE SURE WE DON'T OVERFLOW
         BNH   *+6
         LR    R1,R6
         SR    R6,R1                          ADJUST LENGTH LEFT
         BCTR  R1,0                           SET EXECUTE LENGTH
         L     R7,VARC@                       ADDRESS VARIABLE
         EX    R1,MVCVARC                     MOVE TO MESSAGE
         LA    R4,1(R1,R4)                    GET PAST TEXT MOVED
BLDMLGO  DS    0H
         B     BLDMLP1                    END OF LOOP
BLDMLP1X DS    0H                       MOVE WHAT'S LEFT TO OUTPUT
         LR    R1,R5                    COPY END ADDRESS
         SR    R1,R3                    SUBTRACT CURRENT TO GET LNG
         CR    R1,R6                    IF LENGTH > THEN OUTPUT LEFT,
         BNH   *+6
         LR    R1,R6                      SET IT TO OUTPUT LNG LEFT
         BCTR  R1,0                     SET EXECUTE LENGTH
         EX    R1,MVCMSG                MOVE TO OUTPUT
BLDMSGX  DS    0H
         L     R14,BM14SAVE           RESTORE RETURN REGISTER
         BR    R14                    RETURN TO CALLER
         SPACE
TRTAMPX  TRT   0(0,R3),TRTAMP         EXECUTED
TRTNONAX TRT   0(0,R3),TRTNONA          INSTRUCTIONS
MVCMSG   MVC   0(0,R4),0(R3)
MVCVARCN MVC   VARCN(0),0(R3)
MVCVARC  MVC   0(0,R4),0(R7)
         SPACE
TRTAMP   DC    XL256'0'               TRANSLATE TABLE FOR AMPERSAND
         ORG   TRTAMP+C'&&'
         DC    X'01'
         ORG
TRTNONA  DC    256X'FF'               TRANSLATE TABLE FOR NON-ALPHA
         ORG   TRTNONA+C'$'
         DC    X'0'
         ORG   TRTNONA+C'@'
         DC    X'0'
         ORG   TRTNONA+C'A'
         DC    9X'0'
         ORG   TRTNONA+C'J'
         DC    9X'0'
         ORG   TRTNONA+C'S'
         DC    8X'0'
         ORG   TRTNONA+C'0'
         DC    10X'0'
         ORG
         TITLE 'DEFINE ISPF DIALOG VARIABLES SUBROUTINE'
**********************************************************************
*              D E F V A R S       S U B R O U T I N E               *
*                                                                    *
*    THIS SUBROUTINE CYCLES THROUGHT THE NECESSARY TABLES IN ORDER   *
*  TO EFFICIENTLY ISSUE VDEFINE CALLS.                               *
*  INPUT TO THIS ROUTINE IS VARTBLA, WHICH CONTAINS THREE ADDRESSES. *
*  THE FIRST ADDRESS POINTS TO A TABLE THAT CONTAINS VARIOUS INFOR-  *
*  MATION ABOUT THE VARIABLES TO VDEFINE; THE 2ND  ADDRESS POINTS    *
*  TO A LENGTH TABLE WHERE THE VARIABLE LENGTHS VDEFINED WILL BE     *
*  PLACED (IN CONSECUTIVE FULLWORDS); THE THIRD ADDRESS POINTS TO    *
*  THE FIRST VARIABLE ADDRESS IN THE DSA.                            *
*                                                                    *
**********************************************************************
         SPACE
DEFVARS  ST    R14,DV14SAVE           SAVE RETURN REGISTER
         LM    R2,R4,VARTBLA          LOAD ADDRESSES FROM PARM LIST
DVLOOP   DS    0H
         SLR   R5,R5                  CLEAR REGISTER 2
         IC    R5,0(R2)               GET LENGTH FOR VARIABLE
         LTR   R5,R5                  IF ZERO, THEN
         BZ    DVEXIT                   WE ARE DONE
         ST    R5,0(R3)               SAVE VARIABLE LENGTH IN TABLE
         LH    R5,2(R2)               GET OFFSET FOR VARIABLE
         AR    R5,R4                  ADD TO FIRST VARIABLE
         LA    R6,4(R2)               ADDRESS VARIABLE NAME
         LA    R7,=C'CHAR'            ADDRESS THIS FORMAT
         CLI   1(R2),CHAR             IF NOT CHAR FORMAT, THEN
         BE    DVVDEF
         LA    R7,=C'FIXED'             TRY FIXED FORMAT
         CLI   1(R2),FIXED              IF NOT FIXED FORMAT, THEN
         BE    DVVDEF
         LA    R7,=C'BIT'                 TRY BIT FORMAT
         CLI   1(R2),BIT                  IF NOT BIT FORMAT, THEN
         BE    DVVDEF
         LA    R7,=C'HEX'                   TRY HEX FORMAT
         CLI   1(R2),HEX                    IF NOT HEX FORMAT, THEN
         BE    DVVDEF
         DC    H'0'                           DIE 0C1
DVVDEF   DS    0H
         CALL  ISPLINK,(=CL8'VDEFINE',(R6),(R5),(R7),(R3)),VL,         +
               MF=(E,CALLPL)          ISSUE THE VCOPY
         LA    R2,VTBLLNG(,R2)        NEXT ELEMENT IN THE TABLE
         LA    R3,4(,R3)              NEXT ONE IN LENGTH TABLE
         B     DVLOOP                 AND LOOP
DVEXIT   DS    0H
         L     R14,DV14SAVE           GET RETURN REGISTER
         BR    R14                    RETURN TO CALLER
         TITLE 'LITERAL POOL'
**********************************************************************
*                       L I T E R A L S                              *
**********************************************************************
         SPACE
         LTORG
         TITLE 'DIALOG VARIABLE TABLE'
**********************************************************************
*                V A R I A B L E     T A B L E                       *
**********************************************************************
         SPACE
DEFVTBL  DC    AL1(L'DS@SMSG),AL1(CHAR),AL2(DS@SMSG-DS@SMSG)
         DC    CL10'(DS@SMSG)'
VTBLLNG  EQU   *-DEFVTBL
         DC    AL1(L'DS@LMSG),AL1(CHAR),AL2(DS@LMSG-DS@SMSG)
         DC    CL10'(DS@LMSG)'
         DC    AL1(L'DS@ALARM),AL1(CHAR),AL2(DS@ALARM-DS@SMSG)
         DC    CL10'(DS@ALARM)'
         DC    AL1(L'DS@HELP),AL1(CHAR),AL2(DS@HELP-DS@SMSG)
         DC    CL10'(DS@HELP)'
         DC    AL2(0)                 INDICATES END OF TABLE
         SPACE
CHAR     EQU   0
FIXED    EQU   4
BIT      EQU   8
HEX      EQU   12
         TITLE 'MESSAGE TABLES'
**********************************************************************
*                 M E S S A G E      T A B L E S                     *
**********************************************************************
         SPACE
MAXMSGTB DC    A(1)                   MAXIMUM NUMBER OF MSG TABLES
         SPACE
MSGTBL@  DC    A(FCMSGTBL)
         SPACE
         DS@MSG SMSG='             No Datasets',HELP=DST@FC20,         +
               LMSG='No datasets matched the given Catalog Prefix.'
         DS@MSG SMSG=
         DS@MSG SMSG='           Catalog Error',                       +
               LMSG='An error occurred reading the catalog.'
         DS@MSG SMSG='         Dataset Deleted',                       +
               LMSG='''&&TBDSNAME'' was deleted.'
         DS@MSG SMSG='      Dataset Compressed',                       +
               LMSG='''&&TBDSNAME'' was compressed.'
         DS@MSG SMSG='      Super Locate Error',                       +
               LMSG='An error occurred during Super Locate (SVC 26).'
         DS@MSG SMSG='         Dataset Renamed',                       +
               LMSG='''&&TBDSNAME'' was renamed.'
         DS@MSG SMSG=
         DS@MSG SMSG='     Invalid Select Code',HELP=DST@FC40,         +
               LMSG='Valid Select Codes are S/B, E, M, R, D and C.'
         DS@MSG SMSG=
         DS@MSG SMSG='           Invalid DSORG',                       +
               LMSG='Select Code invalid for ''&&TBDSNAME'' with DSORG +
               &&TBDSORG.'
         DS@MSG SMSG='          Already Exists',                       +
               LMSG='&&DSN already exists--Rename fails.'
         DS@MSG SMSG='         Dataset Deleted',                       +
               LMSG='''&&TBDSNAME'' has already been deleted.'
         DS@MSG SMSG='   Dataset Name Required',HELP=DST@FC50,         +
               LMSG='Dataset name required for command.'
         DS@MSG SMSG='       Dataset Not Found',                       +
               LMSG='&&TBDSNSH not listed.'
         DS@MSG SMSG=
         DS@MSG SMSG=
         DS@MSG SMSG='      Tape Not Supported',                       +
               LMSG='ISPF does not support datasets on tape.'
         DS@MSG SMSG=
         DS@MSG SMSG='          Compress Fails',                       +
               LMSG='''&&TBDSNAME'' - &&ERRFIELD.'
         DS@MSG SMSG='   Invalid Sort Field(s)',HELP=DST@FC52,         +
               LMSG='Sort field(s) entered are invalid.'
         DS@MSG SMSG='         Invalid Command',HELP=DST@FC40,         +
               LMSG='The command entered is invalid.'
         DS@MSG SMSG=
         DS@MSG SMSG='       Dataset Not Found',                       +
               LMSG='''The dataset was cataloged, but not found on any +
               volume.'
         DS@MSG SMSG='    Multi-volume Dataset',HELP=DST@FC43,         +
               LMSG='FSLISTC unable to perform command for this multi-v+
               olume dataset.'
         DS@MSG SMSG='          Dataset In Use',                       +
               LMSG='Command failed because ''&&TBDSNAME'' is in use.'
         DS@MSG SMSG='            Delete Fails',                       +
               LMSG='You are not authorized to delete ''&&TBDSNAME''.'
         DS@MSG SMSG='            Delete Fails',                       +
               LMSG='''&&TBDSNAME'' was not deleted.'
         DS@MSG SMSG='            Rename Fails',                       +
               LMSG='You are not authorized to rename ''&&TBDSNAME''.'
         DS@MSG SMSG='            Rename Fails',                       +
               LMSG='''&&TBDSNAME'' was not renamed.'
         DS@MSG SMSG='          Compress Fails',                       +
               LMSG='You are not authorized to compress ''&&TBDSNAME''.+
               '
         DS@MSG SMSG='        Dataset Released',                       +
               LMSG='Space was released from ''&&TBDSNAME''.'
         DS@MSG SMSG='      Display Incomplete',                       +
               LMSG='Attention Interrupt processed--display is incomple+
               te.'
         DS@MSG SMSG='  Specify Catalog Prefix',                       +
               LMSG='The command entered requires a new Catalog Prefix.+
               '
         DS@MSG SMSG='      Rename Not Allowed',                       +
               LMSG='Rename not allowed for wildcard Catalog Prefix.'
         SPACE 2
FCMSGTBL DS@MSG@ ,
         END
