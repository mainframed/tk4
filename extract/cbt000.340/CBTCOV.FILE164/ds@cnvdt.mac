DS@CNVDT CSECT
         SPACE
***********************************************************************
*                                                                     *
*  PROGRAM-ID:  DS@CNVDT                                              *
*                                                                     *
*  FUNCTION:                                                          *
*      CONVERT A DATE FROM JULIAN (3-BYTES PACKED DECIMAL FORMAT) TO  *
*      GREGORIAN (8 BYTES PRINTABLE EBCDIC IN THE FORM YY/MM/DD), OR  *
*      CONVERT FROM GREGORIAN TO JULIAN (SAME FORMAT).                *
*                                                                     *
*  INPUT:                                                             *
*      UPON INPUT, REGISTER 1 POINTS TO THE ADDRESS OF THE DS@CNDPB   *
*      PARAMETER BLOCK.                                               *
*                                                                     *
*  REGISTER USAGE:                                                    *
*      R11 - BASE REGISTER                                            *
*      R12 - BASE FOR PARAMETER BLOCK                                 *
*      R13-R15 STANDARD USAGE                                         *
*                                                                     *
*  SPECIAL NOTES:                                                     *
*      THIS PROGRAM IS REENTRANT, REUSEABLE.  IT DOES NOT CALL ANY    *
*      EXTERNAL ROUTINES, SO R13 ADDRESSES THE WORK AREA (WHICH       *
*      SHOULD BE PART OF THE CALLING PROGRAM'S DSA).                  *
*                                                                     *
***********************************************************************
         COPY  DS@MACS
         TITLE 'DS@CNVDT PARAMETER BLOCK'
***********************************************************************
*              P A R A M E T E R      B L O C K                       *
***********************************************************************
         SPACE
         DS@CNDPB DSECT=YES
         TITLE 'WORKAREA MAP'
***********************************************************************
*              W O R K A R E A      M A P                             *
***********************************************************************
         SPACE
CNVDTDSA DSECT
SAVER13  DC   F'0'
WORKAREA DC   D'0'
         ORG  WORKAREA
FILLWORK DS   XL6
PCKYEAR  DS   XL2
         ORG
         TITLE 'MAINLINE CODE'
***********************************************************************
*              D S @ C N V D T     P R O G R A M                      *
***********************************************************************
         SPACE
DS@CNVDT CSECT
         USING *,R15
         DS@START ,
         PRINT NOGEN
         DROP  R15                    DROP TEMPORARY BASE
         LR    R11,R15                SET BASE REGISTER
         USING DS@CNVDT,R11
         L     R12,0(R1)              ADDRESS PARAMETER BLOCK
         USING CNVDATPB,R12
         LR    R1,R13                 COPY REGISTER 13
         LA    R13,CNVWRKA@           ADDRESS WORK AREA
         USING CNVDTDSA,R13
         ST    R1,SAVER13             SAVE PREVIOUS REG. 13 CONTENTS
         CLC   CONVTYPE,=C'JG'        JULIAN TO GREGORIAN REQUESTED?
         BE    JULGREG                Y.THEN DO IT
         B     GREGJUL                ELSE DO GREGORIAN TO JULIAN
*
*
RETURN   DS    0H
         L     R13,SAVER13            RESTORE REGISTER 13
         L     R14,12(0,13)           RESTORE REGISTER 14
         LM    0,12,20(13)            RESTORE REGISTERS 0 THRU 12
         BR    R14                    RETURN TO CALLER
         TITLE 'JULIAN TO GREGORIAN DATE CONVERSION'
**********************************************************************
*    J U L I A N    T O    G R E G O R I A N    C O N V E R S I O N  *
**********************************************************************
         SPACE
JULGREG  DS    0H
         MVC   GREGDATE,GREGMASK      MOVE IN SLASHES FOR GREG DATE
         XR    R5,R5                  CLEAR WORK REG
         IC    R5,JULYEAR             GET YEAR IN DATE
         SLL   R5,4                   SHIFT TO ADD IN ZONE FOR PACK
         LA    R6,12                  ZONE FOR PACK IS X'C'
         OR    R5,R6                  R5 NOW HAS YEAR IN PACK FORMAT
         ST    R5,WORKAREA+4          NOW STORE IN WORK AREA
         UNPK  GREGYR,PCKYEAR         PUT YEAR IN OUTPUT DATE
         OI    GREGYR+1,X'F0'         ENSURE THAT IT'S PRINTABLE
         SR    R6,R6                  CLEAR R6 FOR DIVIDE
         CVB   R7,WORKAREA            NOW CONVERT YEAR TO BINARY
         LA    R8,4                   READY TO DIVIDE BY FOUR
         DR    R6,R8                  DIVIDE TO SEE IF YEAR A LEAP YR.
         LTR   R6,R6                  IF REMAINDER ZERO, THEN IT IS
         LA    R5,MONTBL              ASSUME NOT LEAP YEAR
         BNZ   NOTLEAP                IF LEAP YEAR,
         LA    R5,MONTBLLY              USE THIS TABLE
NOTLEAP  DS    0H
         NC    JULDAY,=X'FFFC'        ENSURE ZONE IS "C", NOT "F"
DAYLOOP  DS    0H
         CLC   JULDAY,0(R5)           Q.DAY LESS THAN CURRENT MONTH?
         BNH   ENDLOOP                Y.THEN GET OUT OF LOOP
         SP    JULDAY,0(2,R5)         ELSE SUBTRACE # OF DAYS
         LA    R5,4(R5)               NEXT ELEMENT IN MONTH TABLE
         CLC   0(2,R5),=X'000C'       IF END OF TABLE, THEN BAD DATE
         BE    BADDATE                   SO SKIP
         B     DAYLOOP                AND LOOP
ENDLOOP  DS    0H
         MVC   GREGMO,2(R5)           MOVE IN MONTH
         UNPK  GREGDAY,JULDAY         MOVE IN DAY
         OI    GREGDAY+1,X'F0'        ENSURE THAT IT'S PRINTABLE
         B     RETURN                 DONE
BADDATE  DS    0H
         MVC   GREGDATE,BLANKS        WHEN BAD DATE, GIVE BLANKS BACK
         B     RETURN                 DONE
         TITLE 'GREGORIAN TO JULIAN DATE CONVERSION'
**********************************************************************
*     G R E G O R I A N   T O   J U L I A N   C O N V E R S I O N    *
**********************************************************************
         SPACE
GREGJUL  DS    0H            *********TO BE IMPLEMENTED LATER
         MVC   JULDATE,=X'84001F'     FOR NOW, ENSURE NO S0C7
         B     RETURN                 RETURN TO CALLER
         TITLE 'STORAGE DEFINITIONS'
**********************************************************************
*             S T O R A G E     D E F I N I T I O N S                *
**********************************************************************
         SPACE
*
BLANKS   DC   CL8' '
GREGMASK DC   CL8'  /  /'
*
MONTBL   DC   PL2'31',C'01'             MONTH TABLE ENTRY FOR JAN
         DC   PL2'28',C'02'               "     "     "    "  FEB
         DC   PL2'31',C'03'               "     "     "    "  MAR
         DC   PL2'30',C'04'               "     "     "    "  APR
         DC   PL2'31',C'05'               "     "     "    "  MAY
         DC   PL2'30',C'06'               "     "     "    "  JUN
         DC   PL2'31',C'07'               "     "     "    "  JUL
         DC   PL2'31',C'08'               "     "     "    "  AUG
         DC   PL2'30',C'09'               "     "     "    "  SEP
         DC   PL2'31',C'10'               "     "     "    "  OCT
         DC   PL2'30',C'11'               "     "     "    "  NOV
         DC   PL2'31',C'12'               "     "     "    "  DEC
         DC   PL2'00',C'00'               FENCE FOR END OF TABLE
*
MONTBLLY DC   PL2'31',C'01'             MONTH TABLE ENTRY FOR JAN
         DC   PL2'29',C'02'               "     "     "    "  FEB
         DC   PL2'31',C'03'               "     "     "    "  MAR
         DC   PL2'30',C'04'               "     "     "    "  APR
         DC   PL2'31',C'05'               "     "     "    "  MAY
         DC   PL2'30',C'06'               "     "     "    "  JUN
         DC   PL2'31',C'07'               "     "     "    "  JUL
         DC   PL2'31',C'08'               "     "     "    "  AUG
         DC   PL2'30',C'09'               "     "     "    "  SEP
         DC   PL2'31',C'10'               "     "     "    "  OCT
         DC   PL2'30',C'11'               "     "     "    "  NOV
         DC   PL2'31',C'12'               "     "     "    "  DEC
         DC   PL2'00',C'00'               FENCE FOR END OF TABLE
         END  DS@CNVDT
