       IDENTIFICATION DIVISION.
       PROGRAM-ID.  SMPELMNT.
       AUTHOR.      SOLOMON JAEGER.
       REMARKS.
           WRITTEN ON 03/24/85 FOR SMP4 APPLY CHECK PROCESSING.
           PROGRAM TAKES AS INPUT AN APPLY (OR ACCEPT) CHECK LISTING,
           AND PRODUCES AN 80-BYTE FILE LISTING ALL ELEMENTS
           AFFECTED BY THE APPLY (OR ACCEPT) PROCESSING.  THIS
           FILE CAN BE USED AS INPUT TO AN SMP LIST JOB.  IN ADDITION,
           RELEVANT FACTS FROM THE SMP ELEMENT REPORT ARE INCLUDED
           WITHIN COMMENTS NEXT TO THE ELEMENT NAME, SO THAT THE SMP
           ACTION ON THAT ELEMENT CAN BE DETERMINED TO A CONSIDERABLE
           DEGREE, EVEN THOUGH THE INFORMATION IN THE COMMENTS IS
           SOMEWHAT ABBREVIATED.
       SKIP 3
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.  IBM-370.
       OBJECT-COMPUTER.  IBM-370.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PRINTI-FILE ASSIGN TO   UT-3350-S-SYSUT1.
           SELECT PRINTO-FILE ASSIGN TO   UT-3350-S-SYSUT2.
       DATA DIVISION.
       FILE SECTION.
       FD  PRINTI-FILE
           RECORDING MODE IS F
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS.
       01  PRINTI-RECORD.
           05  PRINTI-CNTL        PIC X.
           05  PRINTI-DATA        PIC X(132).
       FD  PRINTO-FILE
           RECORDING MODE IS F
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 80 CHARACTERS
           BLOCK CONTAINS 0 RECORDS.
       01  PRINTO-RECORD          PIC X(80).
       WORKING-STORAGE SECTION.
       77  IDENT-FIER     PIC X(17) VALUE 'SMPELMNT 03/24/85'.
       01  REPT-FLAG          PIC X    VALUE SPACES.
       01  REPORT-NAME        PIC X(7) VALUE SPACES.
       01  FIRST-7-STOR       PIC X(7) VALUE SPACES.
       01  OUT-WORK.
           05  OUT-FILL           PIC X     VALUE SPACES.
           05  OUT-ELEM-NAME      PIC X(8)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-SLSH-STR       PIC X(2)  VALUE '/*'.
           05  OUT-STT            PIC X(5)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-ELEM-STAT      PIC X(8)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-CUR-FMID       PIC X(7)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-CUR-RMID       PIC X(7)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-MAC-SRC        PIC X(8)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-LOAD-MOD       PIC X(8)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-LMOD-SYSLIB    PIC X(8)  VALUE SPACES.
           05  OUT-STR-SLSH       PIC X(2)  VALUE '*/'.
           05  FILLER             PIC X(9)  VALUE SPACES.
       01  IN-WORK.
           05  IN-INTERM.
               10  IN-CNTL        PIC X.
               10  IN-DATA.
                   15  IN-REPTNAME.
                       20  IN-STT.
                           25  IN-STTX     PIC X.
                           25  IN-STTY     PIC X(4).
                       20  IN-RST          PIC X(2).
                   15  ELEM-NAME           PIC X(8).
                   15  FILLER              PIC X(2).
                   15  ELEM-STAT           PIC X(8).
                   15  FILLER              PIC X(2).
                   15  CUR-FMID            PIC X(7).
                   15  FILLER              PIC X(2).
                   15  CUR-RMID            PIC X(7).
                   15  FILLER              PIC X(2).
                   15  MAC-SRC             PIC X(8).
                   15  FILLER              PIC X(21).
                   15  LOAD-MOD            PIC X(8).
                   15  FILLER              PIC X(2).
                   15  LMOD-SYSLIB         PIC X(8).
                   15  FILLER              PIC X(40).
           EJECT
       PROCEDURE DIVISION.
       PROCESS00.
           OPEN INPUT  PRINTI-FILE
                OUTPUT PRINTO-FILE.
           MOVE SPACES TO REPT-FLAG.
       PROCESS01.
           IF REPT-FLAG EQUAL 'E'
               GO TO PROCESS-END.
       PROCESS02.
           READ PRINTI-FILE INTO IN-WORK AT END GO TO PROCESS-END.
       PROCESS03.
           IF IN-REPTNAME EQUAL 'SYSMOD '
               MOVE SPACES TO REPT-FLAG.
           IF IN-REPTNAME EQUAL 'ELEMENT'
               MOVE IN-REPTNAME TO FIRST-7-STOR
               MOVE 'S' TO REPT-FLAG.
           IF IN-REPTNAME EQUAL 'DELETED' OR 'REGRESS'
               MOVE IN-REPTNAME TO FIRST-7-STOR
               MOVE 'E' TO REPT-FLAG.
           IF IN-REPTNAME EQUAL 'HMA2050'
               MOVE IN-REPTNAME TO FIRST-7-STOR
               MOVE SPACES TO REPT-FLAG.
           IF REPT-FLAG EQUAL SPACES
               GO TO PROCESS02.
           IF REPT-FLAG EQUAL 'E'
               GO TO PROCESS-END.
       PROCESS-SCAN-START.
           IF IN-STTX NOT EQUAL SPACES AND
              IN-REPTNAME NOT EQUAL 'ELEMENT' AND
              IN-REPTNAME NOT EQUAL 'ELEM   ' AND
              IN-REPTNAME NOT EQUAL 'TYPE   '
                  PERFORM MOVE-INPUT-TO-OUTPUT THRU MOVE-END
                  PERFORM WRITE-OUTPUT         THRU WRITE-END.
           GO TO PROCESS02.
       PROCESS-END.
           CLOSE PRINTI-FILE PRINTO-FILE.
           STOP RUN.
           EJECT
       MOVE-INPUT-TO-OUTPUT.
           MOVE ELEM-NAME TO OUT-ELEM-NAME.
           MOVE '/*'      TO OUT-SLSH-STR.
           MOVE IN-STT    TO OUT-STT.
           MOVE ELEM-STAT TO OUT-ELEM-STAT.
           MOVE CUR-FMID  TO OUT-CUR-FMID.
           MOVE CUR-RMID  TO OUT-CUR-RMID.
           MOVE MAC-SRC   TO OUT-MAC-SRC.
           MOVE LOAD-MOD  TO OUT-LOAD-MOD.
           MOVE LMOD-SYSLIB TO OUT-LMOD-SYSLIB.
           MOVE '*/'      TO OUT-STR-SLSH.
       MOVE-END.
       WRITE-OUTPUT.
           WRITE PRINTO-RECORD FROM OUT-WORK.
           MOVE SPACES TO OUT-WORK.
       WRITE-END.
