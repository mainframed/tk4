       IDENTIFICATION DIVISION.
       PROGRAM-ID.  VBTOFB.
       REMARKS.
      *
      *    THIS IS A QUICK COBOL PROGRAM THAT CONVERTS A FILE FROM
      *       VARIABLE BLOCKED TO FIXED BLOCKED RECORD FORMAT.
      *       YOU CAN USE THIS PROGRAM INSTEAD OF THE DSCOPY STEP
      *       THAT CONVERTS RECORD FORMATS FROM VB TO FB TO RUN
      *       SMPFMID AND SMPFMUPD.  OF COURSE SMPFMIDV AND SMPFMUPV
      *       DON'T NEED THIS STEP.  TO RUN THIS PROGRAM, YOU NEED A
      *       //SYSOUT DD    CARD TO PRINT THE DISPLAY, AS WELL AS
      *       //SYSUT1 DD    AND
      *       //SYSUT2 DD    CARDS.
      *
      *       I WROTE THIS TO SHOW SYSTEM PROGRAMMERS HOW EASY THIS
      *       JOB IS IN COBOL, BECAUSE COBOL HANDLES ALL THE CONTROL
      *       INFORMATION FOR THE RECORDS.  YOU DON'T HAVE TO.
      *
      *NOTE THAT THE INPUT RECORD IS READ INTO A WORK AREA AND WRITTEN
      *  OUT FROM THERE.  COBOL DOES THE DATA CONVERSION WHEN YOU READ
      *  TO THE WORKAREA.  IF YOU MODIFY THIS PROGRAM TO CHANGE THE
      *  BLOCKSIZE, YOU MUST CHANGE THE WORK AREA SIZE (IN-WORK) IF YOU
      *  WANT A RECORDSIZE BIGGER THAN 133.  IF YOU DON'T READ INTO
      *  A WORK AREA, YOU DON'T GET PROPER CONVERSIONS.
      *
      *    TO GO FROM FB TO VB, JUST REVERSE THE FD'S AND IMITATE
      *       THE PROCESS.
      *
      *    TO ADAPT THIS PROGRAM TO OTHER RECORD LENGTHS, YOU MUST
      *       MODIFY IT AND RECOMPILE IT.
      *
      *    BLOCKSIZE CAN (AND MUST) BE SET IN THE JCL, BECAUSE I
      *       CODED "BLOCK CONTAINS 0 RECORDS" AND "BLOCK CONTAINS
      *       0 CHARACTERS" (IN THE CASE OF RECFM=V).
      *
       EJECT
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.  IBM-370.
       OBJECT-COMPUTER.  IBM-370.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PRINTI-FILE ASSIGN TO   UT-3350-S-SYSUT1.
           SELECT PRINTO-FILE ASSIGN TO   UT-3350-S-SYSUT2.
       DATA DIVISION.
       FILE SECTION.
       FD  PRINTI-FILE
           RECORDING MODE IS V
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 CHARACTERS.
       01  PRINTI-RECORD.
           05  PRINTI-CNTL        PIC X.
           05  PRINTI-DATA        PIC X(132).
       FD  PRINTO-FILE
           RECORDING MODE IS F
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS.
       01  PRINTO-RECORD.
           05  PRINTO-CNTL        PIC X.
           05  PRINTO-DATA        PIC X(132).
       EJECT
       WORKING-STORAGE SECTION.
       77  IDENT-FIER     PIC X(16) VALUE 'VBTOFB 04/14/86'.
       01  RECD-COUNTER           PIC 9(7) VALUE ZEROES.
       01  RECD-COUNTER-DSPLY     PIC ZZZZZZ9 VALUE ZEROES.
       01  IN-WORK                PIC X(133)  VALUE SPACES.
           EJECT
       PROCEDURE DIVISION.
       PROCESS00.
           OPEN INPUT  PRINTI-FILE
                OUTPUT PRINTO-FILE.
           MOVE ZEROES TO RECD-COUNTER RECD-COUNTER-DSPLY.
           MOVE SPACES TO IN-WORK.
       PROCESS01.
           READ PRINTI-FILE INTO IN-WORK AT END GO TO PROCESS-END.
           ADD 1 TO RECD-COUNTER.
           WRITE PRINTO-RECORD FROM IN-WORK.
           GO TO PROCESS01.
       PROCESS-END.
           MOVE RECD-COUNTER TO RECD-COUNTER-DSPLY.
           DISPLAY RECD-COUNTER-DSPLY
               ' RECORDS CONVERTED FROM VB TO FB FORMAT. '
           CLOSE PRINTI-FILE PRINTO-FILE.
           STOP RUN.
