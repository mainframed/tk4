       IDENTIFICATION DIVISION.
       PROGRAM-ID.  SMPRCSUQ.
       AUTHOR.      SOLOMON JAEGER.
       REMARKS.
           REVISED VERSION 04/11/86 TO READ LRECL(121) LISTINGS
             INSTEAD OF LRECL(133).
           WRITTEN ON 03/26/85 FOR SMP4 RECEIVE PROCESSING.
           PROGRAM TAKES AS INPUT AN SMP4 RECEIVE LISTING,
           AND PRODUCES AN 80-BYTE FILE LISTING ALL SYSMODS
           SUCCESSFULLY RECEIVED.  THIS FILE CAN BE USED AS INPUT
           TO AN SMP LIST JOB.  IN ADDITION, COMMENTS FROM THE
           RECEIVE REPORT AS WELL AS TIME AND DATE RECEIVED, ARE
           INCLUDED WITHIN COMMENTS NEXT TO THE ELEMENT NAME.
           FOR COMPATIBILITY WITH IBM EXCLUDE LISTS, THE SYSMOD NAME
           IS PUT INTO COLUMN 7 OF THE OUTPUT (CARD-IMAGE) RECORD.
       SKIP3
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.  IBM-370.
       OBJECT-COMPUTER.  IBM-370.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PRINTI-FILE ASSIGN TO   UT-3350-S-SYSUT1.
           SELECT PRINTO-FILE ASSIGN TO   UT-3350-S-SYSUT2.
       DATA DIVISION.
       FILE SECTION.
       FD  PRINTI-FILE
           RECORDING MODE IS F
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 121 CHARACTERS
           BLOCK CONTAINS 0 RECORDS.
       01  PRINTI-RECORD.
           05  PRINTI-CNTL                  PIC X.
           05  PRINTI-DATA.
               10  SPOT1                    PIC X(5).
               10  DATE-HEADING.
                   15  WORD1                PIC X(5).
                   15  NUM1                 PIC X(6).
               10  SPOT2                    PIC X(2).
               10  TIME-HEADING.
                   15  WORD2                PIC X(5).
                   15  NUM2                 PIC X(8).
               10  REST-OF-HEADER           PIC X(89).
       01  PRINTI-RECORD-2.
           05  FILLER                       PIC X.
           05  SPOT3                        PIC X(25).
           05  RECEIVE-HEADER               PIC X(24).
           05  REMAINING-HEADER             PIC X(71).
       01  PRINTI-RECORD-3.
           05  FILLER                       PIC X.
           05  END-PROCES                   PIC X(11).
           05  END-SUMMARY                  PIC X(28).
           05  FILL-SPACE                   PIC X(81).
       FD  PRINTO-FILE
           RECORDING MODE IS F
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 80 CHARACTERS
           BLOCK CONTAINS 0 RECORDS.
       01  PRINTO-RECORD                    PIC X(80).
       EJECT
       WORKING-STORAGE SECTION.
       77  IDENT-FIER     PIC X(17) VALUE 'SMPRCSUQ 04/11/86'.
       01  REPT-FLAG              PIC X     VALUE SPACES.
       01  REPORT-NAME            PIC X(7)  VALUE SPACES.
       01  STOR-TIME              PIC X(8)  VALUE SPACES.
       01  STOR-DATE              PIC X(6)  VALUE SPACES.
       01  OUT-WORK.
           05  OUT-FILL           PIC X(6)  VALUE SPACES.
           05  OUT-ELEM-NAME      PIC X(7)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-SLSH-STR       PIC X(2)  VALUE '/*'.
           05  OUT-TYPE           PIC X(8)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-STATUS         PIC X(8)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-VER            PIC X(19) VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-DATE           PIC X(6)  VALUE SPACES.
           05  FILLER             PIC X     VALUE SPACES.
           05  OUT-TIME           PIC X(8)  VALUE SPACES.
           05  OUT-STR-SLSH       PIC X(2)  VALUE '*/'.
           05  FILLER             PIC X(9)  VALUE SPACES.
       01  IN-WORK.
           05  IN-INTERM.
               10  IN-CNTL        PIC X.
               10  IN-DATA.
                   15  FILLER              PIC X(5).
                   15  IN-REPTNAME         PIC X(7).
                   15  FILLER              PIC X(2).
                   15  IN-STATUS           PIC X(8).
                   15  FILLER              PIC X(3).
                   15  IN-TYPE             PIC X(8).
                   15  FILLER              PIC X(2).
                   15  IN-VER              PIC X(19).
                   15  FILLER              PIC X(66).
           EJECT
       PROCEDURE DIVISION.
       PROCESS00.
           OPEN INPUT  PRINTI-FILE
                OUTPUT PRINTO-FILE.
           MOVE SPACES TO REPT-FLAG.
       PROCESS01.
           IF REPT-FLAG EQUAL 'E'
               GO TO PROCESS-END.
       PROCESS02.
           READ PRINTI-FILE INTO IN-WORK AT END GO TO PROCESS-END.
       PROCESS03.
           IF WORD1 EQUAL 'DATE ' AND WORD2 EQUAL 'TIME '
               PERFORM PROCESS04 THRU END-PROCESS04.
           IF RECEIVE-HEADER EQUAL 'RECEIVE  SUMMARY  REPORT'
               MOVE 'S' TO REPT-FLAG.
           IF END-SUMMARY EQUAL 'RECEIVE PROCESSING COMPLETED'
               MOVE 'E' TO REPT-FLAG.
           IF REPT-FLAG EQUAL SPACES
               GO TO PROCESS02.
           IF REPT-FLAG EQUAL 'E'
               GO TO PROCESS-END.
       PROCESS-START.
           IF IN-REPTNAME NOT EQUAL SPACES AND
              WORD1       NOT EQUAL 'DATE ' AND
              WORD1       NOT EQUAL 'SYSMO' AND
              IN-STATUS       EQUAL 'RECEIVED'
                  PERFORM MOVE-INPUT-TO-OUTPUT THRU MOVE-END
                  PERFORM WRITE-OUTPUT         THRU WRITE-END.
           GO TO PROCESS02.
       PROCESS-END.
           CLOSE PRINTI-FILE PRINTO-FILE.
           STOP RUN.
           EJECT
       PROCESS04.
           MOVE NUM1 TO STOR-DATE.
           MOVE NUM2 TO STOR-TIME.
       END-PROCESS04.
           SKIP3
       MOVE-INPUT-TO-OUTPUT.
           MOVE IN-REPTNAME TO OUT-ELEM-NAME.
           MOVE '/*'        TO OUT-SLSH-STR.
           MOVE IN-STATUS   TO OUT-STATUS.
           MOVE IN-TYPE     TO OUT-TYPE.
           MOVE IN-VER      TO OUT-VER.
           MOVE STOR-DATE   TO OUT-DATE.
           MOVE STOR-TIME   TO OUT-TIME.
           MOVE '*/'        TO OUT-STR-SLSH.
       MOVE-END.
           SKIP3
       WRITE-OUTPUT.
           WRITE PRINTO-RECORD FROM OUT-WORK.
           MOVE SPACES TO OUT-WORK.
       WRITE-END.
