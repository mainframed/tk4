         MACRO
         CONTINUE
.**********************************************************************
.*                                                                    *
.*   NAME:               CONTINUE                                     *
.*                                                                    *
.*   FUNCTION:                                                        *
.*       TO BRANCH TO THE STATEMENT BEFORE THE NEXT ENDDO OR ENDLOOP  *
.*       STATEMENT (I.E. CONTINUE NEXT LOOP ITERATION)                *
.*                                                                    *
.*   PARAMETERS:    NONE                                              *
.*                                                                    *
.*   USAGE:                                                           *
.*       ANYWHERE WITHIN A DOWHILE OR LOOP BLOCK                      *
.*                                                                    *
.*   EXAMPLE:                                                         *
.*       LOOP  TIMES=FOREVER                                          *
.*       WHILE CLI,0(R5),C'1',E                                       *
.*         ...                                                        *
.*         IF  LTR,R6,R6,Z                                            *
.*             CONTINUE                                               *
.*         ENDIF                                                      *
.*         ...                                                        *
.*       ENDLOOP                                                      *
.*                                                                    *
.**********************************************************************
         GBLA  &KFQNIV
         GBLC  &KFQCC(20),&KFQEND(20),&KFQTYP(20)
         LCLA  &LEV
.**********************************************************************
.*       FIND PREVIOUS DOWHILE OR LOOP MACRO                          *
.**********************************************************************
&LEV     SETA  &KFQNIV                 CURRENT NESTING LEVEL
.LLOOP   AIF   (&LEV LE 0).ERR1        IF NOT FOUND THEN ERROR EXIT
         AIF   ('&KFQTYP(&LEV)' EQ 'DOWHILE').FDO
         AIF   ('&KFQTYP(&LEV)' EQ 'LOOP').FLOOP
&LEV     SETA  &LEV-1                  KEEP LOOKING
         AGO   .LLOOP
.**********************************************************************
.*       GENERATE BRANCH STATEMENTS                                   *
.**********************************************************************
.FDO     ANOP
         B     &KFQCC(&LEV)
         MEXIT
.FLOOP   ANOP
         B     &KFQEND(&LEV).-4
         MEXIT
.ERR1    ANOP
         MNOTE  8,'*** MISPLACED CONTINUE STATEMENT ***'
         MEND
