         MACRO
         LEAVE
.**********************************************************************
.*                                                                    *
.*   NAME:               LEAVE                                        *
.*                                                                    *
.*   FUNCTION:                                                        *
.*       TO BRANCH TO THE STATEMENT AFTER THE NEXT ENDDO OR ENDLOOP   *
.*       STATEMENT (I.E. EXIT FROM THE LOOP)                          *
.*                                                                    *
.*   PARAMETERS:    NONE                                              *
.*                                                                    *
.*   USAGE:                                                           *
.*       ANYWHERE WITHIN A DOWHILE OR LOOP BLOCK                      *
.*                                                                    *
.*   EXAMPLE:                                                         *
.*       LOOP  TIMES=FOREVER                                          *
.*       WHILE CLI,0(R5),C'1',E                                       *
.*         ...                                                        *
.*         IF  LTR,R6,R6,NH                                           *
.*             LEAVE                                                  *
.*         ENDIF                                                      *
.*         ...                                                        *
.*       ENDLOOP                                                      *
.*                                                                    *
.**********************************************************************
         GBLA  &KFQNIV
         GBLC  &KFQEND(20),&KFQTYP(20)
         LCLA  &LEV
.**********************************************************************
.*       FIND PREVIOUS DOWHILE OR LOOP MACRO                          *
.**********************************************************************
&LEV     SETA  &KFQNIV                 CURRENT NESTING LEVEL
.LLOOP   AIF   (&LEV LE 0).ERR1        IF NOT FOUND THEN ERROR EXIT
         AIF   ('&KFQTYP(&LEV)' EQ 'DOWHILE').FOUND
         AIF   ('&KFQTYP(&LEV)' EQ 'LOOP').FOUND
&LEV     SETA  &LEV-1                  KEEP LOOKING
         AGO   .LLOOP
.**********************************************************************
.*       GENERATE BRANCH STATEMENT                                    *
.**********************************************************************
.FOUND   ANOP
         B     &KFQEND(&LEV)
         MEXIT
.ERR1    ANOP
         MNOTE  8,'*** MISPLACED LEAVE STATEMENT ***'
         MEND
