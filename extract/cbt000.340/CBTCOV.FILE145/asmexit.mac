         MACRO
&NAME    ASMEXIT &RC=NONE
.*
.* ASMEXIT - RETURN CONTROL TO CALLER FROM ROUTINE USING ASMENTRY
.*
.* THE ASMEXIT MACRO ESTABLISHES THE LINKAGE FOR AN ASMENTRY ROUTINE
.* TO RETURN TO ITS CALLER.  THE OPTIONAL NAME CREATES A LABEL ON
.* THE CODE SO THAT IT MAY BE BRANCHED TO.  THE OPTIONAL RC= PARM
.* ALLOWS EITHER AN ABSOLUTE NUMBER TO BE STORED IN R15 ON RETURN
.* OR THE CONTENTS OF A REGISTER TO BE RETURNED IN R15.
.*
.* EXAMPLES:
.*
.*       ASMEXIT             - R14-R12 ARE RESTORED
.* L1    ASMEXIT             - AS ABOVE, WITH A LABEL ON CODE
.* L2    ASMEXIT RC=12       - AS ABOVE, EXCEPT R15 = 12
.*       ASMEXIT RC=(R15)    - AS ABOVE, R15 IS NOT RESTORED
.*       ASMEXIT RC=(R3)     - AS ABOVE, R3 CONTENTS MOVED TO R15
.*
.*
         LCLC  &REG
         GBLC  &ASMETYP,&ASMELEN
         AIF   ('&NAME' EQ '').L010
&NAME    EQU   *
.L010    ANOP
         AIF   ('&RC'(1,1) NE '(').L020
&REG     SETC  '&RC'(2,K'&RC-2)
.L020    ANOP
         AIF   ('&ASMETYP' EQ 'NO').L050
         L     R13,4(R13)              R13->CALLERS SAVE AREA
         AIF   ('&ASMETYP' EQ 'REUS').L050
         AIF   ('&ASMETYP' EQ 'RENT').L030
         MNOTE 12,'** USE ASMENTRY SAVE=RENT, REUS, OR NO'
         MEXIT
.L030    ANOP
         AIF   ('&RC'(1,1) NE '(').L040
         LR    R3,&REG                 R3 = SAVE RETURN CODE
&REG     SETC  'R3'
.L040    ANOP
         L     R1,4(R13)               R1 ->CORE TO FREE
         FREEMAIN R,LV=&ASMELEN,A=(1)
.L050    ANOP
         AIF   ('&RC' NE 'NONE').L060
.*
.*       CASE: NO RETURN CODE SPECIFIED
.*
         LM    R14,R12,12(R13)              RESTORE ALL REGISTERS
         AGO   .L090
.L060    ANOP
.*
.*       RETURN CODE HAS BEEN SPECIFIED - IT MAY BE AN ABSOLUTE
.*       VALUE OR A REGISTER IN BRACKETS
.*
         AIF   ('&RC'(1,1) NE '(').L070
.*
.*       CASE: A REGISTER IS USED AS A RETURN CODE, NO SPECIAL
.*             ACTION IF IT IS R15
.*
         AIF   ('&REG' EQ 'R15').L080
         LR    R15,&REG                R15= RETURN CODE
         AGO   .L080
.L070    ANOP
.*
.*       CASE: RETURN CODE IS ABSOLUTE VALUE
.*
         LA    R15,&RC                 R15= RETURN CODE
.L080    ANOP
         L     R14,12(R13)             R14  RESTORED
         LM    R0,R12,20(R13)          R0-R12 RESTORED
.L090    ANOP
         BR    R14                          RETURN TO CALLER
         MEND
