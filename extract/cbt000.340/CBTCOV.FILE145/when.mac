         MACRO
         WHEN
.**********************************************************************
.*                                                                    *
.*   NAME:               WHEN                                         *
.*                                                                    *
.*   FUNCTION:                                                        *
.*       TO IMPLEMENT THE WHEN CLAUSE OF THE CASE MACRO               *
.*                                                                    *
.*   PARAMETERS:    (1)  ASSEMBLER INSTRUCTION                        *
.*                  (2)  FIRST OPERAND OF INSTRUCTION                 *
.*                  (3)  SECOND OPERAND OF INSTRUCTION                *
.*                  (4)  COMPARISON OPERATOR                          *
.*                               OR                                   *
.*                  (1)  SECOND OPERAND OF INSTRUCTION SPECIFIED      *
.*                       IN THE CASE MACRO STATEMENT OR PREVIOUS      *
.*                       WHEN MACRO STATEMENT                         *
.*                               OR                                   *
.*                  (1)  KEYWORD 'ELSE'                               *
.*                                                                    *
.*   USAGE:                                                           *
.*       CONSULT DOCUMENTATION FOR THE CASE MACRO                     *
.*                                                                    *
.**********************************************************************
         GBLC  &KFQTYP(20),&KFQEND(20)
         GBLA  &KFQNIV,&KFQSEQ(20)
         GBLC  &KFQAA(20),&KFQBB(20)
         GBLC  &KFQCC(20),&KFQDD(20)
         LCLA  &ATIM,&AINX,&ATYP
         LCLC  &COND,&COPER
         AIF   (&KFQNIV EQ 0).ERR2   NOT STARTING WITH CASE
         AIF   ('&KFQTYP(&KFQNIV)' EQ 'CASE').OKSTR
         MNOTE 8,'*** STRUCTURE INVALID   -  &KFQTYP(&KFQNIV) '
.OKSTR   ANOP
         AIF   (N'&SYSLIST NE 4).ONEV
&KFQAA(&KFQNIV) SETC  '&SYSLIST(1)'     INSTRUCTION CODE
&KFQBB(&KFQNIV) SETC  '&SYSLIST(2)'     FIXED OPERAND
         AIF   ('&SYSLIST(4)'(1,1) EQ 'N').NEGCC
&KFQDD(&KFQNIV) SETC  'N&SYSLIST(4)'  INVERT COND. CODE
         AGO   .CCDONE
.NEGCC   ANOP
&KFQDD(&KFQNIV) SETC '&SYSLIST(4)'(2,K'&SYSLIST(4)-1) INVERT CC
.CCDONE  ANOP
.ONEV    ANOP
         B     &KFQEND(&KFQNIV)     GO OUT
ZC&KFQNIV.L&KFQSEQ(&KFQNIV) DS 0H
&KFQSEQ(&KFQNIV) SETA 1+&KFQSEQ(&KFQNIV)    LABEL WITHIN LEVEL
         AIF   (N'&SYSLIST NE 4).SAVEINS
&ATYP    SETA  3
         AIF   (N'&SYSLIST(3) GT 1).LLIST
         &KFQAA(&KFQNIV)  &SYSLIST(2),&SYSLIST(3)
         B&KFQDD(&KFQNIV) ZC&KFQNIV.L&KFQSEQ(&KFQNIV)
         MEXIT
.SAVEINS AIF   (N'&SYSLIST NE 1).ERR1
&ATYP    SETA  1
         AIF   ('&SYSLIST(1)' EQ 'ELSE').NOGEN
         AIF   (N'&SYSLIST(1) GT 1).LLIST
         &KFQAA(&KFQNIV)  &KFQBB(&KFQNIV),&SYSLIST(1)
         B&KFQDD(&KFQNIV)  ZC&KFQNIV.L&KFQSEQ(&KFQNIV)
         MEXIT
.LLIST   ANOP
&COND    SETC  '&KFQDD(&KFQNIV)'       SAVE COND
         AIF   ('&COND'(1,1) EQ 'N').NEGCO
&COND    SETC  'N&COND'               INVERT COND.CODE
         AGO   .CODONE
.NEGCO   ANOP
&COND    SETC '&COND'(2,1)             INVERT COND.CODE
.CODONE  ANOP
&ATIM    SETA  N'&SYSLIST(&ATYP)           NUMBER OF LOOPS
&AINX    SETA  1                       OPERAND INDEX
.LLOOP   AIF   (&AINX GE &ATIM).ELOOP
         &KFQAA(&KFQNIV) &KFQBB(&KFQNIV),&SYSLIST(&ATYP,&AINX)
         B&COND ZC&KFQNIV.D&KFQSEQ(&KFQNIV)
&AINX    SETA  &AINX+1                 NEXT OPERAND
         AGO   .LLOOP
.ELOOP   &KFQAA(&KFQNIV) &KFQBB(&KFQNIV),&SYSLIST(&ATYP,&AINX)
         B&KFQDD(&KFQNIV)  ZC&KFQNIV.L&KFQSEQ(&KFQNIV)
ZC&KFQNIV.D&KFQSEQ(&KFQNIV) DS  0H
.NOGEN   ANOP
         MEXIT
.ERR1    MNOTE  12,'INVALID NUMBER OF OPERANDS'
         MEXIT
.ERR2    MNOTE  12,'THE CASE MACRO HAS NOT BEEN USED BEFORE'
         MEND
