*          DATA SET I38PAGE    AT LEVEL 029 AS OF 15/04/82
         TITLE 'I38PAGE - PRINT CURRENT PAGE IN PAGEPLOT SYSTEM'
***********************************************************************
*                                                                     *
*    NAME:      I38PAGE (ENTRY POINT IS PLTPAGE)                      *
*                                                                     *
*    FUNCTION:  TO MERGE THE CURRENT CORE IMAGES OF THE PLOT ONTO     *
*               A PRINTED PAGE. TO BE USED WITH IBM 3800 PRINTER.     *
*                                                                     *
*    NOTE:      THIS IS NOT A USER INTERFACE ROUTINE , IT IS CALLED   *
*               ONLY BY PLOT                                          *
*                                                                     *
*    PARAMETERS: NONE                                                 *
*                                                                     *
*    PROCESSING:                                                      *
*               IF NOTHING TO DISPLAY                                 *
*               THEN MESSAGE AND RETURN                               *
*               MERGE LINES OF SYMBOL PAGE AND MAP PAGE               *
*                 ONTO OUTPUT BUFFER SUCH THAT LINES DO NOT           *
*                 OBSCURE CHARACTERS                                  *
*               RESET GLOBAL VARIABLES                                *
*               RETURN                                                *
*                                                                     *
***********************************************************************
         EJECT
         PRINT NOGEN
PLTPAGE ASMENTRY SAVE=REUS,DSA=88
     STD       D0,D0SAVE                 DO   PRSERVE
     L         R11,=V(PLTEXT)
     USING     @GLOBAL,R11         R11-> GLOBAL VARIABLES
     IF        CLI,@DATA,C'1',NE   ??    IF NOTHING ON PAGE
       ASMEXIT RC=0
     ENDIF
     LA        R1,0                R1 =  0
     IF        C,R1,@CNTRL,L  ??   IF    LINE-ERROR COUNTER G.T. 0
       L       R1,@CNTRL           R1 =  @CNTRL
       CVD     R1,DOUBLE                 CONVERT TO DECIMAL
       UNPK    VALUE,DOUBLE+5(3)         UNPK VALUE TO OUTPUT LINE
       OI      VALUE+4,X'F0'             OR LAST BYTE
       LA      R0,41               R0 =  MESSAGE LENGTH
       LA      R1,MESSAGE          R1 -> MESSAGE
       CALL    PLTMSG
     ENDIF
     MVI       @DATA,C'0'                CLEAR DATA FLAG
     MVI       @ASA,C'1'                 START NEW PAGE
     L         R9,@PAGEX           R9  = LINE SIZE
     M         R8,@PAGEY           R9  = PAGE SIZE IN BYTES
     L         R6,@PAGEC           R6 -> CHARACTER PAGE
     AR        R6,R9               R6 -> BYTE BEYOND CHARACTER PAGE
     L         R7,@PAGEB           R7 -> BIT MAP PAGE
     AR        R7,R9               R7 -> BYTE BEYOND PAGE
     L         R5,@PAGEX           R5 -> LINE LENGTH FOR PRINT
     BCTR      R5,0                R5 -> MACHINE LEN FOR EX INSTRUCTION
     LOOP      TIMES=@PAGEY,REG=R8
       LA      R1,@DCB             R1 -> DCB
       PRINT   GEN
       PUT     (1)
       PRINT   NOGEN
       MVC     0(1,R1),@ASA        R1 -> BUFFER WITH ASA INSERTED
       MVI     @ASA,C' '                 RESET ASA CONTROL
*                                  R1 -> NEW BUFFER
       S       R6,@PAGEX           R6 -> CURRENT CHAR LINE
       S       R7,@PAGEX           R7 -> CURRENT MAP LINE
       EX      R5,MVCCHAR          R5    MOVE CHAR LINE TO BUFFER
       EX      R5,TRCHAR                 X'00'=> X'FF' OTHERS => X'00'
       EX      R5,ANDMAP               X'FF' CANCEL MAP CHARS IN BUFFER
       EX      R5,ORCHAR                MOVE CHARS INTO PLACE IN BUFFER
     ENDLOOP
     L         R2,@PAGEC           R2 -> FIRST OF TWO PAGES
     L         R3,@CORE            R3 -> TOTAL NUM. OF BYTES
     SR        R4,R4               R4  = 0
     SR        R5,R5               R5  = 0
     ST        R5,@CNTRL                 RESET LINE-ERROR COUNTER
     MVCL      R2,R4                     REINITIALIZE PAGES
     MVI       @ASA,C'1'                 SET FOR NEXT PAGE
     SWR       D0,D0               D0  = ZERO
     STE       E0,@XORG                  RESET GLOBAL VARIABLE
     STE       E0,@YORG                  RESET GLOBAL VARIABLE
     STE       E0,@XLAST                 RESET GLOBAL VARIABLE
     STE       E0,@YLAST                 RESET GLOBAL VARIABLE
     STE       E0,@SXLAST                RESET GLOBAL VARIABLE
     STE       E0,@SYLAST                RESET GLOBAL VARIABLE
     LD        D0,D0SAVE
     ASMEXIT
     EJECT
MVCBLNK MVC    0(0,R2),0(R3)
MVCCHAR MVC    1(0,R1),0(R6)
TRCHAR TR      1(0,R1),MASK
ANDMAP NC      1(0,R1),0(R7)
ORCHAR OC      1(0,R1),0(R6)
MESSAGE  DS    0CL41
         DC    C'NOTE:'
VALUE    DS    CL5
         DC    C' LINES WERE PLOTTED BEYOND PAGE'
         LTORG
MASK     DS    0CL256
         DC    X'FF'
         DC    XL255'00'
DSA      DSECT
D0SAVE   DS    D
DOUBLE   DS    D
DSAEND   EQU   *
         COPY    PLTGLOBL
         END   PLTPAGE
