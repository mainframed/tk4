         MACRO
&NAME    WHILE
.**********************************************************************
.*                                                                    *
.*   NAME:               WHILE                                        *
.*                                                                    *
.*   FUNCTION:                                                        *
.*       TO IMPLEMENT THE WHILE CLAUSE OF THE LOOP MACRO              *
.*                                                                    *
.*   PARAMETERS:    (1)  ASSEMBLER INSTRUCTION                        *
.*                  (2)  FIRST OPERAND OF INSTRUCTION                 *
.*                  (3)  SECOND OPERAND OF INSTRUCTION                *
.*                  (4)  COMPARISON OPERATOR                          *
.*                                                                    *
.*   USAGE:                                                           *
.*       CONSULT DOCUMENTATION FOR THE LOOP MACRO                     *
.*                                                                    *
.**********************************************************************
         GBLA  &KFQNIV,&KFQSEQ(20)
         GBLC  &KFQTYP(20),&KFQEND(20)
         LCLC  &BCOND
         LCLA  &ATIM,&AINX
         AIF   (N'&SYSLIST EQ 4).ALLOP    FOUR OPERANDS  ?
         DIAGNOS 10                       NO
         MEXIT
.ALLOP   ANOP
         AIF   ('&KFQTYP(&KFQNIV)' EQ 'LOOP').OKSTR
         MNOTE 8,'INVALID STRUCTURE - &KFQTYP(&KFQNIV) '
         MEXIT
.OKSTR   ANOP
         AIF   ('&SYSLIST(4)'(1,1) EQ 'N').BC
&BCOND   SETC  'N'.'&SYSLIST(4)'
         AGO   .BGEN
.BC      ANOP
&BCOND   SETC  '&SYSLIST(4)'(2,K'&SYSLIST(4)-1)
.BGEN    ANOP
&ATIM    SETA  N'&SYSLIST(3)           NUMBER OF LOOPS
         AIF   (&ATIM GT 1).LLIST
&NAME    &SYSLIST(1) &SYSLIST(2),&SYSLIST(3)
         B&BCOND &KFQEND(&KFQNIV)
         MEXIT
.LLIST   ANOP
.******************************************************
.* GENERATE ADDITIONAL BRANCHES IF AN OR IS SPECIFIED *
.******************************************************
&AINX    SETA  1                       OPERAND INDEX
.LLOOP   AIF   (&AINX GE &ATIM).ELOOP
         &SYSLIST(1) &SYSLIST(2),&SYSLIST(3,&AINX)
         B&SYSLIST(4) ZL&KFQNIV.D&KFQSEQ(&KFQNIV)
&AINX    SETA  &AINX+1                 NEXT OPERAND
         AGO   .LLOOP
.ELOOP   &SYSLIST(1) &SYSLIST(2),&SYSLIST(3,&AINX)
         B&BCOND &KFQEND(&KFQNIV)
ZL&KFQNIV.D&KFQSEQ(&KFQNIV) DS  0H
         MEND
