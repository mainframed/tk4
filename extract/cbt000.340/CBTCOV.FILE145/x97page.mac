*          DATA SET X97PAGE    AT LEVEL 028 AS OF 15/04/82
         TITLE 'X97PAGE - PRINT CURRENT PAGE IN PAGEPLOT SYSTEM'
***********************************************************************
*                                                                     *
*    NAME:      X97PAGE (ENTRY POINT IS PLTPAGE)                      *
*                                                                     *
*    FUNCTION:  TO MERGE THE CURRENT CORE IMAGES OF THE PLOT ONTO     *
*               A PRINTED PAGE.FOR USE WITH XEROX 9700 APPLICATION.   *
*                                                                     *
*    NOTE:      THIS IS NOT A USER INTERFACE ROUTINE , IT IS CALLED   *
*               ONLY BY PLOT                                          *
*                                                                     *
*    PARAMETERS: NONE                                                 *
*                                                                     *
*    PROCESSING:                                                      *
*               IF NOTHING TO DISPLAY                                 *
*               THEN MESSAGE AND RETURN                               *
*               MERGE LINES OF SYMBOL PAGE AND MAP PAGE               *
*                 ONTO OUTPUT BUFFER SUCH THAT LINES DO NOT           *
*                 OBSCURE CHARACTERS                                  *
*               RESET GLOBAL VARIABLES                                *
*               RETURN                                                *
*                                                                     *
***********************************************************************
         EJECT
         PRINT NOGEN
PLTPAGE ASMENTRY SAVE=REUS,DSA=88
     STD       D0,D0SAVE                 DO   PRSERVE
     L         R11,=V(PLTEXT)
     USING     @GLOBAL,R11         R11-> GLOBAL VARIABLES
     IF        CLI,@DATA,C'1',NE   ??    IF NOTHING ON PAGE
       ASMEXIT RC=0
     ENDIF
     LA        R1,0                R1 =  0
     IF        C,R1,@CNTRL,L  ??   IF    LINE-ERROR COUNTER G.T. 0
       L       R1,@CNTRL           R1 =  @CNTRL
       CVD     R1,DOUBLE                 CONVERT TO DECIMAL
       UNPK    VALUE,DOUBLE+5(3)         UNPK VALUE TO OUTPUT LINE
       OI      VALUE+4,X'F0'             OR LAST BYTE
       LA      R0,41               R0 =  MESSAGE LENGTH
       LA      R1,MESSAGE          R1 -> MESSAGE
       CALL    PLTMSG
     ENDIF
     MVI       @DATA,C'0'                CLEAR DATA FLAG
     MVI       @ASA,C'1'                 SET FOR PAGE EJECT
     L         R9,@PAGEX           R9  = LINE SIZE
     M         R8,@PAGEY           R9  = PAGE SIZE IN BYTES
     L         R6,@PAGEC           R6 -> CHARACTER PAGE
     AR        R6,R9               R6 -> BYTE BEYOND CHARACTER PAGE
     L         R7,@PAGEB           R7 -> BIT MAP PAGE
     AR        R7,R9               R7 -> BYTE BEYOND PAGE
     L         R5,@PAGEX           R5 -> LINE LENGTH FOR PRINT
     BCTR      R5,0                R5 -> MACHINE LEN FOR EX INSTRUCTION
*
     LOOP      TIMES=@PAGEY,REG=R8
       LA      R1,@DCB             R1 -> DCB
       PRINT   GEN
       PUT     (1)
       PRINT   NOGEN
*                                  R1 -> NEW BUFFER
       MVC     0(1,R1),@ASA        R1 -> BUFFER WITH ASA INSERTED
       MVI     @ASA,C' '                 RESET ASA CONTROL
       S       R6,@PAGEX           R6 -> CURRENT CHAR LINE
       S       R7,@PAGEX           R7 -> CURRENT MAP LINE
       EX      R5,MVCCHAR          R5    MOVE CHAR LINE TO BUFFER
       EX      R5,TRCHAR                 X'00'=> X'FF' OTHERS => X'00'
       EX      R5,ANDMAP               X'FF' CANCEL MAP CHARS IN BUFFER
       EX      R5,ORCHAR                MOVE CHARS INTO PLACE IN BUFFER
       EX      R5,TRXROX                 TRANSLATE TO XEROX CHARACTERS
     ENDLOOP
     L         R2,@PAGEC           R2 -> FIRST OF TWO PAGES
     L         R3,@CORE            R3 -> TOTAL NUM. OF BYTES
     SR        R4,R4               R4  = 0
     SR        R5,R5               R5  = 0
     ST        R5,@CNTRL                 RESET LINE-ERROR COUNTER
     MVCL      R2,R4                     REINITIALIZE PAGES
     MVI       @ASA,C'1'                 SET FOR NEXT PAGE
     SWR       D0,D0               D0  = ZERO
     STE       E0,@XORG                  RESET GLOBAL VARIABLE
     STE       E0,@YORG                  RESET GLOBAL VARIABLE
     STE       E0,@XLAST                 RESET GLOBAL VARIABLE
     STE       E0,@YLAST                 RESET GLOBAL VARIABLE
     STE       E0,@SXLAST                RESET GLOBAL VARIABLE
     STE       E0,@SYLAST                RESET GLOBAL VARIABLE
     LD        D0,D0SAVE
     ASMEXIT
     EJECT
MVCCHAR MVC    1(0,R1),0(R6)
TRCHAR  TR     1(0,R1),MASK
TRXROX  TR     1(0,R1),XRXTABLE
ANDMAP  NC     1(0,R1),0(R7)
ORCHAR  OC     1(0,R1),0(R6)
MESSAGE  DS    0CL41
         DC    C'NOTE:'
VALUE    DS    CL5
         DC    C' LINES WERE PLOTTED BEYOND PAGE'
         LTORG
XRXTABLE DS    0CL256
         DC    X'400B0C0D0E0F10111213141516171819'
         DC    X'1A1B1C1D1E1F20212223242526272829'
         DC    X'2A2B2C2D2E2F30313233343536373839'
         DC    X'3A3B3C3D3E3F41424344454647484951'
         DC    X'000000000000000000004A4B4C4D4E4F'
         DC    X'500000535455565758595A5B5C5D5E5F'
         DC    X'60610063646566676869006B6C6D6E6F'
         DC    X'000000000000000000007A7B7C7D7E7F'
         DC    X'00000000000000000000000000000000'
         DC    X'0000000000000000000000009C000000'
         DC    X'00000000000000000000000000000000'
         DC    X'00000000000000000000000000000000'
         DC    X'00C1C2C3C4C5C6C7C8C9000000000000'
         DC    X'00D1D2D3D4D5D6D7D8D9000000000000'
         DC    X'0000E2E3E4E5E6E7E8E9000000000000'
         DC    X'F0F1F2F3F4F5F6F7F8F9000000000000'
MASK     DS    0CL256
         DC    X'FF'
         DC    XL255'00'
DSA      DSECT
D0SAVE   DS    D
DOUBLE   DS    D
DSAEND   EQU   *
         COPY PLTGLOBL
         END   PLTPAGE
