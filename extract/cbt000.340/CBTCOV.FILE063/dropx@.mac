         MACRO
         DROPX &PARMS,&DUMMY
.*.
.*$MACRO=DROPX      DATE=04/00/79       SOURCE=UCLA+LDW
.*
.*  THIS MACRO REMOVES AN ENTRY FROM THE USING TABLE BUILT BY 'USNGX'
.*       (DISPLAYED BY 'INUSE').  IT THEN DOES A REGULAR 'DROP'.
.*.
         GBLB  &USESKIP                 USNGX SKIPPED SAVE TABLE SETUP
         GBLC  &USELAB(32),&USEREG(16)  PGM LABELS,REGISTERS USED
         LCLA  &RCNT,&UNDX              # OF OPERANDS, USE INDICES
         LCLA  &PNDX,&DNDX              INPUT PARM,DROP STRING INDICES
         LCLC  &C(16),&R(16)            COMMAS,REGS TO DROP
         LCLB  &GOTREG                  FOUND A REG FOR DROP
         LCLC  &SPACE                   A SPACE FOR SDM
&RCNT    SETA  N'&SYSLIST               COUNT THE OPERANDS/REGISTERS
         AIF   (&RCNT EQ 0).DROPALL     BR IF ALL
         AIF   ('&PARMS' EQ '').DROPALL   BR IF ALL
         AIF   ('&PARMS' EQ '*').DROPALL    BR IF ALL
.COMLOOP ANOP                           BUILD COMMA/REG LIST LOOP
&PNDX    SETA  &PNDX+1                  UP THE COMMA/REG INDEX
&UNDX    SETA  0                        INITIALIZE USE ARRAY INDEX
.REGCHK  ANOP                           VALID DROP REG CHECK LOOP
&UNDX    SETA  &UNDX+1                  UP THE USE ARRAY COUNT
         AIF   ('&USEREG(&UNDX)' EQ '&SYSLIST(&PNDX)').SETREG REG OK?
         AIF   (&UNDX LT 16).REGCHK     LOOP FOR ALL ENTRIES IF NEEDED
         MNOTE 4,'REGISTER &SYSLIST(&PNDX) WAS NOT A USNGX OPERAND'
         AIF   (NOT &USESKIP).REGLOOP   NO DROP IF USNGX DID NO SKIPS
.SETREG  ANOP
&DNDX    SETA  &DNDX+1                  UP THE DROP STRING INDEX
&R(&DNDX) SETC '&SYSLIST(&PNDX)'        GET A REG
&C(&DNDX) SETC ','                      AND A COMMA
&USEREG(&UNDX) SETC ''                  BLANK THE REG/INDICATOR FIELD
&GOTREG  SETB  1                        SAY HAVE SOMETHING TO DROP
.REGLOOP AIF   (&PNDX LT &RCNT).COMLOOP LOOP TIL DONE WITH REGS
.DROPGO  AIF   (NOT &GOTREG).NOTHING    SKIP THE DROP IF NOTHING TO DO
&C(&DNDX) SETC ''                       KILL THE LAST COMMA
         DROP  &R(1)&C(1)&R(2)&C(2)&R(3)&C(3)&R(4)&C(4)&R(5)&C(5)&R(6)&^
               C(6)&R(7)&C(7)&R(8)&C(8)&R(9)&C(9)&R(10)&C(10)&R(11)&C(1^
               1)&R(12)&C(12)&R(13)&C(13)&R(14)&C(14)&R(15)&C(15)&R(16)
         MEXIT
.DROPALL AIF   (&UNDX GE 16).DROPGO     CHECK FOR END OF TABLE
&UNDX    SETA  &UNDX+1                  UP THE USE INDEX COUNT
         AIF   ('&USEREG(&UNDX)'  EQ '').DROPALL
&DNDX    SETA  &DNDX+1                  UP THE DROP STRING INDEX
&R(&DNDX) SETC '&USEREG(&UNDX)'         GET REG FOR THE DROP
&C(&DNDX) SETC ','                      AND A DELIMITER
&SPACE   SETC  ' '                      ASSUME 1 SPACE
         AIF   (K'&USEREG(&UNDX) GT 2).MNOTE
&SPACE   SETC  '  '
.MNOTE   MNOTE *,'&USEREG(&UNDX)&SPACE.WAS BASE FOR &USELAB(2*&UNDX-1)&$
               USELAB(2*&UNDX)'
&USEREG(&UNDX) SETC ''                  KILL THE REG/INDICATOR
&GOTREG  SETB  1                        SAY WE FOUND SOMETHING
         AGO   .DROPALL                 CONTINUE WITH THE SCAN
.NOTHING MNOTE *,'NO REGISTERS IN USE'
         MEND
