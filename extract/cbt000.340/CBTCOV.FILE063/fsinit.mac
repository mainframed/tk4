         MACRO
&LABEL   FSINIT &SCREEN,&EXTRN=NO
&LABEL   DS    0H
         GBLC  &FSINIT
         AIF   ('&FSINIT' EQ 'YES').ERR1
         AIF   ('&SCREEN' EQ '').ERR2
&FSINIT  SETC  'YES'
         B     CONT&SYSNDX
PGM#     DC    V(FULSCR)
PARM#    DC    A(MOD#)
         AIF   ('&EXTRN' EQ 'YES').SCRNAME
SCR#     DC    A(&SCREEN)
         AGO   .C1
.SCRNAME ANOP
SCR#     DC    V(&SCREEN)
.C1      DC    A(LIN#)
         DC    A(ROW#)
         DC    A(COL#)
         DC    A(ALA#)
         DC    A(UPP#)
         DC    A(KEY#)
MOD#     DS    C
LIN#     DS    H
ROW#     DS    H
COL#     DS    H
ALA#     DS    C
UPP#     DS    C
KEY#     DS    H
CONT&SYSNDX DS 0H
         MVI   MOD#,C'I'
         MVC   LIN#,=H'0'
         MVC   ROW#,=H'0'
         MVC   COL#,=H'0'
         MVI   ALA#,C' '
         MVI   UPP#,C'U'
         MVC   KEY#,=H'0'
         LA    1,PARM#
         L     15,PGM#
         BALR  14,15
         AGO   .MACEND
.ERR1    MNOTE 8,'FSINIT HAS ALREADY BEEN CALLED'
         AGO   .MACEND
.ERR2    MNOTE 8,'SCREEN PARAMETER HAS BEEN OMITTED'
.MACEND  ANOP
         MEND
