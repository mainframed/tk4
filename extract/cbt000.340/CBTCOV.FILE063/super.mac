         MACRO
&NAME    SUPER  &TYPE,&SUP=NO,&REGDEF=YES
         GBLC  &CSECT,&CSECT2
         GBLB  &SUPER,&GS
         LCLA  &I,&MAX
         LCLC  &P
&SUPER   SETB  1
&CSECT   SETC  '&NAME'
&CSECT2  SETC  '&NAME'(1,6).'S2'
&NAME    CSECT
         USING &NAME,15
         AIF   ('&SUP' EQ 'NO').NOSUP
         B     *+24                BRANCH AROUND ID
         DC    V(SUPER)
.BKNOSUP ANOP
         DC    CL8'&NAME.'
         DC    CL8'&SYSDATE'
         STM   14,12,12(13)        SAVE REGS
         DROP  15
         LR    11,15               SET UP ADDRESSABLITY
         LA    12,2048(11)
         LA    12,2048(12)
         USING &NAME,11,12
         AIF   ('&REGDEF' EQ 'NO').NOREGD
         REGDEF
.NOREGD  ANOP
         AIF   ('&TYPE' EQ '').MULT
         AIF   ('&TYPE' EQ 'RENT').RENT
         AIF   ('&TYPE' EQ 'REUS').REUS
         AIF   ('&TYPE' EQ 'MULT').MULT
.RENT    ANOP
         GETS                      GET SAVE AREA
&GS      SETB  1
         AGO   .ENDTYPE
.REUS    ANOP
         REUS                      INSURE ONLY ONE USER AT A TIME
         AGO   .ENDTYPE
.NOSUP   ANOP
         B     *+20                BRANCH AROUND NAME
         AGO   .BKNOSUP
.MULT     ANOP
&CSECT2  CSECT
SAVE     DS   18F
&CSECT   CSECT
         L     14,=V(&CSECT2)      POINT TO SAVE AREA
         ST    13,4(14)
         LR    13,14
         USING &CSECT2,13
.ENDTYPE ANOP
.END     ANOP
         MEND
