         MACRO
.**********************************************************************
.*                                                                    *
.*  04/05/84 U002 LDW  LINK TO GETNAME, INSTEAD OF CALL VIA VCON      *
.*                                                                    *
.* U001 LLM 03/06/84 ADD MSGCLASS TO PARSE. MODIFY CODE TO INCLUDE    *
.*                   INCLUDE THE MSGCLASS. THE PROGRAM USE TO USE     *
.*                   THE SYSOUT CLASS FOR THE MESSAGE CLASS. THIS     *
.*                   CAUSED A PROBLEM WHEN SYSOUT CLASS "F" WAS USED. *
.*                                                                    *
.* MODIFICATION: 01/09/80 - SINCE WE NOW HAVE THE ABILITY TO OBTAIN   *
.*               A USER NAME (EITHER FROM USE OF 'SETNAME' COMMAND OR *
.*               TSO.USER DATASET) THERE IS NO LONGER A REASON NOT TO *
.*               INCLUDE USER DEFINED NAME ON THE BANNER. JOBCARDS    *
.*               MACRO HAS BEEN MODIFIED TO OBTAIN THE USER DEFINED   *
.*               NAME BY USE OF THE SUBPROGRAM 'GETNAME'.             *
.*                                                                    *
.* MODIFICATION: 06/16/83 - THIS MODIFICATION IS BEING MADE TO        *
.*               SUPPORT NON-STANDARD USERID'S. THAT IS THOSE THAT    *
.*               ARE LESS THAN SEVEN CHARACTERS.                      *
.*                                                                    *
.**********************************************************************
.**********************************************************************
.*    TO DATE THIS MACRO IN USED IN THE FOLLOWING PROGRAMS            *
.*       DATASET NAME                                      MEMBER     *
.*       --------------------------------------------      --------   *
.*       SYS5.LISTS.ASM                                    SLISTS     *
.**********************************************************************
         JOBCARDS &POP,&SOP
         AIF   ('&SOP' EQ 'PARSE').PARSE
         AIF   ('&SOP' EQ 'WRKSPACE').WRKSPCE
         AIF   ('&SOP' EQ 'CONSTANT').CNSTANT
         MVC   JOBCARD1(160),JOBCARDS INITIALIZE THE TWO JOB CARDS
         L     R1,CPPLPSCB        R1 -> USERID
*        MVC   JOBNAME(7),0(R1)   SET MOST OF THE JOBNAME      06/16/83
*        MVC   NOTIFY(7),0(R1)    SET THE NOTIFY PARAMETER     06/16/83
         SR    R15,R15            ZERO REG 15                  06/16/83
         IC    R15,7(R1)          R2 <- LENGTH USERID          06/16/83
         BCTR  R15,R0             ADJUST LENGTH                06/16/83
         EX    R15,MOVEJOBN       MOVE USERID TO JOBNAME       06/16/83
         EX    R15,MOVENOT        MOVE USERIF TO NOTIFY        06/16/83
         LA    R1,JOBNAME         ESTABLISH JOBCHAR ADDRESS    06/16/83
         LA    R1,0(R15,R1)                                    06/16/83
         ST    R1,JCHARLOC        SAVE JOBCHAR ADDRESS         06/16/83
         TM    NAME+6,X'80'       NAME FIELD SPECIFIED?
         BO    ISNAME             YES => SET THAT FIELD
         LA    R1,PGMRNAME        A WORD TO RCV A(PGM'S NAME)  01/09/80
         ST    R1,GETNAMPL        STORE A(PGM'S NAME) IN PARM  01/09/80
         LA    R1,GETNAMPL        SET PARM ADDRESS             01/09/80
*U002    L     R15,=V(GETNAME)    ESTABLISH BRANCH ADDRESS     01/09/80
*U002    BALR  R14,R15            BRANCH TO GETNAME            01/09/80
         LINK  EP=GETNAME                                          U002
         LTR   R15,R15            TEST FOR NAME                01/09/80
         BNZ   DEFNAME            NO NAME                      01/09/80
         L     R15,PGMRNAME       R15 <- ADDRESS OF NAME       01/09/80
         CLC   =CL20' ',0(15)     TEST FOR NAME BEING BLANK    01/09/80
         BE    DEFNAME            NO NAME                      01/09/80
         LA    R1,19              R1 <- LENGTH OF NAME         01/09/80
         B     MVETHNME                                        01/09/80
DEFNAME  L     R1,CPPLPSCB        R1 <- USERID                 01/09/80
         MVC   NAMEFLD(7),0(R1)   DEFAULT NAME OF USERID
         MVC   NAMEFLD+7(2),=C''',' CLOSE OFF THIS PARAMETER
         B     TSTJOBCR           TEST FOR JOBCHAR SPECIFICATION
ISNAME   LH    R1,NAME+4          R1 <- LENGTH OF NAME SPECIFIED
         L     R15,NAME           R15 -> WHERE THE NAME COMES FROM
         BCTR  R1,R0              PUT THE NAME
MVETHNME EX    R1,MOVENAME         ON THE JOBCARD
         LA    R14,NAMEFLD+1(R1)  BE SURE TO
         MVC   0(2,R14),=C''','   CLOSE OFF THE PARAMETER
TSTJOBCR TM    JOBCHAR+6,X'80'    JOB CHARACTER SPECIFIED?
         BO    ISJOBCAR           YES => PUT IT IN THE JOBNAME
         RANDOM# MODULO=39    R1 <- RANDOM NUMBER BETWEEN 0 & 39
         LA    R1,JOBCHARS(R1)    SET UP THE JOB CHARACTER
         B     SETJOBCR           PUT IT IN THE JOBNAME
ISJOBCAR L     R1,JOBCHAR         R1 -> THE SPECIFIED JOB CHARACTER
*ETJOBCR MVC   JOBCAR(1),0(R1)    PUT IN THE JOB CHARACTER     06/16/83
SETJOBCR L     R15,JCHARLOC       R2 <- JOBCHAR ADDRESS        06/16/83
         MVC   1(1,R15),0(R1)     PUT IN THE JOB CHARACTER     06/16/83
         TM    ACCOUNT+6,X'80'    AN ACCOUNT # SPECIFIED?
         BO    ISACCNT            YES => CHECK ITS LENGTH
         ACCOUNT ACCNTFLD         PUT IN THE DEFAULT ACCOUNT NUMBER
         B     TESTMSGC           TEST FOR MSGCLASS SPECIFICATION  U001
ISACCNT  L     R15,ACCOUNT        R15 -> ACCOUNT NUMBER
         MVC   ACCNTFLD(6),0(R15) SET THE ACCOUNT NUMBER FIELD
TESTMSGC TM    MSGCL+6,X'80'      A MSGCLASS SPECIFIED             U001
         BO    ISMSGCL            YES                              U001
         MVI   MSGCLASS,C'A'      PUT IN DEFAULT MSGCLASS          U001
         AIF   ('&POP' EQ 'PUNCH').PUNCH10                         U001
         B     TESTSYS            TEST FOR SYSOUT SPECIFICATION    U001
         AGO   .PUNCH11                                            U001
.PUNCH10 B     NOSYS              TEST FOR A REMOTE                U001
.PUNCH11 ANOP                                                      U001
ISMSGCL  L     R15,MSGCL          R15 - > MSGCLASS                 U001
         MVC   MSGCLASS(1),0(R15) SET MSGCLASS                     U001
         AIF   ('&POP' EQ 'PUNCH').PUNCH12
TESTSYS  TM    SYSOUT+6,X'80'     SYSOUT SPECIFIED?
         BZ    NOSYS              NO => DEFAULT TO 'A'
         L     R15,SYSOUT         SAME MESSAGE CLASS
*U001    MVC   MSGCLASS(1),0(R15)  AS SYSOUT
.PUNCH12 ANOP
NOSYS    LA    R14,JOBCARD3       MAY OR MAY NOT BE
         MVI   JOBPARM,2           2 MORE CARDS
         TM    REMOTE+6,X'80'     REMOTE SPECIFIED?
         BZ    NOREMOTE           NO => CLOSE OFF THIS PARAMETER
         AIF   ('&POP' EQ 'PUNCH').PUNCH1
         MVC   0(80,R14),=CL80'/*ROUTE  PRINT'
         AGO   .PUNCH2
.PUNCH1  MVC   0(160,R14),ROUTECRD INITIALIZE THE ROUTE CARDS
.PUNCH2  L     R15,REMOTE         INDICATE WHICH REMOTE
         LH    R1,REMOTE+4        R1 <- THE LENGTH
         BCTR  R1,R0               OF THE SPECIFICATION - 1
         EX    R1,MOVEREM         PUT IN THE SPECIFICATION
         B     SETROUTE       COMPLETE THE ROUTING
NOREMOTE L     R1,CPPLPSCB    A(PSCB)
         USING PSCB,R1        ADDRESSABILITY
         CLI   PSCBATR2+1,0   LOCAL ORIGIN?
         BE    TESTLC         YES => NO NEED FOR A ROUTE CARD
         MVC   0(80,R14),=CL80'/*ROUTE  PRINT RMT'
         CLI   PSCBATR2+1,9   RMT NUMBER > 9?
         BH    TWODIGS        YES => 2 DIGITS
         MVC   18(1,R14),PSCBATR2+1 PUT THE NUMBER IN THE CARD
         OI    18(R14),C'0'   CONVERT IT TO EBCDIC
         B     SETROUTE       COMPLETE THE ROUTING
TWODIGS  SR    R0,R0          FOR THE INSERT CHARACTER
         IC    R0,PSCBATR2+1  ROUTE NUMBER
         CVD   R0,SAVEAREA+16 CONVERT TO DECIMAL
         UNPK  18(2,R14),SAVEAREA+22(2) TO ZONED
         OI    19(R14),C'0'   TO EBCDIC
         AIF   ('&POP' EQ 'PUNCH').PUNCH3
SETROUTE LA    R14,80(R14)        SKIP TO THE NEXT CARD
         MVI   JOBPARM,3          3 JOB RELATED CARDS SO FAR
         AGO   .PUNCH4
.PUNCH3  EX    R1,MOVEREMU         ON BOTH CARDS
         MVI   JOBPARM,4          4 JOB RELATED CARDS
NOREMOTE LA    R1,JOBCARD1        PASS THE CARDS
         AGO   .PUNCH5
.PUNCH4  ANOP
TESTLC   TM    LINECNT+6,X'80'    LINE COUNT SPECIFIED?
         BZ    NOLC               NO => JOB CARDS ARE SET
         L     R15,LINECNT        R15 -> THE SPECIFIED LINE COUNT
         CLI   LINECNT+5,2        2 DIGITS IN THE LINE COUNT?
         BNE   SETLC              NO => SET THE USER'S SPECIFICATION
         CLC   0(2,R15),=C'88'    LINECNT = 88?
         BE    NOLC               YES => THAT'S THE DEFAULT ANYWAY
SETLC    MVC   0(80,R14),=CL80'/*JOBPARM LINECT='
         LH    R1,LINECNT+4       R1 <- THE NUMBER OF
         BCTR  R1,R0               DIGITS - 1
         EX    R1,MOVELC          SET THE LINE COUNT
         IC    R1,JOBPARM     INCREMENT
         LA    R1,1(R1)        THE JOB
         STC   R1,JOBPARM      CARD COUNT
NOLC     LA    R1,JOBCARD1        PASS THE CARDS
.PUNCH5  STCM  R1,7,JOBPARM+1      AND THE COUNT
         MEXIT
.CNSTANT ANOP
MOVEJOBN MVC   JOBNAME(0),0(R1)                                06/16/83
MOVENOT  MVC   NOTIFY(0),0(R1)                                 06/16/83
MOVENAME MVC   NAMEFLD(0),0(R15)
MOVEREM  MVC   15(0,R14),0(R15)
         AIF   ('&POP' EQ 'PUNCH').PUNCH6
MOVELC   MVC   17(0,R14),0(R15)
         AGO   .PUNCH7
.PUNCH6  ANOP
MOVEREMU MVC   95(0,R14),0(R15)
ROUTECRD DC    CL80'/*ROUTE  PRINT'
         DC    CL80'/*ROUTE  PUNCH'
.PUNCH7  ANOP
JOBCARDS DC    CL80'//         JOB (1,TSO,ACCNT#,00,42,SNUMB),'''
         DC   CL80'// MSGCLASS=A,CLASS=E,NOTIFY=        '
JOBCHARS DC    CL40'ABCDEFGHIJKLMONPQRSTUVWXYZ0123456789#@$'
         MEXIT
.PARSE   ANOP
JKEY     IKJKEYWD
         IKJNAME 'JOBCHAR',SUBFLD=JSUB
AKEY     IKJKEYWD
         IKJNAME 'ACCOUNT',SUBFLD=ASUB
NKEY     IKJKEYWD
         IKJNAME 'NAME',SUBFLD=NSUB
MCKEY    IKJKEYWD
         IKJNAME 'MSGCLASS',SUBFLD=MCSUB
RKEY     IKJKEYWD
         IKJNAME 'ROUTE',SUBFLD=RSUB
SKEY     IKJKEYWD
         IKJNAME 'SYSOUT',SUBFLD=SSUB
         AIF   ('&POP' EQ 'PUNCH').PUNCH8
LCKEY    IKJKEYWD
         IKJNAME 'LINECNT',SUBFLD=LCSUB
.PUNCH8  ANOP
JSUB     IKJSUBF
JOBCHAR  IKJIDENT 'JOB CHARACTER',UPPERCASE,MAXLNTH=1,FIRST=ALPHANUM
ASUB     IKJSUBF
ACCOUNT  IKJIDENT 'ACCOUNT NUMBER',UPPERCASE,MAXLNTH=6,FIRST=ALPHA,    *
               OTHER=ALPHANUM,VALIDCK=ACCTCHK,                         *
               HELP='6 CHARACTERS EXACTLY'
NSUB     IKJSUBF
NAME     IKJPOSIT QSTRING,UPPERCASE,VALIDCK=NAMECHK,                   *
               HELP='20 CHARACTERS OR LESS'
MCSUB    IKJSUBF
MSGCL    IKJIDENT 'MSGCLASS',UPPERCASE,MAXLNTH=1,FIRST=ALPHA
RSUB     IKJSUBF
REMOTE   IKJIDENT 'ROUTING',UPPERCASE,MAXLNTH=8,FIRST=ALPHA,           *
               OTHER=ALPHANUM
SSUB     IKJSUBF
SYSOUT   IKJIDENT 'SYSOUT CLASS',UPPERCASE,MAXLNTH=1,FIRST=ALPHA
         AIF   ('&POP' EQ 'PUNCH').MEND
LCSUB    IKJSUBF
LINECNT  IKJIDENT 'LINE COUNT',UPPERCASE,MAXLNTH=2,FIRST=NUMERIC,      *
               OTHER=NUMERIC
         MEXIT
.WRKSPCE ANOP
JCHARLOC DS    F                                               06/16/83
GETNAMPL DS    A                                               01/09/80
PGMRNAME DS    A                                               01/09/80
JOBPARM  DS    A
JOBCARD1 DS    CL2
JOBNAME  DS    CL7
JOBCAR   DS    CL13
ACCNTFLD DS    CL21
NAMEFLD  DS    CL37
JOBCARD2 DS    CL12
MSGCLASS DS    CL17
NOTIFY   DS    CL51
JOBCARD3 DS    CL160
.MEND    MEND
