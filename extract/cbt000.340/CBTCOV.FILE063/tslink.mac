         MACRO
&NAME    TSLINK &PARM=,&LINKEP=,&XCTLEP=,&FREEDA=YES,                  *
               &CUP=,&CUL=,&CPPL=(TSDCPPLP,),&TO=TSLINK,               *
               &UWA=,&LPL=,&PREFIX=LPL,&PREG=NO
         COPY  MATGBL
         LCLC  &R,&P,&N1,&N2,&TONAM
.*
.*
.*
.*
&R       SETC  '&MATREGS'
&P       SETC  '&PREFIX'
&NAME    DS    0H .                     EXIT TSO COMMAND PROCESSOR
         AIF   ('&LPL' EQ '').GETLPL
         LOADR &R.1,&LPL
         RUSE  &P,&R.1
         AGO   .USELPL                  GO FILL IN LPL
.GETLPL  ANOP
         TSDSECT LPL
         STM   &R.15,&R.0,TSDSAVE+16 .  SAVE OVER GETMAIN
*        GETMAIN R,LV=LPLL,SP=1
         GETMAIN R,LV=&P.L,SP=1
         LM    &R.15,&R.0,TSDSAVE+16 .  RESTORE USERS REGS
         RUSE  &P,&R.1 .                ADDRESS THE LPL
         XC    &P.(&P.L),&P .           CLEAR THE LPL
.USELPL  ANOP  FORMAT THE LPL
         IHMLSPTR &PARM,&P.PARM,'SAVE THE USERS PARAMETER'
         IHMLSPTR &UWA,&P.UWA,'SAVE THE USER WORK AREA POINTER'
         IHMLSPTR &CUP,&P.CUP
         IHMLSPTR &CUL,&P.CUL
         IHMLSPTR &CPPL,&P.CPPLP
&N1      SETC  '&LINKEP'
         AIF   ('&LINKEP' NE '').SETLEP
         AIF   ('&LPL' NE '').TSTXEP
         MNOTE 8,'LINKEP MUST BE GIVEN, IEFBR14 ASSUMED'
&N1      SETC  'IEFBR14'
.SETLEP  ANOP
         MVC   &P.LINKN,IHB&SYSNDX.B .  SET THE LINK EP NAME
         IHMLSPTR &P.LINKN,&P.LINKL,'BUILD LINK PARM LIST'
.TSTXEP  ANOP
&N2      SETC  '&XCTLEP'
         AIF   ('&XCTLEP' NE '').SETXEP
         AIF   ('&LPL' NE '').TSTTOEP
         MNOTE *,'XCTLEP OMITTED, NO XCTL ASSUMED'
         AGO   .TSTTOEP
.SETXEP  ANOP
         MVC   &P.XCTLN,IHB&SYSNDX.C .  SET THE XCTL MODULE NAME
         IHMLSPTR &P.XCTLN,&P.XCTLL,'BUILD XCTL PARM LIST'
.TSTTOEP ANOP
         AIF   ('&TO' NE '').SETTO
         MNOTE 0,'''TO'' NAME NOT GIVEN, TSLINK ASSUMED'
&TONAM   SETC  'TSLINK'
         AGO   .CKFREE
.SETTO   ANOP
&TONAM   SETC  '&TO'
.CKFREE  ANOP
         AIF   ('&FREEDA'(1,1) NE 'Y').NOFREE1
         L     &R.15,IHB&SYSNDX.A .     GET CLEAN UP ADDR
         BALR  &R.14,&R.15 .            GO CLEAN UP COMMAND PROCESSOR
.NOFREE1 ANOP
         AIF   ('&PREG'(1,1) NE 'Y').XCTLNOR
         MNOTE *,' XCTL (&R.2,&R.12),EP=&TO,MF=(E,(&R.1)) '
         XCTL  (2,12),EP=&TO,MF=(E,(1)) .  GO TO NEW MODULE
         AGO   .CKCONS
.XCTLNOR ANOP
         MNOTE *,' XCTL EP=&TO,MF=(E,(1)) '
         XCTL  EP=&TO,MF=(E,(1))
.CKCONS  ANOP
         AIF   ('&FREEDA'(1,1) NE 'Y').END
         AIF   ('&MATTSD1' NE '').HAVEBEP
&MATTSD1 SETC  'IHB&SYSNDX.Z'
.HAVEBEP ANOP
         AIF   ('&MATTSL1' NE '').HAVTSL1
&MATTSL1 SETC  'IHB&SYSNDX.Q'
.HAVTSL1 ANOP
IHB&SYSNDX.A DC A(&MATTSL1) .           BRANCH EPA FOR CLEAN UP
IHB&SYSNDX.B DC CL8'&N1'
         AIF   ('&N2' EQ '').END
IHB&SYSNDX.C DC CL8'&N2'
.END     MEND
