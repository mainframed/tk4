         MACRO ,                                                  40000
&NAME    GETTMC &VOLSER,&IOAREA,&ERRTN,&ENQ=YES
.*
         AIF   ('&VOLSER' EQ '').MNOTE1
         AIF   ('&IOAREA' EQ '').MNOTE1
         AIF   ('&ERRTN'  EQ '').MNOTE1
         AIF   ('&ENQ' EQ 'YES' OR '&ENQ' EQ 'NO').GET
         AGO   .MNOTE2
.*
.GET     ANOP
&NAME    DS    0H -               GETTMC  MACRO
         AIF   ('&VOLSER'(1,1) EQ '(').GET01
         LA    R1,&VOLSER -       POINT TO VOLUME SERIAL NUMBER
         AGO   .GET02
.GET01   ANOP
         AIF   ('&VOLSER' EQ '(R1)').GET02A
         LR    R1,&VOLSER(1) -    POINT TO VOLUME SERIAL NUMBER
.GET02   ANOP
         L     R15,TMSHASH -      GET ADDRESS OF TMSHASH
         BALR  R14,R15 -          VERIFY VOLUME SERIAL NUMBER
         LTR   R1,R1 -            Q. VALID VOLUME SERIAL NUMBER
         BZ    &ERRTN -           A. ZERO - NO
.GET02A  ANOP
         XC    TMSPARM,TMSPARM -  CLEAR PARAMETER LIST
         ST    R1,TMRELBLK -      POST RELATIVE BLOCK ADDRESS
         AIF   ('&IOAREA'(1,1) EQ '(').GET03
         LA    R1,&IOAREA -       POINT TO THE TMC IOAREA
         ST    R1,TMIOADDR-1 -    POST IOAREA ADDRESS
         AGO   .GET04
.GET03   ANOP
         ST    &IOAREA(1),TMIOADDR-1 - POST IOAREA ADDRESS
.GET04   ANOP
         MVI   TMOPCODE,24 -      POST OP-CODE
         AIF   ('&ENQ' NE 'YES').GET05
         OI    TMOPBITS,X'80' -   POST AS ENQ REQUESTED
.GET05   ANOP
         L     R15,TMSVCRTN -     GET ENTRY POINT ADDRESS
         LA    R1,TMSPARM -       POINT TO PARAMETER LIST
         BALR  R14,R15 -          READ THE TMC RECORD
.*
         MEXIT
.*
.MNOTE1  MNOTE 8,'GETTMC MACRO MISSING PARAMETERS'
         MEXIT
.MNOTE2  MNOTE 8,'ENQ= INVALID'
         MEXIT
.*
         MEND
