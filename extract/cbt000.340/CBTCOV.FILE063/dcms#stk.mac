         MACRO
&N       DCMS#STK &A,&SIZE=100,&LINE=79,&ENTRY=79,&R=11,&FRAME=20,     X
               &AREA=,&CLEAR=YES
.*.
.*$MACRO=DCMS#STK   DATE=03/31/80       SOURCE=LDW
.*$MACRO=#STAK      DATE=10/23/79       SOURCE=DCMS (DLH@SDC)
.*
.*
.*.
         AIF  (K'&N LT 5).GEN
         MNOTE 9,'STACK NAME MUST BE FOUR BYTES OR LESS'
         MEXIT
.MSG2    MNOTE 9,'AREA ADDRESS SPECIFICATION MISSING OR INVALID'
         MEXIT
.GEN     AIF   ('&A' EQ 'RESET').RESET
         AIF   ('&A' EQ 'BUILD').GETT
         AIF   ('&A' EQ 'GET').GET1
         AIF   ('&A' EQ 'INIT').INIT
         MEXIT
.GET1    CNOP  0,4                      DO AN EXPLICIT GETMAIN
         BAL   1,*+8                       TO
         DC    A(&SIZE*79)                    AVOID
         L     0,0(0,1)                           MACRO
         SVC   10                                      CONFUSION
         ST    R1,#TOP&N                TOP OF STACK
         SPACE 2
         AGO   .INIT2
.GETT    CNOP  0,4                      DO AN EXPLICIT GETMAIN
         BAL   1,*+8                       TO
         DC    A(&SIZE*79+64)                AVOID
         L     0,0(0,1)                           MACRO
         SVC   10                                      CONFUSION
         LR    &R.,1
         USING &N.,&R
         LA    R1,#SSS&N
         ST    R1,#TOP&N                TOP OF STACK
         SPACE 2
         AGO   .INIT2
.INIT    AIF   ('&AREA' EQ '').MSG2
         LA    R1,&AREA                 TOP OF STACK
         ST    R1,#TOP&N
.INIT2   L     R1,=A(&SIZE*79)
         ST    R1,#LEN&N                SIZE OF STACK
         LA    R1,&ENTRY
         ST    R1,#MVC&N                INPUT ENTRY SIZE
         LA    R1,&SIZE
         ST    R1,#SIZ&N                NUMBER OF LINES
         LA    R1,&FRAME                LINES PER FRAME
         ST    R1,#LPF&N
         L     R1,=A(&FRAME*79)
         ST    R1,#SCR&N                SIZE OF FRAME
         L     R1,#TOP&N                SET TOP OF STAK IN:
         ST    R1,#STK&N                - CURRENT LINE
         ST    R1,#NOW&N                - CURRENT FRAME
         A     R1,#LEN&N                AND
         ST    R1,#END&N                - BOTTOM OF STACK
         SPACE 2
.CLEAR   AIF   ('&CLEAR' EQ 'NO').FINISH
         L     R14,#TOP&N               CLEAR
         L     R15,#LEN&N                    THE
         LR    R0,R14                           ENTIRE
         SR    R1,R1                                  STACK
         ICM   R1,8,BLANKS                                 TO
         MVCL  R14,R0                                        BLANKS
.FINISH  SPACE 1
         MEXIT
.RESET   ANOP
*                                       RESET:
         L     R1,#TOP&N                TOP OF STAK IN:
         ST    R1,#STK&N                - CURRENT LINE
         ST    R1,#NOW&N                - CURRENT FRAME
         AGO   .CLEAR
         MEND
