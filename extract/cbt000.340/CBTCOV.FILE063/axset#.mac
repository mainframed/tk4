         MACRO -- AXSET -- SET AUTHORIZATION INDEX
&LABEL   AXSET &AX=
.* 5JAN83 WRITTEN BY STEVEN D. MCGINTY
&LABEL   STM   14,12,12(13)          .SAVE CALLER'S REGS
         AIF   ('&AX'(1,1) EQ '(').REG
         SLR   0,0                   .ASSURE UPPER HALFWD IS ZERO
         ICM   0,B'0011',&AX         .SET REG 0 TO AX VALUE
         AGO     .NOMOVE
.REG     AIF   ('&AX' EQ '(0)').NOMOVE
         AIF   (N'&AX EQ 1).OK1
         LR    0,&AX                 .SET REG 0TO AX VALUE
         AGO     .NOMOVE
.OK1     LR    0,&AX(1)              .SET REG 0TO AX VALUE
.NOMOVE  SLR   1,1                   .PARM REG 1 MUST BE ZERO
         ESAR  2
         ST    2,16(,13)             .SAVE SASID IN REG 15 SLOT
         L     2,16                  .GET ADDR OF CVT
         L     2,772(,2)             .GET ADDR OF SYSTEM FUNCTION TABLE
         L     2,36(,2)              .GET LX/EX FOR AXSET
         PC    0(2)
         L     2,16(,13)             .EXTRACT SAVED SASID
         SSAR  2
         LM    2,12,28(13)           .RESTORE REGS 2-14
         L     14,12(,13)            .RESTORE REG 14
         MEND
