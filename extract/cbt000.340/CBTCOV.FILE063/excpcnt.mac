         MACRO
&LABEL   EXCPCNT &TCT=,&SAVE=
*********************************************************************
*
*   THIS MACRO COUNTS THE TOTAL EXCPS ACCUMULATED FOR THE CURRENT
*   TASK. THE TOTAL EXCP COUNT IS RETURNED IN REGISTER 0.
*   WHEN CALLED, TWO PARAMETERS ARE EXPECTED :
*              &TCT  - THE NUMBER OF A REGISTER POINTING TO THE
*                         CURRENT TASK'S TIMING CONTROL TABLE
*              &SAVE - THE NUMBER OF A REGISTER POINTING TO A
*                         15 WORD REGISTER SAVE AREA
*   REGISTERS 0,1,2,14, AND 15 MAY NOT BE USED FOR &TCT OR &SAVE.
*   A LABEL OF NOT MORE THAN SEVEN CHARACTERS MUST BE USED.
*
*********************************************************************
         STM   R1,R15,0(&SAVE.)    SAVE REGISTERS
         L     R14,12(,&TCT.)      POINT TO TCT I/O TABLE
         XR    R2,R2               CLEAR WORK REGISTER
         XR    R0,R0               CLEAR WORK REGISTER
         LA    R1,10(,R14)         POINT TO FIRST OFFSET VALUE
&LABEL.1 DS    0H
         LH    R2,0(,R1)           TCT I/O TABLE OFFSET TO DD ENTRY
         LTR   R2,R2               IF OFFSET VALUE IS ZERO
         BZ    &LABEL.2               THEN EXIT
         L     R2,4(R2,R14)        LOAD EXCP COUNT OF THE DD ENTRY
         AR    R0,R2               ADD EXCP COUNT TO TOTAL
         LA    R1,4(,R1)           POINT TO NEXT OFFSET VALUE
         B     &LABEL.1            GO LOOK FOR ANOTHER DD ENTRY
&LABEL.2 DS    0H
         LM    R1,R15,0(&SAVE.)    RESTORE REGISTERS
         MEND
