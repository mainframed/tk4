         MACRO ,                                                  40000
&NAME    PUTTMC &IOAREA,&ERRTN
.*
         AIF   ('&IOAREA' EQ '' OR '&ERRTN' EQ '').MNOTE1
.*
.PUT     ANOP
&NAME    DS    0H -               PUTTMC MACRO
         XC    TMSPARM,TMSPARM -  CLEAR PARAMETER LIST
         AIF   ('&IOAREA'(1,1) EQ '(').PUT01
         LA    R1,&IOAREA -       POINT TO IOAREA
         AGO   .PUT02
.PUT01   ANOP
         LR    R1,&IOAREA(1) -    POINT TO IOAREA
.PUT02   ANOP
         ST    R1,TMIOADDR-1 -    POST IOAREA ADDRESS
         LA    R1,71(,R1) -       POINT TO VOLUME SERIAL NUMBER
         L     R15,TMSHASH -      GET ADDRESS OF TMSHASH
         BALR  R14,R15 -          VERIFY VOLUME SERIAL NUMBER
         LTR   R1,R1 -            Q. VALID VOLUME SERIAL NUMBER
         BZ    &ERRTN -           A. ZERO - NO
         ST    R1,TMRELBLK -      POST RELATIVE BLOCK ADDRESS
         MVI   TMOPCODE,28 -      POST OP-CODE
         L     R15,TMSVCRTN -     GET ADDRESS OF SVC INTERFACE MODULE
         LA    R1,TMSPARM -       POINT TO PARAMETER LIST
         BALR  R14,R15 -          PUT TMC RECORD
         LA    R1,4 -             SET RETURN CODE FOR AUDIT FULL
         CLI   TMOPBITS,X'FF' -   Q. AUDIT DATA SET FULL
         BE    &ERRTN -           A. EQUAL - YES
.*
         MEXIT
.*
.MNOTE1  MNOTE 8,'PUTTMC MACRO MISSING PARAMETER'
         MEXIT
.*
         MEND
