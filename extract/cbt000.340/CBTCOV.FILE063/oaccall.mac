         MACRO
&NAME    OACCALL &RTN,&OP,&MF=,&ERROR=
         GBLC  &OACNAME
.*  OAC CALL MACRO (INTERAL OR EXTERNAL) - REENTERENT
.*   &RTN - ROUTINE TO CALL
.*   &OP  - LIST OF OPERANDS (IN PARENTHESIS) OR KEYWORD OF EXT
.*   &EXT - EXT KEYWORD OR VL KEYWORD (OR NULL)
.*          (EXT WILL FORCE VCON INSTEAD OF ACON)
.*   &VL  - VL KEYWORD (OR NULL)
.*          (VL WILL CAUSE AN OI TO BE GENERATED TO SET
.*           HIGH ORDER BIT OF LAST PARM TO 1)
.*   &MF= - E OR B, PARMLIST ADDRESS
.*          (IF OMITTED THEN R1 WILL BE LEFT UNCHANGED
.*           WHICH IS SAME AS MF=(E,(R1)) HOWEVER
.*           ONLY 'EXT' AND ERROR= MAY BE SPECIFIED)
.*
.*  NAME OACCALL FURD,(PARM1,PARM2),MF=(E,LIST)
.*       OACCALL ,(,,,PARM4),MF=(B,LIST)
.*       OACCALL WYLSEND,(PARM1,,PARM2),EXT,MF=(E,LIST)
.*       OACCALL RTN3,(PARM1,,PARM3),EXT,VL,MF=(E,(R5))
.*
         LCLB  &MFB,&VLB
         LCLA  &A,&O
         LCLC  &EXT               A IF INTERNAL, V IF EXTERNAL
&EXT     SETC  'A'                ASSUME INTERNAL, NOT EXTERNAL
&OACNAME SETC  '&NAME'
         AIF   ('&MF' EQ '').LIST
         AIF   ('&MF(1)' EQ 'E').LIST
         AIF   ('&MF(1)' EQ 'B').MFB
         MNOTE 8,'INVALID MF=&MF - MF=E ASSUMED'
         AGO   .LIST
.MFB     ANOP
&MFB     SETB  1
.*  SCAN SYSLIST FOR EXT OR VL PSEUDO-KEYWORDS
.LIST    ANOP
&A       SETA  1                  START AFTER ROUTINE NAME
.LIST1   ANOP
&A       SETA  &A+1
         AIF   (N'&SYSLIST LT &A).OP
         AIF   ('&SYSLIST(&A)' EQ 'EXT').LISTEXT
         AIF   ('&SYSLIST(&A)' EQ 'VL').LISTVL
         AIF   ('&SYSLIST(&A)'(1,1) EQ '(' AND &A EQ 2).LIST1
         MNOTE 8,'POSITIONAL OPERAND &A IS INVALID - &SYSLIST(&A)'
         AGO   .LIST1
.LISTEXT ANOP
&EXT     SETC  'V'
         ANOP  .LIST1
.LISTVL  ANOP
&VLB     SETB  1
         AGO   .LIST1
.OP      ANOP
&A       SETA  0
.OPLOOP  ANOP
&A       SETA  &A+1
&O       SETA  (&A-1)*4
         AIF   (&A GT N'&OP).CALL                 06/03/81 - MAS
         AIF   ('&OP(&A)' EQ '').OPLOOP           06/03/81 - MAS
         OACINNRB A,&MF(2),&O,4,&OP(&A)
         AGO   .OPLOOP
.CALL    AIF   (NOT &VLB).CALL2
&A       SETA  (N'&OP-1)*4        OFFSET OF LAST ADDRESS IN PARMLIST
         OACINNRB OI,&MF(2),&A,1,X'80' SET END OF PARMLIST BIT
.CALL2   AIF   (&MFB).MEND
         OACINNRB A,,(R1),4,&MF(2)
         AIF   ('&RTN'(1,1) EQ '(').CALLR
&OACNAME L     R15,=&EXT.(&RTN)
&OACNAME SETC  ''
.CALL3   ANOP
&OACNAME BALR  R14,R15
         OACINERR &ERROR
         MEXIT
.CALLR   OACINNRB A,,(R15),4,&RTN     ENTRY POINT TO R15
         AGO   .CALL3
.MEND    MEND
