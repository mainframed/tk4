         MACRO
&NFS     GETHJB  &REG,&NONE=
.*  THIS MACRO WILL LOCATE THE HJB (SEE HACHJB MACRO/DSECT)
.*  UPDATES:
.*    04/16/84 LDW  ADD SUPPORT FOR 'NONE=' OPERAND
         LCLC  &R
&R       SETC  '&REG'                   GET CALLER'S OPERAND, IF ANY
         AIF   ('&REG' NE '').OK        CONTINUE IF SUPPLIED
&R       SETC  'R1'                     DEFAULT TO R1 FOR COMPATABILITY
.OK      ANOP
&NFS     L     &R,540                   -> TCB
         L     &R,164(,&R)              -> TCT
         AIF   ('&NONE' EQ '').N1
         N     &R,=X'00FFFFFF'          CLEAR FLAG BYTE
         BZ    &NONE                    ERROR EXIT IF NO TCT
.N1      L     &R,28(,&R)               -> JMR
         AIF   ('&NONE' EQ '').N2
         CLC   =CL4' ',20(&R)           BLANKS ==> NO HJB
         BE    &NONE                    ERROR EXIT IF NO JMR
.N2      L     &R,20(,&R)               -> HJB
         SPACE 1
         MEND
