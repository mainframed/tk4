         MACRO
&LBL     OACBLDPL &DUMMY,&CPPL=,&ECB=,&RS=R0,&RT=R15,&IOPL=,&CSPL=,    X
               &PPL=,&DFPL=,&DRFAIL=,&GNRFAIL=
         LCLC  &B,&C,&LABEL
&LABEL   SETC  '&LBL'
         AIF   ('&CPPL' NE '').L1
         MNOTE 12,'CPPL IS REQUIRED'
.L1      AIF   ('&ECB' NE '').L2
         MNOTE 12,'TSO SERVICE ECB IS REQUIRED'
.L2      AIF   ('&GNRFAIL' EQ '').IOPL
&LABEL   OACINNER T=&GNRFAIL,TO=+16,F=&CPPL,OP=MA,L=4,RS=&RS,RT=&RT,   X
               COM='MOVE IN CPPL ADDR'
&LABEL   SETC  ''
         OACINNER T=&GNRFAIL,TO=+20,F=&ECB,OP=MA,L=4,RS=&RS,RT=&RT,    X
               COM='MOVE IN ECB ADDR'
.IOPL    AIF   ('&IOPL(1)' EQ '').CSPL
&C       SETC  '&IOPL'
         AIF   ('&IOPL(2)' EQ '').IOPL1
&C       SETC  '&IOPL(1)'
.IOPL1   ANOP
&LABEL   OACINNER T=&C,F=&CPPL,FO=+4,OP=M,L=4,RS=&RS,RT=&RT,           X
               COM='MOVE IN UPT ADDR'
         OACINNER T=&C,TO=+4,F=&CPPL,FO=+12,OP=M,L=4,RS=&RS,RT=&RT,    X
               COM='MOVE IN ECT ADDR'
         OACINNER F=&ECB,T=&C,TO=+8,RS=&RS,RT=&RT,OP=MA,               X
               COM='MOVE IN ECB ADDRESS'
&B       SETC  '&C'
         AIF   ('&IOPL(2)' EQ '').CSPL
         OACINNER F=&IOPL(2),T=&C,TO=+12,RS=&RS,RT=&RT,OP=MA,          X
               COM='MOVE IN IOPB ADDR'
.CSPL    AIF   ('&CSPL(1)' EQ '').PPL
         AIF   (1 EQ N'&CSPL).CSPL1
&C       SETC  '&CSPL(1)'
         AGO   .CSPL2
.CSPL1   ANOP
&C       SETC  '&CSPL'
.CSPL2   AIF   ('&B' EQ '').CSPL3
         OACINNER F=&B,T=&C,L=12,RS=&RS,RT=&RT,OP=M,                   X
               COM='MOVE UPD,ECT,AND ECB ADDS'
         AGO   .CSPL4
.CSPL3   ANOP
&LABEL   OACINNER T=&C,F=&CPPL,FO=+4,OP=M,L=4,RS=&RS,RT=&RT,           X
               COM='MOVE IN UPT ADDR'
         OACINNER T=&C,TO=+4,F=&CPPL,FO=+12,OP=M,L=4,RS=&RS,RT=&RT,    X
               COM='MOVE IN ECT ADDR'
         OACINNER F=&ECB,T=&C,TO=+8,RS=&RS,RT=&RT,OP=MA,               X
               COM='MOVE IN ECB ADDRESS'
&B       SETC  '&C'
.CSPL4   AIF   ('&CSPL(2)' EQ '').CSPL5
         OACINNER F=&CSPL(2),T=&C,TO=+12,OP=MA,RS=&RS,RT=&RT,          X
               COM='MOVE IN FLAG ADDRESS'
.CSPL5   AIF   ('&CSPL(3)' EQ '').CSPL6
         OACINNER F=&CSPL(3),T=&C,TO=+16,OP=MA,RS=&RS,RT=&RT,          X
               COM='MOVE IN CSOA ADDRESS'
.CSPL6   AIF   ('&CSPL(4)' EQ '').PPL
         OACINNER F=&CSPL(4),T=&C,TO=+20,OP=MA,RS=&RS,RT=&RT,          X
               COM='MOVE IN CMD BUF ADDRESS'
.PPL     AIF   ('&PPL(1)' EQ '').DFPL
         AIF   (1 EQ N'&PPL).PPL1
&C       SETC  '&PPL(1)'
         AGO   .PPL2
.PPL1    ANOP
&C       SETC  '&PPL'
.PPL2    AIF   ('&B' EQ '').PPL3
         OACINNER F=&B,T=&C,L=12,RS=&RS,RT=&RT,OP=M,                   X
               COM='MOVE UPD,ECT,AND ECB ADDS'
         AGO   .PPL4
.PPL3    ANOP
&LABEL   OACINNER T=&C,F=&CPPL,FO=+4,OP=M,L=4,RS=&RS,RT=&RT,           X
               COM='MOVE IN UPT ADDR'
         OACINNER T=&C,TO=+4,F=&CPPL,FO=+12,OP=M,L=4,RS=&RS,RT=&RT,    X
               COM='MOVE IN ECT ADDR'
         OACINNER F=&ECB,T=&C,TO=+8,RS=&RS,RT=&RT,OP=MA,               X
               COM='MOVE IN ECB ADDRESS'
&B       SETC  '&C'
.PPL4    AIF   ('&PPL(2)' EQ '').PPL5
         OACINNER F=&PPL(2),T=&C,TO=+12,OP=MA,RS=&RS,RT=&RT,           X
               COM='MOVE IN PCL ADDRESS'
.PPL5    AIF   ('&PPL(3)' EQ '').PPL6
         OACINNER F=&PPL(3),T=&C,TO=+16,OP=MA,RS=&RS,RT=&RT,           X
               COM='MOVE IN ANSWER PLACE ADD'
.PPL6    AIF   ('&PPL(4)' EQ '').PPL7
         OACINNER F=&PPL(4),T=&C,TO=+20,OP=MA,RS=&RS,RT=&RT,           X
               COM='MOVE IN PTR TO CMD BUF'
.PPL7    AIF   ('&PPL(5)' EQ '').DFPL
         OACINNER F=&PPL(5),T=&C,TO=+24,OP=MA,RS=&RS,RT=&RT,           X
               COM='MOVE IN PTR TO USER WA'
.DFPL    AIF   ('&DFPL(1)' EQ '').DRFAIL
         AIF   (1 EQ N'&DFPL).DFPL1
&C       SETC  '&DFPL(1)'
         AGO   .DFPL2
.DFPL1   ANOP
&C       SETC  '&DFPL'
.DFPL2   AIF   ('&B' EQ '').DFPL3
         OACINNER F=&B,T=&C,L=12,RS=&RS,RT=&RT,OP=M,                   X
               COM='MOVE UPD,ECT,AND ECB ADDS'
         AGO   .DFPL4
.DFPL3   ANOP
&LABEL   OACINNER T=&C,F=&CPPL,FO=+4,OP=M,L=4,RS=&RS,RT=&RT,           X
               COM='MOVE IN UPT ADDR'
         OACINNER T=&C,TO=+4,F=&CPPL,FO=+12,OP=M,L=4,RS=&RS,RT=&RT,    X
               COM='MOVE IN ECT ADDR'
         OACINNER F=&ECB,T=&C,TO=+8,RS=&RS,RT=&RT,OP=MA,               X
               COM='MOVE IN ECB ADDRESS'
&B       SETC  '&C'
.DFPL4   AIF   (1 EQ N'&DFPL).DRFAIL
         OACINNER F=&DFPL(2),T=&C,TO=+12,OP=MA,RS=&RS,RT=&RT,          X
               COM='MOVE IN DFPB ADDRESS'
.DRFAIL  AIF   ('&DRFAIL(1)' EQ '').END
         AIF   (1 EQ N'&DRFAIL).DRFAIL1
&C       SETC  '&DRFAIL(1)'
         AGO   .DRFAIL2
.DRFAIL1 ANOP
&C       SETC  '&DRFAIL'
.DRFAIL2 ANOP
         OACINNER F=&CPPL,T=&C,TO=+16,OP=MA,RS=&RS,RT=&RT,             X
               COM='MOVE IN CPPL ADDRESS'
         AIF   ('&DRFAIL(2)' EQ '').DRFAIL3
         OACINNER F=&DRFAIL(2),T=&C,OP=MA,RS=&RS,RT=&RT,               X
               COM='MOVE IN PTR TO RB'
.DRFAIL3 AIF   ('&DRFAIL(3)' EQ '').DRFAIL4
         OACINNER F=&DRFAIL(3),T=&C,TO=+4,OP=MA,RS=&RS,RT=&RT,         X
               COM='MOVE IN PTR TO RC'
.DRFAIL4 AIF   ('&DRFAIL(4)' EQ '').DRFAIL5
         OACINNER F=&DRFAIL(4),T=&C,TO=+8,OP=MA,RS=&RS,RT=&RT,         X
               COM='PTR TO ADDR OF IKJEFF02'
.DRFAIL5 AIF   ('&DRFAIL(5)' EQ '').DRFAIL6
         OACINNER F=&DRFAIL(5),T=&C,TO=+12,OP=MA,RS=&RS,RT=&RT,        X
               COM='PTR TO FLAGS AND ID'
.DRFAIL6 AIF   ('&DRFAIL(6)' EQ '').END
         OACSTAD FROM=&DRFAIL(6),TO=(&C,+20),RS=&RS
         OACINNER F=&DRFAIL(6),T=&C,TO=+20,OP=MA,RS=&RS,RT=&RT,        X
               COM='PTR TO MSG BUFFERS'
.END     MEND
