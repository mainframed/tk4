         MACRO
&LABEL   $SF   &OPT
.**********************************************************************
.*
.*  THIS MACRO IS USED IN MAPPING TPUT FULL SCREEN OUTPUT.  IT
.*  DESIGNATES A FIELD START AND THE ATTRIBUTES OF THAT FIELD.
.*  FORMAT OF THE MACRO IS AS FOLLOWS:
.*
.*  LABEL  $SF (PROT,NUM,SKIP,HI,NONDISP,MDT)
.*
.*  NOTE: HI AND NONDISP ARE EXCLUSIVE, THE LAST ENTERED WILL WORK
.**********************************************************************
         LCLA  &CNTR,&MAX
         LCLC  &PROT,&NUM,&SKIP,&HIDISP,&MDT
&MAX     SETA  N'&OPT
         AIF   (&MAX GT 6).ERR1
&CNTR    SETA  1
&PROT    SETC  '0'
&NUM     SETC  '0'
&SKIP    SETC  '0'
&HIDISP  SETC  '00'
&MDT     SETC  '1'
.*
.LOOP    AIF   (&CNTR GT &MAX).CONT
         AIF   ('&OPT(&CNTR)' NE 'PROT').NPROT
&PROT    SETC  '1'
         AGO   .LOOPEND
.NPROT   AIF   ('&OPT(&CNTR)' NE 'NUM').NNUM
&NUM     SETC  '1'
         AGO   .LOOPEND
.NNUM    AIF   ('&OPT(&CNTR)' NE 'SKIP').NSKIP
&PROT    SETC  '1'
&NUM     SETC  '1'
         AGO   .LOOPEND
.NSKIP   AIF   ('&OPT(&CNTR)' NE 'HI').NHI
&HIDISP  SETC  '10'
         AGO   .LOOPEND
.NHI     AIF   ('&OPT(&CNTR)' NE 'NONDISP').NDISP
&HIDISP  SETC  '11'
         AGO   .LOOPEND
.NDISP   AIF   ('&OPT(&CNTR)' NE 'MDT').ERR2
&MDT     SETC  '0'
         AGO   .LOOPEND
.LOOPEND ANOP
&CNTR    SETA  &CNTR+1
         AGO   .LOOP
.CONT    ANOP
&LABEL   DC    X'1D'
         DC    BL1'00&PROT.&NUM.&HIDISP.0&MDT.'
         AGO   .MACEND
.ERR1    MNOTE 8,'$SF MACRO USED WITH TOO MANY OPERANDS'
         AGO   .MACEND
.ERR2    MNOTE 8,'INVALID MACRO OPERAND - &OPT(&CNTR)'
.MACEND  ANOP
         MEND
