         MACRO
&LABEL   SF    &OPT
.**********************************************************************
.*
.*  THIS MACRO IS USED IN MAPPING TPUT FULL SCREEN OUTPUT.  IT
.*  DESIGNATES A FIELD START AND THE ATTRIBUTES OF THAT FIELD.
.*  FORMAT OF THE MACRO IS AS FOLLOWS:
.*
.*  LABEL  SF  (OPERAND1,OPERAND2...,OPERANDN)
.*
.**********************************************************************
         LCLA  &CNTR,&MAX
         LCLC  &PROT,&DISP,&COLOR
&MAX     SETA  N'&OPT
&CNTR    SETA  1
&PROT    SETC  '1'                DEFAULT IS PROTECTED
&DISP    SETC  '00'               DEFAULT IS NORMAL
&COLOR   SETC  '100'              DEFAULT IS GREEN
.*
.LOOP    AIF   (&CNTR GT &MAX).CONT
.*
.*---------------------------------------------------------------------
.PROT    AIF   ('&OPT(&CNTR)' NE 'PROT').NPROT
&PROT    SETC  '1'
         AGO   .LOOPEND
.NPROT   ANOP
.*---------------------------------------------------------------------
.UPRT    AIF   ('&OPT(&CNTR)' NE 'UNPROT').NUPRT
&PROT    SETC  '0'
         AGO   .LOOPEND
.NUPRT   ANOP
.*---------------------------------------------------------------------
.NORM    AIF   ('&OPT(&CNTR)' NE 'NORMAL').NNORM
&DISP    SETC  '00'
         AGO   .LOOPEND
.NNORM   ANOP
.*---------------------------------------------------------------------
.BLNK    AIF   ('&OPT(&CNTR)' NE 'BLINK').NBLNK
&DISP    SETC  '01'
         AGO   .LOOPEND
.NBLNK   ANOP
.*---------------------------------------------------------------------
.RVRS    AIF   ('&OPT(&CNTR)' NE 'REVERSE').NRVRS
&DISP    SETC  '10'
         AGO   .LOOPEND
.NRVRS   ANOP
.*---------------------------------------------------------------------
.UNDR    AIF   ('&OPT(&CNTR)' NE 'UNDERSCORE').NUNDR
&DISP    SETC  '11'
         AGO   .LOOPEND
.NUNDR   ANOP
.*---------------------------------------------------------------------
.NDSP    AIF   ('&OPT(&CNTR)' NE 'NONDISP').NNDSP
&COLOR   SETC  '000'
         AGO   .LOOPEND
.NNDSP   ANOP
.*---------------------------------------------------------------------
.BLUE    AIF   ('&OPT(&CNTR)' NE 'BLUE').NBLUE
&COLOR   SETC  '001'
         AGO   .LOOPEND
.NBLUE   ANOP
.*---------------------------------------------------------------------
.RED     AIF   ('&OPT(&CNTR)' NE 'RED').NRED
&COLOR   SETC  '010'
         AGO   .LOOPEND
.NRED    ANOP
.*---------------------------------------------------------------------
.PINK    AIF   ('&OPT(&CNTR)' NE 'PINK').NPINK
&COLOR   SETC  '011'
         AGO   .LOOPEND
.NPINK   ANOP
.*---------------------------------------------------------------------
.GREN    AIF   ('&OPT(&CNTR)' NE 'GREEN').NGREN
&COLOR   SETC  '100'
         AGO   .LOOPEND
.NGREN   ANOP
.*---------------------------------------------------------------------
.TURQ    AIF   ('&OPT(&CNTR)' NE 'TURQUOISE').NTURQ
&COLOR   SETC  '101'
         AGO   .LOOPEND
.NTURQ   ANOP
.*---------------------------------------------------------------------
.YELO    AIF   ('&OPT(&CNTR)' NE 'YELLOW').NYELO
&COLOR   SETC  '110'
         AGO   .LOOPEND
.NYELO   ANOP
.*---------------------------------------------------------------------
.WHIT    AIF   ('&OPT(&CNTR)' NE 'WHITE').NWHIT
&COLOR   SETC  '111'
         AGO   .LOOPEND
.NWHIT   ANOP
.*---------------------------------------------------------------------
         AGO   .ERR1
.LOOPEND ANOP
&CNTR    SETA  &CNTR+1
         AGO   .LOOP
.*
.CONT    ANOP
&LABEL   DC    BL1'00&PROT.&DISP.&COLOR.'
         AGO   .MACEND
.ERR1    MNOTE 8,'INVALID MACRO OPERAND - &OPT(&CNTR)'
.MACEND  ANOP
         MEND
