         MACRO
&N       ISPEXEC &SERVICE,&PARM1,&PARM2,&PARM3,&PARM4,&PARM5,&PARM6,   X
               &PARM7
         GBLA   &X
         LCLA   &I,&O1,&O2,&O
         LCLC   &P1,&P2,&P3,&P4,&C1,&C2,&C3,&C4
         LCLC   &CHAR1(8),&CHAR2,&CHAR3(8)
         GBLB   &ISPLOAD,&SHARED,&PROFILE,&VDEFINE,&CHAR,&FIXED
         GBLB   &VCOPY,&VGET,&VPUT,&DISPLAY,&ISPMOVE,&LOCATE,&BIT
         GBLB   &HEX,&VREPLAC,&FTOPEN,&FTINCL,&TEMP,&NOFT,&FTCLS,&EDIT
         GBLB   &SELECT,&CONTROL,&VDELETE
&X       SETA   &X+1
&O1      SETA   36         OFFSET TO FIRST LITERAL
&O2      SETA   0          LENGTH OF CURRENT LITERALS
         AIF   ('&PARM7' NE '').HAVEOFF
&O1      SETA  &O1-4
         AIF   ('&PARM6' NE '').HAVEOFF
&O1      SETA  &O1-4
         AIF   ('&PARM5' NE '').HAVEOFF
&O1      SETA  &O1-4
         AIF   ('&PARM4' NE '').HAVEOFF
&O1      SETA  &O1-4
         AIF   ('&PARM3' NE '').HAVEOFF
&O1      SETA  &O1-4
         AIF   ('&PARM2' NE '').HAVEOFF
&O1      SETA  &O1-4
         AIF   ('&PARM1' NE '').HAVEOFF
&O1      SETA  &O1-4
.HAVEOFF ANOP
         CNOP  0,4
&N       BAL   R1,ISPX&X
&O1      SETA  &O1-4          CLOSER TO LITERAL
         AIF   ('&SERVICE' EQ 'FTOPEN').FTOPEN
         AIF   ('&SERVICE' EQ 'FTINCL').FTINCL
         AIF   ('&SERVICE' EQ 'FTCLOSE').FTCLOSE
         AIF   ('&SERVICE'(1,2) EQ 'TB').TBLSRV
         DC    A(&SERVICE)         TYPE OF SERVICE
&O1      SETA  &O1-4          CLOSER TO LITERAL
         AIF   ('&SERVICE' EQ 'VCOPY').VCOPY
         AIF   ('&SERVICE' EQ 'VREPLACE').VREPLAC
         AIF   ('&SERVICE' EQ 'SELECT').SELECT
         AIF   ('&SERVICE' EQ 'VGET').VGET
         AIF   ('&SERVICE' EQ 'VPUT').VPUT
         AIF   ('&SERVICE' EQ 'EDIT').EDIT
         AIF   ('&SERVICE' EQ 'DISPLAY').DISPLAY
         AIF   ('&SERVICE' EQ 'CONTROL').CONTROL
         AIF   ('&SERVICE' EQ 'VDEFINE').VDEFINE
         AIF   ('&SERVICE' EQ 'VDELETE').VDELETE
         AGO   .ANY
.SELECT  ANOP
         DC    A(*+&O1+&O2)        LENGTH
&O1      SETA  &O1-4
&O2      SETA  &O2+4               ADD LENGTH OF FULLWORD
&O1      SETA  &O1-1
         DC    X'80',AL3(&PARM2)   BUFFER LOCATION
         DC    A(&PARM1)           LENGTH
         AIF   ('&SELECT' EQ '1').NOSEL
SELECT   DC    CL8'SELECT'
&SELECT  SETB  1
.NOSEL   AGO   .ENDPARM
.EDIT    AIF   ('&PARM1'(1,1) EQ '''').EQDSN
         AIF   ('&PARM2' EQ '' AND '&PARM3' EQ '').JSTDSN
         DC    A(&PARM1)           DSNAME
&O1      SETA  &O1-4
         AGO   .EP2
.EQDSN   ANOP
         AIF   ('&PARM2' EQ '' AND '&PARM3' EQ '').EQDSNL
         DC    A(*+&O1+&O2)
&O1      SETA  &O1-4
         AGO   .ENDEQDS
.EQDSNL  ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2)
&O1      SETA  &O1-3
&O2      SETA  &O2+56
         AGO    .ENDEDIT
.ENDEQDS ANOP
&O2      SETA  &O2+56
.EP2     ANOP
         AIF   ('&PARM3' EQ '').JSTVSN
         AIF   ('&PARM2' EQ '').JSTPW
         AIF   ('&PARM2'(1,1) EQ '''').EQVSN
         DC    A(&PARM2)           VOLSER
&O1      SETA  &O1-4
         AGO   .JSTPW2
.EQVSN   ANOP
         DC    A(*+O1+O2)
&O1      SETA  &O1-4
&O2      SETA  &O2+8
         AGO   .JSTPW2
.JSTPW   ANOP
         DC    A(0)                NO VOLSER SPECIFIED
&O1      SETA  &O1-4
.JSTPW2  ANOP
         AIF   ('&PARM3'(1,1) EQ '''').EDEQP3
         DC    X'80',AL3(&PARM3)   PASSWORD
         AGO   .ENDEDIT
.EDEQP3  ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2)
         AGO   .ENDEDIT
.JSTDSN  DC    X'80',AL3(&PARM1)   DSNAME
         AGO   .ENDEDIT
.JSTVSN  ANOP
         AIF   ('&PARM2'(1,1) EQ '''').EDEQP2
         DC    X'80',AL3(&PARM2)   VOLSER
         AGO   .ENDEDIT
.EDEQP2  ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2) VOLSER
.ENDEDIT ANOP
         AIF   ('&PARM1'(1,1) NE '''').ENDED2
         DC    CL56&PARM1
.ENDED2  ANOP
         AIF   ('&PARM2' EQ '').ENDED3
         AIF   ('&PARM2'(1,1) NE '''').ENDED3
         DC    CL8&PARM2           VOLSER
.ENDED3  ANOP
         AIF   ('&PARM3' EQ '').ENDED4
         AIF   ('&PARM3'(1,1) NE '''').ENDED4
         DC    CL8&PARM3           PASSWORD
.ENDED4  ANOP
         AIF   ('&EDIT' EQ '1').ENDPARM
EDIT     DC    CL8'EDIT'
&EDIT    SETB  1
         AGO   .ENDPARM
.FTOPEN  ANOP
         AIF   ('&PARM1' EQ '').JSTOPEN
         DC    A(FTOPEN)           OPEN WORK FILE
         AIF   ('&PARM1' NE 'TEMP').ERROPEN
         DC    X'80',AL3(TEMP)     TEMPORARY WORK FILE
         AIF   ('&TEMP' EQ '1').NOT1
TEMP     DC    CL8'TEMP'
&TEMP    SETB  1
.NOT1    AIF   ('&FTOPEN' EQ '1').ENDPARM
FTOPEN   DC    CL8'FTOPEN'
&FTOPEN  SETB  1
         AGO   .ENDPARM
.JSTOPEN ANOP
         DC    X'80',AL3(FTOPEN)   OPEN WORK FILE
         AGO   .NOT1
.ERROPEN ANOP
         MNOTE 8,'ONLY ACCEPTABLE PARM IS TEMP'
         AGO   .ENDPARM
.FTCLOSE ANOP
         AIF   ('&PARM1' EQ '').JSTCLS
         DC    A(FTCLOSE)          CLOSE WORK FILE
         AIF   ('&PARM1'(1,1) EQ '''').CLEQMEM
         DC    X'80',AL3(&PARM1)   MEMBER NAME
&O1      SETA  &O1-4
         AGO   .ENDCLS
.CLEQMEM ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2)
&O1      SETA  &O1-3
&O2      SETA  &O2+8
         AGO   .ENDCLS
.JSTCLS  ANOP
         DC    X'80',AL3(FTCLOSE)  CLOSE WORK FILE
         AGO   .ENDCLS2
.ENDCLS  ANOP
         AIF   ('&PARM1'(1,1) NE '''').ENDCLS2
         DC    CL8&PARM1
.ENDCLS2 ANOP
         AIF   ('&FTCLS' EQ '1').ENDPARM
FTCLOSE  DC    CL8'FTCLOSE'
&FTCLS   SETB  1
         AGO   .ENDPARM
.FTINCL  ANOP
         DC    A(FTINCL)           INCL SKELETON
         AIF   ('&PARM2' EQ '').LSTINCL
         AIF   ('&PARM1'(1,1) EQ '''').INEQSK
         DC    A(&PARM1)           SKELETON FILE
&O1      SETA  &O1-4
         AGO   .CKNOFT
.INEQSK  ANOP
         DC    A(*+&O1+&O2)
&O1      SETA  &O1-4
&O2      SETA  &O2+8
.CKNOFT  ANOP
         AIF   ('&PARM2' NE 'NOFT').ERRINCL
         DC    X'80',AL3(NOFT)     NO FILE TAILORING
         AIF   ('&NOFT' EQ '1').NOT2
NOFT     DC    CL8'NOFT'
&NOFT    SETB  1
         AGO   .NOT2
.LSTINCL ANOP
         AIF   ('&PARM1'(1,1) NE '''').JSTSKEL
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2)
         AGO   .NOT2
.JSTSKEL ANOP
         DC    X'80',AL3(&PARM1)   SKELETON FILE
.NOT2    ANOP
         AIF   ('&PARM1'(1,1) NE '''').NOT22
         DC    CL8&PARM1           MEMBER NAME
.NOT22   AIF   ('&FTINCL' EQ '1').ENDPARM
FTINCL   DC    CL8'FTINCL'
&FTINCL  SETB  1
         AGO   .ENDPARM
.ERRINCL ANOP
         MNOTE 8,'ONLY ACCEPTABLE PARM IS NOFT'
         AGO   .ENDPARM
.VDEFINE ANOP
         DC    A(&PARM1)           LIST OF VARIABLES
&O1      SETA  &O1-4
         DC    A(&PARM2)           VARIABLE ARRAY
&O1      SETA  &O1-4
         DC    A(&PARM3)           FORMAT
&O1      SETA  &O1-5
         DC    X'80',AL3(*+&O1+&O2) LENGTH OF VARIABLE
         DC    A(&PARM4)           LENGTH OF VARIABLE
         AIF   ('&PARM3' EQ 'CHAR').DEFCHAR
         AIF   ('&PARM3' EQ 'FIXED').DEFFIX
         AIF   ('&PARM3' EQ 'BIT').DEFBIT
         AIF   ('&PARM3' EQ 'HEX').DEFHEX
.DEFCHAR ANOP
         AIF   ('&CHAR' EQ '1').ENDFORM
CHAR     DC    CL8'CHAR'
&CHAR    SETB  1
         AGO   .ENDFORM
.DEFFIX  ANOP
         AIF   ('&FIXED' EQ '1').ENDFORM
FIXED    DC    CL8'FIXED'
&FIXED   SETB  1
         AGO   .ENDFORM
.DEFBIT  ANOP
         AIF   ('&BIT' EQ '1').ENDFORM
BIT      DC    CL8'BIT'
&BIT     SETB  1
         AGO   .ENDFORM
.DEFHEX  ANOP
         AIF   ('&HEX' EQ '1').ENDFORM
HEX      DC    CL8'HEX'
&HEX     SETB  1
         AGO   .ENDFORM
.ENDFORM ANOP
         AIF   ('&VDEFINE' EQ '1').ENDPARM
VDEFINE  DC    CL8'VDEFINE'
&VDEFINE SETB  1
         AGO   .ENDPARM
.VDELETE ANOP
         DC    X'80',AL3(&PARM1)    LIST OF VARIABLES
         AIF   ('&VDELETE' EQ '1').ENDPARM
VDELETE  DC    CL8'VDELETE'
&VDELETE SETB  1
         AGO   .ENDPARM
.VGET    ANOP
         DC    A(&PARM1)            LIST OF VARIABLES
         AIF   ('&PARM2' EQ 'PROFILE').PROF1
         DC    X'80',AL3(SHARED)   SOURCE POOL
         AIF   ('&SHARED' EQ '1').ENDVGET
SHARED   DC    CL8'SHARED'
&SHARED  SETB  1
         AGO   .ENDVGET
.PROF1   ANOP
         DC    X'80',AL3(PROFILE)  SOURCE POOL
         AIF   ('&PROFILE' EQ '1').ENDVGET
PROFILE  DC    CL8'PROFILE'
&PROFILE SETB  1
.ENDVGET AIF   ('&VGET' EQ '1').ENDPARM
VGET     DC    CL8'VGET'
&VGET    SETB  1
         AGO   .ENDPARM
.VPUT    ANOP
         DC    A(&PARM1)            LIST OF VARIABLES
         AIF   ('&PARM2' EQ 'PROFILE').PROF2
         DC    X'80',AL3(SHARED)   DESTINATION POOL
         AIF   ('&SHARED' EQ '1').ENDVPUT
SHARED   DC    CL8'SHARED'
&SHARED  SETB  1
         AGO   .ENDVPUT
.PROF2   ANOP
         DC    X'80',AL3(PROFILE)  DESTINATION POOL
         AIF   ('&PROFILE' EQ '1').ENDVPUT
PROFILE  DC    CL8'PROFILE'
&PROFILE SETB  1
.ENDVPUT AIF   ('&VPUT' EQ '1').ENDPARM
VPUT     DC    CL8'VPUT'
&VPUT    SETB  1
         AGO   .ENDPARM
.DISPLAY ANOP
&I       SETA  0
         AIF   ('&PARM1'(1,1) EQ '''').EQUATE
         AIF   ('&PARM2' EQ '' AND '&PARM3' EQ '').LSTEQU
         DC    A(&PARM1)            SCREEN NAME
&O1      SETA  &O1-4
         AGO   .MODIS
.LSTEQU  ANOP
         DC    X'80',AL3(&PARM1)    SCREEN NAME
         AGO   .ENDDIS
.EQUATE  ANOP
         AIF   ('&PARM2' EQ '' AND '&PARM3' EQ '').LSTDIS
         DC    A(*+&O1+&O2)         SCREEN NAME
&O1      SETA  &O1-4
&O2      SETA  &O2+8
         AGO   .MODIS
.LSTDIS  ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2) SCREEN NAME
&O1      SETA  &O1-4
&O2      SETA  &O2+8
         AGO   .ENDDIS
.MODIS   ANOP
         AIF   ('&PARM3' EQ '').ED3
         AIF   ('&PARM2' EQ '').ZP2
         AIF   ('&PARM2'(1,1) NE '''').MSGID
         DC    A(*+&O1+&O2)         MESSAGE ID
&O1      SETA  &O1-4
&O2      SETA  &O2+8
         AGO   .TP3
.MSGID   ANOP
         DC    A(&PARM2)            MESSAGE ID
&O1      SETA  &O1-4
         AGO   .TP3
.ED3     ANOP
         AIF   ('&PARM2'(1,1) EQ '''').ED33
         DC    X'80',AL3(&PARM2)    MESSAGE ID
         AGO   .ENDDIS
.ED33    ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2) MESSAGE ID
         AGO   .ENDDIS
.ZP2     DC    A(0)                 NO MESSAGE SPECIFIED
&O1      SETA  &O1-4
         AIF   ('&PARM3'(1,1) EQ '''').ED43
         DC    X'80',AL3(&PARM3)    CURSOR LOCATION
         AGO   .ENDDIS
.ED43    ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2) CURSOR LOCATION
         AGO   .ENDDIS
.TP3     ANOP
         AIF   ('&PARM3'(1,1) EQ '''').TP33
         DC    X'80',AL3(&PARM3)    CURSOR LOCATION
         AGO   .ENDDIS
.TP33    ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2) CURSOR LOCATION
.ENDDIS  ANOP
         AIF   ('&PARM1'(1,1) NE '''').NOP1
         DC    CL8&PARM1            SCREEN NAME
.NOP1    ANOP
         AIF   ('&PARM2' EQ '').TRY3D
         AIF   ('&PARM2'(1,1) NE '''').TRY3D
         DC    CL8&PARM2            MSG ID
.TRY3D   AIF   ('&PARM3' EQ '').TRYDIS
         AIF   ('&PARM3'(1,1) NE '''').TRYDIS
         DC    CL8&PARM3            CURSOR LOCATION
.TRYDIS  ANOP
         AIF   ('&DISPLAY' EQ '1').ENDPARM
DISPLAY  DC    CL8'DISPLAY'
&DISPLAY SETB  1
         AGO   .ENDPARM
.CONTROL ANOP
         AIF   ('&PARM1' EQ 'DISPLAY' OR '&PARM1' EQ 'NONDISPL').GOODC
         AIF   ('&PARM1' EQ 'ERRORS').GOODC
         MNOTE 8,'&PARM1 NOT SUPPORTED'
         AGO   .ENDPARM
.GOODC   ANOP
         AIF   ('&PARM2' EQ '').JST1
         DC    A(*+&O1+&O2)         TYPE
&O1      SETA  &O1-4
&O2      SETA  &O2+8
         AIF   ('&PARM3' EQ '').JST1
         DC    A(*+&O1+&O2)
&O1      SETA  &O1-4
&O2      SETA  &O2+8
.JST1    ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2)
.ENDCONT ANOP
         DC    CL8'&PARM1'
         AIF   ('&PARM2' EQ '').ENDC2
         DC    CL8'&PARM2'
         AIF   ('&PARM3' EQ '').ENDC2
         DC    A(&PARM3)            LINE NUMBER
.ENDC2   AIF   ('&CONTROL' EQ '1').ENDPARM
CONTROL  DC    CL8'CONTROL'
&CONTROL SETB  1
         AGO   .ENDPARM
.VCOPY   ANOP
         AIF   ('&PARM1'(1,1) EQ '''').VCOP10
         DC    A(&PARM1)            VARIABLE NAME
&O1      SETA  &O1-4
         AGO   .VCOP20
.VCOP10  ANOP
         DC    A(*+&O1+&O2)         VARIABLE NAME
&O1      SETA  &O1-4
&O2      SETA  &O2+8
.VCOP20  ANOP
         AIF   (T'&PARM2 EQ 'N' OR T'&PARM2 EQ 'U').VCOP30 SLF DEF TERM
         DC    A(&PARM2)            MAX LENGTH
         AGO   .VCOP40
.VCOP30  ANOP
         DC    A(*+&O1+&O2)         MAX LENGTH
&O1      SETA  &O1-4
&O2      SETA  &O2+4
.VCOP40  ANOP
         DC    A(&PARM3)            WHERE TO PUT VARIABLE DATA
         AIF   ('&PARM4' EQ 'MOVE').VCOP50
         DC    X'80',AL3(LOCATE)   MODE
         AGO   .VCOP60
.VCOP50  ANOP
         DC    X'80',AL3(MOVE)     MODE
.* GENERATE IN-LINE CONSTANTS AS REQUIRED
.VCOP60  ANOP
         AIF   ('&PARM1'(1,1) NE '''').VCOP70
         DC    CL8&PARM1            VARIABLE NAME
.VCOP70  ANOP
         AIF   (T'&PARM2 NE 'N' AND T'&PARM2 NE 'U').VCOP80
         DC    A(&PARM2)            LENGTH
.VCOP80  ANOP
         AIF   ('&PARM4' EQ 'MOVE').VCOP90
         AIF   (&LOCATE).ENDVCOP
LOCATE   DC    CL8'LOCATE'
&LOCATE  SETB  1
         AGO   .ENDVCOP
.VCOP90  ANOP
         AIF   (&ISPMOVE).ENDVCOP
MOVE     DC    CL8'MOVE'
&ISPMOVE SETB  1
.ENDVCOP AIF   (&VCOPY).ENDVC2
VCOPY    DC    CL8'VCOPY'
&VCOPY   SETB  1
.ENDVC2  AIF   (T'&PARM2 NE 'N' AND T'&PARM2 NE 'U').ENDPARM SLF DEFING
ISPX&X   LA    0,&PARM2.(0)
&X       SETA  &X+1
         L     R14,8(R1)
         ST    R0,0(R14)            RESTORE LENGTH
         B     ISPX&X
         AGO   .ENDPARM
.VREPLAC ANOP
         AIF   ('&PARM1'(1,1) EQ '''').VRPL10
         DC    A(&PARM1)            VARIABLE NAME
&O1      SETA  &O1-4
         AGO   .VRPL20
.VRPL10  ANOP
         DC    A(*+&O1+&O2)         VARIABLE NAME
&O1      SETA  &O1-4
&O2      SETA  &O2+8
.VRPL20  ANOP
         AIF   (T'&PARM2 EQ 'N' OR T'&PARM2 EQ 'U').VRPL30 SLF DEF TERM
         DC    A(&PARM2)            MAX LENGTH
&O1      SETA  &O1-4
         AGO   .VRPL40
.VRPL30  ANOP
         DC    A(*+&O1+&O2)         MAX LENGTH
&O1      SETA  &O1-4
&O2      SETA  &O2+4
.VRPL40  ANOP
         AIF   ('&PARM3'(1,1) EQ '''').VRPL50
         DC    X'80',AL3(&PARM3)    WHERE TO GET VARIABLE DATA
         AGO   .VRPL60
.VRPL50  ANOP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2) WHERE TO GET VARIABLE DATA
&O1      SETA  &O1-4
&O2      SETA  &O2+&PARM2
.* GENERATE IN-LINE CONSTANTS AS REQUIRED
.VRPL60  ANOP
         AIF   ('&PARM1'(1,1) NE '''').VRPL70
         DC    CL8&PARM1            VARIABLE NAME
.VRPL70  ANOP
         AIF   (T'&PARM2 NE 'N' AND T'&PARM2 NE 'U').VRPL80
         DC    A(&PARM2)            LENGTH OF DATA
.VRPL80  ANOP
         AIF   ('&PARM3'(1,1) NE '''').VRPL90
         DC    C&PARM3              VARIABLE DATA
.VRPL90  ANOP
         AIF   (&VREPLAC).ENDPARM
VREPLACE DC    CL8'VREPLACE'
&VREPLAC SETB  1
         AGO   .ENDPARM
.ANY     ANOP
         AIF   ('&PARM2' EQ '').ENDP1
         DC    A(&PARM1)
         AGO   .DO2
.ENDP1   ANOP
         DC    X'80',AL3(&PARM1)
         AGO   .ENDPARM
.DO2     ANOP
         AIF   ('&PARM3' EQ '').ENDP2
         DC    A(&PARM2)
         AGO   .DO3
.ENDP2   ANOP
         DC    X'80',AL3(&PARM2)
         AGO   .ENDPARM
.DO3     ANOP
         DC    X'80',AL3(&PARM3)
         AGO   .ENDPARM
.TBLSRV  ANOP              TABLE SERVICES
         DC    A(*+&O1+&O2)        TYPE OF TABLE SERVICE
&O1      SETA  &O1-4
&O2      SETA  &O2+8
&I       SETA  2
.CYCP    ANOP
&CHAR2   SETC  '&SYSLIST(&I)'
         AIF   ('&SYSLIST(&I)' EQ '').NULLTB
         AIF   (T'&SYSLIST(&I) EQ 'N').SELFD
         AIF   ('&CHAR2'(1,1) NE '''').NOLITT
&CHAR3(&I) SETC 'CL8&CHAR2'
         AIF   (&O1 GT 4).NOLSTP
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2)
         AGO   .ENDTBL
.SELFD   ANOP
&CHAR3(&I) SETC 'A(&CHAR2)'
         AIF   (&O1 GT 4).NOLSTS
&O1      SETA  &O1-1
         DC    X'80',AL3(*+&O1+&O2)
         AGO   .ENDTBL
.NOLSTS  ANOP
         DC    A(*+&O1+&O2)
&O2      SETA  &O2+4
         AGO   .COMINC
.NOLITT  ANOP
         AIF   (&O1 GT 4).NOLSTP2
&O1      SETA  &O1-1
         DC    X'80',AL3(&CHAR2)
         AGO   .ENDTBL
.NOLSTP  ANOP
         DC    A(*+&O1+&O2)
&O2      SETA  &O2+8
         AGO   .COMINC
.NULLTB  ANOP
         DC    A(0)
.COMINC  ANOP
&O1      SETA  &O1-4
&I       SETA  &I+1
         AGO   .CYCP
.NOLSTP2 ANOP
         DC    A(&CHAR2)
         AGO   .COMINC
.ENDTBL  ANOP
         DC    CL8'&SERVICE'
&I       SETA  2
.CYCT2   ANOP
&CHAR2   SETC  '&CHAR1(&I)'
         AIF   ('&CHAR3(&I)' EQ '').TNOP1
         DC    &CHAR3(&I)
         AGO   .TNOP1
.TNOP1   ANOP
&I       SETA  &I+1
         AIF   (&I LT 9).CYCT2
.ENDPARM ANOP
         AIF   ('&ISPLOAD' EQ '1').NOLNK
ISPEXEC  L     R15,ISPLINK
         LTR   R15,R15
         BNZR  R15
         ST    R14,ISPR14
         ST    R1,ISPR1
         LOAD  EP=ISPLINK
         ST    R0,ISPLINK
         L     R14,ISPR14
         L     R1,ISPR1
         B     ISPEXEC
ISPLINK  DC    A(0)
ISPR14   DC    A(0)
ISPR1    DC    A(0)
ISPRC    DC    A(0)               RETURN CODE FROM ISPEXEC
&ISPLOAD SETB  1
.NOLNK   ANOP
ISPX&X   BAL   R14,ISPEXEC
         ST    R15,ISPRC          SAVE RETURN CODE
         MEND
