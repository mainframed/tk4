         MACRO
         QZRCNTRS &PFX=CTR,&ALIGN=YES
         LCLC  &HW,&FW
         AIF   ('&ALIGN' EQ 'NO').PIU
&HW      SETC  'H'
&FW      SETC  'F'
         AGO   .GO
.PIU     ANOP
&HW      SETC  'XL2'
&FW      SETC  'XL4'
.GO      ANOP
         SPACE
***********************************************************************
***                                                                 ***
**                    NPA RESOURCE COUNTERS                          **
***                                                                 ***
***********************************************************************
         SPACE
&PFX.CTRS EQU  *        DATA COUNTERS FOR RESOURCE
*
&PFX.TPS DC    &HW.'0'   VALUE FOR TOTAL PIUS SENT
&PFX.TPR DC    &HW.'0'   VALUE FOR TOTAL PIUS RECEIVED
&PFX.TBS DC    &FW.'0'   VALUE FOR TOTAL BYTES SENT
&PFX.TBR DC    &FW.'0'   VALUE FOR TOTAL BYTES RECEIVED
&PFX.ROQ DC    &HW.'0'   VALUE FOR RESOURCE OUTBOUND QUEUE LENGTH
&PFX.LUE EQU   *-&PFX.CTRS END CNTRS FOR SDLC AND VIRTUAL LU   @NPA0300
&PFX.TPC DC    &HW.'0'   VALUE FOR TOTAL POLL COUNT
&PFX.PPC DC    &HW.'0'   VALUE FOR POSITIVE POLL COUNT
&PFX.ERR DC    &HW.'0'   VALUE FOR ERROR COUNT
&PFX.RPC DC    &HW.'0'   VALUE FOR RETRANSMIT PIU COUNT
&PFX.RBC DC    &HW.'0'   VALUE FOR RETRANSMIT BYTE COUNT
         ORG   &PFX.CTRS     DATA FOR NCP NODE
&PFX.FCC DC    &FW.'0'   FREE CYCLE COUNT
*   ABOVE COUNTER IS CYCLES WHEN RETURNED FROM NCP AND
*   AND CONVERTED TO TENTHS OF SECONDS FOR PROCESSING AND LOGGING
         ORG   &PFX.FCC                                        @NPA0300
&PFX.UCC DC    &FW.'0'   USED CYCLE COUNT                      @NPA0300
*   ABOVE COUNTER IS USED CYCLES WHEN RETURNED FROM NCP
*   IF THE FLAG RURUCC IS SET TO INDICATE THAT USED CYCLES
*   ARE BEING RETURNED IN THE RU CYCLE COUNT FIELD
*   IT WILL BE DIVIDED BY 8 AND CONVERTED TO FREE CYCLES FOR USE
*   IN OTHER NPA CONTROL BLOCKS
&PFX.FBQ DC    &HW.'0'   VALUE FOR FREE BUFFER QUEUE LENGTH    @NPA0300
&PFX.FBH DC    &HW.'0'   VALUE FOR FREE BUFFER HI-WATER MARK   @NPA0300
&PFX.FBHI EQU   X'0000'   INITIAL VALUE FOR HI-WATER MARK      @NPA0300
&PFX.FBL DC    &HW.'0'   VALUE FOR FREE BUFFER LO-WATER MARK   @NPA0300
&PFX.FBLI EQU   X'7FFF'   INITIAL VALUE FOR LO-WATER MARK      @NPA0300
&PFX.CIQ DC    &HW.'0'   VALUE FOR CHANNEL INTERMEDIATE QUEUE L@NPA0300
&PFX.CHQ DC    &HW.'0'   VALUE FOR CHANNEL HOLD QUEUE LENGTH   @NPA0300
&PFX.SL# DC    &HW.'0'   VALUE FOR NCP SLOWDOWN COUNT IN 0.1 SE@NPA0300
         ORG   &PFX.SL#                                        @NPA0300
&PFX.SL  DC    &HW.'0'   VALUE FOR SLOWDOWN COUNT IN 0.1 SECS  @NPA0300
&PFX.SLM DC    &HW.'0'   VALUE FOR NCP BUFFER SLOWDOWN LIMIT   @NPA0300
&PFX.MXF DC    &HW.'0'   VALUE FOR MAX AVAILABLE NCP BUFFERS   @NPA0300
&PFX.CYS DC    &HW.'0'   VALUE FOR CCU CYCLE SPEED             @NPA0300
&PFX.RSV DC    &HW.'0'   RESERVED                              @NPA0300
         ORG
         AIF   ('&ALIGN' EQ 'NO').PIUA                         @NPA0300
         CNOP  0,4       ALIGN END OF COUNTERS                 @NPA0300
.PIUA    ANOP
&PFX.CLEN EQU  *-&PFX.CTRS MAX LENGTH OF COUNTERS AREA         @NPA0300
&PFX.CCNT EQU  15         COUNT OF CYCLES TO EXECUTE L5LOOP    @NPA0300
         MEND
