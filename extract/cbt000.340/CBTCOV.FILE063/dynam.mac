         MACRO
&N       DYNAM &TYPE,&F=,&DA=,&MEM=,&SP=,&DISP=,&VOL=,&UNIT=,&PASS=,   *
               &DIR=,&SYSOUT=,&DEST=,&FORMS=,&COPIES=,&TXTUNIT=,&Q=,   *
               &MSG=,&ABEND=,&NUM=,&RETURN=,&DS=,&SYSPGM=,&TYPESP=,    X
               &RESTORE=,&LRECL=,&BLKSIZE=,&RECFM=,&DSORG=,&EXPIRE=,   X
               &RETAIN=,&CLOSE=,&CYL=,&TRK=,&FUNC=,&BUSY=,&LABEL=,     X
               &UCS=,&FCB=,&DDNAME=,&NOTFND=,&OPTCD=,&EXIST=,&RLSE=
         GBLA  &T,&IO
         GBLB  &DSORGEQ,&FRB
         LCLA  &NF,&NDA,&NM,&INC,&LEN,&COUNT,&NO,&I
         LCLB  &HVLTR
         LCLC  &TEMP,&TEMP2,&CHAR
&INC     SETA  1
&NO      SETA  2
&IO      SETA  0
         AIF   (K'&DA(1) LT 6).GOALLOC
         AIF   ('&DA(1)'(1,6) EQ 'SYSUID').CALCUID
         AGO   .GOALLOC
.CALCUID ANOP
&N       CLC   =C'SYSUID.',&N.DAP+6
         BNE   &N.BAL
         MVC   &N.DAP+6(44),=CL256' '
         MVC   &N.DAP+6(8),PREFIX
         L     R1,PREFIXL              PREFIX LENGTH
         LA    R1,&N.DAP+6(R1)         POINT PAST PREFIX
&I       SETA  K'&DA(1)
&I       SETA  &I-6
&CHAR    SETC  '&DA(1)'(7,&I)
         MVC   0(&I,R1),=C'&CHAR'
         LA    R1,&I.(R1)
         S     R1,=A(&N.DAP+6)
         STH   R1,&N.DAP+4
         CNOP  0,4
&N.BAL   BAL   R1,&N.S9
         AGO   .HVBAL
.GOALLOC ANOP
         CNOP  0,4
&N       BAL   1,&N.S9
.HVBAL   ANOP
         DC    X'80',AL3(&N.RB)
         AIF   ('&TYPE' EQ 'FREE').FREEB
         AIF   ('&TYPE' EQ 'INQ').INQ
         AIF   ('&TYPE' EQ 'CONCAT').CONB
         AIF   ('&TYPE' EQ 'DECON').DECONB
&N.RB    DC    X'14011000',F'0',A(&N.OTP),2F'0'
.BKCOM   ANOP
&N.OTP   DS    0F
         AIF   ('&DDNAME' EQ '').NODDN
&NF      SETA  N'&DDNAME
         AGO   .DODD1
.NODDN   ANOP
         AIF   ('&F' EQ '').NF1
&NF      SETA  N'&F
.DODD1   ANOP
         DC    A(&N.DDP)
.NF1     ANOP
         AIF   ('&DA' EQ '').NM1
         AIF   ('&DA' EQ 'DUMMY').GODUMB
         AIF   ('&DA' EQ '*').TERM
&NDA     SETA  N'&DA
&N.DAPP  DC    A(&N.DAP)
         AIF   ('&MEM' EQ '').NSYS
&NM      SETA  N'&MEM
&N.MPP   DC    A(&N.MP)
         AGO   .NSYS
.NM1     ANOP
         AIF   ('&Q' EQ '').TRYSYS
         DC    A(&N.QN)            TCAM Q
         AGO   .NSYS
.TRYSYS  ANOP
         AIF   ('&SYSOUT' EQ '').NSYS
         DC    A(&N.SYS)
         AIF   ('&DEST' EQ '').NODEST1
&N.DESTP DC    A(&N.DEST)
.NODEST1 AIF   ('&FORMS' EQ '').NOFORM1
&N.FORMP DC    A(&N.FORM)
.NOFORM1 AIF   ('&COPIES' EQ '').NOCOP1
&N.COPP  DC    A(&N.COP)
.NOCOP1  ANOP
         AIF   ('&UCS' EQ '').NOUCS1
&N.UCSP  DC    A(&N.UCS)
.NOUCS1  ANOP
         AIF   ('&FCB' EQ '').NOFCB1
&N.FCBP  DC    A(&N.FCB)
.NOFCB1  ANOP
         AIF   ('&SYSPGM' EQ '').NOPGM1
&N.PGMP  DC    A(&N.PGM)          PROGRAM TO HANDLE SYSOUT
.NOPGM1  ANOP
         AIF   ('&CLOSE' EQ '').NOCLS
         AIF   ('&CLOSE' NE 'FREE').NOFREC
&N.CLSP  DC    A(&N.CLS)          FREE AFTER CLOSE
.NOFREC  ANOP
.NOCLS   ANOP
         AGO   .TRYDCB
.NSYS    AIF   ('&TYPE' EQ 'FREE').FREEB2
         DC    A(&N.TYP)
         DC    A(&N.DISP)
         DC    A(&N.DISP2)
         AIF   ('&SP' EQ '' AND '&CYL' EQ '' AND '&TRK' EQ '').NSP1
         DC    A(&N.SPP)
         AIF   (K'&SP EQ 1).NOSEC
         DC    A(&N.SPP2)
.NOSEC   ANOP
         DC    A(&N.TSP)      TYPE OF SPACE ALLOCATION
         AIF   ('&RLSE' EQ 'YES').RLSE1
         AGO   .NORLSE1
.RLSE1   ANOP
         DC    A(&N.RLSEP)    RELEASE UNUSED SPACE
.NORLSE1 ANOP
         AIF   ('&DIR' EQ '').NSP1
&N.DIRPP DC    A(&N.DIRP)
.NSP1    AIF   ('&VOL' EQ '').NVOL1
&N.VOLPP DC    A(&N.VOLP)
.NVOL1   AIF   ('&UNIT' EQ '').NUNIT1
&N.UNTP  DC    A(&N.UNT)
.NUNIT1  AIF   ('&PASS' EQ '').NPASS1
&N.PASP  DC    A(&N.PASS)
.NPASS1  ANOP
         AIF   ('&RETAIN' EQ '').NORET1
&N.RETP  DC    A(&N.RET)      RETENTION PERIOD
.NORET1  AIF   ('&EXPIRE' EQ '').NOEXP1
&N.EXPP  DC    A(&N.EXP)      EXPIRATION DATE
.NOEXP1  ANOP
         AIF   ('&LABEL' EQ '').NLAB1
&N.FILEP DC    A(&N.FILE#)    FILE NUMBER
&N.LABP  DC    A(&N.LABEL)    TYPE LABEL
.NLAB1   ANOP
.TRYDCB  ANOP
         AIF   ('&LRECL' EQ '').NOLRECL
&N.LREC  DC    A(&N.LRECL)    LRECL SPECIFIED
.NOLRECL AIF   ('&BLKSIZE' EQ '').NOBLK
&N.BLK   DC    A(&N.BLKS)     BLKSIZE SPECIFIED
.NOBLK   AIF   ('&RECFM' EQ '').NOREC
&N.REC   DC    A(&N.RECF)     RECFM SPECIFIED
.NOREC   AIF   ('&DSORG' EQ '').NODSO
&N.DSO   DC    A(&N.DSOR)     DSORG SPECIFIED
.NODSO   ANOP
         AIF   ('&OPTCD' EQ '').NOOPT1
&N.OPT   DC    A(&N.OPTCD)    DCB OPTCD SPECIFIED
.NOOPT1  ANOP
         AIF   ('&FUNC' EQ '').NOFUNC
&N.FUNCP DC    A(&N.FUNC)         FUNCTION SPECIFIED
.NOFUNC  ANOP
         AIF   ('&RETURN' EQ '').NOART1
&INC     SETA  1
&COUNT   SETA  N'&RETURN
.CYCART1 ANOP
         AIF   ('&RETURN(&INC)' EQ 'DDNAME').ARTDD1
         AIF   ('&RETURN(&INC)' EQ 'DDN').ARTDD1
         AIF   ('&RETURN(&INC)' EQ 'DSN').ARTDS1
         AIF   ('&RETURN(&INC)' EQ 'DA').ARTDS1
         AIF   ('&RETURN(&INC)' EQ 'DSORG').ARTDSO1
         AIF   ('&RETURN(&INC)' EQ 'VOL').ARTVOL1
&TEMP    SETC  '&RETURN(&INC)'
         MNOTE 4,'INVALID RETURN SUBPARM &TEMP'
         AGO   .COMART1
.ARTDD1  ANOP
         DC    A(&N.RTDD)         RETURN DDNAME SPECIFIED
         AGO   .COMART1
.ARTDS1  ANOP
         DC    A(&N.RTDA)         RETURN DSNAME SPECIFIED
         AGO   .COMART1
.ARTDSO1 ANOP
         DC    A(&N.RTDSO)        RETURN DSORG SPECIFIED
         AGO   .COMART1
.ARTVOL1 ANOP
         DC    A(&N.RTVOL)        RETURN VOL SER SPECIFIED
         AGO   .COMART1
.COMART1 ANOP
&INC     SETA  &INC+1
         AIF   (&INC GT &COUNT).NOART1
         AGO   .CYCART1
.NOART1  ANOP
.ENDLIST ANOP
         AIF   ('&TYPE' EQ 'FREE').FREEB3
         DC    X'80',AL3(&N.PA)
.SLIST2  ANOP
         AIF   ('&DDNAME' EQ '').CKF
&N.DDP   DC    X'00010001'         FILE NAME PARM
         AIF   (&NF EQ 1).STRDD
&TEMP    SETC  '&DDNAME(1)'
&LEN     SETA  K'&DDNAME(1)
         DC    AL2(&LEN.)          LENGTH OF DDNAME
         DC    CL8'&TEMP.'         DDNAME
         AGO   .NF2
.STRDD   ANOP
&T       SETA  K'&DDNAME
         DC    AL2(&T)             LENGTH OF NAME
         DC    CL8'&DDNAME.'       FILE NAME
         AGO   .NF2
.CKF     ANOP
         AIF   ('&F' EQ '').NF2
&N.DDP   DC    X'00010001'         FILE NAME PARM
         AIF   (&NF EQ 1).STRF
&TEMP    SETC  '&F(1)'
&LEN     SETA  K'&F(1)
         DC    AL2(&LEN.)          LENGTH OF DDNAME
         DC    CL8'&TEMP.'         DDNAME
         AGO   .NF2
.STRF    ANOP
&T       SETA  K'&F
         DC    AL2(&T)             LENGTH OF NAME
         DC    CL8'&F.'            FILE NAME
.NF2     AIF   ('&DA' EQ '').NM2
         AIF   ('&DA' EQ '*').TERM2
         AIF   ('&DA' EQ 'DUMMY').DUMB2
&N.DAP   DC    X'00020001'         DATA SET NAME
         AIF   (&NDA EQ 1).STRDA
&TEMP    SETC  '&DA(1)'
&LEN     SETA  K'&DA(1)
         DC    AL2(&LEN.)          LENGTH OF DATASET NAME
         DC    CL44'&TEMP.'        DATASET NAME
         AGO   .GOMEM
.STRDA   ANOP
&T       SETA  K'&DA
         DC    AL2(&T)             LENGTH OF DATASET NAME
         DC    CL44'&DA.'          DATA SET NAME
.GOMEM   ANOP
         AIF   ('&MEM' EQ '').NSYS2
&N.MP    DC    X'00030001'         MEMBER NAME
         AIF   (&NM EQ 1).STRMEM
&TEMP    SETC  '&MEM(1)'
&LEN     SETA  K'&MEM(1)
         DC    AL2(&LEN.)          LENGTH OF MEMBER NAME
         DC    CL8'&TEMP.'         MEMBER NAME
         AGO   .NSYS2
.STRMEM  ANOP
&T       SETA  K'&MEM
         DC    AL2(&T)             LENGTH OF MEMBER NAME
         DC    CL8'&MEM.'          MEMBER NAME
         AGO   .NSYS2
.NM2     ANOP
         AIF   ('&Q' EQ '').TRYSYS2
&N.QN    DC    X'00270001'
&T       SETA  K'&Q
         DC    XL2'&T.'            LENGTH OF QNAME
         DC    CL8'&Q.'            Q NAME
         AGO   .NSYS2
.TRYSYS2 ANOP
         AIF   ('&TYPE' EQ 'FREE').FREEB4
         AIF   ('&SYSOUT' EQ '').NSYS2
&N.SYS   DC    X'001800010001',C'&SYSOUT.'
         DS    0H
         AIF   ('&FORMS' EQ '').NOFORM2
&N.FORM  DC    X'001A0001'         SPECIAL FORMS
&T       SETA  K'&FORMS
         DC    AL2(&T.)
         DC    CL4'&FORMS.'        FORM NUMBER
.NOFORM2 AIF   ('&DEST' EQ '').NODEST2
&N.DEST  DC    X'00580001'         ROUTE SYSOUT TO REMOTE WORK STATION
&T       SETA  K'&DEST
         DC    AL2(&T.)            LENGTH OF NAME
         DC    CL7'&DEST.'         REMOTE STATION NAME
         DS    0H
.NODEST2 AIF   ('&COPIES' EQ '').NOCOP2
&N.COP   DC    X'001D00010001'     MULTIPLE COPIES
         DC    AL1(&COPIES.)       NUMBER OF COPIES
         DS    0H
.NOCOP2  ANOP
         AIF   ('&UCS' EQ '').NOUCS2
&T       SETA  K'&UCS
&N.UCS   DC    X'00290001'
         DC    AL2(&T.)
         DC    CL4'&UCS'
.NOUCS2  ANOP
         AIF   ('&FCB' EQ '').NOFCB2
&T       SETA  K'&FCB
&N.FCB   DC    X'00250001'
         DC    AL2(&T.)
         DC    CL4'&FCB'
.NOFCB2  ANOP
         AIF   ('&SYSPGM' EQ '').NOPGM2
&T       SETA  K'&SYSPGM
&N.PGM   DC    X'00190001'
         DC    AL2(&T.)            LENGTH OF PGM NAME
         DC    CL8'&SYSPGM.'       PROGRAM TO HANDLE SYSOUT
.NOPGM2  ANOP
         AIF   ('&CLOSE' EQ '').NOCLS2
         AIF   ('&CLOSE' NE 'FREE').NOFREC2
&N.CLS   DC    X'001C0000'         FREE TO PRINT AFTER CLOSE
.NOFREC2 ANOP
.NOCLS2  ANOP
         AGO   .ENDLST2
.NSYS2   ANOP
         AIF   ('&DISP' EQ '').DFLTD
         AIF   ('&DISP(1)' EQ 'NEW').NEW
         AIF   ('&DISP(1)' EQ 'OLD').OLD
         AIF   ('&DISP(1)' EQ 'MOD').MOD
         AIF   ('&DISP(1)' EQ 'SHR').SHR
.CKSPC   ANOP
         AIF   ('&SP' EQ '').TRYCYLX
&TEMP    SETC  '&SP(1)'
&TEMP2   SETC  '&SP(2)'
         DS    0H
         AIF   ('&TYPESP' EQ '').TRK
         AIF   ('&TYPESP' EQ 'TRK').TRK
         AIF   ('&TYPESP' EQ 'CYL').CYL
         AGO   .BLK
.TRK     ANOP
&N.TSP   DC    X'00070000'         TRACK ALLOCATION
         AGO   .SETSP
.CYL     ANOP
&N.TSP   DC    X'00080000'         CYL ALLOCATION
         AGO   .SETSP
.BLK     ANOP
&N.TSP   DC    X'000900010003'     BLOCK ALLOCATION
&N.BLK   DC    AL3(&TYPESP)
         AGO   .SETSP
.TRYCYLX AIF   ('&CYL' EQ '').TRYTRKX
&TEMP    SETC  '&CYL(1)'
&TEMP2   SETC  '&CYL(2)'
&N.TSP   DC    X'00080000'    CYL ALLOCATION
         AGO   .SETSP
.TRYTRKX AIF   ('&TRK' EQ '').NSP2
&TEMP    SETC  '&TRK(1)'
&TEMP2   SETC  '&TRK(2)'
&N.TSP   DC    X'00070000'      TRACK ALLOCATION
.SETSP   ANOP
&N.SPP   DC    X'000A00010003'
         DC    AL3(&TEMP.)         SPACE ALLOCATION
         DS    0H
.CKSP22  ANOP
        AIF   ('&TEMP2' NE '').DOSEC2
&TEMP2  SETC  '0'
.DOSEC2 ANOP
&N.SPP2  DC    X'000B00010003'
         DC    AL3(&TEMP2.)
.NOSEC2  ANOP
         AIF   ('&RLSE' EQ 'YES').RLSE2
         AGO   .NORLSE2
.RLSE2   ANOP
         DS    0H
&N.RLSEP DC    X'000D0000'
.NORLSE2 ANOP
         DS    0H
         AIF   ('&DIR' EQ '').NDIR2
&N.DIRP  DC    X'000C00010003'
         DC    AL3(&DIR.)          DIRECTARY SPACE
         DS    0H
.NDIR2   ANOP
.NSP2    ANOP
         AIF   ('&UNIT' EQ '').NU2
&T       SETA  K'&UNIT
&N.UNT   DC    X'00150001',AL2(&T),CL10'&UNIT.'
.NU2     AIF   ('&VOL' EQ '').NV2
&T       SETA  K'&VOL
&N.VOLP  DC    X'00100001',AL2(&T),CL8'&VOL.'
.NV2     AIF   ('&PASS' EQ '').NP2
&T        SETA  K'&PASS
&N.PASS  DC    X'00500001',AL2(&T),CL8'&PASS.'
.NP2     ANOP
         AIF   ('&RETAIN' EQ '').NORET2
&N.RET   DC    X'002300010002'
         DC    A(&RETAIN)
.NORET2  ANOP
         AIF   ('&EXPIRE' EQ '').NOEXP2
&N.EXP   DC    X'002200010005'
         DC    CL5'&EXPIRE'
.NOEXP2  ANOP
         AIF   ('&LABEL' EQ '').NOLAB2
         AIF   ('&LABEL(1)' EQ '').LABD1
&TEMP    SETC  '&LABEL(1)'
&N.FILE# DC    X'001F00010002',AL2(&TEMP)
         AGO   .LAB22
.LABD1   ANOP
&N.FILE# DC    X'001F00010002',AL2(1)     DEFAULT TO FILE ONE
.LAB22   ANOP
         AIF   ('&LABEL(2)' EQ '').LABSL
         AIF   ('&LABEL(2)' EQ 'SL').LABSL
         AIF   ('&LABEL(2)' EQ 'NL').LABNL
         AIF   ('&LABEL(2)' EQ 'NSL').LABNSL
         AIF   ('&LABEL(2)' EQ 'BLP').LABBLP
.LABSL   ANOP
&I       SETA  2
&CHAR    SETC  'SL'
         AGO   .LAB23
.LABNL   ANOP
&I       SETA  1
&CHAR    SETC  'NL'
         AGO   .LAB23
.LABNSL  ANOP
&I       SETA  4
&CHAR    SETC  'NSL'
         AGO   .LAB23
.LABBLP  ANOP
&I       SETA  16
&CHAR    SETC  'BLP'
         AGO   .LAB23
.LAB23   ANOP
&N.LABEL DC    X'001E00010001',AL1(&I)       &CHAR
.NOLAB2  ANOP
.ENDLST2 ANOP
         AIF   ('&TYPE' EQ 'FREE').FREEB6
         AIF   ('&LRECL' EQ '').TRYBLK
&N.LRECL DC    X'004200010002'
         DC    AL2(&LRECL.)
.TRYBLK  AIF   ('&BLKSIZE' EQ '').TRYRF
&N.BLKS  DC    X'003000010002'
         DC    AL2(&BLKSIZE.)
.TRYRF   AIF  ('&RECFM' EQ '').TRYDSO
&N.RECF  DC    X'004900010001'
         AIF   ('&RECFM' EQ 'U').U
         AIF   ('&RECFM' EQ 'F').F
         AIF   ('&RECFM' EQ 'V').V
         AIF   ('&RECFM' EQ 'FB').FB
         AIF   ('&RECFM' EQ 'VB').VB
         AIF   ('&RECFM' EQ 'FA').FA
         AIF   ('&RECFM' EQ 'FM').FM
         AIF   ('&RECFM' EQ 'VA').VA
         AIF   ('&RECFM' EQ 'VM').VM
         AIF   ('&RECFM' EQ 'FBA').FBA
         AIF   ('&RECFM' EQ 'FBM').FBM
         AIF   ('&RECFM' EQ 'VBA').VBA
         AIF   ('&RECFM' EQ 'VBM').VBM
         AGO   .ENDRF
.U       ANOP
         DC    X'C0'          UNDEFINED RECFM
         AGO   .ENDRF
.F       ANOP
         DC    X'80'          FIXED RECFM
         AOG   .ENDRF
.V       ANOP
         DC    X'40'          VARIBLE RECFM
         AGO   .ENDRF
.FB      ANOP
         DC    X'90'          FB RECFM
         AGO   .ENDRF
.VB      ANOP
         DC    X'50'          VB RECFM
         AGO   .ENDRF
.FA      ANOP
         DC    X'84'          FA RECFM
         AGO   .ENDRF
.FM      ANOP
         DC    X'82'          FM RECFM
         AGO   .ENDRF
.VA      ANOP
         DC    X'44'          VA RECFM
         AGO   .ENDRF
.VM      ANOP
         DC    X'42'          VM RECFM
         AGO   .ENDRF
.FBA     ANOP
         DC    X'94'          FBA RECFM
         AGO   .ENDRF
.FBM     ANOP
         DC    X'92'          FBM RECFM
         AGO   .ENDRF
.VBA     ANOP
         DC    X'54'          VBA RECFM
         AGO   .ENDRF
.VBM     ANOP
         DC    X'52'          VBM RECFM
         AGO   .ENDRF
.ENDRF   ANOP
.TRYDSO  AIF  ('&DSORG' EQ '').NODSO2
&N.DSOR  DC    X'003C00010002'
         AIF   ('&DSORG' EQ 'PS').DSOPS
         AIF   ('&DSORG' EQ 'PO').DSOPO
         AIF   ('&DSORG' EQ 'DA').DSODA
         MNOTE 8,'DSORG IS UNSUPPORTED'
         AGO   .NODSO2
.DSOPS   ANOP
         DC    X'4000'
         AGO   .NODSO2
.DSOPO   ANOP
         DC    X'0200'            PO
         AGO   .NODSO2
.DSODA   ANOP
         DC    X'2000'            DA
.NODSO2  ANOP
         AIF   ('&OPTCD' EQ '').NOOPT2
&N.OPTCD DC    X'004500010001'
         AIF   ('&OPTCD' EQ 'R').OPTR
         AIF   ('&OPTCD' EQ 'T').OPTT
         AIF   ('&OPTCD' EQ 'Z').OPTZ
         AIF   ('&OPTCD' EQ 'F').OPTF
         AIF   ('&OPTCD' EQ 'O').OPTO
.OPTR    ANOP
         DC    X'01'
         AGO   .NOOPT2
.OPTT    ANOP
         DC    X'02'             TOTALING FACILITY
         AGO   .NOOPT2
.OPTZ    ANOP
         DC    X'04'             REDUCED TAPE ERR RECOVERY
         AGO   .NOOPT2
.OPTF    ANOP
         DC    X'10'             FEEDBACK
         AGO   .NOOPT2
.OPTO    ANOP
         DC    X'10'             ONLINE CORRECTION FOR OPTICAL READ
         AGO   .NOOPT2
.NOOPT2  ANOP
         AIF   ('&FUNC' EQ '').NOFUNC2
&N.FUNC  DC    X'005A00010001'
         AIF   ('&FUNC' EQ 'I').DOFI
         AIF   ('&FUNC' EQ 'W').DOFW
&N.FUNCT DC    X'0'
         AGO   .NOFUNC2
.DOFI    ANOP
&N.FUNCT DC    X'80'
         AGO   .NOFUNC2
.DOFW    ANOP
&N.FUNCT DC    X'10'
.NOFUNC2 ANOP
         AIF   ('RETURN' EQ '').NOART2
&INC     SETA  1
&COUNT   SETA  N'&RETURN
.CYCART2 ANOP
         AIF   ('&RETURN(&INC)' EQ 'DDNAME').ARTDD2
         AIF   ('&RETURN(&INC)' EQ 'DDN').ARTDD2
         AIF   ('&RETURN(&INC)' EQ 'DSN').ARTDS2
         AIF   ('&RETURN(&INC)' EQ 'DA').ARTDS2
         AIF   ('&RETURN(&INC)' EQ 'DSORG').ARTDSO2
         AIF   ('&RETURN(&INC)' EQ 'VOL').ARTVOL2
         AGO   .COMART2
.ARTDD2  ANOP
&N.RTDD  DC    X'005500010008',CL8' '
         AGO   .COMART2
.ARTDS2  ANOP
&N.RTDA  DC   X'00560001002C',CL44' '
         AGO   .COMART2
.ARTDSO2 ANOP
&N.RTDSO DC   X'0057000100020000'
DSORG0   DC   X'0000'
DSORGPS  DC   X'4000'
DSORGPO  DC   X'0200'
DSORGDA  DC   X'2000'
DSORGIS  DC   X'8000'
DSORGVS  DC   X'0008'
DSORGPOU DC   X'0201'
DSORGDAU DC   X'2001'
DSORGISU DC   X'8001'
DSORGVSU DC   X'0009'
&DSORGEQ SETB 1
         AGO   .COMART2
.ARTVOL2 ANOP
&N.RTVOL DC   X'005D00010006',CL6' '
         AGO   .COMART2
.COMART2 ANOP
&INC     SETA  &INC+1
         AIF   (&INC GT &COUNT).NOART2
         AGO   .CYCART2
.NOART2  ANOP
&N.PA    DC    X'00520000'         PERMANENTLY ALLOCATED
.BK6     ANOP
&N.BREG  DC    A(&N.END)
&N.C9    ST    14,&N.BREG          SAVE RETURN REG
         LA    1,&N.RB-4
&N.S9    SVC   99                  DYNAMIC ALLOCATION SVC
         AIF   ('&NOTFND' EQ '').TRYBUSY
         AGO   .TESTRC
.TRYBUSY ANOP
         AIF   ('&BUSY' EQ '').TRYEXIS
         AGO   .TESTRC
.TRYEXIS ANOP
         AIF   ('&EXIST' EQ '').NOFRB
.TESTRC  ANOP
         LTR   15,15               CHECK RETURN CODE
&HVLTR   SETB  1
         BZ    &N.BR14             B IR GOOD
         AIF   ('&NOTFND' EQ '').TRYEX2
         CLI   &N.RB+4,X'17'       LOCATE ERROR?
         BE    &NOTFND
         CLI   &N.RB+4,X'67'       OBTAIN ERROR?
         BE    &NOTFND
.TRYEX2  ANOP
         AIF   ('&EXIST' EQ '').TRYB2
         AIF   ('&EXIST' NE 'SHR').TRYOE
&CHAR    SETC  '&N.CHS'
         AGO   .CLCE
.TRYOE   AIF   ('&EXIST' NE 'OLD').TRYME
&CHAR    SETC  '&N.CHO'
         AGO   .CLCE
.TRYME   AIF   ('&EXIST' NE 'MOD').TRYEXE
&CHAR    SETC  '&N.CHM'
         AGO   .CLCE
.TRYEXE  ANOP
&CHAR    SETC  '&EXIST'
.CLCE    ANOP
         CLC   &N.RB+4(2),=X'5708' IS DATASET ALREADY CATALOGED?
         BE    &CHAR                 ERROR EXIT
         CLC   &N.RB+4(2),=X'4704' DOES DATASET ALREADY EXIST?
         AIF   ('&EXIST' EQ 'SHR').CHGSHR
         AIF   ('&EXIST' EQ 'OLD').CHGOLD
         AIF   ('&EXIST' EQ 'MOD').CHGMOD
         BE    &CHAR
         AGO   .TRYB2
.CHGSHR  ANOP
         BNE   &N.NOE
&N.CHS   MVI   &N.TYP+6,X'08'        ALLOCATE SHR
         AGO   .CHGTYPE
.CHGOLD  ANOP
&N.CHO   MVI   &N.TYP+6,X'01'        ALLOCATE OLD
         AGO   .CHGTYPE
.CHGMOD  ANOP
&N.CHM   MVI   &N.TYP+6,X'02'        ALLOCATE MOD
.CHGTYPE ANOP
         MVI   &N.DISP+6,X'08'       KEEP IT
         MVI   &N.DISP2+6,X'08'      KEEP IT
         B     &N                    GO TRY IT AGAIN
&N.NOE   DS    0H                    NOT ALREADY EXISTING
.TRYB2   ANOP
         AIF   ('&BUSY' EQ '').NOFRB
         CLC   &N.RB+4(2),=X'0410' FILE IN USE???
         AIF   ('&BUSY' EQ 'FREE').GOFREE
         BE    &BUSY
         AGO   .NOFRB
.GOFREE  ANOP
         BNE   &N.ENDFR
         MVC   FRBDDP+4(10),&N.DDP+4   FILE NAME
         AIF   (&FRB).BALFRB
&FRB     SETB  1
         BAL   10,FRB
         LTR   15,15
         BZ    &N
         AIF   ('&MSG' NE 'Y').NOFRMSG
         CNOP  0,4
         BAL   1,&N.FAIL
         DC    A(FRBRB,&N.RTNCD,&N.ZERO,&N.CID)
         AGO   .FRB
.NOFRMSG ANOP
         B     &N.ENDFR
.FRB     ANOP
FRB      DYNAM FREE,F=X
         BR    10
         AGO   .ENDFRB
.BALFRB  ANOP
         BAL   10,FRB
         LTR   15,15
         BZ    &N
         AIF   ('&MSG' NE 'Y').NOFRM2
         ST    15,&N.RTNCD
         CNOP  0,4
         BAL   1,&N.FAIL
         DC    A(FRBRB,&N.RTNCD,&N.ZERO,&N.CID)
         AGO   .ENDFRB
.NOFRM2  ANOP
         B     &N.ENDFR
         B     &N
.ENDFRB  ANOP
&N.ENDFR DS   0H
.NOFRB   ANOP
         AIF   ('&MSG' NE 'Y').ABEND
         AIF   (&HVLTR).NOLTR1
         LTR   15,15               CHECK RETURN CODE
         BZ    &N.BR14             BR IF GOOD
         ST    15,&N.RTNCD         SAVE RETURN CODE
.NOLTR1  ANOP
         CNOP  0,4
         BAL   1,&N.FAIL           POINT TO PARM LIST
         DC    A(&N.RB,&N.RTNCD,&N.ZERO,&N.CID)
&N.RTNCD DC    F'0'                RETURN CODE
&N.ZERO  DC    F'0'                A(IKJEFF02)
&N.CID   DC    X'8032'             REQ WTP MSG & IT IS AN SVC99 ERR
&N.FAIL  ST    15,&N.RTNCD
         LINK  EP=IKJEFF18         LINK TO DAIRFAIL ROUTINE
         AIF   ('&ABEND' NE 'Y').NOAB
         AIF   ('&MSG' NE 'Y').NOMSG4
         L     15,&N.RTNCD         GET SVC 99 RETURN CODE
.NOMSG4  ANOP
         ABEND 99,DUMP
.NOAB    ANOP
         AIF   ('&MSG' NE 'Y').NOMSG5
         L     15,&N.RTNCD    GET SVC 99 RETURN CODE
.NOMSG5  ANOP
&COUNT   SETA  0
&N.BR14  L     14,&N.BREG
         BR    14
&N.END   DS    0H
&INC     SETA  1
.CYCMULT ANOP
&INC     SETA  &INC+1
         AIF   (&NF EQ 1).TRYD3
         AIF   (&NF LT &INC).TRYD3
&COUNT   SETA  &COUNT+1
&LEN     SETA  K'&F(&INC)
&TEMP    SETC  '&F(&INC)'
         LA    1,&LEN.(0)         LENGTH OF NEXT FILE NAME
         STH   1,&N.DDP+4         SAVE IT IN FILE PARM
         MVC   &N.DDP+6(8),=CL8'&TEMP.'
.TRYD3   AIF   (&NDA LT &INC).TRYM3
         AIF   (&NDA EQ 1).TRYM3
&COUNT   SETA  &COUNT+1
         AIF   (K'&DA(&INC) LT 6).GOCYCA
         AIF   ('&DA(&INC)'(1,6) EQ 'SYSUID').CUID2
         AGO   .GOCYCA
.CUID2   ANOP
         MVC   &N.DAP+6(44),=CL256' '
         MVC   &N.DAP+6(8),PREFIX
         L     R1,PREFIXL              PREFIX LENGTH
         LA    R1,&N.DAP+6(R1)         POINT PAST PREFIX
&I       SETA  K'&DA(&INC)
&I       SETA  &I-6
&CHAR    SETC  '&DA(&INC)'(7,&I)
         MVC   0(&I,R1),=C'&CHAR'
         LA    R1,&I.(R1)
         S     R1,=A(&N.DAP+6)
         STH   R1,&N.DAP+4
         AGO   .TRYM3
.GOCYCA  ANOP
&LEN     SETA  K'&DA(&INC)
&TEMP    SETC  '&DA(&INC)'
         LA    1,&LEN.(0)         LENGTH OF NEXT DATASET NAME
         STH   1,&N.DAP+4         SAVE IN DA PARM
         MVC   &N.DAP+6(44),=CL44'&TEMP.'
.TRYM3   AIF   (&NM LT &INC).GOCYC3
         AIF   (&NM EQ 1).GOCYC3
&COUNT   SETA  &COUNT+1
&LEN     SETA  K'&MEM(&INC)
&TEMP    SETC  '&MEM(&INC)'
         LA    1,&LEN.(0)         LENGTH OF NEXT MEMBER NAME
         STH   1,&N.MP+4          SAVE IN MEMBER PARM
         MVC   &N.MP+6(8),=CL8'&TEMP.'
.GOCYC3  ANOP
         AIF   (&COUNT EQ 0).GOEND
&COUNT   SETA  0
         AIF   (&NF NE 1).ARNCAT
&I       SETA  K'&F
         AIF   (&I EQ 8).TRUNK
&I       SETA  &I+1
         AIF   (&IO EQ &I).TRUNK
         LA    1,&I.(0)       NEW LENGTH
         STH   1,&N.DDP+4
.TRUNK   ANOP
&IO      SETA  &I
&I       SETA  &I+5
         MVI   &N.DDP+&I,C'&NO.'
&NO      SETA  &NO+1
.ARNCAT  ANOP
         BAL   14,&N.C9
         AGO   .CYCMULT
.GOEND   ANOP
         AIF   ('&RESTORE' NE 'Y').CKMF
         LA    1,&N.END       RESTORE MACRO
         ST    1,&N.BREG
         AIF   (N'&F EQ 1).TRYDAE
&TEMP    SETC  '&F(1)'
&T       SETA  K'&F
         MVC   &N.DDP+6(&T.),=C'&TEMP.'
         LA    1,&T.(0)
         STH   1,&N.DDP+4
.TRYDAE  ANOP
         AIF   (N'&DA EQ 1).TRYMEME
&TEMP    SETC  '&DA(1)'
&T       SETA  K'&DA
         MVC   &N.DAP+6(&T.),=C'&TEMP.'
         LA    1,&T.(0)
         STH   1,&N.DAP+4
.TRYMEME ANOP
         AIF   (N'&MEM EQ 1).CKMF
&TEMP    SETC  '&MEM(1)'
&T       SETA  K'&MEM
         MVC   &N.MP+6(&T.),=C'&TEMP.'
         LA    1,&T.(0)
         STH   1,&N.MP+4
&I       SETA  K'&F
.CKMF    ANOP
         AIF   (&NO EQ 2).ENDCK
         AIF   (&NO EQ 3).TWOCAT
         AIF   (&NO EQ 4).THRECAT
         AIF   (&NO EQ 5).FOURCAT
         AGO   .ENDCK
.TWOCAT  AIF   (&I EQ 8).T2
&N.C     DYNAM CONCAT,F=(&F,&F.2)
         AGO   .ENDCK
.T2      ANOP
&N.C     DYNAM CONCAT,F=(&F,&F(7).2)
         AGO   .ENDCK
.THRECAT AIF   (&I EQ 8).T3
&N.C     DYNAM CONCAT,F=(&F,&F.2,&F.3)
         AGO   .ENDCK
.T3      ANOP
&N.C     DYNAM CONCAT,F=(&F,&F(7).2,&F(7).3)
         AGO   .ENDCK
.FOURCAT AIF   (&I EQ 8).T4
&N.C     DYNAM CONCAT,F=(&F,&F.2,&F.3,&F.4)
         AGO   .ENDCK
.T4      ANOP
&N.C     DYNAM CONCAT,F=(&F,&F(7).2,&F(7).3,&F(7).4)
         AGO   .ENDCK
.ENDCK   ANOP
         AGO   .END
.ABEND   AIF   ('&ABEND' NE 'Y').NOAB
         LTR   15,15          CHECK RETURN CODE
         BZ    &N.BR14
         ABEND 99,DUMP
         AGO   .NOAB
.FREEB   ANOP
&N.RB    DC    X'14020000',F'0',A(&N.OTP),2F'0'
         AGO   .BKCOM
.FREEB2  ANOP
         AIF   ('&DEST' EQ '').NDESTF1
         DC    A(&N.DEST)
.NDESTF1 ANOP
         AIF   ('&DISP' EQ '').NODISPF
         DC    A(&N.DISP)
.NODISPF ANOP
         AGO   .FREEB3
.GODUMB  ANOP
         DC    A(&N.DUMY)
         AGO   .ENDLIST
.TERM    ANOP
         DC    A(&N.TRMP)
         AGO   .ENDLIST
.FREEB3  ANOP
         DC    X'80',AL3(&N.RMVP)
         AGO   .SLIST2
.FREEB4  ANOP
         AIF   ('&SYSOUT' EQ '').NSYSF4
&N.SYS   DC    X'001800010001'     OVERRIDE SYSOUT CLASS
         DC    C'&SYSOUT.'
         DS    0H
.NSYSF4  ANOP
         AIF   ('&DEST' EQ '').NDESTF2
&N.DEST  DC    X'00580001'
&T       SETA  K'&DEST
         DC    AL2(&T.)            LENGTH OF REMOTE NAME
         DC    CL7'&DEST.'         REMOTE NAME
         DS    0H
.NDESTF2 ANOP
         AIF   ('&DISP' EQ '').FREEB6
&N.DISP  DC    X'000500010001'     OVERRIDE DISPOSITION
         AIF   ('&DISP' EQ 'KEEP').GOKPF
         AIF   ('&DISP' EQ 'DELETE').GODELF
         AIF   ('&DISP' EQ 'DEL').GODELF
         AIF   ('&DISP' EQ 'CAT').GOCATF
         AIF   ('&DISP' EQ 'CATLG').GOCATF
         AIF   ('&DISP' EQ 'UNCAT').GOUNCF
         AIF   ('&DISP' EQ 'UNCATLG').GOUNCF
         AGO   .FREEB6
.GOKPF   ANOP
         DC    X'08'               KEEP
         DS    0H
         AGO   .FREEB6
.GODELF  ANOP
         DC    X'04'               DELETE
         DS    0H
         AGO   .FREEB6
.GOCATF  ANOP
         DC    X'02'               CATALOG
         DS    0H
         AGO   .FREEB6
.GOUNCF  ANOP
         DC    X'01'               UNCATALOG
         DS    0H
         AGO   .FREEB6
.TERM2   ANOP
&N.TRMP  DC    X'00280000'         TERMINAL
         AGO   .ENDLST2
.DUMB2   ANOP
&N.DUMY  DC    X'00240000'         DUMMY
         AGO   .ENDLST2
.FREEB6  ANOP
&N.RMVP  DC    X'00070000'         UNALLOCATE
         AGO   .BK6
.DFLTD   ANOP
         AIF   ('&SP' EQ '' AND '&CYL' EQ '' AND '&TRK' EQ '').SHRDFL
&N.TYP    DC    X'000400010001'     TYPE OF ALLOCATION
         DC    X'04'               DEFAULT TO NEW
.DFLTC   ANOP
&N.DISP  DC    X'000500010001'     DISPOSITION
         DC    X'02'               CATALOG
         DS    0H
&N.DISP2 DC    X'000600010001'     CONDITIONAL DISPOSITION
         DC    X'02'               DEFAULT TO CATALOG
         DS    0H
         AGO   .CKSPC
.SHRDFL  ANOP
&N.TYP   DC    X'000400010001'     TYPE OF ALLOCATION
         DC    X'08'               DEFAULT TO SHR
         DS    0H
.DFLTK   ANOP
&N.DISP  DC    X'000500010001'     DISPOSITION
         DC    X'08'               DEFAULT TO KEEP
         DS    0H
&N.DISP2 DC    X'000600010001'     CONDITIONAL DISPOSITION
         DC    X'08'               DEFAULT TO KEEP
         DS    0H
         AGO   .CKSPC
.SHR     ANOP
&N.TYP   DC    X'000400010001'     TYPE OF ALLOCATION
         DC    X'08'               SHR
         DS    0H
         AGO   .MODCOM
.NEW     ANOP
&N.TYP   DC    X'000400010001'     TYPE OF ALLOCATION
         DC    X'04'               NEW
         DS    0H
         AIF   (N'&DISP EQ 1).CAT1
         AGO   .MODCOM2
.OLD     ANOP
&N.TYP   DC    X'000400010001'     TYPE OF ALLOCATION
         DC    X'01'               OLD
         DS    0H
         AGO   .MODCOM
.MOD     ANOP
&N.TYP   DC    X'000400010001'     TYPE OF ALLOCATION
         DC    X'02'               MOD
         DS    0H
.MODCOM  AIF   (N'&DISP EQ 1).DFLTK
.MODCOM2 AIF   ('&DISP(2)' EQ 'CAT').CAT1
         AIF   ('&DISP(2)' EQ 'CATLG').CAT1
         AIF   ('&DISP(2)' EQ 'KEEP').KEEP1
         AIF   ('&DISP(2)' EQ 'DEL').DEL1
         AIF   ('&DISP(2)' EQ 'UNCAT').UNCAT1
.CAT1    ANOP
&N.DISP  DC    X'000500010001'     DISPOSITION
         DC    X'02'               CATALOG
         DS    0H
         AIF   (N'&DISP EQ 3).MODCOM3
.CAT2    ANOP
&N.DISP2 DC    X'000600010001'     CONDITIONAL DISPOSITION
         DC    X'02'               CATALOG
         DS    0H
         AGO   .CKSPC
.UNCAT1  ANOP
&N.DISP  DC    X'000500010001'     DISPOSITION
         DC    X'01'               UNCATALOG
         DS    0H
         AIF   (N'&DISP EQ 3).MODCOM3
.UNCAT2  ANOP
&N.DISP2 DC    X'000600010001'     CONDITIONAL DISPOSITION
         DC    X'01'               UNCATALOG
         DS    0H
         AGO   .CKSPC
.KEEP1   ANOP
&N.DISP  DC    X'000500010001'     DISPOSITION
         DC    X'08'               KEEP
         DS    0H
         AIF   (N'&DISP EQ 3).MODCOM3
.KEEP2   ANOP
&N.DISP2 DC    X'000600010001'     CONDITIONAL DISPOSITION
         DC    X'08'               KEEP
         DS    0H
         AGO   .CKSPC
.DEL1    ANOP
&N.DISP  DC    X'000500010001'     DISPOSITION
         DC    X'04'               DELETE
         DS    0H
         AIF   (N'&DISP EQ 3).MODCOM3
.DEL2    ANOP
&N.DISP2 DC    X'000600010001'         CONDITIONAL DISPOSITION
         DC    X'04'               DELETE
         DS    0H
         AGO   .CKSPC
.MODCOM3 AIF   ('&DISP(3)' EQ 'CAT').CAT2
         AIF   ('&DISP(3)' EQ 'CATLG').CAT2
         AIF   ('&DISP(3)' EQ 'KEEP').KEEP2
         AIF   ('&DISP(3)' EQ 'DEL').DEL2
         AIF   ('&DISP(3)' EQ 'UNCAT').UNCAT2
.CONB    ANOP
&N.RB    DC    X'14030000',F'0',A(&N.OPT),2F'0'
&N.OPT   DS    0F
         DC    A(&N.CONF)
         DC    X'80',AL3(&N.CONP)
&N.CONF  DC    X'0001'
&T       SETA  N'&F
         DC    AL2(&T.)
&I       SETA  1
.LOOP    ANOP
&T       SETA  K'&F(&I)
         DC    AL2(&T)
         DC    C'&F(&I)'
&I       SETA  &I+1
         AIF   (&I GT N'&F).DNCON
         AGO   .LOOP
.DNCON   ANOP
&N.CONP  DC    X'00040000'         PERMANENTLY CONCATENATED
         AGO   .BK6
.INQ     ANOP
&N.RB    DC    X'14071000',F'0',A(&N.OPT),2F'0'
&N.OPT   DS    0F
         AIF   ('&F' EQ '').NODDI
         DC    A(&N.DDP)           FILE ARGUMENT
         AGO   .COMINQ
.NODDI   AIF   ('&DA' EQ '').NODAI
         DC    A(&N.DAP)           DATASET ARGUMENT
         AGO   .COMINQ
.NODAI   AIF   ('&NUM' EQ '').END
         DC    A(&N.NUM)           FILE SEQUENCE NUMBER
.COMINQ  ANOP
&INC     SETA  1
&COUNT   SETA  N'&RETURN
.CYCINQ  ANOP
         AIF   ('&RETURN(&INC)' EQ 'DDNAME').DDINQ
         AIF   ('&RETURN(&INC)' EQ 'DA').DAINQ
         AIF   ('&RETURN(&INC)' EQ 'MEM').MEMINQ
         AIF   ('&RETURN(&INC)' EQ 'DS').DAINQ
         AIF   ('&RETURN(&INC)' EQ 'DSN').DAINQ
         AIF   ('&RETURN(&INC)' EQ 'DSNAME').DAINQ
         AIF   ('&RETURN(&INC)' EQ 'STATUS').STATINQ
         AIF   ('&RETURN(&INC)' EQ 'DISP').DISPINQ
         AIF   ('&RETURN(&INC)' EQ 'ATTR').ATTINQ
         AIF   ('&RETURN(&INC)' EQ 'TYPE').TYPEINQ
         AIF   ('&RETURN(&INC)' EQ 'DSORG').INQDSO
&TEMP    SETC  '&RETURN(&INC)'
         MNOTE 4,'INVALID RETURN SUBPARM &TEMP'
         AGO   .COMINQ2
         AGO   .END
.INQDSO  ANOP
         DC    A(&N.DSO)
         AGO   .COMINQ2
.DDINQ   ANOP
         DC    A(&N.DDI)
         AGO   .COMINQ2
.DAINQ   ANOP
         DC    A(&N.DAI)
         AGO   .COMINQ2
.ATTINQ  ANOP
         DC    A(&N.ATT)
         AGO   .COMINQ2
.MEMINQ  ANOP
         DC    A(&N.MEMI)
         AGO   .COMINQ2
.STATINQ ANOP
         DC   A(&N.STAT)        TYPE OF ALLOCATION
         AGO   .COMINQ2
.DISPINQ ANOP
         DC   A(&N.DISP)       DISPOSITION
         AGO   .COMINQ2
.TYPEINQ ANOP
         DC    A(&N.TYPE)          TYPE OF ALLOCATION
         AGO   .COMINQ2
.COMINQ2 ANOP
&INC     SETA  &INC+1
         AIF   (&INC GT &COUNT).ENDINQ1
         AGO   .CYCINQ
.ENDINQ1 ANOP
         DC    X'80',AL3(&N.LST)
         AIF   ('&F' EQ '').NODDI2
&N.DDP   DC    X'00010001'
&COUNT   SETA  K'&F
         DC    AL2(&COUNT),CL8'&F'
.NODDI2  AIF   ('&DA' EQ '').NODAI2
&N.DAP   DC    X'00020001'
&COUNT   SETA  K'&DA
         DC    AL2(&COUNT.),CL44'&DA'
         AGO   .GOINQ2
.NODAI2  AIF   ('&DS' EQ '').NODSI2
&N.DAP   DC    X'00020001'
&COUNT   SETA  K'&DS
         DC    AL2(&COUNT.),CL44'&DS'
         AGO   .GOINQ2
.NODSI2  AIF   ('&NUM' EQ '').NONUM2
&N.NUM   DC    X'000F0001'
         DC    X'0002',AL2(&NUM.)
.NONUM2  ANOP
.GOINQ2    ANOP
&INC     SETA  1
&COUNT   SETA  N'&RETURN
.CYCINQ2 ANOP
         AIF   ('&RETURN(&INC)' EQ 'DDNAME').DDINQ2
         AIF   ('&RETURN(&INC)' EQ 'DA').DAINQ2
         AIF   ('&RETURN(&INC)' EQ 'MEM').MEMINQ2
         AIF   ('&RETURN(&INC)' EQ 'DS').DAINQ2
         AIF   ('&RETURN(&INC)' EQ 'DSN').DAINQ2
         AIF   ('&RETURN(&INC)' EQ 'DSNAME').DAINQ2
         AIF   ('&RETURN(&INC)' EQ 'STATUS').STATI2
         AIF   ('&RETURN(&INC)' EQ 'DISP').DISPI2
         AIF   ('&RETURN(&INC)' EQ 'ATTR').ATTINQ2
         AIF   ('&RETURN(&INC)' EQ 'TYPE').TYPEI2
         AIF   ('&RETURN(&INC)' EQ 'DSORG').INCDSO2
         AGO   .COMINQ3
         AGO   .END
.ATTINQ2 ANOP
&N.ATT   DC    X'000C',X'0001',X'0001'
&N.ATTR  DC    X'0'
         AGO   .COMINQ3
.INCDSO2 ANOP
&N.DSO   DC    X'000A',X'0001',X'0002'
&N.DSOR  DC    XL2'0'
         AIF   (&DSORGEQ EQ 1).COMINQ3
&DSORGEQ SETB  1
DSOZERO  DC   X'0000'
DSORGPS  DC   X'4000'
DSORGPO  DC   X'0200'
DSORGDA  DC   X'2000'
DSORGIS  DC   X'8000'
DSORGVS  DC   X'0008'
DSORGPSU DC   X'4001'
DSORGPOU DC   X'0201'
DSORGDAU DC   X'2001'
DSORGISU DC   X'8001'
DSORGVSU DC   X'0009'
         AGO   .COMINQ3
.DDINQ2  ANOP
&N.DDI   DC    X'000400010008',CL8' '
         AGO   .COMINQ3
.DAINQ2  ANOP
&N.DAI   DC    X'00050001002C',CL44' '
         AGO   .COMINQ3
.MEMINQ2 ANOP
&N.MEMI  DC    X'000600010008',CL8' '
         AGO   .COMINQ3
.STATI2  ANOP
&N.STAT  DC    X'000700010001',X'00'
          AGO   .COMINQ3
.DISPI2  ANOP
&N.DISP  DC    X'000800010001',X'00'
          AGO   .COMINQ3
.TYPEI2  ANOP
&N.TYPE  DC    X'000E00010001',X'00'
TERM     EQU   X'40'
DUMMY    EQU   X'80'
         AGO   .COMINQ3
.COMINQ3 ANOP
&INC     SETA  &INC+1
         AIF   (&INC GT &COUNT).ENDINQ3
         AGO   .CYCINQ2
.ENDINQ3 ANOP
&N.LST   DC    X'000D00010001',X'00'   LAST INDICATOR
         AGO   .BK6
.DECONB  ANOP
.END     ANOP
         MEND
