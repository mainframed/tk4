         MACRO
&NAME    TMUEXITB
         GBLC  &PRINT
         GBLC  &ABENDAY
         PRINT GEN
.*
*
*        TMUEXITB MACRO  UCC-ONE (TMS)   11/27/84    M. H. HEMP
*
*        THIS UCC-1 (TMS) EXIT WILL ALLOW THE USER TO DEFINE THE
*        DISPOSITION OF HIS TAPE AT ABEND BY SPECIFYING EITHER
*        'KEEP' OR 'CATLG' AS THE CONDITIONAL, OR THIRD, POSITIONAL
*        PARAMETER OF THE 'DISP' KEYWORD PARAMETER.  IF THE PARAMETER
*        IS NOT SPECIFIED, NORMAL UCC-ONE DISPOSITION BASED UPON
*        THE 'ABENDAY' SPECIFICATION WILL OCCUR.
*
.*
         LR    R1,R5              CURRENT RB
FINDTCB  L     R1,28(0,R1)        PREVIOUS RB OR TCB
         CLC   256(4,R1),TCBTCBID Q. IS THIS OUR TCB
         BE    GETJSCB            A. EQUAL - YES
         B     FINDTCB            CONTINUE TO SCAN RB CHAIN
TCBTCBID DC    CL4'TCB '
GETJSCB  L     R1,180(0,R1)       JSCB ADDRESS
         L     R1,320(0,R1)       QDB DSAB ADDRESS
         L     R1,12(0,R1)        FIRST DSAB ADDRESS
SAVDSAB  LR    R0,R1              MAINTAIN DSAB ADDRESS
         L     R1,20(0,R1)        SIOT-16 ADDRESS
         L     R1,80(0,R1)        SIOT UCB POINTER
         CR    R8,R1              Q. DO UCB ADDRESSES AGREE
         BNE   NEXTDSAB           A. UNEQUAL - NO
         LR    R1,R0              RESTORE CURRENT DSAB POINTER
         L     R1,20(0,R1)        SIOT-16 ADDRESS
         L     R1,172(0,R1)       SIOT JFCB POINTER
         CLC   0(44,R1),JFCBDSNM  Q. DO DSNAMES AGREE
         BE    EUREKA             A. EQUAL - YES
NEXTDSAB LR    R1,R0              RESTORE CURRENT DSAB POINTER
         L     R1,4(0,R1)         GET NEXT DSAB IN THE CHAIN
         LTR   R1,R1              Q. END OF DSAB CHAIN
         BNZ   SAVDSAB            A. NOT ZERO - NO
DEFAULT  DS    0H
         MVI   SVCXTEMP,X'98'     POST YEAR
POSTABE  DS    0H
         MVC   SVCXTEMP+1(2),=PL2'&ABENDAY' POST DAY
         AIF   ('&ABENDAY' NE '0').ABEISOK
         ORG   POSTABE
         MVC   SVCXTEMP(3),DATE - POST TODAY'S DATE
.ABEISOK ANOP
         B     CONTINUE
EUREKA   LR    R1,R0              RESTORE CURRENT DSAB POINTER
         L     R1,20(0,R1)        SIOT-16 ADDRESS
         TM    108(R1),X'0A'      Q. IS COND DISP KEEP OR CATLG
         BNM   DEFAULT            A. NOT MIXED - NO
         BAL   R2,CALLSVCY        UPDATE THE TMC RECORD
         MVC   SVCXTEMP(3),TMEXPDT MOVE DATE FOR PREVIOUS VOLUMES
         LA    R1,TMPRVVOL        POINT TO PREVIOUS VOLUME
         B     VOLOOP
CONTINUE DS    0H
         PRINT &PRINT
         MEND
