         MACRO
         KTBTB02                                                       1
               &FCN,                                                   2
               &KEY,                                                   3
               &KVAL
.*
.**********************************************************************
.*
         GBLB  &KTBER
         GBLA  &KTBFN
         GBLA  &KTBV02(100),&KTBM02   KEYWORD VALUE , LAST ENTRY USED
         GBLC  &KTBK02(100)           KEYWORDS
.*
.**********************************************************************
.*
         LCLA  &KTBMAX,&X,&Y
.*
.**********************************************************************
.*
&KTBMAX  SETA  100                MAX TABLE SIZE
&KTBER   SETB  0                  CLEAR ERROR SWITCH
.*
&Y       SETA  &KTBM02
&X       SETA  1
.*
.**********************************************************************
.*
         AIF   ('&FCN' EQ 'CLR').CLR
         AIF   ('&FCN' EQ 'STO' OR '&FCN' EQ 'FND').START
         SPACE 3
* ************************
         MNOTE 4,'INVALID FUNCTION'
* ************************
         SPACE 3
&KTBER   SETB  1
         MEXIT
.*
.**********************************************************************
.*
.START   ANOP
         AIF   (&X GT &Y).NOFND1 - IS INPUT VALUE GREATER THAN NUMBER
.*                                 OF ENTRIES IN TABLE?
.*
.LOOP    ANOP -                                   SEE IF IN TABLE
         AIF   ('&KTBK02(&X)'  EQ '&KEY').FOUND - IS KEYWORD IN TABLE?
         AIF   (&X GE &Y).NOFND -                 IS THIS END OF TABLE?
&X       SETA  &X+1 -                             INCREMENT INDEX COUNT
         AGO   .LOOP -                            LOOP THRU TABLE.
.*
.**********************************************************************
.*
.FOUND   ANOP -                           FOUND PASS BACK INFO
         AIF   ('&FCN' NE 'FND').SETER1 - IS THIS A TABLE SEARCH?
&KTBFN   SETA  &KTBV02(&X) -              GENERATE A TABLE ENTRY NUMBER
         MEXIT
.*
.**********************************************************************
.*
.SETER1  ANOP
         SPACE 3
* *********************************************
         MNOTE  4,'DUPLICATE KEYWORD=&KEY FOUND IN TABLE'
* *********************************************
         SPACE 3
&KTBER   SETB  1
         MEXIT
.*
.**********************************************************************
.*
.NOFND   ANOP
&X       SETA  &X+1
.NOFND1  ANOP
         AIF   ('&FCN' EQ 'STO').STORE -        IS THIS A TABLE STORE?
         SPACE 3
* ***************************************
         MNOTE 4,'KEYWORD=&KEY NOT FOUND IN TABLE'
* ***************************************
         SPACE 3
&KTBER   SETB  1
         MEXIT
.*
.**********************************************************************
.*
.STORE   ANOP
         AIF   (&X LE &KTBMAX).STORE1 - HAVE WE EXCEEED SIZE OF TABLE?
         SPACE 3
* ***************************
         MNOTE 4,'TABLE SIZE EXCEEDED'
* ***************************
         SPACE 3
&KTBER   SETB  1 -                      SET ERROR CODE.
         MEXIT
.*
.**********************************************************************
.*
.STORE1          ANOP -          STORE IN TABLE
&KTBM02          SETA  &X
&KTBK02(&KTBM02) SETC  '&KEY'
&KTBV02(&KTBM02) SETA  &KVAL
&KTBFN           SETA  &KVAL
                 MEXIT
.*
.**********************************************************************
.*
.CLR         ANOP -               CLEAR TABLE
             AIF   (&X GT &Y).CLREND
&KTBK02(&X)  SETC  ''
&KTBV02(&X)  SETA  0
&X           SETA  &X+1
             AGO   .CLR
.*
.**********************************************************************
.*
.CLREND      ANOP
&KTBM02      SETA  0
.*
.**********************************************************************
.*
         MEND
