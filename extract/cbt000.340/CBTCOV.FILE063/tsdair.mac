         MACRO
&NAME    TSDAIR &DAPB,&ERR=
         LCLC  &REG
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   (N'&DAPB GE 2).GEN001
         AIF   ('&DAPB'(1,1) NE '(').GEN003
         ST    &DAPB(1),DAPLDAPB .  STORE DAPB PTR IN DAPL
         AGO   .GEN005
.GEN001  ANOP
         AIF   ('&DAPB(1)'(1,1) NE '(').GEN002
&REG     SETC  '&DAPB(1)'(2,K'&DAPB(1)-2)
         L     R0,0(0,&REG) .   LOAD PTR TO DAPB
         AGO   .GEN004
.GEN002  ANOP
         L     R0,&DAPB(1) . LOAD PTR TO DAPB
         AGO   .GEN004
.GEN003  ANOP
         LA    R0,&DAPB . LOAD PTR TO DAPB
.GEN004  ANOP
         ST    R0,DAPLDAPB .  STORE DAPB PTR IN DAPL
.GEN005  ANOP
         LA    R1,DAPL .      LOAD PTR TO DAPL
*        LINK  EP=IKJDAIR     LINK TO DAIR
         LINK  EP=IKJDAIR
         AIF   ('&ERR' EQ '').END
         LTR   R15,R15 .      TEST DAIR RETURN CODE
         BZ    IHB&SYSNDX.A .     IF 0, BYPASS DAIR ERROR ANALYSIS
         ST    R1,DEPLDAPL .  STORE DAPL PTR IN DEPL
         ST    R15,TSDERTCD . STORE DAIR ERROR RETURN CODE
         LA    R14,TSDERTCD . LOAD PTR TO TSDERTCD
         SR    R0,R0 .        INDICATE IKJEFF02 ENTRY POINT IS ZERO
         ST    R0,TSDE02PT .  STORE IN TSDE02PT
         LA    R15,TSDE02PT . LOAD PTR TO TSDE02PT
         LA    R0,1 .         LOAD DAIR ERROR CALLING CODE
         STH   R0,TSDECALL .  STORE IN TSDECALL
         LA    R0,TSDECALL .  LOAD PTR TO TSDECALL
         STM   R14,R0,DEPLRTCD .  STORE PTRS IN DEPL
         LA    R1,DEPL .      LOAD PTR TO DEPL
*        LINK  EP=IKJEFF18 .  LINK TO DAIR ERROR ANALYSIS ROUTINE
         LINK  EP=IKJEFF18
         AIF   ('&ERR'(1,1) EQ '(').GENBR
         B     &ERR .     GO TO ERROR RETURN
         AGO   .GENEND
.GENBR   ANOP
         BR    &ERR(1) .          GO TO ERROR RETURN
.GENEND  ANOP
IHB&SYSNDX.A DS 0H
.END     ANOP
         MEND
