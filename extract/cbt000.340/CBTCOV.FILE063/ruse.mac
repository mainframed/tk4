         MACRO
&NAME    RUSE  &WHERE,&W1,&W2
         GBLB  &LUSED
         GBLC  &USENM(50),&USEW(25),&USENM1(50),&USEW1(25)
         LCLA  &A,&B,&C,&D,&E,&F
         LCLC  &X1,&X2
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H .           DEFINE NAME
.NONAME  AIF   (N'&SYSLIST LE 10).OK1
         MNOTE 16,'OPERAND ERROR - TOO MANY OPERANDS'
.OPFIELD MNOTE *,'OPERAND FIELD FOLLOWS'
         MNOTE *,'&SYSLIST(1),&SYSLIST(2),&SYSLIST(3),&SYSLIST(4),&SYSL*
               IST(5),&SYSLIST(6),&SYSLIST(7),&SYSLIST(8)'
         AIF   (N'&SYSLIST LE 8).MEND
         MNOTE *,'- REMAINING OPERANDS NOT PRINTED -'
         MEXIT
.OK1     AIF   (N'&SYSLIST LT 2).ERR
&A       SETA  1
&C       SETA  N'&SYSLIST
.OKX     AIF   ('&SYSLIST(&A)' NE '').INCR
         MNOTE 16,'OPERAND ERROR - NULL OPERAND'
         AGO   .OPFIELD
.INCR    AIF   (&A EQ &C).GEN
&A       SETA  &A+1
         AGO   .OKX
.GEN     AIF   (&C NE 2).RECURSE
&A       SETA  1
.FNULL   AIF   ('&USEW(&A)' EQ '').HNULL
         AIF   ('&USEW(&A)' EQ '&W1').REPL
&A       SETA  &A+1
         AIF   (&A LE 25).FNULL
         MNOTE 16,'LUSE USER ERROR - TOO MANY LUSE WITHOUT LDROP'
         AGO   .OPFIELD
.REPL    ANOP
&D       SETA  (&A*2)-1
         AIF   ('&USEW1(&A)' EQ '').REPL1
         MNOTE *,'ADDRESSIBILTY LOST FOR &USENM1(&D)&USENM1(&D+1)'
.REPL1   ANOP
&USEW1(&A) SETC '&USEW(&A)'
&USENM1(&D) SETC '&USENM(&D)'
&USENM1(&D+1) SETC '&USENM(&D+1)'
.HNULL   ANOP
&D       SETA  (&A*2)-1
&E       SETA  K'&WHERE-8
&X1      SETC  '&WHERE'
         AIF   (&E LE 0).COMMON
         AIF   (&E LE 8).SETX2
&E       SETA  8
.SETX2   ANOP
&X2      SETC  '&WHERE'(9,&E)
.COMMON  ANOP
&USENM(&D)     SETC           '&X1'
&USENM(&D+1) SETC '&X2'
&USEW(&A) SETC '&W1'
&E       SETA  1
.MATCH1  AIF   (&E GE &D).MT2
         AIF   ('&USENM(&E)' NE '&USENM(&D)').NOMT1
         AIF   ('&USENM(&E+1)' NE '&USENM(&D+1)').NOMT1
&F       SETA  (&D+1)/2
         AIF   ('&USEW(&F)' EQ '&USEW1(&F)').NOMT1
         MNOTE 0,'&USENM(&D)&USENM(&D+1) PREVIOUSLY LUSED, HAS'
         MNOTE *,' ADDRESSABILITY OFF OF &USEW(&F)'
.NOMT1   ANOP
&E       SETA  &E+2
         AGO   .MATCH1
.MT2     ANOP
&E       SETA  1
.MATCH   AIF   (&E GE &A).GENUSE
         AIF   ('&USEW(&E)' EQ '&USEW(&A)').MN0
&E       SETA  &E+1
         AGO   .MATCH
.MN0     MNOTE 0,'** WARNING **  REGISTER '
         MNOTE *,'&USEW(&E) PREVIOUSLY LUSED AS BASE FOR'
&D       SETA  (&E*2)-1
         MNOTE *,'&USENM(&D)&USENM(&D+1) - NEW USING CREATED'
         LDROP &W1            DELETE LUSE
&A       SETA  &A-1           ADJUST THE TOP OF USEW TABLE
&E       SETA  &E+1
         AGO   .MATCH
.GENUSE  USING &WHERE,&W1 .   GEN USING STMT
         AIF   (&LUSED).MEND
*        --> LUSE, LDROP, AND INUSE MACROS ESTABLISHED <-
&LUSED   SETB  1
         SPACE 1
         MEXIT
.RECURSE ANOP
&B       SETA  2
.RCU     AIF   (&B GT &C).MEND
         LUSE  &WHERE&X1,&SYSLIST(&B)
&B       SETA  &B+1
&E       SETA  &E+4096
&X1      SETC  '+'.'&E'
         AGO   .RCU
.ERR     MNOTE 16,'OPERAND ERROR - AT LEAST 2 OPERANDS REQUIRED'
         AGO   .OPFIELD
.MEND    MEND
