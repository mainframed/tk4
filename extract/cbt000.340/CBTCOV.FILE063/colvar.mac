         MACRO
&NFS     COLVAR  &VAR,&LEN,&NAME
.********************************************************************
.* THIS MACRO IS USED WITH THE COLUP PROGRAMS TO GENERATE TWO LISTS *
.* OF CONSTANTS. ONE LIST IS USED AS LAYOUT OF THE VSAM RECORDS     *
.* (THIS IS VSAMAREA IN COLSAMP). THE 2ND LIST IS USED TO INIT'LIZE *
.* THE ISPF VARIABLE NAMES FOR TRANSFER OF DATA BETWEEN THE PANELS  *
.* AND THE PROGRAM (THIS IS THE VARLIST CSECT).                     *
.********************************************************************
         GBLA  &VEOFF
         GBLC  &COLSECT
         LCLC  &COMMENT
&COMMENT SETC  '                 OFFSET = &VEOFF'
         AIF   ('&NAME' EQ '').NONAME
&NAME    CSECT
         AGO   .NAMEOK
.NONAME  ANOP
&COLSECT CSECT
.NAMEOK  ANOP
         AIF   ('&VAR' EQ '*MIN').END1
         AIF   ('&VAR' EQ '*MAX').END2
P&VAR    DC    CL(&LEN)' '&COMMENT
&VEOFF   SETA  &VEOFF+&LEN
VARLIST  CSECT
&NFS     DC    F'&LEN',A(P&VAR),CL10'(&VAR)',CL2' '
         MEXIT
.END1    ANOP
MINRECD  EQU   *&COMMENT
VARLIST  CSECT
         MEXIT
.END2    ANOP
MAXRECD  EQU   *&COMMENT
VARLIST  CSECT
         MEXIT
         MEND
