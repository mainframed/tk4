         MACRO
         TSCLEAR &LEN=,&REG=,&TYP=
         LR    &REG,R1 .              SAVE DYNAMIC CORE ADDRESS
         AIF   ('&TYP' EQ '').G1
         MVI   0(R1),TSOBLANK .     SETUP TO PROPAGATE BLANK
         BCTR  &LEN,R0 .         DECREMENT LENGTH REGISTER
.G1      ANOP
         B     IHB&SYSNDX.D .         GO CLEAR CORE
         SPACE
IHB&SYSNDX.C DS 0H
         AIF   ('&TYP' NE '').G2
         XC    0(256,R1),0(R1) .   CLEAR 256 BYTES
         AGO   .G3
.G2      ANOP
         MVC   1(256,R1),0(R1) .   CLEAR 256 BYTES
.G3      ANOP
         LA    R1,256(R1) .        INCREMENT DYNAMIC AREA POINTER
         SPACE
IHB&SYSNDX.D DS 0H
         SH    &LEN,*-2 .        OVER 256 BYTES TO CLEAR?
         BP    IHB&SYSNDX.C .           YES - GO CLEAR 256 BYTES
         AH    &LEN,TSOH255 .    ADJUST FOR LAST CLEAR
         AIF   ('&TYP' NE '').G4
         EX    &LEN,IHB&SYSNDX.E . CLEAR REMAINING BYTES
IHB&SYSNDX.E XC 0(0,R1),0(R1)      EXECUTED
         MEXIT
.G4      ANOP
         EX    &LEN,IHB&SYSNDX.F .      CLEAR REMAINING BYTES
IHB&SYSNDX.F   MVC 1(0,R1),0(R1) . EXECUTED
         MEND
