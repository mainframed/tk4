         MACRO
&PROGRAM $IPMODS &TYPE=DSECT
.*I78276A ERROR IN 'USERPGM' KEYWORD FUNCTION                        CS
         GBLB  &$IPERR,&$IP1ST,&$IPTYPE
         GBLC  &$IPPGMS(200),&$IPDATE
         LCLA  &A1
         AIF   (&$IP1ST).NOT1ST
&$IP1ST  SETB  1
         TITLE '&$IPDATE.NETPARS REPORT DIRECTORY DSECT'
$IPMODS  DSECT
$IPPGM   DS    CL8                 REPORT PROCESSOR NAME
$IPDDNAM DS    CL8                 DDNAME
$IPSTDAT DS    A                   START DATE
$IPSTTIM DS    A                   START TIME
$IPSPDAT DS    A                   STOP DATE
$IPSPTIM DS    A                   STOP TIME
$IPSTMIC DS    XL8                 START TIME (MICS)
$IPSPMIC DS    XL8                 STOP TIME (MICS)
$IPRESRV DS    H                   FOR ALIGNMENT
$IPMODF1 DS    X                   MODULE FLAG 1
$IPMODF2 DS    X                   MODULE FLAG 2
$IPPARM1 DS    A                   PARAMETER BLOCK ADDRESS
$IPPARM2 DS    A                   PARAMETER BLOCK ADDRESS
$IPENTSZ EQU   *-$IPMODS           SIZE OF RECORD PROCSEEOR DIR ENTRY
&$IPTYPE SETB  ('&TYPE' EQ 'CSECT')
         AIF   (NOT &$IPTYPE).MEND
         TITLE '&$IPDATE.IMSPARS RECORD PROCESSOR DIRECTORY'
$IPTABLE CSECT
         AGO   .IS1ST
.NOT1ST  AIF   ((&$IPERR)OR(NOT &$IPTYPE)).MEND
         ORG   *-1                OVERWRITE DIRECTORY END INDICATOR
.IS1ST   AIF   ('&PROGRAM' NE '').PGMOK
        MNOTE 0,'RECORD PROCESSOR NAME NOT SPECIFIED - WILL BE SKIPPED'
         MEXIT
.PGMOK   ANOP
.LOOP05  ANOP
&A1      SETA  &A1+1
         AIF   ('&$IPPGMS(&A1)' EQ '').FREEFND
.*       AIF   ('&$IPPGMS(&A1)' EQ '&PROGRAM').DUPREP                CS
         AIF   (&A1 LT 200).LOOP05
         MNOTE 10,'TABLE OVERRUN - INCREASE SIZE OF $IPPGMS MATRIX'
&$IPERR  SETB  1
         MEXIT
.DUPREP  MNOTE 0,'RECORD PROCESSOR ALREADY DEFINED - WILL BE SKIPPED'
         MEXIT
.FREEFND ANOP
&$IPPGMS(&A1) SETC '&PROGRAM'
         SPACE 3
******** START OF ENTRY ******** START OF ENTRY ********
         DS    0A
         DC    CL8'&PROGRAM'           RECORD PROCESSOR NAME
         DC    CL8' '                  REPORT DDNAME
         DC    4A(0)                   REPORT START/STOP DATE AND TIME
         DC    2XL8'00'                REPORT START/STOP IN REL. MICS.
         DC    H'0'
         DC    2AL1(0)                 FLAG BYTES
         DC    2A(0)                   PARAMETER POINTER WORDS
         DC    X'00'                   END OF DIRECTORY INDICATOR
.MEND    MEND
