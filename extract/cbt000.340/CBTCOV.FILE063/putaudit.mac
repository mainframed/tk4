         MACRO ,                                                  40000
&NAME    PUTAUDIT &IOAREA,&ERRCODE
.*
         AIF   ('&IOAREA' EQ '' OR '&ERRCODE' EQ '').MNOTE1
.*
.PUT     ANOP
&NAME    DS    0H -               PUTAUDIT MACRO
         XC    TMSPARM,TMSPARM -  CLEAR PARAMETER LIST
         AIF   ('&IOAREA'(1,1) EQ '(').PUT01
         LA    R1,&IOAREA -       POINT TO AUDIT RECORD
         ST    R1,TMIOADDR-1 -    POST IOAREA ADDRESS
         AGO   .PUT02
.PUT01   ANOP
         ST    &IOAREA(1),TMIOADDR-1 - POST IOAREA ADDRESS
.PUT02   ANOP
         MVI   TMOPCODE,32 -      POST OP-CODE
         AIF   ('&ERRCODE' EQ 'BI').PUT03
         AIF   ('&ERRCODE' EQ 'IC').PUT04
         AIF   ('&ERRCODE' EQ 'PWM').PUT05                        40095
         AIF   ('&ERRCODE' EQ 'OIT').PUT07                        40095
         AIF   ('&ERRCODE' EQ 'CTL').PUT08                        40128
         AGO   .MNOTE2
.PUT03   ANOP
         MVC   TMRELBLK+1(3),=C'002' - POST RECORD TYPE
         AGO   .PUT10
.PUT04   ANOP
         MVC   TMRELBLK+1(3),=C'SCR' - POST RECORD TYPE
         AGO   .PUT10
.PUT05   ANOP                                                     40095
         MVC   TMRELBLK+1(3),=C'012' - POST RECORD TYPE           40095
         AGO   .PUT10                                             40095
.PUT07   ANOP                                                     40095
         MVC   TMRELBLK+1(3),=C'007' - POST RECORD TYPE           40095
         AGO   .PUT10                                             40095
.PUT08   ANOP                                                     40128
         MVC   TMRELBLK+1(3),=C'008' - POST RECORD TYPE           40128
         AGO   .PUT10                                             40128
.PUT10   ANOP
         MVI   TMOPBITS,X'03' -   FLAG RECORD TYPE AS 'EXCEPTION'
         L     R15,TMSVCRTN -     GET ENTRY POINT ADDRESS
         LA    R1,TMSPARM -       POINT TO PARAMETER LIST
         BALR  R14,R15 -          WRITE AUDIT RECORD
         MEXIT
.*
.MNOTE1  MNOTE 8,'PUTAUDIT MACRO MISSING PARAMETERS'
         MEXIT
.MNOTE2  MNOTE 8,'INVALID ERROR CODE'
         MEXIT
.*
         MEND
