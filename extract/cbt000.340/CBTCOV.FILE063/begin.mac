         MACRO
&NAME    BEGIN &SAVE=,&REGS=YES,&ID=*,&TYPE=CSECT,&SAVEID=YES
         GBLC  &R
         LCLA  &SALEN
         LCLC  &C,&D,&N
&D       SETC  '&SYSNDX'
&C       SETC  'IHB&D'
         AIF   ('&NAME' NE '').OK
         MNOTE 4,'NAME OMITTED, &C.A USED'
&N       SETC  '&C'.'A'
         AGO   .SETLEN
.OK      ANOP
&N       SETC  '&NAME'
.SETLEN  ANOP
&SALEN   SETA  80
         AIF   ('&SAVEID' EQ 'YES').SAX
&SALEN   SETA  72
.SAX     AIF   ('&TYPE' EQ 'CSECT').CS
         AIF   ('&TYPE' EQ 'ENTRY').ENTRY
         AIF   ('&TYPE' EQ 'SUBRTN').SUB
         MNOTE 8,'TYPE=&TYPE UNKN... CSECT ASSUMED'
.CS      ANOP
&N       CSECT
         AGO   .COM
.ENTRY   ENTRY &N
.SUB     ANOP
&N       DS    0H .                     DEFINE ENTRY POINT
.COM     AIF   (&SALEN LT 80).NIDX
         MNOTE *,'REGISTER SAVE AREA ID IS ''&D'''
.NIDX    SAVE (14,12),T,&ID
         AIF   ('&REGS' NE 'YES').A1
         AIF   ('&R' EQ 'R').A1
&R       SETC  'R'
         REGS
         EJECT
.A1      LR    &R.12,15 .               ESTABLISH BASE
         LUSE  &N,&R.12 .               TELL ASSEMBLER
         AIF   ('&SAVE' NE '*').CNOP
         LR    &R.2,&R.1 .              SAVE PARM PTR
         GETMAIN R,LV=&SALEN .              DO IT
         LR    &R.15,&R.1 .             SWAP REGS
         LR    &R.1,&R.2 .              AROUND
         LR    &R.2,&R.15 .             BOUGHT CORE IN 2
         AIF   (&SALEN LT 80).CXA
         MVC   72(8,&R.2),*+10 .        LABEL
         B     &C .                     AROUND IT
         AGO   .CX
.CNOP    CNOP  0,4 .                    ALIGN PROPERLY
         BAL   &R.2,&C .                AROUND CONSTANTS
&SAVE    DC    18XL4'00' .              DEFINE SAVE AREA
         AIF   (&SALEN LT 80).CXA
.CX      DC    CL8'SAVE&D' .            DEFINE NAME ID
.CXA     SPACE 1
&C       ST    &R.13,4(,&R.2) .         CHAIN
         ST    &R.2,8(,&R.13) .         SAVE
         LR    &R.13,&R.2 .             AREAS
         INUSE
         SPACE 1
         MEND
