         MACRO
         CPXTAB &T=,              REQUEST TYPE (REQUIRED)              +
               &A=,               ACCOUNTING INFO (OPTIONAL)           +
               &B=,               BUSINESS ELEMENT NUMBER (OPTIONAL)   +
               &D=                BUSINESS ELEMENT DESCRIPTION (OPT.)
.*-------------------------------------------------------------------
.*       GLOBAL AND LOCAL VARIABLES
.*-------------------------------------------------------------------
         GBLC  &F
.*-------------------------------------------------------------------
.*       TYPE DISPATCHER
.*-------------------------------------------------------------------
         AIF   ('&T' EQ 'I').FIRST
         AIF   ('&T' EQ 'E').ENTRY
         AIF   ('&T' EQ 'F').LAST
         AIF   ('&T' EQ 'D').MAP
         AGO   .NOT
.*-------------------------------------------------------------------
.*       T = I
.*-------------------------------------------------------------------
.FIRST   ANOP
         AIF   ('&F' EQ 'E' OR '&F' EQ 'F').SEQERR
&F       SETC  'I'
CPXCPURT CSECT
         DC    A(ENTRY+LENTH)     ADDRESS OF FIRST REAL TABLE ENTRY
         DC    A(LENTH)           LENGTH OF ONE TABLE ENTRY
         DC    A(LAST1)           LAST ENTRY IN THE TABLE
ENTRY    DC    CL9' ',PL2'0',CL30' ' DUMMY ENTRY TO INIT. VARIABLES
LENTH    EQU   *-ENTRY
         AGO   .GOODEND
.*-------------------------------------------------------------------
.*       T = E
.*-------------------------------------------------------------------
.ENTRY   ANOP
         AIF   ('&F' NE 'I' AND '&F' NE 'E').SEQERR
&F       SETC  'E'
         AIF   ('&A' EQ '').NOACCT
         AIF   ('&B' EQ '').NOBE
         AIF   ('&D' EQ '').NODESC
         DC    CL9'&A'
         DC    PL2'&B'
         DC    CL30&D
         AGO   .GOODEND
.*-------------------------------------------------------------------
.*       T = F
.*-------------------------------------------------------------------
.LAST    ANOP
         AIF   ('&F' NE 'E').SEQERR
&F       SETC  'F'
LAST1    EQU   *-LENTH
         END   CPXCPURT
         AGO   .GOODEND
.*-------------------------------------------------------------------
.*       T = D
.*-------------------------------------------------------------------
.MAP     ANOP
CPXDSECT DSECT                    'BETAB' TABLE HEADER
CPXFIRST DS    A                  ADDRESS OF FIRST ENTRY
CPXLENTH DS    A                  LENGTH OF ONE TABLE ENTRY
CPXLAST1 DS    A                  ADDRESS OF LAST ENTRY
*
CPXENTRY DSECT                    TABLE ENTRY MAPPING DSECT
CPXMASK  DS    CL9                ACCOUNT FIELD MASK
CPXBENO  DS    PL2                BUSINESS ELEMENT NUMBER
CPXDESC  DS    CL30               BUSINESS ELEMENT DESCRIPTION
         AGO   .GOODEND
.*-------------------------------------------------------------------
.*       MNOTES
.*-------------------------------------------------------------------
.SEQERR  MNOTE 8,'*** TYPE SEQUENCE NOT I, E, E..., F ***'
         MEXIT
.NOT     MNOTE 8,'*** T IS NOT I, E, F, OR D ***'
         MEXIT
.NOBE    MNOTE 8,'*** NO B SPECIFIED FOR T=E ***'
         MEXIT
.NOACCT  MNOTE 8,'*** NO A SPECIFIED FOR T=E ***'
         MEXIT
.NODESC  MNOTE 8,'*** NO D SPECIFIED FOR T=E ***'
         MEXIT
.GOODEND ANOP
         MEND
