         MACRO
&SOJNAME TMSSOJ &BASEREG,&NAME=,&SAVE=YES,&SAVAREA=YES,                X
               &LABEL=,&SSI=,&EQUS=NO
         LCLC  &REGS(5)
         LCLA  &X,&Y,&L,&N
         LCLC  &GENAME,&SOJLBL
         LCLC  &LBL               SAVEAREA LABEL
*        TMSSOJ MACRO  UCC-ONE  (TMS)   05-31-79  CHG-LVL  4.5    40223
&LBL     SETC  '&LABEL'           SAVEAREA LABEL
         AIF   ('&LBL' NE '').LBLGEN  SPECIFIED - YES
&LBL     SETC  'SAVEAREA'         POST DEFAULT SAVEAREA LABEL
.LBLGEN  ANOP
&SOJLBL  SETC  '&SOJNAME'
         AIF   ('&SOJLBL' NE '').BASEREG
&SOJLBL  SETC  'SOJ'.'&SYSNDX'
.BASEREG ANOP
&L       SETA  K'&BASEREG-2       LENGTH OF &BASEREG ( + ,'S - ()'S)
         AIF   (&L LE 0 OR &L GT 40).ERR01
         AIF   ('&BASEREG'(1,1) NE '(').ERR02
&N       SETA  8
&Y       SETA  2
.REGSLP1 ANOP
&X       SETA  &X+1
         AIF   (&L GE 8).REGLNOK
&N       SETA  &L
.REGLNOK ANOP
&REGS(&X) SETC '&BASEREG'(&Y,&N)
&Y       SETA  &Y+&N
&L       SETA  &L-8
         AIF   (&L GE 0).REGSLP1
.*
&GENAME  SETC  '&NAME'
         AIF   ('&NAME' NE '').CALNAME
         AIF   ('&SYSECT' EQ '').NOID
&GENAME  SETC  '&SYSECT'
         AGO   .CALNAME
.*
.NOID    ANOP
&GENAME  SETC  '********'
.CALNAME ANOP
.*
         AIF   ('&SAVAREA' EQ 'YES').ARNDSVE
&SOJLBL  B     24(,R15) -
         AGO   .GENREGS
.*
.ARNDSVE ANOP
         USING &SOJLBL,R15
&SOJLBL  B     &LBL+72
         DROP  R15
.GENREGS ANOP
         AIF   ('&EQUS' EQ 'NO').GENAME
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
*
.GENAME  ANOP
         DC    X'14'                                              40223
         DC    CL8'&GENAME'
.*
.*       SSI INFO
&N       SETA  K'&SSI
         AIF   (&N NE 8).PADSSI
&GENAME  SETC  '&SSI'
         AGO   .GENSSI
.*
.PADSSI  ANOP
         AIF   (&N GT 8).ERR03
&GENAME  SETC  '&SSI'.'00000000'(1,8-&N)
.GENSSI  ANOP
         DC    C'  '                                              40223
         DC    CL8'&GENAME' -       SSI INFO
         DC    C' '                                               40223
.*
         AIF   ('&SAVAREA' NE 'YES').NOSAVEA
&LBL     DC    18A(0) -           REGISTER SAVEAREA
.NOSAVEA ANOP
*
.*
         AIF   ('&SAVE' EQ 'NO').NOSTM
         AIF   ('&SAVE'(1,1) NE '(' AND '&SAVE' NE 'YES').REGST
         AIF   ('&SAVE'(1,1) NE '(').REGSTM
         AIF   (T'&SAVE(2) EQ 'O').REGST -
    STM  &SAVE(1),&SAVE(2),12+4*(&SAVE(1)+2-(&SAVE(1)+2)/16*16)(R13)
         AGO   .CHAIN0
.REGST   ANOP
         ST    &SAVE(1),12+4*(&SAVE(1)+2-(&SAVE(1)+2)/16*16)(R13)
         AGO   .CHAIN0
.REGSTM  ANOP
         STM   R14,R12,12(R13) -  SAVE CALLING PROGRAM'S REGISTERS
.*
.CHAIN0  ANOP
.NOSTM   ANOP
         LR    &BASEREG(1),R15 -       LOAD BASE REGISTER         40223
.RESTBAS ANOP                                                     40223
&L       SETA  N'&BASEREG -         NUMBER OF BASE REGS SPECIFIED
&N       SETA  1 -                  INITIATE REG SUBLIST POINTER
.BASELOP ANOP                                                     40223
&N       SETA  &N+1                                               40223
         AIF   (&N GT &L).SETBASE                                 40223
         LA    &BASEREG(&N),4095(,&BASEREG(&N-1)) -     LOAD      40223
         LA    &BASEREG(&N),1(,&BASEREG(&N)) - MULTIPLE BASE REGS 40223
         AGO   .BASELOP                                           40223
.SETBASE USING  &SOJLBL,&REGS(1)&REGS(2)&REGS(3)&REGS(4)&REGS(5)  40223
         AIF   ('&SAVAREA' NE 'YES' AND '&LABEL' NE '').CHAIN2    40223
         AIF   ('&SAVAREA' NE 'YES').NOMORE                       40223
.CHAIN2  ANOP                                                     40223
         ST    R15,&LBL -           SAVE ENTRY POINTER
         AIF   ('&SAVE' EQ 'NO').NOCHAIN                          40223
         ST    R13,&LBL+4 -         CHAIN
         LA    R15,&LBL -           REGISTER
         ST    R15,8(,R13) -            SAVE                      40223
         LR    R13,R15 -                AREAS                     40223
         AGO   .NOMORE                                            40223
.*                                                                40223
.NOCHAIN ANOP                                                     40223
         LA    R13,&LBL -           POINT TO REG SAVEAREA
         AGO   .NOMORE                                            40223
.*
.ERR01   MNOTE 'BASE REGISTERS INCORRECTLY SPECIFIED'
         MEXIT
.ERR02   MNOTE 'BASE REGS MUST BE ENCLOSED IN PARENTHESES'
         MEXIT
.ERR03   MNOTE 'SSI INFORMATION GREATER THAN 8 CHARACTERS'
&GENAME  SETC  '&SSI'(1,8)
         AGO   .GENSSI
.*
.NOMORE  ANOP
         SPACE
         MEXIT
.*
         MEND
