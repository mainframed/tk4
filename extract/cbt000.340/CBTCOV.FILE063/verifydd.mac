         MACRO ,
&NAME    VERIFYDD &DDNAME=
.*
         LCLC  &NDX
&NDX     SETC  '&SYSNDX'
*        VERIFYDD MACRO           JUNE 18,1976             UCC-ONE
         AIF   ('&DDNAME' EQ '').MNOTE1
.*
&NAME    DS    0H -               **  VERIFYDD MACRO  **
         L     R1,16 -            GET CVT ADDRESS
         L     R1,0(,R1) -        GET TCB POINTER
         L     R1,4(,R1) -        GET CURRENT TCB ADDRESS
         L     R1,12(,R1) -       GET TIOT ADDRESS
         LA    R1,24(,R1) -       POINT TO DD ENTRY'S
.*
         AIF   ('&DDNAME'(1,1) EQ '(').VER#01
         LA    R14,&DDNAME -      POINT TO DDNAME
         AGO   .VER#02
.VER#01  ANOP  ,
         LR    R14,&DDNAME(1) -   POINT TO DDNAME
.VER#02  ANOP  ,
.*
         SR    R0,R0 -            CLEAR REGISTER ZERO
V#01&NDX DS    0H
         IC    R0,0(,R1) -        GET LENGTH OF DD ENTRY
         CLC   0(8,R14),4(R1) -   Q. IS THIS THE DDNAME
         BXLE  R1,R0,V#02&NDX -      BRANCH IF LAST ENTRY
         BNE   V#01&NDX -         A. UNEQUAL - NO
         B     V#03&NDX -         A. DDNAME FOUND IN TIOT
V#02&NDX DS    0H
         LR    R0,R1 -            INDICATE DDNAME WAS NOT FOUND
V#03&NDX DS    0H
         SR    R1,R0 -            SET TIOT ADDRESS / ZERO IF NOT FOUND
.*
         MEXIT
.*
.MNOTE1  MNOTE 8,'INVALID OR MISSING DDNAME ENTRY'
         MEND
