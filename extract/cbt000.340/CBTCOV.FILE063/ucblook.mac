         MACRO
&NAME    UCBLOOK &ADDR,&PTR,&WRK,&INVALID=
.* FIND A UCB GIVEN THE DEVICE ADDRESS
.*    INVALID DEVICE ADDRESSES WILL SOMETIMES (BUT NOT OFTEN)
.*    RETURN AN ADDRESS FOR THE WRONG UCB
.* &ADDR IS THE REGISTER CONTAINING THE DEVICE ADDRESS
.*    THIS MAY BE AN ALTERNATE PATH
.*    ONLY THE LOW-ORDER 12 BITS ARE USED, ALL OTHERS ARE DROPPED
.* &PTR IS THE REGISTER TO CONTAIN THE UCB ADDRESS WHEN COMPLETE
.*    MAY NOT BE GR0
.* &WRK IS A LIST OF TWO REGISTER PAIRS (FIRST REGISTER SPECIFIED)
.*    THE FIRST PAIR MUST BE AN EVEN-ODD PAIR
.*    THE SECOND PAIR MAY BE EVEN-ODD OR ODD-EVEN
.*    NONE OF THE REGISTERS (SPECIFIED OR IMPLIED) MAY BE GR0
.* &INVALID IS THE BRANCH POINT TO BE TAKEN IF THE ADDRESS IS BAD
.*    IF NOT SPECIFIED, NO VALIDITY CHECKING IS DONE
.*
.* WRITTEN BY DOUG MERCER - 3/12/76
.*
&NAME    LR    &WRK(1)+1,&ADDR
         L     &WRK(2),16              CVT POINTER
         LM    &WRK(2),&WRK(2)+1,36(&WRK(2)) IECILK1 & IECILK2
         SLL   &WRK(1)+1,20            DROP ALL BUT LOW-ORDER 12 BITS
         SR    &WRK(1),&WRK(1)
         SLDL  &WRK(1),4               SEPARATE CHANNEL
         SR    &PTR,&PTR
         IC    &PTR,0(&WRK(1),&WRK(2)) CHANNEL OFFSET
         AR    &PTR,&PTR               * 2
         AIF   ('&INVALID' EQ '').P7
         BZ    &INVALID                CHANNEL IS INVALID
.P7      SR    &WRK(1),&WRK(1)
         SLDL  &WRK(1),4               SEPARATE CONTROL UNIT
         AR    &WRK(1),&WRK(1)         * 2
         AR    &PTR,&WRK(1)
         SRL   &WRK(1)+1,27            DOUBLE & RIGHT JUSTIFY UNIT
         LH    &PTR,0(&PTR,&WRK(2))    CHANNEL + CONTROL UNIT OFFSET
         AR    &PTR,&PTR               * 2
         AR    &PTR,&WRK(1)+1          OFFSET INTO UCB LOOKUP TBL
         LH    &PTR,0(&PTR,&WRK(2)+1)
         N     &PTR,=XL4'0000FFFF'     MAY BE 32K-64K
         AIF   ('&INVALID' EQ '').P14
         BZ    &INVALID
         CLM   &ADDR,1,5(&PTR)         SEE IF LOW-ORDER 8 BITS MATCH
         BNE   &INVALID                NO - ERROR
.P14     SPACE
         MEND
