         MACRO
&NAME    CHECKDD &FILES,&ABEND
         LCLA  &N,&X
         LCLC  &ABENDBR,&REG
*        CHECKDD MACRO   UCC-ONE (TMS)      04-01-74    CHG-LVL 4.0
         AIF   (N'&FILES LE 0).ERR01
&ABENDBR SETC  '&ABEND'
         AIF   ('&ABENDBR' NE '').ABENDOK
&ABENDBR SETC  'ABEND002'
.ABENDOK ANOP
.*
&N       SETA  N'&FILES
&X       SETA  1
.*
.TSTLOOP ANOP
         AIF   ('&FILES(&X)'(1,1) EQ '(').REG
&NAME    LA    R15,&FILES(&X) -        POINT TO DCB
         AGO   .REST
.*
.REG     ANOP
&REG     SETC  '&FILES(&X)'(2,K'&FILES(&X)-2)
&NAME    LR    R15,&REG -          POINT TO DCB
.REST    ANOP
         TM    48(R15),X'10' -    Q. DD CARD PRESENT
         BZ    &ABENDBR -         A. ZERO - NO
&X       SETA  &X+1
         AIF   (&X LE &N).TSTLOOP
         MEXIT
.ERR01   MNOTE 'NO FILES SPECIFIED'
         MEXIT
         MEND
