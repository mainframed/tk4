         MACRO
&LABEL   FCBGEN &LPINCH=,&IMGLEN=,&INDEX=,&OFFSET=,                    $
               &OPSYS=,&DEFAULT=,&VERMSG=,                             $
               &LINE=,&CHAN=
.*.
.* FCBGEN - 08/14/81 GIBRALTAR SAVINGS
.*
.*   (BE SURE THAT THE ABOVE COMMENT MATCHES THE COMMENT AFTER .F01)
.*
.* VALID EXAMPLES:
.*
.*    (OS/VS)
.*
.*    FCB2SP01 FCBGEN LPINCH=8,IMGLEN=68,INDEX=(R,10),OPSYS=OS,       $
.*                  DEFAULT=YES
.*             FCBGEN LINE=1,CHAN=1
.*             FCBGEN LINE=60,CHAN=12
.*             FCBGEN LINE=64,CHAN=9
.*             FCBGEN
.*             END
.*
.*    (DOS/VS)
.*
.*    FCB2SP02 FCBGEN LPINCH=6,IMGLEN=66,OPSYS=DOS,                   $
.*                  VERMSG='FCB2SP02 -  6 LPI, 11 INCHES LONG'
.*             FCBGEN LINE=1,CHAN=1
.*             FCBGEN LINE=60,CHAN=12
.*             FCBGEN LINE=64,CHAN=9
.*             FCBGEN
.*             END
.*
.*
.* FORMS OF THE MACRO:
.*
.**********************************************************************
.*
.*IMGNAME  FCBGEN  LPINCH=,IMGLEN=,INDEX=,OFFSET=,
.*             OPSYS=,DEFAULT=,VERMSG=
.*
.*   FUNCTION:  START THE IMAGE DEFINITION AND SET SOME IMPORTANT
.*              CHARACTERISTICS OF THE IMAGE.
.*
.*   REQUIRED:    IMGNAME
.*    (DOS ONLY): VERMSG=
.*
.*   DEFAULTS:   IMGLEN=66
.*               LPINCH=6
.*               OFFSET=0
.*               OPSYS=OS
.*               INDEX= (NONE)
.*    (OS ONLY): DEFAULT=NO
.*
.*   ACCEPTABLE VALUES:  IMGLEN - 2 TO 180
.*                       LPINCH - 6 OR 8
.*                       OFFSET - 0 TO IMGLEN
.*                       OPSYS - OS OR DOS
.*                       IMGNAME - 5 OR MORE CHAR BEGINNING 'FCB2'
.*                       INDEX - (R,1) TO (R,31) OR (L,1) TO (L,31)
.*            (OS ONLY): DEFAULT - YES OR NO
.*                       VERMSG MAY NOT BE SPECIFIED
.*           (DOS ONLY): DEFAULT MAY NOT BE SPECIFIED
.*                       VERMSG - UP TO 80 CHARACTERS (PLUS QUOTES)
.*
.*   NOTES:  1. PRINT POSITION INDEXING IS A 3211 FEATURE ONLY - IT
.*              WILL BE IGNORED IF ATTEMPTED ON OTHER FCB PRINTERS.
.*           2. ONLY A 3211 WILL ALLOW A CHANNEL SPECIFICATION IN THE
.*              LAST POSITION OF THE FCB IMAGE.  OTHER PRINTERS WILL
.*              RETURN ERROR STATUS IF SUCH AN FCB IS LOADED.
.*
.*   OPERANDS:
.*      DEFAULT - (OS ONLY) INDICATE WHETHER JES2 SHOULD USE THIS
.*                FCB IMAGE TO PRINT OUTPUT THAT DOESN'T SPECIFY
.*                ANY PARTICULAR FCB.
.*      IMGLEN  - THE LENGTH OF THE FCB IMAGE IN LINES (USUALLY THE
.*                LENGTH OF ONE PAGE).
.*      LPINCH  - THE NUMBER OF LINES THAT WILL BE PRINTED PER INCH.
.*      OFFSET  - A NUMBER THAT WILL BE SUBTRACTED FROM EVERY 'LINE'
.*                SPECIFICATION BELOW.  THIS MAKES IT EASIER TO COPY
.*                A CARRIAGE TAPE OR ANOTHER FCB AND MOVE CHANNEL
.*                ONE TO LINE ONE TO SATISFY THE JES2 RESTRICTION.
.*      OPSYS   - SPECIFY THE TYPE OF OPERATING SYSTEM WHICH WILL USE
.*                THIS FCB IMAGE.
.*      IMGNAME - THE NAME BY WHICH THE FCB IMAGE LOAD MODULE WILL BE
.*                KNOWN.
.*      INDEX   - THE 3211 HARDWARE PRINT LINE INDEXING SPECIFICATION.
.*                'R' - (RIGHT SHIFT) SPECIFY THE PRINTER POSITION IN
.*                      WHICH THE FIRST CHARACTER OF DATA WILL BE
.*                      PRINTED.
.*                'L' - (LEFT SHIFT) SPECIFY THE CHARACTER POSITION
.*                      (RELATIVE TO ONE) OF THE DATA THAT WILL BE
.*                      PRINTED IN PRINTER POSITION ONE.
.*      VERMSG  - (DOS ONLY) THE MESSAGE TO BE PRINTED WHEN THIS FCB
.*                IMAGE IS LOADED TO THE PRINTER.
.*
.**********************************************************************
.*
.*       FCBGEN LINE=,CHAN=
.*
.*   FUNCTION:  SPECIFY ONE CHANNEL INDICATION IN THE FCB IMAGE.
.*   REQUIRED:  LINE
.*              CHAN
.*   DEFAULTS:  (NONE)
.*   ACCEPTABLE VALUES:  LINE - 1 (OR OFFSET+1) TO IMGLEN
.*                       CHAN - 1 TO 12
.*
.*   NOTE:  LINE=1,CHAN=1 MUST BE SPECIFIED FOR THE IMAGE TO
.*          COMPATIBLE WITH JES2.
.*
.*   OPERANDS:
.*      LINE - THE POSITION IN THE FCB AT WHICH A SKIP TO THE
.*             CORRESPONDING CHANNEL WILL STOP.  THIS VALUE WILL
.*             BE INTERNALLY ADJUSTED BY THE 'OFFSET' SPECIFICATION
.*             (IF ANY) ABOVE.
.*      CHAN - THE CHANNEL NUMBER THAT STOPS AT THIS 'LINE'.
.*
.**********************************************************************
.*
.*           FCBGEN
.*
.*       FUNCTION:  END THE IMAGE DEFINITION AND GENERATE THE ACTUAL
.*                  FCB IMAGE.
.*       REQUIRED:  (NONE)
.*       DEFAULTS:  (N/A)
.*       ACCEPTABLE VALUES:  (N/A)
.*
.**********************************************************************
.*.
.*   UPDATES:
.*      08/14/81 SDM   DON'T GENERATE CHANNEL IN LAST LINE OF IMAGE
.*      08/07/81 SDM   CHANGE 'INVALID JES2 IMAGE' MESSAGE SEVERITY
.*                        FROM 4 TO 8
.*                     ALLOW CHANNEL SPECIFICATION BEFORE 'OFFSET' LINE
.*                     DISALLOW LINE SPEC AT END OF IMAGE
.*      04/13/81 LDW   ADD OFFSET= OPERAND TO ADJUST LINE=
.*      01/12/81 SDM   ADD DOS/VS SUPPORT (OPSYS=DOS,VERMSG=)
.*                     REMOVE SUPPORT FOR TAKING IMAGE NAME FROM CSECT
.*                     CHANGE FCBLEN= TO IMGLEN=
.*                     ADD PRINT POSITION INDEX SUPPORT (INDEX=)
.*      09/10/80 SDM   ENSURE THAT EVERY CHANNEL APPEARS IN THE IMAGE
.*      09/06/79 SDM   ALLOW A CHAN TO BE PUNCHED IN MULTIPLE LINES
.*      07/28/79 SDM   NEW MACRO
.*
         GBLA  &GALINE(180),&GAIMGL,&GANAMEL,&GANDXA,&GAOFFS
         GBLB  &GBDEFLT,&GBDOS,&GBINIT,&GBFINAL,&GBNOGEN,&GBCHAN(12)
         GBLC  &GCNAME,&GCLPI,&GCVER(11),&GCNDXD
         LCLA  &I,&J,&K
         LCLB  &LBLINE
         LCLC  &LCNAME
.*
.* SELECT ENTRY TYPE
.*
         AIF   (NOT &GBFINAL).S01
         MNOTE 12,'THE FINAL ''FCBGEN'' HAS ALREADY BEEN SUPPLIED'
.S01     AIF   (NOT &GBINIT).I01
         AIF   ('&LABEL&LPINCH&IMGLEN&INDEX&OFFSET&OPSYS&DEFAULT&VERMSG$
               ' EQ '').S02
         MNOTE 12,'IMAGE NAME, LPINCH=, IMGLEN=, OFFSET=, OPSYS=, DEFAU$
               LT=, AND VERMSG= MAY ONLY OCCUR ON THE FIRST ''FCBGEN'''
&GBNOGEN SETB  1
.S02     AIF   ('&LINE&CHAN' NE '').P01
         AIF   ('&LPINCH&IMGLEN&INDEX&OFFSET&OPSYS&DEFAULT&VERMSG'     $
               EQ '').F01
         MEXIT
.*
.* EDIT INITIAL SPECIFICATION
.*
.I01     ANOP
&GBINIT  SETB  1
&GBDOS   SETB  0                        USE OPSYS DEFAULT OF OS
         AIF   ('&LINE&CHAN' EQ '').I02
         MNOTE 12,'LINE= AND CHAN= MAY NOT BE SPECIFIED ON FIRST THE FI$
               RST ''FCBGEN'''
&GBNOGEN SETB  1
.I02     ANOP
&GCNAME  SETC  '&LABEL'
&GANAMEL SETA  K'&LABEL
&GCLPI   SETC  '6'
         AIF   (T'&LPINCH EQ 'O').I04
         AIF   (('&LPINCH' EQ '6') OR ('&LPINCH' EQ '8')).I03
         MNOTE 12,'LPINCH MUST BE 6 OR 8 (DEFAULT IS 6)'
&GBNOGEN SETB  1
         AGO   .I04
.I03     ANOP
&GCLPI   SETC  '&LPINCH'
.I04     AIF   (T'&IMGLEN NE 'O').I05
&GAIMGL  SETA  66
         AGO   .I07
.I05     AIF   (T'&IMGLEN NE 'N').I06
&GAIMGL  SETA  &IMGLEN
         AIF   ((&GAIMGL GE 2) AND (&GAIMGL LE 180)).I07
         MNOTE 12,'IMGLEN IS NOT BETWEEN 2 AND 180'
&GBNOGEN SETB  1
         AGO   .I07
.I06     MNOTE 12,'IMGLEN IS NOT NUMERIC (BETWEEN 2 AND 180)'
&GBNOGEN SETB  1
.I07     AIF   (T'&INDEX EQ 'O').I10
         AIF   (N'&INDEX EQ 2).I08
         MNOTE 12,'INDEX REQUIRES TWO OPERANDS - DIRECTION AND POSITION$
               '
&GBNOGEN SETB  1
         AGO   .I10
.I08     AIF   ('&INDEX(1)' EQ 'L').I09
         AIF   ('&INDEX(1)' EQ 'LEFT').I09
         AIF   ('&INDEX(1)' EQ 'R').I09
         AIF   ('&INDEX(1)' EQ 'RIGHT').I09
         MNOTE 12,'INDEX FIRST ARGUMENT MUST BE L (LEFT), OR R (RIGHT)'
&GBNOGEN SETB  1
.I09     ANOP
&GCNDXD  SETC  '&INDEX(1)'(1,1)
&GANDXA  SETA  &INDEX(2)
         AIF   (&GANDXA GE 1 AND &GANDXA LE 31).I10
         MNOTE 12,'INDEX SECOND ARGUMENT MUST BE BETWEEN 1 AND 31 (INCL$
               USIVE)'
&GBNOGEN SETB  1
.I10     AIF   (T'&OFFSET EQ 'O').I12
         AIF   (T'&OFFSET NE 'N').I11
&GAOFFS  SETA  &OFFSET
         AGO   .I12
.I11     MNOTE 12,'OFFSET IS NOT NUMERIC (BETWEEN 0 AND FCBLEN)'
&GBNOGEN SETB  1
.I12     AIF   (T'&OPSYS EQ 'O').I15
         AIF   ('&OPSYS' NE 'OS').I13
&GBDOS   SETB  0
         AGO   .I15
.I13     AIF   ('&OPSYS' NE 'DOS').I14
&GBDOS   SETB  1
         AGO   .I15
.I14     MNOTE 12,'OPSYS MUST BE DOS OR OS'
&GBNOGEN SETB  1
.I15     AIF   (&GBDOS).ID01
.*
.* EDIT OS DEPENDENT INITIAL SPECIFICATIONS
.*
         AIF   (&GANAMEL LT 5).IO01
         AIF   ('&LABEL'(1,4) EQ 'FCB2').IO02
.IO01    MNOTE 12,'THE IMAGE NAME MUST BE 5 OR MORE CHAR STARTING WITH $
               ''FCB2'''
&GBNOGEN SETB  1
.IO02    ANOP
         AIF   (T'&DEFAULT EQ 'O').IO03
         AIF   ('&DEFAULT' EQ 'NO').IO03
&GBDEFLT SETB  1
         AIF   ('&DEFAULT' EQ 'YES').IO03
         MNOTE 12,'DEFAULT IS NOT YES OR NO (DEFAULT DEFAULT IS NO)'
&GBNOGEN SETB  1
         AGO   .IO04
.IO03    AIF   (T'&VERMSG EQ 'O').IO04
         MNOTE 4,'VERMSG IS NOT APPLICABLE TO OS - IGNORED'
.IO04    MEXIT
.*
.* EDIT DOS DEPENDENT INITIAL SPECIFICATIONS
.*
.ID01    AIF   (&GANAMEL LT 5).ID02
         AIF   ('&LABEL'(1,4) EQ 'FCB2').ID03
.ID02    MNOTE 4,'THE IMAGE NAME SHOULD BE 5 OR MORE CHAR STARTING WITH$
                ''FCB2'''
.ID03    AIF   (T'&DEFAULT EQ 'O').ID04
         MNOTE 4,'DEFAULT IS NOT APPLICABLE TO DOS - IGNORED'
.ID04    AIF   (T'&VERMSG NE 'O').ID05
         MNOTE 12,'A VERIFICATION MESSAGE (VERMSG=) OF UP TO 80 CHARACT$
               ERS IS REQUIRED FOR DOS'
         AGO   .ID06
.ID05    AIF   ('&VERMSG'(1,1) EQ '''').ID07
.ID06    MNOTE 12,'THE VERIFICATION MESSAGE MUST BE SPECIFIED WITHIN QU$
               OTES'
&GBNOGEN SETB  1
         AGO   .ID11
.ID07    AIF   (K'&VERMSG LE 82).ID08
         MNOTE 12,'THE VERIFICATION MESSAGE EXCEEDS 80 CHARACTERS IN LE$
               NGTH'
&GBNOGEN SETB  1
         AGO   .ID11
.ID08    ANOP
&I       SETA  1
&J       SETA  1
.ID09    ANOP
&K       SETA  K'&VERMSG-&J+1
         AIF   (&K LT 1).ID11
         AIF   (&K LE 8).ID10
&K       SETA  8
.ID10    ANOP
&GCVER(&I) SETC '&VERMSG'(&J,&K)
&I       SETA  &I+1
&J       SETA  &J+&K
         AGO   .ID09
.ID11    MEXIT
.*
.* EDIT LINE PUNCH SPECIFICATION
.*
.P01     AIF   (T'&LINE EQ 'O').P04
         AIF   (T'&LINE NE 'N').P05
         AIF   (&LINE LT 1).P06
         AIF   (&LINE GT &GAIMGL).P06
&LBLINE  SETB  1
&I       SETA  &LINE-&GAOFFS
         AIF   (&I GE 1).P02
&I       SETA  &GAIMGL+&I
.P02     AIF   (&GALINE(&I) NE 0).P07
         AIF   (&I LT &GAIMGL).P08
         MNOTE 8,'LINE=&LINE RESOLVES TO THE LAST LINE OF THE IMAGE -- $
               NOT VALID FOR 3203 PRINTER.'
         AGO   .P08
.P04     MNOTE 12,'LINE MUST BE SPECIFIED (BETWEEN 1 AND IMGLEN)'
&GBNOGEN SETB  1
         AGO   .P08
.P05     MNOTE 12,'LINE IS NOT NUMERIC (BETWEEN 1 AND IMGLEN)'
&GBNOGEN SETB  1
         AGO   .P08
.P06     MNOTE 12,'LINE IS NOT BETWEEN 1 AND IMGLEN'
&GBNOGEN SETB  1
         AGO   .P08
.P07     MNOTE 12,'LINE=&LINE HAS BEEN SPECIFIED MULTIPLE TIMES'
&GBNOGEN SETB  1
.P08     AIF   (T'&CHAN EQ 'O').P10
.P09     AIF   (T'&CHAN NE 'N').P11
         AIF   (&CHAN LT 1).P12
         AIF   (&CHAN GT 12).P12
         AIF   (NOT &LBLINE).P13
&GALINE(&I) SETA &CHAN
&GBCHAN(&CHAN) SETB 1
         AGO   .P13
.P10     MNOTE 12,'CHAN MUST BE SPECIFIED (BETWEEN 1 AND 12)'
&GBNOGEN SETB  1
         AGO   .P13
.P11     MNOTE 12,'CHAN IS NOT NUMERIC (BETWEEN 1 AND 12)'
&GBNOGEN SETB  1
         AGO   .P13
.P12     MNOTE 12,'CHAN IS NOT BETWEEN 1 AND 12'
&GBNOGEN SETB  1
.P13     MEXIT
.*
.* EDIT FINAL SPECIFICATION
.*
.F01     ANOP
* FCBGEN - 08/14/81 GIBRALTAR SAVINGS
&GBFINAL SETB  1
         AIF   (&GBCHAN(1)).F02
         MNOTE 12,'NO CHANNEL 1 WAS SPECIFIED.  FCB IMAGE IS INVALID.'
&GBNOGEN SETB  1
.F02     AIF   (NOT &GBNOGEN).F03
         MNOTE 12,'IMAGE SPECIFICATION ERROR DETECTED.  GENERATION TERM$
               INATED'
         MEXIT
.F03     AIF   (&GBDOS).F04
         AIF   (&GALINE(1) EQ 1).F04
&I       SETA  &GAOFFS+1
         MNOTE 8,'''LINE=&I,CHAN=1'' WAS NOT SPECIFIED.  NOT A VALID JE$
               S2 IMAGE.'
.F04     MNOTE *,'THE NAME OF THIS FCB IMAGE IS &GCNAME'
         AIF   (NOT &GBDOS).F05
         PUNCH '   PHASE &GCNAME,+0'
.F05     ANOP
&GCNAME  CSECT
.F06     AIF   (&GBDOS).F08
&I       SETA  &GANAMEL-4
&LCNAME  SETC  '&GCNAME'(5,&I)
         AIF   (&GBDEFLT).F07
         MNOTE *,'ONLY ''FCB=&LCNAME'' OUTPUT WILL BE PRINTED'
         DC    X'00'                    OS/VS FLAGS
         AGO   .F08
.F07   MNOTE *,'DEFAULT FCB AND ''FCB=&LCNAME'' OUTPUT WILL BE PRINTED'
         DC    X'80'                    OS/VS FLAGS
.F08     MNOTE *,'THE FCB IMAGE IS &GAIMGL LINES LONG'
         AIF   (&GBDOS).F10
&I       SETA  &GAIMGL
         AIF   ('&GCNDXD' EQ '').F09
&I       SETA  &I+1
.F09     DC    AL1(&I)                  FCB IMAGE DATA LENGTH
.F10     AIF   ('&GCNDXD' EQ '').F13
&I       SETA  &GANDXA
&J       SETA  &I-1
         MNOTE *,'PRINT POSITION INDEXING (A 3211 FEATURE) HAS BEEN SPE$
               CIFIED.'
         AIF   ('&GCNDXD' EQ 'L').F11
         MNOTE *,'   POSITION 1 OF THE PRINT DATA WILL BE AT COLUMN &I'
&I       SETA  &I+X'80'
         AIF   (&J LT 1).F12
         MNOTE *,'   (PRECEEDED BY &J BLANK(S))'
         AGO   .F12
.F11     MNOTE *,'   POSITION &I OF THE PRINT DATA WILL BE AT COLUMN 1'
&I       SETA  &I+X'40'
         AIF   (&J LT 1).F12
         MNOTE *,'   (&J CHARACTER(S) OF DATA WILL BE IGNORED)'
.F12     DC    AL1(&I)                  INDEX CONTROL BYTE
.F13     MNOTE *,'OUTPUT WILL PRINT AT &GCLPI LINES PER INCH'
         AIF   ('&GCLPI' EQ '6').F14
&GALINE(1) SETA &GALINE(1)+X'10'
.F14     ANOP
&GALINE(&GAIMGL) SETA &GALINE(&GAIMGL)+X'10'
&I       SETA  1
&J       SETA  0
.F15     AIF   (&I GT &GAIMGL).F25
         AIF   (&GALINE(&I) EQ 0).F24
&J       SETA  &I-&J-1
         AIF   (&J LT 1).F16
         DC    &J.AL1(0)
.F16     ANOP
&J       SETA  (&GALINE(&I))
         AIF   (&J LT X'10').F17
         AIF   (&J EQ X'10').F23
&J       SETA  &J-X'10'
.F17     MNOTE *,'CHANNEL &J STOPS AT LINE &I'
         AIF   (&J NE 1).F23
.*
.* GENERATE SPECIFICATION FOR EVERY UNUSED CHANNEL IN THE IMAGE, EXCEPT
.*    CHANNELS 9 AND 12, WHICH CAUSE I/O CHAIN INTERRUPTIONS.
&J       SETA  &I+1
&K       SETA  2
.F18     AIF   (&J GE &GAIMGL).F23      DON'T USE LAST LINE OF IMAGE
.F19     AIF   (&K GT 11).F23           DON'T GENERATE CHANNEL 12
         AIF   (&GBCHAN(&K)).F22
         AIF   (&K EQ 9).F22            DON'T GENERATE CHANNEL 9
         AIF   (&GALINE(&J) NE 0).F21
&GALINE(&J) SETA &K                     PUT CHANNEL INTO MATRIX
&K       SETA  &K+1
.F21     ANOP
&J       SETA  &J+1
         AGO   .F18
.F22     ANOP
&K       SETA  &K+1
         AGO   .F19
.*
.F23     DC    AL1(&GALINE(&I))
&J       SETA  &I
.F24     ANOP
&I       SETA  &I+1
         AGO   .F15
.F25     AIF   (NOT &GBDOS).F27
&I       SETA  255-&J
         AIF   (&I LT 1).F26
         DC    &I.X'00'
.F26     DC    CL80&GCVER(1)&GCVER(2)&GCVER(3)&GCVER(4)&GCVER(5)&GCVER($
               6)&GCVER(7)&GCVER(8)&GCVER(9)&GCVER(10)&GCVER(11)
.F27     MEND
