         MACRO
&NAME    TSFINISH &RC=,&FREEDA=YES
         COPY  MATGBL
         LCLC  &R,&GOTO,&STCON
&R       SETC  '&MATREGS'
         AIF   ('&FREEDA'(1,1) EQ 'Y').DOFREE
         AIF   ('&RC' EQ '').RETNRC
         LOADR &R.15,&RC,'SET RETURN CODE'
         L     &R.13,4(,&R.13) .        GET HSA POINTER
         L     &R.14,12(,&R.13) .       GET RETURN ADDR
         LM    &R.0,&R.12,20(&R.13) .   GET REST OF REGS
         BR    &R.14 .                  RETURN TO CALLER
         MEXIT
.RETNRC  ANOP
         L     &R.13,4(,&R.13) .        GET HSA POINTER
         LM    &R.14,&R.12,12(&R.13) .  RETURN CALLERS REGS
         BR    &R.14 .                  RETURN TO CALLER
         MEXIT
.DOFREE  ANOP
         AIF   ('&MATTSD1' NE '').HAVINDX
&MATTSD1 SETC  'IHB&SYSNDX.Z'
.HAVINDX ANOP
         AIF   ('&MATTSL1' NE '').HAVEXIT
&MATTSL1 SETC  'IHB&SYSNDX.Q'
.HAVEXIT ANOP
         AIF   ('&RC'(1,1) NE '(').NOREG
         ST    &RC(1),0(,&R.13) .       SAVE RET CODE
         AGO   .RCSET
.NOREG   ANOP
         AIF   (T'&RC EQ 'N').SELFDEF
&STCON   SETC  '0(,&R.13)'
         IHMLSPTR &RC,&STCON,'GET RC VALUE','SAVE ACROSS CALL'
.RCSET   ANOP
&MATTSF1 SETB  1
.SELFDEF ANOP
         L     &R.15,IHB&SYSNDX.A .     GET EXIT CODE ADDR
         BALR  &R.14,&R.15 .            GO FREE UP THE MESS
         LM    &R.14,&R.12,12(&R.13) .  GET THE REGS BACK
         AIF   (&MATTSF1).NOSDEF
         LA    &R.15,&RC .              SET THE RETURN CODE
.NOSDEF  ANOP
         BR    &R.14 .                  RETURN TO CALLER
         SPACE 2
IHB&SYSNDX.A DC A(&MATTSL1) .           ADDR OF EXIT CODE
         MEND
