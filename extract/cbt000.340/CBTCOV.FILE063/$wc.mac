         MACRO
&LABEL   $WC   &OPT
.**********************************************************************
.*
.*  THIS MACRO IS USED IN MAPPING TPUT FULL SCREEN OUTPUT.  IT
.*  DEFINES A SCREEN START AND THE COMMAND CODE FOR THAT SCREEN.
.*  THE FORMAT OF THE MACRO IS AS FOLLOWS:
.*
.*  LABEL  $WC     W       (WRITE)
.*                 EW      (ERASE/WRITE)
.*                 EWA     (ERASE/WRITE ALTERNATE)
.*                 RB      (READ BUFFER)
.*                 RM      (READ MODIFIED)
.*                 RMA     (READ MODIFIED ALL)
.*                 EAU     (ERASE ALL UNPROTECTED)
.*                 WSF     (WRITE STRUCTURED FIELD)
.**********************************************************************
         AIF   ('&LABEL' EQ '').ERR1
&LABEL   DC    X'27'           CONTROL CHARACTER FLAG
         AIF   ('&OPT' EQ '').ERR2
         AIF   ('&OPT' EQ 'W').W
         AIF   ('&OPT' EQ 'EW').EW
         AIF   ('&OPT' EQ 'EWA').EWA
         AIF   ('&OPT' EQ 'RB').RB
         AIF   ('&OPT' EQ 'RM').RM
         AIF   ('&OPT' EQ 'RMA').RMA
         AIF   ('&OPT' EQ 'EAU').EAU
         AIF   ('&OPT' EQ 'WSF').WSF
         AGO   .ERR3
.W       ANOP
         DC    X'F1'           WRITE FUNCTION
         AGO   .MACEND
.EW      ANOP
         DC    X'F5'           ERASE/WRITE FUNCTION
         AGO   .MACEND
.EWA     ANOP
         DC    X'7E'           ERASE/WRITE ALTERNATE FUNCTION
         AGO   .MACEND
.RB      ANOP
         DC    X'F2'           READ BUFFER FUNCTION
         AGO   .MACEND
.RM      ANOP
         DC    X'F6'           READ MODIFIED FUNCTION
         AGO   .MACEND
.RMA     ANOP
         DC    X'6E'           READ MODIFIED ALL FUNCTION
         AGO   .MACEND
.EAU     ANOP
         DC    X'6F'           ERASE ALL UNPROTECTED FUNCTION
         AGO   .MACEND
.WSF     ANOP
         DC    X'F3'           WRITE STRUCTURED FIELD FUNCTION
         AGO   .MACEND
.ERR1    MNOTE 8,'LABEL NAME NOT SPECIFIED IN COLUMN 1'
         AGO   .MACEND
.ERR2    MNOTE 8,'NO FUNCTION SPECIFIED FOR $WC'
         AGO   .MACEND
.ERR3    MNOTE 8,'INVALID OPERAND SPECIFIED FOR $WC'
.MACEND  ANOP
         MEND
