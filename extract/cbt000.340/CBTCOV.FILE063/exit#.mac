         MACRO
         EXIT# &RC=(15)
.*
.**********************************************************************
.*       DEFINE VARIABLES                                             *
.**********************************************************************
.*
         GBLA  &GIFLVL,&GDOLVL                   IF/DO LEVEL
         GBLA  &GEXCNT,&GDWCNT                   COUNTERS FOR LABELS
         GBLB  &IFORSW#,&UNORSW#,&DWORSW#
         GBLB  &IEXSW#                           IEXIT CONTROL SWITCH
         GBLC  &SAVE#                            SAVE AREA FOR LINK REG
         GBLC  &GLREG                            LINK REGISTER
         GBLC  &REENT                            CODE TYPE INDICATOR
         GBLC  &EXLIS                            LISTING TYPE INDICATOR
         GBLC  &STARTEX                          FIRST MODULE NAME
         GBLC  &ENTNAME                          CURRENT MOD NAME
         LCLC  &LBLYSS                           TO GENERATE LABELS
.*
.**********************************************************************
.*       CHECK INPUT PARAMETERS                                       *
.**********************************************************************
.*
         AIF   (&GIFLVL EQ 0).L10
         MNOTE 8,'YS000016-MISSING ENDIF STATEMENT(S)'
&GIFLVL  SETA  0
.L10     ANOP
         AIF   (&GDOLVL EQ 0).L15
         MNOTE 8,'YS000017-MISSING ENDDO/UNTIL/WHILE STATEMENT(S)'
&GDOLVL  SETA  0
&GDWCNT  SETA  0
.L15     ANOP
         AIF   (&IFORSW# EQ 0).L17
         MNOTE 8,'YS000018-MISSING IF FOLLOWING IFOR(S)'
&IFORSW# SETB  0
.L17     ANOP
         AIF   (&UNORSW# EQ 0).L17A
         MNOTE 8,'YS000019-MISSING UNTIL FOLLOWING UNTILOR(S)'
&UNORSW# SETB  0
.L17A    ANOP
         AIF   (&DWORSW# EQ 0).L17B
         MNOTE 8,'YS000020-MISSING DOWHILE FOLLOWING DOWHLOR(S)'
&DWORSW# SETB  0
.L17B    ANOP
.*
         AIF   ('&RC'(1,1) EQ '(' AND '&RC' NE '(15)').ERR010
         AGO   .OK
.ERR010  ANOP
         MNOTE 12,'YS000021 - &RC. IS INVALID PARAMETER'
         MEXIT
.*
.**********************************************************************
.*       SET UP GLOBAL VARIABLES                                      *
.**********************************************************************
.*
.OK      ANOP
         SEQCK EXIT                              PRED/SUCCESSOR CHECK
.*
.**********************************************************************
.*       GENERATE CODE                                                *
.**********************************************************************
.*
         SPACE
         AIF   ('&RC' EQ '(15)').CODE010         IS RET.CODE IN REG15 ?
         LA    15,&RC.(0,0)                      LOAD RETURN CODE
.CODE010 ANOP
.*
         AIF   (&IEXSW# EQ 0).CODE015            IS IEXIT PRESENTED ?
.*       GENERATE FIRST LABEL OF EXIT# TO BRANCH ON FROM IEXIT#
&LBLYSS  SETC  'ZLEX&GEXCNT'                     GEN LABEL FOR IEXIT
&LBLYSS  DS    0H
&IEXSW#  SETB  0                                 TURN OFF IEXIT SWITCH
.CODE015 ANOP
.*
.*       CHANGE COUNTER FOR SECOND LABEL OF EXIT# - LABEL OF SAREA
&GEXCNT  SETA  &GEXCNT+1
.*
         AIF   ('&STARTEX' NE '&ENTNAME').CODE020  IS IT FIRST MODULE ?
         B     ZLINEXIT                          EXIT FROM ROUTINE
         AGO   .CODE040
.*
.CODE020 ANOP
         AIF   ('&SAVE#' EQ 'YES').CODE030       IS SAREA STANDARD ?
         AIF   ('&SAVE#' EQ 'NO').CODE050        IS THERE ANY SAREA ?
         AIF   ('&SAVE#'(1,1) EQ '(').CODE060    IS SAREA REG ?
.*
.*       SAVE AREA IS USER SUPPLIED ADDRESS
         L     &GLREG,&SAVE#                     RESTORE LINK REG
         BR    &GLREG                            RETURN TO CALLER
         AGO   .CODE040
.*
.CODE030 ANOP
.*       NEXT STATEMENT USES SECOND LABEL TO RESTORE LINK REG
         L     &GLREG,ZLEX&GEXCNT                RESTORE LINK REG
         BR    &GLREG                            RETURN TO CALLER
         AIF   ('&REENT' EQ 'N').CODE070         IS CODE REENT ?
ZLINDSCT DSECT
.CODE070 ANOP
.*
.*       GENERATE SECOND LABEL OF EXIT# TO SAVE LINK REG
&LBLYSS  SETC  'ZLEX&GEXCNT'                     GET LABEL
&LBLYSS  DC    F'0'                              LINK REG SAVE AREA
.*
         AIF   ('&REENT' EQ 'N').CODE080         IS CODE REENT ?
&SYSECT  CSECT
.CODE080 ANOP
.*
         AGO   .CODE040
.*
.CODE050 ANOP
         BR    &GLREG                            RETURN TO CALLER
         AGO   .CODE040
.*
.CODE060 ANOP
         LA    &GLREG,0&SAVE#                    RESTORE LINK REG
         BR    &GLREG                            RETURN TO CALLER
.*
.CODE040 ANOP
.*
         SPACE
         AIF   ('&EXLIS' EQ 'Y').CODE090         IS PRINT GEN ?
         POP   PRINT
.CODE090 ANOP
.*
         MEND
