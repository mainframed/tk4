         MACRO ,                                                  40000
&NAME    OPENTMC &NOTACT=ABEND001,&MF=S,&OPCODE=12                40041
.*
         AIF   ('&MF' EQ 'S' OR '&MF' EQ 'E' OR '&MF' EQ 'L').OPEN
         AGO   .MNOTE2
.*
.OPEN    ANOP
         AIF   ('&MF' EQ 'L').LIST
.*
&NAME    DS    0H -               OPENTMC MACRO
*        LOAD  EP=TMSSVC -        LOAD THE SVC INTERFACE MODULE
         LOAD  EP=TMSSVC -        LOAD THE SVC INTERFACE MODULE
         ST    R0,TMSVCRTN -      STORE ENTRY POINT ADDRESS
         XC    TMSPARM,TMSPARM -  CLEAR SVC PARAMETER LIST
         MVI   TMOPCODE,&OPCODE -     POST OP-CODE                40041
         L     R15,TMSVCRTN -     GET ENTRY POINT  ADDRESS
         LA    R1,TMSPARM -       POINT TO PARAMETER LIST
         BALR  R14,R15 -          GET ADDRESS OF THE VECTOR TABLE
         LTR   R1,R1 -            Q. TMS ACTIVE
         BNP   &NOTACT -          A. NOT POSITIVE - NO
         MVC   TMSHASH,0(R1) -    POST ADDRESS OF TMSHASH
         AIF   ('&MF' EQ 'E').END
         B     OPEN&SYSNDX -
.*
.LIST    ANOP
         DS    0D
TMSPARM  DS    0XL12 -          ** TMS PARAMETER LIST **
TMOPCODE DS    XL1 -              TMS OP-CODE
TMIOADDR DS    XL3 -              USER I/O ADDRESS
TMRELBLK DS    XL4 -              TMC REL BLK ADDRESS
TMOPBITS DS    XL1 -              TMS OPTION BITS
TMDSNBRB DS    XL3 -              DSNB REL BLK ADDRESS
         DS    0F
TMSVCRTN DS    A(0) -             SVC INTERFACE MODULE ADDRESS
TMSHASH  DC    A(0) -             TMSHASH ADDRESS                 40101
.*
         AIF   ('&MF' EQ 'L').END
OPEN&SYSNDX  DS  0H
.END     ANOP
         MEXIT
.MNOTE2  MNOTE 8,'MF= INVALID'
         MEXIT
.*
         MEND
