         MACRO
         TSDAIRER &Z,&NOGEN=NO,&REGEN=NO
         AIF   ('&NOGEN' NE 'YES').G1
.*       PRINT NOGEN
.G1      ANOP
         USING TSDAIRER,14
TSDAIRER STM   15,0,MPLLEN3             STORE RETURN ADD & RETURN CODE
         CH    15,TSDAIRE8              SEE IF CAT ERR
         BE    TSDAIRE1
         CH    15,TSDAIR12              SEE IF DAIR ERR
         BNE   TSDAIRE2
TSDAIRE1 L     1,16(1)                  GET ADD OF DAPB
         LA    1,4(1)                   GET ADD OF RETURN CODES
         ST    1,MPLLEN1                STORE AS VAR PORTION
         MVI   MPLLEN1,X'80'            SET FLG TO HEX
TSDAIRE2 LA    15,TSDAIRET-4(15)
         MVC   MPLMSGID(4),0(15)
         TSMSG TYPE=HEX,TABLE=TSDAIREC  OUTPUT MSG
         LM    15,0,MPLLEN3             GET RETURN ADD & RETURN CODE
         LR    14,0                     MOVE RETURN CODE TO USABLE REG
         BR    14
         AIF   ('&NOGEN' NE 'PART').G2
.*       PRINT NOGEN
.G2      ANOP
TSDAIRE8 DC    H'8'
TSDAIR12 DC    H'12'
TSDAIRET DC    C'TS1 TS2 TS3 TS4 TS5 TS6 TS7 TS8 TS9 TS10 TS11'
TSDAIREC TSMSGD ' ERROR IN DAIR+',TS1,Q1
         TSMSGD ' THE DAPL CONTAINED INVALID INFORMATION',Q1
         TSMSGD ' ERROR IN DAIR+',TS2,Q2
         TSMSGD ' ERROR OCCURED IN CATALOG MANAGEMENT ROUTINES+',Q2,Q2A
         TSMSGD (' CATALOG RETURN CODE ',),Q2A
         TSMSGD ' ERROR IN DAIR+',TS3,Q3
         TSMSGD (' DAIR RETURN CODE ',),Q3
         TSMSGD ' ERROR IN DAIR+',TS4,Q4
         TSMSGD ' NO TIOT ENTRIES AVAILABL',Q4
         TSMSGD ' ERROR IN DAIR+',TS5,Q5
         TSMSGD ' THE DDNAME REQUESTED IS NOT AVAILABLE',Q5
         TSMSGD ('  DAIR RETURN CODE ',),Q3
         TSMSGD ' ERROR IN DAIR+',TS6,Q6
         TSMSGD ' THE DSNAME REQUESTED IS PART OF A CONCATENATED GROUP'X
               ,Q7
         TSMSGD ' ERROR IN DAIR+',TS7,Q7
         TSMSGD ' THE DDN OR DSN SPECIFIED IS NOT CURRENTLY ALLOCATED',X
               Q7
         TSMSGD ' ERROR IN DAIR+',TS8,Q8
         TSMSGD ' THE REQUESTED DATASET WAS PREVIOUSLY ALLOCATED+',Q8, X
               Q8A
         TSMSGD ' OR WAS ALLOCATED DISP=NEW, AND WAS NOT DELETED.+',Q8AX
               ,Q8B
         TSMSGD ' DISP=NEW CANNOT NOW BE SPECIFIED',Q8B
         TSMSGD ' ERROR IN DAIR+',TS9,Q9
         TSMSGD ' ERROR OCCURED IN THE CATALOG INFORMATION ROUTINE',Q9
         TSMSGD ' ERROR IN DAIR+',TS10,Q10
         TSMSGD ' THE RETURN AREA PROVIDED FOR QUALIFIERS WAS+',Q10,   X
               Q10A
         TSMSGD ' EXHAUSTED AND MORE BLOCKS EXIST.',Q10A
         TSMSGD ' ERROR IN DAIR+',TS11,Q11
         TSMSGD ' THE PREVIOUS ALLOCATION SPECIFIED A DISP OF+',Q11,   X
               Q11A
         TSMSGD ' DELETE FOR THIS NON PERMANENTLY ALLOCATED DATASET',  X
               Q11A
         TSMSGD
         AIF   ('&REGEN' NE 'YES').G3
.*       PRINT GEN
.G3      ANOP
         DROP  14
         MEND
