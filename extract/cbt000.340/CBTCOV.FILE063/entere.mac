         MACRO
&NAME    ENTERE &BASE,&HDR,&LENGTH,&REG
         LCLA  &L,&M
         LCLC  &NAM,&NAM2
.*********************************************************************.
.*                                                                   *.
.*  THE 'ENTERE' MACRO IS USED TO PRODUCE A RE-ENTRANT ENTRY ROUTINE.*.
.*                                                                   *.
.*  THE ENTERE MACRO IS USED IN PLACE OF A 'SAVE' MACRO TO SAVE THE  *.
.*  GENERAL PURPOSE REGISTERS AND TO ESTABLISH ADDRESSABILITY BY     *.
.*  MEANS OF A USING.  IT ALSO ESTABLISHES THE FORWARD AND BACKWARD  *.
.*  SAVEAREA CHAINS.  THE SAVEAREA FOR THE PROGRAM CONTAINING THE    *.
.*  ENTERE MACRO IS ACQUIRED DYNAMICALLY BY MEANS OF A 'GETMAIN'     *.
.*  MACRO.                                                           *.
.*                                                                   *.
.*  THE LABEL OF THIS MACRO IS USED TO DEFINE AN ENTRY POINT TO THE  *.
.*  PROGRAM.                                                         *.
.*                                                                   *.
.*  THE FIRST OPERAND SPECIFIES THE BASE REGISTER TO BE USED IN THE  *.
.*  PROGRAM.  A USING IS ISSUED FOR THIS REGISTER AND IT IS LOADED   *.
.*  WITH THE ENTRY POINT ADDRESS.  IF THIS OPERAND IS OMITTED, OR IS *.
.*  INVALID, REGISTER 12 IS ASSUMED.  REGISTERS 0,1,13 OR 14 MAY NOT *.
.*  BE USED AS THE BASE REGISTER.                                    *.
.*                                                                   *.
.*  THE SECOND OPERAND IS USED AS AN ENTRY POINT IDENTIFIER FOR THE  *.
.*  PROGRAM IN THE SAME MANNER THAT 'SAVE' GENERATES ITS IDENTIFIER. *.
.*  IF AN ASTERISK IS USED (*) THE LABEL IS USED AS THE IDENTIFIER.  *.
.*  IF THIS OPERAND IS OMMITTED, NO ID IS GENERATED.  IT MUST NOT    *.
.*  CONTAIN ANY BLANKS AND SHOULD NOT BE ENCLOSED IN APOSTROPHES. IF *.
.*  APOSTROPHES (') OR AMPERSANDS (&) ARE USED, USE TWO ADJACENT     *.
.*  CHARACTERS RATHER THAN ONE TO REPRESENT THE REQUIRED CHARACTER.  *.
.*                                                                   *.
.*  THE THIRD OPERAND IS THE NUMBER OF ADDITIONAL BYTES TO BE        *.
.*  ACQUIRED BY THE GETMAIN THAT ACQUIRES THE SAVEAREA.  THESE BYTES *.
.*  ARE LOCATED IMMEDIATELY BEHIND THE SAVEAREA AND ARE PADDED TO A  *.
.*  DOUBLE WORD LENGTH AND ARE DOUBLE-WORD ALIGNED.  THE AREA WILL   *.
.*  BE ACQUIRED FROM SUBPOOL 0.                                      *.
.*                                                                   *.
.*   THE FOURTH OPERAND, IF PRESENT, MUST BE NOREG TO DENOTE THE     *.
.*   EXCLUSION OF REGISTER ASSIGNMENTS.                              *.
.*                                                                   *.
.*  EXAMPLES:  MAIN  ENTERE  12,MYPROG,100                           *.
.*             SUB1  ENTERE  11,*,50                                 *.
.*                                                                   *.
.*  WARNING:  REGISTER 15 IS 'USED' AND 'DROPPED' DURING THE EXPAN-  *.
.*  SION OF THE ENTERE MACRO.  IT WILL BE IN EFFECT AT THE END OF THE*.
.*  EXPANSION IF IT HAS BEEN SPECIFIED AS THE BASE REGISTER OPERAND. *.
.*                                                                   *.
.*********************************************************************.
         AIF   ('&REG' NE 'NOREG').REGAS
         SPACE
****************   NO REGISTER ASSIGNMENTS SPECIFIED   ****************
         AGO   .NO1
.REGAS   ANOP
         SPACE
**********************   REGISTER ASSIGNMENTS   ***********************
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.NO1     ANOP
         SPACE
**************************   ENTRY ROUTINE   **************************
         SPACE
         AIF   (K'&NAME GE 1).OKLBL
         SPACE
         MNOTE 4,'* NO LABEL PROVIDED. ENTRY NAME WILL BE GENERATED.'
         SPACE
&NAM     SETC  'IHB&SYSNDX.A' GENERATE ENTRY NAME
&M       SETA  8
         AGO   .USING
.OKLBL   ANOP
&NAM     SETC  '&NAME'
.USING   USING &NAM.,15
         ENTRY &NAM
&NAM     B     IHB&SYSNDX     BRANCH OVER IDENTIFIER
&L       SETA  K'&HDR
         AIF   (&L LE 0).NOHDR
.OKHDR   AIF   ('&HDR' EQ '*').STARLAB
         DC    AL1(&L)        LENGTH OF ID
         DC    C'&HDR.'
         AGO   .DROP
.STARLAB ANOP
         AIF   (&M NE 0).P3
&L       SETA  K'&NAME
         AGO   .P1
.P3      ANOP
&L       SETA  &M
         AGO   .P1
.NOHDR   DC    AL2(0)         IDENTIFIER LENGTH = 0
         AGO   .DROP
.P1      DC    AL1(&L.),C'&NAM.' ID LENGTH AND ID
.DROP    DROP  15             WARNING:  REG15 IS DROPPED HERE
         AIF   (K'&BASE EQ 1 OR K'&BASE EQ 2).OKNUMBR
         AIF   (K'&BASE EQ 0).NOBASE
.PUT     SPACE
         MNOTE 4,'* INVALID BASE REGISTER SPECIFIED, REG 12 ASSUMED.'
.SPACE   SPACE
&L       SETA  12
         AGO   .STM
.NOBASE  SPACE
*        NO BASE REGISTER SPECIFIED.  REG 12 WILL BE USED.
         AGO   .SPACE
.OKNUMBR ANOP
&L       SETA  &BASE
         AIF   (&L LT 2 OR &L GT 15 OR &L EQ 13 OR &L EQ 14).PUT
.STM     ANOP
IHB&SYSNDX STM 14,12,12(13)        STORE ALL REGS IN CALLER'S SAVEAREA
         USING &NAM.,&L            DEFINE BASE REGISTER
         AIF   (&L EQ 15).SKIP
         LR    &L,15               LOAD ENTRY ADDRESS INTO BASE REG
.SKIP    ANOP
         AIF   (T'&LENGTH EQ 'O').SKIP4
         AIF   (T'&LENGTH EQ 'U').SKIP1
         AIF   (T'&LENGTH EQ 'N').SKIP2
.BADADD  ANOP
         MNOTE 4,'* ADDITIONAL AREA SPECIFIED IS INVALID.  IGNORED.'
         AGO   .SKIP4
.SKIP1   ANOP
&L       SETA  K'&LENGTH
         AIF   (&L GT 8).BADADD
         AGO   .SKIP3
.SKIP2   AIF   (&LENGTH GT 4095).BADADD
.SKIP3   LA    0,&LENGTH
         AGO   .GET1
.SKIP4   LA    0,72                 GETMAIN LENGTH GOES IN REG0
.GET1    GETMAIN R,LV=(0)
         LR    14,1
         LR    2,14
         AIF   (T'&LENGTH EQ 'U' OR T'&LENGTH EQ 'N').CLRMOR
         LA    15,72
         AGO   .CLEAR
.CLRMOR  LA    15,&LENGTH
.CLEAR   SR    1,1
         MVCL  14,0
         ST    13,4(2)        SET SAVEAREA BACK CHAIN
         ST    2,8(13)        SET SAVEAREA FORWARD CHAIN
         LR    13,2           LOAD LOCAL SAVEAREA ADDR IN 13
         L     1,4(13)        LOAD BACKWARD SAVEAREA ADDR
         LM    14,2,12(1)     RESTORE 14,15,0,1 & 2 FROM PREV SAVAREA
         SPACE
         MEND
