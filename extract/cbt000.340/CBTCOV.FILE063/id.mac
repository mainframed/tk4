         MACRO
&NFS     ID    &ID,&OPT,&DATE=,&TIME=
.*.
.*$MACRO=ID         DATE=06/15/77       SOURCE=LDW
.*  THIS MACRO WILL GENERATE A VARIABLE LENGTH ID FIELD WITH THE CSECT
.*       NAME, ASSEMBLY DATE, AND ASSEMBLY TIME.  IT IS USED AS AN
.*       INNER MACRO BY 'ENTR'.
.*.
.*  UPDATED 11-29-79 LDW    USE $$ASM MACRO FOR DATE
.*  UPDATED 03-17-79 LDW
.*  UPDATED 02-21-79 LDW
.*  UPDATED 01-03-79 LDW
         GBLC  &$$DATE1
         LCLC  &XDATE,&XTIME
         LCLA  &L
&L       SETA  K'&ID
         AIF   ('&OPT' EQ 'ASIS').ASIS
         AIF   ('&OPT' NE 'NODATE').DATE
.ASIS    ANOP
&NFS     DC    AL1(&L),C'&ID'
         MEXIT
.DATE    $$ASM
&XDATE   SETC  '&$$DATE1'
         AIF   ('&DATE' EQ '').OKDATE
&XDATE   SETC  '&DATE'                  USE WHAT HE WANTS
.OKDATE  ANOP
&L       SETA  &L+K'&XDATE+2
         AIF   ('&OPT' NE 'NOTIME').TIME
&NFS     DC    AL1(&L),C'&ID  &XDATE'
         MEXIT
.TIME    ANOP
&XTIME   SETC  '&SYSTIME'               DEFAULT TO CURRENT TIME
         AIF   ('&TIME' EQ '').OKTIME   HE DIDN'T SPECIFY, USE CURRENT
&XTIME   SETC  '&TIME'                  USE WHAT HE WANTS
.OKTIME  ANOP
&L       SETA &L+K'&XTIME+1             =1+8
         AIF   ('&OPT' NE '').BADOPT
&NFS     DC    AL1(&L),C'&ID  &XDATE &XTIME'
         MEXIT
.BADOPT  MNOTE 0,'*** (ID MACRO) OPT=&OPT IS INVALID, IGNORED.'
         MNOTE *,'(MUST BE NULL, "NOTIME", "NODATE", OR "ASIS")'
         MEND
