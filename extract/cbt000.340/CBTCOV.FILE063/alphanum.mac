         MACRO
&LABEL   ALPHANUM &CHAR
.*
.* ALPHANUM IS A MACRO THAT DETERMINES IF A CHARACTER IS A VALID
.* ALPHA-NUMERIC CHARACTER (0-9, A-Z, @, #, AND $).  THE ONLY
.* PARAMETER IS EITHER A RESOLVABLE LABEL, X(RX), OR (RX) NOTATION.
.*
.* PSW CC IS SET TO EQUAL (CC=0, MASK=8) IF THE CHARACTER IS VALID
.* PSW CC IS SET TO NOT EQUAL (CC^=0, MASK=7) IF CHARACTER IS INVALID.
.*
.* MACRO GENERATES REENTRANT CODE. NO AUXILLIARY STORAGE IS NEEDED.
.*
.* TOM FOTH, WSRCC, 04-AUG-81
.*
         LCLC  &CHARPTR
&CHARPTR SETC  '&CHAR'
         AIF   ('&CHAR'(1,1) NE '(').NOREG
&CHARPTR SETC  '0&CHAR'
.NOREG   ANOP
&LABEL   CLI   &CHARPTR,C'#'      IS IT THIS NATIONAL CHARACTER?
         BE    AC&SYSNDX          YEP
         CLI   &CHARPTR,C'@'      IS IT THIS NATIONAL CHARACTER?
         BE    AC&SYSNDX          YEP
         CLI   &CHARPTR,C'$'      IS IT THIS NATIONAL CHARACTER?
         BE    AC&SYSNDX          YEP
         TM    &CHARPTR,X'C0'     IS IT A C0 CHARACTER
         BNO   AB&SYSNDX         NO, ITS BAD
         CLI   &CHARPTR,X'E1'     IS IT 'E1'
         BE    AB&SYSNDX         YEP, THAT'S BAD
         TM    &CHARPTR,X'0F'     IS IT ZERO
         BNZ   AT&SYSNDX        NO
         CLI   &CHARPTR,X'F0'     IS IT F0?
         BNE   AB&SYSNDX         C0,D0,E0 ARE BAD
AT&SYSNDX TM    &CHARPTR,X'08'     IS 80 ON?
         BNO   AC&SYSNDX          NO, THEN IT'S OKAY
         TM    &CHARPTR,X'06'     EITHER MIDDLE BITS ARE BAD
         BZ    AC&SYSNDX          BUT NEITHER ON SO OKAY
AB&SYSNDX CLI   *,0             FORCE UNEQUAL CONDITION
         B     *+6
AC&SYSNDX CR    0,0             FORCE EQUAL CONDITION
         MEND
