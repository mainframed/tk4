         MACRO
&NAME    DCMSTOP &RC=,&WORK=YES,&SCREEN=NO,&LIT=YES,&IOCB=,&SERV=YES
.*.
.*$MACRO=DCMSTOP    DATE=09/00/79       SOURCE=SDC
.*
.*  THIS MACRO IS PART OF DCMS.  IT IS USED TO DO OS AND DCMS HOUSE-
.*  KEEPING AT ROUTINE TERMINATION.
.*
.*  UPDATES:
.*    11-24-82 LDW  CHANGE ALL R11 TO R1
.*    06-25-80 LDW  DELETE (UN-LOAD) "TERMIO"
.*.
         COPY  DCMSGBLS
         AIF   (NOT &LIST).LISTX
         EJECT
         SPACE 4
*                                                                     *
*                                                                     *
*        D C M S T O P  M A C R O                                     *
*                                                                     *
*                                                                     *
         SPACE 4
*                                                                     *
*        FUNCTION:                                                    *
*                   CREATES TERMINAL CODING (EXIT) FOR AN             *
*                   ASSEMBLY LANGUAGE MODULE:                         *
*                        1.   DELETE SAVE AREA (FREEMAIN)             *
*                        2.   REGISTER RESTORATION                    *
*                        3.   RETURN TO CALLER                        *
*                                                                     *
*        FORMAT:                                                      *
*                                                                     *
*              NAME  EPILOG                                           *
*                                                                     *
*              NOTE THAT THIS MACRO MUST BE USED WITH THE             *
*              - PROLOG -  MACRO                                      *
*                                                                     *
*                                                                     *
         MEXIT
.LISTX   AIF   (&PLG).CONT
         MNOTE 9,'PROLOG MACRO MUST PRECEED EPILOG MACRO'
         MEXIT
.CONT    ANOP
&NAME    DELETE  EPLOC==CL8'TERMIO'
&PLG     SETB  0
         AIF   ('&RC' EQ '').STOFF
&RCD     SETB  1
         AGO   .RCDX
.STOFF   ANOP
&RCD     SETB  0
.*MSRCD  L     11,4(13)                 GET OLD SA
DCMSRCD  L     1,4(,13)                 GET OLD SA
.*       XC    16(4,11),16(11)          SET RC = 0
         XC    16(4,1),16(1)            SET RC = 0
         AGO   .GENX
.*
.*
.RCDX    ANOP
.RTS1    SR    R15,R15
         AIF   (&RCD).GEN
         AGO   .GENX
.GEN     AIF   ('&RC' EQ 'ALL').GNALL
         AIF   ('&RC' EQ '(15)').GN15
         AIF   ('&RC' EQ '15').GN15
         AIF   ('&RC'(1,1) EQ '(').GNLMR
         AGO   .GENX
.GNLMR   ANOP
.*MSRCD  L     11,4(13)                 GET OLD SA
DCMSRCD  L     1,4(,13)                 GET OLD SA
.*       ST    &RC.,16(11)              STORE RC
         ST    &RC.,16(,1)              STORE RC
         AGO   .GENX
.GN15    ANOP
.*MSRCD  L     11,4(13)                 GET OLD SA
DCMSRCD  L     1,4(,13)                 GET OLD SA
.*       ST    15,16(11)                STORE RC
         ST    15,16(,1)                STORE RC
         AGO   .GENX
.GNALL   ANOP
.*MSRCD  L     11,4(13)                 GET OLD SA
DCMSRCD  L     1,4(,13)                 GET OLD SA
.*       STM   15,1,16(11)              STORE R0,R1,R15
         STM   15,1,16(,1)              STORE R0,R1,R15
.GENX    CLI   IOCBTERM+1,X'7F'         WAS STFSMODE DONE?         LDW1
         BNE   IHB&SYSNDX.X             NO - SKIP                  LDW1
*        STFSMODE OFF                   YES - TURN IT OFF          LDW1
         STFSMODE OFF                   YES - TURN IT OFF          LDW1
         MVI   IOCBTERM+1,0             RESET FLAG                 LDW1
IHB&SYSNDX.X DS 0H                                                 LDW1
         AIF   (&RENT).RENT
         AGO   .NORENT
.RENT    LR    1,13
         L     13,4(,13)
         FREEMAIN R,LV=&LVNUM,A=(1)
         RETURN (14,12),T
         SPACE 4
****         ****
*   REENTRANT   *
****         ****
         SPACE 2
         AGO   .END
.NORENT  L     13,4(,13)
         RETURN (14,12),T
         LTORG
         SPACE 4
.END     ANOP
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*     D C M S    S T A N D A R D    E X I T    L I N K A G E          *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         AIF   ('&SERV' NE 'YES').NOSERV
&DCMSCLR SETB  1
&DCMSDEC SETB  1
&DCMSHEX SETB  1
         DCMSERV
         EJECT
.NOSERV  AIF   ('&LIT' NE 'YES').NOLIT
         LTORG
         EJECT
.NOLIT   ANOP
&DCMSCRN SETB   0
         AIF  ('&SCREEN' NE 'YES').NOSCR
&DCMSCRN SETB  1
.NOSCR   ANOP
         AIF  ('&WORK' NE 'YES').NOWRK
&DCMSWRK SETB  1
         DCMSWORK IOCB=&IOCB
.NOWRK   EJECT
         MEND
