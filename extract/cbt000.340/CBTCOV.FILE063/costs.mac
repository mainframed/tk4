COSTS    CSECT                         DEFINE CSECT
***********************************************************************
*
*    ROUTINE NAME ---------> COSTS
*
*    ENTRY POINTS ---------> -NONE-
*
*    EXTERNAL REFERENCES --> T90TMX
*
*    PARMS ----------------> -NONE-
*
*    DATE LAST MODIFIED ---> 04/02/82
*
*    ROUTINE DESCRIPTION:
*
*    THIS ROUTINE DISPLAYS CPU TIME, EXCPS, SYSTEM SECONDS
*    FOR THE CURRENT TSO SESSION.
*
***********************************************************************
*
*     REGISTER EQUATES AND USAGE
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      BASE REG
R13      EQU   13                      WORKAREA BASE REG AND SAVE AREA
R14      EQU   14                      RETURN ADDR
R15      EQU   15                      RETURN CODE
F0       EQU   0
F2       EQU   2
F4       EQU   4
F6       EQU   6
*
*     HOUSEKEEPING
*
         SAVE  (14,12),,*              SAVE REGISTERS
         LR    R12,R15                 SET REG 12 TO ENTRY ADDR
         USING COSTS,R12               ESTABLISH ADDRESSABILITY
         LR    R10,R1                  SAVE R1
         LR    R2,R13                  SAVE ADDR OF CALLING SAVE AREA
         GETMAIN RU,LV=WORKSIZE        GET SOME CORE
         LR    R13,R1                  SAVE ADDR OF CORE OBTAINED
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         ST    R2,SAVEAREA+4           STORE ADDR OF CALLING SAVE AREA
         ST    R13,8(R2)               STORE ADDR OF MY SAVE AREA
*
*     FETCH LOGONID
*
         L     R2,548(R0)              GET MY ASCB ADDR
         L     R2,176(R2)              LOAD MY USERID ADDR
         MVC   LINE(L'MSG1),MSG1       STUFF IN THE CONSTANTS
         MVC   LINE+24(8),0(R2)        SAVE MY USERID
         TPUT  LINE,L'MSG1
         MVC   LINE(L'MSG2),MSG2       STUFF IN THE CONSTANTS
         TPUT  LINE,L'MSG2
*
*     GET THE BASIC INGREDIENTS
*
         LA   R1,CPUSEC                LOAD ADDR OF PARM
         ST   R1,PARMLIST              STORE ADDR IN PARMLIST
         LA   R1,EXCPS                 LOAD ADDR OF PARM
         ST   R1,PARMLIST+4            STORE ADDR IN PARMLIST
         LA   R1,SYSTSEC               LOAD ADDR OF PARM
         ST   R1,PARMLIST+8            STORE ADDR IN PARMLIST
         OI   PARMLIST+8,X'80'         JUST TO BE NEAT
         LA   R1,PARMLIST              NOW POINT TO THE LIST
         LINK EP=T90TMX
*
*     MAKE IT READABLE
*
         LE    F0,CPUSEC
         ME    F0,=E'100'              CONVERT TO HUNDREDTHS
         AE    F0,=E'0.5'              INCLUDE ROUNDING FACTOR
         BAL   R11,CONVERT
         CVD   R0,DBLWORD              CONVERT TO DECIMAL
         MVC   LINE(L'MSG3),MSG3       STUFF IN THE CONSTANTS
         MVC   LINE+17(10),=X'4020206B2021204B2020' EDIT PATTERN
         ED    LINE+17(10),DBLWORD+4   EDIT THE BEAST
         TPUT  LINE,L'MSG3
         LE    F0,EXCPS
         BAL   R11,CONVERT
         CVD   R0,DBLWORD              CONVERT TO DECIMAL
         MVC   LINE(L'MSG4),MSG4       STUFF IN THE CONSTANTS
         MVC   LINE+17(10),=X'40206B2020206B202120' EDIT PATTERN
         ED    LINE+17(10),DBLWORD+4   EDIT THE BEAST
         TPUT  LINE,L'MSG4
         LE    F0,SYSTSEC
         ME    F0,=E'100'              CONVERT TO HUNDREDTHS
         AE    F0,=E'0.5'              INCLUDE ROUNDING FACTOR
         BAL   R11,CONVERT
         CVD   R0,DBLWORD              CONVERT TO DECIMAL
         MVC   LINE(L'MSG5),MSG5       STUFF IN THE CONSTANTS
         MVC   LINE+17(10),=X'4020206B2021204B2020' EDIT PATTERN
         ED    LINE+17(10),DBLWORD+4   EDIT THE BEAST
         TPUT  LINE,L'MSG5
*
*     TERMINATE
*
RETURN0  XR    R15,R15                 SET RETURN CODE TO ZERO
RETURN   LR    R1,R13                  SAVE PTR TO GETMAINED SAVE AREA
         LR    R2,R15                  SAVE RETURN CODE
         L     R13,SAVEAREA+4          LOAD ADDR OF CALLING SAVE AREA
         FREEMAIN R,LV=WORKSIZE,A=(R1) FREE THE CORE WE GOT
         LR    R15,R2                  PUT RETURN CODE IN R15
         L     R14,12(R13)             RESTORE R14
         LM    R0,R12,20(R13)          RESTORE R0 THROUGH R12
         BR    R14                     RETURN
*
*     ROUTINE TO CONVERT SHORT FLOATING NUMBER (F0) TO INTEGER (R0)
*
CONVERT  EQU   *
         LD    F2,=D'0'                CLEAR ENTIRE REGISTER
         LER   F2,F0                   LOAD NUMBER
         AW    F2,=X'4E00000100000000' ZAP, IT'S AN INTEGER
         STD   F2,DBLWORD              SAVE THE WHOLE THING
         L     R0,DBLWORD+4            LOAD THE LOW 4 BYTES
         BR    R11                     AND RETURN
*
*     CONSTANTS AND STORAGE RESERVATIONS
*
         LTORG
MSG1     DC    C'SESSION UTILIZATION FOR XXXXXXXX'
MSG2     DC    C' '
MSG3     DC    C'CPU SECONDS       XX,XXX.XX'
MSG4     DC    C'EXCPS             X,XXX,XXX'
MSG5     DC    C'SYSTEM SECONDS    XX,XXX.XX'
*
*     DSECTS
*
WORKAREA DSECT
SAVEAREA DS    18F
PARMLIST DS    3F
CPUSEC   DS    E
EXCPS    DS    E
SYSTSEC  DS    E
DBLWORD  DS    D
LINE     DS    CL80
WORKSIZE EQU   *-WORKAREA
         END   COSTS
