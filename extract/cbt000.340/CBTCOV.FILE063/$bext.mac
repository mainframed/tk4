         MACRO
         $BEXT &L1,&L2
         AIF   ('&L1' EQ '').BEXT300
         AIF   ('&L1'(1,1) NE '(').BEXT300
         AIF   ('&L1'(2,1) LT 'A').BEXT10
         AIF   ('&L1'(2,1) GT 'Z').ERR0
         AGO   .BEXT20
.BEXT10  ANOP
         AIF   ('&L1'(2,1) EQ '@').BEXT20
         AIF   ('&L1'(2,1) EQ '#').BEXT20
         AIF   ('&L1'(2,1) NE '$').ERR0
.BEXT20  ANOP
         AIF   (K'&L1(1) EQ 0).ERR1
         AIF   (K'&L1(1) GT 8).ERR1
         DC    CL8'&L1(1)'       EXIT ROUTINE MEMBER NAME
         AIF   ('&L1(4)' NE '').BEXT30
         AIF   ('&L1(3)' NE '').BEXT40
         AIF   ('&L1(2)' NE '').BEXT50
         AGO   .ERR2
.BEXT30  ANOP
         AIF   ('&L1(2)' EQ '&L1(4)' OR '&L1(3)' EQ '&L1(4)').ERR3
.BEXT40  AIF   ('&L1(2)' EQ '&L1(3)').ERR3
.BEXT50  ANOP
         AIF   ('&L1(4)' EQ '').BEXT60
         AIF   ('&L1(4)' EQ 'CB' OR '&L1(4)' EQ 'FA').BEXT60
         AIF   ('&L1(4)' NE 'OP').ERR4
.BEXT60  ANOP
         AIF   ('&L1(3)' EQ '').BEXT70
         AIF   ('&L1(3)' EQ 'CB' OR '&L1(3)' EQ 'FA').BEXT70
         AIF   ('&L1(3)' NE 'OP').ERR4
.BEXT70  ANOP
         AIF   ('&L1(2)' EQ 'CB' OR '&L1(2)' EQ 'FA').BEXT80
         AIF   ('&L1(2)' NE 'OP').ERR4
.BEXT80  ANOP
         AIF   ('&L1(4)' NE '').BEXT90
         AIF   ('&L1(3)' NE '').BEXT100
         AGO   .BEXT110
.BEXT90  ANOP
         AIF   ('&L1(4)' EQ 'CB').BEXT120
.BEXT100 AIF   ('&L1(3)' EQ 'CB').BEXT120
.BEXT110 AIF   ('&L1(2)' EQ 'CB').BEXT120
         DC    C'0'              NO COMMAND BUFFER EXIT
         AGO   .BEXT130
.BEXT120 DC    C'1'              CALL COMMAND BUFFER EXIT
.BEXT130 ANOP
         AIF   ('&L1(4)' NE '').BEXT140
         AIF   ('&L1(3)' NE '').BEXT150
         AGO   .BEXT160
.BEXT140 ANOP
         AIF   ('&L1(4)' EQ 'FA').BEXT170
.BEXT150 AIF   ('&L1(3)' EQ 'FA').BEXT170
.BEXT160 AIF   ('&L1(2)' EQ 'FA').BEXT170
         DC    C'0'              NO FILE ALLOCATION EXIT
         AGO   .BEXT180
.BEXT170 DC    C'1'              CALL FILE ALLOCATION EXIT
.BEXT180 ANOP
         AIF   ('&L1(4)' NE '').BEXT190
         AIF   ('&L1(3)' NE '').BEXT200
         AGO   .BEXT210
.BEXT190 ANOP
         AIF   ('&L1(4)' EQ 'OP').BEXT220
.BEXT200 AIF   ('&L1(3)' EQ 'OP').BEXT220
.BEXT210 AIF   ('&L1(2)' EQ 'OP').BEXT220
         DC    C'0'              NO OUTPUT EXIT
         AGO   .BEXT340
.BEXT220 AIF   ('&L2' NE 'G').ERR5
         DC    C'1'              CALL OUTPUT EXIT
         AGO   .BEXT340
.ERR0    ANOP
         MNOTE 12,'EXIT ROUTINE NAME''S 1ST CHARACTER MUST BE ALPHABETIX
               C OR NATIONAL CHARACTER'
         AGO   .BEXT300
.ERR1    ANOP
         MNOTE 12,'EXIT ROUTINE NAME EXCEEDS 8 CHARACTERS'
         AGO   .BEXT300
.ERR2    ANOP
         MNOTE 12,'NO EXIT ROUTINE SYMBOL(S) SPECIFIED'
         AGO   .BEXT310
.ERR3    ANOP
         MNOTE 12,'DUPLICATE EXIT ROUTINE SYMBOLS'
         AGO   .BEXT310
.ERR4    ANOP
         MNOTE 12,'UNKNOWN EXIT ROUTINE SYMBOL'
         AGO   .BEXT310
.ERR5    ANOP
         MNOTE 12,'EXIT ROUTINE SYMBOL ''OP'' ONLY VALID WITH KEYWORD 'X
               'EXITLG'''
         AGO   .BEXT330
.BEXT300 DC    CL8' '            NO EXIT ROUTINE NAME
.BEXT310 DC    C'0'              NO COMMAND BUFFER EXIT
.BEXT320 DC    C'0'              NO FILE ALLOCATION EXIT
.BEXT330 DC    C'0'              NO OUTPUT EXIT
.BEXT340 DC    5C'0'             **RESERVED FOR FUTURE EXITS**
.BEXTEND MEND
