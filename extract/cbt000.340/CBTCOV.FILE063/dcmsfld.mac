         MACRO
&NAME    DCMSFLD &LEN,&TYPE,&INTEN=,&MDT=OFF,&CURSOR=NO,&ROW=,&COLUMN=
.*.
.*$MACRO=DCMSFLD    DATE=02/04/79       SOURCE=LDW
.* MACRO=FIELD      DATE=09/00/79       SOURCE=SDC
.*
.*  THIS MACRO IS PART OF DCMS.  IT IS USED TO DEFINE A FIELD IN THE
.*  THE DISPLAY SCREEN.
.*  (NOTE: THIS WAS THE "FIELD" MACRO)
.*.
         GBLA  &SFCOL,&SFROW,&SFLEN,&SFCNT
         GBLA  &SFLTBL(200),&SFSTRT(20),&SFEND(20),&SFFCT,&SFNCT
         GBLB  &SFNUM(200),&SFFLAG,&SFCUR
         GBLC  &SFFNAM(200),&SFNAM(20)
         LCLA  &I,&J,&K
         AIF   (&SFFLAG).INITOK
         MNOTE 8,'OUT OF SEQUENCE'
         MEXIT
.INITOK  ANOP
.*LDW    AIF   (T'&LEN NE 'N').E1
         AIF   (&LEN LT 1 OR &LEN GT 1900).E1
         AIF   ('&CURSOR' EQ 'NO' OR '&CURSOR' EQ 'YES' OR '&CURSOR' EQX
                'AFTER').CUROK
         MNOTE 4,'CURSOR=&CURSOR ASSUMED TO MEAN CURSOR=YES'
.CUROK   AIF   ('&CURSOR' EQ 'NO').NOCUR
         AIF   (NOT &SFCUR).FIRSTC
         MNOTE 4,' ONLY ONE FIELD MAY HAVE A CURSOR IN IT'
         AGO   .NOCUR
.FIRSTC  ANOP
&SFCUR   SETB  1
         AIF   ('&CURSOR' EQ 'AFTER').CURAFT
         DC    X'80'              FLAGS (CURSOR IN FIELD)
         AGO   .ENDCUR
.CURAFT  DC    X'C0'              FLAGS (CURSOR AFTER DATA IN FIELD)
         AGO   .ENDCUR
.NOCUR   DC    X'00'              FLAGS
.ENDCUR  ANOP
         AIF   (T'&TYPE EQ 'O' OR '&TYPE' EQ 'DISPLAY').DISP
         AIF   ('&TYPE' EQ 'ALPHA').ALPHA
         AIF   ('&TYPE' EQ 'NUMERIC').NUM
         MNOTE 8,' INVALID SECOND ARGUMENT (&TYPE) IGNORED'
         AGO   .DISP
.NUM     ANOP
&I       SETA  16
.ALPHA   AIF   (T'&INTEN NE 'O').CKINT
&I       SETA  &I+4
         AGO   .EINT
.DISP    ANOP
&I       SETA  48
         AIF   (T'&INTEN NE 'O').CKINT
&I       SETA  56
         AGO   .EINT
.CKINT   AIF   ('&INTEN' NE 'OFF').INT1
&I       SETA  &I+12
         AGO   .EINT
.INT1    AIF   ('&INTEN' NE 'LO').INT2
&I       SETA  &I+4
         AGO   .EINT
.INT2    AIF   ('&INTEN' EQ 'HI').INT3
         MNOTE 4,' INTEN=&INTEN ASSUMED TO MEAN INTEN=HI'
.INT3    ANOP
&I       SETA  &I+8
.EINT    ANOP
         AIF   ('&MDT' EQ 'OFF' OR '&MDT' EQ 'ON').MDTOK
         MNOTE 4,' MDT=&MDT ASSUMED TO MEAN MDT=ON'
.MDTOK   AIF   ('&MDT' EQ 'OFF').EMDT
&I       SETA  &I+1
.EMDT    ANOP
&J       SETA  &I-(&I/16)*16
&I       SETA  &I+64
         AIF   (&J GT 9 OR &J EQ 0 OR &I EQ 97).SPEC
&I       SETA  &I+128
.SPEC    DC    AL1(&I)            ATTRIBUTE BYTE
.*
.*       NOW WE MUST FIND THE COLUMN AND ROW THAT THE DATA START IN.
.*       NOTE THAT THE BYTE IN FRONT OF THERE MUST BE FREE FOR THE
.*             ATTRIBUTE BYTE.
.*
&I       SETA  &SFROW
         AIF   (T'&ROW EQ 'O').NOROW
         AIF   (T'&ROW EQ 'N').ROWOK
.BADROW  MNOTE 8,' INVALID ROW=&ROW IGNORED'
&I       SETA  &SFROW
         AGO   .NOROW
.ROWOK   ANOP
         AIF   (&I LT 1 OR &I GT 24).BADROW
&I       SETA  &ROW
         AIF   (&I EQ &SFROW).NOROW
         AIF   (&I GT &SFROW).NEWROW
         MNOTE 8,' ROW=&ROW SPECIFIES ROW ABOVE CURRENT ROW (&SFROW)'
&I       SETA  &SFROW
         AGO   .NOROW
.NEWROW  ANOP
&SFCOL   SETA  1
.NOROW   ANOP
&J       SETA  &SFCOL
         AIF   (T'&COLUMN EQ 'O').NOCOL
         AIF   (T'&COLUMN EQ 'N').COLOK
.BADCOL  MNOTE 8,' INVALID COLUMN=&COLUMN IGNORED'
&J       SETA  &SFCOL
         AGO   .NOCOL
.COLOK   ANOP
&J       SETA  &COLUMN
         AIF   (&J LT 1 OR &J GT 80).BADCOL
         AIF   (&J GE &SFCOL).NOCOL
         AIF   (T'&ROW EQ 'O').BUMPROW
         MNOTE 8,' COLUMN=&COLUMN CAUSES FIELD OVERLAP'
&J       SETA  &SFCOL
         AGO   .NOCOL
.BUMPROW ANOP
&I       SETA  &I+1
.NOCOL   ANOP
         MNOTE *,' &LEN BYTE FIELD IN ROW &I COLUMN &J'
&K       SETA  (&I-1)*80+&J-1
         DC    Y(&K,&SFLEN,&LEN)  BUFF ADDR, IMAGE OFFSET, LENGTH
&SFROW   SETA  &I
&SFCOL   SETA  &J+&LEN+1
.RCTST   AIF   (&SFCOL LE 80).RCUPD
&SFROW   SETA  &SFROW+1
&SFCOL   SETA  &SFCOL-80
         AGO   .RCTST
.RCUPD   ANOP
&SFLEN   SETA  &SFLEN+&LEN
&SFCNT   SETA  &SFCNT+1
&SFFCT   SETA  &SFFCT+1
         AIF   (&SFFCT GT 200).ERR
&SFLTBL(&SFFCT) SETA &LEN
&SFNUM(&SFFCT) SETB ('&TYPE' EQ 'NUMERIC')
&SFFNAM(&SFFCT) SETC '&NAME'
         SPACE 1
         MEXIT
.E1      MNOTE 8,'INVALID LENGTH SPECIFIED'
         MEXIT
.ERR     MNOTE 12,'*** TOO MANY FIELDS TOTAL IN ALL FORMATS ***'
         MEND
