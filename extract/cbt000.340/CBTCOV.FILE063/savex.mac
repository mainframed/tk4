         MACRO
&NAME    SAVEX &REG=,&AREA=,&ID=
         LCLA  &A,&C
         LCLC  &B,&D
         AIF   (K'&ID EQ 0).TCSECT
         AIF   ('&ID' EQ '*').SPCLID
&A       SETA  K'&ID
&B       SETC  '&ID'
         AGO   .CALEN
.SPCLID  AIF   (K'&NAME EQ 0).ERR
&A       SETA  K'&NAME
&B       SETC  '&NAME'
         AGO   .CALEN
.ERR     ANOP
********* NAME NOT SPECIFIED, CSECT NAME USED
.TCSECT  ANOP
         AIF   ('&SYSECT' EQ '').ERROR
&B       SETC  '&SYSECT'
&A       SETA  1
.COUNT   AIF   ('&B'(1,&A) EQ '&B').CALEN
&A       SETA  &A+1
         AGO   .COUNT
.CALEN   ANOP
&C       SETA  ((&A+2)/2)*2+4
&NAME    B     &C.(0,15) -     BRANCH AROUND ID
         DC    AL1(&A) -      SET ID LENGTH
         DC    CL&A'&B' -   IDENTIFIER
         AGO   .OUT
.DEFLTA  ANOP
&B       SETC  'SAVE'.'&SYSNDX'
         AGO   .NEXT
.DEFLTR  ANOP
&D       SETC  '10'
         AGO   .NXT
.ERROR   MNOTE 8,'NO CSECT NAME'
.OUT     ANOP
         AIF   (K'&AREA EQ 0).DEFLTA
&B       SETC  '&AREA'
.NEXT    AIF   (K'&REG EQ 0).DEFLTR
&D       SETC  '&REG'
.NXT     ANOP
         DS    0H
         STM   14,12,12(13) -SAVE ALL REGISTERS
         BALR  &D,0 -        SET UP MAIN PROGRM BASE REGISTER
         USING *,&D
         AIF   ('&D' EQ '15').SET10
&D       SETC  '15'
         AGO   .CODE
.SET10   ANOP
&D       SETC  '10'
.CODE    LA    &D,&B -     POINTER TO THIS PROGRAMS SAVE AREA
         ST    &D,8(13) -    STORE IN CALLING PROGRAMS SAVE AREA
         ST    13,&B+4     - SAVE POINTER TO CALLING PROG SAVE AREA
         LR    13,&D -       PUT POINTER TO CURRENT SAVE AREA IN R13
         AIF   (K'&AREA EQ 0).M1
         MEXIT
.M1      B     A&SYSNDX -       BRANCH AROUND SAVE AREA
&B       DC    18F'0' -      CURRENT PROGRAM DEFAULT SAVE AREA
A&SYSNDX EQU   *
         MEXIT
         MEND
