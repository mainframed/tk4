         MACRO
&N       TSBEGIN &BLOCKS,&BASE=12,&REGS=YES,&WORK=
         COPY  MATGBL
         LCLC  &R,&BA,&OLDBA
         LCLC  &NAME1,&NAME
         LCLA  &A,&B,&C,&KL
         AIF   ('&SYSECT' EQ '').TRYNAME
&NAME    SETC  '&SYSECT'
         AGO   .AROUND
.TRYNAME AIF   ('&N' EQ '').NONE
&NAME    SETC  '&N'
         AGO   .AROUND
.NONE    ANOP
&NAME    SETC  'CODE'.'&SYSNDX'
.AROUND  ANOP
&NAME    CSECT
         AIF   ('&MATTSD2' NE '').NOWK
         AIF   ('&WORK' EQ '').NOWK
&MATTSD2 SETC  '&WORK'
.NOWK    ANOP
         AIF   ('&REGS'(1,1) NE 'Y').NOREGS
         TSDSECT REGS
&MATREGS SETC  'R'                      SET THE GLOBAL REG SW
&R       SETC  'R'                      SET LOCAL REG SW
.NOREGS  ANOP
         AIF   (&MATTSB1).NOTFST
         TSDSECT DAPL,DAECB,CPPL,CPECB,IOPL,PPL,ADAIR,DAPB,            +
               SAV2,BASESAVE
.NOTFST  ANOP
&KL      SETA  K'&NAME
&A       SETA  N'&SYSLIST
&B       SETA  1
.LOOP    AIF   (&B GT &A).ELOOP
         TSDSECT &SYSLIST(&B)
&B       SETA  &B+1
         AGO   .LOOP
.ELOOP   ANOP
         AIF   ('&NAME' EQ '').NOCS
&NAME    CSECT
&NAME1   SETC  '&NAME'
         AGO   .SAVE
.NOCS    ANOP
&NAME1   SETC  'IHB&SYSNDX.A'           GEN DUMMY NAME
&NAME1   EQU   * .                      ASSUMED EPA
.SAVE    ANOP
         STM   &R.14,&R.12,12(&R.13) .         SAVE CALLERS REGS
         AIF   ('&NAME' EQ '').TRYSYS
&MATSECT SETC  '&NAME'
         AGO   .GNAME
.TRYSYS  ANOP
         AIF   ('&SYSECT' EQ '').NONAME
&MATSECT SETC  '&SYSECT'
&KL      SETA  1
.NLOOP   AIF   ('&MATSECT'(1,&KL) EQ '&MATSECT').GNAME
&KL      SETA  &KL+1
         AGO   .NLOOP
.GNAME   ANOP
&A       SETA  ((&KL+2)/2)*2+4+4
         B     &A.(,&R.15) .            BRANCH AROUND NAME
         DC    AL1(&KL) .               LEN OF NAME
         DC    C'&MATSECT' .            SECTION NAME
.NONAME  ANOP
&A       SETA  N'&BASE
&MATTSB2 SETA  &A                       PASS IT TO TSDSECT
         AIF   (&A LT 1 OR &A GT 15).ER1
&BA      SETC  '&BASE(1)'
         AIF   (T'&BASE(1) NE 'N').NOR1
&BA      SETC  '&R&BA'                  SET PREFIX IF ANY
.NOR1    ANOP
         LR    &BA,&R.15 .              SET FIRST BASE REG
         LUSE  &NAME1,&BA .             TELL ASSEMBLER
         AIF   (&A LT 2).ONEBASE
         LA    &R.15,4095 .             SET INCREMENT
&B       SETA  2
.BASLOOP AIF   (&B GT &A).ONEBASE
&OLDBA   SETC  '&BA'
&C       SETA  &C+4096
&BA      SETC  '&BASE(&B)'
         AIF   (T'&BASE(&B) NE 'N').NOR2
&BA      SETC  '&R&BA'
.NOR2    ANOP
         LA    &BA,1(&R.15,&OLDBA) .    ESTABLISH NEXT BASE
         LUSE  &NAME1+&C,&BA .          TELL ASSEMBLER
&B       SETA  &B+1
         AGO   .BASLOOP
.ONEBASE ANOP
&MATTSD1 SETC  'IHB&SYSNDX.Z'
         L     &R.15,IHB&SYSNDX.Y .     GET ADDR OF INIT CODE
         BALR  &R.14,&R.15 .            GO INITIALIZE
         LUSE  TSDSECT,&R.13 .          CP WORK AREA
         B     IHB&SYSNDX.Y+4 .         GO TO USERS CODE
         SPACE 2
IHB&SYSNDX.Y DC A(IHB&SYSNDX.Z) .       A(INITIALIZATION)
         SPACE 2
         INUSE
         MEXIT
.ER1     MNOTE 4,'INVALID NUMBER OF BASES GIVEN'
.END     MEND
