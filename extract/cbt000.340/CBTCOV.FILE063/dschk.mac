         MACRO
&NAME    DSCHK &LOC,&TYPE=OUTPUT,&USE=LINK
         LCLC  &N,&R
&N       SETC  '&NAME'
         AIF   ('&LOC' EQ '').E1
         AIF   ('&LOC'(1,1) EQ '(').REGTYPE
&N       LA    1,&LOC(1) .              GET ADDRESS OF DSNAME
&N       SETC  ''
         AGO   .TYPE
.E1      MNOTE 12,'DSNAME ADDRESS IS MISSING.  REQUIRED OPERAND.'
         MEXIT
.REGTYPE AIF   (('&LOC(1)' EQ '1')OR('&LOC(1)' EQ 'R1')).TYPE
&N       LR    1,&LOC(1) .              LOAD REGISTER CONTAINING DSNAME
&N       SETC  ''
.TYPE    AIF   ('&TYPE' EQ 'INPUT').USE
         AIF   ('&TYPE' EQ 'OUTPUT').TOUT
         AIF   ('&TYPE' NE '').TERR
         MNOTE 4,'TYPE= OMITTED, TYPE=OUTPUT ASSUMED'
         AGO   .TOUT
.TERR    MNOTE 8,'TYPE=&TYPE INVALID, TYPE=OUTPUT ASSUMED'
.TOUT    ANOP
&N       LNR   1,1 .                    INDICATE TYPE=OUTPUT
&N       SETC  ''
.USE     AIF   ('&USE(1)' EQ 'LINK').LINK
         AIF   ('&USE(1)' EQ 'BAL').BAL
         AIF   ('&USE(1)' EQ 'BALR').BALR
         MNOTE 12,'USE=&USE ILLEGAL, USE=LINK ASSUMED'
.LINK    ANOP
&N       LINK  EP=DSNCHECK,MF=(E,(1))
         MEXIT
.BALR    ANOP
&R       SETC  'R'
.BAL     AIF   ('&USE(2)' NE '').DOIT
         MNOTE 16,'USE=&USE MUST HAVE SECOND OPERAND, USE=LINK ASSUMED'
         AGO   .LINK
.DOIT    ANOP
&N       BAL&R 14,&USE(2) .             CALL DSNCHECK ROUTINE
         MEND
