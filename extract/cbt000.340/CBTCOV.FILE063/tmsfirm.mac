         MACRO
&LABEL   TMSFIRM
         LCLC  &ALL(35),&CON(5)                                   40194
         LCLA  &DBLS(35),&TOTDBLS NBR DOUBLE &'S AND QUOTES       40194
         LCLA  &NALL,&NCON,&LMAX
         LCLA  &M,&I,&X,&L,&K,&Y,&Q,&TL
         LCLA  &LN                                                40011
         LCLB  &NSPREAD
         LCLC  &CC
&CC      SETC  '-'
&NALL    SETA  35                                                 40194
&NCON    SETA  5
&LMAX    SETA  133
*        TMSFIRM MACRO        UCC-ONE (TMS)    12-23-74    CHG-LVL 4.0
.*
&LABEL   DS    0C
.I02     ANOP
&M       SETA  &M+1
         AIF   (&M GT 3).I75
         AIF   (N'&SYSLIST(&M) EQ 0).I52
&Q       SETA  1
.I04     ANOP
&ALL(&Q) SETC  ' '
&DBLS(&Q) SETA 0                  FORMAT DOUBLE CHARACTERS
&Q       SETA  &Q+1
         AIF   (&Q LE &NALL).I04
&Q       SETA  1
&TOTDBLS SETA  0                  FORMAT TOTAL DOUBLE CHARACTERS
.*
&K       SETA  K'&SYSLIST(&M)-1
&I       SETA  2
         AIF   ('&SYSLIST(&M)'(1,1) EQ '''').I06
&I       SETA  &I-1
&K       SETA  &K+1               ACTUAL COUNT
.*
.I06     ANOP
&X       SETA  1
.I08     ANOP
&CON(&X) SETC  ' '
&X       SETA  &X+1
         AIF   (&X LE &NCON).I08
&X       SETA  1
&Y       SETA  0                  RESET LENGTH IN &CON
.*
.I10     ANOP
         AIF   (&I+1 GT &K).I26
         AIF   ('&SYSLIST(&M)'(&I+1,1) GE 'A' AND                      *
               '&SYSLIST(&M)'(&I+1,1) LE 'Z').I16
         AIF   ('&SYSLIST(&M)'(&I+1,1) GE '0' AND                      *
               '&SYSLIST(&M)'(&I+1,1) LE '9').I16
         AIF   ('&SYSLIST(&M)'(&I+1,1) EQ '.').I11B
         AIF   ('&SYSLIST(&M)'(&I+1,1) EQ ',').I11B
         AIF   ('&SYSLIST(&M)'(&I+1,1) EQ ';').I11B
         AIF   ('&SYSLIST(&M)'(&I+1,1) EQ ' ').I11B
         AIF  ('&SYSLIST(&M)'(&I,2) EQ '&&').I11A
         AIF   ('&SYSLIST(&M)'(&I,2) EQ '''''').I11A
         AIF  ('&SYSLIST(&M)'(&I,1) EQ '''').I26
         AGO   .I16
.*
.I11A    ANOP
         AIF   (&Q NE 5 AND &Q NE 10).I11A2                       40011
         AIF   (&Y LT 7).I11A2                                    40011
&LN      SETA  &Y       PREVENT SEPARATING DOUBLE CHARACTERS      40011
         AGO   .I21                                               40011
.I11A2   ANOP                                                     40011
&DBLS(&Q) SETA &DBLS(&Q)+1        INCREMENT DOUBLE CHARACTERS
&TOTDBLS SETA  &TOTDBLS+1         INCREMENT TOTAL DOUBLES
         AIF   (&I+2 LE &K).I11B                                  40194
&L       SETA  0                                                  40194
         AGO   .I12                                               40194
.I11B    ANOP
.*
&L       SETA  2
         AIF   ('&SYSLIST(&M)'(&I+1,1) EQ ' ').I12
&L       SETA  1
.I12     ANOP
&CON(&X) SETC  '&SYSLIST(&M)'(&I,2)'  '(1,&L)
&Y       SETA  &Y+&L+2
&L       SETA  &L+2
&I       SETA  &I+2
         AGO   .I18
.*
.I16     ANOP
&CON(&X) SETC  '&SYSLIST(&M)'(&I,1)' '
&Y       SETA  &Y+2
&I       SETA  &I+1
&L       SETA  1+1
.*
.I18     ANOP
         AIF   (&Y GE 8).I20
&X       SETA  &X+1
         AGO   .I10
.*
.I20     ANOP
&LN      SETA  8                  LENGTH FOR SETC                 40011
.I21     ANOP                                                     40011
&ALL(&Q) SETC  '&CON(1)&CON(2)&CON(3)&CON(4)&CON(5)'(1,&LN)       40011
&Q       SETA  &Q+1
&Y       SETA  &Y-&LN                                             40011
         AIF   (&Y LE 0).I06
.*
.I22     ANOP
&CON(1)  SETC  '&CON(&X)'(&L-&Y+1,&Y)
&X       SETA  2
.I24     ANOP
&CON(&X) SETC  ' '
&X       SETA  &X+1
         AIF   (&X LE &NCON).I24
&X       SETA  2
         AGO   .I10
.*
.I26     ANOP
         AIF   (&I GT &K).I26B                                    40194
         AIF   ('&SYSLIST(&M)'(&I,1) NE '''').I27A
.I26B    ANOP                                                     40194
&Y       SETA  &Y-1
         AGO   .I27B
.I27A    ANOP
&CON(&X) SETC  '&SYSLIST(&M)'(&I,1)
.I27B    ANOP
&ALL(&Q) SETC  '&CON(1)&CON(2)&CON(3)&CON(4)&CON(5)'(1,8)         40011
&TL      SETA  8*(&Q-1)+&Y+1-&TOTDBLS
.*
         AIF   (&NSPREAD).I28
.*                                                                40194
         AIF   (&TL LE 100).I32
.* IF A LINE IS TOO LARGE TO DOUBLE SPACE, THEN IT AND FOLLOWING  40194
.* LINES WILL NOT BE DOUBLE SPACED.
&NSPREAD SETB  1
.I28     ANOP
&TL      SETA  (&TL+1)/2                                          40194
         AGO   .I32                                               40194
.I29     ANOP                                                     40194
         AIF   ('&SYSLIST(&M)'(1,1) EQ '''').NOQUO                40194
         DC    CL&TL'&SYSLIST(&M)'                                40194
         AGO   .I48                                               40194
.NOQUO   ANOP                                                     40194
         DC    CL&TL&SYSLIST(&M)                                  40194
         AGO   .I48                                               40194
.*
.I32     ANOP
&L       SETA  (&LMAX-&TL)/2
         AIF   (&L LE 0).I34
         DC    CL&L'&CC '
&CC      SETC  ' '
.I34     ANOP
         AIF   (&NSPREAD).I29                                     40194
&X       SETA  1
&Y       SETA  &TL+&TOTDBLS                                       40011
.I36     ANOP
         AIF   (&Y LE 40).I38                                     40011
&K       SETA  40-(&DBLS(&X)+&DBLS(&X+1)+&DBLS(&X+2)+&DBLS(&X+3)+&DBLS($
               &X+4))
         DC    CL&K'&ALL(&X)&ALL(&X+1)&ALL(&X+2)&ALL(&X+3)&ALL(&X+4)'
&X       SETA  &X+5
&Y       SETA  &Y-40                                              40011
         AGO   .I36
.*
.I38     ANOP
         AIF   (&Y LE 0).I48                                      40011
         AIF   (&Y LE 32).I40                                     40011
&CON(1)  SETC  '&ALL(&X+4)'(1,&Y-32)
&Y       SETA  &Y-(&DBLS(&X)+&DBLS(&X+1)+&DBLS(&X+2)+&DBLS(&X+3)+&DBLS($
               &X+4))
         DC    CL&Y'&ALL(&X)&ALL(&X+1)&ALL(&X+2)&ALL(&X+3)&CON(1)'
         AGO   .I48
.*
.I40     ANOP
         AIF   (&Y LE 24).I42                                     40011
&CON(1)  SETC  '&ALL(&X+3)'(1,&Y-24)
&Y       SETA  &Y-(&DBLS(&X)+&DBLS(&X+1)+&DBLS(&X+2)+&DBLS(&X+3))
         DC    CL&Y'&ALL(&X)&ALL(&X+1)&ALL(&X+2)&CON(1)'
         AGO   .I48
.*
.I42     ANOP
         AIF   (&Y LE 16).I44                                     40011
&CON(1)  SETC  '&ALL(&X+2)'(1,&Y-16)
&Y       SETA  &Y-(&DBLS(&X)+&DBLS(&X+1)+&DBLS(&X+2))
         DC    CL&Y'&ALL(&X)&ALL(&X+1)&CON(1)'
         AGO   .I48
.*
.I44     ANOP
         AIF   (&Y LE 8).I46                                      40011
&CON(1)  SETC  '&ALL(&X+1)'(1,&Y-8)
&Y       SETA  &Y-(&DBLS(&X)+&DBLS(&X+1))
         DC    CL&Y'&ALL(&X)&CON(1)'
         AGO   .I48
.*
.I46     ANOP
&CON(1)  SETC  '&ALL(&X)'(1,&Y)
&Y       SETA  &Y-(&DBLS(&X))
         DC    CL&Y'&CON(1)'
.*
.I48     ANOP
&L       SETA  (&LMAX-(&L+&TL))
         AIF   (&L LE 0).I50
         DC    CL&L' '
.I50     ANOP
*
         AGO   .I02
.*
.*
.I52     ANOP
         DC    CL&LMAX'&CC '
&CC      SETC  ' '
*
         AGO   .I02
.*
.*
.I75     ANOP
*
         MEXIT
.*
         MEND
