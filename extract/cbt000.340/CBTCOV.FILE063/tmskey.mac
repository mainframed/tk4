         MACRO
         TMSKEY                                                        1
               &TYPE=, -          TYPE OF GENERATION.                  2
               &KEYL=, -          LONG KEYWORD.                        3
               &KEYS=, -          SHORT KEYWORD.                       4
               &SAME=, -          ALTERNATE KEYWORD.                   5
               &INTF=, -          INTERNAL FORMAT.                     6
               &LABE=, -          LABEL OF FIELD IN MASTER RECORD.     7
               &XCOR=, -          "X" COORDINATE OR COLUMN ON SCREEN.  8
               &YCOR=, -          "Y" COORDINATE OR ROW ON SCREEN.     9
               &DISL=, -          DISPLAY LENGTH OF FIELD.             0
               &UPDA=, -          UPDATE TABLE FLAG.                   1
               &KEYV=             DEFAULT VALUE FOR KEYWORD.
.*
.*                                                                40313
.**********************************************************************
.*
         GBLB  &LEN -             SWITCH FOR END OF KEYWORD TABLE.
         GBLA  &NUM -             COUNTER FOR NUMBER OF KEYWORDS.
         GBLB  &BEG -             SWITCH FOR 1ST ENTRY OF EACH SECTION.
         GBLB  &BEF -             SWITCH FOR BEGINNING OF KEYWORD TABLE
         GBLC  &TAI -             STORAGE FOR CURRENT TABLE NAME.
         GBLA  &TAB -             STORAGE FOR CURRENT TABLE NUMBER.
         GBLC  &NUK -             STORAGE FOR CURRENT "SYSNDX" VALUE.
.*
         GBLA  &VA -              COUNTER FOR PASSING VALUE TO INNER.
         GBLA  &KTBBIT -          COUNTER FOR PASSING TO & FROM BUILD.
         GBLA  &KTBBYT -          COUNTER FOR PASSING TO & FROM BUILD.
         GBLA  &KTBMSK -          COUNTER FOR PASSING TO & FROM BUILD.
         GBLA  &KTBFN -           COUNTER FOR PASSING TO & FROM BUILD.
         GBLB  &KTBER -           SWITCH FOR ERROR CONDITION.
.*
.**********************************************************************
.*
         GBLA  &KTBM01 -          COUNTER FOR # OF ENTRIES IN TABLE 1.
         GBLA  &KTBM02 -          COUNTER FOR # OF ENTRIES IN TABLE 2.
         GBLA  &KTBM03 -          COUNTER FOR # OF ENTRIES IN TABLE 3.
         GBLA  &KTBM04 -          COUNTER FOR # OF ENTRIES IN TABLE 4.
         GBLA  &KTBM05 -          COUNTER FOR # OF ENTRIES IN TABLE 5.
         GBLA  &KTBM06 -          COUNTER FOR # OF ENTRIES IN TABLE 6.
         GBLA  &KTBM07 -          COUNTER FOR # OF ENTRIES IN TABLE 7.
.*
.**********************************************************************
.*
         GBLA  &KTBAX01 -         TOTAL NUMBER OF ENTRIES IN TABLE 1.
         GBLA  &KTBAX02 -         TOTAL NUMBER OF ENTRIES IN TABLE 2.
         GBLA  &KTBAX03 -         TOTAL NUMBER OF ENTRIES IN TABLE 3.
         GBLA  &KTBAX04 -         TOTAL NUMBER OF ENTRIES IN TABLE 4.
         GBLA  &KTBAX05 -         TOTAL NUMBER OF ENTRIES IN TABLE 5.
         GBLA  &KTBAX06 -         TOTAL NUMBER OF ENTRIES IN TABLE 6.
         GBLA  &KTBAX07 -         TOTAL NUMBER OF ENTRIES IN TABLE 7.
.*
.**********************************************************************
.*
         GBLA  &COU -             LINE SEPARATOR COUNTER.
         GBLA  &SEB -             COUNTER FOR BIT WITHIN SECURITY BYTE
         GBLA  &SEF -             COUNTER FOR SECURITY BYTE.
         GBLB  &DOC -             HEADER DOCUMENTATION INDICATOR.
         GBLB  &TM -              FLAG FOR  "TMC"  GENERATION.
         GBLB  &DS -              FLAG FOR  "DSNB"  GENERATION.
         GBLB  &CT1 -             FLAG FOR  "CTL1"  GENERATION.
         GBLB  &CT2 -             FLAG FOR  "CTL2"  GENERATION.
         GBLB  &CT3 -             FLAG FOR  "CTL3"  GENERATION.
         GBLB  &CT4 -             FLAG FOR  "CTL4"  GENERATION.
         GBLB  &AU -              FLAG FOR  "AUDI"  GENERATION.
.*
.**********************************************************************
.*
         GBLB  &GE#SE -           SWITCH TO INDICATE SECURITY BEING GEN
.*
.**********************************************************************
.*
         LCLB  &SPEC -            FLAG FOR  "SPECIAL"  GENERATION.
.*
.**********************************************************************
.*
         LCLC  &FIEL -            FIELD NAME WE ARE TESTING.
         LCLC  &MAX -             MAXIMUM NUMBER OF CHARACTERS.
         LCLA  &ENT -             NUMBER OF CHARACTERS ENTERED.
.*
.**********************************************************************
.*
         LCLB  &UMU -             INDICATOR FOR  "MUL"  FLAG.
         LCLB  &UIN -             INDICATOR FOR  "INT"  FLAG.
         LCLB  &UNO -             INDICATOR FOR  "NON"  FLAG.
         LCLB  &UNR -             INDICATOR FOR  "NRE"  FLAG.
         LCLB  &UVT -             INDICATOR FOR  "VET"  FLAG.
         LCLB  &UVD -             INDICATOR FOR  "VED"  FLAG.
         LCLB  &UVR -             INDICATOR FOR  "VER"  FLAG.
.*
.**********************************************************************
.*
         LCLC  &KEL -             LONG KEYWORD.
         LCLC  &KES -             SHORTEN KEYWORD OR ALTERNATE KEYWORD
         LCLC  &INF -             INTERNAL FORMAT.
         LCLC  &ACT -             ACTUAL LENGTH OF FIELD.
         LCLC  &MAH -             MACHINE LENGTH OF FIELD.
         LCLC  &XCO -             "X" COORDINATE OR COLUMN ON SCREEN.
         LCLC  &YCO -             "Y" COORDINATE OR ROW ON SCREEN.
         LCLC  &DIT -             DISPLACEMENT INTO "TMC".
         LCLC  &DIL -             DISPLAY LENGTH OF FIELD.
         LCLA  &UPD -             UPDATE TABLE FLAG.
         LCLC  &KEV -             DEFAULT VALUE FOR KEYWORD.
.*
.**********************************************************************
.*
         LCLC  &L -               SPECIAL CHARACTER STORAGE.
         LCLA  &X -               COUNTER FOR ERROR DISPLAY.
         LCLA  &K -               CURRENT LENGTH OF KEYWORD.
         LCLA  &IX -              CURRENT INDEX FOR KEYWORDS.
         LCLA  &IN -              INDEX FOR RUNNING UP KEYWORD TABLE.
         LCLA  &NU -              COUNTER FOR TOTAL # OF SUB PARMS.
         LCLC  &TES -             TEMPERARY TEST AREA.
         LCLA  &CU -              RUNNING COUNTER FOR CURRENT SUB PARM
         LCLA  &VAL -             COUNTER FOR PASSING VALUE TO INNER.
.*
.**********************************************************************
.*
&L       SETC  'L''' -            SET SPECIAL CHARACTER.
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.*           *************************************************
.*           ***  S T A R T   O F   M A I N L I N E   O F  ***
.*           ***                 M A C R O                 ***
.*           *************************************************
.*
         AIF   (&DOC).BY#DOC -    HAS DOCUMENTATION BEEN PRINTED?
&DOC     SETB  1 -                SET DOCUMENTATION INDICATOR.
.*
.**********************************************************************
.*
.*       SPACE 2
.*       TMSKEY                  UCC-1 MACRO JAN 02, 1979
.*                 GENERATES AN ENTRY OR ENTRIES FOR THE KEYWORD TABLE.
.*       SPACE 2
.**********************************************************************
.*
.*   SYMBOLIC     VALUE                   USAGE
.*   PARAMETER
.*
.*       TYPE     TMC              FIRST CARD IN THE GENERATION OF
.*                                 KEYWORDS FOR THE  "TMC"  SECTION.
.*
.*                DSNB             FIRST CARD IN THE GENERATION OF
.*                                 KEYWORDS FOR THE  "DSNB"  SECTION.
.*
.*                CTL              FIRST CARD IN THE GENERATION OF
.*                                 KEYWORDS FOR THE  "CTL"  SECTION.
.*
.*                FINIS            LAST CARD IN ANY OF THE ABOVE
.*                                 SECTIONS  (I.E. TMC, DSNB OR CTL).
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.BY#DOC  ANOP
         AIF   ('&TYPE' EQ 'DSECT').GET#SEC - IS THIS A DSECT ENTRY?
         AIF   ('&TYPE' EQ 'TMC').SET#TMC -   IS THIS A  "TMC"  GEN.
         AIF   ('&TYPE' EQ 'DSNB').SET#DSN -  IS THIS A  "DSNB"  GEN.
         AIF   ('&TYPE' EQ 'CTL1').SET#CT1 -  IS THIS A  "CTL1"  GEN.
         AIF   ('&TYPE' EQ 'CTL2').SET#CT2 -  IS THIS A  "CTL2"  GEN.
         AIF   ('&TYPE' EQ 'CTL3').SET#CT3 -  IS THIS A  "CTL3"  GEN.
         AIF   ('&TYPE' EQ 'CTL4').SET#CT4 -  IS THIS A  "CTL4"  GEN.
         AIF   ('&TYPE' EQ 'AUDI').SET#AUD -  IS THIS A  "AUDI"  GEN.
         AIF   ('&TYPE' EQ 'SPEC').SET#SPE -  IS THIS A  "SPECIAL" GEN.
.*
         AIF   (&TM).TMCSEC -      IS THIS A  "TMC"  GENERATION?
         AIF   (&DS).DSNBSEC -     IS THIS A  "DSNB"  GENERATION?
         AIF   (&CT1).CTL1SEC -    IS THIS A  "CTL1"  GENERATION?
         AIF   (&CT2).CTL2SEC -    IS THIS A  "CTL2"  GENERATION?
         AIF   (&CT3).CTL3SEC -    IS THIS A  "CTL3"  GENERATION?
         AIF   (&CT4).CTL4SEC -    IS THIS A  "CTL4"  GENERATION?
         AIF   (&AU).AUDSEC -      IS THIS A  "AUDI"  GENERATION?
         AIF   (&SPEC).STR#GEN -   IS THIS A  "SPECIAL"  GENERATION?
.*
.**********************************************************************
.*
         SPACE 3
* ************************************************
         MNOTE 8,'A CONTROL CARD WAS NOT THE FIRST CARD IN'
         MNOTE *,'            YOUR GENERATION.'
* ************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.SET#TMC ANOP
         AIF   (&TM).TMCERR -     IS THIS A  "TMC"  GENERATION?
&TM      SETB  1 -                SET  "TMC"  SWITCH.
&LEN     SETB  0 -                SET  "END"  SWITCH.
&BEF     SETB  0 -                SET  "BEGINNING"  SWITCH.
&TAI     SETC  'TMC' -            SET TABLE NAME.
&TAB     SETA  1 -                SET TABLE NUMBER.
&VA      SETA  0-1 -              INITIALIZE ENTRY NUMBER.        40313
&BEG     SETB  0 -                TURN OFF BEGIN BY-PASS SWITCH.
.*
.**********************************************************************
.*
.TMCSEC  ANOP
         AIF   ('&TYPE' EQ 'FINIS').END#TMC -IS THIS END OF GENERATION?
.*
&UMU     SETB  0 -                TURN OFF UPDATE FLAG.
&UIN     SETB  0 -                TURN OFF UPDATE FLAG.
&UNO     SETB  0 -                TURN OFF UPDATE FLAG.
&UNR     SETB  0 -                TURN OFF UPDATE FLAG.
&UVT     SETB  0 -                TURN OFF UPDATE FLAG.
&UVD     SETB  0 -                TURN OFF UPDATE FLAG.
&UVR     SETB  0 -                TURN OFF UPDATE FLAG.
         AGO   .STR#GEN -         BRANCH TO CONTINUE.
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.SET#DSN ANOP
         AIF   (&DS).DSNERR -     IS THIS A  "DSNB"  GENERATION?
&DS      SETB  1 -                SET  "DSNB"  SWITCH.
&LEN     SETB  0 -                SET  "END"  SWITCH.
&BEF     SETB  0 -                SET  "BEGINNING"  SWITCH.
&TAI     SETC  'DSNB' -           SET TABLE NAME.
&TAB     SETA  2 -                SET TABLE NUMBER.
&VA      SETA  0-1 -              INITIALIZE ENTRY NUMBER.        40313
&BEG     SETB  0 -                TURN OFF BEGIN BY-PASS SWITCH.
.*
.**********************************************************************
.*
.DSNBSEC ANOP
         AIF   ('&TYPE' EQ 'FINIS').END#DSN -IS THIS END OF GENERATION?
         AGO   .STR#GEN -         BRANCH TO CONTINUE.
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.SET#CT1 ANOP
         AIF   (&CT1).CTLERR -    IS THIS A  "CTL1"  GENERATION?
&CT1     SETB  1 -                SET  "CTL"  SWITCH.
&LEN     SETB  0 -                SET  "END"  SWITCH.
&BEF     SETB  0 -                SET  "BEGINNING"  SWITCH.
&TAI     SETC  'CTL1' -           SET TABLE NAME.
&TAB     SETA  3 -                SET TABLE NUMBER.
&VA      SETA  0-1 -              INITIALIZE ENTRY NUMBER.        40313
&BEG     SETB  0 -                TURN OFF BEGIN BY-PASS SWITCH.
.*
.**********************************************************************
.*
.CTL1SEC ANOP
         AIF   ('&TYPE' EQ 'FINIS').END#CT1 -IS THIS END OF GENERATION?
         AGO   .STR#GEN -         BRANCH TO CONTINUE.
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.SET#CT2 ANOP
         AIF   (&CT2).CTLERR -    IS THIS A  "CTL2"  GENERATION?
&CT2     SETB  1 -                SET  "CTL"  SWITCH.
&LEN     SETB  0 -                SET  "END"  SWITCH.
&BEF     SETB  0 -                SET  "BEGINNING"  SWITCH.
&TAI     SETC  'CTL2' -           SET TABLE NAME.
&TAB     SETA  4 -                SET TABLE NUMBER.
&VA      SETA  0-1 -              INITIALIZE ENTRY NUMBER.        40313
&BEG     SETB  0 -                TURN OFF BEGIN BY-PASS SWITCH.
.*
.**********************************************************************
.*
.CTL2SEC ANOP
         AIF   ('&TYPE' EQ 'FINIS').END#CT2 -IS THIS END OF GENERATION?
         AGO   .STR#GEN -         BRANCH TO CONTINUE.
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.SET#CT3 ANOP
         AIF   (&CT3).CTLERR -    IS THIS A  "CTL3"  GENERATION?
&CT3     SETB  1 -                SET  "CTL"  SWITCH.
&LEN     SETB  0 -                SET  "END"  SWITCH.
&BEF     SETB  0 -                SET  "BEGINNING"  SWITCH.
&TAI     SETC  'CTL3' -           SET TABLE NAME.
&TAB     SETA  5 -                SET TABLE NUMBER.
&VA      SETA  0-1 -              INITIALIZE ENTRY NUMBER.        40313
&BEG     SETB  0 -                TURN OFF BEGIN BY-PASS SWITCH.
.*
.**********************************************************************
.*
.CTL3SEC ANOP
         AIF   ('&TYPE' EQ 'FINIS').END#CT3 -IS THIS END OF GENERATION?
         AGO   .STR#GEN -         BRANCH TO CONTINUE.
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.SET#CT4 ANOP
         AIF   (&CT4).CTLERR -    IS THIS A  "CTL4"  GENERATION?
&CT4     SETB  1 -                SET  "CTL"  SWITCH.
&LEN     SETB  0 -                SET  "END"  SWITCH.
&BEF     SETB  0 -                SET  "BEGINNING"  SWITCH.
&TAI     SETC  'CTL4' -           SET TABLE NAME.
&TAB     SETA  6 -                SET TABLE NUMBER.
&VA      SETA  0-1 -              INITIALIZE ENTRY NUMBER.        40313
&BEG     SETB  0 -                TURN OFF BEGIN BY-PASS SWITCH.
.*
.**********************************************************************
.*
.CTL4SEC ANOP
         AIF   ('&TYPE' EQ 'FINIS').END#CT4 -IS THIS END OF GENERATION?
         AGO   .STR#GEN -         BRANCH TO CONTINUE.
.*
.**********************************************************************
.*                            * * * * *
.*                             * * * *
.*                              * * *
.*                               * *
.SET#AUD ANOP
         AIF   (&AU).AUDERR -     IS THIS A  "AUDI"  GENERATION?
&AU      SETB  1 -                SET  "AUDI"  SWITCH.
&LEN     SETB  0 -                SET  "END"  SWITCH.
&BEF     SETB  0 -                SET  "BEGINNING"  SWITCH.
&TAI     SETC  'AUDI' -           SET TABLE NAME.
&TAB     SETA  7 -                SET TABLE NUMBER.
&VA      SETA  0-1 -              INITIALIZE ENTRY NUMBER.        40313
&BEG     SETB  0 -                TURN OFF BEGIN BY-PASS SWITCH.
.*
.**********************************************************************
.*
.AUDSEC  ANOP
         AIF   ('&TYPE' EQ 'FINIS').END#AUD -IS THIS END OF GENERATION?
         AGO   .STR#GEN -         BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.SET#SPE ANOP
&SPEC    SETB  1 -                SET  "SPECIAL"  SWITCH.
         AGO   .STR#GEN -         BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.TMCERR  ANOP
.DSNERR  ANOP
.CTLERR  ANOP
.AUDERR  ANOP
         SPACE 3
* ****************************************************
         MNOTE 8,'YOU HAVE ALREADY GENERATED THE SECTION WHICH'
         MNOTE *,'    CONTAINS THE KEYWORDS FOR THE  "&TAI".'
* ****************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.STR#GEN ANOP
.*
.*                       ***********************
.*                       ***  K E Y W O R D  ***
.*                       ***********************
.*
&FIEL    SETC  'KEYL' -           SET NAME OF FIELD.
&MAX     SETC  '7' -              SET MAXIMUM NUMBER OF CHARACTERS.
&ENT     SETA  K'&KEYL -          SET LENGTH OF INPUT FIELD.
.*
         AIF   (T'&KEYL EQ 'O').OMKE -   HAS PARAMETER BEEN ENTERED?
         AIF   ('&KEYL' NE '   ').GOKE - HAVE BLANKS BEEN ENTERED?
.*
.**********************************************************************
.*
.OMKE    ANOP
         SPACE 3
* **********************************************************
         MNOTE 8,'YOU MUST ENTER A VALUE FOR THE PARAMETER  "KEYL" .'
* **********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GOKE  ANOP
         AIF   (K'&KEYL GT 7).MAXERR -      HAVE MORE THAN MAX ENTERED?
.*
         AIF   (&SPEC).CON#01 -   IS THIS A SPECIAL GENERATION?
.*
.**********************************************************************
.*
         AIF   ('&SAME' EQ '    ').NOT#SAM - IS THIS A SPECIAL KEYWORD?
         AIF   (T'&SAME EQ 'O').NOT#SAM -   HAS PARAMETER BEEN ENTERED?
.*
         KTBBLD FND,&TAB,&SAME -     GET ENTRY IN SECURITY TABLE.
         AIF   (&KTBER).NO#FIN -     IS KEYWORD ALREADY IN TABLE?
&VAL     SETA  &KTBFN -              GET SECURITY NUMBER.
         AGO   .GET#CUR -            BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.NOT#SAM ANOP
&VA      SETA  &VA+1 -               GET NEXT ENTRY IN SECURITY TABLE.
&VAL     SETA  &VA -                 SET
         AGO   .GET#CUR -            BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.GET#CUR ANOP
         KTBBLD STO,&TAB,&KEYL,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
         AIF   (&KTBER).DUPER -      IS KEYWORD ALREADY IN TABLE?
.*
.**********************************************************************
.*
         AIF   (T'&KEYS EQ 'O').CON#01 - HAS PARAMETER BEEN ENTERED?
         AIF   ('&KEYS' NE ' ').CON#01 - HAVE BLANKS BEEN ENTERED?
         AIF   (K'&KEYS GT 1).MAXERR -   HAVE MORE THAN MAX ENTERED?
.*
         KTBBLD STO,&TAB,&KEYS,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
         AIF   (&KTBER).DUPER -      IS KEYWORD ALREADY IN TABLE?
.*
         AGO   .CON#01 -             BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.MAXERR  ANOP
         SPACE 3
* *********************************************************
         MNOTE 8,'YOU ENTERED  "&ENT"  CHARACTERS FOR THE SYMBOLIC'
         MNOTE *,'PARAMETER CALLED  "&FIEL"  THE MAXIMUM IS  "&MAX".'
* *********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.DUPER   ANOP
         SPACE 3
* *********************************************************
         MNOTE 8,'YOU ENTERED  "KEYL=&KEYL"  WHICH HAS ALREADY BEEN'
         MNOTE *,'       ENTERED FOR THIS SYMBOLIC PARAMETER.'
* *********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.NO#FIN  ANOP
         SPACE 3
* *********************************************************
         MNOTE 8,'YOU ENTERED  "SAME=&SAME"  BEFORE YOU ENTERED THE'
         MNOTE *,'        SYMBOLIC PARAMETER FOR "KEYL=&SAME".'
* *********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.CON#01  ANOP
&KEL     SETC  '&KEYL' -          GENERATE LONG FORM OF KEYWORD.
&KES     SETC  '&KEYS' -          GENERATE SHORT FORM OF KEYWORD.
&K       SETA  K'&KEYL-1 -        GENERATE LENGTH OF KEYWORD.
.*
.**********************************************************************
.*
.*            *******************************************
.*            ***  G E N E R A T E   S E C U T I T Y  ***
.*            *******************************************
.*
&SEF     SETA  &KTBBYT -          GENERATE NUMBER OF THE SECURITY BYTE.
&SEB     SETA  &KTBBIT -          GENERATE NUMBER OF THE SECURITY BIT.
.*
.**********************************************************************
.*
.*              ***************************************
.*              ***  I N T E R N A L   F O R M A T  ***
.*              ***************************************
.*
         AIF   (T'&INTF EQ 'O').OMIN -      HAS PARAMETER BEEN ENTERED?
         AIF   ('&INTF' NE '   ').GOIN -    HAVE BLANKS BEEN ENTERED?
.*
.**********************************************************************
.*
.OMIN    ANOP
         SPACE 3
* **********************************************************
         MNOTE 8,'YOU MUST ENTER A VALUE FOR THE PARAMETER  "INTF" .'
* **********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GOIN  ANOP
         AIF   ('&INTF' EQ 'CHR').INCH  IS THIS A VALID INTER ENTRY?
         AIF   ('&INTF' EQ 'HEX').INHE  IS THIS A VALID INTER ENTRY?
         AIF   ('&INTF' EQ 'PAC').INPA  IS THIS A VALID INTER ENTRY?
         AIF   ('&INTF' EQ 'BIN').INBI  IS THIS A VALID INTER ENTRY?
         AIF   ('&INTF' EQ 'PAS').INPS  IS THIS A VALID INTER ENTRY?
         AIF   ('&INTF' EQ 'SIN').INSI  IS THIS A VALID INTER ENTRY?
.*
.**********************************************************************
.*
         SPACE 3
* *********************************************************
         MNOTE 8,'YOU ENTERED  "&INTF"  WHICH IS AN INVALID MNEMONIC'
         MNOTE *,'     FOR THE SYMBOLIC PARAMETER CALLED   "INTF".'
* *********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.INCH    ANOP
&INF     SETC  '00' -                       SET INTERNAL FORMAT.
         AGO   .MACH1 -                     BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.INBI    ANOP
&INF     SETC  '04' -                       SET INTERNAL FORMAT.
         AGO   .MACH1 -                     BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.INPA    ANOP
&INF     SETC  '08' -                       SET INTERNAL FORMAT.
         AGO   .MACH1 -                     BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.INHE    ANOP
&INF     SETC  '0C' -                       SET INTERNAL FORMAT.
         AGO   .MACH1 -                     BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.INPS    ANOP
&INF     SETC  '10' -                       SET INTERNAL FORMAT.
         AGO   .MACH1 -                     BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.INSI    ANOP
&INF     SETC  '14' -                       SET INTERNAL FORMAT.
         AGO   .MACH1 -                     BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.*       *******************************************************
.*       ***  D E T E R M I N E   A C T U A L   L E N G T H  ***
.*       *******************************************************
.*
.MACH1   ANOP
         AIF   (T'&LABE EQ 'O').OMMA -      HAS PARAMETER BEEN ENTERED?
         AIF   ('&LABE' NE '   ').GO#ACT -  HAVE BLANKS BEEN ENTERED?
.*
.**********************************************************************
.*
.OMMA    ANOP
         SPACE 3
* **********************************************************
         MNOTE 8,'YOU MUST ENTER A VALUE FOR THE PARAMETER  "LABE" .'
* **********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GO#ACT  ANOP
&ACT     SETC  '&LABE'
.*
.**********************************************************************
.*
.*       *********************************************************
.*       ***  D E T E R M I N E   M A C H I N E   L E N G T H  ***
.*       *********************************************************
.*
.GOMA    ANOP
&MAH     SETC  '&LABE'
.*
.**********************************************************************
.*
.*         *****************************************************
.*         ***  D E T E R M I N E   D I S P L A C E M E N T  ***
.*         *****************************************************
.*
.GODT    ANOP
&DIT     SETC  '&LABE'
.*
.**********************************************************************
.*
.*                ***********************************
.*                ***  "X"   C O O R D I N A T E  ***
.*                ***********************************
.*
         AIF   (T'&XCOR EQ 'O').OMXC -      HAS PARAMETER BEEN ENTERED?
         AIF   ('&XCOR' NE '   ').GOXC -    HAVE BLANKS BEEN ENTERED?
.*
.**********************************************************************
.*
.OMXC    ANOP
         SPACE 3
* **********************************************************
         MNOTE 8,'YOU MUST ENTER A VALUE FOR THE PARAMETER  "XCOR" .'
* **********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GOXC    ANOP
         AIF   ('&XCOR' LE '80').GOXC1
.*
.**********************************************************************
.*
&X       SETA  K'&XCOR
         SPACE 3
* *************************************************
         MNOTE 8,'YOU ENTERED  "XCOR=&X"  WHICH IS GREATER'
         MNOTE *,' THAN ALLOWABLE MAXIMUM VALUE OF  "80" .'
* *************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GOXC1   ANOP
&XCO     SETC  '&XCOR'
.*
.**********************************************************************
.*
.*                ***********************************
.*                ***  "Y"   C O O R D I N A T E  ***
.*                ***********************************
.*
         AIF   (T'&YCOR EQ 'O').OMYC -      HAS PARAMETER BEEN ENTERED?
         AIF   ('&YCOR' NE '   ').GOYC -    HAVE BLANKS BEEN ENTERED?
.*
.**********************************************************************
.*
.OMYC    ANOP
         SPACE 3
* **********************************************************
         MNOTE 8,'YOU MUST ENTER A VALUE FOR THE PARAMETER  "YCOR" .'
* **********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GOYC    ANOP
         AIF   ('&YCOR' LE '24').GOYC1
.*
.**********************************************************************
.*
&X       SETA  K'&YCOR
         SPACE 3
* *************************************************
         MNOTE 8,'YOU ENTERED  "YCOR=&X"  WHICH IS GREATER'
         MNOTE *,' THAN ALLOWABLE MAXIMUM VALUE OF  "24" .'
* *************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GOYC1   ANOP
&YCO     SETC  '&YCOR'
.*
.**********************************************************************
.*
.*               *************************************
.*               ***  D I S P L A Y   L E N G T H  ***
.*               *************************************
.*
         AIF   (T'&DISL EQ 'O').OMDL -      HAS PARAMETER BEEN ENTERED?
         AIF   ('&DISL' NE '   ').GODL -    HAVE BLANKS BEEN ENTERED?
.*
.**********************************************************************
.*
.OMDL    ANOP
         SPACE 3
* **********************************************************
         MNOTE 8,'YOU MUST ENTER A VALUE FOR THE PARAMETER  "DISL" .'
* **********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GODL    ANOP
&DIL     SETC  '&DISL'
.*
.**********************************************************************
.*
.*                 *******************************
.*                 ***  U P D A T E   F L A G  ***
.*                 *******************************
.*
         AIF   (T'&UPDA EQ 'O').OMUP -      HAS PARAMETER BEEN ENTERED?
         AIF   ('&UPDA' NE '   ').GOUP -    HAVE BLANKS BEEN ENTERED?
.*
.**********************************************************************
.*
.OMUP    ANOP
         SPACE 3
* **********************************************************
         MNOTE 8,'YOU MUST ENTER A VALUE FOR THE PARAMETER  "UPDA" .'
* **********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GOUP    ANOP
         AIF   ('&UPDA'(1,1) EQ '(').GOUP1
.*
.**********************************************************************
.*
         SPACE 3
* ****************************************************
         MNOTE 8,'YOU MUST ENCLOSE THE VALUE FOR THE SYMBOLIC '
         MNOTE *,'   PARAMETER CALLED "UPDA" IN PARENTHESIS.'
* ****************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.GOUP1   ANOP
&NU      SETA  N'&UPDA -           GET TOTAL NUMBER OF SUB PARAMETERS.
&CU      SETA  0 -                 INITIALIZE COUNTER.
&UPD     SETA  0 -                 INITIALIZE INTERNAL FLAG.
.*
.LOOP#01 ANOP
&CU      SETA  &CU+1 -            INCREMENT TO NEXT SUB PARAMETER.
&TES     SETC  '&UPDA(&CU)' -     GET VALUE OF SUB PARAMETER.
.*
         AIF   ('&TES' EQ 'MUL').UPMU -    IS THIS A VALID ENTRY?
         AIF   ('&TES' EQ 'INT').UPIN -    IS THIS A VALID ENTRY?
         AIF   ('&TES' EQ 'NON').UPNO -    IS THIS A VALID ENTRY?
         AIF   ('&TES' EQ 'NRE').UPNR -    IS THIS A VALID ENTRY?
         AIF   ('&TES' EQ 'VET').UPVT -    IS THIS A VALID ENTRY?
         AIF   ('&TES' EQ 'VED').UPVD -    IS THIS A VALID ENTRY?
         AIF   ('&TES' EQ 'VER').UPVR -    IS THIS A VALID ENTRY?
         AIF   ('&TES' EQ 'BLK').UPBL -    IS THIS A VALID ENTRY?
.*
.**********************************************************************
.*
         SPACE 3
* **********************************************************
         MNOTE 8,'YOU ENTERED  "&UPDA(&CU)"  WHICH IS AN INVALID'
         MNOTE *,'MNEMONIC FOR THE SYMBOLIC PARAMETER CALLED "UPDA".'
* **********************************************************
         SPACE 3
         MEXIT
.*
.**********************************************************************
.*
.UPMU    ANOP
         AIF   (&UMU).DUP#UP -    WAS THERE A DUPLICATE ENTRY?
&UMU     SETB  1 -                SET BY-PASS SWITCH.
.*
&UPD     SETA  &UPD+128           SET HEX  "80"  IN THE INTERNAL FLAG.
         AGO   .UPDO1 -           BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.UPIN    ANOP
         AIF   (&UIN).DUP#UP -    WAS THERE A DUPLICATE ENTRY?
&UIN     SETB  1 -                SET BY-PASS SWITCH.
.*
&UPD     SETA  &UPD+64            SET HEX  "40"  IN THE INTERNAL FLAG.
         AGO   .UPDO1 -           BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.UPNO    ANOP
         AIF   (&UNO).DUP#UP -    WAS THERE A DUPLICATE ENTRY?
&UNO     SETB  1 -                SET BY-PASS SWITCH.
.*
&UPD     SETA  &UPD+32            SET HEX  "20"  IN THE INTERNAL FLAG.
         AGO   .UPDO1 -           BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.UPNR    ANOP
         AIF   (&UNR).DUP#UP -    WAS THERE A DUPLICATE ENTRY?
&UNR     SETB  1 -                SET BY-PASS SWITCH.
.*
&UPD     SETA  &UPD+8             SET HEX  "08"  IN THE INTERNAL FLAG.
         AGO   .UPDO1 -           BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.UPVT    ANOP
         AIF   (&UVT).DUP#UP -    WAS THERE A DUPLICATE ENTRY?
&UVT     SETB  1 -                SET BY-PASS SWITCH.
.*
&UPD     SETA  &UPD+4             SET HEX  "04"  IN THE INTERNAL FLAG.
         AGO   .UPDO1 -           BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.UPVD    ANOP
         AIF   (&UVD).DUP#UP -    WAS THERE A DUPLICATE ENTRY?
&UVD     SETB  1 -                SET BY-PASS SWITCH.
.*
&UPD     SETA  &UPD+2             SET HEX  "02"  IN THE INTERNAL FLAG.
         AGO   .UPDO1 -           BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.UPVR    ANOP
         AIF   (&UVR).DUP#UP -    WAS THERE A DUPLICATE ENTRY?
&UVR     SETB  1 -                SET BY-PASS SWITCH.
.*
&UPD     SETA  &UPD+1 -           SET HEX  "01"  IN THE INTERNAL FLAG.
         AGO   .UPDO1 -           BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.UPBL    ANOP
&UPD     SETA  00 -               SET HEX  "00"  IN THE INTERNAL FLAG.
         AGO   .UPEND -           BRANCH TO CONTINUE.
.*
.**********************************************************************
.*
.DUP#UP  ANOP
         SPACE 3
* *********************************************
         MNOTE 8,'YOU ALREADY ENTERED  "&TES"  FOR THE'
         MNOTE *,'   SYMBOLIC PARAMETER CALLED "UPDA".'
* *********************************************
         SPACE 3
.*
.**********************************************************************
.*
.UPDO1   ANOP
         AIF   ('&CU' NE '&NU').LOOP#01 -   ARE THERE ANY MORE ENTRIES?
.*
.**********************************************************************
.*
.*
.*                  ***************************************
.*                  ***  D E F A U L T   K E Y W O R D  ***
.*                  ***************************************
.*
.UPEND   ANOP
         AIF   (K'&KEYL GT 7).MAXERR -      HAVE MORE THAN MAX ENTERED?
         AIF   (T'&KEYV EQ 'O').OMEV -   HAS PARAMETER BEEN ENTERED?
         AIF   ('&KEYV' NE '   ').GOEV - HAVE BLANKS BEEN ENTERED?
.*
.**********************************************************************
.*
.OMEV    ANOP
&KEV     SETC  'HEXZEROS'
         AGO   .KEV#END
.*
.**********************************************************************
.*
.GOEV   ANOP
&KEV    SETC   '&KEYV' -            SET INPUT VALUE.
.*
.**********************************************************************
.*
.KEV#END ANOP
&NUM     SETA  &NUM+1 -           INCREMENT NUMBER OF KEYWORDS.
.*
         AIF   (&GE#SE).BY#GEN -   ARE WE GENERATING SECURITY?
.*
.**********************************************************************
.*
.*
.*               *************************************
.*               ***  G E N E R A T E   E N T R Y  ***
.*               *************************************
.*
         SPACE 1
         AIF   (&BEF).BY#CON -    HAS BEGINNING BEEN GENERATED?
.*
.**********************************************************************
.*
.BY#CON  ANOP
         AIF   (&BEG).BY#BEG -    IS THIS FIRST TIME THRU SECTION?
BEG&TAI  DS    0H
&BEG     SETB  1 -                SET BY-PASS SWITCH.
.*
.BY#BEG  ANOP
.*
.**********************************************************************
.*
         AIF   (&SPEC).BY#LAB -   IS THIS A SPECIAL GENERATION?
.*
&TAI&SYSNDX DS 0H
.*
.BY#LAB  ANOP
         DC    CL7'&KEL   ' -     LONG KEYWORD.
         DC    CL1'&KES   ' -     SHORTEN KEYWORD.
.**********
.*
.* NOTE......BE AWARE OF ALIGNMENT ON DISPLACEMENT.
.*
.**********
         DC    AL2(&DIT-TMREC) -  DISPLACEMENT INTO "TMC".
         DC    XL1'&INF' -        INTERNAL FORMAT.
         DC    AL1(&L&MAH) -      ACTUAL LENGTH OF FIELD.
         DC    AL1(&L&MAH-1) -    MACHINE LENGTH OF FIELD.
         DC    AL1(&SEF) -        BYTE IN SECURITY FIELD.
         DC    AL1(&SEB) -        BIT POSITION IN SECURITY FIELD.
         DC    AL1(&XCO) -        "X" COORDINATE OR ROW ON SCREEN.
         DC    AL1(&YCO) -        "Y" COORDINATE OR COLUMN ON SCREEN.
         DC    AL1(&DIL) -        DISPLAY LENGTH OF FIELD.
         DC    AL1(&UPD) -        UPDATE FLAG TABLE.
         DC    AL1(&K) -          MACHINE LENGTH OF KEYWORD.
         DC    CL9'&KEV' -        DEFAULT VALUE FOR KEYWORD.
         DS    0H -               ALIGNMENT
.*
.**********************************************************************
.*
         AIF   (NOT &SPEC).BY#EXIT - IS THIS A SPECIAL GENERATION?
         MEXIT
.*
.**********************************************************************
.*
.BY#EXIT ANOP
.*
         AIF   (&BEF).LINE -      HAS BEGINNING BEEN GENERATED?
&BEF     SETB  1 -                SET  "BEGINNING"  SWITCH.
         SPACE 2
LEO&TAI  EQU   *-&TAI&SYSNDX -    LENGTH OF ENTRY.
LEN&TAI  DS    0C -               GENERATE END OF FIRST ENTRY.
&NUK     SETC  '&SYSNDX' -        SET CURRENT  "SYSNDX"  INDEX.
&COU     SETA  1 -                SET SEPARATOR COUNTER TO FORCE PAGE.
.*
.**********************************************************************
.*
.LINE    ANOP
         SPACE 1
***********************************************************************
.*
&COU     SETA  &COU+1 -                INCREMENT SEPARATOR COUNTER.
         AIF   (&COU NE 2).TAG#02 -    HAVE WE PRINTED  "2"  ON PAGE?
&COU     SETA  0 -                     RESET SEPARATOR COUNTER.
.*       EJECT -
***********************************************************************
.TAG#02  ANOP
         SPACE 1
         MEXIT
.*
.**********************************************************************
.*
.END#TMC ANOP
&TM      SETB  0 -                TURN OFF  "TMC"  SWITCH.
.*       MNOTE 0,'*********************************'
.*       MNOTE 0,'    KTBAX01=&KTBAX01....'
.*       MNOTE 0,'    KTBM01=&KTBM01....'
.*       MNOTE 0,'    VAL=&VAL........'
.*       MNOTE 0,'********************************'
&VAL     SETA  &VA+1 -               INCREMENT VALUE COUNTER.
         KTBBLD STO,&TAB,&KEYL,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
.*
&KTBAX01 SETA  &KTBM01 -           SET TOTAL NUMBER OF ENTRIES.
.*       MNOTE 0,'*********************************'
.*       MNOTE 0,'    KTBAX01=&KTBAX01....'
.*       MNOTE 0,'    KTBM01=&KTBM01....'
.*       MNOTE 0,'    VAL=&VAL........'
.*       MNOTE 0,'********************************'
.*
         AIF   (&GE#SE).BY#GEN -   ARE WE GENERATING SECURITY?
.*
END&TAI  DS    0C
NUM&TAI  EQU   &NUM -                   GENERATE THE NUMBER OF ENTRIES.
         SPACE 2
         ORG
         DS    0H
         DC    (LEO&TAI)XL1'FF'
.*
.**********************************************************************
.*
&NUM     SETA  0 -                CLEAR COUNTER.
         MEXIT
.*
.**********************************************************************
.*
.END#DSN ANOP
&DS      SETB  0 -                TURN OFF  "DSNB"  SWITCH.
.*       MNOTE 0,'*********************************'
.*       MNOTE 0,'    KTBAX02=&KTBAX02....'
.*       MNOTE 0,'    KTBM02=&KTBM02....'
.*       MNOTE 0,'    VAL=&VAL........'
.*       MNOTE 0,'*********************************'
&VAL     SETA  &VA+1 -               INCREMENT VALUE COUNTER.
         KTBBLD STO,&TAB,&KEYL,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
.*
&KTBAX02 SETA  &KTBM02 -           SET TOTAL NUMBER OF ENTRIES.
.*       MNOTE 0,'*********************************'
.*       MNOTE 0,'    KTBAX02=&KTBAX02....'
.*       MNOTE 0,'    KTBM02=&KTBM02....'
.*       MNOTE 0,'    VAL=&VAL........'
.*       MNOTE 0,'*********************************'
.*
         AIF   (&GE#SE).BY#GEN -   ARE WE GENERATING SECURITY?
.*
END&TAI  DS    0C
NUM&TAI  EQU   &NUM -                   GENERATE NUMBER OF ENTRIES.
         SPACE 2
         ORG
         DS    0H
         DC    (LEO&TAI)XL1'FF'
.*
.**********************************************************************
.*
&NUM     SETA  0 -                CLEAR COUNTER.
         MEXIT
.*
.**********************************************************************
.*
.END#CT1 ANOP
&CT1     SETB  0 -                TURN OFF  "CTL1"  SWITCH.
&VAL     SETA  &VA+1 -               INCREMENT VALUE COUNTER.
         KTBBLD STO,&TAB,&KEYL,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
.*
&KTBAX03 SETA  &KTBM03 -           SET TOTAL NUMBER OF ENTRIES.
.*
         AIF   (&GE#SE).BY#GEN -   ARE WE GENERATING SECURITY?
.*
END&TAI  DS    0H
NUM&TAI  EQU   &NUM -                   GENERATE NUMBER OF ENTRIES.
         SPACE 2
         ORG
         DS    0H
         DC    (LEO&TAI)XL1'FF'
.*
.**********************************************************************
.*
&NUM     SETA  0 -                CLEAR COUNTER.
         MEXIT
.*
.**********************************************************************
.*
.END#CT2 ANOP
&CT2     SETB  0 -                TURN OFF  "CTL2"  SWITCH.
&VAL     SETA  &VA+1 -               INCREMENT VALUE COUNTER.
         KTBBLD STO,&TAB,&KEYL,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
.*
&KTBAX04 SETA  &KTBM04 -           SET TOTAL NUMBER OF ENTRIES.
.*
         AIF   (&GE#SE).BY#GEN -   ARE WE GENERATING SECURITY?
.*
END&TAI  DS    0H
NUM&TAI  EQU   &NUM -                   GENERATE NUMBER OF ENTRIES.
         SPACE 2
         ORG
         DS    0H
         DC    (LEO&TAI)XL1'FF'
.*
.**********************************************************************
.*
&NUM     SETA  0 -                CLEAR COUNTER.
         MEXIT
.*
.**********************************************************************
.*
.END#CT3 ANOP
&CT3     SETB  0 -                TURN OFF  "CTL3"  SWITCH.
&VAL     SETA  &VA+1 -               INCREMENT VALUE COUNTER.
         KTBBLD STO,&TAB,&KEYL,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
.*
&KTBAX05 SETA  &KTBM05 -           SET TOTAL NUMBER OF ENTRIES.
.*
         AIF   (&GE#SE).BY#GEN -   ARE WE GENERATING SECURITY?
.*
END&TAI  DS    0H
NUM&TAI  EQU   &NUM -                   GENERATE NUMBER OF ENTRIES.
         SPACE 2
         ORG
         DS    0H
         DC    (LEO&TAI)XL1'FF'
.*
.**********************************************************************
.*
&NUM     SETA  0 -                CLEAR COUNTER.
         MEXIT
.*
.**********************************************************************
.*
.END#CT4 ANOP
&CT4     SETB  0 -                TURN OFF  "CTL4"  SWITCH.
&VAL     SETA  &VA+1 -               INCREMENT VALUE COUNTER.
         KTBBLD STO,&TAB,&KEYL,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
.*
&KTBAX06 SETA  &KTBM06 -           SET TOTAL NUMBER OF ENTRIES.
.*
         AIF   (&GE#SE).BY#GEN -   ARE WE GENERATING SECURITY?
.*
END&TAI  DS    0H
NUM&TAI  EQU   &NUM -                   GENERATE NUMBER OF ENTRIES.
         SPACE 2
         ORG
         DS    0H
         DC    (LEO&TAI)XL1'FF'
.*
.**********************************************************************
.*
&NUM     SETA  0 -                CLEAR COUNTER.
         MEXIT
.*
.**********************************************************************
.*
.END#AUD ANOP
&AU      SETB  0 -                TURN OFF  "AUDI"  SWITCH.
&VAL     SETA  &VA+1 -               INCREMENT VALUE COUNTER.
         KTBBLD STO,&TAB,&KEYL,&VAL -GET NEXT ENTRY IN SECURITY TABLE.
.*
&KTBAX07 SETA  &KTBM07 -           SET TOTAL NUMBER OF ENTRIES.
.*
         AIF   (&GE#SE).BY#GEN -   ARE WE GENERATING SECURITY?
.*
END&TAI  DS    0H
NUM&TAI  EQU   &NUM -                   GENERATE NUMBER OF ENTRIES.
         SPACE 2
         ORG
         DS    0H
         DC    (LEO&TAI)XL1'FF'
.*
.**********************************************************************
.*
&NUM     SETA  0 -                CLEAR COUNTER.
         MEXIT
.*
.**********************************************************************
.*
.GET#SEC ANOP
.*
         SPACE 2
*           ***********************************************
*           ***  K E Y W O R D   T A B L E   D S E C T  ***
*           ***********************************************
         SPACE 1
TMKESEC  DSECT
         SPACE 1
TMKEWOL  DS    CL7 -              LONG KEYWORD.
TMKEWOS  DS    CL1 -              SHORT KEYWORD......(IF ANY).
TMKEDIT  DS    AL2 -              DISPLACEMENT INTO "TMC".
         SPACE 1
TMKEINT  DS    XL1 -              INTERNAL FORMAT.
TMKECHR  EQU   X'00' -              CHARACTER.
TMKEBIN  EQU   X'04' -              BINARY.
TMKEPAC  EQU   X'08' -              PACKED.
TMKEHEX  EQU   X'0C' -              HEXADECIMAL.
TMKESIN  EQU   X'14' -              SIGNED BINARY.
         SPACE 1
TMKEACT  DS    AL1 -              ACTUAL LENGTH OF FIELD.
TMKEMAC  DS    AL1 -              MACHINE LENGTH OF FIELD.
TMKESEF  DS    AL1 -              BYTE IN SECURITY FIELD.
TMKESEB  DS    AL1 -              BIT POSITION IN SECURITY FIELD.
TMKEXCO  DS    AL1 -              "X" COORDINATE OR ROW ON SCREEN.
TMKEYCO  DS    AL1 -              "Y" COORDINATE OR COLUMN ON SCREEN.
TMKEDIL  DS    AL1 -              DISPLAY LENGTH OF FIELD.
         SPACE 1
TMKEUPD  DS    XL1 -              UPDATE TABLE FLAG.
TMKEMUL  EQU   X'80' -              MULTI-VOLUME FIELD.
TMKEINF  EQU   X'40' -              INTERNAL FIELD.
TMKENON  EQU   X'20' -              NON-VARIABLE LENGTH FIELD.
TMKENRE  EQU   X'08' -              REPLACEMENT NOT ALLOWED.
TMKEVET  EQU   X'04' -              VERIFY TIME.
TMKEVED  EQU   X'02' -              VERIFY DATE.
TMKEVER  EQU   X'01' -              VERIFY VOLUME SERIAL NUMBER.
         SPACE 1
TMKELEK  DS    AL1 -              MACHINE LENGTH OF KEYWORD.
TMKEKEV  DS    CL9 -              DEFAULT VALUE FOR KEYWORD.
         DS    0H -               ALIGNMENT
         SPACE 1
TMKELEN  EQU   * -                START OF NEXT KEYWORD ENTRY.
.*
.**********************************************************************
.*
         MEXIT
.*
.**********************************************************************
.*
.BY#GEN  ANOP
         MEXIT
.*
.**********************************************************************
.*
         MEND
