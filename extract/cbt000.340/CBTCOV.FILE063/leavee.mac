         MACRO
&NAME    LEAVEE &SAVE,&CODE,&LENGTH,&T,&NR
.*********************************************************************.
.*                                                                   *.
.*  THE 'LEAVEE' MACRO IS DESIGNED TO PRODUCE AN EXIT ROUTINE THAT IS*.
.*  USED IN COMBINATION WITH THE 'ENTER' MACRO.  IT PRODUCES A       *.
.*  COMPANION RE-ENTRANT ROUTINE FOR 'ENTER'.                        *.
.*                                                                   *.
.*  ALL OPERANDS ARE OPTIONAL AND MAY BE OMITED AS DESIRED.  ANY     *.
.*  OPERAND TO THE LEFT OF ONE THAT IS USED MUST BE PRESENT OR MUST  *.
.*  BE INDICATED AS A NULL PARAMETER BY THE USE OF A COMMA.          *.
.*                                                                   *.
.*  A LABEL MAY BE WRITTEN FOR THE MACRO.  IT WILL ESTABLISH A POINT *.
.*  THAT THE USER MAY BRANCH TO TO ENTER THE 'LEAVEE' MACRO.         *.
.*                                                                   *.
.*  THE FIRST OPERAND EXPRESSES THOSE REGISTERS WHICH THE SUBROUTINE *.
.*  WANTS TO RETURN TO THE CALLING PROGRAM UNCHANGED FROM THEIR      *.
.*  CURRENT VALUES. THIS OPERAND IS EXPRESSED AS A SINGLE NUMBER OR, *.
.*  AS A PAIR OF NUMBERS ENCLOSED WITHIN PARENTHESES AND SEPARATED BY*.
.*  A COMMA.  THE NUMBER OR NUMBERS MUST BE IN THE RANGE 0 - 12. THE *.
.*  FIRST REGISTER MAY BE HIGHER NUMBERED THAN THE SECOND REGISTER.  *.
.*                                                                   *.
.*  THE SECOND OPERAND IS USED AS A RETURN CODE IN REG15.  THE USER  *.
.*  CODES AN EXPLICIT NUMBER (0 - 4095) OR USES THE SPECIAL REGISTER *.
.*  NOTATION '(N)' , WHERE N IS A GPREG THAT ALREADY CONTAINS THE    *.
.*  VALUE TO BE USED AS THE RETURN CODE.  THE RETURN CODE WILL BE    *.
.*  IN REG15 WHEN THE RETURN IS COMPLETED.  IF THIS OPERAND IS       *.
.*  OMITTED, REGISTER 15 WILL BE RESTORED TO ITS VALUE ON ENTRY.     *.
.*                                                                   *.
.*  THE THIRD OPERAND IS THE NUMBER OF ADDITONAL BYTES BEYOND THE 72 *.
.*  BYTE SAVEAREA THAT WERE ACQUIRED BY THE 'ENTER' MACRO.  THESE    *.
.*  BYTES WILL BE RELEASED ALONG WITH THE SAVEAREA UNLESS THE 'NR'   *.
.*  OPERAND IS CODED.  THE AREA FREED IS IN SUBPOOL 0.               *.
.*                                                                   *.
.*  THE FOURTH OPERAND IS THE LETTER 'T'.  IT PERFORMS THE SAME      *.
.*  FUNCTION AS THE OS 'RETURN' MACRO'S 'T' PARAMETER, I.E., IT      *.
.*  FLAGS THE REGISTER 14 FIELD IN THE HIGHER SAVEAREA TO INDICATE   *.
.*  THAT A RETURN HAS OCCURRED.                                      *.
.*                                                                   *.
.*  THE FIFTH OPERAND INDICATES THAT THE LEAVEE MACRO IS NOT TO      *.
.*  RELEASE THE SAVEAREA POINTED TO BY REG13.  THIS PERMITS THE      *.
.*  MACRO TO BE USED IN NON-REENTRANT PROGRAMS.  IT SHOULD BE THE    *.
.*  LETTERS 'NR'.                                                    *.
.*                                                                   *.
.*  EXAMPLES:  LEAVEE (0,1),8,100,T,NR     LEAVEE 1,(15),,,NR        *.
.*                                                                   *.
.*  WARNING: REG14 AND REG13 MAY NOT BE SAVED THROUGH THE EXECUTION  *.
.*           OF THE LEAVEE MACRO. NEITHER REGS 13 OR 14 MAY BE USED  *.
.*           TO PROVIDE A SOURCE FOR THE RETURN CODE.                *.
.*                                                                   *.
.*********************************************************************.
         LCLA  &WORK1,&WORK2,&OFFSET
         LCLC  &NAM1,&NAM2
         SPACE 3
*                            EXIT ROUTINE
         SPACE 1
&NAME    L     14,4(0,13)          PICKUP PTR TO HIGHER SAVE AREA
.CKCODE  AIF   (T'&CODE EQ 'O').CKSAVE      WAS A RETURN CODE OMITTED?
         AIF   ('&CODE'(1,1) EQ '(').INREG  YES.  IS IT IN A GPREG?
         AIF   (&CODE LT 0 OR &CODE GT 4095).BADCODE  IS IT IN RANGE?
         LA    15,&CODE                PUT RETURN CODE IN REG15
.STORE15 ST    15,16(0,14)         STORE RETURN CODE IN REG 15 SAVESPOT
.CKSAVE  AIF   (T'&SAVE EQ 'O').CHECKNR     WAS &SAVE OMITTED?
&WORK1   SETA  &SAVE(1)            NO, CHECK VALIDITY OF 1ST SAVE OPND
         AIF   (&WORK1 LT 0 OR &WORK1 GT 12).BADSAVE  IF OUTSIDE RANGE
         AIF   (N'&SAVE EQ 1).ONESAVE  ONLY ONE REG TO SAVE?
         AIF   (N'&SAVE NE 2).TOOMANY  NO. IS A RANGE SPECIFIED?
.IGNORE  ANOP
&WORK2   SETA  &SAVE(2)            PICKUP SECOND SAVE REG NUMBER
         AIF   (&WORK2 LT 1 OR &WORK2 GT 12).BADSAVE     CHECK VALIDITY
         AIF   (&WORK1 EQ &WORK2).SAMSAVE   SAME REG AS BOTH OPNDS?
&OFFSET  SETA  &WORK1*4+20         GET OVER WORDS 1 - 5 OF SAVE AREA
         AIF   (&WORK1 GT &WORK2).STORE2    SAVE REG1 GREATER THAN REG2
         STM   &WORK1,&WORK2,&OFFSET.(14)         SAVE THE REGISTERS
.CHECKNR AIF   (T'&NR EQ 'O').COMMON        RETAIN CURRENT SAVEAREA?
         AIF   ('&NR' EQ 'NR').RESET        SKIP OVER FREEMAIN CODE
         AIF   ('&NR' NE 'NR').BADNR
.COMMON  LR    1,13                SETUP TO FREE LOCAL SAVE AREA
         AIF   (T'&LENGTH EQ 'O').P1       IF NO LENGTH, SKIP RECOMPUTE
         AIF   (T'&LENGTH EQ 'U').CKLEN1   SELF-DEFINING LENGTH?
         AIF   (T'&LENGTH EQ 'N').CKLEN2   NAME OF EQUATED LABEL
.BADLEN  MNOTE 4,'* LENGTH OPERAND INVALID.'
         AGO   .P1
.CKLEN1  ANOP
&WORK1   SETA  K'&LENGTH
         AIF   (&WORK1 GT 8).BADLEN
         AGO   .OKLENTH
.CKLEN2  AIF   (&LENGTH GT 4095).BADLEN
.OKLENTH LA    0,&LENGTH           COMPUTE FREEMAIN SIZE
         AGO   .AOK
.P1      LA    0,72                     LENGTH OF AREA TO BE FREED
.AOK     FREEMAIN R,LV=(0),A=(1)
.RESET   LR    13,14               RESET SAVEAREA POINTER
         LM    14,12,12(13)        RESTORE GPREGS
         AIF   (T'&T EQ 'O').RETURN         RETURN INDICATION DESIRED?
         AIF   ('&T' NE 'T').BADT             CORRECT CODE (T)?
.RETFLAG MVI   12(13),X'FF'        INDICATE RETURN TO CALLING PROGRAM
.RETURN  BR    14                  RETURN TO CALLING PROGRAM
         SPACE 1
         MEXIT
.BADSAVE SPACE 1
         MNOTE 4,'*** REGS TO BE SAVED NOT IN RANGE 0-12. IGNORED.'
         SPACE 1
         AGO   .CHECKNR
.SAMSAVE SPACE 1
         MNOTE 4,'*** REGS SPECIFIED FOR SAVING ARE AN INVALID RANGE.'
         MNOTE '      TREATED AS SAVE ONE REGISTER ONLY.'
         SPACE 1
.ONESAVE ANOP
&OFFSET  SETA  &WORK1*4+20         COMPUTE OFFSET INTO SAVEAREA
         ST    &WORK1,&OFFSET.(0,14)          SAVE SPECIFIED REGISTER
         AGO   .CHECKNR
.TOOMANY SPACE 1
      MNOTE 4,'*** TOO MANY REGS TO SAVE. 1ST && 2ND TAKEN AS A RANGE.'
         SPACE 1
         AGO   .IGNORE
.INREG   AIF   (&CODE(1) GT 15 OR &CODE(1) LT 0 ).BADCODE
         AIF   (&CODE(1) EQ 15).STORE15     CODE IS ALREADY IN REG15
         ST    &CODE(1).,16(0,14)         STORE RETCD IN REG15 SAVESPOT
         AGO   .CKSAVE
.BADNR   SPACE 1
       MNOTE *,'*** IMPROPER 5TH OPND, ''NR'' (NON-REENTRANT) ASSUMED.'
         SPACE 1
         AGO   .RESET
.BADT    SPACE 1
         MNOTE *,'*** IMPROPER 4TH OPND, ''T'' (FLAG RETURN) ASSUMED.'
         SPACE 1
         AGO   .RETFLAG
.STORE2  STM   &WORK1,12,&OFFSET.(14)         SPLIT REGS TO BE SAVED
         STM   0,&WORK2,20(14)            INTO TWO GROUPS
         AGO   .CHECKNR
.BADCODE SPACE 1
         MNOTE 4,'*** RETURN CODE NOT 0-4095 OR NOT IN VALID GPREG.'
         MNOTE '      ZERO WILL BE USED INSTEAD.'
         SPACE 1
.NOCODE  SR    15,15               ZERO REGISTER 15.
         AGO   .STORE15
         MEND
