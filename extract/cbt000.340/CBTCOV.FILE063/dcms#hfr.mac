         MACRO
&NAME    DCMS#HFR &DIR,&N,&IMAGE
.*.
.*$MACRO=DCMS#HFR   DATE=03/31/80       SOURCE=LDW
.*$MACRO=#HFRAME    DATE=10/23/79       SOURCE=DCMS (DLH@SDC)
.*
.*  THIS MACRO IS PART OF DCMS.
.*
.* UPDATES:
.*    02/24/81 SDM  - MAJOR CHANGE:  FIX FOR STACK SIZE NOT
.*                    MULTIPLE OF FRAME SIZE
.*.
         LCLC  &IHBNX                                              LDW1
&IHBNX   SETC  'IHB&SYSNDX'                                        LDW1
&NAME    DS    0H
         AIF   ('&DIR' EQ 'FORWARD').FOR
         AIF   ('&DIR' EQ 'F').FOR
         AIF   ('&DIR' EQ 'BACKWARD').BAK
         AIF   ('&DIR' EQ 'B').BAK
         MNOTE 9,'DIRECTION SPECIFICATION MISSING OR INVALID'
         MEXIT
.FOR     L     R15,#NOW&N               CURRENT FRAME (SOURCE)
         LA    R14,&IMAGE               TRMIO IMAGE ADDRESS (DEST)
         L     R1,#MVC&N                NUMBER OF BYTES PER ENTRY
         BCTR  R1,0                     -1 FOR EX INSTR
         L     R2,#LPF&N                NUMBER OF LINES TO MOVE
         B     &IHBNX.L
&IHBNX.M MVC   0(0,R14),0(R15)          << EXECUTED >>
&IHBNX.L C     R15,#END&N               EXCEEDED STACK?
         BNL   &IHBNX.X                 YES, GET OUT NOW
         EX    R1,&IHBNX.M              MOVE CURRENT LINE
         LA    R14,79(,R14)             NEXT LINE
         LA    R15,79(,R15)                NEXT LINE
         BCT   R2,&IHBNX.L
&IHBNX.X L     R1,#NOW&N                POINT TO
         A     R1,#SCR&N                           NEXT FRAME
         C     R1,#END&N                FREEZE
         BNH   *+8                              IF
         L     R1,#END&N                            NECESSARY
         ST    R1,#NOW&N                SAVE NEXT FRAME ADDRESS
         MEXIT
.BAK     ANOP
         MEND
