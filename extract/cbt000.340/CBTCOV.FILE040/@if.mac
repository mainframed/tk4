         TITLE 'P D S  --  PDS IF                      09/28/88'
***********************************************************************
***      IF SUBCOMMAND         ADDED BY BRUCE LELAND -- JAN., 1983  ***
***********************************************************************
*
         SPACE 1
IF       CSECT
         USING *,R8
         OI    FLAGSGG,FOUTSUB        SUBCOMMAND GAINED CONTROL
         OC    #ACTIONT(8),#ACTIONT   ANY THEN OR ELSE?
         BNZ   *+10                   YES, BRANCH
         MVC   #ACTIONT(4),IFLA$ATT   DEFAULT THEN ACTION IS ATTRIB
         SPACE 1
         TM    #OPTOPT,X'80'        PARSE INVOKED?
         BNO   IF996                NO, DONE
         CLC   #ATTRLO(3),DIRTTR    IN TTR LOWER BOUND?
         BH    IF997                NO, DO ANY ELSE
         CLC   #ATTRHI(3),DIRTTR    IN TTR UPPER BOUND?
         BL    IF997                NO, DO ANY ELSE
         SPACE 1
         TM    FLAGSCC,RECFMU       A LOAD LIBRARY?
         BO    IF006                YES, BRANCH
         LA    R1,L701              ASSUME LINKEDIT ATTRIBUTES
         TM    #OPTOPT,X'01'        CORRECT?
         BO    MSGNEWXX             YES, ERROR
         B     IF016
         SPACE 2
IF006    MVC   MODESAVE(1),DIRATTR3 RMODE/AMODE BYTE
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BO    *+8                  YES, BRANCH
         MVI   MODESAVE,0           NO, NO MVS/XA BITS
         SR    R15,R15
         IC    R15,MODESAVE
         SRA   R15,2                AMODE FOR ALIAS
         TM    DIRFLAG,X'80'        ALIAS?
         BNO   *+8                  NO, BRANCH
         STC   R15,MODESAVE         YES, USE ALIAS AMODE BITS
         NI    MODESAVE,DIRAM24+DIRAM31 TURN OFF OTHER BITS
         SPACE 3
IF016    CLI   #RMODE,X'01'         ANY RMODE SELECTION?
         BL    IF036                NO, BRANCH
         MVC   DOUBLE(1),DIRATTR3   RMODE/AMODE BYTE
         NI    DOUBLE,DIRRMANY      TURN OFF OTHER BITS
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BO    *+8                  YES, BRANCH
         MVI   DOUBLE,0             NO, NO MVS/XA BITS
         SR    R15,R15
         IC    R15,#RMODE
         SLA   R15,2                #RMODE*4
         B     *(R15)               CHECK FOR:
         B     IF020                  RMODE=24
         B     IF030                  RMODE=ANY
         B     IF030                  NO RMODE=24
         B     IF020                  NO RMODE=ANY
IF020    CLI   DOUBLE,0             RMODE=24?
         BNE   IF997                NO, DO ANY ELSE
         B     IF036                YES, BRANCH
IF030    CLI   DOUBLE,0             RMODE=24?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF036    CLI   #AMODE,X'01'         ANY AMODE SELECTION?
         BL    IF102                NO, BRANCH
         SR    R15,R15
         IC    R15,#AMODE
         SLA   R15,2                #AMODE*4
         B     *(R15)               CHECK FOR:
         B     IF040                  AMODE=24
         B     IF050                  AMODE=31
         B     IF060                  AMODE=ANY
         B     IF070                  NO AMODE=24
         B     IF080                  NO AMODE=31
         B     IF090                  NO AMODE=ANY
IF040    CLI   MODESAVE,DIRAM24     AMODE=24?
         BH    IF997                NO, DO ANY ELSE
         B     IF102                YES, BRANCH
IF050    CLI   MODESAVE,DIRAM31     AMODE=31?
         BNE   IF997                NO, DO ANY ELSE
         B     IF102                YES, BRANCH
IF060    CLI   MODESAVE,DIRAM24+DIRAM31  AMODE=ANY?
         BNE   IF997                     NO, DO ANY ELSE
         B     IF102                     YES, BRANCH
IF070    CLI   MODESAVE,DIRAM24     AMODE=24?
         BNH   IF997                YES, DO ANY ELSE
         B     IF102                NO, BRANCH
IF080    CLI   MODESAVE,DIRAM31     AMODE=31?
         BE    IF997                YES, DO ANY ELSE
         B     IF102                NO, BRANCH
IF090    CLI   MODESAVE,DIRAM24+DIRAM31  AMODE=ANY?
         BE    IF997                     YES, DO ANY ELSE
         SPACE 3
IF102    CLI   #ALIAOPT,X'01'       "ALIAS" OR "NOALIAS"?
         BL    IF106                NEITHER, BRANCH
         BH    IF104                "NOALIAS", BRANCH
         TM    DIRFLAG,X'80'        "ALIAS" -- CORRECT?
         BO    IF106                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF104    TM    DIRFLAG,X'80'        ALIAS?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF106    CLI   #VSLKED,X'01'        "VSLKED" OR "NOVSLKED"?
         BL    IF110                NEITHER, BRANCH
         BH    IF108                "NOVSLKED", BRANCH
         TM    DIRATTR2,DIRAOSLE    "VSLKED" -- CORRECT?
         BO    IF110                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF108    TM    DIRATTR2,DIRAOSLE    VSLKED?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF110    CLI   #AUTHOPT,X'01'       "AUTH" OR "NOAUTH"?
         BL    IF113                NEITHER, BRANCH
         LA    R2,ZERO              ASSUME NOT AUTHORIZED
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  APF INFORMATION AVAILABLE?
         BNO   IF111                       NO, NOT AUTHORIZED
         LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    IF110A               YES, BRANCH
***PDSMAN CHANGES: IF AUTH/NOAUTH                             ABL NOV86
         TM    5(R2),X'0F'          PDSMAN DATE?              ABL NOV86
         BO    IF110B               YES, BRANCH               ABL NOV86
         TM    6(R2),X'0F'          PDSMAN DATE?              ABL NOV86
         BO    IF110B               YES, BRANCH               ABL NOV86
         TM    4+5(R2),X'0F'        PDSMAN DATE AND SSI?      ABL NOV86
         BO    IF110B               YES, BRANCH               ABL NOV86
         TM    4+6(R2),X'0F'        PDSMAN DATE AND SSI?      ABL NOV86
         BO    IF110B               YES, BRANCH               ABL NOV86
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
*        BE    IF110B               NO, BRANCH
         B     IF110B               BRANCH                    DRK JAN98
IF110A   LA    R2,11(,R2)           ADD ALIAS LENGTH          ABL NOV86
IF110B   DS    0H                                             ABL NOV86
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF111                NO, BRANCH
         LA    R2,5(,R2)            YES, FOUR MORE BYTES
         N     R2,=F'-2'                                ROUNDED
IF111    CLI   #AUTHOPT,X'01'       "AUTH" OR "NOAUTH"
         BH    IF112                "NOAUTH", BRANCH
         CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF997                NO, CANNOT BE AUTHORIZED
         CLI   1(R2),0              "AUTH" -- CORRECT?
         BH    IF113                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF112    CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF113                NO, CANNOT BE AUTHORIZED
         CLI   1(R2),0              AUTHORIZED?
         BH    IF997                YES, DO ANY ELSE
         SPACE 3
IF113    CLI   #APFERR,X'01'        "APFERR" OR "NOAPFERR"?
         BL    IF116                NEITHER, BRANCH
         LA    R2,=X'01010101'      ASSUME NO APF ERROR
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  APF INFORMATION AVAILABLE?
         BNO   IF114                       NO, NO APF ERROR
         LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    IF113A               YES, BRANCH
***PDSMAN CHANGES: IF APFERR/NOAPFERR                         ABL NOV86
         TM    5(R2),X'0F'          PDSMAN DATE?              ABL NOV86
         BO    IF113B               YES, BRANCH               ABL NOV86
         TM    6(R2),X'0F'          PDSMAN DATE?              ABL NOV86
         BO    IF113B               YES, BRANCH               ABL NOV86
         TM    4+5(R2),X'0F'        PDSMAN DATE AND SSI?      ABL NOV86
         BO    IF113B               YES, BRANCH               ABL NOV86
         TM    4+6(R2),X'0F'        PDSMAN DATE AND SSI?      ABL NOV86
         BO    IF113B               YES, BRANCH               ABL NOV86
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
*        BE    IF113B               NO, BRANCH
         B     IF113B               BRANCH                    DRK JAN98
IF113A   LA    R2,11(,R2)           ADD ALIAS LENGTH          ABL NOV86
IF113B   DS    0H                                             ABL NOV86
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF114                NO, BRANCH
         LA    R2,5(,R2)            YES, FOUR MORE BYTES
         N     R2,=F'-2'                                ROUNDED
IF114    CLI   #APFERR,X'01'        "APFERR" OR "NOAPFERR"
         BH    IF115                "NOAPFERR", BRANCH
         CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BE    IF997                YES, DO ANY ELSE
         B     IF116                NO, BRANCH
IF115    CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF997                NO, DO ANY ELSE
         SPACE 3
IF116    CLI   #PAGEOPT,X'01'       "PAGE" OR "NOPAGE"?
         BL    IF120                NEITHER, BRANCH
         BH    IF118                "NOPAGE", BRANCH
         TM    DIRATTR2,DIR2PAGA    "PAGE" -- CORRECT?
         BO    IF120                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF118    TM    DIRATTR2,DIR2PAGA    PAGE?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF120    CLI   #SSIOPT,X'01'        "SSI" OR "NOSSI"?
         BL    IF130                NEITHER, BRANCH
         TM    FLAGSCC,RECFMU       RECFM=U?
         BO    IF122                YES, BRANCH
         LA    R2,ZERO              ASSUME NO SSI
         TM    DIRFLAG,X'0F'        SPF STATISTICS?
         BO    IF124                YES, NO SSI INFORMATION
         LA    R2,DIRUSER           POINT TO SSI INFORMATION
         B     IF124                CHECK FOR ANY SSI
IF122    LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    IF122A               YES, BRANCH
***PDSMAN CHANGES: IF SSI/NOSSI                               ABL NOV86
         TM    5(R2),X'0F'          PDSMAN DATE?              ABL NOV86
         BO    IF122B               YES, BRANCH               ABL NOV86
         TM    6(R2),X'0F'          PDSMAN DATE?              ABL NOV86
         BO    IF122B               YES, BRANCH               ABL NOV86
         TM    4+5(R2),X'0F'        PDSMAN DATE AND SSI?      ABL NOV86
         BO    IF122B               YES, BRANCH               ABL NOV86
         TM    4+6(R2),X'0F'        PDSMAN DATE AND SSI?      ABL NOV86
         BO    IF122B               YES, BRANCH               ABL NOV86
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
*        BE    IF122B               NO, BRANCH
         B     IF122B               BRANCH                    DRK JAN98
IF122A   LA    R2,11(,R2)           ADD ALIAS LENGTH          ABL NOV86
IF122B   DS    0H                                             ABL NOV86
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BNO   IF124                NO, BRANCH
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF128                NO, BRANCH
IF124    LA    R2,1(,R2)            ROUND TO START
         N     R2,=F'-2'                          OF NEXT HALFWORD
         CLC   ZERO,0(R2)           ZERO?
         BE    IF128                YES, NO SSI
         CLC   =F'-1',0(R2)         ALL ONES?
         BE    IF128                YES, NO SSI
IF126    CLI   #SSIOPT,X'02'        SEARCH FOR "NOSSI"?
         BE    IF997                YES, BRANCH
         TM    #SSIOPT,X'01'+X'04'  SEARCH FOR "SSI(VALUE)"?
         BNO   IF130                NO, BRANCH
         SR    R0,R0
         IC    R0,SHIFTSSI          # OF SHIFT POSITIONS FOR PARTSSI
         L     R14,0(R2)            SAVE FOR LATER
         ICM   R15,B'1111',SSIMASK  SAVE FOR LATER
         L     R1,#SSITEXT          SAVE FOR LATER
         MVC   DOUBLE(4),0(R2)      GET THE SSI VALUE
         NC    DOUBLE(4),SSIMASK    MASK OFF LOWER BITS
         CLC   DOUBLE(4),#SSITEXT   THIS SSI?
         BE    IF130                YES, BRANCH
         TM    #SSIOPT,X'02'+X'04'  SEARCH FOR "PARTSSI(VALUE)"?
         BNO   IF997                NO, BRANCH
IF127    A     R0,=F'-1'            ANY MORE DIGITS?
         BM    IF997                NO, BRANCH
         SRL   R15,4                GET THE NEXT NIBBLE
         SRL   R1,4                 GET THE NEXT NIBBLE
         LR    R2,R14               GET THE SSI VALUE
         NR    R2,R15               MASK OFF OTHER BITS
         CR    R2,R1                THIS SSI?
         BE    IF130                YES, BRANCH
         B     IF127                NO, KEEP LOOKING
         SPACE 1
IF128    CLI   #SSIOPT,X'02'        SEARCH FOR "NOSSI"?
         BNE   IF997                NO, DO ANY ELSE
         SPACE 3
IF130    TM    FLAGSCC,RECFMU       RECFM=U?
         BNO   IF140                NO, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#ATTRYES  ANY YES+0 ATTRIBUTES?
         BZ    IF132                NO, BRANCH
         TM    DIRATTR,*-*          <<EXECUTED>>
         EX    R1,*-4               ALL ATTRIBUTES MATCH?
         BNO   IF997                NO, DO ANY ELSE
         SPACE 1
IF132    ICM   R1,B'0001',#ATTRYES+1 ANY YES+1 ATTRIBUTES?
         BZ    IF134                NO, BRANCH
         TM    DIRATTR+1,*-*        <<EXECUTED>>
         EX    R1,*-4               ALL ATTRIBUTES MATCH?
         BNO   IF997                NO, DO ANY ELSE
         SPACE 1
IF134    ICM   R1,B'0001',#ATTRNO   NO+0 ATTRIBUTES
         TM    DIRATTR,*-*          <<EXECUTED>>
         EX    R1,*-4               ANY ATTRIBUTES MATCH?
         BNZ   IF997                YES, DO ANY ELSE
         SPACE 1
         ICM   R1,B'0001',#ATTRNO+1 NO+1 ATTRIBUTES
         TM    DIRATTR+1,*-*        <<EXECUTED>>
         EX    R1,*-4               ANY ATTRIBUTES MATCH?
         BNZ   IF997                YES, DO ANY ELSE
         SPACE 3
IF140    CLI   #USERIDO,X'01'       ID, NOID OR NOTID?
         BL    IF144                NO, BRANCH
         LA    R1,L702              ASSUME DATA SET IS A LOAD MODULE
         TM    FLAGSCC,RECFMU       CORRECT?
         BO    MSGNEWXX             YES, ERROR
         TM    DIRFLAG,X'0F'        SPF STATISTICS?
         BNO   IF143                NO, BRANCH
         CLI   #USERIDO,X'02'       "NOID" DESIRED?
         BE    IF997                YES, DO ANY ELSE
         SR    R1,R1
         ICM   R1,B'0001',#USERIDL  ANY ID FILTERING?
         BP    IF141                YES, BRANCH
         CLI   #USERIDO,X'01'       "ID" DESIRED?
         BE    IF144                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
         SPACE 1
IF141    BCTR  R1,0                 MACHINE LENGTH
         CLI   #USERIDO,X'01'       "ID" DESIRED?
         BNE   IF142                NO, BRANCH
         EX    R1,IF142             PARTIAL ID MATCH?
         BNE   IF997                NO, DO ANY ELSE
         B     IF144                YES, BRANCH
         SPACE 1
IF142    CLC   #USERTXT(*-*),DIRSPFID <<EXECUTED>>
         EX    R1,*-6               PARTIAL ID MATCH?
         BE    IF997                YES, DO ANY ELSE
         B     IF144                NO, BRANCH
         SPACE 1
IF143    CLI   #USERIDO,X'01'       "ID" DESIRED?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF144    TM    #OPTOPT,X'02'        NEED ANY MAP OR HISTORY DATA?
         BNO   IF156                NO, BRANCH
         MVI   RLDCOUNT,X'FF'       ASSUME NO RLD/CONTROL DATA FOUND
         SPACE 1
         L     R15,=A(READESD)
         BASR  R14,R15              ANY ESD DATA?
         NOP   0                    NO, IGNORE
         SPACE 1
         ICM   R2,B'1110',#ZAPOPT       SAVE ZAP/USERDATA/TRANS
         MVC   #ZAPOPT(3),=X'01010101'  CHECK ZAP/USERDATA/TRANS
         MVC   LKEDDATE+2,X'FF'         NO LINKAGE-EDIT DATE FOUND
         L     R15,=A(READIDR)
         BASR  R14,R15              ANY IDR DATA?
         B     IF145                NO, BRANCH
         SPACE 2
         MVI   #LKEDOPT,0           ASSUME LINKAGE-EDITOR FOUND
         SR    R1,R1
         ICM   R1,B'0001',#LKEDLEN  ANY LKED NAME CHECK?
         BZ    IF145                NO, BRANCH
         BCTR  R1,0                 MACHINE COMPARE LENGTH
         CLC   LKEDNAME(*-*),#LKEDTXT  <<EXECUTED>>
         EX    R1,*-6               THIS PARTIAL LKED NAME?
         BE    IF145                YES, BRANCH
         MVI   #LKEDOPT,1           NO, LINKAGE-EDITOR NOT FOUND
         SPACE 1
IF145    OC    #IFCREIF(6),#IFCREIF        ANY CREATION DATE RANGE?
         BZ    IF146                       NO, CONTINUE
         ICM   R1,B'0100',LKEDDATE+2       YY OF MMDDYY
         ICM   R1,B'0011',LKEDDATE         MMDD OF MMDDYY
         ICM   R1,B'1000',=X'00'           ASSUME 19XX        Y2K AUG97
         CLI   LKEDDATE+2,X'&WIN19XX'      BELOW 19XX WINDOW? Y2K AUG97
         BNL   IF145A                      NO,  THEN 19XX     Y2K AUG97
         ICM   R1,B'1000',=X'01'           YES, THEN 20XX     Y2K AUG97
IF145A   DS    0H                                             Y2K AUG97
         MVC   DOUBLE+1(3),#IFCREIF        YYMMDD WORK AREA   Y2K AUG97
         MVI   DOUBLE,X'00'                ASSUME 19XX        Y2K AUG97
         CLI   #IFCREIF,X'&WIN19XX'        BELOW 19XX WINDOW? Y2K AUG97
         BNL   IF145B                      NO,  THEN 19XX     Y2K AUG97
         MVI   DOUBLE,X'01'                YES, THEN 20XX     Y2K AUG97
IF145B   DS    0H                                             Y2K AUG97
         CLC   DOUBLE+1(3),=X'000100'    BEFORE/TO 1ST DATE?  Y2K AUG97
         BE    *+12                      YES, IGNORE 1ST DATE Y2K AUG97
         CLM   R1,B'1111',DOUBLE           VALUE < 1ST YYMMDD Y2K AUG97
         BL    IF997                       YES, DO ANY ELSE
         MVC   DOUBLE+5(3),#IFCREIF+3      YYMMDD WORK AREA   Y2K AUG97
         MVI   DOUBLE+4,X'00'              ASSUME 19XX        Y2K AUG97
         CLI   #IFCREIF+3,X'&WIN19XX'      BELOW 19XX WINDOW? Y2K AUG97
         BNL   IF145C                      NO,  THEN 19XX     Y2K AUG97
         MVI   DOUBLE+4,X'01'              YES, THEN 20XX     Y2K AUG97
IF145C   DS    0H                                             Y2K AUG97
         CLM   R1,B'1111',DOUBLE+4         VALUE > 2ND YYMMDD Y2K AUG97
         BH    IF997                       YES, DO ANY ELSE
         SPACE 2
IF146    STCM  R2,B'1110',#ZAPOPT   RESTORE ZAP/USERDATA/TRAN
         USING ESDENTRY,R2
         LA    R2,#ESDPTR           SCAN ESD CHAIN
         TM    #MODOPT,X'01'+X'02'  WKEXTERN OR EXTERN?
         BZ    IF148                NO, BRANCH
         OC    0(4,R2),0(R2)        ANY FOUND?
         BZ    IF148                NO, BRANCH
         MVI   #MODOPT,0            YES, DROP MODULE/WKEXTERN/EXTERN
         SPACE 1
IF148    ICM   R2,B'1111',ESDLINK   ANY MORE ESD ENTRIES?
         BZ    IF154                NO, BRANCH
         CLI   ESDTYPE,CODESD       CSECT ESD ENTRY?
         BNE   IF148                NO, BRANCH
         MVI   #MODOPT,0            YES, MODULE FOUND         ABL FEB87
         SPACE 1
         USING IDRENTRY,R3
         LA    R3,#IDRPTR           SCAN IDR CHAIN
         SPACE 1
IF150    ICM   R3,B'1111',IDRLINK   ANY MORE IDR ENTRIES?
         BZ    IF148                NO, BRANCH
         CLC   ESDID,IDRESDID       THIS CSECT IDENTIFIER?
         BNE   IF150                NO, BRANCH
         SPACE 1
***TEST  MVI   #MODOPT,0            YES, MODULE FOUND
         CLI   IDRTYPE,IDRTRAN      ZAP RECORD?
         BNL   IF152                NO, BRANCH
         MVI   #ZAPOPT,0            YES, ZAP RECORDS MATCH
         OC    #IFMODIF(6),#IFMODIF       ANY MODIFICATION CHECK?
         BE    IF152                      NO, BRANCH
         ICM   R1,B'0100',IDRDATE+2       YY OF MMDDYY
         ICM   R1,B'0011',IDRDATE         MMDD OF MMDDYY
         ICM   R1,B'1000',=X'00'          ASSUME 19XX         Y2K AUG97
         CLI   IDRDATE+2,X'&WIN19XX'      BELOW 19XX WINDOW?  Y2K AUG97
         BNL   IF150A                     NO,  THEN 19XX      Y2K AUG97
         ICM   R1,B'1000',=X'01'          YES, THEN 20XX      Y2K AUG97
IF150A   DS    0H                                             Y2K AUG97
         MVC   DOUBLE+1(3),#IFMODIF       YYMMDD WORK AREA    Y2K AUG97
         MVI   DOUBLE,X'00'               ASSUME 19XX         Y2K AUG97
         CLI   #IFMODIF,X'&WIN19XX'       BELOW 19XX WINDOW?  Y2K AUG97
         BNL   IF150B                     NO,  THEN 19XX      Y2K AUG97
         MVI   DOUBLE,X'01'               YES, THEN 20XX      Y2K AUG97
IF150B   DS    0H                                             Y2K AUG97
         CLC   DOUBLE+1(3),=X'000100'    BEFORE/TO 1ST DATE?  Y2K AUG97
         BE    *+12                      YES, IGNORE 1ST DATE Y2K AUG97
         CLM   R1,B'1111',DOUBLE          VALUE < 1ST YYMMDD  Y2K AUG97
         BL    IF152                      YES, NO MATCH
         MVC   DOUBLE+5(3),#IFMODIF+3     YYMMDD WORK AREA    Y2K AUG97
         MVI   DOUBLE+4,X'00'             ASSUME 19XX         Y2K AUG97
         CLI   #IFMODIF+3,X'&WIN19XX'     BELOW 19XX WINDOW?  Y2K AUG97
         BNL   IF150C                     NO,  THEN 19XX      Y2K AUG97
         MVI   DOUBLE+4,X'01'             YES, THEN 20XX      Y2K AUG97
IF150C   DS    0H                                             Y2K AUG97
         CLM   R1,B'1111',DOUBLE+4        VALUE > 2ND YYMMDD  Y2K AUG97
         BH    IF152                      YES, NO MATCH
         XC    #IFMODIF(6),#IFMODIF       A MODIFICATION RECORD MATCHES
         SPACE 1
IF152    CLI   IDRTYPE,IDRTRAN      IDR RECORD TYPE:
         BNH   *+8
         MVI   #USEROPT,0              **USERDATA
         BNE   *+8
         MVI   #TRANOPT,0              **TRANSLATOR
         B     IF150
         DROP  R2,R3
IF154    OC    #IFMODIF(6),#IFMODIF ANY CHANGES MATCH?
         BNZ   IF997                NO, DO ANY ELSE CONDITIONS
         SPACE 1
IF156    OC    #ZAPOPT(5),#ZAPOPT   ZAP/USER/TRAN/LKED/MODULE FOUND?
         BNZ   IF997                NO, DO ANY ELSE
         SPACE 3
         TM    DIRATTR2,DIRAOSLE    OS/VS LINKAGE EDITOR?
         BO    *+10                 YES, BRANCH
         MVC   RLDCOUNT(1),DIRATTR4 NO, WANT MATCHING RLD/CONTROL
         CLI   #RLDERR,X'01'        "RLDERR" OR "NORLDERR"
         BL    IF160                NEITHER, BRANCH
         BH    IF158                "NORLDERR", BRANCH
         CLC   DIRATTR4(1),RLDCOUNT "RLDERR" -- CORRECT?
         BNE   IF160                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF158    CLC   DIRATTR4(1),RLDCOUNT "NORLDERR" -- CORRECT?
         BNE   IF997                YES, DO ANY ELSE
         SPACE 3
IF160    NI    #IFABOVE,FF-X'01'    ASSUME SIZE IS NOT AVAILABLE
         TM    #IFABOVE,X'80'       ANY ABOVE CODED?
         BO    IF164                YES, BRANCH
         TM    #IFBELOW,X'80'       ANY BELOW CODED?
         BZ    IF170                NO, BRANCH
         SPACE 1
IF164    SR    R1,R1
         ICM   R1,B'0111',DIRSTORE  MODULE SIZE
         TM    FLAGSCC,RECFMU       RECFM=U?
         BO    IF168                YES, BRANCH
         TM    DIRFLAG,X'0F'        SPF STATISTICS PRESENT?
         BNO   IF180                NO, BRANCH
         TM    DIRSPFCR,X'FE'       0C OF 0CYYDDDF ZEROS?     Y2K AUG97
         BNZ   IF180                NO, BRANCH                Y2K AUG97
         TM    DIRSPFCD,X'FE'       0C OF OTHER 0CYYDDDF ZERO?Y2K AUG97
         BNZ   IF180                NO, BRANCH                Y2K AUG97
         SR    R1,R1                                          DRK JAN98
         ICM   R1,B'0011',DIRSPFSI  CURRENT SIZE              DRK JAN98
         SPACE 1
IF168    OI    #IFABOVE,X'01'       SIZE IS AVAILABLE
         ST    R1,#IFLEN            LENGTH OF THE MEMBER
         SPACE 1
IF170    TM    #OPTOPT,X'04'        NEED TO READ THE MEMBER?
         BNO   IF300                NO, BRANCH
         SPACE 1
IF180    SR    R6,R6                COUNT OF INPUT LOGICAL RECORDS
         OI    ##ADRPA#,$Q          QUIET MODE READ IS REQUIRED
         MVC   STARTTR(3),DIRTTR    FIRST DISK ADDRESS TO READ
         CLC   DIRTTR(3),DS1LSTAR   BEYOND END OF DATA MARKER?
         BL    IF204                NO, BRANCH
*** PAST DS1LSTAR -- PERHAPS WE NEED TO REOPEN THE DATA SET
         OI    FLAGSJJ,FNOREAD      CALL EXCP FOR OPEN ONLY
         L     R15,=V(EXCP)
         BASR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD   EXCP CALL IS COMPLETE
         SPACE 1
         CLC   DIRTTR(3),DS1LSTAR   BEYOND END OF DATA MARKER?
         BNL   IF242                YES, REALLY PAST DS1LSTAR
         MVC   STARTTR(3),DIRTTR    FIRST DISK ADDRESS TO READ
         SPACE 3
IF204    L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)             PROCESS RETURN CODE
         B     IF208                  00 - SUCCESSFUL READ
         B     IF240                  04 - END OF MEMBER
         B     IF240                  08 - END OF DATA SET
         B     IF242                  12 - I/O ERROR
         SPACE 2
IF208    LR    R3,R0                BUFFER START
         L     R5,LS                BLOCK LENGTH
         CH    R5,#MAXBLKI          VALID BLKSIZE?
         BNH   IF220                YES, BRANCH
         OC    #MAXBLKI(2),#MAXBLKI ANY MAXBLK()?
         BNZ   IF210                YES, BRANCH
         CH    R5,BLKSI             BLKSIZE VALID?
         BNH   IF220                YES, BRANCH
IF210    OI    #OPTOPT,X'20'        NO, INDICATE A BLOCKSIZE ERROR
         SPACE 1
IF220    TM    FLAGSCC,RECFMF       FIXED FORMAT?
         BNO   IF222                NO, BRANCH
         LH    R14,LRECL            CURRENT LRECL
         LR    R0,R5                BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14               BLKSIZE/LRECL
         LTR   R0,R0                ANY REMAINDER?
         BZ    IF222                YES, BRANCH
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (BLKSIZE/LRECL)
         SPACE 1
IF222    LR    R1,R5
         AR    R5,R3                END OF BUFFER
         BCTR  R5,0                 END OF BUFFER -1
         TM    FLAGSCC,RECFMF       RECFM=F?
         LH    R4,LRECL
         BO    IF228                YES, BRANCH
         TM    FLAGSCC,RECFMU       RECFM=U?
         LR    R4,R1
         BO    IF228                YES, BRANCH
         TM    FLAGSCC,RECFMV       RECFM=V?
         BNO   IF228                NO, ASSUME RECFM=U
         AH    R3,=H'4'             SKIP BLOCK LENGTH WORD
         B     IF226
         SPACE 3
IF224    BXLE  R3,R4,IF226          GET THE NEXT LOGICAL RECORD
         B     IF204                END OF BLOCK, BRANCH
         SPACE 2
IF226    TM    FLAGSCC,RECFMV       RECFM=V?
         BNO   IF228                NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)     RECORD LENGTH +4
         LR    R14,R4               RECORD LENGTH +4
         AH    R3,=H'4'             SKIP RECORD LENGTH WORD
         SH    R4,=H'4'             RECORD LENGTH VALID?
         BNM   IF228                YES, BRANCH
         SR    R4,R4                NO, USE LENGTH 0
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (SHORT RECORD)
         B     IF224                SKIP THE RECORD
         SPACE 2
IF228    LA    R6,1(,R6)            ANOTHER OUTPUT RECORD
         CH    R14,LRECL            VALID LRECL VALUE?
         BNH   IF224                YES, BRANCH
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (LONG RECORD)
         B     IF224
         SPACE 2
IF240    TM    DIRFLAG,DIR2TTR+DIR1TTR    ANY USER TTR?
         BZ    IF244                      NO, BRANCH
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BASR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLM   R0,B'1110',DIRSTART        TTR ACTUALLY INPUT?
         BNH   IF242                      NO, I/O ERROR
         TM    DIRFLAG,DIR2TTR            A SECOND TTR TOO?
         BZ    IF244                      NO, BRANCH
         CLM   R0,B'1110',DIRNOTE         TTR ACTUALLY INPUT?
         BH    IF244                      YES, BRANCH
         SPACE 1
IF242    OI    #OPTOPT,X'40'        PERMANENT I/O ERROR
         SPACE 2
IF244    LTR   R6,R6                ANY RECORDS?
         BNZ   IF246                YES, BRANCH
         OI    #OPTOPT,X'08'        NO, MARK AS NULL
         SPACE 3
IF246    NI    ##ADRPA#,FF-$Q       END OF QUIET MODE READ
         CLI   #IOERROR,X'01'       "IOERROR" OR "NOIOERROR"?
         BL    IF260                NEITHER, BRANCH
         BH    IF248                "NOIOERROR", BRANCH
         TM    #OPTOPT,X'40'        "IOERROR" -- CORRECT?
         BO    IF260                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF248    TM    #OPTOPT,X'40'        IOERROR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF260    CLI   #BLOCKER,X'01'       BLOCKERR/MAXBLK(SIZE)/NOBLOCKERR?
         BL    IF266                NONE, BRANCH
         CLI   #BLOCKER,X'02'       "BLOCKERR" OR "MAXBLK(SIZE)?
         BH    IF262                NO, BRANCH
         TM    #OPTOPT,X'20'        "BLOCKERR" -- CORRECT?
         BO    IF266                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF262    TM    #OPTOPT,X'20'        BLOCKERR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF266    CLI   #LRECLER,X'01'       "LRECLERR" OR "NOLRECLERR"?
         BL    IF280                NEITHER, BRANCH
         BH    IF268                "NOLRECLERR", BRANCH
         TM    #OPTOPT,X'10'        "LRECLERR" -- CORRECT?
         BO    IF280                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF268    TM    #OPTOPT,X'10'        LRECLERR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF280    CLI   #NULL,X'01'          "NULL" OR "NONULL"?
         BL    IF300                NEITHER, BRANCH
         BH    IF282                "NONULL", BRANCH
         TM    #OPTOPT,X'08'        "NULL" -- CORRECT?
         BO    IF300                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF282    TM    #OPTOPT,X'08'        NULL?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF300    TM    #IFABOVE,X'01'         SIZE AVAILABLE?
         BNO   *+8                    NO, BRANCH
         L     R6,#IFLEN              YES, USE THAT LENGTH
         SPACE 1
         TM    #IFABOVE,X'80'         ABOVE CHECK?
         BNO   IF320                  NO, BRANCH
         CLM   R6,B'0111',#IFABOVE+1  ABOVE THE MINIMUM VALUE?
         BNH   IF997                  NO, DO ANY ELSE
         SPACE 1
IF320    TM    #IFBELOW,X'80'         BELOW CHECK?
         BNO   IF360                  NO, BRANCH
         CLM   R6,B'0111',#IFBELOW+1  BELOW THE MAXIMUM VALUE?
         BNL   IF997                  NO, DO ANY ELSE
         SPACE 3
IF360    CLI   #NAMEERR,X'01'       "NAMEERR" OR "NONAMEERR"?
         BL    IF390                NO, BRANCH
         SPACE 1
         SR    R3,R3                RESULT REGISTER
         LA    R1,7
         LA    R14,DOUBLE+8
         MVC   DOUBLE(8),DIRNAME
         SPACE 1
IF382    BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,IF382                                  NON-BLANK
         SPACE 1
         CLI   DIRNAME,C'0'        NUMERIC FIRST CHARACTER?
         BL    *+8                 NO, BRANCH
         LA    R3,1                YES, NON-STANDARD NAME
         SPACE 1
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              STANDARD MEMBER NAME?
         BZ    *+8                 YES, BRANCH
         LA    R3,1                NO, NON-STANDARD NAME
         SPACE 1
         CLI   #NAMEERR,X'01'       "NAMEERR" OR "NONAMEERR"?
         BL    IF390                NEITHER, BRANCH
         BH    IF384                "NONAMEERR", BRANCH
         LTR   R3,R3                "NAMEERR" -- CORRECT?
         BP    IF390                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF384    LTR   R3,R3                NAMEERR?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
* CHECK EACH MODULE FOR LINKAGE EDIT ATTRIBUTE CONFLICTS IF REQUIRED
IF390    CLI   #LKEDERR,X'01'              CONFLICT CHECK REQUESTED?
         BL    IF400                       NO, BRANCH
         SPACE 1
         TM    DIRATTR,ATTRRENT            REENTRANT?
         BNO   IF392                       NO, BRANCH
         TM    DIRATTR,ATTRREUS            REUSABLE?
         BNO   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF392    TM    DIRATTR+1,ATTRSYMS+ATTRNE   TEST AND NOT EDIT
         BO    IF399                       YES, CONFLICT FOUND
         SPACE 1
         TM    DIRATTR,ATTRREUS+ATTRSCTR   REUSABLE AND SCATTER
         BO    IF399                       YES, CONFLICT FOUND
         SPACE 1
         TM    DIRATTR,ATTROVLY            OVERLAY ATTRIBUTE?
         BNO   IF394                       NO, BRANCH
         TM    DIRATTR,ATTRRENT++ATTRREUS+ATTRSCTR  RENT/REUS/SCTR?
         BNZ   IF399                                YES, CONFLICT
         TM    DIRATTR+1,ATTRREFR          REFRESHABLE?
         BO    IF399                       YES, CONFLICT FOUND
         TM    DIRATTR2,DIRAOSLE           MVS LINKAGE-EDITOR?
         BNO   IF394                       NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BO    IF399                       YES, CONFLICT FOUND
         TM    MODESAVE,DIRAM31            AMODE 24?
         BNZ   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF394    TM    DIRATTR2,DIRAOSLE           MVS LINKAGE-EDITOR?
         BNO   IF397                       NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   IF397                       NO, BRANCH
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BO    IF399                       YES, CONFLICT FOUND
         TM    MODESAVE,DIRAM31            AMODE 31?
         BNO   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF397    CLI   #LKEDERR,1                  CONFLICT DESIRED?
         BE    IF997                       YES, DO ANY ELSE
         B     IF400                       NO, NONE FOUND
         SPACE 1
IF399    CLI   #LKEDERR,1                  CONFLICT DESIRED?
         BNE   IF997                       NO, DO ANY ELSE
         SPACE 3
* LOAD EACH MODULE IF REQUIRED
IF400    CLI   #LOADERR,X'01'      LOAD CHECK REQUESTED?
         BL    IF420               NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,$Q         QUIET MODE LOAD IS REQUIRED
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,IF402            RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,MSGTEXT2      SAVE REGISTERS
         MVI   RECOVER,C'L'        LOAD RECOVERY FLAG
         LOAD   EPLOC=DIRNAME,DCB=INDCB
         DELETE EPLOC=DIRNAME
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         MVI   RECOVER,0           LOAD WORKED
         B     IF404
         SPACE 1
IF402    LM    R2,R8,MSGTEXT2      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         MVI   RECOVER,1           LOAD DID NOT WORK
         SPACE 1
IF404    NI    ##ADRPA#,FF-$Q       END OF QUIET MODE LOAD
         CLI   #LOADERR,X'01'       "LOADERR" OR "NOLOADERR"?
         BL    IF420                NEITHER, BRANCH
         BH    IF406                "NOLOADERR", BRANCH
         CLI   RECOVER,1            "LOADERR" -- CORRECT?
         BE    IF420                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF406    CLI   RECOVER,1            LOAD ERROR?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
* CHECK IF THIS MEMBER IS IN USE BY AN SPF EDIT SESSION
IF420    CLI   #SPFEDIT,X'01'      INUSE CHECK REQUESTED?
         BL    IF440               NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,$Q         QUIET MODE SPF USE CHECK
         SR    R3,R3               ASSUME NOT IN USE
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAS   R2,ENQMTEST         MEMBER IN USE?
         LA    R3,1                YES, MARK AS SUCH
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE
         SPACE 1
         CLI   #SPFEDIT,X'01'       "SPFEDIT" OR "NOSPFEDIT"?
         BL    IF440                NEITHER, BRANCH
         BH    IF422                "NOSPFEDIT", BRANCH
         LTR   R3,R3                "SPFEDIT" -- CORRECT?
         BP    IF440                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF422    LTR   R3,R3                SPFEDIT?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF440    CLI   #AALIAS,X'01'        APPARENT ALIAS MEMBER CHECK?
         BL    IF450                NO, BRANCH
         SR    R3,R3                ASSUME NOT AN APPARENT ALIAS
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BO    IF444                YES, CANNOT BE AN APPARENT ALIAS
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         SPACE 1
IF442    BAS   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF444                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BO    IF442                YES, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF442                NO, BRANCH
         CLC   DIRNAME,MEMNAME      SAME MEMBER?
         BE    IF442                YES, BRANCH
         LA    R3,1                 AN APPARENT ALIAS MEMBER
         SPACE 1
IF444    CLI   #AALIAS,X'01'        "APPARENTALIAS" OR "NOAPPAREN.."?
         BL    IF450                NEITHER, BRANCH
         BH    IF446                "NOAPPARENTALIAS", BRANCH
         LTR   R3,R3                "APPARENTALIAS" -- CORRECT?
         BP    IF450                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF446    LTR   R3,R3                APPARENT ALIAS?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF450    CLI   #HASALIA,X'01'       HAS ALIAS MEMBER CHECK?
         BL    IF460                NO, BRANCH
         SR    R3,R3                ASSUME IT HAS NO ALIASES
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BO    IF454                YES, CANNOT HAVE ALIASES
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         SPACE 1
IF452    BAS   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF454                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BNO   IF452                NO, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF452                NO, BRANCH
         LA    R3,1                 YES, HAS AN ALIAS
         SPACE 1
IF454    CLI   #HASALIA,X'01'       "HASALIAS" OR "NOHASALIAS"?
         BL    IF460                NEITHER, BRANCH
         BH    IF456                "NOHASALIAS", BRANCH
         LTR   R3,R3                "HASALIAS" -- CORRECT?
         BP    IF460                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF456    LTR   R3,R3                HAS ALIAS?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF460    CLI   #ORPHAN,X'01'        ORPHAN MEMBER CHECK?
         BL    IF470                NO, BRANCH
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BNO   IF463                NO, CANNOT BE AN ORPHAN
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         LA    R3,1                 ASSUME IT IS AN ORPHAN
         SPACE 1
IF462    BAS   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF464                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BO    IF462                YES, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF462                NO, BRANCH
IF463    SR    R3,R3                NOT AN ORPHAN
         SPACE 1
IF464    CLI   #ORPHAN,X'01'        "ORPHAN" OR "NOORPHAN"?
         BL    IF470                NEITHER, BRANCH
         BH    IF466                "NOORPHAN", BRANCH
         LTR   R3,R3                "ORPHAN" -- CORRECT?
         BP    IF470                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF466    LTR   R3,R3                ORPHAN?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF470    CLI   #RLD0,X'01'          "RLDZERO" OR "NORLDZERO"
         BL    IF476                NEITHER, BRANCH
         BH    IF472                "NORLDZERO", BRANCH
         CLI   DIRATTR4,0           "RLDZERO" -- CORRECT?
         BE    IF476                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF472    CLI   DIRATTR4,0           "NORLDZERO" -- CORRECT?
         BE    IF997                YES, DO ANY ELSE
         SPACE 1
IF476    TM    FLAGSCC,RECFMU            LOAD MODULE?
         BO    IF490                     YES, BRANCH
         CLI   #IFCREIF+2,0              ANY CREATION DATE?
         BE    IF480                     NO, CHECK FOR MODIFICATION
         TM    DIRFLAG,X'0F'             SPF STATISTICS?
         BNO   IF997                     NO, DO ANY ELSE
         MVC   DOUBLE+1(3),#IFCREIF      YYDDDF WORK AREA     Y2K AUG97
         MVI   DOUBLE,X'00'              ASSUME 19XX          Y2K AUG97
         CLI   #IFCREIF,X'&WIN19XX'      BELOW 19XX WINDOW?   Y2K AUG97
         BNL   IF476A                    NO,  THEN 19XX       Y2K AUG97
         MVI   DOUBLE,X'01'              YES, THEN 20XX       Y2K AUG97
IF476A   DS    0H                                             Y2K AUG97
         CLC   DOUBLE+1(3),=X'00000F'    BEFORE/TO 1ST DATE?  Y2K AUG97
         BE    *+14                      YES, IGNORE 1ST DATE Y2K AUG97
         CLC   DOUBLE(4),DIRSPFCR+0      > DIRECTORY DATE?    Y2K AUG97
         BH    IF997                     YES, DO ANY ELSE
         MVC   DOUBLE+5(3),#IFCREIF+3    YYDDDF WORK AREA     Y2K AUG97
         MVI   DOUBLE+4,X'00'            ASSUME 19XX          Y2K AUG97
         CLI   #IFCREIF+3,X'&WIN19XX'    BELOW 19XX WINDOW?   Y2K AUG97
         BNL   IF476B                    NO,  THEN 19XX       Y2K AUG97
         MVI   DOUBLE+4,X'01'            YES, THEN 20XX       Y2K AUG97
IF476B   DS    0H                                             Y2K AUG97
         CLC   DOUBLE+4(4),DIRSPFCR+0    < DIRECTORY DATE?    Y2K AUG97
         BL    IF997                     YES, DO ANY ELSE
         SPACE 1
IF480    CLI   #IFMODIF+2,0              ANY MODIFICATION DATE?
         BE    IF490                     NO, CONTINUE
         TM    DIRFLAG,X'0F'             SPF STATISTICS?
         BNO   IF997                     NO, DO ANY ELSE
         MVC   DOUBLE+1(3),#IFMODIF      YYDDDF WORK AREA     Y2K AUG97
         MVI   DOUBLE,X'00'              ASSUME 19XX          Y2K AUG97
         CLI   #IFMODIF,X'&WIN19XX'      BELOW 19XX WINDOW?   Y2K AUG97
         BNL   IF480A                    NO,  THEN 19XX       Y2K AUG97
         MVI   DOUBLE,X'01'              YES, THEN 20XX       Y2K AUG97
IF480A   DS    0H                                             Y2K AUG97
         CLC   DOUBLE+1(3),=X'00000F'    BEFORE/TO 1ST DATE?  Y2K AUG97
         BE    *+14                      YES, IGNORE 1ST DATE Y2K AUG97
         CLC   DOUBLE(4),DIRSPFCD+0      > DIRECTORY DATE?    Y2K AUG97
         BH    IF997                     YES, DO ANY ELSE
         MVC   DOUBLE+5(3),#IFMODIF+3    YYDDDF WORK AREA     Y2K AUG97
         MVI   DOUBLE+4,X'00'            ASSUME 19XX          Y2K AUG97
         CLI   #IFMODIF+3,X'&WIN19XX'    BELOW 19XX WINDOW?   Y2K AUG97
         BNL   IF480B                    NO,  THEN 19XX       Y2K AUG97
         MVI   DOUBLE+4,X'01'            YES, THEN 20XX       Y2K AUG97
IF480B   DS    0H                                             Y2K AUG97
         CLC   DOUBLE+4(4),DIRSPFCD+0    < DIRECTORY DATE?    Y2K AUG97
         BL    IF997                     YES, DO ANY ELSE
         SPACE 3
IF490    DS    0H
         SPACE 1
IF996    MVC   DELAYMSG+25(4),=C'THEN'
         L     R2,#ACTIONT          THEN ACTION
         B     IF998
         SPACE 1
IF997    MVC   DELAYMSG+25(4),=C'ELSE'
         L     R2,#ACTIONE          ELSE ACTION
         SPACE 1
IF998    LTR   R2,R2                ANY ASSOCIATED ACTION?
         BNP   NEWCMD               NO, BRANCH
         TM    ##ADRPA#-##SUBCOM(R2),$D  SUPRESS DEFAULT MESSAGE?
         BO    IF999                     YES, BRANCH
         SPACE 1
         M$ERRST MSGBLANK
         TM    FLAGSHH,FDELAYM      ANY HEADER MESSAGE?
         BNO   IF999                NO, BRANCH
         XI    FLAGSHH,FDELAYM      NO MORE MESSAGE
         MVC   DELAYMSG+30(8),0(R2) SUBCOMMAND NAME
         M$ERRST DELAYMSG           OUTPUT THE HEADER MESSAGE
         SPACE 1
IF999    MVC   ##ANSWER(LISUBS),ISUBS INITIALIZE THE PDL SAVE AREA
         MVC   ##SUBCOM(PTW),0(R2)    CHANGE THE SUBCOMMAND
         OI    FLAGSGG,FOUTSOME       A MATCH WAS MADE
         B     CALLCMDZ               CALL THE SECONDARY SUBCOMMAND
IFLA$ATT DC    V($ATT)                DEFAULT THEN ACTION
