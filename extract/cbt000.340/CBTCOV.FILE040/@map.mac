         TITLE 'P D S  --  PDS MAP                           09/28/88'
***********************************************************************
***      MAP SUBCOMMAND                                             ***
***********************************************************************
*
         SPACE 1
MAP      CSECT
         USING *,R8
         LA    R1,L530          ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO   CORRECT?
         BZ    MSGNEW           YES, BRANCH
         LA    R1,L701          ASSUME NOT A LOAD LIBRARY
         TM    FLAGSCC,RECFMU   CORRECT?
         BZ    MSGNEW           YES, BRANCH
         OI    FLAGSGG,FOUTSUB  SUBCOMMAND GAINED CONTROL
         MVI   ENTRYPT,C'?'     NO ENTRY POINT YET
         SPACE 1
         L     R15,=A(READESD)  FORMAT THE ESD DATA
         BASR  R14,R15          ANY ESD DATA?
         B     *+8              NO, BRANCH
         B     MAP04            YES, BRANCH
         SPACE 1
MAPNOESD TM    FLAGSHH,FDELAYM  DELAYED MESSAGE AVAILABLE?
         BNO   MAP02            NO, BRANCH
         TM    PMEMMIN,X'80'    MEMBER LIST?
         BO    *+12             YES, BRANCH
         TM    FLAGSAA,FMEM#MEM MEMBER GROUP?
         BNO   MAP02            NO, BRANCH
         CLI   #MODLEN,0        ANY FILTERING?
         BH    NEWCMD           NO, BRANCH
         M$ERRST MSGBLANK
         M$ERRST DELAYMSG
         XI    FLAGSHH,FDELAYM  MESSAGE WAS WRITTEN
         SPACE 1
MAP02    LA    R1,MSGTEXT1               MESSAGE IDENTIFIER
         M$MSG (R1)
         B     MAPNOMSG
         SPACE 1
MAP04    LA    R4,#ESDPTR       ESD TABLE CHAIN POINTER
         OI    FLAGSGG,FOUTSOME SOME OUTPUT WAS GENERATED
         USING ESDENTRY,R4      BASE FOR TABLE
         TM    FLAGSHH,FDELAYM  ANY DELAYED MESSAGE AVAILABLE?
         BNO   MAPESD           NO, BRANCH
         M$ERRST MSGBLANK
         M$ERRST DELAYMSG
         XI    FLAGSHH,FDELAYM  MESSAGE WAS WRITTEN
         SPACE 2
MAPESD   MVC   MSGTEXT1(136),MSGBL132
         ICM   R4,B'1111',ESDLINK    END OF CHAIN?
         BZ    MAPLAST               YES, BRANCH
         SPACE 1
         CLI   #MAPOPT,X'04'         RELINK FORMAT?
         BE    MAP600                YES, BRANCH
         CLI   ESDTYPE,CODELR        EXTERNAL REFERENCE?
         BE    MAPLR                 YES, BRANCH
         CLI   ESDTYPE,CODEER        $UNRESOLVED EXTERNAL REFERENCE?
         BE    MAPER                 YES, BRANCH
         CLI   ESDTYPE,CODEWK        $UNRESOLVED WEAK EXTERNAL REF?
         BE    MAPWK                 YES, BRANCH
         SPACE 1
         CLC   ESDADDR(3),DIREPA     ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,ESDNAME       SAVE THE ENTRY POINT NAME
         MVC   MSGTEXT1+4(8),ESDNAME
         UNPK  MSGTEXT1+14(7),ESDADDR(4)
         TR    MSGTEXT1+14(6),TRTABLE
         MVI   MSGTEXT1+20,X'40'     CLEAR GARBAGE BYTE
         UNPK  MSGTEXT1+22(7),ESDLEN(4)
         TR    MSGTEXT1+22(6),TRTABLE
         MVI   MSGTEXT1+28,X'40'     CLEAR GARBAGE BYTE
         SPACE 1
         TM    DIRATTR,ATTROVLY      OVERLAY PROGRAM?
         BZ    MAPNOVLY              NO, BRANCH
         SR    R0,R0
         IC    R0,ESDSEG#
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+30(2),DOUBLE+6(2)
         B     MAPPRINT
         SPACE 1
MAPNOVLY CLI   ESDXAFLG,AMODE24          RMODE 24 AND AMODE 24?
         BNH   MAPPRINT                  NO, BRANCH
         MVC   MSGTEXT1+30(5),=C'RMODE'
         MVC   MSGTEXT1+36(3),=C'ANY'
         TM    ESDXAFLG,RMODEANY         RMODE ANY?
         BO    *+10                      YES, BRANCH
         MVC   MSGTEXT1+36(3),=C'24 '    RMODE 24
         MVC   MSGTEXT1+40(5),MAPLAMOD
         MVC   MSGTEXT1+46(3),=C'24 '
         TM    ESDXAFLG,AMODE31          AMODE 31?
         BNO   *+10                      NO, BRANCH
         MVC   MSGTEXT1+46(3),=C'31 '    AMODE 31
         TM    ESDXAFLG,AMODE24+AMODE31  AMODE ANY?
         BNO   *+10                      NO, BRANCH
         MVC   MSGTEXT1+46(3),=C'ANY'    AMODE ANY
         B     MAPPRINT
         SPACE 2
MAPWK    MVC   INSERT#1(8),ESDNAME
         M$MSG L441$1
         B     MAPESD
         SPACE 2
MAPER    MVC   INSERT#1(8),ESDNAME
         M$MSG L442$1
         B     MAPESD
         SPACE 1
MAPLR    CLC   ESDADDR(3),DIREPA     ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,ESDNAME       SAVE THE ENTRY POINT NAME
         MVC   MSGTEXT1+56(8),ESDNAME
         UNPK  MSGTEXT1+66(7),ESDADDR(4)
         TR    MSGTEXT1+66(6),TRTABLE
         MVI   MSGTEXT1+66+6,X'40'   CLEAR GARBAGE BYTE
         SPACE 1
MAPPRINT M$ERRST MSGTEXT1
         B     MAPESD
         SPACE 1
MAPLAST  M$ERRST MSGBLANK
MAPNOMSG UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         M$MSG (R1)
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         M$MSG L104$2
         SPACE 1
         TM    DIRFLAG,X'80'      IS MODULE AN ALIAS?
         BZ    NEWCMD             NO, NO ALIAS INFORMATION
         CLI   #MAPOPT,X'01'      SHORT OR ENTRY POINT MAP?
         BH    NEWCMD             YES, DONE
         TM    FLAGSGG,FALINCON   NO ALIAS INFORMATION DEFAULT?
         BO    NEWCMD             YES, DONE
         EJECT
*        FIND THE CORRESPONDING MAIN MODULE
         MVI   STARTTR+2,X'01'    TTR=000001 (START OF DIRECTORY)
         XC    INSERT#1(8),INSERT#1  NO MAIN(S) FOUND
         SPACE 1
MAPAREAL BAS   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     MAPNREAL           LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    MAPAREAL           YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   MAPAREAL           NO, BRANCH
         SPACE 1
         MVC   INSERT#1(8),MEMNAME
         M$MSG L066$1
         B     MAPAREAL
         SPACE 2
MAPNREAL OC    INSERT#1(8),INSERT#1  ANY MAIN(S) FOUND?
         BNZ   NEWCMD                YES, QUIT
         M$MSG L860
         TM    FLAGSCC,RECFMU     LOAD MODULE?
         BNO   NEWCMD             NO, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         LA    R1,L861$1
         B     MSGNEW
         SPACE 1
* GENERATE JCL AND CONTROL CARDS TO LINKAGE EDIT A MODULE
MAP600   TM    DIRFLAG,X'80'                     ALIAS?
         BNO   MAP604                            NO, BRANCH
         MVI   STARTTR+2,X'01'                   READ THE DIRECTORY
         SPACE 1
MAP602   BAS   R14,READDIR                       GET NEXT MEMBER ENTRY
         B     MAP604                            END OF MEMBERS, BRANCH
         SPACE 1
         CLC   DIRTTR(3),MEMTTR                  THIS TTR?
         BNE   MAP602                            NO, LOOP
         TM    MEMTTR+3,X'80'                    ALIAS?
         BO    MAP602                            YES, LOOP
         L     R14,DIRPTRS                       START OF ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)    REPLACE THE ENTRY
         SPACE 2
MAP604   MVC   MSGTEXT1+4(L'MAPLLKED),MAPLLKED   //LKED  EXEC PGM=IEWL,
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
         MVC   MSGTEXT1+4(L'MAPLPARM),MAPLPARM   //      PARM='NCAL, ..
         LA    R1,MSGTEXT1+4+L'MAPLPARM
         SPACE 1
         TM    DIRATTR,ATTRRENT                  REENTRANT?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+00               YES, ADD ,RENT
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRREUS                  REUSABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+05               YES, ADD ,REUS
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR+1,ATTRREFR                REFRESHABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+10               YES, ADD ,REFR
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTROVLY                  OVERLAY?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+15               YES, ADD ,OVLY
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRTEST                  TEST SYMBOLS?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+20               YES, ADD ,TEST
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRSCTR                  SCATTER LOADED?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+25               YES, ADD ,SCTR
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRLOAD                  ONLY LOADABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(3,R1),MAPLATTR+30               YES, ADD ,OL
         LA    R1,3(,R1)
         SPACE 1
         TM    DIRATTR+1,ATTRNE                  NOT EDITABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(3,R1),MAPLATTR+33               YES, ADD ,NE
         LA    R1,3(,R1)
         SPACE 1
         MVI   0(R1),C''''                       ADD FINAL QUOTE
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSUT1),MAPLSUT1   //SYSUT1
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSPRT),MAPLSPRT   //SYSPRINT
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSLIB),MAPLSLIB   //SYSLIB
         MVC   MSGTEXT1+4+L'MAPLSLIB(44),DSNAME  ADD DSNAME
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSMOD),MAPLSMOD   //SYSLMOD
         MVC   MSGTEXT1+4+L'MAPLSMOD(44),DSNAME  ADD DSNAME
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSLIN),MAPLSLIN   //SYSLIN
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLINCL),MAPLINCL   ADD "INCLUDE SYSLIB("
         MVC   MSGTEXT1+4+L'MAPLINCL(8),DIRNAME  ADD MEMBER NAME
         LA    R1,MSGTEXT1+4+L'MAPLINCL+8
         CLI   0(R1),X'40'
         BNE   *+8
         BCT   R1,*-8
         MVI   1(R1),C')'
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         TM    DIRATTR,ATTROVLY                  OVERLAY MODULE?
         BO    MAP700                            YES, BRANCH
         SPACE 3
         LA    R4,#ESDPTR
         SPACE 1
MAP610   LA    R1,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R0,MSGTEXT1+4+60                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(5),MAPLORD           ADD " ORDER"
         SPACE 1
MAP620   ICM   R4,B'1111',ESDLINK                NEXT ESD ELEMENT?
         BZ    MAP670                            NO, BRANCH
         CLI   ESDTYPE,CODESD                    CSECT?
         BE    MAP630                            YES, BRANCH
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP630                            YES, BRANCH
         CLC   ESDNAME(8),MAPL$BLK               BLANK COMMON?
         BE    MAP620                            YES, BRANCH
         CLI   ESDTYPE,CODECM                    COMMON?
         BNE   MAP620                            NO, BRANCH
         B     MAP640                            NO, BRANCH
         SPACE 1
MAP630   CLC   ESDADDR(3),DIREPA                 ENTRY POINT?
         BNE   *+10                              NO, BRANCH
         MVC   ENTRYPT,ESDNAME                   YES, SAVE THE NAME
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP620                            YES, BRANCH
         SPACE 1
MAP640   MVC   1(8,R1),ESDNAME                   ADD THE ESDNAME
         LA    R14,8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         TM    DIRATTR2,DIR2PAGA                 PAGE ALIGN?  ABL NOV86
         BNO   MAP660                            NO, BRANCH   ABL NOV86
         TM    ESDADDR+1,X'0F'                   POSSIBLE 4K MULTIPLE?
         BNZ   MAP660                            NO, BRANCH
         TM    ESDADDR+2,X'FF'                   4K MULTIPLE?
         BNZ   MAP660                            NO, BRANCH
         MVC   1(3,R14),MAPLPAG                  YES, ADD "(P)"
         LA    R14,3(,R14)
         SPACE 1
MAP660   MVI   1(R14),C','                       ADD A COMMA
         LA    R1,1(,R14)
         CR    R1,R0                             END OF CARD?
         BNH   MAP620                            NO, BRANCH
         SPACE 2
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP610
         SPACE 2
MAP670   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP800                            NO, BRANCH
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP800
         SPACE 2
MAP700   MVC   STARTTR(3),DIRSTART               ADDRESS OF NOTELIST
         L     R15,=V(EXCP)
         BASR  R14,R15
         LA    R1,L751                           NOTELIST NOT FOUND
         LTR   R15,R15                           ANY ERROR?
         BP    MSGNEW                            YES, BRANCH
         SPACE 1
         LR    R2,R0                             START OF RECORD
         LR    R5,R0                             START OF RECORD
         LA    R14,24(,R2)                       FIRST SEGMENT
         LA    R0,4                              INCREMENT
         LR    R1,R2
         A     R1,LS                             LAST SEGMENT
         SR    R1,R0                             FOR BXLE
         SR    R15,R15
         SPACE 1
MAP710   LA    R15,1(,R15)                       NEXT SEGMENT #
         STC   R15,3(,R14)                       STORE IN SEGTAB
         BXLE  R14,R0,MAP710                     DO ALL ENTRIES
         SPACE 2
         MVI   0(R14),X'FF'                      MARK END OF TABLE
         MVC   FULLWORD,=F'1'                    REGION = 1
         LA    R2,24(,R5)                        BEGIN SEGMENT 1
         LA    R6,8(,R5)                         BEGIN REGION 1
         SPACE 1
MAP720   SR    R1,R1
         IC    R1,0(,R6)                         # OF LAST SEGMENT
         LTR   R1,R1                             END OF STRUCTURE?
         BZ    MAP800                            YES, BRANCH
         SLL   R1,2                              SEGMENT * 4
         LA    R3,20(R1,R5)                      LAST SEGMENT
         SPACE 2
MAP724   LA    R4,#ESDPTR                        START OF ESD ELEMENTS
         SPACE 1
MAP730   LA    R1,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R0,MSGTEXT1+4+62                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(6),MAPLINS           ADD " INSERT"
         SPACE 1
MAP732   ICM   R4,B'1111',ESDLINK                NEXT ESD ELEMENT?
         BZ    MAP742                            NO, BRANCH
         CLC   ESDSEG#(1),3(R2)                  THIS SEGMENT?
         BNE   MAP732                            NO, BRANCH
         CLI   ESDTYPE,CODESD                    CSECT?
         BE    MAP734                            YES, BRANCH
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP734                            YES, BRANCH
         CLC   ESDNAME(8),MAPL$BLK               BLANK COMMON?
         BE    MAP732                            YES, BRANCH
         CLI   ESDTYPE,CODECM                    COMMON?
         BNE   MAP732                            NO, BRANCH
         B     MAP740
         SPACE 1
MAP734   CLC   ESDADDR(3),DIREPA                 ENTRY POINT?
         BNE   *+10                              NO, BRANCH
         MVC   ENTRYPT,ESDNAME                   YES, SAVE THE NAME
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP732                            YES, BRANCH
         SPACE 1
MAP740   MVC   1(8,R1),ESDNAME                   ADD THE ESDNAME
         LA    R14,8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C','                       ADD A COMMA
         LA    R1,1(,R14)
         CR    R1,R0                             END OF CARD?
         BNH   MAP732                            NO, BRANCH
         SPACE 2
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP730
         SPACE 1
MAP742   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP750                            NO, BRANCH
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP750   CLI   4(R2),X'FF'                       END OF SEGTAB?
         BE    MAP800                            YES, BRANCH
         CR    R2,R3                             END OF REGION?
         BE    MAP764                            YES, BRANCH
         SPACE 1
         MVC   MSGTEXT1+4+1(7),MAPLOVER          ADD " OVERLAY"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD " REGION"
         MVC   MSGTEXT1+4+1+7+1+6(1),FULLWORD+3  ADD REGION NUMBER
         OI    MSGTEXT1+4+1+7+1+6,X'F0'          MAKE PRINTABLE
         SPACE 1
         SR    R1,R1
         ICM   R1,B'0001',4(R2)                  ANY PREVIOUS SEGMENT?
         BZ    MAP760                            NO, BRANCH
         MVC   MSGTEXT1+4+1+7+1(6),MAPLOVLY      ADD " OVLY"
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+4+1+7+1+4(3),DOUBLE+6(2) ADD SEGMENT NUMBER
MAP760   M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
MAP764   LA    R0,4
         LR    R1,R3
         BXLE  R2,R0,MAP724                      DO EACH SEGMENT
         SPACE 2
         LA    R6,2(,R6)
         CLI   0(R6),X'00'                       ANY SEGMENTS IN RGN?
         BE    MAP800                            NO, BRANCH
         L     R1,FULLWORD
         LA    R0,4
         CR    R1,R0                             FOURTH REGION DONE?
         BNL   MAP800                            YES, BRANCH
         LA    R1,1(,R1)                         NEXT REGION
         ST    R1,FULLWORD
         MVC   MSGTEXT1+4+1(7),MAPLOVER          ADD " OVERLAY"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD "REGION"
         STC   R1,MSGTEXT1+4+1+7+1+6             ADD REGION NUMBER
         OI    MSGTEXT1+4+1+7+1+6,X'F0'          MAKE PRINTABLE
         MVC   MSGTEXT1+4+1+7+1+6+1(8),MAPLREG   ADD "(REGION)"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD "REGION"
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP720
         SPACE 2
MAP800   LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE 1
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    MAP802                  YES, BRANCH
***PDSMAN CHANGES: MAP JCL (APF AND SSI)                      ABL NOV86
         TM    5(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    MAP803                  YES, BRANCH            ABL NOV86
         TM    6(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    MAP803                  YES, BRANCH            ABL NOV86
         TM    4+5(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    MAP803                  YES, BRANCH            ABL NOV86
         TM    4+6(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    MAP803                  YES, BRANCH            ABL NOV86
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
*        BE    *+8                     NO, BRANCH
         B     *+8                     BRANCH                 DRK JAN98
MAP802   LA    R2,11(,R2)              ADD ALIAS LENGTH
MAP803   DS    0H                                             ABL NOV86
         SPACE 1
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   MAP804                  NO, BRANCH
         SPACE 1
         TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   MAP820                  NO, SKIP SSI PROCESSING
         SPACE 1
MAP804   LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    MAP820                  YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    MAP820                  YES, NO SSI
         SPACE 1
         MVC   MSGTEXT1+5(6),MAPLSET   ADD " SETSSI"
         UNPK  MSGTEXT1+5+6+2(9),0(5,R2)
         TR    MSGTEXT1+5+6+2(8),TRTABLE
         MVI   MSGTEXT1+5+6+2+8,X'40'
         LA    R2,4(,R2)               ADD SSI SIZE
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP820   TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    MAP860                      NO, CANNOT BE AUTHORIZED
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRAM31   UNUSUAL RMODE OR AMODE?
         BZ    MAP840                      NO, BRANCH
         MVC   DOUBLE(8),BLANKS            RMODE/AMODE FILLER
         MVC   DOUBLE(3),=C'24 '           MOVE IN THE RMODE TEXT
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    *+10                        NO, BRANCH
         MVC   DOUBLE(3),=C'ANY'           MOVE IN THE RMODE TEXT
         MVC   DOUBLE+4(3),=C'24 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM31            AMODE 31?
         BZ    *+10                        NO, BRANCH
         MVC   DOUBLE+4(3),=C'31 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BNO   *+10                        NO, BRANCH
         MVC   DOUBLE+4(3),=C'ANY'         MOVE IN THE AMODE TEXT
         MVC   MSGTEXT1+4(L'MAPLMODE),MAPLMODE
         LA    R1,MSGTEXT1+4+L'MAPLMODE
         MVC   0(3,R1),DOUBLE              ADD 24/ANY
         CLI   2(R1),X'40'                 ANY DATA?
         BE    *+8                         NO, BRANCH
         LA    R1,1(,R1)                   YES, ADD 1
         MVC   2(L'MAPLAMO,R1),MAPLAMO     ADD "),AMODE("
         LA    R1,2+L'MAPLAMO(,R1)
         MVC   0(3,R1),DOUBLE+4            ADD 24/31 OR ANY
         CLI   2(R1),X'40'                 ANY DATA?
         BE    *+8                         NO, BRANCH
         LA    R1,1(,R1)                   YES, ADD 1
         MVI   2(R1),C')'                  ADD FINAL PARENTHESIS
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP840   TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BNO   MAP860                  NO, BRANCH
         SPACE 1
         LA    R1,L721                 ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   MAP860                  NO, BRANCH
         SPACE 3
         LA    R1,L021                 ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BL    MAP860                  NO, BRANCH
         MVC   MSGTEXT1+4(L'MAPLAUTH),MAPLAUTH
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP860   MVC   MSGTEXT1+4(L'MAPLENTR),MAPLENTR
         CLI   ENTRYPT,C'?'           ANY ENTRY POINT?
         BE    *+10                   NO, BRANCH
         MVC   MSGTEXT1+4+L'MAPLENTR-8(8),ENTRYPT
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
         MVC   ENTRYPT(8),DIRNAME      DEFAULT MODULE NAME
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 2
MAP880   LA    R2,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R3,MSGTEXT1+4+62                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(5),MAPLALIA          ADD " ALIAS"
         SPACE 1
MAP890   BAS   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     MAP910             LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   MAP890             NO, BRANCH
         TM    MEMFLAG,X'80'      ALIAS ENTRY FOUND?
         BO    MAP900             YES, BRANCH
         TM    DIRFLAG,X'80'      ALIAS ORIGINALLY?
         BNO   MAP890             NO, BRANCH
         MVC   ENTRYPT(8),MEMNAME SAVE REAL MEMBER NAME
         B     MAP890
         SPACE 1
MAP900   MVC   1(8,R2),MEMNAME                   ADD THE ALIAS NAME
         LA    R14,8(,R2)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C','                       ADD A COMMA
         LA    R2,1(,R14)
         CR    R2,R3                             END OF CARD?
         BNH   MAP890                            NO, BRANCH
         SPACE 2
         MVI   0(R2),X'40'                       CLEAR THE LAST COMMA
         M$ERRST MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP880
         SPACE 2
MAP910   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP920                            NO, BRANCH
         MVI   0(R2),X'40'                       CLEAR THE LAST COMMA
         M$ERRST MSGTEXT1
         SPACE 1
MAP920   MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+5(L'MAPLNAME),MAPLNAME
         MVC   MSGTEXT1+5+8(8),ENTRYPT
         LA    R1,MSGTEXT1+5+8+8
         SPACE 1
         CLI   0(R1),X'40'                       BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R1,*-8
         SPACE 1
         MVC   1(3,R1),MAPLREP
         SPACE 1
         M$ERRST MSGTEXT1
         B     NEWCMD
MAPLLKED DC    C'//LKED   EXEC  PGM=IEWL,'
MAPLPARM DC    C'//       PARM=''NCAL,MAP,LIST,LET'
MAPLATTR DC    C',RENT,REUS,REFR,OVLY,TEST,SCTR,OL,NE'
*MAPLSUT1 DC    C'//SYSUT1   DD  UNIT=SYSDA,SPACE=(1024,(200,20))'
MAPLSUT1 DC    C'//SYSUT1   DD  UNIT=&TUNIT,SPACE=(1024,(200,20))'
MAPLSPRT DC    C'//SYSPRINT DD  SYSOUT=*'
MAPLSLIB DC    C'//SYSLIB   DD  DISP=SHR,DSN='
MAPLSMOD DC    C'//SYSLMOD  DD  DISP=SHR,DSN='
MAPLSLIN DC    C'//SYSLIN   DD  *'
MAPLINCL DC    C' INCLUDE SYSLIB('
MAPLAUTH DC    C' SETCODE AC(1)'
MAPLENTR DC    C' ENTRY   ????????'
MAPLMODE DC    C' MODE    RMODE('
MAPLAMO  DC    C'),AMODE('
MAPLNAME DC    C'NAME'
MAPLSET  DC    C'SETSSI'
MAPLALIA DC    C'ALIAS'
MAPLORD  DC    C'ORDER'
MAPLPAG  DC    C'(P)'
MAPLREP  DC    C'(R)'
MAPLREG  DC    C'(REGION)'
MAPLOVER DC    C'OVERLAY'
MAPLOVLY DC    C'OVLY'
MAPLINS  DC    C'INSERT'
         DROP  R4
         SPACE 3
*
*        ESD SCAN SUBROUTINE
*
         SPACE 2
READESD  STM   R14,R12,ESDIDRSV   SAVE REGISTERS FOR RETURN
         LR    R8,R15             ENTRY POINT ADDRESS
         USING READESD,R8         SUBROUTINE BASE REGISTER
         LA    R2,#ESDPTR         ROOT OF ESD CHAIN
         MVC   STARTTR(3),DIRTTR  FIRST TTR
         SPACE 1
ESDEXCP  L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)           PROCESS RETURN CODE
         B     ESDEXCP1             00 - GOOD READ
         B     ESDLAST              04 - END OF MEMBER
         B     ESDLAST              08 - END OF DATA SET
         B     NEWCMD               12 - I/O ERROR
         SPACE 1
ESDEXCP1 LR    R3,R0              START OF RECORD
         CLI   0(R3),X'40'        TEST SYM RECORD?
         BE    ESDEXCP            YES, SKIP RECORD
         CLI   0(R3),X'20'        CESD RECORD?
         BNE   ESDLAST            NO, BRANCH
         SPACE 1
         LH    R6,4(,R3)          RELATIVE # OF 1ST ESD ID
         LH    R5,6(,R3)          LENGTH OF DATA IN BUFFER
         LA    R3,8(,R3)          START OF ESD DATA
         AR    R5,R3
         LA    R4,16              LENGTH OF ONE ENTRY
         SR    R5,R4
         SPACE 2
         USING ESDNAME,R3
ESDSCAN  IC    R0,ESDTYPE
         LA    R1,CODESEG         CHECK FOR SEGTAB/ENTAB
         TM    DIRATTR,ATTROVLY   OVERLAY MODULE?
         BO    *+8                YES, BRANCH
         NI    ESDXAFLG,RMODEANY+AMODE24+AMODE31
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CLI   #MAPOPT,X'03'      ENTRY POINT ONLY?
         BE    ESDSCAN2           YES, BRANCH
         CLC   ##SUBCOM(8),$ATTL  ATTRIBUTE ENTRY(NAME) OPERAND?
         BE    ESDSCAN2           YES, BRANCH
         CLC   ##SUBCOM(8),$ALI   ALIAS SEARCH REQUEST?
         BNE   ESDMAP             NO, BRANCH
ESDSCAN2 CLI   ESDTYPE,CODESD     VALID EXTERNAL SYMBOL?
         BE    ESDCHK             YES, BRANCH
         CLI   ESDTYPE,CODELR     ANOTHER VALID ENTRY
         BNE   NEXTESD            NO, GET THE NEXT ESD
         SPACE 1
ESDCHK   TM    DIRATTR,ATTROVLY   OVERLAY ATTRIBUTE?
         BNO   ESDCHK1            NO, BRANCH
         CLI   ESDSEG#,1          SYMBOL IN ROOT SEGMENT?
         BNE   NEXTESD            NO, IGNORE
ESDCHK1  CLC   ESDADDR(3),DIREPA  THIS ENTRY POINT ADDRESS?
         BNE   ESDCHK2            NO, BRANCH
         MVC   ENTRYPT,ESDNAME    YES, ENTRY POINT NAME IF NO ESD MATCH
         CLI   #MAPOPT,X'03'      ENTRY POINT MAP ONLY?
         BE    ESDUSE3            YES, BRANCH
ESDCHK2  CLC   ESDNAME,DIRNAME    THIS NAME?
         BNE   NEXTESD            NO, BRANCH
         CLI   #MAPOPT,X'03'      ENTRY POINT MAP ONLY?
         BE    NEXTESD            YES, BRANCH
         IC    R0,ESDXAFLG        GET MVS/XA ESD FLAGS
         SR    R1,R1
         ICM   R1,B'0111',ESDADDR GET SYMBOL OFFSET
         ST    R6,ESDIDRSV+4      RETURN ESDID IN REG 15
         STM   R0,R1,ESDIDRSV+4+4 RETURN ESD FLAGS, OFFSET IN REGS 0,1
         MVC   ENTRYPT,ESDNAME    USE THIS ESDNAME FOR THE ENTRY POINT
         B     ESDOUT             FOUND THE SYMBOL
         SPACE 1
ESDMAP   CLC   ##SUBCOM(8),$IFX   IF SUBCOMMAND?
         BNE   ESDMAP2            NO, BRANCH
         TM    #MODOPT,X'01'      WKEXTERN?
         BNO   ESDMAP0            NO, BRANCH
         CLI   ESDTYPE,CODEWK     FOUND?
         BE    ESDUSE2            YES, BRANCH
         B     NEXTESD            NO, BRANCH
         SPACE 1
ESDMAP0  TM    #MODOPT,X'02'      EXTERN?
         BNO   ESDMAP2            NO, BRANCH
         CLI   ESDTYPE,CODEER     FOUND?
         BE    ESDUSE2            YES, BRANCH
         B     NEXTESD            NO, BRANCH
         SPACE 1
ESDMAP2  CR    R0,R1              THIS SEGTAB/ENTAB ENTRY?
         BE    ESDUSE1            YES, BRANCH
         CLI   ESDTYPE,CODESD     SD ENTRY?
         BE    ESDUSE2            YES, BRANCH
         CLI   ESDTYPE,CODECM     COMMON BLOCK CODE?
         BE    ESDUSE0            YES, BRANCH
         CLC   ##SUBCOM(8),$MAP   MAP SUBCOMMAND?
         BNE   ESDMAP4            NO, BRANCH
         CLI   ESDTYPE,CODEER     $UNRESOLVED EXTERNAL REFERENCE?
         BE    ESDUSE2            YES, BRANCH
         CLI   #MAPOPT,X'02'      SHORT MAP?
         BE    ESDMAP4            YES, BRANCH
         CLI   ESDTYPE,CODEWK     $UNRESOLVED WEAK EXTERNAL REFERENCE?
         BE    ESDUSE2            YES, BRANCH
         SPACE 1
ESDMAP4  CLI   ESDTYPE,CODELR     LR ENTRY?
         BNE   ESDMAPPC           NO, BRANCH
         CLI   #MAPOPT,X'02'      SHORT MAP?
         BL    ESDUSE2            NO, BRANCH
         CLI   #MAPOPT,X'04'      RELINK?
         BE    ESDUSE2            YES, BRANCH
         CLC   ESDADDR(3),DIREPA  THIS ENTRY POINT?
         BE    ESDUSE2            NO, BRANCH
ESDMAPPC CLI   ESDTYPE,CODEPC     PC ENTRY?
         BNE   NEXTESD            NO, BRANCH
         MVC   ESDNAME,MAPL$PRI        PRIVATE CODE
         B     ESDUSE2
         SPACE 1
ESDUSE0  CLC   ESDNAME(8),BLANKS                   BLANK COMMON?
         BNE   ESDUSE2                             NO, BRANCH
         MVC   ESDNAME(8),MAPL$BLK                 YES, USE $BLKCOM
         B     ESDUSE2
         SPACE 1
ESDUSE1  STC   R0,ESDTYPE                          RESTORE OVERLAY TYPE
         MVC   ESDNAME(8),MAPL$SEG                 ASSUME SEGMENT TABLE
         OC    ESDADDR(3),ESDADDR                  CORRECT?
         BZ    ESDUSE2                             YES, BRANCH
         MVC   ESDNAME(8),MAPL$ENT                 NO, ENTRY TABLE
ESDUSE2  SR    R1,R1
         CLC   ##SUBCOM(8),$LIS                    LIST SUBCOMMAND?
         BE    ESDUSE3                             YES, BRANCH
         ICM   R1,B'0001',#MODLEN                  ANY MODULE FILTER?
         BZ    ESDUSE3                             NO, BRANCH
         BCTR  R1,0                                MACHINE LENGTH
         CLC   ESDNAME(*-*),#MODTXT                <<EXECUTED>>
         EX    R1,*-6                              THIS PARTIAL NAME?
         BNE   NEXTESD                             NO, BRANCH
ESDUSE3  BAS   R14,GETESD
         ST    R1,ESDLINK-ESDENTRY(R2)             SAVE LINK POINTER
         STH   R6,ESDID-ESDENTRY(R1)               SAVE ESD ID
         MVC   ESDNAME-ESDENTRY(LENESD1,R1),0(R3)  SAVE ESD ENTRY
         LR    R2,R1                               CHAIN TO NEXT ONE
NEXTESD  LA    R6,1(R6)
         BXLE  R3,R4,ESDSCAN
         B     ESDEXCP            GET NEXT ESD RECORD
         DROP  R3
         SPACE 2
         USING ESDENTRY,R2
ESDLAST  MVC   MSGTEXT1(3),L740
         L     R14,ESDIDRSV
         LA    R2,#ESDPTR
         OC    #ESDPTR,#ESDPTR    ANY ESD DATA AVAILABLE?
         BNZ   ESDSORT            YES, BRANCH
         CLI   #MODLEN,0          ANY ESD FILTERING?
         BH    ESDEXIT            YES, BRANCH
         MVC   MSGTEXT1(3),L703
         B     ESDEXIT
         SPACE 1
ESDSORT  L     R2,ESDLINK
         ICM   R1,B'1111',ESDLINK END OF ESD ENTRIES?
         BZ    ESDOUT             YES, BRANCH
         SPACE 1
ESDSORT2 TM    DIRATTR,ATTROVLY             OVERLAY ATTRIBUTE?
         BNO   ESDNOSEG
         CLC   ESDSEG#,ESDSEG#-ESDENTRY(R1) CHECK ENTRY SEGMENT
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE 1
ESDNOSEG CLC   ESDADDR,ESDADDR-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         CLC   ESDTYPE,ESDTYPE-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         CLC   ESDNAME,ESDNAME-ESDENTRY(R1)
         BNH   ESDSORT3
         SPACE 1
ESDSWAP  XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         XC    ESDNAME-ESDENTRY(LENESD2,R1),ESDNAME
         XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         SPACE 1
ESDSORT3 ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)
         BNZ   ESDSORT2
         B     ESDSORT
         DROP  R2
         SPACE 2
GETESD   MVI   SUBPOOLT,21
         LA    R0,LENESD
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL 21
         GETMAIN R,LV=(0)
         XC    0(LENESD,R1),0(R1)
         BR    R14
         SPACE 1
ESDOUT   L     R14,ESDIDRSV
         LA    R14,4(,R14)         ADJUST EXIT ADDRESS
ESDEXIT  LM    R15,R12,ESDIDRSV+4
         BR    R14                 EXIT
MAPLAMOD DC    C'AMODE'
MAPL$BLK DC    CL8'$BLKCOM'
MAPL$PRI DC    CL8'$PRIVATE'
MAPL$SEG DC    CL8'$SEGTAB'
MAPL$ENT DC    CL8'$ENTAB'
