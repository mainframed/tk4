ICHRIX01 TITLE 'RNB RACINIT PRE-PROCESSING EXIT'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* PURPOSE:  THIS EXIT, IN CONJUNCTION WITH IEFUJV, IS USED            *
*           TO DO PASSWORD CHECKING AND UPDATING FOR BATCH JOBS AT    *
*           JCL CONVERT TIME INSTEAD OF AT JOB INITIATION. THIS       *
*           RESULTS IN FEWER JCL ERRORS CAUSED BY A PASSWORD          *
*           CHANGING AFTER A JOB HAS BEEN SUBMITTED BUT BEFORE IT     *
*           RUNS.                                                     *
*                                                                     *
*           ALSO, FOR RACINIT DELETE PROCESSING, ICHRIX01 WILL FREE   *
*           THE RNBACEE POINTED TO BY ACEEIEP.                        *
*                                                                     *
*                                                                     *
* METHOD:   IEFUJV COLLECTS THE USERID, GROUP NAME (IF ANY) AND       *
*           PASSWORD(S) FOR THE JOB. IT THEN DOES A RACINIT AND       *
*           CANCELS THE JOB IF THE RACINIT FAILS. THEREFORE, IF A     *
*           BATCH JOB GETS THIS FAR, THE PASSWORD IS KNOWN TO BE      *
*           VALID, AND PASSWORD CHECKING WILL BE INHIBITED.           *
*           NOTE: IEFUJV WILL SET THE "INSTALLATION DATA" VALUE TO    *
*           IEFUJV IN THE RACINIT TO TELL ICHRIX01 TO CHECK THE       *
*           PASSWORD WHEN IEFUJV IS DOING HIS CHECK.                  *
*                                                                     *
* REGISTER USAGE:                                                     *
*           R1  - PARAMETER LIST POINTER                              *
*           R2  - PARAMETER POINTER                                   *
*           R12 - PROGRAM BASE                                        *
*           R13 - CALLER'S SAVEAREA                                   *
*           R14 - RETURN ADDRESS                                      *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ICHRIX01 CSECT
         SAVE  (14,12),,ICHRIX01_&SYSDATE_&SYSTIME
*
         LR    R12,R15
         USING ICHRIX01,R12        #####ICHRIX01
         ICM   R2,7,21(R1)         GET INSTALLATION DATA
         BZ    RIX100              IF NO DATA, CALLER NOT IEFUJV
         CLC   =C'IEFUJV',0(R2)    IS OUR CALLER IEFUJV?
         BE    EXIT0               /YES - PASSWORD MUST BE CHECKED
*                                  /NO  -
RIX100   L     R2,4(,R1)           GET FLAG ADDRESS
         TM    0(R2),X'C0'         ENVIR=CREATE?
         BNZ   RIX150              /NO  - GO TRY DELETE
*
         L     R2,80(,R1)          GET JOBNAME ADDRESS
         CLC   =CL8' ',0(R2)       IS THIS A BACKGROUND JOB?
         BE    EXIT0               /NO  - PASSWORD MUST BE CHECKED
*                                         FOR A TSO USER
         L     R2,8(,R1)           USERID ADDRESS
         CLI   0(R2),0             IS THERE A USERID?
         BE    EXIT0               /NO  - GET OUT, WRONG RACINIT
*
         L     R2,12(,R1)          PASSWORD ADDRESS
         CLI   0(R2),0             IS THERE A PASSWORD?
         BE    EXIT0               /NO  - GET OUT, WRONG RACINIT
*
         ICM   R2,7,93(R1)         DID RACINIT ISSUER GIVE ACEE ADDR?
         BNZ   EXIT0               /YES - PASSWORD MUST BE CHECKED IN
                                          THIS CASE.
*
* IF WE GET HERE WE DON'T NEED TO CHECK THE PASSWORD
*
         L     R2,4(,R1)           GET FLAG ADDRESS
         OI    0(R2),X'08'         SET PASSCHK=NO
*
EXIT0    SR    R15,R15             CONTINUE RACINIT PROCESSING
EXIT     RETURN (14,12),RC=(15)    RETURN TO RACINIT WITH RETURN CODE
         EJECT
*
* DELETE RNBACEE
*
RIX150   TM    0(R2),X'80'         IS THIS A RACINIT DELETE?
         BZ    EXIT0               /NO  - GET OUT
*
         L     R2,32(,R1)          GET ACEE ADDRESS
         USING ACEE,R2             #####
         ICM   R3,7,ACEEIEP+1      IS THERE A POINTER TO AN RNBACEE?
         BZ    EXIT0               /NO  - GET OUT
*
         USING RNBACEE,R3          #####
         CLC   =C'RNBA',RNBAKEY    IS IT REALLY AN RNBACEE?
         BNE   EXIT0               /NO  - GET OUT
*
         XC    ACEEIEP(4),ACEEIEP  CLEAR POINTER TO RNBACEE
         L     R0,RNBASPLN         GET SUBPOOL/LENGTH OF RNBACEE
         LR    R1,R3               GET ADDRESS OF RNBACEE
         DROP  R3                  #####
         FREEMAIN R,LV=(0),A=(1)   FREE THE RNBACEE
         B     EXIT0
         EJECT
         COPY  RNBACEE
         END
