*++USERMOD (PZ21002) /*ISPDVCGT: SUPPLY VALUE FOR ZGROUP SYSTEM VAR*/
*GEN                 /* GENZAPS USED FOR THIS MOD */ .
*
*++VER (Z038) FMID(HPF1102)
*GEN          REQ(PZ21001).
*
*++ZAP (ISPDVCGT)    /* RECOGNIZE ZGROUP AND SUPPLY VALUE FOR IT */.
*
*GEN  NAME ISPDVCGT
*GEN  IDRDATA PZ21002
*
*
*
ISPDVCGT CSECT
         REGEQU
I        EQU   R2
LDATAP   EQU   R8
LDLEN    EQU   R7
NOVALUE  EQU   ISPDVCGT+X'025E'
CE0001@  EQU   ISPDVCGT+X'02BC'
@RT00128 EQU   ISPDVCGT+X'0512'
         USING *,R12               ##### PROGRAM BASE
         PRINT GEN,DATA
*
*
*
         ORG   ISPDVCGT+X'0164'
*VER 0164 8BB0,0002                     SLA  @11,2
         B     ISPDVCGT+X'0818'         ISPPATCH
*
*
*
         ORG   ISPDVCGT+X'0818'
*VER 0818 00000000,00000000,00000000,00000000,00000000
*VER 0828 00000000,00000000,00000000,00000000,00000000
*VER 0838 00000000,00000000,00000000,00000000,00000000
*VER 0848 00000000,00000000,00000000,00000000,00000000
         SLA   R11,2                    DO REPLACED INSTR
         CH    I,H14                    IS IT VAR # 14 (ZGROUP) ?
         BE    GROUP                    GO PROCESS IT IF SO
         BH    NOVALUE                  IF HIGH TREAT AS NULL VARIABLE
         B     ISPDVCGT+X'0168'         IF LOW, RETURN TO MAIN CODE
GROUP    L     R11,CVTPTR               GET CVT
         USING CVT,R11                  #####
         L     R11,CVTTCBP              GET TCB WORDS
         L     R11,12(,R11)             GET ASCB
         USING ASCB,R11                 #####
         L     R11,ASCBASXB             GET ASXB
         USING ASXB,R11                 #####
         ICM   R11,7,ASXBSENV+1         GET ACEE
         BZ    NOVALUE                  TREAT AS NULL IF NO ACEE
         USING ACEE,R11                 #####
         LA    LDATAP,ACEEGRPN          POINT TO GROUP NAME
         SR    LDLEN,LDLEN              GET GROUP NAME LENGTH
         IC    LDLEN,ACEEGRPL
         B     CE0001@                  ALL DONE
H14      DC    H'14'                    HIGHEST USED, ALSO ZGROUP
*
*
*
         ORG   ISPDVCGT+X'08AC'
*VER 08AC 0000000D                      OLD DIM(VSYLST)
@CF00042 DC    F'19'                    NEW DIM(VSYLST)
*
*
*
         PRINT NOGEN
         CVT   DSECT=YES
         IHAACEE
         IHAASCB
         IHAASXB
         END
