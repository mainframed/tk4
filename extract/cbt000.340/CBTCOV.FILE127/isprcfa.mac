ISPRCFA  TITLE 'AUTHORIZES USER TO ALLOW USE OF AUTHORIZED COMMANDS'
***********************************************************************
*                          COPYRIGHT: 1981 RAINIER NATIONAL BANK      *
*                                     SEATTLE WASHINGTON              *
*                                     (206) 433-7467                  *
* MEMBER: ISPRCFA                                                     *
* SOURCE LIB: SLICK.PROD.SOURCE                                       *
* SMP USERMOD:                    (BLANK MEANS NOT INSTALLED BY SMP)  *
* LOAD LIB:   SYS1.LPALIB         (PART OF ISPSUBS)                   *
* MACRO LIBS NEEDED: SYS1.AMODGEN                                     *
*                    TEC.SYSTEM.MACLIB                                *
* LINKAGE EDITOR PARMS: LET,NCAL,RENT,REUS                            *
*                                                                     *
* PURPOSE:                                                            *
*        THIS ROUTINE DETERMINES IF THE COMMAND START OR COMMAND END  *
*        IS BEING RECORDED.  IF A COMMAND START IS RECORDED, THIS     *
*        ROUTINE SETS UP AN ESTAE, AND OBTAINS AUTHORIZATION.         *
*        IF A COMMAND END IS BEING RECORDED, THIS ROUTINE RELEASES    *
*        AUTHORIZATION, AND REMOVES THE ESTAE. REGULAR SPF CFA IS     *
*        THEN CALLED.                                                 *
*                                                                     *
* RETURN CODES:                                                       *
*        ALWAYS ZERO                                                  *
*                                                                     *
*                                                                     *
* CHANGE LOG:                                                         *
* 02/10/81 - ERIC ESTEB SETUP THIS HEADER AND MODIFIED SPFCFA TO      *
*            INSTALL WITH SPF 1.0 (NEW SPF)                           *
***********************************************************************
         EJECT
ISPRCFA  CSECT
* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% *
R0       EQU   0                         NOT USED
R1       EQU   1                         PASS PARMS AND WORK REGISTER
R2       EQU   2                         NOT USED
R3       EQU   3                         NOT USED
R4       EQU   4                         NOT USED
R5       EQU   5                         NOT USED
R6       EQU   6                         NOT USED
R7       EQU   7                         NOT USED
R8       EQU   8                         NOT USED
R9       EQU   9                         TLD BASE REGISTER
R10      EQU   10                        NOT USED
R11      EQU   11                        NOT USED
R12      EQU   12                        PROGRAM BASE REGISTER
R13      EQU   13                        DATA DSECT BASE REGISTER
R14      EQU   14                        RETURN REGISTER FROM CALLS
R15      EQU   15                        BRANCH REGISTER TO SUBROUTINES
* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% *
         SPACE 1
         SAVE  (14,12),,ISPRCFA_&SYSDATE_&SYSTIME
         SPACE 1
         L     R9,0(,R1)
         USING TLD,R9
         BAL   R14,TLDPCODE
         DC    AL2(((ENDDATA-DATA+7)/8)*8)
         EJECT
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
* %  THIS SECTION DETERMINES IF A COMMAND START OR COMMAND STOP IS  % *
* %  THE REQUEST TYPE, AND FINDS THE COMMAND NAME IN THE APFCTABL   % *
* %  IF A COMMAND START IS REQUESTED.                               % *
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
         SPACE 1
         USING ISPRCFA,R12               AFTER GETTING A DATA AREA
         USING DATA,R13                  AND SETTING UP BASE REGS,
         ST    R1,PARMADDR               SAVE THE PARAMETER ADDRESS
         LR    R2,R1                     SAVE REGISTER 1
         TESTAUTH FCTN=1                 CHECK FOR AUTHORIZATION
         LTR   R15,R15                   IS PROGRAM AUTHORIZED?
         BNZ   MAINCONT                  NO --- DO REGULAR CFA CALL
         L     R0,=CL4'AOFF'             TURN OFF APF AUTHORIZATION
         SVC   239
         MVC   RNBEST$E(RNBEST$L),RNBEST$M  SET UP ESTAE AREA
         ESTAE 0,MF=(E,RNBEST$E)         CANCEL THE ESTAE
MAINCONT DS    0H
         LR    R1,R2                     RESTORE REGISTER 1
         CLC   ZEROS,0(R2)               IS THIS A STOP REQUEST?
         BE    REGCFA
         L     R3,8(,R1)                 GET COMMAND NAME ADDRESS
         CLC   =CL8' ',0(R3)             IS THIS A BLANK COMMAND NAME?
         BE    REGCFA                    YES - CALL REGULAR CFA
         CLC   =CL3'ISP',0(R3)           IS THIS AN ISP COMMAND NAME?
         BE    REGCFA                    YES - CALL REGULAR CFA
         L     R2,APFCTABL               GET ADDRESS OF APF TABLE
         LA    R2,0(R2)
         LTR   R2,R2                     IS IT ZEROS?
         BZ    NOAUTH                    YES --- DON'T AUTHORIZE
APFLOOP  DS    0H
         CLC   =CL8' ',0(R2)             END OF TABLE?
         BE    NOAUTH                    YES --- DON'T AUTHORIZE
         CLC   0(8,R3),0(R2)             DOES COMMAND NAME MATCH?
         BE    FOUNDIT                   YES --- COMMAND IS FOUND
         LA    R2,8(,R2)                 ELSE POINT TO NEXT ENTRY
         B     APFLOOP                   LOOP THROUGH TABLE
NOAUTH   DS    0H
         B     REGCFA
         EJECT
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
* %  THIS SECTION SETS UP AN ESTAE AND GETS AUTHORIZATION BECAUSE   % *
* %  THE COMMAND IS APPROVED.                                       % *
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
FOUNDIT  DS    0H
         MVC   RNBEST$E(RNBEST$L),RNBEST$M  SET UP ESTAE AREA
         ESTAE MF=(E,RNBEST$E)           DO THE ESTAE
         LTR   R15,R15                   WAS ESTAE SUCESSFUL?
         BNZ   BADESTAE                  NO --- DO BAD ESTAE STUFF
         L     R0,=CL4'AON'              GET APF AUTHORIZATION
         SVC   239
         LTR   R15,R15                   WAS AUTH SVC SUCESSFUL?
         BNZ   BADSVC                    NO --- DO BAD SVC STUFF
         B     REGCFA                    CALL REGULAR CFA
BADESTAE DS    0H
         TPUT  MSG1,LMSG1
         B     REGCFA                    CALL REGULAR CFA
BADSVC   DS    0H
         MVC   RNBEST$E(RNBEST$L),RNBEST$M  SET UP ESTAE AREA
         ESTAE 0,MF=(E,RNBEST$E)         CANCEL THE ESTAE
         TPUT  MSG2,LMSG2
         B     REGCFA                    CALL REGULAR CFA
         EJECT
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
* %  THIS SECTION REMOVES AUTHORIZATION AND CANCELS THE ESTAE       % *
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
         SPACE 3
REGCFA   DS    0H
         L     R15,CFAADDR
         L     R1,PARMADDR               GET ORIGINAL PARM LIST
         BALR  R14,R15                   CALL REGULAR CFA
RETURN   DS    0H
         L     R15,TLDRC                 LOAD CFA RETURN CODE
         B     TLDECODE                  RETURN TO CALLER
         EJECT
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
* %  THIS SECTION IS THE ESTAE ROUTINE WHICH TURNS OFF AUTHORIZATION% *
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
RNBESTAE DS    0H
         USING *,R15
         LR    R2,R0                     SAVE SOME REGISTERS
         LR    R3,R1
         LR    R4,R14
         L     R0,=CL4'AOFF'             TURN OFF APF AUTHORIZATION
         SVC   239
         LR    R0,R2                     RESTORE REGISTERS
         LR    R1,R3
         LR    R14,R4
         SR    R15,R15                   CONTINUE WITH ABEND
         BR    R14
         DROP  R15
         EJECT
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
* %  THESE PAGES CONTAIN CONSTANTS USED BY THE PROGRAM.             % *
* %@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@%@% *
         SPACE 2
ZEROS    DC    F'0'
ONE      DC    F'1'
APFCTABL DC    VL4(APFCTABL)
CFAADDR  DC    VL4(ISPCFA)
RNBEST$M ESTAE RNBESTAE,TERM=YES,MF=L
RNBEST$L EQU   *-RNBEST$M
MSG1     DC    C'BAD ESTAE TRY AGAIN.  IF PROBLEM CONTINUES SEE TEC '
         DC    C'SUPPORT.'
LMSG1    EQU   *-MSG1
MSG2     DC    C'AUTHORIZATION FAILED. IF PROBLEM CONTINUES SEE TEC '
         DC    C'SUPPORT.'
LMSG2    EQU   *-MSG2
         LTORG
SIZEDATA DC    AL1(0)
         DC    AL3(ENDDATA-DATA)
DATA     DSECT
         DS    0D
SAVEAREA DS    18F
PARMAREA DS    50A
PARMADDR DS    F
RNBEST$E ESTAE RNBESTAE,TERM=YES,MF=L
         DS    0D
ENDDATA  EQU   *
         EJECT
--INCLUDE,MEM=ISPTLDDS.TEST
         END
