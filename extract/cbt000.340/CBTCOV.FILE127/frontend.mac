TEC401   TITLE 'PREPROCESSOR TO SEE IF USER CAN EXECUTE PROGRAM'
***********************************************************************
* MEMBER: TEC401                                                      *
* MACRO LIBS NEEDED: TEC.RNB.MACLIB                                   *
*                    SYS1.MACLIB                                      *
*                    SYS1.AMODGEN                                     *
*                                                                     *
* PURPOSE:                                                            *
*        TEC401 IS USED TO CHECK IF THE USER SUBMITTING THE JOB CAN   *
*        RUN THE PROGRAM INVOKED. THIS ROUTINE WILL BE LINKED INTO    *
*        THE LOAD MODULE AS THE ENTRY POINT. IT WILL FIND THE INVOKED *
*        PROGRAM NAME AND DO A RACHECK AGAINST IT IN CLASS 'APPL'.    *
*        IF THE USER IS AUTHORIZED TO RUN THE PROGRAM THEN IT WILL    *
*        RESET ALL THE REGISTERS ANDD CALL THE PROGRAM.               *
*        IF AUTHORIZATION FAILS IT WILL ABEND WITH A SYSTEM 047.      *
*                                                                     *
* RETURN CODES:                                                       *
*        NONE GENERATED BY THIS PROGRAM                               *
*                                                                     *
*                                                                     *
* HOW TO USE:                                                         *
*                                                                     *
*        RELINK THE PROGRAM TO PUT IN WITH LINK CARDS AS FOLLOW       *
*                                                                     *
*        CHANGE   VCON(XXXXXXXX)                                      *
*        INCLUDE  LIB(TEC401)                                         *
*        INCLUDE  PRODLIB(XXXXXXXX)   (PRODLIB IS LOAD LIB WITH       *
*        ENTRY    TEC401               REGULAR COPY OF PROGRAM)       *
*        NAME     XXXXXXXX(R)                                         *
*                                                                     *
*             WHERE XXXXXXXX IS THE PROGRAM NAME                      *
*                                                                     *
* CHANGE LOG:                                                         *
*                                                                     *
*       03/03/81 - REWORKED TO REMOVE SOME LOCAL STRUCTURED MACROS.   *
*                  THIS NEW VERSION HAS NOT BEEN TESTED, BUT I THINK  *
*                  IT SHOULD WORK.                                    *
*                                                                     *
***********************************************************************
         EJECT
TEC401   CSECT
         SAVE  (14,12),,RACF_FRONT_END_&SYSDATE_&SYSTIME
         LA    R12,0(,R15)         GET BASE WITH HIGH BYTE CLEARED
         USING TEC401,R12          ##### PROGRAM BASE
         GETMAIN R,LV=WORKLEN
         ST    R13,4(,R1)          LINK SAVE AREAS
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORK,R13            ##### WORKAREA BASE
         RACSTAT                   IS RACF ACTIVE?
         LTR   R15,R15               .
         BNZ   CALLPGM             /NO  - ALLOW PROGRAM ACCESS
*                                   YES - CHECK IF ACCESS ALLOWED
         L     R2,16              A(CVT)
         L     R2,0(,R2)          A(TCB WORDS)
         L     R2,4(,R2)          A(CURRENT TCB)
         USING TCB,R2
         L     R2,TCBRBP          GET RB POINTER
         USING RBBASIC,R2
         L    R2,RBCDE          GET CDE ADDRESS
         USING CDENTRY,R2
*
         MVC  PGMNAME,CDNAME    GET PROGRAM NAME
         MVC  WTOE(WTOPATL),WTOPAT    MOVE PATTERN WTO (IN CASE ERROR)
         MVC  WTOE+4(8),CDNAME        MOVE PGM NAME INTO WTO MSG
         MVC  RCPARM(RCPATL),RCPAT    MOVE PATTERN RACHECK
         L    R2,CDENTPT
         DROP R2
*
         LA   R2,0(,R2)          CLEAR HIGH BYTE OF ENTRY ADDRESS
         CR   R2,R12             ARE WE THE EXECUTED PROGRAM?
         BNE  TEC40150           /NO  - DON'T LET IT RUN, AS SOMEONE
*                                       IS TRYING TO FOOL US
*                                 ELSE SEE IF ALLOWED TO USE PROGRAM
         RACHECK ENTITY=(PGMNAME),ATTR=READ,MF=(E,RCPARM)
         B    TEC40160
*
TEC40150 LA    R15,X'FF'
*
TEC40160 LTR   R15,R15           OK TO USE PROGRAM?
         BZ    CALLPGM           /YES - GO DO IT
*                                ELSE PREPARE FOR MSG/ABEND
*
         ST     R15,R15SAVE       STORE R15
         UNPK   WORKAREA,R15SAVE+1(4)  UNPACK IT
         TR     WORKAREA,HEXTAB-240
         MVC    WTOE+71(2),WORKAREA+5  PUT IT IN MESSAGE
         WTO    MF=(E,WTOE)       WRITE MESSAGE
         ABEND  71,,,SYSTEM       ABEND WITH S047 ABEND
*
*
CALLPGM  DS    0H
         LR    R1,R13              GET WORKAREA ADDRESS TO FREE
         LR    R13,4(,R13)         GET ORIGINAL SAVE AREA AND R13
         FREEMAIN R,A=(1),LV=WORKLEN  FREE OUR WORKAREA
         L     R15,VCON            GET ADDRESS OF PROGRAM
         L     R14,12(,R13)        GET ORIGINAL R14
         LM    R0,R12,20(R13)      GET ORIGINAL R0-R12
         BR    R15                 GO TO THE ACTUAL PROGRAM
*
*
*
VCON     DC     V(VCON)            THIS IS THE POINTER TO PROGRAM
ZERO     DC     F'0'
FOUR     DC     F'4'
RCPAT    RACHECK CLASS='APPL',MF=L
RCPATL   EQU    *-RCPAT
WTOPAT   WTO   'XXXXXXXX TERMINATED - USER NOT AUTHORIZED TO EXECUTE TH*
               IS PROGRAM. RC',ROUTCDE=(2,9,11),DESC=(6,7),MF=L
WTOPATL  EQU   *-WTOPAT
HEXTAB   DC    C'0123456789ABCDEF'
*
*
*
WORK     DSECT
         DS    18F                 SAVE AREA
R15SAVE  DS    F
WORKAREA DS    CL8
PGMNAME  DS    CL8
RCPARM   DS    0F,XL(RCPATL)
WTOE     DS    0F,XL(WTOPATL)
WORKLEN  EQU   *-WORK
*
*
*
         REGEQU
         PRINT  NOGEN
         IKJTCB      TCB DSECT
         IHARB       RB  DSECT
         IHACDE      CDE DSECT
         END
