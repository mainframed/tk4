TEC002   TITLE 'LOGON PREPROMPT EXIT GENERATES ALL JCL FOR LOGON'
***********************************************************************
*                          COPYRIGHT: 1980 RAINIER NATIONAL BANK      *
*                                     SEATTLE WASHINGTON              *
*                                     (206) 433-7467                  *
* MEMBER: TEC002                                                      *
* SOURCE LIB: SLICK.PROD.SOURCE                                       *
* SMP USERMOD:                    (BLANK MEANS NOT INSTALLED BY SMP)  *
* LOAD LIB:   LOAD LIBRARY NAME                                       *
* MACRO LIBS NEEDED: SYS1.AMODGEN                                     *
*                    TEC.RACF.MACLIB                                  *
*                    TEC.SYSTEM.MACLIB                                *
*                      . . .                                          *
* LINKAGE EDITOR PARMS: LET,NCAL,RENT,REUS                            *
*                                                                     *
* PURPOSE:                                                            *
*          THE PURPOSE OF THIS PROGRAM IS TO SUPPLY ALL THE JCL FOR A *
*          TSO LOGON.  ALL THE PROMPTING NECESSARY TO OBTAIN THE      *
*          INFORMATION FOR THE TSO LOGON IS DONE IN THIS PROGRAM.     *
*          BY BUILDING THE JCL OURSELVES, WE CAN PUT ON THE PROGRAMMER*
*          NAME (FROM RACF) AND CAN CONTROL THE MESSAGE CLASS THAT    *
*          GO ON THE JCL.                                             *
*                                                                     *
* RETURN CODES:                                                       *
*        THE RETURN CODE IS ALWAYS ZERO.  DIFFERENT OPTIONS ARE       *
*        CONTROLED BY THE CONTROL SWITCHES (SEE THE TSO SPL MANUAL    *
*        TO UNDERSTAND THE PARAMETERS THAT ARE PASSED TO THIS PROGRAM *
*        AND WHAT THIS PROGRAM CAN DO TO THOSE PARAMETERS.            *
*        THIS PROGRAM DOES THE WORK OF IKJEFLD                        *
*                                                                     *
*                                                                     *
* CHANGE LOG:                                                         *
*    10/20/80 - INITIAL VERSION BY ERIC ESTEB                         *
*                                                                     *
* COMMENTS:                                                           *
*    THE PARENT PROGRAM TO THIS ONE IS LOGONXIT (3.1) FROM THE CBT    *
*    TAPE (80.171) BUT WE HAVE SO HEAVILY MODIFIED IT THAT ANY        *
*    RESEMBLENCE IS COINSIDENTAL.  WE WANTED TO USE THE RACF DATASET  *
*    TO CONTAIN ADITIONAL INFORMATION RATHER THAN ADDING A USER INFO  *
*    MEMBER TO UADS.                                                  *
*                                                                     *
***********************************************************************
         EJECT
IKJEFLD  CSECT
         SAVE  (14,12),,TEC002_&SYSDATE_&SYSTIME        SAVE REGS
         USING TEC002,R9,R10,R11,R12 ESTABLISH ADDRESSABILITY
         LR    R2,R1               SAVE CALLER'S R1
         LR    R9,R15              LOAD FIRST BASE
         LA    R10,2048(,R9)       START ON SECOND BASE
         LA    R10,2048(,R10)      LOAD SECOND BASE
         LA    R11,2048(,R10)      START ON THIRD BASE
         LA    R11,2048(,R11)      LOAD THIRD BASE
         LA    R12,2048(,R11)      START ON FOURTH BASE
         LA    R12,2048(,R12)      LOAD FOURTH BASE
         GETMAIN RU,LV=GOTNLEN,BNDRY=PAGE GETMAIN DYNAMIC AREA
         LR    R15,R1              GET ADDRESS OF DYNAMIC AREA
         LR    R0,R1               GET ADDRESS OF DYNAMIC AREA
         LA    R1,GOTNLEN          GET LENGTH OF DYNAMIC AREA
         SR    R4,R4               ZERO FROM ADDRESS
         SR    R5,R5               ZERO FROM LENGTH
         MVCL  R0,R4               CLEAR DYNAMIC AREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVEAREAS
         LR    R13,R15             POINT TO MY SAVE AREA
         USING GOTNAREA,R13        ESTABLISH ADDRESSABILITY
         TITLE 'MAIN LINE'
         BAL   R14,HOUSKEEP        GO DO HOUSEKEEPING
         LTR   R15,R15             WAS HOUSEKEEPING SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,SCAN            YES - GO SCAN COMMAND BUFFER
         LTR   R15,R15             WAS SCAN SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,WELCOME         YES - WELCOME THE USER
         LTR   R15,R15             WAS WELCOME SUCESSFUL?
         BNZ   RETURN              NO - EXIT
         BAL   R14,PARSE           YES - GO PARSE COMMAND BUFFER
         LTR   R15,R15             WAS PARSE SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,GETDATA         YES - GO GET DATA FROM PDL
         LTR   R15,R15             WAS DATA RETRIVAL SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,VERDATA         YES - GO VERIFY DATA AGAINST UADS
         LTR   R15,R15             WAS DATA VERIFICATION SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,BUILDJCL        YES - GO BUILD JCL
         LTR   R15,R15             WAS JCL BUILD SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         TITLE 'RETURN TO CALLER'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    RETURN                                                           *
*                                                                     *
* FUNCTION -                                                          *
*    PERFORMS CLEANUP AND RETURNS TO CALLER.                          *
*                                                                     *
* OPERATION -                                                         *
*    1) CANCEL STIMER.                                                *
*    2) CANCEL STAX.                                                  *
*    4) IF UADS WAS OPENED, CLOSE AND FREE BUFFER.                    *
*    5) IF LOGON IS TO BE DENIED, SET DISCONNECT INDICATOR AND WAIT   *
*       5 SECONDS IF THIS IS A DISPLAY TERMINAL TO GIVE THE USER      *
*       TIME TO READ THE DIAGNOSTICS.                                 *
*    6) IF ATTENTION WAS HIT, GO CHECK NEW COMMAND.                   *
*    7) IF LOGON IS TO BE ALLOWED, INDICATE DONT PROMPT AND UPDATE    *
*       DOPE VECTORS.                                                 *
*    8) IF RECONNECT WAS SPECIFIED, TURN ON RECONNECT BIT             *
*    9) CANCEL ESTAE.                                                 *
*   10) RETURN TO CALLER.                                             *
*                                                                     *
*                                                                     *
* ENTRY POINTS -                                                      *
*    RETURN                                                           *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
RETURN   DS    0H                  RETURN TO CALLER
         LR    R2,R15              PROTECT RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       CANCEL STIMER.                                                *
*                                                                     *
***********************************************************************
         TTIMER CANCEL             CANCEL STIMER
         EJECT
***********************************************************************
*                                                                     *
*       CANCEL STAX.                                                  *
*                                                                     *
***********************************************************************
         STAX  0                   CANCEL STAX
         STAX  0                   CANCEL STAX
         EJECT
***********************************************************************
*                                                                     *
*       IF UADS WAS OPENED, CLOSE AND FREE BUFFER.                    *
*                                                                     *
***********************************************************************
         TM    GSYSUADS+DCBOFLGS-IHADCB,DCBOFOPN DID UADS OPEN
         BZ    RTRNNOPN            NO - NO NEED TO DO FREEMAIN
         LH    R0,GSYSUADS+DCBBLKSI-IHADCB YES - GET BLKSIZE
         MH    R0,HALF12           MULTIPLY BY 12
         ICM   R1,B'1111',ADDRINFO GET ADDRESS OF BUFFER
         BZ    RTRNNOPN            IF ZERO - SKIP FREEMAIN
         FREEMAIN R,LV=(0),A=(1)   FREE AREA FOR SYS1.UADS BUFFER
         CLOSE MF=(E,GOPENLST)     CLOSE SYS1.UADS
RTRNNOPN DS    0H
         EJECT
***********************************************************************
*                                                                     *
*       IF LOGON IS TO BE DENIED, SET DISCONNECT INDICATOR AND WAIT   *
*       5 SECONDS IF THIS IS A DISPLAY TERMINAL TO GIVE THE USER      *
*       TIME TO READ THE DIAGNOSTICS.                                 *
*                                                                     *
***********************************************************************
         L     R1,ADDRPARM         RESTORE CALLER'S R1
         TM    GBITS1,GDENY        IS LOGON TO BE DENIED
         BNO   RTRNOK              NO - CONTINUE
RTRNDENY DS    0H
         L     R15,0(,R1)          YES - GET ADDRESS OF CONTROL SW DESC
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCHES
         OI    0(R15),KBIT3        INDICATE DISCONNECT
         GTSIZE ,                  DETERMINE TERMINAL TYPE
         LTR   R0,R0               WAS THIS A DISPLAY
         BZ    RTRNEND             NO - CONTINUE
         STIMER WAIT,DINTVL=WAITTIME YES - WAIT A LITTLE WHILE FIRST
         B     RTRNCLER            THEN CONTINUE
RTRNOK   DS    0H
         EJECT
***********************************************************************
*                                                                     *
*       IF ATTENTION WAS HIT, GO CHECK NEW COMMAND.                   *
*                                                                     *
***********************************************************************
         TM    GBITS1,GATTNHIT     WAS ATTENTION HIT
         BO    RTRNCHEK            YES - GO CHECK NEW COMMAND
         EJECT
***********************************************************************
*                                                                     *
*       IF LOGON IS TO BE ALLOWED, INDICATE DONT PROMPT AND UPDATE    *
*       DOPE VECTORS.                                                 *
*                                                                     *
***********************************************************************
         L     R15,0(,R1)          GET ADDRESS OF CONTROL SW DESC
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCHES
         OI    0(R15),KBIT4        INDICATE NO PROMPT
         L     R15,8(,R1)          GET ADDRESS OF USERID DESCRIPTOR
         MVC   6(2,R15),LUSERID    SET LENGTH OF USERID
         L     R15,0(,R15)         GET ADDRESS OF USERID
         MVC   0(7,R15),GUSERID    MOVE USERID TO DATA AREA
         L     R15,12(,R1)         GET ADDRESS OF PASSWORD DESCRIPTOR
         MVC   6(2,R15),LPASS      SET LENGTH OF PASSWORD
         SR    R3,R3               GET LENGTH OF PASSWORD FOR EXECUTE
         LH    R3,LPASS
         BCTR  R3,0                CHANGE TO DISPLACEMENT
         L     R4,0(,R15)          GET ADDRESS OF PASSWORD
         LA    R2,GPASS
         EX    R3,MOVEIT           MOVE PASSWORD
         L     R15,32(,R1)         GET ADDRESS OF PASSWORD DESCRIPTOR
         MVC   6(2,R15),LNPASS     SET LENGTH OF NEW PASSWORD
         SR    R3,R3               GET LENGTH OF NEW PASSWORD FOR EX
         LH    R3,LNPASS
         LTR   R3,R3
         BZ    MOVEGRUP
         BCTR  R3,0                CHANGE TO DISPLACEMENT
         L     R4,0(,R15)          GET ADDRESS OF NEW PASSWORD
         LA    R2,GNPASS
         EX    R3,MOVEIT           MOVE NEW PASSWORD
MOVEGRUP DS    0H
         L     R15,72(,R1)         GET ADDRESS OF GROUP DESCRIPTOR
         MVC   6(2,R15),LGRUP      SET LENGTH OF RACF GROUP NAME
         SR    R3,R3               GET LENGTH OF RACF GROUP NAME
         LH    R3,LGRUP
         LTR   R3,R3
         BZ    MOVEACCT
         BCTR  R3,0                CHANGE TO DISPLACEMENT
         L     R4,0(,R15)          GET ADDRESS OF RACF GROUP NAME
         LA    R2,GGRUP
         EX    R3,MOVEIT           MOVE RACF GROUP NAME
MOVEACCT DS    0H
         L     R15,16(,R1)         GET ADDRESS OF ACCT NUM DESCRIPTOR
         MVC   6(2,R15),LACCTNUM   SET LENGTH OF ACCOUNT NUMBER
         SR    R3,R3               GET LENGTH OF ACCOUNT NUMBER
         LH    R3,LACCTNUM
         BCTR  R3,0                CHANGE TO DISPLACEMENT
         L     R4,0(,R15)          GET ADDRESS OF ACCOUNT NUMBER
         LA    R2,GACCTNUM
         EX    R3,MOVEIT           MOVE ACCOUNT NUMBER
         L     R15,20(,R1)         GET ADDRESS OF PROC NAME DESCRIPTOR
         MVC   6(2,R15),LPROC      SET LENGTH OF PROCEDURE NAME
         SR    R3,R3               GET LENGTH OF PROCEDURE NAME
         LH    R3,LPROC
         BCTR  R3,0                CHANGE TO DISPLACEMENT
         L     R4,0(,R15)          GET ADDRESS OF PROCEDURE NAME
         LA    R2,GPROC
         EX    R3,MOVEIT           MOVE PROCEDURE NAME
         EJECT
***********************************************************************
*                                                                     *
*       IF RECONNECT WAS SPECIFIED, SET RECONNECT BIT                 *
*                                                                     *
***********************************************************************
         TM    GBITS1,GRECON       WAS 'RECONNECT' SPECIFIED
         BNO   RTRNCLER            NO - ALLOW OTHER OPERANDS
         L     R15,0(,R1)          GET ADDRESS OF CONTROL SW DESC
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCHES
         OI    1(R15),KBIT7        INDICATE RECONNECT
         EJECT
***********************************************************************
*                                                                     *
*       IF A DISPLAY TERMINAL, CLEAR THE SCREEN.                      *
*                                                                     *
***********************************************************************
RTRNCLER DS    0H
         TM    GBITS2,GDISPLAY
         BZ    RTRNEND             NO - CONTINUE
         STLINENO LINE=1,MODE=OFF  CLEAR SCREEN (VTAM WAY)
         LTR   R15,R15             WAS EVERYTHING PROPER?
         BZ    RTRNEND             /YES - CONTINUE
         CH    R15,HALF8           IS THIS NOT A VTAM TERMINAL?
         BNE   RTRNERR1            /NO - IT IS A VTAM ERROR
         TPUT  KMSG,KMSGL,FULLSCR,,HOLD  CLEAR SCREEN (TCAM WAY)
         B     RTRNEND
RTRNERR1 DS    0H
         EJECT
***********************************************************************
*                                                                     *
*       CANCEL ESTAE.                                                 *
*                                                                     *
***********************************************************************
RTRNEND  DS    0H
         ESTAE 0                   CANCEL ESTAE
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO CALLER.                                             *
*                                                                     *
***********************************************************************
         LR    R1,R13              GET POINTER TO DYNAMIC AREA
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         FREEMAIN R,LV=GOTNLEN,A=(1) FREE DYNAMIC AREA
         LR    R15,R2              RESTORE RETURN CODE
         RETURN (14,12),RC=(15)    RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       IF COMMAND WAS NOT 'LOGON' DENY LOGON.  IF COMMAND WAS        *
*       'LOGON', REENTER LOGON EXIT WITH NEW COMMAND.                 *
*                                                                     *
***********************************************************************
RTRNCHEK DS    0H
         BAL   R14,SCAN            CALL SCAN TO SEE IF 'LOGON'
         LTR   R15,R15             WAS THE NEW COMMAND 'LOGON'
         BZ    RTRNRENT            YES - REENTER
         L     R1,ADDRPARM         NO - RESTORE CALLER'S R1
         B     RTRNDENY            WAIT 5 SECONDS AND DISCONNECT
RTRNRENT DS    0H
         ESTAE 0                   CANCEL ESTAE
         LR    R1,R13              GET POINTER TO DYNAMIC AREA
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         FREEMAIN R,LV=GOTNLEN,A=(1) FREE DYNAMIC AREA
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGISTERS
         BR    R15                 REENTER
         TITLE 'HOUSEKEEPING ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    HOUSKEEP                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    PERFORMS HOUSEKEEPING FUNCTIONS FOR LOGONXIT.                    *
*                                                                     *
* OPERATION -                                                         *
*    1) ISSUES ESTAE.  IF ESTAE IS UNSUCCESSFUL, TELLS TSO USER AND   *
*       DENIES LOGON.                                                 *
*    2) MOVES STIMER EXIT TO DYNAMIC AREA AND ISSUES STIMER.          *
*    3) ISSUES STAX.  IF STAX IS UNSUCCESSFUL, TELLS TSO USER AND     *
*       DENIES LOGON.                                                 *
*    4) CHECKS TO SEE IF THIS IS 'RESOURCE FAILURE'.  IF SO, TELLS TSO*
*       USER AND DENIES LOGON.                                        *
*    5) CHECKS TO SEE IF THIS IS 'ABEND'.  IF SO, TELLS TSO USER AND  *
*       DENIES LOGON.                                                 *
*    6) ATTEMPTS TO OPEN SYS1.UADS.  IF OPEN IS UNSUCCESSFUL, TELLS   *
*       TSO USER AND DENIES LOGON.                                    *
*    7) GETS STORAGE FOR UADS DATA BLOCKS.                            *
*    8) BUILDS CBUF AND CPPL.                                         *
*    9) CLEAR TERMINAL BUFFERS.                                       *
*   12) RETURNS TO THE MAINLINE.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    HOUSKEEP                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    'ESTAE UNSUCCESSFUL - RC=NNNN'                                   *
*    'STAX UNSUCCESSFUL - RC=NNNN'                                    *
*    'USERID CURRENTLY LOGGED ON'                                     *
*    'RESOURCE FAILURE. CONTACT TECHNICAL SUPPORT'                    *
*    'LOGON ABEND. CONTACT TECHNICAL SUPPORT'                         *
*    'OPEN OF SYS1.UADS UNSUCCESSFUL'                                 *
*                                                                     *
***********************************************************************
HOUSKEEP DS    0H
         ST    R14,HSKPSAVE        SAVE RETURN ADDRESS
         ST    R2,ADDRPARM         SAVE ADDRESS OF PARMS
         EJECT
***********************************************************************
*                                                                     *
*       ISSUES ESTAE.  IF ESTAE IS UNSUCCESSFUL, TELLS TSO USER AND   *
*       DENIES LOGON.                                                 *
*                                                                     *
***********************************************************************
         MVC   GESTAE(LGESTAE),LESTAE COPY ESTAE PARM LIST
         LA    R3,ESTAEXIT         GET ADDRESS OF ESTAE EXIT
         LA    R4,ESTAEPRM         GET ADDRESS OF ESTAE PARMS
         ST    R9,ESTAEPRM         SAVE 1ST BASE FOR EXIT ROUTINES
         ST    R10,ESTAEPRM+4      SAVE 2ND BASE FOR EXIT ROUTINES
         ST    R11,ESTAEPRM+8      SAVE 3RD BASE FOR EXIT ROUTINES
         ST    R12,ESTAEPRM+12     SAVE 4TH BASE FOR EXIT ROUTINES
         ST    R13,ESTAEPRM+16     SAVE DYNAMIC BASE FOR EXIT RTNS
         ESTAE (3),PARAM=(4),MF=(E,GESTAE) ESTABLISH ESTAE
         LTR   R15,R15             WAS ESTAE SUCCESSFUL
         BNZ   HSKPNSTA            NO - GO GIVE MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*       MOVES STIMER EXIT TO DYNAMIC AREA AND ISSUES STIMER.          *
*                                                                     *
***********************************************************************
         MVC   GTIMRXIT,TIMERXIT   MOVE STIMER EXIT TO DYNAMIC AREA
         STIMER REAL,GTIMRXIT,DINTVL=LOGTIME ISSUE STIMER
         MVC   GTIMRWTO,TIMERWTO   COPY WTO TEXT TO DYNAMIC AREA
         EJECT
***********************************************************************
*                                                                     *
*       ISSUES STAX.  IF STAX IS UNSUCCESSFUL, TELLS TSO USER AND     *
*       DENIES LOGON.                                                 *
*                                                                     *
***********************************************************************
         XC    GDSTAX(LSTAX),GDSTAX CLEAR DUMMY STAX PARM LIST
         LA    R1,GDSTAX           GET ADDRESS OF DUMMY STAX PRM LST
         USING STX,R1              ESTABLISH ADDRESSABILITY
         LA    R15,STAXDXIT        GET ADDRESS OF DUMMY STAX EXIT
         ST    R15,STXEXIT         PUT IN STAX PARAMETER LIST
         LA    R15,ESTAEPRM        GET ADDRESS OF ESTAE PARAM LIST
         ST    R15,STXOPTS         PUT IN STAX PARAMETER LIST
         DROP  R1                  DROP STAX PRM LIST ADDRESSABILITY
         STAX  MF=(E,(1))          ISSUE FIRST STAX
         LTR   R15,R15             WAS STAX SUCCESSFUL
         BNZ   HSKPNSTX            NO - GO GIVE MESSAGE
         XC    GSTAX(LSTAX),GSTAX  YES - CLEAR STAX PARM LIST
         LA    R1,GSTAX            GET ADDRESS OF STAX PARAM LIST
         USING STX,R1              ESTABLISH ADDRESSABILITY
         LA    R15,STAXEXIT        GET ADDRESS OF STAX EXIT
         ST    R15,STXEXIT         PUT IN STAX PARAMETER LIST
         L     R2,ADDRPARM         GET ADDRESS OF ORIGINAL PARMS
         L     R2,4(,R2)           GET ADDRESS OF INPUT BUFFER DESC
         LH    R15,4(,R2)          GET MAX LEN OF INPUT BUFFER AREA
         STH   R15,STXISIZ         PUT IN STAX PARAMETER LIST
         LA    R15,L'STAXOBUF      GET LENGTH OF STAX MESSAGE
         STH   R15,STXOSIZ         PUT IN STAX PARAMETER LIST
         LA    R15,STAXOBUF        GET ADDRESS OF STAX MESSAGE
         ST    R15,STXOBUF         PUT IN STAX PARAMETER LIST
         L     R15,0(,R2)          GET ADDRESS OF ORIGINAL BUFF AREA
         ST    R15,STXIBUF         PUT IN STAX PARAMETER LIST
         LA    R15,ESTAEPRM        GET ADDRESS OF ESTAE PARAM LIST
         ST    R15,STXOPTS         PUT IN STAX PARAMETER LIST
         MVI   GSTAX+STXOPTS-STX,KBIT1 INDICATE REPLACE=NO
         DROP  R1                  DROP STAX PRM LIST ADDRESSABILITY
         STAX  MF=(E,(1))          ESTABLISH STAX
         LTR   R15,R15             WAS STAX SUCCESSFUL
         BNZ   HSKPNSTX            NO - GO GIVE MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THIS IS 'USERID ENQ FAIL'.  IF SO, TELLS TSO *
*       USER AND DENIES LOGON.                                        *
*                                                                     *
***********************************************************************
         L     R2,ADDRPARM         GET CALLER'S R1
         L     R15,0(,R2)          GET ADDRESS OF CONTROL SWITCH DESC.
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCH
         TM    0(R15),KBIT0        WAS THIS 'USERID ENQ FAIL'
         BO    HSKPDUPU            YES - GO GIVE MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THIS IS 'RESOURCE FAILURE'.  IF SO, TELLS TSO*
*       USER AND DENIES LOGON.                                        *
*                                                                     *
***********************************************************************
         TM    0(R15),KBIT2        WAS THIS 'RESOURCE FAILURE'
         BO    HSKPRESC            YES - GO GIVE MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THIS IS 'ABEND'.  IF SO, TELLS TSO USER AND  *
*       DENIES LOGON.                                                 *
*                                                                     *
***********************************************************************
         TM    1(R15),KBIT6        WAS THIS 'ABEND'
         BO    HSKPABND            YES - GO GIVE MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*       GET THE TERMINAL ID                                           *
*                                                                     *
***********************************************************************
         MVC   GTERMID,ABNDWTO     ASSUME 'UNKNOWN'
         L     R15,PSATOLD-PSA     GET ADDRESS OF MY TCB
         L     R15,TCBJSCB-TCB(,R15) GET ADDRESS OF MY JSCB
         TM    JSCBOPTS-IEZJSCB(R15),JSCBAUTH CAN I ISSUE MODESET
         BZ    HSKPGTRM            NO - CANT GET TERMINAL FROM TSB
         L     R15,PSAAOLD-PSA     YES - GET ADDRESS OF MY ASCB
         L     R3,ASCBTSB-ASCB(,R15) GET ADDRESS OF MY TSB
         MODESET MF=(E,KEYZERO)    GET KEY 0 FOR MOVE FROM TSB
         MVC   GTERMID,TSBTRMID-TSB(R3) COPY TERMINAL TO DYN. AREA
         MODESET MF=(E,NONZERO)    RETURN TO USER KEY
HSKPGTRM DS    0H
         MVC   GTIMRWTO+15(8),GTERMID MOVE TERMID TO TIMER WTO
         EJECT
***********************************************************************
*                                                                     *
*       ATTEMPTS TO OPEN SYS1.UADS.  IF OPEN IS UNSUCCESSFUL, TELLS   *
*       TSO USER AND DENIES LOGON.                                    *
*                                                                     *
***********************************************************************
         MVC   GSYSUADS,SYSUADS    MOVE SYS1.UADS DCB TO DYNAMIC AREA
         MVC   GOPENLST,OPENUPDT   MOVE OPEN LIST TO DYNAMIC AREA
         LA    R15,GJFCB           GET ADDRESS OF JFCB AREA
         ST    R15,EXLST           PUT IN EXLST
         MVI   EXLST,X'87'         INDICATE JFCB ADDRESS
         LA    R15,EXLST           GET ADDRESS OF EXLST
         LA    R1,GSYSUADS         GET ADDRESS OF SYSUADS DCB
         USING IHADCB,R1           GET DCB ADDRESSABILITY
         STCM  R15,B'0111',DCBEXLSA PUT ADDRRESS OF EXLST IN DCB
         STCM  R1,B'0111',GOPENLST+1 PUT ADDR OF DCB IN OPEN LIST
         DROP  R1                  DROP DCB ADDRESSABILITY
         OPEN  MF=(E,GOPENLST)     ATTEMPT TO OPEN SYS1.UADS
         TM    GSYSUADS+DCBOFLGS-IHADCB,DCBOFOPN DID UADS OPEN
         BZ    HSKPNOPN            NO - GO TO ERROR ROUTINE
         RDJFCB MF=(E,GOPENLST)    YES - READ JFCB FOR LATER ENQ
         L     R15,PSATNEW-PSA     GET ADDRESS OF MY TCB
         L     R15,TCBTIO-TCB(,R15) GET ADDRESS OF MY TIOT
         AH    R15,GSYSUADS+DCBTIOT-IHADCB GET ADDRESS OF UADS TIOT
         MVC   ADDRUCB,16(R15)     SAVE UCB ADDRESS FOR RESERVE
         EJECT
***********************************************************************
*                                                                     *
*       GETS STORAGE FOR UADS DATA BLOCKS.                            *
*                                                                     *
***********************************************************************
         LH    R0,GSYSUADS+DCBBLKSI-IHADCB GET BLKSIZE
         MH    R0,HALF12           MULTIPLY BY 12
         GETMAIN RU,LV=(0),BNDRY=PAGE GET AREA FOR SYS1.UADS BUFFER
         ST    R1,ADDRINFO         SAVE ADDRESS OF INFO BLOCK BUFFER
         AH    R1,GSYSUADS+DCBBLKSI-IHADCB BUMP UP ONE BLOCK
         ST    R1,ADDR$SYS         SAVE ADDRESS OF $SYSTEM$ BUFFER
         AH    R1,GSYSUADS+DCBBLKSI-IHADCB BUMP UP ONE BLOCK
         ST    R1,ADDRUBUF         SAVE ADDRESS FOR LATER
         EJECT
***********************************************************************
*                                                                     *
*       BUILDS CBUF AND CPPL.                                         *
*                                                                     *
***********************************************************************
         L     R2,ADDRPARM         GET CALLER'S R1
         L     R15,4(,R2)          GET ADDRESS OF INPUT BUFFER DESC
         LH    R14,4(,R15)         GET LENGTH OF INPUT BUFFER
         LA    R14,4(,R14)         ADD LENGTH AND OFFSET
         STH   R14,GCBUF+CBUFLEN-CBUF PUT IN CBUF
         XC    GCBUF+CBUFOFFS-CBUF(2),GCBUF+CBUFOFFS-CBUF ZERO OFFSET
         L     R15,0(,R15)         GET ADDRESS OF INPUT BUFFER
         MVC   GCBUF+CBUFTEXT-CBUF(252),0(R15) COPY TO COMMAND BUFFER
         LA    R15,GCBUF           GET ADDRESS OF COMMAND BUFFER
         ST    R15,GCPPL+CPPLCBUF-CPPL PUT IN CPPL
         L     R15,48(,R2)         GET ADDRESS OF UPT DESC
         L     R15,0(,R15)         GET ADDRESS OF UPT
         ST    R15,GCPPL+CPPLUPT-CPPL PUT IN CPPL
         L     R15,PSATNEW-PSA     GET ADDRESS OF MY TCB
         L     R15,TCBJSCB-TCB(R15) GET ADDRESS OF MY JSCB
         L     R15,JSCBPSCB-IEZJSCB(,R15) GET ADDRESS OF MY PSCB
         ST    R15,GCPPL+CPPLPSCB-CPPL PUT IN CPPL
         L     R15,52(,R2)         GET ADDRESS OF ECT DESC
         L     R15,0(,R15)         GET ADDRESS OF ECT
         ST    R15,GCPPL+CPPLECT-CPPL PUT IN CPPL
         EJECT
***********************************************************************
*                                                                     *
*       GET ASID FOR LATER.                                           *
*                                                                     *
***********************************************************************
         L     R15,PSAANEW-PSA     GET ADDRESS OF MY ASCB
         UNPK  WORKAREA(3),ASCBASID+1-ASCB(2,R15) UNPACK
         TR    WORKAREA(2),TRTABLE-240 CONVERT TO EBCDIC
         MVC   GASID,WORKAREA      COPY TO DYNAMIC AREA
         MVC   GTIMRWTO+76(2),GASID MOVE ASID TO TIMER WTO
         EJECT
***********************************************************************
*                                                                     *
*       GET SYSTEM SMF ID FOR LATER                                   *
*                                                                     *
***********************************************************************
         L     R15,CVTPTR
         USING CVT,R15
         L     R15,CVTSMCA         POINT TO SMCA
         USING SMCABASE,R15        ####
         MVC   GSID,SMCASID        SAVE SYSTEM ID FOR LATER
         DROP  R15
         EJECT
***********************************************************************
*                                                                     *
*       CLEAR TERMINAL BUFFERS.                                       *
*                                                                     *
***********************************************************************
         TCLEARQ INPUT             CLEAR TERMINAL BUFFERS
         GTSIZE ,                  DETERMINE TERMINAL TYPE
         LTR   R0,R0               WAS THIS A DISPLAY
         BZ    HSKPEXIT            NO - CONTINUE
         OI    GBITS2,GDISPLAY     INDICATE DISPLAY TERMINAL
         EJECT
***********************************************************************
*                                                                     *
*       RETURNS TO THE MAINLINE.                                      *
*                                                                     *
***********************************************************************
HSKPEXIT DS    0H
         L     R14,HSKPSAVE        RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT ESTAE RETURN CODE WAS NOT ZERO AND DENY      *
*       LOGON.                                                        *
*                                                                     *
***********************************************************************
HSKPNSTA DS    0H                  NON-ZERO RETURN CODE FROM ESTAE
         LR    R2,R15              PROTECT RETURN CODE
         MVC   GESTAMSG(L'ESTAEMSG),ESTAEMSG COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GESTAMSG+24(4),WORKAREA+1 MOVE INTO MESSAGE
         LA    R0,L'ESTAEMSG       GET LENGTH OF MESSAGE
         LA    R1,GESTAMSG         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
         LR    R15,R2              RESTORE RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON
         B     HSKPEXIT            EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT STAX RETURN CODE WAS NOT ZERO AND DENY       *
*       LOGON.                                                        *
*                                                                     *
***********************************************************************
HSKPNSTX DS    0H                  NON-ZERO RETURN CODE FROM STAX
         LR    R2,R15              PROTECT RETURN CODE
         MVC   GSTAXMSG(L'STAXMSG),STAXMSG COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GSTAXMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE
         LA    R0,L'STAXMSG        GET LENGTH OF MESSAGE
         LA    R1,GSTAXMSG         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
         LR    R15,R2              RESTORE RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON
         B     HSKPEXIT            EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT USERID IN USE AND DENY LOGON.                *
*                                                                     *
***********************************************************************
HSKPDUPU DS    0H
         LA    R0,L'DUPUSER        GET LENGTH OF MESSAGE
         LA    R1,DUPUSER          POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           INFORM USER
         OI    GBITS1,GDENY        DENY LOGON
         LA    R15,4               SET RETURN CODE
         B     HSKPEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT LOGON HAD A RESOURCE FAILURE AND DENY LOGON. *
*                                                                     *
***********************************************************************
HSKPRESC DS    0H
         LA    R0,L'RESCFAIL       GET LENGTH OF MESSAGE
         LA    R1,RESCFAIL         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           INFORM USER
         OI    GBITS1,GDENY        DENY LOGON
         LA    R15,4               SET RETURN CODE
         B     HSKPEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT LOGON HAD AN ABEND AND DENY LOGON.           *
*                                                                     *
***********************************************************************
HSKPABND DS    0H
         LA    R0,L'LOGABEND       GET LENGTH OF MESSAGE
         LA    R1,LOGABEND         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           INFORM USER
         OI    GBITS1,GDENY        DENY LOGON
         LA    R15,4               SET RETURN CODE
         B     HSKPEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT OPEN FAILED AND DENY LOGON.                  *
*                                                                     *
***********************************************************************
HSKPNOPN DS    0H                  SYS1.UADS DID NOT OPEN
         LA    R0,L'OPENMSG        GET LENGTH OF MESSAGE
         LA    R1,OPENMSG          POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R15,4               SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON
         B     HSKPEXIT            EXIT
         TITLE 'SCAN COMMAND BUFFER'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    SCAN                                                             *
*                                                                     *
* FUNCTION -                                                          *
*    SCAN INPUT BUFFER.                                               *
*                                                                     *
* OPERATION -                                                         *
*    1) BUILDS CSPL AND CALLS IKJSCAN TO SCAN INPUT BUFFER.           *
*    2) IF SCAN IS UNSUCCESSFUL, TELLS TSO USER AND DENIES LOGON.     *
*    3) IF COMMAND IS NOT 'LOGON', TELLS TSO USER AND DENIES LOGON.   *
*    4) RETURNS TO THE MAINLINE.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    SCAN                                                             *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    IKJSCAN                                                          *
*                                                                     *
* INPUT -                                                             *
*    COMMAND BUFFER.                                                  *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    'IKJSCAN UNSUCCESSFUL - RC=NNNN'                                 *
*    'COMMAND IS NOT 'LOGON''                                         *
*                                                                     *
***********************************************************************
SCAN     DS    0H
         ST    R14,SCANSAVE        SAVE RETURN ADDRESS
         ST    R2,SCANSAVE+4       SAVE R2
         EJECT
***********************************************************************
*                                                                     *
*       BUILDS CSPL AND CALL IKJSCAN TO SCAN INPUT BUFFER.            *
*                                                                     *
***********************************************************************
         LA    R1,GIKJCSPL         GET ADDRESS OF CSPL
         USING CSPL,R1             ESTABLISH ADDRESSABILITY TO CSPL
         MVC   CSPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDRESS
         MVC   CSPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDRESS
         LA    R15,GCPECB          GET ADDRESS OF CP ECB
         ST    R15,CSPLECB         STORE CP ECB ADDRESS
         XC    GCPECB,GCPECB       CLEAR CP ECB
         LA    R15,GCSPLFLG        GET ADDRESS OF FLAGS
         ST    R15,CSPLFLG         STORE FLAG ADDRESS
         XC    GCSPLFLG,GCSPLFLG   CLEAR FLAGS - REQUEST SYNTAX CHECK
         LA    R15,GCSPLOA         GET ADDRESS OF OUTPUT AREA
         ST    R15,CSPLOA          STORE OUTPUT AREA ADDRESS
         XC    GCSPLOA,GCSPLOA     CLEAR OUTPUT AREA
         MVC   CSPLCBUF,GCPPL+CPPLCBUF-CPPL COPY COMMAND BUFFER ADDRESS
         L     R15,CSPLCBUF        GET COMMAND BUFFER ADDRESS
         USING CBUF,R15            GET CBUF ADDRESSABILITY
         XC    CBUFOFFS,CBUFOFFS   RESET OFFSET FOR SCAN
         CALLTSSR EP=IKJSCAN,MF=(E,(1)) CALL IKJSCAN
         EJECT
***********************************************************************
*                                                                     *
*       IF SCAN IS UNSUCCESSFUL, TELLS TSO USER AND DENIES LOGON.     *
*                                                                     *
***********************************************************************
         LTR   R15,R15             WAS SCAN SUCCESSFUL
         BNZ   SCANBSCN            NO - GO ISSUE MESSAGE
         LA    R1,GCSPLOA          YES - GET PTR TO SCAN OUTPUT AREA
         USING CSOA,R1             GET ADDRESSABILITY TO OUTPUT AREA
         EJECT
***********************************************************************
*                                                                     *
*       IF COMMAND IS NOT 'LOGON', TELLS TSO USER AND DENIES LOGON.   *
*                                                                     *
***********************************************************************
         TM    CSOAFLG,X'20'       WAS THE BUFFER EMPTY?
         BO    SCANEMTY            /YES - EXPLAIN THAT THE BUFFER EMPTY
         TM    CSOAFLG,CSOAVWP+CSOAVNP VALID SYNTAX WITH/WITHOUT PARMS
         BZ    SCANBCMD            NO - BAD COMMAND
         CLI   CSOALNM+1,5         YES - IS COMMAND LENGTH 5 BYTES
         BNE   SCANLOGF            NO - MAY BE 'LOGOFF'
         L     R1,CSOACNM          YES - GET ADDRESS OF COMMAND NAME
         CLC   KLOGON,0(R1)        IS COMMAND NAME 'LOGON'
         BNE   SCANBCMD            NO - BAD COMMAND
         SLR   R15,R15             YES - SET RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       RETURNS TO THE MAINLINE.                                      *
*                                                                     *
***********************************************************************
SCANEXIT DS    0H
         L     R14,SCANSAVE        RESTORE RETURN ADDRESS
         L     R2,SCANSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT IKJSCAN RETURN CODE WAS NOT ZERO AND DENY    *
*       LOGON.                                                        *
*                                                                     *
***********************************************************************
SCANBSCN DS    0H                  NON-ZERO RETURN CODE FROM IKJSCAN
         LR    R2,R15              PROTECT RETURN CODE
         MVC   GSCANMSG(L'SCANMSG),SCANMSG COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GSCANMSG+26(4),WORKAREA+1 MOVE INTO MESSAGE
         LA    R0,L'SCANMSG        GET LENGTH OF MESSAGE
         LA    R1,GSCANMSG         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
         LR    R15,R2              RESTORE RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON
         B     SCANEXIT            EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT COMMAND BUFFER WAS EMPTY                     *
*                                                                     *
***********************************************************************
SCANEMTY DS    0H
         TM    GBITS1,GATTNHIT     HAS ATTENTION BEEN HIT?
         BZ    SCANERET            /NO - RETURN
         LA    R0,L'ECMNDMSG       GET LENGTH OF MESSAGE
         LA    R1,ECMNDMSG         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
SCANERET DS    0H
         LA    R15,20              SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON
         B     SCANEXIT            EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT COMMAND WAS NOT 'LOGON' AND DENY LOGON.      *
*                                                                     *
***********************************************************************
SCANBCMD DS    0H                  COMMAND IS NOT 'LOGON'
         TM    GBITS1,GATTNHIT     HAS ATTENTION BEEN HIT?
         BZ    SCANBRET            /NO - RETURN
         LA    R0,L'BCMNDMSG       GET LENGTH OF MESSAGE
         LA    R1,BCMNDMSG         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
SCANBRET DS    0H
         LA    R15,16              SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON
         B     SCANEXIT            EXIT
         EJECT
***********************************************************************
*                                                                     *
*       IF COMMAND WAS 'LOGOFF', DENY LOGON WITHOUT MESSAGE TO USER.  *
*                                                                     *
***********************************************************************
SCANLOGF DS    0H
         TM    GBITS1,GATTNHIT     WAS ATTENTION HIT
         BZ    SCANBCMD            NO - CONTINUE
         CLI   CSOALNM+1,6         YES - IS COMMAND LENGTH 6 BYTES
         BNE   SCANBCMD            NO - BAD COMMAND
         L     R1,CSOACNM          YES - GET ADDRESS OF COMMAND NAME
         CLC   KLOGOFF,0(R1)       IS COMMAND NAME 'LOGOFF'
         BNE   SCANBCMD            NO - BAD COMMAND
         B     SCANBRET            YES - DENY LOGON WITHOUT MESSAGE
         TITLE 'CLEAR SCREEN AND WELCOME USER'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    WELCOME                                                          *
*                                                                     *
* FUNCTION -                                                          *
*    CLEAR THE SCREEN FOR DISPLAY TERMINALS, AND ISSUE THE WELCOME    *
*                                                                     *
* OPERATION -                                                         *
*    1) TEST FOR DISPLAY TERMINAL AND CLEAR THE SCREEN                *
*    2) ISSUE THE WELCOME MESSAGE                                     *
*    3) RETURNS TO THE MAINLINE.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    WELCOME                                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*                                                                     *
* INPUT -                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*                                                                     *
***********************************************************************
         SPACE 1
WELCOME  DS    0H
         SPACE 1
***********************************************************************
*                                                                     *
*       IF A DISPLAY TERMINAL, CLEAR THE SCREEN.                      *
*       PUT OUT A WELCOME MESSAGE INDICATING TERMINAL AND SYSTEM      *
*                                                                     *
***********************************************************************
         ST    R14,WELCSAVE        SAVE THE RETURN ADDRESS
         TM    GBITS2,GDISPLAY
         BNO   WELCMSG
         STLINENO LINE=1,MODE=OFF  CLEAR SCREEN (VTAM WAY)
         LTR   R15,R15             WAS EVERYTHING PROPER?
         BZ    WELCMSG             /YES - CONTINUE
         CH    R15,HALF8           IS THIS NOT A VTAM TERMINAL?
         BNE   WELCERR1            /NO - IT IS A VTAM ERROR
         TPUT  KMSG,KMSGL,FULLSCR,,HOLD  CLEAR SCREEN (TCAM WAY)
         B     WELCMSG
WELCERR1 DS    0H
         DC    X'0000'             ABEND   0C1
WELCMSG  DS    0H
         MVC   GWELCMSG,WELCOMSG
         MVC   GWELCMSG+25(4),GSID
         MVC   GWELCMSG+40(8),GTERMID
         LA    R1,GWELCMSG
         LA    R0,L'GWELCMSG
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R
         L     R14,WELCSAVE
         SR    R15,R15
         BR    R14
         TITLE 'PARSE COMMAND BUFFER'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    PARSE                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    PARSE INPUT BUFFER.                                              *
*                                                                     *
* OPERATION -                                                         *
*    1) BUILDS PPL AND CALLS IKJPARS TO PARSE INPUT BUFFER.           *
*    2) IF PARSE IS UNSUCCESSFUL AND IKJPARS ISSUED MESSAGE, DENIES   *
*       LOGON.  IF IKJPARS DID NOT ISSUE MESSAGE, CALLS GNRLFAIL TO   *
*       ISSUE MESSAGE.  IF GNRLFAIL IS UNSUCCESSFUL, TELLS TSO USER   *
*       THAT GNRLFAIL HAD A NOT-ZERO RETURN CODE. IN ALL OF THE ABOVE *
*       CASES, DENIES LOGON UNLESS ATTENTION HIT.                     *
*    3) RETURNS TO THE MAINLINE.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    PARSE                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    IKJPARS                                                          *
*    IKJEFF19 (GNRLFAIL)                                              *
*                                                                     *
* INPUT -                                                             *
*    COMMAND BUFFER.                                                  *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    'GNRLFAIL UNSUCCESSFUL - RC=NNNN'                                *
*                                                                     *
***********************************************************************
PARSE    DS    0H
         ST    R14,PARSSAVE        SAVE RETURN ADDRESS
         ST    R2,PARSSAVE+4       SAVE R2
         EJECT
***********************************************************************
*                                                                     *
*       BUILDS PPL AND CALLS IKJPARS TO PARSE INPUT BUFFER.           *
*                                                                     *
***********************************************************************
         LA    R1,GIKJPPL          GET ADDRESS OF PPL
         USING PPL,R1              ESTABLISH ADDRESSABILITY TO PPL
         MVC   PPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDRESS
         MVC   PPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDRESS
         LA    R15,GCPECB          GET ADDRESS OF CP ECB
         ST    R15,PPLECB          STORE CP ECB ADDRESS
         XC    GCPECB,GCPECB       CLEAR CP ECB
         MVC   PPLPCL,PCLADDR      STORE PCL ADDRESS
         LA    R15,GPPLANS         GET ADDRESS OF PDL
         ST    R15,PPLANS          STORE PDL ADDRESS
         MVC   PPLCBUF,GCPPL+CPPLCBUF-CPPL COPY COMMAND BUFFER ADDRESS
         LA    R15,ESTAEPRM        GET ADDRESS OF BASES FOR VAL CHK RTN
         ST    R15,PPLUWA          SAVE FOR VALIDITY CHK ROUTINES
         CALLTSSR EP=IKJPARS,MF=(E,GIKJPPL) CALL IKJPARS
         LTR   R15,R15             WAS PARSE SUCCESSFUL
         BZ    PARSEXIT            YES - RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       IF PARSE IS UNSUCCESSFUL AND IKJPARS ISSUED MESSAGE, DENIES   *
*       LOGON.  IF IKJPARS DID NOT ISSUE MESSAGE, CALLS GNRLFAIL TO   *
*       ISSUE MESSAGE.  IF GNRLFAIL IS UNSUCCESSFUL, TELLS TSO USER   *
*       THAT GNRLFAIL HAD A NOT-ZERO RETURN CODE. IN ALL OF THE ABOVE *
*       CASES, DENIES LOGON UNLESS ATTENTION HIT.                     *
*                                                                     *
***********************************************************************
         TM    GBITS1,GATTNHIT     WAS ATTENTION HIT
         BO    PARSATTN            YES - DONT SET DENY BIT
         OI    GBITS1,GDENY        NO - DENY LOGON
PARSATTN DS    0H
         LA    R14,4               GET 4 IN R14 FOR COMPARISON
         CR    R14,R15             DID PARSE ISSUE MESSAGE
         BE    PARSEXIT            YES - QUIT
         LA    R14,8               NO - GET 8 IN R14
         CR    R14,R15             DID PARSE GET AN ATTENTION KEY
         BE    PARSEXIT            YES - QUIT
         LA    R14,20              NO - GET 20 IN R14
         CR    R14,R15             DID VALIDITY CHECKING ISSUE MSG
         BE    PARSEXIT            YES - QUIT
         LR    R2,R15              NO - PROTECT PARSE RETURN CODE
         XC    GGFPARMS,GGFPARMS   CLEAR GNRLFAIL PARAMETER LIST
         LA    R1,GGFPARMS         POINT TO GNRLFAIL PARM LIST ADDR
         ST    R1,GGFPARMP         STORE IN GNRLFAIL PARM LIST POINTER
         USING GFDSECTD,R1         ESTABLISH ADDRESSABILITY TO GF PARMS
         ST    R15,GFRCODE         STORE PARSE RETURN CODE
         LA    R15,GFPARSE         LOAD CALLER ID FOR PARSE
         STH   R15,GFCALLID        STORE CALLER ID
         LA    R15,GCPPL           GET ADDRESS OF CPPL
         ST    R15,GFCPPLP         STORE POINTER TO CPPL
         LA    R15,GCPECB          GET ADDRESS OF CP ECB
         ST    R15,GFECBP          STORE CP ECB POINTER
         XC    GCPECB,GCPECB       CLEAR CP ECB
         LINK  EP=IKJEFF19,MF=(E,GGFPARMP) LINK TO GNRLFAIL
         LTR   R15,R15             WAS GNRLFAIL SUCCESSFUL
         LR    R15,R2              RESTORE PARSE RETURN CODE
         BZ    PARSEXIT            YES - EXIT
         LR    R2,R15              NO - PROTECT GNRLFAIL RETURN CODE
         MVC   GGFALMSG(L'GFAILMSG),GFAILMSG COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GGFALMSG+27(4),WORKAREA+1 MOVE INTO MESSAGE
         LA    R0,L'GFAILMSG       GET LENGTH OF MESSAGE
         LA    R1,GGFALMSG         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
         LR    R15,R2              RESTORE GNRLFAIL RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       RETURNS TO THE MAINLINE.                                      *
*                                                                     *
***********************************************************************
PARSEXIT DS    0H
         L     R14,PARSSAVE        RESTORE R14
         L     R2,PARSSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         TITLE 'GET DATA FROM PDL ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    GETDATA                                                          *
*                                                                     *
* FUNCTION -                                                          *
*    RETRIEVE DATA RETURNED BY IKJPARS.                               *
*                                                                     *
* OPERATION -                                                         *
*    1) CHECKS TO SEE IF 'RECONNECT' WAS ENTERED.  IF SO, SETS THE    *
*       RECONNECT BIT.                                                *
*    2) CHECKS TO SEE IF 'NOMAIL' WAS ENTERED.  IF SO, SETS THE       *
*       NOMAIL BIT.                                                   *
*    3) CHECKS TO SEE IF 'NONOTICES' WAS ENTERED.  IF SO, SETS THE    *
*       NONOTICES BIT.                                                *
*    4) CHECKS TO SEE IF 'MSGCLASS' WAS ENTERED.  IF SO, MOVES        *
*       MSGCLASS FROM PDE TO DYNAMIC AREA.                            *
*    5) CHECKS TO SEE IF 'SIZE' WAS ENTERED.  IF SO, MOVES SIZE FROM  *
*       PDE TO DYNAMIC AREA.                                          *
*    6) CHECKS TO SEE IF USER IS CURRENTLY LOGGED ON.  IF SO, SETS THE*
*       DUPLICATE USER BIT.                                           *
*    7) CHECKS TO ENSURE THAT THE RECONNECT AND DUPLICATE USER BITS   *
*       AGREE.  IF NOT, ISSUES MESSAGE TO THE TSO USER AND DENIES     *
*       THE LOGON.                                                    *
*    8) RELEASE STORAGE FOR PDL AND RETURN TO THE MAINLINE.           *
*                                                                     *
* ENTRY POINTS -                                                      *
*    GETDATA                                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    PDL RETURNED FROM IKJPARS.                                       *
*                                                                     *
* OUTPUT -                                                            *
*    RECONNECT, NOMAIL, NONOTICES AND JCL BIT(S) SET AS INDICATED.    *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
GETDATA  DS    0H
         ST    R14,GDATSAVE        SAVE RETURN ADDRESS
         ST    R2,GDATSAVE+4       SAVE R2
         L     R2,GPPLANS          GET ADDRESS OF PDL ADDRESS
         USING IKJPARMD,R2         GET ADDRESSABILITY TO PDL
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'RECONNECT' WAS ENTERED.  IF SO, SETS THE    *
*       RECONNECT BIT.                                                *
*                                                                     *
***********************************************************************
         CLI   PRECON+1,0          WAS 'RECONNECT' SPECIFIED
         BE    GDATMAIL            NO - GO TRY 'MAIL'
         OI    GBITS1,GRECON       YES - SET BIT ON
         B     GDATCHKR            SKIP DOWN TO CHECK FOR DUPLICATE
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'NOMAIL' WAS ENTERED.  IF SO, SETS THE       *
*       NOMAIL BIT.                                                   *
*                                                                     *
***********************************************************************
GDATMAIL DS    0H
         CLI   PMAIL+1,2           WAS 'NOMAIL' SPCEIFIED
         BNE   GDATNOTS            NO - GO TRY 'NOTICES'
         OI    GBITS1,GNOMAIL      YES - SET BIT ON
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'NONOTICES' WAS ENTERED.  IF SO, SETS THE    *
*       NONOTICES BIT.                                                *
*                                                                     *
***********************************************************************
GDATNOTS DS    0H
         CLI   PNOTICES+1,2        WAS 'NONOTICES' SPECIFIED
         BNE   GDATMSGC            NO - GO TRY MSGCLASS
         OI    GBITS1,GNONOT       YES - SET BIT ON
         EJECT
GDATMSGC DS    0H
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'MSGCLASS' WAS ENTERED.  IF SO, MOVES        *
*       MSGCLASS FROM PDE TO DYNAMIC AREA.                            *
*                                                                     *
***********************************************************************
         CLI   PMSGCLAS+1,0        WAS 'MSGCLASS' SPECIFIED
         BE    GDATSIZE            NO - CHECK SIZE
         L     R1,SMSGC            YES - GET ADDRESS OF MSGCLASS
         MVC   GMSGCLAS,0(R1)      MOVE MSGCLASS TO DYNAMIC AREA
         EJECT
GDATSIZE DS    0H
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'SIZE' WAS ENTERED.  IF SO, MOVES SIZE       *
*       FROM PDE TO DYNAMIC AREA.                                     *
*                                                                     *
***********************************************************************
         CLI   PSIZE+1,0           WAS 'SIZE' SPECIFIED
         BE    GDATCHKR            NO - CONTINUE
         L     R1,SSIZE            YES - GET ADDRESS OF SIZE
         MVC   GSIZE,0(R1)         MOVE SIZE TO DYNAMIC AREA
         EJECT
GDATCHKU DS    0H
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF USER HAS ALREADY BEEN LOGGED ON.  IF SO      *
*       SET THE DUPLICATE USER BIT.                                   *
*                                                                     *
***********************************************************************
         MVC   GENQLIST,ENQLIST    SETUP THE ENQ LIST
         MVC   GENQLIST+1,LUSERID+1   PUT LENGTH OF USERID IN LIST
         LA    R15,QNAME           PUT IN ADDRESS OF QNAME (SYSIKJUA)
         ST    R15,GENQLIST+4
         LA    R15,GUSERID         PUT IN ADDRESS OF RNAME (USERID)
         ST    R15,GENQLIST+8
         LA    R1,GENQLIST         PUT IN ADDRESS OF RNAME (USERID)
*        ENQ   MF=(E,(1))          DO THE ENQ
*        LTR   R15,R15             IS THE USER LOGGED ON?
*        BZ    GDATCHKR            /NO - CHECK RECONNECT AND DUP BITS
*        OI    GBITS1,GDUPUSER     TURN ON DUPLICATE USER BIT
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO ENSURE THAT THE RECONNECT AND DUPLICATE USER BITS   *
*       AGREE.  IF NOT, ISSUES MESSAGE TO THE TSO USER AND DENIES     *
*       THE LOGON.                                                    *
*                                                                     *
***********************************************************************
GDATCHKR DS    0H
*        TM    GBITS1,GRECON+GDUPUSER PROPER COMBINATION OF BITS
*        BM    GDATERR             NO - GO TO ERROR ROUTINE
         B     GDATRTRN            YES - CONTINUE
GDATERR  DS    0H
         TM    GBITS1,GRECON       WAS RECONNECT SPECIFIED
         BO    GDATRCON            YES - GO ISSUE THAT MESSAGE
         LA    R0,L'DUPUSER        NO - GET LENGTH OF MESSAGE
         LA    R1,DUPUSER          POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R15,4               SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON
         B     GDATEXIT            AND RETURN TO CALLER
GDATRCON DS    0H                  RECONNECT SPECIFIED BUT USER NOT ON
         LA    R0,L'BADRECON       GET LENGTH OF MESSAGE
         LA    R1,BADRECON         POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R15,4               SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON
         B     GDATEXIT            AND RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       RELEASE STORAGE FOR PDL AND RETURN TO THE MAINLINE.           *
*                                                                     *
***********************************************************************
GDATRTRN DS    0H
         SLR   R15,R15             SET RETURN CODE
GDATEXIT DS    0H
         LR    R2,R15              PROTECT RETURN CODE
         LA    R1,GPPLANS          GET ADDRESS OF PDL
         IKJRLSA (1)               FREE PDL
         LR    R15,R2              RESTORE RETURN CODE
         L     R14,GDATSAVE        RESTORE RETURN ADDRESS
         L     R2,GDATSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         TITLE 'VERIFY DATA ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VERDATA                                                          *
*                                                                     *
* FUNCTION -                                                          *
*    VERIFY DATA RETURNED BY IKJPARS.                                 *
*                                                                     *
* OPERATION -                                                         *
*    1) AT THIS POINT A PASSWORD BLOCK HAS BEEN DETERMINED. (THE FIRST*
*       PASSWORD BLOCK IF RACF IS ACTIVE, OR THE MATCHING PASSWORD    *
*       BLOCK IF RACF IS INACTIVE.  IF AN ACCOUNT NUMBER HAS BEEN     *
*       SPECIFIED, IT HAS BEEN VERIFIED.  IF ONLY ONE ACCOUNT NUMBER  *
*       EXISTS FOR THIS PASSWORD, IT IS USED, ELSE AN ACCOUNT NUMBER  *
*       IS REQUESTED OF THE USER.  THE ENTERED ACCOUNT NUMBER IS      *
*       CHECKED AGAINST THE VALID ACCOUNT NUMBERS FOR THIS PASSWORD.  *
*       IF AFTER THREE PROMPTS FOR A VALID ACCOUNT NUMBER, THE USER   *
*       HAS FAILED TO PROVIDE ONE, LOGON IS DENIED.                   *
*    2) WHEN A PROPER ACCOUNT NUMBER HAS BEEN DEFINED, THAT ACCOUNT   *
*       OFFSET BLOCK IS USED AS A BASE FROM WHICH TO VERIFY THE PROC. *
*       IF ONLY ONE PROC EXISTS, IT IS USED.  IF NO PROC WAS PROVIDED *
*       BY THE USER, OR THE PROVIDED PROC DOES NOT MATCH ANY VALID    *
*       PROC FOR THIS ACCOUNT, THE USER IS PROMPTED TO PROVIDE A VALID*
*       PROC.  IF AFTER THREE PROMPTS FOR A VALID PROC NAME, THE      *
*       USER HAS FAILED TO PROVIDE ONE, LOGON IS DENIED.  WHEN THE    *
*       ACCOUNT NUMBER HAS BEEN DETERMINED AND THE PROC HAS BEEN      *
*       DETERMINED, THE DEFAULT SIZE MAY BE USED IF A SIZE HAS NOT    *
*       BEEN PROVIDED.                                                *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VERDATA                                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    GACCTNUM RETURNED BY IKJPARS AND THE UADS DATA BLOCKS READ BY    *
*    VUSERID.                                                         *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS WILL*
*    BE AS FOLLOWS -                                                  *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  ACCOUNT NUMBER GOOD.    CONTINUE WITH LOGON.        *
*       --------------------------------------------------------  *
*          4  USER FAILED TO ENTER    LOGON IS DENIED.            *
*             GOOD ACCOUNT NUMBER IN                              *
*             3 TRIES.                                            *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*                                                                     *
***********************************************************************
VERDATA  DS    0H
         ST    R14,VDATSAVE        SAVE RETURN ADDRESS
         ST    R2,VDATSAVE+4       SAVE R2
         L     R2,ADDRUBUF         GET ADDRESS OF SYS1.UADS BUFFER
         EJECT
***********************************************************************
*                                                                     *
*       IF NO ACCOUNT NUMBER WAS ENTERED AND ONLY ONE ACCOUNT NUMBER  *
*       IS DEFINED FOR THIS PASSWORD, ASSUME THAT IS THE ACCOUNT      *
*       NUMBER FOR THIS LOGON.  IF THERE IS MORE THAN ONE ACCOUNT     *
*       NUMBER DEFINED FOR THIS PASSWORD AND ONE WAS NOT SPECIFIED,   *
*       PROMPT THE USER FOR AN ACCOUNT NUMBER.  IF AFTER THREE (3)    *
*       PROMPTS THE USER HAS NOT ENTERED A VALID ACCOUNT NUMBER, THE  *
*       USER IS INFORMED, AND THE LOGON IS DENIED.                    *
*                                                                     *
***********************************************************************
         MVI   GCOUNTER,0                RESET PROMPT COUNTER
         L     R15,PASSOFF         NO - GET OFFSET TO PSWD OFFSET BLOCK
         L     R15,4(R2,R15)          GET OFFSET OF ACCT # OFFSET BLOCK
         AR    R15,R2                GET ADDRESS OF ACCT # OFFSET BLOCK
         ST    R15,GFRSTACT          SAVE ADDR OF FIRST ACCT OFFSET BLK
         CLI   GACCTNUM,0             HAS ACCOUNT NUMBER BEEN PROVIDED?
         BNE   VDATAHIT                  /YES - CHECK THE PROC
         TM    0(R15),KBIT0              IS THIS THE ONLY ACCT?
         BO    VDAT1ACT                  /YES - USE THIS ACCOUNT
ACCTASK  DS    0H
         IC    R15,GCOUNTER              INCREMENT COUNTER
         LA    R15,1(,R15)
         STC   R15,GCOUNTER
         CLI   GCOUNTER,4                HAS USER HAD THREE CHANCES?
         BNE   VDATAASK                  /NO - ASK FOR ACCT AGAIN
         OI    GBITS1,GDENY              DENY LOGON
         B     VDATEXIT                  EXIT
VDATAASK DS    0H
         MVC   GIKJPGPB(LPGPB),LPUTGET   MOVE LIST TO WORK AREA
         LA    R1,GIKJIOPL               GET ADDRESS OF IOPL
         USING IOPL,R1                   #### USING IOPL
         MVC   IOPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDR TO IOPL
         MVC   IOPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDR TO IOPL
         LA    R15,GCPECB                GET ADDR OF CP ECB
         ST    R15,IOPLECB               PUT IT INTO IOPL
         XC    GCPECB,GCPECB             ZERO ECB
         LA    R15,GIKJPGPB              GET ADDR OF PUTGET PARM LIST
         ST    R15,IOPLIOPB              STORE IT IN IOPL
         XC    GPGOLD(4),GPGOLD          ZERO NUMBER OF SEGMENTS
         MVI   GPGOLD+3,1                SET NUMBER TO 1
         LA    R15,ACCTHDR               GET ADDR OF PSWD MSG HEADER
         ST    R15,GPGOLD+4              SAVE THAT ADDRESS
         PUTGET OUTPUT=(GPGOLD,,PROMPT),MF=(E,GIKJIOPL)
         LTR   R15,R15                   WAS PUTGET SUCCESSFUL?
         BNZ   VDATAER1                  NO THERE IS AN ERROR
         LA    R1,GIKJPGPB               USE ADDR OF PUTGET PARM LIST
         USING PGPB,R1                   #### USING PGPB
         L     R1,PGPBIBUF               GET ADDR OF INPUT BUFFER
         DROP  R1                        #### DROP R1
         LR    R14,R2                    SAVE REGISTER 2
         LR    R2,R1                     SAVE REGISTER 1 FOR FREEMAIN
         CLI   0(R2),44                  WAS RESPONSE TO LONG?
         BH    FREEBUF3                  /YES - FREE INPUT BUFFER
         CLI   1(R2),5                   WAS RESPONSE TO SHORT?
         BL    FREEBUF3                  /YES - FREE INPUT BUFFER
         MVC   GACCTNUM,BLANKS
         SR    R15,R15
         IC    R15,1(,R2)                GET INPUT LENGTH
         SH    R15,HALF5                 SUBTRACT LENGTH OF HEADER + 1
         LA    R2,4(,R2)                 GET ADDR OF INPUT BUFFER
         LA    R4,GACCTNUM               GET ADDR OF RECEIVING FIELD
         EX    R15,MOVEIT                MOVE INPUT TO RECEIVING FIELD
         OC    GACCTNUM,BLANKS
         LR    R2,R14                    RESTORE REGISTER 2
         LA    R0,1                      GET SUBPOOL NUMBER
         SLL   R0,24                     SHIFT TO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)          GET LENGTH
         FREEMAIN R,A=(1),LV=(0)         FREE THE INPUT BUFFER
         L     R15,GFRSTACT              GET ADDR OF FIRST ACCT OFFSET
         B     VDATACCT                  VERIFY ACCOUNT
FREEBUF3 DS    0H
         LR    R2,R14                    RESTORE REGISTER 2
         LA    R0,1                      GET SUBPOOL NUMBER
         SLL   R0,24                     SHIFT TO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)          GET LENGTH
         FREEMAIN R,A=(1),LV=(0)         FREE THE INPUT BUFFER
         B     VDATAASK                  REDISPLAY THE PUTGET
         EJECT
***********************************************************************
*                                                                     *
*       VERIFY ACCOUNT NUMBER ENTERED WITH THE ONES IN UADS           *
*                                                                     *
***********************************************************************
VDATACCT DS    0H
         L     R14,8(,R15)         GET OFFSET OF ACCT # DATA BLOCK
         AR    R14,R2              GET ADDRESS OF ACCT # DATA BLOCK
         SR    R3,R3
         IC    R3,44(,R14)         GET LENGTH OF ACCT NUMBER
         BCTR  R3,0
         LA    R4,GACCTNUM         POINT TO PROVIDED ACCOUNT NUMBER
         LR    R1,R2
         LA    R2,45(,R14)         POINT TO ACCT NUMBER IN UADS
         EX    R3,COMPARIT         COMPARE ACCOUNT NUMBERS
         BE    VDATACT2
         L     R2,ADDRUBUF
         TM    0(R15),KBIT0        IS THIS THE LAST ACCT # OFFSET BLK?
         BO    VDATACT3            /YES - ASK FOR ANOTHER ACCT #
         L     R15,0(,R15)         GET OFFSET OF NEXT ACCT OFFSET BLK
         AR    R15,R2              GET ADDRESS OF NEXT ACCT OFFSET BLK
         B     VDATACCT            CHECK NEXT ACCOUNT
VDATACT2 DS    0H
         LA    R3,1(,R3)
         STH   R3,LACCTNUM         SAVE ACCOUNT LENGTH
         L     R2,ADDRUBUF
         MVI   GCOUNTER,0          RESET PROMPT COUNTER
         ST    R15,GACTADDR        SAVE THE ACCT OFFSET BLK ADDRESS
         B     VDATAHIT
VDATACT3 DS    0H
         LA    R0,L'INVLDACT
         LA    R1,INVLDACT
         TPUT  (1),(0),R
         B     ACCTASK
         EJECT
***********************************************************************
*                                                                     *
*       MOVE ACCOUNT NUMBER TO GACCTNUM                               *
*                                                                     *
***********************************************************************
VDAT1ACT DS    0H
         L     R14,8(,R15)         GET OFFSET OF ACCT # DATA BLOCK
         AR    R14,R2              GET ADDRESS OF ACCT # DATA BLOCK
         SR    R3,R3
         IC    R3,44(,R14)
         BCTR  R3,0
         LA    R4,GACCTNUM
         LA    R2,45(,R14)
         EX    R3,MOVEIT           COMPARE ACCOUNT NUMBERS
         LA    R3,1(,R3)
         STH   R3,LACCTNUM         SAVE ACCOUNT LENGTH
         L     R2,ADDRUBUF
         MVI   GCOUNTER,0          RESET PROMPT COUNTER
         ST    R15,GACTADDR        SAVE THE ACCT OFFSET BLK ADDRESS
         B     VDATAHIT
         EJECT
***********************************************************************
*                                                                     *
*       IF THERE IS ONLY ONE PROC FOR THIS ACCOUNT NUMBER, ASSUME THAT*
*       IT IS THE PROC FOR THIS LOGON.  ELSE VERIFY THAT THE ENTERED  *
*       PROC IS ALLOWED.  IF THERE IS MORE THAN ONE PROC DEFINED FOR  *
*       THIS ACCOUNT NUMBER AND ONE WAS NOT SPECIFIED,  PROMPT THE    *
*       USER FOR A PROC NAME.  IF AFTER THREE (3) PROMPTS THE USER HAS*
*       NOT ENTERED A VALID PROC NAME, THE USER IS INFORMED, AND      *
*       THE LOGON IS DENIED.                                          *
*                                                                     *
***********************************************************************
VDATAHIT DS    0H
         L     R15,GACTADDR        SAVE THE ACCT OFFSET BLK ADDRESS
         L     R15,4(,R15)         GET OFFSET OF PROC NAME OFFSET BLK
         AR    R15,R2              GET ADDR OF PROC NAME OFFSET BLK
         ST    R15,GFRSTPRC        SAVE ADDR OF FIRST PROC OFFSET BLK
         TM    0(R15),KBIT0        IS THIS THE ONLY PROC?
         BO    VDATGOOD            /YES - USE THIS PROC
         CLI   GPROC,X'00'         WAS A PROC PROVIDED?
         BE    VPROCASK            /NO - ASK FOR THE PROC NAME
VDATLOOP DS    0H
         L     R14,8(,R15)         GET OFFSET OF PROC NAME DATA BLOCK
         AR    R14,R2              GET ADDRESS OF PROC NAME DATA BLOCK
         CLC   GPROC,4(R14)        DOES THIS MATCH THE PROVIDED PROC?
         BE    VDATPGD             /YES - THE PROC IS GOOD
         TM    0(R15),KBIT0        IS THIS THE LAST PROC OFFSET?
         BO    VDATPBAD            /YES - THE PROC MUST BE ENTERED
         L     R15,0(,R15)         GET OFFSET OF NEXT PROC OFFSET BLK
         AR    R15,R2              GET ADDR OF NEXT PROC OFFSET BLK
         B     VDATLOOP            GO THRU PROC LOOP AGAIN
VDATPBAD DS    0H
         LA    R0,L'INVLDPRC       GET LENGTH OF MESSAGE
         LA    R1,INVLDPRC         POINT TO MESSAGE
         TPUT  (1),(0),R
         EJECT
***********************************************************************
*                                                                     *
*       ASK FOR AN AUTHORIZED PROC NAME                               *
*                                                                     *
***********************************************************************
VPROCASK DS    0H
         IC    R15,GCOUNTER              INCREMENT COUNTER
         LA    R15,1(,R15)
         STC   R15,GCOUNTER
         CLI   GCOUNTER,4                HAS USER HAD THREE CHANCES?
         BNE   VDATPASK                  /NO - ASK FOR PROC AGAIN
         OI    GBITS1,GDENY              DENY LOGON
         B     VDATEXIT                  EXIT
VDATPASK DS    0H
         MVC   GIKJPGPB(LPGPB),LPUTGET   MOVE LIST TO WORK AREA
         LA    R1,GIKJIOPL               GET ADDRESS OF IOPL
         USING IOPL,R1                   #### USING IOPL
         MVC   IOPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDR TO IOPL
         MVC   IOPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDR TO IOPL
         LA    R15,GCPECB                GET ADDR OF CP ECB
         ST    R15,IOPLECB               PUT IT INTO IOPL
         XC    GCPECB,GCPECB             ZERO ECB
         LA    R15,GIKJPGPB              GET ADDR OF PUTGET PARM LIST
         ST    R15,IOPLIOPB              STORE IT IN IOPL
         XC    GPGOLD(4),GPGOLD          ZERO NUMBER OF SEGMENTS
         MVI   GPGOLD+3,1                SET NUMBER TO 1
         LA    R15,PROCHDR               GET ADDR OF PROC MSG HEADER
         ST    R15,GPGOLD+4              SAVE THAT ADDRESS
         PUTGET OUTPUT=(GPGOLD,,PROMPT),MF=(E,GIKJIOPL)
         LTR   R15,R15                   WAS PUTGET SUCCESSFUL?
         BNZ   VDATPER1                  NO THERE IS AN ERROR
         LA    R1,GIKJPGPB               USE ADDR OF PUTGET PARM LIST
         USING PGPB,R1                   #### USING PGPB
         L     R1,PGPBIBUF               GET ADDR OF INPUT BUFFER
         DROP  R1                        #### DROP R1
         LR    R14,R2                    SAVE REGISTER 2
         LR    R2,R1                     SAVE REGISTER 1 FOR FREEMAIN
         CLI   0(R2),12                  WAS RESPONSE TO LONG?
         BH    FREEBUF2                  /YES - FREE INPUT BUFFER
         CLI   1(R2),5                   WAS RESPONSE TO SHORT?
         BL    FREEBUF2                  /YES - FREE INPUT BUFFER
         MVC   GPROC,BLANKS
         SR    R15,R15
         IC    R15,1(,R2)                GET INPUT LENGTH
         SH    R15,HALF5                 SUBTRACT LENGTH OF HEADER + 1
         LA    R2,4(,R2)                 GET ADDR OF INPUT BUFFER
         LA    R4,GPROC                  GET ADDR OF RECEIVING FIELD
         EX    R15,MOVEIT                MOVE INPUT TO RECEIVING FIELD
         OC    GPROC,BLANKS
         LR    R2,R14                    RESTORE REGISTER 2
         LA    R0,1                      GET SUBPOOL NUMBER
         SLL   R0,24                     SHIFT TO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)          GET LENGTH
         FREEMAIN R,A=(1),LV=(0)         FREE THE INPUT BUFFER
         L     R15,GFRSTPRC              GET ADDR OF FIRST PROC OFFSET
         B     VDATLOOP                  VERIFY PROC
FREEBUF2 DS    0H
         LR    R2,R14                    RESTORE REGISTER 2
         LA    R0,1                      GET SUBPOOL NUMBER
         SLL   R0,24                     SHIFT TO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)          GET LENGTH
         FREEMAIN R,A=(1),LV=(0)         FREE THE INPUT BUFFER
         B     VDATPASK                  REDISPLAY THE PUTGET
VDATGOOD DS    0H
         L     R14,8(,R15)         GET OFFSET OF PROC NAME DATA BLOCK
         AR    R14,R2              GET ADDRESS OF PROC NAME DATA BLOCK
         MVC   GPROC,4(R14)        MOVE THE PROC TO OUR AREA
VDATPGD  DS    0H
         LA    R1,8
         STH   R1,LPROC
         SR    R15,R15             INDICATE EVERYTHING GREAT
         CLI   GSIZE,0             HAS SIZE BEEN SPECIFIED?
         BNE   VDATEXIT            /YES - EXIT
         SR    R2,R2               CLEAR THE REGISTER
         LH    R2,14(,R14)         LOAD THE DEFAULT SIZE INTO THE REG
         CVD   R2,TEMPPACK         CONVERT IT TO DECIMAL
         OI    TEMPPACK+7,X'0F'    DIDDLE THE SIGN
         UNPK  GSIZE,TEMPPACK      UNPACK INTO THE SIZE
         B     VDATEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO THE MAINLINE.                                       *
*                                                                     *
***********************************************************************
VDATAER1 DS    0H
         LA    R1,8
         CR    R1,R15              WAS ATTENTION HIT?
         BNE   VDATAER2            /NO - DENY LOGON
         TM    GBITS1,GATTNHIT     WAS A NEW COMMAND ENTERED?
         BNO   ACCTASK             /NO - ASK FOR ACCOUNT AGAIN
         B     VDATEXIT
VDATAER2 DS    0H
         OI    GBITS1,GDENY        DENY LOGON
         LA    R15,12              INDICATE ERROR
         B     VDATEXIT            EXIT
VDATPER1 DS    0H
         LA    R1,8
         CR    R1,R15              WAS ATTENTION HIT?
         BNE   VDATPER2            /NO - DENY LOGON
         TM    GBITS1,GATTNHIT     WAS A NEW COMMAND ENTERED?
         BNO   VDATPASK            /NO - ASK FOR PROC AGAIN
         B     VDATEXIT
VDATPER2 DS    0H
         OI    GBITS1,GDENY        DENY LOGON
         LA    R15,12              INDICATE ERROR
         B     VDATEXIT            EXIT
VDATEXIT DS    0H
         L     R14,VDATSAVE        RESTORE RETURN ADDRESS
         L     R2,VDATSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         TITLE 'BUILD JCL ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    BUILDJCL                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    1) MOVE MODEL JCL TO JCL IMAGES AREA.                            *
*    2) BUILD JOB CARD.                                               *
*    3) BUILD EXEC CARD.                                              *
*    4) SET LENGTH IN DOPE VECTOR                                     *
*    5) INDICATE JCL AND RETURN TO CALLER.                            *
*                                                                     *
* OPERATION -                                                         *
*                                                                     *
* ENTRY POINTS -                                                      *
*    BUILDJCL                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS     *
*    WILL BE AS FOLLOWS -                                             *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  NO ERRORS ENCOUNTERED.  CONTINUE WITH LOGON.        *
*       --------------------------------------------------------  *
*       NON-0 TPUT/TGET RETURN CODE.  DENY LOGON.                 *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*                                                                     *
***********************************************************************
BUILDJCL DS    0H
         ST    R14,BJCLSAVE        SAVE RETURN ADDRESS
         ST    R2,BJCLSAVE+4       SAVE R2
         ST    R3,BJCLSAVE+8       SAVE R3
         ST    R4,BJCLSAVE+12      SAVE R3
         EJECT
***********************************************************************
*                                                                     *
*       MOVE MODEL JCL TO JCL IMAGES AREA.                            *
*                                                                     *
***********************************************************************
         L     R15,ADDRPARM        GET ADDRESS OF ORIGINAL PARM
         L     R15,28(,R15)        GET ADDRESS OF JCL DESCRIPTOR
         L     R0,0(,R15)          GET ADDRESS OF JCL IMAGES
         LR    R3,R0               SAVE ADDRESS OF JCL IMAGES
         LH    R1,4(,R15)          GET LENGTH OF JCL IMAGES
         LA    R14,JCLMODEL        GET ADDRESS OF JCL MODEL
         L     R15,LJCLMODL        GET FILL CHARACTER AND LENGTH
         MVCL  R0,R14              MOVE MODEL JCL
         EJECT
***********************************************************************
*                                                                     *
*       BUILD JOB CARD.                                               *
*                                                                     *
***********************************************************************
         MVC   2(7,R3),GUSERID     MOVE IN USERID
         LA    R2,GACCTNUM         GET ADDRESS OF ACCOUNT NUMBER
         LA    R4,15(,R3)          POINT TO RECEIVING FIELD
         LH    R1,LACCTNUM         GET LENGTH OF ACCOUNT NUMBER
         BCTR  R1,0                CONVERT TO DISPLACEMENT
         EX    R1,MOVEIT           MOVE ACCOUNT NUMBER TO RECEIVING FLD
         LA    R4,1(R1,R4)         POINT PAST ACCOUNT NUMBER
         MVI   0(R4),C','          AND PUT IN THE COMMA
         MVI   1(R4),X'7D'         AND PUT A QUOTE MARK
         MVC   2(20,R4),GPRGRMR    POINT TO RECEIVING FIELD
         LA    R1,22(,R4)
BJCLPGMR DS    0H
         CLI   0(R1),C' '
         BNE   BJCLREGN
         BCT   R1,BJCLPGMR
BJCLREGN DS    0H
         MVI   1(R1),X'7D'         AND PUT A QUOTE MARK
         LA    R4,2(,R1)           POINT R4 PAST PROGRAMMER NAME
         MVC   0(L'REGIONEQ,R4),REGIONEQ MOVE IN REGION KEYWORD
         MVC   8(4,R4),GSIZE       MOVE IN REGION SIZE
         TRT   8(5,R4),NXTBLANK    FIND NEXT BLANK
         LR    R4,R1
         MVI   0(R4),C'K'
         CLI   GMSGCLAS,0          WAS 'MSGCLASS' SPECIFIED
         BE    BJCLGMCL            NO - CONTINUE
         MVC   1(L'MSGCLASS,R4),MSGCLASS YES - MOVE IN KEYWORD
         MVC   11(1,R4),GMSGCLAS   MOVE IN MSGCLASS
         LA    R4,12(,R4)          BUMP OVER MSGCLASS
BJCLGMCL DS    0H
         EJECT
***********************************************************************
*                                                                     *
*       BUILD EXEC CARD.                                              *
*                                                                     *
***********************************************************************
         LA    R3,80(,R3)          BUMP OVER TO EXEC CARD
         MVC   16(8,R3),GPROC      NO - USE PROC FROM UADS
         MVC   2(8,R3),16(R3)      COPY PROCNAME TO STEPNAME
         L     R1,ADDRPARM         GET ADDRESS OF ORIGINAL PARM
         L     R15,28(,R1)         GET ADDRESS OF JCL DESCRIPTOR
         MVI   7(R15),160          NO - INDICATE 2 JCL CARDS
         EJECT
***********************************************************************
*                                                                     *
*       INDICATE JCL AND RETURN TO CALLER.                            *
*                                                                     *
***********************************************************************
         L     R15,0(,R1)          GET ADDRESS OF CONTROL SW DESC
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCHES
         OI    0(R15),KBIT6        INDICATE JCL
BJCLRTRN DS    0H
         SLR   R15,R15             SET RETURN CODE
BJCLEXIT DS    0H
         L     R14,BJCLSAVE        RESTORE RETURN ADDRESS
         L     R2,BJCLSAVE+4       RESTORE R2
         L     R3,BJCLSAVE+8       RESTORE R3
         BR    R14                 RETURN TO CALLER
         TITLE 'ESTAE EXIT ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    ESTAEXIT                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER AN ABEND TO PASS CONTROL TO A RETRY ROUTINE.       *
*                                                                     *
* OPERATION -                                                         *
*    1) CHECKS TO SEE IF SDWA WAS OBTAINED.  IF NOT, POINT TO RETRY   *
*       ROUTINE, SAVE ABEND CODE AND RETURN TO ABEND.  IF SDWA WAS    *
*       OBTAINED, SAVE ABEND CODE AND ISSUE SETRP MACRO TO RETURN TO  *
*       ABEND SPECIFING RETRY ROUTINE.  IN EITHER CASE, DO NOT RETRY  *
*       IF THIS IS A RECURSIVE ABEND.                                 *
*                                                                     *
* ENTRY POINTS -                                                      *
*    ESTAEXIT                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    IF NO SDWA WAS OBTAINED REGISTER 1 CONTAINS THE ABEND CODE.      *
*    IF AN SDWA WAS OBTAINED REGISTER 1 CONTAINS THE ADDRESS OF THE   *
*    SDWA.                                                            *
*                                                                     *
* OUTPUT -                                                            *
*    ADDDRESS OF RETRY ROUTINE.                                       *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
ESTAEXIT DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         DROP  ,                   DROP MAIN LINE'S ADDRESSABILITY
         USING ESTAEXIT,R12        ESTABLISH EXIT ADDRESSABILITY
         USING GOTNAREA,R13        ESTABLISH DYNAMIC ADDRESSABILITY
         LR    R12,R15             LOAD BASE
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF SDWA WAS OBTAINED.  IF NOT, POINT TO RETRY   *
*       ROUTINE, SAVE ABEND CODE AND RETURN TO ABEND.  IF SDWA WAS    *
*       OBTAINED, SAVE ABEND CODE AND ISSUE SETRP MACRO TO RETURN TO  *
*       ABEND SPECIFING RETRY ROUTINE.  IN EITHER CASE, DO NOT RETRY  *
*       IF THIS IS A RECURSIVE ABEND.                                 *
*                                                                     *
***********************************************************************
         LA    R15,12              GET 12 IN R15
         CR    R0,R15              WAS SDWA OBTAINED
         BNE   HAVESDWA            YES - BRANCH
         LA    R0,ESTAERTY         NO - POINT TO RETRY ROUTINE
         L     R13,16(,R2)         GET DYNAMIC BASE FROM PARM LIST
         ST    R1,ABENDCDE         SAVE ABEND CODE
         XC    ABENDPSW,ABENDPSW   INDICATE PSW NOT AVAILABLE
         LA    R15,4               INDICATE RETRY ROUTINE SUPPLIED
         TM    GBITS2,GABEND       ABENDED BEFORE
         BO    NOSDWA2             YES - ABEND RECURSION
         OI    GBITS2,GABEND       NO - SET ABEND INDICATOR
         BR    R14                 RETURN TO CALLER
NOSDWA2  DS    0H
         SLR   R15,R15             INDICATE NO RETRY
         BR    R14                 RETURN TO CALLER
HAVESDWA DS    0H                  SDWA OBTAINED
         L     R2,0(,R1)           GET ADDRESS OF PARAM LIST FROM SDWA
         L     R13,16(,R2)         GET DYNAMIC BASE FROM PARM LIST
         MVC   ABENDCDE,4(R1)      SAVE ABEND CODE
         MVC   ABENDPSW,8(R1)      SAVE PSW
         ST    R14,12(,R13)        SAVE RETURN ADDRESS
         TM    GBITS2,GABEND       ABENDED BEFORE
         BO    HAVSDWA2            YES - ABEND RECURSION
         OI    GBITS2,GABEND       NO - SET ABEND INDICATOR
         SETRP REGS=(14),RC=4,RETADDR=ESTAERTY,FRESDWA=YES RETURN
HAVSDWA2 DS    0H
         SETRP REGS=(14),RC=0      DO NOT RETRY
         SPACE 3
         POP   USING               RETURN MAIN LINE'S ADDRESSABILITY
         TITLE 'ESTAE RETRY ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    ESTAERTY                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER AN ABEND TO RETRY.                                 *
*                                                                     *
* OPERATION -                                                         *
*    1) ISSUES MESSAGE TO TSO USER AND OPERATOR INDICATING TYPE OF    *
*       ABEND AND DENIES LOGON.  IF ABEND U0522, SKIP MESSAGES AND    *
*       SDUMP.                                                        *
*    2) IF APF AUTHORIZED, TAKE SDUMP.                                *
*                                                                     *
* ENTRY POINTS -                                                      *
*    ESTAERTY                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    ABENDCDE CONTAINS THE ABEND CODE PLACED THERE BY ESTAEXIT.       *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR MAINLINE.  THIS WILL BE *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*         16  LOGONXIT ABENDED.       DENY LOGON SWITCH IS SET.   *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'LOGON USER EXIT ABEND TNNNN'                                    *
*    'USER-ID ON TERMINAL LOGON USER EXIT ABEND TNNNN - ASID X'NN''   *
*                                                                     *
***********************************************************************
ESTAERTY DS    0H                  ESTAE RETRY ROUTINE
         L     R9,0(,R1)           RESTORE 1ST BASE FROM PARM LIST
         L     R10,4(,R1)          RESTORE 2ND BASE FROM PARM LIST
         L     R11,8(,R1)          RESTORE 3RD BASE FROM PARM LIST
         L     R12,12(,R1)         RESTORE 4TH BASE FROM PARM LIST
         L     R13,16(,R1)         RESTORE DYN BASE FROM PARM LIST
         EJECT
***********************************************************************
*                                                                     *
*       ISSUES MESSAGE TO TSO USER AND OPERATOR INDICATING TYPE OF    *
*       ABEND AND DENIES LOGON.  IF ABEND U0522, SKIP MESSAGES AND    *
*       SDUMP.                                                        *
*                                                                     *
***********************************************************************
         CLC   HALF522,ABENDCDE+2  WAS THIS U0522
         BE    NOSDUMP             YES - SKIP MESSAGES AND SDUMP
         MVC   GABNDMSG,ABNDMSG    NO - COPY ABEND MESSAGE
         L     R15,ABENDCDE        GET COMPLETION CODE
         SLL   R15,8               SHIFT OUT TCBFLGS1
         SRL   R15,20              AND SHIFT OUT USER ABEND CODE
         LTR   R15,R15             WAS IT SYSTEM ABEND CODE
         BZ    USERABND            NO - MUST BE USER ABEND
         MVI   GABNDMSG+22,C'S'    YES - INDICATE SYSTEM ABEND
         STH   R15,WORKAREA        PUT COMPLETION CODE IN WORKAREA
         UNPK  WORKAREA+3(5),WORKAREA(3) UNPACK
         TR    WORKAREA+3(4),TRTABLE-240 CONVERT TO EBCDIC
         MVC   GABNDMSG+23(3),WORKAREA+4 MOVE TO MESSAGE
         B     ABNDTPUT            GO TELL OPERATOR
USERABND DS    0H
         MVI   GABNDMSG+22,C'U'    INDICATE USER ABEND
         L     R15,ABENDCDE        GET COMPLETION CODE
         SLL   R15,20              SHIFT OUT TCBFLGS1
         SRL   R15,20              SHIFT BACK
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GABNDMSG+23(4),WORKAREA+1 MOVE TO ABEND MESSAGE
ABNDTPUT DS    0H
         LA    R0,L'ABNDMSG        GET LENGTH FOR TPUT
         LA    R1,GABNDMSG         POINT TO ABEND MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R           TELL USER
         MVC   WORKAREA(5),GABNDMSG+22 SAVE ABEND CODE
         MVC   GABNDWTO,ABNDWTO    COPY WTO TEXT TO DYNAMIC AREA
         MVC   GABNDWTO+46(5),WORKAREA COPY ABEND CODE
         CLI   GUSERID,0           HAS USERID BEEN RETRIEVED
         BE    ABENDWTO            NO - LEAVE TEXT WITH 'UNKNOWN'
         MVC   GABNDWTO+4(7),GUSERID YES - COPY TO WTO
ABENDWTO DS    0H
         MVC   GABNDWTO+15(8),GTERMID COPY TERMID TO DYNAMIC AREA
         MVC   GABNDWTO+61(2),GASID COPY ASID TO DYNAMIC AREA
         WTO   MF=(E,GABNDWTO)     TELL OPERATOR
         EJECT
***********************************************************************
*                                                                     *
*       IF APF AUTHORIZED, TAKE SDUMP.                                *
*                                                                     *
***********************************************************************
         L     R15,PSATNEW-PSA     GET ADDRESS OF MY TCB
         L     R15,TCBJSCB-TCB(,R15) GET ADDRESS OF MY JSCB
         TM    JSCBOPTS-IEZJSCB(R15),JSCBAUTH CAN I TAKE SDUMP
         BZ    NOSDUMP             NO - SKIP SDUMP
         LH    R15,GABNDWTO        YES - GET LENGTH OF WTO
         SH    R15,HALF4           GET LENGTH OF TEXT
         STC   R15,GABNDWTO+3      SET UP FOR SDUMP
         MVC   GSDUMP,SDUMP        COPY SDUMP PARAMETER LIST
         LA    R1,GSDUMP           POINT TO SDUMP PARAMETER LIST
         SDUMP HDRAD=GABNDWTO+3,MF=(E,(1)) TAKE SDUMP
NOSDUMP  DS    0H
         OI    GBITS1,GDENY        DENY LOGON
         LA    R15,16              SET RETURN CODE
         B     RETURN              RETURN
         TITLE 'STAX EXIT ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    STAXEXIT                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER AN ATTENTION INTERRUPT HAS OCCURED.                *
*                                                                     *
* OPERATION -                                                         *
*    1) CHECKS LENGTH OF INPUT BUFFER.  IF ZERO, EXIT.                *
*    2) UPDATES ORIGINAL INPUT BUFFER.                                *
*    3) REBUILDS CBUF.                                                *
*    4) POSTS CP ECB.                                                 *
*    5) RETURNS TO CALLER.                                            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    STAXEXIT                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    ATTENTION EXIT PARAMETER LIST (AEPL)                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
STAXEXIT DS    0H
         SAVE  (14,12),,STAXEXIT   SAVE CALLER'S REGISTERS
         USING AEPL,R1             ESTABLISH ADDRESSABILITY TO AEPL
         L     R2,AEPLUSAD         GET ADDRESS OF USADDR FROM AEPL
         LR    R3,R13              SAVE CALLER'S R13
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST
         L     R13,16(,R2)         RESTORE DYN BASE FROM PARM LIST
         EJECT
***********************************************************************
*                                                                    P*
*       POSTS THE COMMAND PUTGET ECB SO THE REQUEST CAN BE REISSUED.  *
*       CHECKS LENGTH OF INPUT BUFFER.  IF ZERO, EXIT.                *
*                                                                     *
***********************************************************************
         L     R4,AEPLTAIE         GET ADDRESS OF TAIE FROM AEPL
         DROP  R1                  DROP APEL ADDRESSABILITY
         USING TAIE,R4             ESTABLISH ADDRESSABILITY TO TAIE
         POST  GCPECB              POST CP ECB
         ICM   R1,B'0011',TAIEMSGL GET LENGTH OF INPUT BUFFER
         DROP  R4                  DROP TAIE ADDRESSABILITY
         BNZ   STAXGMSG            IF NOT ZERO - THEN GOT MESSAGE
         B     STAXRTRN            ELSE - GET OUT
         EJECT
***********************************************************************
*                                                                     *
*       UPDATES ORIGINAL INPUT BUFFER.                                *
*                                                                     *
***********************************************************************
STAXGMSG DS    0H
         L     R2,ADDRPARM         GET ADDRESS OF ORIGINAL PARMS
         L     R2,4(,R2)           GET ADDRESS OF INPUT BUFFER DESC
         L     R15,0(R2)           GET ADDRESS OF ORIGINAL BUFFER
         STH   R1,6(,R2)           UPDATE ACTUAL LENGTH
         EJECT
***********************************************************************
*                                                                     *
*       REBUILDS CBUF.                                                *
*                                                                     *
***********************************************************************
         LA    R1,4(,R1)           ADD 4 FOR LENGTH AND OFFSET
         STH   R1,GCBUF+CBUFLEN-CBUF UPDATE BUILT CBUF
         XC    GCBUF+CBUFOFFS-CBUF(2),GCBUF+CBUFOFFS-CBUF ZERO OFF
         MVC   GCBUF+CBUFTEXT-CBUF(252),0(R15) COPY STAX RESPONSE
         EJECT
***********************************************************************
*                                                                     *
*       INDICATES THAT AN ATTENTION HAS BEEN HIT AND A NEW COMMAND IS *
*       READY TO BE PROCESSED                                         *
*                                                                     *
***********************************************************************
         OI    GBITS1,GATTNHIT     INDICATE ATTENTION INTERRUPT
         EJECT
***********************************************************************
*                                                                     *
*       RETURNS TO CALLER.                                            *
*                                                                     *
***********************************************************************
STAXRTRN DS    0H
         LR    R13,R3              RESTORE CALLER'S R13
         RETURN (14,12),RC=0       RETURN TO CALLER
         TITLE 'DUMMY STAX EXIT ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    STAXDXIT                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER AN ATTENTION INTERRUPT HAS OCCURED.                *
*                                                                     *
* OPERATION -                                                         *
*    1) RETURNS TO CALLER.                                            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    STAXDXIT                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    ATTENTION EXIT PARAMETER LIST (AEPL)                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
* NOTES -                                                             *
*    ADDRESSABILITY IS ESTABLISHED TO REST OF EXIT AND TO DYNAMIC     *
*    AREA IN CASE THIS MAY BE NEEDED IN THE FUTURE.                   *
*                                                                     *
***********************************************************************
STAXDXIT DS    0H
         SAVE  (14,12),,STAXDXIT   SAVE CALLER'S REGISTERS
         USING AEPL,R1             ESTABLISH ADDRESSABILITY TO AEPL
         L     R2,AEPLUSAD         GET ADDRESS OF USADDR FROM AEPL
         LR    R3,R13              SAVE CALLER'S R13
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST
         L     R13,16(,R2)         RESTORE DYN BASE FROM PARM LIST
         EJECT
***********************************************************************
*                                                                     *
*       RETURNS TO CALLER.                                            *
*                                                                     *
***********************************************************************
         LR    R13,R3              RESTORE CALLER'S R13
         RETURN (14,12),RC=0       RETURN TO CALLER
         TITLE 'TIMER EXIT ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    TIMERXIT                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER A STIMER INTERRUPT HAS OCCURED.                    *
*                                                                     *
* OPERATION -                                                         *
*    1) ISSUE MESSAGE TO OPERATOR AND ABEND WITH U0522.               *
*                                                                     *
* ENTRY POINTS -                                                      *
*    TIMERXIT                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    'USER-ID ON TERMINAL LOGON TERMINATED DUE TO WAIT TIME EXCEEDED  *
*     - ASID X'NN''                                                   *
*                                                                     *
* COMMENTS -                                                          *
*    THIS ROUTINE IS MOVED TO GTIMRXIT BY THE HOUSEKEEPING ROUTINE.   *
*    THE LABEL AFTER LTIMRXIT, TTIMRWTO MUST BE KEPT ALIGNED WITH     *
*    GTIMRWTO IN THE DYNAMIC AREA.                                    *
*                                                                     *
***********************************************************************
TIMERXIT DS    0F
         PUSH  USING               SAVE MAINLINE'S ADDRESSABILITY
         DROP  ,                   DROP MAINLINE'S ADDRESSABILITY
         SAVE  (14,12),,TIMERXIT   SAVE CALLER'S REGISTERS
         USING TIMERXIT,R12        GET TEMPORARY ADDRESSABILITY
         LR    R12,R15             SET TEMPORARY BASE
         EJECT
***********************************************************************
*                                                                     *
*       ISSUE MESSAGE TO OPERATOR AND ABEND WITH U0522.               *
*                                                                     *
***********************************************************************
         WTO   MF=(E,TTIMRWTO)     TELL OPERATOR WHY
         ABEND 522                 AND ABEND WITH U0522
         DS    F                   FILL OUT TO WORD IF NECESSARY
LTIMRXIT EQU   *-TIMERXIT          CALCULATE LENGTH OF STIMER EXIT
TTIMRWTO DS    C                   CORRESPONDS TO GTIMRWTO AFTER MOV
         SPACE 3
         POP   USING               RESTORE MAINLINE'S ADDRESSABILITY
         TITLE 'VALIDITY CHECK ROUTINE FOR ACCOUNT NUMBER'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VACCT                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AS A VALIDITY CHECKING ROUTINE FROM IKJPARS TO VERIFY    *
*    THE KEYWORD PARAMETER 'ACCT'.                                    *
*                                                                     *
* OPERATION -                                                         *
*    1) IF ONLY ONE ACCOUNT NUMBER IS DEFINED FOR THIS PASSWORD,      *
*       IGNORES ACCOUNT FROM PARSE.                                   *
*    2) CHECKS THE LENGTH OF THE ACCOUNT NUMBER TO ENSURE THAT IT IS  *
*       EIGHT (8) BYTES LONG.  IF NOT, LETS IKJPARS ISSUE MESSAGE AND *
*       PROMPT FOR NEW ACCOUNT NUMBER.                                *
*    3) RETURN TO IKJPARS.                                            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VACCT                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT -                                               *
*       --------------------------------------------------------  *
*       NUMBER OF                                                 *
*       BYTES     FIELD   CONTENTS OR MEANING                     *
*       --------------------------------------------------------  *
*           4     PDEADR  THE ADDRESS OF THE PDE                  *
*       --------------------------------------------------------  *
*           4     USERWORDTHE ADDRESS OF THE USER WORK AREA.      *
*       --------------------------------------------------------  *
*           4     VALMSG  THE ADDRESS OF A SECOND LEVEL MESSAGE.  *
*       --------------------------------------------------------  *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR IKJPARS.  THIS WILL BE  *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  ACCOUNT NUMBER IS VALID.IKJPARS CONTINUE.           *
*       --------------------------------------------------------  *
*          4  ACCOUNT NUMBER IS NOT   IKJPARS WILL ISSUE MESSAGE  *
*             VALID.                  AND PROMPT.                 *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
* COMMENTS -                                                          *
*    THIS ROUTINE OPERATES AS A SUBROUTINE TO IKJPARS.  SINCE         *
*    THE SAVE AREA POINTED TO BY THE MAINLINE'S R13 IS BEING USED BY  *
*    IKJPARS, THIS ROUTINE MUST USE ANOTHER SAVE AREA.  HOWEVER,      *
*    THERE IS STILL NEED TO REFERENCE THE MAINLINE'S DYNAMIC AREA.    *
*    ADDRESSABILITY TO THIS IS PROVIDED BY USING R8.                  *
*                                                                     *
***********************************************************************
         EJECT
VACCT    DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         SAVE  (14,12),,VACCT      SAVE PARSE'S REGISTERS
         L     R2,4(,R1)           GET ADDRESS OF USERWORD
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST
         L     R8,16(,R2)          RESTORE DYN BASE FROM PARM LIST
         DROP  R13                 DROP MAIN LINE'S ADDRESSABILITY
         USING GOTNAREA,R8         GET ADDRESSABILITY TO DYN AREA
         LA    R15,VERSAVEA        GET ADDRESS OF VER ROUTINES SAVEAREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVE
         LR    R13,R15                 AREAS
         L     R2,0(,R1)
         SR    R3,R3
         LH    R3,4(,R2)                 GET INPUT LENGTH
         L     R2,0(,R2)                 POINT TO INPUT FIELD
         STH   R3,LACCTNUM               SAVE INPUT LENGTH
         LA    R4,GACCTNUM               POINT TO RECEIVING FIELD
         MVC   GACCTNUM,BLANKS           CLEAR RECEIVING FIELD
         BCTR  R3,0                      CONVERT LENGTH TO DISPLACEMENT
         EX    R3,MOVEIT                 MOVE INPUT TO RECEIVING FIELD
         EJECT
***********************************************************************
*                                                                     *
*       IF ONLY ONE ACCOUNT NUMBER IS DEFINED FOR THIS PASSWORD,      *
*       IGNORES ACCOUNT FROM PARSE.                                   *
*                                                                     *
***********************************************************************
         L     R2,ADDRUBUF         GET ADDRESS OF SYS1.UADS BUFFER
         L     R15,PASSOFF         NO - GET OFFSET TO PSWD OFF BLOCK
         L     R15,4(R2,R15)       GET OFFSET OF ACCT # OFFSET BLOCK
         AR    R15,R2              GET ADDRESS OF ACCT # OFFSET BLK
         TM    0(R15),KBIT0        MORE THAN ONE ACCOUNT NUMBER
         BZ    VACTCHEK            YES - GO CHECK THIS ONE
         L     R14,8(,R15)         NO - GET OFFSET OF ACCT # BLOCK
         AR    R14,R2              GET ADDRESS OF ACCT # DATA BLOCK
         SR    R3,R3
         IC    R3,44(,R14)         GET ACTUAL LENGTH OF ACCT NUMBER
         STH   R3,LACCTNUM
         BCTR  R3,0
         LA    R2,45(,R14)         GET ADDR OF ACCTNUM
         LA    R4,GACCTNUM
         EX    R3,MOVEIT           MOVE TO DYNAMIC AREA
         ST    R15,GACTADDR        SAVE THE ACCT OFFSET BLK ADDRESS
         SR    R15,R15             SET RETURN CODE
         B     VACTEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS THE PROVIDED ACCOUNT NUMBER AGAINST THE AUTHORIZED     *
*       ACCOUNT NUMBERS TO SEE IF THERE IS A MATCH.  ELSE ISSUE A     *
*       MESSAGE AND PROMPT FOR A NEW ACCOUNT NUMBER.                  *
*                                                                     *
***********************************************************************
VACTCHEK DS    0H
         L     R14,8(,R15)         GET OFFSET OF ACCT # DATA BLOCK
         AR    R14,R2              GET ADDRESS OF ACCT # DATA BLOCK
         SR    R3,R3
         IC    R3,44(,R14)         GET LENGTH OF ACCOUNT NUMBER
         BCTR  R3,0
         LA    R4,GACCTNUM
         LA    R2,45(,R14)
         EX    R3,COMPARIT         ARE THE ACCOUNT NUMBERS EQUAL?
         BE    VACTGOOD            /YES - A GOOD ACC NUMBER WAS ENTERED
         TM    0(R15),KBIT0        IS THIS THE LAST ACCT BLOCK?
         BO    VACTBAD             /YES - A BAD ACC NUMBER WAS ENTERED
         L     R15,0(,R15)         GET OFFSET OF NEXT ACCT OFFSET BLK
         L     R2,ADDRUBUF
         AR    R15,R2              GET ADDRESS OF ACCT OFFSET BLK
         B     VACTCHEK            CHECK THIS ACCOUNT NUMBER
VACTGOOD DS    0H
         LA    R3,1(,R3)
         ST    R3,LACCTNUM         SAVE ACCOUNT NUMBER LENGTH
         ST    R15,GACTADDR        SAVE THE ACCT OFFSET BLK ADDRESS
         SR    R15,R15             SET RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO IKJPARS.                                            *
*                                                                     *
***********************************************************************
VACTEXIT DS    0H
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         RETURN (14,12),RC=(15)    RETURN TO PARSE
         EJECT
***********************************************************************
*                                                                     *
*       LET PARSE ISSUE MESSAGE AND PROMPT FOR NEW ACCOUNT NUMBER.    *
*                                                                     *
***********************************************************************
VACTBAD  DS    0H
         LA    R15,4               LET PARSE ISSUE MESSAGE
         B     VACTEXIT            AND EXIT
         SPACE 3
         POP   USING               RESTORE MAIN LINE'S ADDRESSABILITY
         TITLE 'VALIDITY CHECK ROUTINE FOR RACF GROUP'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VGRUP                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AS A VALIDITY CHECKING ROUTINE FROM IKJPARS TO VERIFY    *
*    THE KEYWORD PARAMETER 'GROUP'                                    *
*                                                                     *
* OPERATION -                                                         *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VGRUP                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT -                                               *
*       --------------------------------------------------------  *
*       NUMBER OF                                                 *
*       BYTES     FIELD   CONTENTS OR MEANING                     *
*       --------------------------------------------------------  *
*           4     PDEADR  THE ADDRESS OF THE PDE                  *
*       --------------------------------------------------------  *
*           4     USERWORDTHE ADDRESS OF THE USER WORK AREA.      *
*       --------------------------------------------------------  *
*           4     VALMSG  THE ADDRESS OF A SECOND LEVEL MESSAGE.  *
*       --------------------------------------------------------  *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR IKJPARS.  THIS WILL BE  *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  GROUP NAME IS VALID     IKJPARS CONTINUE.           *
*       --------------------------------------------------------  *
*          4  GROUP NAME IS INVALID   IKJPARS RE-PROMPTS          *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'DEFAULT GROUP ASSUMED'                                          *
*                                                                     *
* COMMENTS -                                                          *
*    THIS ROUTINE OPERATES AS A SUBROUTINE TO IKJPARS.  SINCE         *
*    THE SAVE AREA POINTED TO BY THE MAINLINE'S R13 IS BEING USED BY  *
*    IKJPARS, THIS ROUTINE MUST USE ANOTHER SAVE AREA.  HOWEVER,      *
*    THERE IS STILL NEED TO REFERENCE THE MAINLINE'S DYNAMIC AREA.    *
*    ADDRESSABILITY TO THIS IS PROVIDED BY USING R8.                  *
*                                                                     *
***********************************************************************
         EJECT
VGRUP    DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         SAVE  (14,12),,VGRUP      SAVE PARSE'S REGISTERS
         L     R2,4(,R1)           GET ADDRESS OF USERWORD
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST
         L     R8,16(,R2)          RESTORE DYN BASE FROM PARM LIST
         DROP  R13                 DROP MAIN LINE'S ADDRESSABILITY
         USING GOTNAREA,R8         GET ADDRESSABILITY TO DYN AREA
         LA    R15,VERSAVEA        GET ADDRESS OF VER ROUTINES SAVEAREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVE
         LR    R13,R15                 AREAS
         L     R2,0(,R1)
         SR    R3,R3
         LH    R3,4(,R2)                 GET INPUT LENGTH
         L     R2,0(,R2)                 POINT TO INPUT FIELD
         STH   R3,LGRUP                  SAVE LENGTH
         LA    R4,GGRUP                  POINT TO RECEIVING FIELD
         MVC   GGRUP,BLANKS              MOVE BLANKS TO RECEIVING FLD
         BCTR  R3,0                      CONVERT LENGTH TO DISPLACEMENT
         EX    R3,MOVEIT                 MOVE INPUT TO RECEIVING FIELD
         MVC   GUSERGRP(8),GUSERID       MOVE USERID INTO ENTRY NAME
         LA    R4,SLASHTRT               SETUP TO FIND THE FIRST BLANK
         LA    R3,GUSERGRP
         LA    R5,17
         EX    R5,EXECTRT                FIND THE FIRST BLANK
         MVI   0(R1),X'00'               MOVE IN THE BYTE OF ZEROS
         MVC   1(8,R1),GGRUP             MOVE IN THE GROUP
         EX    R5,EXECTRT                FIND THE FIRST BLANK
         SR    R1,R3                     CALCULATE THE LENGTH OF ENTRY
         STH   R1,LUSERGRP               SAVE THAT LENGTH
         MVC   WINTY2,CINTY2             SETUP LIST FORM OF ICHEINTY
         MVC   WACTION4,CACTION4         SETUP LIST FORM OF ICHEACTN
         XC    WPMRAREA,WPMRAREA
         LA    R1,LPMRAREA
         ST    R1,WPMRAREA
         ICHEINTY LOCATE,TYPE='CON',ENTRY=LUSERGRP+1,                  X
               OPTIONS=(FLDEF,ACTION,TESTC,TESTM),                     X
               WKAREA=WPMRAREA,                                        X
               ACTIONS=(WACTION4),                                     X
               MF=(E,WINTY2)
         LTR   R15,R15
         BZ    VGRPTEST
         LA    R1,12
         CR    R1,R15
         BNE   VGRPER1
         LA    R15,4
         B     VGRPEXIT
VGRPTEST DS    0H
         TM    WPMRAREA+30,X'80'
         BZ    VGRPEXIT
         LA    R15,4
         B     VGRPEXIT
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO IKJPARS.                                            *
*                                                                     *
***********************************************************************
VGRPEXIT DS    0H
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         RETURN (14,12),RC=(15)    RETURN TO PARSE
         SPACE 3
VGRPER1  DS    0H
         MVC   GRACFER2(L'RACFER2),RACFER2  COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GRACFER2+16(4),WORKAREA+1 MOVE INTO MESSAGE
         MVC   GRACFER2+29(17),GUSERGRP  ENTRY ID
         LA    R0,L'RACFER2              GET LENGTH OF MESSAGE
         LA    R1,GRACFER2               POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         OI    GBITS1,GDENY              DENY LOGON
         LA    R15,12
         B     VGRPEXIT            EXIT
         SPACE 3
         POP   USING               RESTORE MAIN LINE'S ADDRESSABILITY
         TITLE 'VALIDITY CHECK ROUTINE FOR PERFORMANCE GROUP'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VPERF                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AS A VALIDITY CHECKING ROUTINE FROM IKJPARS TO VERIFY    *
*    THE KEYWORD PARAMETER 'PERFORM'                                  *
*                                                                     *
* OPERATION -                                                         *
*    PERFORMANCE GROUP IS CURRENTLY IGNORED, AND THE DEFAULT IS ALWAYS*
*    USED.  A MESSAGE IS RETURNED TO THE USER INDICATING THAT HIS     *
*    ENTRY WAS IGNORED.                                               *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VPERF                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT -                                               *
*       --------------------------------------------------------  *
*       NUMBER OF                                                 *
*       BYTES     FIELD   CONTENTS OR MEANING                     *
*       --------------------------------------------------------  *
*           4     PDEADR  THE ADDRESS OF THE PDE                  *
*       --------------------------------------------------------  *
*           4     USERWORDTHE ADDRESS OF THE USER WORK AREA.      *
*       --------------------------------------------------------  *
*           4     VALMSG  THE ADDRESS OF A SECOND LEVEL MESSAGE.  *
*       --------------------------------------------------------  *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR IKJPARS.  THIS WILL BE  *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  PERFORMANCE GROUP VALID.IKJPARS CONTINUE.           *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'DEFAULT PERFORMANCE GROUP USED'                                 *
*                                                                     *
* COMMENTS -                                                          *
*    THIS ROUTINE OPERATES AS A SUBROUTINE TO IKJPARS.  SINCE         *
*    THE SAVE AREA POINTED TO BY THE MAINLINE'S R13 IS BEING USED BY  *
*    IKJPARS, THIS ROUTINE MUST USE ANOTHER SAVE AREA.  HOWEVER,      *
*    THERE IS STILL NEED TO REFERENCE THE MAINLINE'S DYNAMIC AREA.    *
*    ADDRESSABILITY TO THIS IS PROVIDED BY USING R8.                  *
*                                                                     *
***********************************************************************
         EJECT
VPERF    DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         SAVE  (14,12),,VPERF      SAVE PARSE'S REGISTERS
         L     R2,4(,R1)           GET ADDRESS OF USERWORD
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST
         L     R8,16(,R2)          RESTORE DYN BASE FROM PARM LIST
         DROP  R13                 DROP MAIN LINE'S ADDRESSABILITY
         USING GOTNAREA,R8         GET ADDRESSABILITY TO DYN AREA
         LA    R15,VERSAVEA        GET ADDRESS OF VER ROUTINES SAVEAREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVE
         LR    R13,R15                 AREAS
         L     R2,0(,R1)
         SR    R3,R3
         LH    R3,4(,R2)                 GET INPUT LENGTH
         L     R2,0(,R2)                 POINT TO INPUT
         STH   R3,LPERF                  SAVE INPUT LENGTH
         LA    R4,GPERF                  POINT TO RECEIVING FIELD
         BCTR  R3,0                      CONVERT LENGTH TO DISPLACEMENT
         EX    R3,MOVEIT                 MOVE TO RECEIVING FIELD
         LA    R0,L'PERFMSG              GET LENGTH OF MESSAGE
         LA    R1,PERFMSG                POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO IKJPARS.                                            *
*                                                                     *
***********************************************************************
VPRFEXIT DS    0H
         SR    R15,R15
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         RETURN (14,12),RC=(15)    RETURN TO PARSE
         SPACE 3
         POP   USING               RESTORE MAIN LINE'S ADDRESSABILITY
         TITLE 'VALIDITY CHECK ROUTINE FOR PROCEDURE NAME'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VPROC                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AS A VALIDITY CHECKING ROUTINE FROM IKJPARS TO VERIFY    *
*    THE KEYWORD PARAMETER 'PROC'.                                    *
*                                                                     *
* OPERATION -                                                         *
*    1) EXTRACTS THE PROC NAME FROM THE PDE AND PLACES IT IN          *
*       GPROC PADDED WITH BLANKS.                                     *
*    2) RETURN TO IKJPARS.                                            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VPROC                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT -                                               *
*       --------------------------------------------------------  *
*       NUMBER OF                                                 *
*       BYTES     FIELD   CONTENTS OR MEANING                     *
*       --------------------------------------------------------  *
*           4     PDEADR  THE ADDRESS OF THE PDE                  *
*       --------------------------------------------------------  *
*           4     USERWORDTHE ADDRESS OF THE USER WORK AREA.      *
*       --------------------------------------------------------  *
*           4     VALMSG  THE ADDRESS OF A SECOND LEVEL MESSAGE.  *
*       --------------------------------------------------------  *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR IKJPARS.  THIS WILL BE  *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  PROCEDURE NAME IS VALID.IKJPARS CONTINUE.           *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'PROCEDURE IGNORED'                                              *
*                                                                     *
* COMMENTS -                                                          *
*    THIS ROUTINE OPERATES AS A SUBROUTINE TO IKJPARS.  SINCE         *
*    THE SAVE AREA POINTED TO BY THE MAINLINE'S R13 IS BEING USED BY  *
*    IKJPARS, THIS ROUTINE MUST USE ANOTHER SAVE AREA.  HOWEVER,      *
*    THERE IS STILL NEED TO REFERENCE THE MAINLINE'S DYNAMIC AREA.    *
*    ADDRESSABILITY TO THIS IS PROVIDED BY USING R8.                  *
*                                                                     *
***********************************************************************
         EJECT
VPROC    DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         SAVE  (14,12),,VPROC      SAVE PARSE'S REGISTERS
         L     R2,4(,R1)           GET ADDRESS OF USERWORD
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST
         L     R8,16(,R2)          RESTORE DYN BASE FROM PARM LIST
         DROP  R13                 DROP MAIN LINE'S ADDRESSABILITY
         USING GOTNAREA,R8         GET ADDRESSABILITY TO DYN AREA
         LA    R15,VERSAVEA        GET ADDRESS OF VER ROUTINES SAVEAREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVE
         LR    R13,R15                 AREAS
         L     R2,0(,R1)
         SR    R3,R3
         LH    R3,4(,R2)                 GET LENGTH OF INPUT
         L     R2,0(,R2)                 POINT TO INPUT
         STH   R3,LPROC                  SAVE LENGTH OF INPUT
         LA    R4,GPROC                  POINT TO RECEIVING FIELD
         MVC   GPROC,BLANKS              MOVE BLANKS TO RECEIVING FIELD
         BCTR  R3,0                      CONVERT LENGTH TO DISPLACEMENT
         EX    R3,MOVEIT                 MOVE INPUT TO RECEIVING FIELD
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO IKJPARS.                                            *
*                                                                     *
***********************************************************************
VPRCEXIT DS    0H
         SR    R15,R15
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         RETURN (14,12),RC=(15)    RETURN TO PARSE
         SPACE 3
         POP   USING               RESTORE MAIN LINE'S ADDRESSABILITY
         TITLE 'VALIDITY CHECK ROUTINE FOR USERID'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VUSERID                                                          *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AS A VALIDITY CHECKING ROUTINE FROM IKJPARS TO VERIFY    *
*    THE POSITIONAL VALUE OF USERID.                                  *
*                                                                     *
* OPERATION -                                                         *
*    1) EXTRACTS THE USERID FROM THE PDE AND PLACES IT IN GUSERID     *
*       PADDED WITH BLANKS.                                           *
*    2) CHECKS TO SEE IF THE USERID IS FOUND IN SYS1.UADS. IF NOT,    *
*       THE LOGON IS DENIED.                                          *
*    3) PROMPT USER FOR PASSWORD                                      *
*       MOVE PASSWORD TO GPASS PADDED WITH BLANKS.                    *
*    4) IF RACF IS ACTIVE, VERIFY THAT USER IS IN RACF, AND THE       *
*       PROPER PASSWORD HAS BEEN GIVEN.  IF PASSWORD INVALID, PROMPT  *
*       UP TO THREE TIMES FOR THE PROPER PASSWORD.                    *
*    4) IF RACF IS INACTIVE, OR THE USER ISG PASSWORD.  IF NOT FOUND, *
*    4) SEARCH UADS DATA BLOCKS FOR MATCHING PASSWORD.  IF NOT FOUND, *
*       THE USER IS PROMPTED FOR ANOTHER PASSWORD.                    *
*    5) RETURN TO IKJPARS.                                            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VUSERID                                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT -                                               *
*       --------------------------------------------------------  *
*       NUMBER OF                                                 *
*       BYTES     FIELD   CONTENTS OR MEANING                     *
*       --------------------------------------------------------  *
*           4     PDEADR  THE ADDRESS OF THE PDE                  *
*       --------------------------------------------------------  *
*           4     USERWORDTHE ADDRESS OF THE USER WORK AREA.      *
*       --------------------------------------------------------  *
*           4     VALMSG  THE ADDRESS OF A SECOND LEVEL MESSAGE.  *
*       --------------------------------------------------------  *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR IKJPARS.  THIS WILL BE  *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  LOGON IS VALID.         IKJPARS CONTINUE.           *
*       --------------------------------------------------------  *
*          8  USERID NOT IN SYS1.UADS.IKJPARS WILL PROMPT.        *
*       --------------------------------------------------------  *
*         12  USER FAILED TO ENTER A  DENY LOGON SWITCH IS SET.   *
*             VALID USERID OR PASSWORDIKJPARS WILL STOP.          *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'PROMPT LIMIT EXCEEDED'                                          *
*    'PASSWORD NOT AUTHORIZED FOR THIS USERID'                        *
*                                                                     *
* COMMENTS -                                                          *
*    THIS ROUTINE OPERATES AS A SUBROUTINE TO IKJPARS.  SINCE         *
*    THE SAVE AREA POINTED TO BY THE MAINLINE'S R13 IS BEING USED BY  *
*    IKJPARS, THIS ROUTINE MUST USE ANOTHER SAVE AREA.  HOWEVER,      *
*    THERE IS STILL NEED TO REFERENCE THE MAINLINE'S DYNAMIC AREA.    *
*    ADDRESSABILITY TO THIS IS PROVIDED BY USING R8.                  *
*                                                                     *
***********************************************************************
         EJECT
VUSERID  DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         SAVE  (14,12),,VUSERID    SAVE PARSE'S REGISTERS
         L     R2,4(,R1)           GET ADDRESS OF USERWORD
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST
         L     R8,16(,R2)          RESTORE DYN BASE FROM PARM LIST
         DROP  R13                 DROP MAIN LINE'S ADDRESSABILITY
         USING GOTNAREA,R8         GET ADDRESSABILITY TO DYN AREA
         LA    R15,VERSAVEA        GET ADDRESS OF VER ROUTINES SAVEAREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVE
         LR    R13,R15                 AREAS
         L     R2,0(,R1)                 GET ADDRESS OF PDE
         TM    14(R2),X'80'              WAS PASSWORD SUPPLIED?
         BO    VUSERER1                  /YES - PASSWORD NOT ALLOWED
         TM    22(R2),X'80'              WAS NEW PASSWORD SUPPLIED?
         BO    VUSERER1                  /YES - NOT ALLOWED
         SR    R3,R3
         LH    R3,4(,R2)                 GET LENGTH OF USERID
         STH   R3,LUSERID                SAVE USERID LENGTH
         BCTR  R3,0                      CHANGE TO DISPLACEMENT
         L     R2,0(,R2)                 GET ADDRESS OF USERID
         LTR   R2,R2                     WAS USERID SUPPLIED?
         BZ    VUSERER2                  /NO --- PROMPT FOR USERID
         MVI   GUSERID,C' '
         MVC   GUSERID+1(7),GUSERID
         LA    R4,GUSERID
         EX    R3,MOVEIT                 MOVE IN SUPPLIED USERID
         MVC   GMEMBER,GUSERID           SETUP MEMBER NAME
         LA    R3,1(,R3)
         LA    R3,GMEMBER(R3)
*        USING GMEMBER+?,R3              #### LOGICAL USING OF R3
         MVI   0(R3),C'0'                TO POINT AT THE END OF MEMBER
         MVI   GBLL+1,1                  SETUP FOR BLDL
         MVI   GBFF+1,12
         MVC   GBNAME,GMEMBER
         EJECT
***********************************************************************
*          FIND THE MEMBER IN THE UADS                                *
***********************************************************************
         BLDL  GSYSUADS,GBLDL            GET TTR OF MEMBER
         LTR   R15,R15                   IS EVERYTHING FINE?
         BNZ   VUSERER3                  /NO - REPORT ERROR
         MVC   GTTRUSER,GBTTR            SAVE USER TTR
         L     R4,ADDRUBUF               GET THE BUFFER ADDRESS
*        USING UADSDSCT,R4               #### LOGICAL USING OF R4
         POINT GSYSUADS,GTTRUSER         POINT AT THE MEMBER
         READ  DECB,SF,GSYSUADS,(R4),'S',MF=E
         CHECK DECB                      READ AND CHECK THE MEMBER
         LH    R1,GSYSUADS+DCBBLKSI-IHADCB
         L     R15,DECB+16
         SH    R1,14(,R15)
         ST    R1,GLENUSER               SAVE LENGTH OF FIRST BLOCK
         B     VUSEFRST
         EJECT
***********************************************************************
*          READ UAD MEMBER LOOP                                       *
***********************************************************************
VUSELOOP DS    0H
         READ  DECB,SF,GSYSUADS,(R4),'S',MF=E
         CHECK DECB
VUSEFRST DS    0H
         CLI   0(R3),C'9'                HAVE NINE BLOCKS BEEN READ
         BE    VUSEDONE                  /YES - WE ARE DONE READING
         IC    R15,0(,R3)                ELSE
         LA    R15,1(,R15)               ADD 1 TO MEMBER NAME
         STC   R15,0(,R3)
         FIND  GSYSUADS,GMEMBER,D        AND FIND NEXT MEMBER
         LTR   R15,R15                   WAS FIND SUCCESSFUL?
         BNZ   VUSEDONE                  /NO - WE ARE DONE READING
         LH    R1,GSYSUADS+DCBBLKSI-IHADCB
         L     R15,DECB+16
         SH    R1,14(,R15)
         AR    R4,R1                     ADD LENGTH TO REG 4
         B     VUSELOOP
         EJECT
***********************************************************************
*          THIS SECTION PROMPTS FOR THE PASSWORD FOR NON-DISPLAY TERMS*
***********************************************************************
VUSEDONE DS    0H
         MVI   0(R3),C' '
*        DROP  R3                        #### LOGICAL DROP OF R3
         MVI   GCOUNTER,0                INITIALIZE COUNT FIELD
         RACSTAT                         SEE IF RACF IS ACTIVE
         LTR   R15,R15                   IS RACF ACTIVE?
         BNZ   GETPASS                   /NO - GET PASSWORD
         OI    GBITS2,GRACFON            ELSE INDICATE RACF ACTIVE
GETPASS  DS    0H
         IC    R15,GCOUNTER              INCREMENT COUNTER
         LA    R15,1(,R15)
         STC   R15,GCOUNTER
         CLI   GCOUNTER,4                HAS USER HAD THREE CHANCES?
         BE    VUSERDNY                  DENY LOGON
         TM    GBITS2,GDISPLAY           IS IT A DISPLAY TUBE?
         BO    PASSDISP                  /YES - USE PUTGET
         MVC   GPASSMSG,PASSMSG
         MVC   GPASSMSG+19(8),GUSERID
         LA    R5,8
         LA    R3,GPMSGHDR+19            POINT TO THE USER ID
         LA    R4,SLASHTRT
         EX    R5,EXECTRT                FIND THE FIRST BLANK
         MVI   0(R1),C':'                PUT IN THE COLON
         LA    R0,LPASSMSG               ELSE ASK FOR PASSWORD
         LA    R1,GPASSMSG
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 BY DOING A TPUT
         LTR   R15,R15                   WAS TPUT SUCCESSFUL?
         BNZ   VUSERER5                  /NO - WE HAVE AN ERROR
         LA    R0,LOVRPRNT
         LA    R1,OVERPRNT
         ICM   R1,8,PUTOPTN2
         TPUT  (1),(0),R                 DO THE OVERPRINT TPUT
         LTR   R15,R15                   WAS THAT TPUT SUCCESSFUL?
         BNZ   VUSERER5                  /NO - WE HAVE AN ERROR
         LA    R0,L'GPASS
         LA    R1,GPASS
         ICM   R1,8,GETOPTN              INDICATE TGET WAIT EDIT
         TGET  (1),(0),R                 DO THE TGET
         LTR   R15,R15                   WAS THAT TGET SUCCESSFUL?
         BNZ   VUSERER6                  /NO - WE HAVE AN ERROR
         CLC   GPASS,BLANKS              WAS BLANKS SUPPLIED?
         BE    GETPASS                   /YES - ASK AGAIN
         B     CHCKNEWP                  CHECK FOR NEW PASSWORD
         EJECT
***********************************************************************
*        THIS SECTION PROMPTS FOR THE PASSWORD  FOR DISPLAY TERMINALS *
***********************************************************************
PASSDISP DS    0H
         MVC   GIKJPGPB(LPGPB),LPUTGET   MOVE LIST TO WORK AREA
         LA    R1,GIKJIOPL               GET ADDRESS OF IOPL
         USING IOPL,R1                   #### USING IOPL
         MVC   IOPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDR TO IOPL
         MVC   IOPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDR TO IOPL
         LA    R15,GCPECB                GET ADDR OF CP ECB
         ST    R15,IOPLECB               PUT IT INTO IOPL
         XC    GCPECB,GCPECB             ZERO ECB
         LA    R15,GIKJPGPB              GET ADDR OF PUTGET PARM LIST
         ST    R15,IOPLIOPB              STORE IT IN IOPL
         XC    GPGOLD(4),GPGOLD          ZERO NUMBER OF SEGMENTS
         MVI   GPGOLD+3,1                SET NUMBER TO 1
         MVC   GPMSGHDR,PMSGHDR
         MVC   GPMSGHDR+24(8),GUSERID
         LA    R5,8
         LA    R3,GPMSGHDR+24            POINT TO THE USER ID
         LA    R4,SLASHTRT
         EX    R5,EXECTRT                FIND THE FIRST BLANK
         MVI   0(R1),C':'                PUT IN THE COLON
         LA    R15,GPMSGHDR              GET ADDR OF PSWD MSG HEADER
         ST    R15,GPGOLD+4              SAVE THAT ADDRESS
         PUTGET OUTPUT=(GPGOLD,,PTBYPS),MF=(E,GIKJIOPL)
         LTR   R15,R15                   WAS PUTGET SUCCESSFUL?
         BNZ   VUSERER7                  NO THERE IS AN ERROR
         LA    R1,GIKJPGPB               USE ADDR OF PUTGET PARM LIST
         USING PGPB,R1                   #### USING PGPB
         L     R1,PGPBIBUF               GET ADDR OF INPUT BUFFER
         DROP  R1                        #### DROP R1
         LR    R2,R1                     SAVE THAT ADDRESS
         CLI   0(R2),21                  WAS RESPONSE TO LONG?
         BH    FREEBUF                   /YES - FREE INPUT BUFFER
         CLI   1(R2),5                   WAS RESPONSE TO SHORT?
         BL    FREEBUF                   /YES - FREE INPUT BUFFER
         MVC   GPASS,BLANKS
         SR    R15,R15
         IC    R15,1(,R2)                GET INPUT LENGTH
         SH    R15,HALF5                 SUBTRACT LENGTH OF HEADER + 1
         LA    R2,4(,R2)                 GET ADDR OF INPUT BUFFER
         LA    R4,GPASS                  GET ADDR OF RECEIVING FIELD
         EX    R15,MOVEIT                MOVE INPUT TO RECEIVING FIELD
         LA    R0,1                      GET SUBPOOL NUMBER
         SLL   R0,24                     SHIFT TO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)          GET LENGTH
         FREEMAIN R,A=(1),LV=(0)         FREE THE INPUT BUFFER
         B     CHCKNEWP                  CHECK FOR NEW PASSWORD
FREEBUF  DS    0H
         LA    R0,1                      GET SUBPOOL NUMBER
         SLL   R0,24                     SHIFT TO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)          GET LENGTH
         FREEMAIN R,A=(1),LV=(0)         FREE THE INPUT BUFFER
         B     PASSDISP                  REDISPLAY THE PUTGET
         EJECT
***********************************************************************
*        CHECK NEW PASSWORD SECTION                                   *
***********************************************************************
CHCKNEWP DS    0H
         SR    R4,R4
         STH   R4,LNPASS
         LA    R4,SLASHTRT               POINT TO TRANSLATE TABLE
         LA    R3,GPASS                  POINT TO FIELD TO TRANSLATED
         LA    R5,17                     GET LENGTH TO BE TRANSLATED
         EX    R5,EXECTRT                FIND THE FIRST BLANK OR '/'?
         BZ    VUSERER8                  /NO - THERE IS AN ERROR
         LR    R2,R1                     SAVE R1
         SR    R1,R3                     GET THE PASSWORD LENGTH
         CH    R1,HALF8                  IS IT GREATER THAN 8?
         BH    VUSERER8                  /YES - THAT IS AN ERROR
         STH   R1,LPASS                  SAVE PASSWORD LENGTH
         MVC   GNPASS,BLANKS
         CLI   0(R2),C'/'                WAS A NEW PASSWORD SUPPLIED?
         BNE   TESTGET                   /NO - CHECK PASSWORD
         SR    R5,R1                     GET REMAINING LENGTH
         BCTR  R5,0                      CONVERT TO DISPLACEMENT
         LA    R3,1(,R2)                 POINT TO NEW PASSWORD
         LA    R1,GPASS+17               POINT TO END OF PASSWORD
         EX    R5,EXECTRT                FIND END OF NEW PASSWORD
         LR    R2,R3
         SR    R1,R3                     GET LENGTH OF NEW PASSWORD
         CH    R1,HALF8
         BH    VUSERER8
         STH   R1,LNPASS                 SAVE NEW PASSWORD LENGTH
         BCTR  R1,0                      CONVERT TO DISPLACEMENT
         LA    R4,GNPASS
         EX    R1,MOVEIT                 MOVE TO GNPASS
         LR    R4,R2                     POINT TO BEGINNING OF NEW PSWD
         BCTR  R4,0                      POINT TO SLASH
         LA    R2,BLANKS
         EX    R1,MOVEIT                 BLANK OUT END OF GPASS
         EJECT
***********************************************************************
*                                                                     *
*       SEARCH UADS DATA BLOCKS FOR MATCHING PASSWORD.  IF NOT FOUND, *
*       THE USER IS PROMPTED FOR ANOTHER PASSWORD.                    *
*                                                                     *
***********************************************************************
TESTGET  DS    0H
         OC    GPASS,BLANKS              CONVERT PASSWORD TO CAPS
         OC    GNPASS,BLANKS             CONVERT NEW PASSWORD TO CAPS
         L     R15,ADDRUBUF              GET ADDR OF SYS1.UADS BUFFER
         L     R1,24(,R15)               GET OFFSET OF 1ST PSWD OFF BLK
         TM    GBITS2,GRACFON            WAS RACF ACTIVE?
         BNZ   VUSEPHIT                  /YES - CHECK RACF PASSWORD
VUSEPLOP DS    0H                        SEARCH PASSWORD OFFSET BLOCKS
         L     R2,8(R15,R1)              GET OFFSET OF THIS PSWD BLK
         LA    R2,0(R15,R2)              ADD ORIGIN
         CLC   GPASS,4(R2)               IS THIS MATCHING PASSWORD?
         BE    VUSEPUAD                  /YES - GET OUT OF LOOP
         LA    R2,0(R15,R1)              NO - GET ADDR OF PSWD OFF BLK
         TM    0(R2),KBIT0               MORE PASSWORD OFFSET BLOCKS?
         BO    VBADPASS                  /NO - BAD UADS PASSWORD
         L     R1,0(,R2)                 GET OFF OF NXT PSWD OFF BLK
         B     VUSEPLOP                  AND LOOP
VUSEPUAD DS    0H                        MATCHING PASSWORD FOUND
         ST    R1,PASSOFF                SAVE OFFSET TO PSWD OFFSET BLK
         SLR   R15,R15                   SET RETURN CODE
         B     VUSEEXIT                  RETURN
VUSEPHIT DS    0H                  MATCHING PASSWORD FOUND
         ST    R1,PASSOFF          SAVE OFFSET TO PASSWORD OFFSET BLOCK
         SLR   R15,R15             SET RETURN CODE
         LA    R2,GPASS
         LA    R1,LPMRAREA
         ST    R1,WPMRAREA
         MVC   WINTY1,CINTY1             SETUP LIST FORM OF ICHEINTY
         MVC   WACTION1,CACTION1         SETUP LIST FORM OF ICHEACTNS
         MVC   WACTION2,CACTION2
         MVC   WACTION3,CACTION3
         MVC   WACTION4,CACTION4
         MVC   WTEST1,CTEST1             SETUP LIST FORM OF ICHETEST
         ICHETEST FIELD='PASSWORD',FLDATA=(8,(2)),COND=EQ,MF=(E,WTEST1)
         ICHEINTY LOCATE,TYPE='USR',ENTRY=LUSERID+1,                   X
               OPTIONS=(FLDEF,ACTION,TESTC,TESTM),                     X
               WKAREA=WPMRAREA,                                        X
               TESTS=(WTEST1),                                         X
               ACTIONS=(WACTION1,WACTION2,WACTION3,WACTION4),          X
               MF=(E,WINTY1)
         LA    R1,52                     TEST FOR INVALID PASSWORD
         CR    R1,R15                    DID PASSWORD TEST FAIL?
         BE    VUSERER8                  /YES - ASK FOR PASSWORD AGAIN
         LA    R1,12                     TEST FOR NO SUCH USER
         CR    R1,R15                    IS THIS USERID NOT ON RACF?
         BE    VUSERER3                  /YES - ASK FOR USERID AGAIN
         LTR   R15,R15                   DID EVERYTHING WORK FINE?
         BNZ   VUSERER4                  /NO - DETERMINE RACF ERROR
         SR    R1,R1
         IC    R1,WPMRAREA+29            GET LENGTH OF PROGRAMMER NAME
         STH   R1,LPRGRMR                SAVE PROGRAMMER NAME LENGTH
         LA    R1,GPRGRMR                POINT TO START OF PGMR NAME
         LA    R2,20                     POINT TO END OF PROGRAMER NAME
         MVC   GPRGRMR,WPMRAREA+30       SAVE PROGRAMMER NAME
         CLC   GPRGRMR,BLANKS
         BNE   VNAMECHK
         MVC   GPRGRMR(8),GUSERID
VNAMECHK DS    0H
         CLI   0(R1),C''''               IS THIS CHARACTER A QUOTE?
         BNE   NEXTCHR                   /NO - CHECK NEXT CHARACTER
         MVI   0(R1),C' '                /YES - MOVE IN A BLANK
NEXTCHR  DS    0H
         LA    R1,1(,R1)                 POINT TO NEXT CHARACTER
         BCT   R2,VNAMECHK               IS THIS THE LAST CHARACTER?
*                                        /NO - CHECK THE NEXT CHARACTER
*                                        /YES
         TM    WPMRAREA+60,X'80'         HAS USER BEEN REVOKED?
         BO    VUSERER9                  /YES - INFORM THE USER, & QUIT
         EJECT
***********************************************************************
*                                                                     *
*       IF NEW PASSWORD WAS NOT ENTERED AND THE CURRENT PASSWORD HAS  *
*       EXPIRED, THE USER IS PROMPTED FOR A NEW PASSWORD              *
*                                                                     *
***********************************************************************
         MVI   GCOUNTER,0
         LH    R1,LNPASS
         LTR   R1,R1                     WAS NEW PASSWORD ENTERED?
         BNZ   VUSEVERP                  /YES - VERIFY NEW PASSWORD
         MVC   WPASSINT,WPMRAREA+52      GET PASSWORD INTERVAL
         MVC   WPASSDTE,WPMRAREA+55      GET LAST CHANGE DATE
         OI    WPASSDTE+2,X'0F'          SET SIGN
         XC    TEMPPACK,TEMPPACK
         MVC   TEMPPACK+6(2),WPASSDTE+1
         CVB   R2,TEMPPACK               CONVERT DAY TO BINARY
         TIME  DEC,ZONE=LT               GET CURRENT DATE
         ST    R1,WCURDATE
         MVC   TEMPPACK+6(2),WCURDATE+2
         CVB   R1,TEMPPACK               CONVERT DAY TO BINARY
         CLC   WCURDATE+1(1),WPASSDTE    HAS YEAR CHANGED?
         BNH   VUSEXPR                   /NO - TEST FOR PASSWORD EXPIRE
         AL    R1,FULL365                ADD A YEAR TO CURRENT DAY
VUSEXPR  DS    0H
         SR    R3,R3
         IC    R3,WPASSINT               GET PASSWORD INTERVAL
         AR    R2,R3                     ADD TO LAST CHANGE DAY
         LCR   R2,R2                     COMPLEMENT IT
         AR    R1,R2                     ADD TO CURRENT DAY
         LTR   R1,R1                     IS THE RESULT NEGATIVE?
         BM    VUSEEXIT                  /YES - NEW PASSWORD NOT NEEDED
         LA    R0,L'NPASEXPR             GET LENGTH OF MESSAGE
         LA    R1,NPASEXPR               POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
VUSENPAS DS    0H
         IC    R15,GCOUNTER              INCREMENT COUNTER
         LA    R15,1(,R15)
         STC   R15,GCOUNTER
         CLI   GCOUNTER,4                HAS USER HAD THREE CHANCES?
         BNE   VUSENPPT                  /NO - ASK FOR NEW PSWD AGAIN
         B     VUSERDNY                  EXIT
         EJECT
***********************************************************************
*                                                                     *
*       IF THIS IS A NON-DISPLAY TERMINAL, OVERPRINT THE NEW PASSWORD *
*       ELSE USE PUTGET TO DO THE DISPLAY                             *
*                                                                     *
***********************************************************************
VUSENPPT DS    0H
         TM    GBITS2,GDISPLAY           IS IT A DISPLAY TUBE?
         BO    NPASDISP                  /YES - USE PUTGET
         MVC   GNEWPASM,NEWPASMG
         MVC   GNEWPASM+23(8),GUSERID
         LA    R5,8
         LA    R3,GNEWPASM+23            POINT TO THE USER ID
         LA    R4,SLASHTRT
         EX    R5,EXECTRT                FIND THE FIRST BLANK
         MVI   0(R1),C':'                PUT IN THE COLON
         LA    R0,LNEWPASM               ELSE ASK FOR NEW PASSWORD
         LA    R1,GNEWPASM
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 BY DOING A TPUT
         LTR   R15,R15                   WAS TPUT SUCCESSFUL?
         BNZ   VUSERER5                  /NO - WE HAVE AN ERROR
         LA    R0,LOVRPRNT
         LA    R1,OVERPRNT
         ICM   R1,8,PUTOPTN2
         TPUT  (1),(0),R                 DO THE OVERPRINT TPUT
         LTR   R15,R15                   WAS THAT TPUT SUCCESSFUL?
         BNZ   VUSERER5                  /NO - WE HAVE AN ERROR
         MVC   GNPASS,BLANKS
         LA    R0,L'GNPASS
         LA    R1,GNPASS
         ICM   R1,8,GETOPTN              INDICATE TGET WAIT EDIT
         TGET  (1),(0),R                 DO THE TGET
         LTR   R15,R15                   WAS THAT TGET SUCCESSFUL?
         BNZ   VUSERER6                  /NO - WE HAVE AN ERROR
         CLC   GNPASS,BLANKS             WAS BLANKS SUPPLIED?
         BE    VUSENPAS                  /YES - ASK AGAIN
         LA    R15,1
         LA    R1,GNPASS
NEWPASLP DS    0H
         CLI   0(R1),C' '
         BE    NEWPASSL
         LA    R1,1(,R1)
         LA    R15,1(,R15)
         CH    R15,HALF8
         BL    NEWPASLP
NEWPASSL DS    0H
         STH   R15,LNPASS                SAVE INPUT LENGTH
         B     VUSEVERP                  CHECK THE NEW PASSWORD
         EJECT
***********************************************************************
*        THIS SECTION PROMPTS FOR THE PASSWORD FOR DISPLAY TERMINALS  *
***********************************************************************
NPASDISP DS    0H
         MVC   GIKJPGPB(LPGPB),LPUTGET   MOVE LIST TO WORK AREA
         LA    R1,GIKJIOPL               GET ADDRESS OF IOPL
         USING IOPL,R1                   #### USING IOPL
         MVC   IOPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDR TO IOPL
         MVC   IOPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDR TO IOPL
         LA    R15,GCPECB                GET ADDR OF CP ECB
         ST    R15,IOPLECB               PUT IT INTO IOPL
         XC    GCPECB,GCPECB             ZERO ECB
         LA    R15,GIKJPGPB              GET ADDR OF PUTGET PARM LIST
         ST    R15,IOPLIOPB              STORE IT IN IOPL
         XC    GPGOLD(4),GPGOLD          ZERO NUMBER OF SEGMENTS
         MVI   GPGOLD+3,1                SET NUMBER TO 1
         MVC   GNPMSGHD,NPMSGHDR
         MVC   GNPMSGHD+28(8),GUSERID
         LA    R5,8
         LA    R3,GNPMSGHD+28            POINT TO THE USER ID
         LA    R4,SLASHTRT
         EX    R5,EXECTRT                FIND THE FIRST BLANK
         MVI   0(R1),C':'                PUT IN THE COLON
         LA    R15,GNPMSGHD              GET ADDR OF NEW PSWD MSG HDR
         ST    R15,GPGOLD+4              SAVE THAT ADDRESS
         PUTGET OUTPUT=(GPGOLD,,PTBYPS),MF=(E,GIKJIOPL)
         LTR   R15,R15                   WAS PUTGET SUCCESSFUL?
         BNZ   VUSERER7                  NO THERE IS AN ERROR
         LA    R1,GIKJPGPB               USE ADDR OF PUTGET PARM LIST
         USING PGPB,R1                   #### USING PGPB
         L     R1,PGPBIBUF               GET ADDR OF INPUT BUFFER
         DROP  R1                        #### DROP R1
         LR    R2,R1                     SAVE THAT ADDRESS
         CLI   0(R2),21                  WAS RESPONSE TO LONG?
         BH    FREEBUF4                  /YES - FREE INPUT BUFFER
         CLI   1(R2),5                   WAS RESPONSE TO SHORT?
         BL    FREEBUF4                  /YES - FREE INPUT BUFFER
         MVC   GNPASS,BLANKS
         SR    R15,R15
         IC    R15,1(,R2)                GET INPUT LENGTH
         SH    R15,HALF5                 SUBTRACT LENGTH OF HEADER + 1
         LA    R2,4(,R2)                 GET ADDR OF INPUT BUFFER
         LA    R4,GNPASS                 GET ADDR OF RECEIVING FIELD
         EX    R15,MOVEIT                MOVE INPUT TO RECEIVING FIELD
         LA    R15,1(,R15)               SET R15 BACK TO LENGTH
         STH   R15,LNPASS                SAVE INPUT LENGTH
         OC    GNPASS,BLANKS             CHANGE INPUT TO CAPS
         LA    R0,1                      GET SUBPOOL NUMBER
         SLL   R0,24                     SHIFT TO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)          GET LENGTH
         FREEMAIN R,A=(1),LV=(0)         FREE THE INPUT BUFFER
         B     VUSEVERP                  CHECK THE NEW PASSWORD
FREEBUF4 DS    0H
         LA    R0,1                      GET SUBPOOL NUMBER
         SLL   R0,24                     SHIFT TO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)          GET LENGTH
         FREEMAIN R,A=(1),LV=(0)         FREE THE INPUT BUFFER
         B     NPASDISP                  REDISPLAY THE PUTGET
         EJECT
***********************************************************************
*              MAKE SURE THAT THE NEW PASSWORD IS ACCEPTABLE          *
***********************************************************************
VUSEVERP DS    0H
         LA    R1,TEC023CD               CODE INDICATES CHECKING ONLY
         ST    R1,PARMAREA               STORE ITS ADDRESS IN PARM LIST
         LA    R1,LNPASS+1               GET NEW PASSWORD ADDRESS
         ST    R1,PARMAREA+4             STORE IT IN PARAMETER LIST
         LA    R1,LUSERID+1              GET USERID ADDRESS
         ST    R1,PARMAREA+8             STORE IT IN PARAMETER LIST
         MVI   PARMAREA+8,X'80'          INDICATE END OF PARAMETER LIST
         LA    R1,PARMAREA               SETUP R1 WITH PARM LIST ADDR
         LINK  EP=TEC023                 LINK TO PASSWORD CHECK ROUTINE
         LTR   R15,R15                   IS PASSWORD ACCEPTABLE?
         BE    VUSEEXIT                  /YES - WE ARE DONE WITH USERID
         B     VUSENPAS                  PROMPT FOR NEW PASSWORD AGAIN
         EJECT
***********************************************************************
*              BAD UADS PASSWORD SECTION                              *
***********************************************************************
VBADPASS DS    0H                        INCORRECT UADS PASSWORD
         LA    R0,L'BADPASS              GET LENGTH OF MESSAGE
         LA    R1,BADPASS                POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         IC    R15,GCOUNTER              INCREMENT COUNTER
         LA    R15,1(,R15)
         STC   R15,GCOUNTER
         CLI   GCOUNTER,4                HAS USER HAD THREE CHANCES?
         BNE   GETPASS                   /NO - REQUEST PASSWORD AGAIN
         OI    GBITS1,GDENY              DENY LOGON
         LA    R15,12
         B     VUSEEXIT                  EXIT
         EJECT
***********************************************************************
*              PASSWORD NOT ALLOWED ON LOGON LINE                     *
***********************************************************************
VUSERER1 DS    0H                        PASSWORD NOT ALLOWED ON LOGON
         LA    R0,L'PASSLOGN             GET LENGTH OF MESSAGE
         LA    R1,PASSLOGN               POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         B     VUSERDNY                  DENY LOGON
         EJECT
***********************************************************************
*              USERID WAS NOT SUPPLIED PLEASE SUPPLY USERID           *
***********************************************************************
VUSERER2 DS    0H                        PROMPT FOR USERID
         LA    R0,L'SUPUSER              GET LENGTH OF MESSAGE
         LA    R1,SUPUSER                POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         IC    R15,GCOUNTER              INCREMENT COUNTER
         LA    R15,1(,R15)
         STC   R15,GCOUNTER
         LA    R15,8
         CLI   GCOUNTER,4                HAS USER HAD THREE CHANCES?
         BNE   VUSEEXIT                  /NO - LET PARSE PROMPT
         B     VUSERDNY                  DENY LOGON
         EJECT
***********************************************************************
*              BLDL ERROR ON UADS DATA SET                            *
***********************************************************************
VUSERER3 DS    0H                        BLDL ERROR
         LA    R0,L'NOTFOUND             GET LENGTH OF MESSAGE
         LA    R1,NOTFOUND               POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         IC    R15,GCOUNTER              INCREMENT COUNTER
         LA    R15,1(,R15)
         STC   R15,GCOUNTER
         LA    R15,8
         CLI   GCOUNTER,4                HAS USER HAD THREE CHANCES?
         BNE   VUSEEXIT                  /NO - LET PARSE PROMPT
         B     VUSERDNY                  DENY LOGON
         EJECT
***********************************************************************
*              RACF ERROR                                             *
***********************************************************************
VUSERER4 DS    0H                        RACF ERROR
         MVC   GRACFERR(L'RACFERR),RACFERR  COPY MESSAGE
         CVD   R15,WORKAREA              CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'          FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GRACFERR+16(4),WORKAREA+1 MOVE INTO MESSAGE
         MVC   GRACFERR+29(8),GUSERID    MOVE ENTRY INTO MESSAGE
         LA    R0,L'RACFERR              GET LENGTH OF MESSAGE
         LA    R1,GRACFERR               POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         B     VUSERDNY                  DENY LOGON
         EJECT
***********************************************************************
*              UNSUCCESSFUL TPUT                                      *
***********************************************************************
VUSERER5 DS    0H                        UNSUCCESSFUL TPUT
         LA    R1,8
         CR    R1,R15
         BNE   VUSERDNY
         TM    GBITS1,GATTNHIT           WAS A NEW COMMAND ENTERED?
         BZ    VUSERDNY
         LA    R15,12                    TELL PARSE TO QUIT
         B     VUSEEXIT                  EXIT
         EJECT
***********************************************************************
*              UNSUCCESSFUL TGET                                      *
***********************************************************************
VUSERER6 DS    0H                        UNSUCCESSFUL TGET
         LA    R1,8
         CR    R1,R15
         BNE   VUSERDNY
         TM    GBITS1,GATTNHIT           WAS A NEW COMMAND ENTERED?
         BZ    VUSERDNY
         LA    R15,12                    TELL PARSE TO QUIT
         B     VUSEEXIT                  EXIT
         EJECT
***********************************************************************
*              UNSUCCESSFUL PUTGET                                    *
***********************************************************************
VUSERER7 DS    0H                        UNSUCCESSFUL PUTGET
         LA    R1,8
         CR    R1,R15
         BNE   VUSERDNY
         TM    GBITS1,GATTNHIT           WAS A NEW COMMAND ENTERED?
         BZ    VUSERDNY
         LA    R15,12                    TELL PARSE TO QUIT
         B     VUSEEXIT                  EXIT
         B     VUSERDNY                  DENY LOGON
         EJECT
***********************************************************************
*              PASSWORD WAS TO LONG                                   *
***********************************************************************
VUSERER8 DS    0H                        PASSWORD TO LONG
         LA    R0,L'INVPASS              GET LENGTH OF MESSAGE
         LA    R1,INVPASS                POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         LTR   R15,R15
         BZ    GETPASS                   ASK FOR PASSWORD AGAIN
         LA    R1,8
         CR    R1,R15                    WAS AN ATTENTION HIT?
         BNE   VUSERDNY                  /NO - LOG THEM OFF
         TM    GBITS2,GATTNHIT           WAS A COMMAND ENTERED?
         BZ    GETPASS                   /NO - ASK FOR PASSWORD AGAIN
         EJECT
***********************************************************************
*              REVOKED USERID MAY NOT LOGON                           *
***********************************************************************
VUSERER9 DS    0H                        NEW PASSWORD NOT ALLOWED
         LA    R0,L'REVOKMSG             GET LENGTH OF MESSAGE
         LA    R1,REVOKMSG               POINT TO MESSAGE
         ICM   R1,8,PUTOPTN
         TPUT  (1),(0),R                 ISSUE MESSAGE
         B     VUSERDNY                  DENY LOGON
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO IKJPARS.                                            *
*                                                                     *
***********************************************************************
VUSERDNY DS    0H
         OI    GBITS1,GDENY
         LA    R15,12
VUSEEXIT DS    0H
         L     R13,4(,R13)               UNCHAIN SAVE AREAS
         RETURN (14,12),RC=(15)          RETURN TO PARSE
         TITLE 'EQUATES'
R0       EQU   0                         PARAMETER REGISTER
R1       EQU   1                         PARAMETER REGISTER
R2       EQU   2                         WORK REGISTER
R3       EQU   3                   WORK REGISTER
R4       EQU   4                   WORK REGISTER
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8                   USED BY PARSE EXIT FOR DYN AREA
R9       EQU   9                   FIRST BASE REG - VALUE EP+0
R10      EQU   10                  SECOND BASE REG - VALUE EP+4K
R11      EQU   11                  THIRD BASE REG - VALUE EP+8K
R12      EQU   12                  FOURTH BASE REG - VALUE EP+12K
R13      EQU   13                  SAVE AREA & DYNAMIC AREA POINTER
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  RETURN CODE
         SPACE 3
KBIT0    EQU   X'80'
KBIT1    EQU   X'40'
KBIT2    EQU   X'20'
KBIT3    EQU   X'10'
KBIT4    EQU   X'08'
KBIT5    EQU   X'04'
KBIT6    EQU   X'02'
KBIT7    EQU   X'01'
         TITLE 'CONSTANTS'
         LTORG
MOVEIT   MVC   0(0,R4),0(R2)
COMPARIT CLC   0(0,R4),0(R2)
EXECTRT  TRT   0(0,R3),0(R4)
TYPEUSR  DC    C'USR'
CINTY1   ICHEINTY LOCATE,TYPE='USR',ACTIONS=(*-*,*-*,*-*,*-*),         X
               WKAREA=*-*,TESTS=(*-*),MF=L
LINTY1   EQU   *-CINTY1
CINTY2   ICHEINTY LOCATE,TYPE='CON',ACTIONS=(*-*),WKAREA=*-*,MF=L
LINTY2   EQU   *-CINTY2
CACTION1 ICHEACTN FIELD=PGMRNAME,MF=L
LACTION1 EQU   *-CACTION1
CACTION2 ICHEACTN FIELD=PASSINT,MF=L
LACTION2 EQU   *-CACTION2
CACTION3 ICHEACTN FIELD=PASSDATE,MF=L
LACTION3 EQU   *-CACTION3
CACTION4 ICHEACTN FIELD=FLAG4,MF=L
LACTION4 EQU   *-CACTION4
CTEST1   ICHETEST FIELD=PASSWORD,FLDATA=(,*-*),COND=EQ,MF=L
LTEST1   EQU   *-CTEST1
CTEST2   ICHETEST FIELD=FLAG4,FLDATA=(,*-*),COND=EQ,MF=L
LTEST2   EQU   *-CTEST2
LESTAE   ESTAE ,MF=L               ESTAE PARAMETER LIST
LPUTGET  PUTGET ,MF=L              PUTGET PARAMETER LIST
FULL4    DC    F'4'                FOR DIVISION
FULL7    DC    F'7'                FOR DIVISION
FULL100  DC    F'100'              FOR DIVISION
FULL365  DC    F'365'              FOR ADDITION (ONE YEAR)
FULL400  DC    F'400'              FOR DIVISION
FULL1000 DC    F'1000'             FOR DIVISION
FULL2000 DC    F'2000'             FOR DIVISION
FULL4000 DC    F'4000'             FOR DIVISION
HALF4    DC    H'4'                FOR SUBTRACTION
HALF5    DC    H'5'                FOR SUBTRACTION
HALF8    DC    H'8'                FOR COMPARISON
HALF12   DC    H'12'               FOR MULTIPLICATION
HALF45   DC    H'45'               FOR COMPARISON
HALF80   DC    H'80'               FOR COMPARISON
HALF90   DC    H'90'               FOR COMPARISON
HALF365  DC    H'365'              FOR MULTIPLICATION
HALF522  DC    H'522'              FOR COMPARISON
*                HHMMSSTH
LOGTIME  DC    C'00050000'         WALL TIME FOR LOGON TO COMPLETE
KLOGON   DC    C'LOGON'            VALID COMMAND NAME
KLOGOFF  DC    C'LOGOFF'           VALID COMMAND NAME
PUTOPTN  DC    AL1(8)              WAIT HOLD EDIT (TPUT OPTIONS)
PUTOPTN2 DC    XL1'9'              WAIT HOLD ASIS (TPUT OPTIONS)
GETOPTN  DC    XL1'80'             WAIT EDIT (TGET OPTIONS)
$SYSTEM$ DC    CL8'$SYSTEM$'       MEMBER NAME OF $SYSTEM$ BLOCK
TEC023CD DC    XL1'01'             TEC023 CODE INDICATES CHECK PASSWORD
BLANKS   DC    CL80' '             BLANKS
         DC    X'0D'               CARRIAGE RETURN
         DC    17X'16'             RUBOUT
OVERPRNT DC    17C'X'              X'S
         DC    X'0D'               CARRIAGE RETURN
         DC    17X'16'             RUBOUT
         DC    17C'O'              O'S
         DC    X'0D'               CARRIAGE RETURN
         DC    17X'16'             RUBOUT
         DC    17C'A'              A'S
         DC    X'0D'               CARRIAGE RETURN
LOVRPRNT EQU   *-OVERPRNT          LENGTH FOR TPUT
         DC    X'25'               LINE FEED
TRTABLE  DC    C'0123456789ABCDEF' TRANSLATE HEX TO EBCDIC
SLASHTRT DC    256X'00'            TRANSLATE TABLE FOR SLASH AND BLANK
         ORG   SLASHTRT+C' '
         DC    C' '
         ORG   SLASHTRT+C'/'
         DC    C'/'
         ORG   ,
OPENUPDT OPEN  (SYSUADS,INPUT),MF=L OPEN FOR INPUT
SYSUADS  DCB   DDNAME=SYSUADS,MACRF=(R),DSORG=PO
LSYSUADS EQU   *-SYSUADS           CALCULATE LENGTH OF SYSUADS DCB
KEYZERO  MODESET KEY=ZERO,MF=L
NONZERO  MODESET KEY=NZERO,MF=L
ASCBWORD DC    C'ASCB'             ACRONYM FOR ASCB
QNAME    DC    C'SYSIJDUA'         QNAME FOR ENQ
ENQLIST  ENQ   (,,E,,SYSTEM),RET=TEST,MF=L
         DS    F
LENQLIST EQU   *-ENQLIST
SDUMP    SDUMP MF=L                SDUMP PARAMETER LIST
LSDUMP   EQU   *-SDUMP             CALCULATE LENGTH OF SDUMP PARM
ZEROES   DC    F'0'                CONSTANT FOR COMPARISON
HEX02    DC    X'02'               TPUT CONTROL OPTIONS
HEX03    DC    X'03'               TPUT FULLSCR OPTIONS
HEX80    DC    X'80'               TGET OPTIONS
*                HHMMSSTH
WAITTIME DC    C'00000500'         5 SEC WAIT FOR TUBES
DATEMASK DC    X'4021204B202020'   EDIT MASK FOR DATE (BYY.DDD)
MSGCLASS DC    C',MSGCLASS='       MSGCLASS KEYWORD
REGIONEQ DC    C',REGION='         REGION KEYWORD
NXTBLANK DC    64X'00',C' ',191X'00' TRANSLATE TABLE FOR NEXT BLANK
         TITLE 'EXECUTED INSTRUCTIONS'
MVCACCT  MVC   GACCTNUM(*-*),0(R1) MOVE ACCOUNT NUMBER
         TITLE 'MESSAGES'
*                          1         2         3         4         5
*                01234567890123456789012345678901234567890123456789012
WELCOMSG DC    C'RAINIER BANK TSO, SYSTEM XXXX, TERMINAL XXXXXXXX'
ESTAEMSG DC    C'ESTAE UNSUCCESSFUL - RC=XXXX'
STAXMSG  DC    C'STAX UNSUCCESSFUL - RC=XXXX'
TGETMSG  DC    C'TGET UNSUCCESSFUL - RC=XXXX'
STOWMSG  DC    C'STOW UNSUCCESSFUL - RC=XXXX'
SCANMSG  DC    C'IKJSCAN UNSUCCESSFUL - RC=XXXX'
GFAILMSG DC    C'GNRLFAIL UNSUCCESSFUL - RC=XXXX'
RACFERR  DC    C'RACF ERROR - RC=XXXX USER  -                  '
RACFER2  DC    C'RACF ERROR - RC=XXXX GROUP -                  '
PMSGHDR  DC    AL2(LPMSGHDR,0),C' '
PASSMSG  DC    C'ENTER PASSWORD FOR XXXXXX  '
LPASSMSG EQU   *-PASSMSG
LPMSGHDR EQU   *-PMSGHDR
NPMSGHDR DC    AL2(LNPMSGHD,0),C' '
NEWPASMG DC    C'ENTER NEW PASSWORD FOR XXXXXXXX '
LNEWPASM EQU   *-NEWPASMG
LNPMSGHD EQU   *-NPMSGHDR
ABNDMSG  DC    C'LOGON USER EXIT ABEND      '
STAXOBUF DC    C'ENTER ''LOGON'' OR ''LOGOFF'' -'
RESCFAIL DC    C'RESOURCE FAILURE. CONTACT TECHNICAL SUPPORT'
LOGABEND DC    C'LOGON ABEND. CONTACT TECHNICAL SUPPORT'
OPENMSG  DC    C'OPEN OF SYS1.UADS UNSUCCESSFUL'
$SYSMSG  DC    C'$SYSTEM$ NOT PRESENT. CONTACT TECHNICAL SUPPORT'
ECMNDMSG DC    C'COMMAND BUFFER IS EMPTY - LOGON'
BCMNDMSG DC    C'COMMAND IS NOT ''LOGON'''
PASSLOGN DC    C'PASSWORD NOT ALLOWED ON LOGON LINE - BYE'
NEWPASLG DC    C'NEW PASSWORD NOT ALLOWED ON LOGON LINE - BYE'
REVOKMSG DC    C'YOU HAVE BEEN REVOKED - YOU MAY NOT LOGON'
SUPUSER  DC    C'PLEASE SUPPLY YOUR USERID'
NOTFOUND DC    C'USER NOT AUTHORIZED TO USE TIME-SHARING'
BADPASS  DC    C'PASSWORD NOT AUTHORIZED FOR THIS USERID'
NPASEXPR DC    C'PASSWORD HAS EXPIRED'
INVPASS  DC    C'INVALID PASSWORD'
INVLDPRC DC    C'INVALID PROCEDURE NAME'
INVLDACT DC    C'INVALID ACCOUNT NUMBER'
PROCHDR  DC    AL2(LPROCMSG,0),C' '
PROCMSG  DC    C'ENTER PROCEDURE NAME: '
LPROCMSG EQU   *-PROCHDR
ACCTHDR  DC    AL2(LACCTMSG,0),C' '
ACCTMSG  DC    C'ENTER ACCOUNT NUMBER: '
LACCTMSG EQU   *-ACCTHDR
PRMPTMSG DC    C'PROMPT LIMIT EXCEEDED'
BADPROC  DC    C'PROCEDURE IGNORED'
PERFMSG  DC    C'TEC00201 - PERFORMANCE GROUP IGNORED'
DUPUSER  DC    C'USERID CURRENTLY LOGGED ON'
BADRECON DC    C'RECONNECT SPECIFIED BUT USERID NOT LOGGED ON'
*                     1         2         3         4         5
*               4567890123456789012345678901234567890123456789012345678
ABNDWTO  WTO   'UNKNOWN ON TERMINAL LOGON USER EXIT ABEND       - ASID X
               X''NN''',DESC=2,ROUTCDE=(2,11),MF=L
*              90 123
*               6
LABNDWTO EQU   *-ABNDWTO           CALCULATE LENGTH OF WTO MESSAGE
*                     1         2         3         4         5
*               4567890123456789012345678901234567890123456789012345678
TIMERWTO WTO   'UNKNOWN ON TERMINAL LOGON TERMINATED DUE TO WAIT TIME EX
               XCEEDED - ASID X''NN''',ROUTCDE=(2,11),MF=L
*              90123456789012345 678
*               6         7
LTIMRWTO EQU   *-TIMERWTO          CALCULATE LENGTH OF WTO MESSAGE
*                     1         2         3         4
*               4567890123456789012345678901234567890 123
LOGWTO   WTO   'UNKNOWN LOGGING ON TERMINAL - ASID X''NN''',           X
               MCSFLAG=HRDCPY,MF=L
LLOGWTO  EQU   *-LOGWTO            CALCULATE LENGTH OF WTO MESSAGE
KMSG     DC    X'40115D7E114040133C404000'  CLEAR SCREEN
KMSGL    EQU   *-KMSG
         TITLE 'PARS PARAMETER LIST'
         PUSH  PRINT
         PRINT NOGEN
         SPACE 5
PCLADDR  DC    A(PARMTAB)          ADDRESS OF PARS PARAMETER LIST
PARMTAB  IKJPARM DSECT=IKJPARMD    START OF PCL
PUSERID  IKJPOSIT UID2PSWD,PROMPT='USERID', USERID/PASSWORD/NEWPSWD    X
               VALIDCK=VUSERID
PACCTNUM IKJKEYWD
         IKJNAME 'ACCT',SUBFLD=ACCTSUB
PPROC    IKJKEYWD
         IKJNAME 'PROC',SUBFLD=PROCSUB
PSIZE    IKJKEYWD
         IKJNAME 'SIZE',SUBFLD=SIZESUB
PGRUP    IKJKEYWD
         IKJNAME 'GROUP',SUBFLD=GRUPSUB
PRECON   IKJKEYWD
         IKJNAME 'RECONNECT'
PMAIL    IKJKEYWD DEFAULT='MAIL'
         IKJNAME 'MAIL'
         IKJNAME 'NOMAIL'
PNOTICES IKJKEYWD DEFAULT='NOTICES'
         IKJNAME 'NOTICES'
         IKJNAME 'NONOTICES'
POIDCARD IKJKEYWD
         IKJNAME 'OIDCARD'
PMSGCLAS IKJKEYWD
         IKJNAME 'MSGCLASS',SUBFLD=MSGCSUB
         SPACE 3
*        BEGIN SUBFIELDS
ACCTSUB  IKJSUBF
SACCT    IKJIDENT 'ACCOUNT NUMBER',FIRST=ALPHANUM,MAXLNTH=40,          X
               OTHER=ALPHANUM,PROMPT='ACCOUNT NUMBER',VALIDCK=VACCT
         SPACE 3
PROCSUB  IKJSUBF
SPROC    IKJIDENT 'PROCEDURE NAME',FIRST=ALPHA,MAXLNTH=8,              X
               OTHER=ALPHANUM,PROMPT='PROCEDURE NAME',VALIDCK=VPROC
         SPACE 3
*
PERFSUB  IKJSUBF
SPERF    IKJIDENT 'PERFORMANCE GROUP',MAXLNTH=2,FIRST=NUMERIC,         X
               OTHER=NUMERIC,PROMPT='PERFORMANCE GROUP',VALIDCK=VPERF
         SPACE 3
*
SIZESUB  IKJSUBF
SSIZE    IKJIDENT 'REGION SIZE',FIRST=NUMERIC,MAXLNTH=4,OTHER=NUMERIC  X
               PROMPT='REGION SIZE'
         SPACE 3
*
GRUPSUB  IKJSUBF
SGRUP    IKJIDENT 'RACF GROUP',MAXLNTH=8,FIRST=NONATNUM,               X
               OTHER=NONATNUM,PROMPT='RACF GROUP',VALIDCK=VGRUP
         SPACE 3
*
MSGCSUB  IKJSUBF
SMSGC    IKJIDENT 'MSGCLASS',FIRST=NONATNUM,MAXLNTH=1,PROMPT='MSGCLASS'
         SPACE 3
         IKJENDP
         SPACE 5
         POP   PRINT
         TITLE 'MODEL JCL CARDS'
LJCLMODL DC    0F'0',C' ',AL3(LENJMODL) FILL CHARACTER AND LENGTH
JCLMODEL DS    0C                  START OF MODEL FOR JCL
*                             1         2          3         4
*                   012345678901234567890123 456789012345678901234 5
MJOBCARD DC    CL80'//         JOB  '
MEXECARD DC    CL80'//         EXEC '
MJOBPARM DC    CL80'/*JOBPARM '
LENJMODL EQU   *-JCLMODEL          CALCULATE LENGTH OF JCL MODEL
         TITLE 'PATCH AREA'
         ENTRY PATCH               PUT PATCH ADDRESS IN LKED MAP
PATCH    DC    50S(*)              GENERATE SCONS FOR PATCH AREA
         TITLE 'CBUF DSECT'
CBUF     DSECT                     COMMAND BUFFER DSECT
CBUFLEN  DS    H                   LENGTH
CBUFOFFS DS    H                   OFFSET
CBUFTEXT DS    C                   TEXT
         TITLE 'STAX PARAMETER LIST DSECT'
STX      DSECT
STXEXIT  DS    F                   ADDRESS OF STAX EXIT
STXISIZ  DS    H                   LENGTH OF IBUF
STXOSIZ  DS    H                   LENGTH OF OBUF
STXOBUF  DS    F                   ADDRESS OF OBUF
STXIBUF  DS    F                   ADDRESS OF IBUF
STXOPTS  DS    X                   OPTION FLAGS
STXUSER  DS    XL3                 ADDRESS OF USER AREA
         TITLE 'ATTENTION EXIT PARAMETER LIST DSECT'
AEPL     DSECT
AEPLTAIE DS    F                   ADDRESS OF THE TAIE
AEPLIBUF DS    F                   ADDRESS OF IBUF
AEPLUSAD DS    F                   ADDRESS OF USER AREA
         TITLE 'SYSTEM DSECTS NOT PRINTED'
         PUSH  PRINT
         PRINT NOGEN               SUPPRESS PRINTING OF DSECTS
         IKJCPPL ,                 GENERATE CPPL DSECT
         DS    0F
LCPPL    EQU   *-CPPL              CALCULATE LENGTH OF CPPL
         IKJTAIE ,                 GENERATE TAIE DSECT
         IKJCSPL ,                 GENERATE CSPL DSECT
         DS    0F
LCSPL    EQU   *-CSPL              CALCULATE LENGTH OF CSPL
         IKJCSOA ,                 GENERATE CSOA DSECT
         DS    0F
LCSOA    EQU   *-CSOA              CALCULATE LENGTH OF CSOA
         IKJPPL ,                  GENERATE PPL DSECT
         DS    0F
LPPL     EQU   *-PPL               CALCULATE LENGTH OF PPL
         IKJEFFGF GFDSECT=YES      GENERATE GNRLFAIL DSECT
         IKJPGPB ,                 GENERATE PGPB DSECT
         DS    0F
LPGPB    EQU   *-PGPB              CALCULATE LENGTH OF PGPB
         IKJIOPL ,                 GENERATE IOPL DSECT
         DS    0F
LIOPL    EQU   *-IOPL              CALCULATE LENGTH OF IOPL
         IHAASVT ,                 GENERATE ASVT DSECT
         IHAASCB ,                 GENERATE ASCB DSECT
         IHAASXB ,                 GENERATE ASXB DSECT
         IKJTSB ,                  GENERATE TSB DSECT
         CVT   DSECT=YES           GENERATE CVT DSECT
         DCBD  DSORG=PS,DEVD=DA    GENERATE DCB DSECT
         IHASDWA ,                 GENERATE SDWA DSECT
         IHAPSA ,                  GENERATE PSA DSECT
         IKJTCB ,                  GENERATE TCB DSECT
         IEZJSCB ,                 GENERATE JSCB DSECT
         IEESMCA ,                 GENERATE SMCA DSECT
         IKJPSCB ,                 GENERATE PSCB DSECT
         POP   PRINT
         TITLE 'LWA DSECT'
LWA      DSECT
         ORG   LWA+X'18'
LWAPSCB  DS    F                   ADDRESS OF PSCB
         ORG   LWA+X'3B'
LWAFLGS4 DS    X
LWAILGN  EQU   KBIT7               INDICATES INITIAL LOGON
         TITLE 'UPT DSECT'
UPT      DSECT
         DS    H                   RESERVED
UPTUSER  DS    CL10                RESERVED FOR INSTALLATION USE
UPTSWS   DS    X                   USERS ENVIRONMENT SWITCHES
UPTMID   EQU   KBIT2               PRINT MESSAGE IDENTIFICATION
UPTCDEL  DS    X                   CHAR DELETE CHARACTER
UPTLDEL  DS    X                   LINE DELETE CHARACTER
         DS    X                   RESERVED
UPTPREFX DS    CL7                 DSNAME PREFIX
UPTPREFL DS    X                   LENGTH OF DSNAME PREFIX
         TITLE 'DYNAMIC AREA'
GOTNAREA DSECT
SAVEAREA DS    9D                  MAIN LINE SAVE AREA
VERSAVEA DS    9D                  VERIFY ROUTINES SAVE AREA
WORKAREA DS    D                   DOUBLE WORD WORK AREA
DAYSWORK DS    D                   WORK AREA FOR DAYS SUBROUTINE
GDLMSAVE DS    4F                  SAVE AREA FOR GETDVLIM SUBR
GDLMYEAR DS    F                   GETDVLIM YEAR
GDLMDAY  DS    F                   GETDVLIM DAY
ADDRPARM DS    F                   ADDRESS OF PARMS
ADDRUBUF DS    F                   ADDRESS OF SYS1.UADS BUFFER
ADDRINFO DS    F                   ADDRESS OF SYS1.UADS BUFFER
ADDR$SYS DS    F                   ADDRESS OF SYS1.UADS BUFFER
ADDRUCB  DS    F                   ADDRESS OF SYS1.UADS UCB
PASSOFF  DS    F                   OFFSET TO MATCHING PSWD OFFSET BLOCK
EXLST    DS    F                   DCB EXIT LIST FOR RDJFCB
PARMAREA DS    5F                  PARAMETER LIST
GJFCB    DS    XL176               JFCB AREA
GMSGAREA DS    CL100               COMMON MESSAGE AREA
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GWELCMSG DS    CL(L'WELCOMSG)      WELCOME MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GESTAMSG DS    CL(L'ESTAEMSG)      ESTAE MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GRACFERR DS    CL(L'RACFERR)       RACF MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GRACFER2 DS    CL(L'RACFER2)       RACF MESSAGE FOR GROUP
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GSTAXMSG DS    CL(L'STAXMSG)       STAX MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GSTOWMSG DS    CL(L'STOWMSG)       STOW MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GTGETMSG DS    CL(L'TGETMSG)       TGET MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GLOGWTO  DS    CL(LLOGWTO)         LOGON WTO MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GSCANMSG DS    CL(L'SCANMSG)       IKJSCAN MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GGFALMSG DS    CL(L'GFAILMSG)      GNRLFAIL MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GABNDMSG DS    CL(L'ABNDMSG)       ABEND MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GABNDWTO DS    CL(LABNDWTO)        ABEND WTO MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GPMSGHDR DS    CL(LPMSGHDR)        PASSWORD MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GPASSMSG DS    CL(LPASSMSG)        PASSWORD MESSAGE FOR NON-DISPLAY
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GNEWPASM DS    CL(LNEWPASM)        NEW PASSWORD MESSAGE FOR NON-DISPLAY
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GNPMSGHD DS    CL(LNPMSGHD)        NEW PASSWORD MESSAGE
         ORG   ,                   GET BACK IN SYNC
GCOUNTER DS    X                   PROMPT COUNTER
         DS    0D
TEMPPACK DS    PL8                 TEMPORARY PACKED FIELD
WCURDATE DS    PL4                 CURRENT DATE
WPASSDTE DS    PL4                 PASSWORD LAST CHANGE DATE
WPASSINT DS    XL1                 PASSWORD CHANGE INTERVAL
         DS    0F
WINTY1   DS    XL(LINTY1)
         DS    0F
WINTY2   DS    XL(LINTY2)
         DS    0F
WACTION1 DS    XL(LACTION1)
         DS    0F
WACTION2 DS    XL(LACTION2)
         DS    0F
WACTION3 DS    XL(LACTION3)
         DS    0F
WACTION4 DS    XL(LACTION4)
         DS    0F
WTEST1   DS    XL(LTEST1)
         DS    0F
WTEST2   DS    XL(LTEST2)
         DS    0D
WPMRAREA DS    XL100
LPMRAREA EQU   *-WPMRAREA
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR THE CPPL
         DS    0F                  ALIGN FOR CPPL
GCPPL    DS    XL(LCPPL)           CPPL
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR THE CBUF
GCBUF    DS    XL256               CBUF
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR ESTAE
GESTAE   ESTAE ,MF=L               ESTAE PARAMETER LIST
LGESTAE  EQU   *-GESTAE            CALCULATE LENGTH OF ESTAE PARM LIST
ESTAEPRM DS    5F                  ESTAE EXIT PARAMETER LIST
ABENDCDE DS    F                   ABEND CODE FROM ESTAE EXIT
ABENDPSW DS    CL8                 ABEND PSW FROM ESTAE EXIT
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR STAX
GDSTAX   STAX  ,MF=L               DUMMY STAX PARAMETER LIST
GSTAX    STAX  ,MF=L               STAX PARAMETER LIST
LSTAX    EQU   *-GSTAX             CALCULATE LENGTH OF STAX PRM LIST
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR SAVE AREAS FOR INTERNAL ROUTINES
HSKPSAVE DS    F                   HOUSKEEPING ROUTINE
SCANSAVE DS    2F                  SCAN COMMAND BUFFER ROUTINE
PARSSAVE DS    2F                  PARSE COMMAND BUFFER ROUTINE
WELCSAVE DS    2F                  PARSE COMMAND BUFFER ROUTINE
GDATSAVE DS    2F                  GET DATA FROM PDL ROUTINE
GPASSAVE DS    F                   PROMPT FOR PASSWORD ROUTINE
GACTSAVE DS    F                   PROMPT FOR ACCOUNT NUMBER ROUTINE
GNPASAVE DS    F                   PROMPT FOR NEW PASSWORD ROUTINE
VDATSAVE DS    2F                  VERIFY DATA ROUTINE
UDATSAVE DS    3F                  UPDATE DATA ROUTINE
BJCLSAVE DS    4F                  BUILD JCL SUBROUTINE
GFRSTPRC DS    F                   ADDR OF FIRST PROC OFFSET BLK
GFRSTACT DS    F                   ADDR OF FIRST ACCT OFFSET BLK
GACTADDR DS    F                   ADDR OF ACCT OFFSET BLK SELECTED
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR STAX AND STIMER EXITS
         DS    0F                  FORCE ALIGNMENT
GTIMRXIT DS    XL(LTIMRXIT)        STIMER EXIT ROUTINE IS MOVED HERE
GTIMRWTO DS    CL(LTIMRWTO)        TIMER WTO MESSAGE
GCPECB   DS    F                   CP ECB
GBITS    DS    0F                  SWITCHES
GBITS1   DS    X                   SWITCHES
GDENY    EQU   KBIT0               LOGON DENIED
GRECON   EQU   KBIT1               'RECON' SPECIFIED
GNOMAIL  EQU   KBIT2               'NOMAIL' SPECIFIED
GNONOT   EQU   KBIT3               'NONOTICES' SPECIFIED
GDEBUG   EQU   KBIT4               'DEBUG' SPECIFIED
GDUPUSER EQU   KBIT5               DUPLICATE USERID LOGGED ON
GLIST    EQU   KBIT6               'LISTINFO' SPECIFIED
GATTNHIT EQU   KBIT7               ATTENTION HIT
GBITS2   DS    X                   SWITCHES
GSPFRES  EQU   KBIT0               DSN RESERVED
GSPFENQ0 EQU   KBIT1               MEMBER0 ENQ'D ON
GSPFENQI EQU   KBIT2               INFO BLOCK ENQ'D ON
GJCL     EQU   KBIT3               'JCL' SPECIFIED
GABEND   EQU   KBIT4               ABEND HAS OCCURED
GDISPLAY EQU   KBIT5               DISPLAY TERMINAL
GRACFON  EQU   KBIT6               RACF IS ACTIVE
GBITS3   DS    X                   SWITCHES
GBITS4   DS    X                   SWITCHES
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR SCAN
GIKJCSPL DS    XL(LCSPL)           CSPL
GCSPLFLG DS    F                   CSPL FLAG
GCSPLOA  DS    XL(LCSOA)           CSPL OUTPUT AREA
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR PARSE
GIKJPPL  DS    XL(LPPL)            PPL
GPPLANS  DS    F                   PPL PDL
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR GNRLFAIL
GGFPARMS DS    XL(GFLENGF)         GNRLFAIL PARAMETER LIST
GGFPARMP DS    F                   GNRLFAIL PARM LIST POINTER
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR PUTGET
GIKJPGPB DS    XL(LPGPB)           PGPB
GIKJIOPL DS    XL(LIOPL)           IOPL
GPGOLD   DS    2F                  OUTPUT LINE DESCRIPTOR (OLD)
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR INFO RETRIEVED FROM PDL & UADS
LUSERID  DS    H                   LENGTH OF USERID
GUSERID  DS    CL8                 USERID
GMEMBER  DS    CL8                 MEMBER NAME
LPASS    DS    H                   LENGTH OF PASSWORD
GPASS    DS    CL17                PASSWORD
LUSERGRP DS    H                   LENGTH OF USER AND GROUP
GUSERGRP DS    CL17                USER AND GROUP (FOR RACF CONNECT)
LNPASS   DS    H                   LENGTH OF NEW PASSWORD
GNPASS   DS    CL8                 NEW PASSWORD
GACCTNUM DS    CL40                ACCOUNT NUMBER
GPRGRMR  DS    CL20                PROGRAMMER NAME
GPROC    DS    CL8                 PROCEDURE NAME
GGRUP    DS    CL8                 RACF GROUP NAME
GPERF    DS    CL8                 PERFORMANCE GROUP
GNEWPSWD DS    CL8                 NEW PASSWORD
GSIZE    DS    CL4                 SIZE
GTERMID  DS    CL8                 TERMINAL ID FROM TSB
GASID    DS    CL2                 ASID FROM ASCB
GSID     DS    CL4                 SID FROM SMCA
LPROC    DS    H                   LENGTH OF PROCEDURE NAME
LPERF    DS    H                   LENGTH OF PERFORMANCE GROUP
LGRUP    DS    H                   LENGTH OF RACF GROUP NAME
LACCTNUM DS    H                   LENGTH OF ACCOUNT NUMBER
LPRGRMR  DS    H                   LENGTH OF PROGRAMMER NAME
GMSGCLAS DS    C                   MSGCLASS
         SPACE 5
* THE FOLLOWING STORAGE IS FOR ACCESS OF SYS1.UADS
         DS    0F                  ALIGN FOR OPEN LIST
GOPENLST DS    F                   OPEN LIST
GTTRUSER DS    F                   TTR OF FIRST BLOCK
GLENUSER DS    F                   LENGTH OF FIRST BLOCK
GSYSUADS DS    XL(LSYSUADS)        DCB FOR SYS1.UADS
         READ  DECB,SF,MF=L        READ/WRITE DECB LIST
         SPACE 5
* THE FOLLOWING STORAGE IS FOR ENQ
         DS    0F                  ALIGNMENT
GENQLIST DS    XL(LENQLIST)        ENQ PARAMETER LIST
         SPACE 5
* THE FOLLOWING STORAGE IS FOR SDUMP
         DS    0F                  ALIGNMENT
GSDUMP   DS    XL(LSDUMP)          SDUMP PARAMETER LIST
         SPACE 5
* THE FOLLOWING STORAGE IS FOR BLDL
GBLDL    DS    0F                  BLDL LIST
GBLL     DS    H                   LL
GBFF     DS    H                   FF
GBNAME   DS    CL8                 NAME
GBTTR    DS    XL4                 TTRC
         DS    0D                  FILL OUT DSECT
GOTNLEN  EQU   *-GOTNAREA          LENGTH OF DYNAMIC AREA
         END
