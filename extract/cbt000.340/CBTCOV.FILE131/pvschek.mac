         TITLE 'S A D S C  -   PROJECT VALIDATION SYSTEM'
*---------------------------------------------------------------------*
*                                                                     *
*                   P  V  S  C  H  E  K                               *
*                                                                     *
*                                                                     *
* COURTESY =  JIM MARSHALL, CAPT, USAF                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7             WORK REGISTER
R8       EQU   8             WORK REGISTER
R9       EQU   9
R10      EQU   10
R11      EQU   11            RESERVED - FUTURE 2ND BASE REG
R12      EQU   12            BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15            ENTRY ADDRESS
         EJECT
PVSCHEK  CSECT
         SAVE  (14,12),,PVSCHEK_SADSC_&SYSTIME_&SYSDATE
         LR    R12,R15                 LOAD REGISTER 12 FROM 15
*        LR    R11,R12                 ESTABLISH
*        LA    R11,4095(R11)               2ND BASE    (FOR FUTURE)
*        LA    R11,1(R11)                    REGISTER
*        USING PVSCHEK,R12,R11         ESTABLISH ADDRESSABILITY TO
         USING PVSCHEK,R12             ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LR    R9,R1                   SAVE ADDR OF DATA PASSED TO ME
         LA    R0,ENDDSA-DSA           LENGTH FOR GOTTEN AREA
         O     R0,SUBPOOL              INDICATE SUBPOOL 241
         GETMAIN R,LV=(0)              GO DO IT
         USING DSA,R1                  TEMPORARY ADDRESSABILITY
         XC    SAVEAREA,SAVEAREA       CLEAR SAVE AREA
         ST    R13,SAVEAREA+4          PLUG IN LOW SAVE
         ST    R1,8(R13)               PLUG IN HIGH SAVE
         LR    R13,R1                  POINT TO GETMAIN AREA
         DROP  R1                      RID OURSELVES OF R1
         USING DSA,R13                 RE-ESTABLISH R13 AS BASE
         SPACE
*---------------------------------------------------------------------*
*        FIRST WE SEE IF THIS IS A REAL REQUEST ?  IF THIS HAS
*        A CODE OF X'FAB0' WE ARE GOING TO DO A FULL CHECK.
*        IF A CODE OF X'FAB1' COMES IN THEN WE WILL ONLY CHECK
*        THE 1ST 2 CHARACTERS OF THE JOB NAME.
*---------------------------------------------------------------------*
         SPACE
         MVC   PVSCODE(12),0(R9)       MOVE IN THE PASSED DATA
         CLC   CHKCODE(2),=X'FAB0'     CHECK THE 1ST 2 BYTES
         BE    LOADUP
         CLC   CHKCODE(2),=X'FAB1'     CHECK THE 1ST 2 BYTES
         BNE   RETURN8
LOADUP   EQU   *
         LA    R7,SCCTABLE             PT TO SCCTABLE
         SPACE
*---------------------------------------------------------------------*
*        NOW WE LOOP THROUGH THE TO FIND IF THE 1ST TWO CHARS ARE
*        VALID. IF IT IS VALID PICK UP THE ADDRESS TO CHECK THE
*        REST OF THE FIELDS.
*---------------------------------------------------------------------*
         SPACE
SCCLOOP  EQU   *
         CLI   0(R7),X'FF'             ARE WE AT THE END OF TABLE
         BE    RETURN8                 IF SO WE RETURN WITH RC=8
         CLC   COSTCODE(2),0(R7)       FIND A MATCH ??
         BE    CONTCHK                 PRESS ON WITH THE CHECK
         LA    R7,SCCLEN(R7)           MOVE THE POINTER
         B     SCCLOOP                 KEEP ON CHECKING
         SPACE
*---------------------------------------------------------------------*
*        NOW WE HAVE TO CHECK THE ACTUAL CODE.
*---------------------------------------------------------------------*
         SPACE
CONTCHK  EQU   *
         CLC   CHKCODE(2),=X'FAB1'     DO WE CONT WITH THE CHECK
         BE    RETURN0                 NO, RETURN EARLY
         SPACE
CONT     EQU   *
         MVC   SCCADDR(4),2(R7)        GET THE ADDRESS
         L     R7,SCCADDR              LOAD THE ADDRESS OF 'THE' TABLE
         SPACE
TABLOOP  EQU   *
         CLI   0(R7),X'FF'             ARE WE AT END OF THIS TABLE
         BE    RETURN8                 BAD CODE
         CLC   PROJCODE(8),0(R7)       IS IT THERE
         BE    RETURN0                 GREAT GO BACK WITH RC=0
         LA    R7,CODELEN(R7)          BUMP THE POINTER
         B     TABLOOP                 LOOP BACK THROUGH
         SPACE
RETURN0  EQU   *
         LA    R0,ENDDSA-DSA           GET LENGTH OF AREA WE GOT
         O     R0,SUBPOOL              GET SUBPOOL NUMBER
         LR    R1,R13                  GET ADDRESS OF GOTTEN AREA
         L     R13,SAVEAREA+4          GET LOW SAVE ADDRESS
         FREEMAIN R,A=(1),LV=(0)       GIVETH BACK AREA
         RETURN (14,12),RC=0           GO BACK WITH CANCEL
         SPACE
RETURN8  EQU   *
         LA    R0,ENDDSA-DSA           GET LENGTH OF AREA WE GOT
         O     R0,SUBPOOL              GET SUBPOOL NUMBER
         LR    R1,R13                  GET ADDRESS OF GOTTEN AREA
         L     R13,SAVEAREA+4          GET LOW SAVE ADDRESS
         FREEMAIN R,A=(1),LV=(0)       GIVETH BACK AREA
         RETURN (14,12),RC=8           GO BACK WITH CANCEL
         SPACE
*---------------------------------------------------------------------*
*        LITERALS HERE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
SUBPOOL  DC    XL4'00000000'          INDICATE SUBPOOL 0
         SPACE
SCCLEN   EQU   6
CODELEN  EQU   8
         EJECT
*---------------------------------------------------------------------*
*        COPY THE PVS TABLES HERE
*---------------------------------------------------------------------*
         SPACE
         COPY  PVSTABLS
         SPACE
         LTORG
         SPACE
         EJECT
*---------------------------------------------------------------------*
*        DSECTS DEFINED HERE
*---------------------------------------------------------------------*
         SPACE 2
DSA      DSECT
SAVEAREA DS    18F                     SAVE AREA
SCCADDR  DS    F
PVSCODE  DS    0CL12
CHKCODE  DS    CL2                     CHECK CODE
COSTCODE DS    CL2                     USERS COST CODE
PROJCODE DS    CL4                     PROJECT CODE
DSDCODE  DS    CL4                     DSD CODE
ENDDSA   DS    0D
         SPACE
         END
