         TITLE '   U N I V E R T   '
************************************************************
*                                                          *
*        'UNIVERT'                                         *
*                                                          *
************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        DATE WRITTEN. JANUARY 30 1978.
*        DATE UPDATED. OCTOBER 4 1979.
*        ATTRIBUTES. RE-ENTRANT.
*        REMARKS.
*            THIS PROGRAM TRANSLATES A CARD DECK
*            FROM EBCDIC TO UNIVAC (FIELDATA).
*            IF PARM=REVERSE IS SPECIFIED, IT TRANSLATES
*            FROM UNIVAC (FIELDATA) TO EBCDIC.
*            04OCT79 - PRINT ADDED.
         SPACE
UNIVERT  START
         USING *,R12
         B     @PROLOG-*(,15)
         DC    AL1(7),C'CONVERT IBM EBCDIC / UNIVAC FIELDATA '
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL) SUBPOOL AND LENGTH
@PROLOG  STM   14,12,12(R13)
         LR    R12,R15
         LR    R2,R1
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         ST    R13,4(,1)
         ST    1,8(,R13)
         LR    R13,R1
         LR    R9,R1
         USING @DATA,R9
         SPACE 1
*        LA    R0,=CL8'UNIVAC'
*        LOAD  EPLOC=(0)
*        LR    R6,R0
         SPACE
         L     R6,TABLES
         LA    R8,HEAD1
         SPACE
         L     R1,0(,R2)
         CLC   0(2,R1),=H'7'
         BL    NOREV
         CLC   2(7,R1),=C'REVERSE'
         BNE   NOREV
         LA    R6,256(,R6)         POINT TO REVERSE TABLE
         LA    R8,HEAD2
NOREV    EQU   *
         SPACE
         LA    R7,PRTDCBW
         MVC   0(PRTDCBL,R7),PRTDCB
         SPACE
         LA    R15,PRTEXL
         IC    R14,EXLST(,R7)
         ST    R15,EXLST(,R7)
         STC   R14,EXLST(,R7)
         LA    R14,PRTEXT
         ST    R14,0(,R15)
         MVI   0(R15),X'85'
         SPACE
         LA    R1,OPENL
         MVI   0(R1),X'80'
         OPEN  ((R7),OUTPUT),MF=(E,(1))
         TM    OFLGS(R7),X'10'
         BO    PRTOPEN
         B     EXIT4
PRTEXT   CLC   BLKSI(2,R1),=H'0'
         BNER  R14
         MVC   BLKSI(2,R1),PRTBLK
         BR    R14
PRTBLK   DC    H'121'
PRTOPEN  EQU   *
         SPACE
         MVC   LINE,LINE-1         BLANK OUT LINE
         MVC   HEAD,LINE           BLANK OUT HEADING
         MVC   HEAD(L'HEAD1),0(R8) MOVE HEADING TO LINE
         PUT   (R7),HEAD
         PUT   (R7),LINE
         SPACE
         LA    R3,UT1DCBW
         MVC   0(UT1DCBL,R3),UT1DCB
         SPACE
         LA    R15,UT1EOD
         IC    R14,EODAD(,R3)
         ST    R15,EODAD(,R3)
         STC   R14,EODAD(,R3)
         SPACE
         LA    R15,UT1EXL
         IC    R14,EXLST(,R3)
         ST    R15,EXLST(,R3)
         STC   R14,EXLST(,R3)
         LA    R14,UT1EXT
         ST    R14,0(,R15)
         MVI   0(R15),X'85'
         SPACE
         LA    R1,OPENL
         MVI   0(R1),X'80'
         OPEN  ((R3),INPUT),MF=(E,(1))
         TM    OFLGS(R3),X'10'
         BO    OKINPUT
         B     EXIT4
UT1EXT   CLC   BLKSI(2,R1),=H'0'
         BNER  R14
         MVC   BLKSI(2,R1),UT1BLK
         BR    R14
UT1BLK   DC    H'80'
OKINPUT  EQU   *
         SPACE
         LA    R4,UT2DCBW
         MVC   0(UT2DCBL,R4),UT2DCB
         SPACE
         LA    R15,UT2EXL
         IC    R14,EXLST(,R4)
         ST    R15,EXLST(,R4)
         STC   R14,EXLST(,R4)
         LA    R14,UT2EXT
         ST    R14,0(,R15)
         MVI   0(R15),X'85'
         SPACE
         LA    R1,OPENL
         MVI   0(R1),X'80'
         OPEN  ((R4),OUTPUT),MF=(E,(1))
         TM    OFLGS(R4),X'10'
         BO    OKOUTPUT
         B     EXIT4
UT2EXT   CLC   BLKSI(2,R1),=H'0'
         BNER  R14
         MVC   BLKSI(2,R1),UT2BLK
         BR    R14
UT2BLK   DC    H'80'
OKOUTPUT EQU   *
         SPACE
         LA    R5,CARD
READ     EQU   *
         GET   (R3),(R5)
         TR    0(80,R5),0(R6)
         PUT   (R4),(R5)
         B     READ
         SPACE
EXIT4    LA    R15,4
         B     EXIT
         SPACE
UT1EOD   EQU   *
         LA    R1,CLOSEL
         MVI   0(R1),X'80'
         CLOSE ((R4)),MF=(E,(1))   CLOSE OUTPUT
         SPACE
         LA    R1,CLOSEL
         MVI   0(R1),X'80'
         CLOSE ((R3)),MF=(E,(1))   CLOSE INPUT
         SPACE 1
*        LA    R0,=CL8'UNIVAC'
*        DELETE EPLOC=(0)
         SPACE
         LA    R1,CLOSEL
         MVI   0(R1),X'80'
         CLOSE ((R7)),MF=(E,(1))   CLOSE PRINT
         SPACE 1
EXIT0    SR    15,15
EXIT     LR    1,13
         L     0,@SIZE
         L     13,4(,13)
         ST    15,16(,13)
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(13)
         BR    14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
AUTHOR   DC    C' THIS PROGRAM WAS WRITTEN BY BILL GODFREY'
         DC    C' OF PLANNING RESEARCH CORPORATION'
         DC    C' IN MCLEAN VIRGINIA.'
         DC    C' IT CONVERTS A DECK OF CARDS FROM EBCDIC'
         DC    C' TO UNIVAC FIELDATA OR VICE VERSA IF THE'
         DC    C' PARM FIELD SPECIFIES REVERSE. '
         SPACE
         PRINT NOGEN
PRTDCB   DCB   DDNAME=PRINT,MACRF=(PM),DSORG=PS,                       +
               RECFM=FBA,LRECL=121
PRTDCBL  EQU   *-PRTDCB
         SPACE
UT1DCB   DCB   DDNAME=INPUT,MACRF=(GM),DSORG=PS,EODAD=0,               +
               RECFM=FB,LRECL=80
UT1DCBL  EQU   *-UT1DCB
         SPACE
UT2DCB   DCB   DDNAME=OUTPUT,MACRF=(PM),DSORG=PS,                      +
               RECFM=FB,LRECL=80
UT2DCBL  EQU   *-UT2DCB
         PRINT GEN
         SPACE
         LTORG
HEAD1    DC    C'1 --- EBCDIC TO FIELDATA CONVERSION PROGRAM ---'
HEAD2    DC    C'1 --- FIELDATA TO EBCDIC CONVERSION PROGRAM ---'
TABLES   DC    V(UNIVAC)
         DC    0D'0'
EODAD    EQU   32
EXLST    EQU   36
OFLGS    EQU   48
BLKSI    EQU   62
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
OPENL    DS    F
CLOSEL   DS    F
PRTEXL   DS    F
UT1EXL   DS    F
UT2EXL   DS    F
CARD     DS    CL80
PRTDCBW  DS    0F,(PRTDCBL)X
UT1DCBW  DS    0F,(UT1DCBL)X
UT2DCBW  DS    0F,(UT2DCBL)X
LINEB    DS    C                   LINE - 1
LINE     DS    CL121
HEAD     DS    CL121
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   UNIVERT
         TITLE ' U N I V A C '
************************************************************
*                                                          *
*        'UNIVAC' TRANSLATION TABLES                       *
*                                                          *
************************************************************
         SPACE
*        WRITTEN BY BILL GODFREY.
*        DATE WRITTEN. JANUARY 30 1978.
*        DATE UPDATED. OCTOBER 4 1979.
*        COMMENTS.
*         THIS MODULE CONTAINS TWO TABLES USED BY THE
*         TRANSLATE INSTRUCTION.
*
*         TABLE 'A' -
*             CONVERTS EBCDIC TO UNIVAC (FIELDATA). THERE
*             ARE ONLY 64 VALID OUTPUT CHARACTERS. ANY OTHER
*             CHARACTERS WILL BE CONVERTED TO QUESTION MARKS.
*
*         TABLE 'B' -
*             CONVERTS UNIVAC (FIELDATA) TO EBCDIC. THERE
*             ARE ONLY 64 VALID INPUT CHARACTERS. ANY OTHER
*             CHARACTERS WILL BE CONVERTED TO QUESTION MARKS.
*
*         SOME UNIVAC CHARACTERS ARE NOT DEFINED IN EBCDIC.
*         THEREFORE THE FOLLOWING EBCDIC (HEX) VALUES HAVE
*         BEEN ARBITRARILY ASSIGNED TO THEM ....
*
*         FOR UNIVAC CHAR                USE EBCDIC
*         ----------------------------   ---------------------
*         OPEN BRACKETS   O'01' 12-5-8   X'4A' 12-2-8   CENT
*         CLOSE BRACKETS  O'02' 11-5-8   X'4F' 12-7-8   VERTBAR
*         DELTA           O'04' 11-7-8   X'5F' 11-7-8   NOT
*         BACKSLASH       O'57' 0-6-8    X'6D' 0-5-8    UNDERBAR
*         LOZENGE         O'76' 0-7-8    X'7F' 7-8      DBL QUOT
*         STOP            O'77' 0-2-8    X'E0' 0-2-8    0-8-2 KEY
         SPACE
UNIVAC   START
OB       EQU   X'4A'
CB       EQU   X'4F'
DL       EQU   X'5F'
SB       EQU   X'6D'
LZ       EQU   X'7F'
TP       EQU   X'E0'
         SPACE
A        DC    256X'C0'                         ALL QUESTION MARKS
         ORG   A+C'@'
         DC    X'7F'   QUOTES      7-8          AT-SIGN
         ORG   A+OB
         DC    X'4D'   OPAREN      12-5-8       OPEN BRACKETS
         ORG   A+CB
         DC    X'5D'   CPAREN      11-5-8       CLOSE BRACKETS
         ORG   A+C'#'
         DC    X'4F'   VBAR        12-7-8       POUND SIGN
         ORG   A+DL
         DC    X'5F'   NOT         11-7-8       DELTA
         ORG   A+C' '
         DC    X'40'   BLANK       (SPACE)      BLANK
         ORG   A+C')'
         DC    X'4C'   LT          12-4-8       CLOSE PAREN
         ORG   A+C'-'
         DC    X'60'   MINUS       11           MINUS
         ORG   A+C'+'
         DC    X'50'   AMP         12           PLUS
         ORG   A+C'<'
         DC    X'4E'   PLUS        12-6-8       LESS
         ORG   A+C'='
         DC    X'7B'   POUND       3-8          EQUAL
         ORG   A+C'>'
         DC    X'7E'   EQUAL       6-8          GREATER
         ORG   A+C'&&'
         DC    X'7A'   COLON       2-8          AMPERSAND
         ORG   A+C'$'
         DC    X'5B'   DOLLAR      11-3-8       DOLLAR
         ORG   A+C'*'
         DC    X'5C'   ASTERISK    11-4-8       ASTERISK
         ORG   A+C'('
         DC    X'6C'   PERCENT     0-4-8        OPEN PAREN
         ORG   A+C'%'
         DC    X'6D'   UNDER       0-5-8        PERCENT
         ORG   A+C':'
         DC    X'7D'   APOST       5-8          COLON
         ORG   A+C'?'
         DC    X'C0'   ---         12-0         QUESTION
         ORG   A+C'!'
         DC    X'D0'   ---         11-0         EXCLAMATION
         ORG   A+C','
         DC    X'6B'   COMMA       0-3-8        COMMA
         ORG   A+SB
         DC    X'6E'   GREATER     0-6-8        BACKSLASH
         ORG   A+C''''
         DC    X'7C'   AT          4-8          APOSTROPHE
         ORG   A+C';'
         DC    X'5E'   SEMICOLON   11-6-8       SEMICOLON
         ORG   A+C'/'
         DC    X'61'   SLASH       0-1          SLASH
         ORG   A+C'.'
         DC    X'4B'   PERIOD      12-3-8       PERIOD
         ORG   A+LZ
         DC    X'6F'   QUESTION    0-7-8        LOZENGE
         ORG   A+TP
         DC    X'E0'   ---         0-2-8        STOP
         ORG   A+C'A'
         DC    C'ABCDEFGHI'
         ORG   A+C'J'
         DC    C'JKLMNOPQR'
         ORG   A+C'S'
         DC    C'STUVWXYZ'
         ORG   A+C'0'
         DC    C'0123456789'
         ORG   A+256
         SPACE
B        DC    256C'?'
         ORG   B+X'7F' QUOTES      7-8          AT-SIGN
         DC    C'@'
         ORG   B+X'4D' OPAREN      12-5-8       OPEN BRACKETS
         DC    AL1(OB)
         ORG   B+X'5D' CPAREN      11-5-8       CLOSE BRACKETS
         DC    AL1(CB)
         ORG   B+X'4F' VBAR        12-7-8       POUND SIGN
         DC    C'#'
         ORG   B+X'5F' NOT         11-7-8       DELTA
         DC    AL1(DL)
         ORG   B+X'40' BLANK       (SPACE)      BLANK
         DC    C' '
         ORG   B+X'4C' LT          12-4-8       CLOSE PAREN
         DC    C')'
         ORG   B+X'60' MINUS       11           MINUS
         DC    C'-'
         ORG   B+X'50' AMP         12           PLUS
         DC    C'+'
         ORG   B+X'4E' PLUS        12-6-8       LESS
         DC    C'<'
         ORG   B+X'7B' POUND       3-8          EQUAL
         DC    C'='
         ORG   B+X'7E' EQUAL       6-8          GREATER
         DC    C'>'
         ORG   B+X'7A' COLON       2-8          AMPERSAND
         DC    C'&&'
         ORG   B+X'5B' DOLLAR      11-3-8       DOLLAR
         DC    C'$'
         ORG   B+X'5C' ASTERISK    11-4-8       ASTERISK
         DC    C'*'
         ORG   B+X'6C' PERCENT     0-4-8        OPEN PAREN
         DC    C'('
         ORG   B+X'6D' UNDER       0-5-8        PERCENT
         DC    C'%'
         ORG   B+X'7D' APOST       5-8          COLON
         DC    C':'
         ORG   B+X'C0' ---         12-0         QUESTION
         DC    C'?'
         ORG   B+X'D0' ---         11-0         EXCLAMATION
         DC    C'!'
         ORG   B+X'6B' COMMA       0-3-8        COMMA
         DC    C','
         ORG   B+X'6E' GREATER     0-6-8        BACKSLASH
         DC    AL1(SB)
         ORG   B+X'7C' AT          4-8          APOSTROPHE
         DC    C''''
         ORG   B+X'5E' SEMICOLON   11-6-8       SEMICOLON
         DC    C';'
         ORG   B+X'61' SLASH       0-1          SLASH
         DC    C'/'
         ORG   B+X'4B' PERIOD      12-3-8       PERIOD
         DC    C'.'
         ORG   B+X'6F' QUESTION    0-7-8        LOZENGE
         DC    AL1(LZ)
         ORG   B+X'E0' ---         0-2-8        STOP
         DC    AL1(TP)
         ORG   B+C'A'
         DC    C'ABCDEFGHI'
         ORG   B+C'J'
         DC    C'JKLMNOPQR'
         ORG   B+C'S'
         DC    C'STUVWXYZ'
         ORG   B+C'0'
         DC    C'0123456789'
         END
