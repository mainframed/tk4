         TITLE  'SMF IEFU83 EXIT PGM---PROCEDURE CODE'
*-$DOC@-------------------------------------------------------------*
*                 I   E   F   U   8   3                             *
*                                                                   *
*        MAJOR  MODIFICATION OF THE IEFU83 EXIT I GOT FROM          *
*        BLUE CROSS - BLUE SHIELD, RICHMOND, VA. IT HAD THE         *
*        BRANCH TABLE CONCEPT IN IT, BUT IT USED THE BRANCH         *
*        TABLE TO DECIDE ONLY IF IT WAS TO WRITE A RECORD OR        *
*        NOT. I USED THE TABLE TO BRANCH TO THE APPROPRIATE         *
*        CODE TO FURTHER PROCESS A TYPE OF RECORD.                  *
*                                                                   *
*                             JIM MARSHALL, CAPT, USAF              *
*                                                                   *
*  INSTALLED: 10 FEBRUARY 1982                                      *
*                                                                   *
*  CHANGE LOG :                                                     *
*  16 JUL 82 - ADDED SOME CODE TO LOOK AT TEMPORARY ERRORS ON TAPE  *
*              FROM THE TYPE 21 RECORDS. THUS WE CAN TELL THE OPER- *
*              ATORS WHEN TO CLEAN A TAPE DRIVE. THIS CAME BY WAY   *
*              OF THE U.S. SENATE, I TOOK THERE CODE AS A MODEL.    *
*              CODE IS IN LIMBO FOR A WHILE, SOMETHING IS WRONG. JDM*
*  10 FEB 83 - MODIFIED THE EXIT TO LOOK AT TYPE 40 RECORDS. THE    *
*              GOAL IS TO RID OURSELVES OF THOSE RECORDS WITH ZERO  *
*              EXCP COUNTS.                                      JDM*
*   8 JUN 83 - TRYING TO ELIMINATE RECORDS, SO 14'S AND 15'S ARE A  *
*              GOOD PLACE TO DO IT. RECORDS WITH ZERO EXCP COUNTS   *
*              CAN BE TRASHED. SEE THE CODE FOR DETAILS          JDM*
*  20 DEC 84 - FINALLY GETTING AROUND TO PRODUCING MSGS ON TAPES    *
*              TYPE 21 RECORDS. THE SENATE CODE THIS IS BASED IS IN *
*              ERROR IN THE 'SIO' FIELD SIZE. CAPT JIM PETERSON     *
*              FOUND THIS ERROR AND GAVE ME THE FIX. ADDED SOME     *
*              LOGIC TO THE CODE ADD THE VOLSER TO THE MSG THAT     *
*              TELLS THE OPERATOR TO CLEAN THE TAPE DRIVE. ADDED    *
*              THE TAPE NUMBER TO IT. ALSO I PUT OUT THE 'STATS'    *
*              MSG TO BEFORE I PUT THE CLEAN MSG. USERS THEN GET    *
*              THE IDEA THAT THE TAPE CAUSED THE CLEAN MSG TO COME  *
*              OUT AND CAUSED THE PROBLEM. SEE THE CODE !!!!    JDM4*
*-$DOC$-------------------------------------------------------------*
         EJECT
         MACRO                                                   JDM4
&LAB     EDIT21 &FIELD,&DISPL                                    JDM4
         SR    R1,R1                                             JDM4
         IC    R1,&FIELD                                         JDM4
         CVD   R1,DBLEWD                                         JDM4
         MVC   AREA(4),PAT21                                     JDM4
         ED    AREA(4),DBLEWD+6                                  JDM4
         MVC   WTOAREA+&DISPL.(3),AREA+1                         JDM4
         MEND                                                    JDM4
         SPACE
         MACRO                                                   JDM4
&LAB     EDIT21H &FIELD,&DISPL                                   JDM4
         SR    R1,R1                                             JDM4
         MVC   DBLEWD(2),&FIELD                                  JDM4
         LH    R1,DBLEWD                                         JDM4
         CVD   R1,DBLEWD                                         JDM4
         MVC   AREA(6),PAT21H                                    JDM4
         ED    AREA(6),DBLEWD+5                                  JDM4
         MVC   WTOAREA+&DISPL.(5),AREA+1                         JDM4
         MEND                                                    JDM4
         SPACE
R0       EQU   0                  NOT USED
R1       EQU   1                  ENTRY VALUE IS SAVED
R2       EQU   2                  WORK REGISTER
R3       EQU   3                  SAVED REGISTER 1 AT ENTRY
R4       EQU   4                  WORK REGISTER
R5       EQU   5                  WORK REGISTER
R6       EQU   6                  WORK REGISTER
R7       EQU   7                  WORK REGISTER
R8       EQU   8                  ADDRESSABILITY TO SMF REC DSECT
R9       EQU   9                  ADDRESSABILITY TO WORKAREA DSECT
R10      EQU   10                 UNUSED
R11      EQU   11                 KEEP FREE FOR EXTRA BASE
R12      EQU   12                 BASE REGISTER
R13      EQU   13                 POINTER TO SAVE AREA ( MINE )
R14      EQU   14                 DO NOT USE !!!!!!
R15      EQU   15                 UPON ENTRY THE BASE ADDRESS, AFTER
*                                    THAT A WORK REGISTER
IEFU83   CSECT
         SAVE  (14,12),,IEFU83_2ISG_&SYSDATE_&SYSTIME
         LR    R12,R15             SETUP BASE REGISTER
         USING IEFU83,R12          GET NEW BASE REGISTERS
         LR    R3,R1               SAVE REGISTER 1
         L     R0,WORKSIZE         GET SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)          GO GET IT
         LR    R9,R1               SAVE THE ADDRESS
         USING WORKAREA,R9         ESTABLISH ADDRESSABILITY
         ST    R13,SAVE+4          SETUP
         ST    R9,8(R13)            SAVEAREA
         LR    R13,R9                   CHAIN
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        SMF EXIT. USES SMF RECORD TYPE AS INDEX INTO                *
*         A BYTE TABLE. THE BYTE IS THEN MULTIPLIED BY 4             *
*         AND USED AS A BRANCH INDEX TO BRANCH TO A BRANCH.          *
*                                                                    *
*        INPUT  R1 POINTS TO THE ADDRESS OF THE SMF RECORD           *
*               ( SAVED IN R3 )                                      *
*        OUTPUT R15 CONTAINS                                         *
*            0 - WRITE THE RECORD                                    *
*            4 - DELETE THE RECORD                                   *
*        EXIT - RETURN VIA R14                                       *
*                                                                    *
*--------------------------------------------------------------------*
         SPACE
         L     R8,0(R3)            GET ADDRESS OF RECORD
         USING SMFREC,R8           GET ADDR TO RECORD
         SPACE
         L     R7,TABADD           GET OFFSET TO TABLE DSECT
         LA    R7,0(R7,R12)        ADDRESS OF TABLE
         SR    R15,R15             CLEAR WORK REGISTER
         SR    R4,R4               CLEAR WORK REGISTER
         IC    R4,5(R8)            GET REC TYPE AT  SMFREC+4
         IC    R15,0(R4,R7)        USE AS INDEX INTO TABLE
         SLL   R15,2               MULTIPLY BY 4
         B     BRTAB(R15)          BRANCH TO BRANCH
         B     RETURN0             DUMMY FOR SAFETY
         SPACE
*--------BRANCH TABLE AS FOLLOWS ------------------------------------*
         SPACE
BRTAB    DS    0H                  BRANCH TO CORRECT ROUTINE
         B     RETURN0       0     WRITE
         B     RETURN4       1     NOWRITE
         B     TYPE14R       2     PROCESS TYPE 14 RECORDS
         B     TYPE15R       3     PROCESS TYPE 15 RECORDS
         B     TYPE40R       4     PROCESS TYPE 40 RECORDS
         B     TYPE21R       5     PROCESS TYPE 21 RECORDS       JDM4
         B     RETURN0       6     SPARE
         SPACE
*--------RETURN WITH RET CODE = 0   ( WRITE THE RECORD )  -----------*
         SPACE
RETURN0  EQU   *
         L     R4,4(R13)           GET HIGH SAVE AREA
         LA    R15,0               GET RETURN CODE
         ST    R15,16(R4)          PUT IN RETURN CODE
         B     RETURN              GO RETURN
         SPACE
*------- RETURN WITH RET CODE = 4   ( DO NOT WRITE RECORD )  --------*
         SPACE
RETURN4  EQU   *
         L     R4,4(R13)           GET HIGH SAVE AREA
         LA    R15,4               PUT IN RETURN CODE = 4
         ST    R15,16(R4)          PUT IN RETURN CODE
         SPACE
*------- RETURN TO CALLER FOR GOOD, DO FREEMAIN AND GO AWAY !!!  ----*
         SPACE
RETURN   EQU   *
         LR    R1,R13              GET ADDRESS FOR FREEMAIN
         L     R0,WORKSIZE         GET SUBPOOL AND LENGTH
         L     R13,4(R13)          GET HIGH SAVE ADDRESS
         FREEMAIN R,LV=(0),A=(1)   FREE SAVE AREA
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGS
         BR    R14                 RETURN FOR GOOD
         EJECT
*--------------------------------------------------------------------*
*        PROCESS THE TYPE 14 RECORDS. WE WANT TO ELIMINATE           *
*        ALL RECORDS THAT HAVE ZERO EXCP COUNTS.                     *
*--------------------------------------------------------------------*
         SPACE
TYPE14R  EQU   *
         CLC   SMFEXCP(4),=XL4'00' ZERO EXCP COUNTS ??
         BE    RETURN4
         B     RETURN0
         EJECT
*--------------------------------------------------------------------*
*        PROCESS THE TYPE 15 RECORDS. WE WANT TO ELIMINATE           *
*        ALL RECORDS THAT HAVE ZERO EXCP COUNTS BUT ONLY FOR         *
*        VIO AND TEMPORARY FILES. PERSON COULD OPEN A REAL FILE      *
*        AND IMMEDIATELY CLOSE IT DESTROYING THE FILE. NOT GOOD      *
*        NOTE: FOR TYPE 15 RECORDS YOU USE THE 14 DSECT              *
*--------------------------------------------------------------------*
         SPACE
TYPE15R  EQU   *
         CLC   SMFEXCP(4),=XL4'00' ZERO EXCP COUNTS ???
         BNE   RETURN0             DO SAVE THE RECORD
         TM    SMF14RIN,SMF14VIO   IS IT VIO
         BO    RETURN4             DO NOT SAVE IT
         TM    SMF14RIN,SMF14TDS   IS IT A TEMPORARY FILE ???
         BO    RETURN4             DO NOT SAVE IT
         B     RETURN0             SAVE ALL OTHERS
         EJECT
*----------------------------------------------------------------JDM4*
*        LOOK AT THE TYPE 21 RECORDS TO SEE WHEN A TAPE DRIVE    JDM4*
*        SHOULD BE CLEANED (ALSO TELL THE OPERATOR).             JDM4*
*                                                                JDM4*
*        TO ME, THE SEQUENCE OF MSGS COMING OUT ON THE USER LOG  JDM4*
*        IS IMPORTANT. IF THE USER SEES THE CLEAN MSG FOR THE    JDM4*
*        TAPE DRIVE BEFORE THE 'STATS' MSG, THEN THEY WILL       JDM4*
*        BELIEVE THAT THE DRIVE WAS DIRTY BEFORE THERE TAPE WAS  JDM4*
*        MOUNTED AND WILL THEY BE UNHAPPY. WHAT REALLY HAPPENED  JDM4*
*        IS PROBABLY THEIR TAPE HAS LEFT THE DIRT BEHIND. THUS   JDM4*
*        WE WANT THE 'STATS' MSG BEFORE THE 'CLEAN' MSG.         JDM4*
*----------------------------------------------------------------JDM4*
         SPACE
TYPE21R  EQU   *                                                 JDM4
         SR    R1,R1               CLEAR R1 FOR WORKING          JDM4
         IC    R1,SMF21TR                                        JDM4
         SR    R2,R2               CLEAR R2                      JDM4
         IC    R2,SMF21TW                                        JDM4
         AR    R1,R2               ADD THE 2 VALUES              JDM4
         ST    R1,NUMERRS          SAVE THE ERROR CT FOR LATER   JDM4
         SPACE
*----------------------------------------------------------------JDM4*
*        NOW WE WILL CONSTRUCT THE 'STATS' MSG FOR THE USER.     JDM4*
*----------------------------------------------------------------JDM4*
         SPACE
         MVC   WTOAREA(MSG21BL),MSG21B   MOVE IN THE MSG         JDM4
         MVC   WTOAREA+19(6),SMF21VOL    MOVE IN THE VOLSER      JDM4
         EDIT21 SMF21TR,29         # OF TEMP READ ERRORS         JDM4
         EDIT21 SMF21TW,36         # OF TEMP WRITE ERRORS        JDM4
         EDIT21 SMF21PR,43         # OF PERMANENT READ ERRORS    JDM4
         EDIT21 SMF21PW,50         # OF PERMANENT WRITE ERRORS   JDM4
         EDIT21 SMF21NB,57         # OF NOISE BLOCKS             JDM4
         EDIT21H SMF21ERG,64       # OF ERASE GAPS               JDM4
         EDIT21H SMF21CLN,73       # OF CLEANER ACTIONS          JDM4
         EDIT21H SMF21SIO,83       # OF SIOS                     JDM4
         SPACE
         LA    R1,WTOAREA                                        JDM4
         WTO   MF=(E,(R1))         PUT OUT THE MSG               JDM4
S21WTO   EQU   *                                                 JDM4
         CLC   NUMERRS(4),MAXERR   ARE WE ABOVE MAX THRESHOLD    JDM4
         BH    S21CLEAN            IF SO, GO TELL OPERATOR       JDM4
         CLI   SMF21PR,0           ANY PERMANENT READ ERRORS???  JDM4
         BNE   S21CLEAN            IF SO, BYPASS                 JDM4
         CLI   SMF21PW,0           ANY PERMANENT WRITE ERRORS??? JDM4
         BE    RETURN0                                           JDM4
S21CLEAN EQU   *                                                 JDM4
         MVC   WTOAREA(MSG21AL),MSG21A  MOVE IN THE MSG          JDM4
         MVC   WTOAREA+15(6),SMF21VOL   MOVE IN THE VOLSER       JDM4
         UNPK  DBLEWD(5),SMF21CA(3)   GET DEV ADDR               JDM4
         TR    DBLEWD(4),S21TABLE  CONVERT ADDR TO HEX           JDM4
         MVC   WTOAREA+38(3),DBLEWD+1  PUT IN MSG                JDM4
         LA    R1,WTOAREA          GET MSG ADDR                  JDM4
         WTO   MF=(E,(R1))         PUT IT OUT                    JDM4
         SPACE
         B     RETURN0                                           JDM4
         EJECT
*--------------------------------------------------------------------*
*        PROCESS THE TYPE 40 RECORDS. WE WANT TO ELIMINATE           *
*           1.  DD DUMMY RECORDS IF THEY ARE NOT IN A CONCATENATION  *
*           2.  TERM=TS                                              *
*           3.  ZERO EXCP COUNTS IN THE RECORD                       *
*--------------------------------------------------------------------*
         SPACE
TYPE40R  EQU   *
         CLC   TDDVAR(2),=X'000A'     IS THERE ONLY ONE DD CARD ?    R2
         BNE   RETURN0                IF CONCAT, THEN WRITE IT
         CLC   TDDCUAD(4),=X'00000FFF' IS IT VIO ?
         BE    RETURN0                IF SO, WRITE IT
         CLC   TDDNEXCP(4),=XL4'0'    IS EXCP COUNT = 0 ??
         BE    RETURN4                IF SO, DO NOT WRITE IT
         B     RETURN0                GO WRITE IT
         EJECT
*--------------------------------------------------------------------*
*        CONSTANTS                                                   *
*--------------------------------------------------------------------*
         SPACE
         DS    0F
WORKSIZE DC    AL1(245),AL3(WORKEND-WORKAREA)
RECPOOL  DC    AL1(245),AL3(000000)
TABADD   DC    A(SMFTAB-IEFU83)
ONE      DC    F'1'
MAXERR   DC    F'20'               20 ERRORS MAX                 JDM4
PAT21    DC    XL4'F0212020'       EDIT PATTERN 1BYTE            JDM4
PAT21H   DC    XL6'F02120202020'   EDIT PATTERN 2BYTES           JDM4
*AD0060I DC    CL8'ISG0060I'       MSGID FOR IEECVXIT            JDM4
S21TABLE DC    240C'/',C'0123456789ABCDEF'
         DS    0H
MOVEBUF  MVC   0(0,R2),SMFREC
         SPACE
*----------------------------------------------------------------JDM4*
*        WTO'S FOLLOWS, GIVE US SOME NUMBERS FOR DISPLACEMENTS.  JDM4*
*----------------------------------------------------------------JDM4*
* UPPER LINE          1         2         3         4         5  JDM4
*               4567890123456789012345678901234567890123456789012345678
* LOWER LINE    6         7         8         9                  JDM4
*              90123456789012345678901234567890                  JDM4
         DS    0F                                                JDM4
MSG21A   WTO   'ISG0058A V=XXXXXX PLSE CLEAN UNIT=XXX, PROBABLE DIRT ON+
                DRIVE',ROUTCDE=(5),DESC=(2),MF=L                 JDM4
MSG21AL  EQU   *-MSG21A     LENGTH OF MSG                        JDM4
         SPACE
         DS    0F                                                JDM4
MSG21B   WTO   'ISG0059I STATS XXXXXX TR=XXX,TW=XXX,PR=XXX,PW=XXX,NB=XX+
               X,EG=XXXXX,CL=XXXXX,SIO=XXXXX',                   JDM4  +
               ROUTCDE=(5),DESC=(3),MF=L                         JDM4
MSG21BL  EQU   *-MSG21B     LENGTH OF MSG                        JDM4
         SPACE
         LTORG
         SPACE
*--------------------------------------------------------------------*
*                                                                    *
*        THIS TABLE CONTAINS A BRANCH INDEX VALUE FOR EACH           *
*        SMF RECORD TYPE. THE VALUE IS THEN MULTIPLIED BY 4          *
*        TO GET TO THE PROCESSING PROCESSING CODES (BRANCH           *
*        TABLE.  PRESENTLY WE ARE PROCESSING :                       *
*                                                                    *
*         0   -  RETURN WITH R15=0 AND WRITE THE RECORD              *
*         1   -  RETURN WITH R15=4 AND DO NOT WRITE THE RECORD       *
*         2   -  PROCESS TYPE 14 RECORDS                             *
*         3   -  PROCESS TYPE 15 RECORDS                             *
*         4   -  PROCESS TYPE 40 RECORDS                             *
*         5   -  PROCESS TYPE 21 RECORDS                         JDM4*
*--------------------------------------------------------------------*
         SPACE
SMFTAB   CSECT
SMFTYPES DS    0XL256
TYPE0    DC    HL1'0'        WRITE
TYPE1    DC    HL1'0'        WRITE
TYPE2    DC    HL1'0'        WRITE
TYPE3    DC    HL1'0'        WRITE
TYPE4    DC    HL1'0'        WRITE
TYPE5    DC    HL1'0'        WRITE
TYPE6    DC    HL1'0'        WRITE
TYPE7    DC    HL1'0'        WRITE
TYPE8    DC    HL1'0'        WRITE
TYPE9    DC    HL1'0'        WRITE
TYPE10   DC    HL1'0'        WRITE
TYPE11   DC    HL1'0'        WRITE
TYPE12   DC    HL1'0'        WRITE
TYPE13   DC    HL1'0'        WRITE
TYPE14   DC    HL1'2'        *****  PROCESS IT
TYPE15   DC    HL1'3'        *****  PROCESS IT
TYPE16   DC    HL1'0'        WRITE
TYPE17   DC    HL1'0'        WRITE
TYPE18   DC    HL1'0'        WRITE
TYPE19   DC    HL1'0'        WRITE
TYPE20   DC    HL1'0'        WRITE
TYPE21   DC    HL1'5'        *****  PROCESS IT                   JDM4
TYPE22   DC    HL1'0'        WRITE
TYPE23   DC    HL1'0'        WRITE
TYPE24   DC    HL1'0'        WRITE
TYPE25   DC    HL1'0'        WRITE
TYPE26   DC    HL1'0'        WRITE
TYPE27   DC    HL1'0'        WRITE
TYPE28   DC    HL1'0'        WRITE
TYPE29   DC    HL1'0'        WRITE
TYPE30   DC    HL1'0'        WRITE
TYPE31   DC    HL1'0'        WRITE
TYPE32   DC    HL1'0'        WRITE
TYPE33   DC    HL1'0'        WRITE
TYPE34   DC    HL1'0'        WRITE
TYPE35   DC    HL1'0'        WRITE
TYPE36   DC    HL1'0'        WRITE
TYPE37   DC    HL1'0'        WRITE
TYPE38   DC    HL1'0'        WRITE
TYPE39   DC    HL1'0'        WRITE
TYPE40   DC    HL1'4'        *****  PROCESS IT
TYPE41   DC    HL1'0'        WRITE
TYPE42   DC    HL1'0'        WRITE
TYPE43   DC    HL1'0'        WRITE
TYPE44   DC    HL1'0'        WRITE
TYPE45   DC    HL1'0'        WRITE
TYPE46   DC    HL1'0'        WRITE
TYPE47   DC    HL1'0'        WRITE
TYPE48   DC    HL1'0'        WRITE
TYPE49   DC    HL1'0'        WRITE
TYPE50   DC    HL1'0'        WRITE
TYPE51   DC    HL1'0'        WRITE
TYPE52   DC    HL1'0'        WRITE
TYPE53   DC    HL1'0'        WRITE
TYPE54   DC    HL1'0'        WRITE
TYPE55   DC    HL1'0'        WRITE
TYPE56   DC    HL1'0'        WRITE
TYPE57   DC    HL1'0'        WRITE
TYPE58   DC    HL1'0'        WRITE
TYPE59   DC    HL1'0'        WRITE
TYPE60   DC    HL1'0'        WRITE
TYPE61   DC    HL1'0'        WRITE
TYPE62   DC    HL1'0'        WRITE
TYPE63   DC    HL1'0'        WRITE
TYPE64   DC    HL1'0'        WRITE
TYPE65   DC    HL1'0'        WRITE
TYPE66   DC    HL1'0'        WRITE
TYPE67   DC    HL1'0'        WRITE
TYPE68   DC    HL1'0'        WRITE
TYPE69   DC    HL1'0'        WRITE
TYPE70   DC    HL1'0'        WRITE
TYPE71   DC    HL1'0'        WRITE
TYPE72   DC    HL1'0'        WRITE
TYPE73   DC    HL1'0'        WRITE
TYPE74   DC    HL1'0'        WRITE
TYPE75   DC    HL1'0'        WRITE
TYPE76   DC    HL1'0'        WRITE
TYPE77   DC    HL1'0'        WRITE
TYPE78   DC    HL1'0'        WRITE
TYPE79   DC    HL1'0'        WRITE
TYPE80   DC    HL1'0'        WRITE
TYPE81   DC    HL1'0'        WRITE
TYPE82   DC    HL1'0'        WRITE
TYPE83   DC    HL1'0'        WRITE
TYPE84   DC    HL1'0'        WRITE
TYPE85   DC    HL1'0'        WRITE
TYPE86   DC    HL1'0'        WRITE
TYPE87   DC    HL1'0'        WRITE
TYPE88   DC    HL1'0'        WRITE
TYPE89   DC    HL1'0'        WRITE
TYPE90   DC    HL1'0'        WRITE
TYPE91   DC    HL1'0'        WRITE
TYPE92   DC    HL1'0'        WRITE
TYPE93   DC    HL1'0'        WRITE
TYPE94   DC    HL1'0'        WRITE
TYPE95   DC    HL1'0'        WRITE
TYPE96   DC    HL1'0'        WRITE
TYPE97   DC    HL1'0'        WRITE
TYPE98   DC    HL1'0'        WRITE
TYPE99   DC    HL1'0'        WRITE
TYPE100  DC    HL1'0'        WRITE
TYPE101  DC    HL1'0'        WRITE
TYPE102  DC    HL1'0'        WRITE
TYPE103  DC    HL1'0'        WRITE
TYPE104  DC    HL1'0'        WRITE
TYPE105  DC    HL1'0'        WRITE
TYPE106  DC    HL1'0'        WRITE
TYPE107  DC    HL1'0'        WRITE
TYPE108  DC    HL1'0'        WRITE
TYPE109  DC    HL1'0'        WRITE
TYPE110  DC    HL1'0'        WRITE
TYPE111  DC    HL1'0'        WRITE
TYPE112  DC    HL1'0'        WRITE
TYPE113  DC    HL1'0'        WRITE
TYPE114  DC    HL1'0'        WRITE
TYPE115  DC    HL1'0'        WRITE
TYPE116  DC    HL1'0'        WRITE
TYPE117  DC    HL1'0'        WRITE
TYPE118  DC    HL1'0'        WRITE
TYPE119  DC    HL1'0'        WRITE
TYPE120  DC    HL1'0'        WRITE
TYPE121  DC    HL1'0'        WRITE
TYPE122  DC    HL1'0'        WRITE
TYPE123  DC    HL1'0'        WRITE
TYPE124  DC    HL1'0'        WRITE
TYPE125  DC    HL1'0'        WRITE
TYPE126  DC    HL1'0'        WRITE
TYPE127  DC    HL1'0'        WRITE
TYPE128  DC    HL1'0'        WRITE
TYPE129  DC    HL1'0'        WRITE
TYPE130  DC    HL1'0'        WRITE
TYPE131  DC    HL1'0'        WRITE
TYPE132  DC    HL1'0'        WRITE
TYPE133  DC    HL1'0'        WRITE
TYPE134  DC    HL1'0'        WRITE
TYPE135  DC    HL1'0'        WRITE
TYPE136  DC    HL1'0'        WRITE
TYPE137  DC    HL1'0'        WRITE
TYPE138  DC    HL1'0'        WRITE
TYPE139  DC    HL1'0'        WRITE
TYPE140  DC    HL1'0'        WRITE
TYPE141  DC    HL1'0'        WRITE
TYPE142  DC    HL1'0'        WRITE
TYPE143  DC    HL1'0'        WRITE
TYPE144  DC    HL1'0'        WRITE
TYPE145  DC    HL1'0'        WRITE
TYPE146  DC    HL1'0'        WRITE
TYPE147  DC    HL1'0'        WRITE
TYPE148  DC    HL1'0'        WRITE
TYPE149  DC    HL1'0'        WRITE
TYPE150  DC    HL1'0'        WRITE
TYPE151  DC    HL1'0'        WRITE
TYPE152  DC    HL1'0'        WRITE
TYPE153  DC    HL1'0'        WRITE
TYPE154  DC    HL1'0'        WRITE
TYPE155  DC    HL1'0'        WRITE
TYPE156  DC    HL1'0'        WRITE
TYPE157  DC    HL1'0'        WRITE
TYPE158  DC    HL1'0'        WRITE
TYPE159  DC    HL1'0'        WRITE
TYPE160  DC    HL1'0'        WRITE
TYPE161  DC    HL1'0'        WRITE
TYPE162  DC    HL1'0'        WRITE
TYPE163  DC    HL1'0'        WRITE
TYPE164  DC    HL1'0'        WRITE
TYPE165  DC    HL1'0'        WRITE
TYPE166  DC    HL1'0'        WRITE
TYPE167  DC    HL1'0'        WRITE
TYPE168  DC    HL1'0'        WRITE
TYPE169  DC    HL1'0'        WRITE
TYPE170  DC    HL1'0'        WRITE
TYPE171  DC    HL1'0'        WRITE
TYPE172  DC    HL1'0'        WRITE
TYPE173  DC    HL1'0'        WRITE
TYPE174  DC    HL1'0'        WRITE
TYPE175  DC    HL1'0'        WRITE
TYPE176  DC    HL1'0'        WRITE
TYPE177  DC    HL1'0'        WRITE
TYPE178  DC    HL1'0'        WRITE
TYPE179  DC    HL1'0'        WRITE
TYPE180  DC    HL1'0'        WRITE
TYPE181  DC    HL1'0'        WRITE
TYPE182  DC    HL1'0'        WRITE
TYPE183  DC    HL1'0'        WRITE
TYPE184  DC    HL1'0'        WRITE
TYPE185  DC    HL1'0'        WRITE
TYPE186  DC    HL1'0'        WRITE
TYPE187  DC    HL1'0'        WRITE
TYPE188  DC    HL1'0'        WRITE
TYPE189  DC    HL1'0'        WRITE
TYPE190  DC    HL1'0'        WRITE
TYPE191  DC    HL1'0'        WRITE
TYPE192  DC    HL1'0'        WRITE
TYPE193  DC    HL1'0'        WRITE
TYPE194  DC    HL1'0'        WRITE
TYPE195  DC    HL1'0'        WRITE
TYPE196  DC    HL1'0'        WRITE
TYPE197  DC    HL1'0'        WRITE
TYPE198  DC    HL1'0'        WRITE
TYPE199  DC    HL1'0'        WRITE
TYPE200  DC    HL1'0'        WRITE
TYPE201  DC    HL1'0'        WRITE
TYPE202  DC    HL1'0'        WRITE
TYPE203  DC    HL1'0'        WRITE
TYPE204  DC    HL1'0'        WRITE
TYPE205  DC    HL1'0'        WRITE
TYPE206  DC    HL1'0'        WRITE
TYPE207  DC    HL1'0'        WRITE
TYPE208  DC    HL1'0'        WRITE
TYPE209  DC    HL1'0'        WRITE
TYPE210  DC    HL1'0'        WRITE
TYPE211  DC    HL1'0'        WRITE
TYPE212  DC    HL1'0'        WRITE
TYPE213  DC    HL1'0'        WRITE
TYPE214  DC    HL1'0'        WRITE
TYPE215  DC    HL1'0'        WRITE
TYPE216  DC    HL1'0'        WRITE
TYPE217  DC    HL1'0'        WRITE
TYPE218  DC    HL1'0'        WRITE
TYPE219  DC    HL1'0'        WRITE
TYPE220  DC    HL1'0'        WRITE
TYPE221  DC    HL1'0'        WRITE
TYPE222  DC    HL1'0'        WRITE
TYPE223  DC    HL1'0'        WRITE
TYPE224  DC    HL1'0'        WRITE
TYPE225  DC    HL1'0'        WRITE
TYPE226  DC    HL1'0'        WRITE
TYPE227  DC    HL1'0'        WRITE
TYPE228  DC    HL1'0'        WRITE
TYPE229  DC    HL1'0'        WRITE
TYPE230  DC    HL1'0'        WRITE
TYPE231  DC    HL1'0'        WRITE
TYPE232  DC    HL1'0'        WRITE
TYPE233  DC    HL1'0'        WRITE
TYPE234  DC    HL1'0'        WRITE
TYPE235  DC    HL1'0'        WRITE
TYPE236  DC    HL1'0'        WRITE
TYPE237  DC    HL1'0'        WRITE
TYPE238  DC    HL1'0'        WRITE
TYPE239  DC    HL1'0'        WRITE
TYPE240  DC    HL1'0'        WRITE
TYPE241  DC    HL1'0'        WRITE
TYPE242  DC    HL1'0'        WRITE
TYPE243  DC    HL1'0'        WRITE
TYPE244  DC    HL1'0'        WRITE
TYPE245  DC    HL1'0'        WRITE
TYPE246  DC    HL1'0'        WRITE
TYPE247  DC    HL1'0'        WRITE
TYPE248  DC    HL1'0'        WRITE
TYPE249  DC    HL1'0'        WRITE
TYPE250  DC    HL1'0'        WRITE
TYPE251  DC    HL1'0'        WRITE
TYPE252  DC    HL1'0'        WRITE
TYPE253  DC    HL1'0'        WRITE
TYPE254  DC    HL1'0'        WRITE
TYPE255  DC    HL1'0'        WRITE
         SPACE 2
         DS    0F
WORKAREA DSECT
SAVE     DS    18F
DBLEWD   DS    D                                                 JDM4
NUMERRS  DS    F                                                 JDM4
AREA     DS    CL8                                               JDM4
         DS    0F                                                JDM4
WTOAREA  DS    CL100                                             JDM4
WORKEND  EQU   *
         SPACE
         DS    0F
SMFREC   DSECT
         IFASMFR 14
         ORG   SMFJFCB1
         IEFJFCBN LIST=YES
         ORG   SMFREC
         IFASMFR 21                                              JDM4
         ORG   SMFREC
         IFASMFR 40
         END
