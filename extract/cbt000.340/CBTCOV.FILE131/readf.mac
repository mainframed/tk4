UT04     TITLE 'ROUTINES TO READ/WRITE FB RECORDS'
         PRINT ON,NOGEN
         EJECT
*$DOC@*****************************************************************
* THESE ROUTINES ALLOW THE READING AND WRITING OF UNFORMATTED  DATA   *
* DATA SETS OF FIXED LENGTH LOGICAL RECORDS. (QSAM)                   *
*$DOC$*****************************************************************
*
***********************************************************************
* READ ROUTINE                                                        *
***********************************************************************
*
READF    START
         SAVE  (14,12),,*
         LR    12,15
         USING READF,12                 SET UP STANDARD LINKAGE AND
         BAL   14,GETDCB                GET DCB ADDR IN REG. 9.
         USING IHADCB,9
         TM    DCBOFLGS,X'04'           Q. IS TRYING TO READ PASSED EOF
         BO    EOFPASS                  A. YES. MUST RESETF FIRST
         TM    DCBOFLGS,X'90'           TEST FOR DS OPEN PROPERLY.
         BO    NORESET
         TM    DCBOFLGS,X'10'
         BO    GET
         OPEN  ((9),(INPUT))            OPEN DS FOR INPUT.
         TM    DCBOFLGS,X'10'
         BC    8,DDERROR
*
GET      CLI   4(2),X'80'               Q. IS 3RD & LAST ARGUMENT
         BE    DOWN                    LRECL PARM NOT PASSED
         SR    8,8                      CLEAR 8
         L     7,8(0,2)                 GET ADDR OF 3RD  PARAMETER
         L     8,0(0,7)                 GET PASSED LRECL LENGTH
*
         CH    8,DCBLRECL               Q. SAME AS DCBLRECL LENGTH
         BNE   LRECLERR                 A. NO
DOWN     L     2,4(0,2)                 GET ADDR TO MOVE RECORD INTO
         GET   (9),(2)                  MOVE NEXT RECORD TO CALLER.
*
         B     RETURN                   RETURN TO CALLER
         EJECT
***********************************************************************
* WRITE ROUTINE                                                       *
***********************************************************************
*
         ENTRY WRITEF
WRITEF   SAVE  (14,12),,*
         LR    12,15
         USING WRITEF,12                SET UP STANDARD LINKAGE AND
         BAL   14,GETDCB                GET DCB ADDR IN REG. 9.
*
         TM    DCBOFLGS,X'90'           TEST FOR DS OPEN PROPERLY.
         BO    PUT
         TM    DCBOFLGS,X'10'
         BNO   OPEN                     IF NOT OPEN,OPEN IT.
         L     3,16                     ELSE TEST FOR DUMMY ( A(CVT))
         L     3,0(3)                   LOAD A(CVTTCBP)
         L     3,4(3)                   LOAD A(CUR TCB)
         L     3,12(3)                  LOAD A(TIOT)
         LH    4,40(9)                  LOAD TIOT OFFSET
         L     3,16(4,3)                LOAD A(UCB)
         LA    3,0(3)                   CLEAR HIGH ORDER BYTE
         LTR   3,3                      TEST FOR ZERO
         BZ    RETURN                   IF ZERO, RETURN WITHOUT PUT
         B     NORESET                  ELSE, ABEND
*
OPEN     OPEN  ((9),(OUTPUT))           OPEN DS FOR OUTPUT
         TM    DCBOFLGS,X'10'
         BC    8,DDERROR
*
*                                       GET ADDR TO WRITE FROM
PUT      L     2,4(0,2)
         PUT   (9),(2)                  WRITE THE RECORD AND RETURN.
         B     RETURN
         EJECT
***********************************************************************
* RESET ROUTINE                                                       *
***********************************************************************
*
         ENTRY RESETF
RESETF   SAVE  (14,12),,*
         LR    12,15
         USING RESETF,12                SETUP STANDARD LINKAGE AND
         BAL   14,GETDCB                GET DCB ADDR IN REG. 9.
*
         CLOSE ((9),REREAD)             CLOSE THE DS AND RETURN
         B     RETURN
*
***********************************************************************
* END OF DS ROUTINE                                                   *
***********************************************************************
*
EODS     BALR  12,0                     RETURN WITH RC = 4.
         USING *,12
         L     13,4(0,13)
         RETURN (14,12),RC=4
*
***********************************************************************
* RETURN ROUTINE                                                      *
***********************************************************************
*
RETURN   BALR  12,0                     RETURN WITH RC = 0.
         USING *,12
         L     13,4(0,13)
         RETURN (14,12),RC=0
         EJECT
***********************************************************************
* ESTABLISH STANDARD LINKAGE AND GET DCB ADDR IN REG. 9.
***********************************************************************
*
GETDCB   BALR  10,0
         USING *,10
         LR    3,13                     ESTABLISH LINKAGE
         L     13,=A(SAVEAREA)
         ST    13,8(0,3)
         ST    3,4(0,13)
         LR    2,1
         L     9,0(0,2)
         L     9,0(0,9)
         C     9,=F'0'                  Q. DDNAME IN RANGE (0-14)
         BL    RANGERR                  A. NO. OUT OF RANGE
         C     9,=F'14'                 X
         BH    RANGERR                  A. NO. OUT OF RANGE
         SLL   9,2
         L     9,DCBADDRS(9)            GET DCB ADDR.
         BR    14
*
SAVEAREA DS    18F
DCBADDRS DC    A(DCB0)
         DC    A(DCB1)
         DC    A(DCB2)
         DC    A(DCB3)
         DC    A(DCB4)
         DC    A(DCB5)
         DC    A(DCB6)
         DC    A(DCB7)
         DC    A(DCB8)
         DC    A(DCB9)
         DC    A(DCB10)
         DC    A(DCB11)
         DC    A(DCB12)
         DC    A(DCB13)
         DC    A(DCB14)
*
DDERROR  ABEND 3000,DUMP
NORESET  ABEND 3004,DUMP
RANGERR  ABEND 3008,DUMP                DDNAME NOT WITHIN SET0000(0-14)
EOFPASS  ABEND 3012,DUMP                ATTEMPTED TO READ PASSED EOF
*                                       WITHOUT A RESET(DCB)
LRECLERR ABEND 3016,DUMP                RECORD LENGTH WRONG
ERRARGS  ABEND 3020,DUMP
*
         LTORG
         EJECT
DCB0     DCB   DDNAME=FORT0000,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB1     DCB   DDNAME=FORT0001,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB2     DCB   DDNAME=FORT0002,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB3     DCB   DDNAME=FORT0003,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB4     DCB   DDNAME=FORT0004,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB5     DCB   DDNAME=FORT0005,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB6     DCB   DDNAME=FORT0006,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB7     DCB   DDNAME=FORT0007,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB8     DCB   DDNAME=FORT0008,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB9     DCB   DDNAME=FORT0009,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB10    DCB   DDNAME=FORT0010,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB11    DCB   DDNAME=FORT0011,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB12    DCB   DDNAME=FORT0012,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB13    DCB   DDNAME=FORT0013,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
DCB14    DCB   DDNAME=FORT0014,DSORG=PS,MACRF=(GM,PM),EODAD=EODS
*
         DCBD  DSORG=(QS)
         END
