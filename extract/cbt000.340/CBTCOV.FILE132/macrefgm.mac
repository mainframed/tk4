*         *************** - MACREFGM -  ******************        .PRC.
*
*         ENTERED JUST AFTER SYSLIB IS OPENED.
*         GETMAIN AN AREA FOR MACRO REFERENCE TABLES.
*         GET DSNAME AND VOLUME OF EACH LIBRARY.
*         FILL MEMBER TABLE WITH ZEROES.
*
*         WRITTEN BY BILL GODFREY, PLANNING RESEARCH CORPORATION,
*         PRC COMPUTER CENTER INC, MCLEAN VA
*
         L     R1,JSYSLIB               GET ADDRESS OF SYSLIB DCB
         USING IHADCB,R1
         L     R5,DCBDEBAD              GET ADDRESS OF SYSLIB DEB
         DROP  R1
         SLR   R3,R3
         IC    R3,4(,R5)                DEBAMLNG, NUMBER OF CONCATS
         LA    R1,1(,R3)                PLUS 1 FOR FIRST LIBRARY
         LA    R0,@BDSNS                LENGTH OF EACH DSNAME/VOL
         MR    R0,R0                    MULTIPLY R1 BY 50
         LA    R4,@BMAX                 TABLE CAPACITY
         SLL   R4,4                     MULTIPLY BY 16
*        MH    R4,=AL2(@BELL)           (REPLACE SLL IF @BELL NOT 16)
         LA    R0,@BL                   LENGTH OF PREFIX
         AR    R0,R4                    PLUS LENGTH OF MEMBER TABLE
         AR    R0,R1                    PLUS LENGTH OF DSNAME AREA
         LR    R6,R0                    SAVE LENGTH ACROSS GETMAIN
         GETMAIN R,LV=(0)
         ST    R1,JSAFE                 SAVE ADDRESS IN JCOMMON
         MVI   JSAFE,X'77'              INDICATE IT IS OURS
         ST    R6,0(,R1)                SAVE LENGTH FOR FREEMAIN
         LR    R6,R1
         USING @B,R6
         LA    R3,1(,R3)                NUMBER OF CONCATS
         STH   R3,@BDSNN                 PLUS 1 FOR FIRST LIBRARY
         SLR   R2,R2                    NUMBER OF NAMES IN TABLE
         LA    R3,@BL(,R6)              FIRST ENTRY IN TABLE
         LA    R5,0(R4,R3)              END OF TABLE
         ST    R5,@BDSNA                ADDRESS OF DSNAME/VOL TABLE
         LA    R4,@BELL                 LENGTH OF AN ENTRY
         SLR   R5,R4                    LAST ENTRY IN TABLE
         STM   R2,R5,@BCOUNT            SAVE COUNT AND BXLE VALUES
         MVI   @BFLAG,0
         LA    R0,@BDSNS                LENGTH OF
         STH   R0,@BDSNL                 EACH DSNAME ENTRY
         L     R5,16                    CVTPTR
         L     R5,0(,R5)                TCB WORDS
         L     R5,4(,R5)                CURRENT TCB
         L     R5,12(,R5)               TIOT
         L     R4,JSYSLIB
         USING IHADCB,R4
         MVC   4(4,R3),@BDSNA           POINTER TO DSNAME SAVE AREA
         MVC   08(2,R3),DCBTIOT         SAVE ORIGINAL TIOT OFFSET
         MVC   12(4,R3),DCBEXLST        SAVE ORIGINAL EXLST
         XC    DCBEXLST+1(3),DCBEXLST+1 ZERO EXLST ADDRESS
         LA    R1,16(,R3)               POINT TO NEW EXLST
         O     R1,DCBEXLST              RESTORE FIRST 8 BITS
         ST    R1,DCBEXLST              SET NEW EXLST IN DCB
         LA    R0,20(,R3)               POINT TO 176 BYTE JFCB AREA
         ST    R0,0(,R1)                PUT ADDRESS IN EXLST FIELD
         MVI   0(R1),X'87'              MARK EXLST FIELD AS JFCB
         MVI   0(R3),X'80'              SET RDJFCB MF=L BYTE 1
         LH    R2,@BDSNN                NUMBER OF LIBRARIES
@RDJLOOP EQU   *
         RDJFCB ((R4)),MF=(E,(R3))
         SPACE
*               THE FOLLOWING INSTRUCTIONS CAN REPLACE THE RDJFCB
*               ON THE MVS OPERATING SYSTEM, BUT NOT ON OS/MVT.
*        LH    R1,DCBTIOT               GET TIOT OFFSET
*        AR    R1,R5                    POINT TO TIOT ENTRY
*        L     R1,12(,R1)               TIOEJFCB
*        SRL   R1,8                     SHIFT ADDRESS TO RIGHT
*        L     R1,0(,R1)                POINT TO JFCB PREFIX
*        LA    R1,08(,R1)               POINT TO JFCB
*        L     R15,16(,R3)              POINT TO RECEIVING FIELD
*        MVC   0(176,R15),0(R1)         RDJFCB
         SPACE
         L     R1,16(,R3)               POINT TO JFCB
         L     R15,4(,R3)               POINT TO NEXT DSNAME AREA
         MVC   0(44,R15),0(R1)          COPY DSNAME FROM JFCB
         MVC   44(6,R15),118(R1)        COPY VOLUME FROM JFCB
         AH    R15,@BDSNL               POINT TO NEXT DSNAME
         ST    R15,4(,R3)               SAVE NEXT DSNAME POINTER
         LH    R1,DCBTIOT               GET TIOT OFFSET
         AR    R1,R5                    POINT TO TIOT ENTRY
         SLR   R0,R0
         IC    R0,0(,R1)                LENGTH OF THIS ENTRY
         AR    R1,R0                    POINT TO NEXT TIOT ENTRY
         SR    R1,R5                    COMPUTE OFFSET
         STH   R1,DCBTIOT               NEW TIOT OFFSET TO FOOL RDJFCB
         BCT   R2,@RDJLOOP
         MVC   DCBTIOT(2),08(R3)        RESTORE TIOT OFFSET
         MVC   DCBEXLST(4),12(R3)       RESTORE EXLST
         DROP  R4                       IHADCB
         LM    R3,R5,@BSEARCH
         DROP  R6                       @B
@LCLEAR  XC    0(@BELL,R3),0(R3)
         BXLE  R3,R4,@LCLEAR
*        **************** - MACREFGM -  *************** END
