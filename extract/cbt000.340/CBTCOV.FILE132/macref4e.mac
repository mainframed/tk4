*         *************** - MACREF4E -  ******************        .PRC.
*
*         ENTERED JUST AFTER THE ESD IS PRINTED.
*         CHECKS FOR PRESENCE OF 'MACREF' AS A SWITCH.
*         IF NOT PRESENT, JUST DO A FREEMAIN.
*         SORT THE MEMBER NAME TABLE BY RELATIVE LIBRARY.
*         PRINT OUT THE TABLE IN THE SYSPRINT FILE, WITH
*         LIBRARY NAME NEXT TO FIRST MEMBER MAME.
*         FREEMAIN THE TABLE.
*
*         WRITTEN BY BILL GODFREY, PLANNING RESEARCH CORPORATION,
*         PRC COMPUTER CENTER INC, MCLEAN VA.
*
@MACREF  LA    R14,JSAFE                POINT TO TABLE POINTER
         CLI   0(R14),X'77'             IS THERE A BLDL TABLE?
         BNE   EXIT                     NO - BRANCH
         GOIF  JLIST,NO=@NOLIST
         L     R6,0(,R14)               POINT TO GETMAINED AREA
         USING @B,R6
         L     R1,16                    CVTPTR
         L     R1,0(,R1)                TCB WORDS
         L     R1,4(,R1)                CURRENT TCB
         L     R1,12(,R1)               TCBTIOT
         LA    R1,24(,R1)               FIRST DD ENTRY
@TIOT    TM    0(R1),X'FF'              END OF TIOT
         BZ    @NOLIST                  YES - BRANCH
         CLC   4(8,R1),@DDNAME          DDNAME EQUAL?
         BE    @TIOTX                   YES - BRANCH
         SLR   R14,R14
         IC    R14,0(,R1)               LENGTH OF ENTRY
         LA    R1,0(R14,R1)             POINT TO NEXT DD
         B     @TIOT
@TIOTX   EQU   *
         SPACE 1
@SORT    L     R2,@BCOUNT               GET NUMBER OF NAMES
         BCTR  R2,0                     NUMBER OF COMPARES
         LTR   R2,R2                    ANYTHING TO SORT?
         BNP   @SORTX                   NO - BRANCH
@SORTOUT LR    R15,R2                   COMPARES THIS PASS
         SLR   R1,R1                    SET SWAP FLAG OFF
         LM    R3,R4,@BSEARCH           GET SEARCH VALUES
@SORTIN  CLC   1(9,R3),@BELL+1(R3)      COMPARE 2 ENTRIES
         BNH   @SWAPX                   BRANCH IF IN ORDER
         XC    000(@BELL,R3),@BELL(R3)   SWAP
         XC    @BELL(@BELL,R3),000(R3)    SWAP
         XC    000(@BELL,R3),@BELL(R3)     SWAP
         BCTR  R1,0                     SHOW SWAP OCCURRED
@SWAPX   AR    R3,R4                    NEXT ENTRY
         BCT   R15,@SORTIN              BRANCH IF MORE COMPARES
         LTR   R1,R1                    ANY SWAPS DURING PASS
         BZ    @SORTX                   NO - QUIT EARLY
         BCT   R2,@SORTOUT              REDUCE NUM OF COMPARES
@SORTX   EQU   *
         SPACE 1
         LM    R3,R5,@BSEARCH           GET SEARCH VALUES
         SLR   R2,R2                    FORCE FIRST HEADING
@LIST    EQU   *
         CLI   0(R3),0                  BLDL FAIL FOR ENTRY?
         BNZ   @SKIP                    YES - SKIP IT
         CLI   2(R3),0                  ENTRY EMPTY?
         BE    @LISTX                   YES - DONE
         LTR   R2,R2                    LINECOUNT EXHAUSTED?
         BNZ   @PUT                     NO - BRANCH
@PAGETOP EQU   *
         JPRINT                         GET PRINT BUFFER
         USING @DSECT11,R11
         MVI   @CC,0                    EJECT
         MVC   @DECKID,JDECKID
         MVC   @TITLE,@ZTITLE
         TM    @BFLAG,X'01'             INCOMPLETE?
         BZ    *+10                     NO
         MVC   @INC,@ZINC               (INCOMPLETE)
         MVC   @PAGE,@ZPAGE             PAGE
         LH    R14,JPAGENO              GET OLD PAGE NUMBER
         LA    R14,1(,R14)              INCREMENT BY 1
         STH   R14,JPAGENO              SAVE PAGE NUMBER
         CVD   R14,JDWORD               CONVERT PAGE NUMBER TO DECIMAL
         UNPK  @PAGENO,JDWORD+6(2)      UNPACK TO EBCDIC
         NC    @PAGENO,@DIGMASK         TRANSLATE TO INTERNAL CODE
         GOIF  @PAGENO,J0,NOTEQ=@PAGEX  SKIP IF NOT A LEADING ZERO
         MVI   @PAGENO,JBLANK
         GOIF  @PAGENO+1,J0,NOTEQ=@PAGEX
         MVI   @PAGENO+1,JBLANK
@PAGEX   JPRINT                         GET NEXT PRINT BUFFER
         MVC   @HEAD,@ZHEAD
         MVC   @LVTMDT,JLVTMDT          LEVEL, TIME, DATE
         LH    R2,JLNCT                 GET MAXIMUM LINE COUNT
@PUT     JPRINT                         GET NEXT PRINT BUFFER
         MVI   @CC,1                    SPACE1
         CH    R2,JLNCT                 IS THIS FIRST LINE ON THE PAGE
         BNL   @BREAK                   YES, SPACE2 AND DSNAME
         CLC   @BFLAG+1(1),1(R3)        SAME LIBRARY AS LAST MEMBER
         BE    @SAME                    YES
@BREAK   MVI   @CC,2                    NO, SPACE2
         MVC   @BFLAG+1(1),1(R3)        UPDATE BREAK FIELD
         SLR   R15,R15
         IC    R15,1(,R3)
         MH    R15,@BDSNL               GET INDEX INTO DSNAME TABLE
         A     R15,@BDSNA               POINT TO DSNAME ENTRY
         MVC   @DSNAME,0(R15)           GET DSNAME
         TR    @DSNAME,JTRTABLE         TRANSLATE TO INTERNAL
         LA    R1,@DSNAME+43            FIND
@DSREV   CLI   0(R1),JBLANK              LAST
         BNE   @DSEND                     CHARACTER
         BCT   R1,@DSREV                   IN DSNAME
@DSEND   MVC   2(2,R1),@ON              ADD ' ON '
         MVC   5(6,R1),44(R15)          MOVE VOLUME
         TR    5(6,R1),JTRTABLE         TRANSLATE TO INTERNAL
@SAME    MVC   @LINE+1(8),2(R3)         MOVE MEMBER NAME
         TR    @LINE+1(8),JTRTABLE      TRANSLATE TO INTERNAL
         SLR   R15,R15
         IC    R15,1(,R3)               K OF TTRKZC
         LA    R15,1(,R15)              ZERO-BASE TO ONE-BASE
         CVD   R15,JDWORD
         UNPK  @CONCAT,JDWORD+6(2)      UNPACK CONCAT NUMBER
         NC    @CONCAT,@DIGMASK         TRANSLATE TO INTERNAL CODE
         GOIF  @CONCAT,J0,NOTEQ=@CONCX  SKIP IF NOT A LEADING ZERO
         MVI   @CONCAT,JBLANK
         GOIF  @CONCAT+1,J0,NOTEQ=@CONCX
         MVI   @CONCAT+1,JBLANK
@CONCX   EQU   *
         BCTR  R2,0                     DECREMENT LINECOUNT
@SKIP    EQU   *
         BXLE  R3,R4,@LIST
@LISTX   EQU   *
@NOLIST  L     R0,@BFREE
         LR    R1,R6
         DROP  R6                       @B
         FREEMAIN R,LV=(0),A=(1)
         B     EXIT
         SPACE
@DDNAME  DC    CL8'MACREF'
@MAX     DC    AL2(@BMAX)               TABLE CAPACITY 256 NAMES
@DIGMASK DC    X'0F0F0F'
@ZTITLE  JGENIN 'MACRO LIBRARY REFERENCE LIST'
@ZINC    JGENIN '(INCOMPLETE)'
@ZPAGE   JGENIN 'PAGE'
@ZHEAD   JGENIN '3MEMBER    CONCAT  LIBRARY'
@ON      JGENIN 'ON'
*
@DSECT11 DSECT
@LINE    DS    0CL121
@CC      DS    C
@DECKID  DS    CL8
         ORG   @LINE+47
@TITLE   DS    C'MACRO LIBRARY REFERENCE LIST'
         DS    C
@INC     DS    C'(INCOMPLETE)'
         ORG   @LINE+112
@PAGE    DS    CL4,CL2
@PAGENO  DS    CL3
         ORG   @LINE
@HEAD    DS    C'3MEMBER    CONCAT  LIBRARY'
         ORG   @LINE+121-L'JLVTMDT
@LVTMDT  DS    CL(L'JLVTMDT)
         ORG   @LINE
         DS    CL12
@CONCAT  DS    CL3
         DS    CL4
@DSNAME  DS    CL44
*
*        **************** - MACREF4E -  ****************** END
