          TITLE '*----NSP012----*  JCL XREF -- PDS ACCESS SUBROUTINE'
***********************************************************************
*                                                                     *
*              OPERATIONS               PARM LIST                     *
*                                                                     *
*              1. OPEN PDS              0    DDNAME                   *
*                 ABEND 110 = DD MISSING                              *
*                                                                     *
*              2. READ DIRECTORY        4    MEMBER NAME              *
*                 RC 4 = END OF DIRECTORY                             *
*                                                                     *
*              3. FIND MEMBER           8    MEMBER NAME              *
*                 RC 4 = MEMBER NOT FOUND                             *
*                                                                     *
*              4. READ RECORD           12   INPUT AREA               *
*                 RC 4 = END OF MEMBER                                *
*                                                                     *
*              5. CLOSE PDS             16                            *
*                                                                     *
*                                       (OP CODE MUST BE A FULLWORD)  *
*                                                                     *
***********************************************************************
         EJECT
         GENESIS CSECT=NSP012,ID=NSP012,EQU=Y,BASE=R13
*                                                                     *
         PRINT NOGEN
HEX00    EQU   X'00'                    EQUATES
HEX10    EQU   X'10'
HEX1F    EQU   X'1F'
HEXFF    EQU   X'FF'
*                                                                     *
         USING PDSDSCT,R6
*
         LM    R5,R6,0(R1)              PICKUP OP CODE AND AREA
         L     R5,0(,R5)
         LM    R7,R10,SAVEREGS          LOAD REGS
         C     R5,=F'0'                 IS IT OPEN
         BE    OPEN10
         C     R5,=F'4'                 IS IT READ DIRECTORY
         BE    RDD10
         C     R5,=F'8'                 IS IT FIND MEMBER
         BE    FIND10
         C     R5,=F'12'                IS IT READ MEMBER
         BE    RDM10
         C     R5,=F'16'                IS IT CLOSE
         BE    CLOSE10
         L     R15,=F'8'                SET RETURN CODE TO 8
         B     RETURN
*                                                                     *
RETURN   EQU   *                        RETURN TO CALLER
         STM   R7,R10,SAVEREGS          SAVE REGS
         L     R13,4(,R13)
         RETURN (14,12),RC=(15)
*                                                                     *
OPEN10   EQU   *
         MVC   SYSLIB+40(8),0(R6)       MOVE DDNAME TO DCBS
         MVC   DIRCTRY+40(8),0(R6)
*
         CLI   PARMSW,0
         BNE   OPEN15
*
         CLI   PDSPARM,C'A'             IS THERE A PARM?
         BL    OPEN12                   NO -- BRANCH
*
         CLI   PDSPARM,C'Z'             IS IT VALID?
         BH    OPEN12                   NO -- BRANCH
*
         MVC   HIMEMBER,PDSPARM        SAVE THE PARM INFO
OPEN12   MVI   PARMSW,X'FF'             SET THE SWITCH
*                                                                     *
OPEN15   OPEN  (DIRCTRY,,SYSLIB)        OPEN PDS
         TM    SYSLIB+48,HEX10          DID SYSLIB OPEN OK
         BZ    OPEN30                   NO - ASSUME DDCARD MISSING
         MVI   OPENSW,HEXFF             MARK SWITCH AS OPEN
         MVI   DIRSW,HEX00              SET DIR SW OFF
         SR    R15,R15
         B     RETURN
OPEN30   EQU   *
         MVC   OPENWTO+4(8),0(R6)       MOVE DDNAME TO WTO
         LA    R1,OPENWTO
         WTO   MF=(E,(R1))
         ABEND 110,DUMP,STEP
*                                                                     *
OPENWTO  WTO   '          DD MISSING',ROUTCDE=11,MF=L
*                                                                     *
         EJECT
RDD10    EQU   *                        READ DIRECTORY
         CLI   DIRSW,HEX00              IS DIR SW ON
         BE    RDD30                    NO - GO TO GET DIR BLOCK
         NI    11(R9),HEX1F             ZERO NON-HALFWORD BITS
         SR    R2,R2                    ZERO WORK REG
         IC    R2,11(,R9)               PICKUP NO. OF USER HALFWORDS
         SLL   R2,1                     MULTIPLY BY 2
         LA    R9,12(R2,R9)             BASIC LENGTH + USER LENGTH
         LA    R2,12(,R2)               ADD BASIC LENGTH TO USER LNTH
         SR    R8,R2                    IS BLOCK COMPLETE
         BNP   RDD30                    YES - GO TO GET ANOTHER BLOCK
         CLI   0(R9),HEXFF              IS THIS A SHORT BLOCK
         BE    RDD90                    YES - END OF DIRECTORY
         B     RDD80                    NO - RETURN THIS MEMBER TO USR
RDD30    EQU   *
         GET   DIRCTRY,DIRAREA          GET DIRECTORY BLOCK
         CLI   DIRAREA+2,HEXFF          ANY MEMBERS IN THE RECORD
         BE    RDD90                    NO - END OF DIRECTORY
         LH    R8,DIRAREA               YES - PICKUP NO. OF BYTES USED
         LA    R9,DIRAREA+2             START OF DIRECTORY ENTRIES
         S     R8,=F'2'                 SUBTRACT LENGTH OF ENTRY
         MVI   DIRSW,HEXFF              SET DIR SW ON
         B     RDD80
*                                                                     *
RDD80    EQU   *
         MVC   0(8,R6),0(R9)            MOVE MEMBER NAME TO USER
         CLI   HIMEMBER,C' '            ARE WE LOOKING FOR SPECIFIC
         BE    RDD85                    NO -- BRANCH
         CLC   HIMEMBER,0(R6)           DOES THIS ONE MATCH?
         BNE   RDD10                    NO -- BRANCY
RDD85    SR    R15,R15
         B     RETURN
RDD90    EQU   *
         LA    R15,4
         B     RETURN
*                                                                     *
         EJECT
FIND10   EQU   *
         MVC   MEMBER,0(R6)
         FIND  SYSLIB,MEMBER,D          LOCATE MEMBER
         B     *+4(R15)                 TEST RETURN CODE
         B     FIND20                   FIND OK
         B     FIND30                   MEMBER NOT FOUND
         B     FIND40                   IO ERROR
FIND20   EQU   *
         SR    R15,R15                  SET RC TO 0
         B     FIND90
FIND30   EQU   *
         L     R15,=F'4'                SET RC TO 4
         B     FIND90
FIND40   EQU   *
         L     R15,=F'8'                SET RC TO 8
         B     FIND90
FIND90   EQU   *
         B     RETURN
*                                                                     *
         EJECT
RDM10    EQU   *
         CLI   MBRSW,HEXFF
         BE    RDM50
         LA    R7,BLOCK                 ADDR OF INPUT AREA
         READ  BPDECB,SF,SYSLIB,(R7),'S'     READ NEXT BLOCK
         CHECK BPDECB
         LH    R10,SYSLIB+62            BLKSIZE OF RECORD
         L     R1,SYSLIB+68             ADDR OF IOB PREFIX
         CLC   21(3,R1),DCBADR+1        IS THIS THE PREFIX
         BE    *+8                      NO - WE'RE AT THE IOB START
         LA    R1,8(,R1)                YES - INCREMENT TO IOB START
         SH    R10,14(,R1)              SUBTRACT RESIDUAL BYTE COUNT
         MVI   MBRSW,HEXFF
*                                                                     *
RDM20    EQU   *
         LH    R1,SYSLIB+82             PICKUP LRECL
         BCTR  R1,0
         EX    R1,RDMMVC                MOVE RECORD TO USER AREA
         B     RDM90
RDMMVC   MVC   0(0,R6),0(R7)
*                                                                     *
RDM50    EQU   *
         AH    R7,SYSLIB+82             ADD LOGICAL REC LEN TO REC ADR
         SH    R10,SYSLIB+82            DECREMENT REMAINING BYTE CNT
         BNP   RDM60                    ENTIRE BLOCK HAS BEEN READ
         B     RDM20                    DATA LEFT
RDM60    EQU   *
         MVI   MBRSW,HEX00
         B     RDM10
RDM90    EQU   *
         SR    R15,R15
         B     RETURN
MBREOF   EQU   *
         LA    R15,4
         B     RETURN
*                                                                     *
CLOSE10  EQU   *
         CLOSE (DIRCTRY,,SYSLIB)
         MVI   OPENSW,HEX00             RESET SWITCHES
         MVI   DIRSW,HEX00
         MVI   MBRSW,HEX00
         SR    R15,R15
         B     RETURN
*                                                                     *
         EJECT
SYSDCB   DS    0F
SYSLIB   DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=R,EODAD=MBREOF
*                                                                     *
DIRDCB   DS    0F
DIRCTRY  DCB   DDNAME=SYSLIB,DSORG=PS,MACRF=GM,                        X
               RECFM=F,LRECL=256,BLKSIZE=256
*                                                                     *
         LTORG
*                                                                     *
HIMEMBER DC    C' '
PARMSW   DC    X'00'                    PARM PROCESSED SWITCH
OPENSW   DC    X'00'                    SWITCHES
DIRSW    DC    X'00'
MBRSW    DC    X'00'
*                                                                     *
DCBADR   DC    A(SYSLIB)                SYSLIB DCB ADDR
SAVEREGS DC    4F'0'
*                                                                     *
MEMBER   DC    CL8' '                   MEMBER NAME
*                                                                     *
DIRAREA  DC    CL256' '                 DIRECTORY BLOCK
         DC    CL10' '
*                                                                     *
BLOCK    DS    4CL4096                  DATA BLOCK
*                                                                     *
         EJECT
PDSDSCT  DSECT
PDSNAME1 DS    CL8
PDSNAME2 DS    CL8
PDSMBR   DS    CL8
PDSSW    DS    X
PDSPARM  DS    C                INFO FROM THE EXEC PARM FIELD
         END
