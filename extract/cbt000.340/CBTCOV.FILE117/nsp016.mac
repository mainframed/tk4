          TITLE '*----NSP016----*  JCL XREF -- CURRENT DATE SUBROUTINE'
***********************************************************************
*                                                                     *
*              MUST BE LINKED AS REUSABLE........                     *
*              THIS ROUTINE WILL SUPPLY THE USER WITH CURRENT TIME    *
*              AND SEVERAL FORMS OF CURRENT DATE                      *
*                                                                     *
*              THE USER MUST PROVIDE A 55 CHAR AREA                   *
*                                                                     *
*        DS    0CL55                                                  *
*        DS    CL8       HH.MM.SS                                     *
*        DS    CL1                                                    *
*        DS    0CL9                                                   *
*        DS    CL3       MON                                          *
*        DS    CL1                                                    *
*        DS    CL2       DA                                           *
*        DS    CL1                                                    *
*        DS    CL2       YR                                           *
*        DS    CL1                                                    *
*        DS    CL8       MO/DA/YR                                     *
*        DS    CL1                                                    *
*        DS    CL2       YEAR                                         *
*        DS    CL3       DDD                                          *
*        DS    CL1                                                    *
*        DS    CL19      MONTH DA, YEAR                               *
*                                                                     *
*              HH.MM.SS JAN 01 76 01/01/76 76001 JANUARY  1, 1976     *
*                                                                     *
***********************************************************************
         EJECT
NSP016   CSECT
         STM   14,12,12(13)             ENTRY
         BALR  12,0
         USING *,12
         ST    13,MYSAVE+4
         LA    10,MYSAVE
         ST    10,8(13)
         LR    13,10
*                                                                     *
         USING PARM,5
         L     5,0(1)                   LOAD ADDR OF CALLER'S AREA
*                                                                     *
         MVI   DATE1,X'40'              BLANK AREA
         MVC   DATE1+1(54),DATE1
         ST    5,DOUBLE                 IS THIS LAST PARM
         TM    DOUBLE,X'80'
         BO    A50                      YES
         L     6,4(1)                   NO
         MVC   DATE6,0(6)               USE SECOND PARM INSTEAD OF
         OI    DATE6+4,X'F0'                 CURRENT DATE
         B     A80
*                                                                     *
A50      EQU   *
         TIME  DEC                      GET TIME AND DATE
         ST    0,FWRD
         OI    FWRD+3,X'0F'
         UNPK  DATE1,FWRD               MOVE HHMMSS TO DATE1
         MVC   DATE1(2),DATE1+1
         MVI   DATE1+2,C'.'
         MVC   DATE1+7(1),DATE1+6
         MVC   DATE1+6(1),DATE1+5
         MVI   DATE1+5,C'.'
         ST    1,FWRD
         OI    FWRD+3,X'0F'
         UNPK  DATE6,FWRD               MOVE YYDDD TO DATE6
A80      EQU   *
*                                                                     *
         PACK  DOUBLE,DATE6(2)          PACK YY
         CVB   6,DOUBLE
         PACK  DOUBLE,DATE6+2(3)        PACK DDD
         CVB   7,DOUBLE
         LR    9,6                      LOAD R9 WITH YY
         A     9,FCENT                  ADD 1900
         SR    8,8
         LA    10,4
         DR    8,10                     DIVIDE 19YY BY 4
         LTR   8,8                      IS REMAINDER ZERO
         BZ    B10
         LA    9,28                     NO
         B     B20
B10      LA    9,29                     YES
B20      STH   9,DAYFEB                 MOVE 28 OR 29 TO FEB
*                                                                     *
*                                                                     *
         LR    8,7                      LOAD 8 WITH DDD
         LA    2,13                     SET LOOP CTR
         LA    9,DAYTAB
         LA    6,MOTAB
         LA    10,MONTAB
         LA    11,MONTHTAB
C20      SH    8,0(9)                   SUBTRACT DAYS
         LTR   8,8                      IS DDD ZERO OR <ZERO
         BC    12,C40                   YES - GET OUT
         LA    9,2(9)                   NO
         LA    6,2(6)
         LA    10,3(10)
         LA    11,9(11)
         BCT   2,C20
*                                                                     *
*                                       ERROR                         *
         MVC   DATE1(10),=C'DATE ERROR'
         L     13,MYSAVE+4              RETURN
         RETURN (14,12),RC=4
*                                                                     *
*                                                                     *
C40      AH    8,0(9)                   ADD DAYS
         MVC   DATE2,0(10)              MOVE MONTH
         CVD   8,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  DATE3,DOUBLE             MOVE DAY
         MVC   DATE4,DATE6              MOVE YY
*                                                                     *
         MVC   DATE5(2),0(6)
         MVI   DATE5+2,C'/'
         MVC   DATE5+3(2),DATE3
         MVI   DATE5+5,C'/'
         MVC   DATE5+6(2),DATE6
*                                                                     *
*                                                                     *
         MVC   DATE7,0(11)              MOVE MONTH
         MVC   DATE8,DATE3              MOVE DA
         MVI   DATE9,C','
         MVC   DATE10(2),CENT
         MVC   DATE10+2(2),DATE6
         CLI   DATE8,C'0'               IS DA 0X
         BNE   *+8
         MVI   DATE8,C' '
*                                                                     *
D20      CLI   DATE7,C' '               SHIFT LEFT
         BNE   D40
         MVC   DATE7(18),DATE7+1
         B     D20
D40      EQU   *
*                                                                     *
GOBACK   L     13,MYSAVE+4              RETURN
         RETURN (14,12),RC=0
*                                                                     *
         EJECT
*                                                                     *
MYSAVE   DC    18F'0'                   SAVE AREA
*                                                                     *
FWRD     DC    F'0'                     WORK AREAS
DOUBLE   DS    D
*                                                                     *
FCENT    DC    F'1900'
CENT     DC    CL2'19'
*                                                                     *
*                                                                     *
DAYTAB   DS    0H
         DC    H'00'
         DC    H'31'
DAYFEB   DC    H'28'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
*                                                                     *
MONTAB   DS    0H
         DC    C'   '
         DC    C'JAN'
         DC    C'FEB'
         DC    C'MAR'
         DC    C'APR'
         DC    C'MAY'
         DC    C'JUN'
         DC    C'JUL'
         DC    C'AUG'
         DC    C'SEP'
         DC    C'OCT'
         DC    C'NOV'
         DC    C'DEC'
*                                                                     *
MOTAB    DS    0H
         DC    C'  '
         DC    C'01'
         DC    C'02'
         DC    C'03'
         DC    C'04'
         DC    C'05'
         DC    C'06'
         DC    C'07'
         DC    C'08'
         DC    C'09'
         DC    C'10'
         DC    C'11'
         DC    C'12'
*                                                                     *
MONTHTAB DS    0H
         DC    C'         '
         DC    C'  JANUARY'
         DC    C' FEBRUARY'
         DC    C'    MARCH'
         DC    C'    APRIL'
         DC    C'      MAY'
         DC    C'     JUNE'
         DC    C'     JULY'
         DC    C'   AUGUST'
         DC    C'SEPTEMBER'
         DC    C'  OCTOBER'
         DC    C' NOVEMBER'
         DC    C' DECEMBER'
*                                                                     *
PARM     DSECT
DATE1    DS    CL8                      HH.MM.SS
         DS    CL1
DATE2    DS    CL3                      MON
         DS    CL1
DATE3    DS    CL2                      DA
         DS    CL1
DATE4    DS    CL2                      YY
         DS    CL1
DATE5    DS    CL8
         DS    CL1
DATE6    DS    CL5                      YYDDD
         DS    CL1
DATE7    DS    CL9                      MONTH
         DS    CL1
DATE8    DS    CL2                      DA
DATE9    DS    CL1                      ,
         DS    CL1
DATE10   DS    CL4                      YEAR
         DS    CL1
*                                                                     *
         LTORG
         END   NSP016
