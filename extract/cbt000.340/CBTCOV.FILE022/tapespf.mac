TAPESPF  $PROLOG
*----------------------------------------------------------------------
*     RETURN CODES
*     0 - TMC RECORD LOCATED FOR DSN OR VOLSER
*     4 - SOMETHING NOT RIGHT
* BDTTP000 'DATASET NAME MISMATCH  '
* 'USER SUPPLIED DSN NOT EQUAL TO DSN ON USER SUPPLIED VOLSER'
* BDTTP001 'VOLSER NOT IN TMC      '
* 'USER SUPPLIED VOLSER NOT IN THE TMS DATABASE'
* BDTTP002 'VOLSER UNDETERMINABLE  '
* 'USER SUPPLIED DATASET NAME NOT CATALOGUED AND NO VOLSER SPECIFIED'
* BDTTP003 'VOLUME IS NOT A TAPE   '
* 'CATALOG INDICATES DATASET DOES NOT RESIDE ON A TAPE'
* BDTTP004 'NOT FIRST VOLUME       '
* 'VOLSER IS MIDDLE VOLUME OF A MULTIVOLUME DATASET'
* BDTTP005 'MULTIVOLUME DATASET    '
* 'VOLSER IS FIRST VOLUME OF MULTIVOLUME DATASET'
*
*----------------------------------------------------------------------
*----------------------------------------------------------------------
*   DEFINE SPF VARIABLES
*----------------------------------------------------------------------
         ISPF LOAD
         VDEFINE '(TAPEDSN)',TAPEDSN,CHAR,44   INPUT
         VDEFINE '(TAPEVOL)',TAPEVOL,CHAR,6    INPUT
         VDEFINE '(TMDSN)',TMDSN,CHAR,44
         VDEFINE '(TPBLKSIZ)',TPBLKSIZ,CHAR,5
         VDEFINE '(TPLRECL)',TPLRECL,CHAR,5
         VDEFINE  '(BLKCNT)',BLKCNT,CHAR,8
         VDEFINE  '(TMNXTVOL)',TMNXTVOL,CHAR,6
         VDEFINE  '(TPRECTYP)',TPRECTYP,CHAR,1
         VDEFINE  '(TPRECBLK)',TPRECBLK,CHAR,1
         VDEFINE  '(TPRECSPN)',TPRECSPN,CHAR,1
         VDEFINE  '(TMAUTHID)',TMAUTHID,CHAR,3
         VDEFINE  '(TPLABTYP)',TPLABTYP,CHAR,3
         VDEFINE  '(TPDENSE)',TPDENSE,CHAR,4
         VDEFINE  '(TPEXPDT)',TPEXPDT,CHAR,5
*----------------------------------------------------------------------
*    DETERMINE VOLSER FOR GIVEN DATASET NAME
*______________________________________________________________________
         VGET 'TAPEVOL'
         VGET 'TAPEDSN'
         OC   TAPEDSN,=CL44' '   MAKE ZEROS BLANKS
         CLC  TAPEDSN,=CL44' '
         BE   GETTMC
         LOCATE CAMLIST
         LTR  R15,R15
         BZ   GETVOL
         MVC  TPMSGID,=CL8'BDTTP002'   DATASET NOT CATALOGGED
         B    RETURN4
GETVOL   DS   0H
         CLC  TAPEVOL,=CL8' '       WAS VOLSER SPECIFIED
         BE   NODBLCHK
         CLC  TAPEVOL,CAMREPLY+6
         BE   NODBLCHK
         MVC  TPMSGID,=CL8'BDTTP000' VOLSER DONT MATCH
         B    RETURN4
NODBLCHK DS   0H
         MVC  TAPEVOL,CAMREPLY+6    GET THE VOLSER
         TM   CAMREPLY+4,UCB3TAPE      IS IT A TAPE
         BO   GETTMC
         MVC  TPMSGID,=CL8'BDTTP003'   SAY NOT A TAPE
         B    RETURN4
GETTMC   DS   0H
         OPENTMC NOTACT=RETURN4
         GETTMC TAPEVOL,TMRECORD,ERRRTN,ENQ=NO
         LH  R4,TMBLKSI      CONVER BLKSIZE TO CHARACTER
         CVD R4,DOUBLE
         UNPK TPBLKSIZ,DOUBLE
         OI   TPBLKSIZ+4,X'F0'
         LH  R4,TMLRECL      CONVERT LRECL TO CHARACTER
         CVD  R4,DOUBLE
         UNPK TPLRECL,DOUBLE
         OI   TPLRECL+4,X'F0'
         ICM  R4,15,TMBLKCNT    CONVERT BLKCOUNT TO CHARCTER
         CVD  R4,DOUBLE
         UNPK BLKCNT,DOUBLE
         OI   BLKCNT+7,X'F0'
*     SET UP RECFM VARIABLES
         TM   TMRECFM,DCBRECU       IS IT UNDEFINED?
         BO   UNDEFINE              YES,
         TM   TMRECFM,DCBRECF
         BO   FIXED
VARIABLE DS   0H
         MVI  TPRECTYP,C'V'
         B    BLOCKCHK
FIXED    DS   0H
         MVI  TPRECTYP,C'F'
         B    BLOCKCHK
UNDEFINE DS   0H
         MVI  TPRECTYP,C'U'
         B    RECFMDUN              DONE WITH RECFM IF U
BLOCKCHK DS   0H
         TM   TMRECFM,DCBRECBR      BLOCKED RECORDS?
         BZ   SPANCHK               NO, CHECK SPANNED
         MVI  TPRECBLK,C'B'         SET AS BLOCKED
SPANCHK  TM   TMRECFM,DCBRECSB      SPANNED
         BZ   RECFMDUN              NO DONE
         MVI  TPRECSPN,C'S'         SET AS SPANNED
RECFMDUN DS   0H
*   DENSITY TRANSLATION
         MVC  TPDENSE,=C'6250'      SET DEFAULT TO 6250
         CLI  TMDEN,X'E3'           IS IT A 3480
         BNE  CHK800
         MVC  TPDENSE,=C'E3  '
         B    TPDENDUN              ALL DONE DENSITY
CHK800   CLI  TMDEN,X'83'           800 BPI
         BNE  CHK1600
         MVC  TPDENSE,=C' 800'      SET AS 800
         B    TPDENDUN
CHK1600  CLI  TMDEN,X'C3'           1600
         BNE  TPDENDUN
         MVC  TPDENSE,=C'1600'      SET AS 1600
TPDENDUN DS   0H
*  DETERMINE LABEL TYPE
         MVC  TPLABTYP,=C'SL '
         CLI  TMLTYPE,X'01'
         BNE  CHKNSL
         MVC  TPLABTYP,=C'NL '
         B    LABTPDUN
CHKNSL   CLI  TMLTYPE,X'04'
         BNE  CHKBLP
         MVC  TPLABTYP,=C'NSL'
         B    LABTPDUN
CHKBLP   CLI  TMLTYPE,X'10'
         BNE  LABTPDUN
         MVC  TPLABTYP,=C'BLP'
LABTPDUN DS   0H
         UNPK   TPEXPDT,TMEXPDT  EXP DATE TO CHARACTER
         OI    TPEXPDT+4,X'F0'   ENSURE NUMERIC
*
         VPUT '(TMDSN TPBLKSIZ TPLRECL BLKCNT TMNXTVOL)'
         VPUT '(TPRECTYP TPRECBLK TPRECSPN)'
         VPUT '(TMDEN TMLTYPE TMAUTHID)'
         VPUT '(TPLABTYP TPDENSE)'
         VPUT '(TPEXPDT)'
         VPUT '(TAPEVOL)'
         ISPF DELETE
RETURN   $EPILOG 0
RETURN4  DS   0H
         SETMSG MSG=TPMSGID
         $EPILOG 4
ERRRTN   DS    0H
         MVC   TPMSGID,=CL8'BDTTP001'  NOT IN TMC
         B     RETURN4
F6       DC    F'6'
ISPLINK  DS    A
TAPEVOL  DC       CL6' '     USER SUPPLIED VOLSER
         DC       H'0'
TAPEDSN  DC       CL44' '    USER SUPPLIED DSN
TPLRECL  DC    CL5' '
TPBLKSIZ DC    CL5' '
BLKCNT   DC    CL8' '
TPRECTYP DC    C' '
TPRECBLK DC    C' '
TPRECSPN DC    C' '
TPLABTYP DC    CL3' '
TPDENSE  DC    CL4' '
TPEXPDT  DC    CL5' '
TPMSGID  DC    CL8' '
DOUBLE   DS    D
TMC      TMRECORD ,
CAMLIST  CAMLST  NAME,TAPEDSN,,CAMREPLY
CAMREPLY DS    265C
         IHADCB
         IEFUCBOB
         END
