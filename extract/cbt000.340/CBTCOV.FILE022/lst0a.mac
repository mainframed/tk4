000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.    LST0A.
000300 AUTHOR.        BOB ZIMMERMAN.
000400 INSTALLATION.  CNA INSURANCE.
000500 DATE-COMPILEd.
000600
000700******************************************************************
000800*    BOB ZIMMERMAN                                               *
000900*    CNA INSURANCE                                               *
001000*    (312) 822-5719                                              *
001100*    LAST MODIFIED 8506                                          *
001200******************************************************************
001300/
001400 ENVIRONMENT DIVISION.
001500 DATA DIVISION.
001600 WORKING-STORAGE SECTION.
001700
001800******************************************************************
001900*    THE FOLLOWING TWO ALPHABETS ARE USED TO TRANSLATE ANY PARMS *
002000*    PASSED THRU LINKAGE FROM LOWER-CASE TO UPPER-CASE. THE 05   *
002100*    LEVEL BELOW MARKED LOWER-CASE SHOULD BE THE ALPHABET IN     *
002200*    LOWER-CASE.                                                 *
002300******************************************************************
002400 01  THE-ALPHABET.
002500     05  UPPER-CASE                    PIC X(26) VALUE
002600         'ABCDEFGHIJKLMNOPQRSTUVWXYZ'.
002700     05  LOWER-CASE                    PIC X(26) VALUE
002800         'abcdefghijklmnopqrstuvwxyz'.
002900
003000
003100
003200******************************************************************
003300*    SPF-VARIABLES IS THE WORKING STORAGE AREA USED TO CONTAIN   *
003400*    THE CONTENTS OF THE ISPF VARIABLES USED IN THIS DIALOG.     *
003500*    THESE VARIABLES MAY BE ACCESSED USING EITHER THE VDEFINE    *
003600*    SERVICE OR THE VCOPY SERVICE.                               *
003700******************************************************************
003800
003900 01  SPF-VARIABLES.
004000     05  SPF-ZTDTOP                    PIC S9(09) COMP.
004100     05  SPF-CSR                       PIC S9(09) COMP.
004200     05  SPF-SEQUENCE                  PIC S9(09) COMP VALUE ZERO.
004300     05  SPF-LAST-CSR                  PIC S9(09) COMP VALUE ZERO.
004400     05  SPF-DDNAME                    PIC X(08) VALUE SPACES.
004500     05  SPF-ALTDDNAM                  PIC X(08) VALUE SPACES.
004600     05  SPF-SCAN                      PIC X(04) VALUE 'SCAN'.
004700     05  SPF-SCAN-BYTE                 PIC X(01) VALUE ' '.
004800     05  SPF-MORE                      PIC X(01) VALUE SPACES.
004900     05  SPF-DSNAME                    PIC X(56) VALUE SPACES.
005000     05  SPF-LSEL                      PIC X(02) VALUE SPACES.
005100     05  SPF-VOLSER                    PIC X(06) VALUE SPACES.
005200     05  WORK-NUM                      PIC ZZZ9.
005300
005400
005500
005600******************************************************************
005700*    THE FOLLOWING WORKING STORAGE AREA IS USED TO CONTAIN THE   *
005800*    LIST OF DDNAMES CURRENTLY FOUND IN THE TIOT. THIS AREA IS   *
005900*    PASSED TO PROGRAM DDNLIST (WHICH FILLS IN THE DDNAMES).     *
006000******************************************************************
006100
006200 01  DDNLIST-AREA.
006300     05  DDN-LIST-COUNT                PIC S9(04) COMP VALUE +0.
006400     05  DDN-TABLE.
006500         10  DDN-DDNAME                PIC X(08)
006600                                       OCCURS 500 TIMES
006700                                       INDEXED BY DDN-INX.
006800/
006900******************************************************************
007000*    THE FOLLOWING WORKING STORAGE AREA IS USED IN PROCESSING    *
007100*    AND PARSING THE COMMAND LINE.                               *
007200******************************************************************
007300
007400 01  ZCMD-PROCESS-AREA                 SYNC.
007500     05  ZCMD-COMMAND-LINE             PIC X(80) VALUE SPACES.
007600     05  ZCMD-OVERFLOW-SWITCH          PIC 9(01) VALUE 0.
007700     05  ZCMD-COMMAND                  PIC X(80) VALUE SPACES.
007800     05  ZCMD-PARM-COUNT               PIC S9(04) COMP VALUE +0.
007900     05  ZCMD-PARM-TABLE.
008000         10  ZCMD-PARM                 PIC X(80)
008100                                       OCCURS 20 TIMES
008200                                       INDEXED BY ZCMD-INX.
008300
008400
008500
008600******************************************************************
008700*    THE FOLLOWING AREA IS USED TO RETURN THE DSNAME AND         *
008800*    VOLSER FOR ANY GIVEN DDNAME IN THE TIOT. KDSNSUB            *
008900*    (SUBROUTINE) IS CALLED TO RETURN THIS INFORMATION.          *
009000******************************************************************
009100
009200 01  KDSNSUB-AREA                     SYNC.
009300     05  KDSNSUB-DDNAME                PIC X(08) VALUE SPACES.
009400     05  KDSNSUB-COUNT                 PIC S9(04) COMP VALUE +15.
009500     05  KDSNSUB-DSN-TABLE.
009600         10  KDSNSUB-DSN               PIC X(44)
009700                                       OCCURS 15 TIMES
009800                                       INDEXED BY DSN-INX.
009900     05  KDSNSUB-VOL-TABLE.
010000         10  KDSNSUB-VOL               PIC X(06)
010100                                       OCCURS 15 TIMES
010200                                       INDEXED BY VOL-INX.
010300/
010400******************************************************************
010500*    THE FOLLOWING AREA IS USED FOR ISPF DIALOG CALLS. IT        *
010600*    CONTAINS LITERALS, NAMELISTS AND WORKING STORAGE AREAS      *
010700*    THAT THE DIALOG CALLS REQUIRE.                              *
010800******************************************************************
010900
011000 01  PDF-WORK-AREAS                   SYNC.
011100     05  PDF-DUMMY-PARAMETER          PIC X(8) VALUE '        '.
011200     05  PDF-BROWSE                   PIC X(8) VALUE 'BROWSE'.
011300     05  PDF-CANCEL                   PIC X(8) VALUE 'CANCEL'.
011400     05  PDF-CHAR                     PIC X(8) VALUE 'CHAR'.
011500     05  PDF-CONTROL                  PIC X(8) VALUE 'CONTROL'.
011600     05  PDF-DISPLAY                  PIC X(8) VALUE 'DISPLAY'.
011700     05  PDF-EDIT                     PIC X(8) VALUE 'EDIT'.
011800     05  PDF-EDIT-PROFILE             PIC X(8) VALUE '    '.
011900     05  PDF-ERRORS                   PIC X(8) VALUE 'ERRORS'.
012000     05  PDF-LINE                     PIC X(8) VALUE 'LINE'.
012100     05  PDF-MOVE                     PIC X(8) VALUE 'MOVE'.
012200     05  PDF-NEXT                     PIC X(8) VALUE 'NEXT'.
012300     05  PDF-NOWRITE                  PIC X(8) VALUE 'NOWRITE'.
012400     05  PDF-ORDER                    PIC X(8) VALUE 'ORDER'.
012500     05  PDF-PREVIOUS                 PIC X(8) VALUE 'PREVIOUS'.
012600     05  PDF-REPLACE                  PIC X(8) VALUE 'REPLACE'.
012700     05  PDF-RESTORE                  PIC X(8) VALUE 'RESTORE'.
012800     05  PDF-RETURN                   PIC X(8) VALUE 'RETURN'.
012900     05  PDF-SAVE                     PIC X(8) VALUE 'SAVE'.
013000     05  PDF-SELECT                   PIC X(8) VALUE 'SELECT'.
013100     05  PDF-TBADD                    PIC X(8) VALUE 'TBADD'.
013200     05  PDF-TBBOTTOM                 PIC X(8) VALUE 'TBBOTTOM'.
013300     05  PDF-TBCREATE                 PIC X(8) VALUE 'TBCREATE'.
013400     05  PDF-TBDISPL                  PIC X(8) VALUE 'TBDISPL'.
013500     05  PDF-TBEND                    PIC X(8) VALUE 'TBEND'.
013600     05  PDF-TBQUERY                  PIC X(8) VALUE 'TBQUERY'.
013700     05  PDF-TBSARG                   PIC X(8) VALUE 'TBSARG'.
013800     05  PDF-TBSCAN                   PIC X(8) VALUE 'TBSCAN'.
013900     05  PDF-TBSORT                   PIC X(8) VALUE 'TBSORT'.
014000     05  PDF-TBSKIP                   PIC X(8) VALUE 'TBSKIP'.
014100     05  PDF-TBTOP                    PIC X(8) VALUE 'TBTOP'.
014200     05  PDF-TBVCLEAR                 PIC X(8) VALUE 'TBVCLEAR'.
014300     05  PDF-FIXED                    PIC X(8) VALUE 'FIXED'.
014400     05  PDF-VCOPY                    PIC X(8) VALUE 'VCOPY'.
014500     05  PDF-VDEFINE                  PIC X(8) VALUE 'VDEFINE'.
014600     05  PDF-VREPLACE                 PIC X(8) VALUE 'VREPLACE'.
014700     05  PDF-VRESET                   PIC X(8) VALUE 'VRESET'.
014800     05  PDF-FULLWORD                 PIC S9(09) COMP.
014900     05  PDF-RETURN-CODE              PIC S9(09) COMP.
015000     05  PDF-BROWSE-RETURN-CODE       PIC S9(09) COMP.
015100     05  PDF-TBDISPL-ROW              PIC S9(09) COMP.
015200     05  PDF-SELECT-BUFFER-LENGTH     PIC S9(09) COMP.
015300     05  PDF-NAME-LIST                PIC X(256).
015400     05  PDF-COND-LIST                PIC X(80).
015500     05  PDF-TABLE-ID.
015600         10  PDF-TABLE-ID-PREFACE     PIC X(5).
015700         10  FILLER                   PIC X(3).
015800     05  PDF-TBDISPL-PANEL            PIC X(8).
015900     05  PDF-TBDISPL-AUTOSEL          PIC X(3).
016000     05  PDF-TBDISPL-MESSAGE          PIC X(8).
016100     05  PDF-TBDISPL-CURSOR           PIC X(8).
016200     05  PDF-TBQUERY-CRP              PIC X(8).
016300     05  PDF-SHORT-MESSAGE            PIC X(24).
016400     05  PDF-LONG-MESSAGE             PIC X(78).
016500     05  PDF-SELECT-BUFFER            PIC X(256).
016600     05  PDF-DSNAME                   PIC X(56).
016700     05  PDF-VOLSER                   PIC X(6).
016800
016900/
017000******************************************************************
017100*    DDNAME TABLE RECEIVED THRU PARMS                            *
017200******************************************************************
017300 01  MATCH-SW                         PIC X.
017400 01  DDNAME-TABLE.
017500     05  PASSED-DDNAME                PIC X(08)
017600                                      OCCURS 50 TIMES
017700                                      INDEXED BY PDDN-INX.
017800 01  DDNAME-TABLE-1.
017900     05  PASSED-DDNAME-LET-1          PIC X(01)
018000                                      OCCURS 8 TIMES
018100                                      INDEXED BY PDDN-1.
018200 01  DDNAME-TABLE-2.
018300     05  PASSED-DDNAME-LET-2          PIC X(01)
018400                                      OCCURS 8 TIMES
018500                                      INDEXED BY PDDN-2.
018600/
018700******************************************************************
018800*    THE ONLY PARAMETER ALLOWED PASSED IS THE WORD FULL. IT      *
018900*    WILL INITALIZE THE TBDISPL TO BE IN FULL MODE AS APPOSED    *
019000*    TO COMPRESS MODE. THE PARAMETER IS PASSED USING STANDARD    *
019100*    TSO (EXEC PGM= PARM=) CONVENTIONS.                          *
019200******************************************************************
019300
019400 LINKAGE SECTION.
019500 01  PASSED-PARMS.
019600     05  PARM-LENGTH                   PIC S9(04) COMP.
019700     05  PARM-DATA.
019800         10  PARM-DATA-LETTER          PIC X(01)
019900                                       OCCURS 1 TO 256 TIMES
020000                                       DEPENDING ON PARM-LENGTH.
020100/
020200 PROCEDURE DIVISION USING PASSED-PARMS.
020300
020400******************************************************************
020500*    INITALIZE WORKING STORAGE AREAS.                            *
020600*    CAPITOLIZE THE PARMS PASSED (IN CASE THEY ARE LOWER         *
020700*    CASE).                                                      *
020800*    VDEFINE AND VCOPY ANY ISPF VARIABLES THAT ARE REQUIRED.     *
020900******************************************************************
021000
021100 0100-INIT-AND-VDEFINE.
021200
021300     TRANSFORM PARM-DATA FROM LOWER-CASE TO UPPER-CASE.
021400
021500     CALL 'ISPLINK' USING             PDF-CONTROL
021600                                      PDF-ERRORS
021700                                      PDF-CANCEL.
021800
021900     IF PARM-LENGTH > 0 THEN
022000        MOVE '(PASSPARM)' TO PDF-NAME-LIST
022100        MOVE PARM-LENGTH TO PDF-FULLWORD
022200        CALL 'ISPLINK' USING          PDF-VREPLACE
022300                                      PDF-NAME-LIST
022400                                      PDF-FULLWORD
022500                                      PARM-DATA.
022600
022700     IF PARM-DATA = 'FULL'
022800         MOVE SPACES TO PARM-DATA
022900         MOVE 'ALL' TO SPF-SCAN.
023000
023100     IF PARM-DATA NOT = SPACES
023200         UNSTRING PARM-DATA DELIMITED BY ALL SPACES
023300         INTO PASSED-DDNAME (1) PASSED-DDNAME(2) PASSED-DDNAME(3)
023400           PASSED-DDNAME (4) PASSED-DDNAME(5) PASSED-DDNAME(6)
023500           PASSED-DDNAME (7) PASSED-DDNAME(8) PASSED-DDNAME(9)
023600           PASSED-DDNAME (10) PASSED-DDNAME(11) PASSED-DDNAME(12)
023700           PASSED-DDNAME (13) PASSED-DDNAME(14) PASSED-DDNAME(15)
023800           PASSED-DDNAME (16) PASSED-DDNAME(17) PASSED-DDNAME(18)
023900           PASSED-DDNAME (19)
024000           PASSED-DDNAME(20) PASSED-DDNAME(21) PASSED-DDNAME (22)
024100           PASSED-DDNAME(23) PASSED-DDNAME(24) PASSED-DDNAME (25)
024200           PASSED-DDNAME(26) PASSED-DDNAME(27) PASSED-DDNAME (28)
024300           PASSED-DDNAME(29)
024400           PASSED-DDNAME(30) PASSED-DDNAME(31) PASSED-DDNAME (32)
024500           PASSED-DDNAME(33) PASSED-DDNAME(34) PASSED-DDNAME (35)
024600           PASSED-DDNAME(36) PASSED-DDNAME(37) PASSED-DDNAME (38)
024700           PASSED-DDNAME(39)
024800           PASSED-DDNAME(40) PASSED-DDNAME(41) PASSED-DDNAME (42)
024900           PASSED-DDNAME(43) PASSED-DDNAME(44) PASSED-DDNAME (45)
025000           PASSED-DDNAME(46) PASSED-DDNAME(47) PASSED-DDNAME (48)
025100           PASSED-DDNAME(49)
025200           PASSED-DDNAME(50)
025300         MOVE 'ALL' TO SPF-SCAN
025400       ELSE MOVE SPACES TO DDNAME-TABLE.
025500
025600******************************************************************
025700*    ZTDTOP   -  IS THE ISPF VARIABLE FOR TABLE HANDELING        *
025800*    CSR      -  REPRESENTS THE CURRENT CRP FROM TBDISPL         *
025900*    SEQNUM   -  IS USED TO IDENTIFY THE DSNAMES WITHIN DDNAME   *
026000******************************************************************
026100
026200     MOVE '(ZTDTOP CSR SEQNUM)' TO PDF-NAME-LIST.
026300     MOVE +4 TO PDF-FULLWORD.
026400     CALL 'ISPLINK' USING              PDF-VDEFINE
026500                                       PDF-NAME-LIST
026600                                       SPF-ZTDTOP
026700                                       PDF-FIXED
026800                                       PDF-FULLWORD.
026900
027000
027100******************************************************************
027200*    SCAN     -  THE VARIABLE DETERMINING IF THIS ROW IS THE     *
027300*                FIRST ROW OF A CONCATENATION OR NOT.            *
027400*                USED TO CONTROL FULL/COMPRESS OPTIONS           *
027500******************************************************************
027600
027700     MOVE '(SCAN)' TO PDF-NAME-LIST.
027800     CALL 'ISPLINK' USING              PDF-VDEFINE
027900                                       PDF-NAME-LIST
028000                                       SPF-SCAN
028100                                       PDF-CHAR
028200                                       PDF-FULLWORD.
028300
028400******************************************************************
028500*    ZCMD     -  ISPF COMMAND LINE - PRIMARY COMMANDS            *
028600******************************************************************
028700
028800     MOVE '(ZCMD)' TO PDF-NAME-LIST.
028900     MOVE +80 TO PDF-FULLWORD.
029000     CALL 'ISPLINK' USING              PDF-VDEFINE
029100                                       PDF-NAME-LIST
029200                                       ZCMD-COMMAND-LINE
029300                                       PDF-CHAR
029400                                       PDF-FULLWORD.
029500
029600
029700******************************************************************
029800*    SCANSW   -  SWITCH IN TABLE TO DETERMINE IF THE ROW         *
029900*                SHOULD BE DISPLAYED IN COMPRESS MODE.           *
030000******************************************************************
030100
030200     MOVE '(SCANSW MORE)' TO PDF-NAME-LIST.
030300     MOVE +1 TO PDF-FULLWORD.
030400     CALL 'ISPLINK' USING              PDF-VDEFINE
030500                                       PDF-NAME-LIST
030600                                       SPF-SCAN-BYTE
030700                                       PDF-CHAR
030800                                       PDF-FULLWORD.
030900
031000******************************************************************
031100*    DDNAME   -  THE DDNAME DISPLAYED (TBDISPL)                  *
031200*    ALTDDNAME-  SECOND DDNAME FOR IDENTIFYING THE ROW (TBSORT)  *
031300******************************************************************
031400
031500     MOVE '(DDNAME ALTDDNAM)' TO PDF-NAME-LIST.
031600     MOVE +8 TO PDF-FULLWORD.
031700     CALL 'ISPLINK' USING              PDF-VDEFINE
031800                                       PDF-NAME-LIST
031900                                       SPF-DDNAME
032000                                       PDF-CHAR
032100                                       PDF-FULLWORD.
032200
032300
032400******************************************************************
032500*    VOLSER   -  THE VOLSER FOR TBDISPL                          *
032600******************************************************************
032700
032800     MOVE '(VOLSER)' TO PDF-NAME-LIST.
032900     MOVE +6 TO PDF-FULLWORD.
033000     CALL 'ISPLINK' USING              PDF-VDEFINE
033100                                       PDF-NAME-LIST
033200                                       SPF-VOLSER
033300                                       PDF-CHAR
033400                                       PDF-FULLWORD.
033500
033600
033700******************************************************************
033800*    DSNAME   -  THE DSNAME FOR TBDISPL                          *
033900******************************************************************
034000
034100     MOVE '(DSNAME)' TO PDF-NAME-LIST.
034200     MOVE +56 TO PDF-FULLWORD.
034300     CALL 'ISPLINK' USING              PDF-VDEFINE
034400                                       PDF-NAME-LIST
034500                                       SPF-DSNAME
034600                                       PDF-CHAR
034700                                       PDF-FULLWORD.
034800
034900
035000******************************************************************
035100*    LSEL     -  THE LINE COMMAND VARIABLE                       *
035200******************************************************************
035300
035400     MOVE '(LSEL)' TO PDF-NAME-LIST.
035500     MOVE +2 TO PDF-FULLWORD.
035600     CALL 'ISPLINK' USING              PDF-VDEFINE
035700                                       PDF-NAME-LIST
035800                                       SPF-LSEL
035900                                       PDF-CHAR
036000                                       PDF-FULLWORD.
036100
036200
036300******************************************************************
036400*    UMSG     -  SHORT (UPPER) MESSAGE FOR ERRORS                *
036500******************************************************************
036600
036700     MOVE '(UMSG)' TO PDF-NAME-LIST.
036800     MOVE +24 TO PDF-FULLWORD.
036900     CALL 'ISPLINK' USING              PDF-VDEFINE
037000                                       PDF-NAME-LIST
037100                                       PDF-SHORT-MESSAGE
037200                                       PDF-CHAR
037300                                       PDF-FULLWORD.
037400
037500
037600******************************************************************
037700*    LMSG     -  LONG (LOWER) MESSAGE FOR ERRORS                 *
037800******************************************************************
037900
038000     MOVE '(LMSG)' TO PDF-NAME-LIST.
038100     MOVE +78 TO PDF-FULLWORD.
038200     CALL 'ISPLINK' USING              PDF-VDEFINE
038300                                       PDF-NAME-LIST
038400                                       PDF-LONG-MESSAGE
038500                                       PDF-CHAR
038600                                       PDF-FULLWORD.
038700
038800
038900******************************************************************
039000*    RET0TABL -  PROGRAM RETURNS UNIQUE TABLE ID                 *
039100*                THIS SUPPORTS RECURSIVE USE OF THIS FUNCTION    *
039200******************************************************************
039300
039400     CALL 'RET0TABL' USING            PDF-TABLE-ID.
039500     MOVE 'LISTA' TO PDF-TABLE-ID-PREFACE.
039600
039700     MOVE 'LSTDA' TO PDF-TBDISPL-PANEL.
039800     MOVE SPACES TO PDF-TBDISPL-MESSAGE.
039900     MOVE SPACES TO PDF-TBDISPL-CURSOR.
040000     MOVE ZEROS TO PDF-TBDISPL-ROW.
040100
040200     PERFORM 2000-BUILD-TABLE THRU 2999-EXIT.
040300
040400     MOVE ZEROS TO PDF-RETURN-CODE.
040500     PERFORM 1000-DISPLAY-PANEL THRU 1999-EXIT
040600         UNTIL PDF-RETURN-CODE > 4.
040700
040800 EXIT-PROGRAM.
040900
041000
041100     CALL 'ISPLINK' USING PDF-TBEND PDF-TABLE-ID.
041200     CALL 'ISPLINK' USING              PDF-VRESET.
041300     GOBACK.
041400/
041500
041600
041700 1000-DISPLAY-PANEL.
041800
041900     CALL 'ISPLINK' USING              PDF-TBTOP
042000                                       PDF-TABLE-ID.
042100
042200     CALL 'ISPLINK' USING              PDF-TBSKIP
042300                                       PDF-TABLE-ID
042400                                       SPF-ZTDTOP.
042500
042600     IF PDF-TBDISPL-MESSAGE NOT = SPACES
042700         MOVE 'YES' TO PDF-TBDISPL-AUTOSEL
042800     ELSE
042900         MOVE 'NO' TO PDF-TBDISPL-AUTOSEL
043000         MOVE SPF-LAST-CSR TO PDF-TBDISPL-ROW
043100         IF SPF-LAST-CSR NOT = 0
043200             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR.
043300
043400     CALL 'ISPLINK' USING              PDF-TBDISPL
043500                                       PDF-TABLE-ID
043600                                       PDF-TBDISPL-PANEL
043700                                       PDF-TBDISPL-MESSAGE
043800                                       PDF-TBDISPL-CURSOR
043900                                       PDF-TBDISPL-ROW
044000                                       PDF-DUMMY-PARAMETER
044100                                       PDF-TBDISPL-AUTOSEL.
044200     MOVE RETURN-CODE TO PDF-RETURN-CODE.
044300     IF PDF-RETURN-CODE > 4
044400         GO TO 1999-EXIT.
044500
044600
044700     MOVE 'LSTDA' TO PDF-TBDISPL-PANEL.
044800     MOVE SPACES TO PDF-TBDISPL-MESSAGE.
044900     MOVE SPACES TO PDF-TBDISPL-CURSOR.
045000     MOVE ZEROS TO PDF-TBDISPL-ROW.
045100     MOVE ZEROS TO SPF-CSR, SPF-LAST-CSR.
045200
045300
045400
045500 1100-TBQUERY-CRP.
045600     MOVE 'CSR' TO PDF-TBQUERY-CRP.
045700     CALL 'ISPLINK' USING              PDF-TBQUERY
045800                                       PDF-TABLE-ID
045900                                       PDF-DUMMY-PARAMETER
046000                                       PDF-DUMMY-PARAMETER
046100                                       PDF-DUMMY-PARAMETER
046200                                       PDF-DUMMY-PARAMETER
046300                                       PDF-DUMMY-PARAMETER
046400                                       PDF-TBQUERY-CRP.
046500
046600     IF SPF-CSR = 0 GO TO 1500-PROCESS-ZCMD.
046700     MOVE SPF-CSR TO SPF-LAST-CSR.
046800
046900     PERFORM 4000-LINE-COMMAND THRU 4999-EXIT.
047000
047100     IF PDF-TBDISPL-MESSAGE NOT = SPACES
047200         GO TO 1999-EXIT.
047300
047400     IF PDF-RETURN-CODE = 4
047500         MOVE SPACES TO PDF-TBDISPL-PANEL
047600         GO TO 1999-EXIT.
047700
047800 1500-PROCESS-ZCMD.
047900     IF ZCMD-COMMAND-LINE = SPACES
048000         GO TO 1999-EXIT.
048100
048200     MOVE ZEROS TO ZCMD-PARM-COUNT.
048300     MOVE ZEROS TO ZCMD-OVERFLOW-SWITCH.
048400     MOVE SPACES TO ZCMD-COMMAND.
048500     MOVE SPACES TO ZCMD-PARM-TABLE.
048600
048700     UNSTRING ZCMD-COMMAND-LINE
048800              DELIMITED BY ALL SPACES
048900              INTO ZCMD-COMMAND
049000              ZCMD-PARM (1)
049100              ZCMD-PARM (2)
049200              ZCMD-PARM (3)
049300              ZCMD-PARM (4)
049400              ZCMD-PARM (5)
049500              ZCMD-PARM (6)
049600              ZCMD-PARM (7)
049700              ZCMD-PARM (8)
049800              ZCMD-PARM (9)
049900              ZCMD-PARM (10)
050000              ZCMD-PARM (11)
050100              ZCMD-PARM (12)
050200              ZCMD-PARM (13)
050300              ZCMD-PARM (14)
050400              ZCMD-PARM (15)
050500              ZCMD-PARM (16)
050600              ZCMD-PARM (17)
050700              ZCMD-PARM (18)
050800              ZCMD-PARM (19)
050900              ZCMD-PARM (20)
051000              TALLYING ZCMD-PARM-COUNT
051100              ON OVERFLOW
051200                  MOVE +1 TO ZCMD-OVERFLOW-SWITCH.
051300
051400     IF ZCMD-PARM-COUNT > 0
051500         SUBTRACT 1 FROM ZCMD-PARM-COUNT.
051600
051700     IF ZCMD-OVERFLOW-SWITCH NOT = 0
051800         MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
051900         MOVE 'THERE IS A LIMIT OF 20 PARAMETERS TO ANY PRIMARY CO
052000-        'COMMAND' TO PDF-LONG-MESSAGE
052100         MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
052200         MOVE SPACES TO PDF-TBDISPL-PANEL
052300         GO TO 1999-EXIT.
052400
052500 1600-COMPRESS-COMMAND.
052600     IF ZCMD-COMMAND = 'COMPRESS' OR 'CMP'
052700         IF ZCMD-PARM-COUNT NOT = 0
052800             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
052900             MOVE 'THERE ARE NO VALID PARMS FOR THIS COMMAND'
053000             TO PDF-LONG-MESSAGE
053100             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
053200             MOVE SPACES TO PDF-TBDISPL-PANEL
053300             GO TO 1999-EXIT.
053400
053500     IF ZCMD-COMMAND = 'COMPRESS' OR 'CMP'
053600         MOVE 'SCAN' TO SPF-SCAN
053700         MOVE SPACES TO ZCMD-COMMAND-LINE
053800         GO TO 1999-EXIT.
053900
054000 1605-FULL-COMMAND.
054100     IF ZCMD-COMMAND = 'FULL'
054200         IF ZCMD-PARM-COUNT NOT = 0
054300             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
054400             MOVE 'THERE ARE NO VALID PARMS FOR THIS COMMAND'
054500             TO PDF-LONG-MESSAGE
054600             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
054700             MOVE SPACES TO PDF-TBDISPL-PANEL
054800             GO TO 1999-EXIT.
054900
055000     IF ZCMD-COMMAND = 'FULL'
055100         MOVE 'ALL' TO SPF-SCAN
055200         MOVE SPACES TO ZCMD-COMMAND-LINE
055300         GO TO 1999-EXIT.
055400
055500 1610-REFRESH-COMMAND.
055600     IF ZCMD-COMMAND = 'REFRESH' OR '*' OR 'REF'
055700         IF ZCMD-PARM-COUNT NOT = 0
055800             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
055900             MOVE 'THERE ARE NO VALID PARMS FOR THIS COMMAND'
056000             TO PDF-LONG-MESSAGE
056100             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
056200             MOVE SPACES TO PDF-TBDISPL-PANEL
056300             GO TO 1999-EXIT.
056400
056500     IF ZCMD-COMMAND = 'REFRESH' OR '*' OR 'REF'
056600         PERFORM 2000-BUILD-TABLE THRU 2999-EXIT
056700         IF ZCMD-COMMAND NOT = '*'
056800             MOVE SPACES TO ZCMD-COMMAND-LINE
056900             GO TO 1999-EXIT
057000         ELSE
057100             GO TO 1999-EXIT.
057200
057300 1620-LOCATE-COMMAND.
057400     IF ZCMD-COMMAND = 'LOCATE' OR 'L' OR 'LOC'
057500         IF ZCMD-PARM-COUNT = 0
057600             MOVE 'MISSING PARAMETER' TO PDF-SHORT-MESSAGE
057700             MOVE 'EXPECTED DDNAME OR PARTIAL DDNAME TO LOCATE'
057800             TO PDF-LONG-MESSAGE
057900             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
058000             MOVE SPACES TO PDF-TBDISPL-PANEL
058100             GO TO 1999-EXIT
058200
058300         ELSE
058400         IF ZCMD-PARM-COUNT > 1
058500             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
058600             MOVE 'EXPECTED ONLY ONE PARAMETER FOR THIS COMMAND'
058700             TO PDF-LONG-MESSAGE
058800             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
058900             MOVE SPACES TO PDF-TBDISPL-PANEL
059000             GO TO 1999-EXIT.
059100
059200     IF ZCMD-COMMAND = 'LOCATE' OR 'L' OR 'LOC'
059300         PERFORM 5000-LOCATE THRU 5999-EXIT
059400         GO TO 1999-EXIT.
059500
059600 1630-SETPROF-COMMAND.
059700     IF ZCMD-COMMAND = 'SET' OR 'SETPROF'
059800         IF ZCMD-PARM-COUNT = 0
059900             MOVE 'MISSING PARAMETER' TO PDF-SHORT-MESSAGE
060000             MOVE 'EXPECTED EDIT PROFILE NAME'
060100             TO PDF-LONG-MESSAGE
060200             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
060300             MOVE SPACES TO PDF-TBDISPL-PANEL
060400             GO TO 1999-EXIT
060500
060600         ELSE
060700         IF ZCMD-PARM-COUNT > 1
060800             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
060900             MOVE 'EXPECTED ONLY ONE PARAMETER FOR THIS COMMAND'
061000             TO PDF-LONG-MESSAGE
061100             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
061200             MOVE SPACES TO PDF-TBDISPL-PANEL
061300             GO TO 1999-EXIT.
061400
061500     IF ZCMD-COMMAND = 'SET' OR 'SETPROF'
061600         IF ZCMD-PARM (1) = 'SPACES'
061700             MOVE SPACES TO PDF-EDIT-PROFILE
061800             GO TO 1999-EXIT
061900         ELSE
062000             MOVE ZCMD-PARM (1) TO PDF-EDIT-PROFILE
062100             GO TO 1999-EXIT.
062200
062300 1990-INVALID-COMMAND.
062400     MOVE 'ISPZ001' TO PDF-TBDISPL-MESSAGE.
062500     MOVE SPACES TO PDF-TBDISPL-PANEL.
062600
062700 1999-EXIT.
062800     IF PDF-RETURN-CODE = 0 AND
062900        PDF-TBDISPL-MESSAGE = SPACES
063000         PERFORM 2000-BUILD-TABLE THRU 2999-EXIT.
063100
063200/
063300 2000-BUILD-TABLE.
063400
063500     MOVE '(DDNAME DSNAME MORE SCANSW VOLSER ALTDDNAM SEQNUM)'
063600         TO PDF-NAME-LIST.
063700     CALL 'ISPLINK' USING              PDF-TBCREATE
063800                                       PDF-TABLE-ID
063900                                       PDF-DUMMY-PARAMETER
064000                                       PDF-NAME-LIST
064100                                       PDF-NOWRITE
064200                                       PDF-REPLACE.
064300
064400     CALL 'ISPLINK' USING              PDF-TBVCLEAR
064500                                       PDF-TABLE-ID.
064600
064700     MOVE '1' TO SPF-SCAN-BYTE.
064800     CALL 'ISPLINK' USING              PDF-TBSARG
064900                                       PDF-TABLE-ID.
065000
065100     MOVE SPACES TO SPF-SCAN-BYTE.
065200
065300     MOVE '(ALTDDNAM,C,A,SEQNUM,N,A)' TO PDF-NAME-LIST.
065400     MOVE 0 TO SPF-SEQUENCE.
065500     MOVE +500 TO DDN-LIST-COUNT.
065600     MOVE SPACES TO DDN-TABLE.
065700     CALL 'DDNLIST' USING              DDN-LIST-COUNT
065800                                       DDN-TABLE.
065900
066000     PERFORM 3000-BUILD-TABLE THRU 3999-EXIT
066100         VARYING DDN-INX FROM 1 BY 1
066200         UNTIL DDN-INX > DDN-LIST-COUNT.
066300
066400     CALL 'ISPLINK' USING             PDF-TBSORT
066500                                      PDF-TABLE-ID
066600                                      PDF-NAME-LIST.
066700
066800
066900 2999-EXIT.
067000     EXIT.
067100/
067200 3000-BUILD-TABLE.
067300     IF DDNAME-TABLE NOT = SPACES
067400        MOVE SPACES TO MATCH-SW
067500        PERFORM 8000-MATCH THRU 8099-EXIT
067600                VARYING PDDN-INX FROM 1 BY 1
067700                UNTIL PDDN-INX > 50
067800        IF MATCH-SW = SPACES
067900           GO TO 3999-EXIT.
068000     MOVE SPACES TO SPF-LSEL.
068100     MOVE DDN-DDNAME (DDN-INX) TO KDSNSUB-DDNAME.
068200     MOVE +15 TO KDSNSUB-COUNT.
068300     CALL 'KDSNSUB' USING              KDSNSUB-DDNAME
068400                                       KDSNSUB-COUNT
068500                                       KDSNSUB-DSN-TABLE
068600                                       KDSNSUB-VOL-TABLE.
068700     MOVE ' ' TO SPF-MORE.
068800     MOVE DDN-DDNAME (DDN-INX) TO SPF-DDNAME, SPF-ALTDDNAM.
068900     PERFORM 10000-TBADD THRU 10099-EXIT
069000         VARYING DSN-INX FROM 1 BY 1
069100         UNTIL DSN-INX > KDSNSUB-COUNT.
069200
069300 3999-EXIT. EXIT.
069400/
069500 4000-LINE-COMMAND.
069600     IF SPF-LSEL = SPACES
069700         GO TO 4999-EXIT.
069800
069900     IF SPF-LSEL = 'F' AND SPF-DDNAME = SPACES
070000         MOVE 'NO DDNAME FOUND' TO PDF-SHORT-MESSAGE
070100         MOVE SPACES TO PDF-LONG-MESSAGE
070200         MOVE 'USE THE ENTRY WITH A DDNAME PRESENT'
070300             TO PDF-LONG-MESSAGE
070400         MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
070500         MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
070600         MOVE SPF-CSR TO PDF-TBDISPL-ROW
070700         MOVE SPACES TO PDF-TBDISPL-PANEL
070800         GO TO 4999-EXIT.
070900
071000     IF SPF-LSEL = 'F'
071100         MOVE SPACES TO PDF-SELECT-BUFFER
071200         MOVE +3 TO PDF-FULLWORD
071300         CALL 'ISPLINK' USING PDF-CONTROL PDF-DISPLAY
071400              PDF-LINE PDF-FULLWORD
071500         MOVE SPACES TO PDF-SELECT-BUFFER
071600         STRING 'CMD(FREE F(' DELIMITED BY SIZE
071700             SPF-DDNAME DELIMITED BY SPACES
071800             '))' DELIMITED BY SIZE
071900         INTO PDF-SELECT-BUFFER
072000         MOVE +80 TO PDF-SELECT-BUFFER-LENGTH
072100         CALL 'ISPLINK' USING PDF-SELECT
072200                              PDF-SELECT-BUFFER-LENGTH
072300                              PDF-SELECT-BUFFER
072400         GO TO 4999-EXIT.
072500
072600     IF SPF-LSEL = 'B'
072700         MOVE SPACES TO PDF-DSNAME
072800         STRING QUOTE DELIMITED BY SIZE
072900                SPF-DSNAME DELIMITED BY SPACES
073000                QUOTE DELIMITED BY SIZE
073100                INTO PDF-DSNAME
073200         MOVE SPF-VOLSER TO PDF-VOLSER
073300         CALL 'ISPLINK' USING         PDF-CONTROL
073400                                      PDF-DISPLAY
073500                                      PDF-SAVE
073600         CALL 'ISPLINK' USING         PDF-CONTROL
073700                                      PDF-ERRORS
073800                                      PDF-RETURN
073900         CALL 'ISPLINK' USING         PDF-BROWSE
074000                                      PDF-DSNAME
074100                                      PDF-VOLSER
074200         MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE
074300         CALL 'ISPLINK' USING         PDF-CONTROL
074400                                      PDF-ERRORS
074500                                      PDF-CANCEL
074600         CALL 'ISPLINK' USING         PDF-CONTROL
074700                                      PDF-DISPLAY
074800                                      PDF-RESTORE
074900         IF PDF-BROWSE-RETURN-CODE NOT = 0
075000             MOVE +8 TO PDF-FULLWORD
075100             MOVE '(ZERRMSG)' TO PDF-NAME-LIST
075200             CALL 'ISPLINK' USING     PDF-VCOPY
075300                                      PDF-NAME-LIST
075400                                      PDF-FULLWORD
075500                                      PDF-TBDISPL-MESSAGE
075600                                      PDF-MOVE
075700             MOVE SPACE TO PDF-TBDISPL-PANEL
075800             MOVE SPF-CSR TO PDF-TBDISPL-ROW
075900             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
076000             GO TO 4999-EXIT
076100         ELSE
076200         MOVE SPACES TO SPF-LSEL
076300             GO TO 4999-EXIT.
076400
076500     IF SPF-LSEL = 'E'
076600         MOVE SPACES TO PDF-DSNAME
076700         STRING QUOTE DELIMITED BY SIZE
076800                SPF-DSNAME DELIMITED BY SPACES
076900                QUOTE DELIMITED BY SIZE
077000                INTO PDF-DSNAME
077100         MOVE SPF-VOLSER TO PDF-VOLSER
077200         CALL 'ISPLINK' USING         PDF-CONTROL
077300                                      PDF-DISPLAY
077400                                      PDF-SAVE
077500         CALL 'ISPLINK' USING         PDF-CONTROL
077600                                      PDF-ERRORS
077700                                      PDF-RETURN
077800         CALL 'ISPLINK' USING         PDF-EDIT
077900                                      PDF-DSNAME
078000                                      PDF-VOLSER
078100                                      PDF-DUMMY-PARAMETER
078200                                      PDF-DUMMY-PARAMETER
078300                                      PDF-DUMMY-PARAMETER
078400                                      PDF-EDIT-PROFILE
078500         MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE
078600         CALL 'ISPLINK' USING         PDF-CONTROL
078700                                      PDF-ERRORS
078800                                      PDF-CANCEL
078900         CALL 'ISPLINK' USING         PDF-CONTROL
079000                                      PDF-DISPLAY
079100                                      PDF-RESTORE
079200         IF PDF-BROWSE-RETURN-CODE > 4
079300             MOVE +8 TO PDF-FULLWORD
079400             MOVE '(ZERRMSG)' TO PDF-NAME-LIST
079500             CALL 'ISPLINK' USING     PDF-VCOPY
079600                                      PDF-NAME-LIST
079700                                      PDF-FULLWORD
079800                                      PDF-TBDISPL-MESSAGE
079900                                      PDF-MOVE
080000             MOVE SPACE TO PDF-TBDISPL-PANEL
080100             MOVE SPF-CSR TO PDF-TBDISPL-ROW
080200             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
080300             GO TO 4999-EXIT
080400         ELSE
080500         MOVE SPACES TO SPF-LSEL
080600             GO TO 4999-EXIT.
080700
080800     MOVE '(ZSEL)' TO PDF-NAME-LIST.
080900     MOVE +256 TO PDF-SELECT-BUFFER-LENGTH
081000     CALL 'ISPLINK' USING             PDF-VCOPY
081100                                      PDF-NAME-LIST
081200                                      PDF-SELECT-BUFFER-LENGTH
081300                                      PDF-SELECT-BUFFER
081400                                      PDF-MOVE.
081500
081600     MOVE +3 TO PDF-FULLWORD.
081700     CALL 'ISPLINK' USING             PDF-CONTROL
081800                                      PDF-DISPLAY
081900                                      PDF-LINE
082000                                      PDF-FULLWORD.
082100     CALL 'ISPLINK' USING             PDF-CONTROL
082200                                      PDF-DISPLAY
082300                                      PDF-SAVE.
082400     CALL 'ISPLINK' USING             PDF-CONTROL
082500                                      PDF-ERRORS
082600                                      PDF-CANCEL.
082700     CALL 'ISPLINK' USING             PDF-SELECT
082800                                      PDF-SELECT-BUFFER-LENGTH
082900                                      PDF-SELECT-BUFFER.
083000     MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE.
083100     CALL 'ISPLINK' USING             PDF-CONTROL
083200                                      PDF-ERRORS
083300                                      PDF-RETURN.
083400     CALL 'ISPLINK' USING             PDF-CONTROL
083500                                      PDF-DISPLAY
083600                                      PDF-RESTORE.
083700
083800     IF PDF-BROWSE-RETURN-CODE NOT = 0
083900         MOVE SPACES TO PDF-SHORT-MESSAGE
084000         MOVE PDF-BROWSE-RETURN-CODE TO WORK-NUM
084100         STRING 'RETURN CODE = ' DELIMITED BY SIZE
084200                 WORK-NUM DELIMITED BY SIZE
084300                 INTO PDF-SHORT-MESSAGE
084400         MOVE 'THE REQUESTED FUNCTION ENDED WITH A NON-ZERO RETURN
084500-    'CODE'
084600         TO PDF-LONG-MESSAGE
084700         MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
084800         MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
084900         MOVE SPACES TO PDF-TBDISPL-PANEL
085000         MOVE SPF-CSR TO PDF-TBDISPL-ROW.
085100
085200 4999-EXIT.
085300     EXIT.
085400/
085500 5000-LOCATE.
085600     CALL 'ISPLINK' USING              PDF-TBTOP
085700                                       PDF-TABLE-ID.
085800
085900     MOVE ZCMD-PARM (1) TO SPF-ALTDDNAM.
086000     MOVE '(ALTDDNAM)' TO PDF-NAME-LIST.
086100     MOVE '(GE)' TO PDF-COND-LIST.
086200     MOVE 'ZTDTOP' TO PDF-TBQUERY-CRP.
086300     CALL 'ISPLINK' USING              PDF-TBSCAN
086400                                       PDF-TABLE-ID
086500                                       PDF-NAME-LIST
086600                                       PDF-DUMMY-PARAMETER
086700                                       PDF-DUMMY-PARAMETER
086800                                       PDF-NEXT
086900                                       PDF-DUMMY-PARAMETER
087000                                       PDF-TBQUERY-CRP
087100                                       PDF-COND-LIST.
087200
087300     IF RETURN-CODE = 8
087400         CALL 'ISPLINK' USING          PDF-TBBOTTOM
087500                                       PDF-TABLE-ID
087600                                       PDF-DUMMY-PARAMETER
087700                                       PDF-DUMMY-PARAMETER
087800                                       PDF-DUMMY-PARAMETER
087900                                       PDF-TBQUERY-CRP
088000     ELSE
088100     IF SPF-ZTDTOP NOT = 1 AND
088200         SPF-DDNAME NOT = ZCMD-PARM (1)
088300         MOVE SPACES TO SPF-DDNAME
088400         MOVE '(DDNAME)' TO PDF-NAME-LIST
088500         MOVE '(NE)' TO PDF-COND-LIST
088600         CALL 'ISPLINK' USING          PDF-TBSCAN
088700                                       PDF-TABLE-ID
088800                                       PDF-NAME-LIST
088900                                       PDF-DUMMY-PARAMETER
089000                                       PDF-DUMMY-PARAMETER
089100                                       PDF-PREVIOUS
089200                                       PDF-DUMMY-PARAMETER
089300                                       PDF-TBQUERY-CRP
089400                                       PDF-COND-LIST.
089500
089600     MOVE SPACES TO ZCMD-COMMAND-LINE.
089700
089800 5999-EXIT.
089900     EXIT.
090000/
090100 8000-MATCH.
090200     MOVE DDN-DDNAME (DDN-INX) TO DDNAME-TABLE-1.
090300     MOVE PASSED-DDNAME (PDDN-INX) TO DDNAME-TABLE-2.
090400
090500     PERFORM 8100-CHECK THRU 8199-EXIT
090600          VARYING PDDN-1 FROM 1 BY 1 UNTIL PDDN-1 > 8.
090700
090800     IF DDNAME-TABLE-1 = DDNAME-TABLE-2
090900        MOVE '1' TO MATCH-SW.
091000
091100 8099-EXIT. EXIT.
091200
091300 8100-CHECK.
091400     SET PDDN-2 TO PDDN-1.
091500     IF PASSED-DDNAME-LET-2 (PDDN-2) = '*'
091600        AND (PDDN-2 = 8 OR
091700             PASSED-DDNAME-LET-2 (PDDN-2 + 1) = SPACES)
091800
091900        PERFORM 8200-SPACE THRU 8299-EXIT
092000                VARYING PDDN-1 FROM PDDN-2 BY 1
092100                UNTIL PDDN-1 > 8
092200        MOVE SPACES TO PASSED-DDNAME-LET-2 (PDDN-2).
092300
092400 8199-EXIT. EXIT.
092500
092600 8200-SPACE.
092700     MOVE SPACES TO PASSED-DDNAME-LET-1 (PDDN-1).
092800 8299-EXIT. EXIT.
092900/
093000 10000-TBADD.
093100     ADD 1 TO SPF-SEQUENCE.
093200     MOVE KDSNSUB-DSN (DSN-INX) TO SPF-DSNAME.
093300     SET VOL-INX TO DSN-INX.
093400     MOVE KDSNSUB-VOL (VOL-INX) TO SPF-VOLSER.
093500
093600     IF DSN-INX > 1
093700         MOVE SPACES TO SPF-DDNAME, SPF-SCAN-BYTE
093800     ELSE
093900         MOVE '1' TO SPF-SCAN-BYTE.
094000
094100     IF KDSNSUB-COUNT > 1
094200         MOVE '+' TO SPF-MORE.
094300
094400     IF KDSNSUB-COUNT = DSN-INX
094500         MOVE SPACE TO SPF-MORE.
094600
094700     CALL 'ISPLINK' USING              PDF-TBADD
094800                                       PDF-TABLE-ID
094900                                       PDF-ORDER.
095000
095100 10099-EXIT. EXIT.
