         TITLE 'APF TABLE UPDATE MODULE'
************************************************************
* APFUP  - PROC.  THIS CODE WILL SCAN THE APF TABLE IN     *
*          SQA AND REPLACE ALL VOLSER'S OF XXXXXX WITH     *
*          THE VOLSER OF THE IPL VOLUME SERIAL NUMBER.     *
*                                                          *
*          CODED BY LIONEL DYCK                            *
*                   ROCKWELL INTERNATIONAL                 *
*                   PO BOX 2515                            *
*                   SEAL BEACH, CALIF 90740                *
*                   (213) 594-2520                         *
*                                                          *
* ATTRIBUTES:  AUTH, NORENT, NOREUS                        *
*                                                          *
************************************************************
         EJECT
APFUP    MENTER 12,EQU,COM=APFUP,CP=YES
         EJECT
         LR    R11,R1                    SAVE PARM POINTER
         MODESET MODE=SUP,KEY=ZERO       ENTER SUPER STATE
         L     R4,16                     -> CVT
         L     R10,X'30'(R4)             -> IPL VOL UCB
         LA    R10,X'1C'(R10)            -> VOL SERIAL #
         L     R4,X'1E4'(R4)             -> APF TABLE IN SQA
         SR    R2,R2                     INITIALIZE REG 2
         SR    R5,R5                     INITIALIZE REG 5
         SR    R7,R7                     INITIALIZE REG 7
         IC    R7,1(R4)                  LOAD # OF ENTRIES
         LH    R6,=H'01'                 INITIALIZE INCREMENT
         SR    R7,R6                     SUBTRACT 1 FOR BXLE
         USING APF,R4                    ESTABLISH ADDR TO APF
         LA    R4,2(R4)                  -> 1ST ENTRY OF TABLE
         LH    R9,=H'02'                 LOAD LENGTH OF PREFIX
APFCHG   EQU   *                         BEGIN THE ACTUAL CHANGE
         CLC   APFVOL,=CL6'XXXXXX'       CHANGE VOLSER ?
         BNE   NOCHG                     NO
         MVC   APFVOL(6),0(R10)          REPLACE WITH IPL VOLSER
         IC    R2,APFLEN           LOAD LEN                 *LBD 11/85*
         SH    R2,=H'07'           LESS 7 (1 FOR MVC 6 FOR VOL)
         MVC   PDSN,BLANKS         CLEAR DSN                *LBD 11/85*
         EX    R2,MVCDSN           AND MOVE IT              *LBD 11/85*
         AP    COUNT,=P'01'                                 *LBD 11/85*
         PUTLINE PARM=PUTBLOK,OUTPUT=(PLINE,TERM,SINGLE,    *LBD 11/85*X
               DATA),MF=(E,IOPLADS)                         *LBD 11/85*
NOCHG    EQU   *
         IC    R2,APFLEN                 LENGTH OF ENTRY
         AR    R4,R2                     INCREMENT DISPLACEMENT
         LA    R4,1(R4)                  -> NEXT ENTRY
         BXLE  R5,R6,APFCHG              CONTINUE SCANNING
TERME    EQU   *                         FORGET IT - AT THE END
         MODESET MODE=PROB,KEY=NZERO     LEAVE SUPER STATE
         ED    FCNT,COUNT                                   *LBD 11/85*
         PUTLINE PARM=PUTBLOK,OUTPUT=(FLINE,TERM,SINGLE,    *LBD 11/85*X
               DATA),MF=(E,IOPLADS)                         *LBD 11/85*
         MLEAVE
         SPACE
         LTORG
         SPACE
MVCDSN   MVC   PDSN(0),APFDSN      *** EXECUTED             *LBD 11/85*
BLANKS   DC    CL44' '                                      *LBD 11/85*
         SPACE
COUNT    DC    PL2'0'                                       *LBD 11/85*
FLINE    DC    AL2(FL-FLINE),H'00'                          *LBD 11/85*
         DC    C'APFLIST UPDATED FOR '                      *LBD 11/85*
FCNT     DC    X'40202120'                                  *LBD 11/85*
         DC    C' ENTRIES'                                  *LBD 11/85*
FL       EQU   *                                            *LBD 11/85*
PLINE    DC    AL2(PL-PLINE),H'00'                          *LBD 11/85*
         DC    C'APFLIST UPDATED FOR: '                     *LBD 11/85*
PDSN     DC    CL44' '                                      *LBD 11/85*
PL       EQU   *                                            *LBD 11/85*
         SPACE
         DSECT
APF      EQU   *
APFLEN   DS    CL1                       LENGTH OF ENTRY
APFVOL   DS    CL6                       VOLUME SERIAL NUMBER
APFDSN   DS    CL44                      DSN OF LIBRARY
         EJECT                                              *LBD 11/85*
         IKJIOPL                                            *LBD 11/85*
         END
