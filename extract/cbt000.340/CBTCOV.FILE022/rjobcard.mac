RJOBCARD $PROLOG
         CALL  ISPLINK,(VDEFINE,RJCARD,RJCARD1,CHAR,L80,OPT),VL
         CALL  ISPLINK,(VDEFINE,JINFO,RJACCT1,CHAR,L52,OPT),VL
         CALL  ISPLINK,(VDEFINE,PREFIX,ZUSER,CHAR,L8),VL
         CALL  ISPLINK,(VGET,PREFIX),VL                    *LBD 02/86*
         CLC   RJNAME(6),ZUSER    SAME USER ID ?           *LBD 02/86*
         BE    HAVEIT             YES                      *LBD 02/86*
         MVC   RJCARD1,RJMODEL    NO
         MVC   RJNAME,ZUSER        RESET IT            *LBD 02/86*
         LA    R1,RJNAME               -> JOB NAME
         LA    R2,8                    COUNT FOR LOOP
RJLOOP1  DS    0H
         CLI   0(R1),C' '              END OF JOB NAME
         BE    DOJCHAR1                YES
         LA    R1,1(R1)                UP 1
         BCT   R2,RJLOOP1
DOJCHAR1 MVI   0(R1),X'C0'             SET UP INITIAL VALUE
         SPACE 1
HAVEIT   DS    0H
         LA    R1,RJNAME               -> JOB NAME
         LA    R2,7                    COUNT FOR LOOP
RJLOOP2  DS    0H
         CLI   1(R1),C' '              END OF JOB NAME
         BE    DOJCHAR2                YES
         LA    R1,1(R1)                UP 1
         BCT   R2,RJLOOP2
DOJCHAR2 DS    0H                      HAVE JOB CHARACTER
         IC    R2,0(R1)
         LA    R2,1(R2)
         STC   R2,0(R1)
         CLI   0(R1),C'9'
         BH    DOA
         CLI   0(R1),C'0'
         BH    DOACT
         CLI   0(R1),C'Z'
         BH    DO#
         CLI   0(R1),C'S'
         BH    DOACT
         CLI   0(R1),C'R'
         BH    DOS
         CLI   0(R1),C'J'
         BH    DOACT
         CLI   0(R1),C'I'
         BH    DOJ
         B     DOACT
DOA      MVI   0(R1),C'A'
         B     DOACT
DOJ      MVI   0(R1),C'J'
         B     DOACT
DOS      MVI   0(R1),C'S'
         B     DOACT
DO#      MVI   0(R1),C'0'
         B     DOACT
DOACT    DS    0H
         $FINDCB OSACT=R2
         MVC   RJACCT1,X'21'(R2)          MOVE ACCT LIT INTO VARIABLE
         MVI   RJACCT1+48,C'5'            SET TO DEFAULT OF 5
         CALL  ISPLINK,(VPUT,RJCARD,PROFILE),VL
         CALL  ISPLINK,(VPUT,JINFO),VL
         CALL  ISPLINK,(VDELETE,RJCARD),VL
         CALL  ISPLINK,(VDELETE,JINFO),VL
         CALL  ISPLINK,(VDELETE,PREFIX),VL
         $EPILOG
         LTORG
VDEFINE  DC    C'VDEFINE'
CHAR     DC    C'CHAR'
OPT      DC    C'('
COPY     DC    C'COPY'
         DC    C','
NOBSCAN  DC    C'NOBSCAN'
         DC    C')'
VPUT     DC    C'VPUT'
VGET     DC    C'VGET'
VDELETE  DC    C'VDELETE'
PROFILE  DC    C'PROFILE'
L80      DC    F'80'
L52      DC    F'52'
L8       DC    F'8'
PREFIX   DC    C'(ZUSER)'
ZUSER    DC    CL8' '                                      *LBD 02/86*
RJCARD   DC    C'(RJCARD1)'
JINFO    DC    C'(JINFO)'
         SPACE
RJCARD1  DC    CL80'//'
         ORG   RJCARD1+2
RJNAME   DC    CL8' '
         DC    C' JOB '
         DC    X'7D'
RJACCT1  DC    CL52' '
         DC    X'7D'
         DC    C','
         ORG   ,
RJMODEL  DC    CL80'//'
         ORG   RJMODEL+2
         DC    CL8' '
         DC    C' JOB '
         DC    X'7D'
         DC    CL52' '
         DC    X'7D'
         DC    C','
         ORG   ,
         END
