         TITLE 'SHOWTSO - SET CLIST VARIABLES WITH SHOW INFO'
***********************************************************************
*  NAME:     SHOWTSO                                                  *
*  FUNCTION: TO INVOKE THE SHOW UTILITY AND CREATE TSO/E CLIST        *
*            VARIABLES WITH INFORMATION FROM ACF2 AND SYSTEM CONTROL  *
*            BLOCKS FOR USE BY A TSO/E CLIST.                         *
*                                                                     *
*  SYNTAX:   SHOWTSO OPERAND(S)                                       *
*                                                                     *
*  NOTE:       OPERANDS MAY BE SPECIFIED SINGLY OR IN PAIR SEPERATED  *
*              BY AN EQUAL SIGN.   IF AN OPERAND IS NOT EQUATED WITH  *
*              ANOTHER NAME BY AN '=' THEN THE OPERAND VALUE IS ALSO  *
*              THE NAME OF THE CLIST VARIABLE TO BE GENERATED.        *
*              IF AN OPERAND IS SPECIFIED AS OPERAND=NAME THEN THE    *
*              SPECIFIED NAME WILL BE THE NAME OF THE CLIST VARIABLE. *
*                                                                     *
*  OPERANDS:   ALL SUPPORTED VALUES OF THE SHOW PROGRAM.              *
*                                                                     *
*  DEPENDENCIES:  TSO/E AND THE SHOW PROGRAM.                         *
*                                                                     *
*                                                                     *
************************************************************************
*                                                                     *
* AUTHOR:      LIONEL DYCK                                            *
*              ROCKWELL INTERNATIONAL                                 *
*              PO BOX 2515                                            *
*              2201 SEAL BEACH BLVD.                                  *
*              SEAL BEACH, CALIF  90740                               *
*              MAIL CODE 110-SH28                                     *
*              PHONE (213) 594-1125                                   *
*              COMNET 374-1125                                        *
*                                                                     *
************************************************************************
         EJECT                                             *LBD 12/85*
SHOWTSO  MENTER 12,EQU,COM=SHOWTSO,CP=YES
         EJECT
         L     R3,CPPLCBUF        -> CPPL COMMAND BUFFER
         LH    R1,2(R3)           LOAD OFFSET TO 1ST OPER
         LH    R2,0(R3)           LOAD LENGTH
         SR    R2,R1              GET LEN
         SH    R2,=H'03'          LESS HEADER
         LA    R3,4(R3)           -> PAST HEADER
         AR    R3,R1              -> 1ST OPERAND
         EX    R2,MOVEOPER        MOVE OPERANDS
         EJECT
         LOAD  EP=SHOW
         ST    R0,SHOWEPA
         OC    OPERANDS,BLANKS    SET UPPER CASE
         LA    R5,OPERANDS
         LR    R7,R5
         AR    R7,R2              -> END OF DATA
         MVC   CLSTNAME,BLANKS    INITIALIZE CLSTNAME
         MVC   SHOWNAME,BLANKS    INITIALIZE SHOWNAME
         LA    R4,SHOWNAME
LOOP1    DS    0H
         IFON  END,EXIT
         CLI   0(R5),C'='         START OF CLIST NAME ?
         BE    LOOPP              YES
         CLI   0(R5),C' '         BLANK
         BE    INCR2              YES
         MVC   0(1,R4),0(R5)      MOVE CHAR OF NAME
         LA    R4,1(R4)
         SETON DOIT
INCR     DS    0H
         LA    R5,1(R5)           BUMP R5
         CR    R5,R7              AT END ?
         BNH   LOOP1              NO
         IFON  DOIT,PROCESS
         SETON END
         B     EXIT
INCR2    DS    0H
         IFON  DOIT,PROCESS
         B     INCR
         EJECT
LOOPP    DS    0H
         LA    R4,CLSTNAME
         B     INCR
         EJECT
PROCESS  DS    0H
         SETOF DOIT
         CLI   CLSTNAME,C' '
         BNE   SKMV
         MVC   CLSTNAME,SHOWNAME
SKMV     DS    0H
         LA    R1,SHOWNAME+7      -> END OF SHOWNAME
         LA    R2,8               SET LEN
SCKLEN   CLI   0(R1),C' '         END OF DSN
         BNE   SETLEN
         BCTR  R1,R0              LESS 1
         BCT   R2,SCKLEN
SETLEN   ST    R2,NAMELEN
         LA    R1,L'VALUE
         ST    R1,ILENGTH
         L     R15,SHOWEPA
         CALL  (15),(SHOWNAME,ILENGTH,VALUE,VALUELEN),VL
LTR1     CH    R15,=H'04'
         BNH   SETVAR
         MVC   SEMSGNM,SHOWNAME
         PUTLINE PARM=PUTBLOK,OUTPUT=(SEMSG,TERM,SINGLE,               X
               DATA),MF=(E,IOPLADS)
         SETON ERR
         LA    R4,SHOWNAME
         MVC   CLSTNAME,BLANKS
         MVC   SHOWNAME,BLANKS
         B     INCR
         EJECT
SETVAR   DS    0H
         SPACE 1
         CLC   CLSTNAME,SHOWNAME  SAME NAME ?
         BE    DOCLST             YES
         LA    R1,CLSTNAME+7
         LA    R2,8
VLP1     CLI   0(R1),C' '         LAST SIGNIFICANT CHAR
         BNE   SETVLP1
         BCTR  R1,R0
         BCT   R2,VLP1
SETVLP1  ST    R2,NAMELEN
         SPACE 1
         SPACE 1
DOCLST   DS    0H                                          *LBD 12/85*
         L     R15,CVTPTR       -> CVT
         USING CVTMAP,R15
         L     R15,CVTTVT         ACCESS THE TSVT
         DROP  R15
         USING TSVT,R15
         L     R15,TSVTVACC
         DROP  R15
*
         CALL  (15),(ECODE,NAMEPTR,NAMELEN,VALUEPTR,VALUELEN,TOKEN),VL
LTR2     LTR   R15,R15
         BZ    RET
         MVC   CEMSGNM,CLSTNAME
         PUTLINE PARM=PUTBLOK,OUTPUT=(CEMSG,TERM,SINGLE,               X
               DATA),MF=(E,IOPLADS)
         SETON ERR
RET      LA    R4,SHOWNAME
         MVC   CLSTNAME,BLANKS
         MVC   SHOWNAME,BLANKS
         MVC   VALUE,BLANKS
         B     INCR
         EJECT
EXIT     DS    0H
         DELETE EP=SHOW
         SR    R15,R15
         IFOFF ERR,EXIT0
         LA    R15,8
EXIT0    DS    0H
         MLEAVE
         EJECT
         LTORG
         SPACE 2
         BITSW DOIT,END,ERR
         SPACE 2
CEMSG    DC    AL2(CEMSGL),AL2(0)
         DC    C'ERROR SETTING CLIST VARIABLE '
CEMSGNM  DC    CL8' '
CEMSGL   EQU   *-CEMSG
         SPACE 1
SEMSG    DC    AL2(SEMSGL),AL2(0)
         DC    C'INVALID INFORMATION REQUEST FOR VALUE '
SEMSGNM  DC    CL8' '
         DC    C' SEE HELP FOR VALID KEYWORD OPTIONS'
SEMSGL   EQU   *-SEMSG
         SPACE 2
SHOWEPA  DC    A(0)               EPA OF SHOW
MOVEOPER MVC   OPERANDS(0),0(R3)  ***** EXECUTED
SHOWNAME DC    CL8' '             NAME OF SHOW VALUE
CLSTNAME DC    CL8' '             NAME OF CLIST VAR
OPERANDS DC    CL256' '
BLANKS   DC    CL256' '
ECODE    DC    A(TSVEUPDT)      ENTRY CODE
VALUE    DC    CL256' '
VALUEPTR DC    A(VALUE)
VALUELEN DC    A(L'VALUE)
ILENGTH  DC    A(L'VALUE)
NAMEPTR  DC    A(CLSTNAME)
NAMELEN  DC    F'8'
TOKEN    DC    F'0'
         EJECT
         IKJTSVT
         EJECT
         IKJIOPL ,
         EJECT
         CVT   DSECT=YES,LIST=YES
         END
