         TITLE 'OPTIMAL BLKSIZE GENERATOR   12-17-82'
*---------------------------------------------------------------------*
*        MODULE NAME: BLKSIZE                                         *
*                                                                     *
*        DESCRIPTION: CALCULATE THE OPTIMUM BLKSIZE FOR               *
*                      3330, 3350 AND 3380 DASD.                      *
*                                                                     *
*        THE FORMULA IS FROM AN IBM DASD PLANNING CLIST               *
*                                                                     *
*        JCL:  ..STEP EXEC PGM=BLKSIZE,PARM='LRECL=XX'                *
*              ..SYSPRINT DD SYSOUT=*     (REQUIRED FOR BATCH ONLY)   *
*                                                                     *
*        TSO:  CALL (BLKSIZE) 'LRECL=XX'                              *
*                                                                     *
*        NOTE: THE PARM MAY BE ABBREVIATED AS L=XX                    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
         MACRO
&L       $ED   &TO,&FROM
         MVC   &TO,=X'40202021'
         ED    &TO,&FROM+2
         MVI   &TO+4,C'%'
         MEND
         EJECT
BLKSIZE  $PROLOG
         L    1,0(1)            * LOAD ADDR OF PARM
         LH   2,0(1)            * LOAD LENGTH OF PARM
         LTR   2,2              * LENGTH OF ZERO
         BZ    EXIT             * NO - EXIT
         LA    R4,2(R1)         * -> PARM DATA
         LR    R5,R4
         AR    R5,R2            * -> END OF PARM DATA
         EXTRACT DWK,'S',FIELDS=(TSO)
         L     R1,DWK
         TM    0(R1),TSO
         BO    PLOOP
         SETON BAT
PLOOP    EQU   *
*---------------------------------------------------------------------*
         CLC   =C'LRECL=',0(R4)
         BE    DOBLKS
         CLC   =C'L=',0(R4)
         BE    DOBLKSS
*---------------------------------------------------------------------*
         LA    R4,1(R4)
         CR    R4,R5
         BH    ENDSCAN
         B     PLOOP
DOBLKSS  LA    R4,2(R4)
         B     DLA
DOBLKS   LA    R4,6(R4)                 -> LRECL=
DLA      LA    R7,LRECL
         SR    R1,R1
BLKLOOP  EQU   *
         CLI   0(R4),C','
         BE    ENDBLK
         CLI   0(R4),C' '
         BL    ENDBLK
         MVC   0(1,R7),0(R4)
         LA    R1,1(R1)
         LA    R7,1(R7)                 -> NEXT BYTE
         LA    R4,1(R4)
         CR    R4,R5
         BH    ENDBLK
         B     BLKLOOP
ENDBLK   EQU   *
         BCTR  R1,R0
         EX    R1,PKBLK
         ZAP   LRECLP,PACKWK
         B     INCR
PKBLK    PACK  PACKWK(4),LRECL(0)
         EJECT
INCR     EQU   *
         LA    R4,1(R4)
         CR    R4,R5
         BH    ENDSCAN
         B     PLOOP
ENDSCAN  EQU   *
STARTIT  IFON  BAT,NOTCRT
         GTSIZE
         LTR   0,0
         BZ    NOTCRT
         TPUT  ERASE,14,FULLSCR
NOTCRT   EQU   *
         CP    LRECLP,=P'6233'
         BH    TOHIGH
         IFON  BAT,DOBAT
         TPUT  T0,L'T0
         TPUT  T1,L'T1
         TPUT  T2,L'T2
         B     DOZAP
DOBAT    FTNOP DOPUT
         OPEN  (SYSPRINT,OUTPUT)
         MVI   T0,C'1'
DOPUT    PUT   SYSPRINT,T0
         PUT   SYSPRINT,T1
         PUT   SYSPRINT,T2
DOZAP    ZAP   I,=P'1740'
         BAL   R4,DOCALC
         BAL   R4,DOTPUT
         ZAP   I,=P'3024'
         BAL   R4,DOCALC
         BAL   R4,DOTPUT
         ZAP   I,=P'6233'
         BAL   R4,DOCALC
         BAL   R4,DOTPUT
         IFOFF BAT,EXIT
         CLOSE SYSPRINT
         B     EXIT
DOTPUT   EQU   *
         UNPK  UPI(4),I
         OI    UPI+3,X'F0'
         UNPK  UPB(4),B
         OI    UPB+3,X'F0'
         $ED   UP30,U30
         $ED   UP50,U50
         $ED   UP80,U80
         IFOFF BAT,TPUTL1
         PUT   SYSPRINT,L1
         BR    R4
TPUTL1   TPUT  L1,L'L1
         BR    R4
DOCALC   EQU   *
         ZAP   DWK,I
         DP    DWK,LRECLP
         ZAP   B,DWK(4)
         ZAP   DWK,B
         MP    DWK,LRECLP
         ZAP   B,DWK
DOU30    ZAP   DWK,=P'13165'
         ZAP   WK,B
         AP    WK,=P'135'
         DP    DWK,WK
         ZAP   BT,DWK(4)
         ZAP   DWK,B
         MP    DWK,BT
         MP    DWK,=P'100'
         DP    DWK,=P'13030'            3330 BLKSIZE
         ZAP   U30,DWK(5)               SAVE IT
DOU50    ZAP   DWK,=P'19254'
         ZAP   WK,B
         AP    WK,=P'185'
         DP    DWK,WK
         ZAP   BT,DWK(4)
         ZAP   DWK,B
         MP    DWK,BT
         MP    DWK,=P'100'
         DP    DWK,=P'19069'            3350 BLKSIZE
         ZAP   U50,DWK(5)               SAVE IT
DOU80    ZAP   DWK,=P'48000'
         ZAP   WK,B
         AP    WK,=P'524'
         DP    DWK,WK
         ZAP   BT,DWK(4)
         ZAP   DWK,B
         MP    DWK,BT
         MP    DWK,=P'100'
         DP    DWK,=P'47476'            3380 BLKSIZE
         ZAP   U80,DWK(5)               SAVE IT
         BR    R4
TOHIGH   IFOFF BAT,TPUTHI
         OPEN  (SYSPRINT,OUTPUT)
         PUT   SYSPRINT,TOHIGHM
         CLOSE SYSPRINT
         B     EXIT
TPUTHI   TPUT  TOHIGHM,L'TOHIGHM
EXIT     $EPILOG
         LTORG
TSO      EQU   X'80'                    EQU FOR EXTRACT TM
         BITSW BAT
         EJECT
TOHIGHM  DC    CL80' '
         ORG   TOHIGHM+1
         DC    C'**  LRECL PROVIDED EXCEEDS THE MAXIMUM ALLOWABLE'
         DC    C' BY THIS PROGRAM **'
         ORG
         DS    0D
DWK      DC    D'0'
WK       DC    PL4'0'
B        DC    PL4'0'
BT       DC    PL4'0'
U30      DC    PL4'0'
U50      DC    PL4'0'
U80      DC    PL4'0'
I        DC    PL4'0'
LRECLP   DC    PL4'0'
PACKWK   DC    PL4'0'
ERASE    DC    X'C1115D7E1140403C4040001DC813'
T0       DC    CL80' '
         ORG   T0+3
         DC    C'SELECTED LRECL = '
LRECL    DC    CL5' '
         ORG
T1 DC CL80'    IDEAL      ACTUAL        3330        3350        3380'
T2 DC CL80'   BLOCKSIZE  BLOCKSIZE UTILIZATION UTILIZATION UTILIZATION'
L1  DC CL80' '                                                     '
UPI      EQU   L1+5
UPB      EQU   L1+17
UP30     EQU   L1+28,4
UP50     EQU   L1+40,4
UP80     EQU   L1+52,4
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=80,RECFM=FBA
         END
