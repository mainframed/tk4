         TITLE 'PRINT ALL USERMOD UPDATES TO A SOURCE MODULE'
*---------------------------------------------------------------------*
*        PRNTMODS IS DESIGNED TO PRINT ALL USERMOD STATEMENTS         *
*        FOUND IN AN ASSEMBLY LISTING TO SIMPLY THE PROCESS           *
*        OF DETERMINING WHERE A MODULE HAS BEEN MODIFIED.             *
*                                                                     *
*        A USERMOD IS DETERMINED TO BE:                               *
*              AN @ IN COLUMN 64                                      *
*              A VALID USERMOD ID K THRU T (EXCLUDING O) IN COLUMN 65 *
*                                                                     *
*        JCL:  //STEP1 EXEC PGM=PRNTMODS                              *
*              //STEPLIB DD DSN=                                      *
*              //INPUT   DD DSN=INPUT FILE                            *
*              //SYSIN   DD *                                         *
*               ---  CONTROL CARDS ---                                *
*                                                                     *
*              SYSPRINT IF NOT PRE-DEFINED IN THE JCL WILL BE         *
*              DYNAMICALLY ALLOCATED.                                 *
*              NOTE THAT SYSPRINT IS FREED AT CLOSE.                  *
*                                                                     *
*        NOTE: THE FOLLOWING CONTROL CARD OPTIONS MAY BE PASSED       *
*              AS CONTROL CARDS OR VIA THE EXECUTION PARM             *
*                                                                     *
*        VALID CONTROL CARDS OPTIONS ARE:                             *
*              OPTION:  DESCRIPTION                                   *
*              DUPLEX=YES OR NO   BOTH SIDES OF PAPER                 *
*              CLASS=   SYSOUT CLASS                                  *
*              ASM      PROCESS ASSEMBLY FOR MODIFICATIONS AND TITLE  *
*              NOASM    NO ASSEMBLY MOD PROCESSING                    *
*              DEST=PRINTER NAME                                      *
*              COPIES=NN COPY COUNT                                   *
*              JDE=JOB DESCRIPTOR ENTRY (DEFAULT = L03)               *
*              TYPE=NNNN           PRINTER TYPE            *LBD 10/86*
*                                                                     *
*              DEFAULTS ARE: DUPLEX,CLASS=A,JDE=L03,COPIES=1          *
*                                 NOASM,DEST=LOCAL                    *
*                                                                     *
*              TITLE=   1-8 CHARACTER TITLE FOR BANNER PAGE           *
*                       THE CURRENT DATE AND TIME WILL ALSO           *
*                       BE USED IF TITLE IS SPECIFIED                 *
*                       *** THE ASSEMBLY MODS REPORT WILL NOT         *
*                       *** BE PRODUCED WITH THIS OPTION....          *
*                                                                     *
*              TYPE=    1403, 3203, 3211 PRINTERS                     *
*                       9700 FOR  XEROX 8700 AND 9700                 *
*                       FICH FOR AUTOCOM II                *LBD 10/86*
*                                                                     *
*                                                                     *
*              VALID OPTIONS FOR TYPE=9700 ARE:                       *
*                                 COPIES=                             *
*                                 DUPLEX=YES/NO                       *
*                                 JDE=                                *
*                                                                     *
*              VALID OPTIONS FOR ALL PRINTER TYPES:                   *
*                                 DEST=                               *
*                                 CLASS=                              *
*                                 TITLE=                              *
*                                 ASM                                 *
*                                 NOASM                               *
*                                                                     *
*        DEPENDENCIES: PROGRAM PARMBLOC MUST BE IN THE STEPLIB OR     *
*                      LINKLIST.                                      *
*---------------------------------------------------------------------*
PRNTMODS $PROLOG R11,R12
         $REGS
         EJECT
         SETON DUPLEX
         L     1,0(1)              * LOAD ADDR OF PARM
         LH    2,0(1)              * LOAD LENGTH OF PARM
         LTR   2,2                 * LENGTH OF ZERO
         BNZ   HAVEPARM            * YES - GO DO IT
NOPARM   EQU   *
         OPEN  (SYSIN)
         TESTOPEN SYSIN,DOIT      SYSIN NOT PRESENT
         SETON CARDS
RPARM    GET   SYSIN
         LA    R2,70(R1)
         B     PARMLOOP
         EJECT
HAVEPARM DS    0H
         LA    R1,2(R1)           -> START OF PARM
         AR    R2,R1              -> END OF PARM
PARMLOOP DS    0H
         CLI   0(R1),C','         COMMA
         BE    PARMBUMP           YES
         OC    0(20,R1),=CL20' '  MAKE UPPER CASE
         CLC   =CL4' ',0(R1)      END OF PARM ?
         BE    DOIT
         CLC   =C'DUPLEX=',0(R1)  DUPLEX PARM (DEFAULT)
         BE    SETDUP
         CLC   =C'ASM',0(R1)      ASM LISTING
         BE    SETASM
         CLC   =C'NOASM',0(R1)    NO ASM PROCESSING
         BE    SETNASM
         CLC   =C'COPIES=',0(R1)  COPIES SPECIFIED
         BE    DOCOPY
         CLC   =C'TITLE=',0(R1)   TITLE OPTION SPECIFIED
         BE    DOTITLE
         CLC   =C'DEST=',0(R1)    PRINTER SPECIFIED
         BE    DOPRT              YES
         CLC   =C'JDE=',0(R1)     JOB DESCRIPTOR ENTRY
         BE    DOJDE              YES
         CLC   =C'TYPE=',0(R1)    OUTPUT TYPE
         BE    DOTYPE             YES
         CLC   =C'CLASS=',0(R1)   SYSOUT CLASS
         BE    DOCLASS            YES
PARMBUMP LA    R1,1(R1)
         CR    R1,R2              END OF SCAN
         BNH   PARMLOOP           NO - KEEP LOOPING
         B     DOIT
SETASM   SETON ASM
         LA    R1,3(R1)
         B     PARMBUMP
SETNASM  SETOF ASM
         LA    R1,5(R1)
         B     PARMBUMP
SETDUP   DS    0H                 SET DUPLEX
         LA    R1,7(R1)           -> OPTION
         CLC   =C'YES',0(R1)      DUPLEX ON ?
         BE    SETDON             YES
         SETOF DUPLEX
         MVI   X97FORM,C'S'       SET AS SIMPLEX
         LA    R1,2(R1)
         B     PARMBUMP
SETDON   SETON DUPLEX             SET DUPLEX
         LA    R1,3(R1)           -> PAST DUPLEX
         B     PARMBUMP
DOJDE    LA    R1,4(R1)           -> JDE
         MVC   JDE,0(R1)          MOVE IT TO DJDE CARD
         LA    R1,3(R1)           -> PAST JDE
         B     PARMBUMP
DOCLASS  LA    R1,6(R1)           -> CLASS
         MVC   CLASS(1),0(R1)     MOVE IT TO DJDE CARD
         LA    R1,1(R1)           -> PAST CLASS
         B     PARMBUMP
DOCOPY   LA    R1,7(R1)           -> COPY COUNT
         LA    R3,COPY#           SAVE START OF NUMBER
COPYLP   DS    0H
         CLI   0(R1),C' '         END OF COPY
         BE    PARMBUMP           YES
         CLI   0(R1),C','         END OF COPY
         BE    PARMBUMP           YES
         MVC   0(1,R3),0(R1)      MOVE COPY
         LA    R1,1(R1)           BUMP IT
         LA    R3,1(R3)           BUMP IT
         B     COPYLP
DOPRT    LA    R1,5(R1)           -> START OF DEST NAME
         LA    R3,REMOTE          -> REMOTE NAME FIELD
PRTLOOP  MVC   0(1,R3),0(R1)      MOVE PRT NAME
         LA    R1,1(R1)           BUMP PARM
         LA    R3,1(R3)           BUMP DEST  NAME
         CLI   0(R1),C' '         END OF REMOTE NAME
         BE    PARMBUMP           YES
         CLI   0(R1),C','         END OF REMOTE NAME
         BE    PARMBUMP           YES
         B     PRTLOOP            KEEP MOVEING NAME
DOTITLE  LA    R1,6(R1)           -> START OF TITLE
         LA    R3,TITLE+1         -> TITLE FIELD
         MVC   0(8,R3),=CL8' '
TITLOOP  MVC   0(1,R3),0(R1)      MOVE TITLE CHARACTER
         LA    R1,1(R1)           BUMP PARM
         LA    R3,1(R3)           BUMP TITLE NAME
         CLI   0(R1),C' '         END OF TITLE
         BE    PARMBUMP           YES
         CLI   0(R1),C','         END OF TITLE
         BE    PARMBUMP           YES
         B     TITLOOP            KEEP MOVEING TITLE
DOTYPE   DS    0H
         LA    R1,5(R1)           -> TYPE
         CLC   =C'9700',0(R1)     9700 OUTPUT ?
         BE    SET9700            YES
         CLC   =C'1403',0(R1)     HARDCOPY ?
         BE    SETHARD            YES (DEFAULT)
         CLC   =C'3211',0(R1)     HARDCOPY ?
         BE    SETHARD            YES (DEFAULT)
         CLC   =C'3203',0(R1)     HARDCOPY ?
         BE    SETHARD            YES (DEFAULT)
         CLC   =C'FICH',0(R1)      MICRO-FICHE ?           *LBD 10/86*
         BE    SETFICH             YES - SET IT            *LBD 10/86*
         B     PARMBUMP
SETFICH  LA    R1,4(R1)                                    *LBD 10/86*
         SETON FICH                                        *LBD 10/86*
         B     PARMBUMP                                    *LBD 10/86*
SETHARD  LA    R1,4(R1)           BUMP IT
         B     PARMBUMP
SET9700  SETON X9700
         LA    R1,4(R1)           BUMP PAST 9700
         B     PARMBUMP
         EJECT
EOPARM   CLOSE SYSIN
         SETOF CARDS
         B     DOIT
         EJECT
DOIT     DS    0H                 START THE REAL PROCESS
*        GET THE DATE/TIME                                 *LBD 02/86*
         STCK  TOD
         LA    R1,TOD
         CVTOD
         MVC   RDATE(8),OUTDM
         MVC   RTIME(5),OUTTH
         EJECT                                             *LBD 02/86*
         IFON  CARDS,RPARM
         OPEN  (INPUT)            OPEN ASSEMBLER INPUT
         MVC   SYSPRINT+36(1),INPUT+36  FIXUP RECFM
         MVC   SYSPRINT+62(2),INPUT+62  FIXUP BLKSIZE
         MVC   SYSPRINT+82(2),INPUT+82  FIXUP LRECL
         TM    SYSPRINT+36,X'20'  MACH CARRIAGE CONTROL
         BNE   CCOK
         MVI   SKIP1,X'8B'         SET MACH SKIP TO CH1
         MVI   OLINE,X'09'         SET MACH SINGLE SPACE
         MVI   TITLE,X'11'         SET MACH DOUBLE SPACE
CCOK     DS    0H
         CLI   REMOTE,C' '        ANY REMOTE
         BNE   DOIT2
         MVC   REMOTE,=CL8'LOCAL'
         IFOFF X9700,NOSHOW
DOIT2    DS    0H
         LOAD  EP=SHOW,ERRET=NOSHOW
         ST    R0,SHOWEPA
****     GET NAME
         MVC   PARMID,=CL8'NAME'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   NAME,PARMV
*** GET  MAIL  CODE
         MVC   PARMID,=CL8'MAIL'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   MAIL,PARMV
*** GET  ACCTNO
         MVC   PARMID,=CL8'ACCTNO'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   ACCTNO,PARMV
*** GET  FDS
         MVC   PARMID,=CL8'FDS'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   FDS,PARMV
         MVC   DIV,PARMV
*** GET  DEPT
         MVC   PARMID,=CL8'DEPT'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   DEPT,PARMV
*** GET  GROUP
         MVC   PARMID,=CL8'GROUP'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   GROUP,PARMV
         MVC   COPY,COPY#
         LA    R1,COPY
CLP      CLI   0(R1),C' '         END OF COPY
         BE    DC
         LA    R1,1(R1)           BUMP IT
         B     CLP
DC       MVC   0(2,R1),=C',;'
         MVC   DUPOPT,=CL5'NO,;'
         IFOFF DUPLEX,OKDUP
         MVC   DUPOPT,=C'YES,;'
OKDUP    DS    0H
         DELETE EP=SHOW
NOSHOW   DS    0H
         IFOFF FICH,DO97                                   *LBD 10/86*
         LA    R2,ALLOC1           ONLY IF DD PREALLOCATED
         ALLOC DDN='SYSPRINT',SYSOUT=SOC,DEST=REMOTEN,                 X
               FORMS='FICH',ERROR=S99FAIL
         B     ALLOC1                                      *LBD 10/86*
DO97     DS    0H                                          *LBD 10/86*
         IFOFF X9700,NORMALL
         LA    R2,ALLOC1           ONLY IF DD PREALLOCATED
         ALLOC DDN='SYSPRINT',SYSOUT=SOC,DEST=REMOTEN,                 X
               FORMS=X97FORMS,FCB='JB10',ERROR=S99FAIL
ALLOC1   DS    0H
         OPEN  (SYSPRINT,OUTPUT)
         IFON  FICH,COPYIT                                 *LBD 10/86*
         PUT   SYSPRINT,C1
         PUT   SYSPRINT,C2
         PUT   SYSPRINT,C3
         PUT   SYSPRINT,C4
         PUT   SYSPRINT,C5
         PUT   SYSPRINT,C6
         B     COPYIT
NORMALL  LA    R2,ALLOC2
         ALLOC DDN='SYSPRINT',SYSOUT=SOC,DEST=REMOTEN,ERROR=S99FAIL
ALLOC2   DS    0H
         OPEN  (SYSPRINT,OUTPUT)
COPYIT   DS    0H
         CLI   TITLE+1,C' '       ANY TITLE ?
         BE    DOASM
         MVC   TIME+2(5),OUTTH
         MVC   DATE+2(8),OUTDM
         LA    R1,TITLE
         B     GMOD               NO
DOASM    DS    0H
         IFOFF ASM,SKIPBLOC
         GET   INPUT
RD1      DS    0H
         GET   INPUT
         LR    R5,R1
         CLC   =C'SYMBOL',1(R1)
         BE    GDATE
         CLC   =C' SD ',10(R1)
         BE    GMOD
         B     RD1
GDATE    DS    0H
         MVC   DATE+2(8),113(R1)
         MVC   TIME+2(5),107(R1)
         B     RD1
GMOD     DS    0H
         MVC   PMOD+2(8),1(R1)
         IFON  FICH,OPFICH                                 *LBD 10/86*
         CLOSE (INPUT,,SYSPRINT)   CLOSE INPUT AND SYSPRINT
         SPACE  2
*--------------------------------------------------------------*
*        INVOKE PARMBLOC TO PRINT 2 BANNER PAGES               *
*        CONSISTING OF THE MODULE NAME AND DATE/TIME           *
*--------------------------------------------------------------*
         SPACE 1
         LINK  EP=PARMBLOC,PARAM=PMOD
         LINK  EP=PARMBLOC,PARAM=DATE
         LINK  EP=PARMBLOC,PARAM=TIME
         SPACE 1
         IFON  X9700,OPEN
         LINK  EP=PARMBLOC,PARAM=PMOD
         LINK  EP=PARMBLOC,PARAM=DATE
         LINK  EP=PARMBLOC,PARAM=TIME
         SPACE  2
         B     OPEN
         SPACE  2
SKIPBLOC DS    0H
         CLOSE (INPUT,,SYSPRINT)   CLOSE INPUT AND SYSPRINT
         B     OPEN                                        *LBD 10/86*
         SPACE 2                                           *LBD 10/86*
OPFICH   DS    0H                                          *LBD 10/86*
         CLOSE INPUT                                       *LBD 10/86*
         MVC   MODNAME,PMOD+2                              *LBD 10/86*
         MVC   ASMDATE,DATE+2                              *LBD 10/86*
         MVC   ASMTIME,TIME+2                              *LBD 10/86*
         PUT   SYSPRINT,F1CC                               *LBD 10/86*
         PUT   SYSPRINT,F2CC                               *LBD 10/86*
         PUT   SYSPRINT,F3CC                               *LBD 10/86*
         OPEN  (INPUT)                                     *LBD 10/86*
         B     OPENED                                      *LBD 10/86*
         SPACE  2
OPEN     OPEN  (INPUT,,SYSPRINT,OUTPUT)
OPENED   DS    0H                                          *LBD 10/86*
         IFON  ASM,READ
         OI    FLAG,X'01'         SET TO BYPASS ASM PASS
READ     GET   INPUT
         LR    R5,R1               SAVE R1
         TM    FLAG,X'01'          FIRST PASS
         BZ    DOUMOD              YES
         PUT   SYSPRINT,0(R5)
         B     READ
DOUMOD   DS    0H
         CLI   40(R5),C'+'
         BE    READ
         CLI   104(R5),C'@'
         BNE   READ
         CLI   105(R5),C'K'
         BL    READ
         CLI   105(R5),C'T'
         BH    READ
         CLI   105(R5),C'O'        IBM FOUL UP
         BE    READ
         MVC   OLINE+5(80),41(R5)
         AP    LCTR,=P'01'
         CP    LCTR,=P'54'
         BNH   PUTIT
         ZAP   LCTR,=P'03'
         AP    PCTR,=P'01'
         UNPK  PAGE,PCTR
         OI    PAGE+4,X'F0'
         PUT   SYSPRINT,SKIP1
         PUT   SYSPRINT,TITLE
         PUT   SYSPRINT,OLINE
         B     READ
PUTIT    PUT   SYSPRINT,OLINE
         B     READ
EOF      DS    0F
         TM    FLAG,X'01'          FIRST EOF ?
         BO    EOF2                NO - GOTO EOF2
         CLOSE INPUT
         OPEN  INPUT
         OI    FLAG,X'01'          SET ON EOF1 FLAG
         B     READ
EOF2     DS    0H
         IFOFF X9700,CLOSEIT
         PUT   SYSPRINT,SKIP1
         PUT   SYSPRINT,C7
CLOSEIT  DS    0H
         CLOSE (SYSPRINT,FREE,INPUT)
EXIT     DS    0H
EPILOG   $EPILOG 0
S99FAIL  DS    0H
         CLC   4(2,R14),=X'0410'  DD ALREADY ALLOCATED ?
         BER   R2                 YES CONTINUE
         S99FAIL
         $EPILOG 4                EXIT
LCTR     DC    PL2'99'
PCTR     DC    PL2'00'
FLAG     DC    X'00'
SKIP1    DC    CL121'1'
OLINE    DC    CL121' '
TITLE    DC    CL121' '
         ORG   TITLE+10
         DC    C'USER MODIFICATIONS TO MODULE '
MODULE   DC    CL8' '
         ORG   TITLE+75
         DC    C'PAGE '
PAGE     DC    CL5' '
         ORG
PMOD     DC    AL2(8),CL8' '
TIME     DC    AL2(7),CL5' ',C',N'
DATE     DC    AL2(10),CL8' ',C',N'
         DS    0F
PARMBLOC DC    CL8'PARMBLOC'
ABEND    LR    R1,R15
         ABEND (R1)               BAD RC FROM SHOW
         EJECT
         LTORG
TOD      DS    D
PARMS    DS    0D
         DC    A(PARMID,PARMLEN,PARMV)
         DC    AL1(80),AL3(PARMVL)
PARMID   DC    CL8' '
PARMLEN  DC    A(L'PARMV)
PARMV    DC    CL100' '
PARMVL   DC    F'0'
SHOWEPA  DS    F
COPY#    DC    CL8'1'
         SPACE
X97FORMS DC    A(X97FORM),Y(4)
X97FORM  DC    CL4'DZ$H'          XEROX 9700 FORM
         SPACE
REMOTEN  DC    A(REMOTE),Y(8)
REMOTE   DC    CL8' '
         SPACE
SOC      DC    A(CLASS),Y(1)
CLASS    DC    C'A'
         EJECT
         BITSW DUPLEX,X9700,CARDS,ASM,FICH
         SPACE
C1       DC    CL121' '
         ORG   C1
         DC    C'1DFM1'
         ORG   C1+9
         DC    C'JDE='
JDE      DC    CL3'L03'
         DC    C',DEPT='
         DC    X'7D'
FDS      DC    CL3' '
ACCTNO   DC    CL4' '
         DC    X'7D'
         DC    C',COPIES='
COPY     DC    CL8' '
         ORG   ,
C2       DC    CL121' '
         ORG   C2
         DC    C' DFM1'
         ORG   C2+9
         DC    C'SHIFT=YES,COLLATE=YES,DUPLEX='
DUPOPT   DC    CL5' '
         ORG   ,
C3       DC    CL121' '
         ORG   C3
         DC    C' DFM1'
         ORG   C3+9
         DC    C'RTEXT=('
         DC    X'7D'
         DC    C'DELIVER TO:'
NAME     DC    CL20' '
         DC    CL4' '                                      *LBD 02/86*
RDATE    DC    CL8' '                                      *LBD 02/86*
         DC    CL2' '                                      *LBD 02/86*
RTIME    DC    CL5' '                                      *LBD 02/86*
         DC    X'7D'
         DC    C',01,10,01),;'
         ORG   ,
C4       DC    CL121' '
         ORG   C4
         DC    C' DFM1'
         ORG   C4+9
         DC    C'RTEXT=('
         DC    X'7D'
         DC    C'DIV='
DIV      DC    CL3' '
         DC    C' DEPT='
DEPT     DC    CL4' '
         DC    C' GROUP='
GROUP    DC    CL4' '
         DC    X'7D'
         DC    C',01,12,01),;'
         ORG   ,
C5       DC    CL121' '
         ORG   C5
         DC    C' DFM1'
         ORG   C5+9
         DC    C'RTEXT=('
         DC    X'7D'
         DC    C' MAIL CODE='
MAIL     DC    CL8' '
         DC    X'7D'
         DC    C',01,14,01),;'
         ORG   ,
C6       DC    CL121' '
         ORG   C6
         DC    C' DFM1'
         ORG   C6+9
         DC    C'END;'
         ORG   ,
C7       DC    CL121' *END*OF*REPORT*'
         EJECT
F1CC     DC    CL121'1 CUSTOM'
         ORG   F1CC+1
         DC    X'0D'
         ORG
F2CC     DC    CL121'1 INIT TL1 64 OPER 6 '
         ORG   F2CC+1
         DC    X'0D'
         ORG   F2CC+22
         DC    X'7D'
MODNAME  DC    CL8' '
         DC    CL15' ',C'DATE = '
ASMDATE  DC    CL8' '
         DC    CL5' ',C'TIME = '
ASMTIME  DC    CL5' '
         ORG   F2CC+82
         DC    X'7D',C' END'
         ORG
F3CC     DC    CL121'1 INIT IKEY CLEAR END'
         ORG   F3CC+1
         DC    X'0D'
         ORG
         EJECT
         DYNSPACE ,
         EJECT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT
         EJECT
INPUT    DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT,EODAD=EOF
         EJECT
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=EOPARM
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         END   ,
