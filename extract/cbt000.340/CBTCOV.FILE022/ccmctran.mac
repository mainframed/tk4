CCMCTRAN TITLE 'BUILD IEBCOPY CARDS FOR APPLIED SYSMODS'    *LBD 03/84*
*************************************************************LBD 03/84*
*                                                           *LBD 03/84*
* FUNCTION:    TO SCAN THE SMPLOG DATASET AND BUILD IEBCOPY *LBD 03/84*
*              CONTROL CARDS FOR  ALL SYSMODS THAT ARE      *LBD 03/84*
*              SUCCESSFULLY APPLIED.                        *LBD 03/84*
*                                                           *LBD 03/84*
*              THESE CARDS CAN THEN BE PASSED TO THE BDT/IUP*LBD 03/84*
*              PRODUCT TO SEND TO OTHER DATA CENTERS TO     *LBD 03/84*
*              MAINTAIN THE SAME  LEVEL OF MAINTENANCE.     *LBD 03/84*
*                                                           *LBD 03/84*
* JCL:         //STEP EXEC PGM=CCMCTRAN                     *LBD 03/84*
*              //SMPLOG  DD DSN=SYS1.SMPLOG,DISP=SHR        *LBD 03/84*
*              //LOGOUT  DD SYSOUT=*    COPY OF SMPLOG      *LBD 03/84*
*              //COPYCC  DD DSN=&&CARDS,DISP=(,PASS),       *LBD 03/84*
*              //  SPACE=(TRK,(10,10),RLSE),UNIT=SYSDA      *LBD 03/84*
*                                                           *LBD 03/84*
* RETURN CODES:  0 = SYSMODS FOUND AND CONTROL CARDS BUILT  *LBD 03/84*
*                8 = NO SYSMODS FOUND                       *LBD 03/84*
*                                                           *LBD 03/84*
*************************************************************LBD 03/84*
* AUTHOR:      LIONEL DYCK                                  *LBD 03/84*
*              ROCKWELL INTERNATIONAL                       *LBD 03/84*
*              PO BOX 2515                                  *LBD 03/84*
*              SEAL BEACH, CALIF  90740                     *LBD 03/84*
*              MAIL CODE 110-SH28                           *LBD 03/84*
*              PHONE (213) 594-1125                         *LBD 03/84*
*              CASNET 374-1125                              *LBD 03/84*
*************************************************************LBD 03/84*
         EJECT                                              *LBD 03/84*
CCMCTRAN $PROLOG R12                                        *LBD 03/84*
         $REGS                                              *LBD 03/84*
         LA    R10,8              SET RETURN CODE TO N/G    *LBD 03/84*
         OPEN  (SMPLOG,,COPYCC,OUTPUT,LOGOUT,OUTPUT)        *LBD 03/84*
READIT   GET   SMPLOG                                       *LBD 03/84*
         LR    R2,R1                                        *LBD 03/84*
         LH    R3,SMPLOG+X'52'    LOAD LEN OF RECORD        *LBD 03/84*
         MVC   LOGDATA(250),BLANKS                          *LBD 03/84*
         EX    R3,MVCLOG                                    *LBD 03/84*
         PUT   LOGOUT,LOGDATA                               *LBD 03/84*
         CLC   =C'GIM2270',10(R2)  SMP/E SUCCESSFUL APPLY  *LBD 03/87*
         BE    GOTIT                                       *LBD 03/87*
         CLC   =C'HMA2270',10(R2)  SMP/4                    *LBD 03/84*
         BNE   READIT                                       *LBD 03/84*
GOTIT    DS    0H                                          *LBD 03/87*
         SR    R10,R10            RESET RETURN CODE TO OK   *LBD 03/84*
         CLI   72(R2),C' '         SYSMOD HERE              *LBD 01/86*
         BE    MVC2                NO - MUST BE HIGHER LVL  *LBD 01/86*
         MVC   MEM,72(R2)         MOVE SYSMOD NAME          *LBD 03/84*
         B     D1                                           *LBD 01/86*
MVC2     MVC   MEM,73(R2)                                   *LBD 01/86*
D1       DS    0H                                           *LBD 01/86*
         IFON  CC1,SKCC1                                    *LBD 03/84*
         PUT   COPYCC,CARD1                                 *LBD 03/84*
         SETON CC1                                          *LBD 03/84*
SKCC1    PUT   COPYCC,CARD2                                 *LBD 03/84*
         B     READIT                                       *LBD 03/84*
EOFLOG   CLOSE (SMPLOG,,COPYCC,,LOGOUT)                     *LBD 03/84*
         LTR   R10,R10                                      *LBD 03/84*
         BZ    EXITOK                                       *LBD 03/84*
         $EPILOG 8                NO SYSMODS FOUND          *LBD 03/84*
EXITOK   $EPILOG 0                SYSMODS FOUND             *LBD 03/84*
         LTORG                                              *LBD 03/84*
         BITSW CC1                                          *LBD 03/84*
MVCLOG   MVC   LOGDATA(0),0(R2)                             *LBD 03/84*
BLANKS   DC    260C' '                                      *LBD 03/84*
LOGDATA  DC    260C' '                                      *LBD 03/84*
CARD1    DC    CL80' COPY INDD=((IN,R)),OUTDD=OUT'          *LBD 03/84*
CARD2    DC    CL80' '                                      *LBD 03/84*
         ORG   CARD2                                        *LBD 03/84*
         DC    C' SELECT MEMBER='                           *LBD 03/84*
MEM      DC    CL7' '                                       *LBD 03/84*
         ORG   ,                                            *LBD 03/84*
         EJECT                                              *LBD 03/84*
COPYCC   DCB   DSORG=PS,MACRF=PM,DDNAME=COPYCC,RECFM=FB,    *LBD 03/84*X
               LRECL=80,BLKSIZE=6160                        *LBD 03/84*
         EJECT                                              *LBD 03/84*
SMPLOG   DCB   DSORG=PS,MACRF=GL,DDNAME=SMPLOG,EODAD=EOFLOG *LBD 03/84*
         EJECT                                              *LBD 03/84*
LOGOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=LOGOUT,RECFM=F,     *LBD 03/84*X
               BLKSIZE=133,LRECL=133                        *LBD 03/84*
         END   ,                                            *LBD 03/84*
