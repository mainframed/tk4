COPYDS   TITLE 'ISPF PROGRAM TO COPY SEQUENTIAL FILES'
*---------------------------------------------------------*
* NAME:  COPYDS                                           *
*                                                         *
* SAMPLE CLIST :                                          *
*                                                         *
*        SET &FDSN = FROM.DATA.SET.NAME                   *
*        SET &TDSN = 'TO.DATA.SET.NAME'                   *
*        ISPEXEC VPUT (FDSN TDSN)                         *
*        ISPEXEC SELECT PGM(COPYDS)                       *
*                                                         *
*---------------------------------------------------------*
         EJECT
COPYDS   $PROLOG R12
         EJECT
         $VGET FDSN
         MVC   FDSN,0(R1)
         SPACE 2
         $VGET TDSN
         MVC   TDSN,0(R1)
         SPACE 2
         $VGET ZPREFIX
         MVC   ZPREFIX,0(R1)
         EJECT
         LA    R1,FDSN                 -> FDSN
         BAL   R4,FIXDSN
         LA    R1,TDSN                 -> TDSN
         BAL   R4,FIXDSN
         EJECT
         ALLOC DDNRET=DDFDSN,DSN=FDSNA,DISP=SHR,ERROR=S99FAIL
         MVC   IN+40(8),DDFDSN+2
         EJECT
         ALLOC DDNRET=DDTDSN,DSN=TDSNA,DISP=SHR,ERROR=S99FAIL
         MVC   OUT+40(8),DDTDSN+2
         OPEN  (IN,,OUT,OUTPUT)
R        GET   IN
         LR    R5,R1
         PUT   OUT,(R5)
         B     R
EOF      CLOSE (IN,FREE,OUT,FREE)
         $EPILOG 0
         EJECT
FIXDSN   DS    0H
         CLI   0(R1),C''''             QUOTE
         BE    FIXQ
         MVC   WDSN,ZPREFIX
         LA    R2,WDSN
FIXPRE   DS    0H
         CLI   0(R2),C' '
         BE    FIXPREA
         LA    R2,1(R2)
         B     FIXPRE
FIXPREA  DS    0H
         MVI   0(R2),C'.'
         LA    R2,1(R2)
         MVC   0(44,R2),0(R1)
         MVC   0(56,R1),WDSN
         BR    R4
FIXQ     DS    0H
         LR    R3,R1
         LA    R1,1(R1)
         LA    R2,WDSN
FIXQM    MVC   0(1,R2),0(R1)
         LA    R1,1(R1)
         LA    R2,1(R2)
         CLI   0(R1),C''''
         BNE   FIXQM
         MVC   0(56,R3),WDSN
         BR    R4
         EJECT
S99FAIL  S99FAIL
         $EPILOG 4
         EJECT
ZPREFIX  DC    CL8' '
FDSNA    DC    A(FDSN),Y(44)
FDSN     DC    CL56' '
TDSNA    DC    A(TDSN),Y(44)
TDSN     DC    CL56' '
WDSN     DC    CL56' '
         EJECT
IN       DCB   DSORG=PS,MACRF=GL,DDNAME=XXXX,EODAD=EOF
         EJECT
OUT      DCB   DSORG=PS,MACRF=PM,DDNAME=XXXX
         EJECT
         VGETSUB
         EJECT
         DYNSPACE
         SPACE
         IEFZB4D0
         EJECT
         IEFZB4D2
         END
