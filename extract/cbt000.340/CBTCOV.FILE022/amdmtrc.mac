AMDMTRC  TITLE 'AMDMTRC FORMATS SU64 MASTER TRACE AREA'        DG82039
         MACRO
&NAME    AMDPRINT &PARM=(1),&RL=
&NAME    IHBINNRA &PARM           PICK UP PARM LIST ADDRESS
         L     15,ADPLPRNT-ABDPL(,1) LOAD PRINT ROUTINE ADDRESS
         BALR  14,15              CALL PRINT ROUTINE
         AIF   ('&RL' EQ '').OUT  NO RELOAD, EXIT
         L     &RL,ADPLBUF-ABDPL(,1) LOAD NEW BUFFER ADDRESS
.OUT     MEND
         SPACE 1
         MACRO
&NAME    AMDPRSTG &PARM=(1),&STOR=(0),&ERROR=
&NAME    IHBINNRA ,&STOR          PICK UP STORAGE ADDRESS
         IHBINNRA &PARM           PICK UP PARM LIST ADDRESS
         L     15,ADPLMEMA-ABDPL(,1) GET ROUTINE ADDRESS
         BALR  14,15              CALL STORAGE ACCESS ROUTINE
         AIF ('&ERROR' EQ '').OUT
         LTR   15,15              ERROR?
         BNZ   &ERROR             YES, GO TO ERROR ROUTINE
.OUT     MEND
         SPACE 1
         MACRO
&NAME    AMDPRFMT &PARM=(1),&PATTERN=(0),&ERROR=
&NAME    IHBINNRA ,&PATTERN       PICK UP DATA ADDRESS
         IHBINNRA &PARM           PICK UP PARM LIST ADDRESS
         L     15,ADPLFRMT-ABDPL(,1) GET FORMAT ROUTINE ADDRESS
         BALR  14,15              CALL FORMAT ROUTINE
         AIF ('&ERROR' EQ '').OUT
         LTR   15,15              ERROR?
         BNZ   &ERROR             YES, GO TO ERROR ROUTINE
.OUT     MEND
         SPACE 1
AMDMTRC  CSECT ,
         SPACE 1
R0       EQU   0                  EQUATES FOR REGISTERS 0-15
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*
***** START OF SPECIFICATIONS **************************************
***********************************************************************
*     THIS PROGRAM IS DESIGNED TO EXECUTE AS A USER EXIT TO THE       *
*     MVS AMDPRDMP SERVICE AID.  ITS FUNCTION IS TO FORMAT THE        *
*     MASTER TRACE TABLE OF CONSOLE MESSAGES.  THIS TRACE FEATURE     *
*     WAS ADDED BY SU64 AND THE BEST REFERENCE IS MVS DIAGNOSTIC      *
*     TECHNIQUES SUPPLEMENT GD23-0141 (FOR SU64) PAGES 2.6.9 - 2.6.9.1*
*                                                                     *
*     THE PROGRAM IS EXECUTED WHENEVER THE AMDPRDMP COMMAND OF        *
*     "FMTMT" IS ENCOUNTERED IN THE AMDPRDMP SYSIN FILE.  IN ORDER    *
*     TO RECOGNIZE THE "FMTMT" COMMAND, A USER ENTRY MUST BE MADE     *
*     IN THE AMDPRECT USER EXIT MODULE.  APPENDIX "C" OF THE "OS/VS2  *
*     S.P.L.:  SERVICE AIDS" MANUAL (GC28-0674) DESCRIBES HOW TO      *
*     PERFORM THIS FUNCTION                                           *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* MODULE NAME - AMDMTRC
*
* DESCRIPTIVE NAME - AMDPRDMP MASTER TRACE FORMAT ROUTINE
*
* STATUS - CHANGE LEVEL 01
*
* FUNCTION -
*        THIS ROUTINE IS INVOKED BY THE AMDPRDMP 'FMTMT' CONTROL
*        CARD TO FORMAT THE MASTER TRACE TABLE OF CONSOLE MESSAGES.
*        THIS TRACE TABLE WAS ADDED BY SU64.
*
* NOTES -
*        IF SU64 IS NOT IN THE SYSTEM, A MESSAGE ATATING SO IS ISSUED.
*        IF THE TRACE WAS OFF AT THE TIME OF THE DUMP, THE TRACE TABLE
*        WAS FREEMAINED, SO THERE IS NO OLD DATA TO PRINT.
*
* MODULE TYPE - CSECT
*
* ENTRY POINT - AMDMTRC
*
* PATCH AREA  - AT LABEL 'PATCHLOC'
*
* INPUT -
*        THE AMDPRDMP USER PARAMETER BLOCK AS MAPPED BY AMDPRPRM.
*
* OUTPUT -
*        A FORMATTED MASTER TRACE TABLE OR ERROR MESSAGE
*
* SYMBOLS USED -
*        CVTMSER  - ADDRESS OF MSRDA (IEEBASEA)
*        BAMTTBL  - ADDRESS OF MASTER TRACE TABLE (IEEZB806)
*
* EXIT, NORMAL -
*        TO CALLER - NO RETURN CODE
*
* EXIT, ERROR -
*        TO CALLER - NO RETURN CODE
*
* EXTERNAL REFERENCES - NONE
*
* TABLES - NONE
*
* MACROS -
*        AMDPRINT - INVOKE AMDPRDMP PRINT ROUTINE
*        AMDPRSTG - INVOKE AMDPRDMP STORAGE ACCESS ROUTINE
*
* CHANGE ACTIVITY -
* 9/9/81-FIXED SPURRIOUS ABEND0C4, MADE GETMAIN VARIABLE LENGTH
* 2/8/82-ADDED COPYRIGHT INFORMATION TO OBJECT CODE.
* 2/13/82 - FIXED 0C4 WHEN @WRAP = ZERO, FIXED 0C4 WHEN USED AS IPCS
*           VERBEXIT DUE TO IMPROPER SAVEAREA LINKAGE -      DLP820213
* 2/15/82 - FIXED BUG WHEN DEALING WITH ZERO LENGTH ENTRY -  DLP820215
*
****** END OF SPECIFICATIONS ***************************************
         TITLE 'AMDMTRC  FORMAT MASTER TRACE - INITIALIZATION'
         SPACE 1
         SAVE  (14,12),,AMDMTRC_COPYRIGHT_(C)_1985_AMDAHL_CORP_SE_TOOLS*
               _-_SETT010_AMDIPCSE                             DG84130
         SPACE 1
         LR    R12,R15                   SET THE BASE REGISTER
         USING AMDMTRC,R12               IN WITH THE NEW
         ST    R13,SAVE+4                LINK                DLP820213
         LR    R2,R13                     THE                DLP820213
         LA    R13,SAVE                    SAVE              DLP820213
         ST    R13,8(,R2)                   AREAS            DLP820213
         SPACE 1
BEGIN    ST    R1,PARMADDR               SAVE PASSED PARAMETER BLOCK
         USING ABDPL,R1                  USE R1 TO ACCESS P.LIST. DFG00
         LA    R7,1                      GET ASID 1
         STH   R7,ADPLASID               PLUG ASID INTO P.LIST.   DFG00
         L     R7,ADPLBUF                POINT TO SUPPLIED BUFFER
         USING BUFR,R7                   AND GET ADDRESSABILITY
         SPACE 1
         L     R8,ADPLCVT           GET THE CVT ADDRESS           DFG00
         LA    R0,CVTIHASU-CVT(,R8) POINT TO THE SU BIT STRING
         AMDPRSTG ERROR=NOCVT       GET STRING ADDRESS
         LR    R8,R0                MAKE IT USABLE
         L     R0,0(,R8)            GET THE ADDRESS TO RETRIEVE
         AMDPRSTG ERROR=NOSUBITS    GET THE STRING
         LR    R8,R0                MAKE IT USABLE
         SPACE 1
         USING SUBITS,R8            PICK UP ADDRESSABILITY
* THIS CODE ASSUMES THE STORAGE ACCESSED WAS ON A PROPER BOUNDARY.
         TM    SUBYTE9,SU64BIT      SU 64 BIT ON?
         BNO   NOSU64               NO, PRINT ERROR MSG & QUIT
         DROP  R8                   DONE WITH THAT
         SPACE 1
FMTIT    L     R8,ADPLCVT           GET THE CVT ADDRESS           DFG00
         LA    R0,CVTMSER-CVT(,R8)  GET THE MSRDA ADDRESS
         AMDPRSTG ERROR=NOMSRDA     GET ADDRESS OF BASEA
         LR    R8,R0                MAKE IT USABLE
         L     R8,0(,R8)            PICK UP CONTENTS
         LA    R0,BAMTTBL-BASE(,R8) POINT TO THE TRACE TABLE ADDRESS
         AMDPRSTG ERROR=NOMTTBL     GET ADDRESS OF TRACE TABLE
         LR    R8,R0                MAKE IT USEABLE.
         ICM   R8,B'1111',0(R8)     GET THE ADDRESS & SET CC
         USING MTTABLE,R8           FOR LATER FORMATTING.
         BZ    NOTACTV              NOPE, NOTHING TO FORMAT
         SPACE 1
         MVC   OUTDATA+35(L'TTLMSG),TTLMSG PUT IN TITLE
         AMDPRINT ,                 PRINT HEADER MESSAGE
         AMDPRINT RL=R7             SPACE UP 1
         MVC   OUTDATA+3(L'HDR),HDR PUT IN HEADER LABEL
         AMDPRINT RL=R7             AND WRITE IT OUT
         SPACE 1
         ST    R8,HDRDATA1          SAVE ADDRESS OF HEADER START
         AMDPRFMT PATTERN=HDRFMT1,ERROR=NFMT   FORMAT THE LINE
         AMDPRINT RL=R7             AND WRITE IT OUT
         LA    R8,MTTWRPPT-MTTABLE(,R8) POINT TO NEXT DATA
         SPACE 1
         ST    R8,HDRDATA2          SAVE ADDRESS OF HEADER START
         AMDPRFMT PATTERN=HDRFMT2,ERROR=NFMT   FORMAT THE LINE
         AMDPRINT RL=R7             AND WRITE IT OUT
         LA    R8,MTTWK808-MTTWRPPT(,R8) POINT TO NEXT DATA
         SPACE 1
         ST    R8,HDRDATA3          SAVE ADDRESS OF HEADER START
         AMDPRFMT PATTERN=HDRFMT3,ERROR=NFMT   FORMAT THE LINE
         AMDPRINT RL=R7             AND WRITE IT OUT
         LA    R8,32(,R8)           DID ONLY 8 WORDS OF THE 16
         SPACE 1
         ST    R8,HDRDATA4          SAVE ADDRESS OF HEADER START
         AMDPRFMT PATTERN=HDRFMT4,ERROR=NFMT   FORMAT THE LINE
         AMDPRINT RL=R7             AND WRITE IT OUT
         LA    R8,32(,R8)           DO LAST 8 WORDS OF THE 16
         SPACE 1
         ST    R8,HDRDATA5          SAVE ADDRESS OF HEADER START
         AMDPRFMT PATTERN=HDRFMT5,ERROR=NFMT   FORMAT THE LINE
         AMDPRINT ,                 AND WRITE IT OUT
         AMDPRINT ,                 SPACE UP 1
         AMDPRINT RL=R7             SPACE UP AND 2
         SPACE 1
         TITLE 'AMDMTRC  FORMAT MASTER TRACE - MAINLINE'
         MVC   ENTADDR(4),HDRDATA1  POINT TO THE MASTER TRACE HDR
         AMDPRFMT PATTERN=FMTENT,ERROR=NENT ACCES MTT ENTRY VIA FORMAT
         L     R8,ADPLBUF           ACCESS THE OUTPUT LINE.       DFG00
         L     R3,MTTCURPT          CURRENT ENTRY ADDRESS
         L     R4,MTTENTPT          START OF TRACE TABLE AREA
         L     R5,MTTENDPT          END OF TRACE TABLE AREA
         L     R6,MTTWRPPT          WRAP ADDRESS
         STM   R3,R6,CURPT          SAVE THESE POINTERS
         SPACE 1
*  PICK UP LENGTH OF TABLE TO CALCULATE AMOUNT TO GETMAIN
         SPACE 1
         L     R10,MTTSIZE          SIZE OF TRACE TABLE IN BYTES
         SLL   R10,8                GET RID OF SUBPOOL NUMBER
         SRDA  R10,40               PUT IN R11 FOR DIVIDE
         D     R10,=AL4(L'MTENTHDR) CALCULATE NUMBER OF ENTRIES
         LA    R11,1(,R11)          ROUND UP
         M     R10,=F'6'            MULT. BY SIZE OF OUR TABLE ENT
         ICM   R11,B'1000',ADPLSBPL GET SUBPOOL FROM PARM LIST    DFG00
         LR    R0,R11               PARMS FOR GETMAIN
         ST    R0,GMPARMS           SAVE PARMS FOR LATER FREEMAIN
         GETMAIN  EC,LV=(0),A=HWTAB GET HWTAB STORAGE
         LTR   R15,R15              GET IT?
         BNZ   NSTOR                NO, TELL AND QUIT
         SPACE 1
         L     R1,PARMADDR          RELOAD PARAMETER LIST PTR
         L     R2,HWTAB             GET THE TABLE ADDRESS
         SPACE 1
         DROP  R8                   PARMOBUF GETS CLOBBERED SHORTLY
BLDTAB   ST    R3,GETADDR           POINT AT ENTRY FOR FORMAT
         AMDPRFMT PATTERN=GETENT,ERROR=NGET USE FORMAT TO ACCESS ENTRY
         L     R10,ADPLBUF          ACCESS THE OUTPUT LINE        DFG00
         USING MTENTRY,R10          RETRIEVED DATA
         LA    R3,L'MTENTHDR(,R3)   BUMP R3 BY LENGTH OF HEADER INFO
         STCM  R3,B'1111',0(R2)     STORE ENT ADR IN TABLE OF ADDRESSES
         LA    R2,4(,R2)            BUMP TABLE POINTER
         SLR   R11,R11              ZERO REG TO HOLD LENGTH
         ICM   R11,B'0011',MTENTLEN GET THE LENGTH FIELD
         STH   R11,0(,R2)           SAVE THE LENGTH FIELD IN TABLE
         LA    R2,2(,R2)            POINT TO NEXT TABLE ENTRY
         LA    R3,0(R11,R3)         BUMP R3 TO NEXT ENTRY
         CR    R3,R5                AT THE END??
         BL    BLDTAB               NO, CONTINUE BUILDING TABLE
         C     R5,CURPT             YES, ARE WE DOING FIRST OR 2ND 1/2
         BE    PRINT                IF EQUAL IT WAS 2ND 1/2..WE'RE DONE
         L     R5,CURPT             ELSE SETUP SECOND HALF
         ICM   R3,B'1111',WRPPT     R3=START..R5=END         DLP820213
         BNZ   BLDTAB               WRAP POINTER MAY BE      DLP820213
*                                    ZERO IF TABLE HAS NOT YET WRAPPED.
*                                    WRAP POINTER IS NOT INITIALIZED
*                                    TO ZERO AS DIANOSTIC TECHNIQUES
*                                    (GC28-0725 SUPPL GD23-0141
*                                    PAGE 2.6.9.1) SUGGESTS. DLP820213
         SPACE 1
PRINT    S     R2,=F'6'             BACK UP TO PREV ENTRY    DLP820215
         SLR   R11,R11              CLEAR R11 FOR LENGTH
         ICM   R11,B'0011',4(R2)    PICK UP LENGTH           DLP820215
         BNZ   CHECKSIZ             IF ZERO, SKIP IT
*DELETED                                                     DLP820215
         MVC   0(120,R7),=CL120'NO DATA'  EMPTY ENTRY        DLP820215
*DELETED                                                     DLP820215
         B     OUTPUTIT             AND PRINT (SKIP FORMATTING)
CHECKSIZ CH    R11,ONE20            ENTRY TOO BIG?
         BNH   NOT2BIG              NO, CONTINUE
         LH    R11,ONE20            FORCE SIZE TO MAX OF 120
         SPACE 1
NOT2BIG  DS    0H                                            DLP820215
         MVC   ENTADDR(4),0(R2)     INDICATE ADDRESS IN PATTERN
         AMDPRFMT PATTERN=FMTENT
*DELETED                                                     DLP820215
         LA    R7,0(R11,R7)         POINT TO END OF GOODDATA DLP820215
         MVI   0(R7),C' '           GET READY TO BLANK IT OUTDLP820215
         SH    R11,ONE20            GET LENGTH OF BAD DATA
         LCR   R11,R11              LENBAD=ABS(LENGOOD-120)-1
         BZ    OUTPUTIT             IN CASE LENGTH WAS 120 EXACTLY
         BCTR  R11,0                DO THE -1 PART OF ABOVE
         EX    R11,MVCINSTR         EXECUTE THE MVC TO BLANK IT OUT.
OUTPUTIT AMDPRINT RL=R7             AND WRITE IT OUT
         C     R2,HWTAB             BACKED UP TO THE BEGINNING?
         BH    PRINT                NO, KEEP PRINTING
         B     EOJ                  YES, END OF JOB
MVCINSTR MVC   1(*-*,R7),0(R7) *****EXECUTED INSTRUCION***** DLP820215
         SPACE 1
         TITLE 'AMDMTRC  FORMAT MASTER TRACE - SMALL ROUTINES'
ENEXEND  MVC   OUTDATA(L'NTABLE),NTABLE SET UP NO TR. TABLE
         B     ERRMSG                AND LEAVE
NSTOR    MVC   OUTDATA(L'NOSTOR),NOSTOR SET UP NO STORAGE
         B     ERRMSG                AND LEAVE
         SPACE 1
NOCVT    MVC   OUTDATA(L'CVTMSG),CVTMSG      SET UP NO CVT
         B     ERRMSG                AND LEAVE
NOSUBITS MVC   OUTDATA(L'NOBITMSG),NOBITMSG  SET UP NO SUBIT STRING
         B     ERRMSG                AND LEAVE
NOSU64   MVC   OUTDATA(L'NOSUMSG),NOSUMSG    SET UP NOT SU64 MSG
         B     ERRMSG                AND LEAVE
NOMSRDA  MVC   OUTDATA(L'NOMSRMSG),NOMSRMSG  SET UP NO MSRDA MSG
         B     ERRMSG                AND LEAVE
NOMTTBL  MVC   OUTDATA(L'NOMTMSG),NOMTMSG  SET UP NO MTTBL MSG
         B     ERRMSG                AND LEAVE
         SPACE 1
NOTACTV  MVC   OUTDATA(L'NOTONMSG),NOTONMSG SET UP NO TABLE
         B     ERRMSG                AND LEAVE
NFMT     MVC   OUTDATA(L'NOFMTMSG),NOFMTMSG SET UP ERROR IN FMT MSG
         B     ERRMSG                AND LEAVE
NENT     MVC   OUTDATA(L'NOENTMSG),NOENTMSG SET UP ERROR IN MTT MSG
         B     ERRMSG                AND LEAVE
NGET     MVC   OUTDATA(L'NOGETMSG),NOGETMSG SET UP ERROR IN BLDTAB MSG
         SPACE 1
ERRMSG   AMDPRINT RL=R7             PRINT THE MESSAGE
         B     ALLDONE              AND EXIT
         SPACE 1
EOJ      L     R1,HWTAB             ADDRESS TO RETURN
         L     R0,GMPARMS           ATTRIBUTES OF AREA
         FREEMAIN R,LV=(0),A=(1)    GIVE BACK STORAGE
         SPACE 1
ALLDONE  L     R13,4(,R13)          RESTORE CALLERS SAVEAREA DLP820213
         LM    R14,R12,12(R13)      RESTORE CALLERS REGS     DLP820213
         SR    R15,R15              NO PROBLEMS EVER
         BR    R14                  AND EXIT
         SPACE 2
PATCHLOC DC    30S(*)               DEFINE PATCH AREA
         TITLE 'AMDMTRC  FORMAT MASTER TRACE - WORKING STORAGE'
CVTMSG   DC    CL120'UNABLE TO ACCESS NUCLEUS RESIDENT CONTROL BLOCKS'
NOBITMSG DC    CL120'UNABLE TO ACCESS SU BIT STRING'
NOSUMSG  DC    CL120'SU64 NOT APPLIED - MASTER TRACE NOT AVAILABLE'
NOMSRMSG DC    CL120'UNABLE TO ACCESS MASTER SCHEDULER RESIDENT DATA ARX
               EA'
NOMTMSG  DC    CL120'UNABLE TO ACCESS MASTER TRACE TABLE'
NOTONMSG DC    CL120'MASTER TRACE NOT ACTIVE'
NOFMTMSG DC    CL120'ERROR RETURN FROM FORMAT ROUTINE'
NOENTMSG DC    CL120'ERROR RETURN FROM FORMAT ACCESSING TRACE TABLE ENTX
               RY'
NOGETMSG DC    CL120'ERROR RETURN FROM FORMAT ACCESSING HEADER IN BLDTAX
               B'
NOSTOR   DC    CL120'UNABLE TO OBTAIN GETMAINED WORK TABLE'
NTABLE   DC    CL120'UNABLE TO ACCESS MASTER TRACE TABLE'
         SPACE 1
TTLMSG   DC    C'SU64 MASTER TRACE  (RECENT CONSOLE MESSAGES)'
HDR      DC    C'TRACE HEADER'
HDRLN1   DC    C'IDCURRENTSTARTENDSPLENGTHWRAP TIME'
HDRLN2   DC    C'WRAP ADDRPFLAGDATA LENGTHRSV'
HDRLN3   DC    C'WORK'
HDRLN5   DC    C'RSV'
         SPACE 1
         DS    0F                  ALIGN
SAVE     DS    18F                 SAVE AREA                 DLP820213
GMPARMS  DC    X'00',AL3(4096)     GETMAIN DATA
HWTAB    DS    AL4                 RETURNED GETMAINED AREA
PARMADDR DS    AL4                  USED TO SAVE PASSED PARMS ADDRESS
CURPT    DS    F                    SAVE CURRENT ENTRY ADDRESS
ENTPT    DS    F                    SAVE START OF TRACE TABLE AREA
ENDPT    DS    F                    SAVE END OF TRACE TABLE AREA
WRPPT    DS    F                    SAVE WRAP ADDRESS
ONE20    DC    AL2(120)             COUNT BYTES/LINE
         TITLE 'AMDMTRC  FORMAT MASTER TRACE - PRINT FORMAT SKELETONS'
         DS    0F
GETENT   DC    X'2C,0A',AL1(0,0)    FAKE FORMAT TO GET TTE LENGTH
GETADDR  DC    A(0)
         DC    F'0'                END OF LIST
FMTENT   DC    X'2C,0F',AL1(0,0)    FAKE FORMAT TO GET ENTIRE TTE
ENTADDR  DC    A(0)
         DC    X'24,0F',AL1(16,16)
         DC    X'24,0F',AL1(32,32)
         DC    X'24,0F',AL1(48,48)
         DC    X'24,0F',AL1(64,64)
         DC    X'24,0F',AL1(80,80)
         DC    X'24,0F',AL1(96,96)
         DC    X'24,09',AL1(112,112)
         DC    F'0'                END OF LIST
         SPACE 2
HDRFMT1  DC    X'0F,13',AL1(7,10)  CONTROL   ID
         DC    A(HDRLN1)           HEADER1 LABELS
HDRDATA1 DC    A(0)                HEADER DATA
         DC    X'05,63',AL1(20,28) CURRENT ADDRESS
         DC    X'05,43',AL1(38,45) STARTING ADDRESS
         DC    X'05,23',AL1(55,59) ENDING ADDRESS
         DC    X'05,10',AL1(69,72) SUBPOOL
         DC    X'05,52',AL1(76,83) LENGTH
         DC    X'25,8B',AL1(91,101) WRAP TIME
         DC    F'0'                END OF LIST
         SPACE 2
HDRFMT2  DC    X'0F,83',AL1(7,17)  CONTROL   WRAP ADDRESS
         DC    A(HDRLN2)           HEADER2 LABELS
HDRDATA2 DC    A(0)                HEADER DATA
         DC    X'05,43',AL1(27,33) PFLAG
         DC    X'05,A3',AL1(43,55) DATA LENGTH
         DC    X'05,23',AL1(65,69) RESERVED
         DC    F'0'                END OF LIST
         SPACE 2
HDRFMT3  DC    X'0F,33',AL1(7,12)  CONTROL   WORK
         DC    A(HDRLN3)           HEADER3 LABEL
HDRDATA3 DC    A(0)                HEADER DATA
         DC    X'04,03',AL1(0,21) DATA
         DC    X'04,03',AL1(0,30) DATA
         DC    X'04,03',AL1(0,39) DATA
         DC    X'04,03',AL1(0,48) DATA
         DC    X'04,03',AL1(0,57) DATA
         DC    X'04,03',AL1(0,66) DATA
         DC    X'04,03',AL1(0,75) DATA
         DC    F'0'                END OF LIST
         SPACE 2
HDRFMT4  DC    X'0C,03',AL1(0,12)  WORK CONTINUED
HDRDATA4 DC    A(0)                HEADER DATA  ADDRESS
         DC    X'04,03',AL1(0,21) DATA
         DC    X'04,03',AL1(0,30) DATA
         DC    X'04,03',AL1(0,39) DATA
         DC    X'04,03',AL1(0,48) DATA
         DC    X'04,03',AL1(0,57) DATA
         DC    X'04,03',AL1(0,66) DATA
         DC    X'04,03',AL1(0,75) DATA
         DC    F'0'                END OF LIST
         SPACE 2
HDRFMT5  DC    X'0F,23',AL1(7,12)  WORK CONTINUED
         DC    A(HDRLN5)           HEADER4 LABEL
HDRDATA5 DC    A(0)                HEADER DATA  ADDRESS
         DC    X'04,03',AL1(0,21) DATA
         DC    X'04,03',AL1(0,30) DATA
         DC    X'04,03',AL1(0,39) DATA
         DC    F'0'                END OF LIST
         SPACE 2
TABST    DS    XL44                BUILDABLE FORMAT AREA
         LTORG
         TITLE 'AMDMTRC  FORMAT MASTER TRACE - DSECTS'
BUFR     DSECT ,                   OUTPUT LINE
OUTDATA  DS    0CL120              OUTPUT LINE
FTLNE    DS    CL60                LEFT HALF OF THE LINE
STLNE    DS    CL60                RIGHT HALF
         EJECT
         IHAABDPL DSECT=YES        PASSED PARAMETER LIST          DFG00
         EJECT
         CVT   DSECT=YES
         EJECT
         IEEBASEA ,
         EJECT
         IEZMTPRM ,
         EJECT
         IEEZB806 ,
         EJECT
         IHASUBIT ,
         SPACE 2
         END
