         TITLE 'WTO - WTOR PROMPTING PROGRAM'
****************************************************************
* THIS PROGRAM WILL ISSUE A WTO OR WTOR DEPENDING UPON THE     *
* FIRST 4 CHARACTERS OF THE INPUT RECORD. WTO = WTO AND        *
* WTOR = WTOR.  THE ROUTCDE = (1 AND 11) AND DESC = 2.         *
*                                                              *
* THE NEXT 70 CHARACTERS ARE PLACED INTO THE TEXT OF THE       *
* WTO-WTOR.                                                    *
*                                                              *
* FOR WTOR'S A REPLY OF C WILL CAUSE THE PROGRAM TO CONTINUE   *
* ANYTHING ELSE WILL LOOP ON THAT WTOR.                        *
*                                                              *
*   THE REQUIRED JCL IS:                                       *
*              ..STEP EXEC PGM=WTOR                            *
*              ..WTOIN DD *                                    *
*                                                              *
*   THE CONTROL CARD FORMAT IS:                                *
*              COLS 1-4 WTO OR WTOR                            *
*              COLS 5-74 MESSAGE TEXT (KEEP IT CLEAN)          *
*                                                              *
****************************************************************
*  WARRENTY:  NONE IS PROVIDED, EXPRESSLY OR BY IMPLICATION    *
*             THIS CODE WORKS IN MY WORLD - GOOD LUCK IN YOURS *
****************************************************************
*  MACROS USED: PRIME,TERME,OPEN,CLOSE,DCB,WTO,WTOR,WAIT,GET   *
****************************************************************
*   VERSION 1 RELEASE 0 MODIFICATION LEVEL 0                   *
*   DATE: 26 MAY 1982                                          *
*   AUTHOR: LIONEL DYCK (213) 594-1125                         *
****************************************************************
         EJECT
WTORP    PRIME 12,,,,Y
         EJECT
*
**  OPEN THE INPUT FILE
*
         OPEN  WTOIN
*
**  AND READ THE CONTROL RECORDS
*
READ     GET   WTOIN
*
**  DETERMINE WHETHER WTO OR WTOR (ASSUME WTO)
*
         CLC   0(4,R1),=C'WTOR'
         BE    WTOR
*
**  WTO ASSUMED - MOVE DATA AND DO OUR THING
*
         MVC   WTOM(70),4(R1)
DOWTO    WTO   '                                                       X
                                ',ROUTCDE=(1,11),DESC=2
WTOM     EQU   DOWTO+8
         B     READ                      GO FOR NEXT CONTROL CARD
*
**  A WTOR - MOVE DATA - ISSUE WTOR AND THEN WAIT WAIT WAIT
*
WTOR     MVC   WTORM1(70),4(R1)
DOWTOR   WTOR  '                                                       X
                                ',REPLY,1,ECB,ROUTCDE=(1,11)
WTORM1   EQU   DOWTOR+18
         WAIT  ECB=ECB                   WAIT FOR REPLY
         XC    ECB,ECB                   CLEAR ECB FOR NEXT TRY
*
**  WE'VE WAITED AND NOW WE MUST CHECK THE REPLY CONTINUE OR LOOP
*
         CLI   REPLY,C'C'                CONTINUE
         BE    READ                      GO GET NEXT RECORD
         B     DOWTOR                    ELSE LOOP ON WTOR
*
**  LET'S CLOSE OUR FILES AND RETURN TO THE SYSTEM FOR THE NEXT STEP
*
EOINPUT  CLOSE WTOIN
         TERME RC=0                      LEAVE WITH A RETURN CODE OF 0
*
**  OUR DATA AREA'S
*
         LTORG
*
ECB      DC    F'0'                      ECB FOR WAIT
*
REPLY    DC    C' '                      REPLY AREA
         EJECT
*
**  OUR DATA CONTROL BLOCK
*
WTOIN    DCB   DSORG=PS,MACRF=GL,DDNAME=WTOIN,LRECL=80,RECFM=FB,       X
               EODAD=EOINPUT
         END
