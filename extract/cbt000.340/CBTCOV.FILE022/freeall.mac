FREEALL  $PROLOG R12
         L     R1,CVTPTR           -> CVT
         USING CVT,R1
         L     R1,CVTTCBP          -> TCB WORDS
         L     R1,4(R1)            -> MY TCB
         USING TCB,R1
         L     R1,TCBJSCB          -> JSCB
         USING IEZJSCB,R1
         L     R1,JSCDSABQ        -> QDB
         USING QDB,R1
         L     R6,QDBFELMP         -> DSAB Q
         USING DSAB,R6
LOOP     DS    0H
         L     R7,DSABRS01         -> SIOT
         LA    R7,16(R7)           -> PREFIX
         USING INDMSIOT,R7
         MVC   DDNAME,SCTDDNAM
         CLI   DDNAME,C' '             NULL DDNAME
         BE    INCR                    MEMBER OF CONCAT
         CLI   DSABFLG1,DSABPALC       PERM ALLOC
         BE    NOTFR                   DO NOT FREE
         TM    DSABFLG1,DSABDCAT       CONCAT ?
         BO    NOTFR
         TM    DSABFLG1,DSABCATM       CONCAT ?
         BO    NOTFR
         SPACE 1
         TM    SIOTTSTC,SIOTTERM       TERMINAL ?
         BZ    NOTERM                  YES - SKIP IT
         CLC   DDNAME,=CL8'SYSIN'
         BE    NOTFR
         CLC   DDNAME,=CL8'SYSPRINT'
         BE    NOTFR
         B     DOIT
NOTERM   DS    0H
         CLC   =C'ISP',DDNAME          ISPF FILE ?
         BE    NOTFR                   YES - SKIP IT
         SPACE 1
         LA    R1,RESTABLE             -> RESERVED DD'S
RLOOP    DS    0H
         CLC   DDNAME,0(R1)
         BE    NOTFR
         LA    R1,8(R1)
         CLI   0(R1),X'FF'             END OF TABLE
         BNE   RLOOP
RLEND    DS    0H
DOIT     DS    0H
         L     R6,DSABFCHN
         FREE  PERM,DDN=DDN,ERROR=S99FAIL
*        MVC   STATUS,=CL11'FREED'
*        TPT   MSG
         LTR   R6,R6
         BNZ   LOOP
END      $EPILOG 0
S99FAIL  DS    0H
*        S99FAIL
*        MVC   STATUS,=CL11'NOT FREED'
*        TPT   MSG
         LTR   R6,R6
         BNZ   LOOP
         B     END
NOTFR    DS    0H
*        MVC   STATUS,=CL11'NOT FREED'
*        TPT   MSG
INCR     L     R6,DSABFCHN
         LTR   R6,R6
         BNZ   LOOP
         B     END
         LTORG
DDN      DC    A(DDNAME),Y(8)
         SPACE 2
MSG      DC    CL40' '
         ORG   MSG
DDNAME   DC    CL8' '
         ORG   MSG+10
STATUS   DC    CL11' '
         ORG   ,
         DYNSPACE
         EJECT
RESTABLE DS    0D
         DC    CL8'SYSPROC'            CLIST LIBRARY
         DC    CL8'SYSHELP'            HELP LIBRARY
         DC    CL8'TASKLIB$'           $ LOAD LIB
         DC    CL8'TASKLIB@'           @ LOAD LIB
         DC    CL8'SMPTLIB'            SMP/E ISPF TLIB      *LBD 09/85*
         DC    CL8'SMPTABL'            SMP/E ISPF TABLE     *LBD 09/85*
         DC    CL8'SUBCHKDD'           JCLCHECK SUBCHK DD   *LBD 09/85*
         DC    CL8'EDCHKDD'            JCLCHECK EDCHEK DD   *LBD 09/85*
         DC    CL8'IPCMLIB'                                 *LBD 06/86*
         DC    CL8'IPCPLIB'                                 *LBD 06/86*
         DC    CL8'IPCTLIB'                                 *LBD 06/86*
         DC    CL8'ISRCFIL'                                 *LBD 01/87*
         DC    X'FF'                   END OF TABLE
         EJECT
         PRINT NOGEN
         CVT   DSECT=YES
         DSECT
         IKJTCB
         DSECT
         IEZJSCB
         DSECT
         IHADSAB
         DSECT
         IHAQDB
         DSECT
         IEFASIOT
         IEFZB4D0
         IEFZB4D2
         END
