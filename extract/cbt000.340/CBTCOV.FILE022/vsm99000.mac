VSM99000 CSECT
         COPY VSMMACS                                          *LBD*
         TITLE 'VSM99000 => TERMINAL I/O MODULE'
***********************************************************************
*                                                                     *
*    SYSTEM -           VIRTUAL STORAGE MONITOR                       *
*                                                                     *
*    MODNAME -          VSMHELP                                       *
*                                                                     *
*    VERSION -          1.1                                           *
*                                                                     *
*    AUTHOR -           R.P. SHANNON                                  *
*                       HALLMARK CARDS                                *
*                       ENFIELD DISTRIBUTION CENTER                   *
*                                                                     *
*    DATE -             9/1/82                                        *
*                                                                     *
*    FUNCTION -         THIS MODULE DOES ALL THE TERMINAL I/O FOR     *
*                       VSM. THE SCREEN ADDRESSES AND LENGTHS ARE     *
*                       PASSED TO VSM99000 VIA THE LINKAGE AREA.      *
*                                                                     *
*    ATTRIBUTES -       REUS                                          *
*                                                                     *
*    REGS AT ENTRY -    R1   - INTER MODULE LINKAGE                   *
*                       R14  - RETURN ADDRESS                         *
*                       R15  - EPA                                    *
*                                                                     *
*    REG USAGE     -    R0   - SCREEN LENGTH FOR I/O                  *
*                       R1   - SCREEN ADDR FOR I/O                    *
*                       R2   - TGET/TPUT FLAGS                        *
*                       R10  - LINKAGE AREA POINTER                   *
*                       R11  - BASE REG                               *
*                       R13  - SAVEAREA POINTER                       *
*                                                                     *
*    REGS AT EXIT -     R14  - RETURN ADDRESS                         *
*                       R15  - RETURN CODE                            *
*                                                                     *
*    LABELS -           SOME OF THE FIELD USAGE IN THIS MODULE CAN    *
*                       BE DETERMINED BY THE FIRST CHARATTER OF THE   *
*                       FIELD. THE KEYS ARE:                          *
*                            @ - FIELD IS IN INTER MODULE LINKAGE     *
*                                AREA                                 *
*                            # - FIELD IS PART OF THE OUTPUT SCREEN   *
*                            $ - DSECT                                *
*                                                                     *
*    MACROS -           $LNKSECT   => INTER MODULE LINKAGE DSECT      *
*                       TGET       => TERMINAL READ                   *
*                       TPUT       => TERMINAL WRITE                  *
*                                                                     *
*    MODS -                                                           *
*          VERSION 1.0  - BASE                                        *
*                                                                     *
*          VERSION 1.1  - UNCHANGED                                   *
*                                                                     *
***********************************************************************
         EJECT
         EJECT
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU   10                       LINKAGE DSECT REGISTER
R11      EQU   11                       BASE REGISTER
R12      EQU   12
R13      EQU   13                       SAVEAREA POINTER
R14      EQU   14                       LINKAGE REGISTER
R15      EQU   15                       RETURN CODES
         EJECT
***********************************************************************
*        STANDARD OS LINKAGE                                          *
***********************************************************************
         SPACE
         PRINT NOGEN
         SPACE
         SAVE  (14,12),,VSM99000+&SYSDATE&SYSTIME   SAVE CALLER'S REGS
         LR    R11,R15                  LOAD BASE REG
         USING VSM99000,R11             CSECT ADDRESSABILITY
         LA    R2,SAVEAREA              CREATE SAVEAREA CHAIN
         ST    R2,8(R13)                FORWARD CHAIN POINTER
         ST    R13,SAVEAREA+4           BACKWARD CHAIN POINTER
         LR    R13,R2                   POINT TO MY SAVEAREA
         SPACE
P000100  DS    0H
         LR    R10,R1                   POINT TO LINKAGE
         USING $LNKSECT,R10             DSECT ADDRESSABILITY
         MVI   @RSPONSE,X'40'           CLEAR RESPONSE
         SPACE
P000200  DS    0H
         L     R0,@PUTLEN               LOAD SCREEN LENGTH INTO R0
         L     R1,@PUTAREA              POINT TO SCREEN
         LA    R2,PUTFLAGS              MOVE PUT FLAGS TO R2
         SLL   R2,24                    SHIFT FLAGS TO HIGH ORDER BYTE
         OR    R1,R2                    PUT FLAGS IN R1 HI-ORDER BYTE
         TPUT  (1),(0),R                PUT SCREEN
         SPACE
         LA    R0,@GETLEN               INPUT BUFFER LENGTH IN R0
         LA    R1,@GETAREA              INPUT BUFFER ADDR IN R1
         LA    R2,GETFLAGS              MOVE FLAGS TO R2
         SLL   R2,24                    SHIFT FLAGS TO HIGH ORDER BYTE
         OR    R1,R2                    PUT FLAGS IN R1 HI-ORDER BYTE
         TGET  (1),(0),R                READ SCREEN - WAIT FOR RESPONSE
         TR    @RSPONSE,TRTABLE         TRANSLATE TO UPPER CASE
         SPACE
         DS    0H
         L     R13,SAVEAREA+4           POINT TO CALLER'S SAVEAREA
         RETURN (14,12)                 BOOGIE
         EJECT
SAVEAREA DC    18F'0'                   MY SAVEAREA
GETFLAGS EQU   B'10100001'              BIT SETTINGS FOR GET
PUTFLAGS EQU   B'00100011'              BIT SETTINGS FOR PUT
TRTABLE  DS    0CL256                   UPPER CASE TRANSLATE TABLE
         DC    X'000102030405060708090A0B0C0D0E0F'
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'
         DC    X'303132333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'
         DC    X'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'
         DC    X'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         LTORG
         $LNKSECT                       LINKAGE SECTION DSECT
         END
