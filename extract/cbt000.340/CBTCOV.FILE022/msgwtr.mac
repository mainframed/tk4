MSGW     TITLE 'MESSAGE WRITER ROUTINE'
         SPACE
*        THIS ROUTINE WILL PRINT A VARIABLE LENGTH ASA MESSAGE ON
*        A SYSPRINT DATA SET WITH SEQUENTIAL PAGE NUMBERING.
*
*        IF THE MESSAGE WILL NOT FIT ON THE EXISTING PAGE IT WILL
*        BE PRINTED ON THE NEXT PAGE.  UP TO 3 TITLES WILL
*        BE INCLUDED IN THE HEADING IF SPECIFIED.
*
*        IF SYSPRINT CANNOT BE OPENED RETURN IS GIVEN TO
*        THE EXTERNAL ENTRY POINT DDRTN.  IF DDRTN IS NOT
*        DEFINED THE TASK WILL BE ABENED WITH A USER CODE OF 16.
*
*        BLKSIZE WILL BE SET TO 700 IF NOT DEFINED OR TO 129
*        IF SPECIFIED 1 THRU 128.
*
*        IF BUFNO IS NOT DEFINED, THE VALUE WILL BE SET TO 1 IF
*        BLKSIZE > 2800 OR MAXIMUM POSSIBLE WITHIN 2800 BYTES.
*
*        LINKAGE
*
*              LA   1,MESSAGE  OR  CALL MSGWTR,MF=(E,MESSAGE)
*              L    15,=V(MSGWTR)
*              BALR 14,15
*
*        REG 15 MUST CONTAIN ENTRY POINT ADDRESS
*        REG 1 MUST POINT TO A VARIABLE LENGTH MESSAGE
*
*        MESSAGE LIMITS
*
*        THE 1ST TITLE MUST NOT EXCEED 109 CHARACTERS.
*        THE REMAINING TITLES AND MESSAGES SHOULD NOT EXCEED 120 CHAR.
*        THE 1ST CHARACTER MUST BE A VALID ASA CONTROL CHARACTER
*        THE ONLY PERMISSABLE CHARACTERS ARE 1, +, BLANK, 0, & -
*
*
*        EXTERNAL ROUTINES
*
*        DDRTN                                   OPEN ERROR RETURN
*        QSAM PUT MOVE VARIABLE LENGTH           OUTPUT
*
*                  ENTRY POINTS AND THEIR USES
*
*        SYMBOL              USE                               DEFAULT
*
*        TITLE1    ADDRESS OF 1ST TITLE PRINTED IN HEADING       (0)
*        TITLE2    ADDRESS OF 2ND TITLE PRINTED IN HEADING       (0)
*        TITLE3    ADDRESS OF 3RD TITLE PRINTED IN HEADING       (0)
*        TITLE     SAME AS TITLE1
*        SUBTITLE  SAME AS TITLE2
*        PAGE      CURRENT PAGE NUMBER                           (0)
*        LINECNT   MAXIMUM NUMBER OF LINES PER PAGE             (42)
*        LINE      NUMBER OF LINES ON CURRENT PAGE               (0)
*        SYSPRINT  SYSPRINT DCB (MAY BE USED TO CLOSE DCB)
*
         SPACE
MSGWTR   CSECT
         ENTRY TITLE1,TITLE2,TITLE3
         ENTRY TITLE,SUBTITLE,PAGE,LINECNT,LINE,SYSPRINT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4                   MESSAGE CONTROL CHARACTER SAVE REG
R5       EQU   5                   TITLE CONTROL CHARACTER SAVE REG
R6       EQU   6                   SUBTITLE CONTROL CHARACTER SAVE REG
R7       EQU   7                   MESSAGE BASE REG
R8       EQU   8                   TITLE BASE REG
R9       EQU   9                   SUBTITLE BASE REG
R10      EQU   10                  PAGE NUMBER REG
R11      EQU   11                  LINE NUMBER REG
R12      EQU   12                  PROGRAM BASE REG
R13      EQU   13                  SAVEAREA POINTER REG
R14      EQU   14                  RETURN REG
R15      EQU   15
BLANK    EQU   C' '
         USING *,12
         USING MESSAGE,R7
         SPACE
****    INITIALIZE ROUTINE    ****
         SPACE
         SAVE  (14,12),,*
         LR    R12,R15             LOAD PROGRAM BASE REG
         ST    13,SAVEAREA+4       STORE BACKWARD POINTER
         LA    2,SAVEAREA
         ST    2,8(13)             STORE FOREWARD POINTER
         LR    13,2                LOAD SAVEAREA POINTER
         LR    R7,R1               LOAD MESSAGE BASE
         IC    R4,CC               SAVE MESSAGE CONTROL CHARACTER
         SPACE
****    OPEN SYSPRINT IF NOT OPEN    ****
         SPACE
         TM    SYSPRINT+48,X'10'   IS SYSPRINT OPEN
         BO    ITSOPEN
         OPEN  (SYSPRINT,OUTPUT)
         TM    SYSPRINT+48,X'10'   WAS SYSPRINT SUCCESSFULLY OPENED
         BZ    NOPRINT
         B     NEWPAGE
         SPACE
****    ANALYZE MESSAGE CONTROL CHARACTER    ****
         SPACE
ITSOPEN  EQU   *
         CLI   CC,C' '             CHECK CONTROL CHARACTER
         BE    SINGLE
         CLI   CC,C'0'             CHECK CONTROL CHARACTER
         BE    DOUBLE
         CLI   CC,C'-'             CHECK CONTROL CHARACTER
         BE    TRIPLE
         CLI   CC,C'1'             CHECK CONTROL CHARACTER
         BE    NEWPAGE
         CLI   CC,C'+'             CHECK CONTROL CHARACTER
         BE    PRNTMSG
         MVI   CC,C' '             MAKE INVALID CONTROL CHARACTER BLANK
         SPACE
SINGLE   LA    R11,1               LOAD NUMBER OF LINES FOR MESSAGE
         B     LINECHK
         SPACE
DOUBLE   LA    R11,2               LOAD NU4BER OF LINES FOR MESSAGE
         B     LINECHK
         SPACE
TRIPLE   LA    R11,3               LOAD NUMBER OF LINES FOR MESSAGE
         SPACE
LINECHK  EQU   *
         L     R14,LINE            LOAD NUMBER OF LINES USED ON PAGE
         AR    R14,R11             ADD NUMBER OF LINES FOR MESSAGE
         C     R14,LINECNT         WILL LINE FIT ON THIS PAGE
         BH    NEWPAGE             IF NOT START ON A NEW PAGE
         LR    R11,R14             UPDATE LINE NUMBER
         B     PRNTMSG
         SPACE
****    PRINT HEADING FOR NEW PAGE    ****
         SPACE
NEWPAGE  EQU   *
         MVI   CC,C' '             SET CC TO SINGLE SPACE
         LA    R11,3               LOAD LINES FOR HEADING WITHOUT SUBT
         LM    R8,R10,TITLE1       LOAD TITLE POINTERS
         SPACE
*        INITIALIZE HEADING
         SPACE
         MVI   HEADTEXT,BLANK      BLANK 1ST BYTE OF HEADING TEXT
         MVC   HEADTEXT+1(108),HEADTEXT  RIPPLE REMAINDER OF FIELD
         LTR   R8,R8               IS 1ST TITLE PRESENT
         BZ    NOTITLE
         USING MESSAGE,R8
         SR    R1,R1               ZERO LENGTH REG
         IC    R1,LNGH+1           LOAD MSG LENGTH
         SH    R1,MSGCON           DECREMENT OVERHEAD BYTES
         BNP   NOTITLE             IS MESSAGE VALID
         CH    R1,MSGMAX           IS MSG TOO LONG
         BNH   *+8                 IF NOT SKIP NEXT ISN
         LH    R1,MSGMAX           TRUNCATE HEADING TO MAX LNGH
         EX    R1,HEADMOVE         MOVE 1ST TITLE TO HEADING
         DROP  R8
NOTITLE  EQU   *
         SPACE
*        PRINT HEADING WITH PAGE NUMBERS
         SPACE
         L     R10,PAGE            LOAD LAST PAGE NUMBER
         LA    R10,1(R10)          BUMP PAGE NUMBER
         ST    R10,PAGE            UPDATE PAGE NUMBER
         CVD   R10,WORK
         MVC   XPAGE,EDMASK        MOVE EDIT MASK TO PRINT LINE
         ED    XPAGE,WORK+5        EDIT PAGE NUMBER INTO PRINT LINE
         PUT   SYSPRINT,HEADING
         SPACE
****    PRINT SUBTITLE IF SPECIFIED    ****
         SPACE
         SR    R8,R8               ZERO REG FOR FLAG
SUBCHK   EQU   *
         LTR   R0,R9               IS SUBTITLE PRESENT
         BZ    NOSUBT
         USING MESSAGE,R9
         IC    R6,CC               SAVE SUBTITLE CONTROL CHARACTER
         CLI   CC,C'0'             CHECK CONTROL CHARACTER
         BE    SUB2
         CLI   CC,C'-'             CHECK CONTROL CHARACTER
         BE    SUB3
         MVI   CC,C' '             OVERLAY INVALID CONTROL CHARACTER
         B     SUB1                GO PRINT SINGLE SPACE
         SPACE
SUB3     LA    R11,1(R11)          INCREMENT LINE NUMBER FOR TITLE
SUB2     LA    R11,1(R11)          INCREMENT LINE NUMBER FOR TITLE
SUB1     LA    R11,1(R11)          INCREMENT LINE NUMBER FOR TITLE
         SPACE
SUBPRNT  EQU   *
         PUT   SYSPRINT,(0)        WRITE TITLE
         STC   R6,CC               RESTORE SUBTITLE CONTROL CHARACTER
         DROP  R9
NOSUBT   EQU   *
         LTR   R8,R8               IS THIS 1ST SUBTITLE
         BNZ   PRNTBLNK            IF NOT GO PRINT BLANK LINE
         L     R9,TITLE3           LOAD 2ND SUBTITLE POINTER
         BCT   R8,SUBCHK           SET FLAG REG NEG & CHECK 2ND SUB
         SPACE
****    PRINT 1 BLANK LINE    ****
         SPACE
PRNTBLNK EQU   *
         PUT   SYSPRINT,BLNKLINE   WRITE 1 BLANKLINE
         SPACE
****    PRINT MESSAGE LINE    ****
         SPACE
PRNTMSG  EQU   *
         PUT   SYSPRINT,(7)        PRINT MESSAGE
         CLI   CC,C'+'             WAS PRINTED CONTROL CHAR +
         BE    RETURN              IF SO RETURN
         STC   R4,CC               RESTORE MESSAGE CONTROL CHARACTER
         ST    R11,LINE            STORE CURRENT LINE NUMBER
         SPACE
****    NORMAL RETURN    ****
         SPACE
RETURN   EQU   *
         L     13,SAVEAREA+4       LOAD BACKWARD POINTER
         RETURN (14,12),RC=0
         SPACE
*        USE OPTIONAL RETURN ADDR IF SPECIFIED
         SPACE
NOPRINT  EQU   *
         L     R14,DDRTNPTR        LOAD ABNORMAL RETURN ADDRESS
         LTR   R14,R14             IS IT DEFINED
         BZ    ABEND               IF NOT ABEND TASK
         L     R13,SAVEAREA+4
         RETURN  (2,12),RC=16
ABEND    ABEND 20,DUMP             TERMINATE CURRENT TASK
         SPACE
****    DCB EXIT ROUTINE TO PROVIDE DEFAULT BLKSIZE    ****
         SPACE
DCBEXIT  EQU   *
         USING IHADCB,R1
         LH    R4,DCBBLKSI         LOAD BLKSIZE
         LTR   R4,R4               IS IT DEFINED
         BZ    DEFAULT             IF NOT USE DEFAULT
         LA    R3,129              LOAD MIN BLKSIZE
         CR    R4,R3               IS BLKSIZE VALID
         BNL   CHKBUFNO            IF YES CHECK BUFNO
         LR    R4,R3               USE MIN BLKSIZE
         B     CHKBUFNO            GO CHECK BUFNO
DEFAULT  EQU   *
         LA    R4,700              LOAD DEFAULT BLKSIZE
CHKBUFNO EQU   *
         STH   R4,DCBBLKSI         SAVE BLKSIZE
         CLI   DCBBUFNO,0          IS BUFNO DEFINED
         BNZ   EXITRET             IF YES RETURN TO OPEN
         SR    R2,R2               CLEAR REG FOR DIVISION
         LA    R3,2800             LOAD 4 * DEFAULT BLKSIZE
         DR    R2,R4               FIND NUMBER OF BUFFERS
         LTR   R3,R3               IS QUOTIENT ZERO
         BP    *+8                 IF NOT SKIP NEXT ISN
         LA    R3,1                SET BUFNO TO 1
         LA    R4,255              LOAD MAX BUFNO
         CR    R3,R4               IS BUFNO EXCESSIVE
         BNH   *+6                 IF NOT SKIP NEXT ISN
         LR    R3,R4               SET BUFNO TO 255
         STC   R3,DCBBUFNO         PUT BUFNO INTO DCB
EXITRET  EQU   *
         SR    R15,R15             CLEAR REG
         BR    R14                 RETURN TO OPEN
         EJECT
         USING MESSAGE,R8
HEADMOVE MVC   HEADTEXT(0),MSG     MOVE TITLE TO HEADING
         DROP  R8
SAVEAREA DS    18F
WORK     DS    D
TITLE1   DC    A(0)                ADDR OF MAIN TITLE
TITLE2   DC    A(0)                ADDR OF 1ST SUBTITLE
TITLE3   DC    A(0)                ADDR OF 2ND SUBTITLE
TITLE    EQU   TITLE1
SUBTITLE EQU   TITLE2
PAGE     DC    A(0)                NUMBER OF PAGES WRITTEN
LINECNT  DC    A(42)               LINE COUNT LIMIT FOR PAGE
LINE     DC    A(0)                NUMBER OF LINES USED ON PAGE
         WXTRN DDRTN
DDRTNPTR DC    A(DDRTN)            ADDRESS OF ABNORMAL RETURN
EXITLIST DC    X'85',AL3(DCBEXIT)
         LTORG
MSGCON   DC    H'6'                VA MSG OVERHEAD COUNT
MSGMAX   DC    H'108'              MAX HEADING LENGTH
EDMASK   DC    X'402020202120'     5 DIGIT EDIT MASK
         EJECT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=VBA,LRECL=125,  C
               EXLST=EXITLIST
         EJECT
****    PROGRAM MESSAGES    ****
         SPACE
HEADING  DC    AL2(HEADEND-*,0),C'1'
HEADTEXT DC    CL109' '
         DC    C' PAGE'
XPAGE    DS    CL6                 PAGE NUMBER FIELD
HEADEND  EQU   *
         SPACE
BLNKLINE DC    AL2(BLNKEND-*),AL2(0)
         DC    8C' '
BLNKEND  EQU   *
         SPACE
MESSAGE  DSECT
LNGH     DS    CL2                 LENGTH OF MESSAGE
         DC    AL2(0)
CC       DS    C                   CONTROL CHARACTER
MSG      DS    CL120               MAXIMUM OF 120 CHARACTERS
         EJECT
         DCBD  DSORG=QS
         END
