SPFVINIT $PROLOG R12
         EJECT
*--------------------------------------------------------------------*
* NAME:        SPFVINIT                                              *
*                                                                    *
* FUNCTION:    TO INITIALIZE SELECTED ISPF VARIABLES IN THE SHARED   *
*              VARIABLE POOL THE FIRST TIME THAT THE POOL IS BUILD   *
*              BY AN ISPF PANEL.                                     *
*                                                                    *
*              THEN UPDATE THE SHARED AND PROFILE POOLS.             *
*                                                                    *
*                                                                    *
* USE:         IN AN ISPF PANEL  &ZSEL = 'PGM(SPFVINIT) PARM(XX)'    *
*                                                                    *
*              WHERE PARM IS OPTIONAL AND IS A COMMAND TO BE         *
*              SELECTED.                                             *
*                                                                    *
* DEPENDENCIES: THIS PROGRAM INVOKES THE SHOWSPF PROGRAM WHICH WILL  *
*              EXTRACT THE SELECTED INFORMATION AND ESTABLISH THE    *
*              ISPF VARIABLES.                                       *
*                                                                    *
* ASSUMPTIONS: THE CPU ID IS A SINGLE CHARACTER AND IS THE SECOND    *
*              CHARACTER OF THE SMFID.                               *
*                                                                    *
*--------------------------------------------------------------------*
         EJECT                                             *LBD 07/86*
         L     1,0(1)              * LOAD ADDR OF PARM     *LBD 07/86*
         LH    2,0(1)              * LOAD LENGTH OF PARM   *LBD 07/86*
         LTR   2,2                 * LENGTH OF ZERO        *LBD 07/86*
         BZ    START               * NO - BYPASS MOVE      *LBD 07/86*
         ST    R2,CMDLEN           * SAVE COMMAND LENGTH   *LBD 07/86*
         BCTR  2,0                 * SUBTRACT LENGTH BY 1  *LBD 07/86*
         EX    2,MOVEPARM          * EXECUTE THE MOVE      *LBD 07/86*
         B     START                                       *LBD 07/86*
MOVEPARM MVC   PARM(0),2(1)        *  EXECUTED INSTRUCTION *LBD 07/86*
START    EQU   *                                           *LBD 07/86*
         EJECT
         ISPF  LOAD
         VDEFINE '(Z$SAVE)',Z$SAVE,CHAR,80,'(COPY)'
         VDEFINE '(Z$NAME)',Z$NAME,CHAR,20
         VDEFINE '(Z$PHONE)',Z$PHONE,CHAR,12
         VDEFINE '(Z$DEPT Z$GROUP)',Z$DEPT,CHAR,3
         VDEFINE '(Z$LOC Z$CENTER Z$CTR $RCENTER)',Z$LOC,CHAR,4
         VDEFINE '(Z$MAIL)',Z$MAIL,CHAR,8
         VDEFINE '(Z$CPU)',Z$CPU,CHAR,1
         VDEFINE '(Z$MVS)',Z$MVS,CHAR,3                    *LBD 07/86*
         VDEFINE '(SYSID)',SYSID,CHAR,4                    *LBD 07/86*
         VDEFINE '(Z$CPUOS Z$SYS)',Z$CPUOS,CHAR,6          *LBD 07/86*
         EJECT                                             *LBD 06/86*
         SELECT ,'PGM(SHOWSPF) PARM(NAME,Z$NAME)'
         SELECT ,'PGM(SHOWSPF) PARM(PHONE,Z$PHONE)'
         SELECT ,'PGM(SHOWSPF) PARM(DEPT,Z$DEPT)'
         SELECT ,'PGM(SHOWSPF) PARM(GROUP,Z$GROUP)'
         SELECT ,'PGM(SHOWSPF) PARM(LOC,Z$LOC)'
         SELECT ,'PGM(SHOWSPF) PARM(MAIL,Z$MAIL)'
         SELECT ,'PGM(SHOWSPF) PARM(CENTER,Z$CENTER)'
         SELECT ,'PGM(SHOWSPF) PARM(SYSID,SYSID)'
         EJECT
         VGET  '(Z$LOC Z$CENTER Z$NAME Z$PHONE Z$DEPT Z$GROUP Z$MAIL)'
         SPACE 2                                           *LBD 07/86*
         L     R1,CVTPTR           -> CVT                  *LBD 07/86*
         USING CVT,R1                                      *LBD 07/86*
         TM    CVTDCB,CVTMVSE      XA MODE ??              *LBD 07/86*
         BO    SETXA                                       *LBD 07/86*
         MVC   Z$MVS(3),=CL3'370'                          *LBD 07/86*
         B     SETOK                                       *LBD 07/86*
SETXA    MVC   Z$MVS(3),=CL3'XA'                           *LBD 07/86*
SETOK    DS    0H                                          *LBD 07/86*
         SPACE 1                                           *LBD 07/86*
         MVC   Z$CTR,Z$CENTER                              *LBD 07/86*
         MVC   $RCENTER,Z$CENTER                           *LBD 07/86*
         VPUT  '(Z$LOC Z$CENTER Z$NAME Z$PHONE Z$DEPT Z$GROUP Z$MAIL   X
               Z$CTR $RCENTER)',PROFILE
         SPACE 2                                           *LBD 07/86*
         VGET  '(SYSID)'
         MVC   Z$CPU,SYSID+1                               *LBD 07/86*
         MVC   Z$SYS(4),Z$CTR                              *LBD 07/86*
         MVC   Z$SYS+5(1),Z$CPU                            *LBD 07/86*
         MVC   Z$CPUOS(1),Z$CPU                            *LBD 07/86*
         MVC   Z$CPUOS+2(3),Z$MVS                          *LBD 07/86*
         VPUT  '(Z$CPU Z$MVS Z$SYS Z$CPUOS)',SHARED
         SPACE 2                                           *LBD 07/86*
         CLC   CMDLEN,=A(0)        ANY PARM ???            *LBD 07/86*
         BE    LEAVE                                       *LBD 07/86*
         SELECT CMDLEN,PARM                                *LBD 07/86*
LEAVE    DS    0H                                          *LBD 07/86*
         OC    Z$SAVE,BLANKS       INSURE UPPER CASE
         CLC   Z$SAVE,BLANKS                               *LBD 07/86*
         BE    LEAVE2                                      *LBD 07/86*
         CONTROL NONDISPL,ENTER                            *LBD 07/86*
LEAVE2   DS    0H                                          *LBD 07/86*
         VRESET ,
         ISPF  DELETE
         $EPILOG 0
         LTORG ,                                           *LBD 07/86*
         ISPF  PARMS
CMDLEN   DC    F'00'                                       *LBD 07/86*
PARM     DC    CL100' '                                    *LBD 07/86*
TIME     DS    F                                           *LBD 08/85*
Z$LOC    DC    CL4' '
Z$CENTER DC    CL4' '
Z$CTR    DC    CL4' '                                      *LBD 07/86*
$RCENTER DC    CL4' '                                      *LBD 02/87*
Z$NAME   DC    CL20' '
Z$PHONE  DC    CL12' '
Z$DEPT   DC    CL3' '
Z$GROUP  DC    CL3' '
Z$MAIL   DC    CL8' '
BLANKS   DC    CL80' '                                     *LBD 06/86*
Z$SAVE   DC    CL80' '                                     *LBD 06/86*
Z$MVS    DC    CL3' '
Z$CPU    DC    CL1' '                                      *LBD 06/86*
SYSID    DC    CL4' '                                      *LBD 07/86*
Z$CPUOS  DC    CL6'X/MVS'                                  *LBD 07/86*
Z$SYS    DC    CL6'CNTR/X'                                 *LBD 07/86*
         EJECT                                             *LBD 07/86*
         CVT   DSECT=YES                                   *LBD 07/86*
         END
