***      NOTE: THIS PROGRAM WILL NOT ASSEMBLE OR WORK IN AN MVS/370
***      SYSTEM.
         SPACE 1
         MACRO
&L       KFILL  &A,&B,&C
.*       &A = TO FIELD
.*       &B = LENGTH OF OUTPUT
.*       &C = FROM FULLWORD FIELD
         LCLA   &LEN
         LCLC   &R
         AIF    ('&C'(1,1) NE '(').A
&LEN     SETA   K'&C-2
&R       SETC   '&C'(2,&LEN)
&L       LR     R1,&R
         AGO    .B
.A       ANOP
&L       L      R1,&C
.B       SRL    R1,10
         CVD    R1,DWK
         MVC    FORMAT,EDITK
         ED     FORMAT+1(6),DWK+5
         AIF    ('&A'(1,1) NE '(').C
&LEN     SETA   K'&A-2
&R       SETC   '&A'(2,&LEN)
         MVC    0(&B,&R),FORMAT+8-&B
         AGO    .D
.C       MVC    &A.(&B),FORMAT+8-&B
.D       ANOP
         SPACE
         MEND
STORUSED $PROLOG
STORUSED AMODE 31                                          *LBD 02/86*
STORUSED RMODE 24                                          *LBD 02/86*
         L     R1,CVTPTR                  -> CVT
         USING CVT,R1
         L     R3,CVTTCBP
         L     R2,CVTGDA
         USING GDA,R2
         L     R8,PASTRT                  SAVE BOTTOM OF PVT
         KFILL PVTSIZE,6,PASIZE
         L     R9,PASIZE
         A     R9,PASTRT                  GET TOP OF PVT
         DROP  R1
         L     R4,12(R3)                  -> CURRENT ASCB (MINE)
         USING ASCB,R4                    SET UP USING
         L     R1,ASCBJBNS
         MVC   JOBNAME,0(R1)              MOVE MY JOBNAME
         L     R5,ASCBLDA                 -> LDA
         USING LDA,R5                     SET UP USING
         KFILL REQSIZE,6,LDAREGRQ         REQUESTED REGION SIZE
         L     R7,LDACRGTP        CURRENT HIGH ADDR OF PVT
         SR    R7,R8
         ST    R7,FWD
         KFILL CURSIZE,6,FWD              CURRENT REGION USED
         KFILL LIMIT,6,LDALIMIT           LIMIT FROM IEALIMIT
         KFILL LSQAB,6,LDASIZS            SYSTEM REGION SIZE
TPUT     DS    0H
         TPUT  TPL1,TPL1L                                  *LBD 02/86*
         TPUT  TPL2,TPL2L                                  *LBD 02/86*
         TPUT  TPL3,TPL3L                                  *LBD 02/86*
         TPUT  TPL4,TPL4L                                  *LBD 02/86*
         TPUT  TPL5,TPL5L                                  *LBD 02/86*
         TPUT  TPL6,TPL6L                                  *LBD 02/86*
         $EPILOG
         LTORG
EDITK    DC    X'40402020202120D2'
DWK      DS    D          DOUBLE WORD WORK AREA FOR CVD
FORMAT   DS    D          FORMAT WORK AREA
PUTFLAGS EQU   B'00100011'
FWD      DS    F
         SPACE 2
TPL1     DC    C'CURRENT REGION STATISTICS FOR JOB '
JOBNAME  DC    CL8' '
TPL1L    EQU   *-TPL1                                      *LBD 02/86*
         SPACE 1
TPL2     DC C'REQUESTED REGION SIZE: '
REQSIZE  DC    CL6' '
TPL2L    EQU   *-TPL2                                      *LBD 02/86*
         SPACE 2
TPL3     DC C'CURRENT REGION TOP:    '
CURSIZE  DC    CL6' '
TPL3L    EQU   *-TPL3                                      *LBD 02/86*
         SPACE 2
TPL4     DC C'IEALIMIT REGION SIZE:  '
LIMIT    DC    CL6' '
TPL4L    EQU   *-TPL4
         SPACE 2
TPL5     DC C'SYSTEM REGION SIZE:    '
LSQAB    DC    CL6' '
TPL5L    EQU   *-TPL5
         SPACE 2
TPL6     DC C'PRIVATE ADDR SIZE:     '
PVTSIZE  DC    CL6' '
TPL6L    EQU   *-TPL6
         EJECT
         CVT   DSECT=YES
         EJECT
         IHAASCB
         EJECT
         IHALDA
         EJECT
         IHAGDA
         END
