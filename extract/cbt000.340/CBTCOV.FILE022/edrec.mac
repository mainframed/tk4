*          DATA SET EDREC      AT LEVEL 005 AS OF 08/12/85
EDRECUT  TITLE 'STANDARD EDIT RECOVERY ROUTINE FOR ISPF VERSION II'
EDRECUT  CSECT
***********************************************************************
*        STANDARD ENTRY CODE FOR A RE-ENTRANT PROGRAM
*                 REGISTER 12 IS ONLY BASE REGISTER
***********************************************************************
         USING *,15                     TEMPORARY BASE REGISTER
         B     PASSLIT                  BRANCH AROUND LITERALS
         DC    C'EDRECUT '              CSECT NAME
         DC    C'08/12/85 '             DATE WRITTEN
         DC    C'VERSION 1.0 '          VERSION NUMBER
         DC    C'&SYSDATE '             DATE ASSEMBLED
         DC    C'&SYSTIME '             TIME ASSEMBLED
PASSLIT  DS    0H
         STM   14,12,12(13)             SAVE CALLERS REGISTERS
         LR    11,1                     SAVE PARAMETERS PASSED
         LR    12,15                    REGISTER 12 IS BASE REGISTER
         DROP  15                       END TEMPORARY ADDRESSING
         USING EDRECUT,12               PERMANENT ADDRESSING
         SPACE
         GETMAIN R,LV=WSLEN,SP=0        GET WORKING STORAGE AREA
         SPACE
         ST    1,8(13)                  SET FORWARD CHAIN IN SAVEAREA
         ST    13,4(1)                  SET BACKWARD CHAIN
         LM    13,1,8(13)               RESTORE CLOBBERED REGISTERS
         USING WORKSTOR,13              SETUP WORKING STORAGE
         SPACE 3
         ISPF  LOAD,ISPEXEC=(YES,ISPEXEC)
         SPACE
         EDREC 'INIT',,,'ISREDRTI'
         SPACE
         XR    4,4
         LTR   15,15
         BZ    EXITPGM
         SPACE 3
LOOP     EDREC 'QUERY'
         SPACE
         LTR   15,15
         BZ    EXITPGM
         DISPLAY PANEL='ISREDM02'
         SPACE
         LTR   15,15
         BZ    PROCESS
         EDREC 'DEFER',MF=E
         LA    4,20
         B     EXITPGM
PROCESS  DS    0H
         MVI   ZEDCMD,C' '
         MVC   VCOPYLEN,=F'1'
         VCOPY '(ZEDCMD)',VCOPYLEN,ZEDCMD,MOVE,MF=E
         SPACE
         CLI   ZEDCMD,C' '
         BNE   B0001
         ISPEXEC ,'EDREC PROCESS PASSWORD(&&PSWD)'
         LA    4,1(4)
         B     LOOP
B0001    DS    0H
         CLI   ZEDCMD,C'D'
         BNE   B0002
         EDREC 'DEFER'
         B     LOOP
B0002    DS    0H
         EDREC 'CANCEL'
         B     LOOP
EXITPGM  DS    0H
         ISPF  DELETE,ISPEXEC=YES
         LR    1,13                     GET ADDRESS FOR FREEMAIN
         L     13,WORKSTOR+4            GET CALLERS SAVE AREA
         FREEMAIN R,A=(1),LV=WSLEN,SP=0 FREE WORKING STORAGE
         SPACE
         LR    15,4                     GET RETURN CODE
         L     14,12(,13)               RESTORE RETURN ADDRESS
         LM    0,12,20(13)              RESTORE REMAINING REGISTERS
         MVI   12(13),X'FF'             FLAG AS RETURNED FOR SNAP SVC
         BR    14                       RETURN TO CALLER
         EJECT
WORKSTOR DSECT                          WORKING STORAGE DSECT
SAVEAREA DS    18F                      PROGRAM SAVE AREA
         DS    0D                       DOUBLE WORD ALIGN
         SPACE
         ISPF  PARMS,ISPEXEC=YES
VCOPYLEN DS    F
ZEDCMD   DS    C
         DS    0D
WSLEN    EQU   *-WORKSTOR               LENGTH OF WORKING STORAGE
EDRECUT  CSECT ,                        RESUME CSECT
         END   EDRECUT
