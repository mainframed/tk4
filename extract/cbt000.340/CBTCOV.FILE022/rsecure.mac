  TITLE  'RSECURE - ACF2/RI TSO TERMINAL SECURE PROGRAM'
         SPACE 5
**********************************************************************
*                                                                    *
* RSECURE -                                                          *
*     THIS PROGRAM IS REQUIRED TO PERFORM A PASSWORD REVALIDATION    *
*  FOR A TSO USER AT THE ACF2 3.1.4 LEVEL. THE PASSWORD IS NOT       *
*  STORED IN LSQA AND THE VALIDATION MUST BE PERFORMED BY USING      *
*  SVCA TO VALIDATE THE LOGONID/PASSWORD COMBINATION.                *
*     THE CALLER MUST BE AUTHORIZED TO PERFORM THIS SVCA CALL. TO    *
*  ACCOMPLISH THIS RSECURE USES SVC 221 TO PLACE ITSELF IN AND OUT   *
*  OF AUTHORIZED STATE.                                              *
*     THE USER IS NOT ALLOWED TO UPDATE HIS PASSWORD FROM THIS       *
*  PROMPT.                                                           *
*     RSECURE MUST BE MLPA'ED INORDER TO USE SVC221.                 *
*     RSECURE MUST BE LINKEDITED RENT,REUSE,REFRESH.                 *
*                                                                    *
*     WRITTEN: 04-16-84   R.A.SUNDELL                                *
*     UPDATE:  04-16-84   R.A.SUNDELL                                *
*                                                                    *
*   UPDATE HISTORY:                                                  *
*   04-16-84 - RESECURE RE-WRITTEN TO DO ONLY THE SVC CALL.          *
*                                                                    *
**********************************************************************
         EJECT
**********************************************************************
*                                                                    *
*   R E G I S T E R   D E F I N I T I O N S                          *
*                                                                    *
**********************************************************************
*
**********************************************************************
*                REGISTER USAGE                                      *
*   R0         SYSTEM                                                *
*   R1         SYSTEM                                                *
*   R2         SYSTEM                                                *
*   R3         ACCVT (FROM SECURE)                                   *
*   R4         PARMLIST (FROM SECURE)                                *
*   R5         UNUSED                                                *
*   R6         UNUSED                                                *
*   R7         UNUSED                                                *
*   R8         UNUSED                                                *
*   R9         UNUSED                                                *
*   R10        UNUSED                                                *
*   R11        UNUSED                                                *
*   R12        PRIMARY BASE REGISTER                                 *
*   R13        SAVE AREA + WORK AREA                                 *
*   R14        SYSTEM ROUTINE RETURN ADDRESS                         *
*   R15        BRANCH ADDRESS/RETURN CODE                            *
**********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
********************************************************************
*                                                                  *
*          DUMMY  SECTIONS  USED  IN  THIS  PROGRAM                *
*                                                                  *
********************************************************************
*
*  SAVE AND WORK AREA DSECT    LINKED TO REG 13
WORK     DSECT
SAVEAREA DS    9D              REGISTER SAVE
WORKLEN  EQU   *-WORK          LENGTH OF WORK AREA
         SPACE 3
         ACCVT ,
         EJECT
**********************************************************************
*                                                                    *
*        H O U S E   K E E P I N G   A R E A                         *
*                                                                    *
**********************************************************************
*
RSECURE  CSECT
         USING RSECURE,12             ADDRESSIBILITY OF CSECT
         USING WORK,R13               ADDRESSIBILITY OF WORKING STORAGE
         USING ACCVT,R3               ACCVT ADDRESSIBILITY
         SAVE  (14,12),,*             SAVE CALLERS REGISTERS
         LR    R12,R15                GET ENTRY POINT ADDRESS
         B     GETSTOR                BRANCH TO GETMAIN
         DC    CL8'RSECURE '          MODULE NAME
         DC    CL8'&SYSDATE'          DATE ASSEMBLED
         DC    CL5'&SYSTIME'          TIME ASSEMBLED
GETSTOR  CNOP  0,4                    ALIGNMENT
         GETMAIN RU,LV=WORKLEN        GET WORKING STORAGE
         ST    R1,8(,R13)             CHAIN
         ST    R13,4(R1)                    WORK
         LR    R13,R1                           AREAS
         L     R1,4(0,R13)            RESTORE REG 0 AND 1
         LM    R0,R1,20(R1)                 FROM CALLERS SAVE AREA
         ESTAE ABND                   ESATE ENVIRONMENT
         STAX  DEFER=YES              DISABLE ATTENTION INTERRUPTS
         B     SETAUTH                BRANCH TO START
**********************************************************************
*                                                                    *
*    P R O G R A M  R E T U R N  A R E A                             *
*                 R E T U R N  C O D E  I N  R E G  1 5              *
*                                                                    *
**********************************************************************
RETURN   DS    0H
         SR    R11,R11                 CLEAR REGISTER 11
         LR    R11,R15                 TEMPORARY STORE OF RC
         L     R13,4(,R13)             GET OLD SAVE AREA ADDRESS
         L     R1,8(,R13)              GET WORK AREA ADDRESS
         FREEMAIN RU,LV=WORKLEN,A=(1)   FREE STORAGE
         SR    R15,R15                 CLEAR REGISTER 15
         LR    R15,R11                 GET RETURN CODE
         RETURN (14,12),RC=(15)        RETURN TO CALLER
         EJECT
********************************************************************
*                                                                  *
*     SET AUTHORIZATION TO ISSUE SVCA TO VALIDATE LOGONID/PASSWORD *
*     ISSUE SVCA AND PASS RETURN CODE TO SECURE                    *
*                                                                  *
********************************************************************
*
SETAUTH  EQU   *
         LA    R8,2323                 SET VALUE
         CR    R8,R9                   COMPARE FLAG
         BNE   NOACF2                  RETURN
         LA    R0,1                    REQUEST AUTHORIZATION ON
         SVC   221                     SET AUTHORIZATION ON
         ACFSVC (4),TYPE=A,NONE=NOACF2,CVT=HAVE
         LR    R11,R15                 SAVE RETURN CODE
         SR    R0,R0                   REQUEST AUTHORIZATION OFF
         SVC   221                     SET AUTHORIZATION OFF
         LR    R15,R11                 RESTORE RETURN CODE
         B     RETURN                  GOBACK TO SECURE
         SPACE 5
**** IF ACF2 NOT ACTIVE PASS A RETURN CODE OF 20
*
NOACF2   EQU   *
         SR    R0,R0                   REQUEST AUTHORIZATION OFF
         SVC   221                     SET AUTHORIZATION OFF
         LA    R15,20                  NOACF2 RETURN CODE
         B     RETURN                  GOBACK TO SECURE
         SPACE 5
**** CONSTANTS
         LTORG
         SPACE 5
***** ESTAE EXIT
ABND     EQU   *
         USING *,15
         C     R0,=F'12'               IS THERE A SDWA
         BNE   NOSDWA                  NO, JUST ISSUE 221
         SR    R0,R0                   REQUEST AUTHORIZATION OFF
         SVC   221                     SET AUTHORIZATION OFF
         SETRP (1),REGS=(14,12),RC=0
NOSDWA   EQU   *
         SR    R0,R0                   REQUEST AUTHORIZATION OFF
         SVC   221                     SET AUTHORIZATION OFF
         LA    R0,12                   RESTORE VALUE IN REG 0
         LA    R15,0                   INDICATE CONTINUE TERMINATION
         BR    14                      RETURN TO CONTOL PROGRAM
         LTORG
         IHASDWA
         DROP  15
         END   RSECURE
