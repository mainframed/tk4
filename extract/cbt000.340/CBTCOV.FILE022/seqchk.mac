         TITLE 'SEQUENCE NUMBER VERIFICATION MODULE'
*----------------------------------------------------------*
*        NAME: SEQCHK                                      *
*                                                          *
*        PURPOSE:                                          *
*              TO CHECK FOR BLANK SEQUENCE NUMBERS AND TO  *
*              VERIFY THAT ALL SEQUENCE NUMBERS ARE IN     *
*              ASCENDING SEQUENCE.                         *
*                                                          *
*        INPUT:                                            *
*              DDNAME SEQIN REFERENCES ANY ASSEMBLER SOURCE*
*              MODULE.                                     *
*                                                          *
*        OUTPUT:                                           *
*              IF NO ERRORS FOUND THEN RETURN CODE = 0     *
*              IF ERRORS FOUND THEN RETURN CODE = 4 AND    *
*              ISPF MESSAGE IS ISSUED INFORMING THE USER   *
*              OF THE ERROR.                               *
*                                                          *
*        NOTE: ONLY THE FIRST ERROR ENCOUNTERED IS         *
*              REPORTED ON.                                *
*                                                          *
* AUTHOR:      LIONEL DYCK                                 *
*              ROCKWELL INTERNATIONAL                      *
*              PO BOX 2515                                 *
*              2201 SEAL BEACH BLVD.                       *
*              SEAL BEACH, CALIF  90740                    *
*              MAIL CODE 110-SH28                          *
*              PHONE (213) 594-1125                        *
*              COMNET 374-1125                             *
*----------------------------------------------------------*
SEQCHK   $PROLOG 12
         EJECT
         OPEN  (SEQIN)             OPEN SOURCE INPUT FILE
         SPACE 2
         ISPF  LOAD                LOAD ISPLINK
         SPACE 2
         VDEFINE '(SHORT)',SHORT,CHAR,8    DEFINE SHORT MESSAGE
         EJECT
         VDEFINE '(LONG)',LONG,CHAR,70     DEFINE LONG MESSAGE
         EJECT
READ     GET   SEQIN               READ SOURCE INPUT
         CLC   72(8,R1),=CL8' '    ANY SEQ NUMBERS
         BE    BLANK               ALL BLANK
         PACK  SEQ#,72(8,R1)       PACK SEQ #
         CP    PSEQ#,SEQ#          HIGHER OR LOWER
         BH    SEQERR              BRANCH IF NOT HIGHER
         MVC   PSEQ#,SEQ#          RESET PRIOR VALUE FOR COMPARE
         B     READ                CONTINUE READING
         EJECT
BLANK    DS    0H
         MVC   LONG(7),=CL7'BLANK'    MODIFY ERROR MESSAGE
         SPACE 2
SEQERR   DS    0H
         UNPK  SEQERR#,PSEQ#       SET LAST GOOD NUMBER INTO MSG
         OI    SEQERR#+7,X'F0'     INSURE ALL NUMERIC
         MVC   LONG,SEQM1          SET LONG MESSAGE
         B     EXIT4               GOTO EXIT WITH RC=4
         EJECT
EOF      DS    0H
         CLOSE SEQIN               CLOSE SOURCE INPUT FILE
         ISPF  DELETE              DELETE ISPLINK
         $EPILOG 0                 EXIT WITH CODE OF 0 (ALL OK)
         EJECT
EXIT4    DS    0H
         CLOSE SEQIN               CLOSE SOURCE INPUT FILE
         MVC   SHORT,ERRM1         SET ERROR MESSAGE (SHORT)
         VPUT  '(SHORT LONG)'      VPUT SHORT AND LONG VARIABLES
         SETMSG MSG='AOST010'      SET ISPF MESSAGE ID
         ISPF  DELETE              DELETE ISPLINK
         $EPILOG 4                 EXIT WITH CODE OF 4
         EJECT
         LTORG
         SPACE 2
         ISPF  PARMS
         SPACE 2
PSEQ#    DC    PL5'0'              PRIOR SEQUENCE NUMBER
SEQ#     DC    PL5'0'              CURRENT SEQUENCE NUMBER
SHORT    DC    CL8' '              SHORT VARIABLE DATA AREA
LONG     DC    CL70' '             LONG VARIABLE DATA AREA
ERRM1    DC    CL8'*ERROR*'        ERROR MESSAGE SHORT MSG
SEQM1    DC    CL70' '             ERROR LONG MESSAGE
         ORG   SEQM1
         DC    C'INVALID SEQUENCE NUMBER FOUND AFTER SEQUENECE NUMBER:'
         DC    C' '
SEQERR#  DC    XL8'2020202020202021'
         ORG   ,
         EJECT
SEQIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SEQIN,EODAD=EOF
         END
