CDAT     TITLE 'CDATE - CALENDAR DATE ROUTINE'
*        FUNCTION
*
*                  THIS ROUTINE WILL CALCULATE A CALENDAR DATE
*                  FROM EITHER AN OPTIONAL INPUT JULIAN DATE OR
*                  THE CURRENT JULIAN DATE IN THE CVT
*
*        INPUT
*
*                  PARM1 - ADDRESS OF 8 BYTE RESULT ON ANY BOUNDARY
*                        FORMAT - MM/DD/YY
*
*                  PARM2 - ADDRESS OF 3 BYTE JULIAN DATE (OPTIONAL)
*                        FORMAT - YYDDDS (PACKED DECIMAL)
*                          IF MISSING CVTDATE IS USED
*
*        EXIT - RETURN TO CALLER VIA R14
*
*        ERROR MESSAGES - NONE
*
*        SUPERVISOR MACROS - NONE
*
*        EXTERNAL ROUTINES - NONE
*
*        EXTERNAL TABLES - CVT FOR CVTDATE
*
*        ATTRIBUTES - REENTRANT, NON-PRIVLEDGED
*
*
CDATE    CSECT
         USING PARMS,R1
         USING TARGET,R2
         USING JULIAN,R3
         USING WORKAREA,R4
         USING SAVEAREA,R13
         USING CDATE,R15
         SPACE
         SAVE  (14,4),,*           SAVE DESTROYED REGS
         LM    R2,R3,TARGETAD      LOAD TARGET & INPUT JULIAN BASE REGS
         CLI   TARGETAD,X'80'      IS ONLY 1 PARM PRESENT
         BE    GETDATE             IF YES USE CVT DATE
         CLI   DATEAD,X'80'        ARE BOTH PARMS PRESENT
         BE    GOTDATE             IF YES USE 2ND PARM DATE
         SPACE
*        USE CURRENT DATE FROM CVT FOR SINGLE PARM
         SPACE
GETDATE  EQU   *
         L     R1,CVTPTR           LOAD CVT BASE REG
         USING CVT,R1
         LA    R3,CVTDATE+1        LOAD JULIAN BASE REG
GOTDATE  EQU   *
         SPACE
*        GET WORKAREA ON A DOUBLE WORD BOUNDARY FROM END OF
*        UNUSED SAVEAREA
         SPACE
GETWORK  EQU   *
         LA    R4,FREESAVE+7       GET ADDR OF LAST BYTE OF WORK
         N     R4,DBLMASK          SET BASE TO DOUBLE WORD
         SPACE
*        GET SERIAL DAY
         SPACE
GETDAY   EQU   *
         XC    WORK(6),WORK        ZERO WORKAREA
         MVC   WORK+6(2),DDDS      MOVE DAY OF YEAR
         CVB   R14,WORK            GET BINARY VALUE OF DAY
         SPACE
*        CHECK FOR LEAP YEAR
         SPACE
LEAPCHK  EQU   *
         IC    R1,YY               LOAD YEAR
         LA    R0,19               LOAD LEAP YEAR MASK
         NR    R1,R0               CHECK FOR YEARS OF X0, X4, OR X8
         BZ    LEAPYEAR            IF YES DECREMENT DAY
         BCTR  R0,0                DECREMENT MASK FOR 12 YEAR
         XR    R1,R0               CHECK FOR YEARS XX12, 32, 52, 72, 92
         BNZ   STDYEAR             IF NOT DONT DECREMENT DAY OF YEAR
         SPACE
*        LEAP YEAR - CHECK FOR NO DAY MOD & FEB 29
         SPACE
LEAPYEAR EQU   *
         LA    R0,31+29            LOAD DAY OF YEAR FOR FEB 29
         CR    R14,R0              CHECK FOR FEB 29 OR LESS
         BL    STDYEAR             IF FEB 28 OR LESS USE STANDARD YEAR
         BH    DECRDAY             IF MAR 1 OR GREATER DECREMENT DAY
         LA    R1,2                SET MONTH TO FEB
         LA    R14,29              SET DAY TO 29
         B     CONVDATE            GO CONVERT DATE
         SPACE
DECRDAY  EQU   *
         BCTR  R14,0               DECREMENT DAY OF YEAR AFTER FEB 29
         SPACE
*        NON LEAP YEAR - NO DAY OF YEAR MODIFICATION
         SPACE
STDYEAR  EQU   *
         SR    R1,R1               ZERO MONTH INDEX
         SPACE
*        FIND MONTH OF YEAR & DAY OF MONTH
*
*        DECREMENT R14 BY DAYS IN MONTH
*        COUNT MONTHS IN R1
         SPACE
GETMONTH EQU   *
         LA    R1,1(R1)            INCREMENT INDEX TO NEXT MONTH
         IC    R0,MONTHTBL-1(R1)   LOAD NUMBER OF DAYS IN MONTH
         SR    R14,R0              DECREMENT DAYS FOR CURRENT MONTH
         BP    GETMONTH            LOOP TILL ZERO OR MINUS
         AR    R14,R0              RESTORE DAY OF MONTH TO POS VALUE
         SPACE
*        CONVERT DATE INTO TARGET
         SPACE
CONVDATE EQU   *
         CVD   R1,WORK             CONVERT MONTH TO DECIMAL
         OI    WORK+7,15           MAKE LOW DIGIT PRINTABLE
         UNPK  MONTH,WORK          UNPACK MONTH OF YEAR
         MVI   SLASH1,C'/'         INSERT FIELD SEPARATOR
         CVD   R14,WORK            CONVERT DAY OF MONTH
         OI    WORK+7,15           MAKE LOW DIGIT PRINTABLE
         UNPK  DAY,WORK            UNPACK DAY OF MONTH
         MVI   SLASH2,C'/'         INSERT FIELD SEPARATOR
         UNPK  WORK(3),YY(2)       UNPACK YEAR INTO WORKAREA
         MVC   YEAR,WORK           MOVE YEAR TO TARGET FIELD
         SPACE
*        RETURN TO CALLER
         SPACE
RETURN   EQU   *
         RETURN  (14,4),RC=0
         EJECT
         REGEQU
         DS    0F
DBLMASK  DC    X'00FFFFF8'         DOUBLE WORD BOUNDARY MASK
MONTHTBL DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)  DAYS / MONTH
         EJECT
PARMS    DSECT
TARGETAD DS    A                   ADDRESS OF CALENDAR DATE RESULT
DATEAD   DS    A                   ADDRESS OF INPUT DATE (YYDDDF)
         SPACE 3
JULIAN   DSECT
YY       DS    X                   YEAR - PACKED DECIMAL WITHOUT SIGN
DDDS     DS    PL2                 DAY OF YEAR - PACKED DECIMAL
         SPACE 3
TARGET   DSECT
MONTH    DS    CL2                 MONTH OF YEAR
SLASH1   DS    C                   SLASH FIELD SEPERATOR
DAY      DS    CL2                 DAY OF MONTH
SLASH2   DS    C                   SLASH FIELD SEPERATOR
YEAR     DS    CL2                 LAST 2 DIGITS OF YEAR
         SPACE 3
WORKAREA DSECT
WORK     DS    D
         SPACE 3
SAVEAREA DSECT
         DS    3F                  SYSTEM CHAIN WORDS
         DS    7F                  USED PART OF SAVEAREA
FREESAVE DS    F                   START OF USABLE SAVEAREA
         EJECT
         PRINT NOGEN
CVT      DSECT
         CVT
         END
