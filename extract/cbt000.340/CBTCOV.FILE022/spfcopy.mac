***********************************************************************
*     SPFCOPY ALLOWS IEBCOPY TO BE INVOKED AS AN AUTHORIZED PROGRAM
*     UNDER SPF.  IT IS ALSO USED TO INVOKE IEBCOPY AS AUTHORIZED
*     UNDER TSO.
*
*     THIS PROGRAM SHOULD BE LINKEDITED AS RENT,REUS,REFR,APFCODE=1.
*
*     CHANGE WAS MADE TO THE IBM VERSION TO ADDRESS FROM THE BEGINNING
*     OF THE MODULE.
*           4/28/83      SOURCE VERIFIED
*
***********************************************************************
SPFCOPY  CSECT                             SPF COMPRESS ROUTINE
         STM   R14,R12,12(R13)             SAVE REGISTERS
         LR    R12,R15                     PROGRAM ADDRESSABILITY
         USING SPFCOPY,R12
         LR    R2,R1                       SAVE PARM ADDR IN R2
         L     R0,SIZDATD                  DYNAMIC AREA SIZE IN R0
         GETMAIN R,LV=(0)                  GETMAIN DYNAMIC AREA
         LR    R11,R1                      DYNAMIC AREA ADDRESSABILITY
         USING DATD,R11
         ST    R13,SAVEAREA+4              SAVE CALLER SAVEAREA ADR
         ST    R11,8(R13)                  SAVE SPFCOPY SAVEAREA ADR
         LR    R13,R11                     SPFCOPY SAVEAREA ADR IN R13
         STAX  DEFER=YES                   DEFER ATTENTION INTERUPTS
         LA    R3,40                       ERROR CODE IN R3
         LTR   R15,R15                     CHECK FOR STAX ERROR
         BNZ   RETURN                      EXIT STAX ERROR
         ESTAE ESTAEX                      ESTABLISH ESTAE ENVIRONMENT
         LA    R3,44                       ERROR CODE IN R3
         LTR   R15,R15                     CHECK ESTAE ERROR
         BNZ   STAXOFF                     EXIT ESTAE ERROR
         LA    R0,1                        REQUEST AUTHORIZATION ON
         SVC   221                         SET AUTHORIZATION ON
         LR    R1,R2                       USE PARM LIST PASSED BY USER
         LINK  EP=IEBCOPY                  LINK TO IEBCOPY
         LR    R3,R15                      SAVE RETURN CODE IN R3
         XR    R0,R0                       REQUEST AUTHORIZATION OFF
         SVC   221                         SET AUTHORIZATION OFF
         ESTAE 0                           CANCEL ESTAE ENVIRONMENT
STAXOFF  STAX  DEFER=NO                    ALLOW ATTENTION INTERRUPTS
RETURN   L     R13,SAVEAREA+4              RESTORE R13
         L     R0,SIZDATD                  DYNAMIC AREA SIZE IN R0
         LR    R1,R11                      DYNAMIC AREA ADDR IN R1
         FREEMAIN R,LV=(0),A=(1)           FREEMAIN DYNAMIC AREA
         LR    R15,R3                      RETURN CODE IN R15
         L     R14,12(R13)                 RESTORE R14
         LM    R0,R12,20(R13)              RESTORE R0 THRU R12
         BR    R14                         RETURN TO CONTROL PROGRAM
* ESTAE EXIT ROUTINE
ESTAEX   DS    0H                          ESTAE ADDRESSABILITY
         USING ESTAEX,R15
         XR    R0,R0                       REQUEST AUTHORIZATION OFF
         SVC   221                         SET AUTHORIZATION OFF
         STAX  DEFER=NO                    ALLOW ATTENTION INTERRUPTS
         XR    R15,R15                     CONTINUE WITH ABEND
         BR    R14                         RETURN TO CONTROL PROGRAM
DATA     DS    0F
SIZDATD  DC    AL1(0)                      STATIC AREA
         DC    AL3(ENDDATD-DATD)           DYNAMIC AREA SIZE
DATD     DSECT
SAVEAREA DS    18F
ENDDATD  EQU   *
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   SPFCOPY
