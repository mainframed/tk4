IPCSHDR  TITLE 'UPDATE ISPF VARIABLES FROM A DUMP HEADER RECORD'
***********************************************************************
*
*        THIS PROGRAM READS A DUMP HEADER RECORD, EXTRACTS SELECTED
*        INFORMATION FROM IT, AND PUTS THE INFORMATION IN ISPF
*        VARIABLES. IT IS INVOKED AS:
*              ISPEXEC SELECT PGM(IPCHDR) PARM(DDNAME)
*        WHERE "DDNAME" IS A FILE ALLOCATED TO A DUMP DATA SET.
*
*
*        DOUGLAS H. ADAMS
*        MAIL CODE 110-SH28
*        ROCKWELL INTERNATIONAL CORPORATION
*        2201 SEAL BEACH BOULEVARD
*        P.O. BOX 2515
*        SEAL BEACH, CALIFORNIA 90740-1515
*
*        (213) 594-2618
***********************************************************************
IPCSHDR  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         AMDDATA
PRDINPUT DSECT
         ORG   PRDINPUT+12
P37TODVL DS    CL8                 MVS/370 CLOCK VALUE FROM DUMP
P37TITLE DS    CL100               MVS/370 TITLE FROM DUMP
         ORG   PRDINPUT+X'13A'
P37HASID DS    H                   MVS/370 HOME ASID
         ORG   PRDINPUT+X'7F8'
P37SR    EQU   *
         ORG   ,
         EJECT
         DCBD  DSORG=QS,DEVD=DA
         EJECT
***********************************************************************
*              INITIALIZATION
***********************************************************************
IPCSHDR  CSECT
         SAVE  (14,12),,IPCSHDR-&SYSDATE
         LR    R12,R15
         USING IPCSHDR,R12
         ST    R13,SAVE+4          CHAIN SAVE AREAS
         LA    R13,SAVE            ...
         USING SAVE,R13            DATA BASE REGISTER
         XC    RETCODE,RETCODE     CLEAR RETURN CODE
         L     R1,0(R1)            POINT TO PARM FIELD
         LH    R2,0(R1)            GET PARM FIELD LENGTH
         LTR   R2,R2               IS IT ZERO?
         BZ    PARMERR             YES, BRANCH
         CH    R2,=H'8'            IS IT GREATER THAN EIGHT?
         BH    PARMERR             YES, BRANCH
         BCTR  R2,0                DECREMENT LENGTH FOR MOVE
         MVC   DCBDDNAM-IHADCB+DUMP,=CL8' ' CLEAR DDNAME
         EX    R2,MVCPARM          MOVE PARM TO DDNAME
         RDJFCB DUMP               GET DUMP JFCB
         LTR   R15,R15             ERROR?
         BNZ   PARMERR             YES, BRANCH
         LOAD  EP=ISPLINK          PRE-LOAD ISPLINK (ISPF)
         ST    R0,ISPLINK          ...
         LOAD  EP=BLSUMTOD         PRE-LOAD BLSUMTOD (IPCS TOD)
         ST    R0,BLSUMTOD         ...
         EJECT
***********************************************************************
*              DEFINE ALL ISPF VARIABLES
***********************************************************************
         L     R15,ISPLINK         DEFINE ISPF VARIABLES
         CALL  (15),(VDEFINE,NCPU,VCPU,CHAR,LCPU),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NDATE,VDATE,CHAR,LDATE),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NDATETIM,VDATETIM,CHAR,LDATETIM),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NDEVTYPE,VDEVTYPE,CHAR,LDEVTYPE),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NDSNAME,VDSNAME,CHAR,LDSNAME),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NDUMPOS,VDUMPOS,CHAR,LDUMPOS),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NFILE,VFILE,CHAR,LFILE),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NID,VID,CHAR,LID),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NNODE,VNODE,CHAR,LNODE),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NPRDUMPT,VPRDUMPT,CHAR,LPRDUMPT),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NQUAL,VQUAL,CHAR,LQUAL),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSOURCE,VSOURCE,CHAR,LSOURCE),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRABD,VSRABD,CHAR,LSRABD),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRRC,VSRRC,CHAR,LSRRC),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRCID,VSRCID,CHAR,LSRCID),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRLM,VSRLM,CHAR,LSRLM),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRCSECT,VSRCSECT,CHAR,LSRCSECT),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRFRR,VSRFRR,CHAR,LSRFRR),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSREGPSW,VSREGPSW,CHAR,LSREGPSW),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRPGM,VSRPGM,CHAR,LSRPGM),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRLVL,VSRLVL,CHAR,LSRLVL),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRSC,VSRSC,CHAR,LSRSC),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NSRRRL,VSRRRL,CHAR,LSRRRL),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NTIME,VTIME,CHAR,LTIME),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NTITLE,VTITLE,CHAR,LTITLE),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NTITLE2,VTITLE2,CHAR,LTITLE2),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NTYPE,VTYPE,CHAR,LTYPE),VL
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,NVOLUME,VVOLUME,CHAR,LVOLUME),VL
         EJECT
***********************************************************************
*              OPEN DUMP DATA SET AND SEARCH FOR HEADER RECORD
***********************************************************************
OPEN     EQU   *
         OPEN  (DUMP,INPUT)        OPEN DUMP
         LA    R9,10               TRY FIRST TEN RECORDS
RECLOOP  EQU   *
         GET   DUMP                GET A DUMP RECORD
         LR    R10,R1              SET BASE FOR RECORD
         USING PRDINPUT,R10
         CLI   PRDRECID,PRDHDREC   HEADER RECORD?
         BNE   NEXTREC             NO, BRANCH
         CLI   PRDHDRID,PRDHD811   MVS/XA?
         BE    MVSXA               YES, BRANCH
         CLI   PRDHDRID,PRDHDR     MVS/370?
         BE    MVS370              YES, BRANCH
NEXTREC  EQU   *
         BCT   R9,RECLOOP          TRY NEXT RECORD
NOHDR    EQU   *
         LA    R15,4               SET RETURN CODE 4
         ST    R15,RETCODE         ...
         B     CLOSE               BRANCH, HEADER NOT FOUND
***********************************************************************
*              OBTAIN DATA FROM MVX/XA HEADER RECORD.
***********************************************************************
MVSXA    EQU   *
         LA    R8,PRDSR            POINT TO MVS/XA SYMPTOM AREA
         MVC   VDUMPOS,=C'XA '     SET MVS/XA DUMP
         MVC   TOD,PRDTODVL        GET MVS/XA DUMP TIME
         MVC   VTITLE,PRDTITLE     GET MVS/XA DUMP TITLE
         MVC   ASID,PRDASID        GET ADDRESS SPACE
         CLC   ASID,=H'1'          IS IT 1?
         BNE   MVSXA2              NO, BRANCH
         LH    R7,PRDSDMPO         GET COMMON SECTION OFFSET
         LA    R7,0(R7,R10)        POINT TO COMMON SECTION
         USING PRDSDSM,R7
         MVC   ASID,PRDHASID       GET HOME ADDRESS SPACE
         DROP  R7
MVSXA2   EQU   *
         MVC   VPRDUMPT,=CL11'STAND/ALONE'  SET STAND/ALONE DUMP
         CLI   PRDDUMPT,PRDSADP    WAS IT?
         BE    COMMON              YES, BRANCH
         MVC   VPRDUMPT,=CL11'SVC        '  SET SVC DUMP
         CLI   PRDDUMPT,PRDSVCDP   WAS IT?
         BE    COMMON              YES, BRANCH
         MVC   VPRDUMPT,=CL11'SYSMDUMP   '  SET SYSMDUMP
         CLI   PRDDUMPT,PRDSADP    WAS IT?
         BE    COMMON              YES, BRANCH
         MVC   VPRDUMPT,=CL11'SLIP       '  SET SLIP DUMP
         CLI   PRDDUMPT,PRDSLPDP   WAS IT?
         BE    COMMON              YES, BRANCH
         MVI   VPRDUMPT,C' '       CLEAR DUMP TYPE
         MVC   VPRDUMPT+1(L'VPRDUMPT-1),VPRDUMPT
         B     COMMON              BRANCH
***********************************************************************
*              OBTAIN DATA FROM MVS/370 HEADER RECORD.
***********************************************************************
MVS370   EQU   *
         LA    R8,P37SR            POINT TO MVS/370 SYMPTOM AREA
         MVC   VDUMPOS,=C'370'     SET MVS/370 DUMP
         MVC   TOD,P37TODVL        GET MVS/370 DUMP TIME
         MVC   VTITLE,P37TITLE     GET MVC/370 DUMP TITLE
         MVC   ASID,PRDASID        GET ADDRESS SPACE
         CLC   ASID,=H'1'          IS IT 1?
         BNE   MVS370A             NO, BRANCH
         MVC   ASID,P37HASID       YES, GET HOME ADDRESS SPACE
MVS370A  EQU   *
         MVI   VPRDUMPT,C' '       CLEAR DUMP TYPE
         MVC   VPRDUMPT+1(L'VPRDUMPT-1),VPRDUMPT
***********************************************************************
*              OBTAIN DATA FROM EITHER HEADER RECORD.
***********************************************************************
COMMON   EQU   *
         USING PRDSR,R8
         MVC   DSNAME,JFCBDSNM     GET DUMP DSNAME
         TRT   VDSNAME,BLANK       SEARCH FOR BLANK
         MVI   0(R1),C''''         FINISH DSNAME
         MVC   VSOURCE,SRCDSN      GET IPCS SOURCE (DSNAME)
         TRT   VSOURCE,BLANK       SEARCH FOR BLANK
         MVI   0(R1),C')'          FINISH SOURCE
         MVI   VSRBASIC,C' '       CLEAR BASIC SYMPTOM STRING
         MVC   VSRBASIC+1(L'VSRBASIC-1),VSRBASIC
         MVI   VSROPT2,C' '        CLEAR OPTIONAL SYMPTOM STRING
         MVC   VSROPT2+1(L'VSROPT2-1),VSROPT2
         CLC   SR,=C'SR'           IS SYMPTOM STRING PRESENT?
         BNE   PROCESS             NO, BRANCH
         MVC   VSRBASIC,SRBASIC    GET BASIC SYMPTOM STRING
         MVC   VSROPT2,SROPT2      BET OPTIONAL SYMPTOM STRING
         DROP  R8
***********************************************************************
*              CONVERT DATA TO DESIRED FORMAT
***********************************************************************
PROCESS  EQU   *
         LH    R0,ASID             GET ASID
         CVD   R0,WORK             CONVERT TO DECIMAL
         UNPK  QUAL,WORK           CONVERT TO EBCDIC
         OI    QUAL+L'QUAL-1,C'0'  CORRECT THE ZONE
         L     R15,BLSUMTOD        CONVERT TOD TO CHARACTER FORM
         CALL  (15),(TOD,TODBUF),VL
         MVC   SYEAR,YEAR          REARRANGE FOR SORT
         MVC   SMONTH,MONTH        ...
         MVC   SDAY,DAY            ...
         MVC   SHOUR,HOUR          ...
         MVC   SMINUTE,MINUTE      ...
         MVC   SSECOND,SECOND      ...
         L     R15,ISPLINK         UPDATE IPCS VARIABLES
         CALL  (15),(VPUT,NAMES),VL
         DROP  R10                 DUMP
***********************************************************************
*              TERMINATE DUMP PROCESSING
***********************************************************************
CLOSE    EQU   *
         CLOSE (DUMP)              CLOSE DUMP
***********************************************************************
*              CLEAN UP AND TERMINATE
***********************************************************************
CLEANUP  EQU   *
         FREEPOOL DUMP             FREE I/O BUFFER POOL
         L     R15,ISPLINK         RESET ALL DEFINED ISPF VARIABLES
         CALL  (15),(VRESET),VL
RETURN   EQU   *
         DELETE EP=ISPLINK         DELETE ISPF
         DELETE EP=BLSUMTOD        DELETE IPCS TOD CONVERSION
         L     R15,RETCODE         SET RETURN CODE
         L     R13,SAVE+4          POINT TO CALLER'S SAVE AREA
         DROP  R13
         RETURN (14,12),RC=(15)    NORMAL RETURN
PARMERR  EQU   *
         L     R13,SAVE+4          POINT TO CALLER'S SAVE AREA
         RETURN (14,12),RC=12      ERROR RETURN
         EJECT
MVCPARM  MVC   DCBDDNAM-IHADCB+DUMP,2(R1)    MOVE PARM VALUE
         DROP  R12
         EJECT
***********************************************************************
*              DATA
***********************************************************************
SAVE     DC    18F'0'              SAVE AREA
@        DC    CL8' '              BLANK FOR ISPLINK
ASID     DC    H'0'
BLANK    DC    XL256'0'            TRT TABLE
         ORG   BLANK+C' '
         DC    X'FF'
         ORG   ,
BLSUMTOD DC    A(0)                ADDRESS OF BLSUMTOD (IPCS TOD)
CHAR     DC    CL8'CHAR'
CPUDATA  DC    CL8'CPUDATA'
ID       DC    PL2'0'
IPCDUMP  DC    CL8'IPCDUMP'        NAME OF ISPF TABLE
ISPLINK  DC    A(0)                ADDRESS OF ISPLINK (ISPF)
LCPU     DC    A(L'VCPU)
LDATE    DC    A(L'VDATE)
LDATETIM DC    A(L'VDATETIM)
LDEVTYPE DC    A(L'VDEVTYPE)
LDSNAME  DC    A(L'VDSNAME)
LDUMPOS  DC    A(L'VDUMPOS)
LFILE    DC    A(L'VFILE)
LID      DC    A(L'VID)
LNODE    DC    A(L'VNODE)
LPRDUMPT DC    A(L'VPRDUMPT)
LQUAL    DC    A(L'VQUAL)
LSERIAL1 DC    A(L'VSERIAL1)
LSERIAL2 DC    A(L'VSERIAL2)
LSERIAL3 DC    A(L'VSERIAL3)
LSERIAL4 DC    A(L'VSERIAL4)
LSOURCE  DC    A(L'VSOURCE)
LSRABD   DC    A(L'VSRABD)
LSRRC    DC    A(L'VSRRC)
LSRCID   DC    A(L'VSRCID)
LSRLM    DC    A(L'VSRLM)
LSRCSECT DC    A(L'VSRCSECT)
LSRFRR   DC    A(L'VSRFRR)
LSREGPSW DC    A(L'VSREGPSW)
LSRPGM   DC    A(L'VSRPGM)
LSRLVL   DC    A(L'VSRLVL)
LSRSC    DC    A(L'VSRSC)
LSRRRL   DC    A(L'VSRRRL)
LTIME    DC    A(L'VTIME)
LTITLE   DC    A(L'VTITLE)
LTITLE2  DC    A(L'VTITLE2)
LTYPE    DC    A(L'VTYPE)
LVOLUME  DC    A(L'VVOLUME)
*----------------------------------------------------------------------
*              ISPF TABLE NAME LIST
*----------------------------------------------------------------------
NAMES    DS    0D                  ISPF TABLE NAME LIST
         DC    A((NAMEEND-NAMES-8)/8)
         DC    F'0'
NCPU     DC    CL8'CPU'
NDATE    DC    CL8'DATE'
NDATETIM DC    CL8'DATETIME'
NDEVTYPE DC    CL8'DEVTYPE'
NDSNAME  DC    CL8'DSNAME'
NDUMPOS  DC    CL8'DUMPOS'
NFILE    DC    CL8'FILE'
NIBMPMR  DC    CL8'IBMPMR'
NID      DC    CL8'ID'
NJOB     DC    CL8'JOB'
NNODE    DC    CL8'NODE'
NOWNER   DC    CL8'OWNER'
NPRDUMPT DC    CL8'PRDUMPT'
NQUAL    DC    CL8'QUAL'
NSERIAL  DC    CL8'SERIAL'
NSOURCE  DC    CL8'SOURCE'
NSPR     DC    CL8'SPR'
NSRABD   DC    CL8'SRABD'
NSRRC    DC    CL8'SRRC'
NSRCID   DC    CL8'SRCID'
NSRLM    DC    CL8'SRLM'
NSRCSECT DC    CL8'SRCSECT'
NSRFRR   DC    CL8'SRFRR'
NSREGPSW DC    CL8'SREGPSW'
NSRPGM   DC    CL8'SRPGM'
NSRLVL   DC    CL8'SRLVL'
NSRSC    DC    CL8'SRSC'
NSRRRL   DC    CL8'SRRRL'
NTIME    DC    CL8'TIME'
NTITLE   DC    CL8'TITLE'
NTITLE2  DC    CL8'TITLE2'
NTYPE    DC    CL8'TYPE'
NVOLUME  DC    CL8'VOLUME'
NAMEEND  EQU   *
*----------------------------------------------------------------------
NJENODE  DC    CL8' '              PARM = NJE NODE NAME
NOWRITE  DC    CL8'NOWRITE'
NSERIAL1 DC    CL8'SERIAL1'
NSERIAL2 DC    CL8'SERIAL2'
NSERIAL3 DC    CL8'SERIAL3'
NSERIAL4 DC    CL8'SERIAL4'
ORDER    DC    CL8'ORDER'
RETCODE  DC    F'0'
SHARE    DC    CL8'SHARE'
SORTLIST DC    C'(DATETIME,C,D)'   ISPF TABLE IPCDUMP SORT LIST
TBADD    DC    CL8'TBADD'
TBCREATE DC    CL8'TBCREATE'
TBOPEN   DC    CL8'TBOPEN'
TBSORT   DC    CL8'TBSORT'
TBTOP    DC    CL8'TBTOP'
TBSKIP   DC    CL8'TBSKIP'
TOD      DC    XL8'0'
*----------------------------------------------------------------------
*              TIME OF DAY
*----------------------------------------------------------------------
TODBUF   DS    0CL17
VDATE    DS    0CL8
MONTH    DC    C'MM'
         DC    C'/'
DAY      DC    C'DD'
         DC    C'/'
YEAR     DC    C'YY'
         DC    C' '
VTIME    DS    0CL8
HOUR     DC    C'HH'
         DC    C':'
MINUTE   DC    C'MM'
         DC    C':'
SECOND   DC    C'SS'
*----------------------------------------------------------------------
VCPU     DC    CL8' '
*----------------------------------------------------------------------
*              SORTABLE DATE/TIME
*----------------------------------------------------------------------
VDATETIM DS    0CL12' '
SYEAR    DC    C'YY'
SMONTH   DC    C'MM'
SDAY     DC    C'DD'
SHOUR    DC    C'HH'
SMINUTE  DC    C'MM'
SSECOND  DC    C'SS'
*----------------------------------------------------------------------
VDEFINE  DC    CL8'VDEFINE'
VDEVTYPE DC    CL8' '
SRCDSN   DS    0CL54
         DC    C'DSNAME('
VDSNAME  DC    CL46''''
         ORG   VDSNAME+1
DSNAME   DS    CL44                DSNAME
         ORG   ,
         DC    C' '                ROOM FOR )
*----------------------------------------------------------------------
*              FILE NAME
*----------------------------------------------------------------------
SRCFILE  DS    0CL21
         DC    C'FILE('
VFILE    DS    0CL8
FILE     DC    C'    '
FILECPU  DC    C' '
FILEID   DC    CL2' '
         DC    C' '
         DC    CL8')'
*----------------------------------------------------------------------
VDUMPOS  DC    CL3' '
VID      DC    CL2' '
VNODE    DC    CL8' '
VPRDUMPT DC    CL11' '
VPUT     DC    CL8'VPUT'
VQUAL    DC    CL11'ASID(     )'
         ORG   VQUAL+5
QUAL     DS    CL5
         ORG   ,
VRESET   DC    CL8'VRESET'
VSERIAL1 DC    CL6' '
VSERIAL2 DC    CL6' '
VSERIAL3 DC    CL6' '
VSERIAL4 DC    CL6' '
VSOURCE  DC    CL54' '
*----------------------------------------------------------------------
*              SYMPTOM STRING
*----------------------------------------------------------------------
VSRBASIC DS    0CL112              BASIC SYMPTOM STRING
VSRABD   DC    CL8' '
VSRRC    DC    CL16' '
VSRCID   DC    CL16' '
VSRLM    DC    CL16' '
VSRCSECT DC    CL16' '
VSRFRR   DC    CL16' '
VSREGPSW DC    CL24' '
VSROPT2  DS    0CL84               OPTIONAL SYMPTOM STRING
VSRPGM   DC    CL16' '
VSRLVL   DC    CL24' '
VSRSC    DC    CL28' '
VSRRRL   DC    CL16' '
*----------------------------------------------------------------------
VTITLE   DC    CL100' '
         ORG   VTITLE+L'VTITLE/2
VTITLE2  DS    CL(L'VTITLE/2)
         ORG   ,
VTYPE    DC    CL4'DUMP'
VVOLUME  DC    CL6' '
WORK     DC    D'0'
         EJECT
***********************************************************************
*              DCB FOR DUMP
***********************************************************************
DUMP     DCB   BLKSIZE=4104,                                           *
               DSORG=PS,                                               *
               EODAD=NOHDR,                                            *
               EROPT=SKP,                                              *
               EXLST=EXLST,                                            *
               LRECL=4104,                                             *
               MACRF=GL,                                               *
               RECFM=FS
EXLST    DC    X'87',AL3(INFMJFCB)
         EJECT
***********************************************************************
*              JOB FILE CONTROL BLOCK FOR DUMP
***********************************************************************
         IEFJFCBN
         EJECT
         END   IPCSHDR
