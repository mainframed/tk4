         TITLE 'SOURCE SEARCH UTILITY 11-17-82'
*---------------------------------------------------------------------*
*        NAME: SEARCHIT                                               *
*                                                                     *
*        FUNCTION:                                                    *
*              THIS UTILITY WILL SEARCH ALL FILES HAVING A DDNAME     *
*              PREFIXED WITH A @ FOR THE CHARACTER STRINGS SUPPLIED   *
*              IN SYSIN.                                              *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        JCL:                                                         *
*                                                                     *
*            //  EXEC PGM=SEARCHIT                                    *
*                                                                     *
*            //SYSIN    -  CONTROL CARDS (SEE BELOW FOR SYNTAX)       *
*               (LRECL=80)                                            *
*                                                                     *
*            //SYSPRINT -  REPORT OF HITS                             *
*               (RECFM=FBA,LRECL=121)                                 *
*                                                                     *
*            //@AA      -  PDS OR PS INPUT FILE                       *
*               (LRECL=80)                                            *
*            //@BB      -  PDS OR PS INPUT FILE                       *
*               (LRECL=80)                                            *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        TSO:                                                         *
*                                                                     *
*              ALLOC F(SYSIN) DA(*)                                   *
*                                                                     *
*              ALLOC F(SYSPRINT) DA(*)                                *
*                                                                     *
*              ALLOC F(@AA) DA(PDS OR PS INPUT FILE)                  *
*                                                                     *
*              ALLOC F(@BB) DA(PDS OR PS INPUT FILE)                  *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        CONTROL CARD SYNTAX:                                         *
*              THE CONTROL CARD MAY HAVE ANY CHARACTER STRING         *
*              THAT YOU WISH TO SEARCH FOR CODED IN COLUMNS 1-20.     *
*              NOTE THAT 4 TRAILING BLANKS INDICATE THE END OF        *
*              THE CHARACTER STRING TO SEARCHIT.                      *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        ABENDS:                                                      *
*              1000 = MORE THAN 100 CONTROL CARDS                     *
*              2000 = MORE THAN 2000 DIRECTORY ENTRIES IN PDS         *
*                                                                     *
*        SOLUTION TO ABENDS:                                          *
*              1000 = INCREASE LABEL TABSIZE VALUE                    *
*              2000 = INCREASE LABEL DIRSIZE VALUE                    *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        SEMI-LOGIC FLOW:                                             *
*              1) READ CONTROL CARDS PLACING THE LENGTH AND CHARACTER *
*                 STRING INTO A GETMAINED TABLE                       *
*              2) SCAN TIOT FOR FILES WITH DDNAME'S PREFIXED WITH @   *
*              3) CHECK DSORG FOR PS OR PO                            *
*              4) IF PS THEN READ DIRECTORY INTO GETMAINED TABLE      *
*              5) READ PS OR EACH PO MEMBER AND SEARCH FOR CHARACTER  *
*                 STRINGS                                             *
*              6) REPORT ON ALL MATCHES                               *
*              7) BACK TO TIOT OR END                                 *
*                                                                     *
*---------------------------------------------------------------------*
*        IBM MACROS USED:                                             *
*              PUT,GET,OPEN,CLOSE,ABEND,GETMAIN,FREEMAIN,             *
*              RDJFCB,EXTRACT,DCB,IEFJFCBN,IEFTIOT1                   *
*---------------------------------------------------------------------*
*        LOCAL MACROS USED:                                           *
*              $PROLOG,$EPILOG,BITSW,IFON,IFOFF,SETON,SETOF           *
*---------------------------------------------------------------------*
*        AUTHOR:                                                      *
*              LIONEL DYCK                                            *
*              ROCKWELL INTERNATIONAL                                 *
*              PO BOX 2515                                            *
*              SEAL BEACH, CALIF 90740                                *
*              (213) 594-1125                                         *
*---------------------------------------------------------------------*
         EJECT
         MACRO
&LABEL   $$PUT &DATA
&LABEL   LA    R2,&DATA
         BAL   R3,OUTROU
         MEND
         EJECT
SEARCHIT $PROLOG
         L     R2,DIRSIZE
         GETMAIN R,LV=(R2)
         ST    R1,DIRTAB
         AR    R1,R2
         S     R1,DIRENT                REDUCE BY 1
         ST    R1,DIRTEND
         L     R2,TABSIZE
         GETMAIN R,LV=(R2)
         LR    R5,R1
         LA    R6,CCSIZE
         LR    R7,R5
         AR    R7,R2
         SR    R7,R6
         STM   R5,R7,TABLM
         USING CCREC,R5
         OPEN  (SYSIN,,SYSPRINT,OUTPUT)
         SR    R9,R9
         PUT   SYSPRINT,TC1
         PUT   SYSPRINT,BLANKS
RSYSIN   EQU   *
         GET   SYSIN
         LR    R4,R1
         SR    R3,R3
         LA    R2,19(R4)
         OC    0(80,R4),BLANKS          INSURE UPPER CASE
         CLC   =C'END ',0(R4)
         BE    EOFSYSIN
CCLOOP   EQU   *
         CLC   0(4,R4),BLANKS           LOOK FOR 4 BLANKS TO DELIMIT
         BE    CCEND                    END OF THIS CONTROL CARD
         LA    R3,1(R3)
         LA    R4,1(R4)
         CR    R4,R2                    END OF ALLOWABLE CONTROL CARD
         BNL   BADCARD                  YES - BAD CARD
         B     CCLOOP
EOFSYSIN CLOSE SYSIN
         STH   R9,TABENTS               SAVE COUNT OF CONTROL CARDS
         B     STARTIT
CCEND    BCTR  R3,R0                    REDUCE BY 1 FOR MVC
         EX    R3,MVCCC
         STH   R3,CCLEN                 SAVE LENGTH
         MVC   TCLDATA,0(R1)
         PUT   SYSPRINT,TCL
         LA    R9,1(R9)                 INCR CC COUNTER
         BXLE  R5,R6,RSYSIN
         ABEND 1000                     TOO MANY CONTROL CARDS
BADCARD  MVC   BADLINE+1(25),0(R1)
         PUT   SYSPRINT,BADLINE
         B     RSYSIN
MVCCC    MVC   CCDATA(0),0(R1)
         DROP  R5
STARTIT  EXTRACT ANS,'S',FIELDS=(TIOT)
         L     R2,ANS              -> TIOT
         USING TIOCNJOB,R2
         CLI   TIOEDDNM,C'@'       OUR DDNAME ?
         BE    FNDDD               YES
         LA    R2,TIOELNGH
         USING TIOELNGH,R2
TIOL     EQU   *
         CLC   TIOTFEND(4),=F'00'  END OF TIOT
         BE    ENDTIOT             SKIP IT
         CLI   TIOEDDNM,C'@'       OUR DDNAME ?
         BE    FNDDD               YES
TIOLP    SR    R1,R1
         IC    R1,TIOELNGH
         LTR   R1,R1
         BZ    ENDTIOT
         AR    R2,R1
         B     TIOL                KEEP LOOKING
FNDDD    MVC   PDSIN+X'28'(8),TIOEDDNM
         MVC   PDS+X'28'(8),TIOEDDNM
         SETON NEWPDS
         ST    R2,SAVETIOT
         OPEN  PDSIN
         CLOSE PDSIN
         RDJFCB PDSIN
         LA    R9,PDSJFCB
         USING INFMJFCB,R9
         MVC   T1DSN,JFCBDSNM           MOVE DSN TO TITLE
         ZAP   LINECTR,=P'99'
         TM    JFCDSRG1,JFCORGPS      IT PS
         BO    ORGPS
         TM    JFCBIND1,JFCPDS  MEMBER OF PDS
         BO    ORGPDS                  YES - BUT FLAG IT
         TM    JFCDSRG1,JFCORGPO      IT PO
         BO    ORGPO
         B     TIOL             NOT A VALID DSORG
ORGPS    EQU   *
         OPEN  (PDSIN)
         B     READ
ORGPDS   EQU   *
         MVC   MEMBER,JFCBELNM
         OPEN  (PDSIN)
         SETON PDSB             FLAG AS PDS
         B     READ
ORGPO    SETON PO
         CLOSE PDSIN
         B     READDIR
OPENPDS  EQU   *
         OPEN  PDSIN,TYPE=J
READ     GET   PDSIN
         OC    0(80,R1),BLANKS          INSURE ALL UPPER CASE
         MVC   TEMPDATA(80),0(R1)
         LA    R4,TEMPDATA
         LR    R10,R4
         LA    R5,72(R4)        -> END OF RECORD
         LM    R7,R8,TABLM
         LH    R9,TABENTS               LOAD # OF CC'S
         USING CCREC,R7
COMPARE  LH    R3,CCLEN                 LOAD LENGTH FOR COMPARE
EX       EX    R3,CLC                   PERFORM THE COMPARE
         BE    MATCH                    GOT A HIT ON A CC
INCR     AR    R7,R8                    BUMP TABLE BY 1 ENTRY
         BCT   R9,COMPARE               * KEEP CHECKING REST OF CC'S
         LH    R9,TABENTS               RESET COUNT OF CC'S
         L     R7,TABLM                 -> START OF CC'S AGAIN
         LA    R4,1(R4)                 -> NEXT CHAR IN CARD
         CR    R4,R5            END OF RECORD
         BNH   COMPARE
         B     READ
MATCH    MVC   MATCHDAT,0(R10)
         $$PUT MATCHLNE
         B     READ                     GO GET NEXT RECORD
CLC      CLC   CCDATA(0),0(R4)          **  EXECUTED COMPARE
EOF      EQU   *
         CLOSE PDSIN
         IFOFF PO,CLOSE
         IFON  PDSB,CLOSE
         B     READDIR
CLOSE    EQU   *
         L     R2,SAVETIOT
         B     TIOLP
ENDTIOT  CLOSE SYSPRINT
         L     R2,TABSIZE
         L     R3,TABLM
*        FREE THE CONTROL CARD TABLE
         FREEMAIN R,LV=(R2),A=(R3)
         L     R2,DIRSIZE
         L     R3,DIRTAB
*        FREE THE DIRECTORY TABLE
         FREEMAIN R,LV=(R2),A=(R3)
         $EPILOG
READDIR  IFOFF NEWPDS,GIVENEXT
         SETOF NEWPDS
         L     R10,DIRTAB
CLRDIR   XC    0(8,R10),0(R10)     CLEAR DIRECTORY TABLE
         A     R10,DIRENT
         C     R10,DIRTEND
         BNH   CLRDIR
         L     R10,DIRTAB          RESET ->
         OPEN  PDS
RPDS     GET   PDS
         LR    R2,R1            SAVE INPUT ->
         USING DIR,R2           SET UP ADDRESSABILITY
         LH    R11,0(R2)
         AR    R11,R2           -> END OF DIR BLOCK
PLPS     LA    R2,2(R2)         -> PAST LEN
LPS      CLC   NAME,=8X'FF'
         BE    EXIT
         MVC   0(8,R10),NAME
         SR    R1,R1            CLEAR REG 1
         IC    R1,FLAG
         N     R1,=X'0000001F'          TURN OFF MISC BITS
         LA    R1,6(R1)         ADD FIXED # OF HALF WORDS
         SLA   R1,1             MULT BY 2 FOR # OF BYTES OF DATA
         AR    R2,R1
         CR    R2,R11
         BL    NEXTDIR
         SETON GORPDS
NEXTDIR  A     R10,DIRENT               -> NEXT ENTRY
         C     R10,DIRTEND               AT END OF DIRECTORY TABLE ?
         BNH   LPS1                     NO - KEEP LOOKING
         ABEND 2000                     NOT ENOUGH ENTRIES
EXIT     CLOSE PDS
         L     R10,DIRTAB
         MVC   MEMBER(8),0(R10)         MOVE 1ST MEMBER NAME
         B     RETURN                   AND RETURN
LPS1     EQU   *
         IFOFF GORPDS,LPS
         SETOF GORPDS
         B     RPDS
         EJECT
GIVENEXT EQU   *                        PASS THE NEXT DIR ENTRY
         L     R10,SAVEREGA
         A     R10,DIRENT               -> NEXT ENTRY
         C     R10,DIRTEND               AT END OF DIRECTORY ?
         BE    CLOSE
         CLC   =H'00',0(R10)            NULL ENTRY
         BE    CLOSE
         MVC   MEMBER,0(R10)            MOVE MEMBER NAME
RETURN   EQU   *
         ST    R10,SAVEREGA
         LA    R9,PDSJFCB
         MVC   JFCBELNM,MEMBER
         OI    JFCBTSDM,X'08'      TURN ON DO NOT WRITE BACK FLAG
         OI    JFCBIND1,JFCPDS
         B     OPENPDS
         EJECT
OUTROU   EQU   *
         AP    LINECTR,=P'01'
         CP    LINECTR,LINEPPG
         BH    NEWPAGE
         PUT   SYSPRINT,0(R2)
         BR    R3
NEWPAGE  EQU   *
         ZAP   LINECTR,=P'04'
         AP    PAGECTR,=P'01'
         UNPK  PAGE,PAGECTR
         OI    PAGE+3,X'F0'
         PUT   SYSPRINT,T1
         PUT   SYSPRINT,T2
         PUT   SYSPRINT,BLANKS
         PUT   SYSPRINT,0(R2)
         BR    R3
         LTORG
         BITSW NEWPDS,PO,GORPDS,SYSPRINTB,PDSB
         PRINT NOGEN
PDSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=PDSIN,EODAD=EOF,EXLST=JFCB,    X
               LRECL=80
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=121
PDS      DCB   DSORG=PS,MACRF=GL,DDNAME=PDS,LRECL=256,RECFM=F, *       *
               BLKSIZE=256
SYSIN DCB      DSORG=PS,MACRF=GL,DDNAME=SYSIN,LRECL=80,RECFM=FB,       X
               EODAD=EOFSYSIN
         PRINT GEN
         EJECT
JFCB     DC    X'87',AL3(PDSJFCB)
PDSJFCB DC     CL176' '
         SPACE
PJFCB    DC    X'87',AL3(PDSLJFCB)
PDSLJFCB DC    CL176' '
         SPACE
ANS      DS    F
SAVETIOT DS    F
SAVEREGA DS    F
LINECTR  DC    PL2'99'
PAGECTR  DC    PL2'00'
LINEPPG  DC    PL2'54'
         SPACE
DIRSIZE  DC    A(2000*8)                2000 DIRECTORY ENTRIES MAX
DIRTAB   DC    A(0)                     START OF DIRECTORY TABLE
DIRENT   DC    A(8)                     LEN OF ENTRY
DIRTEND  DC    A(0)                     END OF DIR TABLE-1 ENT
         SPACE
TABLM    DC    3A(0)
TABSIZE  DC    A(100*CCSIZE)            MAX 100 ENTRIES
TABENTS  DC    H'00'                    # OF CONTROL CARDS TO PROCESS
TEMPDATA DC    CL120' '                 TEMPORARY STORAGE
BLANKS   DC    CL121' '
BADLINE  DC    CL121' '
         ORG   BADLINE+35
         DC    C'*** INVALID CONTROL CARD MUST BE =< 20'
         ORG
T1       DC    CL121'1'
         ORG   T1+11
         DC    C'SOURCE SEARCH UTILITY DSN='
T1DSN    DC    CL34' '
         DC    CL10' ',C'PAGE '
PAGE     DC    CL4' '
         ORG
T2       DC    CL121'0'
         ORG   T2+1
         DC    CL9'MEMBER '
         DC    C'----×----1----×----2----×----3----×----4----×----5'
         DC    C'----×----6----×----7----×----8'
         ORG
TC1      DC    CL121'1        SOURCE SEARCH UTILITY CONTROL CARDS'
TCL      DC    CL121' '
         ORG   TCL+10
TCLDATA  DC    CL80' '
         ORG
MATCHLNE DC    CL121' '
         ORG   MATCHLNE+1
MEMBER   DC    CL8' '
         DC    C' '
MATCHDAT DC    CL80' '
         ORG
         EJECT
         DSECT
         IEFJFCBN LIST=YES
         EJECT
         DSECT
         IEFTIOT1
         EJECT
CCREC    DSECT
CCLEN    DS    H
CCDATA   DS    CL20
CCSIZE   EQU   *-CCLEN
         EJECT
         DSECT
DIR      DS    0C
NAME     DS    CL8
TTR      DS    CL3
FLAG     DS    X
ALIAS    EQU   128
         END
