         TITLE 'IPCS INTERFACE TO PERMIT SPF BROWSE OF OUTPUT'
***********************************************************************
*        THIS ROUTINE IS AN INTERFACE BETWEEN IPCS AND A VERBEXIT
*        ROUTINE TO PERMIT SPF BROWSE OF THE OUTPUT PRODUCED VIA THE
*        PRINT SERVICE ROUTINE LINKAGE.
*        OUTPUT IS WRITTEN TO A DATA SET DEFINED BY DDNAME IPCSBRWS.
*        TO UTILIZE THE INTERFACE, ALLOCATE A DATA SET TO FILE
*        IPCSBRWS, THEN EXECUTE THE IPCS VERBEXIT SUBCOMMAND AND
*        SPECIFY IPCSBRWS AS THE EXIT ROUTINE. THE NAME OF THE ACTUAL
*        VERBEXIT ROUTINE IS PASSED AS THE FIRST POSITIONAL PARAMETER
*        OF THE EXIT ROUTINE (THE NAME MUST BE TERMINATED WITH A COMMA
*        OR BLANK). ANY SUBSEQUENT PARAMETERS WILL BE PASSED TO THE
*        ACTUAL VERBEXIT ROUTINE.
***********************************************************************
         EJECT
IPCSBRWS CSECT
         DCBD  DSORG=QS,DEVD=DA
         EJECT
         IHAABDPL ,                USER EXIT PARAMETER LIST
ADPLIPCS EQU   X'10'               0=IDENTIFIES VS2 PRDMP
*                                  1=IDENTIFIES MVS/IPCS
ADPLSIZE DS    0XL(*-ABDPL)        LENGTH OF ABDPL
         EJECT
WORK     DSECT                     WORK AREA
EXITSAVE DS    18F                 SAVE AREA FOR EXIT ROUTINE
PARM     DS    XL(L'ADPLSIZE)      COPY OF ABDPL
SAVE     DS    18F                 SAVE AREA FOR SERVICE ROUTINES
AABDPL   DS    A                   ADDRESS OF ORIGINAL ABDPL
NAME     DS    CL8                 EXIT ROUTINE NAME
LINK     LINK  SF=L                LINK PARAMETER LIST
WORKSIZE DS    0XL(*-WORK)         LENGTH OF WORK AREA
         EJECT
IPCSBRWS CSECT
         USING IPCSBRWS,15
         MODID ,
         SAVE  (14,12)             SAVE REGISTERS
         LR    10,15               SET IPCSBRWS BASE REGISTER
         DROP  15                  DROP IPCSBRWS
         USING IPCSBRWS,10
         LR    9,1                 SET PARAMETER LIST BASE REGISTER
         USING ABDPL,9
         LA    0,L'WORKSIZE        GET LENGTH OF WORK AREA
         ICM   0,8,ADPLSBPL        GET SUBPOOL
         GETMAIN R,LV=(0)          GET WORK AREA
         ST    1,8(13)             CHAIN SAVE AREAS
         ST    13,4(1)
         LR    13,1                SET WORK AREA BASE REGISTER
         USING WORK,13
         MVC   PARM,ABDPL          COPY PARAMETER LIST
         ST    9,AABDPL            SAVE A(ORIGINAL ABDPL)
         LA    9,PARM              POINT TO COPY OF ABDPL
         LA    1,PRNT              POINT TO PRINT SERVICE ROUTINE
         ST    1,ADPLPRNT          STORE IN PARAMETER LIST
         LA    1,MEMA              POINT TO MEMORY ACCESS ROUTINE
         ST    1,ADPLMEMA          STORE IN PARAMETER LIST
         LA    1,FRMT              POINT TO FORMAT ROUTINE
         ST    1,ADPLFRMT          STORE IN PARAMETER LIST
         ICM   1,15,ADPLEXT        POINT TO PARM EXTENSION
         BZ    FREEMAIN            NO EXTENSION, RETURN
         ICM   2,15,0(1)           POINT TO CONTROL STATEMENT
         BZ    FREEMAIN            NO CONTROL STATEMENT, RETURN
         MVC   NAME,=CL8' '        CLEAR MODULE NAME
         LA    3,NAME              POINT TO NAME
         LA    4,8                 MAXIMUM EIGHT CHARACTERS
MOVENAME DS    0H
         MVC   0(1,3),0(2)         MOVE ONE CHARACTER
         LA    2,1(2)              POINT TO NEXT INPUT BYTE
         LA    3,1(3)              POINT TO NEXT OUTPUT BYTE
         CLI   0(2),C' '           END OF NAME?
         BE    ENDNAME             YES, BRANCH
         CLI   0(2),C','           END OF NAME?
         BE    ENDNAME             YES, BRANCH
         BCT   4,MOVENAME          LOOP TO NEXT CHARACTER
         LA    0,4                 DELIMITER ERROR RETURN CODE
         ST    0,0(1)              STORE CONTROL STATEMENT RETURN CODE
         B     FREEMAIN            CONTROL STATEMENT ERROR, RETURN
ENDNAME  DS    0H
         LA    2,1(2)              POINT PAST DELIMITER
         ST    2,0(1)              STORE CONTROL STATEMENT POINTER
         OPEN  MF=(E,DCBLIST)      OPEN BROWSE DATA SET
         LA    1,BROWSE            POINT TO DCB
         USING IHADCB,1
         TM    DCBOFLGS,DCBOFOPN   DID DCB OPEN?
         BZ    FREEMAIN            NO, BRANCH
         DROP  1
         LINK  EPLOC=NAME,         LINK TO EXIT ROUTINE                *
               DCB=0,                                                  *
               MF=(E,PARM),                                            *
               SF=(E,LINK)
         LR    2,15                SAVE RETURN CODE
         CLOSE MF=(E,DCBLIST)      CLOSE BROWSE DATA SET
         FREEPOOL BROWSE           FREE BROWSE BUFFER POOL
         DROP  13                  DROP WORK
FREEMAIN DS    0H
         LR    1,13                POINT TO WORK AREA
         L     13,4(1)             UNCHAIN SAVE AREAS
         LA    0,L'WORKSIZE        GET SIZE OF WORK AREA
         ICM   0,8,ADPLSBPL        GET SUBPOOL
         FREEMAIN R,LV=(0),A=(1)   FREE WORK AREA
         DROP  9                   DROP ABDPL
         DROP  10                  DROP IPCSBRWS
         LR    15,2                RESTORE RETURN CODE
         RETURN (14,12),RC=(15)    RETURN TO IPCS
         EJECT
PRNT     DS    0H                  PRINT SERVICE ROUTINE
         SAVE  (14,12)             SAVE EXIT ROUTINE REGISTERS
         LR    10,15               SET PRNT BASE REGISTER
         USING PRNT,10
         USING PARM,1
         ST    13,SAVE+4           CHAIN SAVE AREAS
         LR    2,13
         LA    13,SAVE
         ST    13,8(2)
         USING SAVE,13
         LR    9,1                 SET PARM BASE REGISTER
         DROP  1                   DROP PARM
         USING ABDPL,9
         L     0,ADPLBUF           POINT TO BUFFER
         PUT   BROWSE,(0)          PUT RECORD
         L     1,ADPLBUF           POINT TO BUFFER
         MVI   0(1),C' '           CLEAR BUFFER
         MVC   1(120,1),0(1)
         L     13,SAVE+4           RESTORE SAVE AREA POINTER
         DROP  13                  DROP SAVE
         RETURN (14,12)            RETURN TO EXIT ROUTINE
         DROP  9                   DROP PARM
         DROP  10                  DROP PRINT
         EJECT
MEMA     DS    0H                  MEMORY ACCESS ROUTINE
         SAVE  (14,12)             SAVE EXIT ROUTINE REGISTERS
         USING PARM,1
         ST    13,SAVE+4           CHAIN SAVE AREAS
         LR    2,13
         LA    13,SAVE
         ST    13,8(2)
         USING SAVE,13
         LR    9,1                 SET PARM BASE REGISTER
         DROP  1                   DROP PARM
         USING PARM,9
         L     1,AABDPL            POINT TO ORIGINAL PARAMETER LIST
         USING ABDPL,1
         MVC   ADPLASID,ADPLASID-ABDPL+PARM  COPY MODIFIED ASID
         L     15,ADPLMEMA         POINT TO IPCS MEMORY ACCESS ROUTINE
         CALL  (15),MF=(E,(1))     CALL ROUTINE
         L     13,SAVE+4           RESTORE SAVE AREA POINTER
         DROP  13                  DROP SAVE
         L     14,12(13)           RESTORE RETURN REGISTER
         RETURN (1,12),RC=(15)     RETURN WITH VALUE IN REGISTER ZERO
         DROP  1                   DROP ABDPL
         DROP  9                   DROP PARM
         SPACE 2
FRMT     DS    0H                  FORMAT SERVICE ROUTINE
         SAVE  (14,12)             SAVE EXIT ROUTINE REGISTERS
         USING PARM,1
         ST    13,SAVE+4           CHAIN SAVE AREAS
         LR    2,13
         LA    13,SAVE             SET SAVE AREA BASE REGISTER
         ST    13,8(2)
         USING SAVE,13
         LR    9,1                 SET PARM BASE REGISTER
         DROP  1                   DROP PARM
         USING PARM,9
         L     1,AABDPL            POINT TO ORIGINAL PARAMETER LIST
         USING ABDPL,1
         L     15,ADPLFRMT         POINT TO IPCS FORMAT SERVICE ROUTINE
         CALL  (15),MF=(E,(1))     CALL ROUTINE
         MVC   ADPLFMT1-ABDPL+PARM,ADPLFMT1  COPY FORMAT PATTERN LABEL
         MVC   ADPLFMT2-ABDPL+PARM,ADPLFMT2  COPY FORMAT PATTERN DATA
         L     13,SAVE+4           RESTORE SAVE AREA POINTER
         DROP  13                  DROP SAVE
         L     14,12(13)           RESTORE RETURN REGISTER
         RETURN (1,12),RC=(15)     RETURN WITH VALUE IN REGISTER ZERO
         DROP  1                   DROP ABDPL
         DROP  9                   DROP PARM
         EJECT
DCBEXIT  DS    0H                  DCB EXIT
         USING IHADCB,1
         LH    0,DCBBLKSI          GET BLKSIZE
         LTR   0,0                 BLKSIZE SPECIFIED?
         BNZR  14                  YES, RETURN
         LA    0,4080              ASSUME BLKSIZE
         STH   0,DCBBLKSI          STORE IT
         BR    14                  RETURN
         EJECT
EXLST    DC    X'85',AL3(DCBEXIT)  DCB EXIT ROUTINE
BROWSE   DCB   DDNAME=IPCSBRWS,                                        *
               DSORG=PS,                                               *
               EXLST=EXLST,                                            *
               LRECL=120,                                              *
               MACRF=PM,                                               *
               RECFM=FB
DCBLIST  OPEN  (BROWSE,OUTPUT),MF=L  OPEN/CLOSE LIST
         END
