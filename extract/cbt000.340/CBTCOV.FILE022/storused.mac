***      NOTE: THIS PROGRAM WILL NOT ASSEMBLE OR WORK IN AN MVS/XA
***      SYSTEM.
         SPACE 1
         MACRO
&L       KFILL  &A,&B,&C
.*       &A = TO FIELD
.*       &B = LENGTH OF OUTPUT
.*       &C = FROM FULLWORD FIELD
         LCLA   &LEN
         LCLC   &R
         AIF    ('&C'(1,1) NE '(').A
&LEN     SETA   K'&C-2
&R       SETC   '&C'(2,&LEN)
&L       LR     R1,&R
         AGO    .B
.A       ANOP
&L       L      R1,&C
.B       SRL    R1,10
         CVD    R1,DWK
         MVC    FORMAT,EDITK
         ED     FORMAT+1(6),DWK+5
         AIF    ('&A'(1,1) NE '(').C
&LEN     SETA   K'&A-2
&R       SETC   '&A'(2,&LEN)
         MVC    0(&B,&R),FORMAT+8-&B
         AGO    .D
.C       MVC    &A.(&B),FORMAT+8-&B
.D       ANOP
         SPACE
         MEND
STORUSED $PROLOG
         L     R1,CVTPTR                  -> CVT
         USING CVT,R1
         L     R3,CVTTCBP
         L     R2,CVTGDA
         L     R8,CVTNUCB                 SAVE BOTTOM OF PVT
         USING GDA,R2
         KFILL PVTSIZE,6,PASIZE
         L     R9,PASIZE
         A     R9,PASTRT                  GET TOP OF PVT
         DROP  R1
         L     R4,12(R3)                  -> CURRENT ASCB (MINE)
         USING ASCB,R4                    SET UP USING
         L     R1,ASCBJBNS
         MVC   JOBNAME,0(R1)              MOVE MY JOBNAME
         L     R5,ASCBLDA                 -> LDA
         USING LDA,R5                     SET UP USING
         KFILL REQSIZE,6,VVREGSZ          REQUESTED REGION SIZE
         L     R7,CURRGNTP
         SR    R7,R8
         ST    R7,FWD
         KFILL CURSIZE,6,FWD              CURRENT REGION USED
         KFILL LIMIT,6,LDALIMIT           LIMIT FROM IEALIMIT
         S     R9,LDARSVPT                SUB LSQA START
         ST    R9,FWD                     SAVE LSQA SIZE
         KFILL LSQAB,6,FWD                LSQA SIZE
TPUT     LA    R0,PUTLEN
         LA    R1,PUTAREA
         LA    R2,PUTFLAGS
         SLL   R2,24
         OR    R1,R2
         TPUT  (1),(0),R
         TGET  FWD,1
         $EPILOG
         LTORG
EDITK    DC    X'40402020202120D2'
DWK      DS    D          DOUBLE WORD WORK AREA FOR CVD
FORMAT   DS    D          FORMAT WORK AREA
PUTFLAGS EQU   B'00100011'
FWD      DS    F
PUTAREA  DS    0F
         DC    X'27F5'
         DC    X'C1'              *CLEAR
         HMAP  R=24,C=79          * CLEAR
         HMAP  R=1,C=1             * CLEAR
         HMAP  R=1,C=1,RA=00      *  THE SCREEN OF A MODEL 2
         HMAP  R=1,C=1,MOD=2,A=(ASKIP,BRT)
         HMAP  R=2,C=2,A=(ASKIP,NORM)
         DC    C'CURRENT REGION STATISTICS FOR JOB '
JOBNAME  DC    CL8' '
         HMAP  R=4,C=2,A=(ASKIP,NORM)
         DC C'REQUESTED REGION SIZE: '
REQSIZE  DC    CL6' '
         HMAP  R=6,C=2,A=(ASKIP,NORM)
         DC C'CURRENT REGION TOP:    '
CURSIZE  DC    CL6' '
         HMAP  R=8,C=2,A=(ASKIP,NORM)
         DC C'IEALIMIT REGION SIZE:  '
LIMIT    DC    CL6' '
         HMAP  R=10,C=2,A=(ASKIP,NORM)
         DC C'LSQA SIZE:             '
LSQAB    DC    CL6' '
         HMAP  R=12,C=2,A=(ASKIP,NORM)
         DC C'PRIVATE ADDR SIZE:     '
PVTSIZE  DC    CL6' '
PUTLEN   EQU   *-PUTAREA
         EJECT
         CVT   DSECT=YES
         EJECT
         IHAASCB
         EJECT
         IHALDA
         EJECT
         IHAGDA
         END
