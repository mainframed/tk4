VSM00200 CSECT
         COPY VSMMACS                                          *LBD*
         TITLE 'VSM00200 - SQA/CSA SUBPOOL MONITOR'
***********************************************************************
*                                                                     *
*    SYSTEM -           VIRTUAL STORAGE MONITOR                       *
*                                                                     *
*    VERSION -          1.1                                           *
*                                                                     *
*    MODNAME -          VSM00100                                      *
*                                                                     *
*    AUTHOR -           R.P. SHANNON                                  *
*                       HALLMARK CARDS                                *
*                       ENFIELD DISTRIBUTION CENTER                   *
*                                                                     *
*    DATE -             9/1/82                                        *
*                                                                     *
*    FUNCTION -         THIS MODULE WILL SHOW SUBPOOL UTILIZATION     *
*                       IN CSA AND IN SQA. IT SHOWS THE AMOUNT OF     *
*                       FREE OR UNALLOCATED SPACE IN EACH SUBPOOL,    *
*                       THE LARGEST CONTIGUOUS FREE SPACE AND THE     *
*                       PERCENTAGE FREE OR UNUSED.                    *
*                                                                     *
*    ATTRIBUTES -       REUS                                          *
*                                                                     *
*    REGS AT INPUT-     R1   - INTER MODULE LINKAGE                   *
*                       R14  - RETURN ADDRESS                         *
*                       R15  - EPA                                    *
*                                                                     *
*    REG USAGE     -    R0-R7 - WORK REGS                             *
*                       R8   - GDA POINTER                            *
*                       R9   - INTERNAL BAL REG                       *
*                       R10  - LINKAGE AREA POINTER                   *
*                       R11  - BASE REG                               *
*                       R13  - SAVEAREA POINTER                       *
*                                                                     *
*    REGS AT EXIT -     R14  - RETURN ADDRESS                         *
*                                                                     *
*    NOTE -             SOME OF THE FIELD USAGE IN THIE MODULE CAN    *
*                       BE DETERMINED BY THE FIRST CHARACTER OF THE   *
*                       FIELD. THE KEYS ARE:                          *
*                            @ - THE FIELD IS IN THE INTER MODULE     *
*                                LINKAGE AREA                         *
*                            # - FIELD IS PART OF THE OUTPUT SCREEN   *
*                            $ - DSECT                                *
*                                                                     *
*    MACROS -           $DQE       => RYO DQESECT                     *
*                       $FBQE      => RYO FBQESECT                    *
*                       $FQESECT   => RYO FQESECT                     *
*                       $GDA       => RYO GDA                         *
*                       $PQE       => RYO PQESECT                     *
*                       $SPQE      => RYO SPQESECT                    *
*                       HMAP       => SCREEN BUILDER                  *
*                       PKEY       => AUTHORIZATION MACRO             *
*                                                                     *
*    MODS -                                                           *
*          VERSION 1.0  - BASE                                        *
*                                                                     *
*          VERSION 1.1  - 1. REMOVED AUTOMATIC UPSHIFT OF KEYBOARD    *
*                         2. STARTED USING THE SQASPLFT FIELD OF      *
*                            THE GDA TO DETERMINE AVAILABLE SPACE     *
*                            IN SQA. THIS CAN (WILL) CAUSE SQA        *
*                            FREE SPACE TO APPEAR > SQA SIZE, AND     *
*                            PCT FREE CAN EXCEED 100% (SEE CODE)      *
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11                  BASE REG
R12      EQU   12
R13      EQU   13                  GETMAINED ADDRESSABILITY
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  EPA/RC
RLINK    EQU   9
         EJECT
***********************************************************************
*        STANDARD OS LINKAGE                                          *
***********************************************************************
         SPACE
         PRINT NOGEN
         SPACE
         SAVE  (14,12),,VSM00200+&SYSDATE&SYSTIME  SAVE CALLER'S REGS
         LR    R11,R15                  LOAD BASE REG
         USING VSM00200,R11             CSECT ADDRESSABILITY
         LA    R2,SAVEAREA              CREATE SAVEAREA CHAIN
         ST    R2,8(R13)                FORWARD CHAIN POINTER
         ST    R13,SAVEAREA+4           BACKWARD CHAIN POINTER
         LR    R13,R2                   POINT TO MY SAVEAREA
         EJECT
***********************************************************************
*        FIND ALL NECESSARY ADDRESSES AND POINTERS                    *
***********************************************************************
P000100  DS    0H
         LR    R10,R1              LOAD INTER MOD LNKG ADDR R10
         USING $LNKSECT,R10        DSECT ADDRESSABILITY
         SPACE
P000200  DS    0H
         LA    R2,SQADQE           CLEAR
         LA    R3,CLEARLEN         WORK
         LR    R4,R2               AREA
         LA    R5,0                WITH
         MVCL  R2,R4               ZEROS
         SPACE
         SR    R0,R0               CLEAR R0
         SR    R2,R2               CLEAR R2
         SR    R3,R3               CLEAR R3
         SR    R4,R4               CLEAR R4
         SR    R5,R5               CLEAR R5
         SPACE
         L     R8,@GDA             GDA POINTER
         USING $GDA,R8             GDA DSECT ADDRESSABILITY
         SPACE
P000300  DS    0H
         L     R3,SQASPQEP         SQA SPQE POINTER
         LA    R3,0(R3)            CLEAR HIGH ORDER BYTE
         ST    R3,SQASPQE          SAVE THIS ADDR
         USING $SPQE,R3            SPQE DSECT ADDRESSABILITY
         L     R5,SPDQEPTR         SQA DQE POINTER
         LA    R5,0(R5)            CLEAR HIGH ORDER BYTE
         ST    R5,SQADQE           SAVE SQA DQE ADDR
         USING $DQE,R5             SQA DQE DSECT ADDRESSABILITY
         MVC   SQASIZE,DQELNTH     DETERMINE SQA SIZE
         DROP  R3                  DROP SQA SPQE ADDRESSABILITY
         DROP  R5                  DROP SQA DQE ADDRESSABILITY
         SPACE
         L     R3,CSAPQEP          CSA PQE POINTER
         LA    R3,0(R3)            CLEAR HIGH ORDER BYTE
         ST    R3,CSAPQE           SAVE THIS ADDR FOR LATER USE
         USING $PQE,R3             PQE DSECT ADDRESSABILITY
         MVC   CSASIZE,PQESIZE     DETERMINE CSA SIZE
         DROP  R3                  DROP CSA PQE ADDRESSABILITY
         EJECT
***********************************************************************
*        DETERMINE THE FREE SPACE, UNALLOCATED SPACE AND % UTILIZED   *
***********************************************************************
P000400  DS    0H
         LA    R6,$SPINFO          POINT TO SUBPOOL INFO AREA
         LA    R6,0(R6)            CLEAR HIGH ORDER BYTE
         L     R3,SQASPQE          POINT TO SQA SPQE
*        PKEY  S                   GET INTO KEY ZERO         CBT-AXC
*        SR    1,1                 GET INTO CBT KEY ZERO     CBT-AXC
*        SVC   254                 GET INTO CBT KEY ZERO     CBT-AXC
         MODESET KEY=ZERO,MODE=SUP KEY ZERO SUPERVISOR STATE CBT-AXC
         BAL   R9,SPQELOOP         PERFORM SUB-ROUTINE
         SPACE
         L     R3,SQADQE           POINT TO THE SQA DQE
         USING $DQE,R3             SQA DQE DSECT ADDRESSABILITY
         L     R0,DQEPTR           LOAD NEXT DQE PTR
         LTR   R0,R0               TEST FOR SQA OVERFLOW INTO CSA
         BZ    P000500             BRANCH IF NO OVERFLOW
         MVC   #SQAOVFL,MSG3       MOVE OVERFLOW MSG TO OUTPUT SCREEN
         SPACE
P000500  DS    0H
         DROP  R3                  DROP SQA DQE ADDRESSABILITY
         L     R3,CSAPQE           PTR TO CSA PSA => SETUP FOR PQELOOP
         SR    R1,R1               CLEAR R1 => SETUP FOR PQELOOP
         TM    GDAFLAGS,X'0C'      ANY SQA PROBLEMS ?
         BZ    P000700             NOPE => GO DO CSA
         TM    GDAFLAGS,X'08'      SQA THRESHOLD 1 ?
         BZ    P000600             IF NO, MUST BE THRESHOLD 2
         MVC   #SQATHRS,MSG1       MOVE THRESHOLD 1 MSG TO OUTPUT SCR
         B     P000600             GO DO CSA
         SPACE
P000600  DS    0H
         MVC   #SQATHRS,MSG2       MOVE THRESHOLD 2 MSG TO OUTPUT SCR
         SPACE
P000700  DS    0H
         BAL   R9,PQELOOP          PERFORM CSA UNALLOCATED SUBRTN
         L     R3,CSASPQEP         CSA SPQE POINTER
         BAL   R9,SPQELOOP         PERFORM FREE SPACE SUBRTN
*        PKEY  R                   GET OUT OF KEY ZERO       CBT-AXC
*        LA    1,4                 GET OUT OF CBT KEY ZERO   CBT-AXC
*        SVC   254                 GET OUT OF CBT KEY ZERO   CBT-AXC
 MODESET KEY=NZERO,MODE=PROB ORIG PROTECT KEY, PROBLEM STATE CBT-AXC
         EJECT
**********************************************************************
*        => MOD FOR VERSION 1.1 <=                                   *
*        WE USED TO SET ALL THE SQA VALUES EQUAL TO THE SUBPOOL      *
*        245 VALUES BECAUSE 245 IS SQA'S SUBPOOL. HOWEVER THE        *
*        SQASPLFT FIELD IN THE GDA SHOWS SQA FREE SPACE AS           *
*        SUBPOOL 245 FS + CSA UNALLOCATED SPACE. WE NOW USE          *
*        SQASPLFT TO REFLECT SQA FREE SPACE. THEREFORE IS IS NOW     *
*        POSSIBLE TO SHOW SQA FREE SPACE > SQA SIZE AND PCT FREE     *
*        MAY EXCEED 100%. I CONSIDER THE VALUES IN SP245 TO BE       *
*        THE TRUE VALUES FOR SQA. BY USING THE SQASPLFT VALUE FOR    *
*        SQA WE  REVEAL WHAT MVS THINKS IS AVAILABLE. ALTHOUGH       *
*        MVS CAN GETMAIN THE REMAINING CSA UNALLOCATED SPACE INTO    *
*        SP239 THIS CERTAINLY IS NOT DESIREABLE. I DON'T KNOW WHEN   *
*        MVS UPDATES SQASPLFT BUT IT USUALLY VARIES BY A FEW K FROM  *
*        THE SUM OF CSA UNALLOCATED SPACE + SP245 FREE SPACE AS WE   *
*        CALCULATE THEM.                                             *
**********************************************************************
P000750  DS    0H
         SR    R4,R4               CLEAR R4
         L     R5,TEN              SET R5 TO 10
         MVC   SQAUNAL,SQASPLFT    DETERMINE UNALLOCATED SPACE IN SQA
         LA    R6,$SPINFO          POINT TO SPINFO AREA
         LA    R6,80(R6)           OFFSET TO SP245
         USING $SPDSECT,R6         DSECT ADDRESSABILITY
         CLC   CSAUNAL,$SPLARGE    CSA LARGEST FS > SP245 LARGEST ?
         BH    P000760             BIF YES
         MVC   SQALARGE,$SPLARGE   MAKE SQA LARGEST FS = SP245 LARGEST
         B     P000770             BRANCH
         SPACE
P000760  DS    0H
         MVC   SQALARGE,CSAUNAL    MAKE SQA LARGEST FS = CSA LARGEST
         SPACE
P000770  DS    0H
         DROP  R6                  DROP DSECT ADDRESSABILITY
         LA    R6,SQASIZE          POINT TO SQA SIZE
         USING $SPDSECT,R6         DSECT ADDRESSABILITY
*                                  WE'RE CHEATING HERE AND USING THE
*                                  SUBPOOL AREA DSECT BUT IT WORKS OK
         BAL   R9,SPLOOP01         GO DO SUB-POOL CALCS FOR SQA
         MVC   #SQAFREE,WRKFREE    MOVE SQA FREE SPACE TO SCREEN
         MVC   #SQALRGE,WRKLARGE   MOVE SQA LARGEST UNUSED SP TO SCREEN
         MVC   #SQASIZE,WRKSIZE    MOVE SQA SIZE TO SCREEN
         MVC   #SQAPCT,WRKPCT      MOVE SQA PCT UTILIZED TO SCREEN
         SPACE
P000800  DS    0H
         SR    R4,R4               CLEAR R4
         MVC   FSACCUM,ZERO        SET FREE SPACE ACCUM TO ZERO
         SPACE
         LA    R6,$SPINFO          POINT TO SP INFO AREA
         LA    R6,80(R6)           OFFSET TO SUBPOOL 245
         BAL   R9,SPLOOP           GO DO SUB-POOL CALCS FOR SP 245
         SPACE
         MVC   #245NUMB,WRKNUMB    MOVE SP NUMBER TO SCREEN
         MVC   #245FREE,WRKFREE    MOVE SP FREE SPACE TO SCREEN
         MVC   #245LRGE,WRKLARGE   MOVE SP LARGEST UNUSED SP TO SCREEN
         MVC   #245SIZE,WRKSIZE    MOVE SP SIZE TO SCREEN
         MVC   #245PCT,WRKPCT      MOVE SP PCT UTILIZED TO SCREEN
         SPACE
         SPACE
P000900  DS    0H
         SR    R2,R2               CLEAR R2
         SR    R4,R4               CLEAR R4
         L     R3,CSASIZE          COMPUTE CSA IN K
         SRA   R3,10               BY DIVIDING BY 1024
         CVD   R3,DBLEWORD         CONVERT TO DECIMAL
         MVC   #CSASIZE,EDIT2      MOVE IN PATTERN
         ED    #CSASIZE,DBLEWORD+5 EDIT
         EJECT
P001000  DS    0H
         SR    R2,R2               CLEAR R2
         L     R3,CSAUNAL          COMPUTE CSA UNAL SPACE IN K
         SRA   R3,10               BY DIVIDING BY 1024
         CVD   R3,DBLEWORD         CONVERT TO DECIMAL
         MVC   #CSAUNAL,EDIT2      MOVE IN PATTERN
         ED    #CSAUNAL,DBLEWORD+5 EDIT
         SPACE
P001100  DS    0H
         SR    R2,R2               CLEAR R2
         L     R3,CSALARG          COMPUTE CSA LARGEST UNAL SPACE IN K
         SRA   R3,10               BY DIVIDING BY 1024
         CVD   R3,DBLEWORD         CONVERT TO DECIMAL
         MVC   #CSALARG,EDIT2      MOVE IN PATTERN
         ED    #CSALARG,DBLEWORD+5 EDIT
         SPACE
P001200  DS    0H
         SR    R2,R2               CLEAR R2
         SR    R4,R4               CLEAR R4
         LA    R4,100(R4)          SET R4 TO 100
         L     R3,CSASIZE          LOAD CSA SIZE INTO R3
         DR    R2,R4               DIVIDE SIZE BY 100
         LR    R4,R3               MOVE SIZE/100 INTO R4
         SR    R2,R2               CLEAR R2
         L     R3,CSAUNAL          LOAD SUBPOOL FS INTO R3
         DR    R2,R4               COMPUTE % FREE
         CVD   R3,DBLEWORD         CONVERT THE PCT TO DECIMAL
         MVC   #CSAPCT,EDIT3       MOVE IN EDIT PATTERN
         ED    #CSAPCT,DBLEWORD+6  EDIT THE % UTILIZED
         SPACE
**********************************************************************
*        DO THE SUB-POOL CALCULATIONS FOR CSA                        *
**********************************************************************
P001300  DS    0H
         LA    R6,$SPINFO          POINT TO SP INFO AREA
         LA    R6,0(R6)            CLEAR HIGH ORDER BYTE
         BAL   R9,SPLOOP           GO DO SUB-POOL CALCS
         MVC   #227NUMB,WRKNUMB    MOVE SP NUMBER TO SCREEN
         MVC   #227SIZE,WRKSIZE    MOVE SP SIZE TO SCREEN
         MVC   #227FREE,WRKFREE    MOVE SP FREE SPACE TO SCREEN
         MVC   #227LRGE,WRKLARGE   MOVE SP LARGEST UNUSED SP TO SCREEN
         MVC   #227PCT,WRKPCT      MOVE SP PCT UTILIZED TO SCREEN
         SPACE
         LA    R6,16(R6)           ADDRESS NEXT PART OF SP INFO AREA
         BAL   R9,SPLOOP           GO DO SUB-POOL CALCS
         MVC   #228SIZE,WRKSIZE    MOVE SP SIZE TO SCREEN
         MVC   #228NUMB,WRKNUMB    MOVE SP NUMBER TO SCREEN
         MVC   #228FREE,WRKFREE    MOVE SP FREE SPACE TO SCREEN
         MVC   #228LRGE,WRKLARGE   MOVE SP LARGEST UNUSED SP TO SCREEN
         MVC   #228PCT,WRKPCT      MOVE SP PCT UTILIZED TO SCREEN
         SPACE
         LA    R6,16(R6)           ADDRESS NEXT PART OF SP INFO AREA
         BAL   R9,SPLOOP           GO DO SUB-POOL CALCS
         MVC   #231NUMB,WRKNUMB    MOVE SP NUMBER TO SCREEN
         MVC   #231FREE,WRKFREE    MOVE SP FREE SPACE TO SCREEN
         MVC   #231SIZE,WRKSIZE    MOVE SP SIZE TO SCREEN
         MVC   #231LRGE,WRKLARGE   MOVE SP LARGEST UNUSED SP TO SCREEN
         MVC   #231PCT,WRKPCT      MOVE SP PCT UTILIZED TO SCREEN
         SPACE
         LA    R6,16(R6)           ADDRESS NEXT PART OF SP INFO AREA
         USING $SPDSECT,R6         DSECT ADDRESSABILITY
         MVC   $SPNUMB,SP239       INDICATE THIS IS SUB-POOL 239
         DROP  R6                  DROP DSECT ADDRESSABILITY
         BAL   R9,SPLOOP           GO DO SUB-POOL CALCS
         MVC   #239SIZE,WRKSIZE    MOVE SP SIZE TO SCREEN
         MVC   #239NUMB,WRKNUMB    MOVE SP NUMBER TO SCREEN
         MVC   #239FREE,WRKFREE    MOVE SP FREE SPACE TO SCREEN
         MVC   #239LRGE,WRKLARGE   MOVE SP LARGEST UNSUED SP TO SCREEN
         MVC   #239PCT,WRKPCT      MOVE SP PCT UTILIZED TO SCREEN
         SPACE
         LA    R6,16(R6)           ADDRESS NEXT PART OF SP INFO AREA
         BAL   R9,SPLOOP           GO DO SUB-POOL CALCS
         MVC   #241SIZE,WRKSIZE    MOVE SP SIZE TO SCREEN
         MVC   #241NUMB,WRKNUMB    MOVE SP NUMBER TO SCREEN
         MVC   #241FREE,WRKFREE    MOVE SP FREE SPACE TO SCREEN
         MVC   #241LRGE,WRKLARGE   MOVE SP LARGEST UNSUED SP TO SCREEN
         MVC   #241PCT,WRKPCT      MOVE SP PCT UTILIZED TO SCREEN
         EJECT
***********************************************************************
*        OUTPUT THE SCREEN                                            *
***********************************************************************
P001400  DS    0H
         MVI   @RSPONSE,C' '         MOVE BLANK TO RESPONSE
         LA    R0,#SCREEN            LOAD SCREEN ADDR
         ST    R0,@PUTAREA           STORE IN LINKAGE AREA
         LA    R0,#SCRNLEN           LOAD SCREEN LENGTH
         ST    R0,@PUTLEN            STORE IN LINKAGE AREA
         LR    R1,R10                POINT R1 TO LINKAGE AREA
         L     R15,@IOMOD            LOAD ADDRESS OF TERMINAL I/O MOD
         BALR  R14,R15               GO DO TERMINAL IO
         SPACE
         CLI   @RSPONSE,C'R'         REFRESH SCREEN ?
         BE    P000100               BIF YES
P999999  DS   0H
         L    R13,SAVEAREA+4         PREPARE TO BOOGIE
         RETURN (14,12)              B O O G I E
         EJECT
**********************************************************************
*                                                                    *
*        => PQELOOP <=                                               *
*                                                                    *
*        TOTAL THE UNALLOCATED SPACE IN CSA BY ADDING                *
*        THE SIZE OF ALL THE FBQE'S OF ALL THE PQE'S                 *
*                                                                    *
*        LOOP STRUCTURE FOR THE FOLLOWING CODE:                      *
*                                                                    *
*        => PQELOOP                                                  *
*       ×                                                            *
*       ×      => FBQELOOP                                           *
*       ×     ×                                                      *
*       ×      => FBQEEND                                            *
*       ×                                                            *
*        => PQEEND                                                   *
*                                                                    *
*                                                                    *
*       REGISTER USAGE FOR THIS SUBROUTINE:                          *
*                                                                    *
*       R0 =>   TEMPORARY WORK REGISTER                              *
*       R1 =>   UNALLOCATED SPACE ACCUMULATOR                        *
*       R3 =>   PQE POINTER                                          *
*       R4 =>   FBQE POINTER                                         *
*       R9 =>   RETURN ADDRESS                                       *
*                                                                    *
**********************************************************************
PQELOOP  DS    0H
         LTR   R3,R3               ANY (MORE) PQE'S ?
         BZ    PQEEND              BRANCH IF NO
         USING $PQE,R3             PQE DSECT ADDRESSABILITY
         L     R4,PQEFFBQE         POINT TO FBQE
         SPACE
FBQELOOP DS    0H
         CR    R3,R4               ANY (MORE) FBQE'S ?
         BE    FBQEEND             BRANCH IF NO
         USING $FBQE,R4            FBQE DSECT ADDRESSABILITY
         A     R1,SIZE             ACCUMULATE UNALLOCATED SPACE
         CLC   CSALARG,SIZE        LARGEST UNAL SPACE SO FAR ?
         BNL   FBQEREST            BRANCH IF NO
         MVC   CSALARG,SIZE        SAVE IF YES
         SPACE
FBQEREST DS    0H
         L     R0,FWDPTR           POINT TO NEXT FBQE
         DROP  R4                  DROP FBQE ADDRESSABILITY
         LR    R4,R0               LOAD NEXT FBQE ADDR INTO R4
         B     FBQELOOP            GO DOIT AGAIN
         SPACE
FBQEEND  DS    0H
         L     R0,PQEFPQE          POINT TO NEXT PQE
         LA    R0,0(R0)            CLEAR HIGH ORDER BYTE
         DROP  R3                  DROP PQE ADDRESSABILITY
         LR    R3,R0               POINT R3 TO NEXT PQE
         B     PQELOOP             GO DO PQE AGAIN
         SPACE
PQEEND   DS    0H
         ST    R1,CSAUNAL          STORE UNALLOCATED SPACE ACCUM
         BR    R9                  RETURN
         EJECT
**********************************************************************
*                                                                    *
*       => SPQELOOP <=                                               *
*                                                                    *
*       COMPUTE SUB-POOL SIZE BY FOLLOWING THE DQE CHAINS            *
*       COMPUTE THE SUB-POOL FREE SPACE BY FOLLOWING THE FQE CHAINS  *
*                                                                    *
*                                                                    *
*       LOOP STRUCTURE FOR THE FOLLOWING CODE:                       *
*                                                                    *
*        => SPQELOOP                                                 *
*       ×                                                            *
*       ×       => DQELOOP                                           *
*       ×      ×                                                     *
*       ×      ×       => FQELOOP                                    *
*       ×      ×     ×                                               *
*       ×      ×       => FQEEND                                     *
*       ×      ×                                                     *
*       ×       => DQEEND                                            *
*       ×                                                            *
*        => SPQEEND                                                  *
*                                                                    *
*                                                                    *
*       REGISTER USAGE FOR THIS SUBROUTINE:                          *
*                                                                    *
*       R0 =>   TEMPORARY WORK REGISTER                              *
*       R1 =>   SUB-POOL SIZE ACCUMULATOR;CLEARED BEFORE ENTRY       *
*       R2 =>   SUB-POOL FREE SPACE ACCUMULATOR;CLEARED BEFORE ENTRY *
*       R3 =>   SPQE POINTER                                         *
*       R4 =>   FQE POINTER                                          *
*       R5 =>   FQE  POINTER                                         *
*       R6 =>   SUBPOOL INFO AREA POINTER                            *
*       R9 =>   RETURN ADDRESS                                       *
*                                                                    *
**********************************************************************
SPQELOOP DS    0H
         LA    R3,0(R3)            CLEAR HIGH ORDER BYTE
         LTR   R3,R3               ANY (MORE) SPQE'S ?
         BZ    ENDSPQE             BRANCH IF NO
         USING $SPQE,R3            SPQE DSECT ADDRESSABILITY
         CLC   SPLAST,SPQEID       SAME SUBPOOL AS LAST TIME ?
         BE    SPQEREST            BRANCH IF YES
         MVC   SPLAST,SPQEID       SAVE SUBPOOL ID
         LA    R6,$SPINFO          POINT TO SPINFO AREA
         CLC   SPQEID,SP239        SUBPOOL 239 ?
         BE    SPQE239             BRANCH IF YES
         BH    SPQE241             BRANCH IF HIGH
         CLC   SPQEID,SP228        SUBPOOL 228 ?
         BE    SPQE228             BRANCH IF YES
         BH    SPQE231             BRANCH IF HIGH
         SPACE
SPQE227  DS    0H
         LA    R6,0(R6)            CLEAR HIGH ORDER BYTE FOR SP 227
         B     SPQEREST            KEEP GOING
         SPACE
SPQE228  DS    0H
         LA    R6,16(R6)           OFFSET FOR SUB-POOL 228
         B     SPQEREST            KEEP GOING
         SPACE
SPQE231  DS    0H
         LA    R6,32(R6)           OFFSET FOR SUB-POOL 231
         B     SPQEREST            KEEP GOING
         SPACE
SPQE239  DS    0H
         LA    R6,48(R6)           OFFSET FOR SUBPOOL 239
         B     SPQEREST            KEEP GOING
         SPACE
SPQE241  DS    0H
         CLC   SPQEID,SP241        SUBPOOL 241 ?
         BH    SPQE245             BRANCH IF NO
         LA    R6,64(R6)           OFFSET FOR SUBPOOL 241
         B     SPQEREST            CONTINUE
         SPACE
SPQE245  DS    0H
         LA    R6,80(R6)           OFFSET FOR SUBPOOL 245
         SPACE
SPQEREST DS    0H
         USING $SPDSECT,R6         SUBPOOL AREA DSECT ADDRESSABILITY
         MVC   $SPNUMB,SPQEID      DETERMINE SUBPOOL NUMBER
         L     R4,SPDQEPTR         LOAD DQE ADDR INTO R4
         L     R1,$SPSIZE          RESET R1
         L     R2,$SPFREE          RESET R2
         SPACE
DQELOOP  DS    0H
         LTR   R4,R4               ANY (MORE) DQE'S ?
         BZ    ENDDQE              BRANCH IF NO
         USING $DQE,R4             CSA DQE ADDRESSABILITY
         A     R1,DQELNTH          ACCUMULATE SUB-POOL LENGTH
         L     R5,DQFQEPTR         POINTER TO FIRST FQE
         SPACE
FQELOOP  DS    0H
         LA    R5,0(R5)            CLEAR FLAGS
         LTR   R5,R5               ANY (MORE) FQE'S ?
         BZ    ENDFQE              BRANCH IF NO
         USING $FQESECT,R5         FQE DSECT ADDRESSABILITY
         A     R2,FQELNTH          ACCUMULATE SUB-POOL FREE SPACE
         CLC   $SPLARGE,FQELNTH    IS THIS THE LARGEST FREE SP SO FAR ?
         BNL   FQEREST             BRANCH IF NO
         MVC   $SPLARGE,FQELNTH    SAVE IF YES
         SPACE
FQEREST  DS    0H
         L     R0,FQEPTR           LOAD POINTER TO NEXT FQE
         DROP  R5                  DROP DSECT ADDRESSABILITY
         LR    R5,R0               LOAD NEXT FQE ADDR INTO R5
         B     FQELOOP             GO DO FQE LOOP AGAIN
         SPACE
ENDFQE   DS    0H
         L     R0,DQEPTR           LOAD POINTER TO NEXT DQE
         DROP  R4                  DROP DQE DSECT ADDRESSABILITY
         LR    R4,R0               LOAD NEXT DQE ADDR INTO R4
         B     DQELOOP             GO DO DQE LOOP AGAIN
         SPACE
ENDDQE   DS    0H
         ST    R1,$SPSIZE          STORE SUB-POOL SIZE
         ST    R2,$SPFREE          STORE SUB-POOL FREE SPACE
         TM    SPQEFLGS,LASTSPQE   LAST SPQE ON THE CHAIN ?
         BO    ENDSPQE             BRANCH IF YES
         L     R0,SPQEAD           LOAD POINTER TO NEXT SPQE
         DROP  R3                  DROP SPQE DSECT ADDRESSABILITY
         LR    R3,R0               LOAD NEXT SPQE ADDR INTO R3
         DROP  R6                  DROP SUBPOOL DSECT ADDRESSABILITY
         B     SPQELOOP            GO DO SPQE LOOP AGAIN
         SPACE
ENDSPQE  DS    0H
         BR    R9                  RETURN
         EJECT
**********************************************************************
*                                                                    *
*       => SPLOOP <=                                                 *
*                                                                    *
*       PREPARE THE DATA IN SPINFO AREA FOR OUTPUT                   *
*                                                                    *
*       REGISTER USAGE FOR THIS SUBROUTINE:                          *
*                                                                    *
*       R0  =>   TEMPORARY WORK REGISTER                             *
*       R1  =>   TEMPORARY WORK REGISTER                             *
*       R2  =>   TEMPORARY WORK REGISTER                             *
*       R3  =>   TEMPORARY WORK REGISTER                             *
*       R4  =>   TEMPORARY WORK REGISTER                             *
*       R5  =>   TEMPORARY WORK REGISTER                             *
*       R6  =>   SPINFO AREA POINTER
*       R9  =>   RETURN ADDRESS                                      *
*                                                                    *
**********************************************************************
SPLOOP   DS    0H
         USING $SPDSECT,R6         SP INFO DSECT ADDRESSABILITY
         SR    R0,R0               CLEAR R0
         SR    R4,R4               CLEAR R4
         IC    R0,$SPNUMB          LOAD SUBPOOL ID INTO R0
         SPACE
         DS    0H
         CVD   R0,DBLEWORD         CONVERT
         MVC   WRKNUMB,EDIT1       AND EDIT
         ED    WRKNUMB,DBLEWORD+6  SUBPOOL ID
         SPACE
SPLOOP01 DS    0H
         SR    R2,R2               DETERMINE SUB-POOL SIZE IN K
         L     R3,$SPSIZE          BY DIVIDING THE SIZE IN BYTES
         SRA   R3,10               BY 1024
         CVD   R3,DBLEWORD         CONVERT TO DECIMAL
         MVC   WRKSIZE,EDIT2       MOVE IN PATTERN
         ED    WRKSIZE,DBLEWORD+5  EDIT
         SPACE
         SR    R2,R2               DETERMINE SUB-POOL FS IN K
         L     R3,$SPFREE          BY DIVIDING THE SIZE IN BYTES
         SRA   R3,10               BY 1024
         CVD   R3,DBLEWORD         CONVERT TO DECIMAL
         MVC   WRKFREE,EDIT2       MOVE IN PATTERN
         ED    WRKFREE,DBLEWORD+5  EDIT
         SPACE
         SR    R2,R2               CLEAR R2
         SR    R2,R2               DETERMINE SUBPOOL LARGEST FS IN K
         L     R3,$SPLARGE         BY DIVIDING THE SIZE IN BYTES
         SRA   R3,10               BY 1024
         CVD   R3,DBLEWORD         CONVERT TO DECIMAL
         MVC   WRKLARGE,EDIT2      MOVE IN PATTERN
         ED    WRKLARGE,DBLEWORD+5 EDIT
         SPACE
         CLC   $SPSIZE,ZERO        IF SUBPOOL SIZE ^ ZERO
         BNE   SPLOOP10            GO COMPUTE PCT USED
         CVD   R2,DBLEWORD         ELSE
         MVC   WRKPCT,EDIT3        SET PCT USED
         ED    WRKPCT,DBLEWORD+6   TO ZERO
         B     ENDSPLP             BRANCH TO LOOP END
         SPACE
SPLOOP10 DS    0H
         SR    R2,R2               CLEAR R2
         SR    R4,R4               CLEAR R4
         LA    R4,100(R4)          SET R4 TO 100
         L     R3,$SPSIZE          LOAD SUBPOOL SIZE INTO R3
         DR    R2,R4               DIVIDE SIZE BY 100
         LR    R4,R3               MOVE SIZE/100 INTO R4
         SR    R2,R2               CLEAR R2
         L     R3,$SPFREE          LOAD SUBPOOL FS INTO R3
         DR    R2,R4               COMPUTE % FREE
         CVD   R3,DBLEWORD         CONVERT THE PCT TO DECIMAL
         MVC   WRKPCT,EDIT3        MOVE IN EDIT PATTERN
         ED    WRKPCT,DBLEWORD+6   EDIT THE % UTILIZED
         SPACE
ENDSPLP  DS    0H
         BR    R9                  RETURN
         EJECT
SAVEAREA DS    18F                MY SAVE AREA
SQADQE   DS    F                  SQA DQE ADDR
SQASPQE  DS    F                  SQA SPQE ADDR
CSAPQE   DS    F                  CSA 1ST PQE ADDR
CSASIZE  DS    F                  CSA SIZE
FULLWORD DS    F
DBLEWORD DS    D                  DOUBLE WORD WORK AREA
TENBYTE  DS    XL10
SPLAST   DS    B                  LAST SUBPOOL HOLD
FSACCUM  DS    F                  ACCUMULATOR FOR SUB-POOL FREE SPACE
WRKNUMB  DS    F                  WORK AREA FOR SUB-POOL SIZE
WRKSIZE  DS    CL6                WORK AREA FOR SUB-POOL SIZE
WRKFREE  DS    CL6                WORK AREA FOR SUB-POOL FREE SPACE
WRKLARGE DS    CL6                WORK AREA FOR SUB-POOL UNALLOC SPACE
WRKPCT   DS    F                  WORK AREA FOR SUB-POOL %
SQASIZE  DS    F                  SQA SIZE
SQAUNAL  DS    F                  SQA UNALLOCATED SPACE ACCUM
SQALARGE DS    F                  SQA LARGEST FREE SPACE
CSAUNAL  DS    F                  CSA UNALLOCATED SPACE ACCUM
CSALARG  DS    F                  CSA LARGEST UNALLOCATED SPACE
         DS   0F
$SPINFO  DS   CL96                THIS AREA SHOULD BE ON FW BOUNDARY
CLEARLEN EQU  *-SQADQE            LENGTH FOR CLEAR
TEN      DC   X'0000000A'         TEN IN HEX
WAITTIME DC   X'00000320'         10 SECONDS IN 1/100'S
MSG1     DC   C'=====>      SQA THRESHOLD 1     <====='
MSG2     DC   C'=====>      SQA THRESHOLD 2     <====='
MSG3     DC   C'=====> SQA OVERFLOWING INTO CSA <====='
ZERO     DC   F'0'
EDIT1    DC   X'40202120'         EDIT PATTERN FOR SIZE IN MEG
EDIT2    DC   X'402020202120'     EDIT PATTERN FOR SIZE IN K
EDIT3    DC   X'40202120'         EDIT PATTERN FOR PCT
SP228    DC   X'E4'               SUBPOOL 228
SP239    DC   X'EF'               SUBPOOL 239
SP241    DC   X'F1'               SUBPOOL 241
         DS   0F
#SCREEN  DS   0CL584
         DC   X'C31140403C404000'
         HMAP R=1,C=1,A=(ASKIP,BRT)
         DC   C'SELECT OPTION => '
         HMAP R1,C=18,A=(UNPROT,FSET,IC)
         DS   CL1
         HMAP R=3,C=30,A=(ASKIP,BRT)
         DC   C'SQA/CSA UTILIZATION'
         HMAP R=5,C=35,A=(ASKIP,NORM)
         DC   C'UNAL OR     LARGEST     PCT UNAL'
         HMAP R=6,C=16,A=(ASKIP,NORM)
         DC   C'ID        SIZE     FREE SP    UNAL/FREE    OR FREE'
         HMAP R=8,C=15,A=(ASKIP,NORM)
         DC   C'SQA'
         HMAP R=8,C=23,A=(ASKIP,NORM)
#SQASIZE DS   CL6
         HMAP R=8,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=8,C=33,A=(ASKIP,NORM)
#SQAFREE DS   CL6
         HMAP R=8,C=40,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=8,C=45,A=(ASKIP,NORM)
#SQALRGE DS   CL6
         HMAP R=8,C=52,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=8,C=60,A=(ASKIP,NORM)
#SQAPCT  DS   CL4
         HMAP R=9,C=13,A=(ASKIP,NORM)
         DC   C'SP'
         HMAP R=9,C=16,A=(ASKIP,NORM)
#245NUMB DS   CL4
         HMAP R=9,C=23,A=(ASKIP,NORM)
#245SIZE DS   CL6
         HMAP R=9,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=9,C=33,A=(ASKIP,NORM)
#245FREE DS   CL6
         HMAP R=9,C=40,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=9,C=45,A=(ASKIP,NORM)
#245LRGE DS   CL6
         HMAP R=9,C=52,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=9,C=60,A=(ASKIP,NORM)
#245PCT  DS   CL4
         HMAP R=10,C=13,A=(ASKIP,NORM)
         DC   54C'-'
         HMAP R=11,C=15,A=(ASKIP,NORM)
         DC   C'CSA'
         HMAP R=11,C=23,A=(ASKIP,NORM)
#CSASIZE DS   CL6
         HMAP R=11,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=11,C=33,A=(ASKIP,NORM)
#CSAUNAL DS   CL6
         HMAP R=11,C=40,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=11,C=45,A=(ASKIP,NORM)
#CSALARG DS   CL6
         HMAP R=11,C=52,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=11,C=60,A=(ASKIP,NORM)
#CSAPCT  DS   CL4
         HMAP R=12,C=13,A=(ASKIP,NORM)
         DC   C'SP'
         HMAP R=12,C=16,A=(ASKIP,NORM)
#227NUMB DS   CL4
         HMAP R=12,C=23,A=(ASKIP,NORM)
#227SIZE DS   CL6
         HMAP R=12,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=12,C=33,A=(ASKIP,NORM)
#227FREE DS   CL6
         HMAP R=12,C=40,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=12,C=45,A=(ASKIP,NORM)
#227LRGE DS   CL6
         HMAP R=12,C=52,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=12,C=60,A=(ASKIP,NORM)
#227PCT  DS   CL4
         HMAP R=13,C=13,A=(ASKIP,NORM)
         DC   C'SP'
         HMAP R=13,C=16,A=(ASKIP,NORM)
#228NUMB DS   CL4
         HMAP R=13,C=23,A=(ASKIP,NORM)
#228SIZE DS   CL6
         HMAP R=13,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=13,C=33,A=(ASKIP,NORM)
#228FREE DS   CL6
         HMAP R=13,C=40,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=13,C=45,A=(ASKIP,NORM)
#228LRGE DS   CL6
         HMAP R=13,C=52,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=13,C=60,A=(ASKIP,NORM)
#228PCT  DS   CL4
         HMAP R=14,C=13,A=(ASKIP,NORM)
         DC   C'SP'
         HMAP R=14,C=16,A=(ASKIP,NORM)
#231NUMB DS   CL4
         HMAP R=14,C=23,A=(ASKIP,NORM)
#231SIZE DS   CL6
         HMAP R=14,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=14,C=33,A=(ASKIP,NORM)
#231FREE DS   CL6
         HMAP R=14,C=40,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=14,C=45,A=(ASKIP,NORM)
#231LRGE DS   CL6
         HMAP R=14,C=52,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=14,C=60,A=(ASKIP,NORM)
#231PCT  DS   CL4
         HMAP R=15,C=13,A=(ASKIP,NORM)
         DC   C'SP'
         HMAP R=15,C=16,A=(ASKIP,NORM)
#239NUMB DS   CL4
         HMAP R=15,C=23,A=(ASKIP,NORM)
#239SIZE DS   CL6
         HMAP R=15,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=15,C=33,A=(ASKIP,NORM)
#239FREE DS   CL6
         HMAP R=15,C=40,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=15,C=45,A=(ASKIP,NORM)
#239LRGE DS   CL6
         HMAP R=15,C=52,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=15,C=60,A=(ASKIP,NORM)
#239PCT  DS   CL4
         HMAP R=16,C=13,A=(ASKIP,NORM)
         DC   C'SP'
         HMAP R=16,C=16,A=(ASKIP,NORM)
#241NUMB DS   CL4
         HMAP R=16,C=23,A=(ASKIP,NORM)
#241SIZE DS   CL6
         HMAP R=16,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=16,C=33,A=(ASKIP,NORM)
#241FREE DS   CL6
         HMAP R=16,C=40,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=16,C=45,A=(ASKIP,NORM)
#241LRGE DS   CL6
         HMAP R=16,C=52,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=16,C=60,A=(ASKIP,NORM)
#241PCT  DS   CL4
         HMAP R=18,C=21,A=(ASKIP,BRT)
#SQATHRS DC   38C' '
         HMAP R=19,C=21,A=(ASKIP,BRT)
#SQAOVFL DC   38C' '
#SCRNLEN EQU  *-#SCREEN
$SPDSECT DSECT
$SPSIZE  DS   F
$SPFREE  DS   F
$SPLARGE DS   F
$SPNUMB  DS   CL1
$UNUSED  DS   CL3
         SPACE
         $DQE                     DESCRIPTOR QUEUE ELEMENT DSECT
         $FBQE                    FREE BLOCK QUEUE ELEMENT DSECT
         $FQESECT                 FREE QUEUE ELEMENT DSECT
         $GDA                     GENERAL DATA AREA DSECT
         $PQE                     PARTITIONED QUEUE ELEMENT
         $SPQE                    SUBPOOL QUEUE ELEMENT DSECT
         $LNKSECT
         LTORG
         END
