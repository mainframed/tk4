          PUNCH ' SETCODE AC(1)'
**********************************************************************
*                                                                    *
*    SACOPY                                                          *
*              THIS PROGRAM IS DESIGNED TO COPY A                    *
*              STAND ALONE DUMP FROM FILE IEFRDER                    *
*              TO A DYNAMICALLY CREATED FILE ON THE                  *
*              MSVGP SADUMP.                                         *
*                                                                    *
*              A SYSPRINT FILE IS CREATED WHICH CONTAINS             *
*              THE DATASET NAME OF THE OUTPUT FILE AND               *
*              A COUNT OF THE # OF RECORDS IN THE O/P FILE.          *
*                                                                    *
*              THE DUMPLOG FILE IS A MOD DATA SET THAT CONTAINS      *
*              THE DSN, CPUID, DATE, TIME, AND TITLE OF THE DUMP     *
*              THAT IS MIGRATED.                                     *
*                                                                    *
*              THE JCL TO EXECUTE SACOPY ARE:                        *
*                                                                    *
*                   //A EXEC PGM=SACOPY                              *
*                   //SYSPRINT DD SYSOUT=A                           *
*                   //IEFRDER  DD DSN=SYS1.DUMP01,DISP=SHR,          *
*                   //         UNIT=3400-5,VOL=SER=SADUMP            *
*                                                                    *
*                                                                    *
*        MACROS : $PROLOG,$EPILOG,                                   *
*                 ABEND,OPEN,CLOSE,GET,PUT,S99FAIL                   *
*                 CALL,DCB,REGEQU,IEFZB4D0,IEFZB4D2,                 *
*                 AMDDATA,WTOR,WAIT,ALLOC,DYNSPACE                   *
*                                                                    *
**********************************************************************
* CHANGE HISTORY:                                                    *
*        12/06/85 - DUMPLOG IS NOW DYNAMICALLY ALLOCATED             *
*                                 AND SACOPY MUST NOW BE APF AUTHOR. *
*                   BOTH 370 AND  XA DUMPS ARE NOW SUPPORTED         *
*        04/15/86 - CHANGE TO ALLOC SPACE=(CYL,(200,200),RLSE)       *
*                   ONLY READ FILE ONCE..                            *
*        05/08/86 - CHANGE TO ALLOC SPACE=(CYL,(403,403))  *LBD 04/86*
*                   AND TO USE MSVGP=SADUMP                *LBD 04/86*
*        05/12/86 - CHANGE TO USE ALLOC MACRO FOR OUTPUT   *LBD 05/86*
**********************************************************************
SACOPY   $PROLOG R11,R12
         EJECT
         ALLOC DDN='DUMPWORK',UNIT='SYSDA',DSN='&&TEMP',   *LBD 05/86* X
               SPACE=(CYL,(5,5),RLSE),DISP=(NEW,DELETE),   *LBD 05/86* X
               ERROR=DWFAIL                                *LBD 05/86*
         B     DWOK                                        *LBD 05/86*
DWFAIL   S99FAIL ,                                         *LBD 05/86*
         ABEND 3,DUMP              UNABLE TO ALLOC TEMP    *LBD 05/86*
         SPACE 2                                           *LBD 05/86*
DWOK     DS    0H                                          *LBD 05/86*
         EJECT
         OPEN  (DUMPIN,,DUMPWORK,OUTPUT)
         SR    R8,R8
         LA    R9,10
READHDR  GET   DUMPIN           READ AN INPUT RECORD
         LR    R10,R1
         PUT   DUMPWORK,(10)                               *LBD 05/86*
         USING PRDINPUT,R10
         CLI   PRDRECID,PRDHDREC HEADER RECORD ?
         BE    HEADER           YES - GO PROCESS IT
         BCT   R9,READHDR       NO LOOP THRU
         WTO   'SACOPY - NO HEADER RECORD FOUND - PROBABLE INVALID DUMPX
               TAPE',ROUTCDE=1
         ABEND 2,DUMP           NO HEADER RECORD FOUND
HEADER   EQU   *                DUMP HEADER ROUTINE
         USING PRDINPUT,R10
         CLI   PRDHDRID,X'FE'     XA DUMP                  *LBD 12/85*
         BE    XADUMP                                      *LBD 12/85*
         MVC   TITLE,20(R10)      MOVE 370 TITLE           *LBD 12/85*
         MVC   TOD,12(R10)        MOVE 370 DATE/TIME       *LBD 12/85*
         B     HTITLE                                      *LBD 12/85*
XADUMP   DS    0H                                          *LBD 12/85*
         SETON XADMP                                       *LBD 12/85*
         MVC   TITLE,32(R10)                               *LBD 12/85*
         MVC   DSNSP,=CL3'.XA'                             *LBD 12/85*
         MVC   TOD,16(R10)        MOVE XA DATE/TIME        *LBD 12/85*
HTITLE   DS    0H                                          *LBD 12/85*
         LINK  EP=BLSUMTOD,PARAM=(TOD,TDATE),VL=1
         MVC   DSNT(2),DTIME              HH
         MVC   DSNT+2(2),DTIME+3          MM
         MVC   DSND(2),TDATE              MM
         MVC   DSND+2(2),TDATE+3          DD
         MVC   DSND+4(2),TDATE+6          YY
         DROP  R10
OK       EQU   *
         CLOSE (DUMPWORK,REREAD)
ALLOC    DS    0H
         ALLOC DSN=DMPDSN,DISP=(NEW,CATLG,DELETE),         *LBD 05/86* X
               UNIT='3330V',MSVGP='SADUMP',                *LBD 05/86* X
               DDN='DUMPOUT',UNITCNT=4,                    *LBD 05/86* X
               SPACE=(CYL,(403,403)),                      *LBD 05/86* X
               ERROR=S99FAIL                               *LBD 05/86*
         B     DOIT                ALLOC OK                *LBD 05/86*
S99FAIL  S99FAIL ,
         WTO   'SACOPY - DYNAMIC ALLOCATION ERROR',ROUTCDE=1
         ABEND 1,DUMP           DYNAMIC ALLOCATION ERROR
         SPACE
DOIT     OPEN  (DUMPWORK,,SYSPRINT,OUTPUT,DUMPOUT,OUTPUT)
         EJECT
RWORK    GET   DUMPWORK                                    *LBD 05/86*
         LR    R10,R1                                      *LBD 05/86*
         PUT   DUMPOUT,(10)                                *LBD 05/86*
         AP    CTR,=P'01'       ADD TO RECORD COUNTER      *LBD 05/86*
         B     RWORK                                       *LBD 05/86*
EWORK    CLOSE (DUMPWORK,FREE)                             *LBD 05/86*
         SPACE 2                                           *LBD 05/86*
READ     GET   DUMPIN           READ AN INPUT RECORD
         LR    R10,R1
         PUT   DUMPOUT,(10)     DON'T STOP - PUT IT OUT
         AP    CTR,=P'01'       ADD TO RECORD COUNTER
         B     READ             CONTINUE THE READ UNTIL
         EJECT
EOJ      EQU   *
         CLOSE (DUMPIN,,DUMPOUT)                           *LBD 05/86*
         UNPK  COUNT(6),CTR     UNPACK RECORD COUNTER
         OI    COUNT+5,X'F0'    SET FULL DISPLAY
         PUT   SYSPRINT,MSGA    MESSAGE OF DSNAME OF INPUT
         PUT   SYSPRINT,MSGB    MESSAGE OF DSNAME OF OUTPUT
         PUT   SYSPRINT,TITLINE AND PRINT WHAT WE HAVE
         MVC   DUMPDSN(DSNL),DSNM
         MVC   DUMPTLE,TITLE
         ALLOC DDN='DUMPLOG',DISP=(MOD,KEEP),             *LBD 07/85*  X
               DSN='AOS.DUMPLOG',WAITDSN=YES             *LBD 07/85*
         OPEN  (DUMPLOG,EXTEND)                            *LBD 12/85*
         PUT   DUMPLOG,DUMPDATA
         XC    ECB,ECB
         SPLEVEL SET=1                                     *LBD 05/86*
WTOR     WTOR  'SACOPY - ENTER SA DUMP REASON OR END:',REPLY,100,ECB,  X
               ROUTCDE=1
         SPLEVEL SET=2                                     *LBD 05/86*
         WAIT  ECB=ECB
         XC    ECB,ECB
         CLC   REPLY(4),=C'END '
         BE    WTOREND
         PUT   DUMPLOG,REPLYL
         PUT   SYSPRINT,REPLYL
         MVI   REPLY,C' '
         MVC   REPLY+1(99),REPLY
         B     WTOR
WTOREND  EQU   *
         MVC   FL4TITLE,TITLE      MOVE TITLE
         MVC   FL5DSN(DSNL),DSNM
         PUT   SYSPRINT,FL1
         PUT   SYSPRINT,FL2
         PUT   SYSPRINT,FL3
         PUT   SYSPRINT,FL4
         PUT   SYSPRINT,FL5
*        PUT   SYSPRINT,FL6
*        PUT   SYSPRINT,FL7
*        PUT   SYSPRINT,FL8
*        PUT   SYSPRINT,FL9
         PUT   SYSPRINT,FL10
         PUT   SYSPRINT,FL11
         PUT   SYSPRINT,FL12
         PUT   SYSPRINT,FL13
         PUT   SYSPRINT,FL14
         PUT   SYSPRINT,FL15
         PUT   SYSPRINT,FL16
         PUT   SYSPRINT,FL17
CLOSE    CLOSE (SYSPRINT,,DUMPLOG)
         $EPILOG 0
         EJECT
         LTORG
ECB      DC    F'0'
         SPACE
WORK     DC    F'0'             WORK AREA FOR DATE/TIME
CTR      DC    PL4'0'           COUNT FOR RECORDS WRITTEN
TOD      DC    D'00'            WORK AREA FOR DUMP TIME/DATE
EDMASK   DC    X'21204B20204B2020'  TIME EDIT MASK
         DS    0F
TRTBL    DC    C'0123456789ABCDEF'
BLANK    DC    CL20' '
REPLYL   DC    C'           OPERATOR COMMENTS:  '
REPLY    DC    CL120' '
         DC    CL20' '                   FILLER
         SPACE
         PRINT NOGEN
DUMPIN   DCB   DSORG=PS,MACRF=GL,DDNAME=IEFRDER,EODAD=EOJ,             X
               LRECL=4104,BLKSIZE=4104,RECFM=F
         SPACE
DUMPOUT  DCB   DSORG=PS,MACRF=PM,DDNAME=DUMPOUT,RECFM=F,BLKSIZE=4104,  X
               LRECL=4104,OPTCD=W
         SPACE
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BLKSIZE=121,RECFM=FA
         SPACE
DUMPLOG DCB    DSORG=PS,MACRF=PM,DDNAME=DUMPLOG
         SPACE
DUMPWORK DCB   DSORG=PS,MACRF=(GL,PM),DDNAME=DUMPWORK,     *LBD 05/86* X
               EODAD=EWORK,RECFM=F,LRECL=4104,BLKSIZE=4104 *LBD 05/86*
         PRINT GEN
         EJECT
         BITSW XADMP
         SPACE 2
DMPDSN   DC    A(DSNM),Y(DSNL)                             *LBD 05/86*
         SPACE 1
MSGA     DC    CL5'1DSN='
DSNM     DC    C'AOS.SADUMP'
         DC    C'.D'
DSND     DC    CL6' ',C'.T' DSN DATE
DSNT     DC    CL4' '       DSN TIME
DSNSP    DC    C'.SP'             SP FOR 370 OR XA FOR XA  *LBD 12/85*
DSNL     EQU   *-DSNM       LENGTH OF MESSAGE
         DC    C'   RECORDS WRITTEN = '
COUNT    DC    CL6' ',CL80' '
         SPACE
MSGB     DC    CL155' '
         ORG   MSGB                                        *LBD 05/86*
         DC    C'0INPUT DSN='   INPUT DSNAME
IDSN     DC    CL24'STAND ALONE DUMP'
         ORG   ,                                           *LBD 05/86*
DUMPDATA DS    0CL155
COMM     DC    CL6'SACOPY'
         DC    CL5' '
DUMPDSN  DC    CL31' '
         DC    CL13' '
DUMPTLE  DC    CL100' '
         EJECT
TITLINE  DC    CL6' '
TDATE    DC    CL9' '
DTIME    DC    CL8' '
         DC    CL4' '
TITLE    DC    CL100' '
         EJECT
******************************************************************
*        THIS AREA CONTAINS THE DUMP FORM USED TO ROUTE          *
*        INFORMATION ON SYSTEM DUMPS TO THE RESPONSIBLE          *
*        GROUP                                                   *
******************************************************************
FL1      DC    CL121'1            SYSTEM DUMP NOTIFICATION FORM'
FL2      DC    C'0',CL5' ',CL115'FROM : CONTROL PROGRAM SERVICES '
FL3      DC    CL6' ',C'TO   : '
FL3COMM  DC    CL120' '            TO GROUP
FL4      DC    CL6'0'
         DC    C'DUMP TITLE :'
         DC    CL7' '
FL4TITLE DC    CL100' '            DUMP TITLE + FILLER
FL5      DC    CL6'0'
         DC    C'DATA-SET NAME :'
         DC    CL4' '
FL5DSN   DC    CL110' '
*L6      DC    CL6' '
*        DC    C'FROM DUMP D/S'    S :'
*        DC    CL4' ',9C'_',C'×',CL4' ',C'×',CL5' ',C'×',CL7' ',C'×'
*        DC    CL100' '            FILLER
*L7      DC    CL6' '
*        DC    C'TAKEN ON CPU :'
*        DC    CL5' ',14C'_',C'×',CL5' ',C'×',CL7' ',C'×'
*        DC    CL80' '             FILLER
*L8      DC    CL6' '
*        DC    C'DATE (MMDDYY) :'
*        DC    CL4' ',20C'_',C'×',CL7' ',C'×'
*        DC    CL80' '             FILLER
*L9      DC    CL6' '
*        DC    C'TIME (HHMMSS) :'
*        DC    CL4' ',28C'_',C'×'
*        DC    CL80' '             FILLER
FL10     DC    CL6'0'
         DC    CL115'PLEASE SUPPLY ME WITH THE SPR # IF THIS DUMP IS '
FL11     DC    CL6' '
         DC    CL115'SPR''D OR INFORM ME WHEN THE DUMP IS NO LONGER'
FL12     DC    CL6' '
         DC    CL115'NEEDED AND IT WILL BE DELETED.....'
FL13     DC    CL6'0'
         DC    CL115'THIS DATA-SET WILL BE DELETED IN 7 DAYS FROM '
FL14     DC    CL6' '
         DC    CL115'CREATION UNLESS I AM INFORMED OTHERWISE!'
FL15     DC    CL6'0'
         DC    CL115'PLEASE CHECK AND RETURN :'
FL16     DC    CL6'0'
         DC    C'THE SPR NUMBER IS _____ - HOLD THIS DUMP UNTIL'
         DC    CL100' THE SPR IS CLOSED'
FL17     DC    CL6'0'
         DC    CL115'THIS DUMP MAY BE DELETED _______'
******************************************************************
         EJECT
         DYNSPACE  ,                                       *LBD 12/85*
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
         AMDDATA
         END
