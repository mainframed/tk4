         TITLE 'GENERALIZED DATASET PRINTING UTILITY'
*---------------------------------------------------------------------*
*                                                                     *
*        GENPRINT WILL PRINT ANY DATASET ON A VARIETY OF PRINTERS.    *
*                                                                     *
*        JCL:  //STEP1 EXEC PGM=GENPRINT                              *
*              //STEPLIB DD DSN=                                      *
*              //INPUT   DD DSN=INPUT FILE                            *
*              //SYSIN   DD *                                         *
*               ---  CONTROL CARDS ---                                *
*                                                                     *
*        NOTE: THE INPUT DDNAME MAY CHANGED BY USING THE DDN=         *
*              CONTROL CARD KEYWORD.                                  *
*                                                                     *
*        NOTE: THE FOLLOWING CONTROL CARD OPTIONS MAY BE PASSED       *
*              AS CONTROL CARDS OR VIA THE EXECUTION PARM             *
*                                                                     *
*        VALID CONTROL CARDS OPTIONS ARE:                             *
*              OPTION:  DESCRIPTION                                   *
*              ACCT  =   ACCOUNT CODE  (6 CHARS)          *LBD 07/85*
*              BANNER=   Y OR N    XEROX 2/3700 ONLY       *LBD 02/86*
*              BLDG  =   BUILDING FOR XEROX 9700 DISTRIB              *
*              BOF   =   X2700/X9700 BOTTOM OF FORM                   *
*              CC    =   A FOR ASA OR M FOR MACH                      *
*              CLASS =   SYSOUT CLASS                                 *
*              COLLATE=  YES OR NO (X9700 ONLY)            *LBD 06/86*
*              COMM  =   ANY COMMENT (45 CHARS)           *LBD 07/85*
*              COPIES=   NN COPY COUNT                                *
*              DDN   =   DDNAME FOR INPUT DCB (DEF=INPUT)             *
*              DEPT  =   DEPT CODE  (4 CHARS)             *LBD 07/85*
*              DEST  =   PRINTER NAME                                 *
*              DUPLEX=   YES OR NO   BOTH SIDES OF PAPER              *
*              ESCAPE=   XEROX 2700 ESCAPE CHAR                       *
*              FDS   =   FDS/DIV CODE (3 CHARS)           *LBD 07/85*
*              FLOOR =   FLOOR FOR XEROX 9700 DISTRIBUTION            *
*              FNAME =   XEROX  9700 FORM NAME                        *
*              FORMS =   JCL FORMS FOR 3211 AND 9700                  *
*              FORMAT=   SPECIAL FORMAT FOR XEROX 9700                *
*              FONT  =   XEROX 2700 FONT                              *
*              FPRINT=   YES/NO TO PRINT XEROX 9700 FORMS             *
*              GROUP =   GROUP CODE  (4 CHARS)            *LBD 07/85*
*              HOLES =   YES OR NO  9700 HOLES OPTION     *LBD 07/85*
*              JDE   =   JOB DESCRIPTOR ENTRY (DEFAULT = L03)         *
*              LM    =   X2700  LEFT MARGIN                           *
*              MAIL  =   MAIL CODE (8 CHARS)              *LBD 07/85*
*              NAME  =   NAME   (20 CHARS)                *LBD 07/85*
*              OUTDD =   PREALLOCATED DD FOR OUTPUT        *LBD 01/86*
*              RM    =   X2700  RIGHT MARGIN                          *
*              SREC  =   STARTING  RECORD NUMBER            *LBD 05/87*
*              EREC  =   ENDING RECORD NUMBER               *LBD 05/87*
*              SHIFT =   YES OR NO   SHIFT FOR HOLES                  *
*              SPF   =   YES OR NO                                    *
*              SNUM  =   Y OR N TO SUPPRESS SEQ NUMBERS     *LBD 10/87*
*              TOF   =   X2700/X9700 TOP OF FORM                      *
*              TYPE  =   TYPE OF PRINTER                              *
*              X97CC =   YES ×  NO                                    *
*                        INDICATES THAT XEROX 9700 RTEXT CONTROL CARDS*
*                        FOLLOW IMMEDIATELY IN THE INPUT STREAM...    *
*                                                                     *
*              IF OUTDD IS SPECIFIED THEN THE OUTPUT FILE  *LBD 01/86*
*              MUST BE PREALLOCATED AND THE DCB SPECIFIED  *LBD 01/86*
*              WITH RECFM=VB. THE CARRIAGE                 *LBD 01/86*
*              CONTROL WILL BE AUTOMATICALLY INSERTED BASED*LBD 01/86*
*              UPON THE CC OF THE INPUT DATASET.           *LBD 01/86*
*              OTHERWISE IT WILL BE SET TO                 *LBD 01/86*
*              DCB=(RECFM=VB,LRECL=250,BLKSIZE=254)        *LBD 01/86*
*              NOTE: RECFM WILL BE FORCED TO VB.           *LBD 01/86*
*              NOTE2: THE MINIMUM LRECL IS 104 ...         *LBD 01/86*
*                                                                     *
*              SNUM IS USED TO SUPPRESS THE SEQUENCE        *LBD 10/87*
*                   NUMBERS IF PRESENT AND SNUM=Y           *LBD 10/87*
*                   IS SPECIFIED.                           *LBD 10/87*
*              YES=SUPPRESS   NO=NO TOUCH (DEFAULT)         *LBD 10/87*
*                                                                     *
*              IF SPF = YES THEN GENPRINT IS BEING INVOKED AS AN ISPF *
*              PROGRAM AND THE LINE COUNT WILL BE RETURNED VIA SETMSG.*
*                                                                     *
*              IF SPF = NO THEN DDNAME REPORT WILL BE OPENED TO       *
*              REPORT ON WHAT WE DID.                                 *
*                                                                     *
*              DEFAULTS ARE: DUPLEX,CLASS=A,JDE=L03,COPIES=1          *
*                                 DEST=LOCAL                          *
*                       THE DEFAULT FOR THE XEROX 2700                *
*                                 ESCAPE CHAR IS A HEX A1             *
*                                 BANNER=YES               *LBD 02/86*
*                                                                     *
*              TYPE=    1403, 3203, 3211 PRINTERS                     *
*                       9700 FOR  XEROX 8700 AND 9700                 *
*                       2700 FOR  XEROX 2700 OR 3700                  *
*                                                                     *
*                                                                     *
*              VALID OPTIONS FOR TYPE=9700 ARE:                       *
*                                 COPIES=                             *
*                                 COLLATE=YES/NO           *LBD 06/86*
*                                 DUPLEX=YES/NO                       *
*                                 SHIFT=YES/NO                        *
*                                 JDE=                                *
*                                 FORMS=                              *
*                                 HOLES=YES/NO  (YES)     *LBD 07/85*
*                                 FORMAT=                             *
*                                 FNAME=                              *
*                                 FPRINT=                             *
*                                 TOF=                                *
*                                 BOF=                                *
*                                 BLDG=                               *
*                                 FLOOR=                              *
*                                 NAME=                   *LBD 07/85*
*                                 PHONE=                  *LBD 03/86*
*                                 COMM=                   *LBD 07/85*
*                                 ACCT=                   *LBD 07/85*
*                                 FDS=                    *LBD 07/85*
*                                 DEPT=                   *LBD 07/85*
*                                 GROUP=                  *LBD 07/85*
*                                 MAIL=                   *LBD 07/85*
*                                 X97CC=                              *
*                                                                     *
*              VALID OPTIONS FOR  TYPE=2700 ARE:                      *
*                                 FONT=                               *
*                                 BANNER=                  *LBD 02/86*
*                                 ESCAPE=                             *
*                                 TOF=                                *
*                                 BOF=                                *
*                                 LM=                                 *
*                                 RM=                                 *
*                                 NAME=                   *LBD 07/85*
*                                 PHONE=                  *LBD 03/86*
*                                 COMM=                   *LBD 07/85*
*                                 FDS=                    *LBD 07/85*
*                                 DEPT=                   *LBD 07/85*
*                                 GROUP=                  *LBD 07/85*
*                                 MAIL=                   *LBD 07/85*
*                                                                     *
*              VALID OPTIONS FOR ALL PRINTER TYPES:                   *
*                                 DEST=                               *
*                                 DDN=                                *
*                                 CLASS=                              *
*                                 OUTDD=                   *LBD 01/86*
*                                 SREC=                     *LBD 05/87*
*                                 EREC=                     *LBD 05/87*
*                                 SNUM=                     *LBD 10/87*
*                                                                     *
* NOTES: THE NAME OVERRIDE MUST BE NO LONGER THAN 20 CHARACTERS       *
*        AND IS TERMINATED BY 2 CONSECUTIVE BLANKS OR BY A ','.       *
*                                                                     *
*        THE COMM PARAMETER IS ANY COMMENT YOU WISH BUT IT IS         *
*        TERMIATED BY 2 CONSECUTIVE BLANKS OR A ',' AND CAN BE        *
*        NO MORE THAN 45 CHARACTERS....                               *
*                                                                     *
*---------------------------------------------------------------------*
* CHANGE HISTORY:                                                     *
*              ADDED CODE TO TEST FOR 1ST RECORD AS SKIP  *LBD 07/85* *
*              TO CH1.  IF NOT THEN SET IT.               *LBD 07/85* *
*              ALSO IF NOCC THEN SET SKIP TO CH1 IN COL1  *LBD 07/85* *
*              OF FIRST USER RECORD.                      *LBD 07/85* *
*              ADDED HOLES= OPTION FOR X9700              *LBD 07/85*
*              ADDED THE COMM= PARAMETER                  *LBD 07/85*
*              ADDED OVERRIDE OPTIONS FOR:                *LBD 07/85*
*                               NAME, MAIL, ACCT, FDS/DIV *LBD 07/85*
*                               DEPT, GROUP               *LBD 07/85*
*              ADDED OUTDD= AS AN  OPTION TO ALLOW THE     *LBD 01/86*
*                OUTPUT TO BE DIRECTED TO A DATASET.       *LBD 01/86*
*              ADDED BANNER=YES/NO OPTION FOR X2/3700'S    *LBD 02/86*
*              CORRECTED FNAME= OPTION AND ADDED COLLATE   *LBD 06/86*
*                                  SUPPORT.                *LBD 06/86*
*              UPDATED TO USE ACF2 COMPMAIL IF AVAILABLE   *LBD 06/86*
*              INSTEAD OF DEFAULT MAIL...                  *LBD 06/86*
*              FIXED COPY COUNT                            *LBD 08/86*
*              UPDATE TO REMOVE XEROX 3700 MULTI-COPY      *LBD 08/86*
*                                  OPTION.  PER ACCOUNTING *LBD 08/86*
*                                  REQUEST....             *LBD 08/86*
*              ADDED SREC AND EREC OPTIONS                  *LBD 05/87*
*              ADDED SNUM OPTION                            *LBD 10/87*
*              changed Xerox 9700 forms to remove the $     *lbd 06/88*
*                                                                     *
*---------------------------------------------------------------------*
* CAVEATS:                                                            *
*                                                                     *
*        WE USE DFM1 INSTEAD OF THE CHARACTERS DJDE FOR THE XEROX     *
*        9700.                                                        *
*                                                                     *
*        THE DEFAULT FORMS, ETC. FOR THE XEROX 9700 SHOULD BE VERIFIED*
*                                                                     *
*        THE DEFAULT FONT FOR THE  XEROX 2700/3700 SHOULD BE VERIFIED.*
*                                                                     *
*---------------------------------------------------------------------*
* AUTHOR:      LIONEL DYCK                                            *
*              ROCKWELL INTERNATIONAL                                 *
*              PO BOX 2515                                            *
*              2201 SEAL BEACH BLVD.                                  *
*              SEAL BEACH, CALIF  90740                               *
*              MAIL CODE 110-SH28                                     *
*              PHONE (213) 594-1125                                   *
*              COMNET 374-1125                                        *
*---------------------------------------------------------------------*
         EJECT
         MACRO
&L       $PUT &DCB,&DATA
&L       LA    R1,&DCB
         LA    R5,&DATA
         LA    R3,L'&DATA
         BAL   R8,PUTROU
         MEND
         EJECT
GENPRINT $PROLOG R10,R11,R12
         $REGS
         EJECT
         SR    R9,R9            ZERO LINE COUNTER
         SETON DUPLEX
         L     1,0(1)              * LOAD ADDR OF PARM
         LH    2,0(1)              * LOAD LENGTH OF PARM
         LTR   2,2                 * LENGTH OF ZERO
         BNZ   HAVEPARM            * YES - GO DO IT
NOPARM   EQU   *
         OPEN  (SYSIN)
         TESTOPEN SYSIN,DOIT      SYSIN NOT PRESENT
         SETON CARDS
RPARM    GET   SYSIN
         LA    R5,70(R1)
         B     PARMLOOP
         EJECT
HAVEPARM DS    0H
         LA    R1,2(R1)           -> START OF PARM
         AR    R5,R1              -> END OF PARM
PARMLOOP DS    0H
         CLI   0(R1),C','         COMMA
         BE    PARMBUMP           YES
         CLC   =CL4' ',0(R1)      END OF PARM ?
         BE    DOIT
         CLC   =C'DUPLEX=',0(R1)  DUPLEX PARM (DEFAULT)
         BE    SETDUP
         CLC   =C'HOLES=',0(R1)   HOLES OPTION (DEFAULT)
         BE    SETHOLES
         CLC   =C'SHIFT=',0(R1)   SHIFT PARM (DEFAULT)
         BE    SETSHFT
         CLC   =C'COPIES=',0(R1)  COPIES SPECIFIED
         BE    DOCOPY
         CLC   =C'DEST=',0(R1)    PRINTER SPECIFIED
         BE    DOPRT              YES
         CLC   =C'SNUM=',0(R1)    SUPPRESS SEQ NUMBERS      *LBD 10/87*
         BE    DOSNUM             YES                       *LBD 10/87*
         CLC   =C'JDE=',0(R1)     JOB DESCRIPTOR ENTRY
         BE    DOJDE              YES
         CLC   =C'TYPE=',0(R1)    OUTPUT TYPE
         BE    DOTYPE             YES
         CLC   =C'CLASS=',0(R1)   SYSOUT CLASS
         BE    DOCLASS            YES
         CLC   =C'FORMS=',0(R1)   SYSOUT FORM NAME
         BE    DOFORMS            YES
         CLC   =C'FONT=',0(R1)    XEROX 2700 FONT
         BE    DOFONT             YES
         CLC   =C'BANNER=',0(R1)  XEROX 2700/3700 BANNER   *LBD 02/86*
         BE    DOBANX27           YES                      *LBD 02/86*
         CLC   =C'ESCAPE=',0(R1)  XEROX 2700 ESCAPE CHAR
         BE    DOESC              YES
         CLC   =C'TOF=',0(R1)     X2700 TOP OF FORM
         BE    DOTOF
         CLC   =C'BOF=',0(R1)     X2700 BOTTOM OF FORM
         BE    DOBOF
         CLC   =C'RM=',0(R1)      X2700 RIGHT  MARGIN
         BE    DORM
         CLC   =C'LM=',0(R1)      X2700 LEFT MARGIN
         BE    DOLM
         CLC   =C'DDN=',0(R1)     DDNAME FOR INPUT
         BE    DODDN
         CLC   =C'SPF=',0(R1)     SPF OPTION
         BE    DOSPF
         CLC   =C'X97CC=',0(R1)   DDNAME FOR XEROX 9700 ROUTING CONTROL
         BE    DOX97CC
         CLC   =C'CC=',0(R1)      FORCE CARRIAGE CONTROL
         BE    DOCC
         CLC   =C'FLOOR=',0(R1)
         BE    DOFLR
         CLC   =C'BLDG=',0(R1)
         BE    DOBLDG
         CLC   =C'FNAME=',0(R1)
         BE    DOFNAME
         CLC   =C'FPRINT=',0(R1)
         BE    DOFPRT
         CLC   =C'FORMAT=',0(R1)
         BE    DOFMT
         CLC   =C'NAME=',0(R1)                            *LBD 07/85*
         BE    DONAME                                     *LBD 07/85*
         CLC   =C'PHONE=',0(R1)                            *LBD 03/86*
         BE    DOPHONE                                     *LBD 03/86*
         CLC   =C'MAIL=',0(R1)                            *LBD 07/85*
         BE    DOMAIL                                     *LBD 07/85*
         CLC   =C'ACCT=',0(R1)                            *LBD 07/85*
         BE    DOACCT                                     *LBD 07/85*
         CLC   =C'FDS=',0(R1)                             *LBD 07/85*
         BE    DOFDS                                      *LBD 07/85*
         CLC   =C'DEPT=',0(R1)                            *LBD 07/85*
         BE    DODEPT                                     *LBD 07/85*
         CLC   =C'GROUP=',0(R1)                           *LBD 07/85*
         BE    DOGROUP                                    *LBD 07/85*
         CLC   =C'COMM=',0(R1)                            *LBD 07/85*
         BE    DOCOMM                                     *LBD 07/85*
         CLC   =C'OUTDD=',0(R1)                            *LBD 01/86*
         BE    DOOUTDD                                     *LBD 01/86*
         CLC   =C'COLLATE=',0(R1)                          *LBD 06/86*
         BE    DOCOLL                                      *LBD 06/86*
         CLC   =C'SREC=',0(R1)     STARTING RECORD #        *LBD 05/87*
         BE    DOSREC                                       *LBD 05/87*
         CLC   =C'EREC=',0(R1)     ENDING RECORD #          *LBD 05/87*
         BE    DOEREC                                       *LBD 05/87*
         SPACE 1
PARMBUMP LA    R1,1(R1)
         CR    R1,R5              END OF SCAN
         BNH   PARMLOOP           NO - KEEP LOOPING
         B     DOIT
         SPACE 1
SETDUP   DS    0H                 SET DUPLEX
         LA    R1,7(R1)           -> OPTION
         CLC   =C'YES',0(R1)      DUPLEX ON ?
         BE    SETDON             YES
         SETOF DUPLEX
         MVI   X97FORM,C'S'       SET AS SIMPLEX
         LA    R1,2(R1)
         B     PARMBUMP
SETDON   SETON DUPLEX             SET DUPLEX
         LA    R1,3(R1)           -> PAST DUPLEX
         B     PARMBUMP
         EJECT                                             *LBD 06/86*
DOCOLL   DS    0H                                          *LBD 06/86*
         LA    R1,8(R1)            -> COLLATE=             *LBD 06/86*
         CLC   =C'NO',0(R1)        NO ?                    *LBD 06/86*
         BE    NOCOLL                                      *LBD 06/86*
         SETON FCOLL                                       *LBD 06/86*
         LA    R1,1(R1)                                    *LBD 06/86*
NOCOLL   LA    R1,2(R1)                                    *LBD 06/86*
         B     PARMBUMP                                    *LBD 06/86*
         EJECT                                            *LBD 07/85*
SETHOLES DS    0H                                         *LBD 07/85*
         LA    R1,6(R1)         -> OPTION                 *LBD 07/85*
         CLC   =C'YES',0(R1)    YES - OK                  *LBD 07/85*
         BE    SHOK                                       *LBD 07/85*
*        ASSUME NO  .......                               *LBD 07/85*
         MVI   X97FORM+2,C'N'   SET FOR NO HOLES          *LBD 07/85*
         LA    R1,2(R1)                                   *LBD 07/85*
         B     PARMBUMP                                   *LBD 07/85*
SHOK     LA    R1,3(R1)                                   *LBD 07/85*
         B     PARMBUMP                                   *LBD 07/85*
         EJECT
SETSHFT  DS    0H                 SET SHIFT
         LA    R1,6(R1)           -> OPTION
         CLC   =C'YES',0(R1)      DUPLEX ON ?
         BE    SETSON             YES
         MVC   X97SHFT(4),=C'NO,;'
         LA    R1,2(R1)
         B     PARMBUMP
SETSON   DS    0H
         MVC   X97SHFT(5),=C'YES,;'
         LA    R1,3(R1)
         B     PARMBUMP
         EJECT
DONAME   DS    0H                                         *LBD 07/85*
         LA    R1,5(R1)         -> NAME                   *LBD 07/85*
         LA    R2,NAME                                    *LBD 07/85*
         LA    R3,20            COUNT FOR MOVE            *LBD 07/85*
         SETON FNAME                                      *LBD 07/85*
DONL1    DS    0H                                         *LBD 07/85*
         MVC   0(1,R2),0(R1)                              *LBD 07/85*
         LA    R1,1(R1)         BUMP R1                   *LBD 07/85*
         LA    R2,1(R2)         DUMP R2                   *LBD 07/85*
         CLC   =C'  ',0(R1)     END OF NAME (2 BLANKS)    *LBD 07/85*
         BE    PARMBUMP         YES                       *LBD 07/85*
         CLI   0(R1),C','       END OF NAME (,)           *LBD 07/85*
         BE    PARMBUMP                                   *LBD 07/85*
         BCT   R3,DONL1         KEEP MOVING               *LBD 07/85*
         B     PARMBUMP         MAX LEN OF 20             *LBD 07/85*
         EJECT                                            *LBD 07/85*
DOCOMM   DS    0H                                         *LBD 07/85*
         LA    R1,5(R1)         -> COMM                   *LBD 07/85*
         LA    R2,X27COMM                                 *LBD 07/85*
         LA    R4,X97COMM                                 *LBD 07/85*
         LA    R3,45            COUNT FOR MOVE            *LBD 07/85*
DOCM1    DS    0H                                         *LBD 07/85*
         MVC   0(1,R2),0(R1)                              *LBD 07/85*
         MVC   0(1,R4),0(R1)                              *LBD 07/85*
         LA    R1,1(R1)         BUMP R1                   *LBD 07/85*
         LA    R2,1(R2)         DUMP R2                   *LBD 07/85*
         LA    R4,1(R4)         DUMP R4                   *LBD 07/85*
         CLC   =C'  ',0(R1)     END OF COMM (2 BLANKS)    *LBD 07/85*
         BE    PARMBUMP         YES                       *LBD 07/85*
         CLI   0(R1),C','       END OF COMM (,)           *LBD 07/85*
         BE    PARMBUMP                                   *LBD 07/85*
         BCT   R3,DOCM1         KEEP MOVING               *LBD 07/85*
         B     PARMBUMP         MAX LEN OF 45             *LBD 07/85*
         EJECT                                            *LBD 07/85*
DOMAIL   DS    0H                                         *LBD 07/85*
         LA    R1,5(R1)         -> MAIL                   *LBD 07/85*
         LA    R2,MAIL                                    *LBD 07/85*
         LA    R3,8             COUNT FOR MOVE            *LBD 07/85*
         SETON FMAIL                                      *LBD 07/85*
DOML1    DS    0H                                         *LBD 07/85*
         MVC   0(1,R2),0(R1)                              *LBD 07/85*
         LA    R1,1(R1)         BUMP R1                   *LBD 07/85*
         LA    R2,1(R2)         DUMP R2                   *LBD 07/85*
         CLC   =C'  ',0(R1)     END OF MAIL (2 BLANKS)    *LBD 07/85*
         BE    PARMBUMP         YES                       *LBD 07/85*
         CLI   0(R1),C','       END OF MAIL (,)           *LBD 07/85*
         BE    PARMBUMP                                   *LBD 07/85*
         BCT   R3,DOML1         KEEP MOVING               *LBD 07/85*
         B     PARMBUMP                                   *LBD 07/85*
         EJECT                                            *LBD 07/85*
DOACCT   DS    0H                                         *LBD 07/85*
         LA    R1,5(R1)         -> ACCT                   *LBD 07/85*
         LA    R2,ACCTNO                                  *LBD 07/85*
         LA    R3,6             COUNT FOR MOVE            *LBD 07/85*
         SETON FACCT                                      *LBD 07/85*
DOAL1    DS    0H                                         *LBD 07/85*
         MVC   0(1,R2),0(R1)                              *LBD 07/85*
         LA    R1,1(R1)         BUMP R1                   *LBD 07/85*
         LA    R2,1(R2)         DUMP R2                   *LBD 07/85*
         CLC   =C'  ',0(R1)     END OF ACCT (2 BLANKS)    *LBD 07/85*
         BE    PARMBUMP         YES                       *LBD 07/85*
         CLI   0(R1),C','       END OF ACCT (,)           *LBD 07/85*
         BE    PARMBUMP                                   *LBD 07/85*
         BCT   R3,DOAL1         KEEP MOVING               *LBD 07/85*
         B     PARMBUMP                                   *LBD 07/85*
         EJECT                                            *LBD 07/85*
DOFDS    DS    0H                                         *LBD 07/85*
         LA    R1,4(R1)         -> FDS                    *LBD 07/85*
         LA    R2,FDS                                     *LBD 07/85*
         LA    R3,3             COUNT FOR MOVE            *LBD 07/85*
         SETON FFDS                                       *LBD 07/85*
DOACL1   DS    0H                                         *LBD 07/85*
         MVC   0(1,R2),0(R1)                              *LBD 07/85*
         LA    R1,1(R1)         BUMP R1                   *LBD 07/85*
         LA    R2,1(R2)         DUMP R2                   *LBD 07/85*
         CLC   =C'  ',0(R1)     END OF FDS (2 BLANKS)     *LBD 07/85*
         BE    PARMBUMP         YES                       *LBD 07/85*
         CLI   0(R1),C','       END OF FDS (,)            *LBD 07/85*
         BE    PARMBUMP                                   *LBD 07/85*
         BCT   R3,DOACL1        KEEP MOVING               *LBD 07/85*
         B     PARMBUMP                                   *LBD 07/85*
         EJECT                                            *LBD 07/85*
DODEPT   DS    0H                                         *LBD 07/85*
         LA    R1,5(R1)         -> DEPT                   *LBD 07/85*
         LA    R2,DEPT                                    *LBD 07/85*
         LA    R3,4             COUNT FOR MOVE            *LBD 07/85*
         SETON FDEPT                                      *LBD 07/85*
DODPL1   DS    0H                                         *LBD 07/85*
         MVC   0(1,R2),0(R1)                              *LBD 07/85*
         LA    R1,1(R1)         BUMP R1                   *LBD 07/85*
         LA    R2,1(R2)         DUMP R2                   *LBD 07/85*
         CLC   =C'  ',0(R1)     END OF DEPT (2 BLANKS)    *LBD 07/85*
         BE    PARMBUMP         YES                       *LBD 07/85*
         CLI   0(R1),C','       END OF DEPT (,)           *LBD 07/85*
         BE    PARMBUMP                                   *LBD 07/85*
         BCT   R3,DODPL1        KEEP MOVING               *LBD 07/85*
         B     PARMBUMP                                   *LBD 07/85*
         EJECT                                            *LBD 07/85*
DOPHONE  DS    0H                                         *LBD 07/85*
         LA    R1,6(R1)         -> PHONE                  *LBD 07/85*
         LA    R2,PHONE                                   *LBD 07/85*
         LA    R3,12            COUNT FOR MOVE            *LBD 07/85*
         SETON FPHONE                                     *LBD 07/85*
DOPHL1   DS    0H                                         *LBD 07/85*
         MVC   0(1,R2),0(R1)                              *LBD 07/85*
         LA    R1,1(R1)         BUMP R1                   *LBD 07/85*
         LA    R2,1(R2)         BUMP R2                   *LBD 07/85*
         CLC   =C'  ',0(R1)     END OF PHONE (2 BLANKS)   *LBD 07/85*
         BE    PARMBUMP         YES                       *LBD 07/85*
         CLI   0(R1),C','       END OF PHONE (,)          *LBD 07/85*
         BE    PARMBUMP                                   *LBD 07/85*
         BCT   R3,DOPHL1        KEEP MOVING               *LBD 07/85*
         B     PARMBUMP                                   *LBD 07/85*
         EJECT                                            *LBD 07/85*
DOGROUP  DS    0H                                         *LBD 07/85*
         LA    R1,6(R1)         -> GROUP                  *LBD 07/85*
         LA    R2,GROUP                                   *LBD 07/85*
         LA    R3,4             COUNT FOR MOVE            *LBD 07/85*
         SETON FGROUP                                     *LBD 07/85*
DOGPL1   DS    0H                                         *LBD 07/85*
         MVC   0(1,R2),0(R1)                              *LBD 07/85*
         LA    R1,1(R1)         BUMP R1                   *LBD 07/85*
         LA    R2,1(R2)         BUMP R2                   *LBD 07/85*
         CLC   =C'  ',0(R1)     END OF GROUP (2 BLANKS)   *LBD 07/85*
         BE    PARMBUMP         YES                       *LBD 07/85*
         CLI   0(R1),C','       END OF GROUP (,)          *LBD 07/85*
         BE    PARMBUMP                                   *LBD 07/85*
         BCT   R3,DOGPL1        KEEP MOVING               *LBD 07/85*
         B     PARMBUMP                                   *LBD 07/85*
         EJECT                                            *LBD 07/85*
DOOUTDD  DS    0H                                          *LBD 01/86*
         LA    R1,6(R1)           -> OUTPUT DDNAMEE        *LBD 01/86*
         LA    R2,SYSPRINT+40     -> DCB DDNAME LOCATION   *LBD 01/86*
         MVC   SYSPRINT+40(8),=CL8' '                      *LBD 01/86*
         LA    R3,8               COUNT FOR MOVE           *LBD 01/86*
         SETON ODD                SET ON FLAG              *LBD 01/86*
DOODD1   DS    0H                                          *LBD 01/86*
         MVC   0(1,R2),0(R1)                               *LBD 01/86*
         LA    R1,1(R1)                                    *LBD 01/86*
         LA    R2,1(R2)                                    *LBD 01/86*
         CLC   =CL2' ',0(R1)      END OF OUTDD ?           *LBD 01/86*
         BE    PARMBUMP                                    *LBD 01/86*
         CLI   0(R1),C','         END OF OUTDD ?           *LBD 01/86*
         BE    PARMBUMP                                    *LBD 01/86*
         BCT   R3,DOODD1                                   *LBD 01/86*
         B     PARMBUMP                                    *LBD 01/86*
         EJECT                                             *LBD 01/86*
DOSPF    DS    0H                 SET SPF OPTION
         LA    R1,4(R1)           -> SPF
         CLC   =C'YES',0(R1)      SPF EXECUTION ?
         BE    SETSPF             YES
         LA    R1,2(R1)
         B     PARMBUMP
SETSPF   DS    0H
         SETON SPF
         LA    R1,3(R1)
         B     PARMBUMP
         EJECT
DODDN    LA    R1,4(R1)         -> DDN
         LA    R3,INPUT+40      -> INPUT DCB DDNAME
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF DDN
         MVC   0(8,R3),=CL8' '  BLANK INPUT DCB DDNAME
DODDNL   MVC   0(1,R3),0(R1)    MOVE DDN CHAR
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF DDN
         B     DODDNL           KEEP MOVING IT
         EJECT
DOX97CC  LA    R1,6(R1)         -> DDN
         CLC   =C'YES',0(R1)    YES OR NO
         BNE   PARMBUMP         NO - SKIP IT
         SETON X97CCF           SET FLAG
         SETOF CARDS                                      *LBD 07/85*
         B     DOIT             YES - SO GO DO IT
         SPACE 2
DOFLR    LA    R1,6(R1)         -> FLOOR
         LA    R3,FLOOR         -> FLOOR DEST
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF FLOOR
DOFLRL   MVC   0(1,R3),0(R1)    MOVE FLOOR
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF FLOOR
         B     DOFLRL           KEEP MOVING IT
         SPACE 2
DOBLDG   LA    R1,5(R1)         -> BUILDING
         LA    R3,BLDG          -> BUILDING   DEST
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF BLDG
DOBLDGL  MVC   0(1,R3),0(R1)    MOVE BLDG
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF BLDG
         B     DOBLDGL          KEEP MOVING IT
         SPACE 2
DOFNAME  LA    R1,6(R1)
         LA    R3,X97FORMX
         CLI   0(R1),C','
         BLE   ENDFNAME
         SETON FORMX97             SET FLAG                *LBD 06/86*
DOFNAMEL MVC   0(1,R3),0(R1)
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   ENDFNAME
         B     DOFNAMEL         KEEP MOVING IT
ENDFNAME DS    0H
         MVC   0(2,R3),=C',;'
         B     PARMBUMP
         SPACE 2
DOFMT    LA    R1,7(R1)
         LA    R3,X97FMT
DOFMTL   MVC   0(1,R3),0(R1)
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   ENDFMT
         B     DOFMTL           KEEP MOVING IT
ENDFMT   DS    0H
         MVC   0(2,R3),=C',;'
         B     PARMBUMP
         SPACE 2
DOFPRT   LA    R1,7(R1)
         CLC   =C'NO',0(R1)     NO ?
         BNE   DOFNOF             =NO SO SKIP              *LBD 01/86*
         MVC   X97FORMX(6),=C'NONE,;'
         B     PARMBUMP
DOFNOF   DS    0H                                          *LBD 01/86*
         IFON  FORMX97,PARMBUMP                            *LBD 06/86*
         MVI   X97FORMX,C' '                               *LBD 01/86*
         B     PARMBUMP                                    *LBD 01/86*
         SPACE 2
DOTOF    LA    R1,4(R1)         -> TOF
         LA    R3,TOF           -> WORK AREA
         SETON X2700M
DOTOFL   MVC   0(1,R3),0(R1)    MOVE TOF CHAR
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF TOF
         B     DOTOFL           KEEP MOVING IT
         SPACE 1
DOBOF    LA    R1,4(R1)         -> BOF
         LA    R3,BOF           -> WORK AREA
DOBOFL   MVC   0(1,R3),0(R1)    MOVE BOF CHAR
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF BOF
         B     DOBOFL           KEEP MOVING IT
         SPACE 1
DORM     LA    R1,3(R1)         -> RM
         LA    R3,RM            -> WORK AREA
DORML    MVC   0(1,R3),0(R1)    MOVE RM CHAR
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF RM
         B     DORML            KEEP MOVING IT
         SPACE 1
DOFORMS  LA    R1,6(R1)         -> FORM NAME
         LA    R3,X97FORM       -> FORMS NAME
         MVC   X97FORM,=CL4' '
DOFORML  MVC   0(1,R3),0(R1)    MOVE FORM CHAR
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF FORM NAME
         B     DOFORML          KEEP MOVING IT
         SPACE 1
DOLM     LA    R1,3(R1)         -> LM
         LA    R3,LM            -> WORK AREA
DOLML    MVC   0(1,R3),0(R1)    MOVE LM CHAR
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C','
         BLE   PARMBUMP         END OF LM
         B     DOLML            KEEP MOVING IT
         EJECT
DOCC     DS    0H
         LA    R1,3(R1)         -> CC
         CLI   0(R1),C'A'       ASA
         BNE   DOCCM            NO
         SETON FACC             FORCE ASA
         MVI   SAVECC,C'A'                                 *LBD 01/86*
         B     PARMBUMP
DOCCM    CLI   0(R1),C'M'       MACH CC
         BNE   PARMBUMP         NO
         SETON FMCC             FORCE IT
         MVI   SAVECC,C'M'                                 *LBD 01/86*
         B     PARMBUMP
         EJECT
DOJDE    LA    R1,4(R1)           -> JDE
         MVC   JDE,0(R1)          MOVE IT TO DJDE CARD
         LA    R1,3(R1)           -> PAST JDE
         B     PARMBUMP
DOCLASS  LA    R1,6(R1)           -> CLASS
         MVC   CLASS(1),0(R1)     MOVE IT TO DJDE CARD
         LA    R1,1(R1)           -> PAST CLASS
         B     PARMBUMP
DOCOPY   LA    R1,7(R1)           -> COPY COUNT
         MVC   COPY#,BLANKS                                *LBD 08/86*
         LA    R3,COPY#           SAVE START OF NUMBER
         XC    COPYCLEN,COPYCLEN                            *LBD 10/87*
COPYLP   DS    0H
         CLI   0(R1),C' '         END OF COPY
         BE    PARMBUMP           YES
         CLI   0(R1),C','         END OF COPY
         BE    PARMBUMP           YES
         MVC   0(1,R3),0(R1)      MOVE COPY
         LH    R4,COPYCLEN        GEN LEN OF COPY VALUE
         LA    R4,1(R4)           ADD 1
         STH   R4,COPYCLEN        AND SAVE IT
         LA    R1,1(R1)           BUMP IT
         LA    R3,1(R3)           BUMP IT
         B     COPYLP
         EJECT                                              *LBD 05/87*
DOSREC   LA    R1,5(R1)           -> SREC LINE NUMBER
         MVC   DWD,BLANKS                                  *LBD 08/86*
         SR    R4,R4                                        *LBD 05/87*
         LA    R3,DWD             SAVE START OF NUMBER
SRECLP   DS    0H
         CLI   0(R1),C' '         END OF SREC
         BE    PACKSREC           YES
         CLI   0(R1),C','         END OF SREC
         BE    PACKSREC           YES
         MVC   0(1,R3),0(R1)      MOVE SREC
         LA    R4,1(R4)           ADD 1
         LA    R1,1(R1)           BUMP IT
         LA    R3,1(R3)           BUMP IT
         B     SRECLP
PACKSREC DS    0H                                           *LBD 05/87*
         BCTR  R4,R0                                        *LBD 05/87*
         EX    R4,PAKSREC                                   *LBD 05/87*
         SETON COUNTR                                       *LBD 05/87*
         B     PARMBUMP                                     *LBD 05/87*
PAKSREC  PACK  SREC,DWD(0)                                  *LBD 05/87*
         EJECT                                              *LBD 05/87*
DOEREC   LA    R1,5(R1)           -> EREC LINE NUMBER
         MVC   DWD,BLANKS                                  *LBD 08/86*
         SR    R4,R4                                        *LBD 05/87*
         LA    R3,DWD             SAVE START OF NUMBER
ERECLP   DS    0H
         CLI   0(R1),C' '         END OF EREC
         BE    PACKEREC           YES
         CLI   0(R1),C','         END OF EREC
         BE    PACKEREC           YES
         MVC   0(1,R3),0(R1)      MOVE EREC
         LA    R4,1(R4)           ADD 1
         LA    R1,1(R1)           BUMP IT
         LA    R3,1(R3)           BUMP IT
         B     ERECLP
PACKEREC DS    0H                                           *LBD 05/87*
         BCTR  R4,R0                                        *LBD 05/87*
         EX    R4,PAKEREC                                   *LBD 05/87*
         SETON COUNTR                                       *LBD 05/87*
         B     PARMBUMP                                     *LBD 05/87*
PAKEREC  PACK  EREC,DWD(0)                                  *LBD 05/87*
         EJECT                                              *LBD 05/87*
DOPRT    LA    R1,5(R1)           -> START OF DEST NAME
         LA    R3,REMOTE          -> REMOTE NAME FIELD
PRTLOOP  MVC   0(1,R3),0(R1)      MOVE PRT NAME
         LA    R1,1(R1)           BUMP PARM
         LA    R3,1(R3)           BUMP DEST  NAME
         CLI   0(R1),C' '         END OF REMOTE NAME
         BE    PARMBUMP           YES
         CLI   0(R1),C','         END OF REMOTE NAME
         BE    PARMBUMP           YES
         B     PRTLOOP            KEEP MOVEING NAME
         EJECT                                              *LBD 10/87*
DOSNUM   DS    0H                                           *LBD 10/87*
         LA    R1,5(R1)                                     *LBD 10/87*
         CLI   0(R1),C'Y'                                   *LBD 10/87*
         BNE   PARMBUMP                                     *LBD 10/87*
         SETON SNUM                                         *LBD 10/87*
         B     PARMBUMP                                     *LBD 10/87*
         EJECT                                              *LBD 10/87*
DOTYPE   DS    0H
         LA    R1,5(R1)           -> TYPE
         CLC   =C'9700',0(R1)     9700 OUTPUT ?
         BE    SET9700            YES
         CLC   =C'8700',0(R1)     8700 OUTPUT ?
         BE    SET9700            YES
         CLC   =C'1403',0(R1)     HARDCOPY ?
         BE    SETHARD            YES (DEFAULT)
         CLC   =C'3211',0(R1)     HARDCOPY ?
         BE    SETHARD            YES (DEFAULT)
         CLC   =C'3203',0(R1)     HARDCOPY ?
         BE    SETHARD            YES (DEFAULT)
         CLC   =C'2700',0(R1)     XEROX 2700
         BE    SET2700            YES
         CLC   =C'3700',0(R1)     XEROX 3700
         BE    SET3700            YES
         B     PARMBUMP
SETHARD  LA    R1,4(R1)           BUMP IT
         B     PARMBUMP
SET9700  SETON X9700
         LA    R1,4(R1)           BUMP PAST 9700
         B     PARMBUMP
SET2700  SETON X2700
         LA    R1,4(R1)           -> PAST 2700
         B     PARMBUMP
SET3700  SETON X2700
*ET3700  SETON X2700,X3700
***                                NOTE: IF YOU WISH       *LBD 08/86*
***                                TO USE THE MULTI COPY   *LBD 08/86*
***                                UDK ON THE 3700 THEN    *LBD 08/86*
***                                CHANGE THE ABOVE SETON  *LBD 08/86*
***                                MACRO......             *LBD 08/86*
         LA    R1,4(R1)           -> PAST 3700
         B     PARMBUMP
DOFONT   DS    0H
         LA    R1,5(R1)           -> PAST FONT=
         LA    R3,FONT            -> FONT
FONTLP   DS    0H
         MVC   0(1,R3),0(R1)      MOVE CHAR
         LA    R1,1(R1)
         LA    R3,1(R3)
         CLI   0(R1),C' '         END OF FONT
         BLE   PARMBUMP           YES
         CLI   0(R1),C','         END OF FONT
         BE    PARMBUMP           YES
         B     FONTLP             NO
DOESC    DS    0H
         LA    R1,7(R1)           -> ESCAPE CHAR
         MVC   ESCAPE(1),0(R1)    MOVE IT
         B     PARMBUMP
         SPACE 2                                           *LBD 02/86*
DOBANX27 DS    0H                                          *LBD 02/86*
         LA    R1,7(R1)           -> BANNER OPTION         *LBD 02/86*
         CLI   0(R1),C'Y'         YES                      *LBD 02/86*
         BE    PARMBUMP
         SETON X27BAN             SET BANNER OFF           *LBD 02/86*
         B     PARMBUMP                                    *LBD 02/86*
         EJECT
EOPARM   CLOSE SYSIN
         SETOF CARDS
         EJECT
DOIT     DS    0H                 START THE REAL PROCESS
         IFON  CARDS,RPARM
         RDJFCB INPUT                                       *LBD 06/85*
         MVC   X27DSN(44),JFCBDSNM                          *LBD 06/85*
         TM    JFCBIND1,JFCPDS                              *LBD 06/85*
         BZ    NOMEM                                        *LBD 06/85*
         LA    R1,X27DSN+1                                  *LBD 06/85*
X27DSNL  CLI   0(R1),C' '       END OF DSN ?                *LBD 06/85*
         BE    X27DSNE                                      *LBD 06/85*
         LA    R1,1(R1)                                     *LBD 06/85*
         B     X27DSNL                                      *LBD 06/85*
X27DSNE  MVI   0(R1),C'('                                   *LBD 06/85*
         LA    R1,1(R1)                                     *LBD 06/85*
         MVC   0(8,R1),JFCBELNM MOVE MEMBER NAME            *LBD 06/85*
         LA    R1,1(R1)                                     *LBD 06/85*
X27DSNL2 CLI   0(R1),C' '       END OF MEMBER NAME          *LBD 06/85*
         BE    X27DSNE2                                     *LBD 06/85*
         LA    R1,1(R1)                                     *LBD 06/85*
         B     X27DSNL2                                     *LBD 06/85*
X27DSNE2 MVI   0(R1),C')'                                   *LBD 06/85*
         SPACE 1                                            *LBD 06/85*
NOMEM    DS    0H                                           *LBD 06/85*
         MVC   X97DSN,X27DSN                               *LBD 01/86*
         OPEN  INPUT
         TM    INPUT+36,X'04'     ASA CARRIAGE CONTROL
         BO    CCASA
         TM    INPUT+36,X'02'     MACH CARRIAGE CONTROL
         BZ    NOCC
         SETON FMCC                                       *LBD 07/85*
         OI    SYSPRINT+36,X'02'  SET MACH CC
         MVI   SKIP1,X'8B'         SET MACH SKIP TO CH1
         MVI   OLINE,X'09'         SET MACH SINGLE SPACE
         B     CCOK
NOCC     DS    0H
         IFON  FACC,CCASA
         IFON  FMCC,SETMACH
         SETON NOCCFLG
         SPACE 1
*        ASA CC WILL BE USED
         SPACE 1
         OI    SYSPRINT+36,X'04' SET ASA CC
         SETON FACC              SET ASA FLAGS
         B     CCOK
         SPACE 1
SETMACH  DS    0H
         OI    SYSPRINT+36,X'02'  FORCE MACH CC
         MVI   SKIP1,X'8B'         SET MACH SKIP TO CH1
         MVI   OLINE,X'09'         SET MACH SINGLE SPACE
         B     CCOK                                       *LBD 07/85*
CCASA    DS    0H                                         *LBD 07/85*
         IFON  FMCC,SETMACH       USE MACH AS TOLD
         SETON FACC                                       *LBD 07/85*
         OI    SYSPRINT+36,X'04'  FORCE ASA CC
         B     CCOK                                       *LBD 07/85*
         SPACE 1                                          *LBD 07/85*
CCOK     DS    0H
         CLI   REMOTE,C' '        ANY REMOTE
         BNE   DOIT2
         MVC   REMOTE,=CL8'LOCAL'
DOIT2    DS    0H
         MVC   COPY,COPY#
         LA    R1,COPY
CLP      CLI   0(R1),C' '         END OF COPY
         BE    DC
         LA    R1,1(R1)           BUMP IT
         B     CLP
DC       MVC   0(2,R1),=C',;'
         MVC   DUPOPT,=CL5'NO,;'
         IFOFF DUPLEX,OKDUP
         MVC   DUPOPT,=C'YES,;'
         EJECT
OKDUP    DS    0H
         $STCK NOGEN
         EJECT
         IFOFF X2700,NO2700
         MVC   XCRESET+1(1),ESCAPE
         MVC   XCFONT+1(1),ESCAPE
         MVC   XCFONT#+1(1),ESCAPE
         MVC   XCMARG+1(1),ESCAPE
         MVC   X271+1(1),ESCAPE
         MVC   X272+1(1),ESCAPE
         MVC   X273+1(1),ESCAPE
         MVC   X274+1(1),ESCAPE
         MVC   X275+1(1),ESCAPE
         MVC   X276+1(1),ESCAPE
         MVC   X277+1(1),ESCAPE
         MVC   X279+1(1),ESCAPE
         MVC   X27A1+1(1),ESCAPE
         MVC   X27B+1(1),ESCAPE
         MVC   X27D+1(1),ESCAPE
         MVC   X27E1+1(1),ESCAPE
         MVC   X27F+1(1),ESCAPE
         MVC   X27H+1(1),ESCAPE
         MVC   X27J+1(1),ESCAPE
         MVC   X27PRT,REMOTE
         CLI   SAVECC,C' '                                 *LBD 01/86*
         BE    NOMCC                                       *LBD 01/86*
         MVC   X27CC1,=CL3'CC='                            *LBD 01/86*
         MVC   X27CC2,SAVECC                               *LBD 01/86*
NOMCC    DS    0H                                          *LBD 01/86*
         MVC   X27FONT,FONT                               *LBD 07/85*
         MVC   XDATE,DATE
         MVC   XTIME,TIME
         IFOFF X2700M,NO2700    NO MARGIN TO WORRY ABOUT
         LA    R1,XCMARGN
         MVC   0(4,R1),TOF      MOVE TOP OF FORM
TOFR1UP  LA    R1,1(R1)         BUMP R1
         CLI   0(R1),C' '       END OF TOF
         BNE   TOFR1UP          NO
         MVI   0(R1),C','
         LA    R1,1(R1)
         MVC   0(4,R1),BOF
BOFR1UP  LA    R1,1(R1)
         CLI   0(R1),C' '       END OF BOF
         BNE   BOFR1UP
         MVI   0(R1),C','
         LA    R1,1(R1)
         MVC   0(4,R1),LM
LMR1UP   LA    R1,1(R1)
         CLI   0(R1),C' '       END OF LM
         BNE   LMR1UP
         MVI   0(R1),C','
         LA    R1,1(R1)
         MVC   0(4,R1),RM
         SPACE 1
NO2700   DS    0H
         IFOFF X9700,DOSHOW
         IFON  ODD,DOSHOW         SKIP THIS ALLOC          *LBD 01/86*
         LA    R2,ALLOC1
         ALLOC SYSOUT=SOC,DEST=REMOTEN,                                *
               DDNRET=SYSDDN,                               *LBD 06/85 *
               FORMS=X97FORMS,FCB='JB10',ERROR=S99FAIL
         MVC   SYSPRINT+40(8),SYSDDN+2
         EJECT
DOSHOW   DS    0H
         LOAD  EP=SHOW,ERRET=NOSHOW
         ST    R0,SHOWEPA
****     GET CENTER
         MVC   PARMID,=CL8'CENTER'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   CENTER,PARMV
****     GET USERID
         MVC   PARMID,=CL8'USERID'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   XUSER,PARMV
****     GET NAME
         IFON  FNAME,SNAME      SKIP NAME ?               *LBD 07/85*
         MVC   PARMID,=CL8'NAME'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   NAME,PARMV
SNAME    DS    0H                                         *LBD 07/85*
         MVC   XNAME,NAME
*** GET  MAIL  CODE
         IFON  FMAIL,SMAIL      SKIP MAIL ?               *LBD 07/85*
***                                                        *LBD 06/86*
****     USE COMPMAIL IF AVAILABLE                         *LBD 06/86*
***                                                        *LBD 06/86*
         MVC   PARMID,=CL8'COMPMAIL'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   GMAIL
         MVC   MAIL,PARMV
         OC    MAIL,BLANKS         insure upper case       *LBD 07/86*
         CLI   MAIL,C' '           is it blank             *LBD 07/86*
         BE    GMAIL               yes - get real mail     *LBD 07/86*
         B     SMAIL                                       *LBD 06/86*
GMAIL    DS    0H                                          *LBD 06/86*
         MVC   PARMID,=CL8'MAIL'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   MAIL,PARMV
SMAIL    DS    0H                                         *LBD 07/85*
         MVC   XMAIL,MAIL
*** GET  JOBNAME
         MVC   PARMID,=CL8'JOBNAME'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   XJOBNM,PARMV
*** GET  ACCTNO
         IFON  FACCT,SACCT                                *LBD 07/85*
         MVC   PARMID,=CL8'ACCTNO'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   ACCTNO,PARMV
SACCT    DS    0H                                         *LBD 07/85*
*** GET  FDS
         IFON  FFDS,SFDS                                  *LBD 07/85*
         MVC   PARMID,=CL8'FDS'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   FDS,PARMV
SFDS     DS    0H                                         *LBD 07/85*
         MVC   DIV,FDS
*** GET  PHONE
         IFON  FPHONE,SPHONE                              *LBD 07/85*
         MVC   PARMID,=CL8'PHONE'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   PHONE,PARMV
SPHONE   DS    0H                                         *LBD 07/85*
         MVC   XPHONE,PHONE
*** GET  DEPT
         IFON  FDEPT,SDEPT                                *LBD 07/85*
         MVC   PARMID,=CL8'DEPT'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   DEPT,PARMV
SDEPT    DS    0H                                         *LBD 07/85*
         MVC   XDEPT,DEPT
*** GET  GROUP
         IFON  FGROUP,SGROUP                              *LBD 07/85*
         MVC   PARMID,=CL8'GROUP'
         L     R15,SHOWEPA
         LA    R1,PARMS
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ABEND
         MVC   GROUP,PARMV
SGROUP   DS    0H                                         *LBD 07/85*
         MVC   XGROUP,GROUP
         SPACE 1
         DELETE EP=SHOW
         SPACE 1
         B     NOSHOW
ABEND    ABEND (15)
         SPACE 1
NOSHOW   DS    0H
         IFON  ODD,NOSETSYS                                *LBD 01/86*
         MVC   SYSPRINT+82(2),=H'250'  SET LRECL           *LBD 01/86*
         MVC   SYSPRINT+62(2),=H'254'  SET BLKSIZE         *LBD 01/86*
         OI    SYSPRINT+36,X'40'  SET VARIABLE LEN RECORD  *LBD 01/86*
NOSETSYS DS    0H                                          *LBD 01/86*
         IFOFF X9700,NORMALL
         IFON  ODD,ALLOC1                                  *LBD 01/86*
         LA    R2,ALLOC1
         ALLOC DDNRET=SYSDDNX,SYSOUT=SOC,DEST=REMOTEN,                 X
               FORMS=X97FORMS,FCB='JB10',ERROR=S99FAIL
         MVC   SYSPRINT+40(8),SYSDDNX+2
ALLOC1   DS    0H
         OPEN  (SYSPRINT,OUTPUT)
         $PUT  SYSPRINT,C1
         $PUT  SYSPRINT,C2
         CLI   X97FMT,C' '
         BE    NC4F
         $PUT  SYSPRINT,C4F
NC4F     DS    0H
         CLI   X97SHFT,C' '
         BE    NC4FS
         $PUT  SYSPRINT,C4FS
NC4FS    DS    0H
         CLI   X97FORMX,C' '
         BE    NC4FN
         $PUT  SYSPRINT,C4FN
NC4FN    DS    0H
         IFOFF FCOLL,NOC4FC                                *LBD 06/86*
         $PUT  SYSPRINT,C4FC                               *LBD 06/86*
         B     YC4FC                                       *LBD 06/86*
NOC4FC   DS    0H                                          *LBD 06/86*
         MVC   C4FCOPT(5),=CL5'NO,; '                      *LBD 06/86*
         $PUT  SYSPRINT,C4FC                               *LBD 06/86*
         MVC   C4FCOPT+3(2),BLANKS                         *LBD 06/86*
YC4FC    DS    0H                                          *LBD 06/86*
         CLI   TOF,C' '
         BE    NTOFX97
         MVC   X97TOF,TOF
         LA    R1,X97TOF+1
CTOFX    CLI   0(R1),C' '       END OF TOF
         BE    PC4TOF
         LA    R1,1(R1)
         B     CTOFX
PC4TOF   MVC   0(2,R1),=C',;'
         $PUT  SYSPRINT,C4TOF
NTOFX97  DS    0H
         CLI   BOF,C' '
         BE    NBOFX97
         MVC   X97BOF,BOF
         LA    R1,X97BOF+1
CBOFX    CLI   0(R1),C' '       END OF BOF
         BE    PC4BOF
         LA    R1,1(R1)
         B     CBOFX
PC4BOF   MVC   0(2,R1),=C',;'
         $PUT  SYSPRINT,C4BOF
NBOFX97  DS    0H
         $PUT  SYSPRINT,C3
         $PUT  SYSPRINT,C4
         CLI   BLDG,C' '                                    *LBD 01/86*
         BNE   PC5                                          *LBD 01/86*
         MVC   C5X(FLOOR-C5X),BLANKS                        *LBD 01/86*
PC5      DS    0H                                           *LBD 01/86*
         $PUT  SYSPRINT,C5
         $PUT  SYSPRINT,CD
         $PUT  SYSPRINT,CCOMM                             *LBD 07/85*
         $PUT  SYSPRINT,X97DSM                            *LBD 07/85*
         MVC   C97CEN,CENTER                               *LBD 01/86*
         AP    X97CR#P,=P'01'                              *LBD 01/86*
         UNPK  X97CR#(2),X97CR#P                           *LBD 01/86*
         OI    X97CR#+1,X'F0'                              *LBD 01/86*
         MVC   X97CRD,C97A                                 *LBD 01/86*
         $PUT  SYSPRINT,X97CR                             *LBD 07/85*
         MVC   X97CRD(30),=CL30'OPTIONS USED:'
         AP    X97CR#P,=P'01'                              *LBD 01/86*
         UNPK  X97CR#(2),X97CR#P                           *LBD 01/86*
         OI    X97CR#+1,X'F0'                              *LBD 01/86*
         $PUT  SYSPRINT,X97CR                             *LBD 07/85*
         MVC   C971A,JDE
         MVC   C971C,DUPOPT                                *LBD 01/86*
         MVC   C971B,COPY#                                 *LBD 01/86*
         LA    R1,C971                                     *LBD 01/86*
         BAL   R14,FIXLINE                                 *LBD 01/86*
         AP    X97CR#P,=P'01'                              *LBD 01/86*
         UNPK  X97CR#(2),X97CR#P                           *LBD 01/86*
         OI    X97CR#+1,X'F0'                              *LBD 01/86*
         MVC   X97CRD,C971                                 *LBD 01/86*
         $PUT  SYSPRINT,X97CR                             *LBD 07/85*
         MVC   C972A,X97FMT
         MVC   C972C,X97FORMX                              *LBD 01/86*
         MVC   C972B,X97SHFT                               *LBD 01/86*
         LA    R1,C972                                     *LBD 01/86*
         BAL   R14,FIXLINE                                 *LBD 01/86*
         AP    X97CR#P,=P'01'                              *LBD 01/86*
         UNPK  X97CR#(2),X97CR#P                           *LBD 01/86*
         OI    X97CR#+1,X'F0'                              *LBD 01/86*
         MVC   X97CRD,C972                                 *LBD 01/86*
         $PUT  SYSPRINT,X97CR                             *LBD 07/85*
         MVC   C973A,X97TOF                                *LBD 01/86*
         MVC   C973B,X97BOF                                *LBD 01/86*
         MVC   C973C,SAVECC                                *LBD 01/86*
         LA    R1,C973                                     *LBD 01/86*
         BAL   R14,FIXLINE                                 *LBD 01/86*
         AP    X97CR#P,=P'01'                              *LBD 01/86*
         UNPK  X97CR#(2),X97CR#P                           *LBD 01/86*
         OI    X97CR#+1,X'F0'                              *LBD 01/86*
         MVC   X97CRD,C973                                 *LBD 01/86*
         $PUT  SYSPRINT,X97CR                             *LBD 07/85*
         AP    X97CR#P,=P'01'                              *LBD 01/86*
         UNPK  X97CR#(2),X97CR#P                           *LBD 01/86*
         OI    X97CR#+1,X'F0'                              *LBD 01/86*
         MVC   X97CRD,BLANKS                               *LBD 06/86*
         MVC   X97CRD(L'C4FCO+3),C4FCO                     *LBD 06/86*
         $PUT  SYSPRINT,X97CR                              *LBD 06/86*
         IFOFF X97CCF,DOC6
         SPACE 1
         MVI   OLINE+1,C' '
         MVC   OLINE+2(79),OLINE
         SETON X97CCS
RX97     GET   SYSIN
         MVC   OLINE+1(80),0(R1)
         $PUT  SYSPRINT,OLINE
         B     RX97             AND KEEP READING
X97CCEOJ DS    0H
         SPACE 1
DOC6     $PUT  SYSPRINT,C6
         B     COPYIT
NORMALL  DS    0H
         LA    R2,ALLOC2
         IFON  ODD,ALLOC2                                  *LBD 01/86*
         LA    R1,1                                        *LBD 02/86*
         ST    R1,FWD             SET COPY COUNT OF 1      *LBD 02/86*
         IFON  X3700,DOAL2                                 *LBD 02/86*
         LH    R1,COPYCLEN      LEN OF COPY COUNT
         BCTR  R1,R0            LESS 1 FOR PACK
         EX    R1,PACKCOPY                                 *LBD 08/86*
         CVB   R1,DWD           CONVERT TO BINARY
         ST    R1,FWD           AND SAVE IT
DOAL2    DS    0H                                          *LBD 02/86*
         ALLOC DDNRET=SYSDDN2,SYSOUT=SOC,DEST=REMOTEN,ERROR=S99FAIL,   x
               COPIES=FWD
         MVC   SYSPRINT+40(8),SYSDDN2+2
ALLOC2   DS    0H
         OPEN  (SYSPRINT,OUTPUT)
         IFOFF X2700,COPYIT
         $PUT SYSPRINT,SKIP1    SKIP TO CH1....
         $PUT SYSPRINT,XCUDK
         IFON  X27BAN,NOBANNER                             *LBD 02/86*
         $PUT SYSPRINT,X271
         $PUT SYSPRINT,X272
         $PUT SYSPRINT,X273
         $PUT SYSPRINT,X274
         $PUT SYSPRINT,X275
         $PUT SYSPRINT,X276
         $PUT SYSPRINT,X277
         $PUT SYSPRINT,X278
         $PUT SYSPRINT,X279
         $PUT SYSPRINT,X27A
         $PUT SYSPRINT,X27A1
         $PUT SYSPRINT,X27A2
         $PUT SYSPRINT,X27B
         $PUT SYSPRINT,X27C
         $PUT SYSPRINT,X27D
         $PUT SYSPRINT,X27E
         $PUT SYSPRINT,X27E1
         $PUT SYSPRINT,X27E2
         $PUT SYSPRINT,X27F
         $PUT SYSPRINT,X27G
         $PUT SYSPRINT,X27H
         $PUT SYSPRINT,X27I
         IFOFF X2700M,NOX27M                               *LBD 01/86*
         MVC   X27DA,TOF                                   *LBD 01/86*
         MVC   X27DB,BOF                                   *LBD 01/86*
         MVC   X27DC,LM                                    *LBD 01/86*
         MVC   X27DD,RM                                    *LBD 01/86*
         MVC   X27DCC+1(1),ESCAPE
         $PUT  SYSPRINT,X27DCC                             *LBD 01/86*
         $PUT  SYSPRINT,X27DLINE                           *LBD 01/86*
NOX27M   DS    0H                                          *LBD 01/86*
         $PUT  SYSPRINT,X27J                              *LBD 07/85*
         CLC   X27COMM(2),=CL2' ' NULL COMMENT             *LBD 01/86*
         BE    NOX27COM                                    *LBD 01/86*
         MVC   X27CUON(1),ESCAPE
         MVC   X27CUOFF(1),ESCAPE
         MVI   X27CUON+1,C'u'                              *LBD 01/86*
         MVI   X27CUOFF+1,C'w'                             *LBD 01/86*
NOX27COM DS    0H                                          *LBD 01/86*
         $PUT  SYSPRINT,X27K                              *LBD 07/85*
NOBANNER DS    0H                                          *LBD 02/86*
         $PUT  SYSPRINT,XCFONT
         IFOFF X2700M,NOMARG
         $PUT  SYSPRINT,XCMARG
NOMARG   DS    0H
         $PUT  SYSPRINT,XCFONT#
         IFOFF X3700,COPYIT                                *LBD 02/86*
         LH    R1,COPYCLEN      LEN OF COPY COUNT
         BCTR  R1,R0            LESS 1 FOR PACK
         EX    R1,PACKCOPY                                 *LBD 08/86*
         UNPK  X37CPY#(3),DWD+6(2)                         *LBD 02/86*
         OI    X37CPY#+2,X'F0'                             *LBD 03/86*
         MVC   X37CPY+1(1),ESCAPE                          *LBD 02/86*
         MVC   X37PG+1(1),ESCAPE                           *LBD 02/86*
         $PUT  SYSPRINT,X37PG                              *LBD 02/86*
         $PUT  SYSPRINT,X37CPY                             *LBD 02/86*
         $PUT  SYSPRINT,BLANKS                             *LBD 03/86*
         SPACE 2                                           *LBD 02/86*
COPYIT   DS    0H
         SPACE  2
READ     GET   INPUT
         IFOFF COUNTR,NOCOUNT                               *LBD 05/87*
         AP    RCTR,=P'01'                                  *LBD 05/87*
         CP    RCTR,SREC           AT START ?               *LBD 05/87*
         BL    READ                NO                       *LBD 05/87*
         CP    EREC,=P'0'                                   *LBD 05/87*
         BE    NOCOUNT                                      *LBD 05/87*
         CP    RCTR,EREC                                    *LBD 05/87*
         BH    EOF                                          *LBD 05/87*
NOCOUNT  DS    0H                                           *LBD 05/87*
         LR    R5,R1               SAVE R1
NOUMOD   DS    0H
         LH    R1,INPUT+82         LOAD LRECL
         TM    INPUT+36,X'40'   VARIABLE RECORD
         BZ    DOFIX            NO - FIXED
         SH    R1,=H'05'        LESS RDW FOR MVC
         IFOFF SNUM,NSNUMV                                  *LBD 10/87*
         SH    R1,=H'08'          LESS SEQ LENGTH           *LBD 10/87*
         LA    R5,8(R5)           -> PAST SEQ FIELD         *LBD 10/87*
NSNUMV   DS    0H                                           *LBD 10/87*
         IFON  NOCCFLG,MVARC
         EX    R1,MOVEVAR
         B     DOVARA
MVARC    EX    R1,MOVEVARC
         LA    R1,1(R1)         ADD 1 FOR CC
         B     DOVARA
DOVARA   DS    0H
         AH    R1,=H'05'        ADD RDW BACK
         STH   R1,DATA          SET BACK RDW
         B     DOPUT
DOFIX    DS    0H
         BCTR  R1,R0            LESS 1 FOR MVC
         IFOFF SNUM,NOSNUMF                                 *LBD 10/87*
         SH    R1,=H'08'          LESS 8 FOR SEQ LENGTH     *LBD 10/87*
NOSNUMF  DS    0H                                           *LBD 10/87*
         IFON  NOCCFLG,MFIXC
         EX    R1,MOVEFIX
         B     DOFIXA
MFIXC    EX    R1,MOVEFIXC
         LA    R1,1(R1)         ADD 1 FOR CC
DOFIXA   DS    0H
         LA    R1,5(R1)         ADD 5 FOR RDW AND MVC LEN
         STH   R1,DATA
DOPUT    DS    0H
         IFON  FIRST,DOPUTIT                              *LBD 07/85*
         IFON  NOCCFLG,SETASA1                            *LBD 07/85*
         SPACE 1                                          *LBD 07/85*
DOPUTA   DS    0H                                         *LBD 07/85*
         CLI   DATA+4,C'1'      ASA SKIP TO 1             *LBD 07/85*
         BE    DOPUTIT          YES - OK                  *LBD 07/85*
         CLI   DATA+4,X'8B'     MACH SKIP TO 1            *LBD 07/85*
         BE    DOPUTIT          YES - OK                  *LBD 07/85*
         IFON  FACC,SETASA1                               *LBD 07/85*
         PUT   SYSPRINT,SKIP1VB SKIP MACH TO CH 1         *LBD 07/85*
         B     DOPUTIT                                    *LBD 07/85*
SETASA1  MVI   DATA+4,C'1'      SET ASA SKIP TO 1         *LBD 07/85*
         SPACE 1                                          *LBD 07/85*
DOPUTIT  DS    0H                                         *LBD 07/85*
         LA    R5,DATA          -> DATA
         PUT   SYSPRINT,0(R5)
         LA    R9,1(R9)         ADD 1 TO LINE COUNTER
         IFON  FIRST,READ                                 *LBD 07/85*
         SETON FIRST                                      *LBD 07/85*
         IFOFF FACC,READ                                  *LBD 07/85*
         MVI   DATA+4,C' '      SET ASA TO SKIP 1 LINE    *LBD 07/85*
         B     READ
EOF      DS    0F
         CLOSE INPUT
         IFOFF X2700,CK97
         $PUT  SYSPRINT,XCUDK
         $PUT  SYSPRINT,XCRESET
         B     CLOSEIT
CK97     DS    0H
         IFOFF X9700,CLOSEIT
         $PUT  SYSPRINT,SKIP1
         $PUT  SYSPRINT,C7
CLOSEIT  DS    0H
         CLOSE (SYSPRINT,FREE,INPUT)
EXIT     DS    0H
EPILOG   DS     0H
         LR    R15,R9
         IFON  SPF,SPFEXIT
         OPEN  (REPORT,OUTPUT)
         CVD   R9,DWD
         MVC   RLINE(10),=X'40206B2020206B202120'
         ED    RLINE(10),DWD+4
         PUT   REPORT,RLINEOUT
         CLOSE REPORT
         $EPILOG 0
         EJECT
SPFEXIT  DS    0H
         CVD   R9,DWD
         MVC   SHORT(10),=X'40206B2020206B202120'
         ED    SHORT(10),DWD+4
         MVC   LONG(10),SHORT
         MVC   LPRT,REMOTE
         ISPF  LOAD
         VDEFINE '(SHORT)',SHORT,CHAR,20
         VDEFINE '(LONG)',LONG,CHAR,60
         VPUT    '(SHORT LONG)'
         SETMSG  MSG='AOST010'
         ISPF  DELETE
         $EPILOG 0
         EJECT
S99FAIL  DS    0H
         CLC   4(2,R14),=X'0410'  DD ALREADY ALLOCATED ?
         BER   R2                 YES CONTINUE
         S99FAIL
         $EPILOG 4                EXIT
         EJECT
PUTROU   DS    0H
         EX    R3,MOVEVAR0      MOVE DATA
         LA    R3,4(R3)         ADD LEN OF RDW
         XC    DATA(4),DATA     ZERO RDW
         STH   R3,DATA          SAVE INTO RDW
         PUT   0(R1),DATA
BLANKDAT DS    0H
         MVI   DATA,C' '
         MVC   DATA+1(255),DATA
         BR    R8               RETURN
         EJECT
FIXLINE  DS    0H                                          *LBD 01/86*
         TR    0(40,R1),TRTBL     INSURE ONLY PRINTABLE    *LBD 01/86*
         OC    0(40,R1),BLANKS    UPPER CASE IT ALL        *LBD 01/86*
         BR    R14                AND RETURN               *LBD 01/86*
TRTBL    DS    0F                                          *LBD 01/86*
         DC    256X'00'                                    *LBD 01/86*
         ORG   TRTBL+126                                   *LBD 01/86*
         DC    C'='                                        *LBD 01/86*
         ORG   TRTBL+C'A'                                  *LBD 01/86*
         DC    C'ABCDEFGHI'                                *LBD 01/86*
         ORG   TRTBL+C'J'                                  *LBD 01/86*
         DC    C'JKLMNOPQR'                                *LBD 01/86*
         ORG   TRTBL+C'S'                                  *LBD 01/86*
         DC    C'STUVWXYZ'                                 *LBD 01/86*
         ORG   TRTBL+C'0'                                  *LBD 01/86*
         DC    C'0123456789'                               *LBD 01/86*
         ORG   ,                                           *LBD 01/86*
         EJECT
         LTORG
DWD      DS    D
FWD      EQU   DWD+4,4
DAY      EQU   DWD,1
SREC     DC    PL8'0'                                       *LBD 05/87*
EREC     DC    PL8'0'                                       *LBD 05/87*
RCTR     DC    PL8'0'                                       *LBD 05/87*
LCTR     DC    PL2'99'
PCTR     DC    PL2'00'
FLAG     DC    X'00'
SKIP1VB  DC    AL2(20),AL2(0)                             *LBD 07/85*
SKIP1    DC    CL20'1'
OLINE    DC    CL81' '
SAVECC   DC    C' '                                        *LBD 01/86*
BLANKS   DC    CL80' '                                      *LBD 01/86*
         SPACE 2
***      PARMS FOR SHOW PROGRAM
PARMS    DS    0D
         DC    A(PARMID,PARMLEN,PARMV)
         DC    AL1(80),AL3(PARMVL)
PARMID   DC    CL8' '
PARMLEN  DC    A(L'PARMV)
PARMV    DC    CL100' '
PARMVL   DC    F'0'
SHOWEPA  DS    F
         SPACE 1
PACKCOPY PACK  DWD,COPY#(0)     PACK IT                    *LBD 08/86*
COPYCLEN DC    Y(8)
COPY#    DC    CL8'00000001'
         SPACE
X97FORMS DC    A(X97FORM),Y(4)
X97FORM  DC    CL4'DZH'           XEROX 9700 FORM
         SPACE
REMOTEN  DC    A(REMOTE),Y(8)
REMOTE   DC    CL8' '
         SPACE
SOC      DC    A(CLASS),Y(1)
CLASS    DC    C'A'
         EJECT
         BITSW X97CCS,SPF,DUPLEX,X9700,CARDS,X2700,NOCCFLG,X2700M
         SPACE
         BITSW FACC,FMCC,X97CCF,FIRST,ODD,X27BAN,X3700,FORMX97
         SPACE
         BITSW FNAME,FMAIL,FACCT,FFDS,FDEPT,FGROUP,FPHONE,FCOLL
         SPACE 1                                            *LBD 05/87*
         BITSW COUNTR,SNUM                                  *LBD 05/87*
         SPACE
         ISPF  PARMS
         SPACE
         DS    0H
DATA     DC    CL256' '
         SPACE 2
MOVEVARC MVC   DATA+5(0),4(R5)  *** EXECUTED
MOVEFIXC MVC   DATA+5(0),0(R5)  *** EXECUTED
         SPACE
MOVEVAR  MVC   DATA+4(0),4(R5)  *** EXECUTED
MOVEFIX  MVC   DATA+4(0),0(R5)  *** EXECUTED
         SPACE
MOVEVAR0 MVC   DATA+4(0),0(R5)  *** EXECUTED
         SPACE 2
SHORT    DC    CL20' '
         ORG   SHORT+11
         DC    C'LINES'
         ORG   ,
LONG     DC    CL60' '
         ORG   LONG+11
         DC    C'LINES PRINTED ON '
LPRT     DC    CL8' '
         ORG   ,
         SPACE
RLINEOUT DC    CL80' '
         ORG   RLINEOUT
         DC    C'1  '
RLINE    DC    CL10' '
         DC    C' LINES PRINTED BY GENPRINT'
         ORG   ,
         EJECT                                             *LBD 01/86*
C97A     DC    CL40' '                                     *LBD 01/86*
         ORG   C97A                                        *LBD 01/86*
         DC    C'ORIGINATING CENTER: '                     *LBD 01/86*
C97CEN   DC    CL8' '                                      *LBD 01/86*
         ORG   ,                                           *LBD 01/86*
         SPACE 1                                           *LBD 01/86*
C971     DC    CL40' '                                     *LBD 01/86*
         ORG   C971                                        *LBD 01/86*
         DC    C'JDE='                                     *LBD 01/86*
C971A    DC    CL3' '                                      *LBD 01/86*
         DC    C' COPIES='                                 *LBD 01/86*
C971B    DC    CL8' '                                      *LBD 01/86*
         DC    C'DUPLEX='                                  *LBD 01/86*
C971C    DC    CL3' '                                      *LBD 01/86*
         ORG   ,                                           *LBD 01/86*
         SPACE 2                                           *LBD 01/86*
C972     DC    CL40' '                                     *LBD 01/86*
         ORG   C972                                        *LBD 01/86*
         DC    C'FORMAT='                                  *LBD 01/86*
C972A    DC    CL8' '                                      *LBD 01/86*
         DC    C' SHIFT='                                  *LBD 01/86*
C972B    DC    CL3' '                                      *LBD 01/86*
         DC    C' FORMS='                                  *LBD 01/86*
C972C    DC    CL8' '                                      *LBD 01/86*
         ORG   ,                                           *LBD 01/86*
         SPACE 2                                           *LBD 01/86*
C973     DC    CL40' '                                     *LBD 01/86*
         ORG   C973                                        *LBD 01/86*
         DC    C'TOF='                                     *LBD 01/86*
C973A    DC    CL8' '                                      *LBD 01/86*
         DC    C' BOF='                                    *LBD 01/86*
C973B    DC    CL3' '                                      *LBD 01/86*
         DC    C' CC='                                     *LBD 01/86*
C973C    DC    C' '                                        *LBD 01/86*
         ORG   ,                                           *LBD 01/86*
         EJECT                                             *LBD 01/86*
         SPACE
C1       DC    CL81' '
         ORG   C1
         DC    C'1DFM1'
         ORG   C1+8
         DC    C'JDE='
JDE      DC    CL3'L03'
         DC    C',DEPT='
         DC    X'7D'
FDS      DC    CL3' '
ACCTNO   DC    CL6' '
         DC    X'7D'
         DC    C',COPIES='
COPY     DC    CL8' '
         ORG   ,
C2       DC    CL81' '
         ORG   C2
         DC    C' DFM1'
         ORG   C2+8
         DC    C'DUPLEX='
DUPOPT   DC    CL5' '
         ORG   ,
C3       DC    CL81' '
         ORG   C3
         DC    C' DFM1'
         ORG   C3+8
         DC    C'RTEXT=('
         DC    X'7D'
         DC    C'DELIVER TO: '
NAME     DC    CL20' '
         DC    CL2' '                                      *LBD 03/86*
PHONE    DC    CL12' '                                     *LBD 03/86*
         DC    C' '                                        *LBD 03/86*
         DC    X'7D'
         DC    C',01,03,01),;'
         ORG   ,
C4       DC    CL81' '
         ORG   C4
         DC    C' DFM1'
         ORG   C4+8
         DC    C'RTEXT=('
         DC    X'7D'
         DC    C'DIV='
DIV      DC    CL3' '
         DC    C' DEPT='
DEPT     DC    CL4' '
         DC    C' GROUP='
GROUP    DC    CL4' '
         DC    X'7D'
         DC    C',01,04,01),;'
         ORG   ,
         SPACE 1
C4F      DC    CL80' '
         ORG   C4F
         DC    C' DFM1'
         ORG   C4F+8
         DC    C'FORMAT='
X97FMT   DC    CL8' '
         ORG   ,
         SPACE 1
C4FS     DC    CL80' '
         ORG   C4FS
         DC    C' DFM1'
         ORG   C4FS+8
         DC    C'SHIFT='
X97SHFT  DC    CL5' '
         ORG   ,
         SPACE 1
C4FN     DC    CL80' '
         ORG   C4FN
         DC    C' DFM1'
         ORG   C4FN+8
         DC    C'FORMS='
X97FORMX DC    CL8' '
         ORG   ,
         SPACE 1
C4FC     DC    CL80' '
         ORG   C4FC
         DC    C' DFM1'
         ORG   C4FC+8
C4FCO    DC    C'COLLATE='
C4FCOPT  DC    CL3'YES'                                    *LBD 06/86*
         DC    C',;'                                       *LBD 06/86*
         ORG   ,
         SPACE 1
C4TOF    DC    CL80' '
         ORG   C4TOF
         DC    C' DFM1'
         ORG   C4TOF+8
         DC    C'TOF='
X97TOF   DC    CL8' '
         ORG   ,
         SPACE 1
C4BOF    DC    CL80' '
         ORG   C4BOF
         DC    C' DFM1'
         ORG   C4BOF+8
         DC    C'BOF='
X97BOF   DC    CL8' '
         ORG   ,
         SPACE 1
C5       DC    CL80' '
         ORG   C5
         DC    C' DFM1'
         ORG   C5+8
         DC    C'RTEXT=('
         DC    X'7D'
         DC    C'MAIL CODE='
MAIL     DC    CL8' '
C5X      DC    CL3' ',C'BLDG='
BLDG     DC    CL4' ',CL1' ',C'FLOOR='
FLOOR    DC    CL2' '
         DC    X'7D'
         DC    C',01,05,01),;'
         ORG   ,
         SPACE 1
CD       DC    CL80' '
         ORG   CD
         DC    C' DFM1'
         ORG   CD+8
         DC    C'RTEXT=('
         DC    X'7D'
         DC    C'DATE='
DATE     DC    CL8' '
         DC    CL3' ',C'TIME='
TIME     DC    CL8' '
         DC    X'7D'
         DC    C',01,06,01),;'
         ORG   ,
         SPACE 2
CCOMM    DC    CL80' '                                    *LBD 07/85*
         ORG   CCOMM                                      *LBD 07/85*
         DC    C' DFM1'                                   *LBD 07/85*
         ORG   CCOMM+8                                    *LBD 07/85*
         DC    C'RTEXT=('
         DC    X'7D'
X97COMM  DC    CL45' '
         DC    X'7D'
         DC    C',01,08,01),;'
         ORG   ,
         SPACE 2
X97DSM   DC    CL100' '                                   *LBD 07/85*
         ORG   X97DSM                                     *LBD 07/85*
         DC    C' DFM1'                                   *LBD 07/85*
         ORG   X97DSM+8                                   *LBD 07/85*
         DC    C'RTEXT=('
         DC    X'7D',C'DSN='
X97DSN   DC    CL56' '
         DC    X'7D'
         DC    C',01,10,01),;'
         ORG   ,
         SPACE 2
X97CR    DC    CL100' '                                    *LBD 01/86*
         ORG   X97CR                                       *LBD 01/86*
         DC    C' DFM1'                                   *LBD 07/85*
         ORG   X97CR+8                                    *LBD 07/85*
         DC    C'RTEXT=('
         DC    X'7D'
X97CRD   DC    CL40' '                                     *LBD 01/86*
         DC    X'7D'
         DC    C',01,'
X97CR#   DC    C'13'                                       *LBD 01/86*
         DC    C',01),;'                                   *LBD 01/86*
         ORG   ,
X97CR#P  DC    PL2'10'                                     *LBD 01/86*
         SPACE 2
C6       DC    CL80' '
         ORG   C6
         DC    C' DFM1'
         ORG   C6+8
         DC    C'END;'
         ORG   ,
C7       DC    CL80' *END*OF*REPORT*'
         SPACE 2
         DYNSPACE ,
         EJECT
*        XEROX 2700 CONTROL CARDS
XCUDK    DC    CL80' '
         ORG   XCUDK+2
         DC    C'='
         DC    C'UDK'
         DC    C'='
ESCAPE   DC    C'~'
         ORG   ,
         SPACE 1
XCMARG   DC    CL80' '
         ORG   XCMARG+2
         DC    C'm660,'
XCMARGN  DC    CL30' '
         ORG   ,
         SPACE 1
TOF      DC    CL5' '
BOF      DC    CL5' '
RM       DC    CL5' '
LM       DC    CL5' '
         SPACE 1
XCRESET  DC    CL20'  +X'
         SPACE 1
XCFONT   DC    CL80' '
         ORG   XCFONT
         DC    C'  +3'
FONT     DC    CL30' '
         ORG   ,
         SPACE 1
XCFONT#  DC    CL20'  3'        SET FONT FOR REPORT
         EJECT
*        XEROX 2700 BANNER PAGE
         SPACE
X37PG    DC    C'  +P'                                     *LBD 03/86*
         SPACE
X37CPY   DC    CL10' '            XEROX 3700 ONLY          *LBD 02/86*
         ORG   X37CPY             COPY COUNT               *LBD 02/86*
         DC    C'  +C'                                     *LBD 02/86*
X37CPY#  DC    CL3' '                                      *LBD 02/86*
         ORG   ,                                           *LBD 02/86*
         SPACE
X271     DC    C'  +1Titan10iso-P'  Xerox 2700 Font
         SPACE 1
X272     DC    C'  1'           SET UP FOR BANNER FONT
         SPACE 1
X273     DC    C'  x225,1300,2100,15'
X274     DC    C'  y225,1300,1200,15'
X275     DC    C'  x225,2500,2100,15'
X276     DC    C'  y2325,1300,1215,15'
X277     DC    C'  a500,2390'
X278     DC    CL80' '
         ORG   X278+3
         DC    CL15'Author:     '
XNAME    DC    CL20' '
         DC    C' ('
XUSER    DC    CL7' ',C' - '
XJOBNM   DC    CL8' ',C')'                                *LBD 07/85*
         ORG   ,
X279     DC    C'  a500,2290'
X27A     DC    CL80' '
         ORG   X27A+3
         DC    CL15'Mail:       '
XMAIL    DC    CL8' ',CL12' '
         DC    C' Phone: '                                 *LBD 03/86*
XPHONE   DC    CL12' '                                     *LBD 03/86*
         ORG   ,                                          *LBD 07/85*
X27A1    DC    C'  a500,2190'                             *LBD 07/85*
X27A2    DC    CL80' '                                    *LBD 07/85*
         ORG   X27A2+3
         DC    CL15'Dept:       '
XDEPT    DC    CL4' ',C'/'
XGROUP   DC    CL4' '
         ORG   ,
X27B     DC    C'  a500,2090'
X27C     DC    CL80' '
         ORG   X27C+3
         DC    CL15'Date:       '
XDATE    DC    CL8' '
         DC    CL2' '
XTIME    DC    CL8' '
         ORG   ,
X27D     DC    C'  a500,1990'
X27E     DC    CL80' '
         ORG   X27E+3
         DC    CL15'Printer:    '
X27PRT   DC    CL8' '
         DC    CL3' '                                      *LBD 01/86*
X27CC1   DC    CL3' '                                      *LBD 01/86*
X27CC2   DC    C' '                                        *LBD 01/86*
         ORG   ,
X27E1    DC    C'  a500,1890'
X27E2    DC    CL80' '                                    *LBD 07/85*
         ORG   X27E2+3                                    *LBD 07/85*
         DC    CL15'From Center:'
CENTER   DC    CL8' '
         ORG   ,
X27F     DC    C'  a500,1790'                             *LBD 07/85*
X27G     DC    CL80' '
         ORG   X27G+3                                     *LBD 07/85*
         DC    CL15'Font:       '
X27FONT  DC    CL30' '                                    *LBD 07/85*
         ORG   ,
X27H     DC    C'  a500,1690'
X27I     DC    CL80' '
         ORG   X27I+3
         DC    CL15'Dataset:    '
X27DSN   DC    CL56' '
         ORG   ,
         SPACE 1                                           *LBD 01/86*
X27DCC   DC    C'  a500,1550'
X27DLINE DC    CL80' '                                     *LBD 01/86*
         ORG   X27DLINE+3                                  *LBD 01/86*
         DC    C'Margin Options:   '                       *LBD 01/86*
         DC    C'TOF='                                     *LBD 01/86*
X27DA    DC    CL5' '                                      *LBD 01/86*
         DC    C'BOF='                                     *LBD 01/86*
X27DB    DC    CL5' '                                      *LBD 01/86*
         DC    C'LM='                                      *LBD 01/86*
X27DC    DC    CL5' '                                      *LBD 01/86*
         DC    C'RM='                                      *LBD 01/86*
X27DD    DC    CL5' '                                      *LBD 01/86*
         ORG   ,                                           *LBD 01/86*
         SPACE 1                                           *LBD 01/86*
X27J     DC    C'  a500,1400'
X27K     DC    CL80' '
         ORG   X27K+5
X27CUON  DC    CL2' '                                      *LBD 01/86*
X27COMM  DC    CL45' '
X27CUOFF DC    CL2' '                                      *LBD 01/86*
         ORG   ,
         EJECT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,EXLST=EXITSYSP
         EJECT
INPUT    DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT,EODAD=EOF,EXLST=IJFCB
         EJECT
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=EOPARM
         EJECT
REPORT   DCB   DSORG=PS,MACRF=PM,DDNAME=REPORT,LRECL=80,               X
               RECFM=FBA,BLKSIZE=6160
         EJECT
         DS    0F                                          *LBD 01/86*
EXITSYSP DC    X'85',AL3(SYSPEXIT)                         *LBD 01/86*
SYSPEXIT DS    0H                                          *LBD 01/86*
         OI    SYSPRINT+36,X'50'  FORCE RECFM=VB           *LBD 01/86*
         CLC   SYSPRINT+82(2),=H'00'   ANY LRECL ?         *LBD 01/86*
         BNE   SYSPEX1                 YES - CHECK BLKSIZE *LBD 01/86*
         MVC   SYSPRINT+82(2),=H'250'  SET LRECL           *LBD 01/86*
SYSPEX1  DS    0H                                          *LBD 01/86*
         CLC   SYSPRINT+62(2),=H'00'   ANY BLKSIZE         *LBD 01/86*
         BNER  R14                YES - RETURN             *LBD 01/86*
         MVC   SYSPRINT+62(2),=H'254'  SET BLKSIZE         *LBD 01/86*
         BR    R14                AND RETURN               *LBD 01/86*
         EJECT
         DS    0F                                          *LBD 01/86*
IJFCB    DC    X'87',AL3(INFMJFCB)
         IEFJFCBN LIST=YES
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         END   ,
