         MACRO
&NAME    PSWGEN &FUNC,&PARM,&REG,&VAR                      PROTOTYPE
         GBLB  &EXECN,&EXECF            MULTIPLE EXPANSION SWITCHES
         LCLC  &HEX,&SUFFIX             CHARACTER VARIABLES
&SUFFIX  SETC  '&SYSNDX'(2,4)           SET 3 CHARACTER SYSNDX VALUE
.*
.*
.*             ALL FUNCTIONS OF THIS MACRO WILL USE CSECT NAME OF THE
.*             INVOKING PROCEDURE FOR FUNCTION NAME...
.*
.*             WHEN MORE THAN ONE CSECT INVOKE THE SAME FUNCTION,
.*             THEY WILL ENTER THE SELECTED CODE VIA 'STACKED' ENTRIES-
.*       SUCH AS -                      .FIRST   ANOP
.*                                      .NEXT    ANOP
.*                                       .LAST   ANOP            ETC.
.*
.***********************
         AIF   ('&FUNC' EQ 'IGG019V5A').LPSW01
         AIF   ('&FUNC' EQ 'IGG019V5B').LPSW02
         AGO   .PSWNG3                  UNSUPPORTED FUNCTION
.LPSW01  ANOP
***********************************************************************
*  THIS FUNCTION OF PSWGEN OWNED AND MAINTAINED BY IBM PERSONNEL,
*  GENERATES CODE FOR LPSW INSTRUCTION FOR 1419 SUPPORT
*        IOSGEN  IGG019V5A,&PARM,&REG
*        &PARM--SYMBOL DEFINING INSTRUCTION ADDRESS TO BE PUT IN PSW
*        &REG--A SUBLIST OF 3 REGISTER NAMES. THE FIRST MUST CONTAIN
*              THE DEB ADDRESS AND THE OTHER TWO MUST BE TWO CONTIGUOUS
*              REGISTERS.
***********************************************************************
         SPACE 1
         MVZ   PSW1+1(1),PTKEY(&REG(1)) PUT USERS PROTECT KEY INTO PSW.
         LA    &REG(2),&PARM            LOAD ADDR
         ST &REG(2),PSW1+4              STORE ADDR IN PSW
         L     &REG(2),PSW2             SAVE FIRST HALF OF RETURN PSW
         LPSW  PSW1                     SET USERS PROTECT KEY-BRANCH
         SPACE 1
         DS    0D
PSW1     DC    X'00'                    AREA
         DC    X'04'                    FOR
         DC    2X'00'                   BUILDING USERS
         DC    F'0'                     PSW
         SPACE 1
PSW2     DC    X'00'                    FIRST HALF
         DC    X'04'                    OF RETURN
         DC    2X'00'                   PSW
PTKEY    EQU   24                       DISPLACEMENT TO PROTECT KEY
         MEXIT
.LPSW02  ANOP
*  THIS FUNCTION OF PSWGEN OWNED AND MAINTAINED BY IBM PERSONNEL,
*  GENERATES CODE FOR LPSW INSTRUCTION FOR 1419 SUPPORT
*        IOSGEN  IGG019V5B,&PARM,&REG
*        &PARM--SYMBOL DEFINING INSTRUCTION ADDRESS TO BE PUT IN PSW
*        &REG--A SUBLIST OF 3 REGISTER NAMES. THE FIRST MUST CONTAIN
*              A SAVE AREA ADDRESS (DOUBLE WORD BOUNDARY) AND THE OTHER
*              TWO MUST BE TWO CONTIGUOUS REGISTERS.
         SPACE 1
         LA    &REG(3),&PARM            LOAD ADDR
         STM   &REG(2),&REG(3),0(&REG(1))  PUT PSW IN SAVE AREA
         LPSW  0(&REG(1))               RESTORE KEY 0 AND BRANCH
         MEXIT
.**********************************************************************
.*       ALL MNOTES SHOULD BE PLACED BELOW --
.*
.*                                                                    *
.PSWNG2  MNOTE 8,'*** INSUFFICIENT WORK REGISTERS SPECIFIED.'
         MEXIT
.*                                                                    *
.PSWNG3  MNOTE 8,'*** ERROR IN PARAMETER SET UP, OR ''&FUNC'' NOT A SUP+
               PORTED FUNCTION.'
.*
.PSWGEND MEND
