         MACRO
&NAME    IEAQCB  &SYS,&SECT
.*   IEAQCB IS A PRIVATE MACRO THAT WILL EXPAND THE FOLLOWING -
.*                 IEAQCB01 - MFT CSECT
.*                 IEAQCB02 - MVT CSECT
.*                 NQDQQCB- MFT AND/OR MVT DSECT
.*   THE DEVELOPMENT OF THIS MACRO AND AND ITS 4 EXPANSIONS WAS MADE
.*   NECESSARY BY 'REFRESHABILITY' IN THE MFT AND MVT VERSIONS OF
.*   SVC 48 AND SVC 56.
.*   THE 2 CSECTS EACH CONTAIN THE FOLLOWING -
.*                 POINTER TO THE HEAD OF ENQ/DEQ CHAIN OF CONTROL
.*                      BLOCKS (MAY BE REFERENCED BY OTHER THAN
.*                      THE SVC'S)
.*                 2 SAVE AREAS (TO BE USED ONLY BY THE SVC'S)
.*       THE 2 CSECTS DIFFER IN THAT MFT HAS AN ADDITIONAL ALIAS FOR
.*       HEADQCB POINTER WHICH MVT DOES NOT HAVE.
.*   THE 2 DSECTS ACCORDINGLY MAP OUT THE 2 CSECTS (DEPENDING ON
.*   THE PARAMETERS PASSED). THE DSECTS WOULD SEEM TO BE OF VALUE
.*   ONLY TO SVC 48 AND SVC 56 BUT ARE NOT SO LIMITED.
         AIF   (('&SYS' NE 'MFT') AND ('&SYS' NE 'MVT')).ERR1
         AIF  (('&SECT' NE 'C') AND ('&SECT' NE 'D')).ERR11
         AIF   ('&NAME' EQ '').NONAME
.SECT    AIF   ('&SECT' EQ 'D').DSECT
&NAME    CSECT
         DS    0F
         AGO   .MAIN
.DSECT   ANOP
&NAME    DSECT
         AGO   .MAIN
.NONAME  AIF   ('&SECT' EQ 'D').DSECT2
         AIF   ('&SYS' EQ 'MFT').MFT22
IEAQCB02  CSECT
         DS    0F
         AGO   .MAIN
.MFT22   ANOP
IEAQCB01 CSECT
         DS    0F
         AGO   .MAIN
.DSECT2  ANOP
NQDQQCB  DSECT
.MAIN    AIF   ('&SYS' EQ 'MFT').MFT2
         AIF   ('&SECT' EQ 'D').NOENT
         ENTRY HEADQCB
         ENTRY IEAQQCB0
.NOENT   ANOP
HEADQCB  DC    AL4(0)
IEAQQCB0 EQU   HEADQCB
         AGO   .NOENTT
.MFT2    AIF   ('&SECT' EQ 'D').NOENTRY
         ENTRY HEADQCB
         ENTRY IEA0EQ01
         ENTRY  IEA0QCB0
.NOENTRY ANOP
HEADQCB  DC    AL4(0)
IEA0QCB0 EQU   HEADQCB
IEA0EQ01 EQU   HEADQCB
.NOENTT  ANOP
SAVEA    DS    2F
SAVEB    DS    1F
TEMPSAVE  DS    1F .               SAVE AREA FOR REG 2           A34816
GMREGSAV  DS    13F .              SAVE AREA FOR REGS 3-15       A34816
         AIF   ('&SECT' EQ 'D').NODC
        DC   C'HEADQCB'
.NODC  ANOP
         MEXIT
.ERR1    MNOTE   'SYSTEM KEYWORD MISSING OR INVALID'
         MEXIT
.ERR11   MNOTE  'SECT KEYWORD MISSING OR INVALID'
         MEND
