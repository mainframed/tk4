         MACRO
&NAME    XLATE &AREA,&LENGTH,&TO=E
.*       THIS MACRO IS DESIGNED TO LOAD REGISTER 0 WITH A BYTE COUNT,
.*       REGISTER 1 WITH AN ADDRESS OF DATA TO BE TRANSLATED, AND BIT 0
.*       OF REGISTER 1 IS SET TO INDICATE THE DIRECT OF TRANSLATION.
.*       AREA ARGUMENT MAY BE (1) NAME OF DATA AREA, (2) ADCON WHICH
.*       POINTS TO DATA AREA, (3) REGISTER CONTAINING THE ADDRESS OF
.*       THE DATA AREA. THE LENGTH ARGUMENT MAY (1) NAME OF LOCATION
.*       CONTAINING BYTE COUNT, (2) REGISTER CONTAINING BYTE COUNT.
.*       REGISTERS USED AS ARGUMENTS MUST BE IN THE FORM (R), WHERE R
.*       MAY BE ANY REGISTER NUMBER,2 THRU 12
         LCLA  &K
         AIF   ('&AREA' EQ '&LENGTH').ERR
         AIF   (N'&SYSLIST LT 2).ERR1
         AIF   ('&AREA' EQ '(0)' OR '&LENGTH' EQ '(1)').ERR2
         AIF   ('&TO' EQ 'E').START
         CNOP  0,4
&K       SETA  &SYSNDX
&NAME    B     *+8 BRANCH AROUND CONSTANT
IHB&K    DC    X'80000000' MASK TO TRANSLATE ASCII
.* TEST FOR PROPER NUMBER AND TYPE OF OPERANDS
.START   AIF   ('&NAME' EQ '' OR '&TO' NE 'E').REGS
&NAME    DS    0H
.* DETERMINE REGISTER FORMAT
.REGS    AIF   ('&AREA'(1,1) EQ '(').REG1
         AIF   (T'&AREA EQ 'A' OR T'&AREA EQ 'F').NOREG1
         LA    1,&AREA ADDRESS OF TRANSLATE AREA
         AGO   .LEN1
.REG1    AIF   ('&AREA'(2,2) EQ '1)').LEN1
         LR    1,&AREA(1) ADDRESS OF TRANSLATE AREA
         AGO   .LEN1
.NOREG1  L     1,&AREA ADDRESS OF TRANSLATE AREA
.LEN1    AIF   ('&LENGTH'(1,1) EQ '(').REG2
         AIF   (T'&LENGTH EQ 'A' OR T'&LENGTH EQ 'F').REG3
         AIF   (T'&LENGTH EQ 'H').REG4
         LA    0,&LENGTH LENGTH TO TRANSLATE
.CALL    AIF   ('&TO' EQ 'E').SVC
         O     1,IHB&K INDICATE TRANSLATE TO ASCII
.SVC     SVC   103 ISSUE TRANSLATE SVC
         AGO   .END
.REG2    AIF   ('&LENGTH'(2,2) EQ '0)').CALL
         LR    0,&LENGTH(1) LENGTH TO TRANSLATE
         AGO   .CALL
.REG3    L     0,&LENGTH LENGTH TO TRANSLATE
         AGO   .CALL
.REG4    LH    0,&LENGTH LENGTH TO TRANSLATE
         AGO   .CALL
.ERR     MNOTE 8,'SPECIFIED REGISTERS ARE EQUAL'
         MEXIT
.ERR1    MNOTE 8,'TOO FEW POSITIONAL PARAMETERS IN MACRO INSTRUCTION'
         MEXIT
.ERR2    MNOTE 8,'INVALID REGISTER SPECIFICATION'
.END     MEND
