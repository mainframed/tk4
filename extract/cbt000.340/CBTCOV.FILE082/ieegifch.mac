         MACRO
         IEEGIFCH &F,&D,&N,&A,&L,&S
         GBLC  &SCHDLR
         SPACE
FETCH    EQU   *
*                       SUCCESSOR DETERMINATOR
*                       VERSION 3  5/7/65
*  REPLACEABLE SYMBOLS DIRECT APPROPRIATE CODE FOR VARIOUS POSSIBLE
*  LINKAGE EXITS. IN USE, SYMBOLS ARE INCLUDED FOR EACH POSSIBLE
*  CONDITION.
*
*   F    FAULT     MESSAGE GENERATION.
*   D    DEFER     PPS DEFERRAL CONDITION.
*   N    NAME      USER HAS SUPPLIED NAME OF EXPLICITE SUCCESSOR.
*   A    AUXILARY  NAME PREVIOUSLY SPECIFIED.
*   L    LOCATE    PROCESSING READER SUPPLIED COMMAND.
*   S    SOURCE    RETURN TO BASE MODULE.
*
         SPACE
.FETCHFA AIF   ('&F' NE 'F').FETCHDA
*                                           IS MESSAGE TO BE ASSEMBLED.
*                       F    FAULT
         TM    MSERM,F                      TEST FOR ERROR CODE.
         BZ    *+18                         NO.
FETCHF   EQU   *                            FOR DIRECT ENTRANCE.
         NI    MSFHF,F-MSCSD-MSNMF-MSFAX    KILL FLAGS.
         MVC   FHFHP(8),FHFER               LOAD MODULE NAME.
         B     FETCHG                       TO LINKAGE AFFECTOR.
.FETCHDA AIF   ('&D' NE 'D').FETCHNA
*                                           SEQUENCE DEFERRED.
*                       D    DEFER
         TM    MSFHF,MSCSD                  TEST FOR DEFERRAL REQUEST.
         BZ    *+26                         NO.
FETCHD   EQU   *                            FOR DIRECT ENTRANCE.
         NI    MSFHF,F-MSCSD-MSNMF-MSFAX    KILL FLAGS.
         OI    MSPFG,MSCCS                  OPERATOR PENDING ON.
         MVI   MSERM,MSMSG011               TELL HIM ABOUT REJECTION.
         MVC   FHFHP(8),FHFER               MODULE NAME.
         B     FETCHG                       TO LINKAGE AFFECTOR.
.FETCHNA AIF   ('&N' NE 'N').FETCHAA
*                                           EXPLICITE NAME GIVEN.
*                       N    NAME
         TM    MSFHF,MSNMF                  TEST FLAG FOR NAME SUPPLIED
         BZ    *+12
FETCHN   EQU   *                            FOR DIRECT ENTRANCE.
         NI    MSFHF,F-MSCSD-MSNMF          KILL FLAGS.
         B     FETCHG                       TO LINKAGE AFFECTOR.
.FETCHAA AIF   ('&A' NE 'A').FETCHLA
*                                           AUXILARY (SECOND SUCCESSOR)
*                       A    AUXILARY
         TM    MSFHF,MSFAX                  TEST.
         BZ    *+18                         NO.
FETCHA   EQU   *                            DIRECT ENTRANCE.
         NI    MSFHF,F-MSCSD-MSNMF-MSFAX    KILL FLAGS.
         MVC   FHFHP(8),MSFAL               MODULE NAME FROM RESIDENT.
         B     FETCHG                       TO LINKAGE AFFECTOR.
.FETCHLA AIF   ('&L' NE 'L').FETCHSA
*                                           LOCATE PENDING REQUESTS.
*                       L    LOCATE
         TM    MSFHF,MSTTT
         BZ    *+26
FETCHL   EQU   *
         NI    MSFHF,F-MSCSD-MSNMF-MSFAX    KILL FLAGS.
         MVC   FHFHP(8),FHFLC
         B     FETCHG
         AIF   ('&SCHDLR' EQ 'PSS').FETCHL1
FHFLC    DC    CL8'IEEG21GM'                PPS AND PSS DEPENDENT.
         AGO   .FETCHSA
.FETCHL1 ANOP
FHFLC    DC    CL8'IEEGA1GM'                LOCATE PSS
.FETCHSA AIF   ('&S' NE 'S').FETCHXA
*                                           EXECUTION COMPLETION.
*                       S    SOURCE
*
         AIF   ('&SCHDLR' EQ 'SSS').FETCHSR
*
FETCHS   EQU   *
         NI    MSFHF,F-MSCSD-MSNMF-MSFAX
         MVC   FHFHP(8),FHSRC
         B     FETCHG
FHSRC    DC    CL8'IEEFA001'                SOURCE MODULE.
*
         AGO   .FETCHXA
.FETCHSR ANOP
         SVC   EXIT
*
.FETCHXA ANOP
FETCHG   EQU   *
*                                           ISSUE XCTL.
         LA    BASE,FXCTLP
         SVC   XCTL
XCTL     EQU   7
*
*                            XCTL PARAMETER LIST.
FXCTLP   DS    0D
         DC    A(FHFHP)                     POINTS TO MODULE NAME.
         DC    F'0'                         DCB ADDRESS=0.
FHFHP    DC    CL8' '                       NAME ARGUMENT FOR XCTL.
         AIF   ('&F' NE 'F' AND '&D' NE 'D').FETCHEA
         AIF   ('&SCHDLR' NE 'SSS').FETCHE1
FHFER    DC    CL8'IEEGK1GM'                MESSAGE GENERATION MODULE.
         AGO   .FETCHEA
.FETCHE1 ANOP
FHFER    DC    CL8'IEEGK2GM'                FAULT PPS-PSS.
.FETCHEA ANOP
         MEND
