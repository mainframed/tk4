         MACRO
&TABID   IEMKTCA &R,&ET=,&L=,&OPS=RS,&NPTRS=,&DLF=
.*
.*  THIS MACRO SETS UP THE GLOBAL VARIABLES DESCRIBING THE TABLE WHICH
.*  ARE USED BY THE HTAB MACRO, AND IF &R IS INCLUDED GENERATES CODE
.*  FOR THE TCA.
.*
         GBLA  &N                  INDICATES CURRENT ELEMENT OF ARRAYS
         GBLA  &GAANPTS(15)        HOLDS NPTRS PARAMETERS
         GBLB  &GBAETF(15)         BIT ON IF ET=F CODED
         GBLB  &GBAETV(15)         BIT ON IF ET=T CODED
         GBLB  &GBAOPSR(15)        BIT ON IF OPS=R CODED
         GBLB  &GBAOPSS(15)        BIT ON IF OPS=S CODED
         GBLC  &GCAIDS(15)         HOLD TABLE IDENTIFIERS
.*
         LCLB  &LBAERR
         LCLA  &LAACNT
.*
.*  THE FOLLOWING SECTION CHECKS THAT THE PARAMETERS ARE VALID
.*
         AIF   (T'&ET NE 'O').SSVC00
         MNOTE 12,'NO ET PARAMETER'
&LBAERR  SETB  1
         AGO   .SSVC01
.SSVC00  ANOP
         AIF   ('&ET' EQ 'F' OR '&ET' EQ 'V' OR '&ET' EQ 'T').SSVC01
         MNOTE 12,'INVALID ET PARAMETER'
&LBAERR  SETB  1
.SSVC01  AIF   (NOT('&ET' EQ 'F' AND T'&L EQ 'O')).SSVC02
         MNOTE 8,'NO LENGTH PARAMETER FOR ET=F'
&LBAERR  SETB  1
.SSVC02  AIF   (NOT('&ET' EQ 'V' AND T'&DLF EQ 'O')).SSVC03
         MNOTE 8,'NO DLF PARAMETER FOR ET=V'
&LBAERR  SETB  1
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TCA001-TSS
.SSVC03  AIF   ('&OPS' EQ 'R' OR '&OPS' EQ 'S' OR '&OPS' EQ 'RS' OR    -
               '&OPS' EQ 'SR').SSVC04
         MNOTE 8,'INVALID OPS PARAMETER'
&LBAERR  SETB  1
*        ----------------------------------------------------TCA001-TSS
.SSVC04  AIF   (NOT('&ET' EQ 'F' AND T'&DLF NE 'O')).SSVC05
         MNOTE 8,'DLF PARAMETER ILLEGAL FOR ET=F'
&LBAERR  SETB  1
.SSVC05  AIF   (NOT('&ET' EQ 'V' AND T'&L NE 'O')).SSVC06
         MNOTE 8,'LENGTH PARAMETER ILLEGAL FOR ET=V'
&LBAERR  SETB  1
*        +++++++++++++++++++++++++++++++++++++++++++++++++++*TCA002-TSS
.SSVC06  AIF   (T'&NPTRS EQ 'O' OR '&NPTRS' EQ '1' OR '&NPTRS' EQ '2'  -
               OR '&NPTRS' EQ '3' OR '&NPTRS' EQ '4').SSVC07
         MNOTE 8,'INVALID NPTRS PARAMETER'
&LBAERR  SETB  1
*        ----------------------------------------------------TCA002-TSS
.SSVC07  AIF   ('&ET' NE 'T').SSVC10
         AIF   (T'&L EQ 'O').SSVC08
         MNOTE 8,'LENGTH PARAMETER ILLEGAL FOR ET=T'
&LBAERR  SETB  1
.SSVC08  AIF   (T'&DLF EQ 'O').SSVC09
         MNOTE 8,'DLF PARAMETER ILLEGAL FOR ET=T'
&LBAERR  SETB  1
.SSVC09  AIF   (T'&NPTRS EQ 'O' OR '&NPTRS' EQ '1').SSVC10
         MNOTE 8,'NPTRS PARAMETER INVALID FOR ET=T'
&LBAERR  SETB  1
.*
.SSVC10  AIF   (NOT &LBAERR).SSSG01
         MEXIT
.*
.*  THE FOLLOWING SECTION SETS UP THE GLOBAL VARIABLES
.*
.SSSG01  AIF   (&N LT 15).SSSG02
         MNOTE 12,'TOO MANY TABLES DECLARED IN THIS ASSEMBLY'
         MEXIT
.SSSG02  ANOP
&N       SETA  &N+1                UPDATE CURRENT TABLE POINTER
&GCAIDS(&N) SETC '&TABID'          STORE TABLE IDENTIFIER
&GBAETF(&N) SETB ('&ET' EQ 'F')    SET ET PARAMETER
&GBAETV(&N) SETB ('&ET' EQ 'V')      CODE BITS
         AIF   ('&OPS' NE 'R').SSSG03            SET
&GBAOPSR(&N) SETB 1                              OPS
&GBAOPSS(&N) SETB 0                              PARAMETER
         AGO   .SSSG05                           CODE
.SSSG03  AIF   ('&OPS' NE 'S').SSSG04            BITS
&GBAOPSR(&N) SETB 0                               .
&GBAOPSS(&N) SETB 1                               .
         AGO   .SSSG05                            .
.SSSG04  ANOP                                     .
&GBAOPSR(&N) SETB 1                               .
&GBAOPSS(&N) SETB 1                               .
.SSSG05  AIF   (T'&NPTRS EQ 'O').SSSG06
&GAANPTS(&N) SETA &NPTRS           STORE NPTRS PARAMETER
         AGO   .SSGC01
.SSSG06  AIF   (&GBAOPSS(&N)).SSSG07    STORE
&GAANPTS(&N) SETA 0                     DEFAULT
         AGO   .SSGC01                  VALUES
.SSSG07  ANOP                           FOR
&GAANPTS(&N) SETA 1                     NPTRS
.*
.*  THE FOLLOWING SECTION GENERATES CODE FOR THE TCAS IF &R INCLUDED
.*
.SSGC01  AIF   (T'&R NE 'O').SSGC02
         MEXIT
.SSGC02  ANOP
*
&R       DS    0A
         AIF   ('&ET' EQ 'T').SSGC10
.*
.*  GENERATE TCA FOR ET=F OR ET=V
.*
         AIF   (T'&L EQ 'O').SSGC03
         DC    AL2(&L)
         AGO   .SSGC04
.SSGC03  DC    AL2(0)
.SSGC04  AIF   (T'&DLF EQ 'O').SSGC05
         DC    AL2(&DLF)
         AGO   .SSGC06
.SSGC05  DC    AL2(0)
.SSGC06  DC    B'10&GBAETF(&N)&GBAETV(&N)0000'
         DC    X'00'
         DC    AL2(0)
         DC    2A(0)
         DC    3A(0)
         AIF   (NOT &GBAOPSR(&N)).SSGC08
         AIF   (NOT &GBAOPSS(&N)).SSGC09
         DC    3A(0)
.SSGC08  AIF   (&GAANPTS(&N) EQ &LAACNT+1).SSGC09
&LAACNT  SETA  &LAACNT+1
         DC    3A(0)
         AGO   .SSGC08
.SSGC09  DC    2A(0),X'80',AL3(0)
*
         MEXIT
.*
.*  GENERATE TCA FOR ET=T
.*
.SSGC10  DC    6A(0)
*
         MEND
