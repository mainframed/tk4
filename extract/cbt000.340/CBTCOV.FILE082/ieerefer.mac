         MACRO
&NAME    IEEREFER &CODE,&PARM,&MF=
         LCLA  &NMF,&NPARM,&FSET,&LNG,&Y
         LCLB  &BAL,&CALLG,&X(14),&Z
         LCLC  &FMF,&B,&I(2),&PMF
         AIF   ('&CODE' NE 'MAP').NOMAP
*
*           INPUT                                     OUTPUT
*
*  *          DD NAME              -                               *
*  * CVT OFFSET    - ENTRY OFFSET  -                               *
*  * TIOT ENTRY PTR- TTR/UCB PTR   -                               *
*  * UCB NAME  -   - VOLUME SERIAL         - F - C - RETURN PTR    *
*  *                                         L   O                 *
*  *                               *         A   D                 *
*  *                         *     *         G   E *               *
*  *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *
*
*
REFER    DS    0D                      PARAMETER LIST DEFINITION.
REFSIZE  DS    0CL16                   BLOCK SIZE IN BYTES.
*                               ***
*                                 DEFINITION A.
REFDDN   DC    CL8' '             DD NAME.
         DC    CL2' '
*                               ***
         ORG   REFER              DEFINITION  B.
REFPTA   DS    F                  INPUT TIOT PTR OR CVT OFFSET.
REFPTB   DS    F                  INPUT TTR OR UCB PTR OR 2ND OFFSET.
*                               ***
         ORG   REFER              DEFINITION  C.
REFUCBN  DS    CL3                UCB NAME.
         DS    CL1
REFVOLS  DS    CL6                VOLUME SERIAL,
*
*                               ***
REFFLAG  DC    X'00'              FLAG BITS.
REFNSTR  EQU   1                  1-SUPPRESS STORE INTO LIST.
REFCODE  DC    X'00'              FUNCTION CODE.
REFPTR   DC    F'0'               POINTER VALUE RETURN SLOT.
REFEND   EQU   *
         MEXIT
.NOMAP   ANOP
&Y       SETA  N'&CODE
&NPARM   SETA  N'&PARM
&NMF     SETA  N'&MF
&B       SETC  '&CODE(1)'
&Z       SETB  (&Y GT 1)
&FMF     SETC  '&MF(1)'
&I(1)    SETC  '0'
&I(2)    SETC  '0'
         AIF   ('&PARM(1)' EQ '').RGEN
&I(1)    SETC  '&PARM'
         AIF   (N'&PARM LT 2).RGEN
&I(1)    SETC  '&PARM(1)'
&I(2)    SETC  '&PARM(2)'
.RGEN    ANOP
&CALLG   SETB  ('&FMF' EQ 'E' OR '&FMF' EQ '')
&BAL     SETB  (&NMF GT 0)
.*                 PRE-EMPTED(KEY) SYMBOL REDUCTION.
&X(1)    SETB  ('&B' EQ 'RCVTP')
&X(2)    SETB  ('&B' EQ 'RCVTEP')
&X(3)    SETB  ('&B' EQ 'RCVTEPE')
&X(4)    SETB  ('&B' EQ 'RTCBP')
&X(5)    SETB  ('&B' EQ 'RRBP')
&X(6)    SETB  ('&B' EQ 'RITIOTP')
&X(7)    SETB  ('&B' EQ 'RXTIOTP')
&X(8)    SETB  ('&B' EQ 'RXTIOTE')
&X(9)    SETB  ('&B' EQ 'RXTTRT')
&X(10)   SETB  ('&B' EQ 'RXUCBT')
&X(11)   SETB  ('&B' EQ 'RITTRT')
&X(12)   SETB  ('&B' EQ 'RIUCBT')
&X(13)   SETB  ('&B' EQ 'RVOL')
&X(14)   SETB  ('&B' EQ 'RUCB')
.*                                     FUNCTION CODE GENERATION.
&Y       SETA  1*&X(1)+2*&X(2)+3*&X(3)+4*&X(4)+5*&X(5)+6*&X(6)+7*&X(7)
&Y       SETA  &Y+8*&X(8)+9*&X(9)+10*&X(10)+11*&X(11)+12*&X(12)
&Y       SETA  &Y+13*&X(13)+14*&X(14)
         AIF   (&NMF GT 1).EXC
         AIF   ('&MF' EQ 'E').CALL
         AIF   (&BAL).LFORM
         CNOP  4,8
         BAL   1,*+20                  POINT TO LIST AND BRANCH.
.LFORM   ANOP
&NAME    DS    0D                      REFER PARAMETER LIST.
         AIF   (&X(13)).UCBN
         AIF   (&X(14)).VOL
         AIF   (&X(8) OR &X(9) OR &X(10)).DD
         DC    A(&I(1))                1ST INPUT.
         DC    A(&I(2))                2ND INPUT.
         DC    H'0'                    ZERO.
         AGO   .CODEG
.UCBN    DC    CL3'&PARM'              UCB NAME GIVEN.
         DC    CL1' '                  BLANK.
         DC    CL6' '                  VOLUME SERIAL RETURN.
         AGO   .CODEG
.VOL     DC    CL3' '                  UCB NAME RETURN.
         DC    CL1' '                  BLANK.
         DC    CL6'&PARM'              VOLUME SERIAL GIVEN.
         AGO   .CODEG
.DD      DC    CL8'&PARM'              DD NAME GIVEN.
         DC    H'0'                    ZERO.
.CODEG   ANOP
         DC    AL1(&Z)                 REFFLAG   FLAGS.
         DC    AL1(&Y)                 REFCODE   FUNCTION CODE.
         DC    F'0'                    REFPTR    RETURNED POINTER.
         AGO   .LINK
.EXC     ANOP
&PMF     SETC  '&MF(2)'
         AIF   ('&PMF'(1,1) NE '(').A
&PMF     SETC  '0&PMF'
.A       AIF   ('&I(1)'(1,1) NE '(').B
&I(1)    SETC  '0&I(1)'
.B       AIF   ('&I(2)'(1,1) NE '(').C
&I(2)    SETC  '0&I(2)'
.C       ANOP
&NAME    DS    0H
         LA    1,&PMF                  PARAMETER LIST ADDRESS.
         MVI   10(1),&Z                INSERT FLAGS.
         MVI   11(1),&Y                INSERT CODE.
         AIF   (&X(13)).RUCBN
         AIF   (&X(14)).RVOL
         AIF   (&X(8) OR &X(9) OR &X(10)).RDD
         LA    0,&I(1)
         ST    0,0(1)
         LA    0,&I(2)
         ST    0,4(1)
         AGO   .LINK
.RUCBN   ANOP
&FSET    SETA  0
&LNG     SETA  3
         AGO   .MVC
.RVOL    ANOP
&FSET    SETA  4
&LNG     SETA  6
         AGO   .MVC
.RDD     ANOP
&FSET    SETA  0
&LNG     SETA  8
.MVC     ANOP
         MVC   &FSET.(&LNG,1),&I(1)         MOVE KEY.
.LINK    ANOP
         AIF   (&CALLG).CALL
         MEXIT
.CALL    ANOP
         CALL  IEEVRFRX
         MEND
