         MACRO
         IHEZAP
*        + + + + + + + + + + + + + + + + + + + + + + + + + +*ZAP001-TSS
IHEQFOP  DXD   A
IHEQCFL  DXD   2A
*        - - - - - - - - - - - - - - - - - - - - - - - - - - ZAP001-TSS
         SPACE 2
IHEPZAP  DSECT FCB APPENDAGE
TVAL     DS    XL4                      VALIDATION WORD
TRES     DS    A                        RESERVED
         SPACE
IHEZAPE  DSECT FILE CONTROL BLOCK(FCB).
         SPACE
TBEG     EQU   *
         SPACE
TFLX     DS    0BL1                     ERROR FLAG BYTE.
TDCB     DS    A                        ADDRESS OF DCB FOR FILE.
TTYP     DS    0BL1                     I/O TYPE FLAG BYTE.
TACM     DS    A                        ADDRESS OF I/O TRANSMIT MODULE.
TFLA     DS    BL1                      FLAG BYTE A.
TFLB     DS    BL1                      FLAG BYTE B.
TLEN     DS    H                        LENGTH OF FCB.
TFIO     DS    0BL1                     OPERATION FLAG BYTE.
TDCL     DS    A                        A(DCLCB).
TSWA     DS    0A                       A( USER WORK AREA (TP))
TLAB     DS    0A                       IOCB ANCHOR.
TCBA     DS    A                        CURRENT BYTE ADDRESS.
TPKA     DS    0A                       A(PREVIOUS KEY).
TREM     DS    H                        LENGTH OF RECORD REMAINING.
TMAX     DS    H                        LENGTH OF RECORD.
TBBZ     DS    0A                       LENGTH OF IOCB(C(BYTE 1)=SUBPL)
TREL     DS    0A                       REL/TRACK OR BLOCK COUNT.
TREC     DS    A                        BUFFER WORKSPACE.
TADC     DS    0A                       A(ADCON LISTS).
TCNT     DS    A                        COUNT OF SCALAR ITEMS XMITTED.
TLRR     DS    0A                       A(IOCB) OF LAST RECORD READ.
TPGZ     DS    H                        PAGE SIZE.
TLNZ     DS    H                        LINE SIZE.
TLID     DS    0H                       L( TERM ID AREA )
TLRL     DS    0H                       MAX LOGICAL RECORD LENGTH.
TLNN     DS    H                        LINE NUMBER.
TFLC     DS    BL1                      DEVICE TYPE.
TFLD     DS    BL1                      ORGANISATION FLAG BYTE.
TFLE     DS    0BL1                     HISTORY.
TFOP     DS    A                        OPENED FILE LINKAGE.
TFLF     DS    0BL1                     MODULE LIST CODE.
TTAB     DS    A                        A(TAB CONTROL TABLE).
TFMP     EQU   TTAB+1                   EXCLUSIVE FILE LOCKOUT FLAG
TEVT     DS    2A                       FCB ANCHOR FOR EVENT VAR CHAIN.
         DS    0D
TEND     EQU   *                        END OF FCB (NON-TASKING).
         ORG   TEVT+8
TXLV     DS    2A                       FCB ANCHOR FOR EXCL BLOCK CHAIN
TXLZ     DS    A                        SUBPOOL NO & L(EXCLUSIVE BLOCK)
         DS    0D
TENT     EQU   *                        END OF FCB (TASKING).
         EJECT
*       FLAG BOXES AND MASKS.
         SPACE
* TFLX
         SPACE
TFER     EQU   TFLX                     ERROR FLAGS.
TMEF     EQU   B'10000000'              EOF ON DATA SET.
TMOE     EQU   B'01000000'              ERROR ON OUTPUT.
TMIE     EQU   B'00100000'              ERROR ON INPUT.
TMIT     EQU   B'00010000'              ERROR ON DATAFIELD.
TMNX     EQU   B'00001000'              DO NOT RAISE TRANSMIT.
TMLC     EQU   B'00000010'              LIST TERMINATOR.
TMEP     EQU   B'00000001'              ENDPAGE RAISED.
         SPACE
* TTYP
         SPACE
TFTY     EQU   TTYP                     TYPE OF I/O.
TMDS     EQU   B'00000000'              DATA STREAM I/O.
TMRC     EQU   B'00000001'              RECORD I/O.
TMST     EQU   B'00000010'              STRING I/O.
TMT1     EQU   B'10000000'              TEMPORARY FLAGS.
TMT2     EQU   B'01000000'               VALID FOR SINGLE I/O CALL
TMT3     EQU   B'00100000'               ONLY.
TMT4     EQU   B'00010000'               *
         SPACE
* TFLA
         SPACE
TFMD     EQU   TFLA                     MODE.
TMIN     EQU   B'00000001'              INPUT.
TMOP     EQU   B'00000010'              OUTPUT.
TMUP     EQU   B'00000100'              UPDATE.
TMBK     EQU   B'00001000'              BACKWARDS.
         SPACE
TFRC     EQU   TFLA                     RECORD FORMATS.
TMVB     EQU   B'00010000'              V FORMAT RECORDS.
TMFX     EQU   B'00100000'              F FORMAT RECORDS.
TMUN     EQU   B'01000000'              U FORMAT RECORDS.
TMSR     EQU   B'00000000'              S FORMAT RECORDS.
         SPACE
TMAS     EQU   B'10000000'              ASA CONTROL/PRINT FILE.
         SPACE
* TFLB
         SPACE
TFFP     EQU   TFLB                     FILE PARAMETERS.
TMEX     EQU   B'10000000'              EXCLUSIVE FILE.
TMBU     EQU   B'01000000'              UNBUFFERED I/O.
TMHB     EQU   B'00100000'              HIDDEN BUFFERS EXIST.
TMPT     EQU   B'00010000'              SYSPRINT FILE.
TMTE     EQU   B'00001000'              TASKING ENVIRONMENT.
TMHQ     EQU   B'00000100'              HIDDEN BUFFER MAY BE REQUIRED.
TMKD     EQU   B'00000010'              KEYED.
TMDR     EQU   B'00000001'              DIRECT ACCESS.
         EJECT
* TFIO
         SPACE
TFOO     EQU   TFIO                     OPERATION FLAGS.
TMPW     EQU   B'10000000'              PUT OPERATION.
TMGR     EQU   B'01000000'              GET OPERATION.
TMGK     EQU   B'00001000'              GENERIC KEY OPTION
TMEI     EQU   B'00000010'              EVENT OPTION WITH IGNORE.
TMCY     EQU   B'00000001'              COPY OPTION.
         SPACE
* TFLC
         SPACE
TFDV     EQU   TFLC                     DEVICE TYPE.
TMPA     EQU   B'10000000'              PAPER TAPE.
TMPR     EQU   B'01000000'              PRINTER.
          SPACE
TFHE     EQU   TFLC                     HISTORY EXTENSION
TMPS     EQU   B'00001000'              LAST OP A READ....SET...
TMDT     EQU   B'00000100'              ATTEMPT TO CLOSE IN WRONG TASK.
TMOC     EQU   B'00000010'              OPEN OR CLOSE IN PROGRESS.
         SPACE
* TFLD
         SPACE
TFOG     EQU   TFLD                     ORGANISATION.
TMCN     EQU   X'00'                    CONSECUTIVE.
TMIX     EQU   X'04'                    INDEXED.
TMR1     EQU   X'08'                    REGIONAL(1).
TMR2     EQU   X'0C'                    REGIONAL(2).
TMR3     EQU   X'10'                    REGIONAL(3).
TMTP     EQU   X'14'                    TP
         SPACE
* TFLE
         SPACE
TFHT     EQU   TFLE                     HISTORY.
TMRP     EQU   B'10000000'              PRIOR OPERATION A READ.
TMIG     EQU   B'01000000'              IGNORE IN PROGRESS.
TMCL     EQU   B'00100000'              CLOSE IN PROGRESS.
TMET     EQU   B'00010000'              END OF EXTENT REACHED.
TMWP     EQU   B'00001000'              PRIOR OPERATION A REWRITE.
TMLT     EQU   B'00000100'              PRIOR OPERATION A LOCATE.
TMCC     EQU   B'00000010'              I/O CONDITION ON CLOSE.
TMCT     EQU   B'00000001'              IMPLICIT CLOSE.
         SPACE
*TFLF
         SPACE
TFAC     EQU   TFLF                     LOAD MODULE CODE.
TMQS     EQU   X'00'                    QSAM CODE.
TMBD     EQU   X'04'                    BDAM CODE.
TMQI     EQU   X'08'                    QISAM CODE.
TMBI     EQU   X'0C'                    BISAM CODE.
TMBS     EQU   X'10'                    BSAM CODE.
TMBL     EQU   X'14'                    BSAM LOAD CODE.
TMQT     EQU   X'18'                    QTAM
TMTB     EQU   B'00000001'              TAB CONTROL TABLE EXISTS.
         EJECT
IHEZDCL  DSECT DECLARE CONTROL BLOCK(DCLCB).
         SPACE
DPRO     DS    H                        PRV OFFSET OF FILE REGISTER.
DCLA     DS    BL2                      FILE DECLARATIONS.
DBLK     DS    H                        BLOCKSIZE.
DLRL     DS    H                        LOGICAL RECORD LENGTH.
DCLD     DS    BL1                      FILE OPTIONS.
DBNO     DS    CL1                      NUMBERS OF BUFFERS.
DCLB     DS    BL1                      FILE DECLARATIONS.
DCLC     DS    BL1                      FILE DECLARATIONS.
DXAL     DS    H                        NUMBER IN INDEXAREA SPACE   I10
DNCP     DS    XL1                      NO. OF CHANAL PROGS.
         DS    XL1                      RESERVED
         DS    A                        RESERVED.
         DS    A                        RESERVED.
DFLN     DS    BL1                      LENGTH-1 OF FILE NAME IN BYTES.
DFIL     DS    CL31                     FILENAME.
         SPACE 2
*        + + + + + + + + + + + + + + + + + + + + + + + + + +*ZAP002-TSS
IHEQADC  DXD   A                        PSEUDO REG FOR A(ADCON LIST).
*        - - - - - - - - - - - - - - - - - - - - - - - - - - ZAP002-TSS
         SPACE
IHEZADC  DSECT ADCON LIST.
         SPACE
ISADF    DS    A                        IHESADF.
ISAFD    DS    A                        IHESAFD.
IITAX    DS    A                        IHEITAX.
IERRB    DS    A                        IHEERRB.
IERRC    DS    A                        IHEERRC.
ITABS    DS    A                        IHETABS.
IITAZ    DS    A                        IHEITAZ.
IITAA    DS    A                        IHEITAA.
IFOPE    DS    A
IQFOP    DS    H                        FOP.
IQLW3    DS    H                        LW3.
IQLW2    DS    H                        LW2.
IQLW1    DS    H                        LW1.
IQLCA    DS    H                        LIB COM AREA.
IQERR    DS    H                        ERROR SLOT OFFSET.
IQLW0    DS    H                        LW0.
IQEVT    DS    H                        I/O EVENT VAR CHAIN.
IQXLV    DS    H                        I/O EXCLUSIVE BLOCK CHAIN.
IQATV    DS    H                        TASK VARIABLE.
IQCFL    DS    H                        CURRENT FILE
IQCTS    DS    H                        CONTROL TASKS STORAGE (PLF)
IONUN    DS    A                        ON UNIT ENTRY.
INLWS    DS    A                        GET LWS ENTRY.
IABNP    DS    A                        ABNORMAL END OF PROGRAM ENTRY.
IABNT    DS    A                        ABNORMAL END OF TASK ENTRY.
IPRTA    DS    A                        IHEPRTA/IHEPTTA
IPRTB    DS    A                        IHEPRTB/IHEPTTB
IDDOD    DS    A                        IHEDDOD/IHEDDTD
IOCLC    DS    A                        IHEOCLC/IHEOCTC
         EJECT
IHEZLST  DSECT PARAMETER LIST  FROM COMPILED CODE.
         SPACE
PDCL     DS    A                        A(DCLCB).
PIOB     DS    0A                       A(IOCB)   WAIT ONLY.
PRDV     DS    0A                       A(RECORD DOPE VECTOR).
PIGF     DS    A                        A(IGNORE FACTOR).
PEVT     DS    A                        A(EVENT VARIABLE)
PKEY     DS    A                        A(KEY/KEYTO/KEYFROM SDV).
PREQ     DS    A                        A(REQUEST CODE).
         SPACE
RECA     EQU   0                        OFFSETS IN RDV
RECL     EQU   4
KEYA     EQU   0                        OFFSETS IN KEY SDV
KEYL     EQU   6
         SPACE 2
IHEZREQ  DSECT OPERATION AND OPTIONS.
         SPACE
REQ1     DS    BL1                      OPERATION CODE.
REQ2     DS    BL1                      OPTION CODE GROUP 1.
REQ3     DS    BL1                      OPTION CODE GROUP 2.
REQ4     DS    BL1                      ATTRIBUTES OF OPTIONS.
REQS     DS    A                        STATEMENT TYPE
         SPACE
* REQ1
         SPACE
OPER     EQU   REQ1
         SPACE
ORED     EQU   X'00'                    READ.
OWRT     EQU   X'04'                    WRITE.
OREW     EQU   X'08'                    REWRITE.
ODEL     EQU   X'0C'                    DELETE.
OLOC     EQU   X'10'                    LOCATE.
OUNL     EQU   X'14'                    UNLOCK.
OWAT     EQU   X'18'                    WAIT.
         SPACE
* REQ2
         SPACE
OPT1     EQU   REQ2
         SPACE 2
ONIL     EQU   X'00'                    NONE/SET.
OIGN     EQU   X'04'                    IGNORE.
OIFM     EQU   X'08'                    INTO/FROM.
         SPACE
* REQ3
         SPACE
OPT2     EQU   REQ3
         SPACE
ONOT     EQU   X'00'                    NONE.
OKTO     EQU   X'04'                    KEYTO.
ONLK     EQU   X'08'                    NOLOCK.
         SPACE
* REQ4
         SPACE
OATR     EQU   REQ4
OKTV     EQU   B'10000000'              KEYTO VARIABLE IS VARYING.
OINT     EQU   B'01000000'              INTO VARIABLE IS VARYING.
OEVT     EQU   B'00100000'              EVENT OPTION PRESENT
         EJECT
IHEZIOB  DSECT IOCB (BOTH BSAM AND BDAM/BISAM).
         SPACE
BBEG     EQU   *
BACT     DS    0BL1                     ACTIVITY FLAG FOR DIRECT ACCESS
BPIO     DS    A                        A(PRIOR IOCB)  CHAIN BACKWARDS.
BNIO     DS    A                        A(NEXT IOCB) CHAIN FORWARD.
BERR     DS    0BL1                     ERROR FLAG BYTE.
BFCB     DS    A                        A(FCB).
BREQ     DS    A                        REQUEST CODES
BERC     DS    0H                       ERROR CODE.
BEFC     DS    0H                       EOF ERROR CODE.
BXTC     DS    0H                       TRANSMIT ERROR CODE.
BKYC     DS    H                        KEY ERROR CODE.
BRCC     DS    H                        RECORD ERROR CODE.
BRVS     DS    A                        A(RDV/RECORD VARIABLE'S SDV).
BEVN     DS    A                        A(EVENT VARIABLE).
*      BSAM INTERFACE.
BDF1     DS    A                        A(USER RECORD AREA).
BDF2     DS    H                        L(USER RECORD AREA).
BDF3     DS    H                        L(KEYTO AREA)
BDF4     DS    A                        A(KEYTO AREA).
BDF5     DS    A                        RELATIVE NUMBER.
*      BDAM/BISAM INTERFACE.
         ORG   BDF1
BBF1     DS    A                        A(USER RECORD AREA).
BBF2     DS    H                        L(USER RECORD AREA).
         DS    H                        RESERVED.
BBF3     DS    2A                       RELATIVE NUMBER.(BLKREF)
*                DECB.
BECB     DS    A                        ECB.
         ORG   BECB+1
*        + + + + + + + + + + + + + + + + + + + + + + + + + +*ZAP003-TSS
BEXD     DS    BL.16                    BDAM EXCEPTION CODES.
*        - - - - - - - - - - - - - - - - - - - - - - - - - - ZAP003-TSS
BTYP     DS    H                        TYPE.
BLEN     DS    H                        LENGTH.
BDCB     DS    A                        A(DCB).
BARE     DS    A                        A(USER RECORD/APPENDED BUFFER).
BSTS     DS    0A                       A(STATUS INDICATORS).
BLOG     DS    A                        A(IOB/LOGREC).
BKEY     DS    A                        A(KEY).
BBLK     DS    0A                       A(BLKREF).
BEXI     DS    A                        ISAM EXCEPTION CODES.
*        + + + + + + + + + + + + + + + + + + + + + + + + + +*ZAP004-TSS
*        - - - - - - - - - - - - - - - - - - - - - - - - - - ZAP004-TSS
*               END OF DECB
BXLV     DS    A                        A(ASSOCIATED EXCLUSIVE BLOCK).
         SPACE
         ORG   BKEY           BSAM BUFFER.
BKVS     DS    A                        A(SDV) FOR KEYTO.
         DS    0D
BDBF     DS    0A                       START OF BSAM BUFFER.
BEN1     EQU   *
         ORG   BXLV+4         BDAM/BISAM BUFFER.
         DS    0D
BBBF     DS    0A                       START OF BISAM/BDAM HIDDEN BUFF
BEN2     EQU   *
         SPACE 2
*  BERR.
BMCH     EQU   B'00000001'              IOCB HAS BEEN CHECKED.
BMER     EQU   B'00000010'              I/O CONDITIONS TO RAISE.
BMEF     EQU   B'00000100'              EOF FOUND.
BMPR     EQU   B'00001000'              POSSIBLE LOCK FOR REWRITE.
BMNR     EQU   B'00010000'              LOCK FOR REWRITE.
BMDF     EQU   B'01000000'              IOCB FOR READ UPDATE.
BMDB     EQU   B'10000000'              DUMMY BUFFER ACQUIRED.
         EJECT
         IHEEVT
         MEND
