         MACRO
&NAME    SNDSCH  &GEN=MAXI
.*CHANGE ACTIVITY= AS FOLLOWS:                                   S21903
.*A388000,573000                                                 S99228
.*A186000,217000,462600,488000                                   S22029
.*C001000-002000                                                 S22029
.*A355600,355700                                                 A42390
.*A400000,420000,426000,439000,446000,457000                     S21101
.*A507000,524000,528000,544000,566000                            S21101
.*C128000,442000,447000,458000,534000-537000                     S21101
.*D549000,606000-608000,614000                                   S21101
.*A114000,124000,403000,427050,427560,508400,535000,550000,      S22025
.*A558000,567126,567459                                          S22025
.*C128000,267000-268000,292000-293000,296000,534000,567000       S22025
.*D221000-225000,417000,427055,427085,427100-427300,427400-427700S22025
.*D439060-439240,439360-439840,505000,514000-515000,566180-566900S22025
.*D567000,567639,567657                                          S22025
.*A340450                                                         M2173
.*A220000                                                       SA56891
.*D177000-178000                                                SA56891
.*A565000,565200                                                SA52971
.*C567297-567324                                                SA52971
.*D545000,565400,567225,567477,567630-567648,569000             SA52971
.*A427020-427040,534000-534840                                  SA56606
.*D427420-427480,427560,427580-427800,534000-534500             SA56606
.*A561000                                                       SA60792
.*C567072,573200-573400                                         SA60792
.*C407500,409080,412000,443500,446000-446320                    SA61773
.*A409800,599800                                                SA58984
.*C015100-016000                                                SA58984
.*A461000,447200,524800                                         SA59034
.*C456000                                                       SA59034
.*D524100-524200                                                SA59034
.*C498000                                                       SA62953
.*A357000,357500                                                SA65412
.*A420500                                                       SA66605
.*A427000,427400                                                OX02199
.*C389100,441600                                                SA65379
.*A389800                                                       SA65379
*C 357800-357900                                                SA68703
* A561400                                                       SA69635
         SPACE 3
.**********************************************************************
         AIF   ('&GEN' EQ 'MAXI' OR '&GEN' EQ 'MINI' OR '&GEN' EQ 'NTSO-
               ').OK
         MNOTE 12,'XXX IHB     -INVALID OPERAND.'
         MEXIT
.OK      ANOP
         TITLE '&NAME     SEND SCHEDULER'
&NAME    CSECT
.L1      ANOP
*                                                                     *
***********************************************************************
*                                                                     *
*TITLE --  SEND SCHEDULER MODULE                                      *
*                                                                     *
         AIF   ('&GEN' NE 'MINI').TAG1
*  MODULE NAME = IGG019Q6 (MINI)                                      *
         AGO   .TAG3
.TAG1    AIF   ('&GEN' NE 'NTSO').TAG2
*  MODULE NAME = IGG019Q7 (NTSO)                                      *
         AGO   .TAG3
.TAG2    ANOP
*  MODULE NAME = IGG019R4 (MAXI)                                      *
.TAG3    ANOP
*                                                                     *
*  DESCRIPTIVE NAME = SEND SCHEDULER                                  *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- TO SCHEDULE A SENDING OPERATION AND TO FIND THE ADDRESS  *
*   OF THE DESTINATION LCB WHEN A MESSAGE IS POSTED TO A DESTINATION  *
*   QUEUE OR WHEN A DIAL QCB COMES OUT OF A TIME DELAY.               *
*                                                                     *
*ENTRY POINTS --                                                      *
*   FIRST EXECUTABLE INSTRUCTION                                      *
*                                                                     *
*   'LCBSCAN'  WHEN CALLED BY DESTINATION ASSIGNMENT OR BY THE SEND   *
*   SCHEDULER (INITIATE MODE AND FOR A DIAL QCB).                     *
*                                                                     *
*INPUT --                                                             *
*   WHEN ACTIVATED BY THE DISPATCHER -                                *
*   R1 POINTS TO BUFFER, LCB OR QCB                                   *
*   R11  DISPATCHER BASE                                              *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R15  ENTRY POINT ADDRESS                                          *
*   'LCBSCAN' -                                                       *
*   R7  ADDRESS OF QCB                                                *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R14  RETURN ADDRESS                                               *
*   R15  ADDRESS OF LCBSCAN                                           *
*                                                                     *
*OUTPUT --                                                            *
*   WHEN ACTIVATED BY THE DISPATCHER                                  *
*   R11  ADDRESS OF DISPATCHER                                        *
*   R13  ADDRESS OF AVTSAVE2                                          *
*                                                                     *
*   'LCBSCAN' -                                                       *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R14  RETURN ADDRESS                                               *
*                                                                     *
*EXITS-NORMAL --                                                      *
*   WHEN CALLED BY DISPATCHER                                         *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R11  DISPATCHER BASE                                              *
*   RETURN IS MADE ON R11 WITH AN OFFSET TO HAVE THE DISPATCHER       *
*   PERFORM THE DESIRED FUNCTION.                                     *
*                                                                     *
*   'LCBSCAN'                                                         *
*   R11  DISPATCHER BASE                                              *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R14  RETURN ADDRESS                                               *
*   BR  R14                                                           *
*                                                                     *
*EXTERNAL ROUTINES --                                                 *
*   'IEDQTNT' - TO CONVERT A TWO BYTE TNT OFFSET TO TERMINAL ENTRY    *
*   ADDRESS                                                           *
*                                                                     *
*   'IOHALT' MACRO - TO HALT AN ENABLE OR PREPARE COMMAND IN          *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORK AREAS --VARIOUS CONTROL BLOCKS EXPANDED BY MACROS        *
*   TAVTD                                                             *
*   TCCWD                                                             *
*   DCBD                                                              *
*   TDEBD                                                             *
*   TLCBD                                                             *
*   TPRFD                                                             *
*   TQCBD                                                             *
*   TSCBD                                                             *
*   TSTCBD                                                            *
*   TTRMD                                                             *
*                                                                     *
*ATTRIBUTES -- RE-USABLE, REFRESHABLE, ENABLED, PROBLEM PROGRAM MODE  *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   THE ADDRESS OF LCBSCAN IS COMPUTED BY OPEN AND STORED IN THE AVT  *
*   FOR USE BY DESTINATION ASSIGNMENT.  OPEN EXPECTS THE OFFSET TO    *
*   ALWAYS BE THE FIRST TWO BYTES OF THIS MODULE.  REGISTERS 5 AND 6  *
*   REMAIN TRANSPARENT TO CALLER WHEN THIS SUBROUTINE IS CALLED.      *
*                                                                     *
***********************************************************************
         EJECT
         DC    AL2(LCBSCAN-&NAME) .     USED BY OPEN TO
*                                         SET ADDRESS IN AVT
*                                 REGISTER EQUATES
         SPACE 1
R0       EQU   0                  FLAG REG
RTEMP    EQU   1                        WORK REGISTER
RWORK    EQU   2                        WORK REGISTER
RSTCB    EQU   3                  STCB BASE
RLCB     EQU   4                  LCB BASE
R5       EQU   5                        WORK REGISTER
R6       EQU   6                                                    TSO
RQCB     EQU   7                  QCB ADDR
RHOLD    EQU   8                  FIRST LCB SCANNED FOR AVAILABLE
RLAST    EQU   9                  COUNT OF LCB'S TO SCAN
RDCB     EQU   10                 DCB BASE
RSUPBASE EQU   11                 DISPATCHER BASE REG
RBASE    EQU   12                 SUBTASK BASE REG
R13      EQU   13                 SAVE AREA ADDR (AVTSAVE2)
R14      EQU   14                 RETURN ADDR REG
R15      EQU   15                 ENTRY BASE REG
CLEAR0F  EQU   X'0F'                                             S22025
         SPACE 1
         SPACE 2
         USING IEDQSTCB,RSTCB
         USING IEDQLCB,RLCB
         USING IEDQQCB,RQCB
         USING IHADCB,RDCB
         USING IEDQDISP,RSUPBASE
         USING AVTSAVE2,R13
         USING *,RBASE
         EJECT
TAG      EQU   * .                                               S22025
         LR    RBASE,R15          SET BASE REG
&NAME    IEDHJN ID .                    MODULE ID AND DATE       S22025
         LA    RSTCB,IEDQSTCB     ZERO HI-ORDER BYTE
         LA    RQCB,QCBSTVTO-IEDQQCB GET QCB'S STCB OFFSET
         LCR   RQCB,RQCB          SET NEGATIVE
         AR    RQCB,RSTCB         BACK UP TO QCB BASE
         SPACE 2
*** ANALYZE THE PASSED ELEMENT.  IT MIGHT BE:
*        1. BUFFER (+8 IS A 'TIC' COMMAND CODE)
*             IF END OF MESSAGE, SEARCH FOR AN AVAILABLE LINE ON SHICH
*             TO SEND.
*        2. LCB (+8 IS THE RECEIVE SCHEDULER'S STCBVTO)
*             LOOK FOR THE MESSAGE TO SEND, AND TELL DISK ROUTINES TO
*             START READING MESSAGE.
         AIF   ('&GEN' EQ 'MINI').L5
*        3. QCB (+8 IS THE SEND SCHEDULER'S STCBVTO)
*             SEND TIME DELAY HAS JUST EXPIRED.  LOOK FOR AN AVAILABLE
*             LINE ON WHICH TO CONTACT THE DIAL TERMINAL.
         SPACE 1
         CLI   PRFTIC-IEDQPRF(RTEMP),DSPSNDSC IS THIS A QCB?        TSO
         BE    QCBTSO                   BRANCH IF IT IS A QCB       TSO
.L5      ANOP
         CLI   LCBPRI-IEDQLCB(RTEMP),PRILNFRE  LCB
         BE    SENDSCH                  YES
         L     RLCB,PRFLCB-1-IEDQPRF(,RTEMP) LCB ADDRESS
         TM    LCBSTAT1,LCBINITN        INITIATE MODE
         BZ    EXIT                     NO
         LA    R15,LCBSCAN              ADDRESSABILITY
         ST    RQCB,AVTDOUBL            SAVE QCB ADDRES
         L     R6,QCBSLINK-1            DESTINATION SCHEDULER
         LR    R5,RTEMP                 SAVE BUFFER ADDRESS
         BALR  R14,R15                  LINK TO FIND LCB
         L     RQCB,AVTDOUBL            SET  QCB ADDRES
         LR    RTEMP,R5                 SET  BUFFER ADDRESS
         LR    RSTCB,R6                 DISPATCH NEXT STCB
         B     DSPBYPAS                 DISPATCH NEXT SSTCB
         AIF   ('&GEN' EQ 'MINI').L7
QCBTSO   LA    R15,LCBSCAN              ESTABLISH ADDRESSABILITY    TSO
         LA    R14,DSPDISP              SET EXIT FROM SUBROUTINE
        AIF   ('&GEN' EQ 'NTSO').L6
         TM    QCBFLAG,QCBTSSES         IS THIS FOR TIME SHARING?   TSO
         BZ    SETEXIT                  BRANCH IF NO
         NI    QCBTSOF2,X'FF'-QCBPOSTO  TURN OFF QCB POSTED BIT     TSO
         B     TSO1                                                 TSO
.L6      ANOP
         SPACE 1
*** A QCB QPOST'ED TO ITSELF IS ASSUMED
         SPACE 1
SETEXIT  EQU   *
         EJECT
.L7      ANOP
         USING *,R15
LCBSCAN  EQU   *
         LA    RQCB,0(,RQCB)            LCRER HI ORDER BYTE FOR COM
         AIF   ('&GEN' EQ 'MINI' OR '&GEN' EQ 'NTSO').L8
         TM    QCBDSFLG,QCBDISK+QCBCORE TIME-SHARING QUEUES?        TSO
         BCR   8,R14                    YES. RETURN TO CPB INIT     TSO
         TM    QCBFLAG,QCBTSSES .      QCB IN TSO SESSION        S22029
         BCR   7,R14 .                 YES RETURN TO CPB INIT    S22029
         SPACE
TSO1     EQU   *                                                    TSO
.L8      ANOP
         L     RDCB,QCBDCBAD-1    GET DCB ADDR
         TM    DCBOFLGS,OPEN      IS DCB OPEN
         BCR   8,R14                    NO EXIT
RETRY    EQU   *
         L     RTEMP,DCBDEBAD     GET DEB ADDR
         USING DEBNMSUB,RTEMP
         TM    DEBOPATB,OUTPUT          OPENED FOR OUTPUT
         BCR   8,R14                    NO, RETURN
         DROP  RTEMP
         SR    RLAST,RLAST        ZERO COUNT REG
         IC    RLAST,X'10'(,RTEMP)      DEB NUMBER OF EXTENTS
         SR    RTEMP,RTEMP        ZERO ODD REG
         IC    RTEMP,QCBRELLN     GET QCB'S STARTING RLN
         LR    RLCB,RTEMP               COPY RLN FOR NEXT TEST
         BCTR  RLCB,R0                  RLN MINUS 1
         SR    RLAST,RLCB               GET COUNT OF LCB'S TO SCAN
         BCR   13,R14                   NO, EXIT
         SR    RWORK,RWORK        ZERO SIZE REG
         IC    RWORK,DCBEIOBX     GET LCB SIZE
         MR    RTEMP-1,RWORK      GET STARTING LCB OFFSET
         AL    RTEMP,DCBIOBAD           POINT TO IOB OF LCB
         SH    RTEMP,LCBSIZE            ADJUST TO START OF LCB
         LR    RLCB,RTEMP               LCB BASE
         AIF   ('&GEN' EQ 'MINI').LNODL
         SR    RHOLD,RHOLD              CLEAR WORK REGISTER
        AIF   ('&GEN' EQ 'NTSO').L9
         TM    QCBDSFLG,QCBDISK+QCBCORE TIME-SHARING QUEUES?        TSO
         BZ    NONDIAL                  YES. TREAT AS IF LEASED     TSO
         TM    QCBFLAG,QCBTSSES .      QCB IN TSO SESSION        S22029
         BO    NONDIAL .               YES TREAT AS LEASED       S22029
.L9      ANOP
         TM    LCBSTAT2,LCBDIAL   ARE THESE DIAL LINES
         BZ    NONDIAL            NO, THIS IS THE LINE
         CLI   QCBSTPRI,CALLOPT         CLOCK OR INTERVAL       SA56891
         BL    DIALLOOP                 BRANCH IF NOT           SA56891
         OI    QCBSTAT,QCBSCHDL         FORCE RECEIVE OPERATION SA56891
*                                         PRIOR TO SENDING      SA56891
DIALLOOP EQU   *
         TM    LCBTSTSW,CONNECT         LCB CONNECTED
         BZ    NOTCONNT                  NOT CONNECTED
         SPACE
         LH    RTEMP,LCBTTCIN           CURRENTLY CONNECTED
         LTR   RTEMP,RTEMP              ORIGIN SPECIFIED
         BZ    TRYNEXT                  NO
         STM   14,15,AVTSAVE3           SAVE REGISTERS
         SPACE
         L     R15,AVTRNMPT             CONVERT ROUTINE
         BALR  R14,R15                  GET TERMINAL CADDRESS
         LM    14,15,AVTSAVE3            SET REGISTERS
         L     RTEMP,TRMDESTQ-1-IEDQTRM(,RTEMP) QCB ADDRESS
         LA    RTEMP,0(,RTEMP)          CLEAR HI ORDER BYTE FOR CR
         CR    RTEMP,RQCB               ARE WE CONNECTED TO THIS
         BNE   TRYNEXT                  NO CONNECTION
         SPACE
         NI    QCBSTAT,AVTEFF-QCBSCHDL  TNT WOURCE SET
         B     LEAVE                    EXIT
         SPACE
NOTCONNT EQU   *
         CLI   QCBSTPRI,CALLPRI         CALL=NONE SPECIFIED
         BE    TRYNEXT                  BRANCH YES
         LTR   RHOLD,RHOLD              PREVIOUS LCB SELECTED
         BNZ   TRYNEXT                  YES,ALREADY HAVE AVAILABEL
         LR    RHOLD,RLCB               SAVE FOUND LCB
TRYNEXT  EQU   *
         AR    RLCB,RWORK               ADDRESS OF NEXT LCB
         BCT   RLAST,DIALLOOP           LOOK AT NEXT LCB
         LTR   RLCB,RHOLD               WAS AVAILABLE LINE FOUND
         BZ    CALLQ                    BRANCH NO
         TS    LCBTSTSW                 LINE STILL AVAILABLE
         BNZ   RETRY                    LOOP AGAIN ON LINE GROUP
         OI    LCBSTAT2,LCBNEGRP        SET NEGATIVE RESPONSE IN
*                                         CASE TERMINAL IS HELD
*                                         IF NOT SET, AN ERRONEOUS
*                                         'CONNECTION' APPEARS TO
*                                         RCV SCHED AND ACTIVATE
         NI    LCBTPCD+11,AVTEFF-DISDLEOT DONT SEND DLE EOTSINCE WE
*                                         ARE NOT DISCONNECTING
*                                         A CALL
         TM    LCBSTAT1,LCBFREEN .      LCB FREE
         BZ    HALTLINE .               BRANCH IF NO TO HIO      S22025
         B     POSTLCB                  GET LCB
         SPACE
CALLQ    EQU   *
         L     RSTCB,DCBIOBAD           1ST IOB
         AR    RSTCB,RWORK              POINT TO 1ST IOB
         SH    RSTCB,DOCQAD             BACK UP TO CALL Q
         B     DSPUNAVR                 USE UNAVAIL AND RETURN
*                                         TO CALLER
         SPACE
NONDIAL  EQU   *
.LNODL   ANOP
         CLI   LCBRSPRI,SENDPRI         LINE HAVE SEND PRIORITY
         BH    OBTAIN             NO, GO GET THE LINE
         OI    LCBSTAT2,LCBSNDPR  SET SEND SCHEDULER WANTS LINE
OBTAIN   EQU   *
         AIF   ('&GEN' EQ 'NTSO' OR '&GEN' EQ 'MINI').L10
         TM    QCBFLAG,QCBTSSES         IS THIS TIME SHARING?       TSO
         BZ    TSO2                     BRANCH ON NO                TSO
         L     R6,AVTTSOPT              ADDRESS OF TSINPUT QCB      TSO
         L     R6,TSISCHED-IEDQTSI(,R6) ADDRESS OF TS SCHEDULER     TSO
         BAL   RSTCB,8(,R6)             BRANCH TO ROUTINE           TSO
TSO2     EQU   *                                                    TSO
.L10     ANOP
         TM    LCBSTAT1,LCBFREEN .      IS LINE FREE             S22025
         BZ    TESTPOLL .               NO, GO LOOK FOR NON-REGULS22025
*                                       AR INTERUPTING TERMINALS S22025
*                                      INTERRUPTING TERMINALS
POSTLCB  EQU   *
         XI    LCBSTAT1,LCBSENDN+LCBFREEN . CHANGE LCB STATE TO  S22025
*                                         A MULTIPE POST OF LCB
         MVI   LCBPRI,PRILNFRE          POST PRIORITY
         ST    14,AVTSAVE3+4            SAVE REGISTER
         ST    RQCB,AVTSAVE3            SAVE REGISTER
         ST    RLCB,LCBQCBA-1           LCB IS QCB
         LR    1,RLCB                   PARAMETER FOR POST
         BAL   R14,DSPPOSTR             PUT LCB ON READY Q
         L     RQCB,AVTSAVE3            SET  REGISTER
         L     14,AVTSAVE3+4            SET  REGISTER
         B     LEAVE                    EXIT
         SPACE
TESTPOLL EQU   *
         CLI   LCBCPA+24,POLL           LINE IN AUTOPOLL
         BNE   BASIC                    BRANCH NO
         SPACE
         MVI   LCBCPA+32,CCWNOP         NOP 1ST TIC
         MVI   LCBCPA+56,CCWNOP         NOP 2ND TIC
         B     LEAVE                    EXIT
         SPACE
BASIC    EQU   *
         TM    LCBSTAT2,LCBSYNC         BSC LINE
         BZ    NOTBSC                   BRANCH NO
         SPACE
         CLI   LCBCPA+16,CCWPREP        RECEIVE STATE
         BNE   LEAVE                    NO
         TS    LCBTSTSW                 LINE FREE
         BNZ   LEAVE                    BRANCH BUSY
         B     HALTLINE                 ISSUE HALT IO
NOTBSC   EQU   *
         CLI   LCBCPA+24,CCWSENSE       SENSE COMMAND
         BE    CHKSENSE                 YES, SEE IF NIO REQUIRED
         CLI   LCBCPA+32,CCWSENSE       START STOP CONTENTION
*                                         MAY BE REQUIRED)
         AIF   ('&GEN' EQ 'MAXI').L11A
         BNE   LEAVE .                  BRANCH IF NOT A BASIC
*
         AGO   .L11B
.L11A    ANOP
         BNE   CKTSO .                  BRANCH IF NOT A BASIC
.L11B    ANOP
CHKSENSE EQU   *
         TM    LCBCSWSV,AVTEFF          SENSE BEEN EXECUTED
         AIF   ('&GEN' NE 'MAXI').L11
         BO    HALTLINE .
CKTSO    EQU   *
         TM    QCBFLAG,QCBTSSES         TSO IN CONTROL
         BZ    LEAVE                    BRANCH IF NO
         TM    LCBSTAT1,LCBRECVN .      IS LINE RECEIVING           TSO
         BZ    LEAVE .                  BRANCH IF NO                TSO
         TM    LCBSTAT2,LCBMSGNN .      MSGEN IN PROGRESS           TSO
         BO    LEAVE .                  BRANCH IF YES
         SPACE
         TM    QCBFLAG,QCBNOBRK         CAN TERMINAL BE BROKEN?    2741
         BO    LEAVE                    BRANCH ON NO               2741
         L     RWORK,LCBLSPCI-1         GET ADDRESS OF FIRST BUFFER2741
         LA    RWORK,0(,RWORK) .        TEST ADDRESS ONLY         M2173
         LTR   RWORK,RWORK .            TEST FOR ADDRESS          M2173
         BZ    LEAVE .                  IF NO BFR, CAN NOT BREAK  M2173
         TM    PRFSTAT1-IEDQPRF(RWORK),PRFNHDRN IS IT FIRST BUFFER?2741
         BO    LEAVE                    BRANCH ON NO               2741
         L     RWORK,PRFIOADR-1-IEDQPRF(,RWORK) GET ADDRESS OF DATA2741
         CLI   1(RWORK),0               HAS USER ENTERED ANY DATA? 2741
         BNE   LEAVE
         AGO   .L11C
.L11     ANOP
         BNO   LEAVE
.L11C    ANOP
HALTLINE EQU   *
         MVC   LCBFLAG1(5),NORMHIO      SET ERP NOT IN CONTROLAND
*                                         HIO ECB COMPLETION. -
*                                         PREVENT LOSS OF LINE IF
*                                         ERROR OCCURRED AND LCB IS
*                                         ON WAY TO ERP OR UNDER ERP
*                                         CONTROL
         SR    RWORK,RWORK              CLEAR FOR INDEXING
         IC    RWORK,LCBUCBX            RLN MINUS 1
         SLL   RWORK,2                   MULTIPLY BY 4 FOR INDEX
         L     RTEMP,DCBDEBAD           DEB ADDRESS
         L     RTEMP,X'20'(RTEMP,RWORK) UCB ADDRESS
         LA    RTEMP,0(,RTEMP)          CLEAR HI ORDER BYTE
*                                         ENSURE POSTIVE POINTER
         AIF   ('&GEN' EQ 'MINI' OR '&GEN' EQ 'NTSO').L13
         TM    QCBFLAG,QCBTSSES         TSO OPERATION
         BZ    ISSUESVC                 BRANCH IF NO
         TM    19(RTEMP),AVTEFF-AIBM1   CAN DEVICE BE A 1050 OR 2741TSO
         BNZ   ISSUESVC                 BRANCH ON NO                TSO
         CLI   16(RTEMP),T1050          IS IT A 1050                TSO
         BE    SETBRK                   BRANCH ON YES               TSO
         CLI   16(RTEMP),T2741C .       IS IT A 2741 CORRES         TSO
         BE    SETBRK .                 BRANCH IF YES               TSO
         CLI   16(RTEMP),T2741B .       IS IT A 2741 BCD            TSO
         BNE   ISSUESVC .               BRANCH IF NO                TSO
SETBRK   EQU   *                                                    TSO
         TM    QCBFLAG,QCBNOBRK .       CAN WRITE BREAK BE USED  A42390
         BO    ISSUESVC .               BRANCH NO                A42390
         OI    LCBTSOB,LCBWRBRK         SET SWITCH FOR LINE END 2741
.L13     ANOP
ISSUESVC EQU   *                                                   2741
         DROP  R15                                              SA65412
         LR    RSTCB,R15                COPY BASE ADDRESS       SA65412
         USING LCBSCAN,RSTCB                                    SA65412
         IOHALT (1)
         LTR   R15,R15                  RETURN CODE ZERO        SA65412
         BZ    HIODONE                  BR YES, HIO COMPLETE    SA65412
         OC    LCBCSW,LCBCSW            CHANNEL PROGRAM STILL   SA68703
*                                        OUTSTANDING            SA68703
         LR    R15,RSTCB                COPY BASE REGISTER      SA65412
         BZ    HALTLINE                 BR YES, REISSUE IOHALT  SA65412
HIODONE  EQU   *                                                SA65412
         LR    R15,RSTCB                BASE REGISTER           SA65412
         DROP  RSTCB                                            SA65412
         USING LCBSCAN,R15                                      SA65412
         USING IEDQSTCB,RSTCB                                   SA65412
LEAVE    EQU   *
         LR    RSTCB,RLCB
         B     DSPUNAVR                 LINK STCB INTO CHAIN
*                                         AND RETURN TO CALLER
EXIT     EQU   *
         L     RSTCB,QCBSLINK-1         NEXT STCB (DEST SCHED)
         B     DSPBYPAS           GO ACTIVATE DESTINATION SCHEDULER
         DROP  15
         EJECT
SENDSCH  EQU   *
         LA    RLCB,AVTEZERO(,RTEMP)    PASSED LCB ADDRESS
         SR    RHOLD,RHOLD              CLEAR WORK REGISTER      S99228
         L     RDCB,LCBDCBPT            LOAD DCB ADDRESS         S99228
         IC    RHOLD,DCBEIOBX           INSERT LCB LENGTH        S99228
         SH    RHOLD,AVTHA4             REDUCE LCB LENGTH BY     S99228
         SH    RHOLD,AVTHA4             8 BYTES FOR ADDR LCB EXT S99228
         LA    RHOLD,ZERO(RHOLD,RLCB)   LOAD LCB EXTENSION ADDR  S99228
         USING IEDQLCBX,RHOLD                                    S99228
         TM    LCBXFLAG,LCBGPCTV        GENERAL POLL IN PROGRESS?S99228
         BO    CKDLY1                   BRANCH YES TO RCV SCH   SA65379
         TM    LCBXFLAG,LCBERPND        SOH%R MESSAGE PENDING?   S99228
         DROP  RHOLD                                             S99228
         BZ    NOERP                    NO, SPEC ERP NOT PENDING S99228
CKDLY1   EQU   *                                                SA65379
         TM    LCBINSRC+TWO,DELAY       LCB ON TIME DELAY Q?     S99228
         BZ    NODELAY                  NO, NOT IN POLL DELAY    S99228
         LR    RTEMP,RLCB               LOAD LCB ADDR            S99228
         L     R15,AVTHG02              LOAD ADDR TIME DELAY RTN S99228
         BALR  R14,R15                  BRANCH TO TIME DELAY     S99228
         LA    RQCB,LCBRSKEY            INSERT RCV SCHED IN CHAI S99228
         LR    RTEMP,RQCB               LOAD QCB ADDR            S99228
         BAL   R14,DSPPRIOR             BRANCH TO DISPATCHER     S99228
NODELAY  EQU   *                                                 S99228
         B     RCVSCHED                 BYPASS TO RCV SCHED      S99228
NOERP    EQU   *                                                 S99228
         MVC   AVTDOUBL(1),LCBSTAT1     SAE CURRENT STATE
         NI    LCBSTAT1,LCBOCNI         SAVE STOP LINE BIT
         OI    LCBSTAT1,LCBSENDN .      SET LCB STATE            S21101
FNDSCB   EQU   * .                                               S22025
         TM    LCBSTAT2,LCBDIAL .       IS THIS A DIAL LINE      S22025
         BO    TSTSCB .                 YES-BRANCH               S22025
         SR    R0,R0 .                  CLEAR REG FOR MULTIPLY   S22025
         SR    RTEMP,RTEMP .            CLEAR REG FOR MULTIPLY   S22025
         IC    R0,AVTSCBSZ .            PICK UP SCB SIZE         S22025
         IC    RTEMP,QCBSCBOF .         PICK UP SCB OFFSET       S22025
         MR    R0,R0 .  .               OFFSET X SIZE = PROPER   S22025
*                                       SCB OFFSET IN RTEMP      S22025
         L     R5,LCBSCBDA-1 .          SCB DIRECTORY START ADDR S22025
         LA    R5,0(RTEMP,R5) .         GET SCB FOR THIS QCB     S22025
         ST    R5,LCBSCBA-1 .           SET SCB ADDR IN LCB      S22025
TSTSCB   EQU   * .                                               S22025
         USING IEDQSCB,RLAST
         L     RLAST,LCBSCBA-1          GET SCB ADDRESS
         L     RSTCB,STCBLINK-1         NEXT STCB ADDRESS       SA61773
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN EXTENDD LOCK
         BZ    NOLOCK                   NO
         SPACE 2                                                 S22025
         TM    SCBQTYPE,SCBBFMM  .      MIDDLE MSG               S22025
         BZ    TYPELOCK                 NO-BR                    S22025
         SPACE 2                                                 S22025
         TM    QCBSTAT,QCBSEND          MIDDLE OF SENDING LOCK MSG22025
         BO    NOLOCK                   YES-BR                   S22025
         SPACE 2                                                 S22025
         B     CHKDLY                   BRANCH IF NO            SA61773
         SPACE 2                                                 S22025
TYPELOCK EQU   *                                                 S22025
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN EXTENDED LOCK          S22025
         BM    EXTEND                   BRANCH IF EXTENDED LOCK
         SPACE
         MVI   QCBLKRLN,AVTEZERO        CLEAR TO PREVENT POSSIBLE
*                                       DOUBLE POST OF LCB
         CLI   LCBRSPRI,RECVPRI         RECEIVE PRIORITY        SA58984
         BNE   LOCKTEST                 BR NO, HANDLE NORMALLY  SA58984
         LR    RDCB,RQCB                SAVE QCB ADDRESS        SA58984
         LR    RSTCB,RLCB               QCB TO BE UPDATED       SA58984
         LR    RQCB,RLCB                QCB WITH SEND SCHED STCBSA58984
*                                         AT TOP OF CHAIN       SA58984
         BAL   R14,DSPUNAVR             FOR MESSAGE LOCK AND    SA58984
*                                         RECV PRIORITY, SEND   SA58984
*                                         SCHED IS ON TOP OF    SA58984
*                                         LCB STCB CHAIN OUT OF SA58984
*                                         PRIORITY ORDER. MUST  SA58984
*                                         BE REINSERTED BY PRI  SA58984
         LR    RQCB,RDCB                RESTORE QCB ADDRESS     SA58984
         B     LOCKTEST            USE COMMON CODE
         SPACE
EXTEND   EQU   *
         TM    AVTDOUBL,LCBRECVN        LAST OPERATION A RECEIVE
         BZ    CHKDLY                   BRANCH IF NO            SA61773
LOCKTEST EQU   *
         OC    QCBLKRRN,QCBLKRRN        IS THERE LOCK MSG
         BZ    NOLOCK                   NO
         SPACE
         MVC   SCBSCHDR,QCBLKRRN        SET HEADER
         XC    QCBLKRRN,QCBLKRRN        CLEAR LOCK MSG ADDRESS
         B     SENDINIT .               SEND LOCK MESSAGE       SA52971
         SPACE 2                                                 S21101
         SPACE 3                                                 S21101
NOLOCK   EQU   *
         TM    LCBSTAT2,LCBDIAL         IS THIS A DIAL LINE     OX02199
         BO    CKMIDMSG                 YES,SKIP STOPLINE TEST  OX02199
         TM    LCBSTAT1,LCBOCNI         STOPLINE IN PROGRESS    SA56606
         BO    NEXTSTCB                 YES-BRANCH              SA56606
CKMIDMSG EQU   *                                                OX02199
         TM    SCBQTYPE,SCBBFMM .       MIDDLE OF MSG            S22025
         BZ    TSTDELAY .               NO-BRANCH                S22025
         TM    QCBSTAT,QCBSEND .        QCB IN SEND MODE         S22025
         BO    SETSCERB .               YES GO SET ERB AND SCB UPS22025
*                                       FOR RECALL               S22025
         B     NEXTSTCB .               GET NEXT STCB            S22025
         SPACE 2
TSTDELAY EQU   * .                                               S22025
         TM    LCBSTAT1,LCBOCNI .       STOPLINE IN PROGRESS    OX02199
         BO    NEXTSTCB                 YES,BRANCH              OX02199
         TM    AVTBIT1,AVTDLAYN .       SYSTEM DELAY             S22025
         BO    NEXTSTCB .               BRANCH-YES               S22025
         TM    AVTBIT1,AVTCLOSN+AVTQUCKN QUICK CLOSE             S22025
         BO    NEXTSTCB .               QUICK CLOSE              S22025
         AIF   ('&GEN' EQ 'MINI').L16
         TM    QCBSTAT,QCBSCHDL         HAS SOURCE BEEN SET FOR
*                                         CLOCK OR INTVL
         BZ    NOTDIALQ                 BRANCH YES
         LR    RTEMP,RQCB     SET FOR DIAL SCHEDULER
         LA    RSTCB,LCBRSKEY           NEXT STCB
         B     DSPBYPAS                 ACTIVATE DIAL RCV SCHED
NOTDIALQ EQU   *
.L16     ANOP
         TM    SCBBSCFM,SCBMLMTN        MESSAGE LIMIT REACHED
         BZ    NOLMT                    NO
         NI    SCBBSCFM,SCBMLMTF        RESET BIT
         LR    RSTCB,RLCB .             UNAVAIL PARAMETER        S21101
         LR    RQCB,RLCB .              RESTORE QCB ADDR         S22025
         B     POSTIT .                 POST LCB TO ITSELF       S21101
         SPACE 2                                                 S21101
NOLMT    EQU   *                                                 S21101
         TM    SCBBSCFM,SCBCNTEN .      CONTENTION, RVI, OR OTHERS21101
*                                         BUFFER BUSY CONDITION  S21101
         BZ    NOTBUSY .                BRANCH IF NO             S21101
         NI    SCBBSCFM,AVTEFF-SCBCNTEN RESET CONTENTION BIT     S21101
         L     RSTCB,QCBSLINK-1         GET NEXT STCB
         CLI   STCBPRI-IEDQSTCB(RSTCB),0   QEVENT NEXT
         BNE   CKDLY1                   BR IF NO TO DISPATCH    SA65379
*                                         RECEIVE SCHEDULER      S21101
         SPACE
CHKDLY   EQU   *                                                SA61773
         TM    LCBINSRC+2,DELAY         IN DELAY Q
         BNZ   NEXTSTCB                 YES DISPATCH
RCVSCHED EQU   *                                                SA61773
         LA    RSTCB,LCBRSKEY           RCV SCHED STCB          SA61773
NEXTSTCB EQU   *                                                SA61773
         LR    RTEMP,RLCB               SET ELEMENT REG         SA61773
         LR    RQCB,RLCB                SET QCB REG             SA61773
         B     DSPBYPAS                 BYPASS                  SA61773
         SPACE 3                                                 S21101
NOTBUSY  EQU   *                                                 S21101
         MVC   AVTDOUBL+1(1),SCBBSCFM   SAVE FLAGS              SA59034
         AIF   ('&GEN' EQ 'MINI').L17
         TM    LCBSTAT2,LCBDIAL+LCBSYNC DIAL BSC
         BNO   NOBSD                    BRANCH IF NOT BSC DIAL
         SPACE
         CLI   LCBTSTSW,AVTEFF          WAS THERE HARD ERROR INA
*                                         PREVIOUS SELSECTION
         BE BSDIAL                      BRANCH IF YES
         TM    AVTDOUBL,LCBSENDN        DID WE ALREADY SEND
         BZ    CKEOT                    BRANCH IF NOT           SA59034
         SPACE
         CLI   LCBRSPRI,SENDPRI .       SEND PRIORITY            S21101
         BNE   ITSEQ .                  BRANCH IF NO             S21101
         SPACE 1                                                 S21101
         TM    SCBBSCFM,SCBNOEOT .      HAS AN EOT BEEN WRITTEN YS21101
         BO    GOSEND .                 BRANCH IF NO WE ARE STILLS21101
*                                         IN TEXT MODE AND CAN COS21101
*                                         TINUE FLUSHING QUEUE   S21101
         SPACE 1                                                 S21101
ITSEQ    EQU   *                                                 S21101
         OI    SCBBSCFM,SCBNOEOT .      FLAG FOR I/O GENERATO NOTS21101
*                                         TO BUILD WRITE EOT     S21101
         B     RCVSCHED                 DISPATCH RECEIVE SCHEDULER
*                                         ALTERNATE SEND AND RCV
         SPACE
CKEOT    EQU   *                                                SA59034
         TM    SCBBSCFM,SCBNOEOT        TERMINAL TRANSMIT EOT   SA59034
         BZ    ITSEQ                    BR NO, START RECEIVE    SA59034
*                                         CAN'T SEND UNTIL TERM SA59034
*                                         TERMINATES TRANSMITINGSA59034
         NI    SCBBSCFM,AVTEFF-SCBNOEOT RESET SWITCH            SA59034
         B     GOSEND                   OUR TURN TO TRANSMIT    SA59034
NOBSD    EQU   *
         AIF   ('&GEN' EQ 'NTSO').L17A
         TM    QCBDSFLG,QCBDISK+QCBCORE NON-TSO QUEUES              TSO
         BZ    GOSEND                   BRANCH ON NO                TSO
         TM    QCBFLAG,QCBTSSES         TIME-SHARING SESSION?    S22029
         BO    GOSEND                   YES, START SEND OPRATION S22029
.L17A    ANOP
         CLI   LCBTSTSW,AVTEFF          REQUEST FOR NEW CONNECTION
         BNE   GOSEND                   BRANCH IF NO
BSDIAL   EQU   *
         CLI   QCBSTPRI,CALLPRI         CALL=NONE
         BE    TRMHELD                  BRANCH IF YES-THIS SITUATION
*                                         POSSIBLE IF HARD ERROR ON
*                                         PREVIOUS ATTEMPT TO SEND
GOSEND   EQU   *
         AIF   ('&GEN' EQ 'NTSO').L17
         TM    AVTBIT1,AVTTSON          IS IT TIME SHARING?         TSO
         BZ    TSO3                     BRANCH ON NO                TSO
         L     R6,AVTTSOPT              GET ADDRESS OF TSINPUT QCB  TSO
         L     R6,TSISCHED-IEDQTSI(R6)  ADDRESS OF TS SCHEDULER     TSO
         BAL   RWORK,12(,R6)            BRANCH TO ROUTINE           TSO
         B     TSO4                     PUT SEND SCHED IN QCB       TSO
         B     SENDPREP                 GO TO START SENDING.        TSO
TSO3     EQU   *                                                    TSO
.L17     ANOP
         TM    QCBFLAG,QCBTERMQ         QUEUED BY TERMINAL?
         BZ    NOTRMQ                   BRANCH IF NOT
         SPACE 2
         TM    QCBSTAT,QCBTRMHO         HAS TERMINAL BEEN INTERCEPTED
         BO    TRMHELD                  BRANCH IF  TERMINAL WAS HELD
*                                         TO AVOID GETTING MSG OFF QUE
         SPACE 2
NOTRMQ   EQU   *
         AIF   ('&GEN' EQ 'NTSO' OR '&GEN' EQ 'MINI').MIX0010    S22029
         TM    QCBDSFLG,QCBTSQ          TIME-SHARING QUEUEING    S22029
         BO    NOINIT                   YES. SKIP SENDING INIT   S22029
*                                       MODE MSGS.               S22029
.MIX0010 ANOP                                                    S22029
         L     RSTCB,QCBINSRC-1   GET INITIATE SOURCE LCB CHAIN
         N     RSTCB,CLEAR              CLEAR LOW ORDER BYTE
         LA    RQCB,0(,RQCB)            CLEAR HI ORDER BYTE
         CLR   RSTCB,RQCB               INITIATE MODE
         BE    NOINIT                   NO
         MVC   QCBINSRC(3),LCBINSRC-IEDQLCB(RSTCB) REMOVE LCB FROM
*                                      INITIATE CHAIN
         NI    LCBINSRC+2-IEDQLCB(RSTCB),X'FF'-LCBNSRCD TURN OFF
*                                      INITIATE FLAG
         IC    R0,LCBINSRC-1-IEDQLCB(RSTCB) SAVE HI             SA62953
         ST    RLCB,LCBINSRC-1-IEDQLCB(,RSTCB) DEST LCB INTO SOURCE
         STC   R0,LCBINSRC-1-IEDQLCB(RSTCB) RESTORE             SA62953
         L     RSTCB,LCBSCBA-1-IEDQLCB(RSTCB) SOURCE SCB
         MVC   SCBSCHDR,SCBDCHDR-IEDQSCB(RSTCB)  LOAD SOURCE SCB
         TM    QCBDSFLG,QCBREUS+QCBNREUS  DISK
         BNZ   MOVER                    YES
         MVC   SCBSCHDR,SCBCCHDR-IEDQSCB(RSTCB)   PARAMETER
MOVER    EQU   *
         MVI   SCBPRI,AVTEZERO          RESET PRIORITY           A
         OI    LCBSTAT1,LCBSENDN+LCBINITN SET LCB STATE
         B     SENDINIT                 SEND MSG
         SPACE 3                                                 S21101
NOINIT   EQU   *
         TM    QCBDSFLG,QCBHELD         IS FEFO CHAIN BEING CHANGED
*                                         BY REUS IN DISABED CODE
         BO    TRMHELD                  BRANCH YES, TO REMOVE STCB.
*                                         WILL BE RE-INSERTED LATER
         SPACE
         SR    R0,R0                    CLEAR COUNT REGISTER
         LA    RTEMP,IEDQQCB+QCBMSIZE-QCBPSIZE INITIALIZE SCAN FOR
*                                      LOOP
SCANLOOP EQU   *
         LA    RTEMP,QCBPSIZE(0,RTEMP) BUMP TO NEXT PRIORITY QCB
         MVC   SCBSCHDR-IEDQSCB(3,RLAST),QCBFFEFO-IEDQPQCB(RTEMP)
*                                      NEXT FEFO MESSAGE
         OC    QCBFFEFO-IEDQPQCB(3,RTEMP),QCBFFEFO-IEDQPQCB(RTEMP) IS
*                                      THERE AN UNSENT FEFO MESSAGE
         BNZ   PREPARE            YES, GO PREPARE SCB
         BCTR  R0,R0                    BUMP COUNT
         CLI   QCBPRIPQ-IEDQPQCB(RTEMP),0 LAST PRIORITY QCB
         BNE   SCANLOOP           NO, LOOK AT NEXT ONE
         AIF   ('&GEN' EQ 'MINI').L19
         MVI   QCBPRLVL,AVTEZERO .      RESET HIGHEST PRIORITY LES21101
*                                         MESSAGE INDICATOR FOR DS21101
*                                         LINES.  HM WILL INDICATS21101
*                                         HIGHEST LEVEL IN USE   S21101
.L19     ANOP                                                    S22025
TRMHELD  EQU   * .                                               S22025
         MVC   SCBBSCFM,AVTDOUBL+1      RESTORE FLAGS           SA59034
         AIF   ('&GEN' EQ 'MINI').L19A                           S22025
         LA    RSTCB,LCBRSKEY           RCV SCHED STCB
         LR    RTEMP,RQCB               SET ELEMENT FOR DIAL RCV SN
         CLI   QCBSTPRI,CALLOPT         THIS CLOCK OR INTVL
         BNL   DSPBYPAS                 YES TO RESET DELAY
         SPACE 3                                                 S21101
.L19A    ANOP                                                    S22025
         AIF   ('&GEN' EQ 'MINI' OR '&GEN' EQ 'NTSO').L18
TSO4     EQU   *                                                    TSO
.L18     ANOP
         LA    RSTCB,LCBSTCBA-(STCBLINK-IEDQSTCB)               SA56606
*                                       SET REG                 SA56606
STCBLP   EQU   *                                                SA56606
         LR    RTEMP,RSTCB              SAVE PREVIOUS STCB      SA56606
         L     RSTCB,STCBLINK-IEDQSTCB-1(RSTCB)                 SA56606
*                                       LOAD NEXT STCB          SA56606
         LA    R0,QCBSTVTO-IEDQQCB      STCB OFFSET             SA56606
         LR    RWORK,RSTCB              SET REG                 SA56606
         LA    RWORK,ZERO(,RWORK)       CLEAR HO BYTE           SA56606
         SR    RWORK,R0                 QCB ADDR                SA56606
         CLR   RWORK,RQCB               CORRECT QCB             SA56606
         BNE   STCBLP                   BRANCH IF NO            SA56606
         SH    RTEMP,AVTHA4             SET REG                 SA56606
         LR    RSTCB,RQCB               UNAVAIL REG             SA56606
         LR    RQCB,RTEMP               UNAVAIL REG             SA56606
POSTIT   EQU   *                                                 S21101
         BAL   R14,DSPUNAVR .           REMOVE STCB              S22025
         MVI   SCBSNDCT,AVTEZERO .      RESET SEND LIMIT COUNT   S21101
         LR    RTEMP,RLCB               SET LCB TO BE QPOST'ED
         ST    RTEMP,LCBQCBA-1          GO QPOST
         B     DSPPOST            GO QPOST
         SPACE 1
PREPARE  EQU   *
         LPR   R0,R0                    MAK COUNT POSITIVE
         STC   R0,SCBPRI-IEDQSCB(,RLAST)  SET PRIORITY LEVEL INDEX
         AIF   ('&GEN' EQ 'MINI').L21                            S21101
         STC   R0,QCBPRLVL .            SET HIGHEST LEVEL MSG IN S21101
.L21     ANOP                                                    S21101
         XC    SCBFEFO,SCBFEFO          CLEAR FOR PRIORITY LEVEL
*                                         MESSAGES
SENDINIT EQU   *
         OI    QCBFLAG,QCBSDFFO         SET AS SENDING A MESG   SA52971
SENDPREP EQU   *
         NI    LCBERBST,AVTEFF-LCBDLNKN . RESET DLINK SWITCH     S22025
         NI    LCBSTAT2,AVTEFF-(LCBNEGRP+LCBSNDPR) RESET SNED PRI
*                                       SWITCH AND INSURE TERMINAL
*                                       IS POLLED ONE MORE TIME
         IC    R0,SCBDESTQ-1-IEDQSCB(,RLAST) SAVE HI ORER BYTE
         ST    RQCB,SCBDESTQ-1-IEDQSCB(,RLAST) SET QCB ADDRESS
         STC   R0,SCBDESTQ-1-IEDQSCB(,RLAST) SET  HI ORER BYTE
         MVC   SCBSCSEG(3),SCBSCHDR .   SET SCB SEG              S22025
         MVI   SCBUNTCT,AVTEZERO .      SET UNIT COUNT TO BUILD  S22025
*                                       FROM BEGINNING OF BUFFER S22025
COMMON1  EQU   *                                                 S22025
         XC    LCBRECAD,LCBRECAD .      CLEAR Q RECORD ADDRESS   S22025
         XC    LCBRECOF,LCBRECOF .      ALSO OFFSETR FOR LINEEND S22025
         TM    LCBSTAT2,LCBDIAL                                 SA60792
         BO    NORESET                                          SA60792
         NC    LCBTTCIN,LCBTTCIN        CONNECTED TERMINAL ?    SA69635
         BNZ   NORESET                  YES, BR-LEAVE AS IS     SA69635
         MVI   LCBTTCIN+ONE,ONE                                 SA60792
NORESET  EQU   *
         MVI   LCBERBPY,PRIINTRQ        INITIAL REQUEST PRIORITY
         L     RDCB,LCBDCBPT            DCB BASE
         MVC   LCBERBCT(1),DCBBUFOU     NUMBER INITIAL BUFFERS
         NI    LCBERBCT,X0F             CLEAR 1ST 4 BITS
COMMON   EQU   *                                                 S22025
         LA    RTEMP,AVTDSIOB .         GET DISK I/O QCB ADDRESS S22025
COMMON2  EQU   *                                                 S22025
         ST    RTEMP,LCBERB .           SET TO POST ERB TO IT    S22025
         LA    RTEMP,LCBERB             ADDR OF ERB              S22025
         B     DSPPOST            GO QPOST ERB TO DISK READ ROUTINE
         SPACE 7                                                 S21101
         EJECT                                                   S22025
SETSCERB EQU   * .                                               S22025
         TM    SCBQTYPE,SCBCOREQ .      CORE QUEUEING            S22025
         BO    GOCORE .                 YES-BR                   S22025
         TM    SCBSTAT1,PRFNLSTN  .     EOM BUFFER               S22025
         BO    NOTALL .                 BRANCH IF NO             S22025
         CLC   SCBDEOB+1(3),SCBCRCD .   SENDING LAST SEGMENT     S22025
         BNL   GOCORE                                           SA60792
         NC    SCBXTRA(THREE),SCBXTRA                           SA60792
         BZ    RSET                                             SA60792
         CLC   SCBDEOB+ONE(THREE),SCBXTRA                       SA60792
         BNL   GOCORE                                           SA60792
RSET     EQU   *                                                SA60792
         OI    SCBSTAT1,PRFNLSTN .      FLAG NOT LAST            S22025
NOTALL   EQU   * .                                               S22025
         XC    SCBXTRA(3),SCBXTRA .     CLEAR FOR RECALL         S22025
         XC    SCBNTXT(3),SCBNTXT .     CLEAR FOR RECALL         S22025
GOCORE   EQU   *                                                 S22025
         MVZ   SCBHBFNO,SCBQTYPE        SET QTYPE FLGS FOR FA   SA52971
         MVC   LCBCPA(4),SCBDEOB .      SAVE ACROSS RECALL       S22025
         MVC   LCBCPA+4(3),SCBTRANS .   SAVE SCB TRANS ACROSS RCLS22025
         MVI   LCBERBPY,PRIDKEOB .      RECALL PRIORITY          S22025
         MVI   LCBERBCT,1 .             RECALL BUFFER COUNT      S22025
         MVI   LCBERBCT+1,AVTEZERO .    CLEAR DISABLED COUNT     S22025
         OI    LCBSTAT1,LCBRCLLN .      SET RECALL BIT IN LCB    S22025
         LA    R0,ERBRETRN .            RETURN ADDR FOR ERB      S22025
         ST    R0,LCBRCQCB .            IN LCB                   S22025
         B     COMMON                   GO POST ERB              S22025
         SPACE 3                                                 S22025
         DS    0F .                     WORD BOUNDARY            S22025
ERBRETRN EQU   *-8 .                    QCB                      S22025
         DC    AL1(DSPMCPL4),AL3(*-1) . STCB THAT GETS CONTROL   S22025
*                                       WHEN ERB FOR RECALL IS   S22025
*                                       POSTED BACK TO R4 WHEN   S22025
*                                       READ IS DONE             S22025
         L     RBASE,AVTHD              GET BASE ADDR            CLUP21
         LA    RWORK,LCBERB-IEDQLCB .   ERB OFFSET INTO LCB      S22025
         SR    RTEMP,RWORK .            COMPUTE LCB ADDRESS      S22025
         LR    RLCB,RTEMP .             LCB ADDR                 S22025
         L     RLAST,LCBSCBA-1 .        SCB ADDRESS              S22025
         L     RTEMP,LCBERBCH-1 .       RECALLED BUFFER ADDRESS  S22025
         OI    PRFSTAT1-IEDQPRF(RTEMP),PRFCNCLN . SET CNCL FLG   S22025
         MVC   LCBISZE(1),PRFSCAN+1-IEDQPRF(RTEMP) . SET IDLES CTS22025
         MVC   LCBTTBIN,SCBDCHDR .      SETUP TNTOFFSET          S22025
         LA    RTEMP,AVTINOUT .         INMSG/OUTMSG DELIMITER   S22025
         IC    RWORK,SCBMACR-1 .        SAVE HIGH BYTE           S22025
         ST    RTEMP,SCBMACR-1 .        RESET MACRO ADDRESS      S22025
         STC   RWORK,SCBMACR-1 .        RESTORE HIGH BYTE        S22025
         MVC   SCBUNTCT(1),SCBDEOB .    SET UNIT COUT            S22025
         MVC   SCBDEOB(4),LCBCPA .      RESTORE DEOB OFTER RECALLS22025
         MVC   SCBTRANS(3),LCBCPA+4 .   RESTORE SCB TRANS        S22025
         NI    SCBHBFNO,CLEAR0F         RESET HBFNO              S22025
         LA    RTEMP,AVTACTIB .         ACTIVATE ROUTINE QCB ADDRS22025
         L     RQCB,SCBDESTQ-1 .        PICK UP QCB ADDR         S22025
         NI    SCBQTYPE,AVTEFF-SCBBFMM .RESET MIDDLE OF MSG BIT  S22025
         NI    QCBSTAT,AVTEFF-QCBSEND .RESET QCB SEND BIT        S22025
         NI    LCBSTAT1,AVTEFF-LCBRCLLN RESET RECALL BIT         S22025
         TM    LCBERBST,LCBEOMSG .      EOM IN RECALLED BUFFER   S22025
         BO    COMMON2                  GO POST ERB              S22025
         B     COMMON1                  GO POST ERB              S22025
         EJECT
*                                 OTHER EQUATES
         SPACE 1
AUTOPOLL EQU   X'01'              INVITATION LIST IS CURRENTLY BEING
*                                      AUTOPOLLED
ZERO     EQU   0                                                 S99228
ONE      EQU   1                        CONSTANT OF ONE         SA60792
THREE    EQU   3                        CONSTANT OF THREE       SA60792
TWO      EQU   2                                                 S99228
CALLOPT  EQU   X'70'                    MASK STCB PRI DIAL UP
*                                         CLOCK OR INTVL
SENDPRI  EQU   X'20'                    MASK FOR SEND PRIORITY
CONNECT  EQU   X'80'                    MASK FOR DIAL CONNECTION
CALLPRI  EQU   X'50'                    PRIORITY OF DIAL SEND WITH
*                                         CALL=NONE
DELAY    EQU   X'02'                    MASK FOR LCB IN DELAY Q
STATUS   EQU   X'03'              OFFSET OF STATUS INFORMATION IN
*                                      INVITATION LIST
*                                      FOR OUTPUT
NOP      EQU   X'03'              NO-OP CHANNEL COMMAND CODE
TIC      EQU   X'08'              TIC CHANNEL COMMAND CODE
OPEN     EQU   X'10'              DCB FLAG INDICATING LINE GROUP OPEN
SENSE    EQU   X'FF'              VALUE SET IN SENSE BYTE OF COMMAND
*                                      FOLLOWING PREPARE ON CONTENTION
*                                      TERMINALS
F0       EQU   X'F0'                    DIAL CONNECTION AND NO DIAL
X0F      EQU   X'0F'                    MASK USED FOR INITIAL BUFF
X12      EQU   X'12'                                               2741
POLL     EQU   X'09'                    POLL COMMAND
LCBNSRCD EQU   X'01'                    FLAG FOR INITIATE MODE
ERPCTL   EQU   X'24'                    BITS IN LCBFLAG1-ERP IS IN
*                                         CONTROL
LINEADD  EQU   4                        OFFSET IN UNCB-LINE ADDRESS
SENSMASK EQU   X'FF'                    VALUE OF SENSE FIELD IN
*                                         LCB BEFORE SENSE
AIBM1    EQU   X'1F'                                                TSO
T1050    EQU   X'51'                                                TSO
T2741B   EQU   X'56' .                                              TSO
T2741C   EQU   X'55' .                                              TSO
RECVPRI  EQU   X'80'                    LCB RECV PRI VALUE      SA58984
         SPACE 2
         SPACE 1
         AIF   ('&GEN' EQ 'MINI').L20
DOCQAD   DC    AL2(LCBFLAG1-IEDQLCB+12) CONSTANT TO OBTAIN CALL Q
.L20     ANOP
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    SIZE OF LCB FROM RCB TO IOB
CLEAR    DS    0F
         DC    X'00FFFFFC'
NORMHIO  DC    X'C210000048'
DISDLEOT EQU   X'01'                    FLAG TO WRITE DLE EOT
OUTPUT   EQU   X'01'                    DEB MASK OPEN OUTPUT
         TPRIOR
         TAVTD
         TPRFD
         DCBD  DSORG=TX
         TDISPD
         TLCBD
         TQCBD
         TSCBD
         TSTCBD
         TTRMD
         TTSID                                                      TSO
         TCCWD
         TDEBD
         MEND
