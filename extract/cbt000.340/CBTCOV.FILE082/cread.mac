         MACRO
&NAME    CREAD &INIT
         LCLA  &A,&OPNUM
         LCLB  &CHRNUM,&SETIN,&SETRST
         LCLC  &PARAM
&A       SETA  2
&OPNUM   SETA  N'&SYSLIST               NNUMBER OF OPERANDS
         AIF   (&OPNUM EQ 0).CONT       NO OPERAND
         AIF   (&OPNUM GT 1).CHECK      MORE THAN ONE OPER SPEC
&CHRNUM  SETB  (K'&INIT GT 1)
         AIF   (&CHRNUM).ERR2           OPERAND WITH MORE THAN 1 CHAR
&PARAM   SETC  '&INIT'
&SETRST  SETB  ('&PARAM' EQ 'R')
         AIF   (NOT &SETRST).RESET
&A       SETA  13
         AGO   .CONT
.RESET   ANOP
&SETIN   SETB  ('&PARAM' EQ 'I')
         AIF   (NOT &SETIN).ERR3
&A       SETA  3
.CONT    ANOP
         CNOP  0,4
&NAME    OI    4(15),X'&A'              SET REQUEST FLAGS
         BAL   1,*+8                    LOAD ADD OF NEXT IN REG 1
.PRMLST  DC    F'0'                     FULL WORD
         ST    15,0(0,1)                STORE ADDRESS IN PARM LIST
         L     15,IHKYYLAD              GET ENTRY POINT ADDRESS
         BALR  14,15                    LINK TO LINE ADMINISTRATOR
         MEXIT
.CHECK   AIF   (N'&SYSLIST(1) EQ 0).ERR0     FIRST OPERAND OMITT.
         MNOTE 7,'MORE THAN ONE OPERAND SPECIFIED'
         MEXIT
.ERR2    MNOTE 7,'OPERAND CONSISTS OF MORE THAN ONE CHARACTER'
         MEXIT
.ERR3    MNOTE 7,'OPERAND INCORRECTLY SPECIFIED'
         MEXIT
.ERR0    MNOTE 7,'MORE OPERANDS SPECIFIED AND FIRST OPER OMITTED'
         MEND
